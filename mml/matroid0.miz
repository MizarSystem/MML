:: Introduction to Matroids
::  by Grzegorz Bancerek and Yasunari Shidama
::
:: Received July 30, 2008
:: Copyright (c) 2008 Association of Mizar Users

environ

 vocabularies CLASSES1, PRE_TOPC, FINSET_1, BVFUNC_2, CARD_1, AMI_1, BOOLE,
      VECTSP_1, RLVECT_3, FUNCT_1, RELAT_1, SETFAM_1, ORDERS_1, MATROID0,
      TARSKI, SUBSET_1, JORDAN13, NATTRA_1, TAXONOM2, EQREL_1, MATRLIN, ARYTM,
      ARYTM_1, RLVECT_1, RLVECT_2, AOFA_000, QC_LANG1, LEXBFS;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, DOMAIN_1, SETFAM_1, FINSET_1,
      NUMBERS, FUNCT_1, RELSET_1, FUNCT_2, EQREL_1, ORDERS_1, TAXONOM2,
      XCMPLX_0, XXREAL_0, NAT_1, CARD_1, CLASSES1, LEXBFS, AOFA_000, STRUCT_0,
      RLVECT_1, VECTSP_1, VECTSP_6, VECTSP_7, PRE_TOPC, TDLAT_3, MATRLIN,
      PENCIL_1, RANKNULL;
 constructors XXREAL_0, ARYTM_2, ARYTM_3, XBOOLE_0, NUMBERS, XREAL_0, NAT_1,
      PCOMPS_1, EQREL_1, DOMAIN_1, FINSET_1, XCMPLX_0, CARD_1, CLASSES1,
      COH_SP, TDLAT_3, FINSEQ_1, TAXONOM2, SEQ_1, MEMBERED, LEXBFS, VALUED_0,
      VALUED_1, RANKNULL, VECTSP_1, VECTSP_7, GROUP_1, PRE_TOPC, MATRLIN,
      PENCIL_1, SETFAM_1, TARSKI, ZFMISC_1, RELAT_1, REALSET1, STRUCT_0,
      RLVECT_1, VECTSP_6, VECTSP_4, RELSET_1;
 registrations FINSET_1, XBOOLE_0, CARD_1, RELSET_1, STRUCT_0, SUBSET_1,
      PENCIL_1, TDLAT_3, SETFAM_1, EQREL_1, MATRLIN, XREAL_0, MEMBERED,
      ALGSTR_0, BINOM, RLVECT_1, VECTSP_1, VECTSP_7;
 requirements BOOLE, SUBSET, NUMERALS, ARITHM, REAL;
 definitions TARSKI, FUNCT_1, CLASSES1, TAXONOM2, XBOOLE_0, STRUCT_0, LEXBFS,
      GROUP_1, PRE_TOPC, PENCIL_1, SUBSET_1;
 theorems PENCIL_1, TARSKI, XBOOLE_1, ZFMISC_1, CARD_1, COH_SP, PRE_TOPC,
      NAT_1, CLASSES1, TDLAT_3, XXREAL_0, XREAL_1, XBOOLE_0, TAXONOM2,
      ORDERS_1, FUNCT_2, FUNCT_1, EQREL_1, WELLORD2, VECTSP_7, VECTSP_9,
      MATRLIN, STRUCT_0, CARD_FIN, CARD_2, VECTSP_4, ORDINAL1, CARD_FIL,
      RANKNULL, VECTSP_1, VECTSP_6, ALGSTR_0, SETWISEO, HALLMAR1, BORSUK_5;
 schemes FUNCT_2, NAT_1;

begin :: Definition by Independent Sets

notation
  let x,y be set;
  antonym x c/= y for x c= y;
end;

definition
  mode SubsetFamilyStr is TopStruct;
end;

notation
  let M be SubsetFamilyStr;
  let A be Subset of M;
  synonym A is independent for A is open;
  antonym A is dependent for A is open;
end;

definition
  let M be SubsetFamilyStr;
  func the_family_of M -> Subset-Family of M equals
  the topology of M;
  coherence;
end;

definition
  let M be SubsetFamilyStr;
  let A be Subset of M;
  redefine attr A is independent means
  : Def2:
  A in the_family_of M;
  compatibility by PRE_TOPC:def 5;
end;

definition
  let M be SubsetFamilyStr;
  attr M is subset-closed means
  : Def3:
  the_family_of M is subset-closed;
  attr M is with_exchange_property means
  for A,B being finite Subset of M st A
  in the_family_of M & B in the_family_of M & card B = (card A) + 1 ex e being
  Element of M st e in B \ A & A \/ {e} in the_family_of M;
end;

registration
  cluster strict non empty non void finite subset-closed
    with_exchange_property SubsetFamilyStr;
  existence
  proof
    bool {0} c= bool {0};
    then reconsider S = bool {0} as Subset-Family of {0};
    take M = TopStruct(#{0}, S#);
    thus M is strict;
    thus the carrier of M is non empty;
    thus the topology of M is non empty;
    thus the carrier of M is finite;
    thus the_family_of M is subset-closed by COH_SP:2;
    let A,B be finite Subset of M;
    assume
    A in the_family_of M;
    assume
    B in the_family_of M;
    assume
A1: card B = (card A) + 1;
    reconsider e = 0 as Element of M by TARSKI:def 1;
    take e;
A2: bool {0} = {{},{0}} by ZFMISC_1:30;
    then
A3: B = {} or B = {0} by TARSKI:def 2;
    then
A4: card B = 0 or card B = 1 by CARD_1:50;
A5: A = {} or A = {0} by A2,TARSKI:def 2;
    then card A = 0 or card A = 1 by CARD_1:50;
    hence thesis by A1,A5,A3,A4;
  end;
end;

registration
  let M be non void SubsetFamilyStr;
  cluster independent Subset of M;
  existence
  proof
    consider a being Element of the topology of M;
    reconsider a as Subset of M;
    take a;
    thus a in the_family_of M;
  end;
end;

registration
  let M be subset-closed SubsetFamilyStr;
  cluster the_family_of M -> subset-closed;
  coherence by Def3;
end;

theorem Th1:
  for M being non void subset-closed SubsetFamilyStr for A being
independent Subset of M for B being set st B c= A holds B is independent Subset
  of M
proof
  let M be non void subset-closed SubsetFamilyStr;
  let A be independent Subset of M;
  let B be set;
  assume
A1: B c= A;
  A in the_family_of M by Def2;
  then B in the_family_of M by A1,CLASSES1:def 1;
  hence thesis by Def2;
end;

registration
  let M be non void subset-closed SubsetFamilyStr;
  cluster finite independent Subset of M;
  existence
  proof
    consider a being Element of the topology of M;
    reconsider a as independent Subset of M by PRE_TOPC:def 5;
    {} c= a by XBOOLE_1:2;
    then reconsider b = {} as independent Subset of M by Th1;
    take b;
    thus thesis;
  end;
end;

definition
  mode Matroid is non empty non void subset-closed with_exchange_property
    SubsetFamilyStr;
end;

theorem Th2:
  for M being subset-closed SubsetFamilyStr holds M is non void iff
  {} in the_family_of M
proof
  let M be subset-closed SubsetFamilyStr;
  hereby
    assume
    M is non void;
    then reconsider M' = M as non void subset-closed SubsetFamilyStr;
    consider a being independent Subset of M';
    {} c= a by XBOOLE_1:2;
    then {} is independent Subset of M' by Th1;
    hence {} in the_family_of M by Def2;
  end;
  assume
  {} in the_family_of M;
  hence the topology of M is non empty;
end;

registration
  let M be non void subset-closed SubsetFamilyStr;
  cluster empty -> independent Subset of M;
  coherence
  proof
    let S be Subset of M;
    assume
    S is empty;
    hence S in the_family_of M by Th2;
  end;
end;

theorem Th3:
  for M being non void SubsetFamilyStr holds M is subset-closed iff
  for A,B being Subset of M st A is independent & B c= A holds B is independent
proof
  let M be non void SubsetFamilyStr;
  thus M is subset-closed implies for A,B being Subset of M st A is
  independent & B c= A holds B is independent by Th1;
  assume
A1: for A,B being Subset of M st A is independent & B c= A holds B is
  independent;
  let x,y be set;
  assume
  x in the_family_of M;
  then
A2: x is independent Subset of M by Def2;
  assume
  y c= x;
  then y is independent Subset of M by A1,A2,XBOOLE_1:1;
  hence thesis by Def2;
end;

registration
  let M be non void subset-closed SubsetFamilyStr;
  let A be independent Subset of M;
  let B be set;
  cluster A/\B -> independent Subset of M;
  coherence
  proof
    A/\B c= A by XBOOLE_1:17;
    hence thesis by Th3;
  end;
  cluster B/\A -> independent Subset of M;
  coherence;
  cluster A\B -> independent Subset of M;
  coherence
  proof
    A\B c= A by XBOOLE_1:36;
    hence thesis by Th3;
  end;
end;

theorem Th4:
  for M being non void non empty SubsetFamilyStr holds M is
with_exchange_property iff for A,B being finite Subset of M st A is independent
& B is independent & card B = (card A) + 1 ex e being Element of M st e in B \
  A & A \/ {e} is independent
proof
  let M be non void non empty SubsetFamilyStr;
  consider e0 being Element of M;
  thus M is with_exchange_property implies for A,B being finite Subset of M st
A is independent & B is independent & card B = (card A) + 1 ex e being Element
  of M st e in B \ A & A \/ {e} is independent
  proof
    assume
A1: for A,B being finite Subset of M st A in the_family_of M & B in
the_family_of M & card B = (card A) + 1 ex e being Element of M st e in B \ A &
    A \/ {e} in the_family_of M;
    let A,B be finite Subset of M;
    assume that
A2: A in the_family_of M and
A3: B in the_family_of M and
A4: card B = (card A) + 1;
    consider e being Element of M such that
A5: e in B \ A and
A6: A \/ {e} in the_family_of M by A1,A2,A3,A4;
    take e;
    thus e in B \ A & A \/ {e} in the_family_of M by A5,A6;
  end;
  assume
A7: for A,B being finite Subset of M st A is independent & B is
  independent & card B = (card A) + 1 ex e being Element of M st e in B \ A & A
  \/ {e} is independent;
  let A,B be finite Subset of M;
  assume that
A8: A in the_family_of M and
A9: B in the_family_of M;
A10: B is independent by A9,Def2;
  assume
A11: card B = (card A) + 1;
  A is independent by A8,Def2;
  then consider e being Element of M such that
A12: e in B \ A and
A13: A \/ {e} is independent by A7,A10,A11;
  take e;
  thus thesis by A12,A13,Def2;
end;

notation
  let A be set;
  synonym A is finite-membered for A is with_finite-elements;
end;

definition
  let A be set;
  redefine attr A is finite-membered means
  : Def5:
  for B being set st B in A
  holds B is finite;
  compatibility
  proof
    thus A is finite-membered implies for B being set st B in A holds B is
    finite
    proof
      assume
      for a being Element of A holds a is finite;
      hence thesis;
    end;
    assume
A1: for B being set st B in A holds B is finite;
    let a be Element of A;
    A <> {} or A = {};
    hence thesis by A1;
  end;
end;

definition
  let M be SubsetFamilyStr;
  attr M is finite-membered means
  : Def6:
  the_family_of M is finite-membered;
end;

definition
  let M be SubsetFamilyStr;
  attr M is finite-degree means
  : Def7:
  M is finite-membered & ex n being Nat
  st for A being finite Subset of M st A is independent holds card A <= n;
end;

registration
  cluster finite-degree -> finite-membered SubsetFamilyStr;
  coherence
  proof
    let M be SubsetFamilyStr;
    assume
    M is finite-membered;
    hence thesis;
  end;
  cluster finite -> finite-degree SubsetFamilyStr;
  coherence
  proof
    let M be SubsetFamilyStr;
    assume
    the carrier of M is finite;
    then reconsider X = the carrier of M as finite set;
    thus M is finite-membered
    proof
      let x be set;
      assume
      x in the_family_of M;
      then x c= X;
      hence thesis;
    end;
    take n = card X;
    let A be finite Subset of M;
    thus thesis by NAT_1:44;
  end;
end;

begin :: Examples

registration
  cluster mutually-disjoint non empty with_non-empty_elements set;
  existence
  proof
    take Y = {{0}};
    thus thesis by TAXONOM2:10;
  end;
end;

theorem Th5:
  for A,B being finite set st card A < card B ex x being set st x
  in B \ A
proof
  let A,B be finite set;
  assume
  card A < card B;
  then not B c= A by NAT_1:44;
  then consider x being set such that
A1: x in B and
A2: x nin A by TARSKI:def 3;
  take x;
  thus thesis by A1,A2,XBOOLE_0:def 5;
end;

theorem
  for P being mutually-disjoint with_non-empty_elements non empty set
  for f being Choice_Function of P holds f is one-to-one
proof
  let P be mutually-disjoint with_non-empty_elements non empty set;
  let f be Choice_Function of P;
  let x1,x2 be set;
  assume that
A1: x1 in dom f and
A2: x2 in dom f and
A3: f.x1 = f.x2;
A4: not {} in P;
  then
A5: f.x1 in x1 by A1,ORDERS_1:def 1;
  f.x1 in x2 by A2,A3,A4,ORDERS_1:def 1;
  then x1 meets x2 by A5,XBOOLE_0:3;
  hence thesis by A1,A2,TAXONOM2:def 5;
end;

registration
  cluster -> non void subset-closed with_exchange_property (discrete
    SubsetFamilyStr);
  coherence
  proof
    let T be discrete SubsetFamilyStr;
    the topology of T is non empty by TDLAT_3:def 1;
    hence
A1: T is non void by PENCIL_1:def 4;
    for A,B being Subset of T st A is independent & B c= A holds B is
    independent by TDLAT_3:17;
    hence T is subset-closed by A1,Th3;
    let A,B be finite Subset of T such that
    A in the_family_of T and
    B in the_family_of T and
A2: card B = card A + 1;
    now
      assume
      B c= A;
      then card B c= card A by CARD_1:27;
      then card B <= card A by NAT_1:40;
      then card B + 0 < card A + 1 by XREAL_1:10;
      hence contradiction by A2;
    end;
    then consider x being set such that
A3: x in B and
A4: not x in A by TARSKI:def 3;
    reconsider x as Element of T by A3;
    {x} c= the carrier of T by A3,ZFMISC_1:37;
    then reconsider C = A \/ {x} as Subset of T by XBOOLE_1:8;
    take x;
    thus x in B \ A by A3,A4,XBOOLE_0:def 5;
    C is independent by TDLAT_3:17;
    hence thesis by Def2;
  end;
end;

theorem
  for T being non empty discrete TopStruct holds T is Matroid;

definition
  let P be set;
  func ProdMatroid P -> strict SubsetFamilyStr means
  : Def8:
  the carrier of it
= union P & the_family_of it = {A where A is Subset of union P: for D being set
  st D in P ex d being set st A /\ D c= {d}};
  existence
  proof
    set F = {A where A is Subset of union P: for D being set st D in P ex d
    being set st A /\ D c= {d}};
    set X = union P;
    F c= bool X
    proof
      let x be set;
      assume
      x in F;
      then ex A being Subset of X st x = A & for D being set st D in P ex d
      being set st A /\ D c= {d};
      hence thesis;
    end;
    then reconsider F as Subset-Family of X;
    take M = TopStruct(#X,F#);
    thus thesis;
  end;
  uniqueness;
end;

registration
  let P be non empty with_non-empty_elements set;
  cluster ProdMatroid P -> non empty;
  coherence
  proof
    set M = ProdMatroid P;
    the carrier of M = union P by Def8;
    hence the carrier of M is non empty;
  end;
end;

theorem Th8:
  for P being set for A being Subset of ProdMatroid P holds A is
independent iff for D being Element of P ex d being Element of D st A /\ D c= {
  d}
proof
  let P be set;
  set M = ProdMatroid P;
A1: the_family_of M = {A where A is Subset of union P: for D being set st D
  in P ex d being set st A /\ D c= {d}} by Def8;
  let A be Subset of ProdMatroid P;
A2: the carrier of M = union P by Def8;
  thus A is independent implies for D being Element of P ex d being Element of
  D st A /\ D c= {d}
  proof
    assume
    A in the_family_of M;
    then
A3: ex B being Subset of union P st A = B & for D being set st D in P ex d
    being set st B /\ D c= {d} by A1;
    let D be Element of P;
    P = {} implies A = {} & {} /\ D = {} by A2,ZFMISC_1:2;
    then P = {} implies A /\ D c= {1} by XBOOLE_1:2;
    then consider d being set such that
A4: A /\ D c= {d} by A3;
    consider d0 being Element of D;
    now
      assume
      d nin D;
      then d nin A /\ D by XBOOLE_0:def 4;
      then A /\ D <> {d} by TARSKI:def 1;
      then A /\ D = {} by A4,ZFMISC_1:39;
      hence A /\ D c= {d0} by XBOOLE_1:2;
    end;
    hence thesis by A4;
  end;
  assume
A5: for D being Element of P ex d being Element of D st A /\ D c= {d};
A6: now
    let D be set;
    assume
    D in P;
    then ex d being Element of D st A /\ D c= {d} by A5;
    hence ex d being set st A /\ D c= {d};
  end;
  the carrier of M = union P by Def8;
  hence A in the_family_of M by A1,A6;
end;

registration
  let P be set;
  cluster ProdMatroid P -> non void subset-closed;
  coherence
  proof
    set M = ProdMatroid P;
A1: the_family_of M = {A where A is Subset of union P: for D being set st
    D in P ex d being set st A /\ D c= {d}} by Def8;
    set A = {} union P;
    now
      let D be set;
      assume
      D in P;
      take d={};
      thus A /\ D c= {d} by XBOOLE_1:2;
    end;
    then A in the_family_of M by A1;
    hence the topology of M is non empty;
    thus the_family_of M is subset-closed
    proof
      let a,b be set;
      assume that
A2:   a in the_family_of M and
A3:   b c= a;
A4:   ex B being Subset of union P st a = B & for D being set st D in P ex
      d being set st B /\ D c= {d} by A1,A2;
A5:   now
        let D be set;
        assume
        D in P;
        then consider d being set such that
A6:     a /\ D c= {d} by A4;
        take d;
        b /\ D c= a /\ D by A3,XBOOLE_1:26;
        hence b /\ D c= {d} by A6,XBOOLE_1:1;
      end;
      b is Subset of union P by A3,A4,XBOOLE_1:1;
      hence thesis by A1,A5;
    end;
  end;
end;

theorem Th9:
  for P being mutually-disjoint set for x being Subset of
  ProdMatroid P ex f being Function of x,P st for a being set st a in x holds a
  in f.a
proof
  defpred P[set,set] means $1 in $2;
  let P be mutually-disjoint set;
  let x be Subset of ProdMatroid P;
A1: now
    let a be set;
    assume
    a in x;
    then a in the carrier of ProdMatroid P;
    then a in union P by Def8;
    then ex y being set st a in y & y in P by TARSKI:def 4;
    hence ex y being set st y in P & P[a,y];
  end;
  consider f being Function of x,P such that
A2: for a being set st a in x holds P[a,f.a] from FUNCT_2:sch 1(A1);
  take f;
  thus thesis by A2;
end;

theorem Th10:
  for P being mutually-disjoint set for x being Subset of
ProdMatroid P for f being Function of x,P st for a being set st a in x holds a
  in f.a holds x is independent iff f is one-to-one
proof
  let P be mutually-disjoint set, x be Subset of ProdMatroid P;
  let f be Function of x,P;
  assume
A1: for a being set st a in x holds a in f.a;
  hereby
    assume
A2: x is independent;
    thus f is one-to-one
    proof
      let a,b be set;
      assume that
A3:   a in dom f and
A4:   b in dom f;
A5:   f.b in rng f by A4,FUNCT_1:def 5;
      f.a in rng f by A3,FUNCT_1:def 5;
      then reconsider D1 = f.a, D2 = f.b as Element of P by A5;
      a in D1 by A1,A3;
      then
A6:   a in x /\ D1 by A3,XBOOLE_0:def 4;
      consider d2 being Element of D2 such that
A7:   x /\ D2 c= {d2} by A2,Th8;
      b in D2 by A1,A4;
      then b in x /\ D2 by A4,XBOOLE_0:def 4;
      then b = d2 by A7,TARSKI:def 1;
      hence thesis by A7,A6,TARSKI:def 1;
    end;
  end;
  assume
A8: f is one-to-one;
  now
    let D be Element of P;
    consider d1 being Element of D;
    assume
A9: for d being Element of D holds x /\ D c/= {d};
    then x /\ D c/= {d1};
    then consider d2 being set such that
A10: d2 in x /\ D and
    d2 nin {d1} by TARSKI:def 3;
A11: d2 in D by A10,XBOOLE_0:def 4;
A12: d2 in x by A10,XBOOLE_0:def 4;
    then d2 in f.d2 by A1;
    then
A13: f.d2 meets D by A11,XBOOLE_0:3;
    the carrier of ProdMatroid P = union P by Def8;
    then ex y being set st d2 in y & y in P by A10,TARSKI:def 4;
    then
A14: dom f = x by FUNCT_2:def 1;
    then f.d2 in rng f by A12,FUNCT_1:def 5;
    then
A15: f.d2 = D by A13,TAXONOM2:def 5;
    x /\ D c= {d2}
    proof
      let a be set;
      assume
A16:  a in x /\ D;
      then
A17:  a in x by XBOOLE_0:def 4;
A18:  a in D by A16,XBOOLE_0:def 4;
      a in f.a by A1,A17;
      then
A19:  f.a meets D by A18,XBOOLE_0:3;
      f.a in rng f by A14,A17,FUNCT_1:def 5;
      then f.a = D by A19,TAXONOM2:def 5;
      then a = d2 by A8,A12,A14,A15,A17,FUNCT_1:def 8;
      hence thesis by TARSKI:def 1;
    end;
    hence contradiction by A9,A11;
  end;
  hence thesis by Th8;
end;

registration
  let P be mutually-disjoint set;
  cluster ProdMatroid P -> with_exchange_property;
  coherence
  proof
    defpred P[set,set] means $1 in $2;
    set M = ProdMatroid P;
A1: the_family_of M = {A where A is Subset of union P: for D being set st
    D in P ex d being set st A /\ D c= {d}} by Def8;
    let A,B be finite Subset of M;
    assume that
A2: A in the_family_of M and
A3: B in the_family_of M;
    consider f being Function of A,P such that
A4: for a being set st a in A holds a in f.a by Th9;
    assume
    card B = (card A) + 1;
    then
A5: card B > card A by NAT_1:13;
    consider g being Function of B,P such that
A6: for a being set st a in B holds a in g.a by Th9;
A7: the carrier of ProdMatroid P = union P by Def8;
    then P = {} implies A = {} by ZFMISC_1:2;
    then
A8: dom f = A by FUNCT_2:def 1;
    reconsider A' = rng f, B' = rng g as finite set;
A9: A is independent by A2,Def2;
    then f is one-to-one by A4,Th10;
    then A, A' are_equipotent by A8,WELLORD2:def 4;
    then
A10: card A = card A' by CARD_1:21;
    P = {} implies B = {} by A7,ZFMISC_1:2;
    then
A11: dom g = B by FUNCT_2:def 1;
    B is independent by A3,Def2;
    then g is one-to-one by A6,Th10;
    then B, B' are_equipotent by A11,WELLORD2:def 4;
    then card B = card B' by CARD_1:21;
    then consider a being set such that
A12: a in B' \ A' by A10,A5,Th5;
    consider x' being set such that
A13: x' in B and
A14: a = g.x' by A11,A12,FUNCT_1:def 5;
    reconsider x = x' as Element of M by A13;
    take x;
A15: a nin A' by A12,XBOOLE_0:def 5;
    now
A16:  x in g.x by A6,A13;
      assume
A17:  x in A;
      then x in f.x by A4;
      then
A18:  f.x meets g.x by A16,XBOOLE_0:3;
A19:  g.x in rng g by A11,A13,FUNCT_1:def 5;
      f.x in rng f by A8,A17,FUNCT_1:def 5;
      hence contradiction by A15,A14,A19,A18,TAXONOM2:def 5;
    end;
    hence x in B \ A by A13,XBOOLE_0:def 5;
    reconsider xx = {x} as Subset of M by A13,ZFMISC_1:37;
    reconsider Ax = A \/ xx as Subset of union P by Def8;
A20: a in B' by A12;
    now
      let D be set;
A21:  Ax /\ D = A /\ D \/ xx /\ D by XBOOLE_1:23;
      assume
A22:  D in P;
      then consider d being Element of D such that
A23:  A /\ D c= {d} by A9,Th8;
      per cases;
      suppose
A24:    D = a;
        take x';
        A /\ D c= {}
        proof
          let z be set;
          assume
A25:      z in A /\ D;
          then
A26:      z in D by XBOOLE_0:def 4;
A27:      z in A by A25,XBOOLE_0:def 4;
          then z in f.z by A4;
          then
A28:      D meets f.z by A26,XBOOLE_0:3;
          f.z in rng f by A8,A27,FUNCT_1:def 5;
          hence thesis by A20,A15,A24,A28,TAXONOM2:def 5;
        end;
        then A /\ D = {};
        hence Ax /\ D c= {x'} by A21,XBOOLE_1:17;
      end;
      suppose
A29:    D <> a;
        a in rng g by A11,A13,A14,FUNCT_1:def 5;
        then
A30:    a misses D by A22,A29,TAXONOM2:def 5;
        x in a by A6,A13,A14;
        then x nin D by A30,XBOOLE_0:3;
        then xx c/= D by ZFMISC_1:37;
        then
A31:    xx /\ D <> xx by XBOOLE_1:17;
        reconsider d as set;
        take d;
        xx /\ D c= xx by XBOOLE_1:17;
        then xx /\ D = {} by A31,ZFMISC_1:39;
        hence Ax /\ D c= {d} by A23,A21;
      end;
    end;
    hence thesis by A1;
  end;
end;

registration
  let X be finite set;
  let P be Subset of bool X;
  cluster ProdMatroid P -> finite;
  coherence
  proof
    union P is finite;
    hence the carrier of ProdMatroid P is finite by Def8;
  end;
end;

registration
  let X be set;
  cluster -> mutually-disjoint a_partition of X;
  coherence
  proof
    let P be a_partition of X;
    let x,y be set;
    thus thesis by EQREL_1:def 6;
  end;
end;

registration
  cluster finite strict Matroid;
  existence
  proof
    consider X being finite non empty set, P being a_partition of X;
    take M = ProdMatroid P;
    thus thesis;
  end;
end;

registration
  let M be finite-membered non void SubsetFamilyStr;
  cluster -> finite (independent Subset of M);
  coherence
  proof
    let A be independent Subset of M;
A1: the_family_of M is finite-membered by Def6;
    A in the_family_of M by Def2;
    hence thesis by A1,Def5;
  end;
end;

definition
  let F be Field;
  let V be VectSp of F;
  func LinearlyIndependentSubsets V -> strict SubsetFamilyStr means
  : Def9:
  the
carrier of it = the carrier of V & the_family_of it = {A where A is Subset of V
  : A is linearly-independent};
  existence
  proof
    set F = {A where A is Subset of V: A is linearly-independent};
    set X = the carrier of V;
    F c= bool X
    proof
      let x be set;
      assume
      x in F;
      then ex A being Subset of X st x = A & A is linearly-independent;
      hence thesis;
    end;
    then reconsider F as Subset-Family of X;
    take M = TopStruct(#X,F#);
    thus thesis;
  end;
  uniqueness;
end;

registration
  let F be Field;
  let V be VectSp of F;
  cluster LinearlyIndependentSubsets V -> non empty non void subset-closed;
  coherence
  proof
    set M = LinearlyIndependentSubsets V;
A1: the_family_of M = {A where A is Subset of V: A is linearly-independent
    } by Def9;
    the carrier of M = the carrier of V by Def9;
    hence the carrier of M is non empty;
    {} V is linearly-independent;
    then {} in the_family_of M by A1;
    hence the topology of M is non empty;
    let x,y be set;
    assume
    x in the_family_of M;
    then
A2: ex A being Subset of V st x = A & A is linearly-independent by A1;
    assume
A3: y c= x;
    then reconsider B = y as Subset of V by A2,XBOOLE_1:1;
    B is linearly-independent by A2,A3,VECTSP_7:2;
    hence thesis by A1;
  end;
end;

theorem Th11:
  for F being Field, V being VectSp of F for A being Subset of
  LinearlyIndependentSubsets V holds A is independent iff A is
  linearly-independent Subset of V
proof
  let F be Field;
  let V be VectSp of F;
  set M = LinearlyIndependentSubsets V;
  let B be Subset of M;
  the_family_of M = {A where A is Subset of V: A is linearly-independent}
  by Def9;
  then B in the_family_of M iff ex A being Subset of V st B = A & A is
  linearly-independent;
  hence thesis by Def2;
end;

theorem
  for F being Field for V being VectSp of F for A, B being finite Subset
  of V st B c= A for v being Vector of V st v in Lin(A) & not v in Lin(B) holds
  ex w being Vector of V st w in A\B & w in Lin(A \ {w} \/ {v})
proof
  let F be Field;
  let V be VectSp of F;
  let A, B be finite Subset of V;
  assume
  B c= A;
  then A = B\/(A\B) by XBOOLE_1:45;
  hence thesis by VECTSP_9:22;
end;

theorem Th13:
  for F being Field, V being VectSp of F for A being Subset of V
  st A is linearly-independent for a being Element of V st a nin the carrier of
  Lin A holds A\/{a} is linearly-independent
proof
  let F be Field;
  let V be VectSp of F;
  let A be Subset of V such that
A1: A is linearly-independent;
A2: {Sum s where s is Linear_Combination of A: not contradiction} = the
  carrier of Lin A by VECTSP_7:def 2;
  let a be Element of V;
  set B = A\/{a};
  assume that
A3: a nin the carrier of Lin A and
A4: B is linearly-dependent;
  consider l being Linear_Combination of B such that
A5: Sum l = 0.V and
A6: Carrier l <> {} by A4,VECTSP_7:def 1;
  a in {a} by TARSKI:def 1;
  then
A7: (l!{a}).a = l.a by RANKNULL:25;
  A c= the carrier of Lin A
  proof
    let x be set;
    assume
A8: x in A;
    then reconsider x as Element of V;
    x in Lin A by A8,VECTSP_7:13;
    hence thesis by STRUCT_0:def 5;
  end;
  then a nin A by A3;
  then B\A={a} by XBOOLE_1:88,ZFMISC_1:56;
  then l = (l!A)+(l!{a}) by RANKNULL:27,XBOOLE_1:7;
  then 0.V = Sum (l!A) + Sum (l!{a}) by A5,VECTSP_6:77
    .= Sum (l!A) + (l.a)*a by A7,VECTSP_6:43;
  then
A9: (l.a)*a = - Sum (l!A) by ALGSTR_0:def 13;
A10: (-(l.a)")*(l!A) is Linear_Combination of A by VECTSP_6:61;
  now
    assume
    l.a <> 0.F;
    then a = ((l.a)")*(-Sum(l!A)) by A9,VECTSP_1:67
      .= -((l.a)" * Sum(l!A)) by VECTSP_1:69
      .= (-(l.a)")*(Sum(l!A)) by VECTSP_1:68
      .= Sum((-(l.a)")*(l!A)) by VECTSP_6:78;
    hence contradiction by A3,A2,A10;
  end;
  then
A11: a nin Carrier l by VECTSP_6:20;
A12: Carrier l c= B by VECTSP_6:def 7;
  Carrier l c= A
  proof
    let x be set;
    thus thesis by A11,A12,SETWISEO:6;
  end;
  then l is Linear_Combination of A by VECTSP_6:def 7;
  hence contradiction by A1,A5,A6,VECTSP_7:def 1;
end;

registration
  let F be Field;
  let V be VectSp of F;
  cluster LinearlyIndependentSubsets V -> with_exchange_property;
  coherence
  proof
    set M = LinearlyIndependentSubsets V;
A1: the_family_of M = {A where A is Subset of V: A is linearly-independent
    } by Def9;
    let A,B be finite Subset of M such that
A2: A in the_family_of M and
A3: B in the_family_of M and
A4: card B = (card A) + 1;
A5: B is independent by A3,Def2;
    A is independent by A2,Def2;
    then reconsider
    A' = A, B' = B as linearly-independent finite Subset of V by A5,Th11;
    set V' = Lin (A' \/ B');
    A' c= the carrier of V'
    proof
      let a be set;
      assume
      a in A';
      then a in A' \/ B' by XBOOLE_0:def 3;
      then a in V' by VECTSP_7:13;
      hence thesis by STRUCT_0:def 5;
    end;
    then reconsider A'' = A' as linearly-independent finite Subset of V' by
    VECTSP_9:16;
    B' c= the carrier of V'
    proof
      let a be set;
      assume
      a in B';
      then a in A' \/ B' by XBOOLE_0:def 3;
      then a in V' by VECTSP_7:13;
      hence thesis by STRUCT_0:def 5;
    end;
    then reconsider B'' = B' as linearly-independent finite Subset of V' by
    VECTSP_9:16;
A6: V' = Lin(A''\/ B'') by VECTSP_9:21;
    then consider D being Basis of V' such that
A7: B' c= D by VECTSP_7:27;
    consider C being Basis of V' such that
A8: C c= A'' \/ B'' by A6,VECTSP_7:28;
    reconsider c = C as finite set by A8;
    c is Basis of V';
    then V' is finite-dimensional by MATRLIN:def 3;
    then card c = card D by VECTSP_9:26;
    then card B c= card c by A7,CARD_1:27;
    then card B <= card c by NAT_1:40;
    then
A9: card A < card c by A4,NAT_1:13;
    consider e being Element of C \ the carrier of Lin A';
A10: A' is Basis of Lin A' by RANKNULL:20;
    then
A11: Lin A' is finite-dimensional by MATRLIN:def 3;
    now
      assume
      C c= the carrier of Lin A';
      then reconsider C' = C as Subset of Lin A';
      the carrier of Lin A' c= the carrier of V by VECTSP_4:def 2;
      then reconsider C'' = C' as Subset of V by XBOOLE_1:1;
      C is linearly-independent by VECTSP_7:def 3;
      then C'' is linearly-independent by VECTSP_9:15;
      then consider E being Basis of Lin A' such that
A12:  C' c= E by VECTSP_7:27,VECTSP_9:16;
A13:  card A = card E by A10,A11,VECTSP_9:26;
      then E is finite;
      hence contradiction by A9,A12,A13,NAT_1:44;
    end;
    then consider x being set such that
A14: x in C and
A15: x nin the carrier of Lin A' by TARSKI:def 3;
A16: x in C \ the carrier of Lin A' by A14,A15,XBOOLE_0:def 5;
    then
A17: e nin the carrier of Lin A' by XBOOLE_0:def 5;
A18: e in C by A16,XBOOLE_0:def 5;
    then e in A\/B by A8;
    then reconsider e as Element of M;
    take e;
    A c= the carrier of Lin A'
    proof
      let x be set;
      assume
      x in A;
      then x in Lin A' by VECTSP_7:13;
      hence thesis by STRUCT_0:def 5;
    end;
    then
A19: e nin A by A16,XBOOLE_0:def 5;
    then
A20: e in B' by A8,A18,XBOOLE_0:def 3;
    hence e in B \ A by A19,XBOOLE_0:def 5;
    reconsider a = e as Element of V by A20;
    A'\/{a} is linearly-independent by A17,Th13;
    hence A \/ {e} in the_family_of M by A1;
  end;
end;

registration
  let F be Field;
  let V be finite-dimensional VectSp of F;
  cluster LinearlyIndependentSubsets V -> finite-membered;
  coherence
  proof
    let A be set;
    set M = LinearlyIndependentSubsets V;
    assume
    A in the_family_of M;
    then A is independent Subset of M by Def2;
    then A is linearly-independent Subset of V by Th11;
    hence thesis by VECTSP_9:25;
  end;
end;

begin :: Maximal Independent Subsets, Ranks, and Basis

definition
  let M be SubsetFamilyStr;
  let A,C be Subset of M;
  pred A is_maximal_independent_in C means
  : Def10:
  A is independent & A c= C &
  for B being Subset of M st B is independent & B c= C & A c= B holds A = B;
end;

theorem Th14:
  for M being non void finite-degree SubsetFamilyStr for C,A being
Subset of M st A c= C & A is independent ex B being independent Subset of M st
  A c= B & B is_maximal_independent_in C
proof
  let M be non void finite-degree SubsetFamilyStr;
  let C,A0 be Subset of M;
  assume that
A1: A0 c= C and
A2: A0 is independent;
  reconsider AA = A0 as independent Subset of M by A2;
  defpred P[Nat] means for A being finite Subset of M st A0 c= A & A c= C & A
  is independent holds card A <= $1;
  consider n being Nat such that
A3: for A being finite Subset of M st A is independent holds card A <= n
  by Def7;
  reconsider n as Element of NAT by ORDINAL1:def 13;
  P[n] by A3;
  then
A4: ex n being Nat st P[n];
  consider n0 being Nat such that
A5: P[n0] & for m being Nat st P[m] holds n0 <= m from NAT_1:sch 5(A4);
  now
    0 <= card AA by NAT_1:2;
    then
A6: (card AA)+1 >= 0+1 by XREAL_1:8;
    assume
A7: for A being independent Subset of M st A0 c= A & A c= C holds card
    A < n0;
    then card AA < n0 by A1;
    then (card AA)+1 <= n0 by NAT_1:13;
    then consider n being Nat such that
A8: n0 = 1+n by A6,NAT_1:10,XXREAL_0:2;
    reconsider n as Element of NAT by ORDINAL1:def 13;
    P[n]
    proof
      let A be finite Subset of M;
      assume that
A9:   A0 c= A and
A10:  A c= C and
A11:  A is independent;
      card A < n+1 by A7,A8,A9,A10,A11;
      hence thesis by NAT_1:13;
    end;
    then n+1 <= n by A5,A8;
    hence contradiction by NAT_1:13;
  end;
  then consider A being independent Subset of M such that
A12: A0 c= A and
A13: A c= C and
A14: card A >= n0;
A15: card A <= n0 by A5,A12,A13;
  take A;
  thus A0 c= A & A is independent & A c= C by A12,A13;
  let B be Subset of M;
  assume that
A16: B is independent and
A17: B c= C and
A18: A c= B;
  reconsider B' = B as independent Subset of M by A16;
  card A <= card B' by A18,NAT_1:44;
  then
A19: n0 <= card B' by A14,XXREAL_0:2;
  A0 c= B by A12,A18,XBOOLE_1:1;
  then card B' <= n0 by A5,A17;
  then card B' = n0 by A19,XXREAL_0:1;
  hence thesis by A14,A18,A15,CARD_FIN:1,XXREAL_0:1;
end;

theorem
  for M being non void finite-degree subset-closed SubsetFamilyStr for C
  being Subset of M ex A being independent Subset of M st A
  is_maximal_independent_in C
proof
  let M be non void finite-degree subset-closed SubsetFamilyStr;
  let C be Subset of M;
  {} M c= C by XBOOLE_1:2;
  then ex A being independent Subset of M st {} M c= A & A
  is_maximal_independent_in C by Th14;
  hence thesis;
end;

theorem Th16:
  for M being non empty non void subset-closed finite-degree
  SubsetFamilyStr holds M is Matroid iff for C being Subset of M, A,B being
  independent Subset of M st A is_maximal_independent_in C & B
  is_maximal_independent_in C holds card A = card B
proof
  let M be non empty non void subset-closed finite-degree SubsetFamilyStr;
  hereby
    assume
A1: M is Matroid;
    let C be Subset of M;
A2: now
      let A,B be independent Subset of M such that
A3:   A is_maximal_independent_in C and
A4:   B is_maximal_independent_in C and
A5:   card A < card B;
A6:   A c= C by A3,Def10;
      (card A)+1 <= card B by A5,NAT_1:13;
      then (card A)+1 c= card B by NAT_1:40;
      then consider D being set such that
A7:   D c= B and
A8:   card D = (card A)+1 by CARD_FIL:36;
      reconsider D as finite Subset of M by A7,XBOOLE_1:1;
      D is independent by A7,Th3;
      then consider e being Element of M such that
A9:   e in D \ A and
A10:  A \/ {e} is independent by A1,A8,Th4;
      D \ A c= D by XBOOLE_1:36;
      then
A11:  D \ A c= B by A7,XBOOLE_1:1;
      B c= C by A4,Def10;
      then D \ A c= C by A11,XBOOLE_1:1;
      then {e} c= C by A9,ZFMISC_1:37;
      then
A12:  A \/ {e} c= C by A6,XBOOLE_1:8;
      A c= A \/ {e} by XBOOLE_1:7;
      then A \/ {e} = A by A3,A10,A12,Def10;
      then {e} c= A by XBOOLE_1:7;
      then e in A by ZFMISC_1:37;
      hence contradiction by A9,XBOOLE_0:def 5;
    end;
    let A,B be independent Subset of M such that
A13: A is_maximal_independent_in C and
A14: B is_maximal_independent_in C;
    card A < card B or card B < card A or card A = card B by XXREAL_0:1;
    hence card A = card B by A2,A13,A14;
  end;
  assume
A15: for C being Subset of M, A,B being independent Subset of M st A
is_maximal_independent_in C & B is_maximal_independent_in C holds card A = card
  B;
  M is with_exchange_property
  proof
    let A,B be finite Subset of M;
    reconsider C = A \/ B as Subset of M;
    assume that
A16: A in the_family_of M and
A17: B in the_family_of M and
A18: card B = (card A)+1;
    B is independent by A17,Def2;
    then consider B' being independent Subset of M such that
A19: B c= B' and
A20: B' is_maximal_independent_in C by Th14,XBOOLE_1:7;
A21: card B <= card B' by A19,NAT_1:44;
    assume
A22: for e be Element of M st e in B \ A holds not A \/ {e} in
    the_family_of M;
    reconsider A as independent Subset of M by A16,Def2;
    A is_maximal_independent_in C
    proof
      thus A in the_family_of M by A16;
      thus A c= C by XBOOLE_1:7;
      let D be Subset of M;
      assume that
A23:  D is independent and
A24:  D c= C and
A25:  A c= D;
      assume
      not (A c= D & D c= A);
      then consider e being set such that
A26:  e in D and
A27:  not e in A by A25,TARSKI:def 3;
      reconsider e as Element of M by A26;
      e in B by A24,A26,A27,XBOOLE_0:def 3;
      then e in B \ A by A27,XBOOLE_0:def 5;
      then not A \/ {e} in the_family_of M by A22;
      then
A28:  A \/ {e} is not independent by Def2;
      {e} c= D by A26,ZFMISC_1:37;
      then A \/ {e} c= D by A25,XBOOLE_1:8;
      hence contradiction by A23,A28,Th3;
    end;
    then card A = card B' by A15,A20;
    hence contradiction by A18,A21,NAT_1:13;
  end;
  hence thesis;
end;

definition
  let M be finite-degree Matroid;
  let C be Subset of M;
  func Rnk C -> Nat equals
  union {card A where A is independent Subset of M: A
  c= C};
  coherence
  proof
    set X = {card A where A is independent Subset of M: A c= C};
    defpred Q[Nat] means ex A being independent Subset of M st A c= C & $1 =
    card A;
    defpred P[Nat] means for A being independent Subset of M st A c= C holds
    card A <= $1;
    consider n being Nat such that
A1: for A being finite Subset of M st A is independent holds card A <=
    n by Def7;
A2: ex ne being Nat st P[ne]
    proof
      take n;
      thus thesis by A1;
    end;
    consider n0 being Nat such that
A3: P[n0] & for m being Nat st P[m] holds n0 <= m from NAT_1:sch 5(A2);
    union X = n0
    proof
      now
        let a be set;
        assume
        a in X;
        then consider A being independent Subset of M such that
A4:     a = card A and
A5:     A c= C;
        card A <= n0 by A3,A5;
        hence a c= n0 by A4,NAT_1:40;
      end;
      hence union X c= n0 by ZFMISC_1:94;
A6:   {} M c= C by XBOOLE_1:2;
A7:   for k being Nat st Q[k] holds k <= n0 by A3;
      card {} = card {};
      then
A8:   ex n being Nat st Q[n] by A6;
      consider n being Nat such that
A9:   Q[n] & for m being Nat st Q[m] holds m <= n from NAT_1:sch 6(
      A7,A8 );
      P[n] by A9;
      then
A10:  n0 <= n by A3;
      n <= n0 by A3,A9;
      then n = n0 by A10,XXREAL_0:1;
      then n0 in X by A9;
      hence thesis by ZFMISC_1:92;
    end;
    hence thesis;
  end;
end;

theorem Th17:
  for M being finite-degree Matroid for C being Subset of M for A
  being independent Subset of M st A c= C holds card A <= Rnk C
proof
  let M be finite-degree Matroid;
  let C be Subset of M;
  let A be independent Subset of M;
  assume
  A c= C;
  then card A in {card B where B is independent Subset of M: B c= C};
  then card A c= Rnk C by ZFMISC_1:92;
  hence thesis by NAT_1:40;
end;

theorem Th18:
  for M being finite-degree Matroid for C being Subset of M ex A
  being independent Subset of M st A c= C & card A = Rnk C
proof
  let M be finite-degree Matroid;
  let C be Subset of M;
  defpred P[Nat] means for A being independent Subset of M st A c= C holds
  card A <= $1;
  defpred Q[Nat] means ex A being independent Subset of M st A c= C & $1 =
  card A;
  set X = {card A where A is independent Subset of M: A c= C};
A1: {} M c= C by XBOOLE_1:2;
  card {} = card {};
  then
A2: ex n being Nat st Q[n] by A1;
  consider n being Nat such that
A3: for A being finite Subset of M st A is independent holds card A <= n
  by Def7;
A4: ex ne being Nat st P[ne]
  proof
    take n;
    thus thesis by A3;
  end;
  consider n0 being Nat such that
A5: P[n0] & for m being Nat st P[m] holds n0 <= m from NAT_1:sch 5(A4);
  now
    let a be set;
    assume
    a in X;
    then consider A being independent Subset of M such that
A6: a = card A and
A7: A c= C;
    card A <= n0 by A5,A7;
    hence a c= n0 by A6,NAT_1:40;
  end;
  then
A8: Rnk C c= n0 by ZFMISC_1:94;
A9: for k being Nat st Q[k] holds k <= n0 by A5;
  consider n being Nat such that
A10: Q[n] & for m being Nat st Q[m] holds m <= n from NAT_1:sch 6(A9,
  A2);
  P[n] by A10;
  then
A11: n0 <= n by A5;
  consider A being independent Subset of M such that
A12: A c= C and
A13: n = card A by A10;
  take A;
  n <= n0 by A5,A10;
  then
A14: n = n0 by A11,XXREAL_0:1;
  then n0 in X by A12,A13;
  then n0 c= Rnk C by ZFMISC_1:92;
  hence thesis by A8,A12,A13,A14,XBOOLE_0:def 10;
end;

theorem Th19:
  for M being finite-degree Matroid for C being Subset of M for A
being independent Subset of M holds A is_maximal_independent_in C iff A c= C &
  card A = Rnk C
proof
  let M be finite-degree Matroid;
  let C be Subset of M;
  set X = {card A where A is independent Subset of M: A c= C};
  let A be independent Subset of M;
  consider B being independent Subset of M such that
A1: B c= C and
A2: card B = Rnk C by Th18;
A3: now
    let A be independent Subset of M;
    assume that
A4: A c= C and
A5: card A = Rnk C;
    thus A is_maximal_independent_in C
    proof
      thus A is independent & A c= C by A4;
      let B be Subset of M;
      assume
      B is independent;
      then reconsider B' = B as independent Subset of M;
      assume
      B c= C;
      then card B' in X;
      then
A6:   card B' c= Rnk C by ZFMISC_1:92;
      assume
A7:   A c= B;
      then card A c= card B' by CARD_1:27;
      then card A = card B' by A5,A6,XBOOLE_0:def 10;
      hence thesis by A7,CARD_FIN:1;
    end;
  end;
  hereby
    assume
A8: A is_maximal_independent_in C;
    hence A c= C by Def10;
    B is_maximal_independent_in C by A3,A1,A2;
    hence card A = Rnk C by A2,A8,Th16;
  end;
  thus thesis by A3;
end;

theorem Th20:
  for M being finite-degree Matroid for C being finite Subset of M
  holds Rnk C <= card C
proof
  let M be finite-degree Matroid;
  let C be finite Subset of M;
  ex A being independent Subset of M st A c= C & card A = Rnk C by Th18;
  then Rnk C c= card C by CARD_1:27;
  hence thesis by NAT_1:40;
end;

theorem Th21:
  for M being finite-degree Matroid for C being finite Subset of M
  holds C is independent iff card C = Rnk C
proof
  let M be finite-degree Matroid;
  let C be finite Subset of M;
  set X = {card A where A is independent Subset of M: A c= C};
  hereby
    assume
    C is independent;
    then card C in X;
    then card C c= Rnk C by ZFMISC_1:92;
    then
A1: card C <= Rnk C by NAT_1:40;
    Rnk C <= card C by Th20;
    hence card C = Rnk C by A1,XXREAL_0:1;
  end;
  ex A being independent Subset of M st A c= C & card A = Rnk C by Th18;
  hence thesis by CARD_FIN:1;
end;

definition
  let M be finite-degree Matroid;
  func Rnk M -> Nat equals
  Rnk [#]M;
  coherence;
end;

definition
  let M be non void finite-degree SubsetFamilyStr;
  mode Basis of M -> independent Subset of M means
    : Def13:
    it
    is_maximal_independent_in [#]M;
  existence
  proof
    consider A being independent Subset of M;
    set C = [#]M;
    consider B being independent Subset of M such that
    A c= B and
A1: B is_maximal_independent_in C by Th14;
    take B;
    thus thesis by A1;
  end;
end;

theorem
  for M being finite-degree Matroid for B1,B2 being Basis of M holds
  card B1 = card B2
proof
  let M be finite-degree Matroid;
  let B1,B2 be Basis of M;
A1: B2 is_maximal_independent_in [#]M by Def13;
  B1 is_maximal_independent_in [#]M by Def13;
  hence thesis by A1,Th16;
end;

theorem
  for M being finite-degree Matroid for A being independent Subset of M
  ex B being Basis of M st A c= B
proof
  let M be finite-degree Matroid;
  let A be independent Subset of M;
  consider B being independent Subset of M such that
A1: A c= B and
A2: B is_maximal_independent_in [#]M by Th14;
  reconsider B as Basis of M by A2,Def13;
  take B;
  thus thesis by A1;
end;

reserve M for finite-degree Matroid,
  A,B,C for Subset of M,
  e,f for Element of
  M;

theorem Th24:
  A c= B implies Rnk A <= Rnk B
proof
  ex C being independent Subset of M st C c= A & card C = Rnk A by Th18;
  hence thesis by Th17,XBOOLE_1:1;
end;

theorem Th25:
  Rnk (A\/B) + Rnk (A/\B) <= Rnk A + Rnk B
proof
  consider C being independent Subset of M such that
A1: C c= A/\B and
A2: card C = Rnk (A/\B) by Th18;
  A/\B c= A by XBOOLE_1:17;
  then C c= A by A1,XBOOLE_1:1;
  then consider Ca being independent Subset of M such that
A3: C c= Ca and
A4: Ca is_maximal_independent_in A by Th14;
A5: Ca c= A by A4,Def10;
A6: Ca/\B c= C
  proof
    let x be set;
    assume
A7: x in Ca/\B;
    then
A8: x in Ca by XBOOLE_0:def 4;
    then {x} c= Ca by ZFMISC_1:37;
    then C\/{x} c= Ca by A3,XBOOLE_1:8;
    then reconsider Cx = C\/{x} as independent Subset of M by Th3,XBOOLE_1:1;
    x in B by A7,XBOOLE_0:def 4;
    then x in A/\B by A5,A8,XBOOLE_0:def 4;
    then {x} c= A/\B by ZFMISC_1:37;
    then
A9: Cx c= A/\B by A1,XBOOLE_1:8;
A10: C c= Cx by XBOOLE_1:7;
    C is_maximal_independent_in A/\B by A1,A2,Th19;
    then C = Cx by A9,A10,Def10;
    then {x} c= C by XBOOLE_1:7;
    hence thesis by ZFMISC_1:37;
  end;
  A/\B c= B by XBOOLE_1:17;
  then C c= B by A1,XBOOLE_1:1;
  then C c= Ca /\B by A3,XBOOLE_1:19;
  then
A11: Ca/\B = C by A6,XBOOLE_0:def 10;
  A c= A\/B by XBOOLE_1:7;
  then Ca c= A\/B by A5,XBOOLE_1:1;
  then consider C' being independent Subset of M such that
A12: Ca c= C' and
A13: C' is_maximal_independent_in A\/B by Th14;
A14: Ca/\(C'/\B) = Ca/\C'/\B by XBOOLE_1:16
    .= Ca/\B by A12,XBOOLE_1:28;
A15: C' c= A\/B by A13,Th19;
A16: C' = Ca \/ (C'/\B)
  proof
    thus C' c= Ca \/ (C'/\B)
    proof
      let x be set;
      assume
A17:  x in C';
      then {x} c= C' by ZFMISC_1:37;
      then Ca\/{x} c= C' by A12,XBOOLE_1:8;
      then reconsider Cax = Ca\/{x} as independent Subset of M by
      Th3, XBOOLE_1
      :1;
A18:  now
        assume
        x in A;
        then {x} c= A by ZFMISC_1:37;
        then
A19:    Cax c= A by A5,XBOOLE_1:8;
        Ca c= Cax by XBOOLE_1:7;
        then Ca = Cax by A4,A19,Def10;
        then {x} c= Ca by XBOOLE_1:7;
        hence x in Ca by ZFMISC_1:37;
      end;
      x in B implies x in C'/\B by A17,XBOOLE_0:def 4;
      hence thesis by A15,A17,A18,XBOOLE_0:def 3;
    end;
    let x be set;
    assume
    x in Ca \/ (C'/\B);
    then x in Ca or x in C'/\B by XBOOLE_0:def 3;
    hence thesis by A12,XBOOLE_0:def 4;
  end;
  C'/\B c= B by XBOOLE_1:17;
  then consider Cb being independent Subset of M such that
A20: C'/\B c= Cb and
A21: Cb is_maximal_independent_in B by Th14;
  card Cb = Rnk B by A21,Th19;
  then
A22: card (C'/\B) <= Rnk B by A20,NAT_1:44;
A23: card C' = Rnk (A\/B) by A13,Th19;
  card Ca = Rnk A by A4,Th19;
  then Rnk (A\/B) = Rnk A + card (C'/\B) - Rnk (A/\B) by A2,A23,A16,A14,
  A11,CARD_2:64;
  hence thesis by A22,XREAL_1:8;
end;

theorem Th26:
  Rnk A <= Rnk (A\/B) & Rnk (A \/ {e}) <= Rnk A + 1
proof
A1: card {e} = 1 by CARD_1:50;
  thus Rnk A <= Rnk (A\/B) by Th24,XBOOLE_1:7;
A2: Rnk(A\/{e}) + Rnk(A/\{e}) <= Rnk A + Rnk {e} by Th25;
  Rnk {e} <= card {e} by Th20;
  then Rnk A + Rnk {e} <= Rnk A + 1 by A1,XREAL_1:8;
  then
A3: Rnk(A\/{e}) + Rnk(A/\{e}) <= Rnk A + 1 by A2,XXREAL_0:2;
  Rnk(A\/{e}) <= Rnk(A\/{e}) + Rnk(A/\{e}) by NAT_1:11;
  hence thesis by A3,XXREAL_0:2;
end;

theorem
  Rnk (A\/{e}) = Rnk (A\/{f}) & Rnk (A\/{f}) = Rnk A implies Rnk (A \/ {
  e,f}) = Rnk A
proof
  assume that
A1: Rnk (A\/{e}) = Rnk (A\/{f}) and
A2: Rnk (A\/{f}) = Rnk A;
  consider C being independent Subset of M such that
A3: C c= A and
A4: card C = Rnk A by Th18;
A5: C is_maximal_independent_in A by A3,A4,Th19;
  A c= A\/{f} by XBOOLE_1:7;
  then C c= A\/{f} by A3,XBOOLE_1:1;
  then
A6: C is_maximal_independent_in A\/{f} by A4,A2,Th19;
  A c= A\/{e} by XBOOLE_1:7;
  then C c= A\/{e} by A3,XBOOLE_1:1;
  then
A7: C is_maximal_independent_in A\/{e} by A4,A1,A2,Th19;
  A c= A\/{e,f} by XBOOLE_1:7;
  then C c= A\/{e,f} by A3,XBOOLE_1:1;
  then consider C' being independent Subset of M such that
A8: C c= C' and
A9: C' is_maximal_independent_in A\/{e,f} by Th14;
A10: C' c= A\/{e,f} by A9,Th19;
  now
    assume
    C' <> C;
    then consider x being set such that
A11: not (x in C' iff x in C) by TARSKI:2;
    {x} c= C' by A8,A11,ZFMISC_1:37;
    then C\/{x} c= C' by A8,XBOOLE_1:8;
    then reconsider Cx = C\/{x} as independent Subset of M by Th3,XBOOLE_1:1;
    now
      assume
      x in A;
      then {x} c= A by ZFMISC_1:37;
      then
A12:  Cx c= A by A3,XBOOLE_1:8;
      C c= Cx by XBOOLE_1:7;
      then C = Cx by A5,A12,Def10;
      then {x} c= C by XBOOLE_1:7;
      hence contradiction by A8,A11,ZFMISC_1:37;
    end;
    then x in {e,f} by A8,A10,A11,XBOOLE_0:def 3;
    then x = e or x = f by TARSKI:def 2;
    then {x} c= A\/{e} & C c= A\/{e} or {x} c= A\/{f} & C c= A\/{f} by A3,
    XBOOLE_1:10;
    then
A13: Cx c= A\/{e} or Cx c= A\/{f} by XBOOLE_1:8;
    C c= Cx by XBOOLE_1:7;
    then C = Cx by A7,A6,A13,Def10;
    then {x} c= C by XBOOLE_1:7;
    hence contradiction by A8,A11,ZFMISC_1:37;
  end;
  hence thesis by A4,A9,Th19;
end;

begin :: Dependence from a Set, Spans, and Cycles

definition
  let M be finite-degree Matroid;
  let e be Element of M;
  let A be Subset of M;
  pred e is_dependent_on A means
  : Def14:
  Rnk (A \/ {e}) = Rnk A;
end;

theorem Th28:
  e in A implies e is_dependent_on A
proof
  assume
  e in A;
  then {e} c= A by ZFMISC_1:37;
  hence Rnk(A\/{e}) = Rnk A by XBOOLE_1:12;
end;

theorem Th29:
  A c= B & e is_dependent_on A implies e is_dependent_on B
proof
  assume that
A1: A c= B and
A2: Rnk (A \/ {e}) = Rnk A;
  consider Ca being independent Subset of M such that
A3: Ca c= A and
A4: card Ca = Rnk A by Th18;
A5: Ca c= B by A1,A3,XBOOLE_1:1;
  B c= B\/{e} by XBOOLE_1:7;
  then Ca c= B\/{e} by A5,XBOOLE_1:1;
  then consider E being independent Subset of M such that
A6: Ca c= E and
A7: E is_maximal_independent_in B\/{e} by Th14;
A8: now
    E c= B\/{e} by A7,Th19;
    then
A9: E = E/\(B\/{e}) by XBOOLE_1:28
      .= E/\B\/E/\{e} by XBOOLE_1:23;
    E/\{e} c= {e} by XBOOLE_1:17;
    then
A10: E/\{e} = {} & card {} = 0 or E/\{e} = {e} & card {e} = 1 by
    CARD_1:50,ZFMISC_1:39;
    card (E/\B) <= Rnk B by Th17,XBOOLE_1:17;
    then
A11: card (E/\B)+1 <= Rnk B + 1 by XREAL_1:8;
    Ca c= A\/{e} by A3,XBOOLE_1:10;
    then
A12: Ca is_maximal_independent_in A\/{e} by A2,A4,Th19;
A13: Ca c= Ca\/{e} by XBOOLE_1:10;
    assume
A14: Rnk (B\/{e}) = Rnk B + 1;
    then card E = Rnk B + 1 by A7,Th19;
    then Rnk B + 1 <= card (E/\B) + card (E/\{e}) by A9,CARD_2:62;
    then card (E/\B)+1 <= card (E/\B) + card (E/\{e}) by A11,XXREAL_0:2;
    then e in E/\{e} by A10,TARSKI:def 1,XREAL_1:8;
    then e in E by XBOOLE_0:def 4;
    then {e} c= E by ZFMISC_1:37;
    then Ca\/{e} c= E by A6,XBOOLE_1:8;
    then
A15: Ca\/{e} is independent by Th3;
    Ca\/{e} c= A\/{e} by A3,XBOOLE_1:9;
    then Ca = Ca\/{e} by A13,A15,A12,Def10;
    then {e} c= Ca by XBOOLE_1:7;
    then B = B\/{e} by A5,XBOOLE_1:1,12;
    hence contradiction by A14;
  end;
A16: Rnk (B\/{e}) <= Rnk B + 1 by Th26;
  Rnk B <= Rnk (B\/{e}) by Th26;
  hence Rnk (B\/{e}) = Rnk B by A16,A8,NAT_1:9;
end;

definition
  let M be finite-degree Matroid;
  let A be Subset of M;
  func Span A -> Subset of M equals
  {e where e is Element of M: e
  is_dependent_on A};
  coherence
  proof
    set X = {e where e is Element of M: e is_dependent_on A};
    X c= the carrier of M
    proof
      let x be set;
      assume
      x in X;
      then ex e being Element of M st x = e & e is_dependent_on A;
      hence thesis;
    end;
    hence thesis;
  end;
end;

theorem Th30:
  e in Span A iff Rnk (A \/ {e}) = Rnk A
proof
  hereby
    assume
    e in Span A;
    then ex x being Element of M st e = x & x is_dependent_on A;
    hence Rnk (A \/ {e}) = Rnk A by Def14;
  end;
  assume
  Rnk (A \/ {e}) = Rnk A;
  then e is_dependent_on A by Def14;
  hence thesis;
end;

theorem Th31:
  A c= Span A
proof
  let e be set;
  assume
A1: e in A;
  then reconsider x = e as Element of M;
  x is_dependent_on A by A1,Th28;
  hence thesis;
end;

theorem
  A c= B implies Span A c= Span B
proof
  assume
A1: A c= B;
  let x be set;
  assume
  x in Span A;
  then ex e st x = e & e is_dependent_on A;
  then ex e st x = e & e is_dependent_on B by A1,Th29;
  hence thesis;
end;

theorem Th33:
  Rnk Span A = Rnk A
proof
  consider Ca being independent Subset of M such that
A1: Ca c= A and
A2: card Ca = Rnk A by Th18;
  A c= Span A by Th31;
  then Ca c= Span A by A1,XBOOLE_1:1;
  then consider C being independent Subset of M such that
A3: Ca c= C and
A4: C is_maximal_independent_in Span A by Th14;
  now
    assume
    C c/= Ca;
    then consider x being set such that
A5: x in C and
A6: x nin Ca by TARSKI:def 3;
    C c= Span A by A4,Th19;
    then x in Span A by A5;
    then consider e being Element of M such that
A7: x = e and
A8: e is_dependent_on A;
    {e} c= C by A5,A7,ZFMISC_1:37;
    then Ca\/{e} c= C by A3,XBOOLE_1:8;
    then reconsider Ce = Ca\/{e} as independent Subset of M by Th3;
    Ce c= A\/{e} by A1,XBOOLE_1:9;
    then consider D being independent Subset of M such that
A9: Ce c= D and
A10: D is_maximal_independent_in A\/{e} by Th14;
    card Ca = Rnk (A\/{e}) by A2,A8,Def14
      .= card D by A10,Th19;
    then
A11: card Ce <= card Ca by A9,NAT_1:44;
    card Ca <= card Ce by NAT_1:44,XBOOLE_1:7;
    then card Ca = card Ce by A11,XXREAL_0:1;
    then Ca = Ce by CARD_FIN:1,XBOOLE_1:7;
    then e nin {e} by A6,A7,XBOOLE_0:def 3;
    hence contradiction by TARSKI:def 1;
  end;
  then C = Ca by A3,XBOOLE_0:def 10;
  hence thesis by A2,A4,Th19;
end;

theorem Th34:
  e is_dependent_on Span A implies e is_dependent_on A
proof
  assume
A1: Rnk ((Span A)\/{e}) = Rnk Span A;
A2: Rnk A = Rnk Span A by Th33;
  consider Ca being independent Subset of M such that
A3: Ca c= A and
A4: card Ca = Rnk A by Th18;
A5: Rnk A = Rnk Ca by A4,Th21;
A6: Rnk Ca <= Rnk(A\/{e}) by A3,Th24,XBOOLE_1:10;
  A c= Span A by Th31;
  then Rnk(A\/{e}) <= Rnk A by A1,A2,Th24,XBOOLE_1:9;
  hence Rnk (A\/{e}) = Rnk A by A5,A6,XXREAL_0:1;
end;

theorem
  Span Span A = Span A
proof
  thus Span Span A c= Span A
  proof
    let x be set;
    assume
    x in Span Span A;
    then consider e being Element of M such that
A1: x = e and
A2: e is_dependent_on Span A;
    e is_dependent_on A by A2,Th34;
    hence thesis by A1;
  end;
  thus thesis by Th31;
end;

theorem
  f nin Span A & f in Span (A \/ {e}) implies e in Span (A \/ {f})
proof
  assume that
A1: f nin Span A and
A2: f in Span (A \/ {e});
A3: Rnk A <= Rnk (A\/{f}) by Th26;
A4: Rnk (A\/{f}) <= Rnk A + 1 by Th26;
  Rnk A <> Rnk (A\/{f}) by A1,Th30;
  then
A5: Rnk (A\/{f}) = Rnk A + 1 by A3,A4,NAT_1:9;
A6: A\/{f}\/{e} = A\/({f}\/{e}) by XBOOLE_1:4;
A7: Rnk (A\/{e}) <= Rnk A + 1 by Th26;
A8: A\/{e}\/{f} = A\/({e}\/{f}) by XBOOLE_1:4;
A9: Rnk(A\/{e}\/{f}) = Rnk(A\/{e}) by A2,Th30;
  then Rnk(A\/{f}) <= Rnk(A\/{e}) by A6,A8,Th26;
  then Rnk (A\/{f}) = Rnk (A\/{f}\/{e}) by A9,A5,A6,A8,A7,XXREAL_0:1;
  hence thesis by Th30;
end;

definition
  let M be SubsetFamilyStr;
  let A be Subset of M;
  attr A is cycle means
  : Def16:
  A is dependent & for e being Element of M st e
  in A holds A \ {e} is independent;
end;

theorem Th37:
  A is cycle implies A is non empty finite
proof
  assume that
A1: A is dependent and
A2: for e being Element of M st e in A holds A \ {e} is independent;
  thus A is non empty by A1;
  consider e being Element of A;
  now
    assume
A3: A is non empty set;
    then e in A;
    then reconsider e as Element of M;
    reconsider Ae = A\{e} as independent Subset of M by A2,A3;
    A = Ae\/{e} by A3,ZFMISC_1:140;
    hence thesis;
  end;
  hence thesis;
end;

registration
  let M;
  cluster cycle -> non empty finite Subset of M;
  coherence by Th37;
end;

theorem Th38:
  A is cycle iff A is non empty & for e st e in A holds A\{e}
  is_maximal_independent_in A
proof
  thus A is cycle implies A is non empty & for e st e in A holds A\{e}
  is_maximal_independent_in A
  proof
    assume that
A1: A is dependent and
A2: for e being Element of M st e in A holds A \ {e} is independent;
    thus A is non empty by A1;
    let e;
    set Ae = A\{e};
    assume
A3: e in A;
    hence Ae is independent & Ae c= A by A2,XBOOLE_1:36;
    let B;
    assume that
A4: B is independent and
A5: B c= A and
A6: Ae c= B;
    A = Ae\/{e} by A3,ZFMISC_1:140;
    hence thesis by A1,A4,A5,A6,BORSUK_5:2;
  end;
  consider a being Element of A;
  assume that
A7: A is non empty and
A8: for e st e in A holds A\{e} is_maximal_independent_in A;
  a in A by A7;
  then reconsider a as Element of M;
  set Ae = A\{a};
A9: Ae c= A by XBOOLE_1:36;
A10: Ae is_maximal_independent_in A by A7,A8;
  hereby
    assume
    A is independent;
    then A = Ae by A10,A9,Def10;
    then a nin {a} by A7,XBOOLE_0:def 5;
    hence contradiction by TARSKI:def 1;
  end;
  let e;
  assume
  e in A;
  then A\{e} is_maximal_independent_in A by A8;
  hence thesis by Def10;
end;

theorem Th39:
  A is cycle implies Rnk A + 1 = card A
proof
  assume
A1: A is cycle;
  then reconsider A as non empty finite Subset of M;
  consider a being Element of A;
A2: A\{a} is_maximal_independent_in A by A1,Th38;
  then A\{a} is independent by Def10;
  then
A3: Rnk A = card (A\{a}) by A2,Th19;
  a in {a} by TARSKI:def 1;
  then
A4: a nin A\{a} by XBOOLE_0:def 5;
  A = (A\{a})\/{a} by ZFMISC_1:140;
  hence thesis by A3,A4,CARD_2:54;
end;

theorem
  A is cycle & e in A implies e is_dependent_on A\{e}
proof
  assume that
A1: A is cycle and
A2: e in A;
  reconsider Ae = A\{e} as independent Subset of M by A1,A2,Def16;
  Ae is_maximal_independent_in A by A1,A2,Th38;
  then Rnk A = card Ae by Th19;
  hence Rnk((A\{e})\/{e}) = card Ae by A2,ZFMISC_1:140
    .= Rnk (A\{e}) by Th21;
end;

theorem Th41:
  A is cycle & B is cycle & A c= B implies A = B
proof
  assume that
A1: A is dependent and
  for e st e in A holds A \ {e} is independent and
  B is dependent and
A2: for e st e in B holds B \ {e} is independent;
  assume that
A3: A c= B and
A4: A <> B;
  consider x being set such that
A5: not (x in A iff x in B) by A4,TARSKI:2;
  reconsider x as Element of M by A5;
A6: A c= B\{x} by A3,A5,ZFMISC_1:40;
  B\{x} is independent by A2,A3,A5;
  hence contradiction by A1,A6,Th3;
end;

theorem Th42:
  (for B st B c= A holds B is not cycle) implies A is independent
proof
  assume
A1: for B st B c= A holds B is not cycle;
  consider C being independent Subset of M such that
A2: C c= A and
A3: card C = Rnk A by Th18;
  per cases;
  suppose
    A c= C;
    hence thesis by A2,XBOOLE_0:def 10;
  end;
  suppose
    A c/= C;
    then consider x being set such that
A4: x in A and
A5: x nin C by TARSKI:def 3;
    reconsider x as Element of M by A4;
A6: C c= C\/{x} by SETWISEO:8;
    defpred P[Nat] means ex B being independent Subset of M st card B = $1 & B
    c= C & B\/{x} is dependent;
A7: C\/{x} c= A by A2,A4,SETWISEO:8;
A8: ex n being Nat st P[n]
    proof
      take n = Rnk A, C;
      thus card C = n & C c= C by A3;
      assume
A9:   C\/{x} is independent;
      C is_maximal_independent_in A by A2,A3,Th19;
      then C = C\/{x} by A7,A6,A9,Def10;
      then {x} c= C by XBOOLE_1:7;
      hence contradiction by A5,ZFMISC_1:37;
    end;
    consider n being Nat such that
A10: P[n] & for k being Nat st P[k] holds n <= k from NAT_1:sch 5(A8);
    consider B being independent Subset of M such that
A11: card B = n and
A12: B c= C and
A13: B\/{x} is dependent by A10;
A14: x nin B by A5,A12;
A15: B\/{x} is cycle
    proof
      thus B\/{x} is dependent by A13;
      let e be Element of M;
      set Be = B\{e};
A16:  Be c= B by XBOOLE_1:36;
      assume
A17:  e in B\/{x};
      per cases by A17,SETWISEO:6;
      suppose
A18:    e in B;
A19:    e nin Be by ZFMISC_1:64;
        B = Be\/{e} by A18,ZFMISC_1:140;
        then
A20:    n = card Be+1 by A11,A19,CARD_2:54;
        assume
A21:    (B\/{x}) \ {e} is dependent;
        (B\/{x}) \ {e} = Be\/{x} by A14,A18,XBOOLE_1:87,ZFMISC_1:17;
        then n <= card Be by A10,A12,A16,A21,XBOOLE_1:1;
        hence contradiction by A20,NAT_1:13;
      end;
      suppose
        e = x;
        hence (B\/{x}) \ {e} is independent by A14,ZFMISC_1:141;
      end;
    end;
    B c= A by A2,A12,XBOOLE_1:1;
    then B\/{x} c= A by A4,SETWISEO:8;
    hence thesis by A1,A15;
  end;
end;

theorem Th43:
  A is cycle & B is cycle & A <> B & e in A /\ B implies ex C st C
  is cycle & C c= (A \/ B) \ {e}
proof
  assume that
A1: A is cycle and
A2: B is cycle and
A3: A <> B and
A4: e in A /\ B and
A5: for C st C is cycle holds C c/= (A \/ B) \ {e};
A6: e in A by A4,XBOOLE_0:def 4;
  A/\B c= B by XBOOLE_1:17;
  then A c/= A/\B by A1,A2,A3,Th41,XBOOLE_1:1;
  then consider a being set such that
A7: a in A and
A8: a nin A/\B by TARSKI:def 3;
  reconsider a as Element of M by A7;
  {a} misses A/\B by A8,ZFMISC_1:56;
  then
A9: A/\B c= A\{a} by XBOOLE_1:17,86;
  reconsider A' = A, B' = B as finite Subset of M by A1,A2;
  Rnk(A\/B)+Rnk(A/\B) <= Rnk A + Rnk B by Th25;
  then
A10: Rnk(A\/B)+Rnk(A/\B)+1 <= Rnk A + Rnk B+1 by XREAL_1:8;
  A\{a} is independent by A1,A7,Def16;
  then A/\B is independent by A9,Th3;
  then
A11: card (A'/\B') = Rnk (A/\B) by Th21;
  for C st C c= (A \/ B) \ {e} holds C is not cycle by A5;
  then reconsider C = (A\/B)\{e} as independent Subset of M by Th42;
A12: e in {e} by TARSKI:def 1;
  then
A13: e nin C by XBOOLE_0:def 5;
A14: e in B by A4,XBOOLE_0:def 4;
  then reconsider
  Ae = A\{e}, Be = B\{e} as independent Subset of M by A1,A2,A6,Def16;
A15: e nin Be by A12,XBOOLE_0:def 5;
  B = Be\/{e} by A14,ZFMISC_1:140;
  then
A16: card B' = card Be+1 by A15,CARD_2:54;
  then
A17: Rnk B + 1 = card Be+1 by A2,Th39;
A18: e nin Ae by A12,XBOOLE_0:def 5;
  A = Ae\/{e} by A6,ZFMISC_1:140;
  then
A19: card A' = card Ae+1 by A18,CARD_2:54;
  then Rnk A + 1 = card Ae+1 by A1,Th39;
  then card(A'\/B')+card(A'/\B') = Rnk A+1 + (Rnk B+1) by A19,A16,A17,
  HALLMAR1:1
    .= Rnk A + Rnk B+1+1;
  then
A20: Rnk(A\/B)+Rnk(A/\B)+1+1 <= card(A'\/B')+card(A'/\B') by
  A10,XREAL_1
  :8;
  e in A\/B by A6,XBOOLE_0:def 3;
  then
A21: C\/{e} = A'\/B' by ZFMISC_1:140;
  C is_maximal_independent_in A\/B
  proof
    thus C is independent & C c= A\/B by XBOOLE_1:36;
    let D be Subset of M;
A22: A c= A\/B by XBOOLE_1:7;
    A is dependent by A1,Def16;
    then A\/B is dependent by A22,Th3;
    hence thesis by A21,BORSUK_5:2;
  end;
  then Rnk(A\/B)+1 = card C+1 by Th19
    .= card(A'\/B') by A13,A21, CARD_2:54;
  hence contradiction by A20,A11,NAT_1:13;
end;

theorem
  A is independent & B is cycle & C is cycle & B c= A\/{e} & C c= A\/{e}
  implies B = C
proof
  assume that
A1: A is independent and
A2: B is cycle and
A3: C is cycle and
A4: B c= A\/{e} and
A5: C c= A\/{e};
  now
    assume
    C c= A;
    then C is independent by A1,Th3;
    hence contradiction by A3,Def16;
  end;
  then consider c being set such that
A6: c in C and
A7: c nin A by TARSKI:def 3;
  c in {e} by A5,A6,A7,XBOOLE_0:def 3;
  then
A8: c = e by TARSKI:def 1;
  now
    assume
    B c= A;
    then B is independent by A1,Th3;
    hence contradiction by A2,Def16;
  end;
  then consider b being set such that
A9: b in B and
A10: b nin A by TARSKI:def 3;
  assume
A11: B <> C;
  b in {e} by A4,A9,A10,XBOOLE_0:def 3;
  then b = e by TARSKI:def 1;
  then e in B/\C by A9,A6,A8,XBOOLE_0:def 4;
  then consider D being Subset of M such that
A12: D is cycle and
A13: D c= (B \/ C) \ {e} by A2,A3,A11,Th43;
  D c= A
  proof
    let x be set;
    assume
A14: x in D;
    then x in B\/C by A13,XBOOLE_0:def 5;
    then
A15: x in B or x in C by XBOOLE_0:def 3;
    x nin {e} by A13,A14,XBOOLE_0:def 5;
    hence thesis by A4,A5,A15,XBOOLE_0:def 3;
  end;
  then D is independent by A1,Th3;
  hence thesis by A12,Def16;
end;

