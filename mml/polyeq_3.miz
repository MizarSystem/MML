:: Solving Complex Roots of Polynomial Equation of Degree 2 and 3 with
:: Complex Coefficients
::  by Yuzhong Ding and Xiquan Liang
::
:: Received January 26, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies COMPLEX1, ARYTM_3, ARYTM_1, POLYEQ_1, SQUARE_1, FUNCT_3,
      POLYEQ_3, RELAT_1, POWER, GROUP_1, PREPOWER, FUNCT_1, SIN_COS, HAHNBAN1,
      COMPTRIG, XCMPLX_0, ARYTM;
 notations SUBSET_1, NUMBERS, XBOOLE_0, ORDINAL1, XCMPLX_0, XREAL_0, REAL_1,
      COMPLEX1, POLYEQ_1, SQUARE_1, COMSEQ_1, ARYTM_0, QUIN_1, POWER, NAT_1,
      COMSEQ_3, SIN_COS, HAHNBAN1, PREPOWER, BINARITH, SEQ_1, COMPTRIG;
 constructors COMPLEX1, REAL_1, POLYEQ_1, QUIN_1, NEWTON, GROUP_1, COMSEQ_1,
      ARYTM_0, SQUARE_1, SUBSET_1, POWER, INT_1, NAT_1, COMSEQ_3, COMPLFLD,
      COMPLSP1, RLVECT_1, HAHNBAN1, COMPTRIG, SIN_COS, PREPOWER, BINARITH,
      VECTSP_1, NUMBERS, SEQ_1, COMPLEX2, ARYTM_3, FUNCT_4, BINOP_2;
 registrations REAL_1, SQUARE_1, XREAL_0, INT_1, BINARITH, XCMPLX_0, POLYEQ_1,
      MEMBERED;
 requirements ARITHM, SUBSET, REAL, BOOLE, NUMERALS;
 theorems COMPLEX1, POLYEQ_1, REAL_1, REAL_2, AXIOMS, SQUARE_1, QUIN_1, NEWTON,
      COMSEQ_3, COMPTRIG, HAHNBAN1, NAT_1, BINARITH, POWER, SIN_COS, SIN_COS2,
      PREPOWER, CARD_1, COMPLEX2, XCMPLX_0, XCMPLX_1, ARYTM_0;
 schemes NAT_1, COMPTRIG;

begin

Lm1: 0 = [*0,0*] by ARYTM_0:def 7;

 reserve a,b,c,d,e,a',b',c',d',x,y,x1,y1,x2,y2,u,v for Real,
         s,t,h,z,z1,z2,z3,z4,s1,s2,s3,p,q for Element of COMPLEX,
         m,n for Nat;

definition let a be Real,z;
  redefine func a*z -> Element of COMPLEX;
  correctness by XCMPLX_0:def 2;
  redefine func a + z -> Element of COMPLEX;
  correctness by XCMPLX_0:def 2;
end;

definition let z be Element of COMPLEX;
  redefine func z^2 -> Element of COMPLEX equals
    :Def3: (Re z)^2-(Im z)^2 + 2*(Re z*Im z)*<i>;
  compatibility
  proof
    z = Re z +(Im z)*<i> by COMPLEX1:29;
    then z^2 = (Re z +(Im z)*<i>) * (Re z +(Im z)*<i>) by SQUARE_1:def 3
       .= Re z *Re z + (<i>*<i>)*Im z *Im z+ Re z *Im z*<i> + Re z *Im z*<i>
       .= Re z * Re z + (-1)*Im z * Im z+ Re z * Im z*<i> + Re z * Im z*<i>
       .= Re z * Re z - Im z * Im z + (Re z * Im z + Re z * Im z)*<i>
       .= (Re z)^2 - Im z * Im z + (Re z * Im z + Re z * Im z)*<i>
            by SQUARE_1:def 3
       .= (Re z)^2-(Im z)^2 + 2*(Re z*Im z)*<i> by SQUARE_1:def 3;
    hence thesis;
  end;
  correctness by XCMPLX_0:def 2;
end;

definition let a,b,c,z;
  redefine func Poly2 (a,b,c,z) -> Element of COMPLEX;
  coherence by XCMPLX_0:def 2;
end;

theorem Th1:
    (a+c*<i>)*(b+d*<i>)=a*b-c*d+(a*d+b*c)*<i>;

theorem Th2:
  [*x,y*]^2= x^2-y^2 + 2*x*y*<i>
proof
XY: [*x,y*] = x+y*<i> by COMPLEX1:27;
 set z=[*x,y*];
 thus z^2 = (Re z)^2-(Im z)^2 + (2*(Re z*Im z))*<i> by Def3
     .=x^2-(Im (x+y*<i>))^2+2*(Re (x+y*<i>)*Im (x+y*<i>))*<i> by COMPLEX1:28,XY
     .=x^2-y^2+2*(Re (x+y*<i>)*Im (x+y*<i>))*<i> by COMPLEX1:28
     .=x^2-y^2+2*(x*Im (x+y*<i>))*<i> by COMPLEX1:28
     .=x^2-y^2+2*(x*y)*<i> by COMPLEX1:28
     .=x^2-y^2+2*x*y*<i>;
end;

canceled;

theorem Th4:
    a <> 0 & delta(a,b,c) >=0 & Poly2(a,b,c,z) = 0 implies
         z= (-b+sqrt delta(a,b,c))/(2*a) or
         z= (-b-sqrt delta(a,b,c))/(2*a) or
         z= -b/(2*a)
proof
A: a = a+0*<i>;
      assume
  A1:  a <> 0 & delta(a,b,c) >= 0;
         assume
  A2:  Poly2(a,b,c,z) = 0;
         set x=Re z;
         set y=Im z;
XY: [*x^2-y^2,2*x*y*] = x^2-y^2+(2*x*y)*<i> by COMPLEX1:27;
           a*z^2+b*z+c = 0 by A2,POLYEQ_1:def 2;
         then (a+0*<i>)*([*x^2-y^2,2*(x*y)*])+b*z+c = 0
                            by Def3,XY;
         then 0
         = Re a*Re (x^2-y^2+(2*x*y)*<i>) -Im a*Im (x^2-y^2+2*x*y*<i>)+
             (Re a*Im (x^2-y^2+2*x*y*<i>) +Re (x^2-y^2+2*x*y*<i>)*Im a)*<i>
           +b*z+c by COMPLEX1:def 10,XY
         .=[* Re a*Re (x^2-y^2+2*x*y*<i>)-Im a*
         Im (x^2-y^2+2*x*y*<i>),Re a*Im (x^2-y^2+2*x*y*<i>)
         +Re (x^2-y^2+2*x*y*<i>)*Im a  *]
           +b*z+c by COMPLEX1:27
         .=[*a*Re (x^2-y^2+2*x*y*<i>)-Im a*
         Im (x^2-y^2+2*x*y*<i>),Re a*Im (x^2-y^2+2*x*y*<i>)
         +Re (x^2-y^2+2*x*y*<i>)*Im a  *]
           +b*z+c by COMPLEX1:28,A
         .=[*a*(x^2-y^2)-Im a*
         Im (x^2-y^2+2*x*y*<i>),Re a*Im (x^2-y^2+2*x*y*<i>)
         +Re (x^2-y^2+2*x*y*<i>)*Im a  *]
           +b*z+c by COMPLEX1:28
         .=[*a*(x^2-y^2)-0*Im (x^2-y^2+2*x*y*<i>)
         ,Re a*Im (x^2-y^2+2*x*y*<i>)+Re (x^2-y^2+2*x*y*<i>)
         *Im a  *] +b*z+c by COMPLEX1:28,A
         .=[*a*(x^2-y^2)-0,Re a*(2*x*y)+Re (x^2-y^2+2*x*y*<i>)
         *Im a  *] +b*z+c by COMPLEX1:28
         .=[*a*(x^2-y^2)-0,a*(2*x*y)+Re (x^2-y^2+2*x*y*<i>)
         *Im a *] +b*z+c by COMPLEX1:28,A
         .=[*a*(x^2-y^2)-0,a*(2*x*y)+(x^2-y^2)
         *Im a *] +b*z+c by COMPLEX1:28
         .=[*a*(x^2-y^2)-0,a*(2*x*y)+(x^2-y^2)
         *0*] +b*z+c by COMPLEX1:28,A;
         then [*a*(x^2-y^2)-0*(2*x*y),0+a*(2*x*y)*]
           +(b+0*<i>)*(x+y*<i>)+c = 0 by COMPLEX1:29;
         then [*a*(x^2-y^2)-0,0+a*(2*x*y)*]
           +(b*x-0*y+(x*0+b*y)*<i>)+(c+0*<i>) = 0 by Th1;
         then (a*(x^2-y^2)+(a*(2*x*y))*<i>)
           +(b*x+(b*y)*<i>)+(c+0*<i>) = 0 by COMPLEX1:27;
         then (a*(x^2-y^2)+(a*(2*x*y))*<i>)+b*x+(b*y)*<i>+c = 0;
         then
A4:       a*(x^2-y^2)+b*x+c+(a*(2*x*y)+b*y)*<i> = 0;
         then
B4:       a*(x^2-y^2)+b*x+c = 0 & a*(2*x*y)+b*y = 0 by COMPLEX1:12,28;
A5:      a*(x^2-y^2)+b*x+c = 0 & (2*a*x+b)*y = 0 by COMPLEX1:12,28,A4;
         per cases by A5,XCMPLX_1:6;
          suppose
  A6:      y = 0;
  then Poly2(a,b,c,x)=0 by A4,POLYEQ_1:def 2,SQUARE_1:60;
             then Re z=(-b+sqrt delta(a,b,c))/(2*a) & Im z = 0 or
             Re z= (-b-sqrt delta(a,b,c))/(2*a) & Im z = 0
           by A1,A6,POLYEQ_1:5;
             then z=(-b+sqrt delta(a,b,c))/(2*a)+0*<i> or
             z= (-b-sqrt delta(a,b,c))/(2*a)+0*<i> by COMPLEX1:29;
         hence thesis;
         end;
         suppose 2*a*x+b = 0;
             then A7: (2*a)*x+0= 0-b;
               a"<>0 by A1,XCMPLX_1:203;
             then 2/a" <> 0 by XCMPLX_1:50;
             then 2/(1/a) <> 0 by XCMPLX_1:217;
             then (2*a)/1 <> 0 by XCMPLX_1:78;
             then A8: x= (-b)/(2*a) by A7,XCMPLX_1:90;
  then x = -b/(2*a) by XCMPLX_1:188;
             then a*((b/(2*a))^2-y^2)+b*(-b/(2*a))+c = 0
                  by B4,SQUARE_1:61;
             then a*((b/(2*a))^2-y^2)+((b*(-b/(2*a))+c)-(b*(-b/(2*a))+c))
                          = 0-(b*(-b/(2*a))+c);
             then (b/(2*a))^2-y^2 = (-(b*(-b/(2*a))+c))/a-0
   by A1,XCMPLX_1:90;
             then (b/(2*a))^2- (-(b*(-b/(2*a))+c))/a= y^2-0 by XCMPLX_1:24;
             then y^2 = (b/(2*a))^2- (-(-b*(b/(2*a))+c))/a;
             then y^2 = (b/(2*a))^2- (-(-(b*b)/(2*a)+c))/a by XCMPLX_1:75;
            then y^2 = (b/(2*a))^2- (-((-(b^2/(2*a)))+c))/a by SQUARE_1:def 3;
             then y^2 = (b/(2*a))^2- (-c+b^2/(2*a)+0)/a;
             then y^2 = (b/(2*a))^2- ((-c)/a+(b^2/(2*a))/a+0/a)
                                by XCMPLX_1:64;
             then y^2 = (b/(2*a))^2- ((-c)*a"+(b^2/(2*a))/a) by XCMPLX_0:def 9
;
             then y^2 = (b/(2*a))^2- (-c*a"+(b^2/(2*a))*a") by XCMPLX_0:def 9;
             then y^2 = (b/(2*a))^2+c*a"-((b^2/(2*a))*a");
  then A9:      y^2 *((2*a)^2)=(b^2/(2*a)^2+c*a"-((b^2/(2*a))*a"))*((2*a)^2)
                                by SQUARE_1:69
              .=(b^2/(2*a)^2)*(2*a)^2+(c*a")*(2*a)^2
                             -((b^2/(2*a))*a")*(2*a)^2
              .=(b^2*((2*a)^2)")*(2*a)^2+(c*a")*(2*a)^2
                          -((b^2/(2*a))*a")*(2*a)^2 by XCMPLX_0:def 9
              .=(b^2*((2*a)^2)")*(2*a)^2+(c*a")*(2*a)^2
                          -((b^2*(2*a)")*a")*(2*a)^2 by XCMPLX_0:def 9
              .=b^2/((2*a)^2)*((2*a)^2)+(c*a")*(2*a)^2
                          -(b^2*(2*a)"*a")*(2*a)^2 by XCMPLX_0:def 9;
              a"<>0 by A1,XCMPLX_1:203;
            then 2/a" <> 0 by XCMPLX_1:50;
            then 2/(1/a) <> 0 by XCMPLX_1:217;
  then A10:    (2*a)/1 <> 0 by XCMPLX_1:78;
  then A11:     (2*a)^2 > 0 by SQUARE_1:74;
  then A12:     y^2 *((2*a)^2)=b^2+(c*a")*(2*a)^2-(b^2*((2*a)"*a"))*(2*a)^2 by
A9,XCMPLX_1:88
              .=b^2+(c*a")*(2*a)^2-(b^2*((2*a)*a)")*(2*a)^2
                          by XCMPLX_1:205
              .=b^2+(c*a")*(2*a)^2-(b^2*(2*(a*a))")*(2*a)^2;
            set t = (b^2*(2*(a*a))")*(2*a)^2;
              t*((2*a)^2)" = b^2*(2*(a*a))"*(((2*a)^2)*((2*a)^2)");
            then t*((2*a)^2)" = b^2*(2*(a*a))"*(((2*a)^2)*(1/(2*a)^2))
                             by XCMPLX_1:217;
            then t*((2*a)^2)" = b^2*(2*(a*a))"*1 by A11,XCMPLX_1:107;
            then t*((2*a)^2)" = b^2*(2*a^2)" by SQUARE_1:def 3;
            then t*((2*a)^2)"*2" = b^2*((2*a^2)"*2");
            then t*((2*a)^2)"*2" = b^2*((2*(a^2*2))") by XCMPLX_1:205;
            then t*((2*a)^2)"*2" = b^2*((2*2*a^2)");
            then t*((2*a)^2)"*2" = b^2*((2^2*a^2)") by SQUARE_1:def 3;
            then t*((2*a)^2)"*2" = b^2*((2*a)^2)" by SQUARE_1:68;
            then (t*2")*((2*a)^2)"*(2*a)^2 = b^2*((2*a)^2)"*(2*a)^2
                               by XCMPLX_1:4;
            then (t*2")/(2*a)^2*(2*a)^2 = b^2*((2*a)^2)"*(2*a)^2
                            by XCMPLX_0:def 9;
            then (t*2")/(2*a)^2*(2*a)^2 = b^2/(2*a)^2*(2*a)^2
                       by XCMPLX_0:def 9;
            then t*2" = b^2/(2*a)^2*(2*a)^2 by A11,XCMPLX_1:88;
            then t*2" = b^2 by A11,XCMPLX_1:88;
            then t/2 *2 = b^2 *2 by XCMPLX_0:def 9;
  then A13:     y^2 *((2*a)^2)=b^2+(c*a")*(2*a)^2-2*b^2 by A12,XCMPLX_1:88;
            set t=(c*a")*(2*a)^2;
              t=(c*a")*((2*a)*(2*a)) by SQUARE_1:def 3;
            then t=c*a"*a*2*(2*a);
            then t=(c/a*a)*2*(2*a) by XCMPLX_0:def 9;
            then t=c*2*(2*a) by A1,XCMPLX_1:88;
 then A14:  (y *(2*a))^2=4*a*c-b^2 by A13,SQUARE_1:68;
 A15:      -delta(a,b,c) <= 0 by A1,REAL_1:66;
              -delta(a,b,c)=-(b^2-4*a*c) by QUIN_1:def 1
                                 .=4*a*c-b^2;
            then (y *(2*a))^2 = 0 by A14,A15,SQUARE_1:72;
            then y *(2*a) = 0 by SQUARE_1:73;
            then Re z=-b/(2*a) & Im z=0 by A8,A10,XCMPLX_1:6,188;
            then z = -b/(2*a)+0*<i> by COMPLEX1:29;
          hence thesis;
           end;
end;

theorem Th5:
    a <> 0 & delta(a,b,c) < 0 & Poly2(a,b,c,z) = 0 implies
        z= -b/(2*a)+(sqrt (-delta(a,b,c))/(2*a))*<i> or
        z= -b/(2*a)+(-sqrt (-delta(a,b,c))/(2*a))*<i>
proof
A: [*a,0*]= a+0*<i> by COMPLEX1:27;
      assume
  A1:  a <> 0 & delta(a,b,c) < 0;
           now let z;
         assume
  A2:  Poly2(a,b,c,z) = 0;
         set x=Re z;
         set y=Im z;
XY: [*x^2-y^2,2*x*y*] = x^2-y^2+(2*x*y)*<i> by COMPLEX1:27;
           a*z^2+b*z+c = 0 by A2,POLYEQ_1:def 2;
         then (a+0*<i>)*([*x^2-y^2,2*(x*y)*])+b*z+c = 0 by Def3,XY;
         then Re a*Re (x^2-y^2+2*x*y*<i>)-Im a*Im [*x^2-y^2,2*x*y
*]
         +(Re a*Im (x^2-y^2+2*x*y*<i>)+Re (x^2-y^2+2*x*y*<i>)
         *Im a)*<i>+b*z+c = 0 by COMPLEX1:def 10,XY;
         then [*Re a*Re (x^2-y^2+2*x*y*<i>)-Im a*Im [*x^2-y^2,2*x*y
*]
         ,Re a*Im (x^2-y^2+2*x*y*<i>)+Re (x^2-y^2+2*x*y*<i>)
         *Im a*]+b*z+c = 0 by COMPLEX1:27;
         then [*a*Re (x^2-y^2+2*x*y*<i>)-Im a*Im (x^2-y^2+2*x*y*<i>)
         ,Re a*Im (x^2-y^2+2*x*y*<i>)+Re (x^2-y^2+2*x*y*<i>)
         *Im a*]+b*z+c = 0 by COMPLEX1:28,A,XY;
         then [*a*(x^2-y^2)-Im a*Im (x^2-y^2+2*x*y*<i>)
         ,Re a*Im (x^2-y^2+2*x*y*<i>)+Re (x^2-y^2+2*x*y*<i>)
         *Im a*]+b*z+c = 0 by COMPLEX1:28;
         then [*a*(x^2-y^2)-0*Im (x^2-y^2+2*x*y*<i>)
         ,Re a*Im (x^2-y^2+2*x*y*<i>)+Re (x^2-y^2+2*x*y*<i>)
         *Im a*]+b*z+c = 0 by COMPLEX1:28,A;
         then [*a*(x^2-y^2)-0,Re a*(2*x*y)+Re (x^2-y^2+2*x*y*<i>)
         *Im a*]+b*z+c = 0 by COMPLEX1:28;
         then [*a*(x^2-y^2)-0,a*(2*x*y)+Re (x^2-y^2+2*x*y*<i>)
         *Im a*]+b*z+c = 0 by COMPLEX1:28,A;
         then [*a*(x^2-y^2)-0,a*(2*x*y)+(x^2-y^2)
         *Im a*]+b*z+c = 0 by COMPLEX1:28;
         then [*a*(x^2-y^2)-0,a*(2*x*y)+(x^2-y^2)
         *0*]+b*z+c = 0 by COMPLEX1:28,A;
         then [*a*(x^2-y^2)-0*(2*x*y),(x^2-y^2)*0+a*(2*x*y)*]
           +(b+0*<i>)*(x+y*<i>)+c = 0  by COMPLEX1:29;
         then [*a*(x^2-y^2)-0,0+a*(2*x*y)*]+(b*x-0*y+(x*0+b*y)*<i>)+c = 0
                                  by Th1;
         then a*(x^2-y^2)+(a*(2*x*y))*<i>+(b*x+b*y*<i>)+c = 0
                                  by COMPLEX1:27;
         then A3: a*(x^2-y^2)+b*x+c+(a*(2*x*y)+b*y)*<i> = 0;
  then A4:  a*(x^2-y^2+0)+b*x+c = 0 & a*(2*x*y)+b*y = 0 by COMPLEX1:12,28;
          -a*y^2+((b*x+a*x^2)+c )+a*y^2= 0+a*y^2 by A3,COMPLEX1:12,28;
         then ((b*x+a*x^2)+c )+a*y^2-a*y^2= a*y^2 by XCMPLX_1:155;
         then ((b*x+a*x^2)+c )+(a*y^2-a*y^2)= a*y^2 by XCMPLX_1:29;
         then ((b*x+a*x^2)+c)+0 = a*y^2;
          then a*x^2*a+b*x*a+c*a = a*y^2*a by XCMPLX_1:9;
          then x^2*(a*a)+x*b*a+c*a = y^2*a*a;
          then x^2*a^2+x*b*a+c*a = y^2*(a*a) by SQUARE_1:def 3;
          then x^2*a^2+x*b*a+c*a = y^2*a^2 by SQUARE_1:def 3;
          then ((x*a)^2+x*b*a)+0+c*a = y^2*a^2 by SQUARE_1:68;
          then ((x*a)^2+2*(x*a)*(b/2)+(b/2)^2)-(b/2)^2+c*a = (y*a)^2
                                     by SQUARE_1:68;
          then (x*a+b/2)^2-(b/2)^2+c*a = (y*a)^2 by SQUARE_1:63;
          then (x*a+b/2)^2-b^2/2^2+c*a*4/4 = (y*a)^2
  by SQUARE_1:69;
          then (x*a+b/2)^2+(-b^2/2^2)+c*a*4/4 = (y*a)^2;
          then (x*a+b/2)^2+(-b^2)/2^2+c*a*4/4 = (y*a)^2 by XCMPLX_1:188;
          then (x*a+b/2)^2+(-b^2)/(2*2)+c*a*4/4 = (y*a)^2 by SQUARE_1:def 3;
          then A5: (x*a+b/2)^2+((-b^2)/4+c*a*4/4+0/4) = (y*a)^2 by XCMPLX_1:1;
          set t1= (x*a+b/2)^2;
          set t2=(-b^2+c*a*4)/4;
 A6:    t1>=0 by SQUARE_1:72;
           t2=(-(b^2-4*a*c))/4;
 then A7:    t2=(-delta(a,b,c))/4 by QUIN_1:def 1;
            0-delta(a,b,c)>0 by A1,REAL_2:173;
          then t2>0 by A7,REAL_2:127;
          then 0+0<t1+t2 by A6,REAL_1:67;
          then y^2*a^2>0 by A5,SQUARE_1:68;
          then y^2 <> 0;
          then y*y <> 0 by SQUARE_1:def 3;
 then A8:    y<>0;
           (a*(2*x))*y+b*y = 0 by A3,COMPLEX1:12,28;
         then (a*2*x)*y+(b*y-b*y) = 0-b*y;
         then (a*2*x)*y = (-b)*y;
         then a*(2*x) = -b by A8,XCMPLX_1:5;
         then 2*x = (-b)/a by A1,XCMPLX_1:90;
         then x = (-b)/a/2 by XCMPLX_1:90;
         then x = 1/a*((-b)/2) by XCMPLX_1:105;
         then A9: x = (-b)/(2*a) by XCMPLX_1:104;
 then x = -b/(2*a) by XCMPLX_1:188;
         then a*((b/(2*a))^2-y^2)+b*(-b/(2*a))+c = 0 by A4,SQUARE_1:61;
         then a*((b/(2*a))^2-y^2)+((b*(-b/(2*a))+c)-(b*(-b/(2*a))+c))
                  = 0-(b*(-b/(2*a))+c);
         then (b/(2*a))^2-y^2 = (-(b*(-b/(2*a))+c))/a-0
 by A1,XCMPLX_1:90;
         then (b/(2*a))^2- (-(b*(-b/(2*a))+c))/a= y^2-0 by XCMPLX_1:24;
         then y^2 = (b/(2*a))^2- (-(-b*(b/(2*a))+c))/a;
         then y^2 = (b/(2*a))^2- (-(-(b*b)/(2*a)+c))/a by XCMPLX_1:75;
         then y^2 = (b/(2*a))^2- (-((-(b^2/(2*a)))+c))/a by SQUARE_1:def 3;
         then y^2 = (b/(2*a))^2- (-c+b^2/(2*a)+0)/a;
         then y^2 = (b/(2*a))^2- ((-c)/a+(b^2/(2*a))/a+0/a)
           by XCMPLX_1:64;
         then y^2 = (b/(2*a))^2- ((-c)*a"+(b^2/(2*a))/a) by XCMPLX_0:def 9;
         then y^2 = (b/(2*a))^2- (-c*a"+(b^2/(2*a))*a") by XCMPLX_0:def 9;
         then y^2 = (b/(2*a))^2+c*a"-((b^2/(2*a))*a");
  then A10:   y^2 *((2*a)^2)=(b^2/(2*a)^2+c*a"-((b^2/(2*a))*a"))*((2*a)^2)
                      by SQUARE_1:69
              .=(b^2/(2*a)^2)*(2*a)^2+(c*a")*(2*a)^2
                           -((b^2/(2*a))*a")*(2*a)^2
              .=(b^2*((2*a)^2)")*(2*a)^2+(c*a")*(2*a)^2
                        -((b^2/(2*a))*a")*(2*a)^2 by XCMPLX_0:def 9
              .=(b^2*((2*a)^2)")*(2*a)^2+(c*a")*(2*a)^2
                        -((b^2*(2*a)")*a")*(2*a)^2 by XCMPLX_0:def 9
              .=b^2/((2*a)^2)*((2*a)^2)+(c*a")*(2*a)^2
                        -(b^2*(2*a)"*a")*(2*a)^2 by XCMPLX_0:def 9;
            a"<>0 by A1,XCMPLX_1:203;
          then 2/a" <> 0 by XCMPLX_1:50;
          then 2/(1/a) <> 0 by XCMPLX_1:217;
  then A11:  (2*a)/1 <> 0 by XCMPLX_1:78;
  then A12:  (2*a)^2 > 0 by SQUARE_1:74;
  then A13:   y^2 *((2*a)^2)=b^2+(c*a")*(2*a)^2-(b^2*((2*a)"*a"))*(2*a)^2 by
A10,XCMPLX_1:88
            .=b^2+(c*a")*(2*a)^2-(b^2*((2*a)*a)")*(2*a)^2
                         by XCMPLX_1:205
            .=b^2+(c*a")*(2*a)^2-(b^2*(2*(a*a))")*(2*a)^2;
           set t = (b^2*(2*(a*a))")*(2*a)^2;
             t*((2*a)^2)" = b^2*(2*(a*a))"*(((2*a)^2)*((2*a)^2)");
           then t*((2*a)^2)" = b^2*(2*(a*a))"*(((2*a)^2)*(1/(2*a)^2))
                           by XCMPLX_1:217;
           then t*((2*a)^2)" = b^2*(2*(a*a))"*1 by A12,XCMPLX_1:107;
           then t*((2*a)^2)" = b^2*(2*a^2)" by SQUARE_1:def 3;
           then t*((2*a)^2)"*2" = b^2*((2*a^2)"*2");
           then t*((2*a)^2)"*2" = b^2*((2*(a^2*2))") by XCMPLX_1:205;
           then t*((2*a)^2)"*2" = b^2*((2*2*a^2)");
           then t*((2*a)^2)"*2" = b^2*((2^2*a^2)") by SQUARE_1:def 3;
           then t*((2*a)^2)"*2" = b^2*((2*a)^2)" by SQUARE_1:68;
           then (t*2")*((2*a)^2)"*(2*a)^2 = b^2*((2*a)^2)"*(2*a)^2
                           by XCMPLX_1:4;
           then (t*2")/(2*a)^2*(2*a)^2 = b^2*((2*a)^2)"*(2*a)^2
                           by XCMPLX_0:def 9;
           then (t*2")/(2*a)^2*(2*a)^2 = b^2/(2*a)^2*(2*a)^2
                       by XCMPLX_0:def 9;
            then t*2" = b^2/(2*a)^2*(2*a)^2 by A12,XCMPLX_1:88;
            then t*2" = b^2 by A12,XCMPLX_1:88;
            then t/2 *2 = b^2 *2 by XCMPLX_0:def 9;
  then A14:     y^2 *((2*a)^2)=b^2+(c*a")*(2*a)^2-2*b^2 by A13,XCMPLX_1:88;
            set t=(c*a")*(2*a)^2;
              t=(c*a")*((2*a)*(2*a)) by SQUARE_1:def 3;
            then t=c*a"*a*2*(2*a);
            then t=(c/a*a)*2*(2*a) by XCMPLX_0:def 9;
            then t=c*2*(2*a) by A1,XCMPLX_1:88;
  then A15:    (y *(2*a))^2=4*a*c-b^2 by A14,SQUARE_1:68;
  A16:     -delta(a,b,c) > 0 by A1,REAL_1:66;
               -delta(a,b,c)=-(b^2-4*a*c) by QUIN_1:def 1
                               .=4*a*c-b^2;
           then (y *(2*a))^2  =(sqrt (-delta(a,b,c)))^2
        by A15,A16,SQUARE_1:def 4;
           then (y *(2*a))^2  -(sqrt (-delta(a,b,c)))^2=0;
           then ((y *(2*a))+(sqrt (-delta(a,b,c))))*
           ((y *(2*a))-(sqrt (-delta(a,b,c))))=0 by SQUARE_1:67;
           then ((y *(2*a))+(sqrt (-delta(a,b,c))))=0 or
           ((y *(2*a))-(sqrt (-delta(a,b,c))))=0 by XCMPLX_1:6;
           then (y *(2*a))=0-sqrt (-delta(a,b,c)) or
           (y *(2*a))-sqrt (-delta(a,b,c))=0;
           then y =(-sqrt (-delta(a,b,c)))/(2*a) or
           y *(2*a)/(2*a)=sqrt (-delta(a,b,c))/(2*a)
 by A11,XCMPLX_1:15,90;
           then Re z=-b/(2*a) & Im z=sqrt (-delta(a,b,c))/(2*a)
           or  Re z= -b/(2*a) &  Im z= -sqrt (-delta(a,b,c))/(2*a)
 by A9,A11,XCMPLX_1:90,188;
     hence z= -b/(2*a)+(sqrt(-delta(a,b,c))/(2*a))*<i> or
          z= -b/(2*a)+(-sqrt (-delta(a,b,c))/(2*a))*<i> by COMPLEX1:29;
     end;
    hence thesis;
end;

theorem
      b <> 0 & (for z holds Poly2(0,b,c,z) = 0) implies z = -c/b
proof
Z: 0 = 0+0*<i>;
        assume
 A1:   b <> 0;
        assume
 A2:  (for z holds Poly2(0,b,c,z) = 0);
          now let z1;
          Poly2(0,b,c,z1) = 0 by A2;
        then 0*z1^2+b*z1+c = 0 by POLYEQ_1:def 2;
          then b*(Re z1+(Im z1)*<i>)+c = 0 by COMPLEX1:29;
          then (b*Re z1-0+c+(b*Im z1+0)*<i>)= 0+0*<i>;
           then b*Re z1-0+c = Re 0 & b*Im z1= Im 0
              by COMPLEX1:28;
          then b*(Re z1)-0+c = 0 & b*Im z1 = 0 by COMPLEX1:28,Z;
          then b*(Re z1)+(c-c) =0-c & Im z1 = 0 by A1,XCMPLX_1:6,29;
          then b*(Re z1)+(c-c) =-c  & Im z1 = 0 by XCMPLX_1:150;
          then b*(Re z1)+0 =-c  & Im z1 = 0 by XCMPLX_1:14;
          then Re z1 =(-c)/b & Im z1 = 0 by A1,XCMPLX_1:90;
          then Re z1 =-c/b & Im z1 = 0 by XCMPLX_1:188;
       then z1 = -c/b+0*<i> by COMPLEX1:29;
       hence z1 = -c/b;
    end;hence thesis;
end;

theorem
      for a,b,c being Real,z,z1,z2 being complex number st a <> 0 &
        for z being complex number holds Poly2(a,b,c,z) = Quard(a,z1,z2,z)
       holds b/a =-(z1+z2) & c/a = z1*z2
proof
        let a,b,c be Real,z,z1,z2 be complex number;
         assume
  A1:  a <> 0;
         assume
  A2:  for z being complex number holds
               Poly2(a,b,c,z) = Quard(a,z1,z2,z);
        consider z being Element of COMPLEX;
           Poly2(a,b,c,0c) = Quard(a,z1,z2,0c) by A2;
       then a*0c^2+b*0c+c = Quard(a,z1,z2,0c) by POLYEQ_1:def 2;
       then a*0c^2+b*0c+c = a*((0c-z1)*(0c-z2)) by POLYEQ_1:def 3
            .= a*((0+(-z1))*(0+(-z2))) by COMPLEX1:def 6
            .= a*(z1*z2);
  then A3:  a*(z1*z2)=a*(0c*0c)+b*0c+c by SQUARE_1:def 3
            .=c by COMPLEX1:def 6;
then A4:     z1*z2=c*a" by A1,XCMPLX_1:204
            .= c/a by XCMPLX_0:def 9;
            Quard(a,z1,z2,1) = Poly2(a,b,c,1) by A2
            .=a*1^2+b*1+c by POLYEQ_1:def 2
            .=a*(1*1)+b*1+c by SQUARE_1:def 3
            .=a+b+c;
          then a+b+c =a*((1-z1)*(1-z2)) by POLYEQ_1:def 3
            .=a+a*(-(z1+z2))+c by A3;
         then a+b=a+a*(-(z1+z2)) by XCMPLX_1:2;
         then b=a*(-(z1+z2)) by XCMPLX_1:2;
         then b*a"=-(z1+z2) by A1,XCMPLX_1:204;
  hence -(z1+z2) = b/a & c/a = z1*z2 by XCMPLX_0:def 9, A4;
end;

definition let z be complex number;
    func z^3 -> Element of COMPLEX equals
    :Def4: z^2*z;
 correctness by XCMPLX_0:def 2;
end;

definition let a,b,c,d be Real,z be complex number;
    func Poly_3(a,b,c,d,z) -> Element of COMPLEX equals
    :Def5:   a*z^3+b*z^2+c*z+d;
  coherence by XCMPLX_0:def 2;
end;

theorem Th8:
    0c^3 = 0
proof
    0c^3=0c^2*0c by Def4
     .=0*0 by COMPLEX1:def 6;
  hence 0c^3 = 0;
end;

theorem Th9:
  1^3 = 1
proof
    1^3=1^2*1 by Def4
  .=1*1 by SQUARE_1:def 3;
  hence 1^3 = 1;
end;

theorem Th10:
    (-1)^3=-1
proof
    (-1)^3=(-1)^2*(-1) by Def4
   .=((-1)*(-1))*(-1) by SQUARE_1:def 3
   .=1*(-1);
   hence (-1)^3 = -1;
end;

theorem Th11:
    Re z^3 = (Re z)|^3 - 3*Re z*(Im z)^2
    & Im z^3 = -(Im z)|^ 3+3*(Re z)^2*Im z
proof
X: [*Re z,Im z*] = Re z + (Im z)*<i> by COMPLEX1:27;
 A1:   z^3 = z^2*z by Def4
         .=z*z*z by SQUARE_1:def 3
         .=[*Re z,Im z*]*z*z by COMPLEX1:29,X
         .=[*Re z,Im z*]*[*Re z,Im z*]*z by COMPLEX1:29,X
         .=[*Re z,Im z*]*[*Re z,Im z*]*[*Re z,Im z*] by COMPLEX1:29,X;
         set x = Re z;
         set y = Im z;
XY: [*x^2-y^2,2*x*y*] = x^2-y^2+(2*x*y)*<i> by COMPLEX1:27;
X: [*x,y*] = x+y*<i> by COMPLEX1:27;
Y: [*x|^ 3-3*x*y^2 ,-y|^ 3+3*x^2*y*] = x|^ 3-3*x*y^2 +(-y|^ 3+3*x^2*y)*<i>
                     by COMPLEX1:27;
           z^3 = (x*x-y*y+(x*y+x*y)*<i>)*(x+y*<i>) by A1,X
         .= [*x*x-y*y,x*y+x*y*]*(x+y*<i>) by COMPLEX1:27
         .= [*x^2-y*y,x*y+x*y*]*(x+y*<i>) by SQUARE_1:def 3
         .= (x^2-y^2+(2*(x*y))*<i>)*(x+y*<i>) by SQUARE_1:def 3,XY
         .= Re(x^2-y^2+(2*(x*y))*<i>) *Re (x+y*<i>)-Im(x^2-y^2+(2*(x*y))*<i>)
            *Im (x+y*<i>)+(Re(x^2-y^2+(2*(x*y))*<i>) *Im (x+y*<i>)+
            Re (x+y*<i>)*Im (x^2-y^2+(2*(x*y))*<i>))*<i> by COMPLEX1:def 10
         .= [*Re(x^2-y^2+(2*(x*y))*<i>) *Re (x+y*<i>)-Im(x^2-y^2+(2*(x*y))*<i>)
            *Im (x+y*<i>),Re(x^2-y^2+(2*(x*y))*<i>) *Im (x+y*<i>)+
            Re (x+y*<i>)*Im (x^2-y^2+(2*(x*y))*<i>)*] by COMPLEX1:27
         .= [*(x^2-y^2) *Re (x+y*<i>)-Im(x^2-y^2+(2*(x*y))*<i>)
            *Im (x+y*<i>),Re(x^2-y^2+(2*(x*y))*<i>) *Im (x+y*<i>)+
            Re (x+y*<i>)*Im (x^2-y^2+(2*(x*y))*<i>)*] by COMPLEX1:28
         .= [*(x^2-y^2) *Re (x+y*<i>)-(2*(x*y))
            *Im (x+y*<i>),Re(x^2-y^2+(2*(x*y))*<i>) *Im (x+y*<i>)+
            Re (x+y*<i>)*Im (x^2-y^2+(2*(x*y))*<i>)*] by COMPLEX1:28
         .= [*(x^2-y^2) *Re (x+y*<i>)-(2*(x*y))
            *Im (x+y*<i>),(x^2-y^2) *Im (x+y*<i>)+
            Re (x+y*<i>)*Im (x^2-y^2+(2*(x*y))*<i>)*] by COMPLEX1:28
         .= [*(x^2-y^2) *Re (x+y*<i>)-(2*(x*y))*Im (x+y*<i>)
           ,(x^2-y^2) *Im (x+y*<i>)+Re (x+y*<i>)*(2*(x*y))*] by COMPLEX1:28
         .= [*(x^2-y^2) *x-(2*(x*y))*Im (x+y*<i>)
           ,(x^2-y^2) *Im (x+y*<i>)+Re (x+y*<i>)*(2*(x*y))*] by COMPLEX1:28
         .= [*(x^2-y^2) *x-(2*(x*y))*y
           ,(x^2-y^2) *Im (x+y*<i>)+Re (x+y*<i>)*(2*(x*y))*] by COMPLEX1:28
         .= [*(x^2-y^2) *x-(2*(x*y))*y
           ,(x^2-y^2) *y+Re (x+y*<i>)*(2*(x*y))*] by COMPLEX1:28
         .= [*(x^2*x-y^2*x+0*x)-2*(x*y)*y,(x^2-y^2+0)*y+x*(2*(x*y))*] by
COMPLEX1:28
         .= [*(x*x*x-y^2*x)-2*(x*y)*y,(x^2*y-y^2*y+0*y)+x*(2*(x*y))*]
                                by SQUARE_1:def 3
         .= [*(x|^1*x*x-y^2*x)-2*(x*y)*y,(x^2*y-y^2*y)+x*(2*(x*y))*]
                                by NEWTON:10
         .= [*(x|^ (1+1)*x-y^2*x)-2*(x*y)*y,(x^2*y-y^2*y)+x*(2*(x*y))*]
                                by NEWTON:11
         .= [*(x|^(2+1)-y^2*x)-2*(x*y)*y,(x^2*y-y^2*y)+x*(2*(x*y))*]
                                by NEWTON:11
         .= [*(x|^ 3-y^2*x)-2*(x*y)*y,(x^2*y-y*y*y)+x*(2*(x*y))*]
                                by SQUARE_1:def 3
         .= [*(x|^ 3-y^2*x)-2*(x*y)*y,(x^2*y-y|^ 1*y*y)+x*(2*(x*y))*]
                                by NEWTON:10
         .= [*(x|^ 3-y^2*x)-2*(x*y)*y,(x^2*y-y|^ (1+1)*y)+x*(2*(x*y))*]
                                by NEWTON:11
         .= [*(x|^ 3-y^2*x)-2*(x*(y*y)),(x^2*y-y|^ (2+1))+x*(2*(x*y))*] by
NEWTON:11
         .= [*x|^ 3-y^2*x-2*(x*y^2),(x^2*y-y|^ 3)+x*(2*(x*y))*]
                                by SQUARE_1:def 3
         .= [*x|^ 3-x*y^2-2*x*y^2,x^2*y-y|^ 3+2*(x*x*y)*]
         .= [*x|^ 3-1*(x*y^2)-2*(x*y^2),x^2*y-y|^ 3+2*(x^2*y)*] by
SQUARE_1:def 3
         .= [*x|^ 3-3*x*y^2 ,-y|^ 3+3*x^2*y*];
      hence thesis by COMPLEX1:28,Y;
end;

theorem
 (for z being complex number holds Poly_3(a,b,c,d,z) = Poly_3(a',b',c',d',z))
   implies a = a' & b = b' & c = c' & d = d'
proof
        assume
  A1:  for z being complex number holds
         Poly_3(a,b,c,d,z) = Poly_3(a',b',c',d',z);
         then Poly_3(a,b,c,d,0c) = Poly_3(a',b',c',d',0c);
         then a*0c^3+b*0^2+c*0+d = Poly_3(a',b',c',d',0c)
                by Def5,COMPLEX1:def 6;
         then a*0c^3+b*0^2+c*0+d
         =a'*0c^3+b'*0^2+c'*0+d' by Def5,COMPLEX1:def 6
         .=b'*(0*0)+d' by SQUARE_1:def 3,Th8
         .=d';
         then A2: d'
          = b*(0*0)+ d by SQUARE_1:def 3,Th8
         .=d;
           Poly_3(a,b,c,d,1) =  Poly_3(a',b',c',d',1) by A1;
         then Poly_3(a',b',c',d',1) =a*1^3+b*1^2+c*1+d by Def5;
         then a*1^3+b*1^2+c*1+d
         = a'*1^3+b'*1^2+c'*1+d' by Def5
         .=a'+b'*(1*1)+c'+d' by SQUARE_1:def 3,Th9
         .=a'+b'+c'+d';
         then
A4:       a'+b'+c'+d'
          = a+b*(1*1)+c+d by SQUARE_1:def 3,Th9
         .= a+b+c+d;
         set r1 = a+b+c;
         set r2 = a'+b'+c';
           r1-r2 = d - d by A2,A4,XCMPLX_1:33
              .=  0;
 then A5:   r1 = r2 by XCMPLX_1:15;
           Poly_3(a,b,c,d,-1) =  Poly_3(a',b',c',d',-1) by A1;
         then Poly_3(a',b',c',d',-1)
          =a*(-1)^3+b*(-1)^2+c*(-1)+d by Def5;
         then a*(-1)^3+b*(-1)^2+c*(-1)+d
         = a'*(-1)^3+b'*(-1)^2+c'*(-1)+d' by Def5
         .=a'*(-1)+b'*((-1)*(-1))+c'*(-1)+d'
                                        by Th10,SQUARE_1:def 3
         .=-a'+b'+(-c')+d';
         then
A6:       -a'+b'+(-c')+d'
         =a*(-1)+b*((-1)*(-1))+c*(-1)+d by Th10,SQUARE_1:def 3
         .=-a+b+(-c)+d;
         set s1 = -a+b-c;
         set s2 = -a'+b'-c';
 A7:   s1-s2 = d - d by A2,A6,XCMPLX_1:33
              .= 0;
         then r1+s1 =b'+0+b' by A5
          .=2*b';
          then A8: 2*b' = ( b+a+c)+(-(c+a)+b)
          .=2*b;
            r1-s1 = a'+b'+c'-(b'-a'-c') by A5,A7,XCMPLX_1:15
          .= a'+c'+(a'+c');
         then 2*(a'+c') = a+b+c-(b-a-c)
            .= 2*(a+c);
           then a'+c' = a+c by XCMPLX_1:5;
 then A9:     a-a' = c'-c by XCMPLX_1:33;
       Poly_3(a,b,c,d,2+0*<i>) =  Poly_3(a',b',c',d',2+0*<i>) by A1;
         then Poly_3(a',b',c',d',2+0*<i>)
          =a*(2+0*<i>)^3+b*(2+0*<i>)^2+c*2+d by Def5;
         then a*(2+0*<i>)^3+b*(2+0*<i>)^2+c*(2+0*<i>)+d
          =a'*(2+0*<i>)^3+b'*(2+0*<i>)^2+c'*2+d' by Def5
         .=a'*(2+0*<i>)^3+b'*(2*2) +c'*2+d' by SQUARE_1:def 3
         .=a'*(2^2*2) +b'*4+c'*2+d' by Def4
         .=a'*(2*2*2) +b'*4+c'*2+d' by SQUARE_1:def 3
         .=a'*8+b'*4+c'*2+d';
        then
A10:     a'*8+b'*4+c'*2+d' =a*(2+0*<i>)^3+b*(2*2)
                    +c*2+d by SQUARE_1:def 3
         .=a*(2^2*2)+b*4+c*2+d by Def4
         .=a*(2*2*2) +b*4+c*2+d by SQUARE_1:def 3
         .=a*8+b*4+c*2+d;
         set t1=a*8+b*4+c*2;
         set t2=a'*8+b'*4+c'*2;
  A11:  t1 - t2 =d - d by A2,A10,XCMPLX_1:33
                .=0;
    (a - a')*8 = (c' -c)*2
         proof
           a*8+ (b*4 +c*2) = a'*8 + (b'*4 +c'*2) by A11,XCMPLX_1:15;
          then a*8 - a'*8 = b'*4 +c'*2 -( b*4 +c*2) by XCMPLX_1:33
                  .= b'*4 - b*4+c'*2 -c*2
                  .= (b*4 - b*4)+c'*2 -c*2 by A8,XCMPLX_1:5
                  .=  0 +c'*2 -c*2;hence thesis by XCMPLX_1:40;
         end;
          then (c' - c)*8 - (c' -c)*2 = 0 by A9;
         then (c' - c)*6 = 0;
          then c' - c = 0 or 6 = 0 by XCMPLX_1:6;
   hence thesis by A2,A8,A9,XCMPLX_1:5,15;
end;

theorem  Th13:
    (z+h)^3 = z^3+(3*h)*z^2+(3*h^2)*z+h^3
proof
  A1:  (z+h)^3 = (z+h)^2*(z+h) by Def4
          .=((z+h)*(z+h))*(z+h) by SQUARE_1:def 3
          .=(((z*z)*z+(z*z)*h)+(h*z)*(z+h))+(z*h+h*h)*(z+h)
          .=((z^2*z+(z*z)*h)+(h*z)*(z+h))+(z*h+h*h)*(z+h) by SQUARE_1:def 3
          .=((z^2*z+z^2*h)+(h*z)*(z+h))+(z*h+h*h)*(z+h) by SQUARE_1:def 3
          .=((z^3+z^2*h)+(h*(z*z)+z*(h*h)))+(z*h+h*h)*(z+h) by Def4
          .=((z^3+z^2*h)+(h*(z^2)+z*(h*h)))+(z*h+h*h)*(z+h) by SQUARE_1:def 3
          .=((z^3+z^2*h)+(h*(z^2)+z*(h^2)))+(z*h+h*h)*(z+h) by SQUARE_1:def 3
          .=(z^3+z^2*h+h*(z^2)+z*(h^2))+(z*h+h*h)*(z+h);
          set t = (z*h+h*h)*(z+h);
            t = ((z*h)*z+(z*h)*h)+((h*h)*z+(h*h)*h)
           .= (h*z*z+z*h*h)+((h^2)*z+(h*h)*h) by SQUARE_1:def 3
           .= (h*(z*z)+z*h*h)+((h^2)*z+h^2*h) by SQUARE_1:def 3
           .= (h*(z^2)+z*(h*h))+((h^2)*z+h^2*h) by SQUARE_1:def 3
           .= (h*(z^2)+z*(h^2))+((h^2)*z+h^2*h) by SQUARE_1:def 3
           .= (h*(z^2)+z*(h^2))+((h^2)*z+h^3) by Def4
           .= (h*(z^2)+z*(h^2))+(h^2)*z+h^3;
      hence thesis by A1;
end;

theorem
      (z*h)^3 = z^3*h^3
proof
     (z*h)^3 = (z*h)^2*(z*h) by Def4
           .= (z*h)*(z*h)*(z*h) by SQUARE_1:def 3
           .= z*(h*h)*z*(z*h)
           .= z*h^2*z*(z*h) by SQUARE_1:def 3
           .= (z*z*h^2)*h*z
           .= (z^2*h^2)*h*z by SQUARE_1:def 3
           .= z^2*(h^2*h)*z
           .= z^2*h^3*z by Def4
           .= z^2*z*h^3;
   hence thesis by Def4;
end;

theorem
      b<>0 & Poly_3(0,b,c,d,z)=0 & delta(b,c,d)>=0 implies
       z = (-c+sqrt delta(b,c,d))/(2*b) or
       z = (-c-sqrt delta(b,c,d))/(2*b) or
       z = -c/(2*b)
proof
         assume
 A1:   b<>0 & Poly_3(0,b,c,d,z) = 0 & delta(b,c,d)>=0;
         then 0 =0*(z^3)+b*z^2+c*z+d by Def5;
         then Poly2(b,c,d,z)=0 by POLYEQ_1:def 2;
         hence thesis by A1,Th4;
end;

theorem
      b<>0 & Poly_3(0,b,c,d,z)=0 & delta(b,c,d)<0 implies
      z = -c/(2*b)+(sqrt (-delta(b,c,d))/(2*b))*<i> or
      z = -c/(2*b)+(-sqrt (-delta(b,c,d))/(2*b))*<i>
proof
         assume
 A1:   b<>0 & Poly_3(0,b,c,d,z) = 0 & delta(b,c,d)<0;
         then 0 =0*(z^3)+b*z^2+c*z+d by Def5;
         then Poly2(b,c,d,z)=0 by POLYEQ_1:def 2;
         hence thesis by A1,Th5;
end;

theorem
      a<>0 & Poly_3(a,0,c,0,z)=0 & 4*a*c <= 0 implies
       z = (sqrt -4*a*c)/(2*a) or
       z = (-sqrt -4*a*c)/(2*a) or
       z = 0
proof
         assume
 A1:   a<>0 & Poly_3(a,0,c,0,z) = 0 & 4*a*c <= 0;
         then 0 =a*(z^3)+0*z^2+c*z+0 by Def5;
         then a*(z^2*z)+c*z = 0 by Def4;
         then (a*z^2+c)*z = 0;
         then (a*z^2+0*z) + c = 0 or z = 0 by XCMPLX_1:6;
 then A2:   Poly2(a,0,c,z) = 0 or z = 0 by POLYEQ_1:def 2;
           -4*a*c >= -0 by A1,REAL_1:50;
         then 0^2-4*a*c >= 0 by SQUARE_1:60,XCMPLX_1:150;
         then delta(a,0,c) >= 0 by QUIN_1:def 1;
            then z = (-0+sqrt delta(a,0,c))/(2*a)
            or z = (-0-sqrt delta(a,0,c))/(2*a)
            or z= 0
            or z = -0/(2*a) by A1,A2,Th4;
            then z = (sqrt delta(a,0,c))/(2*a)
            or z = (-sqrt delta(a,0,c))/(2*a)
            or z = 0;
            then z = (sqrt (0-4*a*c))/(2*a)
            or z = (-sqrt (0-4*a*c))/(2*a)
            or z= 0 by QUIN_1:def 1,SQUARE_1:60;
    hence thesis;
end;

theorem
      a<>0 & Poly_3(a,b,c,0,z)=0 & delta(a,b,c)>=0 implies
       z = (-b+sqrt delta(a,b,c))/(2*a) or
       z = (-b-sqrt delta(a,b,c))/(2*a) or
       z = -b/(2*a) or
       z = 0
proof
         assume
 A1:   a<>0 & Poly_3(a,b,c,0,z) = 0 & delta(a,b,c)>=0;
         then 0 =a*(z^3)+b*z^2+c*z+0 by Def5;
          then a*(z^2*z)+b*z^2+c*z = 0 by Def4;
         then a*(z^2*z)+b*(z*z)+c*z = 0 by SQUARE_1:def 3;
         then (a*z^2+b*z+c)*z = 0;
         then a*z^2+b*z+c = 0 or z = 0
           by XCMPLX_1:6;
         then Poly2(a,b,c,z) = 0 or z = 0 by POLYEQ_1:def 2;
     hence thesis by A1,Th4;
end;

theorem
      a<>0 & Poly_3(a,b,c,0,z)=0 & delta(a,b,c)<0 implies
      z= -b/(2*a)+(sqrt (-delta(a,b,c))/(2*a))*<i> or
      z= -b/(2*a)+(-sqrt (-delta(a,b,c))/(2*a))*<i> or
      z = 0
proof
         assume
 A1:   a<>0 & Poly_3(a,b,c,0,z) = 0 & delta(a,b,c)<0;
         then 0 =a*(z^3)+b*z^2+c*z+0 by Def5;
         then a*(z^2*z)+b*z^2+c*z = 0 by Def4;
         then a*z^2*z+b*(z*z)+c*z = 0 by SQUARE_1:def 3;
         then (a*z^2+b*z+c)*z = 0;
         then a*z^2+b*z+c = 0 or z = 0 by XCMPLX_1:6;
         then Poly2(a,b,c,z) = 0 or z = 0 by POLYEQ_1:def 2;
     hence thesis by A1,Th5;
end;

theorem Th20:
    y^2 = a implies y = sqrt a or y = -sqrt a
proof
         assume
 A1:   y^2 = a;
         then 1*y^2 +0*y+(-a) = a-a;
 then A2:   Poly2(1,0,-a,y) =0 by POLYEQ_1:def 2;
a: a >= 0 by A1,SQUARE_1:72;
           delta(1,0,-a) = 0^2 - 4*1*(-a) by QUIN_1:def 1;
         then delta(1,0,-a) = -4*(-a) by SQUARE_1:60,XCMPLX_1:150;
 then A3:   delta(1,0,-a) = 4*a;
         then delta(1,0,-a) >= 0 by REAL_2:121,a;
         then y=(-0+sqrt delta(1,0,-a))/(2*1) or
         y=(-0-sqrt delta(1,0,-a))/(2*1) by A2,POLYEQ_1:5;
         then y=(sqrt (4*a))/2 or
         y=(-sqrt (4*a))/2 by A3;
         then y= sqrt a * 2 /2 or
         y=(-(2 * sqrt a))/2 by SQUARE_1:85,97,a;
    hence y= sqrt a or y=-sqrt a;
end;

theorem
      a<>0 & Poly_3(a,0,c,d,z)=0 & Im z = 0
       implies
       (for u,v st Re z = u+v & 3*v*u+c/a=0 holds
          z = 3-root(-d/(2*a)+sqrt(d^2/(4*a^2)+(c/(3*a)) |^ 3))
             +3-root(-d/(2*a)-sqrt(d^2/(4*a^2)+(c/(3*a)) |^ 3))
       or z = 3-root(-d/(2*a)+sqrt(d^2/(4*a^2)+(c/(3*a)) |^ 3))
             +3-root(-d/(2*a)+sqrt(d^2/(4*a^2)+(c/(3*a)) |^ 3))
       or z = 3-root(-d/(2*a)-sqrt(d^2/(4*a^2)+(c/(3*a)) |^ 3))
             +3-root(-d/(2*a)-sqrt(d^2/(4*a^2)+(c/(3*a)) |^ 3)) )
proof
       assume
 A1:   a <> 0;
       assume
 A2:   Poly_3(a,0,c,d,z) = 0 & Im z = 0;
        let u,v;
       assume
 A3:   Re z=u+v & 3*v*u+c/a=0;
         set x=Re z;
         set y=Im z;
         set p=c/a;
X3: [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]
       = x|^ 3 - 3*x*y^2+(-y|^ 3+3*x^2*y)*<i> by COMPLEX1:27;
A: a = a+0*<i>;
Z: Re z+(Im z)*<i> = [*Re z,Im z*] by COMPLEX1:27;
 A4:   z  = Re z+(Im z)*<i> by COMPLEX1:29
           .= x by A2;
           0 =a*(z^3)+0*z^2+c*z+d by A2,Def5;
         then 0 = a*(Re z^3+(Im z^3)*<i>)+0*z^2+c*z+d
                        by COMPLEX1:29
         .= a*([*Re z^3,Im z^3*])+0*z^2+c*z+d
                        by COMPLEX1:27
         .= a*[*(Re z)|^ 3 - 3*Re z*(Im z)^2,Im z^3*]
               +0*z^2+c*z+d by Th11
         .= a*[*(Re z)|^ 3 - 3*Re z*(Im z)^2,-(Im z)|^ 3+3*(Re z)^2
            *Im z*]+c*z+d by Th11
         .= a*([*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*])
               +c*(Re z+(Im z)*<i>)+d by COMPLEX1:29
         .= (a+0*<i>)*([*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*])
               +c*[*Re z,Im z*]+d by COMPLEX1:27
         .=Re a *Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]-Im a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]+(Re a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]+
          Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]*Im a)*<i>
               +c*[*Re z,Im z*]+d by COMPLEX1:def 10
         .=[*Re a *Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]-Im a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*],Re a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]+
          Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]*Im a *]
               +c*[*Re z,Im z*]+d by COMPLEX1:27
         .=[*Re a *(x|^ 3 - 3*x*y^2)-Im a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*],Re a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]+
          Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]*Im a *]
               +c*[*Re z,Im z*]+d by COMPLEX1:28,X3
         .=[*Re a *(x|^ 3 - 3*x*y^2)-Im a
           *(-y|^ 3+3*x^2*y),Re a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]+
          Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]*Im a *]
               +c*[*Re z,Im z*]+d by COMPLEX1:28,X3
         .=[*Re a *(x|^ 3 - 3*x*y^2)-Im a
           *(-y|^ 3+3*x^2*y),Re a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]+
          (x|^ 3 - 3*x*y^2)*Im a *]
               +c*[*Re z,Im z*]+d by COMPLEX1:28,X3
         .=[*Re a *(x|^ 3 - 3*x*y^2)-Im a
           *(-y|^ 3+3*x^2*y),Re a*(-y|^ 3+3*x^2*y)+
          (x|^ 3 - 3*x*y^2)*Im a *]
               +c*[*Re z,Im z*]+d by COMPLEX1:28,X3
         .=[*a*(x|^ 3 - 3*x*y^2)-Im a
           *(-y|^ 3+3*x^2*y),Re a*(-y|^ 3+3*x^2*y)+
          (x|^ 3 - 3*x*y^2)*Im a *]
               +c*[*Re z,Im z*]+d by COMPLEX1:28,A
         .=[*a*(x|^ 3 - 3*x*y^2)-0*(-y|^ 3+3*x^2*y),
          Re a*(-y|^ 3+3*x^2*y)+(x|^ 3 - 3*x*y^2)*Im a *]
               +c*[*Re z,Im z*]+d by COMPLEX1:28,A
         .=[*a*(x|^ 3 - 3*x*y^2)-0,a*(-y|^ 3+3*x^2*y)
           +(x|^ 3 - 3*x*y^2)*Im a *]
               +c*[*Re z,Im z*]+d by COMPLEX1:28,A
         .=[*a*(x|^ 3 - 3*x*y^2)-0,a*(-y|^ 3+3*x^2*y)
           +(x|^ 3 - 3*x*y^2)*0 *]+c*[*Re z,Im z*]+d
                              by COMPLEX1:28,A
         .= a*(x|^ 3 - 3*x*0)+(a*(-0|^ 3+3*x^2*0))*<i> +c*x+d
                      by A2,SQUARE_1:60,COMPLEX1:27,Z
         .= a*(x|^ 3 - 3*x*0)+c*x+d+(a*(-0|^ 3+0) +0+0)*<i>
         .= a*(x|^ 3 - 0)+c*x+d+(a*(-0))*<i> by NEWTON:16
         .= [*a*x|^ 3 +c*x+d,0*] by COMPLEX1:27;
            then a"*(a*x|^ 3+c*x+d) = a"*0 by ARYTM_0:def 7;
            then a"*(x|^ 3*a)+a"*(c*x)+a"*d = 0 by XCMPLX_1:9;
            then x|^ 3*(a*a")+a"*c*x+a"*d = 0;
            then x|^ 3*(a/a)+a"*c*x+a"*d = 0 by XCMPLX_0:def 9;
            then x|^ 3+c*a"*x+d*a" = 0
  by A1,XCMPLX_1:89;
            then x|^ 3+(c/a)*x+d*a" = 0 by XCMPLX_0:def 9;
            then 1*x|^ 3+0*x^2+(c/a)*x+d/a = 0 by XCMPLX_0:def 9;
 then A5:      Poly3(1,0,c/a,d/a,x) = 0 by POLYEQ_1:def 4;
 A6:   -(d/a)/2 = -1/2*(d/a)
                 .= -d/(a*2) by XCMPLX_1:104;
 A7:   (d/a)^2/4=d^2/a^2/4 by SQUARE_1:69
                 .=1/4*(d^2/a^2)
                 .=d^2/(a^2*4) by XCMPLX_1:104;
           (c/a)/3 =1/3*(c/a)
                .=c/(a*3) by XCMPLX_1:104;
      hence thesis by A3,A4,A5,A6,A7,POLYEQ_1:19;
end;

theorem
      a<>0 & Poly_3(a,0,c,d,z)=0 & Im z <> 0 implies
    (for u,v st  Re z = u+v & 3*v*u+c/(4*a)=0 & c/a>=0 holds
          z=3-root(d/(16*a)+sqrt((d/(16*a))^2+(c/(12*a)) |^ 3))
             +3-root(d/(16*a)-sqrt((d/(16*a))^2+(c/(12*a)) |^ 3))+
             sqrt(3*(3-root(d/(16*a)+sqrt((d/(16*a))^2+(c/(12*a)) |^ 3))
             + 3-root(d/(16*a)-sqrt((d/(16*a))^2+(c/(12*a)) |^ 3)))^2+c/a)*<i>
       or z=3-root(d/(16*a)+sqrt((d/(16*a))^2+(c/(12*a)) |^ 3))
             +3-root(d/(16*a)-sqrt((d/(16*a))^2+(c/(12*a)) |^ 3))
             -sqrt(3*(3-root(d/(16*a)+sqrt((d/(16*a))^2+(c/(12*a)) |^ 3))
             +3-root(d/(16*a)-sqrt((d/(16*a))^2+(c/(12*a)) |^ 3)))^2+c/a)*<i>
       or z=2*(3-root(d/(16*a)+sqrt((d/(16*a))^2+(c/(12*a)) |^ 3)))+
             sqrt(3*(2*(3-root(d/(16*a)+sqrt((d/(16*a))^2
             +(c/(12*a)) |^ 3))))^2+c/a)*<i>
       or z=2*(3-root(d/(16*a)+sqrt((d/(16*a))^2+(c/(12*a)) |^ 3)))
             -sqrt(3*(2*(3-root(d/(16*a)+sqrt((d/(16*a))^2
             +(c/(12*a)) |^ 3))))^2+c/a)*<i>
       or z=2*(3-root(d/(16*a)-sqrt((d/(16*a))^2+(c/(12*a)) |^ 3)))+
             sqrt(3*(2*(3-root(d/(16*a)-sqrt((d/(16*a))^2
             +(c/(12*a)) |^ 3))))^2+c/a)*<i>
       or z=2*(3-root(d/(16*a)-sqrt((d/(16*a))^2+(c/(12*a)) |^ 3)))
             -sqrt(3*(2*(3-root(d/(16*a)-sqrt((d/(16*a))^2
             +(c/(12*a)) |^ 3))))^2+c/a)*<i>
          )
proof
        assume
 A1:   a <> 0;
        assume
 A2:   Poly_3(a,0,c,d,z) = 0  & Im z <> 0;
        let u,v;
        assume
 A3:   Re z=u+v & 3*v*u+c/(4*a)=0 &  c/a>=0;
         set x=Re z;
         set y=Im z;
         set p=c/(4*a);
X3: [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]
       = x|^ 3 - 3*x*y^2+(-y|^ 3+3*x^2*y)*<i> by COMPLEX1:27;
A: a = a+0*<i>;
C: c = c+0*<i>;
Z: Re z+(Im z)*<i> = [*Re z,Im z*] by COMPLEX1:27;
 A4:   z = x+y*<i> by COMPLEX1:29;
:::        .= [*x,y*] by COMPLEX1:27;
        0 =a*(z^3)+0*z^2+c*z+d by A2,Def5;
 then A5:   0 = a*(Re z^3+(Im z^3)*<i>)+0*z^2+c*z+d
                        by COMPLEX1:29
         .= a*([*Re z^3,Im z^3*])+0*z^2+c*z+d
                        by COMPLEX1:27
         .= a*[*(Re z)|^ 3 - 3*Re z*(Im z)^2,Im z^3*]
            +c*z+d by Th11
         .= a*[*(Re z)|^ 3 - 3*Re z*(Im z)^2,-(Im z)|^ 3+3*(Re z)^2
            *Im z*]+c*z+d by Th11
         .= a*([*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*])
               +c*(Re z+(Im z)*<i>)+d by COMPLEX1:29
         .= (a+0*<i>)*([*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*])
               +c*[*Re z,Im z*]+d by COMPLEX1:27
         .=Re a *Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]-Im a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]+(Re a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]+
          Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]*Im a)*<i>
               +c*[*Re z,Im z*]+d by COMPLEX1:def 10
         .=[*Re a *Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]-Im a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*],Re a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]+
          Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]*Im a *]
               +c*[*Re z,Im z*]+d by COMPLEX1:27
         .=[*Re a *(x|^ 3 - 3*x*y^2)-Im a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*],Re a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]+
          Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]*Im a *]
               +c*[*Re z,Im z*]+d by COMPLEX1:28,X3
         .=[*Re a *(x|^ 3 - 3*x*y^2)-Im a
           *(-y|^ 3+3*x^2*y),Re a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]+
          Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]*Im a *]
               +c*[*Re z,Im z*]+d by COMPLEX1:28,X3
         .=[*Re a *(x|^ 3 - 3*x*y^2)-Im a
           *(-y|^ 3+3*x^2*y),Re a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]+
          (x|^ 3 - 3*x*y^2)*Im a *]
               +c*[*Re z,Im z*]+d by COMPLEX1:28,X3
         .=[*Re a *(x|^ 3 - 3*x*y^2)-Im a
           *(-y|^ 3+3*x^2*y),Re a*(-y|^ 3+3*x^2*y)+
          (x|^ 3 - 3*x*y^2)*Im a *]
               +c*[*Re z,Im z*]+d by COMPLEX1:28,X3
         .=[*a*(x|^ 3 - 3*x*y^2)-Im a
           *(-y|^ 3+3*x^2*y),Re a*(-y|^ 3+3*x^2*y)+
          (x|^ 3 - 3*x*y^2)*Im a *]
               +c*[*Re z,Im z*]+d by COMPLEX1:28,A
         .=[*a*(x|^ 3 - 3*x*y^2)-0*(-y|^ 3+3*x^2*y),
          Re a*(-y|^ 3+3*x^2*y)+(x|^ 3 - 3*x*y^2)*Im a *]
               +c*[*Re z,Im z*]+d by COMPLEX1:28,A
         .=[*a*(x|^ 3 - 3*x*y^2)-0,a*(-y|^ 3+3*x^2*y)
           +(x|^ 3 - 3*x*y^2)*Im a *]
               +c*[*Re z,Im z*]+d by COMPLEX1:28,A
         .=[*a*(x|^ 3 - 3*x*y^2)-0,a*(-y|^ 3+3*x^2*y)
           +(x|^ 3 - 3*x*y^2)*0 *]+c*[*Re z,Im z*]+d
                              by COMPLEX1:28,A
         .= [*a*(x|^ 3 - 3*x*y^2),a*(-y|^ 3+3*x^2*y)*]
            +(c*Re z-0*Im z+(c*Im z+Re z*0)*<i>)+d by Th1,C,Z
         .= a*(x|^ 3 - 3*x*y^2)+(a*(-y|^ 3+3*x^2*y))*<i>
            +(c*Re z-0*Im z+(c*Im z+Re z*0)*<i>)+d by COMPLEX1:27
         .= a*(x|^ 3 - 3*x*y^2)+c*x+d+(a*(-y|^ 3+3*x^2*y)
            +c*y+0)*<i>;
then A6:        a*(-y|^ 3+3*x^2*y) +c*y+0 = 0 by COMPLEX1:12,28;
            a*(-y|^(2+1)+3*x^2*y)+c*y = 0 by A5,COMPLEX1:12,28;
          then a*(-y|^2*y+3*x^2*y)+c*y = 0 by NEWTON:11;
          then a*((-y|^2)*y+(3*x^2)*y+0*y)+c*y = 0;
          then a*((-y|^2+3*x^2+0)*y)+c*y = 0 by XCMPLX_1:9;
          then (a*(-y|^2+3*x^2+0))*y+c*y+0*y = 0;
          then (a*(-y|^2+3*x^2)+c+0)*y = 0 by XCMPLX_1:9;
            then (a*(y|^2))+(-a*(y|^2)+(a*(3*x^2)+c))=(a*(y|^2))+0
                      by A2,XCMPLX_1:6;
            then a*(y|^2)+-a*(y|^2)+(a*(3*x^2)+c) = a*(y|^2) by XCMPLX_1:1;
            then y|^(1+1) = (a*(3*x^2)+c)/a by A1,XCMPLX_1:90;
            then y|^1*y = (a*(3*x^2)+c)/a by NEWTON:11;
            then y*y = (a*(3*x^2)+c)/a by NEWTON:10;
            then y^2 = ((3*x^2)*a+c+0)/a by SQUARE_1:def 3;
            then
B7:          y^2 = (3*x^2)*a/a+c/a+0/a by XCMPLX_1:64;
 then A7:      y^2 = 3*x^2+c/a+0*a" by A1,XCMPLX_1:90;
            a*(x|^ 3 - 3*x*(3*x^2+c/a)+0)+c*x+d = 0
                             by A5,A6,B7,A1,XCMPLX_1:90;
            then a*x|^ 3 - a*(3*x*(3*x^2+c/a+0))+a*0+c*x+d = 0 by XCMPLX_1:44;
            then 0 =a*x|^ 3 - a*((3*x)*(3*x^2)+(3*x)*(c/a)+(3*x)*0)+c*x+d
                                 by XCMPLX_1:9
            .=a*x|^ 3 - a*(9*(x*x^2)+(3*x)*(c/a))+c*x+d
            .=a*x|^ 3 - a*(9*(x*(x*x))+(3*x)*(c/a))+c*x+d by SQUARE_1:def 3
            .=a*x|^ 3 - a*(9*(x|^1*x*x)+(3*x)*(c/a))+c*x+d by NEWTON:10
            .=a*x|^ 3 - a*(9*(x|^(1+1)*x)+(3*x)*(c/a))+c*x+d by NEWTON:11
            .=a*x|^ 3 - a*(9*(x|^(2+1))+(3*x)*(c/a)+0)+c*x+d by NEWTON:11
            .=a*x|^ 3 - (a*(9*(x|^3))+a*((3*x)*(c/a))+a*0)+c*x+d by XCMPLX_1:9
            .=a*x|^ 3 - (a*(9*(x|^3))+(3*x)*(a*(c/a)))+c*x+d
            .=a*x|^ 3 - (a*(9*(x|^3))+(3*x)*(a*(c*a")))+c*x+d by XCMPLX_0:def 9
            .=a*x|^ 3 - (a*(9*(x|^3))+(3*x)*(c*(a"*a)))+c*x+d
            .=a*x|^ 3 - (a*(9*(x|^3))+(3*x)*(c*(a/a)))+c*x+d by XCMPLX_0:def 9
            .=a*x|^ 3 - (a*(9*(x|^3))+(3*x)*c)+c*x+d
  by A1,XCMPLX_1:89
            .=(-8*a)*x|^3+(-2*c)*x+d;
             then (-1)*0=(8*a)*x|^3+(2*c)*x+(-d);
             then (8*a)"*0=(8*a)"*((8*a)*x|^3)+(8*a)"*((2*c)*x)+(8*a)"*(-d)
                                     by XCMPLX_1:9;
 then 0=x|^3*((8*a)*(8*a)")+(8*a)"*((2*c)*x)+(8*a)"*(-d);
 then A8:       0=x|^3*((8*a)/(8*a))+(8*a)"*((2*c)*x)+(8*a)"*(-d)
            by XCMPLX_0:def 9;
               8*a <> 0 by A1,XCMPLX_1:6;
             then 0=1*x|^3+0*x^2+((8*a)"*(2*c))*x+((8*a)"*(-d)) by A8,
XCMPLX_1:89;
         then 0=1*x|^3+0*x^2+((2*c)/(8*a))*x+((8*a)"*(-d)) by XCMPLX_0:def 9;
         then 0=1*x|^3+0*x^2+((2*c)/(8*a))*x+((-d)/(8*a)) by XCMPLX_0:def 9;
         then 0=1*x|^3+0*x^2+((2/8*c)/a)*x+((-d)/(8*a)) by XCMPLX_1:84;
         then 0=1*x|^3+0*x^2+(1/a*(c/4))*x+((-d)/(8*a))
 by XCMPLX_1:106;
         then 0=1*x|^3+0*x^2+(c/(4*a))*x+((-d)/(8*a))
 by XCMPLX_1:104;
         then 0=1*x|^3+0*x^2+(c/(4*a))*x+(-d/(8*a)) by XCMPLX_1:188;
 then A9:      Poly3(1,0,c/(4*a),-d/(8*a),x) = 0 by POLYEQ_1:def 4;
             set pp = c/(4*a);
             set q = -d/(8*a);
              -q/2=1/2*(d/(8*a));
             then A10: -q/2=d/(a*8*2) by XCMPLX_1:104;
              q^2/4=
            (d/(8*a))^2/(1^2+2*1*1+1^2) by SQUARE_1:59,61;
              then q^2/4=(d/(8*a))^2/(1+1)^2 by SQUARE_1:63;
              then q^2/4=(d/(8*a)/2)^2 by SQUARE_1:69;
              then q^2/4=(1/2*(d/(8*a)))^2;
              then A11: q^2/4=(d/(a*8*2))^2 by XCMPLX_1:104;
 A12:      x = 3-root(-q/2+sqrt(q^2/4+(pp/3) |^ 3)) +
                3-root(-q/2-sqrt(q^2/4+(pp/3) |^ 3)) or
            x = 3-root(-q/2+sqrt(q^2/4+(pp/3) |^ 3)) +
                3-root(-q/2+sqrt(q^2/4+(pp/3) |^ 3)) or
            x = 3-root(-q/2-sqrt(q^2/4+(pp/3) |^ 3)) +
                3-root(-q/2-sqrt(q^2/4+(pp/3) |^ 3))
         by A3,A9,POLYEQ_1:19;
            set m = 3-root(-q/2+sqrt(q^2/4+(pp/3) |^ 3));
            set n = 3-root(-q/2-sqrt(q^2/4+(pp/3) |^ 3));
                 A14: z=m+n+sqrt(3*(m + n )^2+c/a)*<i>
             or  z=m+n-sqrt(3*(m + n )^2+c/a)*<i>
             or  z=2*m+sqrt(3*(2*m)^2+c/a)*<i>
             or  z=2*m-sqrt(3*(2*m)^2+c/a)*<i>
             or  z=2*n+sqrt(3*(2*n )^2+c/a)*<i>
             or  z=2*n-sqrt(3*(2*n )^2+c/a)*<i>
          proof
           now per cases by A12;
             case x = m + n;
              hence z=m+n+sqrt(3*(m + n )^2+c/a)*<i>
               or  z=m+n+(-sqrt(3*(m + n )^2+c/a))*<i> by A4,A7,Th20;
            end;
            case x = 2*m;
             hence z=2*m+sqrt(3*(2*m)^2+c/a)*<i>
             or  z=2*m+(-sqrt(3*(2*m)^2+c/a))*<i> by A4,A7,Th20;
            end;
            case x = 2*n;
             hence z=2*n+sqrt(3*(2*n )^2+c/a)*<i>
             or  z=2*n+(-sqrt(3*(2*n )^2+c/a))*<i> by A4,A7,Th20;
            end;
             end;
             hence z=m+n+sqrt(3*(m + n )^2+c/a)*<i>
             or  z=m+n-sqrt(3*(m + n )^2+c/a)*<i>
             or  z=2*m+sqrt(3*(2*m)^2+c/a)*<i>
             or  z=2*m-sqrt(3*(2*m)^2+c/a)*<i>
             or  z=2*n+sqrt(3*(2*n )^2+c/a)*<i>
             or  z=2*n-sqrt(3*(2*n )^2+c/a)*<i>;
           end;
         (c/(4*a))/3=1/3*(c/(4*a))
          .=c/(a*4*3) by XCMPLX_1:104
          .=c/(a*(4*3));
    hence thesis by A10,A11,A14;
 end;

theorem
      a<>0 & Poly_3(a,b,c,d,z)=0 & Im z=0 implies
    (for u,v,x1 st x1=Re z + b/(3*a) &
       Re z=u+v-b/(3*a) & 3*u*v+(3*a*c-b^2)/(3*a^2)=0 holds
              z =3-root(-(b/(3*a)) |^ 3-(3*a*d-b*c)/(6*a^2)
                 +sqrt((2*((b/(3*a)) |^ 3)+(3*a*d-b*c)/(3*a^2))^2/4
                 +((3*a*c-b^2)/(9*a^2)) |^ 3)) +
                3-root(-(b/(3*a)) |^ 3-(3*a*d-b*c)/(6*a^2)
                 -sqrt((2*((b/(3*a)) |^ 3)+(3*a*d-b*c)/(3*a^2))^2/4
                 +((3*a*c-b^2)/(9*a^2)) |^ 3))-b/(3*a)+0*<i> or
            z =3-root(-(b/(3*a)) |^ 3-(3*a*d-b*c)/(6*a^2)
                 +sqrt((2*((b/(3*a)) |^ 3)+(3*a*d-b*c)/(3*a^2))^2/4
                 +((3*a*c-b^2)/(9*a^2)) |^ 3)) +
                3-root(-(b/(3*a)) |^ 3-(3*a*d-b*c)/(6*a^2)
                 +sqrt((2*((b/(3*a)) |^ 3)+(3*a*d-b*c)/(3*a^2))^2/4
                 +((3*a*c-b^2)/(9*a^2)) |^ 3))-b/(3*a)+0*<i> or
            z =3-root(-(b/(3*a)) |^ 3-(3*a*d-b*c)/(6*a^2)
                 -sqrt((2*((b/(3*a)) |^ 3)+(3*a*d-b*c)/(3*a^2))^2/4
                 +((3*a*c-b^2)/(9*a^2)) |^ 3)) +
                3-root(-(b/(3*a)) |^ 3-(3*a*d-b*c)/(6*a^2)
                 -sqrt((2*((b/(3*a)) |^ 3)+(3*a*d-b*c)/(3*a^2))^2/4
                 +((3*a*c-b^2)/(9*a^2)) |^ 3))-b/(3*a)+0*<i> )
proof
         assume
 A1:   a<>0;
         assume
 A2:   Poly_3(a,b,c,d,z)=0 & Im z=0;
         let u,v,x1;
         assume
 A3:   x1=Re z +b/(3*a) & Re z=u+v-b/(3*a) & 3*u*v+(3*a*c-b^2)/(3*a^2)=0;
         set x=Re z;
         set y=Im z;
Z: [*Re z,Im z*] = x+y*<i> by COMPLEX1:27;
X3: [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]
       = x|^ 3 - 3*x*y^2+(-y|^ 3+3*x^2*y)*<i> by COMPLEX1:27;
A: a = a+0*<i>;
A4:   z = x+y*<i> by COMPLEX1:29
        .= [*x,y*] by COMPLEX1:27;
           0 =a*(z^3)+b*z^2+c*z+d by A2,Def5;
 then
A5: 0 = a*(Re z^3+(Im z^3)*<i>)+b*z^2+c*z+d
                        by COMPLEX1:29
         .= a*([*Re z^3,Im z^3*])+b*z^2+c*z+d
                        by COMPLEX1:27
         .= a*[*(Re z)|^ 3 - 3*Re z*(Im z)^2,Im z^3*]
               +b*z^2+c*z+d by Th11
         .= a*[*(Re z)|^ 3 - 3*Re z*(Im z)^2,-(Im z)|^ 3+3*(Re z)^2
            *Im z*]+b*z^2+c*z+d by Th11
         .= a*([*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*])
               +b*z^2+c*(Re z+(Im z)*<i>)+d by COMPLEX1:29
         .= (a+0*<i>)*([*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*])
               +b*z^2+c*[*Re z,Im z*]+d by COMPLEX1:27
         .= Re a*Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]
            -Im a*Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]+
           (Re a*Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]
           +Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]*Im a)*<i>
               +b*z^2+c*[*Re z,Im z*]+d by COMPLEX1:def 10
         .= [*Re a*Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]
            -Im a*Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*],
           Re a*Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]
           +Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]*Im a *]
               +b*z^2+c*[*Re z,Im z*]+d by COMPLEX1:27
         .= [*Re a*Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]
            -0*Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*],
           Re a*Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]
           +Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]*Im a *]
               +b*z^2+c*[*Re z,Im z*]+d by COMPLEX1:28,A
         .= [*Re a*Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]
            -0,Re a*Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]
           +Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]*0 *]
               +b*z^2+c*[*Re z,Im z*]+d by COMPLEX1:28,A
         .= [*Re a*Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]
            -0,Re a*(-y|^ 3+3*x^2*y)+0 *]
               +b*z^2+c*[*Re z,Im z*]+d by COMPLEX1:28,X3
         .= [*Re a*(x|^ 3 - 3*x*y^2)-0,Re a*(-y|^ 3+3*x^2*y)*]
               +b*z^2+c*[*Re z,Im z*]+d by COMPLEX1:28,X3
         .= [*a*(x|^ 3 - 3*x*y^2)-0,Re a*(-y|^ 3+3*x^2*y)*]
               +b*z^2+c*[*Re z,Im z*]+d by COMPLEX1:28,A
         .= [*a*(x|^ 3 - 3*x*y^2),a*(-y|^ 3+3*x^2*y)*]+b*z^2
            +(c*Re z-0*Im z+(c*Im z+Re z*0)*<i>)+d by Z,COMPLEX1:28,A
         .= [*a*(x|^ 3 - 3*x*y^2),a*(-y|^ 3+3*x^2*y)*]+b*z^2
            +[*c*Re z-0*Im z,c*Im z+Re z*0*]+d by COMPLEX1:27
         .= [*a*(x|^ 3 - 3*x*y^2),a*(-y|^ 3+3*x^2*y)*]
           +b*(x^2-y^2+2*x*y*<i>)+[*c*x,c*y*]+d by A4,Th2
         .= a*(x|^ 3 - 3*x*y^2)+(a*(-y|^ 3+3*x^2*y))*<i>
           +b*(x^2-y^2+2*x*y*<i>)+[*c*x,c*y*]+d by COMPLEX1:27
         .= a*(x|^ 3 - 3*x*y^2)+(a*(-y|^ 3+3*x^2*y))*<i>
           +b*(x^2-y^2+(2*x*y)*<i>)+(c*x+(c*y)*<i>)+d by COMPLEX1:27;
         0 = a*(x|^ 3 - 3*x*0)+(a*(-0|^ 3))*<i>
           +b*(x^2-0)+c*x+d by SQUARE_1:60,A2,A5
         .= a*(x|^ 3 - 3*x*0)+b*(x^2-0)+c*x+d
              +(a*(-0|^ 3+0)+b*0+0)*<i>
         .= [*a*(x|^ 3 - 3*x*0)+b*(x^2-0)+c*x+d
                 ,a*(-0|^ 3+0)+b*0+0*] by COMPLEX1:27
         .= [*a*x|^ 3+b*x^2+c*x+d,a*(-0)+0*] by NEWTON:16
         .= [*a*x|^ 3+b*x^2+c*x+d,a*0+0*];
         then a*x|^ 3+b*x^2+c*x+d = 0 by ARYTM_0:def 7;
 then A6:      Poly3(a,b,c,d,x) = 0 by POLYEQ_1:def 4;
            set a'=b/a;
            set b'=c/a;
            set c'=d/a;
            set h=-b/(3*a);
              a'=b/a & b'=c/a & c'=d/a & h=-b/(3*a);
 then A7:       x1 |^ 3 + 0*x1^2 + ((3*a*c-b^2)/(3*a^2))*x1
                  + (2*((b/(3*a)) |^ 3)+(3*a*d-b*c)/(3*a^2)) = 0
            by A1,A3,A6,POLYEQ_1:16;
             set  p = (3*a*c-b^2)/(3*a^2);
             set  q = 2*((b/(3*a)) |^ 3)+(3*a*d-b*c)/(3*a^2);
               p/3=1/3*((3*a*c-b^2)/(3*a^2));
             then A8: p/3=(3*a*c-b^2)/(a^2*3*3) by XCMPLX_1:104;
 A9:       -q/2=-((b/(3*a)) |^ 3+1/2*((3*a*d-b*c)/(3*a^2)))
            .=-((b/(3*a)) |^ 3+(3*a*d-b*c)/(a^2*3*2)) by XCMPLX_1:104
            .=0-((b/(3*a)) |^ 3+(3*a*d-b*c)/(6*a^2)) by XCMPLX_1:150
            .=-(b/(3*a)) |^ 3-(3*a*d-b*c)/(6*a^2);
        Poly3(1,0,p,q,x1) = 0 by A7,POLYEQ_1:17;
  then      x1 = 3-root(-q/2+sqrt(q^2/4+(p/3) |^ 3)) +
                 3-root(-q/2-sqrt(q^2/4+(p/3) |^ 3)) or
            x1 = 3-root(-q/2+sqrt(q^2/4+(p/3) |^ 3)) +
                 3-root(-q/2+sqrt(q^2/4+(p/3) |^ 3)) or
            x1 = 3-root(-q/2-sqrt(q^2/4+(p/3) |^ 3)) +
                 3-root(-q/2-sqrt(q^2/4+(p/3) |^ 3))
                    by A3,POLYEQ_1:19;
         hence thesis by A2,A3,A8,A9,COMPLEX1:29;
end;

theorem Th24:
  z1 <> 0 & Poly1(z1,z2,z) = 0 implies z = -(z2/z1)
  proof
   assume A1: z1 <> 0 & Poly1(z1,z2,z) = 0;
    then z1*z+z2 = 0 by POLYEQ_1:def 1;
    then z1*z+z2-z2 = -z2 by XCMPLX_1:150;
    then z1*z = -z2 by XCMPLX_1:26;
    then z = (-z2)*z1" by A1,XCMPLX_1:204;
    then z = -(z2*z1");
  hence z = -(z2/z1) by XCMPLX_0:def 9;
end;

theorem
     z2 <> 0 implies not ex z st Poly1(0,z2,z) = 0
proof
   assume A1: z2 <> 0;
   given s such that
   A2:Poly1(0,z2,s) = 0;
     0*s + z2 = 0 by A2,POLYEQ_1:def 1;
   hence contradiction by A1;
end;

begin

definition let z1,z2,z3,z;
  func CPoly2(z1,z2,z3,z) -> Element of COMPLEX equals
    :Def6: z1*z^2+z2*z+z3;
  coherence;
end;

theorem
      (for z holds CPoly2(z1,z2,z3,z) = CPoly2(s1,s2,s3,z)) implies
    z1 = s1 & z2 = s2 & z3 = s3
proof
       assume
 A1: for z holds CPoly2(z1,z2,z3,z) = CPoly2(s1,s2,s3,z);
       then CPoly2(z1,z2,z3,0c) = CPoly2(s1,s2,s3,0c);
    then z1*0^2+z2*0+z3 = CPoly2(s1,s2,s3,0c) by Def6,COMPLEX1:def 6;
    then z1*(0*0)+z2*0+z3 = CPoly2(s1,s2,s3,0c) by SQUARE_1:def 3;
then A2: z3 = s1*0^2+s2*0+s3 by Def6,COMPLEX1:def 6
    .=s1*(0*0)+s2*0+s3 by SQUARE_1:def 3
    .=s3;
      CPoly2(z1,z2,z3,1r) = CPoly2(s1,s2,s3,1r) by A1;
    then z1*1r^2+z2*1r+z3 = CPoly2(s1,s2,s3,1r) by Def6;
    then z1*(1r*1r)+z2*1r+z3 = CPoly2(s1,s2,s3,1r) by SQUARE_1:def 3;
    then z1+z2+z3 = s1*1r^2+s2*1r+s3 by Def6, COMPLEX1:def 7;
    then z1+z2+z3 = s1*(1r*1r)+s2*1r+s3 by SQUARE_1:def 3;
    then
A3:  z1+z2 = (s1+s2)+s3-s3 by XCMPLX_1:26,A2,COMPLEX1:def 7;
       CPoly2(z1,z2,z3,-1r) = CPoly2(s1,s2,s3,-1r) by A1;
     then z1*(-1r)^2+z2*(-1r)+z3 = CPoly2(s1,s2,s3,-1r) by Def6;
     then z1*((-1r)*(-1r))+z2*(-1r)+z3 = CPoly2(s1,s2,s3,-1r)
                  by SQUARE_1:def 3;
     then z1+z2*(-1)+z3 = CPoly2(s1,s2,s3,-1r) by COMPLEX1:def 7;
     then z1-z2+z3 = s1*(-1r)^2+s2*(-1r)+s3 by Def6;
     then z1-z2+z3 = s1*((-1r)*(-1r))+s2*(-1r)+s3 by SQUARE_1:def 3;
     then z1-z2+z3 = s1+s2*(-1)+s3 by COMPLEX1:def 7;
     then (z1-z2)+z3-z3 = (s1-s2)+s3-s3 by A2;
     then z1-z2+(z2+z1) = s1-s2+(s2+s1) by A3,XCMPLX_1:26;
     then z1-(z2-(z2+z1)) = s1-(s2-(s2+s1)) by XCMPLX_1:37;
     then z1--z1 = s1--s1;
     then z1+z1 = s1+s1 by XCMPLX_1:151;
     then A6: 2*z1 = 2*s1 by XCMPLX_1:11;
 then z1 = s1 by XCMPLX_1:5;
     then z2+z1-z1 = s2+(z1-z1) by A3;
     then z2+(z1-z1) = s2+0 by XCMPLX_1:29;
 hence thesis by A2,A6,XCMPLX_1:5;
end;

theorem
      (-a+sqrt (a^2+b^2))/2 >=0 & (a+sqrt (a^2+b^2))/2>=0
proof
 A1: a^2>=0 & b^2>=0 & a^2>=a^2 by SQUARE_1:72;
 then A2: a^2+b^2>=0+0 & a^2+b^2>=0+a^2 by REAL_1:55;
 then A3: sqrt(a^2+b^2)>=sqrt(a^2) by A1,SQUARE_1:94;
          A4:  sqrt (a^2+b^2)>=0 by A2,SQUARE_1:def 4;
          per cases;
          suppose a>=0;
          then A5:  sqrt(a^2+b^2)>=a & a+sqrt (a^2+b^2)>=0+0
         by A3,A4,REAL_1:55,SQUARE_1:89;
                then sqrt(a^2+b^2)-a>=a-a by REAL_1:49;
               hence thesis by A5,REAL_2:125;
          end;
          suppose
          A6: a<0;
                then -a > 0 by REAL_1:66;
          then A7:  sqrt(a^2+b^2) >= -a & -a+sqrt (a^2+b^2)>=0+0
  by A3,A4,A6,REAL_1:67,SQUARE_1:90;
                then sqrt(a^2+b^2)-(-a) >= -a-(-a) by REAL_1:49;
               hence thesis by A7,REAL_2:125;
              end;
end;

theorem Th28:
   z^2 = s & Im s >= 0 implies
   z=sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
       (sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i> or
   z=-sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
       (-sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i>
proof
      assume
 A1: z^2=s;
       assume
 A2: Im s >=0;
       set a=Re s;
       set b=Im s;
       set u=Re z;
       set v=Im z;
B: [*u,v*] = u+v*<i> & [*a,b*] = a+b*<i> by COMPLEX1:27;
   then
   z=[*u,v*] & s=[*a,b*] & b>=0 by A2,COMPLEX1:29;
       then u^2-v^2+2*u*v*<i>=[*a,b*] by A1,Th2;
 then A3: u^2-v^2=a & 2*u*v=b by COMPLEX1:163,B;
       then (u^2-v^2)^2+(2*(u*v))^2=a^2+b^2 by XCMPLX_1:4;
       then (u^2)^2-2*u^2*v^2+(v^2)^2+(2*(u*v))^2=a^2+b^2 by SQUARE_1:64;
    then ((u^2)^2+(v^2)^2)-2*u^2*v^2+2^2*(u*v)^2=a^2+b^2 by SQUARE_1:68;
    then ((u^2)^2+(v^2)^2)-2*u^2*v^2+2^2*(u^2*v^2)=a^2+b^2 by SQUARE_1:68;
    then ((u^2)^2+(v^2)^2)-2*u^2*v^2+(2*2)*(u^2*v^2)=a^2+b^2
                              by SQUARE_1:def 3;
    then 4*(u^2*v^2)-2*u^2*v^2+((u^2)^2+(v^2)^2)=a^2+b^2 by XCMPLX_1:30;
    then (u^2)^2+2*u^2*v^2+(v^2)^2=a^2+b^2 by XCMPLX_1:1;
 then A4:   (u^2+v^2)^2=a^2+b^2 by SQUARE_1:63;
         u^2>=0 & v^2>=0 by SQUARE_1:72;
       then u^2+v^2>=0+0 by REAL_1:55;
 then A5: u^2+v^2=sqrt (a^2+b^2) by A4,SQUARE_1:89;
       then u^2-v^2+v^2+u^2=a+sqrt (a^2+b^2) by A3,XCMPLX_1:1;
       then 2*u^2=a+sqrt (a^2+b^2) by XCMPLX_1:11;
 then A6: u^2=(a+sqrt (a^2+b^2))/2 by XCMPLX_1:90;
         v^2+u^2-u^2+v^2=sqrt (a^2+b^2)-a by A3,A5,XCMPLX_1:37;
       then v^2*2=sqrt (a^2+b^2)-a by XCMPLX_1:11;
       then A7: v^2=(-(-sqrt (a^2+b^2)+a))/2 by XCMPLX_1:90;
                 -2*(u*v)<=-0 by A2,A3,REAL_1:50;
                then (-2)*(u*v)<=0;
                then u*v>=0 by REAL_2:129;
                then u<=0 & v<=0 or u>=0 & v>=0 by SQUARE_1:25;
           then -u=sqrt ((a+sqrt (a^2+b^2))/2) & -v=sqrt ((-a+sqrt (a^2+b^2))/
2)  or
           u=sqrt ((a+sqrt (a^2+b^2))/2) & v=sqrt ((-a+sqrt (a^2+b^2))/2)
                    by A6,A7,SQUARE_1:89,90;
                then 0-u=0+sqrt ((a+sqrt (a^2+b^2))/2)
                & 0-v=0+sqrt ((-a+sqrt (a^2+b^2))/2) or
           u=sqrt ((a+sqrt (a^2+b^2))/2) & v=sqrt ((-a+sqrt (a^2+b^2))/2)
                             by XCMPLX_1:150;
                then 0+u=0-sqrt ((a+sqrt (a^2+b^2))/2)
                & 0+v=0-sqrt ((-a+sqrt (a^2+b^2))/2) or
           u=sqrt ((a+sqrt (a^2+b^2))/2) & v=sqrt ((-a+sqrt (a^2+b^2))/2)
                             by XCMPLX_1:35;
           hence thesis by COMPLEX1:29;
end;

theorem
     z^2 = s & Im s = 0 & Re s > 0 implies z=sqrt(Re s) or z=-sqrt(Re s)
proof
      assume
 A1: z^2=s;
       assume
 A2: Im s =0 & Re s >0;
    then z=sqrt (( Re s+sqrt ((Re s)^2+0))/2)+
       (sqrt ((-Re s+sqrt ((Re s)^2+0))/2))*<i>   or
    z=-sqrt (( Re s+sqrt ((Re s)^2+0))/2)+
       (-sqrt ((-Re s+sqrt ((Re s)^2+0))/2))*<i>
             by A1,Th28,SQUARE_1:60;
    then z=[*sqrt (( Re s+sqrt ((Re s)^2+0))/2),
       sqrt ((-Re s+sqrt ((Re s)^2+0))/2)*]   or
    z=[*-sqrt (( Re s+sqrt ((Re s)^2+0))/2),
       -sqrt ((-Re s+sqrt ((Re s)^2+0))/2)*]
             by COMPLEX1:27;
    then z=[*sqrt (( Re s+Re s)/2),
       sqrt ((-Re s+sqrt ((Re s)^2+0))/2)*]   or
    z=[*-sqrt (( Re s+sqrt ((Re s)^2+0))/2),
       -sqrt ((-Re s+sqrt ((Re s)^2+0))/2)*] by A2,SQUARE_1:89;
    then z=[*sqrt (( Re s+Re s)/2), sqrt ((-Re s+Re s)/2)*]   or
    z=[*-sqrt (( Re s+Re s)/2),
       -sqrt ((-Re s+sqrt ((Re s)^2+0))/2)*] by A2,SQUARE_1:89;
    then z=[*sqrt (Re s*2/2), sqrt ((-Re s+Re s)/2)*] or
    z=[*-sqrt (Re s*2/2),-sqrt ((-Re s+Re s)/2)*] by A2,SQUARE_1:89;
    then z=[*sqrt (Re s), sqrt ((0+(Re s-Re s))/2)*] or
    z=[*-sqrt (Re s),-sqrt ((0+(Re s-Re s))/2)*];
    hence thesis by ARYTM_0:def 7,SQUARE_1:82;
end;

theorem
     z^2 = s & Im s = 0 & Re s < 0 implies
    z= sqrt (-Re s)*<i> or z= -sqrt (-Re s)*<i>
proof
      assume
 A1: z^2=s;
       assume
 A2: Im s =0 & Re s < 0;
    then z=sqrt (( Re s+sqrt ((Re s)^2+0))/2)+
       (sqrt ((-Re s+sqrt ((Re s)^2+0))/2))*<i>   or
    z=-sqrt (( Re s+sqrt ((Re s)^2+0))/2)+
       (-sqrt ((-Re s+sqrt ((Re s)^2+0))/2))*<i>
 by A1,Th28,SQUARE_1:60;
    then z=sqrt (( Re s+-Re s)/2)+
       sqrt ((-Re s+sqrt ((Re s)^2+0))/2)*<i>   or
    z=-sqrt (( Re s+sqrt ((Re s)^2+0))/2)+
       (-sqrt ((-Re s+sqrt ((Re s)^2+0))/2))*<i> by A2,SQUARE_1:90;
    then z=sqrt (( Re s+-Re s)/2)+ sqrt ((-Re s+-Re s)/2)*<i>   or
    z=-sqrt (( Re s+-Re s)/2)+(-sqrt ((-Re s+sqrt ((Re s)^2+0))/2))*<i>
                       by A2,SQUARE_1:90;
    then z=sqrt (( Re s+-Re s)/2)+ sqrt ((-Re s+-Re s)/2)*<i>   or
    z=-sqrt (( Re s+-Re s)/2)+(-sqrt ((-Re s+-Re s)/2))*<i>
                           by A2,SQUARE_1:90;
    then z=sqrt (0/2)+sqrt (-Re s)*<i> or z=-sqrt (0/2)+(-sqrt (-Re s))*<i>;
   hence thesis by SQUARE_1:82;
end;

theorem
     z^2 = s & Im s <0 implies
   z= sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
       (-sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i> or
   z=-sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
        (sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i>
proof
       assume
 A1: z^2=s;
       assume
 A2: Im s<0;
       set a=Re s;
       set b=Im s;
       set u=Re z;
       set v=Im z;
B: [*u,v*] = u+v*<i> & [*a,b*] = a+b*<i> by COMPLEX1:27;
   then
  z=[*u,v*] & s=[*a,b*] & b <0 by A2,COMPLEX1:29;
       then u^2-v^2+2*u*v*<i>=[*a,b*] by A1,Th2;
 then A4: u^2-v^2=a & 2*u*v=b by B,COMPLEX1:163;
       then (u^2-v^2)^2+(2*(u*v))^2=a^2+b^2 by XCMPLX_1:4;
       then (u^2)^2-2*u^2*v^2+(v^2)^2+(2*(u*v))^2=a^2+b^2 by SQUARE_1:64;
    then ((u^2)^2+(v^2)^2)-2*u^2*v^2+2^2*(u*v)^2=a^2+b^2 by SQUARE_1:68;
    then ((u^2)^2+(v^2)^2)-2*u^2*v^2+2^2*(u^2*v^2)=a^2+b^2 by SQUARE_1:68;
    then ((u^2)^2+(v^2)^2)-2*u^2*v^2+(2*2)*(u^2*v^2)=a^2+b^2
                              by SQUARE_1:def 3;
    then 4*(u^2*v^2)-2*u^2*v^2+((u^2)^2+(v^2)^2)=a^2+b^2 by XCMPLX_1:30;
    then (u^2)^2+2*u^2*v^2+(v^2)^2=a^2+b^2 by XCMPLX_1:1;
 then A5: sqrt (u^2+v^2)^2=sqrt (a^2+b^2) by SQUARE_1:63;
         u^2>=0 & v^2>=0 by SQUARE_1:72;
       then u^2+v^2>=0+0 by REAL_1:55;
 then A6: u^2+v^2=sqrt (a^2+b^2) by A5,SQUARE_1:89;
       then u^2-v^2+v^2+u^2=a+sqrt (a^2+b^2) by A4,XCMPLX_1:1;
       then 2*u^2=a+sqrt (a^2+b^2) by XCMPLX_1:11;
 then A7: u^2=(a+sqrt (a^2+b^2))/2 by XCMPLX_1:90;
         v^2+u^2-u^2+v^2=sqrt (a^2+b^2)-a by A4,A6,XCMPLX_1:37;
       then v^2*2=sqrt (a^2+b^2)-a by XCMPLX_1:11;
       then A8: v^2=(-(-sqrt (a^2+b^2)+a))/2 by XCMPLX_1:90;
                Im s = b;
                then 2*(u*v)<0 by A2,A4;
                then u*v<0 by REAL_2:132;
                then u<0 & v>0 or u>0 & v<0 by REAL_2:132;
          then -u=sqrt ((a+sqrt (a^2+b^2))/2) & v=sqrt ((-a+sqrt (a^2+b^2))/2)
or
           u=sqrt ((a+sqrt (a^2+b^2))/2) & -v=sqrt ((-a+sqrt (a^2+b^2))/2)
                    by A7,A8,SQUARE_1:89,90;
                then 0-u=0+sqrt ((a+sqrt (a^2+b^2))/2)
                & v=sqrt ((-a+sqrt (a^2+b^2))/2) or
                u=sqrt ((a+sqrt (a^2+b^2))/2) &
                0-v=0+sqrt ((-a+sqrt (a^2+b^2))/2) by XCMPLX_1:150;
                then 0+u=0-sqrt ((a+sqrt (a^2+b^2))/2)
                & v=sqrt ((-a+sqrt (a^2+b^2))/2) or
                u=sqrt ((a+sqrt (a^2+b^2))/2)
                & 0+v=0-sqrt ((-a+sqrt (a^2+b^2))/2) by XCMPLX_1:35;
          hence thesis by COMPLEX1:29;
end;

theorem Th32:
   z^2 = s implies
   z=sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
       (sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i> or
   z=-sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
       (-sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i> or
   z= sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
       (-sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i> or
   z=-sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
        (sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i>
proof
       assume
 A1: z^2=s;
       set a=Re s;
       set b=Im s;
       set u=Re z;
       set v=Im z;
B: [*u,v*] = u+v*<i> & [*a,b*] = a+b*<i> by COMPLEX1:27;
   then
  z=[*u,v*] & s=[*a,b*] by COMPLEX1:29;
       then u^2-v^2+2*u*v*<i>=[*a,b*] by A1,Th2;
 then A3: u^2-v^2=a & 2*u*v=b by B,COMPLEX1:163;
       then (u^2-v^2)^2+(2*(u*v))^2=a^2+b^2 by XCMPLX_1:4;
       then (u^2)^2-2*u^2*v^2+(v^2)^2+(2*(u*v))^2=a^2+b^2 by SQUARE_1:64;
    then ((u^2)^2+(v^2)^2)-2*u^2*v^2+2^2*(u*v)^2=a^2+b^2 by SQUARE_1:68;
    then ((u^2)^2+(v^2)^2)-2*u^2*v^2+2^2*(u^2*v^2)=a^2+b^2 by SQUARE_1:68;
    then ((u^2)^2+(v^2)^2)-2*u^2*v^2+(2*2)*(u^2*v^2)=a^2+b^2
                              by SQUARE_1:def 3;
    then 4*(u^2*v^2)-2*u^2*v^2+((u^2)^2+(v^2)^2)=a^2+b^2 by XCMPLX_1:30;
    then (u^2)^2+2*u^2*v^2+(v^2)^2=a^2+b^2 by XCMPLX_1:1;
 then A4: sqrt (u^2+v^2)^2=sqrt (a^2+b^2) by SQUARE_1:63;
         u^2>=0 & v^2>=0 by SQUARE_1:72;
       then u^2+v^2>=0+0 by REAL_1:55;
 then A5: u^2+v^2=sqrt (a^2+b^2) by A4,SQUARE_1:89;
       then u^2-v^2+v^2+u^2=a+sqrt (a^2+b^2) by A3,XCMPLX_1:1;
       then 2*u^2=a+sqrt (a^2+b^2) by XCMPLX_1:11;
 then A6: u^2=(a+sqrt (a^2+b^2))/2 by XCMPLX_1:90;
         v^2+u^2-u^2+v^2=sqrt (a^2+b^2)-a by A3,A5,XCMPLX_1:37;
       then v^2*2=sqrt (a^2+b^2)-a by XCMPLX_1:11;
       then A7: v^2=(-(-sqrt (a^2+b^2)+a))/2 by XCMPLX_1:90;
       per cases;
          suppose b>=0;
                then -2*(u*v)<=-0 by A3,REAL_1:50;
                then (-2)*(u*v)<=0;
                then u*v>=0 by REAL_2:129;
                then u<=0 & v<=0 or u>=0 & v>=0 by SQUARE_1:25;
           then -u=sqrt ((a+sqrt (a^2+b^2))/2) & -v=sqrt ((-a+sqrt (a^2+b^2))/
2)  or
           u=sqrt ((a+sqrt (a^2+b^2))/2) & v=sqrt ((-a+sqrt (a^2+b^2))/2)
                    by A6,A7,SQUARE_1:89,90;
                then 0-u=0+sqrt ((a+sqrt (a^2+b^2))/2)
                & 0-v=0+sqrt ((-a+sqrt (a^2+b^2))/2) or
           u=sqrt ((a+sqrt (a^2+b^2))/2) & v=sqrt ((-a+sqrt (a^2+b^2))/2)
                             by XCMPLX_1:150;
                then 0+u=0-sqrt ((a+sqrt (a^2+b^2))/2)
                & 0+v=0-sqrt ((-a+sqrt (a^2+b^2))/2) or
           u=sqrt ((a+sqrt (a^2+b^2))/2) & v=sqrt ((-a+sqrt (a^2+b^2))/2)
                             by XCMPLX_1:35;
           hence thesis by COMPLEX1:29;
           end;
           suppose
S:          b<0;
            Im s = b;
            then 2*(u*v)<0 by A3,S;
            then u*v<0 by REAL_2:132;
            then u<0 & v>0 or u>0 & v<0 by REAL_2:132;
          then -u=sqrt ((a+sqrt (a^2+b^2))/2) & v=sqrt ((-a+sqrt (a^2+b^2))/2)
or
           u=sqrt ((a+sqrt (a^2+b^2))/2) & -v=sqrt ((-a+sqrt (a^2+b^2))/2)
                    by A6,A7,SQUARE_1:89,90;
                then 0-u=0+sqrt ((a+sqrt (a^2+b^2))/2)
                & v=sqrt ((-a+sqrt (a^2+b^2))/2) or
                u=sqrt ((a+sqrt (a^2+b^2))/2) &
                0-v=0+sqrt ((-a+sqrt (a^2+b^2))/2) by XCMPLX_1:150;
                then 0+u=0-sqrt ((a+sqrt (a^2+b^2))/2)
                & v=sqrt ((-a+sqrt (a^2+b^2))/2) or
                u=sqrt ((a+sqrt (a^2+b^2))/2)
                & 0+v=0-sqrt ((-a+sqrt (a^2+b^2))/2) by XCMPLX_1:35;
          hence thesis by COMPLEX1:29;
          end;
end;

theorem
    CPoly2(0c,0c,0c,z) = 0
proof
    CPoly2(0c,0c,0c,z) = 0*z^2+0*z+0 by Def6,COMPLEX1:def 6
     .=0+0;
  hence thesis;
end;

theorem Th34:
  z1<>0 & CPoly2(z1,0c,0c,z) = 0 implies z=0
proof
    assume
 A1: z1<>0 & CPoly2(z1,0c,0c,z) = 0;
      then 0= z1*z^2+0*z+0 by Def6,COMPLEX1:def 6
       .=z1*z^2;
       then z^2=0 by A1,XCMPLX_1:6;
       then z*z=0 by SQUARE_1:def 3;
       hence z=0 by XCMPLX_1:6;
end;

theorem
      z1<>0 & CPoly2(z1,z2,0c,z)=0 implies z=-(z2/z1) or z=0
proof
       assume
 A1: z1<>0 & CPoly2(z1,z2,0c,z)=0;
       then 0=z1*z^2+z2*z+0 by Def6,COMPLEX1:def 6
       .=z1*(z*z)+z2*z by SQUARE_1:def 3
       .=(z1*z+z2)*z;
       then z1*z+z2=0 or z=0 by XCMPLX_1:6;
       then Poly1(z1,z2,z) = 0 or z=0 by POLYEQ_1:def 1;
    hence z=-(z2/z1) or z=0 by A1,Th24;
end;

theorem Th36:
    z1<>0 & CPoly2(z1,0c,z3,z)=0 implies
    (for s st s=-(z3/z1) holds
       z= sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
            (sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i> or
       z=-sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
           (-sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i> or
       z= sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
           (-sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i> or
       z=-sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
            (sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i>)
proof
       assume
 A1: z1<>0 & CPoly2(z1,0c,z3,z)=0;
       let s;
       assume
 A2: s=-(z3/z1);
         0= z1*z^2+0*z+z3 by A1,Def6,COMPLEX1:def 6
       .=z1*z^2+z3;
       then z1*z^2=-z3 by XCMPLX_0:def 6;
       then z^2=(-z3)/z1 by A1,XCMPLX_1:90;
       then z^2=s by A2,XCMPLX_1:188;
      hence thesis by Th32;
end;

theorem Th37:
    z1<>0 & CPoly2(z1,z2,z3,z)=0 implies
          (for h,t st h=(z2/(2*z1))^2-z3/z1 & t=z2/(2*z1) holds
           z= sqrt (( Re h+sqrt ((Re h)^2+(Im h)^2))/2)+
                (sqrt ((-Re h+sqrt ((Re h)^2+(Im h)^2))/2))*<i>-t   or
           z=-sqrt (( Re h+sqrt ((Re h)^2+(Im h)^2))/2)+
               (-sqrt ((-Re h+sqrt ((Re h)^2+(Im h)^2))/2))*<i>-t   or
           z= sqrt (( Re h+sqrt ((Re h)^2+(Im h)^2))/2)+
               (-sqrt ((-Re h+sqrt ((Re h)^2+(Im h)^2))/2))*<i>-t   or
           z=-sqrt (( Re h+sqrt ((Re h)^2+(Im h)^2))/2)+
                (sqrt ((-Re h+sqrt ((Re h)^2+(Im h)^2))/2))*<i>-t)
proof
       assume
 A1: z1<>0 & CPoly2(z1,z2,z3,z)=0;
       let h,t;
       assume
 A2: h=(z2/(2*z1))^2-z3/z1 & t=z2/(2*z1);
        z1*z^2+z2*z+z3=0 by A1,Def6;
      then (z1*z^2+z2*z+z3)/z1=0;
      then (z1*z^2+z2*z)/z1+z3/z1=0 by XCMPLX_1:63;
      then (z^2*z1)/z1+(z2*z)/z1+z3/z1=0 by XCMPLX_1:63;
      then z^2+(z*z2)/z1+z3/z1=0 by A1,XCMPLX_1:90;
      then z^2+(z*z2)*z1"+z3/z1=0 by XCMPLX_0:def 9;
      then z^2+z*(z2*z1")+z3/z1=0;
 then A3:z^2+(z2/z1)*z+z3/z1=0 by XCMPLX_0:def 9;
      z^2+2*z2/(2*z1)*z+z3/z1=0 by A3,XCMPLX_1:92;
      then z^2+2*(z2/(2*z1))*z+z3/z1=0 by XCMPLX_1:75;
      then z^2+2*z*(z2/(2*z1))+(z2/(2*z1))^2-(z2/(2*z1))^2+z3/z1=0;
      then (z+z2/(2*z1))^2-(z2/(2*z1))^2+z3/z1=0 by SQUARE_1:63;
      then (z+(z2/(2*z1)))^2-((z2/(2*z1))^2-z3/z1)=0;
        then (z+t)^2=h by A2,XCMPLX_1:15;
      then z+t-t= sqrt (( Re h+sqrt ((Re h)^2+(Im h)^2))/2)+
             (sqrt ((-Re h+sqrt ((Re h)^2+(Im h)^2))/2))*<i>-t   or
      z+t-t=-sqrt (( Re h+sqrt ((Re h)^2+(Im h)^2))/2)+
            (-sqrt ((-Re h+sqrt ((Re h)^2+(Im h)^2))/2))*<i>-t   or
      z+t-t= sqrt (( Re h+sqrt ((Re h)^2+(Im h)^2))/2)+
            (-sqrt ((-Re h+sqrt ((Re h)^2+(Im h)^2))/2))*<i>-t   or
      z+t-t=-sqrt (( Re h+sqrt ((Re h)^2+(Im h)^2))/2)+
        (sqrt ((-Re h+sqrt ((Re h)^2+(Im h)^2))/2))*<i>-t by Th32;
    hence thesis by XCMPLX_1:26;
end;

definition let z1,z2,z3,z4,z;
  func CPoly3(z1,z2,z3,z4,z) -> Element of COMPLEX equals
     :Def7:   z1*z^3 +z2*z^2+z3*z+z4;
  coherence;
end;

theorem
    z^2=1 implies z=1 or z=-1
proof
  assume z^2=1;
  then z*z = 1 by SQUARE_1:def 3;
  hence thesis by XCMPLX_1:183;
end;

theorem Th39:
  z#N 3=z*z*z & z#N 3=z^2*z & z#N 3 = z^3
proof
  A1: z#N 3=z GeoSeq.(2+1) by COMSEQ_3:def 2
         .=z GeoSeq.(1+1)*z by COMSEQ_3:def 1
         .=z GeoSeq.(0+1)*z*z by COMSEQ_3:def 1
         .=z GeoSeq.0*z*z*z by COMSEQ_3:def 1
         .=1*z*z*z by COMSEQ_3:def 1,COMPLEX1:def 7
         .=z*z*z;
         then z#N 3=z^2*z by SQUARE_1:def 3;
  hence thesis by A1,Def4;
end;

theorem
      z1<>0 & CPoly3(z1,z2,0c,0c,z)=0 implies z=-z2/z1 or z=0
proof
    assume
 A1: z1<>0 & CPoly3(z1,z2,0c,0c,z)=0;
       then 0=z1*z^3 +z2*z^2+0*z+0 by Def7,COMPLEX1:def 6
        .=z1*(z^2*z) +z2*z^2 by Def4
        .=(z1*z +z2)*z^2;
        then z1*z +z2=0 or z^2=0 by XCMPLX_1:6;
        then Poly1(z1,z2,z)=0 or 1*z^2+0*z+0=0 by POLYEQ_1:def 1;
         then z=-(z2/z1) or CPoly2(1r,0c,0c,z)=0
            by A1,Def6,Th24,COMPLEX1:def 6,def 7;
       hence z=-(z2/z1) or z=0 by Th34,COMPLEX1:def 7;
end;

theorem
      z1<>0 & CPoly3(z1,0c,z3,0c,z)=0 implies
     (for s st s=-(z3/z1) holds
     z=0 or
     z=sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
          (sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i> or
     z=-sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
         (-sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i> or
     z= sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
         (-sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i> or
     z=-sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
          (sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i>)
proof
       assume
 A1: z1<>0 & CPoly3(z1,0c,z3,0c,z)=0;
       let s;
       assume
 A2: s=-(z3/z1);
        0=z1*z^3 +0*z^2+z3*z+0 by A1,Def7,COMPLEX1:def 6
       .=z1*z#N 3+z3*z by Th39
       .=z1*(z^2*z)+z3*z by Th39
       .=(z1*z^2+z3)*z;
       then z1*z^2+0*z+z3=0 or z=0 by XCMPLX_1:6;
       then CPoly2(z1,0c,z3,z)=0 or z=0 by Def6,COMPLEX1:def 6;
     hence thesis by A1,A2,Th36;
end;

theorem
      z1<>0 & CPoly3(z1,z2,z3,0c,z)=0 implies
     (for s,h,t st s=-(z3/z1) & h=(z2/(2*z1))^2-z3/z1 & t=z2/(2*z1) holds
           z=0 or
           z= sqrt (( Re h+sqrt ((Re h)^2+(Im h)^2))/2)+
                (sqrt ((-Re h+sqrt ((Re h)^2+(Im h)^2))/2))*<i>-t   or
           z=-sqrt (( Re h+sqrt ((Re h)^2+(Im h)^2))/2)+
               (-sqrt ((-Re h+sqrt ((Re h)^2+(Im h)^2))/2))*<i>-t   or
           z= sqrt (( Re h+sqrt ((Re h)^2+(Im h)^2))/2)+
               (-sqrt ((-Re h+sqrt ((Re h)^2+(Im h)^2))/2))*<i>-t   or
           z=-sqrt (( Re h+sqrt ((Re h)^2+(Im h)^2))/2)+
                (sqrt ((-Re h+sqrt ((Re h)^2+(Im h)^2))/2))*<i>-t)
proof
       assume
 A1: z1<>0 & CPoly3(z1,z2,z3,0c,z)=0;
       let s,h,t;
       assume
 A2: s=-(z3/z1) & h=(z2/(2*z1))^2-z3/z1 & t=z2/(2*z1);
         0=z1*z^3 +z2*z^2+z3*z+0 by A1,Def7,COMPLEX1:def 6
       .=z1*z#N 3 +z2*z^2+z3*z by Th39
       .=z1*(z^2*z) +z2*z^2+z3*z by Th39
       .=z1*z^2*z +z2*(z*z)+z3*z by SQUARE_1:def 3
       .=(z1*z^2+z2*z+z3)*z
       .=CPoly2(z1,z2,z3,z)*z by Def6;
       then z=0 or CPoly2(z1,z2,z3,z)=0 by XCMPLX_1:6;
     hence thesis by A1,A2,Th37;
end;

3': [*1/3,0*] = 1/3+0*<i> by COMPLEX1:27;
9': [*1/9,0*] = 1/9+0*<i> by COMPLEX1:27;

theorem Th43:
    (s-(1/3)*z1)^2 =s^2+(-(2/3))*z1*s+(1/9)*z1^2
proof
23': [*2/3,0*] = 2/3+0*<i> by COMPLEX1:27;
  set z=s-(1/3)*z1;
       z^2=(s+(-(1/3)*z1))^2
      .=s^2+2*s*(-(1/3)*z1)+(-(1/3)*z1)^2 by SQUARE_1:63
      .=s^2+2*s*(-(1/3)*z1)+((1/3)*z1)^2 by SQUARE_1:61
      .=s^2+(-1)*((2+0*<i>)*(1/3))*z1*s+(1/3)^2*z1^2 by SQUARE_1:68
      .=s^2+(-1)*(Re (2+0*<i>)*Re (1/3)-Im (2+0*<i>)*Im (1/3)+
       (Re(2+0*<i>)*Im(1/3)+Re(1/3)*Im(2+0*<i>))*<i>)*z1*s+(1/3)^2*z1^2
                                by COMPLEX1:def 10,3'
      .=s^2+(-1)*([*Re (2+0*<i>)*Re (1/3)-Im (2+0*<i>)*Im (1/3)
      ,Re(2+0*<i>)*Im(1/3)+Re(1/3)*Im(2+0*<i>)*])*z1*s+(1/3)^2*z1^2
                                by COMPLEX1:27
      .=s^2+(-1)*([*Re 2*Re (1/3)-Im 2*Im (1/3)
      ,Re 2*Im (1/3)+Re (1/3)*0*])*z1*s+(1/3)^2*z1^2
                                by COMPLEX1:28
      .=s^2+(-1)*([*Re 2*Re (1/3)-Im 2*Im (1/3)
      ,Re 2*0+0*])*z1*s+(1/3)^2*z1^2 by COMPLEX1:28,3'
      .=s^2+(-1)*([*Re 2*Re (1/3)-Im 2*0,0*])*z1*s
             +(1/3)^2*z1^2 by COMPLEX1:28,3'
      .=s^2+(-1)*([*Re (2+0*<i>)*(1/3)-0,0*])*z1*s+(1/3)^2*z1^2
                                       by COMPLEX1:28,3'
      .=s^2+(-1)*([*2*(1/3)-0,0*])*z1*s+(1/3+0*<i>)^2*z1^2 by COMPLEX1:28
      .=s^2+(-(2/3))*z1*s+((Re (1/3+0*<i>))^2-(Im (1/3+0*<i>))^2+
         2*(Re (1/3+0*<i>)*Im (1/3+0*<i>))*<i>)*z1^2 by Def3,23'
      .=s^2+(-(2/3))*z1*s+((Re (1/3))^2-(Im (1/3))^2+
         2*(Re (1/3)*0)*<i>)*z1^2 by COMPLEX1:28
      .=s^2+(-(2/3))*z1*s+((Re (1/3))^2-0^2+0*<i>)*z1^2
                    by COMPLEX1:28,3'
      .=s^2+(-(2/3))*z1*s+((1/3)^2-0+0*<i>)*z1^2
          by COMPLEX1:28,SQUARE_1:60,3'
      .=s^2+(-(2/3))*z1*s+((1/3)*(1/3)-0+0*<i>)*z1^2 by SQUARE_1:def 3;
 hence thesis;
end;

theorem Th44:
   z=s-(1/3)*z1 implies
    z^3 =s^3-z1*s^2+(1/3)*z1^2*s-(1/27)*z1^3
proof
3'': 3 = 3+0*<i>;
    assume
         z=s-(1/3)*z1;
 then A1: z^3=(s+(-(1/3)*z1))^3
      .=s^3+3*(-(1/3)*z1)*s^2+3*(-(1/3)*z1)^2*s+(-(1/3)*z1)^3
                                    by Th13;
      set s1=-(1/3)*z1;
 A3: s1^2=((-(1/3))*z1)*((-(1/3))*z1) by SQUARE_1:def 3
       .=(-(1/3))*(-(1/3))*(z1*z1)
       .=(1/3)*(1/3)*z1^2 by SQUARE_1:def 3
       .=(1/3)^2*z1^2 by SQUARE_1:def 3
       .=((Re (1/3))^2-(Im (1/3))^2+2*(Re (1/3)*Im (1/3))*<i>)
               *z1^2 by Def3,3'
       .=((Re (1/3))^2-(Im (1/3))^2+2*(Re (1/3)*0)*<i>)*z1^2
                              by COMPLEX1:28,3'
       .=((Re (1/3))^2-0^2+2*(Re (1/3)*0)*<i>)*z1^2 by COMPLEX1:28,3'
       .=((1/3)^2-0+2*(1/3)*0*<i>)*z1^2 by COMPLEX1:28,SQUARE_1:60,3'
       .=((1/3)*(1/3)-0+2*(1/3)*0*<i>)*z1^2 by SQUARE_1:def 3
       .=(1/9)*z1^2;
 then A4: 3*s1^2 =3*(1/9)*z1^2
            .=(Re 3*Re (1/9)-Im 3 *Im (1/9)+
              (Re 3 *Im (1/9)+Re (1/9)*Im 3)*<i>)*z1^2
                          by COMPLEX1:def 10,3'',9'
            .=(Re 3*Re (1/9)-0*Im (1/9)+
              (Re 3 *Im (1/9)+Re (1/9)*Im 3)*<i>)*z1^2
                          by COMPLEX1:28,3''
            .=(Re 3*Re (1/9)-0+(Re 3 *Im (1/9)
              +Re (1/9)*0)*<i>)*z1^2 by COMPLEX1:28,3''
            .=(Re 3*Re (1/9)-0+(Re 3 *0 +0)*<i>)*z1^2 by COMPLEX1:28,9'
            .= (Re 3*(1/9)-0)*z1^2 by COMPLEX1:28,9'
            .=(3*(1/9)-0)*z1^2 by COMPLEX1:28,3''
            .= (1/3+0*<i>)*z1^2;
       s1^3=((1/9)*z1^2)*((-(1/3))*z1) by A3,Def4
       .=(1/9)*(-(1/3))*(z1^2*z1)
       .=(1/9)*(1/3)*(-1)*z1^3 by Def4
       .= (Re (1/9)*Re (1/3)-Im (1/9)*Im (1/3)+
           (Re (1/9)*Im (1/3)+Re (1/3)*Im (1/9))*<i>)*(-1)*z1^3
                    by COMPLEX1:def 10,3',9'
       .=(Re (1/9)*Re (1/3)-Im (1/9)*Im (1/3)+
        (Re (1/9)*Im (1/3)+Re (1/3)*0)*<i>)*(-1)*z1^3
                    by COMPLEX1:28,9'
       .= (Re (1/9)*Re (1/3)-Im (1/9)*Im (1/3)+
         Re (1/9)*0*<i>)*(-1)*z1^3 by COMPLEX1:28,3'
       .= (Re (1/9)*Re (1/3)-0*Im (1/3))*(-1)*z1^3
                           by COMPLEX1:28,9'
       .=(1/9*Re (1/3)-0*Im (1/3))*(-1)*z1^3 by COMPLEX1:28,9'
       .=(1/9*(1/3)-0)*(-1)*z1^3 by COMPLEX1:28,3'
       .=(-(1/27))*z1^3;
     hence thesis by A1,A4;
end;

theorem
      CPoly3(1r,z1,z2,z3,z)=0 implies
    (for p,q,s st z=s-(1/3)*z1 & p=-(1/3)*z1^2+z2
                & q=(2/27)*z1^3-(1/3)*z1*z2+z3 holds
      CPoly3(1r,0c,p,q,s)=0 )
proof
27': 1/27 = 1/27+0*<i>;
    assume
 A1:CPoly3(1r,z1,z2,z3,z) = 0;
    let p,q,s;
     assume
 A2: z=s-(1/3)*z1 & p=-(1/3)*z1^2+z2
       & q=(2/27)*z1^3-(1/3)*z1*z2+z3;
         0=1*z^3+ z1*z^2+z2*z+z3 by A1,Def7,COMPLEX1:def 7
       .=s^3-z1*s^2+(1/3)*z1^2*s-(1/27)*z1^3+z1*z^2+z2*z+z3
                    by A2,Th44
       .=s^3-z1*s^2+(1/3)*z1^2*s-(1/27)*z1^3+z1*(s^2+(-(2/3))
        *z1*s+(1/9)*z1^2)+z2*(s-(1/3)*z1)+z3 by A2,Th43
       .=s^3-z1*s^2+(1/3)*z1^2*s-(1/27)*z1^3+(z1*s^2+z1*((-(2/3))
        *z1*s)+z1*z1^2*(1/9))+z2*(s-(1/3)*z1)+z3
       .=s^3-z1*s^2+(1/3)*z1^2*s-(1/27)*z1^3+(z1*s^2+z1*z1*
      (-(2/3))*s+z1^3*(1/9))+z2*(s-(1/3)*z1)+z3 by Def4
      .=s^3-z1*s^2+(1/3)*z1^2*s-(1/27)*z1^3+(z1*s^2+z1^2*(-(2/3))
       *s+z1^3*(1/9))+(z2*s-z2*((1/3)*z1))+z3 by SQUARE_1:def 3
      .=s^3+((-(2/3)+(1/3))*z1^2+z2)*s+
        ((1/9)-(1/27))
           *z1^3-z2*((1/3)*z1)+z3
     .=s^3+((-((2/3)-(1/3)))*z1^2+z2)*s+
        (Re (1/9)-Re (1/27)+(Im (1/9)-Im (1/27))*<i>)
           *z1^3-z2*((1/3)*z1)+z3 by COMPLEX1:def 12,9',27'
     .=s^3+((-((2/3)-(1/3)))*z1^2+z2)*s+
         (Re (1/9)-Re(1/27)+(Im (1/9+0*<i>)-0)*<i>)
        *z1^3-z2*((1/3)*z1)+z3 by COMPLEX1:28,27'
     .=s^3+((-((2/3)-(1/3)))*z1^2+z2)*s+
         (Re (1/9)-Re(1/27)+(0-0)*<i>)
        *z1^3-z2*((1/3)*z1)+z3 by COMPLEX1:28
     .=s^3+((-((2/3)-(1/3)))*z1^2+z2)*s+
         (Re (1/9)-1/27+(0-0)*<i>)
        *z1^3-z2*((1/3)*z1)+z3 by COMPLEX1:28,27'
     .=s^3+((-((2/3)-(1/3)))*z1^2+z2)*s+(1/9-1/27+(0-0)*<i>)*z1^3-
        z2*((1/3)*z1)+z3 by COMPLEX1:28,9'
     .=1*s^3+0*s^2+p*s+q by A2;
  hence CPoly3(1r,0c,p,q,s)=0 by Def7,COMPLEX1:def 6,def 7;
end;

theorem Th46:
   for z being Element of COMPLEX holds
    [* |.z.|*cos Arg z, |.z.|*sin Arg z *]
    = [*|.z.|,0*]*[*cos Arg z,sin Arg z *]
proof
    let z be Element of COMPLEX;
X:  [*|.z.|,0*] = |.z.|+0*<i> &
    [*cos Arg z,sin Arg z *] = cos Arg z+(sin Arg z)*<i> by COMPLEX1:27;
      [* |.z.|*cos Arg z, |.z.|*sin Arg z *]
     =|.z.|*cos Arg z-0*sin Arg z+(|.z.|*sin Arg z+(cos Arg z)*0)*<i>
                     by COMPLEX1:27
    .= [*|.z.|,0*]*[*cos Arg z,sin Arg z *] by X;
    hence thesis;
end;

theorem Th47:
  for z be Element of COMPLEX,n be Nat holds
    z #N (n+1)=(z #N n) * z
proof
    let z be Element of COMPLEX,n be Nat;
      z #N (n+1) = (z GeoSeq).(n+1) by COMSEQ_3:def 2
         .= (z GeoSeq).n*z by COMSEQ_3:def 1
         .= (z #N n)*z by COMSEQ_3:def 2;
   hence thesis;
end;

theorem Th48:
  for z be Element of COMPLEX holds
    z#N 1= z
proof
    let z be Element of COMPLEX;
      z #N (0+1) = (z#N 0)*z by Th47
              .= 1*z by COMSEQ_3:11,COMPLEX1:def 7
              .= z;
   hence thesis;
end;

theorem
    for z be Element of COMPLEX holds
    z#N 2= z*z
proof
    let z be Element of COMPLEX;
      z #N (1+1) = (z#N 1)*z by Th47
              .= z*z by Th48;
   hence thesis;
end;

theorem Th50:
  for n be Nat st n>0 holds
    0c #N n = 0
proof
    let n be Nat;
    assume n>0;
       then n >= 0+1 by NAT_1:38;
 then A1: n-1 >= 0 by REAL_1:84;
         n=n-1+1
       .= n-'1+1 by A1,BINARITH:def 3;
       hence 0c #N n = (0c #N (n-'1))*0 by Th47,COMPLEX1:def 6
              .= 0;
end;

theorem Th51:
   for x,y be Element of COMPLEX
    for n be Nat holds
     (x*y) #N n = x #N n * y #N n
proof
    let x,y be Element of COMPLEX;
    defpred P[Nat] means (x*y) #N $1 = x #N $1 * y #N $1;
      (x*y) #N 0 =1 by COMSEQ_3:11,COMPLEX1:def 7
                .=(x #N 0)*1 by COMSEQ_3:11,COMPLEX1:def 7
                .=(x #N 0)*(y #N 0) by COMSEQ_3:11,COMPLEX1:def 7;
then A1:  P[0];
 A2: now let n be Nat;
     assume P[n];
      then (x*y)#N (n+1)= x#N n * y#N n * (y*x) by Th47
        .= x#N n * (y#N n * y*x)
        .= x#N n * (y#N (n+1)*x) by Th47
        .= x#N n * x *(y#N (n+1))
        .= x#N (n+1) * y#N (n+1) by Th47;
     hence P[n+1];
   end;
    thus for n be Nat holds P[n] from NAT_1:sch 1(A1,A2);
end;

theorem Th52:
    for x be Real st x > 0
      for n be Nat holds [*x,0*]#N n = x to_power n
proof
      let x be Real;
      assume
 A1: x > 0;
 defpred P[Nat] means [*x,0*]#N $1 = x to_power $1;
   [*x,0*]#N 0 = 1+0*<i> by COMPLEX1:def 7,COMSEQ_3:11
       .= [**1,0**] by HAHNBAN1:def 1
       .= [**x to_power 0,0**] by POWER:29
       .= x to_power 0+0*<i> by HAHNBAN1:def 1;
 then A2: P[0];
 A3: now let n be Nat;
X:  [*x to_power n,0*] = x to_power n+0*<i>  &
    [*x,0*] = x+0*<i> by COMPLEX1:27;
     assume P[n];
     then [*x,0*]#N (n+1) = (x to_power n)*x by X,Th47
        .= (x to_power n)*(x to_power 1) by POWER:30
        .= x to_power (n+1) by A1,POWER:32;
     hence P[n+1];
    end;
   thus for n be Nat holds P[n] from NAT_1:sch 1(A2,A3);
end;

theorem Th53:
   for x being Real
   for n be Nat holds
    [*cos x,sin x*] #N n = cos (n*x) + sin (n*x)*<i>
proof
 let x be Real;
 defpred P[Nat] means [*cos x,sin x*]#N $1 = cos ($1*x)+sin ($1*x)*<i>;
 A1: P[0] by SIN_COS:34,COMSEQ_3:11,COMPLEX1:def 7;
 A2: now let n be Nat;
      set a'=Re [*cos(n*x),sin(n*x)*];
      set b'=Im [*cos(n*x),sin(n*x)*];
      set c'=Re [*cos x,sin x*];
      set d'=Im [*cos x,sin x*];
X: [*cos(n*x),sin(n*x)*] = cos(n*x)+sin(n*x)*<i> &
   [*cos x,sin x*] = cos x+(sin x)*<i> by COMPLEX1:27;
       assume P[n];
     then [*cos x,sin x*] #N (n+1)
         = (cos(n*x)+sin(n*x)*<i>)*(cos x+(sin x)*<i>) by Th47,X
        .= cos(n*x)*cos x - sin(n*x)*sin x+
           (cos(n*x)*sin x+cos x*sin(n*x))*<i>
        .= cos(n*x+x)+(cos(n*x)*sin x+cos x*sin(n*x))*<i> by SIN_COS:80
        .= cos((n+1)*x)+sin((n+1)*x)*<i> by SIN_COS:80;
      hence P[n+1];
    end;
    thus for n be Nat holds P[n] from NAT_1:sch 1(A1,A2);
 end;

theorem
       for z being Element of COMPLEX
     for n being Nat st z <> 0 or n > 0 holds
    z#N n = (|.z.| to_power n)*cos (n*Arg z) +
            (|.z.| to_power n)*sin (n*Arg z)*<i>
proof
    let z be Element of COMPLEX;
    let n be Nat;
    assume
 A1: z <> 0 or n > 0;
      per cases by A1;
     suppose z <> 0;
 then A2: |.z.| > 0 by COMPLEX1:133;
X:  [*|.z.|,0*] = |.z.|+0*<i> &
    [*cos Arg z,sin Arg z*] = cos Arg z+(sin Arg z)*<i> by COMPLEX1:27;
        set x=Arg z;
Y: [*cos (n*x),sin (n*x)*] = cos (n*x)+sin (n*x)*<i> by COMPLEX1:27;
        z#N n =[*|.z.|*cos Arg z-0*sin Arg z,|.z.|*sin Arg z+cos Arg z*0*]#N n
                                            by COMPLEX2:19
         .=(|.z.|*cos Arg z-0*sin Arg z+(|.z.|*sin Arg z+cos Arg z*0)*<i>)#N n
                                             by COMPLEX1:27
         .=([*|.z.|,0*]*[*cos Arg z,sin Arg z*])#N n by X
         .= [*|.z.|,0*]#N n*[*cos Arg z,sin Arg z*]#N n by Th51;
       hence z#N n= [*|.z.|,0*]#N n*[*cos (n*x),sin (n*x)*] by Th53,Y
       .= (|.z.| to_power n)*[*cos (n*Arg z),sin (n*Arg z)*]
                                     by A2,Th52
       .= (|.z.| to_power n+0*<i>)*(cos (n*Arg z)+(sin (n*Arg z))*<i>)
                                     by COMPLEX1:27
       .= (|.z.| to_power n)*cos(n*Arg z)+(|.z.| to_power n)*sin(n*Arg z)*<i>;
     end;
     suppose
 A3: z=0 & n > 0;
     then 0=|.z.| to_power n by COMPLEX1:130,POWER:def 2;
       hence z#N n= (|.z.| to_power n)*cos(n*Arg z)+
        (|.z.| to_power n)*sin(n*Arg z)*<i> by A3,Th50,COMPLEX1:def 6;
      end;
end;

theorem Th55:
   for n,k be Nat,x be Real st n <> 0 holds
    [*cos((x+2*PI*k)/n),sin((x+2*PI*k)/n)*]#N n = cos x + (sin x)*<i>
   proof
    let n,k be Nat,x be Real;
    assume
 A1: n <> 0;
       thus [*cos((x+2*PI*k)/n),sin((x+2*PI*k)/n)*]#N n
        = cos (n*((x+2*PI*k)/n))+sin (n*((x+2*PI*k)/n))*<i> by Th53
       .= cos(x+2*PI*k)+sin(n*((x+2*PI*k)/n))*<i> by A1,XCMPLX_1:88
       .= cos(x+2*PI*k)+sin(x+2*PI*k)*<i> by A1,XCMPLX_1:88
       .= cos.(x+2*PI*k)+sin(x+2*PI*k)*<i> by SIN_COS:def 23
       .= cos.(x+2*PI*k)+(sin.(x+2*PI*k))*<i> by SIN_COS:def 21
       .= cos.(x+2*PI*k)+(sin.x)*<i> by SIN_COS2:10
       .= cos.x + (sin.x)*<i> by SIN_COS2:11
       .= cos.x + (sin x)*<i> by SIN_COS:def 21
       .= cos x + (sin x)*<i> by SIN_COS:def 23;
   end;

theorem Th56:
   for z be complex number
   for n,k be Nat st n <> 0 holds
    z = [*(n-root |.z.|)*cos((Arg z+2*PI*k)/n),
          (n-root |.z.|)*sin((Arg z+2*PI*k)/n)*]#N n
   proof
    let z be complex number;
    let n,k be Nat;
    assume
 A1: n <> 0;
 then A2: n > 0 by NAT_1:19;
 then A3: n >= 0+1 by NAT_1:38;
    per cases;
      suppose
        z <> 0;
 then A4: |.z.| > 0 by COMPLEX1:133;
       then n-root |.z.| = n -Root |.z.| by A3,POWER:def 1;
 then A5: n-root |.z.| > 0 by A3,A4,PREPOWER:def 3;
     thus [*(n-root |.z.|)*cos((Arg z+2*PI*k)/n),
            (n-root |.z.|)*sin((Arg z+2*PI*k)/n)*]#N n
        =((n-root |.z.|)*cos((Arg z+2*PI*k)/n)-
          0*sin((Arg z+2*PI*k)/n)+((n-root |.z.|)*sin((Arg z+2*PI*k)/n)+
          cos((Arg z+2*PI*k)/n)*0)*<i>)#N n by COMPLEX1:27
       .= ((n-root |.z.|+0*<i>)*
          (cos((Arg z+2*PI*k)/n)+(sin((Arg z+2*PI*k)/n))*<i>))#N n
                       
       .= ((n-root |.z.|+0*<i>)*
          [*cos((Arg z+2*PI*k)/n),sin((Arg z+2*PI*k)/n)*])#N n by COMPLEX1:27
       .= ([*n-root |.z.|,0*]*
          [*cos((Arg z+2*PI*k)/n),sin((Arg z+2*PI*k)/n)*])#N n by COMPLEX1:27
       .= ([*n-root |.z.|,0*]#N n)
         *([*cos((Arg z+2*PI*k)/n),sin((Arg z+2*PI*k)/n)*]#N n) by Th51
       .= ([*n-root |.z.|,0*]#N n)*(cos Arg z+(sin Arg z)*<i>) by A1,Th55
       .= ((n-root |.z.|) to_power n)*(cos Arg z+(sin Arg z)*<i>)
                                    by A5,Th52
       .= |.z.|*(cos Arg z+(sin Arg z)*<i>) by A1,A4,COMPTRIG:17
       .= |.z.|*cos Arg z-0*sin Arg z+(|.z.|*sin Arg z+0*cos Arg z)*<i>
       .= [*|.z.|*cos Arg z-0*sin Arg z,|.z.|*sin Arg z+0*cos Arg z*]
                                     by COMPLEX1:27
       .= z by COMPLEX2:19;
     end;
     suppose
 A6: z = 0;
      hence [*(n-root |.z.|)*cos((Arg z+2*PI*k)/n),
            (n-root |.z.|)*sin((Arg z+2*PI*k)/n)*]#N n
        =([*0*cos((Arg z+2*PI*k)/n),
           (n-root 0)*sin((Arg z+2*PI*k)/n)*]#N n) by A3,COMPLEX1:130,POWER:6
       .= [*0,0*sin((Arg z+2*PI*k)/n)*]#N n by A3,POWER:6
       .= z by A2,A6,Lm1,Th50,COMPLEX1:def 6;
     end;
   end;

definition
   let z be complex number;
   let n be non empty Nat;
   mode CRoot of n,z -> Element of COMPLEX means :Def8:
         it#N n = z;
  existence
   proof
    take [*(n-root |.z.|)*cos((Arg z+2*PI*0)/n),
         (n-root |.z.|)*sin((Arg z+2*PI*0)/n)*];
    thus thesis by Th56;
   end;
end;

theorem
     for z be Element of COMPLEX
   for n be non empty Nat
   for k be Nat holds
    [*(n-root |.z.|)*cos((Arg z+2*PI*k)/n),
      (n-root |.z.|)*sin((Arg z+2*PI*k)/n)*] is CRoot of n,z
proof
   let z be Element of COMPLEX,n be non empty Nat,k be Nat;
      [*(n-root |.z.|)*cos((Arg z+2*PI*k)/n),
      (n-root |.z.|)*sin((Arg z+2*PI*k)/n)*]#N n=z by Th56;
    hence thesis by Def8;
end;

theorem
       for z be complex number
     for v be CRoot of 1,z holds
       v = z
   proof
    let z be complex number;
    let v be CRoot of 1,z;
      v#N 1 = z by Def8;
    hence v = z by Th48;
   end;

theorem
     for n be non empty Nat
   for v be CRoot of n,0 holds v = 0
proof
    let n be non empty Nat;
    let v be CRoot of n,0;
    defpred P[Nat] means v#N $1 = 0;
 A1: ex n be non empty Nat st P[n]
    proof
       take n;
       thus thesis by Def8;
    end;
 A2: now let k be non empty Nat;
      assume that
 A3: k <> 1 and
 A4: P[k];
     consider t be Nat such that
 A5: k = t+1 by NAT_1:22;
         k >= 1 by NAT_1:39;
       then t+1 > 0+1 by A3,A5,REAL_1:def 5;
 then A6: t > 0 by AXIOMS:24;
     reconsider t as non empty Nat by A3,A5,CARD_1:51;
     take t;
     thus t < k by A5,NAT_1:38;
       v#N k = (v#N t)*v by A5,Th47;
     then v#N t = 0 or v = 0 by A4,XCMPLX_1:6;
     hence P[t] by A6,Th50,COMPLEX1:def 6;
    end;
      P[1] from COMPTRIG:sch 1(A1,A2);
    hence thesis by Th48;
end;

theorem
       for n be non empty Nat
     for z be complex number
     for v be CRoot of n,z st v = 0 holds z = 0
proof
     let n be non empty Nat;
     let z be complex number;
     let v be CRoot of n,z;
 A1:n>0 by NAT_1:19;
     assume v = 0;
     then 0c#N n = z by Def8,COMPLEX1:def 6;
     hence thesis by A1,Th50;
end;

theorem
     for n be non empty Nat
   for k be Nat holds
    cos(2*PI*k/n) + sin(2*PI*k/n)*<i> is CRoot of n,1
proof
   let n be non empty Nat,k be Nat;
X: [*cos(2*PI*k/n),sin(2*PI*k/n)*] = cos(2*PI*k/n)+sin(2*PI*k/n)*<i>
               by COMPLEX1:27;
        [*cos(2*PI*k/n),sin(2*PI*k/n)*]#N n
       =cos(n*((2*PI*k)/n))+sin(n*((2*PI*k)/n))*<i> by Th53
      .=cos(2*PI*k)+(sin(n*((2*PI*k)/n)))*<i> by XCMPLX_1:88
      .=cos(2*PI*k+0)+(sin(2*PI*k+0))*<i> by XCMPLX_1:88
      .=cos.(2*PI*k+0)+(sin(2*PI*k+0))*<i> by SIN_COS:def 23
      .=cos.(2*PI*k+0)+(sin.(2*PI*k+0))*<i> by SIN_COS:def 21
      .=cos.(2*PI*k+0)+(sin.0)*<i> by SIN_COS2:10
      .=1 by SIN_COS:33,SIN_COS2:11;
    hence thesis by Def8,X;
end;

theorem
     for k be Nat holds
    cos(2*PI*k/3) + sin(2*PI*k/3)*<i> is CRoot of 3,1
proof
     let k be Nat;
X: [*cos(2*PI*k/3),sin(2*PI*k/3)*] = cos(2*PI*k/3)+sin(2*PI*k/3)*<i>
                 by COMPLEX1:27;
       [*cos(2*PI*k/3),sin(2*PI*k/3)*]#N 3
       = cos(2*PI*k)+sin(3*((2*PI*k)/3))*<i> by Th53
      .= cos.(2*PI*k+0)+(sin(2*PI*k+0)*<i>) by SIN_COS:def 23
      .= cos.(2*PI*k+0)+(sin.(2*PI*k+0)*<i>) by SIN_COS:def 21
      .= cos.(2*PI*k+0)+(sin.0*<i>) by SIN_COS2:10
      .=1 by SIN_COS:33,SIN_COS2:11;
    hence thesis by Def8,X;
end;

theorem
       for z,s being Element of COMPLEX, n being Nat st
     s<>0 & z<>0 & n>=1 & s#N n=z#N n holds
     |.s.| = |.z.|
proof
     let z,s be Element of COMPLEX, n be Nat;
     assume
 A1: s<>0 & z<>0 & n>=1 & s#N n=z#N n;
 then A2: |.s.|>0 & |.z.|>0 by COMPLEX1:133;
         z#N n =[* |.s.|*cos Arg s, |.s.|*sin Arg s *]#N n by A1,COMPLEX2:19
        .=([*|.s.|,0*]*[*cos Arg s,sin Arg s *])#N n by Th46
        .=([*|.s.|,0*]#N n)*([*cos Arg s,sin Arg s *]#N n) by Th51
        .=(|.s.| to_power n)*([*cos Arg s,sin Arg s *]#N n) by A2,Th52
        .=(|.s.| to_power n)*(cos (n*Arg s)+sin (n*Arg s)*<i>) by Th53
       .= (|.s.| to_power n)*cos (n*Arg s)-0*sin (n*Arg s)+
        ((|.s.| to_power n)*sin (n*Arg s)+cos (n*Arg s)*0)*<i>
       .= [*(|.s.| to_power n)*cos (n*Arg s)
           ,(|.s.| to_power n)*sin (n*Arg s)*] by COMPLEX1:27;
       then [*(|.s.| to_power n)*cos (n*Arg s),(|.s.| to_power n)*sin (n*Arg s
)*]
        =[* |.z.|*cos Arg z, |.z.|*sin Arg z *]#N n by COMPLEX2:19
        .=([*|.z.|,0*]*[*cos Arg z,sin Arg z *])#N n by Th46
        .=([*|.z.|,0*]#N n)*([*cos Arg z,sin Arg z *]#N n) by Th51
        .=(|.z.| to_power n)*([*cos Arg z,sin Arg z *]#N n) by A2,Th52
        .=(|.z.| to_power n)*(cos (n*Arg z)+sin (n*Arg z)*<i>) by Th53
       .= (|.z.| to_power n)*cos (n*Arg z)-0*sin (n*Arg z)+
          ((|.z.| to_power n)*sin (n*Arg z)+cos (n*Arg z)*0)*<i>
       .= [*(|.z.| to_power n)*cos (n*Arg z),(|.z.| to_power n)
          *sin (n*Arg z)*] by COMPLEX1:27;
      then (|.s.| to_power n)*cos (n*Arg s)=(|.z.| to_power n)*cos (n*Arg z)
       &(|.s.| to_power n)*sin (n*Arg s)
       =(|.z.| to_power n)*sin (n*Arg z) by ARYTM_0:12;
       then ((|.s.| to_power n))^2*(cos (n*Arg s))^2+
       ((|.s.| to_power n)*sin (n*Arg s))^2
       =((|.z.| to_power n)*cos (n*Arg z))^2+
       ((|.z.| to_power n)*sin (n*Arg z))^2 by SQUARE_1:68;
       then ((|.s.| to_power n))^2*(cos (n*Arg s))^2+
       ((|.s.| to_power n))^2*(sin (n*Arg s))^2
       =((|.z.| to_power n)*cos (n*Arg z))^2+
       ((|.z.| to_power n)*sin (n*Arg z))^2 by SQUARE_1:68;
       then ((|.s.| to_power n))^2*(cos (n*Arg s))^2+
       ((|.s.| to_power n))^2*(sin (n*Arg s))^2
       =((|.z.| to_power n))^2*(cos (n*Arg z))^2+
       ((|.z.| to_power n)*sin (n*Arg z))^2 by SQUARE_1:68;
       then ((|.s.| to_power n))^2*(cos (n*Arg s))^2+
       ((|.s.| to_power n))^2*(sin (n*Arg s))^2
       =((|.z.| to_power n))^2*(cos (n*Arg z))^2+
       ((|.z.| to_power n))^2*(sin (n*Arg z))^2 by SQUARE_1:68;
       then ((|.s.| to_power n))^2*((cos (n*Arg s))^2+(sin (n*Arg s))^2)
       =((|.z.| to_power n))^2*((cos (n*Arg z))^2+(sin (n*Arg z))^2)
                          by XCMPLX_1:8;
       then ((|.s.| to_power n))^2*((cos (n*Arg s))^2+(sin (n*Arg s))^2)
       =((|.z.| to_power n))^2*1 by SIN_COS:32;
 then A3: ((|.s.| to_power n))^2*1=((|.z.| to_power n))^2 by SIN_COS:32;
 A4: |.s.| to_power n > 0 & |.z.| to_power n > 0 by A2,POWER:39;
       then (|.s.| to_power n)
       =sqrt ((|.z.| to_power n))^2 by A3,SQUARE_1:89;
       then |.s.| to_power n=|.z.| to_power n by A4,SQUARE_1:89;
       then |.s.| |^ n=|.z.| to_power n by A2,POWER:46;
       then |.s.| |^ n=|.z.| |^ n by A2,POWER:46;
       then |.s.| = n-root (|.z.| |^ n) by A1,A2,POWER:5;
      hence thesis by A1,A2,POWER:5;
end;
