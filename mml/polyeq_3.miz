:: Solving Complex Roots of Polynomial Equation of Degree 2 and 3 with
:: Complex Coefficients
::  by Yuzhong Ding and Xiquan Liang
::
:: Received January 26, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies COMPLEX1, ARYTM_3, ARYTM_1, POLYEQ_1, SQUARE_1, FUNCT_3,
      POLYEQ_3, RELAT_1, POWER, GROUP_1, PREPOWER, FUNCT_1, SIN_COS, COMPTRIG,
      XCMPLX_0, ARYTM;
 notations SUBSET_1, NUMBERS, XBOOLE_0, ORDINAL1, XCMPLX_0, XXREAL_0, REAL_1,
      COMPLEX1, POLYEQ_1, SQUARE_1, NEWTON, ARYTM_0, QUIN_1, POWER, NAT_1,
      SIN_COS, PREPOWER, BINARITH, SEQ_1, COMPTRIG;
 constructors FUNCT_4, ARYTM_0, REAL_1, SQUARE_1, NAT_1, SEQ_1, QUIN_1,
      PREPOWER, POWER, POLYEQ_1, BINARITH, SIN_COS, COMPTRIG;
 registrations ORDINAL1, NUMBERS, XCMPLX_0, XXREAL_0, XREAL_0, SQUARE_1, NAT_1,
      MEMBERED, NEWTON, POLYEQ_1;
 requirements ARITHM, SUBSET, REAL, BOOLE, NUMERALS;
 definitions POLYEQ_1, SQUARE_1, XCMPLX_0, QUIN_1;
 theorems COMPLEX1, POLYEQ_1, REAL_2, SQUARE_1, NEWTON, COMPTRIG, NAT_1,
      BINARITH, POWER, SIN_COS, SIN_COS2, PREPOWER, COMPLEX2, XCMPLX_0,
      XCMPLX_1, ARYTM_0, XREAL_1;
 schemes NAT_1, COMPTRIG;

begin

Lm1: 0 = [*0,0*] by ARYTM_0:def 7;

 reserve a,b,c,d,a',b',c',d',x,y,x1,u,v for Real,
         s,t,h,z,z1,z2,z3,s1,s2,s3,p,q for Element of COMPLEX;

definition let a be Real,z;
  redefine func a*z -> Element of COMPLEX;
  correctness by XCMPLX_0:def 2;
  redefine func a + z -> Element of COMPLEX;
  correctness by XCMPLX_0:def 2;
end;

definition let z be Element of COMPLEX;
  redefine func z^2 -> Element of COMPLEX equals
      (Re z)^2-(Im z)^2 + 2*(Re z*Im z)*<i>;
  compatibility
  proof
     z = Re z +(Im z)*<i> by COMPLEX1:29;
    hence thesis;
  end;
  correctness by XCMPLX_0:def 2;
end;

definition let a,b,c,z;
  redefine func Polynom (a,b,c,z) -> Element of COMPLEX;
  coherence by XCMPLX_0:def 2;
end;

theorem
   (a+c*<i>)*(b+d*<i>)=a*b-c*d+(a*d+b*c)*<i>;

theorem
   (x+y*<i>)^2= x^2-y^2 + 2*x*y*<i>;

canceled;

theorem Th4:
  a <> 0 & delta(a,b,c) >=0 & Polynom(a,b,c,z) = 0 implies
    z= (-b+sqrt delta(a,b,c))/(2*a) or
    z= (-b-sqrt delta(a,b,c))/(2*a) or
    z= -b/(2*a)
proof
A1: a = a+0*<i>;
   assume
A2:a <> 0 & delta(a,b,c) >= 0;
   assume
A3: Polynom(a,b,c,z) = 0;
A4:  z = Re z + (Im z)*<i> by COMPLEX1:29;
   set x=Re z;
   set y=Im z;
A5:[*x^2-y^2,2*x*y*] = x^2-y^2+(2*x*y)*<i> by COMPLEX1:27;
   then (a+0*<i>)*([*x^2-y^2,2*(x*y)*])+b*z+c = 0 by A3,A4;
   then 0 = Re a*Re (x^2-y^2+(2*x*y)*<i>) -Im a*Im (x^2-y^2+2*x*y*<i>)+
       (Re a*Im (x^2-y^2+2*x*y*<i>) +Re (x^2-y^2+2*x*y*<i>)*Im a)*<i>
           +b*z+c by A5,COMPLEX1:def 10
      .=[* Re a*Re (x^2-y^2+2*x*y*<i>)-Im a*
        Im (x^2-y^2+2*x*y*<i>),Re a*Im (x^2-y^2+2*x*y*<i>)
       +Re (x^2-y^2+2*x*y*<i>)*Im a *] +b*z+c by COMPLEX1:27
      .=[*a*Re (x^2-y^2+2*x*y*<i>)-Im a*
         Im (x^2-y^2+2*x*y*<i>),Re a*Im (x^2-y^2+2*x*y*<i>)
         +Re (x^2-y^2+2*x*y*<i>)*Im a *] +b*z+c by A1,COMPLEX1:28
      .=[*a*(x^2-y^2)-Im a*
         Im (x^2-y^2+2*x*y*<i>),Re a*Im (x^2-y^2+2*x*y*<i>)
         +Re (x^2-y^2+2*x*y*<i>)*Im a *] +b*z+c by COMPLEX1:28
      .=[*a*(x^2-y^2)-0*Im (x^2-y^2+2*x*y*<i>)
         ,Re a*Im (x^2-y^2+2*x*y*<i>)+Re (x^2-y^2+2*x*y*<i>)
         *Im a *] +b*z+c by A1,COMPLEX1:28
      .=[*a*(x^2-y^2)-0,Re a*(2*x*y)+Re (x^2-y^2+2*x*y*<i>)
         *Im a *] +b*z+c by COMPLEX1:28
      .=[*a*(x^2-y^2)-0,a*(2*x*y)+Re (x^2-y^2+2*x*y*<i>)
         *Im a *] +b*z+c by A1,COMPLEX1:28
      .=[*a*(x^2-y^2)-0,a*(2*x*y)+(x^2-y^2)
         *Im a *] +b*z+c by COMPLEX1:28
      .=[*a*(x^2-y^2)-0,a*(2*x*y)+(x^2-y^2)*0*] +b*z+c by A1,COMPLEX1:28;
   then A6: [*a*(x^2-y^2)-0*(2*x*y),0+a*(2*x*y)*]
           +(b+0*<i>)*(x+y*<i>)+c = 0 by COMPLEX1:29;
   then (a*(x^2-y^2)+(a*(2*x*y))*<i>)
           +(b*x+(b*y)*<i>)+(c+0*<i>) = 0 by COMPLEX1:27;
   then A7: a*(x^2-y^2)+b*x+c+(a*(2*x*y)+b*y)*<i> = 0;
then A8:a*(x^2-y^2)+b*x+c = 0 & (2*a*x+b)*y = 0 by COMPLEX1:12,28;
   per cases by A8,XCMPLX_1:6;
     suppose
A9:  y = 0;
     then Polynom(a,b,c,x)=0 by A6,COMPLEX1:27;
     then Re z=(-b+sqrt delta(a,b,c))/(2*a) & Im z = 0 or
     Re z= (-b-sqrt delta(a,b,c))/(2*a) & Im z = 0 by A2,A9,POLYEQ_1:5;
     then z=(-b+sqrt delta(a,b,c))/(2*a)+0*<i> or
        z= (-b-sqrt delta(a,b,c))/(2*a)+0*<i> by COMPLEX1:29;
     hence thesis;
   end;
   suppose
A10:    2*a*x+b = 0;
      2/(1/a) <> 0 by A2;
     then A11: x= (-b)/(2*a) by A10,XCMPLX_1:90;
     then a*((b/(2*a))^2-y^2)+b*(-b/(2*a))+c = 0 by A7,COMPLEX1:12,28;
     then (b/(2*a))^2-y^2 = (-(b*(-b/(2*a))+c))/a-0 by A2,XCMPLX_1:90;
     then (b/(2*a))^2- (-(b*(-b/(2*a))+c))/a= y^2-0;
     then y^2 = (b/(2*a))^2+c*a"-((b^2/(2*a))*a");
     then A12: y^2 *((2*a)^2)=(b^2/(2*a)^2+c*a"-((b^2/(2*a))*a"))*((2*a)^2)
                                by XCMPLX_1:77
              .=b^2/((2*a)^2)*((2*a)^2)+(c*a")*(2*a)^2
                          -(b^2*(2*a)"*a")*(2*a)^2;
  A13:    2*a <> 0 by A2;
  then A14:     (2*a)^2 > 0 by SQUARE_1:74;
 then A15:     y^2 *((2*a)^2)=b^2+(c*a")*(2*a)^2-(b^2*((2*a)"*a"))*(2*a)^2
          by A12,XCMPLX_1:88
              .=b^2+(c*a")*(2*a)^2-(b^2*((2*a)*a)")*(2*a)^2
                          by XCMPLX_1:205
              .=b^2+(c*a")*(2*a)^2-(b^2*(2*(a*a))")*(2*a)^2;
     set t = (b^2*(2*(a*a))")*(2*a)^2;
      t*((2*a)^2)" = b^2*(2*(a*a))"*(((2*a)^2)*(1/(2*a)^2));
     then t*((2*a)^2)" = b^2*(2*(a*a))"*1 by A14,XCMPLX_1:107;
     then t*((2*a)^2)"*2" = b^2*(2*a^2)"*2";
     then t*((2*a)^2)"*2" = b^2*((2*a^2)"*2");
     then t*((2*a)^2)"*2" = b^2*((2*(a^2*2))") by XCMPLX_1:205;
     then (t*2")/(2*a)^2*(2*a)^2 = b^2/(2*a)^2*(2*a)^2;
     then t*2" = b^2/(2*a)^2*(2*a)^2 by A14,XCMPLX_1:88;
     then A16:  t/2 = b^2 by A14,XCMPLX_1:88;
     set t=(c*a")*(2*a)^2;
      t=(c/a*a)*2*(2*a);
     then A17:   t=c*2*(2*a) by A2,XCMPLX_1:88;
       -delta(a,b,c) <= 0 by A2,XREAL_1:60;
     then (y *(2*a))^2 = 0 by A15,A16,A17,XREAL_1:65;
     then y *(2*a) = 0 by XCMPLX_1:6;
     then Re z=-b/(2*a) & Im z=0 by A11,A13,XCMPLX_1:6;
     then z = -b/(2*a)+0*<i> by COMPLEX1:29;
     hence thesis;
   end;
end;

theorem Th5:
  a <> 0 & delta(a,b,c) < 0 & Polynom(a,b,c,z) = 0 implies
    z= -b/(2*a)+(sqrt (-delta(a,b,c))/(2*a))*<i> or
    z= -b/(2*a)+(-sqrt (-delta(a,b,c))/(2*a))*<i>
proof
A1: [*a,0*]= a+0*<i> by COMPLEX1:27;
   assume
A2:a <> 0 & delta(a,b,c) < 0;
    now let z;
     assume
A3:  Polynom(a,b,c,z) = 0;
     set x=Re z;
     set y=Im z;
A4:   z = x + y*<i> by COMPLEX1:29;
A5:  [*x^2-y^2,2*x*y*] = x^2-y^2+(2*x*y)*<i> by COMPLEX1:27;
     then (a+0*<i>)*([*x^2-y^2,2*(x*y)*])+b*z+c = 0 by A3,A4;
         then Re a*Re (x^2-y^2+2*x*y*<i>)-Im a*Im [*x^2-y^2,2*x*y*]
         +(Re a*Im (x^2-y^2+2*x*y*<i>)+Re (x^2-y^2+2*x*y*<i>)
         *Im a)*<i>+b*z+c = 0 by A5,COMPLEX1:def 10;
         then [*Re a*Re (x^2-y^2+2*x*y*<i>)-Im a*Im [*x^2-y^2,2*x*y*]
         ,Re a*Im (x^2-y^2+2*x*y*<i>)+Re (x^2-y^2+2*x*y*<i>)
         *Im a*]+b*z+c = 0 by COMPLEX1:27;
         then [*a*Re (x^2-y^2+2*x*y*<i>)-Im a*Im (x^2-y^2+2*x*y*<i>)
         ,Re a*Im (x^2-y^2+2*x*y*<i>)+Re (x^2-y^2+2*x*y*<i>)
         *Im a*]+b*z+c = 0 by A1,A5,COMPLEX1:28;
         then [*a*(x^2-y^2)-Im a*Im (x^2-y^2+2*x*y*<i>)
         ,Re a*Im (x^2-y^2+2*x*y*<i>)+Re (x^2-y^2+2*x*y*<i>)
         *Im a*]+b*z+c = 0 by COMPLEX1:28;
         then [*a*(x^2-y^2)-0*Im (x^2-y^2+2*x*y*<i>)
         ,Re a*Im (x^2-y^2+2*x*y*<i>)+Re (x^2-y^2+2*x*y*<i>)
         *Im a*]+b*z+c = 0 by A1,COMPLEX1:28;
         then [*a*(x^2-y^2)-0,Re a*(2*x*y)+Re (x^2-y^2+2*x*y*<i>)
         *Im a*]+b*z+c = 0 by COMPLEX1:28;
         then [*a*(x^2-y^2)-0,a*(2*x*y)+Re (x^2-y^2+2*x*y*<i>)
         *Im a*]+b*z+c = 0 by A1,COMPLEX1:28;
         then [*a*(x^2-y^2)-0,a*(2*x*y)+(x^2-y^2)
         *Im a*]+b*z+c = 0 by COMPLEX1:28;
         then [*a*(x^2-y^2)-0,a*(2*x*y)+(x^2-y^2)
         *0*]+b*z+c = 0 by A1,COMPLEX1:28;
         then [*a*(x^2-y^2)-0*(2*x*y),(x^2-y^2)*0+a*(2*x*y)*]
           +(b+0*<i>)*(x+y*<i>)+c = 0  by COMPLEX1:29;
         then a*(x^2-y^2)+(a*(2*x*y))*<i>+(b*x+b*y*<i>)+c = 0
                                  by COMPLEX1:27;
         then A6: a*(x^2-y^2)+b*x+c+(a*(2*x*y)+b*y)*<i> = 0;
          then -a*y^2+((b*x+a*x^2)+c )+a*y^2= 0+a*y^2 by COMPLEX1:12,28;
         then A7:       a*x^2*a+b*x*a+c*a = a*y^2*a by XCMPLX_1:9;
          set t1= (x*a+b/2)^2;
          set t2=(-b^2+c*a*4)/4;
 A8:    t1>=0 by XREAL_1:65;
             0-delta(a,b,c)>0 by A2,REAL_2:173;
          then t2>0 by REAL_2:127;
          then 0+0<t1+t2 by A8,XREAL_1:10;
 then A9:    y<>0 by A7;
            (a*(2*x))*y+b*y = 0 by A6,COMPLEX1:12,28;
         then (a*2*x)*y = (-b)*y;
         then a*(2*x) = -b by A9,XCMPLX_1:5;
         then 2*x = (-b)/a by A2,XCMPLX_1:90;
         then x = 1/a*((-b)/2);
         then A10: x = (-b)/(2*a) by XCMPLX_1:104;
         then a*((b/(2*a))^2-y^2)+b*(-b/(2*a))+c = 0 by A6,COMPLEX1:12,28;
         then (b/(2*a))^2-y^2 = (-(b*(-b/(2*a))+c))/a-0
 by A2,XCMPLX_1:90;
         then (b/(2*a))^2- (-(b*(-b/(2*a))+c))/a= y^2-0;
        then y^2 = (b/(2*a))^2+c*a"-((b^2/(2*a))*a");
  then A11:   y^2 *((2*a)^2)=(b^2/(2*a)^2+c*a"-((b^2/(2*a))*a"))*((2*a)^2)
                      by XCMPLX_1:77
              .=b^2/((2*a)^2)*((2*a)^2)+(c*a")*(2*a)^2
                        -(b^2*(2*a)"*a")*(2*a)^2;
  A12: 2*a <> 0 by A2;
  then A13:  (2*a)^2 > 0 by SQUARE_1:74;
  then A14:   y^2 *((2*a)^2)=b^2+(c*a")*(2*a)^2-(b^2*((2*a)"*a"))*(2*a)^2
      by A11,XCMPLX_1:88
            .=b^2+(c*a")*(2*a)^2-(b^2*((2*a)*a)")*(2*a)^2
                         by XCMPLX_1:205
            .=b^2+(c*a")*(2*a)^2-(b^2*(2*(a*a))")*(2*a)^2;
           set t = (b^2*(2*(a*a))")*(2*a)^2;
            t*((2*a)^2)" = b^2*(2*(a*a))"*(((2*a)^2)*(1/(2*a)^2));
           then t*((2*a)^2)" = b^2*(2*(a*a))"*1 by A13,XCMPLX_1:107;
           then t*((2*a)^2)"*2" = b^2*(2*a^2)"*2";
           then t*((2*a)^2)"*2" = b^2*((2*a^2)"*2");
           then t*((2*a)^2)"*2" = b^2*((2*(a^2*2))") by XCMPLX_1:205;
           then (t*2")/(2*a)^2*(2*a)^2 = b^2/(2*a)^2*(2*a)^2;
            then t*2" = b^2/(2*a)^2*(2*a)^2 by A13,XCMPLX_1:88;
            then A15:         t/2 = b^2 by A13,XCMPLX_1:88;
            set t=(c*a")*(2*a)^2;
             t=(c/a*a)*2*(2*a);
            then A16:         t=c*2*(2*a) by A2,XCMPLX_1:88;
            -delta(a,b,c) > 0 by A2,XREAL_1:60;
           then (y *(2*a))^2  =(sqrt (-delta(a,b,c)))^2
             by A14,A15,A16,SQUARE_1:def 4;
           then ((y *(2*a))+(sqrt (-delta(a,b,c))))*
           ((y *(2*a))-(sqrt (-delta(a,b,c))))=0;
           then ((y *(2*a))+(sqrt (-delta(a,b,c))))=0 or
           ((y *(2*a))-(sqrt (-delta(a,b,c))))=0 by XCMPLX_1:6;
           then y =(-sqrt (-delta(a,b,c)))/(2*a) or
           y *(2*a)/(2*a)=sqrt (-delta(a,b,c))/(2*a) by A12,XCMPLX_1:90;
           then Re z=-b/(2*a) & Im z=sqrt (-delta(a,b,c))/(2*a)
           or  Re z= -b/(2*a) &  Im z= -sqrt (-delta(a,b,c))/(2*a)
 by A10,A12,XCMPLX_1:90;
     hence z= -b/(2*a)+(sqrt(-delta(a,b,c))/(2*a))*<i> or
          z= -b/(2*a)+(-sqrt (-delta(a,b,c))/(2*a))*<i> by COMPLEX1:29;
     end;
    hence thesis;
end;

theorem
   b <> 0 & (for z holds Polynom(0,b,c,z) = 0) implies z = -c/b
proof
A1: 0 = 0+0*<i>;
   assume
A2:b <> 0;
   assume
A3:(for z holds Polynom(0,b,c,z) = 0);
    now let z1;
      Polynom(0,b,c,z1) = 0 by A3;
     then b*(Re z1+(Im z1)*<i>)+c = 0 by COMPLEX1:29;
     then (b*Re z1-0+c+(b*Im z1+0)*<i>)= 0+0*<i>;
     then b*Re z1-0+c = Re 0 & b*Im z1= Im 0 by COMPLEX1:28;
     then b*(Re z1)-0+c = 0 & b*Im z1 = 0 by A1,COMPLEX1:28;
     then Re z1 =(-c)/b & Im z1 = 0 by A2,XCMPLX_1:6,90;
     then z1 = -c/b+0*<i> by COMPLEX1:29;
     hence z1 = -c/b;
   end;hence thesis;
end;

theorem
   for a,b,c being Real,z,z1,z2 being complex number st a <> 0 &
  for z being complex number holds Polynom(a,b,c,z) = Quard(a,z1,z2,z)
    holds b/a =-(z1+z2) & c/a = z1*z2
proof
   let a,b,c be Real,z,z1,z2 be complex number;
   assume
A1:a <> 0;
   assume
A2:for z being complex number holds Polynom(a,b,c,z) = Quard(a,z1,z2,z);
   consider z being Element of COMPLEX;
A3: Polynom(a,b,c,0) = Quard(a,z1,z2,0) by A2;
   Quard(a,z1,z2,1) = Polynom(a,b,c,1) by A2
            .=a+b+c;
  then a+b+c =a+a*(-(z1+z2))+c by A3;
  hence -(z1+z2) = b/a & c/a = z1*z2 by A1,A3,XCMPLX_1:204;
end;

definition let z be complex number;
  func z^3 -> Element of COMPLEX equals
      z^2*z;
  correctness by XCMPLX_0:def 2;
end;

Lm2:  for z be complex number holds
    z|^2= z*z
proof
    let z be complex number;
       z|^(1+1) = (z|^1)*z by NEWTON:11
              .= z*z by NEWTON:10;
   hence thesis;
end;

definition let a,b,c,d,z be complex number;
  redefine func Polynom(a,b,c,d,z) equals
        a*z^3+b*z^2+c*z+d;
  compatibility
   proof let x be set;
      Polynom(a,b,c,d,z) = a*(z |^(2+1))+ b*z^2 +c*z +d
       .= a*((z |^2)*z)+ b*z^2 +c*z +d by NEWTON:11;
    hence x = Polynom(a,b,c,d,z) iff x = a*z^3+b*z^2+c*z+d by Lm2;
   end;
end;

theorem
   0^3 = 0;

theorem
   1^3 = 1;

theorem
   (-1)^3=-1;

theorem Th11:
  Re z^3 = (Re z)|^3 - 3*Re z*(Im z)^2 &
    Im z^3 = -(Im z)|^ 3+3*(Re z)^2*Im z
proof
A1: [*Re z,Im z*] = Re z + (Im z)*<i> by COMPLEX1:27
          .= z by COMPLEX1:29;
         set x = Re z;
         set y = Im z;
A2: [*x,y*] = x+y*<i> by COMPLEX1:27;
A3: [*x|^ 3-3*x*y^2 ,-y|^ 3+3*x^2*y*] = x|^ 3-3*x*y^2 +(-y|^ 3+3*x^2*y)*<i>
                     by COMPLEX1:27;
       z^3
         = Re(x^2-y^2+(2*(x*y))*<i>) *Re (x+y*<i>)-Im(x^2-y^2+(2*(x*y))*<i>)
            *Im (x+y*<i>)+(Re(x^2-y^2+(2*(x*y))*<i>) *Im (x+y*<i>)+
            Re (x+y*<i>)*Im (x^2-y^2+(2*(x*y))*<i>))*<i>
                    by A1,A2,COMPLEX1:def 10
         .= [*Re(x^2-y^2+(2*(x*y))*<i>) *Re (x+y*<i>)-Im(x^2-y^2+(2*(x*y))*<i>)
            *Im (x+y*<i>),Re(x^2-y^2+(2*(x*y))*<i>) *Im (x+y*<i>)+
            Re (x+y*<i>)*Im (x^2-y^2+(2*(x*y))*<i>)*] by COMPLEX1:27
         .= [*(x^2-y^2) *Re (x+y*<i>)-Im(x^2-y^2+(2*(x*y))*<i>)
            *Im (x+y*<i>),Re(x^2-y^2+(2*(x*y))*<i>) *Im (x+y*<i>)+
            Re (x+y*<i>)*Im (x^2-y^2+(2*(x*y))*<i>)*] by COMPLEX1:28
         .= [*(x^2-y^2) *Re (x+y*<i>)-(2*(x*y))
            *Im (x+y*<i>),Re(x^2-y^2+(2*(x*y))*<i>) *Im (x+y*<i>)+
            Re (x+y*<i>)*Im (x^2-y^2+(2*(x*y))*<i>)*] by COMPLEX1:28
         .= [*(x^2-y^2) *Re (x+y*<i>)-(2*(x*y))
            *Im (x+y*<i>),(x^2-y^2) *Im (x+y*<i>)+
            Re (x+y*<i>)*Im (x^2-y^2+(2*(x*y))*<i>)*] by COMPLEX1:28
         .= [*(x^2-y^2) *Re (x+y*<i>)-(2*(x*y))*Im (x+y*<i>)
           ,(x^2-y^2) *Im (x+y*<i>)+Re (x+y*<i>)*(2*(x*y))*] by COMPLEX1:28
         .= [*(x^2-y^2) *x-(2*(x*y))*Im (x+y*<i>)
           ,(x^2-y^2) *Im (x+y*<i>)+Re (x+y*<i>)*(2*(x*y))*] by COMPLEX1:28
         .= [*(x^2-y^2) *x-(2*(x*y))*y
           ,(x^2-y^2) *Im (x+y*<i>)+Re (x+y*<i>)*(2*(x*y))*] by COMPLEX1:28
         .= [*(x^2-y^2) *x-(2*(x*y))*y
           ,(x^2-y^2) *y+Re (x+y*<i>)*(2*(x*y))*] by COMPLEX1:28
         .= [*(x^2*x-y^2*x+0*x)-2*(x*y)*y,(x^2-y^2+0)*y+x*(2*(x*y))*] by
COMPLEX1:28
         .= [*(x|^1*x*x-y^2*x)-2*(x*y)*y,(x^2*y-y^2*y)+x*(2*(x*y))*]
                                by NEWTON:10
         .= [*(x|^ (1+1)*x-y^2*x)-2*(x*y)*y,(x^2*y-y^2*y)+x*(2*(x*y))*]
                                by NEWTON:11
         .= [*(x|^(2+1)-y^2*x)-2*(x*y)*y,(x^2*y-y^2*y)+x*(2*(x*y))*]
                                by NEWTON:11
         .= [*(x|^ 3-y^2*x)-2*(x*y)*y,(x^2*y-y|^ 1*y*y)+x*(2*(x*y))*]
                                by NEWTON:10
         .= [*(x|^ 3-y^2*x)-2*(x*y)*y,(x^2*y-y|^ (1+1)*y)+x*(2*(x*y))*]
                                by NEWTON:11
         .= [*(x|^ 3-y^2*x)-2*(x*(y*y)),(x^2*y-y|^ (2+1))+x*(2*(x*y))*] by
NEWTON:11
         .= [*x|^ 3-3*x*y^2 ,-y|^ 3+3*x^2*y*];
    hence thesis by A3,COMPLEX1:28;
end;

theorem
  (for z being complex number holds Polynom(a,b,c,d,z) = Polynom(a',b',c',d',z)
)
   implies a = a' & b = b' & c = c' & d = d'
proof
    assume
A1: for z being complex number holds
    Polynom(a,b,c,d,z) = Polynom(a',b',c',d',z);
    then A2: Polynom(a,b,c,d,0) = Polynom(a',b',c',d',0);
A3: Polynom(a,b,c,d,1) =  Polynom(a',b',c',d',1) by A1;
A4: Polynom(a,b,c,d,-1) =  Polynom(a',b',c',d',-1) by A1;
A5: Polynom(a,b,c,d,2) =  Polynom(a',b',c',d',2) by A1;
   hence a = a' by A2,A3,A4;
   thus b = b' by A2,A3,A4;
   thus c = c' by A2,A3,A4,A5;
   thus thesis by A2;
end;

theorem
   (z+h)^3 = z^3+(3*h)*z^2+(3*h^2)*z+h^3;

theorem
   (z*h)^3 = z^3*h^3;

theorem
   b<>0 & delta(b,c,d)>=0 & Polynom(0,b,c,d,z)=0 implies
   z = (-c+sqrt delta(b,c,d))/(2*b) or
   z = (-c-sqrt delta(b,c,d))/(2*b) or
   z = -c/(2*b)
proof
    assume
A1: b<>0 & delta(b,c,d)>=0 & Polynom(0,b,c,d,z)=0;
    then Polynom(b,c,d,z)=0;
    hence thesis by A1,Th4;
end;

theorem
   b<>0 & delta(b,c,d)<0 & Polynom(0,b,c,d,z)=0 implies
    z = -c/(2*b)+(sqrt (-delta(b,c,d))/(2*b))*<i> or
    z = -c/(2*b)+(-sqrt (-delta(b,c,d))/(2*b))*<i>
proof
    assume
A1: b<>0 & delta(b,c,d)<0 & Polynom(0,b,c,d,z)=0;
    then Polynom(b,c,d,z)=0;
    hence thesis by A1,Th5;
end;

theorem
   a<>0 & 4*a*c <= 0 & Polynom(a,0,c,0,z)=0 implies
   z = (sqrt -4*a*c)/(2*a) or
   z = (-sqrt -4*a*c)/(2*a) or
   z = 0
proof
    assume
A1: a<>0 & 4*a*c <= 0 & Polynom(a,0,c,0,z)=0;
    then (a*z^2+c)*z = 0;
then A2:   Polynom(a,0,c,z) = 0 or z = 0 by XCMPLX_1:6;
     -4*a*c >= -0 by A1,XREAL_1:26;
    then z = (-0+sqrt delta(a,0,c))/(2*a)
    or z = (-0-sqrt delta(a,0,c))/(2*a) or z = 0
    or z = 0/(2*a) by A1,A2,Th4;
    hence thesis;
end;

theorem
   a<>0 & delta(a,b,c)>=0 & Polynom(a,b,c,0,z)=0 implies
   z = (-b+sqrt delta(a,b,c))/(2*a) or
   z = (-b-sqrt delta(a,b,c))/(2*a) or
   z = -b/(2*a) or
   z = 0
proof
    assume
A1: a<>0 & delta(a,b,c)>=0 & Polynom(a,b,c,0,z)=0;
    then (a*z^2+b*z+c)*z = 0;
    then Polynom(a,b,c,z) = 0 or z = 0 by XCMPLX_1:6;
    hence thesis by A1,Th4;
end;

theorem
   a<>0 & delta(a,b,c)<0 & Polynom(a,b,c,0,z)=0 implies
  z= -b/(2*a)+(sqrt (-delta(a,b,c))/(2*a))*<i> or
  z= -b/(2*a)+(-sqrt (-delta(a,b,c))/(2*a))*<i> or
  z = 0
proof
    assume
A1: a<>0 & delta(a,b,c)<0 & Polynom(a,b,c,0,z)=0;
    then (a*z^2+b*z+c)*z = 0;
    then Polynom(a,b,c,z) = 0 or z = 0 by XCMPLX_1:6;
    hence thesis by A1,Th5;
end;

theorem Th20:
  y^2 = a implies y = sqrt a or y = -sqrt a
proof
    assume
A1: y^2 = a;
then A2:   Polynom(1,0,-a,y) =0;
A3:  a >= 0 by A1,XREAL_1:65;
     delta(1,0,-a) = 4*a;
    then delta(1,0,-a) >= 0 by A3,REAL_2:121;
    then y=(-0+sqrt delta(1,0,-a))/(2*1) or
    y=(-0-sqrt delta(1,0,-a))/(2*1) by A2,POLYEQ_1:5;
    then y=(sqrt (4*a))/2 or y=(0-sqrt (4*a))/2;
    then y= sqrt a * 2 /2 or
    y=(-(2 * sqrt a))/2 by A3,SQUARE_1:85,97;
    hence y= sqrt a or y=-sqrt a;
end;

theorem
   a<>0 & Im z = 0 & Polynom(a,0,c,d,z)=0 implies
    (for u,v st Re z = u+v & 3*v*u+c/a=0 holds
       z = 3-root(-d/(2*a)+sqrt(d^2/(4*a^2)+(c/(3*a)) |^ 3))
          +3-root(-d/(2*a)-sqrt(d^2/(4*a^2)+(c/(3*a)) |^ 3))
    or z = 3-root(-d/(2*a)+sqrt(d^2/(4*a^2)+(c/(3*a)) |^ 3))
          +3-root(-d/(2*a)+sqrt(d^2/(4*a^2)+(c/(3*a)) |^ 3))
    or z = 3-root(-d/(2*a)-sqrt(d^2/(4*a^2)+(c/(3*a)) |^ 3))
          +3-root(-d/(2*a)-sqrt(d^2/(4*a^2)+(c/(3*a)) |^ 3)) )
proof
   assume
A1:a <> 0;
   assume
A2: Im z = 0 & Polynom(a,0,c,d,z)=0;
   let u,v;
   assume
A3:Re z=u+v & 3*v*u+c/a=0;
   set x=Re z;
   set y=Im z;
   set p=c/a;
A4:[*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]
       = x|^ 3 - 3*x*y^2+(-y|^ 3+3*x^2*y)*<i> by COMPLEX1:27;
A5: a = a+0*<i>;
A6: Re z+(Im z)*<i> = [*Re z,Im z*] by COMPLEX1:27;
A7:z  = Re z+(Im z)*<i> by COMPLEX1:29
           .= x by A2;
    0 = a*(Re z^3+(Im z^3)*<i>)+0*z^2+c*z+d by A2,COMPLEX1:29
         .= a*([*Re z^3,Im z^3*])+0*z^2+c*z+d by COMPLEX1:27
         .= a*[*(Re z)|^ 3 - 3*Re z*(Im z)^2,Im z^3*]
               +0*z^2+c*z+d by Th11
         .= a*[*(Re z)|^ 3 - 3*Re z*(Im z)^2,-(Im z)|^ 3+3*(Re z)^2
            *Im z*]+c*z+d by Th11
         .= a*([*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*])
               +c*(Re z+(Im z)*<i>)+d by COMPLEX1:29
         .= (a+0*<i>)*([*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*])
               +c*[*Re z,Im z*]+d by COMPLEX1:27
         .=Re a *Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]-Im a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]+(Re a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]+
          Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]*Im a)*<i>
               +c*[*Re z,Im z*]+d by COMPLEX1:def 10
         .=[*Re a *Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]-Im a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*],Re a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]+
          Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]*Im a *]
               +c*[*Re z,Im z*]+d by COMPLEX1:27
         .=[*Re a *(x|^ 3 - 3*x*y^2)-Im a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*],Re a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]+
          Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]*Im a *]
               +c*[*Re z,Im z*]+d by A4,COMPLEX1:28
         .=[*Re a *(x|^ 3 - 3*x*y^2)-Im a*(-y|^ 3+3*x^2*y),Re a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]+
          Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]*Im a *]
               +c*[*Re z,Im z*]+d by A4,COMPLEX1:28
         .=[*Re a *(x|^ 3 - 3*x*y^2)-Im a*(-y|^ 3+3*x^2*y),Re a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]+
          (x|^ 3 - 3*x*y^2)*Im a *] +c*[*Re z,Im z*]+d by A4,COMPLEX1:28
         .=[*Re a *(x|^ 3 - 3*x*y^2)-Im a
           *(-y|^ 3+3*x^2*y),Re a*(-y|^ 3+3*x^2*y)+
          (x|^ 3 - 3*x*y^2)*Im a *]
               +c*[*Re z,Im z*]+d by A4,COMPLEX1:28
         .=[*a*(x|^ 3 - 3*x*y^2)-Im a
           *(-y|^ 3+3*x^2*y),Re a*(-y|^ 3+3*x^2*y)+
          (x|^ 3 - 3*x*y^2)*Im a *]
               +c*[*Re z,Im z*]+d by A5,COMPLEX1:28
         .=[*a*(x|^ 3 - 3*x*y^2)-0*(-y|^ 3+3*x^2*y),
          Re a*(-y|^ 3+3*x^2*y)+(x|^ 3 - 3*x*y^2)*Im a *]
               +c*[*Re z,Im z*]+d by A5,COMPLEX1:28
         .=[*a*(x|^ 3 - 3*x*y^2)-0,a*(-y|^ 3+3*x^2*y)
           +(x|^ 3 - 3*x*y^2)*Im a *]
               +c*[*Re z,Im z*]+d by A5,COMPLEX1:28
         .=[*a*(x|^ 3 - 3*x*y^2)-0,a*(-y|^ 3+3*x^2*y)
           +(x|^ 3 - 3*x*y^2)*0 *]+c*[*Re z,Im z*]+d by A5,COMPLEX1:28
         .= a*(x|^ 3 - 3*x*0)+(a*(-0|^ 3+3*x^2*0))*<i> +c*x+d
                      by A2,A6,COMPLEX1:27
         .= a*(x|^ 3 - 3*x*0)+c*x+d+(a*(-0|^ 3+0) +0+0)*<i>
         .= a*(x|^ 3 - 0)+c*x+d+(a*(-0))*<i> by NEWTON:16
         .= [*a*x|^ 3 +c*x+d,0*] by COMPLEX1:27;
            then a"*(a*x|^ 3+c*x+d) = a"*0 by ARYTM_0:def 7;
            then x|^ 3*(a/a)+a"*c*x+a"*d = 0;
 then A8:      Polynom(1,0,c/a,d/a,x) = 0 by A1,XCMPLX_1:89;
A9:   -(d/a)/2 = -1/2*(d/a)
                 .= -d/(a*2) by XCMPLX_1:104;
A10:   (d/a)^2/4 =1/4*(d^2/a^2) by XCMPLX_1:77
                 .=d^2/(a^2*4) by XCMPLX_1:104;
   (c/a)/3 =1/3*(c/a) .=c/(a*3) by XCMPLX_1:104;
  hence thesis by A3,A7,A8,A9,A10,POLYEQ_1:19;
end;

theorem
   a<>0 & Im z <> 0 & Polynom(a,0,c,d,z)=0 implies
    (for u,v st Re z = u+v & 3*v*u+c/(4*a)=0 & c/a>=0 holds
          z=3-root(d/(16*a)+sqrt((d/(16*a))^2+(c/(12*a)) |^ 3))
             +3-root(d/(16*a)-sqrt((d/(16*a))^2+(c/(12*a)) |^ 3))+
             sqrt(3*(3-root(d/(16*a)+sqrt((d/(16*a))^2+(c/(12*a)) |^ 3))
             + 3-root(d/(16*a)-sqrt((d/(16*a))^2+(c/(12*a)) |^ 3)))^2+c/a)*<i>
       or z=3-root(d/(16*a)+sqrt((d/(16*a))^2+(c/(12*a)) |^ 3))
             +3-root(d/(16*a)-sqrt((d/(16*a))^2+(c/(12*a)) |^ 3))
             -sqrt(3*(3-root(d/(16*a)+sqrt((d/(16*a))^2+(c/(12*a)) |^ 3))
             +3-root(d/(16*a)-sqrt((d/(16*a))^2+(c/(12*a)) |^ 3)))^2+c/a)*<i>
       or z=2*(3-root(d/(16*a)+sqrt((d/(16*a))^2+(c/(12*a)) |^ 3)))+
             sqrt(3*(2*(3-root(d/(16*a)+sqrt((d/(16*a))^2
             +(c/(12*a)) |^ 3))))^2+c/a)*<i>
       or z=2*(3-root(d/(16*a)+sqrt((d/(16*a))^2+(c/(12*a)) |^ 3)))
             -sqrt(3*(2*(3-root(d/(16*a)+sqrt((d/(16*a))^2
             +(c/(12*a)) |^ 3))))^2+c/a)*<i>
       or z=2*(3-root(d/(16*a)-sqrt((d/(16*a))^2+(c/(12*a)) |^ 3)))+
             sqrt(3*(2*(3-root(d/(16*a)-sqrt((d/(16*a))^2
             +(c/(12*a)) |^ 3))))^2+c/a)*<i>
       or z=2*(3-root(d/(16*a)-sqrt((d/(16*a))^2+(c/(12*a)) |^ 3)))
             -sqrt(3*(2*(3-root(d/(16*a)-sqrt((d/(16*a))^2
             +(c/(12*a)) |^ 3))))^2+c/a)*<i>)
proof
   assume
A1:a <> 0;
   assume
A2: Im z <> 0 & Polynom(a,0,c,d,z)=0;
   let u,v;
   assume
A3:Re z=u+v & 3*v*u+c/(4*a)=0 &  c/a>=0;
   set x=Re z;
   set y=Im z;
   set p=c/(4*a);
A4:[*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]
       = x|^ 3 - 3*x*y^2+(-y|^ 3+3*x^2*y)*<i> by COMPLEX1:27;
A5: a = a+0*<i>;
A6: Re z+(Im z)*<i> = [*Re z,Im z*] by COMPLEX1:27;
 A7:   z = x+y*<i> by COMPLEX1:29;
 A8:   0 = a*(Re z^3+(Im z^3)*<i>)+0*z^2+c*z+d by A2,COMPLEX1:29
         .= a*([*Re z^3,Im z^3*])+0*z^2+c*z+d
                        by COMPLEX1:27
         .= a*[*(Re z)|^ 3 - 3*Re z*(Im z)^2,Im z^3*]
            +c*z+d by Th11
         .= a*[*(Re z)|^ 3 - 3*Re z*(Im z)^2,-(Im z)|^ 3+3*(Re z)^2
            *Im z*]+c*z+d by Th11
         .= a*([*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*])
               +c*(Re z+(Im z)*<i>)+d by COMPLEX1:29
         .= (a+0*<i>)*([*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*])
               +c*[*Re z,Im z*]+d by COMPLEX1:27
         .=Re a *Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]-Im a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]+(Re a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]+
          Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]*Im a)*<i>
               +c*[*Re z,Im z*]+d by COMPLEX1:def 10
         .=[*Re a *Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]-Im a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*],Re a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]+
          Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]*Im a *]
               +c*[*Re z,Im z*]+d by COMPLEX1:27
         .=[*Re a *(x|^ 3 - 3*x*y^2)-Im a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*],Re a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]+
          Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]*Im a *]
               +c*[*Re z,Im z*]+d by A4,COMPLEX1:28
         .=[*Re a *(x|^ 3 - 3*x*y^2)-Im a
           *(-y|^ 3+3*x^2*y),Re a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]+
          Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]*Im a *]
               +c*[*Re z,Im z*]+d by A4,COMPLEX1:28
         .=[*Re a *(x|^ 3 - 3*x*y^2)-Im a
           *(-y|^ 3+3*x^2*y),Re a
           *Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]+
          (x|^ 3 - 3*x*y^2)*Im a *]
               +c*[*Re z,Im z*]+d by A4,COMPLEX1:28
         .=[*Re a *(x|^ 3 - 3*x*y^2)-Im a
           *(-y|^ 3+3*x^2*y),Re a*(-y|^ 3+3*x^2*y)+
          (x|^ 3 - 3*x*y^2)*Im a *]
               +c*[*Re z,Im z*]+d by A4,COMPLEX1:28
         .=[*a*(x|^ 3 - 3*x*y^2)-Im a
           *(-y|^ 3+3*x^2*y),Re a*(-y|^ 3+3*x^2*y)+
          (x|^ 3 - 3*x*y^2)*Im a *]
               +c*[*Re z,Im z*]+d by A5,COMPLEX1:28
         .=[*a*(x|^ 3 - 3*x*y^2)-0*(-y|^ 3+3*x^2*y),
          Re a*(-y|^ 3+3*x^2*y)+(x|^ 3 - 3*x*y^2)*Im a *]
               +c*[*Re z,Im z*]+d by A5,COMPLEX1:28
         .=[*a*(x|^ 3 - 3*x*y^2)-0,a*(-y|^ 3+3*x^2*y)
           +(x|^ 3 - 3*x*y^2)*Im a *]
               +c*[*Re z,Im z*]+d by A5,COMPLEX1:28
         .=[*a*(x|^ 3 - 3*x*y^2)-0,a*(-y|^ 3+3*x^2*y)
           +(x|^ 3 - 3*x*y^2)*0 *]+c*[*Re z,Im z*]+d by A5,COMPLEX1:28
         .= a*(x|^ 3 - 3*x*y^2)+(a*(-y|^ 3+3*x^2*y))*<i>
            +(c*Re z-0*Im z+(c*Im z+Re z*0)*<i>)+d by A6,COMPLEX1:27
         .= a*(x|^ 3 - 3*x*y^2)+c*x+d+(a*(-y|^ 3+3*x^2*y) +c*y+0)*<i>;
then A9:        a*(-y|^ 3+3*x^2*y) +c*y+0 = 0 by COMPLEX1:12,28;
             a*(-y|^(2+1)+3*x^2*y)+c*y = 0 by A8,COMPLEX1:12,28;
          then a*(-y|^2*y+3*x^2*y)+c*y = 0 by NEWTON:11;
          then (a*(-y|^2+3*x^2)+c+0)*y = 0;
            then (a*(y|^2))+(-a*(y|^2)+(a*(3*x^2)+c))=(a*(y|^2))+0
                      by A2,XCMPLX_1:6;
            then y|^(1+1) = (a*(3*x^2)+c)/a by A1,XCMPLX_1:90;
            then y|^1*y = (a*(3*x^2)+c)/a by NEWTON:11;
            then A10:          y^2 = (3*x^2)*a/a+c/a+0/a by NEWTON:10;
 then A11:      y^2 = 3*x^2+c/a+0*a" by A1,XCMPLX_1:90;
             a*(x|^ 3 - 3*x*(3*x^2+c/a)+0)+c*x+d = 0
                             by A1,A8,A9,A10,XCMPLX_1:90;
            then 0 =a*x|^ 3 - a*(9*(x*x^2)+(3*x)*(c/a))+c*x+d
            .=a*x|^ 3 - a*(9*(x|^1*x*x)+(3*x)*(c/a))+c*x+d by NEWTON:10
            .=a*x|^ 3 - a*(9*(x|^(1+1)*x)+(3*x)*(c/a))+c*x+d by NEWTON:11
            .=a*x|^ 3 - a*(9*(x|^(2+1))+(3*x)*(c/a)+0)+c*x+d by NEWTON:11
            .=a*x|^ 3 - (a*(9*(x|^3))+(3*x)*(c*(a/a)))+c*x+d
            .=a*x|^ 3 - (a*(9*(x|^3))+(3*x)*c)+c*x+d by A1,XCMPLX_1:89
            .=(-8*a)*x|^3+(-2*c)*x+d;
             then (-1)*0=(8*a)*x|^3+(2*c)*x+(-d);
 then A12:       0=x|^3*((8*a)/(8*a))+(8*a)"*((2*c)*x)+(8*a)"*(-d);
                8*a <> 0 by A1;
         then 0=1*x|^3+0*x^2+((2*c)/(8*a))*x+((8*a)"*(-d)) by A12,XCMPLX_1:89;
         then 0=1*x|^3+0*x^2+((2/8*c)/a)*x+((-d)/(8*a)) by XCMPLX_1:84;
         then 0=1*x|^3+0*x^2+(1/a*(c/4))*x+((-d)/(8*a));
 then A13:      Polynom(1,0,c/(4*a),-d/(8*a),x) = 0 by XCMPLX_1:104;
             set pp = c/(4*a);
             set q = -d/(8*a);
               -q/2=1/2*(d/(8*a));
             then A14: -q/2=d/(a*8*2) by XCMPLX_1:104;
A15:         q^2/4=(1/2*(d/(8*a)))^2;
 A16:      x = 3-root(-q/2+sqrt(q^2/4+(pp/3) |^ 3)) +
                3-root(-q/2-sqrt(q^2/4+(pp/3) |^ 3)) or
            x = 3-root(-q/2+sqrt(q^2/4+(pp/3) |^ 3)) +
                3-root(-q/2+sqrt(q^2/4+(pp/3) |^ 3)) or
            x = 3-root(-q/2-sqrt(q^2/4+(pp/3) |^ 3)) +
                3-root(-q/2-sqrt(q^2/4+(pp/3) |^ 3)) by A3,A13,POLYEQ_1:19;
            set m = 3-root(-q/2+sqrt(q^2/4+(pp/3) |^ 3));
            set n = 3-root(-q/2-sqrt(q^2/4+(pp/3) |^ 3));
            A17: z=m+n+sqrt(3*(m + n)^2+c/a)*<i>
             or z=m+n-sqrt(3*(m + n)^2+c/a)*<i>
             or z=2*m+sqrt(3*(2*m)^2+c/a)*<i>
             or z=2*m-sqrt(3*(2*m)^2+c/a)*<i>
             or z=2*n+sqrt(3*(2*n )^2+c/a)*<i>
             or z=2*n-sqrt(3*(2*n )^2+c/a)*<i>
          proof
            now per cases by A16;
             case x = m + n;
              hence z=m+n+sqrt(3*(m + n )^2+c/a)*<i>
               or z=m+n+(-sqrt(3*(m + n )^2+c/a))*<i> by A7,A11,Th20;
            end;
            case x = 2*m;
             hence z=2*m+sqrt(3*(2*m)^2+c/a)*<i>
             or z=2*m+(-sqrt(3*(2*m)^2+c/a))*<i> by A7,A11,Th20;
            end;
            case x = 2*n;
             hence z=2*n+sqrt(3*(2*n )^2+c/a)*<i>
             or z=2*n+(-sqrt(3*(2*n )^2+c/a))*<i> by A7,A11,Th20;
            end;
             end;
             hence thesis;
           end;
     (c/(4*a))/3=1/3*(c/(4*a))
          .=c/(a*4*3) by XCMPLX_1:104
          .=c/(a*(4*3));
    hence thesis by A14,A15,A17;
 end;

theorem
   a<>0 & Polynom(a,b,c,d,z)=0 & Im z=0 implies
    (for u,v,x1 st x1=Re z + b/(3*a) &
       Re z=u+v-b/(3*a) & 3*u*v+(3*a*c-b^2)/(3*a^2)=0 holds
              z =3-root(-(b/(3*a)) |^ 3-(3*a*d-b*c)/(6*a^2)
                 +sqrt((2*((b/(3*a)) |^ 3)+(3*a*d-b*c)/(3*a^2))^2/4
                 +((3*a*c-b^2)/(9*a^2)) |^ 3)) +
                3-root(-(b/(3*a)) |^ 3-(3*a*d-b*c)/(6*a^2)
                 -sqrt((2*((b/(3*a)) |^ 3)+(3*a*d-b*c)/(3*a^2))^2/4
                 +((3*a*c-b^2)/(9*a^2)) |^ 3))-b/(3*a)+0*<i> or
            z =3-root(-(b/(3*a)) |^ 3-(3*a*d-b*c)/(6*a^2)
                 +sqrt((2*((b/(3*a)) |^ 3)+(3*a*d-b*c)/(3*a^2))^2/4
                 +((3*a*c-b^2)/(9*a^2)) |^ 3)) +
                3-root(-(b/(3*a)) |^ 3-(3*a*d-b*c)/(6*a^2)
                 +sqrt((2*((b/(3*a)) |^ 3)+(3*a*d-b*c)/(3*a^2))^2/4
                 +((3*a*c-b^2)/(9*a^2)) |^ 3))-b/(3*a)+0*<i> or
            z =3-root(-(b/(3*a)) |^ 3-(3*a*d-b*c)/(6*a^2)
                 -sqrt((2*((b/(3*a)) |^ 3)+(3*a*d-b*c)/(3*a^2))^2/4
                 +((3*a*c-b^2)/(9*a^2)) |^ 3)) +
                3-root(-(b/(3*a)) |^ 3-(3*a*d-b*c)/(6*a^2)
                 -sqrt((2*((b/(3*a)) |^ 3)+(3*a*d-b*c)/(3*a^2))^2/4
                 +((3*a*c-b^2)/(9*a^2)) |^ 3))-b/(3*a)+0*<i> )
proof
   assume
A1:a<>0;
   assume
A2:Polynom(a,b,c,d,z)=0 & Im z=0;
   let u,v,x1;
   assume
A3:x1=Re z +b/(3*a) & Re z=u+v-b/(3*a) & 3*u*v+(3*a*c-b^2)/(3*a^2)=0;
   set x=Re z;
   set y=Im z;
A4: [*Re z,Im z*] = x+y*<i> by COMPLEX1:27;
A5:[*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]
       = x|^ 3 - 3*x*y^2+(-y|^ 3+3*x^2*y)*<i> by COMPLEX1:27;
A6: a = a+0*<i>;
A7:  z = x + y*<i> by COMPLEX1:29;
 0 = a*(Re z^3+(Im z^3)*<i>)+b*z^2+c*z+d by A2,COMPLEX1:29
         .= a*([*Re z^3,Im z^3*])+b*z^2+c*z+d by COMPLEX1:27
         .= a*[*(Re z)|^ 3 - 3*Re z*(Im z)^2,Im z^3*]
               +b*z^2+c*z+d by Th11
         .= a*[*(Re z)|^ 3 - 3*Re z*(Im z)^2,-(Im z)|^ 3+3*(Re z)^2
            *Im z*]+b*z^2+c*z+d by Th11
         .= a*([*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*])
               +b*z^2+c*(Re z+(Im z)*<i>)+d by COMPLEX1:29
         .= (a+0*<i>)*([*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*])
               +b*z^2+c*[*Re z,Im z*]+d by COMPLEX1:27
         .= Re a*Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]
            -Im a*Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]+
           (Re a*Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]
           +Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]*Im a)*<i>
               +b*z^2+c*[*Re z,Im z*]+d by COMPLEX1:def 10
         .= [*Re a*Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]
            -Im a*Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*],
           Re a*Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]
           +Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]*Im a *]
               +b*z^2+c*[*Re z,Im z*]+d by COMPLEX1:27
         .= [*Re a*Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]
            -0*Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*],
           Re a*Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]
           +Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]*Im a *]
               +b*z^2+c*[*Re z,Im z*]+d by A6,COMPLEX1:28
         .= [*Re a*Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]
            -0,Re a*Im [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]
           +Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]*0 *]
               +b*z^2+c*[*Re z,Im z*]+d by A6,COMPLEX1:28
         .= [*Re a*Re [*x|^ 3 - 3*x*y^2,-y|^ 3+3*x^2*y*]
            -0,Re a*(-y|^ 3+3*x^2*y)+0 *]
               +b*z^2+c*[*Re z,Im z*]+d by A5,COMPLEX1:28
         .= [*Re a*(x|^ 3 - 3*x*y^2)-0,Re a*(-y|^ 3+3*x^2*y)*]
               +b*z^2+c*[*Re z,Im z*]+d by A5,COMPLEX1:28
         .= [*a*(x|^ 3 - 3*x*y^2)-0,Re a*(-y|^ 3+3*x^2*y)*]
               +b*z^2+c*[*Re z,Im z*]+d by A6,COMPLEX1:28
         .= [*a*(x|^ 3 - 3*x*y^2),a*(-y|^ 3+3*x^2*y)*]
           +b*z^2+(c*Re z-0*Im z+(c*Im z+Re z*0)*<i>)+d by A4,A6,COMPLEX1:28
         .= [*a*(x|^ 3 - 3*x*y^2),a*(-y|^ 3+3*x^2*y)*]
           +b*(x^2-y^2+2*x*y*<i>)+[*c*x,c*y*]+d by A7,COMPLEX1:27
         .= a*(x|^ 3 - 3*x*y^2)+(a*(-y|^ 3+3*x^2*y))*<i>
           +b*(x^2-y^2+2*x*y*<i>)+[*c*x,c*y*]+d by COMPLEX1:27
         .= a*(x|^ 3 - 3*x*y^2)+(a*(-y|^ 3+3*x^2*y))*<i>
           +b*(x^2-y^2+(2*x*y)*<i>)+(c*x+(c*y)*<i>)+d by COMPLEX1:27;
         then 0 = a*(x|^ 3 - 3*x*0)+b*(x^2-0)+c*x+d
              +(a*(-0|^ 3+0)+b*0+0)*<i> by A2
         .= [*a*(x|^ 3 - 3*x*0)+b*(x^2-0)+c*x+d
                 ,a*(-0|^ 3+0)+b*0+0*] by COMPLEX1:27
         .= [*a*x|^ 3+b*x^2+c*x+d,a*0+0*] by NEWTON:16;
 then A8:      Polynom(a,b,c,d,x) = 0 by ARYTM_0:def 7;
            set a'=b/a;
            set b'=c/a;
            set c'=d/a;
            set h=-b/(3*a);
A9:        a'=b/a & b'=c/a & c'=d/a & h=-b/(3*a);
             set  p = (3*a*c-b^2)/(3*a^2);
             set  q = 2*((b/(3*a)) |^ 3)+(3*a*d-b*c)/(3*a^2);
                p/3=1/3*((3*a*c-b^2)/(3*a^2));
             then A10: p/3=(3*a*c-b^2)/(a^2*3*3) by XCMPLX_1:104;
 A11:       -q/2=-((b/(3*a)) |^ 3+1/2*((3*a*d-b*c)/(3*a^2)))
            .=-((b/(3*a)) |^ 3+(3*a*d-b*c)/(a^2*3*2)) by XCMPLX_1:104
            .=-(b/(3*a)) |^ 3-(3*a*d-b*c)/(6*a^2);
         Polynom(1,0,p,q,x1) = 0 by A1,A3,A8,A9,POLYEQ_1:16;
  then x1 = 3-root(-q/2+sqrt(q^2/4+(p/3) |^ 3)) +
                 3-root(-q/2-sqrt(q^2/4+(p/3) |^ 3)) or
            x1 = 3-root(-q/2+sqrt(q^2/4+(p/3) |^ 3)) +
                 3-root(-q/2+sqrt(q^2/4+(p/3) |^ 3)) or
            x1 = 3-root(-q/2-sqrt(q^2/4+(p/3) |^ 3)) +
                 3-root(-q/2-sqrt(q^2/4+(p/3) |^ 3)) by A3,POLYEQ_1:19;
  hence thesis by A2,A3,A10,A11,COMPLEX1:29;
end;

theorem Th24:
  z1 <> 0 & Polynom(z1,z2,z) = 0 implies z = -(z2/z1)
proof
  assume z1 <> 0 & Polynom(z1,z2,z) = 0;
   then z = (-z2)*z1" by XCMPLX_1:204;
  hence z = -(z2/z1);
end;

theorem
   z2 <> 0 implies not ex z st Polynom(0,z2,z) = 0;

begin

theorem
   (for z holds Polynom(z1,z2,z3,z) = Polynom(s1,s2,s3,z)) implies
    z1 = s1 & z2 = s2 & z3 = s3
proof
   assume
A1:for z holds Polynom(z1,z2,z3,z) = Polynom(s1,s2,s3,z);
   then A2:  Polynom(z1,z2,z3,0c) = Polynom(s1,s2,s3,0c);
A3:  Polynom(z1,z2,z3,1r) = Polynom(s1,s2,s3,1r) by A1;
        Polynom(z1,z2,z3,-1r) = Polynom(s1,s2,s3,-1r) by A1;
 hence thesis by A2,A3,COMPLEX1:def 6,def 7;
end;

theorem
   (-a+sqrt (a^2+b^2))/2 >=0 & (a+sqrt (a^2+b^2))/2>=0
proof
 A1: a^2>=0 & b^2>=0 & a^2>=a^2 by XREAL_1:65;
 then A2: a^2+b^2>=0+0 & a^2+b^2>=0+a^2 by XREAL_1:9;
 then A3: sqrt(a^2+b^2)>=sqrt(a^2) by A1,SQUARE_1:94;
          A4:  sqrt (a^2+b^2)>=0 by A2,SQUARE_1:def 4;
          per cases;
          suppose a>=0;
          then A5:  sqrt(a^2+b^2)>=a & a+sqrt (a^2+b^2)>=0+0
         by A3,A4,SQUARE_1:89,XREAL_1:9;
           then sqrt(a^2+b^2)-a>=a-a by XREAL_1:11;
           hence thesis by A5,REAL_2:125;
          end;
          suppose
          A6: a<0;
          then -a > 0 by XREAL_1:60;
          then A7:  sqrt(a^2+b^2) >= -a & -a+sqrt (a^2+b^2)>=0+0
  by A3,A4,A6,SQUARE_1:90,XREAL_1:10;
         then sqrt(a^2+b^2)-(-a) >= -a-(-a) by XREAL_1:11;
         hence thesis by A7,REAL_2:125;
       end;
end;

theorem Th28:
  z^2 = s & Im s >= 0 implies
   z=sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
       (sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i> or
   z=-sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
       (-sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i>
proof
      assume
 A1: z^2=s;
       assume
 A2: Im s >=0;
       set a=Re s;
       set b=Im s;
       set u=Re z;
       set v=Im z;
A3: [*u,v*] = u+v*<i> & [*a,b*] = a+b*<i> by COMPLEX1:27;
     z = u+v*<i> by COMPLEX1:29;
    then u^2-v^2+2*u*v*<i> =[*a,b*] by A1,A3,COMPLEX1:29;
 then A4: u^2-v^2=a & 2*u*v=b by A3,COMPLEX1:163;
 then A5:   (u^2+v^2)^2=a^2+b^2;
          u^2>=0 & v^2>=0 by XREAL_1:65;
       then u^2+v^2>=0+0 by XREAL_1:9;
 then A6: u^2+v^2=sqrt (a^2+b^2) by A5,SQUARE_1:89;
                  -2*(u*v)<=-0 by A2,A4,XREAL_1:26;
                then (-2)*(u*v)<=0;
                then u*v>=0 by REAL_2:129;
                then u<=0 & v<=0 or u>=0 & v>=0 by SQUARE_1:25;
           then -u=sqrt ((a+sqrt (a^2+b^2))/2) & -v=sqrt ((-a+sqrt (a^2+b^2))/
2)  or
           u=sqrt ((a+sqrt (a^2+b^2))/2) & v=sqrt ((-a+sqrt (a^2+b^2))/2)
                    by A4,A6,SQUARE_1:89,90;
           hence thesis by COMPLEX1:29;
end;

theorem
   z^2 = s & Im s = 0 & Re s > 0 implies z=sqrt(Re s) or z=-sqrt(Re s)
proof
   assume
A1:z^2=s;
   assume
A2:Im s =0 & Re s >0;
   then z=sqrt (( Re s+sqrt ((Re s)^2+0))/2)+
       (sqrt ((-Re s+sqrt ((Re s)^2+0))/2))*<i> or
    z=-sqrt (( Re s+sqrt ((Re s)^2+0))/2)+
       (-sqrt ((-Re s+sqrt ((Re s)^2+0))/2))*<i> by A1,Th28,SQUARE_1:60;
    then z=[*sqrt (( Re s+sqrt ((Re s)^2+0))/2),
       sqrt ((-Re s+sqrt ((Re s)^2+0))/2)*] or
    z=[*-sqrt (( Re s+sqrt ((Re s)^2+0))/2),
       -sqrt ((-Re s+sqrt ((Re s)^2+0))/2)*] by COMPLEX1:27;
    then z=[*sqrt (( Re s+Re s)/2),
       sqrt ((-Re s+sqrt ((Re s)^2+0))/2)*] or
    z=[*-sqrt (( Re s+sqrt ((Re s)^2+0))/2),
       -sqrt ((-Re s+sqrt ((Re s)^2+0))/2)*] by A2,SQUARE_1:89;
    then z=[*sqrt (( Re s+Re s)/2), sqrt ((-Re s+Re s)/2)*] or
    z=[*-sqrt (( Re s+Re s)/2),
       -sqrt ((-Re s+sqrt ((Re s)^2+0))/2)*] by A2,SQUARE_1:89;
    then z=[*sqrt (Re s), sqrt ((0+(Re s-Re s))/2)*] or
    z=[*-sqrt (Re s),-sqrt ((0+(Re s-Re s))/2)*] by A2,SQUARE_1:89;
    hence thesis by ARYTM_0:def 7,SQUARE_1:82;
end;

theorem
   z^2 = s & Im s = 0 & Re s < 0 implies
    z= sqrt (-Re s)*<i> or z= -sqrt (-Re s)*<i>
proof
    assume
A1: z^2=s;
    assume
A2: Im s =0 & Re s < 0;
    then z=sqrt (( Re s+sqrt ((Re s)^2+0))/2)+
       (sqrt ((-Re s+sqrt ((Re s)^2+0))/2))*<i> or
    z=-sqrt (( Re s+sqrt ((Re s)^2+0))/2)+
       (-sqrt ((-Re s+sqrt ((Re s)^2+0))/2))*<i> by A1,Th28,SQUARE_1:60;
    then z=sqrt (( Re s+-Re s)/2)+
       sqrt ((-Re s+sqrt ((Re s)^2+0))/2)*<i> or
    z=-sqrt (( Re s+sqrt ((Re s)^2+0))/2)+
       (-sqrt ((-Re s+sqrt ((Re s)^2+0))/2))*<i> by A2,SQUARE_1:90;
    then z=sqrt (( Re s+-Re s)/2)+ sqrt ((-Re s+-Re s)/2)*<i>   or
    z=-sqrt (( Re s+-Re s)/2)+(-sqrt ((-Re s+sqrt ((Re s)^2+0))/2))*<i>
                       by A2,SQUARE_1:90;
    then z=sqrt (( Re s+-Re s)/2)+ sqrt ((-Re s+-Re s)/2)*<i>   or
    z=-sqrt (( Re s+-Re s)/2)+(-sqrt ((-Re s+-Re s)/2))*<i>
                           by A2,SQUARE_1:90;
   hence thesis by SQUARE_1:82;
end;

theorem
   z^2 = s & Im s <0 implies
   z= sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
       (-sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i> or
   z=-sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
        (sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i>
proof
    assume
A1: z^2=s;
    assume
A2: Im s<0;
    set a=Re s;
       set b=Im s;
       set u=Re z;
       set v=Im z;
A3: [*u,v*] = u+v*<i> & [*a,b*] = a+b*<i> by COMPLEX1:27;
   then A4:  z=[*u,v*] & s=[*a,b*] & b <0 by A2,COMPLEX1:29;
    z = u + v*<i> by COMPLEX1:29;
   then A5:  z^2 = u^2-v^2 + 2*u*v*<i>;
 then A6: u^2-v^2=a & 2*u*v=b by A1,A3,A4,COMPLEX1:163;
 then A7: sqrt (u^2+v^2)^2=sqrt (a^2+b^2);
          u^2>=0 & v^2>=0 by XREAL_1:65;
       then u^2+v^2>=0+0 by XREAL_1:9;
 then A8: u^2+v^2=sqrt (a^2+b^2) by A7,SQUARE_1:89;
                 2*(u*v)<0 by A1,A3,A4,A5,COMPLEX1:163;
                then u*v<0 by XREAL_1:135;
                then u<0 & v>0 or u>0 & v<0 by XREAL_1:135;
          then -u=sqrt ((a+sqrt (a^2+b^2))/2) & v=sqrt ((-a+sqrt (a^2+b^2))/2)
or
           u=sqrt ((a+sqrt (a^2+b^2))/2) & -v=sqrt ((-a+sqrt (a^2+b^2))/2)
                    by A6,A8,SQUARE_1:89,90;
  hence thesis by COMPLEX1:29;
end;

theorem Th32:
  z^2 = s implies
   z=sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
       (sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i> or
   z=-sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
       (-sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i> or
   z= sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
       (-sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i> or
   z=-sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
        (sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i>
proof
       assume
 A1: z^2=s;
       set a=Re s;
       set b=Im s;
       set u=Re z;
       set v=Im z;
A2: [*u,v*] = u+v*<i> & [*a,b*] = a+b*<i> by COMPLEX1:27;
   then A3:  z=[*u,v*] & s=[*a,b*] by COMPLEX1:29;
  z = u + v*<i> by COMPLEX1:29;
 then A4: z^2 = u^2-v^2 + 2*u*v*<i>;
 then A5: u^2-v^2=a & 2*u*v=b by A1,A2,A3,COMPLEX1:163;
 then A6: sqrt (u^2+v^2)^2=sqrt (a^2+b^2);
          u^2>=0 & v^2>=0 by XREAL_1:65;
       then u^2+v^2>=0+0 by XREAL_1:9;
 then A7: u^2+v^2=sqrt (a^2+b^2) by A6,SQUARE_1:89;
       per cases;
        suppose b>=0;
        then -2*(u*v)<=-0 by A5,XREAL_1:26;
        then (-2)*(u*v)<=0;
        then u*v>=0 by REAL_2:129;
        then u<=0 & v<=0 or u>=0 & v>=0 by SQUARE_1:25;
        then -u=sqrt ((a+sqrt (a^2+b^2))/2) & -v=sqrt ((-a+sqrt (a^2+b^2))/
2)  or
           u=sqrt ((a+sqrt (a^2+b^2))/2) & v=sqrt ((-a+sqrt (a^2+b^2))/2)
                    by A5,A7,SQUARE_1:89,90;
           hence thesis by COMPLEX1:29;
           end;
           suppose b<0;
            then 2*(u*v)<0 by A1,A2,A3,A4,COMPLEX1:163;
            then u*v<0 by XREAL_1:135;
            then u<0 & v>0 or u>0 & v<0 by XREAL_1:135;
    then -u=sqrt ((a+sqrt (a^2+b^2))/2) & v=sqrt ((-a+sqrt (a^2+b^2))/2) or
           u=sqrt ((a+sqrt (a^2+b^2))/2) & -v=sqrt ((-a+sqrt (a^2+b^2))/2)
                    by A5,A7,SQUARE_1:89,90;
          hence thesis by COMPLEX1:29;
          end;
end;

theorem
   Polynom(0,0,0,z) = 0;

theorem Th34:
  z1<>0 & Polynom(z1,0,0,z) = 0 implies z=0
proof
   assume z1<>0 & Polynom(z1,0,0,z) = 0;
   then z^2=0 by XCMPLX_1:6;
   hence z=0 by XCMPLX_1:6;
end;

theorem
   z1<>0 & Polynom(z1,z2,0,z)=0 implies z=-(z2/z1) or z=0
proof
   assume
A1:z1<>0 & Polynom(z1,z2,0,z)=0;
   then 0 =(z1*z+z2)*z;
   then Polynom(z1,z2,z) = 0 or z=0 by XCMPLX_1:6;
   hence z=-(z2/z1) or z=0 by A1,Th24;
end;

theorem Th36:
  z1<>0 & Polynom(z1,0,z3,z)=0 implies
    (for s st s=-(z3/z1) holds
       z= sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
            (sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i> or
       z=-sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
           (-sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i> or
       z= sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
           (-sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i> or
       z=-sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
            (sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i>)
proof
    assume
A1: z1<>0 & Polynom(z1,0,z3,z)=0;
    let s;
    assume
A2: s=-(z3/z1);
     z^2=(-z3)/z1 by A1,XCMPLX_1:90;
    hence thesis by A2,Th32;
end;

theorem Th37:
  z1<>0 & Polynom(z1,z2,z3,z)=0 implies
    (for h,t st h=(z2/(2*z1))^2-z3/z1 & t=z2/(2*z1) holds
       z= sqrt (( Re h+sqrt ((Re h)^2+(Im h)^2))/2)+
                (sqrt ((-Re h+sqrt ((Re h)^2+(Im h)^2))/2))*<i>-t or
       z=-sqrt (( Re h+sqrt ((Re h)^2+(Im h)^2))/2)+
               (-sqrt ((-Re h+sqrt ((Re h)^2+(Im h)^2))/2))*<i>-t or
       z= sqrt (( Re h+sqrt ((Re h)^2+(Im h)^2))/2)+
               (-sqrt ((-Re h+sqrt ((Re h)^2+(Im h)^2))/2))*<i>-t or
       z=-sqrt (( Re h+sqrt ((Re h)^2+(Im h)^2))/2)+
                (sqrt ((-Re h+sqrt ((Re h)^2+(Im h)^2))/2))*<i>-t)
proof
       assume
 A1: z1<>0 & Polynom(z1,z2,z3,z)=0;
       let h,t;
       assume
 A2: h=(z2/(2*z1))^2-z3/z1 & t=z2/(2*z1);
       (z1*z^2+z2*z+z3)/z1=0 by A1;
      then (z^2*z1)/z1+(z2*z)/z1+z3/z1=0;
 then z^2+(z2/z1)*z+z3/z1=0 by A1,XCMPLX_1:90;
      then z^2+2*z2/(2*z1)*z+z3/z1=0 by XCMPLX_1:92;
      then (z+(z2/(2*z1)))^2-((z2/(2*z1))^2-z3/z1)=0;
      then z+t-t= sqrt (( Re h+sqrt ((Re h)^2+(Im h)^2))/2)+
             (sqrt ((-Re h+sqrt ((Re h)^2+(Im h)^2))/2))*<i>-t   or
      z+t-t=-sqrt (( Re h+sqrt ((Re h)^2+(Im h)^2))/2)+
            (-sqrt ((-Re h+sqrt ((Re h)^2+(Im h)^2))/2))*<i>-t   or
      z+t-t= sqrt (( Re h+sqrt ((Re h)^2+(Im h)^2))/2)+
            (-sqrt ((-Re h+sqrt ((Re h)^2+(Im h)^2))/2))*<i>-t   or
      z+t-t=-sqrt (( Re h+sqrt ((Re h)^2+(Im h)^2))/2)+
        (sqrt ((-Re h+sqrt ((Re h)^2+(Im h)^2))/2))*<i>-t by A2,Th32;
    hence thesis;
end;

definition let z1,z2,z3,z4,z be complex number;
 redefine  canceled;

func Polynom(z1,z2,z3,z4,z) equals
     z1*z^3 +z2*z^2+z3*z+z4;
  compatibility;
end;

canceled;

theorem
   z|^ 3=z*z*z & z|^ 3=z^2*z & z|^ 3 = z^3
proof
  z|^ 3 = z|^(2+1)
      .= z|^(1+1)*z by NEWTON:11
      .= z|^1*z*z by NEWTON:11
      .= z*z*z by NEWTON:10;
 hence thesis;
end;

theorem
   z1<>0 & Polynom(z1,z2,0,0,z)=0 implies z=-z2/z1 or z=0
proof
    assume
A1: z1<>0 & Polynom(z1,z2,0,0,z)=0;
    then 0 =(z1*z +z2)*z^2;
    then Polynom(z1,z2,z)=0 or 1*z^2+0*z+0=0 by XCMPLX_1:6;
    then z=-(z2/z1) or Polynom(1r,0,0,z)=0
      by A1,Th24;
    hence z=-(z2/z1) or z=0 by Th34,COMPLEX1:def 7;
end;

theorem
   z1<>0 & Polynom(z1,0,z3,0,z)=0 implies
     (for s st s=-(z3/z1) holds
     z=0 or
     z=sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
          (sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i> or
     z=-sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
         (-sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i> or
     z= sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
         (-sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i> or
     z=-sqrt (( Re s+sqrt ((Re s)^2+(Im s)^2))/2)+
          (sqrt ((-Re s+sqrt ((Re s)^2+(Im s)^2))/2))*<i>)
proof
    assume
A1: z1<>0 & Polynom(z1,0,z3,0,z)=0;
    let s;
    assume
A2: s=-(z3/z1);
     0 =(z1*z^2+z3)*z by A1;
    then Polynom(z1,0,z3,z)=0 or z=0 by XCMPLX_1:6;
    hence thesis by A1,A2,Th36;
end;

theorem
   z1<>0 & Polynom(z1,z2,z3,0,z)=0 implies
    (for s,h,t st s=-(z3/z1) & h=(z2/(2*z1))^2-z3/z1 & t=z2/(2*z1) holds
      z=0 or
      z= sqrt (( Re h+sqrt ((Re h)^2+(Im h)^2))/2)+
           (sqrt ((-Re h+sqrt ((Re h)^2+(Im h)^2))/2))*<i>-t   or
      z=-sqrt (( Re h+sqrt ((Re h)^2+(Im h)^2))/2)+
          (-sqrt ((-Re h+sqrt ((Re h)^2+(Im h)^2))/2))*<i>-t   or
      z= sqrt (( Re h+sqrt ((Re h)^2+(Im h)^2))/2)+
          (-sqrt ((-Re h+sqrt ((Re h)^2+(Im h)^2))/2))*<i>-t   or
      z=-sqrt (( Re h+sqrt ((Re h)^2+(Im h)^2))/2)+
           (sqrt ((-Re h+sqrt ((Re h)^2+(Im h)^2))/2))*<i>-t)
proof
    assume
A1: z1<>0 & Polynom(z1,z2,z3,0,z)=0;
    let s,h,t;
    assume
A2: s=-(z3/z1) & h=(z2/(2*z1))^2-z3/z1 & t=z2/(2*z1);
     0 =Polynom(z1,z2,z3,z)*z by A1;
    then z=0 or Polynom(z1,z2,z3,z)=0 by XCMPLX_1:6;
    hence thesis by A1,A2,Th37;
end;

theorem
   (s-(1/3)*z1)^2 =s^2+(-(2/3))*z1*s+(1/9)*z1^2;

theorem
   z=s-(1/3)*z1 implies
    z^3 =s^3-z1*s^2+(1/3)*z1^2*s-(1/27)*z1^3;

canceled;

theorem Th46:
  for z being Element of COMPLEX holds
    [* |.z.|*cos Arg z, |.z.|*sin Arg z *]
    = [*|.z.|,0*]*[*cos Arg z,sin Arg z *]
proof
    let z be Element of COMPLEX;
A1:  [*|.z.|,0*] = |.z.|+0*<i> &
    [*cos Arg z,sin Arg z *] = cos Arg z+(sin Arg z)*<i> by COMPLEX1:27;
     [* |.z.|*cos Arg z, |.z.|*sin Arg z *]
     =|.z.|*cos Arg z-0*sin Arg z+(|.z.|*sin Arg z+(cos Arg z)*0)*<i>
                     by COMPLEX1:27
    .= [*|.z.|,0*]*[*cos Arg z,sin Arg z *] by A1;
    hence thesis;
end;

canceled 2;

theorem
   for z be complex number holds z|^2= z*z by Lm2;

theorem Th50:
  for n be Nat st n>0 holds
    0 |^ n = 0
proof
    let n be Nat;
    assume n>0;
       then n >= 0+1 by NAT_1:13;
 then A1: n-1 >= 0 by XREAL_1:21;
     n=n-1+1
       .= n-'1+1 by A1,BINARITH:def 3;
    hence 0 |^ n = (0 |^ (n-'1))*0 by NEWTON:11
           .= 0;
end;

theorem Th51:
  for x,y be Element of COMPLEX
    for n be Element of NAT holds
     (x*y) |^ n = x |^ n * y |^ n
proof
    let x,y be Element of COMPLEX;
    defpred P[Element of NAT] means (x*y) |^ $1 = x |^ $1 * y |^ $1;
       (x*y) |^ 0 =1 by NEWTON:9
                .=(x |^ 0)*1 by NEWTON:9
                .=(x |^ 0)*(y |^ 0) by NEWTON:9;
then A1:  P[0];
 A2: now let n be Element of NAT;
     assume P[n];
      then (x*y)|^ (n+1)= x|^ n * y|^ n * (y*x) by NEWTON:11
        .= x|^ n * (y|^ n * y*x)
        .= x|^ n * (y|^ (n+1)*x) by NEWTON:11
        .= x|^ n * x *(y|^ (n+1))
        .= x|^ (n+1) * y|^ (n+1) by NEWTON:11;
     hence P[n+1];
   end;
   thus for n be Element of NAT holds P[n] from NAT_1:sch 1(A1,A2);
end;

theorem Th52:
  for x be Real st x > 0
    for n be Element of NAT holds [*x,0*]|^ n = x to_power n
proof
  let x be Real;
  assume
A1: x > 0;
 defpred P[Element of NAT] means [*x,0*]|^ $1 = x to_power $1;
    [*x,0*]|^ 0 = 1+0*<i> by NEWTON:9
       .= x to_power 0+0*<i> by POWER:29;
 then A2: P[0];
 A3: now let n be Element of NAT;
A4:  [*x to_power n,0*] = x to_power n+0*<i> &
    [*x,0*] = x+0*<i> by COMPLEX1:27;
     assume P[n];
     then [*x,0*]|^ (n+1) = (x to_power n)*x by A4,NEWTON:11
        .= (x to_power n)*(x to_power 1) by POWER:30
        .= x to_power (n+1) by A1,POWER:32;
     hence P[n+1];
    end;
   thus for n be Element of NAT holds P[n] from NAT_1:sch 1(A2,A3);
end;

theorem Th53:
  for x being Real,
      n be Element of NAT holds
    [*cos x,sin x*] |^ n = cos (n*x) + sin (n*x)*<i>
proof
  let x be Real;
 defpred P[Element of NAT] means
  [*cos x,sin x*]|^ $1 = cos ($1*x)+sin ($1*x)*<i>;
 A1: P[0] by NEWTON:9,SIN_COS:34;
 A2: now let n be Element of NAT;
    set a'=Re [*cos(n*x),sin(n*x)*];
    set b'=Im [*cos(n*x),sin(n*x)*];
    set c'=Re [*cos x,sin x*];
    set d'=Im [*cos x,sin x*];
A3: [*cos(n*x),sin(n*x)*] = cos(n*x)+sin(n*x)*<i> &
   [*cos x,sin x*] = cos x+(sin x)*<i> by COMPLEX1:27;
     assume P[n];
     then [*cos x,sin x*] |^ (n+1)
         = (cos(n*x)+sin(n*x)*<i>)*(cos x+(sin x)*<i>) by A3,NEWTON:11
        .= cos(n*x)*cos x - sin(n*x)*sin x+
           (cos(n*x)*sin x+cos x*sin(n*x))*<i>
        .= cos(n*x+x)+(cos(n*x)*sin x+cos x*sin(n*x))*<i> by SIN_COS:80
        .= cos((n+1)*x)+sin((n+1)*x)*<i> by SIN_COS:80;
      hence P[n+1];
    end;
    thus for n be Element of NAT holds P[n] from NAT_1:sch 1(A1,A2);
 end;

theorem
   for z being Element of COMPLEX
  for n being Element of NAT st z <> 0 or n > 0 holds
    z|^ n = (|.z.| to_power n)*cos (n*Arg z) +
            (|.z.| to_power n)*sin (n*Arg z)*<i>
proof
    let z be Element of COMPLEX;
    let n be Element of NAT;
    assume
 A1: z <> 0 or n > 0;
      per cases by A1;
     suppose z <> 0;
 then A2: |.z.| > 0 by COMPLEX1:133;
A3:  [*|.z.|,0*] = |.z.|+0*<i> &
    [*cos Arg z,sin Arg z*] = cos Arg z+(sin Arg z)*<i> by COMPLEX1:27;
        set x=Arg z;
A4: [*cos (n*x),sin (n*x)*] = cos (n*x)+sin (n*x)*<i> by COMPLEX1:27;
         z|^ n =[*|.z.|*cos Arg z-0*sin Arg z,|.z.|*sin Arg z+cos Arg z*0*]|^ n
                                            by COMPLEX2:19
         .=(|.z.|*cos Arg z-0*sin Arg z+(|.z.|*sin Arg z+cos Arg z*0)*<i>)|^ n
                                             by COMPLEX1:27
         .=([*|.z.|,0*]*[*cos Arg z,sin Arg z*])|^ n by A3
         .= [*|.z.|,0*]|^ n*[*cos Arg z,sin Arg z*]|^ n by Th51;
       hence z|^ n= [*|.z.|,0*]|^ n*[*cos (n*x),sin (n*x)*] by A4,Th53
       .= (|.z.| to_power n)*[*cos (n*Arg z),sin (n*Arg z)*]
                                     by A2,Th52
       .= (|.z.| to_power n+0*<i>)*(cos (n*Arg z)+(sin (n*Arg z))*<i>)
                                     by COMPLEX1:27
       .= (|.z.| to_power n)*cos(n*Arg z)+(|.z.| to_power n)*sin(n*Arg z)*<i>;
     end;
     suppose
 A5: z=0 & n > 0;
     then 0=|.z.| to_power n by COMPLEX1:130,POWER:def 2;
       hence z|^ n= (|.z.| to_power n)*cos(n*Arg z)+
        (|.z.| to_power n)*sin(n*Arg z)*<i> by A5,Th50;
      end;
end;

theorem Th55:
  for n,k be Element of NAT,x be Real st n <> 0 holds
    [*cos((x+2*PI*k)/n),sin((x+2*PI*k)/n)*]|^ n = cos x + (sin x)*<i>
  proof
    let n,k be Element of NAT,x be Real;
    assume
A1: n <> 0;
       thus [*cos((x+2*PI*k)/n),sin((x+2*PI*k)/n)*]|^ n
        = cos (n*((x+2*PI*k)/n))+sin (n*((x+2*PI*k)/n))*<i> by Th53
       .= cos(x+2*PI*k)+sin(n*((x+2*PI*k)/n))*<i> by A1,XCMPLX_1:88
       .= cos(x+2*PI*k)+sin(x+2*PI*k)*<i> by A1,XCMPLX_1:88
       .= cos.(x+2*PI*k)+sin(x+2*PI*k)*<i> by SIN_COS:def 23
       .= cos.(x+2*PI*k)+(sin.(x+2*PI*k))*<i> by SIN_COS:def 21
       .= cos.(x+2*PI*k)+(sin.x)*<i> by SIN_COS2:10
       .= cos.x + (sin.x)*<i> by SIN_COS2:11
       .= cos.x + (sin x)*<i> by SIN_COS:def 21
       .= cos x + (sin x)*<i> by SIN_COS:def 23;
   end;

theorem Th56:
  for z be complex number
  for n,k be Element of NAT st n <> 0 holds
    z = [*(n-root |.z.|)*cos((Arg z+2*PI*k)/n),
          (n-root |.z.|)*sin((Arg z+2*PI*k)/n)*]|^ n
   proof
    let z be complex number;
    let n,k be Element of NAT;
    assume
A1: n <> 0;
 then A2: n > 0;
 then A3: n >= 0+1 by NAT_1:13;
    per cases;
      suppose z <> 0;
 then A4: |.z.| > 0 by COMPLEX1:133;
       then n-root |.z.| = n -Root |.z.| by A3,POWER:def 1;
 then A5: n-root |.z.| > 0 by A3,A4,PREPOWER:def 3;
     thus [*(n-root |.z.|)*cos((Arg z+2*PI*k)/n),
            (n-root |.z.|)*sin((Arg z+2*PI*k)/n)*]|^ n
        =((n-root |.z.|)*cos((Arg z+2*PI*k)/n)-
          0*sin((Arg z+2*PI*k)/n)+((n-root |.z.|)*sin((Arg z+2*PI*k)/n)+
          cos((Arg z+2*PI*k)/n)*0)*<i>)|^ n by COMPLEX1:27
       .= ((n-root |.z.|+0*<i>)*
          (cos((Arg z+2*PI*k)/n)+(sin((Arg z+2*PI*k)/n))*<i>))|^ n
       .= ((n-root |.z.|+0*<i>)*
          [*cos((Arg z+2*PI*k)/n),sin((Arg z+2*PI*k)/n)*])|^ n by COMPLEX1:27
       .= ([*n-root |.z.|,0*]*
          [*cos((Arg z+2*PI*k)/n),sin((Arg z+2*PI*k)/n)*])|^ n by COMPLEX1:27
       .= ([*n-root |.z.|,0*]|^ n)
         *([*cos((Arg z+2*PI*k)/n),sin((Arg z+2*PI*k)/n)*]|^ n) by Th51
       .= ([*n-root |.z.|,0*]|^ n)*(cos Arg z+(sin Arg z)*<i>) by A1,Th55
       .= ((n-root |.z.|) to_power n)*(cos Arg z+(sin Arg z)*<i>)
                                    by A5,Th52
       .= ((n-root |.z.|) |^ n)*(cos Arg z+(sin Arg z)*<i>) by A3,POWER:47
       .= |.z.|*(cos Arg z+(sin Arg z)*<i>) by A1,A4,COMPTRIG:17
       .= |.z.|*cos Arg z-0*sin Arg z+(|.z.|*sin Arg z+0*cos Arg z)*<i>
       .= [*|.z.|*cos Arg z-0*sin Arg z,|.z.|*sin Arg z+0*cos Arg z*]
                                     by COMPLEX1:27
       .= z by COMPLEX2:19;
     end;
     suppose
 A6: z = 0;
     hence [*(n-root |.z.|)*cos((Arg z+2*PI*k)/n),
            (n-root |.z.|)*sin((Arg z+2*PI*k)/n)*]|^ n
        =([*0*cos((Arg z+2*PI*k)/n),
           (n-root 0)*sin((Arg z+2*PI*k)/n)*]|^ n) by A3,COMPLEX1:130,POWER:6
       .= [*0,0*sin((Arg z+2*PI*k)/n)*]|^ n by A3,POWER:6
       .= z by A2,A6,Lm1,Th50;
     end;
   end;

theorem
   for z be Element of COMPLEX,
      n be non empty Element of NAT,
      k be Element of NAT holds
   [*(n-root |.z.|)*cos((Arg z+2*PI*k)/n),
     (n-root |.z.|)*sin((Arg z+2*PI*k)/n)*] is CRoot of n,z
proof
  let z be Element of COMPLEX,
      n be non empty Element of NAT,k be Element of NAT;
   [*(n-root |.z.|)*cos((Arg z+2*PI*k)/n),
  (n-root |.z.|)*sin((Arg z+2*PI*k)/n)*]|^ n=z by Th56;
  hence thesis by COMPTRIG:def 2;
end;

theorem
   for z be complex number, v be CRoot of 1,z holds
    v = z
  proof
    let z be complex number, v be CRoot of 1,z;
     v|^ 1 = z by COMPTRIG:def 2;
    hence v = z by NEWTON:10;
  end;

theorem
   for n be non empty Nat, v be CRoot of n,0 holds v = 0
  proof
    let n be non empty Nat, v be CRoot of n,0;
    defpred P[Nat] means v|^ $1 = 0;
A1: ex n be non empty Nat st P[n]
    proof
      take n;
      thus thesis by COMPTRIG:def 2;
    end;
A2: now let k be non empty Nat;
      assume that
A3:   k <> 1 and
A4:   P[k];
      consider t be Nat such that
A5:   k = t+1 by NAT_1:6;
      reconsider t as non empty Nat by A3,A5;
      take t;
      thus t < k by A5,NAT_1:13;
       v|^ k = (v|^ t)*v by A5,NEWTON:11;
      then v|^ t = 0 or v = 0 by A4,XCMPLX_1:6;
      hence P[t] by Th50;
    end;
     P[1] from COMPTRIG:sch 1(A1,A2);
    hence thesis by NEWTON:10;
  end;

theorem
   for n be non empty Element of NAT, z be complex number
   for v be CRoot of n,z st v = 0 holds z = 0
proof
  let n be non empty Element of NAT, z be complex number;
  let v be CRoot of n,z;
  assume v = 0;
  then 0|^ n = z by COMPTRIG:def 2;
  hence thesis by Th50;
end;

theorem
   for n be non empty Element of NAT, k be Element of NAT holds
    cos(2*PI*k/n) + sin(2*PI*k/n)*<i> is CRoot of n,1
proof
   let n be non empty Element of NAT,k be Element of NAT;
A1: [*cos(2*PI*k/n),sin(2*PI*k/n)*] = cos(2*PI*k/n)+sin(2*PI*k/n)*<i>
               by COMPLEX1:27;
    [*cos(2*PI*k/n),sin(2*PI*k/n)*]|^n
       =cos(n*((2*PI*k)/n))+sin(n*((2*PI*k)/n))*<i> by Th53
      .=cos(2*PI*k)+(sin(n*((2*PI*k)/n)))*<i> by XCMPLX_1:88
      .=cos(2*PI*k+0)+(sin(2*PI*k+0))*<i> by XCMPLX_1:88
      .=cos.(2*PI*k+0)+(sin(2*PI*k+0))*<i> by SIN_COS:def 23
      .=cos.(2*PI*k+0)+(sin.(2*PI*k+0))*<i> by SIN_COS:def 21
      .=cos.(2*PI*k+0)+(sin.0)*<i> by SIN_COS2:10
      .=1 by SIN_COS:33,SIN_COS2:11;
   hence thesis by A1,COMPTRIG:def 2;
end;

canceled;

theorem
   for z,s being Element of COMPLEX, n being Element of NAT st
     s<>0 & z<>0 & n>=1 & s|^ n=z|^ n holds
     |.s.| = |.z.|
proof
     let z,s be Element of COMPLEX, n be Element of NAT;
     assume
 A1: s<>0 & z<>0 & n>=1 & s|^ n=z|^ n;
 then A2: |.s.|>0 & |.z.|>0 by COMPLEX1:133;
          z|^ n =[* |.s.|*cos Arg s, |.s.|*sin Arg s *]|^ n by A1,COMPLEX2:19
        .=([*|.s.|,0*]*[*cos Arg s,sin Arg s *])|^ n by Th46
        .=([*|.s.|,0*]|^ n)*([*cos Arg s,sin Arg s *]|^ n) by Th51
        .=(|.s.| to_power n)*([*cos Arg s,sin Arg s *]|^ n) by A2,Th52
        .=(|.s.| to_power n)*(cos (n*Arg s)+sin (n*Arg s)*<i>) by Th53
       .= (|.s.| to_power n)*cos (n*Arg s)-0*sin (n*Arg s)+
        ((|.s.| to_power n)*sin (n*Arg s)+cos (n*Arg s)*0)*<i>
       .= [*(|.s.| to_power n)*cos (n*Arg s)
           ,(|.s.| to_power n)*sin (n*Arg s)*] by COMPLEX1:27;
       then [*(|.s.| to_power n)*cos (n*Arg s),(|.s.| to_power n)*sin (n*Arg s
)*]
        =[* |.z.|*cos Arg z, |.z.|*sin Arg z *]|^ n by COMPLEX2:19
        .=([*|.z.|,0*]*[*cos Arg z,sin Arg z *])|^ n by Th46
        .=([*|.z.|,0*]|^ n)*([*cos Arg z,sin Arg z *]|^ n) by Th51
        .=(|.z.| to_power n)*([*cos Arg z,sin Arg z *]|^ n) by A2,Th52
        .=(|.z.| to_power n)*(cos (n*Arg z)+sin (n*Arg z)*<i>) by Th53
       .= (|.z.| to_power n)*cos (n*Arg z)-0*sin (n*Arg z)+
          ((|.z.| to_power n)*sin (n*Arg z)+cos (n*Arg z)*0)*<i>
       .= [*(|.z.| to_power n)*cos (n*Arg z),(|.z.| to_power n)
          *sin (n*Arg z)*] by COMPLEX1:27;
      then (|.s.| to_power n)*cos (n*Arg s)=(|.z.| to_power n)*cos (n*Arg z)
       &(|.s.| to_power n)*sin (n*Arg s)
       =(|.z.| to_power n)*sin (n*Arg z) by ARYTM_0:12;
       then ((|.s.| to_power n))^2*(cos (n*Arg s))^2+
       ((|.s.| to_power n)*sin (n*Arg s))^2
       =((|.z.| to_power n)*cos (n*Arg z))^2+
       ((|.z.| to_power n)*sin (n*Arg z))^2 by SQUARE_1:68;
       then ((|.s.| to_power n))^2*((cos (n*Arg s))^2+(sin (n*Arg s))^2)
       =((|.z.| to_power n))^2*((cos (n*Arg z))^2+(sin (n*Arg z))^2);
       then ((|.s.| to_power n))^2*((cos (n*Arg s))^2+(sin (n*Arg s))^2)
       =((|.z.| to_power n))^2*1 by SIN_COS:32;
 then A3: ((|.s.| to_power n))^2*1=((|.z.| to_power n))^2 by SIN_COS:32;
 A4: |.s.| to_power n > 0 & |.z.| to_power n > 0 by A2,POWER:39;
      then (|.s.| to_power n)
       =sqrt ((|.z.| to_power n))^2 by A3,SQUARE_1:89;
      then |.s.| to_power n=|.z.| to_power n by A4,SQUARE_1:89;
      then |.s.| |^ n=|.z.| to_power n by A2,POWER:46;
      then |.s.| |^ n=|.z.| |^ n by A2,POWER:46;
      then |.s.| = n-root (|.z.| |^ n) by A1,A2,POWER:5;
      hence thesis by A1,A2,POWER:5;
end;
