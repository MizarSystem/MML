:: Some Basic Properties of Sets
::  by Czes{\l}aw Byli\'nski
::
:: Received February 1, 1989
:: Copyright (c) 1990 Association of Mizar Users
 
environ

 vocabularies XBOOLE_0, TARSKI, ZFMISC_1;
 notations TARSKI, XBOOLE_0, ENUMSET1;
 constructors TARSKI, XBOOLE_0, ENUMSET1;
 registrations XBOOLE_0;
 requirements BOOLE;
 definitions TARSKI, XBOOLE_0;
 theorems TARSKI, XBOOLE_0, ENUMSET1, XBOOLE_1;
 schemes XBOOLE_0;

begin

reserve v,x,x1,x2,y,y1,y2,z,A,B,X,X1,X2,X3,X4,Y,Y1,Y2,Z,N,M for set;

registration
  let x, y be set;
  cluster [x,y] -> non empty;
  coherence;
end;

Lm1: {x} c= X iff x in X
proof
  x in {x} by TARSKI:def 1;
  hence {x} c= X implies x in X by TARSKI:def 3;
  assume
A1: x in X;
  let y;
  thus thesis by A1,TARSKI:def 1;
end;

Lm2: Y c= X & not x in Y implies Y c= X \ { x }
proof
  assume
A1: Y c= X & not x in Y;
  let y;
  assume y in Y;
  then y in X & not y in { x } by A1,TARSKI:def 1,def 3;
  hence thesis by XBOOLE_0:def 5;
end;

Lm3: Y c= { x } iff Y = {} or Y = { x }
proof
  thus Y c= { x } implies Y = {} or Y = { x }
  proof
    assume
A1: Y c= { x };
    x in Y or not x in Y;
    then { x } c= Y or Y c= { x } \ { x } by A1,Lm1,Lm2;
    then { x } = Y or Y c= {} by A1,XBOOLE_0:def 10,XBOOLE_1:37;
    hence thesis by XBOOLE_1:3;
  end;
  thus thesis by XBOOLE_1:2;
end;

definition
  let X;
  defpred IT[set] means $1 c= X;
  func bool X means
  :Def1:
  Z in it iff Z c= X;
  existence
  proof
    consider M such that
A1: X in M & for X,Y holds X in M & Y c= X implies Y in M and
    for X st X in M ex Z st Z in M & for Y st Y c= X holds Y in Z and
    for X holds X c= M implies X,M are_equipotent or X in M by TARSKI:9;
    consider IT being set such that
A2: Y in IT iff Y in M & IT[Y] from XBOOLE_0:sch 1;
    take IT;
    let Y;
    thus Y in IT implies Y c= X by A2;
    assume
A3: Y c= X;
    then Y in M by A1;
    hence thesis by A2,A3;
  end;
  uniqueness
  proof
    thus for X1,X2 being set st (for x being set holds x in X1 iff IT[x]) & (
    for x being set holds x in X2 iff IT[x]) holds X1 = X2 from XBOOLE_0:sch 3;
  end;
end;

definition
  let X1,X2;
  defpred X[set] means ex x,y st x in X1 & y in X2 & $1 = [x,y];
  func [: X1,X2 :] means
  :Def2:
  z in it iff ex x,y st x in X1 & y in X2 & z = [x,y];
  existence
  proof
A1: X1 c= X1 \/ X2 by XBOOLE_1:7;
    consider X such that
A2: for z holds z in X iff z in bool(bool(X1 \/ X2)) & X[z] from
    XBOOLE_0:sch 1;
    take X;
    let z;
    thus z in X implies ex x,y st x in X1 & y in X2 & z = [x,y] by A2;
    given x,y such that
A3: x in X1 and
A4: y in X2 and
A5: z = [x,y];
    {x,y} c= X1 \/ X2
    proof
      let z;
      assume z in {x,y};
      then z = x or z = y by TARSKI:def 2;
      hence thesis by A3,A4,XBOOLE_0:def 3;
    end;
    then
A6: {x,y} in bool(X1 \/ X2) by Def1;
    {x} c= X1 by A3,Lm1;
    then {x} c= X1 \/ X2 by A1,XBOOLE_1:1;
    then
A7: {x} in bool(X1 \/ X2) by Def1;
    {{x,y},{x}} c= bool(X1 \/ X2)
    proof
      let z;
      assume z in {{x,y},{x}};
      hence thesis by A7,A6,TARSKI:def 2;
    end;
    then {{x,y},{x}} in bool(bool(X1 \/ X2)) by Def1;
    hence thesis by A2,A3,A4,A5;
  end;
  uniqueness
  proof
    thus for X1,X2 being set st (for x being set holds x in X1 iff X[x]) & (
    for x being set holds x in X2 iff X[x]) holds X1 = X2 from XBOOLE_0:sch 3;
  end;
end;

definition
  let X1,X2,X3;
  func [: X1,X2,X3 :] equals
  [:[:X1,X2:],X3:];
  coherence;
end;

definition
  let X1,X2,X3,X4;
  func [: X1,X2,X3,X4 :] equals
  [:[:X1,X2,X3:],X4:];
  coherence;
end;

begin
 
::
::  Empty set.
::

theorem
  bool {} = { {} }
proof
  now
    let x be set;
    x c= {} iff x = {} by XBOOLE_1:3;
    hence x in bool {} iff x in { {} } by Def1,TARSKI:def 1;
  end;
  hence thesis by TARSKI:2;
end;

theorem
  union {} = {}
proof
  now
    given x such that
A1: x in union {};
    ex X being set st x in X & X in {} by A1,TARSKI:def 4;
    hence contradiction;
  end;
  hence thesis by XBOOLE_0:7;
end;
 
::
::  Singleton and unordered pairs.
::

canceled 3;

theorem Th6:
  {x} c= {y} implies x = y
proof
  x in { x } by TARSKI:def 1;
  then {x} = {y} implies x = y by TARSKI:def 1;
  hence thesis by Lm3;
end;

canceled;

theorem Th8:
  {x} = {y1,y2} implies x = y1
proof
  assume { x } = { y1,y2 };
  then y1 in { x } by TARSKI:def 2;
  hence thesis by TARSKI:def 1;
end;

theorem Th9:
  {x} = {y1,y2} implies y1 = y2
proof
  assume
A1: { x } = { y1,y2 };
  then x = y1 by Th8;
  hence thesis by A1,Th8;
end;

theorem Th10:
  { x1,x2 } = { y1,y2 } implies x1 = y1 or x1 = y2
proof
  x1 in { x1,x2 } by TARSKI:def 2;
  hence thesis by TARSKI:def 2;
end;

canceled;

theorem Th12:
  {x} c= {x,y}
proof
  let z;
  assume z in {x};
  then z=x by TARSKI:def 1;
  hence thesis by TARSKI:def 2;
end;

Lm4: {x} \/ X c= X implies x in X
proof
  assume
A1: {x} \/ X c= X;
  assume not x in X;
  then not x in {x} \/ X by A1,TARSKI:def 3;
  then not x in {x} by XBOOLE_0:def 3;
  hence thesis by TARSKI:def 1;
end;

theorem
  {x} \/ {y} = {x} implies x = y
proof
  assume {x} \/ {y} = {x};
  then y in {x} or x in {y} by Lm4;
  hence thesis by TARSKI:def 1;
end;

Lm5: x in X implies {x} \/ X = X
proof
  assume x in X;
  then {x} c= X by Lm1;
  hence thesis by XBOOLE_1:12;
end;

theorem
  {x} \/ {x,y} = {x,y}
proof
  x in {x,y} by TARSKI:def 2;
  hence thesis by Lm5;
end;

Lm6: {x} misses X implies not x in X
proof
A1: x in {x} by TARSKI:def 1;
  assume {x} /\ X = {} & x in X;
  hence contradiction by A1,XBOOLE_0:def 4;
end;

canceled;

theorem
  {x} misses {y} implies x <> y
proof
  assume {x} misses {y};
  then not x in {y} by Lm6;
  hence thesis by TARSKI:def 1;
end;

Lm7: not x in X implies {x} misses X
proof
  assume
A1: not x in X;
  thus {x} /\ X c= {}
  proof
    let y;
    assume y in {x} /\ X;
    then y in {x} & y in X by XBOOLE_0:def 4;
    hence thesis by A1,TARSKI:def 1;
  end;
  thus thesis by XBOOLE_1:2;
end;

theorem Th17:
  x <> y implies {x} misses {y}
proof
  assume x <> y;
  then not x in {y} by TARSKI:def 1;
  hence thesis by Lm7;
end;

Lm8: X /\ {x} = {x} implies x in X
proof
  assume X /\ {x} = {x};
  then x in X /\ {x} by TARSKI:def 1;
  hence thesis by XBOOLE_0:def 4;
end;

theorem
  {x} /\ {y} = {x} implies x = y
proof
  assume {x} /\ {y} = {x};
  then x in {y} or y in {x} by Lm8;
  hence thesis by TARSKI:def 1;
end;

Lm9: x in X implies X /\ {x} = {x}
proof
  assume x in X;
  then {x} c= X by Lm1;
  hence thesis by XBOOLE_1:28;
end;

theorem
  {x} /\ {x,y} = {x}
proof
  x in {x,y} by TARSKI:def 2;
  hence thesis by Lm9;
end;

Lm10: {x} \ X = {x} iff not x in X
proof
  {x} \ X = {x} iff {x} misses X by XBOOLE_1:83;
  hence thesis by Lm6,Lm7;
end;

theorem
  {x} \ {y} = {x} iff x <> y
proof
  {x} \ {y} = {x} iff not x in {y} by Lm10;
  hence thesis by TARSKI:def 1;
end;

Lm11: {x} \ X = {} iff x in X
proof
  {x} \ X = {} iff {x} c= X by XBOOLE_1:37;
  hence thesis by Lm1;
end;

theorem
  {x} \ {y} = {} implies x = y
proof
  assume {x} \ {y} = {};
  then x in {y} by Lm11;
  hence thesis by TARSKI:def 1;
end;

theorem
  {x} \ {x,y} = {}
proof
  x in {x,y} by TARSKI:def 2;
  hence thesis by Lm11;
end;

Lm12: {x,y} \ X = {x} iff not x in X & (y in X or x = y)
proof
  thus {x,y} \ X = {x} implies not x in X & (y in X or x = y)
  proof
    assume
A1: {x,y} \ X = {x};
    then x in {x,y} \ X by TARSKI:def 1;
    hence not x in X by XBOOLE_0:def 5;
A2: y in {x,y} by TARSKI:def 2;
    assume not y in X;
    then y in {x} by A1,A2,XBOOLE_0:def 5;
    hence thesis by TARSKI:def 1;
  end;
  assume
A3: ( not x in X)&( y in X or x=y);
  z in {x,y} \ X iff z=x
  proof
    z in {x,y} \ X iff z in {x,y} & not z in X by XBOOLE_0:def 5;
    hence thesis by A3,TARSKI:def 2;
  end;
  hence thesis by TARSKI:def 1;
end;

theorem
  x <> y implies { x,y } \ { y } = { x }
proof
  assume x <> y;
  then
A1: not x in {y} by TARSKI:def 1;
  y in {y} by TARSKI:def 1;
  hence thesis by A1,Lm12;
end;

theorem
  {x} c= {y} implies x = y by Th6;

theorem
  {z} c= {x,y} implies z = x or z = y
proof
A1: z in {z} by TARSKI:def 1;
  assume {z} c= {x,y};
  then z in {x,y} by A1,TARSKI:def 3;
  hence thesis by TARSKI:def 2;
end;

theorem Th26:
  {x,y} c= {z} implies x = z
proof
A1: x in {x,y} by TARSKI:def 2;
  assume {x,y} c= {z};
  then x in {z} by A1,TARSKI:def 3;
  hence thesis by TARSKI:def 1;
end;

theorem
  {x,y} c= {z} implies {x,y} = {z}
proof
  assume {x,y} c= {z};
  then x=z & y=z by Th26;
  hence thesis by ENUMSET1:69;
end;

Lm13: X <> {x} & X <> {} implies ex y st y in X & y <> x
proof
  assume that
A1: X <> {x} and
A2: X <> {};
  per cases;
  suppose
A3: not x in X;
    consider y being set such that
A4: y in X by A2,XBOOLE_0:7;
    take y;
    thus thesis by A3,A4;
  end;
  suppose
A5: x in X;
    consider y such that
A6: y in X & not y in {x} or y in {x} & not y in X by A1,TARSKI:2;
    take y;
    thus thesis by A5,A6,TARSKI:def 1;
  end;
end;

Lm14: Z c= {x1,x2} iff Z = {} or Z = {x1} or Z = {x2} or Z = {x1,x2}
proof
  thus Z c= {x1,x2} implies Z={} or Z={x1} or Z={x2} or Z={x1,x2}
  proof
    assume that
A1: Z c= {x1,x2} and
A2: Z <> {} and
A3: Z <> {x1} and
A4: Z <> {x2};
    now
      let z;
      thus z in Z implies z in {x1,x2} by A1,TARSKI:def 3;
A5:   now
        assume
A6:     not x1 in Z;
        consider y such that
A7:     y in Z and
A8:     y<>x2 by A2,A4,Lm13;
        y in {x1,x2} by A1,A7,TARSKI:def 3;
        hence contradiction by A6,A7,A8,TARSKI:def 2;
      end;
A9:   now
        assume
A10:    not x2 in Z;
        consider y such that
A11:    y in Z and
A12:    y<>x1 by A2,A3,Lm13;
        y in {x1,x2} by A1,A11,TARSKI:def 3;
        hence contradiction by A10,A11,A12,TARSKI:def 2;
      end;
      assume z in {x1,x2};
      hence z in Z by A5,A9,TARSKI:def 2;
    end;
    hence thesis by TARSKI:2;
  end;
  thus thesis by Th12,XBOOLE_1:2;
end;

theorem
  {x1,x2} c= {y1,y2} implies x1 = y1 or x1 = y2
proof
  assume {x1,x2} c= {y1,y2};
  then {x1,x2}={} or {x1,x2}={y1} or {x1,x2}={y2} or {x1,x2}={y1,y2} by
  Lm14;
  hence thesis by Th8,Th10;
end;

theorem
  x <> y implies { x } \+\ { y } = { x,y }
proof
  assume
A1: x <> y;
  z in { x } \+\ { y } iff z=x or z=y
  proof
A2: z in {y} iff z=y by TARSKI:def 1;
    z in {x} iff z=x by TARSKI:def 1;
    hence thesis by A1,A2,XBOOLE_0:1;
  end;
  hence thesis by TARSKI:def 2;
end;

theorem
  bool { x } = { {} , { x }}
proof
  now
    let y;
    y c= { x } iff y = {} or y = { x } by Lm3;
    hence y in bool { x } iff y in { {}, { x }} by Def1,TARSKI:def 2;
  end;
  hence thesis by TARSKI:2;
end;

Lm15: X in A implies X c= union A
proof
  assume
A1: X in A;
  let x;
  thus thesis by A1,TARSKI:def 4;
end;

theorem
  union { x } = x
proof
A1: union { x } c= x
  proof
    let y;
    assume y in union { x };
    then ex Y being set st y in Y & Y in { x } by TARSKI:def 4;
    hence thesis by TARSKI:def 1;
  end;
  x in { x } by TARSKI:def 1;
  then x c= union { x } by Lm15;
  hence thesis by A1,XBOOLE_0:def 10;
end;

Lm16: union { X,Y } = X \/ Y
proof
  x in union {X,Y} iff x in X or x in Y
  proof
    thus x in union {X,Y} implies x in X or x in Y
    proof
      assume x in union {X,Y};
      then ex Z st x in Z & Z in {X,Y} by TARSKI:def 4;
      hence thesis by TARSKI:def 2;
    end;
    X in {X,Y} & Y in {X,Y} by TARSKI:def 2;
    hence thesis by TARSKI:def 4;
  end;
  hence thesis by XBOOLE_0:def 3;
end;

theorem
  union {{x},{y}} = {x,y}
proof
  thus union {{x},{y}} = {x} \/ {y} by Lm16
    .= {x,y} by ENUMSET1:41;
end;
 
::
::  Ordered pairs.
::

theorem Th33:
  [x1,x2] = [y1,y2] implies x1 = y1 & x2 = y2
proof
  assume
A1: [x1,x2] = [y1,y2];
A2: now
    assume
A3: y1 <> y2;
    then
A4: {x1} <> {y1,y2} by Th9;
    then {x1} = {y1} by A1,Th10;
    hence
A5: x1 = y1 by Th6;
    {y1,y2} = {x1,x2} by A1,A4,Th10;
    hence x2 = y2 by A3,A5,Th10;
  end;
  now
    assume
A6: y1 = y2;
    then
    { { y1 } } = { {y1}, {y1} } & { {y1}, {y1} } = { {x1,x2},{x1} } by A1,
    ENUMSET1:69;
    then { y1 } = { x1,x2 } by Th8;
    hence thesis by A6,Th8;
  end;
  hence thesis by A2;
end;

Lm17: [x,y] in [:X,Y:] iff x in X & y in Y
proof
  thus [x,y] in [:X,Y:] implies x in X & y in Y
  proof
    assume [x,y] in [:X,Y:];
    then ex x1,y1 st x1 in X & y1 in Y & [x,y]=[x1,y1] by Def2;
    hence thesis by Th33;
  end;
  thus thesis by Def2;
end;

theorem
  [x,y] in [:{x1},{y1}:] iff x = x1 & y = y1
proof
  x=x1 & y=y1 iff x in {x1} & y in {y1} by TARSKI:def 1;
  hence thesis by Lm17;
end;

theorem
  [:{x},{y}:] = {[x,y]}
proof
  now
    let z;
    thus z in [:{x},{y}:] implies z in {[x,y]}
    proof
      assume z in [:{x},{y}:];
      then consider x1,y1 such that
A1:   x1 in {x} & y1 in {y} and
A2:   z=[x1,y1] by Def2;
      x1=x & y1=y by A1,TARSKI:def 1;
      hence thesis by A2,TARSKI:def 1;
    end;
    assume z in {[x,y]};
    then
A3: z=[x,y] by TARSKI:def 1;
    x in {x} & y in {y} by TARSKI:def 1;
    hence z in [:{x},{y}:] by A3,Lm17;
  end;
  hence thesis by TARSKI:2;
end;

theorem Th36:
  [:{x},{y,z}:] = {[x,y],[x,z]} & [:{x,y},{z}:] = {[x,z],[y,z]}
proof
  now
    let v;
A1: z in{y,z} by TARSKI:def 2;
    thus v in [:{x},{y,z}:] implies v in {[x,y],[x,z]}
    proof
      assume v in [:{x},{y,z}:];
      then consider x1,y1 such that
A2:   x1 in {x} and
A3:   y1 in {y,z} and
A4:   v=[x1,y1] by Def2;
A5:   y1=y or y1=z by A3,TARSKI:def 2;
      x1=x by A2,TARSKI:def 1;
      hence thesis by A4,A5,TARSKI:def 2;
    end;
    assume v in {[x,y],[x,z]};
    then
A6: v=[x,y] or v=[x,z] by TARSKI:def 2;
    x in{x} & y in{y,z} by TARSKI:def 1,def 2;
    hence v in [:{x},{y,z}:] by A6,A1,Lm17;
  end;
  hence [:{x},{y,z}:] = {[x,y],[x,z]} by TARSKI:2;
  now
    let v;
A7: z in{z} by TARSKI:def 1;
    thus v in [:{x,y},{z}:] implies v in {[x,z],[y,z]}
    proof
      assume v in [:{x,y},{z}:];
      then consider x1,y1 such that
A8:   x1 in {x,y} and
A9:   y1 in {z} and
A10:  v=[x1,y1] by Def2;
A11:  x1=x or x1=y by A8,TARSKI:def 2;
      y1=z by A9,TARSKI:def 1;
      hence thesis by A10,A11,TARSKI:def 2;
    end;
    assume v in {[x,z],[y,z]};
    then
A12: v=[x,z] or v=[y,z] by TARSKI:def 2;
    x in{x,y} & y in{x,y} by TARSKI:def 2;
    hence v in [:{x,y},{z}:] by A12,A7,Lm17;
  end;
  hence thesis by TARSKI:2;
end;
 
::
::  Singleton and unordered pairs included in a set.
::

theorem
  {x} c= X iff x in X by Lm1;

theorem Th38:
  {x1,x2} c= Z iff x1 in Z & x2 in Z
proof
  x1 in {x1,x2} & x2 in {x1,x2} by TARSKI:def 2;
  hence {x1,x2} c= Z implies x1 in Z & x2 in Z by TARSKI:def 3;
  assume
A1: x1 in Z & x2 in Z;
  let z;
  assume z in {x1,x2};
  hence thesis by A1,TARSKI:def 2;
end;
 
::
::  Set included in a singleton (or unordered pair).
::

theorem
  Y c= { x } iff Y = {} or Y = { x } by Lm3;

theorem
  Y c= X & not x in Y implies Y c= X \ { x } by Lm2;

theorem
  X <> {x} & X <> {} implies ex y st y in X & y <> x by Lm13;

theorem
  Z c= {x1,x2} iff Z = {} or Z = {x1} or Z = {x2} or Z = {x1,x2} by Lm14;
 
::
::   Sum of an unordered pair (or a singleton) and a set.
::

theorem Th43:
  {z} = X \/ Y implies X = {z} & Y = {z} or X = {} & Y = {z} or X
  = {z} & Y = {}
proof
  assume
A1: {z} = X \/ Y;
  then
A2: X c= {z} & Y c= {z} by XBOOLE_1:7;
  X <> {} or Y <> {} by A1;
  hence thesis by A2,Lm3;
end;

theorem
  {z} = X \/ Y & X <> Y implies X = {} or Y = {}
proof
  assume {z} = X \/ Y;
  then X={z} & Y={z} or X={} & Y={z} or X={z} & Y={} by Th43;
  hence thesis;
end;

theorem
  {x} \/ X c= X implies x in X by Lm4;

theorem
  x in X implies {x} \/ X = X by Lm5;

theorem
  {x,y} \/ Z c= Z implies x in Z
proof
  assume
A1: {x,y} \/ Z c= Z;
  assume not x in Z;
  then not x in {x,y} \/ Z by A1,TARSKI:def 3;
  then not x in {x,y} by XBOOLE_0:def 3;
  hence thesis by TARSKI:def 2;
end;

theorem
  x in Z & y in Z implies {x,y} \/ Z = Z
proof
  assume x in Z & y in Z;
  then {x,y} c= Z by Th38;
  hence thesis by XBOOLE_1:12;
end;

theorem
  {x} \/ X <> {};

theorem
  {x,y} \/ X <> {};
 
::
::  Intersection of an unordered pair (or a singleton) and a set.
::

theorem
  X /\ {x} = {x} implies x in X by Lm8;

theorem
  x in X implies X /\ {x} = {x} by Lm9;

theorem
  x in Z & y in Z implies {x,y} /\ Z = {x,y}
proof
  assume x in Z & y in Z;
  then {x,y} c= Z by Th38;
  hence thesis by XBOOLE_1:28;
end;

theorem
  {x} misses X implies not x in X by Lm6;

theorem Th55:
  {x,y} misses Z implies not x in Z
proof
A1: x in {x,y} by TARSKI:def 2;
  assume {x,y} /\ Z = {} & x in Z;
  hence contradiction by A1,XBOOLE_0:def 4;
end;

theorem
  not x in X implies {x} misses X by Lm7;

theorem Th57:
  not x in Z & not y in Z implies {x,y} misses Z
proof
  assume
A1: ( not x in Z)& not y in Z;
  assume {x,y} meets Z;
  then consider z such that
A2: z in {x,y} /\ Z by XBOOLE_0:4;
  z in {x,y} & z in Z by A2,XBOOLE_0:def 4;
  hence contradiction by A1,TARSKI:def 2;
end;

theorem
  {x} misses X or {x} /\ X = {x} by Lm7,Lm9;

theorem
  {x,y} /\ X = {x} implies not y in X or x = y
proof
A1: y in {x,y} by TARSKI:def 2;
  assume {x,y} /\ X = {x} & y in X;
  then y in {x} by A1,XBOOLE_0:def 4;
  hence thesis by TARSKI:def 1;
end;

theorem
  x in X & (not y in X or x = y) implies {x,y} /\ X = {x}
proof
  assume
A1: x in X &( not y in X or x=y);
  z in {x,y} /\ X iff z=x
  proof
    z in {x,y} /\ X iff z in {x,y} & z in X by XBOOLE_0:def 4;
    hence thesis by A1,TARSKI:def 2;
  end;
  hence thesis by TARSKI:def 1;
end;

canceled 2;

theorem
  {x,y} /\ X = {x,y} implies x in X
proof
  assume {x,y} /\ X = {x,y};
  then x in {x,y} /\ X & y in {x,y} /\ X or x in X /\ {x,y} & y in X /\ {x,y}
  by TARSKI:def 2;
  hence thesis by XBOOLE_0:def 4;
end;
 
::
::   Difference of an unordered pair (or a singleton) and a set.
::

theorem Th64:
  z in X \ {x} iff z in X & z <> x
proof
  z in X \ {x} iff z in X & not z in {x} by XBOOLE_0:def 5;
  hence thesis by TARSKI:def 1;
end;

theorem Th65:
  X \ {x} = X iff not x in X
proof
  X \ {x} = X iff X misses {x} by XBOOLE_1:83;
  hence thesis by Lm6,Lm7;
end;

theorem
  X \ {x} = {} implies X = {} or X = {x}
proof
  assume X \ {x} = {};
  then X c= {x} by XBOOLE_1:37;
  hence thesis by Lm3;
end;

theorem
  {x} \ X = {x} iff not x in X by Lm10;

theorem
  {x} \ X = {} iff x in X by Lm11;

theorem
  {x} \ X = {} or {x} \ X = {x}
proof
  assume {x} \ X <> {};
  then not x in X by Lm11;
  hence thesis by Lm10;
end;

theorem
  {x,y} \ X = {x} iff not x in X & (y in X or x = y) by Lm12;

canceled;

theorem Th72:
  {x,y} \ X = {x,y} iff not x in X & not y in X
proof
  {x,y} \ X = {x,y} iff {x,y} misses X by XBOOLE_1:83;
  hence thesis by Th55,Th57;
end;

theorem Th73:
  {x,y} \ X = {} iff x in X & y in X
proof
  {x,y} \ X = {} iff {x,y} c= X by XBOOLE_1:37;
  hence thesis by Th38;
end;

theorem
  {x,y} \ X = {} or {x,y} \ X = {x} or {x,y} \ X = {y} or {x,y} \ X = {x ,y}
proof
  assume that
A1: {x,y} \ X <> {} and
A2: {x,y} \ X <> {x} and
A3: {x,y} \ X <> {y};
A4: not x in X & x<>y or y in X by A3,Lm12;
  x in X or not y in X & x<>y by A2,Lm12;
  hence thesis by A1,A4,Th72,Th73;
end;

theorem
  X \ {x,y} = {} iff X = {} or X = {x} or X = {y} or X = {x,y}
proof
  X \ {x,y} = {} iff X c= {x,y} by XBOOLE_1:37;
  hence thesis by Lm14;
end;
 
::
::  Power Set.
::

canceled 3;

theorem
  A c= B implies bool A c= bool B
proof
  assume
A1: A c= B;
  let x;
  assume x in bool A;
  then x c= A by Def1;
  then x c= B by A1,XBOOLE_1:1;
  hence thesis by Def1;
end;

theorem
  { A } c= bool A
proof
  A in bool A by Def1;
  hence thesis by Lm1;
end;

theorem
  bool A \/ bool B c= bool (A \/ B)
proof
  let x;
  assume x in bool A \/ bool B;
  then x in bool A or x in bool B by XBOOLE_0:def 3;
  then
A1: x c= A or x c= B by Def1;
  A c= A \/ B & B c= A \/ B by XBOOLE_1:7;
  then x c= A \/ B by A1,XBOOLE_1:1;
  hence thesis by Def1;
end;

theorem
  bool A \/ bool B = bool (A \/ B) implies A,B are_c=-comparable
proof
  assume
A1: bool A \/ bool B = bool (A \/ B);
  A \/ B in bool (A \/ B) by Def1;
  then A \/ B in bool A or A \/ B in bool B by A1,XBOOLE_0:def 3;
  then
A2: A \/ B c= A or A \/ B c= B by Def1;
  A c= A \/ B & B c= A \/ B by XBOOLE_1:7;
  hence A c= B or B c= A by A2,XBOOLE_0:def 10;
end;

theorem
  bool (A /\ B) = bool A /\ bool B
proof
  now
    let x;
    A /\ B c= A & A /\ B c= B by XBOOLE_1:17;
    then x c= A & x c= B iff x c= A /\ B by XBOOLE_1:1,19;
    then x in bool A & x in bool B iff x in bool (A /\ B) by Def1;
    hence x in bool (A /\ B) iff x in bool A /\ bool B by XBOOLE_0:def 4;
  end;
  hence thesis by TARSKI:2;
end;

theorem
  bool (A \ B) c= { {} } \/ (bool A \ bool B)
proof
  let x;
  assume x in bool (A \ B);
  then
A1: x c= A \ B by Def1;
  then x misses B by XBOOLE_1:63,79;
  then A \ B c= A & x /\ B = {} by XBOOLE_0:def 7,XBOOLE_1:36;
  then x = {} or x c= A & not x c= B by A1,XBOOLE_1:1,28;
  then x in { {} } or x in bool A & not x in bool B by Def1,TARSKI:def 1;
  then x in { {} } or x in bool A \ bool B by XBOOLE_0:def 5;
  hence thesis by XBOOLE_0:def 3;
end;

canceled;

theorem
  bool (A \ B) \/ bool (B \ A) c= bool (A \+\ B)
proof
  let x;
  assume x in bool (A \ B) \/ bool (B \ A);
  then x in bool (A \ B) or x in bool (B \ A) by XBOOLE_0:def 3;
  then
A1: x c= A \ B or x c= B \ A by Def1;
  x c= (A \ B) \/ (B \ A)
  proof
    let z;
    assume z in x;
    then z in A \ B or z in B \ A by A1,TARSKI:def 3;
    hence thesis by XBOOLE_0:def 3;
  end;
  hence thesis by Def1;
end;
 
::
::  Union of a set.
::

canceled 5;

theorem
  X in A implies X c= union A by Lm15;

theorem
  union { X,Y } = X \/ Y by Lm16;

theorem
  (for X st X in A holds X c= Z) implies union A c= Z
proof
  assume
A1: for X st X in A holds X c= Z;
  let y;
  assume y in union A;
  then consider Y such that
A2: y in Y and
A3: Y in A by TARSKI:def 4;
  Y c= Z by A1,A3;
  hence thesis by A2,TARSKI:def 3;
end;

theorem Th95:
  A c= B implies union A c= union B
proof
  assume
A1: A c= B;
  let x;
  assume x in union A;
  then consider Y such that
A2: x in Y and
A3: Y in A by TARSKI:def 4;
  Y in B by A1,A3,TARSKI:def 3;
  hence thesis by A2,TARSKI:def 4;
end;

theorem
  union (A \/ B) = union A \/ union B
proof
A1: union (A \/ B) c= union A \/ union B
  proof
    let x;
    assume x in union (A \/ B);
    then consider Y such that
A2: x in Y and
A3: Y in A \/ B by TARSKI:def 4;
    Y in A or Y in B by A3,XBOOLE_0:def 3;
    then x in union A or x in union B by A2,TARSKI:def 4;
    hence thesis by XBOOLE_0:def 3;
  end;
  union A c= union (A \/ B) & union B c= union (A \/ B) by Th95,XBOOLE_1:7;
  then union A \/ union B c= union (A \/ B) by XBOOLE_1:8;
  hence thesis by A1,XBOOLE_0:def 10;
end;

theorem Th97:
  union (A /\ B) c= union A /\ union B
proof
  let x;
  assume x in union (A /\ B);
  then consider Y such that
A1: x in Y and
A2: Y in A /\ B by TARSKI:def 4;
  Y in B by A2,XBOOLE_0:def 4;
  then
A3: x in union B by A1,TARSKI:def 4;
  Y in A by A2,XBOOLE_0:def 4;
  then x in union A by A1,TARSKI:def 4;
  hence thesis by A3,XBOOLE_0:def 4;
end;

theorem Th98:
  (for X st X in A holds X misses B) implies union A misses B
proof
  assume
A1: for X st X in A holds X misses B;
  assume union(A) meets B;
  then consider z being set such that
A2: z in union(A) /\ B by XBOOLE_0:4;
  z in union(A) by A2,XBOOLE_0:def 4;
  then consider X such that
A3: z in X and
A4: X in A by TARSKI:def 4;
A5: X misses B by A1,A4;
  z in B by A2,XBOOLE_0:def 4;
  then z in X /\ B by A3,XBOOLE_0:def 4;
  hence contradiction by A5,XBOOLE_0:4;
end;

theorem
  union bool A = A
proof
  now
    let x;
    thus x in union bool A implies x in A
    proof
      assume x in union bool A;
      then consider X such that
A1:   x in X and
A2:   X in bool A by TARSKI:def 4;
      X c= A by A2,Def1;
      hence thesis by A1,TARSKI:def 3;
    end;
    assume x in A;
    then { x } c= A by Lm1;
    then
A3: { x } in bool A by Def1;
    x in { x } by TARSKI:def 1;
    hence x in union bool A by A3,TARSKI:def 4;
  end;
  hence thesis by TARSKI:2;
end;

theorem
  A c= bool union A
proof
  let x;
  assume x in A;
  then x c= union A by Lm15;
  hence thesis by Def1;
end;

theorem
  (for X,Y st X<>Y & X in A \/ B & Y in A \/ B holds X misses Y) implies
  union(A /\ B) = union A /\ union B
proof
  assume
A1: for X,Y st X<>Y & X in A \/ B & Y in A \/ B holds X misses Y;
A2: union A /\ union B c= union (A /\ B)
  proof
    let x;
    assume
A3: x in union A /\ union B;
    then x in union A by XBOOLE_0:def 4;
    then consider X such that
A4: x in X and
A5: X in A by TARSKI:def 4;
    x in union B by A3,XBOOLE_0:def 4;
    then consider Y such that
A6: x in Y and
A7: Y in B by TARSKI:def 4;
    now
      x in X /\ Y by A4,A6,XBOOLE_0:def 4;
      then
A8:   X meets Y by XBOOLE_0:4;
      assume
A9:   X<>Y;
      X in A \/ B & Y in A \/ B by A5,A7,XBOOLE_0:def 3;
      hence contradiction by A1,A9,A8;
    end;
    then Y in A /\ B by A5,A7,XBOOLE_0:def 4;
    hence thesis by A6,TARSKI:def 4;
  end;
  union (A /\ B) c= union A /\ union B by Th97;
  hence thesis by A2,XBOOLE_0:def 10;
end;
 
::
::  Cartesian product.
::

canceled;

theorem Th103:
  A c= [:X,Y:] & z in A implies ex x,y st x in X & y in Y & z = [ x,y]
proof
  assume A c= [:X,Y:] & z in A;
  then z in [:X,Y:] by TARSKI:def 3;
  hence thesis by Def2;
end;

theorem Th104:
  z in [:X1, Y1:] /\ [:X2, Y2:] implies ex x,y st z = [x,y] & x
  in X1 /\ X2 & y in Y1 /\ Y2
proof
  assume
A1: z in [:X1, Y1:] /\ [:X2, Y2:];
  then z in [:X1, Y1:] by XBOOLE_0:def 4;
  then consider x1,y1 such that
A2: x1 in X1 and
A3: y1 in Y1 and
A4: z=[x1,y1] by Def2;
  z in [:X2, Y2:] by A1,XBOOLE_0:def 4;
  then consider x2,y2 such that
A5: x2 in X2 and
A6: y2 in Y2 and
A7: z=[x2,y2] by Def2;
  y1=y2 by A4,A7,Th33;
  then
A8: y1 in Y1 /\ Y2 by A3,A6,XBOOLE_0:def 4;
  x1=x2 by A4,A7,Th33;
  then x1 in X1 /\ X2 by A2,A5,XBOOLE_0:def 4;
  hence thesis by A4,A8;
end;

theorem
  [:X,Y:] c= bool bool (X \/ Y)
proof
  let z;
  assume z in [:X,Y:];
  then consider x,y such that
A1: x in X and
A2: y in Y and
A3: z = [x,y] by Def2;
  z c= bool (X \/ Y)
  proof
    let u be set;
    assume u in z;
    then
A4: u = {x,y} or u = {x} by A3,TARSKI:def 2;
    X c= X \/ Y & {x} c= X by A1,Lm1,XBOOLE_1:7;
    then
A5: {x} c= X \/ Y by XBOOLE_1:1;
    x in X \/ Y & y in X \/ Y by A1,A2,XBOOLE_0:def 3;
    then {x,y} c= X \/ Y by Th38;
    hence thesis by A5,A4,Def1;
  end;
  hence thesis by Def1;
end;

theorem
  [x,y] in [:X,Y:] iff x in X & y in Y by Lm17;

theorem Th107:
  [x,y] in [:X,Y:] implies [y,x] in [:Y,X:]
proof
  [x,y] in [:X,Y:] implies x in X & y in Y by Lm17;
  hence thesis by Lm17;
end;

theorem
  (for x,y holds [x,y] in [:X1,Y1:] iff [x,y] in [:X2,Y2:]) implies [:X1
  ,Y1:]=[:X2,Y2:]
proof
  assume
A1: for x,y holds [x,y] in [:X1,Y1:] iff [x,y] in [:X2,Y2:];
  now
    let z;
    thus z in [:X1,Y1:] implies z in [:X2,Y2:]
    proof
      assume
A2:   z in [:X1,Y1:];
      then ex x,y st x in X1 & y in Y1 & [x,y]=z by Def2;
      hence thesis by A1,A2;
    end;
    assume
A3: z in [:X2,Y2:];
    then ex x,y st x in X2 & y in Y2 & [x,y]=z by Def2;
    hence z in [:X1,Y1:] by A1,A3;
  end;
  hence thesis by TARSKI:2;
end;

Lm18: A c= [:X1,Y1:] & B c= [:X2,Y2:] & (for x,y holds [x,y] in A iff [x,y] in
B) implies A = B
proof
  assume that
A1: A c= [:X1,Y1:] and
A2: B c= [:X2,Y2:] and
A3: for x,y holds [x,y] in A iff [x,y] in B;
  now
    let z;
A4: z in B implies ex x,y st x in X2 & y in Y2 & z=[x,y] by A2,Th103;
    z in A implies ex x,y st x in X1 & y in Y1 & z=[x,y] by A1,Th103;
    hence z in A iff z in B by A3,A4;
  end;
  hence thesis by TARSKI:2;
end;

Lm19: ( for z st z in A ex x,y st z = [x,y]) & (for z st z in B ex x,y st z=[x
,y]) & (for x,y holds [x,y] in A iff [x,y] in B) implies A = B
proof
  assume that
A1: for z st z in A ex x,y st z=[x,y] and
A2: for z st z in B ex x,y st z=[x,y] and
A3: for x,y holds [x,y] in A iff [x,y] in B;
  now
    let z;
A4: z in B implies ex x,y st z=[x, y ] by A2;
    z in A implies ex x,y st z=[x,y] by A1;
    hence z in A iff z in B by A3,A4;
  end;
  hence thesis by TARSKI:2;
end;

canceled 4;

theorem Th113:
  [:X,Y:] = {} iff X = {} or Y = {}
proof
  thus [:X,Y:] = {} implies X={} or Y={}
  proof
    assume
A1: [:X,Y:] = {};
    assume X<>{};
    then consider x being set such that
A2: x in X by XBOOLE_0:7;
    assume Y<>{};
    then consider y being set such that
A3: y in Y by XBOOLE_0:7;
    [x,y] in [:X,Y:] by A2,A3,Def2;
    hence contradiction by A1;
  end;
  assume
A4: not thesis;
  then consider z being set such that
A5: z in [:X,Y:] by XBOOLE_0:7;
A6: now
    assume
A7: Y={};
    ex x,y st x in X & y in Y & [x,y]=z by A5,Def2;
    hence contradiction by A7;
  end;
  now
    assume
A8: X={};
    ex x,y st x in X & y in Y & [x,y]=z by A5,Def2;
    hence contradiction by A8;
  end;
  hence contradiction by A4,A6;
end;

theorem
  X <> {} & Y <> {} & [:X,Y:] = [:Y,X:] implies X = Y
proof
  assume X<>{};
  then consider x being set such that
A1: x in X by XBOOLE_0:7;
  assume Y<>{};
  then consider y being set such that
A2: y in Y by XBOOLE_0:7;
  assume
A3: [:X,Y:]=[:Y,X:];
  z in X iff z in Y
  proof
    thus z in X implies z in Y
    proof
      assume z in X;
      then [z,y] in [:Y,X:] by A2,A3,Lm17;
      hence thesis by Lm17;
    end;
    assume z in Y;
    then [z,x] in [:X,Y:] by A1,A3,Lm17;
    hence thesis by Lm17;
  end;
  hence thesis by TARSKI:2;
end;

theorem
  [:X,X:] = [:Y,Y:] implies X = Y
proof
  assume
A1: [:X,X:] = [:Y,Y:];
  x in X iff x in Y
  proof
    x in X iff [x,x] in [:X,X:] by Lm17;
    hence thesis by A1,Lm17;
  end;
  hence thesis by TARSKI:2;
end;

Lm20: z in [:X,Y:] implies ex x,y st [x,y] = z
proof
  assume z in [:X,Y:];
  then ex x,y st x in X & y in Y & [x,y]=z by Def2;
  hence thesis;
end;

theorem
  X c= [:X,X:] implies X = {}
proof
  assume
A1: X c= [:X,X:];
  assume X <> {};
  then consider z being set such that
A2: z in X by XBOOLE_0:7;
  z in X \/ union X by A2,XBOOLE_0:def 3;
  then consider Y such that
A3: Y in X \/ union X and
A4: not ex x st x in X \/ union X & x in Y by TARSKI:7;
  now
    assume
A5: Y in X;
    then Y in [:X,X:] by A1,TARSKI:def 3;
    then consider x,y such that
A6: Y=[x,y] by Lm20;
A7: {x} in Y by A6,TARSKI:def 2;
    Y c= union X by A5,Lm15;
    then {x} in union X by A7,TARSKI:def 3;
    then {x} in X \/ union X by XBOOLE_0:def 3;
    hence contradiction by A4,A7;
  end;
  then Y in union X by A3,XBOOLE_0:def 3;
  then consider Z such that
A8: Y in Z and
A9: Z in X by TARSKI:def 4;
  Z in [:X,X:] by A1,A9,TARSKI:def 3;
  then consider x,y such that
A10: x in X and
  y in X and
A11: Z=[x,y] by Def2;
  Y={x} or Y={x,y} by A8,A11,TARSKI:def 2;
  then
A12: x in Y by TARSKI:def 1,def 2;
  x in X \/ union X by A10,XBOOLE_0:def 3;
  hence contradiction by A4,A12;
end;

theorem
  Z <> {} & ([:X,Z:] c= [:Y,Z:] or [:Z,X:] c= [:Z,Y:]) implies X c= Y
proof
  assume Z<>{};
  then consider z being set such that
A1: z in Z by XBOOLE_0:7;
  assume
A2: [:X,Z:] c= [:Y,Z:] or [:Z,X:] c= [:Z,Y:];
  let x;
  assume x in X;
  then [x,z] in [:X,Z:] & [z,x] in [:Z,X:] by A1,Def2;
  then [x,z] in [:Y,Z:] or [z,x] in [:Z,Y:] by A2,TARSKI:def 3;
  hence thesis by Lm17;
end;

theorem Th118:
  X c= Y implies [:X,Z:] c= [:Y,Z:] & [:Z,X:] c= [:Z,Y:]
proof
  assume
A1: X c= Y;
  thus [:X,Z:] c= [:Y,Z:]
  proof
    let z;
    assume z in [:X,Z:];
    then consider x,y such that
A2: x in X and
A3: y in Z & z=[x,y] by Def2;
    x in Y by A1,A2,TARSKI:def 3;
    hence thesis by A3,Def2;
  end;
  let z;
  assume z in [:Z,X:];
  then consider y,x such that
A4: y in Z and
A5: x in X and
A6: z=[y,x] by Def2;
  x in Y by A1,A5,TARSKI:def 3;
  hence thesis by A4,A6,Def2;
end;

theorem Th119:
  X1 c= Y1 & X2 c= Y2 implies [:X1,X2:] c= [:Y1,Y2:]
proof
  assume X1 c= Y1 & X2 c= Y2;
  then [:X1,X2:] c= [:Y1,X2:] & [:Y1,X2:] c= [:Y1,Y2:] by Th118;
  hence thesis by XBOOLE_1:1;
end;

theorem Th120:
  [:X \/ Y, Z:] = [:X, Z:] \/ [:Y, Z:] & [:Z, X \/ Y:] = [:Z, X:] \/ [:Z, Y:]
proof
A1: for z st z in [:X \/ Y, Z:] ex x,y st z=[x,y] by Lm20;
A2: for x,y holds [x,y] in [:X \/ Y, Z:] iff [x,y] in [:X, Z:] \/ [:Y, Z:]
  proof
    let x,y;
    thus [x,y] in [:X \/ Y, Z:] implies [x,y] in [:X, Z:] \/ [:Y, Z:]
    proof
      assume
A3:   [x,y] in [:X \/ Y, Z:];
      then x in X \/ Y by Lm17;
      then
A4:   x in X or x in Y by XBOOLE_0:def 3;
      y in Z by A3,Lm17;
      then [x,y] in [:X,Z:] or [x,y] in [:Y,Z:] by A4,Lm17;
      hence thesis by XBOOLE_0:def 3;
    end;
    assume [x,y] in [:X, Z:] \/ [:Y, Z:];
    then [x,y] in [:X, Z:] or [x,y] in [:Y, Z:] by XBOOLE_0:def 3;
    then
A5: x in X & y in Z or x in Y & y in Z by Lm17;
    then x in X \/ Y by XBOOLE_0:def 3;
    hence thesis by A5,Lm17;
  end;
A6: z in [:X1,X2:] \/ [:Y1,Y2:] implies ex x,y st z=[x,y]
  proof
    assume z in [:X1,X2:] \/ [:Y1,Y2:];
    then z in [:X1,X2:] or z in [:Y1,Y2:] by XBOOLE_0:def 3;
    hence thesis by Lm20;
  end;
  then for z st z in [:X,Z:] \/ [:Y,Z:] ex x,y st z=[x,y];
  hence
A7: [:X \/ Y, Z:] = [:X, Z:] \/ [:Y, Z:] by A1,A2,Lm19;
A8: for y,x holds [y,x] in [:Z, X \/ Y:] iff [y,x] in [:Z, X:] \/ [:Z, Y:]
  proof
    let y,x;
A9: [x,y]in[:X, Z:] or [x,y]in[:Y,Z:] iff [y,x]in[:Z,X:] or [y,x]in[:Z,Y
    :] by Th107;
    [y,x] in [:Z, X \/ Y:] iff [x,y] in [:X \/ Y, Z:] by Th107;
    hence thesis by A7,A9,XBOOLE_0:def 3;
  end;
A10: for z st z in [:Z,X \/ Y:] ex x,y st z=[x,y] by Lm20;
  for z st z in [:Z,X:] \/ [:Z,Y:] ex x,y st z=[x,y] by A6;
  hence thesis by A10,A8,Lm19;
end;

theorem
  [:X1 \/ X2, Y1 \/ Y2:] = [:X1,Y1:] \/ [:X1,Y2:] \/ [:X2,Y1:] \/ [:X2, Y2:]
proof
  thus [:X1 \/ X2, Y1 \/ Y2:] = [:X1, Y1 \/ Y2:] \/ [:X2, Y1 \/ Y2:] by Th120
    .= [:X1,Y1:] \/ [:X1,Y2:] \/ [:X2,Y1 \/ Y2:] by Th120
    .= [:X1,Y1:] \/ [:X1,Y2:] \/ ([:X2,Y1:] \/ [:X2,Y2:]) by Th120
    .= [:X1,Y1:] \/ [:X1,Y2:] \/ [:X2,Y1:] \/ [:X2,Y2:] by XBOOLE_1:4;
end;

theorem
  [:X /\ Y, Z:] = [:X, Z:] /\ [:Y, Z:] & [:Z, X /\ Y:] = [:Z, X:] /\ [:Z , Y :]
proof
A1: for x,y holds [x,y] in [:X /\ Y, Z:] iff [x,y] in [:X, Z:] /\ [:Y, Z :]
  proof
    let x,y;
    thus [x,y] in [:X /\ Y, Z:] implies [x,y] in [:X, Z:] /\ [:Y, Z:]
    proof
      assume
A2:   [x,y] in [:X /\ Y, Z:];
      then
A3:   x in X /\ Y by Lm17;
A4:   y in Z by A2,Lm17;
      x in Y by A3,XBOOLE_0:def 4;
      then
A5:   [x,y] in [:Y,Z:] by A4,Lm17;
      x in X by A3,XBOOLE_0:def 4;
      then [x,y] in [:X,Z:] by A4,Lm17;
      hence thesis by A5,XBOOLE_0:def 4;
    end;
    assume
A6: [x,y] in [:X, Z:] /\ [:Y, Z:];
    then [x,y] in [:Y, Z:] by XBOOLE_0:def 4;
    then
A7: x in Y by Lm17;
A8: [x,y] in [:X, Z:] by A6,XBOOLE_0:def 4;
    then x in X by Lm17;
    then
A9: x in X /\ Y by A7,XBOOLE_0:def 4;
    y in Z by A8,Lm17;
    hence thesis by A9,Lm17;
  end;
  [:X, Z:] /\ [:Y, Z:] c= [:X, Z:] by XBOOLE_1:17;
  hence
A10: [:X /\ Y, Z:] = [:X, Z:] /\ [:Y, Z:] by A1,Lm18;
A11: for y,x holds [y,x] in [:Z, X /\ Y:] iff [y,x] in [:Z, X:] /\ [:Z, Y:]
  proof
    let y,x;
A12: [x,y]in[:X, Z:] & [x,y]in[:Y,Z:] iff [y,x]in[:Z,X:] & [y,x]in[:Z,Y:]
    by Th107;
    [y,x] in [:Z, X /\ Y:] iff [x,y] in [:X /\ Y, Z:] by Th107;
    hence thesis by A10,A12,XBOOLE_0:def 4;
  end;
  [:Z, X:] /\ [:Z, Y:] c= [:Z, X:] by XBOOLE_1:17;
  hence thesis by A11,Lm18;
end;

theorem Th123:
  [:X1 /\ X2, Y1 /\ Y2:] = [:X1,Y1:] /\ [:X2, Y2:]
proof
  Y1 /\ Y2 c= Y2 & X1 /\ X2 c= X2 by XBOOLE_1:17;
  then
A1: [:X1 /\ X2, Y1 /\ Y2:] c= [:X2, Y2:] by Th119;
A2: [:X1, Y1:] /\ [:X2, Y2:] c= [:X1 /\ X2, Y1 /\ Y2:]
  proof
    let z;
    assume z in [:X1, Y1:] /\ [:X2, Y2:];
    then ex x,y st z=[x,y] & x in X1 /\ X2 & y in Y1 /\ Y2 by Th104;
    hence thesis by Def2;
  end;
  Y1 /\ Y2 c= Y1 & X1 /\ X2 c= X1 by XBOOLE_1:17;
  then [:X1 /\ X2, Y1 /\ Y2:] c= [:X1, Y1:] by Th119;
  then [:X1 /\ X2, Y1 /\ Y2:] c= [:X1, Y1:] /\ [:X2, Y2:] by
  A1,XBOOLE_1:19;
  hence thesis by A2,XBOOLE_0:def 10;
end;

theorem
  A c= X & B c= Y implies [:A,Y:] /\ [:X,B:] = [:A,B:]
proof
  assume that
A1: A c= X and
A2: B c= Y;
A3: [:A,Y:] /\ [:X,B:] = [:A /\ X, Y /\ B:] by Th123;
  A /\ X = A by A1,XBOOLE_1:28;
  hence thesis by A2,A3,XBOOLE_1:28;
end;

theorem Th125:
  [:X \ Y, Z:] = [:X, Z:] \ [:Y, Z:] & [:Z, X \ Y:] = [:Z, X:] \ [:Z, Y:]
proof
A1: for x,y holds [x,y] in [:X \ Y, Z:] iff [x,y] in [:X, Z:] \ [:Y, Z:]
  proof
    let x,y;
    thus [x,y] in [:X \ Y, Z:] implies [x,y] in [:X, Z:] \ [:Y, Z:]
    proof
      assume
A2:   [x,y] in [:X \ Y, Z:];
      then
A3:   x in X \ Y by Lm17;
      then
A4:   x in X by XBOOLE_0:def 5;
      not x in Y by A3,XBOOLE_0:def 5;
      then
A5:   not [x,y] in [:Y,Z:] by Lm17;
      y in Z by A2,Lm17;
      then [x,y] in [:X,Z:] by A4,Lm17;
      hence thesis by A5,XBOOLE_0:def 5;
    end;
    assume
A6: [x,y] in [:X, Z:] \ [:Y, Z:];
    then
A7: [x,y] in [:X, Z:] by XBOOLE_0:def 5;
    then
A8: y in Z by Lm17;
    not [x,y] in [:Y, Z:] by A6,XBOOLE_0:def 5;
    then
A9: not (x in Y & y in Z) by Lm17;
    x in X by A7,Lm17;
    then x in X \ Y by A7,A9,Lm17,XBOOLE_0:def 5;
    hence thesis by A8,Lm17;
  end;
  [:X, Z:] \ [:Y, Z:] c= [:X, Z:] by XBOOLE_1:36;
  hence
A10: [:X \ Y, Z:] = [:X, Z:] \ [:Y, Z:] by A1,Lm18;
A11: for y,x holds [y,x] in [:Z, X \ Y:] iff [y,x] in [:Z, X:] \ [:Z, Y:]
  proof
    let y,x;
A12: [x,y]in[:X, Z:]& not[x,y]in[:Y,Z:] iff [y,x]in[:Z,X:]& not[y,x]in[:Z,
    Y :] by Th107;
    [y,x] in [:Z, X \ Y:] iff [x,y] in [:X \ Y, Z:] by Th107;
    hence thesis by A10,A12,XBOOLE_0:def 5;
  end;
  [:Z, X:] \ [:Z, Y:] c= [:Z, X:] by XBOOLE_1:36;
  hence thesis by A11,Lm18;
end;

theorem
  [:X1,X2:] \ [:Y1,Y2:] = [:X1\Y1,X2:] \/ [:X1,X2\Y2:]
proof
A1: [:Y1,X2:] /\ [:X1,Y2:] = [:Y1 /\ X1, X2 /\ Y2:] by Th123;
  Y1 /\ X1 c= Y1 & X2 /\ Y2 c= Y2 by XBOOLE_1:17;
  then
A2: [:Y1 /\ X1, X2 /\ Y2:] c= [:Y1,Y2:] by Th119;
A3: [:X1\Y1,X2:] \/ [:X1,X2\Y2:] c= [:X1,X2:] \ [:Y1, Y2:]
  proof
    let z;
A4: now
      assume z in [:X1\Y1,X2:];
      then consider x,y such that
A5:   x in X1\Y1 and
A6:   y in X2 and
A7:   z=[x,y] by Def2;
      not x in Y1 by A5,XBOOLE_0:def 5;
      then
A8:   not z in [:Y1,Y2:] by A7,Lm17;
      x in X1 by A5,XBOOLE_0:def 5;
      then z in [:X1,X2:] by A6,A7,Lm17;
      hence thesis by A8,XBOOLE_0:def 5;
    end;
A9: now
      assume z in [:X1,X2\Y2:];
      then consider x,y such that
A10:  x in X1 and
A11:  y in X2\Y2 and
A12:  z=[x,y] by Def2;
      not y in Y2 by A11,XBOOLE_0:def 5;
      then
A13:  not z in [:Y1,Y2:] by A12,Lm17;
      y in X2 by A11,XBOOLE_0:def 5;
      then z in [:X1,X2:] by A10,A12,Lm17;
      hence thesis by A13,XBOOLE_0:def 5;
    end;
    assume z in [:X1\Y1,X2:] \/ [:X1,X2\Y2:];
    hence thesis by A4,A9,XBOOLE_0:def 3;
  end;
  [:X1\Y1,X2:] = [:X1,X2:] \ [:Y1,X2:] & [:X1,X2\Y2:] = [:X1,X2:] \ [:X1,
  Y2 :] by Th125;
  then [:X1\Y1,X2:] \/ [:X1,X2\Y2:] = [:X1,X2:] \ [:Y1 /\ X1, X2 /\ Y2:] by
  A1,XBOOLE_1:54;
  then [:X1,X2:] \ [:Y1,Y2:] c= [:X1\Y1,X2:] \/ [:X1,X2\Y2:] by
  A2, XBOOLE_1
  :34;
  hence thesis by A3,XBOOLE_0:def 10;
end;

theorem Th127:
  X1 misses X2 or Y1 misses Y2 implies [:X1,Y1:] misses [:X2,Y2:]
proof
  assume
A1: X1 misses X2 or Y1 misses Y2;
  assume not thesis;
  then consider z being set such that
A2: z in [:X1,Y1:] /\ [:X2,Y2:] by XBOOLE_0:4;
  ex x,y st z=[x,y] & x in X1 /\ X2 & y in Y1 /\ Y2 by A2,Th104;
  hence contradiction by A1,XBOOLE_0:4;
end;

theorem Th128:
  [x,y] in [:{z},Y:] iff x = z & y in Y
proof
A1: x in {z} iff x=z by TARSKI:def 1;
  hence [x,y] in [:{z},Y:] implies x=z & y in Y by Lm17;
  thus thesis by A1,Lm17;
end;

theorem Th129:
  [x,y] in [:X,{z}:] iff x in X & y = z
proof
A1: y in {z} iff y=z by TARSKI:def 1;
  hence [x,y] in [:X,{z}:] implies x in X & y=z by Lm17;
  thus thesis by A1,Lm17;
end;

theorem
  X <> {} implies [:{x},X:] <> {} & [:X,{x}:] <> {} by Th113;

theorem
  x <> y implies [:{x},X:] misses [:{y},Y:] & [:X,{x}:] misses [:Y,{y}:]
proof
  assume x<>y;
  then {x} misses {y} by Th17;
  hence thesis by Th127;
end;

theorem
  [:{x,y},X:] = [:{x},X:] \/ [:{y},X:] & [:X,{x,y}:] = [:X,{x}:] \/ [:X, {y} :]
proof
  {x,y}={x} \/ {y} by ENUMSET1:41;
  hence thesis by Th120;
end;

canceled;

theorem Th134:
  X1 <> {} & Y1 <> {} & [:X1,Y1:] = [:X2,Y2:] implies X1 = X2 & Y1 = Y2
proof
  assume
A1: X1<>{};
  then consider x being set such that
A2: x in X1 by XBOOLE_0:7;
  assume
A3: Y1<>{};
  then consider y being set such that
A4: y in Y1 by XBOOLE_0:7;
  assume
A5: [:X1,Y1:] = [:X2,Y2:];
  then
A6: [:X2,Y2:] <> {} by A1,A3,Th113;
  then
A7: Y2 <> {} by Th113;
  z in X1 iff z in X2
  proof
    consider y2 being set such that
A8: y2 in Y2 by A7,XBOOLE_0:7;
    thus z in X1 implies z in X2
    proof
      assume z in X1;
      then [z,y] in [:X2,Y2:] by A4,A5,Lm17;
      hence thesis by Lm17;
    end;
    assume z in X2;
    then [z,y2] in [:X2,Y2:] by A8,Lm17;
    hence thesis by A5,Lm17;
  end;
  hence X1 = X2 by TARSKI:2;
A9: X2 <> {} by A6,Th113;
  z in Y1 iff z in Y2
  proof
    consider x2 being set such that
A10: x2 in X2 by A9,XBOOLE_0:7;
    thus z in Y1 implies z in Y2
    proof
      assume z in Y1;
      then [x,z] in [:X2,Y2:] by A2,A5,Lm17;
      hence thesis by Lm17;
    end;
    assume z in Y2;
    then [x2,z] in [:X2,Y2:] by A10,Lm17;
    hence thesis by A5,Lm17;
  end;
  hence thesis by TARSKI:2;
end;

theorem
  X c= [:X,Y:] or X c= [:Y,X:] implies X = {}
proof
  assume
A1: X c= [:X,Y:] or X c= [:Y,X:];
  assume
A2: X <> {};
A3: now
    defpred P[set] means ex Y st $1=Y & ex z st z in Y & z in X;
    consider Z such that
A4: for y holds y in Z iff y in union X & P[y] from XBOOLE_0:sch 1;
    ex x being set st x in X \/ Z by A2,XBOOLE_0:7;
    then consider Y2 such that
A5: Y2 in X \/ Z and
A6: not ex x st x in X \/ Z & x in Y2 by TARSKI:7;
    now
      assume
A7:   not ex Y2 st Y2 in X & for Y1 st Y1 in Y2 holds for z holds not
      z in Y1 or not z in X;
      now
        assume
A8:     Y2 in X;
        then consider Y1 such that
A9:     Y1 in Y2 and
A10:    ex z st z in Y1 & z in X by A7;
        Y1 in union X by A8,A9,TARSKI:def 4;
        then Y1 in Z by A4,A10;
        then Y1 in X \/ Z by XBOOLE_0:def 3;
        hence contradiction by A6,A9;
      end;
      then Y2 in Z by A5,XBOOLE_0:def 3;
      then ex X2 st Y2=X2 & ex z st z in X2 & z in X by A4;
      then consider z such that
A11:  z in Y2 and
A12:  z in X;
      z in X \/ Z by A12,XBOOLE_0:def 3;
      hence contradiction by A6,A11;
    end;
    then consider Y1 such that
A13: Y1 in X and
A14: not ex Y2 st Y2 in Y1 & ex z st z in Y2 & z in X;
A15: now
      given y,x such that
A16:  x in X and
A17:  Y1 = [y,x];
A18:  x in {y,x} by TARSKI:def 2;
      {y,x} in Y1 by A17,TARSKI:def 2;
      hence contradiction by A14,A16,A18;
    end;
    assume X c= [:Y,X:];
    then Y1 in [:Y,X:] by A13,TARSKI:def 3;
    then ex y,x st y in Y & x in X & Y1 = [y,x] by Def2;
    hence contradiction by A15;
  end;
  now
    consider z being set such that
A19: z in X by A2,XBOOLE_0:7;
    z in X \/ union X by A19,XBOOLE_0:def 3;
    then consider Y1 such that
A20: Y1 in X \/ union X and
A21: not ex x st x in X \/ union X & x in Y1 by TARSKI:7;
    assume
A22: X c= [:X,Y:];
    now
      assume
A23:  Y1 in X;
      then Y1 in [:X,Y:] by A22,TARSKI:def 3;
      then consider x,y such that
A24:  Y1 = [x,y] by Lm20;
A25:  {x} in Y1 by A24,TARSKI:def 2;
      Y1 c= union X by A23,Lm15;
      then {x} in union X by A25,TARSKI:def 3;
      then {x} in X \/ union X by XBOOLE_0:def 3;
      hence contradiction by A21,A25;
    end;
    then Y1 in union X by A20,XBOOLE_0:def 3;
    then consider Y2 such that
A26: Y1 in Y2 and
A27: Y2 in X by TARSKI:def 4;
    Y2 in [:X,Y:] by A22,A27,TARSKI:def 3;
    then consider x,y such that
A28: x in X and
    y in Y and
A29: Y2=[x,y] by Def2;
    Y1={x} or Y1={x,y} by A26,A29,TARSKI:def 2;
    then
A30: x in Y1 by TARSKI:def 1,def 2;
    x in X \/ union X by A28,XBOOLE_0:def 3;
    hence contradiction by A21,A30;
  end;
  hence thesis by A1,A3;
end;

theorem
  ex M st N in M & (for X,Y holds X in M & Y c= X implies Y in M) & (for
  X holds X in M implies bool X in M) & for X holds X c= M implies X,M
  are_equipotent or X in M
proof
  consider M such that
A1: N in M and
A2: for X,Y holds X in M & Y c= X implies Y in M and
A3: for X st X in M ex Z st Z in M & for Y st Y c= X holds Y in Z and
A4: for X holds X c= M implies X,M are_equipotent or X in M by TARSKI:9;
  take M;
  thus N in M by A1;
  thus for X,Y holds X in M & Y c= X implies Y in M by A2;
  thus for X holds X in M implies bool X in M
  proof
    let X;
    assume X in M;
    then consider Z such that
A5: Z in M and
A6: for Y st Y c= X holds Y in Z by A3;
    now
      let Y;
      assume Y in bool X;
      then Y c= X by Def1;
      hence Y in Z by A6;
    end;
    then bool X c= Z by TARSKI:def 3;
    hence thesis by A2,A5;
  end;
  thus thesis by A4;
end;

reserve e,X,X1,X2,Y1,Y2 for set;

theorem
  e in [:X1,Y1:] & e in [:X2,Y2:] implies e in [:X1 /\ X2, Y1 /\ Y2:]
proof
  assume e in [:X1,Y1:] & e in [:X2,Y2:];
  then e in [:X1,Y1:] /\ [:X2,Y2:] by XBOOLE_0:def 4;
  hence thesis by Th123;
end;

begin :: Addenda
:: from BORSUK_1

theorem Th138:
  [:X1,X2:] c= [:Y1,Y2:] & [:X1,X2:] <> {} implies X1 c= Y1 & X2 c= Y2
proof
  assume that
A1: [:X1,X2:] c= [:Y1,Y2:] and
A2: [:X1,X2:] <> {};
A3: [:X1,X2:] = [:X1,X2:] /\ [:Y1,Y2:] by A1,XBOOLE_1:28
    .= [:X1/\Y1,X2/\Y2:] by Th123;
  X1 <> {} & X2 <> {} by A2,Th113;
  then X1 = X1/\Y1 & X2 = X2/\Y2 by A3,Th134;
  hence thesis by XBOOLE_1:17;
end;
 
:: from ALTCAT_1

theorem
  for A being non empty set, B,C,D being set st [:A,B:] c= [:C,D:] or [:
  B,A:] c= [:D,C:] holds B c= D
proof
  let A be non empty set, B,C,D be set such that
A1: [:A,B:] c= [:C,D:] or [:B,A:] c= [:D,C:];
  per cases;
  suppose
    B = {};
    hence thesis by XBOOLE_1:2;
  end;
  suppose
    B <> {};
    then [:A,B:] <> {} & [:B,A:] <> {} by Th113;
    hence thesis by A1,Th138;
  end;
end;

theorem
  x in X implies (X\{x})\/{x}=X
proof
  assume
A1: x in X;
  thus (X\{x})\/{x}c=X
  proof
    let y;
    assume y in (X\{x})\/{x};
    then y in X\{x} or y in {x} by XBOOLE_0:def 3;
    hence thesis by A1,Th64,TARSKI:def 1;
  end;
  thus X c= (X\{x})\/{x}
  proof
    let y;
    assume y in X;
    then y in {x} or y in X\{x} by XBOOLE_0:def 5;
    hence thesis by XBOOLE_0:def 3;
  end;
end;

theorem
  not x in X implies (X\/{x})\{x}=X
proof
A1: (X \/ {x}) \ {x} = X \ {x} by XBOOLE_1:40;
  assume not x in X;
  hence thesis by A1,Th65;
end;
 
:: from WAYBEL18, 2006.01.06, A.T.

theorem
  for x,y,z,Z being set holds Z c= {x,y,z} iff Z = {} or Z = {x} or Z =
  {y} or Z = {z} or Z = {x,y} or Z = {y,z} or Z = {x,z} or Z = {x,y,z}
proof
  let x,y,z,Z be set;
  hereby
    assume that
A1: Z c= {x,y,z} and
A2: Z <> {} and
A3: Z <> {x} and
A4: Z <> {y} and
A5: Z <> {z} and
A6: Z <> {x,y} and
A7: Z <> {y,z} and
A8: Z <> {x,z};
    {x,y,z} c= Z
    proof
      let a be set;
A9:   now
        {x,y,z} \ {x} = ({x} \/ {y,z}) \ {x} by ENUMSET1:42
          .= {y,z} \ {x} by XBOOLE_1:40;
        then
A10:    {x,y,z} \ {x} c= {y,z} by XBOOLE_1:36;
        assume not x in Z;
        then Z c= {x,y,z} \ {x} by A1,Lm2;
        then Z c= {y,z} by A10,XBOOLE_1:1;
        hence contradiction by A2,A4,A5,A7,Lm14;
      end;
A11:  now
        {x,y,z} \ {y} = ({x,y} \/ {z}) \ {y} by ENUMSET1:43
          .= ({x} \/ {y} \/ {z}) \ {y} by ENUMSET1:41
          .= ({x} \/ {z} \/ {y}) \ {y} by XBOOLE_1:4
          .= ({x,z} \/ {y}) \ {y} by ENUMSET1:41
          .= {x,z} \ {y} by XBOOLE_1:40;
        then
A12:    {x,y,z} \ {y} c= {x,z} by XBOOLE_1:36;
        assume not y in Z;
        then Z c= {x,y,z} \ {y} by A1,Lm2;
        then Z c= {x,z} by A12,XBOOLE_1:1;
        hence contradiction by A2,A3,A5,A8,Lm14;
      end;
A13:  now
        {x,y,z} \ {z} = ({x,y} \/ {z}) \ {z} by ENUMSET1:43
          .= {x,y} \ {z} by XBOOLE_1:40;
        then
A14:    {x,y,z} \ {z} c= {x,y} by XBOOLE_1:36;
        assume not z in Z;
        then Z c= {x,y,z} \ {z} by A1,Lm2;
        then Z c= {x,y} by A14,XBOOLE_1:1;
        hence contradiction by A2,A3,A4,A6,Lm14;
      end;
      assume a in {x,y,z};
      hence thesis by A9,A11,A13,ENUMSET1:def 1;
    end;
    hence Z = {x,y,z} by A1,XBOOLE_0:def 10;
  end;
  assume
A15: Z = {} or Z = {x} or Z = {y} or Z = {z} or Z = {x,y} or Z = {y,z}
  or Z = {x,z} or Z = {x,y,z};
  per cases by A15;
  suppose
    Z = {};
    hence thesis by XBOOLE_1:2;
  end;
  suppose
    Z = {x};
    then Z c= {x} \/ {y,z} by XBOOLE_1:7;
    hence thesis by ENUMSET1:42;
  end;
  suppose
A16: Z = {y};
    {x,y} c= {x,y} \/ {z} by XBOOLE_1:7;
    then
A17: {x,y} c= {x,y,z} by ENUMSET1:43;
    Z c= {x,y} by A16,Th12;
    hence thesis by A17,XBOOLE_1:1;
  end;
  suppose
    Z = {z};
    then Z c= {x,y} \/ {z} by XBOOLE_1:7;
    hence thesis by ENUMSET1:43;
  end;
  suppose
    Z = {x,y};
    then Z c= {x,y} \/ {z} by XBOOLE_1:7;
    hence thesis by ENUMSET1:43;
  end;
  suppose
    Z = {y,z};
    then Z c= {x} \/ {y,z} by XBOOLE_1:7;
    hence thesis by ENUMSET1:42;
  end;
  suppose
A18: Z = {x,z};
A19: {x,z} \/ {y} = {x} \/ {z} \/ {y} by ENUMSET1:41
      .= {x} \/ ({y} \/ {z}) by XBOOLE_1:4
      .= {x} \/ {y,z} by ENUMSET1:41;
    Z c= {x,z} \/ {y} by A18,XBOOLE_1:7;
    hence thesis by A19,ENUMSET1:42;
  end;
  suppose
    Z = {x,y,z};
    hence thesis;
  end;
end;
 
:: from PARTFUN1, 2006.12.05, A.T.

theorem
  N c= [:X1,Y1:] & M c= [:X2,Y2:] implies N \/ M c= [:X1 \/ X2,Y1 \/ Y2 :]
proof
  assume N c= [:X1,Y1:] & M c= [:X2,Y2:];
  then
A1: N \/ M c= [:X1,Y1:] \/ [:X2,Y2:] by XBOOLE_1:13;
  X1 c= X1 \/ X2 & Y1 c= Y1 \/ Y2 by XBOOLE_1:7;
  then
A2: [:X1,Y1:] c= [:X1 \/ X2,Y1 \/ Y2:] by Th119;
  Y2 c= Y1 \/ Y2 & X2 c= X1 \/ X2 by XBOOLE_1:7;
  then [:X2,Y2:] c= [:X1 \/ X2,Y1 \/ Y2:] by Th119;
  then [:X1,Y1:] \/ [:X2,Y2:] c= [:X1 \/ X2,Y1 \/ Y2:] by A2,XBOOLE_1:8;
  hence thesis by A1,XBOOLE_1:1;
end;

Lm21: not x in X & not y in X implies {x,y} misses X
proof
  assume
A1: ( not x in X)& not y in X;
  thus {x,y} /\ X c= {}
  proof
    let z;
    assume z in {x,y} /\ X;
    then z in {x,y} & z in X by XBOOLE_0:def 4;
    hence thesis by A1,TARSKI:def 2;
  end;
  thus thesis by XBOOLE_1:2;
end;

theorem Th144:
  not x in X & not y in X implies X = X \ {x,y}
proof
  X \ {x,y} = X iff X misses {x,y} by XBOOLE_1:83;
  hence thesis by Lm21;
end;

theorem
  not x in X & not y in X implies X = X \/ {x,y} \ {x,y}
proof
A1: (X \/ {x,y}) \ {x,y} = X \ {x,y} by XBOOLE_1:40;
  assume ( not x in X)& not y in X;
  hence thesis by A1,Th144;
end;
 
:: from INCPROJ, 2007.01.18. AK

definition
  let x1, x2, x3 be set;
  pred x1, x2, x3 are_mutually_different means
  x1 <> x2 & x1 <> x3 & x2 <> x3;
end;

definition
  let x1, x2, x3, x4 be set;
  pred x1, x2, x3, x4 are_mutually_different means
  x1 <> x2 & x1 <> x3 & x1 <> x4 & x2 <> x3 & x2 <> x4 & x3 <> x4;
end;
 
:: from CARD_2, 2007.01.18. AK

definition
  let x1, x2, x3, x4, x5 be set;
  pred x1, x2, x3, x4, x5 are_mutually_different means
  x1 <> x2 & x1 <> x3 &
x1 <> x4 & x1 <> x5 & x2 <> x3 & x2 <> x4 & x2 <> x5 & x3 <> x4 & x3 <> x5 & x4
  <> x5;
end;
 
:: from BORSUK_5, 2007.01.18. AK

definition
  let x1, x2, x3, x4, x5, x6 be set;
  pred x1, x2, x3, x4, x5, x6 are_mutually_different means
  x1 <> x2 & x1 <> x3
& x1 <> x4 & x1 <> x5 & x1 <> x6 & x2 <> x3 & x2 <> x4 & x2 <> x5 & x2 <> x6 &
  x3 <> x4 & x3 <> x5 & x3 <> x6 & x4 <> x5 & x4 <> x6 & x5 <> x6;
end;

definition
  let x1, x2, x3, x4, x5, x6, x7 be set;
  pred x1, x2, x3, x4, x5, x6, x7 are_mutually_different means
  x1 <> x2 & x1
<> x3 & x1 <> x4 & x1 <> x5 & x1 <> x6 & x1 <> x7 & x2 <> x3 & x2 <> x4 & x2 <>
x5 & x2 <> x6 & x2 <> x7 & x3 <> x4 & x3 <> x5 & x3 <> x6 & x3 <> x7 & x4 <> x5
  & x4 <> x6 & x4 <> x7 & x5 <> x6 & x5 <> x7 & x6 <> x7;
end;
 
:: missing, 2007.02.11, A.T.

theorem
  [:{x1,x2},{y1,y2}:] = {[x1,y1],[x1,y2],[x2,y1],[x2,y2]}
proof
  thus [:{x1,x2},{y1,y2}:] = [:{x1}\/{x2},{y1,y2}:] by ENUMSET1:41
    .= [:{x1},{y1,y2}:] \/ [:{x2},{y1,y2}:] by Th120
    .= {[x1,y1],[x1,y2]} \/ [:{x2},{y1,y2}:] by Th36
    .= {[x1,y1],[x1,y2]} \/ {[x2,y1],[x2,y2]} by Th36
    .= {[x1,y1],[x1,y2],[x2,y1],[x2,y2]} by ENUMSET1:45;
end;
 
:: missing, 2008.03.22, A.T.

theorem
  x <> y implies A \/ {x} \ {y} = A \ {y} \/ {x} by Th17,XBOOLE_1:87;
 
:: comp. REALSET1, 2008.07.05, A.T.

definition
  let X;
  attr X is trivial means
  x in X & y in X implies x = y;
end;

registration
  cluster empty -> trivial set;
  coherence
  proof
    let X;
    assume
A1: X is empty;
    let x,y;
    thus thesis by A1;
  end;
end;

registration
  let x be set;
  cluster {x} -> trivial;
  coherence
  proof
    let y,z;
    assume that
A1: y in {x} and
A2: z in {x};
    y =x by A1,TARSKI:def 1;
    hence thesis by A2,TARSKI:def 1;
  end;
end;
 
:: from SPRECT_3, 2008.09.30, A.T.

theorem
  for A,B,C,p being set st A c= B & B /\ C = {p} & p in A holds A /\ C = {p}
proof
  let A,B,C,p be set such that
A1: A c= B;
  assume
A2: B /\ C = {p};
  then
A3: A /\ C c= { p } by A1,XBOOLE_1:26;
  p in B /\ C by A2,TARSKI:def 1;
  then
A4: p in C by XBOOLE_0:def 4;
  assume p in A;
  then p in A /\ C by A4,XBOOLE_0:def 4;
  hence thesis by A3,Lm3;
end;
 
:: from SPRECT_2, 2008.09.30, A.T.

theorem
  for A,B,C,p being set st A /\ B c= {p} & p in C & C misses B holds A
  \/ C misses B
proof
  let A,B,C,p be set such that
A1: A /\ B c= {p} and
A2: p in C and
A3: C misses B;
  {p} c= C by A2,Lm1;
  then A /\ B c= C by A1,XBOOLE_1:1;
  then A /\ B /\ B c= C /\ B by XBOOLE_1:27;
  then
A4: A /\ (B /\ B) c= C /\ B by XBOOLE_1:16;
  (A \/ C) /\ B = A /\ B \/ C /\ B by XBOOLE_1:23
    .= C /\ B by A4,XBOOLE_1:12
    .= {} by A3,XBOOLE_0:def 7;
  hence thesis by XBOOLE_0:def 7;
end;

canceled;

theorem
  for A,B being set st for x,y being set st x in A & y in B holds x
  misses y holds union A misses union B
proof
  let A,B be set such that
A1: for x,y being set st x in A & y in B holds x misses y;
  for y being set st y in B holds union A misses y
  proof
    let y be set;
    assume y in B;
    then for x being set st x in A holds x misses y by A1;
    hence thesis by Th98;
  end;
  hence thesis by Th98;
end;
 
:: from BORSUK_3, 2009.01.24, A.T.

registration
  let X be set, Y be empty set;
  cluster [:X, Y:] -> empty;
  coherence by Th113;
end;

registration
  let X be empty set, Y be set;
  cluster [:X, Y:] -> empty;
  coherence by Th113;
end;
 
:: new, 2009.08.26, A.T

theorem
 not A in [:A,B:]
  proof
   assume A in [:A,B:];
    then consider x,y such that
W:   x in A & y in B & A = [x,y] by Th103;
    x = {x} or x = {x,y} by W, TARSKI:def 2;
    then x in x by TARSKI:def 1,def 2;
   hence contradiction;
  end;

theorem
 [x,{x}] in [:{x},[x,{x}]:]
  proof
    [:{x},[x,{x}]:] = { [x,{x}], [x,{x,{x}}] } by Th36;
   hence thesis by TARSKI:def 2;
  end;

theorem
 B in [:A,B:] implies ex x st x in A & B = [x,{x}]
  proof
   assume B in [:A,B:];
    then consider x,y such that
W:   x in A & y in B & B = [x,y] by Th103;
   take x;
   thus x in A by W;
pc:  y in [x,y] by W;
   per cases by pc,TARSKI:def 2;
   suppose y = {x};
    hence thesis by W;
   end;
   suppose y = {x,y};
     then y in y by TARSKI:def 2;
    hence thesis;
   end;
  end;
