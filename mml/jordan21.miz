:: On Some Points of a Simple Closed Curve
::  by Artur Korni{\l}owicz
::
:: Received October 6, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies BOOLE, RELAT_1, RELAT_2, PRE_TOPC, COMPTS_1, EUCLID, TOPREAL2,
      JORDAN2C, TOPREAL1, ORDINAL2, ARYTM, JORDAN6, FUNCT_1, PSCOMP_1, MCART_1,
      ARYTM_3, SUBSET_1, ARYTM_1, SPPOL_1, JORDAN3, JORDAN1K, JORDAN1A,
      JORDAN1, FUNCT_5, SEQ_2, LATTICES, JORDAN21;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XREAL_0, XXREAL_0,
      REAL_1, RELSET_1, SEQ_1, SEQ_4, DOMAIN_1, XXREAL_2,
      STRUCT_0, PRE_TOPC, COMPTS_1,
      CONNSP_1, TBSP_1, RCOMP_1, PSCOMP_1, EUCLID, TOPREAL1, TOPREAL2,
      JORDAN2C, SPPOL_1, JORDAN1, JORDAN6, JORDAN5C;
 constructors DOMAIN_1, REAL_1, RCOMP_1, CONNSP_1, COMPTS_1, TBSP_1, TOPREAL1,
      SPPOL_1, JORDAN1, PSCOMP_1, JORDAN5C, JORDAN6, JORDAN2C, SEQ_1, XXREAL_2;
 registrations XBOOLE_0, RELSET_1, NUMBERS, XREAL_0, MEMBERED, STRUCT_0,
      COMPTS_1, EUCLID, TOPREAL1, TOPREAL2, TOPREAL5, JORDAN6, TOPREAL6,
      VALUED_0;
 requirements SUBSET, BOOLE, NUMERALS, ARITHM;
 definitions TARSKI, XBOOLE_0, JORDAN1, EUCLID, PSCOMP_1, SUBSET_1;
 theorems XBOOLE_1, JORDAN6, XBOOLE_0, EUCLID, TOPREAL1, BORSUK_4, TARSKI,
      TOPREAL5, JORDAN16, TOPMETR, JORDAN7, JGRAPH_1, REAL_1, TBSP_1, JORDAN2C,
      PSCOMP_1, SPRECT_1, JORDAN5B, SPPOL_1, TOPREAL3, SEQ_4, FUNCT_2, RELAT_1,
      TOPS_1, RCOMP_1, JCT_MISC, XREAL_1, TOPREAL6, XXREAL_0, XXREAL_2;

begin :: On the Subsets of TOP-REAL 2

reserve C for Simple_closed_curve,
  P for Subset of TOP-REAL 2,
  p for Point of TOP-REAL 2,
  n for Element of NAT;

Lm1: dom proj2 = the carrier of TOP-REAL 2 by FUNCT_2:def 1;

Lm2: for r being real number, X being Subset of TOP-REAL 2 st r in proj2.:X
ex x being Point of TOP-REAL 2 st x in X & proj2.x = r
proof
  let r be real number, X be Subset of TOP-REAL 2;
  assume r in proj2.:X;
  then ex x being set st x in the carrier of TOP-REAL 2 &
  x in X & proj2.x = r by FUNCT_2:115;
  hence thesis;
end;

Lm3: now
  let a,A,B,C be set;
  assume a in A \/ B \/ C;
  then a in A \/ B or a in C by XBOOLE_0:def 2;
  hence a in A or a in B or a in C by XBOOLE_0:def 2;
end;

Lm4: for A,B,C,D being set st A misses D & B misses D & C misses D holds
A \/ B \/ C misses D
proof
  let A,B,C,D be set;
  assume A misses D & B misses D;
  then
A1: A \/ B misses D by XBOOLE_1:70;
  assume C misses D;
  hence A \/ B \/ C misses D by A1,XBOOLE_1:70;
end;

theorem
  for p being Point of TOP-REAL n holds {p} is Bounded
proof
  let p be Point of TOP-REAL n;
  reconsider a = p as Point of Euclid n by TOPMETR:16;
  {a} is bounded by TBSP_1:22;
  hence thesis by JORDAN2C:def 2;
end;

theorem Th2: :: P is an open east halfline
  for s1,t being real number, P being Subset of TOP-REAL 2
  st P = { |[ s,t ]| where s is Real : s1 < s } holds P is convex
proof
  let s1,t be real number, P be Subset of TOP-REAL 2;
  assume
A1: P = { |[ s,t ]| where s is Real: s1<s };
  let w1,w2 be Point of TOP-REAL 2 such that
A2: w1 in P & w2 in P;
  let x be set such that
A3: x in LSeg(w1,w2);
  consider s3 being Real such that
A4: |[ s3,t ]|=w1 & s1<s3 by A1,A2;
A5: w1`1=s3 & w1`2=t by A4,EUCLID:56;
  consider s4 being Real such that
A6: |[ s4,t ]|=w2 & s1<s4 by A1,A2;
A7: w2`1=s4 & w2`2=t by A6,EUCLID:56;
  x in {(1-lambda)*w1 + lambda*w2 where lambda is Real :
  0 <= lambda & lambda <= 1} by A3,TOPREAL1:def 3;
  then consider l being Real such that
A8: x = (1-l)*w1 + l*w2 & 0 <= l & l <= 1;
  set w = (1-l)*w1 + l*w2;
A9: w = |[((1-l)*w1)`1+(l*w2)`1,((1-l)*w1)`2+ (l*w2)`2]| by EUCLID:59;
  (1-l)*w1=|[ (1-l)*w1`1 ,(1-l)*w1`2 ]| & l*w2=|[ l*w2`1 ,l*w2`2 ]|
  by EUCLID:61;
  then ((1-l)*w1)`1= (1-l)*w1`1 & ((1-l)*w1)`2= (1-l)*w1`2
  & (l*w2)`1=l*w2`1 & (l*w2)`2=l*w2`2 by EUCLID:56;
  then
A10: w`1=(1-l)* w1`1+ l* w2`1 & w`2=(1-l)* w1`2+ l* w2`2 by A9,EUCLID:56;
  then
A11: s1 < w`1 & w = |[w`1, w`2]| by A4,A5,A6,A7,A8,EUCLID:57,XREAL_1:177;
  w`2 = (1-l)*t + l*t by A4,A7,A10,EUCLID:56
    .= t - 0;
  hence thesis by A1,A8,A11;
end;

theorem Th3: :: P is an open west halfline
  for s2,t being real number, P being Subset of TOP-REAL 2
  st P = {|[ s,t ]| where s is Real : s < s2 } holds P is convex
proof
  let s2,t be real number, P be Subset of TOP-REAL 2;
  assume
A1: P = { |[ s,t ]| where s is Real: s<s2 };
  let w1,w2 be Point of TOP-REAL 2 such that
A2: w1 in P & w2 in P;
  let x be set such that
A3: x in LSeg(w1,w2);
  consider s3 being Real such that
A4: |[ s3,t ]|=w1 & s3<s2 by A1,A2;
A5: w1`1=s3 & w1`2=t by A4,EUCLID:56;
  consider s4 being Real such that
A6: |[ s4,t ]|=w2 & s4<s2 by A1,A2;
A7: w2`1=s4 & w2`2=t by A6,EUCLID:56;
  x in {(1-lambda)*w1 + lambda*w2 where lambda is Real :
  0 <= lambda & lambda <= 1} by A3,TOPREAL1:def 3;
  then consider l being Real such that
A8: x = (1-l)*w1 + l*w2& 0 <= l & l <= 1;
  set w = (1-l)*w1 + l*w2;
A9: w = |[((1-l)*w1)`1+(l*w2)`1,((1-l)*w1)`2+ (l*w2)`2]| by EUCLID:59;
  (1-l)*w1=|[ (1-l)*w1`1 ,(1-l)*w1`2 ]| & l*w2=|[ l*w2`1 ,l*w2`2 ]|
  by EUCLID:61;
  then ((1-l)*w1)`1= (1-l)*w1`1 & ((1-l)*w1)`2= (1-l)*w1`2
  & (l*w2)`1=l*w2`1 & (l*w2)`2=l*w2`2 by EUCLID:56;
  then
A10: w`1=(1-l)* w1`1+ l* w2`1 & w`2=(1-l)* w1`2+ l* w2`2 by A9,EUCLID:56;
  then
A11: s2 > w`1 & w = |[w`1, w`2]| by A4,A5,A6,A7,A8,EUCLID:57,XREAL_1:178;
  w`2 = (1-l)*t + l*t by A4,A7,A10,EUCLID:56
    .= t - 0;
  hence thesis by A1,A8,A11;
end;

theorem Th4: :: P is an open north halfline
  for s,t1 being real number, P being Subset of TOP-REAL 2
  st P = {|[ s,t ]| where t is Real : t1 < t } holds P is convex
proof
  let s,t1 be real number, P be Subset of TOP-REAL 2;
  assume
A1: P = { |[ s,t ]| where t is Real : t1<t };
  let w1,w2 be Point of TOP-REAL 2 such that
A2: w1 in P & w2 in P;
  let x be set such that
A3: x in LSeg(w1,w2);
  consider t3 being Real such that
A4: |[ s,t3 ]|=w1 & t1<t3 by A1,A2;
A5: w1`1=s & w1`2=t3 by A4,EUCLID:56;
  consider t4 being Real such that
A6: |[ s,t4 ]|=w2 & t1<t4 by A1,A2;
A7: w2`1=s & w2`2=t4 by A6,EUCLID:56;
  x in {(1-lambda)*w1 + lambda*w2 where lambda is Real :
  0 <= lambda & lambda <= 1} by A3,TOPREAL1:def 3;
  then consider l being Real such that
A8: x = (1-l)*w1 + l*w2 & 0 <= l & l <= 1;
  set w = (1-l)*w1 + l*w2;
A9: w = |[((1-l)*w1)`1+(l*w2)`1,((1-l)*w1)`2+ (l*w2)`2]| by EUCLID:59;
  (1-l)*w1=|[ (1-l)*w1`1 ,(1-l)*w1`2 ]| & l*w2=|[ l*w2`1 ,l*w2`2 ]|
  by EUCLID:61;
  then
A10: ((1-l)*w1)`1= (1-l)*w1`1 & ((1-l)*w1)`2= (1-l)*w1`2
  & (l*w2)`1=l*w2`1 & (l*w2)`2=l*w2`2 by EUCLID:56;
  then w`1=(1-l)* w1`1+ l* w2`1 & w`2=(1-l)* w1`2+ l* w2`2 by A9,EUCLID:56;
  then
A11: t1 < w`2 & w = |[w`1, w`2]| by A4,A5,A6,A7,A8,EUCLID:57,XREAL_1:177;
  w`1 = s - 0 by A5,A7,A9,A10,EUCLID:56;
  hence x in P by A1,A8,A11;
end;

theorem Th5: :: P is an open south halfline
  for s,t2 being real number, P being Subset of TOP-REAL 2
  st P = { |[ s,t ]| where t is Real : t < t2 } holds P is convex
proof
  let s,t2 be real number, P be Subset of TOP-REAL 2;
  assume
A1: P = { |[ s,t ]| where t is Real : t<t2 };
  let w1,w2 be Point of TOP-REAL 2 such that
A2: w1 in P & w2 in P;
  let x be set such that
A3: x in LSeg(w1,w2);
  consider t3 being Real such that
A4: |[ s,t3 ]|=w1 & t3<t2 by A1,A2;
A5: w1`1=s & w1`2=t3 by A4,EUCLID:56;
  consider t4 being Real such that
A6: |[ s,t4 ]|=w2 & t4<t2 by A1,A2;
A7: w2`1=s & w2`2=t4 by A6,EUCLID:56;
  x in {(1-lambda)*w1 + lambda*w2 where lambda is Real:
  0 <= lambda & lambda <= 1} by A3,TOPREAL1:def 3;
  then consider l being Real such that
A8: x = (1-l)*w1 + l*w2& 0 <= l & l <= 1;
  set w = (1-l)*w1 + l*w2;
A9: w = |[((1-l)*w1)`1+(l*w2)`1,((1-l)*w1)`2+ (l*w2)`2]| by EUCLID:59;
  (1-l)*w1=|[ (1-l)*w1`1 ,(1-l)*w1`2 ]| & l*w2=|[ l*w2`1 ,l*w2`2 ]|
  by EUCLID:61;
  then ((1-l)*w1)`1= (1-l)*w1`1 & ((1-l)*w1)`2= (1-l)*w1`2
  & (l*w2)`1=l*w2`1 & (l*w2)`2=l*w2`2 by EUCLID:56;
  then
A10: w`1=(1-l)* w1`1+ l* w2`1 & w`2=(1-l)* w1`2+ l* w2`2 by A9,EUCLID:56;
  then
A11: t2 > w`2 & w = |[w`1, w`2]| by A4,A5,A6,A7,A8,EUCLID:57,XREAL_1:178;
  w`1 = (1-l)*s + l*s by A4,A7,A10,EUCLID:56
    .= s - 0;
  hence thesis by A1,A8,A11;
end;

theorem
  north_halfline p \ {p} is convex
proof
  set P = north_halfline p \ {p};
  P = {|[ p`1,r ]| where r is Real : r > p`2 }
  proof
    hereby
      let x be set;
      assume
A1:   x in P;
      then
A2:   x in north_halfline p by XBOOLE_0:def 4;
A3:   not x in {p} by A1,XBOOLE_0:def 4;
      reconsider y = x as Point of TOP-REAL 2 by A1;
A4:   y`1 = p`1 & y`2 >= p`2 by A2,TOPREAL1:def 12;
      now
        assume y`2 = p`2;
        then x = p by A4,TOPREAL3:11;
        hence contradiction by A3,TARSKI:def 1;
      end;
      then
A5:   y`2 > p`2 by A4,REAL_1:def 5;
      x = |[ p`1,y`2 ]| by A4,EUCLID:57;
      hence x in {|[ p`1,r ]| where r is Real : r > p`2} by A5;
    end;
    let x be set;
    assume x in {|[ p`1,r ]| where r is Real : r > p`2 };
    then consider r being Real such that
A6: x = |[ p`1,r ]| and
A7: r > p`2;
    reconsider y = x as Point of TOP-REAL 2 by A6;
A8: y`1 = p`1 & y`2 = r by A6,EUCLID:56;
    then
A9: x in north_halfline p by A7,TOPREAL1:def 12;
    not x in {p} by A7,A8,TARSKI:def 1;
    hence x in P by A9,XBOOLE_0:def 4;
  end;
  hence thesis by Th4;
end;

theorem
  south_halfline p \ {p} is convex
proof
  set P = south_halfline p \ {p};
  P = {|[ p`1,r ]| where r is Real : r < p`2 }
  proof
    hereby
      let x be set;
      assume
A1:   x in P;
      then
A2:   x in south_halfline p by XBOOLE_0:def 4;
A3:   not x in {p} by A1,XBOOLE_0:def 4;
      reconsider y = x as Point of TOP-REAL 2 by A1;
A4:   y`1 = p`1 & y`2 <= p`2 by A2,TOPREAL1:def 14;
      now
        assume y`2 = p`2;
        then x = p by A4,TOPREAL3:11;
        hence contradiction by A3,TARSKI:def 1;
      end;
      then
A5:   y`2 < p`2 by A4,REAL_1:def 5;
      x = |[ p`1,y`2 ]| by A4,EUCLID:57;
      hence x in {|[ p`1,r ]| where r is Real : r < p`2} by A5;
    end;
    let x be set;
    assume x in {|[ p`1,r ]| where r is Real : r < p`2 };
    then consider r being Real such that
A6: x = |[ p`1,r ]| and
A7: r < p`2;
    reconsider y = x as Point of TOP-REAL 2 by A6;
A8: y`1 = p`1 & y`2 = r by A6,EUCLID:56;
    then
A9: x in south_halfline p by A7,TOPREAL1:def 14;
    not x in {p} by A7,A8,TARSKI:def 1;
    hence x in P by A9,XBOOLE_0:def 4;
  end;
  hence thesis by Th5;
end;

theorem
  west_halfline p \ {p} is convex
proof
  set P = west_halfline p \ {p};
  P = {|[ r,p`2 ]| where r is Real : r < p`1 }
  proof
    hereby
      let x be set;
      assume
A1:   x in P;
      then
A2:   x in west_halfline p by XBOOLE_0:def 4;
A3:   not x in {p} by A1,XBOOLE_0:def 4;
      reconsider y = x as Point of TOP-REAL 2 by A1;
A4:   y`1 <= p`1 & y`2 = p`2 by A2,TOPREAL1:def 15;
      now
        assume y`1 = p`1;
        then x = p by A4,TOPREAL3:11;
        hence contradiction by A3,TARSKI:def 1;
      end;
      then
A5:   y`1 < p`1 by A4,REAL_1:def 5;
      x = |[ y`1,p`2 ]| by A4,EUCLID:57;
      hence x in {|[ r,p`2 ]| where r is Real : r < p`1} by A5;
    end;
    let x be set;
    assume x in {|[ r,p`2 ]| where r is Real : r < p`1 };
    then consider r being Real such that
A6: x = |[ r,p`2 ]| and
A7: r < p`1;
    reconsider y = x as Point of TOP-REAL 2 by A6;
A8: y`1 = r & y`2 = p`2 by A6,EUCLID:56;
    then
A9: x in west_halfline p by A7,TOPREAL1:def 15;
    not x in {p} by A7,A8,TARSKI:def 1;
    hence x in P by A9,XBOOLE_0:def 4;
  end;
  hence thesis by Th3;
end;

theorem
  east_halfline p \ {p} is convex
proof
  set P = east_halfline p \ {p};
  P = {|[ r,p`2 ]| where r is Real : r > p`1 }
  proof
    hereby
      let x be set;
      assume
A1:   x in P;
      then
A2:   x in east_halfline p by XBOOLE_0:def 4;
A3:   not x in {p} by A1,XBOOLE_0:def 4;
      reconsider y = x as Point of TOP-REAL 2 by A1;
A4:   y`1 >= p`1 & y`2 = p`2 by A2,TOPREAL1:def 13;
      now
        assume y`1 = p`1;
        then x = p by A4,TOPREAL3:11;
        hence contradiction by A3,TARSKI:def 1;
      end;
      then
A5:   y`1 > p`1 by A4,REAL_1:def 5;
      x = |[ y`1,p`2 ]| by A4,EUCLID:57;
      hence x in {|[ r,p`2 ]| where r is Real : r > p`1} by A5;
    end;
    let x be set;
    assume x in {|[ r,p`2 ]| where r is Real : r > p`1 };
    then consider r being Real such that
A6: x = |[ r,p`2 ]| and
A7: r > p`1;
    reconsider y = x as Point of TOP-REAL 2 by A6;
A8: y`1 = r & y`2 = p`2 by A6,EUCLID:56;
    then
A9: x in east_halfline p by A7,TOPREAL1:def 13;
    not x in {p} by A7,A8,TARSKI:def 1;
    hence x in P by A9,XBOOLE_0:def 4;
  end;
  hence thesis by Th2;
end;

theorem
  for A being Subset of the carrier of TOP-REAL 2 holds UBD A misses A
proof
  let A be Subset of the carrier of TOP-REAL 2;
  UBD A c= A` by JORDAN2C:30;
  hence thesis by XBOOLE_1:63,79;
end;

theorem Th11:
  for P being Subset of the carrier of TOP-REAL 2,
  p1,p2,q1,q2 being Point of TOP-REAL 2 st
  P is_an_arc_of p1,p2 & p1 <> q1 & p2 <> q2 holds
  not p1 in Segment(P,p1,p2,q1,q2) & not p2 in Segment(P,p1,p2,q1,q2)
proof
  let P be Subset of the carrier of TOP-REAL 2,
  p1,p2,q1,q2 be Point of TOP-REAL 2 such that
A1: P is_an_arc_of p1,p2 and
A2: p1 <> q1 and
A3: p2 <> q2;
A4: Segment(P,p1,p2,q1,q2) = R_Segment(P,p1,p2,q1) /\ L_Segment(P,p1,p2,q2)
  by JORDAN6:def 5;
  not p2 in L_Segment(P,p1,p2,q2) & not p1 in R_Segment(P,p1,p2,q1)
  by A1,A2,A3,JORDAN6:74,75;
  hence thesis by A4,XBOOLE_0:def 3;
end;

definition
  let D be Subset of TOP-REAL 2;
  attr D is with_the_max_arc means
  :Def1:
  D meets Vertical_Line((W-bound D+E-bound D)/2);
end;

registration
  cluster with_the_max_arc -> non empty Subset of TOP-REAL 2;
  coherence
  proof
    let D be Subset of TOP-REAL 2;
    assume D meets Vertical_Line((W-bound D+E-bound D)/2);
    hence thesis by XBOOLE_1:65;
  end;
end;

registration
  cluster -> with_the_max_arc Simple_closed_curve;
  coherence
  proof
    let C be Simple_closed_curve;
A1: Upper_Middle_Point C in C by JORDAN6:83;
    (Upper_Middle_Point C)`1 = (W-bound C+E-bound C)/2 by JORDAN6:80;
    then Upper_Middle_Point C in Vertical_Line((W-bound C+E-bound C)/2)
    by JORDAN6:34;
    hence C meets Vertical_Line((W-bound C+E-bound C)/2) by A1,XBOOLE_0:3;
  end;
end;

registration
  cluster non empty Simple_closed_curve;
  existence
  proof
    consider A being Simple_closed_curve;
    take A;
    thus thesis;
  end;
end;

reserve D for compact with_the_max_arc Subset of TOP-REAL 2;

theorem Th12:
  for D being with_the_max_arc Subset of TOP-REAL 2 holds
  proj2.:(D /\ Vertical_Line((W-bound D + E-bound D) / 2)) is not empty
proof
  let D be with_the_max_arc Subset of TOP-REAL 2;
  set w = (W-bound D + E-bound D) / 2;
  D meets Vertical_Line w by Def1;
  then D /\ Vertical_Line w is non empty by XBOOLE_0:def 7;
  hence thesis by Lm1,RELAT_1:152;
end;

theorem Th13:
  for C being compact Subset of TOP-REAL 2 holds
  proj2.:(C /\ Vertical_Line((W-bound C + E-bound C) / 2))
  is closed bounded_below bounded_above
proof
  let C be compact Subset of TOP-REAL 2;
A1: C is Bounded by JORDAN2C:73;
  set w = (W-bound C + E-bound C) / 2;
  set X = C /\ Vertical_Line w;
A2: X c= C by XBOOLE_1:17;
A3: X is Bounded by A1,JORDAN2C:16,XBOOLE_1:17;
  Vertical_Line w is closed by JORDAN6:33;
  then X is closed by TOPS_1:35;
  hence proj2.:X is closed by A3,JCT_MISC:22;
  proj2.:X is bounded by A1,A2,JCT_MISC:23,JORDAN2C:16;
  hence thesis by XXREAL_2:def 11;
end;

begin :: Middle Points

canceled 9;

theorem Th23:
  Lower_Middle_Point C in C
proof
A1: Lower_Middle_Point C in Lower_Arc C by JORDAN6:81;
  Lower_Arc C c= C by JORDAN6:76;
  hence thesis by A1;
end;

theorem Th24:
  (Lower_Middle_Point C)`2 <> (Upper_Middle_Point C)`2
proof
  set l = Lower_Middle_Point C, u = Upper_Middle_Point C,
  w = (W-bound C+E-bound C)/2;
  assume
A1: l`2 = u`2;
A2: l`1 = w & u`1 = w by JORDAN6:79,80;
  then l = u by A1,TOPREAL3:11;
  then l in Lower_Arc C & l in Upper_Arc C by JORDAN6:81,82;
  then l in Lower_Arc C /\ Upper_Arc C by XBOOLE_0:def 3;
  then l in {W-min(C),E-max(C)} by JORDAN6:def 9;
  then l = W-min(C) or l = E-max(C) by TARSKI:def 2;
  then W-bound C = w or E-bound C = w by A2,EUCLID:56;
  hence thesis by SPRECT_1:33;
end;

theorem
  Lower_Middle_Point C <> Upper_Middle_Point C
proof
  (Lower_Middle_Point C)`2 <> (Upper_Middle_Point C)`2 by Th24;
  hence thesis;
end;

begin :: Upper_Arc and Lower_Arc

theorem Th26:
  W-bound C = W-bound Upper_Arc C
proof
  Upper_Arc C c= C by JORDAN6:76;
  then
A1: W-bound C <= W-bound Upper_Arc C by PSCOMP_1:132;
  W-bound Upper_Arc C <= W-bound C
  proof
    assume
A2: W-bound Upper_Arc C > W-bound C;
A3: (W-min C)`1 = W-bound C by EUCLID:56;
A4: west_halfline W-min C misses Upper_Arc C
    proof
      assume west_halfline W-min C meets Upper_Arc C;
      then consider p being set such that
A5:   p in west_halfline W-min C and
A6:   p in Upper_Arc C by XBOOLE_0:3;
      reconsider p as Point of TOP-REAL 2 by A5;
      p`1 >= W-bound Upper_Arc C by A6,PSCOMP_1:71;
      then W-bound C < p`1 by A2,XXREAL_0:2;
      hence contradiction by A3,A5,TOPREAL1:def 15;
    end;
A7: W-min C in west_halfline W-min C by TOPREAL1:45;
    W-min C in Upper_Arc C by JORDAN7:1;
    hence contradiction by A4,A7,XBOOLE_0:3;
  end;
  hence thesis by A1,XXREAL_0:1;
end;

theorem Th27:
  E-bound C = E-bound Upper_Arc C
proof
  Upper_Arc C c= C by JORDAN6:76;
  then
A1: E-bound C >= E-bound Upper_Arc C by PSCOMP_1:130;
  E-bound Upper_Arc C >= E-bound C
  proof
    assume
A2: E-bound Upper_Arc C < E-bound C;
A3: (E-max C)`1 = E-bound C by EUCLID:56;
A4: east_halfline E-max C misses Upper_Arc C
    proof
      assume east_halfline E-max C meets Upper_Arc C;
      then consider p being set such that
A5:   p in east_halfline E-max C and
A6:   p in Upper_Arc C by XBOOLE_0:3;
      reconsider p as Point of TOP-REAL 2 by A5;
      p`1 <= E-bound Upper_Arc C by A6,PSCOMP_1:71;
      then E-bound C > p`1 by A2,XXREAL_0:2;
      hence contradiction by A3,A5,TOPREAL1:def 13;
    end;
A7: E-max C in east_halfline E-max C by TOPREAL1:45;
    E-max C in Upper_Arc C by JORDAN7:1;
    hence contradiction by A4,A7,XBOOLE_0:3;
  end;
  hence thesis by A1,XXREAL_0:1;
end;

theorem Th28:
  W-bound C = W-bound Lower_Arc C
proof
  Lower_Arc C c= C by JORDAN6:76;
  then
A1: W-bound C <= W-bound Lower_Arc C by PSCOMP_1:132;
  W-bound Lower_Arc C <= W-bound C
  proof
    assume
A2: W-bound Lower_Arc C > W-bound C;
A3: (W-min C)`1 = W-bound C by EUCLID:56;
A4: west_halfline W-min C misses Lower_Arc C
    proof
      assume west_halfline W-min C meets Lower_Arc C;
      then consider p being set such that
A5:   p in west_halfline W-min C and
A6:   p in Lower_Arc C by XBOOLE_0:3;
      reconsider p as Point of TOP-REAL 2 by A5;
      p`1 >= W-bound Lower_Arc C by A6,PSCOMP_1:71;
      then W-bound C < p`1 by A2,XXREAL_0:2;
      hence contradiction by A3,A5,TOPREAL1:def 15;
    end;
A7: W-min C in west_halfline W-min C by TOPREAL1:45;
    W-min C in Lower_Arc C by JORDAN7:1;
    hence contradiction by A4,A7,XBOOLE_0:3;
  end;
  hence thesis by A1,XXREAL_0:1;
end;

theorem Th29:
  E-bound C = E-bound Lower_Arc C
proof
  Lower_Arc C c= C by JORDAN6:76;
  then
A1: E-bound C >= E-bound Lower_Arc C by PSCOMP_1:130;
  E-bound Lower_Arc C >= E-bound C
  proof
    assume
A2: E-bound Lower_Arc C < E-bound C;
A3: (E-max C)`1 = E-bound C by EUCLID:56;
A4: east_halfline E-max C misses Lower_Arc C
    proof
      assume east_halfline E-max C meets Lower_Arc C;
      then consider p being set such that
A5:   p in east_halfline E-max C and
A6:   p in Lower_Arc C by XBOOLE_0:3;
      reconsider p as Point of TOP-REAL 2 by A5;
      p`1 <= E-bound Lower_Arc C by A6,PSCOMP_1:71;
      then E-bound C > p`1 by A2,XXREAL_0:2;
      hence contradiction by A3,A5,TOPREAL1:def 13;
    end;
A7: E-max C in east_halfline E-max C by TOPREAL1:45;
    E-max C in Lower_Arc C by JORDAN7:1;
    hence contradiction by A4,A7,XBOOLE_0:3;
  end;
  hence thesis by A1,XXREAL_0:1;
end;

theorem Th30:
  Upper_Arc C /\ Vertical_Line((W-bound C + E-bound C) / 2) is not empty &
  proj2.:(Upper_Arc C /\ Vertical_Line((W-bound C + E-bound C) / 2))
  is not empty
proof
  set w = (W-bound C + E-bound C) / 2;
A1: Upper_Arc C is_an_arc_of W-min C,E-max C by JORDAN6:def 8;
A2: (W-min C)`1 = W-bound C & (E-max C)`1 = E-bound C by EUCLID:56;
  W-bound C < E-bound C by SPRECT_1:33;
  then (W-min C)`1 <= w & w <= (E-max C)`1 by A2,XREAL_1:228;
  then Upper_Arc C meets Vertical_Line w by A1,JORDAN6:64;
  then Upper_Arc C /\ Vertical_Line w is non empty by XBOOLE_0:def 7;
  hence thesis by Lm1,RELAT_1:152;
end;

theorem Th31:
  Lower_Arc C /\ Vertical_Line((W-bound C + E-bound C) / 2) is not empty &
  proj2.:(Lower_Arc C /\ Vertical_Line((W-bound C + E-bound C) / 2))
  is not empty
proof
  set w = (W-bound C + E-bound C) / 2;
  Lower_Arc C is_an_arc_of E-max C,W-min C by JORDAN6:def 9;
  then
A1: Lower_Arc C is_an_arc_of W-min C,E-max C by JORDAN5B:14;
A2: (W-min C)`1 = W-bound C & (E-max C)`1 = E-bound C by EUCLID:56;
  W-bound C < E-bound C by SPRECT_1:33;
  then (W-min C)`1 <= w & w <= (E-max C)`1 by A2,XREAL_1:228;
  then Lower_Arc C meets Vertical_Line w by A1,JORDAN6:64;
  then Lower_Arc C /\ Vertical_Line w is non empty by XBOOLE_0:def 7;
  hence thesis by Lm1,RELAT_1:152;
end;

theorem
  for P being compact connected Subset of TOP-REAL 2 st P c= C
  & W-min C in P & E-max C in P holds Upper_Arc C c= P or Lower_Arc C c= P
proof
  let P be compact connected Subset of TOP-REAL 2 such that
A1: P c= C and
A2: W-min C in P and
A3: E-max C in P;
A4: now
    given p being Point of TOP-REAL 2 such that
A5: P = {p};
    W-min C = p & E-max C = p by A2,A3,A5,TARSKI:def 1;
    hence contradiction by TOPREAL5:25;
  end;
  per cases by A1,A2,A4,BORSUK_4:81;
  suppose ex p1, p2 being Point of TOP-REAL 2 st P is_an_arc_of p1,p2;
    hence Upper_Arc C c= P or Lower_Arc C c= P by A1,A2,A3,JORDAN16:37;
  end;
  suppose P = C;
    hence Upper_Arc C c= P or Lower_Arc C c= P by JORDAN6:76;
  end;
end;

registration
  let C;
  cluster Lower_Arc C -> with_the_max_arc;
  coherence
  proof
    W-bound C = W-bound Lower_Arc C &
    E-bound C = E-bound Lower_Arc C by Th28,Th29;
    hence Lower_Arc C meets
    Vertical_Line((W-bound Lower_Arc C+E-bound Lower_Arc C)/2) by JORDAN6:77;
  end;
  cluster Upper_Arc C -> with_the_max_arc;
  coherence
  proof
    W-bound C = W-bound Upper_Arc C &
    E-bound C = E-bound Upper_Arc C by Th26,Th27;
    hence Upper_Arc C meets
    Vertical_Line((W-bound Upper_Arc C+E-bound Upper_Arc C)/2) by JORDAN6:78;
  end;
end;

begin :: UMP and LMP

definition
  let P be Subset of the carrier of TOP-REAL 2;
  func UMP P -> Point of TOP-REAL 2 equals

  |[ (E-bound P + W-bound P) / 2 ,
  sup (proj2.:(P /\ Vertical_Line ((E-bound P + W-bound P) / 2))) ]|;
  correctness;
  func LMP P -> Point of TOP-REAL 2 equals

  |[ (E-bound P + W-bound P) / 2 ,
  inf (proj2.:(P /\ Vertical_Line ((E-bound P + W-bound P) / 2))) ]|;
  correctness;
end;

canceled 4;

theorem Th37:
  for C being non vertical compact Subset of TOP-REAL 2 holds UMP C <> W-min C
proof
  let C being non vertical compact Subset of TOP-REAL 2;
  assume
A1: UMP C = W-min C;
A2: (W-min C)`1 = W-bound C by EUCLID:56;
  (UMP C)`1 = (W-bound C + E-bound C)/2 by EUCLID:56;
  hence thesis by A1,A2,SPRECT_1:33;
end;

theorem Th38:
  for C being non vertical compact Subset of TOP-REAL 2 holds UMP C <> E-max C
proof
  let C being non vertical compact Subset of TOP-REAL 2;
  assume
A1: UMP C = E-max C;
A2: (E-max C)`1 = E-bound C by EUCLID:56;
  (UMP C)`1 = (W-bound C + E-bound C)/2 by EUCLID:56;
  hence thesis by A1,A2,SPRECT_1:33;
end;

theorem Th39:
  for C being non vertical compact Subset of TOP-REAL 2 holds LMP C <> W-min C
proof
  let C being non vertical compact Subset of TOP-REAL 2;
  assume
A1: LMP C = W-min C;
A2: (W-min C)`1 = W-bound C by EUCLID:56;
  (LMP C)`1 = (W-bound C + E-bound C)/2 by EUCLID:56;
  hence thesis by A1,A2,SPRECT_1:33;
end;

theorem Th40:
  for C being non vertical compact Subset of TOP-REAL 2 holds LMP C <> E-max C
proof
  let C being non vertical compact Subset of TOP-REAL 2;
  assume
A1: LMP C = E-max C;
A2: (E-max C)`1 = E-bound C by EUCLID:56;
  (LMP C)`1 = (W-bound C + E-bound C)/2 by EUCLID:56;
  hence thesis by A1,A2,SPRECT_1:33;
end;

theorem
  for C being compact Subset of TOP-REAL 2 st
  p in C /\ Vertical_Line ((W-bound C + E-bound C) / 2) holds p`2 <= (UMP C)`2
proof
  let C be compact Subset of TOP-REAL 2 such that
A1: p in C /\ Vertical_Line ((W-bound C + E-bound C) / 2);
A2: (UMP C)`2 =
  sup (proj2.:(C /\ Vertical_Line ((E-bound C + W-bound C) / 2)))
  by EUCLID:56;
A3: proj2.:(C /\ Vertical_Line ((E-bound C + W-bound C) / 2)) is non empty
  bounded_above by A1,Lm1,Th13,RELAT_1:152;
  p`2 = proj2.p by PSCOMP_1:def 29;
  then p`2 in proj2.:(C /\ Vertical_Line ((E-bound C + W-bound C) / 2))
  by A1,FUNCT_2:43;
  hence thesis by A2,A3,SEQ_4:def 4;
end;

theorem
  for C being compact Subset of TOP-REAL 2 st
  p in C /\ Vertical_Line ((W-bound C + E-bound C) / 2) holds (LMP C)`2 <= p`2
proof
  let C be compact Subset of TOP-REAL 2 such that
A1: p in C /\ Vertical_Line ((W-bound C + E-bound C) / 2);
A2: (LMP C)`2 =
  inf (proj2.:(C /\ Vertical_Line ((E-bound C + W-bound C) / 2)))
  by EUCLID:56;
A3: proj2.:(C /\ Vertical_Line ((E-bound C + W-bound C) / 2)) is non empty
  bounded_below by A1,Lm1,Th13,RELAT_1:152;
  p`2 = proj2.p by PSCOMP_1:def 29;
  then p`2 in proj2.:(C /\ Vertical_Line ((E-bound C + W-bound C) / 2))
  by A1,FUNCT_2:43;
  hence thesis by A2,A3,SEQ_4:def 5;
end;

theorem Th43:
  UMP D in D
proof
  set w = (W-bound D + E-bound D) / 2;
  set X = D /\ Vertical_Line w;
  D meets Vertical_Line w by Def1;
  then
A1: X is non empty by XBOOLE_0:def 7;
A2: proj2.:X is closed by Th13;
  proj2.:X is bounded_above by Th13;
  then sup (proj2.:X) in proj2.:X by A1,A2,Lm1,RCOMP_1:30,RELAT_1:152;
  then consider x being Point of TOP-REAL 2 such that
A3: x in X and
A4: sup (proj2.:X) = proj2.x by Lm2;
  x in Vertical_Line w by A3,XBOOLE_0:def 3;
  then
A5: x`1 = w by JORDAN6:34
    .= (UMP D)`1 by EUCLID:56;
  x`2 = sup (proj2.:X) by A4,PSCOMP_1:def 29
    .= (UMP D)`2 by EUCLID:56;
  then x = UMP D by A5,TOPREAL3:11;
  hence thesis by A3,XBOOLE_0:def 3;
end;

theorem Th44:
  LMP D in D
proof
  set w = (W-bound D + E-bound D) / 2;
  set X = D /\ Vertical_Line w;
A1: proj2.:X is non empty by Th12;
A2: proj2.:X is closed by Th13;
  proj2.:X is bounded_below by Th13;
  then inf (proj2.:X) in proj2.:X by A1,A2,RCOMP_1:31;
  then consider x being Point of TOP-REAL 2 such that
A3: x in X and
A4: inf (proj2.:X) = proj2.x by Lm2;
  x in Vertical_Line w by A3,XBOOLE_0:def 3;
  then
A5: x`1 = w by JORDAN6:34
    .= (LMP D)`1 by EUCLID:56;
  x`2 = inf (proj2.:X) by A4,PSCOMP_1:def 29
    .= (LMP D)`2 by EUCLID:56;
  then x = LMP D by A5,TOPREAL3:11;
  hence thesis by A3,XBOOLE_0:def 3;
end;

theorem Th45:
  LSeg(UMP P, |[ (W-bound P + E-bound P) / 2, N-bound P]|) is vertical
proof
  set w = (W-bound P + E-bound P) / 2;
A1: |[w,N-bound P]|`1 = w by EUCLID:56;
  (UMP P)`1 = w by EUCLID:56;
  hence thesis by A1,SPPOL_1:37;
end;

theorem Th46:
  LSeg(LMP P, |[ (W-bound P + E-bound P) / 2, S-bound P]|) is vertical
proof
  set w = (W-bound P + E-bound P) / 2;
A1: |[w,S-bound P]|`1 = w by EUCLID:56;
  (LMP P)`1 = w by EUCLID:56;
  hence thesis by A1,SPPOL_1:37;
end;

theorem Th47:
  LSeg(UMP D, |[ (W-bound D + E-bound D) / 2, N-bound D]|) /\ D = { UMP D }
proof
  set C = D;
  set w = (W-bound C + E-bound C) / 2;
  set L = LSeg(UMP C, |[w,N-bound C]|);
  set X = C /\ Vertical_Line(w);
A1: UMP C in L by TOPREAL1:6;
  hereby
    let x be set;
    assume
A2: x in L /\ C;
    then
A3: x in L & x in C by XBOOLE_0:def 3;
    reconsider y = x as Point of TOP-REAL 2 by A2;
A4: |[w,N-bound C]|`2 = N-bound C by EUCLID:56;
A5: (UMP C)`1 = w by EUCLID:56;
A6: (UMP C)`2 = sup (proj2.:X) by EUCLID:56;
A7: proj2.:X is bounded_above by Th13;
    L is vertical by Th45;
    then
A8: y`1 = w by A1,A3,A5,SPPOL_1:def 3;
A9: y`2 = proj2.y by PSCOMP_1:def 29;
    y in Vertical_Line (w) by A8,JORDAN6:34;
    then y in X by A3,XBOOLE_0:def 3;
    then y`2 in proj2.:X by A9,FUNCT_2:43;
    then
A10: y`2 <= sup (proj2.:X) by A7,SEQ_4:def 4;
    UMP C in C by Th43;
    then (UMP C)`2 <= N-bound C by PSCOMP_1:71;
    then (UMP C)`2 <= y`2 by A3,A4,TOPREAL1:10;
    then y`2 = sup (proj2.:X) by A6,A10,XXREAL_0:1;
    then y = UMP C by A5,A6,A8,TOPREAL3:11;
    hence x in {UMP C} by TARSKI:def 1;
  end;
  let x be set;
  assume x in {UMP C};
  then
A11: x = UMP C by TARSKI:def 1;
  UMP C in C by Th43;
  hence thesis by A1,A11,XBOOLE_0:def 3;
end;

theorem Th48:
  LSeg(LMP D, |[ (W-bound D + E-bound D) / 2, S-bound D]|) /\ D = { LMP D }
proof
  set C = D;
  set w = (W-bound C + E-bound C) / 2;
  set L = LSeg(LMP C, |[w,S-bound C]|);
  set X = C /\ Vertical_Line (w);
A1: LMP C in L by TOPREAL1:6;
  hereby
    let x be set;
    assume
A2: x in L /\ C;
    then
A3: x in L & x in C by XBOOLE_0:def 3;
    reconsider y = x as Point of TOP-REAL 2 by A2;
A4: |[w,S-bound C]|`2 = S-bound C by EUCLID:56;
A5: (LMP C)`1 = w by EUCLID:56;
A6: (LMP C)`2 = inf (proj2.:X) by EUCLID:56;
A7: proj2.:X is bounded_below by Th13;
    L is vertical by Th46;
    then
A8: y`1 = w by A1,A3,A5,SPPOL_1:def 3;
A9: y`2 = proj2.y by PSCOMP_1:def 29;
    y in Vertical_Line (w) by A8,JORDAN6:34;
    then y in X by A3,XBOOLE_0:def 3;
    then y`2 in proj2.:X by A9,FUNCT_2:43;
    then
A10: y`2 >= inf (proj2.:X) by A7,SEQ_4:def 5;
    LMP C in C by Th44;
    then (LMP C)`2 >= S-bound C by PSCOMP_1:71;
    then (LMP C)`2 >= y`2 by A3,A4,TOPREAL1:10;
    then y`2 = inf (proj2.:X) by A6,A10,XXREAL_0:1;
    then y = LMP C by A5,A6,A8,TOPREAL3:11;
    hence x in {LMP C} by TARSKI:def 1;
  end;
  let x be set;
  assume x in {LMP C};
  then
A11: x = LMP C by TARSKI:def 1;
  LMP C in C by Th44;
  hence thesis by A1,A11,XBOOLE_0:def 3;
end;

theorem Th49:
  (LMP C)`2 < (UMP C)`2
proof
  set w = (E-bound C + W-bound C) / 2;
  set X = C /\ Vertical_Line (w);
A1: (UMP C)`2 = sup (proj2.:X) & (LMP C)`2 = inf (proj2.:X) by EUCLID:56;
  C is Bounded by JORDAN2C:73;
  then X is Bounded by TOPREAL6:98;
  then
A2: proj2.:X is bounded by JCT_MISC:23;
  set u = Upper_Middle_Point C, l = Lower_Middle_Point C;
A3: u`2 <> l`2 by Th24;
  u`1 = w by JORDAN6:80;
  then u in C & u in Vertical_Line (w) by JORDAN6:34,83;
  then
A4: u in X by XBOOLE_0:def 3;
  u`2 = proj2.u by PSCOMP_1:def 29;
  then
A5: u`2 in proj2.:X by A4,FUNCT_2:43;
  l`1 = w by JORDAN6:79;
  then l in C & l in Vertical_Line (w) by Th23,JORDAN6:34;
  then
A6: l in X by XBOOLE_0:def 3;
  l`2 = proj2.l by PSCOMP_1:def 29;
  then l`2 in proj2.:X by A6,FUNCT_2:43;
  hence thesis by A1,A2,A3,A5,SEQ_4:25;
end;

theorem Th50:
  UMP C <> LMP C
proof
  assume
A1: UMP C = LMP C;
  (LMP C)`2 < (UMP C)`2 by Th49;
  hence contradiction by A1;
end;

theorem Th51:
  S-bound C < (UMP C)`2
proof
  set u = UMP C, l = LMP C;
  u in C by Th43;
  then
A1: S-bound C <= u`2 by PSCOMP_1:71;
  now
    assume
A2: S-bound C = u`2;
A3: l`2 < u`2 by Th49;
    l in C by Th44;
    hence contradiction by A2,A3,PSCOMP_1:71;
  end;
  hence S-bound C < u`2 by A1,REAL_1:def 5;
end;

theorem Th52:
  (UMP C)`2 <= N-bound C
proof
  UMP C in C by Th43;
  hence (UMP C)`2 <= N-bound C by PSCOMP_1:71;
end;

theorem Th53:
  S-bound C <= (LMP C)`2
proof
  LMP C in C by Th44;
  hence S-bound C <= (LMP C)`2 by PSCOMP_1:71;
end;

theorem Th54:
  (LMP C)`2 < N-bound C
proof
  set u = UMP C, l = LMP C;
  l in C by Th44;
  then
A1: l`2 <= N-bound C by PSCOMP_1:71;
  now
    assume
A2: N-bound C = l`2;
A3: l`2 < u`2 by Th49;
    u in C by Th43;
    hence contradiction by A2,A3,PSCOMP_1:71;
  end;
  hence l`2 < N-bound C by A1,REAL_1:def 5;
end;

theorem Th55:
  LSeg(UMP C, |[ (W-bound C + E-bound C) / 2, N-bound C]|) misses
  LSeg(LMP C, |[ (W-bound C + E-bound C) / 2, S-bound C]|)
proof
  set w = (W-bound C + E-bound C) / 2;
  assume LSeg(UMP C, |[w, N-bound C]|) meets LSeg(LMP C, |[w, S-bound C]|);
  then consider x being set such that
A1: x in LSeg(UMP C, |[w, N-bound C]|) and
A2: x in LSeg(LMP C, |[w, S-bound C]|) by XBOOLE_0:3;
  reconsider x as Point of TOP-REAL 2 by A1;
  |[w, S-bound C]|`2 = S-bound C by EUCLID:56;
  then |[w, S-bound C]|`2 <= (LMP C)`2 by Th53;
  then
A3: x`2 <= (LMP C)`2 by A2,TOPREAL1:10;
  |[w, N-bound C]|`2 = N-bound C by EUCLID:56;
  then (UMP C)`2 <= |[w, N-bound C]|`2 by Th52;
  then x`2 >= (UMP C)`2 by A1,TOPREAL1:10;
  hence contradiction by A3,Th49,XXREAL_0:2;
end;

theorem
  for A, B being Subset of TOP-REAL 2 st A c= B &
  W-bound A + E-bound A = W-bound B + E-bound B &
  A /\ Vertical_Line ((W-bound A + E-bound A) / 2) is non empty &
  proj2.:(B /\ Vertical_Line ((W-bound A + E-bound A) / 2)) is bounded_above
  holds (UMP A)`2 <= (UMP B)`2
proof
  let A, B be Subset of TOP-REAL 2;
  assume that
A1: A c= B and
A2: W-bound A + E-bound A = W-bound B + E-bound B and
A3: A /\ Vertical_Line ((W-bound A + E-bound A) / 2) is non empty and
A4: proj2.:(B /\ Vertical_Line ((W-bound A + E-bound A) / 2))
  is bounded_above;
  set w = (W-bound A + E-bound A) / 2;
A5: proj2.:(A /\ Vertical_Line w) is non empty by A3,Lm1,RELAT_1:152;
  A /\ Vertical_Line w c= B /\ Vertical_Line w by A1,XBOOLE_1:26;
  then sup(proj2.:(A /\ Vertical_Line w)) <=
  sup(proj2.:(B /\ Vertical_Line w)) by A4,A5,RELAT_1:156,SEQ_4:65;
  then (UMP A)`2 <= sup(proj2.:(B /\ Vertical_Line w)) by EUCLID:56;
  hence thesis by A2,EUCLID:56;
end;

theorem
  for A, B being Subset of TOP-REAL 2 st A c= B &
  W-bound A + E-bound A = W-bound B + E-bound B &
  A /\ Vertical_Line ((W-bound A + E-bound A) / 2) is non empty &
  proj2.:(B /\ Vertical_Line ((W-bound A + E-bound A) / 2)) is bounded_below
  holds (LMP B)`2 <= (LMP A)`2
proof
  let A, B be Subset of TOP-REAL 2;
  assume that
A1: A c= B and
A2: W-bound A + E-bound A = W-bound B + E-bound B and
A3: A /\ Vertical_Line ((W-bound A + E-bound A) / 2) is non empty and
A4: proj2.:(B /\ Vertical_Line ((W-bound A + E-bound A) / 2))
  is bounded_below;
  set w = (W-bound A + E-bound A) / 2;
A5: proj2.:(A /\ Vertical_Line w) is non empty by A3,Lm1,RELAT_1:152;
  A /\ Vertical_Line w c= B /\ Vertical_Line w by A1,XBOOLE_1:26;
  then inf(proj2.:(A /\ Vertical_Line w)) >=
  inf(proj2.:(B /\ Vertical_Line w)) by A4,A5,RELAT_1:156,SEQ_4:64;
  then (LMP A)`2 >= inf(proj2.:(B /\ Vertical_Line w)) by EUCLID:56;
  hence thesis by A2,EUCLID:56;
end;

theorem
  for A, B being Subset of TOP-REAL 2 st A c= B & UMP B in A &
  A /\ Vertical_Line ((W-bound A + E-bound A) / 2) is non empty &
  proj2.:(B /\ Vertical_Line ((W-bound B + E-bound B) / 2)) is bounded_above &
  W-bound A + E-bound A = W-bound B + E-bound B holds UMP A = UMP B
proof
  let A, B be Subset of TOP-REAL 2 such that
A1: A c= B and
A2: UMP B in A and
A3: A /\ Vertical_Line ((W-bound A + E-bound A) / 2) is non empty and
A4: proj2.:(B /\ Vertical_Line ((W-bound B + E-bound B) / 2))
  is bounded_above and
A5: W-bound A + E-bound A = W-bound B + E-bound B;
  set w = (W-bound A + E-bound A)/2;
A6: (UMP A)`2 = sup (proj2.:(A /\ Vertical_Line w)) by EUCLID:56;
A7: (UMP B)`2 = sup (proj2.:(B /\ Vertical_Line w)) by A5,EUCLID:56;
A8: proj2.:(A /\ Vertical_Line w) is non empty by A3,Lm1,RELAT_1:152;
A9: A /\ Vertical_Line w c= B /\ Vertical_Line w by A1,XBOOLE_1:26;
  then
A10: proj2.:(A /\ Vertical_Line w) c=
  proj2.:(B /\ Vertical_Line w) by RELAT_1:156;
A11: proj2.:(A /\ Vertical_Line w) is bounded_above by A4,A5,A9,XXREAL_2:43
  ,RELAT_1:156;
A12: (UMP A)`1 = w by EUCLID:56;
A13: (UMP B)`1 = w by A5,EUCLID:56;
A14: for r being real number st r in proj2.:(A /\ Vertical_Line w) holds
  (UMP B)`2 >= r by A4,A5,A7,A10,SEQ_4:def 4;
  for s being real number st 0 < s ex r being real number st
  r in proj2.:(A /\ Vertical_Line w) & (UMP B)`2 - s < r
  proof
    let s be real number;
    assume
A15: 0 < s;
    take (UMP B)`2;
A16: proj2.UMP B = (UMP B)`2 by PSCOMP_1:def 29;
    UMP B in Vertical_Line w by A13,JORDAN6:34;
    then UMP B in A /\ Vertical_Line w by A2,XBOOLE_0:def 3;
    hence (UMP B)`2 in proj2.:(A /\ Vertical_Line w) by A16,FUNCT_2:43;
    (UMP B)`2 - s < (UMP B)`2 - 0 by A15,REAL_1:92;
    hence (UMP B)`2 - s < (UMP B)`2;
  end;
  then (UMP A)`2 = (UMP B)`2 by A6,A8,A11,A14,SEQ_4:def 4;
  hence UMP A = UMP B by A12,A13,TOPREAL3:11;
end;

theorem
  for A, B being Subset of TOP-REAL 2 st A c= B & LMP B in A &
  A /\ Vertical_Line ((W-bound A + E-bound A) / 2) is non empty &
  proj2.:(B /\ Vertical_Line ((W-bound B + E-bound B) / 2)) is bounded_below &
  W-bound A + E-bound A = W-bound B + E-bound B holds LMP A = LMP B
proof
  let A, B be Subset of TOP-REAL 2 such that
A1: A c= B and
A2: LMP B in A and
A3: A /\ Vertical_Line ((W-bound A + E-bound A) / 2) is non empty and
A4: proj2.:(B /\ Vertical_Line ((W-bound B + E-bound B) / 2))
  is bounded_below and
A5: W-bound A + E-bound A = W-bound B + E-bound B;
  set w = (W-bound A + E-bound A)/2;
A6: (LMP A)`2 = inf (proj2.:(A /\ Vertical_Line w)) by EUCLID:56;
A7: (LMP B)`2 = inf (proj2.:(B /\ Vertical_Line w)) by A5,EUCLID:56;
A8: proj2.:(A /\ Vertical_Line w) is non empty by A3,Lm1,RELAT_1:152;
A9: A /\ Vertical_Line w c= B /\ Vertical_Line w by A1,XBOOLE_1:26;
  then
A10: proj2.:(A /\ Vertical_Line w) c=
  proj2.:(B /\ Vertical_Line w) by RELAT_1:156;
A11: proj2.:(A /\ Vertical_Line w) is bounded_below by A4,A5,A9,XXREAL_2:44
  ,RELAT_1:156;
A12: (LMP A)`1 = w by EUCLID:56;
A13: (LMP B)`1 = w by A5,EUCLID:56;
A14: for r being real number st r in proj2.:(A /\ Vertical_Line w) holds
  (LMP B)`2 <= r by A4,A5,A7,A10,SEQ_4:def 5;
  for s being real number st 0 < s ex r being real number st
  r in proj2.:(A /\ Vertical_Line w) & r < (LMP B)`2 + s
  proof
    let s be real number;
    assume
A15: 0 < s;
    take (LMP B)`2;
A16: proj2.LMP B = (LMP B)`2 by PSCOMP_1:def 29;
    LMP B in Vertical_Line w by A13,JORDAN6:34;
    then LMP B in A /\ Vertical_Line w by A2,XBOOLE_0:def 3;
    hence (LMP B)`2 in proj2.:(A /\ Vertical_Line w) by A16,FUNCT_2:43;
    (LMP B)`2 + 0 < (LMP B)`2 + s by A15,XREAL_1:8;
    hence (LMP B)`2 < (LMP B)`2 + s;
  end;
  then (LMP A)`2 = (LMP B)`2 by A6,A8,A11,A14,SEQ_4:def 5;
  hence LMP A = LMP B by A12,A13,TOPREAL3:11;
end;

theorem
  (UMP Upper_Arc C)`2 <= N-bound C
proof
  set w = (E-bound C + W-bound C) / 2;
A1: W-bound C = W-bound Upper_Arc C & E-bound C = E-bound Upper_Arc C
  by Th26,Th27;
A2: proj2.:(Upper_Arc C /\ Vertical_Line w) is non empty by Th30;
A3: proj2.:(C /\ Vertical_Line w) is bounded_above by Th13;
  Upper_Arc C c= C by JORDAN6:76;
  then Upper_Arc C /\ Vertical_Line w c= C /\ Vertical_Line w by XBOOLE_1:26;
  then
A4: sup(proj2.:(Upper_Arc C /\ Vertical_Line w)) <=
  sup(proj2.:(C /\ Vertical_Line w)) by A2,A3,RELAT_1:156,SEQ_4:65;
A5: (UMP Upper_Arc C)`2 = sup(proj2.:(Upper_Arc C /\ Vertical_Line w))
  by A1,EUCLID:56;
A6: (UMP C)`2 = sup(proj2.:(C /\ Vertical_Line w)) by EUCLID:56;
  (UMP C)`2 <= N-bound C by Th52;
  hence thesis by A4,A5,A6,XXREAL_0:2;
end;

theorem
  S-bound C <= (LMP Lower_Arc C)`2
proof
  set w = (E-bound C + W-bound C) / 2;
A1: W-bound C = W-bound Lower_Arc C & E-bound C = E-bound Lower_Arc C
  by Th28,Th29;
A2: proj2.:(Lower_Arc C /\ Vertical_Line w) is non empty by Th31;
A3: proj2.:(C /\ Vertical_Line w) is bounded_below by Th13;
  Lower_Arc C c= C by JORDAN6:76;
  then Lower_Arc C /\ Vertical_Line w c= C /\ Vertical_Line w by XBOOLE_1:26;
  then
A4: inf(proj2.:(Lower_Arc C /\ Vertical_Line w)) >=
  inf(proj2.:(C /\ Vertical_Line w)) by A2,A3,RELAT_1:156,SEQ_4:64;
A5: (LMP Lower_Arc C)`2 = inf(proj2.:(Lower_Arc C /\ Vertical_Line w))
  by A1,EUCLID:56;
A6: (LMP C)`2 = inf(proj2.:(C /\ Vertical_Line w)) by EUCLID:56;
  S-bound C <= (LMP C)`2 by Th53;
  hence thesis by A4,A5,A6,XXREAL_0:2;
end;

theorem
  not (LMP C in Lower_Arc C & UMP C in Lower_Arc C)
proof
  assume
A1: LMP C in Lower_Arc C & UMP C in Lower_Arc C;
A2: UMP C in C & LMP C in C by Th43,Th44;
  set n = |[ (W-bound C + E-bound C) / 2, N-bound C ]|;
  set s = |[ (W-bound C + E-bound C) / 2, S-bound C ]|;
  set S1 = LSeg(n, UMP C);
  set S2 = LSeg(LMP C, s);
A3: UMP C <> W-min C by Th37;
A4: UMP C <> E-max C by Th38;
A5: LMP C <> W-min C by Th39;
A6: LMP C <> E-max C by Th40;
A7: LMP C <> UMP C by Th50;
A8: Upper_Arc(C) /\ Lower_Arc(C) = {W-min(C),E-max(C)} by JORDAN6:def 9;
A9: Upper_Arc C c= C by JORDAN6:76;
A10: Lower_Arc C c= C by JORDAN6:76;
A11: Upper_Arc C is_an_arc_of W-min C,E-max C by JORDAN6:def 8;
A12: Lower_Arc C is_an_arc_of E-max C,W-min C by JORDAN6:def 9;
A13: now
    assume UMP C in Upper_Arc C;
    then UMP C in Upper_Arc C /\ Lower_Arc C by A1,XBOOLE_0:def 3;
    hence contradiction by A3,A4,A8,TARSKI:def 2;
  end;
A14: now
    assume LMP C in Upper_Arc C;
    then LMP C in Upper_Arc C /\ Lower_Arc C by A1,XBOOLE_0:def 3;
    hence contradiction by A5,A6,A8,TARSKI:def 2;
  end;
A15: (UMP C)`1 = (W-bound C + E-bound C)/2 by EUCLID:56;
A16: (LMP C)`1 = (W-bound C + E-bound C)/2 by EUCLID:56;
A17: n`1 = (W-bound C + E-bound C)/2 by EUCLID:56;
A18: s`1 = (W-bound C + E-bound C)/2 by EUCLID:56;
A19: s`2 = S-bound C & n`2 = N-bound C by EUCLID:56;
A20: (W-min C)`1 = W-bound C by EUCLID:56;
A21: (E-max C)`1 = E-bound C by EUCLID:56;
A22: S1 is vertical by A15,A17,SPPOL_1:37;
A23: S2 is vertical by A16,A18,SPPOL_1:37;
A24: UMP C in S1 by TOPREAL1:6;
A25: LMP C in S2 by TOPREAL1:6;
A26: W-bound C < E-bound C by SPRECT_1:33;
A27: S1 misses S2 by Th55;
A28: for p being Point of TOP-REAL 2 st p in Upper_Arc C holds
  s`2<=p`2 & p`2<=n`2 by A9,A19,PSCOMP_1:71;
A29: for p being Point of TOP-REAL 2 st p in Upper_Arc C holds
  (W-min C)`1<=p`1 & p`1<=(E-max C)`1 by A9,A20,A21,PSCOMP_1:71;
A30: Upper_Arc C misses S1
  proof
    assume Upper_Arc C meets S1;
    then consider x being set such that
A31: x in Upper_Arc C and
A32: x in S1 by XBOOLE_0:3;
A33: S1 /\ C = {UMP C} by Th47;
    x in S1 /\ C by A9,A31,A32,XBOOLE_0:def 3;
    then
A34: x = UMP C by A33,TARSKI:def 1;
    then x in Lower_Arc C /\ Upper_Arc C by A1,A31,XBOOLE_0:def 3;
    hence contradiction by A3,A4,A8,A34,TARSKI:def 2;
  end;
A35: Upper_Arc C misses S2
  proof
    assume Upper_Arc C meets S2;
    then consider x being set such that
A36: x in Upper_Arc C and
A37: x in S2 by XBOOLE_0:3;
A38: S2 /\ C = {LMP C} by Th48;
    x in S2 /\ C by A9,A36,A37,XBOOLE_0:def 3;
    then
A39: x = LMP C by A38,TARSKI:def 1;
    then x in Lower_Arc C /\ Upper_Arc C by A1,A36,XBOOLE_0:def 3;
    hence contradiction by A5,A6,A8,A39,TARSKI:def 2;
  end;
  per cases by A2,JORDAN16:11;
  suppose LE LMP C, UMP C, C;
    then
A40: LE LMP C, UMP C, Lower_Arc C, E-max C, W-min C by A14,JORDAN6:def 10;
    set S = Segment(Lower_Arc C, E-max C, W-min C, LMP C, UMP C);
A41: UMP C in S by A12,A40,JORDAN16:9;
A42: LMP C in S by A12,A40,JORDAN16:9;
A43: S c= Lower_Arc C by JORDAN16:5;
    then
A44: S c= C by A10,XBOOLE_1:1;
A45: S1 /\ S = {UMP C}
    proof
      S1 /\ C = {UMP C} by Th47;
      hence S1 /\ S c= {UMP C} by A44,XBOOLE_1:26;
      let x be set;
      assume x in {UMP C};
      then
A46:  x = UMP C by TARSKI:def 1;
      UMP C in S1 by TOPREAL1:6;
      hence x in S1 /\ S by A41,A46,XBOOLE_0:def 3;
    end;
A47: S2 /\ S = {LMP C}
    proof
      S2 /\ C = {LMP C} by Th48;
      hence S2 /\ S c= {LMP C} by A44,XBOOLE_1:26;
      let x be set;
      assume x in {LMP C};
      then x = LMP C by TARSKI:def 1;
      hence x in S2 /\ S by A25,A42,XBOOLE_0:def 3;
    end;
A48: (S1 \/ S) /\ S2 = S1 /\ S2 \/ S /\ S2 by XBOOLE_1:23
      .= {} \/ S /\ S2 by A27,XBOOLE_0:def 7;
    S is_an_arc_of LMP C, UMP C by A7,A12,A40,JORDAN16:36;
    then S is_an_arc_of UMP C, LMP C by JORDAN5B:14;
    then S1 \/ S is_an_arc_of n,LMP C by A45,TOPREAL1:17;
    then S1 \/ S \/ S2 is_an_arc_of n,s by A47,A48,TOPREAL1:16;
    then
A49: S1 \/ S \/ S2 is_an_arc_of s,n by JORDAN5B:14;
A50: S c= Lower_Arc C \ {W-min C,E-max C}
    proof
      let s be set;
      assume
A51:  s in S;
      now
        assume s in {W-min C,E-max C};
        then s = W-min C or s = E-max C by TARSKI:def 2;
        hence contradiction by A3,A6,A12,A51,Th11;
      end;
      hence s in Lower_Arc C \ {W-min C,E-max C} by A43,A51,XBOOLE_0:def 4;
    end;
    Upper_Arc C misses S
    proof
      assume Upper_Arc C meets S;
      then consider x being set such that
A52:  x in Upper_Arc C and
A53:  x in S by XBOOLE_0:3;
      x in Lower_Arc C by A50,A53,XBOOLE_0:def 4;
      then x in Lower_Arc C /\ Upper_Arc C by A52,XBOOLE_0:def 3;
      hence contradiction by A8,A50,A53,XBOOLE_0:def 4;
    end;
    then
A54: Upper_Arc C misses S1 \/ S \/ S2 by A30,A35,Lm4;
A55: now
      let p be Point of TOP-REAL 2;
      assume
A56:  p in S1 \/ S \/ S2;
      per cases by A56,Lm3;
      suppose
A57:    p in S1;
A58:    (UMP C)`2<=n`2 by A19,Th52;
        then s`2<=(UMP C)`2 & (UMP C)`2<=p`2 by A19,A57,Th51,TOPREAL1:10;
        hence s`2<=p`2 & p`2<=n`2 by A57,A58,TOPREAL1:10,XXREAL_0:2;
      end;
      suppose p in S;
        hence s`2<=p`2 & p`2<=n`2 by A19,A44,PSCOMP_1:71;
      end;
      suppose
A59:    p in S2;
A60:    s`2<=(LMP C)`2 by A19,Th53;
        hence s`2<=p`2 by A59,TOPREAL1:10;
        p`2<=(LMP C)`2 by A59,A60,TOPREAL1:10;
        hence p`2<=n`2 by A19,Th54,XXREAL_0:2;
      end;
    end;
    now
      let p be Point of TOP-REAL 2;
      assume
A61:  p in S1 \/ S \/ S2;
      per cases by A61,Lm3;
      suppose p in S1;
        then p`1 = (UMP C)`1 by A22,A24,SPPOL_1:def 3;

hence (W-min C)`1<=p`1 & p`1<=(E-max C)`1 by A15,A20,A21,A26,XREAL_1:228;
      end;
      suppose p in S;
        hence (W-min C)`1<=p`1 & p`1<=(E-max C)`1 by A20,A21,A44,PSCOMP_1:71;
      end;
      suppose p in S2;
        then p`1 = (LMP C)`1 by A23,A25,SPPOL_1:def 3;

hence (W-min C)`1<=p`1 & p`1<=(E-max C)`1 by A16,A20,A21,A26,XREAL_1:228;
      end;
    end;
    hence thesis by A11,A28,A29,A49,A54,A55,JGRAPH_1:62;
  end;
  suppose LE UMP C, LMP C, C;
    then
A62: LE UMP C, LMP C, Lower_Arc C, E-max C, W-min C by A13,JORDAN6:def 10;
    set S = Segment(Lower_Arc C, E-max C, W-min C, UMP C, LMP C);
A63: UMP C in S by A12,A62,JORDAN16:9;
A64: LMP C in S by A12,A62,JORDAN16:9;
A65: S c= Lower_Arc C by JORDAN16:5;
    then
A66: S c= C by A10,XBOOLE_1:1;
A67: S1 /\ S = {UMP C}
    proof
      S1 /\ C = {UMP C} by Th47;
      hence S1 /\ S c= {UMP C} by A66,XBOOLE_1:26;
      let x be set;
      assume x in {UMP C};
      then
A68:  x = UMP C by TARSKI:def 1;
      UMP C in S1 by TOPREAL1:6;
      hence x in S1 /\ S by A63,A68,XBOOLE_0:def 3;
    end;
A69: S2 /\ S = {LMP C}
    proof
      S2 /\ C = {LMP C} by Th48;
      hence S2 /\ S c= {LMP C} by A66,XBOOLE_1:26;
      let x be set;
      assume x in {LMP C};
      then x = LMP C by TARSKI:def 1;
      hence x in S2 /\ S by A25,A64,XBOOLE_0:def 3;
    end;
A70: (S1 \/ S) /\ S2 = S1 /\ S2 \/ S /\ S2 by XBOOLE_1:23
      .= {} \/ S /\ S2 by A27,XBOOLE_0:def 7;
    S is_an_arc_of UMP C, LMP C by A12,A62,Th50,JORDAN16:36;
    then S1 \/ S is_an_arc_of n,LMP C by A67,TOPREAL1:17;
    then S1 \/ S \/ S2 is_an_arc_of n,s by A69,A70,TOPREAL1:16;
    then
A71: S1 \/ S \/ S2 is_an_arc_of s,n by JORDAN5B:14;
A72: S c= Lower_Arc C \ {W-min C,E-max C}
    proof
      let s be set;
      assume
A73:  s in S;
      now
        assume s in {W-min C,E-max C};
        then s = W-min C or s = E-max C by TARSKI:def 2;
        hence contradiction by A4,A5,A12,A73,Th11;
      end;
      hence s in Lower_Arc C \ {W-min C,E-max C} by A65,A73,XBOOLE_0:def 4;
    end;
    Upper_Arc C misses S
    proof
      assume Upper_Arc C meets S;
      then consider x being set such that
A74:  x in Upper_Arc C and
A75:  x in S by XBOOLE_0:3;
      x in Lower_Arc C by A72,A75,XBOOLE_0:def 4;
      then x in Lower_Arc C /\ Upper_Arc C by A74,XBOOLE_0:def 3;
      hence contradiction by A8,A72,A75,XBOOLE_0:def 4;
    end;
    then
A76: Upper_Arc C misses S1 \/ S \/ S2 by A30,A35,Lm4;
A77: now
      let p be Point of TOP-REAL 2;
      assume
A78:  p in S1 \/ S \/ S2;
      per cases by A78,Lm3;
      suppose
A79:    p in S1;
A80:    (UMP C)`2<=n`2 by A19,Th52;
        then s`2<=(UMP C)`2 & (UMP C)`2<=p`2 by A19,A79,Th51,TOPREAL1:10;
        hence s`2<=p`2 & p`2<=n`2 by A79,A80,TOPREAL1:10,XXREAL_0:2;
      end;
      suppose p in S;
        hence s`2<=p`2 & p`2<=n`2 by A19,A66,PSCOMP_1:71;
      end;
      suppose
A81:    p in S2;
A82:    s`2<=(LMP C)`2 by A19,Th53;
        hence s`2<=p`2 by A81,TOPREAL1:10;
        p`2<=(LMP C)`2 by A81,A82,TOPREAL1:10;
        hence p`2<=n`2 by A19,Th54,XXREAL_0:2;
      end;
    end;
    now
      let p be Point of TOP-REAL 2;
      assume
A83:  p in S1 \/ S \/ S2;
      per cases by A83,Lm3;
      suppose p in S1;
        then p`1 = (UMP C)`1 by A22,A24,SPPOL_1:def 3;

hence (W-min C)`1<=p`1 & p`1<=(E-max C)`1 by A15,A20,A21,A26,XREAL_1:228;
      end;
      suppose p in S;
        hence (W-min C)`1<=p`1 & p`1<=(E-max C)`1 by A20,A21,A66,PSCOMP_1:71;
      end;
      suppose p in S2;
        then p`1 = (LMP C)`1 by A23,A25,SPPOL_1:def 3;

hence (W-min C)`1<=p`1 & p`1<=(E-max C)`1 by A16,A20,A21,A26,XREAL_1:228;
      end;
    end;
    hence thesis by A11,A28,A29,A71,A76,A77,JGRAPH_1:62;
  end;
end;

theorem
  not (LMP C in Upper_Arc C & UMP C in Upper_Arc C)
proof
  assume
A1: LMP C in Upper_Arc C & UMP C in Upper_Arc C;
A2: UMP C in C & LMP C in C by Th43,Th44;
  set n = |[ (W-bound C + E-bound C) / 2, N-bound C ]|;
  set s = |[ (W-bound C + E-bound C) / 2, S-bound C ]|;
  set S1 = LSeg(n, UMP C);
  set S2 = LSeg(LMP C, s);
A3: UMP C <> W-min C by Th37;
A4: UMP C <> E-max C by Th38;
A5: LMP C <> W-min C by Th39;
A6: LMP C <> E-max C by Th40;
A7: LMP C <> UMP C by Th50;
A8: Upper_Arc(C) /\ Lower_Arc(C) = {W-min(C),E-max(C)} by JORDAN6:def 9;
A9: Lower_Arc C c= C by JORDAN6:76;
A10: Upper_Arc C c= C by JORDAN6:76;
  Lower_Arc C is_an_arc_of E-max C,W-min C by JORDAN6:def 9;
  then
A11: Lower_Arc C is_an_arc_of W-min C,E-max C by JORDAN5B:14;
A12: Upper_Arc C is_an_arc_of W-min C,E-max C by JORDAN6:def 8;
A13: now
    assume UMP C in Lower_Arc C;
    then UMP C in Upper_Arc C /\ Lower_Arc C by A1,XBOOLE_0:def 3;
    hence contradiction by A3,A4,A8,TARSKI:def 2;
  end;
A14: now
    assume LMP C in Lower_Arc C;
    then LMP C in Upper_Arc C /\ Lower_Arc C by A1,XBOOLE_0:def 3;
    hence contradiction by A5,A6,A8,TARSKI:def 2;
  end;
A15: (UMP C)`1 = (W-bound C + E-bound C)/2 by EUCLID:56;
A16: (LMP C)`1 = (W-bound C + E-bound C)/2 by EUCLID:56;
A17: n`1 = (W-bound C + E-bound C)/2 by EUCLID:56;
A18: s`1 = (W-bound C + E-bound C)/2 by EUCLID:56;
A19: s`2 = S-bound C & n`2 = N-bound C by EUCLID:56;
A20: (W-min C)`1 = W-bound C by EUCLID:56;
A21: (E-max C)`1 = E-bound C by EUCLID:56;
A22: S1 is vertical by A15,A17,SPPOL_1:37;
A23: S2 is vertical by A16,A18,SPPOL_1:37;
A24: UMP C in S1 by TOPREAL1:6;
A25: LMP C in S2 by TOPREAL1:6;
A26: W-bound C < E-bound C by SPRECT_1:33;
A27: S1 misses S2 by Th55;
A28: for p being Point of TOP-REAL 2 st p in Lower_Arc C holds
  s`2<=p`2 & p`2<=n`2 by A9,A19,PSCOMP_1:71;
A29: for p being Point of TOP-REAL 2 st p in Lower_Arc C holds
  (W-min C)`1<=p`1 & p`1<=(E-max C)`1 by A9,A20,A21,PSCOMP_1:71;
A30: Lower_Arc C misses S1
  proof
    assume Lower_Arc C meets S1;
    then consider x being set such that
A31: x in Lower_Arc C and
A32: x in S1 by XBOOLE_0:3;
A33: S1 /\ C = {UMP C} by Th47;
    x in S1 /\ C by A9,A31,A32,XBOOLE_0:def 3;
    then
A34: x = UMP C by A33,TARSKI:def 1;
    then x in Lower_Arc C /\ Upper_Arc C by A1,A31,XBOOLE_0:def 3;
    hence contradiction by A3,A4,A8,A34,TARSKI:def 2;
  end;
A35: Lower_Arc C misses S2
  proof
    assume Lower_Arc C meets S2;
    then consider x being set such that
A36: x in Lower_Arc C and
A37: x in S2 by XBOOLE_0:3;
A38: S2 /\ C = {LMP C} by Th48;
    x in S2 /\ C by A9,A36,A37,XBOOLE_0:def 3;
    then
A39: x = LMP C by A38,TARSKI:def 1;
    then x in Lower_Arc C /\ Upper_Arc C by A1,A36,XBOOLE_0:def 3;
    hence contradiction by A5,A6,A8,A39,TARSKI:def 2;
  end;
  per cases by A2,JORDAN16:11;
  suppose LE LMP C, UMP C, C;
    then
A40: LE LMP C, UMP C, Upper_Arc C, W-min C, E-max C by A13,JORDAN6:def 10;
    set S = Segment(Upper_Arc C, W-min C, E-max C, LMP C, UMP C);
A41: UMP C in S by A12,A40,JORDAN16:9;
A42: LMP C in S by A12,A40,JORDAN16:9;
A43: S c= Upper_Arc C by JORDAN16:5;
    then
A44: S c= C by A10,XBOOLE_1:1;
A45: S1 /\ S = {UMP C}
    proof
      S1 /\ C = {UMP C} by Th47;
      hence S1 /\ S c= {UMP C} by A44,XBOOLE_1:26;
      let x be set;
      assume x in {UMP C};
      then
A46:  x = UMP C by TARSKI:def 1;
      UMP C in S1 by TOPREAL1:6;
      hence x in S1 /\ S by A41,A46,XBOOLE_0:def 3;
    end;
A47: S2 /\ S = {LMP C}
    proof
      S2 /\ C = {LMP C} by Th48;
      hence S2 /\ S c= {LMP C} by A44,XBOOLE_1:26;
      let x be set;
      assume x in {LMP C};
      then x = LMP C by TARSKI:def 1;
      hence x in S2 /\ S by A25,A42,XBOOLE_0:def 3;
    end;
A48: (S1 \/ S) /\ S2 = S1 /\ S2 \/ S /\ S2 by XBOOLE_1:23
      .= {} \/ S /\ S2 by A27,XBOOLE_0:def 7;
    S is_an_arc_of LMP C, UMP C by A7,A12,A40,JORDAN16:36;
    then S is_an_arc_of UMP C, LMP C by JORDAN5B:14;
    then S1 \/ S is_an_arc_of n,LMP C by A45,TOPREAL1:17;
    then S1 \/ S \/ S2 is_an_arc_of n,s by A47,A48,TOPREAL1:16;
    then
A49: S1 \/ S \/ S2 is_an_arc_of s,n by JORDAN5B:14;
A50: S c= Upper_Arc C \ {W-min C,E-max C}
    proof
      let s be set;
      assume
A51:  s in S;
      now
        assume s in {W-min C,E-max C};
        then s = W-min C or s = E-max C by TARSKI:def 2;
        hence contradiction by A4,A5,A12,A51,Th11;
      end;
      hence s in Upper_Arc C \ {W-min C,E-max C} by A43,A51,XBOOLE_0:def 4;
    end;
    Lower_Arc C misses S
    proof
      assume Lower_Arc C meets S;
      then consider x being set such that
A52:  x in Lower_Arc C and
A53:  x in S by XBOOLE_0:3;
      x in Upper_Arc C by A50,A53,XBOOLE_0:def 4;
      then x in Lower_Arc C /\ Upper_Arc C by A52,XBOOLE_0:def 3;
      hence contradiction by A8,A50,A53,XBOOLE_0:def 4;
    end;
    then
A54: Lower_Arc C misses S1 \/ S \/ S2 by A30,A35,Lm4;
A55: now
      let p be Point of TOP-REAL 2;
      assume
A56:  p in S1 \/ S \/ S2;
      per cases by A56,Lm3;
      suppose
A57:    p in S1;
A58:    (UMP C)`2<=n`2 by A19,Th52;
        then s`2<=(UMP C)`2 & (UMP C)`2<=p`2 by A19,A57,Th51,TOPREAL1:10;
        hence s`2<=p`2 & p`2<=n`2 by A57,A58,TOPREAL1:10,XXREAL_0:2;
      end;
      suppose p in S;
        hence s`2<=p`2 & p`2<=n`2 by A19,A44,PSCOMP_1:71;
      end;
      suppose
A59:    p in S2;
A60:    s`2<=(LMP C)`2 by A19,Th53;
        hence s`2<=p`2 by A59,TOPREAL1:10;
        p`2<=(LMP C)`2 by A59,A60,TOPREAL1:10;
        hence p`2<=n`2 by A19,Th54,XXREAL_0:2;
      end;
    end;
    now
      let p be Point of TOP-REAL 2;
      assume
A61:  p in S1 \/ S \/ S2;
      per cases by A61,Lm3;
      suppose p in S1;
        then p`1 = (UMP C)`1 by A22,A24,SPPOL_1:def 3;

hence (W-min C)`1<=p`1 & p`1<=(E-max C)`1 by A15,A20,A21,A26,XREAL_1:228;
      end;
      suppose p in S;
        hence (W-min C)`1<=p`1 & p`1<=(E-max C)`1 by A20,A21,A44,PSCOMP_1:71;
      end;
      suppose p in S2;
        then p`1 = (LMP C)`1 by A23,A25,SPPOL_1:def 3;

hence (W-min C)`1<=p`1 & p`1<=(E-max C)`1 by A16,A20,A21,A26,XREAL_1:228;
      end;
    end;
    hence thesis by A11,A28,A29,A49,A54,A55,JGRAPH_1:62;
  end;
  suppose LE UMP C, LMP C, C;
    then
A62: LE UMP C, LMP C, Upper_Arc C, W-min C, E-max C by A14,JORDAN6:def 10;
    set S = Segment(Upper_Arc C, W-min C, E-max C, UMP C, LMP C);
A63: UMP C in S by A12,A62,JORDAN16:9;
A64: LMP C in S by A12,A62,JORDAN16:9;
A65: S c= Upper_Arc C by JORDAN16:5;
    then
A66: S c= C by A10,XBOOLE_1:1;
A67: S1 /\ S = {UMP C}
    proof
      S1 /\ C = {UMP C} by Th47;
      hence S1 /\ S c= {UMP C} by A66,XBOOLE_1:26;
      let x be set;
      assume x in {UMP C};
      then
A68:  x = UMP C by TARSKI:def 1;
      UMP C in S1 by TOPREAL1:6;
      hence x in S1 /\ S by A63,A68,XBOOLE_0:def 3;
    end;
A69: S2 /\ S = {LMP C}
    proof
      S2 /\ C = {LMP C} by Th48;
      hence S2 /\ S c= {LMP C} by A66,XBOOLE_1:26;
      let x be set;
      assume x in {LMP C};
      then x = LMP C by TARSKI:def 1;
      hence x in S2 /\ S by A25,A64,XBOOLE_0:def 3;
    end;
A70: (S1 \/ S) /\ S2 = S1 /\ S2 \/ S /\ S2 by XBOOLE_1:23
      .= {} \/ S /\ S2 by A27,XBOOLE_0:def 7;
    S is_an_arc_of UMP C, LMP C by A12,A62,Th50,JORDAN16:36;
    then S1 \/ S is_an_arc_of n,LMP C by A67,TOPREAL1:17;
    then S1 \/ S \/ S2 is_an_arc_of n,s by A69,A70,TOPREAL1:16;
    then
A71: S1 \/ S \/ S2 is_an_arc_of s,n by JORDAN5B:14;
A72: S c= Upper_Arc C \ {W-min C,E-max C}
    proof
      let s be set;
      assume
A73:  s in S;
      now
        assume s in {W-min C,E-max C};
        then s = W-min C or s = E-max C by TARSKI:def 2;
        hence contradiction by A3,A6,A12,A73,Th11;
      end;
      hence s in Upper_Arc C \ {W-min C,E-max C} by A65,A73,XBOOLE_0:def 4;
    end;
    Lower_Arc C misses S
    proof
      assume Lower_Arc C meets S;
      then consider x being set such that
A74:  x in Lower_Arc C and
A75:  x in S by XBOOLE_0:3;
      x in Upper_Arc C by A72,A75,XBOOLE_0:def 4;
      then x in Lower_Arc C /\ Upper_Arc C by A74,XBOOLE_0:def 3;
      hence contradiction by A8,A72,A75,XBOOLE_0:def 4;
    end;
    then
A76: Lower_Arc C misses S1 \/ S \/ S2 by A30,A35,Lm4;
A77: now
      let p be Point of TOP-REAL 2;
      assume
A78:  p in S1 \/ S \/ S2;
      per cases by A78,Lm3;
      suppose
A79:    p in S1;
A80:    (UMP C)`2<=n`2 by A19,Th52;
        then s`2<=(UMP C)`2 & (UMP C)`2<=p`2 by A19,A79,Th51,TOPREAL1:10;
        hence s`2<=p`2 & p`2<=n`2 by A79,A80,TOPREAL1:10,XXREAL_0:2;
      end;
      suppose p in S;
        hence s`2<=p`2 & p`2<=n`2 by A19,A66,PSCOMP_1:71;
      end;
      suppose
A81:    p in S2;
A82:    s`2<=(LMP C)`2 by A19,Th53;
        hence s`2<=p`2 by A81,TOPREAL1:10;
        p`2<=(LMP C)`2 by A81,A82,TOPREAL1:10;
        hence p`2<=n`2 by A19,Th54,XXREAL_0:2;
      end;
    end;
    now
      let p be Point of TOP-REAL 2;
      assume
A83:  p in S1 \/ S \/ S2;
      per cases by A83,Lm3;
      suppose p in S1;
        then p`1 = (UMP C)`1 by A22,A24,SPPOL_1:def 3;

hence (W-min C)`1<=p`1 & p`1<=(E-max C)`1 by A15,A20,A21,A26,XREAL_1:228;
      end;
      suppose p in S;
        hence (W-min C)`1<=p`1 & p`1<=(E-max C)`1 by A20,A21,A66,PSCOMP_1:71;
      end;
      suppose p in S2;
        then p`1 = (LMP C)`1 by A23,A25,SPPOL_1:def 3;

hence (W-min C)`1<=p`1 & p`1<=(E-max C)`1 by A16,A20,A21,A26,XREAL_1:228;
      end;
    end;
    hence thesis by A11,A28,A29,A71,A76,A77,JGRAPH_1:62;
  end;
end;

