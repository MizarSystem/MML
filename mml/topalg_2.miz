:: The Fundamental Group of Convex Subspaces of TOP-REAL n
::  by Artur Korni{\l}owicz
::
:: Received April 20, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies FUNCT_1, PRE_TOPC, EUCLID, ORDINAL2, RELAT_1, ARYTM_1, TOPALG_2,
      SUBSET_1, BORSUK_1, TOPALG_1, GRAPH_1, ALGSTR_1, SEQM_3, BORSUK_2,
      EQREL_1, WAYBEL20, BORSUK_6, JORDAN1, TOPREAL1, TOPS_2, REALSET1,
      TOPMETR, ARYTM, FINSEQ_1, FINSEQ_4, RCOMP_1, MEMBERED, TREAL_1, SEQ_1,
      ARYTM_3;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, ORDINAL1, EQREL_1, NAT_1,
      RELAT_1, FUNCT_1, FUNCT_2, BINOP_1, FINSEQ_1, NUMBERS, XCMPLX_0, XREAL_0,
      REAL_1, FINSEQ_4, SEQM_3, STRUCT_0, PRE_TOPC, GROUP_6, GRCAT_1, BORSUK_1,
      TOPS_2, TOPREAL1, RCOMP_1, RCOMP_2, TREAL_1, TOPMETR, EUCLID, JORDAN1,
      JORDAN2B, BORSUK_2, BORSUK_6, TOPALG_1;
 constructors NAT_1, EUCLID, BORSUK_1, JGRAPH_6, TOPRNS_1, JORDAN2C, GRCAT_1,
      ABIAN, KNASTER, TOPALG_1, SEQM_3, GR_CY_1, REALSET2, PARTFUN2, FINSEQ_4,
      JORDAN1, TOPREAL1, REAL_1, TOPS_2, ABSVALUE, JGRAPH_2, T_0TOPSP,
      MEMBERED, QUIN_1, POLYEQ_1, SQUARE_1, TOPS_1, CONNSP_1, URYSOHN1,
      JORDAN2B, BINARITH, RCOMP_1, SEQ_4, TREAL_1, RCOMP_2, COMPTS_1, BINOP_2;
 registrations STRUCT_0, RELSET_1, ARYTM_3, XREAL_0, REAL_1, BORSUK_1,
      PRE_TOPC, NUMBERS, XBOOLE_0, FUNCT_1, TOPALG_1, BORSUK_2, TOPREAL1,
      JORDAN5A, TEX_2, JORDAN6, BORSUK_5, BORSUK_6;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM, REAL;
 definitions TARSKI, XBOOLE_0, BORSUK_2, MEMBERED, BORSUK_6;
 theorems JGRAPH_6, XREAL_0, TARSKI, FUNCT_2, TSEP_1, PRE_TOPC, EUCLID,
      GRCAT_1, TOPS_2, BORSUK_1, TOPALG_1, EQREL_1, BORSUK_2, BINOP_1, TOPMETR,
      JORDAN2C, JORDAN1, TOPREAL1, JORDAN5A, FUNCT_3, BORSUK_6, REALSET2,
      JGRAPH_2, FUNCT_1, JORDAN2B, FINSEQ_4, RVSUM_1, GROUP_7, TOPREAL5,
      TREAL_1, XCMPLX_1, REAL_1, AXIOMS, SQUARE_1, JORDAN6, RCOMP_2, BORSUK_4;
 schemes FUNCT_2;

begin :: Convex subspaces of TOP-REAL n

reserve
  n for Nat,
  a, b for real number;

registration
  let n;
  cluster non empty convex Subset of TOP-REAL n;
existence
proof
  consider a, b being Point of TOP-REAL n;
  take LSeg(a,b);
  thus thesis by BORSUK_4:8;
end;
end;

definition
  let n be Nat, T be SubSpace of TOP-REAL n;
  attr T is convex means :Def1:
  [#]T is convex Subset of TOP-REAL n;
end;

registration
  let n be Nat;
  cluster convex -> arcwise_connected (non empty SubSpace of TOP-REAL n);
coherence
proof
  let T be non empty SubSpace of TOP-REAL n;
  assume
A1: [#]T is convex Subset of TOP-REAL n;
  let a, b be Point of T;
A2: a in the carrier of T & b in the carrier of T;
  the carrier of T is Subset of TOP-REAL n by TSEP_1:1;
  then reconsider a1 = a, b1 = b as Point of TOP-REAL n by A2;
  per cases;
  suppose a1 <> b1;
  then LSeg(a1,b1) is_an_arc_of a1,b1 by TOPREAL1:15;
  then consider h being map of I[01], (TOP-REAL n)|LSeg(a1,b1) such that
A3: h is_homeomorphism and
A4: h.0 = a1 & h.1 = b1 by TOPREAL1:def 2;
A5: h is continuous by A3,TOPS_2:def 5;
A6: [#]T = the carrier of T by PRE_TOPC:12;
  [#]((TOP-REAL n)|LSeg(a1,b1)) = LSeg(a1,b1) by PRE_TOPC:def 10;
  then the carrier of (TOP-REAL n)|LSeg(a1,b1) = LSeg(a1,b1) by PRE_TOPC:12;
  then
A7:the carrier of (TOP-REAL n)|LSeg(a1,b1) c= the carrier of T
    by A1,A6,JORDAN1:def 1;
  then reconsider f = h as map of I[01], T by FUNCT_2:9;
  take f;
  (TOP-REAL n)|LSeg(a1,b1) is SubSpace of T by A7,TSEP_1:4;
  hence f is continuous by A5,TOPMETR:7;
  thus f.0 = a & f.1 = b by A4;
  end;
  suppose a1 = b1;
  hence a,b are_connected;
  end;
end;
end;

registration
  let n be Nat;
  cluster strict non empty convex SubSpace of TOP-REAL n;
existence
proof
  reconsider T = TOP-REAL n as SubSpace of TOP-REAL n by TSEP_1:2;
  take T;
  thus T is strict non empty;
  thus [#]T is convex Subset of TOP-REAL n by JORDAN2C:21;
end;
end;

theorem Th1:
  for X being non empty TopSpace, Y being non empty SubSpace of X,
      x1, x2 being Point of X, y1, y2 being Point of Y,
      f being Path of y1,y2 st x1 = y1 & x2 = y2 & y1,y2 are_connected holds
   f is Path of x1,x2
proof
  let X be non empty TopSpace, Y be non empty SubSpace of X,
      x1, x2 be Point of X, y1, y2 be Point of Y,
      f be Path of y1,y2 such that
A1: x1 = y1 & x2 = y2 and
A2: y1, y2 are_connected;
  the carrier of Y is Subset of the carrier of X by TSEP_1:1;
  then reconsider g = f as map of I[01], X by FUNCT_2:9;
  g is Path of x1,x2
  proof
A3: f is continuous by A2,BORSUK_2:def 2;
    thus ex f being map of I[01], X st f is continuous & f.0 = x1 & f.1 = x2
    proof
      take g;
      thus g is continuous by A3,TOPMETR:7;
      thus thesis by A1,A2,BORSUK_2:def 2;
    end;
    thus g is continuous by A3,TOPMETR:7;
    thus g.0 = x1 & g.1 = x2 by A1,A2,BORSUK_2:def 2;
  end;
  hence f is Path of x1,x2;
end;

set I = the carrier of I[01];
Lm1: the carrier of [:I[01],I[01]:] = [:I,I:] by BORSUK_1:def 5;
Lm2: the carrier of [:TOP-REAL 1,I[01]:] = [:the carrier of TOP-REAL 1,I:]
     by BORSUK_1:def 5;
Lm3: the carrier of [:R^1,I[01]:] = [:the carrier of R^1,I:] by BORSUK_1:def 5;
Lm4: dom id I[01] = I by FUNCT_2:def 1;

definition
  let n be Nat,
      T being non empty convex SubSpace of TOP-REAL n,
      a, b be Point of T,
      P, Q be Path of a,b;
  func ConvexHomotopy(P,Q) -> map of [:I[01],I[01]:], T means :Def2:
  for s, t being Element of I[01],
      a1, b1 being Point of TOP-REAL n st a1 = P.s & b1 = Q.s holds
   it.(s,t) = (1-t) * a1 + t * b1;
existence
proof
  defpred P[Element of I,Element of I,set] means
    ex a1, b1 being Point of TOP-REAL n st a1 = P.$1 & b1 = Q.$1 &
      $3 = (1-$2) * a1 + $2 * b1;
A1: for x, y being Element of I ex z being Element of the carrier of T st
   P[x,y,z]
  proof
    let x, y be Element of I;
A2: P.x in the carrier of T & Q.x in the carrier of T;
    the carrier of T is Subset of TOP-REAL n by TSEP_1:1;
    then reconsider a1 = P.x, b1 = Q.x as Point of TOP-REAL n by A2;
A3: [#]T = the carrier of T by PRE_TOPC:12;
    set z = (1-y) * a1 + y * b1;
A4: y is Real by XREAL_0:def 1;
A5: 0 <= y & y <= 1 by JORDAN5A:2;
    LSeg(a1,b1) = { (1-l)*a1 + l*b1 where l is Real: 0 <= l & l <= 1 }
      by TOPREAL1:def 3;
    then
A6: z in LSeg(a1,b1) by A4,A5;
    [#]T is convex Subset of TOP-REAL n by Def1;
    then LSeg(a1,b1) c= [#]T by A3,JORDAN1:def 1;
    then z is Point of T by A6,PRE_TOPC:12;
    hence thesis;
  end;
  consider F being Function of [:I,I:],the carrier of T such that
A7: for x, y being Element of I holds P[x,y,F. [x,y]] from FUNCT_2:sch 7(A1);
  reconsider F as map of [:I[01],I[01]:], T by Lm1;
  take F;
  let x, y be Element of I;
  ex a1, b1 being Point of TOP-REAL n st
    a1 = P.x & b1 = Q.x & F. [x,y] = (1-y) * a1 + y * b1 by A7;
  hence thesis by BINOP_1:def 1;
end;
uniqueness
proof
  let f, g be map of [:I[01],I[01]:], T such that
A8: for s, t being Element of I[01], a1, b1 being Point of TOP-REAL n st
     a1 = P.s & b1 = Q.s holds f.(s,t) = (1-t) * a1 + t * b1 and
A9: for s, t being Element of I[01], a1, b1 being Point of TOP-REAL n st
     a1 = P.s & b1 = Q.s holds g.(s,t) = (1-t) * a1 + t * b1;
  for s, t being Element of I holds f.(s,t) = g.(s,t)
  proof
    let s, t be Element of I;
    reconsider a1 = P.s, b1 = Q.s as Point of TOP-REAL n by TOPMETR:2;
    f.(s,t) = (1-t) * a1 + t * b1 by A8;
    hence f.(s,t) = g.(s,t) by A9;
  end;
  hence f = g by Lm1,BINOP_1:2;
end;
end;

Lm5:
  for T being non empty convex SubSpace of TOP-REAL n, a, b being Point of T,
      P, Q being Path of a,b holds ConvexHomotopy(P,Q) is continuous
proof
  let T be non empty convex SubSpace of TOP-REAL n,
      a, b be Point of T,
      P, Q being Path of a,b;
  set F = ConvexHomotopy(P,Q);
  set X = the carrier of T;
  set E = the carrier of TOP-REAL n;
  set PI = [:P,id I[01]:];
  set QI = [:Q,id I[01]:];
A1: the carrier of T is Subset of the carrier of TOP-REAL n by TSEP_1:1;
  then reconsider G = F as map of [:I[01],I[01]:],TOP-REAL n by FUNCT_2:9;
  a in the carrier of T & b in the carrier of T;
  then reconsider a1 = a, b1 = b as Point of TOP-REAL n by A1;
  a,b are_connected by BORSUK_2:def 3;
  then reconsider P1 = P, Q1 = Q as Path of a1,b1 by Th1;
  set P1I = [:P1,id I[01]:];
  set Q1I = [:Q1,id I[01]:];
A2: P1I is continuous & Q1I is continuous by BORSUK_2:12;
  deffunc Fa(Element of E,Element of I) = (1-$2)*$1;
  deffunc Fb(Element of E,Element of I) = $2*$1;
  consider Pa being Function of [:E,I:],E such that
A3: for x being Element of E, i being Element of I holds
    Pa. [x,i] = Fa(x,i) from FUNCT_2:sch 8;
  consider Pb being Function of [:E,I:],E such that
A4: for x being Element of E, i being Element of I holds
    Pb. [x,i] = Fb(x,i) from FUNCT_2:sch 8;
  the carrier of [:TOP-REAL n,I[01]:] = [:E,I:] by BORSUK_1:def 5;
  then reconsider Pa, Pb as map of [:TOP-REAL n,I[01]:],TOP-REAL n;
A5: for x being Element of E, i being Element of I holds
    Pa.(x,i) = Fa(x,i)
  proof
    let x be Element of E, i be Element of I;
    thus Pa.(x,i) = Pa. [x,i] by BINOP_1:def 1
      .= Fa(x,i) by A3;
  end;
  for x being Element of E, i being Element of I holds
    Pb.(x,i) = Fb(x,i)
  proof
    let x be Element of E, i be Element of I;
    thus Pb.(x,i) = Pb. [x,i] by BINOP_1:def 1
      .= Fb(x,i) by A4;
  end;
  then Pa is continuous & Pb is continuous by A5,TOPALG_1:18,19;
  then
A6: Pa*P1I is continuous & Pb*Q1I is continuous by A2,TOPS_2:58;
A7:  for p being Point of [:I[01],I[01]:] holds G.p = (Pa*P1I).p + (Pb*Q1I).p
  proof
    let p be Point of [:I[01],I[01]:];
    consider s, t being Point of I[01] such that
A8:   p = [s,t] by BORSUK_1:50;
A9: dom P = I by FUNCT_2:def 1;
A10: dom Q = I by FUNCT_2:def 1;
A11: (id I[01]).t = t by GRCAT_1:11;
A12: (Pa*PI).p = Pa.(PI.p) by FUNCT_2:21
      .= Pa. [P.s,t] by A8,A9,A11,Lm4,FUNCT_3:def 9
      .= (1-t) * P1.s by A3;
A13: (Pb*QI).p = Pb.(QI.p) by FUNCT_2:21
      .= Pb. [Q.s,t] by A8,A10,A11,Lm4,FUNCT_3:def 9
      .= t * Q1.s by A4;
    reconsider a1 = P.s, b1 = Q.s as Point of TOP-REAL n by TOPMETR:2;
    F.(s,t) = (1-t) * a1 + t * b1 by Def2;
    hence G.p = (Pa*P1I).p + (Pb*Q1I).p by A8,A12,A13,BINOP_1:def 1;
  end;
  consider g being map of [:I[01],I[01]:],TOP-REAL n such that
A14:  for r being Point of [:I[01],I[01]:] holds g.r = (Pa*P1I).r + (Pb*Q1I).r
  and
A15:  g is continuous by A6,JGRAPH_6:20;
  for p being Point of [:I[01],I[01]:] holds G.p = g.p
  proof
    let p be Point of [:I[01],I[01]:];
    thus G.p = (Pa*P1I).p + (Pb*Q1I).p by A7
      .= g.p by A14;
  end;
  then G = g by FUNCT_2:113;
  hence F is continuous by A15,TOPMETR:8;
end;

Lm6:
  for T being non empty convex SubSpace of TOP-REAL n,
      a, b being Point of T,
      P, Q being Path of a,b holds
  for s being Point of I[01] holds ConvexHomotopy(P,Q).(s,0) = P.s &
                                   ConvexHomotopy(P,Q).(s,1) = Q.s &
  for t being Point of I[01] holds ConvexHomotopy(P,Q).(0,t) = a &
                                   ConvexHomotopy(P,Q).(1,t) = b
proof
  let T be non empty convex SubSpace of TOP-REAL n,
      a, b be Point of T,
      P, Q be Path of a,b;
  set F = ConvexHomotopy(P,Q);
  let s be Point of I[01];
  reconsider x0 = 0, x1 = 1 as Point of I[01] by BORSUK_1:def 17,def 18;
  reconsider a1 = P.s, b1 = Q.s as Point of TOP-REAL n by TOPMETR:2;
A1: F.(s,x0) = (1-x0) * a1 + x0 * b1 by Def2;
  thus F.(s,0) = a1 + 0 * b1 by A1,EUCLID:33
    .= a1 + 0.REAL n by EUCLID:33
    .= P.s by EUCLID:31;
A2: F.(s,x1) = (1-x1) * a1 + x1 * b1 by Def2;
  thus F.(s,1) = 0.REAL n + 1 * b1 by A2,EUCLID:33
    .= 0.REAL n + b1 by EUCLID:33
    .= Q.s by EUCLID:31;
  let t be Point of I[01];
  reconsider a1 = P.x0, b1 = Q.x0 as Point of TOP-REAL n by TOPMETR:2;
A3: F.(0,t) = (1-t) * a1 + t * b1 by Def2;
A4: P.x0 = a by BORSUK_2:def 4;
A5: Q.x0 = a by BORSUK_2:def 4;
A6: P.x1 = b by BORSUK_2:def 4;
A7: Q.x1 = b by BORSUK_2:def 4;
  thus F.(0,t) = 1*a1 - t*a1 + t*a1 by A3,A4,A5,EUCLID:54
    .= 1*a1 by EUCLID:52
    .= a by A4,EUCLID:33;
  reconsider a1 = P.x1, b1 = Q.x1 as Point of TOP-REAL n by TOPMETR:2;
A8: F.(1,t) = (1-t) * a1 + t * b1 by Def2;
  thus F.(1,t) = 1*a1 - t*b1 + t*a1 by A6,A7,A8,EUCLID:54
    .= 1*b1 by A6,A7,EUCLID:52
    .= b by A7,EUCLID:33;
end;

theorem Th2:
  for T being non empty convex SubSpace of TOP-REAL n,
      a, b being Point of T,
      P, Q being Path of a,b holds
  P, Q are_homotopic
proof
  let T be non empty convex SubSpace of TOP-REAL n,
      a, b be Point of T,
      P, Q be Path of a,b;
  take F = ConvexHomotopy(P,Q);
  thus F is continuous by Lm5;
  thus thesis by Lm6;
end;

definition
  let n be Nat,
      T be non empty convex SubSpace of TOP-REAL n,
      a, b be Point of T,
      P, Q be Path of a,b;
  redefine func ConvexHomotopy(P,Q) -> Homotopy of P,Q;
coherence
proof
  thus P, Q are_homotopic by Th2;
  thus ConvexHomotopy(P,Q) is continuous by Lm5;
  thus thesis by Lm6;
end;
end;

registration
  let n be Nat,
      T be non empty convex SubSpace of TOP-REAL n,
      a, b be Point of T,
      P, Q be Path of a,b;
  cluster -> continuous Homotopy of P,Q;
coherence
proof
  let F be Homotopy of P,Q;
  P, Q are_homotopic by Th2;
  hence F is continuous by BORSUK_6:def 13;
end;
end;

theorem Th3:
  for T being non empty convex SubSpace of TOP-REAL n,
      a being Point of T,
      C being Loop of a holds
  the carrier of pi_1(T,a) = { Class(EqRel(T,a),C) }
proof
  let T be non empty convex SubSpace of TOP-REAL n,
      a be Point of T,
      C be Loop of a;
  set E = EqRel(T,a);
  hereby
    let x be set;
    assume x in the carrier of pi_1(T,a);
    then consider P being Loop of a such that
A1:   x = Class(E,P) by TOPALG_1:48;
    P,C are_homotopic by Th2;
    then x = Class(E,C) by A1,TOPALG_1:47;
    hence x in { Class(E,C) } by TARSKI:def 1;
  end;
  let x be set;
  assume x in { Class(E,C) };
  then
A2: x = Class(E,C) by TARSKI:def 1;
  C in Loops a by TOPALG_1:def 1;
  then x in Class E by A2,EQREL_1:def 5;
  hence thesis by TOPALG_1:def 3;
end;

registration
  let n be Nat,
      T be non empty convex SubSpace of TOP-REAL n,
      a be Point of T;
  cluster pi_1(T,a) -> trivial;
coherence
proof
  set E = EqRel(T,a);
  consider C being constant Loop of a;
A1: the carrier of pi_1(T,a) = Class E by TOPALG_1:def 3;
  then { Class(E,C) } = Class E by Th3;
  hence thesis by A1,REALSET2:def 5;
end;
end;

begin :: Convex subspaces of R^1

theorem Th4:
  Proj(|[a]|,1) = a
proof
  reconsider y = a as Real by XREAL_0:def 1;
  |[a]| = <*y*> by JORDAN2B:def 2;
  hence Proj(|[a]|,1) = <*y*>/.1 by JORDAN2B:def 1
    .= a by FINSEQ_4:25;
end;

registration
  cluster -> real-membered SubSpace of R^1;
coherence;
end;

theorem
  a <= b implies
  [.a,b.] = { (1-l)*a + l*b where l is Real: 0 <= l & l <= 1 }
proof
  assume
A1:  a <= b;
  set X = {(1-l)*a+l*b where l is Real: 0 <= l & l <= 1};
  hereby
    let x be set;
    assume
A2:   x in [.a,b.];
    then reconsider y = x as Real;
A3: a <= y & y <= b by A2,TOPREAL5:1;
    per cases by A1,REAL_1:def 5;
    suppose a < b;
    then
A4: b-a > b-b by REAL_1:92;
    reconsider l = (y-a)/(b-a) as Real by XREAL_0:def 1;
    l in the carrier of Closed-Interval-TSpace (0,1) by A3,BORSUK_6:5;
    then l in [.0,1.] by TOPMETR:25;
    then
A5: 0 <= l & l <= 1 by TOPREAL5:1;
    (1-l)*a+l*b = a+l*(b-a)
    .= a+(y-a) by A4,XCMPLX_1:88
    .= y;
    hence x in X by A5;
    end;
    suppose a = b;
    then (1-1)*a+1*b = y by A3,AXIOMS:21;
    hence x in X;
    end;
  end;
  let x be set;
  assume x in X;
  then consider l being Real such that
A6: x = (1-l)*a+l*b and
A7: 0 <= l and
A8: l <= 1;
  a <= (1-l)*a+l*b & (1-l)*a+l*b <= b by A1,A7,A8,JGRAPH_6:2;
  hence x in [.a,b.] by A6,TOPREAL5:1;
end;

theorem Th6:
  for F being map of [:R^1,I[01]:], R^1 st
   for x being Point of R^1, i being Point of I[01] holds
    F.(x,i) = (1-i) * x holds
  F is continuous
proof
  let F be map of [:R^1,I[01]:], R^1 such that
A1: for x being Point of R^1, i being Point of I[01] holds
   F.(x,i) = (1-i) * x;
   deffunc Fa(Element of the carrier of TOP-REAL 1,
              Element of I) = (1-$2)*$1;
   consider G being Function of [:the carrier of TOP-REAL 1,I:],
              the carrier of TOP-REAL 1 such that
A2: for x being Point of TOP-REAL 1, i being Point of I[01] holds
    G. [x,i] = Fa(x,i) from FUNCT_2:sch 8;
    reconsider G as map of [:TOP-REAL 1,I[01]:], TOP-REAL 1 by Lm2;
A3: for x being Point of TOP-REAL 1, i being Point of I[01] holds
    G.(x,i) = Fa(x,i)
    proof
      let x be Point of TOP-REAL 1,
          i be Point of I[01];
      thus G.(x,i) = G. [x,i] by BINOP_1:def 1
        .= Fa(x,i) by A2;
    end;
A4: G is continuous by A3,TOPALG_1:18;
  consider f being map of TOP-REAL 1,R^1 such that
A5:  for p being Element of TOP-REAL 1 holds f.p = Proj(p,1) by JORDAN2B:1;
A6: f is_homeomorphism by A5,JORDAN2B:34;
  then
A7: f is continuous by TOPS_2:def 5;
  f" is continuous by A6,TOPS_2:def 5;
  then [:f",id I[01]:] is continuous by BORSUK_2:12;
  then G*[:f",id I[01]:] is continuous by A4,TOPS_2:58;
  then
A8: f*(G*[:f",id I[01]:]) is continuous by A7,TOPS_2:58;
  for x being Point of [:R^1,I[01]:] holds F.x = (f*(G*[:f",id I[01]:])).x
  proof
    let x be Point of [:R^1,I[01]:];
    consider a being Point of R^1, b being Point of I[01] such that
A9:   x = [a,b] by BORSUK_1:50;
A10: dom f = the carrier of TOP-REAL 1 by FUNCT_2:def 1;
A11: dom (f") = the carrier of R^1 by FUNCT_2:def 1;
    reconsider ff = f as Function;
    set g = ff";
A12: rng f = [#]R^1 by A6,TOPS_2:def 5;
A13: ff is one-to-one by A6,TOPS_2:def 5;
A14: <*a*> = |[a]| by JORDAN2B:def 2;
    consider z being Real such that
A15:   (1-b)*f".a = <*z*> by JORDAN2B:24;
    reconsider w = <*a*> as Element of REAL 1 by A14,EUCLID:25;
A16: 1-b is Real & a is Real by XREAL_0:def 1;
    f.<*a*> = f. |[a]| by JORDAN2B:def 2
      .= Proj(|[a]|,1) by A5
      .= a by Th4;
    then <*a*> = g.a by A10,A13,A14,FUNCT_1:54;
    then f".a = <*a*> by A12,A13,TOPS_2:def 4;
    then
A17: <*z*> = (1-b)*w by A15,EUCLID:def 11
         .= <*(1-b)*a*> by A16,RVSUM_1:69;
    thus (f*(G*[:f",id I[01]:])).x = f.((G*[:f",id I[01]:]).x) by FUNCT_2:21
      .= f.(G.([:f",id I[01]:].x)) by FUNCT_2:21
      .= f.(G. [f".a,id I[01].b]) by A9,A11,Lm4,FUNCT_3:def 9
      .= f.(G. [f".a,b]) by GRCAT_1:11
      .= f.(G.(f".a,b)) by BINOP_1:def 1
      .= f.((1-b)*f".a) by A3
      .= Proj((1-b)*f".a,1) by A5
      .= <*z*>/.1 by A15,JORDAN2B:def 1
      .= z by FINSEQ_4:25
      .= (1-b)*a by A17,GROUP_7:1
      .= F.(a,b) by A1
      .= F.x by A9,BINOP_1:def 1;
  end;
  hence thesis by A8,FUNCT_2:113;
end;

theorem Th7:
  for F being map of [:R^1,I[01]:], R^1 st
  for x being Point of R^1, i being Point of I[01] holds F.(x,i) = i * x
  holds F is continuous
proof
  let F be map of [:R^1,I[01]:], R^1 such that
A1: for x being Point of R^1, i being Point of I[01] holds
   F.(x,i) = i*x;
   deffunc Fa(Element of the carrier of TOP-REAL 1,
              Element of I) = $2*$1;
   consider G being Function of [:the carrier of TOP-REAL 1,I:],
              the carrier of TOP-REAL 1 such that
A2: for x being Point of TOP-REAL 1, i being Point of I[01] holds
    G. [x,i] = Fa(x,i) from FUNCT_2:sch 8;
   reconsider G as map of [:TOP-REAL 1,I[01]:], TOP-REAL 1 by Lm2;
A3: for x being Point of TOP-REAL 1, i being Point of I[01] holds
    G.(x,i) = Fa(x,i)
    proof
      let x be Point of TOP-REAL 1,
          i be Point of I[01];
      thus G.(x,i) = G. [x,i] by BINOP_1:def 1
        .= Fa(x,i) by A2;
    end;
A4: G is continuous by A3,TOPALG_1:19;
  consider f being map of TOP-REAL 1,R^1 such that
A5:  for p being Element of TOP-REAL 1 holds f.p = Proj(p,1) by JORDAN2B:1;
A6: f is_homeomorphism by A5,JORDAN2B:34;
  then
A7: f is continuous by TOPS_2:def 5;
  f" is continuous by A6,TOPS_2:def 5;
  then [:f",id I[01]:] is continuous by BORSUK_2:12;
  then G*[:f",id I[01]:] is continuous by A4,TOPS_2:58;
  then
A8: f*(G*[:f",id I[01]:]) is continuous by A7,TOPS_2:58;
  for x being Point of [:R^1,I[01]:] holds F.x = (f*(G*[:f",id I[01]:])).x
  proof
    let x be Point of [:R^1,I[01]:];
    consider a being Point of R^1, b being Point of I[01] such that
A9:   x = [a,b] by BORSUK_1:50;
A10: dom f = the carrier of TOP-REAL 1 by FUNCT_2:def 1;
A11: dom (f") = the carrier of R^1 by FUNCT_2:def 1;
    reconsider ff = f as Function;
    set g = ff";
A12: rng f = [#]R^1 by A6,TOPS_2:def 5;
A13: ff is one-to-one by A6,TOPS_2:def 5;
A14: <*a*> = |[a]| by JORDAN2B:def 2;
    consider z being Real such that
A15:   b*f".a = <*z*> by JORDAN2B:24;
    reconsider w = <*a*> as Element of REAL 1 by A14,EUCLID:25;
A16: b is Real & a is Real by XREAL_0:def 1;
    f.<*a*> = f. |[a]| by JORDAN2B:def 2
      .= Proj(|[a]|,1) by A5
      .= a by Th4;
    then <*a*> = g.a by A10,A13,A14,FUNCT_1:54;
    then f".a = <*a*> by A12,A13,TOPS_2:def 4;
    then
A17: <*z*> = b*w by A15,EUCLID:def 11
         .= <*b*a*> by A16,RVSUM_1:69;
    thus (f*(G*[:f",id I[01]:])).x = f.((G*[:f",id I[01]:]).x) by FUNCT_2:21
      .= f.(G.([:f",id I[01]:].x)) by FUNCT_2:21
      .= f.(G. [f".a,id I[01].b]) by A9,A11,Lm4,FUNCT_3:def 9
      .= f.(G. [f".a,b]) by GRCAT_1:11
      .= f.(G.(f".a,b)) by BINOP_1:def 1
      .= f.(b*f".a) by A3
      .= Proj(b*f".a,1) by A5
      .= <*z*>/.1 by A15,JORDAN2B:def 1
      .= z by FINSEQ_4:25
      .= b*a by A17,GROUP_7:1
      .= F.(a,b) by A1
      .= F.x by A9,BINOP_1:def 1;
  end;
  hence thesis by A8,FUNCT_2:113;
end;

definition
  let P be Subset of R^1;
  attr P is convex means :Def3:
  for a, b being Point of R^1 st a in P & b in P holds [. a,b .] c= P;
end;

registration
  cluster non empty convex Subset of R^1;
existence
proof
  REAL c= the carrier of R^1 by TOPMETR:24;
  then reconsider P = REAL as non empty Subset of R^1;
  take P;
  thus P is non empty;
  let a, b be Point of R^1;
  thus thesis;
end;
  cluster empty -> convex Subset of R^1;
coherence
proof
  let P be Subset of R^1 such that
A1: P is empty;
  let a, b be Point of R^1;
  thus thesis by A1;
end;
end;

theorem Th8:
  [.a,b.] is convex Subset of R^1
proof
  reconsider P = [.a,b.] as Subset of R^1 by TOPMETR:24;
  P is convex
  proof
    let x, y be Point of R^1;
    assume x in P;
    then
A1: a <= x & x <= b by TOPREAL5:1;
    assume y in P;
    then
A2: a <= y & y <= b by TOPREAL5:1;
    let m be set;
    assume
A3:   m in [. x,y .];
    then reconsider m as Real;
    x <= m & m <= y by A3,TOPREAL5:1;
    then a <= m & m <= b by A1,A2,AXIOMS:22;
    hence thesis by TOPREAL5:1;
  end;
  hence thesis;
end;

theorem
  ].a,b.[ is convex Subset of R^1
proof
  reconsider P = ].a,b.[ as Subset of R^1 by TOPMETR:24;
  P is convex
  proof
    let x, y be Point of R^1;
    assume x in P;
    then
A1: a < x & x < b by JORDAN6:45;
    assume y in P;
    then
A2: a < y & y < b by JORDAN6:45;
    let m be set;
    assume
A3:   m in [. x,y .];
    then reconsider m as Real;
    x <= m & m <= y by A3,TOPREAL5:1;
    then a < m & m < b by A1,A2,AXIOMS:22;
    hence thesis by JORDAN6:45;
  end;
  hence thesis;
end;

theorem
  [.a,b.[ is convex Subset of R^1
proof
  reconsider P = [.a,b.[ as Subset of R^1 by TOPMETR:24;
  P is convex
  proof
    let x, y be Point of R^1;
    assume x in P;
    then
A1: a <= x & x < b by RCOMP_2:3;
    assume y in P;
    then
A2: a <= y & y < b by RCOMP_2:3;
    let m be set;
    assume
A3:   m in [. x,y .];
    then reconsider m as Real;
    x <= m & m <= y by A3,TOPREAL5:1;
    then a <= m & m < b by A1,A2,AXIOMS:22;
    hence thesis by RCOMP_2:3;
  end;
  hence thesis;
end;

theorem
  ].a,b.] is convex Subset of R^1
proof
  reconsider P = ].a,b.] as Subset of R^1 by TOPMETR:24;
  P is convex
  proof
    let x, y be Point of R^1;
    assume x in P;
    then
A1: a < x & x <= b by RCOMP_2:4;
    assume y in P;
    then
A2: a < y & y <= b by RCOMP_2:4;
    let m be set;
    assume
A3:   m in [. x,y .];
    then reconsider m as Real;
    x <= m & m <= y by A3,TOPREAL5:1;
    then a < m & m <= b by A1,A2,AXIOMS:22;
    hence thesis by RCOMP_2:4;
  end;
  hence thesis;
end;

definition
  let T be SubSpace of R^1;
  attr T is convex means :Def4:
  [#]T is convex Subset of R^1;
end;

Lm7:
for T being SubSpace of R^1 st T = R^1 holds T is convex
proof
  let T be SubSpace of R^1 such that
A1: T = R^1;
A2: [#]T = REAL by A1,PRE_TOPC:12,TOPMETR:24;
  reconsider P = [#]T as Subset of R^1 by A1;
  P is convex
  proof
    let a, b be Point of R^1;
    thus thesis by A2;
  end;
  hence [#]T is convex Subset of R^1;
end;

registration
  cluster strict non empty convex SubSpace of R^1;
existence
proof
  reconsider T = R^1 as strict non empty SubSpace of R^1 by TSEP_1:2;
  take T;
  thus thesis by Lm7;
end;
end;

definition
  redefine func R^1 -> strict convex SubSpace of R^1;
coherence by Lm7,TSEP_1:2;
end;

theorem Th12:
  for T being non empty convex SubSpace of R^1,
      a, b being Point of T holds
  [. a, b .] c= the carrier of T
proof
  let T be non empty convex SubSpace of R^1,
      a, b be Point of T;
  reconsider a1 = a, b1 = b as Point of R^1 by TOPMETR:2;
A1: [#]T is convex Subset of R^1 by Def4;
  [#]T = the carrier of T by PRE_TOPC:12;
  then [. a1, b1 .] c= the carrier of T by A1,Def3;
  hence [. a, b .] c= the carrier of T;
end;

registration
  cluster convex -> arcwise_connected (non empty SubSpace of R^1);
coherence
proof
  let T be non empty SubSpace of R^1 such that
A1: T is convex;
  let a, b be Point of T;
  per cases;
  suppose
A2: a <= b;
  set f = L[01]((#)(a,b),(a,b)(#));
A3: [.a,b.] c= the carrier of T by A1,Th12;
  set X = Closed-Interval-TSpace(a,b);
  the carrier of X = [.a,b.] by A2,TOPMETR:25;
  then reconsider f as map of I[01], T by A3,FUNCT_2:9,TOPMETR:27;
  take f;
  the carrier of X is Subset of the carrier of R^1 by TSEP_1:1;
  then reconsider g = f as map of I[01], R^1 by FUNCT_2:9,TOPMETR:27;
  L[01]((#)(a,b),(a,b)(#)) is continuous by A2,TREAL_1:11;
  then g is continuous by TOPMETR:7,27;
  hence f is continuous by TOPMETR:8;
  thus f.0 = f.(#)(0,1) by TREAL_1:def 1
    .= (#)(a,b) by A2,TREAL_1:12
    .= a by A2,TREAL_1:def 1;
  thus f.1 = f.(0,1)(#) by TREAL_1:def 2
    .= (a,b)(#) by A2,TREAL_1:12
    .= b by A2,TREAL_1:def 2;
  end;
  suppose
A4: b <= a;
  set f = L[01]((b,a)(#),(#)(b,a));
A5: [.b,a.] c= the carrier of T by A1,Th12;
  set X = Closed-Interval-TSpace(b,a);
  the carrier of X = [.b,a.] by A4,TOPMETR:25;
  then reconsider f as map of I[01], T by A5,FUNCT_2:9,TOPMETR:27;
  take f;
  the carrier of X is Subset of the carrier of R^1 by TSEP_1:1;
  then reconsider g = f as map of I[01], R^1 by FUNCT_2:9,TOPMETR:27;
  L[01]((b,a)(#),(#)(b,a)) is continuous by A4,TREAL_1:11;
  then g is continuous by TOPMETR:7,27;
  hence f is continuous by TOPMETR:8;
  thus f.0 = f.(#)(0,1) by TREAL_1:def 1
    .= (b,a)(#) by A4,TREAL_1:12
    .= a by A4,TREAL_1:def 2;
  thus f.1 = f.(0,1)(#) by TREAL_1:def 2
    .= (#)(b,a) by A4,TREAL_1:12
    .= b by A4,TREAL_1:def 1;
  end;
end;
end;

theorem Th13:
  a <= b implies Closed-Interval-TSpace(a,b) is convex
proof
  set X = Closed-Interval-TSpace(a,b);
  assume a <= b;
  then [.a,b.] = the carrier of X by TOPMETR:25
    .= [#]X by PRE_TOPC:12;
  hence [#]X is convex Subset of R^1 by Th8;
end;

theorem Th14:
  I[01] is convex by Th13,TOPMETR:27;

theorem
  a <= b implies Closed-Interval-TSpace(a,b) is arcwise_connected
proof
  assume a <= b;
  then reconsider X = Closed-Interval-TSpace(a,b) as
    non empty convex SubSpace of R^1 by Th13;
  X is arcwise_connected;
  hence thesis;
end;

definition
  let T be non empty convex SubSpace of R^1,
      a, b be Point of T,
      P, Q be Path of a,b;
  func R1Homotopy(P,Q) -> map of [:I[01],I[01]:], T means :Def5:
  for s, t being Element of I[01] holds it.(s,t) = (1-t) * P.s + t * Q.s;
existence
proof
  defpred P[Element of I, Element of I, set] means
   $3 = (1-$2) * P.$1 + $2 * Q.$1;
A1: for m, n being Element of I
  ex z being Element of the carrier of T st P[m,n,z]
  proof
    let m, n be Element of I;
A2: 0 <= n & n <= 1 by JORDAN5A:2;
    set z = (1-n) * P.m + n * Q.m;
    per cases;
    suppose P.m <= Q.m;
    then P.m <= z & z <= Q.m by A2,JGRAPH_6:2;
    then
A3: z in [. P.m,Q.m .] by TOPREAL5:1;
    [. P.m,Q.m .] c= the carrier of T by Th12;
    hence thesis by A3;
    end;
    suppose
A4:   P.m > Q.m;
    then Q.m - P.m < Q.m - Q.m by REAL_1:92;
    then n*(Q.m-P.m) <= n*0 by A2,SQUARE_1:23;
    then
A5: P.m+n*(Q.m-P.m) <= P.m+0 by AXIOMS:24;
    1-1 <= 1-n by A2,REAL_1:92;
    then (1-n)*Q.m <= (1-n)*P.m by A4,AXIOMS:25;
    then (1-n)*Q.m+n*Q.m <= (1-n)*P.m+n*Q.m by AXIOMS:24;
    then
A6: z in [. Q.m, P.m .] by A5,TOPREAL5:1;
    [. Q.m, P.m .] c= the carrier of T by Th12;
    hence thesis by A6;
    end;
  end;
  consider F being Function of [:I,I:], the carrier of T such that
A7: for m, n being Element of I holds P[m,n,F. [m,n]] from FUNCT_2:sch 7(A1);
  reconsider F as map of [:I[01],I[01]:], T by Lm1;
  take F;
  let s, t be Element of I;
  thus F.(s,t) = F. [s,t] by BINOP_1:def 1
    .= (1-t) * P.s + t * Q.s by A7;
end;
uniqueness
proof
  let f, g be map of [:I[01],I[01]:], T such that
A8: for s, t being Element of I[01] holds f.(s,t) = (1-t) * P.s + t * Q.s
  and
A9: for s, t being Element of I[01] holds g.(s,t) = (1-t) * P.s + t * Q.s;
  for s, t being Element of I holds f.(s,t) = g.(s,t)
  proof
    let s, t be Element of I;
    thus f.(s,t) = (1-t) * P.s + t * Q.s by A8
      .= g.(s,t) by A9;
  end;
  hence f = g by Lm1,BINOP_1:2;
end;
end;

Lm8:
  for T being non empty convex SubSpace of R^1, a, b being Point of T,
      P, Q being Path of a,b holds R1Homotopy(P,Q) is continuous
proof
  let T be non empty convex SubSpace of R^1,
      a, b be Point of T,
      P, Q being Path of a,b;
  set F = R1Homotopy(P,Q);
  set X = the carrier of T;
  set E = the carrier of R^1;
  set PI = [:P,id I[01]:];
  set QI = [:Q,id I[01]:];
A1: the carrier of T is Subset of the carrier of R^1 by TSEP_1:1;
  then reconsider G = F as map of [:I[01],I[01]:],R^1 by FUNCT_2:9;
  a in the carrier of T & b in the carrier of T;
  then reconsider a1 = a, b1 = b as Point of R^1 by A1;
  a,b are_connected by BORSUK_2:def 3;
  then reconsider P1 = P, Q1 = Q as Path of a1,b1 by Th1;
  set P1I = [:P1,id I[01]:];
  set Q1I = [:Q1,id I[01]:];
A2: P1I is continuous & Q1I is continuous by BORSUK_2:12;
  defpred Fa[Element of E,Element of I,set] means $3 = (1-$2)*$1;
  defpred Fb[Element of E,Element of I,set] means $3 = $2*$1;
A3: for x being Element of E for y being Element of I
    ex z being Element of E st Fa[x,y,z]
  proof
    let x be Element of E,
        y be Element of I;
    (1-y)*x in REAL by XREAL_0:def 1;
    hence thesis by TOPMETR:24;
  end;
  consider Pa being Function of [:E,I:],E such that
A4: for x being Element of E, i being Element of I holds Fa[x,i,Pa. [x,i]]
    from FUNCT_2:sch 7(A3);
A5: for x being Element of E for y being Element of I
      ex z being Element of E st Fb[x,y,z]
  proof
    let x be Element of E,
        y be Element of I;
    y*x in REAL by XREAL_0:def 1;
    hence thesis by TOPMETR:24;
  end;
  consider Pb being Function of [:E,I:],E such that
A6: for x being Element of E, i being Element of I holds Fb[x,i, Pb. [x,i]]
   from FUNCT_2:sch 7(A5);
  reconsider Pa, Pb as map of [:R^1,I[01]:],R^1 by Lm3;
A7: for x being Element of E, i being Element of I holds Pa.(x,i) = (1-i)*x
  proof
    let x be Element of E, i be Element of I;
    thus Pa.(x,i) = Pa. [x,i] by BINOP_1:def 1
    .= (1-i)*x by A4;
  end;
  for x being Element of E, i being Element of I holds Pb.(x,i) = i*x
  proof
    let x be Element of E, i be Element of I;
    thus Pb.(x,i) = Pb. [x,i] by BINOP_1:def 1
    .= i*x by A6;
  end;
  then Pa is continuous & Pb is continuous by A7,Th6,Th7;
  then
A8: Pa*P1I is continuous & Pb*Q1I is continuous by A2,TOPS_2:58;
A9:  for p being Point of [:I[01],I[01]:] holds G.p = (Pa*P1I).p + (Pb*Q1I).p
  proof
    let p be Point of [:I[01],I[01]:];
    consider s, t being Point of I[01] such that
A10:   p = [s,t] by BORSUK_1:50;
A11: dom P = I by FUNCT_2:def 1;
A12: dom Q = I by FUNCT_2:def 1;
A13: (id I[01]).t = t by GRCAT_1:11;
A14: (Pa*PI).p = Pa.(PI.p) by FUNCT_2:21
      .= Pa. [P.s,t] by A10,A11,A13,Lm4,FUNCT_3:def 9
      .= (1-t) * P1.s by A4;
A15: (Pb*QI).p = Pb.(QI.p) by FUNCT_2:21
      .= Pb. [Q.s,t] by A10,A12,A13,Lm4,FUNCT_3:def 9
      .= t * Q1.s by A6;
    reconsider a1 = P.s, b1 = Q.s as Point of R^1 by TOPMETR:2;
    F.(s,t) = (1-t) * a1 + t * b1 by Def5;
    hence G.p = (Pa*P1I).p + (Pb*Q1I).p by A10,A14,A15,BINOP_1:def 1;
  end;
  consider g being map of [:I[01],I[01]:],R^1 such that
A16:  for p being Point of [:I[01],I[01]:],r1,r2 being real number st
      (Pa*P1I).p=r1 & (Pb*Q1I).p=r2 holds g.p=r1+r2 and
A17: g is continuous by A8,JGRAPH_2:29;
  for p being Point of [:I[01],I[01]:] holds G.p = g.p
  proof
    let p be Point of [:I[01],I[01]:];
    thus G.p = (Pa*P1I).p + (Pb*Q1I).p by A9
      .= g.p by A16;
  end;
  then G = g by FUNCT_2:113;
  hence F is continuous by A17,TOPMETR:8;
end;

Lm9:
  for T being non empty convex SubSpace of R^1,
      a, b being Point of T,
      P, Q being Path of a,b holds
  for s being Point of I[01] holds R1Homotopy(P,Q).(s,0) = P.s &
                                   R1Homotopy(P,Q).(s,1) = Q.s &
  for t being Point of I[01] holds R1Homotopy(P,Q).(0,t) = a &
                                   R1Homotopy(P,Q).(1,t) = b
proof
  let T be non empty convex SubSpace of R^1,
      a, b be Point of T,
      P, Q be Path of a,b;
  set F = R1Homotopy(P,Q);
  let s be Point of I[01];
  thus F.(s,0) = (1-0) * P.s + 0 * Q.s by Def5,BORSUK_1:def 17
    .= P.s;
  thus F.(s,1) = (1-1) * P.s + 1 * Q.s by Def5,BORSUK_1:def 18
    .= Q.s;
  let t be Point of I[01];
A1: P.0[01] = a by BORSUK_1:def 17,BORSUK_2:def 4;
A2: Q.0[01] = a by BORSUK_1:def 17,BORSUK_2:def 4;
A3: P.1[01] = b by BORSUK_1:def 18,BORSUK_2:def 4;
A4: Q.1[01] = b by BORSUK_1:def 18,BORSUK_2:def 4;
  thus F.(0,t) = (1-t) * P.0[01] + t * Q.0[01] by Def5,BORSUK_1:def 17
    .= a by A1,A2;
  thus F.(1,t) = (1-t) * P.1[01] + t * Q.1[01] by Def5,BORSUK_1:def 18
    .= b by A3,A4;
end;

theorem Th16:
  for T being non empty convex SubSpace of R^1,
      a, b being Point of T,
      P, Q being Path of a,b holds
  P, Q are_homotopic
proof
  let T be non empty convex SubSpace of R^1,
      a, b be Point of T,
      P, Q be Path of a,b;
  take F = R1Homotopy(P,Q);
  thus F is continuous by Lm8;
  thus thesis by Lm9;
end;

definition
  let T be non empty convex SubSpace of R^1,
      a, b be Point of T,
      P, Q be Path of a,b;
  redefine func R1Homotopy(P,Q) -> Homotopy of P,Q;
coherence
proof
  thus P, Q are_homotopic by Th16;
  thus R1Homotopy(P,Q) is continuous by Lm8;
  thus thesis by Lm9;
end;
end;

registration
  let T be non empty convex SubSpace of R^1,
      a, b be Point of T,
      P, Q be Path of a,b;
  cluster -> continuous Homotopy of P,Q;
coherence
proof
  let F be Homotopy of P,Q;
  P, Q are_homotopic by Th16;
  hence F is continuous by BORSUK_6:def 13;
end;
end;

theorem Th17:
  for T being non empty convex SubSpace of R^1,
      a being Point of T,
      C being Loop of a holds
   the carrier of pi_1(T,a) = { Class(EqRel(T,a),C) }
proof
  let T be non empty convex SubSpace of R^1,
      a be Point of T,
      C be Loop of a;
  set E = EqRel(T,a);
  hereby
    let x be set;
    assume x in the carrier of pi_1(T,a);
    then consider P being Loop of a such that
A1:   x = Class(E,P) by TOPALG_1:48;
    P,C are_homotopic by Th16;
    then x = Class(E,C) by A1,TOPALG_1:47;
    hence x in { Class(E,C) } by TARSKI:def 1;
  end;
  let x be set;
  assume x in { Class(E,C) };
  then
A2: x = Class(E,C) by TARSKI:def 1;
  C in Loops a by TOPALG_1:def 1;
  then x in Class E by A2,EQREL_1:def 5;
  hence thesis by TOPALG_1:def 3;
end;

registration
  let T be non empty convex SubSpace of R^1,
      a be Point of T;
  cluster pi_1(T,a) -> trivial;
coherence
proof
  set E = EqRel(T,a);
  consider C being constant Loop of a;
A1: the carrier of pi_1(T,a) = Class E by TOPALG_1:def 3;
  then { Class(E,C) } = Class E by Th17;
  hence thesis by A1,REALSET2:def 5;
end;
end;

theorem
  a <= b implies
  for x, y being Point of Closed-Interval-TSpace(a,b),
      P, Q being Path of x,y holds
  P, Q are_homotopic
proof
  assume
A1: a <= b;
  let x, y be Point of Closed-Interval-TSpace(a,b),
      P, Q be Path of x,y;
  Closed-Interval-TSpace(a,b) is convex by A1,Th13;
  hence thesis by Th16;
end;

theorem
  a <= b implies
  for x being Point of Closed-Interval-TSpace(a,b),
      C being Loop of x holds
  the carrier of pi_1(Closed-Interval-TSpace(a,b),x) =
  { Class(EqRel(Closed-Interval-TSpace(a,b),x),C) }
proof
  assume
A1: a <= b;
  let x be Point of Closed-Interval-TSpace(a,b),
      C be Loop of x;
  Closed-Interval-TSpace(a,b) is convex by A1,Th13;
  hence thesis by Th17;
end;

theorem
  for x, y being Point of I[01],
      P, Q being Path of x,y holds
  P, Q are_homotopic by Th14,Th16;

theorem Th21:
  for x being Point of I[01],
      C being Loop of x holds
  the carrier of pi_1(I[01],x) = { Class(EqRel(I[01],x),C) } by Th14,Th17;

registration
  let x be Point of I[01];
  cluster pi_1(I[01],x) -> trivial;
coherence
proof
  set E = EqRel(I[01],x);
  consider C being constant Loop of x;
A1: the carrier of pi_1(I[01],x) = Class E by TOPALG_1:def 3;
  then { Class(E,C) } = Class E by Th21;
  hence thesis by A1,REALSET2:def 5;
end;
end;

