:: Cyclic Groups and Some of Their Properties - Part I
::  by Dariusz Surowik
::
:: Received November 22, 1991
:: Copyright (c) 1991 Association of Mizar Users

environ

 vocabularies INT_1, REALSET1, FINSEQ_1, NAT_1, ORDINAL2, ARYTM, BINOP_1,
      FUNCT_1, VECTSP_1, QC_LANG1, SETWISEO, RLVECT_1, FINSEQ_2, GROUP_1,
      GROUP_4, RELAT_1, ARYTM_1, FINSET_1, CARD_1, GROUP_2, TARSKI, ARYTM_3,
      FILTER_0, RLSUB_1, GRAPH_1, GR_CY_1, CARD_3, COMPLEX1, COMPLSP1,
      SUBSET_1, BOOLE;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0,
      RELAT_1, FUNCT_1, REAL_1, SEQ_1, FUNCT_2, CARD_1, FINSET_1, BINOP_1,
      DOMAIN_1, XREAL_0, INT_1, FINSEQ_1, FINSUB_1, FINSOP_1, SETWISEO,
      SETWOP_2, BINOP_2, REALSET1, INT_2, NAT_1, NAT_D, RVSUM_1, RLVECT_1,
      GROUP_2, XXREAL_0, STRUCT_0, GROUP_1, GROUP_4;
 constructors WELLORD2, DOMAIN_1, BINOP_1, SETWISEO, XXREAL_0, REAL_1, NAT_1,
      BINOP_2, MEMBERED, FINSOP_1, REALSET1, INT_2, RLVECT_1, GROUP_4,
      ORDINAL2, RVSUM_1, SETWOP_2, PARTFUN1, FINSEQOP, NAT_D;
 registrations SUBSET_1, INT_1, GROUP_1, GROUP_2, RELSET_1, STRUCT_0, FINSEQ_1,
      XREAL_0, MEMBERED, ORDINAL2, NAT_1, BINOP_2, REALSET1, XXREAL_0, SEQ_1,
      ORDINAL1, RVSUM_1, SETWISEO;
 requirements NUMERALS, REAL, SUBSET, BOOLE, ARITHM;
 definitions GROUP_1, FUNCT_1, TARSKI, BINOP_1, REALSET1, FINSOP_1, FINSEQ_2,
      FINSEQ_1, RVSUM_1, FVSUM_1, GROUP_4, SETWOP_2;
 theorems BINOP_1, INT_1, INT_2, NAT_1, REAL_1, FINSEQ_1, FINSEQ_2, FINSEQ_4,
      ZFMISC_1, FUNCT_2, FUNCOP_1, GROUP_1, GROUP_2, GROUP_4, TARSKI, FUNCT_1,
      CARD_1, WELLORD2, RLVECT_1, FINSOP_1, RELSET_1, XBOOLE_0, XBOOLE_1,
      RELAT_1, BINOP_2, NUMBERS, SETWISEO, XREAL_1, XXREAL_0, ORDINAL1,
      RVSUM_1, SETWOP_2, FINSEQ_3, NAT_D;
 schemes BINOP_1, NAT_1, FINSEQ_1, FINSEQ_2;

begin

 reserve i1 for Element of INT;
 reserve j1,j2,j3 for Integer;
 reserve p,s,k,n,m for Element of NAT;
 reserve x,y,xp,yp for set;
 reserve G for Group;
 reserve a,b for Element of G;
 reserve F for FinSequence of the carrier of G;
 reserve I for FinSequence of INT;

definition let n be natural number such that A1:n > 0;
 func Segm (n) -> non empty Subset of NAT equals
:Def1:  {p: p<n};
coherence
proof
 set X={p:p<n};
     0 in X by A1;
 then reconsider X as non empty set;
     X c= NAT
  proof
       for x holds x in X implies x in NAT
    proof
     let x be set;
     assume x in X;
     then ex p being Element of NAT st p=x & p<n;
     hence thesis;
    end;
   hence thesis by TARSKI:def 3;
 end;
 hence thesis;
 end;
end;

Lm1:
 for n st n > 0 holds x in Segm (n) implies x is Element of NAT;

canceled 9;

theorem Th10:
  for n,s being natural number st n > 0 holds s in Segm (n) iff s < n
 proof
  let n,s be natural number;
  assume A1: n>0;
A2: s is Element of NAT by ORDINAL1:def 13;
      s in { p : p<n } iff ex p st s=p & p<n;
  hence thesis by A1,A2,Def1;
 end;

canceled;

theorem
  for n being natural number st n > 0 holds 0 in Segm (n) by Th10;

theorem Th13:
  Segm (1) = {0}
 proof
      now let x;
  thus x in Segm(1) implies x in { 0 }
    proof assume A1:x in Segm(1);
         Segm(1) = { p : p < 1 } by Def1;
     then consider p such that
     A2:x = p & (p < 1) by A1;
         p < 0 + 1 by A2;
     then p <= 0 by NAT_1:13;
     then p = 0;
     hence thesis by A2,TARSKI:def 1;
    end;
    assume x in { 0 };
    then x = 0 by TARSKI:def 1;
    hence x in Segm(1) by Th10;
    end;
  hence Segm(1) = { 0 } by TARSKI:2;
end;

       :::::::::::::::::::::::::::::::::::::::::::::::::::::
       ::              Definition addint                  ::
       :::::::::::::::::::::::::::::::::::::::::::::::::::::

definition
  redefine func addint means
  for i1,i2 being Element of INT holds
  it.(i1,i2) = addreal.(i1,i2);
  compatibility
  proof let b be BinOp of INT;
   hereby assume
A1:   b = addint;
    let i1,i2 be Element of INT;
     i1 in INT & i2 in INT;
     then
A2:   i1 in REAL & i2 in REAL by NUMBERS:15;
    thus b.(i1,i2) = i1 + i2 by A1,BINOP_2:def 20
        .= addreal.(i1,i2) by A2,BINOP_2:def 9;
   end;
   assume
A3:  for i1,i2 being Element of INT holds
      b.(i1,i2) = addreal.(i1,i2);
    now let i1,i2 be Element of INT;
      i1 in INT & i2 in INT;
      then
A4:   i1 in REAL & i2 in REAL by NUMBERS:15;
     thus b.(i1,i2) = addreal.(i1,i2) by A3
        .= i1 + i2 by A4,BINOP_2:def 9
        .= addint.(i1,i2) by BINOP_2:def 20;
    end;
   hence b = addint by BINOP_1:2;
  end;
end;

theorem Th14:
  addint.(j1,j2)=j1+j2
  proof
    reconsider i1=j1, i2=j2 as Element of INT by INT_1:def 2;
    addint.(i1,i2)=j1+j2 by BINOP_2:def 20;
   hence thesis;
  end;

theorem
  for i1 st i1 = 0 holds i1 is_a_unity_wrt addint by BINOP_2:4,SETWISEO:22;

Lm2: now let n be natural number;
  thus Seg n c= NAT
  proof let x; assume x in Seg n;
    then x in { k : 1 <= k & k <= n };
    then (ex k st x = k & 1 <= k & k <= n) &
    ex x st x in NAT;
    hence x in NAT;
  end;
 end;

theorem Th16:
  Sum I = addint $$ I
  proof
    rng I c= COMPLEX by XBOOLE_1:1,NUMBERS:11; then
    reconsider f = I as FinSequence of COMPLEX by FINSEQ_1:def 4;
    set g = addint, h = addcomplex;
    consider n being Element of NAT such that
A1: dom f = Seg n by FINSEQ_1:def 2;
    set i = [#](I,the_unity_wrt g);
    set j = [#](f,the_unity_wrt h);
A2: g $$ I = g $$ (Seg n,i) by A1,SETWOP_2:def 2;
A3: h $$ f = h $$ (Seg n,j) by A1,SETWOP_2:def 2;
    defpred P[Element of NAT] means
      g $$ (Seg $1,i) = h $$ (Seg $1,j);
A4: Seg 0 = {}.NAT by FINSEQ_1:4;
A5: P[0]
    proof
      g $$ (Seg 0,[#](I,the_unity_wrt g)) =
           the_unity_wrt h by BINOP_2:1,4,SETWISEO:40,A4
        .= h $$ (Seg 0,[#](f,the_unity_wrt h)) by A4,SETWISEO:40;
      hence thesis;
    end;
A6: for k being Element of NAT st P[k] holds P[k+1]
    proof
      let k be Element of NAT;
      assume
A7:   P[k];
A8:   i.(k+1) = j.(k+1)
      proof
        per cases;
        suppose
A9:       k+1 in dom f; then
          j.(k+1) = I.(k+1) by SETWOP_2:22
                 .= i.(k+1) by A9,SETWOP_2:22;
          hence thesis;
        end;
        suppose
       not k+1 in dom f;
          j.(k+1) = i.(k+1) by  BINOP_2:1,4;
          hence thesis;
        end;
      end;
      set a = i.(k+1);
      set b = g $$(Seg k,i);
A10:   not (k + 1) in Seg k by FINSEQ_3:9;
      g $$ (Seg (k+1),i) = g $$ (Seg k \/ {. k+1 .},i) by FINSEQ_1:11
        .= g.(b,a) by A10,SETWOP_2:4
        .= b + a by BINOP_2:def 20
        .= h.(h $$(Seg k,j),j.(k+1)) by BINOP_2:def 3,A7,A8
        .= h $$ (Seg k \/ {. k+1 .},j) by A10,SETWOP_2:4
        .= h $$ (Seg (k+1),j) by FINSEQ_1:11;
      hence thesis;
    end;
    for k being Element of NAT holds P[k] from NAT_1:sch 1(A5,A6); then
    g $$ I = h $$ f by A2,A3;
    hence thesis by RVSUM_1:def 11;
  end;

definition let I;
  redefine func Sum(I) -> Element of INT equals
  addint $$ I;
  coherence
  proof
    Sum I = addint $$ I by Th16;
    hence thesis;
  end;
  compatibility by Th16;
end;

canceled 3;

theorem Th20:
  Sum(I^<*i1*>) = Sum I + i1 by RVSUM_1:104;

canceled;

theorem Th22:
  Sum (<*> INT) = 0 by BINOP_2:4,FINSOP_1:11;

Lm3: Product(((len(<*> INT)) |->a)|^(<*> INT))=a|^Sum(<*> INT)
 proof
  A1:(<*> the carrier of G)|^(<*> INT)=<*> the carrier of G by GROUP_4:27;
    len(<*> INT)|->a =0 |-> a by FINSEQ_1:32 .=<*> the carrier of G
         by FINSEQ_2:72;
       then Product((len(<*> INT)|->a)|^(<*> INT))=1_G by A1,GROUP_4:11 .=
a|^(Sum(<*> INT)) by Th22,GROUP_1:59;
 hence thesis;
end;

Lm4: for I being FinSequence of INT for w being Element of INT
st Product(((len I)|->a)|^I)=a|^Sum I holds
   Product(((len (I^<*w*>))|->a)|^(I^<*w*>))=a|^Sum(I^<*w*>)
proof
 let I;
 let w be Element of INT;
 assume A1:Product(((len I)|->a)|^I)=a|^Sum I;
 A2:len((len I) |->a) =len I by FINSEQ_2:69;
 A3:len <*a*> = 1 by FINSEQ_1:57 .= len <*w*> by FINSEQ_1:57;
     Product(((len (I^<*w*>))|->a)|^(I^<*w*>))
     =Product(((len I+1)|->a)|^(I^<*w*>)) by FINSEQ_2:19
    .= Product((((len I)|->a)^<*a*>)|^(I^<*w*>)) by FINSEQ_2:74
    .= Product((((len I)|->a)|^I)^(<*a*>|^<*w*>)) by A2,A3,GROUP_4:25
    .= Product((((len I)|->a)|^I))*Product(<*a*>|^<*@(@w)*>) by GROUP_4:8
    .= Product((((len I)|->a)|^I))*Product(<*a|^(@w)*>) by GROUP_4:28
    .= (a|^Sum I)*(a|^(@w)) by A1,FINSOP_1:12
    .= a|^(Sum I+@w) by GROUP_1:63
    .= a|^Sum(I^<*w*>) by Th20;
  hence thesis;
end;

canceled;

theorem Th24:
  for I being FinSequence of INT holds Product(((len I)|->a)|^I) = a|^Sum I
proof
 defpred P[FinSequence of INT] means
   Product(((len $1) |->a)|^($1))=a|^Sum($1);
 A1: P[<*> INT] by Lm3;
 A2: for p being FinSequence of INT for x being Element of INT
      st P[p] holds P[p^<*x*>] by Lm4;
   for p being FinSequence of INT holds P[p] from FINSEQ_2:sch 2(A1,A2);
 hence thesis;
end;

    :::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
    ::          Finite groups and their some properties              ::
    :::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

theorem Th25:
  b in gr {a} iff ex j1 st b=a|^j1
proof
 A1:b in gr {a} implies ex j1 st b=a|^j1
  proof
   assume b in gr {a};
   then consider F,I such that A2: len F = len I and
   A3: rng F c= {a} and A4: Product(F |^ I ) = b by GROUP_4:37;
   take Sum I;
A5:dom ((len F) |-> a) = Seg len F by FUNCOP_1:19
                      .= dom F by FINSEQ_1:def 3;
       for p st p in dom F holds F.p = ((len F)|-> a).p
    proof
     let p;
A6:  dom F = Seg len F by FINSEQ_1:def 3;
     assume A7: p in dom F;
     then F.p in rng F by FUNCT_1:def 5;
     then F.p = a by A3,TARSKI:def 1 .= ((len F)|-> a).p by A6,A7,FUNCOP_1:13;
     hence thesis;
    end;
   then b= Product(((len I) |-> a)|^I) by A2,A4,A5,FINSEQ_1:17
           .= a|^Sum I by Th24;
   hence thesis;
end;
     (ex j1 st b=a|^j1) implies b in gr {a}
  proof
   given j1 such that A8: b =a|^j1;
   consider n such that A9: j1 = n or j1 = -n by INT_1:8;
    now per cases by A9;
 suppose A10:j1=n;
        ex F,I st len F = len I & rng F c= {a} & Product(F |^ I) = b
    proof
     take F =(n |-> a);
     A11: len F = n by FINSEQ_2:69 .= len(n |->@ 1) by FINSEQ_2:69;
     A12: rng F c= {a} by FUNCOP_1:19;
          Product(F|^(n |->@ 1)) = Product(F|^(len F |->@ 1)) by FINSEQ_2:69
               .=Product(n |-> a) by GROUP_4:31
               .=b by A8,A10,GROUP_4:15;
     hence thesis by A11,A12;
     end;
   hence thesis by GROUP_4:37;
end;
suppose j1 = -n;
     then A13: b"=a|^(-(-n)) by A8,GROUP_1:70 .= a|^n;
         ex F,I st len F = len I & rng F c= {a} & Product(F |^ I) = b"
      proof
       take F =(n |-> a);
       A14: len F = n by FINSEQ_2:69 .= len(n |->@ 1) by FINSEQ_2:69;
       A15: rng F c= {a} by FUNCOP_1:19;
            Product(F|^(n |->@ 1)) = Product(F|^(len F |->@ 1)) by FINSEQ_2:69
                 .=Product(n |-> a) by GROUP_4:31 .= b" by A13,GROUP_4:15;
       hence thesis by A14,A15;
     end;
  then b" in gr {a} by GROUP_4:37;
  then b"" in gr {a} by GROUP_2:60;
  hence thesis by GROUP_1:19;
 end;
 end;
hence thesis;
end;
hence thesis by A1;
end;

theorem Th26:
  G is finite implies a is_not_of_order_0
  proof
    assume A1:G is finite;
      ex n st n<> 0 & a|^n = 1_G
   proof
    deffunc F(Element of NAT) = a|^$1;
    consider F being FinSequence such that A2: len F = ord G +1 and
    A3: for p st p in Seg (ord G +1) holds F.p = F(p) from FINSEQ_1:sch 2;
    A4: dom F = Seg (ord G + 1) by A2,FINSEQ_1:def 3;
A5: Seg (ord G +1) c= NAT by Lm2;
    A6: for y st y in rng F holds ex n st y=a|^n
      proof
       let y;
       assume y in rng F;
       then consider x such that A7: x in dom F and A8: F.x=y by FUNCT_1:def 5;
       reconsider n=x as Element of NAT by A7,A5,A4;
       take n;
       thus thesis by A3,A4,A7,A8;
      end;
       rng F c= the carrier of G
    proof
         for x holds x in rng F implies x in the carrier of G
       proof
        let y;
        assume y in rng F;
        then ex n st y= a|^n by A6;
        hence thesis;
       end;
      hence thesis by TARSKI:def 3;
    end;
then reconsider F'=F as Function of Seg (ord G +1),the carrier of G by A4,
FUNCT_2:def 1,RELSET_1:11;
  consider c being finite set such that
A9: c = the carrier of G & ord G = card c by A1,GROUP_1:def 15;
A10:Card ord G = Card the carrier of G by A9,CARD_1:def 11;
A11:Card Seg(ord G+1) = Card (ord G +1) by FINSEQ_1:76;
    ord G < ord G +1 by XREAL_1:31;
then Card the carrier of G <` Card Seg (ord G +1) by A10,A11,CARD_1:73;
then consider x,y such that A12:x in Seg (ord G +1) and
A13: y in Seg (ord G +1)
                and A14:x <> y and A15: F'.x = F'.y by FINSEQ_4:80;
reconsider p=x,n=y as Element of NAT by A12,A13,A5;
     now per cases by A14,REAL_1:def 5;
  suppose A16: n>p;
           then reconsider t = n-p as Element of NAT by INT_1:18;
           reconsider e=0 as Integer;
           take t;
           A17: t <>0 by A16;
                F'.p =a|^p by A3,A12;
           then a|^n=a|^p by A3,A13,A15;
           then a|^(n+-p)=a|^p *a|^(-p) by GROUP_1:64;
           then a|^t =a|^(p+-p) by GROUP_1:64;
           then a|^t=1_G by GROUP_1:59;
      hence thesis by A17;
   end;
   suppose A18: p>n;
            then reconsider t = p-n as Element of NAT by INT_1:18;
            take t;
            reconsider e=0 as Integer;
            A19: t <>0 by A18;
                 F'.p =a|^p by A3,A12;
            then a|^n=a|^p by A3,A13,A15;
            then a|^(p+-n)=a|^n *a|^(-n) by GROUP_1:64;
            then a|^t =a|^(n+-n) by GROUP_1:64;
            then a|^t=1_G by GROUP_1:59;
      hence thesis by A19;
    end;
    end;
   hence thesis;
  end;
 hence thesis by GROUP_1:def 11;
end;

theorem Th27:
  G is finite implies ord a = ord gr {a}
 proof
  assume A1:G is finite;
  then A2:gr {a} is finite by GROUP_2:48;
  deffunc F(Element of NAT) = a|^$1;
  consider F being FinSequence such that A3: len F = ord a and
  A4:  for p st p in Seg ord a holds F.p = F(p) from FINSEQ_1:sch 2;
  A5: dom F = Seg ord a by A3,FINSEQ_1:def 3;
      a is_not_of_order_0 by A1,Th26;
  then A6:ord a <> 0 by GROUP_1:def 12;
  then A7:ord a >0;
   A8: a in rng F
    proof
          ex x st x in dom F & F.x=a
        proof
         set x'=1;
         A9:x' in dom F
           proof
                (ord a)+x' > 0+x' by A7,XREAL_1:8;
            then (ord a) >= x' by NAT_1:13;
            hence thesis by A5;
           end;
         then F.x'=a|^x' by A4,A5 .=a by GROUP_1:44;
         hence thesis by A9;
        end;
     hence thesis by FUNCT_1:def 5;
   end;
A10: Seg ord a c= NAT by Lm2;
A11: rng F = the carrier of gr {a}
 proof
   A12:for y st y in rng F holds ex n st y=a|^n
     proof
      let y;
      assume y in rng F;
      then consider x such that
A13:  x in dom F and A14: F.x=y by FUNCT_1:def 5;
      reconsider n=x as Element of NAT by A13,A10,A5;
      take n;
      thus thesis by A4,A5,A13,A14;
     end;
A15: rng F c= the carrier of gr {a}
    proof
         for x holds x in rng F implies x in the carrier of gr {a}
      proof
       let y;
       assume y in rng F;
       then consider n such that A16:y= a|^n by A12;
           y in gr {a} by A16,Th25;
       hence thesis by RLVECT_1:def 1;
      end;
     hence thesis by TARSKI:def 3;
   end;
    the carrier of gr {a} c= rng F
 proof
      ex H being strict Subgroup of G st the carrier of H = rng F
   proof
     reconsider car=rng F as non empty set by A8;
     A17:dom(the mult of G)=
     [:the carrier of G,the carrier of G:] by FUNCT_2:def 1;
         the carrier of gr {a} c= the carrier of G by GROUP_2:def 5;
     then A18: car c= the carrier of G by A15,XBOOLE_1:1;
     then [:car,car:] c= [:the carrier of G,the carrier of G:] by ZFMISC_1:119
;
 then A19:dom((the mult of G)||car)=[:car,car:] by A17,RELAT_1:91;
         rng((the mult of G)||car) c= car
       proof
             for y st y in rng((the mult of G)||car) holds y in car
            proof
              set f=(the mult of G)||car;
              let y;
              assume y in rng(f);
              then consider x such that A20:x in dom(f) and
              A21: f.x=y by FUNCT_1:def 5;
              consider xp,yp such that A22:[xp,yp]=x
               by A19,A20,ZFMISC_1:102;
                  xp in car by A19,A20,A22,ZFMISC_1:106;
              then consider p such that A23:  xp = a|^p by A12;
                  yp in car by A19,A20,A22,ZFMISC_1:106;
              then consider s such that A24:  yp = a|^s by A12;
              A25:  y = a|^p*(a|^s) by A20,A21,A22,A23,A24,FUNCT_1:70
                      .= a|^(p+s) by GROUP_1:48;
                  a|^(p+s) in car
               proof
                 ex x st x in dom F & F.x=a|^(p+s)
                 proof
                  set t=(p+s) mod (ord a);
                  A26:t < ord a by A7,NAT_D:1;
                  per cases;
                    suppose A27:t=0;
                A28:ord a in dom F by A5,A6,FINSEQ_1:5;
  A29:a|^(p+s)=a|^((ord a)*((p+s) div (ord a)) + ((p+s) mod (ord a))) by A7,
NAT_D:2
    .=a|^((ord a)*((p+s) div (ord a)))*(a|^ ((p+s) mod (ord a))) by GROUP_1:48
    .=a|^(ord a)|^((p+s) div (ord a))*(a|^ ((p+s) mod (ord a))) by GROUP_1:50
    .=(1_G)|^((p+s) div (ord a))*(a|^ ((p+s) mod (ord a))) by GROUP_1:82
    .=(1_G) *(a|^ ((p+s) mod (ord a))) by GROUP_1:42
    .=a|^0 by A27,GROUP_1:def 5 .= 1_G by GROUP_1:43
    .= a|^(ord a) by GROUP_1:82 .= F.ord a by A4,A5,A28;
     take x = ord a;
     thus x in dom F & F.x=a|^(p+s) by A5,A6,A29,FINSEQ_1:5;
                          end;
                          suppose t>0;
                             then t+1 > 0+1 by XREAL_1:8;
                             then A30:t >= 1 by NAT_1:13;
                             take x = t;
                             A31:t in dom F by A5,A26,A30;
      a|^(p+s)=a|^((ord a)*((p+s) div (ord a)) + ((p+s) mod (ord a))) by A7,
NAT_D:2
    .=a|^((ord a)*((p+s) div (ord a))) *(a|^ ((p+s) mod (ord a))) by GROUP_1:48
    .=a|^(ord a)|^((p+s) div (ord a)) *(a|^ ((p+s) mod (ord a))) by GROUP_1:50
    .=(1_G)|^((p+s) div (ord a)) *(a|^ ((p+s) mod (ord a))) by GROUP_1:82
    .=(1_G) *(a|^ ((p+s) mod (ord a))) by GROUP_1:42
    .=a|^ ((p+s) mod (ord a)) by GROUP_1:def 5;
         hence x in dom F & F.x=a|^(p+s) by A4,A5,A31;
       end;
       end;
       hence thesis by FUNCT_1:def 5;
     end;
   hence thesis by A25;
   end;
hence thesis by TARSKI:def 3;
  end;
 then reconsider op=(the mult of G)||car as BinOp of car by A19,
FUNCT_2:def 1,RELSET_1:11;
 set H=HGrStr(#car,op#);
     H is Group
 proof
A32: for f,g being Element of H ,
        f',g' being Element of G
    st f = f' & g = g' holds f'*g' = f*g
 proof
  let f,g be Element of H;
  let f' ,g' be Element of G;
  assume A33: f = f' & g =g';
  set A = [:car,car:];
  A34:f*g = ((the mult of G)||car).[f,g];
  thus thesis by A33,A34,FUNCT_1:72;
  end;

 A35:for f,g,h being Element of H holds (f * g) * h = f * (g * h)
 proof
  let f,g,h be Element of H;
  reconsider f'=f as Element of G by A18,TARSKI:def 3;
  reconsider g'=g as Element of G by A18,TARSKI:def 3;
  reconsider h'=h as Element of G by A18,TARSKI:def 3;
  A36:  f'*g' = f*g by A32;
  A37:  g * h = g' * h' by A32;
      (f*g)*h =(f'*g')*h' by A32,A36 .= f'*(g'*h') by GROUP_1:def 4
 .=f*(g*h) by A32,A37;
  hence thesis;
 end;
      ex e being Element of H st
    for h being Element of H holds
     h * e = h & e * h = h &
     ex g being Element of H st h * g = e & g * h = e
     proof
          a|^(ord a) in car
       proof
            ex x st x in dom F & F.x=a|^(ord a)
          proof
           set x'=(ord a);
           A38: x' in Seg ord a by A6,FINSEQ_1:5;
           then F.x'=a|^x' by A4;
           hence thesis by A5,A38;
          end;
        hence thesis by FUNCT_1:def 5;
     end;
 then reconsider e=1_G as Element of H by GROUP_1:82;
 take e;
     for h being Element of H holds
 h * e = h & e * h = h &
 ex g being Element of H st h * g = e & g * h = e
  proof
   let h be Element of H;
   reconsider h'=h as Element of G by A18,TARSKI:def 3;
   thus h * e = h
    proof
         h*e = h' *(1_G) by A32 .= h' by GROUP_1:def 5;
     hence thesis;
    end;
   thus e * h = h
    proof
      e*h = (1_G)*h' by A32 .= h' by GROUP_1:def 5;
     hence thesis;
    end;
   thus ex g being Element of H st h * g = e & g * h = e
    proof
         ex n st h=a|^n & (1 <= n & ord a >= n)
       proof
        consider x such that A39: x in dom F and A40: F.x=h by FUNCT_1:def 5;
        reconsider n=x as Element of NAT by A39,A10,A5;
        take n;
        thus thesis by A4,A5,A39,A40,FINSEQ_1:3;
       end;
     then consider n such that A41:h=a|^n and A42: ord a >= n;
         a|^(ord a - n) in car
      proof
           ex x st x in dom F & F.x =a|^(ord a - n)
        proof
          per cases by A42,REAL_1:def 5;
              suppose A43: ord a = n;
                   set x = ord a;
                   A44: x in dom F by A5,A6,FINSEQ_1:5;
                   then F.x=a|^x by A4,A5 .= 1_G by GROUP_1:82
                   .= a|^(ord a - n) by A43,GROUP_1:43;
                  hence thesis by A44;
              end;
              suppose A45:ord a > n;
                      then reconsider x = ord a - n as Element of NAT
                       by INT_1:18;
                      take x;
                         x in Seg ord a
                        proof
                         reconsider r1=ord a ,r2=n as Real;
                         A46:x >= 1
                          proof
                               r1 - r2 > r2 -r2 by A45,XREAL_1:11;
                           then x >= 0 +1 by NAT_1:13;
                           hence x >= 1;
                          end;
                          x <= ord a
                        proof
                             r1 <= r1 + r2 by NAT_1:11;
                         hence thesis by XREAL_1:22;
                        end;
                      hence thesis by A46;
                     end;
                hence thesis by A3,A4,FINSEQ_1:def 3;
          end;
          end;
        hence thesis by FUNCT_1:def 5;
      end;
       then reconsider g =a|^(ord a - n) as Element of H;
       A47:n+(ord a-n) =ord a;
     A48: h * g = e
       proof
             h * g =a|^n *(a|^(ord a - n)) by A32,A41
               .=a|^(ord a) by A47,GROUP_1:64
               .= e by GROUP_1:82;
           hence thesis;
       end;
         g * h = e
        proof
             g * h =a|^(ord a - n) * (a|^n) by A32,A41
               .=a|^(ord a) by A47,GROUP_1:65
               .= e by GROUP_1:82;
           hence thesis;
        end;
       hence thesis by A48;
           end;
            thus thesis;
           end;
     hence thesis;
           end;
 hence thesis by A35,GROUP_1:5;
 end;
then reconsider H1=H as Group;
    the carrier of gr {a} c= the carrier of G by GROUP_2:def 5;
then the carrier of H1 c= the carrier of G by A15,XBOOLE_1:1;
then H1 is Subgroup of G by GROUP_2:def 5;
hence thesis;
end;
then consider H being strict Subgroup of G such that
A49: the carrier of H = rng F;
    {a} c= the carrier of H by A8,A49,ZFMISC_1:37;
then gr {a} is Subgroup of H by GROUP_4:def 5;
hence the carrier of gr {a} c= rng F by A49,GROUP_2:def 5;
end;
hence thesis by A15,XBOOLE_0:def 10;
end;
    F is one-to-one
 proof
  let x,y;
  assume that A50: x in dom F and A51: y in dom F and A52: F.x = F.y
             and A53:x<>y;
  reconsider p=x as Element of NAT by A10,A50,A5;
  reconsider s=y as Element of NAT by A10,A51,A5;
  reconsider s'=s,p'=p,z= ord a as Real;
  A54:F.p=a|^p by A4,A5,A50;
  A55:F.s=a|^s by A4,A5,A51;
   per cases;
       suppose A56:p<=s;
         then A57:p<s by A53,REAL_1:def 5;
         reconsider r1=s-p as Element of NAT by A56,INT_1:18;
             1_G=a|^s*(a|^p)" by A52,A54,A55,GROUP_1:def 6;
         then a|^0=a|^s*(a|^p)" by GROUP_1:43;
         then a|^0=a|^s*(a|^(-p)) by GROUP_1:71;
         then a|^0=a|^(s+(-p)) by GROUP_1:64;
         then A58:1_G=a|^r1 by GROUP_1:43;
         A59: r1 <> 0 by A57;
         A60:p >0 by A5,A50,FINSEQ_1:3;
         A61:r1 < ord a
            proof
             A62: s'<=z by A5,A51,FINSEQ_1:3;
                 z<z+p' by A60,XREAL_1:31;
             then s'<z+p' by A62,XXREAL_0:2;
             then s'-p'<z+p'-p' by XREAL_1:11;
             hence thesis;
            end;
             a is_not_of_order_0 by A1,Th26;
       hence thesis by A58,A59,A61,GROUP_1:def 12;
   end;
   suppose A63:s<=p;
        then A64:s<p by A53,REAL_1:def 5;
        reconsider r2=p-s as Element of NAT by A63,INT_1:18;
            1_G=a|^p*(a|^s)" by A52,A54,A55,GROUP_1:def 6;
        then a|^0=a|^p*(a|^s)" by GROUP_1:43;
        then a|^0=a|^p*(a|^(-s)) by GROUP_1:71;
        then a|^0=a|^(p+(-s)) by GROUP_1:64;
        then A65:1_G=a|^r2 by GROUP_1:43;
        A66: r2 <> 0 by A64;
        A67:s >0 by A5,A51,FINSEQ_1:3;
       A68:r2 < ord a
         proof
          A69: p'<=z by A5,A50,FINSEQ_1:3;
              z<z+s' by A67,XREAL_1:31;
          then p'<z+s' by A69,XXREAL_0:2;
          then p'-s'<z+s'-s' by XREAL_1:11;
          hence thesis;
         end;
          a is_not_of_order_0 by A1,Th26;
     hence thesis by A65,A66,A68,GROUP_1:def 12;
end;
end;
then A70: Seg ord a,the carrier of gr {a} are_equipotent by A5,A11,
WELLORD2:def 4;
   consider ca being finite set such that
A71: ca = the carrier of gr {a} & ord gr {a} = card ca by A2,GROUP_1:def 15;
    reconsider So = Seg ord a as finite set;
        card So = card ca by A70,A71,CARD_1:21;
hence thesis by A71,FINSEQ_1:78;
end;

theorem Th28:
  G is finite implies ord a divides ord G
proof
 assume A1: G is finite;
 then ord a = ord gr {a} by Th27;
 hence ord a divides ord G by A1,GROUP_2:178;
end;

theorem Th29:
  G is finite implies a|^ord G = 1_G
proof
 assume G is finite;
 then ord a divides ord G by Th28;
 then consider t being Nat such that
 A1: ord G = ord a * t by NAT_D:def 3;
     a|^ord G = a |^ ord a |^ t by A1,GROUP_1:50
         .= (1_G) |^ t by GROUP_1:82 .= 1_G by GROUP_1:42;
 hence thesis;
end;

theorem Th30:
  G is finite implies (a|^n)" = a|^(ord G - (n mod ord G))
  proof
   assume A1: G is finite;
   set q = ord G;
   set p'=n mod q;
       q >=1 by A1,GROUP_1:90;
   then A2: q > 0;
   then n mod q <=q by NAT_D:1;
   then reconsider q'=q -( n mod q) as Element of NAT by INT_1:18;
       a|^n*(a|^(q-(n mod q)))=a|^(n+q') by GROUP_1:48
   .= a|^((q*(n div q)+(n mod q))+q') by A2,NAT_D:2
   .= a|^(q*(n div q)+q +(-p'+p'))
   .= a|^(q*(n div q)+q)*(a|^(-p'+p')) by GROUP_1:64
   .= a|^(q*(n div q)+q)*(a|^(-p')*(a|^p')) by GROUP_1:65
   .= a|^(q*(n div q)+q)*((a|^p')"*(a|^p')) by GROUP_1:71
   .= a|^(q*(n div q)+q)*(1_G) by GROUP_1:def 6
   .= a|^(q*(n div q+1)) by GROUP_1:def 5
   .= a|^q|^(n div q+1) by GROUP_1:50
   .= (1_G)|^(n div q+1) by A1,Th29
   .= 1_G by GROUP_1:42;
   hence thesis by GROUP_1:20;
  end;

theorem Th31:
  for G being strict Group holds
  ord G > 1 implies ex a being Element of G st a <> 1_G
  proof let G be strict Group;
   assume that A1: ord G > 1 and
   A2: for a being Element of G holds a = 1_G;
      for a being Element of G holds a in (1).G
     proof
      let a be Element of G;
          a = 1_G by A2;
      then a in {1_G} by TARSKI:def 1;
      then a in the carrier of (1).G by GROUP_2:def 7;
      hence thesis by RLVECT_1:def 1;
     end;
   then G = (1).G by GROUP_2:71;
   hence contradiction by A1,GROUP_2:81;
 end;

theorem
  for G being strict Group holds
  G is finite & ord G = p & p is prime implies
   for H being strict Subgroup of G holds H = (1).G or H = G
 proof let G be strict Group;
  assume that A1: G is finite and A2: ord G = p and A3: p is prime;
  let H be strict Subgroup of G;
  A4:H is finite by A1,GROUP_2:48;
      ord H divides p by A1,A2,GROUP_2:178;
  then ord H = 1 or ord H = p by A3,INT_2:def 5;
  hence thesis by A1,A2,A4,GROUP_2:82,85;
 end;

theorem Th33:
  HGrStr(#INT,addint#) is associative Group-like
  proof
   set G = HGrStr (# INT, addint #);
   A1: for h,g be Element of G, A,B be Integer
     st h = A & g = B
   holds h * g = A + B by Th14;
 thus for h,g,f being Element of G
  holds (h * g) * f = h * (g * f)
   proof let h,g,f be Element of G;
     reconsider A=h,B = g, C = f as Integer by INT_1:def 2;
     A2: h * g = A + B by A1;
     A3: g * f = B + C by A1;
 thus (h * g) * f = A + B + C by A1,A2
                 .= A + (B + C)
                 .= h * (g * f) by A1,A3;
   end;
  reconsider e=0 as Element of G by INT_1:def 2;
  take e;
  let h be Element of G;
  reconsider A = h as Integer by INT_1:def 2;
  thus h * e = A + 0 by A1
            .= h;
  thus e * h = 0 + A by A1
            .= h;
  reconsider g = - A as Element of G by INT_1:def 2;
  take g;
  thus h * g = A + (- A) by A1
            .= e;
  thus g * h = (- A) + A by A1
             .= e;
  end;

 definition
 func INT.Group -> strict Group equals  HGrStr(#INT,addint#);
       coherence by Th33;
 end;

registration
  cluster -> integer Element of INT.Group;
  coherence by INT_1:def 2;
end;

registration let a,b be Element of INT.Group;
 identify a*b with a+b;
 compatibility
  proof
   thus a*b = a+b by BINOP_2:def 20;
  end;
end;

definition let n be natural number such that A1:n > 0;
 func addint(n) -> BinOp of Segm(n) means
 :Def5: for k,l being Element of Segm(n) holds
  it.(k,l) = (k+l) mod n;
 existence
  proof
    defpred P[Element of Segm n,Element of Segm n,set] means
      $3 = ($1+$2) mod n;
    A2: for k,l being Element of Segm(n) ex c being Element of Segm(n) st
      P[k,l,c]
       proof
       let k,l be Element of Segm(n);
       reconsider k'=k,l'=l as Element of NAT;
            ((k'+l') mod n) < n by A1,NAT_D:1;
        then reconsider c = (k'+l') mod n as Element of Segm(n) by Th10;
       take c;
       thus thesis;
       end;
         ex o being BinOp of Segm n st
         for a,b being Element of Segm n holds
           P[a,b,o.(a,b)] from BINOP_1:sch 3(A2);
       hence thesis;
         end;
 uniqueness
   proof
        let i1,i2 be BinOp of Segm(n) such that
      A3:for k,l being Element of Segm(n) holds
        i1.(k,l)=((k+l) mod n) and
      A4:for k,l being Element of Segm(n) holds
        i2.(k,l)=((k+l) mod n);
          for k,l being Element of Segm(n) holds i1.(k,l)=i2.(k,l)
        proof
         let k,l be Element of Segm(n);
         thus i1.(k,l)=((k+l) mod n) by A3 .=i2.(k,l) by A4;
        end;
         hence thesis by BINOP_1:2;
         end;
end;

theorem Th34:
  for n being natural number st n > 0 holds
  HGrStr(#Segm(n),addint(n)#) is associative Group-like
  proof
   let n be natural number;
   assume A1:n>0;
   set G = HGrStr (# Segm(n), addint(n) #);
   A2: for h,g be Element of G, A,B be Element of Segm(n)
   st h = A & g = B
   holds h * g = (A + B) mod n by A1,Def5;
   thus for h,g,f being Element of G
    holds (h * g) * f = h * (g * f)
    proof let h,g,f be Element of G;
     reconsider A = h, B = g, C = f as Element of Segm(n);
A3:   A is Element of NAT & B is Element of NAT & C is Element of NAT;
     A4: h * g = (A + B) mod n by A2;
     A5: g * f = (B + C) mod n by A2;
     thus (h * g) * f = (((A + B) mod n) + C) mod n by A2,A4
                     .= ((A + B) + C) mod n by A3,NAT_D:22
                     .=(A + (B + C)) mod n
                     .=(A + ((B + C) mod n)) mod n by A3,NAT_D:22
                     .= h * (g * f) by A2,A5;
     end;
    reconsider e=0 as Element of G by A1,Th10;
    take e;
    let h be Element of G;
    reconsider A = h as Element of Segm(n);
reconsider A as Element of NAT;
    A6:A < n by A1,Th10;
  thus h * e = (A + 0) mod n by A2
            .= h by A6,NAT_D:24;
  thus e * h = (0 + A) mod n by A2
              .= h by A6,NAT_D:24;
consider p being Nat such that A7: n=A+p by A6,NAT_1:10;
A8:     p <= n by A7,NAT_1:12;
         (p mod n) < n by A1,NAT_D:1;
     then reconsider g = p mod n as Element of G by Th10;
     take g;
     reconsider B=g as Element of Segm(n);
 thus h * g =e
    proof
         now per cases by A8,REAL_1:def 5;
       suppose A9:p = n;
             then n + 0 =A + n by A7;
             then A10:A = 0;
                 h * g = (A +B) mod n by A2
                  .= (0 * n) mod n by A9,A10,NAT_D:25
                  .= e by NAT_D:13;
                hence thesis;
       end;
       suppose A11:p < n;
                h * g = (A +B) mod n by A2
                 .= n mod n by A7,A11,NAT_D:24
                 .= e by NAT_D:25;
             hence thesis;
           end;
           end;
         hence thesis;
        end;
thus g * h = e
  proof
       now per cases by A8,REAL_1:def 5;
      suppose A12:p = n;
            then n + 0 =A + n by A7;
            then A = 0;
            then g * h = (n mod n + 0) mod n by A2,A12
                  .= (0 * n) mod n by NAT_D:25
                  .= e by NAT_D:13;
                  hence thesis;
      end;
      suppose A13:p < n;
                    g * h = (A + B) mod n by A2
                     .=n mod n by A7,A13,NAT_D:24
                     .= e by NAT_D:25;
                hence thesis;
             end;
             end;
         hence thesis;
      end;
    end;

definition let n be natural number such that A1: n > 0;
 func INT.Group(n) -> strict Group equals
:Def6: HGrStr(#Segm(n),addint(n)#);
coherence by A1,Th34;
end;

theorem Th35:
  1_INT.Group = 0
  proof
  reconsider e = 0 as Element of INT by INT_1:def 2;
  reconsider e as Element of INT.Group;
       for h being Element of INT.Group holds h * e = h & e * h =
h
   proof
    let h be Element of INT.Group;
    reconsider A = h as Integer;
    thus thesis;
    end;
 hence thesis by GROUP_1:10;
 end;

theorem Th36:
  for n be natural number st n>0 holds 1_INT.Group(n) = 0
  proof
   let n be natural number;
   assume A1: n>0;
   then A2: INT.Group(n) = HGrStr(#Segm(n),addint(n)#) by Def6;

   A3: for h,g be Element of INT.Group(n),
              A,B be Element of Segm(n) st h = A & g = B
    holds h * g = (A + B) mod n by A1,A2,Def5;
   reconsider e = 0 as Element of Segm(n) by A1,Th10;
   reconsider e as Element of INT.Group(n) by A2;
       for h being Element of INT.Group(n)
   holds h * e = h & e * h = h
   proof
    let h be Element of INT.Group(n);
    reconsider A = h as Element of Segm(n) by A2;
    reconsider A as Element of NAT;
    A4:A < n by A1,Th10;
    A5: h * e = (A + 0) mod n by A3
             .= h by A4,NAT_D:24;
         e * h = (0 + A) mod n by A3
             .= h by A4,NAT_D:24;
  hence thesis by A5;
 end;
hence thesis by GROUP_1:10;
end;

definition
 let h be Integer;
 canceled;

 func @'h -> Element of INT.Group equals
      h;
 coherence by INT_1:def 2;
end;

theorem Th37:
  for h being Element of INT.Group holds h" = -h
  proof
  let h be Element of INT.Group;
   reconsider g=-h as Element of INT.Group by INT_1:def 2;
       h *g =1_INT.Group by Th35;
   hence thesis by GROUP_1:20;
   end;

 reserve G1 for Subgroup of INT.Group;

theorem Th38:
  (@'1) |^ k = k
  proof
  defpred P[Element of NAT] means (@'1)|^$1=$1;
A1: P[0] by Th35,GROUP_1:43;
 A2: for k st P[k] holds P[k+1]
  proof
    let k; assume (@'1)|^k = k;
    then (@'1)|^k * (@'1) = k + 1;
    hence thesis by GROUP_1:49;
  end;
     for k holds P[k] from NAT_1:sch 1(A1,A2);
   hence thesis;
  end;

theorem Th39:
  j1 = (@'1) |^ j1
  proof
   consider k such that A1:j1 = k or j1 = -k by INT_1:8;
   per cases by A1;
    suppose j1=k;
      hence thesis by Th38;
    end;
    suppose A2: j1=-k;
      reconsider k'=k as Integer;
      reconsider k' as Element of INT.Group by INT_1:def 2;
      (@'1)|^j1 = ((@'1)|^k)" by A2,GROUP_1:71
      .= (k')" by Th38 .= j1 by  Th37,A2;
      hence thesis;
    end;
  end;

       :::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
       ::             Definition of cyclic group                  ::
       :::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

Lm5:
 INT.Group = gr {@'1}
proof
 reconsider h = 1 as Element of INT by INT_1:def 2;
 reconsider h as Element of INT.Group;
     {h} c= the carrier of (Omega).INT.Group &
 (for G1 being strict Subgroup of INT.Group
  st {h} c= the carrier of G1 holds
 (Omega).INT.Group is Subgroup of G1 )
  proof
   A1: {h} c= the carrier of INT.Group;
        for G1 st {h} c= the carrier of G1 holds (Omega).
INT.Group is Subgroup of G1
    proof
      let G1;
      assume A2:{h} c= the carrier of G1;
          the carrier of (Omega).INT.Group c= the carrier of G1
      proof
        let x;
        assume that A3: x in the carrier of (Omega).INT.Group and
        A4: not x in the carrier of G1;
            x in (Omega).INT.Group by A3,RLVECT_1:def 1;
        then x in INT.Group by GROUP_2:def 8;
        then x in INT by RLVECT_1:def 1;
        then reconsider x'=x as Integer by INT_1:def 2;
            h in {h} by TARSKI:def 1;
        then reconsider h''=h as Element of G1 by A2;
        A5: (h'')|^x' in the carrier of G1;
            (h'')|^x'=h|^x' by GROUP_4:4;
        hence contradiction by A4,A5,Th39;
      end;
hence thesis by GROUP_2:66;
end;
hence thesis by A1,GROUP_2:def 8;
end;
then (Omega).INT.Group = gr {h} by GROUP_4:def 5;
hence thesis by GROUP_2:def 8;
end;

definition let IT be Group;
 attr IT is cyclic means
:Def9:  ex a being Element of IT st the HGrStr of IT=gr {a};
end;

registration
 cluster strict cyclic Group;
  existence
   proof INT.Group is cyclic by Def9,Lm5;
    hence thesis;
   end;
end;

theorem Th40:
  (1).G is cyclic
 proof
      1_G in {1_G} by TARSKI:def 1;
  then reconsider PG=1_G as Element of (1).G by GROUP_2:def 7;
  take PG;
      {PG} c= the carrier of (Omega).(1).G &
  ( for G1 being strict Subgroup of (1).G st {PG} c= the carrier of G1 holds
  (Omega).(1).G is Subgroup of G1 )
   proof
A1:    {PG} = the carrier of (1).G by GROUP_2:def 7;
         ( for G1 being Subgroup of (1).G st {PG} c= the carrier of G1 holds
       (Omega).(1).G is Subgroup of G1 )
        proof
         let G1 be Subgroup of (1).G;
         assume A2:{PG} c= the carrier of G1;
         the carrier of (Omega).(1).G =the carrier of (1).G by
GROUP_2:def 8;
         then the carrier of (Omega).
(1).G c=the carrier of G1 by A2,GROUP_2:def 7;
         hence thesis by GROUP_2:66;
       end;
     hence thesis by A1,GROUP_2:def 8;
   end;
  then (Omega).(1).G = gr {PG} by GROUP_4:def 5;
  hence thesis by GROUP_2:def 8;
 end;

registration
  let G be Group;
  cluster (1).G -> cyclic;
  coherence by Th40;
end;

theorem Th41:
  G is cyclic Group iff ex a being Element of G st
  for b being Element of G ex j1 st b=a|^j1
 proof
   thus G is cyclic Group implies ex a being Element of G st
    for b being Element of G ex j1 st b=a|^j1
     proof
      assume G is cyclic Group;
      then consider a being Element of G such that
       A1: the HGrStr of G = gr {a} by Def9;
      take a;
          for b being Element of G ex j1 st b=a|^j1
       proof
        let b be Element of G;
            b in gr {a} by A1,RLVECT_1:def 1;
        hence thesis by Th25;
       end;
     hence thesis;
    end;
  thus (ex a being Element of G st
    for b being Element of G
    ex j1 st b = a|^j1) implies G is cyclic Group
     proof
      given a being Element of G such that
      A2:for b being Element of G
      ex j1 st b = a|^j1;
          for b being Element of G holds b in gr {a}
       proof
        let b be Element of G;
             ex j1 st b=a|^j1 by A2;
        hence thesis by Th25;
       end;
      then the HGrStr of G = gr {a} by GROUP_2:71;
      hence thesis by Def9;
    end;
end;

theorem Th42:
  G is finite implies (G is cyclic Group iff
  ex a being Element of G st
  for b being Element of G ex n st b=a|^n)
 proof
  assume A1: G is finite;
  thus G is cyclic Group implies
  ex a being Element of G st
  for b being Element of G ex n st b=a|^n
     proof
       assume G is cyclic Group;
       then consider a being Element of G such that
       A2: for b being Element of G ex j2 st b=a|^j2
        by Th41;
       take a;
          let b be Element of G;
          consider j2 such that A3: b = a|^j2 by A2;
          consider n such that A4:j2=n or j2=-n by INT_1:8;
          per cases by A4;
            suppose j2=n; hence thesis by A3;
            end;
            suppose A5:j2=-n;
                    ord G >=1 by A1,GROUP_1:90;
                then ord G > 0;
                then n mod ord G <=ord G by NAT_D:1;
     then reconsider q'=ord G -( n mod ord G) as Element of NAT by INT_1:18;
                take q';
                    b=(a|^n)" by A3,A5,GROUP_1:71
                .= a|^q' by A1,Th30;
  hence thesis;
  end;
  end;
      given a being Element of G such that
      A6:  for b being Element of G ex n st b=a|^n;
          for b being Element of G ex j2 st b=a|^j2
        proof
         let b be Element of G;
         consider n such that A7:b=a|^n by A6;
         reconsider n as Integer;
         take n;
         thus thesis by A7;
        end;
   hence thesis by Th41;
 end;

theorem Th43:
  for G being strict Group holds
  G is finite implies
   ( G is cyclic Group iff
      ex a being Element of G st ord a =ord G )
 proof let G be strict Group;
  assume A1: G is finite;
    thus G is cyclic Group implies
     ex a being Element of G st ord a =ord G
      proof
       assume G is cyclic Group;
       then consider a being Element of G such that
       A2: G = gr {a} by Def9;
       take a;
       thus thesis by A1,A2,Th27;
      end;
       given a being Element of G such that A3:ord a =ord G;
            ex a being Element of G st G = gr {a}
        proof
         consider a being Element of G such that
         A4: ord a = ord G by A3;
         take a;
             ord gr {a} = ord G by A1,A4,Th27;
         hence thesis by A1,GROUP_2:85;
        end;
      hence thesis by Def9;
 end;

theorem
  for H being strict Subgroup of G holds
  G is finite & G is cyclic Group implies
   H is cyclic Group
 proof let H be strict Subgroup of G;
  assume that A1: G is finite and A2:G is cyclic Group;
  consider a such that
  A3: for b being Element of G ex n st b = a|^n by A1,A2,Th42;
  A4: H is finite by A1,GROUP_2:48;
  then A5: ord H >= 1 by GROUP_1:90;
  per cases by A5,REAL_1:def 5;
     suppose ord H =1;
          then H = (1).G by A4,GROUP_2:82;
          hence thesis;
     end;
     suppose ord H > 1;
          then consider h being Element of H such that
          A6:h <>1_H by Th31;
          defpred P[Element of NAT] means
            $1 > 0 & a|^$1 is Element of H;
            h is Element of G by GROUP_2:51;
          then consider n such that A7: h = a|^n by A3;
            n <> 0
           proof
            assume n = 0;
            then h = 1_G by A7,GROUP_1:59 .= 1_H by GROUP_2:53;
            hence contradiction by A6;
           end;
            then n > 0;
           then A8:  ex n st P[n] by A7;
         ex h1 being Element of H st
       for b being Element of H ex s st b = h1|^s
      proof
         ex k st P[k] & for n st P[n] holds k <= n from NAT_1:sch 5(A8);
       then consider n such that
       A9: (n>0 & a|^n is Element of H) and
       A10: for k st
       (k>0 & a|^k is Element of H) holds n <= k;
       consider h1 being Element of H such that
       A11: h1 = a|^n by A9;
       take h1;
            for b being Element of H ex s st b = h1|^s
       proof
         let b be Element of H;
             b is Element of G by GROUP_2:51;
         then consider p such that A12: b = a|^p by A3;
         consider s,r being Nat such that A13: p = (n*s) + r and
         A14:r < n by A9,NAT_1:17;
         reconsider s,r as Element of NAT by ORDINAL1:def 13;
         A15:  r =0
          proof
           assume r<>0;
           then A16: r >0;
A17:      a|^p = a|^(n*s) * (a|^r) by A13,GROUP_1:48;
                h1|^s=a|^n|^s by A11,GROUP_4:3;
     then (h1|^s)"=(a|^n|^s)" by GROUP_2:57 .= (a|^(n * s ))" by GROUP_1:50;
          then reconsider g= (a|^(n*s))" as Element of H;
          reconsider b=a|^p as Element of H by A12;
               (a|^(n*s))" * (a|^p)
          = ((a|^(n*s))" *(a|^(n*s)))*(a|^r) by A17,GROUP_1:def 4
          .= 1_G *(a|^r) by GROUP_1:def 6 .= a|^ r by GROUP_1:def 5;
          then g*b = a|^r by GROUP_2:52;
          hence contradiction by A10,A14,A16;
        end;
     take s;
         a|^p = a|^n|^s by A13,A15,GROUP_1:50 .= h1|^s by A11,GROUP_4:3;
     hence thesis by A12;
   end;
 hence thesis;
end;
hence thesis by A4,Th42;
end;
end;

theorem
  for G being strict Group holds
  G is finite & ord (G) = p & p is prime implies
   G is cyclic Group
 proof let G be strict Group;
  assume that A1: G is finite and A2: ord (G) = p and
              A3: p is prime;
      ex a being Element of G st ord a = p
  proof
   assume A4: for a being Element of G holds ord a <> p;
   A5: now let a be Element of G;
        ord a divides p by A1,A2,Th28;
   then ord a = 1 or ord a = p by A3,INT_2:def 5;
   hence ord a = 1 by A4;
end;
A6:the carrier of G c= the carrier of (1).G
 proof
       for x being set holds x in the carrier of G implies
      x in the carrier of (1).G
      proof
       let x be set;
       assume x in the carrier of G;
       then reconsider x'=x as Element of G;
           ord x' = 1 by A5;
       then x' = 1_G by GROUP_1:85;
       then x' in {1_G} by TARSKI:def 1;
      hence thesis by GROUP_2:def 7;
     end;
 hence thesis by TARSKI:def 3;
end;
     the carrier of (1).G c= the carrier of G by GROUP_2:def 5;
 then the carrier of G = the carrier of (1).G by A6,XBOOLE_0:def 10;
 then G = (1).G by GROUP_2:70;
 then ord G = 1 by GROUP_2:81;
 hence contradiction by A2,A3,INT_2:def 5;
end;
hence thesis by A1,A2,Th43;
end;

theorem Th46:
  for n st n>0 ex g being Element of INT.Group(n) st
  for b being Element of INT.Group(n) ex j1 st b = g|^j1
  proof
   let n;
   assume A1:n>0;
   then A2: INT.Group(n) = HGrStr(#Segm(n),addint(n)#) by Def6;

A3: for h,g be Element of INT.Group(n),
            A,B be Element of Segm(n) st h = A & g = B
      holds h * g = (A + B) mod n by A1,A2,Def5;
           0 +1 < n+1 by A1,XREAL_1:8;
       then A4:n >=1 by NAT_1:13;
          now per cases by A4,REAL_1:def 5;
      suppose n=1;
   then the carrier of INT.Group(n) = {1_INT.Group(n)} by A2,Th13,Th36
      .= the carrier of (1).INT.Group(n) by GROUP_2:def 7;
      then INT.Group(n) = (1).INT.Group(n) by GROUP_2:70;
      then INT.Group(n) is cyclic Group;
      hence thesis by Th41;
end;
suppose n>1;
     then reconsider g'= 1 as Element of Segm(n) by Th10;
     reconsider g=g' as Element of INT.Group(n) by A2;
         for b being Element of INT.Group(n) ex j1 st b = g|^j1
      proof
       let b be Element of INT.Group(n);
       reconsider b'=b as Element of NAT by A1,A2,Lm1;
       defpred P[Element of NAT] means g|^$1 = $1 mod n;
         g|^0 = 1_INT.Group(n) by GROUP_1:43 .= 0 by A1,Th36
             .= 0 mod n by NAT_D:26;
       then A5:P[0];
       A6: for k st P[k] holds P[k+1]
       proof
        let k;
          k mod n < n by A1,NAT_D:1;
        then reconsider e=(k mod n) as Element of Segm(n) by Th10;
        assume A7: g|^k = k mod n;
            g|^(k+1) =g|^k*(g|^1) by GROUP_1:48 .=g|^k*g by GROUP_1:44
               .= (e +g') mod n by A3,A7 .=(k+1) mod n by NAT_D:22;
         hence thesis;
         end;
  A8:  for k holds P[k] from NAT_1:sch 1(A5,A6);
  A9:  b'<n by A1,A2,Th10;
A10:   g|^b'=b' mod n by A8 .= b by A9,NAT_D:24;
        reconsider b' as Integer;
        take b';
        thus thesis by A10;
    end;
    hence thesis;
  end;
  end;
  hence thesis;
end;

registration
 cluster cyclic -> commutative Group;
 coherence
proof let G;
 assume A1:G is cyclic;
     for a,b being Element of G holds a * b = b * a
 proof
  let a,b be Element of G;
      ex e being Element of G st
   (ex j2 st a=e|^j2 & ex j3 st b=e|^j3)
  proof
   consider e being Element of G such that
   A2:for d being Element of G ex j3 st d=e|^j3 by A1,Th41;
   take e;
   A3:ex j2 st a=e|^j2 by A2;
       ex j3 st b=e|^j3 by A2;
   hence thesis by A3;
  end;
  then consider e being Element of G,j2,j3 such that
  A4: a = e|^j2 & b = e|^j3;
      a * b = e |^(j3 + j2) by A4,GROUP_1:63 .= b * a by A4,GROUP_1:63;
  hence thesis;
 end;
 hence thesis by GROUP_1:def 16;
end;
end;

theorem
  INT.Group = gr {@'1} by Lm5;

theorem Th48:
  INT.Group is cyclic
proof
  take @'1;
  thus thesis by Lm5;
end;

registration
  cluster INT.Group -> cyclic;
  coherence by Th48;
end;

theorem
  n > 0 implies INT.Group(n) is cyclic
proof
 assume n > 0;
 then ex g being Element of INT.Group(n) st
 for b being Element of INT.Group(n) ex j1 st b = g|^j1 by Th46;
 hence thesis by Th41;
end;

theorem
  INT.Group is commutative;

theorem
  0 < m & m <= n implies Segm m c= Segm n
proof assume
A1: 0 < m & m <= n;
 let x;
 assume
A2: x in Segm m;
  then reconsider x as Element of NAT;
      x < m by A1,A2,Th10;
 then x < n by A1,XXREAL_0:2;
 hence thesis by Th10;
end;

