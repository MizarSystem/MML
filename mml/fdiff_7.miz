:: Several Differentiation Formulas of Special Functions -- Part {III}
::  by Bo Li , Yan Zhang and Xiquan Liang
::
:: Received March 22, 2006
:: Copyright (c) 2006 Association of Mizar Users

environ

 vocabularies FUNCT_1, RELAT_1, SIN_COS6, PRE_TOPC, BOOLE, ARYTM_1, ARYTM_3,
      RCOMP_1, SEQ_1, PARTFUN1, SIN_COS, FDIFF_1, LIMFUNC1, PREPOWER, POWER,
      SQUARE_1, TAYLOR_1, FINSEQ_1, GROUP_1, NAT_1;
 notations TARSKI, SUBSET_1, XXREAL_0, RELSET_1, PARTFUN1, PARTFUN2, RCOMP_1,
      TAYLOR_1, SIN_COS, RFUNCT_1, SQUARE_1, NUMBERS, REAL_1, SEQ_1, FDIFF_1,
      PREPOWER, POWER, SIN_COS6, LIMFUNC1;
 constructors PARTFUN1, REAL_1, SQUARE_1, RCOMP_1, PARTFUN2, RFUNCT_1, FDIFF_1,
      LIMFUNC1, PREPOWER, POWER, SIN_COS, TAYLOR_1, SIN_COS6, NAT_1, FCONT_1,
      SERIES_1, NAT_D;
 registrations RELSET_1, MEMBERED, RCOMP_1, INT_1, SEQ_1, NAT_1, ORDINAL1;
 requirements SUBSET, NUMERALS, ARITHM;
 definitions SQUARE_1, RCOMP_1, LIMFUNC1;
 theorems FUNCT_1, SIN_COS6, FDIFF_2, TAYLOR_1, XBOOLE_1, XCMPLX_1, SEQ_1,
      FDIFF_4, POWER, SQUARE_1, FDIFF_1, TARSKI, PREPOWER, RFUNCT_1, XREAL_1,
      SIN_COS, SIN_COS5, RCOMP_1, NEWTON;

begin

 reserve y for set,
         x,r,a,b for Real,
         n for Element of NAT,
         Z for open Subset of REAL,
         f,f1,f2,f3 for PartFunc of REAL,REAL;

theorem Th1: x #Z 2=x^2
 proof
   x #Z 2 = x |^ 2 by PREPOWER:46 .= x^2 by NEWTON:100;
   hence thesis;
 end;

theorem Th2:x>0 implies x #R (1/2)=sqrt x
  proof
   assume A1:x>0;then
A2:x #R (1/2)>0 by PREPOWER:95;
   x = x #R ((1/2)*2) by PREPOWER:86,A1
    .=x #R (1/2) #R 2 by PREPOWER:105,A1
    .=(x #R (1/2)) #Q 2 by A2,PREPOWER:88
    .=(x #R (1/2)) to_power 2 by A2,POWER:51
    .=(x #R (1/2))^2 by POWER:53;
   hence x #R (1/2)=sqrt x by A2,SQUARE_1:def 4,A1;
  end;

theorem Th3:x>0 implies x #R (-1/2)=1/sqrt x
 proof
   assume A1:x>0;
   hence x #R (-1/2)=1 / x #R (1/2) by PREPOWER:90
    .=1 / sqrt x by Th2,A1;
 end;

Lm1:2*(cos.(x/2))^2=1+cos.x
 proof
A1:(cos(x/2))^2=(1+cos(2*(x/2)))/2 by SIN_COS5:21
     .=(1+cos(x))/2;
   2*(cos.(x/2))^2 = 2*((1+cos(x))/2) by A1,SIN_COS:def 23
    .=1+cos.x by SIN_COS:def 23;
   hence thesis;
 end;

Lm2:2*(sin.(x/2))^2=1-cos.x
 proof
A1:(sin(x/2))^2=(1-cos(2*(x/2)))/2 by SIN_COS5:20
     .=(1-cos(x))/2;
   2*(sin.(x/2))^2=2*((1-cos(x))/2) by A1,SIN_COS:def 21
     .=1-cos.x by SIN_COS:def 23;
   hence thesis;
 end;

::f.x=r*arcsinx
theorem
 Z c=]. -1,1 .[ & Z c= dom (r(#)arcsin) implies
 r(#)arcsin is_differentiable_on Z &
 for x st x in Z holds ((r(#)arcsin)`|Z).x = r / sqrt(1-x^2)
 proof
   assume that A1:Z c= ]. -1,1 .[ and A2:Z c= dom (r(#)arcsin);
A3:arcsin is_differentiable_on Z by A1,SIN_COS6:84,FDIFF_1:34;
   for x st x in Z holds ((r(#)arcsin)`|Z).x = r / sqrt(1-x^2)
   proof
     let x;
     assume A4:x in Z; then
A5:  -1 < x & x < 1 by RCOMP_1:47,A1;
     ((r(#)arcsin)`|Z).x = r*diff(arcsin,x) by A3,A2,FDIFF_1:28,A4
       .= r*(1 / sqrt(1-x^2)) by SIN_COS6:84,A5
       .=r / sqrt(1-x^2) by XCMPLX_1:100;
     hence thesis;
   end;
   hence thesis by A3,A2,FDIFF_1:28;
 end;

::f.x=r*arccosx
theorem
 Z c=]. -1,1 .[ & Z c= dom (r(#)arccos) implies
 r(#)arccos is_differentiable_on Z &
 for x st x in Z holds ((r(#)arccos)`|Z).x =- r / sqrt(1-x^2)
 proof
   assume that A1:Z c= ]. -1,1 .[ and A2:Z c= dom (r(#)arccos);
A3:arccos is_differentiable_on Z by A1,SIN_COS6:108,FDIFF_1:34;
   for x st x in Z holds ((r(#)arccos)`|Z).x = -r / sqrt(1-x^2)
   proof
     let x;
     assume A4:x in Z;then
A5:  -1 < x & x < 1 by A1,RCOMP_1:47;
     ((r(#)arccos)`|Z).x = r*diff(arccos,x) by A3,A2,FDIFF_1:28,A4
       .= r*(-1 / sqrt(1-x^2)) by SIN_COS6:108,A5
       .=-r*(1 / sqrt(1-x^2))
       .=-r / sqrt(1-x^2) by XCMPLX_1:100;
     hence thesis;
   end;
   hence thesis by A3,A2,FDIFF_1:28;
 end;

theorem Th6:
 f is_differentiable_in x & f.x > -1 & f.x < 1 implies
 (arcsin)*f is_differentiable_in x &
 diff((arcsin)*f,x) = diff(f,x)/sqrt(1-(f.x)^2)
 proof
   assume that A1:f is_differentiable_in x and A2:f.x > -1 & f.x < 1;
   f.x in ]. -1,1 .[ by A2; then
A3:arcsin is_differentiable_in f.x by SIN_COS6:84,FDIFF_1:16;
   diff((arcsin)*f,x) = diff((arcsin),f.x)*diff(f,x) by A1,A3,FDIFF_2:13
   .=diff(f,x)*(1 / sqrt(1-(f.x)^2)) by SIN_COS6:84,A2
  .=diff(f,x)/sqrt(1-(f.x)^2) by XCMPLX_1:100;
  hence thesis by A1,A3,FDIFF_2:13;
 end;

theorem Th7:
 f is_differentiable_in x & f.x > -1 & f.x < 1 implies
 (arccos)*f is_differentiable_in x &
 diff((arccos)*f,x) = -diff(f,x)/sqrt(1-(f.x)^2)
 proof
   assume that A1:f is_differentiable_in x and A2:f.x > -1 & f.x < 1;
   f.x in ]. -1,1 .[ by A2;
   then A3:arccos is_differentiable_in f.x by SIN_COS6:108,FDIFF_1:16;
   diff(arccos*f,x) = diff(arccos,f.x)*diff(f,x) by A1,A3,FDIFF_2:13
   .=(-1 / sqrt(1-(f.x)^2))*diff(f,x) by SIN_COS6:108,A2
   .=-diff(f,x)*(1 / sqrt(1-(f.x)^2))
   .=-diff(f,x)/sqrt(1-(f.x)^2) by XCMPLX_1:100;
   hence thesis by A1,A3,FDIFF_2:13;
 end;

::f.x=ln(arcsin.x)
theorem
 Z c= dom (ln*(arcsin)) & Z c= ]. -1,1 .[ &
 (for x st x in Z holds arcsin.x>0) implies
 ln*(arcsin) is_differentiable_on Z & for x st x in Z holds
 ((ln*(arcsin))`|Z).x=1 / (sqrt(1-x^2)*arcsin.x)
 proof
   assume that A1:Z c= dom (ln*arcsin) and
A2:Z c= ]. -1,1 .[ and A3:for x st x in Z holds arcsin.x > 0;
A4:for x st x in Z holds ln*arcsin is_differentiable_in x
  proof
   let x;
   assume A5:x in Z;then
A6: arcsin is_differentiable_in x by FDIFF_1:16,A2,SIN_COS6:84;
   arcsin.x >0 by A3,A5;
   hence ln*arcsin is_differentiable_in x by TAYLOR_1:20,A6;
  end; then
A7:ln*arcsin is_differentiable_on Z by A1,FDIFF_1:16;
  for x st x in Z holds
    ((ln*(arcsin))`|Z).x = 1 / (sqrt(1-x^2)*arcsin.x)
  proof
   let x;
   assume A8:x in Z;
   then A9:-1 < x & x < 1 by A2,RCOMP_1:47;
A10:arcsin is_differentiable_in x by FDIFF_1:16,A2,SIN_COS6:84,A8;
A11:arcsin.x >0 by A3,A8;
   diff(ln*arcsin,x)
  =diff(arcsin,x)/arcsin.x by TAYLOR_1:20,A11,A10
  .=(1 / sqrt(1-x^2))/arcsin.x by SIN_COS6:84,A9
  .=1 / (sqrt(1-x^2)*arcsin.x) by XCMPLX_1:79;
   hence thesis by A8,A7,FDIFF_1:def 8;
  end;
  hence thesis by A4,A1,FDIFF_1:16;
 end;

::f.x=ln(arccos.x)
theorem
 Z c= dom (ln*arccos) & Z c= ]. -1,1 .[ &
 (for x st x in Z holds arccos.x>0) implies
 ln*(arccos) is_differentiable_on Z & for x st x in Z holds
 ((ln*(arccos))`|Z).x=-1 / (sqrt(1-x^2)*arccos.x)
 proof
   assume that A1:Z c= dom (ln*(arccos)) and
A2:Z c= ]. -1,1 .[ and A3:(for x st x in Z holds arccos.x > 0);
A4:for x st x in Z holds ln*(arccos) is_differentiable_in x
  proof
   let x;
   assume A5:x in Z; then
A6:arccos is_differentiable_in x by FDIFF_1:16,A2,SIN_COS6:108;
   arccos.x >0 by A3,A5;
   hence ln*arccos is_differentiable_in x by TAYLOR_1:20,A6;
  end;
  then A7:ln*arccos is_differentiable_on Z by A1,FDIFF_1:16;
  for x st x in Z holds
    ((ln*arccos)`|Z).x = -1 / (sqrt(1-x^2)*arccos.x)
  proof
   let x;
   assume A8:x in Z;then
A9:-1 < x & x < 1 by A2,RCOMP_1:47;
A10:arccos is_differentiable_in x by FDIFF_1:16,A2,SIN_COS6:108,A8;
A11:arccos.x >0 by A3,A8;
   diff(ln*arccos,x)
  =diff(arccos,x)/arccos.x by TAYLOR_1:20,A11,A10
  .=(-1 / sqrt(1-x^2))/arccos.x by SIN_COS6:108,A9
  .=-(1 / sqrt(1-x^2))/arccos.x by XCMPLX_1:188
  .=-1 / (sqrt(1-x^2)*arccos.x) by XCMPLX_1:79;
   hence thesis by A8,A7,FDIFF_1:def 8;
  end;
  hence thesis by A4,A1,FDIFF_1:16;
 end;

::f.x=(arcsin)|^n
theorem Th10:
 Z c= dom (( #Z n)*(arcsin)) & Z c=]. -1,1 .[ implies
 ( #Z n)*(arcsin) is_differentiable_on Z & for x st x in Z holds
 ((( #Z n)*arcsin)`|Z).x =n*(arcsin.x) #Z (n-1) / sqrt(1-x^2)
 proof
   assume that A1:Z c= dom (( #Z n)*arcsin) and A2: Z c=]. -1,1 .[;
A3:for x st x in Z holds ( #Z n)*arcsin is_differentiable_in x
   proof
    let x;
    assume x in Z;
    then arcsin is_differentiable_in x by FDIFF_1:16,A2,SIN_COS6:84;
    hence thesis by TAYLOR_1:3;
   end;
  then A4:( #Z n)*(arcsin) is_differentiable_on Z by A1,FDIFF_1:16;
  for x st x in Z holds
    ((( #Z n)*(arcsin))`|Z).x = n*(arcsin.x) #Z (n-1) / sqrt(1-x^2)
  proof
    let x;
    assume A5:x in Z;then
A6:-1 < x & x < 1 by A2,RCOMP_1:47;
A7:arcsin is_differentiable_in x by FDIFF_1:16,A2,SIN_COS6:84,A5;
   ((( #Z n)*arcsin)`|Z).x = diff(( #Z n)*arcsin,x) by A5,FDIFF_1:def 8,A4
  .=(n*((arcsin.x) #Z (n-1))) * diff(arcsin,x) by TAYLOR_1:3,A7
  .=(n*((arcsin.x) #Z (n-1))) *(1 / sqrt(1-x^2)) by SIN_COS6:84,A6
  .=n*(arcsin.x) #Z (n-1) / sqrt(1-x^2) by XCMPLX_1:100;
   hence thesis;
  end;
  hence thesis by A3,A1,FDIFF_1:16;
 end;

::f.x=(arccos)|^n
theorem Th11:
 Z c= dom (( #Z n)*(arccos)) & Z c=]. -1,1 .[ implies
 ( #Z n)*(arccos) is_differentiable_on Z & for x st x in Z holds
 ((( #Z n)*(arccos))`|Z).x =-n*(arccos.x) #Z (n-1) / sqrt(1-x^2)
 proof
   assume that A1:Z c= dom (( #Z n)*arccos) and A2: Z c=]. -1,1 .[;
A3:for x st x in Z holds ( #Z n)*arccos is_differentiable_in x
 proof
  let x;
  assume A4:x in Z;
  arccos is_differentiable_in x by FDIFF_1:16,A2,SIN_COS6:108,A4;
  hence thesis by TAYLOR_1:3;
 end;then
A5:( #Z n)*arccos is_differentiable_on Z by A1,FDIFF_1:16;
  for x st x in Z holds
    ((( #Z n)*arccos)`|Z).x =-n*(arccos.x) #Z (n-1) / sqrt(1-x^2)
  proof
   let x;
   assume A6:x in Z;
   then A7:-1 < x & x < 1 by A2,RCOMP_1:47;
A8:arccos is_differentiable_in x by FDIFF_1:16,A2,SIN_COS6:108,A6;
   ((( #Z n)*(arccos))`|Z).x
   =diff(( #Z n)*(arccos),x) by A6,FDIFF_1:def 8,A5
  .=(n*((arccos.x) #Z (n-1))) * diff(arccos,x) by TAYLOR_1:3,A8
  .=(n*((arccos.x) #Z (n-1))) *(-(1 / sqrt(1-x^2))) by SIN_COS6:108,A7
  .=-(n*(arccos.x) #Z (n-1)) *(1/ sqrt(1-x^2))
  .=-n*(arccos.x) #Z (n-1) / sqrt(1-x^2) by XCMPLX_1:100;
   hence thesis;
  end;
  hence thesis by A3,A1,FDIFF_1:16;
 end;

::f.x=(1/2)*(arcsin)|^2
theorem
 Z c= dom ((1/2)(#)(( #Z 2)*(arcsin))) & Z c=]. -1,1 .[ implies
 (1/2)(#)(( #Z 2)*(arcsin)) is_differentiable_on Z & for x st x in Z holds
 (((1/2)(#)(( #Z 2)*(arcsin)))`|Z).x =arcsin.x / sqrt(1-x^2)
 proof
 assume that A1:Z c= dom ((1/2)(#)(( #Z 2)*(arcsin))) and A2: Z c=]. -1,1 .[;
A3: Z c= dom (( #Z 2)*(arcsin)) by SEQ_1:def 6,A1;then
A4:( #Z 2)*(arcsin) is_differentiable_on Z & for x st x in Z holds
  ((( #Z 2)*(arcsin))`|Z).x =2*(arcsin.x) #Z (2-1) / sqrt(1-x^2) by A2,Th10;
  for x st x in Z holds
     (((1/2)(#)(( #Z 2)*(arcsin)))`|Z).x =arcsin.x / sqrt(1-x^2)
  proof
   let x;
   assume A5:x in Z;
  (((1/2)(#)(( #Z 2)*(arcsin)))`|Z).x
  =(1/2)*diff((( #Z 2)*arcsin),x) by A1,FDIFF_1:28,A4,A5
 .=(1/2)*((( #Z 2)*(arcsin))`|Z).x by A5,FDIFF_1:def 8,A4
 .=(1/2)*((2*(arcsin.x) #Z (2-1)) / sqrt(1-x^2)) by A3,A2,Th10,A5
 .=((1/2)*(2*(arcsin.x) #Z (2-1))) / sqrt(1-x^2) by XCMPLX_1:75
 .=arcsin.x / sqrt(1-x^2) by PREPOWER:45;
   hence thesis;
  end;
  hence thesis by A1,FDIFF_1:28,A4;
 end;

::f.x=(1/2)*(arccos)|^2
theorem
 Z c= dom ((1/2)(#)(( #Z 2)*(arccos))) & Z c=]. -1,1 .[ implies
 (1/2)(#)(( #Z 2)*(arccos)) is_differentiable_on Z & for x st x in Z holds
 (((1/2)(#)(( #Z 2)*(arccos)))`|Z).x =-arccos.x / sqrt(1-x^2)
 proof
  assume that A1:Z c= dom ((1/2)(#)(( #Z 2)*(arccos))) and A2: Z c=]. -1,1 .[;
A3: Z c= dom (( #Z 2)*(arccos)) by SEQ_1:def 6,A1;then
A4:( #Z 2)*(arccos) is_differentiable_on Z & for x st x in Z holds
  ((( #Z 2)*(arccos))`|Z).x =-2*(arccos.x) #Z (2-1) / sqrt(1-x^2) by A2,Th11;
   for x st x in Z holds
      (((1/2)(#)(( #Z 2)*(arccos)))`|Z).x =-arccos.x / sqrt(1-x^2)
   proof
     let x;
     assume A5:x in Z;
     (((1/2)(#)(( #Z 2)*(arccos)))`|Z).x
   =(1/2)*diff((( #Z 2)*arccos),x) by A1,FDIFF_1:28,A4,A5
   .=(1/2)*((( #Z 2)*(arccos))`|Z).x by A5,FDIFF_1:def 8,A4
   .=(1/2)*(-2*(arccos.x) #Z (2-1) / sqrt(1-x^2)) by A3,A2,Th11,A5
   .=-(1/2)*((2*(arccos.x) #Z (2-1)) / sqrt(1-x^2))
   .=-((1/2)*(2*(arccos.x) #Z (2-1))) / sqrt(1-x^2) by XCMPLX_1:75
   .=-arccos.x / sqrt(1-x^2) by PREPOWER:45;
    hence thesis;
   end;
   hence thesis by A1,FDIFF_1:28,A4;
 end;

::f.x=arcsin.(a*x+b)
theorem Th14:
 Z c= dom ((arcsin)*f) &
 (for x st x in Z holds f.x=a*x+b & f.x > -1 & f.x < 1)
 implies (arcsin)*f is_differentiable_on Z &
 for x st x in Z holds (((arcsin)*f)`|Z).x=a / sqrt(1-(a*x+b)^2)
proof
   assume that A1:Z c= dom ((arcsin)*f) and
A2:for x st x in Z holds f.x=a*x+b & f.x > -1 & f.x < 1;
A3:for x st x in Z holds f.x=a*x+b by A2;
   for y st y in Z holds y in dom f by FUNCT_1:21,A1;then
A4:Z c= dom f by TARSKI:def 3;then
A5:f is_differentiable_on Z & for x st x in Z holds (f`|Z).x = a
    by A3,FDIFF_1:31;
A6:for x st x in Z holds (arcsin)*f is_differentiable_in x
   proof
    let x;
    assume A7:x in Z;then
A8: f is_differentiable_in x by FDIFF_1:16,A5;
    f.x > -1 & f.x < 1 by A2,A7;
    hence (arcsin)*f is_differentiable_in x by A8,Th6;
   end;
   then A9:(arcsin)*f is_differentiable_on Z by FDIFF_1:16,A1;
   for x st x in Z holds (((arcsin)*f)`|Z).x=a / sqrt(1-(a*x+b)^2)
   proof
    let x;
    assume A10:x in Z;then
A11:f is_differentiable_in x by FDIFF_1:16,A5;
    f.x > -1 & f.x < 1 by A2,A10;
    then diff((arcsin)*f,x) = diff(f,x)/sqrt(1-(f.x)^2) by A11,Th6
    .=(f`|Z).x/sqrt(1-(f.x)^2) by FDIFF_1:def 8,A10,A5
    .=a/sqrt(1-(f.x)^2) by A10,A4,A3,FDIFF_1:31
    .=a / sqrt(1-(a*x+b)^2) by A2,A10;
    hence thesis by FDIFF_1:def 8,A10,A9;
   end;
   hence thesis by A6,FDIFF_1:16,A1;
 end;

::f.x=arccos.(a*x+b)
theorem Th15:
 Z c= dom ((arccos)*f) &
 (for x st x in Z holds f.x=a*x+b & f.x > -1 & f.x < 1)
 implies (arccos)*f is_differentiable_on Z &
 for x st x in Z holds (((arccos)*f)`|Z).x=-a / sqrt(1-(a*x+b)^2)
proof
   assume that A1:Z c= dom ((arccos)*f) and
A2: for x st x in Z holds f.x=a*x+b & f.x > -1 & f.x < 1;
A3:for x st x in Z holds f.x=a*x+b by A2;
   for y st y in Z holds y in dom f by FUNCT_1:21,A1;then
A4:Z c= dom f by TARSKI:def 3;then
A5:f is_differentiable_on Z & for x st x in Z holds (f`|Z).x = a
     by A3,FDIFF_1:31;
A6:for x st x in Z holds (arccos)*f is_differentiable_in x
   proof
     let x;
     assume A7:x in Z;
     then A8:f is_differentiable_in x by FDIFF_1:16,A5;
     f.x > -1 & f.x < 1 by A2,A7;
     hence (arccos)*f is_differentiable_in x by A8,Th7;
   end;then
A9:(arccos)*f is_differentiable_on Z by FDIFF_1:16,A1;
   for x st x in Z holds (((arccos)*f)`|Z).x=-a / sqrt(1-(a*x+b)^2)
   proof
    let x;
    assume A10:x in Z;
    then A11:f is_differentiable_in x by FDIFF_1:16,A5;
    f.x > -1 & f.x < 1 by A2,A10;
    then diff((arccos)*f,x) = -diff(f,x)/sqrt(1-(f.x)^2) by A11,Th7
    .=-(f`|Z).x/sqrt(1-(f.x)^2) by FDIFF_1:def 8,A10,A5
    .=-a/sqrt(1-(f.x)^2) by A10,A4,A3,FDIFF_1:31
    .=-a / sqrt(1-(a*x+b)^2) by A2,A10;
    hence thesis by FDIFF_1:def 8,A10,A9;
   end;
   hence thesis by A6,FDIFF_1:16,A1;
 end;

::f.x=x*arcsin.x
theorem Th16:
 Z c= dom ((id Z)(#)(arcsin)) & Z c= ]. -1,1 .[ implies
 (id Z)(#)(arcsin) is_differentiable_on Z &
 for x st x in Z holds (((id Z)(#)(arcsin))`|Z).x =arcsin.x+x/sqrt(1-x^2)
proof
   assume that A1:Z c= dom ((id Z)(#)(arcsin)) and A2:Z c= ]. -1,1 .[;
   Z c= dom (id Z) /\ dom (arcsin) by SEQ_1:def 5,A1;then
A3:Z c= dom (id Z) by XBOOLE_1:18;
A4:for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;then
A5:id Z is_differentiable_on Z & for x st x in Z
    holds ((id Z)`|Z).x = 1 by FDIFF_1:31,A3;
A6:arcsin is_differentiable_on Z by A2,SIN_COS6:84,FDIFF_1:34;
for x st x in Z holds (((id Z)(#)(arcsin))`|Z).x =arcsin.x+x/sqrt(1-x^2)
  proof
    let x;
    assume A7:x in Z;
    then A8:-1 < x & x < 1 by A2,RCOMP_1:47;
    (((id Z)(#)(arcsin))`|Z).x
   =(arcsin.x)*diff((id Z),x) + ((id Z).x)*diff(arcsin,x)
     by A5,A1,FDIFF_1:29,A6,A7
   .=(arcsin.x)*((id Z)`|Z).x+ ((id Z).x)*diff(arcsin,x) by A5,A7,FDIFF_1:def 8
   .=(arcsin.x)*1+ ((id Z).x)*diff(arcsin,x) by A7,FDIFF_1:31,A3,A4
   .=(arcsin.x)*1+ ((id Z).x)*(1 / sqrt(1-x^2)) by SIN_COS6:84,A8
   .=arcsin.x+ x*(1 / sqrt(1-x^2)) by FUNCT_1:35,A7
   .=arcsin.x+x / sqrt(1-x^2) by XCMPLX_1:100;
    hence thesis;
  end;
  hence thesis by A6,A5,A1,FDIFF_1:29;
 end;

::f.x=x*arccos.x
theorem Th17:
 Z c= dom ((id Z)(#)(arccos)) & Z c= ]. -1,1 .[ implies
 (id Z)(#)(arccos) is_differentiable_on Z &
 for x st x in Z holds (((id Z)(#)(arccos))`|Z).x =arccos.x-x/sqrt(1-x^2)
 proof
   assume that A1:Z c= dom ((id Z)(#)(arccos)) and A2:Z c= ]. -1,1 .[;
   Z c= dom (id Z) /\ dom (arccos) by SEQ_1:def 5,A1;then
A3:Z c= dom (id Z) by XBOOLE_1:18;
A4:for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;then
A5:id Z is_differentiable_on Z & for x st x in Z
    holds ((id Z)`|Z).x = 1 by FDIFF_1:31,A3;
A6:arccos is_differentiable_on Z by A2,SIN_COS6:108,FDIFF_1:34;
for x st x in Z holds (((id Z)(#)(arccos))`|Z).x =arccos.x-x/sqrt(1-x^2)
   proof
     let x;
      assume A7:x in Z;
      then A8:-1 < x & x < 1 by A2,RCOMP_1:47;
      (((id Z)(#)(arccos))`|Z).x
     =(arccos.x)*diff((id Z),x) + ((id Z).x)*diff(arccos,x)
        by A5,A1,FDIFF_1:29,A6,A7
     .=(arccos.x)*((id Z)`|Z).x+ ((id Z).x)*diff(arccos,x)
        by A5,A7,FDIFF_1:def 8
     .=(arccos.x)*1+ ((id Z).x)*diff(arccos,x) by A7,FDIFF_1:31,A3,A4
     .=(arccos.x)*1+ ((id Z).x)*(-1 / sqrt(1-x^2)) by SIN_COS6:108,A8
     .=arccos.x+ x*(-1 / sqrt(1-x^2)) by FUNCT_1:35,A7
     .=arccos.x-x*(1 / sqrt(1-x^2))
     .=arccos.x-x / sqrt(1-x^2) by XCMPLX_1:100;
      hence thesis;
   end;
   hence thesis by A6,A5,A1,FDIFF_1:29;
 end;

::f.x=(a*x+b)*arcsin.x
theorem
 Z c= dom (f(#)(arcsin)) & Z c= ]. -1,1 .[ &
 (for x st x in Z holds f.x=a*x+b) implies
 f(#)(arcsin) is_differentiable_on Z & for x st x in Z holds
 ((f(#)(arcsin))`|Z).x =a*arcsin.x+(a*x+b)/sqrt(1-x^2)
 proof
   assume that A1:Z c= dom (f(#)(arcsin)) and A2:Z c= ]. -1,1 .[ and
A3:(for x st x in Z holds f.x=a*x+b);
   Z c= dom f /\ dom arcsin by SEQ_1:def 5,A1;then
A4:Z c= dom f by XBOOLE_1:18;then
A5:f is_differentiable_on Z & for x st x in Z holds (f`|Z).x = a
    by FDIFF_1:31,A3;
A6:arcsin is_differentiable_on Z by A2,SIN_COS6:84,FDIFF_1:34;
   for x st x in Z holds
     ((f(#)(arcsin))`|Z).x =a*arcsin.x+(a*x+b)/sqrt(1-x^2)
   proof
    let x;
    assume A7:x in Z; then
A8:-1 < x & x < 1 by A2,RCOMP_1:47;
    ((f(#)(arcsin))`|Z).x
   =(arcsin.x)*diff(f,x) + (f.x)*diff(arcsin,x) by A5,A1,FDIFF_1:29,A6,A7
  .=(arcsin.x)*(f`|Z).x+ (f.x)*diff(arcsin,x) by A5,A7,FDIFF_1:def 8
  .=(arcsin.x)*a+ (f.x)*diff(arcsin,x) by A7,A4,FDIFF_1:31,A3
  .=(arcsin.x)*a+ (f.x)*(1 / sqrt(1-x^2)) by SIN_COS6:84,A8
  .=a*arcsin.x+ (a*x+b)*(1 / sqrt(1-x^2)) by A3,A7
  .=a*arcsin.x+(a*x+b) / sqrt(1-x^2) by XCMPLX_1:100;
    hence thesis;
  end;
  hence thesis by A6,A5,A1,FDIFF_1:29;
 end;

::f.x=(a*x+b)*arccos.x
theorem
 Z c= dom (f(#)(arccos)) & Z c= ]. -1,1 .[ &
 (for x st x in Z holds f.x=a*x+b) implies
 f(#)(arccos) is_differentiable_on Z & for x st x in Z holds
 ((f(#)(arccos))`|Z).x =a*arccos.x-(a*x+b)/sqrt(1-x^2)
 proof
   assume that A1:Z c= dom (f(#)(arccos)) and A2:Z c= ]. -1,1 .[ and
A3:for x st x in Z holds f.x=a*x+b;
   Z c= dom f /\ dom arccos by SEQ_1:def 5,A1;then
A4:Z c= dom f by XBOOLE_1:18;then
A5:f is_differentiable_on Z & for x st x in Z holds (f`|Z).x = a
    by FDIFF_1:31,A3;
A6:arccos is_differentiable_on Z by A2,SIN_COS6:108,FDIFF_1:34;
   for x st x in Z holds
     ((f(#)(arccos))`|Z).x =a*arccos.x-(a*x+b)/sqrt(1-x^2)
   proof
     let x;
     assume A7:x in Z;
     then A8:-1 < x & x < 1 by A2,RCOMP_1:47;
     ((f(#)(arccos))`|Z).x
    =(arccos.x)*diff(f,x) + (f.x)*diff(arccos,x) by A5,A1,FDIFF_1:29,A6,A7
   .=(arccos.x)*(f`|Z).x+ (f.x)*diff(arccos,x) by A5,A7,FDIFF_1:def 8
   .=(arccos.x)*a+ (f.x)*diff(arccos,x) by A7,A4,FDIFF_1:31,A3
   .=(arccos.x)*a+ (f.x)*(-1 / sqrt(1-x^2)) by SIN_COS6:108,A8
   .=(arccos.x)*a-(f.x)*(1 / sqrt(1-x^2))
   .=a*arccos.x-(a*x+b)*(1 / sqrt(1-x^2)) by A3,A7
   .=a*arccos.x-(a*x+b) / sqrt(1-x^2) by XCMPLX_1:100;
    hence thesis;
   end;
   hence thesis by A6,A5,A1,FDIFF_1:29;
 end;

::f.x=(1/2)*arcsin(2*x)
theorem
 Z c= dom ((1/2)(#)((arcsin)*f)) & (for x st x in Z holds f.x=2*x &
 f.x > -1 & f.x < 1) implies (1/2)(#)((arcsin)*f) is_differentiable_on Z
 & for x st x in Z holds (((1/2)(#)((arcsin)*f))`|Z).x=1/sqrt(1-(2*x)^2)
 proof
   assume that A1:Z c= dom ((1/2)(#)((arcsin)*f)) and
A2: for x st x in Z holds f.x=2*x & f.x > -1 & f.x < 1;
A3: Z c= dom ((arcsin)*f) by SEQ_1:def 6,A1;
A4: for x st x in Z holds f.x=2*x+0 & f.x > -1 & f.x < 1 by A2;then
A5:(arcsin)*f is_differentiable_on Z & for x st x in Z holds
    (((arcsin)*f)`|Z).x = 2 / sqrt(1-(2*x+0)^2) by A3,Th14;
  for x st x in Z holds (((1/2)(#)((arcsin)*f))`|Z).x=1/sqrt(1-(2*x)^2)
  proof
   let x;
   assume A6:x in Z;
   then (((1/2)(#)((arcsin)*f))`|Z).x =(1/2)*diff(((arcsin)*f),x)
      by A5,A1,FDIFF_1:28
   .=(1/2)*(((arcsin)*f)`|Z).x by A5,A6,FDIFF_1:def 8
   .=(1/2)*(2 / sqrt(1-(2*x+0)^2)) by A4,A3,Th14,A6
   .=((1/2)*2 )/ sqrt(1-(2*x)^2) by XCMPLX_1:75
   .=1/sqrt(1-(2*x)^2);
   hence thesis;
  end;
  hence thesis by A5,A1,FDIFF_1:28;
 end;

::f.x=(1/2)*arccos(2*x)
theorem
 Z c= dom ((1/2)(#)((arccos)*f)) & (for x st x in Z holds f.x=2*x &
 f.x > -1 & f.x < 1) implies (1/2)(#)((arccos)*f) is_differentiable_on Z
 & for x st x in Z holds (((1/2)(#)((arccos)*f))`|Z).x=-1/sqrt(1-(2*x)^2)
 proof
   assume that A1:Z c= dom ((1/2)(#)((arccos)*f)) and
A2: for x st x in Z holds f.x=2*x & f.x > -1 & f.x < 1;
A3: Z c= dom ((arccos)*f) by SEQ_1:def 6,A1;
A4: for x st x in Z holds f.x=2*x+0 & f.x > -1 & f.x < 1 by A2;then
A5:(arccos)*f is_differentiable_on Z & for x st x in Z holds
   (((arccos)*f)`|Z).x = -2 / sqrt(1-(2*x+0)^2) by A3,Th15;
for x st x in Z holds (((1/2)(#)((arccos)*f))`|Z).x=-1/sqrt(1-(2*x)^2)
  proof
   let x;
   assume A6:x in Z;
   then (((1/2)(#)((arccos)*f))`|Z).x
   =(1/2)*diff(((arccos)*f),x) by A5,A1,FDIFF_1:28
  .=(1/2)*(((arccos)*f)`|Z).x by A5,A6,FDIFF_1:def 8
  .=(1/2)*(-2 / sqrt(1-(2*x+0)^2)) by A4,A3,Th15,A6
  .=-(1/2)*(2 / sqrt(1-(2*x)^2))
  .=-((1/2)*2)/sqrt(1-(2*x)^2) by XCMPLX_1:75
  .=-1/sqrt(1-(2*x)^2);
   hence thesis;
  end;
  hence thesis by A5,A1,FDIFF_1:28;
 end;

::f.x=(1-x|^2) #R (1/2)
theorem Th22:
 Z c= dom (( #R (1/2))*f) & f=f1-f2 & f2=#Z 2 &
 (for x st x in Z holds f1.x=1 & f.x >0) implies
 (( #R (1/2))*f) is_differentiable_on Z &
 for x st x in Z holds ((( #R (1/2))*f)`|Z).x =-x*(1-x #Z 2) #R (-1/2)
 proof
   assume A1:Z c= dom (( #R (1/2))*f) & f=f1-f2 & f2=#Z 2 &
      (for x st x in Z holds f1.x=1 & f.x >0);
A2: for y st y in Z holds y in dom f by FUNCT_1:21,A1;
A3: f1+(-1)(#)f2=f1+-f2 by RFUNCT_1:38
    .=f by A1, RFUNCT_1:40;then
A4: Z c= dom ((f1+(-1)(#)f2)) by TARSKI:def 3,A2;
A5:f2=#Z 2 & for x st x in Z holds f1.x=1+0*x by A1;then
A6:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x =0+2*(-1)*x by A3,FDIFF_4:12,A4;
A7:for x st x in Z holds ( #R (1/2))*f is_differentiable_in x
   proof
    let x;
    assume A8:x in Z;then
A9:f is_differentiable_in x by FDIFF_1:16,A6;
    f.x >0 by A1,A8;
    hence ( #R (1/2))*f is_differentiable_in x by TAYLOR_1:22,A9;
   end;
   then A10:( #R (1/2))*f is_differentiable_on Z by A1,FDIFF_1:16;
   for x st x in Z holds ((( #R (1/2))*f)`|Z).x =-x*(1-x #Z 2) #R (-1/2)
   proof
    let x;
    assume A11:x in Z;then
A12: x in dom (f1-f2) by FUNCT_1:21,A1;
A13: f is_differentiable_in x by FDIFF_1:16,A6,A11;
A14: (f1-f2).x=f1.x - f2.x by SEQ_1:def 4,A12
      .=1 -(f2.x) by A1,A11
      .=1 -(x #Z 2) by A1,TAYLOR_1:def 1;then
A15: f.x = 1-x #Z 2 & f.x >0 by A1,A11;
    ((( #R (1/2))*f)`|Z).x
   =diff((( #R (1/2))*f),x) by FDIFF_1:def 8,A10,A11
  .=(1/2)*((f.x) #R (1/2-1)) * diff(f,x) by TAYLOR_1:22,A15,A13
  .=(1/2)*((f.x) #R (1/2-1))*(f`|Z).x by A6,A11,FDIFF_1:def 8
  .=(1/2)*((f.x) #R (1/2-1))*(0+2*(-1)*x) by A5,A3,FDIFF_4:12,A4,A11
  .=-x* (1-x #Z 2) #R (-1/2) by A1,A14;
    hence thesis;
   end;
   hence thesis by A7,A1,FDIFF_1:16;
 end;

::f.x=x*arcsin.x+(1-x|^2) #R (1/2)
theorem
 Z c= dom ((id Z)(#)(arcsin)+( #R (1/2))*f) & Z c= ]. -1,1 .[ &
 f=f1-f2 & f2=#Z 2 & (for x st x in Z holds f1.x=1 & f.x >0 & x<>0) implies
 (id Z)(#)(arcsin)+( #R (1/2))*f is_differentiable_on Z &
 for x st x in Z holds (((id Z)(#)(arcsin)+( #R (1/2))*f)`|Z).x =arcsin.x
 proof
   assume that A1:Z c= dom ((id Z)(#)(arcsin)+( #R (1/2))*f) and
A2:Z c= ]. -1,1 .[ and
A3:f=f1-f2 & f2=#Z 2 & (for x st x in Z holds f1.x=1 & f.x >0 & x<>0);
A4:f=f1-f2 & f2=#Z 2 & (for x st x in Z holds f1.x=1 & f.x >0) by A3;
A5:Z c= dom ((id Z)(#)(arcsin)) /\ dom (( #R (1/2))*f) by SEQ_1:def 3,A1;
then A6:Z c= dom ((id Z)(#)(arcsin)) by XBOOLE_1:18;
A7:Z c= dom (( #R (1/2))*f) by XBOOLE_1:18,A5;
A8:(id Z)(#)(arcsin) is_differentiable_on Z & for x st x in Z holds
   (((id Z)(#)(arcsin))`|Z).x =arcsin.x+x/sqrt(1-x^2) by Th16,A2,A6;
A9:(( #R (1/2))*f) is_differentiable_on Z & for x st x in Z holds
   ((( #R (1/2))*f)`|Z).x =-x*(1-x #Z 2) #R (-1/2) by Th22,A4,A7;
A10:for x st x in Z holds x*(1-x #Z 2) #R (-1/2)=x/sqrt(1-x^2)
   proof
     let x;
     assume A11:x in Z;then
A12: x in dom (f1-f2) by FUNCT_1:21,A7,A3;
     (f1-f2).x=f1.x - f2.x by SEQ_1:def 4,A12
      .=1 -(f2.x) by A3,A11
      .=1 -(x #Z 2) by A3,TAYLOR_1:def 1;then
A13: f.x = 1-x #Z 2 & f.x >0 by A3,A11;
A14:  1-x^2>0 by Th1,A13;
     (1-x #Z 2) #R (-1/2)=(1-x^2) #R (-1/2) by Th1
       .=1/sqrt(1-x^2) by Th3,A14;
     hence thesis by XCMPLX_1:100;
   end;
for x st x in Z holds (((id Z)(#)(arcsin)+( #R (1/2))*f)`|Z).x =arcsin.x
   proof
     let x;
     assume A15:x in Z;
     hence (((id Z)(#)(arcsin)+( #R (1/2))*f)`|Z).x
     =diff((id Z)(#)(arcsin),x)+diff(( #R (1/2))*f,x)
       by A8,A1,A9,FDIFF_1:26
    .=(((id Z)(#)(arcsin))`|Z).x+diff(( #R (1/2))*f,x)
       by FDIFF_1:def 8,A8,A15
    .=(((id Z)(#)(arcsin))`|Z).x+((( #R (1/2))*f)`|Z).x
       by FDIFF_1:def 8,A9,A15
    .=arcsin.x+x/sqrt(1-x^2)+((( #R (1/2))*f)`|Z).x by Th16,A2,A6,A15
    .=arcsin.x+x/sqrt(1-x^2)+-x*(1-x #Z 2) #R (-1/2) by Th22,A4,A7,A15
    .=arcsin.x+x/sqrt(1-x^2)-x*(1-x #Z 2) #R (-1/2)
    .=arcsin.x+x/sqrt(1-x^2)-x/sqrt(1-x^2) by A10,A15
    .=arcsin.x;
    end;
    hence thesis by A9,A8,FDIFF_1:26,A1;
  end;

::f.x=x*arccos.x-(1-x|^2) #R (1/2)
theorem
 Z c= dom ((id Z)(#)(arccos)-( #R (1/2))*f) & Z c= ]. -1,1 .[ &
 f=f1-f2 & f2=#Z 2 & (for x st x in Z holds f1.x=1 & f.x >0 & x<>0) implies
 (id Z)(#)(arccos)-( #R (1/2))*f is_differentiable_on Z &
 for x st x in Z holds (((id Z)(#)(arccos)-( #R (1/2))*f)`|Z).x =arccos.x
 proof
   assume that A1:Z c= dom ((id Z)(#)(arccos)-( #R (1/2))*f) and
A2:Z c= ]. -1,1 .[ and
A3:f=f1-f2 & f2=#Z 2 & (for x st x in Z holds f1.x=1 & f.x >0 & x<>0);
A4:f=f1-f2 & f2=#Z 2 & (for x st x in Z holds f1.x=1 & f.x >0 ) by A3;
A5:Z c= dom ((id Z)(#)(arccos)) /\ dom (( #R (1/2))*f) by SEQ_1:def 4,A1;
then A6:Z c= dom ((id Z)(#)(arccos)) by XBOOLE_1:18;
A7:Z c= dom (( #R (1/2))*f) by XBOOLE_1:18,A5;
A8:(id Z)(#)(arccos) is_differentiable_on Z & for x st x in Z holds
   (((id Z)(#)(arccos))`|Z).x =arccos.x-x/sqrt(1-x^2) by Th17,A2,A6;
A9:(( #R (1/2))*f) is_differentiable_on Z & for x st x in Z holds
    ((( #R (1/2))*f)`|Z).x =-x*(1-x #Z 2) #R (-1/2) by Th22,A4,A7;
A10:for x st x in Z holds x*(1-x #Z 2) #R (-1/2)=x/sqrt(1-x^2)
   proof
     let x;
     assume A11:x in Z;then
A12: x in dom (f1-f2) by FUNCT_1:21,A7,A3;
    (f1-f2).x=f1.x - f2.x by SEQ_1:def 4,A12
      .=1 -(f2.x) by A3,A11
      .=1 -(x #Z 2) by A3,TAYLOR_1:def 1;then
A13: f.x = 1-x #Z 2 & f.x >0 by A3,A11;
A14:1-x^2>0 by A13,Th1;
    (1-x #Z 2) #R (-1/2)=(1-x^2) #R (-1/2) by Th1
   .=1/sqrt(1-x^2) by Th3,A14;
    hence thesis by XCMPLX_1:100;
   end;
for x st x in Z holds (((id Z)(#)(arccos)-( #R (1/2))*f)`|Z).x =arccos.x
  proof
   let x;
   assume A15:x in Z;
   hence (((id Z)(#)(arccos)-( #R (1/2))*f)`|Z).x
    =diff((id Z)(#)(arccos),x)-diff(( #R (1/2))*f,x) by A8,A1,A9,FDIFF_1:27
   .=(((id Z)(#)(arccos))`|Z).x-diff(( #R (1/2))*f,x)
      by FDIFF_1:def 8,A8,A15
   .=(((id Z)(#)(arccos))`|Z).x-((( #R (1/2))*f)`|Z).x
      by FDIFF_1:def 8,A9,A15
   .=arccos.x-x/sqrt(1-x^2)-((( #R (1/2))*f)`|Z).x by Th17,A2,A6,A15
   .=arccos.x-x/sqrt(1-x^2)--x*(1-x #Z 2) #R (-1/2) by Th22,A4,A7,A15
   .=arccos.x-x/sqrt(1-x^2)+x*(1-x #Z 2) #R (-1/2)
   .=arccos.x-x/sqrt(1-x^2)+x/sqrt(1-x^2) by A10,A15
   .=arccos.x;
  end;
  hence thesis by A9,A8,FDIFF_1:27,A1;
 end;

::f.x=x*arcsin(x/a)
theorem Th25:
 Z c= dom ((id Z)(#)((arcsin)*f)) &
 (for x st x in Z holds f.x=x/a & f.x > -1 & f.x < 1) implies
 (id Z)(#)((arcsin)*f) is_differentiable_on Z & for x st x in Z holds
 (((id Z)(#)((arcsin)*f))`|Z).x =arcsin.(x/a)+x/(a*sqrt(1-(x/a)^2))
 proof
   assume that A1:Z c= dom ((id Z)(#)((arcsin)*f)) and
A2: for x st x in Z holds f.x=x/a & f.x > -1 & f.x < 1;
  for x st x in Z holds f.x=(1/a)*x+0
  proof
    let x;
    assume x in Z;then
    f.x=x/a by A2;
    hence f.x=(1/a)*x+0 by XCMPLX_1:100;
  end;then
A3:for x st x in Z holds f.x=(1/a)*x+0 & f.x > -1 & f.x < 1 by A2;
A4: Z c= dom (id Z) /\ dom ((arcsin)*f) by SEQ_1:def 5,A1;then
A5: Z c= dom (id Z) by XBOOLE_1:18;
A6: Z c= dom ((arcsin)*f) by XBOOLE_1:18,A4;
A7: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;then
A8: id Z is_differentiable_on Z & for x st x in Z
     holds ((id Z)`|Z).x = 1 by FDIFF_1:31,A5;
A9:(arcsin)*f is_differentiable_on Z & for x st x in Z holds
    (((arcsin)*f)`|Z).x=(1/a)/sqrt(1-((1/a)*x+0)^2) by A6,A3,Th14;
A10:for x st x in Z holds
     (((arcsin)*f)`|Z).x=1/(a*sqrt(1-(x/a)^2))
  proof
    let x;
    assume x in Z;
    then (((arcsin)*f)`|Z).x=(1/a)/sqrt(1-((1/a)*x+0)^2) by A6,A3,Th14
      .=(1/a)/sqrt(1-(x/a)^2) by XCMPLX_1:100
      .=1/(a*sqrt(1-(x/a)^2)) by XCMPLX_1:79;
    hence thesis;
   end;
  for x st x in Z holds
     (((id Z)(#)((arcsin)*f))`|Z).x =arcsin.(x/a)+x/(a*sqrt(1-(x/a)^2))
  proof
    let x;
    assume A11:x in Z;
A12:((arcsin)*f).x=arcsin.(f.x) by A11,A6,FUNCT_1:22
   .=arcsin.(x/a) by A2,A11;
   (((id Z)(#)((arcsin)*f))`|Z).x
  =((arcsin)*f).x*diff((id Z),x) + ((id Z).x)*diff((arcsin)*f,x)
    by A8,A1,FDIFF_1:29,A9,A11
  .=((arcsin)*f).x*((id Z)`|Z).x + ((id Z).x)*diff((arcsin)*f,x)
     by A8,A11,FDIFF_1:def 8
  .=((arcsin)*f).x*1+((id Z).x)*diff((arcsin)*f,x) by A11,FDIFF_1:31,A5,A7
  .=((arcsin)*f).x*1+((id Z).x)*(((arcsin)*f)`|Z).x by A9,A11,FDIFF_1:def 8
  .=((arcsin)*f).x+x*(((arcsin)*f)`|Z).x by FUNCT_1:35,A11
  .=arcsin.(x/a)+x*(1/(a*sqrt(1-(x/a)^2))) by A10,A11,A12
  .=arcsin.(x/a)+x/(a*sqrt(1-(x/a)^2)) by XCMPLX_1:100;
   hence thesis;
  end;
  hence thesis by A9,A8,A1,FDIFF_1:29;
 end;

::f.x=x*arccos(x/a)
theorem Th26:
 Z c= dom ((id Z)(#)((arccos)*f)) &
 (for x st x in Z holds f.x=x/a & f.x > -1 & f.x < 1) implies
 (id Z)(#)((arccos)*f) is_differentiable_on Z & for x st x in Z holds
 (((id Z)(#)((arccos)*f))`|Z).x =arccos.(x/a)-x/(a*sqrt(1-(x/a)^2))
 proof
   assume that A1:Z c= dom ((id Z)(#)((arccos)*f)) and
A2: for x st x in Z holds f.x=x/a & f.x > -1 & f.x < 1;
 for x st x in Z holds f.x=(1/a)*x+0
  proof
    let x;
    assume x in Z;then
    f.x=x/a by A2;
    hence f.x=(1/a)*x+0 by XCMPLX_1:100;
  end;then
A3:for x st x in Z holds f.x=(1/a)*x+0 & f.x > -1 & f.x < 1 by A2;
A4: Z c= dom (id Z) /\ dom ((arccos)*f) by SEQ_1:def 5,A1;then
A5: Z c= dom (id Z) by XBOOLE_1:18;
A6: Z c= dom ((arccos)*f) by XBOOLE_1:18,A4;
A7: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;then
A8: id Z is_differentiable_on Z & for x st x in Z
    holds ((id Z)`|Z).x = 1 by FDIFF_1:31,A5;
A9: (arccos)*f is_differentiable_on Z & for x st x in Z holds
    (((arccos)*f)`|Z).x=-(1/a)/sqrt(1-((1/a)*x+0)^2) by A6,A3,Th15;
A10:for x st x in Z holds
    (((arccos)*f)`|Z).x=-1/(a*sqrt(1-(x/a)^2))
  proof
    let x;
    assume x in Z;
    then (((arccos)*f)`|Z).x=-(1/a)/sqrt(1-((1/a)*x+0)^2) by A6,A3,Th15
    .=-(1/a)/sqrt(1-(x/a)^2) by XCMPLX_1:100
    .=-1/(a*sqrt(1-(x/a)^2)) by XCMPLX_1:79;
    hence thesis;
  end;
 for x st x in Z holds
  (((id Z)(#)((arccos)*f))`|Z).x =arccos.(x/a)-x/(a*sqrt(1-(x/a)^2))
  proof
    let x;
    assume A11:x in Z;
A12: ((arccos)*f).x=arccos.(f.x) by A11,A6,FUNCT_1:22
   .=arccos.(x/a) by A2,A11;
    (((id Z)(#)((arccos)*f))`|Z).x
   =((arccos)*f).x*diff((id Z),x) + ((id Z).x)*diff((arccos)*f,x)
     by A8,A1,FDIFF_1:29,A9,A11
  .=((arccos)*f).x*((id Z)`|Z).x + ((id Z).x)*diff((arccos)*f,x)
     by A8,A11,FDIFF_1:def 8
  .=((arccos)*f).x*1+((id Z).x)*diff((arccos)*f,x) by A11,FDIFF_1:31,A5,A7
  .=((arccos)*f).x*1+((id Z).x)*(((arccos)*f)`|Z).x by A9,A11,FDIFF_1:def 8
  .=((arccos)*f).x+x*(((arccos)*f)`|Z).x by FUNCT_1:35,A11
  .=arccos.(x/a)+x*(-1/(a*sqrt(1-(x/a)^2))) by A10,A11,A12
  .=arccos.(x/a)-x*(1/(a*sqrt(1-(x/a)^2)))
  .=arccos.(x/a)-x/(a*sqrt(1-(x/a)^2)) by XCMPLX_1:100;
   hence thesis;
  end;
  hence thesis by A9,A8,A1,FDIFF_1:29;
 end;

::f.x=(a^2-x|^2) #R (1/2)
theorem Th27:
 Z c= dom (( #R (1/2))*f) & f=f1-f2 & f2=#Z 2 & (for x st x in Z holds
 f1.x=a^2 & f.x >0) implies ( #R (1/2))*f is_differentiable_on Z &
 for x st x in Z holds ((( #R (1/2))*f)`|Z).x = -x*(a^2-x #Z 2) #R (-1/2)
 proof
   assume A1:Z c= dom (( #R (1/2))*f) & f=f1-f2 & f2=#Z 2 &
          (for x st x in Z holds f1.x=a^2 & f.x >0);then
A2: for y st y in Z holds y in dom f by FUNCT_1:21;
A3: f1+(-1)(#)f2=f1+-f2 by RFUNCT_1:38
    .=f by A1,RFUNCT_1:40;then
A4:Z c= dom ((f1+(-1)(#)f2)) by TARSKI:def 3,A2;
A5: f2=#Z 2 & for x st x in Z holds f1.x=a^2+0*x by A1;then
A6:f is_differentiable_on Z &
   for x st x in Z holds (f`|Z).x =0+2*(-1)*x by A3,FDIFF_4:12,A4;
A7:for x st x in Z holds ( #R (1/2))*f is_differentiable_in x
  proof
    let x;
    assume A8:x in Z;
    then A9:f is_differentiable_in x by FDIFF_1:16,A6;
    f.x >0 by A1,A8;
    hence ( #R (1/2))*f is_differentiable_in x by TAYLOR_1:22,A9;
  end;then
A10:( #R (1/2))*f is_differentiable_on Z by A1,FDIFF_1:16;
 for x st x in Z holds ((( #R (1/2))*f)`|Z).x = -x*(a^2-x #Z 2) #R (-1/2)
  proof
    let x;
    assume A11:x in Z;
    then A12: x in dom (f1-f2) by FUNCT_1:21,A1;
A13: f is_differentiable_in x by FDIFF_1:16,A6,A11;
A14: (f1-f2).x=f1.x - f2.x by SEQ_1:def 4,A12
   .=a^2 -(f2.x) by A1,A11
   .=a^2 -(x #Z 2) by A1,TAYLOR_1:def 1;then
A15: f.x = a^2-x #Z 2 & f.x >0 by A1,A11;
    ((( #R (1/2))*f)`|Z).x = diff(( #R (1/2))*f,x) by A10,A11,FDIFF_1:def 8
  .=(1/2)*((f.x) #R (1/2-1)) * diff(f,x) by TAYLOR_1:22,A15,A13
  .=(1/2)*((f.x) #R (1/2-1))*(f`|Z).x by A6,A11,FDIFF_1:def 8
  .=(1/2)*((f.x) #R (1/2-1))*(0+2*(-1)*x) by A3,FDIFF_4:12,A4,A5,A11
  .=-x*(a^2-x #Z 2) #R (-1/2) by A1,A14;
   hence thesis;
  end;
  hence thesis by A7,A1,FDIFF_1:16;
 end;

::f.x=x*arcsin(x/a)+(a^2-x|^2) #R (1/2)
theorem
 Z c= dom ((id Z)(#)((arcsin)*f3)+( #R (1/2))*f) & Z c= ]. -1,1 .[ &
 f=f1-f2 & f2=#Z 2 & (for x st x in Z holds f1.x=a^2 & f.x >0 &
 f3.x=x/a & f3.x > -1 & f3.x < 1 & x<>0 & a>0) implies
 (id Z)(#)((arcsin)*f3)+( #R (1/2))*f is_differentiable_on Z & for x st x
 in Z holds (((id Z)(#)((arcsin)*f3)+( #R (1/2))*f)`|Z).x = arcsin.(x/a)
 proof
   assume A1:Z c= dom ((id Z)(#)((arcsin)*f3)+( #R (1/2))*f) & Z c= ]. -1,1 .[
      & f=f1-f2 & f2=#Z 2 & (for x st x in Z holds f1.x=a^2 & f.x >0 &
      f3.x=x/a & f3.x > -1 & f3.x < 1 & x<>0 & a>0 );then
A2: Z c= dom ((id Z)(#)((arcsin)*f3)) /\ dom (( #R (1/2))*f) by SEQ_1:def 3;
then A3:Z c= dom ((id Z)(#)((arcsin)*f3)) by XBOOLE_1:18;
A4: Z c= dom (( #R (1/2))*f) by XBOOLE_1:18,A2;
A5:Z c= ]. -1,1 .[ &
    (for x st x in Z holds f3.x=x/a & f3.x > -1 & f3.x < 1 ) by A1;then
A6: (id Z)(#)((arcsin)*f3) is_differentiable_on Z & for x st x in Z holds
    (((id Z)(#)((arcsin)*f3))`|Z).x =arcsin.(x/a)+x/(a*sqrt(1-(x/a)^2))
     by A3,Th25;
A7:f=f1-f2 & f2=#Z 2 & (for x st x in Z holds f1.x=a^2 & f.x >0 ) by A1;
then A8:( #R (1/2))*f is_differentiable_on Z & for x st x in Z holds
   ((( #R (1/2))*f)`|Z).x = -x*(a^2-x #Z 2) #R (-1/2) by A4,Th27;
A9:for x st x in Z holds
    (a^2-x #Z 2) #R (-1/2)=1/(a*sqrt(1-(x/a)^2))
  proof
    let x;
    assume A10:x in Z;then
A11:a>0 by A1;then
A12:a^2>0 by SQUARE_1:74;
A13:f3.x=x/a by A1,A10;
   f3.x > -1 by A1,A10;then
A14: 1+f3.x>1+(-1) by XREAL_1:8;
   f3.x < 1 by A1,A10;then
   1-f3.x >1-1 by XREAL_1:12;then
A15: (1+f3.x)*(1-f3.x)>0 by A14,XREAL_1:131;
A16:(a^2)*(1-(x/a)^2)>0 by XREAL_1:131,A12,A13,A15;
   1/(a*sqrt(1-(x/a)^2))
  =1/((sqrt a^2)* sqrt (1-(x/a)^2)) by SQUARE_1:89,A11
 .=1/sqrt ((a^2)*(1-(x/a)^2)) by A12,A13,A15,SQUARE_1:97
 .=(a^2*1 - (a *(x/a))^2) #R (-1/2) by A16,Th3
 .=(a^2 - (x*a/a)^2) #R (-1/2) by XCMPLX_1:75
 .=(a^2 - (x*(a/a))^2) #R (-1/2) by XCMPLX_1:75
 .=(a^2 - (x*1)^2) #R (-1/2) by A11,XCMPLX_1:60
 .=(a^2-x #Z 2) #R (-1/2) by Th1;
  hence thesis;
 end;
 for x st x in Z holds
   (((id Z)(#)((arcsin)*f3)+( #R (1/2))*f)`|Z).x = arcsin.(x/a)
  proof
   let x;
   assume A17:x in Z;
   (((id Z)(#)((arcsin)*f3)+( #R (1/2))*f)`|Z).x
  = diff((id Z)(#)((arcsin)*f3),x) + diff(( #R (1/2))*f,x)
     by A8,A6,A1,FDIFF_1:26,A17
  .=(((id Z)(#)((arcsin)*f3))`|Z).x+ diff(( #R (1/2))*f,x)
     by A6,A17,FDIFF_1:def 8
  .=(((id Z)(#)((arcsin)*f3))`|Z).x+ ((( #R (1/2))*f)`|Z).x
     by A8,A17,FDIFF_1:def 8
  .=arcsin.(x/a)+x/(a*sqrt(1-(x/a)^2)) + ((( #R (1/2))*f)`|Z).x
     by A5,A3,Th25,A17
  .=arcsin.(x/a)+x/(a*sqrt(1-(x/a)^2)) + -x*(a^2-x #Z 2) #R (-1/2)
     by A7,A4,Th27,A17
  .=arcsin.(x/a)+x/(a*sqrt(1-(x/a)^2)) -x*(a^2-x #Z 2) #R (-1/2)
  .=arcsin.(x/a)+x/(a*sqrt(1-(x/a)^2)) -x*(1/(a*sqrt(1-(x/a)^2))) by A9,A17
  .=arcsin.(x/a)+x/(a*sqrt(1-(x/a)^2)) -x/(a*sqrt(1-(x/a)^2))
    by XCMPLX_1:100
  .=arcsin.(x/a);
   hence thesis;
  end;
  hence thesis by A8,A6,A1,FDIFF_1:26;
 end;

::f.x=x*arccos(x/a)-(a^2-x|^2) #R (1/2)
theorem
 Z c= dom ((id Z)(#)((arccos)*f3)-( #R (1/2))*f) & Z c= ]. -1,1 .[ &
 f=f1-f2 & f2=#Z 2 & (for x st x in Z holds f1.x=a^2 & f.x >0 &
 f3.x=x/a & f3.x > -1 & f3.x < 1 & x<>0 & a>0) implies
 (id Z)(#)((arccos)*f3)-( #R (1/2))*f is_differentiable_on Z & for x st x
 in Z holds (((id Z)(#)((arccos)*f3)-( #R (1/2))*f)`|Z).x = arccos.(x/a)
 proof
   assume A1:Z c= dom ((id Z)(#)((arccos)*f3)-( #R (1/2))*f) &
     Z c= ]. -1,1 .[ &
   f=f1-f2 & f2=#Z 2 & (for x st x in Z holds f1.x=a^2 & f.x >0 &
     f3.x=x/a & f3.x > -1 & f3.x < 1 & x<>0 & a>0 );then
A2:Z c= dom ((id Z)(#)((arccos)*f3)) /\ dom (( #R (1/2))*f) by SEQ_1:def 4;
then A3:Z c= dom ((id Z)(#)((arccos)*f3)) by XBOOLE_1:18;
A4: Z c= dom (( #R (1/2))*f) by XBOOLE_1:18,A2;
A5: Z c= ]. -1,1 .[ &
    (for x st x in Z holds f3.x=x/a & f3.x > -1 & f3.x < 1 ) by A1;then
A6: (id Z)(#)((arccos)*f3) is_differentiable_on Z & for x st x in Z holds
    (((id Z)(#)((arccos)*f3))`|Z).x =arccos.(x/a)-x/(a*sqrt(1-(x/a)^2))
     by A3,Th26;
A7:f=f1-f2 & f2=#Z 2 & (for x st x in Z holds f1.x=a^2 & f.x >0) by A1;
then A8:( #R (1/2))*f is_differentiable_on Z & for x st x in Z holds
    ((( #R (1/2))*f)`|Z).x = -x*(a^2-x #Z 2) #R (-1/2) by A4,Th27;
A9:for x st x in Z holds
    (a^2-x #Z 2) #R (-1/2)=1/(a*sqrt(1-(x/a)^2))
  proof
   let x;
   assume A10:x in Z;then
A11:a>0 by A1;then
A12:a^2>0 by SQUARE_1:74;
A13:f3.x=x/a by A1,A10;
   f3.x > -1 by A1,A10;then
A14:1+f3.x>1+(-1) by XREAL_1:8;
   f3.x < 1 by A1,A10;then
   1-f3.x >1-1 by XREAL_1:12;then
A15: (1+f3.x)*(1-f3.x)>0 by A14,XREAL_1:131;
A16:(a^2)*(1-(x/a)^2)>0 by A15,XREAL_1:131,A12,A13;
    1/(a*sqrt(1-(x/a)^2))
  =1/((sqrt a^2)* sqrt (1-(x/a)^2)) by SQUARE_1:89,A11
 .=1/sqrt ((a^2)*(1-(x/a)^2)) by A12,A13,A15,SQUARE_1:97
 .=(a^2*1 - (a *(x/a))^2) #R (-1/2) by A16,Th3
 .=(a^2 - (x*a/a)^2) #R (-1/2) by XCMPLX_1:75
 .=(a^2 - (x*(a/a))^2) #R (-1/2) by XCMPLX_1:75
 .=(a^2 - (x*1)^2) #R (-1/2) by A11,XCMPLX_1:60
 .=(a^2-x #Z 2) #R (-1/2) by Th1;
  hence thesis;
 end;
 for x st x in Z holds
   (((id Z)(#)((arccos)*f3)-( #R (1/2))*f)`|Z).x = arccos.(x/a)
  proof
   let x;
   assume A17:x in Z;
   (((id Z)(#)((arccos)*f3)-( #R (1/2))*f)`|Z).x
  = diff((id Z)(#)((arccos)*f3),x) - diff(( #R (1/2))*f,x)
    by A8,A6,A1,FDIFF_1:27,A17
 .=(((id Z)(#)((arccos)*f3))`|Z).x- diff(( #R (1/2))*f,x)
   by A6,A17,FDIFF_1:def 8
 .=(((id Z)(#)((arccos)*f3))`|Z).x- ((( #R (1/2))*f)`|Z).x
   by A8,A17,FDIFF_1:def 8
 .=arccos.(x/a)-x/(a*sqrt(1-(x/a)^2)) - ((( #R (1/2))*f)`|Z).x
   by A5,A3,Th26,A17
 .=arccos.(x/a)-x/(a*sqrt(1-(x/a)^2)) - -x*(a^2-x #Z 2) #R (-1/2)
   by A7,A4,Th27,A17
 .=arccos.(x/a)-x/(a*sqrt(1-(x/a)^2)) +x*(a^2-x #Z 2) #R (-1/2)
 .=arccos.(x/a)-x/(a*sqrt(1-(x/a)^2)) +x*(1/(a*sqrt(1-(x/a)^2))) by A9,A17
 .=arccos.(x/a)-x/(a*sqrt(1-(x/a)^2)) +x/(a*sqrt(1-(x/a)^2))
   by XCMPLX_1:100
 .=arccos.(x/a);
   hence thesis;
  end;
  hence thesis by A8,A6,A1,FDIFF_1:27;
 end;

::f.x=-1/(n*(sin.x)|^n)
theorem
 Z c= dom ((-1/n)(#)(( #Z n)*(sin^))) & n>0 &
 (for x st x in Z holds sin.x<>0) implies
 (-1/n)(#)(( #Z n)*(sin^)) is_differentiable_on Z & for x st x in Z holds
 (((-1/n)(#)(( #Z n)*(sin^)))`|Z).x=cos.x/((sin.x) #Z (n+1))
 proof
    assume that A1:Z c= dom ((-1/n)(#)(( #Z n)*(sin^))) and
A2: n>0 and
A3:(for x st x in Z holds sin.x<>0);
A4:Z c= dom (( #Z n)*(sin^)) by SEQ_1:def 6,A1;
   for y st y in Z holds y in dom (sin^) by FUNCT_1:21,A4;then
A5:Z c= dom (sin^) by TARSKI:def 3;
A6:sin^ is_differentiable_on Z & for x st x in Z holds
   ((sin^)`|Z).x= -cos.x/(sin.x)^2 by A3,FDIFF_4:40;
    now let x;
      assume x in Z;then
      sin^ is_differentiable_in x by A6,FDIFF_1:16;
      hence ( #Z n)*(sin^) is_differentiable_in x by TAYLOR_1:3;
    end;then
A7:( #Z n)*(sin^) is_differentiable_on Z by A4,FDIFF_1:16;
   for x st x in Z holds
      (((-1/n)(#)(( #Z n)*(sin^)))`|Z).x =cos.x/((sin.x) #Z (n+1))
   proof
     let x;
     assume A8:x in Z;then
A9:  sin^ is_differentiable_in x by A6,FDIFF_1:16;
A10:  sin.x<>0 by A8,A3;
A11:  (sin^).x=(sin.x)" by A5,A8,RFUNCT_1:def 8
    .=1/sin.x by XCMPLX_1:217;
     (((-1/n)(#)(( #Z n)*(sin^)))`|Z).x =(-1/n)*diff((( #Z n)*(sin^)),x)
        by A7,A1,FDIFF_1:28,A8
     .=(-1/n)*(n*(((sin^).x) #Z (n-1)) * diff(sin^,x)) by TAYLOR_1:3,A9
     .=(-1/n)*(n*(((sin^).x) #Z (n-1)) * ((sin^)`|Z).x)
        by FDIFF_1:def 8,A6,A8
     .=(-1/n)*(n*(((sin^).x) #Z (n-1)) *(-cos.x/(sin.x)^2))
        by A3,FDIFF_4:40,A8
     .=-((1/n)*n)*(((sin^).x) #Z (n-1)) *(-cos.x/(sin.x)^2)
     .=-1*(((sin^).x) #Z (n-1)) * (-cos.x/(sin.x)^2) by A2,XCMPLX_1:107
     .=-(1/sin.x) #Z (n-1) *(-cos.x/((sin.x) #Z 2)) by Th1,A11
     .=-(-cos.x/((sin.x) #Z 2))*(1/((sin.x) #Z (n-1))) by PREPOWER:52
     .=(cos.x/((sin.x) #Z 2))*(1/((sin.x) #Z (n-1)))
     .=cos.x/((sin.x) #Z 2)/((sin.x) #Z (n-1)) by XCMPLX_1:100
     .=cos.x/(((sin.x) #Z 2)*((sin.x) #Z (n-1))) by XCMPLX_1:79
     .=cos.x/((sin.x) #Z (2+(n-1))) by PREPOWER:54,A10
     .=cos.x/((sin.x) #Z (n+1));
     hence thesis;
    end;
    hence thesis by A7,A1,FDIFF_1:28;
  end;

::f.x=1/(n*(cos.x)|^n)
theorem
 Z c= dom ((1/n)(#)(( #Z n)*(cos^))) & n>0 &
 (for x st x in Z holds cos.x<>0) implies
 (1/n)(#)(( #Z n)*(cos^)) is_differentiable_on Z & for x st x in Z holds
 (((1/n)(#)(( #Z n)*(cos^)))`|Z).x=sin.x/((cos.x) #Z (n+1))
 proof
    assume that A1:Z c= dom ((1/n)(#)(( #Z n)*(cos^))) and
A2: n>0 and
A3:for x st x in Z holds cos.x<>0;
A4:Z c= dom (( #Z n)*(cos^)) by SEQ_1:def 6,A1;
   for y st y in Z holds y in dom (cos^) by FUNCT_1:21,A4;then
A5:Z c= dom (cos^) by TARSKI:def 3;
A6:cos^ is_differentiable_on Z & for x st x in Z holds
   ((cos^)`|Z).x=sin.x/(cos.x)^2 by A3,FDIFF_4:39;
    now let x;
      assume x in Z;then
      cos^ is_differentiable_in x by A6,FDIFF_1:16;
      hence ( #Z n)*(cos^) is_differentiable_in x by TAYLOR_1:3;
    end;then
A7: ( #Z n)*(cos^) is_differentiable_on Z by A4,FDIFF_1:16;
    for x st x in Z holds
      (((1/n)(#)(( #Z n)*(cos^)))`|Z).x=sin.x/((cos.x) #Z (n+1))
    proof
     let x;
     assume A8:x in Z;then
A9:  cos^ is_differentiable_in x by A6,FDIFF_1:16;
A10:  cos.x<>0 by A8,A3;
A11:  (cos^).x=(cos.x)" by A5,A8,RFUNCT_1:def 8
    .=1/cos.x by XCMPLX_1:217;
     (((1/n)(#)(( #Z n)*(cos^)))`|Z).x =(1/n)*diff((( #Z n)*(cos^)),x)
        by A7,A1,FDIFF_1:28,A8
     .=(1/n)*(n*(((cos^).x) #Z (n-1)) * diff(cos^,x)) by TAYLOR_1:3,A9
     .=(1/n)*(n*(((cos^).x) #Z (n-1)) * ( (cos^)`|Z).x )
        by FDIFF_1:def 8,A6,A8
     .=(1/n)*(n*(((cos^).x) #Z (n-1)) *(sin.x/(cos.x)^2))
       by A3,FDIFF_4:39,A8
     .=((1/n)*n)*(((cos^).x) #Z (n-1)) *(sin.x/(cos.x)^2)
     .=1*(((cos^).x) #Z (n-1)) * (sin.x/(cos.x)^2) by A2,XCMPLX_1:107
     .=(1/cos.x) #Z (n-1) *(sin.x/((cos.x) #Z 2)) by Th1,A11
     .=(1/((cos.x) #Z (n-1))) *(sin.x/((cos.x) #Z 2)) by PREPOWER:52
     .=(sin.x/((cos.x) #Z 2))/((cos.x) #Z (n-1)) by XCMPLX_1:100
     .=sin.x/(((cos.x) #Z 2)*((cos.x) #Z (n-1))) by XCMPLX_1:79
     .=sin.x/((cos.x) #Z (2+(n-1))) by PREPOWER:54,A10
     .=sin.x/((cos.x) #Z (n+1));
     hence thesis;
    end;
    hence thesis by A7,A1,FDIFF_1:28;
  end;

::f.x=sin(ln.x)
theorem
 Z c= dom (sin*ln) & (for x st x in Z holds x>0) implies
 sin*ln is_differentiable_on Z & for x st x in Z holds
 ((sin*ln)`|Z).x =cos.(ln.x)/x
 proof
   assume that A1:Z c= dom (sin*ln) and
A2: for x st x in Z holds x>0;
A3: for x st x in Z holds sin*ln is_differentiable_in x
    proof
      let x;
      assume x in Z;then
      x>0 by A2;then
A4:   ln is_differentiable_in x by TAYLOR_1:18;
      sin is_differentiable_in ln.x by SIN_COS:69;
      hence sin*ln is_differentiable_in x by A4,FDIFF_2:13;
    end;then
A5: sin*ln is_differentiable_on Z by FDIFF_1:16,A1;
    for x st x in Z holds
      ((sin*ln)`|Z).x =cos.(ln.x)/x
   proof
    let x;
    assume A6:x in Z;then
A7: x>0 by A2;then
A8:x in right_open_halfline(0);
A9: ln is_differentiable_in x by TAYLOR_1:18,A7;
    sin is_differentiable_in ln.x by SIN_COS:69;then
    diff(sin*ln,x)
   = diff(sin,ln.x)*diff(ln,x) by A9,FDIFF_2:13
  .=cos.(ln.x)*diff(ln,x) by SIN_COS:69
  .=cos.(ln.x)*(1/x) by A8,TAYLOR_1:18
  .=cos.(ln.x)/x by XCMPLX_1:100;
   hence thesis by FDIFF_1:def 8,A5,A6;
  end;
  hence thesis by FDIFF_1:16,A1,A3;
 end;

::f.x=cos(ln.x)
theorem
 Z c= dom (cos*ln) & (for x st x in Z holds x>0) implies
 cos*ln is_differentiable_on Z & for x st x in Z holds
 ((cos*ln)`|Z).x =-sin.(ln.x)/x
 proof
   assume that A1:Z c= dom (cos*ln) and
A2: for x st x in Z holds x>0;
A3: for x st x in Z holds cos*ln is_differentiable_in x
    proof
      let x;
      assume x in Z;then
      x>0 by A2;then
A4:   ln is_differentiable_in x by TAYLOR_1:18;
      cos is_differentiable_in ln.x by SIN_COS:68;
      hence cos*ln is_differentiable_in x by A4,FDIFF_2:13;
    end;then
A5: cos*ln is_differentiable_on Z by FDIFF_1:16,A1;
    for x st x in Z holds
      ((cos*ln)`|Z).x =-sin.(ln.x)/x
   proof
    let x;
    assume A6:x in Z;then
A7: x>0 by A2;then
A8:x in right_open_halfline(0);
A9: ln is_differentiable_in x by TAYLOR_1:18,A7;
    cos is_differentiable_in ln.x by SIN_COS:68;then
    diff(cos*ln,x)
   = diff(cos,ln.x)*diff(ln,x) by A9,FDIFF_2:13
  .=(-sin.(ln.x))*diff(ln,x) by SIN_COS:68
  .=(-sin.(ln.x))*(1/x) by A8,TAYLOR_1:18
  .=(-sin.(ln.x))/x by XCMPLX_1:100
  .=-sin.(ln.x)/x by XCMPLX_1:188;
   hence thesis by FDIFF_1:def 8,A5,A6;
  end;
  hence thesis by FDIFF_1:16,A1,A3;
 end;

::f.x=sin(exp.x)
theorem
 Z c= dom (sin*exp_R) implies sin*exp_R is_differentiable_on Z &
 for x st x in Z holds ((sin*exp_R)`|Z).x = exp_R.x * cos.(exp_R.x)
 proof
   assume A1:Z c= dom (sin*exp_R);
A2:for x st x in Z holds sin*exp_R is_differentiable_in x
    proof
     let x;
     assume x in Z;
A3:  exp_R is_differentiable_in x by SIN_COS:70;
     sin is_differentiable_in exp_R.x by SIN_COS:69;
     hence sin*exp_R is_differentiable_in x by A3,FDIFF_2:13;
    end;then
A4:sin*exp_R is_differentiable_on Z by FDIFF_1:16,A1;
  for x st x in Z holds ((sin*exp_R)`|Z).x =exp_R.x * cos.(exp_R.x)
  proof
    let x;
    assume A5:x in Z;
A6: exp_R is_differentiable_in x by SIN_COS:70;
    sin is_differentiable_in exp_R.x by SIN_COS:69;then
    diff(sin*exp_R,x)
   = diff(sin,exp_R.x)*diff(exp_R,x) by A6,FDIFF_2:13
  .=cos.(exp_R.x)*diff(exp_R,x) by SIN_COS:69
  .=exp_R.x*cos.(exp_R.x) by SIN_COS:70;
   hence thesis by FDIFF_1:def 8,A4,A5;
  end;
  hence thesis by A2,FDIFF_1:16,A1;
 end;

::f.x=cos(exp_R.x)
theorem
 Z c= dom (cos*exp_R) implies cos*exp_R is_differentiable_on Z &
 for x st x in Z holds ((cos*exp_R)`|Z).x = -exp_R.x * sin.(exp_R.x)
 proof
   assume A1:Z c= dom (cos*exp_R);
A2:for x st x in Z holds cos*exp_R is_differentiable_in x
    proof
     let x;
     assume x in Z;
A3:  exp_R is_differentiable_in x by SIN_COS:70;
     cos is_differentiable_in exp_R.x by SIN_COS:68;
     hence cos*exp_R is_differentiable_in x by A3,FDIFF_2:13;
    end;then
A4:cos*exp_R is_differentiable_on Z by FDIFF_1:16,A1;
   for x st x in Z holds ((cos*exp_R)`|Z).x =-exp_R.x * sin.(exp_R.x)
   proof
    let x;
    assume A5:x in Z;
A6: exp_R is_differentiable_in x by SIN_COS:70;
    cos is_differentiable_in exp_R.x by SIN_COS:68;then
    diff(cos*exp_R,x)
   = diff(cos,exp_R.x)*diff(exp_R,x) by A6,FDIFF_2:13
  .=(-sin.(exp_R.x))*diff(exp_R,x) by SIN_COS:68
  .=(-sin.(exp_R.x))*exp_R.x by SIN_COS:70
  .=-exp_R.x*sin.(exp_R.x);
   hence thesis by FDIFF_1:def 8,A4,A5;
  end;
  hence thesis by A2,FDIFF_1:16,A1;
 end;

::f.x=exp_R.(cos.x)
theorem
 Z c= dom (exp_R*cos) implies exp_R*cos is_differentiable_on Z &
 for x st x in Z holds ((exp_R*cos)`|Z).x = -exp_R.(cos.x) * sin.x
 proof
    assume A1:Z c= dom (exp_R*cos);
A2:for x st x in Z holds exp_R*cos is_differentiable_in x
    proof
     let x;
     assume x in Z;
A3:  cos is_differentiable_in x by SIN_COS:68;
     exp_R is_differentiable_in cos.x by SIN_COS:70;
     hence exp_R*cos is_differentiable_in x by A3,FDIFF_2:13;
    end;then
A4:exp_R*cos is_differentiable_on Z by FDIFF_1:16,A1;
   for x st x in Z holds ((exp_R*cos)`|Z).x =-exp_R.(cos.x) * sin.x
   proof
    let x;
    assume A5:x in Z;
A6: cos is_differentiable_in x by SIN_COS:68;
    exp_R is_differentiable_in cos.x by SIN_COS:70;then
    diff(exp_R*cos,x)
   = diff(exp_R,cos.x)*diff(cos,x) by A6,FDIFF_2:13
  .=diff(exp_R,cos.x)*(-sin.x ) by SIN_COS:68
  .=exp_R.(cos.x)*(-sin.x ) by SIN_COS:70
  .=-exp_R.(cos.x) * sin.x;
   hence thesis by FDIFF_1:def 8,A4,A5;
  end;
  hence thesis by A2,FDIFF_1:16,A1;
 end;

::f.x=exp_R.(sin.x)
theorem
 Z c= dom (exp_R*sin) implies exp_R*sin is_differentiable_on Z &
 for x st x in Z holds ((exp_R*sin)`|Z).x = exp_R.(sin.x) * cos.x
 proof
    assume A1:Z c= dom (exp_R*sin);
A2:for x st x in Z holds exp_R*sin is_differentiable_in x
    proof
     let x;
     assume x in Z;
A3:  sin is_differentiable_in x by SIN_COS:69;
     exp_R is_differentiable_in sin.x by SIN_COS:70;
     hence exp_R*sin is_differentiable_in x by A3,FDIFF_2:13;
    end;then
A4:exp_R*sin is_differentiable_on Z by FDIFF_1:16,A1;
   for x st x in Z holds ((exp_R*sin)`|Z).x = exp_R.(sin.x) * cos.x
   proof
    let x;
    assume A5:x in Z;
A6: sin is_differentiable_in x by SIN_COS:69;
    exp_R is_differentiable_in sin.x by SIN_COS:70;then
    diff(exp_R*sin,x)
   = diff(exp_R,sin.x)*diff(sin,x) by A6,FDIFF_2:13
  .=diff(exp_R,sin.x)*(cos.x ) by SIN_COS:69
  .=exp_R.(sin.x) * cos.x by SIN_COS:70;
   hence thesis by FDIFF_1:def 8,A4,A5;
  end;
  hence thesis by A2,FDIFF_1:16,A1;
 end;

::f.x=sin.x+cos.x
theorem Th38:
 Z c= dom (sin+cos) implies sin+cos is_differentiable_on Z &
 for x st x in Z holds ((sin+cos)`|Z).x =cos.x-sin.x
 proof
    assume A1:Z c= dom (sin+cos);
A2: sin is_differentiable_on Z by SIN_COS:73,FDIFF_1:34;
A3: cos is_differentiable_on Z by SIN_COS:72,FDIFF_1:34;
   now let x;
    assume x in Z;
    hence ((sin+cos)`|Z).x =diff(sin,x) + diff(cos,x)
          by A3,A2,A1,FDIFF_1:26
    .=cos.x +diff(cos,x) by SIN_COS:69
    .=cos.x +(-sin.x) by SIN_COS:68
    .=cos.x-sin.x;
   end;
   hence thesis by A3,A2,A1,FDIFF_1:26;
 end;

::f.x=sin.x-cos.x
theorem Th39:
 Z c= dom (sin-cos) implies sin-cos is_differentiable_on Z &
 for x st x in Z holds ((sin-cos)`|Z).x =cos.x+sin.x
 proof
    assume A1:Z c= dom (sin-cos);
A2: sin is_differentiable_on Z by SIN_COS:73,FDIFF_1:34;
A3: cos is_differentiable_on Z by SIN_COS:72,FDIFF_1:34;
   now let x;
    assume x in Z;
    hence ((sin-cos)`|Z).x =diff(sin,x) - diff(cos,x)
        by A3,A2,A1,FDIFF_1:27
   .=cos.x -diff(cos,x) by SIN_COS:69
   .=cos.x -(-sin.x) by SIN_COS:68
   .=cos.x+sin.x;
   end;
   hence thesis by A3,A2,A1,FDIFF_1:27;
 end;

::f.x=exp_R.x*(sin.x-cos.x)
theorem
 Z c= dom (exp_R(#)(sin-cos)) implies exp_R(#)(sin-cos) is_differentiable_on Z
 & for x st x in Z holds ((exp_R(#)(sin-cos))`|Z).x =2 * exp_R.x * sin.x
 proof
    assume A1:Z c= dom (exp_R(#)(sin-cos));then
    Z c= dom (sin-cos) /\ dom exp_R by SEQ_1:def 5;then
A2: Z c= dom (sin-cos) by XBOOLE_1:18;then
A3: sin-cos is_differentiable_on Z &
    for x st x in Z holds ((sin-cos)`|Z).x =cos.x+sin.x by Th39;
A4: exp_R is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
    for x st x in Z holds ((exp_R(#)(sin-cos))`|Z).x =2 * exp_R.x * sin.x
    proof
      let x;
      assume A5:x in Z;
      ((exp_R(#)(sin-cos))`|Z).x
      = ((sin-cos).x)*diff(exp_R,x) + (exp_R.x)*diff((sin-cos),x)
        by A1,A3,A4,FDIFF_1:29,A5
      .=(sin.x-cos.x)*diff(exp_R,x)+ (exp_R.x)*diff((sin-cos),x)
        by A5,SEQ_1:def 4,A2
      .=(sin.x-cos.x)*(exp_R.x)+ (exp_R.x)*diff((sin-cos),x) by TAYLOR_1:16
      .=(sin.x-cos.x)*(exp_R.x)+(exp_R.x)*((sin-cos)`|Z).x
        by A5,A3,FDIFF_1:def 8
      .=(sin.x-cos.x)*(exp_R.x)+(exp_R.x)*(cos.x+sin.x) by A5,A2,Th39
      .=2 * exp_R.x * sin.x;
      hence thesis;
    end;
    hence thesis by A1,A3,FDIFF_1:29,A4;
 end;

::f.x=exp_R.x*(sin.x+cos.x)
theorem
 Z c= dom (exp_R(#)(sin+cos)) implies exp_R(#)(sin+cos) is_differentiable_on Z
 & for x st x in Z holds ((exp_R(#)(sin+cos))`|Z).x =2 * exp_R.x * cos.x
 proof
   assume A1:Z c= dom (exp_R(#)(sin+cos));then
   Z c= dom (sin+cos) /\ dom exp_R by SEQ_1:def 5;then
A2: Z c= dom (sin+cos) by XBOOLE_1:18;then
A3: sin+cos is_differentiable_on Z &
for x st x in Z holds ((sin+cos)`|Z).x =cos.x-sin.x by Th38;
A4: exp_R is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
 for x st x in Z holds ((exp_R(#)(sin+cos))`|Z).x =2 * exp_R.x * cos.x
    proof
      let x;
      assume A5:x in Z;
      ((exp_R(#)(sin+cos))`|Z).x
      = ((sin+cos).x)*diff(exp_R,x) + (exp_R.x)*diff((sin+cos),x)
        by A1,A3,A4,FDIFF_1:29,A5
      .=(sin.x+cos.x)*diff(exp_R,x)+ (exp_R.x)*diff((sin+cos),x)
        by A5,SEQ_1:def 3,A2
      .=(sin.x+cos.x)*(exp_R.x)+ (exp_R.x)*diff((sin+cos),x) by TAYLOR_1:16
      .=(sin.x+cos.x)*(exp_R.x)+(exp_R.x)*((sin+cos)`|Z).x
        by A5,A3,FDIFF_1:def 8
      .=(sin.x+cos.x)*(exp_R.x)+(exp_R.x)*(cos.x-sin.x) by A5,A2,Th38
      .=2 * exp_R.x * cos.x;
      hence thesis;
    end;
    hence thesis by A4,A1,A3,FDIFF_1:29;
 end;

::f.x=(sin.x+cos.x)/exp_R.x
theorem
 Z c= dom ((sin+cos)/exp_R) implies (sin+cos)/exp_R is_differentiable_on Z &
 for x st x in Z holds (((sin+cos)/exp_R)`|Z).x =-2*sin.x/exp_R.x
 proof
   assume Z c= dom ((sin+cos)/exp_R);then
    Z c= dom (sin+cos) /\ (dom exp_R \ (exp_R)"{0}) by RFUNCT_1:def 4;then
A1: Z c= dom (sin+cos) by XBOOLE_1:18;then
A2: sin+cos is_differentiable_on Z &
   for x st x in Z holds ((sin+cos)`|Z).x =cos.x-sin.x by Th38;
A3: exp_R is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
A4:for x st x in Z holds exp_R.x<>0 by SIN_COS:59;then
A5:(sin+cos)/exp_R is_differentiable_on Z by A3,A2,FDIFF_2:21;
    for x st x in Z holds (((sin+cos)/exp_R)`|Z).x =-2*sin.x/exp_R.x
    proof
      let x;
      assume A6:x in Z;
A7:  exp_R is_differentiable_in x by SIN_COS:70;
A8:  sin+cos is_differentiable_in x by A2,A6,FDIFF_1:16;
A9:  (sin+cos).x=sin.x+cos.x by SEQ_1:def 3,A6,A1;
A10:  exp_R.x <>0 by SIN_COS:59;then
           diff((sin+cos)/exp_R,x)
         =(diff(sin+cos,x) * exp_R.x - diff(exp_R,x) *(sin+cos).x)/(exp_R.x)^2
           by FDIFF_2:14,A7,A8
        .=(((sin+cos)`|Z).x* exp_R.x - diff(exp_R,x) *(sin+cos).x)/(exp_R.x)^2
           by A2,A6,FDIFF_1:def 8
        .=((cos.x-sin.x)* exp_R.x - diff(exp_R,x) *(sin+cos).x)/(exp_R.x)^2
           by A1,Th38,A6
        .=((cos.x-sin.x)* exp_R.x - exp_R.x*(sin.x+cos.x))/(exp_R.x)^2
           by SIN_COS:70,A9
        .=((-2*sin.x)*exp_R.x)/((exp_R.x)*(exp_R.x))
        .=(-2*sin.x)*(exp_R.x/((exp_R.x)*(exp_R.x))) by XCMPLX_1:75
        .=(-2*sin.x)*((exp_R.x)/(exp_R.x)/(exp_R.x)) by XCMPLX_1:79
        .=(-2*sin.x)*(1/exp_R.x) by A10,XCMPLX_1:60
        .=(-(2*sin.x))/exp_R.x by XCMPLX_1:100
        .=-2*sin.x/exp_R.x by XCMPLX_1:188;
      hence thesis by A5,A6,FDIFF_1:def 8;
    end;
    hence thesis by A4,A3,A2,FDIFF_2:21;
 end;

::f.x=(sin.x-cos.x)/exp_R.x
theorem
 Z c= dom ((sin-cos)/exp_R) implies (sin-cos)/exp_R is_differentiable_on Z &
 for x st x in Z holds (((sin-cos)/exp_R)`|Z).x =2*cos.x/exp_R.x
 proof
    assume Z c= dom ((sin-cos)/exp_R);then
    Z c= dom (sin-cos) /\ (dom exp_R \ (exp_R)"{0}) by RFUNCT_1:def 4;then
A1: Z c= dom (sin-cos) by XBOOLE_1:18;then
A2: sin-cos is_differentiable_on Z &
   for x st x in Z holds ((sin-cos)`|Z).x =cos.x+sin.x by Th39;
A3: exp_R is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
A4:for x st x in Z holds exp_R.x<>0 by SIN_COS:59;then
A5:(sin-cos)/exp_R is_differentiable_on Z by A3,A2,FDIFF_2:21;
for x st x in Z holds (((sin-cos)/exp_R)`|Z).x =2*cos.x/exp_R.x
    proof
      let x;
      assume A6:x in Z;
A7:  exp_R is_differentiable_in x by SIN_COS:70;
A8:  sin-cos is_differentiable_in x by A2,A6,FDIFF_1:16;
A9:  (sin-cos).x=sin.x-cos.x by SEQ_1:def 4,A6,A1;
A10:  exp_R.x <>0 by SIN_COS:59;then
      diff((sin-cos)/exp_R,x)
        =(diff(sin-cos,x) * exp_R.x - diff(exp_R,x) *(sin-cos).x)/(exp_R.x)^2
           by FDIFF_2:14,A7,A8
        .=(((sin-cos)`|Z).x* exp_R.x - diff(exp_R,x) *(sin-cos).x)/(exp_R.x)^2
           by A2,A6,FDIFF_1:def 8
        .=((cos.x+sin.x)* exp_R.x - diff(exp_R,x) *(sin-cos).x)/(exp_R.x)^2
           by A1,Th39,A6
        .=((cos.x+sin.x)* exp_R.x - exp_R.x*(sin.x-cos.x))/(exp_R.x)^2
           by SIN_COS:70,A9
        .=((2*cos.x)*exp_R.x)/((exp_R.x)*(exp_R.x))
        .=(2*cos.x)*(exp_R.x/((exp_R.x)*(exp_R.x))) by XCMPLX_1:75
        .=(2*cos.x)*((exp_R.x)/(exp_R.x)/(exp_R.x)) by XCMPLX_1:79
        .=(2*cos.x)*(1/exp_R.x) by A10,XCMPLX_1:60
        .=2*cos.x/exp_R.x by XCMPLX_1:100;
      hence thesis by A5,A6,FDIFF_1:def 8;
    end;
    hence thesis by A4,A3,A2,FDIFF_2:21;
 end;

::f.x=exp_R.x*sin.x
theorem
 Z c= dom (exp_R(#)sin) implies exp_R(#)sin is_differentiable_on Z &
 for x st x in Z holds ((exp_R(#)sin)`|Z).x = exp_R.x*(sin.x+cos.x)
 proof
    assume that A1:Z c= dom (exp_R(#)sin);
A2: sin is_differentiable_on Z by SIN_COS:73,FDIFF_1:34;
A3: exp_R is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
    now let x;
      assume x in Z;
      hence ((exp_R(#)sin)`|Z).x = sin.x * diff(exp_R,x) +
        exp_R.x * diff(sin,x) by A1,A2,A3,FDIFF_1:29
      .=sin.x*exp_R.x+ (exp_R.x)*diff(sin,x) by TAYLOR_1:16
      .=sin.x*exp_R.x+ exp_R.x*cos.x by SIN_COS:69
      .=exp_R.x*(sin.x+cos.x);
    end;
    hence thesis by A1,A2,FDIFF_1:29,A3;
  end;

::f.x=exp_R.x*cos.x
theorem
 Z c= dom (exp_R(#)cos) implies exp_R(#)cos is_differentiable_on Z &
 for x st x in Z holds ((exp_R(#)cos)`|Z).x = exp_R.x*(cos.x-sin.x)
 proof
    assume A1:Z c= dom (exp_R(#)cos);
A2: cos is_differentiable_on Z by SIN_COS:72,FDIFF_1:34;
A3: exp_R is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
    now let x;
      assume x in Z;
      hence ((exp_R(#)cos)`|Z).x =
        cos.x * diff(exp_R,x) + exp_R.x * diff(cos,x) by A1,A2,A3,FDIFF_1:29
      .=cos.x*exp_R.x+ (exp_R.x)*diff(cos,x) by TAYLOR_1:16
      .=cos.x*exp_R.x+ exp_R.x*(-sin.x) by SIN_COS:68
      .=exp_R.x*(cos.x-sin.x);
    end;
    hence thesis by A1,A2,FDIFF_1:29,A3;
  end;

:: f.x=tan.x
theorem Th46:
 cos.x<>0 implies
 sin/cos is_differentiable_in x & diff(sin/cos, x)=1/(cos.x)^2
 proof
A1:sin is_differentiable_in x by SIN_COS:69;
A2:cos is_differentiable_in x by SIN_COS:68;
   assume A3:cos.x<>0;
   then diff(sin/cos,x)
   =(diff(sin,x) * cos.x - diff(cos,x)*sin.x)/(cos.x)^2 by A2,A1,FDIFF_2:14
  .=((cos.x)*cos.x - diff(cos,x)*sin.x)/(cos.x)^2 by SIN_COS:69
  .=((cos.x)*(cos.x)-(-sin.x)*(sin.x))/(cos.x)^2 by SIN_COS:68
  .=((cos.x)*(cos.x) + (sin.x)*(sin.x))/(cos.x)^2
  .=1/(cos.x)^2 by SIN_COS:31;
   hence thesis by A3,A1,A2,FDIFF_2:14;
  end;

::f.x=cot.x
theorem Th47:
 sin.x<>0 implies
 cos/sin is_differentiable_in x & diff(cos/sin, x)=-1/(sin.x)^2
 proof
A1: sin is_differentiable_in x by SIN_COS:69;
A2: cos is_differentiable_in x by SIN_COS:68;
   assume A3:sin.x<>0;then
   diff(cos/sin,x) = (diff(cos,x) * sin.x - diff(sin,x)*cos.x)/(sin.x)^2
     by A2,A1,FDIFF_2:14
  .=((-sin.x)*sin.x - diff(sin,x)*cos.x)/(sin.x)^2 by SIN_COS:68
  .=(-(sin.x)*(sin.x)-(cos.x)*(cos.x))/(sin.x)^2 by SIN_COS:69
  .=(-((cos.x)^2 + (sin.x)*(sin.x)))/(sin.x)^2
  .=-((cos.x)^2 + (sin.x)^2)/(sin.x)^2by XCMPLX_1:188
  .=-1/(sin.x)^2 by SIN_COS:31;
   hence thesis by A3,A1,A2,FDIFF_2:14;
  end;

::f.x=(tan.x) #Z 2
theorem
 Z c= dom (( #Z 2)*(sin/cos)) & (for x st x in Z holds cos.x<>0)
 implies ( #Z 2)*(sin/cos) is_differentiable_on Z &
 for x st x in Z holds ((( #Z 2)*(sin/cos))`|Z).x =2*sin.x/((cos.x) #Z 3)
 proof
    assume that A1:Z c= dom (( #Z 2)*(sin/cos)) and
A2: (for x st x in Z holds cos.x<>0);
    for y st y in Z holds y in dom (sin/cos) by FUNCT_1:21,A1;then
A3: Z c= dom (sin/cos) by TARSKI:def 3;
A4:for x st x in Z holds ( #Z 2)*(sin/cos) is_differentiable_in x
    proof
      let x;
      assume x in Z;then
      cos.x<>0 by A2;then
      sin/cos is_differentiable_in x by Th46;
      hence ( #Z 2)*(sin/cos) is_differentiable_in x by TAYLOR_1:3;
    end;then
A5: ( #Z 2)*(sin/cos) is_differentiable_on Z by FDIFF_1:16,A1;
   for x st x in Z holds ((( #Z 2)*(sin/cos))`|Z).x =2*sin.x/((cos.x) #Z 3)
   proof
     let x;
     assume A6:x in Z;then
A7:  cos.x<>0 by A2;then
A8:  sin/cos is_differentiable_in x by Th46;
A9:  (sin/cos).x = sin.x*(cos.x)" by A3,A6,RFUNCT_1:def 4
   .=sin.x*(1/cos.x) by XCMPLX_1:217
   .=sin.x/cos.x by XCMPLX_1:100;
     ((( #Z 2)*(sin/cos))`|Z).x=diff(( #Z 2)*(sin/cos),x)
      by FDIFF_1:def 8,A5,A6
    .=2*(((sin/cos).x) #Z (2-1)) * diff(sin/cos,x) by TAYLOR_1:3,A8
    .=2*(((sin/cos).x) #Z (2-1)) *(1/(cos.x)^2) by Th46,A7
    .=2*(((sin/cos).x) #Z 1) /(cos.x)^2 by XCMPLX_1:100
    .=2*(sin.x/cos.x)/(cos.x)^2 by A9,PREPOWER:45
    .=2*sin.x/cos.x/(cos.x)^2 by XCMPLX_1:75
    .=2*sin.x/(cos.x*(cos.x)^2) by XCMPLX_1:79
    .=2*sin.x/(cos.x*((cos.x) #Z 2)) by Th1
    .=2*sin.x/(((cos.x) #Z 1) *((cos.x) #Z 2)) by PREPOWER:45
    .=2*sin.x/((cos.x) #Z (1+2)) by A7,PREPOWER:54
    .=2*sin.x/((cos.x) #Z 3);
    hence thesis;
  end;
  hence thesis by A4,FDIFF_1:16,A1;
 end;

::f.x=(cot.x) #Z 2
theorem
 Z c= dom (( #Z 2)*(cos/sin)) & (for x st x in Z holds sin.x<>0)
 implies ( #Z 2)*(cos/sin) is_differentiable_on Z &
 for x st x in Z holds ((( #Z 2)*(cos/sin))`|Z).x =-2*cos.x/((sin.x) #Z 3)
 proof
    assume that A1:Z c= dom (( #Z 2)*(cos/sin)) and
A2: for x st x in Z holds sin.x<>0;
    for y st y in Z holds y in dom (cos/sin) by FUNCT_1:21,A1;then
A3: Z c= dom (cos/sin) by TARSKI:def 3;
A4:for x st x in Z holds ( #Z 2)*(cos/sin) is_differentiable_in x
    proof
      let x;
      assume x in Z;then
      sin.x<>0 by A2;then
      cos/sin is_differentiable_in x by Th47;
      hence ( #Z 2)*(cos/sin) is_differentiable_in x by TAYLOR_1:3;
    end;then
A5: ( #Z 2)*(cos/sin) is_differentiable_on Z by FDIFF_1:16,A1;
for x st x in Z holds ((( #Z 2)*(cos/sin))`|Z).x=-2*cos.x/((sin.x) #Z 3)
   proof
     let x;
     assume A6:x in Z;then
A7:  sin.x<>0 by A2;then
A8:  cos/sin is_differentiable_in x by Th47;
A9:  (cos/sin).x = cos.x*(sin.x)" by A3,A6,RFUNCT_1:def 4
   .=cos.x*(1/sin.x) by XCMPLX_1:217
   .=cos.x/sin.x by XCMPLX_1:100;
     ((( #Z 2)*(cos/sin))`|Z).x=diff(( #Z 2)*(cos/sin),x)
      by FDIFF_1:def 8,A5,A6
    .=2*(((cos/sin).x) #Z (2-1)) * diff(cos/sin,x) by TAYLOR_1:3,A8
    .=2*(((cos/sin).x) #Z (2-1)) *(-1/(sin.x)^2) by Th47,A7
    .=-2*(((cos/sin).x) #Z (2-1)) *(1/(sin.x)^2)
    .=-2*(((cos/sin).x) #Z 1) /(sin.x)^2 by XCMPLX_1:100
    .=-2*(cos.x/sin.x)/(sin.x)^2 by A9,PREPOWER:45
    .=-2*cos.x/sin.x/(sin.x)^2 by XCMPLX_1:75
    .=-2*cos.x/(sin.x*(sin.x)^2) by XCMPLX_1:79
    .=-2*cos.x/(sin.x*((sin.x) #Z 2)) by Th1
    .=-2*cos.x/(((sin.x) #Z 1) *((sin.x) #Z 2)) by PREPOWER:45
    .=-2*cos.x/((sin.x) #Z (1+2)) by A7,PREPOWER:54
    .=-2*cos.x/((sin.x) #Z 3);
    hence thesis;
  end;
  hence thesis by A4,FDIFF_1:16,A1;
end;

::f.x=tan.(x/2)
theorem
 Z c= dom ((sin/cos)*f) & (for x st x in Z holds f.x=x/2 & cos.(f.x)<>0)
 implies (sin/cos)*f is_differentiable_on Z &
 for x st x in Z holds (((sin/cos)*f)`|Z).x = 1/(1+cos.x)
 proof
    assume A1:Z c= dom ((sin/cos)*f) &
    (for x st x in Z holds f.x=x/2 & cos.(f.x)<>0);
A2: for x st x in Z holds f.x=(1/2)*x+0
    proof
      let x;assume x in Z;then
      f.x=x/2 by A1;
      hence thesis;
    end;
    for y st y in Z holds y in dom f by FUNCT_1:21,A1;then
A3:Z c= dom f by TARSKI:def 3;then
A4: f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = 1/2 by A2,FDIFF_1:31;
A5: for x st x in Z holds (sin/cos)*f is_differentiable_in x
    proof
      let x;
      assume A6:x in Z;then
A7:   f is_differentiable_in x by FDIFF_1:16,A4;
      cos.(f.x)<>0 by A6,A1;then
      (sin/cos) is_differentiable_in f.x by Th46;
      hence (sin/cos)*f is_differentiable_in x by A7,FDIFF_2:13;
    end;then
A8: (sin/cos)*f is_differentiable_on Z by FDIFF_1:16,A1;
   for x st x in Z holds (((sin/cos)*f)`|Z).x = 1/(1+cos.x)
   proof
    let x;
    assume A9:x in Z;then
A10: f is_differentiable_in x by FDIFF_1:16,A4;
A11: cos.(f.x)<>0 by A9,A1;then
    (sin/cos) is_differentiable_in f.x by Th46;then
    diff((sin/cos)*f,x) = diff(sin/cos, f.x)*diff(f,x) by A10,FDIFF_2:13
   .=(1/(cos.(f.x))^2) * diff(f,x) by Th46,A11
   .=diff(f,x)/(cos.(f.x))^2 by XCMPLX_1:100
   .=diff(f,x)/(cos.(x/2))^2 by A9,A1
   .=(f`|Z).x/(cos.(x/2))^2 by A4,FDIFF_1:def 8,A9
   .=1/2/(cos.(x/2))^2 by A3,A2,FDIFF_1:31,A9
   .=1/(2*(cos.(x/2))^2) by XCMPLX_1:79
   .=1/(1+cos.x) by Lm1;
    hence thesis by FDIFF_1:def 8,A8,A9;
  end;
  hence thesis by FDIFF_1:16,A1,A5;
 end;

::f.x=cot.(x/2)
theorem
 Z c= dom ((cos/sin)*f) & (for x st x in Z holds f.x=x/2 & sin.(f.x)<>0)
 implies (cos/sin)*f is_differentiable_on Z &
 for x st x in Z holds (((cos/sin)*f)`|Z).x = -1/(1-cos.x)
 proof
    assume A1:Z c= dom ((cos/sin)*f) &
    (for x st x in Z holds f.x=x/2 & sin.(f.x)<>0);
A2: for x st x in Z holds f.x=(1/2)*x+0
   proof
    let x;
    assume x in Z;then
    f.x=x/2 by A1;
    hence thesis;
   end;
    for y st y in Z holds y in dom f by FUNCT_1:21,A1;then
A3:Z c= dom f by TARSKI:def 3;then
A4: f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = 1/2 by A2,FDIFF_1:31;
A5: for x st x in Z holds (cos/sin)*f is_differentiable_in x
    proof
      let x;
      assume A6:x in Z;then
A7:   f is_differentiable_in x by FDIFF_1:16,A4;
      sin.(f.x)<>0 by A6,A1;then
      cos/sin is_differentiable_in f.x by Th47;
      hence (cos/sin)*f is_differentiable_in x by A7,FDIFF_2:13;
    end;then
A8: (cos/sin)*f is_differentiable_on Z by FDIFF_1:16,A1;
for x st x in Z holds (((cos/sin)*f)`|Z).x = -1/(1-cos.x)
   proof
    let x;
    assume A9:x in Z;then
A10: f is_differentiable_in x by FDIFF_1:16,A4;
A11: sin.(f.x)<>0 by A9,A1;then
    cos/sin is_differentiable_in f.x by Th47;then
    diff((cos/sin)*f,x) = diff(cos/sin, f.x)*diff(f,x) by A10,FDIFF_2:13
   .=(-1/(sin.(f.x))^2) * diff(f,x) by Th47,A11
   .=-(1/(sin.(f.x))^2) * diff(f,x)
   .=-diff(f,x)/(sin.(f.x))^2 by XCMPLX_1:100
   .=-diff(f,x)/(sin.(x/2))^2 by A9,A1
   .=-(f`|Z).x/(sin.(x/2))^2 by A4,FDIFF_1:def 8,A9
   .=-1/2/(sin.(x/2))^2 by A3,A2,FDIFF_1:31,A9
   .=-1/(2*(sin.(x/2))^2) by XCMPLX_1:79
   .=-1/(1-cos.x) by Lm2;
   hence thesis by FDIFF_1:def 8,A8,A9;
  end;
  hence thesis by FDIFF_1:16,A1,A5;
 end;
