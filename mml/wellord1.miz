:: The Well Ordering Relations
::  by Grzegorz Bancerek
::
:: Received April 4, 1989
:: Copyright (c) 1990 Association of Mizar Users
 
environ

 vocabularies RELAT_1, RELAT_2, XBOOLE_0, SUBSET_1, TARSKI, FUNCT_1, ZFMISC_1,
      WELLORD1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, RELAT_1, RELAT_2, FUNCT_1;
 constructors TARSKI, SUBSET_1, FUNCT_1, RELAT_2;
 registrations FUNCT_1, RELAT_1;
 requirements SUBSET, BOOLE;
 definitions TARSKI, XBOOLE_0, FUNCT_1, RELAT_1;
 theorems TARSKI, XBOOLE_0, FUNCT_1, RELAT_1, RELAT_2, ZFMISC_1, XBOOLE_1;
 schemes XBOOLE_0, FUNCT_1;

begin

reserve a,b,c,d,x,y,z,X,Y,Z for set;
reserve R,S,T for Relation;

Lm1: R is reflexive iff for x st x in field R holds [x,x] in R
proof
  R is reflexive iff R is_reflexive_in field R by RELAT_2:def 9;
  hence thesis by RELAT_2:def 1;
end;

Lm2: R is transitive iff for x,y,z st [x,y] in R & [y,z] in R holds [x,z] in R
proof
  thus R is transitive implies for x,y,z st [x,y] in R & [y,z] in R holds [x,z
  ] in R
  proof
    assume R is transitive;
    then
A1: R is_transitive_in field R by RELAT_2:def 16;
    let x,y,z;
    assume that
A2: [x,y] in R and
A3: [y,z] in R;
A4: z in field R by A3,RELAT_1:30;
    x in field R & y in field R by A2,RELAT_1:30;
    hence thesis by A1,A2,A3,A4,RELAT_2:def 8;
  end;
  assume for x,y,z st [x,y] in R & [y,z] in R holds [x,z] in R;
  then
  for x,y,z st x in field R & y in field R & z in field R & [x,y] in R & [
  y,z] in R holds [x,z] in R;
  then R is_transitive_in field R by RELAT_2:def 8;
  hence thesis by RELAT_2:def 16;
end;

Lm3: R is antisymmetric iff for x,y st [x,y] in R & [y,x] in R holds x = y
proof
  thus R is antisymmetric implies for x,y st [x,y] in R & [y,x] in R holds x =
  y
  proof
    assume R is antisymmetric;
    then
A1: R is_antisymmetric_in field R by RELAT_2:def 12;
    let x,y;
    assume that
A2: [x,y] in R and
A3: [y,x] in R;
    x in field R & y in field R by A2,RELAT_1:30;
    hence thesis by A1,A2,A3,RELAT_2:def 4;
  end;
  assume for x,y st [x,y] in R & [y,x] in R holds x = y;
  then
  for x,y st x in field R & y in field R & [x,y] in R & [y,x] in R holds x = y;
  then R is_antisymmetric_in field R by RELAT_2:def 4;
  hence thesis by RELAT_2:def 12;
end;

Lm4: R is connected iff for x,y st x in field R & y in field R & x <> y holds
[x,y] in R or [y,x] in R
proof
  R is connected iff R is_connected_in field R by RELAT_2:def 14;
  hence thesis by RELAT_2:def 6;
end;

definition
  let R,a;
  defpred P[set] means $1 <> a & [$1,a] in R;
  func R-Seg(a) -> set means
  :Def1:
  x in it iff x <> a & [x,a] in R;
  existence
  proof
    consider X such that
A1: x in X iff x in field R & P[x] from XBOOLE_0:sch 1;
    take X;
    let x;
    thus x in X implies x <> a & [x,a] in R by A1;
    assume that
A2: x <> a and
A3: [x,a] in R;
    x in field R by A3,RELAT_1:30;
    hence thesis by A1,A2,A3;
  end;
  uniqueness
  proof
    let X1,X2 be set such that
A4: for b holds b in X1 iff P[b] and
A5: for b holds b in X2 iff P[b];
    thus thesis from XBOOLE_0:sch 2(A4,A5);
  end;
end;

canceled;

theorem Th2:
  x in field R or R-Seg(x) = {}
proof
  assume
A1: not x in field R;
  consider y being Element of R-Seg(x);
  assume R-Seg(x) <> {};
  then [y,x] in R by Def1;
  hence contradiction by A1,RELAT_1:30;
end;

definition
  let R;
  attr R is well_founded means
  :Def2:
  for Y st Y c= field R & Y <> {} ex a st a in Y & R-Seg a misses Y;
  let X;
  pred R is_well_founded_in X means
  :Def3:
  for Y st Y c= X & Y <> {} ex a st a in Y & R-Seg a misses Y;
end;

canceled 2;

theorem Th5:
  R is well_founded iff R is_well_founded_in field R
proof
  thus R is well_founded implies R is_well_founded_in field R
  proof
    assume for Y st Y c= field R & Y <> {} ex a st a in Y & R-Seg(a) misses Y;
    hence for Y st Y c= field R & Y <> {} ex a st a in Y & R-Seg(a) misses Y;
  end;
  assume for Y st Y c= field R & Y <> {} ex a st a in Y & R-Seg(a) misses Y;
  hence for Y st Y c= field R & Y <> {} ex a st a in Y & R-Seg(a) misses Y;
end;

definition
  let R;
  attr R is well-ordering means
  :Def4:
  R is reflexive & R is transitive & R is
  antisymmetric & R is connected & R is well_founded;
  let X;
  pred R well_orders X means
  :Def5:
  R is_reflexive_in X & R is_transitive_in X
  & R is_antisymmetric_in X & R is_connected_in X & R is_well_founded_in X;
end;

canceled 2;

theorem
  R well_orders field R iff R is well-ordering
proof
  thus R well_orders field R implies R is well-ordering
  proof
    assume R is_reflexive_in field R & R is_transitive_in field R & R
is_antisymmetric_in field R & R is_connected_in field R & R is_well_founded_in
    field R;
    hence R is reflexive & R is transitive & R is antisymmetric & R is
    connected & R is well_founded by Th5,RELAT_2:def 9,def 12,def 14,def 16;
  end;
  assume R is reflexive & R is transitive & R is antisymmetric & R is
  connected & R is well_founded;
  hence R is_reflexive_in field R & R is_transitive_in field R & R
is_antisymmetric_in field R & R is_connected_in field R & R is_well_founded_in
  field R by Th5,RELAT_2:def 9,def 12,def 14,def 16;
end;

theorem
  R well_orders X implies for Y st Y c= X & Y <> {} ex a st a in Y & for
  b st b in Y holds [a,b] in R
proof
  assume
A1: R well_orders X;
  then
A2: R is_reflexive_in X by Def5;
A3: R is_connected_in X by A1,Def5;
  let Y;
  assume that
A4: Y c= X and
A5: Y <> {};
  R is_well_founded_in X by A1,Def5;
  then consider a such that
A6: a in Y and
A7: R-Seg(a) misses Y by A4,A5,Def3;
  take a;
  thus a in Y by A6;
  let b;
  assume
A8: b in Y;
  then not b in R-Seg(a) by A7,XBOOLE_0:3;
  then a = b or not [b,a] in R by Def1;
  then a <> b implies [a,b] in R by A3,A4,A6,A8,RELAT_2:def 6;
  hence thesis by A2,A4,A6,RELAT_2:def 1;
end;

theorem Th10:
  R is well-ordering implies for Y st Y c= field R & Y <> {} ex a
  st a in Y & for b st b in Y holds [a,b] in R
proof
  assume
A1: R is well-ordering;
  then
A2: R is reflexive by Def4;
A3: R is connected by A1,Def4;
  let Y;
  assume that
A4: Y c= field R and
A5: Y <> {};
  R is well_founded by A1,Def4;
  then consider a such that
A6: a in Y and
A7: R-Seg(a) misses Y by A4,A5,Def2;
  take a;
  thus a in Y by A6;
  let b;
  assume
A8: b in Y;
  then not b in R-Seg(a) by A7,XBOOLE_0:3;
  then a = b or not [b,a] in R by Def1;
  then a <> b implies [a,b] in R by A3,A4,A6,A8,Lm4;
  hence thesis by A2,A4,A6,Lm1;
end;

theorem
  for R st R is well-ordering & field R <> {} ex a st a in field R & for
  b st b in field R holds [a,b] in R by Th10;

theorem
  for R st R is well-ordering for a st a in field R holds (for b st b in
field R holds [b,a] in R) or ex b st b in field R & [a,b] in R & for c st c in
  field R & [a,c] in R holds c = a or [b,c] in R
proof
  let R;
  assume
A1: R is well-ordering;
  then
A2: R is connected by Def4;
A3: R is antisymmetric by A1,Def4;
  let a such that
A4: a in field R;
  defpred P[set] means not [$1,a] in R;
  given b such that
A5: b in field R & not [b,a] in R;
  consider Z such that
A6: c in Z iff c in field R & P[c] from XBOOLE_0:sch 1;
  for b holds b in Z implies b in field R by A6;
  then
A7: Z c= field R by TARSKI:def 3;
  Z <> {} by A5,A6;
  then consider d such that
A8: d in Z and
A9: for c st c in Z holds [d,c] in R by A1,A7,Th10;
  take d;
  thus
A10: d in field R by A6,A8;
A11: not [d,a] in R by A6,A8;
  then a <> d by A8,A9;
  hence [a,d] in R by A2,A4,A10,A11,Lm4;
  let c;
  assume that
A12: c in field R and
A13: [a,c] in R;
  assume c <> a;
  then not [c,a] in R by A3,A13,Lm3;
  then c in Z by A6,A12;
  hence thesis by A9;
end;

reserve F,G for Function;

theorem Th13:
  R-Seg(a) c= field R
proof
  let b;
  assume b in R-Seg(a);
  then [b,a] in R by Def1;
  hence thesis by RELAT_1:30;
end;

definition
  let R,Y;
  func R |_2 Y -> Relation equals
  R /\ [:Y,Y:];
  coherence;
end;

canceled 3;

theorem Th17:
  R |_2 X = X|R|X
proof
  let x,y;
  thus [x,y] in R |_2 X implies [x,y] in X|R|X
  proof
    assume
A1: [x,y] in R |_2 X;
    then
A2: [x,y] in [:X,X:] by XBOOLE_0:def 4;
    then
A3: x in X by ZFMISC_1:106;
A4: y in X by A2,ZFMISC_1:106;
    [x,y] in R by A1,XBOOLE_0:def 4;
    then [x,y] in X|R by A4,RELAT_1:def 12;
    hence thesis by A3,RELAT_1:def 11;
  end;
  assume
A5: [x,y] in X|R|X;
  then
A6: [x,y] in X|R by RELAT_1:def 11;
  then
A7: [x,y] in R by RELAT_1:def 12;
A8: y in X by A6,RELAT_1:def 12;
  x in X by A5,RELAT_1:def 11;
  then [x,y] in [:X,X:] by A8,ZFMISC_1:106;
  hence thesis by A7,XBOOLE_0:def 4;
end;

theorem Th18:
  R |_2 X = X|(R|X)
proof
  thus R |_2 X = X|R|X by Th17
    .= X|(R|X) by RELAT_1:140;
end;

Lm5: dom(X|R) c= dom R
proof
  let x;
  assume x in dom(X|R);
  then consider y such that
A1: [x,y] in X|R by RELAT_1:def 4;
  [x,y] in R by A1,RELAT_1:def 12;
  hence thesis by RELAT_1:def 4;
end;

theorem Th19:
  x in field(R |_2 X) implies x in field R & x in X
proof
A1: dom(X|R|X) = dom(X|R) /\ X & rng(X|(R|X)) = rng(R|X) /\ X by RELAT_1:90,
  119;
  assume x in field(R |_2 X);
  then
A2: x in dom(R |_2 X) or x in rng(R |_2 X) by XBOOLE_0:def 3;
A3: dom(X|R) c= dom R & rng(R|X) c= rng R by Lm5,RELAT_1:99;
  R |_2 X = X|R|X & R |_2 X = X|(R|X) by Th17,Th18;
  then x in dom(X|R) & x in X or x in rng(R|X) & x in X by A2,A1,XBOOLE_0:
  def 4;
  hence thesis by A3,XBOOLE_0:def 3;
end;

theorem Th20:
  field(R |_2 X) c= field R & field(R |_2 X) c= X
proof
  ( for x st x in field(R |_2 X) holds x in field R)& for x st x in field(
  R |_2 X) holds x in X by Th19;
  hence thesis by TARSKI:def 3;
end;

theorem Th21:
  (R |_2 X)-Seg(a) c= R-Seg(a)
proof
  let x;
  assume
A1: x in (R |_2 X)-Seg(a);
  then [x,a] in R |_2 X by Def1;
  then
A2: [x,a] in R by XBOOLE_0:def 4;
  x <> a by A1,Def1;
  hence thesis by A2,Def1;
end;

theorem Th22:
  R is reflexive implies R |_2 X is reflexive
proof
  assume
A1: R is reflexive;
  now
    let a;
    assume
A2: a in field(R |_2 X);
    then a in X by Th19;
    then
A3: [a,a] in [:X,X:] by ZFMISC_1:106;
    a in field R by A2,Th19;
    then [a,a] in R by A1,Lm1;
    hence [a,a] in R |_2 X by A3,XBOOLE_0:def 4;
  end;
  hence thesis by Lm1;
end;

theorem Th23:
  R is connected implies R |_2 Y is connected
proof
  assume
A1: R is connected;
  now
    let a,b;
    assume that
A2: a in field(R |_2 Y) & b in field(R |_2 Y) and
A3: a <> b;
    a in Y & b in Y by A2,Th19;
    then
A4: [a,b] in [:Y,Y:] & [b,a] in [:Y,Y:] by ZFMISC_1:106;
    a in field R & b in field R by A2,Th19;
    then [a,b] in R or [b,a] in R by A1,A3,Lm4;
    hence [a,b] in R |_2 Y or [b,a] in R |_2 Y by A4,XBOOLE_0:def 4;
  end;
  hence thesis by Lm4;
end;

theorem Th24:
  R is transitive implies R |_2 Y is transitive
proof
  assume
A1: R is transitive;
  now
    let a,b,c;
    assume that
A2: [a,b] in R |_2 Y and
A3: [b,c] in R |_2 Y;
    [a,b] in R & [b,c] in R by A2,A3,XBOOLE_0:def 4;
    then
A4: [a,c] in R by A1,Lm2;
    [b,c] in [:Y,Y:] by A3,XBOOLE_0:def 4;
    then
A5: c in Y by ZFMISC_1:106;
    [a,b] in [:Y,Y:] by A2,XBOOLE_0:def 4;
    then a in Y by ZFMISC_1:106;
    then [a,c] in [:Y,Y:] by A5,ZFMISC_1:106;
    hence [a,c] in R |_2 Y by A4,XBOOLE_0:def 4;
  end;
  hence thesis by Lm2;
end;

theorem Th25:
  R is antisymmetric implies R |_2 Y is antisymmetric
proof
  assume
A1: R is antisymmetric;
  now
    let a,b;
    assume [a,b] in R |_2 Y & [b,a] in R |_2 Y;
    then [a,b] in R & [b,a] in R by XBOOLE_0:def 4;
    hence a = b by A1,Lm3;
  end;
  hence thesis by Lm3;
end;

theorem Th26:
  (R |_2 X) |_2 Y = R |_2 (X /\ Y)
proof
  thus (R |_2 X) |_2 Y = R /\ ([:X,X:] /\ [:Y,Y:]) by XBOOLE_1:16
    .= R |_2 (X /\ Y) by ZFMISC_1:123;
end;

theorem
  (R |_2 X) |_2 Y = (R |_2 Y) |_2 X
proof
  thus (R |_2 X) |_2 Y = R |_2 (Y /\ X) by Th26
    .= (R |_2 Y) |_2 X by Th26;
end;

theorem
  (R |_2 Y) |_2 Y = R |_2 Y
proof
  let a,b;
  thus [a,b] in (R |_2 Y) |_2 Y implies [a,b] in R |_2 Y by XBOOLE_0:def 4;
  assume
A1: [a,b] in R |_2 Y;
  then [a,b] in [:Y,Y:] by XBOOLE_0:def 4;
  hence thesis by A1,XBOOLE_0:def 4;
end;

theorem Th29:
  Z c= Y implies (R |_2 Y) |_2 Z = R |_2 Z
proof
  assume
A1: Z c= Y;
  let a,b;
  thus [a,b] in (R |_2 Y) |_2 Z implies [a,b] in R |_2 Z
  proof
    assume
A2: [a,b] in (R |_2 Y) |_2 Z;
    then [a,b] in R |_2 Y by XBOOLE_0:def 4;
    then
A3: [a,b] in R by XBOOLE_0:def 4;
    [a,b] in [:Z,Z:] by A2,XBOOLE_0:def 4;
    hence thesis by A3,XBOOLE_0:def 4;
  end;
  assume
A4: [a,b] in R |_2 Z;
  then
A5: [a,b] in R by XBOOLE_0:def 4;
A6: [a,b] in [:Z,Z:] by A4,XBOOLE_0:def 4;
  then a in Z & b in Z by ZFMISC_1:106;
  then [a,b] in [:Y,Y:] by A1,ZFMISC_1:106;
  then [a,b] in R |_2 Y by A5,XBOOLE_0:def 4;
  hence thesis by A6,XBOOLE_0:def 4;
end;

theorem Th30:
  R |_2 field R = R
proof
  let x,y;
  thus [x,y] in R |_2 field R implies [x,y] in R by XBOOLE_0:def 4;
  assume
A1: [x,y] in R;
  then x in field R & y in field R by RELAT_1:30;
  then [x,y] in [:field R,field R:] by ZFMISC_1:106;
  hence thesis by A1,XBOOLE_0:def 4;
end;

theorem Th31:
  R is well_founded implies R |_2 X is well_founded
proof
  assume
A1: for Y st Y c= field R & Y <> {} ex a st a in Y & R-Seg(a) misses Y;
A2: field(R |_2 X) c= field R by Th20;
  let Y;
  assume Y c= field(R |_2 X) & Y <> {};
  then consider a such that
A3: a in Y and
A4: R-Seg(a) misses Y by A1,A2,XBOOLE_1:1;
  take a;
  thus a in Y by A3;
  assume not thesis;
  then
A5: ex b being set st b in (R |_2 X)-Seg(a) & b in Y by XBOOLE_0:3;
  (R |_2 X)-Seg(a) c= R-Seg(a) by Th21;
  hence contradiction by A4,A5,XBOOLE_0:3;
end;

theorem Th32:
  R is well-ordering implies R |_2 Y is well-ordering
proof
  assume R is well-ordering;
  then R is reflexive transitive connected antisymmetric well_founded by
  Def4;
  hence
  R |_2 Y is reflexive transitive antisymmetric connected well_founded by
  Th22,Th23,Th24,Th25,Th31;
end;

theorem Th33:
  R is well-ordering implies R-Seg(a),R-Seg(b) are_c=-comparable
proof
  assume R is well-ordering;
  then
A1: R is connected transitive reflexive antisymmetric by Def4;
A2: now
    assume
A3: a in field R & b in field R;
    now
      assume a <> b;
A4:   now
        assume
A5:     [b,a] in R;
        now
          let c;
          assume
A6:       c in R-Seg(b);
          then
A7:       [c,b] in R by Def1;
          then
A8:       [c,a] in R by A1,A5,Lm2;
          c <> b by A6,Def1;
          then c <> a by A1,A5,A7,Lm3;
          hence c in R-Seg(a) by A8,Def1;
        end;
        hence R-Seg(b) c= R-Seg(a) by TARSKI:def 3;
      end;
      now
        assume
A9:     [a,b] in R;
        now
          let c;
          assume
A10:      c in R-Seg(a);
          then
A11:      [c,a] in R by Def1;
          then
A12:      [c,b] in R by A1,A9,Lm2;
          c <> a by A10,Def1;
          then c <> b by A1,A9,A11,Lm3;
          hence c in R-Seg(b) by A12,Def1;
        end;
        hence R-Seg(a) c= R-Seg(b) by TARSKI:def 3;
      end;
      hence thesis by A1,A3,A4,Lm4,XBOOLE_0:def 9;
    end;
    hence thesis;
  end;
  now
    assume R-Seg(a) = {} or R-Seg(b) = {};
    then R-Seg(a) c= R-Seg(b) or R-Seg(b) c= R-Seg(a) by XBOOLE_1:2;
    hence thesis by XBOOLE_0:def 9;
  end;
  hence thesis by A2,Th2;
end;

canceled;

theorem Th35:
  R is well-ordering & b in R-Seg(a) implies (R |_2 (R-Seg(a)))
  -Seg(b) = R-Seg(b)
proof
  assume that
A1: R is well-ordering and
A2: b in R-Seg(a);
  set S = R |_2 (R-Seg(a));
A3: R is transitive reflexive antisymmetric by A1,Def4;
  now
    let c;
    assume
A4: c in R-Seg(b);
    then
A5: [c,b] in R by Def1;
A6: [b,a] in R by A2,Def1;
    then
A7: [c,a] in R by A3,A5,Lm2;
A8: c <> b by A4,Def1;
    then c <> a by A3,A5,A6,Lm3;
    then c in R-Seg(a) by A7,Def1;
    then [c,b] in [:R-Seg(a),R-Seg(a):] by A2,ZFMISC_1:106;
    then [c,b] in S by A5,XBOOLE_0:def 4;
    hence c in S-Seg(b) by A8,Def1;
  end;
  then
A9: R-Seg(b) c= S-Seg(b) by TARSKI:def 3;
  now
    let c;
    assume
A10: c in S-Seg(b);
    then [c,b] in S by Def1;
    then
A11: [c,b] in R by XBOOLE_0:def 4;
    c <> b by A10,Def1;
    hence c in R-Seg(b) by A11,Def1;
  end;
  then S-Seg(b) c= R-Seg(b) by TARSKI:def 3;
  hence thesis by A9,XBOOLE_0:def 10;
end;

theorem Th36:
  R is well-ordering & Y c= field R implies (Y = field R or (ex a
st a in field R & Y = R-Seg(a) ) iff for a st a in Y for b st [b,a] in R holds
  b in Y )
proof
  assume that
A1: R is well-ordering and
A2: Y c= field R;
A3: R is antisymmetric by A1,Def4;
A4: R is transitive by A1,Def4;
  now
    given a such that
    a in field R and
A5: Y = R-Seg(a);
    let b such that
A6: b in Y;
A7: [b,a] in R by A5,A6,Def1;
    let c such that
A8: [c,b] in R;
A9: [c,a] in R by A4,A8,A7,Lm2;
    b <> a by A5,A6,Def1;
    then c <> a by A3,A8,A7,Lm3;
    hence c in Y by A5,A9,Def1;
  end;
  hence Y = field R or (ex a st a in field R & Y = R-Seg(a) ) implies for a st
  a in Y for b st [b,a] in R holds b in Y by RELAT_1:30;
  assume
A10: for a st a in Y for b st [b,a] in R holds b in Y;
  assume Y <> field R;
  then ex d st not ( d in field R iff d in Y ) by TARSKI:2;
  then field R \ Y <> {} by A2,XBOOLE_0:def 5;
  then consider a such that
A11: a in field R \ Y and
A12: for b st b in field R \ Y holds [a,b] in R by A1,Th10;
A13: now
    let b;
    assume
A14: b in R-Seg(a);
    then
A15: [b,a] in R by Def1;
    assume
A16: not b in Y;
    b in field R by A15,RELAT_1:30;
    then b in field R \ Y by A16,XBOOLE_0:def 5;
    then
A17: [a,b] in R by A12;
    b <> a by A14,Def1;
    hence contradiction by A3,A15,A17,Lm3;
  end;
  take a;
  thus a in field R by A11;
A18: R is connected by A1,Def4;
  now
A19: not a in Y by A11,XBOOLE_0:def 5;
    let b;
    assume
A20: b in Y;
    assume not b in R-Seg(a);
    then
A21: not [b,a] in R or a = b by Def1;
    a <> b by A11,A20,XBOOLE_0:def 5;
    then [a,b] in R by A2,A18,A11,A20,A21,Lm4;
    hence contradiction by A10,A20,A19;
  end;
  hence Y = R-Seg(a) by A13,TARSKI:2;
end;

theorem Th37:
  R is well-ordering & a in field R & b in field R implies ( [a,b]
  in R iff R-Seg(a) c= R-Seg(b) )
proof
  assume that
A1: R is well-ordering and
A2: a in field R and
A3: b in field R;
A4: R is antisymmetric by A1,Def4;
A5: R is transitive by A1,Def4;
  thus [a,b] in R implies R-Seg(a) c= R-Seg(b)
  proof
    assume
A6: [a,b] in R;
    let c;
    assume
A7: c in R-Seg(a);
    then
A8: [c,a] in R by Def1;
    then
A9: [c,b] in R by A5,A6,Lm2;
    c <> a by A7,Def1;
    then c <> b by A4,A6,A8,Lm3;
    hence thesis by A9,Def1;
  end;
  assume
A10: R-Seg(a) c= R-Seg(b);
A11: R is connected by A1,Def4;
A12: now
    assume
A13: a <> b;
    assume not [a,b] in R;
    then [b,a] in R by A2,A3,A11,A13,Lm4;
    then b in R-Seg(a) by A13,Def1;
    hence contradiction by A10,Def1;
  end;
  R is reflexive by A1,Def4;
  hence thesis by A2,A12,Lm1;
end;

theorem Th38:
  R is well-ordering & a in field R & b in field R implies ( R-Seg
  (a) c= R-Seg(b) iff a = b or a in R-Seg(b) )
proof
  assume
A1: R is well-ordering & a in field R & b in field R;
  thus R-Seg(a) c= R-Seg(b) implies a = b or a in R-Seg(b)
  proof
    assume R-Seg(a) c= R-Seg(b);
    then [a,b] in R by A1,Th37;
    hence thesis by Def1;
  end;
  now
    assume a in R-Seg(b);
    then [a,b] in R by Def1;
    hence R-Seg(a) c= R-Seg(b) by A1,Th37;
  end;
  hence thesis;
end;

theorem Th39:
  R is well-ordering & X c= field R implies field(R |_2 X) = X
proof
  assume that
A1: R is well-ordering and
A2: X c= field R;
  thus field(R |_2 X) c= X by Th20;
  let x;
  assume
A3: x in X;
  then
A4: [x,x] in [:X,X:] by ZFMISC_1:106;
  R is reflexive by A1,Def4;
  then [x,x] in R by A2,A3,Lm1;
  then [x,x] in R |_2 X by A4,XBOOLE_0:def 4;
  hence thesis by RELAT_1:30;
end;

theorem Th40:
  R is well-ordering implies field(R |_2 R-Seg(a)) = R-Seg(a)
proof
  R-Seg(a) c= field R by Th13;
  hence thesis by Th39;
end;

theorem Th41:
  R is well-ordering implies for Z st for a st a in field R & R
  -Seg(a) c= Z holds a in Z holds field R c= Z
proof
  assume
A1: R is well-ordering;
  then
A2: R is antisymmetric by Def4;
  let Z such that
A3: for a st a in field R & R-Seg(a) c= Z holds a in Z;
A4: now
    let a such that
A5: a in field R and
A6: for b st [b,a] in R & a <> b holds b in Z;
    now
      let b;
      assume b in R-Seg(a);
      then [b,a] in R & b <> a by Def1;
      hence b in Z by A6;
    end;
    then R-Seg(a) c= Z by TARSKI:def 3;
    hence a in Z by A3,A5;
  end;
  given a such that
A7: a in field R & not a in Z;
  field R \ Z <> {} by A7,XBOOLE_0:def 5;
  then consider a such that
A8: a in field R \ Z and
A9: for b st b in field R \ Z holds [a,b] in R by A1,Th10;
  not a in Z by A8,XBOOLE_0:def 5;
  then consider b such that
A10: [b,a] in R and
A11: b <> a and
A12: not b in Z by A4,A8;
  b in field R by A10,RELAT_1:30;
  then b in field R \ Z by A12,XBOOLE_0:def 5;
  then [a,b] in R by A9;
  hence contradiction by A2,A10,A11,Lm3;
end;

theorem Th42:
  R is well-ordering & a in field R & b in field R & (for c st c
  in R-Seg(a) holds [c,b] in R & c <> b) implies [a,b] in R
proof
  assume that
A1: R is well-ordering & a in field R & b in field R and
A2: c in R-Seg(a) implies [c,b] in R & c <> b;
A3: R is connected by A1,Def4;
  assume
A4: not [a,b] in R;
  R is reflexive by A1,Def4;
  then a <> b by A1,A4,Lm1;
  then [b,a] in R by A1,A3,A4,Lm4;
  then b in R-Seg(a) by A4,Def1;
  hence contradiction by A2;
end;

theorem Th43:
  R is well-ordering & dom F = field R & rng F c= field R & (for a
,b st [a,b] in R & a <> b holds [F.a,F.b] in R & F.a <> F.b) implies for a st a
  in field R holds [a,F.a] in R
proof
  assume that
A1: R is well-ordering & dom F = field R & rng F c= field R and
A2: [a,b] in R & a <> b implies [F.a,F.b] in R & F.a <> F.b;
A3: R is antisymmetric by A1,Def4;
  defpred P[set] means [$1,F.$1] in R;
  consider Z such that
A4: a in Z iff a in field R & P[a] from XBOOLE_0:sch 1;
A5: R is transitive by A1,Def4;
  now
    let a;
    assume
A6: a in field R;
    assume
A7: R-Seg(a) c= Z;
A8: now
      let b;
      assume
A9:   b in R-Seg(a);
      then
A10:  [b,F.b] in R by A4,A7;
A11:  [b,a] in R & b <> a by A9,Def1;
      then
A12:  [F.b,F.a] in R by A2;
      hence [b,F.a] in R by A5,A10,Lm2;
      F.b <> F.a by A2,A11;
      hence b <> F.a by A3,A10,A12,Lm3;
    end;
    F.a in rng F by A1,A6,FUNCT_1:def 5;
    then [a,F.a] in R by A1,A6,A8,Th42;
    hence a in Z by A4,A6;
  end;
  then
A13: field R c= Z by A1,Th41;
  let a;
  assume a in field R;
  hence thesis by A4,A13;
end;

definition
  let R,S,F;
  pred F is_isomorphism_of R,S means
  :Def7:
  dom F = field R & rng F = field S
& F is one-to-one & for a,b holds [a,b] in R iff a in field R & b in field R &
  [F.a,F.b] in S;
end;

canceled;

theorem Th45:
  F is_isomorphism_of R,S implies for a,b st [a,b] in R & a <> b
  holds [F.a,F.b] in S & F.a <> F.b
proof
  assume
A1: F is_isomorphism_of R,S;
  then
A2: dom F = field R & F is one-to-one by Def7;
  let a,b;
  assume that
A3: [a,b] in R and
A4: a <> b;
  a in field R & b in field R by A1,A3,Def7;
  hence thesis by A1,A2,A3,A4,Def7,FUNCT_1:def 8;
end;

definition
  let R,S;
  pred R,S are_isomorphic means
  :Def8:
  ex F st F is_isomorphism_of R,S;
end;

canceled;

theorem Th47:
  id(field R) is_isomorphism_of R,R
proof
A1: now
    let a,b;
    thus [a,b] in R implies a in field R & b in field R & [id(field R).a,id(
    field R).b] in R
    proof
      assume
A2:   [a,b] in R;
      hence
A3:   a in field R & b in field R by RELAT_1:30;
      then id(field R).a = a by FUNCT_1:35;
      hence thesis by A2,A3,FUNCT_1:35;
    end;
    assume that
A4: a in field R and
A5: b in field R & [id(field R).a,id(field R).b] in R;
    id(field R).a = a by A4,FUNCT_1:35;
    hence [a,b] in R by A5,FUNCT_1:35;
  end;
  dom(id(field R)) = field R & rng(id(field R)) = field R by RELAT_1:71;
  hence thesis by A1,Def7;
end;

theorem
  R,R are_isomorphic
proof
  take id(field R);
  thus thesis by Th47;
end;

theorem Th49:
  F is_isomorphism_of R,S implies F" is_isomorphism_of S,R
proof
  assume
A1: F is_isomorphism_of R,S;
  then
A2: F is one-to-one by Def7;
A3: rng F = field S by A1,Def7;
  hence
A4: dom(F") = field S by A2,FUNCT_1:55;
A5: dom F = field R by A1,Def7;
  hence rng(F") = field R by A2,FUNCT_1:55;
  thus F" is one-to-one by A2;
  let a,b;
  thus [a,b] in S implies a in field S & b in field S & [F".a,F".b] in R
  proof
A6: dom F = rng(F") by A2,FUNCT_1:55;
    assume
A7: [a,b] in S;
    hence
A8: a in field S & b in field S by RELAT_1:30;
    then
A9: F".a in rng(F") & F".b in rng(F") by A4,FUNCT_1:def 5;
    a = F.(F".a) & b = F.(F".b) by A3,A2,A8,FUNCT_1:57;
    hence thesis by A1,A5,A7,A6,A9,Def7;
  end;
  assume that
A10: a in field S & b in field S and
A11: [F".a,F".b] in R;
  F.(F".a) = a & F.(F".b) = b by A3,A2,A10,FUNCT_1:57;
  hence thesis by A1,A11,Def7;
end;

theorem Th50:
  R,S are_isomorphic implies S,R are_isomorphic
proof
  given F such that
A1: F is_isomorphism_of R,S;
  take F";
  thus thesis by A1,Th49;
end;

theorem Th51:
  F is_isomorphism_of R,S & G is_isomorphism_of S,T implies G*F
  is_isomorphism_of R,T
proof
  assume that
A1: dom F = field R and
A2: rng F = field S and
A3: F is one-to-one and
A4: for a,b holds [a,b] in R iff a in field R & b in field R & [F.a,F.b] in S;
  assume that
A5: dom G = field S and
A6: rng G = field T and
A7: G is one-to-one and
A8: for a,b holds [a,b] in S iff a in field S & b in field S & [G.a,G.b] in T;
  thus dom(G*F) = field R by A1,A2,A5,RELAT_1:46;
  thus rng(G*F) = field T by A2,A5,A6,RELAT_1:47;
  thus G*F is one-to-one by A3,A7;
  let a,b;
  thus [a,b] in R implies a in field R & b in field R & [(G*F).a,(G*F).b] in T
  proof
    assume
A9: [a,b] in R;
    hence a in field R & b in field R by RELAT_1:30;
    then
A10: (G*F).a = G.(F.a) & (G*F).b = G.(F.b) by A1,FUNCT_1:23;
    [F.a,F.b] in S by A4,A9;
    hence thesis by A8,A10;
  end;
  assume that
A11: a in field R & b in field R and
A12: [(G*F).a,(G*F).b] in T;
A13: (G*F).a = G.(F.a) & (G*F).b = G.(F.b) by A1,A11,FUNCT_1:23;
  F.a in field S & F.b in field S by A1,A2,A11,FUNCT_1:def 5;
  then [F.a,F.b] in S by A8,A12,A13;
  hence thesis by A4,A11;
end;

theorem Th52:
  R,S are_isomorphic & S,T are_isomorphic implies R,T are_isomorphic
proof
  given F such that
A1: F is_isomorphism_of R,S;
  given G such that
A2: G is_isomorphism_of S,T;
  take G*F;
  thus thesis by A1,A2,Th51;
end;

theorem Th53:
  F is_isomorphism_of R,S implies ( R is reflexive implies S is
  reflexive ) & ( R is transitive implies S is transitive ) & ( R is connected
implies S is connected ) & ( R is antisymmetric implies S is antisymmetric ) &
  ( R is well_founded implies S is well_founded )
proof
  assume
A1: F is_isomorphism_of R,S;
  then
A2: dom F = field R by Def7;
A3: rng F = field S by A1,Def7;
A4: F is one-to-one by A1,Def7;
  then
A5: rng(F") = dom F & dom(F") = rng F by FUNCT_1:55;
  thus R is reflexive implies S is reflexive
  proof
    assume
A6: R is reflexive;
    now
      let a;
      assume
A7:   a in field S;
      then F".a in field R by A2,A3,A5,FUNCT_1:def 5;
      then
A8:   [F".a,F".a] in R by A6,Lm1;
      a = F.(F".a) by A3,A4,A7,FUNCT_1:57;
      hence [a,a] in S by A1,A8,Def7;
    end;
    hence thesis by Lm1;
  end;
  thus R is transitive implies S is transitive
  proof
    assume
A9: R is transitive;
    now
      let a,b,c;
      assume that
A10:  [a,b] in S and
A11:  [b,c] in S;
A12:  c in field S by A11,RELAT_1:30;
      then
A13:  c = F.(F".c) by A3,A4,FUNCT_1:57;
      b in field S by A10,RELAT_1:30;
      then
A14:  b = F.(F".b) & F".b in field R by A2,A3,A4,A5,FUNCT_1:57,def 5;
      F".c in field R by A2,A3,A5,A12,FUNCT_1:def 5;
      then
A15:  [F".b,F".c] in R by A1,A11,A13,A14,Def7;
A16:  a in field S by A10,RELAT_1:30;
      then
A17:  a = F.(F".a) by A3,A4,FUNCT_1:57;
      F".a in field R by A2,A3,A5,A16,FUNCT_1:def 5;
      then [F".a,F".b] in R by A1,A10,A17,A14,Def7;
      then [F".a,F".c] in R by A9,A15,Lm2;
      hence [a,c] in S by A1,A17,A13,Def7;
    end;
    hence thesis by Lm2;
  end;
  thus R is connected implies S is connected
  proof
    assume
A18: R is connected;
    now
      let a,b;
      assume that
A19:  a in field S & b in field S and
A20:  a <> b;
A21:  a = F.(F".a) & b = F.(F".b) by A3,A4,A19,FUNCT_1:57;
      F".a in field R & F".b in field R by A2,A3,A5,A19,FUNCT_1:def 5;
      then [F".a,F".b] in R or [F".b,F".a] in R by A18,A20,A21,Lm4;
      hence [a,b] in S or [b,a] in S by A1,A21,Def7;
    end;
    hence thesis by Lm4;
  end;
  thus R is antisymmetric implies S is antisymmetric
  proof
    assume
A22: R is antisymmetric;
    now
      let a,b;
      assume that
A23:  [a,b] in S and
A24:  [b,a] in S;
A25:  a in field S by A23,RELAT_1:30;
      then
A26:  a = F.(F".a) by A3,A4,FUNCT_1:57;
A27:  b in field S by A23,RELAT_1:30;
      then
A28:  b = F.(F".b) by A3,A4,FUNCT_1:57;
A29:  F".b in field R by A2,A3,A5,A27,FUNCT_1:def 5;
      F".a in field R by A2,A3,A5,A25,FUNCT_1:def 5;
      then [F".a,F".b] in R & [F".b,F".a] in R by A1,A23,A24,A26,A28,A29,
      Def7;
      hence a = b by A22,A26,A28,Lm3;
    end;
    hence thesis by Lm3;
  end;
  assume
A30: for Y st Y c= field R & Y <> {} ex x st x in Y & R-Seg(x) misses Y;
  let Z;
  assume that
A31: Z c= field S and
A32: Z <> {};
A33: F"Z c= dom F by RELAT_1:167;
  then consider x such that
A34: x in F"Z and
A35: R-Seg(x) misses F"Z by A2,A3,A30,A31,A32,RELAT_1:174;
  take F.x;
  thus F.x in Z by A34,FUNCT_1:def 13;
  assume not thesis;
  then consider y being set such that
A36: y in S-Seg(F.x) and
A37: y in Z by XBOOLE_0:3;
A38: F".y in dom F by A3,A5,A31,A37,FUNCT_1:def 5;
A39: [y,F.x] in S by A36,Def1;
A40: y = F.(F".y) by A3,A4,A31,A37,FUNCT_1:57;
  then F".y in F"Z by A37,A38,FUNCT_1:def 13;
  then not F".y in R-Seg(x) by A35,XBOOLE_0:3;
  then not [F".y,x] in R or F".y = x by Def1;
  hence contradiction by A1,A2,A33,A34,A36,A40,A38,A39,Def1,Def7;
end;

theorem Th54:
  R is well-ordering & F is_isomorphism_of R,S implies S is well-ordering
proof
  assume R is reflexive transitive antisymmetric connected well_founded & F
  is_isomorphism_of R,S;
  hence S is reflexive transitive antisymmetric connected well_founded by
  Th53;
end;

theorem Th55:
  R is well-ordering implies for F,G st F is_isomorphism_of R,S &
  G is_isomorphism_of R,S holds F = G
proof
  assume
A1: R is well-ordering;
  let F,G;
  assume that
A2: F is_isomorphism_of R,S and
A3: G is_isomorphism_of R,S;
A4: dom F = field R by A2,Def7;
  S is well-ordering by A1,A2,Th54;
  then
A5: S is antisymmetric by Def4;
A6: rng F = field S by A2,Def7;
A7: G is one-to-one by A3,Def7;
A8: dom G = field R by A3,Def7;
A9: G" is_isomorphism_of S,R by A3,Th49;
  then
A10: G" is one-to-one by Def7;
A11: F is one-to-one by A2,Def7;
A12: rng G = field S by A3,Def7;
A13: F" is_isomorphism_of S,R by A2,Th49;
  then
A14: F" is one-to-one by Def7;
  for a st a in field R holds F.a = G.a
  proof
A15: dom(F") = field S by A6,A11,FUNCT_1:55;
    then
A16: dom(F"*G) = field R by A8,A12,RELAT_1:46;
A17: now
      let a,b;
      assume that
A18:  [a,b] in R and
A19:  a <> b;
A20:  [G.a,G.b] in S by A3,A18,Def7;
A21:  b in field R by A18,RELAT_1:30;
      then
A22:  F".(G.b) = (F"*G).b by A8,FUNCT_1:23;
A23:  a in field R by A18,RELAT_1:30;
      then F".(G.a) = (F"*G).a by A8,FUNCT_1:23;
      hence [(F"*G).a,(F"*G).b] in R by A13,A20,A22,Def7;
      thus (F"*G).a <> (F"*G).b by A14,A7,A16,A19,A23,A21,FUNCT_1:def 8;
    end;
A24: dom(G") = field S by A12,A7,FUNCT_1:55;
    then
A25: dom(G"*F) = field R by A4,A6,RELAT_1:46;
A26: now
      let a,b;
      assume that
A27:  [a,b] in R and
A28:  a <> b;
A29:  [F.a,F.b] in S by A2,A27,Def7;
A30:  b in field R by A27,RELAT_1:30;
      then
A31:  G".(F.b) = (G"*F).b by A4,FUNCT_1:23;
A32:  a in field R by A27,RELAT_1:30;
      then G".(F.a) = (G"*F).a by A4,FUNCT_1:23;
      hence [(G"*F).a,(G"*F).b] in R by A9,A29,A31,Def7;
      thus (G"*F).a <> (G"*F).b by A11,A10,A25,A28,A32,A30,FUNCT_1:def 8;
    end;
    let a such that
A33: a in field R;
A34: F".(G.a) = (F"*G).a by A8,A33,FUNCT_1:23;
    G.a in rng F by A6,A8,A12,A33,FUNCT_1:def 5;
    then
A35: F.(F".(G.a)) = G.a by A11,FUNCT_1:57;
    rng(F") = field R by A4,A11,FUNCT_1:55;
    then rng(F"*G) = field R by A12,A15,RELAT_1:47;
    then [a,(F"*G).a] in R by A1,A33,A16,A17,Th43;
    then
A36: [F.a,G.a] in S by A2,A34,A35,Def7;
    F.a in rng G by A4,A6,A12,A33,FUNCT_1:def 5;
    then
A37: G.(G".(F.a)) = F.a by A7,FUNCT_1:57;
A38: G".(F.a) = (G"*F).a by A4,A33,FUNCT_1:23;
    rng(G") = field R by A8,A7,FUNCT_1:55;
    then rng(G"*F) = field R by A6,A24,RELAT_1:47;
    then [a,(G"*F).a] in R by A1,A33,A25,A26,Th43;
    then [G.a,F.a] in S by A3,A38,A37,Def7;
    hence thesis by A5,A36,Lm3;
  end;
  hence thesis by A4,A8,FUNCT_1:9;
end;

definition
  let R,S;
  assume that
A1: R is well-ordering and
A2: R,S are_isomorphic;
  func canonical_isomorphism_of(R,S) -> Function means
  :Def9:
  it is_isomorphism_of R,S;
  existence by A2,Def8;
  uniqueness by A1,Th55;
end;

canceled;

theorem Th57:
  R is well-ordering implies for a st a in field R holds not R,R
  |_2 (R-Seg(a)) are_isomorphic
proof
  assume
A1: R is well-ordering;
  then
A2: R is antisymmetric by Def4;
  let a such that
A3: a in field R;
  set S = R |_2 (R-Seg(a));
  set F = canonical_isomorphism_of(R,S);
  assume R,R |_2 (R-Seg(a)) are_isomorphic;
  then
A4: F is_isomorphism_of R,S by A1,Def9;
  then
A5: dom F = field R by Def7;
A6: F is one-to-one by A4,Def7;
A7: now
    let b,c;
    assume that
A8: [b,c] in R and
A9: b <> c;
    [F.b,F.c] in R |_2 (R-Seg(a)) by A4,A8,Def7;
    hence [F.b,F.c] in R by XBOOLE_0:def 4;
    b in field R & c in field R by A8,RELAT_1:30;
    hence F.b <> F.c by A5,A6,A9,FUNCT_1:def 8;
  end;
A10: rng F = field S by A4,Def7;
  field S = R-Seg(a) by A1,Th40;
  then F.a in R-Seg(a) by A3,A5,A10,FUNCT_1:def 5;
  then
A11: [F.a,a] in R & F.a <> a by Def1;
  rng F c= field R by A10,Th20;
  then [a,F.a] in R by A1,A3,A5,A7,Th43;
  hence contradiction by A2,A11,Lm3;
end;

theorem Th58:
  R is well-ordering & a in field R & b in field R & a <> b
  implies not R |_2 (R-Seg(a)),R |_2 (R-Seg(b)) are_isomorphic
proof
  assume that
A1: R is well-ordering and
A2: a in field R & b in field R and
A3: a <> b;
A4: R is connected by A1,Def4;
A5: now
    set S = R |_2 (R-Seg(a));
    assume
A6: R-Seg(b) c= R-Seg(a);
    then
A7: S |_2 (R-Seg(b)) = R |_2 (R-Seg(b)) by Th29;
A8: field S = R-Seg(a) by A1,Th40;
A9: b in R-Seg(a)
    proof
      assume not b in R-Seg(a);
      then not [b,a] in R by A3,Def1;
      then [a,b] in R by A2,A3,A4,Lm4;
      then a in R-Seg(b) by A3,Def1;
      hence contradiction by A6,Def1;
    end;
    then R-Seg(b) = S-Seg(b) by A1,Th35;
    hence thesis by A1,A8,A9,A7,Th32,Th57;
  end;
A10: now
    set S = R |_2 (R-Seg(b));
    assume
A11: R-Seg(a) c= R-Seg(b);
    then
A12: S |_2 (R-Seg(a)) = R |_2 (R-Seg(a)) by Th29;
A13: field S = R-Seg(b) & S is well-ordering by A1,Th32,Th40;
A14: a in R-Seg(b)
    proof
      assume not a in R-Seg(b);
      then not [a,b] in R by A3,Def1;
      then [b,a] in R by A2,A3,A4,Lm4;
      then b in R-Seg(a) by A3,Def1;
      hence contradiction by A11,Def1;
    end;
    then R-Seg(a) = S-Seg(a) by A1,Th35;
    hence thesis by A14,A12,A13,Th50,Th57;
  end;
  R-Seg(a),R-Seg(b) are_c=-comparable by A1,Th33;
  hence thesis by A10,A5,XBOOLE_0:def 9;
end;

theorem Th59:
  R is well-ordering & Z c= field R & F is_isomorphism_of R,S
  implies F|Z is_isomorphism_of R |_2 Z,S |_2 (F.:Z) & R |_2 Z,S |_2 (F.:Z)
  are_isomorphic
proof
  assume that
A1: R is well-ordering and
A2: Z c= field R and
A3: F is_isomorphism_of R,S;
A4: F.:Z c= rng F by RELAT_1:144;
  rng F = field S by A3,Def7;
  then
A5: F.:Z = field(S |_2 (F.:Z)) by A1,A3,A4,Th39,Th54;
A6: F is one-to-one by A3,Def7;
A7: Z = field(R |_2 Z) by A1,A2,Th39;
A8: dom F = field R by A3,Def7;
  thus F|Z is_isomorphism_of R |_2 Z,S |_2 (F.:Z)
  proof
    thus
A9: dom(F|Z) = field(R |_2 Z) by A2,A8,A7,RELAT_1:91;
    thus
A10: rng(F|Z) = field(S |_2 (F.:Z)) by A5,RELAT_1:148;
    thus F|Z is one-to-one by A6,FUNCT_1:84;
    let a,b;
    thus [a,b] in R |_2 Z implies a in field(R |_2 Z) & b in field(R |_2 Z) &
    [F|Z.a,F|Z.b] in S |_2 (F.:Z)
    proof
      assume
A11:  [a,b] in R |_2 Z;
      then [a,b] in R by XBOOLE_0:def 4;
      then
A12:  [F.a,F.b] in S by A3,Def7;
      thus
A13:  a in field(R |_2 Z) & b in field(R |_2 Z) by A11,RELAT_1:30;
      then F|Z.a in rng(F|Z) & F|Z.b in rng(F|Z) by A9,FUNCT_1:def 5;
      then
A14:  [F|Z.a,F|Z.b] in [:F.:Z,F.:Z:] by A5,A10,ZFMISC_1:106;
      F.a = F|Z.a & F.b = F|Z.b by A9,A13,FUNCT_1:70;
      hence thesis by A12,A14,XBOOLE_0:def 4;
    end;
    assume that
A15: a in field(R |_2 Z) & b in field(R |_2 Z) and
A16: [F|Z.a,F|Z.b] in S |_2 (F.:Z);
    F.a = F|Z.a & F.b = F|Z.b by A9,A15,FUNCT_1:70;
    then
A17: [F.a,F.b] in S by A16,XBOOLE_0:def 4;
A18: [a,b] in [:Z,Z:] by A7,A15,ZFMISC_1:106;
    a in field R & b in field R by A15,Th19;
    then [a,b] in R by A3,A17,Def7;
    hence thesis by A18,XBOOLE_0:def 4;
  end;
  hence thesis by Def8;
end;

theorem Th60:
  F is_isomorphism_of R,S implies for a st a in field R ex b st b
  in field S & F.:(R-Seg(a)) = S-Seg(b)
proof
  assume
A1: F is_isomorphism_of R,S;
  then
A2: dom F = field R by Def7;
  let a;
  assume
A3: a in field R;
  take b = F.a;
A4: rng F = field S by A1,Def7;
  hence b in field S by A3,A2,FUNCT_1:def 5;
A5: F is one-to-one by A1,Def7;
A6: c in S-Seg(b) implies c in F.:(R-Seg(a))
  proof
    assume
A7: c in S-Seg(b);
    then
A8: c <> b by Def1;
A9: [c,b] in S by A7,Def1;
    then
A10: c in field S by RELAT_1:30;
    then
A11: c = F.(F".c) by A4,A5,FUNCT_1:57;
    rng(F") = dom F & dom(F") = rng F by A5,FUNCT_1:55;
    then
A12: F".c in field R by A2,A4,A10,FUNCT_1:def 5;
    then [F".c,a] in R by A1,A3,A9,A11,Def7;
    then F".c in R-Seg(a) by A8,A11,Def1;
    hence thesis by A2,A11,A12,FUNCT_1:def 12;
  end;
  c in F.:(R-Seg(a)) implies c in S-Seg(b)
  proof
    assume c in F.:(R-Seg(a));
    then consider d such that
A13: d in dom F and
A14: d in R-Seg(a) and
A15: c = F.d by FUNCT_1:def 12;
    [d,a] in R by A14,Def1;
    then
A16: [c,b] in S by A1,A15,Def7;
    d <> a by A14,Def1;
    then c <> b by A3,A2,A5,A13,A15,FUNCT_1:def 8;
    hence thesis by A16,Def1;
  end;
  hence thesis by A6,TARSKI:2;
end;

theorem Th61:
  R is well-ordering & F is_isomorphism_of R,S implies for a st a
  in field R ex b st b in field S & R |_2 (R-Seg(a)),S |_2 (S-Seg(b))
  are_isomorphic
proof
  assume that
A1: R is well-ordering and
A2: F is_isomorphism_of R,S;
  let a;
  assume a in field R;
  then consider b such that
A3: b in field S & F.:(R-Seg(a)) = S-Seg(b) by A2,Th60;
  take b;
  R-Seg(a) c= field R by Th13;
  hence thesis by A1,A2,A3,Th59;
end;

theorem Th62:
  R is well-ordering & S is well-ordering & a in field R & b in
field S & c in field S & R,S |_2 (S-Seg(b)) are_isomorphic & R |_2 (R-Seg(a)),S
  |_2 (S-Seg(c)) are_isomorphic implies S-Seg(c) c= S-Seg(b) & [c,b] in S
proof
  assume that
A1: R is well-ordering and
A2: S is well-ordering and
A3: a in field R and
A4: b in field S and
A5: c in field S and
A6: R,S |_2 (S-Seg(b)) are_isomorphic and
A7: R |_2 (R-Seg(a)),S |_2 (S-Seg(c)) are_isomorphic;
  set Q = S |_2 (S-Seg(b));
  set F1 = canonical_isomorphism_of(R,Q);
A8: F1 is_isomorphism_of R,Q by A1,A6,Def9;
  then consider d such that
A9: d in field Q and
A10: F1.:(R-Seg(a)) = Q-Seg(d) by A3,Th60;
A11: S-Seg(b) = field Q by A2,Th40;
  then
A12: Q-Seg(d) = S-Seg(d) by A2,A9,Th35;
A13: rng F1 = S-Seg(b) by A8,A11,Def7;
  then
A14: Q-Seg(d) c= S-Seg(b) by A10,RELAT_1:144;
  set T = S |_2 (S-Seg(c));
  set P = R |_2 (R-Seg(a));
A15: T,P are_isomorphic by A7,Th50;
A16: d in field S by A9,Th19;
  R-Seg(a) c= field R by Th13;
  then P,Q |_2 (F1.:(R-Seg(a))) are_isomorphic by A1,A8,Th59;
  then T,Q |_2 (Q-Seg(d)) are_isomorphic by A10,A15,Th52;
  then T,S |_2 (S-Seg(d)) are_isomorphic by A10,A12,A13,Th29,RELAT_1:144;
  hence S-Seg(c) c= S-Seg(b) by A2,A5,A12,A14,A16,Th58;
  hence thesis by A2,A4,A5,Th37;
end;

theorem Th63:
  R is well-ordering & S is well-ordering implies R,S
  are_isomorphic or (ex a st a in field R & R |_2 (R-Seg(a)),S are_isomorphic )
  or ex a st a in field S & R,S |_2 (S-Seg(a)) are_isomorphic
proof
  assume that
A1: R is well-ordering and
A2: S is well-ordering;
A3: S is antisymmetric by A2,Def4;
A4: R is reflexive by A1,Def4;
A5: R is connected by A1,Def4;
  defpred P[set] means ex b st b in field S & R |_2 (R-Seg($1)),S |_2 (S-Seg(b
  )) are_isomorphic;
  consider Z such that
A6: a in Z iff a in field R & P[a] from XBOOLE_0:sch 1;
A7: Z c= field R
  proof
    let x;
    thus thesis by A6;
  end;
  defpred P[set,set] means $2 in field S & R |_2 (R-Seg $1),S |_2 (S-Seg $2)
  are_isomorphic;
A8: for a,b,c st P[a, b] & P[a, c] holds b = c
  proof
    let a,b,c;
    assume that
A9: b in field S and
A10: R |_2 (R-Seg(a)),S |_2 (S-Seg(b)) are_isomorphic and
A11: c in field S & R |_2 (R-Seg(a)),S |_2 (S-Seg(c)) are_isomorphic;
    S |_2 (S-Seg(b)),R |_2 (R-Seg(a)) are_isomorphic by A10,Th50;
    hence thesis by A2,A9,A11,Th52,Th58;
  end;
  consider F such that
A12: [a,b] in F iff a in field R & P[a,b] from FUNCT_1:sch 1(A8);
A13: Z = dom F
  proof
    thus a in Z implies a in dom F
    proof
      assume
A14:  a in Z;
      then consider b such that
A15:  b in field S & R |_2 (R-Seg(a)),S |_2 (S-Seg(b)) are_isomorphic by A6;
      a in field R by A6,A14;
      then [a,b] in F by A12,A15;
      hence thesis by RELAT_1:def 4;
    end;
    let a;
    assume a in dom F;
    then consider b such that
A16: [a,b] in F by RELAT_1:def 4;
A17: R |_2 (R-Seg(a)),S |_2 (S-Seg(b)) are_isomorphic by A12,A16;
    a in field R & b in field S by A12,A16;
    hence thesis by A6,A17;
  end;
A18: rng F c= field S
  proof
    let a;
    assume a in rng F;
    then consider b such that
A19: b in dom F & a = F.b by FUNCT_1:def 5;
    [b,a] in F by A19,FUNCT_1:8;
    hence thesis by A12;
  end;
A20: S is reflexive by A2,Def4;
A21: F is_isomorphism_of R |_2 (dom F),S |_2 (rng F)
  proof
    thus dom F = field(R |_2 (dom F)) & rng F = field(S |_2 (rng F)) by A1,A2,
    A7,A18,A13,Th39;
    thus
A22: F is one-to-one
    proof
      let a,b;
      assume that
A23:  a in dom F and
A24:  b in dom F and
A25:  F.a = F.b;
A26:  [b,F.b] in F by A24,FUNCT_1:8;
      then R |_2 (R-Seg(b)),S |_2 (S-Seg(F.a)) are_isomorphic by A12,A25;
      then
A27:  S |_2 (S-Seg(F.a)),R |_2 (R-Seg(b)) are_isomorphic by Th50;
      [a,F.a] in F by A23,FUNCT_1:8;
      then
A28:  a in field R & R |_2 (R-Seg(a)),S |_2 (S-Seg(F.a)) are_isomorphic by A12;
      b in field R by A12,A26;
      hence thesis by A1,A28,A27,Th52,Th58;
    end;
    let a,b;
    set P = R |_2 (R-Seg(a));
A29: field P = R-Seg(a) & P is well-ordering by A1,Th32,Th40;
    thus [a,b] in R |_2 (dom F) implies a in field(R |_2 (dom F)) & b in field
    (R |_2 (dom F)) & [F.a,F.b] in S |_2 (rng F)
    proof
      assume
A30:  [a,b] in R |_2 (dom F);
      hence
A31:  a in field(R |_2 (dom F)) & b in field(R |_2 (dom F)) by
      RELAT_1:30;
      then
A32:  a in dom F by Th19;
      then
A33:  [a,F.a] in F by FUNCT_1:8;
      then
A34:  F.a in field S by A12;
A35:  b in dom F by A31,Th19;
      then
A36:  [b,F.b] in F by FUNCT_1:8;
      then
A37:  b in field R by A12;
A38:  F.b in field S & R |_2 (R-Seg(b)),S |_2 (S-Seg(F.b))
      are_isomorphic by A12,A36;
A39:  [a,b] in R by A30,XBOOLE_0:def 4;
A40:  F.b in rng F by A35,FUNCT_1:def 5;
      F.a in rng F by A32,FUNCT_1:def 5;
      then
A41:  [F.a,F.b] in [:rng F,rng F:] by A40,ZFMISC_1:106;
      a in field R by A12,A33;
      then
A42:  R-Seg(a) c= R-Seg(b) by A1,A37,A39,Th37;
A43:  R |_2 (R-Seg(a)),S |_2 (S-Seg(F.a)) are_isomorphic by A12,A33;
A44:  now
        set P = R |_2 (R-Seg(b));
A45:    field P = R-Seg(b) & P is well-ordering by A1,Th32,Th40;
        assume a <> b;
        then
A46:    a in R-Seg(b) by A39,Def1;
        then P-Seg(a) = R-Seg(a) by A1,Th35;
        then P |_2 (P-Seg(a)),S |_2 (S-Seg(F.a)) are_isomorphic by A43,A42,
        Th29;
        then [F.a,F.b] in S by A2,A34,A38,A46,A45,Th62;
        hence thesis by A41,XBOOLE_0:def 4;
      end;
      a = b implies thesis
      proof
        assume a = b;
        then [F.a,F.b] in S by A20,A34,Lm1;
        hence thesis by A41,XBOOLE_0:def 4;
      end;
      hence thesis by A44;
    end;
    assume that
A47: a in field(R |_2 (dom F)) and
A48: b in field(R |_2 (dom F)) and
A49: [F.a,F.b] in S |_2 (rng F);
A50: [F.a,F.b] in S by A49,XBOOLE_0:def 4;
A51: a in dom F by A47,Th19;
    then
A52: [a,F.a] in F by FUNCT_1:8;
    then
A53: a in field R by A12;
    assume not [a,b] in R |_2 (dom F);
    then
A54: not [a,b] in R or not [a,b] in [:dom F,dom F:] by XBOOLE_0:def
    4;
    then
A55: a <> b by A4,A51,A53,Lm1,ZFMISC_1:106;
A56: b in dom F by A48,Th19;
    then
A57: [b,F.b] in F by FUNCT_1:8;
    then
A58: R |_2 (R-Seg(b)),S |_2 (S-Seg(F.b)) are_isomorphic by A12;
A59: b in field R by A12,A57;
    then
A60: [b,a] in R by A5,A51,A56,A54,A53,A55,Lm4,ZFMISC_1:106;
    then
A61: R-Seg(b) c= R-Seg(a) by A1,A53,A59,Th37;
A62: b in R-Seg(a) by A51,A56,A54,A60,Def1,ZFMISC_1:106;
    then P-Seg(b) = R-Seg(b) by A1,Th35;
    then
A63: P |_2 (P-Seg(b)),S |_2 (S-Seg(F.b)) are_isomorphic by A58,A61, Th29;
A64: F.b in field S by A12,A57;
    F.a in field S & R |_2 (R-Seg(a)),S |_2 (S-Seg(F.a)) are_isomorphic
    by A12,A52;
    then [F.b,F.a] in S by A2,A64,A62,A29,A63,Th62;
    then F.a = F.b by A3,A50,Lm3;
    hence contradiction by A22,A51,A56,A55,FUNCT_1:def 8;
  end;
A65: now
    given a such that
A66: a in field R and
A67: Z = R-Seg(a);
    given b such that
A68: b in field S and
A69: rng F = S-Seg(b);
    R |_2 (R-Seg(a)),S |_2 (S-Seg(b)) are_isomorphic by A13,A21,A67,
    A69,Def8;
    then a in Z by A6,A66,A68;
    hence contradiction by A67,Def1;
  end;
A70: now
    let a such that
A71: a in Z;
    consider c such that
A72: c in field S and
A73: R |_2 (R-Seg(a)),S |_2 (S-Seg(c)) are_isomorphic by A6,A71;
    let b such that
A74: [b,a] in R;
A75: a in field R by A6,A71;
    now
      set Q = S |_2 (S-Seg(c));
      set P = R |_2 (R-Seg(a));
      P is well-ordering by A1,Th32;
      then
A76:  canonical_isomorphism_of(P,Q) is_isomorphism_of P,Q by A73,
      Def9;
      assume a <> b;
      then
A77:  b in R-Seg(a) by A74,Def1;
      then
A78:  P-Seg(b) = R-Seg(b) by A1,Th35;
A79:  b in field R by A74,RELAT_1:30;
      then R-Seg(b) c= R-Seg(a) by A1,A75,A77,Th38;
      then
A80:  P |_2 (R-Seg(b)) = R |_2 (R-Seg(b)) by Th29;
      field P = R-Seg(a) by A1,Th40;
      then consider d such that
A81:  d in field Q and
A82:  P |_2 (P-Seg(b)),Q |_2 (Q-Seg(d)) are_isomorphic by A1,A76,A77,
      Th32,Th61;
A83:  S-Seg(c) = field Q by A2,Th40;
      then
A84:  Q-Seg(d) = S-Seg(d) by A2,A81,Th35;
      [d,c] in S by A81,A83,Def1;
      then
A85:  d in field S by RELAT_1:30;
      then S-Seg(d) c= S-Seg(c) by A2,A72,A81,A83,Th38;
      then
      R |_2 (R-Seg(b)),S |_2 (S-Seg(d)) are_isomorphic by A82,A78,A84,A80,Th29;
      hence b in Z by A6,A79,A85;
    end;
    hence b in Z by A71;
  end;
A86: R |_2 Z,S |_2 (rng F) are_isomorphic by A13,A21,Def8;
A87: now
    assume
A88: Z = field R;
    given a such that
A89: a in field S and
A90: rng F = S-Seg(a);
    take a;
    thus a in field S by A89;
    thus R,S |_2 (S-Seg(a)) are_isomorphic by A86,A88,A90,Th30;
  end;
A91: now
    let a such that
A92: a in rng F;
    consider c such that
A93: c in dom F & a = F.c by A92,FUNCT_1:def 5;
A94: [c,a] in F by A93,FUNCT_1:8;
    then
A95: a in field S by A12;
    let b such that
A96: [b,a] in S;
A97: R |_2 (R-Seg(c)),S |_2 (S-Seg(a)) are_isomorphic by A12,A94;
A98: c in field R by A12,A94;
    now
      set Q = S |_2 (S-Seg(a));
      set P = R |_2 (R-Seg(c));
      assume a <> b;
      then
A99:  b in S-Seg(a) by A96,Def1;
      then
A100: Q-Seg(b) = S-Seg(b) by A2,Th35;
A101: b in field S by A96,RELAT_1:30;
      then S-Seg(b) c= S-Seg(a) by A2,A95,A99,Th38;
      then
A102: Q |_2 (S-Seg(b)) = S |_2 (S-Seg(b)) by Th29;
      Q,P are_isomorphic & Q is well-ordering by A2,A97,Th32,Th50;
      then
A103: canonical_isomorphism_of(Q,P) is_isomorphism_of Q,P by Def9;
      field Q = S-Seg(a) by A2,Th40;
      then consider d such that
A104: d in field P and
A105: Q |_2 (Q-Seg(b)),P |_2 (P-Seg(d)) are_isomorphic by A2,A103,
      A99,
      Th32,Th61;
A106: R-Seg(c) = field P by A1,Th40;
      then
A107: P-Seg(d) = R-Seg(d) by A1,A104,Th35;
      [d,c] in R by A104,A106,Def1;
      then
A108: d in field R by RELAT_1:30;
      then R-Seg(d) c= R-Seg(c) by A1,A98,A104,A106,Th38;
      then
S |_2 (S-Seg(b)),R |_2 (R-Seg(d)) are_isomorphic by A105,A100,A107,A102,Th29;
      then R |_2 (R-Seg(d)),S |_2 (S-Seg(b)) are_isomorphic by Th50;
      then [d,b] in F by A12,A101,A108;
      then d in dom F & b = F.d by FUNCT_1:8;
      hence b in rng F by FUNCT_1:def 5;
    end;
    hence b in rng F by A92;
  end;
A109: now
    assume
A110: rng F = field S;
    given a such that
A111: a in field R and
A112: Z = R-Seg(a);
    take a;
    thus a in field R by A111;
    thus R |_2 (R-Seg(a)),S are_isomorphic by A86,A110,A112,Th30;
  end;
  now
    assume
A113: Z = field R & rng F = field S;
    R |_2 field R = R & S |_2 field S = S by Th30;
    hence R,S are_isomorphic by A13,A21,A113,Def8;
  end;
  hence thesis by A1,A2,A7,A18,A70,A91,A65,A87,A109,Th36;
end;

theorem
  Y c= field R & R is well-ordering implies R,R |_2 Y are_isomorphic or
  ex a st a in field R & R |_2 (R-Seg(a)),R |_2 Y are_isomorphic
proof
  assume that
A1: Y c= field R and
A2: R is well-ordering;
A3: now
    given a such that
A4: a in field(R |_2 Y) and
A5: R,(R |_2 Y) |_2 ((R |_2 Y)-Seg(a)) are_isomorphic;
    consider F such that
A6: F is_isomorphism_of R,(R |_2 Y) |_2 ((R |_2 Y)-Seg(a)) by A5,Def8;
A7: now
      let c,b;
      assume
A8:   [c,b] in R & c <> b;
      then [F.c,F.b] in (R |_2 Y) |_2 ((R |_2 Y)-Seg(a)) by A6,Th45;
      then [F.c,F.b] in R |_2 Y by XBOOLE_0:def 4;
      hence [F.c,F.b] in R & F.c <> F.b by A6,A8,Th45,XBOOLE_0:def 4;
    end;
A9: R is antisymmetric by A2,Def4;
A10: field(R |_2 Y) = Y by A1,A2,Th39;
    field((R |_2 Y) |_2 ((R |_2 Y)-Seg(a))) = (R |_2 Y)-Seg(a) by A2,Th32
    ,Th40;
    then
A11: rng F = (R |_2 Y)-Seg(a) by A6,Def7;
A12: dom F = field R by A6,Def7;
    then
A13: F.a in rng F by A1,A4,A10,FUNCT_1:def 5;
    then
A14: F.a <> a by A11,Def1;
    [F.a,a] in R |_2 Y by A11,A13,Def1;
    then
A15: [F.a,a] in R by XBOOLE_0:def 4;
    (R |_2 Y)-Seg(a) c= Y by A10,Th13;
    then rng F c= field R by A1,A11,XBOOLE_1:1;
    then [a,F.a] in R by A1,A2,A4,A10,A12,A7,Th43;
    hence contradiction by A14,A15,A9,Lm3;
  end;
  R |_2 Y is well-ordering by A2,Th32;
  hence thesis by A2,A3,Th63;
end;
 
:: from AMISTD_3, 2010.01.10, A.T.

theorem
  R,S are_isomorphic & R is well-ordering implies S is well-ordering
proof
  assume R,S are_isomorphic;
  then ex F being Function st F is_isomorphism_of R,S by Def8;
  hence thesis by Th54;
end;
