:: Complex Numbers - Basic Definitions
::  by Library Committee
::
:: Received March 7, 2003
:: Copyright (c) 2003 Association of Mizar Users

environ

 vocabularies XCMPLX_0, ARYTM_0, COMPLEX1, ARYTM, FUNCOP_1, BOOLE, FUNCT_1,
      FUNCT_2, ORDINAL2, RELAT_1, OPPCAT_1, ARYTM_1, ARYTM_2, ARYTM_3, XREAL_0;
 notations TARSKI, XBOOLE_0, SUBSET_1, ZFMISC_1, FUNCT_1, FUNCT_2, FUNCT_4,
      ORDINAL1, ARYTM_2, ARYTM_1, NUMBERS, ARYTM_0;
 constructors FUNCT_4, ARYTM_1, ARYTM_0;
 registrations ZFMISC_1, FUNCT_1, FUNCT_2, NUMBERS, XBOOLE_0;
 requirements BOOLE, SUBSET, NUMERALS;
 theorems ARYTM_0, XBOOLE_0, FUNCT_4, FUNCT_2, ZFMISC_1, ARYTM_2, TARSKI,
      ARYTM_1, XBOOLE_1, NUMBERS, ORDINAL1;

begin

definition
  func <i> equals
  (0,1) --> (0,1);
  coherence;
  let c be number;
  attr c is complex means
  :Def2:
  c in COMPLEX;
end;

registration
  cluster <i> -> complex;
  coherence
  proof
    set X = { x where x is Element of Funcs({0,1},REAL): x.1 = 0};
A1: now
      assume
A2:   <i> in X;
A3:   ex x being Element of Funcs({0,1},REAL) st <i> = x & x.1 = 0 by A2;
      thus contradiction by A3,FUNCT_4:66;
    end;
A4: <i> in Funcs({0,1},REAL) by FUNCT_2:11;
A5: <i> in Funcs({0,1},REAL) \ X by A1,A4,XBOOLE_0:def 5;
    thus <i> in COMPLEX by A5,NUMBERS:def 2,XBOOLE_0:def 3;
  end;
end;

registration
  cluster complex number;
  existence
  proof
    take <i>;
    thus thesis;
  end;
end;

notation
  let x be complex number;
  synonym x is zero for x is empty;
end;

definition
  let x be complex number;
  redefine attr x is zero means
  x = 0;
  compatibility;
end;

definition
  let x,y be complex number;
A1: x in COMPLEX by Def2;
  consider x1,x2 being Element of REAL such that
A2: x = [*x1,x2*] by A1,ARYTM_0:11;
A3: y in COMPLEX by Def2;
  consider y1,y2 being Element of REAL such that
A4: y = [*y1,y2*] by A3,ARYTM_0:11;
  func x+y means
  :Def4:
  ex x1,x2,y1,y2 being Element of REAL st
  x = [*x1,x2*] & y = [*y1,y2*] & it = [*+(x1,y1),+(x2,y2)*];
  existence
  proof
    take [*+(x1,y1),+(x2,y2)*];
    thus thesis by A2,A4;
  end;
  uniqueness
  proof
    let c1,c2 be number;
    given x1,x2,y1,y2 being Element of REAL such that
A5: x = [*x1,x2*] and
A6: y = [*y1,y2*] and
A7: c1 = [*+(x1,y1),+(x2,y2)*];
    given x1',x2',y1',y2' being Element of REAL such that
A8: x = [*x1',x2'*] and
A9: y = [*y1',y2'*] and
A10: c2 = [*+(x1',y1'),+(x2',y2')*];
A11: x1 = x1' & x2 = x2' by A5,A8,ARYTM_0:12;
A12: y1 = y1' by A6,A9,ARYTM_0:12;
    thus thesis by A6,A7,A9,A10,A11,A12,ARYTM_0:12;
  end;
  commutativity;
  func x*y means
  :Def5:
  ex x1,x2,y1,y2 being Element of REAL st
  x = [*x1,x2*] & y = [*y1,y2*] &
  it = [* +(*(x1,y1),opp*(x2,y2)), +(*(x1,y2),*(x2,y1)) *];
  existence
  proof
    take [* +(*(x1,y1),opp*(x2,y2)), +(*(x1,y2),*(x2,y1)) *];
    thus thesis by A2,A4;
  end;
  uniqueness
  proof
    let c1,c2 be number;
    given x1,x2,y1,y2 being Element of REAL such that
A13: x = [*x1,x2*] and
A14: y = [*y1,y2*] and
A15: c1 = [*+(*(x1,y1),opp*(x2,y2)), +(*(x1,y2),*(x2,y1))*];
    given x1',x2',y1',y2' being Element of REAL such that
A16: x = [*x1',x2'*] and
A17: y = [*y1',y2'*] and
A18: c2 = [*+(*(x1',y1'),opp*(x2',y2')), +(*(x1',y2'),*(x2',y1'))*];
A19: x1 = x1' & x2 = x2' by A13,A16,ARYTM_0:12;
A20: y1 = y1' & y2 = y2' by A14,A17,ARYTM_0:12;
    thus thesis by A15,A18,A19,A20;
  end;
  commutativity;
end;

Lm1: 0 = [*0,0*] by ARYTM_0:def 7;
reconsider j = 1 as Element of REAL;

Lm2: for x,y,z being Element of REAL st +(x,y) = 0 & +(x,z) = 0 holds y = z
proof
  let x,y,z be Element of REAL such that
A1: +(x,y) = 0 and
A2: +(x,z) = 0;
  per cases;
  suppose
A3: x in REAL+ & y in REAL+ & z in REAL+;

    A4: (
 ex x',y' being Element of REAL+ st x = x' & y = y' & 0 = x' + y')& ex x',y'
    being Element of REAL+ st x = x' & z = y' & 0 = x' + y' by A1,A2,A3,
    ARYTM_0:def 2;
    thus thesis by A4,ARYTM_2:12;
  end;
  suppose that
A5: x in REAL+ and
A6: y in REAL+ and
A7: z in [:{0},REAL+:];

A8: ex x',y' being Element of REAL+ st x = x' & y = y' & 0 =
    x' + y' by A1,A5,A6,ARYTM_0:def 2;
    consider x'',y'' being Element of REAL+ such that
A9: x = x'' and
A10: z = [0,y''] & 0 = x'' - y'' by A2,A5,A7,ARYTM_0:def 2;
A11: x'' = 0 by A8,A9,ARYTM_2:6;
A12: [{},{}] in {[{},{}]} by TARSKI:def 1;
A13: not [{},{}] in REAL by A12,NUMBERS:def 1,XBOOLE_0:def 5;
A14: z in REAL;
    thus thesis by A10,A11,A13,A14,ARYTM_1:19;
  end;
  suppose that
A15: x in REAL+ and
A16: z in REAL+ and
A17: y in [:{0},REAL+:];

A18: ex x',z' being Element of REAL+ st x = x' & z = z' & 0 =
    x' + z' by A2,A15,A16,ARYTM_0:def 2;
    consider x'',y' being Element of REAL+ such that
A19: x = x'' and
A20: y = [0,y'] & 0 = x'' - y' by A1,A15,A17,ARYTM_0:def 2;
A21: x'' = 0 by A18,A19,ARYTM_2:6;
A22: [0,0] in {[0,0]} by TARSKI:def 1;
A23: not [0,0] in REAL by A22,NUMBERS:def 1,XBOOLE_0:def 5;
A24: y in REAL;
    thus thesis by A20,A21,A23,A24,ARYTM_1:19;
  end;
  suppose that
A25: x in REAL+ and
A26: y in [:{0},REAL+:] and
A27: z in [:{0},REAL+:];
    consider x',y' being Element of REAL+ such that
A28: x = x' and
A29: y = [0,y'] and
A30: 0 = x' - y' by A1,A25,A26,ARYTM_0:def 2;
    consider x'',z' being Element of REAL+ such that
A31: x = x'' and
A32: z = [0,z'] and
A33: 0 = x'' - z' by A2,A25,A27,ARYTM_0:def 2;
A34: y' = x' by A30,ARYTM_0:6
      .= z' by A28,A31,A33,ARYTM_0:6;
    thus thesis by A29,A32,A34;
  end;
  suppose that
A35: z in REAL+ and
A36: y in REAL+ and
A37: x in [:{0},REAL+:];
    consider x',y' being Element of REAL+ such that
A38: x = [0,x'] and
A39: y = y' and
A40: 0 = y' - x' by A1,A36,A37,ARYTM_0:def 2;
    consider x'',z' being Element of REAL+ such that
A41: x = [0,x''] and
A42: z = z' and
A43: 0 = z' - x'' by A2,A35,A37,ARYTM_0:def 2;
A44: x' = x'' by A38,A41,ZFMISC_1:33;
A45: z' = x' by A43,A44,ARYTM_0:6
      .= y' by A40,ARYTM_0:6;
    thus thesis by A39,A42,A45;
  end;
  suppose
A46: not(x in REAL+ & y in REAL+) &
    not(x in REAL+ & y in [:{0},REAL+:]) &
    not(y in REAL+ & x in [:{0},REAL+:]);
A47: ex x',y' being Element of REAL+
    st x = [0,x'] & y = [0,y'] & 0 = [0,x'+y'] by A1,A46,ARYTM_0:def 2;
    thus thesis by A47;
  end;
  suppose
A48: not(x in REAL+ & z in REAL+) &
    not(x in REAL+ & z in [:{0},REAL+:]) &
    not(z in REAL+ & x in [:{0},REAL+:]);
A49: ex x',z' being Element of REAL+
    st x = [0,x'] & z = [0,z'] & 0 = [0,x'+z'] by A2,A48,ARYTM_0:def 2;
    thus thesis by A49;
  end;
end;

registration
  let z,z' be complex number;
  cluster z+z' -> complex;
  coherence
  proof
A1: ex x1,x2,y1,y2 being Element of REAL st
    z = [*x1,x2*] & z' = [*y1,y2*] & z+z' = [*+(x1,y1),+(x2,y2)*] by Def4;
    thus z+z' in COMPLEX by A1;
  end;
  cluster z*z' -> complex;
  coherence
  proof
    A2: ex
 x1,x2,y1,y2 being Element of REAL st z = [*x1,x2*] & z' = [*y1,y2*] &
    z*z' = [* +(*(x1,y1),opp*(x2,y2)), +(*(x1,y2),*(x2,y1)) *] by Def5;
    thus z*z' in COMPLEX by A2;
  end;
end;

definition
  let z be complex number;
A1: z in COMPLEX by Def2;
  consider x,y being Element of REAL such that
A2: z = [*x,y*] by A1,ARYTM_0:11;
  func -z -> complex number means
  :Def6:
  z + it = 0;
  existence
  proof
    reconsider z' = [*opp x, opp y*] as complex number by Def2;
    take z';
A3: 0 = +(x,opp x) & 0 = +(y,opp y) by ARYTM_0:def 4;
    thus thesis by A2,A3,Def4,Lm1;
  end;
  uniqueness
  proof
    let c1,c2 be complex number such that
A4: z+c1 = 0 and
A5: z+c2 = 0;
    consider x1,x2,y1,y2 being Element of REAL such that
A6: z = [*x1,x2*] and
A7: c1 = [*y1,y2*] and
A8: 0 = [*+(x1,y1),+(x2,y2)*] by A4,Def4;
    consider x1',x2',y1',y2' being Element of REAL such that
A9: z = [*x1',x2'*] and
A10: c2 = [*y1',y2'*] and
A11: 0 = [*+(x1',y1'),+(x2',y2')*] by A5,Def4;
A12: x1 = x1' by A6,A9,ARYTM_0:12;
A13: x2 = x2' by A6,A9,ARYTM_0:12;
A14: +(x1,y1) = 0 by A8,Lm1,ARYTM_0:12;
A15: +(x1,y1') = 0 by A11,A12,Lm1,ARYTM_0:12;
A16: y1 = y1' by A14,A15,Lm2;
A17: +(x2,y2) = 0 by A8,Lm1,ARYTM_0:12;
A18: +(x2,y2') = 0 by A11,A13,Lm1,ARYTM_0:12;
    thus thesis by A7,A10,A16,A17,A18,Lm2;
  end;
  involutiveness;
  func z" -> complex number means
  :Def7:
  z*it = 1 if z <> 0 otherwise it = 0;
  existence
  proof
    thus z <> 0 implies ex z' being complex number st z*z' = 1
    proof
      set y1 = *(x,inv +(*(x,x),*(y,y))), y2 = *(opp y,inv +(*(x,x),*(y,y)));
      set z' = [* y1, y2 *];
      reconsider z' as complex number by Def2;
      assume
A19:  z <> 0;
      take z';
A20:  opp *(y,y2) = opp *(y,opp *(y,inv +(*(x,x),*(y,y)))) by ARYTM_0:17
        .= opp opp *(y,*(y,inv +(*(x,x),*(y,y)))) by ARYTM_0:17
        .= *(*(y,y),inv +(*(x,x),*(y,y))) by ARYTM_0:15;
A21:  *(x,y1) = *(*(x,x),inv +(*(x,x),*(y,y))) by ARYTM_0:15;
A22:  now
        assume
A23:    +(*(x,x),*(y,y)) = 0;
A24:    x = 0 & y = 0 by A23,ARYTM_0:19;
        thus contradiction by A2,A19,A24,ARYTM_0:def 7;
      end;
A25:  *(x,y2) = *(opp y,y1) by ARYTM_0:15
        .= opp *(y,y1) by ARYTM_0:17;
A26:  +(*(x,y2),*(y,y1)) = 0 by A25,ARYTM_0:def 4;
A27:  [* +(*(x,y1),opp*(y,y2)), +(*(x,y2),*(y,y1)) *]
      = +(*(x,y1),opp*(y,y2)) by A26,ARYTM_0:def 7
        .=*(+(*(x,x),*(y,y)),inv +(*(x,x),*(y,y))) by A20,A21,ARYTM_0:16
        .= 1 by A22,ARYTM_0:def 5;
      thus thesis by A2,A27,Def5;
    end;
    assume
A28: z = 0;
    thus thesis by A28;
  end;
  uniqueness
  proof
    let c1,c2 be complex number;
    thus z <> 0 & z*c1 = 1 & z*c2 = 1 implies c1 = c2
    proof
      assume that
A29:  z <> 0 and
A30:  z*c1 = 1 and
A31:  z*c2 = 1;
A32:  for z' being complex number st z*z' = 1
holds z' = [* *(x,inv +(*(x,x),*(y,y))) , *(opp y,inv +(*(x,x),*(y,y))) *]
      proof
        let z' being complex number such that
A33:    z*z' = 1;
        consider x1,x2,x',y' being Element of REAL such that
A34:    z = [*x1,x2*] and
A35:    z' = [*x',y'*] and
A36:    1 = [* +(*(x1,x'),opp*(x2,y')), +(*(x1,y'),*(x2,x')) *] by A33,Def5;
A37:    x = x1 & y = x2 by A2,A34,ARYTM_0:12;
        per cases by A2,A29,ARYTM_0:def 7;
        suppose that
A38:      x = 0 and
A39:      y <> 0;
A40:      +(y, opp y) = 0 by ARYTM_0:def 4;
A41:      opp y <> 0 by A39,A40,ARYTM_0:13;
A42:      *(x,y') = 0 & *(x,x') = 0 by A38,ARYTM_0:14;
A43:      1 = [* opp*(y,y'), +(0,*(y,x')) *] by A36,A37,A42,
          ARYTM_0:13
            .= [* opp*(y,y'), *(y,x') *] by ARYTM_0:13;
A44:      1 = [*j,0*] by ARYTM_0:def 7;
A45:      *(opp y,y') = opp*(y,y') by ARYTM_0:17
            .= 1 by A43,A44,ARYTM_0:12;
A46:      y' = inv opp y by A41,A45,ARYTM_0:def 5;
A47:      *(x,x) = 0 by A38,ARYTM_0:14;
A48:      *(opp y,opp inv y) = opp *(y,opp inv y) by ARYTM_0:17
            .= opp opp *(y,inv y) by ARYTM_0:17
            .= 1 by A39,ARYTM_0:def 5;
A49:      inv opp y = opp inv y by A41,A48,ARYTM_0:def 5;
A50:      *(y,x') = 0 by A43,A44,ARYTM_0:12;
          thus z' = [*0,inv opp y*] by A35,A39,A46,A50,ARYTM_0:23
            .= [* 0 , opp *(j,inv y) *] by A49,ARYTM_0:21
            .= [* 0 , opp *(*(y,inv y),inv y) *] by A39,ARYTM_0:def 5
            .= [* 0 , opp *(y,*(inv y,inv y)) *] by ARYTM_0:15
            .= [* 0 , *(opp y,*(inv y,inv y)) *] by ARYTM_0:17
            .= [* 0 , *(opp y,inv *(y,y)) *] by ARYTM_0:24
            .= [* 0 , *(opp y,inv +(0,*(y,y))) *] by ARYTM_0:13
            .= [* *(x,inv +(*(x,x),*(y,y))) , *(opp y,inv +(*(x,x),*(y,y))) *]
          by A38,A47,ARYTM_0:14;
        end;
        suppose that
A51:      opp y = 0 and
A52:      x <> 0;
A53:      +(y,opp y) = 0 by ARYTM_0:def 4;
A54:      y = 0 by A51,A53,ARYTM_0:13;
A55:      *(y,x') = 0 by A54,ARYTM_0:14;
A56:      opp*(y,y') = *(opp y,y') by ARYTM_0:17
            .= 0 by A51,ARYTM_0:14;
A57:      1 = [* *(x,x'), +(*(x,y'),0) *] by A36,A37,A55,A56,
          ARYTM_0:13
            .= [* *(x,x'), *(x,y') *] by ARYTM_0:13;
A58:      1 = [*j,0*] by ARYTM_0:def 7;
A59:      *(x,x') = 1 by A57,A58,ARYTM_0:12;
A60:      x' = inv x by A52,A59,ARYTM_0:def 5;
A61:      *(x,y') = 0 by A57,A58,ARYTM_0:12;
A62:      y' = 0 by A52,A61,ARYTM_0:23;
A63:      *(y,y) = 0 by A54,ARYTM_0:14;
A64:      x' = *(j,inv x) by A60,ARYTM_0:21
            .= *(*(x,inv x),inv x) by A52,ARYTM_0:def 5
            .= *(x,*(inv x,inv x)) by ARYTM_0:15
            .= *(x,inv *(x,x)) by ARYTM_0:24
            .= *(x,inv +(*(x,x),0)) by ARYTM_0:13;
          thus thesis
          by A35,A51,A62,A63,A64,ARYTM_0:14;
        end;
        suppose that
A65:      opp y <> 0 and
A66:      x <> 0;
A67:      now
            assume
A68:        +(*(*(x,x), inv opp y),opp y) = 0;
A69:        +(*(*(x,x), inv opp y),*(opp y,j)) = 0 by A68,ARYTM_0:21;
A70:        +(*(*(x,x), inv opp y),*(opp y,*(opp y, inv opp y))) = 0
            by A65,A69,ARYTM_0:def 5;
A71:        +(*(*(x,x), inv opp y),*(*(opp y,opp y), inv opp y)) = 0
            by A70,ARYTM_0:15;
A72:        *(inv opp y, +(*(x,x),*(opp y,opp y))) = 0 by A71,
            ARYTM_0:16;
A73:        +(*(x,x),*(opp y,opp y)) <> 0 by A66,ARYTM_0:19;
A74:        inv opp y = 0 by A72,A73,ARYTM_0:23;
A75:        *(opp y,inv opp y) = 1 by A65,ARYTM_0:def 5;
            thus contradiction by A74,A75,ARYTM_0:14;
          end;
          reconsider j = 1 as Element of REAL;
A76:      1 = [*j,0*] by ARYTM_0:def 7;
A77:      +(*(x1,y'),*(x2,x')) = 0 by A36,A76,ARYTM_0:12;
A78:      *(x,y') = opp*(y,x') by A37,A77,ARYTM_0:def 4;
A79:      *(x,y') = *(opp y,x') by A78,ARYTM_0:17;
A80:      x' = *(*(x,y'), inv opp y) by A65,A79,ARYTM_0:22
            .= *(x,*(y', inv opp y)) by ARYTM_0:15;
A81:      +
(*(x,*(x,*(y', inv opp y))),opp*(y,y')) = 1 by A36,A37,A76,A80,ARYTM_0:12;
A82:      +(*(*(x,x),*(y', inv opp y)),opp*(y,y')) = 1 by A81,ARYTM_0:15;
A83:      +(*(*(x,x),*(y', inv opp y)),*(opp y,y')) = 1 by A82,ARYTM_0:17;
A84:      +(*(y',*(*(x,x), inv opp y)),*(opp y,y')) = 1 by A83,ARYTM_0:15;
A85:      *(y',+(*(*(x,x), inv opp y),opp y)) = 1 by A84,ARYTM_0:16;
A86:      y' = inv +(*(*(x,x), inv opp y),opp y) by A67,A85,
          ARYTM_0:def 5;
A87:      x' = *(x,inv *(+(*(*(x,x), inv opp y),opp y), opp y)) by
          A80,A86,ARYTM_0:24
.= *(x,inv +(*(*(*(x,x), inv opp y),opp y),*(opp y, opp y))) by ARYTM_0:16
.= *(x,inv +(*(*(*(x,x), inv opp y),opp y),opp*(y, opp y))) by ARYTM_0:17
.= *(x,inv +(*(*(*(x,x), inv opp y),opp y),opp opp*(y,y))) by ARYTM_0:17
            .= *(x,inv +(*(*(x,x), *(inv opp y,opp y)),*(y,y))) by ARYTM_0:15
            .= *(x,inv +(*(*(x,x), j),*(y,y))) by A65,ARYTM_0:def 5
            .= *(x,inv +(*(x,x),*(y,y))) by ARYTM_0:21;
A88:      y' = *(j,inv +(*(*(x,x), inv opp y),opp y)) by A86,ARYTM_0:21
            .= *(*(opp y, inv opp y),inv +(*(*(x,x), inv opp y),opp y))
          by A65,ARYTM_0:def 5
.= *(opp y, *(inv opp y,inv +(*(*(x,x), inv opp y),opp y))) by ARYTM_0:15
.= *(opp y, inv *(opp y,+(*(*(x,x), inv opp y),opp y))) by ARYTM_0:24
            .= *(opp y, inv +(*(opp y,*(*(x,x), inv opp y)),*(opp y,opp y)))
          by ARYTM_0:16
            .= *(opp y, inv +(*(*(x,x),*(opp y, inv opp y)),*(opp y,opp y)))
          by ARYTM_0:15
            .= *(opp y, inv +(*(*(x,x),j),*(opp y,opp y))) by A65,ARYTM_0:def 5
            .= *(opp y, inv +(*(x,x),*(opp y,opp y))) by ARYTM_0:21
            .= *(opp y, inv +(*(x,x),opp *(y,opp y))) by ARYTM_0:17
            .= *(opp y, inv +(*(x,x),opp opp *(y,y))) by ARYTM_0:17
            .= *(opp y, inv +(*(x,x),*(y,y)));
          thus thesis
          by A35,A87,A88;
        end;
      end;
      thus
      c1 = [* *(x,inv +(*(x,x),*(y,y))) , *(opp y,inv +(*(x,x),*(y,y)))
      *] by A30,A32
        .= c2 by A31,A32;
    end;
    thus thesis;
  end;
  consistency;
  involutiveness
  proof
    let z,z' be complex number;
    assume that
A89: z' <> 0 implies z'*z = 1 and
A90: z' = 0 implies z = 0;
    thus z <> 0 implies z*z' = 1 by A89,A90;
    assume
A91: z = 0;
    assume
A92: z' <> 0;
    consider x1,x2,y1,y2 being Element of REAL such that
A93: z = [*x1,x2*] and z' = [*y1,y2*] and
A94: 1 = [*+(*(x1,y1),opp*(x2,y2)), +(*(x1,y2),*(x2,y1))*] by A89,A92,Def5;
A95: z = [*0,0*] by A91,ARYTM_0:def 7;
A96: x1 = 0 by A93,A95,ARYTM_0:12;
A97: x2 = 0 by A93,A95,ARYTM_0:12;
A98: *(x1,y1) = 0 by A96,ARYTM_0:14;
A99: *(x2,y2) = 0 by A97,ARYTM_0:14;
A100: +(*(x1,y1),opp*(x2,y2)) = 0 by A98,A99,ARYTM_0:def 4;
A101: *(x1,y2) = 0 by A96,ARYTM_0:14;
A102: *(x2,y1) = 0 by A97,ARYTM_0:14;
A103: +(*(x1,y2),*(x2,y1)) = 0 by A101,A102,ARYTM_0:13;
    thus contradiction by A94,A100,A103,ARYTM_0:def 7;
  end;
end;

definition
  let x,y be complex number;
  func x-y equals
  x+(-y);
  coherence;
  func x/y equals
  x * y";
  coherence;
end;

registration
  let x,y be complex number;
  cluster x-y -> complex;
  coherence;
  cluster x/y -> complex;
  coherence;
end;

Lm3: for x, y, z being complex number holds x * (y * z) = (x * y) * z
proof
  let x, y, z be complex number;
  consider x1,x2,y1,y2 being Element of REAL such that
A1: x = [*x1,x2*] and
A2: y = [*y1,y2*] and
A3: x*y = [* +(*(x1,y1),opp*(x2,y2)), +(*(x1,y2),*(x2,y1)) *] by Def5;
  consider y3,y4,z1,z2 being Element of REAL such that
A4: y = [*y3,y4*] and
A5: z = [*z1,z2*] and
A6: y*z = [* +(*(y3,z1),opp*(y4,z2)), +(*(y3,z2),*(y4,z1)) *] by Def5;
A7: y1 = y3 by A2,A4,ARYTM_0:12;
A8: y2 = y4 by A2,A4,ARYTM_0:12;
  consider x3,x4,yz1,yz2 being Element of REAL such that
A9: x = [*x3,x4*] and
A10: y*z = [*yz1,yz2*] and
A11: x*(y*z) = [* +(*(x3,yz1),opp*(x4,yz2)), +(*(x3,yz2),*(x4,yz1)) *]
  by Def5;
A12: x1 = x3 by A1,A9,ARYTM_0:12;
A13: x2 = x4 by A1,A9,ARYTM_0:12;
  consider xy1,xy2,z3,z4 being Element of REAL such that
A14: x*y = [*xy1,xy2*] and
A15: z = [*z3,z4*] and
A16: (x*y)*z = [* +(*(xy1,z3),opp*(xy2,z4)), +(*(xy1,z4),*(xy2,z3)) *]
  by Def5;
A17: z1 = z3 by A5,A15,ARYTM_0:12;
A18: z2 = z4 by A5,A15,ARYTM_0:12;
A19: xy1 = +(*(x1,y1),opp*(x2,y2)) by A3,A14,ARYTM_0:12;
A20: xy2 = +(*(x1,y2),*(x2,y1)) by A3,A14,ARYTM_0:12;
A21: yz1 = +(*(y3,z1),opp*(y4,z2)) by A6,A10,ARYTM_0:12;
A22: yz2 = +(*(y3,z2),*(y4,z1)) by A6,A10,ARYTM_0:12;
A23: +(*(opp x4,*(y3,z2)),*(opp x4,*(y4,z1)))
  = +(*(opp x4,*(y4,z1)),*(*(opp x2,y1),z4)) by A7,A13,A18,ARYTM_0:15
    .= +(*(*(opp x2,y2),z3),*(*(opp x2,y1),z4)) by A8,A13,A17,ARYTM_0:15;
A24: +(*(x3,*(opp y4,z2)),+(*(opp x4,*(y3,z2)),*(opp x4,*(y4,z1)) ))
  = +(*(*(x1,opp y2),z4), +(*(*(opp x2,y2),z3),*(*(opp x2,y1),z4)))
  by A8,A12,A18,A23,ARYTM_0:15
    .= +(*(opp *(x1,y2),z4), +(*(*(opp x2,y2),z3),*(*(opp x2,y1),z4)))
  by ARYTM_0:17
    .= +(*(*(opp x1,y2),z4), +(*(*(opp x2,y2),z3),*(*(opp x2,y1),z4)))
  by ARYTM_0:17
    .= +(*(*(opp x2,y2),z3), +(*(*(opp x1,y2),z4),*(*(opp x2,y1),z4)))
  by ARYTM_0:25;
A25: +(*(x3,yz1),opp*(x4,yz2)) = +(*(x3,yz1),*(opp x4,yz2)) by ARYTM_0:17
    .= +(*(x3,+(*(y3,z1),*(opp y4,z2))),*(opp x4,yz2)) by A21,ARYTM_0:17
    .= +(+(*(x3,*(y3,z1)),*(x3,*(opp y4,z2))),
  *(opp x4,+(*(y3,z2),*(y4,z1)))) by A22,ARYTM_0:16
    .= +(+(*(x3,*(y3,z1)),*(x3,*(opp y4,z2))),
  +(*(opp x4,*(y3,z2)),*(opp x4,*(y4,z1)))) by ARYTM_0:16
    .= +(*(x3,*(y3,z1)),+(*(*(opp x2,y2),z3),
  +(*(*(opp x1,y2),z4),*(*(opp x2,y1),z4)))) by A24,ARYTM_0:25
    .= +(+(*(x3,*(y3,z1)),*(*(opp x2,y2),z3)),
  +(*(*(opp x1,y2),z4),*(*(opp x2,y1),z4))) by ARYTM_0:25
    .= +(+(*(*(x1,y1),z3),*(*(opp x2,y2),z3)),
  +(*(*(opp x1,y2),z4),*(*(opp x2,y1),z4))) by A7,A12,A17,ARYTM_0:15
    .= +(*(+(*(x1,y1),*(opp x2,y2)),z3),
  +(*(*(opp x1,y2),z4),*(*(opp x2,y1),z4))) by ARYTM_0:16
    .= +(*(+(*(x1,y1),*(opp x2,y2)),z3),
  +(*(*(opp x1,y2),z4),*(opp *(x2,y1),z4))) by ARYTM_0:17
    .= +(*(+(*(x1,y1),*(opp x2,y2)),z3),
  +(*(*(opp x1,y2),z4),opp *(*(x2,y1),z4))) by ARYTM_0:17
    .= +(*(+(*(x1,y1),*(opp x2,y2)),z3),
  +(*(opp *(x1,y2),z4),opp *(*(x2,y1),z4))) by ARYTM_0:17
    .= +(*(+(*(x1,y1),*(opp x2,y2)),z3),
  +(opp *(*(x1,y2),z4),opp *(*(x2,y1),z4))) by ARYTM_0:17
    .= +(*(+(*(x1,y1),*(opp x2,y2)),z3),
  opp +(*(*(x1,y2),z4),*(*(x2,y1),z4))) by ARYTM_0:27
    .= +(*(+(*(x1,y1),*(opp x2,y2)),z3),
  opp*( +(*(x1,y2),*(x2,y1)),z4)) by ARYTM_0:16
    .= +(*(+(*(x1,y1),*(opp x2,y2)),z3),*(opp xy2,z4)) by A20,ARYTM_0:17
    .= +(*(xy1,z3),*(opp xy2,z4)) by A19,ARYTM_0:17
    .= +(*(xy1,z3),opp*(xy2,z4)) by ARYTM_0:17;
A26: +(*(opp*(x2,y2),z4),*(*(x2,y1),z3))
  = +(opp*(*(x2,y2),z4),*(*(x2,y1),z3)) by ARYTM_0:17
    .= +(*(x4,*(y3,z1)),opp*(*(x2,y2),z4)) by A7,A13,A17,ARYTM_0:15
    .= +(*(x4,*(y3,z1)),opp*(x4,*(y4,z2))) by A8,A13,A18,ARYTM_0:15
    .= +(*(x4,*(y3,z1)),*(x4,opp*(y4,z2))) by ARYTM_0:17;
A27: +(*(opp*(x2,y2),z4),*(xy2,z3))
  = +(*(opp*(x2,y2),z4),+(*(*(x1,y2),z3),*(*(x2,y1),z3))) by A20,ARYTM_0:16
    .= +(*(*(x1,y2),z3),+(*(opp*(x2,y2),z4),*(*(x2,y1),z3))) by ARYTM_0:25
    .= +(*(x3,*(y4,z1)),+(*(x4,*(y3,z1)),*(x4,opp*(y4,z2))))
  by A8,A12,A17,A26,ARYTM_0:15
    .= +(*(x3,*(y4,z1)),*(x4,yz1)) by A21,ARYTM_0:16;
A28: +(*(xy1,z4),*(xy2,z3))
  = +(+(*(*(x1,y1),z4),*(opp*(x2,y2),z4)),*(xy2,z3)) by A19,ARYTM_0:16
    .= +(*(*(x1,y1),z4),+(*(opp*(x2,y2),z4),*(xy2,z3))) by ARYTM_0:25
    .= +(*(x3,*(y3,z2)),+(*(x3,*(y4,z1)),*(x4,yz1)))
  by A7,A12,A18,A27,ARYTM_0:15
    .= +(+(*(x3,*(y3,z2)),*(x3,*(y4,z1))),*(x4,yz1)) by ARYTM_0:25
    .= +(*(x3,yz2),*(x4,yz1)) by A22,ARYTM_0:16;
  thus thesis by A11,A16,A25,A28;
end;

registration
  cluster non zero (complex number);
  existence
  proof
A1: REAL c= COMPLEX & 1 in REAL by NUMBERS:def 2,XBOOLE_1:7;
A2: 1 is complex number by A1,Def2;
    thus thesis by A2;
  end;
end;

Lm4: REAL c= COMPLEX by NUMBERS:def 2,XBOOLE_1:7;

registration
  let x be non zero (complex number);
  cluster -x -> non zero;
  coherence
  proof
    assume
A1: -x = 0;
A2: x + -x = 0 by Def6;
    consider x1,x2,y1,y2 being Element of REAL such that
A3: x = [*x1,x2*] and
A4: -x = [*y1,y2*] and
A5: 0 = [*+(x1,y1),+(x2,y2)*] by A2,Def4;
A6: +(x2,y2) = 0 by A5,ARYTM_0:26;
A7: +(x1,y1) = 0 by A5,A6,ARYTM_0:def 7;
A8: y2 = 0 by A1,A4,ARYTM_0:26;
A9: y1 = 0 by A1,A4,A8,ARYTM_0:def 7;
A10: x2 = 0 by A1,A4,A6,ARYTM_0:13,26;
A11: x = x1 by A3,A10,ARYTM_0:def 7
      .= 0 by A7,A9,ARYTM_0:13;
    thus contradiction by A11;
  end;
  cluster x" -> non zero;
  coherence
  proof
    assume
A12: x" = 0;
A13: x*x" = 1 by Def7;
    consider x1,x2,y1,y2 being Element of REAL such that
    x = [*x1,x2*] and
A14: x" = [*y1,y2*] and
A15: 1 = [* +(*(x1,y1),opp*(x2,y2)), +(*(x1,y2),*(x2,y1)) *] by A13,Def5;
A16: y2 = 0 by A12,A14,ARYTM_0:26;
A17: y1 = 0 by A12,A14,A16,ARYTM_0:def 7;
A18: +(*(x1,y2),*(x2,y1)) = 0 by A15,ARYTM_0:26;
A19: 1 = +(*(x1,y1),opp*(x2,y2)) by A15,A18,ARYTM_0:def 7
      .= +(*(x1,y1),*(opp x2,y2)) by ARYTM_0:17
      .= *(opp x2,y2) by A17,ARYTM_0:13,14;
    thus contradiction by A12,A14,A19,ARYTM_0:14,26;
  end;
  let y be non zero (complex number);
  cluster x*y -> non zero;
  coherence
  proof
A20: 1 in REAL;
    reconsider j =1 as complex number by A20,Def2,Lm4;
    consider u1,u2,v1,v2 being Element of REAL such that
A21: j = [*u1,u2*] and
    A22: y
 = [*v1,v2*] & j*y = [* +(*(u1,v1),opp*(u2,v2)), +(*(u1,v2),*(u2,v1)) *]
    by Def5;
A23: u2 = 0 by A21,ARYTM_0:26;
A24: +(*(u1,v2),*(u2,v1)) = *(u1,v2) by A23,ARYTM_0:13,14;
A25: u1 = 1 by A21,A23,ARYTM_0:def 7;
A26: +(0,opp 0) = 0 by ARYTM_0:def 4;
A27: opp 0 = 0 by A26,ARYTM_0:13;
A28: +(*(u1,v1),opp*(u2,v2)) = +(v1,opp*(u2,v2)) by A25,ARYTM_0:21
      .= +(v1,*(opp u2,v2)) by ARYTM_0:17
      .= +(v1,*(0,v2)) by A21,A27,ARYTM_0:26
      .= v1 by ARYTM_0:13,14;
A29: 0 in REAL;
    reconsider z =0 as complex number by A29,Def2,Lm4;
    consider u1,u2,v1,v2 being Element of REAL such that
    x" = [*u1,u2*] and
A30: z = [*v1,v2*] and
A31: x"*z = [* +(*(u1,v1),opp*(u2,v2)), +(*(u1,v2),*(u2,v1)) *] by Def5;
A32: v2 = 0 by A30,ARYTM_0:26;
A33: v1 = 0 by A30,A32,ARYTM_0:def 7;
A34: +(*(u1,v1),opp*(u2,v2)) = opp*(u2,v2) by A33,ARYTM_0:13,14
      .= 0 by A27,A30,ARYTM_0:14,26;
A35: +(*(u1,v2),*(u2,v1)) = +(0,*(u2,v1)) by A30,ARYTM_0:14,26
      .= *(u2,v1) by ARYTM_0:13
      .= 0 by A33,ARYTM_0:14;
    assume
A36: x*y = 0;
A37: x"*x*y = x"*(x*y) by Lm3
      .= 0 by A31,A34,A35,A36,ARYTM_0:def 7;
A38: x"*x*y = j * y by Def7
      .= y by A22,A24,A25,A28,ARYTM_0:21;
    thus contradiction by A37,A38;
  end;
end;

registration
  let x,y be non zero (complex number);
  cluster x/y -> non zero;
  coherence;
end;

registration
  cluster -> complex Element of REAL;
  coherence
  proof
    let n be Element of REAL;
A1: n in REAL;
    thus thesis by A1,Def2,Lm4;
  end;
end;

registration
  cluster natural -> complex number;
  coherence
  proof
    let n be set;
    assume
A1: n is natural;
A2: n in NAT by A1,ORDINAL1:def 13;
    thus thesis by A2;
  end;
end;

registration
  cluster -> complex Element of COMPLEX;
  coherence by Def2;
end;

