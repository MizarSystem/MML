:: Propositional Calculus for Boolean Valued Functions, II
::  by Shunichi Kobayashi and Yatsuka Nakamura
::
:: Received March 13, 1999
:: Copyright (c) 1999 Association of Mizar Users

environ

 vocabularies FUNCT_2, MARGREL1, BVFUNC_1, ZF_LANG, BINARITH, FUNCT_1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, XREAL_0, MARGREL1,
      VALUAT_1, RELAT_1, FUNCT_1, FUNCT_2, FRAENKEL, BINARITH,
      SEQM_3, BVFUNC_1;
 constructors EQREL_1, BINARITH, BVFUNC_1, XREAL_0, ARYTM_3, SETFAM_1, PARTIT1,
  ORDINAL2, SEQM_3, T_1TOPSP;
 registrations SUBSET_1, MARGREL1, XREAL_0, ARYTM_3, VALUAT_1, BINARITH,
      FRAENKEL, EQREL_1;
 theorems MARGREL1, BINARITH, BVFUNC_1, VALUAT_1;

begin

::Chap. 1  Propositional Calculus

reserve Y for non empty set;

theorem  for a,b being Element of Funcs(Y,BOOLEAN) holds
a 'imp' (b 'imp' (a '&' b))=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds
    (a 'imp' (b 'imp' (a '&' b))).x=TRUE
  proof
    let x be Element of Y;
       (a 'imp' (b 'imp' (a '&' b))).x
    ='not' (a).x 'or' (b 'imp' (a '&' b)).x by BVFUNC_1:def 11
   .='not' (a).x 'or' ('not' b.x 'or' (a '&' b).x) by BVFUNC_1:def 11
   .='not' (a).x 'or' ('not'
(b).x 'or' ((a).x '&' (b).x)) by VALUAT_1:def 6
   .='not' (a).x 'or' (('not' (b).x 'or' (a).x) '&' ('not'
(b).x 'or' (b).x))
     by BINARITH:23
   .='not' (a).x 'or' (TRUE '&' ('not' (b).x 'or' (a).x)) by BINARITH:18
   .='not' (a).x 'or' ((a).x 'or' 'not' (b).x) by MARGREL1:50
   .=('not' (a).x 'or' (a).x) 'or' 'not' (b).x
     by BINARITH:20
   .=TRUE 'or' 'not' (b).x
     by BINARITH:18
   .=TRUE by BINARITH:19;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b being Element of Funcs(Y,BOOLEAN) holds
(a 'imp' b) 'imp' ((b 'imp' a) 'imp' (a 'eqv' b))=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds
    ((a 'imp' b) 'imp' ((b 'imp' a) 'imp' (a 'eqv' b))).x=TRUE
  proof
    let x be Element of Y;
       ((a 'imp' b) 'imp' ((b 'imp' a) 'imp' (a 'eqv' b))).x
    ='not' (a 'imp' b).x 'or' ((b 'imp' a) 'imp' (a 'eqv' b)).x
     by BVFUNC_1:def 11
   .='not'( 'not' (a).x 'or' (b).x) 'or'
   ((b 'imp' a) 'imp' (a 'eqv' b)).x
     by BVFUNC_1:def 11
   .='not'( 'not' (a).x 'or' (b).x) 'or' ('not'
(b 'imp' a).x 'or' (a 'eqv' b).x)
     by BVFUNC_1:def 11
   .='not'( 'not' (a).x 'or' (b).x) 'or'
     ('not'( 'not' (b).x 'or' (a).x) 'or' (a 'eqv' b).x)
     by BVFUNC_1:def 11
   .=('not' 'not' (a).x '&' 'not' (b).x) 'or'
     ('not'( 'not' (b).x 'or' (a).x) 'or' (a 'eqv' b).x)
     by BINARITH:10
   .=('not' 'not' (a).x '&' 'not' (b).x) 'or'
     (('not' 'not' (b).x '&' 'not' (a).x) 'or' (a 'eqv' b).x)
     by BINARITH:10
   .=((a).x '&' 'not' (b).x) 'or'
     (('not' 'not' (b).x '&' 'not' (a).x) 'or' (a 'eqv' b).x)
     by MARGREL1:40
   .=((a).x '&' 'not' (b).x) 'or'
     (((b).x '&' 'not' (a).x) 'or' (a 'eqv' b).x)
     by MARGREL1:40
   .=((a).x '&' 'not' (b).x) 'or' (((b).x '&' 'not' (a).x) 'or'
      'not'( (a).x 'xor' (b).x))
     by BVFUNC_1:def 12
   .=((a).x '&' 'not' (b).x) 'or' (((b).x '&' 'not' (a).x) 'or'
      'not'( ('not' (a).x '&' (b).x) 'or' ((a).x '&' 'not' (b).x)))
     by BINARITH:def 2
   .=((a).x '&' 'not' (b).x) 'or' (('not' (a).x '&' (b).x) 'or'
      ('not'( 'not' (a).x '&' (b).x) '&' 'not'( (a).x '&' 'not'
(b).x))) by BINARITH:10
   .=((a).x '&' 'not' (b).x) 'or'
    ((('not' (a).x '&' (b).x) 'or' 'not'( 'not' (a).x '&' (b).x)) '&'
     (('not' (a).x '&' (b).x) 'or' 'not'( (a).x '&' 'not' (b).x)))
     by BINARITH:23
   .=((a).x '&' 'not' (b).x) 'or'
    (TRUE '&'
     (('not' (a).x '&' (b).x) 'or' 'not'( (a).x '&' 'not' (b).x)))
     by BINARITH:18
   .=((a).x '&' 'not' (b).x) 'or'
     ('not'( (a).x '&' 'not' (b).x) 'or' ('not'
(a).x '&' (b).x)) by MARGREL1:50
   .=(((a).x '&' 'not' (b).x) 'or'
     'not'( (a).x '&' 'not' (b).x)) 'or' ('not' (a).x '&' (b).x)
     by BINARITH:20
   .=TRUE 'or' ('not' (a).x '&' (b).x)
     by BINARITH:18
   .=TRUE by BINARITH:19;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b being Element of Funcs(Y,BOOLEAN) holds
(a 'or' b) 'eqv' (b 'or' a)=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds
    ((a 'or' b) 'eqv' (b 'or' a)).x=TRUE
  proof
    let x be Element of Y;
       ((a 'or' b) 'eqv' (b 'or' a)).x
    ='not'( (a 'or' b).x 'xor' (b 'or' a).x)
    by BVFUNC_1:def 12
   .='not'( ('not' (a 'or' b).x '&' (b 'or' a).x) 'or'
       ((a 'or' b).x '&' 'not' (b 'or' a).x))
    by BINARITH:def 2
   .=('not'( 'not' (a 'or' b).x '&' (b 'or' a).x) '&'
      'not'( (a 'or' b).x '&' 'not' (b 'or' a).x))
    by BINARITH:10
   .=(('not' 'not' (a 'or' b).x 'or' 'not' (b 'or' a).x) '&'
      'not'( (a 'or' b).x '&' 'not' (b 'or' a).x))
    by BINARITH:9
   .=(('not' 'not' (a 'or' b).x 'or' 'not' (b 'or' a).x) '&'
      ('not' (a 'or' b).x 'or' 'not' 'not' (b 'or' a).x))
    by BINARITH:9
   .=(((a 'or' b).x 'or' 'not' (b 'or' a).x) '&'
      ('not' (a 'or' b).x 'or' 'not' 'not' (b 'or' a).x))
    by MARGREL1:40
   .=TRUE '&'
      ('not' (a 'or' b).x 'or' (a 'or' b).x)
    by BINARITH:18
   .=TRUE '&' TRUE by BINARITH:18
   .=TRUE by MARGREL1:45;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b,c being Element of Funcs(Y,BOOLEAN) holds
((a '&' b) 'imp' c) 'imp' (a 'imp' (b 'imp' c))=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds
    (((a '&' b) 'imp' c) 'imp' (a 'imp' (b 'imp' c))).x=TRUE
  proof
    let x be Element of Y;
       (((a '&' b) 'imp' c) 'imp' (a 'imp' (b 'imp' c))).x
    ='not' ((a '&' b) 'imp' c).x 'or' (a 'imp' (b 'imp' c)).x
    by BVFUNC_1:def 11
   .='not'( 'not' (a '&' b).x 'or' (c).x) 'or' (a 'imp' (b 'imp' c)).x
    by BVFUNC_1:def 11
   .='not'( 'not'
((a).x '&' (b).x) 'or' (c).x) 'or' (a 'imp' (b 'imp' c)).x
    by VALUAT_1:def 6
   .='not'( 'not'( (a).x '&' (b).x) 'or' (c).x) 'or'
     ('not' (a).x 'or' (b 'imp' c).x)
    by BVFUNC_1:def 11
   .='not'( 'not'( (a).x '&' (b).x) 'or' (c).x) 'or'
     ('not' (a).x 'or' ('not' (b).x 'or' (c).x))
    by BVFUNC_1:def 11
   .='not'( ('not' (a).x 'or' 'not' (b).x) 'or' (c).x) 'or'
     ('not' (a).x 'or' ('not' (b).x 'or' (c).x))
    by BINARITH:9
   .='not'( ('not' (a).x 'or' 'not' (b).x) 'or' (c).x) 'or'
      (('not' (a).x 'or' 'not' (b).x) 'or' (c).x)
    by BINARITH:20
   .=TRUE by BINARITH:18;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b,c being Element of Funcs(Y,BOOLEAN) holds
(a 'imp' (b 'imp' c)) 'imp' ((a '&' b) 'imp' c)=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds
    ((a 'imp' (b 'imp' c)) 'imp' ((a '&' b) 'imp' c)).x=TRUE
  proof
    let x be Element of Y;
       ((a 'imp' (b 'imp' c)) 'imp' ((a '&' b) 'imp' c)).x
    ='not' (a 'imp' (b 'imp' c)).x 'or' ((a '&' b) 'imp' c).x
    by BVFUNC_1:def 11
   .='not'( 'not' (a).x 'or' (b 'imp' c).x) 'or'
   ((a '&' b) 'imp' c).x
    by BVFUNC_1:def 11
   .='not'( 'not' (a).x 'or' ('not'
(b).x 'or' (c).x)) 'or' ((a '&' b) 'imp' c).x
    by BVFUNC_1:def 11
   .='not'( 'not' (a).x 'or' ('not' (b).x 'or' (c).x)) 'or'
     ('not' (a '&' b).x 'or' (c).x)
    by BVFUNC_1:def 11
   .='not'( 'not' (a).x 'or' ('not' (b).x 'or' (c).x)) 'or'
     ('not'( (a).x '&' (b).x) 'or' (c).x)
    by VALUAT_1:def 6
   .='not'( 'not' (a).x 'or' ('not' (b).x 'or' (c).x)) 'or'
     (('not' (a).x 'or' 'not' (b).x) 'or' (c).x)
    by BINARITH:9
   .='not'( ('not' (a).x 'or' 'not' (b).x) 'or' (c).x) 'or'
      (('not' (a).x 'or' 'not' (b).x) 'or' (c).x)
    by BINARITH:20
   .=TRUE by BINARITH:18;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b,c being Element of Funcs(Y,BOOLEAN) holds
(c 'imp' a) 'imp' ((c 'imp' b) 'imp' (c 'imp' (a '&' b)))=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds
    ((c 'imp' a) 'imp' ((c 'imp' b) 'imp' (c 'imp' (a '&' b)))).x=TRUE
  proof
    let x be Element of Y;
       ((c 'imp' a) 'imp' ((c 'imp' b) 'imp' (c 'imp' (a '&' b)))).x
    ='not' (c 'imp' a).x 'or' ((c 'imp' b) 'imp' (c 'imp' (a '&' b))).x
    by BVFUNC_1:def 11
   .='not'( 'not' (c).x 'or' (a).x) 'or'
     ((c 'imp' b) 'imp' (c 'imp' (a '&' b))).x
    by BVFUNC_1:def 11
   .='not'( 'not' (c).x 'or' (a).x) 'or'
     ('not' (c 'imp' b).x 'or' (c 'imp' (a '&' b)).x)
    by BVFUNC_1:def 11
   .='not'( 'not' (c).x 'or' (a).x) 'or'
     ('not'( 'not' (c).x 'or' (b).x) 'or' (c 'imp' (a '&' b)).x)
    by BVFUNC_1:def 11
   .='not'( 'not' (c).x 'or' (a).x) 'or'
     ('not'( 'not' (c).x 'or' (b).x) 'or' ('not' (c).x 'or'
     (a '&' b).x))
    by BVFUNC_1:def 11
   .='not'( 'not' (c).x 'or' (a).x) 'or'
     ('not'( 'not' (c).x 'or' (b).x) 'or' ('not'
(c).x 'or' ((a).x '&' (b).x)))
    by VALUAT_1:def 6
   .=('not' 'not' (c).x '&' 'not' (a).x) 'or'
     ('not'( 'not' (c).x 'or' (b).x) 'or' ('not'
(c).x 'or' ((a).x '&' (b).x)))
    by BINARITH:10
   .=('not' 'not' (c).x '&' 'not' (a).x) 'or'
     (('not' 'not' (c).x '&' 'not' (b).x) 'or' ('not'
(c).x 'or' ((a).x '&' (b).x)))
    by BINARITH:10
   .=((c).x '&' 'not' (a).x) 'or'
     (('not' 'not' (c).x '&' 'not' (b).x) 'or' ('not'
(c).x 'or' ((a).x '&' (b).x)))
    by MARGREL1:40
   .=((c).x '&' 'not' (a).x) 'or'
     (((c).x '&' 'not' (b).x) 'or' ('not'
(c).x 'or' ((a).x '&' (b).x)))
    by MARGREL1:40
   .=((c).x '&' 'not' (a).x) 'or'
     (((c).x '&' 'not' (b).x) 'or'
      (('not' (c).x 'or' (a).x) '&' ('not' (c).x 'or' (b).x)))
    by BINARITH:23
   .=((c).x '&' 'not' (a).x) 'or'
     ((((c).x '&' 'not' (b).x) 'or' ('not' (c).x 'or' (a).x)) '&'
      (((c).x '&' 'not' (b).x) 'or' ('not' (c).x 'or' (b).x)))
    by BINARITH:23
   .=((c).x '&' 'not' (a).x) 'or'
     ((((c).x '&' 'not' (b).x) 'or' ('not' (c).x 'or' (a).x)) '&'
      (((c).x '&' 'not' (b).x) 'or' ('not'
      (c).x 'or' 'not' 'not' (b).x)))
    by MARGREL1:40
   .=((c).x '&' 'not' (a).x) 'or'
     ((((c).x '&' 'not' (b).x) 'or' ('not' (c).x 'or' (a).x)) '&'
      (((c).x '&' 'not' (b).x) 'or' 'not'( (c).x '&' 'not' (b).x)))
    by BINARITH:9
   .=((c).x '&' 'not' (a).x) 'or'
     (TRUE '&'
      (((c).x '&' 'not' (b).x) 'or' ('not'
(c).x 'or' (a).x))) by BINARITH:18
   .=((c).x '&' 'not' (a).x) 'or'
     (('not' (c).x 'or' (a).x) 'or' ((c).x '&' 'not'
(b).x)) by MARGREL1:50
   .=(((c).x '&' 'not' (a).x) 'or' ('not' (c).x 'or' (a).x)) 'or'
     ((c).x '&' 'not' (b).x)
    by BINARITH:20
   .=(((c).x '&' 'not' (a).x) 'or' ('not' (c).x 'or' 'not' 'not'
(a).x)) 'or'
     ((c).x '&' 'not' (b).x)
    by MARGREL1:40
   .=(((c).x '&' 'not' (a).x) 'or' 'not'( (c).x '&' 'not' (a).x)) 'or'
     ((c).x '&' 'not' (b).x)
    by BINARITH:9
   .=TRUE 'or'
     ((c).x '&' 'not' (b).x)
    by BINARITH:18
   .=TRUE by BINARITH:19;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b,c being Element of Funcs(Y,BOOLEAN) holds
((a 'or' b) 'imp' c) 'imp' ((a 'imp' c) 'or' (b 'imp' c))=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds
    (((a 'or' b) 'imp' c) 'imp' ((a 'imp' c) 'or' (b 'imp' c))).x=TRUE
  proof
    let x be Element of Y;
       (((a 'or' b) 'imp' c) 'imp' ((a 'imp' c) 'or' (b 'imp' c))).x
    ='not' ((a 'or' b) 'imp' c).x 'or' ((a 'imp' c) 'or' (b 'imp' c)).x
    by BVFUNC_1:def 11
   .='not'( 'not' (a 'or' b).x 'or' (c).x) 'or'
      ((a 'imp' c) 'or' (b 'imp' c)).x
    by BVFUNC_1:def 11
   .='not'( 'not'( (a).x 'or' (b).x) 'or' (c).x) 'or'
      ((a 'imp' c) 'or' (b 'imp' c)).x
    by BVFUNC_1:def 7
   .='not'( 'not'( (a).x 'or' (b).x) 'or' (c).x) 'or'
      ((a 'imp' c).x 'or' (b 'imp' c).x)
    by BVFUNC_1:def 7
   .='not'( 'not'( (a).x 'or' (b).x) 'or' (c).x) 'or'
      (('not' (a).x 'or' (c).x) 'or' (b 'imp' c).x)
    by BVFUNC_1:def 11
   .='not'( 'not'( (a).x 'or' (b).x) 'or' (c).x) 'or'
      (('not' (a).x 'or' (c).x) 'or' ('not' (b).x 'or' (c).x))
    by BVFUNC_1:def 11
   .=('not' 'not'( (a).x 'or' (b).x) '&' 'not' (c).x) 'or'
      (('not' (a).x 'or' (c).x) 'or' ('not' (b).x 'or' (c).x))
    by BINARITH:10
   .=('not' (c).x '&' ((a).x 'or' (b).x)) 'or'
      (('not' (a).x 'or' (c).x) 'or' ('not'
(b).x 'or' (c).x)) by MARGREL1:40
   .=(((b).x '&' 'not' (c).x) 'or' ((a).x '&' 'not' (c).x)) 'or'
      (('not' (a).x 'or' (c).x) 'or' ('not'
(b).x 'or' (c).x)) by BINARITH:22
   .=(((b).x '&' 'not' (c).x) 'or' ((a).x '&' 'not' (c).x)) 'or'
      (('not' (a).x 'or' 'not' 'not' (c).x) 'or' ('not'
      (b).x 'or' (c).x))
    by MARGREL1:40
   .=(((b).x '&' 'not' (c).x) 'or' ((a).x '&' 'not' (c).x)) 'or'
      ('not'( (a).x '&' 'not' (c).x) 'or' ('not' (b).x 'or' (c).x))
    by BINARITH:9
   .=((((b).x '&' 'not' (c).x) 'or' ((a).x '&' 'not' (c).x)) 'or'
      'not'( (a).x '&' 'not' (c).x)) 'or' ('not' (b).x 'or' (c).x)
    by BINARITH:20
   .=(((b).x '&' 'not' (c).x) 'or' (((a).x '&' 'not' (c).x) 'or'
      'not'( (a).x '&' 'not' (c).x))) 'or' ('not' (b).x 'or' (c).x)
    by BINARITH:20
   .=(((b).x '&' 'not' (c).x) 'or' TRUE) 'or' ('not' (b).x 'or' (c).x)
    by BINARITH:18
   .=TRUE 'or' ('not' (b).x 'or' (c).x)
    by BINARITH:19
   .=TRUE by BINARITH:19;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b,c being Element of Funcs(Y,BOOLEAN) holds
(a 'imp' c) 'imp' ((b 'imp' c) 'imp' ((a 'or' b) 'imp' c))=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds
    ((a 'imp' c) 'imp' ((b 'imp' c) 'imp' ((a 'or' b) 'imp' c))).x=TRUE
  proof
    let x be Element of Y;
       ((a 'imp' c) 'imp' ((b 'imp' c) 'imp' ((a 'or' b) 'imp' c))).x
    ='not' (a 'imp' c).x 'or' ((b 'imp' c) 'imp' ((a 'or' b) 'imp' c)).x
    by BVFUNC_1:def 11
   .='not'( 'not' (a).x 'or' (c).x) 'or'
     ((b 'imp' c) 'imp' ((a 'or' b) 'imp' c)).x
    by BVFUNC_1:def 11
   .='not'( 'not' (a).x 'or' (c).x) 'or'
     ('not' (b 'imp' c).x 'or' ((a 'or' b) 'imp' c).x)
    by BVFUNC_1:def 11
   .='not'( 'not' (a).x 'or' (c).x) 'or'
     ('not'( 'not' (b).x 'or' (c).x) 'or' ((a 'or' b) 'imp' c).x)
    by BVFUNC_1:def 11
   .='not'( 'not' (a).x 'or' (c).x) 'or'
     ('not'( 'not' (b).x 'or' (c).x) 'or' ('not' (a 'or' b).x 'or'
     (c).x))
    by BVFUNC_1:def 11
   .='not'( 'not' (a).x 'or' (c).x) 'or'
     ('not'( 'not' (b).x 'or' (c).x) 'or' ('not'
((a).x 'or' (b).x) 'or' (c).x))
    by BVFUNC_1:def 7
   .='not'( 'not' (a).x 'or' (c).x) 'or'
     ('not'( 'not' (b).x 'or' (c).x) 'or' ((c).x 'or' ('not' (a).x '&'
'not' (b).x))) by BINARITH:10
   .='not'( 'not' (a).x 'or' (c).x) 'or'
     ('not'( 'not' (b).x 'or' (c).x) 'or'
     (((c).x 'or' 'not' (a).x) '&' ('not'
(b).x 'or' (c).x))) by BINARITH:23
   .='not'( 'not' (a).x 'or' (c).x) 'or'
     (('not'( 'not' (b).x 'or' (c).x) 'or' ((c).x 'or' 'not'
     (a).x)) '&'
      ('not'( 'not' (b).x 'or' (c).x) 'or' ('not' (b).x 'or'
      (c).x)))
    by BINARITH:23
   .='not'( 'not' (a).x 'or' (c).x) 'or'
     (TRUE '&'
      ('not'( 'not' (b).x 'or' (c).x) 'or' ((c).x 'or' 'not'
(a).x))) by BINARITH:18
   .='not'( 'not' (a).x 'or' (c).x) 'or'
      ('not'( 'not' (b).x 'or' (c).x) 'or' ('not'
(a).x 'or' (c).x)) by MARGREL1:50
   .=('not'( 'not' (a).x 'or' (c).x) 'or'
      ('not' (a).x 'or' (c).x)) 'or' 'not'( 'not' (b).x 'or' (c).x)
    by BINARITH:20
   .=TRUE 'or' 'not'( 'not' (b).x 'or' (c).x)
    by BINARITH:18
   .=TRUE by BINARITH:19;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b,c being Element of Funcs(Y,BOOLEAN) holds
((a 'imp' c) '&' (b 'imp' c)) 'imp' ((a 'or' b) 'imp' c)=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds
    (((a 'imp' c) '&' (b 'imp' c)) 'imp' ((a 'or' b) 'imp' c)).x=TRUE
  proof
    let x be Element of Y;
       (((a 'imp' c) '&' (b 'imp' c)) 'imp' ((a 'or' b) 'imp' c)).x
    ='not' ((a 'imp' c) '&' (b 'imp' c)).x 'or' ((a 'or' b) 'imp' c).x
    by BVFUNC_1:def 11
   .='not'( (a 'imp' c).x '&' (b 'imp' c).x) 'or' ((a 'or' b) 'imp' c).x
    by VALUAT_1:def 6
   .='not'( ('not' (a).x 'or' (c).x) '&' (b 'imp' c).x) 'or'
     ((a 'or' b) 'imp' c).x by BVFUNC_1:def 11
   .='not'( ('not' (a).x 'or' (c).x) '&' ('not' (b).x 'or' (c).x)) 'or'
     ((a 'or' b) 'imp' c).x by BVFUNC_1:def 11
   .='not'( ('not' (a).x 'or' (c).x) '&' ('not' (b).x 'or' (c).x)) 'or'
     ('not' (a 'or' b).x 'or' (c).x)
    by BVFUNC_1:def 11
   .='not'( ('not' (a).x 'or' (c).x) '&' ('not' (b).x 'or' (c).x)) 'or'
     ('not'( (a).x 'or' (b).x) 'or' (c).x)
    by BVFUNC_1:def 7
   .=('not'( 'not' (a).x 'or' (c).x) 'or' 'not'( 'not'
(b).x 'or' (c).x)) 'or'
     ((c).x 'or' 'not'( (a).x 'or' (b).x)) by BINARITH:9
   .=('not'( 'not' (a).x 'or' (c).x) 'or' 'not'( 'not'
(b).x 'or' (c).x)) 'or'
     ((c).x 'or' ('not' (a).x '&' 'not' (b).x)) by BINARITH:10
   .=('not'( 'not' (a).x 'or' (c).x) 'or' 'not'( 'not'
(b).x 'or' (c).x)) 'or'
     (('not' (a).x 'or' (c).x) '&' ((c).x 'or' 'not'
(b).x)) by BINARITH:23
   .=(('not'( 'not' (a).x 'or' (c).x) 'or' 'not'( 'not'
(b).x 'or' (c).x)) 'or'
     ('not' (a).x 'or' (c).x)) '&'
     (('not'( 'not' (a).x 'or' (c).x) 'or' 'not'( 'not'
(b).x 'or' (c).x)) 'or'
     ('not' (b).x 'or' (c).x)) by BINARITH:23
   .=('not'( 'not' (b).x 'or' (c).x) 'or' ('not'( 'not'
(a).x 'or' (c).x) 'or'
     ('not' (a).x 'or' (c).x))) '&'
     (('not'( 'not' (a).x 'or' (c).x) 'or' 'not'( 'not'
(b).x 'or' (c).x)) 'or'
     ('not' (b).x 'or' (c).x))
    by BINARITH:20
   .=('not'( 'not' (b).x 'or' (c).x) 'or' ('not'( 'not'
(a).x 'or' (c).x) 'or'
     ('not' (a).x 'or' (c).x))) '&'
     ('not'( 'not' (a).x 'or' (c).x) 'or' ('not'( 'not'
(b).x 'or' (c).x) 'or'
     ('not' (b).x 'or' (c).x)))
    by BINARITH:20
   .=('not'( 'not' (b).x 'or' (c).x) 'or' TRUE) '&'
     ('not'( 'not' (a).x 'or' (c).x) 'or' ('not'( 'not'
(b).x 'or' (c).x) 'or'
     ('not' (b).x 'or' (c).x)))
    by BINARITH:18
   .=('not'( 'not' (b).x 'or' (c).x) 'or' TRUE) '&'
     ('not'( 'not' (a).x 'or' (c).x) 'or' TRUE) by BINARITH:18
   .=TRUE '&' ('not'( 'not' (a).x 'or' (c).x) 'or' TRUE) by BINARITH:19
   .=TRUE '&' TRUE by BINARITH:19
   .=TRUE by MARGREL1:45;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b being Element of Funcs(Y,BOOLEAN) holds
(a 'imp' (b '&' 'not' b)) 'imp' 'not' a=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds
    ((a 'imp' (b '&' 'not' b)) 'imp' 'not' a).x=TRUE
  proof
    let x be Element of Y;
       ((a 'imp' (b '&' 'not' b)) 'imp' 'not' a).x
    ='not' (a 'imp' (b '&' 'not' b)).x 'or' ('not' a).x by BVFUNC_1:def 11
   .='not'( 'not' (a).x 'or' (b '&' 'not' b).x) 'or' ('not'
a).x by BVFUNC_1:def 11
   .='not'( 'not' (a).x 'or' ((b).x '&' ('not' b).x)) 'or' ('not'
a).x by VALUAT_1:def 6
   .=('not' 'not' (a).x '&' 'not'( (b).x '&' ('not' b).x)) 'or' ('not'
a).x by BINARITH:10
   .=((a).x '&' 'not'( (b).x '&' ('not' b).x)) 'or' ('not'
a).x by MARGREL1:40
   .=((a).x '&' ('not' (b).x 'or' 'not' ('not' b).x)) 'or' ('not'
a).x by BINARITH:9
   .=((a).x '&' ('not' (b).x 'or' 'not' 'not' (b).x)) 'or' ('not'
a).x by VALUAT_1:def 5
   .=((a).x '&' TRUE) 'or' ('not' a).x by BINARITH:18
   .=(a).x 'or' ('not' a).x by MARGREL1:50
   .=(a).x 'or' 'not' (a).x by VALUAT_1:def 5
   .=TRUE by BINARITH:18;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b,c being Element of Funcs(Y,BOOLEAN) holds
((a 'or' b) '&' (a 'or' c)) 'imp' (a 'or' (b '&' c))=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds
    (((a 'or' b) '&' (a 'or' c)) 'imp' (a 'or' (b '&' c))).x=TRUE
  proof
    let x be Element of Y;
       (((a 'or' b) '&' (a 'or' c)) 'imp' (a 'or' (b '&' c))).x
    ='not' ((a 'or' b) '&' (a 'or' c)).x 'or' (a 'or' (b '&' c)).x
    by BVFUNC_1:def 11
   .='not'( (a 'or' b).x '&' (a 'or' c).x) 'or' (a 'or' (b '&' c)).x
    by VALUAT_1:def 6
   .='not'
(((a).x 'or' (b).x) '&' (a 'or' c).x) 'or' (a 'or' (b '&' c)).x
    by BVFUNC_1:def 7
   .='not'( ((a).x 'or' (b).x) '&' ((a).x 'or' (c).x)) 'or'
     (a 'or' (b '&' c)).x
    by BVFUNC_1:def 7
   .='not'( ((a).x 'or' (b).x) '&' ((a).x 'or' (c).x)) 'or'
     ((a).x 'or' (b '&' c).x)
    by BVFUNC_1:def 7
   .='not'( ((a).x 'or' (b).x) '&' ((a).x 'or' (c).x)) 'or'
     ((a).x 'or' ((b).x '&' (c).x))
    by VALUAT_1:def 6
   .='not'( ((a).x 'or' (b).x) '&' ((a).x 'or' (c).x)) 'or'
     (((a).x 'or' (b).x) '&' ((a).x 'or' (c).x))
    by BINARITH:23
   .=('not'( (a).x 'or' (b).x) 'or' 'not'( (a).x 'or' (c).x)) 'or'
     (((a).x 'or' (b).x) '&' ((a).x 'or' (c).x))
    by BINARITH:9
   .=(('not'( (a).x 'or' (c).x) 'or' 'not'( (a).x 'or' (b).x)) 'or'
     ((a).x 'or' (b).x)) '&'
     (('not'( (a).x 'or' (b).x) 'or' 'not'( (a).x 'or' (c).x)) 'or'
     ((a).x 'or' (c).x)) by BINARITH:23
   .=('not'( (a).x 'or' (c).x) 'or' ('not'( (a).x 'or' (b).x) 'or'
     ((a).x 'or' (b).x))) '&'
     (('not'( (a).x 'or' (b).x) 'or' 'not'( (a).x 'or' (c).x)) 'or'
     ((a).x 'or' (c).x))
    by BINARITH:20
   .=('not'( (a).x 'or' (c).x) 'or' ('not'( (a).x 'or' (b).x) 'or'
     ((a).x 'or' (b).x))) '&'
     ('not'( (a).x 'or' (b).x) 'or' ('not'( (a).x 'or' (c).x) 'or'
     ((a).x 'or' (c).x)))
    by BINARITH:20
   .=('not'( (a).x 'or' (c).x) 'or' TRUE) '&'
     ('not'( (a).x 'or' (b).x) 'or' ('not'( (a).x 'or' (c).x) 'or'
     ((a).x 'or' (c).x)))
    by BINARITH:18
   .=('not'( (a).x 'or' (c).x) 'or' TRUE) '&'
     ('not'( (a).x 'or' (b).x) 'or' TRUE) by BINARITH:18
   .=TRUE '&' ('not'( (a).x 'or' (b).x) 'or' TRUE) by BINARITH:19
   .=TRUE '&' TRUE by BINARITH:19
   .=TRUE by MARGREL1:45;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b,c being Element of Funcs(Y,BOOLEAN) holds
(a '&' (b 'or' c)) 'imp' ((a '&' b) 'or' (a '&' c))=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds
    ((a '&' (b 'or' c)) 'imp' ((a '&' b) 'or' (a '&' c))).x=TRUE
  proof
    let x be Element of Y;
       ((a '&' (b 'or' c)) 'imp' ((a '&' b) 'or' (a '&' c))).x
    ='not' (a '&' (b 'or' c)).x 'or' ((a '&' b) 'or' (a '&' c)).x
    by BVFUNC_1:def 11
   .='not'( (a).x '&' (b 'or' c).x) 'or' ((a '&' b) 'or' (a '&' c)).x
    by VALUAT_1:def 6
   .='not'
((a).x '&' ((b).x 'or' (c).x)) 'or' ((a '&' b) 'or' (a '&' c)).x
    by BVFUNC_1:def 7
   .='not'( (a).x '&' ((b).x 'or' (c).x)) 'or'
     ((a '&' b).x 'or' (a '&' c).x) by BVFUNC_1:def 7
   .='not'( (a).x '&' ((b).x 'or' (c).x)) 'or'
     (((a).x '&' (b).x) 'or' (a '&' c).x)
    by VALUAT_1:def 6
   .='not'( (a).x '&' ((b).x 'or' (c).x)) 'or'
     (((a).x '&' (b).x) 'or' ((a).x '&' (c).x))
    by VALUAT_1:def 6
   .='not'( ((a).x '&' (b).x) 'or' ((a).x '&' (c).x)) 'or'
     (((a).x '&' (b).x) 'or' ((a).x '&' (c).x))
    by BINARITH:22
   .= (((a).x '&' (b).x) 'or' ((a).x '&' (c).x)) 'or'
      ('not'( (a).x '&' (b).x) '&' 'not'
((a).x '&' (c).x)) by BINARITH:10
   .= ((((a).x '&' (c).x) 'or' ((a).x '&' (b).x)) 'or'
      'not'( (a).x '&' (b).x)) '&'
      ((((a).x '&' (b).x) 'or' ((a).x '&' (c).x)) 'or'
      'not'( (a).x '&' (c).x)) by BINARITH:23
   .= (((a).x '&' (c).x) 'or' (((a).x '&' (b).x) 'or'
      'not'( (a).x '&' (b).x))) '&'
      ((((a).x '&' (b).x) 'or' ((a).x '&' (c).x)) 'or'
      'not'( (a).x '&' (c).x)) by BINARITH:20
   .= (((a).x '&' (c).x) 'or' (((a).x '&' (b).x) 'or'
      'not'( (a).x '&' (b).x))) '&'
      (((a).x '&' (b).x) 'or' (((a).x '&' (c).x) 'or'
      'not'( (a).x '&' (c).x))) by BINARITH:20
   .= (((a).x '&' (c).x) 'or' TRUE) '&'
      (((a).x '&' (b).x) 'or' (((a).x '&' (c).x) 'or'
      'not'( (a).x '&' (c).x)))
    by BINARITH:18
   .= (((a).x '&' (c).x) 'or' TRUE) '&'
      (((a).x '&' (b).x) 'or' TRUE)
    by BINARITH:18
   .= TRUE '&' (((a).x '&' (b).x) 'or' TRUE) by BINARITH:19
   .= TRUE '&' TRUE by BINARITH:19
   .= TRUE by MARGREL1:45;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b,c being Element of Funcs(Y,BOOLEAN) holds
((a 'or' c) '&' (b 'or' c)) 'imp' ((a '&' b) 'or' c)=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds
    (((a 'or' c) '&' (b 'or' c)) 'imp' ((a '&' b) 'or' c)).x=TRUE
  proof
    let x be Element of Y;
       (((a 'or' c) '&' (b 'or' c)) 'imp' ((a '&' b) 'or' c)).x
    ='not' ((a 'or' c) '&' (b 'or' c)).x 'or' ((a '&' b) 'or' c).x
    by BVFUNC_1:def 11
   .='not'( (a 'or' c).x '&' (b 'or' c).x) 'or' ((a '&' b) 'or' c).x
    by VALUAT_1:def 6
   .='not'
(((a).x 'or' (c).x) '&' (b 'or' c).x) 'or' ((a '&' b) 'or' c).x
    by BVFUNC_1:def 7
   .='not'( ((a).x 'or' (c).x) '&' ((b).x 'or' (c).x)) 'or'
     ((a '&' b) 'or' c).x by BVFUNC_1:def 7
   .=('not'( (a).x 'or' (c).x) 'or' 'not'( (b).x 'or' (c).x)) 'or'
     ((a '&' b) 'or' c).x by BINARITH:9
   .=('not'( (a).x 'or' (c).x) 'or' 'not'( (b).x 'or' (c).x)) 'or'
     ((a '&' b).x 'or' (c).x) by BVFUNC_1:def 7
   .=('not'( (a).x 'or' (c).x) 'or' 'not'( (b).x 'or' (c).x)) 'or'
     ((c).x 'or' ((a).x '&' (b).x)) by VALUAT_1:def 6
   .=('not'( (a).x 'or' (c).x) 'or' 'not'( (b).x 'or' (c).x)) 'or'
     (((a).x 'or' (c).x) '&' ((c).x 'or' (b).x)) by BINARITH:23
   .=(('not'( (a).x 'or' (c).x) 'or' 'not'( (b).x 'or' (c).x)) 'or'
     ((a).x 'or' (c).x)) '&'
     (('not'( (a).x 'or' (c).x) 'or' 'not'( (b).x 'or' (c).x)) 'or'
     ((b).x 'or' (c).x)) by BINARITH:23
   .=('not'( (b).x 'or' (c).x) 'or' ('not'( (a).x 'or' (c).x) 'or'
     ((a).x 'or' (c).x))) '&'
     (('not'( (a).x 'or' (c).x) 'or' 'not'( (b).x 'or' (c).x)) 'or'
     ((b).x 'or' (c).x))
    by BINARITH:20
   .=('not'( (b).x 'or' (c).x) 'or' ('not'( (a).x 'or' (c).x) 'or'
     ((a).x 'or' (c).x))) '&'
     ('not'( (a).x 'or' (c).x) 'or' ('not'( (b).x 'or' (c).x) 'or'
     ((b).x 'or' (c).x))) by BINARITH:20
   .=('not'( (b).x 'or' (c).x) 'or' TRUE) '&'
     ('not'( (a).x 'or' (c).x) 'or' ('not'( (b).x 'or' (c).x) 'or'
     ((b).x 'or' (c).x))) by BINARITH:18
   .=('not'( (b).x 'or' (c).x) 'or' TRUE) '&'
     ('not'( (a).x 'or' (c).x) 'or' TRUE) by BINARITH:18
   .=TRUE '&' ('not'( (a).x 'or' (c).x) 'or' TRUE) by BINARITH:19
   .=TRUE '&' TRUE by BINARITH:19
   .=TRUE by MARGREL1:45;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b,c being Element of Funcs(Y,BOOLEAN) holds
((a 'or' b) '&' c) 'imp' ((a '&' c) 'or' (b '&' c))=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds
    (((a 'or' b) '&' c) 'imp' ((a '&' c) 'or' (b '&' c))).x=TRUE
  proof
    let x be Element of Y;
       (((a 'or' b) '&' c) 'imp' ((a '&' c) 'or' (b '&' c))).x
    ='not' ((a 'or' b) '&' c).x 'or' ((a '&' c) 'or' (b '&' c)).x
    by BVFUNC_1:def 11
   .='not'( ((a 'or' b)).x '&' (c).x) 'or' ((a '&' c) 'or' (b '&' c)).x
    by VALUAT_1:def 6
   .='not'( ((a).x 'or' (b).x) '&' (c).x) 'or'
     ((a '&' c) 'or' (b '&' c)).x by BVFUNC_1:def 7
   .='not'( ((a).x 'or' (b).x) '&' (c).x) 'or'
     ((a '&' c).x 'or' (b '&' c).x) by BVFUNC_1:def 7
   .='not'( ((a).x 'or' (b).x) '&' (c).x) 'or'
     (((a).x '&' (c).x) 'or' (b '&' c).x) by VALUAT_1:def 6
   .='not'( (c).x '&' ((a).x 'or' (b).x)) 'or'
     (((a).x '&' (c).x) 'or' ((b).x '&' (c).x)) by VALUAT_1:def 6
   .='not'( ((c).x '&' (a).x) 'or' ((c).x '&' (b).x)) 'or'
     (((a).x '&' (c).x) 'or' ((b).x '&' (c).x)) by BINARITH:22
   .=(((a).x '&' (c).x) 'or' ((b).x '&' (c).x)) 'or'
     ('not'( (a).x '&' (c).x) '&' 'not'( (b).x '&' (c).x))
     by BINARITH:10
   .=((((b).x '&' (c).x) 'or' ((a).x '&' (c).x)) 'or'
     'not'( (a).x '&' (c).x)) '&'
     ((((a).x '&' (c).x) 'or' ((b).x '&' (c).x)) 'or'
     'not'( (b).x '&' (c).x)) by BINARITH:23
   .=(((b).x '&' (c).x) 'or' (((a).x '&' (c).x) 'or'
     'not'( (a).x '&' (c).x))) '&'
     ((((a).x '&' (c).x) 'or' ((b).x '&' (c).x)) 'or'
     'not'( (b).x '&' (c).x)) by BINARITH:20
   .=(((b).x '&' (c).x) 'or' (((a).x '&' (c).x) 'or'
     'not'( (a).x '&' (c).x))) '&'
     (((a).x '&' (c).x) 'or' (((b).x '&' (c).x) 'or'
     'not'( (b).x '&' (c).x))) by BINARITH:20
   .=(((b).x '&' (c).x) 'or' TRUE) '&'
     (((a).x '&' (c).x) 'or' (((b).x '&' (c).x) 'or'
     'not'( (b).x '&' (c).x))) by BINARITH:18
   .=(((b).x '&' (c).x) 'or' TRUE) '&'
     (((a).x '&' (c).x) 'or' TRUE) by BINARITH:18
   .=TRUE '&' (((a).x '&' (c).x) 'or' TRUE) by BINARITH:19
   .=TRUE '&' TRUE by BINARITH:19
   .=TRUE by MARGREL1:45;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b being Element of Funcs(Y,BOOLEAN) holds
(a '&' b)=I_el(Y) implies (a 'or' b)=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  assume A1: (a '&' b)=I_el(Y);
     for x being Element of Y holds
    (a 'or' b).x=TRUE
  proof
    let x be Element of Y;
       (a '&' b).x= TRUE by A1,BVFUNC_1:def 14;
    then (a).x '&' (b).x=TRUE by VALUAT_1:def 6;
    then (a).x=TRUE & (b).x=TRUE by MARGREL1:45;
    then (a 'or' b).x
    =TRUE 'or' TRUE by BVFUNC_1:def 7
   .=TRUE by BINARITH:19;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b,c being Element of Funcs(Y,BOOLEAN) holds
(a 'imp' b)=I_el(Y) implies (a 'or' c) 'imp' (b 'or' c)=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
  assume A1: (a 'imp' b)=I_el(Y);
     for x being Element of Y holds
    ((a 'or' c) 'imp' (b 'or' c)).x=TRUE
  proof
    let x be Element of Y;
       (a 'imp' b).x= TRUE by A1,BVFUNC_1:def 14;
    then A2:'not' (a).x 'or' (b).x = TRUE by BVFUNC_1:def 11;
       ((a 'or' c) 'imp' (b 'or' c)).x
    ='not' (a 'or' c).x 'or' (b 'or' c).x by BVFUNC_1:def 11
   .='not'( (a).x 'or' (c).x) 'or' (b 'or' c).x by BVFUNC_1:def 7
   .='not'( (a).x 'or' (c).x) 'or' ((b).x 'or' (c).x) by BVFUNC_1:def 7
   .=((b).x 'or' (c).x) 'or' ('not' (a).x '&' 'not' (c).x)
   by BINARITH:10
   .=(((c).x 'or' (b).x) 'or' 'not' (a).x) '&'
     (((b).x 'or' (c).x) 'or' 'not' (c).x) by BINARITH:23
   .=((c).x 'or' ('not' (a).x 'or' (b).x)) '&'
     (((b).x 'or' (c).x) 'or' 'not' (c).x) by BINARITH:20
   .=TRUE '&' (((b).x 'or' (c).x) 'or' 'not' (c).x) by A2,BINARITH:19
   .=TRUE '&' ((b).x 'or' ((c).x 'or' 'not' (c).x)) by BINARITH:20
   .=TRUE '&' ((b).x 'or' TRUE) by BINARITH:18
   .=TRUE '&' TRUE by BINARITH:19
   .=TRUE by MARGREL1:45;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b,c being Element of Funcs(Y,BOOLEAN) holds
(a 'imp' b)=I_el(Y) implies (a '&' c) 'imp' (b '&' c)=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
  assume A1: (a 'imp' b)=I_el(Y);
     for x being Element of Y holds
    ((a '&' c) 'imp' (b '&' c)).x=TRUE
  proof
    let x be Element of Y;
       (a 'imp' b).x= TRUE by A1,BVFUNC_1:def 14;
    then A2:'not' (a).x 'or' (b).x = TRUE by BVFUNC_1:def 11;
       ((a '&' c) 'imp' (b '&' c)).x
    ='not' (a '&' c).x 'or' (b '&' c).x by BVFUNC_1:def 11
   .='not'( (a).x '&' (c).x) 'or' (b '&' c).x by VALUAT_1:def 6
   .='not'( (a).x '&' (c).x) 'or' ((b).x '&' (c).x) by VALUAT_1:def 6
   .=('not' (a).x 'or' 'not' (c).x) 'or' ((b).x '&' (c).x)
   by BINARITH:9
   .=(('not' (c).x 'or' 'not' (a).x) 'or' (b).x) '&'
     (('not' (a).x 'or' 'not' (c).x) 'or' (c).x) by BINARITH:23
   .=('not' (c).x 'or' ('not' (a).x 'or' (b).x)) '&'
     (('not' (a).x 'or' 'not' (c).x) 'or' (c).x) by BINARITH:20
   .=('not' (c).x 'or' ('not' (a).x 'or' (b).x)) '&'
     ('not' (a).x 'or' ('not' (c).x 'or' (c).x)) by BINARITH:20
   .=('not' (c).x 'or' ('not' (a).x 'or' (b).x)) '&'
     ('not' (a).x 'or' TRUE) by BINARITH:18
   .=('not' (c).x 'or' TRUE) '&' TRUE by A2,BINARITH:19
   .=TRUE '&' TRUE by BINARITH:19
   .=TRUE by MARGREL1:45;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b,c being Element of Funcs(Y,BOOLEAN) holds
(c 'imp' a)=I_el(Y) & (c 'imp' b)=I_el(Y) implies
c 'imp' (a '&' b)=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
  assume A1:(c 'imp' a)=I_el(Y) & (c 'imp' b)=I_el(Y);
     for x being Element of Y holds
    (c 'imp' (a '&' b)).x=TRUE
  proof
    let x be Element of Y;
       (c 'imp' a).x= TRUE by A1,BVFUNC_1:def 14;
    then A2:'not' (c).x 'or' (a).x = TRUE by BVFUNC_1:def 11;
       (c 'imp' b).x= TRUE by A1,BVFUNC_1:def 14;
    then A3:'not' (c).x 'or' (b).x = TRUE by BVFUNC_1:def 11;
       (c 'imp' (a '&' b)).x
    ='not' (c).x 'or' (a '&' b).x by BVFUNC_1:def 11
   .='not' (c).x 'or' ((a).x '&' (b).x) by VALUAT_1:def 6
   .=TRUE '&' TRUE by A2,A3,BINARITH:23
   .=TRUE by MARGREL1:45;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b,c being Element of Funcs(Y,BOOLEAN) holds
(a 'imp' c)=I_el(Y) & (b 'imp' c)=I_el(Y) implies
(a 'or' b) 'imp' c = I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
  assume A1:(a 'imp' c)=I_el(Y) & (b 'imp' c)=I_el(Y);
     for x being Element of Y holds
    ((a 'or' b) 'imp' c).x=TRUE
  proof
    let x be Element of Y;
       (a 'imp' c).x= TRUE by A1,BVFUNC_1:def 14;
    then A2:'not' (a).x 'or' (c).x = TRUE by BVFUNC_1:def 11;
       (b 'imp' c).x= TRUE by A1,BVFUNC_1:def 14;
    then A3:'not' (b).x 'or' (c).x = TRUE by BVFUNC_1:def 11;
       ((a 'or' b) 'imp' c).x
    ='not' (a 'or' b).x 'or' (c).x by BVFUNC_1:def 11
   .='not'( (a).x 'or' (b).x) 'or' (c).x by BVFUNC_1:def 7
   .=(c).x 'or' ('not' (a).x '&' 'not' (b).x) by BINARITH:10
   .=TRUE '&' TRUE by A2,A3,BINARITH:23
   .=TRUE by MARGREL1:45;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b being Element of Funcs(Y,BOOLEAN) holds
(a 'or' b)=I_el(Y) & 'not' a=I_el(Y) implies b=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  assume A1:(a 'or' b)=I_el(Y) & 'not' a=I_el(Y);
     for x being Element of Y holds
    (b).x=TRUE
  proof
    let x be Element of Y;
       (a 'or' b).x= TRUE by A1,BVFUNC_1:def 14;
    then A2:(a).x 'or' (b).x = TRUE by BVFUNC_1:def 7;
       ('not' a).x= TRUE by A1,BVFUNC_1:def 14;
    then 'not' (a).x = TRUE by VALUAT_1:def 5;
    then (a).x = FALSE by MARGREL1:41;
    hence thesis by A2,BINARITH:7;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b,c,d being Element of Funcs(Y,BOOLEAN) holds
(a 'imp' b)=I_el(Y) & (c 'imp' d)=I_el(Y) implies
(a '&' c) 'imp' (b '&' d)=I_el(Y)
proof
  let a,b,c,d be Element of Funcs(Y,BOOLEAN);
  assume A1:(a 'imp' b)=I_el(Y) & (c 'imp' d)=I_el(Y);
     for x being Element of Y holds
    ((a '&' c) 'imp' (b '&' d)).x=TRUE
  proof
    let x be Element of Y;
       (a 'imp' b).x= TRUE by A1,BVFUNC_1:def 14;
    then A2:'not' (a).x 'or' (b).x = TRUE by BVFUNC_1:def 11;
       (c 'imp' d).x= TRUE by A1,BVFUNC_1:def 14;
    then A3:'not' (c).x 'or' (d).x = TRUE by BVFUNC_1:def 11;
       ((a '&' c) 'imp' (b '&' d)).x
    ='not' (a '&' c).x 'or' (b '&' d).x by BVFUNC_1:def 11
   .='not'( (a).x '&' (c).x) 'or' (b '&' d).x by VALUAT_1:def 6
   .='not'( (a).x '&' (c).x) 'or' ((b).x '&' (d).x) by VALUAT_1:def 6
   .=('not' (a).x 'or' 'not' (c).x) 'or' ((b).x '&' (d).x)
   by BINARITH:9
   .=(('not' (c).x 'or' 'not' (a).x) 'or' (b).x) '&'
     (('not' (a).x 'or' 'not' (c).x) 'or' (d).x) by BINARITH:23
   .=('not' (c).x 'or' ('not' (a).x 'or' (b).x)) '&'
     (('not' (a).x 'or' 'not' (c).x) 'or' (d).x) by BINARITH:20
   .=('not' (c).x 'or' TRUE) '&' ('not' (a).x 'or' TRUE)
   by A2,A3,BINARITH:20
   .=TRUE '&' ('not' (a).x 'or' TRUE) by BINARITH:19
   .=TRUE '&' TRUE by BINARITH:19
   .=TRUE by MARGREL1:45;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b,c,d being Element of Funcs(Y,BOOLEAN) holds
(a 'imp' b)=I_el(Y) & (c 'imp' d)=I_el(Y) implies
(a 'or' c) 'imp' (b 'or' d) =I_el(Y)
proof
  let a,b,c,d be Element of Funcs(Y,BOOLEAN);
  assume A1:(a 'imp' b)=I_el(Y) & (c 'imp' d)=I_el(Y);
     for x being Element of Y holds
    ((a 'or' c) 'imp' (b 'or' d)).x=TRUE
  proof
    let x be Element of Y;
       (a 'imp' b).x= TRUE by A1,BVFUNC_1:def 14;
    then A2:'not' (a).x 'or' (b).x = TRUE by BVFUNC_1:def 11;
       (c 'imp' d).x= TRUE by A1,BVFUNC_1:def 14;
    then A3:'not' (c).x 'or' (d).x = TRUE by BVFUNC_1:def 11;
       ((a 'or' c) 'imp' (b 'or' d)).x
    ='not' (a 'or' c).x 'or' (b 'or' d).x by BVFUNC_1:def 11
   .='not'( (a).x 'or' (c).x) 'or' (b 'or' d).x by BVFUNC_1:def 7
   .='not'( (a).x 'or' (c).x) 'or' ((b).x 'or' (d).x) by BVFUNC_1:def 7
   .=((b).x 'or' (d).x) 'or' ('not' (a).x '&' 'not' (c).x)
   by BINARITH:10
   .=(((d).x 'or' (b).x) 'or' 'not' (a).x) '&'
     (((b).x 'or' (d).x) 'or' 'not' (c).x) by BINARITH:23
   .=((d).x 'or' ((b).x 'or' 'not' (a).x)) '&'
     (((b).x 'or' (d).x) 'or' 'not' (c).x) by BINARITH:20
   .=((d).x 'or' TRUE) '&' ((b).x 'or' TRUE) by A2,A3,BINARITH:20
   .=TRUE '&' ((b).x 'or' TRUE) by BINARITH:19
   .=TRUE '&' TRUE by BINARITH:19
   .=TRUE by MARGREL1:45;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b being Element of Funcs(Y,BOOLEAN) holds
(a '&' 'not' b) 'imp' 'not' a=I_el(Y) implies (a 'imp' b)=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  assume A1: (a '&' 'not' b) 'imp' 'not' a=I_el(Y);
     for x being Element of Y holds
    (a 'imp' b).x=TRUE
  proof
    let x be Element of Y;
       ((a '&' 'not' b) 'imp' 'not' a).x=TRUE by A1,BVFUNC_1:def 14;
    then 'not' (a '&' 'not' b).x 'or' ('not' a).x = TRUE by BVFUNC_1:def 11
;
    then 'not'( (a).x '&' ('not' b).x) 'or' ('not'
a).x=TRUE by VALUAT_1:def 6;
    then ('not' (a).x 'or' 'not' ('not' b).x) 'or' ('not'
a).x=TRUE by BINARITH:9;
    then ('not' (a).x 'or' 'not' 'not' (b).x) 'or' ('not'
a).x=TRUE by VALUAT_1:def 5;
    then ('not' (a).x 'or' 'not' 'not' (b).x) 'or' 'not'
(a).x=TRUE by VALUAT_1:def 5;
    then ('not' (a).x 'or' (b).x) 'or' 'not' (a).x=TRUE by MARGREL1:40;
    then (b).x 'or' ('not' (a).x 'or' 'not' (a).x)=TRUE by BINARITH:20;
    then (b).x 'or' 'not' (a).x=TRUE by BINARITH:21;
    hence thesis by BVFUNC_1:def 11;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

canceled;

theorem  for a,b being Element of Funcs(Y,BOOLEAN) holds
a 'imp' 'not' b=I_el(Y) implies b 'imp' 'not' a=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  assume A1:a 'imp' 'not' b=I_el(Y);
     for x being Element of Y holds (b 'imp' 'not' a).x=TRUE
  proof
    let x be Element of Y;
       (a 'imp' 'not' b).x=TRUE by A1,BVFUNC_1:def 14;
    then ('not' (a).x) 'or' ('not' b).x=TRUE by BVFUNC_1:def 11;
    then A2:'not' (a).x 'or' 'not' (b).x=TRUE by VALUAT_1:def 5;
       (b 'imp' 'not' a).x
     =('not' (b).x) 'or' ('not' a).x by BVFUNC_1:def 11
    .=TRUE by A2,VALUAT_1:def 5;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b being Element of Funcs(Y,BOOLEAN) holds
'not' a 'imp' b=I_el(Y) implies 'not' b 'imp' a=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  assume A1:'not' a 'imp' b=I_el(Y);
     for x being Element of Y holds ('not' b 'imp' a).x=TRUE
  proof
    let x be Element of Y;
       ('not' a 'imp' b).x=TRUE by A1,BVFUNC_1:def 14;
    then ('not' ('not' a).x) 'or' (b).x=TRUE by BVFUNC_1:def 11;
    then 'not' 'not' (a).x 'or' (b).x=TRUE by VALUAT_1:def 5;
    then A2:(a).x 'or' (b).x=TRUE by MARGREL1:40;
       ('not' b 'imp' a).x
     =('not' ('not' b).x) 'or' (a).x by BVFUNC_1:def 11
    .=('not' 'not' (b).x) 'or' (a).x by VALUAT_1:def 5
    .=TRUE by A2,MARGREL1:40;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b being Element of Funcs(Y,BOOLEAN) holds
a 'imp' (a 'or' b)=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds (a 'imp' (a 'or' b)).x=TRUE
  proof
    let x be Element of Y;
       (a 'imp' (a 'or' b)).x
    ='not' (a).x 'or' (a 'or' b).x by BVFUNC_1:def 11
   .='not' (a).x 'or' ((a).x 'or' (b).x) by BVFUNC_1:def 7
   .=('not' (a).x 'or' (a).x) 'or' (b).x by BINARITH:20
   .=TRUE 'or' (b).x by BINARITH:18
   .=TRUE by BINARITH:19;
   hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b being Element of Funcs(Y,BOOLEAN) holds
(a 'or' b) 'imp' ('not' a 'imp' b)=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds ((a 'or' b) 'imp' ('not' a 'imp' b)).x=
TRUE
  proof
    let x be Element of Y;
       ((a 'or' b) 'imp' ('not' a 'imp' b)).x
    ='not' (a 'or' b).x 'or' ('not' a 'imp' b).x by BVFUNC_1:def 11
   .='not'( (a).x 'or' (b).x) 'or' ('not' a 'imp' b).x by BVFUNC_1:def 7
   .='not'( (a).x 'or' (b).x) 'or' ('not' ('not'
a).x 'or' (b).x) by BVFUNC_1:def 11
   .=('not' (a).x '&' 'not' (b).x) 'or' ('not' ('not'
a).x 'or' (b).x) by BINARITH:10
   .=('not' (a).x '&' 'not' (b).x) 'or' ('not' 'not'
(a).x 'or' (b).x) by VALUAT_1:def 5
   .=((a).x 'or' (b).x) 'or' ('not' (a).x '&' 'not' (b).x)
   by MARGREL1:40
   .=(((a).x 'or' (b).x) 'or' 'not' (a).x) '&'
     (((a).x 'or' (b).x) 'or' 'not' (b).x) by BINARITH:23
   .=(((a).x 'or' 'not' (a).x) 'or' (b).x) '&'
     (((a).x 'or' (b).x) 'or' 'not' (b).x) by BINARITH:20
   .=(((a).x 'or' 'not' (a).x) 'or' (b).x) '&'
     ((a).x 'or' ((b).x 'or' 'not' (b).x)) by BINARITH:20
   .=(TRUE 'or' (b).x) '&'
     ((a).x 'or' ((b).x 'or' 'not' (b).x)) by BINARITH:18
   .=(TRUE 'or' (b).x) '&' ((a).x 'or' TRUE) by BINARITH:18
   .=TRUE '&' ((a).x 'or' TRUE) by BINARITH:19
   .=TRUE '&' TRUE by BINARITH:19
   .=TRUE by MARGREL1:45;
   hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b being Element of Funcs(Y,BOOLEAN) holds
'not'( a 'or' b) 'imp' ('not' a '&' 'not' b)=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds ('not'( a 'or' b) 'imp' ('not' a '&'
'not'
b)).x=TRUE
  proof
    let x be Element of Y;
       ('not'( a 'or' b) 'imp' ('not' a '&' 'not' b)).x
    ='not' ('not'( a 'or' b)).x 'or' ('not' a '&' 'not' b).x
    by BVFUNC_1:def 11
   .='not' 'not' (a 'or' b).x 'or' ('not' a '&' 'not' b).x
   by VALUAT_1:def 5
   .=(a 'or' b).x 'or' ('not' a '&' 'not' b).x by MARGREL1:40
   .=((a).x 'or' (b).x) 'or' ('not' a '&' 'not' b).x by BVFUNC_1:def 7
   .=((a).x 'or' (b).x) 'or' (('not' a).x '&' ('not'
b).x) by VALUAT_1:def 6
   .=((a).x 'or' (b).x) 'or' ('not' (a).x '&' ('not'
b).x) by VALUAT_1:def 5
   .=((a).x 'or' (b).x) 'or' ('not' (a).x '&' 'not'
(b).x) by VALUAT_1:def 5
   .=(((a).x 'or' (b).x) 'or' 'not' (a).x) '&'
     (((a).x 'or' (b).x) 'or' 'not' (b).x) by BINARITH:23
   .=(((a).x 'or' 'not' (a).x) 'or' (b).x) '&'
     (((a).x 'or' (b).x) 'or' 'not' (b).x) by BINARITH:20
   .=(((a).x 'or' 'not' (a).x) 'or' (b).x) '&'
     ((a).x 'or' ((b).x 'or' 'not' (b).x)) by BINARITH:20
   .=(TRUE 'or' (b).x) '&'
     ((a).x 'or' ((b).x 'or' 'not' (b).x)) by BINARITH:18
   .=(TRUE 'or' (b).x) '&' ((a).x 'or' TRUE) by BINARITH:18
   .=TRUE '&' ((a).x 'or' TRUE) by BINARITH:19
   .=TRUE '&' TRUE by BINARITH:19
   .=TRUE by MARGREL1:45;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b being Element of Funcs(Y,BOOLEAN) holds
('not' a '&' 'not' b) 'imp' 'not'( a 'or' b)=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds (('not' a '&' 'not' b) 'imp' 'not'
(a 'or' b)).x=TRUE
  proof
    let x be Element of Y;
       (('not' a '&' 'not' b) 'imp' 'not'( a 'or' b)).x
    ='not' ('not' a '&' 'not' b).x 'or' ('not'( a 'or' b)).x
    by BVFUNC_1:def 11
   .='not' ('not' a '&' 'not' b).x 'or' 'not' (a 'or' b).x
   by VALUAT_1:def 5
   .='not'( ('not' a).x '&' ('not' b).x) 'or' 'not'
(a 'or' b).x by VALUAT_1:def 6
   .='not'( 'not' (a).x '&' ('not' b).x) 'or' 'not'
(a 'or' b).x by VALUAT_1:def 5
   .='not'( 'not' (a).x '&' 'not' (b).x) 'or' 'not'
(a 'or' b).x by VALUAT_1:def 5
   .=('not' 'not' (a).x 'or' 'not' 'not' (b).x) 'or' 'not'
(a 'or' b).x by BINARITH:9
   .=((a).x 'or' 'not' 'not' (b).x) 'or' 'not' (a 'or' b).x
   by MARGREL1:40
   .=((a).x 'or' (b).x) 'or' 'not' (a 'or' b).x by MARGREL1:40
   .=((a).x 'or' (b).x) 'or' 'not'( (a).x 'or' (b).x)
   by BVFUNC_1:def 7
   .=((a).x 'or' (b).x) 'or' ('not' (a).x '&' 'not' (b).x)
   by BINARITH:10
   .=(((a).x 'or' (b).x) 'or' 'not' (a).x) '&'
     (((a).x 'or' (b).x) 'or' 'not' (b).x) by BINARITH:23
   .=(((a).x 'or' 'not' (a).x) 'or' (b).x) '&'
     (((a).x 'or' (b).x) 'or' 'not' (b).x) by BINARITH:20
   .=(((a).x 'or' 'not' (a).x) 'or' (b).x) '&'
     ((a).x 'or' ((b).x 'or' 'not' (b).x)) by BINARITH:20
   .=(TRUE 'or' (b).x) '&'
     ((a).x 'or' ((b).x 'or' 'not' (b).x)) by BINARITH:18
   .=(TRUE 'or' (b).x) '&' ((a).x 'or' TRUE) by BINARITH:18
   .=TRUE '&' ((a).x 'or' TRUE) by BINARITH:19
   .=TRUE '&' TRUE by BINARITH:19
   .=TRUE by MARGREL1:45;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b being Element of Funcs(Y,BOOLEAN) holds
'not'( a 'or' b) 'imp' 'not' a=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds ('not'( a 'or' b) 'imp' 'not' a).x=TRUE
  proof
    let x be Element of Y;
       ('not'( a 'or' b) 'imp' 'not' a).x
    ='not' ('not'( a 'or' b)).x 'or' ('not' a).x by BVFUNC_1:def 11
   .='not' 'not' (a 'or' b).x 'or' ('not' a).x by VALUAT_1:def 5
   .='not' 'not' (a 'or' b).x 'or' 'not' (a).x by VALUAT_1:def 5
   .=(a 'or' b).x 'or' 'not' (a).x by MARGREL1:40
   .=((a).x 'or' (b).x) 'or' 'not' (a).x by BVFUNC_1:def 7
   .=((a).x 'or' 'not' (a).x) 'or' (b).x by BINARITH:20
   .=TRUE 'or' (b).x by BINARITH:18
   .=TRUE by BINARITH:19;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a being Element of Funcs(Y,BOOLEAN) holds
(a 'or' a) 'imp' a=I_el(Y)
proof
  let a be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds ((a 'or' a) 'imp' a).x=TRUE
  proof
    let x be Element of Y;
       ((a 'or' a) 'imp' a).x
    ='not' (a 'or' a).x 'or' (a).x by BVFUNC_1:def 11
   .='not'( (a).x 'or' (a).x) 'or' (a).x by BVFUNC_1:def 7
   .=('not' (a).x '&' 'not' (a).x) 'or' (a).x by BINARITH:10
   .='not' (a).x 'or' (a).x by BINARITH:16
   .=TRUE by BINARITH:18;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b being Element of Funcs(Y,BOOLEAN) holds
(a '&' 'not' a) 'imp' b=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds ((a '&' 'not' a) 'imp' b).x=TRUE
  proof
    let x be Element of Y;
       ((a '&' 'not' a) 'imp' b).x
    ='not' (a '&' 'not' a).x 'or' (b).x by BVFUNC_1:def 11
   .='not'( (a).x '&' ('not' a).x) 'or' (b).x by VALUAT_1:def 6
   .='not'( (a).x '&' 'not' (a).x) 'or' (b).x by VALUAT_1:def 5
   .=('not' (a).x 'or' 'not' 'not' (a).x) 'or' (b).x by BINARITH:9
   .=TRUE 'or' (b).x by BINARITH:18
   .=TRUE by BINARITH:19;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b being Element of Funcs(Y,BOOLEAN) holds
(a 'imp' b) 'imp' ('not' a 'or' b)=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds ((a 'imp' b) 'imp' ('not' a 'or' b)).x=
TRUE
  proof
    let x be Element of Y;
       ((a 'imp' b) 'imp' ('not' a 'or' b)).x
    ='not' (a 'imp' b).x 'or' ('not' a 'or' b).x by BVFUNC_1:def 11
   .='not'( 'not' (a).x 'or' (b).x) 'or' ('not'
a 'or' b).x by BVFUNC_1:def 11
   .='not'( 'not' (a).x 'or' (b).x) 'or' (('not'
a).x 'or' (b).x) by BVFUNC_1:def 7
   .=('not' 'not' (a).x '&' 'not' (b).x) 'or' (('not'
a).x 'or' (b).x) by BINARITH:10
   .=((a).x '&' 'not' (b).x) 'or' (('not' a).x 'or' (b).x)
   by MARGREL1:40
   .=('not' (a).x 'or' (b).x) 'or' ((a).x '&' 'not'
(b).x) by VALUAT_1:def 5
   .=(('not' (a).x 'or' (b).x) 'or' (a).x) '&'
     (('not' (a).x 'or' (b).x) 'or' 'not' (b).x) by BINARITH:23
   .=(('not' (a).x 'or' (a).x) 'or' (b).x) '&'
     (('not' (a).x 'or' (b).x) 'or' 'not' (b).x) by BINARITH:20
   .=(('not' (a).x 'or' (a).x) 'or' (b).x) '&'
     ('not' (a).x 'or' ((b).x 'or' 'not' (b).x)) by BINARITH:20
   .=(TRUE 'or' (b).x) '&'
     ('not' (a).x 'or' ((b).x 'or' 'not' (b).x)) by BINARITH:18
   .=(TRUE 'or' (b).x) '&' ('not' (a).x 'or' TRUE) by BINARITH:18
   .=TRUE '&' ('not' (a).x 'or' TRUE) by BINARITH:19
   .=TRUE '&' TRUE by BINARITH:19
   .=TRUE by MARGREL1:45;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b being Element of Funcs(Y,BOOLEAN) holds
(a '&' b) 'imp' 'not'( a 'imp' 'not' b)=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds ((a '&' b) 'imp' 'not'( a 'imp' 'not'
b)).x=TRUE
  proof
    let x be Element of Y;
       ((a '&' b) 'imp' 'not'( a 'imp' 'not' b)).x
    ='not' (a '&' b).x 'or' ('not'( a 'imp' 'not' b)).x by BVFUNC_1:def 11
   .='not'( (a).x '&' (b).x) 'or' ('not'( a 'imp' 'not'
b)).x by VALUAT_1:def 6
   .='not'( (a).x '&' (b).x) 'or' 'not' (a 'imp' 'not'
b).x by VALUAT_1:def 5
   .=('not' (a).x 'or' 'not' (b).x) 'or' 'not' (a 'imp' 'not'
b).x by BINARITH:9
   .=('not' (a).x 'or' 'not' (b).x) 'or' 'not'( 'not' (a).x 'or' ('not'
b).x) by BVFUNC_1:def 11
   .=('not' (a).x 'or' 'not' (b).x) 'or' 'not'( 'not' (a).x 'or' 'not'
(b).x) by VALUAT_1:def 5
   .=('not' (a).x 'or' 'not' (b).x) 'or' ('not' 'not'
   (a).x '&' 'not' 'not'
(b).x) by BINARITH:10
   .=('not' (a).x 'or' 'not' (b).x) 'or' ((a).x '&' 'not' 'not'
(b).x) by MARGREL1:40
   .=('not' (a).x 'or' 'not' (b).x) 'or' ((a).x '&' (b).x)
   by MARGREL1:40
   .=(('not' (a).x 'or' 'not' (b).x) 'or' (a).x) '&'
     (('not' (a).x 'or' 'not' (b).x) 'or' (b).x) by BINARITH:23
   .=(('not' (a).x 'or' (a).x) 'or' 'not' (b).x) '&'
     (('not' (a).x 'or' 'not' (b).x) 'or' (b).x) by BINARITH:20
   .=(('not' (a).x 'or' (a).x) 'or' 'not' (b).x) '&'
     ('not' (a).x 'or' ('not' (b).x 'or' (b).x)) by BINARITH:20
   .=(TRUE 'or' 'not' (b).x) '&'
     ('not' (a).x 'or' ('not' (b).x 'or' (b).x)) by BINARITH:18
   .=(TRUE 'or' 'not' (b).x) '&'
     ('not' (a).x 'or' TRUE) by BINARITH:18
   .=TRUE '&' ('not' (a).x 'or' TRUE) by BINARITH:19
   .=TRUE '&' TRUE by BINARITH:19
   .=TRUE by MARGREL1:45;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b being Element of Funcs(Y,BOOLEAN) holds
'not'( a 'imp' 'not' b) 'imp' (a '&' b)=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds ('not'( a 'imp' 'not'
b) 'imp' (a '&' b)).x=TRUE
  proof
    let x be Element of Y;
       ('not'( a 'imp' 'not' b) 'imp' (a '&' b)).x
    ='not' ('not'( a 'imp' 'not' b)).x 'or' (a '&' b).x by BVFUNC_1:def 11
   .='not' 'not' (a 'imp' 'not' b).x 'or' (a '&' b).x by VALUAT_1:def 5
   .='not' 'not' (a 'imp' 'not'
b).x 'or' ((a).x '&' (b).x) by VALUAT_1:def 6
   .='not' 'not'( 'not' (a).x 'or' ('not'
b).x) 'or' ((a).x '&' (b).x) by BVFUNC_1:def 11
   .=('not' (a).x 'or' ('not' b).x) 'or' ((a).x '&' (b).x)
   by MARGREL1:40
   .=('not' (a).x 'or' 'not'
(b).x) 'or' ((a).x '&' (b).x) by VALUAT_1:def 5
   .=(('not' (a).x 'or' 'not' (b).x) 'or' (a).x) '&'
     (('not' (a).x 'or' 'not' (b).x) 'or' (b).x) by BINARITH:23
   .=(('not' (a).x 'or' (a).x) 'or' 'not' (b).x) '&'
     (('not' (a).x 'or' 'not' (b).x) 'or' (b).x) by BINARITH:20
   .=(('not' (a).x 'or' (a).x) 'or' 'not' (b).x) '&'
     ('not' (a).x 'or' ('not' (b).x 'or' (b).x)) by BINARITH:20
   .=(TRUE 'or' 'not' (b).x) '&'
     ('not' (a).x 'or' ('not' (b).x 'or' (b).x)) by BINARITH:18
   .=(TRUE 'or' 'not' (b).x) '&'
     ('not' (a).x 'or' TRUE) by BINARITH:18
   .=TRUE '&' ('not' (a).x 'or' TRUE) by BINARITH:19
   .=TRUE '&' TRUE by BINARITH:19
   .=TRUE by MARGREL1:45;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b being Element of Funcs(Y,BOOLEAN) holds
'not'( a '&' b) 'imp' ('not' a 'or' 'not' b)=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds ('not'( a '&' b) 'imp' ('not' a 'or'
'not'
b)).x=TRUE
  proof
    let x be Element of Y;
       ('not'( a '&' b) 'imp' ('not' a 'or' 'not' b)).x
    ='not' ('not'( a '&' b)).x 'or' ('not' a 'or' 'not' b).x
    by BVFUNC_1:def 11
   .='not' 'not' (a '&' b).x 'or' ('not' a 'or' 'not' b).x
   by VALUAT_1:def 5
   .=(a '&' b).x 'or' ('not' a 'or' 'not' b).x by MARGREL1:40
   .=((a).x '&' (b).x) 'or' ('not' a 'or' 'not' b).x
   by VALUAT_1:def 6
   .=(('not' a).x 'or' ('not'
b).x) 'or' ((a).x '&' (b).x) by BVFUNC_1:def 7
   .=('not' (a).x 'or' ('not'
b).x) 'or' ((a).x '&' (b).x) by VALUAT_1:def 5
   .=('not' (a).x 'or' 'not'
(b).x) 'or' ((a).x '&' (b).x) by VALUAT_1:def 5
   .=(('not' (a).x 'or' 'not' (b).x) 'or' (a).x) '&'
     (('not' (a).x 'or' 'not' (b).x) 'or' (b).x) by BINARITH:23
   .=(('not' (a).x 'or' (a).x) 'or' 'not' (b).x) '&'
     (('not' (a).x 'or' 'not' (b).x) 'or' (b).x) by BINARITH:20
   .=(('not' (a).x 'or' (a).x) 'or' 'not' (b).x) '&'
     ('not' (a).x 'or' ('not' (b).x 'or' (b).x)) by BINARITH:20
   .=(TRUE 'or' 'not' (b).x) '&'
     ('not' (a).x 'or' ('not' (b).x 'or' (b).x)) by BINARITH:18
   .=(TRUE 'or' 'not' (b).x) '&' ('not' (a).x 'or' TRUE) by BINARITH:18
   .=TRUE '&' ('not' (a).x 'or' TRUE) by BINARITH:19
   .=TRUE '&' TRUE by BINARITH:19
   .=TRUE by MARGREL1:45;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b being Element of Funcs(Y,BOOLEAN) holds
('not' a 'or' 'not' b) 'imp' 'not'( a '&' b)=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds (('not' a 'or' 'not' b) 'imp' 'not'
(a '&' b)).x=TRUE
  proof
    let x be Element of Y;
       (('not' a 'or' 'not' b) 'imp' 'not'( a '&' b)).x
    ='not' ('not' a 'or' 'not' b).x 'or' ('not'( a '&' b)).x
    by BVFUNC_1:def 11
   .='not' ('not' a 'or' 'not' b).x 'or' 'not' (a '&' b).x
   by VALUAT_1:def 5
   .='not'( ('not' a).x 'or' ('not' b).x) 'or' 'not'
(a '&' b).x by BVFUNC_1:def 7
   .='not'( 'not' (a).x 'or' ('not' b).x) 'or' 'not'
(a '&' b).x by VALUAT_1:def 5
   .='not'( 'not' (a).x 'or' 'not' (b).x) 'or' 'not'
(a '&' b).x by VALUAT_1:def 5
   .='not'( 'not' (a).x 'or' 'not' (b).x) 'or' 'not'
((a).x '&' (b).x) by VALUAT_1:def 6
   .=('not' 'not' (a).x '&' 'not' 'not' (b).x) 'or' 'not'
((a).x '&' (b).x) by BINARITH:10
   .=('not' 'not' (a).x '&' 'not' 'not' (b).x) 'or'
   ('not' (a).x 'or' 'not'
(b).x) by BINARITH:9
   .=((a).x '&' 'not' 'not' (b).x) 'or' ('not' (a).x 'or' 'not'
(b).x) by MARGREL1:40
   .=('not' (a).x 'or' 'not' (b).x) 'or' ((a).x '&' (b).x)
   by MARGREL1:40
   .=(('not' (a).x 'or' 'not' (b).x) 'or' (a).x) '&'
     (('not' (a).x 'or' 'not' (b).x) 'or' (b).x) by BINARITH:23
   .=(('not' (a).x 'or' (a).x) 'or' 'not' (b).x) '&'
     (('not' (a).x 'or' 'not' (b).x) 'or' (b).x) by BINARITH:20
   .=(('not' (a).x 'or' (a).x) 'or' 'not' (b).x) '&'
     ('not' (a).x 'or' ('not' (b).x 'or' (b).x)) by BINARITH:20
   .=(TRUE 'or' 'not' (b).x) '&'
     ('not' (a).x 'or' ('not' (b).x 'or' (b).x)) by BINARITH:18
   .=(TRUE 'or' 'not' (b).x) '&'
     ('not' (a).x 'or' TRUE) by BINARITH:18
   .=TRUE '&'
     ('not' (a).x 'or' TRUE) by BINARITH:19
   .=TRUE '&' TRUE by BINARITH:19
   .=TRUE by MARGREL1:45;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b being Element of Funcs(Y,BOOLEAN) holds
(a '&' b) 'imp' a=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds ((a '&' b) 'imp' a).x=TRUE
  proof
    let x be Element of Y;
       ((a '&' b) 'imp' a).x
    ='not' (a '&' b).x 'or' (a).x by BVFUNC_1:def 11
   .='not'( (a).x '&' (b).x) 'or' (a).x by VALUAT_1:def 6
   .=('not' (a).x 'or' 'not' (b).x) 'or' (a).x by BINARITH:9
   .=('not' (a).x 'or' (a).x) 'or' 'not' (b).x by BINARITH:20
   .=TRUE 'or' 'not' (b).x by BINARITH:18
   .=TRUE by BINARITH:19;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b being Element of Funcs(Y,BOOLEAN) holds
(a '&' b) 'imp' (a 'or' b)=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds ((a '&' b) 'imp' (a 'or' b)).x=TRUE
  proof
    let x be Element of Y;
       ((a '&' b) 'imp' (a 'or' b)).x
    ='not' (a '&' b).x 'or' (a 'or' b).x by BVFUNC_1:def 11
   .='not'( (a).x '&' (b).x) 'or' (a 'or' b).x by VALUAT_1:def 6
   .=('not' (a).x 'or' 'not' (b).x) 'or' (a 'or' b).x by BINARITH:9
   .=('not' (a).x 'or' 'not'
(b).x) 'or' ((a).x 'or' (b).x) by BVFUNC_1:def 7
   .=(('not' (b).x 'or' 'not'
(a).x) 'or' (a).x) 'or' (b).x by BINARITH:20
   .=('not' (b).x 'or' ('not'
(a).x 'or' (a).x)) 'or' (b).x by BINARITH:20
   .=('not' (b).x 'or' TRUE) 'or' (b).x by BINARITH:18
   .=TRUE 'or' (b).x by BINARITH:19
   .=TRUE by BINARITH:19;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b being Element of Funcs(Y,BOOLEAN) holds
(a '&' b) 'imp' b=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds ((a '&' b) 'imp' b).x=TRUE
  proof
    let x be Element of Y;
       ((a '&' b) 'imp' b).x
    ='not' (a '&' b).x 'or' (b).x by BVFUNC_1:def 11
   .='not'( (a).x '&' (b).x) 'or' (b).x by VALUAT_1:def 6
   .=('not' (a).x 'or' 'not' (b).x) 'or' (b).x by BINARITH:9
   .='not' (a).x 'or' ('not' (b).x 'or' (b).x) by BINARITH:20
   .='not' (a).x 'or' TRUE by BINARITH:18
   .=TRUE by BINARITH:19;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a being Element of Funcs(Y,BOOLEAN) holds
a 'imp' a '&' a=I_el(Y)
proof
  let a be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds (a 'imp' (a '&' a)).x=TRUE
  proof
    let x be Element of Y;
       (a 'imp' a '&' a).x
    ='not' (a).x 'or' (a '&' a).x by BVFUNC_1:def 11
   .='not' (a).x 'or' ((a).x '&' (a).x) by VALUAT_1:def 6
   .=('not' (a).x 'or' (a).x) '&'
     ('not' (a).x 'or' (a).x) by BINARITH:23
   .=TRUE '&'
     ('not' (a).x 'or' (a).x) by BINARITH:18
   .=TRUE '&'
     TRUE by BINARITH:18
   .=TRUE by MARGREL1:45;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b being Element of Funcs(Y,BOOLEAN) holds
(a 'eqv' b) 'imp' (a 'imp' b)=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds ((a 'eqv' b) 'imp' (a 'imp' b)).x=TRUE
  proof
    let x be Element of Y;
       ((a 'eqv' b) 'imp' (a 'imp' b)).x
    ='not' (a 'eqv' b).x 'or' (a 'imp' b).x by BVFUNC_1:def 11
   .='not' 'not'( (a).x 'xor' (b).x) 'or'
     (a 'imp' b).x by BVFUNC_1:def 12
   .=((a).x 'xor' (b).x) 'or'
     (a 'imp' b).x by MARGREL1:40
   .=(('not' (a).x '&' (b).x) 'or' ((a).x '&' 'not' (b).x)) 'or'
     (a 'imp' b).x by BINARITH:def 2
   .=(('not' (a).x '&' (b).x) 'or' ((a).x '&' 'not' (b).x)) 'or'
     ('not' (a).x 'or' (b).x) by BVFUNC_1:def 11
   .=(('not' (a).x '&' (b).x) 'or' ((a).x '&' 'not' (b).x)) 'or'
     ('not' (a).x 'or' 'not' 'not' (b).x) by MARGREL1:40
   .=(('not' (a).x '&' (b).x) 'or' ((a).x '&' 'not' (b).x)) 'or'
     'not'( (a).x '&' 'not' (b).x) by BINARITH:9
   .=('not' (a).x '&' (b).x) 'or' (((a).x '&' 'not' (b).x) 'or'
     'not'( (a).x '&' 'not' (b).x)) by BINARITH:20
   .=('not' (a).x '&' (b).x) 'or' TRUE by BINARITH:18
   .=TRUE by BINARITH:19;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b being Element of Funcs(Y,BOOLEAN) holds
(a 'eqv' b) 'imp' (b 'imp' a)=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds ((a 'eqv' b) 'imp' (b 'imp' a)).x=TRUE
  proof
    let x be Element of Y;
       ((a 'eqv' b) 'imp' (b 'imp' a)).x
    ='not' (a 'eqv' b).x 'or' (b 'imp' a).x by BVFUNC_1:def 11
   .='not' 'not'( (a).x 'xor' (b).x) 'or'
     (b 'imp' a).x by BVFUNC_1:def 12
   .=((a).x 'xor' (b).x) 'or'
     (b 'imp' a).x by MARGREL1:40
   .=(('not' (a).x '&' (b).x) 'or' ((a).x '&' 'not' (b).x)) 'or'
     (b 'imp' a).x by BINARITH:def 2
   .=(('not' (a).x '&' (b).x) 'or' ((a).x '&' 'not' (b).x)) 'or'
     ((a).x 'or' 'not' (b).x) by BVFUNC_1:def 11
   .=('not' (a).x '&' (b).x) 'or' (((a).x '&' 'not' (b).x) 'or'
     ((a).x 'or' 'not' (b).x)) by BINARITH:20
   .=('not' (a).x '&' (b).x) 'or' (((a).x '&' 'not' (b).x) 'or'
     ('not' 'not' (a).x 'or' 'not' (b).x)) by MARGREL1:40
   .=('not' (a).x '&' (b).x) 'or' ('not'( 'not' (a).x '&' (b).x) 'or'
     ((a).x '&' 'not' (b).x)) by BINARITH:9
   .=(('not' (a).x '&' (b).x) 'or' 'not'( 'not' (a).x '&' (b).x)) 'or'
     ((a).x '&' 'not' (b).x) by BINARITH:20
   .=TRUE 'or' ((a).x '&' 'not' (b).x) by BINARITH:18
   .=TRUE by BINARITH:19;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b,c being Element of Funcs(Y,BOOLEAN) holds
((a 'or' b) 'or' c) 'imp' (a 'or' (b 'or' c))=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds
    (((a 'or' b) 'or' c) 'imp' (a 'or' (b 'or' c))).x=TRUE
  proof
    let x be Element of Y;
       (((a 'or' b) 'or' c) 'imp' (a 'or' (b 'or' c))).x
    ='not' ((a 'or' b) 'or' c).x 'or' (a 'or' (b 'or' c)).x
     by BVFUNC_1:def 11
   .='not'( (a 'or' b).x 'or' (c).x) 'or' (a 'or' (b 'or' c)).x
     by BVFUNC_1:def 7
   .='not'( ((a).x 'or' (b).x) 'or' (c).x) 'or' (a 'or' (b 'or' c)).x
     by BVFUNC_1:def 7
   .='not'( ((a).x 'or' (b).x) 'or' (c).x) 'or'
     ((a).x 'or' (b 'or' c).x)
     by BVFUNC_1:def 7
   .='not'( ((a).x 'or' (b).x) 'or' (c).x) 'or'
     ((a).x 'or' ((b).x 'or' (c).x))
     by BVFUNC_1:def 7
   .='not'( ((a).x 'or' (b).x) 'or' (c).x) 'or'
      (((a).x 'or' (b).x) 'or' (c).x)
     by BINARITH:20
   .=TRUE by BINARITH:18;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b,c being Element of Funcs(Y,BOOLEAN) holds
((a '&' b) '&' c) 'imp' (a '&' (b '&' c))=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds
    (((a '&' b) '&' c) 'imp' (a '&' (b '&' c))).x=TRUE
  proof
    let x be Element of Y;
       (((a '&' b) '&' c) 'imp' (a '&' (b '&' c))).x
    ='not' ((a '&' b) '&' c).x 'or' (a '&' (b '&' c)).x
     by BVFUNC_1:def 11
   .='not'( (a '&' b).x '&' (c).x) 'or' (a '&' (b '&' c)).x
     by VALUAT_1:def 6
   .='not'( ((a).x '&' (b).x) '&' (c).x) 'or' (a '&' (b '&' c)).x
     by VALUAT_1:def 6
   .='not'( ((a).x '&' (b).x) '&' (c).x) 'or'
     ((a).x '&' (b '&' c).x)
     by VALUAT_1:def 6
   .='not'( ((a).x '&' (b).x) '&' (c).x) 'or'
     ((a).x '&' ((b).x '&' (c).x))
     by VALUAT_1:def 6
   .='not'( ((a).x '&' (b).x) '&' (c).x) 'or'
      (((a).x '&' (b).x) '&' (c).x)
     by MARGREL1:52
   .=TRUE by BINARITH:18;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem  for a,b,c being Element of Funcs(Y,BOOLEAN) holds
(a 'or' (b 'or' c)) 'imp' ((a 'or' b) 'or' c)=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
     for x being Element of Y holds
    ((a 'or' (b 'or' c)) 'imp' ((a 'or' b) 'or' c)).x=TRUE
  proof
    let x be Element of Y;
       ((a 'or' (b 'or' c)) 'imp' ((a 'or' b) 'or' c)).x
    ='not' (a 'or' (b 'or' c)).x 'or' ((a 'or' b) 'or' c).x
     by BVFUNC_1:def 11
   .='not'( (a).x 'or' (b 'or' c).x) 'or' ((a 'or' b) 'or' c).x
     by BVFUNC_1:def 7
   .='not'( (a).x 'or' ((b).x 'or' (c).x)) 'or' ((a 'or' b) 'or' c).x
     by BVFUNC_1:def 7
   .='not'( (a).x 'or' ((b).x 'or' (c).x)) 'or'
      ((a 'or' b).x 'or' (c).x)
     by BVFUNC_1:def 7
   .='not'( (a).x 'or' ((b).x 'or' (c).x)) 'or'
      (((a).x 'or' (b).x) 'or' (c).x)
     by BVFUNC_1:def 7
   .='not'( (a).x 'or' ((b).x 'or' (c).x)) 'or'
      ((a).x 'or' ((b).x 'or' (c).x))
     by BINARITH:20
   .=TRUE by BINARITH:18;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

