:: Propositional Calculus for Boolean Valued Functions, II
::  by Shunichi Kobayashi and Yatsuka Nakamura
::
:: Received March 13, 1999
:: Copyright (c) 1999 Association of Mizar Users
 
environ

 vocabularies XBOOLE_0, SUBSET_1, FUNCT_2, MARGREL1, BVFUNC_1, XBOOLEAN,
      FUNCT_1;
 notations XBOOLE_0, SUBSET_1, XBOOLEAN, MARGREL1, FUNCT_2, BVFUNC_1;
 constructors BINARITH, BVFUNC_1;
 registrations XBOOLE_0, XBOOLEAN, MARGREL1;
 requirements ARITHM;
 definitions XBOOLEAN;
 theorems MARGREL1, BINARITH, BVFUNC_1, XBOOLEAN;

begin
 
::Chap. 1  Propositional Calculus

reserve Y for non empty set;

theorem
  for a,b being Element of Funcs(Y,BOOLEAN) holds a 'imp' (b 'imp' (a
  '&' b))=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds (a 'imp' (b 'imp' (a '&' b))).x=TRUE
  proof
    let x be Element of Y;
    (a 'imp' (b 'imp' (a '&' b))).x ='not' (a).x 'or' (b 'imp' (a '&' b)).
    x by BVFUNC_1:def 11
      .='not' (a).x 'or' ('not' b.x 'or' (a '&' b).x) by BVFUNC_1:def 11
      .='not' (a).x 'or' ('not' (b).x 'or' ((a).x '&' (b).x)) by MARGREL1:
    def 21
      .='not' (a).x 'or' (('not' (b).x 'or' (a).x) '&' ('not' (b).x 'or' (b)
    .x)) by XBOOLEAN:9
      .='not' (a).x 'or' (TRUE '&' ('not' (b).x 'or' (a).x)) by XBOOLEAN:102
      .=('not' (a).x 'or' (a).x) 'or' 'not' (b).x
      .=TRUE 'or' 'not' (b).x by XBOOLEAN:102
      .=TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b being Element of Funcs(Y,BOOLEAN) holds (a 'imp' b) 'imp' ((b
  'imp' a) 'imp' (a 'eqv' b))=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds ((a 'imp' b) 'imp' ((b 'imp' a) 'imp' (a
  'eqv' b))).x=TRUE
  proof
    let x be Element of Y;
    ((a 'imp' b) 'imp' ((b 'imp' a) 'imp' (a 'eqv' b))).x ='not' (a 'imp'
    b).x 'or' ((b 'imp' a) 'imp' (a 'eqv' b)).x by BVFUNC_1:def 11
      .='not'( 'not' (a).x 'or' (b).x) 'or' ((b 'imp' a) 'imp' (a 'eqv' b)).
    x by BVFUNC_1:def 11
      .='not'( 'not' (a).x 'or' (b).x) 'or' ('not' (b 'imp' a).x 'or' (a
    'eqv' b).x) by BVFUNC_1:def 11
      .=('not' 'not' (a).x '&' 'not' (b).x) 'or' ('not'( 'not' (b).x 'or' (a
    ).x) 'or' (a 'eqv' b).x) by BVFUNC_1:def 11
      .=((a).x '&' 'not' (b).x) 'or' (((b).x '&' 'not' (a).x) 'or' 'not'( (a
    ).x 'xor' (b).x)) by BVFUNC_1:def 12
      .=((a).x '&' 'not' (b).x) 'or' ((('not' (a).x '&' (b).x) 'or' 'not'(
    'not' (a).x '&' (b).x)) '&' (('not' (a).x '&' (b).x) 'or' 'not'( (a).x '&'
    'not' (b).x))) by XBOOLEAN:9
      .=((a).x '&' 'not' (b).x) 'or' (TRUE '&' (('not' (a).x '&' (b).x) 'or'
    'not'( (a).x '&' 'not' (b).x))) by XBOOLEAN:102
      .=(((a).x '&' 'not' (b).x) 'or' 'not'( (a).x '&' 'not' (b).x)) 'or' (
    'not' (a).x '&' (b).x)
      .=TRUE 'or' ('not' (a).x '&' (b).x) by XBOOLEAN:102
      .=TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b being Element of Funcs(Y,BOOLEAN) holds (a 'or' b) 'eqv' (b
  'or' a)=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds ((a 'or' b) 'eqv' (b 'or' a)).x=TRUE
  proof
    let x be Element of Y;
    ((a 'or' b) 'eqv' (b 'or' a)).x ='not'( (a 'or' b).x 'xor' (b 'or' a).
    x) by BVFUNC_1:def 12
      .=TRUE by XBOOLEAN:102;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b,c being Element of Funcs(Y,BOOLEAN) holds ((a '&' b) 'imp' c)
  'imp' (a 'imp' (b 'imp' c))=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds (((a '&' b) 'imp' c) 'imp' (a 'imp' (b
  'imp' c))).x=TRUE
  proof
    let x be Element of Y;
    (((a '&' b) 'imp' c) 'imp' (a 'imp' (b 'imp' c))).x ='not' ((a '&' b)
    'imp' c).x 'or' (a 'imp' (b 'imp' c)).x by BVFUNC_1:def 11
      .='not'( 'not' (a '&' b).x 'or' (c).x) 'or' (a 'imp' (b 'imp' c)).x by
    BVFUNC_1:def 11
      .='not'( 'not' ((a).x '&' (b).x) 'or' (c).x) 'or' (a 'imp' (b 'imp' c)
    ).x by MARGREL1:def 21
      .='not'( 'not'( (a).x '&' (b).x) 'or' (c).x) 'or' ('not' (a).x 'or' (b
    'imp' c).x) by BVFUNC_1:def 11
      .='not'( ('not' (a).x 'or' 'not' (b).x) 'or' (c).x) 'or' ('not' (a).x
    'or' ('not' (b).x 'or' (c).x)) by BVFUNC_1:def 11
      .=TRUE by XBOOLEAN:102;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b,c being Element of Funcs(Y,BOOLEAN) holds (a 'imp' (b 'imp' c)
  ) 'imp' ((a '&' b) 'imp' c)=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds ((a 'imp' (b 'imp' c)) 'imp' ((a '&' b)
  'imp' c)).x=TRUE
  proof
    let x be Element of Y;
    ((a 'imp' (b 'imp' c)) 'imp' ((a '&' b) 'imp' c)).x ='not' (a 'imp' (b
    'imp' c)).x 'or' ((a '&' b) 'imp' c).x by BVFUNC_1:def 11
      .='not'( 'not' (a).x 'or' (b 'imp' c).x) 'or' ((a '&' b) 'imp' c).x by
    BVFUNC_1:def 11
      .='not'( 'not' (a).x 'or' ('not' (b).x 'or' (c).x)) 'or' ((a '&' b)
    'imp' c).x by BVFUNC_1:def 11
      .='not'( 'not' (a).x 'or' ('not' (b).x 'or' (c).x)) 'or' ('not' (a '&'
    b).x 'or' (c).x) by BVFUNC_1:def 11
      .='not'( 'not' (a).x 'or' ('not' (b).x 'or' (c).x)) 'or' (('not' (a).x
    'or' 'not' (b).x) 'or' (c).x) by MARGREL1:def 21
      .=TRUE by XBOOLEAN:102;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b,c being Element of Funcs(Y,BOOLEAN) holds (c 'imp' a) 'imp' ((
  c 'imp' b) 'imp' (c 'imp' (a '&' b)))=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds ((c 'imp' a) 'imp' ((c 'imp' b) 'imp' (c
  'imp' (a '&' b)))).x=TRUE
  proof
    let x be Element of Y;
    ((c 'imp' a) 'imp' ((c 'imp' b) 'imp' (c 'imp' (a '&' b)))).x ='not' (
c 'imp' a).x 'or' ((c 'imp' b) 'imp' (c 'imp' (a '&' b))).x by BVFUNC_1:def 11
      .='not'( 'not' (c).x 'or' (a).x) 'or' ((c 'imp' b) 'imp' (c 'imp' (a
    '&' b))).x by BVFUNC_1:def 11
      .='not'( 'not' (c).x 'or' (a).x) 'or' ('not' (c 'imp' b).x 'or' (c
    'imp' (a '&' b)).x) by BVFUNC_1:def 11
      .='not'( 'not' (c).x 'or' (a).x) 'or' ('not'( 'not' (c).x 'or' (b).x)
    'or' (c 'imp' (a '&' b)).x) by BVFUNC_1:def 11
      .='not'( 'not' (c).x 'or' (a).x) 'or' ('not'( 'not' (c).x 'or' (b).x)
    'or' ('not' (c).x 'or' (a '&' b).x)) by BVFUNC_1:def 11
      .=((c).x '&' 'not' (a).x) 'or' (('not' 'not' (c).x '&' 'not' (b).x)
    'or' ('not' (c).x 'or' ((a).x '&' (b).x))) by MARGREL1:def 21
      .=((c).x '&' 'not' (a).x) 'or' (((c).x '&' 'not' (b).x) 'or' (('not' (
    c).x 'or' (a).x) '&' ('not' (c).x 'or' (b).x))) by XBOOLEAN:9
      .=((c).x '&' 'not' (a).x) 'or' ((((c).x '&' 'not' (b).x) 'or' ('not' (
    c).x 'or' (a).x)) '&' (((c).x '&' 'not' (b).x) 'or' ('not' (c).x 'or' 'not'
    'not' (b).x))) by XBOOLEAN:9
      .=((c).x '&' 'not' (a).x) 'or' (TRUE '&' (((c).x '&' 'not' (b).x) 'or'
    ('not' (c).x 'or' (a).x))) by XBOOLEAN:102
      .=(((c).x '&' 'not' (a).x) 'or' 'not'( (c).x '&' 'not' (a).x)) 'or' ((
    c).x '&' 'not' (b).x)
      .=TRUE 'or' ((c).x '&' 'not' (b).x) by XBOOLEAN:102
      .=TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b,c being Element of Funcs(Y,BOOLEAN) holds ((a 'or' b) 'imp' c)
  'imp' ((a 'imp' c) 'or' (b 'imp' c))=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds (((a 'or' b) 'imp' c) 'imp' ((a 'imp' c)
  'or' (b 'imp' c))).x=TRUE
  proof
    let x be Element of Y;
    (((a 'or' b) 'imp' c) 'imp' ((a 'imp' c) 'or' (b 'imp' c))).x ='not' (
(a 'or' b) 'imp' c).x 'or' ((a 'imp' c) 'or' (b 'imp' c)).x by BVFUNC_1:def 11
      .='not'( 'not' (a 'or' b).x 'or' (c).x) 'or' ((a 'imp' c) 'or' (b
    'imp' c)).x by BVFUNC_1:def 11
      .='not'( 'not'( (a).x 'or' (b).x) 'or' (c).x) 'or' ((a 'imp' c) 'or' (
    b 'imp' c)).x by BVFUNC_1:def 7
      .='not'( 'not'( (a).x 'or' (b).x) 'or' (c).x) 'or' ((a 'imp' c).x 'or'
    (b 'imp' c).x) by BVFUNC_1:def 7
      .='not'( 'not'( (a).x 'or' (b).x) 'or' (c).x) 'or' (('not' (a).x 'or'
    (c).x) 'or' (b 'imp' c).x) by BVFUNC_1:def 11
      .=('not' 'not'( (a).x 'or' (b).x) '&' 'not' (c).x) 'or' (('not' (a).x
    'or' (c).x) 'or' ('not' (b).x 'or' (c).x)) by BVFUNC_1:def 11
      .=(((b).x '&' 'not' (c).x) 'or' ((a).x '&' 'not' (c).x)) 'or' ('not'(
    (a).x '&' 'not' (c).x) 'or' ('not' (b).x 'or' (c).x)) by XBOOLEAN:8
      .=(((b).x '&' 'not' (c).x) 'or' (((a).x '&' 'not' (c).x) 'or' 'not'( (
    a).x '&' 'not' (c).x))) 'or' ('not' (b).x 'or' (c).x)
      .=(((b).x '&' 'not' (c).x) 'or' TRUE) 'or' ('not' (b).x 'or' (c).x) by
    XBOOLEAN:102
      .=TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b,c being Element of Funcs(Y,BOOLEAN) holds (a 'imp' c) 'imp' ((
  b 'imp' c) 'imp' ((a 'or' b) 'imp' c))=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds ((a 'imp' c) 'imp' ((b 'imp' c) 'imp' ((a
  'or' b) 'imp' c))).x=TRUE
  proof
    let x be Element of Y;
    ((a 'imp' c) 'imp' ((b 'imp' c) 'imp' ((a 'or' b) 'imp' c))).x ='not'
(a 'imp' c).x 'or' ((b 'imp' c) 'imp' ((a 'or' b) 'imp' c)).x by BVFUNC_1:def
    11
      .='not'( 'not' (a).x 'or' (c).x) 'or' ((b 'imp' c) 'imp' ((a 'or' b)
    'imp' c)).x by BVFUNC_1:def 11
      .='not'( 'not' (a).x 'or' (c).x) 'or' ('not' (b 'imp' c).x 'or' ((a
    'or' b) 'imp' c).x) by BVFUNC_1:def 11
      .='not'( 'not' (a).x 'or' (c).x) 'or' ('not'( 'not' (b).x 'or' (c).x)
    'or' ((a 'or' b) 'imp' c).x) by BVFUNC_1:def 11
      .='not'( 'not' (a).x 'or' (c).x) 'or' ('not'( 'not' (b).x 'or' (c).x)
    'or' ('not' (a 'or' b).x 'or' (c).x)) by BVFUNC_1:def 11
      .='not'( 'not' (a).x 'or' (c).x) 'or' ('not'( 'not' (b).x 'or' (c).x)
    'or' ('not' ((a).x 'or' (b).x) 'or' (c).x)) by BVFUNC_1:def 7
      .='not'( 'not' (a).x 'or' (c).x) 'or' ('not'( 'not' (b).x 'or' (c).x)
    'or' (((c).x 'or' 'not' (a).x) '&' ('not' (b).x 'or' (c).x))) by XBOOLEAN:9
      .='not'( 'not' (a).x 'or' (c).x) 'or' (('not'( 'not' (b).x 'or' (c).x)
'or' ((c).x 'or' 'not' (a).x)) '&' ('not'( 'not' (b).x 'or' (c).x) 'or' ('not'
    (b).x 'or' (c).x))) by XBOOLEAN:9
      .='not'( 'not' (a).x 'or' (c).x) 'or' (TRUE '&' ('not'( 'not' (b).x
    'or' (c).x) 'or' ((c).x 'or' 'not' (a).x))) by XBOOLEAN:102
      .='not'( 'not' (a).x 'or' (c).x) 'or' ('not'( 'not' (b).x 'or' (c).x)
    'or' ('not' (a).x 'or' (c).x))
      .=('not'( 'not' (a).x 'or' (c).x) 'or' ('not' (a).x 'or' (c).x)) 'or'
    'not'( 'not' (b).x 'or' (c).x)
      .=TRUE 'or' 'not'( 'not' (b).x 'or' (c).x) by XBOOLEAN:102
      .=TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b,c being Element of Funcs(Y,BOOLEAN) holds ((a 'imp' c) '&' (b
  'imp' c)) 'imp' ((a 'or' b) 'imp' c)=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds (((a 'imp' c) '&' (b 'imp' c)) 'imp' ((a
  'or' b) 'imp' c)).x=TRUE
  proof
    let x be Element of Y;
    (((a 'imp' c) '&' (b 'imp' c)) 'imp' ((a 'or' b) 'imp' c)).x ='not' ((
a 'imp' c) '&' (b 'imp' c)).x 'or' ((a 'or' b) 'imp' c).x by BVFUNC_1:def 11
      .='not'( (a 'imp' c).x '&' (b 'imp' c).x) 'or' ((a 'or' b) 'imp' c).x
    by MARGREL1:def 21
      .='not'( ('not' (a).x 'or' (c).x) '&' (b 'imp' c).x) 'or' ((a 'or' b)
    'imp' c).x by BVFUNC_1:def 11
      .='not'( ('not' (a).x 'or' (c).x) '&' ('not' (b).x 'or' (c).x)) 'or' (
    (a 'or' b) 'imp' c).x by BVFUNC_1:def 11
      .='not'( ('not' (a).x 'or' (c).x) '&' ('not' (b).x 'or' (c).x)) 'or' (
    'not' (a 'or' b).x 'or' (c).x) by BVFUNC_1:def 11
      .=('not'( 'not' (a).x 'or' (c).x) 'or' 'not'( 'not' (b).x 'or' (c).x))
    'or' ((c).x 'or' 'not'( (a).x 'or' (b).x)) by BVFUNC_1:def 7
      .=('not'( 'not' (a).x 'or' (c).x) 'or' 'not'( 'not' (b).x 'or' (c).x))
    'or' (('not' (a).x 'or' (c).x) '&' ((c).x 'or' 'not' (b).x)) by XBOOLEAN:9
      .=(('not'( 'not' (a).x 'or' (c).x) 'or' 'not'( 'not' (b).x 'or' (c).x)
    ) 'or' ('not' (a).x 'or' (c).x)) '&' (('not'( 'not' (a).x 'or' (c).x) 'or'
'not'( 'not' (b).x 'or' (c).x)) 'or' ('not' (b).x 'or' (c).x)) by XBOOLEAN:9
      .=('not'( 'not' (b).x 'or' (c).x) 'or' ('not'( 'not' (a).x 'or' (c).x)
'or' ('not' (a).x 'or' (c).x))) '&' ('not'( 'not' (a).x 'or' (c).x) 'or' ('not'
    ( 'not' (b).x 'or' (c).x) 'or' ('not' (b).x 'or' (c).x)))
      .=('not'( 'not' (b).x 'or' (c).x) 'or' TRUE) '&' ('not'( 'not' (a).x
'or' (c).x) 'or' ('not'( 'not' (b).x 'or' (c).x) 'or' ('not' (b).x 'or' (c).x))
    ) by XBOOLEAN:102
      .=TRUE '&' ('not'( 'not' (a).x 'or' (c).x) 'or' TRUE) by XBOOLEAN:102
      .=TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b being Element of Funcs(Y,BOOLEAN) holds (a 'imp' (b '&' 'not'
  b)) 'imp' 'not' a=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds ((a 'imp' (b '&' 'not' b)) 'imp' 'not' a)
  .x=TRUE
  proof
    let x be Element of Y;
    ((a 'imp' (b '&' 'not' b)) 'imp' 'not' a).x ='not' (a 'imp' (b '&'
    'not' b)).x 'or' ('not' a).x by BVFUNC_1:def 11
      .='not'( 'not' (a).x 'or' (b '&' 'not' b).x) 'or' ('not' a).x by
    BVFUNC_1:def 11
      .=((a).x '&' ('not' (b).x 'or' 'not' ('not' b).x)) 'or' ('not' a).x by
    MARGREL1:def 21
      .=((a).x '&' ('not' (b).x 'or' 'not' 'not' (b).x)) 'or' ('not' a).x by
    MARGREL1:def 20
      .=((a).x '&' TRUE) 'or' ('not' a).x by XBOOLEAN:102
      .=(a).x 'or' 'not' (a).x by MARGREL1:def 20
      .=TRUE by XBOOLEAN:102;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b,c being Element of Funcs(Y,BOOLEAN) holds ((a 'or' b) '&' (a
  'or' c)) 'imp' (a 'or' (b '&' c))=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds (((a 'or' b) '&' (a 'or' c)) 'imp' (a
  'or' (b '&' c))).x=TRUE
  proof
    let x be Element of Y;
    (((a 'or' b) '&' (a 'or' c)) 'imp' (a 'or' (b '&' c))).x ='not' ((a
    'or' b) '&' (a 'or' c)).x 'or' (a 'or' (b '&' c)).x by BVFUNC_1:def 11
      .='not'( (a 'or' b).x '&' (a 'or' c).x) 'or' (a 'or' (b '&' c)).x by
    MARGREL1:def 21
      .='not' (((a).x 'or' (b).x) '&' (a 'or' c).x) 'or' (a 'or' (b '&' c)).
    x by BVFUNC_1:def 7
      .='not'( ((a).x 'or' (b).x) '&' ((a).x 'or' (c).x)) 'or' (a 'or' (b
    '&' c)).x by BVFUNC_1:def 7
      .='not'( ((a).x 'or' (b).x) '&' ((a).x 'or' (c).x)) 'or' ((a).x 'or' (
    b '&' c).x) by BVFUNC_1:def 7
      .='not'( ((a).x 'or' (b).x) '&' ((a).x 'or' (c).x)) 'or' ((a).x 'or' (
    (b).x '&' (c).x)) by MARGREL1:def 21
      .=('not'( (a).x 'or' (b).x) 'or' 'not'( (a).x 'or' (c).x)) 'or' (((a).
    x 'or' (b).x) '&' ((a).x 'or' (c).x)) by XBOOLEAN:9
      .=(('not'( (a).x 'or' (c).x) 'or' 'not'( (a).x 'or' (b).x)) 'or' ((a).
x 'or' (b).x)) '&' (('not'( (a).x 'or' (b).x) 'or' 'not'( (a).x 'or' (c).x))
    'or' ((a).x 'or' (c).x)) by XBOOLEAN:9
      .=('not'( (a).x 'or' (c).x) 'or' ('not'( (a).x 'or' (b).x) 'or' ((a).x
'or' (b).x))) '&' ('not'( (a).x 'or' (b).x) 'or' ('not'( (a).x 'or' (c).x) 'or'
    ((a).x 'or' (c).x)))
      .=('not'( (a).x 'or' (c).x) 'or' TRUE) '&' ('not'( (a).x 'or' (b).x)
    'or' ('not'( (a).x 'or' (c).x) 'or' ((a).x 'or' (c).x))) by XBOOLEAN:102
      .=TRUE '&' ('not'( (a).x 'or' (b).x) 'or' TRUE) by XBOOLEAN:102
      .=TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b,c being Element of Funcs(Y,BOOLEAN) holds (a '&' (b 'or' c))
  'imp' ((a '&' b) 'or' (a '&' c))=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds ((a '&' (b 'or' c)) 'imp' ((a '&' b) 'or'
  (a '&' c))).x=TRUE
  proof
    let x be Element of Y;
    ((a '&' (b 'or' c)) 'imp' ((a '&' b) 'or' (a '&' c))).x ='not' (a '&'
    (b 'or' c)).x 'or' ((a '&' b) 'or' (a '&' c)).x by BVFUNC_1:def 11
      .='not'( (a).x '&' (b 'or' c).x) 'or' ((a '&' b) 'or' (a '&' c)).x by
    MARGREL1:def 21
      .='not' ((a).x '&' ((b).x 'or' (c).x)) 'or' ((a '&' b) 'or' (a '&' c))
    .x by BVFUNC_1:def 7
      .='not'( (a).x '&' ((b).x 'or' (c).x)) 'or' ((a '&' b).x 'or' (a '&' c
    ).x) by BVFUNC_1:def 7
      .='not'( (a).x '&' ((b).x 'or' (c).x)) 'or' (((a).x '&' (b).x) 'or' (a
    '&' c).x) by MARGREL1:def 21
      .='not'( (a).x '&' ((b).x 'or' (c).x)) 'or' (((a).x '&' (b).x) 'or' ((
    a).x '&' (c).x)) by MARGREL1:def 21
      .= (((a).x '&' (b).x) 'or' ((a).x '&' (c).x)) 'or' ('not'( (a).x '&' (
    b).x) '&' 'not' ((a).x '&' (c).x)) by XBOOLEAN:8
      .= ((((a).x '&' (c).x) 'or' ((a).x '&' (b).x)) 'or' 'not'( (a).x '&' (
b).x)) '&' ((((a).x '&' (b).x) 'or' ((a).x '&' (c).x)) 'or' 'not'( (a).x '&' (c
    ).x)) by XBOOLEAN:9
      .= (((a).x '&' (c).x) 'or' (((a).x '&' (b).x) 'or' 'not'( (a).x '&' (b
).x))) '&' (((a).x '&' (b).x) 'or' (((a).x '&' (c).x) 'or' 'not'( (a).x '&' (c)
    .x)))
      .= (((a).x '&' (c).x) 'or' TRUE) '&' (((a).x '&' (b).x) 'or' (((a).x
    '&' (c).x) 'or' 'not'( (a).x '&' (c).x))) by XBOOLEAN:102
      .= TRUE '&' (((a).x '&' (b).x) 'or' TRUE) by XBOOLEAN:102
      .= TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b,c being Element of Funcs(Y,BOOLEAN) holds ((a 'or' c) '&' (b
  'or' c)) 'imp' ((a '&' b) 'or' c)=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds (((a 'or' c) '&' (b 'or' c)) 'imp' ((a
  '&' b) 'or' c)).x=TRUE
  proof
    let x be Element of Y;
    (((a 'or' c) '&' (b 'or' c)) 'imp' ((a '&' b) 'or' c)).x ='not' ((a
    'or' c) '&' (b 'or' c)).x 'or' ((a '&' b) 'or' c).x by BVFUNC_1:def 11
      .='not'( (a 'or' c).x '&' (b 'or' c).x) 'or' ((a '&' b) 'or' c).x by
    MARGREL1:def 21
      .='not' (((a).x 'or' (c).x) '&' (b 'or' c).x) 'or' ((a '&' b) 'or' c).
    x by BVFUNC_1:def 7
      .=('not'( (a).x 'or' (c).x) 'or' 'not'( (b).x 'or' (c).x)) 'or' ((a
    '&' b) 'or' c).x by BVFUNC_1:def 7
      .=('not'( (a).x 'or' (c).x) 'or' 'not'( (b).x 'or' (c).x)) 'or' ((a
    '&' b).x 'or' (c).x) by BVFUNC_1:def 7
      .=('not'( (a).x 'or' (c).x) 'or' 'not'( (b).x 'or' (c).x)) 'or' ((c).x
    'or' ((a).x '&' (b).x)) by MARGREL1:def 21
      .=('not'( (a).x 'or' (c).x) 'or' 'not'( (b).x 'or' (c).x)) 'or' (((a).
    x 'or' (c).x) '&' ((c).x 'or' (b).x)) by XBOOLEAN:9
      .=(('not'( (a).x 'or' (c).x) 'or' 'not'( (b).x 'or' (c).x)) 'or' ((a).
x 'or' (c).x)) '&' (('not'( (a).x 'or' (c).x) 'or' 'not'( (b).x 'or' (c).x))
    'or' ((b).x 'or' (c).x)) by XBOOLEAN:9
      .=('not'( (b).x 'or' (c).x) 'or' ('not'( (a).x 'or' (c).x) 'or' ((a).x
'or' (c).x))) '&' ('not'( (a).x 'or' (c).x) 'or' ('not'( (b).x 'or' (c).x) 'or'
    ((b).x 'or' (c).x)))
      .=('not'( (b).x 'or' (c).x) 'or' TRUE) '&' ('not'( (a).x 'or' (c).x)
    'or' ('not'( (b).x 'or' (c).x) 'or' ((b).x 'or' (c).x))) by XBOOLEAN:102
      .=TRUE '&' ('not'( (a).x 'or' (c).x) 'or' TRUE) by XBOOLEAN:102
      .=TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b,c being Element of Funcs(Y,BOOLEAN) holds ((a 'or' b) '&' c)
  'imp' ((a '&' c) 'or' (b '&' c))=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds (((a 'or' b) '&' c) 'imp' ((a '&' c) 'or'
  (b '&' c))).x=TRUE
  proof
    let x be Element of Y;
    (((a 'or' b) '&' c) 'imp' ((a '&' c) 'or' (b '&' c))).x ='not' ((a
    'or' b) '&' c).x 'or' ((a '&' c) 'or' (b '&' c)).x by BVFUNC_1:def 11
      .='not'( ((a 'or' b)).x '&' (c).x) 'or' ((a '&' c) 'or' (b '&' c)).x
    by MARGREL1:def 21
      .='not'( ((a).x 'or' (b).x) '&' (c).x) 'or' ((a '&' c) 'or' (b '&' c))
    .x by BVFUNC_1:def 7
      .='not'( ((a).x 'or' (b).x) '&' (c).x) 'or' ((a '&' c).x 'or' (b '&' c
    ).x) by BVFUNC_1:def 7
      .='not'( ((a).x 'or' (b).x) '&' (c).x) 'or' (((a).x '&' (c).x) 'or' (b
    '&' c).x) by MARGREL1:def 21
      .='not'( (c).x '&' ((a).x 'or' (b).x)) 'or' (((a).x '&' (c).x) 'or' ((
    b).x '&' (c).x)) by MARGREL1:def 21
      .=(((a).x '&' (c).x) 'or' ((b).x '&' (c).x)) 'or' ('not'( (a).x '&' (c
    ).x) '&' 'not'( (b).x '&' (c).x)) by XBOOLEAN:8
      .=((((b).x '&' (c).x) 'or' ((a).x '&' (c).x)) 'or' 'not'( (a).x '&' (c
).x)) '&' ((((a).x '&' (c).x) 'or' ((b).x '&' (c).x)) 'or' 'not'( (b).x '&' (c)
    .x)) by XBOOLEAN:9
      .=(((b).x '&' (c).x) 'or' (((a).x '&' (c).x) 'or' 'not'( (a).x '&' (c)
.x))) '&' (((a).x '&' (c).x) 'or' (((b).x '&' (c).x) 'or' 'not'( (b).x '&' (c).
    x)))
      .=(((b).x '&' (c).x) 'or' TRUE) '&' (((a).x '&' (c).x) 'or' (((b).x
    '&' (c).x) 'or' 'not'( (b).x '&' (c).x))) by XBOOLEAN:102
      .=TRUE '&' (((a).x '&' (c).x) 'or' TRUE) by XBOOLEAN:102
      .=TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b being Element of Funcs(Y,BOOLEAN) holds (a '&' b)=I_el(Y)
  implies (a 'or' b)=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  assume
A1: (a '&' b)=I_el(Y);
  for x being Element of Y holds (a 'or' b).x=TRUE
  proof
    let x be Element of Y;
    (a '&' b).x= TRUE by A1,BVFUNC_1:def 14;
    then
A2: (a).x '&' (b).x=TRUE by MARGREL1:def 21;
    then (a).x=TRUE by MARGREL1:45;
    then (a 'or' b).x =TRUE 'or' TRUE by A2,BVFUNC_1:def 7
      .=TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b,c being Element of Funcs(Y,BOOLEAN) holds (a 'imp' b)=I_el(Y)
  implies (a 'or' c) 'imp' (b 'or' c)=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
  assume
A1: (a 'imp' b)=I_el(Y);
  for x being Element of Y holds ((a 'or' c) 'imp' (b 'or' c)).x=TRUE
  proof
    let x be Element of Y;
    (a 'imp' b).x= TRUE by A1,BVFUNC_1:def 14;
    then
A2: 'not' (a).x 'or' (b).x = TRUE by BVFUNC_1:def 11;
    ((a 'or' c) 'imp' (b 'or' c)).x ='not' (a 'or' c).x 'or' (b 'or' c).x
    by BVFUNC_1:def 11
      .='not'( (a).x 'or' (c).x) 'or' (b 'or' c).x by BVFUNC_1:def 7
      .=((b).x 'or' (c).x) 'or' ('not' (a).x '&' 'not' (c).x) by BVFUNC_1:
    def 7
      .=(((c).x 'or' (b).x) 'or' 'not' (a).x) '&' (((b).x 'or' (c).x) 'or'
    'not' (c).x) by XBOOLEAN:9
      .=((c).x 'or' ('not' (a).x 'or' (b).x)) '&' (((b).x 'or' (c).x) 'or'
    'not' (c).x)
      .=TRUE '&' ((b).x 'or' ((c).x 'or' 'not' (c).x)) by A2
      .=TRUE '&' ((b).x 'or' TRUE) by XBOOLEAN:102
      .=TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b,c being Element of Funcs(Y,BOOLEAN) holds (a 'imp' b)=I_el(Y)
  implies (a '&' c) 'imp' (b '&' c)=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
  assume
A1: (a 'imp' b)=I_el(Y);
  for x being Element of Y holds ((a '&' c) 'imp' (b '&' c)).x=TRUE
  proof
    let x be Element of Y;
    (a 'imp' b).x= TRUE by A1,BVFUNC_1:def 14;
    then
A2: 'not' (a).x 'or' (b).x = TRUE by BVFUNC_1:def 11;
    ((a '&' c) 'imp' (b '&' c)).x ='not' (a '&' c).x 'or' (b '&' c).x by
    BVFUNC_1:def 11
      .='not'( (a).x '&' (c).x) 'or' (b '&' c).x by MARGREL1:def 21
      .=('not' (a).x 'or' 'not' (c).x) 'or' ((b).x '&' (c).x) by MARGREL1:
    def 21
      .=(('not' (c).x 'or' 'not' (a).x) 'or' (b).x) '&' (('not' (a).x 'or'
    'not' (c).x) 'or' (c).x) by XBOOLEAN:9
      .=('not' (c).x 'or' ('not' (a).x 'or' (b).x)) '&' ('not' (a).x 'or' (
    'not' (c).x 'or' (c).x))
      .=('not' (c).x 'or' ('not' (a).x 'or' (b).x)) '&' ('not' (a).x 'or'
    TRUE) by XBOOLEAN:102
      .=TRUE by A2;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b,c being Element of Funcs(Y,BOOLEAN) holds (c 'imp' a)=I_el(Y)
  & (c 'imp' b)=I_el(Y) implies c 'imp' (a '&' b)=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
  assume that
A1: (c 'imp' a)=I_el(Y) and
A2: (c 'imp' b)=I_el(Y);
  for x being Element of Y holds (c 'imp' (a '&' b)).x=TRUE
  proof
    let x be Element of Y;
    (c 'imp' a).x= TRUE by A1,BVFUNC_1:def 14;
    then
A3: 'not' (c).x 'or' (a).x = TRUE by BVFUNC_1:def 11;
    (c 'imp' b).x= TRUE by A2,BVFUNC_1:def 14;
    then
A4: 'not' (c).x 'or' (b).x = TRUE by BVFUNC_1:def 11;
    (c 'imp' (a '&' b)).x ='not' (c).x 'or' (a '&' b).x by BVFUNC_1:def 11
      .='not' (c).x 'or' ((a).x '&' (b).x) by MARGREL1:def 21
      .=TRUE '&' TRUE by A3,A4,XBOOLEAN:9
      .=TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b,c being Element of Funcs(Y,BOOLEAN) holds (a 'imp' c)=I_el(Y)
  & (b 'imp' c)=I_el(Y) implies (a 'or' b) 'imp' c = I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
  assume that
A1: (a 'imp' c)=I_el(Y) and
A2: (b 'imp' c)=I_el(Y);
  for x being Element of Y holds ((a 'or' b) 'imp' c).x=TRUE
  proof
    let x be Element of Y;
    (a 'imp' c).x= TRUE by A1,BVFUNC_1:def 14;
    then
A3: 'not' (a).x 'or' (c).x = TRUE by BVFUNC_1:def 11;
    (b 'imp' c).x= TRUE by A2,BVFUNC_1:def 14;
    then
A4: 'not' (b).x 'or' (c).x = TRUE by BVFUNC_1:def 11;
    ((a 'or' b) 'imp' c).x ='not' (a 'or' b).x 'or' (c).x by BVFUNC_1:def
    11
      .='not'( (a).x 'or' (b).x) 'or' (c).x by BVFUNC_1:def 7
      .=TRUE '&' TRUE by A3,A4,XBOOLEAN:9
      .=TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b being Element of Funcs(Y,BOOLEAN) holds (a 'or' b)=I_el(Y) &
  'not' a=I_el(Y) implies b=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  assume that
A1: (a 'or' b)=I_el(Y) and
A2: 'not' a=I_el(Y);
  for x being Element of Y holds (b).x=TRUE
  proof
    let x be Element of Y;
    ('not' a).x= TRUE by A2,BVFUNC_1:def 14;
    then
A3: 'not' (a).x = TRUE by MARGREL1:def 20;
    (a 'or' b).x= TRUE by A1,BVFUNC_1:def 14;
    then (a).x 'or' (b).x = TRUE by BVFUNC_1:def 7;
    hence thesis by A3;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b,c,d being Element of Funcs(Y,BOOLEAN) holds (a 'imp' b)=I_el(Y
  ) & (c 'imp' d)=I_el(Y) implies (a '&' c) 'imp' (b '&' d)=I_el(Y)
proof
  let a,b,c,d be Element of Funcs(Y,BOOLEAN);
  assume that
A1: (a 'imp' b)=I_el(Y) and
A2: (c 'imp' d)=I_el(Y);
  for x being Element of Y holds ((a '&' c) 'imp' (b '&' d)).x=TRUE
  proof
    let x be Element of Y;
    (a 'imp' b).x= TRUE by A1,BVFUNC_1:def 14;
    then
A3: 'not' (a).x 'or' (b).x = TRUE by BVFUNC_1:def 11;
    (c 'imp' d).x= TRUE by A2,BVFUNC_1:def 14;
    then
A4: 'not' (c).x 'or' (d).x = TRUE by BVFUNC_1:def 11;
    ((a '&' c) 'imp' (b '&' d)).x ='not' (a '&' c).x 'or' (b '&' d).x by
    BVFUNC_1:def 11
      .='not'( (a).x '&' (c).x) 'or' (b '&' d).x by MARGREL1:def 21
      .=('not' (a).x 'or' 'not' (c).x) 'or' ((b).x '&' (d).x) by MARGREL1:
    def 21
      .=(('not' (c).x 'or' 'not' (a).x) 'or' (b).x) '&' (('not' (a).x 'or'
    'not' (c).x) 'or' (d).x) by XBOOLEAN:9
      .=('not' (c).x 'or' ('not' (a).x 'or' (b).x)) '&' (('not' (a).x 'or'
    'not' (c).x) 'or' (d).x)
      .=TRUE '&' ('not' (a).x 'or' TRUE) by A3,A4,BINARITH:20
      .=TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b,c,d being Element of Funcs(Y,BOOLEAN) holds (a 'imp' b)=I_el(Y
  ) & (c 'imp' d)=I_el(Y) implies (a 'or' c) 'imp' (b 'or' d) =I_el(Y)
proof
  let a,b,c,d be Element of Funcs(Y,BOOLEAN);
  assume that
A1: (a 'imp' b)=I_el(Y) and
A2: (c 'imp' d)=I_el(Y);
  for x being Element of Y holds ((a 'or' c) 'imp' (b 'or' d)).x=TRUE
  proof
    let x be Element of Y;
    (a 'imp' b).x= TRUE by A1,BVFUNC_1:def 14;
    then
A3: 'not' (a).x 'or' (b).x = TRUE by BVFUNC_1:def 11;
    (c 'imp' d).x= TRUE by A2,BVFUNC_1:def 14;
    then
A4: 'not' (c).x 'or' (d).x = TRUE by BVFUNC_1:def 11;
    ((a 'or' c) 'imp' (b 'or' d)).x ='not' (a 'or' c).x 'or' (b 'or' d).x
    by BVFUNC_1:def 11
      .='not'( (a).x 'or' (c).x) 'or' (b 'or' d).x by BVFUNC_1:def 7
      .=((b).x 'or' (d).x) 'or' ('not' (a).x '&' 'not' (c).x) by BVFUNC_1:
    def 7
      .=(((d).x 'or' (b).x) 'or' 'not' (a).x) '&' (((b).x 'or' (d).x) 'or'
    'not' (c).x) by XBOOLEAN:9
      .=((d).x 'or' ((b).x 'or' 'not' (a).x)) '&' (((b).x 'or' (d).x) 'or'
    'not' (c).x)
      .=TRUE '&' ((b).x 'or' TRUE) by A3,A4,BINARITH:20
      .=TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b being Element of Funcs(Y,BOOLEAN) holds (a '&' 'not' b) 'imp'
  'not' a=I_el(Y) implies (a 'imp' b)=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  assume
A1: (a '&' 'not' b) 'imp' 'not' a=I_el(Y);
  for x being Element of Y holds (a 'imp' b).x=TRUE
  proof
    let x be Element of Y;
    ((a '&' 'not' b) 'imp' 'not' a).x=TRUE by A1,BVFUNC_1:def 14;
    then 'not' (a '&' 'not' b).x 'or' ('not' a).x = TRUE by
    BVFUNC_1:def 11;
    then 'not'( (a).x '&' ('not' b).x) 'or' ('not' a).x=TRUE by
    MARGREL1:def
    21;
    then ('not' (a).x 'or' 'not' 'not' (b).x) 'or' ('not' a).x=TRUE by
    MARGREL1:def 20;
    then ('not' (a).x 'or' (b).x) 'or' 'not' (a).x=TRUE by
    MARGREL1:def 20;
    then (b).x 'or' ('not' (a).x 'or' 'not' (a).x)=TRUE by XBOOLEAN:4;
    hence thesis by BVFUNC_1:def 11;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

canceled;

theorem
  for a,b being Element of Funcs(Y,BOOLEAN) holds a 'imp' 'not' b=I_el(Y
  ) implies b 'imp' 'not' a=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  assume
A1: a 'imp' 'not' b=I_el(Y);
  for x being Element of Y holds (b 'imp' 'not' a).x=TRUE
  proof
    let x be Element of Y;
    (a 'imp' 'not' b).x=TRUE by A1,BVFUNC_1:def 14;
    then ('not' (a).x) 'or' ('not' b).x=TRUE by BVFUNC_1:def 11;
    then
A2: 'not' (a).x 'or' 'not' (b).x=TRUE by MARGREL1:def 20;
    (b 'imp' 'not' a).x =('not' (b).x) 'or' ('not' a).x by BVFUNC_1:def 11
      .=TRUE by A2,MARGREL1:def 20;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b being Element of Funcs(Y,BOOLEAN) holds 'not' a 'imp' b=I_el(Y
  ) implies 'not' b 'imp' a=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  assume
A1: 'not' a 'imp' b=I_el(Y);
  for x being Element of Y holds ('not' b 'imp' a).x=TRUE
  proof
    let x be Element of Y;
    ('not' a 'imp' b).x=TRUE by A1,BVFUNC_1:def 14;
    then ('not' ('not' a).x) 'or' (b).x=TRUE by BVFUNC_1:def 11;
    then
A2: 'not' 'not' (a).x 'or' (b).x=TRUE by MARGREL1:def 20;
    ('not' b 'imp' a).x =('not' ('not' b).x) 'or' (a).x by BVFUNC_1:def 11
      .=TRUE by A2,MARGREL1:def 20;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b being Element of Funcs(Y,BOOLEAN) holds a 'imp' (a 'or' b)= I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds (a 'imp' (a 'or' b)).x=TRUE
  proof
    let x be Element of Y;
    (a 'imp' (a 'or' b)).x ='not' (a).x 'or' (a 'or' b).x by BVFUNC_1:def
    11
      .='not' (a).x 'or' ((a).x 'or' (b).x) by BVFUNC_1:def 7
      .=('not' (a).x 'or' (a).x) 'or' (b).x
      .=TRUE 'or' (b).x by XBOOLEAN:102
      .=TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b being Element of Funcs(Y,BOOLEAN) holds (a 'or' b) 'imp' (
  'not' a 'imp' b)=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds ((a 'or' b) 'imp' ('not' a 'imp' b)).x= TRUE
  proof
    let x be Element of Y;
    ((a 'or' b) 'imp' ('not' a 'imp' b)).x ='not' (a 'or' b).x 'or' ('not'
    a 'imp' b).x by BVFUNC_1:def 11
      .='not'( (a).x 'or' (b).x) 'or' ('not' a 'imp' b).x by BVFUNC_1:def 7
      .=('not' (a).x '&' 'not' (b).x) 'or' ('not' ('not' a).x 'or' (b).x) by
    BVFUNC_1:def 11
      .=((a).x 'or' (b).x) 'or' ('not' (a).x '&' 'not' (b).x) by MARGREL1:
    def 20
      .=(((a).x 'or' (b).x) 'or' 'not' (a).x) '&' (((a).x 'or' (b).x) 'or'
    'not' (b).x) by XBOOLEAN:9
      .=(((a).x 'or' 'not' (a).x) 'or' (b).x) '&' ((a).x 'or' ((b).x 'or'
    'not' (b).x))
      .=(TRUE 'or' (b).x) '&' ((a).x 'or' ((b).x 'or' 'not' (b).x)) by
    XBOOLEAN:102
      .=TRUE '&' ((a).x 'or' TRUE) by XBOOLEAN:102
      .=TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem Th29:
  for a,b being Element of Funcs(Y,BOOLEAN) holds 'not'( a 'or' b)
  'imp' ('not' a '&' 'not' b)=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds ('not'( a 'or' b) 'imp' ('not' a '&'
  'not' b)).x=TRUE
  proof
    let x be Element of Y;
    ('not'( a 'or' b) 'imp' ('not' a '&' 'not' b)).x ='not' ('not'( a 'or'
    b)).x 'or' ('not' a '&' 'not' b).x by BVFUNC_1:def 11
      .=(a 'or' b).x 'or' ('not' a '&' 'not' b).x by MARGREL1:def 20
      .=((a).x 'or' (b).x) 'or' ('not' a '&' 'not' b).x by BVFUNC_1:def 7
      .=((a).x 'or' (b).x) 'or' (('not' a).x '&' ('not' b).x) by MARGREL1:
    def 21
      .=((a).x 'or' (b).x) 'or' ('not' (a).x '&' ('not' b).x) by MARGREL1:
    def 20
      .=((a).x 'or' (b).x) 'or' ('not' (a).x '&' 'not' (b).x) by MARGREL1:
    def 20
      .=(((a).x 'or' (b).x) 'or' 'not' (a).x) '&' (((a).x 'or' (b).x) 'or'
    'not' (b).x) by XBOOLEAN:9
      .=(((a).x 'or' 'not' (a).x) 'or' (b).x) '&' ((a).x 'or' ((b).x 'or'
    'not' (b).x))
      .=(TRUE 'or' (b).x) '&' ((a).x 'or' ((b).x 'or' 'not' (b).x)) by
    XBOOLEAN:102
      .=TRUE '&' ((a).x 'or' TRUE) by XBOOLEAN:102
      .=TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b being Element of Funcs(Y,BOOLEAN) holds ('not' a '&' 'not' b)
  'imp' 'not'( a 'or' b)=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  thus ('not' a '&' 'not' b) 'imp' 'not'( a 'or' b) = 'not'( a 'or' b) 'imp'
  'not' (a 'or' b) by BVFUNC_1:16
    .= 'not'( a 'or' b) 'imp' ('not' a '&' 'not' b) by BVFUNC_1:16
    .=I_el Y by Th29;
end;

theorem
  for a,b being Element of Funcs(Y,BOOLEAN) holds 'not'( a 'or' b) 'imp'
  'not' a=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds ('not'( a 'or' b) 'imp' 'not' a).x=TRUE
  proof
    let x be Element of Y;
    ('not'( a 'or' b) 'imp' 'not' a).x ='not' ('not'( a 'or' b)).x 'or' (
    'not' a).x by BVFUNC_1:def 11
      .='not' 'not' (a 'or' b).x 'or' ('not' a).x by MARGREL1:def 20
      .=(a 'or' b).x 'or' 'not' (a).x by MARGREL1:def 20
      .=((a).x 'or' (b).x) 'or' 'not' (a).x by BVFUNC_1:def 7
      .=((a).x 'or' 'not' (a).x) 'or' (b).x
      .=TRUE 'or' (b).x by XBOOLEAN:102
      .=TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a being Element of Funcs(Y,BOOLEAN) holds (a 'or' a) 'imp' a=I_el( Y)
proof
  let a be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds ((a 'or' a) 'imp' a).x=TRUE
  proof
    let x be Element of Y;
    ((a 'or' a) 'imp' a).x ='not' (a).x 'or' (a).x by BVFUNC_1:def 11
      .=TRUE by XBOOLEAN:102;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b being Element of Funcs(Y,BOOLEAN) holds (a '&' 'not' a) 'imp'
  b=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds ((a '&' 'not' a) 'imp' b).x=TRUE
  proof
    let x be Element of Y;
    ((a '&' 'not' a) 'imp' b).x ='not' (a '&' 'not' a).x 'or' (b).x by
    BVFUNC_1:def 11
      .='not'( (a).x '&' ('not' a).x) 'or' (b).x by MARGREL1:def 21
      .=('not' (a).x 'or' 'not' 'not' (a).x) 'or' (b).x by MARGREL1:def 20
      .=TRUE 'or' (b).x by XBOOLEAN:102
      .=TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b being Element of Funcs(Y,BOOLEAN) holds (a 'imp' b) 'imp' (
  'not' a 'or' b)=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds ((a 'imp' b) 'imp' ('not' a 'or' b)).x= TRUE
  proof
    let x be Element of Y;
    ((a 'imp' b) 'imp' ('not' a 'or' b)).x ='not' (a 'imp' b).x 'or' (
    'not' a 'or' b).x by BVFUNC_1:def 11
      .='not'( 'not' (a).x 'or' (b).x) 'or' ('not' a 'or' b).x by BVFUNC_1:
    def 11
      .=('not' 'not' (a).x '&' 'not' (b).x) 'or' (('not' a).x 'or' (b).x) by
    BVFUNC_1:def 7
      .=('not' (a).x 'or' (b).x) 'or' ((a).x '&' 'not' (b).x) by MARGREL1:
    def 20
      .=(('not' (a).x 'or' (b).x) 'or' (a).x) '&' (('not' (a).x 'or' (b).x)
    'or' 'not' (b).x) by XBOOLEAN:9
      .=(('not' (a).x 'or' (a).x) 'or' (b).x) '&' ('not' (a).x 'or' ((b).x
    'or' 'not' (b).x))
      .=(TRUE 'or' (b).x) '&' ('not' (a).x 'or' ((b).x 'or' 'not' (b).x)) by
    XBOOLEAN:102
      .=TRUE '&' ('not' (a).x 'or' TRUE) by XBOOLEAN:102
      .=TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b being Element of Funcs(Y,BOOLEAN) holds (a '&' b) 'imp' 'not'(
  a 'imp' 'not' b)=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds ((a '&' b) 'imp' 'not'( a 'imp' 'not' b))
  .x=TRUE
  proof
    let x be Element of Y;
    ((a '&' b) 'imp' 'not'( a 'imp' 'not' b)).x ='not' (a '&' b).x 'or' (
    'not'( a 'imp' 'not' b)).x by BVFUNC_1:def 11
      .='not'( (a).x '&' (b).x) 'or' ('not'( a 'imp' 'not' b)).x by MARGREL1
    :def 21
      .=('not' (a).x 'or' 'not' (b).x) 'or' 'not' (a 'imp' 'not' b).x by
    MARGREL1:def 20
      .=('not' (a).x 'or' 'not' (b).x) 'or' 'not'( 'not' (a).x 'or' ('not' b
    ).x) by BVFUNC_1:def 11
      .=('not' (a).x 'or' 'not' (b).x) 'or' ((a).x '&' (b).x) by MARGREL1:
    def 20
      .=(('not' (a).x 'or' 'not' (b).x) 'or' (a).x) '&' (('not' (a).x 'or'
    'not' (b).x) 'or' (b).x) by XBOOLEAN:9
      .=(('not' (a).x 'or' (a).x) 'or' 'not' (b).x) '&' ('not' (a).x 'or' (
    'not' (b).x 'or' (b).x))
      .=(TRUE 'or' 'not' (b).x) '&' ('not' (a).x 'or' ('not' (b).x 'or' (b).
    x)) by XBOOLEAN:102
      .=TRUE '&' ('not' (a).x 'or' TRUE) by XBOOLEAN:102
      .=TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b being Element of Funcs(Y,BOOLEAN) holds 'not'( a 'imp' 'not' b
  ) 'imp' (a '&' b)=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds ('not'( a 'imp' 'not' b) 'imp' (a '&' b))
  .x=TRUE
  proof
    let x be Element of Y;
    ('not'( a 'imp' 'not' b) 'imp' (a '&' b)).x ='not' ('not'( a 'imp'
    'not' b)).x 'or' (a '&' b).x by BVFUNC_1:def 11
      .='not' 'not' (a 'imp' 'not' b).x 'or' (a '&' b).x by MARGREL1:def 20
      .='not' 'not' (a 'imp' 'not' b).x 'or' ((a).x '&' (b).x) by MARGREL1:
    def 21
      .=('not' (a).x 'or' ('not' b).x) 'or' ((a).x '&' (b).x) by BVFUNC_1:
    def 11
      .=('not' (a).x 'or' 'not' (b).x) 'or' ((a).x '&' (b).x) by MARGREL1:
    def 20
      .=(('not' (a).x 'or' 'not' (b).x) 'or' (a).x) '&' (('not' (a).x 'or'
    'not' (b).x) 'or' (b).x) by XBOOLEAN:9
      .=(('not' (a).x 'or' (a).x) 'or' 'not' (b).x) '&' ('not' (a).x 'or' (
    'not' (b).x 'or' (b).x))
      .=(TRUE 'or' 'not' (b).x) '&' ('not' (a).x 'or' ('not' (b).x 'or' (b).
    x)) by XBOOLEAN:102
      .=TRUE '&' ('not' (a).x 'or' TRUE) by XBOOLEAN:102
      .=TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem Th37:
  for a,b being Element of Funcs(Y,BOOLEAN) holds 'not'( a '&' b)
  'imp' ('not' a 'or' 'not' b)=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds ('not'( a '&' b) 'imp' ('not' a 'or'
  'not' b)).x=TRUE
  proof
    let x be Element of Y;
    ('not'( a '&' b) 'imp' ('not' a 'or' 'not' b)).x ='not' ('not'( a '&'
    b)).x 'or' ('not' a 'or' 'not' b).x by BVFUNC_1:def 11
      .=(a '&' b).x 'or' ('not' a 'or' 'not' b).x by MARGREL1:def 20
      .=((a).x '&' (b).x) 'or' ('not' a 'or' 'not' b).x by MARGREL1:def 21
      .=(('not' a).x 'or' ('not' b).x) 'or' ((a).x '&' (b).x) by BVFUNC_1:
    def 7
      .=('not' (a).x 'or' ('not' b).x) 'or' ((a).x '&' (b).x) by MARGREL1:
    def 20
      .=('not' (a).x 'or' 'not' (b).x) 'or' ((a).x '&' (b).x) by MARGREL1:
    def 20
      .=(('not' (a).x 'or' 'not' (b).x) 'or' (a).x) '&' (('not' (a).x 'or'
    'not' (b).x) 'or' (b).x) by XBOOLEAN:9
      .=(('not' (a).x 'or' (a).x) 'or' 'not' (b).x) '&' ('not' (a).x 'or' (
    'not' (b).x 'or' (b).x))
      .=(TRUE 'or' 'not' (b).x) '&' ('not' (a).x 'or' ('not' (b).x 'or' (b).
    x)) by XBOOLEAN:102
      .=TRUE '&' ('not' (a).x 'or' TRUE) by XBOOLEAN:102
      .=TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b being Element of Funcs(Y,BOOLEAN) holds ('not' a 'or' 'not' b)
  'imp' 'not'( a '&' b)=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  thus ('not' a 'or' 'not' b) 'imp' 'not'( a '&' b) = 'not'( a '&' b) 'imp'
  'not' (a '&' b) by BVFUNC_1:17
    .= 'not'( a '&' b) 'imp' ('not' a 'or' 'not' b) by BVFUNC_1:17
    .=I_el Y by Th37;
end;

theorem
  for a,b being Element of Funcs(Y,BOOLEAN) holds (a '&' b) 'imp' a=I_el (Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds ((a '&' b) 'imp' a).x=TRUE
  proof
    let x be Element of Y;
    ((a '&' b) 'imp' a).x ='not' (a '&' b).x 'or' (a).x by BVFUNC_1:def 11
      .=('not' (a).x 'or' 'not' (b).x) 'or' (a).x by MARGREL1:def 21
      .=('not' (a).x 'or' (a).x) 'or' 'not' (b).x
      .=TRUE 'or' 'not' (b).x by XBOOLEAN:102
      .=TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b being Element of Funcs(Y,BOOLEAN) holds (a '&' b) 'imp' (a
  'or' b)=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds ((a '&' b) 'imp' (a 'or' b)).x=TRUE
  proof
    let x be Element of Y;
    ((a '&' b) 'imp' (a 'or' b)).x ='not' (a '&' b).x 'or' (a 'or' b).x by
    BVFUNC_1:def 11
      .=('not' (a).x 'or' 'not' (b).x) 'or' (a 'or' b).x by MARGREL1:def 21
      .=('not' (a).x 'or' 'not' (b).x) 'or' ((a).x 'or' (b).x) by BVFUNC_1:
    def 7
      .=('not' (b).x 'or' ('not' (a).x 'or' (a).x)) 'or' (b).x
      .=('not' (b).x 'or' TRUE) 'or' (b).x by XBOOLEAN:102
      .=TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b being Element of Funcs(Y,BOOLEAN) holds (a '&' b) 'imp' b=I_el (Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds ((a '&' b) 'imp' b).x=TRUE
  proof
    let x be Element of Y;
    ((a '&' b) 'imp' b).x ='not' (a '&' b).x 'or' (b).x by BVFUNC_1:def 11
      .=('not' (a).x 'or' 'not' (b).x) 'or' (b).x by MARGREL1:def 21
      .='not' (a).x 'or' ('not' (b).x 'or' (b).x)
      .='not' (a).x 'or' TRUE by XBOOLEAN:102
      .=TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a being Element of Funcs(Y,BOOLEAN) holds a 'imp' a '&' a=I_el(Y)
proof
  let a be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds (a 'imp' (a '&' a)).x=TRUE
  proof
    let x be Element of Y;
    (a 'imp' a '&' a).x =TRUE '&' ('not' (a).x 'or' (a).x) by BVFUNC_1:def
    11
      .=TRUE by XBOOLEAN:102;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem Th43:
  for a,b being Element of Funcs(Y,BOOLEAN) holds (a 'eqv' b)
  'imp' (a 'imp' b)=I_el(Y)
proof
  let a,b be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds ((a 'eqv' b) 'imp' (a 'imp' b)).x=TRUE
  proof
    let x be Element of Y;
    ((a 'eqv' b) 'imp' (a 'imp' b)).x ='not' (a 'eqv' b).x 'or' (a 'imp' b
    ).x by BVFUNC_1:def 11
      .=((a).x 'xor' (b).x) 'or' (a 'imp' b).x by BVFUNC_1:def 12
      .=(('not' (a).x '&' (b).x) 'or' ((a).x '&' 'not' (b).x)) 'or' 'not'( (
    a).x '&' 'not' (b).x) by BVFUNC_1:def 11
      .=('not' (a).x '&' (b).x) 'or' (((a).x '&' 'not' (b).x) 'or' 'not'( (a
    ).x '&' 'not' (b).x))
      .=('not' (a).x '&' (b).x) 'or' TRUE by XBOOLEAN:102
      .=TRUE;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b being Element of Funcs(Y,BOOLEAN) holds (a 'eqv' b) 'imp' (b
  'imp' a)=I_el(Y) by Th43;

theorem
  for a,b,c being Element of Funcs(Y,BOOLEAN) holds ((a 'or' b) 'or' c)
  'imp' (a 'or' (b 'or' c))=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds (((a 'or' b) 'or' c) 'imp' (a 'or' (b
  'or' c))).x=TRUE
  proof
    let x be Element of Y;
    (((a 'or' b) 'or' c) 'imp' (a 'or' (b 'or' c))).x ='not' ((a 'or' b)
    'or' c).x 'or' (a 'or' (b 'or' c)).x by BVFUNC_1:def 11
      .='not'( (a 'or' b).x 'or' (c).x) 'or' (a 'or' (b 'or' c)).x by
    BVFUNC_1:def 7
      .='not'( ((a).x 'or' (b).x) 'or' (c).x) 'or' (a 'or' (b 'or' c)).x by
    BVFUNC_1:def 7
      .='not'( ((a).x 'or' (b).x) 'or' (c).x) 'or' ((a).x 'or' (b 'or' c).x)
    by BVFUNC_1:def 7
      .='not'( ((a).x 'or' (b).x) 'or' (c).x) 'or' ((a).x 'or' ((b).x 'or' (
    c).x)) by BVFUNC_1:def 7
      .=TRUE by XBOOLEAN:102;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b,c being Element of Funcs(Y,BOOLEAN) holds ((a '&' b) '&' c)
  'imp' (a '&' (b '&' c))=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds (((a '&' b) '&' c) 'imp' (a '&' (b '&' c)
  )).x=TRUE
  proof
    let x be Element of Y;
    (((a '&' b) '&' c) 'imp' (a '&' (b '&' c))).x ='not' ((a '&' b) '&' c)
    .x 'or' (a '&' (b '&' c)).x by BVFUNC_1:def 11
      .='not'( (a '&' b).x '&' (c).x) 'or' (a '&' (b '&' c)).x by MARGREL1:
    def 21
      .='not'( ((a).x '&' (b).x) '&' (c).x) 'or' (a '&' (b '&' c)).x by
    MARGREL1:def 21
      .='not'( ((a).x '&' (b).x) '&' (c).x) 'or' ((a).x '&' (b '&' c).x) by
    MARGREL1:def 21
      .='not'( ((a).x '&' (b).x) '&' (c).x) 'or' ((a).x '&' ((b).x '&' (c).x
    )) by MARGREL1:def 21
      .=TRUE by XBOOLEAN:102;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;

theorem
  for a,b,c being Element of Funcs(Y,BOOLEAN) holds (a 'or' (b 'or' c))
  'imp' ((a 'or' b) 'or' c)=I_el(Y)
proof
  let a,b,c be Element of Funcs(Y,BOOLEAN);
  for x being Element of Y holds ((a 'or' (b 'or' c)) 'imp' ((a 'or' b)
  'or' c)).x=TRUE
  proof
    let x be Element of Y;
    ((a 'or' (b 'or' c)) 'imp' ((a 'or' b) 'or' c)).x ='not' (a 'or' (b
    'or' c)).x 'or' ((a 'or' b) 'or' c).x by BVFUNC_1:def 11
      .='not'( (a).x 'or' (b 'or' c).x) 'or' ((a 'or' b) 'or' c).x by
    BVFUNC_1:def 7
      .='not'( (a).x 'or' ((b).x 'or' (c).x)) 'or' ((a 'or' b) 'or' c).x by
    BVFUNC_1:def 7
      .='not'( (a).x 'or' ((b).x 'or' (c).x)) 'or' ((a 'or' b).x 'or' (c).x)
    by BVFUNC_1:def 7
      .='not'( (a).x 'or' ((b).x 'or' (c).x)) 'or' (((a).x 'or' (b).x) 'or'
    (c).x) by BVFUNC_1:def 7
      .=TRUE by XBOOLEAN:102;
    hence thesis;
  end;
  hence thesis by BVFUNC_1:def 14;
end;
