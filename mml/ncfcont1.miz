:: Continuous Functions on Real and Complex Normed Linear Spaces
::  by Noboru Endou
::
:: Received August 20, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies PRE_TOPC, FCONT_1, SUBSET_1, RLVECT_1, FUNCT_1, ARYTM, ARYTM_1,
      ARYTM_3, RELAT_1, ABSVALUE, ORDINAL2, SEQ_1, SEQ_2, SEQM_3, COMPTS_1,
      RCOMP_1, BOOLE, FINSEQ_4, SEQ_4, PARTFUN1, PARTFUN2, NORMSP_1, CLVECT_1,
      COMPLEX1, COMSEQ_1, CFUNCT_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, RELSET_1, PARTFUN1,
      PARTFUN2, FUNCT_2, PRE_TOPC, STRUCT_0, XREAL_0, ORDINAL1, NUMBERS,
      REAL_1, NAT_1, FINSEQ_4, RLVECT_1, SEQ_1, SEQ_2, SEQM_3, SEQ_4, VFUNCT_1,
      NORMSP_1, COMPLEX1, COMSEQ_1, CFUNCT_1, CFCONT_1, RCOMP_1, COMSEQ_2,
      COMSEQ_3, VFUNCT_2, CLVECT_1, LOPBAN_3, NFCONT_1, CLOPBAN3;
 constructors REAL_1, BINARITH, SEQ_2, SEQ_4, PREPOWER, PARTFUN1, PARTFUN2,
      FINSEQ_4, RCOMP_1, VFUNCT_1, LOPBAN_3, NFCONT_1, COMPLEX1, CLOPBAN3,
      COMSEQ_1, COMSEQ_2, COMSEQ_3, VFUNCT_2, CFCONT_1;
 registrations RELSET_1, STRUCT_0, ORDINAL2, MEMBERED, FUNCT_1, SEQM_3, NAT_1;
 requirements SUBSET, REAL, BOOLE, NUMERALS, ARITHM;
 definitions TARSKI, SEQM_3, LOPBAN_3, COMSEQ_3, CLOPBAN3, NFCONT_1;
 theorems AXIOMS, TARSKI, ABSVALUE, XBOOLE_0, XBOOLE_1, SUBSET_1, REAL_1,
      RLVECT_1, XCMPLX_0, XCMPLX_1, ZFMISC_1, RCOMP_1, XREAL_0, SEQ_2, NAT_1,
      SQUARE_1, RELAT_1, FUNCT_1, FINSEQ_4, VFUNCT_1, LOPBAN_1, FUNCT_2, SEQ_4,
      NORMSP_1, LOPBAN_3, PARTFUN1, PARTFUN2, CLVECT_1, CLOPBAN3, COMSEQ_1,
      COMSEQ_3, SEQM_3, COMSEQ_2, VFUNCT_2, NFCONT_1, CLOPBAN1, CFCONT_1,
      COMPLEX1;
 schemes SEQ_1, FUNCT_2;

begin :: Continuous Function on Normed Complex Linear Spaces

 reserve n,m for Nat;
 reserve r,s for Real;
 reserve z for Complex;
 reserve CNS,CNS1,CNS2 for ComplexNormSpace;
 reserve RNS for RealNormSpace;

definition
  let CLS be ComplexLinearSpace;
  let seq be sequence of CLS;
  func - seq -> sequence of CLS means :Def1:
  for n holds it.n = - seq.n;
existence
proof
   deffunc F(Nat) = - seq.$1;
   thus ex s being sequence of CLS st
        for n holds s.n = F(n) from FUNCT_2:sch 4;
end;
uniqueness
proof
   let s1,s2 be sequence of CLS;
   assume that
A1: ( for n holds s1.n = - seq.n ) and
A2: ( for n holds s2.n = - seq.n );
   for n holds s1.n = s2.n
   proof
     let n;
     s1.n = - seq.n by A1;
     hence thesis by A2;
   end;
   hence thesis by FUNCT_2:113;
end;
end;

theorem
for seq1,seq2 be sequence of CNS holds seq1 - seq2 = seq1 +- seq2
proof
   let seq1, seq2 be sequence of CNS;
   for n holds (seq1 - seq2).n = (seq1 +- seq2).n
   proof
     let n;
     thus (seq1 - seq2).n = seq1.n - seq2.n by CLVECT_1:def 12
                    .= seq1.n + -seq2.n by RLVECT_1:def 11
                    .= seq1.n + (-seq2).n by Def1
                    .= (seq1+-seq2).n by CLVECT_1:def 11;
   end;
   hence thesis by FUNCT_2:113;
end;

theorem Th2:
for seq be sequence of CNS holds -seq=(-1r)*seq
proof
   let seq be sequence of CNS;
   now let n;
     thus ((-1r)*seq).n =(-1r)*seq.n by CLVECT_1:def 14
                      .=-seq.n by CLVECT_1:4
                      .=(-seq).n by Def1;
   end;
   hence thesis by FUNCT_2:113;
end;

definition
  let CNS1,CNS2;
  let f be PartFunc of CNS1,CNS2;
  func ||.f.|| -> PartFunc of the carrier of CNS1,REAL means :Def2:
  dom it = dom f & for c being Point of CNS1 st c in dom it holds
    it.c = ||.f/.c.||;
existence
proof
   defpred X[set] means $1 in dom f;
   deffunc U(Element of the carrier of CNS1) = ||.f/.$1 .||;
   consider F be PartFunc of the carrier of CNS1,REAL such that
A1: for c being Element of the carrier of CNS1
     holds c in dom F iff X[c] and
A2: for c being Element of the carrier of CNS1 st c in dom F holds F.c = U(c)
      from SEQ_1:sch 3;
   reconsider F as PartFunc of the carrier of CNS1,REAL;
   take F;
   thus dom F = dom f by A1,SUBSET_1:8;
   thus thesis by A2;
end;
uniqueness
proof
   deffunc U(Element of the carrier of CNS1) = ||. f/.$1 .||;
   for f1, f2 be PartFunc of the carrier of CNS1,REAL st
    (dom f1 = dom f &
     for c being Element of the carrier of CNS1 st c in dom f1 holds
      f1.c = U(c)) &
    (dom f2 = dom f &
     for c being Element of the carrier of CNS1 st c in dom f2 holds
      f2.c = U(c)) holds f1 = f2 from SEQ_1:sch 4;
   hence thesis;
end;
end;

definition
  let CNS,RNS;
  let f be PartFunc of CNS,RNS;
  func ||.f.|| -> PartFunc of the carrier of CNS,REAL means :Def3:
  dom it = dom f & for c being Point of CNS st c in dom it holds
   it.c = ||.f/.c.||;
existence
proof
   defpred X[set] means $1 in dom f;
   deffunc U(Element of the carrier of CNS) = ||.f/.$1 .||;
   consider F be PartFunc of the carrier of CNS,REAL such that
A1: for c being Element of the carrier of CNS
     holds c in dom F iff X[c] and
A2: for c being Element of the carrier of CNS st c in dom F holds F.c = U(c)
      from SEQ_1:sch 3;
   reconsider F as PartFunc of the carrier of CNS,REAL;
   take F;
   thus dom F = dom f by A1,SUBSET_1:8;
   thus thesis by A2;
end;
uniqueness
proof
   deffunc U(Element of the carrier of CNS) = ||. f/.$1 .||;
   for f1, f2 be PartFunc of the carrier of CNS,REAL st
    (dom f1 = dom f &
     for c being Element of the carrier of CNS st c in dom f1 holds
      f1.c = U(c)) &
    (dom f2 = dom f &
     for c being Element of the carrier of CNS st c in dom f2 holds
      f2.c = U(c)) holds f1 = f2 from SEQ_1:sch 4;
   hence thesis;
end;
end;

definition
  let RNS,CNS;
  let f be PartFunc of RNS,CNS;
  func ||.f.|| -> PartFunc of the carrier of RNS,REAL means :Def4:
  dom it = dom f & for c being Point of RNS st c in dom it holds
   it.c = ||.f/.c.||;
existence
proof
   defpred X[set] means $1 in dom f;
   deffunc U(Element of the carrier of RNS) = ||.f/.$1 .||;
   consider F be PartFunc of the carrier of RNS,REAL such that
A1: for c being Element of the carrier of RNS
     holds c in dom F iff X[c] and
A2: for c being Element of the carrier of RNS st c in dom F holds F.c = U(c)
     from SEQ_1:sch 3;
   reconsider F as PartFunc of the carrier of RNS,REAL;
   take F;
   thus dom F = dom f by A1,SUBSET_1:8;
   thus thesis by A2;
end;
uniqueness
proof
   deffunc U(Element of the carrier of RNS) = ||. f/.$1 .||;
   for f1, f2 be PartFunc of the carrier of RNS,REAL st
    (dom f1 = dom f &
     for c being Element of the carrier of RNS st c in dom f1 holds
      f1.c = U(c)) &
    (dom f2 = dom f &
     for c being Element of the carrier of RNS st c in dom f2 holds
      f2.c = U(c)) holds f1 = f2 from SEQ_1:sch 4;
   hence thesis;
end;
end;

definition
  let CNS;
  let x0 be Point of CNS;
  mode Neighbourhood of x0 -> Subset of CNS means :Def5:
  ex g be Real st 0<g & {y where y is Point of CNS: ||.y-x0 .|| < g} c= it;
existence
proof
   set N= {y where y is Point of CNS : ||.y-x0 .|| < 1};
A1:N c= the carrier of CNS
   proof
     let x be set;
     assume
A2:  x in {y where y is Point of CNS : ||.y-x0 .|| < 1};
     consider y be Point of CNS such that
A3:  x=y and
      ||.y-x0 .|| < 1 by A2;
     thus x in the carrier of CNS by A3;
   end;
   take N;
   thus thesis by A1;
end;
end;

theorem Th3:
for x0 be Point of CNS
for g be Real st 0 < g holds
 {y where y is Point of CNS: ||.y-x0 .|| < g} is Neighbourhood of x0
proof
   let x0 be Point of CNS;
   let g be Real such that
A1: g >0;
   set N= {y where y is Point of CNS : ||.y-x0 .|| < g};
A2:N c= the carrier of CNS
   proof
     let x be set;
     assume
A3:  x in {y where y is Point of CNS : ||.y-x0 .|| < g};
     consider y be Point of CNS such that
A4:   x=y and
     ||.y-x0 .|| < g by A3;
     thus x in the carrier of CNS by A4;
   end;
   thus thesis by A1,A2,Def5;
end;

theorem Th4:
for x0 be Point of CNS
for N being Neighbourhood of x0 holds x0 in N
proof
   let x0 be Point of CNS;
   let N be Neighbourhood of x0;
   consider g be Real such that
A1: 0<g and
A2: {z where z is Point of CNS : ||.z-x0 .|| < g} c= N by Def5;
   ||.x0-x0 .|| = ||.0.CNS.|| by RLVECT_1:28
                .=0 by CLVECT_1:103; then
   x0 in {z where z is Point of CNS : ||.z-x0 .|| < g} by A1;
   hence thesis by A2;
end;

definition let CNS;
  let X be Subset of CNS;
  attr X is compact means :Def6:
  for s1 be sequence of CNS st rng s1 c= X
   ex s2 be sequence of CNS st
    s2 is_subsequence_of s1 & s2 is convergent & lim s2 in X;
end;

definition
  let CNS;
  let X be Subset of CNS;
  attr X is closed means
  for s1 be sequence of CNS st
   rng s1 c= X & s1 is convergent holds lim s1 in X;
end;

definition let CNS;
  let X be Subset of CNS;
  attr X is open means X` is closed;
end;

definition let CNS1,CNS2;
  let f be PartFunc of CNS1,CNS2;
  let seq be sequence of CNS1;
assume A1:rng seq c= dom f;
func f*seq -> sequence of CNS2 equals :Def9:
  (f qua Function)*seq;
coherence
proof
   dom seq = NAT by FUNCT_2:def 1; then
A2:dom ((f qua Function)*seq) = NAT by A1,RELAT_1:46;
   rng ((f qua Function)*seq) c= rng f by RELAT_1:45; then
   rng ((f qua Function)*seq) c=  the carrier of CNS2 by XBOOLE_1:1;
   hence (f qua Function)*seq is sequence of CNS2 by A2,FUNCT_2:4;
end;
end;

definition let CNS, RNS;
  let f be PartFunc of CNS,RNS;
  let seq be sequence of CNS;
  assume A1:rng seq c= dom f;
  func f*seq -> sequence of RNS equals :Def10:
  (f qua Function)*seq;
  coherence
  proof
     dom seq = NAT by FUNCT_2:def 1; then
A2:  dom ((f qua Function)*seq) = NAT by A1,RELAT_1:46;
     rng ((f qua Function)*seq) c= rng f by RELAT_1:45; then
     rng ((f qua Function)*seq) c= the carrier of RNS by XBOOLE_1:1;
     hence (f qua Function)*seq is sequence of RNS by A2,FUNCT_2:4;
  end;
end;

definition let CNS, RNS;
  let f be PartFunc of RNS,CNS;
  let seq be sequence of RNS;
assume A1:rng seq c= dom f;
func f*seq -> sequence of CNS equals :Def11:
  (f qua Function)*seq;
coherence
proof
   dom seq = NAT by FUNCT_2:def 1; then
A2:dom ((f qua Function)*seq) = NAT by A1,RELAT_1:46;
   rng ((f qua Function)*seq) c= rng f by RELAT_1:45; then
   rng ((f qua Function)*seq) c= the carrier of CNS by XBOOLE_1:1;
   hence (f qua Function)*seq is sequence of CNS by A2,FUNCT_2:4;
end;
end;

definition
  let CNS;
  let f be PartFunc of the carrier of CNS,COMPLEX;
  let seq be sequence of CNS;
assume A1:rng seq c= dom f;
func f*seq -> Complex_Sequence equals :Def12:
  (f qua Function)*seq;
coherence
proof
   dom seq = NAT by FUNCT_2:def 1; then
A2:dom ((f qua Function)*seq) = NAT by A1,RELAT_1:46;
   rng ((f qua Function)*seq) c= rng f by RELAT_1:45; then
   rng ((f qua Function)*seq) c= COMPLEX by XBOOLE_1:1;
   hence (f qua Function)*seq is Complex_Sequence by A2,FUNCT_2:4;
end;
end;

definition
  let RNS;
  let f be PartFunc of the carrier of RNS,COMPLEX;
  let seq be sequence of RNS;
assume A1:rng seq c= dom f;
func f*seq -> Complex_Sequence equals :Def13:
  (f qua Function)*seq;
coherence
proof
   dom seq = NAT by FUNCT_2:def 1; then
A2:dom ((f qua Function)*seq) = NAT by A1,RELAT_1:46;
   rng ((f qua Function)*seq) c= rng f by RELAT_1:45; then
   rng ((f qua Function)*seq) c= COMPLEX by XBOOLE_1:1;
   hence (f qua Function)*seq is Complex_Sequence by A2,FUNCT_2:4;
end;
end;

definition
  let CNS;
  let f be PartFunc of the carrier of CNS,REAL;
  let seq be sequence of CNS;
assume A1:rng seq c= dom f;
func f*seq -> Real_Sequence equals :Def14:
  (f qua Function)*seq;
coherence
proof
   dom seq = NAT by FUNCT_2:def 1; then
A2:dom ((f qua Function)*seq) = NAT by A1,RELAT_1:46;
   rng ((f qua Function)*seq) c= rng f by RELAT_1:45; then
   rng ((f qua Function)*seq) c= REAL by XBOOLE_1:1;
   hence (f qua Function)*seq is Real_Sequence by A2,FUNCT_2:4;
end;
end;

definition
  let CNS1,CNS2;
  let f be PartFunc of CNS1,CNS2;
  let x0 be Point of CNS1;
  pred f is_continuous_in x0 means :Def15:
  x0 in dom f &
  for seq be sequence of CNS1 st
   rng seq c= dom f & seq is convergent & lim seq = x0 holds
    f*seq is convergent & f/.x0 = lim (f*seq);
end;

definition
  let CNS,RNS;
  let f be PartFunc of CNS,RNS;
  let x0 be Point of CNS;
  pred f is_continuous_in x0 means :Def16:
  x0 in dom f &
  for seq be sequence of CNS st
   rng seq c= dom f & seq is convergent & lim seq = x0 holds
    f*seq is convergent & f/.x0 = lim (f*seq);
end;

definition
  let RNS;
  let CNS;
  let f be PartFunc of RNS,CNS;
  let x0 be Point of RNS;
  pred f is_continuous_in x0 means :Def17:
  x0 in dom f &
  for seq be sequence of RNS st
   rng seq c= dom f & seq is convergent & lim seq = x0 holds
    f*seq is convergent & f/.x0 = lim (f*seq);
end;

definition
  let CNS;
  let f be PartFunc of the carrier of CNS,COMPLEX;
  let x0 be Point of CNS;
  pred f is_continuous_in x0 means :Def18:
  x0 in dom f &
  for seq be sequence of CNS st
   rng seq c= dom f & seq is convergent & lim seq = x0 holds
    f*seq is convergent & f/.x0 = lim (f*seq);
end;

definition
  let CNS;
  let f be PartFunc of the carrier of CNS,REAL;
  let x0 be Point of CNS;
  pred f is_continuous_in x0 means :Def19:
  x0 in dom f &
  for seq be sequence of CNS st
   rng seq c= dom f & seq is convergent & lim seq = x0 holds
    f*seq is convergent & f/.x0 = lim (f*seq);
end;

definition
  let RNS;
  let f be PartFunc of the carrier of RNS,COMPLEX;
  let x0 be Point of RNS;
  pred f is_continuous_in x0 means :Def20:
  x0 in dom f &
  for seq be sequence of RNS st
   rng seq c= dom f & seq is convergent & lim seq = x0 holds
    f*seq is convergent & f/.x0 = lim (f*seq);
end;

theorem Th5:
for seq be sequence of CNS1, h be PartFunc of CNS1,CNS2 st
  rng seq c= dom h holds seq.n in dom h
proof
   let seq be sequence of CNS1;
   let h be PartFunc of CNS1,CNS2;
   assume
A1:rng seq c= dom h; n in NAT; then
   n in dom seq by FUNCT_2:def 1; then
   n in dom ((h qua Function)*seq) by A1,RELAT_1:46;
   hence seq.n in dom h by FUNCT_1:21;
end;

theorem Th6:
for seq be sequence of CNS, h be PartFunc of CNS,RNS st
  rng seq c= dom h holds seq.n in dom h
proof
   let seq be sequence of CNS;
   let h be PartFunc of CNS,RNS;
   assume
A1:rng seq c= dom h; n in NAT; then
   n in dom seq by FUNCT_2:def 1; then
   n in dom ((h qua Function)*seq) by A1,RELAT_1:46;
   hence seq.n in dom h by FUNCT_1:21;
end;

theorem Th7:
for seq be sequence of RNS, h be PartFunc of RNS,CNS st
  rng seq c= dom h holds seq.n in dom h
proof
   let seq be sequence of RNS;
   let h be PartFunc of RNS,CNS;
   assume
A1:rng seq c= dom h; n in NAT; then
   n in dom seq by FUNCT_2:def 1; then
   n in dom ((h qua Function)*seq) by A1,RELAT_1:46;
   hence seq.n in dom h by FUNCT_1:21;
end;

theorem Th8:
for seq be sequence of CNS, x be set holds
 x in rng seq iff ex n st x = seq.n
proof
   let  seq be sequence of CNS;
   let  x be set;
   thus x in rng seq implies ex n st x = seq.n
   proof
     assume x in rng seq; then
     consider y be set such that
A1:   y in dom seq & x = seq.y by FUNCT_1:def 5;
     reconsider m=y as Nat by A1;
     take m;
     thus thesis by A1;
   end;
   given n such that
A2: x = seq.n;
   n in NAT; then
   n in dom seq by FUNCT_2:def 1;
   hence thesis by A2,FUNCT_1:def 5;
end;

theorem Th9:
for seq,seq1 be sequence of CNS st seq1 is_subsequence_of seq holds
 rng seq1 c= rng seq
proof
   let seq be sequence of CNS;
   let seq1 be sequence of CNS;
   assume seq1 is_subsequence_of seq; then
   consider Ns be increasing Seq_of_Nat such that
A1: seq1 = seq * Ns by CLOPBAN3:def 7;
   let x be set;
   assume x in rng seq1; then
   consider n such that
A2: x = (seq *Ns).n by A1,Th8;
   x = seq.(Ns.n) by A2,LOPBAN_3:7;
   hence x in rng seq by Th8;
end;

theorem Th10:
for f be PartFunc of CNS1,CNS2, CSeq be sequence of CNS1 st
 rng CSeq c= dom f
  for n holds (f*CSeq).n=f/.(CSeq.n)
proof
   let f be PartFunc of CNS1,CNS2;
   let CSeq be sequence of CNS1 such that
A1: rng CSeq c= dom f;
   let n;
   dom CSeq = NAT by FUNCT_2:def 1; then
   CSeq.n in rng CSeq by FUNCT_1:12;
   hence f/.(CSeq.n) = (f qua Function).(CSeq.n) by A1,FINSEQ_4:def 4
              .=((f qua Function)*CSeq).n by FUNCT_2:21
              .=(f*CSeq).n by A1,Def9;
end;

theorem Th11:
for f be PartFunc of CNS,RNS, CSeq be sequence of CNS st
 rng CSeq c= dom
  f for n holds (f*CSeq).n=f/.(CSeq.n)
proof
   let f be PartFunc of CNS,RNS;
   let CSeq be sequence of CNS such that
A1: rng CSeq c= dom f;
   let n;
   dom CSeq = NAT by FUNCT_2:def 1; then
   CSeq.n in rng CSeq by FUNCT_1:12;
   hence f/.(CSeq.n) = (f qua Function).(CSeq.n) by A1,FINSEQ_4:def 4
              .=((f qua Function)*CSeq).n by FUNCT_2:21
              .=(f*CSeq).n by A1,Def10;
end;

theorem Th12:
for f be PartFunc of RNS,CNS, RSeq be sequence of RNS st
 rng RSeq c= dom f
  for n holds (f*RSeq).n=f/.(RSeq.n)
proof
   let f be PartFunc of RNS,CNS;
   let RSeq be sequence of RNS such that
A1: rng RSeq c= dom f;
   let n;
   dom RSeq = NAT by FUNCT_2:def 1; then
   RSeq.n in rng RSeq by FUNCT_1:12;
   hence f/.(RSeq.n) = (f qua Function).(RSeq.n) by A1,FINSEQ_4:def 4
              .=((f qua Function)*RSeq).n by FUNCT_2:21
              .=(f*RSeq).n by A1,Def11;
end;

theorem Th13:
for f be PartFunc of the carrier of CNS,COMPLEX
 for CSeq be sequence of CNS st rng CSeq c= dom f
  for n holds (f*CSeq).n=f/.(CSeq.n)
proof
   let f be PartFunc of the carrier of CNS,COMPLEX;
   let CSeq be sequence of CNS such that
A1:rng CSeq c= dom f;
   let n;
   dom CSeq = NAT by FUNCT_2:def 1; then
   CSeq.n in rng CSeq by FUNCT_1:12;
   hence f/.(CSeq.n) = (f qua Function).(CSeq.n) by A1,FINSEQ_4:def 4
              .=((f qua Function)*CSeq).n by FUNCT_2:21
              .=(f*CSeq).n by A1,Def12;
end;

theorem Th14:
for f be PartFunc of the carrier of CNS,REAL
 for CSeq be sequence of CNS st rng CSeq c= dom f
  for n holds (f*CSeq).n = f/.(CSeq.n)
proof
   let f be PartFunc of the carrier of CNS,REAL;
   let CSeq be sequence of CNS such that
A1:rng CSeq c= dom f;
   let n;
   dom CSeq = NAT by FUNCT_2:def 1; then
   CSeq.n in rng CSeq by FUNCT_1:12;
   hence f/.(CSeq.n) = (f qua Function).(CSeq.n) by A1,FINSEQ_4:def 4
              .=((f qua Function)*CSeq).n by FUNCT_2:21
              .=(f*CSeq).n by A1,Def14;
end;

theorem Th15:
for f be PartFunc of the carrier of RNS,COMPLEX
 for RSeq be sequence of RNS st rng RSeq c= dom f
  for n holds (f*RSeq).n = f/.(RSeq.n)
proof
   let f be PartFunc of the carrier of RNS,COMPLEX;
   let RSeq be sequence of RNS such that
A1:rng RSeq c= dom f;
   let n;
   dom RSeq = NAT by FUNCT_2:def 1; then
   RSeq.n in rng RSeq by FUNCT_1:12;
   hence f/.(RSeq.n) = (f qua Function).(RSeq.n) by A1,FINSEQ_4:def 4
              .=((f qua Function)*RSeq).n by FUNCT_2:21
              .=(f*RSeq).n by A1,Def13;
end;

theorem Th16:
for h be PartFunc of CNS1,CNS2
for CSeq be sequence of CNS1
for Ns be increasing Seq_of_Nat st
  rng CSeq c= dom h holds (h*CSeq)*Ns = h * (CSeq*Ns)
proof
   let h be PartFunc of CNS1,CNS2;
   let CSeq be sequence of CNS1;
   let  Ns be increasing Seq_of_Nat;
   assume
A1:rng CSeq c= dom h;
   (CSeq * Ns) is_subsequence_of CSeq by CLOPBAN3:def 7; then
   rng (CSeq*Ns) c= rng CSeq by Th9; then
A2:rng (CSeq*Ns) c= dom h by A1,XBOOLE_1:1;
   thus (h*CSeq)*Ns = ((h qua Function)*CSeq)*Ns by A1,Def9
    .= (h qua Function)*(CSeq*Ns) by RELAT_1:55
    .= h*(CSeq*Ns) by A2,Def9;
end;

theorem Th17:
for h be PartFunc of CNS,RNS
for Cseq be sequence of CNS
for Ns be increasing Seq_of_Nat st
  rng Cseq c= dom h holds (h*Cseq)*Ns = h * (Cseq*Ns)
proof
   let h be PartFunc of CNS,RNS;
   let Cseq be sequence of CNS;
   let  Ns be increasing Seq_of_Nat;
   assume
A1:rng Cseq c= dom h;
   (Cseq * Ns) is_subsequence_of Cseq by CLOPBAN3:def 7; then
   rng (Cseq*Ns) c= rng Cseq by Th9; then
A2:rng (Cseq*Ns) c= dom h by A1,XBOOLE_1:1;
   thus (h*Cseq)*Ns = ((h qua Function)*Cseq)*Ns by A1,Def10
    .= (h qua Function)*(Cseq*Ns) by RELAT_1:55
    .= h*(Cseq*Ns) by A2,Def10;
end;

theorem Th18:
for h be PartFunc of RNS,CNS
for Rseq be sequence of RNS
for Ns be increasing Seq_of_Nat st
  rng Rseq c= dom h holds (h*Rseq)*Ns = h * (Rseq*Ns)
proof
   let h be PartFunc of RNS,CNS;
   let Rseq be sequence of RNS;
   let Ns be increasing Seq_of_Nat;
   assume
A1:rng Rseq c= dom h;
   (Rseq * Ns) is_subsequence_of Rseq by LOPBAN_3:def 7; then
   rng (Rseq*Ns) c= rng Rseq by NFCONT_1:7; then
A2:rng (Rseq*Ns) c= dom h by A1,XBOOLE_1:1;
   thus (h*Rseq)*Ns = ((h qua Function)*Rseq)*Ns by A1,Def11
    .= (h qua Function)*(Rseq*Ns) by RELAT_1:55
    .= h*(Rseq*Ns) by A2,Def11;
end;

theorem Th19:
for h be PartFunc of the carrier of CNS,COMPLEX, Cseq be sequence of CNS,
 Ns be increasing Seq_of_Nat st
  rng Cseq c= dom h holds (h*Cseq)*Ns = h * (Cseq*Ns)
proof
   let h be PartFunc of the carrier of CNS,COMPLEX;
   let Cseq be sequence of CNS;
   let Ns be increasing Seq_of_Nat;
   assume
A1:rng Cseq c= dom h;
   (Cseq * Ns) is_subsequence_of Cseq by CLOPBAN3:def 7; then
   rng (Cseq*Ns) c= rng Cseq by Th9; then
A2:rng (Cseq*Ns) c= dom h by A1,XBOOLE_1:1;
   thus (h*Cseq)*Ns = ((h qua Function)*Cseq)*Ns by A1,Def12
    .= (h qua Function)*(Cseq*Ns) by RELAT_1:55
    .= h*(Cseq*Ns) by A2,Def12;
end;

theorem Th20:
for h be PartFunc of the carrier of CNS,REAL, Cseq be sequence of CNS,
 Ns be increasing Seq_of_Nat st
  rng Cseq c= dom h holds (h*Cseq)*Ns = h * (Cseq*Ns)
proof
   let h be PartFunc of the carrier of CNS,REAL;
   let Cseq be sequence of CNS;
   let Ns be increasing Seq_of_Nat;
   assume
A1:rng Cseq c= dom h;
   (Cseq * Ns) is_subsequence_of Cseq by CLOPBAN3:def 7; then
   rng (Cseq*Ns) c= rng Cseq by Th9; then
A2:rng (Cseq*Ns) c= dom h by A1,XBOOLE_1:1;
   thus (h*Cseq)*Ns = ((h qua Function)*Cseq)*Ns by A1,Def14
    .= (h qua Function)*(Cseq*Ns) by RELAT_1:55
    .= h*(Cseq*Ns) by A2,Def14;
end;

theorem Th21:
for h be PartFunc of the carrier of RNS,COMPLEX, Rseq be sequence of RNS,
 Ns be increasing Seq_of_Nat st
  rng Rseq c= dom h holds (h*Rseq)*Ns = h * (Rseq*Ns)
proof
   let h be PartFunc of the carrier of RNS,COMPLEX;
   let Rseq be sequence of RNS;
   let Ns be increasing Seq_of_Nat;
   assume
A1:rng Rseq c= dom h;
   (Rseq * Ns) is_subsequence_of Rseq by LOPBAN_3:def 7; then
   rng (Rseq*Ns) c= rng Rseq by NFCONT_1:7; then
A2:rng (Rseq*Ns) c= dom h by A1,XBOOLE_1:1;
   thus (h*Rseq)*Ns = ((h qua Function)*Rseq)*Ns by A1,Def13
    .= (h qua Function)*(Rseq*Ns) by RELAT_1:55
    .= h*(Rseq*Ns) by A2,Def13;
end;

theorem Th22:
for h be PartFunc of CNS1,CNS2
for Cseq1,Cseq2  be sequence of CNS1 st
 rng Cseq1 c= dom h & Cseq2 is_subsequence_of Cseq1 holds
  h*Cseq2 is_subsequence_of h*Cseq1
proof
   let h be PartFunc of CNS1,CNS2;
   let Cseq1,Cseq2 be sequence of CNS1;
   assume
A1:rng Cseq1 c= dom h & Cseq2 is_subsequence_of Cseq1; then
   consider Ns be increasing Seq_of_Nat such that
A2: Cseq2=Cseq1*Ns by CLOPBAN3:def 7;
   take Ns;
   thus thesis by A1,A2,Th16;
end;

theorem Th23:
for h be PartFunc of CNS,RNS
for Cseq1,Cseq2  be sequence of CNS st
 rng Cseq1 c= dom h & Cseq2 is_subsequence_of Cseq1 holds
  h*Cseq2 is_subsequence_of h*Cseq1
proof
   let h be PartFunc of CNS,RNS;
   let Cseq1,Cseq2 be sequence of CNS;
   assume
A1:rng Cseq1 c= dom h & Cseq2 is_subsequence_of Cseq1; then
   consider Ns be increasing Seq_of_Nat such that
A2: Cseq2=Cseq1*Ns by CLOPBAN3:def 7;
   take Ns;
   thus thesis by A1,A2,Th17;
end;

theorem Th24:
for h be PartFunc of RNS,CNS
for Rseq1,Rseq2 be sequence of RNS st
 rng Rseq1 c= dom h & Rseq2 is_subsequence_of Rseq1 holds
  h*Rseq2 is_subsequence_of h*Rseq1
proof
   let h be PartFunc of RNS,CNS;
   let Rseq1,Rseq2 be sequence of RNS;
   assume
A1:rng Rseq1 c= dom h & Rseq2 is_subsequence_of Rseq1; then
   consider Ns be increasing Seq_of_Nat such that
A2: Rseq2=Rseq1*Ns by LOPBAN_3:def 7;
   take Ns;
   thus thesis by A1,A2,Th18;
end;

theorem Th25:
for seq be Complex_Sequence, n be Nat, Nseq be increasing Seq_of_Nat holds
(seq*Nseq).n=seq.(Nseq.n)
proof
   let seq be Complex_Sequence;
   let n;
   let Nseq be increasing Seq_of_Nat;
A1:dom Nseq=NAT by FUNCT_2:def 1;
   rng Nseq c= NAT by SEQM_3:def 8; then
   rng Nseq c= dom seq by FUNCT_2:def 1; then
   dom (seq*Nseq)=NAT by A1,RELAT_1:46;
   hence (seq*Nseq).n=seq.(Nseq.n) by FUNCT_1:22;
end;

theorem Th26:
for h be PartFunc of the carrier of CNS,COMPLEX
for Cseq1,Cseq2 be sequence of CNS st
 rng Cseq1 c= dom h & Cseq2 is_subsequence_of Cseq1 holds
  h*Cseq2 is_subsequence_of h*Cseq1
proof
   let h be PartFunc of the carrier of CNS,COMPLEX;
   let Cseq1,Cseq2 be sequence of CNS;
   assume
A1:rng Cseq1 c= dom h & Cseq2 is_subsequence_of Cseq1; then
   consider Ns be increasing Seq_of_Nat such that
A2: Cseq2=Cseq1*Ns by CLOPBAN3:def 7;
   take Ns;
A3:(h*Cseq1)*Ns = h*Cseq2 by A1,A2,Th19;
   h*Cseq2 = (h*Cseq1)(#)Ns
   proof
     for n be Nat holds
      ((h*Cseq1)(#)Ns).n = ((h*Cseq1)*Ns).n
     proof
       let n be Nat;
       ((h*Cseq1)(#)Ns).n = (h*Cseq1).(Ns.n) by COMSEQ_3:def 5;
       hence thesis by Th25;
     end;
     hence thesis by A3,COMSEQ_1:6;
   end;
   hence thesis;
end;

theorem Th27:
for h be PartFunc of the carrier of CNS,REAL
for Cseq1,Cseq2 be sequence of CNS st
 rng Cseq1 c= dom h & Cseq2 is_subsequence_of Cseq1 holds
  h*Cseq2 is_subsequence_of h*Cseq1
proof
   let h be PartFunc of the carrier of CNS,REAL;
   let Cseq1,Cseq2 be sequence of CNS;
   assume
A1:rng Cseq1 c= dom h & Cseq2 is_subsequence_of Cseq1; then
   consider Ns be increasing Seq_of_Nat such that
A2: Cseq2=Cseq1*Ns by CLOPBAN3:def 7;
   take Ns;
   thus thesis by A1,A2,Th20;
end;

theorem Th28:
for h be PartFunc of the carrier of RNS,COMPLEX
for Rseq1,Rseq2 be sequence of RNS st
 rng Rseq1 c= dom h & Rseq2 is_subsequence_of Rseq1 holds
  h*Rseq2 is_subsequence_of h*Rseq1
proof
   let h be PartFunc of the carrier of RNS,COMPLEX;
   let Rseq1,Rseq2 be sequence of RNS;
   assume
A1:rng Rseq1 c= dom h & Rseq2 is_subsequence_of Rseq1; then
   consider Ns  be increasing Seq_of_Nat such that
A2: Rseq2=Rseq1*Ns by LOPBAN_3:def 7;
   take Ns;
A3:(h*Rseq1)*Ns = h*Rseq2 by A1,A2,Th21;
   h*Rseq2 = (h*Rseq1)(#)Ns
   proof
     for n be Nat holds
      ((h*Rseq1)(#)Ns).n = ((h*Rseq1)*Ns).n
     proof
       let n be Nat;
       ((h*Rseq1)(#)Ns).n = (h*Rseq1).(Ns.n) by COMSEQ_3:def 5;
       hence thesis by Th25;
     end;
     hence thesis by A3,COMSEQ_1:6;
   end;
   hence thesis;
end;

theorem Th29:
for f be PartFunc of CNS1,CNS2, x0 be Point of CNS1 holds
f is_continuous_in x0 iff
 x0 in dom f &
 for r st 0<r ex s st 0<s & for x1 be Point of CNS1 st
  x1 in dom f & ||. x1- x0 .|| <s holds ||. f/.x1-f/.x0 .||<r
proof
   let f be PartFunc of CNS1,CNS2;
   let x0 be Point of CNS1;
   thus f is_continuous_in x0 implies x0 in dom f &
    for r st 0<r ex s st 0<s &
    for x1 be Point of CNS1 st x1 in dom f & ||. x1- x0 .|| <s holds
     ||. f/.x1- f/.x0 .|| <r
   proof
     assume
A1:  f is_continuous_in x0;
     hence x0 in dom f by Def15;
     given r such that
A2:   0<r & for s holds not 0<s or ex x1 be Point of CNS1 st x1 in dom f &
       ||. x1- x0 .|| <s & not ||. f/.x1- f/.x0 .|| <r;
     defpred P[Nat ,Point of CNS1] means
      $2 in dom f & ||. $2-x0 .|| < 1/($1+1) & not  ||.f/.$2-f/.x0 .||<r;
A3:  for n ex p be Point of CNS1  st P[n,p]
     proof
       let n;
       0<n+1 by NAT_1:19; then
       0<(n+1)" by REAL_1:72; then
       0<1/(n+1) by XCMPLX_1:217; then
       consider p be Point of CNS1 such that
A4:     p in dom f &
        ||.p-x0 .|| < 1/(n+1) & not  ||.f/.p-f/.x0 .|| <r by A2;
       take p;
       thus thesis by A4;
     end;
     consider s1 being Function of NAT,the carrier of CNS1 such that
A5:   for n being Element of NAT holds P[n,s1.n]
      from FUNCT_2:sch 3(A3);
     reconsider s1 as sequence of CNS1;
A6:  rng s1 c= dom f
     proof
       let v be set such that
A7:    v in rng s1;
       dom s1= NAT by FUNCT_2:def 1; then
       ex n be set st n in NAT & v=s1.n by A7,FUNCT_1:def 5;
       hence thesis by A5;
     end;
A8:  now let n;
       not ||.f/.(s1.n)-f/.x0 .||<r by A5;
       hence not ||.(f*s1).n-f/.x0 .||<r by A6,Th10;
     end;
A9:  now let s be Real;
       assume 0<s; then
A10:    0<s" by REAL_1:72;
       consider n such that
A11:    s"<n by SEQ_4:10;
       take k=n;
       let m such that
A12:    k<=m;
       s"+0 <n+1 by A11,REAL_1:67; then
       1/(n+1)<1/s" by A10,SEQ_2:10; then
A13:   1/(n+1)<s by XCMPLX_1:218;
A14:   0<n+1 by NAT_1:19; k+1<=m+1 by A12,AXIOMS:24; then
       1/(m+1)<=1/(k+1) by A14,SEQ_4:1; then
A15:   1/(m+1)<s by A13,AXIOMS:22;
       ||. s1.m-x0 .|| <1/(m+1) by A5;
       hence ||. s1.m-x0 .|| <s by A15,AXIOMS:22;
     end; then
A16: s1 is convergent by CLVECT_1:def 15; then
     lim s1=x0 by A9,CLVECT_1:def 17; then
     f*s1 is convergent & f/.x0=lim(f*s1) by A1,A6,A16,Def15; then
     consider n such that
A17:  for m st n<=m holds ||.(f*s1).m-f/.x0.||<r by A2,CLVECT_1:def 17;
     ||.(f*s1).n-f/.x0.||<r by A17;
     hence contradiction by A8;
   end;
   assume
A18:x0 in dom f & for r st 0<r ex s st 0<s &
    for x1 be Point of CNS1 st x1 in dom f & ||. x1- x0 .|| <s holds
     ||. f/.x1- f/.x0 .|| <r;
   now let s1 be sequence of CNS1 such that
A19: rng s1 c= dom f & s1 is convergent & lim s1 = x0;
A20: now let p be Real;
       assume 0<p; then
       consider s such that
A21:    0<s & for x1 be Point of CNS1 st x1 in dom f & ||. x1- x0 .|| <s holds
         ||. f/.x1- f/.x0 .|| <p by A18;
       consider n such that
A22:    for m st n<=m holds ||. s1.m-x0 .|| <s by A19,A21,CLVECT_1:def 17;
       take k=n;
       let m such that
A23:   k<=m;
       dom s1 = NAT by FUNCT_2:def 1; then
A24:   s1.m in rng s1 by FUNCT_1:12;
       ||. s1.m-x0 .|| <s by A22,A23; then
       ||.f/.(s1.m)-f/.x0.||<p by A19,A21,A24;
       hence ||.(f*s1).m - f/.x0.||<p by A19,Th10;
     end; then
     f*s1 is convergent by CLVECT_1:def 15;
     hence f*s1 is convergent & f/.x0 = lim (f*s1) by A20,CLVECT_1:def 17;
   end;
   hence thesis by A18,Def15;
end;

theorem Th30:
for f be PartFunc of CNS,RNS, x0 be Point of CNS holds
 f is_continuous_in x0 iff
 x0 in dom f &
 for r st 0<r ex s st 0<s & for x1 be Point of CNS st
  x1 in dom f & ||. x1- x0 .|| <s holds ||. f/.x1-f/.x0 .||<r
proof
   let f be PartFunc of CNS,RNS;
   let x0 be Point of CNS;
   thus f is_continuous_in x0 implies x0 in dom f &
    for r st 0<r ex s st 0<s &
    for x1 be Point of CNS st x1 in dom f & ||. x1- x0 .|| <s holds
     ||. f/.x1- f/.x0 .|| <r
   proof
     assume
A1:  f is_continuous_in x0;
     hence x0 in dom f by Def16;
     given r such that
A2:   0<r & for s holds not 0<s or ex x1 be Point of CNS st x1 in dom f &
       ||. x1- x0 .|| <s & not ||. f/.x1- f/.x0 .|| <r;
     defpred P[Nat ,Point of CNS] means
      $2 in dom f & ||. $2-x0 .|| < 1/($1+1) & not  ||.f/.$2-f/.x0 .||<r;
A3:  for n ex p be Point of CNS  st P[n,p]
     proof
       let n;
       0<n+1 by NAT_1:19; then
       0<(n+1)" by REAL_1:72; then
       0<1/(n+1) by XCMPLX_1:217; then
       consider p be Point of CNS such that
A4:     p in dom f &
        ||.p-x0 .|| < 1/(n+1) & not  ||.f/.p-f/.x0 .|| <r by A2;
       take p;
       thus thesis by A4;
     end;
     consider s1 being Function of NAT,the carrier of CNS such that
A5:   for n being Element of NAT holds P[n,s1.n]
      from FUNCT_2:sch 3(A3);
     reconsider s1 as sequence of CNS;
A6:  rng s1 c= dom f
     proof
       let v be set such that
A7:    v in rng s1;
       dom s1= NAT by FUNCT_2:def 1; then
       ex n be set st n in NAT & v=s1.n by A7,FUNCT_1:def 5;
       hence thesis by A5;
     end;
A8:  now let n;
       not ||.f/.(s1.n)-f/.x0 .||<r by A5;
       hence not ||.(f*s1).n-f/.x0 .||<r by A6,Th11;
     end;
A9:  now let s be Real;
       assume 0<s; then
A10:    0<s" by REAL_1:72;
       consider n such that
A11:    s"<n by SEQ_4:10;
       take k=n;
       let m such that
A12:    k<=m;
       s"+0 <n+1 by A11,REAL_1:67; then
       1/(n+1)<1/s" by A10,SEQ_2:10; then
A13:   1/(n+1)<s by XCMPLX_1:218;
A14:   0<n+1 by NAT_1:19; k+1<=m+1 by A12,AXIOMS:24; then
       1/(m+1)<=1/(k+1) by A14,SEQ_4:1; then
A15:   1/(m+1)<s by A13,AXIOMS:22;
       ||. s1.m-x0 .|| <1/(m+1) by A5;
       hence ||. s1.m-x0 .|| <s by A15,AXIOMS:22;
     end; then
A16: s1 is convergent by CLVECT_1:def 15; then
     lim s1=x0 by A9,CLVECT_1:def 17; then
     f*s1 is convergent & f/.x0=lim(f*s1) by A1,A6,A16,Def16; then
     consider n such that
A17:  for m st n<=m holds ||.(f*s1).m-f/.x0.||<r by A2,NORMSP_1:def 11;
     ||.(f*s1).n-f/.x0.||<r by A17;
     hence contradiction by A8;
   end;
   assume
A18:x0 in dom f & for r st 0<r ex s st 0<s &
    for x1 be Point of CNS st x1 in dom f & ||. x1- x0 .|| <s holds
     ||. f/.x1- f/.x0 .|| <r;
   now let s1 be sequence of CNS such that
A19: rng s1 c= dom f & s1 is convergent & lim s1 = x0;
A20: now let p be Real;
       assume 0<p; then
       consider s such that
A21:    0<s & for x1 be Point of CNS st x1 in dom f & ||. x1- x0 .|| <s holds
         ||. f/.x1- f/.x0 .|| <p by A18;
       consider n such that
A22:    for m st n<=m holds ||. s1.m-x0 .|| <s by A19,A21,CLVECT_1:def 17;
       take k=n;
       let m such that
A23:   k<=m;
       dom s1 = NAT by FUNCT_2:def 1; then
A24:   s1.m in rng s1 by FUNCT_1:12;
       ||. s1.m-x0 .|| <s by A22,A23; then
       ||.f/.(s1.m)-f/.x0.||<p by A19,A21,A24;
       hence ||.(f*s1).m - f/.x0.||<p by A19,Th11;
     end; then
     f*s1 is convergent by NORMSP_1:def 9;
     hence f*s1 is convergent & f/.x0 = lim (f*s1) by A20,NORMSP_1:def 11;
   end;
   hence thesis by A18,Def16;
end;

theorem Th31:
for f be PartFunc of RNS,CNS, x0 be Point of RNS holds
f is_continuous_in x0 iff
 x0 in dom f &
 for r st 0<r ex s st 0<s & for x1 be Point of RNS st
  x1 in dom f & ||. x1- x0 .|| < s holds ||. f/.x1-f/.x0 .||<r
proof
   let f be PartFunc of RNS,CNS;
   let x0 be Point of RNS;
   thus f is_continuous_in x0 implies x0 in dom f &
    for r st 0<r ex s st 0<s &
    for x1 be Point of RNS st x1 in dom f & ||. x1- x0 .|| <s holds
     ||. f/.x1- f/.x0 .|| <r
   proof
     assume
A1:  f is_continuous_in x0;
     hence x0 in dom f by Def17;
     given r such that
A2:   0<r & for s holds not 0<s or ex x1 be Point of RNS st x1 in dom f &
       ||. x1- x0 .|| <s & not ||. f/.x1- f/.x0 .|| <r;
     defpred P[Nat ,Point of RNS] means
      $2 in dom f & ||. $2-x0 .|| < 1/($1+1) & not  ||.f/.$2-f/.x0 .||<r;
A3:  for n ex p be Point of RNS  st P[n,p]
     proof
       let n;
       0<n+1 by NAT_1:19; then
       0<(n+1)" by REAL_1:72; then
       0<1/(n+1) by XCMPLX_1:217; then
       consider p be Point of RNS such that
A4:     p in dom f &
        ||.p-x0 .|| < 1/(n+1) & not  ||.f/.p-f/.x0 .|| <r by A2;
       take p;
       thus thesis by A4;
     end;
     consider s1 being Function of NAT,the carrier of RNS such that
A5:   for n being Element of NAT holds P[n,s1.n]
      from FUNCT_2:sch 3(A3);
     reconsider s1 as sequence of RNS;
A6:  rng s1 c= dom f
     proof
       let v be set such that
A7:    v in rng s1;
       dom s1= NAT by FUNCT_2:def 1; then
       ex n be set st n in NAT & v=s1.n by A7,FUNCT_1:def 5;
       hence thesis by A5;
     end;
A8:  now let n;
       not ||.f/.(s1.n)-f/.x0 .||<r by A5;
       hence not ||.(f*s1).n-f/.x0 .||<r by A6,Th12;
     end;
A9:  now let s be Real;
       assume 0<s; then
A10:    0<s" by REAL_1:72;
       consider n such that
A11:    s"<n by SEQ_4:10;
       take k=n;
       let m such that
A12:    k<=m;
       s"+0 <n+1 by A11,REAL_1:67; then
       1/(n+1)<1/s" by A10,SEQ_2:10; then
A13:   1/(n+1)<s by XCMPLX_1:218;
A14:   0<n+1 by NAT_1:19;
       k+1<=m+1 by A12,AXIOMS:24; then
       1/(m+1)<=1/(k+1) by A14,SEQ_4:1; then
A15:   1/(m+1)<s by A13,AXIOMS:22;
       ||. s1.m-x0 .|| <1/(m+1) by A5;
       hence ||. s1.m-x0 .|| <s by A15,AXIOMS:22;
     end; then
A16: s1 is convergent by NORMSP_1:def 9; then
     lim s1=x0 by A9,NORMSP_1:def 11; then
     f*s1 is convergent & f/.x0=lim(f*s1) by A1,A6,A16,Def17; then
     consider n such that
A17:  for m st n<=m holds ||.(f*s1).m-f/.x0.||<r by A2,CLVECT_1:def 17;
     ||.(f*s1).n-f/.x0.||<r by A17;
     hence contradiction by A8;
   end;
   assume
A18:x0 in dom f & for r st 0<r ex s st 0<s &
    for x1 be Point of RNS st x1 in dom f & ||. x1- x0 .|| <s holds
     ||. f/.x1- f/.x0 .|| <r;
   now let s1 be sequence of RNS such that
A19: rng s1 c= dom f & s1 is convergent & lim s1 = x0;
A20: now let p be Real;
       assume 0<p; then
       consider s such that
A21:    0<s & for x1 be Point of RNS st x1 in dom f & ||. x1- x0 .|| <s holds
         ||. f/.x1- f/.x0 .|| <p by A18;
       consider n such that
A22:    for m st n<=m holds ||. s1.m-x0 .|| <s by A19,A21,NORMSP_1:def 11;
       take k=n;
       let m such that
A23:   k<=m;
       dom s1 = NAT by FUNCT_2:def 1; then
A24:   s1.m in rng s1 by FUNCT_1:12;
       ||. s1.m-x0 .|| <s by A22,A23; then
       ||.f/.(s1.m)-f/.x0.||<p by A19,A21,A24;
       hence ||.(f*s1).m - f/.x0.||<p by A19,Th12;
     end; then
     f*s1 is convergent by CLVECT_1:def 15;
     hence f*s1 is convergent & f/.x0 = lim (f*s1) by A20,CLVECT_1:def 17;
   end;
   hence thesis by A18,Def17;
end;

theorem Th32:
for f be PartFunc of the carrier of CNS,REAL, x0 be Point of CNS holds
 (f is_continuous_in x0 iff x0 in dom f &
  for r st 0<r ex s st 0<s &
  for x1 be Point of CNS st x1 in dom f & ||. x1- x0 .|| <s holds
   abs( f/.x1-f/.x0 )<r )
proof
   let f be PartFunc of  the carrier of CNS,REAL;
   let x0 be Point of CNS;
   thus f is_continuous_in x0 implies x0 in dom f &
    for r st 0<r ex s st 0<s &
    for x1 be Point of CNS st x1 in dom f & ||. x1- x0 .|| <s holds
     abs( f/.x1- f/.x0 ) <r
   proof
     assume
A1:  f is_continuous_in x0;
     hence x0 in dom f by Def19;
     given r such that
A2:   0<r & for s holds not 0<s or ex x1 be Point of CNS st x1 in dom f &
      ||. x1- x0 .|| <s & not abs( f/.x1- f/.x0 ) <r;
     defpred P[Nat ,Point of CNS] means
      $2 in dom f & ||. $2-x0 .|| < 1/($1+1) & not  abs(f/.$2-f/.x0 )<r;
A3:  for n ex p be Point of CNS  st P[n,p]
     proof
       let n;
       0<n+1 by NAT_1:19; then
       0<(n+1)" by REAL_1:72; then
       0<1/(n+1) by XCMPLX_1:217; then
       consider p be Point of CNS such that
A4:     p in dom f &
        ||.p-x0 .|| < 1/(n+1) & not  abs(f/.p-f/.x0 ) <r by A2;
       take p;
       thus thesis by A4;
     end;
     consider s1 being Function of NAT,the carrier of CNS such that
A5:   for n being Element of NAT holds P[n,s1.n]
      from FUNCT_2:sch 3(A3);
     reconsider s1 as sequence of CNS;
A6:  rng s1 c= dom f
     proof
       let v be set such that
A7:    v in rng s1;
       dom s1= NAT by FUNCT_2:def 1; then
       ex n be set st n in NAT & v=s1.n by A7,FUNCT_1:def 5;
       hence thesis by A5;
     end;
A8:  now let n;
       not abs(f/.(s1.n)-f/.x0 )<r by A5;
       hence not abs( (f*s1).n-f/.x0 )<r by A6,Th14;
     end;
A9:  now let s be Real;
       assume 0<s; then
A10:    0<s" by REAL_1:72;
       consider n such that
A11:   s"<n by SEQ_4:10;
       take k=n;
       let m such that
A12:   k<=m;
       s"+0 <n+1 by A11,REAL_1:67; then
       1/(n+1)<1/s" by A10,SEQ_2:10; then
A13:   1/(n+1)<s by XCMPLX_1:218;
A14:   0<n+1 by NAT_1:19;
       k+1<=m+1 by A12,AXIOMS:24; then
       1/(m+1)<=1/(k+1) by A14,SEQ_4:1; then
A15:   1/(m+1)<s by A13,AXIOMS:22;
       ||. s1.m-x0 .|| <1/(m+1) by A5;
       hence ||. s1.m-x0 .|| <s by A15,AXIOMS:22;
     end; then
A16: s1 is convergent by CLVECT_1:def 15; then
     lim s1=x0 by A9,CLVECT_1:def 17; then
     f*s1 is convergent & f/.x0=lim(f*s1) by A1,A6,A16,Def19; then
     consider n such that
A17:  for m st n<=m holds abs( (f*s1).m-f/.x0)<r by A2,SEQ_2:def 7;
     abs((f*s1).n-f/.x0) <r by A17;
     hence contradiction by A8;
   end;
   assume
A18:x0 in dom f &
    for r st 0<r ex s st 0<s & for x1 be Point of CNS st x1 in dom f &
     ||. x1- x0 .|| <s holds abs( f/.x1- f/.x0 ) <r;
   now let s1 be sequence of CNS such that
A19: rng s1 c= dom f & s1 is convergent & lim s1 = x0;
A20: now let p be real number;
       reconsider pp=p as Real by XREAL_0:def 1;
       assume 0<p; then
       consider s such that
A21:    0<s & for x1 be Point of CNS st x1 in dom f & ||. x1- x0 .|| <s holds
        abs( f/.x1- f/.x0 ) <pp by A18;
       consider n such that
A22:   for m st n<=m holds ||. s1.m-x0 .|| <s by A19,A21,CLVECT_1:def 17;
       take k=n;
       let m such that
A23:   k<=m;
       dom s1 = NAT by FUNCT_2:def 1; then
A24:   s1.m in rng s1 by FUNCT_1:12;
       ||. s1.m-x0 .|| <s by A22,A23; then
       abs(f/.(s1.m)-f/.x0) <p by A19,A21,A24;
       hence abs( (f*s1).m - f/.x0) <p by A19,Th14;
     end; then
     f*s1 is convergent by SEQ_2:def 6;
     hence f*s1 is convergent & f/.x0 = lim (f*s1) by A20,SEQ_2:def 7;
   end;
   hence thesis by A18,Def19;
end;

theorem Th33:
for f be PartFunc of the carrier of CNS,COMPLEX, x0 be Point of CNS holds
 (f is_continuous_in x0 iff x0 in dom f &
  for r st 0 < r ex s st 0 < s &
    for x1 be Point of CNS st x1 in dom f & ||. x1- x0 .|| <s holds
     |. f/.x1-f/.x0 .|<r )
proof
   let f be PartFunc of the carrier of CNS,COMPLEX;
   let x0 be Point of CNS;
   thus f is_continuous_in x0 implies x0 in dom f &
    for r st 0<r ex s st 0<s &
    for x1 be Point of CNS st x1 in dom f & ||. x1- x0 .|| <s holds
     |. f/.x1- f/.x0 .| <r
   proof
     assume
A1:  f is_continuous_in x0;
     hence x0 in dom f by Def18;
     given r such that
A2:   0<r & for s holds not 0<s or ex x1 be Point of CNS st x1 in dom f &
      ||. x1- x0 .|| <s & not |. f/.x1- f/.x0 .| <r;
     defpred P[Nat ,Point of CNS] means
      $2 in dom f & ||. $2-x0 .|| < 1/($1+1) & not  |.(f/.$2-f/.x0 ).|<r;
A3:  for n ex p be Point of CNS  st P[n,p]
     proof
       let n;
       0<n+1 by NAT_1:19; then
       0<(n+1)" by REAL_1:72; then
       0<1/(n+1) by XCMPLX_1:217; then
       consider p be Point of CNS such that
A4:     p in dom f &
        ||.p-x0 .|| < 1/(n+1) & not  |. f/.p-f/.x0 .| <r by A2;
       take p;
       thus thesis by A4;
     end;
     consider s1 being Function of NAT,the carrier of CNS such that
A5:   for n being Element of NAT holds P[n,s1.n]
      from FUNCT_2:sch 3(A3);
     reconsider s1 as sequence of CNS;
A6:  rng s1 c= dom f
     proof
       let v be set such that
A7:    v in rng s1;
       dom s1= NAT by FUNCT_2:def 1; then
       ex n be set st n in NAT & v=s1.n by A7,FUNCT_1:def 5;
       hence thesis by A5;
     end;
A8:  now let n;
       not |. f/.(s1.n)-f/.x0 .|<r by A5;
       hence not |. (f*s1).n-f/.x0 .|<r by A6,Th13;
     end;
A9:  now let s be Real;
       assume 0<s; then
A10:    0<s" by REAL_1:72;
       consider n such that
A11:   s"<n by SEQ_4:10;
       take k=n;
       let m such that
A12:   k<=m;
       s"+0 <n+1 by A11,REAL_1:67; then
       1/(n+1)<1/s" by A10,SEQ_2:10; then
A13:   1/(n+1)<s by XCMPLX_1:218;
A14:   0<n+1 by NAT_1:19;
       k+1<=m+1 by A12,AXIOMS:24; then
       1/(m+1)<=1/(k+1) by A14,SEQ_4:1; then
A15:   1/(m+1)<s by A13,AXIOMS:22;
       ||. s1.m-x0 .|| <1/(m+1) by A5;
       hence ||. s1.m-x0 .|| <s by A15,AXIOMS:22;
     end; then
A16: s1 is convergent by CLVECT_1:def 15; then
     lim s1=x0 by A9,CLVECT_1:def 17; then
     f*s1 is convergent & f/.x0=lim(f*s1) by A1,A6,A16,Def18; then
     consider n such that
A17:  for m st n<=m holds |. (f*s1).m-f/.x0 .|<r by A2,COMSEQ_2:def 5;
     |. (f*s1).n-f/.x0 .| <r by A17;
     hence contradiction by A8;
   end;
   assume
A18:x0 in dom f &
    for r st 0<r ex s st 0<s & for x1 be Point of CNS st x1 in dom f &
     ||. x1- x0 .|| <s holds |. f/.x1- f/.x0 .| <r;
   now let s1 be sequence of CNS such that
A19: rng s1 c= dom f & s1 is convergent & lim s1 = x0;
A20: now let p be Real;
       assume 0<p; then
       consider s such that
A21:    0<s & for x1 be Point of CNS st x1 in dom f & ||. x1- x0 .|| <s holds
        |. f/.x1- f/.x0 .| <p by A18;
       consider n such that
A22:   for m st n<=m holds ||. s1.m-x0 .|| <s by A19,A21,CLVECT_1:def 17;
       take k=n;
       let m such that
A23:   k<=m;
       dom s1 = NAT by FUNCT_2:def 1; then
A24:   s1.m in rng s1 by FUNCT_1:12;
       ||. s1.m-x0 .|| <s by A22,A23; then
       |. f/.(s1.m)-f/.x0 .| <p by A19,A21,A24;
       hence |. (f*s1).m - f/.x0 .| <p by A19,Th13;
     end; then
     f*s1 is convergent by COMSEQ_2:def 4;
     hence f*s1 is convergent & f/.x0 = lim (f*s1) by A20,COMSEQ_2:def 5;
   end;
   hence thesis by A18,Def18;
end;

theorem Th34:
for f be PartFunc of the carrier of RNS,COMPLEX, x0 be Point of RNS holds
 (f is_continuous_in x0 iff x0 in dom f &
  for r st 0<r ex s st 0<s &
  for x1 be Point of RNS st x1 in dom f & ||. x1- x0 .|| <s holds
   |. f/.x1-f/.x0 .|<r )
proof
   let f be PartFunc of the carrier of RNS,COMPLEX;
   let x0 be Point of RNS;
   thus f is_continuous_in x0 implies x0 in dom f &
    for r st 0<r ex s st 0<s &
    for x1 be Point of RNS st x1 in dom f & ||. x1- x0 .|| <s holds
     |. f/.x1- f/.x0 .| <r
   proof
     assume
A1:  f is_continuous_in x0;
     hence x0 in dom f by Def20;
     given r such that
A2:   0<r & for s holds not 0<s or ex x1 be Point of RNS st x1 in dom f &
      ||. x1- x0 .|| <s & not |. f/.x1- f/.x0 .| <r;
     defpred P[Nat ,Point of RNS] means
      $2 in dom f & ||. $2-x0 .|| < 1/($1+1) & not  |.(f/.$2-f/.x0 ).|<r;
A3:  for n ex p be Point of RNS st P[n,p]
     proof
       let n;
       0<n+1 by NAT_1:19; then
       0<(n+1)" by REAL_1:72; then
       0<1/(n+1) by XCMPLX_1:217; then
       consider p be Point of RNS such that
A4:     p in dom f &
        ||.p-x0 .|| < 1/(n+1) & not  |. f/.p-f/.x0 .| <r by A2;
       take p;
       thus thesis by A4;
     end;
     consider s1 being Function of NAT,the carrier of RNS such that
A5:   for n being Element of NAT holds P[n,s1.n]
      from FUNCT_2:sch 3(A3);
     reconsider s1 as sequence of RNS;
A6:  rng s1 c= dom f
     proof
       let v be set such that
A7:    v in rng s1;
       dom s1= NAT by FUNCT_2:def 1; then
       ex n be set st n in NAT & v=s1.n by A7,FUNCT_1:def 5;
       hence thesis by A5;
     end;
A8:  now let n;
       not |. f/.(s1.n)-f/.x0 .|<r by A5;
       hence not |. (f*s1).n-f/.x0 .|<r by A6,Th15;
     end;
A9:  now let s be Real;
       assume 0<s; then
A10:    0<s" by REAL_1:72;
       consider n such that
A11:   s"<n by SEQ_4:10;
       take k=n;
       let m such that
A12:   k<=m;
       s"+0 <n+1 by A11,REAL_1:67; then
       1/(n+1)<1/s" by A10,SEQ_2:10; then
A13:   1/(n+1)<s by XCMPLX_1:218;
A14:   0<n+1 by NAT_1:19;
       k+1<=m+1 by A12,AXIOMS:24; then
       1/(m+1)<=1/(k+1) by A14,SEQ_4:1; then
A15:   1/(m+1)<s by A13,AXIOMS:22;
       ||. s1.m-x0 .|| <1/(m+1) by A5;
       hence ||. s1.m-x0 .|| <s by A15,AXIOMS:22;
     end; then
A16: s1 is convergent by NORMSP_1:def 9; then
     lim s1=x0 by A9,NORMSP_1:def 11; then
     f*s1 is convergent & f/.x0=lim(f*s1) by A1,A6,A16,Def20; then
     consider n such that
A17:  for m st n<=m holds |. (f*s1).m-f/.x0 .|<r by A2,COMSEQ_2:def 5;
     |. (f*s1).n-f/.x0 .| <r by A17;
     hence contradiction by A8;
   end;
   assume
A18:x0 in dom f &
    for r st 0<r ex s st 0<s & for x1 be Point of RNS st x1 in dom f &
     ||. x1- x0 .|| <s holds |. f/.x1- f/.x0 .| <r;
   now let s1 be sequence of RNS such that
A19: rng s1 c= dom f & s1 is convergent & lim s1 = x0;
A20: now let p be Real;
       assume 0<p; then
       consider s such that
A21:    0<s & for x1 be Point of RNS st x1 in dom f & ||. x1- x0 .|| <s holds
        |. f/.x1- f/.x0 .| <p by A18;
       consider n such that
A22:   for m st n<=m holds ||. s1.m-x0 .|| <s by A19,A21,NORMSP_1:def 11;
       take k=n;
       let m such that
A23:   k<=m;
       dom s1 = NAT by FUNCT_2:def 1; then
A24:   s1.m in rng s1 by FUNCT_1:12;
       ||. s1.m-x0 .|| <s by A22,A23; then
       |. f/.(s1.m)-f/.x0 .| <p by A19,A21,A24;
       hence |. (f*s1).m - f/.x0 .| <p by A19,Th15;
     end; then
     f*s1 is convergent by COMSEQ_2:def 4;
     hence f*s1 is convergent & f/.x0 = lim (f*s1) by A20,COMSEQ_2:def 5;
   end;
   hence thesis by A18,Def20;
end;

theorem Th35:
for f be PartFunc of CNS1,CNS2 ,x0 be Point of CNS1 holds
 f is_continuous_in x0 iff
  x0 in dom f &
  for N1 being Neighbourhood of f/.x0
   ex N being Neighbourhood of x0 st
    for x1 be Point of CNS1 st x1 in dom f & x1 in N holds f/.x1 in N1
proof
   let f be PartFunc of CNS1,CNS2;
   let x0 be Point of CNS1;
   thus f is_continuous_in x0 implies x0 in dom f &
    for N1 being Neighbourhood of f/.x0
     ex N being Neighbourhood of x0 st
      for x1 be Point of CNS1 st x1 in dom f & x1 in N holds f/.x1 in N1
   proof
     assume
A1:  f is_continuous_in x0;
     hence x0 in dom f by Def15;
     let N1 be Neighbourhood of f/.x0;
     consider r such that
A2:   0<r & {y where y is Point of CNS2 : ||.y-f/.x0 .|| < r} c= N1
     by Def5;
     consider s such that
A3:   0<s & for x1 be Point of CNS1 st x1 in dom f & ||. x1- x0 .|| <s holds
       ||. f/.x1- f/.x0 .|| <r by A1,A2,Th29;
     reconsider N={z where z is Point of CNS1 : ||.z-x0 .|| < s}
      as Neighbourhood of x0 by A3,Th3;
     take N;
     let x1 be Point of CNS1;
     assume
A4:  x1 in dom f & x1 in N;
     consider z be Point of CNS1 such that
A5:  x1=z and
A6: ||.z-x0 .|| < s by A4;
     ||. f/.x1- f/.x0 .|| <r by A3,A4,A5,A6; then
     f/.x1 in  {y where y is Point of CNS2 : ||.y-f/.x0 .|| < r};
     hence thesis by A2;
   end;
   assume
A7: x0 in dom f & for N1 being Neighbourhood of f/.x0
    ex N being Neighbourhood of x0 st
     for x1 be Point of CNS1 st x1 in dom f & x1 in N holds f/.x1 in N1;
   now
     let r; assume 0<r; then
     reconsider N1 = {y where y is Point of CNS2 : ||.y-f/.x0 .|| < r}
      as Neighbourhood of f/.x0 by Th3;
     consider N2 being Neighbourhood of x0 such that
A8:   for x1 be Point of CNS1 st x1 in dom f & x1 in N2 holds
       f/.x1 in N1 by A7;
     consider s such that
A9:   0<s & {z where z is Point of CNS1 : ||.z-x0 .|| < s} c= N2
     by Def5;
     take s;
     for x1 be Point of CNS1 st x1 in dom f & ||. x1- x0 .|| <s holds
      ||. f/.x1- f/.x0 .|| <r
     proof
       let x1 be Point of CNS1;
       assume
A10:    x1 in dom f & ||. x1- x0 .|| <s; then
       x1 in {z where z is Point of CNS1 : ||.z-x0 .|| < s}; then
       f/.x1 in N1 by A8,A9,A10; then
       consider y be Point of CNS2 such that
A11:    f/.x1=y and
A12:   ||.y-f/.x0 .|| < r;
       thus thesis by A11,A12;
     end;
     hence 0<s & for x1 be Point of CNS1 st
      x1 in dom f & ||. x1- x0 .|| < s holds
       ||. f/.x1- f/.x0 .|| <r by A9;
   end;
   hence thesis by A7,Th29;
end;

theorem Th36:
for f be PartFunc of CNS,RNS,x0 be Point of CNS holds
 f is_continuous_in x0 iff
  x0 in dom f &
  for N1 being Neighbourhood of f/.x0
   ex N being Neighbourhood of x0 st
    for x1 be Point of CNS st x1 in dom f & x1 in N holds f/.x1 in N1
proof
   let f be PartFunc of CNS,RNS;
   let x0 be Point of CNS;
   thus f is_continuous_in x0 implies x0 in dom f &
    for N1 being Neighbourhood of f/.x0
     ex N being Neighbourhood of x0 st
      for x1 be Point of CNS st x1 in dom f & x1 in N holds f/.x1 in N1
   proof
     assume
A1:  f is_continuous_in x0;
     hence x0 in dom f by Def16;
     let N1 be Neighbourhood of f/.x0;
     consider r such that
A2:   0<r & {y where y is Point of RNS : ||.y-f/.x0 .|| < r} c= N1
     by NFCONT_1:def 3;
     consider s such that
A3:   0<s & for x1 be Point of CNS st x1 in dom f & ||. x1- x0 .|| <s holds
       ||. f/.x1- f/.x0 .|| <r by A1,A2,Th30;
     reconsider N={z where z is Point of CNS : ||.z-x0 .|| < s}
      as Neighbourhood of x0 by A3,Th3;
     take N;
     let x1 be Point of CNS;
     assume
A4:  x1 in dom f & x1 in N;
     consider z be Point of CNS such that
A5:  x1=z
      and
A6: ||.z-x0 .|| < s by A4;
     ||. f/.x1- f/.x0 .|| <r by A3,A4,A5,A6; then
     f/.x1 in {y where y is Point of RNS : ||.y-f/.x0 .|| < r};
     hence thesis by A2;
   end;
   assume
A7: x0 in dom f & for N1 being Neighbourhood of f/.x0
    ex N being Neighbourhood of x0 st
     for x1 be Point of CNS st x1 in dom f & x1 in N holds f/.x1 in N1;
   now
     let r; assume 0<r; then
     reconsider N1 = {y where y is Point of RNS : ||.y-f/.x0 .|| < r}
      as Neighbourhood of f/.x0 by NFCONT_1:3;
     consider N2 being Neighbourhood of x0 such that
A8:   for x1 be Point of CNS st x1 in dom f & x1 in N2 holds f/.x1 in N1 by A7;
     consider s such that
A9:   0<s & {z where z is Point of CNS : ||.z-x0 .|| < s} c= N2
     by Def5;
     take s;
     for x1 be Point of CNS st x1 in dom f & ||. x1- x0 .|| <s holds
      ||. f/.x1- f/.x0 .|| <r
     proof
       let x1 be Point of CNS;
       assume
A10:    x1 in dom f & ||. x1- x0 .|| <s; then
       x1 in {z where z is Point of CNS : ||.z-x0 .|| < s}; then
       f/.x1 in N1 by A8,A9,A10; then
       consider y be Point of RNS such that
A11:    f/.x1=y and
A12:   ||.y-f/.x0 .|| < r;
       thus thesis by A11,A12;
     end;
     hence 0<s & for x1 be Point of CNS st
      x1 in dom f & ||. x1- x0 .|| < s holds ||. f/.x1- f/.x0 .|| <r by A9;
   end;
   hence thesis by A7,Th30;
end;

theorem Th37:
for f be PartFunc of RNS,CNS, x0 be Point of RNS holds
 f is_continuous_in x0 iff
  x0 in dom f &
  for N1 being Neighbourhood of f/.x0
   ex N being Neighbourhood of x0 st
    for x1 be Point of RNS st x1 in dom f & x1 in N holds f/.x1 in N1
proof
   let f be PartFunc of RNS,CNS;
   let x0 be Point of RNS;
   thus f is_continuous_in x0 implies x0 in dom f &
    for N1 being Neighbourhood of f/.x0
     ex N being Neighbourhood of x0 st
      for x1 be Point of RNS st x1 in dom f & x1 in N holds f/.x1 in N1
   proof
     assume
A1:  f is_continuous_in x0;
     hence x0 in dom f by Def17;
     let N1 be Neighbourhood of f/.x0;
     consider r such that
A2:   0<r & {y where y is Point of CNS : ||.y-f/.x0 .|| < r} c= N1
     by Def5;
     consider s such that
A3:   0<s & for x1 be Point of RNS st x1 in dom f & ||. x1- x0 .|| <s holds
       ||. f/.x1- f/.x0 .|| <r by A1,A2,Th31;
     reconsider N={z where z is Point of RNS : ||.z-x0 .|| < s}
      as Neighbourhood of x0 by A3,NFCONT_1:3;
     take N;
     let x1 be Point of RNS;
     assume
A4:  x1 in dom f & x1 in N;
     consider z be Point of RNS such that
A5:  x1=z
      and
A6: ||.z-x0 .|| < s by A4;
     ||. f/.x1- f/.x0 .|| <r by A3,A4,A5,A6; then
     f/.x1 in {y where y is Point of CNS : ||.y-f/.x0 .|| < r};
     hence thesis by A2;
   end;
   assume
A7: x0 in dom f & for N1 being Neighbourhood of f/.x0
    ex N being Neighbourhood of x0 st
     for x1 be Point of RNS st x1 in dom f & x1 in N holds f/.x1 in N1;
   now
     let r; assume 0<r; then
     reconsider N1 = {y where y is Point of CNS : ||.y-f/.x0 .|| < r}
      as Neighbourhood of f/.x0 by Th3;
     consider N2 being Neighbourhood of x0 such that
A8:   for x1 be Point of RNS st x1 in dom f & x1 in N2 holds f/.x1 in N1 by A7;
     consider s such that
A9:   0<s & {z where z is Point of RNS : ||.z-x0 .|| < s} c= N2
     by NFCONT_1:def 3;
     take s;
     for x1 be Point of RNS st x1 in dom f & ||. x1- x0 .|| <s
      holds ||. f/.x1- f/.x0 .|| <r
     proof
       let x1 be Point of RNS;
       assume
A10:    x1 in dom f & ||. x1- x0 .|| <s; then
       x1 in {z where z is Point of RNS : ||.z-x0 .|| < s}; then
       f/.x1 in N1 by A8,A9,A10; then
       consider y be Point of CNS such that
A11:    f/.x1=y and
A12:   ||.y-f/.x0 .|| < r;
       thus thesis by A11,A12;
     end;
     hence 0<s & for x1 be Point of RNS st
      x1 in dom f & ||. x1 - x0 .|| < s holds ||. f/.x1- f/.x0 .|| <r by A9;
   end;
   hence thesis by A7,Th31;
end;

theorem Th38:
for f be PartFunc of CNS1,CNS2, x0 be Point of CNS1 holds
 f is_continuous_in x0 iff
  x0 in dom f &
  for N1 being Neighbourhood of f/.x0
   ex N being Neighbourhood of x0 st f.:N c= N1
proof
   let f be PartFunc of CNS1,CNS2;
   let x0 be Point of CNS1;
   thus f is_continuous_in x0 implies
    x0 in dom f & for N1 being Neighbourhood of f/.x0
     ex N being Neighbourhood of x0 st f.:N c= N1
   proof
     assume
A1:  f is_continuous_in x0;
     hence x0 in dom f by Def15;
     let N1 be Neighbourhood of f/.x0;
     consider N being Neighbourhood of x0 such that
A2:   for x1 be Point of CNS1 st x1 in dom f & x1 in N holds f/.x1 in N1
     by A1,Th35;
     take N;
     now
       let r be set;
       assume r in f.:N; then
       consider x be Point of CNS1 such that
A3:     x in dom f and
A4:     x in N & r=f.x by PARTFUN2:78;
       x in dom f & x in N & r=f/.x by A3,A4,FINSEQ_4:def 4;
       hence r in N1 by A2;
     end;
     hence thesis by TARSKI:def 3;
   end;
   assume
A5:x0 in dom f & for N1 being Neighbourhood of f/.x0
    ex N being Neighbourhood of x0 st f.:N c= N1;
   now
     let N1 be Neighbourhood of f/.x0;
     consider N being Neighbourhood of x0 such that
A6:   f.:N c= N1 by A5;
     take N;
     let x1 be Point of CNS1;
     assume
A7:  x1 in dom f & x1 in N; then
     f.x1 in f.:N by FUNCT_1:def 12; then
     f.x1 in N1 by A6;
     hence f/.x1 in N1 by A7,FINSEQ_4:def 4;
   end;
   hence thesis by A5,Th35;
end;

theorem Th39:
for f be PartFunc of CNS,RNS, x0 be Point of CNS holds
 f is_continuous_in x0 iff
  x0 in dom f &
  for N1 being Neighbourhood of f/.x0
   ex N being Neighbourhood of x0 st f.:N c= N1
proof
   let f be PartFunc of CNS,RNS;
   let x0 be Point of CNS;
   thus f is_continuous_in x0 implies
    x0 in dom f & for N1 being Neighbourhood of f/.x0
     ex N being Neighbourhood of x0 st f.:N c= N1
   proof
     assume
A1:  f is_continuous_in x0;
     hence x0 in dom f by Def16;
     let N1 be Neighbourhood of f/.x0;
     consider N being Neighbourhood of x0 such that
A2:   for x1 be Point of CNS st x1 in dom f & x1 in N holds f/.x1 in N1
     by A1,Th36;
     take N;
     now
       let r be set;
       assume r in f.:N; then
       consider x be Point of CNS such that
A3:     x in dom f and
A4:     x in N & r=f.x by PARTFUN2:78;
       x in dom f & x in N & r=f/.x by A3,A4,FINSEQ_4:def 4;
       hence r in N1 by A2;
     end;
     hence thesis by TARSKI:def 3;
   end;
   assume
A5:x0 in dom f & for N1 being Neighbourhood of f/.x0
    ex N being Neighbourhood of x0 st f.:N c= N1;
   now
     let N1 be Neighbourhood of f/.x0;
     consider N being Neighbourhood of x0 such that
A6:   f.:N c= N1 by A5;
     take N;
     let x1 be Point of CNS;
     assume
A7:  x1 in dom f & x1 in N; then
     f.x1 in f.:N by FUNCT_1:def 12; then
     f.x1 in N1 by A6;
     hence f/.x1 in N1 by A7,FINSEQ_4:def 4;
   end;
   hence thesis by A5,Th36;
end;

theorem Th40:
for f be PartFunc of RNS,CNS, x0 be Point of RNS holds
 f is_continuous_in x0 iff
  x0 in dom f &
  for N1 being Neighbourhood of f/.x0
   ex N being Neighbourhood of x0 st f.:N c= N1
proof
   let f be PartFunc of RNS,CNS;
   let x0 be Point of RNS;
   thus f is_continuous_in x0 implies
    x0 in dom f & for N1 being Neighbourhood of f/.x0
     ex N being Neighbourhood of x0 st f.:N c= N1
   proof
     assume
A1:  f is_continuous_in x0;
     hence x0 in dom f by Def17;
     let N1 be Neighbourhood of f/.x0;
     consider N being Neighbourhood of x0 such that
A2:   for x1 be Point of RNS st x1 in dom f & x1 in N holds
       f/.x1 in N1 by A1,Th37;
     take N;
     now
       let r be set;
       assume r in f.:N; then
       consider x be Point of RNS such that
A3:     x in dom f and
A4:     x in N & r=f.x by PARTFUN2:78;
       x in dom f & x in N & r=f/.x by A3,A4,FINSEQ_4:def 4;
       hence r in N1 by A2;
     end;
     hence thesis by TARSKI:def 3;
   end;
   assume
A5:x0 in dom f & for N1 being Neighbourhood of f/.x0
    ex N being Neighbourhood of x0 st f.:N c= N1;
   now
     let N1 be Neighbourhood of f/.x0;
     consider N being Neighbourhood of x0 such that
A6:   f.:N c= N1 by A5;
     take N;
     let x1 be Point of RNS;
     assume
A7:  x1 in dom f & x1 in N; then
     f.x1 in f.:N by FUNCT_1:def 12; then
     f.x1 in N1 by A6;
     hence f/.x1 in N1 by A7,FINSEQ_4:def 4;
   end;
   hence thesis by A5,Th37;
end;

theorem
for f be PartFunc of CNS1,CNS2, x0 be Point of CNS1 st
 x0 in dom f &
 (ex N be Neighbourhood of x0 st dom f /\ N = {x0}) holds
  f is_continuous_in x0
proof
   let f be PartFunc of CNS1,CNS2;
   let x0 be Point of CNS1;
   assume
A1:x0 in dom f;
   given N be Neighbourhood of x0 such that
A2: dom f /\ N = {x0};
   now let N1 be Neighbourhood of f/.x0;
     take N;
A3:  f.:N = f.:{x0} by A2,RELAT_1:145
         .= {f.x0} by A1,FUNCT_1:117
         .= {f/.x0} by A1,FINSEQ_4:def 4;
     f/.x0 in N1 by Th4;
     hence f.:N c= N1 by A3,ZFMISC_1:37;
   end;
   hence thesis by A1,Th38;
end;

theorem
for f be PartFunc of CNS,RNS, x0 be Point of CNS st
 x0 in dom f &
 (ex N be Neighbourhood of x0 st dom f /\ N = {x0}) holds
  f is_continuous_in x0
proof
   let f be PartFunc of CNS,RNS;
   let x0 be Point of CNS;
   assume
A1:x0 in dom f;
   given N be Neighbourhood of x0 such that
A2: dom f /\ N = {x0};
   now let N1 be Neighbourhood of f/.x0;
     take N;
A3:  f.:N = f.:{x0} by A2,RELAT_1:145
         .= {f.x0} by A1,FUNCT_1:117
         .= {f/.x0} by A1,FINSEQ_4:def 4;
     f/.x0 in N1 by NFCONT_1:4;
     hence f.:N c= N1 by A3,ZFMISC_1:37;
   end;
   hence thesis by A1,Th39;
end;

theorem
for f be PartFunc of RNS,CNS, x0 be Point of RNS st
 x0 in dom f &
 (ex N be Neighbourhood of x0 st dom f /\ N = {x0}) holds
  f is_continuous_in x0
proof
   let f be PartFunc of RNS,CNS;
   let x0 be Point of RNS;
   assume
A1:x0 in dom f;
   given N be Neighbourhood of x0 such that
A2: dom f /\ N = {x0};
   now let N1 be Neighbourhood of f/.x0;
     take N;
A3:  f.:N = f.:{x0} by A2,RELAT_1:145
         .= {f.x0} by A1,FUNCT_1:117
         .= {f/.x0} by A1,FINSEQ_4:def 4;
     f/.x0 in N1 by Th4;
     hence f.:N c= N1 by A3,ZFMISC_1:37;
   end;
   hence thesis by A1,Th40;
end;

theorem Th44:
for h1,h2 be PartFunc of CNS1,CNS2, seq be sequence of CNS1 st
 rng seq c= dom h1 /\ dom h2 holds
  (h1+h2)*seq=h1*seq+h2*seq & (h1-h2)*seq=h1*seq-h2*seq
proof
   let h1,h2 be PartFunc of CNS1,CNS2;
   let seq be sequence of CNS1;
   assume
A1:rng seq c= dom h1 /\ dom h2;
   dom h1 /\ dom h2 c= dom h1 & dom h1 /\ dom h2 c= dom h2 by XBOOLE_1:17; then
A2:rng seq c= dom h1 & rng seq c= dom h2 by A1,XBOOLE_1:1;
A3:rng seq c= dom (h1 + h2) by A1,VFUNCT_2:def 1;
A4:rng seq c= dom (h1 - h2) by A1,VFUNCT_2:def 2;
   now let n;
A5:  seq.n in dom (h1 + h2) by A3,Th5;
     thus ((h1+h2)*seq).n = (h1+h2)/.(seq.n) by A3,Th10
      .= h1/.(seq.n) + h2/.(seq.n) by A5,VFUNCT_2:def 1
      .= (h1*seq).n + h2/.(seq.n) by A2,Th10
      .= (h1*seq).n + (h2*seq).n by A2,Th10;
   end;
   hence (h1+h2)*seq=h1*seq+h2*seq by CLVECT_1:def 11;
   now let n;
A6:  seq.n in dom (h1 - h2) by A4,Th5;
     thus ((h1-h2)*seq).n = (h1-h2)/.(seq.n) by A4,Th10
      .= h1/.(seq.n) - h2/.(seq.n) by A6,VFUNCT_2:def 2
      .= (h1*seq).n - h2/.(seq.n) by A2,Th10
      .= (h1*seq).n - (h2*seq).n by A2,Th10;
   end;
   hence (h1-h2)*seq=h1*seq-h2*seq by CLVECT_1:def 12;
end;

theorem Th45:
for h1,h2 be PartFunc of CNS,RNS, seq be sequence of CNS st
 rng seq c= dom h1 /\ dom h2 holds
  (h1+h2)*seq=h1*seq+h2*seq & (h1-h2)*seq=h1*seq-h2*seq
proof
   let h1,h2 be PartFunc of CNS,RNS;
   let seq be sequence of CNS;
   assume
A1:rng seq c= dom h1 /\ dom h2;
   dom h1 /\ dom h2 c= dom h1 & dom h1 /\ dom h2 c= dom h2 by XBOOLE_1:17; then
A2:rng seq c= dom h1 & rng seq c= dom h2 by A1,XBOOLE_1:1;
A3:rng seq c= dom (h1 + h2) by A1,VFUNCT_1:def 1;
A4:rng seq c= dom (h1 - h2) by A1,VFUNCT_1:def 2;
   now let n;
A5:  seq.n in dom (h1 + h2) by A3,Th6;
     thus ((h1+h2)*seq).n = (h1+h2)/.(seq.n) by A3,Th11
      .= h1/.(seq.n) + h2/.(seq.n) by A5,VFUNCT_1:def 1
      .= (h1*seq).n + h2/.(seq.n) by A2,Th11
      .= (h1*seq).n + (h2*seq).n by A2,Th11;
   end;
   hence (h1+h2)*seq=h1*seq+h2*seq by NORMSP_1:def 5;
   now let n;
A6:  seq.n in dom (h1 - h2) by A4,Th6;
     thus ((h1-h2)*seq).n = (h1-h2)/.(seq.n) by A4,Th11
      .= h1/.(seq.n) - h2/.(seq.n) by A6,VFUNCT_1:def 2
      .= (h1*seq).n - h2/.(seq.n) by A2,Th11
      .= (h1*seq).n - (h2*seq).n by A2,Th11;
   end;
   hence (h1-h2)*seq=h1*seq-h2*seq by NORMSP_1:def 6;
end;

theorem Th46:
for h1,h2 be PartFunc of RNS,CNS, seq be sequence of RNS st
 rng seq c= dom h1 /\ dom h2 holds
  (h1+h2)*seq=h1*seq+h2*seq & (h1-h2)*seq=h1*seq-h2*seq
proof
   let h1,h2 be PartFunc of RNS,CNS;
   let seq be sequence of RNS;
   assume
A1:rng seq c= dom h1 /\ dom h2;
   dom h1 /\ dom h2 c= dom h1 & dom h1 /\ dom h2 c= dom h2 by XBOOLE_1:17; then
A2:rng seq c= dom h1 & rng seq c= dom h2 by A1,XBOOLE_1:1;
A3:rng seq c= dom (h1 + h2) by A1,VFUNCT_2:def 1;
A4:rng seq c= dom (h1 - h2) by A1,VFUNCT_2:def 2;
   now let n;
A5:  seq.n in dom (h1 + h2) by A3,Th7;
     thus ((h1+h2)*seq).n = (h1+h2)/.(seq.n) by A3,Th12
      .= h1/.(seq.n) + h2/.(seq.n) by A5,VFUNCT_2:def 1
      .= (h1*seq).n + h2/.(seq.n) by A2,Th12
      .= (h1*seq).n + (h2*seq).n by A2,Th12;
   end;
   hence (h1+h2)*seq=h1*seq+h2*seq by CLVECT_1:def 11;
   now let n;
A6:  seq.n in dom (h1 - h2) by A4,Th7;
     thus ((h1-h2)*seq).n = (h1-h2)/.(seq.n) by A4,Th12
      .= h1/.(seq.n) - h2/.(seq.n) by A6,VFUNCT_2:def 2
      .= (h1*seq).n - h2/.(seq.n) by A2,Th12
      .= (h1*seq).n - (h2*seq).n by A2,Th12;
   end;
   hence (h1-h2)*seq=h1*seq-h2*seq by CLVECT_1:def 12;
end;

theorem Th47:
for h be PartFunc of CNS1,CNS2, seq be sequence of CNS1, z be Complex st
 rng seq c= dom h holds (z(#)h)*seq = z*(h*seq)
proof
   let h be PartFunc of CNS1,CNS2;
   let seq be sequence of CNS1;
   let z be Complex;
   assume
A1:rng seq c= dom h; then
A2:rng seq c= dom (z(#)h) by VFUNCT_2:def 4;
   now let n;
A3:  seq.n in dom (z(#)h) by A2,Th5;
     thus ((z(#)h)*seq).n = (z(#)h)/.(seq.n) by A2,Th10
       .= z * (h/.(seq.n)) by A3,VFUNCT_2:def 4
       .= z * (h*seq).n by A1,Th10;
   end;
   hence thesis by CLVECT_1:def 14;
end;

theorem Th48:
for h be PartFunc of CNS,RNS, seq be sequence of CNS, r be Real st
 rng seq c= dom h holds (r(#)h)*seq = r*(h*seq)
proof
   let h be PartFunc of CNS,RNS;
   let seq be sequence of CNS;
   let r be Real;
   assume
A1:rng seq c= dom h; then
A2:rng seq c= dom (r(#)h) by VFUNCT_1:def 4;
   now let n;
A3:  seq.n in dom (r(#)h) by A2,Th6;
     thus ((r(#)h)*seq).n = (r(#)h)/.(seq.n) by A2,Th11
       .= r * (h/.(seq.n)) by A3,VFUNCT_1:def 4
       .= r * (h*seq).n by A1,Th11;
   end;
   hence thesis by NORMSP_1:def 8;
end;

theorem Th49:
for h be PartFunc of RNS,CNS, seq be sequence of RNS, z be Complex st
 rng seq c= dom h holds (z(#)h)*seq = z*(h*seq)
proof
   let h be PartFunc of RNS,CNS;
   let seq be sequence of RNS;
   let z be Complex;
   assume
A1:rng seq c= dom h; then
A2:rng seq c= dom (z(#)h) by VFUNCT_2:def 4;
   now let n;
A3:  seq.n in dom (z(#)h) by A2,Th7;
     thus ((z(#)h)*seq).n = (z(#)h)/.(seq.n) by A2,Th12
       .= z * (h/.(seq.n)) by A3,VFUNCT_2:def 4
       .= z * (h*seq).n by A1,Th12;
   end;
   hence thesis by CLVECT_1:def 14;
end;

theorem Th50:
for h be PartFunc of CNS1,CNS2, seq be sequence of CNS1 st
 rng seq c= dom h holds
  ||. h*seq .|| = ||.h.||*seq & -(h*seq) = (-h)*seq
proof
   let h be PartFunc of CNS1,CNS2;
   let seq be sequence of CNS1;
   assume
A1: rng seq c= dom h; then
A2:rng seq c= dom ||.h.|| by Def2;
   now let n;
     seq.n in rng seq by Th8; then
     seq.n in dom h by A1; then
A3:  seq.n in dom ||.h.|| by Def2;
     thus ||.h*seq.||.n = ||.(h*seq).n.|| by CLVECT_1:def 16
      .= ||.h/.(seq.n).|| by A1,Th10
      .= ||.h.||.(seq.n) by A3,Def2
      .= ||.h.||/.(seq.n) by A3,FINSEQ_4:def 4
      .= (||.h.||*seq).n by A2,Th14;
   end;
   hence ||.h*seq.|| = (||.h.||)*seq by FUNCT_2:113;
   thus -(h*seq) =(-1r)*(h*seq) by Th2
    .= ((-1r)(#)h)*seq by A1,Th47
    .= (-h)*seq by VFUNCT_2:23;
end;

theorem Th51:
for h be PartFunc of CNS,RNS, seq be sequence of CNS st rng seq c= dom h holds
 ||. h*seq .|| = ||.h.||*seq & -(h*seq) = (-h)*seq
proof
   let h be PartFunc of CNS,RNS;
   let seq be sequence of CNS;
   assume
A1: rng seq c= dom h; then
A2:rng seq c= dom ||.h.|| by Def3;
   now let n;
     seq.n in rng seq by Th8; then
     seq.n in dom h by A1; then
A3:  seq.n in dom ||.h.|| by Def3;
     thus ||.h*seq.||.n = ||.(h*seq).n.|| by NORMSP_1:def 10
      .= ||.h/.(seq.n).|| by A1,Th11
      .= ||.h.||.(seq.n) by A3,Def3
      .= ||.h.||/.(seq.n) by A3,FINSEQ_4:def 4
      .= (||.h.||*seq).n by A2,Th14;
   end;
   hence ||.h*seq.|| = (||.h.||)*seq by FUNCT_2:113;
   thus -(h*seq) =(-1)*(h*seq) by NFCONT_1:2
    .= ((-1)(#)h)*seq by A1,Th48
    .= (-h)*seq by VFUNCT_1:29;
end;

theorem Th52:
for h be PartFunc of RNS,CNS, seq be sequence of RNS st rng seq c= dom h holds
 ||. h*seq .|| = ||.h.||*seq & -(h*seq) = (-h)*seq
proof
   let h be PartFunc of RNS,CNS;
   let seq be sequence of RNS;
   assume
A1: rng seq c= dom h; then
A2:rng seq c= dom ||.h.|| by Def4;
   now let n;
     seq.n in rng seq by NFCONT_1:6; then
     seq.n in dom h by A1; then
A3:  seq.n in dom ||.h.|| by Def4;
     thus ||.h*seq.||.n = ||.(h*seq).n.|| by CLVECT_1:def 16
      .= ||.h/.(seq.n).|| by A1,Th12
      .= ||.h.||.(seq.n) by A3,Def4
      .= ||.h.||/.(seq.n) by A3,FINSEQ_4:def 4
      .= (||.h.||*seq).n by A2,NFCONT_1:9;
   end;
   hence ||.h*seq.|| = (||.h.||)*seq by FUNCT_2:113;
   thus -(h*seq) =(-1r)*(h*seq) by Th2
    .= ((-1r)(#)h)*seq by A1,Th49
    .= (-h)*seq by VFUNCT_2:23;
end;

theorem
for f1,f2 be PartFunc of CNS1,CNS2, x0 be Point of CNS1 st
 f1 is_continuous_in x0 & f2 is_continuous_in x0 holds
  f1+f2 is_continuous_in x0 &
  f1-f2 is_continuous_in x0
proof
   let f1,f2 be PartFunc of CNS1,CNS2;
   let x0 be Point of CNS1;
   assume
A1: f1 is_continuous_in x0 & f2 is_continuous_in x0; then
A2:x0 in dom f1 & for s1 be sequence of CNS1 st rng s1 c= dom f1 &
   s1 is convergent & lim s1 = x0 holds
    f1*s1 is convergent & f1/.x0 = lim (f1*s1) by Def15;
A3:x0 in dom f2 & for s1 be sequence of CNS1 st rng s1 c= dom f2 &
   s1 is convergent & lim s1 = x0 holds
    f2*s1 is convergent & f2/.x0 = lim (f2*s1) by A1,Def15;
   now
     x0 in dom f1 /\ dom f2 by A2,A3,XBOOLE_0:def 3;
     hence
A4:  x0 in dom (f1+f2) by VFUNCT_2:def 1;
     let s1 be sequence of CNS1;
     assume
A5:   rng s1 c= dom(f1+f2) & s1 is convergent & lim s1=x0; then
A6:  rng s1 c= dom f1 /\ dom f2 by VFUNCT_2:def 1;
     dom (f1+f2) = dom f1 /\ dom f2 by VFUNCT_2:def 1; then
     dom (f1+f2) c= dom f1 by XBOOLE_1:17; then
     rng s1 c= dom f1 by A5,XBOOLE_1:1; then
A7:  f1*s1 is convergent & f1/.x0 = lim (f1*s1) by A1,A5,Def15;
     dom (f1+f2) = dom f1 /\ dom f2 by VFUNCT_2:def 1; then
     dom (f1+f2) c= dom f2 by XBOOLE_1:17; then
     rng s1 c= dom f2 by A5,XBOOLE_1:1; then
A8:  f2*s1 is convergent & f2/.x0 = lim (f2*s1) by A1,A5,Def15; then
     (f1*s1)+(f2*s1) is convergent by A7,CLVECT_1:115;
     hence (f1+f2)*s1 is convergent by A6,Th44;
     thus (f1+f2)/.x0 = f1/.x0 + f2/.x0 by A4,VFUNCT_2:def 1
      .= lim (f1*s1 + f2*s1) by A7,A8,CLVECT_1:121
      .= lim ((f1+f2)*s1) by A6,Th44;
   end;
   hence f1+f2 is_continuous_in x0 by Def15;
   now
     x0 in dom f1 /\ dom f2 by A2,A3,XBOOLE_0:def 3;
     hence
A9:  x0 in dom (f1-f2) by VFUNCT_2:def 2;
     let s1 be sequence of CNS1;
     assume
A10:  rng s1 c= dom(f1-f2) & s1 is convergent & lim s1=x0; then
A11: rng s1 c= dom f1 /\ dom f2 by VFUNCT_2:def 2;
     dom (f1-f2) = dom f1 /\ dom f2 by VFUNCT_2:def 2; then
     dom (f1-f2) c= dom f1 by XBOOLE_1:17; then
     rng s1 c= dom f1 by A10,XBOOLE_1:1; then
A12: f1*s1 is convergent & f1/.x0 = lim (f1*s1) by A1,A10,Def15;
     dom (f1-f2) = dom f1 /\ dom f2 by VFUNCT_2:def 2; then
     dom (f1-f2) c= dom f2 by XBOOLE_1:17; then
     rng s1 c= dom f2 by A10,XBOOLE_1:1; then
A13: f2*s1 is convergent & f2/.x0 = lim (f2*s1) by A1,A10,Def15; then
     f1*s1-f2*s1 is convergent by A12,CLVECT_1:116;
     hence (f1-f2)*s1 is convergent by A11,Th44;
     thus (f1-f2)/.x0 = f1/.x0 - f2/.x0 by A9,VFUNCT_2:def 2
      .= lim (f1*s1 - f2*s1) by A12,A13,CLVECT_1:122
      .= lim ((f1-f2)*s1) by A11,Th44;
   end;
   hence f1-f2 is_continuous_in x0 by Def15;
end;

theorem
for f1,f2 be PartFunc of CNS,RNS, x0 be Point of CNS st
 f1 is_continuous_in x0 & f2 is_continuous_in x0 holds
  f1+f2 is_continuous_in x0 &
  f1-f2 is_continuous_in x0
proof
   let f1,f2 be PartFunc of CNS,RNS;
   let x0 be Point of CNS;
   assume
A1: f1 is_continuous_in x0 & f2 is_continuous_in x0; then
A2:x0 in dom f1 & for s1 be sequence of CNS st rng s1 c= dom f1 &
   s1 is convergent & lim s1 = x0 holds
    f1*s1 is convergent & f1/.x0 = lim (f1*s1) by Def16;
A3:x0 in dom f2 & for s1 be sequence of CNS st rng s1 c= dom f2 &
   s1 is convergent & lim s1 = x0 holds
    f2*s1 is convergent & f2/.x0 = lim (f2*s1) by A1,Def16;
   now
     x0 in dom f1 /\ dom f2 by A2,A3,XBOOLE_0:def 3;
     hence
A4:  x0 in dom (f1+f2) by VFUNCT_1:def 1;
     let s1 be sequence of CNS;
     assume
A5:   rng s1 c= dom(f1+f2) & s1 is convergent & lim s1=x0; then
A6:  rng s1 c= dom f1 /\ dom f2 by VFUNCT_1:def 1;
     dom (f1+f2) = dom f1 /\ dom f2 by VFUNCT_1:def 1; then
     dom (f1+f2) c= dom f1 by XBOOLE_1:17; then
     rng s1 c= dom f1 by A5,XBOOLE_1:1; then
A7:  f1*s1 is convergent & f1/.x0 = lim (f1*s1) by A1,A5,Def16;
     dom (f1+f2) = dom f1 /\ dom f2 by VFUNCT_1:def 1; then
     dom (f1+f2) c= dom f2 by XBOOLE_1:17; then
     rng s1 c= dom f2 by A5,XBOOLE_1:1; then
A8:  f2*s1 is convergent & f2/.x0 = lim (f2*s1) by A1,A5,Def16; then
     (f1*s1)+(f2*s1) is convergent by A7,NORMSP_1:34;
     hence (f1+f2)*s1 is convergent by A6,Th45;
     thus (f1+f2)/.x0 = f1/.x0 + f2/.x0 by A4,VFUNCT_1:def 1
      .= lim (f1*s1 + f2*s1) by A7,A8,NORMSP_1:42
      .= lim ((f1+f2)*s1) by A6,Th45;
   end;
   hence f1+f2 is_continuous_in x0 by Def16;
   now
     x0 in dom f1 /\ dom f2 by A2,A3,XBOOLE_0:def 3;
     hence
A9:  x0 in dom (f1-f2) by VFUNCT_1:def 2;
     let s1 be sequence of CNS;
     assume
A10:  rng s1 c= dom(f1-f2) & s1 is convergent & lim s1=x0; then
A11: rng s1 c= dom f1 /\ dom f2 by VFUNCT_1:def 2;
     dom (f1-f2) = dom f1 /\ dom f2 by VFUNCT_1:def 2; then
     dom (f1-f2) c= dom f1 by XBOOLE_1:17; then
     rng s1 c= dom f1 by A10,XBOOLE_1:1; then
A12: f1*s1 is convergent & f1/.x0 = lim (f1*s1) by A1,A10,Def16;
     dom (f1-f2) = dom f1 /\ dom f2 by VFUNCT_1:def 2; then
     dom (f1-f2) c= dom f2 by XBOOLE_1:17; then
     rng s1 c= dom f2 by A10,XBOOLE_1:1; then
A13: f2*s1 is convergent & f2/.x0 = lim (f2*s1) by A1,A10,Def16; then
     f1*s1-f2*s1 is convergent by A12,NORMSP_1:35;
     hence (f1-f2)*s1 is convergent by A11,Th45;
     thus (f1-f2)/.x0 = f1/.x0 - f2/.x0 by A9,VFUNCT_1:def 2
      .= lim (f1*s1 - f2*s1) by A12,A13,NORMSP_1:43
      .= lim ((f1-f2)*s1) by A11,Th45;
   end;
   hence f1-f2 is_continuous_in x0 by Def16;
end;

theorem
for f1,f2 be PartFunc of RNS,CNS, x0 be Point of RNS st
 f1 is_continuous_in x0 & f2 is_continuous_in x0 holds
  f1+f2 is_continuous_in x0 &
  f1-f2 is_continuous_in x0
proof
   let f1,f2 be PartFunc of RNS,CNS;
   let x0 be Point of RNS;
   assume
A1: f1 is_continuous_in x0 & f2 is_continuous_in x0; then
A2:x0 in dom f1 & for s1 be sequence of RNS st rng s1 c= dom f1 &
   s1 is convergent & lim s1 = x0 holds
    f1*s1 is convergent & f1/.x0 = lim (f1*s1) by Def17;
A3:x0 in dom f2 & for s1 be sequence of RNS st rng s1 c= dom f2 &
   s1 is convergent & lim s1 = x0 holds
    f2*s1 is convergent & f2/.x0 = lim (f2*s1) by A1,Def17;
   now
     x0 in dom f1 /\ dom f2 by A2,A3,XBOOLE_0:def 3;
     hence
A4:  x0 in dom (f1+f2) by VFUNCT_2:def 1;
     let s1 be sequence of RNS;
     assume
A5:   rng s1 c= dom(f1+f2) & s1 is convergent & lim s1=x0; then
A6:  rng s1 c= dom f1 /\ dom f2 by VFUNCT_2:def 1;
     dom (f1+f2) = dom f1 /\ dom f2 by VFUNCT_2:def 1; then
     dom (f1+f2) c= dom f1 by XBOOLE_1:17; then
     rng s1 c= dom f1 by A5,XBOOLE_1:1; then
A7:  f1*s1 is convergent & f1/.x0 = lim (f1*s1) by A1,A5,Def17;
     dom (f1+f2) = dom f1 /\ dom f2 by VFUNCT_2:def 1; then
     dom (f1+f2) c= dom f2 by XBOOLE_1:17; then
     rng s1 c= dom f2 by A5,XBOOLE_1:1; then
A8:  f2*s1 is convergent & f2/.x0 = lim (f2*s1) by A1,A5,Def17; then
     (f1*s1)+(f2*s1) is convergent by A7,CLVECT_1:115;
     hence (f1+f2)*s1 is convergent by A6,Th46;
     thus (f1+f2)/.x0 = f1/.x0 + f2/.x0 by A4,VFUNCT_2:def 1
      .= lim (f1*s1 + f2*s1) by A7,A8,CLVECT_1:121
      .= lim ((f1+f2)*s1) by A6,Th46;
   end;
   hence f1+f2 is_continuous_in x0 by Def17;
   now
     x0 in dom f1 /\ dom f2 by A2,A3,XBOOLE_0:def 3;
     hence
A9:  x0 in dom (f1-f2) by VFUNCT_2:def 2;
     let s1 be sequence of RNS;
     assume
A10:  rng s1 c= dom(f1-f2) & s1 is convergent & lim s1=x0; then
A11: rng s1 c= dom f1 /\ dom f2 by VFUNCT_2:def 2;
     dom (f1-f2) = dom f1 /\ dom f2 by VFUNCT_2:def 2; then
     dom (f1-f2) c= dom f1 by XBOOLE_1:17; then
     rng s1 c= dom f1 by A10,XBOOLE_1:1; then
A12: f1*s1 is convergent & f1/.x0 = lim (f1*s1) by A1,A10,Def17;
     dom (f1-f2) = dom f1 /\ dom f2 by VFUNCT_2:def 2; then
     dom (f1-f2) c= dom f2 by XBOOLE_1:17; then
     rng s1 c= dom f2 by A10,XBOOLE_1:1; then
A13: f2*s1 is convergent & f2/.x0 = lim (f2*s1) by A1,A10,Def17; then
     f1*s1-f2*s1 is convergent by A12,CLVECT_1:116;
     hence (f1-f2)*s1 is convergent by A11,Th46;
     thus (f1-f2)/.x0 = f1/.x0 - f2/.x0 by A9,VFUNCT_2:def 2
      .= lim (f1*s1 - f2*s1) by A12,A13,CLVECT_1:122
      .= lim ((f1-f2)*s1) by A11,Th46;
   end;
   hence f1-f2 is_continuous_in x0 by Def17;
end;

theorem Th56:
for f be PartFunc of CNS1,CNS2, x0 be Point of CNS1, z be Complex st
 f is_continuous_in x0 holds z(#)f is_continuous_in x0
proof
   let f be PartFunc of CNS1,CNS2;
   let x0 be Point of CNS1;
   let z be Complex;
   assume
A1: f is_continuous_in x0; then
   x0 in dom f &
   for s1 be sequence of CNS1 st
    rng s1 c= dom f & s1 is convergent & lim s1 = x0 holds
     f*s1 is convergent & f/.x0 = lim (f*s1) by Def15;
   hence
A2: x0 in dom (z(#)f) by VFUNCT_2:def 4;
   let s1 be sequence of CNS1;
   assume
A3: rng s1 c= dom(z(#)f) & s1 is convergent & lim s1=x0; then
A4:rng s1 c= dom f by VFUNCT_2:def 4; then
A5:f*s1 is convergent & f/.x0 = lim (f*s1) by A1,A3,Def15; then
   z*(f*s1) is convergent by CLVECT_1:118;
   hence (z(#)f)*s1 is convergent by A4,Th47;
   thus (z(#)f)/.x0 = z*f/.x0 by A2,VFUNCT_2:def 4
    .= lim (z*(f*s1)) by A5,CLVECT_1:124
    .= lim ((z(#)f)*s1) by A4,Th47;
end;

theorem Th57:
for f be PartFunc of CNS,RNS, x0 be Point of CNS, r be Real st
 f is_continuous_in x0 holds r(#)f is_continuous_in x0
proof
   let f be PartFunc of CNS,RNS;
   let x0 be Point of CNS;
   let r be Real;
   assume
A1: f is_continuous_in x0; then
   x0 in dom f &
   for s1 be sequence of CNS st
    rng s1 c= dom f & s1 is convergent & lim s1 = x0 holds
     f*s1 is convergent & f/.x0 = lim (f*s1) by Def16;
   hence
A2: x0 in dom (r(#)f) by VFUNCT_1:def 4;
   let s1 be sequence of CNS;
   assume
A3: rng s1 c= dom(r(#)f) & s1 is convergent & lim s1=x0; then
A4:rng s1 c= dom f by VFUNCT_1:def 4; then
A5:f*s1 is convergent & f/.x0 = lim (f*s1) by A1,A3,Def16; then
   r*(f*s1) is convergent by NORMSP_1:37;
   hence (r(#)f)*s1 is convergent by A4,Th48;
   thus (r(#)f)/.x0 = r*f/.x0 by A2,VFUNCT_1:def 4
    .= lim (r*(f*s1)) by A5,NORMSP_1:45
    .= lim ((r(#)f)*s1) by A4,Th48;
end;

theorem Th58:
for f be PartFunc of RNS,CNS, x0 be Point of RNS, z be Complex st
 f is_continuous_in x0 holds z(#)f is_continuous_in x0
proof
   let f be PartFunc of RNS,CNS;
   let x0 be Point of RNS;
   let z be Complex;
   assume
A1: f is_continuous_in x0; then
   x0 in dom f &
   for s1 be sequence of RNS st
    rng s1 c= dom f & s1 is convergent & lim s1 = x0 holds
     f*s1 is convergent & f/.x0 = lim (f*s1) by Def17;
   hence
A2: x0 in dom (z(#)f) by VFUNCT_2:def 4;
   let s1 be sequence of RNS;
   assume
A3: rng s1 c= dom(z(#)f) & s1 is convergent & lim s1=x0; then
A4:rng s1 c= dom f by VFUNCT_2:def 4; then
A5:f*s1 is convergent & f/.x0 = lim (f*s1) by A1,A3,Def17; then
   z*(f*s1) is convergent by CLVECT_1:118;
   hence (z(#)f)*s1 is convergent by A4,Th49;
   thus (z(#)f)/.x0 = z*f/.x0 by A2,VFUNCT_2:def 4
    .= lim (z*(f*s1)) by A5,CLVECT_1:124
    .= lim ((z(#)f)*s1) by A4,Th49;
end;

theorem
for f be PartFunc of CNS1,CNS2, x0 be Point of CNS1 st
 f is_continuous_in x0 holds
  ||. f .|| is_continuous_in x0 & -f is_continuous_in x0
proof
   let f be PartFunc of CNS1,CNS2;
   let x0 be Point of CNS1;
   assume
A1: f is_continuous_in x0; then
A2:x0 in dom f & for s1 be sequence of CNS1 st rng s1 c= dom f &
   s1 is convergent & lim s1 = x0 holds
    f*s1 is convergent & f/.x0 = lim (f*s1) by Def15;
   now
     thus
A3:  x0 in dom (||.f .||) by A2,Def2;
     let s1 be sequence of CNS1;
     assume
A4:   rng s1 c= dom(||.f .||) & s1 is convergent & lim s1=x0; then
A5:  rng s1 c= dom f by Def2; then
A6:  f*s1 is convergent & f/.x0 = lim (f*s1) by A1,A4,Def15; then
     ||.f*s1.|| is convergent by CLVECT_1:119;
     hence ||.f.||*s1 is convergent by A5,Th50;
     thus ||.f.||/.x0 =||.f.||.x0 by A3,FINSEQ_4:def 4
      .= ||.f/.x0 .|| by A3,Def2
      .= lim ||.f*s1 .|| by A6,CLOPBAN1:45
      .= lim (||.f.|| *s1) by A5,Th50;
   end;
   hence ||.f .|| is_continuous_in x0 by Def19;
   -f=(-1r)(#)f by VFUNCT_2:23;
   hence thesis by A1,Th56;
end;

theorem
for f be PartFunc of CNS,RNS, x0 be Point of CNS st
 f is_continuous_in x0 holds
  ||. f .|| is_continuous_in x0 & -f is_continuous_in x0
proof
   let f be PartFunc of CNS,RNS;
   let x0 be Point of CNS;
   assume
A1: f is_continuous_in x0; then
A2:x0 in dom f & for s1 be sequence of CNS st rng s1 c= dom f &
   s1 is convergent & lim s1 = x0 holds
    f*s1 is convergent & f/.x0 = lim (f*s1) by Def16;
   now
     thus
A3:  x0 in dom (||.f .||) by A2,Def3;
     let s1 be sequence of CNS;
     assume
A4:   rng s1 c= dom(||.f .||) & s1 is convergent & lim s1=x0; then
A5:  rng s1 c= dom f by Def3; then
A6:  f*s1 is convergent & f/.x0 = lim (f*s1) by A1,A4,Def16; then
     ||.f*s1.|| is convergent by NORMSP_1:39;
     hence ||.f.||*s1 is convergent by A5,Th51;
     thus ||.f.||/.x0 =||.f.||.x0 by A3,FINSEQ_4:def 4
      .= ||.f/.x0 .|| by A3,Def3
      .= lim ||.f*s1 .|| by A6,LOPBAN_1:47
      .= lim (||.f.|| *s1) by A5,Th51;
   end;
   hence ||.f .|| is_continuous_in x0 by Def19;
   -f=(-1)(#)f by VFUNCT_1:29;
   hence thesis by A1,Th57;
end;

theorem
for f be PartFunc of RNS,CNS, x0 be Point of RNS st
 f is_continuous_in x0 holds
  ||. f .|| is_continuous_in x0 & -f is_continuous_in x0
proof
   let f be PartFunc of RNS,CNS;
   let x0 be Point of RNS;
   assume
A1: f is_continuous_in x0; then
A2:x0 in dom f &
   for s1 be sequence of RNS st rng s1 c= dom f &
   s1 is convergent & lim s1 = x0 holds
    f*s1 is convergent & f/.x0 = lim (f*s1) by Def17;
   now
     thus
A3:  x0 in dom (||.f .||) by A2,Def4;
     let s1 be sequence of RNS;
     assume
A4:   rng s1 c= dom(||.f .||) & s1 is convergent & lim s1=x0; then
A5:  rng s1 c= dom f by Def4; then
A6:  f*s1 is convergent & f/.x0 = lim (f*s1) by A1,A4,Def17; then
     ||.f*s1.|| is convergent by CLVECT_1:119;
     hence ||.f.||*s1 is convergent by A5,Th52;
     thus ||.f.||/.x0 =||.f.||.x0 by A3,FINSEQ_4:def 4
      .= ||.f/.x0 .|| by A3,Def4
      .= lim ||.f*s1 .|| by A6,CLOPBAN1:45
      .= lim (||.f.|| *s1) by A5,Th52;
   end;
   hence ||.f .|| is_continuous_in x0 by NFCONT_1:def 10;
   -f=(-1r)(#)f by VFUNCT_2:23;
   hence thesis by A1,Th58;
end;

definition
let CNS1,CNS2 be ComplexNormSpace;
let f be PartFunc of CNS1,CNS2;
let X be set;
 pred f is_continuous_on X means :Def21:
 X c= dom f &
 for x0 be Point of CNS1 st x0 in X holds f|X is_continuous_in x0;
end;

definition
let CNS be ComplexNormSpace;
let RNS be RealNormSpace;
let f be PartFunc of CNS,RNS;
let X be set;
 pred f is_continuous_on X means :Def22:
 X c= dom f &
 for x0 be Point of CNS st x0 in X holds f|X is_continuous_in x0;
end;

definition
let RNS be RealNormSpace;
let CNS be ComplexNormSpace;
let g be PartFunc of RNS,CNS;
let X be set;
 pred g is_continuous_on X means :Def23:
 X c= dom g &
 for x0 be Point of RNS st x0 in X holds g|X is_continuous_in x0;
end;

definition
let CNS be ComplexNormSpace;
let f be PartFunc of the carrier of CNS, COMPLEX;
let X be set;
 pred f is_continuous_on X means :Def24:
 X c= dom f &
 for x0 be Point of CNS st x0 in X holds f|X is_continuous_in x0;
end;

definition
let CNS be ComplexNormSpace;
let f be PartFunc of the carrier of CNS, REAL;
let X be set;
 pred f is_continuous_on X means :Def25:
 X c= dom f &
 for x0 be Point of CNS st x0 in X holds f|X is_continuous_in x0;
end;

definition
let RNS be RealNormSpace;
let f be PartFunc of the carrier of RNS, COMPLEX;
let X be set;
 pred f is_continuous_on X means :Def26:
 X c= dom f &
 for x0 be Point of RNS st x0 in X holds f|X is_continuous_in x0;
end;

 reserve X,X1 for set;

theorem Th62:
for f be PartFunc of CNS1,CNS2 holds
 f is_continuous_on X
iff
 X c= dom f &
 for s1 be sequence of CNS1 st
  rng s1 c= X & s1 is convergent & lim s1 in X holds
   f*s1 is convergent & f/.(lim s1) = lim (f*s1)
proof
   let f be PartFunc of CNS1,CNS2;
   thus f is_continuous_on X implies
    X c= dom f &
    for s1 be sequence of CNS1 st
     rng s1 c= X & s1 is convergent & lim s1 in X holds
      f*s1 is convergent & f/.(lim s1) = lim (f*s1)
   proof
     assume
A1:   f is_continuous_on X; then
A2:  X c= dom f &
     for x1 be Point of CNS1 st x1 in X holds f|X is_continuous_in x1
     by Def21;
     now let s1 be sequence of CNS1 such that
A3:     rng s1 c= X & s1 is convergent & lim s1 in X;
A4:    dom (f|X) = dom f /\ X by PARTFUN2:32
                .= X by A2,XBOOLE_1:28;
A5:    f|X is_continuous_in (lim s1) by A1,A3,Def21;
       now let n;
         dom s1 = NAT by FUNCT_2:def 1; then
A6:      s1.n in rng s1 by FUNCT_1:12;
A7:      rng s1 c= dom f by A2,A3,XBOOLE_1:1; thus
         ((f|X)*s1).n = (f|X)/.(s1.n) by A3,A4,Th10
          .= f/.(s1.n) by A3,A4,A6,PARTFUN2:32
          .= (f*s1).n by A7,Th10;
       end; then
A8:    (f|X)*s1 = f*s1 by FUNCT_2:113;
       f/.(lim s1) = (f|X)/.(lim s1) by A3,A4,PARTFUN2:32
        .= lim (f*s1) by A3,A4,A5,A8,Def15;
       hence f*s1 is convergent & f/.(lim s1)
         = lim (f*s1) by A3,A4,A5,A8,Def15;
     end;
     hence thesis by A1,Def21;
   end;
   assume
A9: X c= dom f &
   for s1 be sequence of CNS1 st
    rng s1 c= X & s1 is convergent & lim s1 in X holds
     f*s1 is convergent & f/.(lim s1) = lim (f*s1);
   now
     let x1 be Point of CNS1 such that
A10:  x1 in X;
A11: dom (f|X) = dom f /\ X by PARTFUN2:32
              .= X by A9,XBOOLE_1:28;
     now
       let s1 be sequence of CNS1 such that
A12:    rng s1 c= dom (f|X) & s1 is convergent & lim s1=x1;
       now let n;
         dom s1 = NAT by FUNCT_2:def 1; then
A13:     s1.n in rng s1 by FUNCT_1:12;
A14:     rng s1 c= dom f by A9,A11,A12,XBOOLE_1:1; thus
         ((f|X)*s1).n = (f|X)/.(s1.n) by A12,Th10
         .= f/.(s1.n) by A12,A13,PARTFUN2:32
         .= (f*s1).n by A14,Th10;
       end; then
A15:   (f|X)*s1 = f*s1 by FUNCT_2:113;
       (f|X)/.(lim s1) = f/.(lim s1) by A10,A11,A12,PARTFUN2:32
        .= lim ((f|X)*s1) by A9,A10,A11,A12,A15;
       hence (f|X)*s1 is convergent & (f|X)/.x1 = lim ((f|X)*s1)
       by A9,A10,A11,A12,A15;
     end;
     hence f|X is_continuous_in x1 by A10,A11,Def15;
   end;
   hence thesis by A9,Def21;
end;

theorem Th63:
for f be PartFunc of CNS,RNS holds
 f is_continuous_on X
 iff
 X c= dom f &
 for s1 be sequence of CNS st
  rng s1 c= X & s1 is convergent & lim s1 in X holds
   f*s1 is convergent & f/.(lim s1) = lim (f*s1)
proof
   let f be PartFunc of CNS,RNS;
   thus f is_continuous_on X implies X c= dom f &
    for s1 be sequence of CNS st
     rng s1 c= X & s1 is convergent & lim s1 in X holds
      f*s1 is convergent & f/.(lim s1) = lim (f*s1)
   proof
     assume
A1:   f is_continuous_on X; then
A2:  X c= dom f &
     for x1 be Point of CNS st x1 in X holds f|X is_continuous_in x1
     by Def22;
     now let s1 be sequence of CNS such that
A3:     rng s1 c= X & s1 is convergent & lim s1 in X;
A4:    dom (f|X) = dom f /\ X by PARTFUN2:32
                .= X by A2,XBOOLE_1:28;
A5:    f|X is_continuous_in (lim s1) by A1,A3,Def22;
       now let n;
         dom s1 = NAT by FUNCT_2:def 1; then
A6:      s1.n in rng s1 by FUNCT_1:12;
A7:      rng s1 c= dom f by A2,A3,XBOOLE_1:1; thus
         ((f|X)*s1).n = (f|X)/.(s1.n) by A3,A4,Th11
          .= f/.(s1.n) by A3,A4,A6,PARTFUN2:32
          .= (f*s1).n by A7,Th11;
       end; then
A8:    (f|X)*s1 = f*s1 by FUNCT_2:113;
       f/.(lim s1) = (f|X)/.(lim s1) by A3,A4,PARTFUN2:32
        .= lim (f*s1) by A3,A4,A5,A8,Def16;
       hence f*s1 is convergent & f/.(lim s1)
         = lim (f*s1) by A3,A4,A5,A8,Def16;
     end;
     hence thesis by A1,Def22;
   end;
   assume
A9: X c= dom f &
   for s1 be sequence of CNS st
    rng s1 c= X & s1 is convergent & lim s1 in X holds
     f*s1 is convergent & f/.(lim s1) = lim (f*s1);
   now
     let x1 be Point of CNS such that
A10:  x1 in X;
A11: dom (f|X) = dom f /\ X by PARTFUN2:32
              .= X by A9,XBOOLE_1:28;
     now
       let s1 be sequence of CNS such that
A12:    rng s1 c= dom (f|X) & s1 is convergent & lim s1=x1;
       now let n;
         dom s1 = NAT by FUNCT_2:def 1; then
A13:     s1.n in rng s1 by FUNCT_1:12;
A14:     rng s1 c= dom f by A9,A11,A12,XBOOLE_1:1; thus
         ((f|X)*s1).n = (f|X)/.(s1.n) by A12,Th11
         .= f/.(s1.n) by A12,A13,PARTFUN2:32
         .= (f*s1).n by A14,Th11;
       end; then
A15:   (f|X)*s1 = f*s1 by FUNCT_2:113;
       (f|X)/.(lim s1) = f/.(lim s1) by A10,A11,A12,PARTFUN2:32
        .= lim ((f|X)*s1) by A9,A10,A11,A12,A15;
       hence (f|X)*s1 is convergent & (f|X)/.x1 = lim ((f|X)*s1)
       by A9,A10,A11,A12,A15;
     end;
     hence f|X is_continuous_in x1 by A10,A11,Def16;
   end;
   hence thesis by A9,Def22;
end;

theorem Th64:
for f be PartFunc of RNS,CNS holds
 f is_continuous_on X
 iff
 X c= dom f &
 for s1 be sequence of RNS st
  rng s1 c= X & s1 is convergent & lim s1 in X holds
   f*s1 is convergent & f/.(lim s1) = lim (f*s1)
proof
   let f be PartFunc of RNS,CNS;
   thus f is_continuous_on X implies X c= dom f &
    for s1 be sequence of RNS st
     rng s1 c= X & s1 is convergent & lim s1 in X holds
      f*s1 is convergent & f/.(lim s1) = lim (f*s1)
   proof
     assume
A1:   f is_continuous_on X; then
A2:  X c= dom f &
     for x1 be Point of RNS st x1 in X holds f|X is_continuous_in x1
     by Def23;
     now let s1 be sequence of RNS such that
A3:     rng s1 c= X & s1 is convergent & lim s1 in X;
A4:    dom (f|X) = dom f /\ X by PARTFUN2:32
                .= X by A2,XBOOLE_1:28;
A5:    f|X is_continuous_in (lim s1) by A1,A3,Def23;
       now let n;
         dom s1 = NAT by FUNCT_2:def 1; then
A6:      s1.n in rng s1 by FUNCT_1:12;
A7:      rng s1 c= dom f by A2,A3,XBOOLE_1:1; thus
         ((f|X)*s1).n = (f|X)/.(s1.n) by A3,A4,Th12
          .= f/.(s1.n) by A3,A4,A6,PARTFUN2:32
          .= (f*s1).n by A7,Th12;
       end; then
A8:    (f|X)*s1 = f*s1 by FUNCT_2:113;
       f/.(lim s1) = (f|X)/.(lim s1) by A3,A4,PARTFUN2:32
        .= lim (f*s1) by A3,A4,A5,A8,Def17;
       hence f*s1 is convergent & f/.(lim s1)
         = lim (f*s1) by A3,A4,A5,A8,Def17;
     end;
     hence thesis by A1,Def23;
   end;
   assume
A9: X c= dom f &
   for s1 be sequence of RNS st
    rng s1 c= X & s1 is convergent & lim s1 in X holds
     f*s1 is convergent & f/.(lim s1) = lim (f*s1);
   now
     let x1 be Point of RNS such that
A10:  x1 in X;
A11: dom (f|X) = dom f /\ X by PARTFUN2:32
              .= X by A9,XBOOLE_1:28;
     now
       let s1 be sequence of RNS such that
A12:    rng s1 c= dom (f|X) & s1 is convergent & lim s1=x1;
       now let n;
         dom s1 = NAT by FUNCT_2:def 1; then
A13:     s1.n in rng s1 by FUNCT_1:12;
A14:     rng s1 c= dom f by A9,A11,A12,XBOOLE_1:1; thus
         ((f|X)*s1).n = (f|X)/.(s1.n) by A12,Th12
         .= f/.(s1.n) by A12,A13,PARTFUN2:32
         .= (f*s1).n by A14,Th12;
       end; then
A15:   (f|X)*s1 = f*s1 by FUNCT_2:113;
       (f|X)/.(lim s1) = f/.(lim s1) by A10,A11,A12,PARTFUN2:32
        .= lim ((f|X)*s1) by A9,A10,A11,A12,A15;
       hence (f|X)*s1 is convergent & (f|X)/.x1 = lim ((f|X)*s1)
       by A9,A10,A11,A12,A15;
     end;
     hence f|X is_continuous_in x1 by A10,A11,Def17;
   end;
   hence thesis by A9,Def23;
end;

theorem Th65:
for f be PartFunc of CNS1,CNS2 holds
 f is_continuous_on X iff
  X c= dom f &
  for x0 be Point of CNS1, r st x0 in X & 0<r
   ex s st 0<s & for x1 be Point of CNS1 st
    x1 in X & ||. x1- x0 .|| < s holds ||. f/.x1 - f/.x0 .|| < r
proof
   let f be PartFunc of CNS1,CNS2;
   thus f is_continuous_on X implies
    X c= dom f &
    for x0 be Point of CNS1, r st x0 in X & 0<r
     ex s st 0<s & for x1 be Point of CNS1 st
      x1 in X & ||. x1- x0 .|| < s holds ||. f/.x1 - f/.x0 .|| < r
   proof
     assume
A1:   f is_continuous_on X; then
A2:  X c= dom f &
     for x1 be Point of CNS1 st x1 in X holds f|X is_continuous_in x1
     by Def21;
     thus X c= dom f by A1,Def21;
     let x0 be Point of CNS1,r;
     assume
A3:   x0 in X & 0<r; then
     f|X is_continuous_in x0 by A1,Def21; then
     consider s such that
A4:   0<s & for x1 be Point of CNS1 st x1 in dom(f|X) & ||. x1- x0 .|| <s holds
       ||. (f|X)/.x1-(f|X)/.x0 .||<r by A3,Th29;
A5:  dom(f|X) = dom f /\ X by PARTFUN2:32
             .= X by A2,XBOOLE_1:28;
     take s;
     thus 0<s by A4;
     let x1 be Point of CNS1;
     assume
A6:   x1 in X & ||. x1- x0 .|| <s; then
     ||. f/.x1 - f/.x0 .|| = ||. (f|X)/.x1 - f/.x0 .|| by A5,PARTFUN2:32
                 .= ||. (f|X)/.x1 - (f|X)/.x0 .|| by A3,A5,PARTFUN2:32;
     hence thesis by A4,A5,A6;
   end;
   assume that
A7: X c= dom f and
A8: for x0 be Point of CNS1, r st x0 in X & 0<r
     ex s st 0<s & for x1 be Point of CNS1 st
      x1 in X & ||. x1- x0 .|| < s holds ||. f/.x1 - f/.x0 .|| < r;
A9:dom (f|X) = dom f /\ X by PARTFUN2:32
            .= X by A7,XBOOLE_1:28;
   now
     let x0 be Point of CNS1 such that
A10:  x0 in X;
     for r st 0<r
      ex s st 0<s & for x1 be Point of CNS1 st
       x1 in dom(f|X) & ||. x1- x0 .|| <s holds
        ||. (f|X)/.x1-(f|X)/.x0 .||<r
     proof
       let r;
       assume 0<r; then
       consider s such that
A11:    0<s & for x1 be Point of CNS1 st x1 in X & ||. x1- x0 .|| < s holds
        ||. f/.x1 - f/.x0 .|| < r by A8,A10;
       take s;
       thus 0<s by A11;
       let x1 be Point of CNS1 such that
A12:    x1 in dom(f|X) & ||. x1- x0 .|| <s;
       ||. (f|X)/.x1-(f|X)/.x0.||
        = ||. (f|X)/.x1 - f/.x0.|| by A9,A10,PARTFUN2:32
       .= ||. f/.x1 - f/.x0 .|| by A12,PARTFUN2:32;
       hence thesis by A9,A11,A12;
     end;
     hence f|X is_continuous_in x0 by A9,A10,Th29;
   end;
   hence thesis by A7,Def21;
end;

theorem Th66:
for f be PartFunc of CNS,RNS holds
 f is_continuous_on X iff
  X c= dom f &
  for x0 be Point of CNS, r st x0 in X & 0<r
   ex s st 0<s & for x1 be Point of CNS st
    x1 in X & ||. x1- x0 .|| < s holds ||. f/.x1 - f/.x0 .|| < r
proof
   let f be PartFunc of CNS,RNS;
   thus f is_continuous_on X implies
    X c= dom f &
    for x0 be Point of CNS, r st x0 in X & 0<r
     ex s st 0<s & for x1 be Point of CNS st x1 in X & ||. x1- x0 .|| < s holds
      ||. f/.x1 - f/.x0 .|| < r
   proof
     assume
A1:   f is_continuous_on X; then
A2:  X c= dom f &
     for x1 be Point of CNS st x1 in X holds f|X is_continuous_in x1
    by Def22;
     thus X c= dom f by A1,Def22;
     let x0 be Point of CNS, r;
     assume
A3:   x0 in X & 0<r; then
     f|X is_continuous_in x0 by A1,Def22; then
     consider s such that
A4:   0<s & for x1 be Point of CNS st
       x1 in dom(f|X) & ||. x1- x0 .|| <s holds
        ||. (f|X)/.x1-(f|X)/.x0 .||<r by A3,Th30;
A5:  dom(f|X) = dom f /\ X by PARTFUN2:32
             .= X by A2,XBOOLE_1:28;
     take s;
     thus 0<s by A4;
     let x1 be Point of CNS;
     assume
A6:   x1 in X & ||. x1- x0 .|| <s; then
     ||. f/.x1 - f/.x0 .|| = ||. (f|X)/.x1 - f/.x0 .|| by A5,PARTFUN2:32
                 .= ||. (f|X)/.x1 - (f|X)/.x0 .|| by A3,A5,PARTFUN2:32;
     hence thesis by A4,A5,A6;
   end;
   assume that
A7: X c= dom f and
A8: for x0 be Point of CNS, r st x0 in X & 0<r
     ex s st 0<s & for x1 be Point of CNS st
      x1 in X & ||. x1- x0 .|| < s holds ||. f/.x1 - f/.x0 .|| < r;
A9:dom (f|X) = dom f /\ X by PARTFUN2:32
            .= X by A7,XBOOLE_1:28;
   now
     let x0 be Point of CNS such that
A10:  x0 in X;
     for r st 0<r
      ex s st 0<s & for x1 be Point of CNS st
       x1 in dom(f|X) & ||. x1- x0 .|| <s holds ||. (f|X)/.x1-(f|X)/.x0 .||<r
     proof
       let r;
       assume 0<r; then
       consider s such that
A11:    0<s & for x1 be Point of CNS st x1 in X & ||. x1- x0 .|| < s holds
        ||. f/.x1 - f/.x0 .|| < r by A8,A10;
       take s;
       thus 0<s by A11;
       let x1 be Point of CNS such that
A12:    x1 in dom(f|X) & ||. x1- x0 .|| <s;
       ||. (f|X)/.x1-(f|X)/.x0.||
        = ||. (f|X)/.x1 - f/.x0.|| by A9,A10,PARTFUN2:32
       .= ||. f/.x1 - f/.x0 .|| by A12,PARTFUN2:32;
       hence thesis by A9,A11,A12;
     end;
     hence f|X is_continuous_in x0 by A9,A10,Th30;
   end;
   hence thesis by A7,Def22;
end;

theorem Th67:
for f be PartFunc of RNS,CNS holds
 f is_continuous_on X iff
  X c= dom f &
  for x0 be Point of RNS, r st x0 in X & 0<r
   ex s st 0<s & for x1 be Point of RNS st x1 in X & ||. x1- x0 .|| < s holds
    ||. f/.x1 - f/.x0 .|| < r
proof
   let f be PartFunc of RNS,CNS;
   thus f is_continuous_on X implies
    X c= dom f &
    for x0 be Point of RNS, r st x0 in X & 0<r
     ex s st 0<s & for x1 be Point of RNS st x1 in X & ||. x1- x0 .|| < s holds
      ||. f/.x1 - f/.x0 .|| < r
   proof
     assume
A1:   f is_continuous_on X; then
A2:  X c= dom f &
     for x1 be Point of RNS st x1 in X holds
      f|X is_continuous_in x1 by Def23;
     thus X c= dom f by A1,Def23;
     let x0 be Point of RNS, r;
     assume
A3:   x0 in X & 0<r; then
     f|X is_continuous_in x0 by A1,Def23; then
     consider s such that
A4:   0<s & for x1 be Point of RNS st x1 in dom(f|X) & ||. x1- x0 .|| <s holds
       ||. (f|X)/.x1-(f|X)/.x0 .||<r by A3,Th31;
A5:  dom(f|X) = dom f /\ X by PARTFUN2:32
             .= X by A2,XBOOLE_1:28;
     take s;
     thus 0<s by A4;
     let x1 be Point of RNS;
     assume
A6:   x1 in X & ||. x1- x0 .|| <s; then
     ||. f/.x1 - f/.x0 .|| = ||. (f|X)/.x1 - f/.x0 .|| by A5,PARTFUN2:32
                 .= ||. (f|X)/.x1 - (f|X)/.x0 .|| by A3,A5,PARTFUN2:32;
     hence thesis by A4,A5,A6;
   end;
   assume that
A7: X c= dom f and
A8: for x0 be Point of RNS, r st x0 in X & 0<r
     ex s st 0<s & for x1 be Point of RNS st x1 in X & ||. x1- x0 .|| < s holds
      ||. f/.x1 - f/.x0 .|| < r;
A9:dom (f|X) = dom f /\ X by PARTFUN2:32
            .= X by A7,XBOOLE_1:28;
   now
     let x0 be Point of RNS such that
A10:  x0 in X;
     for r st 0<r
      ex s st
       0<s & for x1 be Point of RNS st x1 in dom(f|X) & ||. x1- x0 .|| <s holds
       ||. (f|X)/.x1-(f|X)/.x0 .||<r
     proof
       let r;
       assume 0<r; then
       consider s such that
A11:    0<s & for x1 be Point of RNS st x1 in X & ||. x1- x0 .|| < s holds
        ||. f/.x1 - f/.x0 .|| < r by A8,A10;
       take s;
       thus 0<s by A11;
       let x1 be Point of RNS such that
A12:    x1 in dom(f|X) & ||. x1- x0 .|| <s;
       ||. (f|X)/.x1-(f|X)/.x0.||
        = ||. (f|X)/.x1 - f/.x0.|| by A9,A10,PARTFUN2:32
       .= ||. f/.x1 - f/.x0 .|| by A12,PARTFUN2:32;
       hence thesis by A9,A11,A12;
     end;
     hence f|X is_continuous_in x0 by A9,A10,Th31;
   end;
   hence thesis by A7,Def23;
end;

theorem
for f be PartFunc of the carrier of CNS, COMPLEX holds
 ( f is_continuous_on X
  iff
   X c= dom f &
   for x0 be Point of CNS, r st x0 in X & 0 < r
    ex s st 0 < s & for x1 be Point of CNS st
     x1 in X & ||. x1- x0 .|| < s holds |. f/.x1 - f/.x0 .| < r )
proof
   let f be PartFunc of the carrier of CNS, COMPLEX;
   thus f is_continuous_on X implies
     X c= dom f &
     for x0 be Point of CNS, r st x0 in X & 0 < r
      ex s st 0 < s & for x1 be Point of CNS st
       x1 in X & ||. x1- x0 .|| < s holds |. f/.x1 - f/.x0 .| < r
   proof
     assume
A1:   f is_continuous_on X; then
A2:  X c= dom f &
     for x1 be Point of CNS st x1 in X holds
      f|X is_continuous_in x1 by Def24;
     thus X c= dom f by A1,Def24;
     let x0 be Point of CNS, r;
     assume
A3:   x0 in X & 0<r; then
     f|X is_continuous_in x0 by A1,Def24; then
     consider s such that
A4:   0<s & for x1 be Point of CNS st x1 in dom(f|X) & ||. x1- x0 .|| <s holds
       |. (f|X)/.x1-(f|X)/.x0 .|<r by A3,Th33;
A5:  dom (f|X) = dom f /\ X by PARTFUN2:32
              .= X by A2,XBOOLE_1:28;
     take s;
     thus 0<s by A4;
     let x1 be Point of CNS;
     assume
A6:   x1 in X & ||. x1- x0 .|| <s; then
     |. f/.x1 - f/.x0 .| = |. (f|X)/.x1 - f/.x0 .| by A5,PARTFUN2:32
                 .= |. (f|X)/.x1 - (f|X)/.x0 .| by A3,A5,PARTFUN2:32;
     hence thesis by A4,A5,A6;
   end;
   assume that
A7: X c= dom f and
A8: for x0 be Point of CNS, r st x0 in X & 0<r
     ex s st 0<s & for x1 be Point of CNS st x1 in X & ||. x1- x0 .|| < s holds
      |. f/.x1 - f/.x0 .| < r;
A9:dom (f|X) = dom f /\ X by PARTFUN2:32
            .= X by A7,XBOOLE_1:28;
   now let x0 be Point of CNS such that
A10: x0 in X;
     for r st 0<r
      ex s st 0<s & for x1 be Point of CNS st
       x1 in dom(f|X) & ||. x1- x0 .|| <s holds |. (f|X)/.x1-(f|X)/.x0 .|<r
     proof let r;
       assume 0<r; then
       consider s such that
A11:    0<s & for x1 be Point of CNS st x1 in X & ||. x1- x0 .|| < s holds
         |. f/.x1 - f/.x0 .| < r by A8,A10;
       take s;
       thus 0<s by A11;
       let x1 be Point of CNS such that
A12:    x1 in dom(f|X) & ||. x1- x0 .|| <s;
       |. (f|X)/.x1-(f|X)/.x0 .|
        = |. (f|X)/.x1 - f/.x0 .| by A9,A10,PARTFUN2:32
       .= |. f/.x1 - f/.x0 .| by A12,PARTFUN2:32;
       hence thesis by A9,A11,A12;
     end;
     hence f|X is_continuous_in x0 by A9,A10,Th33;
   end;
   hence thesis by A7,Def24;
end;

theorem
for f be PartFunc of the carrier of CNS, REAL holds
 ( f is_continuous_on X
  iff
   X c= dom f &
   for x0 be Point of CNS, r st x0 in X & 0 < r
    ex s st 0 < s & for x1 be Point of CNS st
     x1 in X & ||. x1- x0 .|| < s holds abs( f/.x1 - f/.x0 ) < r )
proof
   let f be PartFunc of the carrier of CNS, REAL;
   thus f is_continuous_on X implies
     X c= dom f &
     for x0 be Point of CNS, r st x0 in X & 0 < r
      ex s st 0 < s & for x1 be Point of CNS st
       x1 in X & ||. x1- x0 .|| < s holds abs( f/.x1 - f/.x0 ) < r
   proof
     assume
A1:   f is_continuous_on X; then
A2:  X c= dom f &
     for x1 be Point of CNS st x1 in X holds
      f|X is_continuous_in x1 by Def25;
     thus X c= dom f by A1,Def25;
     let x0 be Point of CNS, r;
     assume
A3:   x0 in X & 0<r; then
     f|X is_continuous_in x0 by A1,Def25; then
     consider s such that
A4:   0<s & for x1 be Point of CNS st x1 in dom(f|X) & ||. x1- x0 .|| <s holds
       abs( (f|X)/.x1-(f|X)/.x0 )<r by A3,Th32;
A5:  dom (f|X) = dom f /\ X by PARTFUN2:32
              .= X by A2,XBOOLE_1:28;
     take s;
     thus 0<s by A4;
     let x1 be Point of CNS;
     assume
A6:   x1 in X & ||. x1- x0 .|| <s; then
     abs( f/.x1 - f/.x0 ) = abs( (f|X)/.x1 - f/.x0 ) by A5,PARTFUN2:32
                 .= abs( (f|X)/.x1 - (f|X)/.x0 ) by A3,A5,PARTFUN2:32;
     hence thesis by A4,A5,A6;
   end;
   assume that
A7: X c= dom f and
A8: for x0 be Point of CNS,r st x0 in X & 0<r
     ex s st 0<s & for x1 be Point of CNS st x1 in X & ||. x1- x0 .|| < s holds
      abs( f/.x1 - f/.x0 ) < r;
A9:dom (f|X) = dom f /\ X by PARTFUN2:32
            .= X by A7,XBOOLE_1:28;
   now let x0 be Point of CNS such that
A10: x0 in X;
     for r st 0<r
      ex s st 0<s & for x1 be Point of CNS st
       x1 in dom(f|X) & ||. x1- x0 .|| <s holds abs( (f|X)/.x1-(f|X)/.x0 )<r
     proof let r;
       assume 0<r; then
       consider s such that
A11:    0<s & for x1 be Point of CNS st x1 in X & ||. x1- x0 .|| < s holds
         abs( f/.x1 - f/.x0 ) < r by A8,A10;
       take s;
       thus 0<s by A11;
       let x1 be Point of CNS such that
A12:    x1 in dom(f|X) & ||. x1- x0 .|| <s;
       abs( (f|X)/.x1-(f|X)/.x0 )
        = abs( (f|X)/.x1 - f/.x0 ) by A9,A10,PARTFUN2:32
       .= abs( f/.x1 - f/.x0 ) by A12,PARTFUN2:32;
       hence thesis by A9,A11,A12;
     end;
     hence f|X is_continuous_in x0 by A9,A10,Th32;
   end;
   hence thesis by A7,Def25;
end;

theorem
for f be PartFunc of the carrier of RNS, COMPLEX holds
 ( f is_continuous_on X
  iff
   X c= dom f &
   for x0 be Point of RNS, r st x0 in X & 0 < r
    ex s st
     0 < s & for x1 be Point of RNS st x1 in X & ||. x1- x0 .|| < s holds
      |. f/.x1 - f/.x0 .| < r )
proof
   let f be PartFunc of the carrier of RNS, COMPLEX;
   thus f is_continuous_on X implies
     X c= dom f &
     for x0 be Point of RNS,r st x0 in X & 0 < r
      ex s st
       0 < s & for x1 be Point of RNS st x1 in X & ||. x1- x0 .|| < s holds
       |. f/.x1 - f/.x0 .| < r
   proof
     assume
A1:   f is_continuous_on X; then
A2:  X c= dom f &
     for x1 be Point of RNS st x1 in X holds
      f|X is_continuous_in x1 by Def26;
     thus X c= dom f by A1,Def26;
     let x0 be Point of RNS,r;
     assume
A3:   x0 in X & 0<r; then
     f|X is_continuous_in x0 by A1,Def26; then
     consider s such that
A4:   0<s & for x1 be Point of RNS st x1 in dom(f|X) & ||. x1- x0 .|| <s holds
       |. (f|X)/.x1-(f|X)/.x0 .|<r by A3,Th34;
A5:  dom (f|X) = dom f /\ X by PARTFUN2:32
              .= X by A2,XBOOLE_1:28;
     take s;
     thus 0<s by A4;
     let x1 be Point of RNS;
     assume
A6:   x1 in X & ||. x1- x0 .|| <s; then
     |. f/.x1 - f/.x0 .| = |. (f|X)/.x1 - f/.x0 .| by A5,PARTFUN2:32
                 .= |. (f|X)/.x1 - (f|X)/.x0 .| by A3,A5,PARTFUN2:32;
     hence thesis by A4,A5,A6;
   end;
   assume that
A7: X c= dom f and
A8: for x0 be Point of RNS, r st x0 in X & 0<r
     ex s st 0<s & for x1 be Point of RNS st x1 in X & ||. x1- x0 .|| < s holds
      |. f/.x1 - f/.x0 .| < r;
A9:dom (f|X) = dom f /\ X by PARTFUN2:32
            .= X by A7,XBOOLE_1:28;
   now let x0 be Point of RNS such that
A10: x0 in X;
     for r st 0<r
      ex s st
       0<s & for x1 be Point of RNS st x1 in dom(f|X) & ||. x1- x0 .|| <s holds
        |. (f|X)/.x1-(f|X)/.x0 .|<r
     proof let r;
       assume 0<r; then
       consider s such that
A11:    0<s & for x1 be Point of RNS st x1 in X & ||. x1- x0 .|| < s holds
         |. f/.x1 - f/.x0 .| < r by A8,A10;
       take s;
       thus 0<s by A11;
       let x1 be Point of RNS such that
A12:    x1 in dom(f|X) & ||. x1- x0 .|| <s;
       |. (f|X)/.x1-(f|X)/.x0 .|
        = |. (f|X)/.x1 - f/.x0 .| by A9,A10,PARTFUN2:32
       .= |. f/.x1 - f/.x0 .| by A12,PARTFUN2:32;
       hence thesis by A9,A11,A12;
     end;
     hence f|X is_continuous_in x0 by A9,A10,Th34;
   end;
   hence thesis by A7,Def26;
end;

theorem
for f be PartFunc of CNS1,CNS2 holds
 f is_continuous_on X iff f|X is_continuous_on X
proof
   let f be PartFunc of CNS1,CNS2;
   thus f is_continuous_on X implies f|X is_continuous_on X
   proof
     assume
A1:   f is_continuous_on X; then
     X c= dom f &
     for r be Point of CNS1 st r in X holds
      f|X is_continuous_in r by Def21; then
     X c= dom f /\ X by XBOOLE_1:28;
     hence X c= dom (f|X) by RELAT_1:90;
     let r be Point of CNS1;
     assume r in X; then
     f|X is_continuous_in r by A1,Def21;
     hence (f|X)|X is_continuous_in r by RELAT_1:101;
   end;
   assume
A2: f|X is_continuous_on X; then
A3:X c= dom(f|X) &
   for r be Point of CNS1 st r in X holds
    (f|X)|X is_continuous_in r by Def21;
A4:dom f /\ X c= dom f by XBOOLE_1:17;
   X c= dom f /\ X by A3,RELAT_1:90;
   hence X c= dom f by A4,XBOOLE_1:1;
   let r be Point of CNS1;
   assume r in X; then
   (f|X)|X is_continuous_in r by A2,Def21;
   hence f|X is_continuous_in r by RELAT_1:101;
end;

theorem
for f be PartFunc of CNS,RNS holds
 f is_continuous_on X iff f|X is_continuous_on X
proof
   let f be PartFunc of CNS,RNS;
   thus f is_continuous_on X implies f|X is_continuous_on X
   proof
     assume
A1:   f is_continuous_on X; then
     X c= dom f &
     for r be Point of CNS st r in X holds
      f|X is_continuous_in r by Def22; then
     X c= dom f /\ X by XBOOLE_1:28;
     hence X c= dom (f|X) by RELAT_1:90;
     let r be Point of CNS;
     assume r in X; then
     f|X is_continuous_in r by A1,Def22;
     hence (f|X)|X is_continuous_in r by RELAT_1:101;
   end;
   assume
A2: f|X is_continuous_on X; then
A3:X c= dom(f|X) &
   for r be Point of CNS st r in X holds
    (f|X)|X is_continuous_in r by Def22;
A4:dom f /\ X c= dom f by XBOOLE_1:17;
   X c= dom f /\ X by A3,RELAT_1:90;
   hence X c= dom f by A4,XBOOLE_1:1;
   let r be Point of CNS;
   assume r in X; then
   (f|X)|X is_continuous_in r by A2,Def22;
   hence f|X is_continuous_in r by RELAT_1:101;
end;

theorem
for f be PartFunc of RNS,CNS holds
 f is_continuous_on X iff f|X is_continuous_on X
proof
   let f be PartFunc of RNS,CNS;
   thus f is_continuous_on X implies f|X is_continuous_on X
   proof
     assume
A1:   f is_continuous_on X; then
     X c= dom f &
     for r be Point of RNS st r in X holds
      f|X is_continuous_in r by Def23; then
     X c= dom f /\ X by XBOOLE_1:28;
     hence X c= dom (f|X) by RELAT_1:90;

     let r be Point of RNS;
     assume r in X; then
     f|X is_continuous_in r by A1,Def23;
     hence (f|X)|X is_continuous_in r by RELAT_1:101;
   end;
   assume
A2: f|X is_continuous_on X; then
A3:X c= dom(f|X) &
   for r be Point of RNS st r in X holds
    (f|X)|X is_continuous_in r by Def23;
A4:dom f /\ X c= dom f by XBOOLE_1:17;
   X c= dom f /\ X by A3,RELAT_1:90;
   hence X c= dom f by A4,XBOOLE_1:1;
   let r be Point of RNS;
   assume r in X; then
   (f|X)|X is_continuous_in r by A2,Def23;
   hence f|X is_continuous_in r by RELAT_1:101;
end;

theorem
for f be PartFunc of the carrier of CNS,COMPLEX
 holds
   (f is_continuous_on X iff f|X is_continuous_on X)
proof
   let f be PartFunc of the carrier of CNS,COMPLEX;
   thus f is_continuous_on X implies f|X is_continuous_on X
   proof
     assume
A1:   f is_continuous_on X; then
     X c= dom f &
     for r be Point of CNS st r in X holds
      f|X is_continuous_in r by Def24; then
     X c= dom f /\ X by XBOOLE_1:28;
     hence X c= dom (f|X) by RELAT_1:90;
     let r be Point of CNS;
     assume r in X; then
     f|X is_continuous_in r by A1,Def24;
     hence (f|X)|X is_continuous_in r by RELAT_1:101;
   end;
   assume
A2: f|X is_continuous_on X; then
A3:X c= dom(f|X) &
   for r be Point of CNS st r in X holds
    (f|X)|X is_continuous_in r by Def24;
A4:dom f /\ X c= dom f by XBOOLE_1:17;
   X c= dom f /\ X by A3,RELAT_1:90;
   hence X c= dom f by A4,XBOOLE_1:1;
   let r be Point of CNS;
   assume r in X; then
   (f|X)|X is_continuous_in r by A2,Def24;
   hence f|X is_continuous_in r by RELAT_1:101;
end;

theorem Th75:
for f be PartFunc of the carrier of CNS,REAL holds
   (f is_continuous_on X iff f|X is_continuous_on X)
proof
   let f be PartFunc of the carrier of CNS,REAL;
   thus f is_continuous_on X implies f|X is_continuous_on X
   proof
     assume
A1:   f is_continuous_on X; then
     X c= dom f &
     for r be Point of CNS st r in X holds
      f|X is_continuous_in r by Def25; then
     X c= dom f /\ X by XBOOLE_1:28;
     hence X c= dom (f|X) by RELAT_1:90;
     let r be Point of CNS;
     assume r in X; then
     f|X is_continuous_in r by A1,Def25;
     hence (f|X)|X is_continuous_in r by RELAT_1:101;
   end;
   assume
A2: f|X is_continuous_on X; then
A3:X c= dom(f|X) &
   for r be Point of CNS st r in X holds
    (f|X)|X is_continuous_in r by Def25;
A4:dom f /\ X c= dom f by XBOOLE_1:17;
   X c= dom f /\ X by A3,RELAT_1:90;
   hence X c= dom f by A4,XBOOLE_1:1;
   let r be Point of CNS;
   assume r in X; then
   (f|X)|X is_continuous_in r by A2,Def25;
   hence f|X is_continuous_in r by RELAT_1:101;
end;

theorem
  for f be PartFunc of the carrier of RNS,COMPLEX holds
   (f is_continuous_on X iff f|X is_continuous_on X)
proof
   let f be PartFunc of the carrier of RNS,COMPLEX;
   thus f is_continuous_on X implies f|X is_continuous_on X
   proof
     assume
A1:   f is_continuous_on X; then
     X c= dom f &
     for r be Point of RNS st r in X holds
      f|X is_continuous_in r by Def26; then
     X c= dom f /\ X by XBOOLE_1:28;
     hence X c= dom (f|X) by RELAT_1:90;
     let r be Point of RNS;
     assume r in X; then
     f|X is_continuous_in r by A1,Def26;
     hence (f|X)|X is_continuous_in r by RELAT_1:101;
   end;
   assume
A2: f|X is_continuous_on X; then
A3:X c= dom(f|X) &
   for r be Point of RNS st r in X holds
    (f|X)|X is_continuous_in r by Def26;
A4:dom f /\ X c= dom f by XBOOLE_1:17;
   X c= dom f /\ X by A3,RELAT_1:90;
   hence X c= dom f by A4,XBOOLE_1:1;
   let r be Point of RNS;
   assume r in X; then
   (f|X)|X is_continuous_in r by A2,Def26;
   hence f|X is_continuous_in r by RELAT_1:101;
end;

theorem Th77:
for f be PartFunc of CNS1,CNS2 st
 f is_continuous_on X & X1 c= X holds f is_continuous_on X1
proof
   let f be PartFunc of CNS1,CNS2;
   assume
A1: f is_continuous_on X & X1 c= X; then
   X c= dom f by Def21;
   hence
A2: X1 c= dom f by A1,XBOOLE_1:1;
   let r be Point of CNS1;
   assume
A3: r in X1; then
A4:f|X is_continuous_in r by A1,Def21;
   thus f|X1 is_continuous_in r
   proof
     r in dom f /\ X1 by A2,A3,XBOOLE_0:def 3;
     hence
A5:   r in dom (f|X1) by RELAT_1:90;
     let s1 be sequence of CNS1 such that
A6:   rng s1 c= dom (f|X1) & s1 is convergent & lim s1 = r;
     dom f /\ X1 c= dom f /\ X by A1,XBOOLE_1:26; then
     dom (f|X1) c= dom f /\ X by RELAT_1:90; then
A7:  dom (f|X1) c= dom (f|X) by RELAT_1:90; then
A8:  rng s1 c= dom (f|X) by A6,XBOOLE_1:1; then
A9:  (f|X)*s1 is convergent &
     (f|X)/.r = lim ((f|X)*s1) by A4,A6,Def15;
A10: (f|X)/.r = f/.r by A5,A7,PARTFUN2:32
     .= (f|X1)/.r by A5,PARTFUN2:32;
     now let n;
       dom s1 = NAT by FUNCT_2:def 1; then
A11:   s1.n in rng s1 by FUNCT_1:12;
       thus ((f|X)*s1).n = (f|X)/.(s1.n) by A8,Th10
       .= f/.(s1.n) by A8,A11,PARTFUN2:32
       .= (f|X1)/.(s1.n) by A6,A11,PARTFUN2:32
       .= ((f|X1)*s1).n by A6,Th10;
     end;
     hence (f|X1)*s1 is convergent & (f|X1)/.r = lim ((f|X1)*s1)
     by A9,A10,FUNCT_2:113;
   end;
end;

theorem Th78:
for f be PartFunc of CNS,RNS st
 f is_continuous_on X & X1 c= X holds f is_continuous_on X1
proof
   let f be PartFunc of CNS,RNS;
   assume
A1: f is_continuous_on X & X1 c= X; then
   X c= dom f by Def22;
   hence
A2: X1 c= dom f by A1,XBOOLE_1:1;
   let r be Point of CNS;
   assume
A3: r in X1; then
A4:f|X is_continuous_in r by A1,Def22;
   thus f|X1 is_continuous_in r
   proof
     r in dom f /\ X1 by A2,A3,XBOOLE_0:def 3;
     hence
A5:   r in dom (f|X1) by RELAT_1:90;
     let s1 be sequence of CNS such that
A6:   rng s1 c= dom (f|X1) & s1 is convergent & lim s1 = r;
     dom f /\ X1 c= dom f /\ X by A1,XBOOLE_1:26; then
     dom (f|X1) c= dom f /\ X by RELAT_1:90; then
A7:  dom (f|X1) c= dom (f|X) by RELAT_1:90; then
A8:  rng s1 c= dom (f|X) by A6,XBOOLE_1:1; then
A9:  (f|X)*s1 is convergent &
     (f|X)/.r = lim ((f|X)*s1) by A4,A6,Def16;
A10: (f|X)/.r = f/.r by A5,A7,PARTFUN2:32
     .= (f|X1)/.r by A5,PARTFUN2:32;
     now let n;
       dom s1 = NAT by FUNCT_2:def 1; then
A11:   s1.n in rng s1 by FUNCT_1:12;
       thus ((f|X)*s1).n = (f|X)/.(s1.n) by A8,Th11
       .= f/.(s1.n) by A8,A11,PARTFUN2:32
       .= (f|X1)/.(s1.n) by A6,A11,PARTFUN2:32
       .= ((f|X1)*s1).n by A6,Th11;
     end;
     hence (f|X1)*s1 is convergent & (f|X1)/.r = lim ((f|X1)*s1)
     by A9,A10,FUNCT_2:113;
   end;
end;

theorem Th79:
for f be PartFunc of RNS,CNS st
 f is_continuous_on X & X1 c= X holds f is_continuous_on X1
proof
   let f be PartFunc of RNS,CNS;
   assume
A1: f is_continuous_on X & X1 c= X; then
   X c= dom f by Def23;
   hence
A2: X1 c= dom f by A1,XBOOLE_1:1;
   let r be Point of RNS;
   assume
A3: r in X1; then
A4:f|X is_continuous_in r by A1,Def23;
   thus f|X1 is_continuous_in r
   proof
     r in dom f /\ X1 by A2,A3,XBOOLE_0:def 3;
     hence
A5:   r in dom (f|X1) by RELAT_1:90;
     let s1 be sequence of RNS such that
A6:   rng s1 c= dom (f|X1) & s1 is convergent & lim s1 = r;
     dom f /\ X1 c= dom f /\ X by A1,XBOOLE_1:26; then
     dom (f|X1) c= dom f /\ X by RELAT_1:90; then
A7:  dom (f|X1) c= dom (f|X) by RELAT_1:90; then
A8:  rng s1 c= dom (f|X) by A6,XBOOLE_1:1; then
A9:  (f|X)*s1 is convergent &
     (f|X)/.r = lim ((f|X)*s1) by A4,A6,Def17;
A10: (f|X)/.r = f/.r by A5,A7,PARTFUN2:32
     .= (f|X1)/.r by A5,PARTFUN2:32;
     now let n;
       dom s1 = NAT by FUNCT_2:def 1; then
A11:   s1.n in rng s1 by FUNCT_1:12;
       thus ((f|X)*s1).n = (f|X)/.(s1.n) by A8,Th12
       .= f/.(s1.n) by A8,A11,PARTFUN2:32
       .= (f|X1)/.(s1.n) by A6,A11,PARTFUN2:32
       .= ((f|X1)*s1).n by A6,Th12;
     end;
     hence (f|X1)*s1 is convergent & (f|X1)/.r = lim ((f|X1)*s1)
     by A9,A10,FUNCT_2:113;
   end;
end;

theorem
for f be PartFunc of CNS1,CNS2, x0 be Point of CNS1 st
 x0 in dom f holds f is_continuous_on {x0}
proof
   let f be PartFunc of CNS1,CNS2;
   let x0 be Point of CNS1;
   assume
A1: x0 in dom f;
   thus {x0} c= dom f
   proof
     let x be set;
     assume x in {x0};
     hence x in dom f by A1,TARSKI:def 1;
   end;
   let p be Point of CNS1 such that
A2: p in {x0};
   thus f|{x0} is_continuous_in p
   proof
A3:  p=x0 by A2,TARSKI:def 1;
     p in dom f by A1,A2,TARSKI:def 1; then
     p in dom f /\ {x0} by A2,XBOOLE_0:def 3;
     hence p in dom (f|{x0}) by RELAT_1:90;
     let s1 be sequence of CNS1;
     assume
A4:   rng s1 c= dom(f|{x0}) & s1 is convergent & lim s1=p; then
A5:  rng s1 c= dom f /\ {x0} by RELAT_1:90;
     dom f /\ {x0} c= {x0} by XBOOLE_1:17; then
A6:  rng s1 c= {x0} by A5,XBOOLE_1:1;
A7:  now let n;
       dom s1 = NAT by FUNCT_2:def 1; then
       s1.n in rng s1 by FUNCT_1:12;
       hence s1.n = x0 by A6,TARSKI:def 1;
     end;
A8:  now
       let g be Real such that
A9:     0<g;
       take n=0;
       let m such that n<=m;
       ||. ((f|{x0})*s1).m - (f|{x0})/.p.||
        = ||. (f|{x0})/.(s1.m) - (f|{x0})/.x0 .|| by A3,A4,Th10
       .= ||. (f|{x0})/.x0 - (f|{x0})/.x0.|| by A7
       .= ||. 0.CNS2 .|| by RLVECT_1:28
       .= 0 by CLVECT_1:103;
       hence ||. ((f|{x0})*s1).m - (f|{x0})/.p .|| < g by A9;
     end;
     hence (f|{x0})*s1 is convergent by CLVECT_1:def 15;
     hence (f|{x0})/.p = lim ((f|{x0})*s1) by A8,CLVECT_1:def 17;
   end;
end;

theorem
for f be PartFunc of CNS,RNS, x0 be Point of CNS st
 x0 in dom f holds f is_continuous_on {x0}
proof
   let f be PartFunc of CNS,RNS;
   let x0 be Point of CNS;
   assume
A1: x0 in dom f;
   thus {x0} c= dom f
   proof
     let x be set;
     assume x in {x0};
     hence x in dom f by A1,TARSKI:def 1;
   end;
   let p be Point of CNS such that
A2: p in {x0};
   thus f|{x0} is_continuous_in p
   proof
A3:  p=x0 by A2,TARSKI:def 1;
     p in dom f by A1,A2,TARSKI:def 1; then
     p in dom f /\ {x0} by A2,XBOOLE_0:def 3;
     hence p in dom (f|{x0}) by RELAT_1:90;
     let s1 be sequence of CNS;
     assume
A4:   rng s1 c= dom(f|{x0}) & s1 is convergent & lim s1=p; then
A5:  rng s1 c= dom f /\ {x0} by RELAT_1:90;
     dom f /\ {x0} c= {x0} by XBOOLE_1:17; then
A6:  rng s1 c= {x0} by A5,XBOOLE_1:1;
A7:  now let n;
       dom s1 = NAT by FUNCT_2:def 1; then
       s1.n in rng s1 by FUNCT_1:12;
       hence s1.n = x0 by A6,TARSKI:def 1;
     end;
A8:  now
       let g be Real such that
A9:     0<g;
       take n=0;
       let m such that n<=m;
       ||. ((f|{x0})*s1).m - (f|{x0})/.p.||
        = ||. (f|{x0})/.(s1.m) - (f|{x0})/.x0 .|| by A3,A4,Th11
       .= ||. (f|{x0})/.x0 - (f|{x0})/.x0.|| by A7
       .= ||. 0.RNS .|| by RLVECT_1:28
       .= 0 by NORMSP_1:5;
       hence ||. ((f|{x0})*s1).m - (f|{x0})/.p .|| < g by A9;
     end;
     hence (f|{x0})*s1 is convergent by NORMSP_1:def 9;
     hence (f|{x0})/.p = lim ((f|{x0})*s1) by A8,NORMSP_1:def 11;
   end;
end;

theorem
for f be PartFunc of RNS,CNS, x0 be Point of RNS st
 x0 in dom f holds f is_continuous_on {x0}
proof
   let f be PartFunc of RNS,CNS;
   let x0 be Point of RNS;
   assume
A1: x0 in dom f;
   thus {x0} c= dom f
   proof
     let x be set;
     assume x in {x0};
     hence x in dom f by A1,TARSKI:def 1;
   end;
   let p be Point of RNS such that
A2: p in {x0};
   thus f|{x0} is_continuous_in p
   proof
A3:  p=x0 by A2,TARSKI:def 1;
     p in dom f by A1,A2,TARSKI:def 1; then
     p in dom f /\ {x0} by A2,XBOOLE_0:def 3;
     hence p in dom (f|{x0}) by RELAT_1:90;
     let s1 be sequence of RNS;
     assume
A4:   rng s1 c= dom(f|{x0}) & s1 is convergent & lim s1=p; then
A5:  rng s1 c= dom f /\ {x0} by RELAT_1:90;
     dom f /\ {x0} c= {x0} by XBOOLE_1:17; then
A6:  rng s1 c= {x0} by A5,XBOOLE_1:1;
A7:  now let n;
       dom s1 = NAT by FUNCT_2:def 1; then
       s1.n in rng s1 by FUNCT_1:12;
       hence s1.n = x0 by A6,TARSKI:def 1;
     end;
A8:  now
       let g be Real such that
A9:     0<g;
       take n=0;
       let m such that n<=m;
       ||. ((f|{x0})*s1).m - (f|{x0})/.p.||
        = ||. (f|{x0})/.(s1.m) - (f|{x0})/.x0 .|| by A3,A4,Th12
       .= ||. (f|{x0})/.x0 - (f|{x0})/.x0.|| by A7
       .= ||. 0.CNS .|| by RLVECT_1:28
       .= 0 by CLVECT_1:103;
       hence ||. ((f|{x0})*s1).m - (f|{x0})/.p .|| < g by A9;
     end;
     hence (f|{x0})*s1 is convergent by CLVECT_1:def 15;
     hence (f|{x0})/.p = lim ((f|{x0})*s1) by A8,CLVECT_1:def 17;
   end;
end;

theorem Th83:
for f1,f2 be PartFunc of CNS1,CNS2 st
 f1 is_continuous_on X & f2 is_continuous_on X holds
  f1+f2 is_continuous_on X & f1-f2 is_continuous_on X
proof
   let f1,f2 be PartFunc of CNS1,CNS2;
   assume that
A1: f1 is_continuous_on X and
A2: f2 is_continuous_on X;
A3:X c= dom f1 by A1,Th62;
   X c= dom f2 by A2,Th62; then
A4:X c= dom f1 /\ dom f2 by A3,XBOOLE_1:19; then
A5:X c= dom (f1+f2) by VFUNCT_2:def 1;
A6:X c= dom (f1-f2) by A4,VFUNCT_2:def 2;
   now let s1 be sequence of CNS1;
     assume
A7:   rng s1 c= X & s1 is convergent & lim s1 in X; then
A8:  rng s1 c= dom f1 /\ dom f2 by A4,XBOOLE_1:1;
A9: f1*s1 is convergent & f1/.(lim s1) = lim (f1*s1) by A1,A7,Th62;
A10: f2*s1 is convergent & f2/.(lim s1) = lim (f2*s1)
     by A2,A7,Th62; then
A11: (f1*s1)+(f2*s1) is convergent by A9,CLVECT_1:115;
     (f1+f2)/.(lim s1)
      = lim (f1*s1) + lim (f2*s1) by A5,A7,A9,A10,VFUNCT_2:def 1
     .= lim (f1*s1 + f2*s1) by A9,A10,CLVECT_1:121
     .= lim ((f1+f2)*s1) by A8,Th44;
     hence
     (f1+f2)*s1 is convergent &
     (f1+f2)/.(lim s1)=lim((f1+f2)*s1) by A8,A11,Th44;
   end;
   hence f1+f2 is_continuous_on X by A5,Th62;
   now let s1 be sequence of CNS1;
     assume
A12:  rng s1 c= X & s1 is convergent & lim s1 in X; then
A13: rng s1 c= dom f1 /\ dom f2 by A4,XBOOLE_1:1;
A14: f1*s1 is convergent & f1/.(lim s1) = lim (f1*s1) by A1,A12,Th62;
A15: f2*s1 is convergent & f2/.(lim s1) = lim (f2*s1)
     by A2,A12,Th62; then
A16: (f1*s1)-(f2*s1) is convergent by A14,CLVECT_1:116;
     (f1-f2)/.(lim s1)
      = lim (f1*s1) - lim (f2*s1) by A6,A12,A14,A15,VFUNCT_2:def 2
     .= lim (f1*s1 - f2*s1) by A14,A15,CLVECT_1:122
     .= lim ((f1-f2)*s1) by A13,Th44;
     hence
     (f1-f2)*s1 is convergent &
     (f1-f2)/.(lim s1)=lim((f1-f2)*s1) by A13,A16,Th44;
   end;
   hence f1-f2 is_continuous_on X by A6,Th62;
end;

theorem Th84:
for f1,f2 be PartFunc of CNS,RNS st
 f1 is_continuous_on X & f2 is_continuous_on X holds
  f1+f2 is_continuous_on X & f1-f2 is_continuous_on X
proof
   let f1,f2 be PartFunc of CNS,RNS;
   assume that
A1: f1 is_continuous_on X and
A2: f2 is_continuous_on X;
A3:X c= dom f1 by A1,Th63;
   X c= dom f2 by A2,Th63; then
A4:X c= dom f1 /\ dom f2 by A3,XBOOLE_1:19; then
A5:X c= dom (f1+f2) by VFUNCT_1:def 1;
A6:X c= dom (f1-f2) by A4,VFUNCT_1:def 2;
   now let s1 be sequence of CNS;
     assume
A7:   rng s1 c= X & s1 is convergent & lim s1 in X; then
A8:  rng s1 c= dom f1 /\ dom f2 by A4,XBOOLE_1:1;
A9: f1*s1 is convergent & f1/.(lim s1) = lim (f1*s1) by A1,A7,Th63;
A10: f2*s1 is convergent & f2/.(lim s1) = lim (f2*s1)
     by A2,A7,Th63; then
A11: (f1*s1)+(f2*s1) is convergent by A9,NORMSP_1:34;
     (f1+f2)/.(lim s1)
      = lim (f1*s1) + lim (f2*s1) by A5,A7,A9,A10,VFUNCT_1:def 1
     .= lim (f1*s1 + f2*s1) by A9,A10,NORMSP_1:42
     .= lim ((f1+f2)*s1) by A8,Th45;
     hence
     (f1+f2)*s1 is convergent &
     (f1+f2)/.(lim s1)=lim((f1+f2)*s1) by A8,A11,Th45;
   end;
   hence f1+f2 is_continuous_on X by A5,Th63;
   now let s1 be sequence of CNS;
     assume
A12:  rng s1 c= X & s1 is convergent & lim s1 in X; then
A13: rng s1 c= dom f1 /\ dom f2 by A4,XBOOLE_1:1;
A14: f1*s1 is convergent & f1/.(lim s1) = lim (f1*s1) by A1,A12,Th63;
A15: f2*s1 is convergent & f2/.(lim s1) = lim (f2*s1)
     by A2,A12,Th63; then
A16: (f1*s1)-(f2*s1) is convergent by A14,NORMSP_1:35;
     (f1-f2)/.(lim s1)
      = lim (f1*s1) - lim (f2*s1) by A6,A12,A14,A15,VFUNCT_1:def 2
     .= lim (f1*s1 - f2*s1) by A14,A15,NORMSP_1:43
     .= lim ((f1-f2)*s1) by A13,Th45;
     hence
     (f1-f2)*s1 is convergent &
     (f1-f2)/.(lim s1)=lim((f1-f2)*s1) by A13,A16,Th45;
   end;
   hence f1-f2 is_continuous_on X by A6,Th63;
end;

theorem Th85:
for f1,f2 be PartFunc of RNS,CNS st
 f1 is_continuous_on X & f2 is_continuous_on X holds
  f1+f2 is_continuous_on X & f1-f2 is_continuous_on X
proof
   let f1,f2 be PartFunc of RNS,CNS;
   assume that
A1: f1 is_continuous_on X and
A2: f2 is_continuous_on X;
A3:X c= dom f1 by A1,Th64;
   X c= dom f2 by A2,Th64; then
A4:X c= dom f1 /\ dom f2 by A3,XBOOLE_1:19; then
A5:X c= dom (f1+f2) by VFUNCT_2:def 1;
A6:X c= dom (f1-f2) by A4,VFUNCT_2:def 2;
   now let s1 be sequence of RNS;
     assume
A7:   rng s1 c= X & s1 is convergent & lim s1 in X; then
A8:  rng s1 c= dom f1 /\ dom f2 by A4,XBOOLE_1:1;
A9: f1*s1 is convergent & f1/.(lim s1) = lim (f1*s1) by A1,A7,Th64;
A10: f2*s1 is convergent & f2/.(lim s1) = lim (f2*s1)
     by A2,A7,Th64; then
A11: (f1*s1)+(f2*s1) is convergent by A9,CLVECT_1:115;
     (f1+f2)/.(lim s1)
      = lim (f1*s1) + lim (f2*s1) by A5,A7,A9,A10,VFUNCT_2:def 1
     .= lim (f1*s1 + f2*s1) by A9,A10,CLVECT_1:121
     .= lim ((f1+f2)*s1) by A8,Th46;
     hence
     (f1+f2)*s1 is convergent &
     (f1+f2)/.(lim s1)=lim((f1+f2)*s1) by A8,A11,Th46;
   end;
   hence f1+f2 is_continuous_on X by A5,Th64;
   now let s1 be sequence of RNS;
     assume
A12:  rng s1 c= X & s1 is convergent & lim s1 in X; then
A13: rng s1 c= dom f1 /\ dom f2 by A4,XBOOLE_1:1;
A14: f1*s1 is convergent & f1/.(lim s1) = lim (f1*s1) by A1,A12,Th64;
A15: f2*s1 is convergent & f2/.(lim s1) = lim (f2*s1)
     by A2,A12,Th64; then
A16: (f1*s1)-(f2*s1) is convergent by A14,CLVECT_1:116;
     (f1-f2)/.(lim s1)
      = lim (f1*s1) - lim (f2*s1) by A6,A12,A14,A15,VFUNCT_2:def 2
     .= lim (f1*s1 - f2*s1) by A14,A15,CLVECT_1:122
     .= lim ((f1-f2)*s1) by A13,Th46;
     hence
     (f1-f2)*s1 is convergent &
     (f1-f2)/.(lim s1)=lim((f1-f2)*s1) by A13,A16,Th46;
   end;
   hence f1-f2 is_continuous_on X by A6,Th64;
end;

theorem
for f1,f2 be PartFunc of CNS1,CNS2 st
 f1 is_continuous_on X & f2 is_continuous_on X1 holds
  f1+f2 is_continuous_on X /\ X1 & f1-f2 is_continuous_on X /\ X1
proof
   let f1,f2 be PartFunc of CNS1,CNS2;
   assume
A1: f1 is_continuous_on X & f2 is_continuous_on X1;
   X /\ X1 c= X & X /\ X1 c= X1 by XBOOLE_1:17; then
   f1 is_continuous_on X /\ X1 &
   f2 is_continuous_on X /\ X1 by A1,Th77;
   hence thesis by Th83;
end;

theorem
for f1,f2 be PartFunc of CNS,RNS st
 f1 is_continuous_on X & f2 is_continuous_on X1 holds
  f1+f2 is_continuous_on X /\ X1 & f1-f2 is_continuous_on X /\ X1
proof
   let f1,f2 be PartFunc of CNS,RNS;
   assume
A1: f1 is_continuous_on X & f2 is_continuous_on X1;
   X /\ X1 c= X & X /\ X1 c= X1 by XBOOLE_1:17; then
   f1 is_continuous_on X /\ X1 &
   f2 is_continuous_on X /\ X1 by A1,Th78;
   hence thesis by Th84;
end;

theorem
for f1,f2 be PartFunc of RNS,CNS st
 f1 is_continuous_on X & f2 is_continuous_on X1 holds
  f1+f2 is_continuous_on X /\ X1 & f1-f2 is_continuous_on X /\ X1
proof
   let f1,f2 be PartFunc of RNS,CNS;
   assume
A1: f1 is_continuous_on X & f2 is_continuous_on X1;
   X /\ X1 c= X & X /\ X1 c= X1 by XBOOLE_1:17; then
   f1 is_continuous_on X /\ X1 &
   f2 is_continuous_on X /\ X1 by A1,Th79;
   hence thesis by Th85;
end;

theorem Th89:
for f be PartFunc of CNS1,CNS2 st
 f is_continuous_on X holds z(#)f is_continuous_on X
proof
   let f be PartFunc of CNS1,CNS2;
   assume
A1: f is_continuous_on X; then
A2:X c= dom f by Th62; then
A3:X c= dom(z(#)f) by VFUNCT_2:def 4;
   now let s1 be sequence of CNS1;
     assume
A4:  rng s1 c= X & s1 is convergent & lim s1 in X; then
A5:  rng s1 c= dom f by A2,XBOOLE_1:1;
A6:  f*s1 is convergent & f/.(lim s1) = lim (f*s1) by A1,A4,Th62; then
A7:  z*(f*s1) is convergent by CLVECT_1:118;
     (z(#)f)/.(lim s1) = z * lim (f*s1) by A3,A4,A6,VFUNCT_2:def 4
           .= lim (z*(f*s1)) by A6,CLVECT_1:124
           .= lim ((z(#)f)*s1) by A5,Th47;
     hence
     (z(#)f)*s1 is convergent &
     (z(#)f)/.(lim s1)=lim((z(#)f)*s1) by A5,A7,Th47;
   end;
   hence thesis by A3,Th62;
end;

theorem Th90:
for f be PartFunc of CNS,RNS st
 f is_continuous_on X holds r(#)f is_continuous_on X
proof
   let f be PartFunc of CNS,RNS;
   assume
A1: f is_continuous_on X; then
A2:X c= dom f by Th63; then
A3:X c= dom(r(#)f) by VFUNCT_1:def 4;
   now let s1 be sequence of CNS;
     assume
A4:  rng s1 c= X & s1 is convergent & lim s1 in X; then
A5:  rng s1 c= dom f by A2,XBOOLE_1:1;
A6:  f*s1 is convergent & f/.(lim s1) = lim (f*s1) by A1,A4,Th63; then
A7:  r*(f*s1) is convergent by NORMSP_1:37;
     (r(#)f)/.(lim s1) = r * lim (f*s1) by A3,A4,A6,VFUNCT_1:def 4
           .= lim (r*(f*s1)) by A6,NORMSP_1:45
           .= lim ((r(#)f)*s1) by A5,Th48;
     hence (r(#)f)*s1 is convergent &
     (r(#)f)/.(lim s1)=lim((r(#)f)*s1) by A5,A7,Th48;
   end;
   hence thesis by A3,Th63;
end;

theorem Th91:
for f be PartFunc of RNS,CNS st
 f is_continuous_on X holds z(#)f is_continuous_on X
proof
   let f be PartFunc of RNS,CNS;
   assume
A1: f is_continuous_on X; then
A2:X c= dom f by Th64; then
A3:X c= dom(z(#)f) by VFUNCT_2:def 4;
   now let s1 be sequence of RNS;
     assume
A4:  rng s1 c= X & s1 is convergent & lim s1 in X; then
A5:  rng s1 c= dom f by A2,XBOOLE_1:1;
A6:  f*s1 is convergent & f/.(lim s1) = lim (f*s1) by A1,A4,Th64; then
A7:  z*(f*s1) is convergent by CLVECT_1:118;
     (z(#)f)/.(lim s1) = z * lim (f*s1) by A3,A4,A6,VFUNCT_2:def 4
           .= lim (z*(f*s1)) by A6,CLVECT_1:124
           .= lim ((z(#)f)*s1) by A5,Th49;
     hence
     (z(#)f)*s1 is convergent &
     (z(#)f)/.(lim s1)=lim((z(#)f)*s1) by A5,A7,Th49;
   end;
   hence thesis by A3,Th64;
end;

theorem Th92:
for f be PartFunc of CNS1,CNS2 st f is_continuous_on X holds
  ||.f.|| is_continuous_on X & -f is_continuous_on X
proof
   let f be PartFunc of CNS1,CNS2;
   assume
A1: f is_continuous_on X;
   thus ||.f.|| is_continuous_on X
   proof
A2: X c= dom f by A1,Def21;
     hence
A3:  X c= dom (||.f.|| ) by Def2;
     let r be Point of CNS1;
     assume
A4:   r in X; then
A5:  f|X is_continuous_in r by A1,Def21;
     thus (||.f.|| )|X is_continuous_in r
     proof
A6:    r in dom (||.f.|| ) /\ X by A3,A4,XBOOLE_0:def 3;
       hence r in dom ((||.f.|| )|X) by RELAT_1:90;
       let s1 be sequence of CNS1;
       assume
A7:     rng s1 c= dom ((||.f.|| )|X) & s1 is convergent & lim s1 = r; then
       rng s1 c= dom (||.f.|| ) /\ X by RELAT_1:90; then
       rng s1 c= dom f /\ X by Def2; then
A8:    rng s1 c= dom (f|X) by RELAT_1:90; then
A9:    (f|X)*s1 is convergent &
       (f|X)/.r = lim ((f|X)*s1) by A5,A7,Def15;
       now let n;
         dom s1 = NAT by FUNCT_2:def 1; then
A10:     s1.n in rng s1 by FUNCT_1:12; then
         s1.n in dom (f|X) by A8; then
         s1.n in dom f /\ X by RELAT_1:90; then
A11:     s1.n in dom f & s1.n in X by XBOOLE_0:def 3; then
A12:     s1.n in dom (||.f.|| ) & s1.n in X by Def2;
         thus (||.(f|X)*s1.||).n
          = ||. ((f|X)*s1).n .|| by CLVECT_1:def 16
         .=||.(f|X)/.(s1.n).|| by A8,Th10
         .=||.f/.(s1.n).|| by A8,A10,PARTFUN2:32
         .=(||.f.|| ).(s1.n) by A12,Def2
         .=((||.f.|| )|X).(s1.n) by A11,FUNCT_1:72
         .=((||.f.|| )|X)/.(s1.n) by A7,A10,FINSEQ_4:def 4
         .=(((||.f.|| )|X)*s1).n by A7,Th14;
       end; then
A13:   ||.(f|X)*s1.|| = ((||.f.|| )|X)*s1 by FUNCT_2:113;
       hence ((||.f.|| )|X)*s1 is convergent by A9,CLVECT_1:119;
       ||.(f|X)/.r.|| = ||.f/.r.|| by A2,A4,PARTFUN2:35
       .= (||.f.|| ).r by A3,A4,Def2
       .= (||.f.|| )/.r by A3,A4,FINSEQ_4:def 4
       .= ((||.f.|| )|X)/.r by A6,PARTFUN2:34;
       hence ((||.f.|| )|X)/.r = lim (((||.f.|| )|X)*s1) by A9,A13,CLOPBAN1:22;
     end;
   end;
   (-1r)(#)f is_continuous_on X by A1,Th89;
   hence thesis by VFUNCT_2:23;
end;

theorem Th93:
for f be PartFunc of CNS,RNS st f is_continuous_on X holds
  ||.f.|| is_continuous_on X & -f is_continuous_on X
proof
   let f be PartFunc of CNS,RNS;
   assume
A1: f is_continuous_on X;
   thus ||.f.|| is_continuous_on X
   proof
A2: X c= dom f by A1,Def22;
     hence
A3:  X c= dom (||.f.|| ) by Def3;
     let r be Point of CNS;
     assume
A4:   r in X; then
A5:  f|X is_continuous_in r by A1,Def22;
     thus (||.f.|| )|X is_continuous_in r
     proof
A6:    r in dom (||.f.|| ) /\ X by A3,A4,XBOOLE_0:def 3;
       hence r in dom ((||.f.|| )|X) by RELAT_1:90;
       let s1 be sequence of CNS;
       assume
A7:     rng s1 c= dom ((||.f.|| )|X) & s1 is convergent & lim s1 = r; then
       rng s1 c= dom (||.f.|| ) /\ X by RELAT_1:90; then
       rng s1 c= dom f /\ X by Def3; then
A8:    rng s1 c= dom (f|X) by RELAT_1:90; then
A9:    (f|X)*s1 is convergent &
       (f|X)/.r = lim ((f|X)*s1) by A5,A7,Def16;
       now let n;
         dom s1 = NAT by FUNCT_2:def 1; then
A10:     s1.n in rng s1 by FUNCT_1:12; then
         s1.n in dom (f|X) by A8; then
         s1.n in dom f /\ X by RELAT_1:90; then
A11:     s1.n in dom f & s1.n in X by XBOOLE_0:def 3; then
A12:     s1.n in dom (||.f.|| ) & s1.n in X by Def3;
         thus (||.(f|X)*s1.||).n
          = ||. ((f|X)*s1).n .|| by NORMSP_1:def 10
         .=||.(f|X)/.(s1.n).|| by A8,Th11
         .=||.f/.(s1.n).|| by A8,A10,PARTFUN2:32
         .=(||.f.|| ).(s1.n) by A12,Def3
         .=((||.f.|| )|X).(s1.n) by A11,FUNCT_1:72
         .=((||.f.|| )|X)/.(s1.n) by A7,A10,FINSEQ_4:def 4
         .=(((||.f.|| )|X)*s1).n by A7,Th14;
       end; then
A13:   ||.(f|X)*s1.|| = ((||.f.|| )|X)*s1 by FUNCT_2:113;
       hence ((||.f.|| )|X)*s1 is convergent by A9,NORMSP_1:39;
       ||.(f|X)/.r.|| = ||.f/.r.|| by A2,A4,PARTFUN2:35
       .= (||.f.|| ).r by A3,A4,Def3
       .= (||.f.|| )/.r by A3,A4,FINSEQ_4:def 4
       .= ((||.f.|| )|X)/.r by A6,PARTFUN2:34;
       hence ((||.f.|| )|X)/.r = lim (((||.f.|| )|X)*s1) by A9,A13,LOPBAN_1:24;
     end;
   end;
   (-1)(#)f is_continuous_on X by A1,Th90;
   hence thesis by VFUNCT_1:29;
end;

theorem Th94:
for f be PartFunc of RNS,CNS st f is_continuous_on X holds
  ||.f.|| is_continuous_on X & -f is_continuous_on X
proof
   let f be PartFunc of RNS,CNS;
   assume
A1: f is_continuous_on X;
   thus ||.f.|| is_continuous_on X
   proof
A2: X c= dom f by A1,Def23;
     hence
A3:  X c= dom (||.f.|| ) by Def4;
     let r be Point of RNS;
     assume
A4:   r in X; then
A5:  f|X is_continuous_in r by A1,Def23;
     thus (||.f.|| )|X is_continuous_in r
     proof
A6:    r in dom (||.f.|| ) /\ X by A3,A4,XBOOLE_0:def 3;
       hence r in dom ((||.f.|| )|X) by RELAT_1:90;
       let s1 be sequence of RNS;
       assume
A7:     rng s1 c= dom ((||.f.|| )|X) & s1 is convergent & lim s1 = r; then
       rng s1 c= dom (||.f.|| ) /\ X by RELAT_1:90; then
       rng s1 c= dom f /\ X by Def4; then
A8:    rng s1 c= dom (f|X) by RELAT_1:90; then
A9:    (f|X)*s1 is convergent &
       (f|X)/.r = lim ((f|X)*s1) by A5,A7,Def17;
       now let n;
         dom s1 = NAT by FUNCT_2:def 1; then
A10:     s1.n in rng s1 by FUNCT_1:12; then
         s1.n in dom (f|X) by A8; then
         s1.n in dom f /\ X by RELAT_1:90; then
A11:     s1.n in dom f & s1.n in X by XBOOLE_0:def 3; then
A12:     s1.n in dom (||.f.|| ) & s1.n in X by Def4;
         thus (||.(f|X)*s1.||).n
          = ||. ((f|X)*s1).n .|| by CLVECT_1:def 16
         .=||.(f|X)/.(s1.n).|| by A8,Th12
         .=||.f/.(s1.n).|| by A8,A10,PARTFUN2:32
         .=(||.f.|| ).(s1.n) by A12,Def4
         .=((||.f.|| )|X).(s1.n) by A11,FUNCT_1:72
         .=((||.f.|| )|X)/.(s1.n) by A7,A10,FINSEQ_4:def 4
         .=(((||.f.|| )|X)*s1).n by A7,NFCONT_1:9;
       end; then
A13:   ||.(f|X)*s1.|| = ((||.f.|| )|X)*s1 by FUNCT_2:113;
       hence ((||.f.|| )|X)*s1 is convergent by A9,CLVECT_1:119;
       ||.(f|X)/.r.|| = ||.f/.r.|| by A2,A4,PARTFUN2:35
       .= (||.f.|| ).r by A3,A4,Def4
       .= (||.f.|| )/.r by A3,A4,FINSEQ_4:def 4
       .= ((||.f.|| )|X)/.r by A6,PARTFUN2:34;
       hence ((||.f.|| )|X)/.r = lim (((||.f.|| )|X)*s1) by A9,A13,CLOPBAN1:22;
     end;
   end;
   (-1r)(#)f is_continuous_on X by A1,Th91;
   hence thesis by VFUNCT_2:23;
end;

theorem
for f be PartFunc of CNS1,CNS2 st
 f is total & (for x1,x2 be Point of CNS1 holds f/.(x1+x2) = f/.x1 + f/.x2) &
 (ex x0 be Point of CNS1 st f is_continuous_in x0) holds
  f is_continuous_on the carrier of CNS1
proof
   let f be PartFunc of CNS1,CNS2;
   assume that
A1: f is total and
A2: for x1,x2 be Point of CNS1 holds f/.(x1+x2) = f/.x1 + f/.x2;
   given x0 be Point of CNS1 such that
A3: f is_continuous_in x0;
A4:dom f = the carrier of CNS1 by A1,PARTFUN1:def 4;
   f/.x0 + 0.CNS2 = f/.x0 by RLVECT_1:10
   .= f/.(x0+0.CNS1) by RLVECT_1:10
   .= f/.x0+f/.0.CNS1 by A2; then
A5:f/.0.CNS1 = 0.CNS2 by RLVECT_1:21;
A6:now let x1 be Point of CNS1;
     0.CNS2 = f/.(x1+-x1) by A5,RLVECT_1:16
     .= f/.x1+f/.(-x1) by A2;
     hence -(f/.x1)=f/.(-x1) by RLVECT_1:19;
   end;
A7:now let x1,x2 be Point of CNS1;
     thus f/.(x1-x2)=f/.(x1+-x2) by RLVECT_1:def 11
     .= f/.x1 + f/.(-x2) by A2
     .= f/.x1 +- f/.x2 by A6
     .= f/.x1 - f/.x2 by RLVECT_1:def 11;
   end;
   now let x1 be Point of CNS1;
     let r be Real;
     assume x1 in the carrier of CNS1 & r>0; then
     consider s such that
A8:   0<s & for x1 be Point of CNS1 st x1 in dom f & ||. x1- x0 .|| <s holds
       ||. f/.x1- f/.x0 .|| <r by A3,Th29;
     take s;
     thus s>0 by A8;
     let x2 be Point of CNS1 such that
A9:   x2 in the carrier of CNS1 & ||. x2-x1 .||<s;
     set y=x1-x0;
A10: y+x0 =x1-(x0-x0) by RLVECT_1:43
         .=x1- 0.CNS1 by RLVECT_1:28
         .=x1 by RLVECT_1:26; then
A11: ||.f/.x2-f/.x1.|| = ||.f/.x2-(f/.y+f/.x0).|| by A2
     .= ||.f/.x2-f/.y-f/.x0 .|| by RLVECT_1:41
     .= ||.f/.(x2-y)-f/.x0.|| by A7;
     ||.x2-y-x0.||=||.x2-x1.|| by A10,RLVECT_1:41;
     hence ||.f/.x2-f/.x1.||<r by A4,A8,A9,A11;
   end;
   hence thesis by A4,Th65;
end;

theorem
for f be PartFunc of CNS,RNS st
 f is total & (for x1,x2 be Point of CNS holds f/.(x1+x2) = f/.x1 + f/.x2) &
 (ex x0 be Point of CNS st f is_continuous_in x0) holds
  f is_continuous_on the carrier of CNS
proof
   let f be PartFunc of CNS,RNS;
   assume that
A1: f is total and
A2: for x1,x2 be Point of CNS holds f/.(x1+x2) = f/.x1 + f/.x2;
   given x0 be Point of CNS such that
A3: f is_continuous_in x0;
A4:dom f = the carrier of CNS by A1,PARTFUN1:def 4;
   f/.x0 + 0.RNS = f/.x0 by RLVECT_1:10
   .= f/.(x0+0.CNS) by RLVECT_1:10
   .= f/.x0+f/.0.CNS by A2; then
A5:f/.0.CNS = 0.RNS by RLVECT_1:21;
A6:now let x1 be Point of CNS;
     0.RNS = f/.(x1+-x1) by A5,RLVECT_1:16
     .= f/.x1+f/.(-x1) by A2;
     hence -(f/.x1)=f/.(-x1) by RLVECT_1:19;
   end;
A7:now let x1,x2 be Point of CNS;
     thus f/.(x1-x2)=f/.(x1+-x2) by RLVECT_1:def 11
     .= f/.x1 + f/.(-x2) by A2
     .= f/.x1 +- f/.x2 by A6
     .= f/.x1 - f/.x2 by RLVECT_1:def 11;
   end;
   now let x1 be Point of CNS;
     let r be Real;
     assume x1 in the carrier of CNS & r>0; then
     consider s such that
A8:   0<s & for x1 be Point of CNS st x1 in dom f & ||. x1- x0 .|| <s holds
       ||. f/.x1- f/.x0 .|| <r by A3,Th30;
     take s;
     thus s>0 by A8;
     let x2 be Point of CNS such that
A9:   x2 in the carrier of CNS & ||. x2-x1 .||<s;
     set y=x1-x0;
A10: y+x0 =x1-(x0-x0) by RLVECT_1:43
         .=x1- 0.CNS by RLVECT_1:28
         .=x1 by RLVECT_1:26; then
A11: ||.f/.x2-f/.x1.|| = ||.f/.x2-(f/.y+f/.x0).|| by A2
     .= ||.f/.x2-f/.y-f/.x0 .|| by RLVECT_1:41
     .= ||.f/.(x2-y)-f/.x0.|| by A7;
     ||.x2-y-x0.||=||.x2-x1.|| by A10,RLVECT_1:41;
     hence ||.f/.x2-f/.x1.||<r by A4,A8,A9,A11;
   end;
   hence thesis by A4,Th66;
end;

theorem
for f be PartFunc of RNS,CNS st
 f is total & (for x1,x2 be Point of RNS holds f/.(x1+x2) = f/.x1 + f/.x2) &
 (ex x0 be Point of RNS st f is_continuous_in x0) holds
  f is_continuous_on the carrier of RNS
proof
   let f be PartFunc of RNS,CNS;
   assume that
A1: f is total and
A2: for x1,x2 be Point of RNS holds f/.(x1+x2) = f/.x1 + f/.x2;
   given x0 be Point of RNS such that
A3: f is_continuous_in x0;
A4:dom f = the carrier of RNS by A1,PARTFUN1:def 4;
   f/.x0 + 0.CNS = f/.x0 by RLVECT_1:10
   .= f/.(x0+0.RNS) by RLVECT_1:10
   .= f/.x0+f/.0.RNS by A2; then
A5:f/.0.RNS = 0.CNS by RLVECT_1:21;
A6:now let x1 be Point of RNS;
     0.CNS = f/.(x1+-x1) by A5,RLVECT_1:16
     .= f/.x1+f/.(-x1) by A2;
     hence -(f/.x1)=f/.(-x1) by RLVECT_1:19;
   end;
A7:now let x1,x2 be Point of RNS;
     thus f/.(x1-x2)=f/.(x1+-x2) by RLVECT_1:def 11
     .= f/.x1 + f/.(-x2) by A2
     .= f/.x1 +- f/.x2 by A6
     .= f/.x1 - f/.x2 by RLVECT_1:def 11;
   end;
   now let x1 be Point of RNS;
     let r be Real;
     assume x1 in the carrier of RNS & r>0; then
     consider s such that
A8:   0<s & for x1 be Point of RNS st x1 in dom f & ||. x1- x0 .|| <s holds
       ||. f/.x1- f/.x0 .|| <r by A3,Th31;
     take s;
     thus s>0 by A8;
     let x2 be Point of RNS such that
A9:   x2 in the carrier of RNS & ||. x2-x1 .||<s;
     set y=x1-x0;
A10: y+x0 =x1-(x0-x0) by RLVECT_1:43
         .=x1- 0.RNS by RLVECT_1:28
         .=x1 by RLVECT_1:26; then
A11: ||.f/.x2-f/.x1.|| = ||.f/.x2-(f/.y+f/.x0).|| by A2
     .= ||.f/.x2-f/.y-f/.x0 .|| by RLVECT_1:41
     .= ||.f/.(x2-y)-f/.x0.|| by A7;
     ||.x2-y-x0.||=||.x2-x1.|| by A10,RLVECT_1:41;
     hence ||.f/.x2-f/.x1.||<r by A4,A8,A9,A11;
   end;
   hence thesis by A4,Th67;
end;

theorem Th98:
for f be PartFunc of CNS1,CNS2 st
 dom f is compact & f is_continuous_on (dom f) holds (rng f) is compact
proof
   let f be PartFunc of CNS1,CNS2;
   assume that
A1: dom f is compact and
A2: f is_continuous_on (dom f);
   now
     let s1 be sequence of CNS2 such that
A3:   rng s1 c= rng f;
     defpred P[set,set] means $2 in dom f & f/.$2=s1.$1;
A4:  for n ex p be Point of CNS1 st P[n,p]
     proof
       let n;
       dom s1 = NAT by FUNCT_2:def 1; then
       s1.n in rng s1 by FUNCT_1:12; then
       consider p be Point of CNS1 such that
A5:     p in dom f & s1.n = f.p by A3,PARTFUN1:26;
       take p;
       thus thesis by A5,FINSEQ_4:def 4;
     end;
     consider q1 be sequence of CNS1 such that
A6:   for n holds P[n,q1.n]from FUNCT_2:sch 3(A4);
     now let x be set;
       assume x in rng q1; then
       ex n st x = q1.n by Th8;
       hence x in dom f by A6;
     end; then
A7:  rng q1 c= dom f by TARSKI:def 3;
     now let n;
       q1.n in dom f & f/.(q1.n)=s1.n by A6;
       hence (f*q1).n= s1.n by A7,Th10;
     end; then
A8:  f*q1=s1 by FUNCT_2:113;
     consider s2 be sequence of CNS1 such that
A9:   s2 is_subsequence_of q1 & s2 is convergent & (lim s2) in dom f
      by A1,A7,Def6;
     f|(dom f) is_continuous_in (lim s2) by A2,A9,Def21; then
A10: f is_continuous_in (lim s2) by RELAT_1:97;
     rng s2 c= rng q1 by A9,Th9; then
     rng s2 c= dom f by A7,XBOOLE_1:1; then
A11: f*s2 is convergent &
     f/.(lim s2) = lim (f*s2) by A9,A10,Def15;
     take q2 = f*s2;
     thus q2 is_subsequence_of s1 & q2 is convergent & (lim q2) in rng f
     by A7,A8,A9,A11,Th22,PARTFUN2:4;
   end;
   hence thesis by Def6;
end;

theorem Th99:
for f be PartFunc of CNS,RNS st
 dom f is compact & f is_continuous_on (dom f) holds (rng f) is compact
proof
   let f be PartFunc of CNS,RNS;
   assume that
A1: dom f is compact and
A2: f is_continuous_on (dom f);
   now
     let s1 be sequence of RNS such that
A3:   rng s1 c= rng f;
     defpred P[set,set] means $2 in dom f & f/.$2=s1.$1;
A4:  for n ex p be Point of CNS st P[n,p]
     proof
       let n;
       dom s1 = NAT by FUNCT_2:def 1; then
       s1.n in rng s1 by FUNCT_1:12; then
       consider p be Point of CNS such that
A5:     p in dom f & s1.n = f.p by A3,PARTFUN1:26;
       take p;
       thus thesis by A5,FINSEQ_4:def 4;
     end;
     consider q1 be sequence of CNS such that
A6:   for n holds P[n,q1.n]from FUNCT_2:sch 3(A4);
     now let x be set;
       assume x in rng q1; then
       ex n st x = q1.n by Th8;
       hence x in dom f by A6;
     end; then
A7:  rng q1 c= dom f by TARSKI:def 3;
     now let n;
       q1.n in dom f & f/.(q1.n)=s1.n by A6;
       hence (f*q1).n= s1.n by A7,Th11;
     end; then
A8:  f*q1=s1 by FUNCT_2:113;
     consider s2 be sequence of CNS such that
A9:   s2 is_subsequence_of q1 & s2 is convergent & (lim s2) in dom f
      by A1,A7,Def6;
     f|(dom f) is_continuous_in (lim s2) by A2,A9,Def22; then
A10: f is_continuous_in (lim s2) by RELAT_1:97;
     rng s2 c= rng q1 by A9,Th9; then
     rng s2 c= dom f by A7,XBOOLE_1:1; then
A11: f*s2 is convergent &
     f/.(lim s2) = lim (f*s2) by A9,A10,Def16;
     take q2 = f*s2;
     thus q2 is_subsequence_of s1 & q2 is convergent & (lim q2) in rng f
     by A7,A8,A9,A11,Th23,PARTFUN2:4;
   end;
   hence thesis by NFCONT_1:def 4;
end;

theorem Th100:
for f be PartFunc of RNS,CNS st
 dom f is compact & f is_continuous_on (dom f) holds (rng f) is compact
proof
   let f be PartFunc of RNS,CNS;
   assume that
A1: dom f is compact and
A2: f is_continuous_on (dom f);
   now
     let s1 be sequence of CNS such that
A3:   rng s1 c= rng f;
     defpred P[set,set] means $2 in dom f & f/.$2=s1.$1;
A4:  for n ex p be Point of RNS st P[n,p]
     proof
       let n;
       dom s1 = NAT by FUNCT_2:def 1; then
       s1.n in rng s1 by FUNCT_1:12; then
       consider p be Point of RNS such that
A5:     p in dom f & s1.n = f.p by A3,PARTFUN1:26;
       take p;
       thus thesis by A5,FINSEQ_4:def 4;
     end;
     consider q1 be sequence of RNS such that
A6:   for n holds P[n,q1.n]from FUNCT_2:sch 3(A4);
     now let x be set;
       assume x in rng q1; then
       ex n st x = q1.n by NFCONT_1:6;
       hence x in dom f by A6;
     end; then
A7:  rng q1 c= dom f by TARSKI:def 3;
     now let n;
       q1.n in dom f & f/.(q1.n)=s1.n by A6;
       hence (f*q1).n= s1.n by A7,Th12;
     end; then
A8:  f*q1=s1 by FUNCT_2:113;
     consider s2 be sequence of RNS such that
A9:   s2 is_subsequence_of q1 & s2 is convergent & (lim s2) in dom f
      by A1,A7,NFCONT_1:def 4;
     f|(dom f) is_continuous_in (lim s2) by A2,A9,Def23; then
A10: f is_continuous_in (lim s2) by RELAT_1:97;
     rng s2 c= rng q1 by A9,NFCONT_1:7; then
     rng s2 c= dom f by A7,XBOOLE_1:1; then
A11: f*s2 is convergent &
     f/.(lim s2) = lim (f*s2) by A9,A10,Def17;
     take q2 = f*s2;
     thus q2 is_subsequence_of s1 & q2 is convergent & (lim q2) in rng f
     by A7,A8,A9,A11,Th24,PARTFUN2:4;
   end;
   hence thesis by Def6;
end;

theorem
for f be PartFunc of the carrier of CNS,COMPLEX st
  dom f is compact & f is_continuous_on (dom f) holds
    (rng f) is compact
proof
   let f be PartFunc of the carrier of CNS,COMPLEX;
   assume that
A1: dom f is compact and
A2: f is_continuous_on (dom f);
   now
     let s1 be Complex_Sequence such that
A3:   rng s1 c= rng f;
     defpred P[set,set] means $2 in dom f & f/.$2=s1.$1;
A4:  for n ex p be Point of CNS st P[n,p]
     proof let n;
       dom s1 = NAT by FUNCT_2:def 1; then
       s1.n in rng s1 by FUNCT_1:12; then
       consider p be  Point of CNS such that
A5:     p in dom f & s1.n = f.p by A3,PARTFUN1:26;
       take p;
       thus thesis by A5,FINSEQ_4:def 4;
     end;
     consider q1 be sequence of CNS such that
A6:   for n holds P[n,q1.n]from FUNCT_2:sch 3(A4);
     now let x be set;
       assume x in rng q1; then
       ex n st x = q1.n by Th8;
       hence x in dom f by A6;
     end; then
A7:  rng q1 c= dom f by TARSKI:def 3;
     now let n;
       q1.n in dom f & f/.(q1.n)=s1.n by A6;
       hence (f*q1).n= s1.n by A7,Th13;
     end; then
A8:  f*q1=s1 by FUNCT_2:113;
     consider s2 be sequence of CNS such that
A9:   s2 is_subsequence_of q1 &
      s2 is convergent & (lim s2) in dom f by A1,A7,Def6;
     f|(dom f) is_continuous_in (lim s2) by A2,A9,Def24; then
A10: f is_continuous_in (lim s2) by RELAT_1:97;
     rng s2 c= rng q1 by A9,Th9; then
     rng s2 c= dom f by A7,XBOOLE_1:1; then
A11: f*s2 is convergent & f/.(lim s2) = lim (f*s2) by A9,A10,Def18;
     take q2 = f*s2;
     thus q2 is_subsequence_of s1 & q2 is convergent & (lim q2) in rng f
     by A7,A8,A9,A11,Th26,PARTFUN2:4;
   end;
   hence thesis by CFCONT_1:def 6;
end;

theorem Th102:
for f be PartFunc of the carrier of CNS,REAL st
  dom f is compact & f is_continuous_on (dom f) holds
    (rng f) is compact
proof
   let f be PartFunc of the carrier of CNS,REAL;
   assume that
A1: dom f is compact and
A2: f is_continuous_on (dom f);
   now
     let s1 be Real_Sequence such that
A3:   rng s1 c= rng f;
     defpred P[set,set] means $2 in dom f & f/.$2=s1.$1;
A4:  for n ex p be Point of CNS st P[n,p]
     proof let n;
       dom s1 = NAT by FUNCT_2:def 1; then
       s1.n in rng s1 by FUNCT_1:12; then
       consider p be  Point of CNS such that
A5:     p in dom f & s1.n = f.p by A3,PARTFUN1:26;
       take p;
       thus thesis by A5,FINSEQ_4:def 4;
     end;
     consider q1 be sequence of CNS such that
A6:   for n holds P[n,q1.n]from FUNCT_2:sch 3(A4);
     now let x be set;
       assume x in rng q1; then
       ex n st x = q1.n by Th8;
       hence x in dom f by A6;
     end; then
A7:  rng q1 c= dom f by TARSKI:def 3;
     now let n;
       q1.n in dom f & f/.(q1.n)=s1.n by A6;
       hence (f*q1).n= s1.n by A7,Th14;
     end; then
A8:  f*q1=s1 by FUNCT_2:113;
     consider s2 be sequence of CNS such that
A9:   s2 is_subsequence_of q1 &
      s2 is convergent & (lim s2) in dom f by A1,A7,Def6;
     f|(dom f) is_continuous_in (lim s2) by A2,A9,Def25; then
A10: f is_continuous_in (lim s2) by RELAT_1:97;
     rng s2 c= rng q1 by A9,Th9; then
     rng s2 c= dom f by A7,XBOOLE_1:1; then
A11: f*s2 is convergent & f/.(lim s2) = lim (f*s2) by A9,A10,Def19;
     take q2 = f*s2;
     thus q2 is_subsequence_of s1 & q2 is convergent & (lim q2) in (rng f)
     by A7,A8,A9,A11,Th27,PARTFUN2:4;
   end;
   hence thesis by RCOMP_1:def 3;
end;

theorem
for f be PartFunc of the carrier of RNS,COMPLEX st
  dom f is compact & f is_continuous_on (dom f) holds
    (rng f) is compact
proof
   let f be PartFunc of the carrier of RNS,COMPLEX;
   assume that
A1: dom f is compact and
A2: f is_continuous_on (dom f);
   now
     let s1 be Complex_Sequence such that
A3:   rng s1 c= rng f;
     defpred P[set,set] means $2 in dom f & f/.$2=s1.$1;
A4:  for n ex p be Point of RNS st P[n,p]
     proof let n;
       dom s1 = NAT by FUNCT_2:def 1; then
       s1.n in rng s1 by FUNCT_1:12; then
       consider p be  Point of RNS such that
A5:     p in dom f & s1.n = f.p by A3,PARTFUN1:26;
       take p;
       thus thesis by A5,FINSEQ_4:def 4;
     end;
     consider q1 be sequence of RNS such that
A6:   for n holds P[n,q1.n] from FUNCT_2:sch 3(A4);
     now let x be set;
       assume x in rng q1; then
       ex n st x = q1.n by NFCONT_1:6;
       hence x in dom f by A6;
     end; then
A7:  rng q1 c= dom f by TARSKI:def 3;
     now let n;
       q1.n in dom f & f/.(q1.n)=s1.n by A6;
       hence (f*q1).n= s1.n by A7,Th15;
     end; then
A8:  f*q1=s1 by FUNCT_2:113;
     consider s2 be sequence of RNS such that
A9:   s2 is_subsequence_of q1 &
      s2 is convergent & (lim s2) in dom f by A1,A7,NFCONT_1:def 4;
     f|(dom f) is_continuous_in (lim s2) by A2,A9,Def26; then
A10: f is_continuous_in (lim s2) by RELAT_1:97;
     rng s2 c= rng q1 by A9,NFCONT_1:7; then
     rng s2 c= dom f by A7,XBOOLE_1:1; then
A11: f*s2 is convergent & f/.(lim s2) = lim (f*s2) by A9,A10,Def20;
     take q2 = f*s2;
     thus q2 is_subsequence_of s1 & q2 is convergent & (lim q2) in rng f
     by A7,A8,A9,A11,Th28,PARTFUN2:4;
   end;
   hence thesis by CFCONT_1:def 6;
end;

theorem
for Y be Subset of CNS1, f be PartFunc of CNS1,CNS2 st
 Y c= dom f & Y is compact & f is_continuous_on Y holds
  (f.:Y) is compact
proof
   let Y be Subset of CNS1;
   let f be PartFunc of CNS1,CNS2;
   assume that
A1: Y c= dom f and
A2: Y is compact and
A3: f is_continuous_on Y;
A4:dom (f|Y) = dom f /\ Y by RELAT_1:90 .= Y by A1,XBOOLE_1:28;
   f|Y is_continuous_on Y
   proof
     thus Y c= dom (f|Y) by A4;
     let r be Point of CNS1;
     assume r in Y; then
     f|Y is_continuous_in r by A3,Def21;
     hence (f|Y)|Y is_continuous_in r by RELAT_1:101;
   end; then
   rng (f|Y) is compact by A2,A4,Th98;
   hence thesis by RELAT_1:148;
end;

theorem
for Y be Subset of CNS, f be PartFunc of CNS,RNS st
 Y c= dom f & Y is compact & f is_continuous_on Y holds
  (f.:Y) is compact
proof
   let Y be Subset of CNS;
   let f be PartFunc of CNS,RNS;
   assume that
A1: Y c= dom f and
A2: Y is compact and
A3: f is_continuous_on Y;
A4:dom (f|Y) = dom f /\ Y by RELAT_1:90 .= Y by A1,XBOOLE_1:28;
   f|Y is_continuous_on Y
   proof
     thus Y c= dom (f|Y) by A4;
     let r be Point of CNS;
     assume r in Y; then
     f|Y is_continuous_in r by A3,Def22;
     hence (f|Y)|Y is_continuous_in r by RELAT_1:101;
   end; then
   rng (f|Y) is compact by A2,A4,Th99;
   hence thesis by RELAT_1:148;
end;

theorem
for Y be Subset of RNS, f be PartFunc of RNS,CNS st
 Y c= dom f & Y is compact & f is_continuous_on Y holds
  (f.:Y) is compact
proof
   let Y be Subset of RNS;
   let f be PartFunc of RNS,CNS;
   assume that
A1: Y c= dom f and
A2: Y is compact and
A3: f is_continuous_on Y;
A4:dom (f|Y) = dom f /\ Y by RELAT_1:90 .= Y by A1,XBOOLE_1:28;
   f|Y is_continuous_on Y
   proof
     thus Y c= dom (f|Y) by A4;
     let r be Point of RNS;
     assume r in Y; then
     f|Y is_continuous_in r by A3,Def23;
     hence (f|Y)|Y is_continuous_in r by RELAT_1:101;
   end; then
   rng (f|Y) is compact by A2,A4,Th100;
   hence thesis by RELAT_1:148;
end;

theorem Th107:
for f be PartFunc of the carrier of CNS,REAL st
 dom f<>{} & (dom f) is compact & f is_continuous_on (dom f) holds
  ex x1,x2 be Point of CNS st
   x1 in dom f & x2 in dom f &
   f/.x1 = upper_bound (rng f) &
   f/.x2 = lower_bound (rng f)
proof
   let f be PartFunc of the carrier of CNS,REAL such that
A1: dom f <> {} and
A2: dom f is compact and
A3: f is_continuous_on (dom f);
A4:rng f <> {} by A1,RELAT_1:65;
   rng f is compact by A2,A3,Th102; then
A5:upper_bound (rng f) in rng f &
   lower_bound (rng f) in rng f by A4,RCOMP_1:32; then
   consider x being Element of the carrier of CNS such that
A6: x in dom f & upper_bound (rng f) = f.x by PARTFUN1:26;
   consider y being Element of the carrier of CNS such that
A7: y in dom f & lower_bound (rng f ) = f.y by A5,PARTFUN1:26;
   take x;
   take y;
   thus thesis by A6,A7,FINSEQ_4:def 4;
end;

theorem Th108:
for f be PartFunc of CNS1,CNS2 st
 dom f<>{} & (dom f) is compact & f is_continuous_on (dom f) holds
  ex x1,x2 be Point of CNS1 st
   x1 in dom f & x2 in dom f &
   ||.f.||/.x1 = upper_bound (rng ||.f.||) &
   ||.f.||/.x2 = lower_bound (rng ||.f.||)
proof
   let f be PartFunc of CNS1,CNS2 such that
A1: dom f <> {} and
A2: dom f is compact and
A3: f is_continuous_on (dom f);
A4:dom ||.f.|| is compact by A2,Def2;
A5:dom f = dom ||.f.|| by Def2; then
A6:||.f.|| is_continuous_on (dom ||.f.||) by A3,Th92;
A7:rng ||.f.|| <> {} by A1,A5,RELAT_1:65;
   rng ||.f.|| is compact by A4,A6,Th102; then
A8:upper_bound (rng ||.f.||) in rng ||.f.|| &
   lower_bound (rng ||.f.||) in rng ||.f.|| by A7,RCOMP_1:32; then
   consider x being Element of the carrier of CNS1 such that
A9: x in dom ||.f.|| &
    upper_bound (rng ||.f.||) = ||.f.||.x by PARTFUN1:26;
   consider y being Element of the carrier of CNS1 such that
A10: y in dom ||.f.|| &
    lower_bound (rng ||.f.||) = ||.f.||.y by A8,PARTFUN1:26;
   take x;
   take y;
   thus thesis by A9,A10,Def2,FINSEQ_4:def 4;
end;

theorem Th109:
for f be PartFunc of CNS,RNS st
 dom f<>{} & (dom f) is compact & f is_continuous_on (dom f) holds
  ex x1,x2 be Point of CNS st
   x1 in dom f & x2 in dom f &
   ||.f.||/.x1 = upper_bound (rng ||.f.||) &
   ||.f.||/.x2 = lower_bound (rng ||.f.||)
proof
   let f be PartFunc of CNS,RNS such that
A1: dom f <> {} and
A2: dom f is compact and
A3: f is_continuous_on (dom f);
A4:dom ||.f.|| is compact by A2,Def3;
A5:dom f = dom ||.f.|| by Def3; then
A6:||.f.|| is_continuous_on (dom ||.f.||) by A3,Th93;
A7:rng ||.f.|| <> {} by A1,A5,RELAT_1:65;
   rng ||.f.|| is compact by A4,A6,Th102; then
A8:upper_bound (rng ||.f.||) in rng ||.f.|| &
   lower_bound (rng ||.f.||) in rng ||.f.|| by A7,RCOMP_1:32; then
   consider x being Element of the carrier of CNS such that
A9: x in dom ||.f.|| &
    upper_bound (rng ||.f.||) = ||.f.||.x by PARTFUN1:26;
   consider y being Element of the carrier of CNS such that
A10: y in dom ||.f.|| &
    lower_bound (rng ||.f.||) = ||.f.||.y by A8,PARTFUN1:26;
   take x;
   take y;
   thus thesis by A9,A10,Def3,FINSEQ_4:def 4;
end;

theorem Th110:
for f be PartFunc of RNS,CNS st
 dom f<>{} & (dom f) is compact & f is_continuous_on (dom f) holds
  ex x1,x2 be Point of RNS st
   x1 in dom f & x2 in dom f &
   ||.f.||/.x1 = upper_bound (rng ||.f.||) &
   ||.f.||/.x2 = lower_bound (rng ||.f.||)
proof
   let f be PartFunc of RNS,CNS such that
A1: dom f <> {} and
A2: dom f is compact and
A3: f is_continuous_on (dom f);
A4:dom ||.f.|| is compact by A2,Def4;
A5:dom f = dom ||.f.|| by Def4; then
A6:||.f.|| is_continuous_on (dom ||.f.||) by A3,Th94;
A7:rng ||.f.|| <> {} by A1,A5,RELAT_1:65;
   rng ||.f.|| is compact by A4,A6,NFCONT_1:38; then
A8:upper_bound (rng ||.f.||) in rng ||.f.|| &
   lower_bound (rng ||.f.||) in rng ||.f.|| by A7,RCOMP_1:32; then
   consider x being Element of the carrier of RNS such that
A9: x in dom ||.f.|| &
    upper_bound (rng ||.f.||) = ||.f.||.x by PARTFUN1:26;
   consider y being Element of the carrier of RNS such that
A10: y in dom ||.f.|| &
    lower_bound (rng ||.f.||) = ||.f.||.y by A8,PARTFUN1:26;
   take x;
   take y;
   thus thesis by A9,A10,Def4,FINSEQ_4:def 4;
end;

theorem Th111:
for f be PartFunc of CNS1,CNS2 holds
 (||.f.||)|X = ||.(f|X).||
proof
   let f be PartFunc of CNS1,CNS2;
A1:dom ((||.f.||)|X) = dom (||.f.||) /\ X by RELAT_1:90
   .= dom f /\ X by Def2
   .= dom (f|X) by RELAT_1:90
   .= dom ( ||.(f|X).||) by Def2;
   now let c be Point of CNS1;
     assume
A2:  c in dom ((||.f.||)|X); then
     c in dom (||.f.||) /\ X by RELAT_1:90; then
A3: c in dom (||.f.||) & c in X by XBOOLE_0:def 3;
A4: c in dom (f|X) by A1,A2,Def2;
     thus ((||.f.||)|X).c = (||.f.||).c by A2,FUNCT_1:70
     .= ||.f/.c.|| by A3,Def2
     .= ||.(f|X)/.c.|| by A4,PARTFUN2:32
     .= ||.(f|X).||.c by A1,A2,Def2;
   end;
   hence thesis by A1,PARTFUN1:34;
end;

theorem Th112:
for f be PartFunc of CNS,RNS holds
 (||.f.||)|X = ||.(f|X).||
proof
   let f be PartFunc of CNS,RNS;
A1:dom ((||.f.||)|X) = dom (||.f.||) /\ X by RELAT_1:90
   .= dom f /\ X by Def3
   .= dom (f|X) by RELAT_1:90
   .= dom ( ||.(f|X).||) by Def3;
   now let c be Point of CNS;
     assume
A2:  c in dom ((||.f.||)|X); then
     c in dom (||.f.||) /\ X by RELAT_1:90; then
A3: c in dom (||.f.||) & c in X by XBOOLE_0:def 3;
A4: c in dom (f|X) by A1,A2,Def3;
     thus ((||.f.||)|X).c = (||.f.||).c by A2,FUNCT_1:70
     .= ||.f/.c.|| by A3,Def3
     .= ||.(f|X)/.c.|| by A4,PARTFUN2:32
     .= ||.(f|X).||.c by A1,A2,Def3;
   end;
   hence thesis by A1,PARTFUN1:34;
end;

theorem Th113:
for f be PartFunc of RNS,CNS holds
 (||.f.||)|X = ||.(f|X).||
proof
   let f be PartFunc of RNS,CNS;
A1:dom ((||.f.||)|X) = dom (||.f.||) /\ X by RELAT_1:90
   .= dom f /\ X by Def4
   .= dom (f|X) by RELAT_1:90
   .= dom ( ||.(f|X).||) by Def4;
   now let c be Point of RNS;
     assume
A2:  c in dom ((||.f.||)|X); then
     c in dom (||.f.||) /\ X by RELAT_1:90; then
A3: c in dom (||.f.||) & c in X by XBOOLE_0:def 3;
A4: c in dom (f|X) by A1,A2,Def4;
     thus ((||.f.||)|X).c = (||.f.||).c by A2,FUNCT_1:70
     .= ||.f/.c.|| by A3,Def4
     .= ||.(f|X)/.c.|| by A4,PARTFUN2:32
     .= ||.(f|X).||.c by A1,A2,Def4;
   end;
   hence thesis by A1,PARTFUN1:34;
end;

theorem
for f be PartFunc of CNS1,CNS2, Y be Subset of CNS1 st
 Y<>{} & Y c= dom f & Y is compact & f is_continuous_on Y holds
  ex x1,x2 be Point of CNS1 st x1 in Y & x2 in Y &
   ||.f.||/.x1 = upper_bound (||.f.||.:Y) &
   ||.f.||/.x2 = lower_bound (||.f.||.:Y)
proof
   let f be PartFunc of CNS1,CNS2;
   let Y be Subset of CNS1 such that
A1: Y <> {} and
A2: Y c= dom f and
A3: Y is compact and
A4: f is_continuous_on Y;
A5:dom (f|Y) = dom f /\ Y by RELAT_1:90
   .= Y by A2,XBOOLE_1:28;
   f|Y is_continuous_on Y
   proof
     thus Y c= dom (f|Y) by A5;
     let r be Point of CNS1;
     assume r in Y; then
     f|Y is_continuous_in r by A4,Def21;
     hence (f|Y)|Y is_continuous_in r by RELAT_1:101;
   end; then
   consider x1,x2 be Point of CNS1 such that
A6: x1 in dom (f|Y) & x2 in dom (f|Y) &
   ||.(f|Y).||/.x1 = upper_bound (rng ||.(f|Y).||) &
   ||.(f|Y).||/.x2 = lower_bound (rng ||.(f|Y).||) by A1,A3,A5,Th108;
A7:dom f = dom ||.f.|| by Def2;
   take x1,x2; thus x1 in Y & x2 in Y by A5,A6;
A8:x1 in dom ||.(f|Y).|| & x2 in dom ||.(f|Y).|| by A6,Def2; then
A9:||.(f|Y).||/.x1 =||.(f|Y).||.x1 by FINSEQ_4:def 4
                  .=||. (f|Y)/.x1.|| by A8,Def2
                  .=||. f/.x1.|| by A6,PARTFUN2:32
                  .=||. f.||.x1 by A2,A5,A6,A7,Def2
                  .=||. f.||/.x1 by A2,A5,A6,A7,FINSEQ_4:def 4;
A10:||.(f|Y).||/.x2 =||.(f|Y).||.x2 by A8,FINSEQ_4:def 4
                  .=||. (f|Y)/.x2.|| by A8,Def2
                  .=||. f/.x2.|| by A6,PARTFUN2:32
                  .=||. f.||.x2 by A2,A5,A6,A7,Def2
                  .=||. f.||/.x2 by A2,A5,A6,A7,FINSEQ_4:def 4;
   ||.f.||.:Y =rng ( ||.f.|| | Y) by RELAT_1:148
             .=rng(  ||.(f|Y).|| ) by Th111;
   hence thesis by A6,A9,A10;
end;

theorem
for f be PartFunc of CNS,RNS ,Y be Subset of CNS st
 Y<>{} & Y c= dom f & Y is compact & f is_continuous_on Y holds
  ex x1,x2 be Point of CNS st x1 in Y & x2 in Y &
   ||.f.||/.x1 = upper_bound (||.f.||.:Y) &
   ||.f.||/.x2 = lower_bound (||.f.||.:Y)
proof
   let f be PartFunc of CNS,RNS;
   let Y be Subset of CNS such that
A1: Y <> {} and
A2: Y c= dom f and
A3: Y is compact and
A4: f is_continuous_on Y;
A5:dom (f|Y) = dom f /\ Y by RELAT_1:90
   .= Y by A2,XBOOLE_1:28;
   f|Y is_continuous_on Y
   proof
     thus Y c= dom (f|Y) by A5;
     let r be Point of CNS;
     assume r in Y; then
     f|Y is_continuous_in r by A4,Def22;
     hence (f|Y)|Y is_continuous_in r by RELAT_1:101;
   end; then
   consider x1,x2 be Point of CNS such that
A6: x1 in dom (f|Y) & x2 in dom (f|Y) &
   ||.(f|Y).||/.x1 = upper_bound (rng ||.(f|Y).||) &
   ||.(f|Y).||/.x2 = lower_bound (rng ||.(f|Y).||) by A1,A3,A5,Th109;
A7:dom f = dom ||.f.|| by Def3;
   take x1,x2; thus x1 in Y & x2 in Y by A5,A6;
A8:x1 in dom ||.(f|Y).|| & x2 in dom ||.(f|Y).|| by A6,Def3; then
A9:||.(f|Y).||/.x1 =||.(f|Y).||.x1 by FINSEQ_4:def 4
                  .=||. (f|Y)/.x1.|| by A8,Def3
                  .=||. f/.x1.|| by A6,PARTFUN2:32
                  .=||. f.||.x1 by A2,A5,A6,A7,Def3
                  .=||. f.||/.x1 by A2,A5,A6,A7,FINSEQ_4:def 4;
A10:||.(f|Y).||/.x2 =||.(f|Y).||.x2 by A8,FINSEQ_4:def 4
                  .=||. (f|Y)/.x2.|| by A8,Def3
                  .=||. f/.x2.|| by A6,PARTFUN2:32
                  .=||. f.||.x2 by A2,A5,A6,A7,Def3
                  .=||. f.||/.x2 by A2,A5,A6,A7,FINSEQ_4:def 4;
   ||.f.||.:Y =rng ( ||.f.|| | Y) by RELAT_1:148
             .=rng(  ||.(f|Y).|| ) by Th112;
   hence thesis by A6,A9,A10;
end;

theorem
for f be PartFunc of RNS,CNS ,Y be Subset of RNS st
 Y<>{} & Y c= dom f & Y is compact & f is_continuous_on Y holds
  ex x1,x2 be Point of RNS st x1 in Y & x2 in Y &
   ||.f.||/.x1 = upper_bound (||.f.||.:Y) &
   ||.f.||/.x2 = lower_bound (||.f.||.:Y)
proof
   let f be PartFunc of RNS,CNS;
   let Y be Subset of RNS such that
A1: Y <> {} and
A2: Y c= dom f and
A3: Y is compact and
A4: f is_continuous_on Y;
A5:dom (f|Y) = dom f /\ Y by RELAT_1:90
   .= Y by A2,XBOOLE_1:28;
   f|Y is_continuous_on Y
   proof
     thus Y c= dom (f|Y) by A5;
     let r be Point of RNS;
     assume r in Y; then
     f|Y is_continuous_in r by A4,Def23;
     hence (f|Y)|Y is_continuous_in r by RELAT_1:101;
   end; then
   consider x1,x2 be Point of RNS such that
A6: x1 in dom (f|Y) & x2 in dom (f|Y) &
   ||.(f|Y).||/.x1 = upper_bound (rng ||.(f|Y).||) &
   ||.(f|Y).||/.x2 = lower_bound (rng ||.(f|Y).||) by A1,A3,A5,Th110;
A7:dom f = dom ||.f.|| by Def4;
   take x1,x2; thus x1 in Y & x2 in Y by A5,A6;
A8:x1 in dom ||.(f|Y).|| & x2 in dom ||.(f|Y).|| by A6,Def4; then
A9:||.(f|Y).||/.x1 =||.(f|Y).||.x1 by FINSEQ_4:def 4
                  .=||. (f|Y)/.x1.|| by A8,Def4
                  .=||. f/.x1.|| by A6,PARTFUN2:32
                  .=||. f.||.x1 by A2,A5,A6,A7,Def4
                  .=||. f.||/.x1 by A2,A5,A6,A7,FINSEQ_4:def 4;
A10:||.(f|Y).||/.x2 =||.(f|Y).||.x2 by A8,FINSEQ_4:def 4
                  .=||. (f|Y)/.x2.|| by A8,Def4
                  .=||. f/.x2.|| by A6,PARTFUN2:32
                  .=||. f.||.x2 by A2,A5,A6,A7,Def4
                  .=||. f.||/.x2 by A2,A5,A6,A7,FINSEQ_4:def 4;
   ||.f.||.:Y =rng ( ||.f.|| | Y) by RELAT_1:148
             .=rng(  ||.(f|Y).|| ) by Th113;
   hence thesis by A6,A9,A10;
end;

theorem
for f be PartFunc of the carrier of CNS,REAL, Y be Subset of CNS st
 Y<>{} & Y c= dom f & Y is compact & f is_continuous_on Y holds
  ex x1,x2 be Point of CNS st x1 in Y & x2 in Y &
   f/.x1 = upper_bound (f.:Y) & f/.x2 = lower_bound (f.:Y)
proof
   let f  be PartFunc of  the carrier of CNS,REAL;
   let Y be Subset of CNS such that
A1: Y <> {} and
A2: Y c= dom f and
A3: Y is compact and
A4: f is_continuous_on Y;
A5:dom (f|Y) = dom f /\ Y by RELAT_1:90 .= Y by A2,XBOOLE_1:28;
   f|Y is_continuous_on Y
   proof
     thus Y c= dom (f|Y) by A5;
     let r be Point of CNS;
     assume r in Y; then
     f|Y is_continuous_in r by A4,Def25;
     hence (f|Y)|Y is_continuous_in r by RELAT_1:101;
   end; then
   consider x1,x2 be Point of CNS such that
A6: x1 in dom (f|Y) & x2 in dom (f|Y) &
    (f|Y)/.x1 = upper_bound (rng (f|Y)) &
    (f|Y)/.x2 = lower_bound (rng (f|Y)) by A1,A3,A5,Th107;
   take x1,x2;
   thus x1 in Y & x2 in Y by A5,A6;
A7:(f|Y)/.x1 =f/.x1 by A6,PARTFUN2:32;
   (f|Y)/.x2 =f/.x2 by A6,PARTFUN2:32;
   hence thesis by A6,A7,RELAT_1:148;
end;

definition
let CNS1,CNS2 be ComplexNormSpace;
let X be set;
let f be PartFunc of CNS1,CNS2;
pred f is_Lipschitzian_on X means :Def27:
  X c= dom f &
  ex r st
   0 < r &
   for x1,x2 be Point of CNS1 st x1 in X & x2 in X holds
    ||.f/.x1-f/.x2.||<=r*||.x1-x2.||;
end;

definition
let CNS be ComplexNormSpace;
let RNS be RealNormSpace;
let X be set;
let f be PartFunc of CNS,RNS;
pred f is_Lipschitzian_on X means :Def28:
  X c= dom f &
  ex r st
   0 < r &
   for x1,x2 be Point of CNS st x1 in X & x2 in X holds
    ||.f/.x1-f/.x2.||<=r*||.x1-x2.||;
end;

definition
let RNS be RealNormSpace;
let CNS be ComplexNormSpace;
let X be set;
let f be PartFunc of RNS,CNS;
pred f is_Lipschitzian_on X means :Def29:
  X c= dom f &
  ex r st
   0 < r &
   for x1,x2 be Point of RNS st x1 in X & x2 in X holds
    ||.f/.x1-f/.x2.||<=r*||.x1-x2.||;
end;

definition
let CNS be ComplexNormSpace;
let X be set;
let f be PartFunc of the carrier of CNS,COMPLEX;
pred f is_Lipschitzian_on X means :Def30:
  X c= dom f &
  ex r st
   0<r &
   for x1,x2 be Point of CNS st
    x1 in X & x2 in X holds
     |.(f/.x1-f/.x2).|<=r*||.x1-x2.||;
end;

definition
let CNS be ComplexNormSpace;
let X be set;
let f be PartFunc of the carrier of CNS,REAL;
pred f is_Lipschitzian_on X means :Def31:
  X c= dom f &
  ex r st
   0<r &
   for x1,x2 be Point of CNS st
    x1 in X & x2 in X holds
     abs(f/.x1-f/.x2)<=r*||.x1-x2.||;
end;

definition
let RNS be RealNormSpace;
let X be set;
let f be PartFunc of the carrier of RNS,COMPLEX;
pred f is_Lipschitzian_on X means :Def32:
  X c= dom f &
  ex r st
   0<r &
   for x1,x2 be Point of RNS st
    x1 in X & x2 in X holds
     |.(f/.x1-f/.x2).|<=r*||.x1-x2.||;
end;

theorem Th118:
for f be PartFunc of CNS1,CNS2 st
 f is_Lipschitzian_on X & X1 c= X holds f is_Lipschitzian_on X1
proof
   let f be PartFunc of CNS1,CNS2;
   assume
A1: f is_Lipschitzian_on X & X1 c= X; then
   X c= dom f by Def27;
   hence X1 c= dom f by A1,XBOOLE_1:1;
   consider s be Real such that
A2: 0<s &
    for x1,x2 be Point of CNS1 st x1 in X & x2 in X holds
     ||. f/.x1-f/.x2.||<=s*||. x1-x2.|| by A1,Def27;
   take s;
   thus 0<s by A2;
   let x1,x2 be Point of CNS1;
   assume x1 in X1 & x2 in X1;
   hence thesis by A1,A2;
end;

theorem Th119:
for f be PartFunc of CNS,RNS st
 f is_Lipschitzian_on X & X1 c= X holds f is_Lipschitzian_on X1
proof
   let f be PartFunc of CNS,RNS;
   assume
A1: f is_Lipschitzian_on X & X1 c= X; then
   X c= dom f by Def28;
   hence X1 c= dom f by A1,XBOOLE_1:1;
   consider s be Real such that
A2: 0<s &
    for x1,x2 be Point of CNS st x1 in X & x2 in X holds
     ||. f/.x1-f/.x2.||<=s*||. x1-x2.|| by A1,Def28;
   take s;
   thus 0<s by A2;
   let x1,x2 be Point of CNS;
   assume x1 in X1 & x2 in X1;
   hence thesis by A1,A2;
end;

theorem Th120:
for f be PartFunc of RNS,CNS st
 f is_Lipschitzian_on X & X1 c= X holds f is_Lipschitzian_on X1
proof
   let f be PartFunc of RNS,CNS;
   assume
A1: f is_Lipschitzian_on X & X1 c= X; then
   X c= dom f by Def29;
   hence X1 c= dom f by A1,XBOOLE_1:1;
   consider s be Real such that
A2: 0<s &
    for x1,x2 be Point of RNS st x1 in X & x2 in X holds
     ||. f/.x1-f/.x2.||<=s*||. x1-x2.|| by A1,Def29;
   take s;
   thus 0<s by A2;
   let x1,x2 be Point of RNS;
   assume x1 in X1 & x2 in X1;
   hence thesis by A1,A2;
end;

theorem
for f1,f2 be PartFunc of CNS1,CNS2 st
  f1 is_Lipschitzian_on X & f2 is_Lipschitzian_on X1 holds
    f1+f2 is_Lipschitzian_on X /\ X1
proof
   let f1,f2 be PartFunc of CNS1,CNS2;
   assume
A1: f1 is_Lipschitzian_on X & f2 is_Lipschitzian_on X1;
A2:X /\ X1 c= X & X /\ X1 c= X1 by XBOOLE_1:17; then
A3:f1 is_Lipschitzian_on X /\ X1 by A1,Th118; then
A4:X /\ X1 c= dom f1 by Def27;
A5:f2 is_Lipschitzian_on X /\ X1 by A1,A2,Th118; then
   X /\ X1 c= dom f2 by Def27; then
   X /\ X1 c= dom f1 /\ dom f2 by A4,XBOOLE_1:19;
   hence
A6:X /\ X1 c= dom (f1+f2) by VFUNCT_2:def 1;
   consider s be Real such that
A7: 0 < s &
    for x1,x2 be Point of CNS1 st x1 in X /\ X1 & x2 in X /\ X1 holds
     ||. f1/.x1-f1/.x2.||<=s*||. x1-x2 .|| by A3,Def27;
   consider g be Real such that
A8: 0<g &
    for x1,x2 be Point of CNS1 st x1 in X /\ X1 & x2 in X /\ X1 holds
     ||. f2/.x1-f2/.x2 .||<=g*||. x1-x2 .|| by A5,Def27;
   take p=s+g;
   0+0<s+g by A7,A8,REAL_1:67;
   hence 0<p;
   let x1,x2 be Point of CNS1;
   assume
A9: x1 in X/\X1 & x2 in X/\X1; then
   ||. (f1+f2)/.x1-(f1+f2)/.x2 .||
    = ||. f1/.x1 + f2/.x1-(f1+f2)/.x2.|| by A6,VFUNCT_2:def 1
   .= ||. f1/.x1 + f2/.x1 - (f1/.x2+f2/.x2) .|| by A6,A9,VFUNCT_2:def 1
   .= ||. f1/.x1 + (f2/.x1 - (f1/.x2+f2/.x2)).|| by RLVECT_1:42
   .= ||. f1/.x1 + (f2/.x1 - f1/.x2-f2/.x2).|| by RLVECT_1:41
   .= ||. f1/.x1 + (-f1/.x2 + f2/.x1-f2/.x2).|| by RLVECT_1:def 11
   .= ||. f1/.x1 + (-f1/.x2 + (f2/.x1-f2/.x2)).|| by RLVECT_1:42
   .= ||. f1/.x1 +- f1/.x2 + (f2/.x1-f2/.x2).|| by RLVECT_1:def 6
   .= ||. f1/.x1 - f1/.x2 + (f2/.x1-f2/.x2).|| by RLVECT_1:def 11; then
A10:||. (f1+f2)/.x1-(f1+f2)/.x2.|| <=||. f1/.x1-f1/.x2.||+||. f2/.x1-f2/.x2.||
    by CLVECT_1:def 10;
A11:||. f1/.x1-f1/.x2.||<=s*||. x1-x2.|| by A7,A9;
   ||. f2/.x1-f2/.x2.||<=g*||. x1-x2.|| by A8,A9; then
   ||. f1/.x1-f1/.x2.||+||. f2/.x1-f2/.x2.|| <= s*||. x1-x2.||+g*||. x1-x2.||
   by A11,REAL_1:55;
   hence thesis by A10,AXIOMS:22;
end;

theorem
for f1,f2 be PartFunc of CNS,RNS st
  f1 is_Lipschitzian_on X & f2 is_Lipschitzian_on X1 holds
    f1+f2 is_Lipschitzian_on X /\ X1
proof
   let f1,f2 be PartFunc of CNS,RNS;
   assume
A1: f1 is_Lipschitzian_on X & f2 is_Lipschitzian_on X1;
A2:X /\ X1 c= X & X /\ X1 c= X1 by XBOOLE_1:17; then
A3:f1 is_Lipschitzian_on X /\ X1 by A1,Th119; then
A4:X /\ X1 c= dom f1 by Def28;
A5:f2 is_Lipschitzian_on X /\ X1 by A1,A2,Th119; then
   X /\ X1 c= dom f2 by Def28; then
   X /\ X1 c= dom f1 /\ dom f2 by A4,XBOOLE_1:19;
   hence
A6:X /\ X1 c= dom (f1+f2) by VFUNCT_1:def 1;
   consider s be Real such that
A7: 0 < s &
    for x1,x2 be Point of CNS st x1 in X /\ X1 & x2 in X /\ X1 holds
     ||. f1/.x1-f1/.x2.||<=s*||. x1-x2 .|| by A3,Def28;
   consider g be Real such that
A8: 0<g &
    for x1,x2 be Point of CNS st x1 in X /\ X1 & x2 in X /\ X1 holds
     ||. f2/.x1-f2/.x2 .||<=g*||. x1-x2 .|| by A5,Def28;
   take p=s+g;
   0+0<s+g by A7,A8,REAL_1:67;
   hence 0<p;
   let x1,x2 be Point of CNS;
   assume
A9: x1 in X/\X1 & x2 in X/\X1; then
   ||. (f1+f2)/.x1-(f1+f2)/.x2 .||
    = ||. f1/.x1 + f2/.x1-(f1+f2)/.x2.|| by A6,VFUNCT_1:def 1
   .= ||. f1/.x1 + f2/.x1 - (f1/.x2+f2/.x2) .|| by A6,A9,VFUNCT_1:def 1
   .= ||. f1/.x1 + (f2/.x1 - (f1/.x2+f2/.x2)).|| by RLVECT_1:42
   .= ||. f1/.x1 + (f2/.x1 - f1/.x2-f2/.x2).|| by RLVECT_1:41
   .= ||. f1/.x1 + (-f1/.x2 + f2/.x1-f2/.x2).|| by RLVECT_1:def 11
   .= ||. f1/.x1 + (-f1/.x2 + (f2/.x1-f2/.x2)).|| by RLVECT_1:42
   .= ||. f1/.x1 +- f1/.x2 + (f2/.x1-f2/.x2).|| by RLVECT_1:def 6
   .= ||. f1/.x1 - f1/.x2 + (f2/.x1-f2/.x2).|| by RLVECT_1:def 11; then
A10:||. (f1+f2)/.x1-(f1+f2)/.x2.|| <=||. f1/.x1-f1/.x2.||+||. f2/.x1-f2/.x2.||
    by NORMSP_1:def 2;
A11:||. f1/.x1-f1/.x2.||<=s*||. x1-x2.|| by A7,A9;
   ||. f2/.x1-f2/.x2.||<=g*||. x1-x2.|| by A8,A9; then
   ||. f1/.x1-f1/.x2.||+||. f2/.x1-f2/.x2.|| <= s*||. x1-x2.||+g*||. x1-x2.||
   by A11,REAL_1:55;
   hence thesis by A10,AXIOMS:22;
end;

theorem
for f1,f2 be PartFunc of RNS,CNS st
  f1 is_Lipschitzian_on X & f2 is_Lipschitzian_on X1 holds
    f1+f2 is_Lipschitzian_on X /\ X1
proof
   let f1,f2 be PartFunc of RNS,CNS;
   assume
A1: f1 is_Lipschitzian_on X & f2 is_Lipschitzian_on X1;
A2:X /\ X1 c= X & X /\ X1 c= X1 by XBOOLE_1:17; then
A3:f1 is_Lipschitzian_on X /\ X1 by A1,Th120; then
A4:X /\ X1 c= dom f1 by Def29;
A5:f2 is_Lipschitzian_on X /\ X1 by A1,A2,Th120; then
   X /\ X1 c= dom f2 by Def29; then
   X /\ X1 c= dom f1 /\ dom f2 by A4,XBOOLE_1:19;
   hence
A6:X /\ X1 c= dom (f1+f2) by VFUNCT_2:def 1;
   consider s be Real such that
A7: 0 < s &
    for x1,x2 be Point of RNS st x1 in X /\ X1 & x2 in X /\ X1 holds
     ||. f1/.x1-f1/.x2.||<=s*||. x1-x2 .|| by A3,Def29;
   consider g be Real such that
A8: 0<g &
    for x1,x2 be Point of RNS st x1 in X /\ X1 & x2 in X /\ X1 holds
     ||. f2/.x1-f2/.x2 .||<=g*||. x1-x2 .|| by A5,Def29;
   take p=s+g;
   0+0<s+g by A7,A8,REAL_1:67;
   hence 0<p;
   let x1,x2 be Point of RNS;
   assume
A9: x1 in X/\X1 & x2 in X/\X1; then
   ||. (f1+f2)/.x1-(f1+f2)/.x2 .||
    = ||. f1/.x1 + f2/.x1-(f1+f2)/.x2.|| by A6,VFUNCT_2:def 1
   .= ||. f1/.x1 + f2/.x1 - (f1/.x2+f2/.x2) .|| by A6,A9,VFUNCT_2:def 1
   .= ||. f1/.x1 + (f2/.x1 - (f1/.x2+f2/.x2)).|| by RLVECT_1:42
   .= ||. f1/.x1 + (f2/.x1 - f1/.x2-f2/.x2).|| by RLVECT_1:41
   .= ||. f1/.x1 + (-f1/.x2 + f2/.x1-f2/.x2).|| by RLVECT_1:def 11
   .= ||. f1/.x1 + (-f1/.x2 + (f2/.x1-f2/.x2)).|| by RLVECT_1:42
   .= ||. f1/.x1 +- f1/.x2 + (f2/.x1-f2/.x2).|| by RLVECT_1:def 6
   .= ||. f1/.x1 - f1/.x2 + (f2/.x1-f2/.x2).|| by RLVECT_1:def 11; then
A10:||. (f1+f2)/.x1-(f1+f2)/.x2.|| <=||. f1/.x1-f1/.x2.||+||. f2/.x1-f2/.x2.||
    by CLVECT_1:def 10;
A11:||. f1/.x1-f1/.x2.||<=s*||. x1-x2.|| by A7,A9;
   ||. f2/.x1-f2/.x2.||<=g*||. x1-x2.|| by A8,A9; then
   ||. f1/.x1-f1/.x2.||+||. f2/.x1-f2/.x2.|| <= s*||. x1-x2.||+g*||. x1-x2.||
   by A11,REAL_1:55;
   hence thesis by A10,AXIOMS:22;
end;

theorem
for f1,f2 be PartFunc of CNS1,CNS2 st
  f1 is_Lipschitzian_on X & f2 is_Lipschitzian_on X1 holds
    f1-f2 is_Lipschitzian_on X /\ X1
proof
   let f1,f2 be PartFunc of CNS1,CNS2;
   assume
A1: f1 is_Lipschitzian_on X & f2 is_Lipschitzian_on X1;
A2:X /\ X1 c= X & X /\ X1 c= X1 by XBOOLE_1:17; then
A3:f1 is_Lipschitzian_on X /\ X1 by A1,Th118; then
A4:X /\ X1 c= dom f1 by Def27;
A5:f2 is_Lipschitzian_on X /\ X1 by A1,A2,Th118; then
   X /\ X1 c= dom f2 by Def27; then
   X /\ X1 c= dom f1 /\ dom f2 by A4,XBOOLE_1:19;
   hence
A6:X /\ X1 c= dom (f1-f2) by VFUNCT_2:def 2;
   consider s be Real such that
A7: 0<s &
    for x1,x2 be Point of CNS1 st x1 in X /\ X1 & x2 in X /\ X1 holds
      ||. f1/.x1-f1/.x2.||<=s*||. x1-x2.|| by A3,Def27;
   consider g be Real such that
A8: 0<g &
    for x1,x2 be Point of CNS1 st x1 in X /\ X1 & x2 in X /\ X1 holds
      ||. f2/.x1-f2/.x2.||<=g*||. x1-x2.|| by A5,Def27;
   take p=s+g;
   0+0<s+g by A7,A8,REAL_1:67;
   hence 0<p;
   let x1,x2 be Point of CNS1;
   assume
A9: x1 in X/\X1 & x2 in X/\X1; then
   ||. (f1-f2)/.x1-(f1-f2)/.x2.||
    = ||. f1/.x1 - f2/.x1-(f1-f2)/.x2.|| by A6,VFUNCT_2:def 2
   .= ||. f1/.x1 - f2/.x1 - (f1/.x2-f2/.x2).|| by A6,A9,VFUNCT_2:def 2
   .= ||. f1/.x1 - (f2/.x1 + (f1/.x2-f2/.x2)).|| by RLVECT_1:41
   .= ||. f1/.x1 - (f1/.x2 + f2/.x1-f2/.x2).|| by RLVECT_1:42
   .= ||. f1/.x1 - (f1/.x2 + (f2/.x1-f2/.x2)).|| by RLVECT_1:42
   .= ||. f1/.x1 - f1/.x2 - (f2/.x1-f2/.x2).|| by RLVECT_1:41; then
A10:||. (f1-f2)/.x1-(f1-f2)/.x2.|| <=||. f1/.x1-f1/.x2.||+||. f2/.x1-f2/.x2.||
   by CLVECT_1:105;
A11:||. f1/.x1-f1/.x2.||<=s*||. x1-x2.|| by A7,A9;
   ||. f2/.x1-f2/.x2.||<=g*||. x1-x2.|| by A8,A9; then
   ||. f1/.x1-f1/.x2.||+||. f2/.x1-f2/.x2.||<= s*||. x1-x2.||+g*||. x1-x2.||
   by A11,REAL_1:55;
   hence thesis by A10,AXIOMS:22;
end;

theorem
for f1,f2 be PartFunc of CNS,RNS st
  f1 is_Lipschitzian_on X & f2 is_Lipschitzian_on X1 holds
    f1-f2 is_Lipschitzian_on X /\ X1
proof
   let f1,f2 be PartFunc of CNS,RNS;
   assume
A1: f1 is_Lipschitzian_on X & f2 is_Lipschitzian_on X1;
A2:X /\ X1 c= X & X /\ X1 c= X1 by XBOOLE_1:17; then
A3:f1 is_Lipschitzian_on X /\ X1 by A1,Th119; then
A4:X /\ X1 c= dom f1 by Def28;
A5:f2 is_Lipschitzian_on X /\ X1 by A1,A2,Th119; then
   X /\ X1 c= dom f2 by Def28; then
   X /\ X1 c= dom f1 /\ dom f2 by A4,XBOOLE_1:19;
   hence
A6:X /\ X1 c= dom (f1-f2) by VFUNCT_1:def 2;
   consider s be Real such that
A7: 0<s &
    for x1,x2 be Point of CNS st x1 in X /\ X1 & x2 in X /\ X1 holds
      ||. f1/.x1-f1/.x2.||<=s*||. x1-x2.|| by A3,Def28;
   consider g be Real such that
A8: 0<g &
    for x1,x2 be Point of CNS st x1 in X /\ X1 & x2 in X /\ X1 holds
      ||. f2/.x1-f2/.x2.||<=g*||. x1-x2.|| by A5,Def28;
   take p=s+g;
   0+0<s+g by A7,A8,REAL_1:67;
   hence 0<p;
   let x1,x2 be Point of CNS;
   assume
A9: x1 in X/\X1 & x2 in X/\X1; then
   ||. (f1-f2)/.x1-(f1-f2)/.x2.||
    = ||. f1/.x1 - f2/.x1-(f1-f2)/.x2.|| by A6,VFUNCT_1:def 2
   .= ||. f1/.x1 - f2/.x1 - (f1/.x2-f2/.x2).|| by A6,A9,VFUNCT_1:def 2
   .= ||. f1/.x1 - (f2/.x1 + (f1/.x2-f2/.x2)).|| by RLVECT_1:41
   .= ||. f1/.x1 - (f1/.x2 + f2/.x1-f2/.x2).|| by RLVECT_1:42
   .= ||. f1/.x1 - (f1/.x2 + (f2/.x1-f2/.x2)).|| by RLVECT_1:42
   .= ||. f1/.x1 - f1/.x2 - (f2/.x1-f2/.x2).|| by RLVECT_1:41; then
A10:||. (f1-f2)/.x1-(f1-f2)/.x2.|| <=||. f1/.x1-f1/.x2.||+||. f2/.x1-f2/.x2.||
   by NORMSP_1:7;
A11:||. f1/.x1-f1/.x2.||<=s*||. x1-x2.|| by A7,A9;
   ||. f2/.x1-f2/.x2.||<=g*||. x1-x2.|| by A8,A9; then
   ||. f1/.x1-f1/.x2.||+||. f2/.x1-f2/.x2.||<= s*||. x1-x2.||+g*||. x1-x2.||
   by A11,REAL_1:55;
   hence thesis by A10,AXIOMS:22;
end;

theorem
for f1,f2 be PartFunc of RNS,CNS st
  f1 is_Lipschitzian_on X & f2 is_Lipschitzian_on X1 holds
    f1-f2 is_Lipschitzian_on X /\ X1
proof
   let f1,f2 be PartFunc of RNS,CNS;
   assume
A1: f1 is_Lipschitzian_on X & f2 is_Lipschitzian_on X1;
A2:X /\ X1 c= X & X /\ X1 c= X1 by XBOOLE_1:17; then
A3:f1 is_Lipschitzian_on X /\ X1 by A1,Th120; then
A4:X /\ X1 c= dom f1 by Def29;
A5:f2 is_Lipschitzian_on X /\ X1 by A1,A2,Th120; then
   X /\ X1 c= dom f2 by Def29; then
   X /\ X1 c= dom f1 /\ dom f2 by A4,XBOOLE_1:19;
   hence
A6:X /\ X1 c= dom (f1-f2) by VFUNCT_2:def 2;
   consider s be Real such that
A7: 0<s &
    for x1,x2 be Point of RNS st x1 in X /\ X1 & x2 in X /\ X1 holds
      ||. f1/.x1-f1/.x2.||<=s*||. x1-x2.|| by A3,Def29;
   consider g be Real such that
A8: 0<g &
    for x1,x2 be Point of RNS st x1 in X /\ X1 & x2 in X /\ X1 holds
      ||. f2/.x1-f2/.x2.||<=g*||. x1-x2.|| by A5,Def29;
   take p=s+g;
   0+0<s+g by A7,A8,REAL_1:67;
   hence 0<p;
   let x1,x2 be Point of RNS;
   assume
A9: x1 in X/\X1 & x2 in X/\X1; then
   ||. (f1-f2)/.x1-(f1-f2)/.x2.||
    = ||. f1/.x1 - f2/.x1-(f1-f2)/.x2.|| by A6,VFUNCT_2:def 2
   .= ||. f1/.x1 - f2/.x1 - (f1/.x2-f2/.x2).|| by A6,A9,VFUNCT_2:def 2
   .= ||. f1/.x1 - (f2/.x1 + (f1/.x2-f2/.x2)).|| by RLVECT_1:41
   .= ||. f1/.x1 - (f1/.x2 + f2/.x1-f2/.x2).|| by RLVECT_1:42
   .= ||. f1/.x1 - (f1/.x2 + (f2/.x1-f2/.x2)).|| by RLVECT_1:42
   .= ||. f1/.x1 - f1/.x2 - (f2/.x1-f2/.x2).|| by RLVECT_1:41; then
A10:||. (f1-f2)/.x1-(f1-f2)/.x2.|| <=||. f1/.x1-f1/.x2.||+||. f2/.x1-f2/.x2.||
   by CLVECT_1:105;
A11:||. f1/.x1-f1/.x2.||<=s*||. x1-x2.|| by A7,A9;
   ||. f2/.x1-f2/.x2.||<=g*||. x1-x2.|| by A8,A9; then
   ||. f1/.x1-f1/.x2.||+||. f2/.x1-f2/.x2.||<= s*||. x1-x2.||+g*||. x1-x2.||
   by A11,REAL_1:55;
   hence thesis by A10,AXIOMS:22;
end;

theorem Th127:
for f be PartFunc of CNS1,CNS2 st f is_Lipschitzian_on X holds
  z(#)f is_Lipschitzian_on X
proof
   let f be PartFunc of CNS1,CNS2;
   assume
A1: f is_Lipschitzian_on X; then
   X c= dom f by Def27;
   hence
A2: X c= dom (z(#)f) by VFUNCT_2:def 4;
   consider s be Real such that
A3: 0 < s &
    for x1,x2 be Point of CNS1 st x1 in X & x2 in X holds
      ||. f/.x1-f/.x2.||<=s*||. x1-x2.|| by A1,Def27;
   now per cases;
     suppose
A4:    z=0;
       take s;
       thus 0<s by A3;
       let x1,x2 be Point of CNS1;
       assume
A5:     x1 in X & x2 in X; then
A6:    ||. (z(#)f)/.x1-(z(#)f)/.x2.||
        = ||. z*(f/.x1)-(z(#)f)/.x2.|| by A2,VFUNCT_2:def 4
       .= ||. 0.CNS2-(z(#)f)/.x2.|| by A4,CLVECT_1:2
       .= ||. 0.CNS2 - z*(f/.x2).|| by A2,A5,VFUNCT_2:def 4
       .= ||. 0.CNS2-0.CNS2.|| by A4,CLVECT_1:2
       .= ||. 0.CNS2.|| by RLVECT_1:26
       .= 0 by CLVECT_1:103;
       0<=||. x1-x2.|| by CLVECT_1:106; then
       s*0<=s*||. x1-x2.|| by A3,AXIOMS:25;
       hence ||. (z(#)f)/.x1-(z(#)f)/.x2.|| <=s*||. x1-x2.|| by A6;
     end;
     suppose
       z<>0; then
       0<|.z.| by COMPLEX1:133; then
A7:    0*s<|.z.|*s by A3,REAL_1:70;
       take g = |.z.| *s; thus 0<g by A7;
       let x1,x2 be Point of CNS1;
       assume
A8:     x1 in X & x2 in X;
A9:   ||. (z(#)f)/.x1-(z(#)f)/.x2.||
        = ||. z*(f/.x1)-(z(#)f)/.x2.|| by A2,A8,VFUNCT_2:def 4
       .= ||. z*(f/.x1) - z*(f/.x2).|| by A2,A8,VFUNCT_2:def 4
       .= ||. z*(f/.x1 - f/.x2).|| by CLVECT_1:10
       .= |.z.|*||. f/.x1 - f/.x2.|| by CLVECT_1:def 10;
A10:   0<=|.z.| by COMPLEX1:132;
       ||. f/.x1-f/.x2 .||<=s*||. x1-x2.|| by A3,A8; then
       |.z.|*||. f/.x1-f/.x2.|| <= |.z.|*(s*||. x1-x2.||)
       by A10,AXIOMS:25;
       hence ||. (z(#)f)/.x1-(z(#)f)/.x2.|| <= g*||. x1-x2.|| by A9;
     end;
   end;
   hence thesis;
end;

theorem Th128:
for f be PartFunc of CNS,RNS st f is_Lipschitzian_on X holds
  r(#)f is_Lipschitzian_on X
proof
   let f be PartFunc of CNS,RNS;
   assume
A1: f is_Lipschitzian_on X; then
   X c= dom f by Def28;
   hence
A2: X c= dom (r(#)f) by VFUNCT_1:def 4;
   consider s be Real such that
A3: 0 < s &
    for x1,x2 be Point of CNS st x1 in X & x2 in X holds
      ||. f/.x1-f/.x2.||<=s*||. x1-x2.|| by A1,Def28;
   now per cases;
     suppose
A4:    r=0;
       take s;
       thus 0<s by A3;
       let x1,x2 be Point of CNS;
       assume
A5:     x1 in X & x2 in X; then
A6:    ||. (r(#)f)/.x1-(r(#)f)/.x2.||
        = ||. r*(f/.x1)-(r(#)f)/.x2.|| by A2,VFUNCT_1:def 4
       .= ||. 0.RNS-(r(#)f)/.x2.|| by A4,RLVECT_1:23
       .= ||. 0.RNS - r*(f/.x2).|| by A2,A5,VFUNCT_1:def 4
       .= ||. 0.RNS-0.RNS.|| by A4,RLVECT_1:23
       .= ||. 0.RNS.|| by RLVECT_1:26
       .= 0 by NORMSP_1:5;
       0 <= ||. x1-x2.|| by CLVECT_1:106; then
       s*0 <= s*||. x1-x2.|| by A3,AXIOMS:25;
       hence ||. (r(#)f)/.x1-(r(#)f)/.x2.|| <=s*||. x1-x2.|| by A6;
     end;
     suppose
       r<>0; then
       0<abs(r) by ABSVALUE:6; then
A7:    0*s<abs(r)*s by A3,REAL_1:70;
       take g = abs(r) *s;
       thus 0<g by A7;
       let x1,x2 be Point of CNS;
       assume
A8:     x1 in X & x2 in X;
A9:   ||. (r(#)f)/.x1-(r(#)f)/.x2.||
        = ||. r*(f/.x1)-(r(#)f)/.x2.|| by A2,A8,VFUNCT_1:def 4
       .= ||. r*(f/.x1) - r*(f/.x2).|| by A2,A8,VFUNCT_1:def 4
       .= ||. r*(f/.x1 - f/.x2).|| by RLVECT_1:48
       .= abs(r)*||. f/.x1 - f/.x2.|| by NORMSP_1:def 2;
A10:   0<=abs(r) by ABSVALUE:5;
       ||. f/.x1-f/.x2 .||<=s*||. x1-x2.|| by A3,A8; then
       abs(r)*||. f/.x1-f/.x2.|| <= abs(r)*(s*||. x1-x2.||) by A10,AXIOMS:25;
       hence ||. (r(#)f)/.x1-(r(#)f)/.x2.|| <= g*||. x1-x2.|| by A9;
     end;
   end;
   hence thesis;
end;

theorem Th129:
for f be PartFunc of RNS,CNS st f is_Lipschitzian_on X holds
  z(#)f is_Lipschitzian_on X
proof
   let f be PartFunc of RNS,CNS;
   assume
A1: f is_Lipschitzian_on X; then
   X c= dom f by Def29;
   hence
A2: X c= dom (z(#)f) by VFUNCT_2:def 4;
   consider s be Real such that
A3: 0 < s &
    for x1,x2 be Point of RNS st x1 in X & x2 in X holds
      ||. f/.x1-f/.x2.||<=s*||. x1-x2.|| by A1,Def29;
   now per cases;
     suppose
A4:    z=0;
       take s;
       thus 0<s by A3;
       let x1,x2 be Point of RNS;
       assume
A5:     x1 in X & x2 in X; then
A6:    ||. (z(#)f)/.x1-(z(#)f)/.x2.||
        = ||. z*(f/.x1)-(z(#)f)/.x2.|| by A2,VFUNCT_2:def 4
       .= ||. 0.CNS-(z(#)f)/.x2.|| by A4,CLVECT_1:2
       .= ||. 0.CNS - z*(f/.x2).|| by A2,A5,VFUNCT_2:def 4
       .= ||. 0.CNS-0.CNS.|| by A4,CLVECT_1:2
       .= ||. 0.CNS.|| by RLVECT_1:26
       .= 0 by CLVECT_1:103;
       0<=||. x1-x2.|| by NORMSP_1:8; then
       s*0<=s*||. x1-x2.|| by A3,AXIOMS:25;
       hence ||. (z(#)f)/.x1-(z(#)f)/.x2.|| <=s*||. x1-x2.|| by A6;
     end;
     suppose
       z<>0; then
       0<|.z.| by COMPLEX1:133; then
A7:    0*s<|.z.|*s by A3,REAL_1:70;
       take g = |.z.| *s; thus 0<g by A7;
       let x1,x2 be Point of RNS;
       assume
A8:     x1 in X & x2 in X;
A9:   ||. (z(#)f)/.x1-(z(#)f)/.x2.||
        = ||. z*(f/.x1)-(z(#)f)/.x2.|| by A2,A8,VFUNCT_2:def 4
       .= ||. z*(f/.x1) - z*(f/.x2).|| by A2,A8,VFUNCT_2:def 4
       .= ||. z*(f/.x1 - f/.x2).|| by CLVECT_1:10
       .= |.z.|*||. f/.x1 - f/.x2.|| by CLVECT_1:def 10;
A10:   0<=|.z.| by COMPLEX1:132;
       ||. f/.x1-f/.x2 .||<=s*||. x1-x2.|| by A3,A8; then
       |.z.|*||. f/.x1-f/.x2.|| <= |.z.|*(s*||. x1-x2.||)
       by A10,AXIOMS:25;
       hence ||. (z(#)f)/.x1-(z(#)f)/.x2.|| <= g*||. x1-x2.|| by A9;
     end;
   end;
   hence thesis;
end;

theorem
for f be PartFunc of CNS1,CNS2 st f is_Lipschitzian_on X holds
 -f is_Lipschitzian_on X & ||.f.|| is_Lipschitzian_on X
proof
   let f be PartFunc of CNS1,CNS2;
   assume
A1: f is_Lipschitzian_on X;
   -f = (-1r)(#)f by VFUNCT_2:23;
   hence -f is_Lipschitzian_on X by A1,Th127;
   X c= dom f by A1,Def27;
   hence
A2: X c= dom (||.f.|| ) by Def2;
   consider s be Real such that
A3: 0 < s & for x1,x2 be Point of CNS1 st x1 in X & x2 in X holds
     ||. f/.x1-f/.x2.||<=s*||. x1-x2.|| by A1,Def27;
   take s;
   thus 0<s by A3;
   let x1,x2 be Point of CNS1;
   assume
A4: x1 in X & x2 in X; then
   abs( (||.f.|| )/.x1-(||.f.|| )/.x2 )
    = abs( (||.f.|| ).x1-(||.f.|| )/.x2 ) by A2,FINSEQ_4:def 4
   .= abs( (||.f.|| ).x1-(||.f.|| ).x2 ) by A2,A4,FINSEQ_4:def 4
   .= abs( ||. f/.x1.||-(||.f.|| ).x2 ) by A2,A4,Def2
   .= abs(||. f/.x1.|| - ||. f/.x2 .||) by A2,A4,Def2; then
A5:abs( (||.f.|| )/.x1-(||.f.|| )/.x2 ) <= ||. f/.x1-f/.x2.||
   by CLVECT_1:111;
   ||. f/.x1-f/.x2 .|| <= s*||. x1-x2.|| by A3,A4;
   hence thesis by A5,AXIOMS:22;
end;

theorem
for f be PartFunc of CNS,RNS st f is_Lipschitzian_on X holds
 -f is_Lipschitzian_on X & ||.f.|| is_Lipschitzian_on X
proof
   let f be PartFunc of CNS,RNS;
   assume
A1: f is_Lipschitzian_on X;
   -f = (-1)(#)f by VFUNCT_1:29;
   hence -f is_Lipschitzian_on X by A1,Th128;
   X c= dom f by A1,Def28;
   hence
A2: X c= dom (||.f.|| ) by Def3;
   consider s be Real such that
A3: 0 < s & for x1,x2 be Point of CNS st x1 in X & x2 in X holds
     ||. f/.x1-f/.x2.||<=s*||. x1-x2.|| by A1,Def28;
   take s;
   thus 0<s by A3;
   let x1,x2 be Point of CNS;
   assume
A4: x1 in X & x2 in X; then
   abs( (||.f.|| )/.x1-(||.f.|| )/.x2 )
    = abs( (||.f.|| ).x1-(||.f.|| )/.x2 ) by A2,FINSEQ_4:def 4
   .= abs( (||.f.|| ).x1-(||.f.|| ).x2 ) by A2,A4,FINSEQ_4:def 4
   .= abs( ||. f/.x1.||-(||.f.|| ).x2 ) by A2,A4,Def3
   .= abs(||. f/.x1.|| - ||. f/.x2 .||) by A2,A4,Def3; then
A5:abs( (||.f.|| )/.x1-(||.f.|| )/.x2 ) <= ||. f/.x1-f/.x2.||
   by NORMSP_1:13;
   ||. f/.x1-f/.x2 .|| <= s*||. x1-x2.|| by A3,A4;
   hence thesis by A5,AXIOMS:22;
end;

theorem
for f be PartFunc of RNS,CNS st f is_Lipschitzian_on X holds
 -f is_Lipschitzian_on X & ||.f.|| is_Lipschitzian_on X
proof
   let f be PartFunc of RNS,CNS;
   assume
A1: f is_Lipschitzian_on X;
   -f = (-1r)(#)f by VFUNCT_2:23;
   hence -f is_Lipschitzian_on X by A1,Th129;
   X c= dom f by A1,Def29;
   hence
A2: X c= dom (||.f.|| ) by Def4;
   consider s be Real such that
A3: 0 < s & for x1,x2 be Point of RNS st x1 in X & x2 in X holds
     ||. f/.x1-f/.x2.||<=s*||. x1-x2.|| by A1,Def29;
   take s;
   thus 0<s by A3;
   let x1,x2 be Point of RNS;
   assume
A4: x1 in X & x2 in X; then
   abs( (||.f.|| )/.x1-(||.f.|| )/.x2 )
    = abs( (||.f.|| ).x1-(||.f.|| )/.x2 ) by A2,FINSEQ_4:def 4
   .= abs( (||.f.|| ).x1-(||.f.|| ).x2 ) by A2,A4,FINSEQ_4:def 4
   .= abs( ||. f/.x1.||-(||.f.|| ).x2 ) by A2,A4,Def4
   .= abs(||. f/.x1.|| - ||. f/.x2 .||) by A2,A4,Def4; then
A5:abs( (||.f.|| )/.x1-(||.f.|| )/.x2 ) <= ||. f/.x1-f/.x2.||
   by CLVECT_1:111;
   ||. f/.x1-f/.x2 .|| <= s*||. x1-x2.|| by A3,A4;
   hence thesis by A5,AXIOMS:22;
end;

theorem Th133:
for X be set, f be PartFunc of CNS1,CNS2 st
 X c= dom f & f is_constant_on X holds f is_Lipschitzian_on X
proof
   let X be set;
   let f be PartFunc of CNS1,CNS2;
   assume
A1: X c= dom f & f is_constant_on X;
   now let x1,x2 be Point of CNS1;
     assume
A2:   x1 in X & x2 in X; then
A3:  x1 in X /\ dom f & x2 in X /\ dom f by A1,XBOOLE_0:def 3;
     f/.x1= f.x1 by A1,A2,FINSEQ_4:def 4
         .= f.x2 by A1,A3,PARTFUN2:77
         .= f/.x2 by A1,A2,FINSEQ_4:def 4; then
     ||. f/.x1-f/.x2.|| = ||. 0.CNS2 .|| by RLVECT_1:28
     .= 0 by CLVECT_1:103;
     hence ||. f/.x1-f/.x2.|| <= 1*||. x1-x2 .|| by CLVECT_1:106;
   end;
   hence thesis by A1,Def27;
end;

theorem Th134:
for X be set, f be PartFunc of CNS,RNS st
 X c= dom f & f is_constant_on X holds f is_Lipschitzian_on X
proof
   let X be set;
   let f be PartFunc of CNS,RNS;
   assume
A1: X c= dom f & f is_constant_on X;
   now let x1,x2 be Point of CNS;
     assume
A2:   x1 in X & x2 in X; then
A3:  x1 in X /\ dom f & x2 in X /\ dom f by A1,XBOOLE_0:def 3;
     f/.x1= f.x1 by A1,A2,FINSEQ_4:def 4
         .= f.x2 by A1,A3,PARTFUN2:77
         .= f/.x2 by A1,A2,FINSEQ_4:def 4; then
     ||. f/.x1-f/.x2.|| = ||. 0.RNS .|| by RLVECT_1:28
     .= 0 by NORMSP_1:5;
     hence ||. f/.x1-f/.x2.|| <= 1*||. x1-x2 .|| by CLVECT_1:106;
   end;
   hence thesis by A1,Def28;
end;

theorem Th135:
for X be set, f be PartFunc of RNS,CNS st
 X c= dom f & f is_constant_on X holds f is_Lipschitzian_on X
proof
   let X be set;
   let f be PartFunc of RNS,CNS;
   assume
A1: X c= dom f & f is_constant_on X;
   now let x1,x2 be Point of RNS;
     assume
A2:   x1 in X & x2 in X; then
A3:  x1 in X /\ dom f & x2 in X /\ dom f by A1,XBOOLE_0:def 3;
     f/.x1= f.x1 by A1,A2,FINSEQ_4:def 4
         .= f.x2 by A1,A3,PARTFUN2:77
         .= f/.x2 by A1,A2,FINSEQ_4:def 4; then
     ||. f/.x1-f/.x2.|| = ||. 0.CNS .|| by RLVECT_1:28
     .= 0 by CLVECT_1:103;
     hence ||. f/.x1-f/.x2.|| <= 1*||. x1-x2 .|| by NORMSP_1:8;
   end;
   hence thesis by A1,Def29;
end;

theorem
for Y be Subset of CNS holds id Y is_Lipschitzian_on Y
proof
   let Y be Subset of CNS;
   thus Y c= dom(id Y) by RELAT_1:71;
   reconsider r=1 as Real;
   take r;
   thus r>0;
   let x1,x2 be Point of CNS;
   assume
A1: x1 in Y & x2 in Y; then
   ||. (id Y)/.x1-(id Y)/.x2 .|| = ||. x1-(id Y)/.x2 .|| by PARTFUN2:12
   .= r*||. x1-x2 .|| by A1,PARTFUN2:12;
   hence thesis;
end;

theorem Th137:
for f be PartFunc of CNS1,CNS2 st f is_Lipschitzian_on X holds
  f is_continuous_on X
proof
   let f be PartFunc of CNS1,CNS2;
   assume
A1: f is_Lipschitzian_on X; then
   consider r be Real such that
A2: 0 < r &
    for x1,x2 be Point of CNS1 st x1 in X & x2 in X holds
      ||. f/.x1-f/.x2.||<=r*||. x1-x2.|| by Def27;
A3:X c= dom f by A1,Def27; then
A4:dom (f|X) = X by RELAT_1:91;
   now
     let x0 be Point of CNS1 such that
A5:   x0 in X;
     now
       let g be Real such that
A6:     0<g;
       set s=g/r;
       take s'=s;
A7:    0<r" by A2,REAL_1:72;
A8:    s'=g*r" by XCMPLX_0:def 9;
       now let x1 be Point of CNS1;
         assume
A9:       x1 in dom (f|X) & ||. x1- x0 .|| <s; then
         r*||. x1- x0 .|| <(g/r)*r by A2,REAL_1:70; then
A10:     r*||. x1- x0 .|| <g by A2,XCMPLX_1:88;
         ||. f/.x1- f/.x0 .|| <=r*||. x1- x0 .|| by A2,A4,A5,A9; then
         ||. f/.x1- f/.x0 .|| <g by A10,AXIOMS:22; then
         ||. (f|X)/.x1-f/.x0 .||<g by A9,PARTFUN2:32;
         hence ||. (f|X)/.x1-(f|X)/.x0 .||<g by A4,A5,PARTFUN2:32;
       end;
       hence 0<s' &
        for x1 be Point of CNS1 st x1 in dom (f|X) & ||. x1- x0 .|| <s' holds
          ||. (f|X)/.x1-(f|X)/.x0.||<g by A6,A7,A8,SQUARE_1:21;
     end;
     hence f|X is_continuous_in x0 by A4,A5,Th29;
   end;
   hence thesis by A3,Def21;
end;

theorem Th138:
for f be PartFunc of CNS,RNS st f is_Lipschitzian_on X holds
  f is_continuous_on X
proof
   let f be PartFunc of CNS,RNS;
   assume
A1: f is_Lipschitzian_on X; then
   consider r be Real such that
A2: 0 < r &
    for x1,x2 be Point of CNS st x1 in X & x2 in X holds
      ||. f/.x1-f/.x2.||<=r*||. x1-x2.|| by Def28;
A3:X c= dom f by A1,Def28; then
A4:dom (f|X) = X by RELAT_1:91;
   now
     let x0 be Point of CNS such that
A5:   x0 in X;
     now
       let g be Real such that
A6:     0<g;
       set s=g/r;
       take s'=s;
A7:    0<r" by A2,REAL_1:72;
A8:    s'=g*r" by XCMPLX_0:def 9;
       now let x1 be Point of CNS;
         assume
A9:       x1 in dom (f|X) & ||. x1- x0 .|| <s; then
         r*||. x1- x0 .|| <(g/r)*r by A2,REAL_1:70; then
A10:     r*||. x1- x0 .|| <g by A2,XCMPLX_1:88;
         ||. f/.x1- f/.x0 .|| <=r*||. x1- x0 .|| by A2,A4,A5,A9; then
         ||. f/.x1- f/.x0 .|| <g by A10,AXIOMS:22; then
         ||. (f|X)/.x1-f/.x0 .||<g by A9,PARTFUN2:32;
         hence ||. (f|X)/.x1-(f|X)/.x0 .||<g by A4,A5,PARTFUN2:32;
       end;
       hence
        0<s' &
        for x1 be Point of CNS st x1 in dom (f|X) & ||. x1- x0 .|| <s' holds
          ||. (f|X)/.x1-(f|X)/.x0.||<g by A6,A7,A8,SQUARE_1:21;
     end;
     hence f|X is_continuous_in x0 by A4,A5,Th30;
   end;
   hence thesis by A3,Def22;
end;

theorem Th139:
for f be PartFunc of RNS,CNS st f is_Lipschitzian_on X holds
  f is_continuous_on X
proof
   let f be PartFunc of RNS,CNS;
   assume
A1: f is_Lipschitzian_on X; then
   consider r be Real such that
A2: 0 < r &
    for x1,x2 be Point of RNS st x1 in X & x2 in X holds
      ||. f/.x1-f/.x2.||<=r*||. x1-x2.|| by Def29;
A3:X c= dom f by A1,Def29; then
A4:dom (f|X) = X by RELAT_1:91;
   now
     let x0 be Point of RNS such that
A5:   x0 in X;
     now
       let g be Real such that
A6:     0<g;
       set s=g/r;
       take s'=s;
A7:    0<r" by A2,REAL_1:72;
A8:    s'=g*r" by XCMPLX_0:def 9;
       now let x1 be Point of RNS;
         assume
A9:       x1 in dom (f|X) & ||. x1- x0 .|| <s; then
         r*||. x1- x0 .|| <(g/r)*r by A2,REAL_1:70; then
A10:     r*||. x1- x0 .|| <g by A2,XCMPLX_1:88;
         ||. f/.x1- f/.x0 .|| <=r*||. x1- x0 .|| by A2,A4,A5,A9; then
         ||. f/.x1- f/.x0 .|| <g by A10,AXIOMS:22; then
         ||. (f|X)/.x1-f/.x0 .||<g by A9,PARTFUN2:32;
         hence ||. (f|X)/.x1-(f|X)/.x0 .||<g by A4,A5,PARTFUN2:32;
       end;
       hence
        0<s' &
        for x1 be Point of RNS st x1 in dom (f|X) & ||. x1- x0 .|| <s' holds
          ||. (f|X)/.x1-(f|X)/.x0.||<g by A6,A7,A8,SQUARE_1:21;
     end;
     hence f|X is_continuous_in x0 by A4,A5,Th31;
   end;
   hence thesis by A3,Def23;
end;

theorem
for f be PartFunc of the carrier of CNS,COMPLEX st
 f is_Lipschitzian_on X holds f is_continuous_on X
proof
   let f be PartFunc of the carrier of CNS,COMPLEX;
   assume
A1: f is_Lipschitzian_on X; then
   consider r be Real such that
A2: 0 < r &
    for x1,x2 be Point of CNS st x1 in X & x2 in X holds
      |.(f/.x1-f/.x2).|<=r*||. x1-x2.|| by Def30;
A3:X c= dom f by A1,Def30; then
A4:dom (f|X) = X by RELAT_1:91;
   now
     let x0 be Point of CNS such that
A5:   x0 in X;
     now
       let g be Real such that
A6:    0<g;
       set s=g/r;
       take s'=s;
A7:    0<r" by A2,REAL_1:72;
A8:    s'=g*r" by XCMPLX_0:def 9;
       now
         let x1 be Point of CNS;
         assume
A9:       x1 in dom (f|X) & ||. x1- x0 .|| <s; then
         r*||. x1- x0 .|| <(g/r)*r by A2,REAL_1:70; then
A10:     r*||. x1- x0 .|| <g by A2,XCMPLX_1:88;
         |.( f/.x1- f/.x0 ).| <=r*||. x1- x0 .|| by A2,A4,A5,A9; then
         |.( f/.x1- f/.x0 ).| <g by A10,AXIOMS:22; then
         |.( (f|X)/.x1-f/.x0 ).| <g by A9,PARTFUN2:32;
         hence |.( (f|X)/.x1-(f|X)/.x0 ).| <g by A4,A5,PARTFUN2:32;
       end;
       hence
        0<s' &
        for x1 be Point of CNS st x1 in dom (f|X) & ||. x1- x0 .|| <s' holds
          |.( (f|X)/.x1-(f|X)/.x0).| <g by A6,A7,A8,SQUARE_1:21;
     end;
     hence f|X is_continuous_in x0 by A4,A5,Th33;
   end;
   hence thesis by A3,Def24;
end;

theorem Th141:
for f be PartFunc of the carrier of CNS,REAL st
 f is_Lipschitzian_on X holds f is_continuous_on X
proof
   let f be PartFunc of the carrier of CNS,REAL;
   assume
A1: f is_Lipschitzian_on X; then
   consider r be Real such that
A2: 0 < r &
    for x1,x2 be Point of CNS st x1 in X & x2 in X holds
      abs(f/.x1-f/.x2)<=r*||. x1-x2.|| by Def31;
A3:X c= dom f by A1,Def31; then
A4:dom (f|X) = X by RELAT_1:91;
   now
     let x0 be Point of CNS such that
A5:   x0 in X;
     now
       let g be Real such that
A6:    0<g;
       set s=g/r;
       take s'=s;
A7:    0<r" by A2,REAL_1:72;
A8:    s'=g*r" by XCMPLX_0:def 9;
       now
         let x1 be Point of CNS;
         assume
A9:       x1 in dom (f|X) & ||. x1- x0 .|| <s; then
         r*||. x1- x0 .|| <(g/r)*r by A2,REAL_1:70; then
A10:     r*||. x1- x0 .|| <g by A2,XCMPLX_1:88;
         abs( f/.x1- f/.x0 ) <=r*||. x1- x0 .|| by A2,A4,A5,A9; then
         abs( f/.x1- f/.x0 ) <g by A10,AXIOMS:22; then
         abs( (f|X)/.x1-f/.x0 ) <g by A9,PARTFUN2:32;
         hence abs( (f|X)/.x1-(f|X)/.x0 ) <g by A4,A5,PARTFUN2:32;
       end;
       hence 0<s' &
        for x1 be Point of CNS st x1 in dom (f|X) & ||. x1- x0 .|| <s' holds
          abs( (f|X)/.x1-(f|X)/.x0) <g by A6,A7,A8,SQUARE_1:21;
     end;
     hence f|X is_continuous_in x0 by A4,A5,Th32;
   end;
   hence thesis by A3,Def25;
end;

theorem
for f be PartFunc of the carrier of RNS,COMPLEX st
 f is_Lipschitzian_on X holds f is_continuous_on X
proof
   let f be PartFunc of the carrier of RNS,COMPLEX;
   assume
A1: f is_Lipschitzian_on X; then
   consider r be Real such that
A2: 0 < r &
    for x1,x2 be Point of RNS st x1 in X & x2 in X holds
      |.(f/.x1-f/.x2).|<=r*||. x1-x2.|| by Def32;
A3:X c= dom f by A1,Def32; then
A4:dom (f|X) = X by RELAT_1:91;
   now
     let x0 be Point of RNS such that
A5:   x0 in X;
     now
       let g be Real such that
A6:    0<g;
       set s=g/r;
       take s'=s;
A7:    0<r" by A2,REAL_1:72;
A8:    s'=g*r" by XCMPLX_0:def 9;
       now
         let x1 be Point of RNS;
         assume
A9:      x1 in dom (f|X) & ||. x1- x0 .|| <s; then
         r*||. x1- x0 .|| <(g/r)*r by A2,REAL_1:70; then
A10:     r*||. x1- x0 .|| <g by A2,XCMPLX_1:88;
         |.( f/.x1- f/.x0 ).| <=r*||. x1- x0 .|| by A2,A4,A5,A9; then
         |.( f/.x1- f/.x0 ).| <g by A10,AXIOMS:22; then
         |.( (f|X)/.x1-f/.x0 ).| <g by A9,PARTFUN2:32;
         hence |.( (f|X)/.x1-(f|X)/.x0 ).| <g by A4,A5,PARTFUN2:32;
       end;
       hence
        0<s' &
        for x1 be Point of RNS st x1 in dom (f|X) & ||. x1- x0 .|| <s' holds
          |.( (f|X)/.x1-(f|X)/.x0).| <g by A6,A7,A8,SQUARE_1:21;
     end;
     hence f|X is_continuous_in x0 by A4,A5,Th34;
   end;
   hence thesis by A3,Def26;
end;

theorem
for f be PartFunc of CNS1,CNS2 st (ex r be Point of CNS2 st rng f = {r}) holds
  f is_continuous_on (dom f)
proof
   let f be PartFunc of CNS1,CNS2;
   given r be Point of CNS2 such that
A1: rng f = {r};
   now let x1,x2 be Point of CNS1;
     assume
A2:   x1 in dom f & x2 in dom f; then
     f.x1 in rng f & f.x2 in rng f by FUNCT_1:def 5; then
     f/.x1 in rng f & f/.x2 in rng f by A2,FINSEQ_4:def 4; then
     f/.x1=r & f/.x2=r by A1,TARSKI:def 1; then
     ||. f/.x1-f/.x2.|| = ||. 0.CNS2 .|| by RLVECT_1:28
     .= 0 by CLVECT_1:103;
     hence ||. f/.x1-f/.x2.|| <= 1*||. x1-x2.|| by CLVECT_1:106;
   end; then
   f is_Lipschitzian_on dom f by Def27;
   hence thesis by Th137;
end;

theorem
for f be PartFunc of CNS,RNS st (ex r be Point of RNS st rng f = {r}) holds
  f is_continuous_on (dom f)
proof
   let f be PartFunc of CNS,RNS;
   given r be Point of RNS such that
A1: rng f = {r};
   now let x1,x2 be Point of CNS;
     assume
A2:   x1 in dom f & x2 in dom f; then
     f.x1 in rng f & f.x2 in rng f by FUNCT_1:def 5; then
     f/.x1 in rng f & f/.x2 in rng f by A2,FINSEQ_4:def 4; then
     f/.x1=r & f/.x2=r by A1,TARSKI:def 1; then
     ||. f/.x1-f/.x2.|| = ||. 0.RNS .|| by RLVECT_1:28
     .= 0 by NORMSP_1:5;
     hence ||. f/.x1-f/.x2.|| <= 1*||. x1-x2.|| by CLVECT_1:106;
   end; then
   f is_Lipschitzian_on dom f by Def28;
   hence thesis by Th138;
end;

theorem
for f be PartFunc of RNS,CNS st (ex r be Point of CNS st rng f = {r}) holds
  f is_continuous_on (dom f)
proof
   let f be PartFunc of RNS,CNS;
   given r be Point of CNS such that
A1: rng f = {r};
   now let x1,x2 be Point of RNS;
     assume
A2:   x1 in dom f & x2 in dom f; then
     f.x1 in rng f & f.x2 in rng f by FUNCT_1:def 5; then
     f/.x1 in rng f & f/.x2 in rng f by A2,FINSEQ_4:def 4; then
     f/.x1=r & f/.x2=r by A1,TARSKI:def 1; then
     ||. f/.x1-f/.x2.|| = ||. 0.CNS .|| by RLVECT_1:28
     .= 0 by CLVECT_1:103;
     hence ||. f/.x1-f/.x2.|| <= 1*||. x1-x2.|| by NORMSP_1:8;
   end; then
   f is_Lipschitzian_on dom f by Def29;
   hence thesis by Th139;
end;

theorem
for f be PartFunc of CNS1,CNS2 st
  X c= dom f & f is_constant_on X holds f is_continuous_on X
proof
   let f be PartFunc of CNS1,CNS2;
   assume X c= dom f & f is_constant_on X; then
   f is_Lipschitzian_on X by Th133;
   hence thesis by Th137;
end;

theorem
for f be PartFunc of CNS,RNS st
  X c= dom f & f is_constant_on X holds f is_continuous_on X
proof
   let f be PartFunc of CNS,RNS;
   assume X c= dom f & f is_constant_on X; then
   f is_Lipschitzian_on X by Th134;
   hence thesis by Th138;
end;

theorem
for f be PartFunc of RNS,CNS st
  X c= dom f & f is_constant_on X holds f is_continuous_on X
proof
   let f be PartFunc of RNS,CNS;
   assume X c= dom f & f is_constant_on X; then
   f is_Lipschitzian_on X by Th135;
   hence thesis by Th139;
end;

theorem Th149:
for f be PartFunc of CNS,CNS st
 (for x0 be Point of CNS st x0 in dom f holds f/.x0 = x0) holds
   f is_continuous_on dom f
proof
   let f be PartFunc of CNS,CNS such that
A1: for x0 be Point of CNS st x0 in dom f holds f/.x0 = x0;
   now
     let x1,x2 be Point of CNS;
     assume x1 in dom f & x2 in dom f; then
     f/.x1 = x1 & f/.x2 = x2 by A1;
     hence ||. f/.x1-f/.x2.|| <= 1*||. x1-x2.||;
   end; then
   f is_Lipschitzian_on dom f by Def27;
   hence thesis by Th137;
end;

theorem
for f be PartFunc of CNS,CNS st f = id dom f holds
  f is_continuous_on dom f
proof
   let f be PartFunc of CNS,CNS;
   assume
A1: f = id dom f;
   now
     let x0 be Point of CNS such that
A2:   x0 in dom f;
     thus f/.x0=f.x0 by A2,FINSEQ_4:def 4
              .= x0 by A1,A2,FUNCT_1:34;
   end;
   hence thesis by Th149;
end;

theorem
for f be PartFunc of CNS,CNS, Y be Subset of CNS st
 Y c= dom f & f|Y = id Y holds f is_continuous_on Y
proof
   let f be PartFunc of CNS,CNS;
   let Y be Subset of CNS;
   assume
A1: Y c= dom f & f|Y = id Y;
   now
     let x1,x2 be Point of CNS;
     assume
A2:   x1 in Y & x2 in Y; then
     x1 in dom f /\ Y & x2 in dom f /\ Y by A1,XBOOLE_0:def 3; then
A3:  x1 in dom (f|Y) & x2 in dom (f|Y) by RELAT_1:90;
     (f|Y).x1 = x1 & (f|Y).x2 = x2 by A1,A2,FUNCT_1:34; then
     f.x1 = x1 & f.x2 = x2 by A3,FUNCT_1:68; then
     f/.x1 = x1 & f/.x2 = x2 by A1,A2,FINSEQ_4:def 4;
     hence ||. f/.x1-f/.x2.|| <= 1*||. x1-x2.||;
   end; then
   f is_Lipschitzian_on Y by A1,Def27;
   hence thesis by Th137;
end;

theorem
for f be PartFunc of CNS,CNS, z be Complex, p be Point of CNS st
 X c= dom f &
 (for x0 be Point of CNS st x0 in X holds f/.x0 = z*x0+p) holds
   f is_continuous_on X
proof
   let f be PartFunc of CNS,CNS;
   let z be Complex;
   let p be Point of CNS;
   assume that
A1: X c= dom f and
A2: for x0 be Point of CNS st x0 in X holds f/.x0 = z*x0+p;
   now
     0 <= |.z.| by COMPLEX1:132; then
     0 + 0 < |.z.| + 1 by REAL_1:67;
     hence 0 < |.z.|+1;
     let x1,x2 be Point of CNS;
     assume x1 in X & x2 in X; then
A3:  f/.x1 = z*x1+p & f/.x2 = z*x2+p by A2;
A4:  ||. f/.x1-f/.x2.||
      = ||. z*x1+(p-(p+z*x2)).|| by A3,RLVECT_1:42
     .= ||. z*x1+(p-p-z*x2).|| by RLVECT_1:41
     .= ||. z*x1+(0.CNS-z*x2).|| by RLVECT_1:28
     .= ||. z*x1+-z*x2.|| by RLVECT_1:27
     .= ||. z*x1-z*x2.|| by RLVECT_1:def 11
     .= ||. z*(x1-x2).|| by CLVECT_1:10
     .= |.z.|*||. x1-x2.|| by CLVECT_1:def 10;
     0<=||. x1-x2.|| by CLVECT_1:106; then
     ||. f/.x1-f/.x2.|| + 0 <= |.z.|*||. x1-x2.|| + 1*||. x1-x2.||
     by A4,REAL_1:55;
     hence ||. f/.x1-f/.x2.|| <= (|.z.|+1)*||. x1-x2.||;
   end; then
   f is_Lipschitzian_on X by A1,Def27;
   hence thesis by Th137;
end;

theorem Th153:
for f be PartFunc of the carrier of CNS,REAL st
  (for x0 be Point of CNS st x0 in dom f holds f/.x0 = ||. x0.|| ) holds
    f is_continuous_on dom f
proof
   let f be PartFunc of the carrier of CNS,REAL;
   assume
A1: for x0 be Point of CNS st x0 in dom f holds f/.x0 = ||. x0 .||;
   now
     let x1,x2 be Point of CNS;
     assume x1 in dom f & x2 in dom f; then
A2:  f/.x1 = ||. x1 .|| & f/.x2 = ||. x2 .|| by A1;
A3:  ||. x1 .|| - ||. x2 .|| <= ||.x1 - x2.|| by CLVECT_1:110;
     ||. x2 .|| - ||. x1 .|| <= ||.x2 - x1.|| by CLVECT_1:110; then
     ||. x2 .|| - ||. x1 .|| <= ||.x1 - x2.|| by CLVECT_1:109; then
     -(-(||. x1 .|| - ||. x2 .||)) >= - (||.x1 - x2.||) by REAL_1:50;
     hence abs(f/.x1 - f/.x2) <= 1*||. x1 - x2 .|| by A2,A3,ABSVALUE:12;
   end; then
   f is_Lipschitzian_on dom f by Def31;
   hence thesis by Th141;
end;

theorem
for f be PartFunc of the carrier of CNS,REAL st
  X c= dom f &
  (for x0 be Point of CNS st x0 in X holds f/.x0 = ||. x0.||) holds
   f is_continuous_on X
proof
   let f be PartFunc of the carrier of CNS,REAL;
   assume
A1: X c= dom f &
    (for x0 be Point of CNS st x0 in X holds f/.x0 = ||. x0 .||); then
   X=dom f /\ X by XBOOLE_1:28; then
A2:X=dom(f|X) by RELAT_1:90;
   now
     let x0 be Point of CNS;
     assume
A3:  x0 in dom(f|X);
     thus (f|X)/.x0=f/.x0 by A3,PARTFUN2:32
                       .=||. x0 .|| by A1,A2,A3;
   end; then
   f|X is_continuous_on X by A2,Th153;
   hence thesis by Th75;
end;
