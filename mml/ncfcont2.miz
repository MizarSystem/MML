:: Uniform Continuity of Functions on Normed Complex Linear Spaces
::  by Noboru Endou
::
:: Received October 6, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies PRE_TOPC, FCONT_1, RLVECT_1, FUNCT_1, ARYTM, ARYTM_1, ARYTM_3,
      RELAT_1, ABSVALUE, ORDINAL2, SEQ_1, SEQ_2, SEQM_3, COMPTS_1, BOOLE,
      FINSEQ_4, SEQ_4, PARTFUN1, PARTFUN2, NORMSP_1, CLVECT_1, COMPLEX1,
      CFUNCT_1, FCONT_2, SQUARE_1, CLOPBAN1, ALI2, FUNCOP_1, POWER, RSSPACE3,
      FUNCT_4, FRECHET2;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0,
      REAL_1, NAT_1, RELAT_1, FUNCT_1, RELSET_1, PARTFUN1, FUNCT_2, PARTFUN2,
      PRE_TOPC, STRUCT_0, FINSEQ_4, RLVECT_1, FUNCOP_1, ALG_1, COMPLEX1, SEQ_1,
      SEQ_2, SEQM_3, SEQ_4, FUNCT_7, VFUNCT_1, NORMSP_1, SQUARE_1, POWER,
      CFUNCT_1, VFUNCT_2, CLVECT_1,
      BHSP_3, LOPBAN_3, NFCONT_1, CLOPBAN1, CLOPBAN3,
      NFCONT_2, NCFCONT1, CSSPACE3;
 constructors REAL_1, SEQ_2, SEQ_4, PREPOWER, PARTFUN1, PARTFUN2, FINSEQ_4,
      VFUNCT_1, LOPBAN_3, NFCONT_1, COMPLEX1, CLOPBAN3, COMSEQ_1, COMSEQ_3,
      VFUNCT_2, SQUARE_1, NCFCONT1, NFCONT_2, POWER, FUNCT_7, NAT_1, CSSPACE3,
      ARYTM_0, SEQ_1, BHSP_3;
 registrations RELSET_1, STRUCT_0, ORDINAL2, XREAL_0, MEMBERED, FUNCT_1,
      SEQM_3, SEQ_1;
 requirements SUBSET, REAL, BOOLE, NUMERALS, ARITHM;
 theorems AXIOMS, TARSKI, ABSVALUE, XBOOLE_1, REAL_1, RLVECT_1, XCMPLX_1,
      SEQ_2, NAT_1, SQUARE_1, RELAT_1, FUNCT_1, FINSEQ_4, VFUNCT_1, FUNCT_2,
      SEQ_4, NORMSP_1, LOPBAN_3, PARTFUN2, CLVECT_1, CLOPBAN3, VFUNCT_2,
      NFCONT_1, CLOPBAN1, COMPLEX1, NCFCONT1, NFCONT_2, FUNCOP_1, RELSET_1,
      REAL_2, POWER, CSSPACE3, FUNCT_7, SEQ_1, BHSP_3;
 schemes FUNCT_2, RECDEF_1, NAT_1;

begin :: Uniform Continuity of Functions on Real and Complex Normed Linear Spaces

 reserve X,X1 for set,
         r,s for Real,
         z for Complex,
         RNS for RealNormSpace,
         CNS,CNS1,CNS2 for ComplexNormSpace;

definition
  let X be set;
  let CNS1,CNS2 be ComplexNormSpace;
  let f be PartFunc of CNS1,CNS2;
  pred f is_uniformly_continuous_on X means :Def1:
  X c= dom f &
  for r st 0 < r ex s st
   0 < s & for x1,x2 be Point of CNS1 st
    x1 in X & x2 in X & ||.x1 - x2.|| < s holds ||. f/.x1 - f/.x2 .|| < r;
end;

definition
  let X be set;
  let RNS be RealNormSpace;
  let CNS be ComplexNormSpace;
  let f be PartFunc of CNS,RNS;
  pred f is_uniformly_continuous_on X means :Def2:
  X c= dom f &
  for r st 0 < r ex s st
  0 < s & for x1,x2 be Point of CNS st
   x1 in X & x2 in X & ||.x1 - x2.|| < s holds ||. f/.x1 - f/.x2 .|| < r;
end;

definition
  let X be set;
  let RNS be RealNormSpace;
  let CNS be ComplexNormSpace;
  let f be PartFunc of RNS,CNS;
  pred f is_uniformly_continuous_on X means :Def3:
  X c= dom f &
  for r st 0 < r ex s st
   0 < s & for x1,x2 be Point of RNS st
    x1 in X & x2 in X & ||.x1 - x2.|| < s holds ||. f/.x1 - f/.x2 .|| < r;
end;

definition
  let X be set;
  let CNS be ComplexNormSpace;
  let f be PartFunc of the carrier of CNS,COMPLEX;
  pred f is_uniformly_continuous_on X means :Def4:
  X c= dom f &
  for r st 0 < r ex s st
   0 < s & for x1,x2 be Point of CNS st x1 in X & x2 in X &
    ||.x1 - x2.|| < s holds |.(f/.x1 - f/.x2).| < r;
end;

definition
  let X be set;
  let CNS be ComplexNormSpace;
  let f be PartFunc of the carrier of CNS,REAL;
  pred f is_uniformly_continuous_on X means :Def5:
  X c= dom f &
  for r st 0 < r ex s st
   0 < s & for x1,x2 be Point of CNS st x1 in X & x2 in X &
   ||.x1 - x2.|| < s holds abs(f/.x1 - f/.x2) < r;
end;

definition
  let X be set;
  let RNS be RealNormSpace;
  let f be PartFunc of the carrier of RNS,COMPLEX;
  pred f is_uniformly_continuous_on X means :Def6:
  X c= dom f &
  for r st 0 < r ex s st
   0 < s & for x1,x2 be Point of RNS st x1 in X & x2 in X &
    ||.x1 - x2.|| < s holds |.(f/.x1 - f/.x2).| < r;
end;

theorem Th1:
 for f be PartFunc of CNS1,CNS2 st
  f is_uniformly_continuous_on X & X1 c= X holds
   f is_uniformly_continuous_on X1
proof
   let f be PartFunc of CNS1,CNS2;
   assume
A1: f is_uniformly_continuous_on X & X1 c= X; then
   X c= dom f by Def1;
   hence X1 c= dom f by A1,XBOOLE_1:1;
   let r;
   assume 0 < r; then
   consider s such that
A2: 0 < s &
    for x1,x2 be Point of CNS1 st
     x1 in X & x2 in X & ||.x1-x2.|| < s holds ||.f/.x1-f/.x2.||<r by A1,Def1;
   take s;
   thus 0 < s by A2;
   let x1,x2 be Point of CNS1;
   assume x1 in X1 & x2 in X1 & ||.x1-x2.||<s;
   hence thesis by A1,A2;
end;

theorem Th2:
  for f be PartFunc of CNS,RNS st
   f is_uniformly_continuous_on X & X1 c= X holds
    f is_uniformly_continuous_on X1
proof
   let f be PartFunc of CNS,RNS;
   assume
A1: f is_uniformly_continuous_on X & X1 c= X; then
   X c= dom f by Def2;
   hence X1 c= dom f by A1,XBOOLE_1:1;
   let r;
   assume 0 < r; then
   consider s such that
A2: 0 < s &
    for x1,x2 be Point of CNS st
     x1 in X & x2 in X & ||.x1-x2.|| < s holds ||.f/.x1-f/.x2.||<r by A1,Def2;
   take s;
   thus 0 < s by A2;
   let x1,x2 be Point of CNS;
   assume x1 in X1 & x2 in X1 & ||.x1-x2.||<s;
   hence thesis by A1,A2;
end;

theorem Th3:
  for f be PartFunc of RNS,CNS st
   f is_uniformly_continuous_on X & X1 c= X holds
    f is_uniformly_continuous_on X1
proof
   let f be PartFunc of RNS,CNS;
   assume
A1: f is_uniformly_continuous_on X & X1 c= X; then
   X c= dom f by Def3;
   hence X1 c= dom f by A1,XBOOLE_1:1;
   let r;
   assume 0 < r; then
   consider s such that
A2: 0 < s &
    for x1,x2 be Point of RNS st
     x1 in X & x2 in X & ||.x1-x2.|| < s holds ||.f/.x1-f/.x2.||<r by A1,Def3;
   take s;
   thus 0 < s by A2;
   let x1,x2 be Point of RNS;
   assume x1 in X1 & x2 in X1 & ||.x1-x2.||<s;
   hence thesis by A1,A2;
end;

theorem
  for f1,f2 be PartFunc of CNS1,CNS2 st
   f1 is_uniformly_continuous_on X & f2 is_uniformly_continuous_on X1 holds
  f1+f2 is_uniformly_continuous_on X/\X1
proof
   let f1,f2 be PartFunc of CNS1,CNS2;
   assume
A1: f1 is_uniformly_continuous_on X &
    f2 is_uniformly_continuous_on X1;
A2:X /\ X1 c= X & X /\ X1 c= X1 by XBOOLE_1:17; then
A3:f1 is_uniformly_continuous_on X /\ X1 by A1,Th1; then
A4:X /\ X1 c= dom f1 by Def1;
A5:f2 is_uniformly_continuous_on X /\ X1 by A1,A2,Th1; then
   X /\ X1 c= dom f2 by Def1; then
   X /\ X1 c= dom f1 /\ dom f2 by A4,XBOOLE_1:19;
   hence
A6: X /\ X1 c= dom (f1+f2) by VFUNCT_2:def 1;
   let r;
   assume 0 < r; then
A7:0<r/2 by SEQ_2:3; then
   consider s such that
A8: 0<s & for x1,x2 be Point of CNS1 st
     x1 in X /\ X1 & x2 in X /\X1 &
      ||.x1-x2.||<s holds ||.f1/.x1-f1/.x2.||<r/2 by A3,Def1;
   consider g be Real such that
A9: 0<g &
    for x1,x2 be Point of CNS1 st x1 in X /\ X1 & x2 in X /\ X1 &
     ||.x1-x2.|| < g holds ||.f2/.x1-f2/.x2.|| < r/2 by A5,A7,Def1;
   take p=min(s,g);
   thus 0<p by A8,A9,SQUARE_1:38;
A10:p <= s & p <= g by SQUARE_1:35;
   let x1,x2 be Point of CNS1;
   assume
A11:x1 in X/\X1 & x2 in X/\X1 & ||.x1-x2.||<p; then
   x1 in X/\X1 & x2 in X/\X1 & ||.x1-x2.||<s by A10,AXIOMS:22; then
A12: ||. f1/.x1 - f1/.x2 .|| < r/2 by A8;
   x1 in X/\X1 & x2 in X/\X1 & ||.x1-x2.||<g by A10,A11,AXIOMS:22; then
   ||.f2/.x1-f2/.x2.||<r/2 by A9; then
A13: ||.f1/.x1-f1/.x2.||+||.f2/.x1-f2/.x2.||<r/2+r/2 by A12,REAL_1:67;
A14: ||.(f1+f2)/.x1-(f1+f2)/.x2.|| = ||.f1/.x1 + f2/.x1-(f1+f2)/.x2.||
    by A6,A11,VFUNCT_2:def 1
   .= ||.f1/.x1 + f2/.x1 - (f1/.x2+f2/.x2).|| by A6,A11,VFUNCT_2:def 1
   .= ||.f1/.x1 + (f2/.x1 - (f1/.x2+f2/.x2)).|| by RLVECT_1:42
   .= ||.f1/.x1 + (f2/.x1 - f1/.x2-f2/.x2).|| by RLVECT_1:41
   .= ||.f1/.x1 + (-f1/.x2 + f2/.x1-f2/.x2).|| by RLVECT_1:def 11
   .= ||.f1/.x1 + (-f1/.x2 + (f2/.x1-f2/.x2)).|| by RLVECT_1:42
   .= ||.f1/.x1 +- f1/.x2 + (f2/.x1-f2/.x2).|| by RLVECT_1:def 6
   .= ||.f1/.x1 - f1/.x2 + (f2/.x1-f2/.x2).|| by RLVECT_1:def 11;
   ||.f1/.x1 - f1/.x2 + (f2/.x1-f2/.x2).||
     <= ||.f1/.x1-f1/.x2.|| + ||.f2/.x1-f2/.x2.|| by CLVECT_1:def 10;
   hence thesis by A13,A14,AXIOMS:22;
end;

theorem
  for f1,f2 be PartFunc of CNS,RNS st
   f1 is_uniformly_continuous_on X & f2 is_uniformly_continuous_on X1 holds
  f1+f2 is_uniformly_continuous_on X/\X1
proof
   let f1,f2 be PartFunc of CNS,RNS;
   assume
A1: f1 is_uniformly_continuous_on X &
    f2 is_uniformly_continuous_on X1;
A2:X /\ X1 c= X & X /\ X1 c= X1 by XBOOLE_1:17; then
A3:f1 is_uniformly_continuous_on X /\ X1 by A1,Th2; then
A4:X /\ X1 c= dom f1 by Def2;
A5:f2 is_uniformly_continuous_on X /\ X1 by A1,A2,Th2; then
   X /\ X1 c= dom f2 by Def2; then
   X /\ X1 c= dom f1 /\ dom f2 by A4,XBOOLE_1:19;
   hence
A6: X /\ X1 c= dom (f1+f2) by VFUNCT_1:def 1;
   let r;
   assume 0 < r; then
A7:0<r/2 by SEQ_2:3; then
   consider s such that
A8: 0<s & for x1,x2 be Point of CNS st
     x1 in X /\ X1 & x2 in X /\X1 &
      ||.x1-x2.||<s holds ||.f1/.x1-f1/.x2.||<r/2 by A3,Def2;
   consider g be Real such that
A9: 0<g &
    for x1,x2 be Point of CNS st x1 in X /\ X1 & x2 in X /\ X1 &
     ||.x1-x2.|| < g holds ||.f2/.x1-f2/.x2.|| < r/2 by A5,A7,Def2;
   take p=min(s,g);
   thus 0<p by A8,A9,SQUARE_1:38;
A10:p <= s & p <= g by SQUARE_1:35;
   let x1,x2 be Point of CNS;
   assume
A11:x1 in X/\X1 & x2 in X/\X1 & ||.x1-x2.||<p; then
   x1 in X/\X1 & x2 in X/\X1 & ||.x1-x2.||<s by A10,AXIOMS:22; then
A12: ||.f1/.x1-f1/.x2.||<r/2 by A8;
   x1 in X/\X1 & x2 in X/\X1 & ||.x1-x2.||<g by A10,A11,AXIOMS:22; then
   ||.f2/.x1-f2/.x2.||<r/2 by A9; then
A13: ||.f1/.x1-f1/.x2.||+||.f2/.x1-f2/.x2.||<r/2+r/2 by A12,REAL_1:67;
A14: ||.(f1+f2)/.x1-(f1+f2)/.x2.|| = ||.f1/.x1 + f2/.x1-(f1+f2)/.x2.||
    by A6,A11,VFUNCT_1:def 1
   .= ||.f1/.x1 + f2/.x1 - (f1/.x2+f2/.x2).|| by A6,A11,VFUNCT_1:def 1
   .= ||.f1/.x1 + (f2/.x1 - (f1/.x2+f2/.x2)).|| by RLVECT_1:42
   .= ||.f1/.x1 + (f2/.x1 - f1/.x2-f2/.x2).|| by RLVECT_1:41
   .= ||.f1/.x1 + (-f1/.x2 + f2/.x1-f2/.x2).|| by RLVECT_1:def 11
   .= ||.f1/.x1 + (-f1/.x2 + (f2/.x1-f2/.x2)).|| by RLVECT_1:42
   .= ||.f1/.x1 +- f1/.x2 + (f2/.x1-f2/.x2).|| by RLVECT_1:def 6
   .= ||.f1/.x1 - f1/.x2 + (f2/.x1-f2/.x2).|| by RLVECT_1:def 11;
   ||.f1/.x1 - f1/.x2 + (f2/.x1-f2/.x2).||
         <= ||.f1/.x1-f1/.x2.|| + ||.f2/.x1-f2/.x2.||
           by NORMSP_1:def 2;
   hence thesis by A13,A14,AXIOMS:22;
end;

theorem
  for f1,f2 be PartFunc of RNS,CNS st
    f1 is_uniformly_continuous_on X & f2 is_uniformly_continuous_on X1 holds
  f1+f2 is_uniformly_continuous_on X/\X1
proof
   let f1,f2 be PartFunc of RNS,CNS;
   assume
A1: f1 is_uniformly_continuous_on X &
    f2 is_uniformly_continuous_on X1;
A2:X /\ X1 c= X & X /\ X1 c= X1 by XBOOLE_1:17; then
A3:f1 is_uniformly_continuous_on X /\ X1 by A1,Th3; then
A4:X /\ X1 c= dom f1 by Def3;
A5:f2 is_uniformly_continuous_on X /\ X1 by A1,A2,Th3; then
   X /\ X1 c= dom f2 by Def3; then
   X /\ X1 c= dom f1 /\ dom f2 by A4,XBOOLE_1:19;
   hence
A6: X /\ X1 c= dom (f1+f2) by VFUNCT_2:def 1;
   let r;
   assume 0 < r; then
A7:0<r/2 by SEQ_2:3; then
   consider s such that
A8: 0<s & for x1,x2 be Point of RNS st
     x1 in X /\ X1 & x2 in X /\X1 &
      ||.x1-x2.||<s holds ||.f1/.x1-f1/.x2.||<r/2 by A3,Def3;
   consider g be Real such that
A9: 0<g &
    for x1,x2 be Point of RNS st x1 in X /\ X1 & x2 in X /\ X1 &
     ||.x1-x2.|| < g holds ||.f2/.x1-f2/.x2.|| < r/2 by A5,A7,Def3;
   take p=min(s,g);
   thus 0<p by A8,A9,SQUARE_1:38;
A10:p <= s & p <= g by SQUARE_1:35;
   let x1,x2 be Point of RNS;
   assume
A11:x1 in X/\X1 & x2 in X/\X1 & ||.x1-x2.||<p; then
   x1 in X/\X1 & x2 in X/\X1 & ||.x1-x2.||<s by A10,AXIOMS:22; then
A12: ||.f1/.x1-f1/.x2.||<r/2 by A8;
   x1 in X/\X1 & x2 in X/\X1 & ||.x1-x2.||<g by A10,A11,AXIOMS:22; then
   ||.f2/.x1-f2/.x2.||<r/2 by A9; then
A13: ||.f1/.x1-f1/.x2.||+||.f2/.x1-f2/.x2.||<r/2+r/2 by A12,REAL_1:67;
A14: ||.(f1+f2)/.x1-(f1+f2)/.x2.|| = ||.f1/.x1 + f2/.x1-(f1+f2)/.x2.||
    by A6,A11,VFUNCT_2:def 1
   .= ||.f1/.x1 + f2/.x1 - (f1/.x2+f2/.x2).|| by A6,A11,VFUNCT_2:def 1
   .= ||.f1/.x1 + (f2/.x1 - (f1/.x2+f2/.x2)).|| by RLVECT_1:42
   .= ||.f1/.x1 + (f2/.x1 - f1/.x2-f2/.x2).|| by RLVECT_1:41
   .= ||.f1/.x1 + (-f1/.x2 + f2/.x1-f2/.x2).|| by RLVECT_1:def 11
   .= ||.f1/.x1 + (-f1/.x2 + (f2/.x1-f2/.x2)).|| by RLVECT_1:42
   .= ||.f1/.x1 +- f1/.x2 + (f2/.x1-f2/.x2).|| by RLVECT_1:def 6
   .= ||.f1/.x1 - f1/.x2 + (f2/.x1-f2/.x2).|| by RLVECT_1:def 11;
   ||.f1/.x1 - f1/.x2 + (f2/.x1-f2/.x2).||
         <= ||.f1/.x1-f1/.x2.|| + ||.f2/.x1-f2/.x2.||
           by CLVECT_1:def 10;
   hence thesis by A13,A14,AXIOMS:22;
end;

theorem
  for f1,f2 be PartFunc of CNS1,CNS2 st
   f1 is_uniformly_continuous_on X & f2 is_uniformly_continuous_on X1 holds
    f1-f2 is_uniformly_continuous_on X/\X1
proof
   let f1,f2 be PartFunc of CNS1,CNS2;
   assume
A1: f1 is_uniformly_continuous_on X &
    f2 is_uniformly_continuous_on X1;
A2:X /\ X1 c= X & X /\ X1 c= X1 by XBOOLE_1:17; then
A3:f1 is_uniformly_continuous_on X /\ X1 by A1,Th1; then
A4:X /\ X1 c= dom f1 by Def1;
A5:f2 is_uniformly_continuous_on X /\ X1 by A1,A2,Th1; then
   X /\ X1 c= dom f2 by Def1; then
   X /\ X1 c= dom f1 /\ dom f2 by A4,XBOOLE_1:19;
   hence
A6: X /\ X1 c= dom (f1-f2) by VFUNCT_2:def 2;
   let r;
   assume 0<r; then
A7:0<r/2 by SEQ_2:3; then
   consider s such that
A8: 0<s &
    for x1,x2 be Point of CNS1 st
     x1 in X /\ X1 & x2 in X /\X1 & ||.x1-x2.||<s holds
      ||.f1/.x1-f1/.x2.||<r/2 by A3,Def1;
   consider g be Real such that
A9: 0<g &
    for x1,x2 be Point of CNS1 st
     x1 in X /\ X1 & x2 in X /\ X1 & ||.x1-x2.||<g holds
      ||.f2/.x1-f2/.x2.||<r/2 by A5,A7,Def1;
   take p=min(s,g);
   thus 0<p by A8,A9,SQUARE_1:38;
A10:p <= s & p <= g by SQUARE_1:35;
   let x1,x2 be Point of CNS1;
   assume
A11:x1 in X/\X1 & x2 in X/\X1 & ||.x1-x2.||<p; then
   x1 in X/\X1 & x2 in X/\X1 & ||.x1-x2.||<s by A10,AXIOMS:22; then
A12: ||.f1/.x1-f1/.x2.||<r/2 by A8;
   x1 in X/\X1 & x2 in X/\X1 & ||.x1-x2.||<g by A10,A11,AXIOMS:22; then
   ||.f2/.x1-f2/.x2.||<r/2 by A9; then
A13: ||.f1/.x1-f1/.x2.||+||.f2/.x1-f2/.x2.||<r/2+r/2 by A12,REAL_1:67;
A14: ||.(f1-f2)/.x1-(f1-f2)/.x2.|| = ||.f1/.x1 - f2/.x1-(f1-f2)/.x2.||
      by A6,A11,VFUNCT_2:def 2
   .= ||.f1/.x1 - f2/.x1 - (f1/.x2-f2/.x2).|| by A6,A11,VFUNCT_2:def 2
   .= ||.f1/.x1 - (f2/.x1 + (f1/.x2-f2/.x2)).|| by RLVECT_1:41
   .= ||.f1/.x1 - (f1/.x2 + f2/.x1-f2/.x2).|| by RLVECT_1:42
   .= ||.f1/.x1 - (f1/.x2 + (f2/.x1-f2/.x2)).|| by RLVECT_1:42
   .= ||.f1/.x1 - f1/.x2 - (f2/.x1-f2/.x2).|| by RLVECT_1:41;
   ||.f1/.x1 - f1/.x2 - (f2/.x1-f2/.x2).||
     <= ||.f1/.x1-f1/.x2.|| + ||.f2/.x1-f2/.x2.|| by CLVECT_1:105;
   hence thesis by A13,A14,AXIOMS:22;
end;

theorem
  for f1,f2 be PartFunc of CNS,RNS st
   f1 is_uniformly_continuous_on X & f2 is_uniformly_continuous_on X1 holds
    f1-f2 is_uniformly_continuous_on X/\X1
proof
   let f1,f2 be PartFunc of CNS,RNS;
   assume
A1: f1 is_uniformly_continuous_on X &
    f2 is_uniformly_continuous_on X1;
A2:X /\ X1 c= X & X /\ X1 c= X1 by XBOOLE_1:17; then
A3:f1 is_uniformly_continuous_on X /\ X1 by A1,Th2; then
A4:X /\ X1 c= dom f1 by Def2;
A5:f2 is_uniformly_continuous_on X /\ X1 by A1,A2,Th2; then
   X /\ X1 c= dom f2 by Def2; then
   X /\ X1 c= dom f1 /\ dom f2 by A4,XBOOLE_1:19;
   hence
A6: X /\ X1 c= dom (f1-f2) by VFUNCT_1:def 2;
   let r;
   assume 0<r; then
A7:0<r/2 by SEQ_2:3; then
   consider s such that
A8: 0<s &
    for x1,x2 be Point of CNS st
     x1 in X /\ X1 & x2 in X /\X1 & ||.x1-x2.||<s holds
      ||.f1/.x1-f1/.x2.||<r/2 by A3,Def2;
   consider g be Real such that
A9: 0<g &
    for x1,x2 be Point of CNS st
     x1 in X /\ X1 & x2 in X /\ X1 & ||.x1-x2.||<g holds
      ||.f2/.x1-f2/.x2.||<r/2 by A5,A7,Def2;
   take p=min(s,g);
   thus 0<p by A8,A9,SQUARE_1:38;
A10:p <= s & p <= g by SQUARE_1:35;
   let x1,x2 be Point of CNS;
   assume
A11:x1 in X/\X1 & x2 in X/\X1 & ||.x1-x2.||<p; then
   x1 in X/\X1 & x2 in X/\X1 & ||.x1-x2.||<s by A10,AXIOMS:22; then
A12: ||.f1/.x1-f1/.x2.||<r/2 by A8;
   x1 in X/\X1 & x2 in X/\X1 & ||.x1-x2.||<g by A10,A11,AXIOMS:22; then
   ||.f2/.x1-f2/.x2.||<r/2 by A9; then
A13: ||.f1/.x1-f1/.x2.||+||.f2/.x1-f2/.x2.||<r/2+r/2 by A12,REAL_1:67;
A14: ||.(f1-f2)/.x1-(f1-f2)/.x2.|| = ||.f1/.x1 - f2/.x1-(f1-f2)/.x2.||
      by A6,A11,VFUNCT_1:def 2
   .= ||.f1/.x1 - f2/.x1 - (f1/.x2-f2/.x2).|| by A6,A11,VFUNCT_1:def 2
   .= ||.f1/.x1 - (f2/.x1 + (f1/.x2-f2/.x2)).|| by RLVECT_1:41
   .= ||.f1/.x1 - (f1/.x2 + f2/.x1-f2/.x2).|| by RLVECT_1:42
   .= ||.f1/.x1 - (f1/.x2 + (f2/.x1-f2/.x2)).|| by RLVECT_1:42
   .= ||.f1/.x1 - f1/.x2 - (f2/.x1-f2/.x2).|| by RLVECT_1:41;
   ||.f1/.x1 - f1/.x2 - (f2/.x1-f2/.x2).||
     <= ||.f1/.x1-f1/.x2.|| + ||.f2/.x1-f2/.x2.|| by NORMSP_1:7;
   hence thesis by A13,A14,AXIOMS:22;
end;

theorem
  for f1,f2 be PartFunc of RNS,CNS st
   f1 is_uniformly_continuous_on X & f2 is_uniformly_continuous_on X1 holds
    f1-f2 is_uniformly_continuous_on X/\X1
proof
   let f1,f2 be PartFunc of RNS,CNS;
   assume
A1: f1 is_uniformly_continuous_on X &
    f2 is_uniformly_continuous_on X1;
A2:X /\ X1 c= X & X /\ X1 c= X1 by XBOOLE_1:17; then
A3:f1 is_uniformly_continuous_on X /\ X1 by A1,Th3; then
A4:X /\ X1 c= dom f1 by Def3;
A5:f2 is_uniformly_continuous_on X /\ X1 by A1,A2,Th3; then
   X /\ X1 c= dom f2 by Def3; then
   X /\ X1 c= dom f1 /\ dom f2 by A4,XBOOLE_1:19;
   hence
A6: X /\ X1 c= dom (f1-f2) by VFUNCT_2:def 2;
   let r;
   assume 0<r; then
A7:0<r/2 by SEQ_2:3; then
   consider s such that
A8: 0<s &
    for x1,x2 be Point of RNS st
     x1 in X /\ X1 & x2 in X /\X1 & ||.x1-x2.||<s holds
      ||.f1/.x1-f1/.x2.||<r/2 by A3,Def3;
   consider g be Real such that
A9: 0<g &
    for x1,x2 be Point of RNS st
     x1 in X /\ X1 & x2 in X /\ X1 & ||.x1-x2.||<g holds
      ||.f2/.x1-f2/.x2.||<r/2 by A5,A7,Def3;
   take p=min(s,g);
   thus 0<p by A8,A9,SQUARE_1:38;
A10:p <= s & p <= g by SQUARE_1:35;
   let x1,x2 be Point of RNS;
   assume
A11:x1 in X/\X1 & x2 in X/\X1 & ||.x1-x2.||<p; then
   x1 in X/\X1 & x2 in X/\X1 & ||.x1-x2.||<s by A10,AXIOMS:22; then
A12: ||.f1/.x1-f1/.x2.||<r/2 by A8;
   x1 in X/\X1 & x2 in X/\X1 & ||.x1-x2.||<g by A10,A11,AXIOMS:22; then
   ||.f2/.x1-f2/.x2.||<r/2 by A9; then
A13: ||.f1/.x1-f1/.x2.||+||.f2/.x1-f2/.x2.||<r/2+r/2 by A12,REAL_1:67;
A14: ||.(f1-f2)/.x1-(f1-f2)/.x2.|| = ||.f1/.x1 - f2/.x1-(f1-f2)/.x2.||
      by A6,A11,VFUNCT_2:def 2
   .= ||.f1/.x1 - f2/.x1 - (f1/.x2-f2/.x2).|| by A6,A11,VFUNCT_2:def 2
   .= ||.f1/.x1 - (f2/.x1 + (f1/.x2-f2/.x2)).|| by RLVECT_1:41
   .= ||.f1/.x1 - (f1/.x2 + f2/.x1-f2/.x2).|| by RLVECT_1:42
   .= ||.f1/.x1 - (f1/.x2 + (f2/.x1-f2/.x2)).|| by RLVECT_1:42
   .= ||.f1/.x1 - f1/.x2 - (f2/.x1-f2/.x2).|| by RLVECT_1:41;
   ||.f1/.x1 - f1/.x2 - (f2/.x1-f2/.x2).||
     <= ||.f1/.x1-f1/.x2.|| + ||.f2/.x1-f2/.x2.|| by CLVECT_1:105;
   hence thesis by A13,A14,AXIOMS:22;
end;

theorem Th10:
  for f be PartFunc of CNS1,CNS2 st
   f is_uniformly_continuous_on X holds z(#)f is_uniformly_continuous_on X
proof
   let f be PartFunc of CNS1,CNS2;
   assume
A1: f is_uniformly_continuous_on X; then
   X c= dom f by Def1;
   hence
A2:X c= dom (z(#)f) by VFUNCT_2:def 4;
   now per cases;
     suppose
A3:    z=0;
       let r;
       assume
A4:     0<r; then
       consider s such that
A5:     0<s & for x1,x2 be Point of CNS1 st
         x1 in X & x2 in X & ||.x1-x2.||<s holds
          ||.f/.x1-f/.x2.||<r by A1,Def1;
       take s;
       thus 0<s by A5;
       let x1,x2 be Point of CNS1;
       assume
A6:     x1 in X & x2 in X & ||.x1-x2.||<s; then
       ||.(z(#)f)/.x1-(z(#)f)/.x2.||
        = ||.z*(f/.x1)-(z(#)f)/.x2.|| by A2,VFUNCT_2:def 4
       .= ||.0.CNS2 -(z(#)f)/.x2.|| by A3,CLVECT_1:2
       .= ||.0.CNS2 - z*(f/.x2).|| by A2,A6,VFUNCT_2:def 4
       .= ||.0.CNS2 - 0.CNS2.|| by A3,CLVECT_1:2
       .= ||.0.CNS2 .|| by RLVECT_1:26
       .= 0 by CLVECT_1:def 10;
       hence ||.(z(#)f)/.x1-(z(#)f)/.x2.|| <r by A4;
     end;
     suppose
       z<>0; then
A7:    0<|.z.| & 0 <> |.z.| by COMPLEX1:133;
       let r;
       assume 0<r; then
       0 < r/|.z.| by A7,SEQ_2:6; then
       consider s such that
A8:    0<s &
        for x1,x2 be Point of CNS1 st
         x1 in X & x2 in X & ||.x1-x2.||<s holds
          ||.f/.x1-f/.x2.||<r/|.z.| by A1,Def1;
       take s;
       thus 0<s by A8;
       let x1,x2 be Point of CNS1;
       assume
A9:    x1 in X & x2 in X & ||.x1-x2.||<s; then
A10:   ||.(z(#)f)/.x1-(z(#)f)/.x2.||
        = ||.z*(f/.x1)-(z(#)f)/.x2.|| by A2,VFUNCT_2:def 4
       .= ||.z*(f/.x1) - z*(f/.x2).|| by A2,A9,VFUNCT_2:def 4
       .= ||.z*(f/.x1 - f/.x2).|| by CLVECT_1:10
       .= |.z.|*||.f/.x1 - f/.x2.|| by CLVECT_1:def 10;
       ||.f/.x1-f/.x2.||<r/|.z.| by A8,A9; then
       |.z.|*||.f/.x1-f/.x2.||<r/|.z.|*|.z.| by A7,REAL_1:70;
       hence ||.(z(#)f)/.x1-(z(#)f)/.x2.|| <r by A7,A10,XCMPLX_1:88;
     end;
   end;
   hence thesis;
end;

theorem Th11:
  for f be PartFunc of CNS,RNS st
   f is_uniformly_continuous_on X holds r(#)f is_uniformly_continuous_on X
proof
   let f be PartFunc of CNS,RNS;
   assume
A1: f is_uniformly_continuous_on X; then
   X c= dom f by Def2;
   hence
A2:X c= dom (r(#)f) by VFUNCT_1:def 4;
   now per cases;
     suppose
A3:    r=0;
       let p be Real;
       assume
A4:     0<p; then
       consider s such that
A5:     0<s & for x1,x2 be Point of CNS st
         x1 in X & x2 in X & ||.x1-x2.||<s holds
          ||.f/.x1-f/.x2.||<p by A1,Def2;
       take s;
       thus 0<s by A5;
       let x1,x2 be Point of CNS;
       assume
A6:     x1 in X & x2 in X & ||.x1-x2.||<s; then
       ||.(r(#)f)/.x1-(r(#)f)/.x2.||
        = ||.r*(f/.x1)-(r(#)f)/.x2.|| by A2,VFUNCT_1:def 4
       .= ||.0.RNS -(r(#)f)/.x2.|| by A3,RLVECT_1:23
       .= ||.0.RNS - r*(f/.x2).|| by A2,A6,VFUNCT_1:def 4
       .= ||.0.RNS - 0.RNS.|| by A3,RLVECT_1:23
       .= ||.0.RNS .|| by RLVECT_1:26
       .= 0 by NORMSP_1:def 2;
       hence ||.(r(#)f)/.x1-(r(#)f)/.x2.|| <p by A4;
     end;
     suppose
       r<>0; then
A7:    0<abs(r) & 0 <> abs(r) by ABSVALUE:6;
       let p be Real;
       assume 0<p; then
       0 < p/abs(r) by A7,SEQ_2:6; then
       consider s such that
A8:    0<s &
        for x1,x2 be Point of CNS st
         x1 in X & x2 in X & ||.x1-x2.||<s holds
          ||.f/.x1-f/.x2.||<p/abs(r) by A1,Def2;
       take s;
       thus 0<s by A8;
       let x1,x2 be Point of CNS;
       assume
A9:    x1 in X & x2 in X & ||.x1-x2.||<s; then
A10:   ||.(r(#)f)/.x1-(r(#)f)/.x2.||
        = ||.r*(f/.x1)-(r(#)f)/.x2.|| by A2,VFUNCT_1:def 4
       .= ||.r*(f/.x1) - r*(f/.x2).|| by A2,A9,VFUNCT_1:def 4
       .= ||.r*(f/.x1 - f/.x2).|| by RLVECT_1:48
       .= abs(r)*||.f/.x1 - f/.x2.|| by NORMSP_1:def 2;
       ||.f/.x1-f/.x2.||<p/abs(r) by A8,A9; then
       abs(r)*||.f/.x1-f/.x2.||<p/abs(r)*abs(r) by A7,REAL_1:70;
       hence ||.(r(#)f)/.x1-(r(#)f)/.x2.|| < p by A7,A10,XCMPLX_1:88;
     end;
   end;
   hence thesis;
end;

theorem Th12:
  for f be PartFunc of RNS,CNS st
   f is_uniformly_continuous_on X holds z(#)f is_uniformly_continuous_on X
proof
   let f be PartFunc of RNS,CNS;
   assume
A1: f is_uniformly_continuous_on X; then
   X c= dom f by Def3;
   hence
A2:X c= dom (z(#)f) by VFUNCT_2:def 4;
   now per cases;
     suppose
A3:    z=0;
       let r;
       assume
A4:     0<r; then
       consider s such that
A5:     0<s & for x1,x2 be Point of RNS st
         x1 in X & x2 in X & ||.x1-x2.||<s holds
          ||.f/.x1-f/.x2.||<r by A1,Def3;
       take s;
       thus 0<s by A5;
       let x1,x2 be Point of RNS;
       assume
A6:     x1 in X & x2 in X & ||.x1-x2.||<s; then
       ||.(z(#)f)/.x1-(z(#)f)/.x2.||
        = ||.z*(f/.x1)-(z(#)f)/.x2.|| by A2,VFUNCT_2:def 4
       .= ||.0.CNS -(z(#)f)/.x2.|| by A3,CLVECT_1:2
       .= ||.0.CNS - z*(f/.x2).|| by A2,A6,VFUNCT_2:def 4
       .= ||.0.CNS - 0.CNS.|| by A3,CLVECT_1:2
       .= ||.0.CNS .|| by RLVECT_1:26
       .= 0 by CLVECT_1:def 10;
       hence ||.(z(#)f)/.x1-(z(#)f)/.x2.|| <r by A4;
     end;
     suppose
       z<>0; then
A7:    0<|.z.| & 0 <> |.z.| by COMPLEX1:133;
       let r;
       assume 0<r; then
       0 < r/|.z.| by A7,SEQ_2:6; then
       consider s such that
A8:    0<s &
        for x1,x2 be Point of RNS st
         x1 in X & x2 in X & ||.x1-x2.||<s holds
          ||.f/.x1-f/.x2.||<r/|.z.| by A1,Def3;
       take s;
       thus 0<s by A8;
       let x1,x2 be Point of RNS;
       assume
A9:    x1 in X & x2 in X & ||.x1-x2.||<s; then
A10:   ||.(z(#)f)/.x1-(z(#)f)/.x2.||
        = ||.z*(f/.x1)-(z(#)f)/.x2.|| by A2,VFUNCT_2:def 4
       .= ||.z*(f/.x1) - z*(f/.x2).|| by A2,A9,VFUNCT_2:def 4
       .= ||.z*(f/.x1 - f/.x2).|| by CLVECT_1:10
       .= |.z.|*||.f/.x1 - f/.x2.|| by CLVECT_1:def 10;
       ||.f/.x1-f/.x2.||<r/|.z.| by A8,A9; then
       |.z.|*||.f/.x1-f/.x2.||<r/|.z.|*|.z.| by A7,REAL_1:70;
       hence ||.(z(#)f)/.x1-(z(#)f)/.x2.|| <r by A7,A10,XCMPLX_1:88;
     end;
   end;
   hence thesis;
end;

theorem
  for f be PartFunc of CNS1,CNS2 st
   f is_uniformly_continuous_on X holds
    -f is_uniformly_continuous_on X
proof
   let f be PartFunc of CNS1,CNS2;
   assume
A1: f is_uniformly_continuous_on X;
   -f = (-1r)(#)f by VFUNCT_2:23;
   hence thesis by A1,Th10;
end;

theorem
  for f be PartFunc of CNS,RNS st
   f is_uniformly_continuous_on X holds
    -f is_uniformly_continuous_on X
proof
   let f be PartFunc of CNS,RNS;
   assume
A1: f is_uniformly_continuous_on X;
   -f = (-1)(#)f by VFUNCT_1:29;
   hence thesis by A1,Th11;
end;

theorem
  for f be PartFunc of RNS,CNS st
   f is_uniformly_continuous_on X holds
    -f is_uniformly_continuous_on X
proof
   let f be PartFunc of RNS,CNS;
   assume
A1: f is_uniformly_continuous_on X;
   -f = (-1r)(#)f by VFUNCT_2:23;
   hence thesis by A1,Th12;
end;

theorem
  for f be PartFunc of CNS1,CNS2 st
   f is_uniformly_continuous_on X holds
    ||.f.|| is_uniformly_continuous_on X
proof
   let f be PartFunc of CNS1,CNS2;
   assume
A1: f is_uniformly_continuous_on X; then
   X c= dom f by Def1;
   hence
A2:X c= dom (||.f.||) by NCFCONT1:def 2;
   let r;
   assume 0<r; then
   consider s such that
A3: 0<s & for x1,x2 be Point of CNS1 st x1 in X & x2 in X &
     ||.x1-x2.||<s holds ||.f/.x1-f/.x2.||<r by A1,Def1;
   take s;
   thus 0<s by A3;
   let x1,x2 be Point of CNS1;
   assume
A4: x1 in X & x2 in X & ||.x1-x2.||<s; then
   abs((||.f.||)/.x1-(||.f.||)/.x2)
    =abs((||.f.||).x1-(||.f.||)/.x2) by A2,FINSEQ_4:def 4
   .=abs((||.f.||).x1-(||.f.||).x2) by A2,A4,FINSEQ_4:def 4
   .= abs(||.f/.x1.||-(||.f.||).x2) by A2,A4,NCFCONT1:def 2
   .= abs(||.f/.x1.|| - ||.f/.x2.||) by A2,A4,NCFCONT1:def 2; then
A5:abs((||.f.||)/.x1-(||.f.||)/.x2) <= ||.f/.x1-f/.x2.|| by CLVECT_1:111;
   ||.f/.x1-f/.x2.||<r by A3,A4;
   hence thesis by A5,AXIOMS:22;
end;

theorem
  for f be PartFunc of CNS,RNS st
   f is_uniformly_continuous_on X holds
    ||.f.|| is_uniformly_continuous_on X
proof
   let f be PartFunc of CNS,RNS;
   assume
A1: f is_uniformly_continuous_on X; then
   X c= dom f by Def2;
   hence
A2:X c= dom (||.f.||) by NCFCONT1:def 3;
   let r;
   assume 0<r; then
   consider s such that
A3: 0<s & for x1,x2 be Point of CNS st x1 in X & x2 in X &
     ||.x1-x2.||<s holds ||.f/.x1-f/.x2.||<r by A1,Def2;
   take s;
   thus 0<s by A3;
   let x1,x2 be Point of CNS;
   assume
A4: x1 in X & x2 in X & ||.x1-x2.||<s; then
   abs((||.f.||)/.x1-(||.f.||)/.x2)
    =abs((||.f.||).x1-(||.f.||)/.x2) by A2,FINSEQ_4:def 4
   .=abs((||.f.||).x1-(||.f.||).x2) by A2,A4,FINSEQ_4:def 4
   .= abs(||.f/.x1.||-(||.f.||).x2) by A2,A4,NCFCONT1:def 3
   .= abs(||.f/.x1.|| - ||.f/.x2.||) by A2,A4,NCFCONT1:def 3; then
A5:abs((||.f.||)/.x1-(||.f.||)/.x2) <= ||.f/.x1-f/.x2.|| by NORMSP_1:13;
   ||.f/.x1-f/.x2.||<r by A3,A4;
   hence thesis by A5,AXIOMS:22;
end;

theorem
for f be PartFunc of RNS,CNS st
 f is_uniformly_continuous_on X holds
  ||.f.|| is_uniformly_continuous_on X
proof
   let f be PartFunc of RNS,CNS;
   assume
A1: f is_uniformly_continuous_on X; then
   X c= dom f by Def3; then
A2:X c= dom (||.f.||) by NCFCONT1:def 4;
   for r be Real st 0 < r ex s be Real st 0 < s &
    for x1,x2 be Point of RNS st x1 in X & x2 in X & ||.x1-x2.|| < s holds
     abs ((||.f.||)/.x1 - (||.f.||)/.x2) < r
   proof
     let r be Real;
     assume 0 < r; then
     consider s be Real such that
A3:   0 < s & for x1,x2 be Point of RNS st x1 in X & x2 in X &
       ||.x1-x2.|| < s holds ||. f/.x1 - f/.x2 .|| < r
       by A1,Def3;
     take s;
     thus 0<s by A3;
     let x1,x2 be Point of RNS;
     assume
A4:   x1 in X & x2 in X & ||.x1-x2.||<s; then
     abs((||.f.||)/.x1-(||.f.||)/.x2)
      =abs((||.f.||).x1-(||.f.||)/.x2) by A2,FINSEQ_4:def 4
     .=abs((||.f.||).x1-(||.f.||).x2) by A2,A4,FINSEQ_4:def 4
     .= abs(||.f/.x1.||-(||.f.||).x2) by A2,A4,NCFCONT1:def 4
     .= abs(||.f/.x1.|| - ||.f/.x2.||) by A2,A4,NCFCONT1:def 4; then
A5:  abs((||.f.||)/.x1-(||.f.||)/.x2) <= ||.f/.x1-f/.x2.|| by CLVECT_1:111;
     ||.f/.x1-f/.x2.||<r by A3,A4;
     hence thesis by A5,AXIOMS:22;
   end;
   hence thesis by A2,NFCONT_2:def 2;
end;

theorem Th19:
for f be PartFunc of CNS1,CNS2 st
f is_uniformly_continuous_on X holds f is_continuous_on X
proof
   let f be PartFunc of CNS1,CNS2;
   assume
A1: f is_uniformly_continuous_on X; then
A2:X c= dom f & for r st 0<r ex s st 0<s & for x1,x2 be Point of CNS1 st
    x1 in X & x2 in X & ||.x1 - x2.|| < s holds ||.f/.x1 - f/.x2.||< r by Def1;
   now
     let x0 be Point of CNS1;
     let r be Real;
     assume
A3:   x0 in X & 0<r; then
     consider s be Real such that
A4:   0<s & for x1,x2 be Point of CNS1 st x1 in X & x2 in X &
       ||.x1 -x2.|| < s holds ||.f/.x1 - f/.x2.||< r by A1,Def1;
     take s;
     thus 0<s by A4;
     let x1 be Point of CNS1;
     assume x1 in X & ||.x1-x0.|| < s;
     hence ||.f/.x1 - f/.x0.|| < r by A3,A4;
   end;
   hence f is_continuous_on X by A2,NCFCONT1:65;
end;

theorem Th20:
for f be PartFunc of CNS,RNS st
f is_uniformly_continuous_on X holds f is_continuous_on X
proof
   let f be PartFunc of CNS,RNS;
   assume
A1: f is_uniformly_continuous_on X; then
A2:X c= dom f & for r st 0<r ex s st 0<s & for x1,x2 be Point of CNS st
    x1 in X & x2 in X & ||.x1 - x2.|| < s holds ||.f/.x1 - f/.x2.||< r
     by Def2;
   now
     let x0 be Point of CNS;
     let r be Real;
     assume
A3:   x0 in X & 0<r; then
     consider s be Real such that
A4:   0<s & for x1,x2 be Point of CNS st x1 in X & x2 in X &
       ||.x1 -x2.|| < s holds ||.f/.x1 - f/.x2.||< r by A1,Def2;
     take s;
     thus 0<s by A4;
     let x1 be Point of CNS;
     assume x1 in X & ||.x1-x0.|| < s;
     hence ||.f/.x1 - f/.x0.|| < r by A3,A4;
   end;
   hence f is_continuous_on X by A2,NCFCONT1:66;
end;

theorem Th21:
for f be PartFunc of RNS,CNS st
f is_uniformly_continuous_on X holds f is_continuous_on X
proof
   let f be PartFunc of RNS,CNS;
   assume
A1: f is_uniformly_continuous_on X; then
A2:X c= dom f & for r st 0<r ex s st 0<s & for x1,x2 be Point of RNS st
    x1 in X & x2 in X & ||.x1 - x2.|| < s holds ||.f/.x1 - f/.x2.||< r
     by Def3;
   now
     let x0 be Point of RNS;
     let r be Real;
     assume
A3:   x0 in X & 0<r; then
     consider s be Real such that
A4:   0<s & for x1,x2 be Point of RNS st x1 in X & x2 in X &
       ||.x1 -x2.|| < s holds ||.f/.x1 - f/.x2.||< r by A1,Def3;
     take s;
     thus 0<s by A4;
     let x1 be Point of RNS;
     assume x1 in X & ||.x1-x0.|| < s;
     hence ||.f/.x1 - f/.x0.|| < r by A3,A4;
   end;
   hence f is_continuous_on X by A2,NCFCONT1:67;
end;

theorem
for f be PartFunc of the carrier of CNS, COMPLEX st
 f is_uniformly_continuous_on X holds f is_continuous_on X
proof
   let f be PartFunc of the carrier of CNS, COMPLEX;
   assume
A1: f is_uniformly_continuous_on X; then
A2:X c= dom f & for r st 0<r ex s st 0<s & for x1,x2 be Point of CNS st
    x1 in X & x2 in X & ||.x1 - x2.|| < s holds |.(f/.x1 - f/.x2).|< r by Def4;
   now
     let x0 be Point of CNS;
     let r be Real;
     assume
A3:   x0 in X & 0<r; then
     consider s be Real such that
A4:   0<s & for x1,x2 be Point of CNS st x1 in X & x2 in X &
      ||.x1 -x2.|| < s holds |.(f/.x1 - f/.x2).|< r by A1,Def4;
      take s;
      thus 0<s by A4;
      let x1 be Point of CNS;
      assume x1 in X & ||.x1-x0.|| < s;
      hence |.(f/.x1 - f/.x0).| < r by A3,A4;
    end;
    hence f is_continuous_on X by A2,NCFCONT1:68;
end;

theorem Th23:
  for f be PartFunc of the carrier of CNS, REAL st
   f is_uniformly_continuous_on X holds f is_continuous_on X
proof
   let f be PartFunc of the carrier of CNS, REAL;
   assume
A1: f is_uniformly_continuous_on X; then
A2:X c= dom f & for r st 0<r ex s st 0<s & for x1,x2 be Point of CNS st
    x1 in X & x2 in X & ||.x1 - x2.|| < s holds abs(f/.x1 - f/.x2)< r
     by Def5;
   now
     let x0 be Point of CNS;
     let r be Real;
     assume
A3:   x0 in X & 0<r; then
     consider s be Real such that
A4:   0<s & for x1,x2 be Point of CNS st x1 in X & x2 in X &
      ||.x1 -x2.|| < s holds abs(f/.x1 - f/.x2)< r by A1,Def5;
      take s;
      thus 0<s by A4;
      let x1 be Point of CNS;
      assume x1 in X & ||.x1-x0.|| < s;
      hence abs(f/.x1 - f/.x0) < r by A3,A4;
    end;
    hence f is_continuous_on X by A2,NCFCONT1:69;
end;

theorem
  for f be PartFunc of the carrier of RNS, COMPLEX st
   f is_uniformly_continuous_on X holds f is_continuous_on X
proof
   let f be PartFunc of the carrier of RNS, COMPLEX;
   assume
A1: f is_uniformly_continuous_on X; then
A2:X c= dom f & for r st 0<r ex s st 0<s & for x1,x2 be Point of RNS st
    x1 in X & x2 in X & ||.x1 - x2.|| < s holds |.(f/.x1 - f/.x2).|< r
     by Def6;
   now
     let x0 be Point of RNS;
     let r be Real;
     assume
A3:   x0 in X & 0<r; then
     consider s be Real such that
A4:   0<s & for x1,x2 be Point of RNS st x1 in X & x2 in X &
      ||.x1 -x2.|| < s holds |.(f/.x1 - f/.x2).|< r by A1,Def6;
      take s;
      thus 0<s by A4;
      let x1 be Point of RNS;
      assume x1 in X & ||.x1-x0.|| < s;
      hence |.(f/.x1 - f/.x0).| < r by A3,A4;
    end;
    hence f is_continuous_on X by A2,NCFCONT1:70;
end;

theorem Th25:
  for f be PartFunc of CNS1,CNS2 st
   f is_Lipschitzian_on X holds f is_uniformly_continuous_on X
proof
   let f be PartFunc of CNS1,CNS2;
   assume
A1: f is_Lipschitzian_on X;
   hence X c= dom f by NCFCONT1:def 27;
   consider r be Real such that
A2: 0<r &
    for x1,x2 be Point of CNS1 st x1 in X & x2 in X holds
     ||.f/.x1-f/.x2.||<=r*||.x1-x2.|| by A1,NCFCONT1:def 27;
   let p be Real such that
A3: 0<p;
   take s=p/r;
   thus 0<s by A2,A3,SEQ_2:6;
   let x1,x2 be Point of CNS1;
   assume
A4: x1 in X & x2 in X & ||.x1 -x2.|| < s; then
A5:r*||.x1 -x2.|| < s*r by A2,REAL_1:70;
   ||.f/.x1-f/.x2.||<=r*||.x1-x2.|| by A2,A4; then
   ||.f/.x1-f/.x2.||<p/r*r by A5,AXIOMS:22;
   hence thesis by A2,XCMPLX_1:88;
end;

theorem Th26:
for f be PartFunc of CNS,RNS st
 f is_Lipschitzian_on X holds f is_uniformly_continuous_on X
proof
   let f be PartFunc of CNS,RNS;
   assume
A1: f is_Lipschitzian_on X;
   hence X c= dom f by NCFCONT1:def 28;
   consider r be Real such that
A2: 0<r &
    for x1,x2 be Point of CNS st x1 in X & x2 in X holds
     ||.f/.x1-f/.x2.||<=r*||.x1-x2.|| by A1,NCFCONT1:def 28;
   let p be Real such that
A3: 0<p;
   take s=p/r;
   thus 0<s by A2,A3,SEQ_2:6;
   let x1,x2 be Point of CNS;
   assume
A4: x1 in X & x2 in X & ||.x1 -x2.|| < s; then
A5:r*||.x1 -x2.|| < s*r by A2,REAL_1:70;
   ||.f/.x1-f/.x2.||<=r*||.x1-x2.|| by A2,A4; then
   ||.f/.x1-f/.x2.||<p/r*r by A5,AXIOMS:22;
   hence thesis by A2,XCMPLX_1:88;
end;

theorem Th27:
for f be PartFunc of RNS,CNS st
 f is_Lipschitzian_on X holds f is_uniformly_continuous_on X
proof
   let f be PartFunc of RNS,CNS;
   assume
A1: f is_Lipschitzian_on X;
   hence X c= dom f by NCFCONT1:def 29;
   consider r be Real such that
A2: 0<r &
    for x1,x2 be Point of RNS st x1 in X & x2 in X holds
     ||.f/.x1-f/.x2.||<=r*||.x1-x2.|| by A1,NCFCONT1:def 29;
   let p be Real such that
A3: 0<p;
   take s=p/r;
   thus 0<s by A2,A3,SEQ_2:6;
   let x1,x2 be Point of RNS;
   assume
A4: x1 in X & x2 in X & ||.x1 -x2.|| < s; then
A5:r*||.x1 -x2.|| < s*r by A2,REAL_1:70;
   ||.f/.x1-f/.x2.||<=r*||.x1-x2.|| by A2,A4; then
   ||.f/.x1-f/.x2.||<p/r*r by A5,AXIOMS:22;
   hence thesis by A2,XCMPLX_1:88;
end;

theorem
for f be PartFunc of CNS1,CNS2, Y be Subset of CNS1 st
 Y is compact & f is_continuous_on Y holds
  f is_uniformly_continuous_on Y
proof
   let f be PartFunc of CNS1,CNS2;
   let Y be Subset of CNS1;
   assume that
A1: Y is compact and
A2: f is_continuous_on Y;
A3: Y c= dom f by A2,NCFCONT1:def 21;
   assume not thesis; then
   consider r such that
A4: 0<r and
A5: for s st 0<s ex x1,x2 be Point of CNS1 st
     x1 in Y & x2 in Y & ||.x1 -x2.|| < s &
     not ||.f/.x1 - f/.x2.||< r by A3,Def1;
A6:now let n be Nat;
     0 <> n+1 by NAT_1:21; then
     0 < n+1 by NAT_1:19;
     hence 0 < 1/(n+1) by SEQ_2:6;
   end;
   defpred P[Nat,Point of CNS1] means
    $2 in Y & ex x2 be Point of CNS1 st x2 in Y &
    ||.$2-x2.||<1/($1+1) & not ||.f/.($2)-f/.x2.|| < r;
A7:now let n be Nat;
     0 < 1/(n+1) by A6; then
     consider x1 be Point of CNS1 such that
A8:   ex x2 be Point of CNS1 st x1 in Y & x2 in Y & ||.x1 -x2.|| < 1/(n+1) &
      not ||.f/.x1 - f/.x2.||< r by A5;
     take x1;
     thus P[n,x1] by A8;
   end;
   consider s1 be sequence of CNS1 such that
A9: for n be Nat holds P[n,s1.n] from FUNCT_2:sch 3(A7);
   defpred P1[Nat, Point of CNS1] means
    $2 in Y & ||.s1.$1-$2.|| < 1/($1+1) & not ||.f/.(s1.$1)-f/.$2.|| < r;
A10:for n be Nat ex x2 be Point of CNS1 st P1[n,x2] by A9;
   consider s2 be sequence of CNS1 such that
A11:for n be Nat holds P1[n,s2.n] from FUNCT_2:sch 3(A10);
A12:now let p be Real such that
A13: 0<p;
     consider k be Nat such that
A14:  p" < k by SEQ_4:10;
A15: 0 < p" by A13,REAL_1:72;
A16: k < k+1 by NAT_1:38; then
     p" < k+1 by A14,AXIOMS:22; then
     1/(k+1) < 1/p" by A15,SEQ_2:10; then
A17: 1/(k+1) < p by XCMPLX_1:218;
     take k;
     let m be Nat such that
A18:  k <= m;

A19: 0 < k+1 by A14,A15,A16;
     k+1 <= m+1 by A18,AXIOMS:24; then
A20: 1/(m+1) <= 1/(k+1) by A19,SEQ_4:1;
A21: ||.(s1-s2).m - 0.CNS1.|| = ||.s1.m - s2.m - 0.CNS1.|| by CLVECT_1:def 12
                             .= ||.s1.m - s2.m .|| by RLVECT_1:26;
     ||.s1.m - s2.m.|| < 1/(m+1) by A11; then
     ||.s1.m - s2.m.|| < 1/(k+1) by A20,AXIOMS:22;
     hence ||.(s1-s2).m - 0.CNS1.|| < p by A17,A21,AXIOMS:22;
   end; then
A22:s1-s2 is convergent by CLVECT_1:def 15; then
A23:lim (s1-s2) = 0.CNS1 by A12,CLVECT_1:def 17;
    now let x be Point of CNS1;
     assume x in rng s1; then
     ex n be Nat st x=s1.n by NCFCONT1:8;
     hence x in Y by A9;
   end; then
   for x be set st x in rng s1 holds x in Y; then
A24:rng s1 c= Y by TARSKI:def 3; then
   consider q1 be sequence of CNS1 such that
A25:q1 is subsequence of s1 & q1 is convergent &
    (lim q1) in Y by A1,NCFCONT1:def 6;
A26:f|Y is_continuous_in (lim q1) by A2,A25,NCFCONT1:def 21;
   consider Ns1 be increasing Seq_of_Nat such that
A27:q1=s1*Ns1 by A25,BHSP_3:def 4;
A28:(s1-s2)*Ns1 is subsequence of s1-s2 by BHSP_3:def 4; then
A29:(s1-s2)*Ns1 is convergent by A22,CLOPBAN3:10;
A30:lim ((s1-s2)*Ns1) = 0.CNS1 by A22,A23,A28,CLOPBAN3:11;
   set q2 = q1 - (s1-s2)*Ns1;
A31:q2 is convergent by A25,A29,CLVECT_1:116;
A32:lim q2 = lim q1 - 0.CNS1 by A25,A29,A30,CLVECT_1:122
          .= lim q1 by RLVECT_1:26;
   now let n be Nat;
     thus q2.n = (s1*Ns1).n - ((s1-s2)*Ns1).n by A27,CLVECT_1:def 12
     .= s1.(Ns1.n) - ((s1-s2)*Ns1).n by LOPBAN_3:7
     .= s1.(Ns1.n) - (s1-s2).(Ns1.n) by LOPBAN_3:7
     .= s1.(Ns1.n) - (s1.(Ns1.n)-s2.(Ns1.n)) by CLVECT_1:def 12
     .= (s1.(Ns1.n) - s1.(Ns1.n)) + s2.(Ns1.n) by RLVECT_1:43
     .= 0.CNS1  + s2.(Ns1.n) by RLVECT_1:28
     .= s2.(Ns1.n) by RLVECT_1:10
     .= (s2*Ns1).n by LOPBAN_3:7;
   end; then
A33:q2 = s2*Ns1 by FUNCT_2:113; then
A34:q2 is subsequence of s2 by BHSP_3:def 4;
   now let x be set;
     assume x in rng s2; then
     ex n be Nat st x=s2.n by NCFCONT1:8;
     hence x in Y by A11;
   end; then
A35:rng s2 c= Y by TARSKI:def 3;
   rng q1 c= rng s1 by A25,NCFCONT1:9; then
A36:rng q1 c= Y by A24,XBOOLE_1:1; then
   rng q1 c= dom f by A3,XBOOLE_1:1; then
   rng q1 c= dom f /\ Y by A36,XBOOLE_1:19; then
A37:rng q1 c= dom (f|Y) by RELAT_1:90; then
A38:(f|Y)*q1 is convergent & (f|Y)/.(lim q1)=lim((f|Y)*q1)
     by A25,A26,NCFCONT1:def 15;
   rng q2 c= rng s2 by A34,NCFCONT1:9; then
A39:rng q2 c= Y by A35,XBOOLE_1:1; then
   rng q2 c= dom f by A3,XBOOLE_1:1; then
   rng q2 c= dom f /\ Y by A39,XBOOLE_1:19; then
A40:rng q2 c= dom (f|Y) by RELAT_1:90; then
A41:(f|Y)*q2 is convergent & (f|Y)/.(lim q1) = lim ((f|Y)*q2)
     by A26,A31,A32,NCFCONT1:def 15; then
A42:(f|Y)*q1 - (f|Y)*q2 is convergent by A38,CLVECT_1:116;
A43:lim ((f|Y)*q1 - (f|Y)*q2) = (f|Y)/.(lim q1) - (f|Y)/.(lim q1)
     by A38,A41,CLVECT_1:122
     .= 0.CNS2 by RLVECT_1:28;
   now let n be Nat;
     consider k be Nat such that
A44:  for m be Nat st k<=m holds
       ||.((f|Y)*q1 - (f|Y)*q2).m-0.CNS2 .||<r
        by A4,A42,A43,CLVECT_1:def 17;
A45: ||.((f|Y)*q1 - (f|Y)*q2).k - 0.CNS2.||<r by A44;
A46: q1.k in rng q1 by NCFCONT1:8;
A47: q2.k in rng q2 by NCFCONT1:8;
     ||.((f|Y)*q1 - (f|Y)*q2).k - 0.CNS2 .||
      = ||.((f|Y)*q1 - (f|Y)*q2).k  .|| by RLVECT_1:26
     .= ||.((f|Y)*q1).k - ((f|Y)*q2).k.|| by CLVECT_1:def 12
     .= ||.(f|Y)/.(q1.k) - ((f|Y)*q2).k.|| by A37,NCFCONT1:10
     .= ||.(f|Y)/.(q1.k) - (f|Y)/.(q2.k) .|| by A40,NCFCONT1:10
     .= ||.f/.(q1.k) - (f|Y)/.(q2.k).|| by A37,A46,PARTFUN2:32
     .= ||.f/.(q1.k) - f/.(q2.k).|| by A40,A47,PARTFUN2:32
     .= ||.f/.(s1.(Ns1.k)) - f/.((s2*Ns1).k).|| by A27,A33,LOPBAN_3:7
     .= ||.f/.(s1.(Ns1.k)) - f/.(s2.(Ns1.k)).|| by LOPBAN_3:7;
     hence contradiction by A11,A45;
   end;
   hence contradiction;
end;

theorem
for f be PartFunc of CNS,RNS, Y be Subset of CNS st
 Y is compact & f is_continuous_on Y holds
  f is_uniformly_continuous_on Y
proof
   let f be PartFunc of CNS,RNS;
   let Y be Subset of CNS;
   assume that
A1: Y is compact and
A2: f is_continuous_on Y;
A3: Y c= dom f by A2,NCFCONT1:def 22;
   assume not thesis; then
   consider r such that
A4: 0<r and
A5: for s st 0<s ex x1,x2 be Point of CNS st
     x1 in Y & x2 in Y & ||.x1 -x2.|| < s &
     not ||.f/.x1 - f/.x2.||< r by A3,Def2;
A6:now let n be Nat;
     0 <> n+1 by NAT_1:21; then
     0 < n+1 by NAT_1:19;
     hence 0 < 1/(n+1) by SEQ_2:6;
   end;
   defpred P[Nat,Point of CNS] means
    $2 in Y & ex x2 be Point of CNS st x2 in Y &
    ||.$2-x2.||<1/($1+1) & not ||.f/.($2)-f/.x2.|| < r;
A7:now let n be Nat;
     0 < 1/(n+1) by A6; then
     consider x1 be Point of CNS such that
A8:   ex x2 be Point of CNS st x1 in Y & x2 in Y & ||.x1 -x2.|| < 1/(n+1) &
      not ||.f/.x1 - f/.x2.||< r by A5;
     take x1;
     thus P[n,x1] by A8;
   end;
   consider s1 be sequence of CNS such that
A9: for n be Nat holds P[n,s1.n] from FUNCT_2:sch 3(A7);
   defpred P1[Nat, Point of CNS] means
    $2 in Y & ||.s1.$1-$2.|| < 1/($1+1) & not ||.f/.(s1.$1)-f/.$2.|| < r;
A10:for n be Nat ex x2 be Point of CNS st P1[n,x2] by A9;
   consider s2 be sequence of CNS such that
A11:for n be Nat holds P1[n,s2.n] from FUNCT_2:sch 3(A10);
A12:now let p be Real such that
A13: 0<p;
     consider k be Nat such that
A14:  p" < k by SEQ_4:10;
A15: 0 < p" by A13,REAL_1:72;
A16: k < k+1 by NAT_1:38; then
     p" < k+1 by A14,AXIOMS:22; then
     1/(k+1) < 1/p" by A15,SEQ_2:10; then
A17: 1/(k+1) < p by XCMPLX_1:218;
     take k;
     let m be Nat such that
A18:  k <= m;
A19: 0 < k+1 by A14,A15,A16;
     k+1 <= m+1 by A18,AXIOMS:24; then
A20: 1/(m+1) <= 1/(k+1) by A19,SEQ_4:1;
A21: ||.(s1-s2).m - 0.CNS.|| = ||.s1.m - s2.m - 0.CNS.|| by CLVECT_1:def 12
                             .= ||.s1.m - s2.m .|| by RLVECT_1:26;
     ||.s1.m - s2.m.|| < 1/(m+1) by A11; then
     ||.s1.m - s2.m.|| < 1/(k+1) by A20,AXIOMS:22;
     hence ||.(s1-s2).m - 0.CNS.|| < p by A17,A21,AXIOMS:22;
   end; then
A22:s1-s2 is convergent by CLVECT_1:def 15; then
A23:lim (s1-s2) = 0.CNS by A12,CLVECT_1:def 17;
    now let x be Point of CNS;
     assume x in rng s1; then
     ex n be Nat st x=s1.n by NCFCONT1:8;
     hence x in Y by A9;
   end; then
   for x be set st x in rng s1 holds x in Y; then
A24:rng s1 c= Y by TARSKI:def 3; then
   consider q1 be sequence of CNS such that
A25:q1 is subsequence of s1 & q1 is convergent &
    (lim q1) in Y by A1,NCFCONT1:def 6;
A26:f|Y is_continuous_in (lim q1) by A2,A25,NCFCONT1:def 22;
   consider Ns1 be increasing Seq_of_Nat such that
A27:q1=s1*Ns1 by A25,BHSP_3:def 4;
A28:(s1-s2)*Ns1 is subsequence of s1-s2 by BHSP_3:def 4; then
A29:(s1-s2)*Ns1 is convergent by A22,CLOPBAN3:10;
A30:lim ((s1-s2)*Ns1) = 0.CNS by A22,A23,A28,CLOPBAN3:11;
   set q2 = q1 - (s1-s2)*Ns1;
A31:q2 is convergent by A25,A29,CLVECT_1:116;
A32:lim q2 = lim q1 - 0.CNS by A25,A29,A30,CLVECT_1:122
          .= lim q1 by RLVECT_1:26;
   now let n be Nat;
     thus q2.n = (s1*Ns1).n - ((s1-s2)*Ns1).n by A27,CLVECT_1:def 12
     .= s1.(Ns1.n) - ((s1-s2)*Ns1).n by LOPBAN_3:7
     .= s1.(Ns1.n) - (s1-s2).(Ns1.n) by LOPBAN_3:7
     .= s1.(Ns1.n) - (s1.(Ns1.n)-s2.(Ns1.n)) by CLVECT_1:def 12
     .= (s1.(Ns1.n) - s1.(Ns1.n)) + s2.(Ns1.n) by RLVECT_1:43
     .= 0.CNS  + s2.(Ns1.n) by RLVECT_1:28
     .= s2.(Ns1.n) by RLVECT_1:10
     .= (s2*Ns1).n by LOPBAN_3:7;
   end; then
A33:q2 = s2*Ns1 by FUNCT_2:113; then
A34:q2 is subsequence of s2 by BHSP_3:def 4;
   now let x be set;
     assume x in rng s2; then
     ex n be Nat st x=s2.n by NCFCONT1:8;
     hence x in Y by A11;
   end; then
A35:rng s2 c= Y by TARSKI:def 3;
   rng q1 c= rng s1 by A25,NCFCONT1:9; then
A36:rng q1 c= Y by A24,XBOOLE_1:1; then
   rng q1 c= dom f by A3,XBOOLE_1:1; then
   rng q1 c= dom f /\ Y by A36,XBOOLE_1:19; then
A37:rng q1 c= dom (f|Y) by RELAT_1:90; then
A38:(f|Y)*q1 is convergent & (f|Y)/.(lim q1)=lim((f|Y)*q1)
     by A25,A26,NCFCONT1:def 16;
   rng q2 c= rng s2 by A34,NCFCONT1:9; then
A39:rng q2 c= Y by A35,XBOOLE_1:1; then
   rng q2 c= dom f by A3,XBOOLE_1:1; then
   rng q2 c= dom f /\ Y by A39,XBOOLE_1:19; then
A40:rng q2 c= dom (f|Y) by RELAT_1:90; then
A41:(f|Y)*q2 is convergent & (f|Y)/.(lim q1) = lim ((f|Y)*q2)
     by A26,A31,A32,NCFCONT1:def 16; then
A42:(f|Y)*q1 - (f|Y)*q2 is convergent by A38,NORMSP_1:35;
A43:lim ((f|Y)*q1 - (f|Y)*q2) = (f|Y)/.(lim q1) - (f|Y)/.(lim q1)
     by A38,A41,NORMSP_1:43
     .= 0.RNS by RLVECT_1:28;
   now let n be Nat;
     consider k be Nat such that
A44:  for m be Nat st k<=m holds
       ||.((f|Y)*q1 - (f|Y)*q2).m-0.RNS .||<r
        by A4,A42,A43,NORMSP_1:def 11;
A45: ||.((f|Y)*q1 - (f|Y)*q2).k - 0.RNS.||<r by A44;
A46: q1.k in rng q1 by NCFCONT1:8;
A47: q2.k in rng q2 by NCFCONT1:8;
     ||.((f|Y)*q1 - (f|Y)*q2).k - 0.RNS .||
      = ||.((f|Y)*q1 - (f|Y)*q2).k  .|| by RLVECT_1:26
     .= ||.((f|Y)*q1).k - ((f|Y)*q2).k.|| by NORMSP_1:def 6
     .= ||.(f|Y)/.(q1.k) - ((f|Y)*q2).k.|| by A37,NCFCONT1:11
     .= ||.(f|Y)/.(q1.k) - (f|Y)/.(q2.k) .|| by A40,NCFCONT1:11
     .= ||.f/.(q1.k) - (f|Y)/.(q2.k).|| by A37,A46,PARTFUN2:32
     .= ||.f/.(q1.k) - f/.(q2.k).|| by A40,A47,PARTFUN2:32
     .= ||.f/.(s1.(Ns1.k)) - f/.((s2*Ns1).k).|| by A27,A33,LOPBAN_3:7
     .= ||.f/.(s1.(Ns1.k)) - f/.(s2.(Ns1.k)).|| by LOPBAN_3:7;
     hence contradiction by A11,A45;
   end;
   hence contradiction;
end;

theorem
  for f be PartFunc of RNS,CNS, Y be Subset of RNS st
   Y is compact & f is_continuous_on Y holds
    f is_uniformly_continuous_on Y
proof
   let f be PartFunc of RNS,CNS;
   let Y be Subset of RNS;
   assume that
A1: Y is compact and
A2: f is_continuous_on Y;
A3: Y c= dom f by A2,NCFCONT1:def 23;
   assume not thesis; then
   consider r such that
A4: 0<r and
A5: for s st 0<s ex x1,x2 be Point of RNS st
     x1 in Y & x2 in Y & ||.x1 -x2.|| < s &
     not ||.f/.x1 - f/.x2.||< r by A3,Def3;
A6:now let n be Nat;
     0 <> n+1 by NAT_1:21; then
     0 < n+1 by NAT_1:19;
     hence 0 < 1/(n+1) by SEQ_2:6;
   end;
   defpred P[Nat,Point of RNS] means
    $2 in Y & ex x2 be Point of RNS st x2 in Y &
    ||.$2-x2.||<1/($1+1) & not ||.f/.($2)-f/.x2.|| < r;
A7:now let n be Nat;
     0 < 1/(n+1) by A6; then
     consider x1 be Point of RNS such that
A8:   ex x2 be Point of RNS st x1 in Y & x2 in Y & ||.x1 -x2.|| < 1/(n+1) &
      not ||.f/.x1 - f/.x2.||< r by A5;
     take x1;
     thus P[n,x1] by A8;
   end;
   consider s1 be sequence of RNS such that
A9: for n be Nat holds P[n,s1.n] from FUNCT_2:sch 3(A7);
   defpred P1[Nat, Point of RNS] means
    $2 in Y & ||.s1.$1-$2.|| < 1/($1+1) & not ||.f/.(s1.$1)-f/.$2.|| < r;
A10:for n be Nat ex x2 be Point of RNS st P1[n,x2] by A9;
   consider s2 be sequence of RNS such that
A11:for n be Nat holds P1[n,s2.n] from FUNCT_2:sch 3(A10);
A12:now let p be Real such that
A13: 0<p;
     consider k be Nat such that
A14:  p" < k by SEQ_4:10;
A15: 0 < p" by A13,REAL_1:72;
A16: k < k+1 by NAT_1:38; then
     p" < k+1 by A14,AXIOMS:22; then
     1/(k+1) < 1/p" by A15,SEQ_2:10; then
A17: 1/(k+1) < p by XCMPLX_1:218;
     take k;
     let m be Nat such that
A18:  k <= m;
A19: 0 < k+1 by A14,A15,A16;
     k+1 <= m+1 by A18,AXIOMS:24; then
A20: 1/(m+1) <= 1/(k+1) by A19,SEQ_4:1;
A21: ||.(s1-s2).m - 0.RNS.|| = ||.s1.m - s2.m - 0.RNS.|| by NORMSP_1:def 6
                             .= ||.s1.m - s2.m .|| by RLVECT_1:26;
     ||.s1.m - s2.m.|| < 1/(m+1) by A11; then
     ||.s1.m - s2.m.|| < 1/(k+1) by A20,AXIOMS:22;
     hence ||.(s1-s2).m - 0.RNS.|| < p by A17,A21,AXIOMS:22;
   end; then
A22:s1-s2 is convergent by NORMSP_1:def 9; then
A23:lim (s1-s2) = 0.RNS by A12,NORMSP_1:def 11;
    now let x be Point of RNS;
     assume x in rng s1; then
     ex n be Nat st x=s1.n by NFCONT_1:6;
     hence x in Y by A9;
   end; then
   for x be set st x in rng s1 holds x in Y; then
A24:rng s1 c= Y by TARSKI:def 3; then
   consider q1 be sequence of RNS such that
A25:q1 is subsequence of s1 & q1 is convergent &
    (lim q1) in Y by A1,NFCONT_1:def 4;
A26:f|Y is_continuous_in (lim q1) by A2,A25,NCFCONT1:def 23;
   consider Ns1 be increasing Seq_of_Nat such that
A27:q1=s1*Ns1 by A25,BHSP_3:def 4;
A28:(s1-s2)*Ns1 is subsequence of s1-s2 by BHSP_3:def 4; then
A29:(s1-s2)*Ns1 is convergent by A22,LOPBAN_3:11;
A30:lim ((s1-s2)*Ns1) = 0.RNS by A22,A23,A28,LOPBAN_3:12;
   set q2 = q1 - (s1-s2)*Ns1;
A31:q2 is convergent by A25,A29,NORMSP_1:35;
A32:lim q2 = lim q1 - 0.RNS by A25,A29,A30,NORMSP_1:43
          .= lim q1 by RLVECT_1:26;
   now let n be Nat;
     thus q2.n = (s1*Ns1).n - ((s1-s2)*Ns1).n by A27,NORMSP_1:def 6
     .= s1.(Ns1.n) - ((s1-s2)*Ns1).n by LOPBAN_3:7
     .= s1.(Ns1.n) - (s1-s2).(Ns1.n) by LOPBAN_3:7
     .= s1.(Ns1.n) - (s1.(Ns1.n)-s2.(Ns1.n)) by NORMSP_1:def 6
     .= (s1.(Ns1.n) - s1.(Ns1.n)) + s2.(Ns1.n) by RLVECT_1:43
     .= 0.RNS  + s2.(Ns1.n) by RLVECT_1:28
     .= s2.(Ns1.n) by RLVECT_1:10
     .= (s2*Ns1).n by LOPBAN_3:7;
   end; then
A33:q2 = s2*Ns1 by FUNCT_2:113; then
A34:q2 is subsequence of s2 by BHSP_3:def 4;
   now let x be set;
     assume x in rng s2; then
     ex n be Nat st x=s2.n by NFCONT_1:6;
     hence x in Y by A11;
   end; then
A35:rng s2 c= Y by TARSKI:def 3;
   rng q1 c= rng s1 by A25,NFCONT_1:7; then
A36:rng q1 c= Y by A24,XBOOLE_1:1; then
   rng q1 c= dom f by A3,XBOOLE_1:1; then
   rng q1 c= dom f /\ Y by A36,XBOOLE_1:19; then
A37:rng q1 c= dom (f|Y) by RELAT_1:90; then
A38:(f|Y)*q1 is convergent & (f|Y)/.(lim q1)=lim((f|Y)*q1)
     by A25,A26,NCFCONT1:def 17;
   rng q2 c= rng s2 by A34,NFCONT_1:7; then
A39:rng q2 c= Y by A35,XBOOLE_1:1; then
   rng q2 c= dom f by A3,XBOOLE_1:1; then
   rng q2 c= dom f /\ Y by A39,XBOOLE_1:19; then
A40:rng q2 c= dom (f|Y) by RELAT_1:90; then
A41:(f|Y)*q2 is convergent & (f|Y)/.(lim q1) = lim ((f|Y)*q2)
     by A26,A31,A32,NCFCONT1:def 17; then
A42:(f|Y)*q1 - (f|Y)*q2 is convergent by A38,CLVECT_1:116;
A43:lim ((f|Y)*q1 - (f|Y)*q2) = (f|Y)/.(lim q1) - (f|Y)/.(lim q1)
     by A38,A41,CLVECT_1:122
     .= 0.CNS by RLVECT_1:28;
   now let n be Nat;
     consider k be Nat such that
A44:  for m be Nat st k<=m holds
       ||.((f|Y)*q1 - (f|Y)*q2).m-0.CNS .||<r
        by A4,A42,A43,CLVECT_1:def 17;
A45: ||.((f|Y)*q1 - (f|Y)*q2).k - 0.CNS.||<r by A44;
A46: q1.k in rng q1 by NFCONT_1:6;
A47: q2.k in rng q2 by NFCONT_1:6;
     ||.((f|Y)*q1 - (f|Y)*q2).k - 0.CNS .||
      = ||.((f|Y)*q1 - (f|Y)*q2).k  .|| by RLVECT_1:26
     .= ||.((f|Y)*q1).k - ((f|Y)*q2).k.|| by CLVECT_1:def 12
     .= ||.(f|Y)/.(q1.k) - ((f|Y)*q2).k.|| by A37,NCFCONT1:12
     .= ||.(f|Y)/.(q1.k) - (f|Y)/.(q2.k) .|| by A40,NCFCONT1:12
     .= ||.f/.(q1.k) - (f|Y)/.(q2.k).|| by A37,A46,PARTFUN2:32
     .= ||.f/.(q1.k) - f/.(q2.k).|| by A40,A47,PARTFUN2:32
     .= ||.f/.(s1.(Ns1.k)) - f/.((s2*Ns1).k).|| by A27,A33,LOPBAN_3:7
     .= ||.f/.(s1.(Ns1.k)) - f/.(s2.(Ns1.k)).|| by LOPBAN_3:7;
     hence contradiction by A11,A45;
   end;
   hence contradiction;
end;

theorem
  for f be PartFunc of CNS1,CNS2, Y be Subset of CNS1 st
   Y c= dom f & Y is compact & f is_uniformly_continuous_on Y holds
    f.:Y is compact
proof
   let f be PartFunc of CNS1,CNS2;
   let Y be Subset of CNS1;
   assume
A1:Y c= dom f & Y is compact & f is_uniformly_continuous_on Y; then
   f is_continuous_on Y by Th19;
   hence thesis by A1,NCFCONT1:104;
end;

theorem
  for f be PartFunc of CNS,RNS, Y be Subset of CNS st
   Y c= dom f & Y is compact & f is_uniformly_continuous_on Y holds
    f.:Y is compact
proof
   let f be PartFunc of CNS,RNS;
   let Y be Subset of CNS;
   assume
A1:Y c= dom f & Y is compact & f is_uniformly_continuous_on Y; then
   f is_continuous_on Y by Th20;
   hence thesis by A1,NCFCONT1:105;
end;

theorem
  for f be PartFunc of RNS,CNS, Y be Subset of RNS st
   Y c= dom f & Y is compact & f is_uniformly_continuous_on Y holds
    f.:Y is compact
proof
   let f be PartFunc of RNS,CNS;
   let Y be Subset of RNS;
   assume
A1:Y c= dom f & Y is compact & f is_uniformly_continuous_on Y; then
   f is_continuous_on Y by Th21;
   hence thesis by A1,NCFCONT1:106;
end;

theorem
  for f be PartFunc of the carrier of CNS,REAL, Y be Subset of CNS st
   Y <> {} & Y c= dom f & Y is compact & f is_uniformly_continuous_on Y
    ex x1,x2 be Point of CNS st x1 in Y & x2 in Y &
     f/.x1 = upper_bound (f.:Y) & f/.x2 = lower_bound (f.:Y)
proof
   let f be PartFunc of the carrier of CNS,REAL;
   let Y be Subset of CNS;
   assume
A1:Y <> {} & Y c= dom f & Y is compact &
    f is_uniformly_continuous_on Y; then
   f is_continuous_on Y by Th23;
   hence thesis by A1,NCFCONT1:117;
end;

theorem
  for f be PartFunc of CNS1,CNS2 st
   X c= dom f & f is_constant_on X holds
    f is_uniformly_continuous_on X
proof
   let f be PartFunc of CNS1,CNS2;
   assume X c= dom f & f is_constant_on X; then
   f is_Lipschitzian_on X by NCFCONT1:133;
   hence thesis by Th25;
end;

theorem
  for f be PartFunc of CNS,RNS st
   X c= dom f & f is_constant_on X holds
    f is_uniformly_continuous_on X
proof
   let f be PartFunc of CNS,RNS;
   assume X c= dom f & f is_constant_on X; then
   f is_Lipschitzian_on X by NCFCONT1:134;
   hence thesis by Th26;
end;

theorem
  for f be PartFunc of RNS,CNS st
   X c= dom f & f is_constant_on X holds
    f is_uniformly_continuous_on X
proof
   let f be PartFunc of RNS,CNS;
   assume X c= dom f & f is_constant_on X; then
   f is_Lipschitzian_on X by NCFCONT1:135;
   hence thesis by Th27;
end;

begin :: Contraction Mapping Principle on Normed Complex Linear Spaces

definition
  let M be ComplexBanachSpace;
  mode contraction of M -> Function of the carrier of M, the carrier of M
    means :Def7:
  ex L being Real st 0 < L & L < 1 &
    for x,y being Point of M holds
      ||.it.x - it.y.|| <=L*||.x - y.||;
existence
proof
   consider x being Point of M;
   (the carrier of M) --> x is Function of the carrier of M, the carrier of M
   proof
A1:  dom ((the carrier of M) --> x) = the carrier of M by FUNCOP_1:19;
     rng ((the carrier of M) --> x) c= {x} by FUNCOP_1:19; then
     rng ((the carrier of M) --> x) c= the carrier of M by XBOOLE_1:1;
     hence thesis by A1,FUNCT_2:def 1,RELSET_1:11;
   end; then
   reconsider f = (the carrier of M) --> x
     as Function of the carrier of M, the carrier of M;
   take f, 1/2;
   thus 0<1/2 & 1/2<1;
   let z,y be Point of M;
   f.z=x & f.y=x by FUNCOP_1:13; then
A2: ||.f.z-f.y.|| = 0 by CLVECT_1:108;
   ||.z-y.||>=0 by CLVECT_1:106;
   hence ||.f.z-f.y.||<=(1/2)*||.z-y.|| by A2,REAL_2:121;
end;
end;

theorem
for X be ComplexNormSpace, x,y be Point of X holds ||.x-y.|| > 0 iff x <> y
proof
   let X be ComplexNormSpace;
   let x, y be Point of X;
   0 < ||.x-y.|| implies x-y <> 0.X by CLVECT_1:def 10;
   hence 0 < ||.x-y.|| implies x <> y by RLVECT_1:28;
   now assume x <> y; then
     0 <> ||.x-y.|| by CLVECT_1:113;
     hence 0 < ||.x-y.|| by CLVECT_1:106;
   end;
   hence x <> y implies 0 < ||.x-y.||;
end;

theorem
for X be ComplexNormSpace, x, y be Point of X holds ||.x-y.|| = ||.y-x.||
proof
   let X be ComplexNormSpace;
   let x, y be Point of X;
   thus ||.x-y.|| = ||.-(x-y).|| by CLVECT_1:104
     .= ||.y+(-x).|| by RLVECT_1:47
     .= ||.y-x.|| by RLVECT_1:def 11;
end;

Lm1:
for X be ComplexNormSpace, x,y,z be Point of X, e be Real st e > 0 holds
 ( (||.x-z.|| <e/2 & ||.z-y.|| <e/2) implies ||.x-y.|| <e )
proof
   let X be ComplexNormSpace;
   let x,y,z be Point of X;
   let e be Real such that e > 0;
   assume ||.x-z.|| < e/2 & ||.z-y.|| < e/2; then
A1: ||.x-z.|| + ||.z-y.|| < e/2 + e/2 by REAL_1:67;
   ||.x-y.|| <= ||.x-z.|| + ||.z-y.|| by CLVECT_1:112; then
   ||.x-y.|| + (||.x-z.|| + ||.z-y.||)
      < (||.x-z.|| + ||.z-y.||) + e by A1,REAL_1:67; then
   ||.x-y.|| + (||.x-z.|| + ||.z-y.||) +- (||.x-z.|| + ||.z-y.||)
      < e + (||.x-z.|| + ||.z-y.||) +- (||.x-z.|| + ||.z-y.||)
       by REAL_1:67;
   hence thesis;
end;

Lm2:
for X be ComplexNormSpace, x,y,z be Point of X, e be Real st e>0 holds
 (||.x-z.|| <e/2 & ||.y-z.|| < e/2) implies ||.x-y.|| <e
proof
   let X be ComplexNormSpace;
   let x,y,z be Point of X;
   let e be Real such that
A1:e > 0;
   assume
A2: ||.x-z.|| < e/2 & ||.y-z.|| < e/2; then
   ||.-(y-z).|| < e/2 by CLVECT_1:104; then
   ||.z+-y.|| < e/2 by RLVECT_1:47; then
   ||.z-y.|| < e/2 by RLVECT_1:def 11;
   hence thesis by A1,A2,Lm1;
end;

Lm3:
for X be ComplexNormSpace, x be Point of X st
 (for e be Real st e>0 holds ||.x.|| <e) holds x=0.X
proof
   let X be ComplexNormSpace;
   let x be Point of X such that
A1:for e be Real st e > 0 holds ||.x.|| <e;
   now assume
A2:  x<>0.X;
     0 <> ||.x.|| by A2,CLVECT_1:def 10; then
     0 < ||.x.|| by CLVECT_1:106;
     hence contradiction by A1;
   end;
   hence thesis;
end;

Lm4:
for X be ComplexNormSpace, x,y be Point of X st
 (for e be Real st e>0 holds ||.x-y.|| <e) holds x=y
proof
   let X be ComplexNormSpace;
   let x,y be Point of X such that
A1:for e be Real st e > 0 holds ||.x-y.|| <e;
   x-y = 0.X by A1,Lm3;
   hence thesis by RLVECT_1:35;
end;

Lm5:
for K, L, e be real number st 0 < K & K < 1 & 0 < e
  ex n be Nat st abs (L* (K to_power n )) <e by NFCONT_2:16;

theorem Th40:
for X be ComplexBanachSpace, f be Function of X,X st
 f is contraction of X
  ex xp be Point of X st f.xp=xp &
   for x be Point of X st f.x=x holds xp=x
proof
   let X be ComplexBanachSpace;
   let f be Function of X,X such that
A1:f is contraction of X;
   consider x0 be Element of the carrier of X;
   deffunc G(set,set)=f.$2;
   consider g be Function such that
A2:dom g = NAT & g.0 = x0 &
    for n being Element of NAT holds g.(n+1) = G(n,g.n)
     from RECDEF_1:sch 3;
   consider K be Real such that
A3: 0 < K and
A4: K < 1 and
A5: for x,y be Point of X holds
       ||.f.x - f.y.|| <= K*||.x - y.|| by A1,Def7;
   defpred P[Nat] means g.$1 in the carrier of X;
A6:P[0] by A2;
A7:for k be Nat st P[k] holds P[k+1]
   proof
    let k be Nat such that
A8: P[k];
    g.(k+1)=G(k,g.k) by A2 .= f.(g.k);
    hence thesis by A8,FUNCT_2:7;
   end;
A9:for n be Nat holds P[n] from NAT_1:sch 1(A6,A7);
   for n be set st n in NAT holds g.n in the carrier of X by A9; then
   reconsider g as sequence of X by A2,FUNCT_2:5;
A10:for n be Nat holds ||.g.(n+1) - g.n .||
      <= ||. g.1-g.0 .|| * (K to_power n )
   proof
    defpred P[Nat] means
     ||.g.($1+1) - g.($1) .|| <= ||. g.1-g.0 .||* (K to_power $1 );
A11: ||.g.(0+1) - g.0 .|| = ||. g.1-g.0 .||  * 1
                          .= ||. g.1-g.0 .|| * (K to_power 0 ) by POWER:29;
A12: P[0] by A11;
A13: for k be Nat st P[k] holds P[k+1]
    proof
     let k be Nat such that
A14: P[k];
A15: g.((k+1)+1) = G(k,g.(k+1)) by A2
         .=f.(g.(k+1));
A16: g.(k+1) = G(k,g.k) by A2
            .=f.(g.k);
A17: ||.f.(g.(k+1)) - f.(g.k) .|| <= K* ||. g.(k+1) - g.k .|| by A5;
     K* ||. g.(k+1) - g.k .||
      <=  K*( ||. g.1-g.0 .||* (K to_power k ) ) by A3,A14,AXIOMS:25; then
     ||.f.(g.(k+1)) - f.(g.k)  .||
      <=||. g.1-g.0 .||*(K* (K to_power k )) by A17,AXIOMS:22; then
     ||.f.(g.(k+1)) - f.(g.k)  .||
      <=||. g.1-g.0 .||*((K to_power 1)* (K to_power k )) by POWER:30;
     hence thesis by A3,A15,A16,POWER:32;
    end;
    for n be Nat holds P[n] from NAT_1:sch 1(A12,A13);
    hence thesis;
   end;
A18:for k,n be Nat holds
   ||.g.(n+k) - g.n .||
      <= ||. g.1-g.0 .||* ((K to_power n - K to_power (n+k)) /(1-K))
   proof
    defpred P[Nat] means
    for n be Nat holds ||.g.(n+$1) - g.n .||
         <= ||. g.1-g.0 .||*( (K to_power n-K to_power (n+$1)) /(1-K));
    now let n be Nat;
A19:  ||.g.(n+0) - g.n .|| = ||.g.n - g.n .||
                         .= ||.0.X.|| by RLVECT_1:28
                         .= 0 by CLVECT_1:103;
A20:  (K to_power n-K to_power (n+0)) /(1-K)
           =( (K to_power n-K to_power  n  ) /(1-K))
          .= 0/(1-K)
          .= 0;
A21:  ||. g.1-g.0 .||*( (K to_power n-K to_power  (n+0)  ) /(1-K))
           = ||. g.1-g.0 .||*0 by A20
          .=0;
     thus ||.g.(n+0) - g.n .||
         <= ||. g.1-g.0 .||* ((K to_power n-K to_power (n+0)) /(1-K))
        by A19,A21;
    end; then
A22: P[0];
A23: now let k be Nat such that
A24: P[k];
     now let n be Nat;
A25:  ||.g.(n+k) - g.n .||
          <= ||. g.1-g.0 .||* ((K to_power n - K to_power (n+k)) /(1-K))
           by A24;
A26:  ||.g.(n+(k+1)) - g.n .||
          <= ||.g.(n+(k+1)) - g.(n+k) .|| +  ||.g.(n+k) - g.n .||
          by CLVECT_1:112;
      ||.g.((n+k)+1) - g.(n+k) .||
          <= ||. g.1-g.0 .||* (K to_power (n+k)) by A10; then
A27:  ||.g.(n+(k+1)) - g.(n+k) .|| +  ||.g.(n+k) - g.n .||
          <= ||. g.1-g.0 .|| * (K to_power (n+k))
           + ||. g.1-g.0 .|| * ((K to_power n - K to_power (n+k)) /(1-K))
               by A25,REAL_1:55;
      1- K <> 0 by A4,XCMPLX_1:15; then
      ||. g.1-g.0 .||*((K to_power n - K to_power (n+k)) /(1-K))
      + ||. g.1-g.0 .||*(K to_power (n+k))
       = ||. g.1-g.0 .||*((K to_power n - K to_power (n+k))/(1-K))
       + ||. g.1-g.0 .||*(K to_power (n+k))*(1-K)/(1-K) by XCMPLX_1:90
      .= ||. g.1-g.0 .||*((K to_power n - K to_power (n+k))/(1-K))
       + ||. g.1-g.0 .||*((K to_power (n+k))*(1-K))/(1-K)
      .= ||. g.1-g.0 .||*((K to_power n - K to_power (n+k))/(1-K))
       + ||. g.1-g.0 .||*((K to_power (n+k)*(1-K))/(1-K)) by XCMPLX_1:75
      .= ||. g.1-g.0 .||*(((K to_power n - K to_power (n+k))/(1-K))
       + ((K to_power (n+k)*(1-K))/(1-K)))
      .= ||. g.1-g.0 .||*(  ((K to_power n - K to_power (n+k))
       + (K to_power (n+k)*(1-K)))/(1-K)) by XCMPLX_1:63
      .= ||. g.1-g.0 .||*((K to_power n - K to_power (n+k)
       + (1-K)*K to_power (n+k) ) /(1-K))
      .= ||. g.1-g.0 .||*(((K to_power n - K to_power (n+k))
       + (1-K)*K to_power (n+k) ) /(1-K))
      .= ||. g.1-g.0 .||*((K to_power n - K to_power (n+k)
       + (1*K to_power (n+k) - K*K to_power (n+k)) ) / (1-K))
      .= ||. g.1-g.0 .||*((K to_power n - K to_power (n+k)
       + (1*K to_power (n+k) - K*K to_power (n+k)) ) / (1-K))
      .= ||. g.1-g.0 .||*((K to_power n - K to_power (n+k)
       + (K to_power (n+k) - K*K to_power (n+k)) ) / (1-K))
      .= ||. g.1-g.0 .||*((K to_power n- K*K to_power (n+k)) / (1-K))
      .= ||. g.1-g.0 .||*((K to_power n- (K to_power 1)
        *K to_power (n+k)) / (1-K)) by POWER:30
      .= ||. g.1-g.0 .||*((K to_power n - K to_power (n+k+1))/(1-K))
        by A3,POWER:32;
      hence ||.g.(n+(k+1)) - g.n .||
         <= ||. g.1-g.0 .||*((K to_power n  - K to_power (n+(k+1)))/(1-K))
         by A26,A27,AXIOMS:22;
     end;
     hence P[k+1];
    end;
    for k be Nat holds P[k] from NAT_1:sch 1(A22,A23);
    hence thesis;
   end;
A28: for k,n be Nat holds
    ||.g.(n+k) - g.n .|| <= ||. g.1-g.0 .||* (K to_power n/(1-K))
   proof
    let k be Nat;
    now let n be Nat;
A29: ||.g.(n+k) - g.n .||
      <= ||. g.1-g.0 .||*( (K to_power n - K to_power (n+k)) /(1-K)) by A18;
A30: (K to_power n - K to_power (n+k)) /(1-K) <= (( K to_power n ) /(1-K))
     proof
A31: K to_power (n+k) > 0 by A3,POWER:39;
A32: 1-K  > 1-1 by A4,REAL_1:92;
      K to_power n - K to_power (n+k) <= K to_power n - 0 by A31,REAL_1:92;
      hence thesis by A32,REAL_1:73;
     end;
     0 <= ||. g.1-g.0 .|| by CLVECT_1:106; then
     ||. g.1-g.0 .|| * ((K to_power n - K to_power (n+k)) /(1-K))
      <= ||. g.1-g.0 .|| * ((K to_power n) /(1-K)) by A30,AXIOMS:25;
     hence ||.g.(n+k) - g.n .|| <= ||. g.1-g.0 .||* ((K to_power n) /(1-K))
        by A29,AXIOMS:22;
    end;
    hence thesis;
   end;
   g is Cauchy_sequence_by_Norm
   proof
    now let e be Real such that
A33: e >0;
A34: e/2 > 0 by A33,SEQ_2:3;
    consider n be Nat such that
A35:abs( ||. g.1-g.0 .||/(1-K)* (K to_power n) ) < e/2 by A3,A4,A34,Lm5;
  ||. g.1-g.0 .||/(1-K)* (K to_power n)
 <= abs( ||. g.1-g.0 .||/(1-K)* (K to_power n) ) by ABSVALUE:11; then
   ||. g.1-g.0 .||/(1-K)* (K to_power n) < e/2 by A35,AXIOMS:22; then
A36:   ||. g.1-g.0 .|| *( (K to_power n)/(1-K)) < e/2 by XCMPLX_1:76;
  take nn=n+1;
   now let m,l be Nat such that
        A37:  nn <= m and
        A38:  nn <= l;
        A39: n < m by A37,NAT_1:38;
        A40: n < l by A38,NAT_1:38;
        consider k1 be Nat such that
   A41: n+k1 =m by A39,NAT_1:28;
        consider k2 be Nat such that
   A42: n+k2 =l by A40,NAT_1:28;
         ||.g.(n+k1) - g.n .||
         <= ||. g.1-g.0 .||*( (K to_power n) /(1-K)) by A28; then
   A43: ||.g.m - g.n .|| < e/2 by A36,A41,AXIOMS:22;
         ||.g.(n+k2) - g.n .||
         <= ||. g.1-g.0 .||*( (K to_power n) /(1-K) ) by A28; then
        ||.g.l - g.n .|| < e/2 by A36,A42,AXIOMS:22;
        hence ||.g.l - g.m .|| < e by A33,A43,Lm2;
       end;
  hence for n, m be Nat st n >= nn & m >= nn holds ||.g.n - g.m.|| < e;
 end;
 hence thesis by CSSPACE3:10;
end; then
A44: g is convergent by CLOPBAN1:def 14;
A45: lim g = f.(lim g)
proof
A46: g^\1 is convergent & lim (g^\1) = lim g by A44,CLOPBAN3:13;
A47:now let n be Nat;
    ||. (g^\1).n - f. (lim g) .||
     = ||. g.(n+1) - f. (lim g) .|| by CLOPBAN3:def 6
     .= ||. f.(g.n)- f. (lim g) .|| by A2;
  hence ||. (g^\1).n - f. (lim g) .|| <= K* ||. g.n - lim g .|| by A5;
 end;
 A48: ||. g - lim g .|| is convergent by A44,CLVECT_1:120;
 A49: K(#)||. g - lim g .|| is convergent by A48,SEQ_2:21;
 A50: lim (K(#)(||.g - lim g.||)) =K*lim ||.(g - lim g).|| by A48,SEQ_2:22
                                    .=K*0 by A44,CLVECT_1:120
                                    .=0;
 for e be Real st e >0 ex n be Nat st
         for m be Nat st n<=m holds ||. (g^\1).m - f. (lim g) .|| <e
proof
  let e be Real such that
   A51: e >0;
   consider n be Nat such that
   A52: for m be Nat st n<=m holds abs( (K(#)||. g - lim g .||).m-0)
      < e by A49,A50,A51,SEQ_2:def 7;
  take n;
 now let m be Nat such that
     A53: n<=m;
     abs( (K(#)||. g - lim g .||).m-0)<e by A52,A53; then
     abs( K*||. g - lim g .||.m) < e by SEQ_1:13; then
      abs( K*||. (g - lim g).m .||) < e by CLVECT_1:def 16; then
     A54: abs( K*||. g.m - lim g .||) < e by CLVECT_1:def 13;
     A55:K*||. g.m - lim g .|| <=abs( K*||. g.m - lim g .||) by ABSVALUE:11;
     A56: K*||. g.m - lim g .|| < e by A54,A55,AXIOMS:22;
      ||. (g^\1).m - f. (lim g) .|| <= K* ||. g.m - lim g .|| by A47;
     hence ||. (g^\1).m - f. (lim g) .|| < e by A56,AXIOMS:22;
   end;
  hence thesis;
end;
hence thesis by A46,CLVECT_1:def 17;
end;
take xp=lim g;
A57:
 now let x be Point of X such that
   A58: f.x=x;
   A59: for k be Nat holds ||.x-xp.|| <= ||.x-xp.||*(K to_power k)
    proof
  defpred P[Nat] means
     ||.x-xp .|| <= ||.x-xp.||*(K to_power $1);
 A60:   ||.x-xp .|| = ||. x-xp .|| * 1
                  .= ||. x-xp .|| * (K to_power 0 ) by POWER:29;
 A61:  P[0] by A60;
 A62:  for k be Nat st P[k] holds P[k+1]
 proof
 let k be Nat such that
A63: P[k];
A64: ||.f.x - f.xp .|| <= K* ||. x-xp .|| by A5;
    K* ||. x-xp .||
   <= K*( ||.x-xp.||*(K to_power k) ) by A3,A63,AXIOMS:25; then
    ||.f.x - f.xp .||
         <= ||. x-xp .||*(K* (K to_power k )) by A64,AXIOMS:22;
   then ||.f.x - f.xp .||
            <= ||. x-xp .||*((K to_power 1)* (K to_power k )) by POWER:30;
   hence thesis by A3,A45,A58,POWER:32;
 end;
 for n be Nat holds P[n] from NAT_1:sch 1(A61,A62);
hence thesis;
 end;
A65: for e be Real st 0 <e holds ||.x-xp.|| < e
      proof
      let e be Real such that
      A66: 0 < e;
      consider n be Nat such that
A67:  abs( ||.x-xp.||*(K to_power n) ) < e by A3,A4,A66,Lm5;
A68:  ||.x-xp.||*(K to_power n)
      <=abs( ||.x-xp.||*(K to_power n) ) by ABSVALUE:11;
A69:  ||.x-xp.||*(K to_power  n) <e by A67,A68,AXIOMS:22;
        ||.x-xp.|| <= ||.x-xp.||*(K to_power n) by A59;
      hence ||.x-xp.|| < e by A69,AXIOMS:22;
      thus thesis;
      end;
      thus x=xp by A65,Lm4;
  end;
  thus thesis by A45,A57;
end;

theorem
for X be ComplexBanachSpace
    for f be Function of X,X  st
      ex n0 be Nat st iter(f,n0) is contraction of X
       holds ex xp be Point of X st
          f.xp=xp &
          (for x be Point of X st f.x=x holds xp=x)
 proof
   let X be ComplexBanachSpace;
   let f be Function of X, X;
   given n0 be Nat such that
    A1:iter(f,n0) is contraction of X;
   consider xp be Point of X such that
    A2:(iter(f,n0)).xp=xp and
    A3: for x be Point of X st iter(f,n0).x=x
            holds xp=x by A1,Th40;
A4:iter(f,n0) is Function of X,X by FUNCT_7:85;
    A5: (iter(f,n0)).(f.xp) =(iter(f,n0)*f).xp by FUNCT_2:21
           .=(iter(f,n0+1)).xp by FUNCT_7:71
           .=(f*iter(f,n0)).xp by FUNCT_7:73
           .=f.((iter(f,n0)).(xp)) by A4,FUNCT_2:21
            .=f.xp by A2;
    A6: f.xp=xp by A3,A5;
     now let x be Point of X such that
              A7: f.x=x;
             for n be Nat holds (iter(f,n)).x=x
             proof
              defpred P[Nat] means (iter(f,$1)).x=x;
             (iter(f,0)).x=(id the carrier of X).x by FUNCT_7:86
                      .=x by FUNCT_1:34; then
             A8: P[0];
             A9: now let n be Nat such that
             A10: P[n];
A11:          iter(f,n) is Function of X,X by FUNCT_7:85;
              (iter(f,n+1)).x =(f*iter(f,n)).x by FUNCT_7:73
                             .=f.(iter(f,n).x) by A11,FUNCT_2:21
                             .=f.x by A10
                             .=x by A7;
               hence P[n+1];
             end;
             for n be Nat holds P[n] from NAT_1:sch 1(A8,A9);
             hence thesis;
           end;
           then (iter(f,n0)).x=x;
           hence xp=x by A3;
    end;
    hence thesis by A6;
end;
