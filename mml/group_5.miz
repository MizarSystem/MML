:: Commutator and Center of a Group
::  by Wojciech A. Trybulec
::
:: Received May 15, 1991
:: Copyright (c) 1991 Association of Mizar Users
 
environ

 vocabularies XBOOLE_0, SUBSET_1, TARSKI, NUMBERS, INT_1, GROUP_1, GROUP_2,
      PRE_TOPC, FINSEQ_1, STRUCT_0, NEWTON, RELAT_1, EQREL_1, BINOP_1, NAT_1,
      FUNCT_1, PARTFUN1, ORDINAL4, ARYTM_3, CARD_1, CARD_3, QC_LANG1, XXREAL_1,
      ARYTM_1, GROUP_4, RLSUB_1, FINSET_1, CQC_SIM1, GROUP_3, GROUP_5;
 notations TARSKI, XBOOLE_0, SUBSET_1, NUMBERS, XCMPLX_0, NAT_1, FUNCT_1,
      PARTFUN1, CARD_1, FINSEQ_1, FINSET_1, FINSEQ_4, INT_1, DOMAIN_1,
      STRUCT_0, ALGSTR_0, GROUP_1, GROUP_2, GROUP_3, GROUP_4;
 constructors PARTFUN1, SETFAM_1, NAT_1, FINSEQ_4, FINSOP_1, REALSET1, GROUP_4,
      RELSET_1, BINOP_2;
 registrations XBOOLE_0, SUBSET_1, RELSET_1, NUMBERS, XREAL_0, INT_1, STRUCT_0,
      GROUP_1, GROUP_2, GROUP_3, ORDINAL1, FINSEQ_1, CARD_1;
 requirements NUMERALS, BOOLE, SUBSET;
 definitions TARSKI, GROUP_1, XBOOLE_0, GROUP_2, GROUP_3, GROUP_4;
 theorems FINSEQ_1, FINSEQ_2, FINSEQ_3, FINSEQ_4, FUNCT_1, GROUP_1, GROUP_2,
      GROUP_3, GROUP_4, TARSKI, ZFMISC_1, XBOOLE_0, XBOOLE_1, CARD_2, FINSOP_1,
      STRUCT_0, PARTFUN1;
 schemes FINSEQ_2, DOMAIN_1;

begin :: Preliminaries.

reserve x,y for set,
  k,n for Element of NAT,
  i for Integer,
  G for Group,
  a,b,c ,d,e for Element of G,
  A,B,C,D for Subset of G,
  H,H1,H2,H3,H4 for Subgroup of G ,
  N1,N2 for normal Subgroup of G,
  F,F1,F2 for FinSequence of the carrier of G,
  I,I1,I2 for FinSequence of INT;

theorem Th1:
  x in (1).G iff x = 1_G
proof
  thus x in (1).G implies x = 1_G
  proof
    assume x in (1).G;
    then x in the carrier of (1).G by STRUCT_0:def 5;
    then x in {1_G} by GROUP_2:def 7;
    hence thesis by TARSKI:def 1;
  end;
  thus thesis by GROUP_2:55;
end;

theorem
  a in H & b in H implies a |^ b in H
proof
  assume a in H & b in H;
  then b" in H & a * b in H by GROUP_2:59,60;
  then b " * (a * b) in H by GROUP_2:59;
  hence thesis by GROUP_1:def 4;
end;

theorem Th3:
  for N being strict normal Subgroup of G holds a in N implies a |^ b in N
proof
  let N be strict normal Subgroup of G;
  assume a in N;
  then a |^ b in N |^ b by GROUP_3:70;
  hence thesis by GROUP_3:def 13;
end;

theorem Th4:
  x in H1 * H2 iff ex a,b st x = a * b & a in H1 & b in H2
proof
  thus x in H1 * H2 implies ex a,b st x = a * b & a in H1 & b in H2
  proof
    assume x in H1 * H2;
    then x in carr H1 * H2;
    then consider a,b such that
A1: x = a * b and
A2: a in carr H1 and
A3: b in H2 by GROUP_2:114;
    a in H1 by A2,STRUCT_0:def 5;
    hence thesis by A1,A3;
  end;
  given a,b such that
A4: x = a * b & a in H1 and
A5: b in H2;
  b in carr H2 by A5,STRUCT_0:def 5;
  then x in H1 * carr H2 by A4,GROUP_2:115;
  hence thesis;
end;

theorem Th5:
  H1 * H2 = H2 * H1 implies (x in H1 "\/" H2 iff ex a,b st x = a *
  b & a in H1 & b in H2)
proof
  assume
A1: H1 * H2 = H2 * H1;
  thus x in H1 "\/" H2 implies ex a,b st x = a * b & a in H1 & b in H2
  proof
    assume x in H1 "\/" H2;
    then x in the carrier of H1 "\/" H2 by STRUCT_0:def 5;
    then x in H1 * H2 by A1,GROUP_4:69;
    hence thesis by Th4;
  end;
  given a,b such that
A2: x = a * b & a in H1 & b in H2;
  x in H1 * H2 by A2,Th4;
  then x in the carrier of H1 "\/" H2 by A1,GROUP_4:69;
  hence thesis by STRUCT_0:def 5;
end;

theorem
  G is commutative Group implies (x in H1 "\/" H2 iff ex a,b st x = a *
  b & a in H1 & b in H2)
proof
  assume G is commutative Group;
  then H1 * H2 = H2 * H1 by GROUP_2:29;
  hence thesis by Th5;
end;

theorem Th7:
  for N1,N2 being strict normal Subgroup of G holds x in N1 "\/" N2
  iff ex a,b st x = a * b & a in N1 & b in N2
proof
  let N1,N2 be strict normal Subgroup of G;
  N1 * N2 = N2 * N1 by GROUP_3:148;
  hence thesis by Th5;
end;

theorem
  for N being normal Subgroup of G holds H * N = N * H
proof
  let N be normal Subgroup of G;
  thus H * N = carr H * N .= N * carr H by GROUP_3:143
    .= N * H;
end;

definition
  let G,F,a;
  func F |^ a -> FinSequence of the carrier of G means
  :Def1:
  len it = len F & for k be Nat st k in dom F holds it.k = (F/.k) |^ a;
  existence
  proof
    deffunc F(Nat) = (F/.$1) |^ a;
    consider F1 such that
A1: len F1 = len F & for k be Nat st k in dom F1 holds F1.k = F(k)
    from FINSEQ_2:sch 1;
    dom F1 = dom F by A1,FINSEQ_3:31;
    hence thesis by A1;
  end;
  correctness
  proof
    let F1,F2;
    assume that
A2: len F1 = len F and
A3: for k be Nat st k in dom F holds F1.k = (F/.k) |^ a and
A4: len F2 = len F and
A5: for k be Nat st k in dom F holds F2.k = (F/.k) |^ a;
A6: dom F1 = Seg len F by A2,FINSEQ_1:def 3;
    now
      let k be Nat;
      assume k in dom F1;
      then
A7:   k in dom F by A6,FINSEQ_1:def 3;
      hence F1.k = (F/.k) |^ a by A3
        .= F2.k by A5,A7;
    end;
    hence thesis by A2,A4,FINSEQ_2:10;
  end;
end;

canceled 3;

theorem Th12:
  (F1 |^ a) ^ (F2 |^ a) = (F1 ^ F2) |^ a
proof
A1: now
    let k;
    assume k in dom(F1 |^ a);
    then k in Seg len(F1 |^ a) by FINSEQ_1:def 3;
    then k in Seg len F1 by Def1;
    then
A2: k in dom F1 by FINSEQ_1:def 3;
    then
A3: F1/.k = (F1 ^ F2)/.k & k in dom(F1 ^ F2) by
    FINSEQ_3:24,FINSEQ_4:83;
    thus (F1 |^ a).k = (F1/.k) |^ a by A2,Def1
      .= ((F1 ^ F2) |^ a).k by A3,Def1;
  end;
A4: now
    let k;
    assume
A5: k in dom(F2 |^ a);
    len F2 = len(F2 |^ a) by Def1;
    then
A6: k in dom F2 by A5,FINSEQ_3:31;
    then len F1 + k in dom(F1 ^ F2) by FINSEQ_1:41;
    then len(F1 |^ a) + k in dom(F1 ^ F2) by Def1;
    hence
    ((F1 ^ F2) |^ a).(len(F1 |^ a) + k) = ((F1 ^ F2)/.(len(F1 |^ a) + k))
    |^ a by Def1
      .= ((F1 ^ F2)/.(len F1 + k)) |^ a by Def1
      .= (F2/.k) |^ a by A6,FINSEQ_4:84
      .= (F2 |^ a).k by A6,Def1;
  end;
  len(F1 |^ a) + len(F2 |^ a) = len F1 + len(F2 |^ a) by Def1
    .= len F1 + len F2 by Def1
    .= len(F1 ^ F2) by FINSEQ_1:35
    .= len((F1 ^ F2) |^ a) by Def1;
  hence thesis by A1,A4,FINSEQ_3:43;
end;

theorem Th13:
  (<*> the carrier of G) |^ a = {}
proof
  len((<*> the carrier of G) |^ a) = len <*> the carrier of G by Def1
    .= 0;
  hence thesis;
end;

theorem Th14:
  <* a *> |^ b = <* a |^ b *>
proof
A1: now
    let k be Nat;
    assume k in dom<* a *>;
    then k in {1} by FINSEQ_1:4,55;
    then
A2: k = 1 by TARSKI:def 1;
    hence <* a |^ b *>.k = a |^ b by FINSEQ_1:57
      .= (<* a *>/.k) |^ b by A2,FINSEQ_4:25;
  end;
  len<* a |^ b *> = 1 by FINSEQ_1:57
    .= len<* a *> by FINSEQ_1:56;
  hence thesis by A1,Def1;
end;

theorem Th15:
  <* a,b *> |^ c = <* a |^ c,b |^ c *>
proof
  thus <* a,b *> |^ c = (<* a *> ^ <* b *>) |^ c by FINSEQ_1:def 9
    .= (<* a *> |^ c) ^ (<* b *> |^ c) by Th12
    .= <* a |^ c *> ^ (<* b *> |^ c) by Th14
    .= <* a |^ c *> ^ <* b |^ c *> by Th14
    .= <* a |^ c, b |^ c *> by FINSEQ_1:def 9;
end;

theorem
  <* a,b,c *> |^ d = <* a |^ d,b |^ d,c |^ d *>
proof
  thus <* a,b,c *> |^ d = (<* a *> ^ <* b,c *>) |^ d by FINSEQ_1:60
    .= (<* a *> |^ d) ^ (<* b,c *> |^ d) by Th12
    .= <* a *> |^ d ^ <* b |^ d,c |^ d *> by Th15
    .= <* a |^ d *> ^ <* b |^ d,c |^ d *> by Th14
    .= <* a |^ d,b |^ d,c |^ d *> by FINSEQ_1:60;
end;

theorem Th17:
  Product(F |^ a) = Product(F) |^ a
proof
  defpred P[FinSequence of the carrier of G] means Product($1 |^ a) = Product(
  $1) |^ a;
A1: now
    let F,b;
    assume
A2: P[F];
    Product((F ^ <* b *>) |^ a) = Product((F |^ a) ^ (<* b *> |^ a)) by
    Th12
      .= Product(F) |^ a * Product(<* b *> |^ a) by A2,GROUP_4:8
      .= Product(F) |^ a * Product<* b |^ a *> by Th14
      .= Product(F) |^ a * (b |^ a) by FINSOP_1:12
      .= (Product(F) * b) |^ a by GROUP_3:28
      .= Product(F ^ <* b *>) |^ a by GROUP_4:9;
    hence P[F^<*b*>];
  end;
A3: P[<*> the carrier of G]
  proof
    set p = <*> the carrier of G;
    thus Product(p |^ a) = Product p by Th13
      .= 1_G by GROUP_4:11
      .= (1_G) |^ a by GROUP_3:22
      .= Product p |^ a by GROUP_4:11;
  end;
  for F holds P[F] from FINSEQ_2:sch 2(A3,A1);
  hence thesis;
end;

theorem Th18:
  (F |^ a) |^ I = (F |^ I) |^ a
proof
  len(F |^ I) = len F by GROUP_4:def 4;
  then
A1: dom(F |^ I) = dom F by FINSEQ_3:31;
A2: len(F |^ a) = len F by Def1;
  then
A3: dom(F |^ a) = dom F by FINSEQ_3:31;
A4: len((F |^ a) |^ I) = len(F |^ a) by GROUP_4:def 4;
  then
A5: dom ((F |^ a) |^ I) = Seg len F by A2,FINSEQ_1:def 3;
A6: now
    let k be Nat;
    assume k in dom ((F |^ a) |^ I);
    then
A7: k in dom F by A5,FINSEQ_1:def 3;
    then
A8: (F |^ a)/.k = (F |^ a).k by A3,PARTFUN1:def 8;
A9: (F |^ I)/.k = (F |^ I).k by A1,A7,PARTFUN1:def 8;
    thus ((F |^ a) |^ I).k = ((F |^ a)/.k) |^ @(I/.k) by A3,A7,GROUP_4:def 4
      .= ((F/.k) |^ a) |^ @(I/.k) by A7,A8,Def1
      .= ((F/.k) |^ @(I/.k)) |^ a by GROUP_3:34
      .= ((F |^ I)/.k) |^ a by A7,A9,GROUP_4:def 4
      .= ((F |^ I) |^ a).k by A1,A7,Def1;
  end;
  len(F |^ I |^ a) = len(F |^ I) by Def1
    .= len F by GROUP_4:def 4;
  hence thesis by A2,A4,A6,FINSEQ_2:10;
end;

begin :: Commutators.

definition
  let G,a,b;
  func [.a,b.] -> Element of G equals
  a" * b" * a * b;
  correctness;
end;

theorem Th19:
  [.a,b.] = a" * b" * a * b & [.a,b.] = a" * (b" * a) * b & [.a,b
.] = a" * (b" * a * b) & [.a,b.] = a" * (b" * (a * b)) & [.a,b.] = (a" * b") *
  (a * b)
proof
  thus [.a,b.] = a" * b" * a * b;
  thus [.a,b.] = a" * (b" * a) * b by GROUP_1:def 4;
  hence [.a,b.] = a" * (b" * a * b) by GROUP_1:def 4;
  hence [.a,b.] = a" * (b" * (a * b)) by GROUP_1:def 4;
  thus thesis by GROUP_1:def 4;
end;

theorem Th20:
  [.a,b.] = (b * a)" * (a * b)
proof
  thus [.a,b.] = (a" * b") * (a * b) by Th19
    .= (b * a)" * (a * b) by GROUP_1:25;
end;

theorem
  [.a,b.] = (b" |^ a) * b & [.a,b.] = a" * (a |^ b) by Th19;

theorem Th22:
  [.1_G,a.] = 1_G & [.a,1_G.] = 1_G
proof
  thus [.1_G,a.] = ((1_G)" * a") * a by GROUP_1:def 5
    .= (1_G * a") * a by GROUP_1:16
    .= a" * a by GROUP_1:def 5
    .= 1_G by GROUP_1:def 6;
  thus [.a,1_G.] = (a" * (1_G)") * a by GROUP_1:def 5
    .= (a" * 1_G) * a by GROUP_1:16
    .= a" * a by GROUP_1:def 5
    .= 1_G by GROUP_1:def 6;
end;

theorem Th23:
  [.a,a.] = 1_G
proof
  thus [.a,a.] = (a * a)" * (a * a) by Th20
    .= 1_G by GROUP_1:def 6;
end;

theorem
  [.a,a".] = 1_G & [.a",a.] = 1_G
proof
  thus [.a,a".] = (a" * a"") * (a * a") by Th19
    .= 1_G * (a * a") by GROUP_1:def 6
    .= a * a" by GROUP_1:def 5
    .= 1_G by GROUP_1:def 6;
  thus [.a",a.] = (a"" * a") * (a" * a) by Th19
    .= (a"" * a") * 1_G by GROUP_1:def 6
    .= a"" * a" by GROUP_1:def 5
    .= 1_G by GROUP_1:def 6;
end;

theorem Th25:
  [.a,b.]" = [.b,a.]
proof
  thus [.a,b.]" = ((a" * b") * (a * b))" by Th19
    .= (a * b)" * (a" * b")" by GROUP_1:25
    .= (b" * a") * (a" * b")" by GROUP_1:25
    .= (b" * a") * (b"" * a"") by GROUP_1:25
    .= (b" * a") * (b"" * a)
    .= (b" * a") * (b * a)
    .= [.b,a.] by Th19;
end;

theorem Th26:
  [.a,b.] |^ c = [.a |^ c,b |^ c.]
proof
  thus [.a,b.] |^ c = c" * (a" * 1_G * b" * a * b) * c by GROUP_1:def 5
    .= c" * (a" * (c * c") * b" * a * b) * c by GROUP_1:def 6
    .= c" * (a" * (c * c") * b" * (a * b)) * c by GROUP_1:def 4
    .= c" * (a" * (c * c") * (b" * (a * b))) * c by GROUP_1:def 4
    .= c" * (a" * ((c * c") * (b" * (a * b)))) * c by GROUP_1:def 4
    .= c" * a" * ((c * c") * (b" * (a * b))) * c by GROUP_1:def 4
    .= c" * a" * (c * (c" * (b" * (a * b)))) * c by GROUP_1:def 4
    .= (a" |^ c) * (c" * (b" * (a * b))) * c by GROUP_1:def 4
    .= (a |^ c)" * (c" * (b" * (a * b))) * c by GROUP_3:32
    .= (a |^ c)" * (c" * (b" * 1_G * (a * b))) * c by GROUP_1:def 5
    .= (a |^ c)" * (c" * (b" * (c * c") * (a * b))) * c by GROUP_1:def 6
    .= (a |^ c)" * (c" * (b" * ((c * c") * (a * b)))) * c by GROUP_1:def 4
    .= (a |^ c)" * (c" * b" * ((c * c") * (a * b))) * c by GROUP_1:def 4
    .= (a |^ c)" * (c" * b" * (c * (c" * (a * b)))) * c by GROUP_1:def 4
    .= (a |^ c)" * ((b" |^ c) * (c" * (a * b))) * c by GROUP_1:def 4
    .= (a |^ c)" * ((b |^ c)" * (c" * (a * b))) * c by GROUP_3:32
    .= (a |^ c)" * ((b |^ c)" * (c" * (a * 1_G * b))) * c by GROUP_1:def 5
    .= (a |^ c)" * ((b |^ c)" * (c" * (a * (c * c") * b))) * c by GROUP_1:
  def 6
    .= (a |^ c)" * ((b |^ c)" * (c" * (a * ((c * c") * b)))) * c by GROUP_1:
  def 4
    .= (a |^ c)" * ((b |^ c)" * (c" * a * ((c * c") * b))) * c by GROUP_1:
  def 4
    .= (a |^ c)" * ((b |^ c)" * (c" * a * (c * (c" * b)))) * c by GROUP_1:
  def 4
    .= (a |^ c)" * ((b |^ c)" * ((a |^ c) * (c" * b))) * c by GROUP_1:def 4
    .= (a |^ c)" * (((b |^ c)" * ((a |^ c) * (c" * b))) * c) by GROUP_1:def
  4
    .= (a |^ c)" * (((b |^ c)" * ((a |^ c) * (c" * b) * c))) by GROUP_1:def
  4
    .= (a |^ c)" * (((b |^ c)" * ((a |^ c) * (b |^ c)))) by GROUP_1:def 4
    .= [.a |^ c,b |^ c.] by Th19;
end;

theorem
  [.a,b.] = (a" |^ 2) * ((a * b") |^ 2)* (b |^ 2)
proof
  thus [.a,b.] = (a" * b") * (a * b) by Th19
    .= (a" * 1_G * b") * (a * b) by GROUP_1:def 5
    .= (a" * 1_G * b") * (a * 1_G * b) by GROUP_1:def 5
    .= (a" * (a" * a) * b") * (a * 1_G * b) by GROUP_1:def 6
    .= (a" * (a" * a) * b") * (a * (b" * b) * b) by GROUP_1:def 6
    .= (a" * a" * a * b") * (a * (b" * b) * b) by GROUP_1:def 4
    .= (a" |^ 2 * a * b") * (a * (b" * b) * b) by GROUP_1:45
    .= (a" |^ 2 * a * b") * (a * ((b" * b) * b)) by GROUP_1:def 4
    .= (a" |^ 2 * a * b") * (a * (b" * (b * b))) by GROUP_1:def 4
    .= (a" |^ 2 * a * b") * (a * (b" * (b |^ 2))) by GROUP_1:45
    .= (a" |^ 2 * (a * b")) * (a * (b" * (b |^ 2))) by GROUP_1:def 4
    .= (a" |^ 2) * ((a * b") * (a * (b" * (b |^ 2)))) by GROUP_1:def 4
    .= (a" |^ 2) * ((a * b") * (a * b" * (b |^ 2))) by GROUP_1:def 4
    .= (a" |^ 2) * ((a * b") * (a * b") * (b |^ 2)) by GROUP_1:def 4
    .= (a" |^ 2) * ((a * b") * (a * b")) * (b |^ 2) by GROUP_1:def 4
    .= (a" |^ 2) * ((a * b") |^ 2) * (b |^ 2) by GROUP_1:45;
end;

theorem Th28:
  [.a * b,c.] = [.a,c.] |^ b * [.b,c.]
proof
  thus [.a * b,c.] = ((a * b)" * c") * (a * b * c) by Th19
    .= (b" * a" * c") * (a * b * c) by GROUP_1:25
    .= (b" * a" * c") * (a * 1_G * b * c) by GROUP_1:def 5
    .= (b" * a" * c") * (a * (c * c") * b * c) by GROUP_1:def 6
    .= (b" * a" * c") * (a * (c * 1_G * c") * b * c) by GROUP_1:def 5
    .= (b" * a" * c") * (a * (c * (b * b") * c") * b * c) by GROUP_1:def 6
    .= b" * (a" * c") * (a * (c * (b * b") * c") * b * c) by GROUP_1:def 4
    .= b" * (a" * c") * (a * (c * (b * b") * c") * (b * c)) by GROUP_1:def 4
    .= b" * (a" * c") * (a * (c * b * b" * c") * (b * c)) by GROUP_1:def 4
    .= b" * (a" * c") * (a * (c * b * (b" * c")) * (b * c)) by GROUP_1:def 4
    .= b" * (a" * c") * (a * (c * (b * (b" * c"))) * (b * c)) by GROUP_1:def
  4
    .= b" * (a" * c") * (a * c * (b * (b" * c")) * (b * c)) by GROUP_1:def 4
    .= b" * (a" * c") * (a * c * ((b * (b" * c")) * (b * c))) by GROUP_1:def
  4
    .= b" * (a" * c") * (a * c) * ((b * (b" * c")) * (b * c)) by GROUP_1:def
  4
    .= b" * ((a" * c") * (a * c)) * ((b * (b" * c")) * (b * c)) by GROUP_1:
  def 4
    .= b" * ((a" * c") * (a * c)) * (b * ((b" * c") * (b * c))) by GROUP_1:
  def 4
    .= b" * ((a" * c") * (a * c)) * b * ((b" * c") * (b * c)) by GROUP_1:def
  4
    .= [.a,c.] |^ b * ((b" * c") * (b * c)) by Th19
    .= [.a,c.] |^ b * [.b,c.] by Th19;
end;

theorem
  [.a,b * c.] = [.a,c.] * ([.a,b.] |^ c)
proof
  thus [.a,b * c.] = (a" * (b * c)") * (a * (b * c)) by Th19
    .= (a" * (c" * b")) * (a * (b * c)) by GROUP_1:25
    .= (a" * (c" * 1_G * b")) * (a * (b * c)) by GROUP_1:def 5
    .= (a" * (c" * (a * a") * b")) * (a * (b * c)) by GROUP_1:def 6
    .= (a" * (c" * (a * 1_G * a") * b")) * (a * (b * c)) by GROUP_1:def 5
    .= (a" * (c" * (a * (c * c") * a") * b")) * (a * (b * c)) by GROUP_1:def
  6
    .= (a" * (c" * (a * c * c" * a") * b")) * (a * (b * c)) by GROUP_1:def 4
    .= (a" * (c" * ((a * c) * (c" * a")) * b")) * (a * (b * c)) by GROUP_1:
  def 4
    .= (a" * (c" * (((a * c) * (c" * a")) * b"))) * (a * (b * c)) by GROUP_1
  :def 4
    .= ((a" * c") * (a * c * (c" * a") * b")) * (a * (b * c)) by GROUP_1:def
  4
    .= ((a" * c") * ((a * c) * (c" * a")) * b") * (a * (b * c)) by GROUP_1:
  def 4
    .= ((a" * c") * (a * c)) * (c" * a") * b" * (a * (b * c)) by GROUP_1:def
  4
    .= ((a" * c") * (a * c)) * ((c" * a") * b") * (a * (b * c)) by GROUP_1:
  def 4
    .= ((a" * c") * (a * c)) * (((c" * a") * b") * (a * (b * c))) by GROUP_1
  :def 4
    .= ((a" * c") * (a * c)) * (((c" * a") * b") * ((a * b) * c)) by GROUP_1
  :def 4
    .= ((a" * c") * (a * c)) * ((c" * (a" * b")) * ((a * b) * c)) by GROUP_1
  :def 4
    .= ((a" * c") * (a * c)) * (c" * ((a" * b") * ((a * b) * c))) by GROUP_1
  :def 4
    .= ((a" * c") * (a * c)) * (c" * ((a" * b") * (a * b) * c)) by GROUP_1:
  def 4
    .= ((a" * c") * (a * c)) * (c" * ((a" * b") * (a * b)) * c) by GROUP_1:
  def 4
    .= [.a,c.] * (c" * ((a" * b") * (a * b)) * c) by Th19
    .= [.a,c.] * ([.a,b.] |^ c) by Th19;
end;

theorem Th30:
  [.a",b.] = [.b,a.] |^ a"
proof
  thus [.a",b.] = a"" * (b" * (a" * b)) by Th19
    .= a"" * (b" * (a" * b)) * 1_G by GROUP_1:def 5
    .= a"" * (b" * (a" * b)) * (a * a") by GROUP_1:def 6
    .= a"" * (b" * (a" * b)) * a * a" by GROUP_1:def 4
    .= a"" * ((b" * (a" * b)) * a) * a" by GROUP_1:def 4
    .= [.b,a.] |^ a" by Th19;
end;

theorem Th31:
  [.a,b".] = [.b,a.] |^ b"
proof
  thus [.a,b".] = a" * b * a * b" .= 1_G * (a" * b * a * b") by GROUP_1:def 5
    .= (b"" * b") * (a" * b * a * b") by GROUP_1:def 6
    .= (b"" * b") * (a" * b * a) * b" by GROUP_1:def 4
    .= b"" * (b" * (a" * b * a)) * b" by GROUP_1:def 4
    .= [.b,a.] |^ b" by Th19;
end;

theorem
  [.a",b".] = [.a,b.] |^ (a * b)" & [.a",b".] = [.a,b.] |^ (b * a)"
proof
  thus [.a",b".] = [.b",a.] |^ a" by Th30
    .= ([.a,b.] |^ b") |^ a" by Th30
    .= [.a,b.] |^ (b" * a") by GROUP_3:29
    .= [.a,b.] |^ (a * b)" by GROUP_1:25;
  thus [.a",b".] = [.b,a".] |^ b" by Th31
    .= [.a,b.] |^ a" |^ b" by Th31
    .= [.a,b.] |^ (a" * b") by GROUP_3:29
    .= [.a,b.] |^ (b * a)" by GROUP_1:25;
end;

theorem
  [.a,b |^ a".] = [.b,a".]
proof
  thus [.a,b |^ a".] = a" * (b" |^ a") * a * (b |^ a") by GROUP_3:32
    .= a" * (a"" * (b" * a")) * a * (b |^ a") by GROUP_1:def 4
    .= (b" * a") * a * (b |^ a") by GROUP_3:1
    .= b" * (a"" * b * a") by GROUP_3:1
    .= [.b,a".] by Th19;
end;

theorem
  [.a |^ b",b.] = [.b",a.]
proof
  thus [.a |^ b",b.] = (a" |^ b") * b" * (a |^ b") * b by GROUP_3:32
    .= (a" |^ b") * (b" * (b"" * a * b")) * b by GROUP_1:def 4
    .= (a" |^ b") * (b" * (b"" * (a * b"))) * b by GROUP_1:def 4
    .= (a" |^ b") * (a * b") * b by GROUP_3:1
    .= (a" |^ b") * ((a * b") * b) by GROUP_1:def 4
    .= [.b",a.] by GROUP_3:1;
end;

theorem
  [.a |^ n,b.] = a |^ (- n) * ((a |^ b) |^ n)
proof
  thus [.a |^ n,b.] = (a |^ n)" * (b" * (a |^ n) * b) by Th19
    .= a |^ (- n) * ((a |^ n) |^ b) by GROUP_1:70
    .= a |^ (- n) * ((a |^ b) |^ n) by GROUP_3:33;
end;

theorem
  [.a,b |^ n.] = (b |^ a) |^ (- n) * (b |^ n)
proof
  thus [.a,b |^ n.] = (b |^ (- n)) |^ a * (b |^ n) by GROUP_1:70
    .= (b |^ a) |^ (- n) * (b |^ n) by GROUP_3:34;
end;

theorem
  [.a |^ i,b.] = a |^ (- i) * ((a |^ b) |^ i)
proof
  thus [.a |^ i,b.] = (a |^ i)" * (b" * (a |^ i) * b) by Th19
    .= a |^ (- i) * ((a |^ i) |^ b) by GROUP_1:70
    .= a |^ (- i) * ((a |^ b) |^ i) by GROUP_3:34;
end;

theorem
  [.a,b |^ i.] = (b |^ a) |^ (- i) * (b |^ i)
proof
  thus [.a,b |^ i.] = (b |^ (- i)) |^ a * (b |^ i) by GROUP_1:70
    .= (b |^ a) |^ (- i) * (b |^ i) by GROUP_3:34;
end;

theorem Th39:
  [.a,b.] = 1_G iff a * b = b * a
proof
  thus [.a,b.] = 1_G implies a * b = b * a
  proof
    assume [.a,b.] = 1_G;
    then (b * a)" * (a * b) = 1_G by Th20;
    then a * b = (b * a)"" by GROUP_1:20;
    hence thesis;
  end;
  assume a * b = b * a;
  then
A1: (b * a)" = b" * a" by GROUP_1:26;
  [.a,b.] = (b * a)" * (a * b) by Th20;
  hence [.a,b.] = (b" * a") * a * b by A1,GROUP_1:def 4
    .= b" * b by GROUP_3:1
    .= 1_G by GROUP_1:def 6;
end;

Lm1: for G being commutative Group for a,b being Element of G holds a * b = b
* a;

theorem
  G is commutative Group iff for a,b holds [.a,b.] = 1_G
proof
  thus G is commutative Group implies for a,b holds [.a,b.] = 1_G
  proof
    assume
A1: G is commutative Group;
    let a,b;
    a * b = b * a by A1,Lm1;
    hence thesis by Th39;
  end;
  assume
A2: for a,b holds [.a,b.] = 1_G;
  G is commutative
  proof
    let a,b;
    [.a,b.] = 1_G by A2;
    hence thesis by Th39;
  end;
  hence thesis;
end;

theorem Th41:
  a in H & b in H implies [.a,b.] in H
proof
  assume
A1: a in H & b in H;
  then a" in H & b" in H by GROUP_2:60;
  then
A2: a" * b" in H by GROUP_2:59;
  a * b in H by A1,GROUP_2:59;
  then (a" * b") * (a * b) in H by A2,GROUP_2:59;
  hence thesis by Th19;
end;

definition
  let G,a,b,c;
  func [.a,b,c.] -> Element of G equals
  [.[.a,b.],c.];
  correctness;
end;

canceled;

theorem
  [.a,b,1_G.] = 1_G & [.a,1_G,b.] = 1_G & [.1_G,a,b.] = 1_G
proof
  thus [.a,b,1_G.] = 1_G by Th22;
  thus [.a,1_G,b.] = [.1_G,b.] by Th22
    .= 1_G by Th22;
  thus [.1_G,a,b.] = [.1_G,b.] by Th22
    .= 1_G by Th22;
end;

theorem
  [.a,a,b.] = 1_G
proof
  thus [.a,a,b.] = [.1_G,b.] by Th23
    .= 1_G by Th22;
end;

theorem
  [.a,b,a.] = [.a |^ b,a.]
proof
  thus [.a,b,a.] = [.b,a.] * a" * [.a,b.] * a by Th25
    .= (a" |^ b) * (a" * b" * a * b) * a by GROUP_3:1
    .= (a |^ b)" * (a" * b" * a * b) * a by GROUP_3:32
    .= (a |^ b)" * (a" * b" * (a * b)) * a by GROUP_1:def 4
    .= (a |^ b)" * (a" * (b" * (a * b))) * a by GROUP_1:def 4
    .= (a |^ b)" * (a" * (a |^ b)) * a by GROUP_1:def 4
    .= [.a |^ b,a.] by Th19;
end;

theorem
  [.b,a,a.] = ([.b,a".] * [.b,a.]) |^ a
proof
  thus [.b,a,a.] = [.a,b.] * a" * [.b,a.] * a by Th25
    .= (a" * (b" * (a * b))) * a" * [.b,a.] * a by Th19
    .= (a" * ((b" * (a"" * b))) * a") * [.b,a.] * a
    .= (a" * (((b" * (a"" * b))) * a")) * [.b,a.] * a by GROUP_1:def 4
    .= a" * [.b,a".] * [.b,a.] * a by Th19
    .= ([.b,a".] * [.b,a.]) |^ a by GROUP_1:def 4;
end;

theorem
  [.a,b,b |^ a.] = [.b,[.b,a.].]
proof
  thus [.a,b,b |^ a.] = [.b,a.] * (b |^ a)" * [.a,b.] * (b |^ a) by Th25
    .= (b" * a" * b * a) * (b" |^ a) * [.a,b.] * (b |^ a) by GROUP_3:32
    .= (b" * a" * b * a) * (a" * (b" * a)) * [.a,b.] * (b |^ a) by GROUP_1:
  def 4
    .= (b" * a" * b * a) * a" * (b" * a) * [.a,b.] * (b |^ a) by GROUP_1:def
  4
    .= (b" * a" * b) * (b" * a) * [.a,b.] * (b |^ a) by GROUP_3:1
    .= b" * a" * (b * (b" * a)) * [.a,b.] * (b |^ a) by GROUP_1:def 4
    .= b" * a" * a * [.a,b.] * (b |^ a) by GROUP_3:1
    .= b" * (a"* b"* a * b) * (b |^ a) by GROUP_3:1
    .= b" * (a"* b"* a * b) * 1_G * (a" * b * a) by GROUP_1:def 5
    .= b" * [.a,b.] * (b * b") * (a" * b * a) by GROUP_1:def 6
    .= b" * [.a,b.] * ((b * b") * (a" * b * a)) by GROUP_1:def 4
    .= b" * [.a,b.] * (b * (b" * (a" * b * a))) by GROUP_1:def 4
    .= b" * [.a,b.] * (b * [.b,a.]) by Th19
    .= b" * [.b,a.]" * (b * [.b,a.]) by Th25
    .= [.b,[.b,a.].] by Th19;
end;

theorem
  [.a * b,c.] = [.a,c.] * [.a,c,b.] * [.b,c.]
proof
  [.a,c.] * [.a,c,b.] * [.b,c.] = a" * c" * a * c * ([.c,a.] * b" * [.a,c
  .] * b) * [.b,c.] by Th25
    .= a" * c" * a * c * ((c" * a") * (c * a) * b" * [.a,c.] * b) * [.b,c.]
  by Th19
    .= a" * c" * a * c * ((a * c)" * (c * a) * b" * [.a,c.] * b) * [.b,c.]
  by GROUP_1:25
    .= a" * c" * (a * c) * ((a * c)" * (c * a) * b" * [.a,c.] * b) * [.b,c.]
  by GROUP_1:def 4
    .= a" * c" * ((a * c) * ((a * c)" * (c * a) * b" * [.a,c.] * b)) * [.b,c
  .] by GROUP_1:def 4
    .= a" * c" * ((a * c) * ((a * c)" * (c * a) * b" * ([.a,c.] * b))) * [.b
  ,c.] by GROUP_1:def 4
    .= a" * c" * ((a * c) * ((a * c)" * (c * a) * (b" * ([.a,c.] * b)))) *
  [.b,c.] by GROUP_1:def 4
    .= a" * c" * ((a * c) * ((a * c)" * ((c * a) * (b" * ([.a,c.] * b))))) *
  [.b,c.] by GROUP_1:def 4
    .= a" * c" * ((a * c) * (a * c)" * ((c * a) * (b" * ([.a,c.] * b)))) *
  [.b,c.] by GROUP_1:def 4
    .= a" * c" * (1_G * ((c * a) * (b" * ([.a,c.] * b)))) * [.b,c.] by
  GROUP_1:def 6
    .= a" * c" * ((c * a) * (b" * ([.a,c.] * b))) * [.b,c.] by GROUP_1:def 5
    .= a" * c" * (c * a) * (b" * ([.a,c.] * b)) * [.b,c.] by GROUP_1:def 4
    .= (c * a)" * (c * a) * (b" * ([.a,c.] * b)) * [.b,c.] by GROUP_1:25
    .= 1_G * (b" * ([.a,c.] * b)) * [.b,c.] by GROUP_1:def 6
    .= b" * ([.a,c.] * b) * [.b,c.] by GROUP_1:def 5
    .= b" * ((a" * c" * a * c) * b) * ((b" * c") * (b * c)) by Th19
    .= b" * ((a" * c" * a) * c) * b * ((b" * c") * (b * c)) by GROUP_1:def 4
    .= b" * (a" * c" * (a * c)) * b * ((b" * c") * (b * c)) by GROUP_1:def 4
    .= b" * (a" * (c" * (a * c))) * b * ((b" * c") * (b * c)) by GROUP_1:def
  4
    .= b" * a" * (c" * (a * c)) * b * ((b" * c") * (b * c)) by GROUP_1:def 4
    .= b" * a" * c" * (a * c) * b * ((b" * c") * (b * c)) by GROUP_1:def 4
    .= b" * a" * c" * a * c * b * ((b" * c") * (b * c)) by GROUP_1:def 4
    .= b" * a" * c" * a * (c * b) * ((b" * c") * (b * c)) by GROUP_1:def 4
    .= b" * a" * c" * a * (c * b) * (b" * c") * (b * c) by GROUP_1:def 4
    .= b" * a" * c" * a * (c * b) * (c * b)" * (b * c) by GROUP_1:25
    .= b" * a" * c" * a * ((c * b) * (c * b)") * (b * c) by GROUP_1:def 4
    .= b" * a" * c" * a * 1_G * (b * c) by GROUP_1:def 6
    .= b" * a" * c" * a * (b * c) by GROUP_1:def 5
    .= (a * b)" * c" * a * (b * c) by GROUP_1:25
    .= (a * b)" * c" * (a * (b * c)) by GROUP_1:def 4
    .= (a * b)" * c" * (a * b * c) by GROUP_1:def 4;
  hence thesis by Th19;
end;

theorem
  [.a,b * c.] = [.a,c.] * [.a,b.] * [.a,b,c.]
proof
  [.a,c.] * [.a,b.] * [.a,b,c.] = [.a,c.] * ([.a,b.] * [.[.a,b.],c.]) by
  GROUP_1:def 4
    .= [.a,c.] * ((a" * b") * (a * b) * ([.a,b.]" * c" * [.a,b.] * c)) by
  Th19
    .= [.a,c.] * ((a" * b") * (a * b) * ([.b,a.] * c" * [.a,b.] * c)) by
  Th25
    .= [.a,c.] * ((a" * b") * (a * b) * ((b" * a" * (b * a)) * c" * [.a,b.]
  * c)) by Th19
    .= [.a,c.] * ((a" * b") * ((a * b) * (((b" * a") * (b * a)) * c" * [.a,b
  .] * c))) by GROUP_1:def 4
    .= [.a,c.] * ((a" * b") * ((a * b) * (((b" * a") * (b * a)) * c" * ([.a,
  b.] * c)))) by GROUP_1:def 4
    .= [.a,c.] * ((a" * b") * ((a * b) * (((b" * a") * (b * a)) * (c" * ([.a
  ,b.] * c))))) by GROUP_1:def 4
    .= [.a,c.] * ((a" * b") * ((a * b) * ((b" * a") * (b * a)) * (c" * ([.a,
  b.] * c)))) by GROUP_1:def 4
    .= [.a,c.] * ((a" * b") * ((a * b) * (b" * a") * (b * a) * (c" * ([.a,b
  .] * c)))) by GROUP_1:def 4
    .= [.a,c.] * ((a" * b") * ((a * b) * (a * b)" * (b * a) * (c" * ([.a,b.]
  * c)))) by GROUP_1:25
    .= [.a,c.] * ((a" * b") * (1_G * (b * a) * (c" * ([.a,b.] * c)))) by
  GROUP_1:def 6
    .= [.a,c.] * ((a" * b") * ((b * a) * (c" * ([.a,b.] * c)))) by GROUP_1:
  def 5
    .= [.a,c.] * ((a" * b") * (b * a) * (c" * ([.a,b.] * c))) by GROUP_1:def
  4
    .= [.a,c.] * ((b * a)" * (b * a) * (c" * ([.a,b.] * c))) by GROUP_1:25
    .= [.a,c.] * (1_G * (c" * ([.a,b.] * c))) by GROUP_1:def 6
    .= [.a,c.] * (c" * ([.a,b.] * c)) by GROUP_1:def 5
    .= (a" * c") * (a * c) * (c" * ([.a,b.] * c)) by Th19
    .= (a" * c") * (a * c) * (c" * ((a" * (b" * a * b)) * c)) by Th19
    .= (a" * c") * (a * c) * (c" * (a" * (b" * a * b)) * c) by GROUP_1:def 4
    .= (a" * c") * (a * c) * ((c" * a") * (b" * a * b) * c) by GROUP_1:def 4
    .= (a" * c") * (a * c) * ((c" * a") * ((b" * a * b) * c)) by GROUP_1:def
  4
    .= (a" * c") * (a * c) * (c" * a") * ((b" * a * b) * c) by GROUP_1:def 4
    .= (a" * c") * ((a * c) * (c" * a")) * ((b" * a * b) * c) by GROUP_1:def
  4
    .= (a" * c") * ((a * c) * (a * c)") * ((b" * a * b) * c) by GROUP_1:25
    .= (a" * c") * 1_G * ((b" * a * b) * c) by GROUP_1:def 6
    .= (a" * c") * ((b" * a * b) * c) by GROUP_1:def 5
    .= (a" * c") * (b" * a * b) * c by GROUP_1:def 4
    .= (a" * c") * (b" * (a * b)) * c by GROUP_1:def 4
    .= a" * c" * b" * (a * b) * c by GROUP_1:def 4
    .= a" * (c" * b") * (a * b) * c by GROUP_1:def 4
    .= a" * (c" * b") * ((a * b) * c) by GROUP_1:def 4
    .= a" * (c" * b") * (a * (b * c)) by GROUP_1:def 4
    .= a" * (b * c)" * (a * (b * c)) by GROUP_1:25;
  hence thesis by Th19;
end;
 
::
::  P. Hall Identity.
::

theorem
  ([.a,b",c.] |^ b) * ([.b,c",a.] |^ c) * ([.c,a",b.] |^ a) = 1_G
proof
  set d = a" * [.b,c".] * a;
  set e = c" * [.a,b".] * c;
  set f = b" * [.c,a".] * b;
  set x = [.a,b",c.] |^ b;
  set y = [.b,c",a.] |^ c;
  set z = [.c,a",b.] |^ a;
A1: [.c",b.] = c"" * (b" * c" * b) by Th19;
A2: [.a",c.] = a"" * (c" * a" * c) by Th19;
A3: [.b",a.] = b"" * (a" * b" * a) by Th19;
  [.a,b",c.] = [.a,b".]" * e by Th19
    .= [.b",a.] * e by Th25;
  then
A4: [.a,b",c.] |^ b = b" * (b"" * ((a" * b" * a) * e)) * b by
  A3, GROUP_1
  :def 4
    .= (a" * b" * a) * e * b by GROUP_3:1;
  [.c,a",b.] = [.c,a".]" * f by Th19
    .= [.a",c.] * f by Th25;
  then
A5: z = a" * (a"" * ((c" * a" * c) * f)) * a by A2,GROUP_1:def 4
    .= (c" * a" * c) * f * a by GROUP_3:1;
  [.b,c",a.] = [.b,c".]" * (a" * [.b,c".] * a) by Th19
    .= [.c",b.] * (a" * [.b,c".] * a) by Th25;
  then [.b,c",a.] |^ c = c" * (c"" * ((b" * c" * b) * d)) * c by
  A1, GROUP_1
  :def 4
    .= (b" * c" * b) * d * c by GROUP_3:1
    .= (b" * c" * b) * (d * c) by GROUP_1:def 4
    .= b" * (c" * b) * (d * c) by GROUP_1:def 4
    .= b" * ((c" * b) * (d * c)) by GROUP_1:def 4;
  then x * y = (a" * b" * a) * e * (b * (b" * ((c" * b) * (d * c)))) by
  A4,
  GROUP_1:def 4
    .= (a" * b" * a) * e * ((c" * b) * (d * c)) by GROUP_3:1
    .= (a" * b" * a) * (c" * ([.a,b".] * c)) * ((c" * b) * (d * c)) by
  GROUP_1:def 4
    .= (a" * b" * a) * c" * ([.a,b".] * c) * ((c" * b) * (d * c)) by GROUP_1
  :def 4
    .= (a" * b" * a) * c" * (([.a,b".] * c) * ((c" * b) * (d * c))) by
  GROUP_1:def 4
    .= (a" * b" * a) * c" * (([.a,b".] * c) * (c" * b) * (d * c)) by GROUP_1
  :def 4
    .= (a" * b" * a) * c" * (([.a,b".] * c) * c" * b * (d * c)) by GROUP_1:
  def 4
    .= (a" * b" * a) * c" * ((a" * b"" * a) * b" * b * (d * c)) by GROUP_3:1
    .= (a" * b" * a) * c" * ((a" * b"" * a) * (d * c)) by GROUP_3:1
    .= (a" * b" * a) * c" * ((a" * b"" * a) * (a" * [.b,c".] * (a * c))) by
  GROUP_1:def 4
    .= (a" * b" * a) * c" * ((a" * b"" * a) * (a" * ([.b,c".] * (a * c))))
  by GROUP_1:def 4
    .= (a" * b" * a) * c" * ((a" * b"" * a) * a" * ([.b,c".] * (a * c))) by
  GROUP_1:def 4
    .= (a" * b" * a) * c" * ((a" * b"") * ([.b,c".] * (a * c))) by GROUP_3:1
    .= (a" * b" * a) * c" * ((a" * b"") * [.b,c".] * (a * c)) by GROUP_1:def
  4
    .= (a" * b" * a) * c" * ((a" * b"") * (b" * (c"" * b * c")) * (a * c))
  by Th19
    .= (a" * b" * a) * c" * ((a" * b"") * ((b" * (c"" * b * c")) * (a * c)))
  by GROUP_1:def 4
    .= (a" * b" * a) * c" * ((a" * b"") * (b" * ((c"" * b * c") * (a * c))))
  by GROUP_1:def 4
    .= (a" * b" * a) * c" * ((a" * b"") * b" * ((c"" * b * c") * (a * c)))
  by GROUP_1:def 4
    .= (a" * b" * a) * c" * (a" * ((c"" * b * c") * (a * c))) by GROUP_3:1;
  then
  x * y * z = ((a" * b" * a) * c" * (a" * (c"" * b * c") * (a * c))) * ((
  c" * a" * c) * f * a) by A5,GROUP_1:def 4
    .= ((a" * b" * a) * (c" * (a" * (c"" * b * c") * (a * c)))) * ((c" * a"
  * c) * f * a) by GROUP_1:def 4
    .= ((a" * b" * a) * (c" * (a" * ((c"" * b * c") * (a * c))))) * ((c" * a
  " * c) * f * a) by GROUP_1:def 4
    .= ((a" * b" * a) * c" * (a" * ((c"" * b * c") * (a * c)))) * ((c" * a"
  * c) * f * a) by GROUP_1:def 4
    .= ((a" * b" * a) * c" * (a" * (c"" * (b * c") * (a * c)))) * ((c" * a"
  * c) * f * a) by GROUP_1:def 4
    .= ((a" * b" * a) * c" * (a" * (c"" * (b * c" * (a * c))))) * ((c" * a"
  * c) * f * a) by GROUP_1:def 4
    .= ((a" * b" * a) * c" * a" * (c"" * (b * c" * (a * c)))) * ((c" * a" *
  c) * f * a) by GROUP_1:def 4
    .= ((a" * b" * a) * c" * a" * c"" * (b * c" * (a * c))) * (c" * a" * c *
  f * a) by GROUP_1:def 4
    .= ((a" * b" * a) * c" * a" * c"" * (b * (c" * (a * c)))) * (c" * a" * c
  * f * a) by GROUP_1:def 4
    .= ((a" * b" * a) * c" * a" * c"" * b * (c" * (a * c))) * (c" * a" * c *
  f * a) by GROUP_1:def 4
    .= (((a" * b" * a) * c" * a" * c"") * b * c" * (a * c)) * (c" * a" * c *
  f * a) by GROUP_1:def 4
    .= (((a" * b") * a * c") * a" * c"" * b * c") * ((a * c) * (c" * a" * c
  * f * a)) by GROUP_1:def 4
    .= (((a" * b") * a * c") * a" * c"" * b * c") * ((a * c) * (c" * a" * c
  * (f * a))) by GROUP_1:def 4
    .= (((a" * b") * a * c") * a" * c"" * b * c") * ((a * c) * (c" * a" * (c
  * (f * a)))) by GROUP_1:def 4
    .= (((a" * b") * a * c") * a" * c"" * b * c") * ((a * c) * (c" * a") * (
  c * (f * a))) by GROUP_1:def 4
    .= (((a" * b") * a * c") * a" * c"" * b * c") * ((a * c) * (a * c)" * (c
  * (f * a))) by GROUP_1:25
    .= (((a" * b") * a * c") * a" * c"" * b * c") * (1_G * (c * (f * a))) by
  GROUP_1:def 6
    .= (((a" * b") * a * c") * a" * c"" * b * c") * ((c * (f * a))) by
  GROUP_1:def 5
    .= (((a" * b") * a * c") * a" * c"" * b * c") * c * (f * a) by GROUP_1:
  def 4
    .= (((a" * b") * a * c") * a" * c"" * b) * (f * a) by GROUP_3:1
    .= (((a" * b") * a * c") * a" * c"" * b) * (b" * [.c,a".] * (b * a)) by
  GROUP_1:def 4
    .= (((a" * b") * a * c") * a" * c"" * b) * (b" * ([.c,a".] * (b * a)))
  by GROUP_1:def 4
    .= (((a" * b") * a * c") * a" * c"" * b) * b" * ([.c,a".] * (b * a)) by
  GROUP_1:def 4
    .= (((a" * b") * a * c") * a" * c"") * ([.c,a".] * (b * a)) by GROUP_3:1
    .= (((a" * b") * a * c") * a" * c"") * (c" * (a"" * c * a") * (b * a))
  by Th19
    .= (((a" * b") * a * c") * a" * c"") * (c" * ((a"" * c * a") * (b * a)))
  by GROUP_1:def 4
    .= (((a" * b") * a * c") * a" * c"") * c" * ((a"" * c * a") * (b * a))
  by GROUP_1:def 4
    .= (((a" * b") * a * c") * a") * ((a"" * c * a") * (b * a)) by GROUP_3:1
    .= (((a" * b") * a * c") * a") * (a"" * (c * a") * (b * a)) by GROUP_1:
  def 4
    .= (((a" * b") * a * c") * a") * (a"" * ((c * a") * (b * a))) by GROUP_1
  :def 4
    .= (((a" * b") * a * c") * a") * a"" * ((c * a") * (b * a)) by GROUP_1:
  def 4
    .= ((a" * b") * a * c") * ((c * a") * (b * a)) by GROUP_3:1
    .= ((a" * b") * a * c") * (c * a") * (b * a) by GROUP_1:def 4
    .= (a" * b") * (a * c") * (c * a") * (b * a) by GROUP_1:def 4
    .= (a" * b") * (a * c") * (c"" * a") * (b * a)
    .= (a" * b") * (a * c") * (a * c")" * (b * a) by GROUP_1:25
    .= (a" * b") * ((a * c") * (a * c")") * (b * a) by GROUP_1:def 4
    .= (a" * b") * 1_G * (b * a) by GROUP_1:def 6
    .= (a" * b") * (b * a) by GROUP_1:def 5
    .= (b * a)" * (b * a) by GROUP_1:25;
  hence thesis by GROUP_1:def 6;
end;

definition
  let G,A,B;
  func commutators(A,B) -> Subset of G equals
  {[.a,b.] : a in A & b in B};
  coherence
  proof
    defpred P[Element of G,Element of G] means $1 in A & $2 in B;
    deffunc F(Element of G,Element of G) = [.$1,$2.];
    {F(a,b) : P[a,b]} is Subset of G from DOMAIN_1:sch 9;
    hence thesis;
  end;
end;

canceled;

theorem Th52:
  x in commutators(A,B) iff ex a,b st x = [.a,b.] & a in A & b in B;

theorem
  commutators({} the carrier of G,A) = {} & commutators(A,{} the carrier
  of G) = {}
proof
  commutators({} the carrier of G,A) c= {}
  proof
    let x;
    assume x in commutators({} the carrier of G,A);
    then ex a,b st x = [.a,b.] & a in {} the carrier of G & b in A;
    hence thesis;
  end;
  hence commutators({} the carrier of G,A) = {};
  thus commutators(A,{} the carrier of G) c= {}
  proof
    let x;
    assume x in commutators(A,{} the carrier of G);
    then ex a,b st x = [.a,b.] & a in A & b in {} the carrier of G;
    hence thesis;
  end;
  thus thesis by XBOOLE_1:2;
end;

theorem
  commutators({a},{b}) = {[.a,b.]}
proof
  thus commutators({a},{b}) c= {[.a,b.]}
  proof
    let x;
    assume x in commutators({a},{b});
    then consider c,d such that
A1: x = [.c,d.] and
A2: c in {a} & d in {b};
    c = a & b = d by A2,TARSKI:def 1;
    hence thesis by A1,TARSKI:def 1;
  end;
  let x;
  assume x in {[.a,b.]};
  then
A3: x = [.a,b.] by TARSKI:def 1;
  a in {a} & b in {b} by TARSKI:def 1;
  hence thesis by A3;
end;

theorem Th55:
  A c= B & C c= D implies commutators(A,C) c= commutators(B,D)
proof
  assume
A1: A c= B & C c= D;
  let x;
  assume x in commutators(A,C);
  then ex a,c st x = [.a,c.] & a in A & c in C;
  hence thesis by A1;
end;

theorem Th56:
  G is commutative Group iff for A,B st A <> {} & B <> {} holds
  commutators(A,B) = {1_G}
proof
  thus G is commutative Group implies for A,B st A <> {} & B <> {} holds
  commutators(A,B) = {1_G}
  proof
    assume
A1: G is commutative Group;
    let A,B;
    assume
A2: A <> {} & B <> {};
    thus commutators(A,B) c= {1_G}
    proof
      let x;
      assume x in commutators(A,B);
      then consider a,b such that
A3:   x = [.a,b.] and
      a in A and
      b in B;
      x = a" * (b" * a) * b by A3,Th19
        .= a" * (a * b") * b by A1,Lm1
        .= b" * b by GROUP_3:1
        .= 1_G by GROUP_1:def 6;
      hence thesis by TARSKI:def 1;
    end;
    consider b being Element of B;
    consider a being Element of A;
    reconsider a,b as Element of G by A2,TARSKI:def 3;
    let x;
    assume x in {1_G};
    then
A4: x = 1_G by TARSKI:def 1;
    [.a,b.] = a" * (b" * a) * b by Th19
      .= a" * (a * b") * b by A1,Lm1
      .= b" * b by GROUP_3:1
      .= x by A4,GROUP_1:def 6;
    hence thesis by A2;
  end;
  assume
A5: for A,B st A <> {} & B <> {} holds commutators(A,B) = {1_G};
  G is commutative
  proof
    let a,b;
    a in {a} & b in {b} by TARSKI:def 1;
    then
A6: [.a,b.] in commutators({a},{b});
    commutators({a},{b}) ={1_G} by A5;
    then [.a,b.] = 1_G by A6,TARSKI:def 1;
    then (a" * b") * (a * b) = 1_G by Th19;
    then (b * a)" * (a * b) = 1_G by GROUP_1:25;
    then a * b = (b * a)"" by GROUP_1:20;
    hence thesis;
  end;
  hence thesis;
end;

definition
  let G,H1,H2;
  func commutators(H1,H2) -> Subset of G equals
  commutators(carr H1,carr H2);
  correctness;
end;

canceled;

theorem Th58:
  x in commutators(H1,H2) iff ex a,b st x = [.a,b.] & a in H1 & b in H2
proof
  thus x in commutators(H1,H2) implies ex a,b st x = [.a,b.] & a in H1 & b in
  H2
  proof
    assume x in commutators(H1,H2);
    then consider a,b such that
A1: x = [.a,b.] and
A2: a in carr H1 & b in carr H2;
    a in H1 & b in H2 by A2,STRUCT_0:def 5;
    hence thesis by A1;
  end;
  given a,b such that
A3: x = [.a,b.] and
A4: a in H1 & b in H2;
  a in carr H1 & b in carr H2 by A4,STRUCT_0:def 5;
  hence thesis by A3;
end;

theorem Th59:
  1_G in commutators(H1,H2)
proof
A1: 1_G in H2 by GROUP_2:55;
  [.1_G,1_G.] = 1_G & 1_G in H1 by Th22,GROUP_2:55;
  hence thesis by A1,Th58;
end;

theorem
  commutators((1).G,H) = {1_G} & commutators(H,(1).G) = {1_G}
proof
A1: commutators((1).G,H) c= {1_G}
  proof
    let x;
    assume x in commutators((1).G,H);
    then consider a,b such that
A2: x = [.a,b.] and
A3: a in (1).G and
    b in H by Th58;
    a = 1_G by A3,Th1;
    then x = 1_G by A2,Th22;
    hence thesis by TARSKI:def 1;
  end;
  1_G in commutators((1).G,H) by Th59;
  hence commutators((1).G,H) = {1_G} by A1,ZFMISC_1:39;
  thus commutators(H,(1).G) c= {1_G}
  proof
    let x;
    assume x in commutators(H,(1).G);
    then consider a,b such that
A4: x = [.a,b.] and
    a in H and
A5: b in (1).G by Th58;
    b = 1_G by A5,Th1;
    then x = 1_G by A4,Th22;
    hence thesis by TARSKI:def 1;
  end;
  1_G in commutators(H,(1).G) by Th59;
  hence thesis by ZFMISC_1:37;
end;

theorem Th61:
  for N being strict normal Subgroup of G holds commutators(H,N)
  c= carr N & commutators(N,H) c= carr N
proof
  let N be strict normal Subgroup of G;
  thus commutators(H,N) c= carr N
  proof
    let x;
    assume x in commutators(H,N);
    then consider a,b such that
A1: x = [.a,b.] and
    a in H and
A2: b in N by Th58;
    b" in N by A2,GROUP_2:60;
    then b" |^ a in N |^ a by GROUP_3:70;
    then b" |^ a in N by GROUP_3:def 13;
    then x in N by A1,A2,GROUP_2:59;
    hence thesis by STRUCT_0:def 5;
  end;
  let x;
  assume x in commutators(N,H);
  then consider a,b such that
A3: x = [.a,b.] and
A4: a in N and
  b in H by Th58;
  a |^ b in N |^ b by A4,GROUP_3:70;
  then
A5: a |^ b in N by GROUP_3:def 13;
  x = a" * (a |^ b) & a" in N by A3,A4,Th19,GROUP_2:60;
  then x in N by A5,GROUP_2:59;
  hence thesis by STRUCT_0:def 5;
end;

theorem Th62:
  H1 is Subgroup of H2 & H3 is Subgroup of H4 implies commutators(
  H1,H3) c= commutators(H2,H4)
proof
  assume H1 is Subgroup of H2 & H3 is Subgroup of H4;
  then the carrier of H1 c= the carrier of H2 & the carrier of H3 c= the
  carrier of H4 by GROUP_2:def 5;
  hence thesis by Th55;
end;

theorem Th63:
  G is commutative Group iff for H1,H2 holds commutators(H1,H2) = {1_G}
proof
  thus G is commutative Group implies for H1,H2 holds commutators(H1,H2) = {
  1_G} by Th56;
  assume
A1: for H1,H2 holds commutators(H1,H2) = {1_G};
  G is commutative
  proof
    let a,b;
    b in {b} by TARSKI:def 1;
    then
A2: b in gr{b} by GROUP_4:38;
    a in {a} by TARSKI:def 1;
    then a in gr{a} by GROUP_4:38;
    then [.a,b.] in commutators(gr{a},gr{b}) by A2,Th58;
    then [.a,b.] in {1_G} by A1;
    then [.a,b.] = 1_G by TARSKI:def 1;
    hence thesis by Th39;
  end;
  hence thesis;
end;

definition
  let G;
  func commutators G -> Subset of G equals
  commutators((Omega).G,(Omega).G);
  correctness;
end;

canceled;

theorem Th65:
  x in commutators G iff ex a,b st x = [.a,b.]
proof
  thus x in commutators G implies ex a,b st x = [.a,b.]
  proof
    assume x in commutators G;
    then ex a,b st x = [.a,b.] & a in (Omega).G & b in (Omega).G by Th58;
    hence thesis;
  end;
  given a,b such that
A1: x = [.a,b.];
  thus thesis by A1;
end;

theorem
  G is commutative Group iff commutators G = {1_G}
proof
  thus G is commutative Group implies commutators G = {1_G} by Th63;
  assume
A1: commutators G = {1_G};
  G is commutative
  proof
    let a,b;
    [.a,b.] in {1_G} by A1;
    then [.a,b.] = 1_G by TARSKI:def 1;
    hence thesis by Th39;
  end;
  hence thesis;
end;

definition
  let G,A,B;
  func [.A,B.] -> strict Subgroup of G equals
  gr commutators(A,B);
  correctness;
end;

canceled;

theorem Th68:
  a in A & b in B implies [.a,b.] in [.A,B.]
proof
  assume a in A & b in B;
  then [.a,b.] in commutators(A,B);
  hence thesis by GROUP_4:38;
end;

theorem Th69:
  x in [.A,B.] iff ex F,I st len F = len I & rng F c= commutators(
  A,B) & x = Product(F |^ I)
proof
  thus x in [.A,B.] implies ex F,I st len F = len I & rng F c= commutators(A,B
  ) & x = Product(F |^ I)
  proof
    assume
A1: x in [.A,B.];
    then x in G by GROUP_2:49;
    then reconsider a = x as Element of G by STRUCT_0:def 5;
    a in gr commutators(A,B) by A1;
    hence thesis by GROUP_4:37;
  end;
  thus thesis by GROUP_4:37;
end;

theorem
  A c= C & B c= D implies [.A,B.] is Subgroup of [.C,D.] by Th55,GROUP_4
  :41;

definition
  let G,H1,H2;
  func [.H1,H2.] -> strict Subgroup of G equals
  [.carr H1,carr H2.];
  correctness;
end;

canceled;

theorem
  [.H1,H2.] = gr commutators(H1,H2);

theorem
  x in [.H1,H2.] iff ex F,I st len F = len I & rng F c= commutators(H1,
  H2) & x = Product(F |^ I) by Th69;

theorem Th74:
  a in H1 & b in H2 implies [.a,b.] in [.H1,H2.]
proof
  assume a in H1 & b in H2;
  then a in carr H1 & b in carr H2 by STRUCT_0:def 5;
  hence thesis by Th68;
end;

theorem
  H1 is Subgroup of H2 & H3 is Subgroup of H4 implies [.H1,H3.] is
  Subgroup of [.H2,H4.]
proof
  assume H1 is Subgroup of H2 & H3 is Subgroup of H4;
  then commutators(H1,H3) c= commutators(H2,H4) by Th62;
  hence thesis by GROUP_4:41;
end;

theorem
  for N being strict normal Subgroup of G holds [.N,H.] is Subgroup of N
  & [.H,N.] is Subgroup of N
proof
  let N be strict normal Subgroup of G;
  now
    let a;
    assume a in [.N,H.];
    then consider F,I such that
A1: len F = len I and
A2: rng F c= commutators(N,H) and
A3: a = Product(F |^ I) by Th69;
    commutators(N,H) c= carr N by Th61;
    then rng F c= carr N by A2,XBOOLE_1:1;
    then a in gr carr N by A1,A3,GROUP_4:37;
    hence a in N by GROUP_4:40;
  end;
  hence [.N,H.] is Subgroup of N by GROUP_2:67;
  now
    let a;
    assume a in [.H,N.];
    then consider F,I such that
A4: len F = len I and
A5: rng F c= commutators(H,N) and
A6: a = Product(F |^ I) by Th69;
    commutators(H,N) c= carr N by Th61;
    then rng F c= carr N by A5,XBOOLE_1:1;
    then a in gr carr N by A4,A6,GROUP_4:37;
    hence a in N by GROUP_4:40;
  end;
  hence thesis by GROUP_2:67;
end;

theorem Th77:
  for N1,N2 being strict normal Subgroup of G holds [.N1,N2.] is
  normal Subgroup of G
proof
  let N1,N2 be strict normal Subgroup of G;
  now
    let a;
    now
      let b;
      assume b in [.N1,N2.] |^ a;
      then consider c such that
A1:   b = c |^ a and
A2:   c in [.N1,N2.] by GROUP_3:70;
      consider F,I such that
A3:   len F = len I and
A4:   rng F c= commutators(carr N1,carr N2) and
A5:   c = Product(F |^ I) by A2,GROUP_4:37;
A6:   len(F |^ a) = len F by Def1;
A7:   rng(F |^ a) c= commutators(carr N1,carr N2)
      proof
        let x;
        assume x in rng(F |^ a);
        then consider y such that
A8:     y in dom(F |^ a) and
A9:     (F |^ a).y = x by FUNCT_1:def 5;
        reconsider y as Element of NAT by A8;
A10:    y in dom F by A6,A8,FINSEQ_3:31;
        then
A11:    F.y = (F/.y) by PARTFUN1:def 8;
        y in dom F by A6,A8,FINSEQ_3:31;
        then F.y in rng F by FUNCT_1:def 5;
        then consider d,e such that
A12:    F.y = [.d,e.] and
A13:    d in carr N1 and
A14:    e in carr N2 by A4,Th52;
        d in N1 by A13,STRUCT_0:def 5;
        then d |^ a in N1 |^ a by GROUP_3:70;
        then d |^ a in N1 by GROUP_3:def 13;
        then
A15:    d |^ a in carr N1 by STRUCT_0:def 5;
        e in N2 by A14,STRUCT_0:def 5;
        then e |^ a in N2 |^ a by GROUP_3:70;
        then e |^ a in N2 by GROUP_3:def 13;
        then
A16:    e |^ a in carr N2 by STRUCT_0:def 5;
        x = (F/.y) |^ a by A9,A10,Def1;
        then x = [.d |^ a,e |^ a.] by A12,A11,Th26;
        hence thesis by A15,A16;
      end;
      b = Product(F |^ I |^ a) by A1,A5,Th17
        .= Product((F |^ a) |^ I) by Th18;
      hence b in [.N1,N2.] by A3,A6,A7,GROUP_4:37;
    end;
    hence [.N1,N2.] |^ a is Subgroup of [.N1,N2.] by GROUP_2:67;
  end;
  hence thesis by GROUP_3:145;
end;

Lm2: [.N1,N2.] is Subgroup of [.N2,N1.]
proof
  now
    let a;
    assume a in [.N1,N2.];
    then consider F,I such that
    len F = len I and
A1: rng F c= commutators(N1,N2) and
A2: a = Product(F |^ I) by Th69;
    deffunc F(Nat) = (F/.$1)";
    consider F1 such that
A3: len F1 = len F and
A4: for k be Nat st k in dom F1 holds F1.k = F(k) from FINSEQ_2:sch 1;
A5: dom F = Seg len F by FINSEQ_1:def 3;
A6: rng F1 c= commutators(N2,N1)
    proof
      let x;
      assume x in rng F1;
      then consider y such that
A7:   y in dom F1 and
A8:   F1.y = x by FUNCT_1:def 5;
      reconsider y as Element of NAT by A7;
      y in dom F by A3,A7,FINSEQ_3:31;
      then
A9:   F/.y = F.y by PARTFUN1:def 8;
      y in dom F by A3,A7,FINSEQ_3:31;
      then F.y in rng F by FUNCT_1:def 5;
      then consider b,c such that
A10:  F.y = [.b,c.] and
A11:  b in N1 & c in N2 by A1,Th58;
      x = (F/.y)" by A4,A7,A8;
      then x = [.c,b.] by A10,A9,Th25;
      hence thesis by A11,Th58;
    end;
A12: len(F |^ I) = len F by GROUP_4:def 4;
    then
A13: dom(F |^ I) = Seg len F by FINSEQ_1:def 3;
    deffunc F(Nat) = @(- @(I/.$1));
    consider I1 such that
A14: len I1 = len F and
A15: for k be Nat st k in dom I1 holds I1.k = F(k) from FINSEQ_2:sch 1;
    set J = F1 |^ I1;
A16: dom F1 = dom F by A3,FINSEQ_3:31;
A17: dom I1 = dom F by A14,FINSEQ_3:31;
A18: now
      let k be Nat;
      assume
A19:  k in dom(F |^ I);
      then
A20:  k in dom F by A13,FINSEQ_1:def 3;
A21:  J.k = (F1/.k) |^ @(I1/.k) & F1/.k = F1.k by A5,A16,A13,A19,GROUP_4:
      def 4 ,PARTFUN1:def 8;
A22:  I1.k = @(- @(I/.k)) by A15,A5,A17,A13,A19;
      F1.k = (F/.k)" & I1.k = (I1/.k) by A4,A5,A16,A17,A13,A19,PARTFUN1:
      def 8;
      then J.k = ((F/.k)" |^ @(I/.k))" by A21,A22,GROUP_1:70
        .= ((F/.k) |^ @(I/.k))"" by GROUP_1:72
        .= (F/.k) |^ @(I/.k);
      hence (F |^ I).k = J.k by A20,GROUP_4:def 4;
    end;
    len J = len F by A3,GROUP_4:def 4;
    then J = F |^ I by A12,A18,FINSEQ_2:10;
    hence a in [.N2,N1.] by A2,A3,A14,A6,Th69;
  end;
  hence thesis by GROUP_2:67;
end;

theorem Th78:
  [.N1,N2.] = [.N2,N1.]
proof
  [.N1,N2.] is Subgroup of [.N2,N1.] & [.N2,N1.] is Subgroup of [.N1,N2.]
  by Lm2;
  hence thesis by GROUP_2:64;
end;

theorem Th79:
  for N1,N2,N3 being strict normal Subgroup of G holds [.N1 "\/"
  N2,N3.] = [.N1,N3.] "\/" [.N2,N3.]
proof
  let N1,N2,N3 be strict normal Subgroup of G;
A1: [.N1,N3.] is normal Subgroup of G by Th77;
A2: [.N2,N3.] is normal Subgroup of G by Th77;
  now
    let a;
A3: N3 is Subgroup of N3 by GROUP_2:63;
    N2 is Subgroup of N1 "\/" N2 by GROUP_4:78;
    then
A4: commutators(N2,N3) c= commutators(N1 "\/" N2,N3) by A3,Th62;
    assume a in [.N1,N3.] "\/" [.N2,N3.];
    then consider b,c such that
A5: a = b * c and
A6: b in [.N1,N3.] and
A7: c in [.N2,N3.] by A1,A2,Th7;
    consider F1,I1 such that
A8: len F1 = len I1 and
A9: rng F1 c= commutators(N1,N3) and
A10: b = Product (F1 |^ I1) by A6,Th69;
    consider F2,I2 such that
A11: len F2 = len I2 and
A12: rng F2 c= commutators(N2,N3) and
A13: c = Product (F2 |^ I2) by A7,Th69;
A14: len(F1 ^ F2) = len I1 + len I2 by A8,A11,FINSEQ_1:35
      .= len(I1 ^ I2) by FINSEQ_1:35;
    rng(F1 ^ F2) = rng F1 \/ rng F2 by FINSEQ_1:44;
    then
A15: rng(F1 ^ F2) c= commutators(N1,N3) \/ commutators(N2,N3) by A9,A12,
    XBOOLE_1:13;
    N1 is Subgroup of N1 "\/" N2 by GROUP_4:78;
    then commutators(N1,N3) c= commutators(N1 "\/" N2,N3) by A3,Th62;
    then
    commutators(N1,N3) \/ commutators(N2,N3) c= commutators(N1 "\/" N2,N3
    ) by A4,XBOOLE_1:8;
    then
A16: rng(F1 ^ F2) c= commutators(N1 "\/" N2,N3) by A15,XBOOLE_1:1;
    Product((F1 ^ F2) |^ (I1 ^ I2)) = Product((F1 |^ I1) ^ (F2 |^ I2)) by
    A8,A11,GROUP_4:25
      .= a by A5,A10,A13,GROUP_4:8;
    hence a in [.N1 "\/" N2,N3.] by A16,A14,Th69;
  end;
  then
A17: [.N1,N3.] "\/" [.N2,N3.] is Subgroup of [.N1 "\/" N2,N3.] by
  GROUP_2:67;
  commutators(N1 "\/" N2,N3) c= [.N1,N3.] * [.N2,N3.]
  proof
    let x;
    assume x in commutators(N1 "\/" N2,N3);
    then consider a,b such that
A18: x = [.a,b.] and
A19: a in N1 "\/" N2 and
A20: b in N3 by Th58;
    consider c,d such that
A21: a = c * d and
A22: c in N1 and
A23: d in N2 by A19,Th7;
    [.c,b.] in [.N1,N3.] by A20,A22,Th74;
    then [.c,b.] |^ d in [.N1,N3.] |^ d by GROUP_3:70;
    then
A24: [.c,b.] |^ d in [.N1,N3.] by A1,GROUP_3:def 13;
    x = [.c,b.] |^ d * [.d,b.] & [.d,b.] in [.N2,N3.] by A18,A20,A21,A23,
    Th28,Th74;
    hence thesis by A24,Th4;
  end;
  then
  gr commutators(N1 "\/" N2,N3) is Subgroup of gr ([.N1,N3.] * [.N2,N3.])
  by GROUP_4:41;
  then [.N1 "\/" N2,N3.] is Subgroup of [.N1,N3.] "\/" [.N2,N3.] by
  GROUP_4:68;
  hence thesis by A17,GROUP_2:64;
end;

theorem
  for N1,N2,N3 being strict normal Subgroup of G holds [.N1,N2 "\/" N3.]
  = [.N1,N2.] "\/" [.N1,N3.]
proof
  let N1,N2,N3 be strict normal Subgroup of G;
  N2 "\/" N3 is normal Subgroup of G by GROUP_4:72;
  hence [.N1,N2 "\/" N3.] = [.N2 "\/" N3,N1.] by Th78
    .= [.N2,N1.] "\/" [.N3,N1.] by Th79
    .= [.N1,N2.] "\/" [.N3,N1.] by Th78
    .= [.N1,N2.] "\/" [.N1,N3.] by Th78;
end;

definition
  let G be Group;
  func G` -> strict normal Subgroup of G equals
  [.(Omega).G,(Omega).G.];
  coherence
  proof
    (Omega).G is normal Subgroup of G by GROUP_3:137;
    hence thesis by Th77;
  end;
end;

canceled;

theorem
  for G being Group holds G` = gr commutators G;

theorem Th83:
  for G being Group holds x in G` iff ex F being FinSequence of
the carrier of G,I st len F = len I & rng F c= commutators G & x = Product(F |^
  I)
proof
  let G be Group;
  thus x in G` implies ex F being FinSequence of the carrier of G,I st len F =
  len I & rng F c= commutators G & x = Product(F |^ I)
  proof
    assume
A1: x in G`;
    then x in G by GROUP_2:49;
    then reconsider a = x as Element of G by STRUCT_0:def 5;
    ex F being FinSequence of the carrier of G,I st len F = len I & rng F
    c= commutators G & a = Product(F |^ I) by A1,GROUP_4:37;
    hence thesis;
  end;
  given F being FinSequence of the carrier of G,I such that
A2: len F = len I & rng F c= commutators G & x = Product(F |^ I);
  thus thesis by A2,GROUP_4:37;
end;

theorem Th84:
  for G being strict Group, a,b be Element of G holds [.a,b.] in G `
proof
  let G be strict Group, a,b be Element of G;
  a in (Omega).G & b in (Omega).G by STRUCT_0:def 5;
  hence thesis by Th74;
end;

theorem
  for G being strict Group holds G is commutative Group iff G` = (1).G
proof
  let G be strict Group;
  thus G is commutative Group implies G` = (1).G
  proof
    assume
A1: G is commutative Group;
    now
      let a be Element of G;
      assume a in G`;
      then a in gr {1_G} by A1,Th56;
      then a in gr({1_G} \ {1_G}) by GROUP_4:44;
      then a in gr {} the carrier of G by XBOOLE_1:37;
      hence a in (1).G by GROUP_4:39;
    end;
    then
A2: G` is Subgroup of (1).G by GROUP_2:67;
    (1).G is Subgroup of G` by GROUP_2:77;
    hence thesis by A2,GROUP_2:64;
  end;
  assume
A3: G` = (1).G;
  G is commutative
  proof
    let a,b be Element of G;
    [.a,b.] in G` by Th84;
    then [.a,b.] = 1_G by A3,Th1;
    hence thesis by Th39;
  end;
  hence thesis;
end;

theorem
  for G being Group, H being strict Subgroup of G holds Left_Cosets H is
  finite & index H = 2 implies G` is Subgroup of H
proof
  let G be Group, H be strict Subgroup of G;
  assume that
A1: Left_Cosets H is finite and
A2: index H = 2;
A3: H is normal Subgroup of G by A1,A2,GROUP_3:151;
  now
    let a be Element of G;
    assume a in G`;
    then consider F being FinSequence of the carrier of G,I such that
A4: len F = len I and
A5: rng F c= commutators G and
A6: a = Product(F |^ I) by Th83;
    rng F c= carr H
    proof
      ex B being finite set st B = Left_Cosets H & index H = card B by A1
      ,GROUP_2:176;
      then consider x,y such that
      x <> y and
A7:   Left_Cosets H = {x,y} by A2,CARD_2:79;
      x in Left_Cosets H by A7,TARSKI:def 2;
      then consider d being Element of G such that
A8:   x = d * H by GROUP_2:def 15;
      y in Left_Cosets H by A7,TARSKI:def 2;
      then consider e being Element of G such that
A9:   y = e * H by GROUP_2:def 15;
      carr H in Left_Cosets H by GROUP_2:165;
      then
      Left_Cosets H = {carr H,e * H} or Left_Cosets H = {d * H,carr H} by
      A7,A8,A9,TARSKI:def 2;
      then consider c being Element of G such that
A10:  Left_Cosets H = {carr H,c * H};
      let X be set;
      assume X in rng F;
      then consider a,b being Element of G such that
A11:  X = [.a,b.] by A5,Th65;
      b in the carrier of G;
      then b in union Left_Cosets H by GROUP_2:167;
      then
A12:  b in carr H \/ c * H by A10,ZFMISC_1:93;
      a in the carrier of G;
      then a in union Left_Cosets H by GROUP_2:167;
      then
A13:  a in carr H \/ c * H by A10,ZFMISC_1:93;
      now
        per cases by A13,A12,XBOOLE_0:def 3;
        suppose
          a in carr H & b in carr H;
          then a in H & b in H by STRUCT_0:def 5;
          then X in H by A11,Th41;
          hence thesis by STRUCT_0:def 5;
        end;
        suppose
          a in carr H & b in c * H;
          then a in H by STRUCT_0:def 5;
          then a |^ b in H & a" in H by A3,Th3,GROUP_2:60;
          then a" * (a |^ b) in H by GROUP_2:59;
          then X in H by A11,Th19;
          hence thesis by STRUCT_0:def 5;
        end;
        suppose
          a in c * H & b in carr H;
          then
A14:      b in H by STRUCT_0:def 5;
          then b" in H by GROUP_2:60;
          then b" |^ a in H by A3,Th3;
          then b" |^ a * b in H by A14,GROUP_2:59;
          hence thesis by A11,STRUCT_0:def 5;
        end;
        suppose
A15:      a in c * H & b in c * H;
          then consider d being Element of G such that
A16:      a = c * d and
A17:      d in H by GROUP_2:125;
          consider e being Element of G such that
A18:      b = c * e and
A19:      e in H by A15,GROUP_2:125;
          e" in H by A19,GROUP_2:60;
          then
A20:      e" |^ c in H by A3,Th3;
          d" in H by A17,GROUP_2:60;
          then
A21:      d" * (e" |^ c) in H by A20,GROUP_2:59;
          d |^ c in H by A3,A17,Th3;
          then
A22:      d |^ c * e in H by A19,GROUP_2:59;
          [.a,b.] = (a" * b") * (a * b) by Th19
            .= (d" * c" * b") * (c * d * (c * e)) by A16,A18,GROUP_1:25
            .= (d" * c" * (e" * c")) * (c * d * (c * e)) by A18,GROUP_1:25
            .= (d" * c" * e" * c") * (c * d * (c * e)) by GROUP_1:def 4
            .= (d" * c" * e" * c") * (c * (d * (c * e))) by GROUP_1:def 4
            .= ((d" * c" * e") * c") * c * (d * (c * e)) by GROUP_1:def 4
            .= (d" * c" * e") * (c" * c) * (d * (c * e)) by GROUP_1:def 4
            .= (d" * c" * e") * (1_G) * (d * (c * e)) by GROUP_1:def 6
            .= (d" * c" * e") * (c * c") * (d * (c * e)) by GROUP_1:def 6
            .= (d" * c" * e") * c * c" * (d * (c * e)) by GROUP_1:def 4
            .= (d" * (c" * e")) * c * c" * (d * (c * e)) by GROUP_1:def 4
            .= d" * (e" |^ c) * c" * (d * (c * e)) by GROUP_1:def 4
            .= d" * (e" |^ c) * c" * (d * c * e) by GROUP_1:def 4
            .= d" * (e" |^ c) * (c" * (d * c * e)) by GROUP_1:def 4
            .= d" * (e" |^ c) * (c" * (d * (c * e))) by GROUP_1:def 4
            .= d" * (e" |^ c) * (c" * d * (c * e)) by GROUP_1:def 4
            .= d" * (e" |^ c) * (d |^ c * e) by GROUP_1:def 4;
          then X in H by A11,A21,A22,GROUP_2:59;
          hence thesis by STRUCT_0:def 5;
        end;
      end;
      hence thesis;
    end;
    then a in gr carr H by A4,A6,GROUP_4:37;
    hence a in H by GROUP_4:40;
  end;
  hence thesis by GROUP_2:67;
end;

begin :: Center of a Group.

definition
  let G;
  func center G -> strict Subgroup of G means
  :Def10:
  the carrier of it = {a : for b holds a * b = b * a};
  existence
  proof
    defpred P[Element of G] means for b holds $1 * b = b * $1;
    reconsider A = {a : P[a]} as Subset of G from DOMAIN_1:sch 7;
A1: now
      let a,b;
      assume that
A2:   a in A and
A3:   b in A;
      consider c such that
A4:   c = a and
A5:   for b holds c * b = b * c by A2;
      consider d such that
A6:   d = b and
A7:   for a holds d * a = a * d by A3;
      now
        let e;
        thus a * b * e = a * (b * e) by GROUP_1:def 4
          .= a * (e * d) by A6,A7
          .= a * e * b by A6,GROUP_1:def 4
          .= e * c * b by A4,A5
          .= e * (a * b) by A4,GROUP_1:def 4;
      end;
      hence a * b in A;
    end;
A8: now
      let a;
      assume a in A;
      then consider b such that
A9:   b = a and
A10:  for c holds b * c = c * b;
      now
        let c;
        thus a" * c = (a" * c)"" .= (c" * a"")" by GROUP_1:25
          .= (c" * b)" by A9
          .= (b * c")" by A10
          .= (a"" * c")" by A9
          .= (c * a")"" by GROUP_1:25
          .= c * a";
      end;
      hence a" in A;
    end;
    now
      let b;
      1_G * b = b by GROUP_1:def 5;
      hence 1_G * b = b * 1_G by GROUP_1:def 5;
    end;
    then 1_G in A;
    hence thesis by A1,A8,GROUP_2:61;
  end;
  uniqueness by GROUP_2:68;
end;

canceled 2;

theorem Th89:
  a in center G iff for b holds a * b = b * a
proof
  thus a in center G implies for b holds a * b = b * a
  proof
    assume a in center G;
    then a in the carrier of center G by STRUCT_0:def 5;
    then a in {b : for c holds b * c = c * b} by Def10;
    then ex b st a = b & for c holds b * c = c * b;
    hence thesis;
  end;
  assume for b holds a * b = b * a;
  then a in {c : for b holds c * b = b * c};
  then a in the carrier of center G by Def10;
  hence thesis by STRUCT_0:def 5;
end;

theorem
  center G is normal Subgroup of G
proof
  now
    let a;
    thus a * center G c= center G * a
    proof
      let x;
      assume x in a * center G;
      then consider b such that
A1:   x = a * b and
A2:   b in center G by GROUP_2:125;
      x = b * a by A1,A2,Th89;
      hence thesis by A2,GROUP_2:126;
    end;
  end;
  hence thesis by GROUP_3:141;
end;

theorem
  for H being Subgroup of G holds H is Subgroup of center G implies H is
  normal Subgroup of G
proof
  let H be Subgroup of G;
  assume
A1: H is Subgroup of center G;
  now
    let a;
    thus H * a c= a * H
    proof
      let x;
      assume x in H * a;
      then consider b such that
A2:   x = b * a and
A3:   b in H by GROUP_2:126;
      b in center G by A1,A3,GROUP_2:49;
      then x = a * b by A2,Th89;
      hence thesis by A3,GROUP_2:125;
    end;
  end;
  hence thesis by GROUP_3:142;
end;

theorem
  center G is commutative
proof
  let a,b be Element of center G;
  reconsider x = a, y = b as Element of G by GROUP_2:51;
A1: a in center G by STRUCT_0:def 5;
  thus a * b = x * y by GROUP_2:52
    .= y * x by A1,Th89
    .= b * a by GROUP_2:52;
end;

theorem
  a in center G iff con_class a = {a}
proof
  thus a in center G implies con_class a = {a}
  proof
    assume
A1: a in center G;
    thus con_class a c= {a}
    proof
      let x;
      assume x in con_class a;
      then consider b such that
A2:   b = x and
A3:   a,b are_conjugated by GROUP_3:95;
      consider c such that
A4:   a = b |^ c by A3,GROUP_3:def 7;
      a = c" * (b * c) by A4,GROUP_1:def 4;
      then c * a = b * c by GROUP_1:21;
      then a * c = b * c by A1,Th89;
      then a = b by GROUP_1:14;
      hence thesis by A2,TARSKI:def 1;
    end;
    a in con_class a by GROUP_3:98;
    hence thesis by ZFMISC_1:37;
  end;
  assume
A5: con_class a = {a};
  now
    let b;
    a |^ b in con_class a by GROUP_3:97;
    then a |^ b = a by A5,TARSKI:def 1;
    then b" * (a * b) = a by GROUP_1:def 4;
    hence a * b = b * a by GROUP_1:21;
  end;
  hence thesis by Th89;
end;

theorem
  for G being strict Group holds G is commutative Group iff center G = G
proof
  let G be strict Group;
  thus G is commutative Group implies center G = G
  proof
    assume
A1: G is commutative Group;
    now
      let a be Element of G;
      for b being Element of G holds a * b = b * a by A1,Lm1;
      hence a in center G by Th89;
    end;
    hence thesis by GROUP_2:71;
  end;
  assume
A2: center G = G;
  G is commutative
  proof
    let a,b be Element of G;
    a in G by STRUCT_0:def 5;
    hence thesis by A2,Th89;
  end;
  hence thesis;
end;
