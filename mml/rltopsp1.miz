:: Introduction to Real Linear Topological Spaces
::  by Czes{\l}aw Byli\'nski
::
:: Received October 6, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies RLTOPSP1, TARSKI, BOOLE, RELAT_2, SUBSET_1, FINSET_1, XREAL_0,
      SETFAM_1, FUNCT_1, BINOP_1, PRE_TOPC, ORDINAL2, ARYTM, ARYTM_1, ABSVALUE,
      ASYMPT_0, EQREL_1, ARYTM_3, TOPREAL1, LATTICES, MSUALG_6, CONVEX1,
      JORDAN1, CANTOR_1, RELAT_1, TOPS_1, TOPS_2, RVSUM_1, CONNSP_2, COMPTS_1,
      URYSOHN1, YELLOW_6, RLVECT_1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, FINSET_1, SETFAM_1, NUMBERS,
      XCMPLX_0, XREAL_0, ORDINAL1, COMPLEX1, RELAT_1, FUNCT_1, FUNCT_2,
      BINOP_1, REAL_1, EQREL_1, STRUCT_0, PRE_TOPC, TOPS_1, TOPS_2, CONNSP_2,
      COMPTS_1, YELLOW13, URYSOHN1, YELLOW_6, RLVECT_1, RUSUB_4, CONVEX1;
 constructors FINSEQ_1, FUNCT_2, BINOP_1, XCMPLX_0, XREAL_0, COMPLEX1, NUMBERS,
      ABSVALUE, REAL_1, FINSET_1, MEMBERED, EQREL_1, ARYTM_0, FUNCOP_1,
      PRE_TOPC, TOPS_1, TOPS_2, CONNSP_2, COMPTS_1, CANTOR_1, SEQ_2, YELLOW13,
      URYSOHN1, RUSUB_4, CONVEX1, DOMAIN_1, LIMFUNC1, RLVECT_1;
 registrations NUMBERS, SUBSET_1, STRUCT_0, XREAL_0, RELSET_1, PRE_TOPC,
      ASYMPT_0, XCMPLX_0, FILTER_1, ARYTM_3, REAL_1, TOPS_1, TOPGRP_1,
      BORSUK_3, RLVECT_1, CONVEX1, FINSEQ_1, PARTFUN1;
 requirements BOOLE, REAL, NUMERALS, SUBSET, ARITHM;
 definitions TARSKI, XBOOLE_0, STRUCT_0, RLVECT_1, PRE_TOPC, COMPTS_1,
      CONNSP_2, TOPS_2, YELLOW13, CONVEX1;
 theorems TARSKI, XBOOLE_0, XBOOLE_1, ZFMISC_1, SUBSET_1, SETFAM_1, XCMPLX_1,
      XREAL_0, SQUARE_1, XCMPLX_0, RELSET_1, BINOP_1, RELAT_1, FUNCT_2,
      FUNCT_1, ABSVALUE, FINSET_1, EQREL_1, WELLORD2, STRUCT_0, PRE_TOPC,
      COMPTS_1, CONNSP_2, TOPS_1, URYSOHN1, BORSUK_1, YELLOW13, RLVECT_1,
      RUSUB_4, CONVEX1, CONVEX2, TOPGRP_1, TOPS_2, FRECHET2, AXIOMS, REAL_1,
      REAL_2, MEASURE1, XREAL_1, TSEP_1;
 schemes BINOP_1, FUNCT_2, PRE_TOPC, EQREL_1, FINSET_1;

begin :: Preliminaires

 reserve X,Y for non empty RLSStruct,
         v,x,y,z for Point of X,
         f,f1,f2 for map of X,Y,
         a,e,r,s,t for Real;

canceled;

Th1:
  for l,r,r1,r2 being real number st r1<=r & r2<=r & 0 <= l & l <= 1
    holds (1-l)*r1+l*r2 <= r by XREAL_1:176;

registration
  cluster non zero Real;
  existence
   proof take 1;
     thus thesis;
   end;
end;

theorem Th2:
  for T being non empty TopSpace, X being non empty Subset of T,
      FX being Subset-Family of T
    st FX is_a_cover_of X
   for x being Point of T st x in X
    ex W being Subset of T st x in W & W in FX
 proof
  let T be non empty TopSpace, X be non empty Subset of T,
      FX be Subset-Family of T;
  assume FX is_a_cover_of X;
  then
A1: X c= union FX by COMPTS_1:def 1;
  let x be Point of T;
  assume x in X;
  then
A2: x in union FX by A1;
  consider W being set such that
A3: x in W and
A4: W in FX by A2,TARSKI:def 4;
  reconsider W as Subset of T by A4;
  take W;
  thus thesis by A3,A4;
 end;

canceled;
Th3:
  for T being non empty TopSpace, X being open Subset of T,
      Y being Subset of T st X misses Y
  holds X misses Cl Y by TSEP_1:40;

Lm1:
   for X being non empty LoopStr, M being Subset of X, x,y being Point of X
      st y in M
     holds x+y in x+M
proof
  let X be non empty LoopStr, M be Subset of X, x,y be Point of X;
 x+M = {x + u where u is Point of X: u in M} by RUSUB_4:def 9;
 hence thesis;
end;

Lm2:
   for X being non empty LoopStr, M,N being Subset of X, x,y being Point of X
      st x in M & y in N
     holds x+y in M+N
proof
  let X be non empty LoopStr, M,N be Subset of X, x,y be Point of X;
 M+N = {u + v where u,v is Point of X: u in M & v in N} by RUSUB_4:def 10;
 hence thesis;
end;

Lm3:
   for X being non empty LoopStr, M,N being Subset of X
    holds {x+N where x is Point of X: x in M}is Subset-Family of X
 proof let X be non empty LoopStr, M,N being Subset of X;
   set F = {u+N where u is Point of X: u in M};
   F c= bool the carrier of X
    proof let x be set;
      assume x in F;
      then ex u being Point of X st x=u+N & u in M;
      then x c= the carrier of X;
      hence thesis;
    end;
   then F is Subset-Family of the carrier of X by SETFAM_1:def 7;
   hence thesis;
 end;

theorem Th4:
   for X being non empty LoopStr, M,N being Subset of X,
       F being Subset-Family of X
     st F = {x+N where x is Point of X: x in M}
    holds M+N = union F
 proof
   let X be non empty LoopStr, M,N being Subset of X, F be Subset-Family of X;
   assume
A1:   F = {x+N where x is Point of X: x in M};
  thus M+N c= union F
   proof let x be set;
     assume x in M+N;
     then x in {u+v where u,v is Point of X: u in M & v in N}
    by RUSUB_4:def 10;
     then consider u,v being Point of X such that
A2:     x = u+v and
A3:     u in M and
A4:     v in N;
A5:     u+N in F by A1,A3;
     u+N = {u + v' where v' is Point of X: v' in N} by RUSUB_4:def 9;
     then x in u+N by A2,A4;
     hence x in union F by A5,TARSKI:def 4;
   end;
  let x be set;
  assume x in union F;
  then consider Y being set such that
A6:  x in Y and
A7:  Y in F by TARSKI:def 4;
  consider u being Point of X such that
A8:  Y = u+N and
A9:  u in M by A1,A7;
  u+N = {u + v where v is Point  of X: v in N} by RUSUB_4:def 9;
  then ex v being Point of X st x = u+v & v in N by A6,A8;
  then x in {u'+v' where u',v' is Point of X: u' in M & v' in N} by A9;
  hence x in M+N by RUSUB_4:def 10;
 end;

theorem Th5:
  for X being add-associative right_zeroed
                   right_complementable (non empty LoopStr),
      M being Subset of X
   holds 0.X+M = M
proof
  let X be add-associative
   right_zeroed right_complementable (non empty LoopStr), M be Subset of X;
A1:  0.X+M = {0.X + u where u is Point of X: u in M}
    by RUSUB_4:def 9;
  thus 0.X+M c= M
   proof let x be set;
    assume x in 0.X+M;
    then consider u being Point of X such that
A2:    x = 0.X+u and
A3:    u in M by A1;
    x = u by A2,RLVECT_1:10;
    hence thesis by A3;
   end;
  let x be set;
  assume
A4:  x in M;
  then reconsider x as Point of X;
  0.X+x = x by RLVECT_1:10;
  hence thesis by A1,A4;
end;

theorem Th6:
  for X being add-associative (non empty LoopStr),x,y being Point of X,
      M being Subset of X
   holds x+y+M = x+(y+M)
 proof
  let X be add-associative (non empty LoopStr),x,y be Point of X,
      M be Subset of X;
 A1: x+y+M = {x+y+u where u is Point of X: u in M} by RUSUB_4:def 9;
 A2: x+(y+M) = {x+u where u is Point of X: u in y+M} by RUSUB_4:def 9;
 A3: y+M = {y+u where u is Point of X: u in M} by RUSUB_4:def 9;
   thus x+y+M c= x+(y+M)
    proof
     let z be set;
     assume z in x+y+M;
    then consider u being Point of X such that
A4:   x+y+u = z and
A5:   u in M by A1;
A6:   x+(y+u) = z by A4,RLVECT_1:def 6;
     y+u in y+M by A3,A5;
     hence z in x+(y+M) by A2,A6;
    end;
   let z be set;
   assume z in x+(y+M);
   then consider u being Point of X such that
A7:  x+u = z and
A8:  u in y+M by A2;
   consider v being Point of X such that
A9:  y+v = u and
A10:  v in M by A3,A8;
   x+y+v = z by A7,A9,RLVECT_1:def 6;
   hence z in x+y+M by A1,A10;
 end;

theorem Th7:
  for X being add-associative (non empty LoopStr), x being Point of X,
      M,N being Subset of X
   holds x+M+N = x+(M+N)
 proof
  let X be add-associative (non empty LoopStr), x be Point of X,
      M,N be Subset of X;
 A1: x+M+N = {u+v where u,v is Point of X: u in x+M & v in N}
    by RUSUB_4:def 10;
 A2: x+(M+N) = {x+u where u is Point of X: u in M+N} by RUSUB_4:def 9;
 A3: x+M = {x+u where u is Point of X: u in M} by RUSUB_4:def 9;
 A4:  M+N = {u+v where u,v is Point of X: u in M & v in N}
    by RUSUB_4:def 10;
   thus x+M+N c= x+(M+N)
    proof
     let z be set;
     assume z in x+M+N;
     then consider u,v being Point of X such that
A5:   u+v = z and
A6:   u in x+M and
A7:   v in N by A1;
     consider u' being Point of X such that
A8:   x+u' = u and
A9:   u' in M by A3,A6;
A10:   x+(u'+v) = z by A5,A8,RLVECT_1:def 6;
     u'+v in M+N by A4,A7,A9;
     hence z in x+(M+N) by A2,A10;
    end;
   let z be set;
   assume z in x+(M+N);
   then consider u being Point of X such that
A11:  x+u = z and
A12:  u in M+N by A2;
   consider w,v being Point of X such that
A13:  w+v = u and
A14:  w in M and
A15:  v in N by A4,A12;
A16:   x+w+v = z by A11,A13,RLVECT_1:def 6;
   x+w in x+M by A3,A14;
   hence z in x+M+N by A1,A15,A16;
 end;

theorem Th8:
   for X being non empty LoopStr, M,N being Subset of X, x being Point of X
       st M c= N
     holds x+M c= x+N
 proof
  let X be non empty LoopStr, M,N be Subset of X, x be Point of X such that
A1:   M c= N;
  let z be set;
  assume
A2:  z in x+M;
 then reconsider z as Point of X;
  x+M = {x + u where u is Element of the carrier of X: u in M}
  by RUSUB_4:def 9;
  then consider u being Point of X such that
A3: x+u = z and
A4: u in M by A2;
  u in N by A1,A4;
  hence thesis by A3,Lm1;
 end;

theorem Th9:
   for X being non empty RealLinearSpace, M being Subset of X,
       x being Point of X
     st x in M
   holds 0.X in -x+M
 proof
   let X be non empty RealLinearSpace, M be Subset of X,
       x be Point of X;
  assume x in M;
  then -x+x in -x+M by Lm1;
  hence thesis by RLVECT_1:16;
 end;

theorem Th10:
   for X being non empty LoopStr, M,N,V being Subset of X
       st M c= N
     holds M+V c= N+V
 proof let X be non empty LoopStr, M,N,V be Subset of X such that
A1:  M c= N;
  let x be set;
  assume x in M+V;
  then x in {u + v where u,v is Point of X: u in M & v in V}
 by RUSUB_4:def 10;
  then consider u,v being Point of X such that
A2:  u+v = x and
A3:  u in M and
A4:  v in V;
  u in N by A1,A3;
  then x in {u' + v' where u',v' is Point of X: u' in N & v' in V} by A2,A4;
  hence x in N+V by RUSUB_4:def 10;
 end;

Lm4:
    for X being non empty LoopStr, M,N,V being Subset of X
       st M c= N
     holds V+M c= V+N
 proof let X be non empty LoopStr, M,N,V be Subset of X such that
A1:  M c= N;
  let x be set;
  assume x in V+M;
  then x in {u + v where u,v is Point of X: u in V & v in M}
 by RUSUB_4:def 10;
  then consider u,v being Point of X such that
A2:  u+v = x and
A3:  u in V and
A4:  v in M;
  v in N by A1,A4;
  then x in {u' + v' where u',v' is Point of X: u' in V & v' in N} by A2,A3;
  hence x in V+N by RUSUB_4:def 10;
 end;

theorem Th11:
    for X being non empty LoopStr, V1,V2,W1,W2 being Subset of X
       st V1 c= W1 & V2 c= W2
     holds V1+V2 c= W1+W2
 proof let X be non empty LoopStr, V1,V2,W1,W2 be Subset of X such that
A1:  V1 c= W1 and
A2: V2 c= W2;
  let x be set;
  assume x in V1+V2;
  then x in {u + v where u,v is Point of X: u in V1 & v in V2}
 by RUSUB_4:def 10;
  then consider u,v being Point of X such that
A3:  u+v = x and
A4:  u in V1 and
A5:  v in V2;
  u in W1 & v in W2 by A1,A2,A4,A5;
  then x in {u' + v' where u',v' is Point of X: u' in W1 & v' in W2}
 by A3;
  hence x in W1+W2 by RUSUB_4:def 10;
 end;

theorem Th12:
    for X being non empty RealLinearSpace, V1,V2 being Subset of X
       st 0.X in V2
     holds V1 c= V1+V2
 proof let X be non empty RealLinearSpace, V1,V2 be Subset of X such that
A1: 0.X in V2;
  let x be set;
  assume
A2:  x in V1;
  then reconsider x as Point of X;
  x+0.X = x by RLVECT_1:def 7;
  then x in {u + v where u,v is Point of X: u in V1 & v in V2} by A1,A2;
  hence thesis by RUSUB_4:def 10;
 end;

Lm5:
   for X being non empty RLSStruct, M being Subset of X,
       x being Point of X, r being Real
      st x in M
     holds r*x in r*M
proof
  let X be non empty RLSStruct, M be Subset of X,
      x be Point of X, r be Real;
  r*M = {r * v where v is Point of X: v in M} by CONVEX1:def 1;
  hence thesis;
end;

theorem Th13:
   for X being non empty RealLinearSpace, r being Real
    holds r*{0.X} = {0.X}
 proof let X be non empty RealLinearSpace, r be Real;
   thus r*{0.X} c= {0.X}
    proof let x be set;
A1:     r*{0.X} = {r*v where v is Point of X: v in {0.X}} by CONVEX1:def 1;
      assume
A2:     x in r*{0.X};
      then reconsider x as Point of X;
      consider v being Point of X such that
A3:    x = r*v and
A4:    v in {0.X} by A1,A2;
      v = 0.X by A4,TARSKI:def 1;
      then r*v = 0.X by RLVECT_1:23;
      hence thesis by A3,TARSKI:def 1;
    end;
   0.X in {0.X} by TARSKI:def 1;
   then r*0.X in r*{0.X} by Lm5;
   then 0.X in r*{0.X} by RLVECT_1:23;
   hence {0.X} c= r*{0.X} by ZFMISC_1:37;
 end;

theorem
   for X being non empty RealLinearSpace, M being Subset of X,
       r being non zero Real
      st 0.X in r*M
     holds 0.X in M
proof
  let X be non empty RealLinearSpace, M be Subset of X, r be non zero Real
    such that
A1:  0.X in r*M;
A2:  r*M = {r * v where v is Point of X: v in M} by CONVEX1:def 1;
  consider v being Point of X such that
A3: r*v = 0.X and
A4: v in M by A1,A2;
  r * 0.X = 0.X by RLVECT_1:23;
  then v = 0.X by A3,RLVECT_1:50;
  then 0.X in M by A4;
  hence thesis;
end;

theorem Th15:
   for X being non empty RealLinearSpace, M,N being Subset of X,
       r being non zero Real
    holds (r * M) /\ (r * N) = r * (M /\ N)
proof
  let X be non empty RealLinearSpace, M,N be Subset of X, r be non zero Real;
A1:  r*M = {r * v where v is Point of X: v in M} by CONVEX1:def 1;
A2:  r*N = {r * v where v is Point of X: v in N} by CONVEX1:def 1;
  thus for x being set st x in (r * M) /\ (r * N) holds x in r * (M /\ N)
   proof let x be set;
     assume
A3:     x in (r * M) /\ (r * N);
     then x in r * M by XBOOLE_0:def 3;
     then consider v1 being Point of X such that
A4:     r*v1 = x and
A5:    v1 in M by A1;
     x in (r * N) by A3,XBOOLE_0:def 3;
     then consider v2 being Point of X such that
A6:     r*v2 = x and
A7:   v2 in N by A2;
      v1 = v2 by A4,A6,RLVECT_1:50;
     then v1 in M /\ N by A5,A7,XBOOLE_0:def 3;
     then x in {r * v where v is Point of X: v in M /\ N} by A4;
    hence thesis by CONVEX1:def 1;
   end;
  let x be set;
  assume x in r * (M /\ N);
  then x in {r * v where v is Point of X: v in M /\ N} by CONVEX1:def 1;
  then consider v being Point of X such that
A8:  r*v = x and
A9:  v in M /\ N;
  v in  M by A9,XBOOLE_0:def 3;
  then x in {r * w where w is Point of X: w in M} by A8;
  then
A10: x in r*M by CONVEX1:def 1;
  v in  N by A9,XBOOLE_0:def 3;
  then x in {r * w where w is Point of X: w in N} by A8;
  then x in r*N by CONVEX1:def 1;
  hence thesis by A10,XBOOLE_0:def 3;
end;

theorem
   for X being non empty TopSpace, x being Point of X,
       A being a_neighborhood of x, B being Subset of X
     st A c= B
   holds B is a_neighborhood of x
 proof
   let X be non empty TopSpace, x be Point of X, A be a_neighborhood of x,
       B be Subset of X such that
A1:  A c= B;
A2:  x in Int A by CONNSP_2:def 1;
   Int A c= Int B by A1,TOPS_1:48;
   then x in Int B by A2;
   hence thesis by CONNSP_2:def 1;
 end;

definition
let V be non empty RealLinearSpace, M be Subset of V;
  redefine attr M is convex means :Def1:
 for u,v being Point of V, r being Real
    st 0 <= r & r <= 1 & u in M & v in M
  holds r*u + (1-r)*v in M;
 compatibility
  proof
   hereby
     assume
A1:    M is convex;
     let u,v be Point of V, r be Real such that
A2:    0 <= r and
A3:    r <= 1 and
A4:    u in M and
A5:    v in M;
     per cases by A2,A3,REAL_1:def 5;
     suppose 0 < r & r < 1;
      hence r*u + (1-r)*v in M by A1,A4,A5,CONVEX1:def 2;
     end;
     suppose
A6:     0 = r;
      then
A7:     r*u = 0.V by RLVECT_1:23;
      (1-r)*v = v by A6,RLVECT_1:def 9;
      hence r*u + (1-r)*v in M by A5,A7,RLVECT_1:def 7;
     end;
     suppose
A8:     r = 1;
       then 1-r = 0;
       then
A9:       (1-r)*v = 0.V by RLVECT_1:23;
       r*u = u by A8,RLVECT_1:def 9;
      hence r*u + (1-r)*v in M by A4,A9,RLVECT_1:def 7;
     end;
    end;
    assume
A10:    for u,v being Point of V, r being Real
         st 0 <= r & r <= 1 & u in M & v in M
        holds r*u + (1-r)*v in M;
    let u,v be Point of V, r be Real;
    assume 0 < r & r < 1;
    then 0 <= r & r <= 1;
    hence thesis by A10;
  end;
end;

theorem :: CONVEX1:13
  for X being non empty RealLinearSpace, M being convex Subset of X,
      r1,r2 being Real
   st 0 <= r1 & 0<=r2
  holds r1*M + r2*M = (r1 + r2)*M
proof
 let X be non empty RealLinearSpace, M be convex Subset of X, r1,r2 be Real;
 assume 0 <= r1 & 0<=r2;
 hence r1*M + r2*M c= (r1 + r2)*M by CONVEX1:13;
 thus (r1 + r2)*M c= r1*M + r2*M by CONVEX1:12;
end;

Lm6:
  for X being non empty RealLinearSpace holds conv({}X) = {}
proof
 let X be non empty RealLinearSpace;
 {}X is convex by CONVEX1:10;
 then
A1: {}X in Convex-Family {}X by CONVEX1:def 4;
 conv({}X) = meet Convex-Family {}X by CONVEX1:def 5;
 hence thesis by A1,SETFAM_1:5;
end;

registration let X be non empty RealLinearSpace, M be empty Subset of X;
 cluster conv(M) -> empty;
  coherence
 proof
   M = {}X;
   hence thesis by Lm6;
 end;
end;

theorem
  for X being non empty RealLinearSpace, M being convex Subset of X
   holds conv(M) = M
proof
  let X be non empty RealLinearSpace, M be convex Subset of X;
  thus conv(M) c= M by CONVEX1:30;
  thus M c= conv(M) by CONVEX2:8;
end;

theorem Th19:
  for X being non empty RealLinearSpace, M being Subset of X, r being Real
    holds r*conv(M) = conv(r*M)
proof
  let X be non empty RealLinearSpace, M be Subset of X, r be Real;
  thus r*conv(M) c= conv(r*M)
   proof let x be set;
   per cases;
   suppose
A1:     r = 0;
     per cases;
     suppose M = {};
       then M = {}X;
       then conv(M) = {} by Lm6;
       then r*conv(M) = {} by CONVEX1:33;
       hence thesis;
     end;
     suppose
A2:      M <> {};
       M c= conv(M) by CONVEX2:8;
       then conv(M) <> {} by A2,XBOOLE_1:3;
       then
A3:      r*conv(M) = {0.X} by A1,CONVEX1:34;
       r*M = {0.X} by A1,A2,CONVEX1:34;
       then {0.X} c= conv(r*M) by CONVEX2:8;
       hence thesis by A3;
     end;
    end;
    suppose
A4:    r <> 0;
A5:    r*conv(M) = {r*v where v is Point of X: v in conv(M)} by CONVEX1:def 1;
     assume x in r*conv(M);
     then consider v being Point of X such that
A6:    x = r*v and
A7:    v in conv(M) by A5;
       Convex-Family M <> {} by CONVEX2:7;
     then consider Y being set such that
A8:    Y in Convex-Family M by XBOOLE_0:def 1;
     reconsider Y as Subset of X by A8;
A9:    v in meet Convex-Family M by A7,CONVEX1:def 5;
A10:    for V being set st V in Convex-Family(r*M) holds r*v in V
      proof let V being set;
       assume
A11:     V in Convex-Family(r*M);
       then reconsider V as Subset of X;
A12:     V is convex by A11,CONVEX1:def 4;
       r*M c= V by A11,CONVEX1:def 4;
       then r"*(r*M) c= r"*V by CONVEX1:39;
       then (r"*r)*M c= r"*V by CONVEX1:37;
       then 1*M c= r"*V by A4,XCMPLX_0:def 7;
       then
A13:     M c= r"*V by CONVEX1:32;
A14:     r"*V = {r"*w where w is Point of X: w in V} by CONVEX1:def 1;
       r"*V is convex by A12,CONVEX1:1;
       then r"*V in Convex-Family M by A13,CONVEX1:def 4;
       then v in r"*V by A9,SETFAM_1:def 1;
       then consider w being Point of X such that
A15:     v = r"*w and
A16:     w in V by A14;
       r*v = (r*r")*w by A15,RLVECT_1:def 9
          .= 1*w by A4,XCMPLX_0:def 7
          .= w by RLVECT_1:def 9;
       hence thesis by A16;
      end;
      Convex-Family(r*M) <> {} by CONVEX2:7;
      then r*v in meet Convex-Family(r*M) by A10,SETFAM_1:def 1;
      hence thesis by A6,CONVEX1:def 5;
    end;
   end;
  M c= conv(M) by CONVEX2:8;
  then
A17:  r*M c= r*conv(M) by CONVEX1:39;
  r*conv(M) is convex by CONVEX1:1;
  hence conv(r*M) c= r*conv(M) by A17,CONVEX1:30;
end;

theorem Th20:
  for X being non empty RealLinearSpace, M1,M2 being Subset of X
   st M1 c= M2
  holds Convex-Family M2 c= Convex-Family M1
proof
  let X be non empty RealLinearSpace, M1,M2 be Subset of X such that
A1: M1 c= M2;
  let M be set;
  assume
A2:  M in Convex-Family M2;
  then reconsider M as Subset of X;
  M2 c= M by A2,CONVEX1:def 4;
  then
A3:  M1 c= M by A1,XBOOLE_1:1;
  M is convex by A2,CONVEX1:def 4;
  hence thesis by A3,CONVEX1:def 4;
end;

theorem Th21:
  for X being non empty RealLinearSpace, M1,M2 being Subset of X
   st M1 c= M2
  holds conv(M1) c= conv(M2)
proof
  let X be non empty RealLinearSpace, M1,M2 be Subset of X such that
A1: M1 c= M2;
  let x be set;
  assume x in conv(M1);
  then
A2: x in meet Convex-Family M1 by CONVEX1:def 5;
A3: Convex-Family M2 <> {} by CONVEX2:7;
  Convex-Family M2 c= Convex-Family M1 by A1,Th20;
  then meet (Convex-Family M1) c= meet (Convex-Family M2) by A3,SETFAM_1:7;
  then x in meet (Convex-Family M2) by A2;
  hence x in conv(M2) by CONVEX1:def 5;
end;

theorem
   for X being non empty RealLinearSpace, M being convex Subset of X,
       r being Real st 0 <= r & r <= 1 & 0.X in M
    holds r*M c= M
 proof
   let X be non empty RealLinearSpace, M be convex Subset of X,
       r be Real such that
A1: 0 <= r and
A2: r <= 1 and
A3: 0.X in M;
A4: r*M = {r * v where v is Point of X: v in M} by CONVEX1:def 1;
  let x be set;
  assume x in r*M;
  then consider v being Point of X such that
A5: r*v = x and
A6: v in M by A4;
  r*v + (1-r)*0.X in M by A1,A2,A3,A6,Def1;
  then r*v + 0.X in M by RLVECT_1:23;
  hence thesis by A5,RLVECT_1:def 7;
 end;

definition
  let X be non empty RealLinearSpace, v,w be Point of X;
   func LSeg(v,w) -> Subset of X equals
:Def2: {(1-r)*v + r*w : 0 <= r & r <= 1 };
  coherence
 proof
    {(1-r)*v + r*w: 0 <= r & r <= 1 } c= the carrier of X
   proof let x be set;
    assume x in {(1-r)*v + r*w: 0 <= r & r <= 1 };
     then ex r st x = (1-r)*v + r*w & 0 <= r & r <= 1;
    hence thesis;
   end;
  hence thesis;
 end;
end;

registration
 let X be non empty RealLinearSpace, v,w be Point of X;
  cluster LSeg(v,w) -> non empty convex;
   coherence
  proof
A1: LSeg(v,w) = { (1-r)*v + r*w: 0 <= r & r <= 1 } by Def2;
   1 - 0 = 1 & 1 * v = v & 0 * w = 0.X & v + 0.X = v
   by RLVECT_1:23,def 7,def 9;
   then v in LSeg(v,w) by A1;
   hence LSeg(v,w) is non empty;
   let u,u' be Point of X,  r be Real;
   assume
A2:   0 <= r & r <= 1;
   assume u in LSeg(v,w);
   then consider s being Real such that
A3:   u = (1-s)*v + s*w and
A4:   0 <= s and
A5:   s <= 1 by A1;
   assume u' in LSeg(v,w);
   then consider t being Real such that
A6:   u' = (1-t)*v + t*w and
A7:   0 <= t and
A8:   t <= 1 by A1;
A9:   r*u = r*((1-s)*v) + r*(s*w) by A3,RLVECT_1:def 9
         .= r*(1-s)*v + r*(s*w) by RLVECT_1:def 9
         .= r*(1-s)*v + r*s*w by RLVECT_1:def 9;
    (1-r)*u' = (1-r)*((1-t)*v) + (1-r)*(t*w) by A6,RLVECT_1:def 9
            .= (1-r)*(1-t)*v + (1-r)*(t*w) by RLVECT_1:def 9
            .= (1-r)*(1-t)*v + (1-r)*t*w by RLVECT_1:def 9;
    then
A10:  r*u + (1-r)*u'
       = r*(1-s)*v + (r*s*w + ((1-r)*(1-t)*v + (1-r)*t*w)) by A9,RLVECT_1:def 6
      .= r*(1-s)*v + (r*s*w + (1-r)*(1-t)*v + (1-r)*t*w) by RLVECT_1:def 6
      .= r*(1-s)*v + ((1-r)*(1-t)*v + r*s*w + (1-r)*t*w)
      .= r*(1-s)*v + ((1-r)*(1-t)*v + (r*s*w + (1-r)*t*w)) by RLVECT_1:def 6
      .= r*(1-s)*v + (1-r)*(1-t)*v + (r*s*w + (1-r)*t*w) by RLVECT_1:def 6
      .= (r*(1-s) + (1-r)*(1-t))*v + (r*s*w + (1-r)*t*w) by RLVECT_1:def 9
      .= (r*(1-s) + (1-r)*(1-t))*v + (r*s + (1-r)*t)*w by RLVECT_1:def 9
      .= (1 - ((r*s + (1-r)*t)))*v + (r*s + (1-r)*t)*w;
    0 <= 1-r by A2,SQUARE_1:12;
    then 0 <= r*s & 0 <= (1-r)*t by A2,A4,A7,REAL_2:121;
    then
A11:  0+0 <= r*s + (1-r)*t by REAL_1:55;
   (1-r)*t + r*s <= 1 by A2,A5,A8,Th1;
   hence r*u + (1-r)*u' in LSeg(v,w) by A1,A10,A11;
  end;
end;

theorem
   for X being non empty RealLinearSpace, M being Subset of X
    holds M is convex iff
      for u,w being Point of X st u in M & w in M holds LSeg(u,w) c= M
proof
  let X be non empty RealLinearSpace, M be Subset of X;
  hereby assume
A1:  M is convex;
   let u,w be Point of X such that
A2:  u in M and
A3:  w in M;
A4:  LSeg(u,w) = { (1-s)*u + s*w: 0 <= s & s <= 1 } by Def2;
   now let x be set;
    assume x in LSeg(u,w);
    then ex r st x = (1-r)*u + r*w &  0 <= r & r <= 1 by A4;
    hence x in M by A1,A2,A3,Def1;
   end;
   hence LSeg(u,w) c= M by TARSKI:def 3;
  end;
  assume
A5:  for w,u being Point of X st w in M & u in M holds LSeg(w,u) c= M;
  let u,w be Point of X, r be Real such that
A6:  0 <= r & r <= 1 and
A7:  u in M and
A8:  w in M;
  LSeg(w,u) = {(1-s)*w + s*u: 0 <= s & s <= 1 } by Def2;
  then
A9:  r*u + (1-r)*w in LSeg(w,u) by A6;
  LSeg(w,u) c= M by A5,A7,A8;
  hence r*u + (1-r)*w in M by A9;
end;

definition
 let V be non empty RLSStruct, P be Subset-Family of V;
  attr P is convex-membered means
:Def3:
   for M being Subset of V st M in P holds M is convex;
end;

registration  let V be non empty RLSStruct;
 cluster non empty convex-membered Subset-Family of V;
  existence
 proof
  reconsider F = {{}V} as Subset-Family of V by MEASURE1:6;
  take F;
  thus F is non empty;
  let M be Subset of V;
  assume M in F;
  then M = {}V by TARSKI:def 1;
  hence M is convex by CONVEX1:10;
 end;
end;

theorem
 for V being non empty RLSStruct, F being convex-membered Subset-Family of V
  holds meet F is convex
 proof
  let V be non empty RLSStruct, F be convex-membered Subset-Family of V;
  for M being Subset of V st M in F holds M is convex by Def3;
  hence thesis by CONVEX1:15;
 end;

definition let X be non empty RLSStruct, A be Subset of X;
  func -A -> Subset of X equals
:Def4:  (-1)*A;
   coherence;
end;

theorem Th25:
   for X being non empty RealLinearSpace, M,N being Subset of X,
       v being Point of X
     holds v+M meets N  iff v in N+(-M)
 proof let X be non empty RealLinearSpace, M,N being Subset of X,
    v being Point of X;
   hereby assume v+M meets N;
     then consider z being set such that
A1:    z in v+M and
A2:    z in N by XBOOLE_0:3;
      v+M = {v + u where u is Point of X: u in M} by RUSUB_4:def 9;
      then consider u being Point of X such that
A3:    v+u = z and
A4:    u in M by A1;
     reconsider z as Point of X by A1;
A5:     z + (-1)*u  = v + (u + (-1)*u) by A3,RLVECT_1:def 6
               .= v + (u + -u) by RLVECT_1:29
               .= v+ 0.X by RLVECT_1:16
               .= v by RLVECT_1:10;
      (-1)*u in (-1)*M by A4,Lm5;
     then v in N+(-1)*M by A2,A5,Lm2;
     hence v in N+-M by Def4;
   end;
A6: N+(-1)*M = {u + w where u,w is Point of X: u in N & w in (-1)*M}
   by RUSUB_4:def 10;
A7:  (-1)*M = {(-1) * u where u is Point of X: u in M} by CONVEX1:def 1;
  assume v in N+(-M);
  then v in N+(-1)*M by Def4;
  then consider u,w being Point of X such that
A8: v = u+w and
A9: u in N and
A10: w in (-1)*M by A6;
  consider w' being Point of X such that
A11:  w = (-1)*w' and
A12:  w' in M by A7,A10;
A13:  (-1)*w = (-1)*(-1)*w' by A11,RLVECT_1:def 9
           .= 1*w'
           .= w' by RLVECT_1:def 9;
  v+w' = u + (w+w') by A8,RLVECT_1:def 6
      .= u + (w+-w) by A13,RLVECT_1:29
      .= u + 0.X by RLVECT_1:16
      .= u by RLVECT_1:10;
  then u in v+M by A12,Lm1;
  hence v+M meets N by A9,XBOOLE_0:3;
 end;

definition let X be non empty RLSStruct, A be Subset of X;
  attr A is symmetric means :Def5:
  A = -A;
end;

registration let X be non empty RealLinearSpace;
  cluster non empty symmetric Subset of X;
   existence
  proof
   take V = {0.X};
   thus V is non empty;
   thus V = (-1)*V by Th13
         .= -V by Def4;
  end;
end;

theorem Th26:
 for X being non empty RealLinearSpace, A being symmetric Subset of X,
     x being Point of X
   st x in A
  holds -x in A
 proof let X be non empty RealLinearSpace, A be symmetric Subset of X,
           x be Point of X such that
A1:   x in A;
A2:   A = -A by Def5
       .= (-1)*A by Def4;
 (-1)*A = {(-1)*v where v is Point of X: v in A} by CONVEX1:def 1;
  then consider v being Point of X such that
A3:   x = (-1)*v and
A4:   v in A by A1,A2;
  (-1)*x = (-1)*(-1)*v by A3,RLVECT_1:def 9
        .= 1*v
        .= v by RLVECT_1:def 9;
  hence thesis by A4,RLVECT_1:29;
 end;

definition let X be non empty RLSStruct, A be Subset of X;
  attr A is circled means :Def6:
  for r being Real st abs(r) <= 1 holds r*A c= A;
end;

Lm7:
  for X being non empty RealLinearSpace
   holds {}X is circled
 proof let X be non empty RealLinearSpace;
  let r be Real such that abs(r) <= 1;
  r*{}X = {} by CONVEX1:33;
  hence thesis;
 end;

registration let X be non empty RealLinearSpace;
  cluster {}X -> circled;
   coherence by Lm7;
end;

theorem Th27:
  for X being non empty RealLinearSpace
   holds {0.X} is circled
 proof let X be non empty RealLinearSpace;
  let r be Real such that abs(r) <= 1;
  r*{0.X} = {0.X} by Th13;
  hence thesis;
 end;

registration let X be non empty RealLinearSpace;
  cluster non empty circled Subset of X;
   existence
 proof
  take {0.X};
  thus thesis by Th27;
 end;
end;

theorem Th28:
  for X being non empty RealLinearSpace, B being non empty circled Subset of X
   holds 0.X in B
proof let X be non empty RealLinearSpace;
  let B be non empty circled Subset of X;
  abs(0) = 0 by ABSVALUE:7;
  then abs(0) < 1;
  then 0*B c= B by Def6;
  then
A1:  {0.X} c= B by CONVEX1:34;
  0.X in {0.X} by TARSKI:def 1;
  hence thesis by A1;
end;

Lm8:
  for X being non empty RealLinearSpace,A,B being circled Subset of X
   holds A+B is circled
 proof let X be non empty RealLinearSpace,A,B be circled Subset of X;
  let r be Real such that
A1:  abs(r) <= 1;
A2:  r*(A+B) = {r * v where v is Point of X: v in A+B} by CONVEX1:def 1;
A3:  A+B = {u + v where u,v is Point of X: u in A & v in B} by RUSUB_4:def 10;
   let x be set;
  assume x in r*(A+B);
  then consider x' being Point of X such that
A4:  x = r*x' and
A5:  x' in A+B by A2;
  consider u,v being Point of X such that
A6:  x' = u+v and
A7:  u in A and
A8: v in B by A3,A5;
A9:  r*A c= A & r*B c= B by A1,Def6;
   r*u in r*A & r*v in r*B by A7,A8,Lm5;
   then
A10:  r*u in A & r*v in B by A9;
  x = r*u+r*v by A4,A6,RLVECT_1:def 9;
  hence x in A+B by A10,Lm2;
 end;

registration
  let X be non empty RealLinearSpace,A,B be circled Subset of X;
  cluster A+B -> circled;
   coherence by Lm8;
end;

theorem Th29:
  for X being non empty RealLinearSpace, A being circled Subset of X
    for r being Real st abs(r) = 1 holds r*A = A
 proof let X be non empty RealLinearSpace, A be circled Subset of X;
  let r be Real;
  assume
A1: abs(r) = 1;
  hence
A2:  r*A c= A by Def6;
  let x be set;
  assume
A3:  x in A;
  then reconsider x as Point of X;
  r*x in r*A by A3,Lm5;
  then
A4:  r*x in A by A2;
  per cases;
  suppose 0 <= r;
   then r = 1 by A1,ABSVALUE:def 1;
   then r*A = A by CONVEX1:32;
   hence thesis by A3;
  end;
  suppose  r < 0;
   then abs(r) = -r by ABSVALUE:def 1;
   then r = -1 by A1;
   then (-1)*((-1)*x) in r*A by A4,Lm5;
   then (-1)*(-1)*x in r*A by RLVECT_1:def 9;
   then 1*x in r*A;
   hence thesis by RLVECT_1:def 9;
  end;
 end;

Lm9:
  for X being non empty RealLinearSpace, A being circled Subset of X
    holds A is symmetric
 proof let X be non empty RealLinearSpace, A be circled Subset of X;
  abs(-1) = --1 by ABSVALUE:def 1
         .= 1;
  hence A = (-1)*A by Th29
         .= -A by Def4;
 end;

registration let X be non empty RealLinearSpace;
  cluster circled -> symmetric Subset of X;
  coherence by Lm9;
end;

Lm10:
  for X being non empty RealLinearSpace, M being circled Subset of X
   holds conv(M) is circled
proof
  let X be non empty RealLinearSpace, M be circled Subset of X;
  let r be Real;
  assume abs(r) <= 1;
  then
A1:   r*M c= M by Def6;
  r*conv(M) = conv(r*M) by Th19;
  hence r*conv(M) c= conv(M) by A1,Th21;
end;

registration let X be non empty RealLinearSpace, M be circled Subset of X;
  cluster conv(M) -> circled;
 coherence by Lm10;
end;

definition let X be non empty RLSStruct, F be Subset-Family of X;
  attr F is circled-membered means
:Def7: for V being Subset of X st V in F holds V is circled;
end;

registration  let V be non empty RealLinearSpace;
 cluster non empty circled-membered Subset-Family of V;
  existence
 proof
  reconsider F = {{}V} as Subset-Family of V by MEASURE1:6;
  take F;
  thus F is non empty;
  let M be Subset of V;
  assume M in F;
  then M = {}V by TARSKI:def 1;
  hence M is circled;
 end;
end;

theorem
  for X being non empty RealLinearSpace,
      F being circled-membered Subset-Family of X
  holds union F is circled
 proof
  let X be non empty RealLinearSpace,F be circled-membered Subset-Family of X;
  let r be Real such that
A1:  abs(r) <= 1;
  let x be set;
A2:  r*(union F) = {r*v where v is Point of X: v in union F} by CONVEX1:def 1;
  assume x in r*(union F);
  then consider x' being Point of X such that
A3:  x = r*x' and
A4:  x' in union F by A2;
  consider Y being set such that
A5: x' in Y and
A6: Y in F by A4,TARSKI:def 4;
  Y is Subset of the carrier of X by A6;
  then reconsider Y as Subset of X;
  Y is circled by A6,Def7;
  then
A7:  r*Y c= Y by A1,Def6;
  r*x' in r*Y by A5,Lm5;
  then x in Y by A3,A7;
  hence x in union F by A6,TARSKI:def 4;
 end;

theorem
  for X being non empty RealLinearSpace,
      F being circled-membered Subset-Family of X
  holds meet F is circled
 proof
  let X be non empty RealLinearSpace,F be circled-membered Subset-Family of X;
  let r be Real such that
A1:  abs(r) <= 1;
  let x be set;
A2:  r*(meet F) = {r*v where v is Point of X: v in meet F} by CONVEX1:def 1;
  assume x in r*(meet F);
  then consider x' being Point of X such that
A3:  x = r*x' and
A4:  x' in meet F by A2;
A5: F <> {} by A4,SETFAM_1:def 1;
  now let Y be set;
   assume
A6:  Y in F;
   then
A7:  x' in Y by A4,SETFAM_1:def 1;
   Y is Subset of the carrier of X by A6;
   then reconsider Y' = Y as Subset of X;
   Y' is circled by A6,Def7;
   then
A8:  r*Y' c= Y' by A1,Def6;
    r*x' in r*Y' by A7,Lm5;
   hence x in Y by A3,A8;
  end;
  hence x in meet F by A5,SETFAM_1:def 1;
 end;

begin

definition
  struct(RLSStruct,TopStruct) RLTopStruct (# carrier -> set,
          Zero -> Element of the carrier,
          add -> BinOp of the carrier,
          Mult -> Function of [:REAL, the carrier:],the carrier,
          topology -> Subset-Family of the carrier
         #);
end;

registration let X be non empty set, O be Element of X,
      F be BinOp of X, G be Function of [:REAL,X:],X,
      T be Subset-Family of X;
 cluster RLTopStruct (# X,O,F,G,T #) -> non empty;
 coherence by STRUCT_0:def 1;
end;

registration
  cluster strict non empty RLTopStruct;
 existence
  proof
   set X = {0};
   reconsider O = 0 as Element of X by TARSKI:def 1;
   consider F being BinOp of X;
   consider G being Function of [:REAL,X:],X;
   consider T being Subset-Family of X;
   take RT = RLTopStruct (# X,O,F,G,T #);
   thus RT is strict;
   thus the carrier of RT is non empty;
  end;
end;

definition let X be non empty RLTopStruct;
  attr X is add-continuous means :Def8:
  for x1,x2 being Point of X, V being Subset of X st V is open & x1+x2 in V
   ex V1,V2 being Subset of X
     st V1 is open & V2 is open & x1 in V1 & x2 in V2 & V1+V2 c= V;
  attr X is Mult-continuous means :Def9:
  for a being Real, x being Point of X, V being Subset of X
    st V is open & a*x in V
   ex r being positive Real, W being Subset of X
      st W is open & x in W &
        for s being Real st abs(s - a) < r holds s*W c= V;
end;

registration
 cluster non empty strict add-continuous Mult-continuous
    TopSpace-like Abelian add-associative right_zeroed
    right_complementable RealLinearSpace-like (non empty RLTopStruct);
  existence
 proof
   set ZS = {0};
   reconsider O = 0 as Element of ZS by TARSKI:def 1;
   deffunc A((Element of ZS), Element of ZS) = O;
   consider F being BinOp of ZS such that
A1: for x,y being Element of ZS holds F.(x,y) = A(x,y) from BINOP_1:sch 2;
   deffunc M((Element of REAL), Element of ZS) = O;
   consider G being Function of [:REAL,ZS:],ZS such that
A2: for a being Element of REAL for x being Element of ZS
         holds G.[a,x qua set] = M(a,x) from FUNCT_2:sch 8;
   set T = {{},ZS};
  T c= bool ZS
   proof
    let x be set;
    assume x in T;
    then x = {} or x = ZS by TARSKI:def 2;
    then x c= ZS by XBOOLE_1:2;
    hence thesis;
   end;
  then reconsider T as Subset-Family of ZS by SETFAM_1:def 7;
  take W = RLTopStruct (# ZS,O,F,G,T #);
  thus W is non empty strict;
  thus W is add-continuous
   proof
    let x1,x2 be Point of W, V be Subset of W such that
A3:   V is open and
A4:   x1+x2 in V;
    {O} c= the carrier of W;
    then reconsider V1 = {O}, V2 = {O} as Subset of W;
    take V1,V2;
    V1 in T & V2 in T by TARSKI:def 2;
    hence V1 is open & V2 is open by PRE_TOPC:def 5;
    thus x1 in V1 & x2 in V2;
    V in T by A3,PRE_TOPC:def 5;
    then V = {} or V = ZS by TARSKI:def 2;
    then V = the carrier of W by A4;
    hence V1+V2 c= V;
   end;
  thus W is Mult-continuous
   proof
    let a be Real, x be Point of W, V be Subset of W such that
 A5:  V is open and
 A6:  a*x in V;
    consider r being positive Real;
    {O} c= the carrier of W;
    then reconsider V' = {O} as Subset of W;
    take r,V';
    V' in T by TARSKI:def 2;
    hence V' is open by PRE_TOPC:def 5;
    thus x in V';
    let s be Real such that abs(s - a) < r;
    V in T by A5,PRE_TOPC:def 5;
    then V = {} or V = ZS by TARSKI:def 2;
    then V = the carrier of W by A6;
    hence s*V' c= V;
   end;
  thus W is TopSpace-like
   proof
    thus the carrier of W in the topology of W by TARSKI:def 2;
    hereby
     let a be Subset-Family of the carrier of W;
     assume a c= the topology of W;
      then a={} or a={{}} or a={ZS} or a={{},ZS} by ZFMISC_1:42;
      then union a={} or union a=ZS or union a = {} \/ ZS by ZFMISC_1:2,31,93;
     hence union a in the topology of W by TARSKI:def 2;
    end;
    let a,b be Subset of the carrier of W such that
A7:  a in the topology of W and
A8:  b in the topology of W;
    (a={} or a=ZS) & (b={} or b=ZS) by A7,A8,TARSKI:def 2;
    hence a /\ b in the topology of W by TARSKI:def 2;
   end;
  thus for x,y being Point of W holds x + y = y + x
   proof let x,y be Point of W;
    x + y = F.[x,y] & y + x = F.[y,x] by RLVECT_1:def 3;
    then
A9:   x + y = F.(x,y) & y + x = F.(y,x) by BINOP_1:def 1;
    reconsider X = x, Y = y as Element of ZS;
     x + y = A(X,Y) & y + x = A(Y,X) by A1,A9;
    hence thesis;
   end;
  thus for x,y,z being Point of W holds (x + y) + z = x + (y + z)
   proof let x,y,z be Point of W;
     (x + y) + z = F.[x + y,z] & x + (y + z) = F.[x,y + z] by RLVECT_1:def 3;
    then
A10:  (x + y) + z = F.(x + y,z) & x + (y + z) = F.(x,y + z) by BINOP_1:def 1;
    reconsider X = x, Y = y, Z = z as Element of ZS;
     (x + y) + z = A(A(X,Y),Z) & x + (y + z) = A(X,A(Y,Z)) by A1,A10;
    hence thesis;
   end;
  thus for x being Point of W holds x + 0.W = x
   proof let x be Point of W;
     reconsider X = x as Element of ZS;
      x + 0.W = F.[x,0.W] by RLVECT_1:def 3
             .= F.(x,0.W) by BINOP_1:def 1
             .= A(X,O) by A1;
    hence thesis by TARSKI:def 1;
   end;
  thus for x being Point of W ex y being Point of W st x + y = 0.W
   proof let x be Point of W;
     reconsider y = O as Point of W;
    take y;
    thus x + y = F.[x,y] by RLVECT_1:def 3
              .= F.(x,y) by BINOP_1:def 1
              .= the Zero of W by A1
              .= 0.W by RLVECT_1:def 2;
   end;
  thus for a being Real, x,y being Point of W
        holds a * (x + y) = a * x + a * y
   proof let a; let x,y be Point of W;
     reconsider X = x, Y = y as Element of ZS;
A11:    a * (x + y) = G.[a,x + y] by RLVECT_1:def 4;
      a * x + a * y = F.[a * x,a * y] by RLVECT_1:def 3
                   .= F.(a * x,a * y) by BINOP_1:def 1
                   .= A(M(a,X),M(a,Y)) by A1;
    hence thesis by A2,A11;
   end;
  thus for a,b being Real for x being Point of W
           holds (a + b) * x = a * x + b * x
   proof let a,b be Real, x be Point of W;
      set c = a + b;
     reconsider X = x as Element of ZS;
A12:   c * x = G.[c,x] by RLVECT_1:def 4
           .= M(c,X) by A2;
      a * x + b * x = F.[a * x,b * x] by RLVECT_1:def 3
                   .= F.(a * x,b * x) by BINOP_1:def 1
                   .= A(M(a,X),M(b,X)) by A1;
    hence thesis by A12;
   end;
  thus for a,b being Real, x being Point of W
         holds (a * b) * x = a * (b * x)
    proof let a,b be Real, x be Point of W;
       set c = a * b;
      reconsider X = x as Element of ZS;
A13:    c * x = G.[c,x] by RLVECT_1:def 4
            .= M(c,X) by A2;
       a * (b * x) = G.[a,b * x] by RLVECT_1:def 4
                  .= M(a,M(b,X)) by A2;
     hence thesis by A13;
    end;
  thus for x being Point of W holds 1 * x = x
   proof let x be Point of W;
     reconsider X = x as Element of ZS;
     reconsider A' = 1 as Element of REAL;
         1 * x = G.[1,x] by RLVECT_1:def 4
              .= M(A',X) by A2;
    hence thesis by TARSKI:def 1;
   end;
 end;
end;

definition
  mode LinearTopSpace is add-continuous Mult-continuous
   TopSpace-like Abelian add-associative right_zeroed
    right_complementable RealLinearSpace-like (non empty RLTopStruct);
end;

theorem Th32:
 for X being non empty LinearTopSpace,
     x1,x2 being Point of X, V being a_neighborhood of x1+x2
   ex V1 being a_neighborhood of x1, V2 being a_neighborhood of x2
    st V1+V2 c= V
 proof let X be non empty LinearTopSpace;
  let x1,x2 be Point of X, V be a_neighborhood of x1+x2;
  x1+x2 in Int V by CONNSP_2:def 1;
  then consider V1,V2 being Subset of X such that
A1: V1 is open and
A2: V2 is open and
A3: x1 in V1 and
A4: x2 in V2 and
A5: V1+V2 c= Int V by Def8;
  Int V1 = V1 by A1,TOPS_1:55;
  then reconsider V1 as a_neighborhood of x1 by A3,CONNSP_2:def 1;
  Int V2 = V2 by A2,TOPS_1:55;
  then reconsider V2 as a_neighborhood of x2 by A4,CONNSP_2:def 1;
  take V1,V2;
  Int V c= V by TOPS_1:44;
  hence V1+V2 c= V by A5,XBOOLE_1:1;
 end;

theorem Th33:
 for X being non empty LinearTopSpace, a being Real, x being Point of X,
     V being a_neighborhood of a*x
   ex r being positive Real, W being a_neighborhood of x
      st for s being Real st abs(s - a) < r holds s*W c= V
 proof
   let X be non empty LinearTopSpace, a be Real, x be Point of X,
       V be a_neighborhood of a*x;
   a*x in Int(V) by CONNSP_2:def 1;
   then consider r being positive Real, W being Subset of X such that
A1:    W is open and
A2:    x in W and
A3:    for s being Real st abs(s - a) < r holds s*W c= Int(V) by Def9;
   take r;
   Int(W) = W by A1,TOPS_1:55;
   then reconsider W as a_neighborhood of x by A2,CONNSP_2:def 1;
   take W;
   let s be Real;
   assume abs(s - a) < r;
   then
A4:   s*W c= Int(V) by A3;
   Int(V) c= V by TOPS_1:44;
   hence thesis by A4,XBOOLE_1:1;
 end;

definition let X be non empty RLTopStruct, a be Point of X;
 func transl(a,X) -> map of X,X means
:Def10:
  for x being Point of X holds it.x = a+x;
   existence
  proof
    deffunc F(Point of X) = a+$1;
    consider F be Function of the carrier of X, the carrier of X such that
 A1: for x being Point of X  holds F.x = F(x) from FUNCT_2:sch 4;
    reconsider F as map of X,X;
    take F;
    thus thesis by A1;
  end;
   uniqueness
  proof
   let F,G be map of X,X such that
 A2: for x being Point of X holds F.x = a+x and
 A3: for x being Point of X holds G.x = a+x;
   now let x be Point of X;
    thus F.x = a+x by A2
            .= G.x by A3;
   end;
   hence F = G by FUNCT_2:113;
  end;
end;

theorem Th34:
  for X being non empty RLTopStruct, a being Point of X, V being Subset of X
   holds transl(a,X).:V = a+V
proof
  let X be non empty RLTopStruct, a be Point of X, V be Subset of X;
  thus transl(a,X).:V c= a+V
   proof
    let y be set;
    assume y in transl(a,X).:V;
     then consider c being Point of X such that
A1:     c in V and
A2:     y = transl(a,X).c by FUNCT_2:116;
    y = a+c by A2,Def10;
    hence y in a+V by A1,Lm1;
   end;
  let x be set;
  assume x in a+V;
  then x in {a + u where u is Point of X: u in V} by RUSUB_4:def 9;
  then consider u being Point of X such that
A3:  x = a+u and
A4:  u in V;
  x = transl(a,X).u by A3,Def10;
  hence x in transl(a,X).:V by A4,FUNCT_2:43;
end;

theorem Th35:
 for  X being non empty LinearTopSpace, a being Point of X
  holds rng transl(a,X) = [#]X
 proof let  X be non empty LinearTopSpace, a be Point of X;
   rng transl(a,X) c= the carrier of X by RELSET_1:12;
   hence rng transl(a,X) c= [#]X by PRE_TOPC:def 3;
   let y be set;
   assume y in [#]X;
   then y in the carrier of X;
   then reconsider y as Point of X;
   transl(a,X).(-a+y) = a+(-a+y) by Def10
                     .= a+-a+y by RLVECT_1:def 6
                     .= 0.X+y by RLVECT_1:16
                     .= y by RLVECT_1:10;
   hence thesis by FUNCT_2:6;
 end;

Lm11:
 for  X being non empty LinearTopSpace, a being Point of X
  holds transl(a,X) is one-to-one
 proof let  X be non empty LinearTopSpace, a be Point of X;
    now
     let x1,x2 be set such that
A1:   x1 in the carrier of X and
A2:   x2 in the carrier of X and
A3:   transl(a,X).x1 = transl(a,X).x2;
     reconsider x1' = x1, x2' = x2 as Point of X by A1,A2;
     transl(a,X).x1 = a+x1' & transl(a,X).x2 = a+x2' by Def10;
     hence x1 = x2 by A3,RLVECT_1:21;
   end;
   hence transl(a,X) is one-to-one by FUNCT_2:25;
 end;

theorem Th36:
 for  X being non empty LinearTopSpace, a being Point of X
  holds transl(a,X)" = transl(-a,X)
 proof let  X be non empty LinearTopSpace, a be Point of X;
A1: rng transl(a,X) = [#]X by Th35;
A2: [#]X = the carrier of X by PRE_TOPC:def 3;
A3: transl(a,X) is one-to-one by Lm11;
   now let x be Point of X;
    consider u being set such that
A4:  u in dom transl(a,X) and
A5:  x = transl(a,X).u by A1,A2,FUNCT_1:def 5;
    dom transl(a,X) = the carrier of X by FUNCT_2:def 1;
    then reconsider u as Point of X by A4;
A6:    x = a+u by A5,Def10;
    thus transl(a,X)".x
             = (transl(a,X) qua Function)".x by A1,A3,TOPS_2:def 4
            .= u by A3,A5,FUNCT_2:32
            .= 0.X+u by RLVECT_1:10
            .= (-a+a)+u by RLVECT_1:16
            .= -a+x by A6,RLVECT_1:def 6
            .= transl(-a,X).x by Def10;
   end;
   hence thesis by FUNCT_2:113;
 end;

Lm12:
 for X being non empty LinearTopSpace, a being Point of X
  holds transl(a,X) is continuous
 proof let X be non empty LinearTopSpace, a being Point of X;
   now let P be Subset of X;
    assume
A1:   P is open;
    per cases;
    suppose P = {};
     then transl(a,X)"P = {} by RELAT_1:171
                       .= {}X;
     hence transl(a,X)"P is open;
     end;
    suppose P <> {};
     reconsider X' = X as TopStruct;
     defpred P[Subset of the carrier of X'] means
        ex D being Subset of X st D = $1 & D is open & a+D c= P;
     consider F being Subset-Family of X' such that
A2:    for A being Subset of the carrier of X' holds A in F iff P[A]
        from PRE_TOPC:sch 1;
     reconsider F as Subset-Family of X;
A3:    F is open
     proof
      let A being Subset of X;
      assume A in F;
      then ex D being Subset of X st D=A & D is open & a+D c= P by A2;
      hence A is open;
     end;
     union F = transl(a,X)"P
      proof
        thus union F c= transl(a,X)"P
         proof
          let x be set;
          assume x in union F;
          then consider A being set such that
A4:        x in A and
A5:        A in F by TARSKI:def 4;
          consider D being Subset of X such that
A6:         D = A and D is open and
A7:         a+D c= P by A2,A5;
           A is Subset of the carrier of X by A5;
           then reconsider x as Point of X by A4;
           a+D = {a + u where u is Point of X: u in D} by RUSUB_4:def 9;
           then a+x in a+D by A4,A6;
           then
A8:         a+x in P by A7;
           transl(a,X).x = a+x by Def10;
          hence thesis by A8,FUNCT_2:46;
         end;
        let x be set;
        assume
A9:       x in transl(a,X)"P;
        then reconsider x as Point of X;
        transl(a,X).x in P by A9,FUNCT_2:46;
        then a+x in P by Def10;
        then consider V,D being Subset of X such that V is open and
A10:       D is open and
A11:       a in V and
A12:       x in D and
A13:       V+D c= P by A1,Def8;
A14:      {a}+D = a+D by RUSUB_4:36;
         {a} c= V by A11,ZFMISC_1:37;
         then a+D c= V+D by A14,Th10;
         then a+D c= P by A13,XBOOLE_1:1;
        then D in F by A2,A10;
        hence thesis by A12,TARSKI:def 4;
      end;
      hence transl(a,X)"P is open by A3,TOPS_2:26;
     end;
   end;
   hence thesis by TOPS_2:55;
 end;

registration let X be non empty LinearTopSpace, a being Point of X;
 cluster transl(a,X) -> being_homeomorphism;
   coherence
  proof
A1:  dom transl(a,X) = the carrier of X by FUNCT_2:def 1;
    thus dom transl(a,X) = [#]X by A1,PRE_TOPC:def 3;
   thus rng transl(a,X) = [#]X by Th35;
   thus transl(a,X) is one-to-one by Lm11;
   thus transl(a,X) is continuous by Lm12;
   transl(a,X)" = transl(-a,X) by Th36;
   hence transl(a,X)" is continuous by Lm12;
  end;
end;

Lm13:
  for X being non empty LinearTopSpace, E being Subset of X,
      x being Point of X
    st E is open
   holds x+E is open
 proof
  let X be non empty LinearTopSpace, E be Subset of X, x be Point of X;
  assume
A1:  E is open;
   transl(x,X).:E = x+E by Th34;
  hence x+E is open by A1,TOPGRP_1:25;
 end;

registration
  let X be non empty LinearTopSpace, E be open Subset of X, x be Point of X;
  cluster x+E -> open;
   coherence by Lm13;
end;

registration
 let X be non empty LinearTopSpace, E be open Subset of X, x be Point of X;
  cluster x+E -> open;
   coherence;
end;

Lm14:
  for X being non empty LinearTopSpace, E being open Subset of X,
      K being Subset of X
   holds K+E is open
 proof let X be non empty LinearTopSpace, E be open Subset of X,
           K be Subset of X;
  reconsider F = { a+E where a is Point of X: a in K} as Subset-Family of X
 by Lm3;
A1: F is open
   proof
    let P being Subset of X;
    assume P in F;
    then ex a being Point of X st P=a+E & a in K;
    hence P is open;
   end;
  K+E = union F by Th4;
  hence thesis by A1,TOPS_2:26;
 end;

registration
 let X be non empty LinearTopSpace, E be open Subset of X, K be Subset of X;
  cluster K+E -> open;
   coherence by Lm14;
end;

Lm15:
  for X being non empty LinearTopSpace, D being closed Subset of X,
      x being Point of X
   holds x+D is closed
 proof
  let X be non empty LinearTopSpace, D be closed Subset of X, x be Point of X;
   transl(x,X).:D = x+D by Th34;
  hence x+D is closed by TOPS_2:72;
 end;

registration
  let X be non empty LinearTopSpace, D be closed Subset of X, x be Point of X;
  cluster x+D -> closed;
   coherence by Lm15;
end;

theorem Th37:
  for X being non empty LinearTopSpace, V1,V2,V being Subset of X
   st V1+V2 c= V
  holds Int V1 + Int V2 c= Int V
 proof
  let X be non empty LinearTopSpace,  V1,V2,V be Subset of X
   such that
A1:  V1+V2 c= V;
  Int V1 c= V1 & Int V2 c= V2 by TOPS_1:44;
  then Int V1 + Int V2 c= V1+V2 by Th11;
  then Int V1 + Int V2 c= V by A1,XBOOLE_1:1;
  hence thesis by TOPS_1:56;
 end;

theorem Th38:
  for X being non empty LinearTopSpace, x being Point of X, V being Subset of X
   holds x+Int(V) = Int(x+V)
 proof let X be non empty LinearTopSpace, x be Point of X, V be Subset of X;
  Int(V) c= V by TOPS_1:44;
  then x+Int(V) c= x+V by Th8;
  hence x+Int(V) c= Int(x+V) by TOPS_1:56;
  let v be set;
  assume
A1:  v in Int(x+V);
  then reconsider v as Point of X;
  consider Q being Subset of X such that
A2:  Q is open and
A3:  Q c= x+V and
A4:  v in Q by A1,TOPS_1:54;
A5:  -x+v in -x+Q by A4,Lm1;
  -x+Q c= -x+(x+V) by A3,Th8;
  then -x+Q c= -x+x+V by Th6;
  then -x+Q c= 0.X+V by RLVECT_1:16;
  then
A6:  -x+Q c= V by Th5;
A7:  x+Int(V) = {x + u where u is Point of X: u in Int V} by RUSUB_4:def 9;
  -x+Q is open by A2,Lm13;
  then -x+Q c=Int(V) by A6,TOPS_1:56;
  then -x+v in Int(V) by A5;
  then x+(-x+v) in x+Int(V) by A7;
  then x+-x+v in x+Int(V) by RLVECT_1:def 6;
  then 0.X+v in x+Int(V) by RLVECT_1:16;
  hence thesis by RLVECT_1:10;
 end;

theorem
  for X being non empty LinearTopSpace, x being Point of X, V being Subset of X
   holds x+Cl(V) = Cl(x+V)
 proof let X be non empty LinearTopSpace, x be Point of X, V be Subset of X;
  thus x+Cl(V) c= Cl(x+V)
   proof let v be set;
     assume
A1:   v in x+Cl(V);
     then reconsider v as Point of X;
      now let G be Subset of X such that
A2:      G is open and
A3:      v in G;
A4:      -x+G = {-x+u where u is Point of X: u in G} by RUSUB_4:def 9;
A5:      x+V = {x + u where u is Point of X: u in V} by RUSUB_4:def 9;
        x+Cl(V) = {x + u where u is Point of X: u in Cl V} by RUSUB_4:def 9;
        then consider u being Point of X such that
A6:       v = x+u and
A7:       u in Cl V by A1;
A8:       -x+v in -x+G by A3,A4;
A9:       -x+G is open by A2,Lm13;
        -x+v = -x+x+u by A6,RLVECT_1:def 6
             .= 0.X+u by RLVECT_1:16
             .= u by RLVECT_1:10;
        then V meets -x+G by A7,A8,A9,PRE_TOPC:54;
        then consider z being set such that
A10:        z in V and
A11:        z in -x+G by XBOOLE_0:3;
        reconsider z as Point of X by A10;
        consider w being Point of X such that
A12:     z = -x+w and
A13:     w in G by A4,A11;
A14:     x+z in x+V by A5,A10;
        x+z = x+-x+w by A12,RLVECT_1:def 6
             .= 0.X+w by RLVECT_1:16
             .= w by RLVECT_1:10;
        hence x+V meets G by A13,A14,XBOOLE_0:3;
       end;
       hence thesis by PRE_TOPC:54;
     end;
  V c= Cl V by PRE_TOPC:48;
  then x+V c= x+Cl(V) by Th8;
  hence Cl(x+V) c= x+Cl(V) by TOPS_1:31;
 end;

theorem
  for X being non empty LinearTopSpace, x,v being Point of X,
      V being a_neighborhood of x
   holds v+V is a_neighborhood of v+x
 proof
  let X be non empty LinearTopSpace, x,v be Point of X,
      V be a_neighborhood of x;
A1:   v+Int(V) = {v + u where u is Point of X: u in Int V} by RUSUB_4:def 9;
A2:   v+Int(V) = Int(v+V) by Th38;
  x in Int V by CONNSP_2:def 1;
  then v+x in v+Int(V) by A1;
  hence v+V is a_neighborhood of v+x by A2,CONNSP_2:def 1;
 end;

theorem
  for X being non empty LinearTopSpace, x being Point of X,
      V being a_neighborhood of x
   holds -x+V is a_neighborhood of 0.X
 proof
  let X be non empty LinearTopSpace, x be Point of X,
      V be a_neighborhood of x;
A1:   -x+Int(V) = {-x + v where v is Point of X: v in Int V} by RUSUB_4:def 9;
A2:   -x+Int(V) c= Int(-x+V) by Th38;
    x in Int V by CONNSP_2:def 1;
  then -x+x in -x+Int(V) by A1;
  then 0.X in -x+Int(V) by RLVECT_1:16;
  then 0.X in Int(-x+V) by A2;
  hence -x+V is a_neighborhood of 0.X by CONNSP_2:def 1;
 end;

definition let X be non empty RLTopStruct;
  mode local_base of X is basis of 0.X;
end;

definition let X be non empty RLTopStruct;
  attr X is locally-convex means
:Def11:
  ex P being local_base of X st P is convex-membered;
end;

definition let X be non empty LinearTopSpace, E being Subset of X;
  attr E is bounded means
:Def12:
  for V being a_neighborhood of 0.X
   ex s st s > 0 & for t st t > s holds E c= t*V;
end;

registration let X be non empty LinearTopSpace;
 cluster {}X -> bounded;
  coherence
 proof
   let V be a_neighborhood of 0.X;
   take s = 1;
   thus thesis by XBOOLE_1:2;
 end;
end;

registration let X be non empty LinearTopSpace;
 cluster bounded Subset of X;
  existence
 proof
   take {}X;
   thus thesis;
 end;
end;

theorem Th42:
  for X being non empty LinearTopSpace, V1,V2 being bounded Subset of X
   holds V1 \/ V2 is bounded
 proof
   let X be non empty LinearTopSpace,  V1,V2 be bounded Subset of X;
     now per cases;
      suppose V2 = {};
       hence thesis;
      end;
      suppose V2 <> {};
       thus thesis
        proof
         let V be a_neighborhood of 0.X;
         consider s such that
A1:       s > 0 and
A2:       for t st t > s holds V1 c= t*V by Def12;
         consider r such that
A3:       r > 0 and
A4:       for t st t > r holds V2 c= t*V by Def12;
         per cases;
         suppose
A5:         r <= s;
          take s;
          thus s > 0 by A1;
          let t such that
A6:        t > s;
A7:        V1 c= t*V by A2,A6;
          t > r by A5,A6,AXIOMS:22;
          then V2 c= t*V by A4;
          hence V1 \/ V2 c= t*V by A7,XBOOLE_1:8;
         end;
         suppose
A8:         r > s;
          take r;
          thus r > 0 by A3;
          let t such that
A9:        t > r;
A10:        V2 c= t*V by A4,A9;
          t > s by A8,A9,AXIOMS:22;
          then V1 c= t*V by A2;
          hence V1 \/ V2 c= t*V by A10,XBOOLE_1:8;
         end;
        end;
      end;
     end;
     hence thesis;
 end;

theorem
  for X being non empty LinearTopSpace, P being bounded Subset of X,
      Q being Subset of X
    st Q c= P
   holds Q is bounded
 proof
   let X be non empty LinearTopSpace,  P be bounded Subset of X,
       Q being Subset of X such that
A1:  Q c= P;
   let V be a_neighborhood of 0.X;
   consider s such that
A2:  s > 0 and
A3:  for t st t > s holds P c= t*V by Def12;
   take s;
   thus s > 0 by A2;
   let t;
   assume t > s;
   then P c= t*V by A3;
   hence Q c= t*V by A1,XBOOLE_1:1;
 end;

theorem
  for X being non empty LinearTopSpace, F being Subset-Family of X
    st F is finite & F = {P where P is bounded Subset of X: not contradiction}
   holds union F is bounded
 proof
  let X be non empty LinearTopSpace, F be Subset-Family of X such that
A1: F is finite and
A2: F = {P where P is bounded Subset of X: not contradiction};
A3:  now
      let P be Subset of X;
      assume P in F;
      then ex A being bounded Subset of X st P=A by A2;
      hence P is bounded;
     end;
  defpred P[set] means
    ex A being Subset of X st A = union $1 & A is bounded;
  {}X = union {} & {}X is bounded by ZFMISC_1:2;
  then
A4: P[{}];
A5: for x,B being set st x in F & B c= F & P[B] holds P[B \/ {x}]
   proof
    let x,B be set such that
A6:   x in F and B c= F and
A7:   P[B];
    consider A being Subset of X such that
A8:   A = union B and
A9:   A is bounded by A7;
    reconsider x as Subset of X by A6;
A10:   union (B \/ {x}) = union B \/ union {x} by ZFMISC_1:96
                      .= union B \/ x by ZFMISC_1:31;
A11:  x is bounded by A3,A6;
    ex Y being Subset of X st Y = union (B \/ {x}) & Y is bounded
     proof
      take A \/ x;
      thus thesis by A8,A9,A10,A11,Th42;
     end;
    hence thesis;
   end;
   P[F] from FINSET_1:sch 2(A1,A4,A5);
  hence thesis;
 end;

theorem Th45:
  for X being non empty LinearTopSpace, P being Subset-Family of X
   st P = {U where U is a_neighborhood of 0.X: not contradiction}
  holds P is local_base of X
 proof
  let  X be non empty LinearTopSpace, P be Subset-Family of X such that
A1: P = {U where U is a_neighborhood of 0.X: not contradiction};
  let A be a_neighborhood of 0.X;
  take A;
   A in P by A1;
  hence thesis;
 end;

theorem
  for X being non empty LinearTopSpace, O being local_base of X,
      P being Subset-Family of X
   st P = {a+U where a is Point of X, U is Subset of X: U in O}
  holds P is basis of X
 proof
   let  X be non empty LinearTopSpace, O be basis of 0.X,
      P be Subset-Family of X such that
A1: P = {a+U where a is Point of X, U is Subset of X: U in O};
  let p be Point of X;
  let A be a_neighborhood of p;
A2:  p in Int(A) by CONNSP_2:def 1;
A3:  0.X in -p+Int(A) by A2,Th9;
    -p+Int(A) is a_neighborhood of 0.X by A3,CONNSP_2:5;
    then consider V being a_neighborhood of 0.X such that
A4:  V in O and
A5:  V c= -p+Int(A) by YELLOW13:def 2;
   take U = p+V;
A6:  p+Int(V) c= Int(U) by Th38;
    0.X in Int(V) by CONNSP_2:def 1;
    then p+0.X in p+Int(V) by Lm1;
    then p in p+Int(V) by RLVECT_1:10;
    then p in Int(U) by A6;
   hence U is a_neighborhood of p by CONNSP_2:def 1;
   thus U in P by A1,A4;
A7:  Int(A) c= A by TOPS_1:44;
    U c= p+(-p+Int(A)) by A5,Th8;
    then U c= p+-p+Int(A) by Th6;
    then U c= 0.X+Int(A) by RLVECT_1:16;
    then U c= Int(A) by Th5;
   hence U c= A by A7,XBOOLE_1:1;
 end;

definition let X be non empty RLTopStruct, r be Real;
 func mlt(r,X) -> map of X,X means
:Def13:
  for x being Point of X holds it.x = r*x;
   existence
  proof
    deffunc F(Point of X) = r*$1;
    consider F be Function of the carrier of X, the carrier of X such that
 A1: for x being Point of X  holds F.x = F(x) from FUNCT_2:sch 4;
    reconsider F as map of X,X;
    take F;
    thus thesis by A1;
  end;
   uniqueness
  proof
   let F,G be map of X,X such that
 A2: for x being Point of X holds F.x = r*x and
 A3: for x being Point of X holds G.x = r*x;
   now let x be Point of X;
    thus F.x = r*x by A2
            .= G.x by A3;
   end;
   hence F = G by FUNCT_2:113;
  end;
end;

theorem Th47:
  for X being non empty RLTopStruct, V being Subset of X, r being non zero Real
   holds mlt(r,X).:V = r*V
 proof
  let X be non empty RLTopStruct, V be Subset of X, r be non zero Real;
  thus mlt(r,X).:V c= r*V
   proof
    let y be set;
    assume y in  mlt(r,X).:V;
     then consider c being Point of X such that
A1:     c in V and
A2:     y = mlt(r,X).c by FUNCT_2:116;
    y = r*c by A2,Def13;
    hence y in r*V by A1,Lm5;
   end;
  let x be set;
  assume x in r*V;
  then x in {r*u where u is Point of X: u in V} by CONVEX1:def 1;
  then consider u being Point of X such that
A3:  x = r*u and
A4:  u in V;
  x = mlt(r,X).u by A3,Def13;
  hence x in  mlt(r,X).:V by A4,FUNCT_2:43;
 end;

theorem Th48:
 for X being non empty LinearTopSpace, r being non zero Real
  holds rng mlt(r,X) = [#]X
 proof let  X be non empty LinearTopSpace, r be non zero Real;
   rng mlt(r,X) c= the carrier of X by RELSET_1:12;
   hence rng mlt(r,X) c= [#]X by PRE_TOPC:def 3;
   let y be set;
   assume y in [#]X;
   then y in the carrier of X;
   then reconsider y as Point of X;
   mlt(r,X).(r"*y) = r*(r"*y) by Def13
                     .= r*r"*y by RLVECT_1:def 9
                     .= 1*y by XCMPLX_0:def 7
                     .= y by RLVECT_1:def 9;
   hence thesis by FUNCT_2:6;
 end;

Lm16:
 for X being non empty LinearTopSpace, r being non zero Real
  holds mlt(r,X) is one-to-one
 proof let  X be non empty LinearTopSpace, r be non zero Real;
    now
     let x1,x2 be set such that
A1:   x1 in the carrier of X and
A2:   x2 in the carrier of X and
A3:   mlt(r,X).x1 = mlt(r,X).x2;
     reconsider x1' = x1, x2' = x2 as Point of X by A1,A2;
     mlt(r,X).x1 = r*x1' & mlt(r,X).x2 = r*x2' by Def13;
     hence x1 = x2 by A3,RLVECT_1:50;
   end;
   hence mlt(r,X) is one-to-one by FUNCT_2:25;
 end;

theorem Th49:
 for X being non empty LinearTopSpace, r being non zero Real
  holds mlt(r,X)" = mlt(r",X)
 proof let  X be non empty LinearTopSpace, r be non zero Real;
A1: rng mlt(r,X) = [#]X by Th48;
A2: [#]X = the carrier of X by PRE_TOPC:def 3;
A3:  mlt(r,X) is one-to-one by Lm16;
   now let x be Point of X;
    consider u being set such that
A4:  u in dom mlt(r,X) and
A5:  x = mlt(r,X).u by A1,A2,FUNCT_1:def 5;
    dom mlt(r,X) = the carrier of X by FUNCT_2:def 1;
    then reconsider u as Point of X by A4;
A6:    x = r*u by A5,Def13;
    thus mlt(r,X)".x
             = (mlt(r,X) qua Function)".x by A1,A3,TOPS_2:def 4
            .= u by A3,A5,FUNCT_2:32
            .= 1*u by RLVECT_1:def 9
            .= (r*r")*u by XCMPLX_0:def 7
            .= r"*x by A6,RLVECT_1:def 9
            .= mlt(r",X).x by Def13;
   end;
   hence thesis by FUNCT_2:113;
 end;

Lm17:
 for X being non empty LinearTopSpace, r being non zero Real
   holds mlt(r,X) is continuous
 proof let  X be non empty LinearTopSpace, r be non zero Real;
   now let P be Subset of X;
    assume
A1:   P is open;
    per cases;
    suppose P = {};
     then mlt(r,X)"P = {} by RELAT_1:171
                       .= {}X;
     hence mlt(r,X)"P is open;
    end;
    suppose P <> {};
     defpred P[Subset of X] means $1 is open & r*$1 c= P;
     consider F being Subset-Family of X such that
A2:    for A being Subset of X holds A in F iff P[A]
        from PRE_TOPC:sch 1;
     reconsider F as Subset-Family of X;
A3:    F is open
     proof
      let A being Subset of X;
      assume A in F;
      hence A is open by A2;
     end;
     union F = mlt(r,X)"P
      proof
       thus union F c= mlt(r,X)"P
        proof
         let x be set;
         assume x in union F;
         then consider A being set such that
A4:        x in A and
A5:        A in F by TARSKI:def 4;
         reconsider A as Subset of X by A5;
A6:        r*A c= P by A2,A5;
          A is Subset of the carrier of X;
          then reconsider x as Point of X by A4;
          r*A = {r*u where u is Point of X: u in A} by CONVEX1:def 1;
          then r*x in r*A by A4;
          then
A7:         r*x in P by A6;
          mlt(r,X).x = r*x by Def13;
         hence thesis by A7,FUNCT_2:46;
        end;
       let x be set;
       assume
A8:       x in mlt(r,X)"P;
       then reconsider x as Point of X;
       mlt(r,X).x in P by A8,FUNCT_2:46;
       then r*x in P by Def13;
       then consider e being positive Real, W being Subset of X such that
A9:       W is open and
A10:       x in W and
A11:       for s being Real st abs(s - r) < e holds s*W c= P by A1,Def9;
       abs(r - r) < e by ABSVALUE:7;
       then r*W c= P by A11;
       then W in F by A2,A9;
       hence thesis by A10,TARSKI:def 4;
      end;
     hence mlt(r,X)"P is open by A3,TOPS_2:26;
    end;
   end;
  hence thesis by TOPS_2:55;
 end;

registration let X be non empty LinearTopSpace, r be non zero Real;
 cluster mlt(r,X) -> being_homeomorphism;
 coherence
  proof
A1:  dom mlt(r,X) = the carrier of X by FUNCT_2:def 1;
    thus dom mlt(r,X) = [#]X by A1,PRE_TOPC:def 3;
   thus rng mlt(r,X)  = [#]X by Th48;
   thus mlt(r,X) is one-to-one by Lm16;
   thus mlt(r,X) is continuous by Lm17;
    mlt(r,X)" = mlt(r",X) by Th49;
   hence mlt(r,X)" is continuous by Lm17;
  end;
end;

theorem Th50:
  for X being non empty LinearTopSpace, V being open Subset of X,
      r being non zero Real
   holds r*V is open
proof let X be non empty LinearTopSpace, V be open Subset of X,
           r be non zero Real;
   mlt(r,X).:V = r*V by Th47;
  hence thesis by TOPGRP_1:25;
end;

theorem Th51:
  for X being non empty LinearTopSpace, V being closed Subset of X,
      r being non zero Real
   holds r*V is closed
proof let X be non empty LinearTopSpace, V be closed Subset of X,
           r be non zero Real;
   mlt(r,X).:V = r*V by Th47;
  hence thesis by TOPS_2:72;
end;

theorem Th52:
  for X being non empty LinearTopSpace, V being Subset of X,
      r be non zero Real
   holds r*Int(V) = Int(r*V)
proof
   let X be non empty LinearTopSpace, V be Subset of X, r being non zero Real;
A1:  r*Int(V) is open by Th50;
  Int(V) c= V by TOPS_1:44;
  then r*Int(V) c= r*V by CONVEX1:39;
  hence r*Int(V) c= Int(r*V) by A1,TOPS_1:56;
  let x be set;
  assume
A2:  x in Int(r*V);
  then reconsider x as Point of X;
  consider Q being Subset of X such that
A3: Q is open and
A4: Q c= r*V and
A5: x in Q by A2,TOPS_1:54;
  r"*Q c= r"*(r*V) by A4,CONVEX1:39;
  then r"*Q c= r"*r*V by CONVEX1:37;
  then r"*Q c= 1*V by XCMPLX_0:def 7;
  then
A6:  r"*Q c= V by CONVEX1:32;
A7:  r"*x in r"*Q by A5,Lm5;
  r"*Q is open by A3,Th50;
  then r"*x in Int(V) by A6,A7,TOPS_1:54;
  then r*(r"*x) in r*Int(V) by Lm5;
  then r*r"*x in r*Int(V) by RLVECT_1:def 9;
  then 1*x in r*Int(V) by XCMPLX_0:def 7;
  then x in r*Int(V) by RLVECT_1:def 9;
  hence thesis;
end;

theorem Th53:
  for X being non empty LinearTopSpace, A being Subset of X,
      r being non zero Real
    holds r*Cl(A) = Cl(r*A)
 proof let X be non empty LinearTopSpace, A be Subset of X, r be non zero Real;
    thus r*Cl(A) c= Cl(r*A)
     proof let z be set;
       assume
A1:       z in r*Cl(A);
       then reconsider z as Point of X;
       now let G be Subset of X such that
A2:      G is open and
A3:      z in G;
        r*Cl(A) = {r*v where v is Point of X: v in Cl A} by CONVEX1:def 1;
        then consider v being Point of X such that
A4:      z = r*v and
A5:      v in Cl A by A1;
A6:      r"*G = {r"*u where u is Point of X: u in G} by CONVEX1:def 1;
        then
A7:       r"*z in r"*G by A3;
A8:       r"*G is open by A2,Th50;
        r"*z = r"*r*v by A4,RLVECT_1:def 9
             .= 1*v by XCMPLX_0:def 7
             .= v by RLVECT_1:def 9;
        then A meets r"*G by A5,A7,A8,PRE_TOPC:54;
        then consider x being set such that
A9:        x in A and
A10:        x in r"*G by XBOOLE_0:3;
        reconsider x as Point of X by A9;
        consider u being Point of X such that
A11:     x = r"*u and
A12:     u in G by A6,A10;
        r*A = {r*v' where v' is Point of X: v' in A} by CONVEX1:def 1;
        then
A13:     r*x in r*A by A9;
        r*x = r*r"*u by A11,RLVECT_1:def 9
             .= 1*u by XCMPLX_0:def 7
             .= u by RLVECT_1:def 9;
        hence r*A meets G by A12,A13,XBOOLE_0:3;
       end;
       hence thesis by PRE_TOPC:54;
     end;
A14:  r*Cl(A) is closed by Th51;
    A c= Cl(A) by PRE_TOPC:48;
    then r*A c= r*Cl(A) by CONVEX1:39;
    hence Cl(r*A) c= r*Cl(A) by A14,TOPS_1:31;
 end;

theorem
  for X being non empty LinearTopSpace,A being Subset of X
     st X is_T1
    holds 0*Cl(A) = Cl(0*A)
 proof let X be non empty LinearTopSpace, A be Subset of X such that
A1: X is_T1;
  per cases;
   suppose
A2:   A is empty;
    then
A3:   Cl A = {}X by PRE_TOPC:52;
    hence 0*Cl(A) = {}X by CONVEX1:33
                 .= Cl(0*A) by A2,A3,CONVEX1:33;
   end;
   suppose
A4:   A is non empty;
     A c= Cl A by PRE_TOPC:48;
     then Cl A is non empty by A4,XBOOLE_1:3;
     hence 0*Cl(A) = {0.X} by CONVEX1:34
                  .= Cl {0.X} by A1,FRECHET2:47
                  .= Cl(0*A) by A4,CONVEX1:34;
   end;
 end;

theorem Th55:
  for X being non empty LinearTopSpace, x being Point of X,
      V being a_neighborhood of x, r be non zero Real
   holds r*V is a_neighborhood of r*x
 proof
  let X be non empty LinearTopSpace, x be Point of X,
     V be a_neighborhood of x, r be non zero Real;
A1: r*Int(V) c= Int(r*V) by Th52;
  x in Int(V) by CONNSP_2:def 1;
  then r*x in r*Int(V) by Lm5;
  hence r*x in Int(r*V) by A1;
 end;

theorem Th56:
  for X being non empty LinearTopSpace, V being a_neighborhood of 0.X,
      r be non zero Real
   holds r*V is a_neighborhood of 0.X
 proof
  let X be non empty LinearTopSpace, V be a_neighborhood of 0.X,
      r be non zero Real;
  r*V is a_neighborhood of r*0.X by Th55;
  hence thesis by RLVECT_1:23;
 end;

Lm18:
  for X being non empty LinearTopSpace, V being bounded Subset of X,
      r being Real
   holds r*V is bounded
 proof
  let X be non empty LinearTopSpace,  V be bounded Subset of X;
  let r be Real;
  per cases;
  suppose
A1:    r = 0;
    per cases;
    suppose V is empty;
      then r*V = {}X by CONVEX1:33;
      hence thesis;
    end;
    suppose
A2:     V is non empty;
      let U being a_neighborhood of 0.X;
      take s = 1;
      thus s > 0;
      let t;
      assume
A3:      t > s;
A4:     r*V = {0.X} by A1,A2,CONVEX1:34;
     t > 0 by A3;
     then t*U is a_neighborhood of 0.X by Th56;
     then 0.X in t*U by CONNSP_2:6;
      hence r*V c= t*U by A4,ZFMISC_1:37;
    end;
  end;
  suppose
A5:  r <> 0;
   let U be a_neighborhood of 0.X;
   1/r <> 0 by A5,XCMPLX_1:50;
   then (1/r)*U is a_neighborhood of 0.X by Th56;
   then consider s such that
A6:  s > 0 and
A7:  for t st t > s holds V c= t*((1/r)*U) by Def12;
   take s;
   thus s > 0 by A6;
   let t;
   assume
A8:   t > s;
   let x be set;
   assume
A9:   x in r*V;
   t > s by A8;
   then V c= t*((1/r)*U) by A7;
   then r*V c= r*(t*((1/r)*U)) by CONVEX1:39;
   then r*V c= r*(t*(1/r)*U) by CONVEX1:37;
   then r*V c= r*((1/r)*(t*U)) by CONVEX1:37;
   then r*V c= r*(1/r)*(t*U) by CONVEX1:37;
   then r*V c= 1*(t*U) by A5,XCMPLX_1:88;
   then r*V c= t*U by CONVEX1:32;
   hence x in t*U by A9;
  end;
end;

registration
  let X be non empty LinearTopSpace, V be bounded Subset of X, r be Real;
  cluster r*V -> bounded;
  coherence by Lm18;
end;

theorem Th57:
  for X being non empty LinearTopSpace, W being a_neighborhood of 0.X
   ex U being open a_neighborhood of 0.X st U is symmetric & U+U c= W
 proof
  let X be non empty LinearTopSpace, W be a_neighborhood of 0.X;
A1:  0.X+0.X = 0.X by RLVECT_1:10;
  consider V1 being a_neighborhood of 0.X, V2 being a_neighborhood of 0.X
   such that
A2: V1+V2 c= W by A1,Th32;
  set U = Int(V1) /\ Int(V2) /\ ((-1)*Int(V1)) /\ ((-1)*Int(V2));
  (-1)*Int(V1) is open & (-1)*Int(V2) is open by Th50;
  then Int(V1) /\ Int(V2) is open & ((-1)*Int(V1)) /\ ((-1)*Int(V2)) is open
  by TOPS_1:38;
  then Int(V1) /\ Int(V2) /\ (((-1)*Int(V1)) /\ ((-1)*Int(V2))) is open
 by TOPS_1:38;
  then
A3:  U is open by XBOOLE_1:16;
   0.X in Int V1 & 0.X in Int V2 by CONNSP_2:def 1;
  then
A4:  0.X in Int(V1) /\ Int(V2) by XBOOLE_0:def 3;
   (-1)*V1 is a_neighborhood of 0.X by Th56;
  then 0.X in Int((-1)*V1) by CONNSP_2:def 1;
  then 0.X in (-1)*Int(V1) by Th52;
  then
A5:  0.X in Int(V1) /\ Int(V2) /\ ((-1)*Int(V1)) by A4,XBOOLE_0:def 3;
   (-1)*V2 is a_neighborhood of 0.X by Th56;
  then 0.X in Int((-1)*V2) by CONNSP_2:def 1;
  then 0.X in (-1)*Int(V2) by Th52;
  then 0.X in U by A5,XBOOLE_0:def 3;
  then 0.X in Int(U) by A3,TOPS_1:55;
  then reconsider U as open a_neighborhood of 0.X by A3,CONNSP_2:def 1;
  take U;
  (-1)*(-1)*Int(V1) = Int(V1) & (-1)*(-1)*Int(V2) = Int(V2) by CONVEX1:32;
  then
A6: (-1)*((-1)*Int(V1)) = Int(V1) & (-1)*((-1)*Int(V2)) = Int(V2)
 by CONVEX1:37;
  thus U = Int(V1) /\ Int(V2) /\ (((-1)*Int(V1)) /\ ((-1)*Int(V2)))
           by XBOOLE_1:16
        .= ((-1)*(Int(V1)) /\ ((-1)*Int(V2))) /\ (Int(V1) /\ Int(V2))
        .= (-1)*(Int(V1) /\ Int(V2)) /\ (Int(V1) /\ Int(V2))
           by Th15
        .= (-1)*(Int(V1) /\ Int(V2)) /\ ((-1)*((-1)*Int(V1) /\ ((-1)*Int(V2))))
           by A6,Th15
        .= (-1)*(Int(V1) /\ Int(V2) /\ ((-1)*Int(V1) /\ ((-1)*Int(V2))))
          by Th15
        .= (-1)*U by XBOOLE_1:16
        .= -U by Def4;
  let x be set;
  assume x in U + U;
  then x in {u + v where u,v is Point of X: u in U & v in U}
 by RUSUB_4:def 10;
  then consider u,v being Point of X such that
A7: u+v = x and
A8: u in U & v in U;
  U = Int(V1) /\ Int(V2) /\ (((-1)*Int(V1)) /\ ((-1)*Int(V2))) by XBOOLE_1:16;
  then u in Int(V1) /\ Int(V2) &  v in Int(V1) /\ Int(V2)
 by A8,XBOOLE_0:def 3;
  then u in Int(V1) & v in Int(V2) by XBOOLE_0:def 3;
  then u+v in {u'+v' where u',v' is Point of X: u' in Int(V1) & v' in Int(V2)};
  then
A9:  u+v in Int(V1)+Int(V2) by RUSUB_4:def 10;
  Int(V1) c= V1 & Int(V2) c= V2 by TOPS_1:44;
  then Int(V1)+Int(V2) c= V1+V2 by Th11;
  then u+v in V1+V2 by A9;
  hence x in W by A2,A7;
 end;

theorem Th58:
  for X being non empty LinearTopSpace,
      K being compact Subset of X, C being closed Subset of X
    st K misses C
   ex V being a_neighborhood of 0.X st K+V misses C+V
 proof
  let X be non empty LinearTopSpace,
      K be compact Subset of X, C be closed Subset of X
  such that
A1: K misses C;
  per cases;
  suppose
A2:  K = {};
   take V = [#]X;
   thus V is a_neighborhood of 0.X by TOPGRP_1:21;
    K+V = {} by A2,CONVEX1:40;
   hence thesis by XBOOLE_1:65;
  end;
  suppose
A3:  K <> {};
A4:
   now let x be Point of X such that
A5:   x in K;
A6:   -x+C` = {-x + u where u is Point of X: u in C`} by RUSUB_4:def 9;
    K c= C` by A1,SUBSET_1:43;
    then x in C` by A5;
    then -x+x in -x+C` by A6;
    then 0.X in -x+C` by RLVECT_1:16;
    then -x+C` is a_neighborhood of 0.X by CONNSP_2:5;
    then consider V' being open a_neighborhood of 0.X such that
     V' is symmetric and
A7:   V'+V' c= -x+C` by Th57;
    consider Vx being open a_neighborhood of 0.X such that
A8:   Vx is symmetric and
A9:   Vx+Vx c= V' by Th57;
     take Vx;
     thus Vx is symmetric by A8;
     Vx c= V'
      proof let z be set;
        assume
A10:       z in Vx;
        then reconsider z as Point of X;
        0.X in Vx by CONNSP_2:6;
        then z+0.X in Vx+Vx by A10,Lm2;
        then z in Vx+Vx by RLVECT_1:10;
        hence thesis by A9;
      end;
     then Vx+(Vx+Vx) c= V' + V' by A9,Th11;
     then Vx+Vx+Vx c= V' + V';
     then Vx+Vx+Vx c=-x+C` by A7,XBOOLE_1:1;
     then x+(Vx+Vx+Vx) c= x+(-x+C`) by Th8;
     then x+Vx+(Vx+Vx) c= x+(-x+C`) by Th7;
     then x+Vx+Vx+Vx c= x+(-x+C`) by CONVEX1:36;
     then x+Vx+Vx+Vx c= x+-x+C` by Th6;
     then x+Vx+Vx+Vx c= 0.X+C` by RLVECT_1:def 10;
     then x+Vx+Vx+Vx c= C` by Th5;
     hence x+Vx+Vx+Vx misses C by SUBSET_1:43;
   end;
  set xV = { [x,Vx] where x is Point of X, Vx is open a_neighborhood of 0.X:
               x in K & Vx is symmetric & x+Vx+Vx+Vx misses C};
  now
   consider x be set such that
A11:  x in K by A3,XBOOLE_0:def 1;
   reconsider x as Point of X by A11;
   consider Vx being open a_neighborhood of 0.X such that
A12:  Vx is symmetric and
A13:  x+Vx+Vx+Vx misses C by A4,A11;
   take z = [x,Vx];
   thus z in xV by A11,A12,A13;
  end;
  then reconsider xV as non empty set;
  defpred P[set,set] means
    ex v1,v2 being Point of X, V1,V2 being open a_neighborhood of 0.X
     st $1 = [v1,V1] & $2 = [v2,V2] & v1+V1 = v2+V2;
A14: for x being set st x in xV holds P[x,x]
     proof let x be set;
       assume x in xV;
       then ex v being Point of X, V being open a_neighborhood of 0.X st
         x = [v,V] & v in K & V is symmetric & v+V+V+V misses C;
       hence thesis;
     end;
A15: for x,y being set st P[x,y] holds P[y,x];
A16: for x,y,z being set st P[x,y] & P[y,z] holds P[x,z]
     proof let x,y,z be set;
      given v1,v2 being Point of X, V1,V2 being open a_neighborhood of 0.X
       such that
A17:     x = [v1,V1] and
A18:     y = [v2,V2] and
A19:     v1+V1 = v2+V2;
      given w1,w2 being Point of X, W1,W2 being open a_neighborhood of 0.X
       such that
A20:     y = [w1,W1] and
A21:     z = [w2,W2] and
A22:     w1+W1 = w2+W2;
     take v1,w2,V1,W2;
      v2 = w1 & V2 = W1 by A18,A20,ZFMISC_1:33;
      hence thesis by A17,A19,A21,A22;
     end;
   consider eqR being Equivalence_Relation of xV such that
A23:  for x,y being set holds [x,y] in eqR iff x in xV & y in xV & P[x,y]
      from EQREL_1:sch 1(A14,A15,A16);
   now let X be set;
     assume X in Class eqR;
     then ex x being set st x in xV & X = Class(eqR,x) by EQREL_1:def 5;
     hence X <> {} by EQREL_1:28;
   end;
   then consider g being Function such that
A24:  dom g = Class eqR and
A25:  for X being set st X in Class eqR holds g.X in X by WELLORD2:28;
A26:  rng g c= xV
       proof let x be set;
        assume x in rng g;
        then consider y being set such that
A27:       y in dom g & x = g.y by FUNCT_1:def 5;
         x in y & y c= xV by A24,A25,A27;
        hence thesis;
       end;
  set xVV = rng g;
  set F = { x+Vx where x is Point of X, Vx is open a_neighborhood of 0.X:
                 [x,Vx] in xVV};
   F c= bool the carrier of X
    proof let A be set;
      assume A in F;
      then ex x being Point of X, Vx being open a_neighborhood of 0.X
       st A = x+Vx & [x,Vx] in xVV;
      then A c= the carrier of X;
      hence thesis;
    end;
   then F is Subset-Family of the carrier of X by SETFAM_1:def 7;
   then reconsider F as Subset-Family of X;
A28: F is_a_cover_of K
 proof let x be set;
  assume
A29:  x in K;
  then reconsider x as Point of X;
  consider Vx being open a_neighborhood of 0.X such that
A30:  Vx is symmetric and
A31:  x+Vx+Vx+Vx misses C by A4,A29;
  0.X in Vx by CONNSP_2:6;
  then x+0.X in x+Vx by Lm1;
  then
A32:  x in x+Vx by RLVECT_1:10;
  [x,Vx] in xV by A29,A30,A31;
  then
A33:  Class(eqR,[x,Vx]) in Class eqR by EQREL_1:def 5;
  then g.Class(eqR,[x,Vx]) in Class(eqR,[x,Vx]) by A25;
  then [g.Class(eqR,[x,Vx]),[x,Vx]] in eqR by EQREL_1:27;
  then consider v1,v2 being Point of X, V1,V2 being open a_neighborhood of 0.X
   such that
A34:  g.Class(eqR,[x,Vx]) = [v1,V1] and
A35:  [x,Vx] = [v2,V2] and
A36:  v1+V1 = v2+V2 by A23;
A37:  x = v2 & Vx = V2 by A35,ZFMISC_1:33;
  g.Class(eqR,[x,Vx]) in xVV by A24,A33,FUNCT_1:def 5;
  then x+Vx in F by A34,A36,A37;
  hence thesis by A32,TARSKI:def 4;
 end;
A38:  F is open
  proof
   let P being Subset of X;
   assume P in F;
   then ex x being Point of X, Vx being open a_neighborhood of 0.X
    st P = x+Vx & [x,Vx] in xVV;
   hence P is open;
  end;
  consider G being Subset-Family of X such that
A39:  G c= F and
A40:  G is_a_cover_of K and
A41:  G is finite by A28,A38,COMPTS_1:def 7;
A42:  for A being Subset of X st A in G
      ex x being Point of X, Vx being open a_neighborhood of 0.X
       st A = x+Vx & [x,Vx] in xVV
   proof let A be Subset of X;
     assume A in G;
     then A in F by A39;
     hence ex x being Point of X, Vx being open a_neighborhood of 0.X
      st A = x+Vx & [x,Vx] in xVV;
   end;
A43:  for x being Point of X, Vx being open a_neighborhood of 0.X
       st x+Vx in G & [x,Vx] in xVV
      holds x in K & Vx is symmetric & x+Vx+Vx+Vx misses C
  proof let x be Point of X, Vx be open a_neighborhood of 0.X such that
A44:   x+Vx in G and
A45:   [x,Vx] in xVV;
   x+Vx in F by A39,A44;
   then consider v being Point of X, Vv being open a_neighborhood of 0.X
    such that
A46:   x+Vx = v+Vv and
A47:   [v,Vv] in xVV;
A48:   [v,Vv] in xV by A26,A47;
   then consider u being Point of X, Vu being open a_neighborhood of 0.X
    such that
A49:   [u,Vu] = [v,Vv] and
A50:   u in K & Vu is symmetric & u+Vu+Vu+Vu misses C;
    [x,Vx] in xV by A26,A45;
    then [[x,Vx],[v,Vv]] in eqR by A23,A46,A48;
    then [x,Vx] in Class(eqR,[v,Vv]) by EQREL_1:27;
    then
A51:   Class(eqR,[v,Vv]) = Class(eqR,[x,Vx]) by A48,EQREL_1:31;
    consider A being set such that
A52:   A in dom g and
A53:   [x,Vx] = g.A by A45,FUNCT_1:def 5;
    consider B being set such that
A54:   B in dom g and
A55:   [v,Vv] = g.B by A47,FUNCT_1:def 5;
    consider a being set such that
A56:   a in xV and
A57:   A = Class(eqR,a) by A24,A52,EQREL_1:def 5;
    consider b being set such that
A58:   b in xV and
A59:   B = Class(eqR,b) by A24,A54,EQREL_1:def 5;
    [x,Vx] in Class(eqR,a) by A24,A25,A52,A53,A57;
    then
A60:   Class(eqR,a) = Class(eqR,[x,Vx]) by A56,EQREL_1:31;
    [v,Vv] in Class(eqR,b) by A24,A25,A54,A55,A59;
    then Class(eqR,b) = Class(eqR,[v,Vv]) by A58,EQREL_1:31;
    then [x,Vx] = [v,Vv] by A51,A53,A55,A57,A59,A60;
    then x = v & Vx = Vv & v = u & Vv = Vu by A49,ZFMISC_1:33;
    hence thesis by A50;
  end;
  set FVx = {Vx where Vx is open a_neighborhood of 0.X:
                 ex x being Point of X st x+Vx in G & [x,Vx] in xVV};
  FVx c= bool the carrier of X
    proof let A be set;
      assume A in FVx;
      then ex Vx being open a_neighborhood of 0.X
       st A = Vx & ex x being Point of X st x+Vx in G & [x,Vx] in xVV;
      then A c= the carrier of X;
      hence thesis;
    end;
  then FVx is Subset-Family of the carrier of X by SETFAM_1:def 7;
  then reconsider FVx as Subset-Family of X;
  take V = meet FVx;
A61:  FVx is open
  proof
   let P being Subset of X;
   assume P in FVx;
   then ex Vx being open a_neighborhood of 0.X
    st P = Vx & ex x being Point of X st x+Vx in G& [x,Vx] in xVV;
   hence P is open;
  end;
 now
   consider y be Point of X such that
A62:  y in K by A3,SUBSET_1:10;
   consider W being Subset of X such that
   y in W and
A63:  W in G by A40,A62,Th2;
   consider x being Point of X, Vx being open a_neighborhood of 0.X
    such that
A64:  W = x+Vx & [x,Vx] in xVV by A42,A63;
   Vx in FVx by A63,A64;
   hence ex z being set st z in FVx;
 end;
 then
A65:  FVx is non empty;
 now
  let A be set;
  assume
A66:  A in FVx;
  then reconsider A'=A as Subset of the carrier of X;
A67: Int A' c= A' by TOPS_1:44;
  ex Vx being open a_neighborhood of 0.X
    st A = Vx & ex x being Point of X st x+Vx in G & [x,Vx] in xVV by A66;
  then 0.X in Int A' by CONNSP_2:def 1;
  hence 0.X in A by A67;
 end;
 then
A68:  0.X in V by A65,SETFAM_1:def 1;
  defpred P[set,set] means
      ex x being Point of X, Vx being open a_neighborhood of 0.X
       st $1 = x+Vx & x+Vx in G & [x,Vx] in xVV & $2 = Vx;
A69: for x being set st x in G ex y being set st y in FVx & P[x,y]
     proof let x be set;
      assume
A70:     x in G;
      then x in F by A39;
      then consider z being Point of X, Vz being open a_neighborhood of 0.X
       such that
A71:     x = z+Vz and
A72:     [z,Vz] in xVV;
      take Vz;
      thus thesis by A70,A71,A72;
     end;
  consider f being Function of G,FVx such that
A73:  for x being set st x in G holds P[x,f.x] from FUNCT_2:sch 1(A69);
A74:  dom f = G by A65,FUNCT_2:def 1;
A75:  FVx c= rng f
   proof let z be set;
    assume z in FVx;
    then consider Vx being open a_neighborhood of 0.X such that
A76:    z = Vx and
A77:    ex x being Point of X st x+Vx in G & [x,Vx] in xVV;
     consider x being Point of X such that
A78:    x+Vx in G and
A79:    [x,Vx] in xVV by A77;
     consider v being Point of X, Vv being open a_neighborhood of 0.X
      such that
A80:    x+Vx = v+Vv and
       v+Vv in G and
A81:    [v,Vv] in xVV and
A82:    f.(x+Vx) = Vv by A73,A78;
A83:    [v,Vv] in xV by A26,A81;
    [x,Vx] in xV by A26,A79;
    then [[x,Vx],[v,Vv]] in eqR by A23,A80,A83;
    then [x,Vx] in Class(eqR,[v,Vv]) by EQREL_1:27;
    then
A84:   Class(eqR,[v,Vv]) = Class(eqR,[x,Vx]) by A83,EQREL_1:31;
    consider A being set such that
A85:   A in dom g and
A86:   [x,Vx] = g.A by A79,FUNCT_1:def 5;
    consider B being set such that
A87:   B in dom g and
A88:   [v,Vv] = g.B by A81,FUNCT_1:def 5;
    consider a being set such that
A89:   a in xV and
A90:   A = Class(eqR,a) by A24,A85,EQREL_1:def 5;
    consider b being set such that
A91:   b in xV and
A92:   B = Class(eqR,b) by A24,A87,EQREL_1:def 5;
    [x,Vx] in Class(eqR,a) by A24,A25,A85,A86,A90;
    then
A93:    Class(eqR,a) = Class(eqR,[x,Vx]) by A89,EQREL_1:31;
    [v,Vv] in Class(eqR,b) by A24,A25,A87,A88,A92;
    then Class(eqR,b) = Class(eqR,[v,Vv]) by A91,EQREL_1:31;
    then [x,Vx] = [v,Vv] by A84,A86,A88,A90,A92,A93;
    then x = v & Vx = Vv by ZFMISC_1:33;
     hence z in rng f by A74,A76,A78,A82,FUNCT_1:12;
   end;
  f"FVx is finite by A41,FINSET_1:13;
  then FVx is finite by A75,FINSET_1:27;
  then V is open by A61,TOPS_2:27;
  then Int V = V by TOPS_1:55;
  hence V is a_neighborhood of 0.X by A68,CONNSP_2:def 1;
  set FxVxV = {x+Vx+V where x is Point of X, Vx is open a_neighborhood of 0.X:
                   x+Vx in G & [x,Vx] in xVV};
  set FxVxVx = {x+Vx+Vx where x is Point of X, Vx is open a_neighborhood of 0.X
                   : x+Vx in G & [x,Vx] in xVV};
A94:   K+V c= union FxVxV
   proof let z be set;
    assume
A95:   z in K+V;
    K+V = {u + v where u,v is Point of X: u in K & v in V} by RUSUB_4:def 10;
    then consider u,v being Point of X such that
A96:   z = u+v and
A97:   u in K and
A98:   v in V by A95;
    consider Vu being Subset of X such that
A99:   u in Vu and
A100:   Vu in G by A40,A97,Th2;
    consider x being Point of X, Vx being open a_neighborhood of 0.X
     such that
A101:   Vu = x+Vx & [x,Vx] in xVV by A42,A100;
A102:   z in x+Vx+V by A96,A98,A99,A101,Lm2;
    x+Vx+V in FxVxV by A100,A101;
    hence z in union FxVxV by A102,TARSKI:def 4;
   end;
  union FxVxV c= union FxVxVx
   proof let z be set;
    assume z in union FxVxV;
    then consider Y being set such that
A103:    z in Y and
A104:    Y in FxVxV by TARSKI:def 4;
    consider x being Point of X, Vx being open a_neighborhood of 0.X
    such that
A105:   Y = x+Vx+V and
A106:   x+Vx in G & [x,Vx] in xVV by A104;
    x+Vx+V = {u + v where u,v is Point of X: u in x+Vx & v in V}
   by RUSUB_4:def 10;
    then consider u,v being Point of X such that
A107:   z = u+v and
A108:   u in x+Vx and
A109:   v in V by A103,A105;
    Vx in FVx by A106;
    then v in Vx by A109,SETFAM_1:def 1;
    then
A110:    u+v in x+Vx+Vx by A108,Lm2;
    x+Vx+Vx in FxVxVx by A106;
    hence z in union FxVxVx by A107,A110,TARSKI:def 4;
   end;
  then
A111:  K+V c= union FxVxVx by A94,XBOOLE_1:1;
  now let Z be set;
    hereby
      assume Z in {{}};
      then
A112:     Z = {} by TARSKI:def 1;
      reconsider A = C+V as set;
      consider y be Point of X such that
A113:     y in K by A3,SUBSET_1:10;
      consider W being Subset of X such that
        y in W and
A114:     W in G by A40,A113,Th2;
      consider x being Point of X, Vx being open a_neighborhood of 0.X
       such that
A115:     W = x+Vx & [x,Vx] in xVV by A42,A114;
A116:     Vx is symmetric by A43,A114,A115;
A117:     x+Vx+Vx+Vx misses C by A43,A114,A115;
      reconsider B = x+Vx+Vx as set;
      take A,B;
      thus A in {C+V} by TARSKI:def 1;
      thus B in FxVxVx by A114,A115;
      now assume A meets B;
       then consider z being set such that
A118:      z in A and
A119:      z in B by XBOOLE_0:3;
       reconsider z as Point of X by A118;
       C+V = {u + v where u,v is Point of X: u in C & v in V}
      by RUSUB_4:def 10;
       then consider u,v being Point of X such that
A120:      z = u+v and
A121:      u in C and
A122:      v in V by A118;
       Vx in FVx by A114,A115;
       then v in Vx by A122,SETFAM_1:def 1;
       then -v in Vx by A116,Th26;
       then
A123:    z+-v in x+Vx+Vx+Vx by A119,Lm2;
       z+-v = u+(v+-v) by A120,RLVECT_1:def 6
            .= u+0.X by RLVECT_1:16
            .= u by RLVECT_1:10;
       hence contradiction by A117,A121,A123,XBOOLE_0:3;
      end;
      hence Z = A /\ B by A112,XBOOLE_0:def 7;
    end;
    given A,B being set such that
A124:   A in {C+V} and
A125:   B in FxVxVx and
A126:   Z = A /\ B;
A127:   A = C+V by A124,TARSKI:def 1;
    consider x being Point of X, Vx being open a_neighborhood of 0.X
     such that
A128:   B = x+Vx+Vx and
A129:   x+Vx in G & [x,Vx] in xVV by A125;
A130:   Vx is symmetric by A43,A129;
A131:   x+Vx+Vx+Vx misses C by A43,A129;
    now assume A meets B;
     then consider z being set such that
A132:    z in A and
A133:    z in B by XBOOLE_0:3;
     reconsider z as Point of X by A127,A132;
     C+V = {u + v where u,v is Point of X: u in C & v in V}
     by RUSUB_4:def 10;
     then consider u,v being Point of X such that
A134:    z = u+v and
A135:    u in C and
A136:    v in V by A127,A132;
     Vx in FVx by A129;
     then v in Vx by A136,SETFAM_1:def 1;
     then -v in Vx by A130,Th26;
     then
A137:    z+-v in x+Vx+Vx+Vx by A128,A133,Lm2;
     z+-v = u+(v+-v) by A134,RLVECT_1:def 6
         .= u+0.X by RLVECT_1:16
         .= u by RLVECT_1:10;
     hence contradiction by A131,A135,A137,XBOOLE_0:3;
    end;
    then A /\ B = {} by XBOOLE_0:def 7;
    hence Z in {{}} by A126,TARSKI:def 1;
  end;
  then INTERSECTION ({C+V}, FxVxVx) = {{}} by SETFAM_1:def 5;
  then union INTERSECTION ({C+V}, FxVxVx) = {} by ZFMISC_1:31;
  then (C+V) /\ union FxVxVx = {} by SETFAM_1:36;
  then C+V misses union FxVxVx by XBOOLE_0:def 7;
  hence K+V misses C+V by A111,XBOOLE_1:63;
 end;
end;

theorem Th59:
  for X being non empty LinearTopSpace, B being local_base of X,
      V being a_neighborhood of 0.X
     ex W being a_neighborhood of 0.X st W in B & Cl W c= V
 proof let X be non empty LinearTopSpace, B be local_base of X;
   let V be a_neighborhood of 0.X;
  set C = (Int V)`;
  set K = {0.X};
A1:  K is compact by BORSUK_1:41;
  0.X in Int V by CONNSP_2:def 1;
  then not 0.X in (Int V)` by SUBSET_1:54;
  then K misses C by ZFMISC_1:56;
  then consider P being a_neighborhood of 0.X  such that
A2:  K+P misses C+P by A1,Th58;
A3:  0.X in Int P by CONNSP_2:def 1;
  then reconsider P' = Int P as open a_neighborhood of 0.X by CONNSP_2:5;
  P' c= P by TOPS_1:44;
  then K+P' c= K+P & C+P' c= C+P by Lm4;
  then
A4:  K+P' misses C+P' by A2,XBOOLE_1:64;
A5:  C c= C+P' by A3,Th12;
  Cl(K+P') misses C+P' by A4,Th3;
  then Cl(K+P') misses C by A5,XBOOLE_1:63;
  then Cl P' misses C by CONVEX1:35;
  then
A6:  Cl P' c= Int V by SUBSET_1:44;
  Int V c= V by TOPS_1:44;
  then
A7:  Cl P' c= V by A6,XBOOLE_1:1;
  consider W being a_neighborhood of 0.X such that
A8:  W in B and
A9:  W c= P' by YELLOW13:def 2;
  take W;
  thus W in B by A8;
  Cl W c= Cl P' by A9,PRE_TOPC:49;
  hence thesis by A7,XBOOLE_1:1;
end;

theorem Th60:
  for X being non empty LinearTopSpace, V being a_neighborhood of 0.X
     ex W being a_neighborhood of 0.X st Cl W c= V
proof let X be non empty LinearTopSpace, V be a_neighborhood of 0.X;
  set B = {U where U is a_neighborhood of 0.X: not contradiction};
  B c= bool the carrier of X
   proof let A be set;
     assume A in B;
     then ex U being a_neighborhood of 0.X st A = U;
     then A c= the carrier of X;
     hence thesis;
   end;
  then B is Subset-Family of the carrier of X by SETFAM_1:def 7;
  then B is Subset-Family of X;
  then B is local_base of X by Th45;
  then consider W being a_neighborhood of 0.X such that W in B and
A1: Cl W c= V by Th59;
  take W;
  thus thesis by A1;
end;

registration
  cluster being_T1 -> Hausdorff (non empty LinearTopSpace);
 coherence
  proof let X be non empty LinearTopSpace;
   assume
A1:  X is being_T1;
    let p, q being Point of X;
    assume p <> q;
    then
A2:   {p} misses {q} by ZFMISC_1:17;
A3:   {p} is compact by BORSUK_1:41;
    {q} is closed by A1,URYSOHN1:27;
    then consider V being a_neighborhood of 0.X such that
A4:   {p}+V misses {q}+V by A2,A3,Th58;
A5:   {p}+V = p+V & {q}+V = q+V by RUSUB_4:36;
    take p+Int(V),q+Int(V);
    thus p+Int(V) is open & q+Int(V) is open;
    0.X in Int(V) by CONNSP_2:def 1;
    then p+0.X in p+Int(V) & q+0.X in q+Int(V) by Lm1;
    hence p in p+Int(V) & q in q+Int(V) by RLVECT_1:10;
    Int V c= V by TOPS_1:44;
    then p+Int(V) c= p+V & q+Int(V) c= q+V by Th8;
    hence p+Int(V) misses q+Int(V) by A4,A5,XBOOLE_1:64;
  end;
end;

theorem
  for X being non empty LinearTopSpace, A being Subset of X
   holds Cl A = meet {A+V where V is a_neighborhood of 0.X: not contradiction}
 proof let X be non empty LinearTopSpace, A be Subset of X;
   set AV = {A+V where V is a_neighborhood of 0.X: not contradiction};
A1:   for x being Point of X holds x in Cl A
    iff  for V being a_neighborhood of 0.X holds A meets x+Int(V)
   proof let x be Point of X;
     hereby assume
A2:    x in Cl A;
       let V be a_neighborhood of 0.X;
       0.X in Int(V) by CONNSP_2:def 1;
        then x+0.X in x+Int(V) by Lm1;
        then x in x+Int(V) by RLVECT_1:10;
        hence A meets x+Int(V) by A2,TOPS_1:39;
      end;
    assume
A3:  for V being a_neighborhood of 0.X holds A meets x+Int(V);
    now let V be Subset of X such that
A4:    V is open and
A5:    x in V;
     -x+V is open by A4,Lm13; then
A6:    Int(-x+V) = -x+V by TOPS_1:55;
     -x+x in -x+V by A5,Lm1;
     then 0.X in -x+V by RLVECT_1:16;
     then -x+V is a_neighborhood of 0.X by A6,CONNSP_2:def 1;
     then A meets x+(-x+V) by A3,A6;
     then A meets x+-x+V by Th6;
     then A meets 0.X+V by RLVECT_1:16;
     hence A meets V by Th5;
    end;
    hence x in Cl A by TOPS_1:39;
   end;
A7:  for x being Point of X, V being a_neighborhood of 0.X
      holds A meets x+Int(V) iff x in A+(-1)*Int(V)
    proof let x be Point of X, V be a_neighborhood of 0.X;
      hereby
        assume
A8:       A meets x+Int(V);
        x in A+-Int(V) by A8,Th25;
        then
A9:       x in A+(-1)*Int(V) by Def4;
        thus x in A+(-1)*Int(V) by A9;
      end;
A10:   A+(-1)*Int(V) = {a+v where a,v is Point of X: a in A & v in (-1)*Int(V)}
      by RUSUB_4:def 10;
     assume x in A+(-1)*Int(V);
     then consider a,v being Point of X such that
A11:    x = a+v and
A12:    a in A and
A13:    v in (-1)*Int(V) by A10;
    (-1)*Int(V) = {(-1)*v' where v' is Point of X: v' in Int(V)}
      by CONVEX1:def 1;
    then consider v' being Point of X such that
A14: v = (-1)*v' and
A15: v' in Int(V) by A13;
     -v = (-1)*v by RLVECT_1:29
       .= (-1)*(-1)*v' by A14,RLVECT_1:def 9
       .= 1*v'
       .= v' by RLVECT_1:def 9;
    then
A16:  x+v' = a + (v + (-v)) by A11,RLVECT_1:def 6
          .= a+0.X by RLVECT_1:16
          .= a by RLVECT_1:10;
     x+Int(V) = {x+w where w is Point of X: w in Int(V)} by RUSUB_4:def 9;
     then x+v' in x+Int(V) by A15;
     hence thesis by A12,A16,XBOOLE_0:3;
    end;
    consider V being a_neighborhood of 0.X;
A17:   A+V in AV;
   thus Cl A c= meet AV
    proof let x be set;
      assume
A18:     x in Cl A;
      then reconsider x as Point of X;
      now let Y be set;
       assume Y in AV;
        then consider V being a_neighborhood of 0.X such that
A19:       Y = A+V;
A20:       A+(-1)*Int((-1)*V) =
            {a+v where a,v is Point of X: a in A & v in (-1)*Int((-1)*V)}
         by RUSUB_4:def 10;
A21:      A+V = {a+v where a,v is Point of X: a in A & v in V}
         by RUSUB_4:def 10;
A22:      (-1)*Int((-1)*V) = {(-1)*v where v is Point of X: v in Int((-1)*V)}
         by CONVEX1:def 1;
A23:      (-1)*V = {(-1)*v where v is Point of X: v in V} by CONVEX1:def 1;
A24:      (-1)*V is a_neighborhood of 0.X by Th56;
        then A meets x+Int((-1)*V) by A1,A18;
        then x in A+(-1)*Int(((-1)*V)) by A7,A24;
        then consider a,v being Point of X such that
A25:      x = a+v and
A26:      a in A and
A27:      v in (-1)*Int((-1)*V) by A20;
        consider v' being Point of X such that
A28:      v=(-1)*v' and
A29:      v' in Int((-1)*V) by A22,A27;
        Int((-1)*V) c= (-1)*V by TOPS_1:44;
        then v' in (-1)*V by A29;
        then consider v'' being Point of X such that
A30:      v' = (-1)*v'' and
A31:      v'' in V by A23;
        v = (-1)*(-1)*v'' by A28,A30,RLVECT_1:def 9
         .= 1*v''
         .= v'' by RLVECT_1:def 9;
        then v in V by A31;
        hence x in Y by A19,A21,A25,A26;
      end;
     hence thesis by A17,SETFAM_1:def 1;
    end;
   let x be set;
   assume
A32:   x in meet AV;
  AV c= bool the carrier of X
   proof
     let x be set;
     assume x in AV;
     then ex V being a_neighborhood of 0.X st x = A+V;
     hence thesis;
   end;
  then reconsider AV' = AV as Subset-Family of the carrier of X
 by SETFAM_1:def 7;
  meet AV' c= the carrier of X;
  then reconsider x as Point of X by A32;
  now let V be a_neighborhood of 0.X;
     0.X in Int(V) by CONNSP_2:def 1;
     then
A33:   Int(V) is a_neighborhood of 0.X by CONNSP_2:5;
    (-1)*Int(V) is a_neighborhood of 0.X by A33,Th56;
    then A+(-1)*Int(V) in AV;
    then x in A+(-1)*Int(V) by A32,SETFAM_1:def 1;
   hence A meets x+Int(V) by A7;
  end;
  hence thesis by A1;
 end;

theorem Th62:
  for X being non empty LinearTopSpace, A,B being Subset of X
    holds Int A + Int B c= Int(A+B)
proof let X be non empty LinearTopSpace, A,B be Subset of X;
  Int A c= A & Int B c= B by TOPS_1:44;
  then Int A + Int B c= A+B by Th11;
  hence thesis by TOPS_1:56;
end;

theorem Th63:
  for X being non empty LinearTopSpace, A,B being Subset of X
    holds Cl A + Cl B c= Cl(A+B)
proof let X be non empty LinearTopSpace, A,B be Subset of X;
  let z be set;
A1:  {u + v where u,v is Point of X: u in Cl  A & v in Cl B} = Cl A + Cl B
     by RUSUB_4:def 10;
  assume
A2: z in Cl A + Cl B;
  then reconsider z as Point of X;
  consider a,b being Point of X such that
A3:   z = a+b and
A4:   a in Cl A and
A5:   b in Cl B by A1,A2;
  now let W being Subset of X such that
A6:   W is open and
A7:   z in W;
   W is a_neighborhood of z by A6,A7,CONNSP_2:5;
   then consider W1 being a_neighborhood of a, W2 being a_neighborhood of b
    such that
A8:   W1+W2 c= W by A3,Th32;
   a in Int W1 by CONNSP_2:def 1;
   then A meets Int W1 by A4,TOPS_1:39;
   then consider x being set such that
A9:  x in A and
A10:  x in Int W1 by XBOOLE_0:3;
   reconsider x as Point of X by A9;
   b in Int W2 by CONNSP_2:def 1;
   then B meets Int W2 by A5,TOPS_1:39;
   then consider y being set such that
A11:  y in B and
A12:  y in Int W2 by XBOOLE_0:3;
   reconsider y as Point of X by A11;
A13:  x+y in A+B by A9,A11,Lm2;
A14:  Int W1 + Int W2 c= Int W by A8,Th37;
   x+y in Int W1 + Int W2 by A10,A12,Lm2;
   then x+y in Int W by A14;
   then A+B meets Int W by A13,XBOOLE_0:3;
   hence A+B meets W by A6,TOPS_1:55;
  end;
 hence thesis by TOPS_1:39;
end;

Lm19:
 for X being non empty LinearTopSpace, C being convex Subset of X
  holds Cl C is convex
proof let X be non empty LinearTopSpace, C be convex Subset of X;
  now
   let r be Real such that
A1:  0 < r and
A2:  r < 1;
   r*C + (1-r)*C c= C by A1,A2,CONVEX1:4;
   then
A3:  Cl(r*C + (1-r)*C) c= Cl(C) by PRE_TOPC:49;
A4:  Cl(r*C) + Cl((1-r)*C) c= Cl(r*C + (1-r)*C) by Th63;
A5:  Cl(r*C) = r*Cl(C) by A1,Th53;
   0+r < 1 by A2;
   then 0 < 1-r by REAL_1:86;
   then (1-r)*Cl(C) = Cl((1-r)*C) by Th53;
   hence r*Cl(C) + (1-r)*Cl(C) c= Cl(C) by A3,A4,A5,XBOOLE_1:1;
  end;
  hence thesis by CONVEX1:4;
end;

registration
  let X be non empty LinearTopSpace, C be convex Subset of X;
  cluster Cl C -> convex;
  coherence by Lm19;
end;

Lm20:
 for X being non empty LinearTopSpace, C being convex Subset of X
  holds Int C is convex
proof let X be non empty LinearTopSpace, C be convex Subset of X;
  now
   let r be Real such that
A1:  0 < r and
A2:  r < 1;
   r*C + (1-r)*C c= C by A1,A2,CONVEX1:4;
   then
A3:  Int(r*C + (1-r)*C) c= Int(C) by TOPS_1:48;
A4:  Int(r*C) + Int((1-r)*C) c= Int(r*C + (1-r)*C) by Th62;
A5:  Int(r*C) = r*Int(C) by A1,Th52;
   0+r < 1 by A2;
   then 0 < 1-r by REAL_1:86;
   then (1-r)*Int(C) = Int((1-r)*C) by Th52;
   hence r*Int(C) + (1-r)*Int(C) c= Int(C) by A3,A4,A5,XBOOLE_1:1;
  end;
 hence thesis by CONVEX1:4;
end;

registration
  let X be non empty LinearTopSpace, C be convex Subset of X;
  cluster Int C -> convex;
  coherence by Lm20;
end;

Lm21:
 for X being non empty LinearTopSpace, B being circled Subset of X
  holds Cl B is circled
 proof
   let X be non empty LinearTopSpace, B be circled Subset of X;
  let r be Real such that
A1:  abs(r) <= 1;
 per cases;
  suppose
A2:   r = 0;
   now per cases;
     suppose B = {}X;
      then Cl B = {}X by PRE_TOPC:52;
      hence r*Cl(B) c= Cl(B) by CONVEX1:33;
     end;
     suppose B <> {}X;
      then
A3:    0.X in B by Th28;
       B c= Cl B by PRE_TOPC:48;
      then
A4:    0.X in Cl B by A3;
      then r*Cl(B) = {0.X} by A2,CONVEX1:34;
      hence thesis by A4,ZFMISC_1:37;
     end;
   end;
   hence thesis;
  end;
  suppose
A5:   r <> 0;
   r*B c= B by A1,Def6;
   then Cl(r*B) c= Cl(B) by PRE_TOPC:49;
   hence r*Cl(B) c= Cl(B) by A5,Th53;
  end;
end;

registration
 let X be non empty LinearTopSpace, B be circled Subset of X;
 cluster Cl B -> circled;
 coherence by Lm21;
end;

theorem
 for X being non empty LinearTopSpace, B being circled Subset of X
   st 0.X in Int B
  holds Int B is circled
 proof
   let X be non empty LinearTopSpace, B be circled Subset of X such that
A1: 0.X in Int B;
  let r be Real such that
A2:   abs(r) <= 1;
  r*B c= B by A2,Def6;
  then
A3:  Int(r*B) c= Int(B) by TOPS_1:48;
 per cases;
  suppose r = 0;
   then r*Int(B) = {0.X} by A1,CONVEX1:34;
   hence thesis by A1,ZFMISC_1:37;
  end;
  suppose r <> 0;
   hence r*Int(B) c= Int(B) by A3,Th52;
  end;
 end;

Lm22:
 for X being non empty LinearTopSpace, E being bounded Subset of X
  holds Cl E is bounded
 proof let X be non empty LinearTopSpace, E be bounded Subset of X;
  let V be a_neighborhood of 0.X;
  consider W be a_neighborhood of 0.X such that
A1:  Cl W c= V by Th60;
  consider s such that
A2: s > 0 and
A3: for t st t > s holds E c= t*W by Def12;
  take s;
  thus s > 0 by A2;
  let t;
  assume
A4:  t > s;
  then E c= t*W by A3;
  then
A5:  Cl E c= Cl(t*W) by PRE_TOPC:49;
  t > 0 by A2,A4;
  then
A6:  Cl(t*W) = t*Cl(W) by Th53;
  t*Cl(W) c= t*V by A1,CONVEX1:39;
  hence Cl E c= t*V by A5,A6,XBOOLE_1:1;
 end;

registration
 let X be non empty LinearTopSpace, E be bounded Subset of X;
 cluster Cl E -> bounded;
 coherence by Lm22;
end;

Lm23:
  for X being non empty LinearTopSpace, U,V being a_neighborhood of 0.X,
      F being Subset-Family of X, r being positive Real
    st (for s being Real st abs(s) < r holds s*V c= U) &
     F = {a*V where a is Real: abs(a) < r}
   holds union F is a_neighborhood of 0.X & union F is circled &
    union F c= U
 proof let X be non empty LinearTopSpace, U,V be a_neighborhood of 0.X,
      F be Subset-Family of X, r be positive Real such that
A1:    for s being Real st abs(s) < r holds s*V c= U and
A2:    F = {a*V where a is Real: abs(a) < r};
   set W = union F;
   thus W is a_neighborhood of 0.X
     proof
      set F' = {a*Int(V) where a is non zero Real: abs(a) < r};
      consider a being real number such that
A3:    0 < a and
A4:    a < r by REAL_1:75;
      reconsider a as Real by XREAL_0:def 1;
A5:    abs(a) < r by A3,A4,ABSVALUE:def 1;
A6:     0.X in Int(V) by CONNSP_2:def 1;
A7:     a*Int(V) in F' by A3,A5;
      a*0.X in a*Int(V) by A6,Lm5;
      then 0.X in a*Int(V) by RLVECT_1:23;
      then
A8:     0.X in union F' by A7,TARSKI:def 4;
      F' c= bool the carrier of X
       proof let A be set;
        assume A in F';
         then ex a being non zero Real st A = a*Int(V) & abs(a) < r;
         then A c= the carrier of X;
        hence thesis;
       end;
      then F' is Subset-Family of the carrier of X by SETFAM_1:def 7;
      then reconsider F' as Subset-Family of X;
      F' is open
       proof
        let P be Subset of X;
        assume P in F';
        then ex a being non zero Real st P = a*Int(V) & abs(a) < r;
        hence P is open by Th50;
       end;
      then union F' is open by TOPS_2:26;
      then
A9:      0.X in Int union F' by A8,TOPS_1:55;
      union F' c= W
       proof let x be set;
         assume x in union F';
         then consider P being set such that
A10:        x in P and
A11:        P in F' by TARSKI:def 4;
        consider a being non zero Real such that
A12:        P = a*Int(V) and
A13:        abs(a) < r by A11;
          a*Int(V) = {a*v where v is Point of X: v in Int(V)} by CONVEX1:def 1;
         then consider v being Point of X such that
A14:        x = a*v and
A15:        v in Int(V) by A10,A12;
         Int V c= V by TOPS_1:44;
         then
A16:        v in V by A15;
A17:        a*V in F by A2,A13;
         a*V = {a*v' where v' is Point of X: v' in V} by CONVEX1:def 1;
         then x in a*V by A14,A16;
         hence x in W by A17,TARSKI:def 4;
       end;
      then Int union F' c= Int W by TOPS_1:48;
      hence 0.X in Int W by A9;
     end;
    thus W is circled
     proof
      let s be Real such that
A18:    abs(s) <= 1;
A19:    s*W = {s*u where u is Point of X: u in W} by CONVEX1:def 1;
      let z be set;
      assume z in s*W;
      then consider u being Point of X such that
A20:    z = s*u and
A21:    u in W by A19;
      consider Y being set such that
A22:    u in Y and
A23:    Y in F by A21,TARSKI:def 4;
      consider a being Real such that
A24:    Y = a*V and
A25:    abs(a) < r by A2,A23;
      a*V = {a*v where v is Point of X: v in V} by CONVEX1:def 1;
      then consider v being Point of X such that
A26:    u = a*v and
A27:   v in V by A22,A24;
       z = s*a*v by A20,A26,RLVECT_1:def 9;
       then
A28:     z in s*a*V by A27,Lm5;
A29:      abs(s)*r <= r by A18,REAL_2:147;
      per cases;
       suppose 0 <> abs(s);
        then s <> 0 by ABSVALUE:7;
        then 0 < abs(s) by ABSVALUE:6;
        then abs(s)*abs(a) < abs(s) * r by A25,REAL_1:70;
        then abs(s)*abs(a) < r by A29,AXIOMS:22;
        then abs(s*a) < r by ABSVALUE:10;
        then s*a*V in F by A2;
        hence z in W by A28,TARSKI:def 4;
       end;
       suppose abs(s) = 0;
         then s = 0 by ABSVALUE:7;
         then s*a = 0;
         then abs(s*a) = 0 by ABSVALUE:def 1;
         then abs(s*a) < r;
         then s*a*V in F by A2;
         hence z in W by A28,TARSKI:def 4;
       end;
     end;
    let z be set;
    assume
A30:    z in W;
    then reconsider z as Point of X;
    consider Y being set such that
A31:    z in Y and
A32:    Y in F by A30,TARSKI:def 4;
    consider a being Real such that
A33:    Y = a*V and
A34:    abs(a) < r by A2,A32;
    a*V c= U by A1,A34;
    hence thesis by A31,A33;
 end;

theorem Th65:
  for X being non empty LinearTopSpace, U being a_neighborhood of 0.X
   ex W being a_neighborhood of 0.X st W is circled & W c= U
 proof let X be non empty LinearTopSpace, U be a_neighborhood of 0.X;
   0.X = 0*0.X by RLVECT_1:23;
   then consider r being positive Real, V being a_neighborhood of 0.X such that
A1: for s being Real st abs(s-0) < r holds s*V c= U by Th33;
    set F = {a*V where a is Real: abs(a) < r};
    F c= bool the carrier of X
    proof let A be set;
      assume A in F;
      then ex a being Real st A = a*V  & abs(a) < r;
      then A c= the carrier of X;
      hence thesis;
    end;
    then F is Subset-Family of the carrier of X by SETFAM_1:def 7;
    then reconsider F as Subset-Family of X;
    take W = union F;
    now
      let s be Real;
      assume abs(s) < r;
      then abs(s-0) < r;
      hence s*V c= U by A1;
    end;
    hence thesis by Lm23;
 end;

theorem Th66:
  for X being non empty LinearTopSpace, U being a_neighborhood of 0.X
    st U is convex
   ex W being a_neighborhood of 0.X st W is circled convex & W c= U
 proof let X be non empty LinearTopSpace, U be a_neighborhood of 0.X such that
A1:  U is convex;
   set V = U /\ -U;
A2:   -U = (-1)*U by Def4;
   then -U is a_neighborhood of 0.X by Th56;
   then reconsider V as a_neighborhood of 0.X by CONNSP_2:4;
   take V;
A3:   -U is convex by A1,A2,CONVEX1:1;
   thus V is circled
    proof
     let r be Real such that
A4:     abs(r) <= 1;
      let u be set;
      assume
A5:    u in r*V;
       r*V = {r*v where v is Point of X: v in V} by CONVEX1:def 1;
      then consider v being Point of X such that
A6:    u = r*v and
A7:    v in V by A5;
A8:    v in -U by A7,XBOOLE_0:def 3;
A9:    v in U by A7,XBOOLE_0:def 3;
A10:    (-1)*U = {(-1)*w where w is Point of X: w in U} by CONVEX1:def 1;
     0.X in V by CONNSP_2:6;
     then
A11:   0.X in -U by XBOOLE_0:def 3;
A12:   0.X in U by CONNSP_2:6;
    per cases;
     suppose
A13:    r < 0;
A14:    -r <= 1 by A4,A13,ABSVALUE:def 1;
A15:    -0 <= -r by A13,REAL_1:50;
      then (-r)*v + (1--r)*0.X in -U by A3,A8,A11,A14,Def1;
      then (-r)*v + 0.X in -U by RLVECT_1:23;
      then (-r)*v in -U by RLVECT_1:def 7;
      then consider u' being Point of X such that
A16:    (-r)*v = (-1)*u' and
A17:    u' in U by A2,A10;
A18:    u' = 1*u' by RLVECT_1:def 9
          .= ((-1)*(-1))*u'
          .= (-1)*((-1)*u') by RLVECT_1:def 9
          .= ((-r)*(-1))*v by A16,RLVECT_1:def 9
          .= r*v;
       (-r)*v + (1--r)*0.X in U by A1,A9,A12,A14,A15,Def1;
       then (-r)*v + 0.X in U by RLVECT_1:23;
       then (-r)*v in U by RLVECT_1:def 7;
       then ((-1)*r)*v in U;
       then (-1)*(((-1)*r)*v) in (-1)*U by A10;
       then ((-1)*((-1)*r))*v in (-1)*U by RLVECT_1:def 9;
      hence u in V by A2,A6,A17,A18,XBOOLE_0:def 3;
     end;
     suppose
A19:    r >= 0;
A20:    r <= 1 by A4,ABSVALUE:def 1;
      then r*v + (1-r)*0.X in U by A1,A9,A12,A19,Def1;
      then r*v + 0.X in U by RLVECT_1:23;
      then
A21:    r*v in U by RLVECT_1:def 7;
      r*v + (1-r)*0.X in -U by A3,A8,A11,A19,A20,Def1;
      then r*v + 0.X in -U by RLVECT_1:23;
      then r*v in -U by RLVECT_1:def 7;
      hence u in V by A6,A21,XBOOLE_0:def 3;
     end;
    end;
   thus V is convex by A1,A3,CONVEX2:1;
   thus V c= U by XBOOLE_1:17;
 end;

theorem
  for X being non empty LinearTopSpace
   ex P being local_base of X st P is circled-membered
 proof let X be non empty LinearTopSpace;
   defpred P[Subset of X] means $1 is circled;
   consider P being Subset-Family of X such that
A1:   for V being Subset of X holds V in P iff P[V]
       from PRE_TOPC:sch 1;
   reconsider P as Subset-Family of X;
   take P;
   thus P is local_base of X
    proof
     let V be a_neighborhood of 0.X;
      consider W being a_neighborhood of 0.X such that
A2:      W is circled and
A3:      W c= V by Th65;
      take W;
      thus W in P by A1,A2;
      thus W c= V by A3;
    end;
   let V be Subset of X;
   assume V in P;
   hence V is circled by A1;
 end;

theorem
  for X being non empty LinearTopSpace
    st X is locally-convex
   ex P being local_base of X st P is convex-membered
 proof let X be non empty LinearTopSpace such that
A1:   X is locally-convex;
   defpred P[Subset of X] means $1 is circled convex;
   consider P being Subset-Family of X such that
A2:   for V being Subset of X holds V in P iff P[V]
       from PRE_TOPC:sch 1;
   reconsider P as Subset-Family of X;
   take P;
   thus P is local_base of X
    proof
     let V be a_neighborhood of 0.X;
     consider Q being local_base of X such that
A3:     Q is convex-membered by A1,Def11;
     consider U being a_neighborhood of 0.X such that
A4:     U in Q and
A5:     U c= V by YELLOW13:def 2;
     U is convex by A3,A4,Def3;
     then consider W being a_neighborhood of 0.X such that
A6:      W is circled convex and
A7:      W c= U by Th66;
      take W;
      thus W in P by A2,A6;
      thus W c= V by A5,A7,XBOOLE_1:1;
    end;
  let V be Subset of X;
  assume V in P;
  hence V is convex by A2;
 end;
  
