:: A Theory of Sequential Files
::  by Hirofumi Fukura and Yatsuka Nakamura
::
:: Received August 12, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies FILEREC1, FINSEQ_8, ARYTM_1, RELAT_1, FINSEQ_1, FUNCT_1,
      FINSEQ_4, FINSEQ_5, BOOLE, RFINSEQ, JORDAN3;
 notations TARSKI, XBOOLE_0, SUBSET_1, XXREAL_0, REAL_1, NAT_1, BINARITH,
      RELAT_1, RFINSEQ, FINSEQ_1, FUNCT_1, FUNCT_2, FINSEQ_4, JORDAN3,
      FINSEQ_5, FINSEQ_8;
 constructors GOBOARD9, BINARITH, REAL_1, RFINSEQ, JORDAN3, FINSEQ_8, POLYNOM1,
      CQC_LANG, XXREAL_0;
 registrations ARYTM_3, XREAL_0, RELSET_1, FINSEQ_1, FINSEQ_5, XXREAL_0;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;
 theorems REAL_1, NAT_1, NAT_2, REVROT_1, JORDAN3, JORDAN4, FINSEQ_1, FINSEQ_2,
      FINSEQ_4, FINSEQ_5, FINSEQ_6, BINARITH, RFINSEQ, FUNCT_2, FINSEQ_8,
      SPRECT_3, GENEALG1, TOPREAL3, CALCUL_1, FINSEQ_3, XREAL_1, CARD_1,
      XXREAL_0;

begin

 reserve a,b,c for set;

theorem Th1:
  for D be non empty set,p,q,r,s being FinSequence of D holds
    p^q^r^s=p^(q^r)^s & p^q^r^s=(p^q)^(r^s) & p^(q^r)^s=(p^q)^(r^s)
proof let D be non empty set,p,q,r,s be FinSequence of D;
  p^q^r^s=(p^q)^(r^s) by FINSEQ_1:45;
  hence thesis by FINSEQ_1:45;
end;

theorem Th2:
  for D being set, f being FinSequence of D holds
    f|(len f) = f
proof
  let D be set, f be FinSequence of D;
  f|len f = f|(Seg len f) by FINSEQ_1:def 15;
  hence f = f|len f by FINSEQ_2:23;
end;

theorem Th3:
  for D being non empty set,p,q being FinSequence of D st
    len p=0 holds q = p ^ q
proof let D be non empty set,p,q be FinSequence of D;
  assume len p = 0; then
  p = <*>D by FINSEQ_1:32
   .= {};
  hence q = p ^ q by FINSEQ_1:47;
end;

theorem Th4:
  for D being non empty set,f being FinSequence of D,n,m being Nat st
    n<=m holds len (f/^m)<=len (f/^n)
proof let D be non empty set,f be FinSequence of D,n,m be Nat;
 assume A1: n<=m; then
 A2: n-n<=m-n by XREAL_1:11;
A3: len (f/^m) = len f -' m by JORDAN3:19;
A4: len (f/^n) - len (f/^m) = len f -' n - (len f -' m) by A3,JORDAN3:19;
  now per cases;
  suppose A5: len f <= n; then
    A6: len f <= m by A1,XXREAL_0:2;
    len f -' n - (len f -' m) = len f -' n - 0 by A6,NAT_2:10
                             .= 0 by A5,NAT_2:10;
    hence len (f/^n) - len (f/^m) >= 0 by A3,JORDAN3:19;
  end;
  suppose A7: len f > n; then
    A8: len f - n >= n-n by XREAL_1:11;
    per cases;
    suppose len f <= m; then
      len f -' n - (len f -' m) = len f -' n - 0 by NAT_2:10
                               .= len f -' n;
      hence len (f/^n) - len (f/^m) >= 0 by A8,BINARITH:def 3,A4;
    end;
    suppose len f > m;then
      len f-'n-(len f-'m)= len f-'n-(len f-m) by BINARITH:50
                        .= len f-n-(len f-m) by A7,BINARITH:50
                        .= m-n;
      hence len (f/^n) - len (f/^m) >= 0 by A3,JORDAN3:19,A2;
    end;
  end;
  end;
 then len (f/^n) - len (f/^m) + len (f/^m) >= 0 + len (f/^m) by XREAL_1:8;
 hence len (f/^m)<=len (f/^n);
end;

theorem Th5:
  for D being non empty set,f,g being FinSequence of D st
    len g>=1 holds mid(f^g,len f+1,len f+len g)=g
proof let D be non empty set,f,g be FinSequence of D;
  assume A1: len g>=1;
A2: g|len g=g|(Seg len g) by FINSEQ_1:def 15;
  per cases;
  suppose A3: len f+1<=len f+len g;then
A4: len f < len f+len g by NAT_1:38;then
   len f-len f < len f+len g-len f by REAL_1:92;then
A5: len f+len g-'len f=len g by BINARITH:def 3;
  mid(f^g,len f+1,len f+len g)=((f^g)/^(len f+1-'1))|(len f+len g-'(len f+1)+1)
     by A3,JORDAN3:def 1
  .=((f^g)/^len f)|(len f+len g-'(len f+1)+1) by BINARITH:39
  .=((f^g)/^len f)|(len f+len g-'len f) by A4,NAT_2:9
  .=g|len g by A5,FINSEQ_5:40;
 hence mid(f^g,len f+1,len f+len g)=g by A2,FINSEQ_2:23;
end;
suppose len f+1>len f+len g;then
  len f+1-len f>len f+len g-len f by REAL_1:92;
  hence mid(f^g,len f+1,len f+len g)=g by A1;
end;
end;

theorem Th6:
  for D being non empty set,f,g being FinSequence of D, i,j being Nat st
    1<=i & i<=j & j<=len f holds mid(f^g,i,j)=mid(f,i,j)
proof let D be non empty set,f,g be FinSequence of D,i,j be Nat;
 assume A1: 1<=i & i<=j & j<=len f;then
A2: j-i>=i-i by XREAL_1:11;
 len f-i>=j-i by A1,XREAL_1:11;then
A3: len f-i+1>=j-i+1 by XREAL_1:8;
  len f>=i by A1,XXREAL_0:2;then
      len f-0 >=i-1 by REAL_1:92; then
A4:  len f >=i-'1 by A1,BINARITH:50;
A5: len (f/^(i-'1))+(i-1)-(i-1)=len (f/^(i-'1));
 len (f/^(i-'1))+(i-'1)=len f-'(i-'1)+(i-'1) by JORDAN3:19
                      .=len f by A4,BINARITH:53;then
 len (f/^(i-'1))=len f-(i-1) by A5,A1,BINARITH:50
               .=len f-i+1;then
A6: len (f/^(i-'1))>=j-'i+1 by A3,A2,BINARITH:def 3;
 A7: i-'1<=i by BINARITH:52;
     i<=len f by A1,XXREAL_0:2; then
 A8: i-'1<=len f by A7,XXREAL_0:2;
 mid(f^g,i,j) = ((f^g)/^(i-'1))|(j-'i+1) by A1,JORDAN3:def 1
             .= ((f/^(i-'1))^g)|(j-'i+1) by A8,GENEALG1:1
             .= (f/^(i-'1))|(j-'i+1) by A6,FINSEQ_5:25;
 hence mid(f^g,i,j)=mid(f,i,j) by A1,JORDAN3:def 1;
end;

theorem
  for D being non empty set,f being FinSequence of D, i,j,n being Nat st
    1<=i & i<=j & i<=len (f|n) & j<=len (f|n) holds
      mid(f,i,j)=mid(f|n,i,j)
proof let D be non empty set,f be FinSequence of D,i,j,n be Nat;
  assume A1: 1<=i & i<=j & i<=len (f|n) & j<=len (f|n);
A2: len (f|n) <= n by FINSEQ_5:19;
A3: i-'1 = i-1 by A1,BINARITH:50;
    n>=i by A1,A2,XXREAL_0:2; then
    n-0>=i-1 by REAL_1:92; then
A4: n-'(i-'1) = n-(i-1) by A3,BINARITH:50
                 .= n-i+1;
A5: j-'i+1 = j-i+1 by A1,BINARITH:50;
    j<=n by A1,A2,XXREAL_0:2; then
    j-i <= n-i by XREAL_1:11; then
A6: j-i+1<=n-i+1 by XREAL_1:8;
    mid(f|n,i,j)= ((f|n)/^(i-'1))|(j-'i+1) by A1,JORDAN3:def 1
               .= ((f/^(i-'1))|(n-'(i-'1)))|(j-'i+1) by JORDAN3:21
               .= (f/^(i-'1))|(j-'i+1) by A4,A5,A6,FINSEQ_5:80;
    hence thesis by A1,JORDAN3:def 1;
end;

theorem
  for D being non empty set,f being FinSequence of D st
    f=<*a*> holds a in D
proof let D be non empty set,f be FinSequence of D;
  assume A1: f=<*a*>;then
A2: len f=1 & f.1=a by FINSEQ_1:57;
    1<=len f by A1,FINSEQ_1:57;then
A3: 1 in dom f by FINSEQ_3:27;
  f is Function of dom f,D by FINSEQ_2:30;
  hence thesis by A2,A3,FUNCT_2:7;
end;

theorem
  for D being non empty set,f being FinSequence of D st
    f=<*a,b*> holds a in D & b in D
proof let D be non empty set,f be FinSequence of D;
  assume A1: f=<*a,b*>;then
  A2: len f=2 & f.1=a & f.2=b by FINSEQ_1:61;
  A3: 1 in dom f & 2 in dom f by A1,CALCUL_1:14;
  f is Function of dom f,D by FINSEQ_2:30;
  hence thesis by A2,A3,FUNCT_2:7;
end;

theorem Th10:
  for D being non empty set,f being FinSequence of D st
    f=<*a,b,c*> holds a in D & b in D & c in D
proof let D be non empty set,f be FinSequence of D;
  assume A1: f=<*a,b,c*>;then
A2: len f=3 & f.1=a & f.2=b & f.3=c by FINSEQ_1:62;
A3: 1 in dom f & 2 in dom f & 3 in dom f by A1,TOPREAL3:6;
  f is Function of dom f,D by FINSEQ_2:30;
  hence thesis by A2,A3,FUNCT_2:7;
end;

theorem
  for D being non empty set,f being FinSequence of D st
    f=<*a*> holds f|1=<*a*>
proof let D be non empty set,f be FinSequence of D;
  assume A1: f=<*a*>;then
  len f <=1 by FINSEQ_1:57;
  hence f|1=<*a*> by FINSEQ_1:79,A1;
end;

theorem :: FINSEQ_6:50
  for D being non empty set,f being FinSequence of D st
    f=<*a,b*> holds f/^1=<*b*>
  proof let D be non empty set,f be FinSequence of D;
    assume A1: f = <*a,b*>;then
A2: len f=2 & f.1=a & f.2=b by FINSEQ_1:61;
A3:1 in dom f & 2 in dom f by A1,CALCUL_1:14;
    f is Function of (dom f),D by FINSEQ_2:30;then
    reconsider a2=a, b2=b as Element of D by A2,FUNCT_2:7,A3;
    f/^1 = <*a2,b2*>/^1 by A1
        .= <*b2*> by FINSEQ_6:50;
    hence thesis;
  end;

theorem
  for D being non empty set,f being FinSequence of D st
    f=<*a,b,c*> holds f|1=<*a*>
proof let D be non empty set,f be FinSequence of D;
  assume A1: f=<*a,b,c*>;
  f|1 = f|Seg 1 by FINSEQ_1:def 15
     .= <*a*> by A1,FINSEQ_6:6;
  hence f|1=<*a*>;
end;

theorem Th14:
  for D being non empty set,f being FinSequence of D st
    f=<*a,b,c*> holds f|2=<*a,b*>
proof let D be non empty set,f be FinSequence of D;
  assume A1: f=<*a,b,c*>;
  f|2 = f|Seg 2 by FINSEQ_1:def 15
     .= <*a,b*> by A1,FINSEQ_6:7;
  hence f|2=<*a,b*>;
end;

theorem
  for D being non empty set,f being FinSequence of D st
    f=<*a,b,c*> holds f/^1=<*b,c*>
  proof let D be non empty set,f be FinSequence of D;
    assume A1: f = <*a,b,c*>;then
    reconsider a2=a,b2=b,c2=c as Element of D by Th10;
    len f=3 & f.1=a2 & f.2=b2 & f.3=c2 by FINSEQ_1:62,A1;
   hence thesis by A1,FINSEQ_6:51;
  end;

theorem
  for D being non empty set,f being FinSequence of D st
    f=<*a,b,c*> holds f/^2=<*c*>
  proof let D be non empty set,f be FinSequence of D;
    assume A1: f = <*a,b,c*>;then
    reconsider a2=a,b2=b,c2=c as Element of D by Th10;
    f/^2 = (<*a2*>^<*b2,c2*>)/^(1+1) by A1,FINSEQ_1:60
        .= (<*a2*>^<*b2,c2*>)/^(len <*a2*> +1) by FINSEQ_1:57
        .= <*b2,c2*>/^1 by FINSEQ_5:39
        .= <*c2*> by FINSEQ_6:50;
   hence thesis;
  end;

theorem
  for D being non empty set,f being FinSequence of D st len f=0 holds
    Rev f=f
proof let D be non empty set,f be FinSequence of D;
  assume len f=0; then
A1: f =<*>D by FINSEQ_1:32
       .= {};
  Rev (<*>D)=<*>D;
  hence Rev f=f by A1;
end;

theorem Th18:
  for D being non empty set, r being FinSequence of D,i being Nat st
    i<=len r holds Rev(r/^i)=(Rev r)|(len r-'i)
proof let D be non empty set,r be FinSequence of D,i be Nat;
  assume A1:  i<=len r;then
  len r -i>=0 by XREAL_1:50;then
A2: len r-i=len r -'i by BINARITH:def 3;
  set s=Rev r;
  len r-'i<=len r by JORDAN3:13;then
  len r-'i<=len s by FINSEQ_5:def 3; then
A3: len s-'i <= len s by FINSEQ_5:def 3;
A4: len (s|((len r)-'i)) = len (s|((len s)-'i)) by FINSEQ_5:def 3
                      .= len s -'i by A3,FINSEQ_1:80;
A5: len Rev(r/^i)= len (r/^i) by FINSEQ_5:def 3
  .=len r-'i by JORDAN3:19
  .=len (s|((len r)-'i)) by A4,FINSEQ_5:def 3;
  for k being Nat st 1<=k & k<=len Rev(r/^i) holds
   (Rev(r/^i)).k= (s|((len r)-'i)).k
   proof let k be Nat;assume
     A6: 1<=k & k<=len Rev(r/^i);then
     k in dom Rev(r/^i) by FINSEQ_3:27;then
     A7: (Rev(r/^i)).k = (r/^i).(len (r/^i) -k+1) by FINSEQ_5:def 3
                  .=(r/^i).(len r -i-k+1) by RFINSEQ:def 2,A1;
       1<=k & k<=len (r/^i) by A6,FINSEQ_5:def 3;then
A8:   1<=k & k<=len r -i by RFINSEQ:def 2,A1;then
A9:   len r-'i-k>=0 by A2,XREAL_1:50;then
A10:   len r -'i-'k=len r-'i-k by BINARITH:def 3;then
A11:   len r -'i-'k +1>=0+1 by A9,XREAL_1:9;
      k-1>=0 by A6,XREAL_1:50;then
      len r-i+0<=len r-i+(k-1) by XREAL_1:9;then
      len r-i-(k-1)<=len r-i+(k-1)-(k-1) by XREAL_1:11;then
      len r-'i-'k+1<=len (r/^i) by RFINSEQ:def 2,A1,A10,A2;then
      len r -i-k+1 in Seg len (r/^i) by A11,A10,FINSEQ_1:3,A2;then
A12:   len r -i-k+1 in dom (r/^i) by FINSEQ_1:def 3;
A13:  i+(len r -'i-'k+1)=i+(len r-i-k+1) by A9,BINARITH:def 3,A2
                       .=len r-i+i-k+1;
      0<=i by NAT_1:18;then
      len r+0<=len r +i by XREAL_1:9;then
      len r-i<=len r+i-i by XREAL_1:11;then
      len (r/^i)<=len r by A1,RFINSEQ:def 2;then
      len (Rev(r/^i))<=len r by FINSEQ_5:def 3;then
A14:  k<=len r by A6,XXREAL_0:2;then
A15: len r-k>=0 by XREAL_1:50;then
A16:  len r-k=len r-'k by BINARITH:def 3;
      k<k+1 by NAT_1:38;then
      k-1<k+1-1 by XREAL_1:11;then
      k-1<len r by A14,XXREAL_0:2;then
      len r-(k-1)>0 by XREAL_1:52;then
      len r-k+1>0;then
      len r-'k+1>0 by A15,BINARITH:def 3;then
A17:  len r-k+1>=0+1 by A16,NAT_1:38;
      k-1>=0 by A6,XREAL_1:50;then
      len r+0<=len r+(k-1) by XREAL_1:9;then
      len r-(k-1)<=len r+(k-1)-(k-1) by XREAL_1:11;then
      len r-k+1<=len r;then
      len r-'k+1<=len r by A15,BINARITH:def 3;then
A18:  len r-k+1 in dom r by A17,A16,FINSEQ_3:27;
A19:  r=r/^0 by FINSEQ_5:31;
A20:  (r/^i).(len r -i-k+1)
         = (r/^i)/.(len r -'i-'k+1) by A12,A10,A2,FINSEQ_4:def 4
        .=(r/^0) /. (i+(len r -'i-'k+1)) by A19,A12,A10,A2,FINSEQ_5:30
        .=r.(len r-k+1) by A13,A19,FINSEQ_4:def 4,A18;
        k in dom r by A6,A14,FINSEQ_3:27; then
A21:    k in dom Rev r by FINSEQ_5:60;
        k<=len r-'i by A8,BINARITH:def 3;then
       (s|((len r)-'i)).k=(Rev r).k by FINSEQ_3:121
                            .=r.(len r-k+1) by FINSEQ_5:def 3,A21;
    hence (Rev(r/^i)).k= (s|((len r)-'i)).k by A20,A7;
   end;
  hence Rev(r/^i)=(Rev r)|(len r-'i) by A5,FINSEQ_1:18;
end;

theorem Th19:
  for D being non empty set,f,CR being FinSequence of D st
    not CR is_substring_of f,1 & CR separates_uniquely holds
      instr(1,f^CR,CR)=len f +1
proof let D be non empty set,f,CR be FinSequence of D;
  assume A1: not (CR is_substring_of f,1) & CR separates_uniquely;then
   A2: len CR>0 & not (ex i being Nat st 1<=i & i<=len f
   & mid(f,i,(i-'1)+len CR)=CR) by FINSEQ_8:def 7;
A3: len CR>=0+1 by NAT_1:38,A2;
A4: (f^CR)/^(len f)=CR by FINSEQ_5:40;
  for j being Nat st j >= 1 & j > 0
   & CR is_preposition_of (f^CR)/^(j-'1) holds j >= len f+1
   proof let j be Nat;
     assume A5: j >= 1 & j > 0 & (CR is_preposition_of (f^CR)/^(j-'1));then
A6:   len CR>0 implies 1<=len ((f^CR)/^(j-'1))
       & mid((f^CR)/^(j-'1),1,len CR)=CR by FINSEQ_8:def 8;
A7:   0+j<len CR +j by XREAL_1:10,A2;then
       j+1<=len CR+j by NAT_1:38;then
A8:   j<=len CR+j-1 by XREAL_1:21;
       1<=len CR +j by A7,A5,XXREAL_0:2;then
A9:  len CR +j -1>=0 by XREAL_1:50;then
A10:   len CR+j-'1=len CR+j-1 by BINARITH:def 3;
       len CR-1>=0 by A3,XREAL_1:50;then
       len CR-'1=len CR-1 by BINARITH:def 3;then
       j<=len CR -1+j by NAT_1:37;then
       len CR+j-'1-j>=0 by A10,XREAL_1:50;then
       ((len CR+j-'1)-'j+1)=len CR+j-1-j+1 by BINARITH:def 3,A10
       .=len CR;then
A11:  ((f^CR)/^(j-'1))|((len CR+j-'1)-'j+1)=CR by A6,A3,JORDAN3:25;
       set i0=len f+1,j0=i0+len CR -'1;
A12:   i0+len CR-'1=len f+len CR+1-'1
                  .=len f+len CR by BINARITH:39;
A13:   i0<=i0+len CR -'1 by A12,A3,XREAL_1:9;
       j0= len CR+len f +1-'1 .=len CR+len f by BINARITH:39;then
       mid(f^CR,i0,i0+len CR-'1)=CR by A3,Th5;then
A14:   smid(f^CR,i0,i0+len CR-'1)=CR by FINSEQ_8:4,A13;
A15:   j-1>=0 by A5,XREAL_1:50;then
A16:    j-'1=j-1 by BINARITH:def 3;
       1<=j+len CR by A5,NAT_1:37;then
       j+len CR-1>=0 by XREAL_1:50;then
A17:    j+len CR-'1=j+len CR-1 by BINARITH:def 3;
A18:    len CR>0 implies len CR>=0+1 by NAT_1:38;then
       j+len CR>=j+1 by XREAL_1:9,A1,FINSEQ_8:def 7;then
A19:   j+len CR-1>=j+1-1 by XREAL_1:11;
       j+len CR>=j+1 by A1,FINSEQ_8:def 7,XREAL_1:9,A18;then
A20:  j+len CR-1>=j+1-1 by XREAL_1:11;
       j-'1+len CR=j-1+len CR by A15,BINARITH:def 3
                 .=len CR+j-'1 by A9,BINARITH:def 3;then
A21:   mid(f^CR,j,j-'1+len CR)=CR by A11,A10,A8,JORDAN3:def 1;
       j-'1+len CR=j-1+len CR by A15,BINARITH:def 3 .=j+len CR -1;then
A22:   smid(f^CR,j,j+len CR-'1)=CR by FINSEQ_8:4,A21,A17,A20;
       now assume A23: j<i0;then
A24:     i0-j>0 by XREAL_1:52;
         1<=j & j<i0 & i0+len CR-'1<=len (f^CR) by A5,A23,A12,FINSEQ_1:35;then
A25:      i0-'j>=len CR by FINSEQ_8:def 6,A1,A22,A14;
         i0-'j =len f+1-j by BINARITH:def 3,A24;then
         len f+1-j+j>=len CR+j by A25,XREAL_1:9;then
         len f+1-1>=j+len CR-1 by XREAL_1:11;then
         mid(f,j,(j-'1)+len CR)=CR by A21,Th6,A5,A19,A16;then
         j>len f by A1,FINSEQ_8:def 7,A5;
         hence contradiction by A23,NAT_1:38;
       end;
      hence j >= len f+1;
    end;then
   (len f+1 <> 0 implies
   1 <= len f+1 & CR is_preposition_of (f^CR)/^(len f+1-'1) &
   for j being Nat st j >= 1 & j > 0 &
   CR is_preposition_of (f^CR)/^(j-'1) holds j >= len f+1) &
    (len f+1=0 implies not CR is_substring_of f^CR,1)
     by NAT_1:29,BINARITH:39,A4;
  hence instr(1,f^CR,CR)=len f +1 by FINSEQ_8:def 10;
end;

theorem
  for D being non empty set,f,g being FinSequence of D holds
    g is_preposition_of (f^g)/^len f
proof let D be non empty set,f,g be FinSequence of D;
  (f^g)/^len f=g by FINSEQ_5:40;
  hence g is_preposition_of (f^g)/^len f;
end;

theorem
  for D being non empty set,f,CR being FinSequence of D st
    not (CR is_substring_of f,1) & CR separates_uniquely holds
      f^CR is_terminated_by CR
proof let D be non empty set,f,CR be FinSequence of D;
  assume A1: not (CR is_substring_of f,1) & CR separates_uniquely;
  len CR<=len f+len CR by NAT_1:37;then
A2:len CR <=len (f^CR) by FINSEQ_1:35;
  len (f^CR) + 1 -'len CR=len f+len CR +1-'len CR by FINSEQ_1:35
                         .=len CR+(len f+1)-'len CR
                         .=len f+1 by BINARITH:39;
  then instr(1,f^CR,CR)=len (f^CR) + 1 -'len CR by Th19,A1;
  hence f^CR is_terminated_by CR by A2,FINSEQ_8:def 12;
end;

::==========================================================================
::  file and record
::==========================================================================

notation let D be set;
  synonym File of D for FinSequence of D;
end;

definition let D be non empty set,
               r,f,CR be File of D;
  pred r is_a_record_of f,CR means :Def1:
    (CR^r is_substring_of addcr(f,CR),1 or
    r is_preposition_of addcr(f,CR)) &
    r is_terminated_by CR;
end;

theorem Th22:
  for D being non empty set,r being FinSequence of D holds
    ovlpart(<*>D,r)=<*>D & ovlpart(r,<*>D)=<*>D
proof let D be non empty set,r be FinSequence of D;
  <*>D^r=r & r^<*>D=r by FINSEQ_1:47;
  hence ovlpart(<*>D,r)=<*>D & ovlpart(r,<*>D)=<*>D by FINSEQ_8:14;
end;

theorem Th23:
  for D being non empty set,CR being FinSequence of D holds
    CR is_a_record_of <*>D,CR
proof let D be non empty set,CR be FinSequence of D;
  addcr(<*>D,CR)=ovlcon(<*>D,CR) by FINSEQ_8:def 11
             .=<*>D^(CR/^(len ovlpart(<*>D,CR))) by FINSEQ_8:def 3
             .=<*>D^(CR/^(len <*>D)) by Th22
             .=<*>D^(CR/^0) by FINSEQ_1:32
             .=(CR/^0) by FINSEQ_1:47
             .=CR by FINSEQ_5:31;
then A1: CR is_preposition_of addcr(<*>D,CR);
          CR is_terminated_by CR by FINSEQ_8:28;
  hence CR is_a_record_of <*>D,CR by Def1,A1;
end;

theorem
  for D being non empty set,a,b be set,f,r,CR being File of D st
    a <> b & D = {a,b} & CR = <*b*> & f = <*b,a,b*> & r = <*a,b*> holds
      CR is_a_record_of f,CR & r is_a_record_of f,CR
proof let D be non empty set,a,b be set,f,r,CR be File of D;
  assume A1: a <> b & D = {a,b} & CR = <*b*> & f = <*b,a,b*> & r = <*a,b*>;
  then reconsider b2=b,a2=a as Element of D by Th10;
A2: CR.1=b by A1,FINSEQ_1:def 8;
A3: f=CR^r by A1,FINSEQ_1:60;
A4: len CR = 1 by A1,FINSEQ_1:57;
A5: len f = len CR + len r by A3,FINSEQ_1:35
           .= 1 + len <*a,b*> by A1,FINSEQ_1:57
           .= 1 + 2 by FINSEQ_1:61
           .= 3;
A6: len r = 2 by A1,FINSEQ_1:61;

A7:
  f =<*b,a*>^<*b*> by A1,FINSEQ_1:60
   .=(f|2)^CR by Th14,A1;
  ovlpart(f,CR)=CR by A7,FINSEQ_8:14;
  then CR/^(len ovlpart(f,CR))={} by REVROT_1:2;
  then f^(CR/^(len ovlpart(f,CR)))=f by FINSEQ_1:47;
  then A8: ovlcon(f,CR)=f by FINSEQ_8:def 3;
  mid(f,1,len CR) = (f/^(1-'1))|(1-'1+1) by A4,JORDAN3:def 1
                     .= (f/^(1-'1))|(1+(0)) by NAT_2:10
                     .= (f/^(0))|1 by BINARITH:39
                     .= (f)|1 by FINSEQ_5:31
                     .= (<*b2,a2,b2*>)|Seg 1 by A1,FINSEQ_1:def 15
                     .= CR by A1,FINSEQ_6:6;
  then 1<= len f & CR is_preposition_of f by A5,FINSEQ_8:def 8;
  then A9: 1<= len f & CR is_preposition_of addcr(f,CR)
                      by A8,FINSEQ_8:def 11;
  1<= len f & CR^r is_substring_of f,1 by A3,FINSEQ_8:19,A5;
  then A10: 1<= len f & CR^r is_substring_of addcr(f,CR),1
    by A8,FINSEQ_8:def 11;
A11: r/^(2-'1) = r/^(1+1-'1)
           .= <*a2,b2*>/^1 by A1,BINARITH:39
           .= CR by A1,FINSEQ_6:50;
A12: r/^(1-'1)=r/^(0+1-'1) .=r/^0 by BINARITH:39 .=r by FINSEQ_5:31;
A13: len CR=1 by A1,FINSEQ_1:57;
   (r/^(1-'1)).1=a by A12,A1,FINSEQ_1:61;then
A14: not (CR is_preposition_of (r/^(1-'1))) by A13,A2,A1,FINSEQ_8:21;
  A15: 2<=2 & CR is_preposition_of r/^(2-'1) by A11;
  for j being Nat st j >= 1 & j > 0
   & (CR is_preposition_of r/^(j-'1)) holds j >= 2
   proof let j be Nat;
    assume A16: j >= 1 & j > 0 & CR is_preposition_of r/^(j-'1);
    j>1 by A14,A16,XXREAL_0:1; then
    1+1<=j by NAT_1:38;
    hence j >= 2;
   end; then
A17: instr(1,r,CR) = 2 by FINSEQ_8:def 10,A15;
A18: len r + 1 -' len CR = 2 by A4,A6,BINARITH:39;
A19: CR is_terminated_by CR by FINSEQ_8:28;
  r is_terminated_by CR by A17,A4,A6,A18,FINSEQ_8:def 12;
  hence thesis by A19,A9,A10,Def1;
end;

theorem Th25:
  for D being non empty set,f,CR being File of D holds
    f is_preposition_of f^CR
proof let D be non empty set,f,CR be File of D;
  per cases;
  suppose A1: len f>0;
A2: len CR>=0 by NAT_1:18;
A3: 0+1<= len f by A1,NAT_1:38; then
A4: 0+1<= len f + len CR by A2,XREAL_1:9;
    1<= len (f^CR) & mid(f^CR,1,len f)=f by A4,FINSEQ_1:35,A3,FINSEQ_8:1;
    hence f is_preposition_of f^CR by FINSEQ_8:def 8;
  end;
  suppose len f<=0;
    hence f is_preposition_of f^CR by FINSEQ_8:def 8;
  end;
end;

theorem
  for D being non empty set,f,CR being File of D holds
    f is_preposition_of addcr(f,CR)
proof let D be non empty set,f,CR be File of D;
  addcr(f,CR)=ovlcon(f,CR) by FINSEQ_8:def 11
            .=f^(CR/^(len ovlpart(f,CR))) by FINSEQ_8:def 3;
  hence f is_preposition_of addcr(f,CR) by Th25;
end;

theorem Th27:
  for D being non empty set,r,CR being File of D st
    CR is_postposition_of r holds 0<=len r-len CR
proof
  let D be non empty set,f,g be File of D;
  assume g is_postposition_of f; then
  len g <= len f by FINSEQ_8:23;then
  len g - len g <= len f - len g by XREAL_1:11;
  hence thesis;
end;

theorem Th28:
  for D being non empty set,CR,r being File of D st
    CR is_postposition_of r holds r=addcr(r,CR)
proof
let D be non empty set,CR,r be File of D;
assume A1: CR is_postposition_of r;
A2: len CR = len Rev CR by FINSEQ_5:def 3;
A3: len r = len Rev r by FINSEQ_5:def 3;
    0<=len r - len CR by A1,Th27;then
A4: 0+len CR<=len r-len CR+len CR by XREAL_1:9;
A5: Rev CR is_preposition_of Rev r by A1,FINSEQ_8:def 9;
  per cases;
  suppose A6: len CR>0;
A7: len CR<=len r & mid(r,(len r+1) -' len CR,len r)=CR by A6,A1,FINSEQ_8:24;
A8: len CR >=0+1 by A6,NAT_1:38;
A9: len Rev r = len Rev r - len CR +len CR;
      Rev mid(Rev r,1,len CR)=Rev Rev CR by A6,FINSEQ_8:def 8,A5,A2;then
A10: mid(Rev r,len CR,1)=Rev Rev CR by JORDAN4:30
                     .=CR by FINSEQ_6:29;
       now per cases;
         case A11: len CR>1;
           len CR>=1+1 by NAT_1:38,A11; then
           len CR-1>=1+1-1 by XREAL_1:11; then
A12:     len CR-'1+1=len CR by JORDAN3:6;
A13:       len Rev r -'len CR=len Rev r -len CR by A4,BINARITH:50,A3;
           mid(Rev r,len CR,1)=Rev (((Rev r)/^(1-'1))|(len CR-'1+1))
                            by A11,JORDAN3:def 1
              .=Rev (((Rev r)/^0)|len CR) by NAT_2:10,A12
              .=Rev (Rev r|len CR) by FINSEQ_5:31
              .=Rev (Rev r)/^(len (Rev r)-'len CR) by A9,A13,FINSEQ_6:91
              .=r/^(len r-'len CR) by A3,FINSEQ_6:29; then
A14:       ovlpart(r,CR) = ovlpart((r|(len r-'len CR))^CR,CR)
                                      by A10,RFINSEQ:21
                            .= CR by FINSEQ_8:14;
           ovlcon(r,CR)=r^(CR/^(len ovlpart(r,CR))) by FINSEQ_8:def 3
                      .=r^{} by A14,REVROT_1:2
                      .=r by FINSEQ_1:47;
           hence r=addcr(r,CR) by FINSEQ_8:def 11;
         end;
         case len CR<=1;then
A15:     CR=mid(r,(len r+1) -'1,len r) by A7,A8,XXREAL_0:1
            .=mid(r,len r,len r) by BINARITH:39
            .=r/^(len r-'1) by JORDAN3:26;
A16:     CR/^len CR={} by REVROT_1:2;
A17:    r=(r|(len r-'1))^CR by A15,RFINSEQ:21;
           ovlcon(r,CR)=r^(CR/^(len ovlpart(r,CR))) by FINSEQ_8:def 3
                      .=r^(CR/^len CR) by A17,FINSEQ_8:14
                      .=r by A16,FINSEQ_1:47;
           hence r=addcr(r,CR) by FINSEQ_8:def 11;
         end;
       end;
       hence r=addcr(r,CR);
     end;
     suppose len CR<= 0; then
       len CR=0 by NAT_1:18;then
A18: CR={} by FINSEQ_1:25;then
A19: len ovlpart(r,CR)=len <*>D by Th22
                       .=0 by FINSEQ_1:32;
       thus addcr(r,CR)=ovlcon(r,CR) by FINSEQ_8:def 11
                 .=r^(CR/^(len ovlpart(r,CR))) by FINSEQ_8:def 3
                 .=r^CR by A19,FINSEQ_5:31
                 .=r by A18,FINSEQ_1:47;
     end;
end;

theorem Th29:
  for D being non empty set,CR,r being File of D st
    r is_terminated_by CR holds r=addcr(r,CR)
proof
  let D be non empty set,CR,r be File of D;
  assume A1: r is_terminated_by CR;
  per cases;
  suppose len CR<=0; then
    len CR=0 by NAT_1:18;then
    len Rev CR=0 by FINSEQ_5:def 3;then
    Rev CR is_preposition_of Rev r by FINSEQ_8:def 8;then
    CR is_postposition_of r by FINSEQ_8:def 9;
   hence r=addcr(r,CR) by Th28;
  end;
  suppose A2: len CR>0;then
A3: 0+1<=len CR by NAT_1:38;
     len CR -1>=0 by A3,XREAL_1:50;then
    A4: len CR -'1=len CR -1 by BINARITH:def 3;
    A5: len r>=len CR by A1,FINSEQ_8:def 12,A2;
    A6: 0<len r by A2,A1,FINSEQ_8:def 12;
    A7: len r-len CR>=0 by A5,XREAL_1:50;then
    A8: len r-len CR=len r-'len CR by BINARITH:def 3;
    len r +1>len CR by A5,NAT_1:38;then
    A9: len r+1-len CR>0 by XREAL_1:52;then
    A10: len r+1-'len CR=len r+1-len CR by BINARITH:def 3;then
    len r+1-len CR>=0+1 by A9,NAT_1:38;then
    len r+1-'len CR-1>=0 by A10,XREAL_1:50;then
    A11: len r+1-'len CR-'1=len r-len CR by BINARITH:def 3,A10;
    instr(1,r,CR) = len r + 1 -'len CR by A1,FINSEQ_8:def 12,A2;then
     CR is_preposition_of r/^(len r + 1 -'len CR-'1)
                                by FINSEQ_8:def 10,A9,A10;then
     1<=len (r/^(len r + 1 -'len CR-'1)) &
     mid(r/^(len r + 1 -'len CR-'1),1,len CR)=CR by FINSEQ_8:def 8,A2;then
     ((r/^(len r + 1 -'len CR-'1))/^(1-'1))|(len CR-'1+1)=CR
                                          by JORDAN3:def 1,A3;then
     ((r/^(len r + 1 -'len CR-'1))/^(0))|(len CR-'1+1)=CR
                                          by NAT_2:10;then
A12: ((r/^(len r -'len CR)))|(len CR)=CR by A8,A11,FINSEQ_5:31,A4;
A13: len (r/^(len r -'len CR))=len r-'(len r -'len CR) by JORDAN3:19;
A14: len r-(len r -'len CR)=len r-len r+len CR by A8
     .=len CR;then
A15: len r-(len r -'len CR)=len r-'(len r -'len CR)
                                   by A2,BINARITH:def 3;
A16: len r-'len CR <=len r by BINARITH:52;then
     len r-(len r-'len CR)>=0 by XREAL_1:50;then
A17: len r-'(len r-'len CR)=len r-(len r-'len CR) by BINARITH:def 3
                         .=len r-(len r-len CR) by A7,BINARITH:def 3
                         .=len CR;
A18:  mid(Rev r,1,len CR) =(Rev r/^(1-'1))|(len CR-'1+1)
                                   by A3,JORDAN3:def 1
                    .=(Rev r/^(0))|(len CR) by A4,NAT_2:10
                    .=(Rev r)|(len r-'(len r-'len CR)) by A17,FINSEQ_5:31
                    .=Rev(r/^(len r-'len CR)) by Th18,A16
                    .=Rev CR by A15,A14,A13,A12,FINSEQ_1:79;
A19:  0+1<=len r by A6,NAT_1:38;
     len Rev CR >0 & 1<=len Rev r & mid(Rev r,1,len Rev CR)=Rev CR
                    by FINSEQ_5:def 3,A2,A18,A19;
     then Rev CR is_preposition_of Rev r by FINSEQ_8:def 8;
     then CR is_postposition_of r by FINSEQ_8:def 9;
   hence r=addcr(r,CR) by Th28;
  end;
end;

theorem
  for D being non empty set,f,g being File of D st
    f is_terminated_by g holds len g <=len f
proof
  let D be non empty set,f,g be File of D;
  assume A1: f is_terminated_by g;
  per cases;
  suppose len g>0;
    hence len g <=len f by FINSEQ_8:def 12,A1;
  end;
  suppose len g <=0;
    hence len g <=len f by NAT_1:18;
  end;
end;

theorem Th31:
  for D being non empty set,f,CR being File of D holds
    len addcr(f,CR) >= len f & len addcr(f,CR) >= len CR
proof let D be non empty set,f,CR be File of D;
A1: addcr(f,CR)=ovlcon(f,CR) by FINSEQ_8:def 11;
A2: len ovlcon(f,CR)=len f+(len CR -'len ovlpart(f,CR)) by FINSEQ_8:15;
  thus len addcr(f,CR) >= len f by A1,A2,NAT_1:37;
  ovlcon(f,CR)=(f|(len f-'len ovlpart(f,CR)))^CR by FINSEQ_8:11;then
  len ovlcon(f,CR)= len (f|(len f-'len ovlpart(f,CR))) + len CR
                         by FINSEQ_1:35;
 hence len addcr(f,CR) >= len CR by A1,NAT_1:37;
end;

theorem Th32:
  for D being non empty set,f,g being FinSequence of D holds
    g = ovlpart(f,g)^ovlrdiff(f,g)
proof let D be non empty set,f,g be FinSequence of D;
A1: ovlpart(f,g)=smid(g,1,len ovlpart(f,g)) by FINSEQ_8:def 2
           .=g|len ovlpart(f,g) by FINSEQ_8:5;
ovlpart(f,g)^ovlrdiff(f,g)=(g|len ovlpart(f,g))^(g/^(len ovlpart(f,g)))
                                  by A1,FINSEQ_8:def 5
                         .=g by RFINSEQ:21;
hence ovlpart(f,g)^ovlrdiff(f,g)=g;
end;

theorem
  for D being non empty set,f,g being FinSequence of D holds
    ovlcon(f,g) = ovlldiff(f,g) ^ g
proof
  let D be non empty set,f,g be FinSequence of D;
  ovlcon(f,g) = ovlldiff(f,g)^(ovlpart(f,g)^ovlrdiff(f,g)) by FINSEQ_8:12;
  hence ovlcon(f,g) = ovlldiff(f,g) ^ g by Th32;
end;

theorem
  for D being non empty set,CR,r being File of D holds
    addcr(r,CR)=ovlldiff(r,CR)^CR
proof let D be non empty set,CR,r be File of D;
  addcr(r,CR) = ovlcon(r,CR) by FINSEQ_8:def 11
     .= ovlldiff(r,CR)^(ovlpart(r,CR)^ovlrdiff(r,CR)) by FINSEQ_8:12
     .= ovlldiff(r,CR)^CR by Th32;
  hence addcr(r,CR)=ovlldiff(r,CR)^CR;
end;

theorem Th35:
  for D being non empty set,r1,r2,f being File of D st f = r1 ^ r2 holds
    r1 is_substring_of f,1 & r2 is_substring_of f,1
proof let D be non empty set,r1,r2,f be File of D;
assume A1: f = r1 ^ r2;
A2: len r1 >=0 by NAT_1:18;
A3: len r2 >=0 by NAT_1:18;
A4: len (r1^r2)=len r1+len r2 by FINSEQ_1:35;
A5: len (r1^r2)=len r1+len r2 by FINSEQ_1:35;
A6: now per cases;
suppose len r1>0;then
  A7: len r1>=0+1 by NAT_1:38;
  A8: mid(r1^r2,1,len r1)=
    ((r1^r2)/^(1-'1))|(len r1-'1+1) by A7,JORDAN3:def 1
   .= ((r1^r2)/^0)|(len r1-'1+1) by BINARITH:51
   .= ((r1^r2)/^0)|len r1 by A7,BINARITH:53
   .= (r1^r2)|len r1 by FINSEQ_5:31
   .= r1 by FINSEQ_5:26;
  len r1>0 implies ex i being Nat st 1<=i & i<=len (r1^r2) &
             mid(r1^r2,i,(i-'1)+len r1)=r1
    proof set i = 1;
A9: (1-'1)+len r1=0+len r1 by BINARITH:51
                      .=len r1;
      1<=i & i<=len (r1^r2) & mid(r1^r2,i,(i-'1)+len r1)=r1
                       by A9,A8,A7,A3,XREAL_1:9,A5;
      hence thesis;
    end;
  hence r1 is_substring_of f,1 by A1,FINSEQ_8:def 7;
end;
suppose len r1<=0;then
  len r1=0 by NAT_1:18;
  hence r1 is_substring_of f,1 by FINSEQ_8:def 7;
end;
end;
now per cases;
suppose A10: len r2>0;then
  A11: len r2>=0+1 by NAT_1:38;then
  A12: len r1+1<=len (r1^r2) by A4,XREAL_1:8;
  A13:  len r1 + len r2 > len r1 + 0 by A10,XREAL_1:10;
  A14: len r1+len r2-'len r1=len r1+len r2-len r1 by A10,BINARITH:def 3
                           .=len r2;
  A15: r2|len r2=r2|(Seg len r2) by FINSEQ_1:def 15;
  A16: mid(r1^r2,len r1+1,len (r1^r2))=
    ((r1^r2)/^(len r1+1-'1))|(len (r1^r2)-'(len r1+1)+1)
          by A12,JORDAN3:def 1
   .= ((r1^r2)/^len r1)|(len (r1^r2)-'(len r1+1)+1) by BINARITH:39
   .= ((r1^r2)/^len r1)|(len r1 + len r2 -'(len r1+1)+1) by FINSEQ_1:35
   .= ((r1^r2)/^len r1)|(len r1 + len r2 -'len r1) by A13,NAT_2:9
   .= r2|len r2 by FINSEQ_5:40,A14
   .= r2 by A15,FINSEQ_2:23;
  len r2>0 implies
    ex i being Nat st 1<=i & i<=len (r1^r2) &
             mid(r1^r2,i,(i-'1)+len r2)=r2
    proof consider i being Nat such that A17: i=len r1+1;
      A18: (len r1+1)-'1+len r2=len r1+len r2 by BINARITH:39
                               .=len (r1^r2) by FINSEQ_1:35;
      1<=i & i<=len (r1^r2) & mid(r1^r2,i,(i-'1)+len r2)=r2
                     by A17,A18,A2,XREAL_1:8,A11,A4,A16;
      hence thesis;
    end;
  hence r2 is_substring_of f,1 by A1,FINSEQ_8:def 7;
end;
suppose len r2<=0;then
  len r2=0 by NAT_1:18;
  hence r2 is_substring_of f,1 by FINSEQ_8:def 7;
end;
end;
hence thesis by A6;
end;

theorem Th36:
  for D being non empty set,r1,r2,r3,f being File of D st
    f = r1 ^ r2 ^ r3 holds
    r1 is_substring_of f,1 & r2 is_substring_of f,1 & r3 is_substring_of f,1
proof let D be non empty set,r1,r2,r3,f be File of D;
  assume A1: f = r1 ^ r2 ^ r3;
A2:   f=r1^(r2^r3) by A1,FINSEQ_1:45;
A3: len r3 >=0 by NAT_1:18;
A4:  len (r1^r2)=len r1+len r2 by FINSEQ_1:35;
now per cases;
suppose A5: len r2>0;then
       len r2>=0+1 by NAT_1:38;then
A6:   len r1+1<=len (r1^r2) by A4,XREAL_1:8;then
A7:  len r1+1+0<=len r1+len r2+len r3 by A3,XREAL_1:9,A4;
       len r1>=0 by NAT_1:18;then
A8:   len r1 + 1>=0+1 by XREAL_1:8;
A9: len r1 + len r2 > len r1 + 0 by A5,XREAL_1:10;
A10:  len r1+len r2-'len r1=len r1+len r2-len r1 by A5,BINARITH:def 3
                           .=len r2;
A11: mid(r1^r2^r3,len r1+1,len (r1^r2))=
       ((r1^r2^r3)/^(len r1+1-'1))|(len (r1^r2)-'(len r1+1)+1)
            by A6,JORDAN3:def 1
   .= ((r1^r2^r3)/^len r1)|(len (r1^r2)-'(len r1+1)+1) by BINARITH:39
   .= ((r1^r2^r3)/^len r1)|(len r1 + len r2 -'(len r1+1)+1) by FINSEQ_1:35
   .= ((r1^r2^r3)/^len r1)|(len r1 + len r2 -'len r1) by A9,NAT_2:9
   .= ((r1^(r2^r3))/^len r1)|len r2 by A10,FINSEQ_1:45
   .= (r2^r3)|len r2 by FINSEQ_5:40
   .= r2 by FINSEQ_5:26;
  len r2>0 implies
    ex i being Nat st 1<=i & i<=len (r1^r2^r3) &
             mid(r1^r2^r3,i,(i-'1)+len r2)=r2
    proof consider i being Nat such that A12: i=len r1+1;
       (len r1+1)-'1+len r2=len r1+len r2 by BINARITH:39
                               .=len (r1^r2) by FINSEQ_1:35;
      then 1<=i & i<=len (r1^r2^r3) &
             mid(r1^r2^r3,i,(i-'1)+len r2)=r2
                  by A12,A11,A8,A7,A4,FINSEQ_1:35;
      hence thesis;
    end;
  hence r2 is_substring_of f,1 by A1,FINSEQ_8:def 7;
end;
suppose len r2<=0;then
  len r2=0 by NAT_1:18;
  hence r2 is_substring_of f,1 by FINSEQ_8:def 7;
end;
end;
hence thesis by A2,Th35,A1;
end;

theorem Th37:
  for D being non empty set,CR,r1,r2 being File of D st
    r1 is_terminated_by CR & r2 is_terminated_by CR holds
      CR^r2 is_substring_of addcr(r1^r2,CR),1
proof let D be non empty set,CR,r1,r2 be File of D;
  assume A1: r1 is_terminated_by CR & r2 is_terminated_by CR;
  per cases;
   suppose len CR >0;
     r1 = addcr(r1,CR) by A1,Th29; then
A2:r1 = ovlcon(r1,CR) by FINSEQ_8:def 11;
     r2 = addcr(r2,CR) by A1,Th29; then
     r2 = ovlcon(r2,CR) by FINSEQ_8:def 11;then
A3: r2 = r2^(CR/^len ovlpart(r2,CR)) by FINSEQ_8:def 3;
A4:
  r1^r2=((r1|(len r1-'len ovlpart(r1,CR)))^CR)^(r2^(CR/^len ovlpart(r2,CR)))
                   by A2,A3,FINSEQ_8:11;
   addcr(r1^r2,CR) = ovlcon(r1^r2,CR) by FINSEQ_8:def 11
                    .= r1^r2^(CR/^(len ovlpart(r1^r2,CR))) by FINSEQ_8:def 3
         .=(r1|(len r1-'len ovlpart(r1,CR)))^(CR^r2)^(CR/^len ovlpart(r2,CR))
                    ^(CR/^(len ovlpart(r1^r2,CR))) by A4,Th1
         .=(r1|(len r1-'len ovlpart(r1,CR)))^(CR^r2)^
                 ( (CR/^len ovlpart(r2,CR))^(CR/^(len ovlpart(r1^r2,CR))) )
                       by FINSEQ_1:45;
     hence CR^r2 is_substring_of addcr(r1^r2,CR),1 by Th36;
   end;
   suppose A5: len CR<=0;
     A6: len CR = 0 by A5,NAT_1:18; then
A7: CR^r2 = {}^r2 by FINSEQ_1:25
             .= r2 by FINSEQ_1:47;
      len ovlpart(r1^r2,CR)<=0 by A5,FINSEQ_8:16; then
A8: len ovlpart(r1^r2,CR)=0 by NAT_1:18;
   addcr(r1^r2,CR) = ovlcon(r1^r2,CR) by FINSEQ_8:def 11
                   .= (r1^r2)^(CR/^(len ovlpart(r1^r2,CR))) by FINSEQ_8:def 3
                   .= (r1^r2)^CR by A8,FINSEQ_5:31
                   .= (r1^r2)^{} by A6,FINSEQ_1:25
                   .= r1^r2 by FINSEQ_1:47;
      hence CR^r2 is_substring_of addcr(r1^r2,CR),1 by A7,Th35;
   end;
end;

theorem Th38:
  for D being non empty set,f,g being File of D,n being Nat st
    0<n & g={} holds instr(n,f,g)=n
proof let D be non empty set,f,g being File of D,n being Nat;
  assume A1: 0<n & g={};then
A2: len g=0 by FINSEQ_1:25;
  instr(n,f,g) <> 0
  proof assume instr(n,f,g)=0;then
    not(g is_substring_of f,n) by FINSEQ_8:def 10;
    hence contradiction by FINSEQ_8:def 7,A2;
  end; then
A3: n <= instr(n,f,g) & g is_preposition_of f/^(instr(n,f,g)-'1) &
  for j being Nat st j >= n & j > 0
    & (g is_preposition_of f/^(j-'1)) holds j >= instr(n,f,g)
                        by FINSEQ_8:def 10;
  g is_preposition_of f/^(n-'1) by FINSEQ_8:def 8,A2;then
  n >= instr(n,f,g) by FINSEQ_8:def 10,A1;
  hence instr(n,f,g)=n by A3,XXREAL_0:1;
end;

theorem
  for D being non empty set,f,g being File of D,n being Nat st
    0<n & n<=len f holds instr(n,f,g)<=len f
proof let D be non empty set,f,g be File of D,n be Nat;
   assume A1: 0<n & n<=len f;
A2: len g>=0 by NAT_1:18;
    assume A3: instr(n,f,g)>len f; then
A4:instr(n,f,g) >= len f + 1 by NAT_1:38;
    instr(n,f,g)>0 by NAT_1:18,A3;then
A5:n <= instr(n,f,g) & g is_preposition_of f/^(instr(n,f,g)-'1) &
       for j being Nat st j >= n & j > 0
         & (g is_preposition_of f/^(j-'1)) holds j >= instr(n,f,g)
                                      by FINSEQ_8:def 10;
A6:instr(n,f,g)-1 >= len f +1-1 by XREAL_1:11,A4;then
    instr(n,f,g)-1 >= 0 by NAT_1:18;then
    instr(n,f,g)-'1>=len f by A6,BINARITH:def 3;then
    f/^(instr(n,f,g)-'1)={} by FINSEQ_5:35;then
    len (f/^(instr(n,f,g)-'1)) < 1 by FINSEQ_1:25;then
    len g =0 by A2,FINSEQ_8:def 8,A5;then
    g={} by FINSEQ_1:25;
    hence thesis by Th38,A1,A3;
end;

theorem Th40:
  for D being non empty set,f,CR being File of D holds
    CR is_substring_of ovlcon(f,CR),1
proof let D be non empty set,f,CR be File of D;
  per cases;
    suppose len CR >0;then
A1: len CR>= 0+1 by NAT_1:38;
    set i4= len f-'len ovlpart(f,CR)+1;
A2: ovlcon(f,CR)=f^(CR/^(len ovlpart(f,CR))) by FINSEQ_8:def 3;
    len f>=len ovlpart(f,CR) by FINSEQ_8:10;then
A3: len f - len ovlpart(f,CR)>=0 by XREAL_1:50;
    1-len ovlpart(f,CR)<=len CR - len ovlpart(f,CR) by A1,XREAL_1:11;then
    1-len ovlpart(f,CR)+len f <=len CR - len ovlpart(f,CR) + len f
                                  by XREAL_1:9;then
A4: len f -len ovlpart(f,CR)+1<= len f + (len CR - len ovlpart(f,CR));
    len ovlpart(f,CR)<= len CR by FINSEQ_8:def 2;then
A5: len CR - len ovlpart(f,CR) >=0 by XREAL_1:50;
    len (CR/^(len ovlpart(f,CR)))= len CR -' len ovlpart(f,CR) by JORDAN3:19
           .= len CR - len ovlpart(f,CR) by BINARITH:def 3,A5;then
A6:len f-'len ovlpart(f,CR)+1<= len f + len (CR/^(len ovlpart(f,CR)))
                                           by A4,A3,BINARITH:def 3;
    set i3=len f-'len ovlpart(f,CR);
A7: i3 <= len f by BINARITH:52;
A8: i3+1 <= i3+ len CR by A1,XREAL_1:8;
A9:i3+len CR-'(i3+1)+1=i3+len CR-(i3+1)+1 by A8,BINARITH:50
         .=len CR;
A10:i3<=len (f|i3) by A7,FINSEQ_1:80;
A11:(f|i3)/^i3=(f/^i3)|(i3-'i3) by JORDAN3:21
                .=(f/^i3)|0 by BINARITH:51
                .={};
    mid((ovlcon(f,CR)),i4,(i4-'1)+len CR)
           =mid(((f|i3)^CR),i4,(i4-'1)+len CR) by FINSEQ_8:11
          .=mid(((f|i3)^CR),i3+1,i3+len CR) by BINARITH:39
          .=(((f|i3)^CR)/^(i3+1-'1)) | (i3+len CR-'(i3+1)+1 )
                                         by A8,JORDAN3:def 1
          .=(((f|i3)^CR)/^i3) |(len CR) by A9,BINARITH:39
          .=(((f|i3)/^i3)^CR) | (len CR) by A10,GENEALG1:1
          .= CR|(len CR) by A11,FINSEQ_1:47
          .=CR by Th2;then
    1<=i4 & i4<=len ovlcon(f,CR) & mid(ovlcon(f,CR),i4,(i4-'1)+len CR)=CR
      by NAT_1:29,A6,A2,FINSEQ_1:35;
    hence CR is_substring_of ovlcon(f,CR),1 by FINSEQ_8:def 7;
  end;
  suppose len CR<=0;
    hence CR is_substring_of ovlcon(f,CR),1 by FINSEQ_8:def 7;
  end;
end;

theorem Th41:
  for D being non empty set,f,CR being File of D holds
    CR is_substring_of addcr(f,CR),1
proof let D be non empty set,f,CR be File of D;
  addcr(f,CR)=ovlcon(f,CR) by FINSEQ_8:def 11;
  hence CR is_substring_of addcr(f,CR),1 by Th40;
end;

theorem Th42:
  for D being non empty set,f,g being FinSequence of D,
      n being Nat st g is_substring_of f|n,1 & len g>0 & len g <= n holds
   g is_substring_of f,1
proof let D be non empty set,f,g be FinSequence of D,n be Nat;
    assume A1: g is_substring_of f|n,1 & len g>0 & len g <= n;then
    len g>= 0+1 by NAT_1:38;then
A2:len g -1>=0 by XREAL_1:50;
    consider i2 being Nat such that
A3: 1<=i2 & i2<=len (f|n) & mid(f|n,i2,(i2-'1)+len g)=g
      by A1,FINSEQ_8:def 7;
A4: len g-1+i2>=0+i2 by XREAL_1:9,A2; then
A5:i2-1+len g >=i2;
    per cases;
    suppose A6: len f>=n;
A7: i2-1>=0 by A3,XREAL_1:50;
A8:  i2<=(i2-'1)+len g by A7,BINARITH:def 3,A5;
A9: (i2-'1)+len g=(i2-1)+len g by A3,BINARITH:50;
A10:  (i2-'1)+len g-'i2+1 = (i2-1)+len g-i2+1 by A9,A4,BINARITH:50
              .= len g;
A11:  g = ((f|n)/^(i2-'1))|((i2-'1)+len g-'i2+1) by A3,A8,JORDAN3:def 1
                 .=((f/^(i2-'1))|(n-'(i2-'1)))|len g by A10,JORDAN3:21;
      now assume A12: n-'(i2-'1)<len g;then
        A13: g=((f/^(i2-'1))|(n-'(i2-'1))) by A11,FINSEQ_5:80;
        A14: len (f/^(i2-'1))=len f -'(i2-'1) by JORDAN3:19;
        n-'(i2-'1)<=len f -'(i2-'1) by A6,JORDAN3:5;
       hence contradiction by A12,A13,A14,FINSEQ_1:80;
      end;then
A15:  g=(f/^(i2-'1))|len g by A11,FINSEQ_5:80
       .= mid(f,i2,(i2-'1)+len g) by A10,A8,JORDAN3:def 1;
      len (f|n)<=len f by FINSEQ_5:18;
      then 1<=i2 & i2<=len f & mid(f,i2,(i2-'1)+len g)=g by A3,A15,XXREAL_0:2;
    hence g is_substring_of f,1 by FINSEQ_8:def 7;
  end;
  suppose len f<n;
    hence g is_substring_of f,1 by A1,FINSEQ_1:79;
  end;
end;

theorem
  for D being non empty set, f,CR being File of D
    ex r being File of D st r is_a_record_of f,CR
proof let D be non empty set,f,CR be File of D;
   set i0=instr(1,addcr(f,CR),CR);
A1:i0<>0
   proof
     assume i0=0; then
     not CR is_substring_of addcr(f,CR),1 by FINSEQ_8:def 10;
     hence contradiction by Th41;
   end;then
   i0>0 by NAT_1:19;then
A2: i0>=0+1 by NAT_1:38;then
   i0-1>=0 by XREAL_1:50;then
A3: i0-'1=i0-1 by BINARITH:def 3;
   per cases;
   suppose len f = 0;then
A4:  f= <*>D by FINSEQ_1:25;
     CR is_a_record_of <*>D,CR by Th23;
     hence ex r2 being File of D st r2 is_a_record_of f,CR by A4;
   end;
   suppose A5: len f<>0;
   A6: ( 1 <= i0 & CR is_preposition_of (addcr(f,CR))/^(i0-'1) &
    for j being Nat st j >= 1 & j > 0
       & (CR is_preposition_of (addcr(f,CR))/^(j-'1)) holds j >= i0)
                 by FINSEQ_8:def 10,A1;
   set r=addcr(f,CR)|(i0+len CR-'1);
  A7: now per cases;
  suppose A8: len CR<=0;
A9: len CR=0 by A8,NAT_1:18;
A10: r is_terminated_by CR by A8,FINSEQ_8:def 12;
A11: CR=<*>D by A9,FINSEQ_1:32;then
    CR/^(len ovlpart(f,CR))=CR by FINSEQ_6:86;then
    f^(CR/^(len ovlpart(f,CR)))=f by A11,FINSEQ_1:47;then
A12: ovlcon(f,CR)=f by FINSEQ_8:def 3;then
A13: addcr(f,CR)=f by FINSEQ_8:def 11;
    len f> 0 by A5,NAT_1:19;then
A14: len f>=0+1 by NAT_1:38;
    len r>0 implies 1<=len f & mid(f,1,len r)=r
    proof assume len r>0;then
A15:  len r>=0+1 by NAT_1:38;
      per cases;
      suppose A16: len r < i0+len CR-'1;
        r=(addcr(f,CR)|(i0+len CR-'1))|(len r) by FINSEQ_1:79
         .= addcr(f,CR)|(len r) by FINSEQ_5:80,A16
         .=f|(len r) by A12,FINSEQ_8:def 11
         .=mid(f,1,len r) by A15,JORDAN3:25;
       hence 1<=len f & mid(f,1,len r)=r by A14;
      end;
      suppose A17: len r>= i0+len CR-'1;
A18:     mid(f,1,len r)= f|(len r) by A15,JORDAN3:25
          .=addcr(f,CR)|(len r) by A12,FINSEQ_8:def 11;
        len r<= (i0+len CR-'1) by FINSEQ_5:19;
        hence 1<=len f & mid(f,1,len r)=r by A14,A18,A17,XXREAL_0:1;
      end;
    end;then
    r is_preposition_of addcr(f,CR) by A13,FINSEQ_8:def 8;
   hence r is_a_record_of f,CR by A10,Def1;
  end;
  suppose A19: len CR>0;then
A20: 1<=len ((addcr(f,CR))/^(i0-'1))
   & mid((addcr(f,CR))/^(i0-'1),1,len CR)=CR by A6,FINSEQ_8:def 8;
A21: len CR>=0+1 by NAT_1:38,A19;then
   ((addcr(f,CR))/^(i0-'1))|(len CR)=CR by A20,JORDAN3:25;then
   A22: len CR <=len ((addcr(f,CR))/^(i0-'1)) by FINSEQ_5:18;
   A23: len ((addcr(f,CR))/^(i0-'1)) <= len (addcr(f,CR)) by FINSEQ_5:28;
   A24: now per cases;
   suppose i0+len CR-'1>=len addcr(f,CR);then
     addcr(f,CR)|(i0+len CR-'1)=addcr(f,CR) by FINSEQ_1:79;
     hence len (addcr(f,CR)|(i0+len CR-'1))>=len CR by A22,A23,XXREAL_0:2;
   end;
   suppose A25: i0+len CR-'1<len addcr(f,CR);
     i0-1>=0 by A2,XREAL_1:50;then
     A26: len CR +(i0-1)>=len CR +0 by XREAL_1:9;
     i0+ len CR -'1=len CR+i0 -1 by BINARITH:def 3,A19,A26;
    hence len (addcr(f,CR)|(i0+len CR-'1))>=len CR by A25,FINSEQ_1:80,A26;
   end;
   end;
    1 <= i0 & CR is_preposition_of addcr(f,CR)/^(i0-'1)
      by FINSEQ_8:def 10,A1;then
A27: len CR <=len (addcr(f,CR)/^(i0-'1)) by CARD_1:80;
    now assume i0-'1>len addcr(f,CR);then
      addcr(f,CR)/^(i0-'1)=<*>D by RFINSEQ:def 2;
      hence contradiction by A19,A27,FINSEQ_1:25;
    end;then
    len (addcr(f,CR)/^(i0-'1)) =len addcr(f,CR) -(i0-'1)
                                  by RFINSEQ:def 2;then
    A28: len CR +(i0-'1)<=len addcr(f,CR) by XREAL_1:21,A27;
    1<=i0+len CR by A2,NAT_1:37;then
    i0+len CR -1>=0 by XREAL_1:50;then
A29: len CR+i0 -'1<= len addcr(f,CR) by A28,A3,BINARITH:def 3;then
A30:  len r= i0+len CR-'1 by FINSEQ_1:80;
    ((len r + 1 -'len CR) <> 0 implies 1 <= (len r + 1 -'len CR)
    & CR is_preposition_of r/^((len r + 1 -'len CR)-'1) &
    for j being Nat st j >= 1 & j > 0
     & (CR is_preposition_of r/^(j-'1)) holds j >= len r + 1 -'len CR) &
    ((len r + 1 -'len CR)=0 implies not(CR is_substring_of r,1))
     proof
       thus ((len r + 1 -'len CR) <> 0 implies 1 <= len r + 1 -'len CR
        & CR is_preposition_of r/^((len r + 1 -'len CR)-'1) &
        for j being Nat st j >= 1 & j > 0
        & (CR is_preposition_of r/^(j-'1)) holds j >= len r + 1 -'len CR)
        proof assume len r + 1 -'len CR <> 0;then
         len r + 1 -'len CR>0 by NAT_1:19;then
         0+1<= len r + 1 -'len CR by NAT_1:38;
         hence 1 <= len r + 1 -'len CR;
         1<= i0+len CR by A21,NAT_1:37;then
A31:     i0+len CR -1>=0 by XREAL_1:50;then
A32:      i0+len CR-'1+1=i0 +len CR -1+1 by BINARITH:def 3
                          .=i0+len CR;
         i0+len CR-len CR>=0 by NAT_1:18;then
         i0+len CR-'len CR=i0 by BINARITH:def 3;then
     A33: r/^((len r + 1 -'len CR)-'1)
                 =(addcr(f,CR)|(i0+len CR-'1))/^(i0-'1) by A32,A29,FINSEQ_1:80;
         i0+len CR>=i0 by NAT_1:37;then
         i0+len CR-'1>=i0-'1 by JORDAN3:5;then
         i0+len CR -'1-(i0-'1)>=0 by XREAL_1:50;then
    A34: i0+len CR-'1-'(i0-'1) = i0+len CR-'1-(i0-'1) by BINARITH:def 3
             .=i0+len CR-1-(i0-1) by A31,BINARITH:def 3,A3
             .=len CR;
    A35: ((addcr(f,CR))|(i0+len CR-'1))/^(i0-'1)
          = ((addcr(f,CR))/^(i0-'1))|(len CR) by A34,JORDAN3:21;
         set f2= (addcr(f,CR)|(i0+len CR-'1))/^(i0-'1);
         1<= i0+len CR by A21,NAT_1:37;then
A36:    i0+len CR -1>=0 by XREAL_1:50;then
A37:     i0+len CR-'1=i0 +len CR -1 by BINARITH:def 3;
         i0<=i0+len CR by NAT_1:37;then
A38:      i0-'1<=len r by A30,A3,A37,XREAL_1:11;
         i0-1>=0 by A2,XREAL_1:50;then
A39:     i0-'1=i0-1 by BINARITH:def 3;
A40:    len f2= len r -(i0-'1) by A38,RFINSEQ:def 2
         .=i0+len CR-'1-(i0-'1) by A29,FINSEQ_1:80
         .= i0+len CR-1-(i0-1) by A36,BINARITH:def 3,A39
         .=len CR;
         mid((addcr(f,CR))/^(i0-'1),1,len CR)=CR by A19,A6,FINSEQ_8:def 8;then
         f2=CR by A35,JORDAN3:25,A21;then
         f2|(len CR)=CR by FINSEQ_1:79;then
         1<=len f2 & mid(f2,1,len CR)=CR by JORDAN3:25,A21,A40;
        hence CR is_preposition_of r/^((len r + 1 -'len CR)-'1)
                            by A33,FINSEQ_8:def 8;
         1<=i0+len CR by A2,NAT_1:37;then
         i0+len CR -1>=0 by XREAL_1:50;then
         len r +1=i0+len CR-1+1 by A30,BINARITH:def 3;then
A41:    len r+1-'len CR=i0 by BINARITH:39;
         1<= i0+len CR by A21,NAT_1:37;then
         i0+len CR -1>=0 by XREAL_1:50;then
     A42: i0+len CR-'1+1=i0 +len CR -1+1 by BINARITH:def 3
                      .=i0+len CR;
         i0+len CR-len CR>=0 by NAT_1:18;then
A43:     i0+len CR-'len CR=i0 by BINARITH:def 3;
         1<= i0+len CR by A21,NAT_1:37;then
         i0+len CR -1>=0 by XREAL_1:50;then
A44:     i0+len CR-'1=i0 +len CR -1 by BINARITH:def 3;
        thus for j being Nat st j >= 1 & j > 0
        & CR is_preposition_of r/^(j-'1) holds j >= len r + 1 -'len CR
         proof let j be Nat;
           assume A45: j >= 1 & j > 0 & CR is_preposition_of r/^(j-'1);
A46:        mid(r/^(j-'1),1,len CR)
               =(r/^(j-'1))|(len CR) by JORDAN3:25,A21;
A47:       (r/^(j-'1))|(len CR)=(r/^(j-'1))|((j-'1)+len CR -'(j-'1))
                                    by BINARITH:39
              .=(r|(len CR+(j-'1)))/^(j-'1) by JORDAN3:21;
A48:       j-1>=0 by A45,XREAL_1:50;
           now assume A49: j < len r + 1 -'len CR;then
A50:        j< i0+len CR-'1 +1-'len CR by A29,FINSEQ_1:80;
A51:       j<i0 by A49,A29,FINSEQ_1:80,A42,A43;
A52:       j-'1<i0-'1 by A51,A45,SPRECT_3:9;
           1<=j+len CR by A45,NAT_1:37;then
A53:       j+len CR -1>=0 by XREAL_1:50;then
A54:       j+len CR -'1=len CR +(j-1) by BINARITH:def 3
                      .=len CR +(j-'1) by A48,BINARITH:def 3;
           j+len CR<i0+len CR by A50,A42,A43,XREAL_1:10;then
           j+len CR -1 < i0+len CR -1 by XREAL_1:11;then
A55:       j+len CR-'1<i0+len CR-'1 by BINARITH:def 3,A53,A44;
A56:       ((addcr(f,CR))/^(j-'1))|(len CR)
           =((addcr(f,CR))/^(j-'1))|((j-'1)+len CR -'(j-'1)) by BINARITH:39
           .=((addcr(f,CR))|(len CR+(j-'1)))/^(j-'1) by JORDAN3:21
           .=((addcr(f,CR)|(i0+len CR-'1))|(j+len CR-'1))/^(j-'1)
                                   by A54,A55,FINSEQ_5:80
           .= CR by A54,A45,FINSEQ_8:def 8,A19,A46,A47;
A57:       1<=len ((addcr(f,CR))/^(i0-'1)) by A6,FINSEQ_8:def 8,A19;
A58:     len ((addcr(f,CR))/^(i0-'1))<=len ((addcr(f,CR))/^(j-'1))
                            by A52,Th4;
           1<=len ((addcr(f,CR))/^(j-'1))
                  & mid(((addcr(f,CR))/^(j-'1)),1,len CR)=CR
                        by A58,A57,XXREAL_0:2,A56,A21,JORDAN3:25;then
           CR is_preposition_of ((addcr(f,CR))/^(j-'1)) by FINSEQ_8:def 8;
           hence contradiction by A49,A41,A45,FINSEQ_8:def 10;
           end;
           hence thesis;
         end;
        end;
        assume A59: len r + 1 -'len CR=0;
         1<= i0+len CR by A21,NAT_1:37;then
         i0+len CR -1>=0 by XREAL_1:50;then
A60:      i0+len CR-'1+1=i0 +len CR -1+1 by BINARITH:def 3
                      .=i0+len CR;
         i0+len CR-len CR>=0 by NAT_1:18;then
         i0+len CR-'len CR=i0 by BINARITH:def 3;then
         instr(1,addcr(f,CR),CR)=0 by A59,A29,FINSEQ_1:80,A60;then
A61:      not(CR is_substring_of addcr(f,CR),1) by FINSEQ_8:def 10;
         1<=i0+len CR by A2,NAT_1:37;then
A62:     i0+len CR -1>=0 by XREAL_1:50;
         len CR + i0 >= len CR +1 by A2,XREAL_1:9; then
         i0 + len CR -1 >= len CR +1 -1 by XREAL_1:11; then
         i0 + len CR -'1 >= len CR by A62,BINARITH:def 3;
        hence not(CR is_substring_of r,1) by A61,A19,Th42;
     end;then
A63: len r>=len CR & instr(1,r,CR) = len r + 1 -'len CR
                                   by A24,FINSEQ_8:def 10;
    per cases;
    suppose A64: 1<=len r;
A65:  addcr(f,CR)|(len r)=r by A29,FINSEQ_1:80;
     len (addcr(f,CR)|(i0+len CR-'1))<= len addcr(f,CR) by FINSEQ_5:18;then
     len r>0 implies 1<=len (addcr(f,CR)) & mid(addcr(f,CR),1,len r)=r
                      by A64,XXREAL_0:2,A65,JORDAN3:25;then
    (CR^r is_substring_of addcr(f,CR),1 or
      r is_preposition_of addcr(f,CR)) &
      r is_terminated_by CR by A63,FINSEQ_8:def 12,FINSEQ_8:def 8;
    hence r is_a_record_of f,CR by Def1;
    end;
    suppose 1>len r;then
A66:  0+1>=len r +1 by NAT_1:38;then
A67:   0>=len r by XREAL_1:8;
A68:   0=len r by A67,NAT_1:18;
A69:   len CR<=0 by A66,XREAL_1:8,A24;
A70:   r is_terminated_by CR by FINSEQ_8:def 12,A69;
      r= {} by FINSEQ_1:25,A68;then
      CR^r=CR by FINSEQ_1:47;then
      CR^r is_substring_of addcr(f,CR),1 by A69,FINSEQ_8:def 7;
     hence r is_a_record_of f,CR by Def1,A70;
    end;
  end;
  end;
  thus thesis by A7;
 end;
end;

theorem
  for D being non empty set,f,CR,r being File of D st
    r is_a_record_of f,CR holds r is_a_record_of r,CR
proof let D be non empty set,f,CR,r be File of D;
  assume r is_a_record_of f,CR; then
  r is_preposition_of r & r is_terminated_by CR by Def1;
  then r is_preposition_of addcr(r,CR) & r is_terminated_by CR by Th29;
  hence r is_a_record_of r,CR by Def1;
end;

theorem
  for D being non empty set,CR,r1,r2,f being File of D st
    r1 is_terminated_by CR & r2 is_terminated_by CR &
      f = r1 ^ r2 holds r1 is_a_record_of f,CR & r2 is_a_record_of f,CR
proof
  let D be non empty set,CR,r1,r2,f be File of D;
  assume A1: r1 is_terminated_by CR & r2 is_terminated_by CR & f = r1 ^ r2;
  per cases;
  suppose A2: len r1<=0 & len r2 <=0; then
A3: r1 is_preposition_of addcr(f,CR) by FINSEQ_8:def 8;
   r2 is_preposition_of addcr(f,CR) by FINSEQ_8:def 8,A2;
   hence r1 is_a_record_of f,CR & r2 is_a_record_of f,CR by A3,A1,Def1;
  end;
  suppose A4: len r1<=0 & len r2 >0; then
A5: len r1=0 by NAT_1:18;
A6: r1 is_preposition_of addcr(f,CR) by FINSEQ_8:def 8,A4;
A7: f = r2 by A1,A5,Th3;
   f is_preposition_of addcr(f,CR) by A7,A1,Th29;
   hence r1 is_a_record_of f,CR & r2 is_a_record_of f,CR
                   by A6,A7,A1,Def1;
end;
suppose A8: len r1>0 & len r2 <=0; then
A9:  0+1<=len r1 by NAT_1:38;
  0<=len r2 by NAT_1:18; then
  1+0<=len r1 + len r2 by A9,XREAL_1:9; then
A10: 1 <=len f by A1,FINSEQ_1:35;
A11: len f<= len addcr(f,CR) by Th31;
  mid(addcr(f,CR),1,len r1)=addcr(f,CR)|len r1 by JORDAN3:25,A9
        .=ovlcon(f,CR)|len r1 by FINSEQ_8:def 11
        .=(f^(CR/^(len ovlpart(f,CR))))|len r1 by FINSEQ_8:def 3
        .=(r1^(r2^(CR/^(len ovlpart(f,CR)))))|len r1 by A1,FINSEQ_1:45
        .= r1|len r1 by FINSEQ_5:25
        .= r1 by FINSEQ_1:79;then
  len r1>0 implies 1<=len addcr(f,CR) & mid(addcr(f,CR),1,len r1)=r1
                             by A11,A10,XXREAL_0:2;then
A12: r1 is_preposition_of addcr(f,CR) by FINSEQ_8:def 8;
   r2 is_preposition_of addcr(f,CR) by A8,FINSEQ_8:def 8;
   hence r1 is_a_record_of f,CR & r2 is_a_record_of f,CR by A12,A1,Def1;
end;
suppose A13: len r1>0 & len r2>0;
A14: 0+1<=len r1 by NAT_1:38,A13;
      0<=len r2 by NAT_1:18; then
      1+0<=len r1 + len r2 by A14,XREAL_1:9; then
A15: 1 <=len f by A1,FINSEQ_1:35;
A16: len f<= len addcr(f,CR) by Th31;
  mid(addcr(f,CR),1,len r1)=addcr(f,CR)|len r1 by JORDAN3:25,A14
     .=ovlcon(f,CR)|len r1 by FINSEQ_8:def 11
     .=(f^(CR/^(len ovlpart(f,CR))))|len r1 by FINSEQ_8:def 3
     .=(r1^(r2^(CR/^(len ovlpart(f,CR)))))|len r1 by A1,FINSEQ_1:45
     .= r1|len r1 by FINSEQ_5:25
     .= r1 by FINSEQ_1:79;then
  len r1>0 implies 1<=len addcr(f,CR) & mid(addcr(f,CR),1,len r1)=r1
                             by A16,A15,XXREAL_0:2; then
A17: r1 is_preposition_of addcr(f,CR) by FINSEQ_8:def 8;
   CR^r2 is_substring_of addcr(r1^r2,CR),1 by A1,Th37;
   hence r1 is_a_record_of f,CR & r2 is_a_record_of f,CR by A17,A1,Def1;
end;
end;
