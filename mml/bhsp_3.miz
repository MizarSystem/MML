:: Introduction to Banach and Hilbert spaces - Part III
::  by Jan Popio{\l}ek
::
:: Received July 19, 1991
:: Copyright (c) 1991 Association of Mizar Users

environ

 vocabularies NUMBERS, BHSP_1, PRE_TOPC, REAL_1, NAT_1, ORDINAL2, SEQ_1,
      SUBSET_1, SUPINF_2, XXREAL_0, CARD_1, METRIC_1, FUNCT_1, ARYTM_3,
      NORMSP_1, ARYTM_1, RELAT_1, COMPLEX1, SEQ_2, XXREAL_2, XREAL_0, ORDINAL1,
      VALUED_0, REWRITE1, BHSP_3;
 notations ORDINAL1, SUBSET_1, NUMBERS, XREAL_0, COMPLEX1, REAL_1, SEQ_1,
      NAT_1, VALUED_0, STRUCT_0, PRE_TOPC, RLVECT_1, NORMSP_1, BHSP_1, BHSP_2,
      XXREAL_0;
 constructors PARTFUN1, FUNCT_3, XXREAL_0, REAL_1, COMPLEX1, SEQ_1, SEQM_3,
      BHSP_2, VALUED_1, RECDEF_1, NAT_1, RELSET_1;
 registrations XBOOLE_0, ORDINAL1, RELSET_1, NUMBERS, XXREAL_0, XREAL_0, NAT_1,
      MEMBERED, STRUCT_0, VALUED_0;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 theorems NAT_1, SEQM_3, FUNCT_2, RLVECT_1, BHSP_1, BHSP_2, NORMSP_1, XREAL_0,
      XCMPLX_0, XREAL_1, COMPLEX1, XXREAL_0, ORDINAL1, VALUED_0;
 schemes NAT_1;

begin

reserve X for RealUnitarySpace,
  x, g, g1, h for Point of X,
  a, p, r, M, M1, M2 for Real,
  seq, seq1, seq2, seq3 for sequence of X,
  Nseq for increasing sequence of NAT,

  k, l, l1, l2, l3, n, m, m1, m2 for Element of NAT;

deffunc 09(RealUnitarySpace) = 0.$1;

definition
  let X;
  let seq;
  attr seq is Cauchy means
  :Def1:
  for r st r > 0 ex k st for n, m st n >= k &
  m >= k holds dist(seq.n, seq.m) < r;
end;

theorem
  seq is constant implies seq is Cauchy
proof
  assume
A1: seq is constant;
  let r such that
A2: r > 0;
  take k = 0;
  let n, m such that
  n >= k and
  m >= k;
  dist((seq.n), (seq.m)) = dist((seq.n), (seq.n)) by A1,VALUED_0:23
    .= 0 by BHSP_1:41;
  hence thesis by A2;
end;

theorem
  seq is Cauchy iff for r st r > 0 ex k st for n, m st n >= k & m >= k
  holds ||.(seq.n) - (seq.m).|| < r
proof
  thus seq is Cauchy implies for r st r > 0 ex k st for n, m st n >= k & m >=
  k holds ||.(seq.n) - (seq.m).|| < r
  proof
    assume
A1: seq is Cauchy;
    let r;
    assume r > 0;
    then consider l such that
A2: for n, m st n >= l & m >= l holds dist((seq.n), (seq.m)) < r by A1,Def1;
    take k = l;
    let n, m;
    assume n >= k & m >= k;
    then dist((seq.n), (seq.m)) < r by A2;
    hence thesis by BHSP_1:def 5;
  end;
  ( for r st r > 0 ex k st for n, m st ( n >= k & m >= k ) holds ||.(seq.n
  ) - (seq.m).|| < r ) implies seq is Cauchy
  proof
    assume
A3: for r st r > 0 ex k st for n, m st n >= k & m >= k holds ||.(seq.
    n) - (seq.m).|| < r;
    let r;
    assume r > 0;
    then consider l such that
A4: for n, m st n >= l & m >= l holds ||.(seq.n) - (seq.m).|| < r by A3;
    take k = l;
    let n, m;
    assume n >= k & m >= k;
    then ||.(seq.n) - (seq.m).|| < r by A4;
    hence thesis by BHSP_1:def 5;
  end;
  hence thesis;
end;

theorem
  seq1 is Cauchy & seq2 is Cauchy implies seq1 + seq2 is Cauchy
proof
  assume that
A1: seq1 is Cauchy and
A2: seq2 is Cauchy;
  let r;
  assume r > 0;
  then
A3: r/2 > 0 by XREAL_1:217;
  then consider m1 such that
A4: for n, m st n >= m1 & m >= m1 holds dist((seq1.n), (seq1.m)) < r/2
  by A1,Def1;
  consider m2 such that
A5: for n, m st n >= m2 & m >= m2 holds dist((seq2.n), (seq2.m)) < r/2
  by A2,A3,Def1;
  take k = m1 + m2;
  let n, m such that
A6: n >= k & m >= k;
  k >= m2 by NAT_1:12;
  then n >= m2 & m >= m2 by A6,XXREAL_0:2;
  then
A7: dist((seq2.n), (seq2.m)) < r/2 by A5;
  dist((seq1 + seq2).n, (seq1 + seq2).m) = dist((seq1.n) + (seq2.n), (
  seq1 + seq2).m) by NORMSP_1:def 5
    .= dist((seq1.n) + (seq2.n), (seq1.m) + (seq2.m)) by NORMSP_1:def 5;
  then
A8: dist((seq1 + seq2).n, (seq1 + seq2).m) <= dist((seq1.n), (seq1.m)) +
  dist((seq2.n), (seq2.m)) by BHSP_1:47;
  m1 + m2 >= m1 by NAT_1:12;
  then n >= m1 & m >= m1 by A6,XXREAL_0:2;
  then dist((seq1.n), (seq1.m)) < r/2 by A4;
  then dist((seq1.n), (seq1.m)) + dist((seq2.n), (seq2.m)) < r/2 + r/2 by A7,
XREAL_1:10;
  hence thesis by A8,XXREAL_0:2;
end;

theorem
  seq1 is Cauchy & seq2 is Cauchy implies seq1 - seq2 is Cauchy
proof
  assume that
A1: seq1 is Cauchy and
A2: seq2 is Cauchy;
  let r;
  assume r > 0;
  then
A3: r/2 > 0 by XREAL_1:217;
  then consider m1 such that
A4: for n, m st n >= m1 & m >= m1 holds dist((seq1.n), (seq1.m)) < r/2
  by A1,Def1;
  consider m2 such that
A5: for n, m st n >= m2 & m >= m2 holds dist((seq2.n), (seq2.m)) < r/2
  by A2,A3,Def1;
  take k = m1 + m2;
  let n, m such that
A6: n >= k & m >= k;
  k >= m2 by NAT_1:12;
  then n >= m2 & m >= m2 by A6,XXREAL_0:2;
  then
A7: dist((seq2.n), (seq2.m)) < r/2 by A5;
  dist((seq1 - seq2).n, (seq1 - seq2).m) = dist((seq1.n) - (seq2.n), (
  seq1 - seq2).m) by NORMSP_1:def 6
    .= dist((seq1.n) - (seq2.n), (seq1.m) - (seq2.m)) by NORMSP_1:def 6;
  then
A8: dist((seq1 - seq2).n, (seq1 - seq2).m) <= dist((seq1.n), (seq1.m)) +
  dist((seq2.n), (seq2.m)) by BHSP_1:48;
  m1 + m2 >= m1 by NAT_1:12;
  then n >= m1 & m >= m1 by A6,XXREAL_0:2;
  then dist((seq1.n), (seq1.m)) < r/2 by A4;
  then dist((seq1.n), (seq1.m)) + dist((seq2.n), (seq2.m)) < r/2 + r/2 by A7,
XREAL_1:10;
  hence thesis by A8,XXREAL_0:2;
end;

theorem Th5:
  seq is Cauchy implies a * seq is Cauchy
proof
  assume
A1: seq is Cauchy;
A2: now
A3: 0/abs(a) = 0;
    assume
A4: a <> 0;
    then
A5: abs(a) > 0 by COMPLEX1:133;
    let r;
    assume r > 0;
    then r/abs(a) > 0 by A5,A3,XREAL_1:76;
    then consider m1 such that
A6: for n, m st n >= m1 & m >= m1 holds dist(seq.n, seq.m) < r/abs(a)
    by A1,Def1;
    take k = m1;
    let n, m;
    assume n >= k & m >= k;
    then
A7: dist(seq.n, seq.m) < r/abs(a) by A6;
A8: abs(a) <> 0 by A4,COMPLEX1:133;
A9: abs(a) * (r/abs(a)) = abs(a) * (abs(a)" * r) by XCMPLX_0:def 9
      .= abs(a) *abs(a)" * r
      .= 1 * r by A8,XCMPLX_0:def 7
      .= r;
    dist(a * (seq.n), a * (seq.m)) = ||.(a * (seq.n)) - (a * (seq.m)).||
    by BHSP_1:def 5
      .= ||.a * ((seq.n) - (seq.m)).|| by RLVECT_1:48
      .= abs(a) * ||.(seq.n) - (seq.m).|| by BHSP_1:33
      .= abs(a) * dist(seq.n, seq.m) by BHSP_1:def 5;
    then dist((a * (seq.n)), (a * (seq.m))) < r by A5,A7,A9,XREAL_1:70;
    then dist((a * seq).n, (a * (seq.m))) < r by NORMSP_1:def 8;
    hence dist((a * seq).n, (a * seq).m) < r by NORMSP_1:def 8;
  end;
  now
    assume
A10: a = 0;
    let r;
    assume r > 0;
    then consider m1 such that
A11: for n, m st n >= m1 & m >= m1 holds dist((seq.n), (seq.m)) < r by A1,Def1;
    take k = m1;
    let n, m;
    assume n >= k & m >= k;
    then
A12: dist((seq.n), (seq.m)) < r by A11;
    dist(a * (seq.n), a * (seq.m)) = dist(09(X), 0 * (seq.m)) by A10,
RLVECT_1:23
      .= dist(09(X), 09(X)) by RLVECT_1:23
      .= 0 by BHSP_1:41;
    then dist(a * (seq.n), a * (seq.m)) < r by A12,BHSP_1:44;
    then dist((a * seq).n, a * (seq.m)) < r by NORMSP_1:def 8;
    hence dist((a * seq).n, (a * seq).m) < r by NORMSP_1:def 8;
  end;
  hence thesis by A2,Def1;
end;

theorem
  seq is Cauchy implies - seq is Cauchy
proof
  assume seq is Cauchy;
  then (-1) * seq is Cauchy by Th5;
  hence thesis by BHSP_1:77;
end;

theorem Th7:
  seq is Cauchy implies seq + x is Cauchy
proof
  assume
A1: seq is Cauchy;
  let r;
  assume r > 0;
  then consider m1 such that
A2: for n, m st n >= m1 & m >= m1 holds dist((seq.n), (seq.m)) < r by A1,Def1;
  take k = m1;
  let n, m;
  dist((seq.n) + x, (seq.m) + x) <= dist((seq.n), (seq.m)) + dist(x, x) by
BHSP_1:47;
  then
A3: dist((seq.n) + x, (seq.m) + x) <= dist((seq.n), (seq.m)) + 0 by BHSP_1:41;
  assume n >= k & m >= k;
  then dist((seq.n), (seq.m)) < r by A2;
  then dist((seq.n) + x, (seq.m) + x) < r by A3,XXREAL_0:2;
  then dist((seq + x).n, (seq.m) + x) < r by BHSP_1:def 12;
  hence thesis by BHSP_1:def 12;
end;

theorem
  seq is Cauchy implies seq - x is Cauchy
proof
  assume seq is Cauchy;
  then seq + (-x) is Cauchy by Th7;
  hence thesis by BHSP_1:78;
end;

theorem
  seq is convergent implies seq is Cauchy
proof
  assume seq is convergent;
  then consider h such that
A1: for r st r > 0 ex k st for n st n >= k holds dist((seq.n), h) < r by
BHSP_2:def 1;
  let r;
  assume r > 0;
  then consider m1 such that
A2: for n st n >= m1 holds dist((seq.n), h) < r/2 by A1,XREAL_1:217;
  take k = m1;
  let n, m;
  assume n >= k & m >= k;
  then dist((seq.n), h) < r/2 & dist((seq.m), h) < r/2 by A2;
  then dist((seq.n), (seq.m)) <= dist((seq.n), h) + dist(h, (seq.m)) & dist((
  seq.n) , h) + dist(h, (seq.m)) < r/2 + r/2 by BHSP_1:42,XREAL_1:10;
  hence thesis by XXREAL_0:2;
end;

definition
  let X;
  let seq1, seq2;
  pred seq1 is_compared_to seq2 means
  :Def2:
  for r st r > 0 ex m st for n st n >= m holds dist(seq1.n, seq2.n) < r;
end;

theorem Th10:
  seq is_compared_to seq
proof
  let r such that
A1: r > 0;
  take m = 0;
  let n such that
  n >= m;
  thus thesis by A1,BHSP_1:41;
end;

theorem Th11:
  seq1 is_compared_to seq2 implies seq2 is_compared_to seq1
proof
  assume
A1: seq1 is_compared_to seq2;
  let r;
  assume r > 0;
  then consider k such that
A2: for n st n >= k holds dist((seq1.n), (seq2.n)) < r by A1,Def2;
  take m = k;
  let n;
  assume n >= m;
  hence thesis by A2;
end;

definition
  let X;
  let seq1, seq2;
  redefine pred seq1 is_compared_to seq2;
  reflexivity by Th10;
  symmetry by Th11;
end;

theorem
  seq1 is_compared_to seq2 & seq2 is_compared_to seq3 implies seq1
  is_compared_to seq3
proof
  assume that
A1: seq1 is_compared_to seq2 and
A2: seq2 is_compared_to seq3;
  let r;
  assume r > 0;
  then
A3: r/2 > 0 by XREAL_1:217;
  then consider m1 such that
A4: for n st n >= m1 holds dist((seq1.n), (seq2.n)) < r/2 by A1,Def2;
  consider m2 such that
A5: for n st n >= m2 holds dist((seq2.n), (seq3.n)) < r/2 by A2,A3,Def2;
  take m = m1 + m2;
  let n such that
A6: n >= m;
  m >= m2 by NAT_1:12;
  then n >= m2 by A6,XXREAL_0:2;
  then
A7: dist((seq2.n), (seq3.n)) < r/2 by A5;
A8: dist((seq1.n), (seq3.n)) <= dist((seq1.n), (seq2.n)) + dist((seq2.n), (
  seq3.n)) by BHSP_1:42;
  m1 + m2 >= m1 by NAT_1:12;
  then n >= m1 by A6,XXREAL_0:2;
  then dist((seq1.n), (seq2.n)) < r/2 by A4;
  then dist((seq1.n), (seq2.n)) + dist((seq2.n), (seq3.n)) < r/2 + r/2 by A7,
XREAL_1:10;
  hence thesis by A8,XXREAL_0:2;
end;

theorem
  seq1 is_compared_to seq2 iff for r st r > 0 ex m st for n st n >= m
  holds ||.(seq1.n) - (seq2.n).|| < r
proof
  thus seq1 is_compared_to seq2 implies for r st r > 0 ex m st for n st n >= m
  holds ||.(seq1.n) - (seq2.n).|| < r
  proof
    assume
A1: seq1 is_compared_to seq2;
    let r;
    assume r > 0;
    then consider m1 such that
A2: for n st n >= m1 holds dist((seq1.n), (seq2.n)) < r by A1,Def2;
    take m = m1;
    let n;
    assume n >= m;
    then dist((seq1.n), (seq2.n)) < r by A2;
    hence thesis by BHSP_1:def 5;
  end;
  ( for r st r > 0 ex m st for n st n >= m holds ||.(seq1.n) - (seq2.n).||
  < r ) implies seq1 is_compared_to seq2
  proof
    assume
A3: for r st r > 0 ex m st for n st n >= m holds ||.(seq1.n) - (seq2.n
    ).|| < r;
    let r;
    assume r > 0;
    then consider m1 such that
A4: for n st n >= m1 holds ||.(seq1.n) - (seq2.n).|| < r by A3;
    take m = m1;
    let n;
    assume n >= m;
    then ||.(seq1.n) - (seq2.n).|| < r by A4;
    hence thesis by BHSP_1:def 5;
  end;
  hence thesis;
end;

theorem
  ( ex k st for n st n >= k holds seq1.n = seq2.n ) implies seq1
  is_compared_to seq2
proof
  assume ex k st for n st n >= k holds seq1.n = seq2.n;
  then consider m such that
A1: for n st n >= m holds seq1.n = seq2.n;
  let r such that
A2: r > 0;
  take k = m;
  let n;
  assume n >= k;
  then dist((seq1.n), (seq2.n)) = dist((seq1.n), (seq1.n)) by A1
    .= 0 by BHSP_1:41;
  hence thesis by A2;
end;

theorem
  seq1 is Cauchy & seq1 is_compared_to seq2 implies seq2 is Cauchy
proof
  assume that
A1: seq1 is Cauchy and
A2: seq1 is_compared_to seq2;
  let r;
  assume r > 0;
  then
A3: r/3 > 0 by XREAL_1:224;
  then consider m1 such that
A4: for n, m st n >= m1 & m >= m1 holds dist((seq1.n), (seq1.m)) < r/3
  by A1,Def1;
  consider m2 such that
A5: for n st n >= m2 holds dist((seq1.n), (seq2.n)) < r/3 by A2,A3,Def2;
  take k = m1 + m2;
  let n, m such that
A6: n >= k and
A7: m >= k;
  m1 + m2 >= m1 by NAT_1:12;
  then n >= m1 & m >= m1 by A6,A7,XXREAL_0:2;
  then
A8: dist((seq1.n), (seq1.m)) < r/3 by A4;
A9: dist((seq2.n), (seq1.m)) <= dist((seq2.n), (seq1.n)) + dist((seq1.n), (
  seq1.m)) by BHSP_1:42;
A10: k >= m2 by NAT_1:12;
  then n >= m2 by A6,XXREAL_0:2;
  then dist((seq1.n), (seq2.n)) < r/3 by A5;
  then
  dist((seq2.n), (seq1.n)) + dist((seq1.n), (seq1.m)) < r/3 + r/3 by A8,
XREAL_1:10;
  then
A11: dist((seq2.n), (seq1.m)) < r/3 + r/3 by A9,XXREAL_0:2;
A12: dist((seq2.n), (seq2.m)) <= dist((seq2.n), (seq1.m)) + dist((seq1.m), (
  seq2.m)) by BHSP_1:42;
  m >= m2 by A7,A10,XXREAL_0:2;
  then dist((seq1.m), (seq2.m)) < r/3 by A5;
  then dist((seq2.n), (seq1.m)) + dist((seq1.m), (seq2.m)) < r/3 + r/3 + r/3
  by A11,XREAL_1:10;
  hence thesis by A12,XXREAL_0:2;
end;

theorem
  seq1 is convergent & seq1 is_compared_to seq2 implies seq2 is convergent
proof
  assume that
A1: seq1 is convergent and
A2: seq1 is_compared_to seq2;
  now
    let r;
    assume r > 0;
    then
A3: r/2 > 0 by XREAL_1:217;
    then consider m1 such that
A4: for n st n >= m1 holds dist((seq1.n), (lim seq1)) < r/2 by A1,BHSP_2:def 2;
    consider m2 such that
A5: for n st n >= m2 holds dist((seq1.n), (seq2.n)) < r/2 by A2,A3,Def2;
    take m = m1 + m2;
    let n such that
A6: n >= m;
    m >= m2 by NAT_1:12;
    then n >= m2 by A6,XXREAL_0:2;
    then
A7: dist((seq1.n), (seq2.n)) < r/2 by A5;
A8: dist((seq2.n), (lim seq1)) <= dist((seq2.n), (seq1.n)) + dist((seq1.n
    ), (lim seq1)) by BHSP_1:42;
    m1 + m2 >= m1 by NAT_1:12;
    then n >= m1 by A6,XXREAL_0:2;
    then dist((seq1.n), (lim seq1)) < r/2 by A4;
    then dist((seq2.n), (seq1.n)) + dist((seq1.n), (lim seq1)) < r/2 + r/2 by
A7,XREAL_1:10;
    hence dist((seq2.n), (lim seq1)) < r by A8,XXREAL_0:2;
  end;
  hence thesis by BHSP_2:def 1;
end;

theorem
  seq1 is convergent & lim seq1 = g & seq1 is_compared_to seq2 implies
  seq2 is convergent & lim seq2 = g
proof
  assume that
A1: seq1 is convergent & lim seq1 = g and
A2: seq1 is_compared_to seq2;
A3: now
    let r;
    assume r > 0;
    then
A4: r/2 > 0 by XREAL_1:217;
    then consider m1 such that
A5: for n st n >= m1 holds dist((seq1.n), g) < r/2 by A1,BHSP_2:def 2;
    consider m2 such that
A6: for n st n >= m2 holds dist((seq1.n), (seq2.n)) < r/2 by A2,A4,Def2;
    take m = m1 + m2;
    let n such that
A7: n >= m;
    m >= m2 by NAT_1:12;
    then n >= m2 by A7,XXREAL_0:2;
    then
A8: dist((seq1.n), (seq2.n)) < r/2 by A6;
A9: dist((seq2.n), g) <= dist((seq2.n), (seq1.n)) + dist((seq1.n), g) by
BHSP_1:42;
    m1 + m2 >= m1 by NAT_1:12;
    then n >= m1 by A7,XXREAL_0:2;
    then dist((seq1.n), g) < r/2 by A5;
    then dist((seq2.n), (seq1.n)) + dist((seq1.n), g) < r/2 + r/2 by A8,
XREAL_1:10;
    hence dist((seq2.n), g) < r by A9,XXREAL_0:2;
  end;
  then seq2 is convergent by BHSP_2:def 1;
  hence thesis by A3,BHSP_2:def 2;
end;

definition
  let X;
  let seq;
  attr seq is bounded means
  :Def3:
  ex M st M > 0 & for n holds ||.seq.n.|| <= M;
end;

theorem Th18:
  seq1 is bounded & seq2 is bounded implies seq1 + seq2 is bounded
proof
  assume that
A1: seq1 is bounded and
A2: seq2 is bounded;
  consider M2 such that
A3: M2 > 0 and
A4: for n holds ||.seq2.n.|| <= M2 by A2,Def3;
  consider M1 such that
A5: M1 > 0 and
A6: for n holds ||.seq1.n.|| <= M1 by A1,Def3;
  now
    let n;
    ||.(seq1 + seq2).n.|| = ||.seq1.n + seq2.n.|| by NORMSP_1:def 5;
    then
A7: ||.(seq1 + seq2).n.|| <= ||.seq1.n.|| + ||.seq2.n.|| by BHSP_1:36;
    ||.seq1.n.|| <= M1 & ||.seq2.n.|| <= M2 by A6,A4;
    then ||.seq1.n.|| + ||.seq2.n.|| <= M1 + M2 by XREAL_1:9;
    hence ||.(seq1 + seq2).n.|| <= M1 + M2 by A7,XXREAL_0:2;
  end;
  hence thesis by A5,A3,Def3;
end;

theorem Th19:
  seq is bounded implies -seq is bounded
proof
  assume seq is bounded;
  then consider M such that
A1: M > 0 and
A2: for n holds ||.seq.n.|| <= M by Def3;
  now
    let n;
    ||.(- seq).n.|| = ||.- (seq.n).|| by BHSP_1:def 10
      .= ||.seq.n.|| by BHSP_1:37;
    hence ||.(- seq).n.|| <= M by A2;
  end;
  hence thesis by A1,Def3;
end;

theorem
  seq1 is bounded & seq2 is bounded implies seq1 - seq2 is bounded
proof
  assume that
A1: seq1 is bounded and
A2: seq2 is bounded;
A3: seq1 - seq2 = seq1 + (- seq2) by BHSP_1:71;
  - seq2 is bounded by A2,Th19;
  hence thesis by A1,A3,Th18;
end;

theorem
  seq is bounded implies a * seq is bounded
proof
  assume seq is bounded;
  then consider M such that
A1: M > 0 and
A2: for n holds ||.seq.n.|| <= M by Def3;
A3: a <> 0 implies a * seq is bounded
  proof
A4: now
      let n;
A5:   abs(a) >= 0 by COMPLEX1:132;
      ||.(a * seq).n.|| = ||.a * (seq.n).|| by NORMSP_1:def 8
        .= abs(a) * ||.seq.n.|| by BHSP_1:33;
      hence ||.(a * seq).n.|| <= abs(a) * M by A2,A5,XREAL_1:66;
    end;
    assume a <> 0;
    then abs(a) > 0 by COMPLEX1:133;
    then abs(a) * M > 0 by A1,XREAL_1:131;
    hence thesis by A4,Def3;
  end;
  a = 0 implies a * seq is bounded
  proof
    assume
A6: a = 0;
    now
      let n;
      ||.(a * seq).n.|| = ||.0 * (seq.n).|| by A6,NORMSP_1:def 8
        .= ||.09(X).|| by RLVECT_1:23
        .= 0 by BHSP_1:32;
      hence ||.(a * seq).n.|| <= M by A1;
    end;
    hence thesis by A1,Def3;
  end;
  hence thesis by A3;
end;

theorem
  seq is constant implies seq is bounded
proof
  assume seq is constant;
  then consider x such that
A1: for n being Nat holds seq.n = x by VALUED_0:def 18;
A2: x = 09(X) implies seq is bounded
  proof
    consider M being real number such that
A3: M > 0 by XREAL_1:3;
    reconsider M as Real by XREAL_0:def 1;
    assume
A4: x = 09(X);
    now
      let n;
      seq.n = 09(X) by A1,A4;
      hence ||.seq.n.|| <= M by A3,BHSP_1:32;
    end;
    hence thesis by A3,Def3;
  end;
  x <> 09(X) implies seq is bounded
  proof
    assume x <> 09(X);
    consider M being real number such that
A5: ||.x.|| < M by XREAL_1:3;
    reconsider M as Real by XREAL_0:def 1;
    ||.x.|| >= 0 & for n holds ||.seq.n.|| <= M by A1,A5,BHSP_1:34;
    hence thesis by A5,Def3;
  end;
  hence thesis by A2;
end;

theorem Th23:
  for m ex M st ( M > 0 & for n st n <= m holds ||.seq.n.|| < M )
proof
  defpred P[Element of NAT] means ex M st ( M > 0 & for n st n <= $1 holds ||.
  seq.n.|| < M );
A1: for m st P[m] holds P[m+1]
  proof
    let m;
    given M1 such that
A2: M1 > 0 and
A3: for n st n <= m holds ||.seq.n.|| < M1;
A4: now
      assume
A5:   ||.seq.(m+1).|| >= M1;
      take M = ||.seq.(m+1).|| + 1;
      M > 0 + 0 by BHSP_1:34,XREAL_1:10;
      hence M > 0;
      let n such that
A6:   n <= m + 1;
A7:   now
        assume m >= n;
        then ||.seq.n.|| < M1 by A3;
        then ||.seq.n.|| < ||.seq.(m+1).|| by A5,XXREAL_0:2;
        then ||.seq.n.|| + 0 < M by XREAL_1:10;
        hence ||.seq.n.|| < M;
      end;
      now
        assume n = m + 1;
        then ||.seq.n.|| + 0 < M by XREAL_1:10;
        hence ||.seq.n.|| < M;
      end;
      hence ||.seq.n.|| < M by A6,A7,NAT_1:8;
    end;
    now
      assume
A8:   ||.seq.(m+1).|| <= M1;
      take M = M1 + 1;
      thus M > 0 by A2;
      let n such that
A9:   n <= m + 1;
A10:  now
        assume m >= n;
        then
A11:    ||.seq.n.|| < M1 by A3;
        M > M1 + 0 by XREAL_1:10;
        hence ||.seq.n.|| < M by A11,XXREAL_0:2;
      end;
      now
A12:    M > M1 + 0 by XREAL_1:10;
        assume n = m + 1;
        hence ||.seq.n.|| < M by A8,A12,XXREAL_0:2;
      end;
      hence ||.seq.n.|| < M by A9,A10,NAT_1:8;
    end;
    hence thesis by A4;
  end;
A13: P[0]
  proof
    take M = ||.(seq.0).|| + 1;
    ||.(seq.0).|| + 1 > 0 + 0 by BHSP_1:34,XREAL_1:10;
    hence M > 0;
    let n;
    assume n <= 0;
    then n = 0;
    then ||.seq.n.|| + 0 < M by XREAL_1:10;
    hence thesis;
  end;
  thus for m holds P[m] from NAT_1:sch 1(A13,A1);
end;

theorem Th24:
  seq is convergent implies seq is bounded
proof
  assume seq is convergent;
  then consider g such that
A1: for r st r > 0 ex m st for n st n >= m holds ||.seq.n - g.|| < r by
BHSP_2:9;
  consider m1 such that
A2: for n st n >= m1 holds ||.seq.n - g.|| < 1 by A1;
A3: now
    take p = ||.g.|| + 1;
    0 + 0 < p by BHSP_1:34,XREAL_1:10;
    hence p > 0;
    let n;
    assume n >= m1;
    then
A4: ||.seq.n - g.|| < 1 by A2;
    ||.seq.n.|| - ||.g.|| <= ||.seq.n - g.|| by BHSP_1:38;
    then ||.seq.n.|| - ||.g.|| < 1 by A4,XXREAL_0:2;
    hence ||.seq.n.|| < p by XREAL_1:21;
  end;
  now
    consider M2 such that
A5: M2 > 0 and
A6: for n st n <= m1 holds ||.seq.n.|| < M2 by Th23;
    consider M1 such that
A7: M1 > 0 and
A8: for n st n >= m1 holds ||.seq.n.|| < M1 by A3;
    take M = M1 + M2;
    thus M > 0 by A7,A5;
    let n;
A9: M > 0 + M2 by A7,XREAL_1:10;
A10: now
      assume n <= m1;
      then ||.seq.n.|| < M2 by A6;
      hence ||.seq.n.|| <= M by A9,XXREAL_0:2;
    end;
A11: M > M1 + 0 by A5,XREAL_1:10;
    now
      assume n >= m1;
      then ||.seq.n.|| < M1 by A8;
      hence ||.seq.n.|| <= M by A11,XXREAL_0:2;
    end;
    hence ||.seq.n.|| <= M by A10;
  end;
  hence thesis by Def3;
end;

theorem
  seq1 is bounded & seq1 is_compared_to seq2 implies seq2 is bounded
proof
  assume that
A1: seq1 is bounded and
A2: seq1 is_compared_to seq2;
  consider m1 such that
A3: for n st n >= m1 holds dist((seq1.n), (seq2.n)) < 1 by A2,Def2;
  consider p such that
A4: p > 0 and
A5: for n holds ||.seq1.n.|| <= p by A1,Def3;
A6: ex M st ( M > 0 & for n st n >= m1 holds ||.seq2.n.|| < M )
  proof
    take M = p + 1;
    now
      let n;
      assume n >= m1;
      then dist((seq1.n), (seq2.n)) < 1 by A3;
      then
A7:   ||.seq2.n - seq1.n.|| < 1 by BHSP_1:def 5;
      ||.seq2.n.|| - ||.seq1.n.|| <= ||.seq2.n - seq1.n.|| by BHSP_1:38;
      then ||.seq2.n.|| - ||.seq1.n.|| < 1 by A7,XXREAL_0:2;
      then
A8:   ||.seq2.n.|| < ||.seq1.n.|| + 1 by XREAL_1:21;
      ||.seq1.n.|| <= p by A5;
      then ||.seq1.n.|| + 1 <= p + 1 by XREAL_1:8;
      hence ||.seq2.n.|| < M by A8,XXREAL_0:2;
    end;
    hence thesis by A4;
  end;
  now
    consider M2 such that
A9: M2 > 0 and
A10: for n st n <= m1 holds ||.seq2.n.|| < M2 by Th23;
    consider M1 such that
A11: M1 > 0 and
A12: for n st n >= m1 holds ||.seq2.n.|| < M1 by A6;
    take M = M1 + M2;
    thus M > 0 by A11,A9;
    let n;
A13: M > 0 + M2 by A11,XREAL_1:10;
A14: now
      assume n <= m1;
      then ||.seq2.n.|| < M2 by A10;
      hence ||.seq2.n.|| <= M by A13,XXREAL_0:2;
    end;
A15: M > M1 + 0 by A9,XREAL_1:10;
    now
      assume n >= m1;
      then ||.seq2.n.|| < M1 by A12;
      hence ||.seq2.n.|| <= M by A15,XXREAL_0:2;
    end;
    hence ||.seq2.n.|| <= M by A14;
  end;
  hence thesis by Def3;
end;

canceled 5;

theorem Th31:
  seq is bounded & seq1 is subsequence of seq implies seq1 is bounded
proof
  assume that
A1: seq is bounded and
A2: seq1 is subsequence of seq;
  consider Nseq such that
A3: seq1 = seq * Nseq by A2,VALUED_0:def 17;
  consider M1 such that
A4: M1 > 0 and
A5: for n holds ||.seq.n.|| <= M1 by A1,Def3;
  take M = M1;
  now
    let n;
    seq1.n = seq.(Nseq.n) by A3,FUNCT_2:21;
    hence ||.seq1.n.|| <= M by A5;
  end;
  hence thesis by A4;
end;

theorem Th32:
  seq is convergent & seq1 is subsequence of seq implies seq1 is convergent
proof
  assume that
A1: seq is convergent and
A2: seq1 is subsequence of seq;
  consider g1 such that
A3: for r st r > 0 ex m st for n st n >= m holds ||.(seq.n) - g1.|| < r
  by A1,BHSP_2:9;
  consider Nseq such that
A4: seq1 = seq * Nseq by A2,VALUED_0:def 17;
  now
    let r;
    assume r > 0;
    then consider m1 such that
A5: for n st n >= m1 holds ||.(seq.n) - g1.|| < r by A3;
    take m = m1;
    let n such that
A6: n >= m;
    Nseq.n >= n by SEQM_3:33;
    then
A7: Nseq.n >= m by A6,XXREAL_0:2;
    seq1.n = seq.(Nseq.n) by A4,FUNCT_2:21;
    hence ||.(seq1.n) - g1.|| < r by A5,A7;
  end;
  hence thesis by BHSP_2:9;
end;

theorem Th33:
  seq1 is subsequence of seq & seq is convergent implies lim seq1= lim seq
proof
  assume that
A1: seq1 is subsequence of seq and
A2: seq is convergent;
  consider Nseq such that
A3: seq1 = seq * Nseq by A1,VALUED_0:def 17;
A4: now
    let r;
    assume r > 0;
    then consider m1 such that
A5: for n st n >= m1 holds dist((seq.n), (lim seq)) < r by A2,BHSP_2:def 2;
    take m = m1;
    let n such that
A6: n >= m;
    Nseq.n >= n by SEQM_3:33;
    then
A7: Nseq.n >= m by A6,XXREAL_0:2;
    seq1.n = seq.(Nseq.n) by A3,FUNCT_2:21;
    hence dist((seq1.n), (lim seq)) < r by A5,A7;
  end;
  seq1 is convergent by A1,A2,Th32;
  hence thesis by A4,BHSP_2:def 2;
end;

theorem Th34:
  seq is Cauchy & seq1 is subsequence of seq implies seq1 is Cauchy
proof
  assume that
A1: seq is Cauchy and
A2: seq1 is subsequence of seq;
  consider Nseq such that
A3: seq1 = seq * Nseq by A2,VALUED_0:def 17;
  now
    let r;
    assume r > 0;
    then consider l such that
A4: for n, m st n >= l & m >= l holds dist((seq.n), (seq.m)) < r by A1,Def1;
    take k = l;
    let n, m such that
A5: n >= k and
A6: m >= k;
    Nseq.n >= n by SEQM_3:33;
    then
A7: Nseq.n >= k by A5,XXREAL_0:2;
    Nseq.m >= m by SEQM_3:33;
    then
A8: Nseq.m >= k by A6,XXREAL_0:2;
    seq1.n = seq.(Nseq.n) & seq1.m = seq.(Nseq.m) by A3,FUNCT_2:21;
    hence dist((seq1.n), (seq1.m)) < r by A4,A7,A8;
  end;
  hence thesis by Def1;
end;

canceled;

theorem
  (seq ^\k)^\m = (seq ^\m)^\k
proof
  now
    let n;
    thus ((seq ^\k)^\m).n = (seq ^\k).(n + m) by NAT_1:def 3
      .= seq.(n + m + k) by NAT_1:def 3
      .= seq.(n + k + m)
      .= (seq ^\m).(n + k) by NAT_1:def 3
      .= ((seq ^\m)^\k).n by NAT_1:def 3;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem
  (seq ^\k)^\m=seq ^\(k + m)
proof
  now
    let n;
    thus ((seq ^\k)^\m).n = (seq ^\k).(n + m) by NAT_1:def 3
      .= seq.(n + m + k) by NAT_1:def 3
      .= seq.(n + (k + m))
      .= (seq ^\(k + m)).n by NAT_1:def 3;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th38:
  (seq1 + seq2) ^\k = (seq1 ^\k) + (seq2 ^\k)
proof
  now
    let n;
    thus ((seq1 + seq2) ^\k).n = (seq1 + seq2).(n + k) by NAT_1:def 3
      .= seq1.(n + k) + seq2.(n + k) by NORMSP_1:def 5
      .= (seq1 ^\k).n + seq2.(n + k) by NAT_1:def 3
      .= (seq1 ^\k).n + (seq2 ^\k).n by NAT_1:def 3
      .= ((seq1 ^\k) + (seq2 ^\k)).n by NORMSP_1:def 5;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th39:
  (-seq) ^\k = -(seq ^\k)
proof
  now
    let n;
    thus ((-seq) ^\k).n = (-seq).(n + k) by NAT_1:def 3
      .= -(seq.(n + k)) by BHSP_1:def 10
      .= -((seq ^\ k).n) by NAT_1:def 3
      .= (-(seq ^\k)).n by BHSP_1:def 10;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem
  (seq1 - seq2) ^\k = (seq1 ^\k) - (seq2 ^\k)
proof
  thus (seq1 - seq2) ^\k = (seq1 + (-seq2)) ^\k by BHSP_1:71
    .= (seq1 ^\k) + ((-seq2) ^\k) by Th38
    .= (seq1 ^\k) + -(seq2 ^\k) by Th39
    .= (seq1 ^\k) - (seq2 ^\k) by BHSP_1:71;
end;

theorem
  (a * seq) ^\k = a * (seq ^\k)
proof
  now
    let n;
    thus ((a * seq) ^\k).n = (a * seq).(n + k) by NAT_1:def 3
      .= a * (seq.(n + k)) by NORMSP_1:def 8
      .= a * ((seq ^\k).n) by NAT_1:def 3
      .= (a * (seq ^\k)).n by NORMSP_1:def 8;
  end;
  hence thesis by FUNCT_2:113;
end;

canceled 2;

theorem
  seq is convergent implies (seq ^\k) is convergent & lim (seq ^\k)=lim
  seq by Th32,Th33;

canceled;

theorem
  seq is convergent & (ex k st seq = seq1 ^\k) implies seq1 is convergent
proof
  assume that
A1: seq is convergent and
A2: ex k st seq = seq1 ^\k;
  consider k such that
A3: seq = seq1 ^\k by A2;
  consider g1 such that
A4: for r st r > 0 ex m st for n st n >= m holds ||.seq.n - g1.|| < r by A1,
BHSP_2:9;
  now
    take g = g1;
    let r;
    assume r > 0;
    then consider m1 such that
A5: for n st n >= m1 holds ||.seq.n - g.|| < r by A4;
    take m = m1 + k;
    let n;
    assume
A6: n >= m;
    then consider m2 being Nat such that
A7: n = m1 + k + m2 by NAT_1:10;
    reconsider m2 as Element of NAT by ORDINAL1:def 13;
    n - k = m1 + m2 by A7;
    then consider l such that
A8: l = n - k;
    now
      assume l < m1;
      then l + k < m1 + k by XREAL_1:8;
      hence contradiction by A6,A8;
    end;
    then
A9: ||.seq.l - g.|| < r by A5;
    l + k = n by A8;
    hence ||.seq1.n - g.|| < r by A3,A9,NAT_1:def 3;
  end;
  hence thesis by BHSP_2:9;
end;

theorem
  seq is Cauchy & (ex k st seq = seq1 ^\k) implies seq1 is Cauchy
proof
  assume that
A1: seq is Cauchy and
A2: ex k st seq = seq1 ^\k;
  consider k such that
A3: seq = seq1 ^\k by A2;
  let r;
  assume r > 0;
  then consider l1 such that
A4: for n, m st n >= l1 & m >= l1 holds dist((seq.n), (seq.m)) < r by A1,Def1;
  take l = l1 + k;
  let n, m;
  assume that
A5: n >= l and
A6: m >= l;
  consider m1 being Nat such that
A7: n = l1 + k + m1 by A5,NAT_1:10;
  reconsider m1 as Element of NAT by ORDINAL1:def 13;
  n - k = l1 + m1 by A7;
  then consider l2 such that
A8: l2 = n - k;
  consider m2 being Nat such that
A9: m = l1 + k + m2 by A6,NAT_1:10;
  reconsider m2 as Element of NAT by ORDINAL1:def 13;
  m - k = l1 + m2 by A9;
  then consider l3 such that
A10: l3 = m - k;
A11: now
    assume l2 < l1;
    then l2 + k < l1 + k by XREAL_1:8;
    hence contradiction by A5,A8;
  end;
A12: l2 + k = n by A8;
  now
    assume l3 < l1;
    then l3 + k < l1 + k by XREAL_1:8;
    hence contradiction by A6,A10;
  end;
  then dist((seq.l2), (seq.l3)) < r by A4,A11;
  then
A13: dist((seq1.n), (seq.l3)) < r by A3,A12,NAT_1:def 3;
  l3 + k = m by A10;
  hence thesis by A3,A13,NAT_1:def 3;
end;

theorem
  seq is Cauchy implies (seq ^\k) is Cauchy by Th34;

theorem
  seq1 is_compared_to seq2 implies (seq1 ^\k) is_compared_to (seq2 ^\k)
proof
  assume
A1: seq1 is_compared_to seq2;
  let r;
  assume r > 0;
  then consider m1 such that
A2: for n st n >= m1 holds dist((seq1.n), (seq2.n)) < r by A1,Def2;
  take m = m1;
  let n such that
A3: n >= m;
  n + k >= n by NAT_1:11;
  then n + k >= m by A3,XXREAL_0:2;
  then dist((seq1.(n + k)), (seq2.(n + k))) < r by A2;
  then dist((seq1 ^\k).n, (seq2.(n + k))) < r by NAT_1:def 3;
  hence thesis by NAT_1:def 3;
end;

theorem
  seq is bounded implies (seq ^\k) is bounded by Th31;

theorem
  seq is constant implies (seq ^\k) is constant;

definition
  canceled 2;
  let X;
  attr X is complete means
  :Def6:
  for seq holds seq is Cauchy implies seq is convergent;
end;

canceled;

theorem
  X is complete & seq is Cauchy implies seq is bounded
proof
  assume X is complete & seq is Cauchy;
  then seq is convergent by Def6;
  hence thesis by Th24;
end;
