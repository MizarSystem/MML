:: Real Function One-Side Differantiability
::  by Ewa Burakowska and Beata Madras
::
:: Received December 12, 1991
:: Copyright (c) 1991 Association of Mizar Users
 
environ

 vocabularies NUMBERS, FDIFF_1, SEQ_1, FUNCT_1, PARTFUN1, REAL_1, SUBSET_1,
      XXREAL_0, CARD_1, XXREAL_1, ARYTM_1, TARSKI, RELAT_1, ARYTM_3, FUNCOP_1,
      VALUED_0, SEQ_2, ORDINAL2, VALUED_1, FUNCT_2, NAT_1, LIMFUNC1, XBOOLE_0,
      XREAL_0, ORDINAL1, COMPLEX1, SQUARE_1, ORDINAL4, RCOMP_1, FDIFF_3;
 notations TARSKI, ORDINAL1, SUBSET_1, NUMBERS, XXREAL_0, XREAL_0, COMPLEX1,
      REAL_1, NAT_1, RELSET_1, PARTFUN1, FUNCT_2, FUNCOP_1, VALUED_0, VALUED_1,
      SEQ_1, SEQ_2, SQUARE_1, RFUNCT_1, RCOMP_1, FDIFF_1, LIMFUNC1, RECDEF_1;
 constructors FUNCOP_1, REAL_1, SQUARE_1, NAT_1, COMPLEX1, SEQ_2, SEQM_3,
      PROB_1, RCOMP_1, RFUNCT_1, RFUNCT_2, LIMFUNC1, PARTFUN1, FDIFF_1, SEQ_1,
      VALUED_1, RECDEF_1, XXREAL_2, SETFAM_1, RELSET_1, BINOP_2, RVSUM_1;
 registrations ORDINAL1, RELSET_1, NUMBERS, XXREAL_0, XREAL_0, MEMBERED,
      FDIFF_1, VALUED_0, VALUED_1, FUNCT_2, XXREAL_2, FUNCOP_1, SEQ_4,
      SETFAM_1, REALSET1;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, XBOOLE_0, SQUARE_1, PROB_1, LIMFUNC1, VALUED_1;
 theorems NAT_1, SEQ_1, SEQ_2, SEQM_3, SEQ_4, ABSVALUE, RFUNCT_1, RFUNCT_2,
      RCOMP_1, FDIFF_1, LIMFUNC2, FDIFF_2, TARSKI, SCHEME1, ZFMISC_1, FCONT_3,
      FUNCT_1, FUNCT_2, XBOOLE_0, XBOOLE_1, XCMPLX_0, XCMPLX_1, FUNCOP_1,
      XREAL_1, COMPLEX1, XXREAL_0, VALUED_1, XXREAL_1, XXREAL_2, VALUED_0,
      ORDINAL1;
 schemes SEQ_1, SCHEME1, FUNCT_2, FDIFF_2;

begin

reserve h,h1,h2 for convergent_to_0 Real_Sequence,
  c,c1 for constant Real_Sequence,
  f,f1,f2 for PartFunc of REAL,REAL,
  x0,r,r0,r1,r2,g,g1,g2 for Real,
  n0,k,n,m for Element of NAT,
  a,b,d for Real_Sequence,
  x for set;

theorem Th1:
  (ex r st r>0 & [.x0-r,x0.] c= dom f) implies ex h,c st rng c ={x0
  } & rng (h+c) c= dom f & for n holds h.n < 0
proof
  given r such that
A1: r>0 and
A2: [.x0-r,x0.] c= dom f;
  reconsider a = NAT --> x0 as Real_Sequence;
  reconsider a as constant Real_Sequence;
  deffunc F(Element of NAT) = (-r)/($1+2);
  consider b such that
A3: for n holds b.n = F(n) from SEQ_1:sch 1;
A4: now
    let n;
    0<=n by NAT_1:2;
    then 0 < r/(n+2) by A1,XREAL_1:141;
    then -(r/(n+2)) < -0 by XREAL_1:26;
    then (-r)/(n+2) < 0 by XCMPLX_1:188;
    hence b.n < 0 by A3;
  end;
  then for n holds 0 <> b.n;
  then
A5: b is non-zero by SEQ_1:7;
  b is convergent & lim b = 0 by A3,SEQ_4:46;
  then reconsider b as convergent_to_0 Real_Sequence by A5,FDIFF_1:def 1;
  take b;
  take a;
  thus rng a = {x0}
  proof
    thus rng a c= {x0}
    proof
      let x;
      assume x in rng a;
      then ex n st x = a.n by FUNCT_2:190;
      then x = x0 by FUNCOP_1:13;
      hence thesis by TARSKI:def 1;
    end;
    let x;
    assume x in {x0};
    then x = x0 by TARSKI:def 1
      .= a.0 by FUNCOP_1:13;
    hence thesis by VALUED_0:28;
  end;
  thus rng (b+a) c= dom f
  proof
    let x;
    assume x in rng (b+a);
    then consider n such that
A6: x = (b+a).n by FUNCT_2:190;
A7: 0+1 < n + 2 by NAT_1:2,XREAL_1:10;
    then r*1 < r*(n+2) by A1,XREAL_1:99;
    then r * (n+2)" < r*(n + 2)*((n + 2)") by A7,XREAL_1:70;
    then r * ((n+2)") < r*((n + 2)*(n + 2)");
    then r * (n+2)" < r * 1 by A7,XCMPLX_0:def 7;
    then r/(n+2) < r by XCMPLX_0:def 9;
    then x0 - r < x0 - r/(n+2) by XREAL_1:17;
    then x0 - r < x0 + - r/(n+2);
    then
A8: x0 - r <= x0 + (-r)/(n+2) by XCMPLX_1:188;
    0 < r/(n+2) by A1,A7,XREAL_1:141;
    then x0 - r/(n+2) < x0 - 0 by XREAL_1:17;
    then x0 + - r/(n+2) <= x0;
    then x0 + (-r)/(n+2) <= x0 by XCMPLX_1:188;
    then
A9: x0 + (-r)/(n+2) in {g1: x0 - r <= g1 & g1 <= x0 } by A8;
    x = b.n +a.n by A6,SEQ_1:11
      .= (-r)/(n+2) + a.n by A3
      .= x0 + (-r)/(n+2) by FUNCOP_1:13;
    then x in [.x0 - r, x0.] by A9,RCOMP_1:def 1;
    hence thesis by A2;
  end;
  thus thesis by A4;
end;

theorem Th2:
  (ex r st r>0 & [.x0,x0+r.] c= dom f) implies ex h,c st rng c ={x0
  } & rng (h+c) c= dom f & for n holds h.n > 0
proof
  given r such that
A1: r>0 and
A2: [.x0,x0+r.] c= dom f;
  reconsider a = NAT --> x0 as Real_Sequence;
  reconsider a as constant Real_Sequence;
  deffunc F(Element of NAT) = r/($1+2);
  consider b such that
A3: for n holds b.n = F(n) from SEQ_1:sch 1;
A4: now
    let n;
    0<=n by NAT_1:2;
    then 0 < r/(n+2) by A1,XREAL_1:141;
    hence 0 < b.n by A3;
  end;
  then for n holds 0<>b.n;
  then
A5: b is non-zero by SEQ_1:7;
  b is convergent & lim b = 0 by A3,SEQ_4:46;
  then reconsider b as convergent_to_0 Real_Sequence by A5,FDIFF_1:def 1;
  take b;
  take a;
  thus rng a = {x0}
  proof
    thus rng a c= {x0}
    proof
      let x;
      assume x in rng a;
      then ex n st x = a.n by FUNCT_2:190;
      then x = x0 by FUNCOP_1:13;
      hence thesis by TARSKI:def 1;
    end;
    let x;
    assume x in {x0};
    then x = x0 by TARSKI:def 1
      .= a.0 by FUNCOP_1:13;
    hence thesis by VALUED_0:28;
  end;
  thus rng (b+a) c= dom f
  proof
    let x;
    assume x in rng (b+a);
    then consider n such that
A6: x = (b+a).n by FUNCT_2:190;
A7: 0+1 < n + 2 by NAT_1:2,XREAL_1:10;
    then r*1 < r*(n+2) by A1,XREAL_1:99;
    then r * (n+2)" < r*(n + 2)*((n + 2)") by A7,XREAL_1:70;
    then r * (n+2)" < r*((n + 2)*(n + 2)");
    then r * (n+2)" < r * 1 by A7,XCMPLX_0:def 7;
    then r/(n+2) < r by XCMPLX_0:def 9;
    then
A8: x0 + r/(n+2) <= x0 + r by XREAL_1:8;
    0 < r/(n+2) by A1,A7,XREAL_1:141;
    then x0 + 0 < x0 + r/(n+2) by XREAL_1:10;
    then
A9: x0 + r/(n+2) in {g1: x0 <= g1 & g1 <= x0 + r} by A8;
    x = b.n +a.n by A6,SEQ_1:11
      .= r/(n+2) + a.n by A3
      .= r/(n+2) + x0 by FUNCOP_1:13;
    then x in [.x0,x0 + r.] by A9,RCOMP_1:def 1;
    hence thesis by A2;
  end;
  thus thesis by A4;
end;

theorem Th3:
  (for h,c st rng c ={x0} & rng (h+c) c= dom f & (for n holds h.n <
0) holds h"(#)(f/*(h+c) - f/*c) is convergent) & {x0} c= dom f implies for h1,
h2,c st rng c ={x0} & rng (h1+c) c= dom f & (for n holds h1.n < 0) & rng (h2+c)
c= dom f & (for n holds h2.n < 0) holds lim (h1"(#)(f/*(h1+c) - f/*c)) = lim (
  h2"(#)(f/*(h2+c) - f/*c))
proof
  assume that
A1: for h,c st rng c ={x0} & rng (h+c) c= dom f & (for n holds h.n < 0)
  holds h"(#)(f/*(h+c) - f/*c) is convergent and
A2: {x0} c= dom f;
  let h1,h2,c such that
A3: rng c ={x0} and
A4: rng (h1+c) c= dom f and
A5: for n holds h1.n < 0 and
A6: rng (h2+c) c= dom f and
A7: for n holds h2.n < 0;
  deffunc G(Element of NAT) = h2.$1;
  deffunc F(Element of NAT) = h1.$1;
  consider d such that
A8: for n holds d.(2*n) = F(n) & d.(2*n + 1) = G(n) from SCHEME1:sch 2;
A9: h2 is non-zero by FDIFF_1:def 1;
A10: h1 is non-zero by FDIFF_1:def 1;
  now
    let n;
    consider m such that
A11: n = 2*m or n = 2*m + 1 by SCHEME1:1;
    now
      per cases by A11;
      suppose
A12:    n = 2*m;
        d.(2*m) = h1.m by A8;
        hence d.n <> 0 by A10,A12,SEQ_1:7;
      end;
      suppose
A13:    n = 2*m + 1;
        d.(2*m + 1) = h2.m by A8;
        hence d.n <> 0 by A9,A13,SEQ_1:7;
      end;
    end;
    hence d.n <> 0;
  end;
  then
A14: d is non-zero by SEQ_1:7;
A15: h2 is convergent & lim h2 = 0 by FDIFF_1:def 1;
  h1 is convergent & lim h1 = 0 by FDIFF_1:def 1;
  then d is convergent & lim d = 0 by A8,A15,FDIFF_2:1;
  then reconsider d as convergent_to_0 Real_Sequence by A14,FDIFF_1:def 1;
  deffunc F(Element of NAT) = 2*$1;
  consider a such that
A16: for n holds a.n = F(n) from SEQ_1:sch 1;
  deffunc F(Element of NAT) = 2*$1+1;
  consider b such that
A17: for n holds b.n = F(n) from SEQ_1:sch 1;
  reconsider b as increasing sequence of NAT by A17,FDIFF_2:3;
A18: rng (d + c) c= dom f
  proof
    let x;
    assume x in rng (d + c);
    then consider n such that
A19: x = (d + c).n by FUNCT_2:190;
    consider m such that
A20: n = 2*m or n = 2*m + 1 by SCHEME1:1;
A21: x = d.n + c.n by A19,SEQ_1:11;
    now
      per cases by A20;
      suppose
        n = 2*m;
        then x = h1.m + c.(2*m) by A8,A21
          .= h1.m + c.m by VALUED_0:23
          .= (h1 + c).m by SEQ_1:11;
        then x in rng (h1 + c) by VALUED_0:28;
        hence thesis by A4;
      end;
      suppose
        n = 2*m + 1;
        then x = h2.m + c.(2*m +1) by A8,A21
          .= h2.m + c.m by VALUED_0:23
          .= (h2 + c).m by SEQ_1:11;
        then x in rng (h2 + c) by VALUED_0:28;
        hence thesis by A6;
      end;
    end;
    hence thesis;
  end;
  now
    let n;
    thus ((d"(#)(f/*(d+c) - f/*c))*b).n=(d"(#)(f/*(d+c) - f/*c)).(b.n) by
    FUNCT_2:21
      .= (d"(#)(f/*(d+c) - f/*c)).(2*n + 1) by A17
      .= (d").(2*n + 1)*(f/*(d+c) - f/*c).(2*n + 1) by SEQ_1:12
      .= (d").(2*n +1)*((f/*(d+c)).(2*n + 1) - (f/*c).(2*n + 1)) by RFUNCT_2
    :6
      .= (d.( 2*n + 1))"*((f/*(d+c)).(2*n + 1) - (f/*c).(2*n + 1)) by
    VALUED_1:10
      .= (h2.n)"*((f/*(d+c)).(2*n + 1) - (f/*c).(2*n + 1)) by A8
      .= (h2").n*((f/*(d+c)).(2*n + 1) - (f/*c).(2*n + 1)) by VALUED_1:10
      .= (h2").n*(f.((d + c).(2*n + 1)) - (f/*c).(2*n + 1)) by A18,FUNCT_2:
    185
      .= (h2").n*(f.(d.(2*n + 1) + c.(2*n + 1)) - (f/*c).(2*n + 1)) by SEQ_1
    :11
      .= (h2").n*(f.(h2.n + c.(2*n + 1)) - (f/*c).(2*n + 1)) by A8
      .= (h2").n*(f.(h2.n + c.n) - (f/*c).(2*n + 1)) by VALUED_0:23
      .= (h2").n*(f.((h2 + c).n) - (f/*c).(2*n + 1)) by SEQ_1:11
      .= (h2").n*((f/*(h2+c)).n - (f/*c).(2*n + 1)) by A6,FUNCT_2:185
      .= (h2").n*((f/*(h2+c)).n - f.(c.(2*n + 1))) by A2,A3,FUNCT_2:185
      .= (h2").n*((f/*(h2+c)).n - f.(c.n)) by VALUED_0:23
      .= (h2").n*((f/*(h2+c)).n - (f/*c).n) by A2,A3,FUNCT_2:185
      .= (h2").n*(f/*(h2+c) - f/*c).n by RFUNCT_2:6
      .= (h2"(#)(f/*(h2+c) - f/*c)).n by SEQ_1:12;
  end;
  then
A22: h2"(#)(f/*(h2+c) - f/*c) is subsequence of d"(#)(f/*(d+c) - f/*c) by
  FUNCT_2:113;
  now
    let n;
    consider m such that
A23: n = 2*m or n = 2*m + 1 by SCHEME1:1;
    now
      per cases by A23;
      suppose
A24:    n = 2*m;
        d.(2*m) = h1.m by A8;
        hence d.n < 0 by A5,A24;
      end;
      suppose
A25:    n = 2*m + 1;
        d.(2*m + 1) = h2.m by A8;
        hence d.n < 0 by A7,A25;
      end;
    end;
    hence d.n < 0;
  end;
  then
A26: d"(#)(f/*(d+c) - f/*c) is convergent by A1,A3,A18;
  reconsider a as increasing sequence of NAT by A16,FDIFF_2:2;
  now
    let n;
    thus ((d"(#)(f/*(d+c) - f/*c))*a).n = (d"(#)(f/*(d+c) - f/*c)).(a.n ) by
    FUNCT_2:21
      .= (d"(#)(f/*(d+c) - f/*c)).(2*n) by A16
      .= (d").(2*n)*(f/*(d+c) - f/*c).(2*n) by SEQ_1:12
      .= (d").(2*n)*((f/*(d+c)).(2*n) - (f/*c).(2*n)) by RFUNCT_2:6
      .= (d.(2*n))"*((f/*(d+c)).(2*n) - (f/*c).(2*n)) by VALUED_1:10
      .= (h1.n)"*((f/*(d+c)).(2*n) - (f/*c).(2*n)) by A8
      .= (h1").n*((f/*(d+c)).(2*n) - (f/*c).(2*n)) by VALUED_1:10
      .= (h1").n*(f.((d + c).(2*n)) - (f/*c).(2*n)) by A18,FUNCT_2:185
      .= (h1").n*(f.(d.(2*n) + c.(2*n)) - (f/*c).(2*n)) by SEQ_1:11
      .= (h1").n*(f.(h1.n + c.(2*n)) - (f/*c).(2*n)) by A8
      .= (h1").n*(f.(h1.n + c.n) - (f/*c).(2*n)) by VALUED_0:23
      .= (h1").n*(f.((h1 + c).n) - (f/*c).(2*n)) by SEQ_1:11
      .= (h1").n*((f/*(h1+c)).n - (f/*c).(2*n)) by A4,FUNCT_2:185
      .= (h1").n*((f/*(h1+c)).n - f.(c.(2*n))) by A2,A3,FUNCT_2:185
      .= (h1").n*((f/*(h1+c)).n - f.(c.n)) by VALUED_0:23
      .= (h1").n*((f/*(h1+c)).n - (f/*c).n) by A2,A3,FUNCT_2:185
      .= (h1").n*(f/*(h1+c) - f/*c).n by RFUNCT_2:6
      .= (h1"(#)(f/*(h1+c) - f/*c)).n by SEQ_1:12;
  end;
  then h1"(#)(f/*(h1+c) - f/*c) is subsequence of d"(#)(f/*(d+c) - f/*c) by
  FUNCT_2:113;
  then lim (h1"(#)(f/*(h1+c) - f/*c)) = lim (d"(#) (f/*(d+c) - f/*c)) by A26,
  SEQ_4:30;
  hence thesis by A26,A22,SEQ_4:30;
end;

theorem Th4:
  (for h,c st rng c ={x0} & rng (h+c) c= dom f & (for n holds h.n>0
) holds h"(#)(f/*(h+c) - f/*c) is convergent) & {x0} c= dom f implies for h1,h2
,c st rng c ={x0} & rng (h1 + c)c= dom f & rng (h2 + c) c= dom f & (for n holds
h1.n >0) & (for n holds h2.n >0) holds lim (h1"(#)(f/*(h1+c) - f/*c)) = lim (h2
  "(#)(f/*(h2+c) - f/*c))
proof
  assume that
A1: for h,c st rng c ={x0} & rng (h+c) c= dom f & (for n holds h.n>0)
  holds h"(#)(f/*(h+c) - f/*c) is convergent and
A2: {x0} c= dom f;
  let h1,h2,c such that
A3: rng c ={x0} and
A4: rng (h1 + c) c= dom f and
A5: rng (h2 + c) c= dom f and
A6: for n holds h1.n >0 and
A7: for n holds h2.n >0;
  deffunc G(Element of NAT) = h2.$1;
  deffunc F(Element of NAT) = h1.$1;
  consider d such that
A8: for n holds d.(2*n)=F(n) & d.(2*n+1)=G(n) from SCHEME1:sch 2;
A9: h2 is non-zero by FDIFF_1:def 1;
A10: h1 is non-zero by FDIFF_1:def 1;
  now
    let n;
    consider m such that
A11: n = 2*m or n = 2*m+1 by SCHEME1:1;
    now
      per cases by A11;
      suppose
        n = 2*m;
        then d.n = h1.m by A8;
        hence d.n<>0 by A10,SEQ_1:7;
      end;
      suppose
        n = 2*m+1;
        then d.n = h2.m by A8;
        hence d.n<>0 by A9,SEQ_1:7;
      end;
    end;
    hence d.n<>0;
  end;
  then
A12: d is non-zero by SEQ_1:7;
A13: h2 is convergent & lim h2 =0 by FDIFF_1:def 1;
  h1 is convergent & lim h1 =0 by FDIFF_1:def 1;
  then d is convergent & lim d =0 by A8,A13,FDIFF_2:1;
  then reconsider d as convergent_to_0 Real_Sequence by A12,FDIFF_1:def 1;
  deffunc F(Element of NAT) = 2*$1;
  consider a such that
A14: for n holds a.n = F(n) from SEQ_1:sch 1;
  deffunc F(Element of NAT) = 2*$1+1;
  consider b such that
A15: for n holds b.n = F(n) from SEQ_1:sch 1;
  reconsider b as increasing sequence of NAT by A15,FDIFF_2:3;
A16: rng (d+c) c= dom f
  proof
    let x;
    assume x in rng (d+c);
    then consider n such that
A17: x = (d+c).n by FUNCT_2:190;
    consider m such that
A18: n=2*m or n=2*m+1 by SCHEME1:1;
    now
      per cases by A18;
      suppose
        n = 2*m;
        then x = d.(2*m) + c.(2*m) by A17,SEQ_1:11
          .= h1.m + c.(2*m) by A8
          .= h1.m + c.m by VALUED_0:23
          .= (h1+c).m by SEQ_1:11;
        then x in rng(h1+c) by VALUED_0:28;
        hence thesis by A4;
      end;
      suppose
        n = 2*m+1;
        then x = d.(2*m+1) + c.(2*m+1) by A17,SEQ_1:11
          .= h2.m + c.(2*m+1) by A8
          .= h2.m + c.m by VALUED_0:23
          .= (h2 + c).m by SEQ_1:11;
        then x in rng(h2+c) by VALUED_0:28;
        hence thesis by A5;
      end;
    end;
    hence thesis;
  end;
  now
    let n;
    thus ((d"(#)(f/*(d+c) - f/*c))*b).n = (d"(#) (f/*(d+c) - f/*c)).(b.n) by
    FUNCT_2:21
      .= (d"(#)(f/*(d+c) - f/*c)).(2*n+1) by A15
      .= (d").(2*n+1) * (f/*(d+c) - f/*c).(2*n+1) by SEQ_1:12
      .= (d.(2*n+1))" * (f/*(d+c) - f/*c).(2*n+1) by VALUED_1:10
      .= (h2.n)" * (f/*(d+c) - f/*c).(2*n+1) by A8
      .= (h2").n * (f/*(d+c) - f/*c).(2*n+1) by VALUED_1:10
      .= (h2").n *((f/*(d+c)).(2*n+1) - (f/*c).(2*n+1)) by RFUNCT_2:6
      .= (h2").n *(f.((d+c).(2*n+1)) - (f/*c).(2*n+1)) by A16,FUNCT_2:185
      .= (h2").n *(f.((d+c).(2*n+1)) - f.(c.(2*n+1))) by A2,A3,FUNCT_2:185
      .= (h2").n *(f.(d.(2*n+1)+c.(2*n+1)) - f.(c.(2*n+1))) by SEQ_1:11
      .= (h2").n *(f.(h2.n+c.(2*n+1)) - f.(c.(2*n+1))) by A8
      .= (h2").n *(f.(h2.n+c.(2*n+1)) - f.(c.n)) by VALUED_0:23
      .= (h2").n *(f.(h2.n+c.n) - f.(c.n)) by VALUED_0:23
      .= (h2").n *(f.((h2+c).n) - f.(c.n)) by SEQ_1:11
      .= (h2").n *((f/*(h2+c)).n - f.(c.n)) by A5,FUNCT_2:185
      .= (h2").n *((f/*(h2+c)).n - (f/*c).n) by A2,A3,FUNCT_2:185
      .= (h2").n *(f/*(h2+c) - f/*c).n by RFUNCT_2:6
      .= (h2"(#)(f/*(h2+c) - f/*c)).n by SEQ_1:12;
  end;
  then
A19: h2"(#)(f/*(h2+c) - f/*c) is subsequence of d"(#)(f/*(d+c) - f/*c) by
  FUNCT_2:113;
  for n holds d.n > 0
  proof
    let n;
    consider m such that
A20: n = 2*m or n = 2*m+1 by SCHEME1:1;
    now
      per cases by A20;
      suppose
        n = 2*m;
        then d.n = h1.m by A8;
        hence thesis by A6;
      end;
      suppose
        n = 2*m+1;
        then d.n = h2.m by A8;
        hence thesis by A7;
      end;
    end;
    hence thesis;
  end;
  then
A21: d"(#)(f/*(d+c) - f/*c) is convergent by A1,A3,A16;
  reconsider a as increasing sequence of NAT by A14,FDIFF_2:2;
  now
    let n;
    thus ((d"(#)(f/*(d+c) - f/*c))*a).n = (d"(#) (f/*(d+c) - f/*c)).(a.n) by
    FUNCT_2:21
      .= (d"(#)(f/*(d+c) - f/*c)).(2*n) by A14
      .= (d").(2*n) * (f/*(d+c) - f/*c).(2*n) by SEQ_1:12
      .= (d.(2*n))" * (f/*(d+c) - f/*c).(2*n) by VALUED_1:10
      .= (h1.n)" * (f/*(d+c) - f/*c).(2*n) by A8
      .= (h1").n * (f/*(d+c) - f/*c).(2*n) by VALUED_1:10
      .= (h1").n *((f/*(d+c)).(2*n) - (f/*c).(2*n)) by RFUNCT_2:6
      .= (h1").n *(f.((d+c).(2*n)) - (f/*c).(2*n)) by A16,FUNCT_2:185
      .= (h1").n *(f.((d+c).(2*n)) - f.(c.(2*n))) by A2,A3,FUNCT_2:185
      .= (h1").n *(f.(d.(2*n)+c.(2*n)) - f.(c.(2*n))) by SEQ_1:11
      .= (h1").n *(f.(h1.n+c.(2*n)) - f.(c.(2*n))) by A8
      .= (h1").n *(f.(h1.n+c.(2*n)) - f.(c.n)) by VALUED_0:23
      .= (h1").n *(f.(h1.n+c.n) - f.(c.n)) by VALUED_0:23
      .= (h1").n *(f.((h1+c).n) - f.(c.n)) by SEQ_1:11
      .= (h1").n *((f/*(h1+c)).n - f.(c.n)) by A4,FUNCT_2:185
      .= (h1").n *((f/*(h1+c)).n - (f/*c).n) by A2,A3,FUNCT_2:185
      .= (h1").n *(f/*(h1+c) - f/*c).n by RFUNCT_2:6
      .= (h1"(#)(f/*(h1+c) - f/*c)).n by SEQ_1:12;
  end;
  then h1"(#)(f/*(h1+c) - f/*c) is subsequence of d"(#)(f/*(d+c) - f/*c) by
  FUNCT_2:113;
  then lim (h1"(#)(f/*(h1+c) - f/*c)) = lim (d"(#) (f/*(d+c) -f/*c)) by A21,
  SEQ_4:30;
  hence thesis by A21,A19,SEQ_4:30;
end;

definition
  let f,x0;
  pred f is_Lcontinuous_in x0 means
  :Def1:
  x0 in dom f & for a st rng a c=
  left_open_halfline(x0) /\ dom f & a is convergent & lim a = x0 holds f/*a is
  convergent & f.x0 = lim(f/*a);
  pred f is_Rcontinuous_in x0 means
  :Def2:
  x0 in dom f & for a st rng a c=
  right_open_halfline(x0) /\ dom f & a is convergent & lim a = x0 holds f/*a is
  convergent & f.x0 = lim(f/*a);
  pred f is_right_differentiable_in x0 means
  :Def3:
  (ex r st r>0 & [.x0, x0+r
.] c= dom f) & for h,c st rng c ={x0} & rng (h+c) c= dom f & (for n holds h.n >
  0) holds h"(#)(f/*(h+c) - f/*c) is convergent;
  pred f is_left_differentiable_in x0 means
  :Def4:
  (ex r st r>0 & [.x0-r,x0.]
  c= dom f) & for h,c st rng c ={x0} & rng (h+c) c= dom f & (for n holds h.n<0)
  holds h"(#)(f/*(h+c) - f/*c) is convergent;
end;

theorem Th5:
  f is_left_differentiable_in x0 implies f is_Lcontinuous_in x0
proof
  assume
A1: f is_left_differentiable_in x0;
A2: for a st rng a c= left_open_halfline(x0) /\ dom f & a is convergent &
  lim a = x0 holds f/*a is convergent & f.x0 = lim(f/*a)
  proof
    reconsider b = NAT --> x0 as Real_Sequence;
    let a such that
A3: rng a c= left_open_halfline(x0) /\ dom f and
A4: a is convergent and
A5: lim a = x0;
    consider r such that
A6: 0 < r and
A7: [.x0 - r, x0.] c= dom f by A1,Def4;
    consider n0 such that
A8: for k st k>=n0 holds x0 - r<a.k by A4,A5,A6,LIMFUNC2:1,XREAL_1
    :46;
    deffunc F(Element of NAT) = a.$1-b.$1;
    consider d such that
A9: for n holds d.n = F(n) from SEQ_1:sch 1;
A10: d = a - b by A9,RFUNCT_2:6;
    then
A11: d is convergent by A4,SEQ_2:25;
    reconsider c = b^\n0 as constant Real_Sequence;
A12: rng c = {x0}
    proof
      thus rng c c= {x0}
      proof
        let x;
        assume x in rng c;
        then consider n such that
A13:    x = (b^\n0).n by FUNCT_2:190;
        x = b.(n + n0) by A13,NAT_1:def 3;
        then x = x0 by FUNCOP_1:13;
        hence thesis by TARSKI:def 1;
      end;
      let x;
      assume x in {x0};
      then
A14:  x = x0 by TARSKI:def 1;
      c.0 = b.(0 + n0) by NAT_1:def 3
        .= x by A14,FUNCOP_1:13;
      hence thesis by VALUED_0:28;
    end;
A15: now
      let g be real number such that
A16:  0<g;
      take n=0;
      let m such that
      n<=m;
      x0 - r <= x0 by A6,XREAL_1:46;
      then x0 in [.x0 - r,x0.] by XXREAL_1:1;
      then rng c c= dom f by A7,A12,ZFMISC_1:37;
      then abs((f/*c).m-f.x0) = abs(f.(c.m)-f.x0) by FUNCT_2:185
        .= abs(f.(b.(m+n0))-f.x0) by NAT_1:def 3
        .= abs(f.x0-f.x0) by FUNCOP_1:13
        .= 0 by ABSVALUE:7;
      hence abs((f/*c).m-f.x0)<g by A16;
    end;
    then
A17: f/*c is convergent by SEQ_2:def 6;
    lim b = b.0 by SEQ_4:41
      .= x0 by FUNCOP_1:13;
    then lim d = x0 - x0 by A4,A5,A10,SEQ_2:26
      .= 0;
    then
A18: lim (d^\n0) = 0 by A11,SEQ_4:33;
A19: for n holds d.n < 0 & d.n <> 0
    proof
      let n;
A20:  d.n = a.n - b.n by A9;
      a.n in rng a by VALUED_0:28;
      then a.n in left_open_halfline(x0) by A3,XBOOLE_0:def 4;
      then a.n in { r1: r1 < x0 } by XXREAL_1:229;
      then
A21:  ex r1 st r1 = a.n & r1 < x0;
      then a.n - x0 < x0 - x0 by XREAL_1:11;
      hence d.n < 0 by A20,FUNCOP_1:13;
      thus thesis by A20,A21,FUNCOP_1:13;
    end;
A22: for n holds (d^\n0).n < 0
    proof
      let n;
      (d^\n0).n = d.(n + n0) by NAT_1:def 3;
      hence thesis by A19;
    end;
    then for n holds (d^\n0).n <>0;
    then d ^\n0 is non-zero by SEQ_1:7;
    then reconsider h = d^\n0 as convergent_to_0 Real_Sequence by A11,A18,
    FDIFF_1:def 1;
A23: rng a c= dom f
    proof
      let x;
      assume x in rng a;
      hence thesis by A3,XBOOLE_0:def 4;
    end;
    now
      let n;
      thus (f/*(h+c)-f/*c+f/*c).n=(f/*(h+c)-f/*c).n+(f/*c).n by SEQ_1:11
        .= (f/*(h+c)).n-(f/*c).n+(f/*c).n by RFUNCT_2:6
        .= (f/*(h+c)).n;
    end;
    then
A24: f/*(h+c)-f/*c+(f/*c)=f/*(h+c) by FUNCT_2:113;
    now
      let n;
      thus (h+c).n=((a-b+b)^\n0).n by A10,SEQM_3:37
        .= (a-b+b).(n+n0) by NAT_1:def 3
        .= (a-b).(n+n0)+b.(n+n0) by SEQ_1:11
        .= a.(n+n0)-b.(n+n0)+b.(n+n0) by RFUNCT_2:6
        .= (a^\n0).n by NAT_1:def 3;
    end;
    then
A25: f/*(h+c)-f/*c+(f/*c)=f/*(a^\n0) by A24,FUNCT_2:113
      .= (f/*a)^\n0 by A23,VALUED_0:27;
A26: for n holds (a^\n0).n in [.x0 - r,x0.]
    proof
      let n;
      a.(n + n0) in rng a by VALUED_0:28;
      then (a^\n0).n in rng a by NAT_1:def 3;
      then (a^\n0).n in left_open_halfline(x0) by A3,XBOOLE_0:def 4;
      then (a^\n0).n in {g:g < x0} by XXREAL_1:229;
      then
A27:  ex g st g = (a^\n0).n & g < x0;
      0 <= n & 0 + n0 = n0 by NAT_1:2;
      then n0 <= n0 + n by XREAL_1:8;
      then x0 - r <= a.(n + n0) by A8;
      then x0 - r <= (a^\n0).n by NAT_1:def 3;
      then (a^\n0).n in {g: x0 - r <= g & g <= x0} by A27;
      hence thesis by RCOMP_1:def 1;
    end;
    rng (h + c) c= [.x0 - r,x0.]
    proof
      let x;
      assume x in rng(h + c);
      then consider n such that
A28:  x = (h+c).n by FUNCT_2:190;
      (h+c).n=((a - b +b)^\n0).n by A10,SEQM_3:37
        .= (a - b + b) .(n + n0) by NAT_1:def 3
        .= (a - b).(n + n0) + b.(n + n0) by SEQ_1:11
        .= a.(n + n0) - b.(n + n0) + b.(n + n0) by RFUNCT_2:6
        .= (a^\n0).n by NAT_1:def 3;
      hence thesis by A26,A28;
    end;
    then rng (h + c) c= dom f by A7,XBOOLE_1:1;
    then
A29: h"(#)(f/*(h+c) - f/*c) is convergent by A1,A22,A12,Def4;
    then
A30: lim (h(#)(h"(#)(f/*(h+c) - f/*c)))=0*lim(h"(#) (f/*(h+c) - f/*c)) by
    A11,A18,SEQ_2:29
      .=0;
    now
      let n;
A31:  h.n<>0 by A22;
      thus (h(#)(h"(#)(f/*(h+c) - f/*c))).n=h.n *(h"(#) (f/*(h+c) - f/*c)).n
      by SEQ_1:12
        .= h.n*((h").n*(f/*(h+c) - f/*c).n) by SEQ_1:12
        .= h.n*(((h.n)")*(f/*(h+c) - f/*c).n) by VALUED_1:10
        .= h.n*((h.n)")*(f/*(h+c) - f/*c).n
        .= 1*(f/*(h+c) - f/*c).n by A31,XCMPLX_0:def 7
        .= (f/*(h+c) - f/*c).n;
    end;
    then
A32: h(#)(h"(#)(f/*(h+c) - f/*c))=f/*(h+c)-f/*c by FUNCT_2:113;
    then
A33: f/*(h+c)-f/*c is convergent by A11,A29,SEQ_2:28;
    then
A34: f/*(h+c)-f/*c+f/*c is convergent by A17,SEQ_2:19;
    hence f/*a is convergent by A25,SEQ_4:35;
    lim(f/*c)=f.x0 by A15,A17,SEQ_2:def 7;
    then lim(f/*(h+c)-f/*c+f/*c) = 0+f.x0 by A30,A32,A33,A17,SEQ_2:20
      .= f.x0;
    hence thesis by A34,A25,SEQ_4:36;
  end;
  x0 in dom f
  proof
    consider r such that
A35: 0 < r and
A36: [.x0 - r,x0.] c= dom f by A1,Def4;
    x0 - r <= x0 by A35,XREAL_1:46;
    then x0 in [.x0 - r,x0.] by XXREAL_1:1;
    hence thesis by A36;
  end;
  hence thesis by A2,Def1;
end;

theorem Th6:
  f is_Lcontinuous_in x0 & f.x0<>g2 & (ex r st r>0 & [.x0-r,x0.] c=
dom f) implies ex r1 st r1 > 0 & [.x0-r1,x0.] c= dom f & for g st g in [.x0-r1,
  x0.] holds f.g <> g2
proof
  assume that
A1: f is_Lcontinuous_in x0 and
A2: f.x0 <> g2;
  given r such that
A3: r>0 and
A4: [.x0-r,x0.] c= dom f;
  defpred P[Element of NAT,set] means $2 in [.x0-r/($1+1),x0.] & $2 in dom f &
  f.$2 = g2;
  assume
A5: for r1 st r1>0 & [.x0-r1,x0.] c= dom f ex g st g in [.x0-r1,x0.] & f
  .g = g2;
A6: for n ex g be Real st P[n,g]
  proof
    let n;
    x0 - r <= x0 by A3,XREAL_1:46;
    then
A7: x0 in [.x0 - r,x0.] by XXREAL_1:1;
A8: 0 <= n by NAT_1:2;
    then 0 + 1 <= n + 1 by XREAL_1:9;
    then r/(n+1) <= r/1 by A3,XREAL_1:120;
    then
A9: x0 - r <= x0 - r/(n+1) by XREAL_1:15;
    x0 - r/(n+1) <= x0 by A3,A8,XREAL_1:46,141;
    then x0 - r/(n+1) in {g1: x0 - r <= g1 & g1 <= x0} by A9;
    then x0 - r/(n+1) in [.x0 - r, x0.] by RCOMP_1:def 1;
    then [.x0 - r/(n+1),x0.] c= [.x0 - r,x0.] by A7,XXREAL_2:def 12;
    then
A10: [.x0 - r/(n+1),x0.] c= dom f by A4,XBOOLE_1:1;
    then consider g such that
A11: g in [.x0-r/(n+1),x0.] & f.g = g2 by A3,A5,A8,XREAL_1:141;
    take g;
    thus thesis by A10,A11;
  end;
  consider a such that
A12: for n holds P[n,a.n] from FUNCT_2:sch 3(A6);
A13: rng a c= left_open_halfline(x0) /\ dom f
  proof
    let x;
    assume x in rng a;
    then consider n such that
A14: x=a.n by FUNCT_2:190;
    a.n in [.x0-r/(n+1),x0.] by A12;
    then a.n in {g1: x0 - r/(n+1) <= g1 & g1 <= x0} by RCOMP_1:def 1;
    then
A15: ex g1 st g1 = a.n & x0 - r/(n+1) <= g1 & g1 <=x0;
    a.n <> x0 by A2,A12;
    then a.n < x0 by A15,XXREAL_0:1;
    then a.n in {g1: g1 < x0};
    then
A16: a.n in left_open_halfline(x0) by XXREAL_1:229;
    a.n in dom f by A12;
    hence thesis by A14,A16,XBOOLE_0:def 4;
  end;
A17: left_open_halfline(x0) /\ dom f c= dom f by XBOOLE_1:17;
A18: for n holds (f/*a).n = g2
  proof
    let n;
    thus (f/*a).n = f.(a.n) by A13,A17,FUNCT_2:185,XBOOLE_1:1
      .= g2 by A12;
  end;
  now
    let n be Nat;
    n in NAT by ORDINAL1:def 13;
    then (f/*a).n = g2 by A18;
    hence (f/*a).n =(f/*a).(n+1) by A18;
  end;
  then
A19: lim (f/*a) = (f/*a).0 by SEQ_4:41,VALUED_0:25
    .= g2 by A18;
  reconsider d = NAT --> x0 as Real_Sequence;
  deffunc F(Element of NAT) = x0 - r/($1+1);
  consider b such that
A20: for n holds b.n = F(n) from SEQ_1:sch 1;
A21: now
    let n;
    a.n in [.x0 - r/(n+1), x0.] by A12;
    then a.n in {g1: x0 - r/(n+1) <= g1 & g1 <= x0} by RCOMP_1:def 1;
    then ex g1 st g1 = a.n & x0 - r/(n+1) <= g1 & g1 <=x0;
    hence b.n <= a.n & a.n <= d.n by A20,FUNCOP_1:13;
  end;
A22: lim d = d.0 by SEQ_4:41
    .= x0 by FUNCOP_1:13;
  b is convergent & lim b = x0 by A20,FCONT_3:13;
  then a is convergent & lim a = x0 by A22,A21,SEQ_2:33,34;
  hence contradiction by A1,A2,A13,A19,Def1;
end;

theorem Th7:
  f is_right_differentiable_in x0 implies f is_Rcontinuous_in x0
proof
  assume
A1: f is_right_differentiable_in x0;
  then consider r such that
A2: r>0 and
A3: [.x0,x0 + r.] c= dom f by Def3;
A4: for a st rng a c= right_open_halfline(x0) /\ dom f & a is convergent &
  lim a = x0 holds f/*a is convergent & f.x0 = lim(f/*a)
  proof
    reconsider b = NAT --> x0 as Real_Sequence;
    let a such that
A5: rng a c= right_open_halfline(x0) /\ dom f and
A6: a is convergent and
A7: lim a = x0;
    consider r such that
A8: r > 0 and
A9: [.x0, x0 + r.] c= dom f by A1,Def3;
    x0 + 0 < x0 + r by A8,XREAL_1:8;
    then consider n0 be Element of NAT such that
A10: for k st k>=n0 holds a.k < x0 +r by A6,A7,LIMFUNC2:2;
    deffunc F(Element of NAT) = a.$1-b.$1;
    consider d such that
A11: for n holds d.n = F(n) from SEQ_1:sch 1;
A12: d = a - b by A11,RFUNCT_2:6;
    then
A13: d is convergent by A6,SEQ_2:25;
    reconsider c = b^\n0 as constant Real_Sequence;
A14: rng c = {x0}
    proof
      thus rng c c= {x0}
      proof
        let x;
        assume x in rng c;
        then consider n such that
A15:    x = (b^\n0).n by FUNCT_2:190;
        x = b.(n + n0) by A15,NAT_1:def 3;
        then x = x0 by FUNCOP_1:13;
        hence thesis by TARSKI:def 1;
      end;
      let x;
      assume x in {x0};
      then
A16:  x = x0 by TARSKI:def 1;
      c.0 = b.(0 + n0) by NAT_1:def 3
        .= x by A16,FUNCOP_1:13;
      hence thesis by VALUED_0:28;
    end;
A17: now
      let g be real number such that
A18:  0<g;
      take n=0;
      let m such that
      n<=m;
      x0 + 0 <= x0 +r by A8,XREAL_1:9;
      then x0 in [.x0, x0 + r.] by XXREAL_1:1;
      then rng c c= dom f by A9,A14,ZFMISC_1:37;
      then abs((f/*c).m-f.x0) = abs(f.(c.m)-f.x0) by FUNCT_2:185
        .= abs(f.(b.(m+n0))-f.x0) by NAT_1:def 3
        .= abs(f.x0-f.x0) by FUNCOP_1:13
        .= 0 by ABSVALUE:7;
      hence abs((f/*c).m-f.x0)<g by A18;
    end;
    then
A19: f/*c is convergent by SEQ_2:def 6;
    lim b = b.0 by SEQ_4:41
      .= x0 by FUNCOP_1:13;
    then lim d = x0 - x0 by A6,A7,A12,SEQ_2:26
      .= 0;
    then
A20: lim (d^\n0) = 0 by A13,SEQ_4:33;
A21: for n holds d.n > 0 & d.n <> 0
    proof
      let n;
A22:  d.n = a.n - b.n by A11;
      a.n in rng a by VALUED_0:28;
      then a.n in right_open_halfline(x0) by A5,XBOOLE_0:def 4;
      then a.n in { r1: x0 < r1 } by XXREAL_1:230;
      then
A23:  ex r1 st r1 = a.n & x0 < r1;
      then a.n - x0 > x0 - x0 by XREAL_1:11;
      hence d.n > 0 by A22,FUNCOP_1:13;
      thus thesis by A22,A23,FUNCOP_1:13;
    end;
A24: for n holds (d^\n0).n > 0
    proof
      let n;
      (d^\n0).n = d.(n + n0) by NAT_1:def 3;
      hence thesis by A21;
    end;
    then for n holds (d^\n0).n <>0;
    then d ^\n0 is non-zero by SEQ_1:7;
    then reconsider h = d^\n0 as convergent_to_0 Real_Sequence by A13,A20,
    FDIFF_1:def 1;
A25: rng a c= dom f
    proof
      let x;
      assume x in rng a;
      hence thesis by A5,XBOOLE_0:def 4;
    end;
    now
      let n;
      thus (f/*(h+c)-f/*c+f/*c).n=(f/*(h+c)-f/*c).n+(f/*c).n by SEQ_1:11
        .= (f/*(h+c)).n-(f/*c).n+(f/*c).n by RFUNCT_2:6
        .= (f/*(h+c)).n;
    end;
    then
A26: f/*(h+c)-f/*c+(f/*c)=f/*(h+c) by FUNCT_2:113;
    now
      let n;
      thus (h+c).n=((a-b+b)^\n0).n by A12,SEQM_3:37
        .= (a-b+b).(n+n0) by NAT_1:def 3
        .= (a-b).(n+n0)+b.(n+n0) by SEQ_1:11
        .= a.(n+n0)-b.(n+n0)+b.(n+n0) by RFUNCT_2:6
        .= (a^\n0).n by NAT_1:def 3;
    end;
    then
A27: f/*(h+c)-f/*c+(f/*c)=f/*(a^\n0) by A26,FUNCT_2:113
      .= (f/*a)^\n0 by A25,VALUED_0:27;
A28: for n holds (a^\n0).n in [.x0, x0 + r.]
    proof
      let n;
      a.(n + n0) in rng a by VALUED_0:28;
      then (a^\n0).n in rng a by NAT_1:def 3;
      then (a^\n0).n in right_open_halfline(x0) by A5,XBOOLE_0:def 4;
      then (a^\n0).n in {g:x0 < g} by XXREAL_1:230;
      then
A29:  ex g st g = (a^\n0).n & x0 < g;
      0 <= n & 0 + n0 = n0 by NAT_1:2;
      then n0 <= n0 + n by XREAL_1:8;
      then a.(n + n0) <= x0 +r by A10;
      then (a^\n0).n <= x0 + r by NAT_1:def 3;
      then (a^\n0).n in {g: x0 <= g & g <= x0 + r} by A29;
      hence thesis by RCOMP_1:def 1;
    end;
    rng (h + c) c= [.x0,x0 + r.]
    proof
      let x;
      assume x in rng(h + c);
      then consider n such that
A30:  x = (h+c).n by FUNCT_2:190;
      (h+c).n=((a - b +b)^\n0).n by A12,SEQM_3:37
        .= (a - b + b) .(n + n0) by NAT_1:def 3
        .= (a - b).(n + n0) + b.(n + n0) by SEQ_1:11
        .= a.(n + n0) - b.(n + n0) + b.(n + n0) by RFUNCT_2:6
        .= (a^\n0).n by NAT_1:def 3;
      hence thesis by A28,A30;
    end;
    then rng (h + c) c= dom f by A9,XBOOLE_1:1;
    then
A31: h"(#)(f/*(h+c) - f/*c) is convergent by A1,A24,A14,Def3;
    then
A32: lim (h(#)(h"(#)(f/*(h+c) - f/*c)))=0*lim(h"(#) (f/*(h+c) - f/*c)) by
    A13,A20,SEQ_2:29
      .=0;
    now
      let n;
A33:  h.n<>0 by A24;
      thus (h(#)(h"(#)(f/*(h+c) - f/*c))).n=h.n *(h"(#) (f/*(h+c) - f/*c)).n
      by SEQ_1:12
        .= h.n*((h").n*(f/*(h+c) - f/*c).n) by SEQ_1:12
        .= h.n*(((h.n)")*(f/*(h+c) - f/*c).n) by VALUED_1:10
        .= h.n*((h.n)")*(f/*(h+c) - f/*c).n
        .= 1*(f/*(h+c) - f/*c).n by A33,XCMPLX_0:def 7
        .= (f/*(h+c) - f/*c).n;
    end;
    then
A34: h(#)(h"(#)(f/*(h+c) - f/*c))=f/*(h+c)-f/*c by FUNCT_2:113;
    then
A35: f/*(h+c)-f/*c is convergent by A13,A31,SEQ_2:28;
    then
A36: f/*(h+c)-f/*c+f/*c is convergent by A19,SEQ_2:19;
    hence f/*a is convergent by A27,SEQ_4:35;
    lim(f/*c)=f.x0 by A17,A19,SEQ_2:def 7;
    then lim(f/*(h+c)-f/*c+f/*c) = 0+f.x0 by A32,A34,A35,A19,SEQ_2:20
      .= f.x0;
    hence thesis by A36,A27,SEQ_4:36;
  end;
  x0 + 0 <= x0 +r by A2,XREAL_1:9;
  then x0 in [.x0, x0 + r.] by XXREAL_1:1;
  hence thesis by A3,A4,Def2;
end;

theorem Th8:
  f is_Rcontinuous_in x0 & f.x0 <> g2 & (ex r st r>0 & [.x0, x0+r.]
c= dom f) implies ex r1 st r1>0 & [.x0, x0+r1.] c= dom f & for g st g in [.x0,
  x0+r1.] holds f.g <> g2
proof
  assume that
A1: f is_Rcontinuous_in x0 and
A2: f.x0 <> g2;
  given r such that
A3: r>0 and
A4: [.x0, x0+r.] c= dom f;
  defpred P[Element of NAT,set] means $2 in [.x0, x0+r/($1+1).] & $2 in dom f
  & f.$2 = g2;
  assume
A5: for r1 st r1>0 & [.x0, x0+r1.] c= dom f ex g st g in [.x0, x0+r1.] &
  f.g = g2;
A6: for n ex g be Real st P[n,g]
  proof
    let n;
    x0 + 0 <= x0 +r by A3,XREAL_1:9;
    then
A7: x0 in [.x0, x0 + r.] by XXREAL_1:1;
A8: 0 <= n by NAT_1:2;
    then 0 + 1 <= n + 1 by XREAL_1:9;
    then r/(n+1) <= r/1 by A3,XREAL_1:120;
    then
A9: x0 + r/(n+1) <= x0 + r by XREAL_1:9;
    x0 + 0 =x0;
    then x0 <= x0 + r/(n+1) by A3,A8,XREAL_1:9;
    then x0 + r/(n+1) in {g1:x0 <= g1 & g1 <= x0+r} by A9;
    then x0 +r/(n+1) in [.x0,x0+r.] by RCOMP_1:def 1;
    then [.x0,x0+r/(n+1).] c= [.x0,x0+r.] by A7,XXREAL_2:def 12;
    then
A10: [.x0,x0+r/(n+1).] c= dom f by A4,XBOOLE_1:1;
    then consider g such that
A11: g in [.x0,x0+r/(n+1).] & f.g = g2 by A3,A5,A8,XREAL_1:141;
    take g;
    thus thesis by A10,A11;
  end;
  consider a such that
A12: for n holds P[n,a.n] from FUNCT_2:sch 3(A6);
A13: rng a c= right_open_halfline(x0) /\ dom f
  proof
    let x;
    assume x in rng a;
    then consider n such that
A14: x = a.n by FUNCT_2:190;
    a.n in [.x0, x0 + r/(n+1).] by A12;
    then a.n in{g1: x0<=g1 & g1<=x0+ r/(n+1)} by RCOMP_1:def 1;
    then
A15: ex g1 st g1=a.n & x0<=g1 & g1<=x0+ r/(n+1);
    x0 <> a.n by A2,A12;
    then x0 < a.n by A15,XXREAL_0:1;
    then a.n in {g1:x0 <g1};
    then
A16: a.n in right_open_halfline(x0)by XXREAL_1:230;
    a.n in dom f by A12;
    hence thesis by A14,A16,XBOOLE_0:def 4;
  end;
A17: right_open_halfline(x0) /\ dom f c= dom f by XBOOLE_1:17;
A18: for n holds (f/*a).n = g2
  proof
    let n;
    thus g2 =f.(a.n) by A12
      .=(f/*a).n by A13,A17,FUNCT_2:185,XBOOLE_1:1;
  end;
  now
    let n be Nat;
    n in NAT by ORDINAL1:def 13;
    then (f/*a).n=g2 by A18;
    hence (f/*a).n=(f/*a).(n+1) by A18;
  end;
  then
A19: lim (f/*a) = (f/*a).0 by SEQ_4:41,VALUED_0:25
    .= g2 by A18;
  deffunc F(Element of NAT) = x0+r/($1+1);
  reconsider b = NAT --> x0 as Real_Sequence;
A20: lim b = b.0 by SEQ_4:41
    .=x0 by FUNCOP_1:13;
  consider d such that
A21: for n holds d.n = F(n) from SEQ_1:sch 1;
A22: now
    let n;
    a.n in [.x0, x0 + r/(n+1).] by A12;
    then a.n in{g1: x0<=g1 & g1<=x0+ r/(n+1)} by RCOMP_1:def 1;
    then ex g1 st g1=a.n & x0<=g1 & g1<=x0+ r/(n+1);
    hence b.n <=a.n & a.n <= d.n by A21,FUNCOP_1:13;
  end;
  d is convergent & lim d = x0 by A21,FCONT_3:14;
  then a is convergent & lim a = x0 by A20,A22,SEQ_2:33,34;
  hence contradiction by A1,A2,A13,A19,Def2;
end;

definition
  let x0,f;
  assume
A1: f is_left_differentiable_in x0;
  func Ldiff (f,x0) -> Real means
  : Def5:
  for h,c st rng c = {x0} & rng (h + c
  ) c= dom f & (for n holds h.n <0) holds it =lim (h"(#)(f/*(h+c) - f/*c));
  existence
  proof
A2: {x0} c= dom f
    proof
      let x;
      assume x in {x0};
      then
A3:   x = x0 by TARSKI:def 1;
      consider r such that
A4:   r > 0 and
A5:   [.x0 - r,x0.] c= dom f by A1,Def4;
      x0 - r <= x0 by A4,XREAL_1:46;
      then x0 in [.x0 - r,x0.] by XXREAL_1:1;
      hence thesis by A3,A5;
    end;
A6: for h,c st rng c = {x0} & rng (h + c) c= dom f & (for n holds h.n <0)
    holds h"(#)(f/*(h+c) - f/*c) is convergent by A1,Def4;
    ex r st r > 0 & [.x0 - r, x0.] c= dom f by A1,Def4;
    then consider h1,c1 such that
A7: rng c1 = {x0} and
A8: rng (h1 + c1) c= dom f & for n holds h1.n < 0 by Th1;
    take lim (h1"(#)(f/*(h1+c1) - f/*c1));
    let h,c such that
A9: rng c = {x0} and
A10: rng (h + c) c= dom f & for n holds h.n <0;
    c1 = c by A7,A9,FDIFF_2:5;
    hence thesis by A7,A8,A2,A10,A6,Th3;
  end;
  uniqueness
  proof
    ex r st r > 0 & [.x0 - r, x0.] c= dom f by A1,Def4;
    then consider h,c such that
A11: rng c = {x0} & rng (h + c) c= dom f & for n holds h.n < 0 by
    Th1;
    let g1,g2 such that
A12: for h,c st rng c = {x0} & rng (h + c) c= dom f & (for n holds h.n
    <0) holds g1=lim (h"(#)(f/*(h+c) - f/*c)) and
A13: for h,c st rng c = {x0} & rng (h + c) c= dom f & (for n holds h.n
    < 0) holds g2=lim (h"(#)(f/*(h+c) - f/*c));
    g1 = lim (h"(#)(f/*(h+c) - f/*c)) by A12,A11;
    hence thesis by A13,A11;
  end;
end;

definition
  let x0,f;
  assume
A1: f is_right_differentiable_in x0;
  func Rdiff(f,x0) -> Real means
  :Def6:
  for h,c st rng c = {x0} & rng (h+c) c=
  dom f & (for n holds h.n > 0) holds it=lim (h"(#)(f/*(h+c) - f/*c));
  existence
  proof
A2: {x0} c= dom f
    proof
      let x;
      assume x in {x0};
      then
A3:   x = x0 by TARSKI:def 1;
      consider r such that
A4:   r>0 and
A5:   [.x0,x0+r.] c= dom f by A1,Def3;
      x0 + 0 <= x0 + r by A4,XREAL_1:9;
      then x0 in [.x0,x0+r.] by XXREAL_1:1;
      hence thesis by A3,A5;
    end;
A6: for h,c st rng c = {x0} & rng (h+c) c= dom f & (for n holds h.n>0)
    holds h"(#)(f/*(h+c) - f/*c) is convergent by A1,Def3;
    ex r st r>0 & [.x0,x0+r.] c=dom f by A1,Def3;
    then consider h,c such that
A7: rng c = {x0} and
A8: rng (h+c) c= dom f & for n holds h.n>0 by Th2;
    take lim(h"(#)(f/*(h+c) - f/*c));
    let h1,c1 such that
A9: rng c1 ={x0} and
A10: rng(h1 + c1) c=dom f & for n holds h1.n>0;
    c1 = c by A7,A9,FDIFF_2:5;
    hence thesis by A7,A8,A10,A6,A2,Th4;
  end;
  uniqueness
  proof
    ex r st r>0 & [.x0,x0 + r.] c=dom f by A1,Def3;
    then consider h,c such that
A11: rng c ={x0} & rng (h+c) c= dom f & for n holds h.n >0 by Th2;
    let g1,g2 such that
A12: for h,c st rng c ={x0} & rng (h+c) c= dom f & (for n holds h.n >
    0) holds g1=lim (h"(#)(f/*(h+c) - f/*c)) and
A13: for h,c st rng c ={x0} & rng (h+c) c= dom f & (for n holds h.n >
    0) holds g2=lim (h"(#)(f/*(h+c) - f/*c));
    g1 = lim (h"(#)(f/*(h+c) - f/*c)) by A12,A11;
    hence thesis by A13,A11;
  end;
end;

theorem Th9:
  f is_left_differentiable_in x0 & Ldiff(f,x0) = g iff (ex r st 0 <
  r & [.x0 -r,x0.] c= dom f) & for h,c st rng c = {x0} & rng (h+c) c= dom f & (
for n holds h.n < 0) holds h"(#)(f/*(h+c) - f/*c) is convergent & lim(h"(#)(f/*
  (h+c) - f/*c)) = g
proof
  thus f is_left_differentiable_in x0 & Ldiff(f,x0) = g implies (ex r st 0 < r
& [.x0 -r,x0.] c= dom f) & for h,c st rng c = {x0} & rng (h+c) c= dom f & (for
n holds h.n < 0) holds h"(#)(f/*(h+c) - f/*c) is convergent & lim(h"(#)(f/*(h+c
  ) - f/*c)) = g by Def4,Def5;
  thus (ex r st 0 <r & [.x0 -r,x0.] c= dom f) & (for h,c st rng c = {x0} & rng
  (h+c) c= dom f & (for n holds h.n < 0) holds h"(#)(f/*(h+c) - f/*c) is
  convergent & lim(h"(#)(f/*(h+c) - f/*c)) = g) implies f
  is_left_differentiable_in x0 & Ldiff(f,x0) = g
  proof
    assume that
A1: ex r st 0 < r & [.x0 -r,x0.] c= dom f and
A2: for h,c st rng c = {x0} & rng (h+c) c= dom f & (for n holds h.n <
0) holds h"(#)(f/*(h+c) - f/*c) is convergent & lim(h"(#) (f/*(h+c) - f/*c)) =
    g;
    for h,c holds rng c = {x0} & rng (h+c) c= dom f & (for n holds h.n < 0
    ) implies h"(#)(f/*(h+c) - f/*c) is convergent by A2;
    hence
A3: f is_left_differentiable_in x0 by A1,Def4;
    for h,c holds rng c = {x0} & rng (h+c) c= dom f & (for n holds h.n < 0
    ) implies lim(h"(#)(f/*(h+c) - f/*c)) = g by A2;
    hence thesis by A3,Def5;
  end;
end;

theorem
  f1 is_left_differentiable_in x0 & f2 is_left_differentiable_in x0
implies f1 + f2 is_left_differentiable_in x0 & Ldiff(f1+f2,x0) = Ldiff(f1,x0) +
  Ldiff(f2,x0)
proof
  assume that
A1: f1 is_left_differentiable_in x0 and
A2: f2 is_left_differentiable_in x0;
  consider r2 such that
A3: 0 < r2 and
A4: [.x0 -r2,x0.] c= dom f2 by A2,Def4;
  consider r1 such that
A5: 0 < r1 and
A6: [.x0 -r1,x0.] c= dom f1 by A1,Def4;
  set r = min(r1,r2);
A7: 0 < r by A5,A3,XXREAL_0:15;
  then
A8: x0 - r <= x0 by XREAL_1:45;
  r <= r2 by XXREAL_0:17;
  then
A9: x0 - r2 <= x0 - r by XREAL_1:15;
  then x0 - r2 <= x0 by A8,XXREAL_0:2;
  then
A10: x0 in [.x0 - r2,x0.] by XXREAL_1:1;
  x0 - r in { g: x0 - r2 <= g & g <= x0 } by A8,A9;
  then x0 - r in [.x0 - r2,x0.] by RCOMP_1:def 1;
  then [.x0 - r,x0.] c= [.x0 - r2,x0.] by A10,XXREAL_2:def 12;
  then
A11: [.x0 - r,x0.] c= dom f2 by A4,XBOOLE_1:1;
  r <= r1 by XXREAL_0:17;
  then
A12: x0 - r1 <= x0 - r by XREAL_1:15;
  then x0 - r1 <= x0 by A8,XXREAL_0:2;
  then
A13: x0 in [.x0 - r1,x0.] by XXREAL_1:1;
  x0 - r in { g: x0 - r1 <= g & g <= x0 } by A8,A12;
  then x0 - r in [.x0 - r1,x0.] by RCOMP_1:def 1;
  then [.x0 - r,x0.] c= [.x0 - r1,x0.] by A13,XXREAL_2:def 12;
  then [.x0 - r,x0.] c= dom f1 by A6,XBOOLE_1:1;
  then
A14: [.x0 - r,x0.] c= dom f1 /\ dom f2 by A11,XBOOLE_1:19;
A15: for h,c st rng c = {x0} & rng (h+c) c= dom (f1 + f2) & (for n holds h.n
< 0) holds h"(#)((f1+f2)/*(h+c) - (f1+f2)/*c) is convergent & lim(h"(#)((f1+f2)
  /*(h+c) - (f1+f2)/*c)) = Ldiff(f1,x0) + Ldiff(f2,x0)
  proof
    let h,c;
    assume that
A16: rng c = {x0} and
A17: rng (h+c) c= dom (f1 + f2) and
A18: for n holds h.n < 0;
A19: rng (h + c) c= dom f1 /\ dom f2 by A17,VALUED_1:def 1;
A20: now
      let n;
A21:  rng c c= dom f1 /\ dom f2
      proof
        let x;
        assume x in rng c;
        then
A22:    x = x0 by A16,TARSKI:def 1;
        x0 in [.x0 - r,x0.] by A8,XXREAL_1:1;
        hence thesis by A14,A22;
      end;
      thus (f1/*(h+c) - f1/*c + (f2/*(h+c) - f2/*c)).n =(f1/*(h+c) + (-(f1/*c)
      )).n + (f2/*(h+c) - f2/*c).n by SEQ_1:11
        .=(f1/*(h+c)).n + (-(f1/*c)).n + (f2/*(h+c) + (-(f2/*c))).n by SEQ_1
      :11
        .=(f1/*(h+c)).n + (-(f1/*c)).n + ((f2/*(h+c)).n + (-(f2/*c)).n) by
      SEQ_1:11
        .=(f1/*(h+c)).n + (f2/*(h+c)).n+ ((-(f1/*c)).n +(-(f2/*c)).n)
        .=(f1/*(h+c)).n + (f2/*(h+c)).n + (-(f1/*c).n + (-(f2/*c)).n) by
      SEQ_1:14
        .=(f1/*(h+c)).n + (f2/*(h+c)).n + (-(f1/*c).n + -(f2/*c).n) by SEQ_1
      :14
        .=(f1/*(h+c)).n + (f2/*(h+c)).n - ((f1/*c).n + (f2/*c).n)
        .=(f1/*(h+c) + f2/*(h+c)).n - ((f1/*c).n + (f2/*c).n) by SEQ_1:11
        .=(f1/*(h+c) + f2/*(h+c)).n - ((f1/*c + f2/*c).n) by SEQ_1:11
        .=(f1/*(h+c) + f2/*(h+c) - (f1/*c + f2/*c)).n by RFUNCT_2:6
        .=((f1+f2)/*(h+c) - (f1/*c + f2/*c)).n by A19,RFUNCT_2:23
        .=((f1+f2)/*(h+c) - (f1+f2)/*c).n by A21,RFUNCT_2:23;
    end;
    then
A23: f1/*(h+c) - f1/*c + (f2/*(h+c) - f2/*c) = (f1+f2)/*(h+c) - (f1+f2)/*c
    by FUNCT_2:113;
    dom f1 /\ dom f2 c=dom f2 by XBOOLE_1:17;
    then
A24: rng (h + c) c= dom f2 by A19,XBOOLE_1:1;
    then
A25: lim(h"(#)(f2/*(h+c) - f2/*c)) = Ldiff(f2,x0) by A2,A16,A18,Th9;
    Ldiff(f2,x0) = Ldiff(f2,x0);
    then
A26: h"(#)(f2/*(h+c) - f2/*c) is convergent by A2,A16,A18,A24,Th9;
    dom f1 /\ dom f2 c= dom f1 by XBOOLE_1:17;
    then
A27: rng (h + c) c= dom f1 by A19,XBOOLE_1:1;
A28: (h"(#)(f1/*(h+c) - f1/*c) + h"(#)(f2/*(h+c) - f2/*c)) = h"(#)(f1/*(h+
    c) - f1/*c + (f2/*(h+c) - f2/*c)) by SEQ_1:24;
    Ldiff(f1,x0) = Ldiff(f1,x0);
    then
A29: h"(#)(f1/*(h+c) - f1/*c) is convergent by A1,A16,A18,A27,Th9;
    then (h"(#)(f1/*(h+c) - f1/*c) + h"(#) (f2/*(h+c) - f2/*c)) is convergent
    by A26,SEQ_2:19;
    hence h"(#)((f1+f2)/*(h+c) - (f1+f2)/*c) is convergent by A28,A20,
    FUNCT_2:113;
    lim(h"(#)(f1/*(h+c) - f1/*c)) = Ldiff(f1,x0) by A1,A16,A18,A27,Th9;
    hence thesis by A29,A26,A25,A28,A23,SEQ_2:20;
  end;
  [.x0 - r,x0.] c= dom (f1 + f2) by A14,VALUED_1:def 1;
  hence thesis by A7,A15,Th9;
end;

theorem
  f1 is_left_differentiable_in x0 & f2 is_left_differentiable_in x0
implies f1 - f2 is_left_differentiable_in x0 & Ldiff(f1-f2,x0) = Ldiff(f1,x0) -
  Ldiff(f2,x0)
proof
  assume that
A1: f1 is_left_differentiable_in x0 and
A2: f2 is_left_differentiable_in x0;
  consider r2 such that
A3: 0 < r2 and
A4: [.x0 -r2,x0.] c= dom f2 by A2,Def4;
  consider r1 such that
A5: 0 < r1 and
A6: [.x0 -r1,x0.] c= dom f1 by A1,Def4;
  set r = min(r1,r2);
A7: 0 < r by A5,A3,XXREAL_0:15;
  then
A8: x0 - r <= x0 by XREAL_1:45;
  r <= r2 by XXREAL_0:17;
  then
A9: x0 - r2 <= x0 - r by XREAL_1:15;
  then x0 - r2 <= x0 by A8,XXREAL_0:2;
  then
A10: x0 in [.x0 - r2,x0.] by XXREAL_1:1;
  x0 - r in { g: x0 - r2 <= g & g <= x0 } by A8,A9;
  then x0 - r in [.x0 - r2,x0.] by RCOMP_1:def 1;
  then [.x0 - r,x0.] c= [.x0 - r2,x0.] by A10,XXREAL_2:def 12;
  then
A11: [.x0 - r,x0.] c= dom f2 by A4,XBOOLE_1:1;
  r <= r1 by XXREAL_0:17;
  then
A12: x0 - r1 <= x0 - r by XREAL_1:15;
  then x0 - r1 <= x0 by A8,XXREAL_0:2;
  then
A13: x0 in [.x0 - r1,x0.] by XXREAL_1:1;
  x0 - r in { g: x0 - r1 <= g & g <= x0 } by A8,A12;
  then x0 - r in [.x0 - r1,x0.] by RCOMP_1:def 1;
  then [.x0 - r,x0.] c= [.x0 - r1,x0.] by A13,XXREAL_2:def 12;
  then [.x0 - r,x0.] c= dom f1 by A6,XBOOLE_1:1;
  then
A14: [.x0 - r,x0.] c= dom f1 /\ dom f2 by A11,XBOOLE_1:19;
A15: for h,c st rng c = {x0} & rng (h+c) c= dom (f1 - f2) & (for n holds h.n
< 0) holds h"(#)((f1-f2)/*(h+c) - (f1-f2)/*c) is convergent & lim(h"(#)((f1-f2)
  /*(h+c) - (f1-f2)/*c)) = Ldiff(f1,x0) - Ldiff(f2,x0)
  proof
    let h,c;
    assume that
A16: rng c = {x0} and
A17: rng (h+c) c= dom (f1 - f2) and
A18: for n holds h.n < 0;
A19: rng (h + c) c= dom f1 /\ dom f2 by A17,VALUED_1:12;
A20: now
      let n;
A21:  rng c c= dom f1 /\ dom f2
      proof
        let x;
        assume x in rng c;
        then
A22:    x = x0 by A16,TARSKI:def 1;
        x0 in [.x0 - r,x0.] by A8,XXREAL_1:1;
        hence thesis by A14,A22;
      end;
      thus (f1/*(h+c) - f1/*c - (f2/*(h+c) - f2/*c)).n = (f1/*(h+c) - f1/*c).n
      - (f2/*(h+c) - f2/*c).n by RFUNCT_2:6
        .=(f1/*(h+c)).n - (f1/*c).n - (f2/*(h+c) - (f2/*c)).n by RFUNCT_2:6
        .=(f1/*(h+c)).n - (f1/*c).n - ((f2/*(h+c)).n - (f2/*c).n) by
      RFUNCT_2:6
        .=(f1/*(h+c)).n - (f2/*(h+c)).n - ((f1/*c).n - (f2/*c).n)
        .=(f1/*(h+c) - f2/*(h+c)).n - ((f1/*c).n - (f2/*c).n) by RFUNCT_2:6
        .=(f1/*(h+c) - f2/*(h+c)).n - ((f1/*c - f2/*c).n) by RFUNCT_2:6
        .=(f1/*(h+c) - f2/*(h+c) - (f1/*c - f2/*c)).n by RFUNCT_2:6
        .=((f1-f2)/*(h+c) - (f1/*c - f2/*c)).n by A19,RFUNCT_2:23
        .=((f1-f2)/*(h+c) - (f1-f2)/*c).n by A21,RFUNCT_2:23;
    end;
    then
A23: f1/*(h+c) - f1/*c - (f2/*(h+c) - f2/*c) = (f1-f2)/*(h+c) - (f1-f2)/*c
    by FUNCT_2:113;
    dom f1 /\ dom f2 c=dom f2 by XBOOLE_1:17;
    then
A24: rng (h + c) c= dom f2 by A19,XBOOLE_1:1;
    then
A25: lim(h"(#)(f2/*(h+c) - f2/*c)) = Ldiff(f2,x0) by A2,A16,A18,Th9;
    Ldiff(f2,x0) = Ldiff(f2,x0);
    then
A26: h"(#)(f2/*(h+c) - f2/*c) is convergent by A2,A16,A18,A24,Th9;
    dom f1 /\ dom f2 c= dom f1 by XBOOLE_1:17;
    then
A27: rng (h + c) c= dom f1 by A19,XBOOLE_1:1;
A28: (h"(#)(f1/*(h+c) - f1/*c) - h"(#)(f2/*(h+c) - f2/*c)) = h"(#)(f1/*(h+
    c) - f1/*c - (f2/*(h+c) - f2/*c)) by SEQ_1:29;
    Ldiff(f1,x0) = Ldiff(f1,x0);
    then
A29: h"(#)(f1/*(h+c) - f1/*c) is convergent by A1,A16,A18,A27,Th9;
    then (h"(#)(f1/*(h+c) - f1/*c) - h"(#) (f2/*(h+c) - f2/*c)) is convergent
    by A26,SEQ_2:25;
    hence h"(#)((f1-f2)/*(h+c) - (f1-f2)/*c) is convergent by A28,A20,
    FUNCT_2:113;
    lim(h"(#)(f1/*(h+c) - f1/*c)) = Ldiff(f1,x0) by A1,A16,A18,A27,Th9;
    hence thesis by A29,A26,A25,A28,A23,SEQ_2:26;
  end;
  [.x0 - r,x0.] c= dom (f1 - f2) by A14,VALUED_1:12;
  hence thesis by A7,A15,Th9;
end;

theorem
  f1 is_left_differentiable_in x0 & f2 is_left_differentiable_in x0
implies f1(#)f2 is_left_differentiable_in x0 & Ldiff(f1(#)f2,x0) = Ldiff(f1,x0)
  *f2.x0 + Ldiff(f2,x0)*f1.x0
proof
  assume that
A1: f1 is_left_differentiable_in x0 and
A2: f2 is_left_differentiable_in x0;
  consider r2 such that
A3: 0 < r2 and
A4: [.x0 -r2,x0.] c= dom f2 by A2,Def4;
  consider r1 such that
A5: 0 < r1 and
A6: [.x0 -r1,x0.] c= dom f1 by A1,Def4;
  set r = min(r1,r2);
A7: 0 < r by A5,A3,XXREAL_0:15;
  then
A8: x0 - r <= x0 by XREAL_1:45;
  r <= r2 by XXREAL_0:17;
  then
A9: x0 - r2 <= x0 - r by XREAL_1:15;
  then x0 - r2 <= x0 by A8,XXREAL_0:2;
  then
A10: x0 in [.x0 - r2,x0.] by XXREAL_1:1;
  x0 - r in { g: x0 - r2 <= g & g <= x0 } by A8,A9;
  then x0 - r in [.x0 - r2,x0.] by RCOMP_1:def 1;
  then [.x0 - r,x0.] c= [.x0 - r2,x0.] by A10,XXREAL_2:def 12;
  then
A11: [.x0 - r,x0.] c= dom f2 by A4,XBOOLE_1:1;
  r <= r1 by XXREAL_0:17;
  then
A12: x0 - r1 <= x0 - r by XREAL_1:15;
  then x0 - r1 <= x0 by A8,XXREAL_0:2;
  then
A13: x0 in [.x0 - r1,x0.] by XXREAL_1:1;
  x0 - r in { g: x0 - r1 <= g & g <= x0 } by A8,A12;
  then x0 - r in [.x0 - r1,x0.] by RCOMP_1:def 1;
  then [.x0 - r,x0.] c= [.x0 - r1,x0.] by A13,XXREAL_2:def 12;
  then
A14: [.x0 - r,x0.] c= dom f1 by A6,XBOOLE_1:1;
A15: for h,c st rng c = {x0} & rng (h+c) c= dom (f1(#)f2) & (for n holds h.n
< 0) holds h"(#)((f1(#)f2)/*(h+c) - (f1(#)f2)/*c) is convergent & lim(h"(#)((f1
  (#)f2)/*(h+c) - (f1(#)f2)/*c)) = Ldiff(f1,x0)*f2.x0 + Ldiff(f2,x0)*f1.x0
  proof
    let h,c;
    assume that
A16: rng c = {x0} and
A17: rng (h+c) c= dom (f1(#)f2) and
A18: for n holds h.n < 0;
A19: rng (h + c) c= dom f1 /\ dom f2 by A17,VALUED_1:def 4;
    now
      let n;
      thus (f1/*c + (f1/*(h+c) - f1/*c)).n = (f1/*c).n + (f1/*(h+c) - f1/*c).
      n by SEQ_1:11
        .= (f1/*c).n + ((f1/*(h+c)).n - (f1/*c).n) by RFUNCT_2:6
        .= (f1/*(h+c)).n;
    end;
    then
A20: f1/*c + (f1/*(h+c) - f1/*c) = f1/*(h+c) by FUNCT_2:113;
A21: for m holds c.m = x0
    proof
      let m;
      c.m in rng c by VALUED_0:28;
      hence thesis by A16,TARSKI:def 1;
    end;
    0 <= r by A5,A3,XXREAL_0:15;
    then x0 - r <= x0 by XREAL_1:45;
    then
A22: x0 in [.x0 -r,x0.] by XXREAL_1:1;
    then
A23: x0 in dom f1 by A14;
A24: rng c c= dom f1
    proof
      let x;
      assume x in rng c;
      hence thesis by A16,A23,TARSKI:def 1;
    end;
A25: for g be real number st 0 < g ex n st for m st n <=m holds abs((f1/*c
    ).m - f1.x0) < g
    proof
      let g be real number such that
A26:  0 < g;
      take n = 0;
      let m such that
      n <= m;
      abs((f1/*c).m - f1.x0) = abs(f1.(c.m) - f1.x0) by A24,FUNCT_2:185
        .= abs(f1.x0 - f1.x0) by A21
        .= 0 by ABSVALUE:def 1;
      hence thesis by A26;
    end;
    then
A27: f1/*c is convergent by SEQ_2:def 6;
A28: x0 in dom f2 by A11,A22;
A29: rng c c= dom f2
    proof
      let x;
      assume x in rng c;
      hence thesis by A16,A28,TARSKI:def 1;
    end;
A30: for g be real number st 0 < g ex n st for m st n <=m holds abs((f2/*c
    ).m - f2.x0) < g
    proof
      let g be real number such that
A31:  0 < g;
      take n = 0;
      let m such that
      n <= m;
      abs((f2/*c).m - f2.x0) = abs(f2.(c.m) - f2.x0) by A29,FUNCT_2:185
        .= abs(f2.x0 - f2.x0) by A21
        .= 0 by ABSVALUE:def 1;
      hence thesis by A31;
    end;
    then
A32: f2/*c is convergent by SEQ_2:def 6;
    dom f1 /\ dom f2 c= dom f1 by XBOOLE_1:17;
    then
A33: rng (h + c) c= dom f1 by A19,XBOOLE_1:1;
    then
A34: lim(h"(#)(f1/*(h+c) - f1/*c)) = Ldiff(f1,x0) by A1,A16,A18,Th9;
A35: lim h = 0 by FDIFF_1:def 1;
A36: h is convergent & h"(#)(f1/*(h+c) - f1/*c) is convergent by A1,A16,
    A18,A33, Def4,FDIFF_1:def 1;
    then
A37: lim (h(#)(h"(#)(f1/*(h+c) - f1/*c))) = lim h *lim (h"(#) (f1/*(h+c) -
    f1/*c)) by SEQ_2:29
      .= 0 by A35;
    dom f1 /\ dom f2 c=dom f2 by XBOOLE_1:17;
    then
A38: rng (h + c) c= dom f2 by A19,XBOOLE_1:1;
    then
A39: lim(h"(#)(f2/*(h+c) - f2/*c)) = Ldiff(f2,x0) by A2,A16,A18,Th9;
    Ldiff(f1,x0) = Ldiff(f1,x0);
    then
A40: h"(#)(f1/*(h+c) - f1/*c) is convergent by A1,A16,A18,A33,Th9;
    then
A41: h"(#) (f1/*(h+c) - f1/*c)(#)(f2/*c) is convergent by
    A32,SEQ_2:28;
A42: rng c c= dom f1 /\ dom f2 by A24,A29,XBOOLE_1:19;
A43: now
      let n;
      thus (h"(#)((f1(#)f2)/*(h+c) - (f1(#)f2)/*c)).n = ((h").n) * ((f1(#)f2)
      /*(h+c) - (f1(#)f2)/*c).n by SEQ_1:12
        .= ((h").n) * (((f1(#)f2)/*(h+c)).n - ((f1(#)f2)/*c).n) by RFUNCT_2:
      6
        .= h".n * (((f1/*(h+c))(#)(f2/*(h+c))).n - ((f1(#)f2)/*c).n) by A19,
      RFUNCT_2:23
        .= h".n * (((f1/*(h+c))(#)(f2/*(h+c))).n - ((f1/*c)(#)(f2/*c)).n) by
      A42,RFUNCT_2:23
        .= h".n * ((f1/*(h+c)).n * (f2/*(h+c)).n - ((f1/*c)(#)(f2/*c)).n) by
      SEQ_1:12
        .= h".n * ((f1/*(h+c)).n * (f2/*(h+c)).n - ((f1/*c).n * (f2/*c).n))
      by SEQ_1:12
        .= h".n * ((f2/*(h+c)).n - (f2/*c).n)*(f1/*(h+c)).n + (h".n * ((f1/*
      (h+c)).n - (f1/*c).n))*(f2/*c).n
        .= h".n * (f2/*(h+c) - f2/*c).n * (f1/*(h+c)).n + (h".n * ((f1/*(h+c
      )).n - (f1/*c).n))*(f2/*c).n by RFUNCT_2:6
        .= (h"(#) (f2/*(h+c) - f2/*c)).n * (f1/*(h+c)).n + (h".n * ((f1/*(h+
      c)).n - (f1/*c).n))*(f2/*c).n by SEQ_1:12
        .= (h"(#) (f2/*(h+c) - f2/*c)).n * (f1/*(h+c)).n + (h".n * (f1/*(h+c
      ) - (f1/*c)).n)*(f2/*c).n by RFUNCT_2:6
        .= (h"(#) (f2/*(h+c) - f2/*c)).n * (f1/*(h+c)).n + (h"(#) (f1/*(h+c)
      - f1/*c)).n*(f2/*c).n by SEQ_1:12
        .= (h"(#) (f2/*(h+c) - f2/*c) (#) (f1/*(h+c))).n + (h"(#) (f1/*(h+c)
      - f1/*c)).n*(f2/*c).n by SEQ_1:12
        .= (h"(#) (f2/*(h+c) - f2/*c)(#)(f1/*(h+c))).n + (h"(#) (f1/*(h+c) -
      f1/*c)(#)(f2/*c)).n by SEQ_1:12
        .= (h"(#) (f2/*(h+c) - f2/*c)(#)(f1/*(h+c)) + h"(#) (f1/*(h+c) - f1
      /*c)(#)(f2/*c)).n by SEQ_1:11;
    end;
    then
A44: h"(#)((f1(#)f2)/*(h+c) - (f1(#)f2)/*c) = h"(#) (f2/*(h+c) - f2/*c )
    (#) (f1/*(h+c)) + h"(#) (f1/*(h+c) - f1/*c)(#)(f2/*c) by FUNCT_2:113;
    now
      let n;
A45:  h.n <> 0 by A18;
      thus (h(#)(h"(#)(f1/*(h+c) - f1/*c))).n = ((h(#)h")(#) (f1/*(h+c) - f1/*
      c)).n by SEQ_1:22
        .= (h(#)h").n *(f1/*(h+c) - f1/*c).n by SEQ_1:12
        .= h.n * (h").n *(f1/*(h+c) - f1/*c).n by SEQ_1:12
        .= h.n * (h.n)" *(f1/*(h+c) - f1/*c).n by VALUED_1:10
        .= 1*(f1/*(h+c) - f1/*c).n by A45,XCMPLX_0:def 7
        .= (f1/*(h+c) - f1/*c).n;
    end;
    then
A46: h(#)(h"(#)(f1/*(h+c) - f1/*c)) = (f1/*(h+c) - f1/*c) by FUNCT_2:
    113;
    h(#)(h"(#)(f1/*(h+c) - f1/*c)) is convergent by A36,SEQ_2:28;
    then
A47: f1/*(h+c) is convergent by A27,A46,A20,SEQ_2:19;
    lim(f1/*c) = f1.x0 by A25,A27,SEQ_2:def 7;
    then
A48: 0 = lim (f1/*(h+c)) - f1.x0 by A27,A46,A47,A37,SEQ_2:26;
    Ldiff(f2,x0) = Ldiff(f2,x0);
    then
A49: h"(#)(f2/*(h+c) - f2/*c) is convergent by A2,A16,A18,A38,Th9;
    then
A50: h"(#) (f2/*(h+c) - f2/*c)(#) (f1/*(h+c)) is convergent by A47,
    SEQ_2:28;
    lim (h"(#)((f1(#)f2)/*(h+c) - (f1(#)f2)/*c)) = lim (h"(#) (f2/*(h+c)
- f2/*c)(#)(f1/*(h+c)) + h"(#) (f1/*(h+c) - f1/*c)(#) (f2/*c)) by A43,FUNCT_2:
    113
      .= lim (h"(#) (f2/*(h+c) - f2/*c)(#)(f1/*(h+c))) + lim (h"(#) (f1/*(h+
    c) - f1/*c)(#)(f2/*c)) by A50,A41,SEQ_2:20
      .= lim (h"(#) (f2/*(h+c) - f2/*c))*lim(f1/*(h+c)) + lim (h"(#) (f1/*(h
    +c) - f1/*c)(#)(f2/*c)) by A49,A47,SEQ_2:29
      .= lim (h"(#) (f2/*(h+c) - f2/*c))* f1.x0 + lim (h"(#) (f1/*(h+c) - f1
    /*c))*lim(f2/*c) by A40,A48,A32,SEQ_2:29
      .= Ldiff(f1,x0) * f2.x0 + Ldiff(f2,x0) * f1.x0 by A34,A39,A30,A32,
    SEQ_2:def 7;
    hence thesis by A50,A41,A44,SEQ_2:19;
  end;
  [.x0 - r,x0.] c= dom f1 /\ dom f2 by A14,A11,XBOOLE_1:19;
  then [.x0 - r,x0.] c= dom (f1(#)f2) by VALUED_1:def 4;
  hence thesis by A7,A15,Th9;
end;

Lm1: f1 is_left_differentiable_in x0 & f2 is_left_differentiable_in x0 & (ex
r0 st r0 > 0 & for g st g in dom f2 & g in [.x0 - r0,x0.] holds f2.g <> 0)
implies f1/f2 is_left_differentiable_in x0 & Ldiff(f1/f2,x0) = (Ldiff(f1,x0)*f2
.x0 - Ldiff(f2,x0)*f1.x0)/(f2.x0)^2
proof
  assume that
A1: f1 is_left_differentiable_in x0 and
A2: f2 is_left_differentiable_in x0;
  consider r1 such that
A3: 0 < r1 and
A4: [.x0 -r1,x0.] c= dom f1 by A1,Def4;
  given r0 such that
A5: r0 > 0 and
A6: for g st g in dom f2 & g in [.x0 - r0,x0.] holds f2.g <> 0;
  consider r2 such that
A7: 0 < r2 and
A8: [.x0 -r2,x0.] c= dom f2 by A2,Def4;
  set r3 = min(r0,r2);
A9: 0 < r3 by A5,A7,XXREAL_0:15;
  then
A10: x0 - r3 <= x0 by XREAL_1:45;
  r3 <= r2 by XXREAL_0:17;
  then
A11: x0 - r2 <= x0 - r3 by XREAL_1:15;
  then x0 - r2 <= x0 by A10,XXREAL_0:2;
  then
A12: x0 in [.x0 - r2,x0.] by XXREAL_1:1;
  x0 - r3 in { g: x0 - r2 <= g & g <= x0 } by A10,A11;
  then x0 - r3 in [.x0 - r2,x0.] by RCOMP_1:def 1;
  then [.x0 - r3,x0.] c= [.x0 - r2,x0.] by A12,XXREAL_2:def 12;
  then
A13: [.x0 - r3,x0.] c= dom f2 by A8,XBOOLE_1:1;
  r3 <= r0 by XXREAL_0:17;
  then
A14: x0 - r0 <= x0 - r3 by XREAL_1:15;
  then x0 - r0 <= x0 by A10,XXREAL_0:2;
  then
A15: x0 in [.x0 - r0,x0.] by XXREAL_1:1;
  x0 - r3 in { g: x0 - r0 <= g & g <= x0 } by A10,A14;
  then x0 - r3 in [.x0 - r0,x0.] by RCOMP_1:def 1;
  then
A16: [.x0 - r3,x0.] c= [.x0 - r0,x0.] by A15,XXREAL_2:def 12;
  set r = min(r1,r3);
A17: 0 < r by A3,A9,XXREAL_0:15;
  then
A18: x0 - r <= x0 by XREAL_1:45;
  r <= r3 by XXREAL_0:17;
  then
A19: x0 - r3 <= x0 - r by XREAL_1:15;
  then x0 - r3 <= x0 by A18,XXREAL_0:2;
  then
A20: x0 in [.x0 - r3,x0.] by XXREAL_1:1;
  x0 - r in { g: x0 - r3 <= g & g <= x0 } by A18,A19;
  then x0 - r in [.x0 - r3,x0.] by RCOMP_1:def 1;
  then
A21: [.x0 - r,x0.] c= [.x0 - r3,x0.] by A20,XXREAL_2:def 12;
  [.x0 - r,x0.] c= dom(f2^)
  proof
    assume not thesis;
    then consider x such that
A22: x in [.x0 - r,x0.] and
A23: not x in dom (f2^) by TARSKI:def 3;
    reconsider x as Real by A22;
A24: x in [.x0 - r3,x0.] by A21,A22;
A25: not x in dom f2 \ f2"{0} by A23,RFUNCT_1:def 8;
    now
      per cases by A25,XBOOLE_0:def 5;
      suppose
        not x in dom f2;
        hence contradiction by A13,A24;
      end;
      suppose
A26:    x in f2"{0};
        then f2.x in {0} by FUNCT_1:def 13;
        then
A27:    f2.x = 0 by TARSKI:def 1;
        x in dom f2 by A26,FUNCT_1:def 13;
        hence contradiction by A6,A16,A24,A27;
      end;
    end;
    hence contradiction;
  end;
  then
A28: [.x0 - r,x0.] c= dom f2\f2"{0} by RFUNCT_1:def 8;
  r <= r1 by XXREAL_0:17;
  then
A29: x0 - r1 <= x0 - r by XREAL_1:15;
  then x0 - r1 <= x0 by A18,XXREAL_0:2;
  then
A30: x0 in [.x0 - r1,x0.] by XXREAL_1:1;
  x0 - r in { g: x0 - r1 <= g & g <= x0 } by A18,A29;
  then x0 - r in [.x0 - r1,x0.] by RCOMP_1:def 1;
  then [.x0 - r,x0.] c= [.x0 - r1,x0.] by A30,XXREAL_2:def 12;
  then
A31: [.x0 - r,x0.] c= dom f1 by A4,XBOOLE_1:1;
A32: for h,c st rng c = {x0} & rng (h+c) c= dom (f1/f2) & (for n holds h.n <
0) holds h"(#)((f1/f2)/*(h+c) - (f1/f2)/*c) is convergent & lim(h"(#)((f1/f2)/*
  (h+c) - (f1/f2)/*c)) = (Ldiff(f1,x0)*f2.x0 - Ldiff(f2,x0)*f1.x0)/(f2.x0)^2
  proof
    let h,c;
    assume that
A33: rng c = {x0} and
A34: rng (h+c) c= dom (f1/f2) and
A35: for n holds h.n < 0;
A36: rng (h + c) c= dom f1 /\ (dom f2\f2"{0}) by A34,RFUNCT_1:def 4;
    0 <= r by A3,A9,XXREAL_0:15;
    then x0 - r <= x0 by XREAL_1:45;
    then
A37: x0 in [.x0 -r,x0.] by XXREAL_1:1;
    then
A38: x0 in dom f1 by A31;
A39: rng c c= dom f1
    proof
      let x be set;
      assume x in rng c;
      hence thesis by A33,A38,TARSKI:def 1;
    end;
    dom f1 /\ (dom f2\f2"{0}) c= dom f1 by XBOOLE_1:17;
    then
A40: rng (h + c) c= dom f1 by A36,XBOOLE_1:1;
    then
A41: lim(h"(#)(f1/*(h+c) - f1/*c)) = Ldiff(f1,x0) by A1,A33,A35,Th9;
A42: x0 in (dom f2\f2"{0}) by A28,A37;
    rng c c= dom f2\f2"{0}
    proof
      let x be set;
      assume x in rng c;
      hence thesis by A33,A42,TARSKI:def 1;
    end;
    then
A43: rng c c= dom (f2^) by RFUNCT_1:def 8;
    then
A44: rng c c= dom f1 /\ dom (f2^) by A39,XBOOLE_1:19;
A45: h"(#)(f1/*(h+c) - f1/*c) is convergent by A1,A33,A35,A40,Def4;
A46: f2/*c is non-zero by A43,RFUNCT_2:26;
A47: now
      let n;
      thus (h"(#)((f1/*(h+c))(#)(f2/*c) - (f1/*c)(#)(f2/*(h+c)))).n = (h").n *
      ((f1/*(h+c))(#)(f2/*c) - (f1/*c)(#)(f2/*(h+c))).n by SEQ_1:12
        .= (h").n * (((f1/*(h+c))(#)(f2/*c)).n - ((f1/*c)(#) (f2/*(h+c))).n)
      by RFUNCT_2:6
        .= (h").n * ((f1/*(h+c)).n * (f2/*c).n - ((f1/*c)(#)(f2/*(h+c))).n)
      by SEQ_1:12
        .= (h").n * (((f1/*(h+c)).n - (f1/*c).n) * (f2/*c).n + ((f1/*c).n*(
      f2/*c).n) - ((f1/*c).n * (f2/*(h+c)).n)) by SEQ_1:12
        .= (h").n * ((f1/*(h+c)).n - (f1/*c).n) * (f2/*c).n - (f1/*c).n * ((
      h").n * ((f2/*(h+c)).n - (f2/*c).n))
        .= (h").n * ((f1/*(h+c)) - (f1/*c)).n * (f2/*c).n - (f1/*c).n * ((h"
      ).n * ((f2/*(h+c)).n - (f2/*c).n)) by RFUNCT_2:6
        .= (h").n * (f1/*(h+c) - (f1/*c)).n * (f2/*c).n - (f1/*c).n * ((h").
      n * (f2/*(h+c) - (f2/*c)).n) by RFUNCT_2:6
        .= (h" (#) (f1/*(h+c) - (f1/*c))).n * (f2/*c).n - (f1/*c).n * ((h").
      n * (f2/*(h+c) - (f2/*c)).n) by SEQ_1:12
        .= (h"(#)(f1/*(h+c) - (f1/*c))).n * (f2/*c).n - (f1/*c).n * (h"(#)(
      f2/*(h+c) - (f2/*c))).n by SEQ_1:12
        .= (h"(#)(f1/*(h+c) - (f1/*c))(#)(f2/*c)).n - (f1/*c).n * (h"(#)(f2
      /*(h+c) - (f2/*c))).n by SEQ_1:12
        .= (h"(#)(f1/*(h+c) - (f1/*c))(#)(f2/*c)).n - ((f1/*c)(#)(h"(#)(f2/*
      (h+c)-(f2/*c)))).n by SEQ_1:12
        .= (h"(#)(f1/*(h+c) - (f1/*c))(#)(f2/*c) - (f1/*c)(#)(h"(#) (f2/*(h+
      c)-(f2/*c)))).n by RFUNCT_2:6;
    end;
    now
      let n;
      thus (f2/*c + (f2/*(h+c) - f2/*c)).n = (f2/*c).n + (f2/*(h+c) - f2/*c).n
      by SEQ_1:11
        .= (f2/*c).n + ((f2/*(h+c)).n - (f2/*c).n) by RFUNCT_2:6
        .= (f2/*(h+c)).n;
    end;
    then
A48: f2/*c + (f2/*(h+c) - f2/*c) = f2/*(h+c) by FUNCT_2:113;
A49: lim h = 0 by FDIFF_1:def 1;
    dom f1 /\ (dom f2\f2"{0}) c=(dom f2\f2"{0}) by XBOOLE_1:17;
    then
A50: rng (h + c) c= (dom f2\f2"{0}) by A36,XBOOLE_1:1;
    then
A51: rng (h + c) c= dom (f2^) by RFUNCT_1:def 8;
    then
A52: rng (h + c) c= dom f1 /\ dom (f2^) by A40,XBOOLE_1:19;
A53: f2/*(h+c) is non-zero by A51,RFUNCT_2:26;
    then
A54: (f2/*(h+c)) (#) (f2/*c) is non-zero by A46,SEQ_1:43;
    h"(#) ((f1/f2)/*(h+c) - (f1/f2)/*c)=h"(#)((f1(#) (f2^))/*(h+c) - (f1
    /f2)/*c) by RFUNCT_1:47
      .=h"(#)((f1(#)(f2^))/*(h+c) - (f1(#)(f2^))/*c) by RFUNCT_1:47
      .=h"(#)((f1/*(h+c))(#)((f2^)/*(h+c)) - (f1(#) (f2^))/*c) by A52,
    RFUNCT_2:23
      .=h"(#)((f1/*(h+c))/"(f2/*(h+c)) - (f1(#)(f2^))/*c) by A51,RFUNCT_2:27
      .=h"(#)((f1/*(h+c))/"(f2/*(h+c)) - (f1/*c)(#) ((f2^)/*c)) by A44,
    RFUNCT_2:23
      .=h"(#)((f1/*(h+c))/"(f2/*(h+c)) - (f1/*c)/"(f2/*c)) by A43,RFUNCT_2:
    27
      .=h"(#)(((f1/*(h+c))(#)(f2/*c) - (f1/*c)(#)(f2/*(h+c)))/"((f2/*(h+c))
    (#) (f2/*c))) by A53,A46,SEQ_1:58
      .=(h"(#)((f1/*(h+c))(#)(f2/*c) - (f1/*c)(#)(f2/*(h+c))))/"((f2/*(h+c))
    (#) (f2/*c)) by SEQ_1:49;
    then
A55: h"(#) ((f1/f2)/*(h+c) - (f1/f2)/*c) =((h"(#)(f1/*(h+c) - (f1/*c)) )
(#) (f2/*c) - (f1/*c)(#)(h"(#)(f2/*(h+c)-(f2/*c))))/"(f2/*(h+c)(#) (f2/*c)) by
    A47,FUNCT_2:113;
    dom f2 \ f2"{0} c= dom f2 by XBOOLE_1:36;
    then
A56: rng (h + c) c= dom f2 by A50,XBOOLE_1:1;
    then
A57: lim(h"(#)(f2/*(h+c) - f2/*c)) = Ldiff(f2,x0) by A2,A33,A35,Th9;
    Ldiff(f2,x0) = Ldiff(f2,x0);
    then
A58: h"(#)(f2/*(h+c) - f2/*c) is convergent by A2,A33,A35,A56,Th9;
    now
      let n;
A59:  h.n <> 0 by A35;
      thus (h(#)(h"(#)(f2/*(h+c) - f2/*c))).n = ((h(#)h")(#)(f2/*(h+c) - f2/*c
      )). n by SEQ_1:22
        .= (h(#)h").n *(f2/*(h+c) - f2/*c).n by SEQ_1:12
        .= h.n * (h").n *(f2/*(h+c) - f2/*c).n by SEQ_1:12
        .= h.n * (h.n)" *(f2/*(h+c) - f2/*c).n by VALUED_1:10
        .= 1*(f2/*(h+c) - f2/*c).n by A59,XCMPLX_0:def 7
        .= (f2/*(h+c) - f2/*c).n;
    end;
    then
A60: h(#)(h"(#)(f2/*(h+c) - f2/*c)) = (f2/*(h+c) - f2/*c) by FUNCT_2
    :113;
A61: for m holds c.m = x0
    proof
      let m;
      c.m in rng c by VALUED_0:28;
      hence thesis by A33,TARSKI:def 1;
    end;
A62: for g be real number st 0 < g ex n st for m st n <=m holds abs((f1/*c
    ).m - f1.x0) < g
    proof
      let g be real number such that
A63:  0 < g;
      take n = 0;
      let m such that
      n <= m;
      abs((f1/*c).m - f1.x0) = abs(f1.(c.m) - f1.x0) by A39,FUNCT_2:185
        .=abs(f1.x0 - f1.x0) by A61
        .= 0 by ABSVALUE:def 1;
      hence thesis by A63;
    end;
    then
A64: f1/*c is convergent by SEQ_2:def 6;
    then
A65: lim(f1/*c) = f1.x0 by A62,SEQ_2:def 7;
    dom (f2^) = dom f2 \ f2"{0} by RFUNCT_1:def 8;
    then
A66: dom (f2^) c= dom f2 by XBOOLE_1:36;
A67: for g be real number st 0 < g ex n st for m st n <=m holds abs((f2/*
    c).m - f2.x0) < g
    proof
      let g be real number such that
A68:  0 < g;
      take n = 0;
      let m such that
      n <= m;
      abs((f2/*c).m - f2.x0) = abs(f2.(c.m) - f2.x0) by A43,A66,
      FUNCT_2:185,XBOOLE_1:1
        .=abs(f2.x0 - f2.x0) by A61
        .= 0 by ABSVALUE:def 1;
      hence thesis by A68;
    end;
    then
A69: f2/*c is convergent by SEQ_2:def 6;
    then
A70: lim(f2/*c) = f2.x0 by A67,SEQ_2:def 7;
A71: h is convergent by FDIFF_1:def 1;
    then h(#)(h"(#)(f2/*(h+c) - f2/*c)) is convergent by A58,SEQ_2:28;
    then
A72: f2/*(h+c) is convergent by A69,A60,A48,SEQ_2:19;
    then
A73: f2/*(h+c) (#) (f2/*c) is convergent by A69,SEQ_2:28;
    h"(#)(f2/*(h+c) - f2/*c) is convergent by A2,A33,A35,A56,Def4;
    then lim (h(#)(h"(#)(f2/*(h+c) - f2/*c))) = lim h *lim (h"(#)(f2/*(h+c) -
    f2/* c ) ) by A71,SEQ_2:29
      .= 0 by A49;
    then 0 = lim (f2/*(h+c)) - f2.x0 by A69,A70,A60,A72,SEQ_2:26;
    then
A74: lim(f2/*(h+c) (#) (f2/*c)) = (f2.x0)^2 by A69,A70,A72,SEQ_2:
    29;
A75: lim(f2/*(h+c) (#) (f2/*c))<> 0
    proof
      assume not thesis;
      then f2.x0 = 0 by A74,XCMPLX_1:6;
      hence contradiction by A6,A8,A15,A12;
    end;
    Ldiff(f1,x0) = Ldiff(f1,x0);
    then h"(#)(f1/*(h+c) - f1/*c) is convergent by A1,A33,A35,A40,Th9;
    then
A76: h"(#) (f1/*(h+c) - f1/*c)(#)(f2/*c) is convergent by
    A69,SEQ_2:
    28;
A77: (f1/*c)(#)(h"(#) (f2/*(h+c) - f2/*c)) is convergent by A58,A64,SEQ_2
    :28;
    then
A78: ((h"(#)(f1/*(h+c) - (f1/*c)))(#)(f2/*c) - (f1/*c)(#)(h"(#) (f2/*(h+c
    )-(f2/*c)))) is convergent by A76,SEQ_2:25;
    then lim(h"(#) ((f1/f2)/*(h+c) - (f1/f2)/*c)) = lim((h"(#)(f1/*(h+c) - (
f1/*c)))(#)(f2/*c) - (f1/*c)(#)(h"(#) (f2/*(h+c)-(f2/*c))))/ lim(f2/*(h+c)(#)(
    f2/*c)) by A54,A73,A75,A55,SEQ_2:38
      .=(lim((h"(#)(f1/*(h+c)-(f1/*c)))(#)(f2/*c))-lim((f1/*c)(#)(h"(#)(f2/*
    (h+c)- (f2/*c)))))/lim(f2/*(h+c)(#)(f2/*c)) by A77,A76,SEQ_2:26
      .=( lim(h"(#)(f1/*(h+c)-(f1/*c)))*lim(f2/*c) - lim((f1/*c) (#) (h"(#)(
    f2/*(h+c)-(f2/*c)))) ) / lim(f2/*(h+c)(#)(f2/*c)) by A45,A69,SEQ_2:29
      .=(Ldiff(f1,x0) * f2.x0 - Ldiff(f2,x0) * f1.x0)/(f2.x0)^2 by A41,A58,
    A57,A64,A65,A70,A74,SEQ_2:29;
    hence thesis by A54,A73,A75,A78,A55,SEQ_2:37;
  end;
  [.x0 - r,x0.] c= dom f1 /\ (dom f2\f2"{0}) by A31,A28,XBOOLE_1:19;
  then [.x0 - r,x0.] c= dom (f1/f2) by RFUNCT_1:def 4;
  hence thesis by A17,A32,Th9;
end;

theorem
  f1 is_left_differentiable_in x0 & f2 is_left_differentiable_in x0 & f2
.x0 <> 0 implies f1/f2 is_left_differentiable_in x0 & Ldiff(f1/f2,x0) = (Ldiff(
  f1,x0)*f2.x0 - Ldiff(f2,x0)*f1.x0)/(f2.x0)^2
proof
  assume that
A1: f1 is_left_differentiable_in x0 and
A2: f2 is_left_differentiable_in x0 and
A3: f2.x0 <> 0;
  ex r st r > 0 &[.x0-r,x0.] c= dom f2 by A2,Def4;
  then consider r1 such that
A4: r1 > 0 and
  [.x0-r1,x0.] c= dom f2 and
A5: for g st g in [.x0-r1,x0.] holds f2.g <> 0 by A2,A3,Th5,Th6;
  now
    take r1;
    thus r1 > 0 by A4;
    let g;
    assume that
    g in dom f2 and
A6: g in [.x0 - r1,x0.];
    thus f2.g <> 0 by A5,A6;
  end;
  hence thesis by A1,A2,Lm1;
end;

Lm2: f is_left_differentiable_in x0 & (ex r0 st r0 > 0 & for g st g in dom f &
g in [.x0 - r0,x0.] holds f.g <> 0) implies f^ is_left_differentiable_in x0 &
Ldiff(f^,x0) = - Ldiff(f,x0)/(f.x0)^2
proof
  assume
A1: f is_left_differentiable_in x0;
  then consider r2 such that
A2: 0 < r2 and
A3: [.x0 -r2,x0.] c= dom f by Def4;
  given r0 such that
A4: r0 > 0 and
A5: for g st g in dom f & g in [.x0 - r0,x0.] holds f.g <> 0;
  set r3 = min(r0,r2);
A6: 0 < r3 by A4,A2,XXREAL_0:15;
  then
A7: x0 - r3 <= x0 by XREAL_1:45;
  r3 <= r2 by XXREAL_0:17;
  then
A8: x0 - r2 <= x0 - r3 by XREAL_1:15;
  then x0 - r2 <= x0 by A7,XXREAL_0:2;
  then
A9: x0 in [.x0 - r2,x0.] by XXREAL_1:1;
  x0 - r3 in { g: x0 - r2 <= g & g <= x0 } by A7,A8;
  then x0 - r3 in [.x0 - r2,x0.] by RCOMP_1:def 1;
  then [.x0 - r3,x0.] c= [.x0 - r2,x0.] by A9,XXREAL_2:def 12;
  then
A10: [.x0 - r3,x0.] c= dom f by A3,XBOOLE_1:1;
  r3 <= r0 by XXREAL_0:17;
  then
A11: x0 - r0 <= x0 - r3 by XREAL_1:15;
  then x0 - r0 <= x0 by A7,XXREAL_0:2;
  then
A12: x0 in [.x0 - r0,x0.] by XXREAL_1:1;
  x0 - r3 in { g: x0 - r0 <= g & g <= x0 } by A7,A11;
  then x0 - r3 in [.x0 - r0,x0.] by RCOMP_1:def 1;
  then
A13: [.x0 - r3,x0.] c= [.x0 - r0,x0.] by A12,XXREAL_2:def 12;
A14: [.x0 - r3,x0.] c= dom(f^)
  proof
    assume not thesis;
    then consider x such that
A15: x in [.x0 - r3,x0.] and
A16: not x in dom (f^) by TARSKI:def 3;
    reconsider x as Real by A15;
A17: not x in dom f \ f"{0} by A16,RFUNCT_1:def 8;
    now
      per cases by A17,XBOOLE_0:def 5;
      suppose
        not x in dom f;
        hence contradiction by A10,A15;
      end;
      suppose
A18:    x in f"{0};
        then f.x in {0} by FUNCT_1:def 13;
        then
A19:    f.x = 0 by TARSKI:def 1;
        x in dom f by A18,FUNCT_1:def 13;
        hence contradiction by A5,A13,A15,A19;
      end;
    end;
    hence contradiction;
  end;
A20: x0 in [.x0 - r3,x0.] by A7,XXREAL_1:1;
  for h,c st rng c = {x0} & rng (h+c) c= dom (f^) & (for n holds h.n < 0)
holds h"(#)((f^)/*(h+c) - (f^)/*c) is convergent & lim(h"(#)((f^)/*(h+c) - (f^)
  /*c)) = - Ldiff(f,x0)/(f.x0)^2
  proof
    let h,c;
    assume that
A21: rng c = {x0} and
A22: rng (h+c) c= dom (f^) and
A23: for n holds h.n < 0;
A24: lim h = 0 by FDIFF_1:def 1;
A25: for m holds c.m = x0
    proof
      let m;
      c.m in rng c by VALUED_0:28;
      hence thesis by A21,TARSKI:def 1;
    end;
A26: dom f \ f"{0} c= dom f by XBOOLE_1:36;
    rng (h + c) c= dom f\f"{0} by A22,RFUNCT_1:def 8;
    then
A27: rng (h + c) c= dom f by A26,XBOOLE_1:1;
    then
A28: lim(h"(#)(f/*(h+c) - f/*c)) = Ldiff(f,x0) by A1,A21,A23,Th9;
    Ldiff(f,x0) = Ldiff(f,x0);
    then
A29: h"(#)(f/*(h+c) - f/*c) is convergent by A1,A21,A23,A27,Th9;
    then
A30: -(h"(#)((f/*(h+c)) - (f/*c))) is convergent by SEQ_2:23;
    x0 in dom (f^) by A20,A14;
    then
A31: x0 in (dom f\f"{0}) by RFUNCT_1:def 8;
    rng c c= dom f\f"{0}
    proof
      let x;
      assume x in rng c;
      hence thesis by A21,A31,TARSKI:def 1;
    end;
    then
A32: rng c c= dom (f^) by RFUNCT_1:def 8;
    then
A33: f/*c is non-zero by RFUNCT_2:26;
    now
      let n;
A34:  h.n <> 0 by A23;
      thus (h(#)(h"(#)(f/*(h+c) - f/*c))).n = ((h(#)h")(#) (f/*(h+c) - f/*c)).
      n by SEQ_1:22
        .= (h(#)h").n *(f/*(h+c) - f/*c).n by SEQ_1:12
        .= h.n * (h").n *(f/*(h+c) - f/*c).n by SEQ_1:12
        .= h.n * (h.n)" *(f/*(h+c) - f/*c).n by VALUED_1:10
        .= 1*(f/*(h+c) - f/*c).n by A34,XCMPLX_0:def 7
        .= (f/*(h+c) - f/*c).n;
    end;
    then
A35: h(#)(h"(#)(f/*(h+c) - f/*c)) = (f/*(h+c) - f/*c) by
    FUNCT_2:113;
A36: f/*(h+c) is non-zero by A22,RFUNCT_2:26;
    then
A37: (f/*(h+c)) (#) (f/*c) is non-zero by A33,SEQ_1:43;
    now
      let n;
      thus (f/*c + (f/*(h+c) - f/*c)).n = (f/*c).n + (f/*(h+c) - f/*c).n by
      SEQ_1:11
        .= (f/*c).n + ((f/*(h+c)).n - (f/*c).n) by RFUNCT_2:6
        .= (f/*(h+c)).n;
    end;
    then
A38: f/*c + (f/*(h+c) - f/*c) = f/*(h+c) by FUNCT_2:113;
    dom (f^) = dom f \ f"{0} by RFUNCT_1:def 8;
    then
A39: dom (f^) c= dom f by XBOOLE_1:36;
A40: for g be real number st 0 < g ex n st for m st n <=m holds abs((f/*c)
    .m - f.x0) < g
    proof
      let g be real number such that
A41:  0 < g;
      take n = 0;
      let m such that
      n <= m;
      abs((f/*c).m - f.x0) = abs(f.(c.m) - f.x0) by A32,A39,FUNCT_2:185 ,
      XBOOLE_1:1
        .=abs(f.x0 - f.x0) by A25
        .= 0 by ABSVALUE:def 1;
      hence thesis by A41;
    end;
    then
A42: f/*c is convergent by SEQ_2:def 6;
    then
A43: lim(f/*c) = f.x0 by A40,SEQ_2:def 7;
A44: h is convergent by FDIFF_1:def 1;
    then h(#)(h"(#)(f/*(h+c) - f/*c)) is convergent by A29,SEQ_2:28;
    then
A45: f/*(h+c) is convergent by A42,A35,A38,SEQ_2:19;
    lim (h(#)(h"(#)(f/*(h+c) - f/*c))) = lim h *lim (h"(#)(f/*(h+c) - f/*
    c ) ) by A44,A29,SEQ_2:29
      .= 0 by A24;
    then 0 = lim (f/*(h+c)) - f.x0 by A42,A43,A35,A45,SEQ_2:26;
    then
A46: lim(f/*(h+c) (#) (f/*c)) =(f.x0)^2 by A42,A43,A45,SEQ_2:29;
A47: lim(f/*(h+c) (#) (f/*c))<> 0
    proof
      assume not thesis;
      then f.x0 = 0 by A46,XCMPLX_1:6;
      hence contradiction by A5,A3,A12,A9;
    end;
    now
      let n;
A48:  (f/*(h+c)).n <>0 & (f/*c).n <>0 by A36,A33,SEQ_1:7;
      thus (h"(#) ((f^)/*(h+c) - ((f^)/*c))).n = (h").n* ((f^)/*(h+c) - ((f^)
      /*c)).n by SEQ_1:12
        .= (h").n* (((f^)/*(h+c)).n - ((f^)/*c).n) by RFUNCT_2:6
        .= (h").n* (((f/*(h+c))").n - ((f^)/*c).n) by A22,RFUNCT_2:27
        .= (h").n* (((f/*(h+c))").n - ((f/*c)").n) by A32,RFUNCT_2:27
        .= (h").n* (((f/*(h+c)).n)" - ((f/*c)").n) by VALUED_1:10
        .= (h").n* (((f/*(h+c)).n)" - ((f/*c).n)") by VALUED_1:10
        .= (h").n* (1/(f/*(h+c)).n - ((f/*c).n)") by XCMPLX_1:217
        .= (h").n* (1/(f/*(h+c)).n - 1/(f/*c).n) by XCMPLX_1:217
        .= (h").n* ((1*((f/*c).n) - 1*((f/*(h+c)).n))/((f/*(h+c)).n*(f/*c).n
      )) by A48,XCMPLX_1:131
        .= (h").n* ((-((f/*(h+c)).n - (f/*c).n))/((f/*(h+c))(#)(f/*c)).n) by
      SEQ_1:12
        .= (h").n* ((-((f/*(h+c) - (f/*c)).n))/((f/*(h+c))(#)(f/*c)).n) by
      RFUNCT_2:6
        .= (h").n*(-(((f/*(h+c) - (f/*c)).n)/((f/*(h+c))(#)(f/*c)).n)) by
      XCMPLX_1:188
        .= -((h").n*(((f/*(h+c) - (f/*c)).n)/(((f/*(h+c))(#)(f/*c)).n)))
        .= -((h").n*((f/*(h+c) - (f/*c)).n))/((f/*(h+c))(#)(f/*c)).n by
      XCMPLX_1:75
        .= -( ((h" (#) ((f/*(h+c)) - (f/*c))).n) )/((f/*(h+c))(#)(f/*c)).n
      by SEQ_1:12
        .= -(((h"(#)((f/*(h+c)) - (f/*c))).n))*(((f/*(h+c))(#) (f/*c)).n)"
      by XCMPLX_0:def 9
        .= -((((h"(#)((f/*(h+c)) - (f/*c))).n))*(((f/*(h+c))(#) (f/*c))").n)
      by VALUED_1:10
        .= -(((h")(#)((f/*(h+c)) - (f/*c)))/"((f/*(h+c))(#)(f/*c))).n by
      SEQ_1:12
        .= (-((h"(#)((f/*(h+c)) - (f/*c)))/"((f/*(h+c))(#)(f/*c)))).n by
      SEQ_1:14
        .= ((-(h"(#)((f/*(h+c)) - (f/*c))))/"((f/*(h+c))(#)(f/*c))).n by
      SEQ_1:56;
    end;
    then
A49: h"(#) ((f^)/*(h+c) - (f^)/*c) = (-(h"(#)((f/*(h+c)) - (f/*c))))/"((f
    /*(h+c))(#) (f/*c)) by FUNCT_2:113;
A50: f/*(h+c) (#) (f/*c) is convergent by A42,A45,SEQ_2:28;
    then
    lim(h"(#) ((f^)/*(h+c) - (f^)/*c)) = lim (-(h"(#) ((f/*(h+c)) - (f/*c
    ))))/(f.x0)^2 by A37,A46,A47,A30,A49,SEQ_2:38
      .= (-Ldiff(f,x0))/(f.x0)^2 by A29,A28,SEQ_2:24
      .= -Ldiff(f,x0)/(f.x0)^2 by XCMPLX_1:188;
    hence thesis by A37,A50,A47,A30,A49,SEQ_2:37;
  end;
  hence thesis by A6,A14,Th9;
end;

theorem
  f is_left_differentiable_in x0 & f.x0 <> 0 implies f^
  is_left_differentiable_in x0 & Ldiff(f^,x0) = - Ldiff(f,x0)/(f.x0)^2
proof
  assume that
A1: f is_left_differentiable_in x0 and
A2: f.x0 <> 0;
  ex r st r > 0 &[.x0-r,x0.] c= dom f by A1,Def4;
  then consider r1 such that
A3: r1 > 0 and
  [.x0-r1,x0.] c= dom f and
A4: for g st g in [.x0-r1,x0.] holds f.g <> 0 by A1,A2,Th5,Th6;
  now
    take r1;
    thus r1 > 0 by A3;
    let g;
    assume that
    g in dom f and
A5: g in [.x0 - r1,x0.];
    thus f.g <> 0 by A4,A5;
  end;
  hence thesis by A1,Lm2;
end;

theorem Th15:
  f is_right_differentiable_in x0 & Rdiff(f,x0) = g1 iff (ex r st
r>0 & [.x0,x0+r.] c= dom f) & for h,c st rng c = {x0} & rng (h+c) c= dom f & (
for n holds h.n > 0) holds h"(#)(f/*(h+c) - f/*c) is convergent & lim (h"(#)(f
  /*(h+c) - f/*c)) = g1
proof
  thus f is_right_differentiable_in x0 & Rdiff(f,x0) = g1 implies (ex r st r>0
& [.x0,x0+r.] c= dom f) & for h,c st rng c = {x0} & rng (h+c) c= dom f & (for n
holds h.n > 0) holds h"(#)(f/*(h+c) - f/*c) is convergent & lim (h"(#)(f/*(h+c)
  - f/*c)) = g1 by Def3,Def6;
  assume that
A1: ex r st r>0 & [.x0,x0+r.] c= dom f and
A2: for h,c st rng c = {x0} & rng (h+c) c= dom f & (for n holds h.n > 0)
holds h"(#)(f/*(h+c) - f/*c) is convergent & lim (h"(#)(f/*(h+c) - f/*c)) = g1;
  for h,c holds ( rng c = {x0} & rng (h+c) c= dom f & for n holds h.n > 0
  ) implies h"(#)(f/*(h+c) - f/*c) is convergent by A2;
  hence
A3: f is_right_differentiable_in x0 by A1,Def3;
  for h,c holds ( rng c = {x0} & rng (h+c) c= dom f & for n holds h.n > 0)
  implies lim (h"(#)(f/*(h+c) - f/*c)) = g1 by A2;
  hence thesis by A3,Def6;
end;

theorem
  f1 is_right_differentiable_in x0 & f2 is_right_differentiable_in x0
  implies f1+f2 is_right_differentiable_in x0 & Rdiff(f1+f2,x0) = Rdiff(f1,x0)+
  Rdiff(f2,x0)
proof
  assume that
A1: f1 is_right_differentiable_in x0 and
A2: f2 is_right_differentiable_in x0;
  consider r2 such that
A3: r2>0 and
A4: [.x0,x0+r2.] c=dom f2 by A2,Def3;
  consider r1 such that
A5: r1>0 and
A6: [.x0,x0+r1.] c= dom f1 by A1,Def3;
A7: x0+0 = x0;
  set r = min (r1,r2);
  0 <= r by A5,A3,XXREAL_0:15;
  then
A8: x0 <= x0 + r by A7,XREAL_1:9;
  r<=r2 by XXREAL_0:17;
  then
A9: x0 + r <= x0 + r2 by XREAL_1:9;
  then x0 + r in {g: x0 <= g & g <= x0 +r2} by A8;
  then
A10: x0 + r in [.x0,x0+r2.] by RCOMP_1:def 1;
  x0 <= x0 + r2 by A8,A9,XXREAL_0:2;
  then x0 in [.x0,x0+r2.] by XXREAL_1:1;
  then [.x0,x0+r.] c= [.x0,x0+r2.] by A10,XXREAL_2:def 12;
  then
A11: [.x0,x0+r.] c= dom f2 by A4,XBOOLE_1:1;
  r<=r1 by XXREAL_0:17;
  then
A12: x0 + r <= x0 + r1 by XREAL_1:9;
  then x0 + r in {g: x0 <= g & g <= x0 +r1} by A8;
  then
A13: x0 + r in [.x0,x0+r1.] by RCOMP_1:def 1;
  x0 <= x0 + r1 by A12,A8,XXREAL_0:2;
  then x0 in [.x0,x0+r1.] by XXREAL_1:1;
  then [.x0,x0+r.] c= [.x0,x0+r1.] by A13,XXREAL_2:def 12;
  then [.x0,x0+r.] c= dom f1 by A6,XBOOLE_1:1;
  then
A14: [.x0,x0+r.] c= dom f1 /\ dom f2 by A11,XBOOLE_1:19;
  then
A15: [.x0,x0+r.] c= dom(f1 + f2) by VALUED_1:def 1;
A16: for h,c st rng c = {x0} & rng (h+c) c= dom (f1+f2) & (for n holds h.n >
0 ) holds h"(#)((f1+f2)/*(h+c) - (f1+f2)/*c) is convergent & lim (h"(#)((f1+f2)
  /*(h+c) - (f1+f2)/*c)) = Rdiff(f1,x0) + Rdiff(f2,x0)
  proof
    let h,c;
    assume that
A17: rng c = {x0} and
A18: rng (h+c) c= dom (f1+f2) and
A19: for n holds h.n > 0;
A20: rng (h+c) c= dom f1 /\ dom f2 by A18,VALUED_1:def 1;
A21: now
      let n;
A22:  rng c c= dom f1 /\ dom f2
      proof
        let x;
        assume x in rng c;
        then
A23:    x = x0 by A17,TARSKI:def 1;
        x0 in [.x0,x0 + r.] by A8,XXREAL_1:1;
        hence thesis by A14,A23;
      end;
      thus (f1/*(h+c) - f1/*c + (f2/*(h+c) - f2/*c)).n = (f1/*(h+c) + (-(f1/*c
      ))).n + (f2/*(h+c) - f2/*c).n by SEQ_1:11
        .= (f1/*(h+c)).n + (-(f1/*c)).n + (f2/*(h+c) + (-(f2/*c))).n by
      SEQ_1:11
        .= (f1/*(h+c)).n + (-(f1/*c)).n + ((f2/*(h+c)).n + (-(f2/*c)).n) by
      SEQ_1:11
        .= (f1/*(h+c)).n + (f2/*(h+c)).n+ ((-(f1/*c)).n +(-(f2/*c)).n)
        .= (f1/*(h+c)).n + (f2/*(h+c)).n + (-(f1/*c).n + (-(f2/*c)).n) by
      SEQ_1:14
        .= (f1/*(h+c)).n + (f2/*(h+c)).n + (-(f1/*c).n + -(f2/*c).n) by
      SEQ_1:14
        .= (f1/*(h+c)).n + (f2/*(h+c)).n - ((f1/*c).n + (f2/*c).n)
        .= (f1/*(h+c) + f2/*(h+c)).n - ((f1/*c).n + (f2/*c).n) by SEQ_1:11
        .= (f1/*(h+c) + f2/*(h+c)).n - ((f1/*c + f2/*c).n) by SEQ_1:11
        .= (f1/*(h+c) + f2/*(h+c) - (f1/*c + f2/*c)).n by RFUNCT_2:6
        .= ((f1+f2)/*(h+c) - (f1/*c + f2/*c)).n by A20,RFUNCT_2:23
        .= ((f1+f2)/*(h+c) - (f1+f2)/*c).n by A22,RFUNCT_2:23;
    end;
    then
A24: f1/*(h+c)-f1/*c + (f2/*(h+c) - f2/*c)=(f1+f2)/*(h+c) - (f1+f2)/*c by
    FUNCT_2:113;
    dom f1 /\dom f2 c= dom f2 by XBOOLE_1:17;
    then
A25: rng (h+c) c= dom f2 by A20,XBOOLE_1:1;
    then
A26: lim(h"(#)(f2/*(h+c) - f2/*c)) = Rdiff(f2,x0) by A2,A17,A19,Th15;
    Rdiff(f2,x0) = Rdiff(f2,x0);
    then
A27: h"(#)(f2/*(h+c) - f2/*c) is convergent by A2,A17,A19,A25,Th15;
    dom f1 /\ dom f2 c= dom f1 by XBOOLE_1:17;
    then
A28: rng (h+c) c= dom f1 by A20,XBOOLE_1:1;
A29: (h"(#)(f1/*(h+c) - f1/*c) +h"(#)(f2/*(h+c) - f2/*c)) = h"(#)(f1/*(h+c
    ) -f1/*c + (f2/*(h+c) - f2/*c)) by SEQ_1:24;
    Rdiff(f1,x0) = Rdiff(f1,x0);
    then
A30: h"(#)(f1/*(h+c) - f1/*c) is convergent by A1,A17,A19,A28,Th15;
    then (h"(#)(f1/*(h+c) - f1/*c) + h"(#)(f2/*(h+c) - f2/*c)) is convergent
    by A27,SEQ_2:19;
    hence h"(#)((f1+f2)/*(h+c) - (f1+f2)/*c) is convergent by A29,A21,
    FUNCT_2:113;
    lim(h"(#)(f1/*(h+c) - f1/*c)) = Rdiff(f1,x0) by A1,A17,A19,A28,Th15;
    hence thesis by A30,A27,A26,A29,A24,SEQ_2:20;
  end;
  0<r by A5,A3,XXREAL_0:15;
  hence thesis by A15,A16,Th15;
end;

theorem
  f1 is_right_differentiable_in x0 & f2 is_right_differentiable_in x0
implies f1 - f2 is_right_differentiable_in x0 & Rdiff(f1-f2,x0) = Rdiff(f1,x0)
  - Rdiff(f2,x0)
proof
  assume that
A1: f1 is_right_differentiable_in x0 and
A2: f2 is_right_differentiable_in x0;
  consider r2 such that
A3: r2>0 and
A4: [.x0,x0+r2.] c=dom f2 by A2,Def3;
  consider r1 such that
A5: r1>0 and
A6: [.x0,x0+r1.] c= dom f1 by A1,Def3;
  set r = min (r1,r2);
A7: 0<r by A5,A3,XXREAL_0:15;
  then
A8: x0 + 0 <= x0 + r by XREAL_1:9;
  r<=r2 by XXREAL_0:17;
  then
A9: x0 + r <= x0 + r2 by XREAL_1:9;
  then x0 + r in {g: x0 <= g & g <= x0 +r2} by A8;
  then
A10: x0 + r in [.x0,x0+r2.] by RCOMP_1:def 1;
  x0 <= x0 + r2 by A8,A9,XXREAL_0:2;
  then x0 in [.x0,x0+r2.] by XXREAL_1:1;
  then [.x0,x0+r.] c= [.x0,x0+r2.] by A10,XXREAL_2:def 12;
  then
A11: [.x0,x0+r.] c= dom f2 by A4,XBOOLE_1:1;
  r<=r1 by XXREAL_0:17;
  then
A12: x0 + r <= x0 + r1 by XREAL_1:9;
  then x0 + r in {g: x0 <= g & g <= x0 +r1} by A8;
  then
A13: x0 + r in [.x0,x0+r1.] by RCOMP_1:def 1;
  x0 <= x0 + r1 by A12,A8,XXREAL_0:2;
  then x0 in [.x0,x0+r1.] by XXREAL_1:1;
  then [.x0,x0+r.] c= [.x0,x0+r1.] by A13,XXREAL_2:def 12;
  then [.x0,x0+r.] c= dom f1 by A6,XBOOLE_1:1;
  then
A14: [.x0,x0+r.] c= dom f1 /\ dom f2 by A11,XBOOLE_1:19;
A15: for h,c st rng c = {x0} & rng (h+c) c= dom (f1 - f2) & (for n holds h.n
> 0 ) holds h"(#)((f1-f2)/*(h+c) - (f1-f2)/*c) is convergent & lim(h"(#)((f1-f2
  )/*(h+c) - (f1-f2)/*c)) = Rdiff(f1,x0) - Rdiff(f2,x0)
  proof
    let h,c;
    assume that
A16: rng c ={x0} and
A17: rng (h+c) c= dom(f1-f2) and
A18: for n holds h.n > 0;
A19: rng (h+c) c= dom f1 /\ dom f2 by A17,VALUED_1:12;
A20: now
      let n;
A21:  rng c c= dom f1 /\ dom f2
      proof
        let x;
        assume x in rng c;
        then
A22:    x = x0 by A16,TARSKI:def 1;
        x0 in [.x0,x0+r.] by A8,XXREAL_1:1;
        hence thesis by A14,A22;
      end;
      thus (f1/*(h+c) - f1/*c - (f2/*(h+c) - f2/*c)).n = (f1/*(h+c) - f1/*c).n
      - (f2/*(h+c) - f2/*c).n by RFUNCT_2:6
        .= (f1/*(h+c)).n - (f1/*c).n - (f2/*(h+c) - (f2/*c)).n by RFUNCT_2:6
        .= (f1/*(h+c)).n - (f1/*c).n - ((f2/*(h+c)).n - (f2/*c).n) by
      RFUNCT_2:6
        .= (f1/*(h+c)).n - (f2/*(h+c)).n - ((f1/*c).n - (f2/*c).n)
        .= (f1/*(h+c) - f2/*(h+c)).n - ((f1/*c).n - (f2/*c).n) by RFUNCT_2:6
        .= (f1/*(h+c) - f2/*(h+c)).n - ((f1/*c - f2/*c).n) by RFUNCT_2:6
        .= (f1/*(h+c) - f2/*(h+c) - (f1/*c - f2/*c)).n by RFUNCT_2:6
        .= ((f1-f2)/*(h+c) - (f1/*c - f2/*c)).n by A19,RFUNCT_2:23
        .= ((f1-f2)/*(h+c) - (f1-f2)/*c).n by A21,RFUNCT_2:23;
    end;
    then
A23: f1/*(h+c) - f1/*c - (f2/*(h+c) - f2/*c) = (f1-f2)/*(h+c) - (f1-f2)/*c
    by FUNCT_2:113;
    dom f1 /\ dom f2 c= dom f2 by XBOOLE_1:17;
    then
A24: rng (h+c) c= dom f2 by A19,XBOOLE_1:1;
    then
A25: lim(h"(#)((f2/*(h+c) - f2/*c))) =Rdiff(f2,x0) by A2,A16,A18,
    Th15;
    Rdiff(f2,x0) = Rdiff(f2,x0);
    then
A26: h"(#)(f2/*(h+c) - f2/*c) is convergent by A2,A16,A18,A24,Th15;
    dom f1 /\ dom f2 c= dom f1 by XBOOLE_1:17;
    then
A27: rng (h+c) c= dom f1 by A19,XBOOLE_1:1;
A28: (h"(#)(f1/*(h+c) - f1/*c) - h"(#)(f2/*(h+c) - f2/*c)) = h"(#)(f1/*(h+
    c) -f1/*c - (f2/*(h+c) - f2/*c)) by SEQ_1:29;
    Rdiff(f1,x0) = Rdiff(f1,x0);
    then
A29: h"(#)(f1/*(h+c) - f1/*c) is convergent by A1,A16,A18,A27,Th15;
    then (h"(#)(f1/*(h+c) - f1/*c) - h"(#)(f2/*(h+c) - f2/*c)) is convergent
    by A26,SEQ_2:25;
    hence h"(#)((f1-f2)/*(h+c) - (f1-f2)/*c) is convergent by A28,A20,
    FUNCT_2:113;
    lim(h"(#)((f1/*(h+c) - f1/*c))) =Rdiff(f1,x0) by A1,A16,A18,A27,Th15;
    hence thesis by A29,A26,A25,A28,A23,SEQ_2:26;
  end;
  [.x0,x0+r.] c= dom (f1 - f2) by A14,VALUED_1:12;
  hence thesis by A7,A15,Th15;
end;

theorem
  f1 is_right_differentiable_in x0 & f2 is_right_differentiable_in x0
implies f1(#)f2 is_right_differentiable_in x0 & Rdiff(f1(#)f2,x0) = Rdiff(f1,x0
  )*f2.x0 + Rdiff(f2,x0)*f1.x0
proof
  assume that
A1: f1 is_right_differentiable_in x0 and
A2: f2 is_right_differentiable_in x0;
A3: x0 + 0 =x0;
  consider r2 such that
A4: r2>0 and
A5: [.x0,x0+r2.] c=dom f2 by A2,Def3;
  consider r1 such that
A6: r1>0 and
A7: [.x0,x0+r1.] c= dom f1 by A1,Def3;
  set r = min (r1,r2);
  0 <= r by A6,A4,XXREAL_0:15;
  then
A8: x0 <= x0 + r by A3,XREAL_1:9;
  r<=r2 by XXREAL_0:17;
  then
A9: x0 + r <= x0 + r2 by XREAL_1:9;
  then x0 + r in {g: x0 <= g & g <= x0 +r2} by A8;
  then
A10: x0 + r in [.x0,x0+r2.] by RCOMP_1:def 1;
  x0 <= x0 + r2 by A8,A9,XXREAL_0:2;
  then x0 in [.x0,x0+r2.] by XXREAL_1:1;
  then [.x0,x0+r.] c= [.x0,x0+r2.] by A10,XXREAL_2:def 12;
  then
A11: [.x0,x0+r.] c= dom f2 by A5,XBOOLE_1:1;
  r<=r1 by XXREAL_0:17;
  then
A12: x0 + r <= x0 + r1 by XREAL_1:9;
  then x0 + r in {g: x0 <= g & g <= x0 +r1} by A8;
  then
A13: x0 + r in [.x0,x0+r1.] by RCOMP_1:def 1;
  x0 <= x0+r1 by A12,A8,XXREAL_0:2;
  then x0 in [.x0,x0+r1.] by XXREAL_1:1;
  then [.x0,x0+r.] c= [.x0,x0+r1.] by A13,XXREAL_2:def 12;
  then
A14: [.x0,x0+r.] c= dom f1 by A7,XBOOLE_1:1;
A15: 0<r by A6,A4,XXREAL_0:15;
A16: for h,c st rng c = {x0} & rng (h+c) c= dom (f1(#)f2) & (for n holds h.n
> 0 ) holds h"(#)((f1(#)f2)/*(h+c) - (f1(#)f2)/*c) is convergent & lim(h"(#)((
  f1(#)f2)/*(h+c) - (f1(#) f2)/*c))=Rdiff(f1,x0)*f2.x0 + Rdiff(f2,x0)*f1.x0
  proof
    let h,c;
    assume that
A17: rng c = {x0} and
A18: rng (h+c) c= dom (f1(#)f2) and
A19: for n holds h.n > 0;
A20: rng (h + c) c= dom f1 /\ dom f2 by A18,VALUED_1:def 4;
    x0 + 0 <= x0 +r by A15,XREAL_1:9;
    then
A21: x0 in [.x0,x0+ r.] by XXREAL_1:1;
    then
A22: x0 in dom f2 by A11;
A23: rng c c=dom f2
    proof
      let x;
      assume x in rng c;
      hence thesis by A17,A22,TARSKI:def 1;
    end;
A24: for m holds c.m = x0
    proof
      let m;
      c.m in rng c by VALUED_0:28;
      hence thesis by A17,TARSKI:def 1;
    end;
A25: for g be real number st 0<g ex n st for m st n <= m holds abs((f2/*c)
    .m - f2.x0 )<g
    proof
      let g be real number such that
A26:  0<g;
      take n=0;
      let m such that
      n <= m;
      abs((f2/*c).m - f2.x0 ) = abs(f2.(c.m) - f2.x0 ) by A23,FUNCT_2:185
        .=abs( f2.x0 - f2.x0 ) by A24
        .= 0 by ABSVALUE:def 1;
      hence thesis by A26;
    end;
    then
A27: f2/*c is convergent by SEQ_2:def 6;
    dom f1 /\ dom f2 c=dom f2 by XBOOLE_1:17;
    then
A28: rng (h + c) c= dom f2 by A20,XBOOLE_1:1;
    then
A29: lim(h"(#)(f2/*(h+c) - f2/*c)) = Rdiff(f2,x0) by A2,A17,A19,Th15;
    now
      let n;
A30:  h.n <> 0 by A19;
      thus (h(#)(h"(#)(f1/*(h+c) - f1/*c))).n =((h(#)h")(#)(f1/*(h+c) - f1/*c)
      ). n by SEQ_1:22
        .= (h(#)h").n*(f1/*(h+c) - f1/*c).n by SEQ_1:12
        .= h.n*(h").n*(f1/*(h+c) - f1/*c).n by SEQ_1:12
        .= h.n*(h.n)"*(f1/*(h+c) - f1/*c).n by VALUED_1:10
        .= 1*(f1/*(h+c) - f1/*c).n by A30,XCMPLX_0:def 7
        .= (f1/*(h+c) - f1/*c).n;
    end;
    then
A31: h(#)(h"(#)(f1/*(h+c) - f1/*c)) =(f1/*(h+c) - f1/*c) by FUNCT_2:
    113;
A32: x0 in dom f1 by A14,A21;
A33: rng c c=dom f1
    proof
      let x;
      assume x in rng c;
      hence thesis by A17,A32,TARSKI:def 1;
    end;
A34: for g be real number st 0<g ex n st for m st n <= m holds abs((f1/*c)
    .m - f1.x0 )<g
    proof
      let g be real number such that
A35:  0<g;
      take n=0;
      let m such that
      n <= m;
      abs((f1/*c).m - f1.x0 ) = abs(f1.(c.m) - f1.x0 ) by A33,FUNCT_2:185
        .=abs( f1.x0 - f1.x0 ) by A24
        .= 0 by ABSVALUE:def 1;
      hence thesis by A35;
    end;
    then
A36: f1/*c is convergent by SEQ_2:def 6;
    dom f1 /\ dom f2 c= dom f1 by XBOOLE_1:17;
    then
A37: rng (h + c) c= dom f1 by A20,XBOOLE_1:1;
    then
A38: lim(h"(#)(f1/*(h+c) - f1/*c)) = Rdiff(f1,x0) by A1,A17,A19,Th15;
    Rdiff(f1,x0) = Rdiff(f1,x0);
    then
A39: h"(#)(f1/*(h+c) - f1/*c) is convergent by A1,A17,A19,A37,Th15;
    then
A40: h"(#) (f1/*(h+c) - f1/*c) (#) (f2/*c) is convergent by
    A27,SEQ_2:
    28;
A41: rng c c= dom f1 /\ dom f2 by A33,A23,XBOOLE_1:19;
A42: now
      let n;
      thus (h"(#)((f1(#)f2)/*(h+c) - (f1(#)f2)/*c)).n = ((h").n) * ((f1(#)f2)
      /*(h+c) - (f1(#)f2)/*c).n by SEQ_1:12
        .= ((h").n) * (((f1(#)f2)/*(h+c)).n - ((f1(#)f2)/*c).n) by RFUNCT_2:
      6
        .= h".n * (((f1/*(h+c))(#)(f2/*(h+c))).n - ((f1(#)f2)/*c).n) by A20,
      RFUNCT_2:23
        .= h".n * (((f1/*(h+c))(#)(f2/*(h+c))).n - ((f1/*c)(#)(f2/*c)).n) by
      A41,RFUNCT_2:23
        .= h".n * ((f1/*(h+c)).n * (f2/*(h+c)).n - ((f1/*c)(#)(f2/*c)).n) by
      SEQ_1:12
        .= h".n * ((f1/*(h+c)).n * (f2/*(h+c)).n - ((f1/*c).n * (f2/*c).n))
      by SEQ_1:12
        .= h".n * ((f2/*(h+c)).n - (f2/*c).n)*(f1/*(h+c)).n + (h".n * ((f1/*
      (h+c)).n - (f1/*c).n))*(f2/*c).n
        .= h".n * (f2/*(h+c) - f2/*c).n * (f1/*(h+c)).n + (h".n * ((f1/*(h+c
      )).n - (f1/*c).n))*(f2/*c).n by RFUNCT_2:6
        .= (h"(#) (f2/*(h+c) - f2/*c)).n * (f1/*(h+c)).n + (h".n * ((f1/*(h+
      c)).n - (f1/*c).n))*(f2/*c).n by SEQ_1:12
        .= (h"(#) (f2/*(h+c) - f2/*c)).n * (f1/*(h+c)).n + (h".n * (f1/*(h+c
      ) - (f1/*c)).n)*(f2/*c).n by RFUNCT_2:6
        .= (h"(#) (f2/*(h+c) - f2/*c)).n * (f1/*(h+c)).n + (h"(#) (f1/*(h+c)
      - f1/*c)).n*(f2/*c).n by SEQ_1:12
        .= (h"(#) (f2/*(h+c) - f2/*c) (#) (f1/*(h+c))).n + (h"(#) (f1/*(h+c)
      - f1/*c)).n*(f2/*c).n by SEQ_1:12
        .= (h"(#) (f2/*(h+c) - f2/*c)(#)(f1/*(h+c))).n + (h"(#) (f1/*(h+c) -
      f1/*c)(#)(f2/*c)).n by SEQ_1:12
        .= (h"(#) (f2/*(h+c) - f2/*c)(#)(f1/*(h+c)) + h"(#) (f1/*(h+c) - f1
      /*c)(#)(f2/*c)).n by SEQ_1:11;
    end;
    then
A43: h"(#)((f1(#)f2)/*(h+c) - (f1(#)f2)/*c) = h"(#) (f2/*(h+c) - f2/*c)(#)
    (f1/*(h+c)) + h"(#) (f1/*(h+c) - f1/*c)(#)(f2/*c) by FUNCT_2:113;
    now
      let n;
      thus (f1/*c + (f1/*(h+c) - f1/*c)).n = (f1/*c).n + (f1/*(h+c) - f1/*c).n
      by SEQ_1:11
        .= (f1/*c).n + ((f1/*(h+c)).n - (f1/*c).n) by RFUNCT_2:6
        .= (f1/*(h+c)).n;
    end;
    then
A44: f1/*c + (f1/*(h+c) - f1/*c) = f1/*(h+c) by FUNCT_2:113;
A45: lim h = 0 by FDIFF_1:def 1;
A46: h is convergent & h"(#)(f1/*(h+c) - f1/*c) is convergent by A1,A17,
    A19,A37, Def3,FDIFF_1:def 1;
    then h(#)(h"(#)(f1/*(h+c) - f1/*c)) is convergent by SEQ_2:28;
    then
A47: f1/*(h+c) is convergent by A36,A31,A44,SEQ_2:19;
    lim(h(#)(h"(#)(f1/*(h+c) - f1/*c))) =lim h *lim(h"(#)(f1/*(h+c) - f1
    /*c) ) by A46,SEQ_2:29
      .= 0 by A45;
    then
A48: 0 = lim (f1/*(h+c)) - lim(f1/*c) by A36,A31,A47,SEQ_2:26
      .= lim (f1/*(h+c)) - f1.x0 by A34,A36,SEQ_2:def 7;
    Rdiff(f2,x0) = Rdiff(f2,x0);
    then
A49: h"(#)(f2/*(h+c) - f2/*c) is convergent by A2,A17,A19,A28,Th15;
    then
A50: h"(#) (f2/*(h+c)-f2/*c) (#) (f1/*(h+c)) is convergent by A47,
    SEQ_2:28;
    lim (h"(#)((f1(#)f2)/*(h+c) - (f1(#)f2)/*c)) = lim (h"(#) (f2/*(h+c)
- f2/*c)(#)(f1/*(h+c)) + h"(#) (f1/*(h+c) - f1/*c)(#) (f2/*c)) by A42,FUNCT_2:
    113
      .= lim (h"(#) (f2/*(h+c) - f2/*c)(#)(f1/*(h+c))) + lim (h"(#) (f1/*(h+
    c) - f1/*c)(#)(f2/*c)) by A50,A40,SEQ_2:20
      .= lim (h"(#) (f2/*(h+c) - f2/*c))*lim(f1/*(h+c)) + lim (h"(#) (f1/*(h
    +c) - f1/*c)(#)(f2/*c)) by A49,A47,SEQ_2:29
      .= lim (h"(#) (f2/*(h+c) - f2/*c))* f1.x0 + lim (h"(#) (f1/*(h+c) - f1
    /*c))*lim(f2/*c) by A39,A48,A27,SEQ_2:29
      .= Rdiff(f1,x0)*f2.x0 + Rdiff(f2,x0)*f1.x0 by A38,A29,A25,A27,SEQ_2:
    def 7;
    hence thesis by A50,A40,A43,SEQ_2:19;
  end;
  [.x0,x0+r.] c= dom f1 /\ dom f2 by A14,A11,XBOOLE_1:19;
  then [.x0,x0+r.] c= dom (f1 (#) f2) by VALUED_1:def 4;
  hence thesis by A15,A16,Th15;
end;

Lm3: f1 is_right_differentiable_in x0 & f2 is_right_differentiable_in x0 & (
ex r0 st r0 > 0 & for g st g in dom f2 & g in [.x0, x0 + r0.] holds f2.g <> 0 )
implies f1/f2 is_right_differentiable_in x0 & Rdiff(f1/f2,x0) =(Rdiff(f1,x0)*f2
.x0 - Rdiff(f2,x0)*f1.x0)/(f2.x0)^2
proof
  assume that
A1: f1 is_right_differentiable_in x0 and
A2: f2 is_right_differentiable_in x0;
  consider r2 such that
A3: 0 < r2 and
A4: [.x0,x0 + r2.] c= dom f2 by A2,Def3;
  consider r1 such that
A5: 0 < r1 and
A6: [.x0,x0 + r1.] c= dom f1 by A1,Def3;
  given r0 such that
A7: r0 > 0 and
A8: for g st g in dom f2 & g in [.x0,x0 + r0.] holds f2.g <> 0;
A9: 0 + x0 =x0;
  set r3 = min(r0,r2);
  0 <= r3 by A7,A3,XXREAL_0:15;
  then
A10: x0 <= x0 +r3 by A9,XREAL_1:8;
  r3 <= r2 by XXREAL_0:17;
  then
A11: x0 + r3 <= x0 + r2 by XREAL_1:9;
  then x0 <= x0 + r2 by A10,XXREAL_0:2;
  then
A12: x0 in [.x0,x0 + r2.] by XXREAL_1:1;
  x0 + r3 in { g:x0 <= g & g <= x0 + r2 } by A10,A11;
  then x0 + r3 in [.x0,x0 +r2.] by RCOMP_1:def 1;
  then [.x0,x0 + r3.] c= [.x0,x0 + r2.] by A12,XXREAL_2:def 12;
  then
A13: [.x0,x0 + r3.] c= dom f2 by A4,XBOOLE_1:1;
  r3 <= r0 by XXREAL_0:17;
  then
A14: x0 + r3 <= x0 + r0 by XREAL_1:9;
  then x0 <= x0 +r0 by A10,XXREAL_0:2;
  then
A15: x0 in [.x0,x0 + r0.] by XXREAL_1:1;
A16: x0 +0 = x0;
  set r = min(r1,r3);
A17: 0 < r3 by A7,A3,XXREAL_0:15;
  then 0 <= r by A5,XXREAL_0:15;
  then
A18: x0 <= x0 + r by A16,XREAL_1:9;
  r <= r3 by XXREAL_0:17;
  then
A19: x0 + r <= x0 + r3 by XREAL_1:9;
  then x0 <= x0 + r3 by A18,XXREAL_0:2;
  then
A20: x0 in [.x0,x0 + r3.] by XXREAL_1:1;
  x0 + r3 in { g: x0 <= g & g<= x0 + r0 } by A10,A14;
  then x0 + r3 in [.x0,x0 + r0.] by RCOMP_1:def 1;
  then
A21: [.x0,x0 +r3.] c= [.x0,x0 +r0.] by A15,XXREAL_2:def 12;
  x0 + r in {g:x0 <= g & g <= x0 + r3} by A18,A19;
  then x0 + r in [.x0,x0 +r3.] by RCOMP_1:def 1;
  then
A22: [.x0,x0 + r.] c= [.x0,x0 +r3.] by A20,XXREAL_2:def 12;
  [.x0,x0 + r.] c= dom(f2^)
  proof
    assume not thesis;
    then consider x such that
A23: x in [.x0,x0 + r.] and
A24: not x in dom (f2^) by TARSKI:def 3;
    reconsider x as Real by A23;
A25: x in [.x0,x0 + r3.] by A22,A23;
A26: not x in dom f2 \ f2"{0} by A24,RFUNCT_1:def 8;
    now
      per cases by A26,XBOOLE_0:def 5;
      suppose
        not x in dom f2;
        hence contradiction by A13,A25;
      end;
      suppose
A27:    x in f2"{0};
        then f2.x in {0} by FUNCT_1:def 13;
        then
A28:    f2.x = 0 by TARSKI:def 1;
        x in dom f2 by A27,FUNCT_1:def 13;
        hence contradiction by A8,A21,A25,A28;
      end;
    end;
    hence contradiction;
  end;
  then
A29: [.x0,x0 + r.] c= dom f2\f2"{0} by RFUNCT_1:def 8;
  r <= r1 by XXREAL_0:17;
  then
A30: x0 + r <= x0 + r1 by XREAL_1:9;
  then x0 <= x0 + r1 by A18,XXREAL_0:2;
  then
A31: x0 in [.x0,x0 +r1.] by XXREAL_1:1;
  x0 + r in { g:x0 <= g & g <= x0 + r1 } by A18,A30;
  then x0 + r in [.x0,x0 + r1.] by RCOMP_1:def 1;
  then [.x0,x0 +r.] c= [.x0,x0 + r1.] by A31,XXREAL_2:def 12;
  then
A32: [.x0,x0 + r.] c= dom f1 by A6,XBOOLE_1:1;
  then [.x0,x0 + r.] c= dom f1 /\ (dom f2\f2"{0}) by A29,XBOOLE_1:19;
  then
A33: [.x0,x0 + r.] c= dom (f1/f2) by RFUNCT_1:def 4;
A34: for h,c st rng c = {x0} & rng (h+c) c= dom (f1/f2) & (for n holds h.n >
0) holds h"(#)((f1/f2)/*(h+c) - (f1/f2)/*c) is convergent & lim(h"(#)((f1/f2)/*
  (h+c) - (f1/f2)/*c)) = (Rdiff(f1,x0)*f2.x0 - Rdiff(f2,x0)*f1.x0)/(f2.x0)^2
  proof
    let h,c;
    assume that
A35: rng c = {x0} and
A36: rng (h+c) c= dom (f1/f2) and
A37: for n holds h.n > 0;
A38: rng (h + c) c= dom f1 /\ (dom f2\f2"{0}) by A36,RFUNCT_1:def 4;
    0 <= r & x0 + 0 = x0 by A5,A17,XXREAL_0:15;
    then x0 <= x0 + r by XREAL_1:9;
    then
A39: x0 in [.x0,x0 + r.] by XXREAL_1:1;
    then
A40: x0 in dom f1 by A32;
A41: rng c c= dom f1
    proof
      let x be set;
      assume x in rng c;
      hence thesis by A35,A40,TARSKI:def 1;
    end;
    dom f1 /\ (dom f2\f2"{0}) c= dom f1 by XBOOLE_1:17;
    then
A42: rng (h + c) c= dom f1 by A38,XBOOLE_1:1;
    then
A43: lim(h"(#)(f1/*(h+c) - f1/*c)) = Rdiff(f1,x0) by A1,A35,A37,Th15;
A44: x0 in (dom f2\f2"{0}) by A29,A39;
    rng c c= (dom f2\f2"{0})
    proof
      let x be set;
      assume x in rng c;
      hence thesis by A35,A44,TARSKI:def 1;
    end;
    then
A45: rng c c= dom (f2^) by RFUNCT_1:def 8;
    then
A46: rng c c= dom f1 /\ dom (f2^) by A41,XBOOLE_1:19;
A47: h"(#)(f1/*(h+c) - f1/*c) is convergent by A1,A35,A37,A42,Def3;
A48: f2/*c is non-zero by A45,RFUNCT_2:26;
A49: now
      let n;
      thus (h"(#)((f1/*(h+c))(#)(f2/*c) - (f1/*c)(#)(f2/*(h+c)))).n = (h").n *
      ((f1/*(h+c))(#)(f2/*c) - (f1/*c)(#)(f2/*(h+c))).n by SEQ_1:12
        .= (h").n * (((f1/*(h+c))(#)(f2/*c)).n - ((f1/*c)(#) (f2/*(h+c))).n)
      by RFUNCT_2:6
        .= (h").n * ((f1/*(h+c)).n * (f2/*c).n - ((f1/*c)(#)(f2/*(h+c))).n)
      by SEQ_1:12
        .= (h").n * (((f1/*(h+c)).n - (f1/*c).n) * (f2/*c).n + ((f1/*c).n*(
      f2/*c).n) - ((f1/*c).n * (f2/*(h+c)).n)) by SEQ_1:12
        .= (h").n * ((f1/*(h+c)).n - (f1/*c).n) * (f2/*c).n - (f1/*c).n * ((
      h").n * ((f2/*(h+c)).n - (f2/*c).n))
        .= (h").n * ((f1/*(h+c)) - (f1/*c)).n * (f2/*c).n - (f1/*c).n * ((h"
      ).n * ((f2/*(h+c)).n - (f2/*c).n)) by RFUNCT_2:6
        .= (h").n * (f1/*(h+c) - (f1/*c)).n * (f2/*c).n - (f1/*c).n * ((h").
      n * (f2/*(h+c) - (f2/*c)).n) by RFUNCT_2:6
        .= (h" (#) (f1/*(h+c) - (f1/*c))).n * (f2/*c).n - (f1/*c).n * ((h").
      n * (f2/*(h+c) - (f2/*c)).n) by SEQ_1:12
        .= (h"(#)(f1/*(h+c) - (f1/*c))).n * (f2/*c).n - (f1/*c).n * (h"(#)(
      f2/*(h+c) - (f2/*c))).n by SEQ_1:12
        .= (h"(#)(f1/*(h+c) - (f1/*c))(#)(f2/*c)).n - (f1/*c).n * (h"(#)(f2
      /*(h+c) - (f2/*c))).n by SEQ_1:12
        .= (h"(#)(f1/*(h+c) - (f1/*c))(#)(f2/*c)).n - ((f1/*c)(#)(h"(#)(f2/*
      (h+c)-(f2/*c)))).n by SEQ_1:12
        .= (h"(#)(f1/*(h+c) - (f1/*c))(#)(f2/*c) - (f1/*c)(#)(h"(#) (f2/*(h+
      c)-(f2/*c)))).n by RFUNCT_2:6;
    end;
    now
      let n;
      thus (f2/*c + (f2/*(h+c) - f2/*c)).n = (f2/*c).n + (f2/*(h+c) - f2/*c).n
      by SEQ_1:11
        .= (f2/*c).n + ((f2/*(h+c)).n - (f2/*c).n) by RFUNCT_2:6
        .= (f2/*(h+c)).n;
    end;
    then
A50: f2/*c + (f2/*(h+c) - f2/*c) = f2/*(h+c) by FUNCT_2:113;
A51: lim h = 0 by FDIFF_1:def 1;
    dom f1 /\ (dom f2\f2"{0}) c=(dom f2\f2"{0}) by XBOOLE_1:17;
    then
A52: rng (h + c) c= (dom f2\f2"{0}) by A38,XBOOLE_1:1;
    then
A53: rng (h + c) c= dom (f2^) by RFUNCT_1:def 8;
    then
A54: rng (h + c) c= dom f1 /\ dom (f2^) by A42,XBOOLE_1:19;
A55: f2/*(h+c) is non-zero by A53,RFUNCT_2:26;
    then
A56: (f2/*(h+c)) (#) (f2/*c) is non-zero by A48,SEQ_1:43;
    h"(#) ((f1/f2)/*(h+c) - (f1/f2)/*c)=h"(#)((f1(#) (f2^))/*(h+c) - (f1
    /f2)/*c) by RFUNCT_1:47
      .=h"(#)((f1(#)(f2^))/*(h+c) - (f1(#)(f2^))/*c) by RFUNCT_1:47
      .=h"(#)((f1/*(h+c))(#)((f2^)/*(h+c)) - (f1(#) (f2^))/*c) by A54,
    RFUNCT_2:23
      .=h"(#)((f1/*(h+c))/"(f2/*(h+c)) - (f1(#)(f2^))/*c) by A53,RFUNCT_2:27
      .=h"(#)((f1/*(h+c))/"(f2/*(h+c)) - (f1/*c)(#) ((f2^)/*c)) by A46,
    RFUNCT_2:23
      .=h"(#)((f1/*(h+c))/"(f2/*(h+c)) - (f1/*c)/"(f2/*c)) by A45,RFUNCT_2:
    27
      .=h"(#)(((f1/*(h+c))(#)(f2/*c) - (f1/*c)(#)(f2/*(h+c)))/"((f2/*(h+c))
    (#) (f2/*c))) by A55,A48,SEQ_1:58
      .=(h"(#)((f1/*(h+c))(#)(f2/*c) - (f1/*c)(#)(f2/*(h+c))))/"((f2/*(h+c))
    (#) (f2/*c)) by SEQ_1:49;
    then
A57: h"(#) ((f1/f2)/*(h+c) - (f1/f2)/*c) =((h"(#)(f1/*(h+c) - (f1/*c)) )
(#) (f2/*c) - (f1/*c)(#)(h"(#)(f2/*(h+c)-(f2/*c))))/"(f2/*(h+c)(#) (f2/*c)) by
    A49,FUNCT_2:113;
    dom f2 \ f2"{0} c= dom f2 by XBOOLE_1:36;
    then
A58: rng (h + c) c= dom f2 by A52,XBOOLE_1:1;
    then
A59: lim(h"(#)(f2/*(h+c) - f2/*c)) = Rdiff(f2,x0) by A2,A35,A37,Th15;
    Rdiff(f2,x0) = Rdiff(f2,x0);
    then
A60: h"(#)(f2/*(h+c) - f2/*c) is convergent by A2,A35,A37,A58,Th15;
    now
      let n;
A61:  h.n <> 0 by A37;
      thus (h(#)(h"(#)(f2/*(h+c) - f2/*c))).n = ((h(#)h")(#)(f2/*(h+c) - f2/*c
      )). n by SEQ_1:22
        .= (h(#)h").n *(f2/*(h+c) - f2/*c).n by SEQ_1:12
        .= h.n * (h").n *(f2/*(h+c) - f2/*c).n by SEQ_1:12
        .= h.n * (h.n)" *(f2/*(h+c) - f2/*c).n by VALUED_1:10
        .= 1*(f2/*(h+c) - f2/*c).n by A61,XCMPLX_0:def 7
        .= (f2/*(h+c) - f2/*c).n;
    end;
    then
A62: h(#)(h"(#)(f2/*(h+c) - f2/*c)) = (f2/*(h+c) - f2/*c) by FUNCT_2
    :113;
A63: for m holds c.m = x0
    proof
      let m;
      c.m in rng c by VALUED_0:28;
      hence thesis by A35,TARSKI:def 1;
    end;
A64: for g be real number st 0 < g ex n st for m st n <=m holds abs((f1/*c
    ).m - f1.x0) < g
    proof
      let g be real number such that
A65:  0 < g;
      take n = 0;
      let m such that
      n <= m;
      abs((f1/*c).m - f1.x0) = abs(f1.(c.m) - f1.x0) by A41,FUNCT_2:185
        .=abs(f1.x0 - f1.x0) by A63
        .= 0 by ABSVALUE:def 1;
      hence thesis by A65;
    end;
    then
A66: f1/*c is convergent by SEQ_2:def 6;
    then
A67: lim(f1/*c) = f1.x0 by A64,SEQ_2:def 7;
    dom (f2^) = dom f2 \ f2"{0} by RFUNCT_1:def 8;
    then
A68: dom (f2^) c= dom f2 by XBOOLE_1:36;
A69: for g be real number st 0 < g ex n st for m st n <=m holds abs((f2/*
    c).m - f2.x0) < g
    proof
      let g be real number such that
A70:  0 < g;
      take n = 0;
      let m such that
      n <= m;
      abs((f2/*c).m - f2.x0) = abs(f2.(c.m) - f2.x0) by A45,A68,
      FUNCT_2:185,XBOOLE_1:1
        .=abs(f2.x0 - f2.x0) by A63
        .= 0 by ABSVALUE:def 1;
      hence thesis by A70;
    end;
    then
A71: f2/*c is convergent by SEQ_2:def 6;
    then
A72: lim(f2/*c) = f2.x0 by A69,SEQ_2:def 7;
A73: h is convergent by FDIFF_1:def 1;
    then h(#)(h"(#)(f2/*(h+c) - f2/*c)) is convergent by A60,SEQ_2:28;
    then
A74: f2/*(h+c) is convergent by A71,A62,A50,SEQ_2:19;
    then
A75: f2/*(h+c) (#) (f2/*c) is convergent by A71,SEQ_2:28;
    h"(#)(f2/*(h+c) - f2/*c) is convergent by A2,A35,A37,A58,Def3;
    then lim (h(#)(h"(#)(f2/*(h+c) - f2/*c))) = lim h *lim (h"(#)(f2/*(h+c) -
    f2/* c ) ) by A73,SEQ_2:29
      .= 0 by A51;
    then 0 = lim (f2/*(h+c)) - f2.x0 by A71,A72,A62,A74,SEQ_2:26;
    then
A76: lim(f2/*(h+c) (#) (f2/*c)) = (f2.x0)^2 by A71,A72,A74,SEQ_2:
    29;
A77: lim(f2/*(h+c) (#) (f2/*c))<> 0
    proof
      assume not thesis;
      then f2.x0 = 0 by A76,XCMPLX_1:6;
      hence contradiction by A8,A4,A15,A12;
    end;
    Rdiff(f1,x0) = Rdiff(f1,x0);
    then h"(#)(f1/*(h+c) - f1/*c) is convergent by A1,A35,A37,A42,Th15;
    then
A78: h"(#) (f1/*(h+c) - f1/*c)(#)(f2/*c) is convergent by
    A71,SEQ_2:
    28;
A79: (f1/*c)(#)(h"(#) (f2/*(h+c) - f2/*c)) is convergent by A60,A66,
    SEQ_2:28;
    then
A80: ((h"(#)(f1/*(h+c) - (f1/*c)))(#)(f2/*c) - (f1/*c)(#)(h"(#) (f2/*(h+c
    )-(f2/*c)))) is convergent by A78,SEQ_2:25;
    then lim(h"(#) ((f1/f2)/*(h+c) - (f1/f2)/*c)) = lim((h"(#)(f1/*(h+c) - (
f1/*c)))(#)(f2/*c) - (f1/*c)(#)(h"(#) (f2/*(h+c)-(f2/*c))))/ lim(f2/*(h+c)(#)(
    f2/*c)) by A56,A75,A77,A57,SEQ_2:38
      .=(lim((h"(#)(f1/*(h+c)-(f1/*c)))(#)(f2/*c))-lim((f1/*c)(#)(h"(#)(f2/*
    (h+c)- (f2/*c)))))/lim(f2/*(h+c)(#)(f2/*c)) by A79,A78,SEQ_2:26
      .=( lim(h"(#)(f1/*(h+c)-(f1/*c)))*lim(f2/*c) - lim((f1/*c) (#) (h"(#)(
    f2/*(h+c)-(f2/*c)))) ) / lim(f2/*(h+c)(#)(f2/*c)) by A47,A71,SEQ_2:29
      .=(Rdiff(f1,x0) * f2.x0 - Rdiff(f2,x0) * f1.x0)/(f2.x0)^2 by A43,A60,
    A59,A66,A67,A72,A76,SEQ_2:29;
    hence thesis by A56,A75,A77,A80,A57,SEQ_2:37;
  end;
  0 < r by A5,A17,XXREAL_0:15;
  hence thesis by A33,A34,Th15;
end;

theorem
  f1 is_right_differentiable_in x0 & f2 is_right_differentiable_in x0 &
  f2.x0 <> 0 implies f1/f2 is_right_differentiable_in x0 & Rdiff(f1/f2,x0) = (
  Rdiff(f1,x0)*f2.x0 - Rdiff(f2,x0)*f1.x0)/(f2.x0)^2
proof
  assume that
A1: f1 is_right_differentiable_in x0 and
A2: f2 is_right_differentiable_in x0 and
A3: f2.x0 <> 0;
  ex r st r > 0 & [.x0,x0 + r.] c= dom f2 by A2,Def3;
  then consider r1 such that
A4: r1 > 0 and
  [.x0,x0 + r1.] c= dom f2 and
A5: for g st g in [.x0,x0 + r1.] holds f2.g <> 0 by A2,A3,Th7,Th8;
  now
    take r1;
    thus r1 > 0 by A4;
    let g;
    assume that
    g in dom f2 and
A6: g in [.x0,x0+r1.];
    thus f2.g <> 0 by A5,A6;
  end;
  hence thesis by A1,A2,Lm3;
end;

Lm4: f is_right_differentiable_in x0 & (ex r0 st r0 > 0 & for g st g in dom f
& g in [.x0, x0 + r0.] holds f.g <> 0) implies f^ is_right_differentiable_in x0
& Rdiff(f^,x0) = - Rdiff(f,x0)/(f.x0)^2
proof
A1: 0 + x0 =x0;
  assume
A2: f is_right_differentiable_in x0;
  then consider r2 such that
A3: 0 < r2 and
A4: [.x0,x0 + r2.] c= dom f by Def3;
  given r0 such that
A5: r0 > 0 and
A6: for g st g in dom f & g in [.x0,x0 + r0.] holds f.g <> 0;
  set r3 = min(r0,r2);
  0 <= r3 by A5,A3,XXREAL_0:15;
  then
A7: x0 <= x0 +r3 by A1,XREAL_1:8;
  r3 <= r2 by XXREAL_0:17;
  then
A8: x0 + r3 <= x0 + r2 by XREAL_1:9;
  then x0 <= x0 + r2 by A7,XXREAL_0:2;
  then
A9: x0 in [.x0,x0 + r2.] by XXREAL_1:1;
  x0 + r3 in { g:x0 <= g & g <= x0 + r2 } by A7,A8;
  then x0 + r3 in [.x0,x0 +r2.] by RCOMP_1:def 1;
  then [.x0,x0 + r3.] c= [.x0,x0 + r2.] by A9,XXREAL_2:def 12;
  then
A10: [.x0,x0 + r3.] c= dom f by A4,XBOOLE_1:1;
  r3 <= r0 by XXREAL_0:17;
  then
A11: x0 + r3 <= x0 + r0 by XREAL_1:9;
  then x0 <= x0 +r0 by A7,XXREAL_0:2;
  then
A12: x0 in [.x0,x0 + r0.] by XXREAL_1:1;
  x0 + r3 in { g: x0 <= g & g<= x0 + r0 } by A7,A11;
  then x0 + r3 in [.x0,x0 + r0.] by RCOMP_1:def 1;
  then
A13: [.x0,x0 +r3.] c= [.x0,x0 +r0.] by A12,XXREAL_2:def 12;
A14: [.x0,x0 + r3.] c= dom(f^)
  proof
    assume not thesis;
    then consider x such that
A15: x in [.x0,x0 + r3.] and
A16: not x in dom (f^) by TARSKI:def 3;
    reconsider x as Real by A15;
A17: not x in dom f \ f"{0} by A16,RFUNCT_1:def 8;
    now
      per cases by A17,XBOOLE_0:def 5;
      suppose
        not x in dom f;
        hence contradiction by A10,A15;
      end;
      suppose
A18:    x in f"{0};
        then f.x in {0} by FUNCT_1:def 13;
        then
A19:    f.x = 0 by TARSKI:def 1;
        x in dom f by A18,FUNCT_1:def 13;
        hence contradiction by A6,A13,A15,A19;
      end;
    end;
    hence contradiction;
  end;
A20: x0 in [. x0, x0 + r3 .] by A7,XXREAL_1:1;
A21: for h,c st rng c = {x0} & rng (h+c) c= dom (f^) & (for n holds h.n > 0)
holds h"(#)((f^)/*(h+c) - (f^)/*c) is convergent & lim(h"(#)((f^)/*(h+c) - (f^)
  /*c)) = - Rdiff(f,x0)/(f.x0)^2
  proof
    let h,c;
    assume that
A22: rng c = {x0} and
A23: rng (h+c) c= dom (f^) and
A24: for n holds h.n > 0;
A25: lim h = 0 by FDIFF_1:def 1;
A26: for m holds c.m = x0
    proof
      let m;
      c.m in rng c by VALUED_0:28;
      hence thesis by A22,TARSKI:def 1;
    end;
A27: dom f \ f"{0} c= dom f by XBOOLE_1:36;
    rng (h + c) c= dom f\f"{0} by A23,RFUNCT_1:def 8;
    then
A28: rng (h + c) c= dom f by A27,XBOOLE_1:1;
    then
A29: lim(h"(#)(f/*(h+c) - f/*c)) = Rdiff(f,x0) by A2,A22,A24,Th15;
    Rdiff(f,x0) = Rdiff(f,x0);
    then
A30: h"(#)(f/*(h+c) - f/*c) is convergent by A2,A22,A24,A28,Th15;
    then
A31: -(h"(#)((f/*(h+c)) - (f/*c))) is convergent by SEQ_2:23;
    x0 in dom (f^) by A20,A14;
    then
A32: x0 in (dom f\f"{0}) by RFUNCT_1:def 8;
    rng c c= (dom f\f"{0})
    proof
      let x be set;
      assume x in rng c;
      hence thesis by A22,A32,TARSKI:def 1;
    end;
    then
A33: rng c c= dom (f^) by RFUNCT_1:def 8;
    then
A34: f/*c is non-zero by RFUNCT_2:26;
    now
      let n;
A35:  h.n <> 0 by A24;
      thus (h(#)(h"(#)(f/*(h+c) - f/*c))).n = ((h(#)h")(#)(f/*(h+c) - f/*c)).n
      by SEQ_1:22
        .= (h(#)h").n *(f/*(h+c) - f/*c).n by SEQ_1:12
        .= h.n * (h").n *(f/*(h+c) - f/*c).n by SEQ_1:12
        .= h.n * (h.n)" *(f/*(h+c) - f/*c).n by VALUED_1:10
        .= 1*(f/*(h+c) - f/*c).n by A35,XCMPLX_0:def 7
        .= (f/*(h+c) - f/*c).n;
    end;
    then
A36: h(#)(h"(#)(f/*(h+c) - f/*c)) = (f/*(h+c) - f/*c) by
    FUNCT_2:113;
A37: f/*(h+c) is non-zero by A23,RFUNCT_2:26;
    then
A38: (f/*(h+c)) (#) (f/*c) is non-zero by A34,SEQ_1:43;
    now
      let n;
      thus (f/*c + (f/*(h+c) - f/*c)).n = (f/*c).n + (f/*(h+c) - f/*c).n by
      SEQ_1:11
        .= (f/*c).n + ((f/*(h+c)).n - (f/*c).n) by RFUNCT_2:6
        .= (f/*(h+c)).n;
    end;
    then
A39: f/*c + (f/*(h+c) - f/*c) = f/*(h+c) by FUNCT_2:113;
    dom (f^) = dom f \ f"{0} by RFUNCT_1:def 8;
    then
A40: dom (f^) c= dom f by XBOOLE_1:36;
A41: for g be real number st 0 < g ex n st for m st n <=m holds abs((f/*c)
    .m - f.x0) < g
    proof
      let g be real number such that
A42:  0 < g;
      take n = 0;
      let m such that
      n <= m;
      abs((f/*c).m - f.x0) = abs(f.(c.m) - f.x0) by A33,A40,FUNCT_2:185 ,
      XBOOLE_1:1
        .=abs(f.x0 - f.x0) by A26
        .= 0 by ABSVALUE:def 1;
      hence thesis by A42;
    end;
    then
A43: f/*c is convergent by SEQ_2:def 6;
    then
A44: lim(f/*c) = f.x0 by A41,SEQ_2:def 7;
A45: h is convergent by FDIFF_1:def 1;
    then h(#)(h"(#)(f/*(h+c) - f/*c)) is convergent by A30,SEQ_2:28;
    then
A46: f/*(h+c) is convergent by A43,A36,A39,SEQ_2:19;
    lim (h(#)(h"(#)(f/*(h+c) - f/*c))) = lim h *lim (h"(#)(f/*(h+c) - f/*
    c)) by A45,A30,SEQ_2:29
      .= 0 by A25;
    then 0 = lim (f/*(h+c)) - f.x0 by A43,A44,A36,A46,SEQ_2:26;
    then
A47: lim(f/*(h+c) (#) (f/*c)) = (f.x0)^2 by A43,A44,A46,SEQ_2:29;
A48: lim(f/*(h+c) (#) (f/*c))<> 0
    proof
      assume not thesis;
      then f.x0 = 0 by A47,XCMPLX_1:6;
      hence contradiction by A6,A4,A12,A9;
    end;
    now
      let n;
A49:  (f/*(h+c)).n <>0 & (f/*c).n <>0 by A37,A34,SEQ_1:7;
      thus (h"(#) ((f^)/*(h+c) - (f^)/*c)).n = (h").n* ((f^)/*(h+c) - (f^)/*c)
      .n by SEQ_1:12
        .= (h").n* (((f^)/*(h+c)).n - ((f^)/*c).n) by RFUNCT_2:6
        .= (h").n* (((f/*(h+c))").n - ((f^)/*c).n) by A23,RFUNCT_2:27
        .= (h").n* (((f/*(h+c))").n - ((f/*c)").n) by A33,RFUNCT_2:27
        .= (h").n* (((f/*(h+c)).n)" - ((f/*c)").n) by VALUED_1:10
        .= (h").n* (((f/*(h+c)).n)" - ((f/*c).n)") by VALUED_1:10
        .= (h").n* (1/(f/*(h+c)).n - ((f/*c).n)") by XCMPLX_1:217
        .= (h").n* (1/(f/*(h+c)).n - 1/(f/*c).n) by XCMPLX_1:217
        .= (h").n* ((1*((f/*c).n) - 1*((f/*(h+c)).n))/((f/*(h+c)).n*(f/*c).n
      )) by A49,XCMPLX_1:131
        .= (h").n* ((-((f/*(h+c)).n - (f/*c).n))/((f/*(h+c))(#)(f/*c)).n) by
      SEQ_1:12
        .= (h").n* ((-((f/*(h+c) - (f/*c)).n))/((f/*(h+c))(#)(f/*c)).n) by
      RFUNCT_2:6
        .= (h").n*(-(((f/*(h+c) - (f/*c)).n)/((f/*(h+c))(#)(f/*c)).n)) by
      XCMPLX_1:188
        .= -((h").n*(((f/*(h+c) - (f/*c)).n)/(((f/*(h+c))(#)(f/*c)).n)))
        .= -((h").n*((f/*(h+c) - (f/*c)).n))/((f/*(h+c))(#)(f/*c)).n by
      XCMPLX_1:75
        .= -( ((h" (#) ((f/*(h+c)) - (f/*c))).n) )/((f/*(h+c))(#)(f/*c)).n
      by SEQ_1:12
        .= -(((h"(#)((f/*(h+c)) - (f/*c))).n))*(((f/*(h+c))(#) (f/*c)).n)"
      by XCMPLX_0:def 9
        .= -((((h"(#)((f/*(h+c)) - (f/*c))).n))*(((f/*(h+c))(#) (f/*c))").n)
      by VALUED_1:10
        .= -(((h")(#)((f/*(h+c)) - (f/*c)))/"((f/*(h+c))(#)(f/*c))).n by
      SEQ_1:12
        .= (-((h"(#)((f/*(h+c)) - (f/*c)))/"((f/*(h+c))(#)(f/*c)))).n by
      SEQ_1:14
        .= ((-(h"(#)((f/*(h+c)) - (f/*c))))/"((f/*(h+c))(#)(f/*c))).n by
      SEQ_1:56;
    end;
    then
A50: h"(#) ((f^)/*(h+c) - (f^)/*c) = (-(h"(#)((f/*(h+c)) - (f/*c))))/"((f
    /*(h+c ))(#) (f/*c)) by FUNCT_2:113;
A51: f/*(h+c) (#) (f/*c) is convergent by A43,A46,SEQ_2:28;
    then
    lim(h"(#) ((f^)/*(h+c) - (f^)/*c)) = lim(-(h"(#) ((f/*(h+c))- (f/*c))
    ))/(f.x0)^2 by A38,A47,A48,A31,A50,SEQ_2:38
      .=(- Rdiff(f,x0)) /(f.x0)^2 by A30,A29,SEQ_2:24
      .=- Rdiff(f,x0) /(f.x0)^2 by XCMPLX_1:188;
    hence thesis by A38,A51,A48,A31,A50,SEQ_2:37;
  end;
  0 < r3 by A5,A3,XXREAL_0:15;
  hence thesis by A14,A21,Th15;
end;

theorem
  f is_right_differentiable_in x0 & f.x0 <> 0 implies f^
  is_right_differentiable_in x0 & Rdiff(f^,x0) = - Rdiff(f,x0)/(f.x0)^2
proof
  assume that
A1: f is_right_differentiable_in x0 and
A2: f.x0 <> 0;
  ex r st r > 0 & [.x0,x0 + r.] c= dom f by A1,Def3;
  then consider r1 such that
A3: r1 > 0 and
  [.x0,x0 + r1.] c= dom f and
A4: for g st g in [.x0,x0 + r1.] holds f.g <> 0 by A1,A2,Th7,Th8;
  now
    take r1;
    thus r1 > 0 by A3;
    let g;
    assume that
    g in dom f and
A5: g in [.x0,x0+r1.];
    thus f.g <> 0 by A4,A5;
  end;
  hence thesis by A1,Lm4;
end;

theorem
  f is_right_differentiable_in x0 & f is_left_differentiable_in x0 &
Rdiff(f,x0) = Ldiff(f,x0) implies f is_differentiable_in x0 & diff(f,x0)=Rdiff(
  f,x0) & diff(f,x0)=Ldiff(f,x0)
proof
  assume that
A1: f is_right_differentiable_in x0 and
A2: f is_left_differentiable_in x0 and
A3: Rdiff(f,x0) = Ldiff(f,x0);
A4: ex N being Neighbourhood of x0 st N c= dom f
  proof
    consider r2 such that
A5: r2>0 and
A6: [.x0,x0+r2.] c= dom f by A1,Def3;
    consider r1 such that
A7: r1>0 and
A8: [.x0-r1,x0.] c= dom f by A2,Def4;
    set r = min(r1,r2);
    r > 0 by A7,A5,XXREAL_0:15;
    then reconsider N = ].x0 -r,x0+r.[ as Neighbourhood of x0 by
    RCOMP_1:def 7;
    take N;
    let x;
    assume x in N;
    then x in { g: x0 - r < g & g < x0 + r} by RCOMP_1:def 2;
    then consider g such that
A9: g = x and
A10: x0 - r < g and
A11: g < x0 + r;
    now
      per cases;
      suppose
A12:    g <= x0;
        r <= r1 by XXREAL_0:17;
        then x0 - r1 <= x0 - r by XREAL_1:15;
        then x0 - r1 <= g by A10,XXREAL_0:2;
        then g in {g1: x0 - r1 <= g1 & g1 <= x0} by A12;
        then g in [.x0-r1,x0.] by RCOMP_1:def 1;
        hence thesis by A8,A9;
      end;
      suppose
A13:    g > x0;
        r <= r2 by XXREAL_0:17;
        then x0 + r <= x0 + r2 by XREAL_1:9;
        then g <= x0 + r2 by A11,XXREAL_0:2;
        then g in {g1: x0 <= g1 & g1 <= x0+r2} by A13;
        then g in [.x0,x0+r2.] by RCOMP_1:def 1;
        hence thesis by A6,A9;
      end;
    end;
    hence thesis;
  end;
  for h,c st rng c = {x0} & rng (h + c) c= dom f holds h"(#)(f/*(h+c) - f
  /*c) is convergent & lim (h"(#) (f/*(h+c) - f/*c)) = Ldiff(f,x0)
  proof
    let h,c;
    assume that
A14: rng c = {x0} and
A15: rng (h + c) c= dom f;
A16: rng c c= dom f
    proof
      consider S being Neighbourhood of x0 such that
A17:  S c= dom f by A4;
      x0 in S by RCOMP_1:37;
      hence thesis by A14,A17,ZFMISC_1:37;
    end;
    now
      per cases;
      suppose
        ex N be Element of NAT st for n st n >= N holds h.n > 0;
        then consider N be Element of NAT such that
A18:    for n st n >= N holds h.n > 0;
        set h1 = h ^\N;
A19:    for n holds h1.n > 0
        proof
          let n;
          0 <= n by NAT_1:2;
          then h1.n = h.(n+N) & N + 0 <= n + N by NAT_1:def 3,XREAL_1:9;
          hence thesis by A18;
        end;
        set c1 = c ^\N;
A20:    rng c1 = {x0}
        proof
          thus rng c1 c= {x0} by A14,VALUED_0:21;
          let x;
A21:      c.N in rng c by VALUED_0:28;
          assume x in {x0};
          then
A22:      x = x0 by TARSKI:def 1;
          c1.0 = c.(0+N) by NAT_1:def 3
            .= c.N;
          then c1.0 = x by A14,A22,A21,TARSKI:def 1;
          hence thesis by VALUED_0:28;
        end;
A23:    h1 + c1 = (h + c)^\N by SEQM_3:37;
        then
A24:    h1"(#)(f/*(h1+c1) - f/*c1) = h1"(#)((f/*(h+c))^\N - f/*c1) by A15
        ,VALUED_0:27
          .= h1"(#)((f/*(h+c))^\N - (f/*c)^\N) by A16,VALUED_0:27
          .= h1"(#)((f/*(h+c) - f/*c)^\N) by SEQM_3:39
          .= ((h")^\N) (#)((f/*(h+c) - f/*c)^\N) by SEQM_3:41
          .= (h"(#)(f/*(h+c) - f/*c))^\N by SEQM_3:42;
        rng (h1 + c1) c= rng (h + c) by A23,VALUED_0:21;
        then
A25:    rng (h1 + c1) c= dom f by A15,XBOOLE_1:1;
        then
A26:    h1"(#)(f/*(h1+c1) - f/*c1) is convergent by A1,A3,A20,A19,
        Th15;
        hence h"(#)(f/*(h+c) - f/*c) is convergent by A24,SEQ_4:35;
        lim (h1"(#)(f/*(h1+c1) - f/*c1)) = Rdiff(f,x0) by A1,A20,A25,A19,
        Th15;
        hence lim (h"(#)(f/*(h+c) - f/*c)) =Ldiff(f,x0) by A3,A26,A24,
        SEQ_4:36;
      end;
      suppose
A27:    for N be Element of NAT ex n st n >= N & h.n <= 0;
        now
          per cases;
          suppose
            ex M be Element of NAT st for m st m >= M holds h.m < 0;
            then consider M be Element of NAT such that
A28:        for n st n >= M holds h.n < 0;
            set h1 = h ^\M;
A29:        for n holds h1.n < 0
            proof
              let n;
              0 <= n by NAT_1:2;
              then h1.n = h.(n+M) & M + 0 <= n + M by
              NAT_1:def 3,XREAL_1:
              9;
              hence thesis by A28;
            end;
            set c1 = c ^\M;
A30:        rng c1 = {x0}
            proof
              thus rng c1 c= {x0} by A14,VALUED_0:21;
              let x;
A31:          c.M in rng c by VALUED_0:28;
              assume x in {x0};
              then
A32:          x = x0 by TARSKI:def 1;
              c1.0 = c.(0+M) by NAT_1:def 3
                .= c.M;
              then c1.0 = x by A14,A32,A31,TARSKI:def 1;
              hence thesis by VALUED_0:28;
            end;
A33:        h1 + c1 = (h + c)^\M by SEQM_3:37;
            then
A34:        h1"(#)(f/*(h1+c1) - f/*c1) = h1"(#)((f/*(h+c))^\M - f/*c1) by
            A15,VALUED_0:27
              .= h1"(#)((f/*(h+c))^\M - (f/*c)^\M) by A16,VALUED_0:27
              .= h1"(#)((f/*(h+c) - f/*c)^\M) by SEQM_3:39
              .= ((h")^\M) (#)((f/*(h+c) - f/*c)^\M) by SEQM_3:41
              .= (h"(#)(f/*(h+c) - f/*c))^\M by SEQM_3:42;
            rng (h1 + c1) c= rng (h + c) by A33,VALUED_0:21;
            then
A35:        rng (h1 + c1) c= dom f by A15,XBOOLE_1:1;
            then
A36:        h1"(#)(f/*(h1+c1) - f/*c1) is convergent by A2,A3,A30,
            A29
            ,Th9;
            hence h"(#)(f/*(h+c) - f/*c) is convergent by A34,SEQ_4:35;
            lim (h1"(#)(f/*(h1+c1) - f/*c1)) = Ldiff(f,x0) by A2,A30,A35,
            A29,Th9;
            hence lim (h"(#)(f/*(h+c) - f/*c)) =Ldiff(f,x0) by A36,A34,
            SEQ_4:36;
          end;
          suppose
A37:        for M be Element of NAT ex m st m >= M & h.m >= 0;
            set s = (h")(#)(f/*(h+c)-f/*c);
            defpred R[real number] means $1 > 0;
            defpred P[real number] means $1 < 0;
A38:        h is non-zero by FDIFF_1:def 1;
A39:        for N be Element of NAT ex n st n >= N & P[h.n]
            proof
              let m;
              consider n such that
A40:          n >= m and
A41:          h.n <= 0 by A27;
              take n;
              thus n >= m by A40;
              h.n <> 0 by A38,SEQ_1:7;
              hence thesis by A41;
            end;
            consider q1 being increasing sequence of NAT such that
A42:        (for n holds P[(h*q1).n]) & for n st (for r st r = h.n
            holds P[r]) ex m st n = q1.m from FDIFF_2:sch 1(A39);
A43:        for N be Element of NAT ex n st n >= N & R[h.n]
            proof
              let m;
              consider n such that
A44:          n >= m and
A45:          h.n >= 0 by A37;
              take n;
              thus n >= m by A44;
              h.n <> 0 by A38,SEQ_1:7;
              hence thesis by A45;
            end;
            consider q2 being increasing sequence of NAT such that
A46:        (for n holds R[(h*q2).n]) & for n st (for r st r = h.n
            holds R[r]) ex m st n = q2.m from FDIFF_2:sch 1(A43);
            set h1 = h*q1;
            reconsider h1 as subsequence of h;
A47:        h is convergent by FDIFF_1:def 1;
            then
A48:        h1 is convergent by SEQ_4:29;
A49:        lim h = 0 by FDIFF_1:def 1;
            then
A50:        lim h1 = 0 by A47,SEQ_4:30;
            set h2 = h*q2;
A51:        h2 is convergent by A47,SEQ_4:29;
            lim h2 = 0 by A47,A49,SEQ_4:30;
            then reconsider h2 as convergent_to_0 Real_Sequence by A38,A51,
            FDIFF_1:def 1;
            set c2 = c*q2;
A52:        rng c2 = {x0} by A14,VALUED_0:26;
            reconsider c2 as constant Real_Sequence;
            rng ((h+c)*q2) c= rng (h+c) by VALUED_0:21;
            then rng ((h+c)*q2) c= dom f by A15,XBOOLE_1:1;
            then rng (h2+c2) c= dom f by RFUNCT_2:13;
            then
A53:        h2"(#)(f/*(h2+c2)-f/*c2) is convergent & lim(h2"(#)(f/*(h2+
            c2)-f/*c2))=Ldiff (f,x0) by A1,A3,A46,A52,Th15;
A54:        h2"(#)(f/*(h2+c2)-f/*c2) = h2"(#)(f/*((h+c)*q2)-f/*c2) by
            RFUNCT_2:13
              .= h2"(#)(f/*(h+c)*q2-f/*c2) by A15,FUNCT_2:187
              .= ((h")*q2)(#)(f/*(h+c)*q2-f/*c2) by RFUNCT_2:16
              .= ((h")*q2)(#)(f/*(h+c)*q2-f/*c*q2) by A16,FUNCT_2:187
              .= ((h")*q2)(#)((f/*(h+c)-f/*c)*q2) by RFUNCT_2:13
              .= ((h")(#)(f/*(h+c)-f/*c))*q2 by RFUNCT_2:13;
            reconsider h1 as convergent_to_0 Real_Sequence by A38,A48,A50,
            FDIFF_1:def 1;
            set c1 = c*q1;
A55:        rng c1 = {x0} by A14,VALUED_0:26;
            reconsider c1 as constant Real_Sequence;
            rng ((h+c)*q1) c= rng (h+c) by VALUED_0:21;
            then rng ((h+c)*q1) c= dom f by A15,XBOOLE_1:1;
            then rng (h1+c1) c= dom f by RFUNCT_2:13;
            then
A56:        h1"(#)(f/*(h1+c1)-f/*c1) is convergent & lim(h1"(#)(f/*(h1+
            c1)-f/*c1))=Ldiff (f,x0) by A2,A3,A42,A55,Th9;
A57:        h1"(#)(f/*(h1+c1)-f/*c1) = h1"(#)(f/*((h+c)*q1)-f/*c1) by
            RFUNCT_2:13
              .= h1"(#)(f/*(h+c)*q1-f/*c1) by A15,FUNCT_2:187
              .= ((h")*q1)(#)(f/*(h+c)*q1-f/*c1) by RFUNCT_2:16
              .= ((h")*q1)(#)(f/*(h+c)*q1-f/*c*q1) by A16,FUNCT_2:187
              .= ((h")*q1)(#)((f/*(h+c)-f/*c)*q1) by RFUNCT_2:13
              .= ((h")(#)(f/*(h+c)-f/*c))*q1 by RFUNCT_2:13;
A58:        for g1 be real number st 0<g1 ex n st for m st n<=m holds
            abs(s.m - Ldiff(f,x0))<g1
            proof
              let g1 be real number;
              assume
A59:          0 < g1;
              then consider n1 be Element of NAT such that
A60:          for m st n1 <= m holds abs((s*q1).m - Ldiff(f,x0))<g1
              by A56,A57,SEQ_2:def 7;
              consider n2 be Element of NAT such that
A61:          for m st n2 <= m holds abs((s*q2).m - Ldiff(f,x0))<g1
              by A53,A54,A59,SEQ_2:def 7;
              take n = max(q1.n1,q2.n2);
              let m such that
A62:          n <= m;
A63:          n >= q2.n2 by XXREAL_0:25;
A64:          n >= q1.n1 by XXREAL_0:25;
              now
                per cases;
                suppose
                  h.m > 0;
                  then for r st r = h.m holds r > 0;
                  then consider k such that
A65:              m = q2.k by A46;
                  dom q2 = NAT & q2.k >= q2.n2 by A62,A63,A65,FUNCT_2
                  :def 1,XXREAL_0:2;
                  then not k < n2 by VALUED_0:def 13;
                  then abs((s*q2).k - Ldiff(f,x0))<g1 by A61;
                  hence thesis by A65,FUNCT_2:21;
                end;
                suppose
A66:              h.m <= 0;
                  h.m <> 0 by A38,SEQ_1:7;
                  then for r st r = h.m holds r < 0 by A66;
                  then consider k such that
A67:              m = q1.k by A42;
                  dom q1 = NAT & q1.k >= q1.n1 by A62,A64,A67,FUNCT_2
                  :def 1,XXREAL_0:2;
                  then not k < n1 by VALUED_0:def 13;
                  then abs((s*q1).k - Ldiff(f,x0))<g1 by A60;
                  hence thesis by A67,FUNCT_2:21;
                end;
              end;
              hence thesis;
            end;
            hence s is convergent by SEQ_2:def 6;
            hence lim s = Ldiff(f,x0) by A58,SEQ_2:def 7;
          end;
        end;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
  hence thesis by A3,A4,FDIFF_2:12;
end;

theorem
  f is_differentiable_in x0 implies f is_right_differentiable_in x0 & f
is_left_differentiable_in x0 & diff(f,x0) = Rdiff(f,x0) & diff(f,x0) = Ldiff(f,
  x0)
proof
  assume
A1: f is_differentiable_in x0;
  then consider N be Neighbourhood of x0 such that
A2: N c= dom f by FDIFF_2:11;
  consider g1 be real number such that
A3: g1 > 0 and
A4: N = ].x0 - g1,x0 + g1.[ by RCOMP_1:def 7;
A5: g1 > g1/2 by A3,XREAL_1:218;
A6: ex r st r>0 & [.x0,x0+r.] c= dom f
  proof
    take r = g1/2;
    thus r > 0 by A3,XREAL_1:217;
    abs((x0 + g1/2) -x0 ) = g1/2 by A3,ABSVALUE:def 1;
    then
A7: x0 + r in ].x0 -g1,x0 +g1.[ by A5,RCOMP_1:8;
    x0 in ].x0 - g1, x0 + g1 .[ by A4,RCOMP_1:37;
    then [.x0,x0 +r.] c= ].x0 -g1,x0 +g1.[ by A7,XXREAL_2:def 12;
    hence thesis by A2,A4,XBOOLE_1:1;
  end;
A8: ex r st 0 < r & [.x0 -r,x0.] c= dom f
  proof
    take r = g1/2;
    thus r > 0 by A3,XREAL_1:217;
    abs((x0 - g1/2) -x0 ) =abs(-g1/2 + 0 ) .=abs(g1/2 )by COMPLEX1:138
      .=g1/2 by A3,ABSVALUE:def 1;
    then
A9: x0 - r in ].x0 -g1,x0 +g1.[ by A5,RCOMP_1:8;
    x0 in ].x0 - g1, x0 + g1 .[ by A4,RCOMP_1:37;
    then [.x0 -r,x0 .] c= ].x0 -g1,x0 +g1.[ by A9,XXREAL_2:def 12;
    hence thesis by A2,A4,XBOOLE_1:1;
  end;
  diff(f,x0) = diff(f,x0);
  then ( for h,c st rng c = {x0} & rng (h+c) c= dom f & (for n holds h.n>0)
  holds h" (#)(f/*(h+c) - f/*c) is convergent & lim (h"(#) (f/*(h+c) - f/*c)) =
diff(f,x0) )& for h,c st rng c = {x0} & rng (h+c) c= dom f & (for n holds h.n <
  0) holds h"(#)(f/*(h+c) - f/*c) is convergent & lim(h"(#)(f/*(h+c) - f/*c)) =
  diff(f,x0) by A1,FDIFF_2:12;
  hence thesis by A6,A8,Th9,Th15;
end;
