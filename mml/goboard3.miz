:: Properties of Go-Board - Part III
::  by Jaros{\l}aw Kotowicz and Yatsuka Nakamura
::
:: Received August 24, 1992
:: Copyright (c) 1992 Association of Mizar Users

environ

 vocabulary PRE_TOPC, EUCLID, FINSEQ_1, GOBOARD1, RELAT_1, TOPREAL1, FINSEQ_4,
      BOOLE, MATRIX_1, FUNCT_1, ABSVALUE, ARYTM_1, MCART_1, TREES_1, INCSP_1,
      SEQM_3, ORDINAL2, TARSKI;
 notation TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, XCMPLX_0, XREAL_0, NAT_1,
      RELAT_1, FUNCT_1, FINSEQ_1, PRE_TOPC, ABSVALUE, FINSEQ_4, MATRIX_1,
      EUCLID, TOPREAL1, GOBOARD1;
 constructors SEQM_3, NAT_1, ABSVALUE, TOPREAL1, GOBOARD1, FINSEQ_4, INT_1,
      MEMBERED, REAL_1, XBOOLE_0;
 clusters RELSET_1, STRUCT_0, EUCLID, INT_1, XREAL_0, MEMBERED, ZFMISC_1,
      XBOOLE_0, NUMBERS, ORDINAL2;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, TOPREAL1, XBOOLE_0;
 theorems AXIOMS, TARSKI, REAL_1, NAT_1, ZFMISC_1, FUNCT_1, FINSEQ_1, ABSVALUE,
      SQUARE_1, FINSEQ_3, REAL_2, MATRIX_1, EUCLID, TOPREAL1, TOPREAL3,
      GOBOARD1, GOBOARD2, FINSEQ_4, PROB_1, GROUP_5, PARTFUN2, INT_1, XBOOLE_0,
      XBOOLE_1, XCMPLX_1;
 schemes NAT_1, GOBOARD1, GOBOARD2;

begin

 reserve p,p1,p2,q for Point of TOP-REAL 2,
         f,g,g1,g2 for FinSequence of TOP-REAL 2,
         n,m,i,j,k for Nat,
         G for Go-board,
         x for set;

Lm1:
now let f,k;
    assume A1: len f=k+1;
    assume k<>0;
    then A2: 0<k & k<=k+1 by NAT_1:19,29;
    then 0+1<=k & k<=len f & k+1 <= len f by A1,NAT_1:38;
then A3: k in dom f by FINSEQ_3:27;
A4: len (f|k)=k by A1,A2,FINSEQ_1:80;
A5: dom(f|k)=Seg len(f|k) by FINSEQ_1:def 3;
   assume A6:f is unfolded;
  thus f|k is unfolded
   proof set f1 = f|k;
   let n; assume
A7: 1<=n & n+2 <= len f1;
    then n in dom f1 & n+1 in dom f1 & n+2 in dom f1 & n+1+1=n+(1+1)
       by GOBOARD2:4;
then A8: LSeg(f1,n)=LSeg(f,n) & LSeg(f1,n+1)=LSeg(f,n+1) &
     f1/.(n+1)=f/.(n+1) by A3,A4,A5,GOBOARD1:10,TOPREAL3:24;
     len f1 <= len f by A1,A2,FINSEQ_1:80;
  then n+2 <= len f by A7,AXIOMS:22;
  hence LSeg(f1,n) /\ LSeg(f1,n+1)={f1/.(n+1)} by A6,A7,A8,TOPREAL1:def 8;
 end;
end;

theorem Th1:
(for n st n in dom f ex i,j st [i,j] in Indices G & f/.n=G*(i,j)) &
 f is one-to-one unfolded s.n.c. special
implies
ex g st g is_sequence_on G & g is one-to-one unfolded s.n.c. special &
 L~f = L~g & f/.1 = g/.1 & f/.len f = g/.len g & len f <= len g
proof
defpred P[Nat] means for f st len f = $1 &
 (for n st n in dom f ex i,j st [i,j] in Indices G & f/.n=G*(i,j)) &
  f is one-to-one unfolded s.n.c. special
 ex g st g is_sequence_on G &
  g is one-to-one unfolded s.n.c. special &
  L~f=L~g & f/.1=g/.1 & f/.len f=g/.len g & len f<=len g;
A1: P[0]
 proof let f such that
  A2: len f=0 &
  (for n st n in dom f ex i,j st [i,j] in Indices G & f/.n=G*(i,j)) &
  f is one-to-one unfolded s.n.c. special;
  take g=f;
     f= {} by A2,FINSEQ_1:25;
   then for n holds n in dom g & n+1 in dom g implies (
    for m,k,i,j st [m,k] in Indices G & [i,j] in Indices G & g/.n=G*(m,k) &
       g/.(n+1)=G*(i,j) holds abs(m-i)+abs(k-j)=1) by FINSEQ_1:26;
  hence thesis by A2,GOBOARD1:def 11;
 end;
A3: for k st P[k] holds P[k+1]
 proof let k such that
  A4: P[k];
  let f such that
A5: len f=k+1 and
A6: for n st n in dom f ex i,j st [i,j] in Indices G & f/.n=G*(i,j) and
A7: f is one-to-one and
A8: f is unfolded and
A9: f is s.n.c. and
A10: f is special;
   per cases;
   suppose k=0;
    then A11: dom f = {1} by A5,FINSEQ_1:4,def 3;
    take g=f;
        now let n; assume
         n in dom g & n+1 in dom g;
      then n=1 & n+1=1 by A11,TARSKI:def 1;
      hence for i1,i2,j1,j2 be Nat st [i1,i2] in Indices G &
      [j1,j2] in Indices G &
       g/.n=G*(i1,i2) & g/.(n+1)=G*(j1,j2) holds abs(i1-j1)+abs(i2-j2)=1;
     end;
    hence g is_sequence_on G by A6,GOBOARD1:def 11;
    thus g is one-to-one unfolded s.n.c. special &
     L~f=L~g & f/.1=g/.1 & f/.len f=g/.len g & len f<=len g by A7,A8,A9,A10;
   end;
   suppose A12: k<>0;
    then A13: 0<k & k<=k+1 by NAT_1:19,29;
    then A14: 0+1<=k & k<=len f & k+1 <= len f by A5,NAT_1:38;
then A15: k in dom f by FINSEQ_3:27;
A16: len (f|k)=k by A5,A13,FINSEQ_1:80;
A17: dom(f|k)=Seg len(f|k) by FINSEQ_1:def 3;
A18: k in Seg k by A14,FINSEQ_1:3;
A19: 1 in Seg k by A14,FINSEQ_1:3;
    set f1=f|k;
    A20: now let n; assume A21: n in dom f1;
       then n in dom f by A15,A16,A17,GOBOARD1:10;
      then consider i,j such that
      A22: [i,j] in Indices G & f/.n=G*(i,j) by A6;
      take i,j;
      thus [i,j] in Indices G & f1/.n=G*(i,j)
       by A15,A16,A17,A21,A22,GOBOARD1:10;
     end;
       f1 = f|Seg k by FINSEQ_1:def 15;
    then A23: f1 is one-to-one by A7,FUNCT_1:84;
    A24: f1 is unfolded by A5,A8,A12,Lm1;
    A25: f1 is s.n.c. by A9,GOBOARD2:12;
       f1 is special
     proof let n; assume
      A26: 1<=n & n+1 <= len f1;
      then n in dom f1 & n+1 in dom f1 by GOBOARD2:3;
      then A27: f1/.n=f/.n & f1/.(n+1)=f/.(n+1) by A15,A16,A17,GOBOARD1:10;
         len f1 <=len f by A5,A13,FINSEQ_1:80;
      then n+1 <= len f by A26,AXIOMS:22;
      hence (f1/.n)`1=(f1/.(n+1))`1 or (f1/.n)`2=(f1/.(n+1))`2
       by A10,A26,A27,TOPREAL1:def 7;
     end;
    then consider g1 such that
A28: g1 is_sequence_on G and
A29: g1 is one-to-one and
A30: g1 is unfolded and
A31: g1 is s.n.c. and
A32: g1 is special and
A33: L~g1=L~f1 and
A34: g1/.1=f1/.1 and
A35: g1/.len g1=f1/.len f1 and
A36: len f1<=len g1 by A4,A16,A20,A23,A24,A25;
    A37: k=1 implies L~g1={} & rng g1 = {f/.k}
     proof assume A38: k=1;
      hence L~g1={} by A16,A33,TOPREAL1:28;
then A39:  (len g1=1 or len g1=0) & 1<=len g1
           by A5,A36,A38,FINSEQ_1:80,TOPREAL1:28;
then A40:  len g1 in dom g1 by FINSEQ_3:27;
      A41: g1/.len g1=f/.k by A15,A16,A18,A35,GOBOARD1:10;
      then f/.k in rng g1 by A40,PARTFUN2:4;
      then A42: {f/.k} c= rng g1 by ZFMISC_1:37;
         rng g1 c= {f/.k}
       proof let x; assume x in rng g1; then consider n such that
        A43: n in dom g1 & g1/.n=x by PARTFUN2:4;
           n in Seg len g1 by A43,FINSEQ_1:def 3;
        then n=len g1 by A39,FINSEQ_1:4,TARSKI:def 1;
        hence x in {f/.k} by A41,A43,TARSKI:def 1;
       end;
      hence rng g1={f/.k} by A42,XBOOLE_0:def 10;
     end;
    A44: 1<k implies rng g1 c= L~f1
     proof assume 1<k; then 1+1<=k by NAT_1:38;
      then A45: 2<=len g1 by A16,A36,AXIOMS:22;
      let x; assume x in rng g1;
      then consider n such that
      A46: n in dom g1 & g1/.n=x by PARTFUN2:4;
      thus x in L~f1 by A33,A45,A46,GOBOARD1:16;
     end;
    consider i1,i2 be Nat such that
    A47: [i1,i2] in Indices G & f/.k=G*(i1,i2) by A6,A15;
       1<=len f by A14,AXIOMS:22;
    then A48: k+1 in dom f by A5,FINSEQ_3:27;
    then consider j1,j2 be Nat such that
    A49: [j1,j2] in Indices G & f/.(k+1)=G*(j1,j2) by A6;
    A50: Indices G = [:dom G,Seg width G:] by MATRIX_1:def 5;
    then A51: i1 in dom G & i2 in Seg width G &
        j1 in dom G & j2 in Seg width G by A47,A49,ZFMISC_1:106;
    A52:dom G = Seg len G by FINSEQ_1:def 3;
    A53: (for n st n in dom g1 ex m,k st [m,k] in Indices G &
    g1/.n=G*(m,k)) &
    for n st n in dom g1 & n+1 in dom g1 holds
     for m,k,i,j st [m,k] in Indices G &
     [i,j] in Indices G & g1/.n = G*(m,k) &
      g1/.(n+1) = G*(i,j) holds abs(m-i)+abs(k-j) = 1 by A28,GOBOARD1:def 11;
    reconsider l1 = Line(G,i1), c1 = Col(G,i2) as FinSequence of TOP-REAL 2;
    set x1 = X_axis(l1), y1 = Y_axis(l1),
        x2 = X_axis(c1), y2 = Y_axis(c1);
A54:  x1 is constant by A51,GOBOARD1:def 6;
A55:  y1 is increasing by A51,GOBOARD1:def 8;
A56:  x2 is increasing by A51,GOBOARD1:def 9;
A57:  y2 is constant by A51,GOBOARD1:def 7;
A58: dom x1=Seg len x1 by FINSEQ_1:def 3;
A59: dom y1=Seg len y1 by FINSEQ_1:def 3;
A60: len y1=len l1 by GOBOARD1:def 4;
A61: len x1=len l1 by GOBOARD1:def 3;
A62: len l1=width G by MATRIX_1:def 8;
then A63:Seg width G = dom l1 by FINSEQ_1:def 3;
A64: dom x2= Seg len x2 by FINSEQ_1:def 3;
A65: dom y2=Seg len y2 by FINSEQ_1:def 3;
A66: len x2=len c1 by GOBOARD1:def 3;
then A67: dom c1 = Seg len x2 by FINSEQ_1:def 3
            .= dom x2 by FINSEQ_1:def 3;
A68: len y2=len c1 by GOBOARD1:def 4;
A69: len c1 = len G by MATRIX_1:def 9;
then A70: dom c1 = Seg len G by FINSEQ_1:def 3
           .= dom G by FINSEQ_1:def 3;
        now per cases by A10,A15,A47,A48,A49,GOBOARD2:16;
     suppose A71: i1=j1;
      set ppi = G*(i1,i2), pj = G*(i1,j2);
          now per cases by AXIOMS:21;
       case A72: i2>j2;
        then reconsider l=i2-j2 as Nat by INT_1:18;
        A73: now let n; assume n in Seg l;
          then A74: 1<=n & n<=l & 0<=j2 by FINSEQ_1:3,NAT_1:18;
          then l<=i2 by PROB_1:2;
          then n<=i2 by A74,AXIOMS:22;
          then reconsider w=i2-n as Nat by INT_1:18;
             0<=n & i2-l<=i2-n by A74,AXIOMS:22,REAL_1:92;
          then i2-n<=i2 & i2<=width G & j2<=w & 1<=j2
            by A51,FINSEQ_1:3,PROB_1:2;
          then 1<=w & w<=width G by AXIOMS:22;
          then w in Seg width G by FINSEQ_1:3;
          hence i2-n is Nat & [i1,i2-n] in Indices G & i2-n in Seg width G
          by A50,A51,ZFMISC_1:106;
         end;
        defpred P1[Nat,set] means for m st m=i2-$1 holds $2=G*(i1,m);
        A75: now let n; assume n in Seg l;
          then reconsider m=i2-n as Nat by A73;
          take p=G*(i1,m);
          thus P1[n,p];
         end;
        consider g2 such that
        A76: len g2=l & for n st n in Seg l holds P1[n,g2/.n]
         from GOBOARD1:sch 1(A75);
A77:    dom g2 = Seg l by A76,FINSEQ_1:def 3;
        take g=g1^g2;
            now let n; assume A78: n in dom g2;
          then reconsider m=i2-n as Nat by A73,A77;
          take k=i1,m;
          thus [k,m] in Indices G & g2/.n=G*(k,m) by A73,A76,A77,A78;
         end;
        then A79: for n st n in dom g ex i,j st [i,j] in Indices G & g/.n=G*
             (i,j) by A53,GOBOARD1:39;
        A80: now let n; assume
A81:      n in dom g2 & n+1 in dom g2;
          then A82: [i1,i2-n] in Indices G
           & [i1,i2-(n+1)] in Indices G by A73,A77;
          reconsider m1=i2-n ,m2=i2-(n+1) as Nat by A73,A77,A81;
          A83: g2/.n=G*(i1,m1) & g2/.(n+1)=G*(i1,m2) by A76,A77,A81;
          let l1,l2,l3,l4 be Nat; assume
             [l1,l2] in Indices G & [l3,l4] in Indices G & g2/.n=G*(l1,l2) &
           g2/.(n+1)=G*(l3,l4);
           then l1=i1 & l2=m1 & l3=i1 & l4=m2 by A82,A83,GOBOARD1:21;
          hence abs(l1-l3)+abs(l2-l4)= 0+abs(i2-n-(i2-(n+1))) by ABSVALUE:7
           .= 1 by ABSVALUE:def 1;
         end;
         now let l1,l2,l3,l4 be Nat; assume
          A85: [l1,l2] in Indices G & [l3,l4] in Indices G &
          g1/.len g1=G*(l1,l2) &
          g2/.1=G*(l3,l4) & len g1 in dom g1 & 1 in dom g2;
          then A86: [i1,i2-1] in Indices G &
          f1/.len f1=f/.k by A15,A16,A18,A73,A77,GOBOARD1:10;
          reconsider m1=i2-1 as Nat by A73,A77,A85;
             g2/.1=G*(i1,m1) by A76,A77,A85;
           then l1=i1 & l2=i2 & l3=i1 & l4=m1
            by A35,A47,A85,A86,GOBOARD1:21;
          hence abs(l1-l3)+abs(l2-l4)=0+abs(i2-(i2-1)) by ABSVALUE:7
          .=1 by ABSVALUE:def 1;
         end;
        then for n st n in dom g & n+1 in dom g holds
         for m,k,i,j st [m,k] in Indices G & [i,j] in Indices G &
         g/.n=G*(m,k) &
          g/.(n+1)=G*(i,j) holds abs(m-i)+abs(k-j)=1 by A53,A80,GOBOARD1:40;
        hence g is_sequence_on G by A79,GOBOARD1:def 11;
     set lk={w where w is Point of TOP-REAL 2: w`1=ppi`1 & pj`2<=w`2 & w`2<=
ppi`2};
           l1/.i2=l1.i2 & l1/.j2 = l1.j2 by A51,A63,FINSEQ_4:def 4;
        then A87: l1/.i2=ppi & l1/.j2=pj by A51,MATRIX_1:def 8;
        then A88: y1.i2=ppi`2 & y1.j2=pj`2 & x1.i2=ppi`1 & x1.j2=pj`1 & l=len
g2
          by A51,A58,A59,A60,A61,A62,A76,GOBOARD1:def 3,def 4;
   then A89: pj`2<ppi`2 & ppi`1=pj`1 & ppi=|[ppi`1,ppi`2]| & pj=|[pj`1,pj`2]|
        by A51,A54,A55,A58,A59,A60,A61,A62,A72,EUCLID:57,GOBOARD1:def 1,def 2;
        A90: LSeg(f,k)=LSeg(pj,ppi) by A14,A47,A49,A71,TOPREAL1:def 5
        .= lk by A89,TOPREAL3:15;
        A91: LSeg(f,k)=LSeg(ppi,pj) by A14,A47,A49,A71,TOPREAL1:def 5;
            now let n,m; assume
          A92: n in dom g2 & m in dom g2 & n<>m;
          then A93: [i1,i2-n] in Indices G& [i1,i2-m] in Indices G by A73,A77;
          reconsider n1=i2-n, m1=i2-m as Nat by A73,A77,A92;
          A94: g2/.n=G*(i1,n1) & g2/.m=G*(i1,m1) by A76,A77,A92;
          assume g2/.n=g2/.m;
          then n1=m1 by A93,A94,GOBOARD1:21;
          hence contradiction by A92,XCMPLX_1:20;
         end;
         then for n,m st n in dom g2 & m in dom g2 &
         g2/.n = g2/.m holds n = m;
then A95:     g2 is one-to-one & dom g2=Seg len g2 by FINSEQ_1:def 3,PARTFUN2:
16;
        A96: not f/.k in rng g2
         proof assume f/.k in rng g2;
          then consider n such that
          A97: n in dom g2 & g2/.n=f/.k by PARTFUN2:4;
          A98: [i1,i2-n] in Indices G by A73,A76,A95,A97;
          reconsider n1=i2-n as Nat by A73,A76,A95,A97;
             g2/.n=G*(i1,n1) & 0+1<=n by A76,A95,A97,FINSEQ_1:3;
           then n1=i2 & 0<n by A47,A97,A98,GOBOARD1:21,NAT_1:38;
          hence contradiction by SQUARE_1:29;
         end;
        A99: now let n,p; assume
          A100: n in dom g2 & g2/.n=p;
          then A101: P1[n,g2/.n] &
          1<=n & n<=len g2& i2-n in Seg width G by A73,A76,A95,FINSEQ_3:27;
          reconsider n1=i2-n as Nat by A73,A77,A100;
          set pn = G*(i1,n1);
             l1/.n1=l1.n1 by A63,A101,FINSEQ_4:def 4;
          then A102: g2/.n=pn & l1/.n1=pn & 0<=n & i2-len g2<=n1
            by A101,AXIOMS:22,MATRIX_1:def 8,REAL_1:92;
          then A103: x1.n1=pn`1 & x1.n1=x1.i2 & y1.n1=pn`2 & n1<=i2 & j2<=n1
           by A51,A54,A58,A59,A60,A61,A62,A76,A101,GOBOARD1:def 2,def 3,def 4,
REAL_2:173;
          hence p`1=ppi`1 & pj`2<=p`2 & p`2<=ppi`2
            by A51,A55,A59,A60,A62,A88,A100,A101,A102,GOBOARD2:18;
             dom l1 = Seg len l1 by FINSEQ_1:def 3;
          hence p in rng l1
          by A62,A100,A101,A102,PARTFUN2:4;  0<n by A101,AXIOMS:22;
          then n1<i2 by SQUARE_1:29;
          hence p`2<ppi`2 by A51,A55,A59,A60,A62,A88,A100,A101,A102,A103,
GOBOARD1:def 1;
         end;
        A104: now let n,m,p,q; assume
          A105: n in dom g2 & m in dom g2 & n<m & g2/.n=p & g2/.m=q;
          then A106: P1[n,g2/.n] & i2-n in Seg width G &
              P1[m,g2/.m] & i2-m in Seg width G by A73,A76,A77;
          reconsider n1=i2-n, m1=i2-m as Nat by A73,A77,A105;
          set pn = G*(i1,n1), pm = G*(i1,m1);
             l1/.n1=l1.n1 & l1/.m1 = l1.m1 by A63,A106,FINSEQ_4:def 4;
          then A107: g2/.n=pn & l1/.n1=pn & g2/.m=pm & l1/.m1=pm & m1<n1
            by A105,A106,MATRIX_1:def 8,REAL_1:92;
          then y1.n1=pn`2 & y1.m1=pm`2 by A59,A60,A62,A106,GOBOARD1:def 4;
          hence q`2<p`2 by A55,A59,A60,A62,A105,A106,A107,GOBOARD1:def 1;
         end;
        A108: rng g2 c= LSeg(f,k)
         proof let x; assume x in rng g2;
          then consider n such that
          A109: n in dom g2 & g2/.n=x by PARTFUN2:4;
          reconsider n1=i2-n as Nat by A73,A76,A95,A109;
          set pn = G*(i1,n1);
          A110: g2/.n=pn by A76,A95,A109;
          then pn`1=ppi`1 & pj`2<=pn`2 & pn`2<=ppi`2 by A99,A109;
          hence x in LSeg(f,k) by A90,A109,A110;
         end;
           rng g1 /\ rng g2 = {}
         proof consider x being Element of rng g1 /\ rng g2;
          assume not thesis;
          then A111: x in rng g1 & x in rng g2 by XBOOLE_0:def 3;
              now per cases by A14,REAL_1:def 5;
           suppose k=1;
            hence contradiction by A37,A96,A111,TARSKI:def 1;
           end;
           suppose 1<k;
            then x in L~f1 /\ LSeg(f,k) & L~f1 /\ LSeg(f,k)={f/.k}
             by A5,A8,A9,A44,A108,A111,GOBOARD2:9,XBOOLE_0:def 3;
            hence contradiction by A96,A111,TARSKI:def 1;
           end;
           end;
          hence contradiction;
         end;
        then rng g1 misses rng g2 by XBOOLE_0:def 7;
        hence g is one-to-one by A29,A95,FINSEQ_3:98;
        A112: for n st 1<=n & n+2 <= len g2 holds
             LSeg(g2,n) /\ LSeg(g2,n+1) = {g2/.(n+1)}
         proof let n; assume
          A113: 1<=n & n+2 <= len g2;
          then A114: n in dom g2 & n+1 in dom g2 & n+2 in dom g2 by GOBOARD2:4;
          then g2/.n in rng g2 & g2/.(n+1) in rng g2 &
          g2/.(n+2) in rng g2 by PARTFUN2:4;
then A115: g2/.n in lk & g2/.(n+1) in lk & g2/.(n+2) in lk by A90,A108;
          then consider u be Point of TOP-REAL 2 such that
          A116: g2/.n=u & u`1=ppi`1 & pj`2<=u`2 & u`2<=ppi`2;
          consider u1 be Point of TOP-REAL 2 such that
          A117: g2/.(n+1)=u1 & u1`1=ppi`1 & pj`2<=u1`2 & u1`2<=ppi`2 by A115;
          consider u2 be Point of TOP-REAL 2 such that
          A118: g2/.(n+2)=u2 & u2`1=ppi`1 & pj`2<=u2`2 & u2`2<=ppi`2 by A115;
          set lg = {w where w is Point of TOP-REAL 2:
                     w`1=ppi`1 & u2`2<=w`2 & w`2<=u`2};
          A119: n+(1+1) = n+1+1;
             n+1 <= n+2 by AXIOMS:24;
           then n+1 <= len g2 by A113,AXIOMS:22;
then A120:      LSeg(g2,n)=LSeg(u,u1) by A113,A116,A117,TOPREAL1:def 5;
             1 <= n+1 by NAT_1:29;
then A121:      LSeg(g2,n+1)=LSeg(u1,u2) by A113,A117,A118,A119,TOPREAL1:def 5;
             n<n+1 & n+1<n+1+1 & n+1+1=n+(1+1) by NAT_1:38;
          then A122: u2`2<u1`2 & u1`2<u`2 by A104,A114,A116,A117,A118;
          then A123: u2`2<=u1`2 & u1`2<=u`2 & u2`2<u`2 by AXIOMS:22;
          A124: u1 in lg & u=|[u`1,u`2]| & u2=|[u2`1,u2`2]| by A117,A122,EUCLID
:57;
          then LSeg(g2/.n,g2/.(n+2))=lg by A116,A118,A123,TOPREAL3:15;
          hence thesis by A116,A117,A118,A120,A121,A124,TOPREAL1:14;
         end;
        thus g is unfolded
         proof let n; assume
          A125: 1<=n & n+2 <= len g;
then A126:     n+1+1<=len g;
          A127: 1 <= n+1 by NAT_1:29;
          A128: n<=n+1 & n+1<=n+1+1 by NAT_1:29;
then A129:      n+1 <= len g by A125,AXIOMS:22;
A130:      n+(1+1)=n+1+1;
A131:      len g=len g1+len g2 by FINSEQ_1:35;
              n+2-len g1 = n-len g1 +2;
          then A132: n-len g1 + 2 <= len g2 by A125,A131,REAL_1:86;
A133:       n+1+1 = n+(1+1);
           per cases;
           suppose A134: n+2 <= len g1;
        then n in dom g1 & n+1 in dom g1 & n+2 in dom g1 & n+(1+1)=n+1+1
              by A125,GOBOARD2:4;
            then LSeg(g1,n)=LSeg(g,n) & LSeg(g1,n+1)=LSeg(g,n+1) &
            g/.(n+1)=g1/.(n+1) by GROUP_5:95,TOPREAL3:25;
            hence thesis by A30,A125,A134,TOPREAL1:def 8;
           end;
           suppose len g1 < n+2;
            then len g1+1<=n+2 by NAT_1:38;
            then A135: len g1<=n+2-1 by REAL_1:84;
                now per cases;
             suppose A136: len g1=n+1;
                  now assume A137: k=1;
                   1<len g1 by A125,A136,NAT_1:38;
                then 1+1<=len g1 by NAT_1:38;
                hence contradiction by A37,A137,TOPREAL1:29;
               end;
              then A138: 1<k & LSeg(g1,n) c= L~f1
                 by A14,A33,REAL_1:def 5,TOPREAL3:26;
              then A139: L~f1 /\ LSeg(f,k)={f/.k} by A5,A8,A9,GOBOARD2:9;
                 1<=n+1 by NAT_1:29;
              then A140: n in dom g1 & n+1 in dom g1
              by A125,A128,A136,FINSEQ_3:27;
              then A141: LSeg(g,n)=LSeg(g1,n) by TOPREAL3:25;
              A142: g/.(n+1)=f1/.len f1 by A35,A136,A140,GROUP_5:95
              .= ppi by A15,A16,A18,A47,GOBOARD1:10;
                A143: 1<=len g-len g1 by A126,A136,REAL_1:84;
              then A144: g/.(n+2)=g2/.1 by A130,A131,A136,GOBOARD2:5;
                 1 in dom g2 by A131,A143,FINSEQ_3:27;
              then A145: g2/.1 in rng g2 by PARTFUN2:4;
              then g2/.1 in lk by A90,A108;
              then consider u1 be Point of TOP-REAL 2 such that
              A146: g2/.1=u1 & u1`1=ppi`1 & pj`2<=u1`2 & u1`2<=ppi`2;
                 ppi in LSeg(ppi,pj) by TOPREAL1:6;
              then LSeg(ppi,u1) c= LSeg(f,k) & LSeg(g,n+1)=LSeg(ppi,u1)
                by A91,A108,A125,A127,A133,A142,A144,A145,A146,TOPREAL1:12,def
5;
              then A147: LSeg(g,n) /\ LSeg(g,n+1) c= {g/.(n+1)}
               by A47,A138,A139,A141,A142,XBOOLE_1:27;
                 g/.(n+1) in LSeg(g,n) & g/.(n+1) in LSeg(g,n+1)
                by A125,A127,A129,A133,TOPREAL1:27;
              then g/.(n+1) in LSeg(g,n) /\ LSeg(g,n+1) by XBOOLE_0:def 3;
              then {g/.(n+1)} c= LSeg(g,n) /\ LSeg(g,n+1) by ZFMISC_1:37;
              hence thesis by A147,XBOOLE_0:def 10;
             end;
             suppose len g1<>n+1; then len g1<n+1 by A135,REAL_1:def 5;
              then A148: len g1<=n by NAT_1:38;
              then reconsider n1=n-len g1 as Nat by INT_1:18;
                  now per cases;
               suppose A149: len g1=n;
                   1<=len g1 by A14,A16,A36,AXIOMS:22;
                then len g1 in dom g1 by FINSEQ_3:27;
                then A150: g/.n=f1/.len f1 by A35,A149,GROUP_5:95
                .= ppi by A15,A16,A18,A47,GOBOARD1:10;
                   1 <= len g2 by A129,A131,A149,REAL_1:53;
                then A151: g/.(n+1)=g2/.1 by A149,GOBOARD2:5;
                   2 <= len g2 by A125,A131,A149,REAL_1:53;
                then A152: g/.(n+2)=g2/.2 by A149,GOBOARD2:5;
                A153: 0+2<=len g2 by A125,A131,A149,REAL_1:53;
                then 1<=len g2 by AXIOMS:22;
                then A154: 1 in dom g2 & 2 in dom g2 by A153,FINSEQ_3:27;
                then g2/.1 in rng g2 & g2/.2 in rng g2 by PARTFUN2:4;
                then A155: g2/.1 in lk & g2/.2 in lk by A90,A108;
                then consider u1 be Point of TOP-REAL 2 such that
                A156: g2/.1=u1 & u1`1=ppi`1 & pj`2<=u1`2 & u1`2<=ppi`2;
                consider u2 be Point of TOP-REAL 2 such that
          A157: g2/.2=u2 & u2`1=ppi`1 & pj`2<=u2`2 & u2`2<=ppi`2 by A155;
                A158: u2`2<u1`2 by A104,A154,A156,A157;
                then A159: u2`2<=u1`2 & u2`2<ppi`2 & u2=|[u2`1,u2`2]|
                  by A156,AXIOMS:22,EUCLID:57;
                set lg = {w where w is Point of TOP-REAL 2 :
                          w`1=ppi`1 & u2`2<=w`2 & w`2<=ppi`2};
                A160: u1 in lg by A156,A158;
A161:            LSeg(g,n)=LSeg(ppi,u1)
                 by A125,A129,A150,A151,A156,TOPREAL1:def 5;
A162:            LSeg(g,n+1)=LSeg(u1,u2)
                 by A125,A127,A133,A151,A152,A156,A157,TOPREAL1:def 5;
                   LSeg(ppi,g2/.2)=lg by A89,A157,A159,TOPREAL3:15;
                hence thesis by A151,A156,A157,A160,A161,A162,TOPREAL1:14;
               end;
               suppose len g1<>n;
then A163:              len g1<n by A148,REAL_1:def 5;
              n1 + len g1 = n;
then A164:            LSeg(g,n)=LSeg(g2,n1) by A129,A163,GOBOARD2:10;
A165:            len g1<n+1 by A128,A163,AXIOMS:22;
A166:            len g1+1<=n by A163,NAT_1:38;
A167:            n+1 = n1+1+len g1;
A168:             1<=n1+1 by NAT_1:29;
                   n1 + 1 + len g1 = n + 1;
then A169:              n1+1 <= len g2 by A129,A131,REAL_1:53;
A170:             LSeg(g,n+1)=LSeg(g2,n1+1) by A126,A165,A167,GOBOARD2:10;
A171:             g/.(n+1)=g2/.(n1+1) by A167,A168,A169,GOBOARD2:5;
                   1<=n1 by A166,REAL_1:84;
                hence thesis by A112,A132,A164,A170,A171;
               end;
               end;
              hence thesis;
             end;
             end;
            hence thesis;
         end;
         end;
              for n,m st m>n+1 & n in dom g2 & n+1 in dom g2 & m in dom g2 &
           m+1 in dom g2 holds LSeg(g2,n) misses LSeg(g2,m)
         proof let n,m; assume that
          A172: m>n+1 and
          A173: n in dom g2 & n+1 in dom g2 & m in dom g2 & m+1 in dom g2 and
          A174: LSeg(g2,n) /\ LSeg(g2,m) <> {};
          A175: 1 <= n & n+1 <= len g2 & 1 <= m & m+1<=
 len g2 by A173,FINSEQ_3:27;
          reconsider p1=g2/.n, p2=g2/.(n+1), q1=g2/.m, q2=g2/.(m+1)
           as Point of TOP-REAL 2;
          set lp = {w where w is Point of TOP-REAL 2: w`1=ppi`1 &
                         p2`2<=w`2 & w`2<=p1`2},
              lq = {w where w is Point of TOP-REAL 2: w`1=ppi`1 &
                         q2`2<=w`2 & w`2<=q1`2};
              m<m+1 & n<n+1 by NAT_1:38;
           then A176: p2`2<p1`2 & q2`2<q1`2 & p1`1=ppi`1 & p2`1=ppi`1 & q1`1=
ppi`1 &
           q2`1=ppi`1 & p1=|[p1`1,p1`2]| & q1=|[q1`1,q1`2]| &
           p2=|[p2`1,p2`2]| &
           q2=|[q2`1,q2`2]| by A99,A104,A173,EUCLID:57;
           A177: LSeg(g2,n) = LSeg(p2,p1) by A175,TOPREAL1:def 5
           .=lp by A176,TOPREAL3:15;
           A178: LSeg(g2,m) = LSeg(q2,q1) by A175,TOPREAL1:def 5
           .=lq by A176,TOPREAL3:15;
           consider x being Element of LSeg(g2,n) /\ LSeg(g2,m);
           A179: x in LSeg(g2,n) & x in LSeg(g2,m) by A174,XBOOLE_0:def 3;
then A180:         ex tn be Point of TOP-REAL 2
               st tn=x & tn`1=ppi`1 & p2`2<=tn`2 & tn`2<=p1`2 by A177;
A181:         ex tm be Point of TOP-REAL 2 st
              tm=x & tm`1=ppi`1 & q2`2<=tm`2 & tm`2<=q1`2 by A178,A179;
               q1`2<p2`2 by A104,A172,A173;
            hence contradiction by A180,A181,AXIOMS:22;
         end;
then A182:    g2 is s.n.c. by GOBOARD2:6;
        A183: L~g2 c= LSeg(f,k)
         proof
          set ls={LSeg(g2,m): 1<=m & m+1 <= len g2}; let x; assume
             x in L~g2; then x in union ls by TOPREAL1:def 6;
          then consider X be set such that
          A184: x in X & X in ls by TARSKI:def 4;
          consider m such that
          A185: X=LSeg(g2,m) & 1<=m & m+1 <= len g2 by A184;
              m in dom g2 & m+1 in dom g2 by A185,GOBOARD2:3;
          then A186: g2/.m in rng g2 & g2/.(m+1) in rng g2 by PARTFUN2:4;
          reconsider q1=g2/.m, q2=g2/.(m+1) as Point of TOP-REAL 2;
          A187: LSeg(q1,q2) c= LSeg(ppi,pj) by A91,A108,A186,TOPREAL1:12;
             LSeg(g2,m)=LSeg(q1,q2) by A185,TOPREAL1:def 5;
          hence thesis by A91,A184,A185,A187;
         end;
        A188: not f/.k in L~g2
         proof set ls={LSeg(g2,m): 1<=m & m+1 <= len g2}; assume
             f/.k in L~g2; then f/.k in union ls by TOPREAL1:def 6;
          then consider X be set such that
          A189: f/.k in X & X in ls by TARSKI:def 4;
          consider m such that
          A190: X=LSeg(g2,m) & 1<=m & m+1 <= len g2 by A189;
          A191: m in dom g2 & m+1 in dom g2 & m<m+1
          by A190,GOBOARD2:3,NAT_1:38;
          reconsider q1=g2/.m, q2=g2/.(m+1) as Point of TOP-REAL 2;
          set lq={w where w is Point of TOP-REAL 2: w`1=ppi`1 &
                       q2`2<=w`2 & w`2<=q1`2};
          A192: q1`1=ppi`1 & q2`1=ppi`1 & q2`2<q1`2 & q1=|[q1`1,q1`2]|
          & q2=|[q2`1,q2`2]| by A99,A104,A191,EUCLID:57;
             LSeg(g2,m)=LSeg(q2,q1) by A190,TOPREAL1:def 5
          .=lq by A192,TOPREAL3:15;
          then ex p st p=f/.k & p`1=ppi`1 & q2`2<=p`2 & p`2<=q1`2 by A189,A190
;
          hence contradiction by A47,A99,A191;
         end;
        A193: L~g1 /\ L~g2 = {}
         proof
           per cases;
           suppose k=1; hence thesis by A37;
           end;
           suppose k<>1; then 1<k by A14,REAL_1:def 5;
            then L~g1 /\ LSeg(f,k)={f/.k} by A5,A8,A9,A33,GOBOARD2:9;
            then A194: L~g1 /\ L~g2 c= {f/.k} by A183,XBOOLE_1:26;
                now consider x being Element of L~g1 /\ L~g2;
              assume L~g1 /\ L~g2 <> {};
              then x in {f/.k} & x in L~g2 by A194,TARSKI:def 3,XBOOLE_0:def 3
;
              hence contradiction by A188,TARSKI:def 1;
             end;
            hence thesis;
         end;
         end;
           for n,m st m>n+1 & n in dom g & n+1 in dom g &
        m in dom g & m+1 in dom g holds LSeg(g,n) misses LSeg(g,m)
         proof let n,m; assume that
          A195: m>n+1 and
          A196: n in dom g & n+1 in dom g & m in dom g & m+1 in dom g;
          A197: 1<=n & 1<=n+1 & n+1<=len g & 1<=m & 1<=m+1 &
          m+1<=len g & n<=n+1 & m<=m+1 & len g=len g1+len g2
            by A196,FINSEQ_1:35,FINSEQ_3:27,NAT_1:29;
          set l1 = {LSeg(g1,i): 1<=i & i+1 <= len g1},
              l2 = {LSeg(g2,j): 1<=j & j+1 <= len g2};
          A198: L~g1=union l1 & L~g2=union l2 & n+1 <= len g & m+1 <= len g
            by A196,FINSEQ_3:27,TOPREAL1:def 6;
             j2+1<=i2 by A72,NAT_1:38;
          then A199: 1<=l by REAL_1:84;
          then A200: 1 in dom g2 by A76,FINSEQ_3:27;
          reconsider qq=g2/.1 as Point of TOP-REAL 2;
          set ql={z where z is Point of TOP-REAL 2: z`1=ppi`1 &
                     qq`2<=z`2 & z`2<=ppi`2};
          A201: 1<=len g1 by A14,A16,A36,AXIOMS:22;
           then len g1 in dom g1 & dom g1 c= dom g by FINSEQ_1:39,FINSEQ_3:27;
          then A202: g/.len g1=g1/.len g1 by GROUP_5:95
             .= ppi by A15,A16,A18,A35,A47,GOBOARD1:10;
          A203: len g1<len g1 +1 & len g1+1<=len g
            by A76,A197,A199,NAT_1:38,REAL_1:55;
          A204: g/.(len g1+1)=qq by A76,A199,GOBOARD2:5;
          A205: qq`1=ppi`1 & pj`2<=qq`2 & qq`2<ppi`2 & qq=|[qq`1,qq`2]|
           by A99,A200,EUCLID:57;
    A206: LSeg(g,len g1)=LSeg(qq,ppi) by A201,A202,A203,A204,TOPREAL1:def 5
           .= ql by A89,A205,TOPREAL3:15;
            A207: n+1<=m+1 by A195,A197,AXIOMS:22;
                now per cases;
             suppose A208: m+1<=len g1;
              then A209: m<=len g1 & n+1<=len g1 by A197,A207,AXIOMS:22;
              then n<=len g1 by A197,AXIOMS:22;
              then n in dom g1 & n+1 in dom g1 & m in dom g1 & m+1 in dom g1
               by A197,A208,A209,FINSEQ_3:27;
              then LSeg(g,n)=LSeg(g1,n) & LSeg(g,m)=LSeg(g1,m) by TOPREAL3:25;
              hence thesis by A31,A195,TOPREAL1:def 9;
             end;
             suppose len g1<m+1;
              then A210: len g1<=m by NAT_1:38;
              then reconsider m1=m-len g1 as Nat by INT_1:18;
                  now per cases;
               suppose A211: m=len g1;
                    now assume A212: k=1;
                     1<len g1 by A195,A197,A211,AXIOMS:22;
                  then 1+1<=len g1 by NAT_1:38;
                  hence contradiction by A37,A212,TOPREAL1:29;
                 end;
                then 1<k by A14,REAL_1:def 5;
                then A213: L~f1 /\ LSeg(f,k) ={f/.k}
                 by A5,A8,A9,GOBOARD2:9;
                   n<=len g1 by A195,A197,A211,AXIOMS:22;
                then A214: n in dom g1 & n+1 in dom g1
                by A195,A197,A211,FINSEQ_3:27;
                then A215: LSeg(g,n)=LSeg(g1,n) by TOPREAL3:25;
                then LSeg(g,n) in l1 by A195,A197,A211;
                then A216: LSeg(g,n) c= L~f1 by A33,A198,ZFMISC_1:92;
                   LSeg(g,m) c= LSeg(f,k)
                 proof let x; assume x in LSeg(g,m);
                  then consider px be Point of TOP-REAL 2 such that
                  A217: px=x & px`1=ppi`1 & qq`2<=px`2 & px`2<=
ppi`2 by A206,A211;
                     pj`2<=px`2 by A205,A217,AXIOMS:22;
                  hence thesis by A90,A217;
                 end;
                then A218: LSeg(g,n) /\ LSeg(g,m) c= {f/.k}
                 by A213,A216,XBOOLE_1:27;
                    now consider x being Element of LSeg(g,n) /\ LSeg(g,m);
                  assume LSeg(g,n)/\ LSeg(g,m)<>{};
                  then A219: x in LSeg(g,n) & x in {f/.k}
                  by A218,TARSKI:def 3,XBOOLE_0:def 3;
                  then A220: x=f/.k by TARSKI:def 1;
                     f/.k=g1/.len g1 by A15,A16,A18,A35,GOBOARD1:10;
                  hence contradiction
                  by A29,A30,A31,A195,A211,A214,A215,A219,A220,GOBOARD2:7;
                 end;
                hence thesis by XBOOLE_0:def 7;
               end;
               suppose
              m<>len g1;
                then A221: len g1<m & m+1 = m1+1 +len g1
                 by A210,REAL_1:def 5;
                   m = m1+len g1;
                then A222: LSeg(g,m)=LSeg(g2,m1) by A197,A221,GOBOARD2:10;
                   m1+1<=len g2 & len g1+1<=
m by A197,A221,NAT_1:38,REAL_1:53;
                then A223: 1<=m1 & m1+1 <= len g2 by REAL_1:84;
                then LSeg(g,m) in l2 by A222;
                then A224: LSeg(g,m) c= L~g2 by A198,ZFMISC_1:92;
                    now per cases;
                 suppose A225: n+1<=len g1;
                  then n<=len g1 by A197,AXIOMS:22;
                  then n in dom g1 & n+1 in dom g1 by A197,A225,FINSEQ_3:27;
                   then LSeg(g,n)=LSeg(g1,n) by TOPREAL3:25;
                  then LSeg(g,n) in l1 by A197,A225;
                  then LSeg(g,n) c= L~g1 by A198,ZFMISC_1:92;
                  then LSeg(g,n) /\ LSeg(g,m) c= {} by A193,A224,XBOOLE_1:27;
                  then LSeg(g,n) /\ LSeg(g,m) = {} by XBOOLE_1:3;
                  hence thesis by XBOOLE_0:def 7;
                 end;
                 suppose len g1<n+1;
                  then A226: len g1<=n by NAT_1:38;
                  then reconsider n1=n-len g1 as Nat by INT_1:18;
A227:               n - len g1 + 1 = n + 1 - len g1;
A228:               n = n1 + len g1;
                      now per cases;
                   suppose A229: len g1=n;
                        now consider x being Element of LSeg(g,n) /\ LSeg(g,m);
                      assume LSeg(g,n) /\ LSeg(g,m)<>{};
                      then A230: x in LSeg(g,n) & x in LSeg(g,m)
                      by XBOOLE_0:def 3;
then A231:                      ex qx be Point of TOP-REAL 2 st
               qx=x & qx`1=ppi`1 & qq`2<=qx`2 & qx`2<=ppi`2 by A206,A229;
                      A232: m1 in dom g2 & m1+1 in dom g2 & m1<m1+1
                        by A223,GOBOARD2:3,NAT_1:38;
                      reconsider q1=g2/.m1, q2=g2/.(m1+1)
                       as Point of TOP-REAL 2;
                      set q1l={v where v is Point of TOP-REAL 2: v`1=ppi`1 &
                                 q2`2<=v`2 & v`2<=q1`2};
A233: q1`1=ppi`1 & q2`1=ppi`1 & q2`2<q1`2 & q1=|[q1`1,q1`2]|&
                      q2=|[q2`1,q2`2]| by A99,A104,A232,EUCLID:57;
                         m1 > n1 + 1 & n1 + 1 >=
 1 by A195,A227,NAT_1:29,REAL_1:54;
then A234:                   m1 > 1 by AXIOMS:22;
                         LSeg(g2,m1)=LSeg(q2,q1) by A223,TOPREAL1:def 5
                      .=q1l by A233,TOPREAL3:15;
then A235:                      ex qy be Point of TOP-REAL 2 st
 qy=x & qy`1=ppi`1 & q2`2<=qy`2 & qy`2<=q1`2 by A222,A230;
                         q1`2 < qq`2 by A104,A200,A232,A234;
                      hence contradiction by A231,A235,AXIOMS:22;
                     end;
                    hence thesis by XBOOLE_0:def 7;
                   end;
                   suppose n<>len g1;
                    then len g1<n by A226,REAL_1:def 5;
                    then LSeg(g,n)=LSeg(g2,n1) & m1>n1+1
                     by A195,A197,A227,A228,GOBOARD2:10,REAL_1:54;
                    hence thesis by A182,A222,TOPREAL1:def 9;
                   end;
                   end;
                  hence thesis;
                 end;
                 end;
                hence thesis;
               end;
               end;
              hence thesis;
             end;
             end;
            hence thesis;
         end;
        hence g is s.n.c. by GOBOARD2:6;
        A236: g2 is special
         proof let n;
         set p = g2/.n, q = g2/.(n+1);
         assume 1<=n & n+1 <= len g2;
          then n in dom g2 & n+1 in dom g2 by GOBOARD2:3;
          then p`1=ppi`1 & q`1=ppi`1 by A99;
          hence p`1=q`1 or p`2=q`2;
         end;
            now set p=g1/.len g1, q=g2/.1;
             j2+1<=i2 by A72,NAT_1:38;
          then 1<=l by REAL_1:84;
          then 1 in dom g2 by A77,FINSEQ_1:3;
           then q`1=ppi`1 by A99;
          hence p`1=q`1 or p`2=q`2
           by A15,A16,A18,A35,A47,GOBOARD1:10;
         end;
        hence g is special by A32,A236,GOBOARD2:13;
        thus L~g=L~f
         proof
          set lg = {LSeg(g,i): 1<=i & i+1 <= len g},
              lf = {LSeg(f,j): 1<=j & j+1 <= len f};
          A237: len g = len g1 + len g2 by FINSEQ_1:35;
          A238: now let j; assume
            A239: len g1<=j & j<=len g;
            then reconsider w = j-len g1 as Nat by INT_1:18;
            let p such that
            A240: p=g/.j;
                now per cases;
             suppose A241: j=len g1;
                 1<=len g1 by A14,A16,A36,AXIOMS:22;
              then len g1 in dom g1 by FINSEQ_3:27;
              then A242: g/.len g1 = f1/.len f1 by A35,GROUP_5:95
              .= G*(i1,i2) by A15,A16,A18,A47,GOBOARD1:10;
              hence p`1=G*(i1,i2)`1 by A240,A241;
              thus G*(i1,j2)`2<=p`2 & p`2<=G*(i1,i2)`2 by A51,A55,A59,A60,A62,
A72,A88,A240,A241,A242,GOBOARD1:def 1;
                  dom l1 = Seg len l1 by FINSEQ_1:def 3;
              hence p in rng l1 by A51,A62,A87,A240,A241,A242,PARTFUN2:4;
             end;
             suppose j<>len g1;
           then len g1 < j by A239,REAL_1:def 5;
then len g1 + 1<=j by NAT_1:38;
then A243:           1<=w & w<=len g2 by A237,A239,REAL_1:84,86;
              w + len g1 = j;
              then A244: g/.j=g2/.w by A243,GOBOARD2:5;
                 w in dom g2 by A243,FINSEQ_3:27;
              hence p`1=ppi`1 & pj`2<=p`2 & p`2<=
ppi`2 & p in rng l1 by A99,A240,A244;
             end;
             end;
            hence p`1=ppi`1 & pj`2<=p`2 & p`2<=ppi`2 & p in rng l1;
           end;
          thus L~g c= L~f
           proof let x; assume x in L~g;
            then x in union lg by TOPREAL1:def 6;
            then consider X be set such that
            A245: x in X & X in lg by TARSKI:def 4;
            consider i such that
            A246: X=LSeg(g,i) & 1<=i & i+1 <= len g by A245;
             per cases;
             suppose A247: i+1 <= len g1;
              then i<=i+1 & i+1<=len g1 by NAT_1:29;
              then 1<=i & i<=len g1 & 1<=i+1 & i+1<=len g1 by A246,AXIOMS:22;
              then i in dom g1 & i+1 in dom g1 by FINSEQ_3:27;
              then X=LSeg(g1,i) by A246,TOPREAL3:25;
              then X in {LSeg(g1,j): 1<=j & j+1 <= len g1} by A246,A247;
              then x in union {LSeg(g1,j): 1<=j & j+1 <= len g1}
                by A245,TARSKI:def 4;
              then x in L~f1 & L~f1 c= L~f by A33,TOPREAL1:def 6,TOPREAL3:27;
              hence thesis;
             end;
             suppose A248: i+1 > len g1;
              then A249: len g1<=i & i<=i+1 & i+1<=len g by A246,NAT_1:38;
              A250: 1<=i+1 & len g1<=i+1 & i<=len g by A246,A248,NAT_1:38;
              reconsider q1=g/.i, q2=g/.(i+1) as Point of TOP-REAL 2;
              A251: q1`1=ppi`1 & pj`2<=q1`2 & q1`2<=ppi`2 & q2`1=ppi`1 & pj`2<=
q2`2 &
                  q2`2<=ppi`2 by A238,A249,A250;
              then A252: q1=|[q1`1,q1`2]| & q2=|[q1`1,q2`2]| by EUCLID:57;
              A253: LSeg(g,i)=LSeg(q2,q1) by A246,TOPREAL1:def 5;
                  now per cases by AXIOMS:21;
               suppose q1`2>q2`2;
                then LSeg(g,i)={p2: p2`1=q1`1 & q2`2<=p2`2 & p2`2<=q1`2}
                  by A252,A253,TOPREAL3:15;
                then consider p2 such that
                A254: p2=x & p2`1=q1`1 & q2`2<=p2`2 & p2`2<=q1`2 by A245,A246;
                   pj`2<=p2`2 & p2`2<=ppi`2 by A251,A254,AXIOMS:22;
                then x in LSeg(f,k) & LSeg(f,k) in lf by A14,A90,A251,A254;
                then x in union lf by TARSKI:def 4;
                hence thesis by TOPREAL1:def 6;
               end;
               suppose q1`2=q2`2;
                then LSeg(g,i)={q1} by A252,A253,TOPREAL1:7;
                then x=q1 by A245,A246,TARSKI:def 1;
                then x in LSeg(f,k) & LSeg(f,k) in lf by A14,A90,A251;
                then x in union lf by TARSKI:def 4;
                hence thesis by TOPREAL1:def 6;
               end;
               suppose q1`2<q2`2;
                then LSeg(g,i)= {p1: p1`1=q1`1 & q1`2<=p1`2 & p1`2<=q2`2}
                  by A252,A253,TOPREAL3:15;
                then consider p2 such that
                A255: p2=x & p2`1=q1`1 & q1`2<=p2`2 & p2`2<=q2`2 by A245,A246;
                   pj`2<=p2`2 & p2`2<=ppi`2 by A251,A255,AXIOMS:22;
                then x in LSeg(f,k) & LSeg(f,k) in lf by A14,A90,A251,A255;
                then x in union lf by TARSKI:def 4;
                hence thesis by TOPREAL1:def 6;
               end;
               end;
              hence thesis;
           end;
           end;
          let x; assume x in L~f;
          then A256: x in L~f1 \/ LSeg(f,k) by A5,A12,GOBOARD2:8;
           per cases by A256,XBOOLE_0:def 2;
           suppose x in L~f1; then x in L~g1 & L~g1 c= L~g
            by A33,GOBOARD2:11;
            hence x in L~g;
           end;
           suppose x in LSeg(f,k);
            then consider p1 such that
            A257: p1=x & p1`1=ppi`1 & pj`2<=p1`2 & p1`2<=ppi`2 by A90;
            defpred P2[Nat] means
            len g1<=$1 & $1<=len g & for q st q=g/.$1 holds q`2>=p1`2;
            A258: now
              take n=len g1;
              thus P2[n]
              proof
                 0<=len g2 by NAT_1:18;
               hence len g1<=n & n<=len g by A237,REAL_2:173;
               let q such that
               A259: q=g/.n;
                  1<=len g1 by A14,A16,A36,AXIOMS:22;
               then len g1 in dom g1 by FINSEQ_3:27;
               then q=f1/.len f1 by A35,A259,GROUP_5:95
               .=G*(i1,i2) by A15,A16,A18,A47,GOBOARD1:10;
               hence p1`2<=q`2 by A257;
              end;
             end;
            A260: for n holds P2[n] implies n<=len g;
            consider ma be Nat such that
            A261: P2[ma] & for n st P2[n] holds n<=ma from NAT_1:sch 6
(A260,A258);
                now per cases;
             suppose A262: ma=len g;
                 j2+1<=i2 by A72,NAT_1:38;
              then A263: 1<=l & l=len g2 by A76,REAL_1:84;
              then A264: l in dom g2 & i2-l=j2 by FINSEQ_3:27;
              then A265: g/.ma=g2/.l by A76,A237,A262,GROUP_5:96
               .= pj by A76,A77,A264;
              then p1`2<=pj`2 by A261;
              then A266: p1`2=pj`2 by A257,AXIOMS:21;
              A267: ma-1<=len g & len g1+1<=ma
                by A237,A262,A263,PROB_1:2,REAL_1:55;
              then A268: len g1<=ma-1 & 0<=len g1 by NAT_1:18,REAL_1:84;
then A269:           0+1<=ma by REAL_1:84;
              reconsider m1=ma-1 as Nat by A269,INT_1:18;
                 1<=len g1 by A14,A16,A36,AXIOMS:22;
              then A271: 1<=m1 by A268,AXIOMS:22;
              reconsider q=g/.m1 as Point of TOP-REAL 2;
              A273: q`1=ppi`1 & pj`2<=q`2 & q=|[q`1,q`2]| & m1+1=ma
               by A238,A267,A268,EUCLID:57;
              then A274: LSeg(g,m1)=LSeg(pj,q)
               by A262,A265,A271,TOPREAL1:def 5;
              set lq={e where e is Point of TOP-REAL 2: e`1=ppi`1 & pj`2<=e`2 &
                      e`2<=q`2};
              LSeg(g,m1)=lq by A89,A273,A274,TOPREAL3:15;
              then p1 in LSeg(g,m1) & LSeg(g,m1) in lg
              by A257,A262,A266,A271,A273;
              then x in union lg by A257,TARSKI:def 4;
              hence x in L~g by TOPREAL1:def 6;
             end;
             suppose ma<>len g;
              then ma<len g by A261,REAL_1:def 5;
              then A275: ma+1<=len g & k<=ma & ma<=ma+1
                by A16,A36,A261,AXIOMS:22,NAT_1:38;
              then A276: 1<=ma & len g1<=ma+1 by A14,A261,AXIOMS:22;
              reconsider qa=g/.ma, qa1=g/.(ma+1) as Point of TOP-REAL 2;
              A277: p1`2<=qa`2 by A261;
               A278: now assume p1`2<=qa1`2;
                 then for q holds q=g/.(ma+1) implies p1`2<=q`2;
                then ma+1<=ma by A261,A275,A276;
                hence contradiction by REAL_2:174;
               end;
              then A279: qa1`2<qa`2 & qa1`2<=p1`2 & p1`2<=qa`2 &
        qa`1=ppi`1 & qa1 `1 = ppi`1 by A238,A261,A275,A276,A277,AXIOMS:22;
              set lma = {p2: p2`1=ppi`1 & qa1`2<=p2`2 & p2`2<=qa`2};
              A280: qa=|[qa`1,qa`2]| & qa1=|[qa1 `1, qa1 `2]| by EUCLID:57;
                 LSeg(g,ma)=LSeg(qa1,qa) by A275,A276,TOPREAL1:def 5
              .= lma by A279,A280,TOPREAL3:15;
              then x in LSeg(g,ma) & LSeg(g,ma) in lg by A257,A275,A276,A277,
A278;
              then x in union lg by TARSKI:def 4;
              hence x in L~g by TOPREAL1:def 6;
             end;
             end;
            hence x in L~g;
         end;
         end;
           1<=len g1 by A14,A16,A36,AXIOMS:22;
        then 1 in dom g1 by FINSEQ_3:27;
        hence g/.1=f1/.1 by A34,GROUP_5:95
        .=f/.1 by A15,A19,GOBOARD1:10;
           j2+1<=i2 by A72,NAT_1:38;
        then A281: 1<=l by REAL_1:84;
         then A282: l in dom g2 & len g=len g1 + len g2 & len g2 = l
         by A77,FINSEQ_1:3,35,def 3;
         reconsider m1=i2-l as Nat;
         thus g/.len g=g2/.l by A282,GROUP_5:96
         .=G*(i1,m1) by A76,A77,A282
         .=f/.len f by A5,A49,A71;
         thus len f<=len g by A5,A16,A36,A281,A282,REAL_1:55;
       end;
       case A283: i2=j2;
           k<>k+1 by NAT_1:38;
        hence contradiction by A7,A15,A47,A48,A49,A71,A283,PARTFUN2:17;
       end;
       case A284: i2<j2;
        then reconsider l=j2-i2 as Nat by INT_1:18;
        deffunc F(Nat) = G*(i1,i2+$1);
        consider g2 such that
        A285: len g2=l & for n st n in dom g2 holds g2/.n=F(n) from
GOBOARD2:sch 1;
        take g=g1^g2;
        A286: now let n; assume n in Seg l;
          then A287: 1<=n & n<=l by FINSEQ_1:3;
          then n<=n+i2 & i2+n<=l+i2 by NAT_1:29,REAL_1:55;
          then n<=i2+n & i2+n<=j2 & j2<=width G
           by A51,FINSEQ_1:3;
          then 1<=i2+n & i2+n<=width G by A287,AXIOMS:22;
          hence i2+n in Seg width G by FINSEQ_1:3;
          hence [i1,i2+n] in Indices G by A50,A51,ZFMISC_1:106;
         end;
        A288: dom g2 = Seg len g2 by FINSEQ_1:def 3;
            now let n such that A289: n in dom g2;
          take m=i1,k=i2+n;
          thus [m,k] in Indices G & g2/.n=G*(m,k) by A285,A286,A288,A289;
         end;
        then A290: for n st n in dom g ex i,j st [i,j] in Indices G &
        g/.n=G*(i,j) by A53,GOBOARD1:39;
        A291: now let n; assume
             n in dom g2 & n+1 in dom g2;
          then A292: g2/.n=G*(i1,i2+n) & [i1,i2+n] in Indices G &
          g2/.(n+1)=G*(i1,i2+(n+1)) &
          [i1,i2+(n+1)] in Indices G by A285,A286,A288;
          let l1,l2,l3,l4 be Nat; assume
             [l1,l2] in Indices G & [l3,l4] in Indices G & g2/.n=G*(l1,l2) &
          g2/.(n+1)=G*(l3,l4);
           then l1=i1 & l2=i2+n & l3=i1 & l4=i2+(n+1) by A292,GOBOARD1:21;
          hence abs(l1-l3)+abs(l2-l4)= 0+abs(i2+n-(i2+(n+1))) by ABSVALUE:7
          .= abs(-1)
          .= abs(1) by ABSVALUE:17
          .= 1 by ABSVALUE:def 1;
         end;
         now let l1,l2,l3,l4 be Nat; assume
A294: [l1,l2] in Indices G & [l3,l4] in Indices G & g1/.len g1=G*(l1,l2) &
          g2/.1=G*(l3,l4) & len g1 in dom g1 & 1 in dom g2;
          then g2/.1=G*
(i1,i2+1) & [i1,i2+1] in Indices G & f1/.len f1=f/.k
            by A15,A16,A18,A285,A286,A288,GOBOARD1:10;
           then l1=i1 & l2=i2 & l3=i1 & l4=i2+1 by A35,A47,A294,GOBOARD1:21;
          hence abs(l1-l3)+abs(l2-l4)=0+abs(i2-(i2+1)) by ABSVALUE:7
          .=abs(i2-i2+-1)
          .=abs(1) by ABSVALUE:17
          .=1 by ABSVALUE:def 1;
         end;
        then for n st n in dom g & n+1 in dom g holds
         for m,k,i,j st [m,k] in Indices G &
         [i,j] in Indices G & g/.n=G*(m,k) &
          g/.(n+1)=G*(i,j) holds abs(m-i)+abs(k-j)=1 by A53,A291,GOBOARD1:40;
        hence g is_sequence_on G by A290,GOBOARD1:def 11;
        set
        lk={w where w is Point of TOP-REAL 2: w`1=ppi`1 & ppi`2<=w`2 & w`2<=
pj`2};
           l1/.i2=l1.i2 & l1/.j2=l1.j2 by A51,A63,FINSEQ_4:def 4;
        then A295: l1/.i2=ppi & l1/.j2=pj by A51,MATRIX_1:def 8;
        then A296: y1.i2=ppi`2 & y1.j2=pj`2 & x1.i2=ppi`1 & x1.j2=pj`1
          by A51,A58,A59,A60,A61,A62,GOBOARD1:def 3,def 4;
        then A297: ppi`2<pj`2 & ppi`1=pj`1 & ppi=|[ppi`1,ppi`2]| & pj=|[pj`1,pj
`2]|
        by A51,A54,A55,A58,A59,A60,A61,A62,A284,EUCLID:57,GOBOARD1:def 1,def 2;
        A298: LSeg(f,k)=LSeg(ppi,pj) by A14,A47,A49,A71,TOPREAL1:def 5
        .= lk by A297,TOPREAL3:15;
        A299: LSeg(f,k)=LSeg(ppi,pj) by A14,A47,A49,A71,TOPREAL1:def 5;
            now let n,m; assume
          A300: n in dom g2 & m in dom g2 & n<>m;
          then A301: g2/.n=G*(i1,i2+n) & g2/.m=G*(i1,i2+m) & [i1,i2+n] in
Indices G &
          [i1,i2+m] in Indices G by A285,A286,A288;
          assume g2/.n=g2/.m;
          then i2+n=i2+m by A301,GOBOARD1:21;
          hence contradiction by A300,XCMPLX_1:2;
         end;
         then for n,m st n in dom g2 & m in dom g2 &
         g2/.n = g2/.m holds n = m;
        then A302: g2 is one-to-one by PARTFUN2:16;
        A303: not f/.k in rng g2
         proof assume f/.k in rng g2;
          then consider n such that
          A304: n in dom g2 & g2/.n=f/.k by PARTFUN2:4;
         dom g2 = Seg len g2 by FINSEQ_1:def 3;
          then A305: g2/.n=G*(i1,i2+n) & [i1,i2+n] in Indices G
            by A285,A286,A304;
             0+1<=n by A304,FINSEQ_3:27;
           then i2+n=i2 & 0<n by A47,A304,A305,GOBOARD1:21,NAT_1:38;
          hence contradiction by XCMPLX_1:3;
         end;
        A306: now let n,p; assume
          A307: n in dom g2 & g2/.n=p;
          then A308: g2/.n=G*(i1,i2+n) & 1<=n & n<=len g2
& i2+n in Seg width G by A285,A286,A288,FINSEQ_1:3;
          set n1=i2+n;
          set pn = G*(i1,n1);
             l1/.n1=l1.n1 by A63,A308,FINSEQ_4:def 4;
          then A309: g2/.n=pn & l1/.n1=pn & 0<=n & n1<=i2+len g2
            by A308,AXIOMS:22,MATRIX_1:def 8,REAL_1:55;
          then A310: x1.n1=pn`1 & x1.n1=x1.i2 & y1.n1=pn`2 & n1<=j2 & i2<=n1
           by A51,A54,A58,A59,A60,A61,A62,A285,A308,GOBOARD1:def 2,def 3,def 4,
REAL_2:173;
          hence p`1=ppi`1 & ppi`2<=p`2 & p`2<=pj`2
            by A51,A55,A59,A60,A62,A296,A307,A308,GOBOARD2:18;
              dom l1 = Seg len l1 by FINSEQ_1:def 3;
          hence p in rng l1
          by A62,A307,A308,A309,PARTFUN2:4;  0<n by A308,AXIOMS:22;
          then i2<n1 by REAL_2:174;
          hence p`2>ppi`2 by A51,A55,A59,A60,A62,A296,A307,A308,A310,GOBOARD1:
def 1;
         end;
        A311: now let n,m,p,q; assume
          A312: n in dom g2 & m in dom g2 & n<m & g2/.n=p & g2/.m=q;
          then A313: g2/.n=G*(i1,i2+n) & i2+n in Seg width G &
              g2/.m=G*(i1,i2+m) & i2+m in Seg width G by A285,A286,A288;
          set n1=i2+n, m1=i2+m;
          set pn = G*(i1,n1),
              pm = G*(i1,m1);
             l1/.n1=l1.n1 & l1/.m1 = l1.m1 by A63,A313,FINSEQ_4:def 4;
          then A314: l1/.n1=pn & l1/.m1=pm & n1<m1
            by A312,A313,MATRIX_1:def 8,REAL_1:67;
          then y1.n1=pn`2 & y1.m1=pm`2 by A59,A60,A62,A313,GOBOARD1:def 4;
          hence p`2<q`2 by A55,A59,A60,A62,A312,A313,A314,GOBOARD1:def 1;
         end;
        A315: rng g2 c= LSeg(f,k)
         proof let x; assume x in rng g2;
          then consider n such that
          A316: n in dom g2 & g2/.n=x by PARTFUN2:4;
          A317: g2/.n=G*(i1,i2+n) by A285,A316;
          set pn = G*(i1,(i2+n));
             pn`1=ppi`1 & ppi`2<=pn`2 & pn`2<=pj`2 by A306,A316,A317;
          hence x in LSeg(f,k) by A298,A316,A317;
         end;
           rng g1 /\ rng g2 = {}
         proof consider x being Element of rng g1 /\ rng g2;
          assume not thesis;
          then A318: x in rng g1 & x in rng g2 by XBOOLE_0:def 3;
              now per cases by A14,REAL_1:def 5;
           suppose k=1;
            hence contradiction by A37,A303,A318,TARSKI:def 1;
           end;
           suppose 1<k;
            then x in L~f1 /\ LSeg(f,k) & L~f1 /\ LSeg(f,k)={f/.k}
             by A5,A8,A9,A44,A315,A318,GOBOARD2:9,XBOOLE_0:def 3;
            hence contradiction by A303,A318,TARSKI:def 1;
           end;
           end;
          hence contradiction;
         end;
        then rng g1 misses rng g2 by XBOOLE_0:def 7;
        hence g is one-to-one by A29,A302,FINSEQ_3:98;
        A319: for n st 1<=n & n+2 <= len g2 holds
             LSeg(g2,n) /\ LSeg(g2,n+1) = {g2/.(n+1)}
         proof let n; assume
          A320: 1<=n & n+2 <= len g2;
          then A321: n in dom g2 & n+1 in dom g2 & n+2 in dom g2 by GOBOARD2:4;
          then g2/.n in rng g2 & g2/.(n+1) in rng g2 &
          g2/.(n+2) in rng g2 by PARTFUN2:4;
          then A322: g2/.n in lk & g2/.(n+1) in lk &
          g2/.(n+2) in lk by A298,A315;
          then consider u be Point of TOP-REAL 2 such that
          A323: g2/.n=u & u`1=ppi`1 & ppi`2<=u`2 & u`2<=pj`2;
          consider u1 be Point of TOP-REAL 2 such that
          A324: g2/.(n+1)=u1 & u1`1=ppi`1 & ppi`2<=u1`2 & u1`2<=pj`2 by A322;
          consider u2 be Point of TOP-REAL 2 such that
          A325: g2/.(n+2)=u2 & u2`1=ppi`1 & ppi`2<=u2`2 & u2`2<=pj`2 by A322;
          set lg = {w where w is Point of TOP-REAL 2:
                     w`1=ppi`1 & u`2<=w`2 & w`2<=u2`2};
          A326: 1<= n+1 by NAT_1:29;
          A327: n+1+1 = n+(1+1);
             n+1 <= n+2 by AXIOMS:24;
          then n+1 <= len g2 by A320,AXIOMS:22;
          then A328: LSeg(g2,n)=LSeg(u,u1) & LSeg(g2,n+1)=LSeg(u1,u2) &
          LSeg(g2/.n,g2/.(n+2))=LSeg(u,u2)
           by A320,A323,A324,A325,A326,A327,TOPREAL1:def 5;
             n<n+1 & n+1<n+1+1 & n+1+1=n+(1+1) by NAT_1:38;
          then A329: u1`2<u2`2 & u`2<u1`2 by A311,A321,A323,A324,A325;
          then A330: u1`2<=u2`2 & u`2<=u1`2 & u`2<u2`2 by AXIOMS:22;
          A331: u1 in lg & u=|[u`1,u`2]| & u2=|[u2`1,u2`2]| by A324,A329,EUCLID
:57;
          then LSeg(g2/.n,g2/.(n+2))=lg by A323,A325,A330,TOPREAL3:15;
          hence thesis by A324,A328,A331,TOPREAL1:14;
         end;
        thus g is unfolded
         proof let n; assume
          A332: 1<=n & n+2 <= len g;
then A333:           n+1+1<=len g;
          A334: 1<= n+1 by NAT_1:29;
          A335: n<=n+1 & n+1<=n+1+1 by NAT_1:29;
then A336:      n+1 <= len g by A332,AXIOMS:22;
A337:      n+(1+1)=n+1+1;
A338:      len g=len g1+len g2 by FINSEQ_1:35;
              n+2-len g1 = n-len g1 +2;
          then A339: n-len g1 + 2 <= len g2 by A332,A338,REAL_1:86;
           per cases;
           suppose A340: n+2 <= len g1;
          then n in dom g1 & n+1 in dom g1 & n+2 in dom g1 & n+(1+1)=n+1+1
              by A332,GOBOARD2:4;
            then LSeg(g1,n)=LSeg(g,n) & LSeg(g1,n+1)=LSeg(g,n+1) &
            g/.(n+1)=g1/.(n+1) by GROUP_5:95,TOPREAL3:25;
            hence thesis by A30,A332,A340,TOPREAL1:def 8;
           end;
           suppose len g1 < n+2;
            then len g1+1<=n+2 by NAT_1:38;
            then A341: len g1<=n+2-1 by REAL_1:84;
                now per cases;
             suppose A342: len g1=n+1;
                  now assume A343: k=1;
                   1<len g1 by A332,A342,NAT_1:38;
                then 1+1<=len g1 by NAT_1:38;
                hence contradiction by A37,A343,TOPREAL1:29;
               end;
              then A344: 1<k & LSeg(g1,n) c= L~f1
                 by A14,A33,REAL_1:def 5,TOPREAL3:26;
              then A345: L~f1 /\ LSeg(f,k)={f/.k} by A5,A8,A9,GOBOARD2:9;
                 1<=n+1 by NAT_1:29;
        then A346: n in dom g1 & n+1 in dom g1 by A332,A335,A342,FINSEQ_3:27;
              then A347: LSeg(g,n)=LSeg(g1,n) by TOPREAL3:25;
              A348: g/.(n+1)=f1/.len f1
               by A35,A342,A346,GROUP_5:95
              .= ppi by A15,A16,A18,A47,GOBOARD1:10;
                A349: 1<=len g-len g1 by A333,A342,REAL_1:84;
              then A350: g/.(n+2)=g2/.1 by A337,A338,A342,GOBOARD2:5;
                 1 in dom g2 by A338,A349,FINSEQ_3:27;
              then A351: g2/.1 in rng g2 by PARTFUN2:4;
              then g2/.1 in lk by A298,A315;
              then consider u1 be Point of TOP-REAL 2 such that
              A352: g2/.1=u1 & u1`1=ppi`1 & ppi`2<=u1`2 & u1`2<=pj`2;
                 ppi in LSeg(ppi,pj) by TOPREAL1:6;
              then LSeg(ppi,u1) c= LSeg(f,k) & LSeg(g,n+1)=LSeg(ppi,u1)
                by A299,A315,A332,A334,A337,A348,A350,A351,A352,TOPREAL1:12,def
5;
              then A353: LSeg(g,n) /\ LSeg(g,n+1) c= {g/.(n+1)}
               by A47,A344,A345,A347,A348,XBOOLE_1:27;
                 g/.(n+1) in LSeg(g,n) & g/.(n+1) in LSeg(g,n+1)
                by A332,A334,A336,A337,TOPREAL1:27;
              then g/.(n+1) in LSeg(g,n) /\ LSeg(g,n+1) by XBOOLE_0:def 3;
              then {g/.(n+1)} c= LSeg(g,n) /\ LSeg(g,n+1) by ZFMISC_1:37;
              hence thesis by A353,XBOOLE_0:def 10;
             end;
             suppose len g1<>n+1; then len g1<n+1 by A341,REAL_1:def 5;
              then A354: len g1<=n by NAT_1:38;
              then reconsider n1=n-len g1 as Nat by INT_1:18;
                  now per cases;
               suppose A355: len g1=n;
                   1<=len g1 by A14,A16,A36,AXIOMS:22;
                then len g1 in dom g1 by FINSEQ_3:27;
                then A356: g/.n=f1/.len f1 by A35,A355,GROUP_5:95
                .= ppi by A15,A16,A18,A47,GOBOARD1:10;
A357:             2 <= len g2 by A332,A338,A355,REAL_1:53;
                then 1 <= len g2 by AXIOMS:22;
                then A358: g/.(n+1)=g2/.1 by A355,GOBOARD2:5;
                A359: g/.(n+2)=g2/.2 by A355,A357,GOBOARD2:5;
                    1<=len g2 by A357,AXIOMS:22;
                then A360: 1 in dom g2 & 2 in dom g2 by A357,FINSEQ_3:27;
                then g2/.1 in rng g2 & g2/.2 in rng g2 by PARTFUN2:4;
                then A361: g2/.1 in lk & g2/.2 in lk by A298,A315;
                then consider u1 be Point of TOP-REAL 2 such that
                A362: g2/.1=u1 & u1`1=ppi`1 & ppi`2<=u1`2 & u1`2<=pj`2;
                consider u2 be Point of TOP-REAL 2 such that
          A363: g2/.2=u2 & u2`1=ppi`1 & ppi`2<=u2`2 & u2`2<=pj`2 by A361;
                A364: u1`2<u2`2 by A311,A360,A362,A363;
                then A365: u1`2<=u2`2 & ppi`2<u2`2 & u2=|[u2`1,u2`2]|
                  by A362,AXIOMS:22,EUCLID:57;
                set lg = {w where w is Point of TOP-REAL 2 :
                          w`1=ppi`1 & ppi`2<=w`2 & w`2<=u2`2};
                A366: u1 in lg by A362,A364;
                A367: LSeg(g,n)=LSeg(ppi,u1) & LSeg(g,n+1)=LSeg(u1,u2)
               by A332,A334,A336,A337,A356,A358,A359,A362,A363,TOPREAL1:def 5;
                   u1 in LSeg(ppi,u2) by A297,A363,A365,A366,TOPREAL3:15;
                hence thesis by A358,A362,A367,TOPREAL1:14;
               end;
               suppose len g1<>n;
then A368:              len g1<n by A354,REAL_1:def 5;
                   n1 + len g1 = n;
then A369:            LSeg(g,n)=LSeg(g2,n1) by A336,A368,GOBOARD2:10;
A370:            len g1<n+1 by A335,A368,AXIOMS:22;
A371:            len g1+1<=n by A368,NAT_1:38;
A372:             n1+1+len g1 = n+1;
A373:             1<=n1+1 by NAT_1:29;
                   n1+1<=len g2 by A336,A338,A372,REAL_1:53;
                then LSeg(g,n+1)=LSeg(g2,n1+1)& g/.(n+1)=g2/.(n1+1) & 1<=n1
                  by A333,A370,A371,A372,A373,GOBOARD2:5,10,REAL_1:84;
                hence thesis by A319,A339,A369;
               end;
               end;
              hence thesis;
             end;
             end;
            hence thesis;
         end;
         end;
            for n,m st m>n+1 & n in dom g2 & n+1 in dom g2 & m in dom g2 &
         m+1 in dom g2 holds LSeg(g2,n) misses LSeg(g2,m)
         proof let n,m; assume that
          A374: m>n+1 and
          A375: n in dom g2 & n+1 in dom g2 & m in dom g2 & m+1 in dom g2 and
          A376: LSeg(g2,n) /\ LSeg(g2,m) <> {};
          A377: 1 <= n & n+1 <= len g2 & 1 <= m & m+1<=
 len g2 by A375,FINSEQ_3:27;
          reconsider p1=g2/.n, p2=g2/.(n+1), q1=g2/.m, q2=g2/.(m+1)
           as Point of TOP-REAL 2;
          set lp = {w where w is Point of TOP-REAL 2: w`1=ppi`1 &
                         p1`2<=w`2 & w`2<=p2`2},
              lq = {w where w is Point of TOP-REAL 2: w`1=ppi`1 &
                         q1`2<=w`2 & w`2<=q2`2};
              m<m+1 & n<n+1 by NAT_1:38;
           then A378: p1`2<p2`2 & q1`2<q2`2 & p1`1=ppi`1 & p2`1=ppi`1 & q1`1=
ppi`1 &
           q2`1=ppi`1 & p1=|[p1`1,p1`2]| & q1=|[q1`1,q1`2]| &
           p2=|[p2`1,p2`2]| &
           q2=|[q2`1,q2`2]| by A306,A311,A375,EUCLID:57;
           A379: LSeg(g2,n) = LSeg(p1,p2) by A377,TOPREAL1:def 5
           .=lp by A378,TOPREAL3:15;
           A380: LSeg(g2,m) = LSeg(q1,q2) by A377,TOPREAL1:def 5
           .=lq by A378,TOPREAL3:15;
           consider x being Element of LSeg(g2,n) /\ LSeg(g2,m);
           A381: x in LSeg(g2,n) & x in LSeg(g2,m) by A376,XBOOLE_0:def 3;
then A382:           ex tn be Point of TOP-REAL 2 st
 tn=x & tn`1=ppi`1 & p1`2<=tn`2 & tn`2<=p2`2 by A379;
A383:           ex tm be Point of TOP-REAL 2 st
 tm=x & tm`1=ppi`1 & q1`2<=tm`2 & tm`2<=q2`2 by A380,A381;
               p2`2<q1`2 by A311,A374,A375;
            hence contradiction by A382,A383,AXIOMS:22;
         end;
then A384:    g2 is s.n.c. by GOBOARD2:6;
        A385: L~g2 c= LSeg(f,k)
         proof
          set ls={LSeg(g2,m): 1<=m & m+1 <= len g2}; let x; assume
             x in L~g2; then x in union ls by TOPREAL1:def 6;
          then consider X be set such that
          A386: x in X & X in ls by TARSKI:def 4;
          consider m such that
          A387: X=LSeg(g2,m) & 1<=m & m+1 <= len g2 by A386;
              m in dom g2 & m+1 in dom g2 by A387,GOBOARD2:3;
          then A388: g2/.m in rng g2 & g2/.(m+1) in rng g2
           by PARTFUN2:4;
          reconsider q1=g2/.m, q2=g2/.(m+1) as Point of TOP-REAL 2;
          A389: LSeg(q1,q2) c= LSeg(ppi,pj) by A299,A315,A388,TOPREAL1:12;
             LSeg(g2,m)=LSeg(q1,q2) by A387,TOPREAL1:def 5;
          hence thesis by A299,A386,A387,A389;
         end;
        A390: not f/.k in L~g2
         proof set ls={LSeg(g2,m): 1<=m & m+1 <= len g2}; assume
             f/.k in L~g2; then f/.k in union ls by TOPREAL1:def 6;
          then consider X be set such that
          A391: f/.k in X & X in ls by TARSKI:def 4;
          consider m such that
          A392: X=LSeg(g2,m) & 1<=m & m+1 <= len g2 by A391;
          A393: m in dom g2 & m+1 in dom g2 &
          m<m+1 by A392,GOBOARD2:3,NAT_1:38;
          reconsider q1=g2/.m, q2=g2/.(m+1) as Point of TOP-REAL 2;
          set lq={w where w is Point of TOP-REAL 2: w`1=ppi`1 &
                       q1`2<=w`2 & w`2<=q2`2};
          A394: q1`1=ppi`1 & q2`1=ppi`1 & q1`2<q2`2 & q1=|[q1`1,q1`2]|
          & q2=|[q2`1,q2`2]| by A306,A311,A393,EUCLID:57;
             LSeg(g2,m)=LSeg(q1,q2) by A392,TOPREAL1:def 5
          .=lq by A394,TOPREAL3:15;
          then ex p st p=f/.k & p`1=ppi`1 & q1`2<=p`2 & p`2<=q2`2 by A391,A392
;
          hence contradiction by A47,A306,A393;
         end;
        A395: L~g1 /\ L~g2 = {}
         proof
           per cases;
           suppose k=1; hence thesis by A37;
           end;
           suppose k<>1; then 1<k by A14,REAL_1:def 5;
            then L~g1 /\ LSeg(f,k)={f/.k} by A5,A8,A9,A33,GOBOARD2:9;
            then A396: L~g1 /\ L~g2 c= {f/.k} by A385,XBOOLE_1:26;
                now consider x being Element of L~g1 /\ L~g2;
              assume L~g1 /\ L~g2 <> {};
              then x in {f/.k} & x in L~g2 by A396,TARSKI:def 3,XBOOLE_0:def 3
;
              hence contradiction by A390,TARSKI:def 1;
             end;
            hence thesis;
         end;
         end;
           for n,m st m>n+1 & n in dom g &
        n+1 in dom g & m in dom g & m+1 in dom g
         holds LSeg(g,n) misses LSeg(g,m)
         proof let n,m; assume that
          A397: m>n+1 and
          A398: n in dom g & n+1 in dom g & m in dom g & m+1 in dom g;
          A399: 1<=n & 1<=n+1 & n+1<=len g & 1<=m & 1<=m+1 &
          m+1<=len g & n<=n+1 & m<=m+1 & len g=len g1+len g2
            by A398,FINSEQ_1:35,FINSEQ_3:27,NAT_1:29;
          set l1 = {LSeg(g1,i): 1<=i & i+1 <= len g1},
              l2 = {LSeg(g2,j): 1<=j & j+1 <= len g2};
          A400: L~g1=union l1 & L~g2=union l2 & n+1 <= len g & m+1 <= len g
            by A398,FINSEQ_3:27,TOPREAL1:def 6;
             i2+1<=j2 by A284,NAT_1:38;
          then A401: 1<=l by REAL_1:84;
          then A402: 1 in dom g2 by A285,FINSEQ_3:27;
          reconsider qq=g2/.1 as Point of TOP-REAL 2;
          set ql={z where z is Point of TOP-REAL 2: z`1=ppi`1 &
                     ppi`2<=z`2 & z`2<=qq`2};
          A403: 1<=len g1 by A14,A16,A36,AXIOMS:22;
           then len g1 in dom g1 & dom g1 c= dom g by FINSEQ_1:39,FINSEQ_3:27;
          then A404: g/.len g1=g1/.len g1 by GROUP_5:95
             .= ppi by A15,A16,A18,A35,A47,GOBOARD1:10;
          A405: len g1<len g1 +1 & len g1+1<=len g
            by A285,A399,A401,NAT_1:38,REAL_1:55;
          A406: g/.(len g1+1)=qq by A285,A401,GOBOARD2:5;
          A407: qq`1=ppi`1 & qq`2<=pj`2 & qq`2>ppi`2 & qq=|[qq`1,qq`2]|
           by A306,A402,EUCLID:57;
    A408: LSeg(g,len g1)=LSeg(ppi,qq) by A403,A404,A405,A406,TOPREAL1:def 5
           .= ql by A297,A407,TOPREAL3:15;
            A409: n+1<=m+1 by A397,A399,AXIOMS:22;
                now per cases;
             suppose A410: m+1<=len g1;
              then A411: m<=len g1 & n+1<=len g1 by A399,A409,AXIOMS:22;
              then n<=len g1 by A399,AXIOMS:22;
              then n in dom g1 & n+1 in dom g1 & m in dom g1 & m+1 in dom g1
               by A399,A410,A411,FINSEQ_3:27;
              then LSeg(g,n)=LSeg(g1,n) & LSeg(g,m)=LSeg(g1,m)
               by TOPREAL3:25;
              hence thesis by A31,A397,TOPREAL1:def 9;
             end;
             suppose len g1<m+1;
              then A412: len g1<=m by NAT_1:38;
              then reconsider m1=m-len g1 as Nat by INT_1:18;
                  now per cases;
               suppose A413: m=len g1;
                    now assume A414: k=1;
                     1<len g1 by A397,A399,A413,AXIOMS:22;
                  then 1+1<=len g1 by NAT_1:38;
                  hence contradiction by A37,A414,TOPREAL1:29;
                 end;
                then 1<k by A14,REAL_1:def 5;
                then A415: L~f1 /\ LSeg(f,k) ={f/.k} by A5,A8,A9,GOBOARD2:9;
                   n<=len g1 by A397,A399,A413,AXIOMS:22;
                then A416: n in dom g1 & n+1 in dom g1
                by A397,A399,A413,FINSEQ_3:27;
                then A417: LSeg(g,n)=LSeg(g1,n) by TOPREAL3:25;
                then LSeg(g,n) in l1 by A397,A399,A413;
                then A418: LSeg(g,n) c= L~f1 by A33,A400,ZFMISC_1:92;
                   LSeg(g,m) c= LSeg(f,k)
                 proof let x; assume x in LSeg(g,m);
                  then consider px be Point of TOP-REAL 2 such that
          A419: px=x & px`1=ppi`1 & ppi`2<=px`2 & px`2<= qq`2 by A408,A413;
                     pj`2>=px`2 by A407,A419,AXIOMS:22;
                  hence thesis by A298,A419;
                 end;
                then A420: LSeg(g,n) /\ LSeg(g,m) c= {f/.k}
                 by A415,A418,XBOOLE_1:27;
                    now consider x being Element of LSeg(g,n)/\ LSeg(g,m);
                  assume LSeg(g,n)/\ LSeg(g,m)<>{};
                  then A421: x in LSeg(g,n) & x in {f/.k}
                  by A420,TARSKI:def 3,XBOOLE_0:def 3;
                  then A422: x=f/.k by TARSKI:def 1;
                     f/.k=g1/.len g1 by A15,A16,A18,A35,GOBOARD1:10;
                  hence contradiction by A29,A30,A31,A397,A413,A416,A417,A421,
A422,GOBOARD2:7;
                 end;
                hence thesis by XBOOLE_0:def 7;
               end;
               suppose
              m<>len g1;
                then A423: len g1<m & m+1=m1+1+len g1
                 by A412,REAL_1:def 5;
                   m = m1+len g1;
                then A424: LSeg(g,m)=LSeg(g2,m1) by A399,A423,GOBOARD2:10;
                   m1+1<=len g2 & len g1+1<=
m by A399,A423,NAT_1:38,REAL_1:53;
                then A425: 1<=m1 & m1+1 <= len g2 by REAL_1:84;
                then LSeg(g,m) in l2 by A424;
                then A426: LSeg(g,m) c= L~g2 by A400,ZFMISC_1:92;
                    now per cases;
                 suppose A427: n+1<=len g1;
                  then n<=len g1 by A399,AXIOMS:22;
                  then n in dom g1 & n+1 in dom g1 by A399,A427,FINSEQ_3:27;
                   then LSeg(g,n)=LSeg(g1,n) by TOPREAL3:25;
                  then LSeg(g,n) in l1 by A399,A427;
                  then LSeg(g,n) c= L~g1 by A400,ZFMISC_1:92;
                  then LSeg(g,n) /\ LSeg(g,m) c= {} by A395,A426,XBOOLE_1:27;
                  then LSeg(g,n) /\ LSeg(g,m) = {} by XBOOLE_1:3;
                  hence thesis by XBOOLE_0:def 7;
                 end;
                 suppose len g1<n+1;
                  then A428: len g1<=n by NAT_1:38;
                  then reconsider n1=n-len g1 as Nat by INT_1:18;
A429:               n - len g1 + 1 = n + 1 - len g1;
A430:               n = n1 + len g1;
                      now per cases;
                   suppose A431: len g1=n;
                        now consider x being
                      Element of LSeg(g,n) /\ LSeg(g,m);
                      assume LSeg(g,n) /\ LSeg(g,m)<>{};
                then A432: x in LSeg(g,n) & x in LSeg(g,m) by XBOOLE_0:def 3;
then A433:                      ex qx be Point of TOP-REAL 2 st
                      qx=x & qx`1=ppi`1 & ppi`2<=qx`2 & qx`2<=qq`2
                      by A408,A431;
                      A434: m1 in dom g2 & m1+1 in dom g2 & m1<m1+1
                        by A425,GOBOARD2:3,NAT_1:38;
                      reconsider q1=g2/.m1, q2=g2/.(m1+1)
                       as Point of TOP-REAL 2;
                      set q1l={v where v is Point of TOP-REAL 2: v`1=ppi`1 &
                      q1`2<=v`2 & v`2<=q2`2};
A435: q1`1=ppi`1 & q2`1=ppi`1 & q1`2<q2`2 & q1=|[q1`1,q1`2]|&
                      q2=|[q2`1,q2`2]| by A306,A311,A434,EUCLID:57;
                         m1 > n1 + 1 & n1 + 1 >=
 1 by A397,A429,NAT_1:29,REAL_1:54;
then A436:                   m1 > 1 by AXIOMS:22;
                         LSeg(g2,m1)=LSeg(q1,q2) by A425,TOPREAL1:def 5
                      .=q1l by A435,TOPREAL3:15;
then A437:                      ex qy be Point of TOP-REAL 2 st
 qy=x & qy`1=ppi`1 & q1`2<=qy`2 & qy`2<=q2`2 by A424,A432;
                         qq`2 < q1`2 by A311,A402,A434,A436;
                      hence contradiction by A433,A437,AXIOMS:22;
                     end;
                    hence thesis by XBOOLE_0:def 7;
                   end;
                   suppose n<>len g1;
                    then len g1<n by A428,REAL_1:def 5;
                    then LSeg(g,n)=LSeg(g2,n1) & m1>n1+1
                     by A397,A399,A429,A430,GOBOARD2:10,REAL_1:54;
                    hence thesis by A384,A424,TOPREAL1:def 9;
                   end;
                   end;
                  hence thesis;
                 end;
                 end;
                hence thesis;
               end;
               end;
              hence thesis;
             end;
             end;
            hence thesis;
         end;
        hence g is s.n.c. by GOBOARD2:6;
        A438: g2 is special
         proof let n;
          set p = g2/.n, q = g2/.(n+1);
         assume 1<=n & n+1 <= len g2;
          then n in dom g2 & n+1 in dom g2 by GOBOARD2:3;
          then p`1=ppi`1 & q`1=ppi`1 by A306;
          hence p`1=q`1 or p`2=q`2;
         end;
            now
          set p=g1/.len g1, q=g2/.1;
             i2+1<=j2 by A284,NAT_1:38;
          then 1<=l by REAL_1:84;
          then 1 in dom g2 by A285,FINSEQ_3:27;
           then q`1=ppi`1 by A306;
          hence p`1=q`1 or p`2=q`2 by A15,A16,A18,A35,A47,GOBOARD1:10;
         end;
        hence g is special by A32,A438,GOBOARD2:13;
        thus L~g=L~f
         proof
          set lg = {LSeg(g,i): 1<=i & i+1 <= len g},
              lf = {LSeg(f,j): 1<=j & j+1 <= len f};
          A439: len g = len g1 + len g2 by FINSEQ_1:35;
          A440: now let j; assume
            A441: len g1<=j & j<=len g;
            then reconsider w = j-len g1 as Nat by INT_1:18;
            let p such that
            A442: p=g/.j;
             per cases;
             suppose A443: j=len g1;
                 1<=len g1 by A14,A16,A36,AXIOMS:22;
              then len g1 in dom g1 by FINSEQ_3:27;
              then A444: g/.len g1 = f1/.len f1 by A35,GROUP_5:95
              .= G*(i1,i2) by A15,A16,A18,A47,GOBOARD1:10;
              hence p`1=G*(i1,i2)`1 by A442,A443;
              thus G*(i1,i2)`2<=p`2 & p`2<=G*
(i1,j2)`2 by A51,A55,A59,A60,A62,A284,A296,A442,A443,A444,GOBOARD1:def 1;
                  dom l1 = Seg len l1 by FINSEQ_1:def 3;
              hence p in rng l1 by A51,A62,A295,A442,A443,A444,PARTFUN2:4;
             end;
             suppose j<>len g1;
then A445:           len g1 < j by A441,REAL_1:def 5;
A446:           j = w + len g1;
                  len g1 + 1<=j by A445,NAT_1:38;
then A447:           1<=w & w<=len g2 by A439,A441,REAL_1:84,86;
then A448:          g/.j=g2/.w by A446,GOBOARD2:5;
                 w in dom g2 by A447,FINSEQ_3:27;
              hence p`1=ppi`1 & ppi`2<=p`2 & p`2<=
              pj`2 & p in rng l1 by A306,A442,A448;
           end;
           end;
          thus L~g c= L~f
           proof let x; assume x in L~g;
            then x in union lg by TOPREAL1:def 6;
            then consider X be set such that
            A449: x in X & X in lg by TARSKI:def 4;
            consider i such that
            A450: X=LSeg(g,i) & 1<=i & i+1 <= len g by A449;
             per cases;
             suppose A451: i+1 <= len g1;
              then i<=i+1 & i+1<=len g1 by NAT_1:29;
              then 1<=i & i<=len g1 & 1<=i+1 & i+1<=len g1 by A450,AXIOMS:22;
              then i in dom g1 & i+1 in dom g1 by FINSEQ_3:27;
              then X=LSeg(g1,i) by A450,TOPREAL3:25;
              then X in {LSeg(g1,j): 1<=j & j+1 <= len g1} by A450,A451;
              then x in union {LSeg(g1,j): 1<=j & j+1 <= len g1}
                by A449,TARSKI:def 4;
              then x in L~f1 & L~f1 c= L~f by A33,TOPREAL1:def 6,TOPREAL3:27;
              hence thesis;
             end;
             suppose A452: i+1 > len g1;
              then A453: len g1<=i & i<=i+1 & i+1<=len g by A450,NAT_1:38;
              A454: 1<=i+1 & len g1<=i+1 & i<=len g by A450,A452,NAT_1:38;
              reconsider q1=g/.i, q2=g/.(i+1) as Point of TOP-REAL 2;
        A455: q1`1=ppi`1 & ppi`2<=q1`2 & q1`2<=pj`2 & q2`1=ppi`1 & ppi`2<=
q2`2 &
                  q2`2<=pj`2 by A440,A453,A454;
              then A456: q1=|[q1`1,q1`2]| & q2=|[q1`1,q2`2]| by EUCLID:57;
              A457: LSeg(g,i)=LSeg(q2,q1) by A450,TOPREAL1:def 5;
                  now per cases by AXIOMS:21;
               suppose q1`2>q2`2;
                then LSeg(g,i)={p2: p2`1=q1`1 & q2`2<=p2`2 & p2`2<=q1`2}
                  by A456,A457,TOPREAL3:15;
                then consider p2 such that
                A458: p2=x & p2`1=q1`1 & q2`2<=p2`2 & p2`2<=q1`2 by A449,A450;
                   ppi`2<=p2`2 & p2`2<=pj`2 by A455,A458,AXIOMS:22;
                then x in LSeg(f,k) & LSeg(f,k) in lf by A14,A298,A455,A458;
                then x in union lf by TARSKI:def 4;
                hence thesis by TOPREAL1:def 6;
               end;
               suppose q1`2=q2`2;
                then LSeg(g,i)={q1} by A456,A457,TOPREAL1:7;
                then x=q1 by A449,A450,TARSKI:def 1;
                then x in LSeg(f,k) & LSeg(f,k) in lf by A14,A298,A455;
                then x in union lf by TARSKI:def 4;
                hence thesis by TOPREAL1:def 6;
               end;
               suppose q1`2<q2`2;
                then LSeg(g,i)= {p1: p1`1=q1`1 & q1`2<=p1`2 & p1`2<=q2`2}
                  by A456,A457,TOPREAL3:15;
                then consider p2 such that
                A459: p2=x & p2`1=q1`1 & q1`2<=p2`2 & p2`2<=q2`2 by A449,A450;
                   ppi`2<=p2`2 & p2`2<=pj`2 by A455,A459,AXIOMS:22;
                then x in LSeg(f,k) & LSeg(f,k) in lf by A14,A298,A455,A459;
                then x in union lf by TARSKI:def 4;
                hence thesis by TOPREAL1:def 6;
               end;
               end;
              hence thesis;
           end;
           end;
          let x; assume x in L~f;
          then A460: x in L~f1 \/ LSeg(f,k) by A5,A12,GOBOARD2:8;
              now per cases by A460,XBOOLE_0:def 2;
           suppose x in L~f1; then x in L~g1 & L~g1 c= L~g
            by A33,GOBOARD2:11;
            hence x in L~g;
           end;
           suppose x in LSeg(f,k);
            then consider p1 such that
            A461: p1=x & p1`1=ppi`1 & ppi`2<=p1`2 & p1`2<=pj`2 by A298;
            defpred P2[Nat] means
            len g1<=$1 & $1<=len g & for q st q=g/.$1 holds q`2<=p1`2;
            A462: now take n=len g1;
              thus P2[n]
              proof
                 0<=len g2 by NAT_1:18;
               hence len g1<=n & n<=len g by A439,REAL_2:173;
               let q such that
               A463: q=g/.n;
                  1<=len g1 by A14,A16,A36,AXIOMS:22;
               then len g1 in dom g1 by FINSEQ_3:27;
               then q=f1/.len f1
                by A35,A463,GROUP_5:95
               .=G*(i1,i2) by A15,A16,A18,A47,GOBOARD1:10;
               hence q`2<=p1`2 by A461;
              end;
             end;
            A464: for n holds P2[n] implies n<=len g;
            consider ma be Nat such that
            A465: P2[ma] & for n st P2[n] holds n<=ma from NAT_1:sch 6
(A464,A462);
                now per cases;
             suppose A466: ma=len g;
                 i2+1<=j2 by A284,NAT_1:38;
              then A467: 1<=l by REAL_1:84;
              then A468: l in dom g2 & i2+l=j2 by A285,FINSEQ_3:27;
              then A469: g/.ma=g2/.l by A285,A439,A466,GROUP_5:96
               .= pj by A285,A468;
              then pj`2<=p1`2 by A465;
              then A470: p1`2=pj`2 by A461,AXIOMS:21;
              A471: ma-1<=len g & len g1+1<=ma
                by A285,A439,A466,A467,PROB_1:2,REAL_1:55;
              then A472: len g1<=ma-1 & 0<=len g1 by NAT_1:18,REAL_1:84;
then A473:           0+1<=ma by REAL_1:84;
              reconsider m1=ma-1 as Nat by A473,INT_1:18;
                 1<=len g1 by A14,A16,A36,AXIOMS:22;
              then A475: 1<=m1 by A472,AXIOMS:22;
              reconsider q=g/.m1 as Point of TOP-REAL 2;
              A477: q`1=ppi`1 & q`2<=pj`2 & q=|[q`1,q`2]| & m1+1=ma
               by A440,A471,A472,EUCLID:57;
              then A478: LSeg(g,m1)=LSeg(q,pj)
               by A466,A469,A475,TOPREAL1:def 5;
              set lq={e where e is Point of TOP-REAL 2: e`1=ppi`1 & q`2<=e`2 &
                      e`2<=pj`2};
              LSeg(g,m1)=lq by A297,A477,A478,TOPREAL3:15;
              then p1 in LSeg(g,m1) & LSeg(g,m1) in lg
              by A461,A466,A470,A475,A477;
              then x in union lg by A461,TARSKI:def 4;
              hence x in L~g by TOPREAL1:def 6;
             end;
             suppose ma<>len g;
              then ma<len g by A465,REAL_1:def 5;
              then A479: ma+1<=len g & k<=ma & ma<=ma+1
                by A16,A36,A465,AXIOMS:22,NAT_1:38;
              then A480: 1<=ma & len g1<=ma+1 by A14,A465,AXIOMS:22;
              reconsider qa=g/.ma, qa1=g/.(ma+1) as Point of TOP-REAL 2;
              A481: qa`2<=p1`2 by A465;
               A482: now assume qa1`2<=p1`2;
                 then for q holds q=g/.(ma+1) implies q`2<=p1`2;
                then ma+1<=ma by A465,A479,A480;
                hence contradiction by REAL_2:174;
               end;
              then A483: qa`2<qa1`2 & qa`2<=p1`2 & p1`2<=qa1`2 &
              qa`1=ppi`1 & qa1 `1 = ppi`1
              by A440,A465,A479,A480,A481,AXIOMS:22;
              set lma = {p2: p2`1=ppi`1 & qa`2<=p2`2 & p2`2<=qa1`2};
              A484: qa=|[qa`1,qa`2]| & qa1=|[qa1 `1, qa1 `2]| by EUCLID:57;
                 LSeg(g,ma)=LSeg(qa,qa1) by A479,A480,TOPREAL1:def 5
              .= lma by A483,A484,TOPREAL3:15;
              then x in LSeg(g,ma) & LSeg(g,ma) in lg by A461,A479,A480,A481,
A482;
              then x in union lg by TARSKI:def 4;
              hence x in L~g by TOPREAL1:def 6;
             end;
             end;
            hence x in L~g;
           end;
           end;
          hence x in L~g;
         end;
           1<=len g1 by A14,A16,A36,AXIOMS:22;
        then 1 in dom g1 by FINSEQ_3:27;
        hence g/.1=f1/.1 by A34,GROUP_5:95
        .=f/.1 by A15,A19,GOBOARD1:10;
           i2+1<=j2 by A284,NAT_1:38;
        then A485: 1<=l by REAL_1:84;
        then A486: l in dom g2 & len g=len g1 + l by A285,FINSEQ_1:35,FINSEQ_3:
27;
        hence g/.len g=g2/.l by GROUP_5:96
        .=G*(i1,i2+l) by A285,A486
        .=f/.len f by A5,A49,A71;
        thus len f<=len g by A5,A16,A36,A485,A486,REAL_1:55;
        end;
        end;
       hence thesis;
     end;
     suppose A487: i2=j2;
      set ppi = G*(i1,i2), pj = G*(j1,i2);
          now per cases by AXIOMS:21;
       case A488: i1>j1;
        then reconsider l=i1-j1 as Nat by INT_1:18;
        A489: now let n; assume n in Seg l;
          then A490: 1<=n & n<=l & 0<=j1 by FINSEQ_1:3,NAT_1:18;
          then l<=i1 by PROB_1:2;
          then n<=i1 by A490,AXIOMS:22;
          then reconsider w=i1-n as Nat by INT_1:18;
             0<=n & i1-l<=i1-n by A490,AXIOMS:22,REAL_1:92;
          then i1-n<=i1 & i1<=len G & j1<=w & 1<=j1
            by A51,FINSEQ_3:27,PROB_1:2;
          then 1<=w & w<=len G by AXIOMS:22;
          then w in dom G by FINSEQ_3:27;
          hence i1-n is Nat & [i1-n,i2] in Indices G & i1-n in dom G
            by A50,A51,ZFMISC_1:106;
         end;
        defpred P1[Nat,set] means for m st m=i1-$1 holds $2=G*(m,i2);
        A491: now let n; assume n in Seg l;
          then reconsider m=i1-n as Nat by A489;
          take p=G*(m,i2);
          thus P1[n,p];
         end;
        consider g2 such that
        A492: len g2 = l & for n st n in Seg l holds P1[n,g2/.n]
         from GOBOARD1:sch 1(A491);
A493:     dom g2=Seg l by A492,FINSEQ_1:def 3;
        take g=g1^g2;
            now let n; assume A494: n in dom g2;
          then reconsider m=i1-n as Nat by A489,A493;
          take m,k=i2;
          thus [m,k] in Indices G & g2/.n=G*(m,k) by A489,A492,A493,A494;
         end;
        then A495: for n st n in dom g ex i,j st [i,j] in Indices G &
        g/.n=G*(i,j) by A53,GOBOARD1:39;
        A496: now let n; assume
A497:          n in dom g2 & n+1 in dom g2;
          then A498: [i1-n,i2] in Indices G
           & [i1-(n+1),i2] in Indices G by A489,A493;
          reconsider m1=i1-n ,m2=i1-(n+1) as Nat by A489,A493,A497;
          A499: g2/.n=G*(m1,i2) & g2/.(n+1)=G*(m2,i2) by A492,A493,A497;
          let l1,l2,l3,l4 be Nat; assume
             [l1,l2] in Indices G & [l3,l4] in Indices G & g2/.n=G*(l1,l2) &
           g2/.(n+1)=G*(l3,l4);
           then l1=m1 & l2=i2 & l3=m2 & l4=i2 by A498,A499,GOBOARD1:21;
          hence abs(l1-l3)+abs(l2-l4)= abs(i1-n-(i1-(n+1)))+0 by ABSVALUE:7
           .= 1 by ABSVALUE:def 1;
         end;
         now let l1,l2,l3,l4 be Nat; assume
A501: [l1,l2] in Indices G & [l3,l4] in Indices G & g1/.len g1=G*(l1,l2) &
          g2/.1=G*(l3,l4) & len g1 in dom g1 & 1 in dom g2;
          then A502: [i1-1,i2] in Indices G
           & f1/.len f1=f/.k by A15,A16,A18,A489,A493,GOBOARD1:10;
          reconsider m1=i1-1 as Nat by A489,A493,A501;
             g2/.1=G*(m1,i2) by A492,A493,A501;
           then l1=i1 & l2=i2 & l3=m1 & l4=i2
            by A35,A47,A501,A502,GOBOARD1:21;
          hence abs(l1-l3)+abs(l2-l4)=abs(i1-(i1-1))+0 by ABSVALUE:7
          .=1 by ABSVALUE:def 1;
         end;
        then for n st n in dom g & n+1 in dom g holds
         for m,k,i,j st [m,k] in Indices G &
         [i,j] in Indices G & g/.n=G*(m,k) &
          g/.(n+1)=G*(i,j) holds abs(m-i)+abs(k-j)=1 by A53,A496,GOBOARD1:40;
        hence g is_sequence_on G by A495,GOBOARD1:def 11;
        set
        lk={w where w is Point of TOP-REAL 2: w`2=ppi`2 & pj`1<=w`1 & w`1<=
ppi`1};
           c1/.i1=c1.i1 & c1/.j1=c1.j1 by A51,A70,FINSEQ_4:def 4;
        then A503: c1/.i1=ppi & c1/.j1=pj by A51,MATRIX_1:def 9;
        then A504: y2.i1=ppi`2 & y2.j1=pj`2 & x2.i1=ppi`1 & x2.j1=pj`1 & len g2
=l
          by A51,A52,A64,A65,A66,A68,A69,A492,GOBOARD1:def 3,def 4;
        then A505: pj`1<ppi`1 & ppi`2=pj`2 & ppi=|[ppi`1,ppi`2]| & pj=|[pj`1,pj
`2]|
        by A51,A52,A56,A57,A64,A65,A66,A68,A69,A488,EUCLID:57,GOBOARD1:def 1,
def 2;
        A506: LSeg(f,k)=LSeg(pj,ppi) by A14,A47,A49,A487,TOPREAL1:def 5
        .= lk by A505,TOPREAL3:16;
        A507: LSeg(f,k)=LSeg(ppi,pj) by A14,A47,A49,A487,TOPREAL1:def 5;
            now let n,m; assume
          A508: n in dom g2 & m in dom g2 & n<>m;
          then A509: [i1-n,i2] in Indices G& [i1-m,i2] in Indices G by A489,
A493;
          reconsider n1=i1-n, m1=i1-m as Nat by A489,A493,A508;
          A510: g2/.n=G*(n1,i2) & g2/.m=G*(m1,i2) by A492,A493,A508;
          assume g2/.n=g2/.m;
          then n1=m1 by A509,A510,GOBOARD1:21;
          hence contradiction by A508,XCMPLX_1:20;
         end;
         then for n,m st n in dom g2 & m in dom g2 &
         g2/.n = g2/.m holds n = m;
then A511: g2 is one-to-one & Seg len g2=dom g2 by FINSEQ_1:def 3,PARTFUN2:16;
        A512: not f/.k in rng g2
         proof assume f/.k in rng g2;
          then consider n such that
          A513: n in dom g2 & g2/.n=f/.k by PARTFUN2:4;
          A514: [i1-n,i2] in Indices G
            by A489,A492,A511,A513;
          reconsider n1=i1-n as Nat by A489,A492,A511,A513;
             g2/.n=G*(n1,i2) & 0+1<=n by A492,A511,A513,FINSEQ_1:3;
           then n1=i1 & 0<n by A47,A513,A514,GOBOARD1:21,NAT_1:38;
          hence contradiction by SQUARE_1:29;
         end;
        A515: now let n,p; assume
          A516: n in dom g2 & g2/.n=p;
          then A517: P1[n,g2/.n] &
          1<=n & n<=len g2& i1-n in dom G by A489,A492,A511,FINSEQ_1:3;
          reconsider n1=i1-n as Nat by A489,A493,A516;
          set pn = G*(n1,i2);
             c1/.n1=c1.n1 by A70,A517,FINSEQ_4:def 4;
          then A518: g2/.n=pn & c1/.n1=pn & 0<=n & i1-len g2<=n1
            by A517,AXIOMS:22,MATRIX_1:def 9,REAL_1:92;
          then A519: y2.n1=pn`2 & y2.n1=y2.i1 & x2.n1=pn`1 & n1<=i1 & j1<=n1
           by A51,A52,A57,A64,A65,A66,A68,A69,A492,A517,GOBOARD1:def 2,def 3,
def 4,REAL_2:173;
          hence p`2=ppi`2 & pj`1<=p`1 & p`1<=ppi`1
            by A51,A52,A56,A64,A66,A69,A504,A516,A517,A518,GOBOARD2:18;
          thus p in rng c1 by A70,A516,A517,A518,PARTFUN2:4;
             0<n by A517,AXIOMS:22;
          then n1<i1 by SQUARE_1:29;
          hence p`1<ppi`1
           by A51,A52,A56,A64,A66,A69,A504,A516,A517,A518,A519,GOBOARD1:def 1;
         end;
        A520: now let n,m,p,q; assume
          A521: n in dom g2 & m in dom g2 & n<m & g2/.n=p & g2/.m=q;
          then A522: P1[n,g2/.n] & i1-n in dom G &
              P1[m,g2/.m] & i1-m in dom G by A489,A492,A493;
          reconsider n1=i1-n, m1=i1-m as Nat by A489,A493,A521;
          set pn = G*(n1,i2), pm = G*(m1,i2);
             c1/.n1=c1.n1 & c1/.m1 = c1.m1 by A70,A522,FINSEQ_4:def 4;
          then A523: g2/.n=pn & c1/.n1=pn & g2/.m=pm & c1/.m1=pm & m1<n1
            by A521,A522,MATRIX_1:def 9,REAL_1:92;
          then x2.n1=pn`1 & x2.m1=pm`1 by A67,A70,A522,GOBOARD1:def 3;
          hence q`1<p`1 by A56,A67,A70,A521,A522,A523,GOBOARD1:def 1;
         end;
        A524: rng g2 c= LSeg(f,k)
         proof let x; assume x in rng g2;
          then consider n such that
          A525: n in dom g2 & g2/.n=x by PARTFUN2:4;
          reconsider n1=i1-n as Nat by A489,A492,A511,A525;
          set pn = G*(n1,i2);
          A526: g2/.n=pn by A492,A511,A525;
          then pn`2=ppi`2 & pj`1<=pn`1 & pn`1<=ppi`1 by A515,A525;
          hence x in LSeg(f,k) by A506,A525,A526;
         end;
           rng g1 /\ rng g2 = {}
         proof consider x being Element of rng g1 /\ rng g2;
          assume not thesis;
          then A527: x in rng g1 & x in rng g2 by XBOOLE_0:def 3;
              now per cases by A14,REAL_1:def 5;
           suppose k=1;
            hence contradiction by A37,A512,A527,TARSKI:def 1;
           end;
           suppose 1<k;
            then x in L~f1 /\ LSeg(f,k) & L~f1 /\ LSeg(f,k)={f/.k}
             by A5,A8,A9,A44,A524,A527,GOBOARD2:9,XBOOLE_0:def 3;
            hence contradiction by A512,A527,TARSKI:def 1;
           end;
           end;
          hence contradiction;
         end;
        then rng g1 misses rng g2 by XBOOLE_0:def 7;
        hence g is one-to-one
         by A29,A511,FINSEQ_3:98;
        A528: for n st 1<=n & n+2 <= len g2 holds
             LSeg(g2,n) /\ LSeg(g2,n+1) = {g2/.(n+1)}
         proof let n; assume
          A529: 1<=n & n+2 <= len g2;
          then A530: n in dom g2 & n+1 in dom g2 & n+2 in dom g2 by GOBOARD2:4;
          then g2/.n in rng g2 & g2/.(n+1) in rng g2 &
          g2/.(n+2) in rng g2 by PARTFUN2:4;
          then A531: g2/.n in lk & g2/.(n+1) in lk &
          g2/.(n+2) in lk by A506,A524;
          then consider u be Point of TOP-REAL 2 such that
          A532: g2/.n=u & u`2=ppi`2 & pj`1<=u`1 & u`1<=ppi`1;
          consider u1 be Point of TOP-REAL 2 such that
          A533: g2/.(n+1)=u1 & u1`2=ppi`2 & pj`1<=u1`1 & u1`1<=ppi`1 by A531;
          consider u2 be Point of TOP-REAL 2 such that
          A534: g2/.(n+2)=u2 & u2`2=ppi`2 & pj`1<=u2`1 & u2`1<=ppi`1 by A531;
          set lg = {w where w is Point of TOP-REAL 2:
                     w`2=ppi`2 & u2`1<=w`1 & w`1<=u`1};
          A535: 1<= n+1 by NAT_1:29;
          A536: n+1+1 = n+(1+1);
             n+1 <= n+2 by AXIOMS:24;
          then n+1 <= len g2 by A529,AXIOMS:22;
          then A537: LSeg(g2,n)=LSeg(u,u1) & LSeg(g2,n+1)=LSeg(u1,u2) &
          LSeg(g2/.n,g2/.(n+2))=LSeg(u,u2)
           by A529,A532,A533,A534,A535,A536,TOPREAL1:def 5;
             n<n+1 & n+1<n+1+1 & n+1+1=n+(1+1) by NAT_1:38;
          then A538: u2`1<u1`1 & u1`1<u`1 by A520,A530,A532,A533,A534;
          then A539: u2`1<=u1`1 & u1`1<=u`1 & u2`1<u`1 by AXIOMS:22;
          A540: u1 in lg & u=|[u`1,u`2]| & u2=|[u2`1,u2`2]| by A533,A538,EUCLID
:57;
          then LSeg(g2/.n,g2/.(n+2))=lg by A532,A534,A539,TOPREAL3:16;
          hence thesis by A533,A537,A540,TOPREAL1:14;
         end;
        thus g is unfolded
         proof let n; assume
          A541: 1<=n & n+2 <= len g;
then A542:           n+1+1<=len g;
          A543: 1 <= n+1 by NAT_1:29;
          A544: n<=n+1 & n+1<=n+1+1 by NAT_1:29;
then A545:      n+1 <= len g by A541,AXIOMS:22;
A546:      n+(1+1)=n+1+1;
A547:      len g=len g1+len g2 by FINSEQ_1:35;
              n+2-len g1 = n-len g1 +2;
          then A548: n-len g1 + 2 <= len g2 by A541,A547,REAL_1:86;
           per cases;
           suppose A549: n+2 <= len g1;
        then n in dom g1 & n+1 in dom g1 & n+2 in dom g1 & n+(1+1)=n+1+1
              by A541,GOBOARD2:4;
            then LSeg(g1,n)=LSeg(g,n) & LSeg(g1,n+1)=LSeg(g,n+1) &
            g/.(n+1)=g1/.(n+1) by GROUP_5:95,TOPREAL3:25;
            hence thesis by A30,A541,A549,TOPREAL1:def 8;
           end;
           suppose len g1 < n+2;
            then len g1+1<=n+2 by NAT_1:38;
            then A550: len g1<=n+2-1 by REAL_1:84;
             thus thesis
             proof per cases;
             suppose A551: len g1=n+1;
                  now assume A552: k=1;
                   1<len g1 by A541,A551,NAT_1:38;
                then 1+1<=len g1 by NAT_1:38;
                hence contradiction by A37,A552,TOPREAL1:29;
               end;
              then A553: 1<k & LSeg(g1,n) c= L~f1
                 by A14,A33,REAL_1:def 5,TOPREAL3:26;
              then A554: L~f1 /\ LSeg(f,k)={f/.k}
               by A5,A8,A9,GOBOARD2:9;
                 1<=n+1 by NAT_1:29;
              then A555: n in dom g1 & n+1 in dom g1
              by A541,A544,A551,FINSEQ_3:27;
              then A556: LSeg(g,n)=LSeg(g1,n) by TOPREAL3:25;
              A557: g/.(n+1)=f1/.len f1
               by A35,A551,A555,GROUP_5:95
              .= ppi by A15,A16,A18,A47,GOBOARD1:10;
                 1 <= len g2 by A541,A546,A547,A551,REAL_1:53;
              then A558: g/.(n+2)=g2/.1 by A546,A551,GOBOARD2:5;
                 1<=len g-len g1 by A542,A551,REAL_1:84;
              then 1 in dom g2 by A547,FINSEQ_3:27;
              then A559: g2/.1 in rng g2 by PARTFUN2:4;
              then g2/.1 in lk by A506,A524;
              then consider u1 be Point of TOP-REAL 2 such that
              A560: g2/.1=u1 & u1`2=ppi`2 & pj`1<=u1`1 & u1`1<=ppi`1;
                 ppi in LSeg(ppi,pj) by TOPREAL1:6;
              then LSeg(ppi,u1) c= LSeg(f,k) & LSeg(g,n+1)=LSeg(ppi,u1)
                by A507,A524,A541,A543,A546,A557,A558,A559,A560,TOPREAL1:12,def
5;
              then A561: LSeg(g,n) /\ LSeg(g,n+1) c= {g/.(n+1)}
               by A47,A553,A554,A556,A557,XBOOLE_1:27;
                 g/.(n+1) in LSeg(g,n) & g/.(n+1) in LSeg(g,n+1)
                by A541,A543,A545,A546,TOPREAL1:27;
              then g/.(n+1) in LSeg(g,n) /\ LSeg(g,n+1) by XBOOLE_0:def 3;
              then {g/.(n+1)} c= LSeg(g,n) /\ LSeg(g,n+1) by ZFMISC_1:37;
              hence thesis by A561,XBOOLE_0:def 10;
             end;
             suppose len g1<>n+1; then len g1<n+1 by A550,REAL_1:def 5;
              then A562: len g1<=n by NAT_1:38;
              then reconsider n1=n-len g1 as Nat by INT_1:18;
              thus thesis
               proof per cases;
               suppose A563: len g1=n;
                   1<=len g1 by A14,A16,A36,AXIOMS:22;
                then len g1 in dom g1 by FINSEQ_3:27;
                then A564: g/.n=f1/.len f1 by A35,A563,GROUP_5:95
                .= ppi by A15,A16,A18,A47,GOBOARD1:10;
A565:             2 <= len g2 by A541,A547,A563,REAL_1:53;
                then 1<= len g2 by AXIOMS:22;
                then A566: g/.(n+1)=g2/.1 by A563,GOBOARD2:5;
                A567: g/.(n+2)=g2/.2 by A563,A565,GOBOARD2:5;
                   1<=len g2 by A565,AXIOMS:22;
                then A568: 1 in dom g2 & 2 in dom g2 by A565,FINSEQ_3:27;
                then g2/.1 in rng g2 & g2/.2 in rng g2 by PARTFUN2:4;
                then A569: g2/.1 in lk & g2/.2 in lk by A506,A524;
                then consider u1 be Point of TOP-REAL 2 such that
                A570: g2/.1=u1 & u1`2=ppi`2 & pj`1<=u1`1 & u1`1<=ppi`1;
                consider u2 be Point of TOP-REAL 2 such that
                A571:
                g2/.2=u2 & u2`2=ppi`2 & pj`1<=u2`1 & u2`1<=ppi`1 by A569;
                A572: u2`1<u1`1 by A520,A568,A570,A571;
                then A573: u2`1<=u1`1 & u2`1<ppi`1 & u2=|[u2`1,u2`2]|
                  by A570,AXIOMS:22,EUCLID:57;
                set lg = {w where w is Point of TOP-REAL 2 :
                          w`2=ppi`2 & u2`1<=w`1 & w`1<=ppi`1};
                A574: u1 in lg by A570,A572;
          A575: LSeg(ppi,g2/.2)=LSeg(ppi,u2) & LSeg(g,n)=LSeg(ppi,u1) &
                LSeg(g,n+1)=LSeg(u1,u2)
               by A541,A543,A545,A546,A564,A566,A567,A570,A571,TOPREAL1:def 5;
                   LSeg(ppi,g2/.2)=lg by A505,A571,A573,TOPREAL3:16;
                hence thesis by A566,A570,A574,A575,TOPREAL1:14;
               end;
               suppose len g1<>n;
then A576:              len g1<n by A562,REAL_1:def 5;
                   n1 + len g1 = n;
then A577:            LSeg(g,n)=LSeg(g2,n1) by A545,A576,GOBOARD2:10;
A578:            len g1<n+1 by A544,A576,AXIOMS:22;
A579:            len g1+1<=n by A576,NAT_1:38;
A580:            n+1 = n1+1+len g1;
A581:             1<=n1+1 by NAT_1:29;
                 n1 + 1 + len g1 = n + 1;
                then n1+1 <= len g2 by A545,A547,REAL_1:53;
                then LSeg(g,n+1)=LSeg(g2,n1+1)& g/.(n+1)=g2/.(n1+1) & 1<=n1
                  by A542,A578,A579,A580,A581,GOBOARD2:5,10,REAL_1:84;
                hence thesis by A528,A548,A577;
               end;
               end;
             end;
             end;
         end;
         end;
              for n,m st m>n+1 & n in dom g2 & n+1 in dom g2 & m in dom g2 &
           m+1 in dom g2 holds LSeg(g2,n) misses LSeg(g2,m)
         proof let n,m; assume that
          A582: m>n+1 and
          A583: n in dom g2 & n+1 in dom g2 & m in dom g2 & m+1 in dom g2 and
          A584: LSeg(g2,n) /\ LSeg(g2,m) <> {};
          A585: 1 <= n & n+1 <= len g2 & 1 <= m & m+1<=
 len g2 by A583,FINSEQ_3:27;
          reconsider p1=g2/.n, p2=g2/.(n+1), q1=g2/.m, q2=g2/.(m+1)
           as Point of TOP-REAL 2;
          set lp = {w where w is Point of TOP-REAL 2: w`2=ppi`2 &
                         p2`1<=w`1 & w`1<=p1`1},
              lq = {w where w is Point of TOP-REAL 2: w`2=ppi`2 &
                         q2`1<=w`1 & w`1<=q1`1};
              m<m+1 & n<n+1 by NAT_1:38;
           then A586: p2`1<p1`1 & q2`1<q1`1 & p1`2=ppi`2 & p2`2=ppi`2 & q1`2=
ppi`2 &
           q2`2=ppi`2 & p1=|[p1`1,p1`2]| & q1=|[q1`1,q1`2]| &
           p2=|[p2`1,p2`2]| &
           q2=|[q2`1,q2`2]| by A515,A520,A583,EUCLID:57;
           A587: LSeg(g2,n) = LSeg(p2,p1) by A585,TOPREAL1:def 5
           .=lp by A586,TOPREAL3:16;
           A588: LSeg(g2,m) = LSeg(q2,q1) by A585,TOPREAL1:def 5
           .=lq by A586,TOPREAL3:16;
           consider x being Element of LSeg(g2,n) /\ LSeg(g2,m);
           A589: x in LSeg(g2,n) & x in LSeg(g2,m) by A584,XBOOLE_0:def 3;
then A590:           ex tn be Point of TOP-REAL 2 st
               tn=x & tn`2=ppi`2 & p2`1<=tn`1 & tn`1<=p1`1 by A587;
A591:           ex tm be Point of TOP-REAL 2 st
               tm=x & tm`2=ppi`2 & q2`1<=tm`1 & tm`1<=q1`1 by A588,A589;
               q1`1<p2`1 by A520,A582,A583;
            hence contradiction by A590,A591,AXIOMS:22;
         end;
then A592:    g2 is s.n.c. by GOBOARD2:6;
        A593: L~g2 c= LSeg(f,k)
         proof
          set ls={LSeg(g2,m): 1<=m & m+1 <= len g2}; let x; assume
             x in L~g2; then x in union ls by TOPREAL1:def 6;
          then consider X be set such that
          A594: x in X & X in ls by TARSKI:def 4;
          consider m such that
          A595: X=LSeg(g2,m) & 1<=m & m+1 <= len g2 by A594;
              m in dom g2 & m+1 in dom g2 by A595,GOBOARD2:3;
          then A596: g2/.m in rng g2 & g2/.(m+1) in rng g2
           by PARTFUN2:4;
          reconsider q1=g2/.m, q2=g2/.(m+1) as Point of TOP-REAL 2;
          A597: LSeg(q1,q2) c= LSeg(ppi,pj) by A507,A524,A596,TOPREAL1:12;
             LSeg(g2,m)=LSeg(q1,q2) by A595,TOPREAL1:def 5;
          hence thesis by A507,A594,A595,A597;
         end;
        A598: not f/.k in L~g2
         proof set ls={LSeg(g2,m): 1<=m & m+1 <= len g2}; assume
             f/.k in L~g2; then f/.k in union ls by TOPREAL1:def 6;
          then consider X be set such that
          A599: f/.k in X & X in ls by TARSKI:def 4;
          consider m such that
          A600: X=LSeg(g2,m) & 1<=m & m+1 <= len g2 by A599;
          A601: m in dom g2 & m+1 in dom g2 &
          m<m+1 by A600,GOBOARD2:3,NAT_1:38;
          reconsider q1=g2/.m, q2=g2/.(m+1) as Point of TOP-REAL 2;
          set lq={w where w is Point of TOP-REAL 2: w`2=ppi`2 &
                       q2`1<=w`1 & w`1<=q1`1};
          A602: q1`2=ppi`2 & q2`2=ppi`2 & q2`1<q1`1 & q1=|[q1`1,q1`2]|
          & q2=|[q2`1,q2`2]| by A515,A520,A601,EUCLID:57;
             LSeg(g2,m)=LSeg(q2,q1) by A600,TOPREAL1:def 5
          .=lq by A602,TOPREAL3:16;
          then ex p st p=f/.k & p`2=ppi`2 & q2`1<=p`1 & p`1<=q1`1 by A599,A600
;
          hence contradiction by A47,A515,A601;
         end;
        A603: L~g1 /\ L~g2 = {}
         proof
           per cases;
           suppose k=1; hence thesis by A37;
           end;
           suppose k<>1; then 1<k by A14,REAL_1:def 5;
            then L~g1 /\ LSeg(f,k)={f/.k}
             by A5,A8,A9,A33,GOBOARD2:9;
            then A604: L~g1 /\ L~g2 c= {f/.k} by A593,XBOOLE_1:26;
                now consider x being Element of L~g1 /\ L~g2;
              assume L~g1 /\ L~g2 <> {};
              then x in {f/.k} & x in L~g2 by A604,TARSKI:def 3,XBOOLE_0:def 3
;
              hence contradiction by A598,TARSKI:def 1;
             end;
            hence thesis;
         end;
         end;
           for n,m st m>n+1 & n in dom g & n+1 in dom g &
        m in dom g & m+1 in dom g
         holds LSeg(g,n) misses LSeg(g,m)
         proof let n,m; assume that
          A605: m>n+1 and
          A606: n in dom g & n+1 in dom g & m in dom g & m+1 in dom g;
          A607: 1<=n & 1<=n+1 & n+1<=len g & 1<=m & 1<=m+1 &
          m+1<=len g & n<=n+1 & m<=m+1 & len g=len g1+len g2
            by A606,FINSEQ_1:35,FINSEQ_3:27,NAT_1:29;
          set l1 = {LSeg(g1,i): 1<=i & i+1 <= len g1},
              l2 = {LSeg(g2,j): 1<=j & j+1 <= len g2};
          A608: L~g1=union l1 & L~g2=union l2 & n+1 <= len g & m+1 <= len g
            by A606,FINSEQ_3:27,TOPREAL1:def 6;
             j1+1<=i1 by A488,NAT_1:38;
          then A609: 1<=l by REAL_1:84;
          then A610: 1 in dom g2 by A492,FINSEQ_3:27;
          reconsider qq=g2/.1 as Point of TOP-REAL 2;
          set ql={z where z is Point of TOP-REAL 2: z`2=ppi`2 &
                     qq`1<=z`1 & z`1<=ppi`1};
          A611: 1<=len g1 by A14,A16,A36,AXIOMS:22;
           then len g1 in dom g1 & dom g1 c= dom g by FINSEQ_1:39,FINSEQ_3:27;
          then A612: g/.len g1=g1/.len g1 by GROUP_5:95
             .= ppi by A15,A16,A18,A35,A47,GOBOARD1:10;
          A613: len g1<len g1 +1 & len g1+1<=len g
            by A492,A607,A609,NAT_1:38,REAL_1:55;
          A614: g/.(len g1+1)=qq by A492,A609,GOBOARD2:5;
          A615: qq`2=ppi`2 & pj`1<=qq`1 & qq`1<ppi`1 & qq=|[qq`1,qq`2]|
           by A515,A610,EUCLID:57;
    A616: LSeg(g,len g1)=LSeg(qq,ppi) by A611,A612,A613,A614,TOPREAL1:def 5
           .= ql by A505,A615,TOPREAL3:16;
            A617: n+1<=m+1 by A605,A607,AXIOMS:22;
                now per cases;
             suppose A618: m+1<=len g1;
              then A619: m<=len g1 & n+1<=len g1 by A607,A617,AXIOMS:22;
              then n<=len g1 by A607,AXIOMS:22;
              then n in dom g1 & n+1 in dom g1 & m in dom g1 & m+1 in dom g1
               by A607,A618,A619,FINSEQ_3:27;
              then LSeg(g,n)=LSeg(g1,n) & LSeg(g,m)=LSeg(g1,m)
               by TOPREAL3:25;
              hence thesis by A31,A605,TOPREAL1:def 9;
             end;
             suppose len g1<m+1;
              then A620: len g1<=m by NAT_1:38;
              then reconsider m1=m-len g1 as Nat by INT_1:18;
                  now per cases;
               suppose A621: m=len g1;
                    now assume A622: k=1;
                     1<len g1 by A605,A607,A621,AXIOMS:22;
                  then 1+1<=len g1 by NAT_1:38;
                  hence contradiction by A37,A622,TOPREAL1:29;
                 end;
                then 1<k by A14,REAL_1:def 5;
                then A623: L~f1 /\ LSeg(f,k) ={f/.k}
                 by A5,A8,A9,GOBOARD2:9;
                   n<=len g1 by A605,A607,A621,AXIOMS:22;
                then A624: n in dom g1 & n+1 in dom g1
                by A605,A607,A621,FINSEQ_3:27;
                then A625: LSeg(g,n)=LSeg(g1,n) by TOPREAL3:25;
                then LSeg(g,n) in l1 by A605,A607,A621;
                then A626: LSeg(g,n) c= L~f1
                 by A33,A608,ZFMISC_1:92;
                   LSeg(g,m) c= LSeg(f,k)
                 proof let x; assume x in LSeg(g,m);
                  then consider px be Point of TOP-REAL 2 such that
                  A627: px=x & px`2=ppi`2 & qq`1<=px`1 & px`1<=
ppi`1 by A616,A621;
                     pj`1<=px`1 by A615,A627,AXIOMS:22;
                  hence thesis by A506,A627;
                 end;
                then A628: LSeg(g,n) /\ LSeg(g,m) c= {f/.k}
                 by A623,A626,XBOOLE_1:27;
                    now consider x being Element of LSeg(g,n)/\ LSeg(g,m);
                  assume
                   LSeg(g,n)/\ LSeg(g,m)<>{};
                  then A629: x in LSeg(g,n) & x in {f/.k}
                  by A628,TARSKI:def 3,XBOOLE_0:def 3;
                  then A630: x=f/.k by TARSKI:def 1;
                     f/.k=g1/.len g1
                   by A15,A16,A18,A35,GOBOARD1:10;
                  hence contradiction
                  by A29,A30,A31,A605,A621,A624,A625,A629,A630,GOBOARD2:7;
                 end;
                hence thesis by XBOOLE_0:def 7;
               end;
               suppose
              m<>len g1;
                then A631: len g1<m & m+1=m1+1+len g1
                 by A620,REAL_1:def 5;
                   m = m1+len g1;
                then A632: LSeg(g,m)=LSeg(g2,m1) by A607,A631,GOBOARD2:10;
                   m1+1<=len g2 & len g1+1<=
m by A607,A631,NAT_1:38,REAL_1:53;
                then A633: 1<=m1 & m1+1 <= len g2 by REAL_1:84;
                then LSeg(g,m) in l2 by A632;
                then A634: LSeg(g,m) c= L~g2 by A608,ZFMISC_1:92;
                    now per cases;
                 suppose A635: n+1<=len g1;
                  then n<=len g1 by A607,AXIOMS:22;
                  then n in dom g1 & n+1 in dom g1 by A607,A635,FINSEQ_3:27;
                   then LSeg(g,n)=LSeg(g1,n) by TOPREAL3:25;
                  then LSeg(g,n) in l1 by A607,A635;
                  then LSeg(g,n) c= L~g1 by A608,ZFMISC_1:92;
                  then LSeg(g,n) /\ LSeg(g,m) c= {} by A603,A634,XBOOLE_1:27;
                  then LSeg(g,n) /\ LSeg(g,m) = {} by XBOOLE_1:3;
                  hence thesis by XBOOLE_0:def 7;
                 end;
                 suppose len g1<n+1;
                  then A636: len g1<=n by NAT_1:38;
                  then reconsider n1=n-len g1 as Nat by INT_1:18;
A637:               n - len g1 + 1 = n + 1 - len g1;
A638:               n = n1 + len g1;
                      now per cases;
                   suppose A639: len g1=n;
                        now consider x being
                      Element of LSeg(g,n) /\ LSeg(g,m);
                      assume
                       LSeg(g,n) /\ LSeg(g,m)<>{};
                      then A640: x in LSeg(g,n) & x in LSeg(g,m)
                      by XBOOLE_0:def 3;
then A641:                    ex qx be Point of TOP-REAL 2 st
                         qx=x & qx`2=ppi`2 & qq`1<=qx`1 & qx`1<=
ppi`1 by A616,A639;
                      A642: m1 in dom g2 & m1+1 in dom g2 & m1<m1+1
                        by A633,GOBOARD2:3,NAT_1:38;
                      reconsider q1=g2/.m1, q2=g2/.(m1+1)
                       as Point of TOP-REAL 2;
                      set q1l={v where v is Point of TOP-REAL 2: v`2=ppi`2 &
                                 q2`1<=v`1 & v`1<=q1`1};
A643:       q1`2=ppi`2 & q2`2=ppi`2 & q2`1<q1`1 & q1=|[q1`1,q1`2]|&
                      q2=|[q2`1,q2`2]| by A515,A520,A642,EUCLID:57;
                         m1 > n1 + 1 & n1 + 1 >=
 1 by A605,A637,NAT_1:29,REAL_1:54;
then A644:                   m1 > 1 by AXIOMS:22;
                         LSeg(g2,m1)=LSeg(q2,q1) by A633,TOPREAL1:def 5
                      .=q1l by A643,TOPREAL3:16;
then A645:                      ex qy be Point of TOP-REAL 2 st
                          qy=x & qy`2=ppi`2 & q2`1<=qy`1 & qy`1<=
q1`1 by A632,A640;
                         q1`1 < qq`1 by A520,A610,A642,A644;
                      hence contradiction by A641,A645,AXIOMS:22;
                     end;
                    hence thesis by XBOOLE_0:def 7;
                   end;
                   suppose n<>len g1;
                    then len g1<n by A636,REAL_1:def 5;
                    then LSeg(g,n)=LSeg(g2,n1) & m1>n1+1
                     by A605,A607,A637,A638,GOBOARD2:10,REAL_1:54;
                    hence thesis by A592,A632,TOPREAL1:def 9;
                   end;
                   end;
                  hence thesis;
                 end;
                 end;
                hence thesis;
               end;
               end;
              hence thesis;
             end;
             end;
            hence thesis;
         end;
        hence g is s.n.c. by GOBOARD2:6;
        A646: g2 is special
        proof let n;
         set p = g2/.n, q = g2/.(n+1);
        assume
              1<=n & n+1 <= len g2;
          then n in dom g2 & n+1 in dom g2 by GOBOARD2:3;
          then p`2=ppi`2 & q`2=ppi`2 by A515;
          hence p`1=q`1 or p`2=q`2;
         end;
            now
          set p=g1/.len g1, q=g2/.1;
             j1+1<=i1 by A488,NAT_1:38;
          then 1<=l by REAL_1:84;
          then 1 in dom g2 by A493,FINSEQ_1:3;
           then q`2=ppi`2 by A515;
          hence p`1=q`1 or p`2=q`2
           by A15,A16,A18,A35,A47,GOBOARD1:10;
         end;
        hence g is special by A32,A646,GOBOARD2:13;
        thus L~g=L~f
         proof
          set lg = {LSeg(g,i): 1<=i & i+1 <= len g},
              lf = {LSeg(f,j): 1<=j & j+1 <= len f};
          A647: len g = len g1 + len g2 by FINSEQ_1:35;
          A648: now let j; assume
            A649: len g1<=j & j<=len g;
            then reconsider w = j-len g1 as Nat by INT_1:18;
            let p such that
            A650: p=g/.j;
             per cases;
             suppose A651: j=len g1;
                 1<=len g1 by A14,A16,A36,AXIOMS:22;
              then len g1 in dom g1 by FINSEQ_3:27;
              then A652: g/.len g1 = f1/.len f1 by A35,GROUP_5:95
              .= G*(i1,i2) by A15,A16,A18,A47,GOBOARD1:10;
              hence p`2=G*(i1,i2)`2 by A650,A651;
              thus G*(j1,i2)`1<=p`1 & p`1<=G*
              (i1,i2)`1 by A51,A52,A56,A64,A66,A69,A488,A504,A650,A651,A652,
GOBOARD1:def 1;
                 Seg len c1 = dom c1 by FINSEQ_1:def 3;
              hence p in rng c1 by A51,A52,A69,A503,A650,A651,A652,PARTFUN2:4;
             end;
             suppose j<>len g1;
then A653:           len g1 < j by A649,REAL_1:def 5;
A654:           j = w + len g1;
                  len g1 + 1<=j by A653,NAT_1:38;
              then A655:           1<=w & w<=len g2 by A647,A649,REAL_1:84,86;
then A656:          g/.j=g2/.w by A654,GOBOARD2:5;
                 w in dom g2 by A655,FINSEQ_3:27;
              hence p`2=ppi`2 & pj`1<=p`1 & p`1<=
              ppi`1 & p in rng c1 by A515,A650,A656;
           end;
           end;
          thus L~g c= L~f
           proof let x; assume x in L~g;
            then x in union lg by TOPREAL1:def 6;
            then consider X be set such that
            A657: x in X & X in lg by TARSKI:def 4;
            consider i such that
            A658: X=LSeg(g,i) & 1<=i & i+1 <= len g by A657;
             per cases;
             suppose A659: i+1 <= len g1;
              then i<=i+1 & i+1<=len g1 by NAT_1:29;
              then 1<=i & i<=len g1 & 1<=i+1 & i+1<=len g1 by A658,AXIOMS:22;
              then i in dom g1 & i+1 in dom g1 by FINSEQ_3:27;
              then X=LSeg(g1,i) by A658,TOPREAL3:25;
              then X in {LSeg(g1,j): 1<=j & j+1 <= len g1} by A658,A659;
              then x in union {LSeg(g1,j): 1<=j & j+1 <= len g1}
                by A657,TARSKI:def 4;
              then x in L~f1 & L~f1 c= L~f by A33,TOPREAL1:def 6,TOPREAL3:27;
              hence thesis;
             end;
             suppose A660: i+1 > len g1;
              then A661: len g1<=i & i<=i+1 & i+1<=len g by A658,NAT_1:38;
                A662: 1<=i+1 & len g1<=i+1 & i<=len g by A658,A660,NAT_1:38;
              reconsider q1=g/.i, q2=g/.(i+1) as Point of TOP-REAL 2;
              A663: q1`2=ppi`2 & pj`1<=q1`1 & q1`1<=ppi`1 & q2`2=ppi`2 & pj`1<=
q2`1 &
              q2`1<=ppi`1 by A648,A661,A662;
              then A664: q1=|[q1`1,q1`2]| & q2=|[q2`1,q1`2]| by EUCLID:57;
              A665: LSeg(g,i)=LSeg(q2,q1) by A658,TOPREAL1:def 5;
                  now per cases by AXIOMS:21;
               suppose q1`1>q2`1;
                then LSeg(g,i)={p2: p2`2=q1`2 & q2`1<=p2`1 & p2`1<=q1`1}
                  by A664,A665,TOPREAL3:16;
                then consider p2 such that
                A666: p2=x & p2`2=q1`2 & q2`1<=p2`1 & p2`1<=q1`1 by A657,A658;
                   pj`1<=p2`1 & p2`1<=ppi`1 by A663,A666,AXIOMS:22;
                then x in LSeg(f,k) & LSeg(f,k) in lf by A14,A506,A663,A666;
                then x in union lf by TARSKI:def 4;
                hence thesis by TOPREAL1:def 6;
               end;
               suppose q1`1=q2`1;
                then LSeg(g,i)={q1} by A664,A665,TOPREAL1:7;
                then x=q1 by A657,A658,TARSKI:def 1;
                then x in LSeg(f,k) & LSeg(f,k) in lf by A14,A506,A663;
                then x in union lf by TARSKI:def 4;
                hence thesis by TOPREAL1:def 6;
               end;
               suppose q1`1<q2`1;
                then LSeg(g,i)= {p1: p1`2=q1`2 & q1`1<=p1`1 & p1`1<=q2`1}
                  by A664,A665,TOPREAL3:16;
                then consider p2 such that
                A667: p2=x & p2`2=q1`2 & q1`1<=p2`1 & p2`1<=q2`1 by A657,A658;
                   pj`1<=p2`1 & p2`1<=ppi`1 by A663,A667,AXIOMS:22;
                then x in LSeg(f,k) & LSeg(f,k) in lf by A14,A506,A663,A667;
                then x in union lf by TARSKI:def 4;
                hence thesis by TOPREAL1:def 6;
               end;
               end;
              hence thesis;
           end;
           end;
          let x; assume x in L~f;
          then A668: x in L~f1 \/ LSeg(f,k) by A5,A12,GOBOARD2:8;
           per cases by A668,XBOOLE_0:def 2;
           suppose x in L~f1; then x in L~g1 & L~g1 c= L~g
            by A33,GOBOARD2:11;
            hence x in L~g;
           end;
           suppose x in LSeg(f,k);
            then consider p1 such that
            A669: p1=x & p1`2=ppi`2 & pj`1<=p1`1 & p1`1<=ppi`1 by A506;
            defpred P2[Nat] means
            len g1<=$1 & $1<=len g & for q st q=g/.$1 holds q`1>=p1`1;
            A670: now
              take n=len g1;
              thus P2[n]
              proof
                 0<=len g2 by NAT_1:18;
               hence len g1<=n & n<=len g by A647,REAL_2:173;
               let q such that
               A671: q=g/.n;
                  1<=len g1 by A14,A16,A36,AXIOMS:22;
               then len g1 in dom g1 by FINSEQ_3:27;
               then q=f1/.len f1
                by A35,A671,GROUP_5:95
               .=G*(i1,i2) by A15,A16,A18,A47,GOBOARD1:10;
               hence p1`1<=q`1 by A669;
              end;
             end;
            A672: for n holds P2[n] implies n<=len g;
            consider ma be Nat such that
            A673: P2[ma] & for n st P2[n] holds n<=ma from NAT_1:sch 6
(A672,A670);
                now per cases;
             suppose A674: ma=len g;
                 j1+1<=i1 by A488,NAT_1:38;
              then A675: 1<=l & l=len g2 by A492,REAL_1:84;
              then A676: l in dom g2 & i1-l=j1 by FINSEQ_3:27;
              then A677: g/.ma=g2/.l by A492,A647,A674,GROUP_5:96
               .= pj by A492,A493,A676;
              then p1`1<=pj`1 by A673;
              then A678: p1`1=pj`1 by A669,AXIOMS:21;
              A679: ma-1<=len g & len g1+1<=ma
                by A647,A674,A675,PROB_1:2,REAL_1:55;
              then A680: len g1<=ma-1 & 0<=len g1 by NAT_1:18,REAL_1:84;
then A681:           0+1<=ma by REAL_1:84;
              reconsider m1=ma-1 as Nat by A681,INT_1:18;
                 1<=len g1 by A14,A16,A36,AXIOMS:22;
              then A683: 1<=m1 by A680,AXIOMS:22;
              reconsider q=g/.m1 as Point of TOP-REAL 2;
              A685: q`2=ppi`2 & pj`1<=q`1 & q=|[q`1,q`2]| & m1+1=ma
               by A648,A679,A680,EUCLID:57;
              then A686: LSeg(g,m1)=LSeg(pj,q)
               by A674,A677,A683,TOPREAL1:def 5;
              set lq={e where e is Point of TOP-REAL 2: e`2=ppi`2 & pj`1<=e`1 &
                      e`1<=q`1};
              LSeg(g,m1)=lq by A505,A685,A686,TOPREAL3:16;
              then p1 in LSeg(g,m1) & LSeg(g,m1) in lg
              by A669,A674,A678,A683,A685;
              then x in union lg by A669,TARSKI:def 4;
              hence x in L~g by TOPREAL1:def 6;
             end;
             suppose ma<>len g;
              then ma<len g by A673,REAL_1:def 5;
              then A687: ma+1<=len g & k<=ma & ma<=ma+1
                by A16,A36,A673,AXIOMS:22,NAT_1:38;
              then A688: 1<=ma & len g1<=ma+1 by A14,A673,AXIOMS:22;
              reconsider qa=g/.ma, qa1=g/.(ma+1) as Point of TOP-REAL 2;
              A689: p1`1<=qa`1 by A673;
               A690: now assume p1`1<=qa1`1;
                 then for q holds q=g/.(ma+1) implies p1`1<=q`1;
                then ma+1<=ma by A673,A687,A688;
                hence contradiction by REAL_2:174;
               end;
              then A691: qa1`1<qa`1 & qa1`1<=p1`1 & p1`1<=qa`1 &
              qa`2=ppi`2 & qa1 `2 = ppi`2
              by A648,A673,A687,A688,A689,AXIOMS:22;
              set lma = {p2: p2`2=ppi`2 & qa1`1<=p2`1 & p2`1<=qa`1};
              A692: qa=|[qa`1,qa`2]| & qa1=|[qa1 `1, qa1 `2]| by EUCLID:57;
                 LSeg(g,ma)=LSeg(qa1,qa) by A687,A688,TOPREAL1:def 5
              .= lma by A691,A692,TOPREAL3:16;
              then x in LSeg(g,ma) & LSeg(g,ma) in lg by A669,A687,A688,A689,
A690;
              then x in union lg by TARSKI:def 4;
              hence x in L~g by TOPREAL1:def 6;
             end;
             end;
            hence x in L~g;
         end;
         end;
           1<=len g1 by A14,A16,A36,AXIOMS:22;
        then 1 in dom g1 by FINSEQ_3:27;
        hence g/.1=f1/.1
         by A34,GROUP_5:95
        .=f/.1 by A15,A19,GOBOARD1:10;
           j1+1<=i1 by A488,NAT_1:38;
        then A693: 1<=l by REAL_1:84;
        then A694: l in dom g2 & len g=len g1 + len g2 & len g2 = l
         by A493,FINSEQ_1:3,35,def 3;
        reconsider m1=i1-l as Nat;
        thus g/.len g=g2/.l by A694,GROUP_5:96
        .=G*(m1,i2) by A492,A493,A694
        .=f/.len f by A5,A49,A487;
        thus len f<=len g by A5,A16,A36,A693,A694,REAL_1:55;
       end;
       case A695: i1=j1;
           k<>k+1 by NAT_1:38;
        hence contradiction by A7,A15,A47,A48,A49,A487,A695,PARTFUN2:17;
       end;
       case A696: i1<j1;
        then reconsider l=j1-i1 as Nat by INT_1:18;
        deffunc F(Nat) = G*(i1+$1,i2);
        consider g2 such that
        A697: len g2 = l & for n st n in dom g2 holds g2/.n = F(n)
          from GOBOARD2:sch 1;
A698:    Seg l = dom g2 by A697,FINSEQ_1:def 3;
        take g=g1^g2;
        A699: now let n; assume n in Seg l;
          then A700: 1<=n & n<=l by FINSEQ_1:3;
          then n<=n+i1 & i1+n<=l+i1 by NAT_1:29,REAL_1:55;
          then n<=i1+n & i1+n<=j1 & j1<=len G by A51,FINSEQ_3:27;
          then 1<=i1+n & i1+n<=len G by A700,AXIOMS:22;
          hence i1+n in dom G by FINSEQ_3:27;
          hence [i1+n,i2] in Indices G by A50,A51,ZFMISC_1:106;
         end;
        A701: Seg len g2 = dom g2 by FINSEQ_1:def 3;
            now let n such that A702: n in dom g2;
          take m=i1+n,k=i2;
          thus [m,k] in Indices G & g2/.n=G*(m,k) by A697,A699,A701,A702;
         end;
        then A703: for n st n in dom g ex i,j st [i,j] in Indices G &
        g/.n=G*(i,j) by A53,GOBOARD1:39;
        A704: now let n; assume
             n in dom g2 & n+1 in dom g2;
          then A705: g2/.n=G*(i1+n,i2) & [i1+n,i2] in Indices G &
          g2/.(n+1)=G*(i1+(n+1),i2) & [i1+(n+1),i2] in Indices G
          by A697,A699,A701;
          let l1,l2,l3,l4 be Nat; assume
             [l1,l2] in Indices G & [l3,l4] in Indices G & g2/.n=G*(l1,l2) &
           g2/.(n+1)=G*(l3,l4);
           then l1=i1+n & l2=i2 & l3=i1+(n+1) & l4=i2 by A705,GOBOARD1:21;
          hence abs(l1-l3)+abs(l2-l4)= abs(i1+n-(i1+(n+1)))+0 by ABSVALUE:7
           .= abs(-1)
           .= abs(1) by ABSVALUE:17
           .= 1 by ABSVALUE:def 1;
         end;
         now let l1,l2,l3,l4 be Nat; assume
          A707: [l1,l2] in Indices G & [l3,l4] in Indices G & g1/.len g1=G*(l1,
l2) &
          g2/.1=G*(l3,l4) & len g1 in dom g1 & 1 in dom g2;
          then g2/.1=G*
(i1+1,i2) & [i1+1,i2] in Indices G & f1/.len f1=f/.k
            by A15,A16,A18,A697,A699,A701,GOBOARD1:10;
           then l1=i1 & l2=i2 & l3=i1+1 & l4=i2 by A35,A47,A707,GOBOARD1:21;
          hence abs(l1-l3)+abs(l2-l4)=abs(i1-(i1+1))+0 by ABSVALUE:7
          .=abs(i1-i1+-1)
          .=abs(1) by ABSVALUE:17
          .=1 by ABSVALUE:def 1;
         end;
        then for n st n in dom g & n+1 in dom g holds
         for m,k,i,j st [m,k] in Indices G &
         [i,j] in Indices G & g/.n=G*(m,k) &
          g/.(n+1)=G*(i,j) holds abs(m-i)+abs(k-j)=1 by A53,A704,GOBOARD1:40;
        hence g is_sequence_on G by A703,GOBOARD1:def 11;
        set
        lk={w where w is Point of TOP-REAL 2: w`2=ppi`2 & ppi`1<=w`1 & w`1<=
pj`1};
           c1/.i1=c1.i1 & c1/.j1=c1.j1 by A51,A70,FINSEQ_4:def 4;
        then A708: c1/.i1=ppi & c1/.j1=pj by A51,MATRIX_1:def 9;
        then A709: y2.i1=ppi`2 & y2.j1=pj`2 & x2.i1=ppi`1 & x2.j1=pj`1
          by A51,A64,A65,A66,A67,A68,A70,GOBOARD1:def 3,def 4;
        then A710: ppi`1<pj`1 & ppi`2=pj`2 &
        ppi=|[ppi`1,ppi`2]| & pj=|[pj`1,pj`2]|
        by A51,A56,A57,A64,A65,A66,A67,A68,A70,A696,EUCLID:57,GOBOARD1:def 1,
def 2;
        A711: LSeg(f,k)=LSeg(ppi,pj) by A14,A47,A49,A487,TOPREAL1:def 5
        .= lk by A710,TOPREAL3:16;
        A712: LSeg(f,k)=LSeg(ppi,pj) by A14,A47,A49,A487,TOPREAL1:def 5;
            now let n,m; assume
          A713: n in dom g2 & m in dom g2 & n<>m;
          then A714: g2/.n=G*(i1+n,i2) & g2/.m=G*(i1+m,i2) & [i1+n,i2] in
Indices G &
          [i1+m,i2] in Indices G by A697,A699,A701;
          assume g2/.n=g2/.m;
          then i1+n=i1+m by A714,GOBOARD1:21;
          hence contradiction by A713,XCMPLX_1:2;
         end;
         then for n,m st n in dom g2 & m in dom g2 &
         g2/.n = g2/.m holds n = m;
        then A715: g2 is one-to-one by PARTFUN2:16;
        A716: not f/.k in rng g2
         proof assume f/.k in rng g2;
          then consider n such that
          A717: n in dom g2 & g2/.n=f/.k by PARTFUN2:4;
          A718: g2/.n=G*(i1+n,i2) & [i1+n,i2] in Indices G
            by A697,A698,A699,A717;
             0+1<=n by A717,FINSEQ_3:27;
           then i1+n=i1 & 0<n by A47,A717,A718,GOBOARD1:21,NAT_1:38;
          hence contradiction by XCMPLX_1:3;
         end;
        A719: now let n,p; assume
          A720: n in dom g2 & g2/.n=p;
          then A721: g2/.n=G*(i1+n,i2) & 1<=n & n<=len g2 & i1+n in dom G
           by A697,A699,A701,FINSEQ_3:27;
          set n1=i1+n;
          set pn = G*(n1,i2);
             c1/.n1=c1.n1 by A70,A721,FINSEQ_4:def 4;
          then A722: g2/.n=pn & c1/.n1=pn & 0<=n & n1<=i1+len g2
            by A721,AXIOMS:22,MATRIX_1:def 9,REAL_1:55;
          then A723: y2.n1=pn`2 & y2.n1=y2.i1 & x2.n1=pn`1 & n1<=j1 & i1<=n1
          by A51,A57,A64,A65,A66,A67,A68,A70,A697,A721,GOBOARD1:def 2,def 3,def
4,REAL_2:173;
          hence p`2=ppi`2 & ppi`1<=p`1 & p`1<=pj`1
            by A51,A56,A67,A70,A709,A720,A721,GOBOARD2:18;
          thus p in rng c1 by A70,A720,A721,A722,PARTFUN2:4;
             0<n by A721,AXIOMS:22;
          then i1<n1 by REAL_2:174;
          hence p`1>ppi`1 by A51,A56,A67,A70,A709,A720,A721,A723,GOBOARD1:def
1
;
         end;
        A724: now let n,m,p,q; assume
          A725: n in dom g2 & m in dom g2 & n<m & g2/.n=p & g2/.m=q;
          then A726: g2/.n=G*(i1+n,i2) & i1+n in dom G &
              g2/.m=G*(i1+m,i2) & i1+m in dom G by A697,A699,A701;
          set n1=i1+n, m1=i1+m;
          set pn = G*(n1,i2),
              pm = G*(m1,i2);
             c1/.n1=c1.n1 & c1/.m1 = c1.m1 by A70,A726,FINSEQ_4:def 4;
          then A727: c1/.n1=pn & c1/.m1=pm & n1<m1
            by A725,A726,MATRIX_1:def 9,REAL_1:67;
          then x2.n1=pn`1 & x2.m1=pm`1 by A67,A70,A726,GOBOARD1:def 3;
          hence p`1<q`1 by A56,A67,A70,A725,A726,A727,GOBOARD1:def 1;
         end;
        A728: rng g2 c= LSeg(f,k)
         proof let x; assume x in rng g2;
          then consider n such that
          A729: n in dom g2 & g2/.n=x by PARTFUN2:4;
          A730: g2/.n=G*(i1+n,i2) by A697,A729;
          set pn = G*((i1+n),i2);
             pn`2=ppi`2 & ppi`1<=pn`1 & pn`1<=pj`1 by A719,A729,A730;
          hence x in LSeg(f,k) by A711,A729,A730;
         end;
           rng g1 /\ rng g2 = {}
         proof consider x being Element of rng g1 /\ rng g2;
          assume not thesis;
          then A731: x in rng g1 & x in rng g2 by XBOOLE_0:def 3;
              now per cases by A14,REAL_1:def 5;
           suppose k=1;
            hence contradiction by A37,A716,A731,TARSKI:def 1;
           end;
           suppose 1<k;
            then x in L~f1 /\ LSeg(f,k) & L~f1 /\ LSeg(f,k)={f/.k}
             by A5,A8,A9,A44,A728,A731,GOBOARD2:9,XBOOLE_0:def 3;
            hence contradiction by A716,A731,TARSKI:def 1;
           end;
           end;
          hence contradiction;
         end;
        then rng g1 misses rng g2 by XBOOLE_0:def 7;
        hence g is one-to-one by A29,A715,FINSEQ_3:98;
        A732: for n st 1<=n & n+2 <= len g2 holds
             LSeg(g2,n) /\ LSeg(g2,n+1) = {g2/.(n+1)}
         proof let n; assume
          A733:1<=n & n+2 <= len g2;
          then A734: n in dom g2 & n+1 in dom g2 & n+2 in dom g2 by GOBOARD2:4;
          then g2/.n in rng g2 & g2/.(n+1) in rng g2 &
          g2/.(n+2) in rng g2 by PARTFUN2:4;
          then A735: g2/.n in lk & g2/.(n+1) in lk &
          g2/.(n+2) in lk by A711,A728;
          then consider u be Point of TOP-REAL 2 such that
          A736: g2/.n=u & u`2=ppi`2 & ppi`1<=u`1 & u`1<=pj`1;
          consider u1 be Point of TOP-REAL 2 such that
          A737: g2/.(n+1)=u1 & u1`2=ppi`2 & ppi`1<=u1`1 & u1`1<=pj`1 by A735;
          consider u2 be Point of TOP-REAL 2 such that
          A738: g2/.(n+2)=u2 & u2`2=ppi`2 & ppi`1<=u2`1 & u2`1<=pj`1 by A735;
          set lg = {w where w is Point of TOP-REAL 2:
                     w`2=ppi`2 & u`1<=w`1 & w`1<=u2`1};
          A739: 1<= n+1 by NAT_1:29;
          A740: n+1+1 = n+(1+1);
             n+1 <= n+2 by AXIOMS:24;
          then n+1 <= len g2 by A733,AXIOMS:22;
          then A741: LSeg(g2,n)=LSeg(u,u1) & LSeg(g2,n+1)=LSeg(u1,u2) &
          LSeg(g2/.n,g2/.(n+2))=LSeg(u,u2)
           by A733,A736,A737,A738,A739,A740,TOPREAL1:def 5;
             n<n+1 & n+1<n+1+1 & n+1+1=n+(1+1) by NAT_1:38;
          then A742: u1`1<u2`1 & u`1<u1`1 by A724,A734,A736,A737,A738;
          then A743: u1`1<=u2`1 & u`1<=u1`1 & u`1<u2`1 by AXIOMS:22;
          A744: u1 in lg & u=|[u`1,u`2]| & u2=|[u2`1,u2`2]| by A737,A742,EUCLID
:57;
          then LSeg(g2/.n,g2/.(n+2))=lg by A736,A738,A743,TOPREAL3:16;
          hence thesis by A737,A741,A744,TOPREAL1:14;
         end;
        thus g is unfolded
         proof let n; assume
          A745: 1<=n & n+2 <= len g;
then A746:           n+1+1<=len g;
          A747: 1<= n+1 by NAT_1:29;
          A748: n<=n+1 & n+1<=n+1+1 by NAT_1:29;
then A749:      n+1 <= len g by A745,AXIOMS:22;
A750:      n+(1+1)=n+1+1;
A751:      len g=len g1+len g2 by FINSEQ_1:35;
              n+2-len g1 = n-len g1 +2;
          then A752: n-len g1 + 2 <= len g2 by A745,A751,REAL_1:86;
           per cases;
           suppose A753: n+2 <= len g1;
        then n in dom g1 & n+1 in dom g1 & n+2 in dom g1 & n+(1+1)=n+1+1
              by A745,GOBOARD2:4;
            then LSeg(g1,n)=LSeg(g,n) & LSeg(g1,n+1)=LSeg(g,n+1) &
            g/.(n+1)=g1/.(n+1) by GROUP_5:95,TOPREAL3:25;
            hence thesis by A30,A745,A753,TOPREAL1:def 8;
           end;
           suppose len g1 < n+2;
            then len g1+1<=n+2 by NAT_1:38;
            then A754: len g1<=n+2-1 by REAL_1:84;
                now per cases;
             suppose A755: len g1=n+1;
                  now assume A756: k=1;
                   1<len g1 by A745,A755,NAT_1:38;
                then 1+1<=len g1 by NAT_1:38;
                hence contradiction by A37,A756,TOPREAL1:29;
               end;
              then A757: 1<k & LSeg(g1,n) c= L~f1
                 by A14,A33,REAL_1:def 5,TOPREAL3:26;
              then A758: L~f1 /\ LSeg(f,k)={f/.k} by A5,A8,A9,GOBOARD2:9;
                 1<=n+1 by NAT_1:29;
              then A759: n in dom g1 & n+1 in dom g1
              by A745,A748,A755,FINSEQ_3:27;
              then A760: LSeg(g,n)=LSeg(g1,n) by TOPREAL3:25;
              A761: g/.(n+1)=f1/.len f1 by A35,A755,A759,GROUP_5:95
              .= ppi by A15,A16,A18,A47,GOBOARD1:10;
                 n+2 = 1+len g1 & 1 <= len g2 by A745,A750,A751,A755,REAL_1:53;
              then A762: g/.(n+2)=g2/.1 by GOBOARD2:5;
                 1<=len g-len g1 by A746,A755,REAL_1:84;
              then 1 in dom g2 by A751,FINSEQ_3:27;
              then A763: g2/.1 in rng g2 by PARTFUN2:4;
              then g2/.1 in lk by A711,A728;
              then consider u1 be Point of TOP-REAL 2 such that
              A764: g2/.1=u1 & u1`2=ppi`2 & ppi`1<=u1`1 & u1`1<=pj`1;
                 ppi in LSeg(ppi,pj) by TOPREAL1:6;
              then LSeg(ppi,u1) c= LSeg(f,k) & LSeg(g,n+1)=LSeg(ppi,u1)
                by A712,A728,A745,A747,A750,A761,A762,A763,A764,TOPREAL1:12,def
5;
              then A765: LSeg(g,n) /\ LSeg(g,n+1) c= {g/.(n+1)}
               by A47,A757,A758,A760,A761,XBOOLE_1:27;
                 g/.(n+1) in LSeg(g,n) & g/.(n+1) in LSeg(g,n+1)
                by A745,A747,A749,A750,TOPREAL1:27;
              then g/.(n+1) in LSeg(g,n) /\ LSeg(g,n+1) by XBOOLE_0:def 3;
              then {g/.(n+1)} c= LSeg(g,n) /\ LSeg(g,n+1) by ZFMISC_1:37;
              hence thesis by A765,XBOOLE_0:def 10;
             end;
             suppose len g1<>n+1; then len g1<n+1 by A754,REAL_1:def 5;
              then A766: len g1<=n by NAT_1:38;
              then reconsider n1=n-len g1 as Nat by INT_1:18;
                  now per cases;
               suppose A767: len g1=n;
                   1<=len g1 by A14,A16,A36,AXIOMS:22;
                then len g1 in dom g1 by FINSEQ_3:27;
                then A768: g/.n=f1/.len f1 by A35,A767,GROUP_5:95
                .= ppi by A15,A16,A18,A47,GOBOARD1:10;
A769:             2 <= len g2 by A745,A751,A767,REAL_1:53;
                then 1 <= len g2 by AXIOMS:22;
                then A770: g/.(n+1)=g2/.1 by A767,GOBOARD2:5;
                A771: g/.(n+2)=g2/.2 by A767,A769,GOBOARD2:5;
                    1<=len g2 by A769,AXIOMS:22;
                then A772: 1 in dom g2 & 2 in dom g2 by A769,FINSEQ_3:27;
                then g2/.1 in rng g2 & g2/.2 in rng g2 by PARTFUN2:4;
                then A773: g2/.1 in lk & g2/.2 in lk by A711,A728;
                then consider u1 be Point of TOP-REAL 2 such that
                A774: g2/.1=u1 & u1`2=ppi`2 & ppi`1<=u1`1 & u1`1<=pj`1;
                consider u2 be Point of TOP-REAL 2 such that
          A775: g2/.2=u2 & u2`2=ppi`2 & ppi`1<=u2`1 & u2`1<=pj`1 by A773;
                A776: u1`1<u2`1 by A724,A772,A774,A775;
                then A777: u1`1<=u2`1 & ppi`1<u2`1 & u2=|[u2`1,u2`2]|
                  by A774,AXIOMS:22,EUCLID:57;
                set lg = {w where w is Point of TOP-REAL 2 :
                          w`2=ppi`2 & ppi`1<=w`1 & w`1<=u2`1};
                A778: u1 in lg by A774,A776;
                A779: LSeg(g,n)=LSeg(ppi,u1) & LSeg(g,n+1)=LSeg(u1,u2)
             by A745,A747,A749,A750,A768,A770,A771,A774,A775,TOPREAL1:def 5;
                   u1 in LSeg(ppi,u2) by A710,A775,A777,A778,TOPREAL3:16;
                hence thesis by A770,A774,A779,TOPREAL1:14;
               end;
               suppose len g1<>n;
then A780:              len g1<n by A766,REAL_1:def 5;
                   n1 + len g1 = n;
then A781:            LSeg(g,n)=LSeg(g2,n1) by A749,A780,GOBOARD2:10;
A782:            len g1<n+1 by A748,A780,AXIOMS:22;
A783:            len g1+1<=n by A780,NAT_1:38;
A784:            n+1 = n1+1+len g1;
A785:             1<=n1+1 by NAT_1:29;
                   n1+1<=len g2 by A749,A751,A784,REAL_1:53;
                then LSeg(g,n+1)=LSeg(g2,n1+1)& g/.(n+1)=g2/.(n1+1) & 1<=n1
                  by A746,A782,A783,A784,A785,GOBOARD2:5,10,REAL_1:84;
                hence thesis by A732,A752,A781;
               end;
               end;
              hence thesis;
             end;
             end;
            hence thesis;
         end;
         end;
              for n,m st m>n+1 & n in dom g2 & n+1 in dom g2 & m in dom g2 &
           m+1 in dom g2 holds LSeg(g2,n) misses LSeg(g2,m)
         proof let n,m; assume that
          A786: m>n+1 and
          A787: n in dom g2 & n+1 in dom g2 & m in dom g2 & m+1 in dom g2 and
          A788: LSeg(g2,n) /\ LSeg(g2,m) <> {};
          A789: 1 <= n & n+1 <= len g2 & 1 <= m & m+1<=
 len g2 by A787,FINSEQ_3:27;
          reconsider p1=g2/.n, p2=g2/.(n+1), q1=g2/.m, q2=g2/.(m+1)
           as Point of TOP-REAL 2;
          set lp = {w where w is Point of TOP-REAL 2: w`2=ppi`2 &
                         p1`1<=w`1 & w`1<=p2`1},
              lq = {w where w is Point of TOP-REAL 2: w`2=ppi`2 &
                         q1`1<=w`1 & w`1<=q2`1};
              m<m+1 & n<n+1 by NAT_1:38;
           then A790: p1`1<p2`1 & q1`1<q2`1 & p1`2=ppi`2 & p2`2=ppi`2 & q1`2=
ppi`2 &
           q2`2=ppi`2 & p1=|[p1`1,p1`2]| & q1=|[q1`1,q1`2]| &
           p2=|[p2`1,p2`2]| &
           q2=|[q2`1,q2`2]| by A719,A724,A787,EUCLID:57;
           A791: LSeg(g2,n) = LSeg(p1,p2) by A789,TOPREAL1:def 5
           .=lp by A790,TOPREAL3:16;
           A792: LSeg(g2,m) = LSeg(q1,q2) by A789,TOPREAL1:def 5
           .=lq by A790,TOPREAL3:16;
           consider x being Element of LSeg(g2,n) /\ LSeg(g2,m);
           A793: x in LSeg(g2,n) & x in LSeg(g2,m) by A788,XBOOLE_0:def 3;
then A794:           ex tn be Point of TOP-REAL 2 st
 tn=x & tn`2=ppi`2 & p1`1<=tn`1 & tn`1<=p2`1 by A791;
A795:           ex tm be Point of TOP-REAL 2 st
 tm=x & tm`2=ppi`2 & q1`1<=tm`1 & tm`1<=q2`1 by A792,A793;
               p2`1<q1`1 by A724,A786,A787;
            hence contradiction by A794,A795,AXIOMS:22;
         end;
then A796:    g2 is s.n.c. by GOBOARD2:6;
        A797: L~g2 c= LSeg(f,k)
         proof
          set ls={LSeg(g2,m): 1<=m & m+1 <= len g2}; let x; assume
             x in L~g2; then x in union ls by TOPREAL1:def 6;
          then consider X be set such that
          A798: x in X & X in ls by TARSKI:def 4;
          consider m such that
          A799: X=LSeg(g2,m) & 1<=m & m+1 <= len g2 by A798;
              m in dom g2 & m+1 in dom g2 by A799,GOBOARD2:3;
          then A800: g2/.m in rng g2 & g2/.(m+1) in rng g2 by PARTFUN2:4;
          reconsider q1=g2/.m, q2=g2/.(m+1) as Point of TOP-REAL 2;
          A801: LSeg(q1,q2) c= LSeg(ppi,pj) by A712,A728,A800,TOPREAL1:12;
             LSeg(g2,m)=LSeg(q1,q2) by A799,TOPREAL1:def 5;
          hence thesis by A712,A798,A799,A801;
         end;
        A802: not f/.k in L~g2
         proof set ls={LSeg(g2,m): 1<=m & m+1 <= len g2}; assume
             f/.k in L~g2; then f/.k in union ls by TOPREAL1:def 6;
          then consider X be set such that
          A803: f/.k in X & X in ls by TARSKI:def 4;
          consider m such that
          A804: X=LSeg(g2,m) & 1<=m & m+1 <= len g2 by A803;
          A805: m in dom g2 & m+1 in dom g2 & m<m+1
          by A804,GOBOARD2:3,NAT_1:38;
          reconsider q1=g2/.m, q2=g2/.(m+1) as Point of TOP-REAL 2;
          set lq={w where w is Point of TOP-REAL 2: w`2=ppi`2 &
                       q1`1<=w`1 & w`1<=q2`1};
          A806: q1`2=ppi`2 & q2`2=ppi`2 & q1`1<q2`1 & q1=|[q1`1,q1`2]|
          & q2=|[q2`1,q2`2]| by A719,A724,A805,EUCLID:57;
             LSeg(g2,m)=LSeg(q1,q2) by A804,TOPREAL1:def 5
          .=lq by A806,TOPREAL3:16;
          then ex p st p=f/.k & p`2=ppi`2 & q1`1<=p`1 & p`1<=q2`1 by A803,A804
;
          hence contradiction by A47,A719,A805;
         end;
        A807: L~g1 /\ L~g2 = {}
         proof per cases;
           suppose k=1; hence thesis by A37;
           end;
           suppose k<>1; then 1<k by A14,REAL_1:def 5;
            then L~g1 /\ LSeg(f,k)={f/.k}
             by A5,A8,A9,A33,GOBOARD2:9;
             then A808: L~g1 /\ L~g2 c= {f/.k} by A797,XBOOLE_1:26;
                now consider x being Element of L~g1 /\ L~g2;
              assume L~g1 /\ L~g2 <> {};
              then x in {f/.k} & x in L~g2 by A808,TARSKI:def 3,XBOOLE_0:def 3
;
              hence contradiction by A802,TARSKI:def 1;
             end;
            hence thesis;
         end;
         end;
           for n,m st m>n+1 & n in dom g & n+1 in dom g &
        m in dom g & m+1 in dom g
         holds LSeg(g,n) misses LSeg(g,m)
         proof let n,m; assume that
          A809: m>n+1 and
          A810: n in dom g & n+1 in dom g & m in dom g & m+1 in dom g;
          A811: 1<=n & 1<=n+1 & n+1<=len g & 1<=m & 1<=m+1 &
          m+1<=len g & n<=n+1 & m<=m+1 & len g=len g1+len g2
            by A810,FINSEQ_1:35,FINSEQ_3:27,NAT_1:29;
          set l1 = {LSeg(g1,i): 1<=i & i+1 <= len g1},
              l2 = {LSeg(g2,j): 1<=j & j+1 <= len g2};
          A812: L~g1=union l1 & L~g2=union l2 & n+1 <= len g & m+1 <= len g
            by A810,FINSEQ_3:27,TOPREAL1:def 6;
             i1+1<=j1 by A696,NAT_1:38;
          then A813: 1<=l by REAL_1:84;
          then A814: 1 in dom g2 by A697,FINSEQ_3:27;
          reconsider qq=g2/.1 as Point of TOP-REAL 2;
          set ql={z where z is Point of TOP-REAL 2: z`2=ppi`2 &
                     ppi`1<=z`1 & z`1<=qq`1};
          A815: 1<=len g1 by A14,A16,A36,AXIOMS:22;
           then len g1 in dom g1 & dom g1 c= dom g by FINSEQ_1:39,FINSEQ_3:27;
          then A816: g/.len g1=g1/.len g1 by GROUP_5:95
             .= ppi by A15,A16,A18,A35,A47,GOBOARD1:10;
          A817: len g1<len g1 +1 & len g1+1<=len g
            by A697,A811,A813,NAT_1:38,REAL_1:55;
          A818: g/.(len g1+1)=qq by A697,A813,GOBOARD2:5;
          A819: qq`2=ppi`2 & qq`1<=pj`1 & qq`1>ppi`1 & qq=|[qq`1,qq`2]|
           by A719,A814,EUCLID:57;
          A820: LSeg(g,len g1)=LSeg(ppi,qq)
          by A815,A816,A817,A818,TOPREAL1:def 5
           .= ql by A710,A819,TOPREAL3:16;
            A821: n+1<=m+1 by A809,A811,AXIOMS:22;
                now per cases;
             suppose A822: m+1<=len g1;
              then A823: m<=len g1 & n+1<=len g1 by A811,A821,AXIOMS:22;
              then n<=len g1 by A811,AXIOMS:22;
              then n in dom g1 & n+1 in dom g1 & m in dom g1 & m+1 in dom g1
               by A811,A822,A823,FINSEQ_3:27;
              then LSeg(g,n)=LSeg(g1,n) & LSeg(g,m)=LSeg(g1,m) by TOPREAL3:25;
              hence thesis by A31,A809,TOPREAL1:def 9;
             end;
             suppose len g1<m+1;
              then A824: len g1<=m by NAT_1:38;
              then reconsider m1=m-len g1 as Nat by INT_1:18;
                  now per cases;
               suppose A825: m=len g1;
                    now assume A826: k=1;
                     1<len g1 by A809,A811,A825,AXIOMS:22;
                  then 1+1<=len g1 by NAT_1:38;
                  hence contradiction by A37,A826,TOPREAL1:29;
                 end;
                then 1<k by A14,REAL_1:def 5;
                then A827: L~f1 /\ LSeg(f,k) ={f/.k}
                 by A5,A8,A9,GOBOARD2:9;
                   n<=len g1 by A809,A811,A825,AXIOMS:22;
                then A828: n in dom g1 & n+1 in dom g1
                by A809,A811,A825,FINSEQ_3:27;
                then A829: LSeg(g,n)=LSeg(g1,n) by TOPREAL3:25;
                then LSeg(g,n) in l1 by A809,A811,A825;
                then A830: LSeg(g,n) c= L~f1
                 by A33,A812,ZFMISC_1:92;
                   LSeg(g,m) c= LSeg(f,k)
                 proof let x; assume x in LSeg(g,m);
                  then consider px be Point of TOP-REAL 2 such that
            A831: px=x & px`2=ppi`2 & ppi`1<=px`1 & px`1<=qq`1 by A820,A825;
                     pj`1>=px`1 by A819,A831,AXIOMS:22;
                  hence thesis by A711,A831;
                 end;
                then A832: LSeg(g,n) /\ LSeg(g,m) c= {f/.k}
                 by A827,A830,XBOOLE_1:27;
                    now consider x being Element of LSeg(g,n)/\ LSeg(g,m);
                  assume
                   LSeg(g,n)/\ LSeg(g,m)<>{};
                  then A833: x in LSeg(g,n) & x in {f/.k}
                  by A832,TARSKI:def 3,XBOOLE_0:def 3;
                  then A834: x=f/.k by TARSKI:def 1;
                     f/.k=g1/.len g1
                   by A15,A16,A18,A35,GOBOARD1:10;
                  hence contradiction by A29,A30,A31,A809,A825,A828,A829,A833,
A834,GOBOARD2:7;
                 end;
                hence thesis by XBOOLE_0:def 7;
               end;
               suppose
              m<>len g1;
                then A835: len g1<m & m+1=m1+1+len g1
                 by A824,REAL_1:def 5;
                   m = m1+len g1;
                then A836: LSeg(g,m)=LSeg(g2,m1) by A811,A835,GOBOARD2:10;
                   m1+1<=len g2 & len g1+1<=
m by A811,A835,NAT_1:38,REAL_1:53;
                then A837: 1<=m1 & m1+1 <= len g2 by REAL_1:84;
                then LSeg(g,m) in l2 by A836;
                then A838: LSeg(g,m) c= L~g2 by A812,ZFMISC_1:92;
                    now per cases;
                 suppose A839: n+1<=len g1;
                  then n<=len g1 by A811,AXIOMS:22;
                  then n in dom g1 & n+1 in dom g1 by A811,A839,FINSEQ_3:27;
                   then LSeg(g,n)=LSeg(g1,n) by TOPREAL3:25;
                  then LSeg(g,n) in l1 by A811,A839;
                  then LSeg(g,n) c= L~g1 by A812,ZFMISC_1:92;
                  then LSeg(g,n) /\ LSeg(g,m) c= {} by A807,A838,XBOOLE_1:27;
                  then LSeg(g,n) /\ LSeg(g,m) = {} by XBOOLE_1:3;
                  hence thesis by XBOOLE_0:def 7;
                 end;
                 suppose len g1<n+1;
                  then A840: len g1<=n by NAT_1:38;
                  then reconsider n1=n-len g1 as Nat by INT_1:18;
A841:               n - len g1 + 1 = n + 1 - len g1;
A842:               n = n1 + len g1;
                      now per cases;
                   suppose A843: len g1=n;
                        now consider x being
                      Element of LSeg(g,n) /\ LSeg(g,m);
                      assume
                       LSeg(g,n) /\ LSeg(g,m)<>{};
                      then A844: x in LSeg(g,n) & x in LSeg(g,m)
                      by XBOOLE_0:def 3;
then A845:                      ex qx be Point of TOP-REAL 2 st
                       qx=x & qx`2=ppi`2 & ppi`1<=qx`1 & qx`1<=qq`1
                       by A820,A843;
                      A846: m1 in dom g2 & m1+1 in dom g2 & m1<m1+1
                        by A837,GOBOARD2:3,NAT_1:38;
                      reconsider q1=g2/.m1, q2=g2/.(m1+1)
                       as Point of TOP-REAL 2;
                      set q1l={v where v is Point of TOP-REAL 2: v`2=ppi`2 &
                                 q1`1<=v`1 & v`1<=q2`1};
                      A847: q1`2=ppi`2 & q2`2=ppi`2 & q1`1<q2`1 & q1=|[q1`1,q1
`2]|&
                      q2=|[q2`1,q2`2]| by A719,A724,A846,EUCLID:57;
                         m1 > n1 + 1 & n1 + 1 >=
 1 by A809,A841,NAT_1:29,REAL_1:54;
then A848:                   m1 > 1 by AXIOMS:22;
                         LSeg(g2,m1)=LSeg(q1,q2) by A837,TOPREAL1:def 5
                      .=q1l by A847,TOPREAL3:16;
then A849:                   ex qy be Point of TOP-REAL 2 st
                       qy=x & qy`2=ppi`2 & q1`1<=qy`1 & qy`1<=q2`1
                       by A836,A844;
                         qq`1<q1`1 by A724,A814,A846,A848;
                      hence contradiction by A845,A849,AXIOMS:22;
                     end;
                    hence thesis by XBOOLE_0:def 7;
                   end;
                   suppose n<>len g1;
                    then len g1<n by A840,REAL_1:def 5;
                    then LSeg(g,n)=LSeg(g2,n1) & m1>n1+1
                     by A809,A811,A841,A842,GOBOARD2:10,REAL_1:54;
                    hence thesis by A796,A836,TOPREAL1:def 9;
                   end;
                   end;
                  hence thesis;
                 end;
                 end;
                hence thesis;
               end;
               end;
              hence thesis;
             end;
             end;
            hence thesis;
         end;
        hence g is s.n.c. by GOBOARD2:6;
        A850: g2 is special
         proof let n;
          set p = g2/.n, q = g2/.(n+1);
         assume 1<=n & n+1 <= len g2;
          then n in dom g2 & n+1 in dom g2 by GOBOARD2:3;
          then p`2=ppi`2 & q`2=ppi`2 by A719;
          hence p`1=q`1 or p`2=q`2;
         end;
            now
          set p=g1/.len g1, q=g2/.1;
             i1+1<=j1 by A696,NAT_1:38;
          then 1<=l by REAL_1:84;
          then 1 in dom g2 by A697,FINSEQ_3:27;
           then q`2=ppi`2 by A719;
          hence p`1=q`1 or p`2=q`2 by A15,A16,A18,A35,A47,GOBOARD1:10;
         end;
        hence g is special by A32,A850,GOBOARD2:13;
        thus L~g=L~f
         proof
          set lg = {LSeg(g,i): 1<=i & i+1 <= len g},
              lf = {LSeg(f,j): 1<=j & j+1 <= len f};
          A851: len g = len g1 + len g2 by FINSEQ_1:35;
          A852: now let j; assume
            A853: len g1<=j & j<=len g;
            then reconsider w = j-len g1 as Nat by INT_1:18;
            let p such that
            A854: p=g/.j;
                now per cases;
             suppose A855: j=len g1;
                 1<=len g1 by A14,A16,A36,AXIOMS:22;
              then len g1 in dom g1 by FINSEQ_3:27;
              then A856: g/.len g1 = f1/.len f1 by A35,GROUP_5:95
              .= G*(i1,i2) by A15,A16,A18,A47,GOBOARD1:10;
              hence p`2=G*(i1,i2)`2 by A854,A855;
              thus G*(i1,i2)`1<=p`1 & p`1<=G*
(j1,i2)`1 by A51,A56,A67,A70,A696,A709,A854,A855,A856,GOBOARD1:def 1;
              thus p in rng c1 by A51,A70,A708,A854,A855,A856,PARTFUN2:4;
             end;
             suppose j<>len g1;
then A857:           len g1 < j by A853,REAL_1:def 5;
A858:           j = w + len g1;
                  len g1 + 1 <= j by A857,NAT_1:38;
then A859:           1<=w & w<=len g2 by A851,A853,REAL_1:84,86;
then A860:          g/.j=g2/.w by A858,GOBOARD2:5;
                 w in dom g2 by A859,FINSEQ_3:27;
              hence p`2=ppi`2 & ppi`1<=p`1 & p`1<=
pj`1 & p in rng c1 by A719,A854,A860;
             end;
             end;
            hence p`2=ppi`2 & ppi`1<=p`1 & p`1<=pj`1 & p in rng c1;
           end;
          thus L~g c= L~f
           proof let x; assume x in L~g;
            then x in union lg by TOPREAL1:def 6;
            then consider X be set such that
            A861: x in X & X in lg by TARSKI:def 4;
            consider i such that
            A862: X=LSeg(g,i) & 1<=i & i+1 <= len g by A861;
                now per cases;
             suppose A863: i+1 <= len g1;
              then i<=i+1 & i+1<=len g1 by NAT_1:29;
              then 1<=i & i<=len g1 & 1<=i+1 & i+1<=len g1 by A862,AXIOMS:22;
              then i in dom g1 & i+1 in dom g1 by FINSEQ_3:27;
              then X=LSeg(g1,i) by A862,TOPREAL3:25;
              then X in {LSeg(g1,j): 1<=j & j+1 <= len g1} by A862,A863;
              then x in union {LSeg(g1,j): 1<=j & j+1 <= len g1}
              by A861,TARSKI:def 4;
              then x in L~f1 & L~f1 c= L~f by A33,TOPREAL1:def 6,TOPREAL3:27;
              hence thesis;
             end;
             suppose A864: i+1 > len g1;
              then A865: len g1<=i & i<=i+1 & i+1<=len g by A862,NAT_1:38;
              A866: 1<=i+1 & len g1<=i+1 & i<=len g by A862,A864,NAT_1:38;
              reconsider q1=g/.i, q2=g/.(i+1) as Point of TOP-REAL 2;
              A867: q1`2=ppi`2 & ppi`1<=q1`1 & q1`1<=pj`1 & q2`2=ppi`2 &
              ppi`1<= q2`1 &
              q2`1<=pj`1 by A852,A865,A866;
              then A868: q1=|[q1`1,q1`2]| & q2=|[q2`1,q1`2]| by EUCLID:57;
              A869: LSeg(g,i)=LSeg(q2,q1) by A862,TOPREAL1:def 5;
                  now per cases by AXIOMS:21;
               suppose q1`1>q2`1;
                then LSeg(g,i)={p2: p2`2=q1`2 & q2`1<=p2`1 & p2`1<=q1`1}
                  by A868,A869,TOPREAL3:16;
                then consider p2 such that
                A870: p2=x & p2`2=q1`2 & q2`1<=p2`1 & p2`1<=q1`1 by A861,A862;
                   ppi`1<=p2`1 & p2`1<=pj`1 by A867,A870,AXIOMS:22;
                then x in LSeg(f,k) & LSeg(f,k) in lf by A14,A711,A867,A870;
                then x in union lf by TARSKI:def 4;
                hence thesis by TOPREAL1:def 6;
               end;
               suppose q1`1=q2`1;
                then LSeg(g,i)={q1} by A868,A869,TOPREAL1:7;
                then x=q1 by A861,A862,TARSKI:def 1;
                then x in LSeg(f,k) & LSeg(f,k) in lf by A14,A711,A867;
                then x in union lf by TARSKI:def 4;
                hence thesis by TOPREAL1:def 6;
               end;
               suppose q1`1<q2`1;
                then LSeg(g,i)= {p1: p1`2=q1`2 & q1`1<=p1`1 & p1`1<=q2`1}
                  by A868,A869,TOPREAL3:16;
                then consider p2 such that
                A871: p2=x & p2`2=q1`2 & q1`1<=p2`1 & p2`1<=q2`1 by A861,A862;
                   ppi`1<=p2`1 & p2`1<=pj`1 by A867,A871,AXIOMS:22;
                then x in LSeg(f,k) & LSeg(f,k) in lf by A14,A711,A867,A871;
                then x in union lf by TARSKI:def 4;
                hence thesis by TOPREAL1:def 6;
               end;
               end;
              hence thesis;
             end;
             end;
            hence thesis;
           end;
          let x; assume x in L~f;
          then A872: x in L~f1 \/ LSeg(f,k) by A5,A12,GOBOARD2:8;
              now per cases by A872,XBOOLE_0:def 2;
           suppose x in L~f1; then x in L~g1 & L~g1 c= L~g
            by A33,GOBOARD2:11;
            hence x in L~g;
           end;
           suppose x in LSeg(f,k);
            then consider p1 such that
            A873: p1=x & p1`2=ppi`2 & ppi`1<=p1`1 & p1`1<=pj`1 by A711;
            defpred P2[Nat] means
            len g1<=$1 & $1<=len g & for q st q=g/.$1 holds q`1<=p1`1;
            A874: now
              take n=len g1;
              thus P2[n]
              proof
                 0<=len g2 by NAT_1:18;
               hence len g1<=n & n<=len g by A851,REAL_2:173;
               let q such that
               A875: q=g/.n;
                  1<=len g1 by A14,A16,A36,AXIOMS:22;
               then len g1 in dom g1 by FINSEQ_3:27;
               then q=f1/.len f1 by A35,A875,GROUP_5:95
               .=G*(i1,i2) by A15,A16,A18,A47,GOBOARD1:10;
               hence q`1<=p1`1 by A873;
              end;
             end;
            A876: for n holds P2[n] implies n<=len g;
            consider ma be Nat such that
            A877: P2[ma] & for n st P2[n] holds n<=ma from NAT_1:sch 6
(A876,A874);
                now per cases;
             suppose A878: ma=len g;
                 i1+1<=j1 by A696,NAT_1:38;
              then A879: 1<=l by REAL_1:84;
              then A880: l in dom g2 & i1+l=j1 by A697,FINSEQ_3:27;
              then A881: g/.ma=g2/.l by A697,A851,A878,GROUP_5:96
               .= pj by A697,A880;
              then pj`1<=p1`1 by A877;
              then A882: p1`1=pj`1 by A873,AXIOMS:21;
              A883: ma-1<=len g & len g1+1<=ma
                by A697,A851,A878,A879,PROB_1:2,REAL_1:55;
              then A884: len g1<=ma-1 & 0<=len g1 by NAT_1:18,REAL_1:84;
then A885:           0+1<=ma by REAL_1:84;
              reconsider m1=ma-1 as Nat by A885,INT_1:18;
                 1<=len g1 by A14,A16,A36,AXIOMS:22;
              then A887: 1<=m1 by A884,AXIOMS:22;
              reconsider q=g/.m1 as Point of TOP-REAL 2;
              A889: q`2=ppi`2 & q`1<=pj`1 & q=|[q`1,q`2]| & m1+1=ma
               by A852,A883,A884,EUCLID:57;
              then A890: LSeg(g,m1)=LSeg(q,pj)
               by A878,A881,A887,TOPREAL1:def 5;
              set lq={e where e is Point of TOP-REAL 2: e`2=ppi`2 & q`1<=e`1 &
                      e`1<=pj`1};
              LSeg(g,m1)=lq by A710,A889,A890,TOPREAL3:16;
              then p1 in LSeg(g,m1) & LSeg(g,m1) in lg
                by A873,A878,A882,A887,A889;
              then x in union lg by A873,TARSKI:def 4;
              hence x in L~g by TOPREAL1:def 6;
             end;
             suppose ma<>len g;
              then ma<len g by A877,REAL_1:def 5;
              then A891: ma+1<=len g & k<=ma & ma<=ma+1
                by A16,A36,A877,AXIOMS:22,NAT_1:38;
              then A892: 1<=ma & len g1<=ma+1
                by A14,A877,AXIOMS:22;
              reconsider qa=g/.ma, qa1=g/.(ma+1) as Point of TOP-REAL 2;
              A893: qa`1<=p1`1 by A877;
               A894: now assume qa1`1<=p1`1;
                 then for q holds q=g/.(ma+1) implies q`1<=p1`1;
                then ma+1<=ma by A877,A891,A892;
                hence contradiction by REAL_2:174;
               end;
              then A895: qa`1<qa1`1 & qa`1<=p1`1 & p1`1<=qa1`1 &
              qa`2=ppi`2 & qa1 `2 = ppi`2 by A852,A877,A891,A892,A893,AXIOMS:22
;
              set lma = {p2: p2`2=ppi`2 & qa`1<=p2`1 & p2`1<=qa1`1};
              A896: qa=|[qa`1,qa`2]| & qa1=|[qa1 `1, qa1 `2]| by EUCLID:57;
                 LSeg(g,ma)=LSeg(qa,qa1) by A891,A892,TOPREAL1:def 5
              .= lma by A895,A896,TOPREAL3:16;
              then x in LSeg(g,ma) & LSeg(g,ma) in lg by A873,A891,A892,A893,
A894;
              then x in union lg by TARSKI:def 4;
              hence x in L~g by TOPREAL1:def 6;
             end;
             end;
            hence x in L~g;
           end;
           end;
          hence x in L~g;
         end;
           1<=len g1 by A14,A16,A36,AXIOMS:22;
        then 1 in dom g1 by FINSEQ_3:27;
        hence g/.1=f1/.1 by A34,GROUP_5:95
        .=f/.1 by A15,A19,GOBOARD1:10;
           i1+1<=j1 by A696,NAT_1:38;
        then A897: 1<=l by REAL_1:84;
        then A898: l in dom g2 & len g=len g1 + l by A697,FINSEQ_1:35,FINSEQ_3:
27;
        hence g/.len g=g2/.l by GROUP_5:96
        .=G*(i1+l,i2) by A697,A898
        .=f/.len f by A5,A49,A487;
        thus len f<=len g by A5,A16,A36,A897,A898,REAL_1:55;
       end;
       end;
      hence thesis;
     end;
     end;
    hence thesis;
 end;
 end;
     for k holds P[k] from NAT_1:sch 1(A1,A3);
 hence thesis;
end;

theorem
   (for n st n in dom f ex i,j st [i,j] in Indices G &
f/.n=G*(i,j)) & f is_S-Seq implies
ex g st g is_sequence_on G & g is_S-Seq & L~f = L~g & f/.1 = g/.1 &
        f/.len f = g/.len g & len f<=len g
 proof assume that
  A1: for n st n in dom f ex i,j st [i,j] in Indices G & f/.n=G*(i,j) and
  A2: f is_S-Seq;
  A3: f is one-to-one & 2<=len f & f is unfolded s.n.c. special
         by A2,TOPREAL1:def 10;
  then consider g such that
  A4: g is_sequence_on G and
  A5: g is one-to-one unfolded s.n.c. special &
  L~f = L~g & f/.1 = g/.1 & f/.len f = g/.len g & len f <= len g by A1,Th1;
  take g;
  thus g is_sequence_on G by A4;
     2<=len g by A3,A5,AXIOMS:22;
  hence g is_S-Seq by A5,TOPREAL1:def 10;
  thus thesis by A5;
 end;
