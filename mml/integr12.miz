:: Integrability Formulas -- Part {I}
::  by Bo Li and Na Ma
::
:: Received November 7, 2009
:: Copyright (c) 2009 Association of Mizar Users

environ

 vocabularies RELAT_1, FUNCT_1, ARYTM_1, SIN_COS, INTEGRA1, FDIFF_1, XBOOLE_0,
      SQUARE_1, ARYTM_3, ORDINAL2, RCOMP_1, PREPOWER, NEWTON, NUMBERS, REAL_1,
      SUBSET_1, XXREAL_0, XXREAL_1, PARTFUN1, TAYLOR_1, SIN_COS6, SIN_COS9,
      LIMFUNC1, TARSKI, VALUED_1, CARD_1, ORDINAL4, INTEGRA5, NAT_1, XXREAL_2,
      SEQ_4;
 notations TARSKI, XBOOLE_0, SEQ_1, SIN_COS, SUBSET_1, NUMBERS, VALUED_1,
      NAT_1, XXREAL_0, XREAL_0, REAL_1, RELAT_1, RELSET_1, PARTFUN1, PARTFUN2,
      RCOMP_1, RFUNCT_1, MEASURE6, INTEGRA1, FCONT_1, SQUARE_1, INTEGRA5,
      TAYLOR_1, FDIFF_1, SEQ_2, SEQ_4, SIN_COS6, SIN_COS9, BINOP_2, ORDINAL1,
      NEWTON, LIMFUNC1, PREPOWER;
 constructors SIN_COS, TAYLOR_1, SEQ_1, REAL_1, FDIFF_1, FCONT_1, MEASURE6,
      BINOP_2, SQUARE_1, PREPOWER, INTEGRA5, SEQ_4, XXREAL_3, XBOOLE_0, SEQ_2,
      LIMFUNC1, NAT_1, POWER, RELSET_1, TARSKI, SIN_COS9, PARTFUN1, PARTFUN2,
      FUNCT_1, RCOMP_1, RELAT_1, SIN_COS6, RFUNCT_1, VALUED_1, FDIFF_9, NEWTON;
 registrations VALUED_1, NAT_1, XBOOLE_0, NUMBERS, MEMBERED, VALUED_0,
      INTEGRA1, INT_1, RELAT_1, ORDINAL1, FUNCT_2, RCOMP_1, RELSET_1, RFUNCT_1,
      XREAL_0, NEWTON, SQUARE_1, FUNCT_1, XXREAL_0, FDIFF_1;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM, REAL;
 definitions SIN_COS, VALUED_1, SUBSET_1, SQUARE_1, SIN_COS6, TARSKI, FCONT_1,
      XBOOLE_0, SIN_COS9, RCOMP_1, LIMFUNC1, NAT_1, XXREAL_0, XCMPLX_0;
 theorems PARTFUN1, RFUNCT_1, FUNCT_1, FDIFF_1, XCMPLX_1, RELAT_1, INTEGRA5,
      SIN_COS, FDIFF_4, TAYLOR_1, VALUED_1, FCONT_1, TARSKI, SIN_COS6,
      XXREAL_1, XBOOLE_1, PREPOWER, FDIFF_2, XREAL_1, NAT_1, FDIFF_5, FDIFF_7,
      FDIFF_8, SIN_COS9, XBOOLE_0, XREAL_0;

begin

 reserve a,b,x,r for Real;
 reserve y for set;
 reserve n for Element of NAT;
 reserve A for closed-interval Subset of REAL;
 reserve f,g,f1,f2,g1,g2 for PartFunc of REAL,REAL;
 reserve Z for open Subset of REAL;
 
::f.x=1/(1+x^2)

X: -1 is Real by XREAL_0:def 1;

theorem Th1:
 Z c= dom ((f1+f2)^) & (for x st x in Z holds f1.x=1) & f2=#Z 2
 implies (f1+f2)^ is_differentiable_on Z & for x st x in Z holds
 (((f1+f2)^)`|Z).x = -2*x/(1+x |^2)^2
 proof
  assume
A1:Z c= dom ((f1+f2)^) & (for x st x in Z holds f1.x=1) & f2=#Z 2;
dom ((f1+f2)^) c= dom (f1+f2) by RFUNCT_1:11;
then A3:Z c= dom (f1+f2) by A1,XBOOLE_1:1;
then A4:f1+f2 is_differentiable_on Z &
   for x st x in Z holds ((f1+f2)`|Z).x = 2*x by A1,SIN_COS9:101;
A5:for x st x in Z holds (f1+f2).x<>0 by RFUNCT_1:13,A1;
then A6:(f1+f2)^ is_differentiable_on Z by A4,FDIFF_2:22;
  for x st x in Z holds (((f1+f2)^)`|Z).x = -2*x/(1+x |^2)^2
  proof
    let x;
    assume
A7: x in Z; then
A8: (f1+f2).x<>0 by RFUNCT_1:13,A1;
A9: f1+f2 is_differentiable_in x by A4,A7,FDIFF_1:16;
A10:f2.x=x #Z 2 by A1,TAYLOR_1:def 1
      .=x |^2 by PREPOWER:46;
A11:(f1+f2).x=f1.x+f2.x by A3,A7,VALUED_1:def 1
      .=1+x |^2 by A1,A7,A10;
    (((f1+f2)^)`|Z).x = diff((f1+f2)^,x) by A6,A7,FDIFF_1:def 8
                   .= - diff((f1+f2),x)/((f1+f2).x)^2 by A8,A9,FDIFF_2:15
                   .= -((f1+f2)`|Z).x/((f1+f2).x)^2 by A4,A7,FDIFF_1:def 8
                   .= -2*x/(1+x |^2)^2 by A1,A3,SIN_COS9:101,A7,A11;
    hence thesis;
  end;
  hence thesis by A4,A5,FDIFF_2:22;
end;
 
::f.x=1/((1+x^2)*arccot.x)

theorem
 A c= Z & f = ((g1+g2)^)/f2 & f2 = arccot & Z c= ]. -1,1 .[ & g2=#Z 2
 & (for x st x in Z holds g1.x=1 & f2.x>0) & Z = dom f implies
 integral(f,A)=(-ln*arccot).(upper_bound A)-(-ln*arccot).(lower_bound A)
proof
   assume
A1:A c= Z & f = ((g1+g2)^)/f2 & f2 = arccot & Z c= ]. -1,1 .[ & g2=#Z 2
 & (for x st x in Z holds g1.x=1 & f2.x>0) & Z = dom f;
then  Z = dom ((g1+g2)^) /\ (dom f2 \ f2"{0}) by RFUNCT_1:def 4;
then A4: Z c= dom ((g1+g2)^) & Z c= dom f2 \ f2"{0} by XBOOLE_1:18;
    dom ((g1+g2)^) c= dom (g1+g2) by RFUNCT_1:11;then
AB:Z c= dom (g1+g2) by A4,XBOOLE_1:1;
 for x st x in Z holds g1.x=1 by A1;
then A6: (g1+g2)^ is_differentiable_on Z by A1,A4,Th1;
A7: f2 is_differentiable_on Z by A1,SIN_COS9:82;
 for x st x in Z holds f2.x<>0 by A1;
then  f is_differentiable_on Z by A1,A6,A7,FDIFF_2:21;
then f|Z is continuous by FDIFF_1:33;
then A11:f|A is continuous by FCONT_1:17,A1;
A12:Z c= dom (f2^) by A4,RFUNCT_1:def 8;
    dom (f2^) c= dom f2 by RFUNCT_1:11;then
A13:Z c= dom f2 by A12,XBOOLE_1:1;
A14:for x st x in Z holds f2.x>0 by A1;
rng (f2|Z) c= right_open_halfline(0)
    proof
      let x be set;
      assume x in rng (f2|Z); then
      consider y be set such that
A18:  y in dom (f2|Z) & x = (f2|Z).y by FUNCT_1:def 5;
      y in Z by A18,RELAT_1:86; then
      f2.y > 0 by A1; then
      (f2|Z).y > 0 by A18,FUNCT_1:70;
      hence thesis by A18,XXREAL_1:235;
    end;
then f2.:Z c= dom ln by RELAT_1:148,TAYLOR_1:18;
then A22:Z c= dom (ln*arccot) by A1,A13,FUNCT_1:171;
A23:f is_integrable_on A & f|A is bounded by A1,A11,INTEGRA5:10,11;
A24:ln*arccot is_differentiable_on Z by A1,A22,A14,SIN_COS9:90;
Z c= dom (-ln*arccot) by A22,VALUED_1:8;
then A26:-ln*arccot is_differentiable_on Z by A24,FDIFF_1:28,X;
A27:for x st x in Z holds ((-ln*arccot)`|Z).x = 1/((1+x^2)*arccot.x)
    proof
      let x;
      assume
A28:x in Z;then
A29:-1 < x & x < 1 by A1,XXREAL_1:4;
    arccot is_differentiable_on Z by SIN_COS9:82,A1;then
A30:arccot is_differentiable_in x by A28,FDIFF_1:16;
A31:arccot.x >0 by A1,A28;
A32:ln*arccot is_differentiable_in x by A24,A28,FDIFF_1:16;
 ((-ln*arccot)`|Z).x=diff((-ln*arccot),x) by A26,A28,FDIFF_1:def 8
                   .=(-1)*diff(ln*arccot,x) by A32,FDIFF_1:23,X
                   .=(-1)*(diff(arccot,x)/arccot.x) by A30,A31,TAYLOR_1:20
                   .=(-1)*((-1/(1+x^2))/arccot.x) by A29,SIN_COS9:76
                   .= (1/(1+x^2))/arccot.x
                   .= 1/((1+x^2)*arccot.x) by XCMPLX_1:79;
                 hence thesis;
     end;
B1:for x st x in Z holds f.x=1/((1+x^2)*arccot.x)
   proof
   let x;
   assume
B2:x in Z;
   then (((g1+g2)^)/f2).x =((g1+g2)^).x/f2.x by RFUNCT_1:def 4,A1
  .=((g1+g2).x)"/f2.x by RFUNCT_1:def 8,A4,B2
  .=(g1.x+g2.x)"/f2.x by VALUED_1:def 1,AB,B2
  .=1/((g1.x+g2.x)*f2.x) by XCMPLX_1:223
  .=1/((1+( #Z 2).x)*f2.x) by A1,B2
  .=1/((1+(x #Z 2))*f2.x) by TAYLOR_1:def 1
  .=1/((1+x^2)*arccot.x) by A1, FDIFF_7:1;
    hence thesis by A1;
    end;
A33:for x st x in dom ((-ln*arccot)`|Z) holds ((-ln*arccot)`|Z).x=f.x
   proof
     let x;
     assume x in dom ((-ln*arccot)`|Z);then
A34:x in Z by A26,FDIFF_1:def 8;then
  ((-ln*arccot)`|Z).x=1/((1+x^2)*arccot.x) by A27
                      .=f.x by B1,A34;
  hence thesis;
  end;
  dom ((-ln*arccot)`|Z)=dom f by A1,A26,FDIFF_1:def 8;
  then ((-ln*arccot)`|Z)= f by A33,PARTFUN1:34;
  hence thesis by A1,A23,A26,INTEGRA5:13;
end;
 
::f.x=exp_R.x/(1+(exp_R.x)^2)

theorem
 A c= Z & (for x st x in Z holds exp_R.x < 1 & f1.x=1)
 & Z c= dom (arctan*exp_R)
 & Z = dom f & f=exp_R/(f1+(exp_R)^2)
 implies integral(f,A)=(arctan*exp_R).(upper_bound A)
                      -(arctan*exp_R).(lower_bound A)
proof
   assume
A1: A c= Z & (for x st x in Z holds exp_R.x < 1 & f1.x=1)
    & Z c= dom (arctan*exp_R)
    & Z = dom f & f=exp_R/(f1+(exp_R)^2);
then Z c= dom exp_R /\ (dom (f1+(exp_R)^2) \ (f1+(exp_R)^2)"{0})
   by RFUNCT_1:def 4;
then Z c= dom exp_R & Z c= dom (f1+(exp_R)^2) \ (f1+(exp_R)^2)"{0}
   by XBOOLE_1:18;
then A5:Z c= dom ((f1+(exp_R)^2)^) by RFUNCT_1:def 8;
   dom ((f1+(exp_R)^2)^) c= dom (f1+(exp_R)^2) by RFUNCT_1:11;then
A6:Z c= dom (f1+(exp_R)^2) by XBOOLE_1:1,A5;
then B1:Z c= dom f1 /\ dom ((exp_R)^2) by VALUED_1:def 1;
then B2:Z c= dom f1 & Z c= dom ((exp_R)^2) by XBOOLE_1:18;
A7:Z c= dom ((exp_R)(#)(exp_R)) by XBOOLE_1:18,B1;
A8:exp_R is_differentiable_on Z by FDIFF_1:34,TAYLOR_1:16;
then A9:(exp_R)(#)(exp_R) is_differentiable_on Z by A7, FDIFF_1:29;
for x st x in Z holds f1.x=0*x+1 by A1;
then f1 is_differentiable_on Z by FDIFF_1:31,B2;
then A13:f1+(exp_R)^2 is_differentiable_on Z by A6,A9,FDIFF_1:26;
for x st x in Z holds (f1+(exp_R)^2).x<>0
   proof
   let x;
   assume x in Z;then
   x in dom exp_R /\ (dom (f1+(exp_R)^2) \ (f1+(exp_R)^2)"{0})
     by A1,RFUNCT_1:def 4;then
   x in dom (f1+(exp_R)^2) \ (f1+(exp_R)^2)"{0} by XBOOLE_0:def 4; then
   x in dom ((f1+(exp_R)^2)^) by RFUNCT_1:def 8;
      hence thesis by RFUNCT_1:13;
   end;
then f is_differentiable_on Z by A1,A8,A13,FDIFF_2:21;
then f|Z is continuous by FDIFF_1:33;
then f|A is continuous by A1,FCONT_1:17;
then A18:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A19:for x st x in Z holds exp_R.x < 1 by A1;then
A20:arctan*exp_R is_differentiable_on Z by A1,SIN_COS9:115;
B3:for x st x in Z holds f.x=exp_R.x/(1+(exp_R.x)^2)
   proof
   let x;
   assume
B4:x in Z;
    then (exp_R/(f1+(exp_R)^2)).x=(exp_R.x)*((f1+(exp_R)^2).x)"
                                 by RFUNCT_1:def 4,A1
                           .=(exp_R.x)*(f1.x+((exp_R)^2).x)"
                                by VALUED_1:def 1,B4,A6
                           .=(exp_R.x)*(f1.x+(exp_R.x)^2)"
                                by VALUED_1:11
                           .=exp_R.x/(1+(exp_R.x)^2) by A1,B4;
        hence thesis by A1;
      end;
A21:for x st x in dom ((arctan*exp_R)`|Z) holds ((arctan*exp_R)`|Z).x=f.x
    proof
       let x;
       assume x in dom ((arctan*exp_R)`|Z);then
A22:x in Z by A20, FDIFF_1:def 8;then
   ((arctan*exp_R)`|Z).x=exp_R.x/(1+(exp_R.x)^2) by A1,A19,SIN_COS9:115
                       .=f.x by A22,B3;
   hence thesis;
   end;
   dom ((arctan*exp_R)`|Z)=dom f by A1,A20,FDIFF_1:def 8;
   then ((arctan*exp_R)`|Z)= f by A21,PARTFUN1:34;
   hence thesis by A1,A18,A20,INTEGRA5:13;
end;
 
::f.x=-exp_R.x/(1+(exp_R.x)^2)

theorem
A c= Z & (for x st x in Z holds exp_R.x < 1 & f1.x=1)
& Z c= dom (arccot*exp_R) & Z = dom f & f=(-exp_R)/(f1+(exp_R)^2)
 implies integral(f,A)=(arccot*exp_R).(upper_bound A)
                      -(arccot*exp_R).(lower_bound A)
proof
   assume
A1:A c= Z & (for x st x in Z holds exp_R.x < 1 & f1.x=1)
   & Z c= dom (arccot*exp_R) & Z = dom f
   & f=(-exp_R)/(f1+(exp_R)^2);
then Z c= dom (-exp_R) /\ (dom (f1+(exp_R)^2) \ (f1+(exp_R)^2)"{0})
   by RFUNCT_1:def 4;
then A4:Z c= dom (-exp_R) & Z c= dom (f1+(exp_R)^2) \ (f1+(exp_R)^2)"{0}
   by XBOOLE_1:18;
then A5:Z c= dom ((f1+(exp_R)^2)^) by RFUNCT_1:def 8;
   dom ((f1+(exp_R)^2)^) c= dom (f1+(exp_R)^2) by RFUNCT_1:11;then
A6:Z c= dom (f1+(exp_R)^2) by XBOOLE_1:1,A5;
then A7:Z c= dom f1 /\ dom ((exp_R)^2) by VALUED_1:def 1;
then A8:Z c= dom f1 & Z c= dom ((exp_R)^2) by XBOOLE_1:18;
A9:Z c= dom ((exp_R)(#)(exp_R)) by XBOOLE_1:18,A7;
A10:exp_R is_differentiable_on Z by FDIFF_1:34,TAYLOR_1:16;
then A11:(-1)(#)(exp_R) is_differentiable_on Z by A4,FDIFF_1:28,X;
A12:(exp_R)(#)(exp_R) is_differentiable_on Z by A9,A10,FDIFF_1:29;
for x st x in Z holds f1.x=0*x+1 by A1;
then f1 is_differentiable_on Z by FDIFF_1:31,A8;
then A16:f1+(exp_R)^2 is_differentiable_on Z by A6,A12,FDIFF_1:26;
for x st x in Z holds (f1+(exp_R)^2).x<>0
   proof
   let x;
   assume x in Z;then
   x in dom (-exp_R) /\ (dom (f1+(exp_R)^2) \ (f1+(exp_R)^2)"{0})
     by RFUNCT_1:def 4,A1;then
   x in dom (f1+(exp_R)^2) \ (f1+(exp_R)^2)"{0} by XBOOLE_0:def 4; then
   x in dom ((f1+(exp_R)^2)^) by RFUNCT_1:def 8;
   hence thesis by RFUNCT_1:13;
   end;
then f is_differentiable_on Z by A1,A11,A16,FDIFF_2:21;
then f|Z is continuous by FDIFF_1:33;
then f|A is continuous by A1,FCONT_1:17;
then A21:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A22:for x st x in Z holds exp_R.x < 1 by A1;then
A23:arccot*exp_R is_differentiable_on Z by A1,SIN_COS9:116;
B1:for x st x in Z holds f.x=-exp_R.x/(1+(exp_R.x)^2)
   proof
   let x;
   assume
B2:x in Z;
    then ((-exp_R)/(f1+(exp_R)^2)).x=((-exp_R).x)*((f1+(exp_R)^2).x)"
                                 by RFUNCT_1:def 4,A1
                              .=(-exp_R.x)*((f1+(exp_R)^2).x)"
                                by RFUNCT_1:74
                              .=(-exp_R.x)*(f1.x+((exp_R)^2).x)"
                                by VALUED_1:def 1,B2,A6
                              .=(-exp_R.x)*(f1.x+(exp_R.x)^2)"
                                by VALUED_1:11
                              .=(-exp_R.x)/(1+(exp_R.x)^2) by A1,B2
                              .=-exp_R.x/(1+(exp_R.x)^2);
        hence thesis by A1;
      end;
A24:for x st x in dom ((arccot*exp_R)`|Z) holds ((arccot*exp_R)`|Z).x=f.x
    proof
       let x;
       assume x in dom ((arccot*exp_R)`|Z);then
A25:x in Z by A23, FDIFF_1:def 8;
   then
((arccot*exp_R)`|Z).x=-exp_R.x/(1+(exp_R.x)^2) by A1,A22,SIN_COS9:116
                       .=f.x by A25,B1;
   hence thesis;
   end;
   dom ((arccot*exp_R)`|Z)=dom f by A1,A23,FDIFF_1:def 8;
   then ((arccot*exp_R)`|Z)= f by A24,PARTFUN1:34;
   hence thesis by A1,A21,A23,INTEGRA5:13;
end;
 
::f.x=exp_R.x*sin.x/cos.x+exp_R.x/(cos.x)^2

theorem
 A c= Z & Z = dom f & f=exp_R(#)(sin/cos)+exp_R/cos^2 implies
 integral(f,A)=(exp_R(#)tan).(upper_bound A)-(exp_R(#)tan).(lower_bound A)
 proof
 assume
A1:A c= Z & Z = dom f & f=exp_R(#)(sin/cos)+exp_R/cos^2;
then Z = dom (exp_R(#)(sin/cos)) /\ dom (exp_R/cos^2) by VALUED_1:def 1;
then
A4:Z c= dom (exp_R(#)(sin/cos)) & Z c= dom (exp_R/cos^2) by XBOOLE_1:18;
A5:dom (exp_R(#)(sin/cos)) c= dom exp_R /\ dom (sin/cos) by VALUED_1:def 4;
dom (exp_R/cos^2) c= dom exp_R /\ (dom cos^2 \ (cos^2)"{0})
   by RFUNCT_1:def 4;
   then dom (exp_R(#)(sin/cos)) c= dom exp_R &
   dom (exp_R(#)(sin/cos)) c= dom (sin/cos) &
   dom (exp_R/cos^2) c= dom cos^2 \ (cos^2)"{0} by XBOOLE_1:18,A5; then
A7:Z c= dom exp_R & Z c= dom (sin/cos) & Z c= dom cos^2 \ (cos^2)"{0}
   by XBOOLE_1:1,A4;
A9:exp_R is_differentiable_on Z by FDIFF_1:34,TAYLOR_1:16;
for x st x in Z holds sin/cos is_differentiable_in x
    proof
    let x;
    assume x in Z;then
    cos.x<>0 by A7,FDIFF_8:1;
    hence thesis by FDIFF_7:46;
    end;
then sin/cos is_differentiable_on Z by A7,FDIFF_1:16;
then A12:exp_R(#)(sin/cos) is_differentiable_on Z by A4,A9,FDIFF_1:29;
cos is_differentiable_on Z by FDIFF_1:34,SIN_COS:72;
then A14:cos^2 is_differentiable_on Z by FDIFF_2:20;
x in Z implies (cos^2).x<>0
    proof
    assume x in Z;then
    x in dom (exp_R/cos^2) by A4;
    then x in dom exp_R /\ (dom cos^2 \ (cos^2)"{0}) by RFUNCT_1:def 4;
    then x in dom cos^2 \ (cos^2)"{0} by XBOOLE_0:def 4;
    then x in dom ((cos^2)^) by RFUNCT_1:def 8;
      hence thesis by RFUNCT_1:13;
    end;
then exp_R/cos^2 is_differentiable_on Z by A9,A14,FDIFF_2:21;
    then f|Z is continuous by FDIFF_1:33,FDIFF_1:26,A1,A12;then
f|A is continuous by A1,FCONT_1:17;
then A19:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A20:(exp_R(#)tan) is_differentiable_on Z by A4,FDIFF_8:30;
B1:for x st x in Z holds f.x=exp_R.x*sin.x/cos.x+exp_R.x/(cos.x)^2
   proof
   let x;
   assume
B2:x in Z;
  then (exp_R(#)(sin/cos)+exp_R/cos^2).x
  =((exp_R(#)(sin/cos)).x)+(exp_R/cos^2).x by VALUED_1:def 1,A1
 .=((exp_R.x)*((sin/cos).x))+(exp_R/cos^2).x by VALUED_1:5
 .=((exp_R.x)*(sin.x*(cos.x)"))+(exp_R/cos^2).x by RFUNCT_1:def 4,A7,B2
 .=(exp_R.x)*sin.x/cos.x+exp_R.x/(cos^2).x by RFUNCT_1:def 4,A4,B2
 .=exp_R.x*sin.x/cos.x+exp_R.x/(cos.x)^2 by VALUED_1:11;
   hence thesis by A1;
   end;
A21:for x st x in dom((exp_R(#)tan)`|Z) holds ((exp_R(#)tan)`|Z).x=f.x
   proof
     let x;
     assume x in dom((exp_R(#)tan)`|Z);then
A22: x in Z by A20,FDIFF_1:def 8; then
 ((exp_R(#)tan)`|Z).x=exp_R.x*sin.x/cos.x+exp_R.x/(cos.x)^2 by A4,FDIFF_8:30
                    .=f.x by A22,B1;
   hence thesis;
   end;
   dom((exp_R(#)tan)`|Z)=dom f by A1,A20,FDIFF_1:def 8;
   then ((exp_R(#)tan)`|Z)= f by A21,PARTFUN1:34;
   hence thesis by A1,A19,A4,FDIFF_8:30,INTEGRA5:13;
 end;
 
::f.x=exp_R.x*cos.x/sin.x-exp_R.x/(sin.x)^2

theorem
 A c= Z & Z = dom f & f=exp_R(#)(cos/sin)-exp_R/sin^2 implies
 integral(f,A)=(exp_R(#)cot).(upper_bound A)-(exp_R(#)cot).(lower_bound A)
proof
   assume
A1:A c= Z & Z = dom f & f=exp_R(#)(cos/sin)-exp_R/sin^2;
then Z = dom (exp_R(#)(cos/sin)) /\ dom (exp_R/sin^2) by VALUED_1:12;
then
A4:Z c= dom (exp_R(#)(cos/sin)) & Z c= dom (exp_R/sin^2) by XBOOLE_1:18;
A5:dom (exp_R(#)(cos/sin)) c= dom exp_R /\ dom (cos/sin) by VALUED_1:def 4;
dom (exp_R/sin^2) c= dom exp_R /\ (dom sin^2 \ (sin^2)"{0})
   by RFUNCT_1:def 4;
   then dom (exp_R(#)(cos/sin)) c= dom exp_R &
   dom (exp_R(#)(cos/sin)) c= dom (cos/sin) &
   dom (exp_R/sin^2) c= dom sin^2 \ (sin^2)"{0} by XBOOLE_1:18,A5; then
A7:Z c= dom exp_R & Z c= dom (cos/sin) & Z c= dom sin^2 \ (sin^2)"{0}
   by XBOOLE_1:1,A4;
A9:exp_R is_differentiable_on Z by FDIFF_1:34,TAYLOR_1:16;
for x st x in Z holds cos/sin is_differentiable_in x
    proof
    let x;
    assume x in Z;then
    sin.x<>0 by A7,FDIFF_8:2;
    hence thesis by FDIFF_7:47;
    end;
then cos/sin is_differentiable_on Z by A7,FDIFF_1:16;
then A12:exp_R(#)(cos/sin) is_differentiable_on Z by A4,A9,FDIFF_1:29;
sin is_differentiable_on Z by FDIFF_1:34,SIN_COS:73;
then A14:sin^2 is_differentiable_on Z by FDIFF_2:20;
x in Z implies (sin^2).x<>0
    proof
    assume x in Z;then
    x in dom (exp_R/sin^2) by A4;
    then x in dom exp_R /\ (dom sin^2 \ (sin^2)"{0}) by RFUNCT_1:def 4;
    then x in dom sin^2 \ (sin^2)"{0} by XBOOLE_0:def 4;
    then x in dom ((sin^2)^) by RFUNCT_1:def 8;
      hence thesis by RFUNCT_1:13;
    end;
then exp_R/sin^2 is_differentiable_on Z by A9,A14,FDIFF_2:21;
    then f|Z is continuous by FDIFF_1:33,FDIFF_1:27,A1,A12;then
f|A is continuous by A1,FCONT_1:17;
then A19:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A20:(exp_R(#)cot) is_differentiable_on Z by A4,FDIFF_8:31;
B1:for x st x in Z holds f.x=exp_R.x*cos.x/sin.x-exp_R.x/(sin.x)^2
   proof
   let x;
   assume
B2:x in Z;
  then (exp_R(#)(cos/sin)-exp_R/sin^2).x
  =((exp_R(#)(cos/sin)).x)-(exp_R/sin^2).x by VALUED_1:13,A1
 .=((exp_R.x)*((cos/sin).x))-(exp_R/sin^2).x by VALUED_1:5
 .=((exp_R.x)*(cos.x*(sin.x)"))-(exp_R/sin^2).x
   by RFUNCT_1:def 4,A7,B2
 .=exp_R.x*cos.x/sin.x-exp_R.x/(sin^2).x by RFUNCT_1:def 4,A4,B2
 .=exp_R.x*cos.x/sin.x-exp_R.x/(sin.x)^2 by VALUED_1:11;
     hence thesis by A1;
   end;
A21:for x st x in dom((exp_R(#)cot)`|Z) holds ((exp_R(#)cot)`|Z).x=f.x
   proof
     let x;
     assume x in dom((exp_R(#)cot)`|Z);then
A22: x in Z by A20,FDIFF_1:def 8; then
 ((exp_R(#)cot)`|Z).x=exp_R.x*cos.x/sin.x-exp_R.x/(sin.x)^2 by A4,FDIFF_8:31
  .=f.x by A22,B1;
   hence thesis;
   end;
  dom((exp_R(#)cot)`|Z)=dom f by A1,A20,FDIFF_1:def 8;
  then ((exp_R(#)cot)`|Z)= f by A21,PARTFUN1:34;
  hence thesis by A1,A19,A4,FDIFF_8:31,INTEGRA5:13;
end;
 
::f.x=exp_R.x*arctan.x+exp_R.x/(1+x^2)

theorem
 A c= Z & (for x st x in Z holds f1.x=1) & Z c= ]. -1,1 .[ & Z = dom f
 & f=exp_R(#)arctan+exp_R/(f1+#Z 2) implies
 integral(f,A
 )=(exp_R(#)arctan).(upper_bound A)-(exp_R(#)arctan).(lower_bound A)
proof
   assume
A1:A c= Z & (for x st x in Z holds f1.x=1) & Z c= ]. -1,1 .[ & Z = dom f
   & f=exp_R(#)arctan+exp_R/(f1+#Z 2);
then A3:Z = dom (exp_R(#)arctan) /\ dom (exp_R/(f1+#Z 2)) by VALUED_1:def 1;
A5:exp_R is_differentiable_on Z by FDIFF_1:34,TAYLOR_1:16;
A6:exp_R(#)arctan is_differentiable_on Z by A1,SIN_COS9:123;
A7:Z c= dom (exp_R/(f1+#Z 2)) by XBOOLE_1:18,A3;
then A8:Z c= dom (exp_R(#)((f1+#Z 2)^)) by RFUNCT_1:47;
   then Z c= dom exp_R /\ dom ((f1+#Z 2)^) by VALUED_1:def 4;then
A9:Z c= dom ((f1+#Z 2)^) by XBOOLE_1:18;
   dom ((f1+ #Z 2)^) c= dom (f1+#Z 2) by RFUNCT_1:11;then
AA:Z c= dom (f1+#Z 2) by XBOOLE_1:1,A9;
(f1+#Z 2)^ is_differentiable_on Z by A1,A9,Th1;
then exp_R(#)((f1+#Z 2)^) is_differentiable_on Z by A5,A8,FDIFF_1:29;
then exp_R/(f1+#Z 2) is_differentiable_on Z by RFUNCT_1:47;
    then f|Z is continuous by FDIFF_1:33,A1,A6,FDIFF_1:26;then
f|A is continuous by A1,FCONT_1:17;
then A15:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
B1:for x st x in Z holds f.x=exp_R.x*arctan.x+exp_R.x/(1+x^2)
    proof
    let x;
    assume
B2:x in Z;
   then (exp_R(#)arctan+exp_R/(f1+#Z 2)).x
   =(exp_R(#)arctan).x+(exp_R/(f1+#Z 2)).x by VALUED_1:def 1,A1
  .=exp_R.x*arctan.x+(exp_R/(f1+#Z 2)).x by VALUED_1:5
  .=exp_R.x*arctan.x+exp_R.x/(f1+#Z 2).x by RFUNCT_1:def 4,A7,B2
  .=exp_R.x*arctan.x+exp_R.x/(f1.x+(( #Z 2).x)) by VALUED_1:def 1,AA,B2
  .=exp_R.x*arctan.x+exp_R.x/(f1.x+(x #Z 2)) by TAYLOR_1:def 1
  .=exp_R.x*arctan.x+exp_R.x/(f1.x+x^2) by FDIFF_7:1
  .=exp_R.x*arctan.x+exp_R.x/(1+x^2) by A1,B2;
      hence thesis by A1;
    end;
A16:for x st x in dom((exp_R(#)arctan)`|Z) holds ((exp_R(#)arctan)`|Z).x=f.x
   proof
     let x;
     assume x in dom((exp_R(#)arctan)`|Z);then
A17:x in Z by A6,FDIFF_1:def 8;then
  ((exp_R(#)arctan)`|Z).x=exp_R.x*arctan.x+exp_R.x/(1+x^2) by A1,SIN_COS9:123
                       .=f.x by A17,B1;
   hence thesis;
   end;
  dom((exp_R(#)arctan)`|Z)=dom f by A1,A6,FDIFF_1:def 8;
  then ((exp_R(#)arctan)`|Z)= f by A16,PARTFUN1:34;
  hence thesis by A1,A15,SIN_COS9:123,INTEGRA5:13;
end;
 
::f.x=exp_R.x*arccot.x-exp_R.x/(1+x^2)

theorem
 A c= Z & (for x st x in Z holds f1.x=1) & Z c= ]. -1,1 .[ & Z = dom f
 & f=exp_R(#)arccot-exp_R/(f1+#Z 2) implies
 integral(f,A)
 =(exp_R(#)arccot).(upper_bound A)-(exp_R(#)arccot).(lower_bound A)
proof
   assume
A1:A c= Z & (for x st x in Z holds f1.x=1) & Z c= ]. -1,1 .[ & Z = dom f
   & f=exp_R(#)arccot-exp_R/(f1+#Z 2);
then A3:Z = dom (exp_R(#)arccot) /\ dom (exp_R/(f1+#Z 2)) by VALUED_1:12;
A5:exp_R is_differentiable_on Z by FDIFF_1:34,TAYLOR_1:16;
A6:exp_R(#)arccot is_differentiable_on Z by A1,SIN_COS9:124;
A7:Z c= dom (exp_R/(f1+#Z 2)) by XBOOLE_1:18,A3;
then A8:Z c= dom (exp_R(#)((f1+#Z 2)^)) by RFUNCT_1:47;
   then Z c= dom exp_R /\ dom ((f1+#Z 2)^) by VALUED_1:def 4;then
A9:Z c= dom ((f1+#Z 2)^) by XBOOLE_1:18;
   dom ((f1+ #Z 2)^) c= dom (f1+#Z 2) by RFUNCT_1:11;then
AA:Z c= dom (f1+#Z 2) by XBOOLE_1:1,A9;
(f1+#Z 2)^ is_differentiable_on Z by A1,A9,Th1;
then exp_R(#)((f1+#Z 2)^) is_differentiable_on Z by A5,A8,FDIFF_1:29;
then exp_R/(f1+#Z 2) is_differentiable_on Z by RFUNCT_1:47;
    then f|Z is continuous by FDIFF_1:33,A1,A6,FDIFF_1:27;then
f|A is continuous by A1,FCONT_1:17;
then A15:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
B1:for x st x in Z holds f.x=exp_R.x*arccot.x-exp_R.x/(1+x^2)
    proof
    let x;
    assume
B2:x in Z;
   then (exp_R(#)arccot-exp_R/(f1+#Z 2)).x
   =(exp_R(#)arccot).x-(exp_R/(f1+#Z 2)).x by VALUED_1:13,A1
  .=exp_R.x*arccot.x-(exp_R/(f1+#Z 2)).x by VALUED_1:5
  .=exp_R.x*arccot.x-exp_R.x/(f1+#Z 2).x by RFUNCT_1:def 4,A7,B2
  .=exp_R.x*arccot.x-exp_R.x/(f1.x+(( #Z 2).x)) by VALUED_1:def 1,AA,B2
  .=exp_R.x*arccot.x-exp_R.x/(f1.x+(x #Z 2)) by TAYLOR_1:def 1
  .=exp_R.x*arccot.x-exp_R.x/(f1.x+x^2) by FDIFF_7:1
  .=exp_R.x*arccot.x-exp_R.x/(1+x^2) by A1,B2;
      hence thesis by A1;
    end;
A16:for x st x in dom((exp_R(#)arccot)`|Z) holds ((exp_R(#)arccot)`|Z).x=f.x
   proof
     let x;
     assume x in dom((exp_R(#)arccot)`|Z);then
A17:x in Z by A6,FDIFF_1:def 8;then
  ((exp_R(#)arccot)`|Z).x=exp_R.x*arccot.x-exp_R.x/(1+x^2) by A1,SIN_COS9:124
                       .=f.x by A17,B1;
   hence thesis;
   end;
  dom((exp_R(#)arccot)`|Z)=dom f by A1,A6,FDIFF_1:def 8;
  then ((exp_R(#)arccot)`|Z)= f by A16,PARTFUN1:34;
  hence thesis by A1,A15,SIN_COS9:124,INTEGRA5:13;
end;
 
::f.x=exp_R.(sin.x) * cos.x

theorem
 A c= Z & Z = dom f & f=(exp_R*sin)(#)cos implies
 integral(f,A)=(exp_R*sin).(upper_bound A)-(exp_R*sin).(lower_bound A)
proof
  assume
A1:A c= Z & Z = dom f & f=(exp_R*sin)(#)cos;
then Z = dom (exp_R*sin) /\ dom cos by VALUED_1:def 4;
then A4:Z c= dom (exp_R*sin) by XBOOLE_1:18;
then A5:exp_R*sin is_differentiable_on Z by FDIFF_7:37;
cos is_differentiable_on Z by FDIFF_1:34,SIN_COS:72;
   then f|Z is continuous by FDIFF_1:33,A1,A5,FDIFF_1:29;then
f|A is continuous by A1,FCONT_1:17;
then A9:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
B1:for x st x in Z holds f.x=exp_R.(sin.x) * cos.x
   proof
   let x;
   assume
B2:x in Z;
   then ((exp_R*sin)(#)cos).x=(exp_R*sin).x*cos.x by A1,VALUED_1:def 4
                       .=exp_R.(sin.x) * cos.x by FUNCT_1:22,A4,B2;
   hence thesis by A1;
   end;
A10:for x st x in dom((exp_R*sin)`|Z) holds ((exp_R*sin)`|Z).x=f.x
   proof
     let x;
     assume x in dom((exp_R*sin)`|Z);then
A11: x in Z by A5,FDIFF_1:def 8; then
  ((exp_R*sin)`|Z).x=exp_R.(sin.x) * cos.x by A4,FDIFF_7:37
   .=f.x by B1,A11;
   hence thesis;
   end;
  dom((exp_R*sin)`|Z)=dom f by A1,A5,FDIFF_1:def 8;
  then((exp_R*sin)`|Z)= f by A10,PARTFUN1:34;
  hence thesis by A1,A4,FDIFF_7:37,A9,INTEGRA5:13;
end;
 
::f.x=exp_R.(cos.x) * sin.x

theorem
  A c= Z & Z = dom f & f=(exp_R*cos)(#)sin implies
  integral(f,A)=(-exp_R*cos).(upper_bound A)-(-exp_R*cos).(lower_bound A)
proof
  assume
A1:A c= Z & Z = dom f & f=(exp_R*cos)(#)sin;
then Z = dom (exp_R*cos) /\ dom sin by VALUED_1:def 4;
then A4:Z c= dom (exp_R*cos) by XBOOLE_1:18;
then A5:exp_R*cos is_differentiable_on Z by FDIFF_7:36;
sin is_differentiable_on Z by FDIFF_1:34,SIN_COS:73;
   then f|Z is continuous by FDIFF_1:33,A1,A5,FDIFF_1:29;then
f|A is continuous by A1,FCONT_1:17;
then AA:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A9:Z c= dom (-exp_R*cos) by A4,VALUED_1:8;
then A10:(-1)(#)(exp_R*cos) is_differentiable_on Z by A5,FDIFF_1:28,X;
A11:for x st x in Z holds ((-exp_R*cos)`|Z).x = exp_R.(cos.x) * sin.x
  proof
  let x;
  assume
A12:x in Z;
A13: cos is_differentiable_in x by SIN_COS:68;
A14: exp_R is_differentiable_in cos.x by SIN_COS:70;
A15: exp_R*cos is_differentiable_in x by A5,A12,FDIFF_1:16;
 ((-exp_R*cos)`|Z).x=diff(-exp_R*cos,x) by A10,A12,FDIFF_1:def 8
                   .=(-1)*diff(exp_R*cos,x) by A15,FDIFF_1:23,X
                   .=(-1)*(diff(exp_R,cos.x)*diff(cos,x)) by A13,A14,FDIFF_2:13
                   .=(-1)*(diff(exp_R,cos.x)*(-sin.x )) by SIN_COS:68
                   .=(-1)*(exp_R.(cos.x)*(-sin.x )) by SIN_COS:70
                   .=exp_R.(cos.x) * sin.x;
  hence thesis;
  end;
B1:for x st x in Z holds f.x=exp_R.(cos.x) * sin.x
   proof
   let x;
   assume
B2:x in Z;
   then ((exp_R*cos)(#)sin).x=(exp_R*cos).x*sin.x by A1,VALUED_1:def 4
                       .=exp_R.(cos.x) * sin.x by FUNCT_1:22,A4,B2;
   hence thesis by A1;
   end;
A16:for x st x in dom((-exp_R*cos)`|Z) holds ((-exp_R*cos)`|Z).x=f.x
   proof
     let x;
     assume x in dom((-exp_R*cos)`|Z);then
A17: x in Z by A10,FDIFF_1:def 8; then
  ((-exp_R*cos)`|Z).x=exp_R.(cos.x) * sin.x by A11
   .=f.x by A17,B1;
   hence thesis;
   end;
  dom((-exp_R*cos)`|Z)=dom f by A1,A10,FDIFF_1:def 8;
  then ((-exp_R*cos)`|Z)= f by A16,PARTFUN1:34;
  hence thesis by A1,AA,A5,A9,FDIFF_1:28,X,INTEGRA5:13;
end;
 
::f.x=cos.(ln.x)/x

theorem
 A c= Z & (for x st x in Z holds x>0)
 & Z = dom f & f=(cos*ln)(#)((id Z)^) implies
 integral(f,A)=(sin*ln).(upper_bound A)-(sin*ln).(lower_bound A)
 proof
   assume
A1:A c= Z & (for x st x in Z holds x>0) & Z = dom f & f=(cos*ln)(#)((id Z)^);
then AA:Z = dom ((cos*ln)/(id Z)) by RFUNCT_1:47;
Z = dom (cos*ln) /\ dom ((id Z)^) by VALUED_1:def 4,A1;
then A4:Z c= dom (cos*ln) by XBOOLE_1:18;
for y st y in Z holds y in dom (sin*ln)
   proof
   let y;
   assume y in Z;then
   y in dom ln & ln.y in dom sin by A4,FUNCT_1:21,SIN_COS:27;
   hence thesis by FUNCT_1:21;
   end;
then B6:Z c= dom (sin*ln) by TARSKI:def 3;
A6:cos*ln is_differentiable_on Z by FDIFF_7:33,A4,A1;
not 0 in Z by A1;
then (id Z)^ is_differentiable_on Z by FDIFF_5:4;
   then f|Z is continuous by FDIFF_1:33,A1,A6,FDIFF_1:29;then
f|A is continuous by A1,FCONT_1:17;
then A11:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A12:sin*ln is_differentiable_on Z by B6,A1,FDIFF_7:32;
B1:for x st x in Z holds f.x=cos.(ln.x)/x
   proof
   let x;
   assume
B2:x in Z;
   ((cos*ln)(#)((id Z)^)).x=((cos*ln)/(id Z)).x by RFUNCT_1:47
                          .=(cos*ln).x*((id Z).x)" by RFUNCT_1:def 4,AA,B2
                          .=(cos*ln).x/x by FUNCT_1:35,B2
                          .=cos.(ln.x)/x by FUNCT_1:22,A4,B2;
   hence thesis by A1;
   end;
A13:for x st x in dom((sin*ln)`|Z) holds ((sin*ln)`|Z).x=f.x
   proof
     let x;
     assume x in dom((sin*ln)`|Z);then
A14: x in Z by A12,FDIFF_1:def 8; then
  ((sin*ln)`|Z).x=cos.(ln.x)/x by B6,A1,FDIFF_7:32
   .=f.x by A14,B1;
   hence thesis;
   end;
  dom((sin*ln)`|Z)=dom f by A1,A12,FDIFF_1:def 8;
  then ((sin*ln)`|Z)= f by A13,PARTFUN1:34;
  hence thesis by A11,B6,A1,FDIFF_7:32,INTEGRA5:13;
end;

Lm1: right_open_halfline 0 = {g where g is Real : 0<g} by XXREAL_1:230;
 
::f.x=sin.(ln.x)/x

theorem
 A c= Z & (for x st x in Z holds x>0)
 & Z = dom f & f=(sin*ln)(#)((id Z)^) implies
 integral(f,A)=(-cos*ln).(upper_bound A)-(-cos*ln).(lower_bound A)
proof
  assume
A1:A c= Z & (for x st x in Z holds x>0)
   & Z = dom f & f=(sin*ln)(#)((id Z)^); then
AA:Z = dom ((sin*ln)/(id Z)) by RFUNCT_1:47;
Z = dom (sin*ln) /\ dom ((id Z)^) by VALUED_1:def 4,A1;
then A4:Z c= dom (sin*ln) by XBOOLE_1:18;
for y st y in Z holds y in dom (cos*ln)
   proof
   let y;
   assume y in Z;then
   y in dom ln & ln.y in dom cos by A4,FUNCT_1:21,SIN_COS:27;
   hence thesis by FUNCT_1:21;
   end;
then B6:Z c= dom (cos*ln) by TARSKI:def 3;
A6:sin*ln is_differentiable_on Z by FDIFF_7:32,A4,A1;
not 0 in Z by A1;
then (id Z)^ is_differentiable_on Z by FDIFF_5:4;
   then f|Z is continuous by FDIFF_1:33,A1,A6,FDIFF_1:29;then
f|A is continuous by A1,FCONT_1:17;
then A11:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A12:cos*ln is_differentiable_on Z by B6,A1,FDIFF_7:33;
A13:Z c= dom (-cos*ln) by B6,VALUED_1:8;
then A14:(-1)(#)(cos*ln) is_differentiable_on Z by A12,FDIFF_1:28,X;
A15:for x st x in Z holds ((-cos*ln)`|Z).x =sin.(ln.x)/x
   proof
     let x;
     assume
A16:x in Z;then
    x>0 by A1;then
A18:x in right_open_halfline(0) by Lm1;
A19:ln is_differentiable_in x by A16,A1,TAYLOR_1:18;
A20:cos is_differentiable_in ln.x by SIN_COS:68;
A21:cos*ln is_differentiable_in x by A12,A16,FDIFF_1:16;
 ((-cos*ln)`|Z).x=diff(-cos*ln,x) by A14,A16,FDIFF_1:def 8
                .=(-1)*(diff(cos*ln,x)) by A21,FDIFF_1:23,X
                .=(-1)*(diff(cos,ln.x)*diff(ln,x)) by A19,A20,FDIFF_2:13
                .=(-1)*((-sin.(ln.x))*diff(ln,x)) by SIN_COS:68
                .=(-1)*((-sin.(ln.x))*(1/x)) by A18,TAYLOR_1:18
                .=sin.(ln.x)/x;
       hence thesis;
   end;
B7:for x st x in Z holds f.x=sin.(ln.x)/x
    proof
    let x;
    assume
B8: x in Z;
   ((sin*ln)(#)((id Z)^)).x=((sin*ln)/(id Z)).x by RFUNCT_1:47
                          .=(sin*ln).x*((id Z).x)" by RFUNCT_1:def 4,AA,B8
                          .=(sin*ln).x/x by FUNCT_1:35,B8
                          .=sin.(ln.x)/x by FUNCT_1:22,A4,B8;
   hence thesis by A1;
   end;
A22:for x st x in dom((-cos*ln)`|Z) holds ((-cos*ln)`|Z).x=f.x
   proof
     let x;
     assume x in dom((-cos*ln)`|Z);then
A23:x in Z by A14,FDIFF_1:def 8; then
  ((-cos*ln)`|Z).x=sin.(ln.x)/x by A15
   .=f.x by A23,B7;
   hence thesis;
   end;
  dom((-cos*ln)`|Z)=dom f by A1,A14,FDIFF_1:def 8;
  then ((-cos*ln)`|Z)= f by A22,PARTFUN1:34;
  hence thesis by A1,A11,A12,A13,FDIFF_1:28,X,INTEGRA5:13;
end;
 
::f.x=exp_R.x * cos.(exp_R.x)

theorem
 A c= Z & Z = dom f & f=exp_R(#)(cos*exp_R) implies
 integral(f,A)=(sin*exp_R).(upper_bound A)-(sin*exp_R).(lower_bound A)
proof
 assume
A1:A c= Z & Z = dom f & f=exp_R(#)(cos*exp_R);
then Z = dom exp_R /\ dom (cos*exp_R) by VALUED_1:def 4;
then A4:Z c= dom exp_R & Z c= dom (cos*exp_R) by XBOOLE_1:18;
for y st y in Z holds y in dom (sin*exp_R)
   proof
   let y;
   assume y in Z;then
   y in dom exp_R & exp_R.y in dom sin by A4,SIN_COS:27;
   hence thesis by FUNCT_1:21;
   end;
then B6:Z c= dom (sin*exp_R) by TARSKI:def 3;
A5:cos*exp_R is_differentiable_on Z by A4,FDIFF_7:35;
exp_R is_differentiable_on Z by FDIFF_1:34,TAYLOR_1:16;
   then f|Z is continuous by FDIFF_1:33,A1,A5,FDIFF_1:29;then
f|A is continuous by A1,FCONT_1:17;
then A9:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A10:sin*exp_R is_differentiable_on Z by B6,FDIFF_7:34;
B7:for x st x in Z holds f.x=exp_R.x * cos.(exp_R.x)
    proof
    let x;
    assume
B8:x in Z;
   then (exp_R(#)(cos*exp_R)).x=exp_R.x*(cos*exp_R).x by A1,VALUED_1:def 4
                         .=exp_R.x*cos.(exp_R.x) by FUNCT_1:22,A4,B8;
      hence thesis by A1;
   end;
A11:for x st x in dom((sin*exp_R)`|Z) holds ((sin*exp_R)`|Z).x = f.x
   proof
     let x;
     assume x in dom((sin*exp_R)`|Z);then
A12: x in Z by A10,FDIFF_1:def 8; then
     ((sin*exp_R)`|Z).x=exp_R.x * cos.(exp_R.x) by B6,FDIFF_7:34
       .=f.x by A12,B7;
     hence thesis;
   end;
  dom((sin*exp_R)`|Z)=dom f by A1,A10,FDIFF_1:def 8;
  then ((sin*exp_R)`|Z)= f by A11,PARTFUN1:34;
  hence thesis by A1,A9,B6,FDIFF_7:34,INTEGRA5:13;
end;
 
::f.x = exp_R.x * sin.(exp_R.x)

theorem
 A c= Z & Z = dom f & f=exp_R(#)(sin*exp_R) implies
 integral(f,A)=(-cos*exp_R).(upper_bound A)-(-cos*exp_R).(lower_bound A)
proof
 assume
A1:A c= Z & Z = dom f & f=exp_R(#)(sin*exp_R);
then Z = dom exp_R /\ dom (sin*exp_R) by VALUED_1:def 4;
then A4:Z c= dom exp_R & Z c= dom (sin*exp_R) by XBOOLE_1:18;
for y st y in Z holds y in dom (cos*exp_R)
   proof
   let y;
   assume y in Z;then
   y in dom exp_R & exp_R.y in dom cos by A4,SIN_COS:27;
   hence thesis by FUNCT_1:21;
   end;
then B6:Z c= dom (cos*exp_R) by TARSKI:def 3;
A5:sin*exp_R is_differentiable_on Z by A4,FDIFF_7:34;
exp_R is_differentiable_on Z by FDIFF_1:34,TAYLOR_1:16;
   then f|Z is continuous by FDIFF_1:33,A1,A5,FDIFF_1:29;then
f|A is continuous by A1,FCONT_1:17;
then A9:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A10:cos*exp_R is_differentiable_on Z by B6,FDIFF_7:35;
A11:Z c= dom (-cos*exp_R) by B6,VALUED_1:8;
then A12:(-1)(#)(cos*exp_R) is_differentiable_on Z by A10,FDIFF_1:28,X;
A13:for x st x in Z holds ((-cos*exp_R)`|Z).x = exp_R.x * sin.(exp_R.x)
   proof
     let x;
     assume
A14:x in Z;
A15: exp_R is_differentiable_in x by SIN_COS:70;
A16: cos is_differentiable_in exp_R.x by SIN_COS:68;
A17: cos*exp_R is_differentiable_in x by A10,A14,FDIFF_1:16;
  ((-cos*exp_R)`|Z).x=diff(-cos*exp_R,x) by A12,A14,FDIFF_1:def 8
                    .=(-1)*(diff(cos*exp_R,x)) by A17,FDIFF_1:23,X
                    .=(-1)*(diff(cos,exp_R.x)*diff(exp_R,x))
      by A15,A16,FDIFF_2:13
                    .=(-1)*((-sin.(exp_R.x))*diff(exp_R,x)) by SIN_COS:68
                    .=(-1)*((-sin.(exp_R.x))*exp_R.x) by SIN_COS:70
                    .=exp_R.x*sin.(exp_R.x);
     hence thesis;
   end;
B7:for x st x in Z holds f.x=exp_R.x * sin.(exp_R.x)
    proof
    let x;
    assume
B8:x in Z;
   then (exp_R(#)(sin*exp_R)).x=exp_R.x*(sin*exp_R).x by A1,VALUED_1:def 4
                         .=exp_R.x*sin.(exp_R.x) by FUNCT_1:22,A4,B8;
      hence thesis by A1;
   end;
A18:for x st x in dom((-cos*exp_R)`|Z) holds ((-cos*exp_R)`|Z).x = f.x
   proof
     let x;
     assume x in dom((-cos*exp_R)`|Z);then
A19:x in Z by A12,FDIFF_1:def 8; then
  ((-cos*exp_R)`|Z).x=exp_R.x * sin.(exp_R.x) by A13
   .=f.x by A19,B7;
   hence thesis;
   end;
  dom((-cos*exp_R)`|Z)=dom f by A1,A12,FDIFF_1:def 8;
  then ((-cos*exp_R)`|Z)= f by A18,PARTFUN1:34;
  hence thesis by A1,A9,A10,A11,FDIFF_1:28,X,INTEGRA5:13;
end;
 
::f.x=arctan.(x/r)

theorem
A c= Z & Z c= dom (ln*(f1+f2))
& r <> 0 & (for x st x in Z holds g.x=x/r & g.x > -1 & g.x < 1 & f1.x=1)
& f2=( #Z 2)*g & Z = dom f & f=arctan*g
implies
integral(f,A)=((id Z)(#)(arctan*g)-(r/2)(#)(ln*(f1+f2))).(upper_bound A)
             -((id Z)(#)(arctan*g)-(r/2)(#)(ln*(f1+f2))).(lower_bound A)
proof
  assume
A1:A c= Z & Z c= dom (ln*(f1+f2))
& r <> 0 & (for x st x in Z holds g.x=x/r & g.x > -1 & g.x < 1 & f1.x=1)
& f2=( #Z 2)*g & Z = dom f & f=arctan*g;
Z = dom id Z by RELAT_1:71;
then Z c= dom id Z /\ dom f by A1;
then A4:Z c= dom ((id Z)(#)(arctan*g)) by VALUED_1:def 4,A1;
Z c= dom ((r/2)(#)(ln*(f1+f2))) by A1,VALUED_1:def 5;
then Z c= dom ((id Z)(#)(arctan*g)) /\ dom ((r/2)(#)(ln*(f1+f2)))
   by XBOOLE_1:19,A4;
then A7:Z c= dom ((id Z)(#)(arctan*g)-(r/2)(#)(ln*(f1+f2))) by VALUED_1:12;
     for x st x in Z holds g.x=(1/r)*x+0
     proof
       let x;
       assume x in Z;
       then g.x=x/r by A1;
       hence thesis;
     end; then
 for x st x in Z holds g.x=(1/r)*x+0 & g.x > -1 & g.x < 1 by A1;
then f is_differentiable_on Z by A1,SIN_COS9:87;
    then f|Z is continuous by FDIFF_1:33;then
f|A is continuous by A1,FCONT_1:17;
then A12:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A13:(for x st x in Z holds g.x=x/r & g.x > -1 & g.x < 1)
    & (for x st x in Z holds f1.x=1)
    &(for x st x in Z holds g.x=x/r) by A1; then
A14:(id Z)(#)(arctan*g)-(r/2)(#)(ln*(f1+f2)) is_differentiable_on Z
   by A1,A7,SIN_COS9:109;
B1:for x st x in Z holds f.x=arctan.(x/r)
   proof
   let x;
   assume
B2:x in Z;
   then (arctan*g).x=arctan.(g.x) by FUNCT_1:22,A1
              .=arctan.(x/r) by A1,B2;
   hence thesis by A1;
   end;
A15:for x st x in dom(((id Z)(#)(arctan*g)-(r/2)(#)(ln*(f1+f2)))`|Z) holds
 (((id Z)(#)(arctan*g)-(r/2)(#)(ln*(f1+f2)))`|Z).x=f.x
  proof
   let x;
   assume x in dom(((id Z)(#)(arctan*g)-(r/2)(#)(ln*(f1+f2)))`|Z);
   then
A16:x in Z by A14,FDIFF_1:def 8;then
 (((id Z)(#)(arctan*g)-(r/2)(#)(ln*(f1+f2)))`|Z).x=arctan.(x/r)
 by A1,A7,A13,SIN_COS9:109
                                                .=f.x by B1,A16;
   hence thesis;
   end;
  dom(((id Z)(#)(arctan*g)-(r/2)(#)(ln*(f1+f2)))`|Z)=dom f
  by A1,A14,FDIFF_1:def 8;
  then (((id Z)(#)(arctan*g)-(r/2)(#)(ln*(f1+f2)))`|Z) = f by A15,PARTFUN1:34;
   hence thesis by A1,A12,A14,INTEGRA5:13;
end;
 
::f.x=arccot.(x/r)

theorem
A c= Z & Z c= dom (ln*(f1+f2))
& r <> 0 & (for x st x in Z holds g.x=x/r & g.x > -1 & g.x < 1 & f1.x=1)
& f2=( #Z 2)*g & Z = dom f & f=arccot*g implies
integral(f,A)=((id Z)(#)((arccot)*g)+(r/2)(#)(ln*(f1+f2))).(upper_bound A)
             -((id Z)(#)((arccot)*g)+(r/2)(#)(ln*(f1+f2))).(lower_bound A)
proof
  assume
A1:A c= Z & Z c= dom (ln*(f1+f2))
  & r <> 0 & (for x st x in Z holds g.x=x/r & g.x > -1 & g.x < 1 & f1.x=1)
  & f2=( #Z 2)*g & Z = dom f & f=(arccot)*g;
Z = dom id Z by RELAT_1:71;
then Z c= dom id Z /\ dom f by A1;
then A4:Z c= dom ((id Z)(#)((arccot)*g)) by VALUED_1:def 4,A1;
Z c= dom ((r/2)(#)(ln*(f1+f2))) by A1,VALUED_1:def 5;
then Z c= dom ((id Z)(#)((arccot)*g)) /\ dom ((r/2)(#)(ln*(f1+f2)))
   by XBOOLE_1:19,A4;
then
A7:Z c= dom ((id Z)(#)((arccot)*g)+(r/2)(#)(ln*(f1+f2))) by VALUED_1:def 1;
A8:for x st x in Z holds g.x=x/r & g.x > -1 & g.x < 1 by A1;
     for x st x in Z holds g.x=(1/r)*x+0
     proof
       let x;
       assume x in Z;
       then g.x=x/r by A1;
       hence thesis;
     end;
     then
 for x st x in Z holds g.x=(1/r)*x+0 & g.x > -1 & g.x < 1 by A1;
then f is_differentiable_on Z by A1,SIN_COS9:88;
    then f|Z is continuous by FDIFF_1:33;then
f|A is continuous by A1,FCONT_1:17;
then A12:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A13:(for x st x in Z holds f1.x=1)
    & (for x st x in Z holds g.x=x/r) by A1; then
A14:(id Z)(#)((arccot)*g)+(r/2)(#)(ln*(f1+f2)) is_differentiable_on Z
   by A1,A7,A8,SIN_COS9:110;
B1:for x st x in Z holds f.x=arccot.(x/r)
   proof
   let x;
   assume
B2:x in Z;
  then (arccot*g).x=arccot.(g.x) by FUNCT_1:22,A1
             .=arccot.(x/r) by A1,B2;
   hence thesis by A1;
   end;
A15:for x st x in dom(((id Z)(#)((arccot)*g)+(r/2)(#)(ln*(f1+f2)))`|Z) holds
 (((id Z)(#)((arccot)*g)+(r/2)(#)(ln*(f1+f2)))`|Z).x=f.x
  proof
   let x;
   assume x in dom(((id Z)(#)((arccot)*g)+(r/2)(#)(ln*(f1+f2)))`|Z); then
A16:x in Z by A14,FDIFF_1:def 8;then
 (((id Z)(#)((arccot)*g)+(r/2)(#)(ln*(f1+f2)))`|Z).x=arccot.(x/r)
 by A1,A7,A8,A13,SIN_COS9:110
                                                .=f.x by B1,A16;
   hence thesis;
   end;
dom(((id Z)(#)((arccot)*g)+(r/2)(#)(ln*(f1+f2)))`|Z)=dom f
   by A1,A14,FDIFF_1:def 8;
then (((id Z)(#)((arccot)*g)+(r/2)(#)(ln*(f1+f2)))`|Z) = f by A15,PARTFUN1:34;
   hence thesis by A1,A12,A14,INTEGRA5:13;
end;
 
::f.x=arctan.(x/r)+x/(r*(1+(x/r)^2))

theorem
 A c= Z & f=arctan*f1+(id Z)/(r(#)(g+f1^2))
 & (for x st x in Z holds g.x=1 & f1.x=x/r & f1.x > -1 & f1.x < 1)
 & Z = dom f & f|A is continuous
 implies integral(f,A)=((id Z)(#)(arctan*f1)).(upper_bound A)
                      -((id Z)(#)(arctan*f1)).(lower_bound A)
proof
   assume
A1:A c= Z & f=arctan*f1+(id Z)/(r(#)(g+f1^2))
   & (for x st x in Z holds g.x=1 & f1.x=x/r & f1.x > -1 & f1.x < 1)
   & Z = dom f & f|A is continuous;
then
Z = dom (arctan*f1) /\ dom ((id Z)/(r(#)(g+f1^2))) by VALUED_1:def 1;
then
A3:Z c= dom (arctan*f1) & Z c= dom ((id Z)/(r(#)(g+f1^2))) by XBOOLE_1:18;
   Z = dom id Z by RELAT_1:71;then
Z c= dom id Z /\ dom (arctan*f1) by A3,XBOOLE_1:19;
then A5:Z c= dom ((id Z)(#)(arctan*f1)) by VALUED_1:def 4;
Z c= dom (id Z) /\ (dom (r(#)(g+f1^2)) \ (r(#)(g+f1^2))"{0})
   by RFUNCT_1:def 4,A3;
then Z c= dom (r(#)(g+f1^2)) \ (r(#)(g+f1^2))"{0} by XBOOLE_1:18;
then A8:Z c= dom ((r(#)(g+f1^2))^) by RFUNCT_1:def 8;
   dom ((r(#)(g+f1^2))^) c= dom (r(#)(g+f1^2)) by RFUNCT_1:11; then
Z c= dom (r(#)(g+f1^2)) by XBOOLE_1:1,A8;
then A10:Z c= dom (g+f1^2) by VALUED_1:def 5;
A11:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A12:for x st x in Z holds f1.x=x/r & f1.x > -1 & f1.x < 1 by A1; then
A13:(id Z)(#)(arctan*f1) is_differentiable_on Z by A5,SIN_COS9:105;
A14:for x st x in Z holds f.x=arctan.(x/r)+x/(r*(1+(x/r)^2))
   proof
   let x;
   assume
A15:x in Z;
   then (arctan*f1+(id Z)/(r(#)(g+f1^2))).x
   =(arctan*f1).x+((id Z)/(r(#)(g+f1^2))).x by VALUED_1:def 1,A1
  .=arctan.(f1.x)+((id Z)/(r(#)(g+f1^2))).x by FUNCT_1:22,A3,A15
  .=arctan.(f1.x)+(id Z).x/(r(#)(g+f1^2)).x by RFUNCT_1:def 4,A3,A15
  .=arctan.(f1.x)+x/(r(#)(g+f1^2)).x by FUNCT_1:35,A15
  .=arctan.(f1.x)+x/(r*(g+f1^2).x) by VALUED_1:6
  .=arctan.(f1.x)+x/(r*(g.x+(f1^2).x)) by VALUED_1:def 1,A10,A15
  .=arctan.(f1.x)+x/(r*(g.x+((f1.x)^2))) by VALUED_1:11
  .=arctan.(x/r)+x/(r*(g.x+(f1.x)^2)) by A1,A15
  .=arctan.(x/r)+x/(r*(1+(f1.x)^2)) by A1,A15
  .=arctan.(x/r)+x/(r*(1+(x/r)^2)) by A1,A15;
   hence thesis by A1;
   end;
A16:for x st x in dom(((id Z)(#)(arctan*f1))`|Z) holds
   (((id Z)(#)(arctan*f1))`|Z).x=f.x
   proof
     let x;
     assume x in dom(((id Z)(#)((arctan)*f1))`|Z);then
A17: x in Z by A13,FDIFF_1:def 8;then
     (((id Z)(#)((arctan)*f1))`|Z).x=arctan.(x/r)+x/(r*(1+(x/r)^2))
       by A5,A12,SIN_COS9:105
                                .=f.x by A14,A17;
     hence thesis;
   end;
  dom(((id Z)(#)((arctan)*f1))`|Z) = dom f by A1,A13,FDIFF_1:def 8;
  then (((id Z)(#)((arctan)*f1))`|Z) = f by A16,PARTFUN1:34;
  hence thesis by A1,A11,A13,INTEGRA5:13;
end;
 
::f.x=arccot.(x/r)-x/(r*(1+(x/r)^2))

theorem
 A c= Z & f=arccot*f1-(id Z)/(r(#)(g+f1^2))
 & (for x st x in Z holds g.x=1 & f1.x=x/r & f1.x > -1 & f1.x < 1)
 & Z = dom f & f|A is continuous
 implies integral(f,A)=((id Z)(#)((arccot)*f1)).(upper_bound A)
                      -((id Z)(#)((arccot)*f1)).(lower_bound A)
proof
   assume
A1:A c= Z & f=arccot*f1-(id Z)/(r(#)(g+f1^2))
   & (for x st x in Z holds g.x=1 & f1.x=x/r & f1.x > -1 & f1.x < 1)
   & Z = dom f & f|A is continuous;
then Z = dom (arccot*f1) /\ dom ((id Z)/(r(#)(g+f1^2))) by VALUED_1:12;
then
A3:Z c= dom (arccot*f1) & Z c= dom ((id Z)/(r(#)(g+f1^2))) by XBOOLE_1:18;
   Z = dom id Z by RELAT_1:71;then
Z c= dom id Z /\ dom (arccot*f1) by A3,XBOOLE_1:19;
then A5:Z c= dom ((id Z)(#)(arccot*f1)) by VALUED_1:def 4;
Z c= dom (id Z) /\ (dom (r(#)(g+f1^2)) \ (r(#)(g+f1^2))"{0})
   by RFUNCT_1:def 4,A3;
then Z c= dom (r(#)(g+f1^2)) \ (r(#)(g+f1^2))"{0} by XBOOLE_1:18;
then A8:Z c= dom ((r(#)(g+f1^2))^) by RFUNCT_1:def 8;
   dom ((r(#)(g+f1^2))^) c= dom (r(#)(g+f1^2)) by RFUNCT_1:11; then
Z c= dom (r(#)(g+f1^2)) by XBOOLE_1:1,A8;
then A10:Z c= dom (g+f1^2) by VALUED_1:def 5;
A11:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A12:for x st x in Z holds f1.x=x/r & f1.x > -1 & f1.x < 1 by A1; then
A13:(id Z)(#)((arccot)*f1) is_differentiable_on Z by A5,SIN_COS9:106;
A14:for x st x in Z holds f.x=arccot.(x/r)-x/(r*(1+(x/r)^2))
   proof
   let x;
   assume
A15:x in Z;
   then (arccot*f1-(id Z)/(r(#)(g+f1^2))).x
   =(arccot*f1).x-((id Z)/(r(#)(g+f1^2))).x by VALUED_1:13,A1
  .=arccot.(f1.x)-((id Z)/(r(#)(g+f1^2))).x by FUNCT_1:22,A3,A15
  .=arccot.(f1.x)-(id Z).x/(r(#)(g+f1^2)).x by RFUNCT_1:def 4,A3,A15
  .=arccot.(f1.x)-x/(r(#)(g+f1^2)).x by FUNCT_1:35,A15
  .=arccot.(f1.x)-x/(r*(g+f1^2).x) by VALUED_1:6
  .=arccot.(f1.x)-x/(r*(g.x+(f1^2).x)) by VALUED_1:def 1,A10,A15
  .=arccot.(f1.x)-x/(r*(g.x+((f1.x)^2))) by VALUED_1:11
  .=arccot.(x/r)-x/(r*(g.x+(f1.x)^2)) by A1,A15
  .=arccot.(x/r)-x/(r*(1+(f1.x)^2)) by A1,A15
  .=arccot.(x/r)-x/(r*(1+(x/r)^2)) by A1,A15;
   hence thesis by A1;
   end;
A16:for x st x in dom(((id Z)(#)((arccot)*f1))`|Z) holds
   (((id Z)(#)((arccot)*f1))`|Z).x=f.x
   proof
     let x;
     assume x in dom(((id Z)(#)((arccot)*f1))`|Z);then
A17: x in Z by A13,FDIFF_1:def 8;then
     (((id Z)(#)((arccot)*f1))`|Z).x=arccot.(x/r)-x/(r*(1+(x/r)^2))
       by A5,A12,SIN_COS9:106
                                .=f.x by A14,A17;
    hence thesis;
   end;
  dom (((id Z)(#)((arccot)*f1))`|Z) = dom f by A1,A13,FDIFF_1:def 8;
  then (((id Z)(#)((arccot)*f1))`|Z) = f by A16,PARTFUN1:34;
  hence thesis by A1,A11,A13,INTEGRA5:13;
end;
 
::f.x=n*(arcsin.x) #Z (n-1) / sqrt(1-x^2)

theorem
 A c= Z & Z c= ]. -1,1 .[  & (for x st x in Z holds f1.x=1)
 & Z = dom f & Z c= dom (( #Z n)*arcsin) & 1<n &
 f=n(#)(( #Z (n-1))*arcsin)/(( #R (1/2))*(f1-#Z 2))
 implies integral(f,A) = (( #Z n)*arcsin).(upper_bound A)
                        -(( #Z n)*arcsin).(lower_bound A)
proof
   assume
A1:A c= Z & Z c= ]. -1,1 .[  & (for x st x in Z holds f1.x=1)
   & Z = dom f & Z c= dom (( #Z n)*arcsin) & 1<n &
   f=n(#)(( #Z (n-1))*arcsin)/(( #R (1/2))*(f1-#Z 2));
then Z = dom (n(#)(( #Z (n-1))*(arcsin))) /\
      (dom (( #R (1/2))*(f1-#Z 2)) \ (( #R (1/2))*(f1-#Z 2))"{0})
      by RFUNCT_1:def 4;
then A3:Z c= dom (n(#)(( #Z (n-1))*arcsin)) &
   Z c= dom (( #R (1/2))*(f1-#Z 2)) \ (( #R (1/2))*(f1-#Z 2))"{0}
   by XBOOLE_1:18;
then A4:Z c= dom (( #Z (n-1))*arcsin) by VALUED_1:def 5;
A5:Z c= dom ((( #R (1/2))*(f1-#Z 2))^) by RFUNCT_1:def 8,A3;
   dom ((( #R (1/2))*(f1-#Z 2))^) c= dom (( #R (1/2))*(f1-#Z 2))
   by RFUNCT_1:11; then
A6:Z c= dom (( #R (1/2))*(f1-#Z 2)) by A5,XBOOLE_1:1;
for x st x in Z holds ( #Z (n-1))*arcsin is_differentiable_in x
   proof
     let x;
     assume x in Z; then
A8:  arcsin is_differentiable_in x by A1,FDIFF_1:16,SIN_COS6:84;
     consider m being Nat such that
A9:  n = m + 1 by A1,NAT_1:6;
     thus thesis by A8,A9,TAYLOR_1:3;
   end;
then ( #Z (n-1))*arcsin is_differentiable_on Z by A4,FDIFF_1:16;
then A11:n(#)(( #Z (n-1))*arcsin) is_differentiable_on Z by A3,FDIFF_1:28;
    set f2=#Z 2;
for x st x in Z holds (f1-#Z 2).x >0
   proof
   let x;
   assume
A14:x in Z; then
   -1 < x & x < 1 by A1,XXREAL_1:4; then
   0 < 1+x & 0 < 1-x by XREAL_1:150,XREAL_1:52; then
A15:0 < (1+x)*(1-x) by XREAL_1:131;
for x st x in Z holds x in dom (f1-f2) by FUNCT_1:21,A6;
     then (f1-f2).x = f1.x - f2.x by A14,VALUED_1:13
             .=f1.x - (x #Z (1+1)) by TAYLOR_1:def 1
             .=f1.x - ((x #Z 1)*(x #Z 1)) by TAYLOR_1:1
             .=f1.x - (x*(x #Z 1)) by PREPOWER:45
             .=f1.x - x*x by PREPOWER:45
             .=1 - x*x by A1,A14;
     hence thesis by A15;
    end;
then for x st x in Z holds f1.x=1 & (f1-#Z 2).x >0 by A1;
then A23:(( #R (1/2))*(f1-#Z 2)) is_differentiable_on Z by A6,FDIFF_7:22;
x in Z implies (( #R (1/2))*(f1-#Z 2)).x<>0 by RFUNCT_1:13,A5;
then f is_differentiable_on Z by A1,A11,A23,FDIFF_2:21;
   then f|Z is continuous by FDIFF_1:33;then
f|A is continuous by A1,FCONT_1:17;
then A27:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A28:( #Z n)*arcsin is_differentiable_on Z by A1,FDIFF_7:10;
B1:for x st x in Z holds f.x=n*(arcsin.x) #Z (n-1) / sqrt(1-x^2)
    proof
    let x;
    assume
B2:x in Z;
then
B3:x in dom (f1-#Z 2) & (f1-#Z 2).x in dom ( #R (1/2)) by FUNCT_1:21,A6;
then B4:(f1-#Z 2).x in right_open_halfline(0) by TAYLOR_1:def 4;
   -1 < x & x < 1 by A1,XXREAL_1:4,B2; then
   0 < 1+x & 0 < 1-x by XREAL_1:150,XREAL_1:52; then
B6:0 < (1+x)*(1-x) by XREAL_1:131;
   (n(#)(( #Z (n-1))*arcsin)/(( #R (1/2))*(f1-#Z 2))).x
  =(n(#)(( #Z (n-1))*arcsin)).x/(( #R (1/2))*(f1-#Z 2)).x
    by RFUNCT_1:def 4,A1,B2
 .=n*(( #Z (n-1))*arcsin).x/(( #R (1/2))*(f1-#Z 2)).x by VALUED_1:6
 .=n*(( #Z (n-1)).(arcsin.x))/(( #R (1/2))*(f1-#Z 2)).x
    by FUNCT_1:22,A4,B2
 .=n*(arcsin.x) #Z (n-1) / (( #R (1/2))*(f1-#Z 2)).x by TAYLOR_1:def 1
 .=n*(arcsin.x) #Z (n-1) / (( #R (1/2)).((f1-#Z 2).x)) by FUNCT_1:22,A6,B2
 .=n*(arcsin.x) #Z (n-1) / (((f1-#Z 2).x) #R (1/2)) by TAYLOR_1:def 4,B4
 .=n*(arcsin.x) #Z (n-1) / ((f1.x-(( #Z 2).x)) #R (1/2))
    by VALUED_1:13,B3
 .=n*(arcsin.x) #Z (n-1) / ((f1.x-(x #Z 2)) #R (1/2)) by TAYLOR_1:def 1
 .=n*(arcsin.x) #Z (n-1) / ((f1.x-x^2) #R (1/2)) by FDIFF_7:1
 .=n*(arcsin.x) #Z (n-1) / ((1-x^2) #R (1/2)) by A1,B2
 .=n*(arcsin.x) #Z (n-1) / sqrt(1-x^2) by FDIFF_7:2,B6;
    hence thesis by A1;
    end;
A29:for x st x in dom ((( #Z n)*(arcsin))`|Z) holds
    ((( #Z n)*(arcsin))`|Z).x=f.x
     proof
     let x;
     assume x in dom ((( #Z n)*(arcsin))`|Z);then
A30:x in Z by A28,FDIFF_1:def 8; then
  ((( #Z n)*(arcsin))`|Z).x=n*(arcsin.x) #Z (n-1) / sqrt(1-x^2)
   by A1,FDIFF_7:10
   .=f.x by B1,A30;
   hence thesis;
   end;
  dom ((( #Z n)*(arcsin))`|Z)=dom f by A1,A28,FDIFF_1:def 8;
  then ((( #Z n)*(arcsin))`|Z)= f by A29,PARTFUN1:34;
  hence thesis by A1,A27,FDIFF_7:10,INTEGRA5:13;
end;
 
::f.x=n*(arccos.x) #Z (n-1) / sqrt(1-x^2)

theorem
 A c= Z & Z c= ]. -1,1 .[  & (for x st x in Z holds f1.x=1)
 & Z c= dom (( #Z n)*(arccos)) & Z = dom f & 1<n &
 f=n(#)(( #Z (n-1))*arccos)/(( #R (1/2))*(f1-#Z 2))
 implies integral(f,A) = (-( #Z n)*(arccos)).(upper_bound A)
                       - (-( #Z n)*(arccos)).(lower_bound A)
proof
   assume
A1:A c= Z & Z c= ]. -1,1 .[  & (for x st x in Z holds f1.x=1)
   & Z c= dom (( #Z n)*(arccos)) & Z = dom f & 1<n &
   f=n(#)(( #Z (n-1))*arccos)/(( #R (1/2))*(f1-#Z 2));
then Z = dom (n(#)(( #Z (n-1))*(arccos))) /\
      (dom (( #R (1/2))*(f1-#Z 2)) \ (( #R (1/2))*(f1-#Z 2))"{0})
      by RFUNCT_1:def 4;
then A3:Z c= dom (n(#)(( #Z (n-1))*(arccos))) &
   Z c= dom (( #R (1/2))*(f1-#Z 2)) \ (( #R (1/2))*(f1-#Z 2))"{0}
   by XBOOLE_1:18;
then A4:Z c= dom (( #Z (n-1))*(arccos)) by VALUED_1:def 5;
A5:Z c= dom ((( #R (1/2))*(f1-#Z 2))^) by RFUNCT_1:def 8,A3;
   dom ((( #R (1/2))*(f1-#Z 2))^) c= dom (( #R (1/2))*(f1-#Z 2))
   by RFUNCT_1:11; then
A6:Z c= dom (( #R (1/2))*(f1-#Z 2)) by A5,XBOOLE_1:1;
for x st x in Z holds ( #Z (n-1))*(arccos) is_differentiable_in x
     proof
     let x;
     assume x in Z; then
A8:  arccos is_differentiable_in x by A1,FDIFF_1:16,SIN_COS6:108;
     consider m being Nat such that
A9:  n = m + 1 by A1,NAT_1:6;
      thus thesis by A8,A9,TAYLOR_1:3;
      end;
then ( #Z (n-1))*(arccos) is_differentiable_on Z by A4,FDIFF_1:16;
then
A11:n(#)(( #Z (n-1))*(arccos)) is_differentiable_on Z by A3,FDIFF_1:28;
    set f2=#Z 2;
for x st x in Z holds (f1-#Z 2).x >0
   proof
   let x;
   assume
A14:x in Z; then
   -1 < x & x < 1 by A1,XXREAL_1:4; then
   0 < 1+x & 0 < 1-x by XREAL_1:150,XREAL_1:52; then
A15:0 < (1+x)*(1-x) by XREAL_1:131;
for x st x in Z holds x in dom (f1-f2) by FUNCT_1:21,A6;
     then (f1-f2).x = f1.x - f2.x by A14,VALUED_1:13
             .=f1.x - (x #Z (1+1)) by TAYLOR_1:def 1
             .=f1.x - ((x #Z 1)*(x #Z 1)) by TAYLOR_1:1
             .=f1.x - (x*(x #Z 1)) by PREPOWER:45
             .=f1.x - x*x by PREPOWER:45
             .=1 - x*x by A1,A14;
     hence thesis by A15;
    end;
then for x st x in Z holds f1.x=1 & (f1-#Z 2).x >0 by A1;
then A23:(( #R (1/2))*(f1-#Z 2)) is_differentiable_on Z by A6,FDIFF_7:22;
x in Z implies (( #R (1/2))*(f1-#Z 2)).x<>0 by RFUNCT_1:13,A5;
then f is_differentiable_on Z by A1,A11,A23,FDIFF_2:21;
   then f|Z is continuous by FDIFF_1:33;then
f|A is continuous by A1,FCONT_1:17;
then A27:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A28:( #Z n)*arccos is_differentiable_on Z by A1,FDIFF_7:11;
A29:Z c= dom (-( #Z n)*arccos) by A1,VALUED_1:8;
then
A30:(-1)(#)(( #Z n)*arccos) is_differentiable_on Z by A28,FDIFF_1:28,X;
B1:for x st x in Z holds f.x=n*(arccos.x) #Z (n-1) / sqrt(1-x^2)
    proof
    let x;
    assume
B2:x in Z;
then
B3:x in dom (f1-#Z 2) & (f1-#Z 2).x in dom ( #R (1/2)) by FUNCT_1:21,A6;
then B4:(f1-#Z 2).x in right_open_halfline(0) by TAYLOR_1:def 4;
   -1 < x & x < 1 by A1,XXREAL_1:4,B2; then
   0 < 1+x & 0 < 1-x by XREAL_1:150,XREAL_1:52; then
B6:0 < (1+x)*(1-x) by XREAL_1:131;
   (n(#)(( #Z (n-1))*arccos)/(( #R (1/2))*(f1-#Z 2))).x
  =(n(#)(( #Z (n-1))*arccos)).x/(( #R (1/2))*(f1-#Z 2)).x
    by RFUNCT_1:def 4,A1,B2
 .=n*(( #Z (n-1))*arccos).x/(( #R (1/2))*(f1-#Z 2)).x by VALUED_1:6
 .=n*(( #Z (n-1)).(arccos.x))/(( #R (1/2))*(f1-#Z 2)).x
    by FUNCT_1:22,A4,B2
 .=n*(arccos.x) #Z (n-1) / (( #R (1/2))*(f1-#Z 2)).x by TAYLOR_1:def 1
 .=n*(arccos.x) #Z (n-1) / (( #R (1/2)).((f1-#Z 2).x))
    by FUNCT_1:22,A6,B2
 .=n*(arccos.x) #Z (n-1) / (((f1-#Z 2).x) #R (1/2)) by TAYLOR_1:def 4,B4
 .=n*(arccos.x) #Z (n-1) / ((f1.x-(( #Z 2).x)) #R (1/2))
    by VALUED_1:13,B3
 .=n*(arccos.x) #Z (n-1) / ((f1.x-(x #Z 2)) #R (1/2)) by TAYLOR_1:def 1
 .=n*(arccos.x) #Z (n-1) / ((f1.x-x^2) #R (1/2)) by FDIFF_7:1
 .=n*(arccos.x) #Z (n-1) / ((1-x^2) #R (1/2)) by A1,B2
 .=n*(arccos.x) #Z (n-1) / sqrt(1-x^2) by FDIFF_7:2,B6;
    hence thesis by A1;
    end;
A31:for x st x in Z holds ((-( #Z n)*(arccos))`|Z).x
                          =n*(arccos.x) #Z (n-1) / sqrt(1-x^2)
   proof
     let x;
     assume
A32:x in Z;then
A33:-1 < x & x < 1 by A1,XXREAL_1:4;
A34:arccos is_differentiable_in x by A1,A32,FDIFF_1:16,SIN_COS6:108;
A35:( #Z n)*(arccos) is_differentiable_in x by A28,A32,FDIFF_1:16;
 ((-( #Z n)*(arccos))`|Z).x=diff(-( #Z n)*(arccos),x) by A30,A32,FDIFF_1:def 8
                          .=(-1)*diff(( #Z n)*(arccos),x) by A35,FDIFF_1:23,X
                          .=(-1)*((n*((arccos.x) #Z (n-1)))
* diff(arccos,x)) by A34,TAYLOR_1:3
                          .=(-1)*((n*((arccos.x) #Z (n-1)))
*(-(1 / sqrt(1-x^2)))) by A33,SIN_COS6:108
                          .=n*(arccos.x) #Z (n-1) / sqrt(1-x^2);
     hence thesis;
    end;
A36:for x st x in dom ((-( #Z n)*(arccos))`|Z) holds
    ((-( #Z n)*(arccos))`|Z).x=f.x
     proof
     let x;
     assume x in dom ((-( #Z n)*(arccos))`|Z);then
A37:x in Z by A30,FDIFF_1:def 8; then
 ((-( #Z n)*(arccos))`|Z).x=n*(arccos.x) #Z (n-1) / sqrt(1-x^2) by A31
   .=f.x by B1,A37;
   hence thesis;
   end;
  dom ((-( #Z n)*(arccos))`|Z)=dom f by A1,A30,FDIFF_1:def 8;
  then ((-( #Z n)*(arccos))`|Z)= f by A36,PARTFUN1:34;
  hence thesis by A1,A27,A28,A29,FDIFF_1:28,X,INTEGRA5:13;
end;
 
::f.x=arcsin.x+x/sqrt(1-x^2)

theorem
 A c= Z & (for x st x in Z holds f1.x=1)
 & Z c= ]. -1,1 .[ & Z = dom f & f=arcsin+(id Z)/(( #R (1/2))*(f1-#Z 2))
 implies
 integral(f,A)
 =((id Z)(#)(arcsin)).(upper_bound A)-((id Z)(#)(arcsin)).(lower_bound A)
proof
  assume
A1:A c= Z & (for x st x in Z holds f1.x=1) & Z c= ]. -1,1 .[ & Z = dom f
   & f=arcsin+(id Z)/(( #R (1/2))*(f1-#Z 2));
then
Z = dom arcsin /\ dom ((id Z)/(( #R (1/2))*(f1-#Z 2))) by VALUED_1:def 1;
then A3:Z c= dom arcsin & Z c= dom ((id Z)/(( #R (1/2))*(f1-#Z 2)))
   by XBOOLE_1:18;
A4:Z = dom id Z by RELAT_1:71;
then Z c= dom id Z /\ dom arcsin by XBOOLE_1:19,A3;
then A6:Z c= dom ((id Z)(#)(arcsin)) by VALUED_1:def 4;
A7:arcsin is_differentiable_on Z by A1,FDIFF_1:34,SIN_COS6:84;
   for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;then
A8:id Z is_differentiable_on Z by FDIFF_1:31,A4;
Z c= dom id Z /\ (dom (( #R (1/2))*(f1-#Z 2)) \ (( #R (1/2))*(f1-#Z 2))"{0})
   by RFUNCT_1:def 4,A3;
then Z c= dom (( #R (1/2))*(f1-#Z 2)) \ (( #R (1/2))*(f1-#Z 2))"{0}
    by XBOOLE_1:18;
then A11:Z c= dom ((( #R (1/2))*(f1-#Z 2))^) by RFUNCT_1:def 8;
   dom ((( #R (1/2))*(f1-#Z 2))^) c= dom (( #R (1/2))*(f1-#Z 2))
   by RFUNCT_1:11; then
A12:Z c= dom (( #R (1/2))*(f1-#Z 2)) by A11,XBOOLE_1:1;
    set f2=#Z 2;
for x st x in Z holds (f1-#Z 2).x >0
   proof
   let x;
   assume
A15:x in Z; then
   -1 < x & x < 1 by A1,XXREAL_1:4; then
   0 < 1+x & 0 < 1-x by XREAL_1:150,XREAL_1:52; then
A16:0 < (1+x)*(1-x) by XREAL_1:131;
for x st x in Z holds x in dom (f1-f2) by FUNCT_1:21,A12;
     then (f1-f2).x = f1.x - f2.x by A15,VALUED_1:13
             .=f1.x - (x #Z (1+1)) by TAYLOR_1:def 1
             .=f1.x - ((x #Z 1)*(x #Z 1)) by TAYLOR_1:1
             .=f1.x - (x*(x #Z 1)) by PREPOWER:45
             .=f1.x - x*x by PREPOWER:45
             .=1 - x*x by A1,A15;
     hence thesis by A16;
    end;
then for x st x in Z holds f1.x=1 & (f1-#Z 2).x >0 by A1;
then A24:(( #R (1/2))*(f1-#Z 2)) is_differentiable_on Z by A12,FDIFF_7:22;
x in Z implies (( #R (1/2))*(f1-#Z 2)).x<>0 by RFUNCT_1:13,A11;
then (id Z)/(( #R (1/2))*(f1-#Z 2)) is_differentiable_on Z
    by A8,A24,FDIFF_2:21;
    then f|Z is continuous by FDIFF_1:33,A1,A7,FDIFF_1:26;then
f|A is continuous by A1,FCONT_1:17;
then A29:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A30:(id Z)(#)(arcsin) is_differentiable_on Z by A1,A6,FDIFF_7:16;
B1:for x st x in Z holds f.x=arcsin.x+x/sqrt(1-x^2)
   proof
   let x;
   assume
B2:x in Z;
then
B3:x in dom (f1-#Z 2) & (f1-#Z 2).x in dom ( #R (1/2)) by FUNCT_1:21,A12;
then B4:(f1-#Z 2).x in right_open_halfline(0) by TAYLOR_1:def 4;
   -1 < x & x < 1 by A1,XXREAL_1:4,B2; then
   0 < 1+x & 0 < 1-x by XREAL_1:150,XREAL_1:52; then
B6:0 < (1+x)*(1-x) by XREAL_1:131;
   (arcsin+(id Z)/(( #R (1/2))*(f1-#Z 2))).x
   =arcsin.x+((id Z)/(( #R (1/2))*(f1-#Z 2))).x by VALUED_1:def 1,A1,B2
  .=arcsin.x+(id Z).x/(( #R (1/2))*(f1-#Z 2)).x by RFUNCT_1:def 4,A3,B2
  .=arcsin.x+x/(( #R (1/2))*(f1-#Z 2)).x by FUNCT_1:35,B2
  .=arcsin.x+x/(( #R (1/2)).((f1-#Z 2).x)) by A12,FUNCT_1:22,B2
  .=arcsin.x+x/(((f1-#Z 2).x) #R (1/2)) by TAYLOR_1:def 4,B4
  .=arcsin.x+x/((f1.x-(( #Z 2).x)) #R (1/2)) by VALUED_1:13,B3
  .=arcsin.x+x/((f1.x-(x #Z 2)) #R (1/2)) by TAYLOR_1:def 1
  .=arcsin.x+x/((f1.x-x^2) #R (1/2)) by FDIFF_7:1
  .=arcsin.x+x/((1-x^2) #R (1/2)) by A1,B2
  .=arcsin.x+x/sqrt(1-x^2) by FDIFF_7:2,B6;
    hence thesis by A1;
   end;
A31:for x st x in dom (((id Z)(#)(arcsin))`|Z) holds
   (((id Z)(#)(arcsin))`|Z).x=f.x
    proof
    let x;
    assume x in dom(((id Z)(#)(arcsin))`|Z);then
A32:x in Z by A30,FDIFF_1:def 8; then
  (((id Z)(#)(arcsin))`|Z).x=arcsin.x+x/sqrt(1-x^2) by A1,A6,FDIFF_7:16
   .=f.x by B1,A32;
   hence thesis;
   end;
   dom(((id Z)(#)(arcsin))`|Z)=dom f by A1,A30,FDIFF_1:def 8;
   then(((id Z)(#)(arcsin))`|Z)= f by A31,PARTFUN1:34;
   hence thesis by A1,A29,A6,FDIFF_7:16,INTEGRA5:13;
end;
 
::f.x=arccos.x-x/sqrt(1-x^2)

theorem
 A c= Z & (for x st x in Z holds f1.x=1)
 & Z c= ]. -1,1 .[ & Z = dom f & f=arccos-(id Z)/(( #R (1/2))*(f1-#Z 2))
 implies
 integral(f,A)
 =((id Z)(#)(arccos)).(upper_bound A)-((id Z)(#)(arccos)).(lower_bound A)
proof
  assume
A1:A c= Z & (for x st x in Z holds f1.x=1) & Z c= ]. -1,1 .[ & Z = dom f
   & f=arccos-(id Z)/(( #R (1/2))*(f1-#Z 2));
then
Z = dom arccos /\ dom ((id Z)/(( #R (1/2))*(f1-#Z 2))) by VALUED_1:12;
then A3:Z c= dom arccos & Z c= dom ((id Z)/(( #R (1/2))*(f1-#Z 2)))
   by XBOOLE_1:18;
A4:Z = dom id Z by RELAT_1:71;
then Z c= dom id Z /\ dom arccos by XBOOLE_1:19,A3;
then A6:Z c= dom ((id Z)(#)(arccos)) by VALUED_1:def 4;
A7:arccos is_differentiable_on Z by A1,FDIFF_1:34,SIN_COS6:108;
   for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;then
A8:id Z is_differentiable_on Z by FDIFF_1:31,A4;
Z c= dom id Z /\ (dom (( #R (1/2))*(f1-#Z 2)) \ (( #R (1/2))*(f1-#Z 2))"{0})
   by RFUNCT_1:def 4,A3;
then Z c= dom (( #R (1/2))*(f1-#Z 2)) \ (( #R (1/2))*(f1-#Z 2))"{0}
    by XBOOLE_1:18;
then A11:Z c= dom ((( #R (1/2))*(f1-#Z 2))^) by RFUNCT_1:def 8;
   dom ((( #R (1/2))*(f1-#Z 2))^) c= dom (( #R (1/2))*(f1-#Z 2))
   by RFUNCT_1:11; then
A12:Z c= dom (( #R (1/2))*(f1-#Z 2)) by A11,XBOOLE_1:1;
    set f2=#Z 2;
for x st x in Z holds (f1-#Z 2).x >0
   proof
   let x;
   assume
A15:x in Z; then
   -1 < x & x < 1 by A1,XXREAL_1:4; then
   0 < 1+x & 0 < 1-x by XREAL_1:150,XREAL_1:52; then
A16:0 < (1+x)*(1-x) by XREAL_1:131;
for x st x in Z holds x in dom (f1-f2) by FUNCT_1:21,A12;
     then (f1-f2).x = f1.x - f2.x by A15,VALUED_1:13
             .=f1.x - (x #Z (1+1)) by TAYLOR_1:def 1
             .=f1.x - ((x #Z 1)*(x #Z 1)) by TAYLOR_1:1
             .=f1.x - (x*(x #Z 1)) by PREPOWER:45
             .=f1.x - x*x by PREPOWER:45
             .=1 - x*x by A1,A15;
     hence thesis by A16;
    end;
then for x st x in Z holds f1.x=1 & (f1-#Z 2).x >0 by A1;
then A24:(( #R (1/2))*(f1-#Z 2)) is_differentiable_on Z by A12,FDIFF_7:22;
x in Z implies (( #R (1/2))*(f1-#Z 2)).x<>0 by RFUNCT_1:13,A11;
then (id Z)/(( #R (1/2))*(f1-#Z 2)) is_differentiable_on Z
    by A8,A24,FDIFF_2:21;
    then f|Z is continuous by FDIFF_1:33,A1,A7,FDIFF_1:27;then
f|A is continuous by A1,FCONT_1:17;
then A29:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A30:(id Z)(#)(arccos) is_differentiable_on Z by A1,A6,FDIFF_7:17;
B1:for x st x in Z holds f.x=arccos.x-x/sqrt(1-x^2)
   proof
   let x;
   assume
B2:x in Z;
then
B3:x in dom (f1-#Z 2) & (f1-#Z 2).x in dom ( #R (1/2)) by FUNCT_1:21,A12;
then B4:(f1-#Z 2).x in right_open_halfline(0) by TAYLOR_1:def 4;
   -1 < x & x < 1 by A1,XXREAL_1:4,B2; then
   0 < 1+x & 0 < 1-x by XREAL_1:150,XREAL_1:52; then
B6:0 < (1+x)*(1-x) by XREAL_1:131;
   (arccos-(id Z)/(( #R (1/2))*(f1-#Z 2))).x
   =arccos.x-((id Z)/(( #R (1/2))*(f1-#Z 2))).x by VALUED_1:13,A1,B2
  .=arccos.x-(id Z).x/(( #R (1/2))*(f1-#Z 2)).x by RFUNCT_1:def 4,A3,B2
  .=arccos.x-x/(( #R (1/2))*(f1-#Z 2)).x by FUNCT_1:35,B2
  .=arccos.x-x/(( #R (1/2)).((f1-#Z 2).x)) by A12,FUNCT_1:22,B2
  .=arccos.x-x/(((f1-#Z 2).x) #R (1/2)) by TAYLOR_1:def 4,B4
  .=arccos.x-x/((f1.x-(( #Z 2).x)) #R (1/2)) by VALUED_1:13,B3
  .=arccos.x-x/((f1.x-(x #Z 2)) #R (1/2)) by TAYLOR_1:def 1
  .=arccos.x-x/((f1.x-x^2) #R (1/2)) by FDIFF_7:1
  .=arccos.x-x/((1-x^2) #R (1/2)) by A1,B2
  .=arccos.x-x/sqrt(1-x^2) by FDIFF_7:2,B6;
    hence thesis by A1;
   end;
A31:for x st x in dom (((id Z)(#)(arccos))`|Z) holds
   (((id Z)(#)(arccos))`|Z).x=f.x
    proof
    let x;
    assume x in dom(((id Z)(#)(arccos))`|Z);then
A32:x in Z by A30,FDIFF_1:def 8; then
  (((id Z)(#)(arccos))`|Z).x=arccos.x-x/sqrt(1-x^2) by A1,A6,FDIFF_7:17
   .=f.x by B1,A32;
   hence thesis;
   end;
  dom(((id Z)(#)(arccos))`|Z)=dom f by A1,A30,FDIFF_1:def 8;
  then(((id Z)(#)(arccos))`|Z)= f by A31,PARTFUN1:34;
  hence thesis by A1,A29,A6,FDIFF_7:17,INTEGRA5:13;
end;
 
::f.x=a*arcsin.x+(a*x+b)/sqrt(1-x^2)

theorem
A c= Z & Z c= ]. -1,1 .[ &
(for x st x in Z holds f1.x=a*x+b & f2.x=1) & Z = dom f
& f=a(#)arcsin+f1/(( #R (1/2))*(f2-#Z 2)) implies
integral(f,A)=(f1(#)(arcsin)).(upper_bound A)-(f1(#)(arcsin)).(lower_bound A)
proof
   assume
A1:A c= Z & Z c= ]. -1,1 .[ &
  (for x st x in Z holds f1.x=a*x+b & f2.x=1) & Z = dom f
  & f=a(#)arcsin+f1/(( #R (1/2))*(f2-#Z 2));
then Z = dom (a(#)arcsin) /\ dom (f1/(( #R (1/2))*(f2-#Z 2)))
   by VALUED_1:def 1;
then A3:Z c= dom (a(#)arcsin) & Z c= dom (f1/(( #R (1/2))*(f2-#Z 2)))
   by XBOOLE_1:18;
then A4:Z c= dom arcsin by VALUED_1:def 5;
Z c= dom f1 /\ (dom (( #R (1/2))*(f2-#Z 2)) \ (( #R (1/2))*(f2-#Z 2))"{0})
   by A3,RFUNCT_1:def 4;
then
A6:Z c= dom f1 & Z c= dom (( #R (1/2))*(f2-#Z 2)) \ (( #R (1/2))*(f2-#Z 2))"{0}
   by XBOOLE_1:18;
then Z c= dom f1 /\ dom arcsin by XBOOLE_1:19,A4;
then A8:Z c= dom (f1(#)arcsin) by VALUED_1:def 4;
A9:Z c= dom ((( #R (1/2))*(f2-#Z 2))^) by RFUNCT_1:def 8,A6;
   dom ((( #R (1/2))*(f2-#Z 2))^) c= dom (( #R (1/2))*(f2-#Z 2))
   by RFUNCT_1:11; then
A10:Z c= dom (( #R (1/2))*(f2-#Z 2)) by XBOOLE_1:1,A9;
A11:arcsin is_differentiable_on Z by A1,FDIFF_1:34,SIN_COS6:84;
then A12:a(#)arcsin is_differentiable_on Z by A3,FDIFF_1:28;
A13:for x st x in Z holds f1.x = a*x+b by A1;
then A14:f1 is_differentiable_on Z by A6,FDIFF_1:31;
    set f3=#Z 2;
for x st x in Z holds (f2-#Z 2).x >0
   proof
   let x;
   assume
A16:x in Z; then
   -1 < x & x < 1 by A1,XXREAL_1:4; then
   0 < 1+x & 0 < 1-x by XREAL_1:150,XREAL_1:52; then
A17:0 < (1+x)*(1-x) by XREAL_1:131;
for x st x in Z holds x in dom (f2-f3) by FUNCT_1:21,A10;
    then (f2-f3).x = f2.x - f3.x by A16,VALUED_1:13
             .=f2.x - (x #Z (1+1)) by TAYLOR_1:def 1
             .=f2.x - ((x #Z 1)*(x #Z 1)) by TAYLOR_1:1
             .=f2.x - (x*(x #Z 1)) by PREPOWER:45
             .=f2.x - x*x by PREPOWER:45
             .=1 - x*x by A1,A16;
     hence thesis by A17;
    end;
then for x st x in Z holds f2.x=1 & (f2-#Z 2).x >0 by A1;
then A25:(( #R (1/2))*(f2-#Z 2)) is_differentiable_on Z by A10,FDIFF_7:22;
x in Z implies (( #R (1/2))*(f2-#Z 2)).x<>0 by RFUNCT_1:13,A9;
then f1/(( #R (1/2))*(f2-#Z 2)) is_differentiable_on Z
    by A14,A25,FDIFF_2:21;
then f is_differentiable_on Z by A1,A12,FDIFF_1:26;
    then f|Z is continuous by FDIFF_1:33;then
f|A is continuous by A1,FCONT_1:17;
then A30:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A31:f1(#)(arcsin) is_differentiable_on Z by A8,A11,A14,FDIFF_1:29;
B1:for x st x in Z holds f.x=a*arcsin.x+(a*x+b)/sqrt(1-x^2)
   proof
   let x;
   assume
B2:x in Z;
then
B3:x in dom (f2-#Z 2) & (f2-#Z 2).x in dom ( #R (1/2)) by FUNCT_1:21,A10;
then B4:(f2-#Z 2).x in right_open_halfline(0) by TAYLOR_1:def 4;
   -1 < x & x < 1 by A1,XXREAL_1:4,B2; then
   0 < 1+x & 0 < 1-x by XREAL_1:150,XREAL_1:52; then
B6:0 < (1+x)*(1-x) by XREAL_1:131;
   (a(#)arcsin+f1/(( #R (1/2))*(f2-#Z 2))).x
   =(a(#)arcsin).x+(f1/(( #R (1/2))*(f2-#Z 2))).x by VALUED_1:def 1,A1,B2
  .=a*arcsin.x+(f1/(( #R (1/2))*(f2-#Z 2))).x by VALUED_1:6
  .=a*arcsin.x+f1.x/(( #R (1/2))*(f2-#Z 2)).x by RFUNCT_1:def 4,A3,B2
  .=a*arcsin.x+(a*x+b)/(( #R (1/2))*(f2-#Z 2)).x by A1,B2
  .=a*arcsin.x+(a*x+b)/(( #R (1/2)).((f2-#Z 2).x)) by A10,FUNCT_1:22,B2
  .=a*arcsin.x+(a*x+b)/(((f2-#Z 2).x) #R (1/2)) by TAYLOR_1:def 4,B4
  .=a*arcsin.x+(a*x+b)/((f2.x-(( #Z 2).x)) #R (1/2)) by VALUED_1:13,B3
  .=a*arcsin.x+(a*x+b)/((f2.x-(x #Z 2)) #R (1/2)) by TAYLOR_1:def 1
  .=a*arcsin.x+(a*x+b)/((f2.x-x^2) #R (1/2)) by FDIFF_7:1
  .=a*arcsin.x+(a*x+b)/((1-x^2) #R (1/2)) by A1,B2
  .=a*arcsin.x+(a*x+b)/sqrt(1-x^2) by FDIFF_7:2,B6;
    hence thesis by A1;
   end;
A32:for x st x in dom ((f1(#)(arcsin))`|Z) holds ((f1(#)(arcsin))`|Z).x=f.x
   proof
   let x;
   assume x in dom((f1(#)(arcsin))`|Z);then
A33:x in Z by A31,FDIFF_1:def 8; then
   ((f1(#)(arcsin))`|Z).x=a*arcsin.x+(a*x+b)/sqrt(1-x^2)
   by A1,A13,A8,FDIFF_7:18
   .=f.x by B1,A33;
   hence thesis;
   end;
  dom ((f1(#)(arcsin))`|Z)=dom f by A1,A31,FDIFF_1:def 8;
  then ((f1(#)(arcsin))`|Z)= f by A32,PARTFUN1:34;
  hence thesis by A1,A30,A31,INTEGRA5:13;
end;
 
::f.x=a*arccos.x-(a*x+b)/sqrt(1-x^2)

theorem
A c= Z & Z c= ]. -1,1 .[ &
(for x st x in Z holds f1.x=a*x+b & f2.x=1) & Z = dom f &
f=a(#)arccos-f1/(( #R (1/2))*(f2-#Z 2)) implies
integral(f,A)=(f1(#)(arccos)).(upper_bound A)-(f1(#)(arccos)).(lower_bound A)
proof
   assume
A1:A c= Z & Z c= ]. -1,1 .[ &
  (for x st x in Z holds f1.x=a*x+b & f2.x=1) & Z = dom f &
  f=a(#)arccos-f1/(( #R (1/2))*(f2-#Z 2));
then
Z = dom (a(#)arccos) /\ dom (f1/(( #R (1/2))*(f2-#Z 2))) by VALUED_1:12;
then A3:Z c= dom (a(#)arccos) & Z c= dom (f1/(( #R (1/2))*(f2-#Z 2)))
   by XBOOLE_1:18;
then A4:Z c= dom arccos by VALUED_1:def 5;
Z c= dom f1 /\ (dom (( #R (1/2))*(f2-#Z 2)) \ (( #R (1/2))*(f2-#Z 2))"{0})
   by A3,RFUNCT_1:def 4;
then
A6:Z c= dom f1 & Z c= dom (( #R (1/2))*(f2-#Z 2)) \ (( #R (1/2))*(f2-#Z 2))"{0}
   by XBOOLE_1:18;
then Z c= dom f1 /\ dom arccos by XBOOLE_1:19,A4;
then A8:Z c= dom (f1(#)arccos) by VALUED_1:def 4;
A9:Z c= dom ((( #R (1/2))*(f2-#Z 2))^) by RFUNCT_1:def 8,A6;
   dom ((( #R (1/2))*(f2-#Z 2))^) c= dom (( #R (1/2))*(f2-#Z 2))
   by RFUNCT_1:11; then
A10:Z c= dom (( #R (1/2))*(f2-#Z 2)) by XBOOLE_1:1,A9;
A11:arccos is_differentiable_on Z by A1,FDIFF_1:34,SIN_COS6:108;
then A12:a(#)arccos is_differentiable_on Z by A3,FDIFF_1:28;
A13:for x st x in Z holds f1.x = a*x+b by A1;
then A14:f1 is_differentiable_on Z by A6,FDIFF_1:31;
    set f3=#Z 2;
for x st x in Z holds (f2-#Z 2).x >0
   proof
   let x;
   assume
A16:x in Z; then
   -1 < x & x < 1 by A1,XXREAL_1:4; then
   0 < 1+x & 0 < 1-x by XREAL_1:150,XREAL_1:52; then
A17:0 < (1+x)*(1-x) by XREAL_1:131;
for x st x in Z holds x in dom (f2-f3) by FUNCT_1:21,A10;
    then (f2-f3).x = f2.x - f3.x by A16,VALUED_1:13
             .=f2.x - (x #Z (1+1)) by TAYLOR_1:def 1
             .=f2.x - ((x #Z 1)*(x #Z 1)) by TAYLOR_1:1
             .=f2.x - (x*(x #Z 1)) by PREPOWER:45
             .=f2.x - x*x by PREPOWER:45
             .=1 - x*x by A1,A16;
     hence thesis by A17;
    end;
then for x st x in Z holds f2.x=1 & (f2-#Z 2).x >0 by A1;
then A25:(( #R (1/2))*(f2-#Z 2)) is_differentiable_on Z by A10,FDIFF_7:22;
x in Z implies (( #R (1/2))*(f2-#Z 2)).x<>0 by RFUNCT_1:13,A9;
then f1/(( #R (1/2))*(f2-#Z 2)) is_differentiable_on Z
    by A14,A25,FDIFF_2:21;
then f is_differentiable_on Z by A1,A12,FDIFF_1:27;
    then f|Z is continuous by FDIFF_1:33;then
f|A is continuous by A1,FCONT_1:17;
then A30:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A31:f1(#)(arccos) is_differentiable_on Z by A8,A11,A14,FDIFF_1:29;
B1:for x st x in Z holds f.x=a*arccos.x-(a*x+b)/sqrt(1-x^2)
   proof
   let x;
   assume
B2:x in Z;
then
B3:x in dom (f2-#Z 2) & (f2-#Z 2).x in dom ( #R (1/2)) by FUNCT_1:21,A10;
then B4:(f2-#Z 2).x in right_open_halfline(0) by TAYLOR_1:def 4;
   -1 < x & x < 1 by A1,XXREAL_1:4,B2; then
   0 < 1+x & 0 < 1-x by XREAL_1:150,XREAL_1:52; then
B6:0 < (1+x)*(1-x) by XREAL_1:131;
   (a(#)arccos-f1/(( #R (1/2))*(f2-#Z 2))).x
   =(a(#)arccos).x-(f1/(( #R (1/2))*(f2-#Z 2))).x by VALUED_1:13,A1,B2
  .=a*arccos.x-(f1/(( #R (1/2))*(f2-#Z 2))).x by VALUED_1:6
  .=a*arccos.x-f1.x/(( #R (1/2))*(f2-#Z 2)).x by RFUNCT_1:def 4,A3,B2
  .=a*arccos.x-(a*x+b)/(( #R (1/2))*(f2-#Z 2)).x by A1,B2
  .=a*arccos.x-(a*x+b)/(( #R (1/2)).((f2-#Z 2).x)) by A10,FUNCT_1:22,B2
  .=a*arccos.x-(a*x+b)/(((f2-#Z 2).x) #R (1/2)) by TAYLOR_1:def 4,B4
  .=a*arccos.x-(a*x+b)/((f2.x-(( #Z 2).x)) #R (1/2)) by VALUED_1:13,B3
  .=a*arccos.x-(a*x+b)/((f2.x-(x #Z 2)) #R (1/2)) by TAYLOR_1:def 1
  .=a*arccos.x-(a*x+b)/((f2.x-x^2) #R (1/2)) by FDIFF_7:1
  .=a*arccos.x-(a*x+b)/((1-x^2) #R (1/2)) by A1,B2
  .=a*arccos.x-(a*x+b)/sqrt(1-x^2) by FDIFF_7:2,B6;
    hence thesis by A1;
   end;
A32:for x st x in dom ((f1(#)(arccos))`|Z) holds ((f1(#)(arccos))`|Z).x=f.x
   proof
   let x;
   assume x in dom((f1(#)(arccos))`|Z);then
A33:x in Z by A31,FDIFF_1:def 8; then
   ((f1(#)(arccos))`|Z).x=a*arccos.x-(a*x+b)/sqrt(1-x^2)
   by A1,A13,A8,FDIFF_7:19
   .=f.x by B1,A33;
   hence thesis;
   end;
  dom ((f1(#)(arccos))`|Z)=dom f by A1,A31,FDIFF_1:def 8;
  then ((f1(#)(arccos))`|Z)= f by A32,PARTFUN1:34;
  hence thesis by A1,A30,A31,INTEGRA5:13;
end;
 
::f.x=arcsin.(x/a)+x/(a*sqrt(1-(x/a)^2))

theorem
 A c= Z & (for x st x in Z holds g.x=1 & f1.x=x/a & f1.x>-1 & f1.x<1)
 & Z = dom f & f|A is continuous
 & f=arcsin*f1+(id Z)/(a(#)(( #R (1/2))*(g-f1^2))) implies
 integral(f,A)=((id Z)(#)((arcsin)*f1)).(upper_bound A)
              -((id Z)(#)((arcsin)*f1)).(lower_bound A)
proof
   assume
A1:A c= Z & (for x st x in Z holds g.x=1 & f1.x=x/a & f1.x>-1 & f1.x<1)
   & Z = dom f & f|A is continuous
   & f=arcsin*f1+(id Z)/(a(#)(( #R (1/2))*(g-f1^2)));
then Z = dom (arcsin*f1) /\ dom ((id Z)/(a(#)(( #R (1/2))*(g-f1^2))))
   by VALUED_1:def 1;
then A3:Z c= dom (arcsin*f1) & Z c= dom ((id Z)/(a(#)(( #R (1/2))*(g-f1^2))))
   by XBOOLE_1:18;
   Z = dom id Z by RELAT_1:71;then
Z c= dom id Z /\ dom (arcsin*f1) by A3,XBOOLE_1:19;
then A5:Z c= dom ((id Z)(#)((arcsin)*f1)) by VALUED_1:def 4;
   Z c= dom (id Z) /\ (dom (a(#)(( #R (1/2))*(g-f1^2)))
                             \ (a(#)(( #R (1/2))*(g-f1^2)))"{0})
   by A3,RFUNCT_1:def 4;then
Z c= dom (a(#)(( #R (1/2))*(g-f1^2))) \ (a(#)(( #R (1/2))*(g-f1^2)))"{0}
   by XBOOLE_1:18;
then A7:Z c= dom ((a(#)(( #R (1/2))*(g-f1^2)))^) by RFUNCT_1:def 8;
   dom ((a(#)(( #R (1/2))*(g-f1^2)))^) c= dom (a(#)(( #R (1/2))*(g-f1^2)))
   by RFUNCT_1:11; then
Z c= dom (a(#)(( #R (1/2))*(g-f1^2))) by XBOOLE_1:1,A7;
then A9:Z c= dom (( #R (1/2))*(g-f1^2)) by VALUED_1:def 5;
A10:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A11:for x st x in Z holds f1.x=x/a & f1.x>-1 & f1.x<1 by A1;then
A12:(id Z)(#)((arcsin)*f1) is_differentiable_on Z by A5,FDIFF_7:25;
A13:for x st x in Z holds f.x=arcsin.(x/a)+x/(a*sqrt(1-(x/a)^2))
   proof
   let x;
   assume
A14:x in Z;
then
A15:x in dom (g-f1^2) & (g-f1^2).x in dom ( #R (1/2)) by FUNCT_1:21,A9;
then A16:(g-f1^2).x in right_open_halfline(0) by TAYLOR_1:def 4;
    -1 < f1.x & f1.x < 1 by A1,A14; then
   0 < 1+f1.x & 0 < 1-f1.x by XREAL_1:150,XREAL_1:52; then
A18:0 < (1+f1.x)*(1-f1.x) by XREAL_1:131;
aa: f1.x=x/a by A1,A14;
   (arcsin*f1+(id Z)/(a(#)(( #R (1/2))*(g-f1^2)))).x
  =(arcsin*f1).x+((id Z)/(a(#)(( #R (1/2))*(g-f1^2)))).x
    by VALUED_1:def 1,A1,A14
 .=arcsin.(f1.x)+((id Z)/(a(#)(( #R (1/2))*(g-f1^2)))).x
    by FUNCT_1:22,A3,A14
 .=arcsin.(x/a)+((id Z)/(a(#)(( #R (1/2))*(g-f1^2)))).x by A1,A14
 .=arcsin.(x/a)+(id Z).x/(a(#)(( #R (1/2))*(g-f1^2))).x
    by RFUNCT_1:def 4,A3,A14
 .=arcsin.(x/a)+x/(a(#)(( #R (1/2))*(g-f1^2))).x by FUNCT_1:35,A14
 .=arcsin.(x/a)+x/(a*(( #R (1/2))*(g-f1^2)).x) by VALUED_1:6
 .=arcsin.(x/a)+x/(a*(( #R (1/2)).((g-f1^2).x))) by A9,FUNCT_1:22,A14
 .=arcsin.(x/a)+x/(a*(((g-f1^2).x) #R (1/2))) by TAYLOR_1:def 4,A16
 .=arcsin.(x/a)+x/(a*((g.x-((f1^2).x)) #R (1/2))) by VALUED_1:13,A15
 .=arcsin.(x/a)+x/(a*((g.x-((f1.x)^2)) #R (1/2))) by VALUED_1:11
 .=arcsin.(x/a)+x/(a*((1-(f1.x)^2) #R (1/2))) by A1,A14
 .=arcsin.(x/a)+x/(a*((1-(x/a)^2) #R (1/2))) by A1,A14
 .=arcsin.(x/a)+x/(a*sqrt(1-(x/a)^2)) by FDIFF_7:2,aa,A18;
    hence thesis by A1;
    end;
A20:for x st x in dom (((id Z)(#)((arcsin)*f1))`|Z) holds
   (((id Z)(#)((arcsin)*f1))`|Z).x=f.x
   proof
   let x;
   assume x in dom (((id Z)(#)((arcsin)*f1))`|Z);then
A21:x in Z by A12,FDIFF_1:def 8; then
   (((id Z)(#)((arcsin)*f1))`|Z).x=arcsin.(x/a)+x/(a*sqrt(1-(x/a)^2))
   by A5,A11,FDIFF_7:25
   .=f.x by A13,A21;
   hence thesis;
   end;
  dom (((id Z)(#)((arcsin)*f1))`|Z)=dom f by A1,A12,FDIFF_1:def 8;
  then (((id Z)(#)((arcsin)*f1))`|Z)= f by A20,PARTFUN1:34;
   hence thesis by A1,A10,A12,INTEGRA5:13;
end;
 
::f.x=arccos.(x/a)-x/(a*sqrt(1-(x/a)^2))

theorem
 A c= Z & (for x st x in Z holds g.x=1 & f1.x=x/a & f1.x>-1 & f1.x<1)
 & Z = dom f & f|A is continuous
 & f=arccos*f1-(id Z)/(a(#)(( #R (1/2))*(g-f1^2))) implies
 integral(f,A)=((id Z)(#)(arccos*f1)).(upper_bound A)
              -((id Z)(#)(arccos*f1)).(lower_bound A)
proof
   assume
A1:A c= Z & (for x st x in Z holds g.x=1 & f1.x=x/a & f1.x>-1 & f1.x<1)
   & Z = dom f & f|A is continuous
   & f=arccos*f1-(id Z)/(a(#)(( #R (1/2))*(g-f1^2)));
then Z = dom (arccos*f1) /\ dom ((id Z)/(a(#)(( #R (1/2))*(g-f1^2))))
   by VALUED_1:12;
then A3:Z c= dom (arccos*f1) & Z c= dom ((id Z)/(a(#)(( #R (1/2))*(g-f1^2))))
   by XBOOLE_1:18;
   Z = dom id Z by RELAT_1:71;then
Z c= dom id Z /\ dom (arccos*f1) by A3,XBOOLE_1:19;
then A5:Z c= dom ((id Z)(#)((arccos)*f1)) by VALUED_1:def 4;
   Z c= dom (id Z) /\ (dom (a(#)(( #R (1/2))*(g-f1^2)))
                             \ (a(#)(( #R (1/2))*(g-f1^2)))"{0})
   by A3,RFUNCT_1:def 4;then
Z c= dom (a(#)(( #R (1/2))*(g-f1^2))) \ (a(#)(( #R (1/2))*(g-f1^2)))"{0}
   by XBOOLE_1:18;
then A7:Z c= dom ((a(#)(( #R (1/2))*(g-f1^2)))^) by RFUNCT_1:def 8;
   dom ((a(#)(( #R (1/2))*(g-f1^2)))^) c= dom (a(#)(( #R (1/2))*(g-f1^2)))
   by RFUNCT_1:11; then
Z c= dom (a(#)(( #R (1/2))*(g-f1^2))) by XBOOLE_1:1,A7;
then A9:Z c= dom (( #R (1/2))*(g-f1^2)) by VALUED_1:def 5;
A10:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A11:for x st x in Z holds f1.x=x/a & f1.x>-1 & f1.x<1 by A1;then
A12:(id Z)(#)((arccos)*f1) is_differentiable_on Z by A5,FDIFF_7:26;
A13:for x st x in Z holds f.x=arccos.(x/a)-x/(a*sqrt(1-(x/a)^2))
   proof
   let x;
   assume
A14:x in Z;
then
A15:x in dom (g-f1^2) & (g-f1^2).x in dom ( #R (1/2)) by FUNCT_1:21,A9;
then A16:(g-f1^2).x in right_open_halfline(0) by TAYLOR_1:def 4;
    -1 < f1.x & f1.x < 1 by A1,A14; then
   0 < 1+f1.x & 0 < 1-f1.x by XREAL_1:150,XREAL_1:52; then
A18:0 < (1+f1.x)*(1-f1.x) by XREAL_1:131;
aa:f1.x=x/a by A1,A14;
   (arccos*f1-(id Z)/(a(#)(( #R (1/2))*(g-f1^2)))).x
  =(arccos*f1).x-((id Z)/(a(#)(( #R (1/2))*(g-f1^2)))).x
    by VALUED_1:13,A1,A14
 .=arccos.(f1.x)-((id Z)/(a(#)(( #R (1/2))*(g-f1^2)))).x
    by FUNCT_1:22,A3,A14
 .=arccos.(x/a)-((id Z)/(a(#)(( #R (1/2))*(g-f1^2)))).x by A1,A14
 .=arccos.(x/a)-(id Z).x/(a(#)(( #R (1/2))*(g-f1^2))).x
    by RFUNCT_1:def 4,A3,A14
 .=arccos.(x/a)-x/(a(#)(( #R (1/2))*(g-f1^2))).x by FUNCT_1:35,A14
 .=arccos.(x/a)-x/(a*(( #R (1/2))*(g-f1^2)).x) by VALUED_1:6
 .=arccos.(x/a)-x/(a*(( #R (1/2)).((g-f1^2).x))) by A9,FUNCT_1:22,A14
 .=arccos.(x/a)-x/(a*(((g-f1^2).x) #R (1/2))) by TAYLOR_1:def 4,A16
 .=arccos.(x/a)-x/(a*((g.x-((f1^2).x)) #R (1/2))) by VALUED_1:13,A15
 .=arccos.(x/a)-x/(a*((g.x-((f1.x)^2)) #R (1/2))) by VALUED_1:11
 .=arccos.(x/a)-x/(a*((1-(f1.x)^2) #R (1/2))) by A1,A14
 .=arccos.(x/a)-x/(a*((1-(x/a)^2) #R (1/2))) by A1,A14
 .=arccos.(x/a)-x/(a*sqrt(1-(x/a)^2)) by FDIFF_7:2,aa,A18;
    hence thesis by A1;
    end;
A20:for x st x in dom (((id Z)(#)((arccos)*f1))`|Z) holds
   (((id Z)(#)((arccos)*f1))`|Z).x=f.x
   proof
   let x;
   assume x in dom (((id Z)(#)((arccos)*f1))`|Z);then
A21:x in Z by A12,FDIFF_1:def 8; then
   (((id Z)(#)((arccos)*f1))`|Z).x=arccos.(x/a)-x/(a*sqrt(1-(x/a)^2))
   by A5,A11,FDIFF_7:26
   .=f.x by A13,A21;
   hence thesis;
   end;
   dom (((id Z)(#)((arccos)*f1))`|Z)=dom f by A1,A12,FDIFF_1:def 8;
   then (((id Z)(#)((arccos)*f1))`|Z)= f by A20,PARTFUN1:34;
   hence thesis by A1,A10,A12,INTEGRA5:13;
end;
 
::f.x = n*(sin.x) #Z (n-1)/(cos.x) #Z (n+1)

theorem
 A c= Z & f=(n(#)(( #Z (n-1))*sin))/(( #Z (n+1))*cos) &
 1<=n & Z c= dom (( #Z n)*tan) & Z = dom f
 implies
 integral(f,A)=(( #Z n)*tan).(upper_bound A)-(( #Z n)*tan).(lower_bound A)
proof
  assume
A1:A c= Z & f=n(#)(( #Z (n-1))*sin)/(( #Z (n+1))*cos)
 & 1<=n & Z c= dom (( #Z n)*tan) & Z = dom f;
then Z = dom (n(#)(( #Z (n-1))*sin)) /\
                (dom (( #Z (n+1))*cos) \ (( #Z (n+1))*cos)"{0})
   by RFUNCT_1:def 4;
then A3:Z c= dom (n(#)(( #Z (n-1))*sin)) &
   Z c= dom (( #Z (n+1))*cos) \ (( #Z (n+1))*cos)"{0} by XBOOLE_1:18;
then A4:Z c= dom ((( #Z (n+1))*cos)^) by RFUNCT_1:def 8;
    dom ((( #Z (n+1))*cos)^) c= dom (( #Z (n+1))*cos) by RFUNCT_1:11;then
A5:Z c= dom (( #Z (n+1))*cos) by A4,XBOOLE_1:1;
A6: x in Z implies (( #Z (n+1))*cos).x <> 0
   proof
   assume x in Z;
   then x in dom (n(#)(( #Z (n-1))*sin)) /\
                (dom (( #Z (n+1))*cos) \ (( #Z (n+1))*cos)"{0})
        by A1,RFUNCT_1:def 4;
   then x in dom (( #Z (n+1))*cos) \ (( #Z (n+1))*cos)"{0}
        by XBOOLE_0:def 4; then
   x in dom ((( #Z (n+1))*cos)^) by RFUNCT_1:def 8;
     hence thesis by RFUNCT_1:13;
   end;
A9:Z c= dom (( #Z (n-1))*sin) by VALUED_1:def 5, A3;
A10:#Z (n-1)*sin is_differentiable_in x
   proof
   consider m being Nat such that
A11: n = m + 1 by A1,NAT_1:6;
    sin is_differentiable_in x by SIN_COS:69;
   hence thesis by A11,TAYLOR_1:3;
   end;
 #Z (n-1)*sin is_differentiable_on Z
   proof
   for x st x in Z holds #Z (n-1)*sin is_differentiable_in x by A10;
   hence thesis by A9,FDIFF_1:16;
   end;
then A15:(n(#)(( #Z (n-1))*sin)) is_differentiable_on Z by A3,FDIFF_1:28;
A16: #Z (n+1)*cos is_differentiable_in x
    proof
    cos is_differentiable_in x by SIN_COS:68;
    hence thesis by TAYLOR_1:3;
    end;
(( #Z (n+1))*cos) is_differentiable_on Z
   proof
   for x st x in Z holds ( #Z (n+1))*cos is_differentiable_in x by A16;
   hence thesis by A5,FDIFF_1:16;
   end;
then f|Z is continuous by FDIFF_1:33,A1,A6,A15,FDIFF_2:21;
then f|A is continuous by A1,FCONT_1:17;
then A22:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A23:(( #Z n)*tan) is_differentiable_on Z by A1,FDIFF_8:20;
B1:for x st x in Z holds f.x = n*(sin.x) #Z (n-1)/(cos.x) #Z (n+1)
   proof
   let x;
   assume
B2:x in Z;
    then ((n(#)(( #Z (n-1))*sin))/(( #Z (n+1))*cos)).x
    =(n(#)(( #Z (n-1))*sin)).x/(( #Z (n+1))*cos).x by RFUNCT_1:def 4,A1
   .=n*(( #Z (n-1))*sin).x/(( #Z (n+1))*cos).x by VALUED_1:6
   .=n*(( #Z (n-1)).(sin.x))/(( #Z (n+1))*cos).x by FUNCT_1:22,A9,B2
   .=n*(sin.x) #Z (n-1)/(( #Z (n+1))*cos).x by TAYLOR_1:def 1
   .=n*(sin.x) #Z (n-1)/(( #Z (n+1)).(cos.x)) by FUNCT_1:22,A5,B2
   .=n*(sin.x) #Z (n-1)/(cos.x) #Z (n+1) by TAYLOR_1:def 1;
   hence thesis by A1;
   end;
A24:for x st x in dom ((( #Z n)*tan)`|Z) holds ((( #Z n)*tan)`|Z).x = f.x
   proof
      let x;
      assume x in dom ((( #Z n)*tan)`|Z);then
A25: x in Z by A23,FDIFF_1:def 8; then
    ((( #Z n)*tan)`|Z).x = n*(sin.x) #Z (n-1)/(cos.x) #Z (n+1) by A1,FDIFF_8:20
    .=f.x by B1,A25;
    hence thesis;
   end;
  dom ((( #Z n)*tan)`|Z) = dom f by A1,A23,FDIFF_1:def 8;
  then ((( #Z n)*tan)`|Z)=f by A24,PARTFUN1:34;
  hence thesis by A1,A22,FDIFF_8:20,INTEGRA5:13;
end;
 
::f.x=n*(cos.x) #Z (n-1)/(sin.x) #Z (n+1)

theorem
 A c= Z & f=(n(#)(( #Z (n-1))*cos))/(( #Z (n+1))*sin)
 & 1<=n & Z c= dom (( #Z n)*cot) & Z = dom f
 implies
 integral(f,A)=(-( #Z n)*cot).(upper_bound A)-(-( #Z n)*cot).(lower_bound A)
proof
  assume
A1:A c= Z & f=(n(#)(( #Z (n-1))*cos))/(( #Z (n+1))*sin)
 & 1<=n & Z c= dom (( #Z n)*cot) & Z = dom f;
then Z = dom (n(#)(( #Z (n-1))*cos)) /\
                (dom (( #Z (n+1))*sin) \ (( #Z (n+1))*sin)"{0})
   by RFUNCT_1:def 4;
then A3:Z c= dom (n(#)(( #Z (n-1))*cos)) &
   Z c= dom (( #Z (n+1))*sin) \ (( #Z (n+1))*sin)"{0} by XBOOLE_1:18;
then A4:Z c= dom ((( #Z (n+1))*sin)^) by RFUNCT_1:def 8;
    dom ((( #Z (n+1))*sin)^) c= dom (( #Z (n+1))*sin) by RFUNCT_1:11;then
A5:Z c= dom (( #Z (n+1))*sin) by A4,XBOOLE_1:1;
A6: x in Z implies (( #Z (n+1))*sin).x <> 0
    proof
    assume x in Z;
   then x in dom (n(#)(( #Z (n-1))*cos)) /\
                (dom (( #Z (n+1))*sin) \ (( #Z (n+1))*sin)"{0})
        by A1,RFUNCT_1:def 4;
   then x in dom (( #Z (n+1))*sin) \ (( #Z (n+1))*sin)"{0}
        by XBOOLE_0:def 4; then
   x in dom ((( #Z (n+1))*sin)^) by RFUNCT_1:def 8;
     hence thesis by RFUNCT_1:13;
   end;
A9:Z c= dom (( #Z (n-1))*cos) by A3,VALUED_1:def 5;
A10:#Z (n-1)*cos is_differentiable_in x
   proof
   consider m being Nat such that
A11: n = m + 1 by A1,NAT_1:6;
    cos is_differentiable_in x by SIN_COS:68;
   hence thesis by A11,TAYLOR_1:3;
   end;
 #Z (n-1)*cos is_differentiable_on Z
   proof
   for x st x in Z holds #Z (n-1)*cos is_differentiable_in x by A10;
   hence thesis by A9,FDIFF_1:16;
   end;
then A15:(n(#)(( #Z (n-1))*cos)) is_differentiable_on Z by A3,FDIFF_1:28;
A16: #Z (n+1)*sin is_differentiable_in x
    proof
    sin is_differentiable_in x by SIN_COS:69;
    hence thesis by TAYLOR_1:3;
    end;
(( #Z (n+1))*sin) is_differentiable_on Z
   proof
   for x st x in Z holds ( #Z (n+1))*sin is_differentiable_in x by A16;
   hence thesis by A5,FDIFF_1:16;
   end;
then f|Z is continuous by FDIFF_1:33,A1,A6,A15,FDIFF_2:21;
then f|A is continuous by A1,FCONT_1:17;
then A22:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A23:( #Z n)*cot is_differentiable_on Z by A1,FDIFF_8:21;
A24:dom (( #Z n)*cot) c= dom cot by RELAT_1:44;
A25:Z c= dom (-( #Z n)*cot) by A1,VALUED_1:8;
then A26:(-1)(#)(( #Z n)*cot) is_differentiable_on Z by A23,FDIFF_1:28,X;
A27:for x st x in Z holds sin.x<>0
  proof
    let x;
    assume x in Z;
    then x in dom (cos/sin) by A1,FUNCT_1:21;
    hence thesis by FDIFF_8:2;
  end;
A28:for x st x in Z holds ((-( #Z n)*cot)`|Z).x
                              =n*(cos.x) #Z (n-1)/(sin.x) #Z (n+1)
    proof
      let x;
      assume
A29:x in Z;then
A30: sin.x<>0 by A27; then
A31: cot is_differentiable_in x by FDIFF_7:47;
    consider m being Nat such that
A32: n = m + 1 by A1,NAT_1:6;
    set m = n-1;
A33:( #Z n)*cot is_differentiable_in x by A23,A29,FDIFF_1:16;
   ((-( #Z n)*cot)`|Z).x=diff(-( #Z n)*cot,x) by A26,A29,FDIFF_1:def 8
                     .=(-1)*diff(( #Z n)*cot,x) by A33,FDIFF_1:23,X
                     .=(-1)*(n*(cot.x) #Z (n-1) * diff(cot,x))
by A31,TAYLOR_1:3
                     .=(-1)*(n*(cot.x) #Z (n-1) *(-1/(sin.x)^2))
by A30,FDIFF_7:47
                     .=(-1)*(-(n*(cot.x) #Z (n-1))/(sin.x)^2)
                     .=(-1)*(-n*((cos.x) #Z m/(sin.x) #Z m)/(sin.x)^2)
                                    by A1,A24,A29,A32,FDIFF_8:3,XBOOLE_1:1
                     .=(-1)*(-n*(cos.x) #Z (n-1)/(sin.x) #Z (n-1)/(sin.x)^2)
                     .=(-1)*(-n*(cos.x) #Z (n-1)/((sin.x) #Z (n-1)*(sin.x)^2))
by XCMPLX_1:79
                .=(-1)*(-n*((cos.x) #Z (n-1))/((sin.x) #Z (n-1)*(sin.x) #Z 2))
by FDIFF_7:1
                     .=(-1)*(-n*((cos.x) #Z (n-1))/((sin.x) #Z (n-1+2)))
by A27,A29,PREPOWER:54
                     .=n*((cos.x) #Z (n-1))/(sin.x) #Z (n+1);
    hence thesis;
    end;
B1:for x st x in Z holds f.x = n*(cos.x) #Z (n-1)/(sin.x) #Z (n+1)
   proof
   let x;
   assume
B2:x in Z;
     then ((n(#)(( #Z (n-1))*cos))/(( #Z (n+1))*sin)).x
    =(n(#)(( #Z (n-1))*cos)).x/(( #Z (n+1))*sin).x by RFUNCT_1:def 4,A1
   .=n*(( #Z (n-1))*cos).x/(( #Z (n+1))*sin).x by VALUED_1:6
   .=n*(( #Z (n-1)).(cos.x))/(( #Z (n+1))*sin).x by FUNCT_1:22,A9,B2
   .=n*(cos.x) #Z (n-1)/(( #Z (n+1))*sin).x by TAYLOR_1:def 1
   .=n*(cos.x) #Z (n-1)/(( #Z (n+1)).(sin.x)) by FUNCT_1:22,A5,B2
   .=n*(cos.x) #Z (n-1)/(sin.x) #Z (n+1) by TAYLOR_1:def 1;
   hence thesis by A1;
   end;
A34:for x st x in dom ((-( #Z n)*cot)`|Z) holds ((-( #Z n)*cot)`|Z).x = f.x
   proof
      let x;
      assume x in dom ((-( #Z n)*cot)`|Z);then
A35:x in Z by A26,FDIFF_1:def 8; then
    ((-( #Z n)*cot)`|Z).x = n*(cos.x) #Z (n-1)/(sin.x) #Z (n+1) by A28
    .=f.x by B1,A35;
    hence thesis;
   end;
   dom ((-( #Z n)*cot)`|Z) = dom f by A1,A26,FDIFF_1:def 8;
   then ((-( #Z n)*cot)`|Z)=f by A34,PARTFUN1:34;
   hence thesis by A1,A22,A23,A25,FDIFF_1:28,X,INTEGRA5:13;
end;
 
::f.x=(sin.(a*x))^2/(cos.(a*x))^2

theorem
 A c= Z & Z c= dom (tan*f1) & f=(sin*f1)^2/(cos*f1)^2
 & (for x st x in Z holds f1.x=a*x & a<>0) & Z = dom f
 implies
 integral(f,A)=((1/a)(#)(tan*f1)-id Z).(upper_bound A)
              -((1/a)(#)(tan*f1)-id Z).(lower_bound A)
proof
  assume
A1: A c= Z & Z c= dom (tan*f1) & f=(sin*f1)^2/(cos*f1)^2
 & (for x st x in Z holds f1.x=a*x & a<>0) & Z = dom f;
then A2:Z c= dom ((1/a)(#)(tan*f1)) by VALUED_1:def 5;
   dom id Z = Z by RELAT_1:71;then
Z c= dom ((1/a)(#)(tan*f1)) /\ dom id Z by XBOOLE_1:19,A2;
then A4:Z c= dom ((1/a)(#)(tan*f1)-id Z) by VALUED_1:12;
A5:for x st x in Z holds f1.x=a*x+0 by A1;
Z = dom ((sin*f1)^2) /\ (dom ((cos*f1)^2) \ ((cos*f1)^2)"{0})
   by A1,RFUNCT_1:def 4;
then A7:Z c= dom ((sin*f1)^2) & Z c= dom ((cos*f1)^2) \ ((cos*f1)^2)"{0}
   by XBOOLE_1:18;
then A8:Z c= dom (sin*f1) by VALUED_1:11;
A9:Z c= dom (((cos*f1)^2)^) by RFUNCT_1:def 8,A7;
   dom (((cos*f1)^2)^) c= dom ((cos*f1)^2) by RFUNCT_1:11;then
Z c= dom ((cos*f1)^2) by XBOOLE_1:1,A9;
then A11:Z c= dom (cos*f1) by VALUED_1:11;
A12:sin*f1 is_differentiable_on Z by FDIFF_4:37,A8,A5;
A13:cos*f1 is_differentiable_on Z by FDIFF_4:38,A5,A11;
A15:(sin*f1)^2 is_differentiable_on Z by FDIFF_2:20,A12;
A17:(cos*f1)^2 is_differentiable_on Z by FDIFF_2:20,A13;
x in Z implies ((cos*f1)^2).x<>0
    proof
    assume x in Z;
    then x in dom ((sin*f1)^2) /\ (dom ((cos*f1)^2) \ ((cos*f1)^2)"{0})
      by A1,RFUNCT_1:def 4;
    then x in dom ((cos*f1)^2) \ ((cos*f1)^2)"{0} by XBOOLE_0:def 4;
    then x in dom (((cos*f1)^2)^) by RFUNCT_1:def 8;
      hence thesis by RFUNCT_1:13;
    end;
then f is_differentiable_on Z by A1,A15,A17,FDIFF_2:21;
    then f|Z is continuous by FDIFF_1:33;then
f|A is continuous by A1,FCONT_1:17;
then A21:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A22:((1/a)(#)(tan*f1)-id Z) is_differentiable_on Z by A1,A4,FDIFF_8:26;
B1:for x st x in Z holds f.x = (sin.(a*x))^2/(cos.(a*x))^2
   proof
   let x;
   assume
B2:x in Z;
     then ((sin*f1)^2/(cos*f1)^2).x
    =((sin*f1)^2).x/((cos*f1)^2).x by RFUNCT_1:def 4,A1
   .=((sin*f1).x)^2/((cos*f1)^2).x by VALUED_1:11
   .=((sin*f1).x)^2/((cos*f1).x)^2 by VALUED_1:11
   .=(sin.(f1.x))^2/((cos*f1).x)^2 by FUNCT_1:22,A8,B2
   .=(sin.(f1.x))^2/(cos.(f1.x))^2 by FUNCT_1:22,A11,B2
   .=(sin.(a*x))^2/(cos.(f1.x))^2 by B2,A1
   .=(sin.(a*x))^2/(cos.(a*x))^2 by B2,A1;
    hence thesis by A1;
    end;
A23:for x st x in dom(((1/a)(#)(tan*f1)-id Z)`|Z) holds
   (((1/a)(#)(tan*f1)-id Z)`|Z).x=f.x
   proof
      let x;
      assume x in dom(((1/a)(#)(tan*f1)-id Z)`|Z);then
A24: x in Z by A22,FDIFF_1:def 8; then
    (((1/a)(#)(tan*f1)-id Z)`|Z).x
   =(sin.(a*x))^2/(cos.(a*x))^2 by A1,A4,FDIFF_8:26
  .=f.x by B1,A24;
   hence thesis;
   end;
   dom (((1/a)(#)(tan*f1)-id Z)`|Z)=dom f by A1,A22,FDIFF_1:def 8;
   then (((1/a)(#)(tan*f1)-id Z)`|Z)=f by A23,PARTFUN1:34;
   hence thesis by A1,A21,A22,INTEGRA5:13;
end;
 
::f.x=(cos.(a*x))^2/(sin.(a*x))^2

theorem
 A c= Z & Z c= dom (cot*f1) & f=(cos*f1)^2/(sin*f1)^2 &
 (for x st x in Z holds f1.x=a*x & a<>0) & Z = dom f
 implies
 integral(f,A)=((-1/a)(#)(cot*f1)-id Z).(upper_bound A)
              -((-1/a)(#)(cot*f1)-id Z).(lower_bound A)
proof
  assume
A1:A c= Z & Z c= dom (cot*f1) & f=(cos*f1)^2/(sin*f1)^2 &
 (for x st x in Z holds f1.x=a*x & a<>0) & Z = dom f;
then A2:Z c= dom ((-1/a)(#)(cot*f1)) by VALUED_1:def 5;
   dom id Z = Z by RELAT_1:71;then
Z c= dom ((-1/a)(#)(cot*f1)) /\ dom id Z by XBOOLE_1:19,A2;
then A4:Z c= dom ((-1/a)(#)(cot*f1)-id Z) by VALUED_1:12;
A5:for x st x in Z holds f1.x=a*x+0 by A1;
Z = dom ((cos*f1)^2) /\ (dom ((sin*f1)^2) \ ((sin*f1)^2)"{0})
   by A1,RFUNCT_1:def 4;
then A7:Z c= dom ((cos*f1)^2) & Z c= dom ((sin*f1)^2) \ ((sin*f1)^2)"{0}
   by XBOOLE_1:18;
then A8:Z c= dom (cos*f1) by VALUED_1:11;
A9:Z c= dom (((sin*f1)^2)^) by RFUNCT_1:def 8,A7;
   dom (((sin*f1)^2)^) c= dom ((sin*f1)^2) by RFUNCT_1:11;then
Z c= dom ((sin*f1)^2) by XBOOLE_1:1,A9;
then A11:Z c= dom (sin*f1) by VALUED_1:11;
then A12:sin*f1 is_differentiable_on Z by FDIFF_4:37,A5;
A13:cos*f1 is_differentiable_on Z by FDIFF_4:38,A5,A8;
A15:(sin*f1)^2 is_differentiable_on Z by FDIFF_2:20,A12;
A17:(cos*f1)^2 is_differentiable_on Z by FDIFF_2:20,A13;
x in Z implies ((sin*f1)^2).x<>0
    proof
    assume x in Z;
    then x in dom ((cos*f1)^2) /\ (dom ((sin*f1)^2) \ ((sin*f1)^2)"{0})
      by A1,RFUNCT_1:def 4;
    then x in dom ((sin*f1)^2) \ ((sin*f1)^2)"{0} by XBOOLE_0:def 4;
    then x in dom (((sin*f1)^2)^) by RFUNCT_1:def 8;
      hence thesis by RFUNCT_1:13;
    end;
then f is_differentiable_on Z by A1,A15,A17,FDIFF_2:21;
    then f|Z is continuous by FDIFF_1:33;then
f|A is continuous by A1,FCONT_1:17;
then A21:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A22:((-1/a)(#)(cot*f1)-id Z) is_differentiable_on Z by A1,A4,FDIFF_8:27;
B1:for x st x in Z holds f.x =(cos.(a*x))^2/(sin.(a*x))^2
    proof
    let x;
     assume
B2:x in Z;
   then ((cos*f1)^2/(sin*f1)^2).x
    =((cos*f1)^2).x/((sin*f1)^2).x by RFUNCT_1:def 4,A1
   .=((cos*f1).x)^2/((sin*f1)^2).x by VALUED_1:11
   .=((cos*f1).x)^2/((sin*f1).x)^2 by VALUED_1:11
   .=(cos.(f1.x))^2/((sin*f1).x)^2 by FUNCT_1:22,A8,B2
   .=(cos.(f1.x))^2/(sin.(f1.x))^2 by FUNCT_1:22,A11,B2
   .=(cos.(a*x))^2/(sin.(f1.x))^2 by B2,A1
   .=(cos.(a*x))^2/(sin.(a*x))^2 by B2,A1;
     hence thesis by A1;
     end;
A23:for x st x in dom(((-1/a)(#)(cot*f1)-id Z)`|Z) holds
   (((-1/a)(#)(cot*f1)-id Z)`|Z).x=f.x
   proof
      let x;
      assume x in dom(((-1/a)(#)(cot*f1)-id Z)`|Z);then
A24: x in Z by A22,FDIFF_1:def 8; then
    (((-1/a)(#)(cot*f1)-id Z)`|Z).x
   =(cos.(a*x))^2/(sin.(a*x))^2 by A1,A4,FDIFF_8:27
  .=f.x by B1,A24;
   hence thesis;
   end;
   dom (((-1/a)(#)(cot*f1)-id Z)`|Z)=dom f by A1,A22,FDIFF_1:def 8;
   then (((-1/a)(#)(cot*f1)-id Z)`|Z)=f by A23,PARTFUN1:34;
   hence thesis by A1,A21,A22,INTEGRA5:13;
end;
 
::f.x=a*sin.x/cos.x+(a*x+b)/(cos.x)^2

theorem
 A c= Z & (for x st x in Z holds f1.x=a*x+b) & Z = dom f &
 f=a(#)(sin/cos)+f1/cos^2 implies
 integral(f,A)=(f1(#)tan).(upper_bound A)-(f1(#)tan).(lower_bound A)
proof
  assume
A1:A c= Z & (for x st x in Z holds f1.x=a*x+b) & Z = dom f &
   f=a(#)(sin/cos)+f1/cos^2;
then A3:Z = dom (a(#)(sin/cos)) /\ dom (f1/cos^2) by VALUED_1:def 1;
then A4:Z c= dom (a(#)(sin/cos)) by XBOOLE_1:18;
then A5:Z c= dom (sin/cos) by VALUED_1:def 5;
A6:Z c= dom (f1/cos^2) by XBOOLE_1:18,A3;
dom (f1/cos^2) = dom f1 /\ (dom cos^2 \ (cos^2)"{0}) by RFUNCT_1:def 4;
then A8:Z c= dom f1 by A6,XBOOLE_1:18;
then Z c= dom f1 /\ dom tan by XBOOLE_1:19,A5;
then A11:Z c= dom (f1(#)tan) by VALUED_1:def 4;
for x st x in Z holds sin/cos is_differentiable_in x
    proof
    let x;
    assume x in Z;then
    cos.x<>0 by A5,FDIFF_8:1;
    hence thesis by FDIFF_7:46;
    end;
then sin/cos is_differentiable_on Z by A5,FDIFF_1:16;
then A14:a(#)(sin/cos) is_differentiable_on Z by A4,FDIFF_1:28;
A15:f1 is_differentiable_on Z by A1,A8,FDIFF_1:31;
cos is_differentiable_on Z by FDIFF_1:34,SIN_COS:72;
then A17:cos^2 is_differentiable_on Z by FDIFF_2:20;
x in Z implies (cos^2).x<>0
    proof
    assume x in Z;then
    x in dom (f1/cos^2) by A6;
    then x in dom f1 /\ (dom cos^2 \ (cos^2)"{0}) by RFUNCT_1:def 4;
    then x in dom cos^2 \ (cos^2)"{0} by XBOOLE_0:def 4;
    then x in dom ((cos^2)^) by RFUNCT_1:def 8;
      hence thesis by RFUNCT_1:13;
    end;
    then f1/cos^2 is_differentiable_on Z by A15,A17,FDIFF_2:21; then
    f|Z is continuous by FDIFF_1:33,A1,A14,FDIFF_1:26;then
f|A is continuous by A1,FCONT_1:17;
then A22:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A23:(f1(#)tan) is_differentiable_on Z by A1,A11,FDIFF_8:28;
B1:for x st x in Z holds f.x=a*sin.x/cos.x+(a*x+b)/(cos.x)^2
   proof
   let x;
   assume
B2:x in Z;
   then (a(#)(sin/cos)+f1/cos^2).x
  =(a(#)(sin/cos)).x+(f1/cos^2).x by VALUED_1:def 1,A1
 .=a*(sin/cos).x+(f1/cos^2).x by VALUED_1:6
 .=a*(sin.x/cos.x)+(f1/cos^2).x by RFUNCT_1:def 4,B2,A5
 .=a*sin.x/cos.x+f1.x/(cos^2).x by RFUNCT_1:def 4,B2,A6
 .=a*sin.x/cos.x+f1.x/(cos.x)^2 by VALUED_1:11
 .=a*sin.x/cos.x+(a*x+b)/(cos.x)^2 by A1,B2;
    hence thesis by A1;
    end;
A24:for x st x in dom((f1(#)tan)`|Z) holds ((f1(#)tan)`|Z).x=f.x
   proof
      let x;
      assume x in dom((f1(#)tan)`|Z);then
A25: x in Z by A23,FDIFF_1:def 8; then
 ((f1(#)tan)`|Z).x=a*sin.x/cos.x+(a*x+b)/(cos.x)^2 by A1,A11,FDIFF_8:28
  .=f.x by B1,A25;
   hence thesis;
   end;
  dom((f1(#)tan)`|Z)=dom f by A1,A23,FDIFF_1:def 8;
   then ((f1(#)tan)`|Z)=f by A24,PARTFUN1:34;
   hence thesis by A1,A22,A11,FDIFF_8:28,INTEGRA5:13;
end;
 
::f.x=a*cos.x/sin.x-(a*x+b)/(sin.x)^2

theorem
 A c= Z & (for x st x in Z holds f1.x=a*x+b) & Z = dom f &
 f=a(#)(cos/sin)-f1/sin^2 implies
 integral(f,A)=(f1(#)cot).(upper_bound A)-(f1(#)cot).(lower_bound A)
proof
  assume
A1:A c= Z & (for x st x in Z holds f1.x=a*x+b) & Z = dom f &
   f=a(#)(cos/sin)-f1/sin^2;
then A3:Z = dom (a(#)(cos/sin)) /\ dom (-f1/sin^2) by VALUED_1:def 1;
then A4:Z c= dom (a(#)(cos/sin)) by XBOOLE_1:18;
then A5:Z c= dom (cos/sin) by VALUED_1:def 5;
Z c= dom (-f1/sin^2) by XBOOLE_1:18,A3;
then A7:Z c= dom (f1/sin^2) by VALUED_1:8;
   dom (f1/sin^2) = dom f1 /\ (dom sin^2 \ (sin^2)"{0}) by RFUNCT_1:def 4;then
A9:Z c= dom f1 by A7,XBOOLE_1:18;
then Z c= dom f1 /\ dom cot by XBOOLE_1:19,A5;
then A12:Z c= dom (f1(#)cot) by VALUED_1:def 4;
for x st x in Z holds cos/sin is_differentiable_in x
    proof
    let x;
    assume x in Z;then
    sin.x<>0 by A5,FDIFF_8:2;
    hence thesis by FDIFF_7:47;
    end;
then cos/sin is_differentiable_on Z by A5,FDIFF_1:16;
then A15:a(#)(cos/sin) is_differentiable_on Z by A4,FDIFF_1:28;
A16:f1 is_differentiable_on Z by A1,A9,FDIFF_1:31;
sin is_differentiable_on Z by FDIFF_1:34,SIN_COS:73;
then A18:sin^2 is_differentiable_on Z by FDIFF_2:20;
x in Z implies (sin^2).x<>0
    proof
    assume x in Z;then
    x in dom (f1/sin^2) by A7;
    then x in dom f1 /\ (dom sin^2 \ (sin^2)"{0}) by RFUNCT_1:def 4;
    then x in dom sin^2 \ (sin^2)"{0} by XBOOLE_0:def 4;
    then x in dom ((sin^2)^) by RFUNCT_1:def 8;
      hence thesis by RFUNCT_1:13;
    end;
    then f1/sin^2 is_differentiable_on Z by A16,A18,FDIFF_2:21; then
    f|Z is continuous by FDIFF_1:33,A1,A15,FDIFF_1:27;then
f|A is continuous by A1,FCONT_1:17;
then A23:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A24:(f1(#)cot) is_differentiable_on Z by A1,A12,FDIFF_8:29;
B1:for x st x in Z holds f.x=a*cos.x/sin.x-(a*x+b)/(sin.x)^2
    proof
    let x;
    assume
B2:x in Z;
   then (a(#)(cos/sin)-f1/sin^2).x
  =(a(#)(cos/sin)).x-(f1/sin^2).x by VALUED_1:13,A1
 .=a*(cos/sin).x-(f1/sin^2).x by VALUED_1:6
 .=a*(cos.x/sin.x)-(f1/sin^2).x by RFUNCT_1:def 4,B2,A5
 .=a*cos.x/sin.x-f1.x/(sin^2).x by RFUNCT_1:def 4,B2,A7
 .=a*cos.x/sin.x-f1.x/(sin.x)^2 by VALUED_1:11
 .=a*cos.x/sin.x-(a*x+b)/(sin.x)^2 by A1,B2;
    hence thesis by A1;
    end;
A25:for x st x in dom((f1(#)cot)`|Z) holds ((f1(#)cot)`|Z).x=f.x
   proof
      let x;
      assume x in dom((f1(#)cot)`|Z);then
A26: x in Z by A24,FDIFF_1:def 8; then
   ((f1(#)cot)`|Z).x=a*cos.x/sin.x-(a*x+b)/(sin.x)^2 by A1,A12,FDIFF_8:29
  .=f.x by B1,A26;
   hence thesis;
   end;
  dom((f1(#)cot)`|Z)=dom f by A1,A24,FDIFF_1:def 8;
  then ((f1(#)cot)`|Z) = f by A25,PARTFUN1:34;
  hence thesis by A1,A23,A12,FDIFF_8:29,INTEGRA5:13;
end;
 
::f.x=(sin.x)^2/(cos.x)^2

theorem
 A c= Z & (for x st x in Z holds f.x=(sin.x)^2/(cos.x)^2)
 & Z c= dom (tan-id Z) & Z = dom f & f|A is continuous implies
 integral(f,A)=(tan-id Z).(upper_bound A)-(tan-id Z).(lower_bound A)
proof
  assume
A1:A c= Z & (for x st x in Z holds f.x=(sin.x)^2/(cos.x)^2)
   & Z c= dom (tan-id Z) & Z = dom f & f|A is continuous;then
A2:f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
A3:tan-id Z is_differentiable_on Z by A1,FDIFF_8:24;
A4:for x st x in dom ((tan-id Z)`|Z) holds ((tan-id Z)`|Z).x=f.x
   proof
   let x;
   assume x in dom ((tan-id Z)`|Z);then
A5:x in Z by A3,FDIFF_1:def 8;then
   ((tan-id Z)`|Z).x=(sin.x)^2/(cos.x)^2 by A1,FDIFF_8:24
   .=f.x by A1,A5;
   hence thesis;
   end;
   dom ((tan-id Z)`|Z)=dom f by A1,A3,FDIFF_1:def 8;
   then ((tan-id Z)`|Z)= f by A4,PARTFUN1:34;
    hence thesis by A1,A2,FDIFF_8:24,INTEGRA5:13;
end;
 
::f.x=(cos.x)^2/(sin.x)^2

theorem
 A c= Z & (for x st x in Z holds f.x=(cos.x)^2/(sin.x)^2)
 & Z c= dom (-cot-id Z) & Z = dom f & f|A is continuous implies
 integral(f,A)=(-cot-id Z).(upper_bound A)-(-cot-id Z).(lower_bound A)
proof
  assume
A1:A c= Z & (for x st x in Z holds f.x=(cos.x)^2/(sin.x)^2)
   & Z c= dom (-cot-id Z) & Z = dom f & f|A is continuous;then
A2:f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
A3:-cot-id Z is_differentiable_on Z by A1,FDIFF_8:25;
A4:for x st x in dom ((-cot-id Z)`|Z) holds ((-cot-id Z)`|Z).x=f.x
   proof
   let x;
   assume x in dom ((-cot-id Z)`|Z);then
A5:x in Z by A3,FDIFF_1:def 8;then
   ((-cot-id Z)`|Z).x=(cos.x)^2/(sin.x)^2 by A1,FDIFF_8:25
   .=f.x by A1,A5;
   hence thesis;
   end;
   dom ((-cot-id Z)`|Z)=dom f by A1,A3,FDIFF_1:def 8;
   then ((-cot-id Z)`|Z)= f by A4,PARTFUN1:34;
   hence thesis by A1,A2,FDIFF_8:25,INTEGRA5:13;
end;
 
::f.x=1/(x*(1+(ln.x)^2))

theorem
 A c= Z & (for x st x in Z holds f.x=1/(x*(1+(ln.x)^2))
 & ln.x > -1 & ln.x < 1) & Z c= dom (arctan*ln) & Z = dom f
 & f|A is continuous implies integral(f,A)=(arctan*ln).(upper_bound A)
                                          -(arctan*ln).(lower_bound A)
proof
   assume
A1:A c= Z & (for x st x in Z holds f.x=1/(x*(1+(ln.x)^2))
 & ln.x > -1 & ln.x < 1) & Z c= dom (arctan*ln) & Z = dom f
 & f|A is continuous;then
A2:f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
A3:for x st x in Z holds ln.x > -1 & ln.x < 1 by A1;then
A4:arctan*ln is_differentiable_on Z by A1,SIN_COS9:117;
A5:for x st x in dom ((arctan*ln)`|Z) holds ((arctan*ln)`|Z).x=f.x
    proof
       let x;
       assume x in dom ((arctan*ln)`|Z);then
A6:x in Z by A4, FDIFF_1:def 8;then
   ((arctan*ln)`|Z).x=1/(x*(1+(ln.x)^2)) by A1,A3,SIN_COS9:117
                       .=f.x by A1,A6;
   hence thesis;
   end;
  dom ((arctan*ln)`|Z)=dom f by A1,A4,FDIFF_1:def 8;
  then ((arctan*ln)`|Z)= f by A5,PARTFUN1:34;
  hence thesis by A1,A2,A4,INTEGRA5:13;
end;
 
::f.x=-1/(x*(1+(ln.x)^2))

theorem
 A c= Z & (for x st x in Z holds f.x=-1/(x*(1+(ln.x)^2))
 & ln.x > -1 & ln.x < 1) & Z c= dom (arccot*ln) & Z = dom f
 & f|A is continuous implies integral(f,A)=(arccot*ln).(upper_bound A)
                                          -(arccot*ln).(lower_bound A)
proof
   assume
A1:A c= Z & (for x st x in Z holds f.x=-1/(x*(1+(ln.x)^2))
 & ln.x > -1 & ln.x < 1) & Z c= dom (arccot*ln) & Z = dom f
 & f|A is continuous;then
A2:f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
A3:for x st x in Z holds ln.x > -1 & ln.x < 1 by A1;then
A4:arccot*ln is_differentiable_on Z by A1,SIN_COS9:118;
A5:for x st x in dom ((arccot*ln)`|Z) holds ((arccot*ln)`|Z).x=f.x
    proof
       let x;
       assume x in dom ((arccot*ln)`|Z);then
A6:x in Z by A4, FDIFF_1:def 8;then
   ((arccot*ln)`|Z).x=-1/(x*(1+(ln.x)^2)) by A1,A3,SIN_COS9:118
                       .=f.x by A1,A6;
   hence thesis;
   end;
  dom ((arccot*ln)`|Z)=dom f by A1,A4,FDIFF_1:def 8;
  then ((arccot*ln)`|Z)= f by A5,PARTFUN1:34;
  hence thesis by A1,A2,A4,INTEGRA5:13;
end;
 
::f.x=a / sqrt(1-(a*x+b)^2)

theorem
 A c= Z & (for x st x in Z holds f.x=a / sqrt(1-(a*x+b)^2)
 & f1.x=a*x+b & f1.x>-1 & f1.x<1) & Z c= dom ((arcsin)*f1)
 & Z = dom f & f|A is continuous implies
 integral(f,A)=((arcsin)*f1).(upper_bound A)-((arcsin)*f1).(lower_bound A)
proof
   assume
A1:A c= Z & (for x st x in Z holds f.x=a / sqrt(1-(a*x+b)^2)
   & f1.x=a*x+b & f1.x>-1 & f1.x<1) & Z c= dom ((arcsin)*f1)
   & Z = dom f & f|A is continuous;then
A2:f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
A3:for x st x in Z holds f1.x=a*x+b & f1.x>-1 & f1.x<1 by A1;
then A4:(arcsin)*f1 is_differentiable_on Z by A1,FDIFF_7:14;
A5:for x st x in dom (((arcsin)*f1)`|Z) holds (((arcsin)*f1)`|Z).x=f.x
    proof
    let x;
    assume x in dom(((arcsin)*f1)`|Z);then
A6:x in Z by A4,FDIFF_1:def 8; then
  (((arcsin)*f1)`|Z).x=a / sqrt(1-(a*x+b)^2) by A1,A3,FDIFF_7:14
  .=f.x by A1,A6;
   hence thesis;
   end;
  dom(((arcsin)*f1)`|Z)=dom f by A1,A4,FDIFF_1:def 8;
  then (((arcsin)*f1)`|Z)= f by A5,PARTFUN1:34;
  hence thesis by A1,A2,A4,INTEGRA5:13;
end;

theorem
 A c= Z & (for x st x in Z holds f.x=a / sqrt(1-(a*x+b)^2)
 & f1.x=a*x+b & f1.x>-1 & f1.x<1) & Z c= dom ((arccos)*f1)
 & Z = dom f & f|A is continuous implies
 integral(f,A)=(-(arccos)*f1).(upper_bound A)-(-(arccos)*f1).(lower_bound A)
proof
   assume
A1:A c= Z & (for x st x in Z holds f.x=a / sqrt(1-(a*x+b)^2)
   & f1.x=a*x+b & f1.x>-1 & f1.x<1) & Z c= dom ((arccos)*f1)
   & Z = dom f & f|A is continuous;then
A2:f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
A3:for x st x in Z holds f1.x=a*x+b by A1;
AA:for x st x in Z holds f1.x=a*x+b & f1.x>-1 & f1.x<1 by A1;
A4:Z c= dom (-(arccos)*f1) by A1,VALUED_1:8;
A5:(arccos)*f1 is_differentiable_on Z by A1,AA,FDIFF_7:15;
then A6:(-1)(#)((arccos)*f1) is_differentiable_on Z by A4,FDIFF_1:28,X;
  for y st y in Z holds y in dom f1 by A1,FUNCT_1:21; then
A7: Z c= dom f1 by TARSKI:def 3; then
A8: f1 is_differentiable_on Z & for x st x in Z holds (f1`|Z).x = a
  by A3,FDIFF_1:31;
A9:for x st x in Z holds ((-(arccos)*f1)`|Z).x=a / sqrt(1-(a*x+b)^2)
  proof
    let x;
    assume
A10:x in Z;then
A11:f1 is_differentiable_in x by A8,FDIFF_1:16;
A12:f1.x > -1 & f1.x < 1 by A1,A10;
A13:(arccos)*f1 is_differentiable_in x by A5,A10,FDIFF_1:16;
 ((-(arccos)*f1)`|Z).x=diff(-(arccos)*f1,x) by A6,A10,FDIFF_1:def 8
                    .=(-1)*diff((arccos)*f1,x) by A13,FDIFF_1:23,X
                    .=(-1)*(-diff(f1,x)/sqrt(1-(f1.x)^2)) by A11,A12,FDIFF_7:7
                    .=(-1)*(-(f1`|Z).x/sqrt(1-(f1.x)^2))
      by A8,A10,FDIFF_1:def 8
                    .=(-1)*(-a/sqrt(1-(f1.x)^2)) by A3,A7,A10,FDIFF_1:31
                    .=a / sqrt(1-(a*x+b)^2) by A1,A10;
    hence thesis;
  end;
A14:for x st x in dom ((-(arccos)*f1)`|Z) holds ((-(arccos)*f1)`|Z).x=f.x
    proof
    let x;
    assume x in dom((-(arccos)*f1)`|Z);then
A15:x in Z by A6,FDIFF_1:def 8; then
    ((-(arccos)*f1)`|Z).x=a / sqrt(1-(a*x+b)^2) by A9
     .=f.x by A1,A15;
    hence thesis;
   end;
  dom((-(arccos)*f1)`|Z)=dom f by A1,A6,FDIFF_1:def 8;
  then ((-(arccos)*f1)`|Z)= f by A14,PARTFUN1:34;
  hence thesis by A1,A2,A6,INTEGRA5:13;
end;
 
::f.x=x*(1-x #Z 2) #R (-1/2)

theorem
A c= Z & f1=g-f2 & f2=#Z 2 & (for x st x in Z holds f.x=x*(1-x #Z 2) #R (-1/2)
& g.x=1 & f1.x >0) & Z c= dom (( #R (1/2))*f1)
& Z = dom f & f|A is continuous implies
integral(f,A)=(-( #R (1/2))*f1).(upper_bound A)
             -(-( #R (1/2))*f1).(lower_bound A)
proof
   assume
A1:A c= Z & f1=g-f2 & f2=#Z 2
  & (for x st x in Z holds f.x=x*(1-x #Z 2) #R (-1/2)
  & g.x=1 & f1.x >0) & Z c= dom (( #R (1/2))*f1)
  & Z = dom f & f|A is continuous;then
A2:f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
A3:for x st x in Z holds g.x=1 & f1.x >0 by A1;
A4:Z c= dom (-( #R (1/2))*f1) by A1,VALUED_1:8;
for y st y in Z holds y in dom f1 by A1,FUNCT_1:21;
then A6:Z c= dom ((g+(-1)(#)f2)) by A1,TARSKI:def 3;
A7:(( #R (1/2))*f1) is_differentiable_on Z by A1,A3,FDIFF_7:22;
then A8:(-1)(#)(( #R (1/2))*f1) is_differentiable_on Z by A4,FDIFF_1:28,X;
A9:f2=#Z 2 & for x st x in Z holds g.x=1+0*x by A1;
then A10:f1 is_differentiable_on Z &
  for x st x in Z holds (f1`|Z).x =0+2*(-1)*x by A1,A6,FDIFF_4:12,X;
A11:for x st x in Z holds ((-( #R (1/2))*f1)`|Z).x =x*(1-x #Z 2) #R (-1/2)
   proof
     let x;
     assume
A12:x in Z;then
A13:x in dom (g-f2) by A1,FUNCT_1:21;
A14:f1 is_differentiable_in x by A10,A12,FDIFF_1:16;
A15:(g-f2).x=g.x - f2.x by A13,VALUED_1:13
      .=1 -(f2.x) by A1,A12
      .=1 -(x #Z 2) by A1,TAYLOR_1:def 1; then
A16:f1.x = 1-x #Z 2 & f1.x >0 by A1,A12;
A17:( #R (1/2))*f1 is_differentiable_in x by A7,A12,FDIFF_1:16;
 ((-( #R (1/2))*f1)`|Z).x=diff((-( #R (1/2))*f1),x) by A8,A12,FDIFF_1:def 8
                       .=(-1)*diff((( #R (1/2))*f1),x) by A17,FDIFF_1:23,X
                       .=(-1)*((1/2)*((f1.x) #R (1/2-1)) * diff(f1,x))
   by A14,A16,TAYLOR_1:22
      .=(-1)*((1/2)*((f1.x) #R (1/2-1))*(f1`|Z).x) by A10,A12,FDIFF_1:def 8
      .=(-1)*((1/2)*((f1.x) #R (1/2-1))*(0+2*(-1)*x))
        by A1,A6,A9,FDIFF_4:12,X,A12
      .=x*(1-x #Z 2) #R (-1/2) by A1,A15;
     hence thesis;
   end;
A18:for x st x in dom ((-( #R (1/2))*f1)`|Z) holds ((-( #R (1/2))*f1)`|Z).x=f.x
   proof
   let x;
   assume x in dom ((-( #R (1/2))*f1)`|Z);then
A19:x in Z by A8,FDIFF_1:def 8; then
   ((-( #R (1/2))*f1)`|Z).x=x*(1-x #Z 2) #R (-1/2) by A11
   .=f.x by A1,A19;
   hence thesis;
   end;
  dom((-( #R (1/2))*f1)`|Z)=dom f by A1,A8,FDIFF_1:def 8;
  then((-( #R (1/2))*f1)`|Z) = f by A18,PARTFUN1:34;
   hence thesis by A1,A2,A8,INTEGRA5:13;
end;
 
::f.x=x*(a^2-x #Z 2) #R (-1/2)

theorem
 A c= Z & g=f1-f2 & f2=#Z 2 & (for x st x in Z holds
 f.x=x*((a^2-x #Z 2) #R (-1/2)) & f1.x=a^2 & g.x >0)
 & Z c= dom (( #R (1/2))*g) & Z = dom f & f|A is continuous implies
 integral(f,A)
 =(-( #R (1/2))*g).(upper_bound A)-(-( #R (1/2))*g).(lower_bound A)
proof
   assume
A1:A c= Z & g=f1-f2 & f2=#Z 2 & (for x st x in Z  holds
   f.x=x*((a^2-x #Z 2) #R (-1/2)) & f1.x=a^2 & g.x >0)
   & Z c= dom (( #R (1/2))*g) & Z = dom f & f|A is continuous;then
A2:f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
A3:for x st x in Z holds f1.x=a^2 & g.x >0 by A1;
A4:Z c= dom (-( #R (1/2))*g) by A1,VALUED_1:8;
for y st y in Z holds y in dom g by A1,FUNCT_1:21;
then A6:Z c= dom ((f1+(-1)(#)f2)) by A1,TARSKI:def 3;
A7:(( #R (1/2))*g) is_differentiable_on Z by A1,A3,FDIFF_7:27;
then A8:(-1)(#)(( #R (1/2))*g) is_differentiable_on Z by A4,FDIFF_1:28,X;
A9:f2=#Z 2 & for x st x in Z holds f1.x=a^2+0*x by A1;
then A10:g is_differentiable_on Z &
  for x st x in Z holds (g`|Z).x =0+2*(-1)*x by A1,A6,FDIFF_4:12,X;
A11:for x st x in Z holds ((-( #R (1/2))*g)`|Z).x = x*(a^2-x #Z 2) #R (-1/2)
  proof
    let x;
    assume
A12:x in Z;then
A13:x in dom (f1-f2) by A1,FUNCT_1:21;
A14:g is_differentiable_in x by A10,A12,FDIFF_1:16;
A15:(f1-f2).x=f1.x - f2.x by A13,VALUED_1:13
            .=a^2 -(f2.x) by A1,A12
            .=a^2 -(x #Z 2) by A1,TAYLOR_1:def 1; then
A16:g.x = a^2-x #Z 2 & g.x >0 by A1,A12;
A17:( #R (1/2))*g is_differentiable_in x by A7,A12,FDIFF_1:16;
 ((-( #R (1/2))*g)`|Z).x = diff(-( #R (1/2))*g,x) by A8,A12,FDIFF_1:def 8
            .=(-1)*diff(( #R (1/2))*g,x) by A17,FDIFF_1:23,X
            .=(-1)*((1/2)*((g.x) #R (1/2-1)) * diff(g,x))
              by A14,A16,TAYLOR_1:22
            .=(-1)*((1/2)*((g.x) #R (1/2-1))*(g`|Z).x)
              by A10,A12,FDIFF_1:def 8
            .=(-1)*((1/2)*((g.x) #R (1/2-1))*(0+2*(-1)*x))
              by A1,A6,A9,FDIFF_4:12,X,A12
            .=x*(a^2-x #Z 2) #R (-1/2) by A1,A15;
     hence thesis;
   end;
A18:for x st x in dom ((-( #R (1/2))*g)`|Z) holds ((-( #R (1/2))*g)`|Z).x=f.x
   proof
   let x;
   assume x in dom ((-( #R (1/2))*g)`|Z);then
A19:x in Z by A8,FDIFF_1:def 8;then
   ((-( #R (1/2))*g)`|Z).x=x*(a^2-x #Z 2) #R (-1/2) by A11
   .=f.x by A1,A19;
   hence thesis;
   end;
  dom ((-( #R (1/2))*g)`|Z)=dom f by A1,A8,FDIFF_1:def 8;
  then((-( #R (1/2))*g)`|Z)= f by A18,PARTFUN1:34;
   hence thesis by A1,A2,A8,INTEGRA5:13;
end;
 
::f.x=cos.x/((sin.x) #Z (n+1))

theorem
 A c= Z & n>0 & (for x st x in Z holds f.x=cos.x/((sin.x) #Z (n+1))
 & sin.x<>0) & Z c= dom (( #Z n)*(sin^)) & Z =dom f
 & f|A is continuous implies
 integral(f,A)=((-1/n)(#)(( #Z n)*(sin^))).(upper_bound A)
              -((-1/n)(#)(( #Z n)*(sin^))).(lower_bound A)
proof
  assume
A1:A c= Z & n>0 & (for x st x in Z holds f.x=cos.x/((sin.x) #Z (n+1))
   & sin.x<>0) & Z c= dom (( #Z n)*(sin^)) & Z =dom f
   & f|A is continuous;
then AA:Z c= dom ((-1/n)(#)(( #Z n)*(sin^))) by VALUED_1:def 5;
A2:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A3:for x st x in Z holds sin.x<>0 by A1;then
A4:(-1/n)(#)(( #Z n)*(sin^)) is_differentiable_on Z by A1,AA,FDIFF_7:30;
A5:for x st x in dom (((-1/n)(#)(( #Z n)*(sin^)))`|Z) holds
   (((-1/n)(#)(( #Z n)*(sin^)))`|Z).x=f.x
   proof
   let x;
   assume x in dom (((-1/n)(#)(( #Z n)*(sin^)))`|Z);then
A6:x in Z by A4,FDIFF_1:def 8;then
  (((-1/n)(#)(( #Z n)*(sin^)))`|Z).x=cos.x/((sin.x) #Z (n+1))
    by A1,AA,A3,FDIFF_7:30
   .=f.x by A1,A6;
   hence thesis;
   end;
   dom (((-1/n)(#)(( #Z n)*(sin^)))`|Z)=dom f by A1,A4,FDIFF_1:def 8;
   then(((-1/n)(#)(( #Z n)*(sin^)))`|Z)= f by A5,PARTFUN1:34;
   hence thesis by A1,A2,A4,INTEGRA5:13;
end;
 
::f.x=sin.x/((cos.x) #Z (n+1))

theorem
 A c= Z & n>0 & (for x st x in Z holds f.x=sin.x/((cos.x) #Z (n+1))
 & cos.x<>0) & Z c= dom (( #Z n)*(cos^)) & Z =dom f
 & f|A is continuous implies
 integral(f,A)=((1/n)(#)(( #Z n)*(cos^))).(upper_bound A)
              -((1/n)(#)(( #Z n)*(cos^))).(lower_bound A)
proof
  assume
A1:A c= Z & n>0 & (for x st x in Z holds f.x=sin.x/((cos.x) #Z (n+1))
   & cos.x<>0) & Z c= dom (( #Z n)*(cos^)) & Z =dom f
   & f|A is continuous;
then AA:Z c= dom ((1/n)(#)(( #Z n)*(cos^))) by VALUED_1:def 5;
A2:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A3:for x st x in Z holds cos.x<>0 by A1;then
A4:(1/n)(#)(( #Z n)*(cos^)) is_differentiable_on Z by A1,AA,FDIFF_7:31;
A5:for x st x in dom (((1/n)(#)(( #Z n)*(cos^)))`|Z) holds
   (((1/n)(#)(( #Z n)*(cos^)))`|Z).x=f.x
   proof
   let x;
   assume x in dom (((1/n)(#)(( #Z n)*(cos^)))`|Z);then
A6:x in Z by A4,FDIFF_1:def 8;then
  (((1/n)(#)(( #Z n)*(cos^)))`|Z).x=sin.x/((cos.x) #Z (n+1))
    by A1,AA,A3,FDIFF_7:31
   .=f.x by A1,A6;
   hence thesis;
   end;
   dom (((1/n)(#)(( #Z n)*(cos^)))`|Z)=dom f by A1,A4,FDIFF_1:def 8;
   then(((1/n)(#)(( #Z n)*(cos^)))`|Z)= f by A5,PARTFUN1:34;
   hence thesis by A1,A2,A4,INTEGRA5:13;
end;
 
::f.x=1/((1+x^2)*arccot.x)

theorem
 A c= Z & f = ((g1+g2)^)/f2 & f2 = arccot & Z c= ]. -1,1 .[ & g2=#Z 2
 & (for x st x in Z holds f.x=1/((1+x^2)*arccot.x) & g1.x=1 & f2.x>0) &
 Z = dom f implies
 integral(f,A)=(-ln*arccot).(upper_bound A)-(-ln*arccot).(lower_bound A)
proof
   assume
A1:A c= Z & f = ((g1+g2)^)/f2 & f2 = arccot & Z c= ]. -1,1 .[ & g2=#Z 2
 & (for x st x in Z holds f.x=1/((1+x^2)*arccot.x) & g1.x=1
 & f2.x>0) & Z = dom f;
then  Z = dom ((g1+g2)^) /\ (dom f2 \ f2"{0}) by RFUNCT_1:def 4;
then A4: Z c= dom ((g1+g2)^) & Z c= dom f2 \ f2"{0} by XBOOLE_1:18;
 for x st x in Z holds g1.x=1 by A1;
then A6: (g1+g2)^ is_differentiable_on Z by A1,A4,Th1;
A7: f2 is_differentiable_on Z by A1,SIN_COS9:82;
 for x st x in Z holds f2.x<>0 by A1;
then  f is_differentiable_on Z by A1,A6,A7,FDIFF_2:21;
then f|Z is continuous by FDIFF_1:33;
then A11:f|A is continuous by FCONT_1:17,A1;
A12:Z c= dom (f2^) by A4,RFUNCT_1:def 8;
    dom (f2^) c= dom f2 by RFUNCT_1:11;then
A13:Z c= dom f2 by A12,XBOOLE_1:1;
A14:for x st x in Z holds f2.x>0 by A1;
rng (f2|Z) c= right_open_halfline(0)
    proof
      let x be set;
      assume x in rng (f2|Z); then
      consider y be set such that
A18:  y in dom (f2|Z) & x = (f2|Z).y by FUNCT_1:def 5;
      y in Z by A18,RELAT_1:86; then
      f2.y > 0 by A1; then
      (f2|Z).y > 0 by A18,FUNCT_1:70;
      hence thesis by A18,XXREAL_1:235;
    end;
then f2.:Z c= dom ln by RELAT_1:148,TAYLOR_1:18;
then A22:Z c= dom (ln*arccot) by A1,A13,FUNCT_1:171;
A23:f is_integrable_on A & f|A is bounded by A1,A11,INTEGRA5:10,11;
A24:ln*arccot is_differentiable_on Z by A1,A22,A14,SIN_COS9:90;
Z c= dom (-ln*arccot) by A22,VALUED_1:8;
then A26:-ln*arccot is_differentiable_on Z by A24,FDIFF_1:28,X;
A27:for x st x in Z holds ((-ln*arccot)`|Z).x = 1/((1+x^2)*arccot.x)
    proof
      let x;
      assume
A28:x in Z;then
A29:-1 < x & x < 1 by A1,XXREAL_1:4;
    arccot is_differentiable_on Z by SIN_COS9:82,A1;then
A30:arccot is_differentiable_in x by A28,FDIFF_1:16;
A31:arccot.x >0 by A1,A28;
A32:ln*arccot is_differentiable_in x by A24,A28,FDIFF_1:16;
 ((-ln*arccot)`|Z).x=diff((-ln*arccot),x) by A26,A28,FDIFF_1:def 8
                   .=(-1)*diff(ln*arccot,x) by A32,FDIFF_1:23,X
                   .=(-1)*(diff(arccot,x)/arccot.x) by A30,A31,TAYLOR_1:20
                   .=(-1)*((-1/(1+x^2))/arccot.x) by A29,SIN_COS9:76
                   .= (1/(1+x^2))/arccot.x
                   .= 1/((1+x^2)*arccot.x) by XCMPLX_1:79;
                 hence thesis;
     end;
A33:for x st x in dom ((-ln*(arccot))`|Z) holds ((-ln*(arccot))`|Z).x=f.x
   proof
     let x;
     assume x in dom ((-ln*arccot)`|Z);then
A34:x in Z by A26,FDIFF_1:def 8;then
  ((-ln*(arccot))`|Z).x=1/((1+x^2)*arccot.x) by A27
                      .=f.x by A1,A34;
  hence thesis;
  end;
  dom ((-ln*arccot)`|Z)=dom f by A1,A26,FDIFF_1:def 8;
  then ((-ln*arccot)`|Z)= f by A33,PARTFUN1:34;
  hence thesis by A1,A23,A26,INTEGRA5:13;
end;
 
::f.x=1 / (sqrt(1-x^2)*arcsin.x)

theorem
 A c= Z & Z c= ]. -1,1 .[ & (for x st x in Z holds
 arcsin.x>0 & f1.x=1) & Z c= dom (ln*(arcsin)) & Z = dom f
 & f=((( #R (1/2))*(f1-#Z 2))(#)arcsin)^ implies
 integral(f,A)=(ln*(arcsin)).(upper_bound A)-(ln*(arcsin)).(lower_bound A)
proof
   assume
A1:A c= Z & Z c= ]. -1,1 .[  & (for x st x in Z holds
   arcsin.x>0 & f1.x=1) & Z c= dom (ln*(arcsin)) & Z = dom f
   & f=((( #R (1/2))*(f1-#Z 2))(#)arcsin)^;
   set g=((( #R (1/2))*(f1-#Z 2))(#)arcsin);
A3:Z c= dom g by A1,RFUNCT_1:11;
dom g = dom (( #R (1/2))*(f1-#Z 2)) /\ dom arcsin by VALUED_1:def 4;
then
A5:Z c= dom (( #R (1/2))*(f1-#Z 2)) & Z c= dom arcsin by A3,XBOOLE_1:18;
A6:arcsin is_differentiable_on Z by A1,FDIFF_1:34,SIN_COS6:84;
   set f2=#Z 2;
for x st x in Z holds (f1-#Z 2).x >0
   proof
   let x;
   assume
A9:x in Z; then
   -1 < x & x < 1 by A1,XXREAL_1:4; then
   0 < 1+x & 0 < 1-x by XREAL_1:150,XREAL_1:52; then
A10:0 < (1+x)*(1-x) by XREAL_1:131;
 for x st x in Z holds x in dom (f1-f2) by FUNCT_1:21,A5;
    then (f1-f2).x = f1.x - f2.x by A9,VALUED_1:13
             .=f1.x - (x #Z (1+1)) by TAYLOR_1:def 1
             .=f1.x - ((x #Z 1)*(x #Z 1)) by TAYLOR_1:1
             .=f1.x - (x*(x #Z 1)) by PREPOWER:45
             .=f1.x - x*x by PREPOWER:45
             .=1 - x*x by A1,A9;
     hence thesis by A10;
    end;
then for x st x in Z holds f1.x=1 & (f1-#Z 2).x >0 by A1;
then (( #R (1/2))*(f1-#Z 2)) is_differentiable_on Z by A5,FDIFF_7:22;
then A19:g is_differentiable_on Z by A3,A6,FDIFF_1:29;
for x st x in Z holds g.x<>0 by RFUNCT_1:13,A1;
then f is_differentiable_on Z by A1,A19,FDIFF_2:22;
    then f|Z is continuous by FDIFF_1:33;then
f|A is continuous by A1,FCONT_1:17;
then A23:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
A24:for x st x in Z holds arcsin.x>0 by A1;
then A25:ln*arcsin is_differentiable_on Z by A1,FDIFF_7:8;
B1:for x st x in Z holds f.x=1 / (sqrt(1-x^2)*arcsin.x)
   proof
   let x;
   assume
B2:x in Z;
then
B3:x in dom (f1-#Z 2) & (f1-#Z 2).x in dom ( #R (1/2)) by FUNCT_1:21,A5;
then B4:(f1-#Z 2).x in right_open_halfline(0) by TAYLOR_1:def 4;
-1 < x & x < 1 by A1,XXREAL_1:4,B2;
   then 0 < 1+x & 0 < 1-x by XREAL_1:150,XREAL_1:52; then
B6:0 < (1+x)*(1-x) by XREAL_1:131;
    (((( #R (1/2))*(f1-#Z 2))(#)arcsin)^).x
  =1/(((( #R (1/2))*(f1-#Z 2))(#)arcsin).x) by RFUNCT_1:def 8,A1,B2
  .=1/((( #R (1/2))*(f1-#Z 2)).x*arcsin.x) by VALUED_1:5
  .=1/(( #R (1/2)).((f1-#Z 2).x)*arcsin.x) by FUNCT_1:22,A5,B2
  .=1/((((f1-#Z 2).x) #R (1/2))*arcsin.x) by TAYLOR_1:def 4,B4
  .=1/(((f1.x-(( #Z 2).x)) #R (1/2))*arcsin.x) by VALUED_1:13,B3
  .=1/(((f1.x-(x #Z 2)) #R (1/2))*arcsin.x) by TAYLOR_1:def 1
  .=1/(((f1.x-x^2) #R (1/2))*arcsin.x) by FDIFF_7:1
  .=1/(((1-x^2) #R (1/2))*arcsin.x) by A1,B2
  .=1/(sqrt(1-x^2)*arcsin.x) by FDIFF_7:2,B6;
    hence thesis by A1;
    end;
A26:for x st x in dom (ln*(arcsin)`|Z) holds (ln*(arcsin)`|Z).x=f.x
    proof
     let x;
     assume x in dom (ln*(arcsin)`|Z);then
A27: x in Z by A25,FDIFF_1:def 8; then
     (ln*(arcsin)`|Z).x=1 / (sqrt(1-x^2)*arcsin.x) by A1,A24,FDIFF_7:8
     .=f.x by B1,A27;
     hence thesis;
   end;
  dom (ln*(arcsin)`|Z)=dom f by A1,A25,FDIFF_1:def 8;
  then (ln*(arcsin)`|Z)= f by A26,PARTFUN1:34;
  hence thesis by A1,A23,A25,INTEGRA5:13;
end;
 
::f.x=1 / (sqrt(1-x^2)*arccos.x)

theorem
 A c= Z & Z c= ]. -1,1 .[  & (for x st x in Z holds f1.x=1
 & arccos.x>0) & Z c= dom (ln*arccos) & Z = dom f &
 f=((( #R (1/2))*(f1-#Z 2))(#)arccos)^ implies
 integral(f,A)=(-ln*arccos).(upper_bound A)-(-ln*arccos).(lower_bound A)
proof
  assume
A1:A c= Z & Z c= ]. -1,1 .[  & (for x st x in Z holds
   f1.x=1 & arccos.x>0) & Z c= dom (ln*arccos) & Z = dom f &
   f=((( #R (1/2))*(f1-#Z 2))(#)arccos)^;
   set g=((( #R (1/2))*(f1-#Z 2))(#)arccos);
A3:Z c= dom g by A1,RFUNCT_1:11;
dom g = dom (( #R (1/2))*(f1-#Z 2)) /\ dom arccos by VALUED_1:def 4;
then
A5:Z c= dom (( #R (1/2))*(f1-#Z 2)) & Z c= dom arccos by A3,XBOOLE_1:18;
A6:arccos is_differentiable_on Z by A1,FDIFF_1:34,SIN_COS6:108;
   set f2=#Z 2;
for x st x in Z holds (f1-#Z 2).x >0
   proof
   let x;
   assume
A9:x in Z; then
   -1 < x & x < 1 by A1,XXREAL_1:4; then
   0 < 1+x & 0 < 1-x by XREAL_1:150,XREAL_1:52; then
A10:0 < (1+x)*(1-x) by XREAL_1:131;
 for x st x in Z holds x in dom (f1-f2) by FUNCT_1:21,A5;
     then (f1-f2).x = f1.x - f2.x by A9,VALUED_1:13
             .=f1.x - (x #Z (1+1)) by TAYLOR_1:def 1
             .=f1.x - ((x #Z 1)*(x #Z 1)) by TAYLOR_1:1
             .=f1.x - (x*(x #Z 1)) by PREPOWER:45
             .=f1.x - x*x by PREPOWER:45
             .=1 - x*x by A1,A9;
     hence thesis by A10;
    end;
then for x st x in Z holds f1.x=1 & (f1-#Z 2).x >0 by A1;
then (( #R (1/2))*(f1-#Z 2)) is_differentiable_on Z by A5,FDIFF_7:22;
then A19:g is_differentiable_on Z by A3,A6,FDIFF_1:29;
for x st x in Z holds g.x<>0 by RFUNCT_1:13,A1;
then f is_differentiable_on Z by A1,A19,FDIFF_2:22;
    then f|Z is continuous by FDIFF_1:33;then
f|A is continuous by A1,FCONT_1:17;
then A23:f is_integrable_on A & f|A is bounded by A1,INTEGRA5:10,11;
for x st x in Z holds arccos.x>0 by A1;
then A25:ln*arccos is_differentiable_on Z by A1,FDIFF_7:9;
Z c= dom (-ln*arccos) by A1,VALUED_1:8;
then A27:(-1)(#)(ln*arccos) is_differentiable_on Z by A25,FDIFF_1:28,X;
A28:for x st x in Z holds ((-ln*arccos)`|Z).x = 1 / (sqrt(1-x^2)*arccos.x)
   proof
     let x;
     assume
A29:x in Z;then
A30:-1 < x & x < 1 by A1,XXREAL_1:4;
A31:arccos is_differentiable_in x by A1,A29,FDIFF_1:16,SIN_COS6:108;
A32:arccos.x >0 by A1,A29;
A33:ln*arccos is_differentiable_in x by A25,A29,FDIFF_1:16;
 ((-ln*arccos)`|Z).x=diff(-ln*(arccos),x) by A27,A29,FDIFF_1:def 8
      .=(-1)*diff(ln*arccos,x) by A33,FDIFF_1:23,X
      .=(-1)*(diff(arccos,x)/arccos.x) by A31,A32,TAYLOR_1:20
      .=(-1)*((-1 / sqrt(1-x^2))/arccos.x) by A30,SIN_COS6:108
      .=(-1)*(-(1 / sqrt(1-x^2))/arccos.x)
      .=1 / (sqrt(1-x^2)*arccos.x) by XCMPLX_1:79;
     hence thesis;
   end;
B1:for x st x in Z holds f.x=1 / (sqrt(1-x^2)*arccos.x)
   proof
   let x;
   assume
B2:x in Z;
then
B3:x in dom (f1-#Z 2) & (f1-#Z 2).x in dom ( #R (1/2)) by FUNCT_1:21,A5;
then B4:(f1-#Z 2).x in right_open_halfline(0) by TAYLOR_1:def 4;
-1 < x & x < 1 by A1,XXREAL_1:4,B2;
   then 0 < 1+x & 0 < 1-x by XREAL_1:150,XREAL_1:52; then
B6:0 < (1+x)*(1-x) by XREAL_1:131;
   (((( #R (1/2))*(f1-#Z 2))(#)arccos)^).x
  =1/(((( #R (1/2))*(f1-#Z 2))(#)arccos).x) by RFUNCT_1:def 8,A1,B2
  .=1/((( #R (1/2))*(f1-#Z 2)).x*arccos.x) by VALUED_1:5
  .=1/(( #R (1/2)).((f1-#Z 2).x)*arccos.x) by FUNCT_1:22,A5,B2
  .=1/((((f1-#Z 2).x) #R (1/2))*arccos.x) by TAYLOR_1:def 4,B4
  .=1/(((f1.x-(( #Z 2).x)) #R (1/2))*arccos.x) by VALUED_1:13,B3
  .=1/(((f1.x-(x #Z 2)) #R (1/2))*arccos.x) by TAYLOR_1:def 1
  .=1/(((f1.x-x^2) #R (1/2))*arccos.x) by FDIFF_7:1
  .=1/(((1-x^2) #R (1/2))*arccos.x) by A1,B2
  .=1/(sqrt(1-x^2)*arccos.x) by FDIFF_7:2,B6;
    hence thesis by A1;
    end;
A34:for x st x in dom ((-ln*arccos)`|Z) holds ((-ln*arccos)`|Z).x=f.x
    proof
     let x;
     assume x in dom ((-ln*arccos)`|Z);then
A35: x in Z by A27,FDIFF_1:def 8; then
     ((-ln*arccos)`|Z).x=1 / (sqrt(1-x^2)*arccos.x) by A28
       .=f.x by B1,A35;
     hence thesis;
   end;
  dom ((-ln*arccos)`|Z)=dom f by A1,A27,FDIFF_1:def 8;
  then ((-ln*arccos)`|Z)= f by A34,PARTFUN1:34;
  hence thesis by A1,A23,A27,INTEGRA5:13;
end;
