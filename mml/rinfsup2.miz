:: Inferior Limit, Superior Limit and Convergence of Sequences of Extended
:: Real Numbers
::  by Hiroshi Yamazaki , Noboru Endou , Yasunari Shidama and Hiroyuki Okazaki
::
:: Received August 28, 2007
:: Copyright (c) 2007 Association of Mizar Users

environ

 vocabularies PARTFUN1, RELAT_1, ABSVALUE, FUNCT_1, ORDINAL2, LATTICES,
      COMPLEX1, SEQ_1, SEQ_2, SEQM_3, MEASURE6, ARYTM, ARYTM_1, PROB_1,
      SQUARE_1, RLVECT_1, GROUP_1, MESFUNC5, SUPINF_1, RINFSUP1, FINSET_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0,
      COMPLEX1, REAL_1, XXREAL_0, RELSET_1, FUNCT_2, PARTFUN1, NAT_1, SUPINF_1,
      SUPINF_2, PSCOMP_1, SEQ_1, SEQ_2, SEQM_3, SEQ_4, KURATO_2, LIMFUNC1,
      RINFSUP1, MEASURE6, EXTREAL1, MESFUNC1, MESFUNC2, MESFUNC5;
 constructors REAL_1, NAT_1, DOMAIN_1, EXTREAL1, INT_1, COMPLEX1, MEASURE6,
      MESFUNC1, PARTFUN3, LIMFUNC1, KURATO_2, RINFSUP1, MESFUNC5;
 registrations SUBSET_1, NAT_1, RELSET_1, XREAL_0, SEQ_1, MEMBERED, ORDINAL1,
      INT_1, NUMBERS, XBOOLE_0;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, SUPINF_2, MESFUNC1, RINFSUP1, MEASURE6;
 theorems NAT_1, TARSKI, SUPINF_1, FUNCT_1, FUNCT_2, KURATO_2, SUPINF_2,
      EXTREAL1, INT_1, EXTREAL2, SEQ_2, SEQM_3, SEQ_4, RINFSUP1, XREAL_0,
      XBOOLE_0, MESFUNC2, XREAL_1, XXREAL_0, ORDINAL1, MESFUNC5;
 schemes FUNCT_2, DOMAIN_1;

begin :: Inferior limit, superior limit and convergence of sequence
      :: of extended real numbers

 reserve n,m,k for Element of NAT;
 reserve X for non empty Subset of ExtREAL;
 reserve Y for non empty Subset of REAL;

theorem Th1:
X = Y & Y is bounded_above implies X is bounded_above & sup X = sup Y
proof
   assume that
A1: X=Y and
A2: Y is bounded_above;
   now let r be ext-real number;
    assume A3:r in X; then
    r is real number by A1,XREAL_0:def 1;
    hence r <= sup Y by A1,A2,A3,SEQ_4:def 4;
   end; then
A4:sup Y is majorant of X by SUPINF_1:def 9;
   hence
A5: X is bounded_above by SUPINF_1:def 11;
A6:sup X <= sup Y by A4,SUPINF_1:def 16;
   not -infty in X by A1,SUPINF_1:6; then
   X <> {-infty} by TARSKI:def 1; then
   sup X in REAL by A5,SUPINF_1:83; then
A7:sup X is real number by XREAL_0:def 1;
   for s be real number st s in Y holds s <= sup X by A1,SUPINF_1:76; then
   sup Y <= sup X by A7,SEQ_4:62;
   hence sup Y = sup X by A6,XXREAL_0:1;
end;

theorem Th2:
X = Y & X is bounded_above implies Y is bounded_above & sup X = sup Y
proof
   assume A1: X=Y & X is bounded_above; then
   consider UB being majorant of X such that
A2: UB in REAL by SUPINF_1:def 11;
   reconsider UB as real number by A2,XREAL_0:def 1;
   for x be real number st x in Y holds x <= UB by A1,SUPINF_1:def 9; then
   Y is bounded_above by SEQ_4:def 1;
   hence thesis by A1,Th1;
end;

theorem Th3:
X = Y & Y is bounded_below implies X is bounded_below & inf X = inf Y
proof
   assume A1: X=Y & Y is bounded_below;
   now let r be ext-real number;
    assume A2:r in X; then
    r is real number by A1,XREAL_0:def 1;
    hence inf Y <= r by A1,A2,SEQ_4:def 5;
   end; then
A3:inf Y is minorant of X by SUPINF_1:def 10;
   hence
A4: X is bounded_below by SUPINF_1:def 12;
A5:inf Y <= inf X by A3,SUPINF_1:def 17;
   not +infty in X by A1,SUPINF_1:1; then
   X <> {+infty} by TARSKI:def 1; then
   inf X in REAL by A4,SUPINF_1:84; then
A6:inf X is real number by XREAL_0:def 1;
   for s be real number st s in Y holds inf X <= s by A1,SUPINF_1:79; then
   inf X <= inf Y by A6,SEQ_4:60;
   hence inf Y = inf X by A5,XXREAL_0:1;
end;

theorem Th4:
X = Y & X is bounded_below implies Y is bounded_below & inf X = inf Y
proof
   assume A1: X=Y & X is bounded_below; then
   consider UB being minorant of X such that
A2: UB in REAL by SUPINF_1:def 12;
   reconsider UB as real number by A2,XREAL_0:def 1;
   for x be real number st x in Y holds UB <= x by A1,SUPINF_1:def 10; then
   Y is bounded_below by SEQ_4:def 2;
   hence thesis by A1,Th3;
end;

definition let seq be ExtREAL_sequence;
  func sup seq -> Element of ExtREAL equals sup rng seq;
  coherence;
  func inf seq -> Element of ExtREAL equals inf rng seq;
  coherence;
end;

definition let seq be ExtREAL_sequence;
  attr seq is bounded_below means :Def3:
   rng seq is bounded_below;
  attr seq is bounded_above means :Def4:
   rng seq is bounded_above;
end;

definition let seq be ExtREAL_sequence;
  attr seq is bounded means :Def5:
   seq is bounded_above & seq is bounded_below;
end;

 reserve seq for ExtREAL_sequence;

theorem Th5:
for seq,n holds
 {seq.k where k is Element of NAT: n <= k} is non empty Subset of ExtREAL
proof
   let seq,n;
   deffunc F(Element of NAT) = seq.$1;
   defpred P[Element of NAT] means n <= $1;
   set Y = {F(k) where k is Element of NAT: P[k]};
A1: Y is Subset of ExtREAL from DOMAIN_1:sch 8;
   seq.n in Y;
   hence thesis by A1;
end;

definition let seq be ExtREAL_sequence;
 func inferior_realsequence seq -> ExtREAL_sequence means :Def6:
  for n be Element of NAT
   ex Y being non empty Subset of ExtREAL st
    Y = {seq.k where k is Element of NAT : n <= k} &
    it.n = inf Y;
existence
proof
   defpred P[Element of NAT, Element of ExtREAL] means
   ex Y being non empty Subset of ExtREAL st
     Y = {seq.k where k is Element of NAT : $1 <= k} &
     $2 = inf Y;
A1:for n being Element of NAT ex y being Element of ExtREAL st P[n,y]
   proof
    let n be Element of NAT;
    reconsider Y={seq.k where k is Element of NAT : n <= k}
     as non empty Subset of ExtREAL by Th5;
    reconsider y = inf Y as Element of ExtREAL;
    take y;
    thus thesis;
   end;
   thus ex f being Function of NAT,ExtREAL st
    for n being Element of NAT holds P[n, f.n] from FUNCT_2:sch 3(A1);
end;
uniqueness
proof
   let seq1,seq2 be ExtREAL_sequence such that
A2: for n be Element of NAT ex Y being non empty Subset of ExtREAL st
     Y = {seq.k where k is Element of NAT : n <= k} & seq1.n = inf Y and
A3: for n be Element of NAT ex Y being non empty Subset of ExtREAL st
     Y = {seq.k where k is Element of NAT : n <= k} & seq2.n = inf Y;
A4:for y be set st y in NAT holds seq1.y = seq2.y
   proof
    let y be set;
    assume y in NAT; then
    reconsider n=y as Element of NAT;
    (ex Y be non empty Subset of ExtREAL st
      Y = {seq.k where k is Element of NAT: n <= k} & seq1.n=inf Y) &
    (ex Z be non empty Subset of ExtREAL st
      Z = {seq.k where k is Element of NAT: n <= k} & seq2.n=inf Z) by A2,A3;
    hence thesis;
   end;
   NAT = dom seq1 & NAT = dom seq2 by FUNCT_2:def 1;
   hence thesis by A4,FUNCT_1:9;
end;
end;

definition let seq be ExtREAL_sequence;
  func superior_realsequence seq -> ExtREAL_sequence means :Def7:
  for n be Element of NAT
   ex Y being non empty Subset of ExtREAL st
    Y = {seq.k where k is Element of NAT: n <= k} & it.n = sup Y;
existence
proof
   defpred P[Element of NAT, Element of ExtREAL] means
    ex Y being non empty Subset of ExtREAL st
     Y = {seq.k where k is Element of NAT : $1 <= k} & $2 = sup Y;
A1:for n being Element of NAT ex y being Element of ExtREAL st P[n,y]
   proof
    let n be Element of NAT;
    reconsider Y={seq.k where k is Element of NAT : n <= k}
     as non empty Subset of ExtREAL by Th5;
    reconsider y = sup Y as Element of ExtREAL;
    take y;
    thus thesis;
   end;
   thus ex f being Function of NAT,ExtREAL st
    for n being Element of NAT holds P[n, f.n] from FUNCT_2:sch 3(A1);
end;
uniqueness
proof
   let seq1,seq2 be ExtREAL_sequence such that
A2: for n be Element of NAT ex Y being non empty Subset of ExtREAL st
     Y = {seq.k where k is Element of NAT : n <= k} & seq1.n = sup Y and
A3: for m be Element of NAT ex Y being non empty Subset of ExtREAL st
     Y = {seq.k where k is Element of NAT : m <= k} & seq2.m = sup Y;
A4:for y be set st y in NAT holds seq1.y = seq2.y
   proof
    let y be set;
    assume y in NAT; then
    reconsider n=y as Element of NAT;
    (ex Y be non empty Subset of ExtREAL st
      Y = {seq.k where k is Element of NAT: n <= k} & seq1.n=sup Y) &
    (ex Z be non empty Subset of ExtREAL st
      Z = {seq.k where k is Element of NAT: n <= k} & seq2.n=sup Z) by A2,A3;
    hence thesis;
   end;
   NAT = dom seq1 & NAT = dom seq2 by FUNCT_2:def 1;
   hence thesis by A4,FUNCT_1:9;
end;
end;

theorem
seq is finite implies seq is Real_Sequence
proof
   assume seq is finite; then
   rng seq is Subset of REAL by MESFUNC2:35;
   hence thesis by FUNCT_2:8;
end;

definition let f be PartFunc of NAT,ExtREAL;
 attr f is increasing means :Def8:
  for m,n st m in dom f & n in dom f & m < n holds f.m < f.n;
 attr f is decreasing means :Def9:
  for m,n st m in dom f & n in dom f & m < n holds f.m > f.n;
 attr f is non-decreasing means :Def10:
  for m,n st m in dom f & n in dom f & m <= n holds f.m <= f.n;
 attr f is non-increasing means :Def11:
  for m,n st m in dom f & n in dom f & m <= n holds f.m >= f.n;
end;

theorem Th7:
(seq is increasing iff
  for n,m be Element of NAT st m<n holds seq.m<seq.n) &
(seq is decreasing iff
  for n,m be Element of NAT st m<n holds seq.n<seq.m) &
(seq is non-decreasing iff
  for n,m be Element of NAT st m<=n holds seq.m<=seq.n) &
(seq is non-increasing iff
  for n,m be Element of NAT st m<=n holds seq.n<=seq.m)
proof
A1:dom seq = NAT by FUNCT_2:def 1;
   (seq is increasing iff
     for m, n st m in dom seq & n in dom seq & m < n holds seq.m < seq.n) &
   (seq is decreasing iff
     for m,n st m in dom seq & n in dom seq & m < n holds seq.n < seq.m) &
   (seq is non-decreasing iff
     for m,n st m in dom seq & n in dom seq & m <= n holds seq.m <= seq.n) &
   (seq is non-increasing iff
     for m,n st m in dom seq & n in dom seq & m <= n holds seq.n <= seq.m)
      by Def8,Def9,Def10,Def11;
   hence thesis by A1;
end;

theorem Th8:
  (inferior_realsequence seq).n <= seq.n &
  seq.n <= (superior_realsequence seq).n
proof
   consider Y being non empty Subset of ExtREAL such that
A1: Y = {seq.k where k is Element of NAT: n <= k} &
    (inferior_realsequence seq).n = inf Y by Def6;
A2:seq.n in Y by A1;
   hence (inferior_realsequence seq).n <= seq.n by A1,SUPINF_1:79;
   ex Z being non empty Subset of ExtREAL st
    Z = {seq.k where k is Element of NAT: n <= k} &
    (superior_realsequence seq).n = sup Z by Def7;
   hence seq.n <=(superior_realsequence seq).n by A1,A2,SUPINF_1:76;
end;

Lm1:
superior_realsequence seq is non-increasing
proof
   now let n,m be Element of NAT;
    assume A1: m<=n;
    consider Y being non empty Subset of ExtREAL such that
A2:  Y = {seq.k where k is Element of NAT: m <= k} &
     (superior_realsequence seq).m = sup Y by Def7;
    consider Z being non empty Subset of ExtREAL such that
A3:  Z = {seq.k where k is Element of NAT: n <= k} &
     (superior_realsequence seq).n = sup Z by Def7;
    Z c= Y
    proof
     let z be set;
     assume z in Z; then
     consider k be Element of NAT such that
A4:   z=seq.k & n <= k by A3;
     m <= k by A1,A4,XXREAL_0:2;
     hence z in Y by A2,A4;
    end;
    hence (superior_realsequence seq).n <= (superior_realsequence seq).m
      by A2,A3,SUPINF_1:91;
   end;
   hence thesis by Th7;
end;

Lm2:
inferior_realsequence seq is non-decreasing
proof
   now let n,m be Element of NAT;
    assume A1: m<=n;
    consider Y being non empty Subset of ExtREAL such that
A2:  Y = {seq.k where k is Element of NAT: m <= k} &
     (inferior_realsequence seq).m = inf Y by Def6;
    consider Z being non empty Subset of ExtREAL such that
A3:  Z = {seq.k where k is Element of NAT: n <= k} &
     (inferior_realsequence seq).n = inf Z by Def6;
    Z c= Y
    proof
     let z be set;
     assume z in Z; then
     consider k be Element of NAT such that
A4:  z=seq.k & n <= k by A3;
     m <= k by A1,A4,XXREAL_0:2;
     hence z in Y by A2,A4;
    end;
    hence (inferior_realsequence seq).m <= (inferior_realsequence seq).n
      by A2,A3,SUPINF_1:94;
   end;
   hence thesis by Th7;
end;

registration let seq;
  cluster superior_realsequence seq -> non-increasing;
  coherence by Lm1;
  cluster inferior_realsequence seq -> non-decreasing;
  coherence by Lm2;
end;

definition let seq be ExtREAL_sequence;
  func lim_sup seq -> Element of ExtREAL equals
   inf superior_realsequence seq;
  coherence;
  func lim_inf seq -> Element of ExtREAL equals
   sup inferior_realsequence seq;
  coherence;
end;

 reserve rseq for Real_Sequence;

theorem Th9:
seq = rseq & rseq is bounded implies
 superior_realsequence seq = superior_realsequence rseq &
 lim_sup seq = lim_sup rseq
proof
   assume A1: seq=rseq & rseq is bounded;
A2:now let x be set;
    assume x in NAT; then
    reconsider n = x as Element of NAT;
A3:ex Y1 being non empty Subset of ExtREAL st
     Y1 = {seq.k where k is Element of NAT: n <= k} &
     (superior_realsequence seq).n = sup Y1 by Def7;
    {rseq.k where k is Element of NAT: n <= k} is Subset of REAL
    proof
     now let x be set;
      assume x in {rseq.k where k is Element of NAT: n <= k}; then
      ex k be Element of NAT st x=rseq.k & n<= k;
      hence x in REAL;
     end;
     hence thesis by TARSKI:def 3;
    end; then
    reconsider Y2={rseq.k where k is Element of NAT: n <= k} as Subset of REAL;
    rseq is bounded_above by A1,SEQ_2:def 5; then
    Y2 is bounded_above by RINFSUP1:31; then
    (superior_realsequence seq).x =sup Y2 by A1,A3,Th1;
    hence (superior_realsequence seq).x =(superior_realsequence rseq).x
     by RINFSUP1:def 5;
   end;
   NAT = dom superior_realsequence seq &
   NAT = dom superior_realsequence rseq by FUNCT_2:def 1; then
A4:superior_realsequence seq = superior_realsequence rseq by A2,FUNCT_1:9;
   superior_realsequence rseq is bounded by A1,RINFSUP1:58; then
   superior_realsequence rseq is bounded_below by SEQ_2:def 5; then
   rng superior_realsequence rseq is bounded_below by RINFSUP1:6;
   hence thesis by A4,Th3;
end;

theorem Th10:
seq = rseq & rseq is bounded implies
 inferior_realsequence seq = inferior_realsequence rseq &
 lim_inf seq=lim_inf rseq
proof
   assume A1: seq=rseq & rseq is bounded;
A2:now let x be set;
    assume x in NAT; then
    reconsider n = x as Element of NAT;
    consider Y1 being non empty Subset of ExtREAL such that
A3: Y1 = {seq.k where k is Element of NAT: n <= k} &
     (inferior_realsequence seq).n = inf Y1 by Def6;
    {rseq.k where k is Element of NAT: n <= k} is Subset of REAL
    proof
     now let x be set;
      assume x in {rseq.k where k is Element of NAT: n <= k}; then
      ex k be Element of NAT st x=rseq.k & n<= k;
      hence x in REAL;
     end;
     hence thesis by TARSKI:def 3;
    end; then
    reconsider Y2={rseq.k where k is Element of NAT: n <= k} as Subset of REAL;
    rseq is bounded_below by A1,SEQ_2:def 5; then
    Y2 is bounded_below by RINFSUP1:32; then
    inf Y1= inf Y2 by A1,A3,Th3;
    hence (inferior_realsequence seq).x = (inferior_realsequence rseq).x
     by A3,RINFSUP1:def 4;
   end;
   NAT = dom (inferior_realsequence seq) &
   NAT = dom (inferior_realsequence rseq) by FUNCT_2:def 1; then
A4:inferior_realsequence seq = inferior_realsequence rseq by A2,FUNCT_1:9;
   inferior_realsequence rseq is bounded by A1,RINFSUP1:58; then
   inferior_realsequence rseq is bounded_above by SEQ_2:def 5; then
   rng inferior_realsequence rseq is bounded_above by RINFSUP1:5;
   hence thesis by A4,Th1;
end;

theorem Th11:
seq is bounded implies seq is Real_Sequence
proof
   assume A1: seq is bounded; then
   seq is bounded_above by Def5; then
   rng seq is bounded_above by Def4; then
   consider UB being majorant of rng seq such that
A2: UB in REAL by SUPINF_1:def 11;
   seq is bounded_below by A1,Def5; then
   rng seq is bounded_below by Def3; then
   consider UL being minorant of rng seq such that
A3: UL in REAL by SUPINF_1:def 12;
A4:dom seq =NAT by FUNCT_2:def 1;
   now let x be set;
    assume x in NAT; then
A5: seq.x in rng seq by FUNCT_2:6;
    UB < +infty & -infty < UL by A2,A3,XXREAL_0:9,12; then
    seq.x <> +infty & seq.x <> -infty by A5,SUPINF_1:def 9,def 10; then
    seq.x is Real by EXTREAL1:1;
    hence seq.x in REAL;
   end;
   hence thesis by A4,FUNCT_2:5;
end;

theorem Th12:
seq = rseq implies (seq is bounded_above iff rseq is bounded_above)
proof
   assume A1: seq=rseq;
   hereby assume seq is bounded_above; then
    rng seq is bounded_above by Def4; then
    rng rseq is bounded_above by A1,Th2; then
    consider p be real number such that
A2:  for y be real number st y in rng rseq holds y <= p by SEQ_4:def 1;
    now let n be Element of NAT;
     rseq.n in rng rseq by FUNCT_2:6; then
     rseq.n <= p by A2; then
     0 + rseq.n < 1+ p by XREAL_1:10;
     hence rseq.n < p+1;
    end;
    hence rseq is bounded_above by SEQ_2:def 3;
   end;
   assume rseq is bounded_above; then
   consider q be real number such that
A3: for n be Element of NAT holds rseq.n < q by SEQ_2:def 3;
A4:q in REAL by XREAL_0:def 1;
   now let r be ext-real number;
    assume r in rng seq; then
    ex x be set st x in dom rseq & r=rseq.x by A1,FUNCT_1:def 5;
    hence r <= q by A3;
   end; then
   q is majorant of rng seq by SUPINF_1:def 9;
   hence rng seq is bounded_above by A4,SUPINF_1:def 11;
end;

theorem Th13:
seq = rseq implies (seq is bounded_below iff rseq is bounded_below)
proof
   assume A1: seq=rseq;
   hereby assume seq is bounded_below; then
    rng seq is bounded_below by Def3; then
    rng rseq is bounded_below by A1,Th4; then
    consider p be real number such that
A2:  for y be real number st y in rng rseq holds p <= y by SEQ_4:def 2;
    now let n be Element of NAT;
     rseq.n in rng rseq by FUNCT_2:6; then
     p <= rseq.n by A2; then
     p-1 < rseq.n - 0 by XREAL_1:17;
     hence p-1 <rseq.n;
    end;
    hence rseq is bounded_below by SEQ_2:def 4;
   end;
   assume rseq is bounded_below; then
   consider q be real number such that
A3: for n be Element of NAT holds q < rseq.n by SEQ_2:def 4;
A4:q in REAL by XREAL_0:def 1;
   now let r be ext-real number;
    assume r in rng seq; then
    ex x be set st x in dom rseq & r = rseq.x by A1,FUNCT_1:def 5;
    hence q<=r by A3;
   end; then
   q is minorant of rng seq by SUPINF_1:def 10;
   hence rng seq is bounded_below by A4,SUPINF_1:def 12;
end;

Lm3:
seq=rseq implies
  (rseq is non-increasing iff seq is non-increasing) &
  (rseq is non-decreasing iff seq is non-decreasing)
proof
   assume seq=rseq; then
A1:((for n,m be Element of NAT st m<=n holds seq.m<=seq.n)
    iff (for n,m be Element of NAT st m<=n holds rseq.m<=rseq.n)) &
   ((for n,m be Element of NAT st m<=n holds seq.n<=seq.m)
    iff (for n,m be Element of NAT st m<=n holds rseq.n<=rseq.m));
   (rseq is non-decreasing iff
    for n,m be Element of NAT st n<=m holds rseq.n<=rseq.m) &
   (rseq is non-increasing iff
    for n,m be Element of NAT st n<=m holds rseq.m<=rseq.n) by SEQM_3:12,14;
   hence thesis by A1,Th7;
end;

theorem Th14:
seq=rseq & rseq is convergent implies
  seq is convergent_to_finite_number & seq is convergent & lim seq = lim rseq
proof
   assume A1: seq = rseq & rseq is convergent;
A2:now let e be real number;
    assume 0 < e; then
    consider n be Element of NAT such that
A3:  for m be Element of NAT st n <= m holds abs(rseq.m-lim rseq) < e
      by A1,SEQ_2:def 7;
    set N=n;
    now let m be Nat;
     assume A4: N <= m;
     m is Element of NAT by ORDINAL1:def 13; then
A5:  abs(rseq.m-lim rseq) < e by A3,A4;
     rseq.m - lim rseq = seq.m - R_EAL(lim rseq) by A1,SUPINF_2:5;
     hence |. seq.m - R_EAL(lim rseq) .| < e by A5,EXTREAL2:49;
    end;
    hence ex N be Nat st
      for m be Nat st N <= m holds |. seq.m - R_EAL(lim rseq) .| < e;
   end; then
A6:seq is convergent_to_finite_number by MESFUNC5:def 8; then
   seq is convergent by MESFUNC5:def 11;
   hence thesis by A2,A6,MESFUNC5:def 12;
end;

theorem Th15:
seq=rseq & seq is convergent_to_finite_number implies
  rseq is convergent & lim seq = lim rseq
proof
   assume A1: seq=rseq & seq is convergent_to_finite_number; then
A2:seq is convergent by MESFUNC5:def 11;
   not (lim seq = +infty & seq is convergent_to_+infty) &
   not (lim seq = -infty & seq is convergent_to_-infty)
     by A1,MESFUNC5:56,57; then
   consider g be real number such that
A3: lim seq = g &
    (for p be real number st 0<p ex n be Nat st
      for m be Nat st n<=m holds |.seq.m-lim seq.| < p) &
    seq is convergent_to_finite_number by A2,MESFUNC5:def 12;
A4:for p be real number st 0<p
    ex n be Element of NAT st
     for m be Element of NAT st n<=m holds abs(rseq.m-g) < p
   proof
    let p be real number;
    assume 0 < p; then
    consider n be Nat such that
A5:for m be Nat st n <= m holds |.seq.m-lim seq .| < p by A3;
    reconsider N=n as Element of NAT by ORDINAL1:def 13;
    take N;
    hereby let m be Element of NAT;
     assume N <= m; then
A6:  |.seq.m-lim seq .| < p by A5;
     rseq.m is Real & g is Real by XREAL_0:def 1; then
A7:  rseq.m - g = seq.m - lim seq by A1,A3,SUPINF_2:5;
     rseq.m - g is Real by XREAL_0:def 1;
     hence abs(rseq.m - g) < p by A6,A7,EXTREAL2:49;
    end;
   end; then
   rseq is convergent by SEQ_2:def 6;
   hence thesis by A3,A4,SEQ_2:def 7;
end;

theorem Th16:
seq^\k is convergent_to_finite_number implies
  seq is convergent_to_finite_number &
  seq is convergent & lim seq = lim(seq^\k)
proof
   assume A1: seq^\k is convergent_to_finite_number;
   set seq0=seq^\k;
A2:seq0 is convergent by A1,MESFUNC5:def 11;
   not(lim seq0 = +infty & seq0 is convergent_to_+infty) &
   not(lim seq0 = -infty & seq0 is convergent_to_-infty)
     by A1,MESFUNC5:56,57; then
A3:ex g be real number st lim seq0 = g &
    (for p be real number st 0<p
      ex n be Nat st for m be Nat st n<=m holds |.seq0.m-lim seq0 .| < p) &
    seq0 is convergent_to_finite_number by A2,MESFUNC5:def 12; then
   consider g be real number such that
A4: lim seq0 = g;
A5: for p be real number st 0<p
    ex n be Nat st for m be Nat st n<=m holds |.seq.m-lim seq0 .| < p
   proof
    let p be real number;
    assume 0 < p; then
    consider n be Nat such that
A6:for m be Nat st n<=m holds |.seq0.m-lim seq0 .| < p by A3;
    take n1 = n+k;
    hereby let m be Nat;
     assume
A7:n1 <= m; then
A8:n+k-k <= m-k by XREAL_1:11;
     k <= n+k by NAT_1:11; then
     k <= m by A7,XXREAL_0:2; then
     reconsider mk = m-k as Element of NAT by INT_1:18;
     seq0.(m-k) = seq.(mk+k) by KURATO_2:def 2;
     hence |.seq.m -lim seq0 .| < p by A6,A8;
    end;
   end;
   lim seq0 = R_EAL g by A4;
   hence
A9:seq is convergent_to_finite_number by A5,MESFUNC5:def 8;
   hence seq is convergent by MESFUNC5:def 11;
   hence lim seq = lim seq0 by A4,A5,A9,MESFUNC5:def 12;
end;

theorem Th17:
seq^\k is convergent implies seq is convergent & lim seq = lim(seq^\k)
proof
   set seq0=seq^\k;
   assume A1: seq^\k is convergent;
   per cases by A1,MESFUNC5:def 11;
   suppose seq0 is convergent_to_finite_number;
    hence seq is convergent & lim seq = lim seq0 by Th16;
   end;
   suppose
A2: seq0 is convergent_to_+infty; then
A3: lim seq0 = +infty & seq0 is convergent_to_+infty by A1,MESFUNC5:def 12;
    for g be real number st 0 < g
     ex n be Nat st for m be Nat st n<=m holds g <= seq.m
    proof
     let g be real number;
     assume 0<g; then
     consider n be Nat such that
A4: for m be Nat st n<=m holds g <= seq0.m by A2,MESFUNC5:def 9;
     take n1=n+k;
     hereby let m be Nat;
      assume A5: n1 <= m; then
A6:n+k-k <= m-k by XREAL_1:11;
      k <= n+k by NAT_1:11; then
      k <=m by A5,XXREAL_0:2; then
      reconsider mk = m-k as Element of NAT by INT_1:18;
      seq0.(m-k) = seq.(mk+k) by KURATO_2:def 2;
      hence g <= seq.m by A4,A6;
     end;
    end; then
A7:seq is convergent_to_+infty by MESFUNC5:def 9;
    hence seq is convergent by MESFUNC5:def 11;
    hence lim seq = lim seq0 by A3,A7,MESFUNC5:def 12;
   end;
   suppose
A8: seq0 is convergent_to_-infty; then
A9: lim seq0=-infty & seq0 is convergent_to_-infty by A1,MESFUNC5:def 12;
    for g be real number st
     g<0 ex n be Nat st for m be Nat st n<=m holds seq.m <= g
    proof
     let g be real number;
     assume g < 0; then
     consider n be Nat such that
A10: for m be Nat st n<=m holds seq0.m <= g by A8,MESFUNC5:def 10;
     take n1=n+k;
     hereby let m be Nat;
      assume A11: n1 <= m; then
A12:n+k-k <= m-k by XREAL_1:11;
      k <= n+k by NAT_1:11; then
      k <= m by A11,XXREAL_0:2; then
      reconsider mk = m-k as Element of NAT by INT_1:18;
      seq0.(m-k) = seq.(mk+k) by KURATO_2:def 2;
      hence seq.m <= g by A10,A12;
     end;
    end; then
A13:seq is convergent_to_-infty by MESFUNC5:def 10;
    hence seq is convergent by MESFUNC5:def 11;
    hence lim seq = lim seq0 by A9,A13,MESFUNC5:def 12;
   end;
end;

theorem Th18:
lim_sup seq = lim_inf seq & lim_inf seq in REAL implies
  ex k st seq^\k is bounded
proof
   assume A1: lim_sup seq = lim_inf seq & lim_inf seq in REAL; then
   reconsider a=lim_inf seq as Real;
   set K1=a+1;
   set K2=a-1;
   ex k1 be Element of NAT st (superior_realsequence seq).k1 <= K1
   proof
    assume
A2:  not ex k1 be Element of NAT st (superior_realsequence seq).k1 <= K1;
    now let x be ext-real number;
     assume x in rng (superior_realsequence seq); then
     ex n be set st
      n in dom (superior_realsequence seq) &
      x = (superior_realsequence seq).n by FUNCT_1:def 5;
     hence K1 <= x by A2;
    end; then
    K1 is minorant of rng superior_realsequence seq by SUPINF_1:def 10; then
    a+1 <= a by A1,SUPINF_1:def 17;
    hence contradiction by XREAL_1:31;
   end; then
   consider k1 be Element of NAT such that
A3: (superior_realsequence seq).k1 <= K1;
   ex k2 be Element of NAT st K2 <= (inferior_realsequence seq).k2
   proof
    assume
A4:  not (ex k2 be Element of NAT st K2 <= (inferior_realsequence seq).k2);
    now let x be ext-real number;
     assume x in rng (inferior_realsequence seq); then
     ex n be set st
      n in dom (inferior_realsequence seq) &
      x = (inferior_realsequence seq).n by FUNCT_1:def 5;
     hence x <= K2 by A4;
    end; then
    K2 is majorant of rng inferior_realsequence seq by SUPINF_1:def 9; then
    a <= a-1 by SUPINF_1:def 16; then
    a+0 < a-1+1 by XREAL_1:10;
    hence contradiction;
   end; then
   consider k2 be Element of NAT such that
A5: K2 <= (inferior_realsequence seq).k2;
   take k=max(k1,k2);
   k1<=k & k2<=k by XXREAL_0:25; then
   (superior_realsequence seq).k <= (superior_realsequence seq).k1 &
   (inferior_realsequence seq).k2 <= (inferior_realsequence seq).k
     by Th7; then
A6:(superior_realsequence seq).k <= K1 &
   K2<=(inferior_realsequence seq).k by A3,A5,XXREAL_0:2;
A7:for n be Element of NAT st k <=n holds seq.n <= K1 & K2 <= seq.n
   proof
    let n be Element of NAT;
    assume k <= n; then
    (superior_realsequence seq).n <= (superior_realsequence seq).k &
    (inferior_realsequence seq).k <= (inferior_realsequence seq).n
      by Th7; then
A8: (superior_realsequence seq).n <= K1 &
    K2<= (inferior_realsequence seq).n by A6,XXREAL_0:2;
    seq.n <= (superior_realsequence seq).n &
    (inferior_realsequence seq).n <= seq.n by Th8;
    hence seq.n <=K1 & K2 <= seq.n by A8,XXREAL_0:2;
   end;
   now let x be ext-real number;
    assume x in rng (seq^\k); then
    consider n be set such that
A9:  n in dom (seq^\k) & x=(seq^\k).n by FUNCT_1:def 5;
    reconsider n as Element of NAT by A9;
A10: x=seq.(n+k) by A9,KURATO_2:def 2;
    k <= n+k by NAT_1:11;
    hence x <= K1 by A7,A10;
   end; then
   K1 is majorant of rng (seq^\k) by SUPINF_1:def 9;
   hence rng (seq^\k) is bounded_above by SUPINF_1:def 11;
   now let x be ext-real number;
    assume x in rng(seq^\k); then
    consider n be set such that
A11:  n in dom(seq^\k) & x = (seq^\k).n by FUNCT_1:def 5;
    reconsider n as Element of NAT by A11;
A12: x=seq.(n+k) by A11,KURATO_2:def 2;
    k <= n+k by NAT_1:11;
    hence K2 <= x by A7,A12;
   end; then
   K2 is minorant of rng (seq^\k) by SUPINF_1:def 10;
   hence rng (seq^\k) is bounded_below by SUPINF_1:def 12;
end;

theorem Th19:
seq is convergent_to_finite_number implies ex k st seq^\k is bounded
proof
   assume A1: seq is convergent_to_finite_number; then
A2:seq is convergent by MESFUNC5:def 11;
   not (lim seq = +infty & seq is convergent_to_+infty) &
   not (lim seq = -infty & seq is convergent_to_-infty)
    by A1,MESFUNC5:56,57; then
A3: ex g be real number st
    lim seq =g &
    (for p be real number st 0<p ex n be Nat st for m be Nat st n<=m
     holds |.seq.m-lim seq .| < p) &
    seq is convergent_to_finite_number by A2,MESFUNC5:def 12; then
   consider g be real number such that
A4: lim seq = g;
   consider k be Nat such that
A5: for m be Nat st k<=m holds |.seq.m-lim seq .| < 1 by A3;
A6: g is Real by XREAL_0:def 1; then
A7: - lim seq = -g by A4,SUPINF_2:3;
   reconsider K = k as Element of NAT by ORDINAL1:def 13;
   take K;
   set UB = g+1;
   set UL = g-1;
   now let r be ext-real number;
    assume r in rng (seq^\K); then
    consider n be set such that
A8:  n in dom(seq^\K) & r=(seq^\K).n by FUNCT_1:def 5;
    reconsider n as Element of NAT by A8;
    k <= n+k by NAT_1:11; then
    |.seq.(n+k)-lim seq .| <= 1. by A5; then
    seq.(n+k)-lim seq <= 1. by EXTREAL2:60; then
    seq.(n+k)+-lim seq +lim seq <= 1. +lim seq by A4,SUPINF_2:14; then
    seq.(n+k)+(-lim seq +lim seq) <= 1. +lim seq
     by A4,A7,EXTREAL1:8; then
    seq.(n+k)+0. <= 1. +lim seq by EXTREAL1:9; then
    seq.(n+k) <= 1. +lim seq by SUPINF_2:18; then
    seq.(n+k) <= UB by A4,A6,SUPINF_2:1;
    hence r <= UB by A8,KURATO_2:def 2;
   end; then
A9:UB is majorant of rng(seq^\K) by SUPINF_1:def 9;
   UB in REAL by XREAL_0:def 1;
   hence rng (seq^\K) is bounded_above by A9,SUPINF_1:def 11;
   now let r be ext-real number;
    assume r in rng (seq^\K); then
    consider n be set such that
A10:  n in dom (seq^\K) & r=(seq^\K).n by FUNCT_1:def 5;
    reconsider n as Element of NAT by A10;
    k <= n+k by NAT_1:11; then
    |.seq.(n+k)-lim seq .| < 1 by A5;  then
    -1. <=seq.(n+k)-lim seq by EXTREAL2:60; then
    -1. +lim seq <= seq.(n+k)+-lim seq +lim seq by A4,SUPINF_2:14; then
    -1. +lim seq <= seq.(n+k)+(-lim seq +lim seq) by A4,A7,EXTREAL1:8; then
A11: -1. +lim seq <= seq.(n+k)+0. by EXTREAL1:9;
    -1.= -1 by SUPINF_2:3; then
    -1+ g = -1.+ lim seq by A4,A6,SUPINF_2:1; then
    UL <= seq.(n+k) by A11,SUPINF_2:18;
    hence UL <= r by A10,KURATO_2:def 2;
   end; then
A12:UL is minorant of rng (seq^\K) by SUPINF_1:def 10;
   UL in REAL by XREAL_0:def 1;
   hence rng (seq^\K) is bounded_below by A12,SUPINF_1:def 12;
end;

theorem Th20:
seq is convergent_to_finite_number implies
  seq^\k is convergent_to_finite_number &
  seq^\k is convergent & lim seq = lim(seq^\k)
proof
   set seq0=seq^\k;
   assume A1: seq is convergent_to_finite_number; then
A2:seq is convergent by MESFUNC5:def 11;
   not (lim seq = +infty & seq is convergent_to_+infty) &
   not (lim seq = -infty & seq is convergent_to_-infty)
     by A1,MESFUNC5:56,57; then
A3:ex g be real number st
    lim seq = g &
    (for p be real number st 0<p
      ex n be Nat st
       for m be Nat st n<=m holds |.seq.m-lim seq .| < p ) &
    seq is convergent_to_finite_number by A2,MESFUNC5:def 12; then
   consider g be real number such that
A4: lim seq = g;
A5: for p be real number st 0<p
    ex n be Nat st for m be Nat st n<=m holds |.seq0.m-lim seq .| < p
   proof
    let p be real number;
    assume 0<p; then
    consider n be Nat such that
A6:for m be Nat st n<=m holds |.seq.m-lim seq .| < p by A3;
    take n;
    hereby let m be Nat such that
A7:n <= m;
     m <= m+k by NAT_1:11; then
     n <= m+k by A7,XXREAL_0:2; then
A8: |.seq.(m+k) -lim seq .| < p by A6;
     m is Element of NAT by ORDINAL1:def 13;
     hence |.seq0.m -lim seq .| < p by A8,KURATO_2:def 2;
    end;
   end;
   lim seq = R_EAL g by A4;
   hence
A9:seq0 is convergent_to_finite_number by A5,MESFUNC5:def 8;
   hence seq0 is convergent by MESFUNC5:def 11;
   hence lim seq0 = lim seq by A4,A5,A9,MESFUNC5:def 12;
end;

theorem
seq is convergent implies seq^\k is convergent & lim seq = lim(seq^\k)
proof
   set seq0 = seq^\k;
   assume A1: seq is convergent;
   per cases by A1,MESFUNC5:def 11;
   suppose seq is convergent_to_finite_number;
    hence seq0 is convergent & lim seq0 = lim seq by Th20;
   end;
   suppose
A2: seq is convergent_to_+infty; then
A3: lim seq = +infty & seq is convergent_to_+infty by A1,MESFUNC5:def 12;
    for g be real number st 0 < g
     ex n be Nat st for m be Nat st n<=m holds g <= seq0.m
    proof
     let g be real number;
     assume 0<g; then
     consider n be Nat such that
A4: for m be Nat st n <= m holds g <= seq.m by A2,MESFUNC5:def 9;
     take n;
     hereby let m be Nat such that
A5: n <= m;
      m <= m+k by NAT_1:11; then
      n <= m+k by A5,XXREAL_0:2; then
A6:  g <=seq.(m+k) by A4;
      m is Element of NAT by ORDINAL1:def 13;
      hence g <= seq0.m by A6,KURATO_2:def 2;
     end;
    end; then
A7:seq0 is convergent_to_+infty by MESFUNC5:def 9;
    hence seq0 is convergent by MESFUNC5:def 11;
    hence lim seq0 = lim seq by A3,A7,MESFUNC5:def 12;
   end;
   suppose
A8: seq is convergent_to_-infty; then
A9: lim seq = -infty & seq is convergent_to_-infty by A1,MESFUNC5:def 12;
    for g be real number st g < 0
     ex n be Nat st for m be Nat st n <= m holds seq0.m <= g
    proof
     let g be real number;
     assume g < 0; then
     consider n be Nat such that
A10: for m be Nat st n <= m holds seq.m <= g by A8,MESFUNC5:def 10;
     take n;
     hereby let m be Nat such that
A11: n <= m;
      m <= m+k by NAT_1:11; then
      n <= m+k by A11,XXREAL_0:2; then
A12:  seq.(m+k) <= g by A10;
      m is Element of NAT by ORDINAL1:def 13;
      hence seq0.m <= g by A12,KURATO_2:def 2;
     end;
    end; then
A13:seq0 is convergent_to_-infty by MESFUNC5:def 10;
    hence seq0 is convergent by MESFUNC5:def 11;
    hence lim seq0 = lim seq by A9,A13,MESFUNC5:def 12;
   end;
end;

theorem
  (seq is bounded_above implies seq^\k is bounded_above) &
  (seq is bounded_below implies seq^\k is bounded_below)
proof
   hereby assume seq is bounded_above; then
    rng seq is bounded_above by Def4; then
    consider UB being majorant of rng seq such that
A1:  UB in REAL by SUPINF_1:def 11;
    now let r be ext-real number;
     assume r in rng (seq^\k); then
     consider n be set such that
A2:  n in dom (seq^\k) & r = (seq^\k).n by FUNCT_1:def 5;
     reconsider n as Element of NAT by A2;
     seq.(n+k) in rng seq by FUNCT_2:6; then
     seq.(n+k) <= UB by SUPINF_1:def 9;
     hence r <= UB by A2,KURATO_2:def 2;
    end; then
    UB is majorant of rng (seq^\k) by SUPINF_1:def 9; then
    rng (seq^\k) is bounded_above by A1,SUPINF_1:def 11;
    hence seq^\k is bounded_above by Def4;
   end;
   hereby assume seq is bounded_below; then
    rng seq is bounded_below by Def3; then
    consider UB being minorant of rng seq such that
A3:  UB in REAL by SUPINF_1:def 12;
    now let r be ext-real number;
     assume r in rng (seq^\k); then
     consider n be set such that
A4:  n in dom (seq^\k) & r=(seq^\k).n by FUNCT_1:def 5;
     reconsider n as Element of NAT by A4;
     seq.(n+k) in rng seq by FUNCT_2:6; then
     UB <= seq.(n+k) by SUPINF_1:def 10;
     hence UB <= r by A4,KURATO_2:def 2;
    end; then
    UB is minorant of rng (seq^\k) by SUPINF_1:def 10; then
    rng (seq^\k) is bounded_below by A3,SUPINF_1:def 12;
    hence seq^\k is bounded_below by Def3;
   end;
end;

theorem Th23:
inf seq <= seq.n & seq.n <= sup seq
proof
A1:seq.n in rng seq by FUNCT_2:6;
   inf rng seq is minorant of rng seq &
   sup rng seq is majorant of rng seq by SUPINF_1:def 16,def 17;
   hence thesis by A1,SUPINF_1:def 9,def 10;
end;

theorem Th24:
inf seq <= sup seq
proof
   inf seq <= seq.0 & seq.0 <= sup seq by Th23;
   hence thesis by XXREAL_0:2;
end;

theorem Th25:
seq is non-increasing implies
  seq^\k is non-increasing & inf seq = inf(seq^\k)
proof
   assume A1: seq is non-increasing;
   set seq0 = seq^\k;
   now let n,m be Element of NAT;
    assume m<=n; then
    k+m <= k+n by XREAL_1:8; then
    seq.(k+n) <=seq.(k+m) by A1,Th7; then
    seq0.n <= seq.(k+m) by KURATO_2:def 2;
    hence seq0.n <= seq0.m by KURATO_2:def 2;
   end;
   hence seq^\k is non-increasing by Th7;
   now let y be ext-real number;
    assume y in rng seq0; then
    consider n be set such that
    A2: n in dom seq0 & y=seq0.n by FUNCT_1:def 5;
    reconsider n as Element of NAT by A2;
    seq0.n= seq.(n+k) by KURATO_2:def 2; then
    inf seq <= seq0.n by Th23;
    hence inf rng seq <= y by A2;
   end; then
   inf rng seq is minorant of rng seq0 by SUPINF_1:def 10; then
A3:inf rng seq <= inf rng seq0 by SUPINF_1:def 17;
   now let y be ext-real number;
    assume y in rng seq;then
    consider n be set such that
    A4: n in dom seq & y=seq.n by FUNCT_1:def 5;
    reconsider n as Element of NAT by A4;
    n <= n+k by NAT_1:11; then
    A5: seq.(n+k) <= seq.n by A1,Th7;
    seq0.n = seq.(n+k) by KURATO_2:def 2; then
    inf seq0 <= seq.(n+k) by Th23;
    hence inf rng seq0 <= y by A4,A5,XXREAL_0:2;
   end; then
   inf rng seq0 is minorant of rng seq by SUPINF_1:def 10; then
   inf rng seq0 <= inf rng seq by SUPINF_1:def 17;
   hence thesis by A3,XXREAL_0:1;
end;

theorem Th26:
seq is non-decreasing implies
  seq^\k is non-decreasing & sup seq = sup(seq^\k)
proof
   assume A1: seq is non-decreasing;
   set seq0 = seq^\k;
   now let n,m be Element of NAT;
    assume m <= n; then
    k+m <= k+n by XREAL_1:8; then
    seq.(k+m) <=seq.(k+n) by A1,Th7; then
    seq0.m <= seq.(k+n) by KURATO_2:def 2;
    hence seq0.m <= seq0.n by KURATO_2:def 2;
   end;
   hence seq^\k is non-decreasing by Th7;
   now let y be ext-real number;
    assume y in rng seq0; then
    consider n be set such that
A2:  n in dom seq0 & y=seq0.n by FUNCT_1:def 5;
    reconsider n as Element of NAT by A2;
    seq0.n= seq.(n+k) by KURATO_2:def 2; then
    seq0.n <=sup seq by Th23;
    hence y <= sup rng seq by A2;
   end; then
   sup rng seq is  majorant of rng seq0 by SUPINF_1:def 9; then
A3:sup rng seq0 <= sup rng seq by SUPINF_1:def 16;
   now let y be ext-real number;
    assume y in rng seq; then
    consider n be set such that
A4:  n in dom seq & y=seq.n by FUNCT_1:def 5;
    reconsider n as Element of NAT by A4;
    n <=n+k by NAT_1:11; then
A5: seq.n <= seq.(n+k) by A1,Th7;
    seq0.n= seq.(n+k) by KURATO_2:def 2; then
    seq.(n+k) <= sup seq0 by Th23;
    hence y <= sup rng seq0 by A4,A5,XXREAL_0:2;
   end; then
   sup rng seq0 is majorant of rng seq by SUPINF_1:def 9; then
   sup rng seq <= sup rng seq0 by SUPINF_1:def 16;
   hence thesis by A3,XXREAL_0:1;
end;

theorem
(superior_realsequence seq).n = sup(seq^\n) &
(inferior_realsequence seq).n = inf(seq^\n)
proof
   set rseq=seq^\n;
A1:ex Y being non empty Subset of ExtREAL st
    Y = {seq.k where k is Element of NAT: n <= k} &
    (superior_realsequence seq).n = sup Y by Def7;
A2:ex Z being non empty Subset of ExtREAL st
    Z = {seq.k where k is Element of NAT: n <= k} &
    (inferior_realsequence seq).n = inf Z by Def6;
   {seq.k where k is Element of NAT: n <= k} = rng rseq
   proof
    now let x be set;
     assume x in {seq.k where k is Element of NAT: n <= k}; then
     consider k be Element of NAT such that
A3:  x=seq.k & n <= k;
     reconsider k1=k-n as Element of NAT by A3,INT_1:18;
     x = seq.(n+k1) by A3; then
     x = rseq.k1 by KURATO_2:def 2;
     hence x in rng rseq by FUNCT_2:6;
    end; then
A4: {seq.k where k is Element of NAT: n <= k} c= rng rseq by TARSKI:def 3;
    now let x be set;
     assume x in rng rseq; then
     consider z be set such that
A5:  z in dom rseq & x=rseq.z by FUNCT_1:def 5;
     reconsider k0=z as Element of NAT by A5;
A6: x = seq.(n+k0) by A5,KURATO_2:def 2;
     n <=n+k0 by NAT_1:11;
     hence x in {seq.k where k is Element of NAT: n <= k} by A6;
    end; then
    rng rseq c= { seq.k where k is Element of NAT: n <= k} by TARSKI:def 3;
    hence thesis by A4,XBOOLE_0:def 10;
   end;
   hence thesis by A1,A2;
end;

theorem Th28:
for seq be ExtREAL_sequence,j be Element of NAT holds
 superior_realsequence (seq^\j) = (superior_realsequence seq)^\j &
 lim_sup (seq^\j) = lim_sup seq
proof
   let seq be ExtREAL_sequence,j be Element of NAT;
   set rseq=seq^\j;
   now let n be Element of NAT;
A1:ex Y2 being non empty Subset of ExtREAL st
     Y2 = {rseq.k where k is Element of NAT: n <= k} &
     (superior_realsequence rseq).n = sup Y2 by Def7;
A2:ex Y3 being non empty Subset of ExtREAL st
     Y3 = { seq.k where k is Element of NAT: (j+n) <= k} &
     (superior_realsequence seq).(j+n) = sup Y3 by Def7;
    now let x be set;
     assume x in {rseq.k where k is Element of NAT: n <= k}; then
     consider k be Element of NAT such that
A3:  x=rseq.k & n <= k;
     x = seq.(j+k) & n <= k by A3,KURATO_2:def 2;
     hence x in { seq.(j+k1) where k1 is Element of NAT: n <= k1};
    end; then
A4: {rseq.k where k is Element of NAT: n <= k}
     c= { seq.(j+k) where k is Element of NAT: n <= k} by TARSKI:def 3;
    now let x be set;
     assume x in {seq.(j+k) where k is Element of NAT: n <= k}; then
     consider k be Element of NAT such that
A5: x=seq.(j+k) & n <= k;
     x = rseq.k & n <= k by A5,KURATO_2:def 2;
     hence x in { rseq.k1 where k1 is Element of NAT: n<= k1};
    end; then
    {seq.(j+k) where k is Element of NAT: n <= k}
     c= {rseq.k where k is Element of NAT: n <= k} by TARSKI:def 3; then
A6: {rseq.k where k is Element of NAT: n <= k}
     = {seq.(j+k) where k is Element of NAT: n <= k} by A4,XBOOLE_0:def 10;
    now let x be set;
     assume x in {seq.(j+k) where k is Element of NAT: n <= k}; then
     consider k be Element of NAT such that
A7:  x=seq.(j+k) & n <= k;
     x=seq.(j+k) & j+n <= j+k by A7,XREAL_1:8;
     hence x in { seq.k1 where k1 is Element of NAT: j+n <= k1};
    end; then
A8: {seq.(j+k) where k is Element of NAT: n <= k}
     c= {seq.k where k is Element of NAT: j+n <= k} by TARSKI:def 3;
    now let x be set;
     assume x in {seq.k where k is Element of NAT: j+n <= k}; then
     consider k be Element of NAT such that
A9:  x=seq.k & j+n <= k;
     j <= j+n by NAT_1:11; then
     j <= k by A9,XXREAL_0:2; then
     reconsider k1=k-j as Element of NAT by INT_1:18;
     j+n - j <= k - j by A9,XREAL_1:11; then
     x = seq.(j+k1) & n <= k1 by A9;
     hence x in {seq.(j+k2) where k2 is Element of NAT: n<= k2};
    end; then
    {seq.k where k is Element of NAT: j+n <= k}
     c= {seq.(j+k) where k is Element of NAT: n <= k} by TARSKI:def 3; then
    {seq.(j+k) where k is Element of NAT: n <= k}
     = { seq.k where k is Element of NAT: (j+n) <= k} by A8,XBOOLE_0:def 10;
    hence
    (superior_realsequence rseq).n = ((superior_realsequence seq)^\j).n
      by A1,A2,A6,KURATO_2:def 2;
   end; then
   (superior_realsequence seq)^\j = superior_realsequence rseq by FUNCT_2:113;
   hence thesis by Th25;
end;

theorem Th29:
for seq be ExtREAL_sequence,j be Element of NAT holds
 inferior_realsequence (seq^\j) = (inferior_realsequence seq)^\j &
 lim_inf (seq^\j) = lim_inf seq
proof
   let seq be ExtREAL_sequence,j be Element of NAT;
   set rseq=seq^\j;
   now let n be Element of NAT;
A1:ex Y2 being non empty Subset of ExtREAL st
     Y2 = {rseq.k where k is Element of NAT: n <= k} &
     (inferior_realsequence rseq).n = inf Y2 by Def6;
A2:ex Y3 being non empty Subset of ExtREAL st
     Y3 = { seq.k where k is Element of NAT: (j+n) <= k} &
     (inferior_realsequence seq).(j+n) = inf Y3 by Def6;
    now let x be set;
     assume x in {rseq.k where k is Element of NAT: n <= k}; then
     consider k be Element of NAT such that
A3:  x=rseq.k & n <= k;
     x = seq.(j+k) & n <= k by A3,KURATO_2:def 2;
     hence x in {seq.(j+k1) where k1 is Element of NAT: n <= k1};
    end; then
A4: {rseq.k where k is Element of NAT: n <= k}
     c= {seq.(j+k) where k is Element of NAT: n <= k} by TARSKI:def 3;
    now let x be set;
     assume x in {seq.(j+k) where k is Element of NAT: n <= k}; then
     consider k be Element of NAT such that
A5:  x=seq.(j+k) & n <= k;
     x = rseq.k & n <= k by A5,KURATO_2:def 2;
     hence x in { rseq.k1 where k1 is Element of NAT: n<= k1};
    end; then
    {seq.(j+k) where k is Element of NAT: n <= k}
     c= {rseq.k where k is Element of NAT: n <= k} by TARSKI:def 3; then
A6: {rseq.k where k is Element of NAT: n <= k}
     = {seq.(j+k) where k is Element of NAT: n <= k} by A4,XBOOLE_0:def 10;
    now let x be set;
     assume x in {seq.(j+k) where k is Element of NAT: n <= k}; then
     consider k be Element of NAT such that
A7:  x=seq.(j+k) & n <= k;
     x = seq.(j+k) & j+n <= j+k by A7,XREAL_1:8;
     hence x in { seq.k1 where k1 is Element of NAT: j+n <= k1};
    end; then
A8: {seq.(j+k) where k is Element of NAT: n <= k}
     c= {seq.k where k is Element of NAT: j+n <= k} by TARSKI:def 3;
    now let x be set;
     assume x in {seq.k where k is Element of NAT: j+n <= k}; then
     consider k be Element of NAT such that
A9:  x=seq.k & j+n <= k;
     j <= j+n by NAT_1:11; then
     j <= k by A9,XXREAL_0:2; then
     reconsider k1=k-j as Element of NAT by INT_1:18;
     j+n - j <=k - j by A9,XREAL_1:11; then
     x=seq.(j+k1) & n <= k1 by A9;
     hence x in { seq.(j+k2) where k2 is Element of NAT: n<= k2};
    end; then
    {seq.k where k is Element of NAT: j+n <= k} c=
    {seq.(j+k) where k is Element of NAT: n <= k} by TARSKI:def 3; then
    {seq.(j+k) where k is Element of NAT: n <= k}
     = {seq.k where k is Element of NAT: (j+n) <= k} by A8,XBOOLE_0:def 10;
    hence
    (inferior_realsequence rseq).n = ((inferior_realsequence seq)^\j).n
      by A1,A2,A6,KURATO_2:def 2;
   end; then
   (inferior_realsequence seq)^\j = inferior_realsequence rseq by FUNCT_2:113;
   hence thesis by Th26;
end;

theorem Th30:
for seq be ExtREAL_sequence, k be Element of NAT st
 seq is non-increasing & -infty < seq.k & seq.k < +infty holds
  seq^\k is bounded_above & sup(seq^\k) = seq.k
proof
   let seq be ExtREAL_sequence, k be Element of NAT;
   assume A1: seq is non-increasing & -infty < seq.k & seq.k < +infty;
   set seq0=seq^\k;
   now let y be ext-real number;
    assume y in rng seq0; then
    consider n be set such that
A2:  n in dom seq0 & y=seq0.n by FUNCT_1:def 5;
    reconsider n as Element of NAT by A2;
    k <= n+k by NAT_1:11; then
    seq.(n+k) <= seq.k by A1,Th7;
    hence y <= seq.k by A2,KURATO_2:def 2;
   end; then
A3:seq.k is majorant of rng seq0 by SUPINF_1:def 9;
   seq.k in REAL by A1,XXREAL_0:14; then
A4:rng seq0 is bounded_above by A3,SUPINF_1:def 11;
   seq0.0= seq.(0+k) by KURATO_2:def 2; then
   seq.k in rng seq0 by FUNCT_2:6;
   hence thesis by A3,A4,Def4,SUPINF_1:80;
end;

theorem Th31:
for seq be ExtREAL_sequence, k be Element of NAT st
 seq is non-decreasing & -infty < seq.k & seq.k < +infty holds
  seq^\k is bounded_below & inf (seq^\k) = seq.k
proof
   let seq be ExtREAL_sequence, k be Element of NAT;
   assume A1: seq is non-decreasing & -infty < seq.k & seq.k < +infty;
   set seq0=seq^\k;
   now let y be ext-real number;
    assume y in rng seq0; then
    consider n be set such that
A2:  n in dom seq0 & y=seq0.n by FUNCT_1:def 5;
    reconsider n as Element of NAT by A2;
    k <= n+k by NAT_1:11; then
    seq.k <= seq.(n+k) by A1,Th7;
    hence seq.k <= y by A2,KURATO_2:def 2;
   end; then
A3:seq.k is minorant of rng seq0 by SUPINF_1:def 10;
   seq.k in REAL by A1,XXREAL_0:14; then
A4:rng seq0 is bounded_below by A3,SUPINF_1:def 12;
   seq0.0 = seq.(0+k) by KURATO_2:def 2; then
   seq.k in rng seq0 by FUNCT_2:6;
   hence thesis by A3,A4,Def3,SUPINF_1:81;
end;

Lm4:
for seq be ExtREAL_sequence st seq is bounded & seq is non-increasing holds
 seq is convergent_to_finite_number & seq is convergent & lim seq = inf seq
proof
   let seq be ExtREAL_sequence;
   assume A1: seq is bounded & seq is non-increasing; then
   reconsider rseq=seq as Real_Sequence by Th11;
A2:seq is bounded_below by A1,Def5; then
A3:rseq is bounded_below by Th13;
A4:rseq is non-increasing by A1,Lm3; then
A5:rseq is convergent by A3,SEQ_4:52; then
   lim rseq = lim_sup rseq by RINFSUP1:91; then
   lim rseq = inf rseq by A4,RINFSUP1:72; then
A6:lim seq = inf rseq by A5,Th14;
   rng seq is bounded_below by A2,Def3;
   hence thesis by A5,A6,Th4,Th14;
end;

Lm5:
for seq be ExtREAL_sequence st seq is bounded & seq is non-decreasing holds
 seq is convergent_to_finite_number & seq is convergent & lim seq = sup seq
proof
   let seq be ExtREAL_sequence;
   assume A1: seq is bounded & seq is non-decreasing; then
   reconsider rseq = seq as Real_Sequence by Th11;
A2:seq is bounded_above by A1,Def5; then
A3:rseq is bounded_above by Th12;
A4:rseq is non-decreasing by A1,Lm3; then
A5:rseq is convergent by A3,SEQ_4:51; then
   lim rseq = lim_inf rseq by RINFSUP1:91; then
   lim rseq = sup rseq by A4,RINFSUP1:66; then
A6:lim seq = sup rseq by A5,Th14;
   rng seq is bounded_above by A2,Def4;
   hence thesis by A5,A6,Th2,Th14;
end;

theorem Th32:
for seq be ExtREAL_sequence st
 (for n be Element of NAT holds +infty <= seq.n) holds
   seq is convergent_to_+infty
proof
   let seq be ExtREAL_sequence;
   assume A1: for n be Element of NAT holds +infty <= seq.n;
   now let g be real number;
    assume 0 < g;
    now let m be Nat;
     assume 0 <= m;
A2: g <= +infty by XXREAL_0:3;
     m is Element of NAT by ORDINAL1:def 13; then
     +infty <= seq.m by A1;
     hence g <= seq.m by A2,XXREAL_0:2;
    end;
    hence ex n be Nat st for m be Nat st n <= m holds g <= seq.m;
   end;
   hence thesis by MESFUNC5:def 9;
end;

theorem Th33:
for seq be ExtREAL_sequence st
 (for n be Element of NAT holds seq.n <= -infty) holds
   seq is convergent_to_-infty
proof
   let seq be ExtREAL_sequence;
   assume A1: for n be Element of NAT holds seq.n <= -infty;
   now let g be real number;
    assume g < 0;
    now let m be Nat;
     assume 0 <= m;
A2: -infty <= g by XXREAL_0:5;
     m is Element of NAT by ORDINAL1:def 13; then
     seq.m <= -infty by A1;
     hence seq.m <= g by A2,XXREAL_0:2;
    end;
    hence ex n be Nat st for m be Nat st n <=m  holds seq.m <= g;
   end;
   hence thesis by MESFUNC5:def 10;
end;

theorem Th34:
for seq be ExtREAL_sequence st seq is non-increasing & -infty = inf seq holds
 seq is convergent_to_-infty & lim seq = -infty
proof
   let seq be ExtREAL_sequence;
   assume A1: seq is non-increasing & -infty = inf seq;
A2:seq is convergent_to_-infty
   proof
    assume not seq is convergent_to_-infty; then
    consider g be real number such that
A3: g < 0 & for n be Nat ex m be Nat st n<=m & g < seq.m by MESFUNC5:def 10;
    for y be ext-real number st y in rng seq  holds g <= y
    proof
     let y be ext-real number;
     assume y in rng seq; then
     consider n be set such that
A4: n in NAT & y=seq.n by FUNCT_2:17;
     reconsider n as Element of NAT by A4;
     consider m be Nat such that
A5: n<=m & g < seq.m by A3;
     m is Element of NAT by ORDINAL1:def 13; then
     seq.m <= seq.n by A1,A5,Th7;
     hence g <= y by A4,A5,XXREAL_0:2;
    end; then
    g is minorant of rng seq by SUPINF_1:def 10; then
A6:g <= inf rng seq by SUPINF_1:def 17;
    g in REAL by XREAL_0:def 1;
    hence contradiction by A1,A6,XXREAL_0:12;
   end; then
   seq is convergent by MESFUNC5:def 11;
   hence thesis by A2,MESFUNC5:def 12;
end;

theorem Th35:
for seq be ExtREAL_sequence st
 seq is non-decreasing & +infty = sup seq holds
  seq is convergent_to_+infty & lim seq = +infty
proof
   let seq be ExtREAL_sequence;
   assume A1: seq is non-decreasing & +infty = sup seq;
A2:seq is convergent_to_+infty
   proof
    assume not (seq is convergent_to_+infty); then
    consider g be real number such that
A3: 0 < g & for n be Nat ex m be Nat st n<=m & seq.m < g by MESFUNC5:def 9;
    for y be ext-real number st y in rng seq holds y <= g
    proof
     let y be ext-real number;
     assume y in rng seq; then
     consider n be set such that
A4: n in NAT & y=seq.n by FUNCT_2:17;
     reconsider n as Element of NAT by A4;
     consider m be Nat such that
A5: n<=m & seq.m < g by A3;
     m is Element of NAT by ORDINAL1:def 13; then
     seq.n <= seq.m by A1,A5,Th7;
     hence y <= g by A4,A5,XXREAL_0:2;
    end; then
    g is majorant of rng seq by SUPINF_1:def 9; then
A6:sup rng seq <= g by SUPINF_1:def 16;
    g in REAL by XREAL_0:def 1;
    hence contradiction by A1,A6,XXREAL_0:9;
   end; then
   seq is convergent by MESFUNC5:def 11;
   hence thesis by A2,MESFUNC5:def 12;
end;

theorem Th36:
for seq be ExtREAL_sequence st seq is non-increasing holds
  seq is convergent & lim seq = inf seq
proof
   let seq be ExtREAL_sequence;
   assume A1: seq is non-increasing;
   per cases;
   suppose A2: for n be Element of NAT holds +infty <= seq.n; then
A3: seq is convergent_to_+infty by Th32;
    hence A4: seq is convergent by MESFUNC5:def 11;
A5: for n be Element of NAT holds seq.n =+infty
    proof
     let n be Element of NAT;
     +infty <= seq.n by A2;
     hence thesis by XXREAL_0:4;
    end;
    rng seq = {+infty}
    proof
     now let y be set;
      assume y in rng seq; then
      ex x be set st x in dom seq & seq.x=y by FUNCT_1:def 5; then
      y = +infty by A5;
      hence y in {+infty} by TARSKI:def 1;
     end; then
A6:  rng seq c={+infty} by TARSKI:def 3;
     now let y be set;
      assume y in {+infty}; then
A7:   y=+infty by TARSKI:def 1;
      now assume A8: not y in rng seq;
       now let n be Element of NAT;
        n in NAT; then
        n in dom seq by FUNCT_2:def 1; then
        seq.n<>+infty by A7,A8,FUNCT_1:def 5;
        hence contradiction by A5;
       end;
       hence contradiction;
      end;
      hence y in rng seq;
     end; then
     {+infty} c= rng seq by TARSKI:def 3;
     hence thesis by A6,XBOOLE_0:def 10;
    end; then
    inf seq = +infty by SUPINF_1:86;
    hence lim seq = inf seq by A3,A4,MESFUNC5:def 12;
   end;
   suppose not (for n be Element of NAT holds +infty <= seq.n); then
    consider k be Element of NAT such that
A9:  seq.k < +infty;
    per cases;
    suppose A10: -infty <> inf seq;
     set seq0=seq^\k;
A11: inf seq = inf seq0 & seq0 is non-increasing by A1,Th25;
A12:  inf seq <= seq.k by Th23;
     -infty < inf seq by A10,XXREAL_0:6; then
     -infty < seq.k by A12,XXREAL_0:2; then
A13:  seq0 is bounded_above by A1,A9,Th30; then
A14: rng seq0 is bounded_above by Def4;
     now assume A15: rng seq0 ={-infty};
A16:  -infty is minorant of rng seq0 by SUPINF_1:36;
      -infty in rng seq0 by A15,TARSKI:def 1;
      hence contradiction by A10,A11,A16,SUPINF_1:81;
     end; then
     sup rng seq0 in REAL by A14,SUPINF_1:83; then
A17:  sup rng seq0 < +infty by XXREAL_0:9;
     inf seq0 <= sup seq0 by Th24; then
A18:  inf rng seq0 in REAL by A10,A11,A17,XXREAL_0:14;
     inf rng seq0 is minorant of rng seq0 by SUPINF_1:def 17; then
     rng seq0 is bounded_below by A18,SUPINF_1:def 12; then
     seq0 is bounded_below by Def3; then
     seq0 is bounded by A13,Def5; then
     seq0 is convergent & lim seq0 = inf seq0 by A11,Lm4;
     hence seq is convergent & lim seq = inf seq by A11,Th17;
    end;
    suppose A19: -infty = inf seq; then
     seq is convergent_to_-infty & lim seq = -infty by A1,Th34;
     hence seq is convergent by MESFUNC5:def 11;
     thus lim seq = inf seq by A1,A19,Th34;
    end;
   end;
end;

theorem Th37:
for seq be ExtREAL_sequence st seq is non-decreasing holds
  seq is convergent & lim seq = sup seq
proof
   let seq be ExtREAL_sequence;
   assume A1: seq is non-decreasing;
   per cases;
   suppose A2: for n be Element of NAT holds seq.n <= -infty;
A3: seq is convergent_to_-infty by A2,Th33;
    hence A4: seq is convergent by MESFUNC5:def 11;
A5: for n be Element of NAT holds seq.n = -infty
    proof
     let n be Element of NAT;
     seq.n <= -infty by A2;
     hence thesis by XXREAL_0:6;
    end;
    rng seq = {-infty}
    proof
     now let y be set;
      assume y in rng seq; then
      ex x be set st x in dom seq & seq.x=y by FUNCT_1:def 5; then
      y = -infty by A5;
      hence y in {-infty} by TARSKI:def 1;
     end; then
A6:  rng seq c={-infty} by TARSKI:def 3;
     now let y be set; assume y in {-infty}; then
A7:   y = -infty by TARSKI:def 1;
      now assume A8: not y in rng seq;
       now let n be Element of NAT;
        n in NAT; then
        n in dom seq by FUNCT_2:def 1; then
        seq.n <> -infty by A7,A8,FUNCT_1:def 5;
        hence contradiction by A5;
       end;
       hence contradiction;
      end;
      hence y in rng seq;
     end; then
     {-infty} c= rng seq by TARSKI:def 3;
     hence thesis by A6,XBOOLE_0:def 10;
    end; then
    sup seq = -infty by SUPINF_1:85;
    hence lim seq = sup seq by A3,A4,MESFUNC5:def 12;
   end;
   suppose A9: not (for n be Element of NAT holds seq.n <= -infty);
    consider k be Element of NAT such that
A10:  -infty < seq.k by A9;
    per cases;
    suppose A11: +infty <> sup seq;
     set seq0=seq^\k;
A12: sup seq = sup seq0 by A1,Th26;
A13:seq0 is non-decreasing by A1,Th26;
A14:  seq.k <= sup seq by Th23;
     sup seq < +infty by A11,XXREAL_0:4; then
     seq.k < +infty by A14,XXREAL_0:2; then
A15:  seq0 is bounded_below by A1,A10,Th31; then
A16: rng seq0 is bounded_below by Def3;
     now assume rng seq0 ={+infty}; then
A17:  +infty in rng seq0  by TARSKI:def 1;
      +infty is majorant of rng seq0 by SUPINF_1:33;
      hence contradiction by A11,A12,A17,SUPINF_1:80;
     end; then
     inf rng seq0 in REAL by A16,SUPINF_1:84; then
A18:  -infty < inf rng seq0 by XXREAL_0:12;
     inf seq0 <= sup seq0 by Th24; then
A19:  sup rng seq0 in REAL by A11,A12,A18,XXREAL_0:14;
     sup rng seq0 is majorant of rng seq0 by SUPINF_1:def 16; then
     rng seq0 is bounded_above by A19,SUPINF_1:def 11; then
     seq0 is bounded_above by Def4; then
     seq0 is bounded by A15,Def5; then
     seq0 is convergent & lim seq0 = sup seq0 by A13,Lm5;
     hence seq is convergent & lim seq = sup seq by A12,Th17;
    end;
    suppose A20: +infty = sup seq; then
     seq is convergent_to_+infty & lim seq = +infty by A1,Th35;
     hence seq is convergent by MESFUNC5:def 11;
     thus lim seq = sup seq by A1,A20,Th35;
    end;
   end;
end;

theorem Th38:
for seq1,seq2 be ExtREAL_sequence st
 seq1 is convergent & seq2 is convergent &
 (for n be Element of NAT holds seq1.n <=seq2.n) holds
  lim seq1 <= lim seq2
proof
   let seq1,seq2 be ExtREAL_sequence;
   assume A1: seq1 is convergent & seq2 is convergent &
              (for n be Element of NAT holds seq1.n <=seq2.n);
   per cases by A1,MESFUNC5:def 11;
   suppose A2: seq1 is convergent_to_finite_number;
A3:not seq2 is convergent_to_-infty
    proof
     assume A4: seq2 is convergent_to_-infty;
     now let g be real number;
      assume g < 0; then
      consider n be Nat such that
 A5:  for m be Nat st n<=m holds seq2.m <= g by A4,MESFUNC5:def 10;
      now let m be Nat;
       assume n <= m; then
A6:   seq2.m <= g by A5;
       m is Element of NAT by ORDINAL1:def 13; then
       seq1.m <= seq2.m by A1;
       hence seq1.m <= g by A6,XXREAL_0:2;
      end;
      hence ex n be Nat st for m be Nat st n<=m holds seq1.m <= g;
     end; then
     seq1 is convergent_to_-infty by MESFUNC5:def 10;
     hence contradiction by A2,MESFUNC5:57;
    end;
    per cases by A1,A3,MESFUNC5:def 11;
    suppose A7: seq2 is convergent_to_finite_number;
     consider k1 be Element of NAT such that
A8:seq1^\k1 is bounded by A2,Th19;
     consider k2 be Element of NAT such that
A9:seq2^\k2 is bounded by A7,Th19;
     reconsider k = max(k1,k2) as Element of NAT;
     seq1^\k1 is bounded_below &
     seq1^\k1 is bounded_above by A8,Def5; then
A10:  rng(seq1^\k1) is bounded_below & rng(seq1^\k1) is bounded_above
      by Def3,Def4;
     consider LB being minorant of rng (seq1^\k1) such that
A11:   LB in REAL by A10,SUPINF_1:def 12;
     consider UB being majorant of rng (seq1^\k1) such that
A12:   UB in REAL by A10,SUPINF_1:def 11;
A13: dom(seq1^\k1) = NAT by FUNCT_2:def 1;
     now let y be set;
      assume y in rng (seq1^\k); then
      consider n be set such that
A14:    n in dom (seq1^\k) & (seq1^\k).n=y by FUNCT_1:def 5;
      reconsider n as Element of NAT by A14;
A15:   y=seq1.(k+n) by A14,KURATO_2:def 2;
      k1 <= k by XXREAL_0:25; then
      reconsider k2=k-k1 as Element of NAT by INT_1:18;
      y = seq1.(k1 +(k2+n)) by A15; then
      y = (seq1^\k1).(k2+n) by KURATO_2:def 2;
      hence y in rng(seq1^\k1) by A13,FUNCT_1:def 5;
     end; then
     rng (seq1^\k) c=rng (seq1^\k1) by TARSKI:def 3; then
     LB is minorant of rng (seq1^\k) &
     UB is majorant of rng (seq1^\k) by SUPINF_1:34,39; then
     rng (seq1^\k)is bounded_below &
     rng (seq1^\k)is bounded_above by A11,A12,SUPINF_1:def 11,def 12; then
     seq1^\k is bounded_below & seq1^\k is bounded_above
      by Def3,Def4; then
     seq1^\k is bounded by Def5; then
     reconsider rseq1=seq1^\k as Real_Sequence by Th11;
     seq2^\k2 is bounded_below & seq2^\k2 is bounded_above
       by A9,Def5; then
A16:  rng(seq2^\k2)is bounded_below & rng(seq2^\k2) is bounded_above
       by Def3,Def4;
     consider LB being minorant of rng (seq2^\k2) such that
A17:   LB in REAL by A16,SUPINF_1:def 12;
     consider UB being majorant of rng (seq2^\k2) such that
A18:   UB in REAL by A16,SUPINF_1:def 11;
A19: dom(seq2^\k2) = NAT by FUNCT_2:def 1;
     now let y be set;
      assume y in rng (seq2^\k); then
      consider n be set such that
A20:    n in dom (seq2^\k) & (seq2^\k).n=y by FUNCT_1:def 5;
      reconsider n as Element of NAT by A20;
A21:   y=seq2.(k+n) by A20,KURATO_2:def 2;
      k2 <= k by XXREAL_0:25; then
      reconsider k3=k-k2 as Element of NAT by INT_1:18;
      y=seq2.(k2 +(k3+n)) by A21; then
      y=(seq2^\k2).(k3+n) by KURATO_2:def 2;
      hence y in rng(seq2^\k2) by A19,FUNCT_1:def 5;
     end; then
     rng (seq2^\k) c= rng (seq2^\k2) by TARSKI:def 3; then
     LB is minorant of rng (seq2^\k) &
     UB is majorant of rng (seq2^\k) by SUPINF_1:34,39; then
     rng (seq2^\k)is bounded_below & rng (seq2^\k ) is bounded_above
      by A17,A18,SUPINF_1:def 11,def 12; then
     seq2^\k is bounded_below & seq2^\k is bounded_above
       by Def3,Def4; then
     seq2^\k is bounded by Def5; then
     reconsider rseq2=seq2^\k as Real_Sequence by Th11;
     seq1^\k is convergent_to_finite_number &
     seq2^\k is convergent_to_finite_number by A2,A7,Th20; then
A22:rseq1 is convergent & rseq2 is convergent &
      lim(seq1^\k) = lim rseq1 & lim(seq2^\k) = lim rseq2 by Th15;
A23:  for n be Element of NAT holds rseq1.n <= rseq2.n
     proof
      let n be Element of NAT;
      (seq1^\k).n =seq1.(k+n) & (seq2^\k).n =seq2.(k+n) by KURATO_2:def 2;
      hence rseq1.n <= rseq2.n by A1;
     end;
     lim seq1 = lim(seq1^\k) & lim seq2 = lim(seq2^\k) by A2,A7,Th20;
     hence lim seq1 <= lim seq2 by A22,A23,SEQ_2:32;
    end;
    suppose A24:seq2 is convergent_to_+infty; then
     seq2 is convergent by MESFUNC5:def 11; then
     lim seq2=+infty by A24,MESFUNC5:def 12;
     hence lim seq1 <= lim seq2 by XXREAL_0:3;
    end;
   end;
   suppose A25:seq1 is convergent_to_+infty;
    now let g be real number;
     assume 0 < g; then
     consider n be Nat such that
A26:  for m be Nat st n<=m holds g <= seq1.m by A25,MESFUNC5:def 9;
     now let m be Nat;
      assume n<=m; then
A27:  g <= seq1.m by A26;
      m is Element of NAT by ORDINAL1:def 13; then
      seq1.m <= seq2.m  by A1;
      hence g <= seq2.m by A27,XXREAL_0:2;
     end;
     hence ex n be Nat st for m be Nat st n<=m holds g <= seq2.m;
    end; then
A28: seq2 is convergent_to_+infty by MESFUNC5:def 9; then
    seq2 is convergent by MESFUNC5:def 11; then
    lim seq2=+infty by A28,MESFUNC5:def 12;
    hence lim seq1 <= lim seq2 by XXREAL_0:3;
   end;
   suppose A29:seq1 is convergent_to_-infty; then
    seq1 is convergent by MESFUNC5:def 11; then
    lim seq1 = -infty by A29,MESFUNC5:def 12;
    hence lim seq1 <= lim seq2 by XXREAL_0:5;
   end;
end;

theorem
for seq be ExtREAL_sequence holds lim_inf seq <= lim_sup seq
proof
   let seq be ExtREAL_sequence;
A1:superior_realsequence seq is convergent &
   lim superior_realsequence seq = lim_sup seq &
   inferior_realsequence seq is convergent &
   lim inferior_realsequence seq = lim_inf seq by Th36,Th37;
   now let n be Element of NAT;
    (inferior_realsequence seq).n <=seq.n &
    seq.n <= (superior_realsequence seq).n by Th8;
    hence (inferior_realsequence seq).n <= (superior_realsequence seq).n
      by XXREAL_0:2;
   end;
   hence thesis by A1,Th38;
end;

Lm6:
for seq be ExtREAL_sequence st
 lim_inf seq = lim_sup seq & lim_inf seq = +infty holds
  seq is convergent & lim seq = lim_inf seq & lim seq = lim_sup seq
proof
   let seq be ExtREAL_sequence;
   assume A1: lim_inf seq = lim_sup seq & lim_inf seq = +infty; then
A2:inferior_realsequence seq is convergent_to_+infty &
   lim inferior_realsequence seq = +infty by Th35;
   now let g be real number;
    assume 0 < g; then
    consider n be Nat such that
A3: for m be Nat st n<=m holds g <= (inferior_realsequence seq).m
       by A2,MESFUNC5:def 9;
    now let m be Nat;
     assume n<=m; then
A4: g <= (inferior_realsequence seq).m by A3;
     m is Element of NAT by ORDINAL1:def 13; then
     (inferior_realsequence seq).m <= seq.m by Th8; hence
     g <= seq.m by A4,XXREAL_0:2;
    end;
    hence ex n be Nat st for m be Nat st n<=m holds g <= seq.m;
   end; then
A5:seq is convergent_to_+infty by MESFUNC5:def 9; then
   seq is convergent by MESFUNC5:def 11;
   hence thesis by A1,A5,MESFUNC5:def 12;
end;

Lm7:
for seq be ExtREAL_sequence st
 lim_inf seq = lim_sup seq & lim_inf seq = -infty holds
  seq is convergent & lim seq = lim_inf seq & lim seq = lim_sup seq
proof
   let seq be ExtREAL_sequence;
   assume A1: lim_inf seq = lim_sup seq & lim_inf seq = -infty; then
A2:superior_realsequence seq is convergent_to_-infty &
   lim superior_realsequence seq = -infty by Th34;
   now let g be real number;
    assume g < 0; then
    consider n be Nat such that
A3: for m be Nat st n<=m holds (superior_realsequence seq).m <= g
       by A2,MESFUNC5:def 10;
    now let m be Nat;
     assume n<=m; then
A4: (superior_realsequence seq).m <= g by A3;
     m is Element of NAT by ORDINAL1:def 13; then
     seq.m <=(superior_realsequence seq).m by Th8;
     hence seq.m <= g by A4,XXREAL_0:2;
    end;
    hence ex n be Nat st for m be Nat st n<=m holds seq.m <= g;
   end; then
A5:seq is convergent_to_-infty by MESFUNC5:def 10; then
   seq is convergent by MESFUNC5:def 11;
   hence thesis by A1,A5,MESFUNC5:def 12;
end;

Lm8:
for seq be ExtREAL_sequence st
 lim_inf seq = lim_sup seq & lim_inf seq in REAL holds
  seq is convergent & lim seq = lim_inf seq & lim seq = lim_sup seq
proof
   let seq be ExtREAL_sequence;
   assume A1: lim_inf seq = lim_sup seq & lim_inf seq in REAL;
   set a=lim_inf seq;
   consider k be Element of NAT such that
A2: seq^\k is bounded by A1,Th18;
   reconsider rseq0=seq^\k as Real_Sequence by A2,Th11;
   seq^\k is bounded_above & seq^\k is bounded_below by A2,Def5; then
   rseq0 is bounded_above & rseq0 is bounded_below by Th12,Th13; then
A3:rseq0 is bounded by SEQ_2:def 5; then
   lim_inf rseq0 = lim_inf (seq^\k) &
   lim_sup rseq0 = lim_sup (seq^\k) by Th9,Th10; then
A4:lim_inf rseq0 = lim_inf seq &
   lim_sup rseq0 = lim_sup seq by Th28,Th29; then
   rseq0 is convergent by A1,A3,RINFSUP1:90; then
   lim rseq0 = lim_sup seq & lim rseq0 = lim_inf seq &
   lim rseq0 = lim (seq^\k) & seq^\k is convergent by A4,Th14,RINFSUP1:91;
   hence thesis by Th17;
end;

theorem Th40:
for seq be ExtREAL_sequence holds
 seq is convergent iff lim_inf seq = lim_sup seq
proof
   let seq be ExtREAL_sequence;
  thus seq is convergent implies lim_inf seq = lim_sup seq
   proof
    assume A1: seq is convergent;
    per cases by A1,MESFUNC5:def 11;
    suppose A2: seq is convergent_to_finite_number; then
     consider k be Element of NAT such that
A3:  seq^\k is bounded by Th19;
     reconsider rseq0=seq^\k as Real_Sequence by A3,Th11;
     seq^\k is convergent_to_finite_number by A2,Th20; then
A4:rseq0 is convergent by Th15; then
     rseq0 is bounded by RINFSUP1:90; then
     lim_inf rseq0 = lim_inf (seq^\k) &
     lim_sup rseq0 = lim_sup (seq^\k) by Th9,Th10; then
     lim_inf rseq0 = lim_inf seq & lim_sup rseq0 = lim_sup seq
       by Th28,Th29;
     hence lim_inf seq = lim_sup seq by A4,RINFSUP1:90;
    end;
    suppose A5: seq is convergent_to_-infty;
     now let g be real number;
      assume g < 0; then
      consider n be Nat such that
A6:   for m be Nat st n<=m holds seq.m <= g by A5,MESFUNC5:def 10;
      now let m be Nat;
       assume n<=m; then
A7:   seq.m <= g by A6;
       m is Element of NAT by ORDINAL1:def 13; then
       (inferior_realsequence seq).m <= seq.m  by Th8;
       hence (inferior_realsequence seq).m <= g by A7,XXREAL_0:2;
      end;
      hence ex n be Nat st
             for m be Nat st n<=m holds (inferior_realsequence seq).m <= g;
     end; then
A8:  inferior_realsequence seq is convergent_to_-infty by MESFUNC5:def 10; then
     inferior_realsequence seq is convergent by MESFUNC5:def 11; then
     lim inferior_realsequence seq = -infty by A8,MESFUNC5:def 12; then
A9:  lim_inf seq =-infty by Th37;
A10: superior_realsequence seq is convergent_to_-infty
     proof
      set iseq=superior_realsequence seq;
      assume not iseq is convergent_to_-infty; then
      consider g be real number such that
A11:   g < 0 &
       for n be Nat ex m be Nat st n<=m & g < iseq.m by MESFUNC5:def 10;
      consider N be Nat such that
A12:   for m be Nat st N<=m holds seq.m <= g by A5,A11,MESFUNC5:def 10;
      now let m be Nat;
       m is Element of NAT by ORDINAL1:def 13; then
       consider Y being non empty Subset of ExtREAL such that
A13:   Y = {seq.k where k is Element of NAT: m <= k} &
        (superior_realsequence seq).m = sup Y by Def7;
       assume A14: N <= m;
       now let x be ext-real number;
        assume x in Y; then
        consider k be Element of NAT such that
A15:      x=seq.k & m <= k by A13;
        N <= k by A14,A15,XXREAL_0:2;
        hence x <= g by A12,A15;
       end; then
       g is majorant of Y by SUPINF_1:def 9;
       hence iseq.m <= g by A13,SUPINF_1:def 16;
      end;
      hence contradiction by A11;
     end; then
     superior_realsequence seq is convergent by MESFUNC5:def 11; then
     lim superior_realsequence seq = -infty by A10,MESFUNC5:def 12;
     hence lim_inf seq = lim_sup seq by A9,Th36;
    end;
    suppose A16: seq is convergent_to_+infty;
     now let g be real number;
      assume 0 < g; then
      consider n be Nat such that
A17:   for m be Nat st n<=m holds g <= seq.m by A16,MESFUNC5:def 9;
      now let m be Nat;
       assume n <= m; then
A18:   g <= seq.m by A17;
       m is Element of NAT by ORDINAL1:def 13; then
       seq.m <= (superior_realsequence seq).m by Th8;
       hence g <= (superior_realsequence seq).m by A18,XXREAL_0:2;
      end;
      hence ex n be Nat st for m be Nat st n<=m holds
              g <= (superior_realsequence seq).m;
     end; then
A19:  superior_realsequence seq is convergent_to_+infty by MESFUNC5:def 9; then
     superior_realsequence seq is convergent by MESFUNC5:def 11; then
     lim superior_realsequence seq = +infty by A19,MESFUNC5:def 12; then
A20:  lim_sup seq =+infty by Th36;
A21: inferior_realsequence seq is convergent_to_+infty
     proof
      set iseq=inferior_realsequence seq;
      assume not iseq is convergent_to_+infty; then
      consider g be real number such that
A22:   0 < g & for n be Nat ex m be Nat st n<=m & iseq.m < g
         by MESFUNC5:def 9;
      consider N be Nat such that
A23:   for m be Nat st N<=m holds g <= seq.m by A16,A22,MESFUNC5:def 9;
      now let m be Nat;
       assume A24: N <= m;
       m is Element of NAT by ORDINAL1:def 13; then
       consider Y being non empty Subset of ExtREAL such that
A25:   Y = {seq.k where k is Element of NAT: m <= k} &
        (inferior_realsequence seq).m = inf Y by Def6;
       now let x be ext-real number;
        assume x in Y; then
        consider k be Element of NAT such that
A26:      x = seq.k & m <= k by A25;
        N <= k by A24,A26,XXREAL_0:2;
        hence g <= x by A23,A26;
       end; then
       g is minorant of Y by SUPINF_1:def 10;
       hence g<=iseq.m by A25,SUPINF_1:def 17;
      end;
      hence contradiction by A22;
     end; then
     inferior_realsequence seq is convergent by MESFUNC5:def 11; then
     lim inferior_realsequence seq = +infty by A21,MESFUNC5:def 12;
     hence lim_inf seq = lim_sup seq by A20,Th37;
    end;
   end;
    assume A27: lim_inf seq = lim_sup seq;
    set a=lim_inf seq;
    per cases by XXREAL_0:14;
    suppose a in REAL;
      hence thesis by A27,Lm8;
    end;
    suppose a = +infty;
      hence thesis by A27,Lm6;
    end;
    suppose a = -infty;
      hence thesis by A27,Lm7;
    end;
end;

theorem
for seq be ExtREAL_sequence st seq is convergent holds
  lim seq = lim_inf seq & lim seq = lim_sup seq
proof
   let seq be ExtREAL_sequence;
   assume seq is convergent; then
A1:lim_inf seq = lim_sup seq by Th40;
   set a = lim_inf seq;
   per cases by XXREAL_0:14;
   suppose a in REAL;
     hence thesis by A1,Lm8;
   end;
   suppose a = +infty;
     hence thesis by A1,Lm6;
   end;
   suppose a = -infty;
     hence thesis by A1,Lm7;
   end;
end;
