:: Tarski's Classes and Ranks
::  by Grzegorz Bancerek
::
:: Received March 23, 1990
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies FUNCT_1, TARSKI, ZFMISC_1, SETFAM_1, XBOOLE_0, CARD_1, SUBSET_1,
      ORDINAL1, ORDINAL2, RELAT_1, NUMBERS, NAT_1, ARYTM_3, FUNCT_2, CLASSES1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, SETFAM_1, RELAT_1, FUNCT_1,
      FUNCT_2, CARD_1, NUMBERS, NAT_1, ORDINAL1, MCART_1, ORDINAL2;
 constructors SETFAM_1, MCART_1, WELLORD2, ORDINAL2, NAT_1, CARD_1, FUNCT_2,
      RELSET_1;
 registrations XBOOLE_0, SUBSET_1, ORDINAL1, FUNCT_1, FUNCT_2, RELSET_1,
      RELAT_1;
 requirements NUMERALS, SUBSET, BOOLE;
 definitions ORDINAL1, TARSKI, WELLORD2, FUNCT_1, XBOOLE_0, RELAT_1;
 theorems TARSKI, SETFAM_1, ZFMISC_1, ORDINAL1, MCART_1, FUNCT_1, CARD_1,
      WELLORD2, ENUMSET1, XBOOLE_0, XBOOLE_1, ORDINAL2, RELAT_1, FUNCT_2;
 schemes ORDINAL1, ORDINAL2, TARSKI, PARTFUN1, NAT_1, XBOOLE_0, WELLORD2;

begin

reserve W,X,Y,Z for set,
  f,g for Function,
  a,x,y,z for set;

definition
  let B be set;
  attr B is subset-closed means
  :Def1:
  for X,Y holds X in B & Y c= X implies Y in B;
end;

definition
  let B be set;
  attr B is Tarski means
  :Def2:
  B is subset-closed &
  (for X holds X in B implies bool X in B) &
  for X holds X c= B implies X,B are_equipotent or X in B;
end;

definition
  let A,B be set;
  pred B is_Tarski-Class_of A means
  :Def3:
  A in B & B is Tarski;
end;

definition
  let A be set;
  func Tarski-Class A -> set means
  :Def4:
  it is_Tarski-Class_of A &
  for D being set st D is_Tarski-Class_of A holds it c= D;
  existence
  proof
    consider Big being set such that
A1: A in Big and
A2: for X,Y holds X in Big & Y c= X implies Y in Big and
    A3: (
 for X holds X in Big implies bool X in Big)& for X holds X c= Big implies X,
    Big are_equipotent or X in Big
    by ZFMISC_1:136;
A4: Big is subset-closed by A2,Def1;
A5: Big is Tarski by A3,A4,Def2;
    defpred P[set] means $1 is_Tarski-Class_of A;
    consider Classes being set such that
A6: X in Classes iff X in bool Big & P[X] from XBOOLE_0:sch 1;
    set IT = meet Classes;
    A7: Big
 in bool Big & Big is_Tarski-Class_of A by A1,A5,Def3,ZFMISC_1:def 1;
A8: Big in Classes by A6,A7;
A9: Classes <> {} by A6,A7;
A10: now
      let X;
      assume
A11:  X in Classes;
A12:  X is_Tarski-Class_of A by A6,A11;
      thus A in X by A12,Def3;
    end;
A13: A in IT by A9,A10,SETFAM_1:def 1;
    take IT;
    thus A in IT by A9,A10,SETFAM_1:def 1;
    thus
A14: X in IT & Y c= X implies Y in IT
    proof
      assume that
A15:  X in IT and
A16:  Y c= X;
A17:  now
        let Z;
        assume
A18:    Z in Classes;
A19:    Z is_Tarski-Class_of A by A6,A18;
A20:    Z is Tarski by A19,Def3;
A21:    Z is subset-closed by A20,Def2;
A22:    X in Z by A15,A18,SETFAM_1:def 1;
        thus Y in Z by A16,A21,A22,Def1;
      end;
      thus thesis by A9,A17,SETFAM_1:def 1;
    end;
    thus
A23: X in IT implies bool X in IT
    proof
      assume
A24:  X in IT;
A25:  now
        let Z;
        assume
A26:    Z in Classes;
A27:    Z is_Tarski-Class_of A by A6,A26;
A28:    Z is Tarski by A27,Def3;
A29:    X in Z by A24,A26,SETFAM_1:def 1;
        thus bool X in Z by A28,A29,Def2;
      end;
      thus thesis by A9,A25,SETFAM_1:def 1;
    end;
    thus
A30: X c= IT implies X,IT are_equipotent or X in IT
    proof
      assume that
A31:  X c= IT and
A32:  not X,IT are_equipotent;
A33:  now
        let Z;
        assume
A34:    Z in Classes;
A35:    Z is_Tarski-Class_of A by A6,A34;
A36:    Z is Tarski by A35,Def3;
A37:    IT c= Z by A34,SETFAM_1:4;
A38:    X c= Z by A31,A37,XBOOLE_1:1;
A39:    X,Z are_equipotent or X in Z by A36,A38,Def2;
        thus X in Z by A31,A32,A37,A39,CARD_1:44;
      end;
      thus thesis by A9,A33,SETFAM_1:def 1;
    end;
    let D be set;
    assume
A40: D is_Tarski-Class_of A;
A41: A in D by A40,Def3;
A42: D is Tarski by A40,Def3;
A43: D is subset-closed by A42,Def2;
A44: IT /\ D is_Tarski-Class_of A
    proof
      thus A in IT /\ D by A13,A41,XBOOLE_0:def 4;
      thus X in IT /\ D & Y c= X implies Y in IT /\ D
      proof
        assume that
A45:    X in IT /\ D and
A46:    Y c= X;
A47:    X in IT by A45,XBOOLE_0:def 4;
A48:    X in D by A45,XBOOLE_0:def 4;
A49:    Y in IT by A14,A46,A47;
A50:    Y in D by A43,A46,A48,Def1;
        thus thesis by A49,A50,XBOOLE_0:def 4;
      end;
      thus X in IT /\ D implies bool X in IT /\ D
      proof
        assume
A51:    X in IT /\ D;
A52:    X in IT by A51,XBOOLE_0:def 4;
A53:    X in D by A51,XBOOLE_0:def 4;
A54:    bool X in IT by A23,A52;
A55:    bool X in D by A42,A53,Def2;
        thus thesis by A54,A55,XBOOLE_0:def 4;
      end;
      let X such that
A56:  X c= IT /\ D and
A57:  not X,IT /\ D are_equipotent;
A58:  IT /\ D c= IT by XBOOLE_1:17;
A59:  IT /\ D c= D by XBOOLE_1:17;
A60:  not X,IT are_equipotent by A56,A57,A58,CARD_1:44;
      A61:  X c= D & not X,D are_equipotent by A56,A57,A59,CARD_1:44,XBOOLE_1:1
      ;
A62:  X in IT by A30,A56,A58,A60,XBOOLE_1:1;
A63:  X in D by A42,A61,Def2;
      thus thesis by A62,A63,XBOOLE_0:def 4;
    end;
A64: IT /\ D c= Big
    proof
      let x;
      assume
A65:  x in IT /\ D;
A66:  x in IT by A65,XBOOLE_0:def 4;
      thus thesis by A8,A66,SETFAM_1:def 1;
    end;
A67: IT /\ D in Classes by A6,A44,A64;
A68: IT c= IT /\ D by A67,SETFAM_1:4;
A69: IT /\ D c= D by XBOOLE_1:17;
    thus thesis by A68,A69,XBOOLE_1:1;
  end;
  uniqueness
  proof
    let D1,D2 be set such that
    A70: (
 D1 is_Tarski-Class_of A & for D being set st D is_Tarski-Class_of A holds
    D1 c= D )&( D2 is_Tarski-Class_of A & for D being set st D
    is_Tarski-Class_of A holds D2 c= D );
    thus D1 c= D2 & D2 c= D1 by A70;
  end;
end;

registration
  let A be set;
  cluster Tarski-Class A -> non empty;
  coherence
  proof
A1: Tarski-Class A is_Tarski-Class_of A by Def4;
    thus thesis by A1,Def3;
  end;
end;

canceled;

theorem
  W is Tarski iff W is subset-closed &
  (for X st X in W holds bool X in W) &
  for X st X c= W & card X in card W holds X in W
proof
  hereby
    assume
A1: W is Tarski;
    thus W is subset-closed & for X st X in W holds bool X in W by A1,Def2;
    let X;
    assume that
A2: X c= W and
A3: card X in card W;
A4: card X <> card W by A3;
A5: not X,W are_equipotent by A4,CARD_1:21;
    thus X in W by A1,A2,A5,Def2;
  end;
A6: now
    assume
A7: for X st X c= W & card X in card W holds X in W;
    let X;
    assume
A8: X c= W;
A9: card X c= card W & not card X in card W or X in W by A7,A8,CARD_1:27;
A10: card X = card W or X in W by A9,CARD_1:13;
    thus X,W are_equipotent or X in W by A10,CARD_1:21;
  end;
  thus thesis by A6,Def2;
end;

canceled 2;

theorem Th5:
  X in Tarski-Class X
proof
A1: Tarski-Class X is_Tarski-Class_of X by Def4;
  thus thesis by A1,Def3;
end;

theorem Th6:
  Y in Tarski-Class X & Z c= Y implies Z in Tarski-Class X
proof
A1: Tarski-Class X is_Tarski-Class_of X by Def4;
A2: Tarski-Class X is Tarski by A1,Def3;
A3: Tarski-Class X is subset-closed by A2,Def2;
  thus thesis by A3,Def1;
end;

theorem Th7:
  Y in Tarski-Class X implies bool Y in Tarski-Class X
proof
A1: Tarski-Class X is_Tarski-Class_of X by Def4;
A2: Tarski-Class X is Tarski by A1,Def3;
  thus thesis by A2,Def2;
end;

theorem Th8:
  Y c= Tarski-Class X implies
  Y,Tarski-Class X are_equipotent or Y in Tarski-Class X
proof
A1: Tarski-Class X is_Tarski-Class_of X by Def4;
A2: Tarski-Class X is Tarski by A1,Def3;
  thus thesis by A2,Def2;
end;

theorem
  Y c= Tarski-Class X & card Y in card Tarski-Class X implies
  Y in Tarski-Class X
proof
  assume that
A1: Y c= Tarski-Class X and
A2: card Y in card Tarski-Class X;
A3: card Y <> card Tarski-Class X by A2;
A4: not Y,Tarski-Class X are_equipotent by A3,CARD_1:21;
  thus thesis by A1,A4,Th8;
end;

reserve u,v for Element of Tarski-Class(X),
  A,B,C for Ordinal,
  L for T-Sequence;

definition
  let X,A;
  func Tarski-Class(X,A) means
  :Def5:
  ex L st it = last L & dom L = succ A & L.{} = { X } &
  (for C st succ C in succ A holds
  L.succ C = { u : ex v st v in L.C & u c= v } \/ { bool v : v in L.C } \/
  bool(L.C) /\ Tarski-Class X) &
  for C st C in succ A & C <> {} & C is limit_ordinal
  holds L.C = (union rng(L|C)) /\ Tarski-Class X;
  correctness
  proof
    deffunc C(Ordinal,set) = { u : ex v st v in $2 & u c= v } \/
    { bool v : v in $2 } \/ bool $2 /\ Tarski-Class X;
    deffunc D(Ordinal,T-Sequence) = (union rng $2) /\ Tarski-Class X;
    thus (ex x,L st x = last L & dom L = succ A & L.{} = {X} &
    (for C st succ C in succ A holds L.succ C = C(C,L.C)) &
    for C st C in succ A & C <> {} & C is limit_ordinal
    holds L.C = D(C,L|C) ) & for x1,x2 being set st
    (ex L st x1 = last L & dom L = succ A & L.{} = {X} &
    (for C st succ C in succ A holds L.succ C = C(C,L.C)) &
    for C st C in succ A & C <> {} & C is limit_ordinal
    holds L.C = D(C,L|C) ) &
    (ex L st x2 = last L & dom L = succ A & L.{} = {X} &
    (for C st succ C in succ A holds L.succ C = C(C,L.C)) &
    for C st C in succ A & C <> {} & C is limit_ordinal holds L.C = D(C,L|C) )
    holds x1 = x2 from ORDINAL2:sch 7;
  end;
end;

Lm1: now
  let X;
  deffunc F(Ordinal) = Tarski-Class(X,$1);
  deffunc C(Ordinal,set) = { u : ex v st v in $2 & u c= v } \/
  { bool v : v in $2 } \/ bool $2 /\ Tarski-Class X;
  deffunc D(Ordinal,T-Sequence) = (union rng $2) /\ Tarski-Class X;
A1: for A,x holds x = F(A) iff
  ex L st x = last L & dom L = succ A & L.{} = { X } &
  (for C st succ C in succ A holds L.succ C = C(C,L.C)) &
  for C st C in succ A & C <> {} & C is limit_ordinal
  holds L.C = D(C,L|C) by Def5;
  thus F({}) = { X } from ORDINAL2:sch 8(A1);
  thus for A holds F(succ A) = C(A,F(A)) from ORDINAL2:sch 9(A1);
  thus A <> {} & A is limit_ordinal & dom L = A &
  (for B st B in A holds L.B = Tarski-Class(X,B)) implies
  Tarski-Class(X,A) = (union rng L) /\ Tarski-Class X
  proof
    assume that
A2: A <> {} & A is limit_ordinal and
A3: dom L = A and
A4: for B st B in A holds L.B = F(B);
    thus F(A) = D(A,L) from ORDINAL2:sch 10(A1,A2,A3,A4);
  end;
end;

definition
  let X,A;
  redefine func Tarski-Class(X,A) -> Subset of Tarski-Class X;
  coherence
  proof
    defpred P[Ordinal] means Tarski-Class(X,$1) is Subset of Tarski-Class X;
A1: { X } c= Tarski-Class X
    proof
      let x;
      assume
A2:   x in { X };
A3:   x = X by A2,TARSKI:def 1;
      thus thesis by A3,Th5;
    end;
A4: P[{}] by A1,Lm1;
A5: P[B] implies P[succ B]
    proof
      assume
A6:   Tarski-Class(X,B) is Subset of Tarski-Class X;
      reconsider S = Tarski-Class(X,B) as Subset of Tarski-Class X by A6;
      set Y = Tarski-Class(X,succ B);
A7:   Y c= Tarski-Class X
      proof
        let x;
        assume
A8:     x in Y;
A9:     x in { u : ex v st v in S & u c= v } \/ { bool v : v in S } \/
        bool S /\ Tarski-Class X by A8,Lm1;
        A10:    x in { u : ex v st v in S & u c= v } \/ { bool v : v in S } or
        x in bool S /\ Tarski-Class X by A9,XBOOLE_0:def 3;
A11:    now
          assume
A12:      x in { u : ex v st v in S & u c= v };
A13:      ex u st x = u & ex v st v in S & u c= v by A12;
          thus thesis by A13;
        end;
A14:    now
          assume
A15:      x in { bool v : v in S };
A16:      ex v st x = bool v & v in S by A15;
          thus thesis by A16,Th7;
        end;
        thus thesis by A10,A11,A14,XBOOLE_0:def 3,def 4;
      end;
      thus thesis by A7;
    end;
A17: for B st B <> {} & B is limit_ordinal &
    for C st C in B holds P[C] holds P[B]
    proof
      let B such that
A18:  B <> {} & B is limit_ordinal and
      for C st C in B holds Tarski-Class(X,C) is Subset of Tarski-Class X;
      deffunc f(Ordinal) = Tarski-Class(X,$1);
      consider L such that
A19:  dom L = B & for C st C in B holds L.C = f(C) from ORDINAL2:sch 2;
A20:  Tarski-Class(X,B) = (union rng L) /\ Tarski-Class X by A18,A19,Lm1;
      thus thesis by A20,XBOOLE_1:17;
    end;
A21: for A holds P[A] from ORDINAL2:sch 1(A4,A5,A17);
    thus thesis by A21;
  end;
end;

theorem
  Tarski-Class(X,{}) = { X } by Lm1;

theorem
  Tarski-Class(X,succ A) = { u : ex v st v in Tarski-Class(X,A) & u c= v } \/
  { bool v : v in Tarski-Class(X,A) } \/
  bool Tarski-Class(X,A) /\ Tarski-Class X by Lm1;

theorem Th12:
  A <> {} & A is limit_ordinal implies
  Tarski-Class(X,A) = { u : ex B st B in A & u in Tarski-Class(X,B) }
proof
  assume
A1: A <> {} & A is limit_ordinal;
  deffunc f(Ordinal) = Tarski-Class(X,$1);
  consider L such that
A2: dom L = A & for B st B in A holds L.B = f(B) from ORDINAL2:sch 2;
A3: Tarski-Class(X,A) = (union rng L) /\ Tarski-Class X by A1,A2,Lm1;
  thus
  Tarski-Class(X,A) c= { u : ex B st B in A & u in Tarski-Class(X,B) }
  proof
    let x;
    assume
A4: x in Tarski-Class(X,A);
A5: x in union rng L by A3,A4,XBOOLE_0:def 4;
    consider Y such that
A6: x in Y and
A7: Y in rng L by A5,TARSKI:def 4;
    consider y such that
A8: y in dom L and
A9: Y = L.y by A7,FUNCT_1:def 5;
    reconsider y as Ordinal by A8;
A10: Y = Tarski-Class(X,y) by A2,A8,A9;
    thus thesis by A2,A6,A8,A10;
  end;
  let x;
  assume
A11: x in { u : ex B st B in A & u in Tarski-Class(X,B) };
  consider u such that
A12: x = u and
A13: ex B st B in A & u in Tarski-Class(X,B) by A11;
  consider B such that
A14: B in A and
A15: u in Tarski-Class(X,B) by A13;
A16: L.B = Tarski-Class(X,B) by A2,A14;
A17: Tarski-Class(X,B) in rng L by A2,A14,A16,FUNCT_1:def 5;
A18: u in union rng L by A15,A17,TARSKI:def 4;
  thus thesis by A3,A12,A18,XBOOLE_0:def 4;
end;

theorem Th13:
  Y in Tarski-Class(X,succ A) iff
  Y c= Tarski-Class(X,A) & Y in Tarski-Class X or
  ex Z st Z in Tarski-Class(X,A) & (Y c= Z or Y = bool Z)
proof
  set T1 = { u : ex v st v in Tarski-Class(X,A) & u c= v };
  set T2 = { bool v : v in Tarski-Class(X,A) };
  set T3 = bool Tarski-Class(X,A) /\ Tarski-Class X;
A1: Tarski-Class(X,succ A) = T1 \/ T2 \/ T3 by Lm1;
  thus Y in Tarski-Class(X,succ A) implies
  Y c= Tarski-Class(X,A) & Y in Tarski-Class X or
  ex Z st Z in Tarski-Class(X,A) & (Y c= Z or Y = bool Z)
  proof
    assume
A2: Y in Tarski-Class(X,succ A);
A3: Y in T1 \/ T2 or Y in T3 by A1,A2,XBOOLE_0:def 3;
A4: now
      assume
A5:   Y in T1;
A6:   ex u st Y = u & ex v st v in Tarski-Class(X,A) & u c= v by A5;
      thus ex Z st Z in Tarski-Class(X,A) & (Y c= Z or Y = bool Z)
      by A6;
    end;
A7: now
      assume
A8:   Y in T2;
A9:   ex v st Y = bool v & v in Tarski-Class(X,A) by A8;
      thus ex Z st Z in Tarski-Class(X,A) & (Y c= Z or Y = bool Z)
      by A9;
    end;
A10: now
      assume
A11:  Y in T3;
A12:  Y in bool Tarski-Class(X,A) by A11,XBOOLE_0:def 4;
      thus thesis by A11,A12,XBOOLE_0:def 4;
    end;
    thus thesis by A3,A4,A7,A10,XBOOLE_0:def 3;
  end;
  assume
A13: Y c= Tarski-Class(X,A) & Y in Tarski-Class X or
  ex Z st Z in Tarski-Class(X,A) & (Y c= Z or Y = bool Z);
A14: now
    assume
A15: Y c= Tarski-Class(X,A) & Y in Tarski-Class X;
A16: Y in T3 by A15,XBOOLE_0:def 4;
    thus thesis by A1,A16,XBOOLE_0:def 3;
  end;
A17: now
    given Z such that
A18: Z in Tarski-Class(X,A) and
A19: Y c= Z or Y = bool Z;
    reconsider Z as Element of Tarski-Class X by A18;
    reconsider y = Y as Element of Tarski-Class X by A18,A19,Th6,Th7;
A20: now
      assume
A21:  Y c= Z;
A22:  y in T1 by A18,A21;
A23:  Y in T1 \/ T2 by A22,XBOOLE_0:def 3;
      thus thesis by A1,A23,XBOOLE_0:def 3;
    end;
A24: now
      assume
A25:  Y = bool Z;
A26:  y in T2 by A18,A25;
A27:  Y in T1 \/ T2 by A26,XBOOLE_0:def 3;
      thus thesis by A1,A27,XBOOLE_0:def 3;
    end;
    thus thesis by A19,A20,A24;
  end;
  thus thesis by A13,A14,A17;
end;

theorem
  Y c= Z & Z in Tarski-Class(X,A) implies Y in Tarski-Class(X,succ A) by Th13;

theorem
  Y in Tarski-Class(X,A) implies bool Y in Tarski-Class(X,succ A) by Th13;

theorem Th16:
  A <> {} & A is limit_ordinal implies
  (x in Tarski-Class(X,A) iff ex B st B in A & x in Tarski-Class(X,B))
proof
  assume
A1: A <> {} & A is limit_ordinal;
A2: Tarski-Class(X,A) = { u : ex B st B in A & u in Tarski-Class(X,B) }
  by A1,Th12;
  thus
  x in Tarski-Class(X,A) implies ex B st B in A & x in Tarski-Class(X,B)
  proof
    assume
A3: x in Tarski-Class(X,A);
A4: ex u st x = u & ex B st B in A & u in Tarski-Class(X,B) by A2,A3;
    thus thesis by A4;
  end;
  given B such that
A5: B in A and
A6: x in Tarski-Class(X,B);
  reconsider u = x as Element of Tarski-Class X by A6;
A7: u in { v : ex B st B in A & v in Tarski-Class(X,B) } by A5,A6;
  thus thesis by A1,A7,Th12;
end;

theorem
  A <> {} & A is limit_ordinal & Y in Tarski-Class(X,A) &
  (Z c= Y or Z = bool Y) implies Z in Tarski-Class(X,A)
proof
  assume that
A1: A <> {} and
A2: A is limit_ordinal and
A3: Y in Tarski-Class(X,A);
  consider B such that
A4: B in A and
A5: Y in Tarski-Class(X,B) by A1,A2,A3,Th16;
A6: bool Y in Tarski-Class(X,succ B) by A5,Th13;
A7: Z c= Y implies Z in Tarski-Class(X,succ B) by A5,Th13;
A8: succ B in A by A2,A4,ORDINAL1:41;
  assume
A9: Z c= Y or Z = bool Y;
  thus thesis by A2,A6,A7,A8,A9,Th16;
end;

theorem Th18:
  Tarski-Class(X,A) c= Tarski-Class(X,succ A)
proof
  let x;
  assume
A1: x in Tarski-Class(X,A);
A2: x in { u : ex v st v in Tarski-Class(X,A) & u c= v } by A1;
A3: x in { u : ex v st v in Tarski-Class(X,A) & u c= v } \/
  { bool v : v in Tarski-Class(X,A) } by A2,XBOOLE_0:def 3;
  A4: Tarski-Class
(X,succ A) = { u : ex v st v in Tarski-Class(X,A) & u c= v } \/
  { bool v : v in Tarski-Class(X,A) } \/
  bool Tarski-Class(X,A) /\ Tarski-Class X by Lm1;
  thus thesis by A3,A4,XBOOLE_0:def 3;
end;

theorem Th19:
  A c= B implies Tarski-Class(X,A) c= Tarski-Class(X,B)
proof
  defpred OnP[Ordinal] means
  A c= $1 implies Tarski-Class(X,A) c= Tarski-Class(X,$1);
A1: for B st for C st C in B holds OnP[C] holds OnP[B]
  proof
    let B such that
A2: for C st C in B holds OnP[C] and
A3: A c= B;
    let x;
    assume
A4: x in Tarski-Class(X,A);
A5: now
      assume
A6:   A <> B;
A7:   A c< B by A3,A6,XBOOLE_0:def 8;
A8:   A in B by A7,ORDINAL1:21;
A9:   B <> {} by A3,A6;
A10:  now
        given C such that
A11:    B = succ C;
A12:    A c= C & C in B by A8,A11,ORDINAL1:34;
A13:    Tarski-Class(X,A) c= Tarski-Class(X,C) by A2,A12;
A14:    Tarski-Class(X,C) c= Tarski-Class(X,B) by A11,Th18;
A15:    Tarski-Class(X,A) c= Tarski-Class(X,B) by A13,A14,XBOOLE_1:1;
        thus thesis by A4,A15;
      end;
A16:  now
        assume
A17:    for C holds B <> succ C;
A18:    B is limit_ordinal by A17,ORDINAL1:42;
A19:    Tarski-Class(X,B) = { v : ex C st C in B & v in Tarski-Class(X,C)
        } by A9,A18,Th12;
        thus thesis by A4,A8,A19;
      end;
      thus thesis by A10,A16;
    end;
    thus thesis by A4,A5;
  end;
A20: for B holds OnP[B] from ORDINAL1:sch 2(A1);
  thus thesis by A20;
end;

theorem Th20:
  ex A st Tarski-Class(X,A) = Tarski-Class(X,succ A)
proof
  assume
A1: for A holds Tarski-Class(X,A) <> Tarski-Class(X,succ A);
  defpred P[set] means ex A st $1 in Tarski-Class(X,A);
  consider Z such that
A2: x in Z iff x in Tarski-Class X & P[x] from XBOOLE_0:sch 1;
  defpred P[set,set] means
  ex A st $2 = A & $1 in Tarski-Class(X,succ A) & not $1 in Tarski-Class(X,A);
A3: for x,y,z st P[x,y] & P[x,z] holds y = z
  proof
    let x,y,z;
    given A such that
A4: y = A and
A5: x in Tarski-Class(X,succ A) and
A6: not x in Tarski-Class(X,A);
    given B such that
A7: z = B and
A8: x in Tarski-Class(X,succ B) and
A9: not x in Tarski-Class(X,B);
    assume
A10: y <> z;
A11: A c= B or B c= A;
A12: A c< B or B c< A by A4,A7,A10,A11,XBOOLE_0:def 8;
A13: now
      assume
A14:  A c< B;
A15:  A in B by A14,ORDINAL1:21;
A16:  succ A c= B by A15,ORDINAL1:33;
A17:  Tarski-Class(X,succ A) c= Tarski-Class(X,B) by A16,Th19;
      thus contradiction by A5,A9,A17;
    end;
A18: B in A by A12,A13,ORDINAL1:21;
A19: succ B c= A by A18,ORDINAL1:33;
A20: Tarski-Class(X,succ B) c= Tarski-Class(X,A) by A19,Th19;
    thus contradiction by A6,A8,A20;
  end;
  consider Y such that
A21: x in Y iff ex y st y in Z & P[y,x] from TARSKI:sch 1(A3);
A22: now
    let A;
A23: Tarski-Class(X,A) <> Tarski-Class(X,succ A) by A1;
A24: Tarski-Class(X,A) c= Tarski-Class(X,succ A) by Th18;
    consider x such that
A25: not (x in Tarski-Class(X,A) iff x in Tarski-Class(X,succ A)) by A23,
    TARSKI:2;
A26: x in Z by A2,A25;
    thus A in Y by A21,A24,A25,A26;
  end;
  thus contradiction by A22,ORDINAL1:38;
end;

theorem Th21:
  Tarski-Class(X,A) = Tarski-Class(X,succ A) implies
  Tarski-Class(X,A) = Tarski-Class X
proof
  assume
A1: Tarski-Class(X,A) = Tarski-Class(X,succ A);
A2: {} c= A;
A3: Tarski-Class(X,{}) c= Tarski-Class(X,A) by A2,Th19;
A4: Tarski-Class(X,{}) = { X } & X in { X } by Lm1,TARSKI:def 1;
A5: Tarski-Class(X,A) is_Tarski-Class_of X
  proof
    thus X in Tarski-Class(X,A) by A3,A4;
A6: Tarski-Class(X,succ A) =
    { u : ex v st v in Tarski-Class(X,A) & u c= v } \/
    { bool v : v in Tarski-Class(X,A) } \/
    bool Tarski-Class(X,A) /\ Tarski-Class X by Lm1;
A7: Tarski-Class X is_Tarski-Class_of X by Def4;
A8: Tarski-Class X is Tarski by A7,Def3;
    thus Z in Tarski-Class(X,A) & Y c= Z implies Y in Tarski-Class(X,A)
    proof
      assume
A9:   Z in Tarski-Class(X,A) & Y c= Z;
A10:  Tarski-Class X is_Tarski-Class_of X by Def4;
A11:  Tarski-Class X is Tarski by A10,Def3;
A12:  Tarski-Class X is subset-closed by A11,Def2;
      reconsider y = Y as Element of Tarski-Class X by A9,A12,Def1;
A13:  ex v st v in Tarski-Class(X,A) & y c= v by A9;
A14:  Y in { u : ex v st v in Tarski-Class(X,A) & u c= v } by A13;
A15:  Y in { u : ex v st v in Tarski-Class(X,A) & u c= v } \/
      { bool v : v in Tarski-Class(X,A) } by A14,XBOOLE_0:def 3;
      thus thesis by A1,A6,A15,XBOOLE_0:def 3;
    end;
    thus Y in Tarski-Class(X,A) implies bool Y in Tarski-Class(X,A)
    proof
      assume
A16:  Y in Tarski-Class(X,A);
A17:  bool Y in { bool u : u in Tarski-Class(X,A) } by A16;
A18:  bool Y in { u : ex v st v in Tarski-Class(X,A) & u c= v } \/
      { bool v : v in Tarski-Class(X,A) } by A17,XBOOLE_0:def 3;
      thus thesis by A1,A6,A18,XBOOLE_0:def 3;
    end;
    let Y;
    assume that
A19: Y c= Tarski-Class(X,A) and
A20: not Y,Tarski-Class(X,A) are_equipotent;
A21: Y c= Tarski-Class X by A19,XBOOLE_1:1;
A22: Y,Tarski-Class X are_equipotent or Y in Tarski-Class X by A8,A21,Def2;
    thus thesis by A1,A19,A20,A22,Th13,CARD_1:44;
  end;
  thus Tarski-Class(X,A) c= Tarski-Class X &
  Tarski-Class X c= Tarski-Class(X,A) by A5,Def4;
end;

theorem Th22:
  ex A st Tarski-Class(X,A) = Tarski-Class X
proof
  consider A such that
A1: Tarski-Class(X,A) = Tarski-Class(X,succ A) by Th20;
  take A;
  thus thesis by A1,Th21;
end;

theorem
  ex A st Tarski-Class(X,A) = Tarski-Class X &
  for B st B in A holds Tarski-Class(X,B) <> Tarski-Class X
proof
  defpred P[Ordinal] means Tarski-Class(X,$1) = Tarski-Class X;
A1: ex A st P[A] by Th22;
  consider A such that
A2: P[A] & for B st P[B] holds A c= B from ORDINAL1:sch 1(A1);
  take A;
  thus Tarski-Class(X,A) = Tarski-Class X by A2;
  let B;
  assume
A3: B in A;
  thus thesis by A2,A3,ORDINAL1:7;
end;

theorem
  Y <> X & Y in Tarski-Class X implies
  ex A st not Y in Tarski-Class(X,A) & Y in Tarski-Class(X,succ A)
proof
  assume that
A1: Y <> X and
A2: Y in Tarski-Class X;
  defpred P[Ordinal] means Y in Tarski-Class(X,$1);
A3: ex A st Tarski-Class(X,A) = Tarski-Class X by Th22;
A4: ex A st P[A] by A2,A3;
  consider A such that
A5: P[A] & for B st P[B] holds A c= B from ORDINAL1:sch 1(A4);
A6: not Y in { X } by A1,TARSKI:def 1;
A7: Tarski-Class(X,{}) = { X } by Lm1;
A8: now
    assume
A9: A is limit_ordinal;
A10: ex B st B in A & Y in Tarski-Class(X,B) by A5,A6,A7,A9,Th16;
    thus contradiction by A5,A10,ORDINAL1:7;
  end;
  consider B such that
A11: A = succ B by A8,ORDINAL1:42;
  take B;
A12: not A c= B by A11,ORDINAL1:7,10;
  thus thesis by A5,A11,A12;
end;

theorem Th25:
  X is epsilon-transitive implies
  for A st A <> {} holds Tarski-Class(X,A) is epsilon-transitive
proof
  assume
A1: Y in X implies Y c= X;
  defpred OnP[Ordinal] means
  $1 <> {} implies Tarski-Class(X,$1) is epsilon-transitive;
A2: for A st for B st B in A holds OnP[B] holds OnP[A]
  proof
    let A such that
A3: for B st B in A holds OnP[B] and
A4: A <> {};
    let Y such that
A5: Y in Tarski-Class(X,A);
A6: now
      given B such that
A7:   A = succ B;
A8:   B in A by A7,ORDINAL1:10;
A9:   B c= A by A8,ORDINAL1:def 2;
A10:  OnP[B] by A3,A7,ORDINAL1:10;
A11:  Tarski-Class(X,B) c= Tarski-Class(X,A) by A9,Th19;
A12:  now
        assume
A13:    not Y c= Tarski-Class(X,B);
        consider Z such that
A14:    Z in Tarski-Class(X,B) and
A15:    Y c= Z or Y = bool Z by A5,A7,A13,Th13;
A16:    now
          assume
A17:      Y = bool Z;
          thus thesis
          proof
            let x;
            assume
A18:        x in Y;
            thus thesis by A7,A14,A17,A18,Th13;
          end;
        end;
A19:    now
          assume
A20:      Y c= Z;
          thus thesis
          proof
            let x;
            assume
A21:        x in Y;
A22:        x in Z by A20,A21;
A23:        now
              assume
A24:          B = {};
A25:          Tarski-Class(X,B) = { X } by A24,Lm1;
A26:          Z = X by A14,A25,TARSKI:def 1;
A27:          x c= X by A1,A20,A21,A26;
              thus thesis by A7,A14,A26,A27,Th13;
            end;
A28:        now
              assume
A29:          B <> {};
A30:          Z c= Tarski-Class(X,B) by A10,A14,A29,ORDINAL1:def 2;
A31:          x in Tarski-Class(X,B) by A22,A30;
              thus thesis by A11,A31;
            end;
            thus thesis by A23,A28;
          end;
        end;
        thus thesis by A15,A16,A19;
      end;
      thus thesis by A11,A12,XBOOLE_1:1;
    end;
A32: now
      assume
A33:  for B holds A <> succ B;
A34:  A is limit_ordinal by A33,ORDINAL1:42;
      consider B such that
A35:  B in A and
A36:  Y in Tarski-Class(X,B) by A4,A5,A34,Th16;
A37:  succ B c= A by A35,ORDINAL1:33;
A38:  succ B <> A by A33;
A39:  Tarski-Class(X,B) c= Tarski-Class(X,succ B) by Th18;
A40:  succ B c< A by A37,A38,XBOOLE_0:def 8;
A41:  Tarski-Class(X,succ B) c= Tarski-Class(X,A) by A37,Th19;
A42:  Tarski-Class(X,succ B) is epsilon-transitive by A3,A40,ORDINAL1:21;
A43:  Y c= Tarski-Class(X,succ B) by A36,A39,A42,ORDINAL1:def 2;
      thus thesis by A41,A43,XBOOLE_1:1;
    end;
    thus thesis by A6,A32;
  end;
  thus for A holds OnP[A] from ORDINAL1:sch 2(A2);
end;

theorem
  Tarski-Class(X,{}) in Tarski-Class(X,1) &
  Tarski-Class(X,{}) <> Tarski-Class(X,1)
proof
A1: Tarski-Class(X,{}) = { X } by Lm1;
A2: X in Tarski-Class(X,{}) by A1,TARSKI:def 1;
A3: bool X in Tarski-Class X by A2,Th7;
A4: { X } c= bool X
  proof
    let a;
    assume
A5: a in { X };
A6: a = X by A5,TARSKI:def 1;
    thus thesis by A6,ZFMISC_1:def 1;
  end;
A7: 1 = succ 0 & { X } in Tarski-Class X by A3,A4,Th6;
  thus
A8: Tarski-Class(X,{}) in Tarski-Class(X,1) by A1,A7,Th13;
  assume
A9: Tarski-Class(X,{}) = Tarski-Class(X,1);
  thus contradiction by A8,A9;
end;

theorem Th27:
  X is epsilon-transitive implies Tarski-Class X is epsilon-transitive
proof
  consider A such that
A1: Tarski-Class(X,A) = Tarski-Class X by Th22;
A2: Tarski-Class(X,A) c= Tarski-Class(X,succ A) by Th18;
A3: Tarski-Class(X,A) = Tarski-Class(X,succ A) by A1,A2,XBOOLE_0:def 10;
  assume
A4: X is epsilon-transitive;
  thus thesis by A1,A3,A4,Th25;
end;

theorem Th28:
  Y in Tarski-Class X implies card Y in card Tarski-Class X
proof
  assume
A1: Y in Tarski-Class X;
A2: bool Y c= Tarski-Class X
  proof
    let x;
    assume
A3: x in bool Y;
    thus thesis by A1,A3,Th6;
  end;
  A4: card
 Y in card bool Y & card bool Y c= card Tarski-Class X by A2,CARD_1:27
  ,30;
  thus thesis by A4;
end;

theorem Th29:
  Y in Tarski-Class X implies not Y,Tarski-Class X are_equipotent
proof
  assume
A1: Y in Tarski-Class X;
A2: card Y in card Tarski-Class X by A1,Th28;
A3: card Y <> card Tarski-Class X by A2;
  thus thesis by A3,CARD_1:21;
end;

theorem Th30:
  x in Tarski-Class X & y in Tarski-Class X implies
  {x} in Tarski-Class X & {x,y} in Tarski-Class X
proof
  assume that
A1: x in Tarski-Class X and
A2: y in Tarski-Class X;
A3: bool x in Tarski-Class X by A1,Th7;
  thus
A4: {x} in Tarski-Class X by A3,Th6,ZFMISC_1:80;
A5: bool {x} = {{},{x}} by ZFMISC_1:30;
A6: not {{},{x}},Tarski-Class X are_equipotent by A4,A5,Th7,Th29;
A7: now
    assume
A8: x <> y;
A9: {{},{x}},{x,y} are_equipotent
    proof
      defpred C[set] means $1 = {};
      deffunc f(set) = x;
      deffunc g(set) = y;
      consider f such that
A10:  dom f = {{},{x}} & for z st z in {{},{x}} holds
      (C[z] implies f.z = f(z)) & (not C[z] implies f.z = g(z))
      from PARTFUN1:sch 1;
      take f;
      thus f is one-to-one
      proof
        let x1,x2 be set;
        assume that
A11:    x1 in dom f and
A12:    x2 in dom f;
A13:    x2 = {} or x2 = {x} by A10,A12,TARSKI:def 2;
A14:    x1 = {} implies f.x1 = x by A10,A11;
A15:    x1 <> {} implies f.x1 = y by A10,A11;
        thus thesis by A8,A10,A11,A12,A13,A14,A15,TARSKI:def 2;
      end;
      thus dom f = {{},{x}} by A10;
      thus rng f c= {x,y}
      proof
        let z;
        assume
A16:    z in rng f;
A17:    ex u being set st u in dom f & z = f.u by A16,FUNCT_1:def 5;
A18:    z = x or z = y by A10,A17;
        thus thesis by A18,TARSKI:def 2;
      end;
      let z;
      assume
A19:  z in {x,y};
A20:  z = x or z = y by A19,TARSKI:def 2;
A21:  {} in dom f by A10,TARSKI:def 2;
A22:  {x} in dom f by A10,TARSKI:def 2;
A23:  f.{} = x by A10,A21;
A24:  f.{x} = y by A10,A22;
      thus thesis by A20,A21,A22,A23,A24,FUNCT_1:def 5;
    end;
A25: not {x,y},Tarski-Class X are_equipotent by A6,A9,WELLORD2:22;
A26: {x,y} c= Tarski-Class X by A1,A2,ZFMISC_1:38;
    thus thesis by A25,A26,Th8;
  end;
  thus thesis by A4,A7,ENUMSET1:69;
end;

theorem Th31:
  x in Tarski-Class X & y in Tarski-Class X implies [x,y] in Tarski-Class X
proof
  assume
A1: x in Tarski-Class X & y in Tarski-Class X;
  A2: {x,y} in Tarski-Class X & {x} in Tarski-Class X by A1,Th30;
  thus thesis by A2,Th30;
end;

theorem
  Y c= Tarski-Class X & Z c= Tarski-Class X implies [:Y,Z:] c= Tarski-Class X
proof
  assume
A1: Y c= Tarski-Class X & Z c= Tarski-Class X;
  let x,y;
  assume [x,y] in [:Y,Z:];
  then x in Y & y in Z by ZFMISC_1:106;
  hence thesis by A1,Th31;
end;

definition
  let A;
  func Rank(A) means
  :Def6:
  ex L st it = last L & dom L = succ A & L.{} = {} &
  (for C st succ C in succ A holds L.succ C = bool(L.C)) &
  for C st C in succ A & C <> {} & C is limit_ordinal
  holds L.C = union rng(L|C);
  correctness
  proof
    deffunc C(Ordinal,set) = bool $2;
    deffunc D(Ordinal,T-Sequence) = union rng $2;
    thus (ex x,L st x = last L & dom L = succ A & L.{} = {} &
    (for C st succ C in succ A holds L.succ C = C(C,L.C)) &
    for C st C in succ A & C <> {} & C is limit_ordinal
    holds L.C = D(C,L|C) ) & for x1,x2 being set st
    (ex L st x1 = last L & dom L = succ A & L.{} = {} &
    (for C st succ C in succ A holds L.succ C = C(C,L.C)) &
    for C st C in succ A & C <> {} & C is limit_ordinal
    holds L.C = D(C,L|C) ) &
    (ex L st x2 = last L & dom L = succ A & L.{} = {} &
    (for C st succ C in succ A holds L.succ C = C(C,L.C)) &
    for C st C in succ A & C <> {} & C is limit_ordinal holds L.C = D(C,L|C) )
    holds x1 = x2 from ORDINAL2:sch 7;
  end;
end;
deffunc F(Ordinal) = Rank $1;

Lm2: now
  deffunc C(Ordinal,set) = bool $2;
  deffunc D(Ordinal,T-Sequence) = union rng $2;
A1: for A,x holds x = F(A) iff
  ex L st x = last L & dom L = succ A & L.{} = {} &
  (for C st succ C in succ A holds L.succ C = C(C,L.C)) &
  for C st C in succ A & C <> {} & C is limit_ordinal
  holds L.C = D(C,L|C) by Def6;
  thus F({}) = {} from ORDINAL2:sch 8(A1);
  thus for A holds F(succ A) = C(A,F(A)) from ORDINAL2:sch 9(A1);
  thus A <> {} & A is limit_ordinal & dom L = A &
  (for B st B in A holds L.B = Rank B) implies Rank A = union rng L
  proof
    assume that
A2: A <> {} & A is limit_ordinal and
A3: dom L = A and
A4: for B st B in A holds L.B = F(B);
    thus F(A) = D(A,L) from ORDINAL2:sch 10(A1,A2,A3,A4);
  end;
end;

theorem
  Rank {} = {} by Lm2;

theorem
  Rank succ A = bool Rank A by Lm2;

theorem Th35:
  A <> {} & A is limit_ordinal implies
  for x holds x in Rank A iff ex B st B in A & x in Rank B
proof
  assume
A1: A <> {} & A is limit_ordinal;
  consider L such that
A2: dom L = A & for B st B in A holds L.B = F(B) from ORDINAL2:sch 2;
A3: Rank A = union rng L by A1,A2,Lm2;
  let x;
  thus x in Rank A implies ex B st B in A & x in Rank B
  proof
    assume
A4: x in Rank A;
    consider Y such that
A5: x in Y and
A6: Y in rng L by A3,A4,TARSKI:def 4;
    consider y such that
A7: y in dom L and
A8: Y = L.y by A6,FUNCT_1:def 5;
    reconsider y as Ordinal by A7;
    take y;
    thus thesis by A2,A5,A7,A8;
  end;
  given B such that
A9: B in A and
A10: x in Rank B;
A11: L.B = Rank B by A2,A9;
A12: Rank B in rng L by A2,A9,A11,FUNCT_1:def 5;
  thus thesis by A3,A10,A12,TARSKI:def 4;
end;

theorem Th36:
  X c= Rank A iff X in Rank succ A
proof
  thus X c= Rank A implies X in Rank succ A
  proof
    assume
A1: X c= Rank A;
A2: X in bool Rank A by A1;
    thus thesis by A2,Lm2;
  end;
  assume
A3: X in Rank succ A;
A4: X in bool Rank A by A3,Lm2;
  thus thesis by A4;
end;

registration
  let A;
  cluster Rank A -> epsilon-transitive;
  coherence
  proof
    defpred P[Ordinal] means X in Rank $1 implies X c= Rank $1;
A1: for A st for B st B in A holds P[B] holds P[A]
    proof
      let A such that
A2:   for B st B in A holds P[B];
      let X such that
A3:   X in Rank A;
      let x such that
A4:   x in X;
A5:   now
        assume
A6:     A is limit_ordinal;
        consider B such that
A7:     B in A and
A8:     X in Rank B by A3,A6,Lm2,Th35;
A9:     X c= Rank B by A2,A7,A8;
        thus thesis by A4,A6,A7,A9,Th35;
      end;
A10:  now
        assume
A11:    not A is limit_ordinal;
        consider B such that
A12:    A = succ B by A11,ORDINAL1:42;
A13:    X c= Rank B by A3,A12,Th36;
A14:    x c= Rank B by A2,A4,A12,A13,ORDINAL1:10;
        thus thesis by A12,A14,Th36;
      end;
      thus thesis by A5,A10;
    end;
A15: for A holds P[A] from ORDINAL1:sch 2(A1);
    thus P[A] by A15;
  end;
end;

canceled 2;

theorem
  Rank A c= Rank succ A
proof
A1: Rank A in bool Rank A by ZFMISC_1:def 1;
A2: Rank A in Rank succ A by A1,Lm2;
  thus thesis by A2,ORDINAL1:def 2;
end;

theorem Th40:
  union Rank A c= Rank A
proof
  let x;
  assume
A1: x in union Rank A;
  consider X such that
A2: x in X and
A3: X in Rank A by A1,TARSKI:def 4;
A4: X c= Rank A by A3,ORDINAL1:def 2;
  thus thesis by A2,A4;
end;

theorem
  X in Rank A implies union X in Rank A
proof
  assume
A1: X in Rank A;
A2: now
    given B such that
A3: A = succ B;
A4: X c= Rank B by A1,A3,Th36;
A5: union X c= union Rank B by A4,ZFMISC_1:95;
A6: union Rank B c= Rank B by Th40;
A7: union X c= Rank B by A5,A6,XBOOLE_1:1;
    thus thesis by A3,A7,Th36;
  end;
A8: now
    assume that
A9: A <> {} and
A10: for B holds A <> succ B;
A11: A is limit_ordinal by A10,ORDINAL1:42;
    consider B such that
A12: B in A and
A13: X in Rank B by A1,A9,A11,Th35;
A14: X c= Rank B by A13,ORDINAL1:def 2;
A15: union X c= union Rank B by A14,ZFMISC_1:95;
A16: union Rank B c= Rank B by Th40;
A17: union X c= Rank B by A15,A16,XBOOLE_1:1;
A18: succ B c= A by A12,ORDINAL1:33;
A19: succ B <> A by A10;
A20: succ B c< A by A18,A19,XBOOLE_0:def 8;
A21: union X in Rank succ B by A17,Th36;
A22: succ B in A by A20,ORDINAL1:21;
    thus thesis by A11,A21,A22,Th35;
  end;
  thus thesis by A1,A2,A8,Lm2;
end;

theorem Th42:
  A in B iff Rank A in Rank B
proof
  defpred OnP[Ordinal,Ordinal] means $1 in $2 implies Rank $1 in Rank $2;
A1: now
    let A;
    defpred P[Ordinal] means OnP[A,$1];
A2: for B st for C st C in B holds P[C] holds P[B]
    proof
      let B such that
A3:   for C st C in B holds OnP[A,C] and
A4:   A in B;
A5:   now
        given C such that
A6:     B = succ C;
A7:     A in C implies Rank A in Rank C by A3,A6,ORDINAL1:10;
A8:     now
          assume
A9:       not A in C;
A10:      A c= C & A <> C iff A c< C by XBOOLE_0:def 8;
          thus Rank A = Rank C by A4,A6,A9,A10,ORDINAL1:21,34;
        end;
A11:    Rank A c= Rank C by A7,A8,ORDINAL1:def 2;
        thus thesis by A6,A11,Th36;
      end;
A12:  now
        assume
A13:    for C holds B <> succ C;
A14:    B is limit_ordinal by A13,ORDINAL1:42;
A15:    B <> succ A by A13;
A16:    succ A c= B by A4,ORDINAL1:33;
A17:    succ A c< B by A15,A16,XBOOLE_0:def 8;
A18:    succ A in B by A17,ORDINAL1:21;
A19:    Rank A in Rank succ A by Th36;
        thus thesis by A14,A18,A19,Th35;
      end;
      thus thesis by A5,A12;
    end;
    thus for B holds P[B] from ORDINAL1:sch 2(A2);
  end;
  thus OnP[A,B] by A1;
  assume that
A20: Rank A in Rank B and
A21: not A in B;
A22: B in A or B = A by A21,ORDINAL1:24;
  thus contradiction by A1,A20,A22;
end;

theorem Th43:
  A c= B iff Rank A c= Rank B
proof
  thus A c= B implies Rank A c= Rank B
  proof
A1: A c< B iff A c= B & A <> B by XBOOLE_0:def 8;
    assume
A2: A c= B;
A3: A = B or A in B by A1,A2,ORDINAL1:21;
A4: Rank A = Rank B or Rank A in Rank B by A3,Th42;
    thus thesis by A4,ORDINAL1:def 2;
  end;
  assume that
A5: Rank A c= Rank B and
A6: not A c= B;
A7: B in A by A6,ORDINAL1:26;
A8: Rank B in Rank A by A7,Th42;
  thus contradiction by A5,A8,ORDINAL1:7;
end;

theorem Th44:
  A c= Rank A
proof
  defpred P[Ordinal] means $1 c= Rank $1;
A1: P[{}] by XBOOLE_1:2;
A2: P[B] implies P[succ B]
  proof
    assume
A3: B c= Rank B;
A4: B in Rank succ B by A3,Th36;
    A5: B c= Rank succ B & {B} c= Rank succ B by A4,ORDINAL1:def 2,ZFMISC_1:37;
    thus thesis by A5,XBOOLE_1:8;
  end;
A6: for A st A <> {} & A is limit_ordinal & for B st B in A holds P[B]
  holds P[A]
  proof
    let A such that
    A <> {} and
A7: A is limit_ordinal and
A8: for B st B in A holds B c= Rank B;
    let x;
    assume
A9: x in A;
    reconsider B = x as Ordinal by A9;
A10: succ B in A by A7,A9,ORDINAL1:41;
A11: B c= Rank B by A8,A9;
A12: succ B c= A by A10,ORDINAL1:def 2;
A13: B in Rank succ B by A11,Th36;
A14: Rank succ B c= Rank A by A12,Th43;
    thus thesis by A13,A14;
  end;
A15: for B holds P[B] from ORDINAL2:sch 1(A1,A2,A6);
  thus thesis by A15;
end;

theorem
  for A,X st X in Rank A holds
  not X,Rank A are_equipotent & card X in card Rank A
proof
  defpred OnP[Ordinal] means
  for X st X in Rank $1 holds not X,Rank $1 are_equipotent;
A1: for A st for B st B in A holds OnP[B] holds OnP[A]
  proof
    let A such that
A2: for B st B in A holds OnP[B];
    let X;
    assume
A3: X in Rank A;
A4: now
      given B such that
A5:   A = succ B;
A6:   B in A by A5,ORDINAL1:10;
A7:   B c= A by A6,ORDINAL1:def 2;
A8:   Rank succ B = bool Rank B by Lm2;
A9:   not Rank B,Rank A are_equipotent by A5,A8,CARD_1:29;
A10:  Rank B c= Rank A by A7,Th43;
      thus thesis by A3,A5,A8,A9,A10,CARD_1:44;
    end;
A11: now
      assume that
A12:  A <> {} and
A13:  for B holds A <> succ B;
A14:  A is limit_ordinal by A13,ORDINAL1:42;
      consider B such that
A15:  B in A and
A16:  X in Rank B by A3,A12,A14,Th35;
A17:  Rank B in Rank A by A15,Th42;
      A18:  (
 not X,Rank B are_equipotent)& X c= Rank B by A2,A15,A16,ORDINAL1:def 2;
A19:  Rank B c= Rank A by A17,ORDINAL1:def 2;
      thus thesis by A18,A19,CARD_1:44;
    end;
    thus thesis by A3,A4,A11,Lm2;
  end;
A20: for A holds OnP[A] from ORDINAL1:sch 2(A1);
  let A,X;
  assume
A21: X in Rank A;
A22: X c= Rank A by A21,ORDINAL1:def 2;
A23: not X,Rank A are_equipotent by A20,A21;
A24: card X c= card Rank A by A22,CARD_1:27;
A25: card X <> card Rank A by A23,CARD_1:21;
  thus thesis by A20,A21,A24,A25,CARD_1:13;
end;

theorem
  X c= Rank A iff bool X c= Rank succ A
proof
  thus X c= Rank A implies bool X c= Rank succ A
  proof
    assume
A1: X c= Rank A;
    let x;
    assume
A2: x in bool X;
A3: x c= Rank A by A1,A2,XBOOLE_1:1;
A4: Rank succ A = bool Rank A by Lm2;
    thus thesis by A3,A4;
  end;
  assume
A5: bool X c= Rank succ A;
  let x;
  assume
A6: x in X;
A7: { x } c= X by A6,ZFMISC_1:37;
A8: { x } in bool X by A7;
A9: Rank succ A = bool Rank A & x in { x } by Lm2,TARSKI:def 1;
  thus thesis by A5,A8,A9;
end;

theorem Th47:
  X c= Y & Y in Rank A implies X in Rank A
proof
  assume that
A1: X c= Y and
A2: Y in Rank A;
A3: now
    given B such that
A4: A = succ B;
A5: Rank succ B = bool Rank B by Lm2;
A6: X c= Rank B by A1,A2,A4,A5,XBOOLE_1:1;
    thus thesis by A4,A5,A6;
  end;
A7: now
    assume
A8: for B holds A <> succ B;
A9: A is limit_ordinal by A8,ORDINAL1:42;
    consider B such that
A10: B in A and
A11: Y in Rank B by A2,A9,Lm2,Th35;
A12: Y c= Rank B by A11,ORDINAL1:def 2;
A13: X c= Rank B by A1,A12,XBOOLE_1:1;
A14: bool Rank B = Rank succ B by Lm2;
A15: succ B in A by A9,A10,ORDINAL1:41;
    thus thesis by A9,A13,A14,A15,Th35;
  end;
  thus thesis by A3,A7;
end;

theorem Th48:
  X in Rank A iff bool X in Rank succ A
proof
  thus X in Rank A implies bool X in Rank succ A
  proof
    assume
A1: X in Rank A;
A2: bool X c= Rank A
    proof
      let x;
      assume
A3:   x in bool X;
      thus thesis by A1,A3,Th47;
    end;
    thus thesis by A2,Th36;
  end;
  assume
A4: bool X in Rank succ A;
A5: X in bool X & bool X c= Rank A by A4,Th36,ZFMISC_1:def 1;
  thus thesis by A5;
end;

theorem Th49:
  x in Rank A iff {x} in Rank succ A
proof
A1: x in Rank A iff {x} c= Rank A by ZFMISC_1:37;
  thus thesis by A1,Th36;
end;

theorem Th50:
  x in Rank A & y in Rank A iff {x,y} in Rank succ A
proof
A1: x in Rank A & y in Rank A iff {x,y} c= Rank A by ZFMISC_1:38;
  thus thesis by A1,Th36;
end;

theorem
  x in Rank A & y in Rank A iff [x,y] in Rank succ succ A
proof
A1: x in Rank A iff {x} in Rank succ A by Th49;
A2: x in Rank A & y in Rank A iff {x,y} in Rank succ A by Th50;
  thus thesis by A1,A2,Th50;
end;

theorem Th52:
  X is epsilon-transitive &
  Rank A /\ Tarski-Class X = Rank succ A /\ Tarski-Class X implies
  Tarski-Class X c= Rank A
proof
  assume that
A1: X is epsilon-transitive and
A2: Rank A /\ Tarski-Class X = Rank succ A /\ Tarski-Class X;
  given x such that
A3: x in Tarski-Class X & not x in Rank A;
A4: x in (Tarski-Class X) \ Rank A by A3,XBOOLE_0:def 5;
  consider Y such that
A5: Y in (Tarski-Class X) \ Rank A and
A6: not ex x st x in (Tarski-Class X) \ Rank A & x in Y by A4,TARSKI:7;
A7: Tarski-Class X is epsilon-transitive by A1,Th27;
A8: Y c= Tarski-Class X by A5,A7,ORDINAL1:def 2;
A9: Y c= Rank A
  proof
    let x;
    assume
A10: x in Y;
A11: not x in (Tarski-Class X) \ Rank A by A6,A10;
    thus thesis by A8,A10,A11,XBOOLE_0:def 5;
  end;
A12: Y in Rank succ A by A9,Th36;
A13: Y in Rank succ A /\ Tarski-Class X by A5,A12,XBOOLE_0:def 4;
A14: not Y in Rank A by A5,XBOOLE_0:def 5;
  thus contradiction by A2,A13,A14,XBOOLE_0:def 4;
end;

theorem Th53:
  X is epsilon-transitive implies ex A st Tarski-Class X c= Rank A
proof
  assume
A1: X is epsilon-transitive;
  assume
A2: not Tarski-Class X c= Rank A;
  defpred P[set] means ex A st $1 in Rank A;
  consider Power being set such that
A3: x in Power iff x in Tarski-Class X & P[x] from XBOOLE_0:sch 1;
  defpred P[set,set] means
  ex A st $2 = A & not $1 in Rank A & $1 in Rank succ A;
A4: for x,y,z st P[x,y] & P[x,z] holds y = z
  proof
    let x,y,z;
    given A1 being Ordinal such that
A5: y = A1 and
A6: not x in Rank A1 and
A7: x in Rank succ A1;
    given A2 being Ordinal such that
A8: z = A2 and
A9: not x in Rank A2 and
A10: x in Rank succ A2;
    assume
A11: y <> z;
A12: A1 in A2 or A2 in A1 by A5,A8,A11,ORDINAL1:24;
A13: succ A1 c= A2 or succ A2 c= A1 by A12,ORDINAL1:33;
A14: now
      assume
A15:  succ A1 c= A2;
A16:  Rank succ A1 c= Rank A2 by A15,Th43;
      thus contradiction by A7,A9,A16;
    end;
A17: Rank succ A2 c= Rank A1 by A13,A14,Th43;
    thus contradiction by A6,A10,A17;
  end;
  consider Y such that
A18: x in Y iff ex y st y in Power & P[y,x] from TARSKI:sch 1(A4);
A19: now
    let A;
A20: Rank A /\ Tarski-Class X <> Rank succ A /\ Tarski-Class X by A1,A2,
    Th52;
    consider y such that
    A21: not
 (y in Rank A /\ Tarski-Class X iff y in Rank succ A /\ Tarski-Class X)
    by A20,TARSKI:2;
A22: A in succ A by ORDINAL1:10;
A23: A c= succ A by A22,ORDINAL1:def 2;
A24: Rank A c= Rank succ A by A23,Th43;
A25: Rank A /\ Tarski-Class X c= Rank succ A /\
    Tarski-Class X by A24,XBOOLE_1:26;
A26: y in Rank succ A by A21,A25,XBOOLE_0:def 4;
A27: y in Tarski-Class X by A21,XBOOLE_0:def 4;
A28: not y in Rank A or not y in Tarski-Class X by A21,A24,XBOOLE_0:def 4;
A29: y in Power by A3,A26,A27;
    thus A in Y by A18,A21,A26,A28,A29,XBOOLE_0:def 4;
  end;
  thus contradiction by A19,ORDINAL1:38;
end;

theorem Th54:
  X is epsilon-transitive implies union X c= X
proof
  assume
A1: Y in X implies Y c= X;
  let x;
  assume
A2: x in union X;
  consider Y such that
A3: x in Y and
A4: Y in X by A2,TARSKI:def 4;
A5: Y c= X by A1,A4;
  thus thesis by A3,A5;
end;

theorem Th55:
  X is epsilon-transitive & Y is epsilon-transitive implies
  X \/ Y is epsilon-transitive
proof
  assume that
A1: ( Z in X implies Z c= X) and
A2: ( Z in Y implies Z c= Y);
  let Z;
  assume
A3: Z in X \/ Y;
A4: Z in X or Z in Y by A3,XBOOLE_0:def 3;
A5: Z c= X or Z c= Y by A1,A2,A4;
A6: X c= X \/ Y & Y c= X \/ Y by XBOOLE_1:7;
  thus thesis by A5,A6,XBOOLE_1:1;
end;

theorem
  X is epsilon-transitive & Y is epsilon-transitive implies
  X /\ Y is epsilon-transitive
proof
  assume that
A1: Z in X implies Z c= X and
A2: Z in Y implies Z c= Y;
  let Z;
  assume
A3: Z in X /\ Y;
A4: Z in X by A3,XBOOLE_0:def 4;
A5: Z in Y by A3,XBOOLE_0:def 4;
A6: Z c= X by A1,A4;
A7: Z c= Y by A2,A5;
  thus thesis by A6,A7,XBOOLE_1:19;
end;

reserve k,n for Element of NAT;
deffunc f(set,set) = union $2;

definition
  let X;
  func the_transitive-closure_of X -> set means
  :Def7:
  x in it iff ex f,n st x in f.n & dom f = NAT & f.0 = X &
  for k being Nat holds f.(k+1) = union(f.k);
  existence
  proof
    consider f such that
A1: dom f = NAT & f.0 = X &
    for n being Nat holds f.(n+1) = f(n,f.n) from NAT_1:sch 11;
    take UNI = union rng f;
    let x;
    thus x in UNI implies ex f,n st x in f.n & dom f = NAT & f.0 = X &
    for k being Nat holds f.(k+1) = union(f.k)
    proof
      assume
A2:   x in UNI;
      consider Y such that
A3:   x in Y and
A4:   Y in rng f by A2,TARSKI:def 4;
      consider y such that
A5:   y in dom f and
A6:   Y = f.y by A4,FUNCT_1:def 5;
      reconsider y as Element of NAT by A1,A5;
      take f,y;
      thus thesis by A1,A3,A6;
    end;
    deffunc f(set,set) = union $2;
    given g,n such that
A7: x in g.n and
A8: dom g = NAT and
A9: g.0 = X and
A10: for k being Nat holds g.(k+1) = f(k,g.k);
A11: dom f = NAT by A1;
A12: f.0 = X by A1;
A13: for n being Nat holds f.(n+1) = f(n,f.n) by A1;
A14: g = f from NAT_1:sch 15(A8,A9,A10,A11,A12,A13);
A15: g.n in rng f by A1,A14,FUNCT_1:def 5;
    thus thesis by A7,A15,TARSKI:def 4;
  end;
  uniqueness
  proof
    defpred P[set] means ex f,n st $1 in f.n & dom f = NAT & f.0 = X &
    for k being Nat holds f.(k+1) = union(f.k);
    let U1,U2 be set such that
A16: x in U1 iff P[x] and
A17: x in U2 iff P[x];
    thus U1 = U2 from XBOOLE_0:sch 2(A16,A17);
  end;
end;

canceled;

theorem Th58:
  the_transitive-closure_of X is epsilon-transitive
proof
  let Y;
  assume
A1: Y in the_transitive-closure_of X;
  consider f,n such that
A2: Y in f.n and
A3: dom f = NAT & f.0 = X and
A4: for k being Nat holds f.(k+1) = union(f.k) by A1,Def7;
A5: f.(n+1) = union(f.n) by A4;
  let x;
  assume
A6: x in Y;
A7: x in union(f.n) by A2,A6,TARSKI:def 4;
  thus thesis by A3,A4,A5,A7,Def7;
end;

theorem Th59:
  X c= the_transitive-closure_of X
proof
  let x such that
A1: x in X;
A2: ex f st dom f = NAT & f.0 = X &
  for n being Nat holds f.(n+1) = f(n,f.n) from NAT_1:sch 11;
  thus thesis by A1,A2,Def7;
end;

theorem Th60:
  X c= Y & Y is epsilon-transitive implies the_transitive-closure_of X c= Y
proof
  assume that
A1: X c= Y and
A2: Y is epsilon-transitive;
  let x;
  assume
A3: x in the_transitive-closure_of X;
  consider f,n such that
A4: x in f.n
  and dom f = NAT and
A5: f.0 = X and
A6: for k being Nat holds f.(k+1) = union(f.k) by A3,Def7;
  defpred P[Element of NAT] means f.$1 c= Y;
A7: P[0] by A1,A5;
A8: for k st P[k] holds P[k+1]
  proof
    let k;
    assume
A9: f.k c= Y;
A10: union (f.k) c= union Y by A9,ZFMISC_1:95;
A11: f.(k+1) = union (f.k) & union Y c= Y by A2,A6,Th54;
    thus thesis by A10,A11,XBOOLE_1:1;
  end;
A12: P[k] from NAT_1:sch 1(A7,A8);
A13: f.n c= Y by A12;
  thus thesis by A4,A13;
end;

theorem Th61:
  (for Z st X c= Z & Z is epsilon-transitive holds Y c= Z) & X c= Y &
  Y is epsilon-transitive implies the_transitive-closure_of X = Y
proof
  assume
A1: for Z st X c= Z & Z is epsilon-transitive holds Y c= Z;
  assume
A2: X c= Y & Y is epsilon-transitive;
  thus the_transitive-closure_of X c= Y by A2,Th60;
A3: the_transitive-closure_of X is epsilon-transitive by Th58;
  thus thesis by A1,A3,Th59;
end;

theorem Th62:
  X is epsilon-transitive implies the_transitive-closure_of X = X
proof
A1: for Z st X c= Z & Z is epsilon-transitive holds X c= Z;
  thus thesis by A1,Th61;
end;

theorem
  the_transitive-closure_of {} = {} by Th62;

theorem
  the_transitive-closure_of A = A by Th62;

theorem Th65:
  X c= Y implies the_transitive-closure_of X c= the_transitive-closure_of Y
proof
  assume
A1: X c= Y;
A2: Y c= the_transitive-closure_of Y by Th59;
A3: X c= the_transitive-closure_of Y by A1,A2,XBOOLE_1:1;
  thus thesis by A3,Th58,Th60;
end;

theorem
  the_transitive-closure_of the_transitive-closure_of X =
  the_transitive-closure_of X by Th58,Th62;

theorem
  the_transitive-closure_of (X \/ Y) =
  the_transitive-closure_of X \/ the_transitive-closure_of Y
proof
  A1: X
 c= the_transitive-closure_of X & Y c= the_transitive-closure_of Y by Th59;
A2: X \/ Y c= the_transitive-closure_of X \/ the_transitive-closure_of Y by
  A1,XBOOLE_1:13;
  A3: the_transitive-closure_of X is epsilon-transitive &
  the_transitive-closure_of Y is epsilon-transitive by Th58;
A4: the_transitive-closure_of (X \/ Y) c= the_transitive-closure_of (
  the_transitive-closure_of X \/ the_transitive-closure_of Y) by A2,Th65;
  thus the_transitive-closure_of (X \/ Y) c=
  the_transitive-closure_of X \/ the_transitive-closure_of Y by A3,A4,Th55,Th62
  ;
  A5: the_transitive-closure_of X c= the_transitive-closure_of (X \/ Y) &
  the_transitive-closure_of Y c= the_transitive-closure_of (X \/ Y) by Th65,
  XBOOLE_1:7;
  thus thesis by A5,XBOOLE_1:8;
end;

theorem
  the_transitive-closure_of (X /\ Y) c=
  the_transitive-closure_of X /\ the_transitive-closure_of Y
proof
  A1: the_transitive-closure_of (X /\ Y) c= the_transitive-closure_of X &
  the_transitive-closure_of (X /\ Y) c= the_transitive-closure_of Y by Th65,
  XBOOLE_1:17;
  thus thesis by A1,XBOOLE_1:19;
end;

theorem Th69:
  ex A st X c= Rank A
proof
  consider A such that
A1: Tarski-Class the_transitive-closure_of X c= Rank A by Th53,Th58;
  take A;
A2: the_transitive-closure_of X in Tarski-Class the_transitive-closure_of X
  by Th5;
A3: X in Tarski-Class the_transitive-closure_of X by A2,Th6,Th59;
  thus thesis by A1,A3,ORDINAL1:def 2;
end;

definition
  let X;
  func the_rank_of X -> Ordinal means
  :Def8:
  X c= Rank it & for B st X c= Rank B holds it c= B;
  existence
  proof
    defpred P[Ordinal] means X c= Rank $1;
A1: ex A st P[A] by Th69;
    thus ex A st P[A] & for B st P[B] holds A c= B from ORDINAL1:sch 1(
    A1);
  end;
  uniqueness
  proof
    let A1,A2 be Ordinal such that
    A2: ( X c= Rank A1 & for B st X c= Rank B holds A1 c= B )&( X c= Rank A2 &
    for B st X c= Rank B holds A2 c= B );
    thus A1 c= A2 & A2 c= A1 by A2;
  end;
end;

canceled;

theorem Th71:
  the_rank_of bool X = succ the_rank_of X
proof
A1: X c= Rank the_rank_of X by Def8;
A2: bool X c= Rank succ the_rank_of X
  proof
    let x;
    assume
A3: x in bool X;
A4: x c= Rank the_rank_of X by A1,A3,XBOOLE_1:1;
A5: bool Rank the_rank_of X = Rank succ the_rank_of X by Lm2;
    thus thesis by A4,A5;
  end;
A6: for A st bool X c= Rank A holds succ the_rank_of X c= A
  proof
    let A such that
A7: bool X c= Rank A;
    defpred P[Ordinal] means X in Rank $1;
A8: X in bool X by ZFMISC_1:def 1;
A9: ex A st P[A] by A7,A8;
    consider B such that
A10: P[B] & for C st P[C] holds B c= C from ORDINAL1:sch 1(A9);
A11: now
      assume
A12:  for C holds B <> succ C;
A13:  B is limit_ordinal by A12,ORDINAL1:42;
A14:  ex C st C in B & X in Rank C by A10,A13,Lm2,Th35;
      thus contradiction by A10,A14,ORDINAL1:7;
    end;
    consider C such that
A15: B = succ C by A11;
A16: X c= Rank C by A10,A15,Th36;
A17: the_rank_of X c= C by A16,Def8;
A18: the_rank_of X in B by A15,A17,ORDINAL1:34;
A19: B c= A by A7,A8,A10;
    thus thesis by A18,A19,ORDINAL1:33;
  end;
  thus thesis by A2,A6,Def8;
end;

theorem
  the_rank_of Rank A = A
proof
A1: for B st Rank A c= Rank B holds A c= B by Th43;
  thus thesis by A1,Def8;
end;

theorem Th73:
  X c= Rank A iff the_rank_of X c= A
proof
  thus X c= Rank A implies the_rank_of X c= A by Def8;
  assume
A1: the_rank_of X c= A;
A2: Rank the_rank_of X c= Rank A by A1,Th43;
A3: X c= Rank the_rank_of X by Def8;
  thus thesis by A2,A3,XBOOLE_1:1;
end;

theorem Th74:
  X in Rank A iff the_rank_of X in A
proof
  thus X in Rank A implies the_rank_of X in A
  proof
    assume
A1: X in Rank A;
A2: bool X in Rank succ A by A1,Th48;
A3: bool X c= Rank A by A2,Th36;
A4: the_rank_of bool X = succ the_rank_of X by Th71;
A5: the_rank_of X in the_rank_of bool X by A4,ORDINAL1:10;
A6: the_rank_of bool X c= A by A3,Def8;
    thus thesis by A5,A6;
  end;
  assume
A7: the_rank_of X in A;
A8: succ the_rank_of X c= A by A7,ORDINAL1:33;
A9: X c= Rank the_rank_of X by Def8;
A10: X in Rank succ the_rank_of X by A9,Th36;
A11: Rank succ the_rank_of X c= Rank A by A8,Th43;
  thus thesis by A10,A11;
end;

theorem
  X c= Y implies the_rank_of X c= the_rank_of Y
proof
  assume
A1: X c= Y;
A2: Y c= Rank the_rank_of Y by Def8;
A3: X c= Rank the_rank_of Y by A1,A2,XBOOLE_1:1;
  thus thesis by A3,Def8;
end;

theorem Th76:
  X in Y implies the_rank_of X in the_rank_of Y
proof
  assume
A1: X in Y;
A2: Y c= Rank the_rank_of Y by Def8;
  thus thesis by A1,A2,Th74;
end;

theorem Th77:
  the_rank_of X c= A iff for Y st Y in X holds the_rank_of Y in A
proof
  set R = the_rank_of X;
A1: X c= Rank R by Def8;
  thus
  the_rank_of X c= A implies for Y st Y in X holds the_rank_of Y in A
  proof
    assume
A2: the_rank_of X c= A;
    let Y;
    assume
A3: Y in X;
A4: Y in Rank R by A1,A3;
A5: Rank R c= Rank A by A2,Th43;
    thus thesis by A4,A5,Th74;
  end;
  assume
A6: for Y st Y in X holds the_rank_of Y in A;
A7: X c= Rank A
  proof
    let x;
    assume
A8: x in X;
A9: the_rank_of x in A by A6,A8;
    thus thesis by A9,Th74;
  end;
  thus thesis by A7,Def8;
end;

theorem Th78:
  A c= the_rank_of X iff for B st B in A ex Y st Y in X & B c= the_rank_of Y
proof
  thus A c= the_rank_of X implies
  for B st B in A ex Y st Y in X & B c= the_rank_of Y
  proof
    assume
A1: A c= the_rank_of X;
    let B;
    assume
A2: B in A;
A3: not the_rank_of X c= B by A1,A2,ORDINAL1:7;
A4: not X c= Rank B by A3,Def8;
A5: X \ Rank B <> {} by A4,XBOOLE_1:37;
    consider x being Element of X \ Rank B;
    take x;
A6: not x in Rank B by A5,XBOOLE_0:def 5;
    thus x in X by A5,XBOOLE_0:def 5;
A7: not the_rank_of x in B by A6,Th74;
    thus thesis by A7,ORDINAL1:26;
  end;
  assume
A8: for B st B in A ex Y st Y in X & B c= the_rank_of Y;
  let x;
  assume
A9: x in A;
  reconsider x as Ordinal by A9;
  consider Y such that
A10: Y in X and
A11: x c= the_rank_of Y by A8,A9;
A12: the_rank_of Y in the_rank_of X by A10,Th76;
  thus thesis by A11,A12,ORDINAL1:22;
end;

theorem
  the_rank_of X = {} iff X = {}
proof
  thus the_rank_of X = {} implies X = {}
  proof
    assume
A1: the_rank_of X = {};
A2: X c= Rank {} by A1,Def8;
    thus thesis by A2,Lm2;
  end;
  assume
A3: X = {};
A4: for Y st Y in X holds the_rank_of Y in {} by A3;
  thus the_rank_of X c= {} by A4,Th77;
  thus {} c= the_rank_of X;
end;

theorem Th80:
  the_rank_of X = succ A implies ex Y st Y in X & the_rank_of Y = A
proof
  assume
A1: the_rank_of X = succ A;
A2: A in succ A by ORDINAL1:10;
  consider Y such that
A3: Y in X and
A4: A c= the_rank_of Y by A1,A2,Th78;
  take Y;
A5: the_rank_of Y in the_rank_of X by A3,Th76;
A6: the_rank_of Y c= A by A1,A5,ORDINAL1:34;
  thus thesis by A3,A4,A6,XBOOLE_0:def 10;
end;

theorem
  the_rank_of A = A
proof
A1: A c= Rank A by Th44;
  thus the_rank_of A c= A by A1,Th73;
  defpred P[Ordinal] means $1 c= the_rank_of $1;
A2: for A st for B st B in A holds P[B] holds P[A]
  proof
    let A such that
A3: for B st B in A holds B c= the_rank_of B;
A4: now
      let B such that
A5:   B in A;
      reconsider Y = B as set;
      take Y;
      thus Y in A & B c= the_rank_of Y by A3,A5;
    end;
    thus thesis by A4,Th78;
  end;
A6: P[B] from ORDINAL1:sch 2(A2);
  thus thesis by A6;
end;

theorem
  the_rank_of Tarski-Class X <> {} &
  the_rank_of Tarski-Class X is limit_ordinal
proof
A1: Tarski-Class X c= Rank the_rank_of Tarski-Class X by Def8;
  thus the_rank_of Tarski-Class X <> {}
  proof
    assume
A2: the_rank_of Tarski-Class X = {};
A3: Tarski-Class X c= {} by A2,Def8,Lm2;
    thus contradiction by A3;
  end;
  assume
A4: not thesis;
  consider A such that
A5: the_rank_of Tarski-Class X = succ A by A4,ORDINAL1:42;
  consider Y such that
A6: Y in Tarski-Class X and
A7: the_rank_of Y = A by A5,Th80;
A8: bool Y in Tarski-Class X by A6,Th7;
A9: the_rank_of bool Y = succ A by A7,Th71;
A10: bool Y c= Rank A by A1,A5,A8,Th36;
A11: succ A c= A by A9,A10,Def8;
A12: A in A by A11,ORDINAL1:33;
  thus contradiction by A12;
end;

begin :: Addenda

:: from ZFREFLE1, 2007.03, A.T.

reserve e,u for set;

scheme NonUniqFuncEx { X() -> set, P[set,set] }:
  ex f being Function st dom f = X() & for e st e in X() holds P[e,f.e]
provided
A1: for e st e in X() ex u st P[e,u]
proof
  per cases;
  suppose
A2: X() = {};
A3: for e st e in X() ex u st u in {} & P[e,u] by A2;
A4: ex f being Function st dom f = X() & rng f c= {} &
    for e st e in X() holds P[e,f.e] from WELLORD2:sch 1(A3);
    thus thesis by A4;
  end;
  suppose
A5: X() <> {};
    reconsider D = X() as non empty set by A5;
    defpred Q[set,Ordinal] means ex u st u in Rank $2 & P[$1,u];
A6: for e being Element of D ex A st Q[e,A]
    proof
      let e be Element of D;
      consider u such that
A7:   P[e,u] by A1;
A8:   u c= Rank the_rank_of u by Def8;
A9:   u in Rank succ the_rank_of u by A8,Th36;
      thus thesis by A7,A9;
    end;
    consider F be Function such that
A10: dom F = D & for d being Element of D ex A st A = F.d & Q[d,A] &
    for B st Q[d,B] holds A c= B from ORDINAL1:sch 6(A6);
A11: for e st e in X() ex u st u in Rank sup rng F & P[e,u]
    proof
      let e;
      assume
A12:  e in X();
      consider A such that
A13:  A = F.e and
A14:  ex u st u in Rank A & P[e,u]
      and for B st ex u st u in Rank B & P[e,u] holds A c= B by A10,A12;
      consider u such that
A15:  u in Rank A & P[e,u] by A14;
      take u;
A16:  A in rng F by A10,A12,A13,FUNCT_1:def 5;
A17:  A in sup rng F by A16,ORDINAL2:27;
A18:  A c= sup rng F by A17,ORDINAL1:def 2;
A19:  Rank A c= Rank sup rng F by A18,Th43;
      thus thesis by A15,A19;
    end;
A20: ex f being Function st dom f = X() & rng f c= Rank sup rng F &
    for e st e in X() holds P[e, f.e] from WELLORD2:sch 1(A11);
    thus thesis by A20;
  end;
end;

:: from RFINSEQ, 2008.10.10, A.T.

definition
  let F,G be Relation;
  pred F,G are_fiberwise_equipotent means
  :Def9:
  for x be set holds card Coim(F,x) = card Coim(G,x);
  reflexivity;
  symmetry;
end;

Lm3: for F be Function, x be set st not x in rng F holds Coim(F,x) = {}
proof
  let F be Function,x;
  assume
A1: not x in rng F;
A2: now
    assume
A3: rng F meets {x};

A4: ex y being set st y in rng F & y in {x} by A3,XBOOLE_0:3;
    thus contradiction by A1,A4,TARSKI:def 1;
  end;
  thus thesis by A2,RELAT_1:173;
end;

theorem Th83:
  for F,G be Function st F,G are_fiberwise_equipotent holds rng F = rng G
proof
  let F,G be Function;
  assume
A1: F,G are_fiberwise_equipotent;
  thus rng F c= rng G
  proof
    let x;
    assume that
A2: x in rng F and
A3: not x in rng G;
A4: card Coim(F,x) = card Coim(G,x) by A1,Def9;
A5: Coim(F,x), Coim(G,x) are_equipotent by A4,CARD_1:21;
A6: ex y st y in dom F & F.y = x by A2,FUNCT_1:def 5;
A7: Coim(G,x) = {} by A3,Lm3;
A8: x in {x} & F"{x} = {} by A5,A7,CARD_1:46,TARSKI:def 1;
    thus contradiction by A6,A8,FUNCT_1:def 13;
  end;
  let x;
  assume that
A9: x in rng G and
A10: not x in rng F;
A11: card Coim(G,x) = card Coim(F,x) by A1,Def9;
A12: G"{x},F"{x} are_equipotent by A11,CARD_1:21;
A13: ex y st y in dom G & G.y = x by A9,FUNCT_1:def 5;
A14: Coim(F,x) = {} by A10,Lm3;
A15: x in {x} & Coim(G,x) = {} by A12,A14,CARD_1:46,TARSKI:def 1;
  thus contradiction by A13,A15,FUNCT_1:def 13;
end;

theorem
  for F,G,H be Function
  st F,G are_fiberwise_equipotent & F,H are_fiberwise_equipotent holds
  G,H are_fiberwise_equipotent
proof
  let F,G,H be Function;
  assume that
A1: F,G are_fiberwise_equipotent and
A2: F,H are_fiberwise_equipotent;
  let x;
  thus card Coim(G,x) = card Coim(F,x) by A1,Def9
    .= card Coim(H,x) by A2,Def9;
end;

theorem Th85:
  for F,G be Function holds F,G are_fiberwise_equipotent iff
  ex H be Function st dom H = dom F & rng H = dom G & H is one-to-one & F = G*H
proof
  let F,G be Function;
  thus F,G are_fiberwise_equipotent implies
  ex H be Function st dom H = dom F & rng H = dom G & H is one-to-one & F = G*H
  proof
    assume
A1: F,G are_fiberwise_equipotent;
    defpred P[set,set] means $2 is Function &
    for f be Function st $2 = f holds dom f = F"{$1} & rng f = G"{$1} &
    f is one-to-one;
A2: for x st x in rng F ex y st P[x,y]
    proof
      let x;
      assume x in rng F;
A3:   card Coim(F,x) = card Coim(G,x) by A1,Def9;
A4:   F"{x},G"{x} are_equipotent by A3,CARD_1:21;
      consider H be Function such that
A5:   H is one-to-one & dom H = F"{x} & rng H = G"{x}
      by A4,WELLORD2:def 4;
      take H;
      thus H is Function;
      let g be Function;
      assume
A6:   g = H;
      thus thesis by A5,A6;
    end;
    consider W be Function such that
A7: dom W = rng F & for x st x in rng F holds P[x,W.x]
    from NonUniqFuncEx(A2);
    defpred Q[set,set] means for H be Function st H = W.(F.$1) holds $2 = H.$1;
    set df = dom F, dg = dom G;
A8: for x st x in df ex y st y in dg & Q[x,y]
    proof
      let x;
      assume
A9:   x in df;
A10:  F.x in rng F by A9,FUNCT_1:def 5;
      reconsider f = W.(F.x) as Function by A7,A10;
A11:  dom f = F"{F.x} & rng f = G"{F.x} by A7,A10;
      take y = f.x;
A12:  F.x in {F.x} by TARSKI:def 1;
A13:  x in F"{F.x} by A9,A12,FUNCT_1:def 13;
A14:  f.x in G"{F.x} by A11,A13,FUNCT_1:def 5;
      thus y in dg by A14,FUNCT_1:def 13;
      let g be Function;
      assume
A15:  g = W.(F.x);
      thus thesis by A15;
    end;
    consider IT be Function such that
A16: dom IT = df & rng IT c= dg & for x st x in df holds Q[x,IT.x]
    from WELLORD2:sch 1(A8);
    take IT;
    thus dom IT = df by A16;
A17: dom G c= rng IT
    proof
      let x;
      assume
A18:  x in dg;
A19:  G.x in rng G by A18,FUNCT_1:def 5;
A20:  rng F = rng G by A1,Th83;
      reconsider f = W.(G.x) as Function by A7,A19,A20;
A21:  dom f = F"{G.x} by A7,A19,A20;
A22:  rng f = G"{G.x} by A7,A19,A20;
A23:  G.x in {G.x} by TARSKI:def 1;
A24:  x in rng f by A18,A22,A23,FUNCT_1:def 13;
      consider z be set such that
A25:  z in dom f and
A26:  f.z = x by A24,FUNCT_1:def 5;
A27:  z in df by A21,A25,FUNCT_1:def 13;
A28:  F.z in {G.x} by A21,A25,FUNCT_1:def 13;
A29:  F.z = G.x by A28,TARSKI:def 1;
A30:  IT.z = x by A16,A26,A27,A29;
      thus thesis by A16,A27,A30,FUNCT_1:def 5;
    end;
    thus rng IT = dg by A16,A17,XBOOLE_0:def 10;
A31: now
      let x,y;
      assume that
A32:  x in dom IT and
A33:  y in dom IT and
A34:  IT.x = IT.y;
A35:  F.x in rng F by A16,A32,FUNCT_1:def 5;
A36:  F.y in rng F by A16,A33,FUNCT_1:def 5;
      reconsider f1 = W.(F.x), f2 = W.(F.y) as Function by A7,A35,A36;
A37:  IT.x = f1.x & IT.y = f2.y by A16,A32,A33;
A38:  dom f1 = F"{F.x} by A7,A35;
A39:  rng f1 = G"{F.x} by A7,A35;
A40:  f1 is one-to-one by A7,A35;
A41:  dom f2 = F"{F.y} by A7,A36;
A42:  rng f2 = G"{F.y} by A7,A36;
A43:  F.x in {F.x} by TARSKI:def 1;
A44:  F.y in {F.y} by TARSKI:def 1;
A45:  x in F"{F.x} by A16,A32,A43,FUNCT_1:def 13;
A46:  y in F"{F.y} by A16,A33,A44,FUNCT_1:def 13;
A47:  f1.x in rng f1 by A38,A45,FUNCT_1:def 5;
A48:  f2.y in rng f2 by A41,A46,FUNCT_1:def 5;
A49:  f1.x in G"{F.x} /\ G"{F.y} by A34,A37,A39,A42,A47,A48,XBOOLE_0:def 4;
A50:  f1.x in G"({F.x} /\ {F.y}) by A49,FUNCT_1:137;
A51:  G.(f1.x) in {F.x} /\ {F.y} by A50,FUNCT_1:def 13;
A52:  G.(f1.x) in {F.x} by A51,XBOOLE_0:def 4;
A53:  G.(f1.x) in {F.y} by A51,XBOOLE_0:def 4;
A54:  G.(f1.x) = F.x by A52,TARSKI:def 1;
A55:  G.(f1.x) = F.y by A53,TARSKI:def 1;
      thus x = y by A34,A37,A38,A40,A45,A46,A54,A55,FUNCT_1:def 8;
    end;
    thus IT is one-to-one by A31,FUNCT_1:def 8;
A56: dom(G*IT) = df by A16,RELAT_1:46;
A57: now
      let x;
      assume
A58:  x in df;
A59:  F.x in rng F by A58,FUNCT_1:def 5;
      reconsider f = W.(F.x) as Function by A7,A59;
A60:  dom f = F"{F.x} & rng f = G"{F.x} by A7,A59;
A61:  F.x in {F.x} by TARSKI:def 1;
A62:  x in F"{F.x} by A58,A61,FUNCT_1:def 13;
A63:  f.x in G"{F.x} by A60,A62,FUNCT_1:def 5;
A64:  G.(f.x) in {F.x} by A63,FUNCT_1:def 13;
A65:  G.(f.x) = F.x by A64,TARSKI:def 1;
A66:  IT.x = f.x by A16,A58;
      thus F.x = (G*IT).x by A16,A58,A65,A66,FUNCT_1:23;
    end;
    thus thesis by A56,A57,FUNCT_1:9;
  end;
  given H be Function such that
A67: dom H = dom F and
A68: rng H = dom G and
A69: H is one-to-one and
A70: F = G*H;
  let x;
  set t = H|(F"{x});
A71: t is one-to-one by A69,FUNCT_1:84;
A72: dom t = F"{x} by A67,RELAT_1:91,167;
A73: rng t = G"{x}
  proof
    thus rng t c= G"{x}
    proof
      let z be set;
      assume
A74:  z in rng t;
      consider y such that
A75:  y in dom t and
A76:  t.y = z by A74,FUNCT_1:def 5;
A77:  F.y in {x} by A72,A75,FUNCT_1:def 13;
A78:  F.y = x by A77,TARSKI:def 1;
A79:  z = H.y by A75,A76,FUNCT_1:70;
A80:  dom t = dom H /\ F"{x} by RELAT_1:90;
A81:  y in dom H by A75,A80,XBOOLE_0:def 4;
A82:  z in dom G by A68,A79,A81,FUNCT_1:def 5;
A83:  x = G.z by A70,A78,A79,A81,FUNCT_1:23;
A84:  G.z in {x} by A83,TARSKI:def 1;
      thus thesis by A82,A84,FUNCT_1:def 13;
    end;
    let z be set;
    assume
A85: z in G"{x};
A86: z in dom G by A85,FUNCT_1:def 13;
A87: G.z in {x} by A85,FUNCT_1:def 13;
A88: G.z = x by A87,TARSKI:def 1;
    consider y such that
A89: y in dom H and
A90: H.y = z by A68,A86,FUNCT_1:def 5;
A91: F.y = x by A70,A88,A89,A90,FUNCT_1:23;
A92: F.y in {x} by A91,TARSKI:def 1;
A93: y in dom t by A67,A72,A89,A92,FUNCT_1:def 13;
A94: t.y in rng t by A93,FUNCT_1:def 5;
    thus thesis by A90,A93,A94,FUNCT_1:70;
  end;
A95: F"{x},G"{x} are_equipotent by A71,A72,A73,WELLORD2:def 4;
  thus thesis by A95,CARD_1:21;
end;

theorem Th86:
  for F,G be Function holds
  F,G are_fiberwise_equipotent iff for X be set holds card (F"X) = card (G"X)
proof
  let F,G be Function;
  thus F,G are_fiberwise_equipotent implies
  for X be set holds card(F"X) = card(G"X)
  proof
    assume
A1: F,G are_fiberwise_equipotent;
    consider H be Function such that
A2: dom H = dom F and
A3: rng H = dom G and
A4: H is one-to-one and
A5: F = G*H by A1,Th85;
    let X be set;
    set t = H|(F"X);
A6: t is one-to-one by A4,FUNCT_1:84;
A7: dom t = F"X by A2,RELAT_1:91,167;
A8: rng t = G"X
    proof
      thus rng t c= G"X
      proof
        let z be set;
        assume
A9:     z in rng t;
        consider y such that
A10:    y in dom t and
A11:    t.y = z by A9,FUNCT_1:def 5;
A12:    F.y in X by A7,A10,FUNCT_1:def 13;
A13:    z = H.y by A10,A11,FUNCT_1:70;
A14:    dom t = dom H /\ F"X by RELAT_1:90;
A15:    y in dom H by A10,A14,XBOOLE_0:def 4;
A16:    z in dom G by A3,A13,A15,FUNCT_1:def 5;
A17:    G.z in X by A5,A12,A13,A15,FUNCT_1:23;
        thus thesis by A16,A17,FUNCT_1:def 13;
      end;
      let z be set;
      assume
A18:  z in G"X;
A19:  z in dom G by A18,FUNCT_1:def 13;
A20:  G.z in X by A18,FUNCT_1:def 13;
      consider y such that
A21:  y in dom H and
A22:  H.y = z by A3,A19,FUNCT_1:def 5;
A23:  F.y in X by A5,A20,A21,A22,FUNCT_1:23;
A24:  y in dom t by A2,A7,A21,A23,FUNCT_1:def 13;
A25:  t.y in rng t by A24,FUNCT_1:def 5;
      thus thesis by A22,A24,A25,FUNCT_1:70;
    end;
A26: F"X,G"X are_equipotent by A6,A7,A8,WELLORD2:def 4;
    thus thesis by A26,CARD_1:21;
  end;
  assume
A27: for X be set holds card(F"X) = card(G"X);
  thus for x holds card Coim(F,x) = card Coim(G,x) by A27;
end;

theorem
  for D be non empty set, F,G be Function st rng F c= D & rng G c= D &
  for d be Element of D holds card Coim(F,d) = card Coim(G,d)
  holds F,G are_fiberwise_equipotent
proof
  let D be non empty set, F,G be Function;
  assume that
A1: rng F c= D and
A2: rng G c= D;
  assume
A3: for d be Element of D holds card Coim(F,d) = card Coim(G,d);
  let x;
  per cases;
  suppose
A4: not x in rng F;
A5: Coim(F,x) = {} by A4,Lm3;
A6: now
      assume
A7:   x in rng G;
      reconsider d = x as Element of D by A2,A7;
A8:   card Coim(G,d) = card Coim(F,d) by A3;
A9:   G"{d},F"{d} are_equipotent by A8,CARD_1:21;
A10:  G"{x} = {} by A5,A9,CARD_1:46;
      consider y such that
A11:  y in dom G and
A12:  G.y = x by A7,FUNCT_1:def 5;
A13:  G.y in {x} by A12,TARSKI:def 1;
      thus contradiction by A10,A11,A13,FUNCT_1:def 13;
    end;
    thus thesis by A5,A6,Lm3;
  end;
  suppose
A14: x in rng F;
    reconsider d = x as Element of D by A1,A14;
A15: card Coim(F,d) = card Coim(G,d) by A3;
    thus thesis by A15;
  end;
end;

theorem
  for F,G be Function st dom F = dom G holds
  F,G are_fiberwise_equipotent iff ex P be Permutation of dom F st F = G*P
proof
  let F,G be Function;
  assume
A1: dom F = dom G;
  thus F,G are_fiberwise_equipotent implies
  ex P be Permutation of dom F st F = G*P
  proof
    assume
A2: F,G are_fiberwise_equipotent;
    consider I be Function such that
A3: dom I = dom F and
A4: rng I = dom G and
A5: I is one-to-one and
A6: F = G*I by A2,Th85;
    reconsider I as Function of dom F,dom F by A1,A3,A4,FUNCT_2:4;
    reconsider I as Permutation of dom F by A1,A4,A5,FUNCT_2:83;
    take I;
    thus thesis by A6;
  end;
  given P be Permutation of dom F such that
A7: F = G*P;
A8: P is Function of dom F,dom F & dom F = {} implies dom F ={};
A9: rng P = dom F & dom P = dom F by A8,FUNCT_2:def 1,def 3;
  thus thesis by A1,A7,A9,Th85;
end;

theorem
  for F,G be Function
  st F,G are_fiberwise_equipotent holds card dom F = card dom G
proof
  let F,G be Function;
  assume
A1: F,G are_fiberwise_equipotent;
A2: card(F"(rng F)) = card(G"(rng F)) & rng F = rng G by A1,Th83,Th86;
  thus card(dom F) = card(G"(rng G)) by A2,RELAT_1:169
    .= card(dom G) by RELAT_1:169;
end;

:: from CIRCCOMB, 2009.01.26, A.T.

theorem
  for f being set, p being Relation
  for x being set st x in rng p holds the_rank_of x in the_rank_of [p,f]
proof
  let f be set;
  let p be Relation;
  let y be set;
  assume
A1: y in rng p;
  consider x being set such that
A2: [x,y] in p by A1,RELAT_1:def 5;
A3: p in {p,f} by TARSKI:def 2;
A4: {p,f} in {{p,f},{p}} by TARSKI:def 2;
A5: y in {x,y} by TARSKI:def 2;
A6: {x,y} in {{x,y},{x}} by TARSKI:def 2;
A7: the_rank_of y in the_rank_of {x,y} by A5,Th76;
A8: the_rank_of {x,y} in the_rank_of [x,y] by A6,Th76;
A9: the_rank_of p in the_rank_of {p,f} by A3,Th76;
A10: the_rank_of {p,f} in the_rank_of [p,f] by A4,Th76;
A11: the_rank_of y in the_rank_of [x,y] by A7,A8,ORDINAL1:19;
A12: the_rank_of [x,y] in the_rank_of p by A2,Th76;
A13: the_rank_of p in the_rank_of [p,f] by A9,A10,ORDINAL1:19;
A14: the_rank_of y in the_rank_of p by A11,A12,ORDINAL1:19;
  thus thesis by A13,A14,ORDINAL1:19;
end;

