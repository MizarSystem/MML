:: Some properties of special polygonal curves
::  by Andrzej Trybulec and Yatsuka Nakamura
::
:: Received October 22, 1998
:: Copyright (c) 1998 Association of Mizar Users

environ

 vocabularies NUMBERS, NAT_1, XBOOLE_0, FINSEQ_1, ORDINAL4, PARTFUN1, XXREAL_0,
      ARYTM_3, MATRIX_1, MATRIX_2, ZFMISC_1, SUBSET_1, PRE_TOPC, EUCLID,
      REAL_1, CARD_1, ARYTM_1, RELAT_1, SUPINF_2, RLTOPSP1, METRIC_1, RELAT_2,
      CONVEX1, RCOMP_1, CONNSP_1, TARSKI, PCOMPS_1, XREAL_0, ORDINAL1, SPPOL_1,
      MCART_1, GOBOARD1, TREES_1, PSCOMP_1, TOPREAL1, SPRECT_1, SPPOL_2,
      JORDAN3, FUNCT_1, RFINSEQ, GROUP_2, GOBOARD5, GOBOARD9, TOPREAL4,
      JORDAN5D, GOBOARD2, SPRECT_2, COMPLEX1, ORDINAL2, TOPS_1, JORDAN5C,
      FINSEQ_5;
 notations TARSKI, XBOOLE_0, ENUMSET1, ZFMISC_1, SUBSET_1, ORDINAL1, NUMBERS,
      XCMPLX_0, XREAL_0, COMPLEX1, REAL_1, NAT_1, NAT_D, CARD_1, RFINSEQ,
      FUNCT_1, PARTFUN1, FINSEQ_1, FINSEQ_4, FINSEQ_5, MATRIX_1, MATRIX_2,
      SEQM_3, SEQ_4, FINSEQ_6, STRUCT_0, METRIC_1, PRE_TOPC, TOPS_1, COMPTS_1,
      CONNSP_1, PCOMPS_1, RLVECT_1, RLTOPSP1, EUCLID, TOPREAL1, TOPREAL4,
      PSCOMP_1, GOBOARD1, GOBOARD2, SPPOL_1, SPPOL_2, GOBOARD5, GOBOARD9,
      JORDAN3, JORDAN5C, JORDAN5D, SPRECT_1, SPRECT_2, XXREAL_0;
 constructors XXREAL_0, REAL_1, NAT_1, COMPLEX1, FINSEQ_4, REALSET1, RFINSEQ,
      NAT_D, TOPS_1, CONNSP_1, TOPS_2, COMPTS_1, MATRIX_2, TOPMETR, TOPREAL2,
      TOPREAL4, GOBOARD2, SPPOL_1, JORDAN1, PSCOMP_1, GOBOARD9, JORDAN3,
      JORDAN5C, SPRECT_1, SPRECT_2, JORDAN5D, SEQ_1, GOBOARD1, SEQM_3,
      PCOMPS_1, BINARITH, FUNCSDOM, CONVEX1, SEQ_4, RVSUM_1;
 registrations XBOOLE_0, RELSET_1, NUMBERS, XXREAL_0, XREAL_0, MEMBERED,
      FINSEQ_1, REALSET1, STRUCT_0, EUCLID, GOBOARD2, SPPOL_2, SPRECT_1,
      SPRECT_2, VALUED_0, CARD_1, FUNCT_1, SEQ_4, SPPOL_1, TOPS_1;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, RLTOPSP1, SPRECT_2, JORDAN1, TOPREAL4, GOBOARD1, XBOOLE_0,
      PSCOMP_1, SEQM_3;
 theorems FINSEQ_4, TARSKI, FINSEQ_3, FINSEQ_1, SPRECT_2, GOBOARD7, PSCOMP_1,
      SPRECT_1, TOPREAL3, SPPOL_2, NAT_1, SPPOL_1, EUCLID, ZFMISC_1, TOPREAL1,
      GOBOARD5, JORDAN5D, FINSEQ_5, FINSEQ_6, JORDAN4, JORDAN3, TOPREAL5,
      TOPS_1, GOBRD11, GOBOARD9, TOPMETR, GOBOARD6, JORDAN1, CONNSP_1,
      SUBSET_1, GOBRD12, JORDAN5C, JORDAN6, MATRIX_2, GOBOARD1, ENUMSET1,
      CARD_2, GOBOARD2, NAT_2, ABSVALUE, GOBOARD8, PARTFUN2, FUNCT_1, XBOOLE_0,
      XBOOLE_1, XREAL_0, FINSEQ_2, XREAL_1, XXREAL_0, PARTFUN1, MATRIX_1,
      NAT_D, PRE_TOPC, RLTOPSP1, SEQM_3, SEQ_4;
 schemes DOMAIN_1;

begin :: Preliminaries



canceled 10;

theorem
  for D being non empty set for f being non empty FinSequence of D, g
  being FinSequence of D holds (g^f)/.len(g^f) = f/.len f
proof
  let D be non empty set, f be non empty FinSequence of D, g be FinSequence of
  D;
A1: len f >= 1 by NAT_1:14;
  len(g^f) = len g + len f by FINSEQ_1:35;
  hence thesis by A1,SEQ_4:153;
end;

theorem Th12:
  for a,b,c,d being set holds Indices (a,b)][(c,d) = {[1,1],[1,2], [2,1],[2,2]}
proof
  let a,b,c,d be set;
  thus Indices (a,b)][(c,d) = [:Seg 2,Seg 2:] by MATRIX_2:3
    .= [:{1},Seg 2:] \/ [:{2},Seg 2:] by FINSEQ_1:4,ZFMISC_1:132
    .= [:{1},Seg 2:] \/ {[2,1], [2,2]} by FINSEQ_1:4,ZFMISC_1:36
    .= { [1,1], [1,2]} \/ {[2,1], [2,2]} by FINSEQ_1:4,ZFMISC_1:36
    .= {[1,1],[1,2],[2,1],[2,2]} by ENUMSET1:45;
end;

begin :: Euclidean Space

reserve i,j,k,n,m for Element of NAT;

theorem Th13:
  for p,q being Point of TOP-REAL n, r being Real st 0 < r & p = (
  1-r)*p+r*q holds p = q
proof
  let p,q be Point of TOP-REAL n, r be Real such that
A1: 0 < r and
A2: p = (1-r)*p+r*q;
A3: (1-r)*p+r*p = ((1-r)+r)*p by EUCLID:37
    .= p by EUCLID:33;
  r*p = r*p + 0.TOP-REAL n by EUCLID:31
    .= r*p + ((1-r)*p + -(1-r)*p) by EUCLID:40
    .= r*q + (1-r)*p + -(1-r)*p by A2,A3,EUCLID:30
    .= r*q + ((1-r)*p + -(1-r)*p) by EUCLID:30
    .= r*q + 0.TOP-REAL n by EUCLID:40
    .= r*q by EUCLID:31;
  hence thesis by A1,EUCLID:38;
end;

theorem Th14:
  for p,q being Point of TOP-REAL n, r being Real st r < 1 & p = (
  1-r)*q+r*p holds p = q
proof
  let p,q be Point of TOP-REAL n, r be Real such that
A1: r < 1 and
A2: p = (1-r)*q+r*p;
  set s = 1 -r;
  r + 0 < 1 by A1;
  then
A3: 0 < 1 - r by XREAL_1:22;
  p = (1-s)*p+s*q by A2;
  hence thesis by A3,Th13;
end;

theorem
  for p,q being Point of TOP-REAL n st p = 1/2*(p+q) holds p = q
proof
  let p,q be Point of TOP-REAL n;
  assume p = 1/2*(p+q);
  then p = (1 - 1/2)*p + 1/2*q by EUCLID:36;
  hence thesis by Th13;
end;

theorem Th16:
  for p,q,r being Point of TOP-REAL n st q in LSeg(p,r) & r in
  LSeg(p,q) holds q = r
proof
  let p,q,r be Point of TOP-REAL n;
  assume q in LSeg(p,r);
  then consider r1 being Real such that
A1: q = (1-r1)*p+r1*r and
A2: 0<=r1 and
A3: r1<=1;
  assume r in LSeg(p,q);
  then consider r2 being Real such that
A4: r = (1-r2)*p+r2*q and
  0<=r2 and
A5: r2<=1;
A6: r1*r2 <= 1 by A2,A3,A5,XREAL_1:162;
A7: (1-r2)+r2*(1-r1) = 1 - r2*r1;
A8: r = (1-r2)*p+(r2*((1-r1)*p)+r2*(r1*r)) by A1,A4,EUCLID:36
    .= (1-r2)*p+r2*((1-r1)*p)+r2*(r1*r) by EUCLID:30
    .= (1-r2)*p+r2*(1-r1)*p+r2*(r1*r) by EUCLID:34
    .= (1 - r2*r1)*p+r2*(r1*r) by A7,EUCLID:37
    .= (1 - r2*r1)*p+r2*r1*r by EUCLID:34;
A9: (1-r1)+r1*(1-r2) = 1 - r1*r2;
A10: q = (1-r1)*p+(r1*((1-r2)*p)+r1*(r2*q)) by A1,A4,EUCLID:36
    .= (1-r1)*p+r1*((1-r2)*p)+r1*(r2*q) by EUCLID:30
    .= (1-r1)*p+r1*(1-r2)*p+r1*(r2*q) by EUCLID:34
    .= (1 - r1*r2)*p+r1*(r2*q) by A9,EUCLID:37
    .= (1 - r1*r2)*p+r1*r2*q by EUCLID:34;
  per cases by A6,XXREAL_0:1;
  suppose
A11: r1*r2 = 1;
    then 1 <= r1 or 1 <= r2 by A2,XREAL_1:164;
    then 1*r1 = 1 or 1*r2 = 1 by A3,A5,XXREAL_0:1;
    hence q = 0 * p+r by A1,A11,EUCLID:33
      .= 0.TOP-REAL n+r by EUCLID:33
      .= r by EUCLID:31;
  end;
  suppose
A12: r1*r2 < 1;
    hence q = p by A10,Th14
      .= r by A8,A12,Th14;
  end;
end;

begin :: Euclidean Plane

theorem Th17:
  for A being non empty Subset of TOP-REAL 2, p being Element of
  Euclid 2, r being Real st A = Ball(p,r) holds A is connected
proof
  let A be non empty Subset of TOP-REAL 2, p be Element of Euclid 2, r be Real
  such that
A1: A = Ball(p,r);
  A is convex
  proof
    let w1,w2 be Point of TOP-REAL 2;
    assume that
A2: w1 in A and
A3: w2 in A;
    thus thesis by A1,A2,A3,TOPREAL3:28;
  end;
  hence thesis by JORDAN1:9;
end;

theorem Th18:
  for A, B being Subset of TOP-REAL 2 st A is open & B
  is_a_component_of A holds B is open
proof
  let A, B be Subset of TOP-REAL 2 such that
A1: A is open and
A2: B is_a_component_of A;
A3: B c= A by A2,SPRECT_1:7;
A4: the TopStruct of TOP-REAL 2 = TopSpaceMetr Euclid 2 by EUCLID:def 8;
  then reconsider C = B, D = A as Subset of TopSpaceMetr Euclid 2;
A5: D is open by A1,A4,PRE_TOPC:60;
  for p being Point of Euclid 2 st p in C ex r being real number st r>0 &
  Ball(p,r) c= C
  proof
    let p be Point of Euclid 2;
    assume
A6: p in C;
    then consider r being real number such that
A7: r > 0 and
A8: Ball(p,r) c= D by A3,A5,TOPMETR:22;
    reconsider r as Real by XREAL_0:def 1;
    take r;
    thus r>0 by A7;
    reconsider E = Ball(p,r) as Subset of TOP-REAL 2 by A4,TOPMETR:16;
A9: p in E by A7,GOBOARD6:4;
    then
A10: E is connected by Th17;
    B meets E by A6,A9,XBOOLE_0:3;
    hence thesis by A2,A8,A10,GOBOARD9:6;
  end;
  then C is open by TOPMETR:22;
  hence B is open by A4,PRE_TOPC:60;
end;

canceled 2;

theorem
  for p,q,r,s being Point of TOP-REAL 2 st LSeg(p,q) is horizontal &
  LSeg(r,s) is horizontal & LSeg(p,q) meets LSeg(r,s) holds p`2= r`2
proof
  let p,q,r,s be Point of TOP-REAL 2 such that
A1: LSeg(p,q) is horizontal and
A2: LSeg(r,s) is horizontal;
  assume LSeg(p,q) meets LSeg(r,s);
  then LSeg(p,q) /\ LSeg(r,s) <> {} by XBOOLE_0:def 7;
  then consider x being Point of TOP-REAL 2 such that
A3: x in LSeg(p,q) /\ LSeg(r,s) by SUBSET_1:10;
A4: x in LSeg(r,s) by A3,XBOOLE_0:def 4;
  x in LSeg(p,q) by A3,XBOOLE_0:def 4;
  hence p`2 = x`2 by A1,SPPOL_1:63
    .= r`2 by A2,A4,SPPOL_1:63;
end;

theorem
  for p,q,r being Point of TOP-REAL 2 st LSeg(p,q) is vertical & LSeg(q,
  r) is horizontal holds LSeg(p,q) /\ LSeg(q,r) = {q}
proof
  let p,q,r be Point of TOP-REAL 2 such that
A1: LSeg(p,q) is vertical and
A2: LSeg(q,r) is horizontal;
  for x being set holds x in LSeg(p,q) /\ LSeg(q,r) iff x = q
  proof
    let x be set;
    thus x in LSeg(p,q) /\ LSeg(q,r) implies x = q
    proof
      assume
A3:   x in LSeg(p,q) /\ LSeg(q,r);
      then reconsider x as Point of TOP-REAL 2;
      x in LSeg(q,r) by A3,XBOOLE_0:def 4;
      then
A4:   x`2 = q`2 by A2,SPPOL_1:63;
      x in LSeg(p,q) by A3,XBOOLE_0:def 4;
      then x`1 = q`1 by A1,SPPOL_1:64;
      hence thesis by A4,TOPREAL3:11;
    end;
    assume
A5: x = q;
    then
A6: x in LSeg(q,r) by RLTOPSP1:69;
    x in LSeg(p,q) by A5,RLTOPSP1:69;
    hence thesis by A6,XBOOLE_0:def 4;
  end;
  hence thesis by TARSKI:def 1;
end;

theorem
  for p,q,r,s being Point of TOP-REAL 2 st LSeg(p,q) is horizontal &
  LSeg(s,r) is vertical & r in LSeg(p,q) holds LSeg(p,q) /\ LSeg(s,r) = {r}
proof
  let p,q,r,s be Point of TOP-REAL 2 such that
A1: LSeg(p,q) is horizontal and
A2: LSeg(s,r) is vertical and
A3: r in LSeg(p,q);
  for x being set holds x in LSeg(p,q) /\ LSeg(s,r) iff x = r
  proof
    let x be set;
    thus x in LSeg(p,q) /\ LSeg(s,r) implies x = r
    proof
      assume
A4:   x in LSeg(p,q) /\ LSeg(s,r);
      then reconsider x as Point of TOP-REAL 2;
      x in LSeg(p,q) by A4,XBOOLE_0:def 4;
      then
A5:   x`2 = p`2 by A1,SPPOL_1:63;
      x in LSeg(s,r) by A4,XBOOLE_0:def 4;
      then
A6:   x`1 = r`1 by A2,SPPOL_1:64;
      p`2 = r`2 by A1,A3,SPPOL_1:63;
      hence thesis by A5,A6,TOPREAL3:11;
    end;
    assume
A7: x = r;
    then x in LSeg(s,r) by RLTOPSP1:69;
    hence thesis by A3,A7,XBOOLE_0:def 4;
  end;
  hence thesis by TARSKI:def 1;
end;

begin :: Main

reserve p,q for Point of TOP-REAL 2;
reserve G for Go-board;

theorem
  1 <= j & j <= k & k <= width G & 1 <= i & i <= len G implies G*(i,j)`2
  <= G*(i,k)`2
proof
  assume that
A1: 1 <= j and
A2: j <= k and
A3: k <= width G and
A4: 1 <= i and
A5: i <= len G;
  per cases by A2,XXREAL_0:1;
  suppose
    j < k;
    hence thesis by A1,A3,A4,A5,GOBOARD5:5;
  end;
  suppose
    j = k;
    hence thesis;
  end;
end;

theorem
  1 <= j & j <= width G & 1 <= i & i <= k & k <= len G implies G*(i,j)`1
  <= G*(k,j)`1
proof
  assume that
A1: 1 <= j and
A2: j <= width G and
A3: 1 <= i and
A4: i <= k and
A5: k <= len G;
  per cases by A4,XXREAL_0:1;
  suppose
    i < k;
    hence thesis by A1,A2,A3,A5,GOBOARD5:4;
  end;
  suppose
    i = k;
    hence thesis;
  end;
end;

reserve C for Subset of TOP-REAL 2;

theorem Th26:
  LSeg(NW-corner C,NE-corner C) c= L~SpStSeq C
proof
A1: LSeg(NW-corner C,NE-corner C) \/ LSeg(NE-corner C,SE-corner C) c= (LSeg(
NW-corner C,NE-corner C) \/ LSeg(NE-corner C,SE-corner C)) \/ (LSeg(SE-corner C
  ,SW-corner C) \/ LSeg(SW-corner C,NW-corner C)) by XBOOLE_1:7;
  LSeg(NW-corner C,NE-corner C) c= LSeg(NW-corner C,NE-corner C) \/ LSeg(
  NE-corner C,SE-corner C) by XBOOLE_1:7;
  then
  LSeg(NW-corner C,NE-corner C) c= (LSeg(NW-corner C,NE-corner C) \/ LSeg(
NE-corner C,SE-corner C)) \/ (LSeg(SE-corner C,SW-corner C) \/ LSeg(SW-corner C
  ,NW-corner C)) by A1,XBOOLE_1:1;
  hence thesis by SPRECT_1:43;
end;

canceled;

theorem Th28:
  for C being non empty compact Subset of TOP-REAL 2 holds N-min C
  in LSeg(NW-corner C,NE-corner C)
proof
  let C be non empty compact Subset of TOP-REAL 2;
A1: N-most C c= LSeg(NW-corner C,NE-corner C) by XBOOLE_1:17;
  N-min C in N-most C by PSCOMP_1:101;
  hence thesis by A1;
end;

registration
  let C;
  cluster LSeg(NW-corner C,NE-corner C) -> horizontal;
coherence  
proof
  (NW-corner C)`2 = N-bound C by EUCLID:56
    .= (NE-corner C)`2 by EUCLID:56;
  hence thesis by SPPOL_1:36;
end;
end;

canceled 2;

theorem
  for g being FinSequence of TOP-REAL 2, p being Point of TOP-REAL 2 st
g/.1 <> p & ((g/.1)`1 = p`1 or (g/.1)`2 = p`2) & g is being_S-Seq & LSeg(p,g/.1
  ) /\ L~g={ g/.1 } holds <*p*>^g is being_S-Seq
proof
  let g be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2 such that
A1: g/.1 <> p and
A2: (g/.1)`1 = p`1 or (g/.1)`2 = p`2 and
A3: g is being_S-Seq and
A4: LSeg(p,g/.1) /\ L~g={ g/.1 };
  set f = <*p,g/.1*>;
A5: f is being_S-Seq by A1,A2,SPPOL_2:46;
  reconsider g9 = g as S-Sequence_in_R2 by A3;
A6: 1 in dom g9 by FINSEQ_5:6;
A7: len f = 1+1 by FINSEQ_1:61;
  then len f -' 1 = 1 by NAT_D:34;
  then
A8: mid(f,1,len f-'1) = <*f/.1*> by A7,JORDAN4:27
    .= <*p*> by FINSEQ_4:26;
A9: L~f /\ L~g ={ g/.1 } by A4,SPPOL_2:21
    .={g.1} by A6,PARTFUN1:def 8;
  f.len f = g/.1 by A7,FINSEQ_1:61
    .= g.1 by A6,PARTFUN1:def 8;
  hence thesis by A3,A5,A9,A8,JORDAN3:80;
end;

canceled;

theorem
  for f being S-Sequence_in_R2, p being Point of TOP-REAL 2 st 1 <j & j
  <= len f & p in L~mid(f,1,j) holds LE p, f/.j, L~f, f/.1, f/.len f
proof
  let f be S-Sequence_in_R2, p be Point of TOP-REAL 2 such that
A1: 1 <j and
A2: j <= len f and
A3: p in L~mid(f,1,j);
  consider i such that
A4: 1 <= i and
A5: i+1 <= len mid(f,1,j) and
A6: p in LSeg(mid(f,1,j),i) by A3,SPPOL_2:13;
A7: j -' 1 + 1 = j by A1,XREAL_1:237;
  then
A8: i+1 <= j by A1,A2,A5,JORDAN4:20;
  then i < j-'1+1 by A7,NAT_1:13;
  then
A9: LSeg(mid(f,1,j),i) = LSeg(f,i+1-'1) by A1,A2,A4,JORDAN4:31;
  1 <= i+1 by NAT_1:11;
  then
A10: LE f/.(i+1), f/.j, L~f, f/.1, f/.len f by A2,A8,JORDAN5C:24;
A11: i = i + 1 -' 1 by NAT_D:34;
  i+1 <= len f by A2,A8,XXREAL_0:2;
  then LE p, f/.(i+1), L~f, f/.1, f/.len f by A4,A6,A11,A9,JORDAN5C:26;
  hence thesis by A10,JORDAN5C:13;
end;

theorem :: JORDAN4:47
  for h being FinSequence of TOP-REAL 2 st i in dom h & j in dom h holds
  L~mid(h,i,j) c= L~h
proof
  let h be FinSequence of TOP-REAL 2;
  assume that
A1: i in dom h and
A2: j in dom h;
A3: i <= len h by A1,FINSEQ_3:27;
A4: j <= len h by A2,FINSEQ_3:27;
A5: 1 <= j by A2,FINSEQ_3:27;
  1 <= i by A1,FINSEQ_3:27;
  hence thesis by A3,A5,A4,JORDAN4:47;
end;

theorem
  1 <= i & i < j implies for f being FinSequence of TOP-REAL 2 st j <=
  len f holds L~mid(f,i,j) = LSeg(f,i) \/ L~mid(f,i+1,j)
proof
  assume that
A1: 1 <= i and
A2: i < j;
  let f be FinSequence of TOP-REAL 2 such that
A3: j <= len f;
  set A = { LSeg(f,k): i <= k & k < j}, B = { LSeg(f,k): i+1 <= k & k < j};
A4: A = B \/ {LSeg(f,i)}
  proof
    thus A c= B \/ {LSeg(f,i)}
    proof
      let e be set;
      assume e in A;
      then consider k such that
A5:   e = LSeg(f,k) and
A6:   i <= k and
A7:   k < j;
      i = k or i < k by A6,XXREAL_0:1;
      then i = k or i+1 <= k by NAT_1:13;
      then e in B or e in {LSeg(f,i)} by A5,A7,TARSKI:def 1;
      hence thesis by XBOOLE_0:def 3;
    end;
    let e be set;
    assume
A8: e in B \/ {LSeg(f,i)};
    per cases by A8,XBOOLE_0:def 3;
    suppose
      e in B;
      then consider k such that
A9:   e = LSeg(f,k) and
A10:  i+1 <= k and
A11:  k < j;
      i < k by A10,NAT_1:13;
      hence thesis by A9,A11;
    end;
    suppose
      e in {LSeg(f,i)};
      then e = LSeg(f,i) by TARSKI:def 1;
      hence thesis by A2;
    end;
  end;
A12: 1 <= i+1 by NAT_1:11;
A13: i+1 <= j by A2,NAT_1:13;
  thus L~mid(f,i,j) = union A by A1,A2,A3,SPRECT_2:18
    .= union B \/ union{LSeg(f,i)} by A4,ZFMISC_1:96
    .= union B \/ LSeg(f,i) by ZFMISC_1:31
    .= LSeg(f,i) \/ L~mid(f,i+1,j) by A3,A12,A13,SPRECT_2:18;
end;

theorem
  for f being FinSequence of TOP-REAL 2 st 1 <= i holds i < j & j <= len
  f implies L~mid(f,i,j) = L~mid(f,i,j -' 1) \/ LSeg(f,j -' 1)
proof
  let f be FinSequence of TOP-REAL 2 such that
A1: 1 <= i and
A2: i < j and
A3: j <= len f;
  j -' 1 <= j by NAT_D:35;
  then
A4: j -' 1 <= len f by A3,XXREAL_0:2;
  set A = { LSeg(f,k): i <= k & k < j}, B = { LSeg(f,k): i <= k & k < j -' 1};
A5: i <= j -' 1 by A2,NAT_D:49;
A6: A = B \/ {LSeg(f,j -' 1)}
  proof
    thus A c= B \/ {LSeg(f,j -' 1)}
    proof
      let e be set;
      assume e in A;
      then consider k such that
A7:   e = LSeg(f,k) and
A8:   i <= k and
A9:   k < j;
      k <= j -' 1 by A9,NAT_D:49;
      then j -' 1 = k or k < j -' 1 by XXREAL_0:1;
      then e in B or e in {LSeg(f,j -' 1)} by A7,A8,TARSKI:def 1;
      hence thesis by XBOOLE_0:def 3;
    end;
    let e be set;
A10: j -' 1 <= j by NAT_D:35;
    assume
A11: e in B \/ {LSeg(f,j -' 1)};
    per cases by A11,XBOOLE_0:def 3;
    suppose
      e in B;
      then consider k such that
A12:  e = LSeg(f,k) and
A13:  i <= k and
A14:  k < j -' 1;
      k < j by A10,A14,XXREAL_0:2;
      hence thesis by A12,A13;
    end;
    suppose
A15:  e in {LSeg(f,j -' 1)};
      1 <= j -' 1 by A1,A5,XXREAL_0:2;
      then
A16:  j-' 1 < j by NAT_D:51;
      e = LSeg(f,j -' 1) by A15,TARSKI:def 1;
      hence thesis by A5,A16;
    end;
  end;
  thus L~mid(f,i,j) = union A by A1,A2,A3,SPRECT_2:18
    .= union B \/ union{LSeg(f,j -' 1)} by A6,ZFMISC_1:96
    .= union B \/ LSeg(f,j -' 1) by ZFMISC_1:31
    .= L~mid(f,i,j -' 1) \/ LSeg(f,j -' 1) by A1,A5,A4,SPRECT_2:18;
end;

canceled;

theorem
  for f, g being FinSequence of TOP-REAL 2 st f is being_S-Seq & g is
being_S-Seq & ((f/.len f)`1 = (g/.1)`1 or (f/.len f)`2 = (g/.1)`2) & L~f misses
L~g & LSeg(f/.len f,g/.1) /\ L~f={ f/.len f } & LSeg(f/.len f,g/.1) /\ L~g={ g
  /.1 } holds f^g is being_S-Seq
proof
  let f,g be FinSequence of TOP-REAL 2 such that
A1: f is being_S-Seq and
A2: g is being_S-Seq and
A3: (f/.len f)`1 = (g/.1)`1 or (f/.len f)`2 = (g/.1)`2 and
A4: L~f misses L~g and
A5: LSeg(f/.len f,g/.1) /\ L~f={ f/.len f } and
A6: LSeg(f/.len f,g/.1) /\ L~g={ g/.1 };
A7: len g >= 2 by A2,TOPREAL1:def 10;
  then
A8: len g >= 1 by XXREAL_0:2;
  then 1 in dom g by FINSEQ_3:27;
  then
A9: g/.1 in L~g by A7,GOBOARD1:16;
  set h = <*f/.len f*>^g;
A10: len f >= 2 by A1,TOPREAL1:def 10;
  then 1 <= len f by XXREAL_0:2;
  then
A11: len f in dom f by FINSEQ_3:27;
  then
A12: f.len f = f/.len f by PARTFUN1:def 8
    .=h.1 by FINSEQ_1:58;
A13: len h = len<*f/.len f*>+len g by FINSEQ_1:35
    .= len g + 1 by FINSEQ_1:56;
  then len h >= 1+1 by A8,XREAL_1:8;
  then
A14: mid(h,2,len h) = (h/^(1+1-'1))|(len h-'2+1) by FINSEQ_6:def 3
    .= (h/^1)|(len h-'2+1) by NAT_D:34
    .= g|(len h-'2+1) by FINSEQ_6:49
    .= g|(len h-'1-'1+1) by NAT_D:45
    .= g|(len g-'1+1) by A13,NAT_D:34
    .= g|len g by A7,XREAL_1:237,XXREAL_0:2
    .= g by FINSEQ_1:79;
  f/.len f in L~f by A10,A11,GOBOARD1:16;
  then f/.len f <> g/.1 by A4,A9,XBOOLE_0:3;
  then
A15: h is being_S-Seq by A2,A3,A6,SPRECT_2:64;
  L~f /\ L~h = L~f /\ (LSeg(f/.len f,g/.1) \/ L~g) by A2,SPPOL_2:20
    .= L~f /\ LSeg(f/.len f,g/.1) \/ L~f /\ L~g by XBOOLE_1:23
    .= L~f /\ LSeg(f/.len f,g/.1) \/ {} by A4,XBOOLE_0:def 7
    .={h.1} by A5,A11,A12,PARTFUN1:def 8;
  hence thesis by A1,A12,A15,A14,JORDAN3:73;
end;

theorem
  for f be FinSequence of TOP-REAL 2 for p be Point of TOP-REAL 2 st p
  in L~f holds R_Cut(f,p)/.1 = f/.1
proof
  let f be FinSequence of TOP-REAL 2;
  let p be Point of TOP-REAL 2;
  set i = Index(p,f);
  assume
A1: p in L~f;
  then
A2: 1 <= i by JORDAN3:41;
  i <= len f by A1,JORDAN3:41;
  then f <> {} by A2;
  then
A3: 1 in dom f by FINSEQ_5:6;
  p = f.1 or p <> f.1;
  then len R_Cut(f,p) = Index(p,f) or len R_Cut(f,p) = Index(p,f) + 1 by A1,
JORDAN3:60;
  then 1 <= len R_Cut(f,p) by A1,JORDAN3:41,NAT_1:11;
  hence (R_Cut(f,p))/.1 = R_Cut(f,p).1 by FINSEQ_4:24
    .= f.1 by A1,A2,JORDAN3:59
    .= f/.1 by A3,PARTFUN1:def 8;
end;

theorem
  for f being S-Sequence_in_R2, p,q being Point of TOP-REAL 2 st 1 <=j &
j < len f & p in LSeg(f,j) & q in LSeg(f/.j,p) holds LE q, p, L~f, f/.1, f/.len
  f
proof
  let f be S-Sequence_in_R2, p,q be Point of TOP-REAL 2 such that
A1: 1 <=j and
A2: j < len f and
A3: p in LSeg(f,j) and
A4: q in LSeg(f/.j,p);
A5: j+1 <= len f by A2,NAT_1:13;
  then
A6: LSeg(f,j) = LSeg(f/.j,f/.(j+1)) by A1,TOPREAL1:def 5;
A7: f/.j in LSeg(f,j) by A1,A5,TOPREAL1:27;
  then
A8: LSeg(f/.j,p) c= LSeg(f,j) by A3,A6,TOPREAL1:12;
  then
A9: q in LSeg(f,j) by A4;
A10: LSeg(f,j) c= L~f by TOPREAL3:26;
  per cases;
  suppose
    p = q;
    hence thesis by A3,A10,JORDAN5C:9;
  end;
  suppose
A11: q <> p;
    for i, j being Element of NAT st q in LSeg(f,i) & p in LSeg(f,j) & 1
<= i & i+1 <= len f & 1 <= j & j+1 <= len f holds i <= j & (i = j implies LE q,
    p,f/.i,f/.(i+1))
    proof
      1 <= j+1 by NAT_1:11;
      then
A12:  j+1 in dom f by A5,FINSEQ_3:27;
      let i1, i2 be Element of NAT such that
A13:  q in LSeg(f,i1) and
A14:  p in LSeg(f,i2) and
      1 <= i1 and
A15:  i1+1 <= len f and
A16:  1 <= i2 and
      i2+1 <= len f;
A17:  p in LSeg(f,i2) /\ LSeg(f,j) by A3,A14,XBOOLE_0:def 4;
      then
A18:  LSeg(f,i2) meets LSeg(f,j) by XBOOLE_0:4;
      then
A19:  j + 1 >= i2 by TOPREAL1:def 9;
A20:  q in LSeg(f,i1) /\ LSeg(f,j) by A4,A8,A13,XBOOLE_0:def 4;
      then
A21:  LSeg(f,i1) meets LSeg(f,j) by XBOOLE_0:4;
      then
A22:  i1 + 1 >= j by TOPREAL1:def 9;
A23:  now
        assume
A24:    i2 + 1 = j;
        i2+(1+1) = i2+1+1;
        then i2+2 <= len f by A2,A24,NAT_1:13;
        then p in {f/.j} by A16,A17,A24,TOPREAL1:def 8;
        then p = f/.j by TARSKI:def 1;
        then q in {p} by A4,RLTOPSP1:71;
        hence contradiction by A11,TARSKI:def 1;
      end;
A25:  now
        assume that
A26:    i2 + 1 > j and
A27:    j+1 > i2;
A28:    j <= i2 by A26,NAT_1:13;
        i2 <= j by A27,NAT_1:13;
        hence i2 = j by A28,XXREAL_0:1;
      end;
      i2 + 1 >= j by A18,TOPREAL1:def 9;
      then i2 + 1 = j or i2 = j or j + 1 = i2 by A25,A19,XXREAL_0:1;
      then
A29:  i2 >= j by A23,NAT_1:11;
A30:  now
        assume that
A31:    i1 + 1 > j and
A32:    j+1 > i1;
A33:    j <= i1 by A31,NAT_1:13;
        i1 <= j by A32,NAT_1:13;
        hence i1 = j by A33,XXREAL_0:1;
      end;
A34:  j in dom f by A1,A2,FINSEQ_3:27;
A35:  now
        assume f/.(j+1)=f/.j;
        then j = j+1 by A12,A34,PARTFUN2:17;
        hence contradiction;
      end;
A36:  now
A37:    j+(1+1) = j+1+1;
        assume i1 = j + 1;
        then q in {f/.(j+1)} by A1,A15,A20,A37,TOPREAL1:def 8;
        then q = f/.(j+1) by TARSKI:def 1;
        hence contradiction by A3,A4,A6,A7,A11,A35,SPPOL_1:24,TOPREAL1:12;
      end;
A38:  j + 1 >= i1 by A21,TOPREAL1:def 9;
      then i1 + 1 = j or i1 = j or j + 1 = i1 by A30,A22,XXREAL_0:1;
      then i1 <= j by A36,NAT_1:11;
      hence i1 <= i2 by A29,XXREAL_0:2;
      assume
A39:  i1 = i2;
      not p in LSeg(f/.j,q) by A4,A11,Th16;
      then not LE p,q,f/.j,f/.(j+1) by JORDAN3:65;
      then LT q,p,f/.j,f/.(j+1) by A3,A6,A13,A23,A30,A22,A38,A35,A36,A39,
JORDAN3:63,XXREAL_0:1;
      hence thesis by A23,A30,A22,A38,A36,A39,JORDAN3:def 7,XXREAL_0:1;
    end;
    hence thesis by A3,A9,A10,A11,JORDAN5C:30;
  end;
end;

begin :: Special circular

theorem Th41:
  for f being non constant standard special_circular_sequence
  holds LeftComp f is open & RightComp f is open
proof
  let f be non constant standard special_circular_sequence;
A1: (L~f)` is open;
  LeftComp f is_a_component_of (L~f)` by GOBOARD9:def 1;
  hence LeftComp f is open by Th18;
  RightComp f is_a_component_of (L~f)` by GOBOARD9:def 2;
  hence thesis by Th18;
end;

registration
  let f be non constant standard special_circular_sequence;
  cluster L~f -> non vertical non horizontal;
  coherence
  proof
    W-bound L~f <> E-bound L~f by TOPREAL5:23;
    hence L~f is non vertical by SPRECT_1:17;
    S-bound L~f <> N-bound L~f by TOPREAL5:22;
    hence thesis by SPRECT_1:18;
  end;
  cluster LeftComp f -> being_Region;
  coherence
  proof
    thus LeftComp f is open by Th41;
    LeftComp f is_a_component_of (L~f)` by GOBOARD9:def 1;
    then consider A being Subset of (TOP-REAL 2)|(L~f)` such that
A1: A = LeftComp f and
A2: A is a_component by CONNSP_1:def 6;
    A is connected by A2,CONNSP_1:def 5;
    hence thesis by A1,CONNSP_1:24;
  end;
  cluster RightComp f -> being_Region;
  coherence
  proof
    thus RightComp f is open by Th41;
    RightComp f is_a_component_of (L~f)` by GOBOARD9:def 2;
    then consider A being Subset of (TOP-REAL 2)|(L~f)` such that
A3: A = RightComp f and
A4: A is a_component by CONNSP_1:def 6;
    A is connected by A4,CONNSP_1:def 5;
    hence thesis by A3,CONNSP_1:24;
  end;
end;

theorem Th42:
  for f being non constant standard special_circular_sequence
  holds RightComp f misses L~f
proof
  let f be non constant standard special_circular_sequence;
  (L~f)` =LeftComp f \/ RightComp f by GOBRD12:11;
  then RightComp f c= (L~f)` by XBOOLE_1:7;
  hence thesis by SUBSET_1:43;
end;

theorem Th43:
  for f being non constant standard special_circular_sequence
  holds LeftComp f misses L~f
proof
  let f be non constant standard special_circular_sequence;
  (L~f)` =LeftComp f \/ RightComp f by GOBRD12:11;
  then LeftComp f c= (L~f)` by XBOOLE_1:7;
  hence thesis by SUBSET_1:43;
end;

theorem Th44:
  for f being non constant standard special_circular_sequence
  holds i_w_n f < i_e_n f
proof
  let f be non constant standard special_circular_sequence;
  set G = GoB f;
A1: i_w_n f <= len G by JORDAN5D:47;
A2: G*(i_w_n f,width G) = N-min L~f by JORDAN5D:def 7;
  assume
A3: i_w_n f >= i_e_n f;
A4: 1 <= i_e_n f by JORDAN5D:47;
A5: (N-min L~f)`1 < (N-max L~f)`1 by SPRECT_2:55;
A6: G*(i_e_n f,width G) = N-max L~f by JORDAN5D:def 8;
  then i_w_n f <> i_e_n f by A5,JORDAN5D:def 7;
  then
A7: i_w_n f > i_e_n f by A3,XXREAL_0:1;
  width G > 1 by GOBOARD7:35;
  hence contradiction by A1,A2,A4,A6,A5,A7,GOBOARD5:4;
end;

theorem Th45:
  for f being non constant standard special_circular_sequence ex i
  st 1 <= i & i < len GoB f & N-min L~f = (GoB f)*(i,width GoB f)
proof
  let f be non constant standard special_circular_sequence;
  take i = i_w_n f;
  thus 1 <= i by JORDAN5D:47;
  i_e_n f <= len GoB f by JORDAN5D:47;
  hence i < len GoB f by Th44,XXREAL_0:2;
  thus thesis by JORDAN5D:def 7;
end;

theorem Th46:
  for f being clockwise_oriented (non constant standard
special_circular_sequence) st i in dom GoB f & f/.1 = (GoB f)*(i,width GoB f) &
  f/.1 = N-min L~f holds f/.2 = (GoB f)*(i+1,width GoB f) & f/.(len f -' 1) = (
  GoB f)*(i,width GoB f -' 1)
proof
  let f be clockwise_oriented (non constant standard special_circular_sequence
  ) such that
A1: i in dom GoB f and
A2: f/.1 = (GoB f)*(i,width GoB f) and
A3: f/.1 = N-min L~f;
A4: i <= len GoB f by A1,FINSEQ_3:27;
  4 < len f by GOBOARD7:36;
  then
A5: len f -' 1 + 1 = len f by XREAL_1:237,XXREAL_0:2;
A6: f/.len f = f/.1 by FINSEQ_6:def 1;
A7: 1 <= len f by GOBOARD7:36,XXREAL_0:2;
  then
A8: 1 in dom f by FINSEQ_3:27;
A9: 1 <= width GoB f by GOBRD11:34;
A10: f/.2 in N-most L~f by A3,SPRECT_2:34;
A11: 1+1 <= len f by GOBOARD7:36,XXREAL_0:2;
  then
A12: 1+1 in dom f by FINSEQ_3:27;
  then consider i1,j1 being Element of NAT such that
A13: [i1,j1] in Indices GoB f and
A14: f/.2 = (GoB f)*(i1,j1) by GOBOARD2:25;
A15: 1 <= j1 by A13,MATRIX_1:39;
A16: j1 <= width GoB f by A13,MATRIX_1:39;
A17: 1 <= i1 by A13,MATRIX_1:39;
A18: i1 <= len GoB f by A13,MATRIX_1:39;
  now
A19: (f/.2)`2 = (N-min L~f)`2 by A10,PSCOMP_1:98
      .= N-bound L~f by EUCLID:56;
    assume
A20: j1 < width GoB f;
    (GoB f)*(i1,width GoB f)`2 = (GoB f)*(1,width GoB f)`2 by A9,A17,A18,
GOBOARD5:2
      .= N-bound L~f by JORDAN5D:42;
    hence contradiction by A14,A15,A17,A18,A20,A19,GOBOARD5:5;
  end;
  then
A21: j1 = width GoB f by A16,XXREAL_0:1;
A22: now
    assume i > i1;
    then (f/.2)`1 < (N-min L~f)`1 by A2,A3,A14,A4,A15,A17,A21,GOBOARD5:4;
    hence contradiction by A10,PSCOMP_1:98;
  end;
A23: 1 <= i by A1,FINSEQ_3:27;
  then
A24: [i,width GoB f] in Indices GoB f by A9,A4,MATRIX_1:37;
  abs(i-i1)+0 =abs(i-i1)+abs(width GoB f-width GoB f) by ABSVALUE:7
    .= 1 by A2,A12,A13,A14,A8,A24,A21,GOBOARD5:13;
  hence
A25: f/.2 = (GoB f)*(i+1,width GoB f) by A14,A21,A22,SEQM_3:81;
A26: len f -' 1 <= len f by NAT_D:44;
  1 <= len f -' 1 by A11,NAT_D:49;
  then
A27: len f -' 1 in dom f by A26,FINSEQ_3:27;
  then consider i2,j2 being Element of NAT such that
A28: [i2,j2] in Indices GoB f and
A29: f/.(len f -' 1) = (GoB f)*(i2,j2) by GOBOARD2:25;
A30: 1 <= i2 by A28,MATRIX_1:39;
A31: j2 <= width GoB f by A28,MATRIX_1:39;
A32: i2 <= len GoB f by A28,MATRIX_1:39;
  len f in dom f by A7,FINSEQ_3:27;
  then
A33: abs(i2-i)+abs(j2-width GoB f) = 1 by A2,A24,A6,A5,A27,A28,A29,GOBOARD5:13;
  per cases by A33,SEQM_3:82;
  suppose that
A34: abs(i2-i)=1 and
A35: j2=width GoB f;
    (f/.(len f -' 1))`2 = ((GoB f)*(1,width GoB f))`2 by A9,A29,A30,A32,A35,
GOBOARD5:2
      .= (N-min L~f)`2 by A2,A3,A9,A23,A4,GOBOARD5:2
      .= N-bound L~f by EUCLID:56;
    then
A36: f/.(len f -' 1) in N-most L~f by A11,A27,GOBOARD1:16,SPRECT_2:14;
    now
      per cases by A34,SEQM_3:81;
      suppose
        i > i2;
        then (f/.(len f -' 1))`1 < (N-min L~f)`1 by A2,A3,A9,A4,A29,A30,A35,
GOBOARD5:4;
        hence contradiction by A36,PSCOMP_1:98;
      end;
      suppose
        i+1 = i2;
        then 2 >= len f -' 1 by A25,A26,A29,A35,GOBOARD7:39;
        then 2+1 >= len f by NAT_D:52;
        hence contradiction by GOBOARD7:36,XXREAL_0:2;
      end;
    end;
    hence thesis;
  end;
  suppose that
A37: abs(j2-width GoB f)=1 and
A38: i2=i;
    width GoB f in NAT;
    then width GoB f = j2+1 by A31,A37,SEQM_3:81;
    hence thesis by A29,A38,NAT_D:34;
  end;
end;

theorem
  for f being non constant standard special_circular_sequence st 1 <= i
  & i < j & j <= len f & f/.1 in L~mid(f,i,j) holds i = 1 or j = len f
proof
  let f be non constant standard special_circular_sequence such that
A1: 1 <= i and
A2: i < j and
A3: j <= len f;
  1+1 <= len f by GOBOARD7:36,XXREAL_0:2;
  then
A4: f/.1 in LSeg(f,1) by TOPREAL1:27;
  assume f/.1 in L~mid(f,i,j);
  then consider n such that
A5: 1 <= n and
A6: n+1 <= len mid(f,i,j) and
A7: f/.1 in LSeg(mid(f,i,j),n) by SPPOL_2:13;
  n < len mid(f,i,j) by A6,NAT_1:13;
  then
A8: n<j-'i+1 by A1,A2,A3,JORDAN4:20;
  then LSeg(mid(f,i,j),n)=LSeg(f,n+i-'1) by A1,A2,A3,A5,JORDAN4:31;
  then
A9: f/.1 in LSeg(f,1) /\ LSeg(f,n+i-'1) by A7,A4,XBOOLE_0:def 4;
  then
A10: LSeg(f,1) meets LSeg(f,n+i-'1) by XBOOLE_0:4;
  assume that
A11: i <> 1 and
A12: j <> len f;
  per cases by A10,GOBOARD5:def 4;
  suppose
    1+1 >= n+i-'1;
    then
A13: n+i <= 2+1 by NAT_D:52;
    i > 1 by A1,A11,XXREAL_0:1;
    then
A14: i >= 1+1 by NAT_1:13;
    n+i >= i+1 by A5,XREAL_1:8;
    then i+1 <= 2+1 by A13,XXREAL_0:2;
    then i <= 2 by XREAL_1:8;
    then
A15: i = 2 by A14,XXREAL_0:1;
    then n <= 1 by A13,XREAL_1:8;
    then n = 1 by A5,XXREAL_0:1;
    then
A16: n+i-'1 = 2 by A15,NAT_D:34;
A17: 2 < len f by GOBOARD7:36,XXREAL_0:2;
    1+2 <= len f by GOBOARD7:36,XXREAL_0:2;
    then LSeg(f,1) /\ LSeg(f,1+1) = {f/.(1+1)} by TOPREAL1:def 8;
    then f/.1 = f/.2 by A9,A16,TARSKI:def 1;
    hence contradiction by A17,GOBOARD7:38;
  end;
  suppose
A18: n+i-'1+1 >= len f;
    n <= n+i by NAT_1:11;
    then
A19: len f <= n+i by A5,A18,XREAL_1:237,XXREAL_0:2;
    j-'i+1+i = j-'i+i+1 .= j+1 by A2,XREAL_1:237;
    then n+i < j+1 by A8,XREAL_1:8;
    then
A20: n+i <= j by NAT_1:13;
    j < len f by A3,A12,XXREAL_0:1;
    hence contradiction by A19,A20,XXREAL_0:2;
  end;
end;

theorem
  for f being clockwise_oriented (non constant standard
  special_circular_sequence) st f/.1 = N-min L~f holds LSeg(f/.1,f/.2) c= L~
  SpStSeq L~f
proof
  let f be clockwise_oriented (non constant standard special_circular_sequence
  );
A1: N-most L~f c= LSeg(NW-corner L~f,NE-corner L~f) by XBOOLE_1:17;
  assume
A2: f/.1 = N-min L~f;
  then
A3: f/.2 in N-most L~f by SPRECT_2:34;
A4: LSeg(NW-corner L~f,NE-corner L~f) c= L~SpStSeq L~f by Th26;
  f/.1 in LSeg(NW-corner L~f,NE-corner L~f) by A2,Th28;
  then LSeg(f/.1,f/.2) c= LSeg(NW-corner L~f,NE-corner L~f) by A3,A1,
TOPREAL1:12;
  hence thesis by A4,XBOOLE_1:1;
end;

begin :: Rectangular

theorem Th49:
  for f being rectangular FinSequence of TOP-REAL 2, p being Point
of TOP-REAL 2 st p in L~f holds p`1 = W-bound L~f or p`1 = E-bound L~f or p`2 =
  S-bound L~f or p`2 = N-bound L~f
proof
  let f be rectangular FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2
  such that
A1: p in L~f;
  consider D being non vertical non horizontal non empty compact Subset of
  TOP-REAL 2 such that
A2: f = SpStSeq D by SPRECT_1:def 2;
  L~f = (LSeg(NW-corner D,NE-corner D) \/ LSeg(NE-corner D,SE-corner D))
  \/ (LSeg(SE-corner D,SW-corner D) \/ LSeg(SW-corner D,NW-corner D)) by A2,
SPRECT_1:43;
  then
A3: p in LSeg(NW-corner D,NE-corner D) \/ LSeg(NE-corner D,SE-corner D ) or
  p in LSeg(SE-corner D,SW-corner D) \/ LSeg(SW-corner D,NW-corner D) by A1,
XBOOLE_0:def 3;
  per cases by A3,XBOOLE_0:def 3;
  suppose
A4: p in LSeg(NW-corner D,NE-corner D);
A5: N-bound L~SpStSeq D = N-bound D by SPRECT_1:68;
    then
A6: (NE-corner D)`2 = N-bound L~f by A2,EUCLID:56;
    (NW-corner D)`2 = N-bound L~f by A2,A5,EUCLID:56;
    hence thesis by A4,A6,GOBOARD7:6;
  end;
  suppose
A7: p in LSeg(NE-corner D,SE-corner D);
A8: E-bound L~SpStSeq D = E-bound D by SPRECT_1:69;
    then
A9: (SE-corner D)`1 = E-bound L~f by A2,EUCLID:56;
    (NE-corner D)`1 = E-bound L~f by A2,A8,EUCLID:56;
    hence thesis by A7,A9,GOBOARD7:5;
  end;
  suppose
A10: p in LSeg(SE-corner D,SW-corner D);
A11: S-bound L~SpStSeq D = S-bound D by SPRECT_1:67;
    then
A12: (SW-corner D)`2 = S-bound L~f by A2,EUCLID:56;
    (SE-corner D)`2 = S-bound L~f by A2,A11,EUCLID:56;
    hence thesis by A10,A12,GOBOARD7:6;
  end;
  suppose
A13: p in LSeg(SW-corner D,NW-corner D);
A14: W-bound L~SpStSeq D = W-bound D by SPRECT_1:66;
    then
A15: (NW-corner D)`1 = W-bound L~f by A2,EUCLID:56;
    (SW-corner D)`1 = W-bound L~f by A2,A14,EUCLID:56;
    hence thesis by A13,A15,GOBOARD7:5;
  end;
end;

registration
  cluster rectangular special_circular_sequence;
  existence
  proof
    consider C being non empty compact non vertical non horizontal Subset of
    TOP-REAL2;
    SpStSeq C is special_circular_sequence;
    hence thesis;
  end;
end;

theorem Th50:
  for f being rectangular special_circular_sequence, g being
  S-Sequence_in_R2 st g/.1 in LeftComp f & g/.len g in RightComp f holds L~f
  meets L~g
proof
  let f be rectangular special_circular_sequence, g be S-Sequence_in_R2 such
  that
A1: g/.1 in LeftComp f and
A2: g/.len g in RightComp f;
A3: len g >= 2 by TOPREAL1:def 10;
  then g/.1 in L~g by JORDAN3:34;
  then g/.1 in LeftComp f /\ L~g by A1,XBOOLE_0:def 4;
  then
A4: LeftComp f meets L~g by XBOOLE_0:4;
  g/.len g in L~g by A3,JORDAN3:34;
  then g/.len g in RightComp f /\ L~g by A2,XBOOLE_0:def 4;
  then
A5: RightComp f meets L~g by XBOOLE_0:4;
A6: LeftComp f misses RightComp f by SPRECT_1:96;
  assume L~f misses L~g;
  then L~g c= (L~f)` by SUBSET_1:43;
  then
A7: L~g c= LeftComp f \/ RightComp f by GOBRD12:11;
A8: L~g is_an_arc_of g/.1,g/.len g by TOPREAL1:31;
A9: RightComp f is open by Th41;
  LeftComp f is open by Th41;
  hence contradiction by A7,A9,A4,A5,A6,A8,JORDAN6:11,TOPREAL5:4;
end;

theorem Th51:
  for f being rectangular special_circular_sequence holds SpStSeq L~f = f
proof
  let f be rectangular special_circular_sequence;
  set C = L~f, g = SpStSeq C;
  consider D being non vertical non horizontal non empty compact Subset of
  TOP-REAL 2 such that
A1: f = SpStSeq D by SPRECT_1:def 2;
A2: 5 = len f by SPRECT_1:90;
  SpStSeq L~f = <*NW-corner L~f,NE-corner L~f,SE-corner L~f*>^ <*SW-corner
  L~f,NW-corner L~f*> by SPRECT_1:def 1;
  then
A3: len g = len<*NW-corner L~f,NE-corner L~f,SE-corner L~f*> +len<*SW-corner
  L~f,NW-corner L~f*> by FINSEQ_1:35
    .= 3+len<*SW-corner L~f,NW-corner L~f*> by FINSEQ_1:62
    .= 3+2 by FINSEQ_1:61;
A4: for i being Nat st i in dom f holds f/.i = g/.i
  proof
    let i be Nat;
    assume
A5: i in dom f;
    then
A6: 0 <> i by FINSEQ_3:27;
A7: i <= len f by A5,FINSEQ_3:27;
A8: f/.1 = W-max C by SPRECT_1:91
      .= NW-corner D by A1,SPRECT_1:83
      .= NW-corner C by A1,SPRECT_1:70
      .= g/.1 by SPRECT_1:37;
    per cases by A2,A6,A7,NAT_1:30;
    suppose
      i = 1;
      hence thesis by A8;
    end;
    suppose
A9:   i = 2;
      hence f/.i = E-max C by SPRECT_1:92
        .= NE-corner D by A1,SPRECT_1:87
        .= NE-corner C by A1,SPRECT_1:71
        .= g/.i by A9,SPRECT_1:38;
    end;
    suppose
A10:  i = 3;
      hence f/.i = E-min C by SPRECT_1:93
        .= SE-corner D by A1,SPRECT_1:86
        .= SE-corner C by A1,SPRECT_1:73
        .= g/.i by A10,SPRECT_1:39;
    end;
    suppose
A11:  i = 4;
      hence f/.i = W-min C by SPRECT_1:94
        .= SW-corner D by A1,SPRECT_1:82
        .= SW-corner C by A1,SPRECT_1:72
        .= g/.i by A11,SPRECT_1:40;
    end;
    suppose
A12:  i = 5;
      hence f/.i = f/.1 by A2,FINSEQ_6:def 1
        .= g/.i by A3,A8,A12,FINSEQ_6:def 1;
    end;
  end;
  dom f = dom g by A2,A3,FINSEQ_3:31;
  hence thesis by A4,FINSEQ_5:13;
end;

theorem Th52:
  for f being rectangular special_circular_sequence holds L~f = {
p where p is Point of TOP-REAL 2: p`1 = W-bound L~f & p`2 <= N-bound L~f & p`2
  >= S-bound L~f or p`1 <= E-bound L~f & p`1 >= W-bound L~f & p`2 = N-bound L~f
or p`1 <= E-bound L~f & p`1 >= W-bound L~f & p`2 = S-bound L~f or p`1 = E-bound
  L~f & p`2 <= N-bound L~f & p`2 >= S-bound L~f}
proof
  let f be rectangular special_circular_sequence;
  set C = L~f, E = { p : p`1 = E-bound C & p`2 <= N-bound C & p`2 >= S-bound C
}, S = { p : p`1 <= E-bound C & p`1 >= W-bound C & p`2 = S-bound C}, N = { p :
p`1 <= E-bound C & p`1 >= W-bound C & p`2 = N-bound C}, W = { p : p`1 = W-bound
  C & p`2 <= N-bound C & p`2 >= S-bound C};
A1: LSeg(SE-corner C, NE-corner C) = E by SPRECT_1:25;
A2: LSeg(SW-corner C, SE-corner C) = S by SPRECT_1:26;
A3: LSeg(SW-corner C, NW-corner C) = W by SPRECT_1:28;
A4: LSeg(NW-corner C, NE-corner C) = N by SPRECT_1:27;
A5: C = L~SpStSeq C by Th51;
  thus C c= { p where p is Point of TOP-REAL 2: p`1 = W-bound C & p`2 <=
  N-bound C & p`2 >= S-bound C or p`1 <= E-bound C & p`1 >= W-bound C & p`2 =
  N-bound C or p`1 <= E-bound C & p`1 >= W-bound C & p`2 = S-bound C or p`1 =
  E-bound C & p`2 <= N-bound C & p`2 >= S-bound C}
  proof
    let x be set;
    assume
A6: x in C;
    then reconsider q=x as Point of TOP-REAL 2;
    q in (LSeg(NW-corner C,NE-corner C) \/ LSeg(NE-corner C,SE-corner C))
\/ (LSeg(SE-corner C,SW-corner C) \/ LSeg(SW-corner C,NW-corner C)) by A5,A6,
SPRECT_1:43;
    then
    q in (LSeg(NW-corner C,NE-corner C) \/ LSeg(NE-corner C,SE-corner C))
    or q in (LSeg(SE-corner C,SW-corner C) \/ LSeg(SW-corner C,NW-corner C))
by XBOOLE_0:def 3;
    then
    q in LSeg(NW-corner C,NE-corner C) or q in LSeg(NE-corner C,SE-corner
C) or q in LSeg(SE-corner C,SW-corner C) or q in LSeg(SW-corner C,NW-corner C)
    by XBOOLE_0:def 3;
    then (ex p st x = p & p`1 = E-bound C & p`2 <= N-bound C & p`2 >= S-bound
C) or (ex p st x = p & p`1 <= E-bound C & p`1 >= W-bound C & p`2 = S-bound C)
or (ex p st x = p & p`1 <= E-bound C & p`1 >= W-bound C & p`2 = N-bound C) or
ex p st x = p & p`1 = W-bound C & p`2 <= N-bound C & p`2 >= S-bound C by A1,A2
,A4,A3;
    hence thesis;
  end;
  let x be set;
  assume x in { p where p is Point of TOP-REAL 2: p`1 = W-bound C & p`2 <=
  N-bound C & p`2 >= S-bound C or p`1 <= E-bound C & p`1 >= W-bound C & p`2 =
  N-bound C or p`1 <= E-bound C & p`1 >= W-bound C & p`2 = S-bound C or p`1 =
  E-bound C & p`2 <= N-bound C & p`2 >= S-bound C};
  then ex p st x = p & (p`1 = W-bound C & p`2 <= N-bound C & p`2 >= S-bound C
or p`1 <= E-bound C & p`1 >= W-bound C & p`2 = N-bound C or p`1 <= E-bound C &
p`1 >= W-bound C & p`2 = S-bound C or p`1 = E-bound C & p`2 <= N-bound C & p`2
  >= S-bound C);
  then
  x in LSeg(NW-corner C,NE-corner C) or x in LSeg(NE-corner C,SE-corner C
  ) or x in LSeg(SE-corner C,SW-corner C) or x in LSeg(SW-corner C,NW-corner C)
  by A1,A2,A4,A3;
  then x in (LSeg(NW-corner C,NE-corner C) \/ LSeg(NE-corner C,SE-corner C))
  or x in (LSeg(SE-corner C,SW-corner C) \/ LSeg(SW-corner C,NW-corner C)) by
XBOOLE_0:def 3;
  then x in (LSeg(NW-corner C,NE-corner C) \/ LSeg(NE-corner C,SE-corner C))
  \/ (LSeg(SE-corner C,SW-corner C) \/ LSeg(SW-corner C,NW-corner C)) by
XBOOLE_0:def 3;
  hence thesis by A5,SPRECT_1:43;
end;

theorem Th53:
  for f being rectangular special_circular_sequence holds GoB f =
  (f/.4,f/.1)][(f/.3,f/.2)
proof
  let f be rectangular special_circular_sequence;
  set G = (f/.4,f/.1)][(f/.3,f/.2), v1 = Incr X_axis f, v2 = Incr Y_axis f;
A1: f/.2 = N-max L~f by SPRECT_1:92;
A2: f/.1 = N-min L~f by SPRECT_1:91;
  then
A3: (f/.1)`1 < (f/.2)`1 by A1,SPRECT_2:55;
A4: (f/.2)`2 = (f/.1)`2 by A2,A1,PSCOMP_1:95;
A5: f/.4 = S-min L~f by SPRECT_1:94;
A6: f/.3 = S-max L~f by SPRECT_1:93;
  then
A7: (f/.3)`2 = (f/.4)`2 by A5,PSCOMP_1:115;
A8: len<*(f/.3)`1,(f/.4)`1,(f/.5)`1*> = 3 by FINSEQ_1:62;
A9: len f = 5 by SPRECT_1:90;
  then
A10: f/.1 = f/.5 by FINSEQ_6:def 1;
  set g = <*(f/.1)`1,(f/.2)`1*>, h = <*(f/.1)`1,(f/.2)`1*>^<*(f/.3)`1,(f/.4)`1
  ,(f/.5)`1*>;
A11: f/.3 = E-min L~f by SPRECT_1:93;
A12: f/.2 = E-max L~f by SPRECT_1:92;
  then
A13: (f/.3)`1 = (f/.2)`1 by A11,PSCOMP_1:105;
A14: len g = 2 by FINSEQ_1:61;
A15: dom g = {1,2} by FINSEQ_1:4,FINSEQ_3:29;
A16: g is increasing
  proof
A17: g.2 = (f/.2)`1 by FINSEQ_1:61;
A18: g.1 = (f/.1)`1 by FINSEQ_1:61;
    let n,m such that
A19: n in dom g and
A20: m in dom g and
A21: n<m;
A22: m = 1 or m = 2 by A15,A20,TARSKI:def 2;
    n = 1 or n = 2 by A15,A19,TARSKI:def 2;
    hence g.n < g.m by A2,A1,A21,A18,A17,A22,SPRECT_2:55;
  end;
A23: f/.4 = W-min L~f by SPRECT_1:94;
A24: len h = len <*(f/.1)`1,(f/.2)`1*> + len<*(f/.3)`1,(f/.4)`1,(f/.5)`1*>
  by FINSEQ_1:35
    .= len <*(f/.1)`1,(f/.2)`1*> + 3 by FINSEQ_1:62
    .= 2 + 3 by FINSEQ_1:61
    .= len f by SPRECT_1:90;
  for n st n in dom h holds h.n = (f/.n)`1
  proof
    let n;
    assume
A25: n in dom h;
    then
A26: 1 <= n by FINSEQ_3:27;
A27: n <= 5 by A9,A24,A25,FINSEQ_3:27;
    per cases by A26,A27,NAT_1:30;
    suppose
A28:  n=1;
      then n in dom g by A14,FINSEQ_3:27;
      hence h.n = <*(f/.1)`1,(f/.2)`1*>.1 by A28,FINSEQ_1:def 7
        .= (f/.n)`1 by A28,FINSEQ_1:61;
    end;
    suppose
A29:  n=2;
      then n in dom g by A14,FINSEQ_3:27;
      hence h.n = <*(f/.1)`1,(f/.2)`1*>.2 by A29,FINSEQ_1:def 7
        .= (f/.n)`1 by A29,FINSEQ_1:61;
    end;
    suppose
A30:  n=3;
      hence h.n = h.(2+1)
        .= <*(f/.3)`1,(f/.4)`1,(f/.5)`1*>.1 by A14,A8,FINSEQ_1:86
        .= (f/.n)`1 by A30,FINSEQ_1:62;
    end;
    suppose
A31:  n=4;
      hence h.n = h.(2+2)
        .= <*(f/.3)`1,(f/.4)`1,(f/.5)`1*>.2 by A14,A8,FINSEQ_1:86
        .= (f/.n)`1 by A31,FINSEQ_1:62;
    end;
    suppose
A32:  n=5;
      hence h.n = h.(2+3)
        .= <*(f/.3)`1,(f/.4)`1,(f/.5)`1*>.3 by A14,A8,FINSEQ_1:86
        .= (f/.n)`1 by A32,FINSEQ_1:62;
    end;
  end;
  then
A33: X_axis f = h by A24,GOBOARD1:def 3;
A34: rng g = { (f/.1)`1,(f/.2)`1 } by FINSEQ_2:147;
A35: f/.1 = W-max L~f by SPRECT_1:91;
  then
A36: (f/.4)`2 < (f/.5)`2 by A23,A10,SPRECT_2:61;
  {(f/.3)`1,(f/.4)`1,(f/.5)`1} c= { (f/.1)`1,(f/.2)`1 }
  proof
    let x be set;
    assume
A37: x in {(f/.3)`1,(f/.4)`1,(f/.5)`1};
    per cases by A37,ENUMSET1:def 1;
    suppose
      x = (f/.3)`1;
      then x = (f/.2)`1 by A12,A11,PSCOMP_1:105;
      hence thesis by TARSKI:def 2;
    end;
    suppose
      x = (f/.4)`1;
      then x = (f/.1)`1 by A35,A23,PSCOMP_1:85;
      hence thesis by TARSKI:def 2;
    end;
    suppose
      x = (f/.5)`1;
      then x = (f/.1)`1 by A9,FINSEQ_6:def 1;
      hence thesis by TARSKI:def 2;
    end;
  end;
  then
A38: rng g = rng<*(f/.1)`1,(f/.2)`1*> \/ {(f/.3)`1,(f/.4)`1,(f/.5)`1} by A34,
XBOOLE_1:12
    .= rng<*(f/.1)`1,(f/.2)`1*> \/ rng<*(f/.3)`1,(f/.4)`1,(f/.5)`1*> by
FINSEQ_2:148
    .= rng X_axis f by A33,FINSEQ_1:44;
  len g = 2 by FINSEQ_1:61
    .= card rng X_axis f by A34,A38,A3,CARD_2:76;
  then
A39: v1 = <*(f/.1)`1,(f/.2)`1*> by A38,A16,SEQ_4:def 25;
  then
A40: v1.1 = (f/.1)`1 by FINSEQ_1:61;
  set g = <*(f/.4)`2,(f/.5)`2*>, h = <*(f/.1)`2,(f/.2)`2,(f/.3)`2*>^<*(f/.4)`2
  ,(f/.5)`2*>;
A41: len h = len <*(f/.1)`2,(f/.2)`2,(f/.3)`2*> + len<*(f/.4)`2,(f/.5)`2*>
  by FINSEQ_1:35
    .= len <*(f/.4)`2,(f/.5)`2*> + 3 by FINSEQ_1:62
    .= 2 + 3 by FINSEQ_1:61
    .= len f by SPRECT_1:90;
A42: len<*(f/.1)`2,(f/.2)`2,(f/.3)`2*> = 3 by FINSEQ_1:62;
A43: dom g = {1,2} by FINSEQ_1:4,FINSEQ_3:29;
A44: g is increasing
  proof
A45: g.2 = (f/.5)`2 by FINSEQ_1:61;
A46: g.1 = (f/.4)`2 by FINSEQ_1:61;
    let n,m such that
A47: n in dom g and
A48: m in dom g and
A49: n<m;
A50: m = 1 or m = 2 by A43,A48,TARSKI:def 2;
    n = 1 or n = 2 by A43,A47,TARSKI:def 2;
    hence g.n < g.m by A35,A23,A10,A49,A46,A45,A50,SPRECT_2:61;
  end;
A51: v1.2 = (f/.2)`1 by A39,FINSEQ_1:61;
A52: len g = 2 by FINSEQ_1:61;
  for n st n in dom h holds h.n = (f/.n)`2
  proof
    let n;
    assume
A53: n in dom h;
    then
A54: 1 <= n by FINSEQ_3:27;
A55: n <= 5 by A9,A41,A53,FINSEQ_3:27;
    per cases by A54,A55,NAT_1:30;
    suppose
A56:  n=1;
      then n in dom<*(f/.1)`2,(f/.2)`2,(f/.3)`2*> by A42,FINSEQ_3:27;
      hence h.n = <*(f/.1)`2,(f/.2)`2,(f/.3)`2*>.1 by A56,FINSEQ_1:def 7
        .= (f/.n)`2 by A56,FINSEQ_1:62;
    end;
    suppose
A57:  n=2;
      then n in dom<*(f/.1)`2,(f/.2)`2,(f/.3)`2*> by A42,FINSEQ_3:27;
      hence h.n = <*(f/.1)`2,(f/.2)`2,(f/.3)`2*>.2 by A57,FINSEQ_1:def 7
        .= (f/.n)`2 by A57,FINSEQ_1:62;
    end;
    suppose
A58:  n=3;
      then n in dom<*(f/.1)`2,(f/.2)`2,(f/.3)`2*> by A42,FINSEQ_3:27;
      hence h.n = <*(f/.1)`2,(f/.2)`2,(f/.3)`2*>.3 by A58,FINSEQ_1:def 7
        .= (f/.n)`2 by A58,FINSEQ_1:62;
    end;
    suppose
A59:  n=4;
      hence h.n = h.(3+1) .= <*(f/.4)`2,(f/.5)`2*>.1 by A52,A42,FINSEQ_1:86
        .= (f/.n)`2 by A59,FINSEQ_1:61;
    end;
    suppose
A60:  n=5;
      hence h.n = h.(2+3) .= <*(f/.4)`2,(f/.5)`2*>.2 by A52,A42,FINSEQ_1:86
        .= (f/.n)`2 by A60,FINSEQ_1:61;
    end;
  end;
  then
A61: Y_axis f = h by A41,GOBOARD1:def 4;
A62: rng g = { (f/.4)`2,(f/.5)`2 } by FINSEQ_2:147;
  {(f/.1)`2,(f/.2)`2,(f/.3)`2} c= { (f/.4)`2,(f/.5)`2 }
  proof
    let x be set;
    assume
A63: x in {(f/.1)`2,(f/.2)`2,(f/.3)`2};
    per cases by A63,ENUMSET1:def 1;
    suppose
      x = (f/.1)`2;
      hence thesis by A10,TARSKI:def 2;
    end;
    suppose
      x = (f/.2)`2;
      then x = (f/.1)`2 by A2,A1,PSCOMP_1:95;
      hence thesis by A10,TARSKI:def 2;
    end;
    suppose
      x = (f/.3)`2;
      then x = (f/.4)`2 by A6,A5,PSCOMP_1:115;
      hence thesis by TARSKI:def 2;
    end;
  end;
  then
A64: rng g = {(f/.1)`2,(f/.2)`2,(f/.3)`2} \/ {(f/.4)`2,(f/.5)`2} by A62,
XBOOLE_1:12
    .= rng<*(f/.1)`2,(f/.2)`2,(f/.3)`2*> \/ {(f/.4)`2,(f/.5)`2} by FINSEQ_2:148
    .= rng<*(f/.1)`2,(f/.2)`2,(f/.3)`2*> \/ rng<*(f/.4)`2,(f/.5)`2*> by
FINSEQ_2:147
    .= rng Y_axis f by A61,FINSEQ_1:44;
  len g = 2 by FINSEQ_1:61
    .= card rng Y_axis f by A62,A64,A36,CARD_2:76;
  then
A65: v2 = <*(f/.4)`2,(f/.1)`2*> by A10,A64,A44,SEQ_4:def 25;
  then
A66: v2.1 = (f/.4)`2 by FINSEQ_1:61;
A67: v2.2 = (f/.1)`2 by A65,FINSEQ_1:61;
A68: (f/.1)`1 = (f/.4)`1 by A35,A23,PSCOMP_1:85;
A69: for n,m st [n,m] in Indices G holds G*(n,m) = |[v1.n,v2.m]|
  proof
    let n,m;
    assume [n,m] in Indices G;
    then
A70: [n,m]in { [1,1], [1,2],[2,1], [2,2]} by Th12;
    per cases by A70,ENUMSET1:def 2;
    suppose
A71:  [n,m] = [1,1];
      then
A72:  m = 1 by ZFMISC_1:33;
A73:  n = 1 by A71,ZFMISC_1:33;
      hence G*(n,m) = f/.4 by A72,MATRIX_2:6
        .= |[v1.n,v2.m]| by A40,A66,A68,A73,A72,EUCLID:57;
    end;
    suppose
A74:  [n,m] = [1,2];
      then
A75:  m = 2 by ZFMISC_1:33;
A76:  n = 1 by A74,ZFMISC_1:33;
      hence G*(n,m) = f/.1 by A75,MATRIX_2:6
        .= |[v1.n,v2.m]| by A40,A67,A76,A75,EUCLID:57;
    end;
    suppose
A77:  [n,m] = [2,1];
      then
A78:  m = 1 by ZFMISC_1:33;
A79:  n = 2 by A77,ZFMISC_1:33;
      hence G*(n,m) = f/.3 by A78,MATRIX_2:6
        .= |[v1.n,v2.m]| by A51,A66,A13,A7,A79,A78,EUCLID:57;
    end;
    suppose
A80:  [n,m] = [2,2];
      then
A81:  m = 2 by ZFMISC_1:33;
A82:  n = 2 by A80,ZFMISC_1:33;
      hence G*(n,m) = f/.2 by A81,MATRIX_2:6
        .= |[v1.n,v2.m]| by A51,A67,A4,A82,A81,EUCLID:57;
    end;
  end;
A83: width G = 2 by MATRIX_2:3
    .= len v2 by A65,FINSEQ_1:61;
  len G = 2 by MATRIX_2:3
    .= len v1 by A39,FINSEQ_1:61;
  then GoB(v1,v2) = G by A83,A69,GOBOARD2:def 1;
  hence thesis by GOBOARD2:def 3;
end;

theorem Th54:
  for f being rectangular special_circular_sequence holds LeftComp
f = {p : not(W-bound L~f <= p`1 & p`1 <= E-bound L~f & S-bound L~f <= p`2 & p`2
  <= N-bound L~f)} & RightComp f = {q : W-bound L~f < q`1 & q`1 < E-bound L~f &
  S-bound L~f < q`2 & q`2 < N-bound L~f}
proof
  let f be rectangular special_circular_sequence;
  defpred U[Element of TOP-REAL 2] means not(W-bound L~f <= $1`1 & $1`1 <=
  E-bound L~f & S-bound L~f <= $1`2 & $1`2 <= N-bound L~f);
  defpred V[Element of TOP-REAL 2] means W-bound L~f < $1`1 & $1`1 < E-bound
  L~f & S-bound L~f < $1`2 & $1`2 < N-bound L~f;
  defpred W[Element of TOP-REAL 2] means $1`1 = W-bound L~f & $1`2 <= N-bound
L~f & $1`2 >= S-bound L~f or $1`1 <= E-bound L~f & $1`1 >= W-bound L~f & $1`2 =
  N-bound L~f or $1`1 <= E-bound L~f & $1`1 >= W-bound L~f & $1`2 = S-bound L~f
  or $1`1 = E-bound L~f & $1`2 <= N-bound L~f & $1`2 >= S-bound L~f;
  set LC = {p : U[p] }, RC = {q : V[q] }, Lf = {p : W[p] };
A1: S-bound L~f < N-bound L~f by SPRECT_1:34;
A2: Lf is Subset of TOP-REAL 2 from DOMAIN_1:sch 7;
A3: RC is Subset of TOP-REAL 2 from DOMAIN_1:sch 7;
A4: L~f = Lf by Th52;
  LC is Subset of TOP-REAL 2 from DOMAIN_1:sch 7;
  then reconsider Lc9=LC,Rc9=RC,Lf as Subset of TOP-REAL 2 by A3,A2;
  reconsider Lf as Subset of TOP-REAL 2;
  reconsider Lc9, Rc9 as Subset of TOP-REAL 2;
A5: W-bound L~f < E-bound L~f by SPRECT_1:33;
  then reconsider Lc=Lc9, Rc=Rc9 as Subset of (TOP-REAL 2)|Lf` by A1,JORDAN1:44
,46;
  reconsider Lc, Rc as Subset of (TOP-REAL 2)|Lf`;
A6: 1/2*(S-bound L~f) + 1/2*(S-bound L~f) = (S-bound L~f);
  Rc = Rc9;
  then Lc is a_component by A5,A1,JORDAN1:41;
  then
A7: Lc9 is_a_component_of Lf` by CONNSP_1:def 6;
  set p = 1/2*((GoB f)*(1,width GoB f)+(GoB f)*(1+1,width GoB f))+|[0,1]|, q =
  1/2*((GoB f)*(1,width GoB f)+(GoB f)*(1+1,width GoB f));
A8: 1+1 <= len f by GOBOARD7:36,XXREAL_0:2;
A9: p`2 = q`2+|[0,1]|`2 by TOPREAL3:7
    .= q`2+1 by EUCLID:56;
A10: GoB f = (f/.4,f/.1)][(f/.3,f/.2) by Th53;
  then
A11: 1+1 = width GoB f by MATRIX_2:3;
  then q`2 = (1/2*((GoB f)*(1,width GoB f)+f/.2))`2 by A10,MATRIX_2:6
    .= (1/2*(f/.1+f/.2))`2 by A10,A11,MATRIX_2:6
    .= 1/2*(f/.1+f/.2)`2 by TOPREAL3:9
    .= 1/2*((f/.1)`2+(f/.2)`2) by TOPREAL3:7
    .= 1/2*((N-min L~f)`2+(f/.2)`2) by SPRECT_1:91
    .= 1/2*((N-min L~f)`2+(N-max L~f)`2) by SPRECT_1:92
    .= 1/2*(N-bound L~f+(N-max L~f)`2) by EUCLID:56
    .= 1/2*(N-bound L~f+N-bound L~f) by EUCLID:56
    .= N-bound L~f;
  then p`2 > 0 + N-bound L~f by A9,XREAL_1:10;
  then
A12: p in LC;
A13: 1+1 = len GoB f by A10,MATRIX_2:3;
  then
A14: p in Int cell(GoB f,1,1+1) by A11,GOBOARD6:35;
A15: f/.(1+1) = (GoB f)*(1+1,1+1) by A10,MATRIX_2:6;
  1 < width GoB f by GOBOARD7:35;
  then
A16: 1+1 <= width GoB f by NAT_1:13;
  then
A17: [1+1,1+1] in Indices GoB f by A13,MATRIX_1:37;
  1 <= len GoB f by GOBOARD7:34;
  then
A18: [1,1+1] in Indices GoB f by A16,MATRIX_1:37;
A19: f/.1 = (GoB f)*(1,1+1) by A10,MATRIX_2:6;
  then right_cell(f,1) = cell(GoB f,1,1) by A8,A18,A17,A15,GOBOARD5:29;
  then
A20: Int cell(GoB f,1,1) c= RightComp f by GOBOARD9:def 2;
  left_cell(f,1) = cell(GoB f,1,1+1)by A8,A18,A19,A17,A15,GOBOARD5:29;
  then Int cell(GoB f,1,1+1) c= LeftComp f by GOBOARD9:def 1;
  then
A21: LC meets LeftComp f by A14,A12,XBOOLE_0:3;
A22: (f/.2)`1 = (E-max L~f)`1 by SPRECT_1:92
    .= E-bound L~f by EUCLID:56;
  set p = 1/2*((GoB f)*(1,1)+(GoB f)*(2,2));
A23: p in Int cell(GoB f,1,1) by A13,A11,GOBOARD6:34;
A24: p = 1/2*((GoB f)*(1,1)+f/.2) by A10,MATRIX_2:6
    .= 1/2*(f/.4+f/.2) by A10,MATRIX_2:6;
  then
A25: p`1 = 1/2*(f/.4+f/.2)`1 by TOPREAL3:9
    .= 1/2*((f/.4)`1+(f/.2)`1) by TOPREAL3:7
    .= 1/2*(f/.4)`1+ 1/2*(f/.2)`1;
  LeftComp f is_a_component_of (L~f)` by GOBOARD9:def 1;
  hence LeftComp f = LC by A4,A7,A21,GOBOARD9:3;
A26: 1/2*(W-bound L~f) + 1/2*(W-bound L~f) = (W-bound L~f);
A27: 1/2*(E-bound L~f) + 1/2*(E-bound L~f) = (E-bound L~f);
A28: 1/2*(N-bound L~f) + 1/2*(N-bound L~f) = (N-bound L~f);
A29: (f/.4)`1 = (W-min L~f)`1 by SPRECT_1:94
    .= W-bound L~f by EUCLID:56;
  then 1/2*(f/.4)`1 < 1/2*E-bound L~f by SPRECT_1:33,XREAL_1:70;
  then
A30: p`1 < E-bound L~f by A27,A25,A22,XREAL_1:8;
A31: p`2 = 1/2*(f/.4+f/.2)`2 by A24,TOPREAL3:9
    .= 1/2*((f/.4)`2+(f/.2)`2) by TOPREAL3:7
    .= 1/2*(f/.4)`2+ 1/2*(f/.2)`2;
  Lc = Lc9;
  then Rc is a_component by A5,A1,JORDAN1:41;
  then
A32: Rc9 is_a_component_of Lf` by CONNSP_1:def 6;
A33: (f/.2)`2 = (N-max L~f)`2 by SPRECT_1:92
    .= N-bound L~f by EUCLID:56;
A34: (f/.4)`2 = (S-min L~f)`2 by SPRECT_1:94
    .= S-bound L~f by EUCLID:56;
  then 1/2*(f/.4)`2 < 1/2*N-bound L~f by SPRECT_1:34,XREAL_1:70;
  then
A35: p`2 < N-bound L~f by A28,A31,A33,XREAL_1:8;
  1/2*(f/.2)`2 > 1/2*S-bound L~f by A33,SPRECT_1:34,XREAL_1:70;
  then
A36: S-bound L~f < p`2 by A6,A31,A34,XREAL_1:8;
  1/2*(f/.2)`1 > 1/2*W-bound L~f by A22,SPRECT_1:33,XREAL_1:70;
  then W-bound L~f < p`1 by A26,A25,A29,XREAL_1:8;
  then p in RC by A30,A36,A35;
  then
A37: RC meets RightComp f by A23,A20,XBOOLE_0:3;
  RightComp f is_a_component_of (L~f)` by GOBOARD9:def 2;
  hence thesis by A4,A32,A37,GOBOARD9:3;
end;

registration
  cluster clockwise_oriented (rectangular special_circular_sequence);
  existence
  proof
    consider C being non empty compact non vertical non horizontal Subset of
    TOP-REAL2;
    SpStSeq C is clockwise_oriented;
    hence thesis;
  end;
end;

registration
  cluster -> clockwise_oriented (rectangular special_circular_sequence);
  coherence
  proof
    let f be rectangular special_circular_sequence;
    ex D being non vertical non horizontal non empty compact Subset of
    TOP-REAL 2 st f = SpStSeq D by SPRECT_1:def 2;
    hence thesis;
  end;
end;

theorem
  for f being rectangular special_circular_sequence, g being
  S-Sequence_in_R2 st g/.1 in LeftComp f & g/.len g in RightComp f holds
  Last_Point(L~g,g/.1,g/.len g,L~f) <> NW-corner L~f
proof
  let f be rectangular special_circular_sequence, g be S-Sequence_in_R2 such
  that
A1: g/.1 in LeftComp f and
A2: g/.len g in RightComp f;
A3: L~f meets L~g by A1,A2,Th50;
A5: L~f /\ L~g is closed;
  assume
A6: Last_Point(L~g,g/.1,g/.len g,L~f) = NW-corner L~f;
  set nw = NW-corner L~f, inw = Index(nw,g);
A8: len g in dom g by FINSEQ_5:6;
  then
A9: g.len g = g/.len g by PARTFUN1:def 8;
A10: 1 <= inw+1 by NAT_1:11;
  L~g is_an_arc_of g/.1, g/.len g by TOPREAL1:31;
  then
A11: nw in L~g /\ L~f by A3,A6,JORDAN5C:def 2;
  then
A12: nw in L~g by XBOOLE_0:def 4;
  then
A13: 1 <= inw by JORDAN3:41;
A14: nw in LSeg(g,inw) by A12,JORDAN3:42;
A15: inw < len g by A12,JORDAN3:41;
  then
A16: inw+1 <= len g by NAT_1:13;
  then
A17: inw+1 in dom g by A10,FINSEQ_3:27;
A18: L~f misses RightComp f by Th42;
A19: now
A20: len g >= 1 by A16,A10,XXREAL_0:2;
    assume nw <> g.(inw+1);
    then
A21: nw <> g/.(inw+1) by A17,PARTFUN1:def 8;
    per cases;
    suppose
A22:  g/.(inw+1) in L~f;
      then inw+1 <> len g by A2,A18,XBOOLE_0:3;
      then inw+1 < len g by A16,XXREAL_0:1;
      then
A23:  inw+1+1 <= len g by NAT_1:13;
      then g/.(inw+1) in LSeg(g,inw+1) by A10,TOPREAL1:27;
      then inw >= inw+1 by A3,A6,A13,A16,A14,A10,A21,A22,A23,JORDAN5C:28;
      hence contradiction by XREAL_1:31;
    end;
    suppose
A24:  not g/.(inw+1) in L~f;
A25:  now
        assume
A26:    g/.(inw+1) in RightComp f;
        RightComp f = {q : W-bound L~f < q`1 & q`1 < E-bound L~f &
        S-bound L~f < q`2 & q`2 < N-bound L~f} by Th54;
        then
A27:    ex q st g/.(inw+1) = q & W-bound L~f < q`1 & q`1 < E-bound L~f &
        S-bound L~f < q`2 & q`2 < N-bound L~f by A26;
A28:    LSeg(g,inw) is vertical or LSeg(g,inw) is horizontal by SPPOL_1:41;
        LSeg(g,inw) = LSeg(g/.inw,g/.(inw+1)) by A13,A16,TOPREAL1:def 5;
        then (g/.(inw+1))`1 = nw`1 or (g/.(inw+1))`2 = nw`2 by A14,A28,
SPPOL_1:63,64;
        hence contradiction by A27,EUCLID:56;
      end;
      then reconsider
      m = mid(g,inw+1,len g) as S-Sequence_in_R2 by A2,A16,A10,A20,JORDAN3:39;
A29:  inw+1<len g by A2,A16,A25,XXREAL_0:1;
      g/.(inw+1) in (L~f)` by A24,SUBSET_1:50;
      then g/.(inw+1) in LeftComp f \/ RightComp f by GOBRD12:11;
      then g/.(inw+1) in LeftComp f by A25,XBOOLE_0:def 3;
      then
A30:  m/.1 in LeftComp f by A8,A17,SPRECT_2:12;
      m/.len m in RightComp f by A2,A8,A17,SPRECT_2:13;
      then L~f meets L~m by A30,Th50;
      then consider q being set such that
A31:  q in L~f and
A32:  q in L~m by XBOOLE_0:3;
      reconsider q as Point of TOP-REAL 2 by A32;
      consider i such that
A33:  1 <= i and
A34:  i+1 <= len m and
A35:  q in LSeg(m,i) by A32,SPPOL_2:13;
      set j = i+(inw+1)-'1;
A36:  j = i+inw+1-'1 .= i+inw by NAT_D:34;
A37:  len m = len g-'(inw+1)+1 by A16,A10,JORDAN4:20;
      then len m = len g -' inw by A12,JORDAN3:41,NAT_2:9;
      then len m + inw = len g by A15,XREAL_1:237;
      then i+1 + inw <= len g by A34,XREAL_1:8;
      then
A38:  j + 1 <= len g by A36;
      i < len m by A34,NAT_1:13;
      then
A39:  LSeg(m,i)=LSeg(g,i+(inw+1)-'1) by A10,A29,A33,A37,JORDAN4:31;
A40:  j >= inw+1 by A33,A36,XREAL_1:8;
A41:  now
        assume nw = q;
        then
A42:    nw in LSeg(g,inw) /\ LSeg(g,j) by A14,A35,A39,XBOOLE_0:def 4;
        then
A43:    LSeg(g,inw) meets LSeg(g,j) by XBOOLE_0:4;
        per cases by A40,XXREAL_0:1;
        suppose
A44:      inw+1 = j;
          inw+1+1 <= len g by A29,NAT_1:13;
          then inw + (1+1) <= len g;
          then LSeg(g,inw) /\ LSeg(g,inw+1) = {g/.(inw+1)} by A13,
TOPREAL1:def 8;
          hence contradiction by A21,A42,A44,TARSKI:def 1;
        end;
        suppose
          inw+1 < j;
          hence contradiction by A43,TOPREAL1:def 9;
        end;
      end;
      inw >= 0 by NAT_1:2;
      then 0+1 <= j by A33,A36,XREAL_1:9;
      then inw >= j by A3,A6,A13,A16,A14,A31,A35,A39,A38,A41,JORDAN5C:28;
      then inw >= inw+1 by A40,XXREAL_0:2;
      hence contradiction by XREAL_1:31;
    end;
  end;
  nw in L~f by A11,XBOOLE_0:def 4;
  then nw <> g.len g by A2,A18,A9,XBOOLE_0:3;
  then
A45: inw+1 < len g by A16,A19,XXREAL_0:1;
  then
A46: inw+1+1 <= len g by NAT_1:13;
  then g/.(inw+1) in LSeg(g,inw+1) by A10,TOPREAL1:27;
  then
A47: nw in LSeg(g,inw+1) by A17,A19,PARTFUN1:def 8;
A48: 1 <= inw+1+1 by NAT_1:11;
  then
A49: len g >= 1 by A46,XXREAL_0:2;
A50: inw+1+1 in dom g by A46,A48,FINSEQ_3:27;
  inw+1 < inw+1+1 by NAT_1:13;
  then
A51: nw <> g.(inw+1+1) by A17,A19,A50,FUNCT_1:def 8;
  then
A52: nw <> g/.(inw+1+1) by A50,PARTFUN1:def 8;
  per cases;
  suppose
A53: g/.(inw+1+1) in L~f;
A54: nw <> g/.(inw+1+1) by A50,A51,PARTFUN1:def 8;
    inw+1+1 <> len g by A2,A18,A53,XBOOLE_0:3;
    then inw+1+1 < len g by A46,XXREAL_0:1;
    then
A55: inw+1+1+1 <= len g by NAT_1:13;
    then g/.(inw+1+1) in LSeg(g,inw+1+1) by A48,TOPREAL1:27;
    then inw+1 >= inw+1+1 by A3,A6,A10,A46,A48,A47,A53,A55,A54,JORDAN5C:28;
    hence contradiction by XREAL_1:31;
  end;
  suppose
A56: not g/.(inw+1+1) in L~f;
A57: now
      assume
A58:  g/.(inw+1+1) in RightComp f;
      RightComp f = {q : W-bound L~f < q`1 & q`1 < E-bound L~f & S-bound
      L~f < q`2 & q`2 < N-bound L~f} by Th54;
      then
A59:  ex q st g/.(inw+1+1) = q & W-bound L~f < q`1 & q`1 < E-bound L~f &
      S-bound L~f < q`2 & q`2 < N-bound L~f by A58;
A60:  LSeg(g,inw+1) is vertical or LSeg(g,inw+1) is horizontal by SPPOL_1:41;
      LSeg(g,inw+1) = LSeg(g/.(inw+1),g/.(inw+1+1)) by A10,A46,TOPREAL1:def 5;
      then (g/.(inw+1+1))`1 = nw`1 or (g/.(inw+1+1))`2 = nw`2 by A47,A60,
SPPOL_1:63,64;
      hence contradiction by A59,EUCLID:56;
    end;
    then reconsider
    m = mid(g,inw+1+1,len g) as S-Sequence_in_R2 by A2,A46,A48,A49,JORDAN3:39;
A61: inw+1+1<len g by A2,A46,A57,XXREAL_0:1;
    g/.(inw+1+1) in (L~f)` by A56,SUBSET_1:50;
    then g/.(inw+1+1) in LeftComp f \/ RightComp f by GOBRD12:11;
    then g/.(inw+1+1) in LeftComp f by A57,XBOOLE_0:def 3;
    then
A62: m/.1 in LeftComp f by A8,A50,SPRECT_2:12;
    m/.len m in RightComp f by A2,A8,A50,SPRECT_2:13;
    then L~f meets L~m by A62,Th50;
    then consider q being set such that
A63: q in L~f and
A64: q in L~m by XBOOLE_0:3;
    reconsider q as Point of TOP-REAL 2 by A64;
    consider i such that
A65: 1 <= i and
A66: i+1 <= len m and
A67: q in LSeg(m,i) by A64,SPPOL_2:13;
    set j = i+(inw+1+1)-'1;
A68: len m = len g-'(inw+1+1)+1 by A46,A48,JORDAN4:20;
    then len m = len g -' (inw+1) by A45,NAT_2:9;
    then len m + (inw+1) = len g by A16,XREAL_1:237;
    then i+ 1 + (inw +1) <= len g by A66,XREAL_1:8;
    then
A69: i+ 1 + inw +1 <= len g;
    i < len m by A66,NAT_1:13;
    then
A70: LSeg(m,i)=LSeg(g,i+(inw+1+1)-'1) by A48,A61,A65,A68,JORDAN4:31;
A71: j = i+inw+1+1-'1 .= i+inw+1 by NAT_D:34;
    then j = i+(inw+1);
    then
A72: j >= inw+1+1 by A65,XREAL_1:8;
A73: now
      assume nw = q;
      then
A74:  nw in LSeg(g,inw+1) /\ LSeg(g,j) by A47,A67,A70,XBOOLE_0:def 4;
      then
A75:  LSeg(g,inw+1) meets LSeg(g,j) by XBOOLE_0:4;
      per cases by A72,XXREAL_0:1;
      suppose
A76:    inw+1+1 = j;
        inw+1+1+1 <= len g by A61,NAT_1:13;
        then inw+1 + (1+1) <= len g;
        then LSeg(g,inw+1) /\ LSeg(g,inw+1+1) = {g/.(inw+1+1)} by A10,
TOPREAL1:def 8;
        hence contradiction by A52,A74,A76,TARSKI:def 1;
      end;
      suppose
        inw+1+1 < j;
        hence contradiction by A75,TOPREAL1:def 9;
      end;
    end;
    0+1 <= j by A71,NAT_1:11;
    then inw+1 >= j by A3,A6,A10,A46,A47,A63,A67,A70,A71,A69,A73,JORDAN5C:28
;
    then inw+1 >= inw+1+1 by A72,XXREAL_0:2;
    hence contradiction by XREAL_1:31;
  end;
end;

theorem
  for f being rectangular special_circular_sequence, g being
  S-Sequence_in_R2 st g/.1 in LeftComp f & g/.len g in RightComp f holds
  Last_Point(L~g,g/.1,g/.len g,L~f) <> SE-corner L~f
proof
  let f be rectangular special_circular_sequence, g be S-Sequence_in_R2 such
  that
A1: g/.1 in LeftComp f and
A2: g/.len g in RightComp f;
A3: L~f meets L~g by A1,A2,Th50;
A5: L~f /\ L~g is closed;
  assume
A6: Last_Point(L~g,g/.1,g/.len g,L~f) = SE-corner L~f;
  set se = SE-corner L~f, ise = Index(se,g);
A8: len g in dom g by FINSEQ_5:6;
  then
A9: g.len g = g/.len g by PARTFUN1:def 8;
A10: 1 <= ise+1 by NAT_1:11;
  L~g is_an_arc_of g/.1, g/.len g by TOPREAL1:31;
  then
A11: se in L~g /\ L~f by A3,A6,JORDAN5C:def 2;
  then
A12: se in L~g by XBOOLE_0:def 4;
  then
A13: 1 <= ise by JORDAN3:41;
A14: se in LSeg(g,ise) by A12,JORDAN3:42;
A15: ise < len g by A12,JORDAN3:41;
  then
A16: ise+1 <= len g by NAT_1:13;
  then
A17: ise+1 in dom g by A10,FINSEQ_3:27;
A18: L~f misses RightComp f by Th42;
A19: now
A20: len g >= 1 by A16,A10,XXREAL_0:2;
    assume se <> g.(ise+1);
    then
A21: se <> g/.(ise+1) by A17,PARTFUN1:def 8;
    per cases;
    suppose
A22:  g/.(ise+1) in L~f;
      then ise+1 <> len g by A2,A18,XBOOLE_0:3;
      then ise+1 < len g by A16,XXREAL_0:1;
      then
A23:  ise+1+1 <= len g by NAT_1:13;
      then g/.(ise+1) in LSeg(g,ise+1) by A10,TOPREAL1:27;
      then ise >= ise+1 by A3,A6,A13,A16,A14,A10,A21,A22,A23,JORDAN5C:28;
      hence contradiction by XREAL_1:31;
    end;
    suppose
A24:  not g/.(ise+1) in L~f;
A25:  now
        assume
A26:    g/.(ise+1) in RightComp f;
        RightComp f = {q : W-bound L~f < q`1 & q`1 < E-bound L~f &
        S-bound L~f < q`2 & q`2 < N-bound L~f} by Th54;
        then
A27:    ex q st g/.(ise+1) = q & W-bound L~f < q`1 & q`1 < E-bound L~f &
        S-bound L~f < q`2 & q`2 < N-bound L~f by A26;
A28:    LSeg(g,ise) is vertical or LSeg(g,ise) is horizontal by SPPOL_1:41;
        LSeg(g,ise) = LSeg(g/.ise,g/.(ise+1)) by A13,A16,TOPREAL1:def 5;
        then (g/.(ise+1))`1 = se`1 or (g/.(ise+1))`2 = se`2 by A14,A28,
SPPOL_1:63,64;
        hence contradiction by A27,EUCLID:56;
      end;
      then reconsider
      m = mid(g,ise+1,len g) as S-Sequence_in_R2 by A2,A16,A10,A20,JORDAN3:39;
A29:  ise+1<len g by A2,A16,A25,XXREAL_0:1;
      g/.(ise+1) in (L~f)` by A24,SUBSET_1:50;
      then g/.(ise+1) in LeftComp f \/ RightComp f by GOBRD12:11;
      then g/.(ise+1) in LeftComp f by A25,XBOOLE_0:def 3;
      then
A30:  m/.1 in LeftComp f by A8,A17,SPRECT_2:12;
      m/.len m in RightComp f by A2,A8,A17,SPRECT_2:13;
      then L~f meets L~m by A30,Th50;
      then consider q being set such that
A31:  q in L~f and
A32:  q in L~m by XBOOLE_0:3;
      reconsider q as Point of TOP-REAL 2 by A32;
      consider i such that
A33:  1 <= i and
A34:  i+1 <= len m and
A35:  q in LSeg(m,i) by A32,SPPOL_2:13;
      set j = i+(ise+1)-'1;
A36:  j = i+ise+1-'1 .= i+ise by NAT_D:34;
A37:  len m = len g-'(ise+1)+1 by A16,A10,JORDAN4:20;
      then len m = len g -' ise by A12,JORDAN3:41,NAT_2:9;
      then len m + ise = len g by A15,XREAL_1:237;
      then i+1 + ise <= len g by A34,XREAL_1:8;
      then
A38:  j + 1 <= len g by A36;
      i < len m by A34,NAT_1:13;
      then
A39:  LSeg(m,i)=LSeg(g,i+(ise+1)-'1) by A10,A29,A33,A37,JORDAN4:31;
A40:  j >= ise+1 by A33,A36,XREAL_1:8;
A41:  now
        assume se = q;
        then
A42:    se in LSeg(g,ise) /\ LSeg(g,j) by A14,A35,A39,XBOOLE_0:def 4;
        then
A43:    LSeg(g,ise) meets LSeg(g,j) by XBOOLE_0:4;
        per cases by A40,XXREAL_0:1;
        suppose
A44:      ise+1 = j;
          ise+1+1 <= len g by A29,NAT_1:13;
          then ise + (1+1) <= len g;
          then LSeg(g,ise) /\ LSeg(g,ise+1) = {g/.(ise+1)} by A13,
TOPREAL1:def 8;
          hence contradiction by A21,A42,A44,TARSKI:def 1;
        end;
        suppose
          ise+1 < j;
          hence contradiction by A43,TOPREAL1:def 9;
        end;
      end;
      ise >= 0 by NAT_1:2;
      then 0+1 <= j by A33,A36,XREAL_1:9;
      then ise >= j by A3,A6,A13,A16,A14,A31,A35,A39,A38,A41,JORDAN5C:28;
      then ise >= ise+1 by A40,XXREAL_0:2;
      hence contradiction by XREAL_1:31;
    end;
  end;
  se in L~f by A11,XBOOLE_0:def 4;
  then se <> g.len g by A2,A18,A9,XBOOLE_0:3;
  then
A45: ise+1 < len g by A16,A19,XXREAL_0:1;
  then
A46: ise+1+1 <= len g by NAT_1:13;
  then g/.(ise+1) in LSeg(g,ise+1) by A10,TOPREAL1:27;
  then
A47: se in LSeg(g,ise+1) by A17,A19,PARTFUN1:def 8;
A48: 1 <= ise+1+1 by NAT_1:11;
  then
A49: len g >= 1 by A46,XXREAL_0:2;
A50: ise+1+1 in dom g by A46,A48,FINSEQ_3:27;
  ise+1 < ise+1+1 by NAT_1:13;
  then
A51: se <> g.(ise+1+1) by A17,A19,A50,FUNCT_1:def 8;
  then
A52: se <> g/.(ise+1+1) by A50,PARTFUN1:def 8;
  per cases;
  suppose
A53: g/.(ise+1+1) in L~f;
A54: se <> g/.(ise+1+1) by A50,A51,PARTFUN1:def 8;
    ise+1+1 <> len g by A2,A18,A53,XBOOLE_0:3;
    then ise+1+1 < len g by A46,XXREAL_0:1;
    then
A55: ise+1+1+1 <= len g by NAT_1:13;
    then g/.(ise+1+1) in LSeg(g,ise+1+1) by A48,TOPREAL1:27;
    then ise+1 >= ise+1+1 by A3,A6,A10,A46,A48,A47,A53,A55,A54,JORDAN5C:28;
    hence contradiction by XREAL_1:31;
  end;
  suppose
A56: not g/.(ise+1+1) in L~f;
A57: now
      assume
A58:  g/.(ise+1+1) in RightComp f;
      RightComp f = {q : W-bound L~f < q`1 & q`1 < E-bound L~f & S-bound
      L~f < q`2 & q`2 < N-bound L~f} by Th54;
      then
A59:  ex q st g/.(ise+1+1) = q & W-bound L~f < q`1 & q`1 < E-bound L~f &
      S-bound L~f < q`2 & q`2 < N-bound L~f by A58;
A60:  LSeg(g,ise+1) is vertical or LSeg(g,ise+1) is horizontal by SPPOL_1:41;
      LSeg(g,ise+1) = LSeg(g/.(ise+1),g/.(ise+1+1)) by A10,A46,TOPREAL1:def 5;
      then (g/.(ise+1+1))`1 = se`1 or (g/.(ise+1+1))`2 = se`2 by A47,A60,
SPPOL_1:63,64;
      hence contradiction by A59,EUCLID:56;
    end;
    then reconsider
    m = mid(g,ise+1+1,len g) as S-Sequence_in_R2 by A2,A46,A48,A49,JORDAN3:39;
A61: ise+1+1<len g by A2,A46,A57,XXREAL_0:1;
    g/.(ise+1+1) in (L~f)` by A56,SUBSET_1:50;
    then g/.(ise+1+1) in LeftComp f \/ RightComp f by GOBRD12:11;
    then g/.(ise+1+1) in LeftComp f by A57,XBOOLE_0:def 3;
    then
A62: m/.1 in LeftComp f by A8,A50,SPRECT_2:12;
    m/.len m in RightComp f by A2,A8,A50,SPRECT_2:13;
    then L~f meets L~m by A62,Th50;
    then consider q being set such that
A63: q in L~f and
A64: q in L~m by XBOOLE_0:3;
    reconsider q as Point of TOP-REAL 2 by A64;
    consider i such that
A65: 1 <= i and
A66: i+1 <= len m and
A67: q in LSeg(m,i) by A64,SPPOL_2:13;
    set j = i+(ise+1+1)-'1;
A68: len m = len g-'(ise+1+1)+1 by A46,A48,JORDAN4:20;
    then len m = len g -' (ise+1) by A45,NAT_2:9;
    then len m + (ise+1) = len g by A16,XREAL_1:237;
    then i+ 1 + (ise +1) <= len g by A66,XREAL_1:8;
    then
A69: i+ 1 + ise +1 <= len g;
    i < len m by A66,NAT_1:13;
    then
A70: LSeg(m,i)=LSeg(g,i+(ise+1+1)-'1) by A48,A61,A65,A68,JORDAN4:31;
A71: j = i+ise+1+1-'1 .= i+ise+1 by NAT_D:34;
    then j = i+(ise+1);
    then
A72: j >= ise+1+1 by A65,XREAL_1:8;
A73: now
      assume se = q;
      then
A74:  se in LSeg(g,ise+1) /\ LSeg(g,j) by A47,A67,A70,XBOOLE_0:def 4;
      then
A75:  LSeg(g,ise+1) meets LSeg(g,j) by XBOOLE_0:4;
      per cases by A72,XXREAL_0:1;
      suppose
A76:    ise+1+1 = j;
        ise+1+1+1 <= len g by A61,NAT_1:13;
        then ise+1 + (1+1) <= len g;
        then LSeg(g,ise+1) /\ LSeg(g,ise+1+1) = {g/.(ise+1+1)} by A10,
TOPREAL1:def 8;
        hence contradiction by A52,A74,A76,TARSKI:def 1;
      end;
      suppose
        ise+1+1 < j;
        hence contradiction by A75,TOPREAL1:def 9;
      end;
    end;
    0+1 <= j by A71,NAT_1:11;
    then ise+1 >= j by A3,A6,A10,A46,A47,A63,A67,A70,A71,A69,A73,JORDAN5C:28
;
    then ise+1 >= ise+1+1 by A72,XXREAL_0:2;
    hence contradiction by XREAL_1:31;
  end;
end;

theorem Th57:
  for f being rectangular special_circular_sequence, p being Point
of TOP-REAL 2 st W-bound L~f > p`1 or p`1 > E-bound L~f or S-bound L~f > p`2 or
  p`2 > N-bound L~f holds p in LeftComp f
proof
  let f be rectangular special_circular_sequence, p be Point of TOP-REAL 2;
  LeftComp f = {q : not(W-bound L~f <= q`1 & q`1 <= E-bound L~f & S-bound
  L~f <= q`2 & q`2 <= N-bound L~f)} by Th54;
  hence thesis;
end;

theorem
  for f being clockwise_oriented (non constant standard
  special_circular_sequence) st f/.1 = N-min L~f holds LeftComp SpStSeq L~f c=
  LeftComp f
proof
  let f be clockwise_oriented (non constant standard special_circular_sequence
  ) such that
A1: f/.1 = N-min L~f;
  defpred X[Element of TOP-REAL 2] means $1`2 < S-bound L~f;
  reconsider P1 = { p: X[p] } as Subset of TOP-REAL 2 from DOMAIN_1:sch 7;
  defpred X[Element of TOP-REAL 2] means $1`2 > N-bound L~f;
  reconsider P2 = { p: X[p] } as Subset of TOP-REAL 2 from DOMAIN_1:sch 7;
  defpred X[Element of TOP-REAL 2] means $1`1 > E-bound L~f;
  reconsider P3 = { p: X[p] } as Subset of TOP-REAL 2 from DOMAIN_1:sch 7;
  defpred X[Element of TOP-REAL 2] means $1`1 < W-bound L~f;
  reconsider P4 = { p: X[p] } as Subset of TOP-REAL 2 from DOMAIN_1:sch 7;
A2: W-bound L~SpStSeq L~f = W-bound L~f by SPRECT_1:66;
  for p st p in P2 holds p`2 > (GoB f)*(1, width GoB f)`2
  proof
    let p;
    assume p in P2;
    then ex q st p = q & q`2 > N-bound L~f;
    hence thesis by JORDAN5D:42;
  end;
  then
A3: P2 misses L~f by GOBOARD8:24;
A4: 1+1 <= len f by GOBOARD7:36,XXREAL_0:2;
A5: width GoB f -' 1 + 1 = width GoB f by GOBRD11:34,XREAL_1:237;
  consider i such that
A6: 1 <= i and
A7: i < len GoB f and
A8: N-min L~f = (GoB f)*(i,width GoB f) by Th45;
A9: i+1 <= len GoB f by A7,NAT_1:13;
A10: i in dom GoB f by A6,A7,FINSEQ_3:27;
  then
A11: f/.2 = (GoB f)*(i+1,width GoB f) by A1,A8,Th46;
A12: width GoB f >= 1 by GOBRD11:34;
  then
A13: [i,width GoB f] in Indices GoB f by A6,A7,MATRIX_1:37;
A14: 1 <= i+1 by A6,NAT_1:13;
  then
A15: (f/.2)`2 = ((GoB f)*(1,width GoB f))`2 by A11,A12,A9,GOBOARD5:2
    .= (N-min L~f)`2 by A6,A7,A8,A12,GOBOARD5:2
    .= N-bound L~f by EUCLID:56;
  set a = 1/2*((GoB f)*(i,width GoB f)+(GoB f)*(i+1,width GoB f))+|[0,1]|, q =
  1/2*((GoB f)*(i,width GoB f)+(GoB f)*(i+1,width GoB f));
A16: a`2 = q`2+|[0,1]|`2 by TOPREAL3:7
    .= q`2+1 by EUCLID:56;
  q`2 = (1/2*((GoB f)*(i,width GoB f)+f/.2))`2 by A1,A8,A10,Th46
    .= 1/2*(f/.1+f/.2)`2 by A1,A8,TOPREAL3:9
    .= 1/2*((f/.1)`2+(f/.2)`2) by TOPREAL3:7
    .= 1/2*(N-bound L~f+N-bound L~f) by A1,A15,EUCLID:56
    .= N-bound L~f;
  then a`2 > 0 + N-bound L~f by A16,XREAL_1:10;
  then
A17: a`2 > N-bound L~SpStSeq L~f by SPRECT_1:68;
  LeftComp SpStSeq L~f = {p : not(W-bound L~SpStSeq L~f <= p`1 & p`1 <=
E-bound L~SpStSeq L~f & S-bound L~SpStSeq L~f <= p`2 & p`2 <= N-bound L~SpStSeq
  L~f)} by Th54;
  then
A18: a in LeftComp SpStSeq L~f by A17;
  [i+1,width GoB f] in Indices GoB f by A12,A14,A9,MATRIX_1:37;
  then left_cell(f,1) = cell(GoB f,i,width GoB f) by A1,A8,A11,A5,A4,A13,
GOBOARD5:29;
  then
A19: Int cell(GoB f,i,width GoB f) c= LeftComp f by GOBOARD9:def 1;
  a in Int cell(GoB f,i,width GoB f) by A6,A9,GOBOARD6:35;
  then
A20: LeftComp f meets LeftComp SpStSeq L~f by A19,A18,XBOOLE_0:3;
A21: S-bound L~SpStSeq L~f = S-bound L~f by SPRECT_1:67;
  for p st p in P4 holds p`1 < (GoB f)*(1,1)`1
  proof
    let p;
    assume p in P4;
    then ex q st p = q & q`1 < W-bound L~f;
    hence thesis by JORDAN5D:39;
  end;
  then
A22: P4 misses L~f by GOBOARD8:21;
  for p st p in P3 holds p`1 > (GoB f)*(len GoB f,1)`1
  proof
    let p;
    assume p in P3;
    then ex q st p = q & q`1 > E-bound L~f;
    hence thesis by JORDAN5D:41;
  end;
  then P3 misses L~f by GOBOARD8:22;
  then
A23: P3 \/ P4 misses L~f by A22,XBOOLE_1:70;
  LeftComp SpStSeq L~f is_a_component_of (L~SpStSeq L~f)` by GOBOARD9:def 1;
  then consider B1 being Subset of (TOP-REAL 2)|(L~SpStSeq L~f)` such that
A24: B1 = LeftComp SpStSeq L~f and
A25: B1 is a_component by CONNSP_1:def 6;
  B1 is connected by A25,CONNSP_1:def 5;
  then
A26: LeftComp SpStSeq L~f is connected by A24,CONNSP_1:24;
A27: E-bound L~SpStSeq L~f = E-bound L~f by SPRECT_1:69;
A28: N-bound L~SpStSeq L~f = N-bound L~f by SPRECT_1:68;
A29: LeftComp SpStSeq L~f = P1 \/ P2 \/ (P3 \/ P4)
  proof
    thus LeftComp SpStSeq L~f c= P1 \/ P2 \/ (P3 \/ P4)
    proof
      let x be set;
      assume x in LeftComp SpStSeq L~f;
      then
      x in { p : not(W-bound L~f <= p`1 & p`1 <= E-bound L~f & S-bound L~
      f <= p`2 & p`2 <= N-bound L~f)} by A28,A2,A21,A27,Th54;
      then ex p st p = x & not(W-bound L~f <= p`1 & p`1 <= E-bound L~f &
      S-bound L~f <= p`2 & p`2 <= N-bound L~f);
      then x in P1 or x in P2 or x in P3 or x in P4;
      then x in P1 \/ P2 or x in P3 \/ P4 by XBOOLE_0:def 3;
      hence thesis by XBOOLE_0:def 3;
    end;
    let x be set;
    assume x in P1 \/ P2 \/ (P3 \/ P4);
    then
A30: x in P1 \/ P2 or x in P3 \/ P4 by XBOOLE_0:def 3;
    per cases by A30,XBOOLE_0:def 3;
    suppose
      x in P1;
      then ex p st x = p & p`2 < S-bound L~f;
      hence thesis by A21,Th57;
    end;
    suppose
      x in P2;
      then ex p st x = p & p`2 > N-bound L~f;
      hence thesis by A28,Th57;
    end;
    suppose
      x in P3;
      then ex p st x = p & p`1 > E-bound L~f;
      hence thesis by A27,Th57;
    end;
    suppose
      x in P4;
      then ex p st x = p & p`1 < W-bound L~f;
      hence thesis by A2,Th57;
    end;
  end;
  for p st p in P1 holds p`2 < (GoB f)*(1,1)`2
  proof
    let p;
    assume p in P1;
    then ex q st p = q & q`2 < S-bound L~f;
    hence thesis by JORDAN5D:40;
  end;
  then P1 misses L~f by GOBOARD8:23;
  then P1 \/ P2 misses L~f by A3,XBOOLE_1:70;
  then LeftComp SpStSeq L~f misses L~f by A29,A23,XBOOLE_1:70;
  then
A31: LeftComp SpStSeq L~f c= (L~f)` by SUBSET_1:43;
  LeftComp f is_a_component_of (L~f)` by GOBOARD9:def 1;
  hence thesis by A26,A20,A31,GOBOARD9:6;
end;

begin :: In the area

theorem Th59:
  for f being FinSequence of TOP-REAL 2, p,q being Point of
TOP-REAL 2 holds <*p,q*> is_in_the_area_of f iff <*p*> is_in_the_area_of f & <*
  q*> is_in_the_area_of f
proof
  let f be FinSequence of TOP-REAL 2, p,q be Point of TOP-REAL 2;
  thus <*p,q*> is_in_the_area_of f implies <*p*> is_in_the_area_of f & <*q*>
  is_in_the_area_of f
  proof
A1: dom<*p,q*> = {1,2} by FINSEQ_1:4,FINSEQ_3:29;
    then
A2: 1 in dom<*p,q*> by TARSKI:def 2;
    assume
A3: <*p,q*> is_in_the_area_of f;
A4: <*p,q*>/.1 = p by FINSEQ_4:26;
    then
A5: p`1 <= E-bound L~f by A3,A2,SPRECT_2:def 1;
A6: p`2 <= N-bound L~f by A3,A2,A4,SPRECT_2:def 1;
A7: S-bound L~f <= p`2 by A3,A2,A4,SPRECT_2:def 1;
A8: W-bound L~f <= p`1 by A3,A2,A4,SPRECT_2:def 1;
    thus <*p*> is_in_the_area_of f
    proof
      let i;
      assume i in dom<*p*>;
      then i in {1} by FINSEQ_1:4,55;
      then i = 1 by TARSKI:def 1;
      hence thesis by A8,A5,A7,A6,FINSEQ_4:25;
    end;
    let i;
    assume i in dom<*q*>;
    then i in {1} by FINSEQ_1:4,55;
    then
A9: i = 1 by TARSKI:def 1;
A10: <*p,q*>/.2 = q by FINSEQ_4:26;
A11: 2 in dom<*p,q*> by A1,TARSKI:def 2;
    then
A12: q`1 <= E-bound L~f by A3,A10,SPRECT_2:def 1;
A13: q`2 <= N-bound L~f by A3,A11,A10,SPRECT_2:def 1;
A14: S-bound L~f <= q`2 by A3,A11,A10,SPRECT_2:def 1;
    W-bound L~f <= q`1 by A3,A11,A10,SPRECT_2:def 1;
    hence thesis by A12,A14,A13,A9,FINSEQ_4:25;
  end;
A15: <*p*>/.1 = p by FINSEQ_4:25;
  dom<*q*> = {1} by FINSEQ_1:4,55;
  then
A16: 1 in dom<*q*> by TARSKI:def 1;
  dom<*p*> = {1} by FINSEQ_1:4,55;
  then
A17: 1 in dom<*p*> by TARSKI:def 1;
  assume
A18: <*p*> is_in_the_area_of f;
  then
A19: p`1 <= E-bound L~f by A17,A15,SPRECT_2:def 1;
A20: p`2 <= N-bound L~f by A18,A17,A15,SPRECT_2:def 1;
A21: S-bound L~f <= p`2 by A18,A17,A15,SPRECT_2:def 1;
A22: <*q*>/.1 = q by FINSEQ_4:25;
  assume
A23: <*q*> is_in_the_area_of f;
  then
A24: W-bound L~f <= q`1 by A16,A22,SPRECT_2:def 1;
A25: q`1 <= E-bound L~f by A23,A16,A22,SPRECT_2:def 1;
  let i;
  assume i in dom<*p,q*>;
  then i in {1,2} by FINSEQ_1:4,FINSEQ_3:29;
  then
A26: i =1 or i =2 by TARSKI:def 2;
A27: q`2 <= N-bound L~f by A23,A16,A22,SPRECT_2:def 1;
A28: S-bound L~f <= q`2 by A23,A16,A22,SPRECT_2:def 1;
  W-bound L~f <= p`1 by A18,A17,A15,SPRECT_2:def 1;
  hence thesis by A19,A21,A20,A24,A25,A28,A27,A26,FINSEQ_4:26;
end;

theorem Th60:
  for f being rectangular FinSequence of TOP-REAL 2, p being Point
  of TOP-REAL 2 st <*p*> is_in_the_area_of f & (p`1 = W-bound L~f or p`1 =
  E-bound L~f or p`2 = S-bound L~f or p`2 = N-bound L~f) holds p in L~f
proof
  let f be rectangular FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2;
A1: <*p*>/.1 = p by FINSEQ_4:25;
  dom<*p*> = {1} by FINSEQ_1:4,55;
  then
A2: 1 in dom<*p*> by TARSKI:def 1;
  assume
A3: <*p*> is_in_the_area_of f;
  then
A4: W-bound L~f <= p`1 by A2,A1,SPRECT_2:def 1;
A5: p`2 <= N-bound L~f by A3,A2,A1,SPRECT_2:def 1;
A6: S-bound L~f <= p`2 by A3,A2,A1,SPRECT_2:def 1;
A7: p`1 <= E-bound L~f by A3,A2,A1,SPRECT_2:def 1;
  consider D being non vertical non horizontal non empty compact Subset of
  TOP-REAL 2 such that
A8: f = SpStSeq D by SPRECT_1:def 2;
A9: E-bound L~SpStSeq D = E-bound D by SPRECT_1:69;
A10: N-bound L~SpStSeq D = N-bound D by SPRECT_1:68;
A11: S-bound L~SpStSeq D = S-bound D by SPRECT_1:67;
A12: W-bound L~SpStSeq D = W-bound D by SPRECT_1:66;
A13: L~f = (LSeg(NW-corner D,NE-corner D) \/ LSeg(NE-corner D,SE-corner D))
  \/ (LSeg(SE-corner D,SW-corner D) \/ LSeg(SW-corner D,NW-corner D)) by A8,
SPRECT_1:43;
  assume
A14: p`1 = W-bound L~f or p`1 = E-bound L~f or p`2 = S-bound L~f or p`2
  = N-bound L~f;
  per cases by A14;
  suppose
A15: p`1 = W-bound L~f;
A16: (NW-corner D)`1 = W-bound D by EUCLID:56;
A17: (NW-corner D)`2 = N-bound D by EUCLID:56;
A18: (SW-corner D)`2 = S-bound D by EUCLID:56;
    (SW-corner D)`1 = W-bound D by EUCLID:56;
    then p in LSeg(SW-corner D,NW-corner D) by A6,A5,A8,A12,A11,A10,A15,A16,A18
,A17,GOBOARD7:8;
    then p in LSeg(SE-corner D,SW-corner D) \/ LSeg(SW-corner D,NW-corner D)
    by XBOOLE_0:def 3;
    hence thesis by A13,XBOOLE_0:def 3;
  end;
  suppose
A19: p`1 = E-bound L~f;
A20: (SE-corner D)`1 = E-bound D by EUCLID:56;
A21: (SE-corner D)`2 = S-bound D by EUCLID:56;
A22: (NE-corner D)`2 = N-bound D by EUCLID:56;
    (NE-corner D)`1 = E-bound D by EUCLID:56;
    then p in LSeg(NE-corner D,SE-corner D) by A6,A5,A8,A11,A10,A9,A19,A20,A22
,A21,GOBOARD7:8;
    then p in LSeg(NW-corner D,NE-corner D) \/ LSeg(NE-corner D,SE-corner D)
    by XBOOLE_0:def 3;
    hence thesis by A13,XBOOLE_0:def 3;
  end;
  suppose
A23: p`2 = S-bound L~f;
A24: (SW-corner D)`1 = W-bound D by EUCLID:56;
A25: (SW-corner D)`2 = S-bound D by EUCLID:56;
A26: (SE-corner D)`2 = S-bound D by EUCLID:56;
    (SE-corner D)`1 = E-bound D by EUCLID:56;
    then p in LSeg(SE-corner D,SW-corner D) by A4,A7,A8,A12,A11,A9,A23,A24,A26
,A25,GOBOARD7:9;
    then p in LSeg(SE-corner D,SW-corner D) \/ LSeg(SW-corner D,NW-corner D)
    by XBOOLE_0:def 3;
    hence thesis by A13,XBOOLE_0:def 3;
  end;
  suppose
A27: p`2 = N-bound L~f;
A28: (NE-corner D)`1 = E-bound D by EUCLID:56;
A29: (NE-corner D)`2 = N-bound D by EUCLID:56;
A30: (NW-corner D)`2 = N-bound D by EUCLID:56;
    (NW-corner D)`1 = W-bound D by EUCLID:56;
    then p in LSeg(NW-corner D,NE-corner D) by A4,A7,A8,A12,A10,A9,A27,A28,A30
,A29,GOBOARD7:9;
    then p in LSeg(NW-corner D,NE-corner D) \/ LSeg(NE-corner D,SE-corner D)
    by XBOOLE_0:def 3;
    hence thesis by A13,XBOOLE_0:def 3;
  end;
end;

theorem Th61:
  for f being FinSequence of TOP-REAL 2, p,q being Point of
  TOP-REAL 2, r being Real st 0<=r & r <= 1 & <*p,q*> is_in_the_area_of f holds
  <*(1-r)*p+r*q*> is_in_the_area_of f
proof
  let f be FinSequence of TOP-REAL 2, p,q be Point of TOP-REAL 2, r be Real
  such that
A1: 0 <= r and
A2: r <= 1 and
A3: <*p,q*> is_in_the_area_of f;
A4: dom<*p,q*> = {1,2} by FINSEQ_1:4,FINSEQ_3:29;
  then
A5: 2 in dom<*p,q*> by TARSKI:def 2;
A6: <*p,q*>/.2 = q by FINSEQ_4:26;
  then W-bound L~f <= q`1 by A3,A5,SPRECT_2:def 1;
  then
A7: r*W-bound L~f <= r*q`1 by A1,XREAL_1:66;
  q`1 <= E-bound L~f by A3,A5,A6,SPRECT_2:def 1;
  then
A8: r*q`1<= r*E-bound L~f by A1,XREAL_1:66;
A9: <*p,q*>/.1 = p by FINSEQ_4:26;
A10: 1-r >= 0 by A2,XREAL_1:50;
A11: 1 in dom<*p,q*> by A4,TARSKI:def 2;
  then W-bound L~f <= p`1 by A3,A9,SPRECT_2:def 1;
  then
A12: (1-r)*W-bound L~f <= (1-r)*p`1 by A10,XREAL_1:66;
  p`1 <= E-bound L~f by A3,A11,A9,SPRECT_2:def 1;
  then
A13: (1-r)*p`1 <= (1-r)*E-bound L~f by A10,XREAL_1:66;
  S-bound L~f <= p`2 by A3,A11,A9,SPRECT_2:def 1;
  then
A14: (1-r)*S-bound L~f <= (1-r)*p`2 by A10,XREAL_1:66;
A15: ((1-r)*p+r*q)`1 = ((1-r)*p)`1+(r*q)`1 by TOPREAL3:7
    .= (1-r)*p`1+(r*q)`1 by TOPREAL3:9
    .= (1-r)*p`1+r*q`1 by TOPREAL3:9;
  p`2 <= N-bound L~f by A3,A11,A9,SPRECT_2:def 1;
  then
A16: (1-r)*p`2 <= (1-r)*N-bound L~f by A10,XREAL_1:66;
  let n;
A17: dom <*(1-r)*p+r*q*> = {1} by FINSEQ_1:4,55;
  assume n in dom <*(1-r)*p+r*q*>;
  then
A18: n = 1 by A17,TARSKI:def 1;
  (1-r)*W-bound L~f+r*W-bound L~f = 1*W-bound L~f;
  then W-bound L~f <= (1-r)*p`1+r*q`1 by A7,A12,XREAL_1:9;
  hence W-bound L~f <= (<*(1-r)*p+r*q*>/.n)`1 by A18,A15,FINSEQ_4:25;
  (1-r)*E-bound L~f+r*E-bound L~f = 1*E-bound L~f;
  then (1-r)*p`1+r*q`1 <= E-bound L~f by A8,A13,XREAL_1:9;
  hence (<*(1-r)*p+r*q*>/.n)`1 <= E-bound L~f by A18,A15,FINSEQ_4:25;
A19: ((1-r)*p+r*q)`2 = ((1-r)*p)`2+(r*q)`2 by TOPREAL3:7
    .= (1-r)*p`2+(r*q)`2 by TOPREAL3:9
    .= (1-r)*p`2+r*q`2 by TOPREAL3:9;
  q`2 <= N-bound L~f by A3,A5,A6,SPRECT_2:def 1;
  then
A20: r*q`2<= r*N-bound L~f by A1,XREAL_1:66;
  S-bound L~f <= q`2 by A3,A5,A6,SPRECT_2:def 1;
  then
A21: r*S-bound L~f <= r*q`2 by A1,XREAL_1:66;
  (1-r)*S-bound L~f+r*S-bound L~f = 1*S-bound L~f;
  then S-bound L~f <= (1-r)*p`2+r*q`2 by A21,A14,XREAL_1:9;
  hence S-bound L~f <= (<*(1-r)*p+r*q*>/.n)`2 by A18,A19,FINSEQ_4:25;
  (1-r)*N-bound L~f+r*N-bound L~f = 1*N-bound L~f;
  then (1-r)*p`2+r*q`2 <= N-bound L~f by A20,A16,XREAL_1:9;
  hence thesis by A18,A19,FINSEQ_4:25;
end;

theorem Th62:
  for f, g being FinSequence of TOP-REAL 2 st g is_in_the_area_of
  f & i in dom g holds <*g/.i*> is_in_the_area_of f
proof
  let f, g be FinSequence of TOP-REAL 2 such that
A1: g is_in_the_area_of f and
A2: i in dom g;
  let n;
A3: dom <*g/.i*> = {1} by FINSEQ_1:4,55;
  assume n in dom <*g/.i*>;
  then n = 1 by A3,TARSKI:def 1;
  then <*g/.i*>/.n = g/.i by FINSEQ_4:25;
  hence thesis by A1,A2,SPRECT_2:def 1;
end;

theorem Th63:
  for f, g being FinSequence of TOP-REAL 2, p being Point of
TOP-REAL 2 st g is_in_the_area_of f & p in L~g holds <*p*> is_in_the_area_of f
proof
  let f, g be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2 such that
A1: g is_in_the_area_of f;
  assume p in L~g;
  then consider i such that
A2: 1 <= i and
A3: i+1 <= len g and
A4: p in LSeg(g/.i,g/.(i+1)) by SPPOL_2:14;
A5: ex r being Real st p = (1-r)*g/.i+r*g/.(i+1) & 0<=r & r<=1 by A4;
  i <= i+1 by NAT_1:11;
  then i <= len g by A3,XXREAL_0:2;
  then i in dom g by A2,FINSEQ_3:27;
  then
A6: <*g/.i*> is_in_the_area_of f by A1,Th62;
  1 <= i+1 by NAT_1:11;
  then i+1 in dom g by A3,FINSEQ_3:27;
  then <*g/.(i+1)*> is_in_the_area_of f by A1,Th62;
  then <*g/.i,g/.(i+1)*> is_in_the_area_of f by A6,Th59;
  hence thesis by A5,Th61;
end;

theorem Th64:
  for f being rectangular FinSequence of TOP-REAL 2, p,q being
Point of TOP-REAL 2 st not q in L~f & <*p,q*> is_in_the_area_of f holds LSeg(p,
  q) /\ L~f c= {p}
proof
  let f be rectangular FinSequence of TOP-REAL 2, p,q be Point of TOP-REAL 2
  such that
A1: not q in L~f;
  assume
A2: <*p,q*> is_in_the_area_of f;
A3: dom <*p,q*> = {1,2} by FINSEQ_1:4,FINSEQ_3:29;
  then
A4: 2 in dom <*p,q*> by TARSKI:def 2;
A5: <*p,q*>/.2 = q by FINSEQ_4:26;
  then
A6: W-bound L~f <= q`1 by A2,A4,SPRECT_2:def 1;
A7: <*q*> is_in_the_area_of f by A2,Th59;
  then q`1 <> W-bound L~f by A1,Th60;
  then
A8: W-bound L~f < q`1 by A6,XXREAL_0:1;
A9: q`2 <= N-bound L~f by A2,A4,A5,SPRECT_2:def 1;
  q`2 <> N-bound L~f by A1,A7,Th60;
  then
A10: q`2 < N-bound L~f by A9,XXREAL_0:1;
  let x be set;
A11: <*p,q*>/.1 = p by FINSEQ_4:26;
A12: q`1 <= E-bound L~f by A2,A4,A5,SPRECT_2:def 1;
  q`1 <> E-bound L~f by A1,A7,Th60;
  then
A13: q`1 < E-bound L~f by A12,XXREAL_0:1;
  assume
A14: x in LSeg(p,q) /\ L~f;
  then reconsider p9 = x as Point of TOP-REAL 2;
A15: p9 in L~f by A14,XBOOLE_0:def 4;
A16: 1 in dom <*p,q*> by A3,TARSKI:def 2;
  then
A17: W-bound L~f <= p`1 by A2,A11,SPRECT_2:def 1;
A18: p`2 <= N-bound L~f by A2,A16,A11,SPRECT_2:def 1;
A19: S-bound L~f <= p`2 by A2,A16,A11,SPRECT_2:def 1;
A20: p`1 <= E-bound L~f by A2,A16,A11,SPRECT_2:def 1;
A21: S-bound L~f <= q`2 by A2,A4,A5,SPRECT_2:def 1;
  q`2 <> S-bound L~f by A1,A7,Th60;
  then
A22: S-bound L~f < q`2 by A21,XXREAL_0:1;
  x in LSeg(p,q) by A14,XBOOLE_0:def 4;
  then consider r being Real such that
A23: p9 = (1-r)*p+r*q and
A24: 0<=r and
A25: r<=1;
A26: p9`1 = ((1-r)*p)`1+(r*q)`1 by A23,TOPREAL3:7
    .= (1-r)*p`1+(r*q)`1 by TOPREAL3:9
    .= (1-r)*p`1+r*q`1 by TOPREAL3:9;
A27: p9`2 = ((1-r)*p)`2+(r*q)`2 by A23,TOPREAL3:7
    .= (1-r)*p`2+(r*q)`2 by TOPREAL3:9
    .= (1-r)*p`2+r*q`2 by TOPREAL3:9;
  per cases by A15,Th49;
  suppose
    p9`1 = W-bound L~f;
    then r = 0 by A17,A8,A24,A25,A26,XREAL_1:182;
    then p9 = 1*p+0.TOP-REAL 2 by A23,EUCLID:33
      .= 1*p by EUCLID:31
      .= p by EUCLID:33;
    hence thesis by ZFMISC_1:37;
  end;
  suppose
    p9`1 = E-bound L~f;
    then r = 0 by A20,A13,A24,A25,A26,XREAL_1:181;
    then p9 = 1*p+0.TOP-REAL 2 by A23,EUCLID:33
      .= 1*p by EUCLID:31
      .= p by EUCLID:33;
    hence thesis by ZFMISC_1:37;
  end;
  suppose
    p9`2 = S-bound L~f;
    then r = 0 by A19,A22,A24,A25,A27,XREAL_1:182;
    then p9 = 1*p+0.TOP-REAL 2 by A23,EUCLID:33
      .= 1*p by EUCLID:31
      .= p by EUCLID:33;
    hence thesis by ZFMISC_1:37;
  end;
  suppose
    p9`2 = N-bound L~f;
    then r = 0 by A18,A10,A24,A25,A27,XREAL_1:181;
    then p9 = 1*p+0.TOP-REAL 2 by A23,EUCLID:33
      .= 1*p by EUCLID:31
      .= p by EUCLID:33;
    hence thesis by ZFMISC_1:37;
  end;
end;

theorem
  for f being rectangular FinSequence of TOP-REAL 2, p,q being Point of
TOP-REAL 2 st p in L~f & not q in L~f & <*q*> is_in_the_area_of f holds LSeg(p,
  q) /\ L~f = {p}
proof
  let f be rectangular FinSequence of TOP-REAL 2, p,q be Point of TOP-REAL 2
  such that
A1: p in L~f and
A2: not q in L~f and
A3: <*q*> is_in_the_area_of f;
A4: <*p,q*> = <*p*>^<*q*> by FINSEQ_1:def 9;
  <*p*> is_in_the_area_of f by A1,Th63,SPRECT_2:25;
  hence LSeg(p,q) /\ L~f c= {p} by A2,A3,A4,Th64,SPRECT_2:28;
  p in LSeg(p,q) by RLTOPSP1:69;
  then p in LSeg(p,q) /\ L~f by A1,XBOOLE_0:def 4;
  hence thesis by ZFMISC_1:37;
end;

theorem Th66:
  for f being non constant standard special_circular_sequence st 1
  <= i & i <= len GoB f & 1 <= j & j <= width GoB f holds <*(GoB f)*(i,j)*>
  is_in_the_area_of f
proof
  let f be non constant standard special_circular_sequence such that
A1: 1 <= i and
A2: i <= len GoB f and
A3: 1 <= j and
A4: j <= width GoB f;
  set G = GoB f;
A5: 1 <= width G by A3,A4,XXREAL_0:2;
A6: 1 <= len G by A1,A2,XXREAL_0:2;
A7: N-bound L~f = (G)*(1,width G)`2 by JORDAN5D:42
    .= (G)*(i,width G)`2 by A1,A2,A5,GOBOARD5:2;
A8: j = 1 or j > 1 by A3,XXREAL_0:1;
A9: S-bound L~f = (G)*(1,1)`2 by JORDAN5D:40
    .= (G)*(i,1)`2 by A1,A2,A5,GOBOARD5:2;
A10: i = 1 or i > 1 by A1,XXREAL_0:1;
A11: E-bound L~f = (G)*(len G,1)`1 by JORDAN5D:41
    .= (G)*(len G,j)`1 by A3,A4,A6,GOBOARD5:3;
A12: j = width G or j < width G by A4,XXREAL_0:1;
A13: i = len G or i < len G by A2,XXREAL_0:1;
  let n;
  set p = (GoB f)*(i,j);
  assume n in dom<*(GoB f)*(i,j)*>;
  then n in {1} by FINSEQ_1:4,55;
  then n = 1 by TARSKI:def 1;
  then
A14: <*p*>/.n = p by FINSEQ_4:25;
  W-bound L~f = G*(1,1)`1 by JORDAN5D:39
    .= G*(1,j)`1 by A3,A4,A6,GOBOARD5:3;
  hence thesis by A14,A10,A9,A8,A11,A13,A7,A12,GOBOARD5:4,5;
end;

theorem
  for g being FinSequence of TOP-REAL 2, p,q being Point of TOP-REAL 2
  st <*p,q*> is_in_the_area_of g holds <*1/2*(p+q)*> is_in_the_area_of g
proof
  let g be FinSequence of TOP-REAL 2, p,q be Point of TOP-REAL 2;
  1/2*(p+q) = (1 - 1/2)*p + 1/2*q by EUCLID:36;
  hence thesis by Th61;
end;

theorem
  for f, g being FinSequence of TOP-REAL 2 st g is_in_the_area_of f
  holds Rev g is_in_the_area_of f
proof
  let f, g be FinSequence of TOP-REAL 2 such that
A1: g is_in_the_area_of f;
A2: Rev Rev g = g by FINSEQ_6:29;
  let n such that
A3: n in dom Rev g;
  n >= 1 by A3,FINSEQ_3:27;
  then
A4: n-1 >= 0 by XREAL_1:50;
  set i = len g + 1 -' n;
A5: len Rev g = len g by FINSEQ_5:def 3;
  then
A6: n <= len g by A3,FINSEQ_3:27;
  then
A7: i = len g -' n + 1 by NAT_D:38;
  then i = len g - n + 1 by A6,XREAL_1:235
    .= len g - (n-1);
  then
A8: i <= len g - 0 by A4,XREAL_1:15;
  1 <= i by A7,NAT_1:11;
  then
A9: i in dom g by A8,FINSEQ_3:27;
  len g <= len g + 1 by NAT_1:11;
  then n + i = len g + 1 by A6,XREAL_1:237,XXREAL_0:2;
  then (Rev g)/.n = g/.i by A2,A5,A3,FINSEQ_5:69;
  hence thesis by A1,A9,SPRECT_2:def 1;
end;

theorem
  for f, g being FinSequence of TOP-REAL 2, p being Point of TOP-REAL 2
st g is_in_the_area_of f & <*p*> is_in_the_area_of f & g is being_S-Seq & p in
  L~g holds R_Cut(g,p) is_in_the_area_of f
proof
  let f, g be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2 such that
A1: g is_in_the_area_of f and
A2: <*p*> is_in_the_area_of f and
A3: g is being_S-Seq;
  2 <= len g by A3,TOPREAL1:def 10;
  then
A4: 1 <= len g by XXREAL_0:2;
  then
A5: 1 in dom g by FINSEQ_3:27;
  assume
A6: p in L~g;
  then
A7: Index(p,g) < len g by JORDAN3:41;
  1<=Index(p,g) by A6,JORDAN3:41;
  then
A8: Index(p,g) in dom g by A7,FINSEQ_3:27;
  per cases;
  suppose
    p<>g.1;
    then
A9: R_Cut(g,p) = mid(g,1,Index(p,g))^<*p*> by JORDAN3:def 5;
    mid(g,1,Index(p,g)) is_in_the_area_of f by A1,A5,A8,SPRECT_2:26;
    hence thesis by A2,A9,SPRECT_2:28;
  end;
  suppose
    p=g.1;
    then R_Cut(g,p) = <*g.1*> by JORDAN3:def 5
      .= <*g/.1*> by A5,PARTFUN1:def 8
      .= mid(g,1,1) by A4,JORDAN4:27;
    hence thesis by A1,A5,SPRECT_2:26;
  end;
end;

theorem
  for f being non constant standard special_circular_sequence, g being
  FinSequence of TOP-REAL2 holds g is_in_the_area_of f iff g is_in_the_area_of
  SpStSeq L~f
proof
  let f be non constant standard special_circular_sequence, g be FinSequence
  of TOP-REAL2;
A1: S-bound L~SpStSeq L~f = S-bound L~f by SPRECT_1:67;
A2: N-bound L~SpStSeq L~f = N-bound L~f by SPRECT_1:68;
A3: E-bound L~SpStSeq L~f = E-bound L~f by SPRECT_1:69;
A4: W-bound L~SpStSeq L~f = W-bound L~f by SPRECT_1:66;
  thus g is_in_the_area_of f implies g is_in_the_area_of SpStSeq L~f
  proof
    assume
A5: g is_in_the_area_of f;
    let n;
    thus thesis by A4,A1,A2,A3,A5,SPRECT_2:def 1;
  end;
  assume
A6: g is_in_the_area_of SpStSeq L~f;
  let n;
  thus thesis by A4,A1,A2,A3,A6,SPRECT_2:def 1;
end;

theorem
  for f being rectangular special_circular_sequence, g being
S-Sequence_in_R2 st g/.1 in LeftComp f & g/.len g in RightComp f holds L_Cut(g,
  Last_Point(L~g,g/.1,g/.len g,L~f)) is_in_the_area_of f
proof
  let f be rectangular special_circular_sequence, g be S-Sequence_in_R2 such
  that
A1: g/.1 in LeftComp f and
A2: g/.len g in RightComp f;
A3: L~g meets L~f by A1,A2,Th50;
  1 in dom g by FINSEQ_5:6;
  then
A4: len g >= 1 by FINSEQ_3:27;
  set lp = Last_Point(L~g,g/.1,g/.len g,L~f), ilp = Index(lp,g), h = L_Cut(g,
  lp);
A6: L~g /\ L~f is closed;
  L~g is_an_arc_of g/.1,g/.len g by TOPREAL1:31;
  then
A7: lp in L~g /\ L~f by A3,JORDAN5C:def 2;
  then
A8: lp in L~g by XBOOLE_0:def 4;
  then
A9: 1 <= ilp by JORDAN3:41;
A10: lp in LSeg(g,ilp) by A8,JORDAN3:42;
A11: ilp < len g by A8,JORDAN3:41;
  then
A12: ilp+1<=len g by NAT_1:13;
  given n such that
A13: n in dom h and
A14: W-bound L~f > (h/.n)`1 or (h/.n)`1 > E-bound L~f or S-bound L~f > (
  h/.n)`2 or (h/.n)`2 > N-bound L~f;
A15: 1 <= n by A13,FINSEQ_3:27;
  then
A16: ilp+n-'1 = ilp+(n-'1) by NAT_D:38;
  LeftComp f = {p : not(W-bound L~f <= p`1 & p`1 <= E-bound L~f & S-bound
  L~f <= p`2 & p`2 <= N-bound L~f)} by Th54;
  then
A17: h/.n in LeftComp f by A14;
A18: 1<=ilp+1 by NAT_1:11;
  then
A19: ilp+1 in dom g by A12,FINSEQ_3:27;
A20: LeftComp f misses RightComp f by SPRECT_1:96;
A21: L~f misses LeftComp f by Th43;
A22: len g in dom g by FINSEQ_5:6;
A23: lp in L~f by A7,XBOOLE_0:def 4;
  now
    assume
A24: n = 1;
    per cases;
    suppose
      lp <> g.(ilp+1);
      then h = <*lp*>^mid(g,ilp+1,len g) by JORDAN3:def 4;
      then h/.n = lp by A24,FINSEQ_5:16;
      hence contradiction by A21,A23,A17,XBOOLE_0:3;
    end;
    suppose
A25:  lp = g.(ilp+1);
      then h = mid(g,ilp+1,len g) by JORDAN3:def 4;
      then h/.n = g/.(1+(ilp+1)-'1) by A22,A13,A12,A19,A24,SPRECT_2:7
        .= g/.(ilp+1) by NAT_D:34
        .= lp by A19,A25,PARTFUN1:def 8;
      hence contradiction by A21,A23,A17,XBOOLE_0:3;
    end;
  end;
  then
A26: n > 1 by A15,XXREAL_0:1;
  then
A27: 1+1 < ilp+n by A9,XREAL_1:10;
  then
A28: 1 <= ilp+n-'1 by NAT_D:49;
  set m = mid(g,ilp+n,len g);
A29: len<*lp*> = 1 by FINSEQ_1:56;
A30: n <= len h by A13,FINSEQ_3:27;
  then
A31: n+ilp <= len h + ilp by XREAL_1:8;
A32: n = n -'1 +1 by A15,XREAL_1:237;
  then
A33: 1 <= n-'1 by A26,NAT_1:13;
A34: len mid(g,ilp+1,len g)=len g-'(ilp+1)+1 by A12,A18,JORDAN4:20
    .= len g -' ilp by A8,JORDAN3:41,NAT_2:9;
  then
A35: ilp + len mid(g,ilp+1,len g) + 1 = len g + 1 by A11,XREAL_1:237;
  now
A36: ilp+1 <= ilp+n-'1 by A16,A33,XREAL_1:8;
    assume
A37: lp <> g.(ilp+1);
    then
A38: h = <*lp*>^mid(g,ilp+1,len g) by JORDAN3:def 4;
    then
A39: len h = 1 + len mid(g,ilp+1,len g) by A29,FINSEQ_1:35;
    then
A40: ilp+n-'1 <= len g by A35,A31,NAT_D:53;
A41: len h -' 1 = len mid(g,ilp+1,len g) by A39,NAT_D:34;
    then n-'1 <= len mid(g,ilp+1,len g) by A30,NAT_D:42;
    then
A42: n-'1 in dom mid(g,ilp+1,len g) by A33,FINSEQ_3:27;
    h/.n = (mid(g,ilp+1,len g))/.(n-'1) by A30,A32,A29,A33,A38,A41,NAT_D:42
,SEQ_4:153;
    then
A43: h/.n = g/.(n-'1+(ilp+1)-'1) by A22,A12,A19,A42,SPRECT_2:7
      .= g/.(n+ilp-'1) by A32;
    then
A44: ilp+n-'1<>len g by A2,A17,A20,XBOOLE_0:3;
    then
A45: ilp+n-'1<len g by A40,XXREAL_0:1;
    reconsider m = mid(g,ilp+n-'1,len g) as S-Sequence_in_R2 by A4,A28,A40,A44,
JORDAN3:39;
A46: ilp+n-'1 in dom g by A28,A40,FINSEQ_3:27;
    then
A47: m/.len m in RightComp f by A2,A22,SPRECT_2:13;
    m/.1 in LeftComp f by A22,A17,A43,A46,SPRECT_2:12;
    then L~f meets L~m by A47,Th50;
    then consider q being set such that
A48: q in L~f and
A49: q in L~m by XBOOLE_0:3;
    reconsider q as Point of TOP-REAL 2 by A49;
    consider i such that
A50: 1 <= i and
A51: i+1 <= len m and
A52: q in LSeg(m,i) by A49,SPPOL_2:13;
A53: len m = len g-'(ilp+n-'1)+1 by A28,A40,JORDAN4:20;
    then i <= len g-'(ilp+n-'1) by A51,XREAL_1:8;
    then
A54: i+(ilp+n-'1) <= len g by A40,NAT_D:54;
    i < len m by A51,NAT_1:13;
    then
A55: LSeg(m,i)=LSeg(g,i+(ilp+n-'1)-'1) by A28,A50,A53,A45,JORDAN4:31;
    set j = i+(ilp+n-'1)-'1;
    i <= i+(ilp+n-'1) by NAT_1:11;
    then
A56: j + 1 <= len g by A50,A54,XREAL_1:237,XXREAL_0:2;
    j = i-'1+(ilp+n-'1) by A50,NAT_D:38;
    then j >= ilp+n-'1 by NAT_1:11;
    then
A57: ilp +1 <= j by A36,XXREAL_0:2;
A58: lp <> g/.(ilp+1) by A19,A37,PARTFUN1:def 8;
A59: now
      assume lp = q;
      then
A60:  lp in LSeg(g,ilp) /\ LSeg(g,j) by A10,A52,A55,XBOOLE_0:def 4;
      then
A61:  LSeg(g,ilp) meets LSeg(g,j) by XBOOLE_0:4;
      per cases by A57,XXREAL_0:1;
      suppose
A62:    ilp+1 = j;
        then ilp + (1+1) <= len g by A56;
        then LSeg(g,ilp) /\ LSeg(g,ilp+1) = {g/.(ilp+1)} by A9,TOPREAL1:def 8;
        hence contradiction by A58,A60,A62,TARSKI:def 1;
      end;
      suppose
        ilp+1 < j;
        hence contradiction by A61,TOPREAL1:def 9;
      end;
    end;
    1 <= j by A18,A57,XXREAL_0:2;
    then ilp >= j by A3,A10,A12,A9,A48,A52,A55,A56,A59,JORDAN5C:28;
    then ilp >= ilp+1 by A57,XXREAL_0:2;
    hence contradiction by XREAL_1:31;
  end;
  then
A63: h = mid(g,ilp+1,len g) by JORDAN3:def 4;
  then
A64: ilp + len h = len g by A11,A34,XREAL_1:237;
  then
A65: m = g/^(ilp+n-'1) by A31,FINSEQ_6:123;
A66: 1 <= ilp+n by A27,XXREAL_0:2;
  ilp+n -' 1 + 1 = ilp+n by A27,XREAL_1:237,XXREAL_0:2;
  then ilp+n-'1 < len g by A31,A64,NAT_1:13;
  then
A67: m/.len m in RightComp f by A2,A65,JORDAN4:18;
A68: h/.n = g/.(n+(ilp+1)-'1) by A22,A13,A12,A19,A63,SPRECT_2:7
    .= g/.(n+ilp+1-'1)
    .= g/.(ilp+n) by NAT_D:34;
  then
A69: ilp+n <> len g by A2,A17,A20,XBOOLE_0:3;
  then reconsider m as S-Sequence_in_R2 by A4,A31,A64,A66,JORDAN3:39;
  ilp+n in dom g by A31,A64,A66,FINSEQ_3:27;
  then m/.1 in LeftComp f by A22,A17,A68,SPRECT_2:12;
  then L~f meets L~m by A67,Th50;
  then consider q being set such that
A70: q in L~f and
A71: q in L~m by XBOOLE_0:3;
  reconsider q as Point of TOP-REAL 2 by A71;
  consider i such that
A72: 1 <= i and
A73: i+1 <= len m and
A74: q in LSeg(m,i) by A71,SPPOL_2:13;
  set j = i+(ilp+n)-'1;
A75: j = i-'1+(ilp+n) by A72,NAT_D:38;
  then
A76: j >= ilp+n by NAT_1:11;
  len m = len g -' (ilp+n) + 1 by A4,A31,A64,A66,FINSEQ_6:124;
  then
A77: i < len g -'(ilp+n) +1 by A73,NAT_1:13;
  then i-'1 < len g-'(ilp+n) by A72,NAT_D:54;
  then i-'1+(ilp+n) < len g by NAT_D:53;
  then
A78: j + 1 <= len g by A75,NAT_1:13;
  ilp+n < len g by A31,A64,A69,XXREAL_0:1;
  then
A79: LSeg(m,i) = LSeg(g,i+(ilp+n)-'1) by A66,A72,A77,JORDAN4:31;
  ilp+1 < ilp+n by A26,XREAL_1:8;
  then
A80: j > ilp+1 by A76,XXREAL_0:2;
A81: now
    assume lp = q;
    then lp in LSeg(g,ilp) /\ LSeg(g,j) by A10,A74,A79,XBOOLE_0:def 4;
    then LSeg(g,ilp) meets LSeg(g,j) by XBOOLE_0:4;
    hence contradiction by A80,TOPREAL1:def 9;
  end;
  1 <= j by A66,A76,XXREAL_0:2;
  then ilp >= j by A3,A10,A12,A9,A70,A74,A79,A81,A78,JORDAN5C:28;
  then ilp >= ilp+1 by A80,XXREAL_0:2;
  hence contradiction by XREAL_1:31;
end;

theorem
  for f being non constant standard special_circular_sequence st 1 <= i
& i < len GoB f & 1 <= j & j < width GoB f holds Int cell(GoB f,i,j) misses L~
  SpStSeq L~f
proof
  let f be non constant standard special_circular_sequence such that
A1: 1 <= i and
A2: i < len GoB f and
A3: 1 <= j and
A4: j < width GoB f;
A5: i+1 <= len GoB f by A2,NAT_1:13;
  set G = GoB f;
A6: Int cell(G,i,j) = { |[r,s]| where r,s is Real: G*(i,1)`1 < r & r < G*(i+
  1,1)`1 & G*(1,j)`2 < s & s < G*(1,j+1)`2 } by A1,A2,A3,A4,GOBOARD6:29;
A7: N-bound L~SpStSeq L~f = N-bound L~f by SPRECT_1:68;
A8: 1 <= width GoB f by GOBRD11:34;
  then
A9: <*(GoB f)*(i,1)*> is_in_the_area_of f by A1,A2,Th66;
  1 <= i+1 by A1,NAT_1:13;
  then
A10: <*(GoB f)*(i+1,1)*> is_in_the_area_of f by A8,A5,Th66;
  assume Int cell(GoB f,i,j) meets L~SpStSeq L~f;
  then consider x being set such that
A11: x in Int cell(GoB f,i,j) and
A12: x in L~SpStSeq L~f by XBOOLE_0:3;
A13: W-bound L~SpStSeq L~f = W-bound L~f by SPRECT_1:66;
A14: 1 <= len GoB f by GOBRD11:34;
  then
A15: <*(GoB f)*(1,j)*> is_in_the_area_of f by A3,A4,Th66;
A16: j+1 <= width GoB f by A4,NAT_1:13;
  1 <= j+1 by A3,NAT_1:13;
  then
A17: <*(GoB f)*(1,j+1)*> is_in_the_area_of f by A14,A16,Th66;
A18: L~SpStSeq L~f = { p: p`1 = W-bound L~SpStSeq L~f & p`2 <= N-bound L~
SpStSeq L~f & p`2 >= S-bound L~SpStSeq L~f or p`1 <= E-bound L~SpStSeq L~f & p
`1 >= W-bound L~SpStSeq L~f & p`2 = N-bound L~SpStSeq L~f or p`1 <= E-bound L~
SpStSeq L~f & p`1 >= W-bound L~SpStSeq L~f & p`2 = S-bound L~SpStSeq L~f or p`1
  = E-bound L~SpStSeq L~f & p`2 <= N-bound L~SpStSeq L~f & p`2 >= S-bound L~
  SpStSeq L~f} by Th52;
A19: E-bound L~SpStSeq L~f = E-bound L~f by SPRECT_1:69;
  consider p such that
A20: p = x and
A21: p`1 = W-bound L~SpStSeq L~f & p`2 <= N-bound L~SpStSeq L~f & p`2 >=
  S-bound L~SpStSeq L~f or p`1 <= E-bound L~SpStSeq L~f & p`1 >= W-bound L~
SpStSeq L~f & p`2 = N-bound L~SpStSeq L~f or p`1 <= E-bound L~SpStSeq L~f & p`1
  >= W-bound L~SpStSeq L~f & p`2 = S-bound L~SpStSeq L~f or p`1 = E-bound L~
  SpStSeq L~f & p`2 <= N-bound L~SpStSeq L~f & p`2 >= S-bound L~SpStSeq L~f by
A12,A18;
A22: S-bound L~SpStSeq L~f = S-bound L~f by SPRECT_1:67;
  consider r,s being Real such that
A23: x = |[r,s]| and
A24: G*(i,1)`1 < r and
A25: r < G*(i+1,1)`1 and
A26: G*(1,j)`2 < s and
A27: s < G*(1,j+1)`2 by A6,A11;
A28: p`1 = r by A23,A20,EUCLID:56;
A29: p`2 = s by A23,A20,EUCLID:56;
  per cases by A21;
  suppose
A30: p`1 = W-bound L~SpStSeq L~f;
A31: 1 in dom<*G*(i,1)*> by FINSEQ_5:6;
    <*G*(i,1)*>/.1 = G*(i,1) by FINSEQ_4:25;
    hence contradiction by A24,A9,A28,A13,A30,A31,SPRECT_2:def 1;
  end;
  suppose
A32: p`2 = N-bound L~SpStSeq L~f;
A33: 1 in dom<*G*(1,j+1)*> by FINSEQ_5:6;
    <*G*(1,j+1)*>/.1 = G*(1,j+1) by FINSEQ_4:25;
    hence contradiction by A27,A17,A29,A7,A32,A33,SPRECT_2:def 1;
  end;
  suppose that
A34: p`2 = S-bound L~SpStSeq L~f;
A35: 1 in dom<*G*(1,j)*> by FINSEQ_5:6;
    <*G*(1,j)*>/.1 = G*(1,j) by FINSEQ_4:25;
    hence contradiction by A26,A15,A29,A22,A34,A35,SPRECT_2:def 1;
  end;
  suppose that
A36: p`1 = E-bound L~SpStSeq L~f;
A37: 1 in dom<*G*(i+1,1)*> by FINSEQ_5:6;
    <*G*(i+1,1)*>/.1 = G*(i+1,1) by FINSEQ_4:25;
    hence contradiction by A25,A10,A28,A19,A36,A37,SPRECT_2:def 1;
  end;
end;

theorem
  for f, g being FinSequence of TOP-REAL 2, p being Point of TOP-REAL 2
st g is_in_the_area_of f & <*p*> is_in_the_area_of f & g is being_S-Seq & p in
  L~g holds L_Cut(g,p) is_in_the_area_of f
proof
  let f, g be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2 such that
A1: g is_in_the_area_of f and
A2: <*p*> is_in_the_area_of f and
A3: g is being_S-Seq;
  2 <= len g by A3,TOPREAL1:def 10;
  then 1 <= len g by XXREAL_0:2;
  then
A4: len g in dom g by FINSEQ_3:27;
  assume p in L~g;
  then Index(p,g) < len g by JORDAN3:41;
  then
A5: Index(p,g)+1 <= len g by NAT_1:13;
  1<=Index(p,g)+1 by NAT_1:11;
  then
A6: Index(p,g)+1 in dom g by A5,FINSEQ_3:27;
  per cases;
  suppose
    p<>g.(Index(p,g)+1);
    then
A7: L_Cut(g,p) = <*p*>^mid(g,Index(p,g)+1,len g) by JORDAN3:def 4;
    mid(g,Index(p,g)+1,len g) is_in_the_area_of f by A1,A4,A6,SPRECT_2:26;
    hence thesis by A2,A7,SPRECT_2:28;
  end;
  suppose
    p=g.(Index(p,g)+1);
    then L_Cut(g,p) = mid(g,Index(p,g)+1,len g) by JORDAN3:def 4;
    hence thesis by A1,A4,A6,SPRECT_2:26;
  end;
end;
