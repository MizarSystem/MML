:: Integral of Measurable Function
::  by Noboru Endou and Yasunari Shidama
:: 
:: Received May 24, 2006
:: Copyright (c) 2006 Association of Mizar Users

environ

 vocabularies PARTFUN1, SUPINF_1, MEASURE1, FINSEQ_1, RELAT_1, FUNCT_1, RAT_1,
      ORDINAL2, COMPLEX1, INT_1, SEQ_1, SEQ_2, NAT_1, MEASURE6, BOOLE, ARYTM,
      ARYTM_1, MESFUNC1, TARSKI, ARYTM_3, SQUARE_1, RLVECT_1, GROUP_1, PROB_1,
      PROB_2, INTEGRA1, MESFUNC2, MESFUNC3, SEQFUNC, TDGROUP, MESFUNC5,
      SUPINF_2, FINSET_1, MEMBERED, SETLIM_1, FUNCT_3, RFUNCT_3;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, ORDINAL1, NUMBERS,
      XCMPLX_0, XXREAL_0, XREAL_0, REAL_1, BINARITH, ORDINAL2, 
      RELAT_1, FUNCT_1,
      FINSEQ_1, RELSET_1, FINSET_1, PARTFUN1, FUNCT_2, BINOP_1, FUNCOP_1,
      INT_1, NAT_1, RAT_1, MEMBERED, SQUARE_1, SUPINF_1, SUPINF_2, MEASURE1,
      EXTREAL1, FUNCT_3, EXTREAL2, MESFUNC1, MESFUNC2, MESFUNC3, FINSEQOP,
      CONVFUN1, RFUNCT_3, PRE_CIRC, ABSVALUE, PROB_1, PROB_2, MEASURE6, NEWTON,
      LIMFUNC1, SEQFUNC, KURATO_2, SETLIM_1;
 constructors REAL_1, NAT_1, BINARITH, FINSEQOP, FINSOP_1, EXTREAL1, SUPINF_2,
      RAT_1, FUNCSDOM, SEQ_2, MESFUNC1, MESFUNC3, MEASURE6, MESFUNC2, CONVFUN1,
      MEASURE3, SEQFUNC, PARTFUN2, LIMFUNC1, INTEGRA2, RFUNCT_3, KURATO_2,
      SETLIM_1, PRE_CIRC, WELLORD2, FUNCT_3, EXTREAL2, FINSEQ_1, XXREAL_0,
      SUPINF_1, NUMBERS, SQUARE_1, ORDINAL1, ORDINAL2;
 registrations SUBSET_1, SUPINF_1, NAT_1, RELSET_1, XREAL_0, RAT_1, SEQ_1,
      MEMBERED, ORDINAL1, PARTFUN1, FINSEQ_1, MEASURE1, COMPLEX1, INT_1,
      FINSET_1, WSIERP_1, PRE_CIRC, NUMBERS, XXREAL_0, ARYTM_3;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, XBOOLE_0, SUPINF_2, FINSEQ_1, CARD_3, MESFUNC1, MESFUNC3,
      MEASURE6;
 theorems MEASURE1, MEASURE2, TARSKI, SUPINF_1, PARTFUN1, FUNCT_1, FUNCT_2,
      SEQFUNC, MEASURE6, NAT_1, REAL_1, SUPINF_2, RELSET_1, EXTREAL1, INT_1,
      EXTREAL2, MESFUNC1, FINSEQ_1, XREAL_0, PROB_1, PROB_2, XBOOLE_0, NAT_2,
      XBOOLE_1, XCMPLX_1, MESFUNC2, CONVFUN1, MESFUNC3, NEWTON, XREAL_1,
      SQUARE_1, COMPLEX1, MEASURE5, PREPOWER, TOPREALA, PCOMPS_2, XXREAL_0,
      MESFUNC4, ZFMISC_1, PRE_CIRC, KURATO_2, SETLIM_1, JORDAN3, BINARITH,
      FINSEQ_3, REAL_2, RAT_1, HAHNBAN, FUNCT_3, RELAT_1, FINSUB_1, GRFUNC_1,
      FINSEQ_2, ORDINAL1;
 schemes FUNCT_1, FUNCT_2, FINSEQ_1, FINSEQ_2, NAT_1, SEQFUNC, PARTFUN1,
      KURATO_2;

begin :: Lemmas for extended real numbers

theorem AR01:
  for x,y be R_eal holds |.x-y.| = |.y-x.|
proof
   let x,y be R_eal;
   |.y-x.| = |.-(x-y).| by EXTREAL2:15;
   hence thesis by EXTREAL2:66;
end;

theorem AR01q:
  for x,y be R_eal holds y-x <= |.x-y.|
proof
   let x,y be R_eal;
   x-y <= |.x-y.| & -|.x-y.| <= (x-y) by EXTREAL2:57; then
   x-y <= |.x-y.| & -(x-y) <= |.x-y.| by EXTREAL2:47;
   hence y-x <= |.x-y.| by EXTREAL2:15;
end;

theorem AR01a:
for x,y be R_eal, e be real number st |.x-y.| < e &
  not ((x = +infty & y = +infty) or (x = -infty & y = -infty))
  holds x <> +infty & x <> -infty & y <> +infty & y <>-infty
proof
   let x,y be R_eal, e be real number;
   assume A1:|.x-y.| < e;
   x-y <= |.x-y.| & y-x <= |.x-y.| by AR01q,EXTREAL2:57; then
   x-y < R_EAL e & y-x < e by A1,XXREAL_0:2;
   hence thesis by EXTREAL2:29;
end;

theorem AR01t:
for x,y be R_eal st
 (for e be real number st 0 < e holds x < y + R_EAL e) holds x <= y
proof
   let x,y be R_eal;
   assume
A1: for e be real number st 0 < e holds x < y + R_EAL e;
   per cases;
   suppose
A2: y = +infty or y=-infty;
    per cases by A2;
    suppose y=+infty;
     hence x<= y by SUPINF_1:20;
    end;
    suppose
A3:  y=-infty;
     x < y+ R_EAL(1) by A1;
     hence x <= y by A3,SUPINF_2:def 2;
    end;
   end;
   suppose
A4: y <> +infty & y <>-infty;
    per cases;
    suppose
A5:  x = +infty;
     x < y+ R_EAL(1) by A1;
     hence x <= y by A5,SUPINF_1:24;
    end;
    suppose
A6:  x <> +infty;
     now assume
A7:   x <> -infty;
      assume
A8:    not x <= y;
      y <= +infty & -infty <= x by SUPINF_1:20,21; then
      y < +infty & -infty < x by A4,A7,SUPINF_1:22; then
B0:   0 < x- y by A8,EXTREAL1:41;
      reconsider x1=x as Real by A6,A7,EXTREAL1:1;
      reconsider y1=y as Real by A4,EXTREAL1:1;
B1:   x-y=x1-y1 by SUPINF_2:5; then 
      x < y + R_EAL(x1-y1) by A1,B0; then
      x < y + x-y by B1,EXTREAL1:11; then
      x < x +(y-y) by EXTREAL1:11,A4; then
      x < x +0. by EXTREAL1:9;
      hence contradiction by SUPINF_2:18;
     end;
     hence x <= y by SUPINF_1:21;
    end;
   end;
end;

theorem AR05p:
for x,y,t being R_eal holds
  t <> -infty & t <> +infty & x < y implies x + t < y + t
proof
   let x,y,t be R_eal;
   assume
A1: t <> -infty & t <> +infty & x < y; then
A2:x + t <= y + t by SUPINF_2:14;
A3:t-t = 0 by EXTREAL1:9;
   now assume x + t = y + t; then
    x + (t -t) = y + t-t by A1,EXTREAL1:11; then
    x+ R_EAL 0 = y + (t-t) by A3,A1,EXTREAL1:11; then
    x=y+ R_EAL 0 by A3,SUPINF_2:18;
    hence contradiction by A1,SUPINF_2:18;
   end;
   hence thesis by A2,SUPINF_1:22;
end;

theorem
for x,y,t being R_eal holds
  t <> -infty & t <> +infty & x < y implies x - t < y - t
proof
   let x,y,t be R_eal;
   assume
A1: t <> -infty & t <> +infty & x < y; then
A2:x - t <= y - t by SUPINF_2:15;
A3:t-t = 0 by EXTREAL1:9;
   now assume x - t = y - t; then
    x - (t -t) = y - t+t by A1,EXTREAL2:41; then
    x- R_EAL 0 = y - (t-t) by A3,A1,EXTREAL2:41; then
    x=y+ R_EAL 0 by A3,SUPINF_2:18,EXTREAL1:24;
    hence contradiction by A1,SUPINF_2:18;
   end;
   hence thesis by A2,SUPINF_1:22;
end;

theorem Cor1:
for a,b be real number holds
R_EAL a + R_EAL b = a+b & -R_EAL a = -a
proof
   let a,b be real number;
   reconsider a'=a,b'=b,c'=a+b as Real by XREAL_0:def 1;
   a' = R_EAL a & b' = R_EAL b & c' = R_EAL(a+b);
   hence R_EAL a + R_EAL b = a+b by SUPINF_2:1;
   -a' = R_EAL(-a);
   hence -R_EAL a = -a by SUPINF_2:3;
end;

theorem Cor2:
for n be Nat, p be R_eal st 0 <= p & p < n holds
 ex k be Nat st 1 <= k & k <= 2|^n*n & (k-1)/(2|^n) <= p & p < k/(2|^n)
proof
   let n be Nat;
   let p be R_eal;
   assume
A1: 0 <= p & p < n; then 
   p < R_EAL n; then
   reconsider p1 = p as Real by A1,SUPINF_2:19,MEASURE5:16;
   reconsider n as Element of NAT by ORDINAL1:def 13;
   set k = [\ p1*(2|^n)+1 /];
B1:0 <= p1 & 0 < 2|^n by A1,PREPOWER:13; then
A2:0 <= p1*(2|^n) by XREAL_1:129;
A3:p1*(2|^n) + 1 - 1 = p1*(2|^n); then
B3:0 < k by A2,INT_1:def 4; then
   reconsider k as Element of NAT by INT_1:16;
B4:0+1 <= k by B3,NAT_1:38;
B5:now assume
C1: k > 2|^n * n;
    k <= p1*(2|^n)+1 by INT_1:def 4; then
C3: 2|^n * n < p1*(2|^n)+1 by C1,XREAL_1:2;
    p1*(2|^n) < 2|^n * n by A1,B1,XREAL_1:70; then
C4: p1*(2|^n) +1 < 2|^n * n +1 by XREAL_1:8;
    reconsider N = 2|^n * n as Integer;
    [\ N /] = N by INT_1:47;
    hence contradiction by C1,C3,C4,TOPREALA:2;
   end;
   take k;
   p1*(2|^n) < k & k <= p1*(2|^n)+1 by A3,INT_1:def 4; then
   p1 < k/(2|^n) & k-1 <= p1*(2|^n) by B1,XREAL_1:22,83;
   hence thesis by B4,B5,B1,XREAL_1:81;
end;

theorem Cor3:
for n,k be Nat, p be R_eal st
 1 <= k & k <= 2|^n*n & n <= p & (k-1)/(2|^n) <= p
  holds k/(2|^n) <= p
proof
   let n,k be Nat;
   let p be R_eal;
   assume
W1:1 <= k & k <= 2|^n*n & n <= p & (k-1)/(2|^n) <= p;
Z1:0 < 2|^n by PREPOWER:13;
   assume p < k/(2|^n); then
    n < k/(2|^n) by W1,XXREAL_0:2;
   hence contradiction by W1,Z1,XREAL_1:81;
end;

theorem CorD:
for x,y,w,z being R_eal st -infty < w holds
  x < y & w < z implies x + w < y + z
proof
   let x,y,w,z be R_eal;
   assume
A1: -infty < w;
   assume
A2: x < y & w <z;
   per cases;
   suppose
A3: y = +infty; then
A4: y + z = +infty by A1,A2,SUPINF_2:def 2;
    z <= +infty by SUPINF_1:20; then
    x + w <> +infty by A2,A3,SUPINF_2:8;
    hence thesis by A4,SUPINF_1:24;
   end;
   suppose
B1: y <> +infty;
     z <= +infty by SUPINF_1:20; then
     w in REAL by A1,A2,XXREAL_0:14; then
B2:  x + w < y + w by A2,MEASURE6:12;
     -infty <= x by SUPINF_1:21; then
     y in REAL by XXREAL_0:14,B1,A2; then
     y + w < y + z by MEASURE6:12,A2;
    hence thesis by B2,SUPINF_1:29;
   end;
end;

theorem CorE:
for x,y,k being R_eal st 0 <= k holds
 k*max(x,y) = max(k*x,k*y) & k*min(x,y) = min(k*x,k*y)
proof
   let x,y,k be R_eal;
   assume
A1: 0 <= k;
   now
   per cases by EXTREAL2:95;
    suppose
A2:  max(x,y) = x; then
     y <= x by XXREAL_0:def 9; then
     k*y <= k*x by A1,EXTREAL1:42;
     hence k*max(x,y) = max(k*x,k*y) by A2,XXREAL_0:def 9;
    end;
    suppose
A3:  max(x,y) = y; then
     x <= y by XXREAL_0:def 9; then
     k*x <= k*y by A1,EXTREAL1:42;
     hence k*max(x,y) = max(k*x,k*y) by A3,XXREAL_0:def 9;
    end;
   end;
   hence k*max(x,y) = max(k*x,k*y);
   per cases by EXTREAL2:84;
   suppose
A4: min(x,y) = x; then
    x <= y by XXREAL_0:def 8; then
    k*x <= k*y by A1,EXTREAL1:42;
    hence k*min(x,y) = min(k*x,k*y) by A4,XXREAL_0:def 8;
   end;
   suppose
A5: min(x,y) = y; then
    y <= x by XXREAL_0:def 8; then
    k*y <= k*x by A1,EXTREAL1:42;
    hence k*min(x,y) = min(k*x,k*y) by A5,XXREAL_0:def 8;
   end;
end;

theorem
for x,y,k being R_eal st k <= 0 holds
 k*min(x,y) = max(k*x,k*y) & k*max(x,y) = min(k*x,k*y)
proof
   let x,y,k be R_eal;
   assume
A1: k <= 0;
   hereby per cases by EXTREAL2:95;
    suppose max(x,y) = x; then
B1:  y <= x by XXREAL_0:def 9; then
     k*x <= k*y by A1,EXTREAL1:43; then
     max(k*x,k*y) = k*y by XXREAL_0:def 9;
     hence k*min(x,y) = max(k*x,k*y) by B1,XXREAL_0:def 8;
    end;
    suppose max(x,y) = y; then
B1:  x <= y by XXREAL_0:def 9; then
     k*y <= k*x by A1,EXTREAL1:43; then
     max(k*x,k*y) = k*x by XXREAL_0:def 9;
     hence k*min(x,y) = max(k*x,k*y) by B1,XXREAL_0:def 8;
    end;
   end;
   per cases by EXTREAL2:84;
   suppose min(x,y) = x; then
B1: x <= y by XXREAL_0:def 8; then
    k*y <= k*x by A1,EXTREAL1:43; then
    min(k*x,k*y) = k*y by XXREAL_0:def 8;
    hence k*max(x,y) = min(k*x,k*y) by B1,XXREAL_0:def 9;
   end;
   suppose min(x,y) = y; then
B1: y <= x by XXREAL_0:def 8; then
    k*x <= k*y by A1,EXTREAL1:43; then
    min(k*y,k*x) = k*x by XXREAL_0:def 8;
    hence k*max(x,y) = min(k*x,k*y) by B1,XXREAL_0:def 9;
   end;
end;

theorem CorI:
  for x,y,z being R_eal holds
    0 <= x & 0 <= z & z + x <= y implies z <= y
proof
   let x,y,z be R_eal;
   assume
A1:0 <= x & 0 <= z & z + x <= y;
   per cases by A1,SUPINF_2:2;
   suppose A4:x in REAL; then
A7: x < +infty & -infty < x by XXREAL_0:9,12;
    (z + x) - x = z & y - 0. = y by A4,MEASURE6:8,21;
    hence thesis by A1,A7,SUPINF_2:15;
   end;
   suppose x = +infty; then
    z+x = +infty by A1,SUPINF_2:def 2; then
    +infty = y by A1,SUPINF_1:24;
    hence thesis by SUPINF_1:20;
   end;
end;

begin :: Lemmas for partial function of non empty set,extended real numbers

definition let IT be set;
  attr IT is nonpositive means :DefNP:
  for x being R_eal holds x in IT implies x <= 0;
end;

definition let R be Relation;
  attr R is nonpositive means :DefNP1:
  rng R is nonpositive;
end;

theorem ThNP01:
for X being set, F being PartFunc of X,ExtREAL holds
   F is nonpositive iff for n being set holds F.n <= 0.
proof
   let X be set,
       F be PartFunc of X,ExtREAL;
   hereby assume F is nonpositive; then 
A1: rng F is nonpositive by DefNP1;
    let n be set;
    per cases;
    suppose n in dom F; then
     F.n in rng F by FUNCT_1:def 5;
     hence F.n <= 0. by A1,DefNP;
    end;
    suppose not n in dom F;
     hence F.n <= 0. by FUNCT_1:def 4;
    end;
   end;
   assume
A2:for n being set holds F.n <= 0.;
   let y be R_eal;
   assume y in rng F; then 
   ex x being set st x in dom F & y = F.x by FUNCT_1:def 5;
   hence thesis by A2;
  end;

theorem ThNP02:
for X being set, F being PartFunc of X,ExtREAL st
  for n being set st n in dom F holds F.n <= 0. holds F is nonpositive 
proof
   let X be set,
       F be PartFunc of X,ExtREAL such that
A2:for n being set st n in dom F holds F.n <= 0.;
   let y be R_eal;
   assume y in rng F; then 
   consider x being set such that
A3: x in dom F & y = F.x by FUNCT_1:def 5;
   thus thesis by A2,A3;
end;

definition let R be Relation;
  attr R is without-infty means :Def0001:
  not -infty in rng R;

  attr R is without+infty means :Def0002:
  not +infty in rng R;
end;

definition
  let X be non empty set, f be PartFunc of X,ExtREAL;
  redefine attr f is without-infty means :Def0003:
  for x being set holds -infty < f.x;
  compatibility 
proof
   hereby assume f is without-infty; then
A1: not -infty in rng f by Def0001;
    hereby let x be set;
     per cases;
     suppose x in dom f; then
      f.x <> -infty by A1,FUNCT_1:def 5;
      hence -infty < f.x by SUPINF_1:23;
     end;
     suppose not x in dom f; then
      0 = f.x by FUNCT_1:def 4;
      hence -infty < f.x;
     end;
    end;
   end;
   assume 
A2: for x being set holds -infty < f.x;
   now assume -infty in rng f; then
    consider x be set such that
A3:  x in dom f & f.x = -infty by FUNCT_1:def 5;
    thus contradiction by A2,A3;
   end;
   hence f is without-infty by Def0001;
end;   

  redefine attr f is without+infty means :Def0004:
  for x being set holds f.x < +infty;
  compatibility 
proof
   hereby assume f is without+infty; then
A1: not +infty in rng f by Def0002;
    hereby let x be set;
     per cases;
     suppose x in dom f; then
      f.x <> +infty by A1,FUNCT_1:def 5;
      hence f.x < +infty by SUPINF_1:24;
     end;
     suppose not x in dom f; then
      0 = f.x by FUNCT_1:def 4;
      hence f.x < +infty;
     end;
    end;
   end;
   assume 
A2: for x being set holds f.x < +infty;
   now assume +infty in rng f; then
    consider x be set such that
A3:  x in dom f & f.x = +infty by FUNCT_1:def 5;
    thus contradiction by A2,A3;
   end;
   hence f is without+infty by Def0002;
end;   
end;

theorem WMI01:
for X be non empty set, f be PartFunc of X,ExtREAL holds
 (for x be set st x in dom f holds -infty < f.x) iff f is without-infty 
proof
   let X be non empty set,
       f be PartFunc of X,ExtREAL;
   hereby assume A:for x be set st x in dom f holds -infty < f.x;
    now let x be set;
     per cases;
     suppose x in dom f;
      hence -infty < f.x by A;
     end;
     suppose not x in dom f; then
      f.x = 0 by FUNCT_1:def 4;
      hence -infty < f.x;
     end;
    end;
    hence f is without-infty by Def0003;
   end;
   assume f is without-infty;
   hence thesis by Def0003;
end;

theorem WPI01:
for X be non empty set, f be PartFunc of X,ExtREAL holds
 (for x be set st x in dom f holds f.x < +infty) iff f is without+infty 
proof
   let X be non empty set,
       f be PartFunc of X,ExtREAL;
   hereby assume A:for x be set st x in dom f holds f.x < +infty;
    now let x be set;
     per cases;
     suppose x in dom f;
      hence f.x < +infty by A;
     end;
     suppose not x in dom f; then
      f.x = 0 by FUNCT_1:def 4;
      hence f.x < +infty;
     end;
    end;
    hence f is without+infty by Def0004;
   end;
   assume f is without+infty;
   hence thesis by Def0004;
end;

theorem WMI02:
for X be non empty set, f be PartFunc of X,ExtREAL st
 f is nonnegative holds f is without-infty
proof
   let X be non empty set, f be PartFunc of X,ExtREAL;
   assume f is nonnegative; then
   for x be set holds -infty < f.x by SUPINF_2:70;
   hence f is without-infty by Def0003;
end;

theorem Wm02:
for X be non empty set, f be PartFunc of X,ExtREAL st
 f is nonpositive holds f is without+infty
proof
   let X be non empty set, f be PartFunc of X,ExtREAL;
   assume f is nonpositive; then
   for x be set holds f.x < +infty by ThNP01;
   hence f is without+infty by Def0004;
end;

registration let X be non empty set;
  cluster nonnegative -> without-infty PartFunc of X,ExtREAL;
  coherence by WMI02;
  cluster nonpositive -> without+infty PartFunc of X,ExtREAL;
  coherence by Wm02;
end;

theorem WI01:
for X be non empty set,
    S be SigmaField of X,
    f be PartFunc of X,ExtREAL st 
  f is_simple_func_in S holds f is without+infty & f is without-infty
proof
   let X be non empty set,
       S be SigmaField of X,
       f be PartFunc of X,ExtREAL;
   assume A1:f is_simple_func_in S;
   hereby assume not f is without+infty; then
    +infty in rng f by Def0002; then
    f"{+infty} <> {} by FUNCT_1:142; then
    consider x be set such that
X11: x in f"{+infty} by XBOOLE_0:def 1;
X12:x in dom f & f.x in {+infty} by X11,FUNCT_1:def 13; then
    f.x = +infty by TARSKI:def 1; then
X13:|. f.x .| = +infty by EXTREAL2:67;
    f is_finite by A1,MESFUNC2:def 5;
    hence contradiction by X13,X12,MESFUNC2:def 1;
   end;
   hereby assume not f is without-infty; then
    -infty in rng f by Def0001; then
    f"{-infty} <> {} by FUNCT_1:142; then
    consider x be set such that
X21: x in f"{-infty} by XBOOLE_0:def 1;
X22:x in dom f & f.x in {-infty} by X21,FUNCT_1:def 13; then
    f.x = -infty by TARSKI:def 1; then
X23:|. f.x .| = +infty by EXTREAL2:67;
    f is_finite by A1,MESFUNC2:def 5;
    hence contradiction by X23,X22,MESFUNC2:def 1;
   end;
end;

theorem LL0:
for X be non empty set, Y be set, f be PartFunc of X,ExtREAL st
 f is nonnegative holds f|Y is nonnegative
proof
   let X be non empty set,
       Y be set,
       f be PartFunc of X,ExtREAL;
   assume 
A1: f is nonnegative;
   now let x be set;
    assume A2:x in dom(f|Y); then
    (f|Y).x = f.x by FUNCT_1:70;
    hence 0 <= (f|Y).x by A1,A2,SUPINF_2:58;
   end;
   hence f|Y is nonnegative by SUPINF_2:71;
end;

theorem MESFUNC2'2:
for X be non empty set, f,g be PartFunc of X,ExtREAL st
 f is without-infty & g is without-infty holds
   dom(f+g)=dom f /\ dom g
proof
   let X be non empty set;
   let f,g be PartFunc of X,ExtREAL;
   assume f is without-infty & g is without-infty; then
   not -infty in rng f & not -infty in rng g by Def0001; then
   f"{-infty} = {} & g"{-infty} = {} by FUNCT_1:142; then 
   f"{+infty} /\ g"{-infty} \/ f"{-infty} /\ g"{+infty} = {}; then
   dom(f+g) = (dom f /\ dom g)\{} by MESFUNC1:def 3;
   hence dom(f+g)=dom f /\ dom g;
end;

theorem
for X be non empty set, f,g be PartFunc of X,ExtREAL st
 f is without-infty & g is without+infty holds
   dom(f-g)=dom f /\ dom g
proof
   let X be non empty set;
   let f,g be PartFunc of X,ExtREAL;
   assume f is without-infty & g is without+infty; then
   not -infty in rng f & not +infty in rng g by Def0001,Def0002; then
   f"{-infty} = {} & g"{+infty} = {} by FUNCT_1:142; then 
   f"{+infty} /\ g"{+infty} \/ f"{-infty} /\ g"{-infty} = {}; then
   dom(f-g) = (dom f /\ dom g)\{} by MESFUNC1:def 4;
   hence thesis;
end;
    
theorem MESFUNC2'3:
for X be non empty set, S be SigmaField of X, f,g be PartFunc of X,ExtREAL,
    F be Function of RAT,S, r be Real, A be Element of S st
f is without-infty & g is without-infty &
(for p be Rational holds
  F.p = A /\ less_dom(f,R_EAL p) /\ (A /\ less_dom(g,R_EAL (r-p))))
holds A /\ less_dom(f+g,R_EAL r) = union rng F
proof
   let X be non empty set;
   let S be SigmaField of X;
   let f,g be PartFunc of X,ExtREAL;
   let F be Function of RAT,S;
   let r be Real;
   let A be Element of S;
   assume that 
A1:f is without-infty and
A2:g is without-infty and
A3: for p be Rational holds 
     F.p = A /\ less_dom(f,R_EAL p) /\ (A /\ less_dom(g,R_EAL(r-p)));
A4:dom(f+g) = dom f /\ dom g by A1,A2,MESFUNC2'2;
A5:A /\ less_dom(f+g,R_EAL r) c= union rng F
   proof
    let x be set;
    assume 
B2: x in A /\ less_dom(f+g,R_EAL r); then
A6: x in A & x in less_dom(f+g,R_EAL r) by XBOOLE_0:def 3; then
A7: x in dom(f+g) & ex y being R_eal st y = (f+g).x & y < R_EAL r
      by MESFUNC1:def 12; then
A9: x in dom f & x in dom g by A4,XBOOLE_0:def 3;
    reconsider x as Element of X by B2;
A8: f.x + g.x < R_EAL r by A7,MESFUNC1:def 3;
A10:-infty < f.x & -infty < g.x by A1,A2,Def0003; then
A11:f.x < R_EAL r - g.x & f.x <> +infty & g.x <> +infty by A8,EXTREAL2:27; then
    reconsider f1 = f.x, g1 = g.x as Real by A10,EXTREAL1:1;
    R_EAL r - g.x = r - g1 by SUPINF_2:5; then
    consider p being Rational such that
A13: f1 < p & p < r - g1 by A11,RAT_1:22;
    not p <= f1 & not r - p <= g1 by A13,REAL_2:165; then
    x in less_dom(f,R_EAL p) & x in less_dom(g,R_EAL(r-p)) 
      by A9,MESFUNC1:def 12; then
    x in A /\ less_dom(f,R_EAL p) & x in A /\ less_dom(g,R_EAL(r-p))
      by A6,XBOOLE_0:def 3; then 
A15:x in A /\ less_dom(f,R_EAL p)/\(A /\ less_dom(g,R_EAL(r-p)))
      by XBOOLE_0:def 3;
    p in RAT by RAT_1:def 2; then
    p in dom F by FUNCT_2:def 1; then
    x in F.p & F.p in rng F by A3,A15,FUNCT_1:def 5;
    hence thesis by TARSKI:def 4;
   end;
   union rng F c= A /\ less_dom(f+g,R_EAL r)
   proof
    let x be set;
    assume x in union rng F; then 
    consider Y being set such that
A17: x in Y & Y in rng F by TARSKI:def 4;
    consider p being set such that
A18: p in dom F & Y = F.p by A17,FUNCT_1:def 5;
    reconsider p as Rational by A18,RAT_1:def 2;
B1: x in A /\ less_dom(f,R_EAL p)/\(A /\ less_dom(g,R_EAL (r-p)))
      by A3,A17,A18; then
    x in A /\ less_dom(f,R_EAL p) & x in A /\ less_dom(g,R_EAL (r-p))
      by XBOOLE_0:def 3; then 
A19:x in A & x in less_dom(f,R_EAL p) & x in less_dom(g,R_EAL (r-p))
      by XBOOLE_0:def 3; 
    reconsider x as Element of X by B1;
    consider y1 being R_eal such that
A21: y1 = f.x & y1 < R_EAL p by A19,MESFUNC1:def 12;
    consider y2 being R_eal such that
A22: y2 = g.x & y2 < R_EAL(r-p) by A19,MESFUNC1:def 12;
T0: -infty < y1 & -infty < y2 by A1,A2,A21,A22,Def0003;
    y1 <> +infty & y2 <> +infty by XXREAL_0:4,A21,A22; then
    reconsider f1 = y1,g1 = y2 as Real by T0,EXTREAL1:1;
    f1 < p & p < r- g1 by A21,A22,REAL_2:165; then
    f1 < r - g1 by XREAL_1:2; then 
A25:f1 + g1 < r by XREAL_1:22;
    x in dom f & x in dom g by A19,MESFUNC1:def 12; then
A26:x in dom (f+g) by A4,XBOOLE_0:def 3; then 
    (f+g).x = f.x + g.x by MESFUNC1:def 3
           .= f1+g1 by A21,A22,SUPINF_2:1; then 
    x in less_dom(f+g,R_EAL r) by A25,A26,MESFUNC1:def 12;
    hence thesis by A19,XBOOLE_0:def 3;
   end;
   hence thesis by A5,XBOOLE_0:def 10;
end;

definition
  let X be non empty set;
  let f be PartFunc of X,REAL;
  func R_EAL f -> PartFunc of X,ExtREAL equals f;
  coherence by PARTFUN1:31;
end;

theorem LL4:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
 f is nonnegative & g is nonnegative holds f+g is nonnegative
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f,g be PartFunc of X,ExtREAL;
   assume 
A1:f is nonnegative & g is nonnegative;
   for x be set st x in dom(f+g) holds 0 <= (f+g).x
   proof
    let x be set;
    assume
A4: x in dom(f+g);
A5: 0 <= f.x & 0 <= g.x by A1,SUPINF_2:70; then
    g.x <= f.x + g.x by SUPINF_2:20;
    hence 0 <= (f+g).x by A4,A5,MESFUNC1:def 3;
   end;
   hence f+g is nonnegative by SUPINF_2:71;
end;

theorem LL6:
for X be non empty set,
    f be PartFunc of X,ExtREAL,c be Real st f is nonnegative holds
  (0 <= c implies c(#)f is nonnegative) & 
  (c <= 0 implies c(#)f is nonpositive)
proof
   let X be non empty set;
   let f be PartFunc of X,ExtREAL;
   let c be Real;
   assume
A1:f is nonnegative;
   hereby assume
A3: 0 <= c;
    set g = c(#)f;
    for x be set st x in dom g holds 0 <= g.x
    proof
     let x be set;
     assume
A5:  x in dom g;
     0 <= f.x by A1,SUPINF_2:70; then
     0 <= (R_EAL c)*f.x by A3,EXTREAL2:45;
     hence 0 <= (c(#)f).x by A5,MESFUNC1:def 6;
    end;
    hence c(#)f is nonnegative by SUPINF_2:71;
   end;
   assume 
A6: c <= 0;
   set g = c(#)f;
   now let x be set;
    assume 
A8: x in dom g;
    0 <= f.x by A1,SUPINF_2:70; then
    (R_EAL c)*f.x <= 0 by A6,EXTREAL2:46;
    hence g.x <= 0 by A8,MESFUNC1:def 6;
   end;
   hence c(#)f is nonpositive by ThNP02;
end;

theorem LL7:
for X be non empty set, f,g be PartFunc of X,ExtREAL st
 (for x be set st x in dom f /\ dom g holds 
   g.x <= f.x & -infty < g.x & f.x < +infty) holds
 f-g is nonnegative
proof
   let X be non empty set,
       f,g be PartFunc of X,ExtREAL;
   assume    
A1:for x be set st x in dom f /\ dom g holds
     g.x <= f.x & -infty < g.x & f.x < +infty;
   now let x be set;
    assume A:x in dom(f-g);
    dom(f-g)
     = (dom f /\ dom g)\(f"{+infty}/\g"{+infty} \/ f"{-infty}/\g"{-infty})
       by MESFUNC1:def 4; then
    dom(f-g) c= dom f /\ dom g by XBOOLE_1:36; then
A2: g.x <= f.x & -infty < g.x & f.x < +infty by A,A1; then
    -infty < f.x & g.x < +infty by SUPINF_1:29; then
    0 <= f.x - g.x by A2,EXTREAL1:40;
    hence 0 <= (f-g).x by A,MESFUNC1:def 4;
   end;
   hence f-g is nonnegative by SUPINF_2:71;
end;

MAX:
for X be non empty set, S be SigmaField of X, M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL holds 
 max+f is nonnegative & max-f is nonnegative & |.f.| is nonnegative
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f be PartFunc of X,ExtREAL;
   dom max+ f = dom f & dom max- f = dom f by MESFUNC2:def 2,def 3; then
   ( for x be set st x in dom max+ f holds 0<= (max+f).x ) &
   ( for x be set st x in dom max- f holds 0<= (max-f).x ) by MESFUNC2:14,15;
   hence max+f is nonnegative & max-f is nonnegative by SUPINF_2:71;
   now let x be set;
    assume x in dom |.f.|; then
    (|.f.|).x =|. f.x .| by MESFUNC1:def 10;
    hence 0 <= (|.f.|).x by EXTREAL2:51;
   end;
   hence |.f.| is nonnegative by SUPINF_2:71;
end;

theorem CorMES2'2'1:
for X be non empty set,
    f,g be PartFunc of X,ExtREAL st
 f is nonnegative & g is nonnegative holds
  dom(f+g)=dom f /\ dom g & f+g is nonnegative
proof
   let X be non empty set,
       f,g be PartFunc of X,ExtREAL;
   assume
A:  f is nonnegative & g is nonnegative; then
   (for x being set st x in dom f holds -infty < f.x) &
   for x being set st x in dom g holds -infty < g.x by SUPINF_2:70; then
   f is without-infty & g is without-infty by WMI01;
   hence
P3:dom(f+g)=dom f /\ dom g by MESFUNC2'2;
   now let x be set;
    assume A1: x in dom f /\ dom g;
    0 <= f.x & 0 <= g.x by A,SUPINF_2:70; then
    0 <= f.x +g.x by EXTREAL2:7;
    hence 0 <= (f+g).x by A1,P3,MESFUNC1:def 3;
   end;
   hence f+g is nonnegative by P3,SUPINF_2:71;
end;

theorem CorMES2'2'2:
for X be non empty set, f,g,h be PartFunc of X,ExtREAL st
 f is nonnegative & g is nonnegative & h is nonnegative holds
   dom(f+g+h) = dom f /\ dom g /\ dom h & f+g+h is nonnegative &
   for x be set st x in dom f /\ dom g /\ dom h holds (f+g+h).x=f.x+g.x+h.x
proof
   let X be non empty set;
   let f,g,h be PartFunc of X,ExtREAL;
   assume
A0: f is nonnegative & g is nonnegative & h is nonnegative; then
P3:f+g is nonnegative by CorMES2'2'1; then
P4:dom(f+g+h) = dom(f+g) /\ dom h by A0,CorMES2'2'1;
   hence dom(f+g+h) = dom f /\ dom g /\ dom h by A0,CorMES2'2'1;
   thus f+g+h is nonnegative by P3,A0,CorMES2'2'1;
   hereby let x be set;
    assume x in dom f /\ dom g /\ dom h; then
A2: x in dom(f+g) /\ dom h by A0,CorMES2'2'1; then
A3: x in dom(f+g) by XBOOLE_0:def 3;
    thus (f+g+h).x = (f+g).x + h.x by A2,P4,MESFUNC1:def 3
                  .= f.x + g.x + h.x by A3,MESFUNC1:def 3;
   end;
end;

theorem LM019a:
for X be non empty set,
    f,g being PartFunc of X,ExtREAL st
 f is without-infty & g is without-infty holds
   dom(max+(f+g) + max- f) = dom f /\ dom g & 
   dom(max-(f+g) + max+ f) = dom f /\ dom g &
   dom(max+(f+g) + max- f + max- g) = dom f /\ dom g &
   dom(max-(f+g) + max+ f + max+ g) = dom f /\ dom g &
   max+(f+g) + max-f is nonnegative & max-(f+g) + max+f is nonnegative 
proof
   let X be non empty set;
   let f,g be PartFunc of X,ExtREAL;
   assume that
A1: f is without-infty and
A2: g is without-infty;
A4:dom(f+g) = dom f /\ dom g by A1,A2,MESFUNC2'2; then
A5:dom(max+(f+g)) = dom f /\ dom g &
   dom(max-(f+g)) = dom f /\ dom g by MESFUNC2:def 2,def 3; then
   for x be set holds (x in dom max+(f+g) implies -infty < (max+(f+g)).x) &
   (x in dom max-(f+g) implies -infty < (max-(f+g)).x) 
     by A4,MESFUNC2:14,15; then
B6:max+(f+g) is without-infty & max-(f+g) is without-infty by WMI01;
A3:dom(max+ f) = dom f & dom(max- f) = dom f &
   dom(max+ g) = dom g & dom(max- g) = dom g by MESFUNC2:def 2,def 3; then
   for x be set holds (x in dom(max- f) implies -infty < (max- f).x) &
   (x in dom(max+ f) implies -infty < (max+ f).x) &
   (x in dom(max+ g) implies -infty < (max+ g).x) &
   (x in dom(max- g) implies -infty < (max- g).x)
     by MESFUNC2:14,15; then
A6:max+f is without-infty & max-f is without-infty &
   max+g is without-infty & max-g is without-infty by WMI01; then
A7:dom(max+(f+g) + max- f) = dom(max+(f+g)) /\ dom(max- f) & 
   dom(max-(f+g) + max+ f) = dom(max-(f+g)) /\ dom(max+ f)
      by B6,MESFUNC2'2; then
B2:dom(max+(f+g) + max- f) = dom g /\ (dom f /\ dom f) & 
   dom(max-(f+g) + max+ f) = dom g /\ (dom f /\ dom f) by A3,A5,XBOOLE_1:16;
   hence dom(max+(f+g) + max- f) = dom f /\ dom g &
         dom(max-(f+g) + max+ f) = dom f /\ dom g;
C0:for x be set holds
    ( x in dom(max+(f+g) + max-f) implies 0 <= (max+(f+g) + max-f).x ) &
    ( x in dom(max-(f+g) + max+f) implies 0 <= (max-(f+g) + max+f).x ) 
   proof
    let x be set;
    hereby assume
C1:  x in dom(max+(f+g) + max- f); then
     x in dom f by A7,A3,XBOOLE_0:def 3;then
     0 <= (max+(f+g)).x & 0 <= (max- f).x by A4,B2,C1,MESFUNC2:14,15; then
     0 <= (max+(f+g)).x + (max- f).x by EXTREAL2:7;
     hence 0 <= (max+(f+g) + max- f).x by C1,MESFUNC1:def 3;
    end;
    assume 
C2:  x in dom(max-(f+g) + max+ f); then
    x in dom f by A3,A7,XBOOLE_0:def 3; then
    0 <= (max-(f+g)).x & 0 <= (max+ f).x by A4,B2,C2,MESFUNC2:14,15; then
    0 <= (max-(f+g)).x + (max+ f).x by EXTREAL2:7;
    hence 0 <= (max-(f+g) + max+ f).x by C2,MESFUNC1:def 3;
   end; then
   for x be set holds 
    (x in dom(max+(f+g) + max-f) implies -infty < (max+(f+g) + max-f).x ) &
    (x in dom(max-(f+g) + max+f) implies -infty < (max-(f+g) + max+f).x ); then
D1:max+(f+g) + max-f is without-infty &
   max-(f+g) + max+f is without-infty by WMI01; then
   dom(max+(f+g) + max-f + max-g)
    = dom f /\ dom g /\ dom g by A3,A6,B2,MESFUNC2'2
   .= dom f /\ (dom g /\ dom g) by XBOOLE_1:16;
   hence dom(max+(f+g) + max- f + max- g) = dom f /\ dom g;
   dom(max-(f+g) + max+ f + max+ g)
    = dom f /\ dom g /\ dom g by A3,B2,A6,D1,MESFUNC2'2; then
   dom(max-(f+g) + max+f + max+g) = dom f /\ (dom g /\ dom g) by XBOOLE_1:16;
   hence dom(max-(f+g) + max+f + max+g) = dom f /\ dom g;
   thus thesis by C0,SUPINF_2:71;
end;

theorem LM019d:
for X being non empty set, f,g being PartFunc of X,ExtREAL st
 f is without-infty & f is without+infty &
 g is without-infty & g is without+infty holds
 max+(f+g) + max- f + max- g = max-(f+g) + max+ f + max+ g
proof
   let X be non empty set,
       f,g be PartFunc of X,ExtREAL;
   assume
A:  f is without-infty & f is without+infty &
    g is without-infty & g is without+infty; then
   dom(max+(f+g) + max- f + max- g) = dom f /\ dom g by LM019a; then
A3:dom(max+(f+g) + max- f + max- g) = dom(max-(f+g) + max+ f + max+ g) 
     by A,LM019a;
L0:dom(max+(f+g))= dom (f+g) & dom(max-(f+g))= dom (f+g) &
   dom max+ f = dom f & dom max- f = dom f &
   dom max+ g = dom g & dom max- g = dom g by MESFUNC2:def 2,def 3; then
   ( for x be set st x in dom max+(f+g) holds 0 <= (max+(f+g)).x ) &
   ( for x be set st x in dom max-(f+g) holds 0 <= (max-(f+g)).x ) &
   ( for x be set st x in dom max+ f holds 0<= (max+f).x ) &
   ( for x be set st x in dom max- f holds 0<= (max-f).x ) &
   ( for x be set st x in dom max+ g holds 0<= (max+g).x ) &
   for x be set st x in dom max- g holds 0<= (max-g).x by MESFUNC2:14,15; then
S1:max+(f+g) is nonnegative & max-(f+g) is nonnegative &
   max+f is nonnegative & max-f is nonnegative & max+g is nonnegative &
   max-g is nonnegative by SUPINF_2:71; then
T2:dom(max+(f+g) + max- f + max- g)
    = dom(f+g) /\ dom f /\ dom g by L0,CorMES2'2'2; then
T4:dom(max+(f+g) + max- f + max- g)
    = dom(f+g) /\ (dom f /\ dom g) by XBOOLE_1:16;
   for x be set st x in dom(max+(f+g) + max- f + max- g) holds
    (max+(f+g) + max- f + max- g).x = (max-(f+g) + max+ f + max+ g).x
   proof
    let x be set;
    assume
P1: x in dom(max+(f+g) + max-f + max-g); then
UU2:(max-(f+g)+ max+f + max+g).x
      =(max-(f+g)).x +(max+f).x +(max+g).x by L0,S1,CorMES2'2'2,T2;
W0: dom(f+g) = dom f /\ dom g by A,MESFUNC2'2; then
G1: (max+(f+g)).x = max((f+g).x,0) by MESFUNC2:def 2,L0,T4,P1
                 .=max((f.x+g.x),0) by W0,T4,P1,MESFUNC1:def 3;
G2: (max-(f+g)).x = max(-(f+g).x,0) by MESFUNC2:def 3,L0,W0,T4,P1
                 .=max(-(f.x+g.x),0) by W0,T4,P1,MESFUNC1:def 3;
G0: x in dom f & x in dom g by P1,T4,W0,XBOOLE_0:def 3; then
G3: (max+f).x = max(f.x,0) & (max-f).x = max(-f.x,0) &
    (max+g).x = max(g.x,0) & (max-g).x = max(-g.x,0)
      by L0,MESFUNC2:def 2,def 3;
H1: f.x <> +infty & f.x <> -infty &
    g.x <> +infty & g.x <> -infty by G0,A,WMI01,WPI01; then
H3: -f.x <> +infty & -f.x <> -infty &
    -g.x <> +infty & -g.x <> -infty by EXTREAL1:4;
T1: dom(max+(f+g) + max-f + max-g)
     = dom max+(f+g) /\ dom max-f /\ dom max-g by S1,CorMES2'2'2;
    now per cases;
     suppose 
   C1:0 <= f.x; then
      -f.x <= 0 by EXTREAL1:25; then
   K2:max-f.x =0 by G3,XXREAL_0:def 9;
      per cases;
      suppose 
   C11:0 <= g.x; then
    K0:0 <= f.x + g.x by C1,EXTREAL2:7; then
       - g.x <= 0 & -(f.x + g.x) <= 0 by C11,EXTREAL1:25; then
    K3:(max-g).x=0 & (max-(f+g)).x=0 by G2,G3,XXREAL_0:def 9; then
    K7:(max+(f+g)).x + (max-f).x + (max-g).x
         =f.x + g.x + R_EAL 0 + R_EAL 0 by K0,K2,G1,XXREAL_0:def 9
        .= f.x + g.x + R_EAL 0 by SUPINF_2:18
        .= f.x + g.x by SUPINF_2:18;
       (max+g).x = g.x by G3,C11,XXREAL_0:def 9; then
       (max-(f+g)).x + (max+f).x + (max+g).x
         = R_EAL 0 +f.x + g.x by K3,G3,C1,XXREAL_0:def 9;
       hence (max+(f+g)).x + (max-f).x + (max-g).x
         =(max-(f+g)).x + (max+f).x + (max+g).x by K7,SUPINF_2:18;
      end;
      suppose 
   C12:g.x < 0; then
       0 <= -g.x by EXTREAL1:25; then
    K3:(max-g).x= -g.x & (max+g).x = 0 by G3,C12,XXREAL_0:def 9;
       per cases;
       suppose 
   C121:0 <= f.x + g.x; then
        -(f.x + g.x) <= 0 by EXTREAL1:25; then
        (max-(f+g)).x=0 by G2, XXREAL_0:def 9; then
     K7:(max-(f+g)).x + (max+f).x + (max+g).x
          = R_EAL 0 + f.x + R_EAL 0 by G3,C1,XXREAL_0:def 9,K3;
        (max+(f+g)).x + (max-f).x + (max-g).x
          =f.x+g.x + R_EAL 0 + -g.x by C121,K2,K3,G1,XXREAL_0:def 9
         .=f.x + g.x - g.x by SUPINF_2:18
         .=f.x +(g.x - g.x) by EXTREAL1:11,H1
         .=f.x + R_EAL 0 by EXTREAL1:9;
        hence (max+(f+g)).x+ (max-f).x + (max-g).x
          =(max-(f+g)).x + (max+f).x + (max+g).x by K7,SUPINF_2:18;
       end;
       suppose 
   C122:f.x + g.x < 0; then
        0 <= -(f.x + g.x) by EXTREAL1:25; then
     K4:(max-(f+g)).x = -(f.x + g.x) by G2,XXREAL_0:def 9;
        (max+(f+g)).x + (max-f).x + (max-g).x
          =R_EAL 0 + R_EAL 0 + -g.x by C122,K2,K3,G1,XXREAL_0:def 9; then
     K7:(max+(f+g)).x + (max-f).x + (max-g).x
          =R_EAL 0 + -g.x by SUPINF_2:18;
        (max-(f+g)).x + (max+f).x + (max+g).x
          = -(f.x + g.x) +f.x + R_EAL 0 by K4,G3,C1,XXREAL_0:def 9,K3
         .= -(f.x + g.x) +f.x by SUPINF_2:18
         .=-g.x - f.x + f.x by EXTREAL2:14
         .=-g.x + (-f.x +f.x) by EXTREAL1:8,H3;
        hence (max+(f+g)).x + (max-f).x + (max-g).x
          =(max-(f+g)).x + (max+f).x + (max+g).x by K7,EXTREAL1:9;
       end;
      end;
     end;
     suppose 
   C2:f.x < 0; then
      0 <= -f.x by EXTREAL1:25; then
   K2:(max-f).x = -f.x by G3,XXREAL_0:def 9;
      per cases;
      suppose 
   C21:0 <= g.x; then
       -g.x <= 0 by EXTREAL1:25; then
    K3:(max-g).x = 0 by G3,XXREAL_0:def 9;
    K6:(max+g).x = g.x by G3,C21,XXREAL_0:def 9;
       per cases;
       suppose C121:0 <= f.x + g.x; then
        -(f.x + g.x) <= 0 by EXTREAL1:25; then
     K4:(max-(f+g)).x = 0 by G2, XXREAL_0:def 9;
     K7:(max+(f+g)).x + (max-f).x + (max-g).x
          =f.x + g.x+ -f.x + R_EAL 0 by C121,K2,K3,G1,XXREAL_0:def 9
         .=f.x + g.x+ -f.x by SUPINF_2:18
         .=g.x + (f.x+ -f.x) by EXTREAL1:8,H1
         .=g.x + R_EAL 0 by EXTREAL1:9;
        (max-(f+g)).x + (max+f).x + (max+g).x
          = R_EAL 0 + R_EAL 0 + g.x by K4,K6,G3,C2,XXREAL_0:def 9;
        hence (max+(f+g)).x + (max-f).x + (max-g).x
          =(max-(f+g)).x + (max+f).x + (max+g).x by K7,SUPINF_2:18;
       end;
       suppose C122:f.x + g.x < 0; then
        0 <= -(f.x + g.x) by EXTREAL1:25; then
     K4:(max-(f+g)).x = -(f.x + g.x) by G2,XXREAL_0:def 9;
     K7:(max+(f+g)).x + (max-f).x + (max-g).x
          =R_EAL 0 + -f.x + R_EAL 0 by C122,K2,K3,G1,XXREAL_0:def 9
         .=R_EAL 0 + -f.x by SUPINF_2:18;
        (max-(f+g)).x + (max+f).x + (max+g).x
          = -(f.x + g.x) + R_EAL 0 + g.x by K4,G3,C2,XXREAL_0:def 9,K6
         .= -(f.x + g.x) +g.x by SUPINF_2:18
         .=-f.x - g.x +g.x by EXTREAL2:14
         .=-f.x + (-g.x +g.x) by EXTREAL1:8,H3;
        hence (max+(f+g)).x + (max-f).x + (max-g).x
          =(max-(f+g)).x + (max+f).x + (max+g).x by K7,EXTREAL1:9;
       end;
      end;
      suppose C22:g.x < 0; then
    K0:f.x + g.x <= 0 by C2,EXTREAL2:9;
       0 <= -g.x by C22,EXTREAL1:25; then
    K3:(max-g).x=-g.x by G3,XXREAL_0:def 9;
       0 <= -(f.x + g.x) by K0,EXTREAL1:25; then
    K4:(max-(f+g)).x=-(f.x + g.x) by G2, XXREAL_0:def 9;
    K6:(max+g).x = 0 by G3,C22,XXREAL_0:def 9;
    K7:(max+(f+g)).x+ (max-f).x + (max-g).x
         =R_EAL 0 + -f.x + -g.x by K0,K2,K3,G1,XXREAL_0:def 9
        .=-f.x -g.x by SUPINF_2:18;
       (max-(f+g)).x + (max+f).x + (max+g).x
         = -(f.x + g.x) + R_EAL 0 + R_EAL 0 by K4,G3,C2,XXREAL_0:def 9,K6
        .= -(f.x + g.x) + R_EAL 0 by SUPINF_2:18
        .= -(f.x + g.x) by SUPINF_2:18;
       hence (max+(f+g)).x+ (max-f).x + (max-g).x
         =(max-(f+g)).x + (max+f).x + (max+g).x by K7,EXTREAL2:14;
      end;
     end;
    end;
    hence thesis by P1,S1,CorMES2'2'2,T1,UU2;
   end;
   hence thesis by A3,FUNCT_1:9;
end;

theorem LM020:
for C being non empty set, f being PartFunc of C,ExtREAL, c be Real st
 0 <= c holds max+(c(#)f) = c(#)max+f & max-(c(#)f) = c(#)max-f
proof
   let C be non empty set;
   let f be PartFunc of C,ExtREAL;
   let c be Real;
   assume
A1: 0 <= c;
A2:dom max+(c(#)f) = dom(c(#)f) by MESFUNC2:def 2
                   .= dom f by MESFUNC1:def 6
                   .= dom max+f by MESFUNC2:def 2
                   .= dom(c(#)max+f) by MESFUNC1:def 6;
A3:dom(max-(c(#)f)) = dom(c(#)f) by MESFUNC2:def 3
                   .= dom f by MESFUNC1:def 6
                   .= dom max-f by MESFUNC2:def 3
                   .= dom(c(#)max-f) by MESFUNC1:def 6;
   for x be Element of C st x in dom max+(c(#)f) holds
    (max+(c(#)f)).x = (c(#)max+f).x
   proof
    let x be Element of C;
    assume
 B1: x in dom max+(c(#)f); then
 B2:x in dom(c(#)f) by MESFUNC2:def 2; then
    x in dom f by MESFUNC1:def 6; then
 B3:x in dom max+ f by MESFUNC2:def 2;
 B4:(max+(c(#)f)).x = max((c(#)f).x,0) by B1,MESFUNC2:def 2
                   .= max(R_EAL c * f.x,0) by B2,MESFUNC1:def 6;
    (c(#)max+f).x = R_EAL c * max+f.x by B1,A2,MESFUNC1:def 6
                   .= R_EAL c * max(f.x,R_EAL 0) by B3,MESFUNC2:def 2
                   .= max(R_EAL c * f.x,R_EAL c * R_EAL 0) by A1,CorE;
    hence thesis by B4,EXTREAL1:22;
   end;
   hence max+(c(#)f) = c(#)max+f by A2,PARTFUN1:34;
   for x be Element of C st x in dom max-(c(#)f) holds
    (max-(c(#)f)).x = (c(#)max-f).x
   proof
    let x be Element of C;
    assume
 B1: x in dom max-(c(#)f); then
 B2:x in dom(c(#)f) by MESFUNC2:def 3; then
    x in dom f by MESFUNC1:def 6; then
 B3:x in dom max- f by MESFUNC2:def 3;
 B4:(max-(c(#)f)).x = max(-(c(#)f).x,0) by B1,MESFUNC2:def 3
                   .= max(-(R_EAL c)*f.x,0) by B2,MESFUNC1:def 6;
    (c(#)max-f).x = R_EAL c * max-f.x by B1,A3,MESFUNC1:def 6
                 .= R_EAL c * max(-f.x,R_EAL 0) by B3,MESFUNC2:def 3
                 .= max(R_EAL c * (-f.x),R_EAL c * R_EAL 0) by A1,CorE
                 .= max(-(R_EAL c)*f.x,R_EAL c * R_EAL 0) by EXTREAL1:26;
    hence thesis by B4,EXTREAL1:22;
   end;
   hence max-(c(#)f) = c(#)max-f by A3,PARTFUN1:34;
end;

theorem LM021:
for C being non empty set, f being PartFunc of C,ExtREAL, c be Real st
 0 <= c holds max+((-c)(#)f) = c(#)max-f & max-((-c)(#)f) = c(#)max+f
proof
   let C be non empty set;
   let f be PartFunc of C,ExtREAL;
   let c be Real;
   assume
A1: 0 <= c;
P2:dom max+((-c)(#)f) = dom((-c)(#)f) & dom max-((-c)(#)f) = dom((-c)(#)f)
     by MESFUNC2:def 2,def 3; then
   dom max+((-c)(#)f) = dom f & dom max-((-c)(#)f) = dom f
     by MESFUNC1:def 6; then
P4:dom max+((-c)(#)f) = dom max-f & dom max-((-c)(#)f) = dom max+f
     by MESFUNC2:def 2,def 3; then
P5:dom max+((-c)(#)f) = dom(c(#)max-f) & dom max-((-c)(#)f) = dom(c(#)max+f)
     by MESFUNC1:def 6;
C2:- R_EAL c = -c by SUPINF_2:3;
   for x be Element of C st x in dom max+((-c)(#)f) holds
    max+((-c)(#)f).x = (c(#)max-f).x
   proof
    let x be Element of C;
    assume
 B1: x in dom max+((-c)(#)f); then
 B4:(max+((-c)(#)f)).x = max(((-c)(#)f).x,0) by MESFUNC2:def 2
                      .= max(R_EAL(-c)*f.x,0) by B1,P2,MESFUNC1:def 6
                      .= max(-(R_EAL c * f.x),0) by C2,EXTREAL1:26;
    (c(#)max-f).x = R_EAL c * max-f.x by B1,P5,MESFUNC1:def 6
                   .= R_EAL c * max(-f.x,R_EAL 0) by B1,P4,MESFUNC2:def 3
                   .= max(R_EAL c * (-f.x),R_EAL c * R_EAL 0) by A1,CorE
                   .= max(-(R_EAL c * f.x),R_EAL c * R_EAL 0) by EXTREAL1:26;
    hence thesis by B4,EXTREAL1:22;
   end;
   hence max+((-c)(#)f) = c(#)max-f by P5,PARTFUN1:34;
   for x be Element of C st x in dom max-((-c)(#)f) holds
     max-((-c)(#)f).x = (c(#)max+f).x
   proof
    let x be Element of C;
    assume
 B1: x in dom max-((-c)(#)f); then
 B4: max-((-c)(#)f).x = max(-((-c)(#)f).x,0) by MESFUNC2:def 3
                     .= max(-(R_EAL(-c))*f.x,0) by B1,P2,MESFUNC1:def 6
                     .= max((-(-(R_EAL c)))*f.x,0) by C2,EXTREAL1:26;
    (c(#)max+f).x = R_EAL c * max+f.x by B1,P5,MESFUNC1:def 6
                   .= R_EAL c * max(f.x,R_EAL 0) by B1,P4,MESFUNC2:def 2
                   .= max(R_EAL c * f.x,R_EAL c * R_EAL 0) by A1,CorE;
    hence thesis by B4,EXTREAL1:22;
   end;
   hence max-((-c)(#)f) = c(#)max+f by P5,PARTFUN1:34;
end;

theorem LM022g01:
for X be non empty set, f be PartFunc of X,ExtREAL, A be set holds
 max+(f|A)=max+f|A & max-(f|A)=max-f|A
proof
   let X be non empty set;
   let f be PartFunc of X,ExtREAL;
   let A be set;
A1:dom max+(f|A) = dom(f|A) by MESFUNC2:def 2
                .= dom f /\ A by FUNCT_1:68
                .= dom max+f /\ A by MESFUNC2:def 2
                .= dom(max+f|A) by FUNCT_1:68;
   for x being Element of X st x in dom max+(f|A) holds 
     (max+(f|A)).x = (max+f|A).x
   proof
    let x be Element of X;
    assume 
A2:  x in dom max+(f|A); then
    x in dom max+f /\ A by A1,FUNCT_1:68; then
A3: x in dom max+f & x in A by XBOOLE_0:def 3;
A4: (max+(f|A)).x = max((f|A).x,0) by A2,MESFUNC2:def 2
                 .= max(f.x,0) by A3,FUNCT_1:72;
    (max+f|A).x = (max+f).x by A1,A2,FUNCT_1:70;
    hence thesis by A4,A3,MESFUNC2:def 2;
   end;
   hence max+(f|A) = max+f|A by A1,PARTFUN1:34;
B1:dom max-(f|A) = dom(f|A) by MESFUNC2:def 3
                .= dom f /\ A by FUNCT_1:68
                .= dom max-f /\ A by MESFUNC2:def 3
                .= dom(max-f|A) by FUNCT_1:68;
   for x being Element of X st x in dom max-(f|A) holds
     (max-(f|A)).x = (max-f|A).x
   proof
    let x be Element of X;
    assume 
B2:  x in dom max-(f|A); then
    x in dom max-f /\ A by B1,FUNCT_1:68; then
B3: x in dom max-f & x in A by XBOOLE_0:def 3;
B4: (max-(f|A)).x = max(-(f|A).x,0) by B2,MESFUNC2:def 3
                 .= max(-f.x,0) by B3,FUNCT_1:72;
    (max-f|A).x = (max-f).x by B1,B2,FUNCT_1:70;
    hence thesis by B4,B3,MESFUNC2:def 3;
   end;
   hence max-(f|A)=max-f|A by B1,PARTFUN1:34;
end;

theorem LM022w:
for X be non empty set, f,g be PartFunc of X,ExtREAL, B be set st
 B c= dom(f+g) holds dom((f+g)|B) =B & dom(f|B+g|B)=B & (f+g)|B = f|B+g|B
proof
   let X be non empty set,
       f,g be PartFunc of X,ExtREAL,
       B be set such that
A5:B c= dom(f+g);
   dom(f+g) =
    (dom f /\ dom g)\(f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty})
      by MESFUNC1:def 3; then
   dom(f+g) c= dom f /\ dom g by XBOOLE_1:36; then
   B c= dom f /\ dom g by A5,XBOOLE_1:1; then
A3:B c= dom f & B c= dom g by XBOOLE_1:18;
   for x be set st x in dom(f|B) holds (f|B).x in ExtREAL; then
   reconsider fb = f|B as Function of dom(f|B),ExtREAL by FUNCT_2:5;
   for x be set st x in dom f holds f.x in ExtREAL; then
   reconsider ff = f as Function of dom f,ExtREAL by FUNCT_2:5;
   for x be set st x in dom(g|B) holds (g|B).x in ExtREAL; then
   reconsider gb = g|B as Function of dom (g|B), ExtREAL by FUNCT_2:5;
   for x be set st x in dom g holds g.x in ExtREAL; then
   reconsider gg = g as Function of dom g,ExtREAL by FUNCT_2:5;
   dom((f+g)|B) =dom(f+g) /\ B & dom(f|B) = dom f /\ B & dom(g|B) = dom g /\ B
     by FUNCT_1:68; then
X1:dom((f+g)|B) = B & dom(f|B) = B & dom(g|B) = B by A3,A5,XBOOLE_1:28;
Y1:(f|B)"{+infty}=f"{+infty} /\ B
   proof
    now let x be set;
     assume 
  A1: x in (f|B)"{+infty};
  A2:x in dom fb & fb.x in {+infty} by FUNCT_2:46,A1; then
     x in dom f /\ B & (f|B).x in {+infty} by FUNCT_1:68; then
     x in dom f & x in B & f.x in {+infty} by FUNCT_1:68,A2,XBOOLE_0:def 3;then
     x in ff"{+infty} & x in B by FUNCT_2:46;
     hence x in f"{+infty} /\ B by XBOOLE_0:def 3;
    end; then
 L1:(f|B)"{+infty} c= f"{+infty} /\ B by TARSKI:def 3;
    now let x be set;
     assume x in f"{+infty} /\ B; then
     x in f"{+infty} & x in B by XBOOLE_0:def 3; then
     x in dom ff & ff.x in {+infty} & x in B by FUNCT_2:46; then
     x in dom ff /\ B & ff.x in {+infty} by XBOOLE_0:def 3; then
     x in dom(ff|B) & ff.x in {+infty} by FUNCT_1:68; then
     x in dom fb & fb.x in {+infty} by FUNCT_1:68;
     hence x in (f|B)"{+infty} by FUNCT_2:46;
    end; then
    f"{+infty} /\ B c= (f|B)"{+infty} by TARSKI:def 3;
    hence thesis by L1,XBOOLE_0:def 10;
   end;
Y2:(f|B)"{-infty}=f"{-infty} /\B
   proof
    now let x be set;
     assume x in (f|B)"{-infty}; then
  A2:x in dom fb & fb.x in {-infty} by FUNCT_2:46; then
     x in dom f /\ B & (f|B).x in {-infty} by FUNCT_1:68; then
     x in dom f & x in B & f.x in {-infty} by FUNCT_1:68,A2,XBOOLE_0:def 3;then
     x in ff"{-infty} & x in B by FUNCT_2:46;
     hence x in f"{-infty} /\ B by XBOOLE_0:def 3;
    end; then
 L1:(f|B)"{-infty} c= f"{-infty} /\ B by TARSKI:def 3;
    now let x be set;
     assume x in f"{-infty} /\ B; then
     x in f"{-infty} & x in B by XBOOLE_0:def 3; then
     x in dom ff & ff.x in {-infty} & x in B by FUNCT_2:46; then
     x in dom ff /\ B & ff.x in {-infty} by XBOOLE_0:def 3; then
     x in dom(ff|B) & ff.x in {-infty} by FUNCT_1:68; then
     x in dom fb & fb.x in {-infty} by FUNCT_1:68;
     hence x in (f|B)"{-infty} by FUNCT_2:46;
    end; then
    f"{-infty} /\ B c= (f|B)"{-infty} by TARSKI:def 3;
    hence thesis by L1,XBOOLE_0:def 10;
   end;
Y3:(g|B)"{+infty}=g"{+infty} /\ B
   proof
    now let x be set;
     assume x in (g|B)"{+infty}; then
  A2: x in dom gb & gb.x in {+infty} by FUNCT_2:46; then
     x in dom g /\ B & (g|B).x in {+infty} by FUNCT_1:68; then
     x in dom g & x in B & g.x in {+infty} by FUNCT_1:68,A2,XBOOLE_0:def 3;then
     x in gg"{+infty} & x in B by FUNCT_2:46;
     hence x in g"{+infty} /\ B by XBOOLE_0:def 3;
    end; then
 L1:(g|B)"{+infty} c= g"{+infty} /\ B by TARSKI:def 3;
    now let x be set;
     assume x in g"{+infty} /\ B; then
     x in g"{+infty} & x in B by XBOOLE_0:def 3; then
     x in dom gg & gg.x in {+infty} & x in B by FUNCT_2:46; then
     x in dom gg /\ B & gg.x in {+infty} by XBOOLE_0:def 3; then
     x in dom(gg|B) & gg.x in {+infty} by FUNCT_1:68; then
     x in dom gb & gb.x in {+infty} by FUNCT_1:68;
     hence x in (g|B)"{+infty} by FUNCT_2:46;
    end; then
    g"{+infty} /\ B c= (g|B)"{+infty} by TARSKI:def 3;
    hence thesis by L1,XBOOLE_0:def 10;
   end;
Y4:(g|B)"{-infty}=g"{-infty} /\B
   proof
    now let x be set;
     assume x in (g|B)"{-infty}; then
  A2:x in dom gb & gb.x in {-infty} by FUNCT_2:46; then
     x in dom g /\ B & (g|B).x in {-infty} by FUNCT_1:68; then
     x in dom g & x in B & g.x in {-infty} by FUNCT_1:68,A2,XBOOLE_0:def 3;then
     x in gg"{-infty} & x in B by FUNCT_2:46;
     hence x in g"{-infty} /\ B by XBOOLE_0:def 3;
    end; then
 L1:(g|B)"{-infty} c= g"{-infty} /\ B by TARSKI:def 3;
    now let x be set;
     assume x in g"{-infty} /\ B; then
     x in g"{-infty} & x in B by XBOOLE_0:def 3; then
     x in dom gg & gg.x in {-infty} & x in B by FUNCT_2:46; then
     x in dom gg /\ B & gg.x in {-infty} by XBOOLE_0:def 3; then
     x in dom(gg|B) & gg.x in {-infty} by FUNCT_1:68; then
     x in dom gb & gb.x in {-infty} by FUNCT_1:68;
     hence x in (g|B)"{-infty} by FUNCT_2:46;
    end; then
    g"{-infty} /\ B c= (g|B)"{-infty} by TARSKI:def 3;
    hence thesis by L1,XBOOLE_0:def 10;
   end;
Y23:(f|B)"{-infty} /\ (g|B)"{+infty}
    = f"{-infty} /\B /\ g"{+infty} /\B  by XBOOLE_1:16,Y2,Y3
   .= f"{-infty} /\ g"{+infty} /\ B /\B  by XBOOLE_1:16
   .= f"{-infty} /\ g"{+infty} /\ (B /\ B) by XBOOLE_1:16;
   (f|B)"{+infty} /\ (g|B)"{-infty}
    = f"{+infty} /\B /\ g"{-infty} /\ B by XBOOLE_1:16,Y1,Y4
   .= f"{+infty} /\ g"{-infty} /\ B /\ B  by XBOOLE_1:16
   .= f"{+infty} /\ g"{-infty} /\ (B /\ B) by XBOOLE_1:16; then
Y5:(f|B)"{-infty} /\ (g|B)"{+infty} \/ (f|B)"{+infty} /\ (g|B)"{-infty}
    =(f"{-infty}/\g"{+infty} \/ (f"{+infty}/\g"{-infty})) /\ B
      by XBOOLE_1:23,Y23;
   dom(f|B) /\ dom(g|B) = dom f /\ B /\ dom(g|B) by FUNCT_1:68
    .= dom f /\ B /\ (dom g /\ B) by FUNCT_1:68
    .= dom f /\ B /\ dom g /\ B by XBOOLE_1:16
    .= dom f /\ dom g /\ B /\ B by XBOOLE_1:16
    .= dom f /\ dom g /\ (B /\ B) by XBOOLE_1:16; then
YX:dom(f|B+g|B) = (dom f /\ dom g /\ B)
      \((f|B)"{-infty} /\ (g|B)"{+infty} \/ ((f|B)"{+infty} /\ (g|B)"{-infty}))
         by MESFUNC1:def 3
   .=((dom f /\ dom g) \ ((f"{-infty} /\ g"{+infty})
      \/ (f"{+infty} /\ g"{-infty}))) /\ B by XBOOLE_1:50,Y5
   .=dom(f+g) /\ B by MESFUNC1:def 3
   .=B by A5,XBOOLE_1:28;
   now let x be set;
    assume PA1: x in dom ((f+g)|B);
    hence ((f+g)|B).x = (f+g).x by FUNCT_1:68
               .=f.x+g.x by PA1,A5,X1,MESFUNC1:def 3
               .=(f|B).x + g.x by PA1,X1,FUNCT_1:68
               .=(f|B).x + (g|B).x by PA1,X1,FUNCT_1:68
               .= ((f|B)+(g|B)).x by PA1,X1,YX,MESFUNC1:def 3;
   end;
   hence thesis by YX,X1,FUNCT_1:9;
end;

theorem CorG:
for X be non empty set, f be PartFunc of X,ExtREAL, a be R_eal holds
 eq_dom(f,a) = f"{a}
proof
   let X be non empty set;
   let f be PartFunc of X,ExtREAL;
   let a be R_eal;
   now let x be set;
    assume x in eq_dom(f,a); then
A1: x in dom f & ex y being R_eal st y = f.x & a=y by MESFUNC1:def 16; then
    f.x in {a} by TARSKI:def 1;
    hence x in f"{a} by A1,FUNCT_1:def 13;
   end; then
A3:eq_dom(f,a) c= f"{a} by TARSKI:def 3;
   now let x be set;
    assume x in f"{a}; then
A4: x in dom f & f.x in {a} by FUNCT_1:def 13; then
    f.x = a by TARSKI:def 1;
    hence x in eq_dom(f,a) by A4,MESFUNC1:def 16;
   end; then
   f"{a} c= eq_dom(f,a) by TARSKI:def 3;
   hence thesis by A3,XBOOLE_0:def 10;
end;

begin :: Lemmas for measurable function and simple valued function

theorem MESFUNC2'7:
for X be non empty set, 
    S be SigmaField of X, 
    f,g be PartFunc of X,ExtREAL, 
    A be Element of S st
 f is without-infty & g is without-infty &
 f is_measurable_on A & g is_measurable_on A holds 
   f+g is_measurable_on A
proof
   let X be non empty set, 
    S be SigmaField of X, 
    f,g be PartFunc of X,ExtREAL, 
    A be Element of S;
   assume that 
A1: f is without-infty & g is without-infty and
A3: f is_measurable_on A & g is_measurable_on A;
   for r be real number holds A /\ less_dom(f+g,R_EAL r) is_measurable_on S
   proof
    let r be real number;
    reconsider r as Real by XREAL_0:def 1;
    consider F being Function of RAT,S such that
A5:  for p being Rational holds
      F.p = A /\ less_dom(f,R_EAL p) /\ (A /\ less_dom(g,R_EAL(r-p)))
         by A3,MESFUNC2:6;
A7: A /\ less_dom(f+g,R_EAL r) = union rng F by A1,A5,MESFUNC2'3;
    ex G be Function of NAT,S st rng F = rng G by MESFUNC2:5,MESFUNC1:5; then
    rng F is N_Sub_set_fam of X by MEASURE1:52; then
    union rng F in S by MEASURE1:def 9;
    hence thesis by A7,MESFUNC1:def 11;
   end;
   hence thesis by MESFUNC1:def 17;
end;

MESFUNC2'7a:
for X be non empty set, 
    S be SigmaField of X, 
    f,g be PartFunc of X,ExtREAL, 
    A be Element of S st
  f is nonnegative & g is nonnegative &
  f is_measurable_on A & g is_measurable_on A holds 
    dom(f+g) = dom f /\ dom g & f+g is_measurable_on A  
proof
   let X be non empty set, 
    S be SigmaField of X, 
    f,g be PartFunc of X,ExtREAL, 
    A be Element of S;
   assume that 
A1: f is nonnegative & g is nonnegative and
A3: f is_measurable_on A & g is_measurable_on A;
   f is without-infty & g is without-infty by A1,WMI02;
   hence thesis by MESFUNC2'2,MESFUNC2'7,A3;
end;

theorem
for X be non empty set, S be SigmaField of X,
M be sigma_Measure of S, f be PartFunc of X,ExtREAL st
  f is_simple_func_in S & dom f = {} holds
ex F be Finite_Sep_Sequence of S, a,x be FinSequence of ExtREAL st
  F,a are_Re-presentation_of f & a.1 = 0 &
  (for n be Nat st 2 <= n & n in dom a holds 0 < a.n & a.n < +infty) &
  dom x = dom F &
  (for n be Nat st n in dom x holds x.n = a.n*(M*F).n) &
  Sum x = 0
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f be PartFunc of X,ExtREAL;
   assume
A1: f is_simple_func_in S & dom f = {};
   for x be set st x in dom f holds 0 <= f.x by A1; then
   consider F be Finite_Sep_Sequence of S,
            a be FinSequence of ExtREAL such that
A2: F,a are_Re-presentation_of f &
    a.1 = 0 &
    for n be Nat st 2 <= n & n in dom a holds 0 < a.n & a.n < +infty
     by A1,MESFUNC3:14;
   deffunc F(Nat) = a.$1*(M*F).$1;
   consider x be FinSequence of ExtREAL such that
A3: len x = len F and
A4: for n be Element of NAT st n in Seg len F holds x.n = F(n)
from FINSEQ_2:sch 1;
take F,a,x;
A5:dom x = Seg len F by A3,FINSEQ_1:def 3; then
A6:dom x = dom F by FINSEQ_1:def 3;
   consider sumx be Function of NAT,ExtREAL such that
A8: Sum x = sumx.(len x) & sumx.0 = 0 &
    for i be  Element of NAT st i < len x holds sumx.(i+1)=sumx.i+x.(i+1)
     by CONVFUN1:def 5;
   defpred P[Nat] means $1 <= len x implies sumx.$1 = 0;
A11:union rng F = {} by A1,A2,MESFUNC3:def 1;
A12:for n be Nat st n in dom F holds F.n = {}
   proof
    let n be Nat;
    assume n in dom F; then
A9: F.n in rng F by FUNCT_1:12;
    assume F.n <> {}; then
    ex v be set st v in F.n by XBOOLE_0:def 1;
    hence contradiction by A11,A9,TARSKI:def 4;
   end;
B1:P[0] by A8;
B2:for i be Nat st P[i] holds P[i+1]
   proof
    let i be Nat;
    assume
B3:  P[i];
    assume
    B4:  i+1 <= len x;
    reconsider i as Element of NAT by ORDINAL1:def 13;
    1 <= i+1 by NAT_1:29; then
B5: i+1 in dom x by B4,FINSEQ_3:27;
    i < len x by B4,NAT_1:38; then
B7: sumx.(i+1) = sumx.i + x.(i+1) by A8;
    F.(i+1) = {} by B5,A6,A12; then
    M.(F.(i+1)) = 0 by MEASURE1:def 11; then
B9: (M*F).(i+1) = 0 by B5,A6,FUNCT_1:23;
    x.(i+1) = a.(i+1)*(M*F).(i+1) by B5,A4,A5
           .= 0 by B9,EXTREAL1:22;
    hence thesis by B7,B3,B4,NAT_1:38,SUPINF_2:18;
   end;
   for i be Nat holds P[i] from NAT_1:sch 2(B1,B2); then
   Sum x = 0 by A8;
   hence thesis by A2,A6,A4,A5;
end;

theorem Cor4:
for X be non empty set, S be SigmaField of X,
    f be PartFunc of X,ExtREAL, A be Element of S, r,s be Real st
f is_measurable_on A & A c= dom f holds
 A /\ great_eq_dom(f,R_EAL r) /\ less_dom(f,R_EAL s) is_measurable_on S
proof
   let X be non empty set;
   let S be SigmaField of X;
   let f be PartFunc of X,ExtREAL;
   let A be Element of S;
   let r,s be Real;
   assume
A1: f is_measurable_on A & A c= dom f;
   A /\ less_dom(f,R_EAL s) is_measurable_on S by A1,MESFUNC1:def 17; then
A3:A /\ less_dom(f,R_EAL s) in S by MESFUNC1:def 11;
   A /\ great_eq_dom(f,R_EAL r) is_measurable_on S by A1,MESFUNC1:31; then
   A /\ great_eq_dom(f,R_EAL r) in S by MESFUNC1:def 11; then
A9:A /\ great_eq_dom(f,R_EAL r) /\ (A /\ less_dom(f,R_EAL s)) in S
     by A3,FINSUB_1:def 2;
   A /\ great_eq_dom(f,R_EAL r) /\ (A /\ less_dom(f,R_EAL s))
    = A /\ great_eq_dom(f,R_EAL r) /\ A /\ less_dom(f,R_EAL s) by XBOOLE_1:16
   .= great_eq_dom(f,R_EAL r) /\ (A/\A) /\ less_dom(f,R_EAL s) by XBOOLE_1:16;
   hence thesis by A9,MESFUNC1:def 11;
end;

theorem Cor5:
for X be non empty set, S be SigmaField of X,
    M be sigma_Measure of S, f be PartFunc of X,ExtREAL, A be Element of S st 
 f is_simple_func_in S holds f|A is_simple_func_in S
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f be PartFunc of X,ExtREAL;
   let A be Element of S;
   assume
A0:f is_simple_func_in S; then
   consider F be Finite_Sep_Sequence of S such that
A1: dom f = union rng F and
A2: for n be Nat, x,y being Element of X st
     n in dom F & x in F.n & y in F.n holds f.x = f.y by MESFUNC2:def 5;
B2:f is_finite by A0,MESFUNC2:def 5;
   set g = f|A;
   now let x be Element of X;
    assume
C01: x in dom g; then
    x in dom f /\ A by FUNCT_1:68; then
    x in dom f by XBOOLE_0:def 3; then
    |. f.x .| < +infty by B2,MESFUNC2:def 1;
    hence |. g.x .| < +infty by C01,FUNCT_1:70;
   end; then
C0:g is_finite by MESFUNC2:def 1;
   deffunc FA(Nat) = F.$1 /\ A;
   consider G be FinSequence such that
C1: len G = len F &
    for n be  Element of NAT st n in Seg len F holds G.n = FA(n)
    from FINSEQ_1:sch 2;
   rng G c= S
   proof
    let P be set;
    assume P in rng G; then
    consider k be  Element of NAT such that
C11: k in dom G & P = G.k by FINSEQ_2:11;
C12:k in Seg len F by C1,C11,FINSEQ_1:def 3; then
C13:G.k = F.k /\ A by C1;
    k in dom F by C12,FINSEQ_1:def 3; then
    F.k in rng F & rng F c= S by FUNCT_1:12;
    hence thesis by C11,C13,FINSUB_1:def 2;
   end; then
   reconsider G as FinSequence of S by FINSEQ_1:def 4;
   for i,j be Nat st i in dom G & j in dom G & i <> j holds G.i misses G.j
   proof
    let i,j be Nat;
    assume
C21: i in dom G & j in dom G & i <> j; then
C22:i in Seg len F & j in Seg len F by C1,FINSEQ_1:def 3; then
    i in dom F & j in dom F by FINSEQ_1:def 3; then
C23:F.i misses F.j by C21,MESFUNC3:4;
    G.i = F.i /\ A & G.j = F.j /\ A by C22,C1; then
    G.i /\ G.j = F.i /\ A /\ F.j /\ A by XBOOLE_1:16
              .= F.i /\ F.j /\ A /\ A by XBOOLE_1:16
              .= {} /\ A /\ A by C23,XBOOLE_0:def 7;
    hence thesis by XBOOLE_0:def 7;
   end; then
   reconsider G as Finite_Sep_Sequence of S by MESFUNC3:4;
   for v be set st v in dom(f|A) holds v in union rng G
   proof
    let v be set;
    assume v in dom(f|A); then
    v in dom f /\ A by FUNCT_1:68; then
C31:v in dom f & v in A by XBOOLE_0:def 3; then
    consider W be set such that
C32: v in W & W in rng F by A1,TARSKI:def 4;
    consider k be  Element of NAT such that
C33: k in dom F & W = F.k by C32,FINSEQ_2:11;
C34:k in Seg len F by C33,FINSEQ_1:def 3; then
    G.k = F.k /\ A by C1; then
C35:v in G.k by C31,C32,C33,XBOOLE_0:def 3;
    k in dom G by C34,C1,FINSEQ_1:def 3; then
    G.k in rng G by FUNCT_1:12;
    hence thesis by C35,TARSKI:def 4;
   end; then
C36:dom(f|A) c= union rng G by TARSKI:def 3;
   for v be set st v in union rng G holds v in dom(f|A)
   proof
    let v be set;
    assume v in union rng G; then
    consider W be set such that
C37: v in W & W in rng G by TARSKI:def 4;
    consider k be  Element of NAT such that
C38: k in dom G & W = G.k by C37,FINSEQ_2:11;
C39:k in Seg(len F) by C1,C38,FINSEQ_1:def 3; then
    G.k = F.k /\ A by C1; then
C40:v in F.k & v in A by C37,C38,XBOOLE_0:def 3;
    k in dom F by C39,FINSEQ_1:def 3; then
    F.k in rng F by FUNCT_1:12; then
    v in union rng F by C40,TARSKI:def 4; then
    v in dom f /\ A by A1,C40,XBOOLE_0:def 3;
    hence thesis by FUNCT_1:68;
   end; then
   union rng G c= dom(f|A) by TARSKI:def 3; then
C3:dom(f|A) = union rng G by C36,XBOOLE_0:def 10;
   for n be Nat, x,y be Element of X st
    n in dom G & x in G.n & y in G.n holds (f|A).x = (f|A).y
   proof
    let n be Nat;
    let x,y be Element of X;
    assume
C41: n in dom G & x in G.n & y in G.n; then
    G.n in rng G by FUNCT_1:12; then
C46:x in dom(f|A) & y in dom(f|A) by C3,C41,TARSKI:def 4;
C42:n in Seg(len F) by C41,C1,FINSEQ_1:def 3; then
    G.n = F.n /\ A by C1; then
C43:x in F.n & y in F.n by C41,XBOOLE_0:def 3;
    n in dom F by C42,FINSEQ_1:def 3; then
    f.x = f.y by C43,A2; then
    (f|A).x = f.y by C46,FUNCT_1:70;
    hence thesis by C46,FUNCT_1:70;
   end;
   hence f|A is_simple_func_in S by C0,C3,MESFUNC2:def 5;
end;

theorem Cor6:
for X be non empty set, S be SigmaField of X, A be Element of S,
    F be Finite_Sep_Sequence of S, G be FinSequence st 
 dom F = dom G & (for n be Nat st n in dom F holds G.n = F.n /\ A) holds
  G is Finite_Sep_Sequence of S
proof
   let X be non empty set;
   let S be SigmaField of X;
   let A be Element of S;
   let F be Finite_Sep_Sequence of S;
   let G be FinSequence;
   assume that
A2:dom F = dom G and
A3:for n be Nat st n in dom F holds G.n = F.n /\ A;
   rng G c= S
   proof
    let v be set;
    assume v in rng G; then
    consider k be set such that
A5:  k in dom G & v = G.k by FUNCT_1:def 5;
    k is Nat by A5,ORDINAL1:def 13;
    then
A6: G.k = F.k /\ A by A2,A5,A3;
    F.k in rng F by A2,A5,FUNCT_1:12;
    hence thesis by A5,A6,FINSUB_1:def 2;
   end; then
   reconsider G as FinSequence of S by FINSEQ_1:def 4;
   now let i,j be Nat;
    assume
A8:  i in dom G & j in dom G & i <> j; then
A9: G.i = F.i /\ A & G.j = F.j /\ A by A3,A2;
    F.i misses F.j by A8,PROB_2:def 3;
    hence G.i misses G.j by A9,XBOOLE_1:76;
   end;
   hence thesis by MESFUNC3:4;
end;

theorem Cor7:
for X be non empty set, S be SigmaField of X, f be PartFunc of X,ExtREAL,
    A be Element of S, F,G be Finite_Sep_Sequence of S,
    a be FinSequence of ExtREAL st 
 dom F = dom G &
 (for n be Nat st n in dom F holds G.n = F.n /\ A) &
 F,a are_Re-presentation_of f 
holds
   G,a are_Re-presentation_of f|A
proof
   let X be non empty set;
   let S be SigmaField of X;
   let f be PartFunc of X,ExtREAL;
   let A be Element of S;
   let F,G be Finite_Sep_Sequence of S;
   let a be FinSequence of ExtREAL;
   assume that
A1: dom F = dom G and
A2: for n be Nat st n in dom F holds G.n = F.n /\ A and
A3: F,a are_Re-presentation_of f;
   now let v be set;
    assume v in dom(f|A); then
    v in dom f /\ A by FUNCT_1:68; then
A4: v in dom f & v in A by XBOOLE_0:def 3; then
    v in union rng F by A3,MESFUNC3:def 1; then
    consider C be set such that
A5:  v in C & C in rng F by TARSKI:def 4;
    consider k be  Element of NAT such that
A6:  k in dom F & C = F.k by A5,FINSEQ_2:11;
A7: G.k = F.k /\ A by A2,A6;
A8: G.k in rng G by A1,A6,FUNCT_1:12;
    v in F.k /\ A by A4,A5,A6,XBOOLE_0:def 3;
    hence v in union rng G by A8,A7,TARSKI:def 4;
   end; then
A9:dom(f|A) c= union rng G by TARSKI:def 3;
   now let v be set;
    assume v in union rng G; then
    consider C be set such that
F34: v in C & C in rng G by TARSKI:def 4;
    consider k be set such that
F35: k in dom G & C = G.k by F34,FUNCT_1:def 5;
    k is Nat by F35,ORDINAL1:def 13;
    then G.k = F.k /\ A by A1,A2,F35; then
F36:v in F.k & v in A by F34,F35,XBOOLE_0:def 3;
    F.k in rng F by A1,F35,FUNCT_1:12; then
    v in union rng F by F36,TARSKI:def 4; then
    v in dom f by A3,MESFUNC3:def 1; then
    v in dom f /\ A by F36,XBOOLE_0:def 3;
    hence v in dom(f|A) by FUNCT_1:68;
   end; then
G: union rng G c= dom(f|A) by TARSKI:def 3; then
F4:dom(f|A) = union rng G by A9,XBOOLE_0:def 10;
F5:dom G = dom a by A1,A3,MESFUNC3:def 1;
   for k be Nat st k in dom G for x be set st x in G.k holds (f|A).x = a.k
   proof
    let k be Nat;
    assume
F51: k in dom G;
    let x be set;
    assume
F52: x in G.k;
    G.k in rng G by F51,FUNCT_1:12; then
    x in union rng G by F52,TARSKI:def 4; then
F55:(f|A).x = f.x by G,FUNCT_1:70;
    for k be Nat st k in dom G for x be set st x in G.k holds f.x = a.k
    proof
     let k be Nat;
     assume
H1:   k in dom G;
     let x be set;
     assume x in G.k; then
     x in F.k /\ A by A1,A2,H1; then
     x in F.k by XBOOLE_0:def 3;
     hence f.x = a.k by H1,A1,A3,MESFUNC3:def 1;
    end;
    hence (f|A).x = a.k by F51,F52,F55;
   end;
   hence G,a are_Re-presentation_of f|A by F4,F5,MESFUNC3:def 1;
end;

theorem Cor10:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL st
f is_simple_func_in S holds dom f is Element of S
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f be PartFunc of X,ExtREAL;
   assume f is_simple_func_in S; then
   ex F be Finite_Sep_Sequence of S st
    dom f = union rng F &
    for n being Nat,x,y being Element of X st
     n in dom F & x in F.n & y in F.n holds f.x = f.y by MESFUNC2:def 5;
   hence thesis by MESFUNC2:34;
end;

Th14:
for Y being set, p be FinSequence holds
 (for i be Nat st i in dom p holds p.i in Y) implies p is FinSequence of Y
proof let Y be set;
   let p be FinSequence;
   assume
A1: for i be Nat st i in dom p holds p.i in Y;
   let b be set;
   assume b in rng p; then 
   ex i be  Element of NAT st i in dom p & p.i = b by FINSEQ_2:11;
   hence thesis by A1;
end;

LL3:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st 
f is_simple_func_in S & dom f <> {} & g is_simple_func_in S & dom g = dom f
 holds
    f+g is_simple_func_in S & dom(f+g) <> {}
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL such that
A1:f is_simple_func_in S and
A2:dom f <> {} and
A4:g is_simple_func_in S and
A5:dom g = dom f;
   consider F be Finite_Sep_Sequence of S,
      a be FinSequence of ExtREAL such that
A7: F,a are_Re-presentation_of f by A1,MESFUNC3:12;
   consider G be Finite_Sep_Sequence of S,
      b be FinSequence of ExtREAL such that
A8: G,b are_Re-presentation_of g by A4,MESFUNC3:12;
A9:dom f = union rng F & dom g = union rng G by A7,A8,MESFUNC3:def 1;
   set la = len F;
   set lb = len G;
   deffunc FG1(Nat) = F.(($1-'1) div lb + 1) /\ G.(($1-'1) mod lb + 1);
   consider FG be FinSequence such that
A13:len FG = la*lb and
A14:for k be Element of NAT st k in Seg (la*lb) holds FG.k=FG1(k)
from FINSEQ_1:sch 2;
   now let k be Nat;
    assume k in dom FG; then
A15:k in Seg(la*lb) by A13,FINSEQ_1:def 3; then
A16:FG.k = F.((k-'1) div lb + 1) /\ G.((k-'1) mod lb + 1) by A14;
    set i=(k-'1) div lb + 1;
    set j=(k-'1) mod lb + 1;
A17:i >= 0+1 & j >= 0+1 by XREAL_1:8;
    reconsider la'=la,lb'=lb as natural number;
A18:1 <= k & k <= la*lb by A15,FINSEQ_1:3; then
A19:lb <> 0; then
A20:lb > 0; then
    lb >= 0+1 by NAT_1:38; then
    lb' divides (la'*lb') & 1 <= la*lb & 1 <= lb
      by A18,NAT_1:def 3,XREAL_1:2; then
A21:((la'*lb') -' 1) div lb' = ((la'*lb') div lb') - 1 by NAT_2:17;
    (k -' 1) <= (la*lb -' 1) by A18,JORDAN3:5; then
    (k -' 1) div lb <= (la*lb -' 1) div lb by NAT_2:26; then
    (k -' 1) div lb + 1 <= la*lb div lb by A21,XREAL_1:21; then
    i <= la by A19,NAT_1:68; then
    i in Seg la by A17; then
    i in dom F by FINSEQ_1:def 3; then
A29:F.i in rng F by FUNCT_1:12;
    (k -' 1) mod lb < lb by A20,NAT_1:46; then
    j <= lb by NAT_1:38; then
    j in dom G by A17,FINSEQ_3:27; then
    G.j in rng G by FUNCT_1:12; then
    F.i is_measurable M & G.j is_measurable M by A29,MEASURE1:def 12; then
    F.i /\ G.j is_measurable M by MEASURE1:66;
    hence FG.k in S by A16,MEASURE1:def 12;
   end; then
   reconsider FG as FinSequence of S by Th14;
   for k,l be Nat st k in dom FG & l in dom FG & k <> l holds FG.k misses FG.l
   proof
    let k,l be Nat;
    assume
A33: k in dom FG & l in dom FG & k <> l;
    set i=(k-'1) div lb + 1;
    set j=(k-'1) mod lb + 1;
    set n=(l-'1) div lb + 1;
    set m=(l-'1) mod lb + 1;
A34:k in Seg(la*lb) & l in Seg (la*lb) by A33,A13,FINSEQ_1:def 3; then
A35:FG.k = F.i /\ G.j by A14;
A37:i >= 0+1 & j >= 0+1 & n >= 0+1 & m >= 0+1 by XREAL_1:8;
    reconsider la'=la,lb'=lb as natural number;
A38:1 <= k & k <= la*lb & 1 <= l & l <= la*lb by A34,FINSEQ_1:3; then
A39:lb <> 0; then
A40:lb > 0; then
    lb >= 0+1 by NAT_1:38; then
    lb' divides (la'*lb') & 1 <= la*lb & 1 <= lb
       by A38,NAT_1:def 3,XREAL_1:2; then
A41:((la'*lb') -' 1) div lb' = ((la'*lb') div lb') - 1 by NAT_2:17;
    (k -' 1) <= (la*lb -' 1) & (l -' 1) <= (la*lb -' 1) by A38,JORDAN3:5; then
    (k -' 1) div lb <= (la*lb div lb) - 1 &
    (l -' 1) div lb <= (la*lb div lb) - 1 by A41,NAT_2:26; then
    (k -' 1) div lb + 1 <= la*lb div lb &
    (l -' 1) div lb + 1 <= la*lb div lb by XREAL_1:21; then
    i <= la & n <= la by A39,NAT_1:68; then
    i in Seg la & n in Seg la by A37; then
A45:i in dom F & n in dom F by FINSEQ_1:def 3;
    (k -' 1) mod lb < lb & (l -' 1) mod lb < lb by A40,NAT_1:46; then
    j <= lb & m <= lb by NAT_1:38; then
A46:j in dom G & m in dom G by A37,FINSEQ_3:27;
A54:now assume A55:i=n & j=m;
     (k-'1)+1=(i-1)*lb+(j-1)+1 & (l-'1)+1=(n-1)*lb+(m-1)+1 
       by A40,NAT_1:47; then
     k - 1 + 1 = (i-1)*lb + j & l - 1 + 1 = (n-1)*lb+m by A38,BINARITH:50;
     hence contradiction by A33,A55;
    end;
    per cases by A54;
    suppose i <> n; then
A58: F.i misses F.n by A45,MESFUNC3:4;
     FG.k /\ FG.l= F.i /\ G.j /\ (F.n /\ G.m) by A34,A35,A14; then
     FG.k /\ FG.l= F.i /\ (G.j /\ (F.n /\ G.m)) by XBOOLE_1:16; then
     FG.k /\ FG.l= F.i /\ (F.n /\ (G.j /\ G.m)) by XBOOLE_1:16; then
     FG.k /\ FG.l= F.i /\ F.n /\ (G.j /\ G.m) by XBOOLE_1:16; then
     FG.k /\ FG.l= {} /\ (G.j /\ G.m) by A58,XBOOLE_0:def 7;
     hence FG.k misses FG.l by XBOOLE_0:def 7;
    end;
    suppose j <> m; then
A59: G.j misses G.m by A46,MESFUNC3:4;
     FG.k /\ FG.l= F.i /\ G.j /\ (F.n /\ G.m) by A34,A35,A14; then
     FG.k /\ FG.l= F.i /\ (G.j /\ (F.n /\ G.m)) by XBOOLE_1:16; then
     FG.k /\ FG.l= F.i /\ (F.n /\ (G.j /\ G.m)) by XBOOLE_1:16; then
     FG.k /\ FG.l= F.i /\ F.n /\ (G.j /\ G.m) by XBOOLE_1:16; then
     FG.k /\ FG.l= F.i /\ F.n /\ {} by A59,XBOOLE_0:def 7;
     hence FG.k misses FG.l by XBOOLE_0:def 7;
    end;
   end; then
   reconsider FG as Finite_Sep_Sequence of S by MESFUNC3:4;
A77:dom f = union rng FG
   proof
    now let z be set;
     assume A78:z in dom f; then
     consider Y be set such that
A79:  z in Y & Y in rng F by A9,TARSKI:def 4;
     consider i be Element of NAT such that
A80:  i in dom F & Y = F.i by A79,FINSEQ_2:11;
     i in Seg len F by A80,FINSEQ_1:def 3; then
A83: 1 <= i & i <= la by FINSEQ_1:3; then
     consider i' being Nat such that
A84:  i = 1 + i' by NAT_1:28;
     consider Z be set such that
A81:  z in Z & Z in rng G by A9,A78,A5,TARSKI:def 4;
     consider j be Element of NAT such that
A82:  j in dom G & Z = G.j by A81,FINSEQ_2:11;
     i'*lb + j is Nat; then
     reconsider k=(i-1)*lb+j as Element of NAT by A84,ORDINAL1:def 13;
     j in Seg len G by A82,FINSEQ_1:def 3; then
A85: 1 <= j & j <= lb by FINSEQ_1:3; then
     consider j' being Nat such that
A86:  j = 1 + j' by NAT_1:28;
     i'*lb >= 0; then
     k >= 0 + j by A84,XREAL_1:8; then
A87: k >= 1 by A85,XREAL_1:2; then
A88: k -' 1 = k - 1 by BINARITH:50 .= i'*lb + j' by A84,A86;
     j' < lb by A85,A86,NAT_1:38; then
A90: i = (k-'1) div lb +1 & j = (k-'1) mod lb +1 
       by A84,A86,A88,NAT_1:def 1,def 2;
     i-1 <= la-1 by A83,XREAL_1:11; then
     (i-1)*lb <= (la - 1)*lb by XREAL_1:66; then
A92: k <= (la - 1) * lb + j by XREAL_1:8;
     (la - 1) * lb + j <= (la - 1) * lb + lb by A85,XREAL_1:8; then
     k <= la*lb by A92,XREAL_1:2; then
A94: k in Seg (la*lb) by A87;
     z in F.i /\ G.j by A79,A80,A81,A82,XBOOLE_0:def 3; then
A96: z in FG.k by A94,A90,A14;
     k in dom FG by A13,A94,FINSEQ_1:def 3; then
     FG.k in rng FG by FUNCT_1:def 5;
     hence z in union rng FG by A96,TARSKI:def 4;
    end;
    hence dom f c= union rng FG by TARSKI:def 3;
    let z be set;
    assume z in union rng FG; then
    consider Y be set such that
A97: z in Y & Y in rng FG by TARSKI:def 4;
    consider k be Element of NAT such that
A98: k in dom FG & Y = FG.k by A97,FINSEQ_2:11;
    set i=(k-'1) div lb +1;
    set j=(k-'1) mod lb +1;
    k in Seg (la*lb) by A13,A98,FINSEQ_1:def 3; then
    FG.k=F.i /\ G.j by A14; then
A99:z in F.i by A97,A98,XBOOLE_0:def 3;
    reconsider la'=la,lb'=lb as natural number;
    k in Seg len FG by A98,FINSEQ_1:def 3; then
A100:1 <= k & k <= la*lb by A13,FINSEQ_1:3; then
A102:lb <> 0; then
    lb > 0; then
    lb >= 0+1 by NAT_1:38; then
    lb' divides (la'*lb') & 1 <= la*lb & 1 <= lb
      by A100,NAT_1:def 3,XREAL_1:2; then
A103:((la'*lb') -' 1) div lb' = ((la'*lb') div lb') - 1 by NAT_2:17;
    (k -' 1) <= (la*lb -' 1) by A100,JORDAN3:5; then
    (k -' 1) div lb <= (la*lb div lb) - 1 by A103,NAT_2:26; then
A104:i <= la*lb div lb by XREAL_1:21;
A101:i >= 0+1 & j >= 0+1 by NAT_1:38;
    la*lb div lb = la by A102,NAT_1:68; then
    i in Seg la by A101,A104; then
    i in dom F by FINSEQ_1:def 3; then
    F.i in rng F by FUNCT_1:def 5;
    hence thesis by A9,A99,TARSKI:def 4;
   end;
A105:g is_finite by A4,MESFUNC2:def 5; then
B106:dom(f+g) = dom f /\ dom g by MESFUNC2:2;
   now let x be Element of X;
    assume 
A109:x in dom(f+g); then
A112:|. (f+g).x .| = |. f.x + g.x .| by MESFUNC1:def 3;
    f is_finite by A1,MESFUNC2:def 5; then
A110:|. f.x .| < +infty & |. g.x .| < +infty 
       by A5,A105,A109,B106,MESFUNC2:def 1; then
    not (f.x = +infty or f.x = -infty) &
    not (g.x = +infty or g.x = -infty) by EXTREAL2:67; then
A113:|. (f+g).x .| <= |. f.x .| + |. g.x .| by A112,EXTREAL2:61;
    |. f.x .| + |. g.x .| <> +infty by A110,SUPINF_2:8; then
    |. f.x .| + |. g.x .| < +infty by SUPINF_1:24;
    hence |. (f+g).x .| < +infty by A113,SUPINF_1:29;
   end; then
A108:f+g is_finite by MESFUNC2:def 1;
   for k being Nat,x,y being Element of X st
     k in dom FG & x in FG.k & y in FG.k holds (f+g).x = (f+g).y
   proof
    let k be Nat;
    let x,y be Element of X;
    assume
A116:k in dom FG & x in FG.k & y in FG.k;
    set i=(k-'1) div lb + 1;
    set j=(k-'1) mod lb + 1;
    k in Seg (la*lb) by A13,A116,FINSEQ_1:def 3; then
    FG.k = F.( (k-'1) div lb + 1 ) /\ G.( (k-'1) mod lb  + 1 ) by A14; then
A127:x in F.i & x in G.j & y in F.i & y in G.j by A116,XBOOLE_0:def 3;
    k in Seg len FG by A116,FINSEQ_1:def 3; then
A118:1 <= k & k <= la*lb by A13,FINSEQ_1:3;
A119:i >= 0+1 & j >= 0+1 by XREAL_1:8;
A120:lb <> 0 by A118; then
A121:lb > 0; then
    lb >= 0+1 by NAT_1:38; then
    lb divides (la*lb) & 1 <= la*lb & 1 <= lb
       by A118,NAT_1:def 3,XREAL_1:2; then
A122:((la*lb) -' 1) div lb = ((la*lb) div lb) - 1 by NAT_2:17;
    (k -' 1) <= (la*lb -' 1) by A118,JORDAN3:5; then
    (k -' 1) div lb <= (la*lb div lb) - 1 by A122,NAT_2:26; then
    (k -' 1) div lb + 1 <= la*lb div lb by XREAL_1:21; then
    i <= la by A120,NAT_1:68; then
    i in Seg la by A119; then
A125:i in dom F by FINSEQ_1:def 3; then
A129:f.x=a.i by A127,A7,MESFUNC3:def 1;
    (k -' 1) mod lb < lb by A121,NAT_1:46; then
    j <= lb by NAT_1:38; then
    j in Seg lb by A119; then
A126:j in dom G by FINSEQ_1:def 3;
A132:f.y=a.i by A127,A125,A7,MESFUNC3:def 1;
    FG.k in rng FG by A116,FUNCT_1:def 5; then
A134:x in dom (f+g) & y in dom(f+g) by A77,B106,A5,A116,TARSKI:def 4; then
    (f+g).x= f.x+g.x by MESFUNC1:def 3; then
    (f+g).x= a.i+b.j by A129,A127,A126,A8,MESFUNC3:def 1; then
    (f+g).x= f.y+g.y by A132,A127,A126,A8,MESFUNC3:def 1;
    hence (f+g).x= (f+g).y by A134,MESFUNC1:def 3;
   end;
   hence f+g is_simple_func_in S by A108,A77,B106,A5,MESFUNC2:def 5;
   thus dom (f+g) <> {} by B106,A2,A5;
end;

theorem LL2:
for X be non empty set, S be SigmaField of X, 
f,g be PartFunc of X,ExtREAL st
 f is_simple_func_in S & g is_simple_func_in S holds
   f+g is_simple_func_in S
proof
   let X be non empty set;
   let S be SigmaField of X;
   let f,g be PartFunc of X,ExtREAL;
   assume
A1: f is_simple_func_in S & g is_simple_func_in S;
   per cases;
   suppose
B1: dom(f+g) = {};
    for x be Element of X st x in dom(f+g) holds
     |. (f+g).x .| < +infty by B1; then
 B2:f+g is_finite by MESFUNC2:def 1;
    ex F being Finite_Sep_Sequence of S st
     (dom(f+g) = union rng F &
      for n being Nat,x,y being Element of X st
       n in dom F & x in F.n & y in F.n holds (f+g).x = (f+g).y)
    proof
     reconsider EMPTY = {} as Element of S by PROB_1:42;
     deffunc F(Nat) = EMPTY;
     consider F being FinSequence of S such that
  B3: len F = 1 &
      for n be Element of NAT st n in Seg 1 holds F.n = F(n)
from FINSEQ_2:sch 1;
     now let i,j be Nat;
      assume
   B4: i in dom F & j in dom F & i <> j; then
      i in Seg len F & j in Seg len F by FINSEQ_1:def 3; then
      i = 1 & j = 1 by B3,FINSEQ_1:4,TARSKI:def 1;
      hence F.i misses F.j by B4;
     end; then
     reconsider F as Finite_Sep_Sequence of S by MESFUNC3:4;
     take F;
     F = <* F.1 *> by B3,FINSEQ_1:57; then
  B5:rng F = {F.1} by FINSEQ_1:56;
     1 in Seg 1; then
     union rng F = union bool {} by B3,B5,ZFMISC_1:1;
     hence dom(f+g) = union rng F by B1,ZFMISC_1:99;
     hereby let n be Nat;
      let x,y be Element of X;
      assume
   B6: n in dom F & x in F.n & y in F.n; then
      n in Seg 1 by B3,FINSEQ_1:def 3;
      hence (f+g).x = (f+g).y by B3,B6;
     end;
    end;
    hence (f+g) is_simple_func_in S by B2,MESFUNC2:def 5;
   end;
   suppose
C1: dom(f+g) <> {};
    reconsider F = dom f as Element of S by A1,Cor10;
    reconsider G = dom g as Element of S by A1,Cor10;
    f is without+infty & g is without+infty by A1,WI01; then
    not +infty in rng f & not +infty in rng g by Def0002; then
C2: f"{+infty} = {} & g"{+infty} = {} by FUNCT_1:142; then
C4: (dom f /\ dom g)\(f"{+infty}/\g"{-infty} \/ f"{-infty}/\g"{+infty})
      = dom f /\ dom g; then
C4a:dom(f+g) = dom f /\ dom g by MESFUNC1:def 3;
    dom f is Element of S & dom g is Element of S by A1,Cor10; then
    dom(f+g) in S by C4a,FINSUB_1:def 2; then
C5: f|dom(f+g) is_simple_func_in S & g|dom(f+g) is_simple_func_in S by A1,Cor5;
    dom(f|dom(f+g)) = dom f /\ dom(f+g) &
    dom(g|dom(f+g)) = dom g /\ dom(f+g) by FUNCT_1:68; then
C6: dom(f|dom(f+g)) = dom f /\ dom f /\ dom g &
    dom(g|dom(f+g)) = dom g /\ dom g /\ dom f by C4a,XBOOLE_1:16; then
    dom(f|dom(f+g)) = dom(f+g) & dom(g|dom(f+g)) = dom(f+g) 
      by C4,MESFUNC1:def 3; then
C7: f|dom(f+g) + g|dom(f+g) is_simple_func_in S by C5,C1,LL3;
C7a:(f|dom(f+g))"{+infty} = dom(f+g) /\ f"{+infty} &
    (g|dom(f+g))"{+infty} = dom(f+g) /\ g"{+infty} by FUNCT_1:139;
C8: dom(f|dom(f+g) + g|dom(f+g))
     = (dom(f|dom(f+g)) /\ dom(g|dom(f+g))) \
       (((f|dom(f+g))"{+infty} /\ (g|dom(f+g))"{-infty}) \/
     ((f|dom(f+g))"{-infty} /\ (g|dom(f+g))"{+infty})) by MESFUNC1:def 3
    .= dom(f+g) by C6,C7a,C2,C4,MESFUNC1:def 3;
    for x be Element of X st x in dom(f|dom(f+g) + g|dom(f+g)) holds
      (f|dom(f+g) + g|dom(f+g)).x = (f+g).x
    proof
     let x be Element of X;
     assume
 C81: x in dom(f|dom(f+g) + g|dom(f+g)); then
     (f|dom(f+g) + g|dom(f+g)).x
       = (f|dom(f+g)).x + (g|dom(f+g)).x by MESFUNC1:def 3
      .= f.x + (g|dom(f+g)).x by C8,C81,FUNCT_1:72
      .= f.x + g.x by C8,C81,FUNCT_1:72;
     hence thesis by C8,C81,MESFUNC1:def 3;
    end;
    hence f+g is_simple_func_in S by C7,C8,PARTFUN1:34;
   end;
end;

theorem LL5:
for X be non empty set, S be SigmaField of X, M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,c be Real st
 f is_simple_func_in S holds c(#)f is_simple_func_in S
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f be PartFunc of X,ExtREAL;
   let c be Real;
   assume
A1: f is_simple_func_in S;
   set g = c(#)f;
B1:dom g = dom f by MESFUNC1:def 6;   
P0:f is_finite by A1,MESFUNC2:def 5;
   now let x be Element of X;
    assume 
A:  x in dom g; then
    |. f.x .| < +infty by B1,P0,MESFUNC2:def 1; then
    f.x <> -infty & f.x <> +infty by EXTREAL2:67; then
    R_EAL c * f.x <> +infty & R_EAL c * f.x <> -infty by EXTREAL1:27,28; then
    g.x <> +infty & g.x <> -infty by A,MESFUNC1:def 6; then
    -infty < g.x & g.x < +infty by SUPINF_1:23,24; then
    -(+infty) < g.x & g.x < +infty by SUPINF_2:def 3;
    hence |. g.x .| < +infty by EXTREAL2:59;
   end; then
P1:g is_finite by MESFUNC2:def 1;
   consider G be Finite_Sep_Sequence of S such that
P21:dom f = union rng G and
P22:for n be Nat, x,y be Element of X st
     n in dom G & x in G.n & y in G.n holds f.x = f.y by A1,MESFUNC2:def 5;
   now let n be Nat;
    let x,y be Element of X; assume 
P25:n in dom G & x in G.n & y in G.n; then
    G.n in rng G by FUNCT_1:12; then
    x in dom g & y in dom g by B1,P21,P25,TARSKI:def 4; then
    g.x = (R_EAL c)*f.x & g.y = (R_EAL c)*f.y by MESFUNC1:def 6;
    hence g.x = g.y by P25,P22;
   end;
   hence g is_simple_func_in S by P1,MESFUNC2:def 5,P21,B1;
end;

theorem LL8:
for X be non empty set, S be SigmaField of X, M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st 
 f is_simple_func_in S & g is_simple_func_in S & 
 (for x be set st x in dom(f-g) holds g.x <= f.x) holds
 f-g is nonnegative
proof
   let X be non empty set,
      S be SigmaField of X,
      M be sigma_Measure of S,
      f,g be PartFunc of X,ExtREAL such that
AF1:f is_simple_func_in S and 
AG1:g is_simple_func_in S and
AG4:for x be set st x in dom(f-g) holds g.x <= f.x;
   f is without+infty & g is without-infty by AF1,AG1,WI01; then
   not +infty in rng f & not -infty in rng g by Def0001,Def0002; then
X2:f"{+infty} = {} & g"{-infty} = {} by FUNCT_1:142; then
   (dom f /\ dom g) \ (f"{+infty}/\g"{+infty} \/ f"{-infty}/\g"{-infty})
     = dom f /\ dom g; then
P1:dom (f-g) = dom f /\ dom g by MESFUNC1:def 4;
   for x be set st x in dom f /\ dom g holds
     g.x <= f.x & -infty < g.x & f.x < +infty
   proof
    let x be set;
    assume 
P11: x in dom f /\ dom g;
    hence g.x <= f.x by AG4,P1;
P12:x in dom f & x in dom g by P11,XBOOLE_0:def 3;
    not g.x in {-infty} by X2,P12,FUNCT_1:def 13; then
    not g.x = -infty by TARSKI:def 1;
    hence -infty < g.x by SUPINF_1:23;
    not f.x in {+infty} by X2,P12,FUNCT_1:def 13; then
    not f.x = +infty by TARSKI:def 1;
    hence f.x < +infty by SUPINF_1:24;
   end;
   hence f-g is nonnegative by LL7;
end;

theorem Cor9b:
for X be non empty set, S be SigmaField of X, M be sigma_Measure of S,
    A be Element of S, c be R_eal st 
 c <> +infty & c <> -infty holds 
   ex f be PartFunc of X,ExtREAL st
    f is_simple_func_in S & dom f = A &
    (for x be set st x in A holds f.x=c)
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let A be Element of S;
   let c be R_eal;
   assume
A0:c <> +infty & c <> -infty;   
   defpred P[set] means $1 in A;
   deffunc F(set) = c;
A1:for x be set st P[x] holds F(x) in ExtREAL;
   consider f be PartFunc of X,ExtREAL such that
A2: (for x be set holds x in dom f iff x in X & P[x]) &
    (for x be set st x in dom f holds f.x = F(x)) from PARTFUN1:sch 3(A1);
   take f;
   -infty < c & c < +infty by A0,SUPINF_1:23,24; then
   -(+infty) < c & c < +infty by SUPINF_2:def 3; then
   |.c.| < +infty by EXTREAL2:59; then
   for x be Element of X st x in dom f holds |. f.x .| < +infty by A2; then
A4:f is_finite by MESFUNC2:def 1;
A5:dom f c= A
   proof
    let x be set;
    assume x in dom f;
    hence x in A by A2;
   end; 
A6:A c= dom f
   proof
    let x be set;
    assume x in A;
    hence thesis by A2;
   end;
   ex F being Finite_Sep_Sequence of S st
    (dom f = union rng F &
     for n being Nat, x,y being Element of X st
      n in dom F & x in F.n & y in F.n holds f.x = f.y)
   proof
    set F = <* dom f *>;
A8a:rng F = {dom f} by FINSEQ_1:55; then
A8: rng F = {A} by A6,A5,XBOOLE_0:def 10;
    rng F c= S
    proof
     let z be set;
     assume z in rng F; then
     z = A by A8,TARSKI:def 1;
     hence thesis;
    end; then
    reconsider F as FinSequence of S by FINSEQ_1:def 4;
    now let i,j be Nat; assume
A9:   i in dom F & j in dom F & i <> j;
     dom F = Seg 1 by FINSEQ_1:55; then
     i = 1 & j = 1 by A9,TARSKI:def 1,FINSEQ_1:4;
     hence F.i misses F.j by A9;
    end; then
    reconsider F as Finite_Sep_Sequence of S by MESFUNC3:4;
    take F;
    thus dom f = union rng F by A8a,ZFMISC_1:31;
    hereby let n be Nat;
     let x,y be Element of X;
     assume 
A10:  n in dom F & x in F.n & y in F.n;
     dom F = Seg 1 by FINSEQ_1:55; then
     n = 1 by A10,FINSEQ_1:4,TARSKI:def 1; then
A11: x in dom f & y in dom f by A10,FINSEQ_1:57; then
     f.x = c by A2;
     hence f.x = f.y by A11,A2;
    end;
   end;
   hence f is_simple_func_in S by A4,MESFUNC2:def 5;
   thus dom f = A by A6,A5,XBOOLE_0:def 10;
   thus for x be set st x in A holds f.x = c by A2,A6;
end;

theorem Cor18:
for X be non empty set, S be SigmaField of X, M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL, B,BF be Element of S st
 f is_measurable_on B & BF = dom f /\ B holds f|B is_measurable_on BF
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       B,BF be Element of S such that
A1:f is_measurable_on B & BF = dom f /\ B;
   now let r be real number;
    now let x be set;
     (x in dom(f|B) & ex y being R_eal st y=f|B.x & y < R_EAL r) iff 
     x in dom f /\ B & ex y being R_eal st y=f|B.x & y < R_EAL r
            by FUNCT_1:68; then
 P7: x in BF & x in less_dom(f|B,R_EAL r) iff 
     x in B & x in dom f & ex y being R_eal st y=(f|B).x & y < R_EAL r
        by A1,MESFUNC1:def 12,XBOOLE_0:def 3;
     x in B & x in dom f implies
      ((ex y being R_eal st y=f.x & y < R_EAL r) iff
      (ex y being R_eal st y=(f|B).x & y < R_EAL r))
     proof
      assume
Q1:    x in B & x in dom f; then
      (ex y being R_eal st y=f.x & y < R_EAL r) implies
        f.x=(f|B).x by FUNCT_1:72;
      hence (ex y being R_eal st y=f.x & y < R_EAL r) implies
              ex y being R_eal st y=(f|B).x & y < R_EAL r;
      assume
Q2:    ex y being R_eal st y=(f|B).x & y < R_EAL r;
      f.x=(f|B).x by Q1,FUNCT_1:72;
      hence ex y being R_eal st y=f.x & y < R_EAL r by Q2;
     end; then
     x in BF /\ less_dom(f|B,R_EAL r) iff
     x in B & x in less_dom(f,R_EAL r) by XBOOLE_0:def 3,P7,MESFUNC1:def 12;
     hence x in BF /\ less_dom(f|B,R_EAL r)
           iff x in B /\ less_dom(f,R_EAL r) by XBOOLE_0:def 3;
    end; then
    BF /\ less_dom(f|B,R_EAL r) c= B /\ less_dom(f,R_EAL r) &
    B /\ less_dom(f,R_EAL r) c= BF /\ less_dom(f|B,R_EAL r) by TARSKI:def 3; 
    then
    BF /\ less_dom(f|B,R_EAL r) = B /\ less_dom(f,R_EAL r) by XBOOLE_0:def 10;
    hence BF /\ less_dom(f|B,R_EAL r) is_measurable_on S by A1,MESFUNC1:def 17;
   end;
   hence thesis by MESFUNC1:def 17;
end;

theorem LM019b:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    A be Element of S,
    f,g being PartFunc of X,ExtREAL st
 A c= dom f &
 f is_measurable_on A & g is_measurable_on A &
 f is without-infty & g is without-infty holds
   max+(f+g) + max-f is_measurable_on A
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       A be Element of S,
       f,g be PartFunc of X,ExtREAL;
   assume that
A0:A c= dom f and   
A1:f is_measurable_on A & g is_measurable_on A and
A3:f is without-infty & g is without-infty;
   f+g is_measurable_on A by A1,A3,MESFUNC2'7; then
A5:max+(f+g) is_measurable_on A by MESFUNC2:27;
A6:max-f is_measurable_on A by A0,A1,MESFUNC2:28;
   max+(f+g) is nonnegative & max-f is nonnegative by MAX;
   hence thesis by A5,A6,MESFUNC2'7a;
end;

theorem LM019c:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    A be Element of S,
    f,g being PartFunc of X,ExtREAL st
 A c= dom f /\ dom g &
 f is_measurable_on A & g is_measurable_on A &
 f is without-infty & g is without-infty holds
   max-(f+g) + max+f is_measurable_on A
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       A be Element of S,
       f,g be PartFunc of X,ExtREAL;
   assume that
A0:A c= dom f /\ dom g and   
A1:f is_measurable_on A & g is_measurable_on A and
A3:f is without-infty & g is without-infty;
   f+g is_measurable_on A & dom(f+g) = dom f /\ dom g
     by A1,A3,MESFUNC2'2,MESFUNC2'7; then
A5:max-(f+g) is_measurable_on A by A0,MESFUNC2:28;
A6:max+ f is_measurable_on A by A1,MESFUNC2:27;
   max-(f+g) is nonnegative & max+f is nonnegative by MAX;
   hence thesis by A5,A6,MESFUNC2'7a;
end;

theorem CorH:
for X be non empty set, S being SigmaField of X,
    M being sigma_Measure of S, A being set st
 A in S holds 0 <= M.A
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let A be set;
   assume A in S; then
   reconsider A as Element of S;
   reconsider E = {} as Element of S by PROB_1:42;
   E c= A by XBOOLE_1:2; then
   M.E <= M.A by MEASURE1:62;
   hence thesis by MEASURE1:def 11;
end;

CorJa:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL, r be Real st
 dom f in S &
 (for x be set st x in dom f holds f.x = r)
 holds
   f is_simple_func_in S
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f be PartFunc of X,ExtREAL;
   let r be Real;
   assume that
A0:dom f in S and
A3:for x be set st x in dom f holds f.x = r;
   reconsider Df = dom f as Element of S by A0;
   now let x be Element of X;
    assume x in dom f; then
    f.x = R_EAL r by A3; then
    -infty < f.x & f.x < +infty by SUPINF_1:31; then
    -(+infty) < f.x & f.x < +infty by SUPINF_2:def 3;
    hence |. f.x .| < +infty by EXTREAL2:59;   
   end; then
C1:f is_finite by MESFUNC2:def 1;
   ex F being Finite_Sep_Sequence of S st (dom f = union rng F &
    for n being Nat,x,y being Element of X st
     n in dom F & x in F.n & y in F.n holds f.x = f.y)
   proof
    deffunc F(Nat) = Df;
    consider F be FinSequence of S such that
 C2: len F = 1 &
     for n be Element of NAT st n in Seg 1 holds F.n = F(n)
     from FINSEQ_2:sch 1;
    now let i,j be Nat;
     assume A:i in dom F & j in dom F & i <> j; then
     i in Seg(len F) & j in Seg(len F) by FINSEQ_1:def 3; then
     i = 1 & j = 1 by C2,FINSEQ_1:4,TARSKI:def 1;
     hence F.i misses F.j by A;
    end; then
    reconsider F as Finite_Sep_Sequence of S by MESFUNC3:4;
    take F;
    1 in Seg 1; then
 C3:F.1 = Df by C2;
    F = <* F.1 *> by C2,FINSEQ_1:57; then
    rng F = {F.1} by FINSEQ_1:55;
    hence dom f = union rng F by C3,ZFMISC_1:31;
    hereby let n be Nat;
     let x,y be Element of X;
     assume 
 C5:  n in dom F & x in F.n & y in F.n; then
     n in Seg 1 by C2,FINSEQ_1:def 3; then
     F.n = Df by C2; then
     f.x = R_EAL r & f.y = R_EAL r by A3,C5;
     hence f.x = f.y;
    end;
   end;
   hence thesis by C1,MESFUNC2:def 5;
end;

theorem LM022y2a:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
(ex E1 be Element of S st E1=dom f & f is_measurable_on E1) &
(ex E2 be Element of S st E2=dom g & g is_measurable_on E2) & 
f"{+infty} in S & f"{-infty} in S & 
g"{+infty} in S & g"{-infty} in S holds
   dom(f+g) in S
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL;
   assume
AS0:(ex E1 be Element of S st E1=dom f & f is_measurable_on E1) &
    (ex E2 be Element of S st E2=dom g & g is_measurable_on E2) &
    f"{+infty} in S & f"{-infty} in S & g"{+infty} in S & g"{-infty} in S;
   consider E1 be Element of S such that
P3: E1=dom f & f is_measurable_on E1 by AS0;
   consider E2 be Element of S such that
P4: E2=dom g & g is_measurable_on E2 by AS0;
P6:dom(f+g)
    = (E1 /\ E2) \(f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty})
     by P3,P4,MESFUNC1:def 3;
   f"{-infty} is_measurable M & g"{+infty} is_measurable M &
   f"{+infty} is_measurable M & g"{-infty} is_measurable M
     by MEASURE1:def 12,AS0; then
   f"{-infty} /\ g"{+infty} is_measurable M &
   f"{+infty} /\ g"{-infty} is_measurable M by MEASURE1:66; then
   f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty} is_measurable M 
     by MEASURE1:66; then
P7:X \ (f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty})
      is_measurable M by MEASURE1:66;
P11:E1/\E2/\(X \ (f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty}))
    = (E1/\E2/\X) \ (f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty})
       by XBOOLE_1:49
   .=(E1/\E2) \ (f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty})
       by XBOOLE_1:28;
   E1/\E2 is_measurable M by MEASURE1:def 12; then
   E1/\E2/\(X \ (f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty}))
     is_measurable M by MEASURE1:66,P7;
   hence thesis by P6,P11,MEASURE1:def 12;
end;

LM022y4a:
for X be non empty set,
    S be SigmaField of X,
    A be Element of S,
    f be PartFunc of X,ExtREAL,
    r be real number holds
 A /\ less_dom(f,R_EAL r) = less_dom(f|A,R_EAL r)
proof
   let X be non empty set;
   let S be SigmaField of X;
   let A be Element of S;
   let f be PartFunc of X,ExtREAL;
   let r be real number;
   now let v be set;
    assume v in A /\ less_dom(f,R_EAL r); then
A2: v in A & v in less_dom(f,R_EAL r) by XBOOLE_0:def 3; then
    v in dom f & 
    ex w be R_eal st w = f.v & w < R_EAL r by MESFUNC1:def 12; then
    v in A /\ dom f by A2,XBOOLE_0:def 3; then
A5: v in dom(f|A) by FUNCT_1:68;
    consider w be R_eal such that
A4:  w = f.v & w < R_EAL r by A2,MESFUNC1:def 12;
    w = (f|A).v by A4,A2,FUNCT_1:72;
    hence v in less_dom(f|A,R_EAL r) by A4,A5,MESFUNC1:def 12;
   end; 
   hence A /\ less_dom(f,R_EAL r) c= less_dom(f|A,R_EAL r) by TARSKI:def 3;
   let v be set; assume
B0: v in less_dom(f|A,R_EAL r); then
B1:v in dom(f|A) by MESFUNC1:def 12;
   consider w being R_eal such that
B2: w = (f|A).v & w < R_EAL r by B0,MESFUNC1:def 12;
   v in dom f /\ A & w = f.v by B1,B2,FUNCT_1:68; then
   v in dom f & v in A & ex w be R_eal st w=f.v & w<R_EAL r
     by B2,XBOOLE_0:def 3; then
   v in A & v in less_dom(f,R_EAL r) by MESFUNC1:def 12;
   hence thesis by XBOOLE_0:def 3;
end;

LM022n4:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    A be Element of S,
    f be PartFunc of X,ExtREAL holds
 f|A is_measurable_on A iff f is_measurable_on A
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let A be Element of S;
   let f be PartFunc of X,ExtREAL;
   now assume 
A1: f|A is_measurable_on A; 
    now let r be real number;
     A /\ less_dom(f|A,R_EAL r) is_measurable_on S by A1,MESFUNC1:def 17; then
     A /\ (A /\ less_dom(f,R_EAL r)) is_measurable_on S by LM022y4a; then
     A /\ A /\ less_dom(f,R_EAL r) is_measurable_on S by XBOOLE_1:16;
     hence A /\ less_dom(f,R_EAL r) is_measurable_on S;
    end;
    hence f is_measurable_on A by MESFUNC1:def 17;
   end;
   hence f|A is_measurable_on A implies f is_measurable_on A;
   assume
A2: f is_measurable_on A;
   now let r be real number;
    A/\A /\ less_dom(f,R_EAL r) is_measurable_on S by A2,MESFUNC1:def 17; then
    A /\ (A /\ less_dom(f,R_EAL r)) is_measurable_on S by XBOOLE_1:16;
    hence A /\ less_dom(f|A,R_EAL r) is_measurable_on S by LM022y4a;
   end;
   hence thesis by MESFUNC1:def 17;
end;

LM022y3:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
(ex E1 be Element of S st E1=dom f & f is_measurable_on E1) &
(ex E2 be Element of S st E2=dom g & g is_measurable_on E2) &
dom f = dom g
  holds
   ex DFPG be Element of S st DFPG=dom(f+g) & f+g is_measurable_on DFPG
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL such that
A0:(ex E1 be Element of S st E1=dom f & f is_measurable_on E1) &
   (ex E2 be Element of S st E2=dom g & g is_measurable_on E2) &
   dom f = dom g;
   consider E0 be Element of S such that
A1: E0=dom f & f is_measurable_on E0 by A0;
A2:E0 /\ eq_dom(f,+infty) is_measurable_on S &
   E0 /\ eq_dom(f,-infty) is_measurable_on S by A1,MESFUNC1:37,38;
   now let v be set;
    assume v in f"{+infty}; then
    v in dom f & f.v in {+infty} by FUNCT_1:def 13; then
    v in dom f & f.v = +infty by TARSKI:def 1; then
    v in E0 & v in eq_dom(f,+infty) by A1,MESFUNC1:def 16;
    hence v in E0 /\ eq_dom(f,+infty) by XBOOLE_0:def 3;
   end; then
A10:f"{+infty} c= E0 /\ eq_dom(f,+infty) by TARSKI:def 3;
   now let v be set;
    assume v in E0 /\ eq_dom(f,+infty); then
A3: v in E0 & v in eq_dom(f,+infty) by XBOOLE_0:def 3; then
A4: v in dom f by MESFUNC1:def 16; 
    ex w be R_eal st w = f.v & +infty = w by A3,MESFUNC1:def 16; then
    f.v in {+infty} by TARSKI:def 1;
    hence v in f"{+infty} by A4,FUNCT_1:def 13;
   end; then
   E0 /\ eq_dom(f,+infty) c= f"{+infty} by TARSKI:def 3; then
   f"{+infty} = E0 /\ eq_dom(f,+infty) by A10,XBOOLE_0:def 10; then
A5:f"{+infty} in S by A2,MESFUNC1:def 11;
   now let v be set;
    assume v in f"{-infty}; then
    v in dom f & f.v in {-infty} by FUNCT_1:def 13; then
    v in dom f & f.v = -infty by TARSKI:def 1; then
    v in E0 & v in eq_dom(f,-infty) by A1,MESFUNC1:def 16;
    hence v in E0 /\ eq_dom(f,-infty) by XBOOLE_0:def 3;
   end; then
A6:f"{-infty} c= E0 /\ eq_dom(f,-infty) by TARSKI:def 3;
   now let v be set;
    assume v in E0 /\ eq_dom(f,-infty); then
A7: v in E0 & v in eq_dom(f,-infty) by XBOOLE_0:def 3; then
A8: v in dom f by MESFUNC1:def 16;
    ex w be R_eal st w = f.v & -infty = w by A7,MESFUNC1:def 16; then
    f.v in {-infty} by TARSKI:def 1;
    hence v in f"{-infty} by A8,FUNCT_1:def 13;
   end; then
   E0 /\ eq_dom(f,-infty) c= f"{-infty} by TARSKI:def 3; then
   f"{-infty} = E0 /\ eq_dom(f,-infty) by A6,XBOOLE_0:def 10; then
A9:f"{-infty} in S by A2,MESFUNC1:def 11;
   consider Gf be Element of S such that
B0:Gf=dom g & g is_measurable_on Gf by A0;
   Gf /\ eq_dom(g,+infty) is_measurable_on S &
   Gf /\ eq_dom(g,-infty) is_measurable_on S by B0,MESFUNC1:37,38; then
B1:Gf /\ eq_dom(g,+infty) in S & Gf /\ eq_dom(g,-infty) in S
     by MESFUNC1:def 11;
   now let v be set;
    assume v in g"{+infty}; then
    v in dom g & g.v in {+infty} by FUNCT_1:def 13; then
    v in dom g & g.v = +infty by TARSKI:def 1; then
    v in Gf & v in eq_dom(g,+infty) by B0,MESFUNC1:def 16;
    hence v in Gf /\ eq_dom(g,+infty) by XBOOLE_0:def 3;
   end; then
B2:g"{+infty} c= Gf /\ eq_dom(g,+infty) by TARSKI:def 3;
   now let v be set;
    assume v in Gf /\ eq_dom(g,+infty); then
B3: v in Gf & v in eq_dom(g,+infty) by XBOOLE_0:def 3; then
B4:  v in dom g by MESFUNC1:def 16; 
    ex w be R_eal st w = g.v & +infty = w by B3,MESFUNC1:def 16; then
    g.v in {+infty} by TARSKI:def 1;
    hence v in g"{+infty} by B4,FUNCT_1:def 13;
   end; then
   Gf /\ eq_dom(g,+infty) c= g"{+infty} by TARSKI:def 3; then
B5:g"{+infty} in S by B1,B2,XBOOLE_0:def 10;
   now let v be set;
    assume v in g"{-infty}; then
    v in dom g & g.v in {-infty} by FUNCT_1:def 13; then
    v in dom g & g.v = -infty by TARSKI:def 1; then
    v in Gf & v in eq_dom(g,-infty) by B0,MESFUNC1:def 16;
    hence v in Gf /\ eq_dom(g,-infty) by XBOOLE_0:def 3;
   end; then
B6:g"{-infty} c= Gf /\ eq_dom(g,-infty) by TARSKI:def 3;
   now let v be set;
    assume v in Gf /\ eq_dom(g,-infty); then
B7: v in Gf & v in eq_dom(g,-infty) by XBOOLE_0:def 3; then
B8: v in dom g by MESFUNC1:def 16;
    ex w be R_eal st w = g.v & -infty = w by B7,MESFUNC1:def 16; then
    g.v in {-infty} by TARSKI:def 1;
    hence v in g"{-infty} by B8,FUNCT_1:def 13;
   end; then
   Gf /\ eq_dom(g,-infty) c= g"{-infty} by TARSKI:def 3; then
B9:g"{-infty} in S by B6,B1,XBOOLE_0:def 10;
   reconsider NF=f"{+infty}\/f"{-infty} as Element of S by A5,A9,PROB_1:41;
   reconsider NG=g"{+infty}\/g"{-infty} as Element of S by B5,B9,PROB_1:41;
   reconsider NFG = NF \/ NG as Element of S;
   reconsider E = E0 \ NFG as Element of S;
   set f1=f|E;
   set g1=g|E;
C1:dom(f+g) = (dom f /\ dom g)
    \(f"{-infty} /\ g"{+infty} \/ f"{+infty} /\ g"{-infty}) by MESFUNC1:def 3;
   f"{-infty} /\ g"{+infty} c= f"{-infty} & f"{-infty} c= NF &
   f"{+infty} /\ g"{-infty} c= g"{-infty} & g"{-infty} c= NG
     by XBOOLE_1:7,17; then
   f"{-infty}/\g"{+infty} c= NF & f"{+infty}/\g"{-infty} c= NG 
     by XBOOLE_1:1; then
   f"{-infty} /\ g"{+infty} \/ f"{+infty} /\ g"{-infty} c= NF \/ NG 
     by XBOOLE_1:13; then
C3:E c= dom(f+g) by A1,A0,C1,XBOOLE_1:34; then
C4:(f+g)|E = f1+g1 & dom(f1+g1)=E by LM022w;
   reconsider DFPG=dom(f+g) as Element of S by LM022y2a,A0,A5,A9,B5,B9;
   set NFPG=DFPG \ E;
C5:E c= dom f & E c= dom g by A0,A1,XBOOLE_1:36; then
C6:f is_measurable_on E & g is_measurable_on E by A0,MESFUNC1:34;
   dom f /\ E = E & dom g /\ E = E by C5,XBOOLE_1:28; then
C7:f1 is_measurable_on E & g1 is_measurable_on E by C6,Cor18;
C8:now let x be set;
    assume L1:x in dom f1; then
    x in dom f /\ E by FUNCT_1:68; then
 L5:x in dom f & x in E by XBOOLE_0:def 3; then
 L8:not x in NFG by XBOOLE_0:def 4;
    for x be set st x in dom f holds f.x in ExtREAL; then
    reconsider ff=f as Function of dom f,ExtREAL by FUNCT_2:5;
 L2:now assume f1.x = +infty; then
     f.x = +infty by FUNCT_1:68,L1; then
     f.x in {+infty} by TARSKI:def 1; then
  PP:x in ff"{+infty} by L5,FUNCT_2:46;
     f"{+infty} c= NF by XBOOLE_1:7;
     hence contradiction by L8,PP,XBOOLE_0:def 2;
    end;
    now assume f1.x = -infty; then
     f.x = -infty by FUNCT_1:68,L1; then
     ff.x in {-infty} by TARSKI:def 1; then
  PP:x in ff"{-infty} by L5,FUNCT_2:46;
     f"{-infty} c= NF by XBOOLE_1:7;
     hence contradiction by L8,PP,XBOOLE_0:def 2;
    end;
    hence -infty < f1.x & f1.x < +infty by L2,SUPINF_1:23,SUPINF_1:24;
   end;
C9:now let x be set;
    assume L1: x in dom g1; then
    x in dom g /\ E by FUNCT_1:68; then
 L5:x in dom g & x in E by XBOOLE_0:def 3; then
 L8:not x in NFG by XBOOLE_0:def 4;
    for x be set st x in dom g holds g.x in ExtREAL; then
    reconsider gg=g as Function of dom g, ExtREAL by FUNCT_2:5;
 L2:now assume g1.x = +infty; then
     g.x = +infty by FUNCT_1:68,L1; then
     gg.x in {+infty} by TARSKI:def 1; then
  PP:x in gg"{+infty} by L5,FUNCT_2:46;
     g"{+infty} c= NG by XBOOLE_1:7;
     hence contradiction by L8,PP,XBOOLE_0:def 2;
    end;
    now assume g1.x = -infty; then
     g.x = -infty by FUNCT_1:68,L1; then
     gg.x in {-infty} by TARSKI:def 1; then
  PP:x in gg"{-infty} by L5,FUNCT_2:46;
     g"{-infty} c= NG by XBOOLE_1:7;
     hence contradiction by L8,PP,XBOOLE_0:def 2;
    end;
    hence -infty < g1.x & g1.x < +infty by L2,SUPINF_1:23,SUPINF_1:24;
   end;
   now let x be Element of X;
    assume x in dom f1; then
 P1:-infty < f1.x & f1.x < +infty by C8;
    -(+infty) = -infty by SUPINF_2:def 3;
    hence |.f1.x.| < +infty by P1,EXTREAL2:59;
   end; then
D0:f1 is_finite by MESFUNC2:def 1;
   now let x be Element of X;
    assume x in dom g1; then
 P1:-infty < g1.x & g1.x < +infty by C9;
    -(+infty) = -infty by SUPINF_2:def 3;
    hence |.g1.x.| < +infty by P1,EXTREAL2:59;
   end; then
   g1 is_finite by MESFUNC2:def 1; then
D1:E =dom(f1+g1) & f1+g1 is_measurable_on E by C3,C7,LM022w,D0,MESFUNC2:7;
D2:for r being real number holds
    DFPG /\ less_dom(f+g,R_EAL r)
      = E/\less_dom(f1+g1,R_EAL r) \/ f"{-infty}/\(DFPG \ g"{+infty})
       \/ g"{-infty}/\(DFPG \ f"{+infty})
   proof
    let r be real number;
    set SL= DFPG/\less_dom(f+g,R_EAL r);
    set SR= E/\less_dom(f1+g1,R_EAL r)
         \/ f"{-infty}/\(DFPG \ g"{+infty}) \/ g"{-infty}/\(DFPG \ f"{+infty});
  R:now let x be set;
     assume x in SL; then
  R1:x in DFPG & x in less_dom(f+g,R_EAL r) by XBOOLE_0:def 3; then
     x in (dom f /\ dom g)\(f"{-infty}/\g"{+infty}
          \/ f"{+infty}/\g"{-infty}) by MESFUNC1:def 3; then
 R45:not x in f"{+infty}/\g"{-infty} \/ f"{-infty}/\g"{+infty} &
     x in dom f /\ dom g by XBOOLE_0:def 4; then
 R46:x in dom f & x in dom g by XBOOLE_0:def 3; then
 K41:(x in f"{+infty} iff f.x in {+infty}) &
     (x in f"{-infty} iff f.x in {-infty}) &
     (x in g"{+infty} iff g.x in {+infty}) &
     (x in g"{-infty} iff g.x in {-infty}) by FUNCT_1:def 13; then
  K1:(x in f"{+infty} iff f.x=+infty) & (x in f"{-infty} iff f.x=-infty) &
     (x in g"{+infty} iff g.x=+infty) & (x in g"{-infty} iff g.x=-infty)
        by TARSKI:def 1;
  R3:ex y be R_eal st y=(f+g).x & y < R_EAL r by R1,MESFUNC1:def 12; then
  R4:f.x + g.x < R_EAL r by R1,MESFUNC1:def 3;
 R50:not x in (f"{+infty}/\g"{-infty}) &
     not x in (f"{-infty}/\g"{+infty}) by R45,XBOOLE_0:def 2;
     per cases;
     suppose f.x =-infty; then
      x in f"{-infty} & x in DFPG \ g"{+infty}
        by K41,R1,R50,XBOOLE_0:def 3,def 4,TARSKI:def 1; then
      x in f"{-infty} /\ (DFPG \ g"{+infty}) by XBOOLE_0:def 3; then
      x in E /\ less_dom(f1+g1,R_EAL r)
        \/ f"{-infty} /\ (DFPG \ g"{+infty}) by XBOOLE_0:def 2;
      hence x in SR by XBOOLE_0:def 2;
     end;
     suppose C2:f.x <>-infty;
      per cases;
      suppose g.x = -infty; then
       x in g"{-infty} & x in DFPG \ f"{+infty}
           by R50,XBOOLE_0:def 3,def 4,R1,K41,TARSKI:def 1; then
       x in g"{-infty}/\(DFPG \ f"{+infty}) by XBOOLE_0:def 3;
       hence x in SR by XBOOLE_0:def 2;
      end;
      suppose g.x <> -infty; then
       not (x in f"{-infty}\/f"{+infty} or x in g"{-infty} or x in g"{+infty})
              by K1,R4,EXTREAL2:27,XBOOLE_0:def 2,C2; then
       not (x in f"{-infty} \/ f"{+infty} \/ g"{-infty} or x in g"{+infty})
          by XBOOLE_0:def 2; then
       not x in f"{-infty} \/ f"{+infty} \/ g"{-infty} \/ g"{+infty}
          by XBOOLE_0:def 2; then
       not x in NFG by XBOOLE_1:4; then
    J2:x in E by A1,R46,XBOOLE_0:def 4; then
       (f1+g1).x = (f+g).x by C4,FUNCT_1:68; then
       x in less_dom(f1+g1,R_EAL r) by R3,J2,D1,MESFUNC1:def 12; then
       x in E/\less_dom(f1+g1,R_EAL r) by J2,XBOOLE_0:def 3; then
       x in E/\less_dom(f1+g1,R_EAL r) \/ f"{-infty}/\(DFPG \ g"{+infty})
         by XBOOLE_0:def 2;
       hence x in SR by XBOOLE_0:def 2;
      end;
     end;
    end;
    now let x be set;
     assume x in SR; then
  L1:x in E /\ less_dom(f1+g1,R_EAL r) \/ f"{-infty} /\ (DFPG\ g"{+infty}) or
     x in g"{-infty} /\ (DFPG \ f"{+infty}) by XBOOLE_0:def 2;
     per cases by L1,XBOOLE_0:def 2;
     suppose x in E /\ less_dom(f1+g1,R_EAL r); then
  C11:x in E & x in less_dom(f1+g1,R_EAL r) by XBOOLE_0:def 3; then
      ex y be R_eal st y=(f1+g1).x & y < R_EAL r by MESFUNC1:def 12; then
      (f+g).x < R_EAL r by C4,C11,FUNCT_1:68; then
      x in less_dom(f+g,R_EAL r) by C3,C11,MESFUNC1:def 12;
      hence x in SL by C3,C11,XBOOLE_0:def 3;
     end;
     suppose 
   C2:x in f"{-infty} /\ (DFPG \ g"{+infty}) or
      x in g"{-infty} /\ (DFPG \ f"{+infty});
      per cases by C2;
      suppose x in f"{-infty} /\ (DFPG \ g"{+infty}); then
  C21a:x in f"{-infty} & x in DFPG \ g"{+infty} by XBOOLE_0:def 3; then
   C20:x in DFPG by XBOOLE_0:def 4; then
       x in (dom f /\ dom g) 
              \ (f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty}) 
                by MESFUNC1:def 3; then
       x in dom f /\ dom g by XBOOLE_0:def 4; then
       x in dom f & x in dom g by XBOOLE_0:def 3; then
       (x in f"{+infty} iff f.x in {+infty}) &
       (x in f"{-infty} iff f.x in {-infty}) &
       (x in g"{+infty} iff g.x in {+infty}) &
       (x in g"{-infty} iff g.x in {-infty}) by FUNCT_1:def 13; then
       (x in f"{+infty} iff f.x=+infty) & (x in f"{-infty} iff f.x=-infty) &
       (x in g"{+infty} iff g.x=+infty) & (x in g"{-infty} iff g.x=-infty)
         by TARSKI:def 1; then
   EE1:f.x + g.x = -infty by C21a,XBOOLE_0:def 4,SUPINF_2:def 2;
       R_EAL r in REAL by XREAL_0:def 1; then
       -infty < R_EAL r by SUPINF_1:31; then
       (f+g).x < R_EAL r by EE1,C20,MESFUNC1:def 3; then
       x in less_dom(f+g,R_EAL r) by C20,MESFUNC1:def 12;
       hence x in DFPG /\ less_dom(f+g,R_EAL r) by C20,XBOOLE_0:def 3;
      end;
      suppose x in g"{-infty}/\(DFPG \ f"{+infty}); then
  C21a:x in g"{-infty} & x in DFPG \ f"{+infty} by XBOOLE_0:def 3; then
   C20:x in DFPG by XBOOLE_0:def 4; then
       x in (dom f /\ dom g)
            \(f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty}) 
               by MESFUNC1:def 3; then
       x in dom f /\ dom g by XBOOLE_0:def 4; then
       x in dom f & x in dom g by XBOOLE_0:def 3; then
       (x in f"{+infty} iff f.x in {+infty}) &
       (x in f"{-infty} iff f.x in {-infty}) &
       (x in g"{+infty} iff g.x in {+infty}) &
       (x in g"{-infty} iff g.x in {-infty}) by FUNCT_1:def 13; then
       (x in f"{+infty} iff f.x=+infty) & (x in f"{-infty} iff f.x=-infty) &
       (x in g"{+infty} iff g.x=+infty) & (x in g"{-infty} iff g.x=-infty)
          by TARSKI:def 1; then
   EE1:f.x + g.x = -infty & x in DFPG by C21a,XBOOLE_0:def 4,SUPINF_2:def 2;
       R_EAL r in REAL by XREAL_0:def 1; then
       -infty < R_EAL r by SUPINF_1:31; then
       (f+g).x < R_EAL r & x in DFPG by EE1,MESFUNC1:def 3; then
       x in less_dom(f+g,R_EAL r) by MESFUNC1:def 12;
       hence x in DFPG /\ less_dom(f+g,R_EAL r) by C20,XBOOLE_0:def 3;
      end;
     end;
    end;
    hence SL=SR by R,TARSKI:2;
   end;
   now let r be real number;
A17:DFPG /\ less_dom(f+g,R_EAL r)
     = E /\ less_dom(f1+g1,R_EAL r) \/ f"{-infty}/\(DFPG \ g"{+infty})
      \/ g"{-infty}/\(DFPG \ f"{+infty}) by D2;
    DFPG \ g"{+infty} in S & DFPG \ f"{+infty} in S by A5,B5,PROB_1:44; then
    f"{-infty}/\(DFPG \ g"{+infty}) in S &
    g"{-infty}/\(DFPG \ f"{+infty}) in S by A9,B9,FINSUB_1:def 2; then
A23:f"{-infty}/\(DFPG \ g"{+infty}) \/ g"{-infty}/\(DFPG \ f"{+infty}) in S
       by PROB_1:41;
    E/\less_dom(f1+g1,R_EAL r) is_measurable_on S by D1,MESFUNC1:def 17; then
    E/\less_dom(f1+g1,R_EAL r) in S by MESFUNC1:def 11; then
    E/\less_dom(f1+g1,R_EAL r) \/ (f"{-infty}/\(DFPG \ g"{+infty})
     \/ g"{-infty}/\(DFPG \ f"{+infty})) in S by A23,PROB_1:41; then
    DFPG /\ less_dom(f+g,R_EAL r) in S by A17,XBOOLE_1:4;
    hence DFPG /\ less_dom(f+g,R_EAL r) is_measurable_on S by MESFUNC1:def 11;
   end; then
   f+g is_measurable_on DFPG by MESFUNC1:def 17;
   hence thesis;
end;

theorem LM022y3a:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
 (ex E1 be Element of S st E1=dom f & f is_measurable_on E1) &
 (ex E2 be Element of S st E2=dom g & g is_measurable_on E2)
  holds 
   ex E be Element of S st E=dom(f+g) & (f+g) is_measurable_on E
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL;
   assume that
A1:(ex E1 be Element of S st E1=dom f & f is_measurable_on E1) and
A2:ex E2 be Element of S st E2=dom g & g is_measurable_on E2;
   consider E1 being Element of S such that
B1: E1 = dom f & f is_measurable_on E1 by A1;
   consider E2 being Element of S such that
B2: E2 = dom g & g is_measurable_on E2 by A2;
   set E3 = E1 /\ E2;
   set f1 = f|E3;
   set g1 = g|E3;
C1:E3 c= E1 & E3 c= E2 by XBOOLE_1:17; then
   f is_measurable_on E3 & g is_measurable_on E3 by B1,B2,MESFUNC1:34; then
C2:f1 is_measurable_on E3 & g1 is_measurable_on E3 by LM022n4;
   dom f1 = dom f /\ E3 & dom g1 = dom g /\ E3 by FUNCT_1:68; then
C3:dom f1 = E3 & dom g1 = E3 by B1,B2,C1,XBOOLE_1:28; then
   consider E be Element of S such that
D1: E = dom(f1+g1) &
    (f1+g1) is_measurable_on E by C2,LM022y3;
E1:dom(f1+g1) = (dom f1 /\ dom g1) \
     (f1"{-infty}/\g1"{+infty} \/ f1"{+infty}/\g1"{-infty}) by MESFUNC1:def 3;
E2:f1"{-infty} = E3 /\ f"{-infty} & f1"{+infty} = E3 /\ f"{+infty} &
   g1"{-infty} = E3 /\ g"{-infty} & g1"{+infty} = E3 /\ g"{+infty} 
     by FUNCT_1:139; then
E3:f1"{-infty} /\ g1"{+infty} 
    = f"{-infty} /\ (E3 /\ (E3 /\ g"{+infty})) by XBOOLE_1:16
   .= f"{-infty} /\ (E3 /\ E3 /\ g"{+infty}) by XBOOLE_1:16
   .= f"{-infty} /\ g"{+infty} /\ E3 by XBOOLE_1:16;
   f1"{+infty} /\ g1"{-infty}
    = f"{+infty} /\ (E3 /\ (E3 /\ g"{-infty})) by E2,XBOOLE_1:16
   .= f"{+infty} /\ (E3 /\ E3 /\ g"{-infty}) by XBOOLE_1:16
   .= f"{+infty} /\ g"{-infty} /\ E3 by XBOOLE_1:16; then
E4:f1"{-infty}/\g1"{+infty} \/ f1"{+infty}/\g1"{-infty}
    = E3 /\ (f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty})
       by E3,XBOOLE_1:23;
F0:dom(f+g) = (dom f /\ dom g) \
    (f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty}) by MESFUNC1:def 3; then
F1:dom(f+g) = E by E4,D1,C3,E1,XBOOLE_1:47,B1,B2;
   take E;
G2:dom((f+g)|E) = dom(f+g) /\ E by FUNCT_1:68;
   now let v be Element of X; assume 
G3:  v in dom((f+g)|E); then
    v in dom(f+g) /\ E by FUNCT_1:68; then
G4: v in dom(f+g) & v in E by XBOOLE_0:def 3;
G5: E c= E3 by D1,C3,E1,XBOOLE_1:36;
G6: ((f+g)|E).v = (f+g).v by G3,FUNCT_1:70
               .= f.v + g.v by G4,MESFUNC1:def 3;
    (f1+g1).v = f1.v + g1.v by F1,G2,D1,G3,MESFUNC1:def 3
             .= f.v + g1.v by G5,C3,G4,FUNCT_1:70;
    hence ((f+g)|E).v = (f1+g1).v by G6,G5,C3,G4,FUNCT_1:70;
   end; then
   (f+g)|E = f1+g1 by F1,G2,D1,PARTFUN1:34;
   hence thesis by F0,E4,D1,C3,E1,XBOOLE_1:47,B1,B2,LM022n4;
end;

theorem LM0251:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    A,B be Element of S st
 dom f = A holds f is_measurable_on B iff f is_measurable_on (A/\B)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       A,B be Element of S such that
A0:dom f = A;
B1:now let r be real number;
    now let x be set;
     x in A /\ less_dom(f,R_EAL r) iff x in A & x in less_dom(f,R_EAL r)
       by XBOOLE_0:def 3;
     hence x in A /\ less_dom(f,R_EAL r) iff x in less_dom(f,R_EAL r)
       by A0,MESFUNC1:def 12;
    end; then
    A /\ less_dom(f,R_EAL r) c= less_dom(f,R_EAL r) &
    less_dom(f,R_EAL r) c= A /\ less_dom(f,R_EAL r) by TARSKI:def 3;
    hence A /\ less_dom(f,R_EAL r) = less_dom(f,R_EAL r) by XBOOLE_0:def 10;
   end;
   hereby assume
 A1:f is_measurable_on B;
    now let r be real number;
     A /\ B /\ less_dom(f,R_EAL r) 
       = B /\ (A /\ less_dom(f,R_EAL r)) by XBOOLE_1:16
      .= B /\ less_dom(f,R_EAL r) by B1;
     hence A/\B/\less_dom(f,R_EAL r) is_measurable_on S by MESFUNC1:def 17,A1;
    end;
    hence f is_measurable_on A/\B by MESFUNC1:def 17;
   end;
   assume
 A1:f is_measurable_on (A/\B);
   now let r be real number;
    A /\ B /\ less_dom(f,R_EAL r)
      = B /\ (A /\ less_dom(f,R_EAL r)) by XBOOLE_1:16
     .=B /\ less_dom(f,R_EAL r) by B1;
    hence B /\ less_dom(f,R_EAL r) is_measurable_on S by MESFUNC1:def 17,A1;
   end;
   hence f is_measurable_on B by MESFUNC1:def 17;
end;

theorem LM0252:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL st
 ( ex A be Element of S st dom f = A )
 for c be Real, B be Element of S st f is_measurable_on B holds
   c(#)f is_measurable_on B
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL such that
A0:ex A be Element of S st A = dom f;
   let c be Real, B be Element of S such that
A1: f is_measurable_on B;
   consider A be Element of S such that
A2: A = dom f by A0;
A3:f is_measurable_on A /\ B by A2,A1,LM0251;
   A /\ B c= dom f by A2,XBOOLE_1:17; then
A5:c(#)f is_measurable_on A /\ B by A3,MESFUNC1:41;
   dom(c(#)f) = A by A2,MESFUNC1:def 6;
   hence c(#)f is_measurable_on B by A5,LM0251;
end;

begin :: Sequence of extended real numbers

definition
  mode ExtREAL_sequence is Function of NAT, ExtREAL;
end;

definition let seq be ExtREAL_sequence;
  attr seq is convergent_to_finite_number means :Defconverge1:
  ex g be real number st
  for p be real number st 0<p
   ex n be Nat st
    for m be Nat st n<=m holds |.seq.m-R_EAL(g).| < p;
end;

definition let seq be ExtREAL_sequence;
  attr seq is convergent_to_+infty means :Defconverge2:
  for g be real number st 0 < g
  ex n be Nat st for m be Nat st n<=m holds g <= seq.m;
end;

definition let seq be ExtREAL_sequence;
  attr seq is convergent_to_-infty means :Defconverge3:
  for g be real number st g < 0
   ex n be Nat st for m be Nat st n<=m holds seq.m <= g;
end;

theorem LM011b:
for seq be ExtREAL_sequence st seq is convergent_to_+infty holds
  not seq is convergent_to_-infty & not seq is convergent_to_finite_number
proof
   let seq be ExtREAL_sequence;
   assume
A1: seq is convergent_to_+infty;
   hereby assume seq is convergent_to_-infty; then
    consider n1 being Nat such that
A4:  for m be Nat st n1 <= m holds seq.m <= R_EAL -1 by Defconverge3;
    consider n2 being Nat such that
A6:  for m be Nat st n2 <= m holds R_EAL 1 <= seq.m by A1,Defconverge2;
    reconsider n1,n2 as Element of NAT by ORDINAL1:def 13;
    set m = max(n1,n2);
    m >= n1 & m >= n2 by SQUARE_1:46; then
    seq.m <= R_EAL -1 & R_EAL 1 <= seq.m by A4,A6;
    hence contradiction;
   end;
   assume seq is convergent_to_finite_number; then
   consider g being real number such that
A8: for p be real number st 0 < p
     ex n be Nat st
      for m be Nat st n<=m holds |.seq.m-R_EAL g.| < p by Defconverge1;
   per cases;
   suppose
A10:g > 0; then
    consider n1 be Nat such that
A11: for m be Nat st n1 <= m holds |.seq.m-R_EAL g.| < R_EAL(g) by A8;
A12:now let m be Nat;
     assume n1 <= m; then
     |.seq.m-R_EAL g.| < R_EAL g by A11; then
     seq.m - R_EAL g < R_EAL g by EXTREAL2:58; then
     seq.m < R_EAL g + R_EAL g by EXTREAL2:29; then
     seq.m < R_EAL(g+g) by Cor1;
     hence seq.m < R_EAL(2*g);
    end;
    g+g > 0 by A10,XREAL_1:36; then
    consider n2 be Nat such that
    A13: for m be Nat st n2 <= m holds R_EAL(2*g) <= seq.m by A1,Defconverge2;
    reconsider n1,n2 as Element of NAT by ORDINAL1:def 13;
    set m = max(n1,n2);
    n1 <= m & n2 <= m by SQUARE_1:46; then
    seq.m < R_EAL(2*g) & R_EAL(2*g) <= seq.m by A12,A13;
    hence contradiction;
   end;
   suppose
A14:g = 0;
    consider n1 be Nat such that
    A15: for m be Nat st n1 <= m holds |. seq.m-R_EAL g .| < R_EAL 1 by A8;
    consider n2 be Nat such that
    A16: for m be Nat st n2 <= m holds R_EAL 1 <= seq.m by A1,Defconverge2;
    reconsider n1,n2 as Element of NAT by ORDINAL1:def 13;
    set m = max(n1,n2);
A17:n1 <= m & n2 <= m by SQUARE_1:46; then
    |. seq.m-R_EAL g .| < R_EAL 1 by A15; then
    seq.m - R_EAL g < R_EAL 1 by EXTREAL2:58; then
    seq.m < R_EAL 1 + R_EAL g by EXTREAL2:29; then
    seq.m < R_EAL 1 by A14,SUPINF_2:18;
    hence contradiction by A16,A17;
   end;
   suppose
A18:g < 0;
    set g1 = -g;
    g1 > 0 by A18,XREAL_1:60; then
    consider n1 be Nat such that
A19: for m be Nat st n1 <= m holds |.seq.m-R_EAL g.| < R_EAL g1 by A8;
A20:now let m be Element of NAT;
     assume n1 <= m; then
     |.seq.m-R_EAL g.| < R_EAL g1 by A19; then
     seq.m - R_EAL g < R_EAL g1 by EXTREAL2:58; then
     seq.m < R_EAL g1 + R_EAL g by EXTREAL2:29; then
     seq.m < R_EAL(g1+g) by Cor1;
     hence seq.m < 0;
    end;
    consider n2 be Nat such that
A21: for m be Nat st n2 <= m holds 1 <= seq.m by A1,Defconverge2;
    reconsider n1,n2 as Element of NAT by ORDINAL1:def 13;
    set m = max(n1,n2);
    n1 <= m & n2 <= m by SQUARE_1:46; then
    seq.m < 0 & 1 <= seq.m by A20,A21;
    hence contradiction;
   end;
end;

theorem LM011c:
for seq be ExtREAL_sequence st seq is convergent_to_-infty holds
  not seq is convergent_to_+infty & not seq is convergent_to_finite_number
proof
   let seq be ExtREAL_sequence;
   assume
A1: seq is convergent_to_-infty;
   hereby assume seq is convergent_to_+infty; then
    consider n1 being Nat such that
A4:  for m be Nat st n1 <= m holds R_EAL 1 <= seq.m by Defconverge2;
    consider n2 being Nat such that
A6:  for m be Nat st n2 <= m holds seq.m <= R_EAL -1 by A1,Defconverge3;
    reconsider n1,n2 as Element of NAT by ORDINAL1:def 13;
    set m = max(n1,n2);
    m >= n1 & m >= n2 by SQUARE_1:46; then
    seq.m <= R_EAL -1 & R_EAL 1 <= seq.m by A4,A6;
    hence contradiction;
   end;
   assume seq is convergent_to_finite_number; then
   consider g being real number such that
A8: for p be real number st 0 < p
     ex n be Nat st
      for m be Nat st n<=m holds |.seq.m-R_EAL g.| < p by Defconverge1;
   reconsider g1 = -g as Real by XREAL_0:def 1;
   per cases;
   suppose
A10:g > 0; then
B1: g1 < -0 by XREAL_1:26;
    consider n1 be Nat such that
A11: for m be Nat st n1 <= m holds |.seq.m-R_EAL g.| < R_EAL(g) by A10,A8;
A12:now let m be Element of NAT;
     assume n1 <= m; then
     |.seq.m-R_EAL g.| < R_EAL g by A11; then
     -R_EAL g < seq.m - R_EAL g by EXTREAL2:58; then
     -R_EAL g + R_EAL g < seq.m by EXTREAL2:28;
     hence 0 < seq.m by EXTREAL1:9;
    end;
    consider n2 be Nat such that
A13: for m be Nat st n2 <= m holds seq.m <= R_EAL g1 by A1,B1,Defconverge3;
    reconsider n1,n2 as Element of NAT by ORDINAL1:def 13;
    set m = max(n1,n2);
    n1 <= m & n2 <= m by SQUARE_1:46; then
    seq.m <= R_EAL g1 & 0 < seq.m by A12,A13;
    hence contradiction by B1;
   end;
   suppose
A14:g = 0;
    consider n1 be Nat such that
A15: for m be Nat st n1 <= m holds |. seq.m-R_EAL g .| < R_EAL 1 by A8;
    consider n2 be Nat such that
A16: for m be Nat st n2 <= m holds seq.m <= R_EAL -1 by A1,Defconverge3;
    reconsider n1,n2 as Element of NAT by ORDINAL1:def 13;
    set m = max(n1,n2);
A17:n1 <= m & n2 <= m by SQUARE_1:46; then
    |. seq.m-R_EAL g .| < R_EAL 1 by A15; then
    -R_EAL 1 < seq.m - R_EAL g by EXTREAL2:58; then
    -R_EAL 1 + R_EAL g < seq.m by EXTREAL2:28; then
    -R_EAL 1 < seq.m by A14,SUPINF_2:18; then
    R_EAL -1 < seq.m by Cor1;
    hence contradiction by A16,A17;
   end;
   suppose
A18:g < 0; then
    g1 > 0 by XREAL_1:60; then
    consider n1 be Nat such that
A19: for m be Nat st n1 <= m holds |.seq.m-R_EAL g.| < R_EAL g1 by A8;
A20:now let m be Element of NAT;
     assume n1 <= m; then
     |.seq.m-R_EAL g.| < R_EAL g1 by A19; then
     -R_EAL g1 < seq.m - R_EAL g by EXTREAL2:58; then
     -R_EAL g1 + R_EAL g < seq.m by EXTREAL2:28; then
     R_EAL -g1 + R_EAL g < seq.m by Cor1; then
     R_EAL(-g1+g) < seq.m by Cor1;
     hence R_EAL(2*g) < seq.m;
    end;
    2*g < 0 by A18,XREAL_1:134; then
    consider n2 be Nat such that
A21: for m be Nat st n2 <= m holds seq.m <= R_EAL(2*g) by A1,Defconverge3;
    reconsider n1,n2 as Element of NAT by ORDINAL1:def 13;
    set m = max(n1,n2);
    n1 <= m & n2 <= m by SQUARE_1:46; then
    seq.m <= R_EAL(2*g) & R_EAL(2*g) < seq.m by A20,A21;
    hence contradiction;
   end;
end;

definition let seq be ExtREAL_sequence;
  attr seq is convergent means :Defconvergent:
    seq is convergent_to_finite_number or
    seq is convergent_to_+infty or
    seq is convergent_to_-infty;
end;

definition let seq be ExtREAL_sequence;
assume A1:seq is convergent;
func lim seq -> R_eal means :Deflim:
( ex g be real number st it = g &
  (for p be real number st 0<p
    ex n be Nat st
     for m be Nat st n<=m holds |.seq.m-it.| < p) &
   seq is convergent_to_finite_number )  or
( it=+infty & seq is convergent_to_+infty )  or
( it=-infty & seq is convergent_to_-infty );
existence
proof
   defpred P[R_eal] means
   ( ex g be real number st $1=g &
      (for p be real number st 0<p
        ex n be Nat st for m be Nat st n<=m holds |.seq.m-$1.| < p) &
      seq is convergent_to_finite_number ) or
   ( $1=+infty & seq is convergent_to_+infty ) or
   ( $1=-infty & seq is convergent_to_-infty );
   per cases by A1,Defconvergent;
   suppose
C1: seq is convergent_to_finite_number;
    ex g be real number st
     for p be real number st 0<p
      ex n be Nat st
       for m be Nat st n<=m holds |.seq.m-R_EAL g.| < p by C1,Defconverge1;
    hence ex s be R_eal st P[s] by C1;
   end;
   suppose seq is convergent_to_+infty or seq is convergent_to_-infty;
    hence ex s be R_eal st P[s];
   end;
end;
uniqueness
proof
   defpred P[R_eal] means
   ( ex g be real number st
      $1= g &
      (for p be real number st 0 < p
       ex n be Nat st
        for m be Nat st n<=m holds |.seq.m-$1.| < p) &
       seq is convergent_to_finite_number )
or ( $1=+infty & seq is convergent_to_+infty )
or ( $1=-infty & seq is convergent_to_-infty );
   given g1,g2 be R_eal such that
A2: P[g1] and
A3: P[g2] and
A4: g1<>g2;
   per cases by A1,Defconvergent;
   suppose
C2: seq is convergent_to_finite_number; then
    consider g being real number such that
C7:  g1 = g &
     (for p be real number st 0 < p
       ex n be Nat st
        for m be Nat st n <= m holds |.seq.m-g1.| < p) &
     seq is convergent_to_finite_number by LM011b,LM011c,A2;
    consider h being real number such that
C8:  g2 = h &
     (for p be real number st 0 < p
       ex n be Nat st
        for m be Nat st n <= m holds |.seq.m-g2.| < p) &
     seq is convergent_to_finite_number by C2,LM011b,LM011c,A3;
    g - h <> 0 by A4,C7,C8; then
    |.g-h.| > 0 by COMPLEX1:133; then
C10:|.g-h.|/2 > 0 by XREAL_1:141; then
    consider n1 being Nat such that
C11: for m be Nat st n1 <= m holds |.seq.m-g1.| < R_EAL(|.g-h.|/2) by C7;
    consider n2 being Nat such that
C12: for m be Nat st n2 <= m holds |.seq.m-g2.| < R_EAL(|.g-h.|/2) by C8,C10;
    reconsider n1,n2 as Element of NAT by ORDINAL1:def 13;
    set m = max(n1,n2);
    n1 <= m & n2 <= m by SQUARE_1:46; then
D2: |.seq.m-g1.| < R_EAL(|.g-h.|/2) & |.seq.m-g2.| < R_EAL(|.g-h.|/2)
      by C11,C12;
    reconsider g,h as Real by XREAL_0:def 1;
D6: -seq.m + g1 = -(seq.m - g1) by EXTREAL2:15; then
    |.-seq.m+g1.| < R_EAL(|.g-h.|/2) by D2,EXTREAL2:66; then
E1: -R_EAL(|.g-h.|/2) < -seq.m+g1 & -seq.m+g1 < R_EAL(|.g-h.|/2) &
    -R_EAL(|.g-h.|/2) < seq.m-g2 & seq.m-g2 < R_EAL(|.g-h.|/2)
        by D2,EXTREAL2:58; then
D8: -seq.m + g1 in REAL & seq.m-g2 in REAL by MEASURE5:16;
    reconsider w = seq.m - g2 as Real by E1,MEASURE5:16;
D1: |.g-h.|/2 in REAL by XREAL_0:def 1;
    |.w.| in REAL by XREAL_0:def 1; then
    |.seq.m-g2.| in REAL by EXTREAL2:49; then
    |.seq.m-g1.| + |.seq.m-g2.| < R_EAL(|.g-h.|/2) + |.seq.m-g2.| &
    R_EAL(|.g-h.|/2) + |.seq.m-g2.| < R_EAL(|.g-h.|/2) + R_EAL(|.g-h.|/2)
        by D2,D1,MEASURE6:12; then
C13:|.seq.m-g1.| + |.seq.m-g2.| < R_EAL(|.g-h.|/2) + R_EAL(|.g-h.|/2)
        by SUPINF_1:29;
D5: -seq.m + g1 <> +infty & -seq.m + g1 <> -infty &
    seq.m - g2 <> +infty & seq.m - g2 <> -infty &
    seq.m - g2 < +infty & -infty < seq.m - g2 by D8,SUPINF_1:31; then
D9: seq.m <> +infty & seq.m <> -infty by C8,EXTREAL2:25,26;
    |.g1-g2.| = |.g1 + R_EAL 0 - g2.| by SUPINF_2:18
             .= |.g1 + (seq.m + -seq.m) - g2.| by EXTREAL1:9
             .= |.-seq.m + g1 + seq.m - g2.| by C7,D9,EXTREAL1:8
             .= |.-seq.m + g1 +(seq.m - g2).| by C8,D5,EXTREAL1:11; then
    |.g1-g2.| <= |.-seq.m + g1.| + |.seq.m - g2.| by D5,EXTREAL2:61; then
C14:|.g1-g2.| <= |.seq.m-g1.| + |.seq.m-g2.| by D6,EXTREAL2:66;
C15:R_EAL(|.g-h.|/2) + R_EAL(|.g-h.|/2) = |.g-h.|/2 + |.g-h.|/2 by Cor1;
    g-h = g1-g2 by C7,C8,SUPINF_2:5;
    hence contradiction by C13,C14,C15,EXTREAL2:49;
   end;
   suppose seq is convergent_to_+infty or seq is convergent_to_-infty;
    hence contradiction by A2,A3,A4,LM011b,LM011c;
   end;
end;
end;

theorem LL9:
for seq be ExtREAL_sequence, r be real number st
 (for n be Nat holds seq.n = r) holds
   seq is convergent_to_finite_number & lim seq = r
proof
   let seq be ExtREAL_sequence;
   let r be real number;
   assume
A1: for n be Nat holds seq.n = r;
A2:now let p be real number;
    assume
B:   0 < p;
    now let m be Nat such that 1 <= m;
     seq.m = R_EAL r by A1; then
     seq.m - R_EAL r = 0 by EXTREAL1:9;
     hence |. seq.m - R_EAL r .| < p by B,EXTREAL2:53;
    end;
    hence ex n be Nat st for m be Nat st n<=m holds |.seq.m-R_EAL r.| < p;
   end;
   hence
A3: seq is convergent_to_finite_number by Defconverge1; then
   seq is convergent by Defconvergent;
   hence thesis by A2,A3,Deflim;
end;

theorem Cor8:
for F be FinSequence of ExtREAL st
(for n be Nat st n in dom F holds 0 <= F.n) holds
  0 <= Sum F
proof
   let F be FinSequence of ExtREAL;
   assume
A1:for n be Nat st n in dom F holds 0 <= F.n;
   consider sumf be Function of NAT,ExtREAL such that
A2: Sum F = sumf.(len F) & sumf.0 = 0 &
    for n be Element of NAT st n < len F holds sumf.(n+1) = sumf.n + F.(n+1)
     by CONVFUN1:def 5;
   defpred P[Nat] means $1 <= len F implies 0 <= sumf.($1);
A3:P[0] by A2;
A4:for n be Nat st P[n] holds P[n+1]
   proof
   let n be Nat;
    assume A5:P[n];
    assume
A6:  n+1 <= len F;
    reconsider n as Element of NAT by ORDINAL1:def 13;
    n < len F by A6,NAT_1:38; then
A8: sumf.(n+1) = sumf.n + F.(n+1) by A2;
    1 <= n+1 by NAT_1:29; then
    n+1 in Seg len F by A6; then
    n+1 in dom F by FINSEQ_1:def 3; then
    0 <= sumf.n & 0 <= F.(n+1) by A5,A6,A1,NAT_1:38;
    hence thesis by A8,EXTREAL2:7;
   end;
   for n be Nat holds P[n] from NAT_1:sch 2(A3,A4);
   hence thesis by A2;
end;

theorem Cor12a:
for L be ExtREAL_sequence st
  (for n,m be Nat st n <=m holds L.n <= L.m)
 holds L is convergent & lim L = sup rng L
proof
   let L be ExtREAL_sequence;
   assume 
A0:for n,m be Nat st n <=m holds L.n <= L.m;
A6:now let n be Nat;
P1: sup rng L is majorant of rng L by SUPINF_1:def 16;
    reconsider n1=n as Element of NAT by ORDINAL1:def 13;

    dom L = NAT by FUNCT_2:def 1; then
    L.n1 in rng L by FUNCT_1:def 5;
    hence L.n <= sup rng L by P1,SUPINF_1:def 9;
   end;
   per cases;
   suppose
EA1:not ex k0 be Nat st -infty < L.k0;
T2: now let k be Nat;
     L.k <= -infty by EA1;
     hence L.k=-infty by SUPINF_1:23;
    end;
    now let K be real number such that K < 0;
     now let n be Nat such that 0 <= n;
      L.n=-infty by T2;
      hence L.n <= R_EAL K by SUPINF_1:21;
     end;
     hence ex N0 be Nat st for n be Nat st N0<=n holds L.n <= K;
    end; then
D4: L is convergent_to_-infty by Defconverge3; then
D5: L is convergent by Defconvergent;
    now let x be R_eal;
     assume x in rng L; then
     consider n be set such that
E1:  n in dom L and
E2:  x=L.n by FUNCT_1:def 5;
     n is Nat by E1,ORDINAL1:def 13;
     hence x <= -infty by T2,E2;
    end; then
PP1:-infty is majorant of rng L by SUPINF_1:def 9;
    for y be R_eal st y is majorant of rng L holds -infty <= y
      by SUPINF_1:21; then
    -infty =sup rng L by SUPINF_1:def 16,PP1;
    hence L is convergent & lim L = sup rng L by D4,D5,Deflim;
   end;
   suppose ex k0 be Nat st -infty < L.k0; then
    consider k0 be Nat such that
D3d: -infty < L.k0;
    reconsider k0 as Element of NAT by ORDINAL1:def 13;
    per cases;
    suppose ex K be real number st for n be Nat holds L.n < K; then
     consider K be real number such that
AS2:  for n be Nat holds L.n < K;
AS3: sup rng L <> -infty by D3d,A6;
     now let x be R_eal;
      assume x in rng L; then
      consider z be set such that
E1:   z in dom L and
E2:   x=L.z by FUNCT_1:def 5; 
      z is Nat by E1,ORDINAL1:def 13;
      hence x <= R_EAL K by AS2,E2;
     end; then
     R_EAL K is majorant of rng L by SUPINF_1:def 9; then
P2:  sup rng L <= R_EAL K by SUPINF_1:def 16;
     R_EAL K is Real by XREAL_0:def 1; then
AS4: sup rng L <>+infty by P2,SUPINF_1:31;
CA12:for p be real number st 0<p
      ex N0 be Nat st
       for m be Nat st N0<=m holds |. L.m - sup rng L .| < p
     proof
      let p be real number;
      assume
CAS0:  0 < p;
      reconsider e=p as Real by XREAL_0:def 1;
      sup rng L is Real by AS3,AS4,EXTREAL1:1; then
      consider y being R_eal such that
K4:    y in rng L & sup rng L - R_EAL e < y by CAS0,MEASURE6:33;
      consider x be set such that
K5:    x in dom L & y=L.x by K4,FUNCT_1:def 5;
      reconsider N1=x as Element of NAT by K5;
      set N0=max(N1,k0);
      take N0;
      hereby let n be Nat;
       assume
K8:     N0 <= n;
       N1<=N0 by SQUARE_1:46; then
       N1 <=n by K8,XREAL_1:2; then
       L.N1 <= L.n by A0; then
K10:   sup rng L - R_EAL e < L.n by K5,K4,XXREAL_0:2;
M15:   L.n <= sup rng L by A6;
       sup rng L + R_EAL 0 <= sup rng L + R_EAL e by SUPINF_2:14,CAS0; then
M17:   sup rng L <= sup rng L + R_EAL e by SUPINF_2:18;
       now assume
M16:    sup rng L = sup rng L + R_EAL e;
        R_EAL e + sup rng L + -sup rng L
          =R_EAL e + (sup rng L + -sup rng L) by AS3,EXTREAL1:8,AS4
         .=R_EAL e + R_EAL 0 by EXTREAL1:9
         .=R_EAL e by SUPINF_2:18;
        hence contradiction by CAS0,M16,EXTREAL1:9;
       end; then
       sup rng L < sup rng L + R_EAL e by M17,SUPINF_1:22; then
K15:   L.n < sup rng L + R_EAL e by M15,XXREAL_0:2;
       sup rng L < L.n + R_EAL e by K10,EXTREAL2:29; then
       sup rng L - L.n < R_EAL e by EXTREAL2:30; then
       -R_EAL e < - (sup rng L - L.n) by SUPINF_2:17; then
K18:   -R_EAL e < L.n -sup rng L by EXTREAL2:15;
       L.n -sup rng L < R_EAL e by K15,EXTREAL2:30;
       hence |.L.n-sup rng L .| < p by K18,EXTREAL2:59;
      end;
     end;
     reconsider h=sup rng L as real number by AS3,AS4,EXTREAL1:1;
CA15:R_EAL h = sup rng L; then
D4:  L is convergent_to_finite_number by Defconverge1,CA12;
     hence L is convergent by Defconvergent;
     hence lim L = sup rng L by CA12,CA15,D4,Deflim;
    end;
    suppose
CA21:not (ex K be real number st 0 < K & for n be Nat holds L.n < K);
     now let K be real number;
      assume 0 < K; then
      consider N0 be Nat such that
CA24:  R_EAL K <= L.N0 by CA21;
      now let n be Nat;
       assume N0 <=n; then
       L.N0 <= L.n by A0;
       hence R_EAL K <= L.n by CA24,SUPINF_1:29;
      end;
      hence ex N0 be Nat st for n be Nat st N0<=n holds K <= L.n;
     end; then
D4:  L is convergent_to_+infty by Defconverge2;
     hence 
D6:   L is convergent by Defconvergent;
     now assume
ASX:  sup rng L <> +infty;
      L.k0 <= sup(rng L) by A6; then
      reconsider h=sup rng L as Real by D3d,ASX,EXTREAL1:1;
      set K=max(0,h);
      0 <=K by SQUARE_1:46; then
AP6:  0 < K + 1 by XREAL_1:31;
      h <=K by SQUARE_1:46; then
AP8:  h+0 < K+1 by XREAL_1:10;
      consider N0 be Nat such that
AP8a:  R_EAL(K+1) <= L.N0 by AP6,CA21;
      sup rng L < L.N0 by XXREAL_0:2,AP8,AP8a;
      hence contradiction by A6;
     end;
     hence lim L = sup rng L by D6,D4,Deflim;
    end;
   end;
end;

theorem Cor12b:
 for L,G be ExtREAL_sequence st
  (for n be Nat holds L.n <= G.n)
 holds sup rng L <= sup rng G 
proof
   let L,G be ExtREAL_sequence;
   assume
A0:for n be Nat holds L.n <= G.n;
T1:now let n be Element of NAT;
P1: sup rng G is majorant of rng G by SUPINF_1:def 16;
    dom G = NAT by FUNCT_2:def 1; then
    G.n in rng G by FUNCT_1:def 5; then
P4: G.n <= sup rng G by P1,SUPINF_1:def 9;
    L.n <= G.n by A0;
    hence L.n <= sup rng G by P4,SUPINF_1:29;
   end; 
   now let x be R_eal;
    assume x in rng L; then
    ex z be set st z in dom L & x=L.z by FUNCT_1:def 5;
    hence x <= sup rng G by T1;  
   end; then
   sup rng G is majorant of rng L by SUPINF_1:def 9;
   hence thesis by SUPINF_1:def 16;
end;
  
theorem Cor12c:
for L be ExtREAL_sequence holds
 for n be Nat holds L.n <= sup rng L
proof
   let L be ExtREAL_sequence;
   let n be Nat;
P1:sup rng L is majorant of rng L by SUPINF_1:def 16;
reconsider n as Element of NAT by ORDINAL1:def 13;
   dom L = NAT by FUNCT_2:def 1; then
   L.n in rng L by FUNCT_1:def 5;
   hence thesis by P1,SUPINF_1:def 9;
end; 
 
theorem Cor12d:
for L be ExtREAL_sequence, K be R_eal st
  (for n be Nat holds L.n <= K) holds sup rng L <= K 
proof
   let L be ExtREAL_sequence, K be R_eal;
   assume 
T1: for n be Nat holds L.n <= K;
   now let x be R_eal;
    assume x in rng L; then
    consider z be set such that
E1: z in dom L and
E2: x=L.z by FUNCT_1:def 5;
    z is Nat by E1,ORDINAL1:def 13;
    hence x <= K by T1,E2;  
   end; then
   K is majorant of rng L by SUPINF_1:def 9;
   hence thesis by SUPINF_1:def 16;
end;

theorem
for L be ExtREAL_sequence, K be R_eal st
  K <> +infty & (for n be Nat holds L.n <= K) holds
   sup rng L < +infty 
proof
   let L be ExtREAL_sequence, K be R_eal;
   assume that
T0: K <> +infty and   
T1: for n be Nat holds L.n <= K;
   now let x be R_eal;
    assume x in rng L; then
    consider z be set such that
E1: z in dom L and
E2: x=L.z by FUNCT_1:def 5;
    z is Nat by E1,ORDINAL1:def 13;
    hence x <= K by T1,E2;  
   end; then
   K is majorant of rng L by SUPINF_1:def 9; then
P2:sup rng L <= K by SUPINF_1:def 16;
   K < +infty by T0,SUPINF_1:24;
   hence thesis by P2,SUPINF_1:29;
end;

theorem Cor13:
for L be ExtREAL_sequence st L is without-infty holds
   sup rng L <> +infty
   iff
   ex K be real number st 0 <K & for n be Nat holds L.n <= K 
proof
   let L be ExtREAL_sequence;
   assume
A1:L is without-infty;   
A2:dom L = NAT by FUNCT_2:def 1; then
   L.1 in rng L by FUNCT_1:12; then
A3:-infty < L.1 & L.1 <= sup rng L by A1,Def0003,SUPINF_1:76;
A4:now assume
    sup rng L <> +infty; then
    not sup rng L in {-infty,+infty} by A3,TARSKI:def 2; then
    sup rng L in REAL by XXREAL_0:def 4,XBOOLE_0:def 2; then
    reconsider S = sup rng L as real number by XREAL_0:def 1;
    set K = max(S,1);
 B3:S <= K & 1 <= K by SQUARE_1:46;
 now let n be Nat;
   n in NAT by ORDINAL1:def 13;
   then L.n in rng L by A2,FUNCT_1:12; then
     L.n <= sup rng L by SUPINF_1:76;
     hence L.n <= R_EAL K by B3,SUPINF_1:29;
    end;
    hence ex K be real number st
      0 <K & for n be Nat holds L.n <= R_EAL K by B3;
   end;
   now assume
    ex K be real number st 0 <K & for n be Nat holds L.n <= K; then
    consider K being real number such that
 C1: 0 < K & for n be Nat holds L.n <= K;
    now let w be R_eal;
     assume w in rng L; then
     consider v be set such that
E1:  v in dom L and
E2:  w = L.v by FUNCT_1:def 5;
     v is Nat by E1,ORDINAL1:def 13;
     hence w <= R_EAL K by C1,E2;
    end; then
    R_EAL K is majorant of rng L by SUPINF_1:def 9; then
 C3:sup rng L <= R_EAL K by SUPINF_1:def 16;
    K in REAL by XREAL_0:def 1;
    hence sup rng L <> +infty by C3,SUPINF_1:31;
   end;
   hence thesis by A4;
end;

theorem Cor14:
for L be ExtREAL_sequence,c be R_eal st
 (for n be Nat holds L.n=c) holds
   L is convergent & lim L = c & lim L = sup rng L
proof
   let L be ExtREAL_sequence;
   let c be R_eal;
   assume
A1: for n be Nat holds L.n = c;
A2:c in REAL or c in {-infty,+infty} by XXREAL_0:def 4,XBOOLE_0:def 2;
   now let v be R_eal;
    assume v in rng L; then
    consider n be set such that
E1: n in dom L and
E2: v = L.n by FUNCT_1:def 5;
    n is Nat by E1,ORDINAL1:def 13;
    hence v <= c by A1,E2;
   end; then
P2:c is majorant of rng L by SUPINF_1:def 9;
   1 in NAT & dom L = NAT by FUNCT_2:def 1; then
A3:L.1 = c & L.1 in rng L by A1,FUNCT_1:12;
   per cases by A2,TARSKI:def 2;
   suppose c in REAL; then
    reconsider rc = c as real number by XREAL_0:def 1;
B1: now let p be real number;
     assume
B11:  0 < p;
     now let m be Nat such that 0 <= m;
      L.m - R_EAL rc = L.m + -L.m by A1; then
      L.m - R_EAL rc = 0 by EXTREAL1:9;
      hence |. L.m - R_EAL rc .| < p by B11,EXTREAL2:53;
     end;
     hence ex n be Nat st for m be Nat st n <= m holds |. L.m-R_EAL rc .| < p;
    end; then
B2: L is convergent_to_finite_number by Defconverge1;
    hence L is convergent by Defconvergent;
    hence lim L = c by B1,B2,Deflim;
    hence lim L = sup rng L by A3,P2,SUPINF_1:80;    
   end;
   suppose C1:c = -infty;
    for p be real number st p < 0
     ex n be Nat st for m be Nat st n<=m holds L.m <= p
    proof
     let p be real number such that p < 0;
B31: p in REAL by XREAL_0:def 1;
     take 0;
     now let m be Nat such that 0 <= m;
      L.m = -infty by A1,C1;
      hence L.m < p by B31,SUPINF_1:31;
     end;
     hence thesis;
    end; then
C2: L is convergent_to_-infty by Defconverge3;
    hence L is convergent by Defconvergent;
    hence lim(L) = c by C1,C2,Deflim;
    hence lim L = sup rng L by A3,P2,SUPINF_1:80;    
   end;
   suppose D1:c = +infty;
    for p be real number st 0 < p
     ex n be Nat st for m be Nat st n <= m holds p <= L.m
    proof
     let p be real number such that 0 < p;
C31: p in REAL by XREAL_0:def 1;
     take 0;
     now let m be Nat such that 0 <= m;
      L.m = +infty by A1,D1;
      hence p < L.m by C31,SUPINF_1:31;
     end;
     hence thesis;
    end; then
D2: L is convergent_to_+infty by Defconverge2;
    hence L is convergent by Defconvergent;
    hence lim L = c by D1,D2,Deflim;
    hence lim L = sup rng L by A3,P2,SUPINF_1:80;    
   end;
end;

CorB:
for J be ExtREAL_sequence st J is without-infty holds
  sup rng J in REAL or sup rng J = +infty
proof
   let J be ExtREAL_sequence;
   assume 
A1:J is without-infty;
   dom J = NAT by FUNCT_2:def 1; then
   J.1 in rng J & -infty < J.1 by A1,Def0003,FUNCT_1:12; then
   not -infty is majorant of rng J by SUPINF_1:def 9; then
   sup rng J <> -infty by SUPINF_1:def 16;
   hence sup rng J in REAL or sup rng J = +infty by XXREAL_0:14;
end;

theorem Cor16:
for J,K,L be ExtREAL_sequence st
 (for n,m be Nat st n <=m holds J.n <= J.m) &
 (for n,m be Nat st n <=m holds K.n <= K.m) &
 J is without-infty & K is without-infty &
 (for n be Nat holds J.n+K.n =L.n ) holds
   L is convergent & lim L= sup rng L &
   lim L = lim J + lim K &
   sup rng L = sup rng K + sup rng J
proof
   let J,K,L be ExtREAL_sequence;
   assume that
A1: for n,m be Nat st n <=m holds J.n <= J.m and
A2: for n,m be Nat st n <=m holds K.n <= K.m and
A3: J is without-infty & K is without-infty and
A5: for n be Nat holds J.n+K.n =L.n;
B1:dom J = NAT & dom K = NAT & dom L = NAT by FUNCT_2:def 1;
P1:now per cases by A3,CorB;
    suppose
C1:  sup rng J in REAL; then
     reconsider SJ = sup rng J as real number by XREAL_0:def 1;
     per cases by A3,CorB;
     suppose
D1:   sup rng K in REAL; then
      reconsider SK = sup rng K as real number by XREAL_0:def 1;
 E9:  R_EAL(SJ+SK) = R_EAL SJ + R_EAL SK by Cor1;
 E0:  now let p be real number;
       assume 0 < p; then
 E4:   0 < p/2 by XREAL_1:217;
       consider SJ' be R_eal such that
 E5:    SJ' in rng J & sup rng J - R_EAL(p/2) < SJ' by C1,E4,MEASURE6:33;
       consider nj be set such that
 E6:    nj in dom J & SJ' = J.nj by E5,FUNCT_1:def 5;
       reconsider nj as Element of NAT by E6;
       consider SK' be R_eal such that
 E7:    SK' in rng K & sup rng K - R_EAL(p/2) < SK' by D1,E4,MEASURE6:33;
       consider nk be set such that
 E8:    nk in dom K & SK' = K.nk by E7,FUNCT_1:def 5;
       reconsider nk as Element of NAT by E8;
       p/2 + p/2 = p; then
 E10:  R_EAL p = R_EAL(p/2) + R_EAL(p/2) by Cor1;
       reconsider n = max(nj,nk) as Nat;
       take n;
       hereby let m be Nat; assume
    F0: n <= m;
        reconsider m1=m as Element of NAT by ORDINAL1:def 13;
        R_EAL SJ in REAL & R_EAL SK in REAL &
        R_EAL(SJ+SK) in REAL by XREAL_0:def 1; then
    F1: R_EAL SJ < +infty & -infty < R_EAL SJ &
        R_EAL SK < +infty & -infty < R_EAL SK &
        R_EAL(SJ+SK) < +infty & -infty < R_EAL(SJ+SK) by SUPINF_1:31;
        J.m1 in rng J & K.m1 in rng K by B1,FUNCT_1:12; then
    F5: J.m <= R_EAL SJ & K.m <= R_EAL SK by SUPINF_1:76; then
    F3: -infty < J.m & J.m < +infty &
        -infty < K.m & K.m < +infty by A3,F1,Def0003,SUPINF_1:29; then
    F2: -infty <> J.m + K.m & J.m + K.m <> +infty by SUPINF_2:8,9;
        L.m = J.m + K.m by A5; then
        L.m <= R_EAL(SJ+SK) & L.m < +infty
          by E9,F2,F5,F1,SUPINF_1:24,SUPINF_2:14; then
    G2: 0 <= R_EAL(SJ+SK) - L.m by F1,EXTREAL1:40;
    G3: -(L.m - R_EAL(SJ+SK)) = R_EAL(SJ+SK) - L.m by EXTREAL2:15; then
        L.m - R_EAL(SJ+SK) <= 0 by G2,EXTREAL1:25; then
    G3a:|. L.m - R_EAL(SJ+SK) .| = R_EAL(SJ+SK) - L.m by G3,EXTREAL2:55;
    F9: R_EAL SJ - J.m + (R_EAL SK - K.m)
         = R_EAL SJ - J.m + R_EAL SK - K.m by F3,EXTREAL1:11
        .= R_EAL SK + R_EAL SJ - J.m - K.m by EXTREAL1:11
        .= R_EAL(SK+SJ) - (J.m + K.m) by E9,F5,F1,EXTREAL2:40
        .= R_EAL(SK+SJ) - L.m by A5;
        R_EAL SJ < R_EAL(p/2) + SJ' & R_EAL(p/2) <> -infty &
        R_EAL SK < R_EAL(p/2) + SK' & R_EAL(p/2) <> -infty
          by E5,E7,EXTREAL2:29; then
    F11:R_EAL SJ - SJ' < R_EAL(p/2) & R_EAL SK - SK' < R_EAL(p/2)
          by EXTREAL2:30;
        nk <= n by SQUARE_1:46; then
        nk <= m by F0,XREAL_1:2; then
        K.nk <= K.m by A2; then
        -infty < SK' & SK' < +infty by A3,E8,F3,Def0003,SUPINF_1:29; then
        reconsider s0 = SK' as Real by XXREAL_0:14;
        reconsider s1 = R_EAL SK as Real by XREAL_0:def 1;
        R_EAL SK - SK' = s1 - s0 by SUPINF_2:5; then
     PP:(R_EAL SJ - SJ') + (R_EAL SK - SK') < R_EAL(p/2) + (R_EAL SK - SK')
          by F11,MEASURE6:12;
        R_EAL(p/2) in REAL by XREAL_0:def 1; then
        R_EAL(p/2) + (R_EAL SK - SK') < R_EAL(p/2) + R_EAL(p/2)
          by F11,MEASURE6:12; then
    F13:(R_EAL SJ - SJ') + (R_EAL SK - SK') < R_EAL(p/2) + R_EAL(p/2)
          by PP,SUPINF_1:29;
        -infty < SJ' & -infty < SK' by A3,E6,E8,Def0003; then
        R_EAL SJ - SJ' <>+infty & R_EAL SK - SK' <>+infty by SUPINF_2:10; then
     F8:R_EAL SJ - SJ' < +infty & R_EAL SK - SK' < +infty by SUPINF_1:24;
        nj <= n & nk <=n by SQUARE_1:46; then
        nj <= m & nk <= m by F0,XREAL_1:2; then
        SJ' <= J.m & SK' <= K.m by A1,A2,E6,E8; then
        R_EAL SJ - J.m <= R_EAL SJ - SJ' &
        R_EAL SK - K.m <= R_EAL SK - SK' by SUPINF_2:15; then
        (R_EAL SJ - J.m) + (R_EAL SK - K.m)
          <= (R_EAL SJ - SJ') + (R_EAL SK - SK') by F8,SUPINF_2:14;
        hence |. L.m - R_EAL(SJ+SK) .| < p by G3a,E10,F9,F13,SUPINF_1:29;
       end;
      end; then
   PP:L is convergent_to_finite_number by Defconverge1;
      hence L is convergent by Defconvergent;
      hence lim L = sup rng J + sup rng K by E9,E0,PP,Deflim;
     end;
     suppose
D2:   sup rng K = +infty;
      for p be real number st 0 < p
       ex n be Nat st for m be Nat st n<=m holds p <= L.m
      proof
       let p be real number;
       assume 0 < p; then
       0 < p/2 by XREAL_1:217; then
       consider j be R_eal such that
 E5:    j in rng J & sup rng J - R_EAL(p/2) < j by C1,MEASURE6:33;
       consider n1 being set such that
 E6:    n1 in dom J & j = J.n1 by E5,FUNCT_1:def 5;
       reconsider n1 as Element of NAT by E6;
       reconsider supj = sup rng J as Real by C1;
       reconsider p'2 = p/2, p' = p as Real by XREAL_0:def 1;
 E9:   supj - p'2 = sup rng J - R_EAL(p/2) by SUPINF_2:5; then
 E9b:  p' - (supj - p'2) = R_EAL p - (sup rng J - R_EAL(p/2)) 
         by SUPINF_2:5; then
       R_EAL p - (sup rng J - R_EAL(p/2)) < sup rng K by D2,SUPINF_1:31; then
       consider k being R_eal such that
 E10:   k in rng K & R_EAL p - (sup rng J - R_EAL(p/2)) < k by HAHNBAN:8;
       consider n2 being set such that
 E11:   n2 in dom K & k = K.n2 by E10,FUNCT_1:def 5;
       reconsider n2 as Element of NAT by E11;
       set n = max(n1,n2);
       n1 <= n & n2 <= n by SQUARE_1:46; then
       J.n1 <= J.n & K.n2 <= K.n by A1,A2; then
 E12:  sup rng J - R_EAL(p/2) < J.n &
       R_EAL p - (sup rng J - R_EAL(p/2)) < K.n by E5,E6,E10,E11,SUPINF_1:29;
 E13:  -infty < R_EAL p - (sup rng J - R_EAL(p/2)) by E9b,SUPINF_1:31;
 E14:  sup rng J is majorant of rng J by SUPINF_1:def 16;
       J.n in rng J by B1,FUNCT_1:12; then
 E15:  J.n <= sup rng J & sup rng J < +infty
         by E14,C1,SUPINF_1:def 9,31; then
 E16:  -infty < J.n & J.n < +infty by A3,Def0003,SUPINF_1:29;
       p' = (p' - (supj - p'2)) + (supj - p'2); then
       R_EAL p - (sup rng J - R_EAL(p/2)) + (sup rng J - R_EAL(p/2)) = p'
        by E9,E9b,SUPINF_2:1; then
 E17:  R_EAL p < J.n + K.n by E13,E12,CorD;
       now let m be Nat;
        assume n <= m; then
  F1:   J.n <= J.m & K.n <= K.m by A1,A2;
        reconsider m1=m as Element of NAT by ORDINAL1:def 13;
        J.m1 in rng J by B1,FUNCT_1:12; then
        J.m <= sup rng J by E14,SUPINF_1:def 9; then
        J.n + K.n <= J.m + K.m by E16,F1,E15,SUPINF_2:14; then
        J.n + K.n <= L.m by A5;
        hence p <= L.m by E17,SUPINF_1:29;
       end;
       hence ex n be Nat st for m be Nat st n <= m holds p <= L.m;
      end; then
   PP:L is convergent_to_+infty by Defconverge2;
      hence L is convergent by Defconvergent; then
   PQ:lim L = +infty by PP,Deflim;
      -infty < sup rng J by C1,SUPINF_1:31;
      hence lim L = sup rng J + sup rng K by PQ,D2,SUPINF_2:def 2;
     end;
    end;
    suppose
C2:  sup rng J = +infty;
     now let p be real number;
      assume 0 < p; then
 E2:  0 < p/2 by XREAL_1:217;
      per cases by A3,CorB;
      suppose
  D1:  sup rng K in REAL; then
       consider k be R_eal such that
  D2:   k in rng K & sup rng K - R_EAL(p/2) < k by E2,MEASURE6:33;
       consider n1 being set such that
  D3:   n1 in dom K & k = K.n1 by D2,FUNCT_1:def 5;
       reconsider n1 as Element of NAT by D3;
       reconsider supk = sup rng K as Real by D1;
       reconsider p'2 = p/2, p' = p as Real by XREAL_0:def 1;
  D6:  supk - p'2 = sup rng K - R_EAL(p/2) by SUPINF_2:5; then
  D7:  p' - (supk - p'2) = R_EAL p - (sup rng K - R_EAL(p/2)) 
         by SUPINF_2:5; then
       R_EAL p - (sup rng K - R_EAL(p/2)) < sup rng J by C2,SUPINF_1:31; then
       consider j being R_eal such that
  D9:   j in rng J & R_EAL p - (sup rng K - R_EAL(p/2)) < j by HAHNBAN:8;
       consider n2 being set such that
  D10:  n2 in dom J & j = J.n2 by D9,FUNCT_1:def 5;
       reconsider n2 as Element of NAT by D10;
       set n = max(n1,n2);
       n1 <= n & n2 <= n by SQUARE_1:46; then
       J.n2 <= J.n & K.n1 <= K.n by A1,A2; then
 E12:  sup rng K - R_EAL(p/2) < K.n &
       R_EAL p - (sup rng K - R_EAL(p/2)) < J.n by D2,D3,D9,D10,SUPINF_1:29;
 E13:  -infty < R_EAL p - (sup rng K - R_EAL(p/2)) by D7,SUPINF_1:31;
 E14:  sup rng K is majorant of rng K by SUPINF_1:def 16;
       K.n in rng K by B1,FUNCT_1:12; then
 E15:  K.n <= sup rng K & sup rng K < +infty
         by E14,D1,SUPINF_1:def 9,SUPINF_1:31; then
 E16:  -infty < K.n & K.n < +infty by A3,Def0003,SUPINF_1:29;
       p' = p' - (supk - p'2) + (supk - p'2); then
       R_EAL p - (sup rng K - R_EAL(p/2)) + (sup rng K - R_EAL(p/2)) = p'
        by D6,D7,SUPINF_2:1; then
 E17:  R_EAL p < J.n + K.n by E13,E12,CorD;
       now let m be Nat;
        assume n <= m; then
  F1:   J.n <= J.m & K.n <= K.m by A1,A2;
        reconsider m1=m as Element of NAT by ORDINAL1:def 13;
        K.m1 in rng K by B1,FUNCT_1:12; then
        K.m <= sup rng K by E14,SUPINF_1:def 9; then
        J.n + K.n <= J.m + K.m by E16,F1,E15,SUPINF_2:14; then
        J.n + K.n <= L.m by A5;
        hence p <= L.m by E17,SUPINF_1:29;
       end;
       hence ex n be Nat st for m be Nat st n <= m holds p <= L.m;
      end;
      suppose
       sup rng K = +infty; then
       consider n1 be Nat such that
  G1:   R_EAL(p/2) < K.n1 by E2,A3,Cor13;
       consider n2 be Nat such that
  G2:   R_EAL(p/2) < J.n2 by E2,A3,C2,Cor13;
        reconsider n1,n2 as Element of NAT by ORDINAL1:def 13;
       set n = max(n1,n2);
       n1 <= n & n2 <= n by SQUARE_1:46; then
       J.n2 <= J.n & K.n1 <= K.n by A1,A2; then
  G3:  R_EAL(p/2) < J.n & R_EAL(p/2) < K.n by G1,G2,SUPINF_1:29;
       p/2 in REAL by XREAL_0:def 1; then
       -infty < R_EAL(p/2) by SUPINF_1:31; then
  G4:  R_EAL(p/2) + R_EAL(p/2) < J.n + K.n by G3,CorD;
       p/2 + p/2 = p; then
       R_EAL p < J.n + K.n by G4,Cor1; then
  G5:  R_EAL p < L.n by A5;
       now let m be Nat;
        assume n <= m; then
  F1:   J.n <= J.m & K.n <= K.m by A1,A2;
        -infty < J.n & -infty < K.n by A3,Def0003; then
        J.n + K.n <= J.m + K.m by F1,SUPINF_2:14; then
        J.n + K.n <= L.m by A5; then
        L.n <= L.m by A5;
        hence p <= L.m by G5,SUPINF_1:29;
       end;
       hence ex n be Nat st for m be Nat st n <= m holds p <= L.m;
      end;
     end; then
  PP:L is convergent_to_+infty by Defconverge2;
     hence L is convergent by Defconvergent; then
  PQ:lim L = +infty by PP,Deflim;
     K.0 in rng K by B1,FUNCT_1:12; then
     -infty < K.0 & K.0 <= sup rng K by A3,Def0003,SUPINF_1:76;
     hence lim L = sup rng J + sup rng K by PQ,C2,SUPINF_2:def 2;
    end;
   end;
   hence L is convergent;
P2:now let n,m be Nat;
    assume n <= m; then
 G1:J.n <= J.m & K.n <= K.m by A1,A2;
    -infty < J.n & -infty < J.m & 
    -infty < K.n & -infty < K.m by A3,Def0003; then
    J.n + K.n <= J.m + K.m by G1,SUPINF_2:14; then
    L.n <= J.m + K.m by A5;
    hence L.n <= L.m by A5;
   end; 
   hence lim L= sup rng L by Cor12a;
   lim J = sup rng J & lim K = sup rng K by A1,A2,Cor12a;
   hence lim L = lim J + lim K &
   sup rng L= sup rng K + sup rng J by P1,P2,Cor12a;
end;

theorem Cor17a:
for L,K be ExtREAL_sequence,c be Real st
  0 <= c & L is without-infty & 
  (for n be Nat holds K.n = R_EAL c * L.n ) holds
    sup rng K = R_EAL c * sup rng L & K is without-infty
proof
   let L,K be ExtREAL_sequence;
   let c be Real;
   assume that
A1: 0 <= c and
A2: L is without-infty and
A3: for n be Nat holds K.n = R_EAL c * L.n;
A4b:-infty < R_EAL c & R_EAL c < +infty by SUPINF_1:31;
   now 
   per cases by A2,CorB;
    suppose
B2:  sup rng L in REAL; then
B2a: -infty < sup rng L & sup rng L < +infty by SUPINF_1:31;
     now let x be R_eal;
      assume x in rng K; then
      consider m be set such that
B4:    m in dom K & x = K.m by FUNCT_1:def 5;
B5:   dom K = NAT & dom L = NAT by FUNCT_2:def 1;
      reconsider m as Element of NAT by B4;
B6:   x = R_EAL c * L.m by B4,A3;
B7:   sup rng L is majorant of rng L by SUPINF_1:def 16;
      L.m in rng L by B5,FUNCT_1:12; then
      L.m <= sup rng L by B7,SUPINF_1:def 9;
      hence x <= R_EAL c*sup rng L by B6,A1,EXTREAL1:42;
     end; then
B3:  R_EAL c * sup rng L is majorant of rng K by SUPINF_1:def 9;
     now let y be R_eal;
      assume
C1:   y is majorant of rng K;
C2:   dom L = NAT & dom K = NAT by FUNCT_2:def 1;
      per cases;
      suppose
   D1: R_EAL c = 0; then
   D2: R_EAL c * sup rng L = 0 by EXTREAL1:22;
       K.1 in rng K by C2,FUNCT_1:12; then
       K.1 = R_EAL c * L.1 & K.1 <= y by A3,C1,SUPINF_1:def 9;
       hence R_EAL c * sup rng L <= y by D2,D1,EXTREAL1:22;
      end;
      suppose
   D1: R_EAL c <> 0;
       now let x be R_eal;
        assume x in rng L; then
        consider n be set such that
     E1: n in dom L & x = L.n by FUNCT_1:def 5;
        reconsider n as Element of NAT by E1;
        K.n = R_EAL c * L.n & K.n in rng K by A3,C2,FUNCT_1:12; then
        R_EAL c * L.n <= y by C1,SUPINF_1:def 9; then
        R_EAL c * L.n / R_EAL c <= y/R_EAL c by D1,A1,EXTREAL1:48;
        hence x <= y / R_EAL c by E1,A4b,D1,EXTREAL2:19;
       end; then
       y/R_EAL c is majorant of rng L by SUPINF_1:def 9; then
   D2a:sup rng L <= y/R_EAL c by SUPINF_1:def 16;
   D3: now assume
     F1:y = -infty; 
        K.1 in rng K by C2,FUNCT_1:12; then
        K.1 <= -infty by F1,C1,SUPINF_1:def 9; then
        K.1 = -infty by SUPINF_1:23; then
     D2:R_EAL c * L.1 = -infty by A3;
        L.1 in rng L by C2,FUNCT_1:12; then
        L.1 <= sup rng L by SUPINF_1:76; then
        -infty < L.1 & L.1 < +infty by A2,Def0003,B2a,SUPINF_1:29;
        hence contradiction by D2,EXTREAL1:28;
       end;
       per cases by D3,XXREAL_0:14;
       suppose y = +infty;
        hence R_EAL c * sup rng L <= y by SUPINF_1:20;
       end;
       suppose y in REAL; then
        reconsider ry = y as Real;
        reconsider sl = sup rng L as Real by B2;
        y/R_EAL c = ry/c by D1,EXTREAL1:32; then
        sl*c <= ry by A1,D1,D2a,XREAL_1:85;
        hence R_EAL c * sup rng L <= y by EXTREAL1:13;
       end;
      end;
     end;
     hence sup rng K = R_EAL c * sup rng L by B3,SUPINF_1:def 16;
    end;
    suppose
K1:  sup rng L = +infty;
     per cases;
     suppose
K2:   c = 0;
      now let n be Nat;
       K.n = R_EAL c * L.n by A3;
       hence K.n = R_EAL 0 by K2,EXTREAL1:22;
      end; then
      lim K = sup rng K & lim K = R_EAL 0 by Cor14;
      hence sup rng K = R_EAL c * sup rng L by K2,EXTREAL1:22;
     end;
     suppose
L0c:  c <> 0; then
L0:   R_EAL c * sup rng L = +infty by A1,K1,EXTREAL1:def 1;
      now let n be set;
       -infty < L.n by A2,Def0003; then
L2:    -infty*R_EAL c < R_EAL c * L.n by L0c,A1,EXTREAL1:44;
       per cases;
       suppose n in dom K; then
        reconsider n1=n as Element of NAT;
        -infty*R_EAL c = -infty by L0c,A1,EXTREAL1:def 1; then
        -infty < K.n1 by A3,L2;
        hence -infty < K.n;
       end;
       suppose not n in dom K; then
        K.n = 0 by FUNCT_1:def 4;
        hence -infty < K.n;
       end;
      end; then
L1:   K is without-infty by Def0003;
      now let k be real number;
       assume 0 < k; then
       0 < k/c by L0c,A1,XREAL_1:141; then
       consider n be Nat such that
  L5:   R_EAL(k/c) < L.n by K1,A2,Cor13;
  L6:  R_EAL(k/c) * R_EAL c < R_EAL c * L.n by L5,L0c,A1,EXTREAL1:44;
       reconsider k1 = k as Real by XREAL_0:def 1;
       R_EAL(k/c)*R_EAL c = k1/c * c by EXTREAL1:13
         .= k1/(c/c) by XCMPLX_1:83
         .= k by L0c,XCMPLX_1:51; then
       R_EAL k < K.n by A3,L6;
       hence ex n be Nat st not K.n <= k;
      end;
      hence sup rng K = R_EAL c * sup rng L by L0,L1,Cor13;
     end;
    end;
   end;
   hence sup rng K = R_EAL c * sup rng L;
   now let n be set;
 C1:-infty < L.n by A2,Def0003;
    L.n = +infty implies R_EAL c * L.n <> -infty by A1,EXTREAL2:45; then
 C2:-infty <> R_EAL c * L.n by C1,EXTREAL1:28;
    per cases;
    suppose n in dom K; then
     reconsider n1=n as Element of NAT;
     K.n1 <> -infty by A3,C2;
     hence -infty < K.n by SUPINF_1:23;
    end;
    suppose not n in dom K; then
     K.n = 0 by FUNCT_1:def 4;
     hence -infty < K.n;
    end;
   end;
   hence K is without-infty by Def0003;
end;

theorem Cor17b:
for L,K be ExtREAL_sequence, c be Real st
  0 <=c &
  (for n,m be Nat st n <=m holds L.n <= L.m) &
  (for n be Nat holds K.n = R_EAL c * L.n) &
  L is without-infty holds
    ( (for n,m be Nat st n <=m holds K.n <= K.m) &
      K is without-infty &
      K is convergent & lim K = sup rng K & lim K = R_EAL c * lim L )
proof
   let L,K be ExtREAL_sequence, c be Real;
   assume that
A1:0 <= c and
A2:(for n,m be Nat st n <=m holds L.n <= L.m) and
A3:(for n be Nat holds K.n = R_EAL c * L.n ) and
A4:L is without-infty;
   thus
P1:now let n,m be Nat;
    assume n <= m; then
    L.n <= L.m by A2; then
    R_EAL c * L.n <= R_EAL c * L.m by A1,EXTREAL1:42; then
    K.n <= R_EAL c * L.m by A3;
    hence K.n <= K.m by A3;
   end;
   thus K is without-infty by A1,A4,A3,Cor17a;
   thus K is convergent & lim K= sup rng K by P1,Cor12a;
   sup rng K = lim K & sup rng L = lim L by A2,P1,Cor12a;
   hence lim K = R_EAL c * lim L by A1,A3,A4,Cor17a;
end;

begin :: Sequence of extended real valued functions

definition
let X be non empty set,
    H be Functional_Sequence of X,ExtREAL,
    x be Element of X;
func H#x -> ExtREAL_sequence means :DefHx:
for n be Nat holds it.n = (H.n).x;
existence
proof
   deffunc U(Nat) = (H.$1).x;
   consider f be Function of NAT,ExtREAL such that
A1: for n be Element of NAT holds f.n = U(n) from FUNCT_2:sch 4;
take f;
let n be Nat;
n in NAT by ORDINAL1:def 13;
hence thesis by A1;
end;
uniqueness
proof
   let S1,S2 be ExtREAL_sequence such that
A1: (for n be Nat holds S1.n = (H.n).x) & for n be Nat holds S2.n = (H.n).x;
   now let n be Element of NAT;
    S1.n = (H.n).x & S2.n = (H.n).x by A1;
    hence S1.n = S2.n;
   end;
   hence thesis by FUNCT_2:113;
end;
end;

definition let D1,D2 be set, F be Function of NAT,PFuncs(D1,D2), n be Nat;
redefine func F.n -> PartFunc of D1,D2;
coherence
proof
   n in NAT by ORDINAL1:def 13; then
   n in dom F by FUNCT_2:def 1; then
   F.n in rng F by FUNCT_1:def 5;
   hence thesis by PARTFUN1:120;
end;
end;

theorem LM015:
for X be non empty set, S be SigmaField of X,
    f be PartFunc of X,ExtREAL st
 (ex A be Element of S st A = dom f & f is_measurable_on A) &
 f is nonnegative
holds
  ex F be Functional_Sequence of X,ExtREAL st
   (for n be Nat holds F.n is_simple_func_in S & dom(F.n) = dom f) &
   (for n be Nat holds F.n is nonnegative) &
   (for n,m be Nat st n <=m holds
     for x be Element of X st x in dom f holds (F.n).x <= (F.m).x ) &
   (for x be Element of X st x in dom f holds
     (F#x) is convergent & lim(F#x) = f.x)
proof
   let X be non empty set,
       S be SigmaField of X,
       f be PartFunc of X,ExtREAL such that
A2: ex A be Element of S st A = dom f & f is_measurable_on A and
A3: f is nonnegative;
   consider A be Element of S such that
A2':A = dom f & f is_measurable_on A by A2;
   defpred PF[Nat,PartFunc of X,ExtREAL] means
    dom $2 = dom f &
    (for x be Element of X st x in dom f holds
      ( for k be Nat st
         1 <= k & k <= 2|^$1*$1 &
         (k-1)/(2|^$1) <= f.x &
         f.x < k/(2|^$1) holds $2.x= (k-1)/(2|^$1) ) &
      ( $1 <= f.x implies $2.x= $1 ));
V1:for n be Element of NAT
    ex y being Element of PFuncs(X,ExtREAL) st PF[n,y]
   proof
    let n be Element of NAT;
    defpred PP[set,set] means
    ( for k be Nat st
       1 <= k & k <= 2|^n*n &
       (k-1)/(2|^n) <= f.$1 &
       f.$1 < k/(2|^n) holds $2=(k-1)/(2|^n)) &
    ( n <= f.$1 implies $2 = n);
V11:for x be set st x in dom f ex y be set st PP[x,y]
    proof
     let x be set; assume x in dom f;
Z1:  0 < 2|^n by PREPOWER:13;
     per cases;
     suppose
W1:   f.x < n;
      0 <= f.x by A3,SUPINF_2:70; then
      consider k be Nat such that
W21:   1 <= k & k <= 2|^n*n &
       (k-1)/(2|^n) <= f.x & f.x < k/(2|^n) by W1,Cor2;
      take y = (k-1)/(2|^n);
      now let k1 be Nat; assume
W31:   1 <= k1 & k1 <= 2|^n*n & (k1-1)/(2|^n) <= f.x & f.x < k1/(2|^n);
Z2:    R_EAL(k/(2|^n)) = k/(2|^n) & R_EAL((k1-1)/(2|^n)) = (k1-1)/(2|^n) &
       R_EAL(k1/(2|^n)) = k1/(2|^n) & R_EAL((k-1)/(2|^n)) = (k-1)/(2|^n);
W32:   now assume k < k1; then
        k+1 <= k1 by NAT_1:38; then
        k <= k1 - 1 by XREAL_1:21; then
        k/(2|^n) <= (k1-1)/(2|^n) by Z1,XREAL_1:74;
        hence contradiction by Z2,W21,W31,SUPINF_1:29;
       end;
       now assume k1 < k; then
        k1+1 <= k by NAT_1:38; then
        k1 <= k - 1 by XREAL_1:21; then
        k1/(2|^n) <= (k-1)/(2|^n) by Z1,XREAL_1:74;
        hence contradiction by Z2,W31,W21,SUPINF_1:29;
       end;
       hence y=(k1-1)/(2|^n) by W32,XREAL_1:1;
      end;
      hence PP[x,y] by W1;
     end;
     suppose
W5:   n <= f.x;
      reconsider y = n as Real;
      take y;
      thus PP[x,y] by W5,Cor3;
     end;
    end;
V12:for x,y1,y2 be set st x in dom f & PP[x,y1] & PP[x,y2] holds y1 = y2
    proof
     let x,y1,y2 be set;
     assume
V13:  x in dom f & PP[x,y1] & PP[x,y2];
     now assume
V18:  f.x < R_EAL n;
      0 <= f.x by A3,SUPINF_2:70; then
      consider k be Nat such that
V20:   1 <= k & k <= 2|^n*n &
       (k-1)/(2|^n) <= f.x & f.x < k/(2|^n) by V18,Cor2;
      y1 = (k-1)/(2|^n) by V13,V20;
      hence y1 = y2 by V13,V20;
     end;
     hence y1 = y2 by V13;
    end;
    consider fn be Function such that
V2:  dom fn = dom f &
     for x be set st x in dom f holds PP[x,fn.x] from FUNCT_1:sch 2(V12,V11);
    rng fn c= ExtREAL
    proof
     now let w be set;
      assume w in rng fn; then
      consider v be set such that
V22:   v in dom fn & w = fn.v by FUNCT_1:def 5;
      per cases;
      suppose R_EAL n <= f.v; then
       fn.v = R_EAL n by V2,V22;
       hence w in ExtREAL by V22;
      end;
      suppose
V23:   f.v < R_EAL n;
       0 <= f.v by A3,SUPINF_2:70; then
       consider k be Nat such that
V24:    1 <= k & k <= 2|^n*n &
        (k-1)/(2|^n) <= f.v & f.v < k/(2|^n) by V23,Cor2;
       fn.v = (k-1)/(2|^n) by V2,V22,V24;
       hence w in ExtREAL by V22,XXREAL_0:def 1;
      end;
     end;
     hence rng fn c= ExtREAL by TARSKI:def 3;
    end; then
    reconsider fn as PartFunc of dom f,ExtREAL by V2,PARTFUN1:25;
    reconsider fn as PartFunc of X,ExtREAL by V2,PARTFUN1:28;
    reconsider y = fn as Element of PFuncs(X,ExtREAL) by PARTFUN1:119;
    take y;
    thus thesis by V2;
   end;
   ex F be Function of NAT,PFuncs(X,ExtREAL) st
    for n be Element of NAT holds PF[n,F.n] from FUNCT_2:sch 3(V1); then
   consider F being Function of NAT,PFuncs(X,ExtREAL) such that
aB1: for n be Element of NAT holds dom (F.n) = dom f and
B2: for n be Element of NAT, x be Element of X st x in dom f holds
     ( for k be Nat st
        1 <= k & k <= 2|^n*n &
        (k-1)/(2|^n) <= f.x & f.x < k/(2|^n) holds
         (F.n).x=(k-1)/(2|^n) ) &
     ( n <= f.x implies (F.n).x= n );
B1: now let n be Nat; n in NAT by ORDINAL1:def 13;
      hence dom (F.n) = dom f by aB1;
    end;
  dom F = NAT & rng F c= PFuncs(X,ExtREAL) by FUNCT_2:def 1; then
   reconsider F as Functional_Sequence of X,ExtREAL by SEQFUNC:def 1;
P1:for n be Nat holds F.n is_simple_func_in S
   proof
    let n be Nat;
    reconsider n as Element of NAT by ORDINAL1:def 13;
    now let x be Element of X;
     assume x in dom(F.n); then
P111:x in dom f by B1;
     per cases;
     suppose R_EAL n <= f.x; then
      (F.n).x = R_EAL n by B2,P111; then
      - +infty < (F.n).x & (F.n).x < +infty by SUPINF_2:def 3,SUPINF_1:31;
      hence |. (F.n).x .| < +infty by EXTREAL2:59;
     end;
     suppose
Q2:   f.x < R_EAL n;
Q4:   0 <= f.x & R_EAL n < +infty by A3,SUPINF_2:70,SUPINF_1:31; then
      reconsider y = f.x as Real by Q2,EXTREAL1:1;
      set k = [\ 2|^n*y /] + 1;
Q3:   0 < 2|^n by PREPOWER:13; then
Q8:   0 <= 2|^n*y & 2|^n*y < 2|^n*n by Q2,Q4,XREAL_1:70,129;
      [\ 2|^n*y /] <= 2|^n*y & 2|^n*y -1 < [\ 2|^n*y /] by INT_1:def 4; then
Q9:   2|^n*y < k & [\ 2|^n*y /] < 2|^n*n by Q8,XREAL_1:2,21; then
P112: 0+1 <=k & k <= 2|^n*n by Q8,INT_1:20;
      reconsider k as Element of NAT by Q9,Q8,INT_1:16;
      k - 1 <= 2|^n*y & 2|^n*y < k by INT_1:def 4,52; then
      (k-1)/(2|^n) <= y & y < k/(2|^n) by Q3,XREAL_1:81,83; then
      (F.n).x = (k-1)/(2|^n) by B2,P111,P112; then
      -infty < (F.n).x & (F.n).x < +infty by SUPINF_1:31; then
      - +infty < (F.n).x & (F.n).x < +infty by SUPINF_2:def 3;
      hence |. (F.n).x .| < +infty by EXTREAL2:59;
     end;
    end; then
P11:F.n is_finite by MESFUNC2:def 1;
    reconsider N = 2|^n as Element of NAT by NEWTON:99;
    defpred PG[Nat,set] means
    ( $1 <= N*n implies
       $2 = A /\ great_eq_dom(f,R_EAL (($1-1)/(2|^n)))
           /\ less_dom(f,R_EAL ($1/(2|^n))) ) &
    ( $1 = N*n + 1 implies
       $2 = A /\ great_eq_dom(f,R_EAL (n)) );
R1: now let k be Element of NAT;
     assume k in Seg(N*n+1);
     per cases;
     suppose
R11a: k <> N*n+1;
      set B = A /\ great_eq_dom(f,R_EAL ((k-1)/(2|^n)))
              /\ less_dom(f,R_EAL (k/(2|^n)));
      B is_measurable_on S by A2',Cor4; then
      reconsider B as Element of S by MESFUNC1:def 11;
      take B;
      thus PG[k,B] by R11a;
     end;
     suppose
R14:  k = N*n+1;
      set B = A /\ great_eq_dom(f,R_EAL n);
      B is_measurable_on S by A2',MESFUNC1:31; then
      reconsider B as Element of S by MESFUNC1:def 11;
      take B;
      thus PG[k,B] by R14,NAT_1:38;
     end;
    end;
    consider G be FinSequence of S such that
R2:  dom G = Seg(N*n +1) &
     for k be Element of NAT st k in Seg(N*n +1) holds PG[k,G.k]
     from FINSEQ_1:sch 5(R1);
P12:len G = 2|^n*n +1 by R2,FINSEQ_1:def 3;
P13:now let k be Nat; assume
P131:1 <= k & k <= 2|^n*n;
E1:  k in NAT by ORDINAL1:def 13;
     k <= N*n + 1 by P131,NAT_1:37; then
     k in Seg(N*n+1) by P131,E1;
     hence G.k= A /\ great_eq_dom(f,R_EAL((k-1)/(2|^n)))
           /\ less_dom(f,R_EAL(k/(2|^n))) by R2,P131;
    end;
P15:G is disjoint_valued
    proof
     now let x,y be set;
      assume
P151:  x <> y;
      per cases;
      suppose not x in dom G or not y in dom G; then
       G.x = {} or G.y = {} by FUNCT_1:def 4;
       hence G.x misses G.y by XBOOLE_1:65;
      end;
      suppose
P152:  x in dom G & y in dom G; then
       reconsider x1=x,y1=y as Element of NAT;
       x1 in Seg len G & y1 in Seg len G by P152,FINSEQ_1:def 3; then
P153:  1 <= x1 & x1 <= 2|^n*n + 1 &
       1 <= y1 & y1 <= 2|^n*n + 1 by P12,FINSEQ_1:3;
       now 
       per cases by P151,REAL_1:def 5;
        case
P154:    x1 < y1;
         hereby assume
P155:     y1 = 2|^n*n+1; then
Z2:       x1 <= N*n by P154,NAT_1:38; then
P157:     G.x = A /\ great_eq_dom(f,R_EAL((x1-1)/(2|^n)))
                 /\ less_dom(f,R_EAL(x1/(2|^n))) by P153,P13;
P158:     G.y = A /\ great_eq_dom(f,R_EAL n) by P152,P155,R2;
          now assume ex v be set st v in G.x /\ G.y; then
           consider v be set such that
P159:       v in G.x /\ G.y;
           v in G.x & v in G.y by P159,XBOOLE_0:def 3; then
P160:      v in less_dom(f,R_EAL(x1/(2|^n))) &
           v in great_eq_dom(f,R_EAL n) by P157,P158,XBOOLE_0:def 3; then
P161:      ex z1 be R_eal st
            z1 = f.v & z1 < R_EAL(x1/(2|^n)) by MESFUNC1:def 12;
P162:      ex z2 be R_eal st
            z2 = f.v & R_EAL n <= z2 by P160,MESFUNC1:def 15;
Z1:        0 < 2|^n by PREPOWER:13;
           R_EAL n < R_EAL(x1/(2|^n)) by P161,P162,SUPINF_1:29;
           hence contradiction by Z2,Z1,XREAL_1:81;
          end; then
          G.x /\ G.y = {} by XBOOLE_0:def 1;
          hence G.x misses G.y by XBOOLE_0:def 7;
         end;
         assume y1 <> 2|^n*n+1; then
         y1 < N*n+1 by P153,REAL_1:def 5; then
P164:    y1 <= N*n by NAT_1:38; then
         x1 <= 2|^n*n by P154,XREAL_1:2; then
P165:    G.x = A /\ great_eq_dom(f,R_EAL((x1-1)/(2|^n)))
                 /\ less_dom(f,R_EAL(x1/(2|^n))) &
         G.y = A /\ great_eq_dom(f,R_EAL((y1-1)/(2|^n)))
                 /\ less_dom(f,R_EAL(y1/(2|^n))) by P13,P153,P164;
         now assume ex v be set st v in G.x /\ G.y; then
          consider v be set such that
P166:      v in G.x /\ G.y;
          v in G.x & v in G.y by P166,XBOOLE_0:def 3; then
P166a:    v in less_dom(f,R_EAL(x1/(2|^n))) &
          v in A /\ great_eq_dom(f,R_EAL((y1-1)/(2|^n)))
            by P165,XBOOLE_0:def 3; then
P167:     v in less_dom(f,R_EAL(x1/(2|^n))) &
          v in great_eq_dom(f,R_EAL((y1-1)/(2|^n))) by XBOOLE_0:def 3;
P168:     ex z1 be R_eal st z1 = f.v & z1 < R_EAL(x1/(2|^n)) 
           by P166a,MESFUNC1:def 12;
P169:     ex z2 be R_eal st z2 = f.v & R_EAL((y1-1)/(2|^n)) <= z2 
           by P167,MESFUNC1:def 15;
Z1:       0 < 2|^n by PREPOWER:13;
          R_EAL((y1-1)/(2|^n)) < R_EAL(x1/(2|^n))
            by P168,P169,SUPINF_1:29; then
          y1-1 < x1 by Z1,XREAL_1:74; then
          y1 < x1 + 1 by XREAL_1:21;
          hence contradiction by P154,NAT_1:38;
         end; then
         G.x /\ G.y = {} by XBOOLE_0:def 1;
         hence G.x misses G.y by XBOOLE_0:def 7;
        end;
        case
Q154:    y1 < x1;
         hereby assume x1 <> 2|^n*n+1; then
          x1 < N*n+1 by P153,REAL_1:def 5; then
Q164:     x1 <= N*n by NAT_1:38; then
          y1 <= 2|^n*n by Q154,XREAL_1:2; then
Q165:     G.y = A /\ great_eq_dom(f,R_EAL((y1-1)/(2|^n)))
                  /\ less_dom(f,R_EAL(y1/(2|^n))) &
          G.x = A /\ great_eq_dom(f,R_EAL((x1-1)/(2|^n)))
                  /\ less_dom(f,R_EAL(x1/(2|^n))) by P13,P153,Q164;
          now assume ex v be set st v in G.x /\ G.y; then
           consider v be set such that
Q166:       v in G.x /\ G.y;
           v in G.x & v in G.y by Q166,XBOOLE_0:def 3; then
Q166a:     v in less_dom(f,R_EAL(y1/(2|^n))) &
           v in A /\ great_eq_dom(f,R_EAL((x1-1)/(2|^n)))
             by Q165,XBOOLE_0:def 3; then
Q167:      v in less_dom(f,R_EAL(y1/(2|^n))) &
           v in great_eq_dom(f,R_EAL((x1-1)/(2|^n))) by XBOOLE_0:def 3;
           consider z1 be R_eal such that
Q168:       z1 = f.v & z1 < R_EAL(y1/(2|^n)) by Q166a,MESFUNC1:def 12;
           consider z2 be R_eal such that
Q169:       z2 = f.v & R_EAL((x1-1)/(2|^n)) <= z2 by Q167,MESFUNC1:def 15;
Z1:        0 < 2|^n by PREPOWER:13;
           R_EAL((x1-1)/(2|^n)) < R_EAL(y1/(2|^n)) 
             by Q168,Q169,SUPINF_1:29; then
           x1-1 < y1 by Z1,XREAL_1:74; then
           x1 < y1 + 1 by XREAL_1:21;
           hence contradiction by Q154,NAT_1:38;
          end; then
          G.x /\ G.y = {} by XBOOLE_0:def 1;
          hence G.x misses G.y by XBOOLE_0:def 7;
         end;
         assume
Q155:     x1 = 2|^n*n+1; then
Z2:      y1 <= N*n by Q154,NAT_1:38; then
Q157:    G.y = A /\ great_eq_dom(f,R_EAL((y1-1)/(2|^n)))
                /\ less_dom(f,R_EAL(y1/(2|^n))) by P153,P13;
Q158:    G.x = A /\ great_eq_dom(f,R_EAL n) by P152,Q155,R2;
         now assume ex v be set st v in G.x /\ G.y; then
          consider v be set such that
Q159:      v in G.x /\ G.y;
          v in G.x & v in G.y by Q159,XBOOLE_0:def 3; then
Q160:     v in less_dom(f,R_EAL(y1/(2|^n))) &
          v in great_eq_dom(f,R_EAL n) by Q157,Q158,XBOOLE_0:def 3; then
          consider z1 be R_eal such that
Q161:      z1 = f.v & z1 < R_EAL(y1/(2|^n)) by MESFUNC1:def 12;
          consider z2 be R_eal such that
Q162:      z2 = f.v & R_EAL n <= z2 by Q160,MESFUNC1:def 15;
Z1:       0 < 2|^n by PREPOWER:13;
          R_EAL n < R_EAL(y1/(2|^n)) by Q161,Q162,SUPINF_1:29;
          hence contradiction by Z2,Z1,XREAL_1:81;
         end; then
         G.x /\ G.y = {} by XBOOLE_0:def 1;
         hence G.x misses G.y by XBOOLE_0:def 7;
        end;
       end;
       hence G.x misses G.y;
      end;
     end;
     hence thesis by PROB_2:def 3;
    end;
    reconsider G as Finite_Sep_Sequence of S by P15;
    union rng G = dom f
    proof
     for v be set st v in union rng G holds v in dom f
     proof
      let v be set;
      assume v in union rng G; then
      consider B be set such that
S1:    v in B & B in rng G by TARSKI:def 4;
      consider m be set such that
S2:    m in dom G & B = G.m by S1,FUNCT_1:def 5;
      reconsider m as Element of NAT by S2;
S4:   1 <= m & m <= N*n+1 by R2,S2,FINSEQ_1:3;
      now per cases;
       suppose m = N*n+1; then
        B = A /\ great_eq_dom(f,R_EAL n) by R2,S2;
        hence v in A by S1,XBOOLE_0:def 3;
       end;
       suppose m <> N*n+1; then
        m < N*n+1 by S4,REAL_1:def 5; then
        m <= N*n by NAT_1:38; then
        B = A /\ great_eq_dom(f,R_EAL ((m-1)/(2|^n)))
              /\ less_dom(f,R_EAL (m/(2|^n))) by S2,R2; then
        v in A /\ great_eq_dom(f,R_EAL ((m-1)/(2|^n))) by S1,XBOOLE_0:def 3;
        hence v in A by XBOOLE_0:def 3;
       end;
      end;
      hence v in dom f by A2';
     end; then
T0:  union rng G c= dom f by TARSKI:def 3;
     for v be set st v in dom f holds v in union rng G
     proof
      let v be set; assume
T1:    v in dom f;
      ex B being set st v in B & B in rng G
      proof
       per cases;
       suppose
T2:     f.v < R_EAL n;
        0 <= f.v by A3,SUPINF_2:70; then
        consider k be Nat such that
T3:      1 <= k & k <= 2|^n*n &
         (k-1)/(2|^n) <= f.v & f.v < k/(2|^n) by T2,Cor2;
        v in great_eq_dom(f,R_EAL((k-1)/(2|^n)))
           by T3,T1,MESFUNC1:def 15; then
T4:     v in A /\ great_eq_dom(f,R_EAL((k-1)/(2|^n)))
           by T1,A2',XBOOLE_0:def 3;
        v in less_dom(f,R_EAL (k/(2|^n))) by T3,T1,MESFUNC1:def 12; then
T5:     v in A /\ great_eq_dom(f,R_EAL((k-1)/(2|^n)))
               /\ less_dom(f,R_EAL (k/(2|^n))) by T4,XBOOLE_0:def 3;
E1:     k in NAT by ORDINAL1:def 13;
        N*n <= N*n + 1 by NAT_1:29; then
        k <= N*n + 1 by T3,XREAL_1:2; then
T6:     k in Seg(N*n+1) by T3,E1;
        take B = G.k;
        thus thesis by T5,T6,R2,T3,FUNCT_1:12;
       end;
       suppose R_EAL n <= f.v; then
        v in great_eq_dom(f,R_EAL n) by T1,MESFUNC1:def 15; then
U3:     v in A /\ great_eq_dom(f,R_EAL n) by T1,A2',XBOOLE_0:def 3;
        set k = N*n+1;
        take B = G.k;
        1 <= k by NAT_1:29; then
        k in Seg(N*n+1);
        hence thesis by U3,R2,FUNCT_1:12;
       end;
      end;
      hence v in union rng G by TARSKI:def 4;
     end; then
     dom f c= union rng G by TARSKI:def 3;
     hence thesis by T0,XBOOLE_0:def 10;
    end; then
P16:dom (F.n) = union rng G by B1;
    for k be Nat, x,y be Element of X st
     k in dom G & x in G.k & y in G.k holds (F.n).x =(F.n).y
    proof
     let k be Nat, x,y be Element of X;
     assume
P171: k in dom G & x in G.k & y in G.k; then
P172:1 <= k & k <= N*n+1 by R2,FINSEQ_1:3;

     now per cases;
      suppose k = N*n + 1; then
       G.k = A /\ great_eq_dom(f,R_EAL n) by P171,R2; then
P173:  x in A & x in great_eq_dom(f,R_EAL n) &
       y in A & y in great_eq_dom(f,R_EAL n) by P171,XBOOLE_0:def 3; then
       ex z1 be R_eal st z1 = f.x & R_EAL n <= z1 by MESFUNC1:def 15; then
P175:  (F.n).x = R_EAL n by P173,A2',B2;
       ex z2 be R_eal st z2 = f.y & R_EAL n <= z2 by P173,MESFUNC1:def 15;
       hence (F.n).x = (F.n).y by P175,P173,A2',B2;
      end;
      suppose k <> N*n + 1; then
       k < N*n+1 by P172,REAL_1:def 5; then
P177:  k <= N*n by NAT_1:38; then
       G.k = A /\ great_eq_dom(f,R_EAL ((k-1)/(2|^n)))
               /\ less_dom(f,R_EAL (k/(2|^n))) by P171,R2; then
P178:  x in A /\ great_eq_dom(f,R_EAL ((k-1)/(2|^n))) &
       x in less_dom(f,R_EAL (k/(2|^n))) &
       y in A /\ great_eq_dom(f,R_EAL ((k-1)/(2|^n))) &
       y in less_dom(f,R_EAL (k/(2|^n))) by P171,XBOOLE_0:def 3; then
P179:  x in A & x in great_eq_dom(f,R_EAL ((k-1)/(2|^n))) &
       y in A & y in great_eq_dom(f,R_EAL ((k-1)/(2|^n))) by XBOOLE_0:def 3;
P180:  ex z1 be R_eal st z1 = f.x & z1 < R_EAL (k/(2|^n))
         by P178,MESFUNC1:def 12;
P182:  ex z3 be R_eal st z3 = f.y & z3 < R_EAL(k/(2|^n)) 
         by P178,MESFUNC1:def 12;
P183:  ex z4 be R_eal st z4 = f.y & R_EAL((k-1)/(2|^n)) <= z4 
         by P179,MESFUNC1:def 15;
       ex z2 be R_eal st z2 = f.x & R_EAL ((k-1)/(2|^n)) <= z2 
         by P179,MESFUNC1:def 15; then
       (F.n).x = R_EAL((k-1)/(2|^n)) by B2,P172,P179,A2',P177,P180;
       hence (F.n).x = (F.n).y by B2,P172,P179,A2',P177,P182,P183;
      end;
     end;
     hence (F.n).x =(F.n). y;
    end;
    hence thesis by P11,P16,MESFUNC2:def 5;
   end;
P4:for n be Nat holds F.n is nonnegative
   proof
    let n be Nat;
    reconsider n as Element of NAT by ORDINAL1:def 13;
    now let x be set;
     assume x in dom (F.n); then
P42: x in dom f by B1;
     per cases;
     suppose n <= f.x;
      hence 0 <= (F.n).x by B2,P42;
     end;
     suppose
P43:  f.x < n;
      0 <= f.x by A3,SUPINF_2:70; then
      consider k be Nat such that
P44:   1 <= k & k <= 2|^n*n &
       (k-1)/(2|^n) <= f.x & f.x < k/(2|^n) by P43,Cor2;
P45:  (F.n).x = (k-1)/(2|^n) by P44,B2,P42;
      0 <= k-1 & 2|^n > 0 by P44,XREAL_1:50,PREPOWER:13;
      hence 0 <= (F.n).x by P45,XREAL_1:138;
     end;
    end;
    hence thesis by SUPINF_2:71;
   end;
P5:for n,m be Nat st n <=m holds
    for x be Element of X st x in dom f holds (F.n).x <= (F.m).x
   proof
    let n,m be Nat such that
P51: n <=m;
     reconsider n,m as Element of NAT by ORDINAL1:def 13;
    let x be Element of X such that
P52: x in dom f;
    per cases;
    suppose
P53: R_EAL m <= f.x; then
P54: (F.m).x = R_EAL m by P52,B2;
     R_EAL n <= f.x by P51,P53,SUPINF_1:29;
     hence thesis by P51,P52,B2,P54;
    end;
    suppose
P56: f.x < R_EAL m;
P57: 0 <= f.x by A3,SUPINF_2:70; then
     consider M be Nat such that
P58:  1 <= M & M <= 2|^m*m &
      (M-1)/(2|^m) <= f.x & f.x < M/(2|^m) by P56,Cor2;
      reconsider M as Element of NAT by ORDINAL1:def 13;
P59:  (F.m).x = (M-1)/(2|^m) by P52,P58,B2;
     per cases;
     suppose
P60:  R_EAL n <= f.x; then
P61:  (F.n).x = R_EAL n by P52,B2;
      R_EAL n = n & R_EAL(M/(2|^m)) = M/(2|^m); then
P63:  n < M/(2|^m) by P58,P60,SUPINF_1:29;
Z1:   0 < 2|^m by PREPOWER:13; then
P64:  2|^m*n < M by P63,XREAL_1:81;
      reconsider M1 = 2|^m as Element of NAT by NEWTON:99;
      M1*n+1 <= M by P64,NAT_1:38; then
      M1*n <= M-1 by XREAL_1:21;
      hence thesis by P61,P59,Z1,XREAL_1:79;
     end;
     suppose
      f.x < R_EAL n; then
      consider N1 be Nat such that
P67:   1 <= N1 & N1 <= 2|^n*n &
       (N1-1)/(2|^n) <= f.x & f.x < N1/(2|^n) by P57,Cor2;
       reconsider N1 as Element of NAT by ORDINAL1:def 13;
P68:  (F.n).x = (N1-1)/(2|^n) by P52,P67,B2;
      consider k being Nat such that
      P69:   m = n + k by P51,NAT_1:28;
      reconsider k as Element of NAT by ORDINAL1:def 13;
P71:  2|^n > 0 & 2|^k > 0 & 2|^(n+k) > 0 by PREPOWER:13;
      reconsider K = 2|^k as Element of NAT by NEWTON:99;
      R_EAL((N1-1)/(2|^n)) < R_EAL(M/(2|^(n+k)))
        by P58,P67,P69,SUPINF_1:29; then
      (N1-1)/(2|^n) < M/(2|^n*2|^k) by NEWTON:13; then
      (N1-1)/(2|^n) < M/(2|^k)/(2|^n) by XCMPLX_1:79; then
      N1-1 < M/(2|^k) by P71,XREAL_1:74; then
      K*(N1-1) < M by P71,XREAL_1:81; then
      K*(N1-1)+1 <= M by INT_1:20; then
      K*(N1-1) <= M-1 by XREAL_1:21; then
      K*(N1-1)/(2|^(n+k)) <= (M-1)/(2|^(n+k)) by P71,XREAL_1:74; then
      K*(N1-1)/(2|^n*2|^k) <= (M-1)/(2|^(n+k)) by NEWTON:13;
      hence thesis by P59,P68,P69,P71,XCMPLX_1:92;
     end;
    end;
   end;
   now let x be Element of X such that
AS1:x in dom f;
    per cases;
    suppose
R0:  |.f.x.| =+infty;
     now assume -f.x = +infty; then
      f.x < 0 by EXTREAL1:25;
      hence contradiction by A3,SUPINF_2:70;
     end; then
R1:  f.x = +infty by R0,EXTREAL2:50;
     for g be real number st 0 < g
      ex n be Nat st for m be Nat st n <= m holds g <= (F#x).m
     proof
      let g be real number;
      assume 0 < g; then
      reconsider n = [/ g \] as Nat by INT_1:80;
R11:  g <= n by INT_1:def 5;
      for m be Nat st n <= m holds g <= (F#x).m
      proof
       let m be Nat;
       assume n <= m; then
R12:   g <= m by R11,XREAL_1:2;
       reconsider m as Element of NAT by ORDINAL1:def 13;
       reconsider g1 = g as Real by XREAL_0:def 1;
       R_EAL m <= f.x by R1,SUPINF_1:20; then
       (F.m).x = R_EAL m by B2,AS1;
       hence thesis by DefHx,R12;
      end;
      hence thesis;
     end; then
R2:  F#x is convergent_to_+infty by Defconverge2; then
     F#x is convergent by Defconvergent;
     hence F#x is convergent & lim(F#x) = f.x by R1,R2,Deflim;
    end;
    suppose
     |.f.x.| <> +infty; then
     f.x <> +infty & f.x <> -infty by EXTREAL2:67; then
     reconsider g=f.x as Real by EXTREAL1:1;
Q0:  f.x=R_EAL g;
Q4:  for p be real number st 0 <p
      ex k be Nat st for j be Nat st j>=k holds |.(F#x).j - f.x.| < p
     proof
      let p be real number; assume
Q40:   0 < p;
      consider N1 be Element of NAT such that
Q41:   1/(2|^N1) <= p by Q40,PCOMPS_2:3;
T2:   for k be Nat st k >= N1 holds 1/(2|^k) <= p
      proof
       let k be Nat;
       assume k >= N1; then
       consider i being Nat such that
Q42:    k = N1 + i by NAT_1:28;
Q43:   2|^N1 > 0 & 2|^i >= 1 by PREPOWER:19; then
       (2|^N1)*(2|^i) >= 2|^N1 by XREAL_1:153; then
       2|^k >= 2|^N1 by Q42,NEWTON:13; then
       (2|^k)" <= (2|^N1)" by Q43,XREAL_1:87; then
       1/(2|^k) <= (2|^N1)" by XCMPLX_1:217; then
       1/(2|^k) <= 1/(2|^N1) by XCMPLX_1:217;
       hence thesis by Q41,XREAL_1:2;
      end;
Q45:  0 <= g by A3,SUPINF_2:70;
      set N2 = [/g\] + 1;
Q46a: g <= [/g\] & [/g\] < [/g\] + 1 by INT_1:def 5,XREAL_1:31; then
Q46:  g < N2 by XREAL_1:2;
      reconsider N2 as Element of NAT by Q45,Q46a,INT_1:16;
T3:   for N be Nat st N >= N2 holds |.(F#x).N - f.x.| < 1/(2|^N)
      proof
       let N be Nat;
       assume
W1:    N >= N2;
       reconsider N as Element of NAT by ORDINAL1:def 13;
       0 <= f.x & f.x < N by A3,Q46,XREAL_1:2,SUPINF_2:70,W1; then
       consider m be Nat such that
Q48:    1 <= m & m <= 2|^N*N &
        (m-1)/(2|^N) <= f.x & f.x < m/(2|^N) by Cor2;
        reconsider m as Element of NAT by ORDINAL1:def 13;
Q49:   (F#x).N = (F.N).x by DefHx .= (m-1)/(2|^N) by AS1,B2,Q48;
       -((F#x).N - f.x) = f.x - (F#x).N by EXTREAL2:15; then
Q51:   |.(F#x).N - f.x.| = |.f.x - (F#x).N .| by EXTREAL2:66;
       R_EAL(m/(2|^N)) - R_EAL((m-1)/(2|^N))
        = m/(2|^N) - (m-1)/(2|^N) by SUPINF_2:5
       .= m/(2|^N) + -((m-1)/(2|^N))
       .= m/(2|^N) + (-(m-1))/(2|^N) by XCMPLX_1:188
       .= (m + -(m-1))/(2|^N) by XCMPLX_1:63; then
Q54:   f.x - (F#x).N < 1/(2|^N) by Q48,Q49,MEASURE6:12;
       2|^N > 0 by PREPOWER:13; then
       (2|^N)" > 0 by XREAL_1:124; then
       1/(2|^N) > 0 by XCMPLX_1:217; then
Q56:   -R_EAL(1/(2|^N)) < 0 by EXTREAL1:25;
       0 <= f.x - (F#x).N by Q48,Q49,MEASURE5:2;
       hence thesis by Q51,Q54,Q56,EXTREAL2:59;
      end;
      reconsider k=max(N2,N1) as Element of NAT;
      now let j be Nat; assume
T41:    j >= k;
       k >= N1 & k >= N2 by SQUARE_1:46; then
       j >= N1 & j >= N2 by T41,XREAL_1:2; then
T42:   1/(2|^j) <= p & |.(F#x).j - f.x.| < R_EAL(1/(2|^j)) by T2,T3;
       R_EAL(1/(2|^j)) = 1/(2|^j) & R_EAL p = p;
       hence |.(F#x).j - f.x.| < p by T42,SUPINF_1:29;
      end;
      hence ex k be Nat st for j be Nat st j >= k holds |.(F#x).j - f.x.| < p;
     end; then
Q5:  F#x is convergent_to_finite_number by Q0,Defconverge1; then
     F#x is convergent by Defconvergent;
     hence F#x is convergent & lim(F#x) = f.x by Deflim,Q0,Q4,Q5;
    end;
   end;
   hence thesis by P1,B1,P4,P5;
end;

begin :: Integral of non negative simple valued function

definition
  let X be non empty set;
  let S be SigmaField of X;
  let M be sigma_Measure of S;
  let f be PartFunc of X,ExtREAL;
  func integral'(M,f) -> Element of ExtREAL equals :Defintegral':
  integral(X,S,M,f) if dom f <> {} otherwise 0.;
  correctness;
end;

theorem Th1a:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
 f is_simple_func_in S & g is_simple_func_in S &
 f is nonnegative & g is nonnegative holds
   dom(f+g) = dom f /\ dom g &
   integral'(M,f+g) = integral'(M,f|dom(f+g)) + integral'(M,g|dom(f+g))
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f,g be PartFunc of X,ExtREAL;
   assume that
A1:f is_simple_func_in S & g is_simple_func_in S and
A3:f is nonnegative & g is nonnegative;
B0:f+g is_simple_func_in S by A1,LL2;
   f is without-infty & g is without-infty by A3,WMI02; then
   not -infty in rng f & not -infty in rng g by Def0001; then
E0:f"{-infty} = {} & g"{-infty} = {} by FUNCT_1:142; then
P0:(dom f /\ dom g) \ (f"{-infty} /\ g"{+infty} \/ f"{+infty} /\ g"{-infty})
    = dom f /\ dom g;
   hence
P1: dom(f+g) = dom f /\ dom g by MESFUNC1:def 3;
   dom(f+g) is Element of S by B0,Cor10; then
C1:f|dom(f+g) is_simple_func_in S &
   g|dom(f+g) is_simple_func_in S by A1,Cor5;
   f|dom(f+g) is nonnegative & g|dom(f+g) is nonnegative by A3,LL0; then
C3:for x be set holds
    ( x in dom(f|dom(f+g)) implies 0 <= (f|dom(f+g)).x ) &
    ( x in dom(g|dom(f+g)) implies 0 <= (g|dom(f+g)).x ) by SUPINF_2:70;
   dom(f|dom(f+g)) = dom f /\ dom(f+g) &
   dom(g|dom(f+g)) = dom g /\ dom(f+g) by FUNCT_1:68; then
C4:dom(f|dom(f+g)) = dom f /\ dom f /\ dom g &
   dom(g|dom(f+g)) = dom g /\ dom g /\ dom f by P1,XBOOLE_1:16;
   per cases;
   suppose
B1: dom(f+g) = {}; then
B7: integral'(M,f+g) = 0 by Defintegral';
    dom(f|dom(f+g)) = dom f /\ dom(f+g) &
    dom(g|dom(f+g)) = dom g /\ dom(f+g) by FUNCT_1:68; then
    integral'(M,f|dom(f+g)) = 0 & integral'(M,g|dom(f+g)) = 0
      by B1,Defintegral';
    hence integral'(M,f+g) = integral'(M,f|dom(f+g)) + integral'(M,g|dom(f+g))
      by B7,SUPINF_2:18;
   end;
   suppose
D1: dom(f+g) <> {}; then
D2: integral(X,S,M,(f|dom(f+g) + g|dom(f+g)))
     = integral(X,S,M,f|dom(f+g))+integral(X,S,M,g|dom(f+g))
        by C4,P1,C1,C3,MESFUNC4:5;
D21:(f|dom(f+g))"{-infty} = dom(f+g) /\ f"{-infty} by FUNCT_1:139 .= {} by E0;
    (g|dom(f+g))"{-infty} = dom(f+g) /\ g"{-infty} by FUNCT_1:139
     .= {} by E0; then
    (dom(f|dom(f+g)) /\ dom(g|dom(f+g))) \
     ( (f|dom(f+g))"{-infty} /\ (g|dom(f+g))"{+infty} \/
      (f|dom(f+g))"{+infty} /\ (g|dom(f+g))"{-infty} )
     = dom(f+g) by C4,P0,D21,MESFUNC1:def 3; then
D3: dom(f|dom(f+g) + g|dom(f+g)) = dom(f+g) by MESFUNC1:def 3;
    for x be Element of X st x in dom(f|dom(f+g) + g|dom(f+g)) holds
     (f|dom(f+g) + g|dom(f+g)).x = (f+g).x
    proof
     let x be Element of X;
     assume
D31:  x in dom(f|dom(f+g) + g|dom(f+g)); then
     (f|dom(f+g) + g|dom(f+g)).x
       = (f|dom(f+g)).x + (g|dom(f+g)).x by MESFUNC1:def 3
      .= f.x + (g|dom(f+g)).x by D31,D3,FUNCT_1:72
      .= f.x + g.x by D31,D3,FUNCT_1:72;
     hence thesis by D31,D3,MESFUNC1:def 3;
    end; then
D4: integral(X,S,M,f+g)
     = integral(X,S,M,f|dom(f+g)) + integral(X,S,M,g|dom(f+g))
       by D2,D3,PARTFUN1:34;
D5: integral(X,S,M,f|dom(f+g)) = integral'(M,f|dom(f+g))
      by C4,P1,D1,Defintegral';
    integral(X,S,M,g|dom(f+g)) = integral'(M,g|dom(f+g))
      by C4,P1,D1,Defintegral';
    hence integral'(M,f+g) = integral'(M,f|dom(f+g)) + integral'(M,g|dom(f+g))
        by D4,D5,D1,Defintegral';
   end;
end;

theorem Th1b:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,c be Real st
 f is_simple_func_in S & f is nonnegative & 0 <= c holds
   integral'(M,c(#)f) = R_EAL(c)*integral'(M,f)
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f be PartFunc of X,ExtREAL;
   let c be Real;
   assume that
A1: f is_simple_func_in S and
A2: f is nonnegative and
A3: 0 <= c;
   set g = c(#)f;
B1:dom g = dom f by MESFUNC1:def 6;   
D2:for x be set st x in dom f holds 0 <= f.x by A2,SUPINF_2:70;
B2:for x be set st x in dom g holds g.x = (R_EAL c)*f.x by MESFUNC1:def 6;
B3:0 <= R_EAL c & R_EAL c < +infty by A3,SUPINF_1:31;
   per cases;
   suppose
C1: dom g = {}; then
    integral'(M,f) = 0 by B1,Defintegral'; then
    (R_EAL c)*integral'(M,f) = 0 by EXTREAL1:22;
    hence integral'(M,g) = R_EAL(c)*integral'(M,f) by C1,Defintegral';
   end;
   suppose
C2: dom g <> {}; then
    integral'(M,f) = integral(X,S,M,f) by B1,Defintegral'; then
    integral(X,S,M,g) = (R_EAL c)*integral'(M,f) 
      by C2,A1,D2,B3,B1,B2,MESFUNC4:6;
    hence integral'(M,g) = R_EAL(c)*integral'(M,f) by C2,Defintegral';
   end;
end;

theorem Th1:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    A,B be Element of S st
f is_simple_func_in S & f is nonnegative & A misses B holds
  integral'(M,f|(A\/B)) = integral'(M,f|A) + integral'(M,f|B)
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f be PartFunc of X,ExtREAL;
   let A,B be Element of S;
   assume that
A1:f is_simple_func_in S and
A2:f is nonnegative and
A3:A misses B;
   set g = f|(A\/B);
   set g1 = f|A;
   set g2 = f|B;
B1:g is_simple_func_in S by A1,Cor5;
   g is nonnegative by A2,LL0; then
B2:for x be set st x in dom g holds 0 <= g.x by SUPINF_2:70; then
   consider G be Finite_Sep_Sequence of S,
            b be FinSequence of ExtREAL such that
B3: G,b are_Re-presentation_of g &
    b.1 = 0 &
    for n be Nat st 2 <= n & n in dom b holds 0 < b.n & b.n < +infty
     by B1,MESFUNC3:14;
   deffunc Fy(Nat) = b.$1*(M*G).$1;
   consider y be FinSequence of ExtREAL such that
B4: len y = len G &
for j be Element of NAT st j in Seg len G holds y.j = Fy(j)
from FINSEQ_2:sch 1;
B5:dom y = Seg len y by FINSEQ_1:def 3
        .= dom G by FINSEQ_1:def 3,B4;

B6:f|A is_simple_func_in S by A1,Cor5;
B7:f|B is_simple_func_in S by A1,Cor5;
   deffunc G1(Nat) = G.$1 /\ A;
   consider G1 be FinSequence such that
B8: len G1 = len G &
    for k be Element of NAT st k in Seg len G holds G1.k = G1(k)
      from FINSEQ_1:sch 2;
B9:dom G = Seg len G1 by B8,FINSEQ_1:def 3 .= dom G1 by FINSEQ_1:def 3;
B10:for k be Nat st k in dom G holds G1.k = G.k /\ A
   proof
    let k be Nat;
    assume k in dom G; then
    k in Seg len G by FINSEQ_1:def 3;
    hence thesis by B8;
   end; then
   reconsider G1 as Finite_Sep_Sequence of S by B9,Cor6;
C1:dom(g|A) = dom g /\ A by FUNCT_1:68
           .= dom f /\ (A\/B) /\ A by FUNCT_1:68
           .= dom f /\ ((A\/B) /\ A) by XBOOLE_1:16
           .= dom f /\ A by XBOOLE_1:21
           .= dom g1 by FUNCT_1:68;
   for x be set st x in dom(g|A) holds (g|A).x = g1.x
   proof
    let x be set; assume
C2:  x in dom(g|A); then
    x in dom g /\ A by FUNCT_1:68; then
C3: x in dom g by XBOOLE_0:def 3;
    (g|A).x = g.x by C2,FUNCT_1:70 .= f.x by C3,FUNCT_1:70;
    hence (g|A).x = g1.x by C1,C2,FUNCT_1:70;
   end; then
   g|A = g1 by C1,FUNCT_1:9; then
F6:G1,b are_Re-presentation_of g1 by B3,B9,B10,Cor7;
   deffunc G2(Nat) = G.$1 /\ B;
   consider G2 be FinSequence such that
D8: len G2 = len G &
    for k be Element of NAT st k in Seg len G holds G2.k = G2(k)
      from FINSEQ_1:sch 2;
D9:dom G = Seg len G2 by D8,FINSEQ_1:def 3 .= dom G2 by FINSEQ_1:def 3;
D10:for k be Nat st k in dom G holds G2.k = G.k /\ B
   proof
    let k be Nat;
    assume k in dom G; then
    k in Seg len G by FINSEQ_1:def 3;
    hence G2.k = G.k /\ B by D8;
   end; then
   reconsider G2 as Finite_Sep_Sequence of S by D9,Cor6;
D11:dom(g|B) = dom g /\ B by FUNCT_1:68
           .= dom f /\ (A\/B) /\ B by FUNCT_1:68
           .= dom f /\ ((A\/B) /\ B) by XBOOLE_1:16
           .= dom f /\ B by XBOOLE_1:21
           .= dom g2 by FUNCT_1:68;
   for x be set st x in dom(g|B) holds (g|B).x = g2.x
   proof
    let x be set; assume
D12:  x in dom(g|B); then
    x in dom g /\ B by FUNCT_1:68; then
D13: x in dom g by XBOOLE_0:def 3;
    (g|B).x = g.x by D12,FUNCT_1:70 .= f.x by D13,FUNCT_1:70;
    hence (g|B).x = g2.x by D11,D12,FUNCT_1:70;
   end; then
   g|B = g2 by D11,FUNCT_1:9; then
G6:G2,b are_Re-presentation_of g2 by B3,D9,D10,Cor7;
   per cases;
   suppose
C1: dom(f|(A\/B)) = {}; then
D1: integral'(M,g) = 0 by Defintegral';
    A c= A\/B by XBOOLE_1:7; then
    dom f /\ A c= dom f /\ (A\/B) by XBOOLE_1:26; then
    dom(f|A) c= dom f /\ (A\/B) by FUNCT_1:68; then
    dom(f|A) c= dom(f|(A\/B)) by FUNCT_1:68; then
C2: dom(f|A) = {} by C1,XBOOLE_1:3;
C3: integral'(M,g1) = 0 by C2,Defintegral';
    B c= A\/B by XBOOLE_1:7; then
    dom f /\ B c= dom f /\ (A\/B) by XBOOLE_1:26; then
    dom(f|B) c= dom f /\ (A\/B) by FUNCT_1:68; then
    dom(f|B) c= dom(f|(A\/B)) by FUNCT_1:68; then
    dom(f|B) = {} by C1,XBOOLE_1:3; then
    integral'(M,g2) = 0 by Defintegral';
    hence integral'(M,g) = integral'(M,g1) + integral'(M,g2) 
      by D1,C3,SUPINF_2:18;
   end;
   suppose
C5: dom(f|(A\/B)) <> {};
    for n be Nat st n in dom y holds y.n = b.n*(M*G).n
    proof
     let n be Nat;
     assume n in dom y; then
     n in Seg len y by FINSEQ_1:def 3;
     hence thesis by B4;
    end; then
    integral(X,S,M,g) = Sum y by B1,B2,B3,B5,C5,MESFUNC4:3; then
C6: integral'(M,g) = Sum y by C5,Defintegral';
    per cases;
    suppose
C7:  dom(f|A) = {};
C8:  integral'(M,g1) = 0 by C7,Defintegral';
C9:  dom(f|(A\/B)) = dom f /\ (A\/B) by FUNCT_1:68
       .= dom f /\ A \/ dom f /\ B by XBOOLE_1:23
       .= dom(f|A) \/ dom f /\ B by FUNCT_1:68
       .= dom(f|B) by C7,FUNCT_1:68;
     now let x be set;
      assume
D2:    x in dom g; then
      g.x = f.x by FUNCT_1:70;
      hence g.x = g2.x by C9,D2,FUNCT_1:70;
     end; then
     g = g2 by C9,FUNCT_1:9;
     hence thesis by C8,SUPINF_2:18;
    end;
    suppose
E1:  dom(f|A) <> {};
     per cases;
      suppose
E2:    dom(f|B) = {};
E3:    integral'(M,g2) = 0 by E2,Defintegral';
E4:    dom(f|(A\/B)) = dom f /\ (A\/B) by FUNCT_1:68
       .= (dom f /\ B) \/ (dom f /\ A) by XBOOLE_1:23
       .= dom(f|B) \/ (dom f /\ A) by FUNCT_1:68
       .= dom(f|A) by E2,FUNCT_1:68;
       now let x be set;
        assume
D2:      x in dom g; then
        g.x = f.x by FUNCT_1:70;
        hence g.x = g1.x by E4,D2,FUNCT_1:70;
       end; then
       g = g1 by E4,FUNCT_1:9;
       hence integral'(M,g)=integral'(M,g1)+integral'(M,g2) by E3,SUPINF_2:18;
      end;
      suppose
E5:    dom(f|B) <> {};
       g1 is nonnegative by A2,LL0; then
E7:    for x be set st x in dom g1 holds 0 <= g1.x by SUPINF_2:70;
       g2 is nonnegative by A2,LL0; then
E8:    for x be set st x in dom g2 holds 0 <= g2.x by SUPINF_2:70;
       deffunc Fy1(Nat) = b.$1*(M*G1).$1;
       consider y1 be FinSequence of ExtREAL such that
F8:     len y1 = len G1 &
        for j be Element of NAT st j in Seg(len G1) holds y1.j = Fy1(j)
          from FINSEQ_2:sch 1;
F9:    dom y1 = Seg len G1 by F8,FINSEQ_1:def 3 .= dom G1 by FINSEQ_1:def 3;
       now let k be Nat;
        assume k in dom y1; then
        k in Seg len G1 by F9,FINSEQ_1:def 3;
        hence y1.k = b.k*(M*G1).k by F8;
       end; then
       integral(X,S,M,g1) = Sum y1 by B6,E1,F6,E7,F9,MESFUNC4:3; then
P1:    integral'(M,g1) = Sum y1 by E1,Defintegral';
       deffunc Fy2(Nat) = b.$1*(M*G2).$1;
       consider y2 be FinSequence of ExtREAL such that
G8:     len y2 = len G2 &
        for j be Element of NAT st j in Seg len G2 holds y2.j = Fy2(j)
          from FINSEQ_2:sch 1;
G9:    dom y2 = Seg len G2 by G8,FINSEQ_1:def 3 .= dom G2 by FINSEQ_1:def 3;
       now let k be Nat;
        assume k in dom y2; then
        k in Seg len G2 by G9,FINSEQ_1:def 3;
        hence y2.k = b.k*(M*G2).k by G8;
       end; then
       integral(X,S,M,g2) = Sum y2 by B7,E5,G6,E8,G9,MESFUNC4:3; then
P2:    integral'(M,g2) = Sum y2 by E5,Defintegral';
H1:    for k be Nat st k in dom y1 holds 0 <= y1.k
       proof
        let k be Nat;
        assume
H11:     k in dom y1; then
        k in Seg len G1 by F8,FINSEQ_1:def 3; then
H12:    y1.k = b.k * (M*G1).k by F8;
H13a:   dom b = dom G by B3,MESFUNC3:def 1
             .= Seg len y1 by B8,F8,FINSEQ_1:def 3
             .= dom y1 by FINSEQ_1:def 3;
        k in Seg len y1 by H11,FINSEQ_1:def 3; then
H14:    1 <= k & k <= len y1 by FINSEQ_1:3;
H15a:   now per cases;
         suppose k = 1;
          hence 0 <= b.k by B3;
         end;
         suppose k <> 1; then
          1 < k by H14,REAL_1:def 5; then
          1+1 <= k by NAT_1:38;
          hence 0 <= b.k by B3,H13a,H11;
         end;
        end;
        k in Seg(len G1) by H11,F8,FINSEQ_1:def 3; then
H16:    k in dom G1 by FINSEQ_1:def 3; then
H17:    (M*G1).k = M.(G1.k) by FUNCT_1:23;
        G1.k in rng G1 & rng G1 c= S by H16,FUNCT_1:12; then
        reconsider G1k = G1.k as Element of S;
        M is nonnegative by MEASURE1:def 11; then
        0 <= M.G1k by SUPINF_2:58;
        hence 0 <= y1.k by H12,H15a,H17,EXTREAL2:45;
       end;
H2:    for k be Nat st k in dom y2 holds 0 <= y2.k
       proof
        let k be Nat;
        assume
H21:     k in dom y2; then
        k in Seg len G2 by G8,FINSEQ_1:def 3; then
H22:    y2.k = b.k * (M*G2).k by G8;
H23a:   dom b = dom G by B3,MESFUNC3:def 1
             .= Seg len y2 by D8,G8,FINSEQ_1:def 3
             .= dom y2 by FINSEQ_1:def 3;
        k in Seg len y2 by H21,FINSEQ_1:def 3; then
H24:    1 <= k & k <= len y2 by FINSEQ_1:3;
H25a:   now per cases;
         suppose k = 1;
          hence 0 <= b.k by B3;
         end;
         suppose k <> 1; then
          1 < k by H24,REAL_1:def 5; then
          1+1 <= k by NAT_1:38;
          hence 0 <= b.k by B3,H23a,H21;
         end;
        end;
        k in Seg len G2 by H21,G8,FINSEQ_1:def 3; then
H26:    k in dom G2 by FINSEQ_1:def 3; then
H27:    (M*G2).k = M.(G2.k) by FUNCT_1:23;
        G2.k in rng G2 & rng G2 c= S by H26,FUNCT_1:12; then
        reconsider G2k = G2.k as Element of S;
        M is nonnegative by MEASURE1:def 11; then
        0 <= M.G2k by SUPINF_2:58;
        hence 0 <= y2.k by H22,H25a,H27,EXTREAL2:45;
       end;
H3:    dom y1 = Seg len y2 by B8,F8,D8,G8,FINSEQ_1:def 3
             .= dom y2 by FINSEQ_1:def 3;
       y = y1 + y2
       proof
H42a:   for x be set st x in dom y1 holds not y1.x in {-infty}
        proof
         let x be set;
         assume
H41:      x in dom y1; then
         reconsider x as Element of NAT;
         0 <= y1.x by H1,H41;
         hence thesis by TARSKI:def 1;
        end;
        for x be set holds not x in y1"{-infty}
        proof
         let x be set;
         not (x in dom y1 & y1.x in {-infty}) by H42a;
         hence thesis by FUNCT_1:def 13;
        end; then
H43:    y1"{-infty} = {} by XBOOLE_0:def 1;
H45a:   for x be set st x in dom y2 holds not y2.x in {-infty}
        proof
         let x be set;
         assume
H44:      x in dom y2; then
         reconsider x as Element of NAT;
         0 <= y2.x by H2,H44;
         hence thesis by TARSKI:def 1;
        end;
        for x be set holds not x in y2"{-infty}
        proof
         let x be set;
         not (x in dom y2 & y2.x in {-infty}) by H45a;
         hence thesis by FUNCT_1:def 13;
        end; then
H46a:   y2"{-infty} = {} by XBOOLE_0:def 1;
H47:    dom y = Seg len G /\ Seg len G by B5,FINSEQ_1:def 3
             .= dom y1 /\ Seg len G2 by F8,FINSEQ_1:def 3,B8,D8
             .= dom y1 /\ dom y2 by G8,FINSEQ_1:def 3; then
H4:     dom y = (dom y1 /\ dom y2)\(
                 y1"{-infty}/\y2"{+infty}\/y1"{+infty}/\y2"{-infty})
                   by H46a,H43;
        for n be Element of NAT st n in dom y holds y.n = y1.n + y2.n
        proof
         let n be Element of NAT; assume
H51:     n in dom y; then
H61:     n in Seg len G by B4,FINSEQ_1:def 3; then
H52:     n in dom G by FINSEQ_1:def 3;
         n in dom y1 & n in dom y2 by H47,H51,XBOOLE_0:def 3; then
H62:     n in Seg len G1 & n in Seg len G2 by F8,G8,FINSEQ_1:def 3; then
H59:     n in dom G1 & n in dom G2 by FINSEQ_1:def 3;
H55:     G2.n = G.n /\ B by H52,D10;
         now let v be set; assume
H56:      v in G.n;
          G.n in rng G by H52,FUNCT_1:12; then
          v in union rng G by H56,TARSKI:def 4; then
          v in dom g by B3,MESFUNC3:def 1; then
          v in dom f /\ (A\/B) by FUNCT_1:68;
          hence v in A\/B by XBOOLE_0:def 3;
         end; then
         G.n c= A \/ B by TARSKI:def 3; then
H57:     G.n = G.n /\ (A\/B) by XBOOLE_1:28
            .= G.n /\ A \/ G.n /\ B by XBOOLE_1:23
            .= G1.n \/ G2.n by H52,B10,H55;
         G1.n in rng G1 & rng G1 c= S by H59,FUNCT_1:12; then
         reconsider G1n = G1.n as Element of S;
         G2.n in rng G2 & rng G2 c= S by H59,FUNCT_1:12; then
         reconsider G2n = G2.n as Element of S;
H58:     now assume G1.n meets G2.n; then
          consider v be set such that
J1:        v in G1.n & v in G2.n by XBOOLE_0:3;
          v in G.n /\ A & v in G.n /\ B by J1,H52,B10,D10; then
          v in A & v in B by XBOOLE_0:def 3;
          hence contradiction by A3,XBOOLE_0:3;
         end;
H60:     (M*G).n = M.(G.n) by H52,FUNCT_1:23
                .= M.G1n + M.G2n by H57,H58,MEASURE1:61
                .= (M*G1).n + M.(G2.n) by H59,FUNCT_1:23
                .= (M*G1).n + (M*G2).n by H59,FUNCT_1:23;
         M is nonnegative by MEASURE1:def 11; then
         0 <= M.G1n & 0 <= M.G2n by MEASURE1:def 4; then
         (0 = (M*G1).n or 0 < (M*G1).n) &
         (0 = (M*G2).n or 0 < (M*G2).n) by H59,FUNCT_1:23; then
         b.n*(M*G).n = b.n*(M*G1).n + b.n*(M*G2).n by H60,EXTREAL1:29; then
         y.n = b.n*(M*G1).n + b.n*(M*G2).n by H61,B4; then
         y.n = y1.n + b.n*(M*G2).n by H62,F8;
         hence y.n = y1.n + y2.n by H62,G8;
        end;
        hence thesis by H4,MESFUNC1:def 3;
       end; 
       hence integral'(M,g)=integral'(M,g1)+integral'(M,g2)
        by C6,P1,P2,H1,H2,H3,MESFUNC4:1;
      end;
    end;
   end;
end;

theorem Cor9:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL st
f is_simple_func_in S & f is nonnegative holds 0 <= integral'(M,f)
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f be PartFunc of X,ExtREAL;
   assume that
A1:f is_simple_func_in S and
A2:f is nonnegative;
A2a:for x be set st x in dom f holds 0 <= f.x by A2,SUPINF_2:70;
   per cases;
   suppose dom f = {};
    hence 0 <= integral'(M,f) by Defintegral';
   end;
   suppose
A3: dom f <> {}; then
A4: integral'(M,f) = integral(X,S,M,f) by Defintegral';
    consider F be Finite_Sep_Sequence of S,
             a,x be FinSequence of ExtREAL such that
A5:  F,a are_Re-presentation_of f &
     dom x = dom F &
     (for n be Nat st n in dom x holds x.n=a.n*(M*F).n) &
     integral(X,S,M,f)=Sum x by A1,A2a,A3,MESFUNC4:4;
    for n be Nat st n in dom x holds 0 <= x.n
    proof
     let n be Nat; assume
A6:  n in dom x;
     per cases;
     suppose F.n = {}; then
      M.(F.n) = 0 by MEASURE1:def 11; then
      (M*F).n = 0 by A5,A6,FUNCT_1:23; then
      a.n*(M*F).n = 0 by EXTREAL1:22;
      hence 0 <= x.n by A6,A5;
     end;
     suppose F.n <> {}; then
      consider v be set such that
A7:    v in F.n by XBOOLE_0:def 1;
      f.v = a.n by A5,A6,A7,MESFUNC3:def 1; then
A9:   0 <= a.n by A2,SUPINF_2:70;
      F.n in rng F & rng F c= S by A5,A6,FUNCT_1:12; then
      reconsider Fn=F.n as Element of S;
      M is nonnegative by MEASURE1:def 11; then
      0 <= M.Fn by MEASURE1:def 4; then
      0 <= (M*F).n by A5,A6,FUNCT_1:23; then
      0 <= a.n*(M*F).n by A9,EXTREAL2:45;
      hence 0 <= x.n by A6,A5;
     end;
    end;
    hence 0 <= integral'(M,f) by A4,A5,Cor8;
   end;
end;

Diff:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL 
  st f is_simple_func_in S & dom f <> {} & f is nonnegative & 
     g is_simple_func_in S & dom g = dom f & g is nonnegative & 
     (for x be set st x in dom f holds g.x <= f.x)
 holds 
    f-g is_simple_func_in S & dom (f-g) <> {} &
    f-g is nonnegative &
    integral(X,S,M,f)=integral(X,S,M,f-g)+integral(X,S,M,g)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL such that
AF1:f is_simple_func_in S and 
AF2:dom f <> {} and
AF3:f is nonnegative and
AG1:g is_simple_func_in S and
AG20:dom g = dom f and
AG3:g is nonnegative and
AG4:for x be set st x in dom f holds g.x <= f.x;
XF:for x be set st x in dom f holds 0 <= f.x by AF3,SUPINF_2:70; then
   consider F be Finite_Sep_Sequence of S,
   a,x be FinSequence of ExtREAL such that
F1: F,a are_Re-presentation_of f and
    dom x = dom F and
    (for n be Nat st n in dom x holds x.n=a.n*(M*F).n) and
    integral(X,S,M,f)=Sum(x) by AF1,AF2,MESFUNC4:4;
XG:for x be set st x in dom g holds 0 <= g.x by AG3,SUPINF_2:70; then
   consider G be Finite_Sep_Sequence of S,
   b,y be FinSequence of ExtREAL such that
G1: G,b are_Re-presentation_of g and
    dom y = dom G and
   (for n be Nat st n in dom y holds y.n=b.n*(M*G).n) and
   integral(X,S,M,g)=Sum(y) by AG1,AG20,AF2,MESFUNC4:4;
   set la = len a;
   set lb = len b;
F7:dom f = union rng F & dom g = union rng G &
   dom F = dom a & dom G = dom b by F1,G1,MESFUNC3:def 1;
   deffunc FG1(Nat) =
   F.(($1-'1) div lb + 1) /\ G.(($1-'1) mod lb + 1);
   consider FG be FinSequence such that
FG1:len FG = la*lb and
FG2:for k be Element of NAT st k in Seg (la*lb) holds FG.k=FG1(k)
from FINSEQ_1:sch 2;
   FG is FinSequence of S
   proof
P01:now let k be Element of NAT;
     assume k in dom FG; then
P00: k in Seg (la*lb) by FG1,FINSEQ_1:def 3; then
P0:  FG.k = F.((k -' 1) div lb + 1) /\ G.((k-'1) mod lb + 1) by FG2;
     set i=(k -' 1) div lb + 1;
     set j=(k -' 1) mod lb + 1;
Q0:  i >= 0+1 & j >= 0+1 by XREAL_1:8;
Q1:  1 <= k & k <= la*lb by P00,FINSEQ_1:3;
Q8:  lb <> 0 by Q1; then
Q7:  lb > 0; then
     lb >= 0+1 by NAT_1:38; then
     lb divides la*lb & 1 <= la*lb & 1 <= lb
       by Q1,NAT_1:def 3,XREAL_1:2; then
Q3:  ((la*lb) -' 1) div lb = ((la*lb) div lb) - 1 by NAT_2:17;
     (k -' 1) <= (la*lb -' 1) by Q1,JORDAN3:5; then
     (k -' 1) div lb <= (la*lb -' 1) div lb by NAT_2:26; then
Q4:  (k -' 1) div lb + 1 <= la*lb div lb by Q3,XREAL_1:21;
Pa1: i <= la by Q8,Q4,NAT_1:68;
     (k -' 1) mod lb < lb by Q7,NAT_1:46; then
     j <= lb by NAT_1:38; then
     i in Seg la & j in Seg lb by Pa1,Q0; then
     i in dom F & j in dom G by F7,FINSEQ_1:def 3; then
     F.i in rng F & G.j in rng G by FUNCT_1:12; then
     F.i is_measurable M & G.j is_measurable M by MEASURE1:def 12; then
     F.i /\ G.j is_measurable M by MEASURE1:66;
     hence FG.k in S by P0,MEASURE1:def 12;
    end;
    let b be set; assume b in rng FG;
    then ex a be set st a in dom FG & b = FG.a by FUNCT_1:def 5;
    hence thesis by P01;
   end; then 
   reconsider FG as FinSequence of S;
   for k,l be Nat st k in dom FG & l in dom FG & k <> l holds
    FG.k misses FG.l 
   proof
    let k,l be Nat;
    assume 
AS2: k in dom FG & l in dom FG & k <> l;
    set i=(k-'1) div lb + 1;
    set j=(k-'1) mod lb + 1;
    set n=(l-'1) div lb + 1;
    set m=(l-'1) mod lb + 1;
P00:k in Seg (la*lb) & l in Seg (la*lb) by AS2,FG1,FINSEQ_1:def 3; then
Q1: 1 <= k & k <= la*lb by FINSEQ_1:3; then
Q8: lb <> 0; then
Q7: lb > 0; then
    lb >= 0+1 by NAT_1:38; then
    lb divides la*lb & 1 <= la*lb & 1 <= lb
       by Q1,NAT_1:def 3,XREAL_1:2; then
Q3: ((la*lb) -' 1) div lb = ((la*lb) div lb) - 1 by NAT_2:17;
    k -' 1 <= la*lb -' 1 by Q1,JORDAN3:5; then
    (k -' 1) div lb <= (la*lb div lb) - 1 by Q3,NAT_2:26; then
    (k -' 1) div lb + 1 <= la*lb div lb by XREAL_1:21; then
Pa1:i <= la by Q8,NAT_1:68;
R0: i >= 0+1 & j >= 0+1 & n >= 0+1 & m >= 0+1 by XREAL_1:8;
    (k -' 1) mod lb < lb & (l -' 1) mod lb < lb by Q7,NAT_1:46; then
    j <= lb & m <= lb by NAT_1:38; then
    i in Seg la & j in Seg lb & m in Seg lb by Pa1,R0; then
Pa2:i in dom F & j in dom G & m in dom G by F7,FINSEQ_1:def 3;
R1: 1 <= l & l <= la*lb by P00,FINSEQ_1:3; then
    l -' 1 <= la*lb -' 1 by JORDAN3:5; then
    (l -' 1) div lb <= (la*lb div lb) - 1 by Q3,NAT_2:26; then
    (l -' 1) div lb + 1 <= la*lb div lb by XREAL_1:21; then
    n <= la by Q8,NAT_1:68; then
    n in Seg la by R0; then
Pc2:n in dom F by F7,FINSEQ_1:def 3;
Q0: now assume QA:i=n & j=m;
     (k-'1)+1=(i-1)*lb+(j-1)+1 & (l-'1)+1=(n-1)*lb+(m-1)+1 by Q7,NAT_1:47; then
     k - 1 + 1 = (i-1)*lb + j & l - 1 + 1 = (n-1)*lb+m by Q1,R1,BINARITH:50;
     hence contradiction by AS2,QA;
    end;
    FG.k = F.i /\ G.j by P00,FG2; then
XX: FG.k /\ FG.l= F.i /\ G.j /\ (F.n /\ G.m) by P00,FG2
               .= F.i /\ (G.j /\ (F.n /\ G.m)) by XBOOLE_1:16
               .= F.i /\ (F.n /\ (G.j /\ G.m)) by XBOOLE_1:16
               .= F.i /\ F.n /\ (G.j /\ G.m) by XBOOLE_1:16;
    per cases by Q0;
    suppose i <> n; then
     F.i misses F.n by Pa2,Pc2,MESFUNC3:4; then
     FG.k /\ FG.l= {} /\ (G.j /\ G.m) by XX,XBOOLE_0:def 7;
     hence FG.k misses FG.l by XBOOLE_0:def 7;
    end;
    suppose j <> m; then
     G.j misses G.m by Pa2,MESFUNC3:4; then
     FG.k /\ FG.l= (F.i /\ F.n) /\ {} by XX,XBOOLE_0:def 7;
     hence FG.k misses FG.l by XBOOLE_0:def 7;
    end;
   end; then
   reconsider FG as Finite_Sep_Sequence of S by MESFUNC3:4;
   defpred PA[Nat,set] means
    (F.(($1 -' 1) div lb + 1) = {} implies $2 = 0) &
    (F.(($1 -' 1) div lb + 1) <> {} implies $2 = a.(($1 -' 1) div lb + 1));
XX1:for k be Element of NAT st k in Seg len FG
    ex v be Element of ExtREAL st PA[k,v]
   proof
    let k be Element of NAT;
    assume k in Seg len FG;
    per cases;
    suppose
W1:  F.((k-'1) div lb + 1)={};
     take v = 0.;
     thus PA[k,v] by W1;
    end;
    suppose
W2:  F.((k-'1) div lb + 1)<>{};
     take v = a.((k-'1) div lb + 1);
     thus PA[k,v] by W2;
    end;
   end;
   consider a1 be FinSequence of ExtREAL such that
XX2:dom a1 = Seg len FG &
for k be Element of NAT st k in Seg len FG holds PA[k,a1.k]
from FINSEQ_1:sch 5(XX1);
   deffunc X1(Nat) = a1.$1*(M*FG).$1;
   consider x1 be FinSequence of ExtREAL such that
FQ1: len x1 = len FG &
    for k be Element of NAT st k in Seg len FG holds x1.k=X1(k) 
     from FINSEQ_2:sch 1;
F41:dom x1 = dom FG by FQ1,FINSEQ_3:31;
F51:for n be Nat st n in dom x1 holds x1.n=a1.n*(M*FG).n
   proof
    let n be Nat;
    assume n in dom x1; then
    n in Seg len x1 by FINSEQ_1:def 3;
    hence x1.n = a1.n*(M*FG).n by FQ1;
   end;
   defpred PB[Nat,set] means
    (G.(($1 -' 1) mod lb + 1) = {} implies $2 = 0) &
    (G.(($1 -' 1) mod lb + 1) <> {} implies $2 = b.(($1 -' 1) mod lb + 1));
YY1:for k be Element of NAT st k in Seg len FG
    ex v be Element of ExtREAL st PB[k,v]
   proof
    let k be Element of NAT;
    assume k in Seg len FG;
    per cases;
    suppose
W1:  G.((k-'1) mod lb + 1)={};
     take v = R_EAL 0;
     thus PB[k,v] by W1;
    end;
    suppose
W2:  G.((k-'1) mod lb + 1)<>{};
     take v = b.((k-'1) mod lb + 1);
     thus PB[k,v] by W2;
    end;
   end;
   consider b1 be FinSequence of ExtREAL such that
YY2:dom b1 = Seg len FG &
for k be Element of NAT st k in Seg len FG holds PB[k,b1.k]
from FINSEQ_1:sch 5(YY1);
   deffunc Y1(Nat) = b1.$1*(M*FG).$1;
   consider y1 be FinSequence of ExtREAL such that
GQ1: len y1 = len FG &
for k be Element of NAT st k in Seg len FG holds y1.k=Y1(k)
from FINSEQ_2:sch 1;
G41:dom y1 = dom FG by GQ1,FINSEQ_3:31;
G51:now let n be Nat;
    assume n in dom y1; then
    n in Seg len y1 by FINSEQ_1:def 3;
    hence y1.n = b1.n*(M*FG).n by GQ1;
   end;
   deffunc C1(Nat) = a1.$1-b1.$1;
   consider c1 be FinSequence of ExtREAL such that
AC1:len c1 = len FG and
AC2:for k be Element of NAT st k in Seg len FG holds c1.k=C1(k)
from FINSEQ_2:sch 1;
   deffunc Z1(Nat) = c1.$1*(M*FG).$1;
   consider z1 be FinSequence of ExtREAL such that
Q1: len z1 = len FG  &
for k be Element of NAT st k in Seg len FG holds z1.k=Z1(k)
from FINSEQ_2:sch 1;
FG41:dom z1 = dom FG by Q1,FINSEQ_3:31;
FG51:for n be Nat st n in dom z1 holds z1.n=c1.n*(M*FG).n
   proof
    let n be Nat;
    assume n in dom z1; then
    n in Seg len z1 by FINSEQ_1:def 3;
    hence z1.n = c1.n*(M*FG).n by Q1;
   end;
F00:dom f = union rng FG
   proof
    thus dom f c= union rng FG
    proof
     let z be set;
     assume AS0:z in dom f; then
     consider Y be set such that
K0:  z in Y & Y in rng F by F7,TARSKI:def 4;
     consider i be Element of NAT such that
K1:  i in dom F & Y = F.i by K0,FINSEQ_2:11;
     consider Z be set such that
L0:  z in Z & Z in rng G by F7,AS0,AG20,TARSKI:def 4;
     consider j be Element of NAT such that
L1:  j in dom G & Z = G.j by L0,FINSEQ_2:11;
     i in Seg len a & j in Seg len b by F7,K1,L1,FINSEQ_1:def 3; then
LL0: 1 <= i & i <= la & 1 <= j & j <= lb by FINSEQ_1:3; then
     consider i' being Nat such that
LL1: i = 1 + i' by NAT_1:28;
     i'*lb + j is Nat; then
     reconsider k=(i-1)*lb+j as Element of NAT by LL1, ORDINAL1:def 13;
     consider j' being Nat such that
LL3:  j = 1 + j' by LL0,NAT_1:28;
     i'*lb >= 0; then
     k >= 0 + j by LL1,XREAL_1:8; then
L6:  k >= 1 by LL0,XREAL_1:2; then
     k -' 1 = k - 1 by BINARITH:50; then
LL6: k -' 1 = i'*lb + j' by LL1,LL3;
LL7: j' < lb by LL0,LL3,NAT_1:38; then
L2:  i = (k-'1) div lb +1 by LL1,LL6,NAT_1:def 1;
L3:  j = (k-'1) mod lb +1 by LL3,LL6,LL7,NAT_1:def 2;
     i-1 <= la-1 by LL0,XREAL_1:11; then
     (i-1)*lb <= (la - 1)*lb by XREAL_1:66; then
L4:  k <= (la - 1) * lb + j by XREAL_1:8;
L5:  (la - 1) * lb + j <= (la - 1) * lb + lb by LL0,XREAL_1:8;
     k <= la*lb by L4,L5,XREAL_1:2; then
M0:  k in Seg (la*lb) by L6; then
     k in dom FG by FG1,FINSEQ_1:def 3; then
M4:  FG.k in rng FG by FUNCT_1:def 5;
     z in F.i /\ G.j by K0,K1,L0,L1,XBOOLE_0:def 3; then
     z in FG.k by M0,L2,L3,FG2;
     hence thesis by M4,TARSKI:def 4;
    end;
    let z be set;
    assume z in union rng FG; then
    consider Y be set such that
K0:  z in Y & Y in rng FG by TARSKI:def 4;
    consider k be Element of NAT such that
K1:  k in dom FG & Y = FG.k by K0,FINSEQ_2:11;
    set i=(k-'1) div lb +1;
    set j=(k-'1) mod lb +1;
    k in Seg (la*lb) by FG1,K1,FINSEQ_1:def 3; then
    FG.k=F.i /\ G.j by FG2; then
M2: z in F.i by K0,K1,XBOOLE_0:def 3;
    k in Seg len FG by K1,FINSEQ_1:def 3; then
M3: 1 <= k & k <= la*lb by FG1,FINSEQ_1:3;
Q0: i >= 0+1 & j >= 0+1 by XREAL_1:8;
Q8: lb <> 0 by M3; then
    lb > 0; then
    lb >= 0+1 by NAT_1:38; then
    lb divides la*lb & 1 <= la*lb & 1 <= lb
      by M3,NAT_1:def 3,XREAL_1:2; then
Q3: ((la*lb) -' 1) div lb = ((la*lb) div lb) - 1 by NAT_2:17;
    (k -' 1) <= (la*lb -' 1) by M3,JORDAN3:5; then
    (k -' 1) div lb <= (la*lb div lb) - 1 by Q3,NAT_2:26; then
Q6: i <= la*lb div lb by XREAL_1:21;
    la*lb div lb = la by Q8,NAT_1:68; then
    i in Seg la by Q0,Q6; then
    i in dom F by F7,FINSEQ_1:def 3; then
    F.i in rng F by FUNCT_1:def 5;
    hence thesis by F7,M2,TARSKI:def 4;
   end;
   g is_finite by AG1,MESFUNC2:def 5; then
SF1:dom(f-g) = dom f /\ dom g by MESFUNC2:2;
SFG0:f-g is_finite
    proof
     now let x be Element of X;
      assume A0: x in dom (f-g); then
  P3: |. (f-g).x .| = |. f.x - g.x .| by MESFUNC1:def 4;
      f is_finite & g is_finite by AF1,AG1,MESFUNC2:def 5; then
  P1: |. f.x .| < +infty & |. g.x .| < +infty 
         by A0,SF1,AG20,MESFUNC2:def 1; then
      not (f.x = +infty or f.x = -infty) &
      not (g.x = +infty or g.x = -infty) by EXTREAL2:67; then
  P4: |. (f-g).x .| <= |. f.x .| + |. g.x .| by P3,EXTREAL2:69;
      |. f.x .| + |. g.x .| <> +infty by P1,SUPINF_2:8; then
      |. f.x .| + |. g.x .| < +infty by SUPINF_1:24;
      hence |. (f-g).x .| < +infty by P4,SUPINF_1:29;
     end;
     hence thesis by MESFUNC2:def 1;
    end;
    for k being Nat,x,y being Element of X st
      k in dom FG & x in FG.k & y in FG.k holds (f-g).x = (f-g).y
    proof
     let k be Nat;
     let x,y be Element of X;
     assume 
   A: k in dom FG & x in FG.k & y in FG.k; then
     k in Seg (la*lb) by FG1,FINSEQ_1:def 3; then
P0:  FG.k = F.( (k-'1) div lb + 1 ) /\ G.( (k-'1) mod lb + 1 ) by FG2;
     set i=(k-'1) div lb + 1;
     set j=(k-'1) mod lb + 1;
     k in Seg len FG by A,FINSEQ_1:def 3; then
M3:  1 <= k & k <= la*lb by FG1,FINSEQ_1:3;
Q0:  i >= 0+1 & j >= 0+1 by XREAL_1:8;
Q8:  lb <> 0 by M3; then
Q7:  lb > 0; then
     lb >= 0+1 by NAT_1:38; then
     lb divides la*lb & 1 <= la*lb & 1 <= lb
       by M3,NAT_1:def 3,XREAL_1:2; then
Q3:  ((la*lb) -' 1) div lb = ((la*lb) div lb) - 1 by NAT_2:17;
     (k -' 1) <= (la*lb -' 1) by M3,JORDAN3:5; then
     (k -' 1) div lb <= (la*lb div lb) - 1 by Q3,NAT_2:26; then
     (k -' 1) div lb + 1 <= la*lb div lb by XREAL_1:21; then
Pa1: i <= la by Q8,NAT_1:68;
     (k -' 1) mod lb < lb by Q7,NAT_1:46; then
     j <= lb by NAT_1:38; then
     i in Seg la & j in Seg lb by Q0,Pa1; then
Pa2: i in dom F & j in dom G by F7,FINSEQ_1:def 3;
X2:  x in F.i & x in G.j & y in F.i & y in G.j by A,P0,XBOOLE_0:def 3; then
X4:  f.x=a.i & f.y=a.i by Pa2,F1,MESFUNC3:def 1;
     FG.k in rng FG by A,FUNCT_1:def 5; then
X0:  x in dom (f-g) & y in dom (f-g) by F00,SF1,AG20,A,TARSKI:def 4; then
     (f-g).x= f.x-g.x by MESFUNC1:def 4
           .= a.i-b.j by X4,X2,Pa2,G1,MESFUNC3:def 1
           .= f.y-g.y by X4,X2,Pa2,G1,MESFUNC3:def 1;
     hence (f-g).x= (f-g).y by X0,MESFUNC1:def 4;
    end;
    hence
AFG1:f-g is_simple_func_in S by SFG0,F00,SF1,AG20,MESFUNC2:def 5;
    thus dom (f-g) <> {} by SF1,AG20,AF2;
AFG3:for x be set st x in dom (f-g) holds 0 <= (f-g).x
    proof
     let x be set;
     assume A:x in dom (f-g);
     g is_finite by AG1,MESFUNC2:def 5; then
     |. g.x .| < +infty by A,SF1,AG20,MESFUNC2:def 1; then
P3:  g.x < +infty & -infty < f.x by AF3,EXTREAL2:58,SUPINF_2:70;
     g.x <= f.x by AG4,SF1,AG20,A; then
     0 <= f.x - g.x by P3,EXTREAL1:40;
     hence 0 <= (f - g).x by A,MESFUNC1:def 4;
    end;
    hence f-g is nonnegative by SUPINF_2:71;
F11:for k be Nat st k in dom FG
     for x be set st x in FG.k holds f.x=a1.k
    proof
     let k be Nat;
     assume 
A:   k in dom FG;
     let x be set;
     assume
A0:  x in FG.k;
     k in Seg (la*lb) by A,FG1,FINSEQ_1:def 3; then
P0:  FG.k = F.((k-'1) div lb + 1) /\ G.((k-'1) mod lb + 1) by FG2;
     set i=(k-'1) div lb + 1;
     set j=(k-'1) mod lb + 1;
     k in Seg len FG by A,FINSEQ_1:def 3; then
M3:  1 <= k & k <= la*lb by FG1,FINSEQ_1:3;
Q0:  i >= 0+1 & j >= 0+1 by XREAL_1:8;
Q8:  lb <> 0 by M3; then
     lb > 0; then
     lb >= 0+1 by NAT_1:38; then
     lb divides la*lb & 1 <= la*lb & 1 <= lb 
      by M3,NAT_1:def 3,XREAL_1:2; then
Q3:  ((la*lb) -' 1) div lb = ((la*lb) div lb) - 1 by NAT_2:17;
     (k -' 1) <= (la*lb -' 1) by M3,JORDAN3:5; then
     (k -' 1) div lb <= (la*lb -' 1) div lb by NAT_2:26; then
Q6:  i <= la*lb div lb by Q3,XREAL_1:21;
     la*lb div lb = la by Q8,NAT_1:68; then
     i in Seg la by Q6,Q0; then
Pa2: i in dom F by F7,FINSEQ_1:def 3;
X4:  k in Seg len FG by A,FINSEQ_1:def 3;
X2:  x in F.i by A0,P0,XBOOLE_0:def 3; then
     f.x = a.i by Pa2,F1,MESFUNC3:def 1;
     hence f.x = a1.k by X2,X4,XX2;
    end;
    dom FG = dom a1 by XX2,FINSEQ_1:def 3; then
    FG,a1 are_Re-presentation_of f by F00,F11,MESFUNC3:def 1; then
P1: integral(X,S,M,f)=Sum x1 by AF1,AF2,XF,F41,F51,MESFUNC4:3;
G11:for k be Nat st k in dom FG
     for x be set st x in FG.k holds g.x=b1.k
    proof
     let k be Nat;
     assume A: k in dom FG;
     let x be set;
     assume
  A0: x in FG.k;
     k in Seg (la*lb) by A,FG1,FINSEQ_1:def 3; then
 P0: FG.k = F.( (k-'1) div lb + 1 ) /\ G.( (k-'1) mod lb + 1 ) by FG2;
     set i=(k-'1) div lb + 1;
     set j=(k-'1) mod lb + 1;
     k in Seg len FG by A,FINSEQ_1:def 3; then
M3:  1 <= k & k <= la*lb by FG1,FINSEQ_1:3;
Q0:  i >= 0+1 & j >= 0+1 by XREAL_1:8;
     lb <> 0 by M3; then
     lb > 0; then
     (k -' 1) mod lb < lb by NAT_1:46; then
     j <= lb by NAT_1:38; then
     j in Seg lb by Q0; then
Pb2: j in dom G by F7,FINSEQ_1:def 3;
 X3: x in G.j by A0,P0,XBOOLE_0:def 3;
 X4: k in Seg len FG by A,FINSEQ_1:def 3;
     thus g.x=b.j by X3,Pb2,G1,MESFUNC3:def 1
                  .=b1.k by X3,X4,YY2;
    end;
    dom FG = dom b1 by YY2,FINSEQ_1:def 3; then
    FG,b1 are_Re-presentation_of g by F00,AG20,G11,MESFUNC3:def 1; then
P2: integral(X,S,M,g)=Sum y1 by AG1,AG20,AF2,XG,G41,G51,MESFUNC4:3;
FG5:for k be Nat st k in dom FG
     for x be set st x in FG.k holds (f-g).x=c1.k
    proof
     let k be Nat;
     assume 
   A: k in dom FG;
     let x be set;
     assume 
A1:   x in FG.k;
     FG.k in rng FG by A,FUNCT_1:def 5; then
     x in dom (f-g) by F00,SF1,AG20,A1,TARSKI:def 4; then
 X0: (f-g).x= f.x-g.x by MESFUNC1:def 4;
     k in Seg len FG by A,FINSEQ_1:def 3; then
     a1.k-b1.k = c1.k by AC2; then
     a1.k-g.x = c1.k by A,A1,G11;
     hence (f-g).x =c1.k by A,A1,F11,X0;
    end;
    dom FG = dom c1 by AC1,FINSEQ_3:31; then
    FG,c1 are_Re-presentation_of (f-g) by SF1,AG20,F00,FG5,MESFUNC3:def 1; then
P3: integral(X,S,M,f-g)=Sum z1
              by AFG1,SF1,AG20,AF2,AFG3,FG41,FG51,MESFUNC4:3;
P6: for i be Nat st i in dom y1 holds 0 <= y1.i
    proof
     let i be Nat;
     assume A: i in dom y1;
P1:  y1.i=b1.i*(M*FG).i by G51,A; 
K3:  i in Seg len FG by A,GQ1,FINSEQ_1:def 3;
     set i' = (i -' 1) mod lb + 1;
M3:  1 <= i & i <= la*lb by K3,FG1,FINSEQ_1:3;
Q0:  i' >= 0+1 by XREAL_1:8;
     lb <> 0 by M3; then
Q7:  lb > 0;
     (i -' 1) mod lb < lb by Q7,NAT_1:46; then
     i' <= lb by NAT_1:38; then
     i' in Seg lb by Q0; then
K2:  i' in dom G by F7,FINSEQ_1:def 3;
     per cases;
     suppose NE1:G.i' <> {}; then
      consider x' be set such that
NE2:   x' in G.i' by XBOOLE_0:def 1;
      g.x' = b.i' by K2,NE2,G1,MESFUNC3:def 1 .= b1.i by NE1,K3,YY2; then
NE4:  0 <= b1.i by AG3,SUPINF_2:70;
      FG.i in rng FG by A,G41,FUNCT_1:12; then
      reconsider FGi = FG.i as Element of S;
      reconsider EMPTY = {} as Element of S by MEASURE1:21;
      EMPTY c= FGi by XBOOLE_1:2; then
NE6:  M.({}) <= M.FGi by MEASURE1:62;
      M.{} = 0 by MEASURE1:def 11; then
      0 <= (M*FG).i by NE6,A,G41,FUNCT_1:23;
      hence 0 <= y1.i by P1,NE4,EXTREAL2:45;
     end;
     suppose
E1:   G.i' = {};
      FG.i = F.((i-'1) div lb + 1) /\ G.i' by K3,FG1,FG2; then
      M.(FG.i) = 0 by E1,MEASURE1:def 11; then
      (M*FG).i = 0 by A,G41,FUNCT_1:23;
      hence 0 <= y1.i by P1,EXTREAL1:22;
     end;
    end;
P8: for i be Nat st i in dom x1 holds 0 <= z1.i
    proof
     let i be Nat;
     assume
A:    i in dom x1; then
R2:  (M*FG).i = M.(FG.i) by F41,FUNCT_1:23;
     FG.i in rng FG & rng FG c= S by A,F41,FUNCT_1:12; then
     reconsider V = FG.i as Element of S;
     reconsider EMPTY = {} as Element of S by PROB_1:42;
R9:  i in Seg len FG by A,FQ1,FINSEQ_1:def 3;
     EMPTY c= V by XBOOLE_1:2; then
     M.EMPTY <= M.V by MEASURE1:62; then
R3:  0 <= (M*FG).i by R2,MEASURE1:def 11;
     per cases;
     suppose FG.i <> {}; then
      consider x be set such that
R6:    x in FG.i by XBOOLE_0:def 1;
      FG.i in rng FG by A,F41,FUNCT_1:12; then
R7:   x in union rng FG by R6,TARSKI:def 4;then
      g.x <= f.x by AG4,F00; then
      g.x <= a1.i by A,F41,F11,R6; then
R8:   b1.i <= a1.i by R6,A,F41,G11;
R8b:  g is_finite by AG1,MESFUNC2:def 5;
      reconsider x as Element of X by R7,F00;
      |. g.x .| < +infty by R7,F00,AG20,R8b,MESFUNC2:def 1; then
      |. b1.i .| < +infty by R6,A,F41,G11; then
      -(+infty) < b1.i & b1.i < +infty by EXTREAL2:58; then
      b1.i <> -infty & b1.i <> +infty by SUPINF_2:def 3; then
      0 <= a1.i - b1.i by R8,MEASURE5:1; then
      0 <= c1.i by R9,AC2; then
      0 <= c1.i*(M*FG).i by R3,EXTREAL2:45;
      hence thesis by A,F41,FG41,FG51;
     end;
     suppose FG.i = {}; then
      (M*FG).i = 0 by R2,MEASURE1:def 11; then
      c1.i * (M*FG).i = 0 by EXTREAL1:22;
      hence thesis by A,F41,FG41,FG51;
     end;
    end;
    x1=z1+y1
    proof
     not -infty in rng y1
     proof
      assume -infty in rng y1; then
      consider i be set such that
E1:   i in dom y1 and
E2:   y1.i = -infty by FUNCT_1:def 5;
      i is Nat by E1,ORDINAL1:def 13;
      hence contradiction by P6,E1,E2;
     end; then
U1:  z1"{+infty} /\ y1"{-infty}
       = z1"{+infty} /\ {} by FUNCT_1:142
       .={};
     not -infty in rng z1
     proof
      assume -infty in rng z1; then
      consider i be set such that
E1:   i in dom z1 and
E2:   z1.i = -infty by FUNCT_1:def 5;
      i is Nat by E1,ORDINAL1:def 13;
      hence contradiction by P8,F41,FG41,E1,E2;
     end; then
U2:  z1"{-infty} /\ y1"{+infty}
       ={} /\ y1"{+infty} by FUNCT_1:142 .={};
P1:  dom (z1+y1)
      =(dom z1 /\ dom y1) \ ({} \/ {}) by U1,U2,MESFUNC1:def 3
     .=dom x1 by FQ1,FINSEQ_3:31,G41,FG41;
P2:  for k be Element of NAT  st k in dom x1 holds x1.k = (z1+y1).k
     proof
      let k be Element of NAT;
      assume 
A:     k in dom x1; then
P4:   k in Seg len FG by FQ1,FINSEQ_1:def 3;
      set p=(k-'1) div lb + 1;
      set q=(k-'1) mod lb + 1;
M3:   1 <= k & k <= la*lb by FG1,P4,FINSEQ_1:3;
Q0:   p >= 0+1 & q >= 0+1 by XREAL_1:8;
Q7:   lb <> 0 by M3; then
Q8:   lb > 0; then
      lb >= 0+1 by NAT_1:38; then
      lb divides la*lb & 1 <= la*lb & 1 <= lb 
        by M3,NAT_1:def 3,XREAL_1:2; then
Q3:   ((la*lb) -' 1) div lb = ((la*lb) div lb) - 1 by NAT_2:17;
      (k -' 1) <= (la*lb -' 1) by M3,JORDAN3:5; then
      (k -' 1) div lb <= (la*lb div lb) - 1 by Q3,NAT_2:26; then
      p <= la*lb div lb by XREAL_1:21; then
      p <= la by Q7,NAT_1:68; then
Pa1:  p in Seg la by Q0;
      (k -' 1) mod lb < lb by Q8,NAT_1:46; then
      q <= lb by NAT_1:38; then
      q in Seg lb by Q0; then
Q1:   p in dom F & q in dom G by F7,FINSEQ_1:def 3,Pa1;
L1:   (c1.k+b1.k)*(M*FG).k = c1.k*(M*FG).k + b1.k*(M*FG).k
      proof
       per cases;
       suppose FG.k <> {}; then
        F.p /\ G.q <> {} by P4,FG1,FG2; then
        consider v be set such that
S2:      v in F.p /\ G.q by XBOOLE_0:def 1;
S3:     v in F.p & v in G.q by S2,XBOOLE_0:def 3; then
S4:     a.p = f.v & b.q = g.v by F1,G1,Q1,MESFUNC3:def 1;
S11:    F.p <> {} & G.q <> {} by S2;
        F.p in rng F & G.q in rng G by Q1,FUNCT_1:12; then
S8:     v in dom f & v in dom g by F7,S3,TARSKI:def 4;
        0 <= b.q by S4,AG3,SUPINF_2:70; then
S7:     0 = b1.k or 0 < b1.k by S11,YY2,P4;
        reconsider v as Element of X by S8;
        g is_finite by AG1,MESFUNC2:def 5; then
        |. g.v .| < +infty by S8,MESFUNC2:def 1; then
        |. b1.k .| < +infty by S4,S11,YY2,P4; then
        -(+infty) < b1.k & b1.k < +infty by EXTREAL2:58; then
S9:     -infty < b1.k & b1.k < +infty by SUPINF_2:def 3;
        b.q <= a.p by S4,S8,AG4; then
        b1.k <= a.p by S11,YY2,P4; then
        b1.k <= a1.k by S11,XX2,P4; then
        0 <= a1.k - b1.k by S9,MEASURE5:1; then
        0 = c1.k or 0 < c1.k by AC2,P4;
        hence thesis by S7,EXTREAL1:29;
       end;
       suppose FG.k = {}; then
        M.(FG.k) = 0 by MEASURE1:def 11; then
        (M*FG).k = 0 by A,F41,FUNCT_1:23;
        hence thesis by EXTREAL1:29;
       end;
      end;
T1:   a1.k <> +infty & a1.k <> -infty & b1.k <> +infty & b1.k <> -infty
      proof
       now per cases;
        suppose
T2:      F.p <> {}; then
         consider v be set such that
T3:       v in F.p by XBOOLE_0:def 1;
         a1.k = a.p by T2,P4,XX2; then
T5:      a1.k = f.v by Q1,F1,MESFUNC3:def 1,T3;
         F.p in rng F by Q1,FUNCT_1:12; then
T4:      v in dom f by F7,T3,TARSKI:def 4; then
         reconsider v as Element of X;
         f is_finite by AF1,MESFUNC2:def 5; then
         |. f.v .| < +infty by T4,MESFUNC2:def 1; then
         -(+infty) < a1.k & a1.k < +infty by T5,EXTREAL2:58;
         hence a1.k <> +infty & -infty <> a1.k by SUPINF_2:def 3;
        end;
        suppose F.p = {};
         hence a1.k <> +infty & -infty <> a1.k by P4,XX2;
        end;
       end;
       hence +infty <> a1.k & a1.k <> -infty;
       now per cases;
        suppose 
      T2:G.q <> {}; then
         consider v be set such that
T3:       v in G.q by XBOOLE_0:def 1;
         b1.k = b.q by T2,P4,YY2; then
T5:      b1.k = g.v by G1,Q1,MESFUNC3:def 1,T3;
         G.q in rng G by Q1,FUNCT_1:12; then
T4:      v in dom g by F7,T3,TARSKI:def 4; then
         reconsider v as Element of X;
         g is_finite by AG1,MESFUNC2:def 5; then
         |. g.v .| < +infty by T4,MESFUNC2:def 1; then
         -(+infty) < b1.k & b1.k < +infty by T5,EXTREAL2:58;
         hence b1.k <> +infty & -infty <> b1.k by SUPINF_2:def 3;
        end;
        suppose G.q = {};
         hence b1.k <> +infty & -infty <> b1.k by P4,YY2;
        end;
       end;
       hence +infty <> b1.k & b1.k <> -infty;
      end;
      c1.k = a1.k - b1.k by P4,AC2; then
      c1.k + b1.k = a1.k - (b1.k - b1.k) by T1,EXTREAL2:41
               .= a1.k + 0. by EXTREAL1:9,24
               .= a1.k by SUPINF_2:18;
      hence x1.k = (c1.k+b1.k)*(M*FG).k by F51,A
              .= z1.k + b1.k*(M*FG).k by F41,A,FG41,FG51,L1
              .= z1.k + y1.k by F41,G41,G51,A
              .= (z1+y1).k by P1,A,MESFUNC1:def 3;
     end;
     dom(z1+y1) = Seg len x1 by P1,FINSEQ_1:def 3; then
     z1+y1 is FinSequence by FINSEQ_1:def 2;
     hence thesis by P1,P2,FINSEQ_1:17;
    end;
   hence thesis by P1,P2,P3,P6,F41,P8,G41,FG41,MESFUNC4:1;
end;

theorem Diff1:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st 
  f is_simple_func_in S & f is nonnegative & 
  g is_simple_func_in S & g is nonnegative &
  (for x be set st x in dom(f-g) holds g.x <= f.x)
 holds 
  dom (f-g) = dom f /\ dom g & 
  integral'(M,f|dom(f-g))=integral'(M,f-g)+integral'(M,g|dom(f-g))
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL such that
AF1:f is_simple_func_in S and 
AF3:f is nonnegative and
AG1:g is_simple_func_in S and
AG3:g is nonnegative and
AG4:for x be set st x in dom(f-g) holds g.x <= f.x;
   f is without+infty & g is without-infty by AF1,AG1,WI01; then
   not +infty in rng f & not -infty in rng g by Def0001,Def0002; then
X0:f"{+infty} = {} & g"{-infty} = {} by FUNCT_1:142; then
W1:(dom f /\ dom g)
    \((f"{+infty} /\ g"{+infty})\/(f"{-infty} /\ g"{-infty}))
     = dom f /\ dom g;
   hence
P1:dom(f-g) = dom f /\ dom g by MESFUNC1:def 4;
   (-1)(#)g is_simple_func_in S by AG1,LL5; then
   -g is_simple_func_in S by MESFUNC2:11; then
   f+(-g) is_simple_func_in S by AF1,LL2; then
B0:f-g is_simple_func_in S by MESFUNC2:9;
   dom(f-g) is Element of S by B0,Cor10; then
C1:f|dom(f-g) is_simple_func_in S &
   g|dom(f-g) is_simple_func_in S by AF1,AG1,Cor5;
C2:f|dom(f-g) is nonnegative & g|dom(f-g) is nonnegative by AF3,AG3,LL0;
   dom(f|dom(f-g)) = dom f /\ dom(f-g) &
   dom(g|dom(f-g)) = dom g /\ dom(f-g) by FUNCT_1:68; then
C4:dom(f|dom(f-g)) = dom f /\ dom f /\ dom g &
   dom(g|dom(f-g)) = dom g /\ dom g /\ dom f by P1,XBOOLE_1:16;
C6:for x be set st x in dom(f|dom(f-g)) holds
    (g|dom(f-g)).x <= (f|dom(f-g)).x
   proof
    let x be set;
    assume 
C62: x in dom(f|dom(f-g)); then
    g.x <= f.x by AG4,C4,P1; then
    (g|dom(f-g)).x <= f.x by C62,C4,P1,FUNCT_1:72;
    hence (g|dom(f-g)).x <= (f|dom(f-g)).x by C62,C4,P1,FUNCT_1:72;
   end;
   thus integral'(M,f|dom(f-g))=integral'(M,f-g)+integral'(M,g|dom(f-g))
   proof
    per cases;
    suppose
B1:  dom(f-g) = {}; then
B7:  integral'(M,f-g) = 0 by Defintegral';
     dom(f|dom(f-g)) = dom f /\ dom(f-g) by FUNCT_1:68; then
B8:  integral'(M,f|dom(f-g)) = 0 by B1,Defintegral';
     dom(g|dom(f-g)) = dom g /\ dom(f-g) by FUNCT_1:68; then
     integral'(M,g|dom(f-g)) = 0 by B1,Defintegral';
     hence integral'(M,f|dom(f-g))
       = integral'(M,f-g) + integral'(M,g|dom(f-g)) by B7,B8,SUPINF_2:18;
    end;
    suppose
D1:  dom(f-g) <> {}; then
D2:  integral(X,S,M,f|dom(f-g))
      = integral(X,S,M,(f|dom(f-g) - g|dom(f-g)))
        +integral(X,S,M,g|dom(f-g)) by C4,P1,C1,C2,C6,Diff;
     (f|dom(f-g))"{+infty} = dom(f-g) /\ f"{+infty} &
     (g|dom(f-g))"{-infty} = dom(f-g) /\ g"{-infty} by FUNCT_1:139; then
     (dom(f|dom(f-g)) /\ dom(g|dom(f-g))) \
      ( ((f|dom(f-g))"{+infty} /\ (g|dom(f-g))"{+infty}) \/
       ((f|dom(f-g))"{-infty} /\ (g|dom(f-g))"{-infty}) )
      = dom(f-g) by C4,X0,W1,MESFUNC1:def 4; then
D3:  dom(f|dom(f-g) - g|dom(f-g)) = dom(f-g) by MESFUNC1:def 4;
     for x be Element of X st x in dom(f|dom(f-g) - g|dom(f-g)) holds
      (f|dom(f-g) - g|dom(f-g)).x = (f-g).x
     proof
      let x be Element of X;
      assume
D31:   x in dom(f|dom(f-g) - g|dom(f-g)); then
      (f|dom(f-g) - g|dom(f-g)).x
        = (f|dom(f-g)).x - (g|dom(f-g)).x by MESFUNC1:def 4
       .= f.x - (g|dom(f-g)).x by D31,D3,FUNCT_1:72
       .= f.x - g.x by D31,D3,FUNCT_1:72;
      hence thesis by D31,D3,MESFUNC1:def 4;
     end; then
D4:  integral(X,S,M,f|dom(f-g))
      = integral(X,S,M,f-g) + integral(X,S,M,g|dom(f-g))
        by D2,D3,PARTFUN1:34;
D5:  integral(X,S,M,f|dom(f-g)) = integral'(M,f|dom(f-g))
       by C4,P1,D1,Defintegral';
     integral(X,S,M,g|dom(f-g)) = integral'(M,g|dom(f-g))
       by C4,P1,D1,Defintegral';
     hence integral'(M,f|dom(f-g))
      = integral'(M,f-g) + integral'(M,g|dom(f-g))
         by D4,D5,D1,Defintegral';
    end;
   end;
end;

theorem Cor9a:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
f is_simple_func_in S &
g is_simple_func_in S &
f is nonnegative & g is nonnegative &
(for x be set st x in dom(f-g) holds g.x <= f.x) 
holds
  integral'(M,g|dom(f-g)) <= integral'(M,f|dom(f-g))
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f,g be PartFunc of X,ExtREAL;
   assume that
A1:f is_simple_func_in S & g is_simple_func_in S and
A3:f is nonnegative & g is nonnegative and
A4:(for x be set st x in dom(f-g) holds g.x <= f.x);
   dom f is Element of S & dom g is Element of S by A1,Cor10; then
   dom f /\ dom g in S by FINSUB_1:def 2; then
   dom(f-g) in S by A1,A3,A4,Diff1; then
A5:f|dom(f-g) is_simple_func_in S &
   g|dom(f-g) is_simple_func_in S by A1,Cor5;
A7:g|dom(f-g) is nonnegative by A3,LL0;
   (-1)(#)g is_simple_func_in S by A1,LL5; then
   -g is_simple_func_in S by MESFUNC2:11; then
   f+(-g) is_simple_func_in S by A1,LL2; then
B0:f-g is_simple_func_in S by MESFUNC2:9;
X1:integral'(M,f|dom(f-g)) = integral'(M,f-g)+integral'(M,g|dom(f-g))
     by A1,A3,A4,Diff1;
   now assume integral'(M,f|dom(f-g)) <> +infty;
    f-g is nonnegative by A1,A4,LL8; then
    0 <= integral'(M,f-g) & 0 <= integral'(M,g|dom(f-g)) by A5,A7,B0,Cor9;
    hence integral'(M,g|dom(f-g)) <= integral'(M,f|dom(f-g)) by X1,SUPINF_2:20;
   end;
   hence thesis by SUPINF_1:20;
end;

theorem Cor9c:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL, 
    c be R_eal st
 0 <= c & f  is_simple_func_in S &
 (for x be set st x in dom f holds f.x=c) 
holds
   integral'(M,f) = c*(M.(dom f))
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f be PartFunc of X,ExtREAL;
   let c be R_eal;
   assume that
A1:0 <= c and
A2:f is_simple_func_in S and
A4:for x be set st x in dom f holds f.x = c;
   reconsider A = dom f as Element of S by A2,Cor10;
B0:for x be set st x in dom f holds 0 <= f.x by A1,A4;
   per cases;
   suppose
C1: dom f = {}; then
C2: integral'(M,f) = 0 by Defintegral';
    M.A = 0 by C1,MEASURE1:def 11;
    hence thesis by C2,EXTREAL1:22;
   end;
   suppose
C3: dom f <> {};
    set F = <* dom f *>;
C5: rng F = {A} by FINSEQ_1:55;
    rng F c= S
    proof
     let z be set;
     assume z in rng F; then
     z = A by C5,TARSKI:def 1;
     hence z in S;
    end; then
    reconsider F as FinSequence of S by FINSEQ_1:def 4;
    for i,j be Nat st i in dom F & j in dom F & i <> j holds
     F.i misses F.j
    proof
     let i,j be Nat;
     assume 
C6:   i in dom F & j in dom F & i <> j;
     dom F = {1} by FINSEQ_1:4,55; then
     i = 1 & j = 1 by C6,TARSKI:def 1;
     hence thesis by C6;
    end; then
    reconsider F as Finite_Sep_Sequence of S by MESFUNC3:4;
    reconsider a = <* c *> as FinSequence of ExtREAL;
C7: dom f = union rng F by C5,ZFMISC_1:31;
C8: dom F = Seg 1 by FINSEQ_1:55 .= dom a by FINSEQ_1:55;
    for n be Nat st n in dom F
     for x be set st x in F.n holds f.x = a.n
    proof
     let n be Nat; assume 
C9:   n in dom F;
     let x be set; assume 
C10:  x in F.n;
     n in {1} by C9,FINSEQ_1:4,55; then
C11: n = 1 by TARSKI:def 1; then
     x in dom f by C10,FINSEQ_1:57; then
     f.x = c by A4;
     hence f.x = a.n by C11,FINSEQ_1:57;
    end; then
B2: F,a are_Re-presentation_of f by C7,C8,MESFUNC3:def 1;
    set x = <* c * M.A *>;
    reconsider x as FinSequence of ExtREAL;
C12:dom x = Seg 1 by FINSEQ_1:55 .= dom F by FINSEQ_1:55;
    for n be Nat st n in dom x holds x.n = a.n*(M*F).n
    proof
     let n be Nat;
     assume 
C13:  n in dom x; then
     n in {1} by FINSEQ_1:4,55; then
C14: n = 1 by TARSKI:def 1; then
C15: x.n = c*M.A by FINSEQ_1:57;
     (M*F).n = M.(F.n) by C12,C13,FUNCT_1:23
            .= M.A by C14,FINSEQ_1:57;
     hence thesis by C15,C14,FINSEQ_1:57;
    end; then
    integral(X,S,M,f) = Sum x by A2,C3,B0,B2,C12,MESFUNC4:3; then
C16:integral'(M,f) = Sum x by C3,Defintegral';
B3: 1 = len x by FINSEQ_1:57;
    for n be Nat st n in dom x holds x.n = c*M.A
    proof
     let n be Nat;
     assume n in dom x; then
     n in {1} by FINSEQ_1:4,55; then
     n = 1 by TARSKI:def 1;
     hence x.n = c*M.A by FINSEQ_1:57;
    end; then
    Sum x = R_EAL 1 *(c*M.A) by B3,MESFUNC3:18;
    hence thesis by C16,EXTREAL2:4;
   end;
end;

theorem Cor11:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL st
 f is_simple_func_in S & f is nonnegative holds
  integral'(M,f|eq_dom(f,R_EAL 0)) = 0
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f be PartFunc of X,ExtREAL;
   assume that
A1:f is_simple_func_in S and
A2:f is nonnegative;
   set A = dom f;
   set g = f|(A /\ eq_dom(f,R_EAL 0));
B1:g is_simple_func_in S
   proof
C0: f is_finite by A1,MESFUNC2:def 5;
    for x be Element of X st x in dom g holds |. g.x .| < +infty
    proof
     let x be Element of X;
     assume
C01:  x in dom g; then
     x in dom f /\ (A/\eq_dom(f,R_EAL 0)) by FUNCT_1:68; then
C02: x in dom f by XBOOLE_0:def 3;
     g.x = f.x by C01,FUNCT_1:70;
     hence thesis by C0,C02,MESFUNC2:def 1;
    end; then
C1: g is_finite by MESFUNC2:def 1;
    ex G be Finite_Sep_Sequence of S st
     (dom g = union rng G &
      for n be Nat, x,y be Element of X st
        n in dom G & x in G.n & y in G.n holds g.x = g.y)
    proof
     consider F be Finite_Sep_Sequence of S such that
C21:   dom f = union rng F &
      for n be Nat, x,y be Element of X st
       n in dom F & x in F.n & y in F.n holds f.x = f.y by A1,MESFUNC2:def 5;
     deffunc G(Nat) = F.$1 /\ (A/\eq_dom(f,R_EAL 0));
     reconsider A as Element of S by C21,MESFUNC2:34;
     f is_measurable_on A by A1,MESFUNC2:37; then
     A /\ less_dom(f,R_EAL 0) is_measurable_on S by MESFUNC1:def 17; then
     A /\ less_dom(f,R_EAL 0) in S by MESFUNC1:def 11; then
     A\(A /\ less_dom(f,R_EAL 0)) in S by PROB_1:44; then
     reconsider A1 = A/\great_eq_dom(f,0.) as Element of S by MESFUNC1:18;
     f is_measurable_on A1 by A1,MESFUNC2:37; then
     (A/\great_eq_dom(f,R_EAL 0))/\less_eq_dom(f,R_EAL 0) is_measurable_on S
       by MESFUNC1:32; then
     A /\ eq_dom(f,R_EAL 0) is_measurable_on S by MESFUNC1:22; then
     reconsider A2 = A /\ eq_dom(f,R_EAL 0) as Element of S by MESFUNC1:def 11;
     consider G be FinSequence such that
C22:  len G = len F &
for n be Element of NAT st n in Seg len F holds G.n = G(n)
from FINSEQ_1:sch 2;
C23: dom G = Seg len F by C22,FINSEQ_1:def 3; then
C24: dom G = dom F by FINSEQ_1:def 3;
     dom F = Seg len F by FINSEQ_1:def 3; then
     for i be Nat st i in dom F holds G.i = F.i /\ A2 by C22; then
     reconsider G as Finite_Sep_Sequence of S by C24,Cor6;
     take G;
     for i be Nat st i in dom G holds G.i = A2 /\ F.i by C22,C23; then
C27: union rng G = A2 /\ dom f by C21,C24,MESFUNC3:6
                .= dom g by FUNCT_1:68;
     for i be Nat, x,y be Element of X st
      i in dom G & x in G.i & y in G.i holds g.x = g.y
     proof
      let i be Nat;
      let x,y be Element of X;
      assume
C28:   i in dom G & x in G.i & y in G.i; then
      G.i = F.i /\ A2 by C23,C22; then
C30:  x in F.i & y in F.i by C28,XBOOLE_0:def 3;
      G.i in rng G by C28,FUNCT_1:12; then
      x in dom g & y in dom g by C27,C28,TARSKI:def 4; then
      g.x = f.x & g.y = f.y by FUNCT_1:70;
      hence thesis by C30,C21,C28,C24;
     end;
     hence thesis by C27;
    end;
    hence thesis by C1,MESFUNC2:def 5;
   end;
B0:for x be set st x in dom g holds 0 <= g.x
   proof
    let x be set;
    assume
B21: x in dom g;
    0 <= f.x by A2,SUPINF_2:70;
    hence thesis by B21,FUNCT_1:70;
   end;
   for x be set st x in eq_dom(f,R_EAL 0) holds x in A by MESFUNC1:def 16; then
   eq_dom(f,R_EAL 0) c= A by TARSKI:def 3; then
B3:f|(A/\eq_dom(f,R_EAL 0)) = f|eq_dom(f,R_EAL 0) by XBOOLE_1:28;
   now assume
X1: dom g <> {}; then
    consider F be Finite_Sep_Sequence of S,
             a,x be FinSequence of ExtREAL such that
C2:  F,a are_Re-presentation_of g &
     a.1 =0 &
     (for n be Nat st 2 <= n & n in dom a holds
        0 < a.n & a.n < +infty ) &
     dom x = dom F &
     (for n be Nat st n in dom x holds x.n=a.n*(M*F).n) &
     integral(X,S,M,g)=Sum(x) by B1,B0,MESFUNC3:def 2;
C4: for x be set st x in dom g holds g.x = 0
    proof
     let x be set;
     assume
C21:  x in dom g; then
     x in dom f /\ (A /\ eq_dom(f,R_EAL 0)) by FUNCT_1:68; then
     x in dom f & x in A /\ eq_dom(f,R_EAL 0) by XBOOLE_0:def 3; then
     x in eq_dom(f,R_EAL 0) by XBOOLE_0:def 3; then
     ex z be R_eal st z = f.x & 0 = z by MESFUNC1:def 16;
     hence thesis by C21,FUNCT_1:70;
    end;
C5: for n be Nat st n in dom F holds a.n = 0 or F.n = {}
    proof
     let n be Nat;
     assume
D1:   n in dom F;
     now assume F.n <> {}; then
      consider x be set such that
D2:    x in F.n by XBOOLE_0:def 1;
      F.n in rng F by D1,FUNCT_1:12; then
      x in union rng F by D2,TARSKI:def 4; then
      x in dom g by C2,MESFUNC3:def 1; then
      g.x = 0 by C4;
      hence thesis by D1,D2,C2,MESFUNC3:def 1;
     end;
     hence thesis;
    end;
C6: for n be Nat st n in dom x holds x.n = 0
    proof
     let n be Nat;
     assume
E0:   n in dom x;
     per cases by E0,C2,C5;
     suppose a.n = 0; then
      a.n*(M*F).n = 0 by EXTREAL1:22;
      hence thesis by C2,E0;
     end;
     suppose
      F.n = {}; then
      M.(F.n) = 0 by MEASURE1:def 11; then
      (M*F).n = 0 by E0,C2,FUNCT_1:23; then
      a.n*(M*F).n = 0 by EXTREAL1:22;
      hence thesis by C2,E0;
     end;
    end;
    Sum x = 0
    proof
     consider sumx be Function of NAT,ExtREAL such that
F1:   Sum x = sumx.(len x) & sumx.0 = 0 &
      for i be Element of NAT st i < len x holds sumx.(i+1)=sumx.i + x.(i+1)
       by CONVFUN1:def 5;
     now assume x <> {};
      defpred P[Nat] means
       $1 <= len x implies sumx.$1 = 0;
F7:   P[0] by F1;
F8:   for k be Nat st P[k] holds P[k+1]
      proof
       let k be Nat;
       assume
F2:     P[k];
       assume
F3:     k+1 <= len x;
        reconsider k as Element of NAT by ORDINAL1:def 13;
       k < len x by F3,NAT_1:38; then
F4:    sumx.(k+1) = sumx.k + x.(k+1) by F1;
       1 <= k+1 by NAT_1:29; then
       k+1 in Seg(len x) by F3; then
       k+1 in dom x by FINSEQ_1:def 3; then
       x.(k+1) = 0 by C6;
       hence thesis by F4,F2,F3,NAT_1:38,SUPINF_2:18;
      end;
      for i be Nat holds P[i] from NAT_1:sch 2(F7,F8);
      hence thesis by F1;
     end;
     hence thesis by F1,FINSEQ_1:25;
    end;
    hence thesis by B3,C2,X1,Defintegral';
   end;
   hence thesis by B3,Defintegral';
end;

theorem Cor11a:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    B be Element of S,
    f be PartFunc of X,ExtREAL st
 f is_simple_func_in S & M.B=0 & f is nonnegative holds
  integral'(M,f|B) = 0
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let B be Element of S; 
   let f be PartFunc of X,ExtREAL;
   assume that
A1: f is_simple_func_in S and
A3: M.B=0 and
A2: f is nonnegative;
   set A = dom f;
   set g = f|(A/\B);
B1:g is_simple_func_in S
   proof
C00:f is_finite by A1,MESFUNC2:def 5;
    for x be Element of X st x in dom g holds |. g.x .| < +infty
    proof
     let x be Element of X;
     assume
C01:  x in dom g; then
     x in dom f /\ (A/\B) by FUNCT_1:68; then
C02: x in dom f by XBOOLE_0:def 3;
     g.x = f.x by C01,FUNCT_1:70;
     hence thesis by C00,C02,MESFUNC2:def 1;
    end; then
C1: g is_finite by MESFUNC2:def 1;
    ex G be Finite_Sep_Sequence of S st
     (dom g = union rng G &
      for n be Nat, x,y be Element of X st
        n in dom G & x in G.n & y in G.n holds g.x = g.y)
    proof
     consider F be Finite_Sep_Sequence of S such that
C21:  dom f = union rng F &
      for n be Nat, x,y be Element of X st
       n in dom F & x in F.n & y in F.n holds f.x = f.y by A1,MESFUNC2:def 5;
     deffunc G(Nat) = F.$1 /\ (A/\ B);
     reconsider A as Element of S by C21,MESFUNC2:34;
     reconsider A2 = A/\B as Element of S;
     consider G be FinSequence such that
C22:  len G = len F &
for n be Element of NAT st n in Seg len F holds G.n = G(n)
from FINSEQ_1:sch 2;
C23: dom G = Seg len F by C22,FINSEQ_1:def 3; then
C24: dom G = dom F by FINSEQ_1:def 3;
     dom F = Seg len F by FINSEQ_1:def 3; then
     for i be Nat st i in dom F holds G.i = F.i /\ A2 by C22; then
     reconsider G as Finite_Sep_Sequence of S by C24,Cor6;
     take G;
     for i be Nat st i in dom G holds G.i = A2 /\ F.i by C22,C23; then
C27: union rng G = A2 /\ dom f by C21,C24,MESFUNC3:6
                .= dom g by FUNCT_1:68;
     for i be Nat, x,y be Element of X st
      i in dom G & x in G.i & y in G.i holds g.x = g.y
     proof
      let i be Nat;
      let x,y be Element of X;
      assume
C28:   i in dom G & x in G.i & y in G.i; then
      G.i = F.i /\ A2 by C22,C23; then
C30:  x in F.i & y in F.i by C28,XBOOLE_0:def 3;
      G.i in rng G by C28,FUNCT_1:12; then
      x in dom g & y in dom g by C27,C28,TARSKI:def 4; then
      g.x = f.x & g.y = f.y by FUNCT_1:70;
      hence thesis by C30,C21,C28,C24;
     end;
     hence thesis by C27;
    end;
    hence thesis by C1,MESFUNC2:def 5;
   end;
B0:for x be set st x in dom g holds 0 <= g.x
   proof
    let x be set;
    assume
B21: x in dom g;
    0 <= f.x by A2,SUPINF_2:70;
    hence thesis by B21,FUNCT_1:70;
   end; 
   dom(f|(A/\B)) = A/\(A/\B) by FUNCT_1:68; then
BB2:dom(f|(A/\B)) = (A/\A)/\B by XBOOLE_1:16; then
B22:dom(f|(A/\B)) = dom(f|B) by FUNCT_1:68;
   for x be set st x in dom(f|(A/\B)) holds (f|(A/\B)).x = (f|B).x
   proof
    let x be set;
    assume 
 B23:x in dom(f|(A/\B)); then
    (f|(A/\B)).x = f.x by FUNCT_1:70;
    hence thesis by B22,B23,FUNCT_1:70;
   end; then
B3:f|(A/\B) = f|B by B22,FUNCT_1:9;
   now per cases;
    suppose dom g = {};
     hence thesis by B3,Defintegral';
    end;
    suppose
X1:  dom g <> {}; then
     consider F be Finite_Sep_Sequence of S,
              a,x be FinSequence of ExtREAL such that
C2:   F,a are_Re-presentation_of g &
      a.1 =0 &
      (for n be Nat st 2 <= n & n in dom a holds
         0 < a.n & a.n < +infty ) &
      dom x = dom F &
      (for n be Nat st n in dom x holds x.n=a.n*(M*F).n) &
      integral(X,S,M,g)=Sum(x) by B1,B0,MESFUNC3:def 2;
C5:  for n be Nat st n in dom F holds M.(F.n) = 0
     proof
      let n be Nat;
      assume
D1:    n in dom F;
       reconsider BB=B as measure_zero of M by A3, MEASURE1:def 13;
   D11:union rng F = dom g by C2,MESFUNC3:def 1;
   D12:F.n c= union rng F 
       proof
        for v be set st v in F.n holds v in union rng F
        proof
         let v be set;
         assume
  H56:    v in F.n; 
         F.n in rng F by D1,FUNCT_1:12; 
         hence v in union rng F by H56,TARSKI:def 4; 
        end;
        hence thesis by TARSKI:def 3;
       end;
       F.n in rng F & rng F c= S by D1,FUNCT_1:12; then
       reconsider FF=F.n as Element of S;
       dom g c= B by BB2,XBOOLE_1:17; then
       FF c= BB by D11,D12, XBOOLE_1:1; then
       F.n is measure_zero of M by MEASURE1:69;
      hence M.(F.n) =0 by MEASURE1:def 13;
     end;
C6:  for n be Nat st n in dom x holds x.n = 0
     proof
      let n be Nat;
      assume
E0:    n in dom x; then
      M.(F.n) = 0 by C5,C2; then
      (M*F).n = 0 by E0,C2,FUNCT_1:23; then
      a.n*(M*F).n = 0 by EXTREAL1:22;
      hence thesis by C2,E0;
     end;
     Sum(x) = 0
     proof
      consider sumx be Function of NAT,ExtREAL such that
F1:    Sum(x) = sumx.(len x) & sumx.0 = 0 &
       for i be Element of NAT st i < len x holds sumx.(i+1)=sumx.i + x.(i+1)
        by CONVFUN1:def 5;
      now assume x <> {};
       defpred P[Nat] means
        $1 <= len x implies sumx.$1 = 0;
F7:    P[0] by F1;
F8:    for k be Nat st P[k] holds P[k+1]
       proof
        let k be Nat;
        assume
F2:      P[k];
        assume
F3:      k+1 <= len x;
         reconsider k as Element of NAT by ORDINAL1:def 13;
        k < len x by F3,NAT_1:38; then
F4:     sumx.(k+1) = sumx.k + x.(k+1) by F1;
        1 <= k+1 by NAT_1:29; then
        k+1 in Seg(len x) by F3; then
        k+1 in dom x by FINSEQ_1:def 3; then
        x.(k+1) = 0 by C6;
        hence thesis by F4,F2,F3,NAT_1:38,SUPINF_2:18;
       end;
       for i be Nat holds P[i] from NAT_1:sch 2(F7,F8);
       hence thesis by F1;
      end;
      hence thesis by F1,FINSEQ_1:25;
     end;
     hence thesis by B3,C2,X1,Defintegral';
    end;
   end;
   hence thesis;
end;

theorem Cor12:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    g be PartFunc of X,ExtREAL,
    F be Functional_Sequence of X,ExtREAL,
    L be ExtREAL_sequence st
g is_simple_func_in S & 
(for x be set st x in dom g holds 0 < g.x) &
(for n be Nat holds F.n is_simple_func_in S) &
(for n be Nat holds dom(F.n) = dom g) &
(for n be Nat holds F.n is nonnegative) &
(for n,m be Nat st n <=m holds
   for x be Element of X st x in dom g holds (F.n).x <= (F.m).x ) &
(for x be Element of X st x in dom g holds
   (F#x) is convergent & g.x <= lim(F#x) ) &
(for n be Nat holds L.n = integral'(M,F.n))
holds
    L is convergent &
    integral'(M,g) <= lim(L)
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let g be PartFunc of X,ExtREAL;
   let F be Functional_Sequence of X,ExtREAL;
   let L be ExtREAL_sequence;
   assume that
A1:g is_simple_func_in S and 
A2:for x be set st x in dom g holds 0 < g.x and
A3:for n be Nat holds F.n is_simple_func_in S and
A4:for n be Nat holds dom(F.n) = dom g and
A5:for n be Nat holds F.n is nonnegative and
A6:for n,m be Nat st n <= m holds
    for x be Element of X st x in dom g holds (F.n).x <= (F.m).x and
A7:for x be Element of X st x in dom g holds
    (F#x) is convergent & g.x <= lim(F#x) and
A8:for n be Nat holds L.n = integral'(M,F.n);
   per cases;
   suppose
A9: dom g = {};
    now let n be Nat;
     dom(F.n) = {} & F.n is_simple_func_in S by A9,A4,A3; then
     integral'(M,F.n) = 0 by Defintegral';
     hence L.n = R_EAL 0 by A8;
    end; then
B2: L is convergent_to_finite_number & lim L = R_EAL 0 by LL9;
    hence L is convergent by Defconvergent;
    thus integral'(M,g) <= lim L by B2,A9,Defintegral';
   end;
   suppose
B33: dom g <> {};
    for v be set st v in dom g holds 0 <= g.v by A2; then
    consider G be Finite_Sep_Sequence of S,
             a be FinSequence of ExtREAL such that
B44:  G,a are_Re-presentation_of g &
     a.1 = 0 &
     for n be Nat st 2 <= n & n in dom a holds
      0 < a.n & a.n < +infty by A1,MESFUNC3:14;
B55: len a <> 0
    proof
     assume len a = 0; then
B6:  dom a = Seg 0 by FINSEQ_1:def 3;
B7:  rng G = {} 
     proof
      assume rng G <> {}; then
      consider y be set such that 
B8:    y in rng G by XBOOLE_0:def 1;
      ex x be set st x in dom G & y=G.x by B8,FUNCT_1:def 5;
      hence contradiction by B6,B44,MESFUNC3:def 1,FINSEQ_1:4;
     end;   
      union rng G <> {} by B33,MESFUNC3:def 1,B44; then
      consider x be set such that
B9:   x in union rng G by XBOOLE_0:def 1;
      ex Y be set st x in Y & Y in rng G by B9,TARSKI:def 4;
      hence contradiction by B7;
    end;
    defpred PP1[Nat,set] means $2 = a.$1;
C00: for k be Element of NAT st k in Seg len a
     ex x be Element of REAL st PP1[k,x]
    proof
     let k be Element of NAT;
     assume k in Seg len a; then
C1:  1 <= k & k <= len a & k in dom a by FINSEQ_1:3,def 3;
     per cases;
     suppose C2:k = 1;
      take x = 0;
      thus x = a.k by C2,B44;
     end;
     suppose k <> 1; then
      k > 1 by C1,REAL_1:def 5; then
      k >= 1+1 by NAT_1:38; then
      0 < a.k & a.k < +infty by B44,C1; then
      reconsider x = a.k as Element of REAL by EXTREAL1:1;
      take x;
      thus x = a.k;
     end;
    end;
    consider a1 be FinSequence of REAL such that
C33: dom a1 = Seg len a &
for k be Element of NAT st k in Seg len a holds PP1[k,a1.k]
from FINSEQ_1:sch 5(C00);
C44:2 <= len a
    proof
     assume not (2<=len a); then
     len a = 1 by B55,NAT_1:71; then
     dom a = {1} by FINSEQ_1:def 3,FINSEQ_1:4; then
C5:  dom G = {1} by B44,MESFUNC3:def 1; then
C6:  1 in dom G by TARSKI:def 1;
C7:  dom g = union rng G by B44,MESFUNC3:def 1
          .=union{G.1} by C5,FUNCT_1:14
          .= G.1 by ZFMISC_1:31; then
     consider x be set such that 
C8:   x in G.1 by B33,XBOOLE_0:def 1;
     g.x=0 by B44,C8,C6,MESFUNC3:def 1;
     hence contradiction by A2,C8,C7;
    end; then
C9: 2 in dom a1 by C33; then
    a1.2 = a.2 & 2 in dom a by C33,FINSEQ_1:def 3; then
D0: a1.2 <> a.1 by B44;
    1 <= len a by C44,XREAL_1:2; then
    1 in Seg len a; then
    a.1=a1.1 by C33; then
    not a1.2 in {a1.1} & a1.2 in rng a1 by D0,C9,FUNCT_1:12,TARSKI:def 1; then
    reconsider RINGA = (rng a1)\{a1.1} as finite non empty real-membered set
      by XBOOLE_0:def 4;
    set alpha = R_EAL(min RINGA);
    set beta = R_EAL(max RINGA);
    reconsider beta1=max RINGA as Real by XREAL_0:def 1;
    min RINGA in RINGA by PRE_CIRC:def 4; then
D11: min RINGA in rng a1 & not min RINGA in {a1.1} by XBOOLE_0:def 4; then
    consider i be set such that
D22:  i in dom a1 & min RINGA = a1.i by FUNCT_1:def 5;
    reconsider i as Element of NAT by D22;
D33: i <> 1 by D11,D22,TARSKI:def 1;
    i in Seg len a1 by D22,FINSEQ_1:def 3; then
    1 <= i by FINSEQ_1:3; then
    1 < i by D33,REAL_1:def 5; then
D44: 1+1 <= i by NAT_1:38;
C60:g is_finite by A1,MESFUNC2:def 5;
P1: for n be Nat holds dom(g - F.n) = dom g
    proof
     let n be Nat;
C92: dom(F.n) = dom g by A4;
     g is without+infty & g is without-infty by A1,WI01; then
     not +infty in rng g & not -infty in rng g by Def0001,Def0002; then
C94: g"{+infty} = {} & g"{-infty} = {} by FUNCT_1:142;
     dom(g - F.n) = (dom(F.n) /\ dom g)\
              ( (F.n)"{+infty} /\ g"{+infty} \/
               (F.n)"{-infty} /\ g"{-infty} ) by MESFUNC1:def 4;
     hence dom(g - F.n) = dom g by C92,C94;
    end;
C5: for e be R_eal st R_EAL 0 < e & e < alpha holds
     ex H be SetSequence of X, MF be ExtREAL_sequence st
      (for n be Nat holds H.n = less_dom(g-(F.n),e)) &
      (for n,m be Nat st n <= m holds H.n c= H.m) &
      (for n be Nat holds H.n c= dom g) &
      (for n be Nat holds MF.n = M.(H.n)) &
      M.(dom g) = sup rng MF &
      (for n be Nat holds H.n in S)
    proof
     let e be R_eal;
     assume
C51:  R_EAL 0 < e & e < alpha;
     deffunc FFH(Nat) = less_dom(g-(F.$1),e);
     consider H be SetSequence of X such that
aC52: for n be Element of NAT holds H.n = FFH(n)
     from KURATO_2:sch 1;
C52: now let n be Nat; n in NAT by ORDINAL1:def 13;
       hence H.n = FFH(n) by aC52;
     end;
C53: for n,m be Nat st n <= m holds H.n c= H.m
     proof
      let n,m be Nat;
      assume
C81:   n <= m;
      now let x be set;
       assume x in H.n; then
C90:   x in less_dom(g-F.n,e) by C52; then
C91:   x in dom(g-F.n) &
       (ex y be R_eal st y = (g-F.n).x & y < e) by MESFUNC1:def 12; then
C98a:  (g-F.n).x = g.x - (F.n).x by MESFUNC1:def 4;
C91a:  ex y1 be R_eal st y1 = (g-F.n).x & y1 < e by C90,MESFUNC1:def 12;
C96:   dom(g-F.n) = dom g & dom(g-F.m) = dom g by P1; then
C98:   (g-F.m).x = g.x - (F.m).x by C91,MESFUNC1:def 4;
C99:   (F.n).x <= (F.m).x by C81,C91,C96,A6;
       |. g.x .| < +infty by C96,C91,C60,MESFUNC2:def 1; then
       g.x <> -infty & g.x <> +infty by EXTREAL2:67; then
       (g-F.m).x <= (g-F.n).x by C98a,C98,C99,SUPINF_2:15; then
       (g-F.m).x < e by C91a,SUPINF_1:29; then
       x in less_dom((g-F.m),e) by C91,C96,MESFUNC1:def 12;
       hence x in H.m by C52;
      end;
      hence H.n c= H.m by TARSKI:def 3;
     end;
     now let x be set;
      assume x in lim_inf H; then
      x in Union inferior_setsequence H by SETLIM_1:def 4; then
      consider V be set such that
D2:    x in V & V in rng inferior_setsequence H by TARSKI:def 4;
      consider n be set such that
D3:    n in dom inferior_setsequence H & V = (inferior_setsequence H).n
        by D2,FUNCT_1:def 5;
      reconsider n as Element of NAT by D3;
      x in H.(n+0) by D2,D3,SETLIM_1:24; then
      x in less_dom(g-F.n,e) by C52; then
      x in dom(g-F.n) by MESFUNC1:def 12;
      hence x in dom g by P1;
     end; then
D0:  lim_inf H c= dom g by TARSKI:def 3;
     dom g c= lim_inf H
     proof
      now let x be set;
       assume
D2:     x in dom g; then
       reconsider x1=x as Element of X;
D4:    F#x1 is convergent & g.x <= lim(F#x1) by D2,A7;
D3:    now assume 
T1:     F#x1 is convergent_to_-infty;
        reconsider E = e as Real by C51,MEASURE5:16;
T2:     -E < -0 by C51,XREAL_1:26; then
        consider N be Nat such that
T3:      for m be Nat st N <= m holds (F#x1).m <= R_EAL(-E) by T1,Defconverge3;
        F.N is nonnegative by A5; then
        (F#x1).N < 0 & 0 <= (F.N).x by T2,T3,SUPINF_2:70;
        hence contradiction by DefHx;
       end;
       now per cases by D3,D4,Defconvergent;
        suppose
E1:      F#x1 is convergent_to_finite_number;
         ( ex limFx be real number st lim(F#x1) = limFx &
           (for p be real number st 0<p ex n be Nat st
             for m be Nat st n<=m holds |. (F#x1).m - lim(F#x1).| < p) &
           F#x1 is convergent_to_finite_number ) or
         ( lim(F#x1) = +infty & F#x1 is convergent_to_+infty ) or
         lim(F#x1) = -infty & F#x1 is convergent_to_-infty by D4,Deflim; then
         consider limFx be real number such that
E23:      lim(F#x1) = limFx &
          (for p be real number st 0 < p
            ex n be Nat st
             for m be Nat st n <= m holds
               |. (F#x1).m - lim(F#x1) .| < p) by E1,LM011b,LM011c;
         ex N be Nat st for m be Nat st N <= m holds x1 in less_dom(g-F.m,e)
         proof
          reconsider E = e as Real by C51,MEASURE5:16;
E25:      0 < E/2 by C51,XREAL_1:217;
          set E2 = E/2;
          g.x <= lim(F#x1) by A7,D2; then
          g.x - R_EAL(E/2) <= lim(F#x1)-0. by E25,SUPINF_2:15; then
F1:       g.x - R_EAL(E/2) <= lim(F#x1) by SUPINF_2:18,EXTREAL1:24;
          consider N be Nat such that
E27:       for m be Nat st N <= m holds
            |. (F#x1).m - lim(F#x1) .| < R_EAL(E/2) by E25,E23;
E28:      for n be Nat holds 0 <= lim(F#x1) - (F#x1).n
          proof
           let n be Nat;
           now let e be real number;
            assume 0 < e; then
            consider N0 be Nat such that 
EA2:         for m be Nat st N0 <= m holds
              |. (F#x1).m - lim(F#x1) .| < R_EAL e by E23;
            reconsider N0,n1=n as Element of NAT by ORDINAL1:def 13;
            set m=max(N0,n1);
            N0 <= m by SQUARE_1:46; then
EA5:        |. (F#x1).m - lim(F#x1) .| < R_EAL e by EA2;
            (F#x1).m - lim(F#x1) <= |. (F#x1).m - lim(F#x1) .| 
              by EXTREAL2:57; then
            (F#x1).m - lim(F#x1) < R_EAL e by XXREAL_0:2,EA5; then
EA8:        (F#x1).m < R_EAL e + lim(F#x1) by EXTREAL2:29,E23;
            n <= m by SQUARE_1:46; then
            (F.n).x1 <= (F.m).x1 by A6,D2; then
            (F.n).x1 <= (F#x1).m by DefHx; then    
            (F#x1).n <= (F#x1).m by DefHx;
            hence (F#x1).n < lim(F#x1) + R_EAL e by EA8,XXREAL_0:2;
           end; then
L1:        (F#x1).n <= lim(F#x1) by AR01t;
           -infty <= lim(F#x1) & lim(F#x1) <= +infty by SUPINF_1:20,21; then
L2:        -infty < lim(F#x1) & lim(F#x1) < +infty by SUPINF_1:22,E23; then
           (F#x1).n < +infty by L1,XXREAL_0:2;
           hence 0 <= lim(F#x1) - (F#x1).n by L1,L2,EXTREAL1:40;
          end;
E29:      now let n be Nat;
P1:        0 <= lim(F#x1) - (F#x1).n by E28;
           thus |. (F#x1).n - lim(F#x1) .| = |. lim(F#x1)- (F#x1).n .| by AR01
           .= lim(F#x1) - (F#x1).n by P1,EXTREAL1:def 3;
          end;  
F2:       now let m be Nat; assume
P1:        N <= m; then
           |. (F#x1).m - lim(F#x1) .| < R_EAL(E/2) by E27; then
           (F#x1).m <> +infty & (F#x1).m <> -infty by E23,AR01a; then
E26d:      (F.m).x <> +infty & (F.m).x <> -infty by DefHx;
           |. (F#x1).m - lim(F#x1) .| = lim(F#x1) - (F#x1).m by E29; then   
           lim(F#x1) - (F#x1).m < R_EAL(E/2) by P1,E27; then
           lim(F#x1) - (F.m).x1 < R_EAL(E/2) by DefHx;
           hence lim(F#x1) < (F.m).x + R_EAL(E/2) by E26d,EXTREAL2:29;
          end;
          now let m be Nat such that 
LP1:       N <= m;
LP2:       x1 in dom(g - F.m) by P1,D2;
LP3:       g.x <= lim(F#x1)+R_EAL(E/2) by F1,EXTREAL2:33;
           lim(F#x1) < (F.m).x + R_EAL(E/2) by F2,LP1; then
           lim(F#x1) + R_EAL(E/2) < (F.m).x + R_EAL(E/2)+ R_EAL(E/2) 
            by AR05p; then
           g.x < (F.m).x1 +R_EAL(E/2) +R_EAL(E/2) by LP3,XXREAL_0:2; then
           g.x < (F.m).x1 +(R_EAL(E/2) +R_EAL(E/2) ) by EXTREAL1:8; then
           g.x < (F.m).x1 +R_EAL(E/2+E/2) by Cor1; then
           g.x - (F.m).x1 < e by EXTREAL2:30; then
           (g-F.m).x1 < e by LP2,MESFUNC1:def 4;
           hence x1 in less_dom(g-F.m,e) by LP2,MESFUNC1:def 12;
          end;
          hence thesis;
         end; then
         consider N be Nat such that
E25:      for m be Nat st N <= m holds x in less_dom(g-F.m,e);
         reconsider N as Element of NAT by ORDINAL1:def 13;
         now let k be Element of NAT;
          N+k >= N by NAT_1:29; then
          x in less_dom(g-F.(N+k),e) by E25;
          hence x in H.(N+k) by C52;
         end; then
E8:      x in (inferior_setsequence H).N by SETLIM_1:24;
         dom inferior_setsequence H = NAT by FUNCT_2:def 1;
         hence ex N be Nat st
               N in dom inferior_setsequence H &
               x in (inferior_setsequence H).N by E8;
        end;
        suppose
SS0:     F#x1 is convergent_to_+infty;
         ex N be Nat st for m be Nat st N <=m holds g.x1 - (F.m).x1 < e  
         proof
          e in REAL by C51,MEASURE5:16; then
AP1:      e <> +infty & e <> -infty by SUPINF_1:17,18;
          per cases;
          suppose
S1:        g.x1-e <= 0;
           consider N be Nat such that 
S13:        for m be Nat st N <= m holds R_EAL 1 <= (F#x1).m 
               by SS0,Defconverge2;
           now let m be Nat;
            assume N <=m; then
            g.x1-e < (F#x1).m by S1,S13; then
            g.x1 < (F#x1).m+e  by AP1,EXTREAL2:29; then 
            g.x1 - (F#x1).m < e by AP1,EXTREAL2:30;
            hence g.x1 - (F.m).x1 < e by DefHx;
           end;
           hence ex N be Nat st for m be Nat st N <=m holds g.x1-(F.m).x1 < e;
          end;
          suppose
S2:        0 < g.x1-e;
           |. g.x .| < +infty by D2,C60,MESFUNC2:def 1; then
           g.x <> -infty & g.x <> +infty by EXTREAL2:67; then 
           reconsider gx1=g.x as Real by EXTREAL1:1;
           reconsider e1=e as Real by C51,MEASURE5:16;
           g.x-e = gx1-e1 by SUPINF_2:5; then
           reconsider ee=g.x1-e as Real;
S23:       0 < ee+1 & ee < ee+1 by S2,XREAL_1:31;
S25:       R_EAL ee < R_EAL(ee+1) by XREAL_1:31;
           consider N be Nat such that 
S13:        for m be Nat st N <= m holds
             R_EAL(ee+1) <= (F#x1).m by S23,SS0,Defconverge2;
           now let m be Nat;
            assume N <=m; then
            R_EAL(ee+1) <= (F#x1).m by S13; then
            R_EAL ee < (F#x1).m by S25,XXREAL_0:2; then
            g.x1 < (F#x1).m+e by AP1,EXTREAL2:29; then
            g.x1 - (F#x1).m < e by AP1,EXTREAL2:30;
            hence g.x1 - (F.m).x1 < e by DefHx;
           end;
           hence ex N be Nat st for m be Nat st N <=m holds g.x1-(F.m).x1 < e;
          end;
         end; then
         consider N be Nat such that 
T1:       for m be Nat st N <=m holds g.x1 - (F.m).x1 < e;  
          reconsider N as Element of NAT by ORDINAL1:def 13;
E25:     now let m be Nat;
          assume N <= m; then
LP1:      g.x1 - (F.m).x1 < e  by T1;
          x1 in dom(g - F.m) by P1,D2; then
          x1 in dom(g-F.m) & (g-F.m).x1 < e by LP1,MESFUNC1:def 4;
          hence x1 in less_dom(g-F.m,e) by MESFUNC1:def 12;
         end;
         now let k be Element of NAT;
          N+k >= N by NAT_1:29; then
          x in less_dom((g-F.(N+k)),e) by E25;
          hence x in H.(N+k) by C52;
         end; then
E8:      x in (inferior_setsequence H).N by SETLIM_1:24;
         dom inferior_setsequence H = NAT by FUNCT_2:def 1;
         hence ex N be Nat st N in dom inferior_setsequence H &
                             x in (inferior_setsequence H).N by E8;
        end;
       end; then
       consider N be Nat such that
D1:     N in dom inferior_setsequence H & x in (inferior_setsequence H).N;
       (inferior_setsequence H).N in rng inferior_setsequence H
          by D1,FUNCT_1:12; then
       x in Union inferior_setsequence H by D1,TARSKI:def 4;
       hence x in lim_inf H by SETLIM_1:def 4;
      end;
      hence dom g c= lim_inf H by TARSKI:def 3;
     end; then
LH1: lim_inf H = dom g by D0,XBOOLE_0:def 10;
SP1: for n be Nat holds H.n c= dom g
     proof
      let n be Nat;
      now let x be set;
       assume x in H.n; then
       x in less_dom(g-(F.n),e) by C52; then
       x in dom(g-F.n) by MESFUNC1:def 12;
       hence x in dom g by P1;
      end;
      hence thesis by TARSKI:def 3;
     end;
     for n,m be Element of NAT st n <= m holds H.n c= H.m by C53;
     then
SP2: H is non-decreasing by PROB_1:def 7;
SP3: Union H c= dom g   
     proof
      let x be set;
      assume x in Union H; then 
      consider n be Element of NAT such that 
L1:    x in H.n by PROB_1:25;
      H.n c= dom g by SP1;
      hence x in dom g by L1;
     end;
SP6: lim_inf H c= lim_sup H by KURATO_2:9;
     deffunc U(Nat) = M.(H.$1);
     consider MF be ExtREAL_sequence such that
aC56: for n be Element of NAT holds MF.n = U(n) from FUNCT_2:sch 4;
C56: now let n be Nat; n in NAT by ORDINAL1:def 13;
       hence MF.n = U(n) by aC56;
     end;
WW:  M.(dom g) = sup rng MF & (for n be Element of NAT holds H.n in S)
     proof
L1:    now let x be set; 
         assume x in NAT; then
         reconsider n=x as Element of NAT;
AA4:     F.n is_simple_func_in S by A3; then
         consider GF being Finite_Sep_Sequence of S such that 
AA7:     dom(F.n) = union rng GF &
         for m being Nat,x,y being Element of X st
         m in dom GF & x in GF.m & y in GF.m holds (F.n).x = (F.n).y
         by MESFUNC2:def 5;
         reconsider DGH=union rng GF as Element of S by MESFUNC2:34;
AA5:     g is_finite & F.n is_finite by A1,AA4,MESFUNC2:def 5;
AA12:    g is_measurable_on DGH by A1,MESFUNC2:37;
         reconsider E = e as Real by C51,MEASURE5:16;
         F.n is_measurable_on DGH by AA4,MESFUNC2:37; then
         g-F.n is_measurable_on DGH by MESFUNC2:13,AA12,AA5,AA7; then
         DGH /\ less_dom(g-F.n,R_EAL E) is_measurable_on S
         by MESFUNC1:def 17; then
B2:      DGH /\ less_dom(g-F.n,R_EAL E) in S by MESFUNC1:def 11;
LL:      less_dom(g-F.n,R_EAL E) c= dom(g-F.n)
         proof
           let x be set;
           assume x in less_dom(g-F.n,R_EAL E);
           hence x in dom(g-F.n) by MESFUNC1:def 12;
         end;
         dom(F.n) = dom g by A4; then
         DGH /\ less_dom(g-F.n,R_EAL E) = dom(g-F.n) /\ less_dom(g-F.n,R_EAL E)
         by P1,AA7; then
         DGH /\ less_dom(g-F.n,R_EAL E) = less_dom(g-F.n,R_EAL E) 
         by XBOOLE_1:28,LL;
         hence H.x in S by B2,C52;
       end;
       dom H = NAT by FUNCT_2:def 1; then
       reconsider HH= H as Function of NAT,S by L1,FUNCT_2:5;
K0:    now let n be Element of NAT;
         n <= n+1 by NAT_1:29;
         hence HH.n c= HH.(n+1) by C53;
       end;
       lim_sup H = Union H by SETLIM_1:65,SP2; then
K5:    M.(union rng H) = M.(dom g) by SP6,LH1,SP3,XBOOLE_0:def 10;
L0:    dom MF = NAT & dom H = NAT by FUNCT_2:def 1;
       rng HH c= S by RELSET_1:12;then
L3:    rng H c= dom M by FUNCT_2:def 1;
L4:    for x be set holds x in dom MF iff x in dom H & H.x in dom M
       proof
         let x be set;
         now assume 
A2:        x in dom MF; then
           H.x in rng H by L0,FUNCT_1:12;
           hence x in dom H & H.x in dom M by A2,L3,L0;
         end;
         hence thesis by L0;  
       end;
       for x be set st x in dom MF holds MF.x = M.(H.x) by aC56; then
       M*H =MF by L4,FUNCT_1:20;
       hence thesis by L1,K0,K5,MEASURE2:27;
     end;
     now let n be Nat; n in NAT by ORDINAL1:def 13;
       hence H.n in S by WW;
     end;
     hence thesis by SP1,C52,C53,WW,C56;
   end;
C0: i in dom a & a.i = R_EAL(a1.i) by C33,D22,FINSEQ_1:def 3; then
C1: 0 < alpha by B44,D44,D22;
C2: for x be set st x in dom g holds alpha <= g.x & g.x <= beta
    proof
     let x be set;
     assume 
C2A:  x in dom g; then
     x in union rng G by B44,MESFUNC3:def 1; then
     consider Y be set such that
C21:  x in Y & Y in rng G by TARSKI:def 4;
     consider k be set such that
C22:  k in dom G & Y = G.k by C21,FUNCT_1:def 5;
     reconsider k as Element of NAT by C22;
     k in dom a by B44,C22,MESFUNC3:def 1; then
C24: k in Seg len a by FINSEQ_1:def 3; then
P1:  a1.k in rng a1 by C33,FUNCT_1:12;
     now assume S1:a1.k=a1.1;
      1 <= len a by C44,XREAL_1:2; then
C7:   1 in dom a1 by C33;
      a.k=a1.k by C33,C24; then
S2:   a.k=a.1 by C7,C33,S1;
      g.x = a.k by C21,C22,B44,MESFUNC3:def 1;
      hence contradiction by C2A,A2,S2,B44;
     end; then
     not a1.k in {a1.1} by TARSKI:def 1; then
L1:  a1.k in RINGA by P1,XBOOLE_0:def 4;
     g.x = a.k by C21,C22,B44,MESFUNC3:def 1 .= a1.k by C33,C24;
     hence thesis by L1,PRE_CIRC:def 1,def 4;
    end;
    per cases;
    suppose
D1:  M.(dom g) <> +infty;
     reconsider DG=dom g as Element of S by Cor10,A1;
K5a: M.{} = 0 by MEASURE1:def 11;
     {} is_measurable M by MEASURE1:66; then
K5b: {} in S by MEASURE1:def 12;
     {} c= dom g & dom g is Element of S by Cor10,A1,XBOOLE_1:2; then
D02a:M.(dom g) <> -infty & M.(dom g) <> +infty by D1,K5a,K5b,MEASURE1:62;
     for x be set st x in dom g holds 0 <= g.x by A2; then
D03: g is nonnegative by SUPINF_2:71;
D06: 0 < beta
     proof
      consider x be set such that
A:     x in dom g by B33,XBOOLE_0:def 1;
      alpha <= g.x & g.x <= beta by C2,A;
      hence 0 < beta by C0,B44,D44,D22;
     end;
D05: integral'(M,g) <= beta*(M.DG)
     proof
      consider GP be PartFunc of X,ExtREAL such that
DA29:  GP is_simple_func_in S & dom GP = DG &
       for x be set st x in DG holds GP.x = beta by Cor9b;
      for x be set st x in dom GP holds 0 <= GP.x by DA29,D06; then
DA31h:GP is nonnegative by SUPINF_2:71;
DA31i:for x be set st x in dom(GP-g) holds g.x <= GP.x 
      proof
       let x be set;
       assume x in dom(GP-g); then
       x in (dom GP /\ dom g)\
            (GP"{+infty}/\g"{+infty} \/ GP"{-infty}/\g"{-infty})
               by MESFUNC1:def 4; then
A0:    x in dom GP /\ dom g by XBOOLE_0:def 4; then
       GP.x = beta by DA29; 
       hence g.x <= GP.x by C2,DA29,A0;
      end; then
   B1:integral'(M,g|dom(GP-g)) <= integral'(M,GP|dom(GP-g))
         by Cor9a,A1,D03,DA31h,DA29;
      dom(GP-g) = dom GP /\ dom g by A1,DA29,DA31h,D03,DA31i,Diff1; then
      g|dom(GP-g) = g & GP|dom(GP-g) = GP by GRFUNC_1:64,DA29;
      hence thesis by B1,D06,DA29,Cor9c;
     end;
     reconsider MG=M.(dom g) as Real by D02a,EXTREAL1:1;
     beta*(M.DG)=beta1*MG by EXTREAL1:13; then
D07: integral'(M,g) <> +infty & integral'(M,g) <> -infty
      by Cor9,A1,D03,D05,SUPINF_1:31;
D2:  for e be R_eal st 0 < e & e < alpha holds
       ex N0 be Nat st
        for n be Nat st N0<= n holds
         integral'(M,g) - e*(beta + M.(dom g)) < integral'(M,F.n)
     proof
      let e be R_eal;
      assume 
DA0:   0 < e & e < alpha; then
W100: e <> -infty & e <> +infty by XXREAL_0:4;

W101: now let x be set;
       assume x in dom g; then
       |. g.x .| < +infty by C60,MESFUNC2:def 1; 
       hence g.x <> -infty & g.x <> +infty by EXTREAL2:67;
      end;             
      consider H be SetSequence of X, MF be ExtREAL_sequence such that
DA1:   for n be Nat holds H.n = less_dom(g-F.n,e) and
DA2:   for n,m be Nat st n <= m holds H.n c= H.m and
DA2a:  for n be Nat holds H.n c= dom g and
DA5:   for n be Nat holds MF.n = M.(H.n) and
DA6:   M.(dom g) = sup rng MF and
DA7:   for n be Nat holds H.n in S by DA0,C5;
      sup rng MF is Real by D02a,DA6,EXTREAL1:1; then
      consider y being R_eal such that
K4:    y in rng MF & sup rng MF - e < y by DA0,MEASURE6:33;
      consider N0 be set such that
K5:    N0 in dom MF & y=MF.N0 by K4,FUNCT_1:def 5;
      reconsider N0 as Element of NAT by K5;
      reconsider B0=H.N0 as Element of S by DA7;
      M.(dom g) -e < M.(H.N0) by DA6,DA5,K5,K4; then
      M.(dom g) < M.(H.N0) + e by W100,EXTREAL2:29; then
K9:   M.(dom g) - M.(H.N0) < e by W100,EXTREAL2:30;
K10a: B0 c= DG by DA2a; then
K10b: M.B0 <= M.DG by MEASURE1:62;
      M.DG <= +infty by SUPINF_1:20; then
      M.DG < +infty by D1,SUPINF_1:22; then
      M.B0 < +infty by K10b,XXREAL_0:2; then
K10:  M.(DG \ B0) = M.DG - M.B0 by K10a,MEASURE1:63;
      take N0;
DA8a: now let n be Nat;
       assume 
K12:   N0 <= n;
       reconsider BN=H.n as Element of S by DA7;
       H.N0 c= H.n by DA2,K12; then
       DG \ BN c= DG \ B0 by XBOOLE_1:34; then
       M.(DG \ BN) <= M.(DG \ B0) by MEASURE1:62;
       hence M.((dom g) \ H.n) <e by K9,K10,XXREAL_0:2;
      end;
      now let n be Nat;
       assume
DA9a0:  N0 <= n;
DA10:  H.n c= DG by DA2a; then
       DG =DG \/ H.n by XBOOLE_1:12; then
DA10b: DG =(DG \ H.n) \/ H.n by XBOOLE_1:39;
       H.n is Element of S by DA7; then
       H.n is_measurable M by MEASURE1:def 12; then
DA10a: X \ H.n is_measurable M by MEASURE1:66;
       DG is_measurable M by MEASURE1:def 12; then
DA10c: DG /\ (X \ H.n) is_measurable M by MEASURE1:66,DA10a;
       DG /\ (X \ H.n) =(DG /\ X) \ H.n  by XBOOLE_1:49; then
       DG /\ (X \ H.n) = DG \ H.n by XBOOLE_1:28; then
       reconsider A=DG \ H.n as Element of S by DA10c,MEASURE1:def 12;
       reconsider B=H.n as Element of S by DA7;
DA11a: dom(F.n) = dom g by A4; then
J1:    dom(F.n) = (A \/ B) /\ dom(F.n) by DA10b;
       for x be set st x in dom(F.n) holds (F.n).x=(F.n).x; then
DA12:  F.n = (F.n)|(A \/ B) by J1,FUNCT_1:68;
DA12b: F.n is nonnegative by A5; then
DA13c: (F.n)|A is nonnegative & (F.n)|B is nonnegative by LL0;
DA12c: A misses B by XBOOLE_1:79;
DA12d: F.n is_simple_func_in S by A3; then
DA13:  integral'(M,F.n) = integral'(M,(F.n) |A) + integral'(M,(F.n)|B)
               by DA12b,DA12c,Th1,DA12;
DDA13d:(F.n)|A is_simple_func_in S & (F.n)|B is_simple_func_in S
         by DA12d, Cor5; then
       0 <= integral'(M,(F.n)|A) & 0 <= integral'(M,(F.n)|B) 
         by Cor9,DA13c; then
DA15:  integral'(M,(F.n)|B) <= integral'(M,F.n) by DA13,SUPINF_2:20;
       dom((F.n)|B) = dom(F.n) /\ B by FUNCT_1:68; then
DDA13b:dom((F.n)|B) = B by DA11a,DA10,XBOOLE_1:28;
       consider EP be PartFunc of X,ExtREAL such that
DA16:   EP is_simple_func_in S & dom EP= B &
        for x be set st x in B holds EP.x = e by W100,Cor9b;
DA19:  integral'(M,EP) = e * M.B by DA16,DA0,Cor9c;
       M.(dom g) <= +infty by SUPINF_1:20; then
K2:    M.(dom g) < +infty by D1,SUPINF_1:22;
L6:    {} c= B by XBOOLE_1:2; then
K6:    0 <= M.B by K5a,K5b,MEASURE1:62;
K3:    M.B <= M.DG by DA10,MEASURE1:62; then
       M.B < +infty by XXREAL_0:2,K2; then
       e * M.B < e * +infty by DA0,W100,EXTREAL1:44; then
DA19a: integral'(M,EP) <> +infty & integral'(M,EP) <> -infty
         by DA19,K6,DA0,EXTREAL2:45,XXREAL_0:4;
DA20a: g|B is_simple_func_in S & g|A is_simple_func_in S by A1,Cor5;
DA30c: A c= dom g by DA10b,XBOOLE_1:7; then
DA20d: dom(g|A) = A & dom(g|B) = B by DA10,RELAT_1:91;
DA20g: g|A is nonnegative & g|B is nonnegative by D03,LL0; then
DA26k: integral'(M,g|A) <> -infty by Cor9,DA20a;
DA16a: (F.n)|B+EP is_simple_func_in S by DDA13d,DA16,LL2;
       for x be set st x in dom EP holds 0 <= EP.x by DA16,DA0; then
DA20f: EP is nonnegative by SUPINF_2:71; then
DA16b: dom((F.n)|B+EP)
          = dom((F.n)|B) /\ dom EP by DDA13d,DA16,DA13c,Th1a
         .= B by DA16,DDA13b;
DA16d: (F.n)|B+EP is nonnegative by DA13c,DA20f,LL4;
N0:    for x be set st
        x in dom( ((F.n)|B+EP) - g|B ) holds (g|B).x <= ((F.n)|B+EP).x 
       proof
        let x be set;
        assume x in dom( ((F.n)|B+EP) - g|B ); then
        x in (dom ((F.n)|B+EP) /\ dom(g|B)) \
          ( ((F.n)|B+EP)"{+infty} /\ (g|B)"{+infty}
             \/((F.n)|B+EP)"{-infty} /\ (g|B)"{-infty} ) 
               by MESFUNC1:def 4; then
AA:     x in dom((F.n)|B+EP) /\ dom(g|B) by XBOOLE_0:def 4; then
A0:     x in dom((F.n)|B+EP) by XBOOLE_0:def 3; then
        ((F.n)|B+EP).x =((F.n)|B).x + EP.x by MESFUNC1:def 3; then
        ((F.n)|B+EP).x =(F.n).x + EP.x by A0,DDA13b,DA16b,FUNCT_1:68; then
Y4:     ((F.n)|B+EP).x =(F.n).x + e by A0,DA16,DA16b; 
Y5:     (g|B).x = g.x by AA,DA16b,DA20d,FUNCT_1:68;
        set f=g-(F.n);
Y6:     dom f = dom g by P1; 
        x in less_dom(g-F.n,e)  by A0,DA16b,DA1; then
Y9:     x in dom f & ex y being R_eal st y=f.x & y < e by MESFUNC1:def 12; then
Y13:    g.x - (F.n).x <= e by MESFUNC1:def 4;
        g.x <>-infty & g.x <>+infty by Y9,Y6,W101;
        hence thesis by Y5,Y4,Y13,EXTREAL2:33,W100;
       end; then
N2:    dom( ((F.n)|B+EP) - g|B ) = dom((F.n)|B+EP) /\ dom(g|B)
          by Diff1,DA16a,DA20a,DA16d,DA20g;
       dom((F.n)|B+EP) = dom((F.n)|B) /\ dom EP
          by Th1a,DDA13d,DA16,DA13c,DA20f; then
       (F.n)|B+EP = ((F.n)|B+EP)|dom( ((F.n)|B+EP) - g|B ) &
       g|B = (g|B)|dom( ((F.n)|B+EP) - g|B ) 
         by GRFUNC_1:64,N2,DA20d,DDA13b,DA16; then
DA21:  integral'(M,g|B) <= integral'(M,(F.n)|B+EP) 
          by N0,Cor9a,DA16a,DA20a,DA16d,DA20g;
       integral'(M,(F.n)|B+EP) = integral'(M,((F.n)|B)|B) + integral'(M,EP|B) 
          by DA16b,DA16,DA13c,DDA13d,DA20f,Th1a
       .= integral'(M,((F.n)|B)|B) + integral'(M,EP) by DA16,GRFUNC_1:64
       .= integral'(M,(F.n)|B) + integral'(M,EP) by FUNCT_1:82; then
DA24:  integral'(M,g|B) - integral'(M,EP) <= integral'(M,(F.n)|B) 
          by EXTREAL2:34,DA19a,DA21;
J1:    dom g = (A \/ B) /\ dom g by DA10b;
       for x be set st x in dom g holds g.x=g.x; then
       g=g|(A\/B) by J1,FUNCT_1:68; then
DA26:  integral'(M,g) = integral'(M,g|A) +integral'(M,g|B) by DA12c,Th1,A1,D03;
       consider GP be PartFunc of X,ExtREAL such that
DA29:   GP is_simple_func_in S & dom GP = A &
        for x be set st x in A holds GP.x = beta by Cor9b;
       for x be set st x in dom GP holds 0 <= GP.x by DA29,D06; then
DA31h: GP is nonnegative by SUPINF_2:71;
N0:    for x be set st x in dom(GP-(g|A)) holds (g|A).x <= GP.x 
       proof
        let x be set;
        assume x in dom(GP-(g|A)); then
        x in (dom GP /\ dom(g|A)) 
          \((GP"{+infty}/\(g|A)"{+infty}) \/ (GP"{-infty}/\(g|A)"{-infty}))
           by MESFUNC1:def 4; then
AA:     x in (dom GP /\ dom(g|A)) by XBOOLE_0:def 4; then
A0:     x in dom GP by XBOOLE_0:def 3; 
A2:     (g|A).x=g.x by DA20d,DA29,AA,FUNCT_1:70;
        x in dom g /\ A by DA20d,DA29,AA,FUNCT_1:68; then 
        x in dom g by XBOOLE_0:def 3; then
        g.x <= beta by C2;
        hence (g|A).x <= GP.x by A2,A0,DA29; 
       end; then
       dom(GP-(g|A)) = dom GP /\dom(g|A) by Diff1,DA20a,DA29,DA20g,DA31h; then
       (g|A)|dom(GP-(g|A)) = g|A & GP|dom(GP-(g|A)) = GP 
          by GRFUNC_1:64,DA29,DA20d; then
DA31:  integral'(M,g|A) <= integral'(M,GP) by N0,Cor9a,DA20a,DA29,DA20g,DA31h;
DA32:  integral'(M,GP) = beta*(M.A) by D06,DA29,Cor9c;
D32a:  M.A <> +infty by K2,DA30c,MEASURE1:62;
       M.A <= M.DG by DA30c,MEASURE1:62; then
       M.A < +infty by XXREAL_0:2,K2; then
       beta*(M.A) < beta*(+infty) by D06,EXTREAL1:44; then
D32c:  beta*(M.A) <> +infty by D06,EXTREAL1:def 1;
       {} c= A by XBOOLE_1:2; then
D32d:  0 <= M.A by K5a,K5b,MEASURE1:62; then
DA32g: beta*(M.A) <> -infty by D06,EXTREAL2:45;
       M.(DG \ H.n ) < e by DA8a,DA9a0; then
DA33:  beta*(M.A) < beta*e by D06,EXTREAL1:44; then
       integral'(M,(g|A)) < beta*e by DA31,DA32,XXREAL_0:2; then
D26a:  integral'(M,(g|A)) <> +infty by XXREAL_0:4;
       e <> +infty & e <> -infty by DA0,XXREAL_0:4; then
       reconsider ee=e as Real by EXTREAL1:1;
DA26n: beta*e = beta1*ee by EXTREAL1:13;
W1c:   M.B <>-infty by K5a,K5b,L6,MEASURE1:62;
       M.(dom g) <> -infty by K3,L6,K5a,K5b,MEASURE1:62; then
W2:    M.(dom g) is Real by D1,EXTREAL1:1;
       reconsider XSMg = integral'(M,g) as Real by D07,EXTREAL1:1;
       reconsider XSMgA =integral'(M,g|A) as Real by D26a,DA26k,EXTREAL1:1;
       reconsider
          XSMGP =integral'(M,GP) as Real by DA32,D32c,DA32g,EXTREAL1:1;
       reconsider MA=M.A as Real by D32a,D32d,EXTREAL1:1;
       reconsider betae=beta*e as Real by DA26n;
       reconsider XSMEP = integral'(M,EP) as Real by DA19a,EXTREAL1:1;
       M.B is Real by K3,W1c,K2,EXTREAL1:1; then
       consider MB,MG be Real such that
W4:     MB=M.B & MG=M.(dom g) & MB<=MG by K3,W2;
W9:    XSMEP = e*(M.B) by DA16,DA0,Cor9c .=ee*MB by W4,EXTREAL1:13;
DA27:  integral'(M,g) - integral'(M,g|A) = integral'(M,g|B) 
            by D26a,DA26,DA26k,EXTREAL2:11;
DA34:  integral'(M,g) -integral'(M,GP) <= integral'(M,g) - integral'(M,g|A) 
            by D26a,DA31,SUPINF_2:15,DA26k,DA32,D32c,DA32g;
       XSMg - XSMgA =integral'(M,g) - integral'(M,g|A) by SUPINF_2:5
                   .= integral'(M,g|B) by D26a,DA26,DA26k,EXTREAL2:11; then
       reconsider XSMgB = integral'(M,g|B) as Real;
U1:    XSMg - betae < XSMg -XSMGP by REAL_1:92,DA32,DA33;
       integral'(M,g) -integral'(M,GP) = XSMg -XSMGP by SUPINF_2:5; then
       XSMg-betae < XSMgB by U1,DA27,DA34,XXREAL_0:2; then
U50:   XSMg-betae- ee*MB < XSMgB - XSMEP by W9,REAL_1:92;
W10:   betae=ee*beta1 by EXTREAL1:13;
       ee*MB <= ee*MG by DA0,XREAL_1:66,W4; then
       XSMg-betae-ee*MG <= XSMg-betae- ee*MB by XREAL_1:15;then
U49:   XSMg-ee*(beta1+MG) < XSMgB - XSMEP by W10,U50,XREAL_1:2;
       beta1+MG = beta + M.(dom g) by W4,SUPINF_2:1; then
       ee*(beta1+MG) =e*(beta + M.(dom g)) by EXTREAL1:13; then
U51:   XSMg-ee*(beta1+MG) = integral'(M,g)-e*(beta + M.(dom g)) by SUPINF_2:5;
       XSMgB - XSMEP =integral'(M,g|B) - integral'(M,EP) by SUPINF_2:5; then
       integral'(M,g) - e*(beta+M.(dom g)) < integral'(M,(F.n)|B) 
            by U49,U51,DA24,XXREAL_0:2;
       hence integral'(M,g) - e*(beta + M.(dom g)) < integral'(M,F.n) 
            by DA15,XXREAL_0:2;
      end;
      hence thesis;
     end;
D3:  for e be R_eal st 0 < e & e < alpha holds
      ex N0 be Nat st for n be Nat st N0<= n holds
       integral'(M,g) - e*(beta + M.(dom g)) < L.n
     proof
      let e be R_eal;
      assume 0 < e & e < alpha; then
      consider N0 be Nat such that        
D:     for n be Nat st N0<= n holds
        integral'(M,g) - e*(beta + M.(dom g)) < integral'(M,F.n) by D2;
      now let n be Nat;
       assume N0 <=n; then
       integral'(M,g)-e*(beta + M.(dom g)) < integral'(M,F.n) by D;
       hence integral'(M,g) - e*(beta + M.(dom g)) < L.n by A8;
      end;
      hence thesis;
     end;
D3a: for e1 be R_eal st 0 < e1
      ex e be R_eal st 0 < e & e < alpha & e*(beta + M.(dom g)) <= e1
     proof
      let e1 be R_eal; assume
P0:    0 < e1;
      reconsider ralpha=alpha as Real by EXTREAL1:1;
      reconsider rdomg=M.(dom g) as Real by D02a,EXTREAL1:1;
      {} c= DG by XBOOLE_1:2; then
P3d:  0 <= rdomg by K5a,K5b,MEASURE1:62;
      per cases;
      suppose
CA1:   e1 = +infty;
       set z=ralpha/2;
CA13:  0 < z & z < alpha by C1,XREAL_1:217,218;
       R_EAL z*(beta+M.(dom g)) <= +infty by SUPINF_1:20;
       hence ex e be R_eal st 0 < e & e < alpha &
              e*(beta+M.(dom g)) <= e1 by CA13,CA1;
      end;
      suppose e1 <> +infty; then
       reconsider re1=e1 as Real by P0,EXTREAL1:1;
CA26:  0+0 < beta1+rdomg by P3d,D06,XREAL_1:10;
       set x=re1/(beta1 + rdomg);
       set y=ralpha/2;

CA29:  y < ralpha by C1,XREAL_1:218;
       set z=min(x,y);
CA30:  now per cases by SQUARE_1:38;
        suppose min(x,y)=x;
         hence 0 < z by CA26,P0,XREAL_1:141;
        end;
        suppose min(x,y)=y;
         hence 0 < z by C1,XREAL_1:217;
        end;
       end;
       z <= y by SQUARE_1:35; then
CA32:  z < ralpha by CA29,XREAL_1:2;
       z <=x by SQUARE_1:35; then
       z*(beta1 + rdomg) <= re1/(beta1 + rdomg)*(beta1 + rdomg)
         by CA26,XREAL_1:66; then
CA35:  z*(beta1 + rdomg) <= re1 by CA26,XCMPLX_1:88;
       R_EAL(beta1 + rdomg) =R_EAL beta1 + R_EAL rdomg by SUPINF_2:1; then
       z*(beta1 + rdomg) = R_EAL z*(beta+M.(dom g)) by EXTREAL1:38;
       hence ex e be R_eal st 0 < e & 
              e < alpha & e*(beta+M.(dom g)) <= e1 by CA30,CA32,CA35;
      end;
     end;             
D3b: for e1 be R_eal st 0 < e1 holds
      ex N0 be Nat st 
       for n be Nat st N0<= n holds integral'(M,g) - e1 < L.n
     proof
      let e1 be R_eal;
      assume 0 < e1; then
      consider e be R_eal such that
D3BA1: 0 < e & e < alpha & e*(beta + M.(dom g)) <= e1 by D3a;
      consider N0 be Nat such that 
R2:    for n be Nat st N0<= n holds
        integral'(M,g) - e*(beta + M.(dom g)) < L.n by D3,D3BA1;
      take N0;
      now let n be Nat;
       assume N0 <= n; then
R3:    integral'(M,g) - e*(beta + M.(dom g)) < L.n by R2;
       integral'(M,g) - e1 <= integral'(M,g) - e*(beta + M.(dom g))
         by SUPINF_2:15,D07,D3BA1;
       hence integral'(M,g) - e1 < L.n by XXREAL_0:2,R3;
      end;
      hence thesis;
     end;
D3c: for n,m be Nat st n<=m holds L.n <= L.m
     proof
      let n,m be Nat;
      assume 
P0:    n <=m;
P11:  dom (F.n) = dom g & dom (F.m) = dom g by A4;
P21:  F.n is nonnegative & F.m is nonnegative by A5;
P41:  F.n is_simple_func_in S & F.m is_simple_func_in S by A3;
N1:   for x be set st x in dom(F.m - F.n) holds (F.n).x <= (F.m).x
      proof
       let x be set;
       assume x in dom(F.m - F.n); then
       x in (dom(F.m) /\ dom(F.n)) \ ( 
        ((F.m)"{+infty}/\(F.n)"{+infty})\/((F.m)"{-infty}/\(F.n)"{-infty}) )
          by MESFUNC1:def 4; then
       x in dom(F.m) /\ dom(F.n) by XBOOLE_0:def 4;
       hence thesis by P0,A6,P11;
      end; then
N2:   integral'(M,(F.n)|dom(F.m - F.n)) <= integral'(M,(F.m)|dom(F.m - F.n))
         by Cor9a,P41,P21;
      dom(F.m - F.n) = dom(F.m) /\ dom(F.n) by P41,P21,N1,Diff1; then
      (F.m)|dom(F.m - F.n) = F.m & (F.n)|dom(F.m - F.n) = F.n
         by GRFUNC_1:64,P11; then
      L.n <= integral'(M,(F.m) ) by A8,N2;
      hence L.n <= L.m by A8;
     end;
D3d: for n be Nat holds 0 <= L.n
     proof
      let n be Nat;
P21:  F.n is nonnegative by A5;
      F.n is_simple_func_in S by A3; then
      0 <= integral'(M,F.n) by Cor9,P21;
      hence 0 <= L.n by A8;
     end;
     per cases;
     suppose ex K be real number st 0 < K & for n be Nat holds L.n < K; then
      consider K be real number such that 
       0 < K and
AS2:   for n be Nat holds L.n < K;
AS6:  for n be Nat holds L.n <= sup rng L
      proof
       let n be Nat;
P1:    sup rng L is majorant of rng L by SUPINF_1:def 16;
       reconsider n as Element of NAT by ORDINAL1:def 13;
       dom L = NAT by FUNCT_2:def 1; then
       L.n in rng L by FUNCT_1:def 5;
       hence thesis by P1,SUPINF_1:def 9;
      end; then
      L.1 <= sup rng L; then
AS4a: sup rng L <> -infty by D3d;
      R_EAL K is majorant of rng L
      proof
       now let x be R_eal;
        assume x in rng L; then
        consider z be set such that
E1:     z in dom L and
E2:     x=L.z by FUNCT_1:def 5;
        z is Nat by E1,ORDINAL1:def 13;
        hence x <= R_EAL K by AS2,E2;  
       end;   
       hence thesis by SUPINF_1:def 9;
      end; then
P2:   sup rng L <= R_EAL K by SUPINF_1:def 16;
      R_EAL K is Real by XREAL_0:def 1; then
AS4:  sup rng L <>+infty by P2,SUPINF_1:31;
CA12: for p be real number st 0<p
       ex N0 be Nat st for m be Nat st N0<=m holds |.(L.m)-sup(rng L) .| < p
      proof
       let p be real number;
       assume
CAS0:   0 < p;
       sup rng L is Real by AS4a,AS4,EXTREAL1:1; then
       consider y being R_eal such that
K4:     y in rng L & sup rng L - R_EAL p < y by CAS0,MEASURE6:33;
       consider x be set such that
K5:     x in dom L & y=L.x by K4,FUNCT_1:def 5;
       reconsider N0=x as Element of NAT by K5;
       take N0;
       let n be Nat;
       assume N0 <= n; then
       L.N0 <= L.n by D3c; then
K10:   sup rng L -R_EAL p < L.n by K5,K4,XXREAL_0:2;
M15:   L.n <= sup rng L by AS6;
       sup rng L + 0. <= sup rng L + R_EAL p by SUPINF_2:14,CAS0; then
M17:   sup rng L <= sup rng L + R_EAL p by SUPINF_2:18;
       sup rng L <> sup rng L + R_EAL p
       proof
        assume
M17:     sup rng L = sup rng L + R_EAL p;
        R_EAL p +sup rng L + -sup rng L
          =R_EAL p +(sup rng L + -sup rng L) by AS4a,EXTREAL1:8,AS4
         .=R_EAL p + R_EAL 0 by EXTREAL1:9
         .=R_EAL p by SUPINF_2:18;
        hence contradiction by CAS0,M17,EXTREAL1:9;
       end; then
       sup rng L < sup rng L + R_EAL p by M17,SUPINF_1:22; then
       L.n < sup rng L + R_EAL p by M15,XXREAL_0:2; then
K15:   L.n -sup rng L < R_EAL p by EXTREAL2:30;
       sup rng L < L.n + R_EAL p by K10,EXTREAL2:29; then
       sup rng L - L.n < R_EAL p by EXTREAL2:30; then
       -R_EAL p < - (sup rng L - L.n) by SUPINF_2:17; then
       -R_EAL p < L.n -sup rng L by EXTREAL2:15;
       hence thesis by K15,EXTREAL2:59;
      end;
      reconsider h=sup rng L as real number by AS4a,AS4,EXTREAL1:1;
CA15: R_EAL h=sup rng L; then
D4:   L is convergent_to_finite_number by Defconverge1,CA12;
      hence L is convergent by Defconvergent; then
LQ:   lim L = sup rng L by CA12,CA15,D4,Deflim;
      now let e be real number;
       assume 0 < e; then
       consider N0 be Nat such that
P2:     for n be Nat st N0<= n holds integral'(M,g) - R_EAL e < L.n by D3b;
P3:    integral'(M,g) - R_EAL e < L.N0 by P2;
       L.N0 <= sup rng L by AS6; then
       integral'(M,g) - R_EAL e < sup rng L by P3,XXREAL_0:2;
       hence integral'(M,g) < lim L+ R_EAL e by LQ,EXTREAL2:29;
      end;
      hence integral'(M,g) <= lim L by AR01t;
     end;
     suppose
CA2:  not (ex K be real number st 0 < K & for n be Nat holds L.n < K);
      now let K be real number;
       assume 0 < K; then
       consider N0 be Nat such that
CA24:   R_EAL K <= L.N0 by CA2;
       now let n be Nat;
        assume N0 <=n; then
        L.N0 <= L.n by D3c;
        hence R_EAL K <= L.n by CA24,SUPINF_1:29;
       end;
       hence ex N0 be Nat st for n be Nat st N0<=n holds K <= L.n;
      end; then
D4:   L is convergent_to_+infty by Defconverge2;
      hence L is convergent by Defconvergent; then
      lim L = +infty by D4,Deflim;
      hence integral'(M,g) <= lim L by SUPINF_1:20;
     end;
    end;
    suppose
E1:  M.(dom g) = +infty;
     reconsider DG=dom g as Element of S by A1,Cor10;
E2:  for e be R_eal st 0 < e & e < alpha holds
      for n be Nat holds (alpha - e)*M.less_dom(g-F.n,e) <= integral'(M,F.n)
     proof
      let e be R_eal;
      assume
DA0:   0 < e & e < alpha; then
W100: e <> -infty & e <> +infty by XXREAL_0:4;
      e <= +infty by SUPINF_1:20; then
      e <+infty by W100,SUPINF_1:22; then
DA0a: 0<= alpha-e by DA0,EXTREAL1:40;
      consider H be SetSequence of X, MF be ExtREAL_sequence such that
DA1:   for n be Nat holds H.n = less_dom(g-(F.n),e) and
       for n,m be Nat st n <= m holds H.n c= H.m and
DA2a:  for n be Nat holds H.n c= dom g and
       for n be Nat holds MF.n = M.(H.n) and
       M.(dom g) = sup(rng MF) and
DA7:   for n be Nat holds H.n in S by DA0,C5;
      now let n be Nat;
       H.n c= DG by DA2a; then
DA10:  DG =DG \/ H.n by XBOOLE_1:12
         .=(DG \ H.n) \/ H.n by XBOOLE_1:39;
       H.n is Element of S by DA7; then
       H.n is_measurable M by MEASURE1:def 12; then
DA10a: X \ H.n is_measurable M by MEASURE1:66;
       DG is_measurable M by MEASURE1:def 12; then
DA10b: DG /\ (X \ H.n) is_measurable M by MEASURE1:66,DA10a;
       DG /\ (X \ H.n) =(DG /\ X) \ H.n  by XBOOLE_1:49
                      .= DG \ H.n by XBOOLE_1:28; then
       reconsider A=DG \ H.n as Element of S by DA10b,MEASURE1:def 12;
       reconsider B=H.n as Element of S by DA7;
DA11a: dom(F.n) = dom g by A4; then
DA11c: B c= dom(F.n) by DA10,XBOOLE_1:7;
J1:    dom (F.n) = (A \/ B) /\ dom(F.n) by DA11a,DA10;
       for x be set st x in dom(F.n) holds (F.n).x=(F.n).x; then
DA12:  F.n = (F.n)|(A \/ B) by J1,FUNCT_1:68;
DA12b: F.n is nonnegative by A5;
DA12c: A misses B by XBOOLE_1:79;
DA12d: F.n is_simple_func_in S by A3; then
DA13:  integral'(M,F.n) =integral'(M,(F.n)|A) + integral'(M,(F.n)|B)
                        by DA12b,DA12c,Th1,DA12;
DA13c: (F.n)|A is nonnegative & (F.n)|B is nonnegative by DA12b,LL0;
DDA13d:(F.n)|A is_simple_func_in S &
       (F.n)|B is_simple_func_in S by DA12d, Cor5; then
       0 <= integral'(M,(F.n)|A) & 0 <= integral'(M,(F.n)|B) 
         by Cor9,DA13c; then
DA15:  integral'(M,(F.n)|B) <= integral'(M,F.n) by DA13,SUPINF_2:20;
DDA13b:dom((F.n)|B) =dom(F.n) /\ B by FUNCT_1:68
                   .= B by DA11c,XBOOLE_1:28;
       alpha - e <> +infty by DA0,SUPINF_2:10; then
       consider EP be PartFunc of X,ExtREAL such that
DA16:   EP is_simple_func_in S &
        dom EP= B &
        for x be set st x in B holds EP.x = alpha- e by Cor9b,DA0a;
       for x be set st x in dom EP holds 0 <= EP.x by DA16,DA0a; then
DA20f: EP is nonnegative by SUPINF_2:71;
DA20:  for x be set st x in dom((F.n)|B - EP) holds EP.x <= ((F.n)|B) .x 
       proof
        let x be set;
        assume x in dom((F.n)|B - EP); then
        x in (dom((F.n)|B) /\ dom EP)\
         ( (((F.n)|B)"{+infty} /\ EP"{+infty}) 
           \/ (((F.n)|B)"{-infty} /\ EP"{-infty}) ) by MESFUNC1:def 4; then
AA:     x in dom((F.n)|B) /\ dom EP by XBOOLE_0:def 4; then
A0:     x in dom((F.n)|B) by XBOOLE_0:def 3; then
Y4:     ((F.n)|B).x =(F.n).x by FUNCT_1:68;
        set f=g-F.n;
Y6:     dom f = dom g by P1; 
        x in less_dom(g-F.n,e) by DA1,A0,DDA13b; then
Y9:     x in dom f & ex y being R_eal st y=f.x & y < e by MESFUNC1:def 12; then
Y13:    g.x - (F.n).x <= e by MESFUNC1:def 4;
        for x be set st x in dom g holds g.x <> -infty & g.x <> +infty
        proof
         let x be  set;
         assume x in dom g; then
         |. g.x .| < +infty by C60,MESFUNC2:def 1; 
         hence g.x <> -infty & g.x <> +infty by EXTREAL2:67;
        end; then
        g.x <> -infty & g.x <> +infty by Y9,Y6; then
        g.x <= (F.n).x + e by Y13,EXTREAL2:33,W100; then
T1:     g.x-e <= (F.n).x by W100,EXTREAL2:34;
        alpha <= g.x by C2,Y6,Y9; then
        alpha-e <= g.x-e by SUPINF_2:15,W100; then
        alpha -e <= (F.n).x by T1,XXREAL_0:2;
        hence thesis by Y4,AA,DDA13b,DA16;
       end; then
N1:    integral'(M,EP|dom((F.n)|B - EP))
        <= integral'(M,((F.n)|B)|dom((F.n)|B - EP) )
           by Cor9a,DDA13d,DA16,DA13c,DA20f;
DA19:  integral'(M,EP) = (alpha-e)* (M.B) by DA16,DA0a,Cor9c;
       dom((F.n)|B - EP) = dom((F.n)|B) /\ dom EP 
          by DDA13d,DA16,DA13c,DA20f,DA20,Diff1; then
       EP|dom((F.n)|B - EP) = EP &
       ((F.n)|B)|dom((F.n)|B - EP) = (F.n)|B by GRFUNC_1:64,DA16,DDA13b; then
       integral'(M,EP) <= integral'(M,F.n) by DA15,XXREAL_0:2,N1;
       hence (alpha-e)* M.less_dom(g-F.n,e) <= integral'(M,F.n) by DA1,DA19;
      end;
      hence thesis;
     end;
     for y be real number st 0 < y
      ex n be Nat st for m be Nat st n<=m holds y <= L.m
     proof
      let y be real number;
      assume 0 < y;
      set e=alpha/R_EAL 2;
      reconsider ralpha=alpha as Real by EXTREAL1:1;
      set a2=ralpha/2;
Q8:   e =a2 by EXTREAL1:39; then
E32a: 0 < e by C1,XREAL_1:217;
E32b: e < alpha by C1,XREAL_1:218,Q8;
Q11:  alpha - e = ralpha - a2 by Q8,SUPINF_2:5;
Q14:  a2-a2 < ralpha - a2  by C1,XREAL_1:16; then
E32d: 0 < alpha - e by Q8,SUPINF_2:5;
      reconsider y1=y as Real by XREAL_0:def 1;
      y =(ralpha - a2) * (y1/(ralpha - a2)) by XCMPLX_1:88,Q14; then
      y =R_EAL(ralpha - a2) *R_EAL(y1/(ralpha - a2)) by EXTREAL1:38; then
E32e: (alpha - e)*(R_EAL y / (alpha - e)) = y by Q11,Q14,EXTREAL1:39;
Q17:  y/(ralpha - a2) is Real by XREAL_0:def 1;
      y/(ralpha - a2) =R_EAL y1/ R_EAL(ralpha - a2) by Q14,EXTREAL1:39
                    .=R_EAL y/ (alpha - e) by Q8,SUPINF_2:5; then
E32:  R_EAL y / (alpha - e) < +infty by Q17,SUPINF_1:31;
      consider H be SetSequence of X, MF be ExtREAL_sequence such that
DA1:   for n be Nat holds H.n = less_dom(g-(F.n),e) and
DA2:   for n,m be Nat st n <= m holds H.n c= H.m and
       ( for n be Nat holds H.n c= dom g ) and
DA5:   for n be Nat holds MF.n = M.(H.n) and
DA6:   M.(dom g) = sup rng MF and
DA7:   for n be Nat holds H.n in S by E32a,E32b,C5;
      ex z be R_eal st z in rng MF & (R_EAL y/ (alpha - e)) <= z
      proof
       assume not (ex z be R_eal st z in rng MF &
                  (R_EAL y / (alpha - e)) <= z); then
       for z be R_eal st z in rng MF holds
        z <= (R_EAL y / (alpha - e)); then
       R_EAL y / (alpha - e) is majorant of rng MF by SUPINF_1:def 9;
       hence contradiction by E32,DA6,E1,SUPINF_1:def 16;
      end; then
      consider z be R_eal such that
E36:   z in rng MF & R_EAL y / (alpha - e) <= z;
      consider x be set such that
E37:   x in dom MF & z=MF.x by E36,FUNCT_1:def 5;
      reconsider N0=x as Element of NAT by E37;
      take N0;
      thus for m be Nat st N0 <= m holds y <= L.m
      proof
       let m be Nat;
       assume
E38:    N0 <= m;
       R_EAL y / (alpha - e) <= M.(H.N0) by DA5,E37,E36; then
E41:   R_EAL y <= (alpha - e)*M.(H.N0) by E32d,E32e,EXTREAL1:42;
E39:   H.N0 c= H.m by DA2,E38;
       H.N0 in S & H.m in S by DA7; then
       M.(H.N0) <= M.(H.m) by E39,MEASURE1:62; then
       (alpha - e)*M.(H.N0) <= (alpha - e)*M.(H.m) by E32d,EXTREAL1:42; then
       R_EAL y <= (alpha - e)*M.(H.m) by E41,XXREAL_0:2; then
E43:   R_EAL y <= (alpha - e)*M.less_dom(g-(F.m),e) by DA1;
       (alpha - e)*M.less_dom(g-(F.m),e) <= integral'(M,F.m)
         by E2,E32a,E32b; then
       R_EAL y <= integral'(M,F.m) by SUPINF_1:29,E43;
       hence y <= L.m by A8;
      end;
     end; then
E4:  L is convergent_to_+infty by Defconverge2;
     hence L is convergent by Defconvergent; then
     ( ex g be real number st lim L = g &
       (for p be real number st 0<p
         ex n be Nat st for m be Nat st n<=m holds |. L.m- lim L .| < p) &
       L is convergent_to_finite_number ) or
     ( lim L = +infty & L is convergent_to_+infty ) or
     ( lim L = -infty & L is convergent_to_-infty ) by Deflim;
     hence integral'(M,g) <= lim L by E4,LM011b,SUPINF_1:20;
    end;
   end;
end;

theorem LM016:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    g be PartFunc of X,ExtREAL,
    F be Functional_Sequence of X,ExtREAL st
g is_simple_func_in S & g is nonnegative &
(for n be Nat holds F.n is_simple_func_in S) &
(for n be Nat holds dom(F.n) = dom g) &
(for n be Nat holds F.n is nonnegative) &
(for n,m be Nat st n <=m holds
   for x be Element of X st x in dom g holds (F.n).x <= (F.m).x ) &
(for x be Element of X st x in dom g holds
   (F#x) is convergent & g.x <= lim(F#x) ) holds
  ex G be ExtREAL_sequence st
    (for n be Nat holds G.n = integral'(M,F.n)) &
    G is convergent & sup(rng G)=lim (G) &
    integral'(M,g) <= lim(G) 
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let g be PartFunc of X,ExtREAL;
   let F be Functional_Sequence of X,ExtREAL;
   assume that
AS1:g is_simple_func_in S and
AS2:g is nonnegative and
AS3:for n be Nat holds F.n is_simple_func_in S and
AS4:for n be Nat holds dom(F.n) = dom g and
AS5:for n be Nat holds F.n is nonnegative and
AS6:for n,m be Nat st n <= m holds
     for x be Element of X st x in dom g holds (F.n).x <= (F.m).x and
AS7:for x be Element of X st x in dom g holds
     F#x is convergent & g.x <= lim(F#x);
   reconsider DG=dom g as Element of S by AS1,Cor10;
   set E0 = eq_dom(g,R_EAL 0);
   set E' = dom g \ E0;
   for x be set st x in E0 holds x in dom g by MESFUNC1:def 16; then
A1:E0 c= DG by TARSKI:def 3; then
A2:DG =DG \/ E0 by XBOOLE_1:12 .=(DG \ E0) \/ E0 by XBOOLE_1:39;
   g is_measurable_on DG by AS1,MESFUNC2:37; then
   DG/\great_eq_dom(g,R_EAL 0) is_measurable_on S by MESFUNC1:31; then
   reconsider GG= DG/\great_eq_dom(g,R_EAL 0) as Element of S 
     by MESFUNC1:def 11;
   g is_measurable_on GG by AS1,MESFUNC2:37; then
   GG/\less_eq_dom(g,R_EAL 0) is_measurable_on S by MESFUNC1:32; then
   DG /\ eq_dom(g,R_EAL 0) is_measurable_on S by MESFUNC1:22; then
A3:eq_dom(g,0.) is_measurable_on S by A1,XBOOLE_1:28; then
   E0 is Element of S by MESFUNC1:def 11; then
   E0 is_measurable M by MEASURE1:def 12; then
A4:X \ E0 is_measurable M by MEASURE1:66;
   DG is_measurable M by MEASURE1:def 12; then
A5:DG /\ (X \ E0) is_measurable M by MEASURE1:66,A4;
   DG /\ (X \ E0) =(DG /\ X) \ E0 by XBOOLE_1:49
                 .= DG \ E0 by XBOOLE_1:28; then
   reconsider E' as Element of S by A5,MEASURE1:def 12;
   reconsider E0 as Element of S by A3,MESFUNC1:def 11;
A6:E0 misses E' by XBOOLE_1:79;
   thus ex G be ExtREAL_sequence st
    (for n be Nat holds G.n = integral'(M,F.n)) &
    G is convergent &sup rng G = lim G &
    integral'(M,g) <= lim G
   proof
B0: E' c= dom g by A2,XBOOLE_1:7;
B1: dom(g|E') =dom g /\ E' by FUNCT_1:68 .= E' by B0,XBOOLE_1:28;
P18:g|E' is_simple_func_in S by AS1, Cor5;
P19:for x be set st x in dom(g|E') holds 0 < (g|E').x
    proof
     let x be set;
     assume
A0:   x in dom(g|E'); then
     x in DG & not x in E0 by B1,XBOOLE_0:def 4; then
     g.x <> 0 by MESFUNC1:def 16; then
     0 < g.x by AS2,SUPINF_2:70;
     hence 0 < (g|E').x by A0,FUNCT_1:70;
    end;
    deffunc W(Nat) = (F.$1)|E';
    consider F' be Functional_Sequence of X,ExtREAL such that
aP24A: for n be Element of NAT holds F'.n=W(n)
    from SEQFUNC:sch 1;
    P24A:now let n be Nat; n in NAT by ORDINAL1:def 13;
    hence F'.n=W(n) by aP24A;
  end;
P21:for n be Nat holds dom(F.n|E') = dom(g|E') & dom(F'.n) = dom(g|E')
    proof
     let n be Nat;
     dom(F.n) = E' \/ E0 by AS4,A2; then
DA1: E' c= dom(F.n) by XBOOLE_1:7;
     thus dom(F.n|E') =dom(F.n) /\ E' by FUNCT_1:68
                     .=dom(g|E') by B1,DA1,XBOOLE_1:28;
     hence dom(F'.n) = dom(g|E') by P24A;
    end;             
P23:for n,m be Nat st n <= m holds
     for x be Element of X st x in dom(g|E') holds 
      (F.n|E').x <= (F.m|E').x & (F'.n).x <= (F'.m).x
    proof
     let n,m be Nat;
     assume 
AS:   n<=m;
     thus for x be Element of X st x in dom(g|E') holds 
       (F.n|E').x <= (F.m|E').x & (F'.n).x <= (F'.m).x
     proof
      let x be Element of X;
      assume
PA0:   x in dom(g|E'); then
PA2:  x in dom(F.m|E') & x in dom(F.n|E') by P21;
      (F.n).x <= (F.m).x by AS,AS6,PA0,B0,B1; then 
      (F.n|E').x <= (F.m).x by PA2,FUNCT_1:70;
      hence (F.n|E').x <= (F.m|E').x by PA2,FUNCT_1:70; then
      (F'.n).x <= (F.m|E').x by P24A;
      hence (F'.n).x <= (F'.m).x by P24A;
     end;       
    end;
P20:for n be Nat holds (F.n)|E' is_simple_func_in S & F'.n is_simple_func_in S
    proof
     let n be Nat;
     F.n is_simple_func_in S by AS3;
     hence F.n|E' is_simple_func_in S by Cor5;
     hence F'.n is_simple_func_in S by P24A;
    end;
P24:for x be Element of X st x in dom(g|E') holds
     F'#x is convergent & (g|E').x <= lim(F'#x)
    proof
     let x be Element of X;
     assume 
PA0:  x in dom(g|E'); then
     x in dom g /\ E' by FUNCT_1:68; then
PA1: x in dom g by XBOOLE_0:def 3;
PA4: (F'#x)=(F#x) 
     proof
      now let n be Element of NAT;
C:     x in dom(F.n|E') by PA0,P21;
       thus (F'#x).n = (F'.n).x by DefHx 
                    .= (F.n|E').x by P24A
                    .= (F.n).x by C,FUNCT_1:70
                    .= (F#x).n by DefHx;
      end;
      hence thesis by FUNCT_2:113;
     end;
     F#x is convergent & g.x <= lim(F#x) by PA1,AS7;
     hence thesis by PA4,FUNCT_1:70,PA0;
    end;
A7: dom g = (E0 \/ E') /\ dom g by A2;
    for x be set st x in dom g holds g.x=g.x; then
    g|(E0\/E') = g by A7,FUNCT_1:68; then
A8: integral'(M,g) = integral'(M,g|E0) + integral'(M,g|E') by AS1,AS2,A6,Th1;
    integral'(M,g|E0) = 0 by AS1,AS2,Cor11; then
A9: integral'(M,g) = integral'(M,g|E') by A8,SUPINF_2:18;
    deffunc U(Nat) = integral'(M,F.$1);
    consider G be ExtREAL_sequence such that
aP16: for n be Element of NAT holds G.n = U(n) from FUNCT_2:sch 4;
P16: now let n be Nat; n in NAT by ORDINAL1:def 13;
       hence G.n = U(n) by aP16;
     end;
T1: for n,m be Nat st n <=m holds G.n <= G.m
    proof
     let n,m be Nat;
     assume SA0:n<=m;
PA3: F.n is_simple_func_in S & F.m is_simple_func_in S by AS3;
PA4: dom(F.n) = dom g & dom(F.m) = dom g by AS4;
PA5: F.n is nonnegative & F.m is nonnegative by AS5;
PA6: for x be set st x in dom (F.m - F.n) holds (F.n).x <= (F.m).x
     proof
      let x be set;
      assume x in dom(F.m - F.n); then
      x in (dom(F.m) /\ dom(F.n))
        \ ( (F.m)"{+infty}/\(F.n)"{+infty} \/ (F.m)"{-infty}/\(F.n)"{-infty} )
         by MESFUNC1:def 4; then
      x in dom(F.m) /\ dom(F.n) by XBOOLE_0:def 4;
      hence thesis by PA4,AS6,SA0;
     end; then
     dom(F.m - F.n) = dom(F.m) /\ dom(F.n) by Diff1,PA3,PA5; then
N1:  F.n|dom(F.m - F.n) = F.n & F.m|dom(F.m - F.n) = F.m by GRFUNC_1:64,PA4;
     G.n = integral'(M,F.n) & G.m = integral'(M,F.m) by P16;
     hence thesis by PA6,PA3,PA5,Cor9a,N1;
    end;
    take G;
ST1:G is convergent & lim G = sup rng G by T1,Cor12a;
H22:for n be Nat holds F'.n is nonnegative
    proof
     let n be Nat;
     F.n is nonnegative by AS5; then
     F.n|E' is nonnegative by LL0;
     hence F'.n is nonnegative by P24A;
    end;
    deffunc V(Nat) = integral'(M,(F.$1)|E');
    consider L be ExtREAL_sequence such that
aP17: for n be Element of NAT holds L.n = V(n) from FUNCT_2:sch 4;
P17: now let n be Nat; n in NAT by ORDINAL1:def 13;
      hence L.n=V(n) by aP17;
    end;
H24:for n,m be Nat st n <= m holds
     for x be Element of X st x in dom(g|E') holds (F'.n).x <= (F'.m).x
       by P23;
H17:for n be Nat holds L.n = integral'(M,F'.n) 
    proof
     let n be Nat;
     thus L.n = integral'(M,F.n|E') by P17
             .=integral'(M,F'.n) by P24A;
    end; then
P25:L is convergent & integral'(M,g|E') <= lim L 
      by Cor12,P18,P19,P20,P21,H22,H24,P24;
    for n,m be Nat st n <= m holds L.n <= L.m
    proof
     let n,m be Nat;
     assume SA0:n<=m;
PA3: F'.n is_simple_func_in S & F'.m is_simple_func_in S by P20;
PA4: dom(F'.n) = dom(g|E') & dom(F'.m) =dom(g|E') by P21;
PA5: F'.n is nonnegative & F'.m is nonnegative by H22;
N0:  for x be set st x in dom(F'.m - F'.n) holds (F'.n).x <= (F'.m).x
     proof
      let x be set;
      assume x in dom(F'.m - F'.n); then
      x in (dom(F'.m) /\ dom(F'.n)
        \(((F'.m)"{+infty}/\(F'.n)"{+infty})
          \/((F'.m)"{-infty}/\(F'.n)"{-infty}))) by MESFUNC1:def 4; then
      x in dom(F'.m) /\ dom(F'.n) by XBOOLE_0:def 4;
      hence thesis by PA4,P23,SA0;
     end; then
     dom(F'.m - F'.n) = dom(F'.m) /\ dom(F'.n) by PA3,PA5,Diff1; then
N2:  (F'.n)|dom(F'.m - F'.n) = F'.n &
     (F'.m)|dom(F'.m - F'.n) = F'.m by GRFUNC_1:64,PA4; 
     L.n = integral'(M,F'.n) &
     L.m = integral'(M,F'.m) by H17;
     hence thesis by N0,N2,PA3,PA5,Cor9a;
    end; then
ST2:L is convergent & lim L = sup rng L by Cor12a;
    for n be Nat holds L.n <= G.n
    proof
     let n be Nat;
 P:  G.n = integral'(M,F.n) by P16;
R:   dom(F.n) = E' \/ E0 by AS4,A2; 
D12d:F.n is_simple_func_in S by AS3;
     F.n is nonnegative by AS5; then
D13c:(F.n|E0) is nonnegative & F.n|E' is nonnegative by LL0;
     F.n|E0 is_simple_func_in S & F.n|E' is_simple_func_in S
      by D12d, Cor5; then
E14: 0 <= integral'(M,F.n|E0) & 0 <= integral'(M,F.n|E') by Cor9,D13c;
 R1: dom(F.n) = (E0 \/ E') /\ dom (F.n) by R;
     for x be set st x in dom(F.n) holds (F.n).x=(F.n).x; then
Q1:  F.n = F.n|(E0 \/ E') by R1,FUNCT_1:68; then
Q2:  F.n|(E0 \/ E') is nonnegative by AS5;
     F.n is_simple_func_in S by AS3; then
     integral'(M,F.n) =integral'(M,F.n|E0) + integral'(M,F.n|E') 
      by Q1,Q2,A6,Th1; then
     integral'(M,F.n|E') <= integral'(M,F.n) by E14,SUPINF_2:20;
     hence L.n <= G.n by P,P17;
    end; then 
    sup rng L <= sup rng G by Cor12b;
    hence thesis by P25,SUPINF_1:29,A9,ST2,ST1,P16;
   end;
end;

theorem LM017:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    A be Element of S,
    F,G be Functional_Sequence of X,ExtREAL,
    K,L be ExtREAL_sequence st
(for n be Nat holds F.n is_simple_func_in S & dom(F.n)=A ) &
(for n be Nat holds F.n is nonnegative) &
(for n,m be Nat st n <=m holds
   for x be Element of X st x in A holds (F.n).x <= (F.m).x ) &
(for n be Nat holds G.n is_simple_func_in S & dom(G.n)=A) &
(for n be Nat holds G.n is nonnegative) &
(for n,m be Nat st n <=m holds
   for x be Element of X st x in A holds (G.n).x <= (G.m).x ) &
(for x be Element of X st x in A holds
     F#x is convergent & G#x is convergent & lim(F#x) = lim(G#x)) &
(for n be Nat holds K.n=integral'(M,F.n) & L.n=integral'(M,G.n))
holds
  K is convergent & L is convergent & lim K = lim L
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       A be Element of S,
       F,G be Functional_Sequence of X,ExtREAL,
       K,L be ExtREAL_sequence such that
A1:for n be Nat holds F.n is_simple_func_in S & dom(F.n)=A and
A2:for n be Nat holds F.n is nonnegative and
A3:for n,m be Nat st n <=m holds for x be Element of X st x in A holds
       (F.n).x <= (F.m).x and
A4:for n be Nat holds G.n is_simple_func_in S & dom(G.n)=A and
A5:for n be Nat holds G.n is nonnegative and
A6:for n,m be Nat st n <=m holds for x be Element of X st x in A holds
       (G.n).x <= (G.m).x and
A7:for x be Element of X st x in A holds
     F#x is convergent & G#x is convergent & lim(F#x) = lim(G#x) and
A8:for n be Nat holds K.n=integral'(M,F.n) & L.n=integral'(M,G.n);
P5:for n0 be Nat holds K is convergent & sup rng K = lim K & L.n0 <= lim K
   proof
    let n0 be Nat; 
    reconsider g=G.n0 as PartFunc of X,ExtREAL;
AS41:dom g = A by A4; 
AS4:for x be Element of X st x in dom g holds
     F#x is convergent & g.x <= lim(F#x)
    proof
     let x be Element of X;
     assume x in dom g; then
Q1F: x in A by A4;
     now let n,m be Nat;
      assume PS1: n<=m;
      (G#x).n=(G.n).x & (G#x).m=(G.m).x by DefHx;
      hence (G#x).n <= (G#x).m by Q1F,A6,PS1;
     end; then
P4:  lim(G#x)=sup rng(G#x) by Cor12a;
Q3:  g.x=(G#x).n0 by DefHx;
     (G#x).n0 <= sup rng(G#x) by Cor12c;
     hence thesis by P4,Q3,A7,Q1F;
    end;
    g is_simple_func_in S &
    g is nonnegative &
    (for n be Nat holds F.n is_simple_func_in S) &
    (for n be Nat holds dom(F.n) = dom g) &
    (for n be Nat holds F.n is nonnegative) &
    (for n,m be Nat st n <=m holds
     for x be Element of X st x in dom g holds (F.n).x <= (F.m).x ) &
    (for x be Element of X st x in dom g holds
      F#x is convergent & g.x <= lim(F#x) ) by A1,A2,A3,A4,A5,AS4,AS41; then
    consider GG be ExtREAL_sequence such that
P1:  (for n be Nat holds GG.n = integral'(M,F.n)) &
     GG is convergent & sup rng GG = lim GG &
     integral'(M,g) <= lim GG by LM016;
    GG=K
    proof
     now let n be Element of NAT;
      GG.n = integral'(M,F.n) by P1;
      hence GG.n = K.n by A8;
     end;
     hence thesis by FUNCT_2:113;
    end;
    hence K is convergent & sup rng K = lim K & L.n0 <= lim K by A8,P1;
   end;
P6:for n0 be Nat holds
    L is convergent & sup rng L=lim L & K.n0 <= lim L
   proof
    let n0 be Nat; 
    reconsider f=F.n0 as PartFunc of X,ExtREAL;
AS41:dom f = A by A1; 
AS4:for x be Element of X st x in dom f holds
     G#x is convergent & f.x <= lim(G#x)
    proof
     let x be Element of X;
     assume x in dom f; then
Q1F: x in A by A1;
Q3:  f.x=(F#x).n0 by DefHx;
     now let n,m be Nat; assume
PS1:  n<=m;
      (F#x).n=(F.n).x & (F#x).m=(F.m).x by DefHx;
      hence (F#x).n <= (F#x).m by Q1F,A3,PS1;
     end; then
P3:  lim(F#x)=sup rng(F#x) by Cor12a;
     (F#x).n0 <= sup rng (F#x) by Cor12c;
     hence thesis by P3,Q3,A7,Q1F;
    end;
    f is_simple_func_in S & f is nonnegative &
    (for n be Nat holds G.n is_simple_func_in S) &
    (for n be Nat holds dom(G.n) = dom f) &
    (for n be Nat holds G.n is nonnegative) &
    (for n,m be Nat st n <=m holds
      for x be Element of X st x in dom f holds (G.n).x <= (G.m).x ) &
    (for x be Element of X st x in dom f holds
      G#x is convergent & f.x <= lim(G#x)) by A1,A2,A4,A5,A6,AS4,AS41; then
    consider FF be ExtREAL_sequence such that
P1:  (for n be Nat holds FF.n = integral'(M,G.n)) &
     FF is convergent & sup rng FF = lim FF &
     integral'(M,f) <= lim FF by LM016;
    now let n be Element of NAT;
     FF.n = integral'(M,G.n) by P1;
     hence FF.n = L.n by A8;
    end; then
    FF=L by FUNCT_2:113;
    hence L is convergent & sup rng L = lim L & K.n0 <= lim L by A8,P1;
   end;
   hence K is convergent & L is convergent by P5;
   lim L <= lim K & lim K <= lim L by P5,P6,Cor12d;
   hence thesis by SUPINF_1:22;
end;

definition 
let X be non empty set;
let S be SigmaField of X;
let M be sigma_Measure of S;
let f be PartFunc of X,ExtREAL;
  assume that 
AS1:ex A be Element of S st A = dom f & f is_measurable_on A and
AS2:f is nonnegative;
func integral+(M,f) -> Element of ExtREAL means :Defintegral1:
ex F be Functional_Sequence of X,ExtREAL,
   K be ExtREAL_sequence st
    (for n be Nat holds F.n is_simple_func_in S & dom(F.n) = dom f) &
    (for n be Nat holds F.n is nonnegative) &
    (for n,m be Nat st n <=m holds
      for x be Element of X st x in dom f holds (F.n).x <= (F.m).x ) &
    (for x be Element of X st  x in dom f holds
      F#x is convergent & lim(F#x) = f.x) &
    (for n be Nat holds K.n=integral'(M,F.n)) &
    K is convergent &
    it=lim K;
existence
proof
   consider F be Functional_Sequence of X,ExtREAL such that
A3: (for n be Nat holds F.n is_simple_func_in S & dom(F.n) = dom f) &
    (for n be Nat holds F.n is nonnegative) &
    (for n,m be Nat st n <=m holds
      for x be Element of X st x in dom f holds (F.n).x <= (F.m).x ) &
    (for x be Element of X st x in dom f holds
      F#x is convergent & lim(F#x) = f.x) by AS1,AS2,LM015;
   reconsider g=F.0 as PartFunc of X,ExtREAL;
A4:for x be Element of X st x in dom f holds
    F#x is convergent & g.x <= lim(F#x)
   proof
    let x be Element of X such that
 AS:x in dom f;
 Q1:now let n,m be Nat;
     assume 
  PS1:n<=m;
     (F#x).n=(F.n).x & (F#x).m = (F.m).x by DefHx;
     hence (F#x).n <= (F#x).m by AS,A3,PS1;
    end;
 Q2:F#x is convergent & lim(F#x)=sup rng(F#x) by Q1,Cor12a;
    g.x=(F#x).0 by DefHx;
    hence thesis by Q2,Cor12c;
   end;
   dom g = dom f by A3; then
   g is_simple_func_in S & g is nonnegative &
   (for n be Nat holds F.n is_simple_func_in S) &
   (for n be Nat holds dom(F.n) = dom g) &
   (for n be Nat holds F.n is nonnegative) &
   (for n,m be Nat st n <=m holds
     for x be Element of X st x in dom g holds (F.n).x <= (F.m).x ) &
   for x be Element of X st x in dom g holds
     F#x is convergent & g.x <= lim(F#x) by A3,A4; then
   ex G be ExtREAL_sequence st
    (for n be Nat holds G.n = integral'(M,F.n)) &
    G is convergent & sup rng G=lim G &
    integral'(M,g) <= lim G by LM016; then
   consider G be ExtREAL_sequence such that
 P1:(for n be Nat holds G.n = integral'(M,F.n)) &
    G is convergent &
    integral'(M,g) <= lim G;
   take lim G;
   thus thesis by A3,P1;
end;
uniqueness
proof
   let s1,s2 be Element of ExtREAL such that
A1:ex F1 be Functional_Sequence of X,ExtREAL,
      K1 be ExtREAL_sequence st
    (for n be Nat holds F1.n is_simple_func_in S & dom(F1.n) = dom f) &
    (for n be Nat holds F1.n is nonnegative) &
    (for n,m be Nat st n <=m holds
      for x be Element of X st x in dom f holds (F1.n).x <= (F1.m).x ) &
    (for x be Element of X st x in dom f holds
      F1#x is convergent & lim(F1#x) = f.x) &
    (for n be Nat holds K1.n=integral'(M,F1.n)) &
    K1 is convergent & s1=lim K1 and
A2:ex F2 be Functional_Sequence of X,ExtREAL,
      K2 be ExtREAL_sequence st
    (for n be Nat holds F2.n is_simple_func_in S & dom(F2.n) = dom f) &
    (for n be Nat holds F2.n is nonnegative) &
    (for n,m be Nat st n <=m holds
      for x be Element of X st x in dom f holds (F2.n).x <= (F2.m).x ) &
    (for x be Element of X st x in dom f holds
      F2#x is convergent & lim(F2#x) = f.x) &
    (for n be Nat holds K2.n=integral'(M,F2.n)) &
    K2 is convergent & s2=lim K2;
   consider F1 be Functional_Sequence of X,ExtREAL,
            K1 be ExtREAL_sequence such that
A3: (for n be Nat holds F1.n is_simple_func_in S & dom(F1.n) = dom f) &
    (for n be Nat holds F1.n is nonnegative) &
    (for n,m be Nat st n <=m holds
      for x be Element of X st x in dom f holds (F1.n).x <= (F1.m).x ) &
    (for x be Element of X st x in dom f holds
      F1#x is convergent & lim(F1#x) = f.x) &
    (for n be Nat holds K1.n=integral'(M,F1.n)) &
    K1 is convergent & s1=lim(K1) by A1;
   consider F2 be Functional_Sequence of X,ExtREAL,
            K2 be ExtREAL_sequence such that
A4: (for n be Nat holds F2.n is_simple_func_in S & dom(F2.n) = dom f) &
    (for n be Nat holds F2.n is nonnegative) &
    (for n,m be Nat st n <=m holds
      for x be Element of X st x in dom f holds (F2.n).x <= (F2.m).x ) &
    (for x be Element of X st x in dom f holds
      (F2#x) is convergent & lim(F2#x) = f.x) &
    (for n be Nat holds K2.n=integral'(M,F2.n)) &
    K2 is convergent & s2=lim K2 by A2;
   for x be Element of X st x in dom f holds
    F1#x is convergent & F2#x is convergent & lim(F1#x) = lim(F2#x)
   proof
    let x be Element of X; assume 
 V2:x in dom f; then
    lim(F1#x) = f.x by A3 .= lim(F2#x) by V2,A4;
    hence thesis by V2,A3,A4;
   end;
   hence s1=s2 by AS1,A3,A4,LM017;
end;
end;

theorem LM018a:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL st
 f is_simple_func_in S & f is nonnegative holds
   integral+(M,f) =integral'(M,f)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL such that
AS:f is_simple_func_in S & f is nonnegative;
   ex GF be Finite_Sep_Sequence of S st
    dom f = union rng GF &
     for n being Nat,x,y being Element of X st
      n in dom GF & x in GF.n & y in GF.n holds f.x = f.y
       by MESFUNC2:def 5,AS; then
   reconsider A=dom f as Element of S by MESFUNC2:34;
A1:f is_measurable_on A by MESFUNC2:37,AS;
   deffunc PF(Nat) = f;
   consider F be Functional_Sequence of X,ExtREAL such that
aA2: for n be Element of NAT holds F.n=PF(n) from SEQFUNC:sch 1;
A2: now let n be Nat; n in NAT by ORDINAL1:def 13;
      hence F.n=PF(n) by aA2;
    end;
   deffunc PK(Nat) = integral'(M,(F.$1));
   consider K be Function of NAT,ExtREAL such that
aA3: for n be Element of NAT holds K.n = PK(n) from FUNCT_2:sch 4;
A3: now let n be Nat; n in NAT by ORDINAL1:def 13;
      hence K.n=PK(n) by aA3;
    end;
A4:for n be Nat holds F.n is_simple_func_in S & dom(F.n) = dom f by AS,A2;
A5:for n be Nat holds F.n is nonnegative by AS,A2;
A6:for n,m be Nat st n <=m holds
    for x be Element of X st x in dom f holds (F.n).x <= (F.m).x
   proof
    let n,m be Nat;
    assume n<=m;
    let x be Element of X;
    assume x in dom f;
    (F.n).x=f.x by A2;
    hence (F.n).x <=(F.m).x by A2;
   end;
A7:for x be Element of X st x in dom f holds
     F#x is convergent & lim(F#x) = f.x
   proof
    let x be Element of X;
    assume x in dom f;
    now let n be Nat;
     thus (F#x).n = (F.n).x by DefHx .=f.x by A2;
    end;
    hence F#x is convergent & lim(F#x) = f.x by Cor14;
   end;
   for n be Nat holds K.n=integral'(M,f)
   proof
    let n be Nat;
    thus K.n=integral'(M,F.n) by A3
           .=integral'(M,f) by A2;
   end; then
   K is convergent & lim K=integral'(M,f) by Cor14;
   hence integral+(M,f)=integral'(M,f)
      by Defintegral1,AS,A1,A3,A4,A5,A6,A7;
end;

LM018:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
 ( ex A be Element of S st 
    A = dom f & A = dom g & f is_measurable_on A & g is_measurable_on A ) &
 f is nonnegative & g is nonnegative
 holds integral+(M,f+g) = integral+(M,f) + integral+(M,g)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL such that
H1: ex A be Element of S st 
     A = dom f & A = dom g & f is_measurable_on A & g is_measurable_on A and
H2: f is nonnegative & g is nonnegative;
   consider F1 be Functional_Sequence of X,ExtREAL,
            K1 be ExtREAL_sequence such that
A1: (for n be Nat holds F1.n is_simple_func_in S & dom(F1.n) = dom f) &
    (for n be Nat holds F1.n is nonnegative) &
    (for n,m be Nat st n <=m holds
      for x be Element of X st x in dom f holds (F1.n).x <= (F1.m).x ) &
    (for x be Element of X st x in dom f holds
      F1#x is convergent & lim(F1#x) = f.x) &
    (for n be Nat holds K1.n=integral'(M,F1.n)) &
    K1 is convergent &
    integral+(M,f)=lim K1 by H1,H2,Defintegral1;
   consider F2 be Functional_Sequence of X,ExtREAL,
            K2 be ExtREAL_sequence such that
A2: (for n be Nat holds F2.n is_simple_func_in S & dom(F2.n) = dom g) &
    (for n be Nat holds F2.n is nonnegative) &
    (for n,m be Nat st n <=m holds
      for x be Element of X st x in dom g holds (F2.n).x <= (F2.m).x ) &
    (for x be Element of X st x in dom g holds
      F2#x is convergent & lim(F2#x) = g.x) &
    (for n be Nat holds K2.n=integral'(M,F2.n)) &
    K2 is convergent &
    integral+(M,g)=lim K2 by H1,H2,Defintegral1;
   now let n be set;
    assume n in dom K1; then
    reconsider n1 = n as Element of NAT;
    K1.n1 = integral'(M,F1.n1) & F1.n1 is_simple_func_in S &
    F1.n1 is nonnegative by A1;
    hence -infty < K1.n by Cor9;
   end; then
A3:K1 is without-infty by WMI01;
A4:for n,m be Nat st n<=m holds K1.n <= K1.m
   proof
    let n,m be Nat such that Q:n<=m;
B1: K1.n = integral'(M,F1.n) & K1.m = integral'(M,F1.m) & dom(F1.n) = dom f & 
    F1.n is_simple_func_in S & F1.m is_simple_func_in S & dom(F1.m) = dom f &
    F1.n is nonnegative & F1.m is nonnegative by A1;
N0: now let x be set;
     assume x in dom(F1.m - F1.n); then
     x in (dom(F1.m) /\ dom(F1.n)) \ 
      ( (F1.m)"{+infty}/\(F1.n)"{+infty} \/ (F1.m)"{-infty}/\(F1.n)"{-infty} )
        by MESFUNC1:def 4; then
     x in dom(F1.m) /\ dom(F1.n) by XBOOLE_0:def 4;
     hence (F1.n).x <= (F1.m).x by A1,B1,Q;
    end; then
    dom(F1.m - F1.n) = dom(F1.m) /\ dom(F1.n) by B1,Diff1; then
    (F1.n)|dom(F1.m - F1.n) = F1.n & (F1.m)|dom(F1.m - F1.n) = F1.m 
       by B1,GRFUNC_1:64; 
    hence K1.n <= K1.m by N0,B1,Cor9a;
   end;
   now let n be set;
    assume n in dom K2; then
    reconsider n1=n as Element of NAT;
    K2.n1 = integral'(M,F2.n1) & F2.n1 is_simple_func_in S &
    F2.n1 is nonnegative by A2;
    hence -infty < K2.n by Cor9;
   end; then
A5:K2 is without-infty by WMI01;
P4:for n,m be Nat st n<=m holds K2.n <= K2.m
   proof
    let n,m be Nat such that Q:n<=m;
B1: K2.n = integral'(M,F2.n) & K2.m = integral'(M,F2.m) & dom(F2.n) = dom g & 
    F2.n is_simple_func_in S & F2.m is_simple_func_in S & dom(F2.m) = dom g &
    F2.n is nonnegative & F2.m is nonnegative by A2;
N0: now let x be set;
     assume x in dom(F2.m - F2.n); then
     x in (dom(F2.m) /\ dom(F2.n)) \ 
      ( ((F2.m)"{+infty}/\(F2.n)"{+infty})
         \/((F2.m)"{-infty}/\(F2.n)"{-infty}) ) by MESFUNC1:def 4; then
     x in dom(F2.m) /\ dom(F2.n) by XBOOLE_0:def 4;
     hence (F2.n).x <= (F2.m).x by A2,B1,Q;
    end; then
    dom(F2.m - F2.n) = dom(F2.m) /\ dom(F2.n) by B1,Diff1; then
    F2.n|dom(F2.m - F2.n) = F2.n & F2.m|dom(F2.m - F2.n) = F2.m 
      by GRFUNC_1:64,B1;
    hence K2.n <= K2.m by N0,B1,Cor9a;
   end;
   consider A be Element of S such that
P5: A = dom f & A = dom g &
    f is_measurable_on A & g is_measurable_on A by H1;
P6:f is without-infty & g is without-infty by H2,WMI02; then
P7:f+g is_measurable_on A by MESFUNC2'7,P5;
   A =dom f /\ dom g by P5; then
P8:A =dom(f+g) by MESFUNC2'2,P6;
P9:f+g is nonnegative by H2,LL4;
   deffunc PF(Nat) = F1.$1+F2.$1;
   consider F be Functional_Sequence of X,ExtREAL such that
aQ1: for n be Element of NAT holds F.n=PF(n) from SEQFUNC:sch 1;
Q1:  now let n be Nat; n in NAT by ORDINAL1:def 13;
       hence F.n=PF(n) by aQ1;
     end;
   deffunc PK(Nat) = integral'(M,F.$1);
   consider K be ExtREAL_sequence such that
aQ2: for n be Element of NAT holds K.n = PK(n) from FUNCT_2:sch 4;
Q2: now let n be Nat; n in NAT by ORDINAL1:def 13;
      hence K.n = PK(n) by aQ2;
    end;
C1:for n be Nat holds
    F.n is_simple_func_in S & dom(F.n) = dom(f+g) & F.n is nonnegative
   proof
    let n be Nat;
 P0:dom(F1.n)=dom f & F1.n is_simple_func_in S & F1.n is nonnegative by A1;
 P2:dom(F2.n)=dom g & F2.n is_simple_func_in S & F2.n is nonnegative by A2;
 P3:F.n=F1.n+F2.n by Q1;
    hence F.n is_simple_func_in S by P0,P2,LL2;
    dom(F.n)= dom(F1.n) /\ dom(F2.n) by P0,P2,P3,Th1a;
    hence dom(F.n) = dom(f+g) by P0,P2,MESFUNC2'2,P6;
    F1.n is nonnegative & F2.n is nonnegative & F.n=F1.n+F2.n by Q1,A1,A2;
    hence F.n is nonnegative by LL4;
   end;
C3:for n,m be Nat st n <=m holds
    for x be Element of X st x in dom (f+g) holds (F.n).x <= (F.m).x
   proof
    let n,m be Nat;
    assume A:n<=m;
    let x be Element of X;
    assume B:x in dom (f+g);
    dom(F1.n+F2.n) = dom(F.n) & dom(F1.m+F2.m) = dom(F.m) by Q1; then
T2: dom(F1.n+F2.n) = dom(f+g) & dom(F1.m+F2.m) = dom(f+g) by C1;
    (F.n).x =(F1.n+F2.n).x & (F.m).x =(F1.m+F2.m).x by Q1; then
T6: (F.n).x = (F1.n).x+(F2.n).x &
    (F.m).x = (F1.m).x+(F2.m).x by B,T2,MESFUNC1:def 3;
    F1.n is nonnegative & F2.n is nonnegative by A1,A2; then
    F1.n is without-infty & F2.n is without-infty by WMI02; then
T10:-infty < (F1.n).x & -infty < (F2.n).x by Def0003;
    (F1.n).x <= (F1.m).x & (F2.n).x <= (F2.m).x by A,A1,A2,B,P8,P5;
    hence (F.n).x <= (F.m).x by T6,T10,SUPINF_2:14;
   end;
C4:for x be Element of X st x in dom(f+g) holds
    F#x is convergent & lim(F#x) = (f+g).x
   proof
    let x be Element of X;
    assume A0:x in dom(f+g);
 B1:now let n be Nat;
 QQ2:dom(F1.n+F2.n) = dom(F.n) by Q1 .=dom(f+g)  by C1;
     (F#x).n = (F.n).x by DefHx; then
     (F#x).n = (F1.n+F2.n).x by Q1; then
     (F#x).n = (F1.n).x+(F2.n).x by A0,QQ2,MESFUNC1:def 3; then
     (F#x).n = (F1#x).n+(F2.n).x by DefHx;
     hence (F#x).n = (F1#x).n+(F2#x).n by DefHx;
    end;
    now let n be set;
     hereby assume n in dom(F1#x); then
      reconsider n1=n as Element of NAT;
      F1.n1 is nonnegative by A1; then
   Q0:F1.n1 is without-infty by WMI02;
      (F1#x).n1 = (F1.n1).x by DefHx;
      hence -infty < (F1#x).n by Q0,Def0003;
     end;
     assume n in dom(F2#x); then
     reconsider n1=n as Element of NAT;
     F2.n1 is nonnegative by A2; then
  Q0:F2.n1 is without-infty by WMI02;
     (F2#x).n1 = (F2.n1).x by DefHx;
     hence -infty < (F2#x).n by Q0,Def0003;
    end; then
 Q2:F1#x is without-infty & F2#x is without-infty by WMI01;
 S1:now let n,m be Nat;
     assume AA:n <=m;
     (F1#x).n = (F1.n).x & (F1#x).m = (F1.m).x by DefHx;
     hence (F1#x).n<= (F1#x).m by A0,AA,A1,P5,P8;
    end;
 S2:now let n,m be Nat;
     assume AA:n <=m;
     (F2#x).n = (F2.n).x & (F2#x).m = (F2.m).x by DefHx;
     hence (F2#x).n<= (F2#x).m by A0,AA,A2,P5,P8;
    end;
    lim(F1#x) + lim(F2#x) =f.x + lim(F2#x) by A0,P5,P8,A1; then
    lim(F1#x) + lim(F2#x) =f.x + g.x by A0,P5,P8,A2; then
    lim(F1#x) + lim(F2#x) =(f+g).x by A0,MESFUNC1:def 3;
    hence thesis by Cor16,B1,Q2,S1,S2;
   end;
   for n be Nat holds K.n=K1.n+K2.n
   proof
    let n be Nat;
 P1:F1.n is_simple_func_in S & F1.n is nonnegative by A1;
 P2:F2.n is_simple_func_in S & F2.n is nonnegative by A2;
 P3:F.n=F1.n+F2.n by Q1;
 P4:dom(F1.n) =dom f by A1
             .= dom(F2.n) by A2,P5;
 P7:K2.n=integral'(M,F2.n) by A2;
P8: dom(F.n) = dom(F1.n) /\ dom(F2.n) by P1,P2,P3,Th1a;
    K.n=integral'(M,F.n) by Q2; then
    K.n=integral'(M,F1.n|dom(F1.n)) + integral'(M,F2.n|dom(F2.n))
         by P4,P1,P8,P2,P3,Th1a; then
    K.n=integral'(M,F1.n) + integral'(M,F2.n|dom(F2.n)) by GRFUNC_1:64; then
    K.n=integral'(M,F1.n) + integral'(M,F2.n) by GRFUNC_1:64;
    hence K.n= K1.n +K2.n by A1,P7;
   end; then
   K is convergent & lim K=lim K1+lim K2 by Cor16,A3,A4,A5,P4;
   hence thesis by A1,A2,Defintegral1,C1,C3,C4,P7,P8,P9,Q2;
end;

theorem LM018b:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
 ( ex A be Element of S st A = dom f & f is_measurable_on A ) &
 ( ex B be Element of S st B = dom g & g is_measurable_on B ) &
 f is nonnegative & g is nonnegative
 holds
   ex C be Element of S st 
    C = dom(f+g) & 
    integral+(M,f+g) = integral+(M,f|C) + integral+(M,g|C)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL;
   assume that
A1: ( ex A be Element of S st A = dom f & f is_measurable_on A ) and
A2: ( ex B be Element of S st B = dom g & g is_measurable_on B ) and
A3: f is nonnegative & g is nonnegative;
   consider A be Element of S such that
A5: A = dom f & f is_measurable_on A by A1;
   consider B be Element of S such that
A6: B = dom g & g is_measurable_on B by A2;
   set f1 = f|(dom f /\ dom g);
   set g1 = g|(dom f /\ dom g);
   take C = A /\ B;
   f is without-infty & g is without-infty by A3,WMI02; then
A8:C = dom(f+g) by A5,A6,MESFUNC2'2;
   dom f1 = dom f /\ (dom f /\ dom g) & dom g1 = dom g /\ (dom f /\ dom g)
     by FUNCT_1:68; then
B1:dom f1 = dom f /\ dom f /\ dom g & dom g1 = dom g /\ dom g /\ dom f
     by XBOOLE_1:16;
   C c= dom f & C c= dom g by A5,A6,XBOOLE_1:17; then
   f is_measurable_on C & C = dom f /\ C &
   g is_measurable_on C & C = dom g /\ C by A5,A6,XBOOLE_1:28,MESFUNC1:34; then
B3:f|C is_measurable_on C & g|C is_measurable_on C by Cor18;
C1:f1 is nonnegative & g1 is nonnegative by A3,LL0; then
P1:integral+(M,f1+g1) = integral+(M,f1) + integral+(M,g1) by A5,A6,B1,B3,LM018;
   f1 is without-infty & g1 is without-infty by C1,WMI02; then
R1:dom(f1+g1) = dom f1 /\ dom g1 & dom(f1+g1) = C /\ C by B1,A5,A6,MESFUNC2'2;
   for x be set st x in dom(f1+g1) holds (f1+g1).x = (f+g).x
   proof
    let x be set;
    assume 
 V1:x in dom(f1+g1); then
 V2:x in dom f1 & x in dom g1 by R1,XBOOLE_0:def 3;
    (f1+g1).x = f1.x + g1.x by V1,MESFUNC1:def 3
             .= f.x + g1.x by V2,FUNCT_1:70
             .= f.x + g.x by V2,FUNCT_1:70;
    hence (f1+g1).x = (f+g).x by V1,R1,A8,MESFUNC1:def 3;
   end;
   hence thesis by A5,A6,A8,P1,R1,FUNCT_1:9;
end;

theorem LM018c:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL st
 (ex A be Element of S st A = dom f & f is_measurable_on A) &
 f is nonnegative holds
   0 <= integral+(M,f)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL; 
   assume (ex A be Element of S st A = dom f & f is_measurable_on A  )&
          f is nonnegative; then
   consider F1 be Functional_Sequence of X,ExtREAL,
            K1 be ExtREAL_sequence such that
AS2:(for n be Nat holds F1.n is_simple_func_in S & dom(F1.n) = dom f) &
    (for n be Nat holds F1.n is nonnegative) &
    (for n,m be Nat st n <=m holds
       for x be Element of X st x in dom f holds (F1.n).x <= (F1.m).x ) &
    (for x be Element of X st x in dom f holds
       F1#x is convergent & lim(F1#x) = f.x) &
       (for n be Nat holds K1.n=integral'(M,F1.n)) &
    K1 is convergent &
    integral+(M,f)=lim K1 by Defintegral1;
P1:for n be Nat holds 0 <= K1.n
   proof
    let n be Nat;
    K1.n = integral'(M,F1.n) & F1.n is_simple_func_in S &
    F1.n is nonnegative by AS2;
    hence 0<= K1.n by Cor9;
   end;
   for n,m be Nat st n<=m holds K1.n <= K1.m
   proof
    let n,m be Nat;
    assume Q:n<=m;
P12:F1.n is_simple_func_in S & F1.m is_simple_func_in S &
    dom(F1.n) = dom f & dom(F1.m) = dom f &
    F1.n is nonnegative & F1.m is nonnegative &
    K1.m = integral'(M,F1.m) by AS2;
N0: for x be set st x in dom(F1.m - F1.n) holds (F1.n).x <= (F1.m).x
    proof
     let x be set;
     assume x in dom(F1.m - F1.n); then
     x in (dom(F1.m) /\ dom(F1.n))\
      (((F1.m)"{+infty}/\(F1.n)"{+infty})
        \/((F1.m)"{-infty}/\(F1.n)"{-infty})) by MESFUNC1:def 4; then
     x in dom(F1.m) /\ dom(F1.n) by XBOOLE_0:def 4;
     hence thesis by AS2,P12,Q;
    end; then
N1: integral'(M,F1.n|dom(F1.m - F1.n)) <= integral'(M,F1.m|dom(F1.m - F1.n))
       by P12,Cor9a;
    dom(F1.m - F1.n) = dom(F1.m) /\ dom(F1.n) by P12,N0,Diff1; then
    F1.n|dom(F1.m - F1.n) = F1.n & F1.m|dom(F1.m - F1.n) = F1.m 
      by GRFUNC_1:64,P12;
    hence K1.n <= K1.m by AS2,P12,N1;
   end; then
   lim K1 =sup rng K1 by Cor12a; then
   K1.0 <= lim K1 by Cor12c;
   hence thesis by AS2,P1;
end;

theorem LM018d1:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    A be Element of S st
  (ex E be Element of S st E = dom f & f is_measurable_on E ) &
  f is nonnegative holds
    0<= integral+(M,f|A)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       A be Element of S;
   assume
A1: (ex E be Element of S st E = dom f & f is_measurable_on E) &
    f is nonnegative; then
   consider E be Element of S such that
V1: E = dom f & f is_measurable_on E;
   set C = E/\A;
P0:C c= E by XBOOLE_1:17; then
A2:f is_measurable_on C by V1,MESFUNC1:34;
A3:C = dom(f|A) by V1,FUNCT_1:68;
   dom f /\ C = C by P0,V1,XBOOLE_1:28; then
P1:f|C is_measurable_on C by A2,Cor18;
P4:dom(f|A) = C by V1,FUNCT_1:68 .= dom f /\ C by P0,V1,XBOOLE_1:28 
     .= dom(f|C) by FUNCT_1:68;
   for x be set st x in dom(f|A) holds (f|A).x = (f|C).x
   proof
    let x be set;
    assume P2:x in dom(f|A); then
    (f|A).x = f.x by FUNCT_1:70;
    hence thesis by P2,P4,FUNCT_1:70;
   end; then
A4:f|A is_measurable_on C by P1,P4,FUNCT_1:9;
   f|A is nonnegative by A1,LL0;
   hence thesis by A3,A4,LM018c;
end;

theorem LM018d: 
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    A,B be Element of S st
  (ex E be Element of S st E = dom f & f is_measurable_on E) &
  f is nonnegative & A misses B
 holds
    integral+(M,f|(A\/B)) = integral+(M,f|A)+integral+(M,f|B)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       A,B be Element of S;
   assume
AS: (ex E be Element of S st E = dom f & f is_measurable_on E) &
    f is nonnegative & A misses B; then
   consider E be Element of S such that
V1: E = dom f and
V2: f is_measurable_on E;
   set C = E/\(A\/B);
A1:dom(f|(A\/B)) = C by V1,FUNCT_1:68;
N1:C c= dom f by V1,XBOOLE_1:17; then
N2:C = dom f /\ C by XBOOLE_1:28; then
A2:dom(f|(A\/B)) = dom(f|C) by A1,FUNCT_1:68;
   for x be set st x in dom(f|(A\/B)) holds f|(A\/B).x = f|C.x
   proof
    let x be set;
    assume
A3:  x in dom(f|(A\/B)); then
    f|(A\/B).x = f.x by FUNCT_1:70;
    hence thesis by A2,A3,FUNCT_1:70;
   end; then
P1:f|(A\/B) = f|C by A2,FUNCT_1:9;
   f is_measurable_on C by N1,V1,V2,MESFUNC1:34; then
X0:f|(A\/B) is_measurable_on C by P1,N2,Cor18;
X2:f|(A\/B) is nonnegative by AS,LL0;
   consider F0 be Functional_Sequence of X,ExtREAL,
            K0 be ExtREAL_sequence such that
AS02:(for n be Nat holds F0.n is_simple_func_in S & dom(F0.n) = dom f) &
    (for n be Nat holds F0.n is nonnegative) &
    (for n,m be Nat st n <=m holds
      for x be Element of X st x in dom f holds (F0.n).x <= (F0.m).x ) &
    (for x be Element of X st  x in dom f holds
      F0#x is convergent & lim(F0#x) = f.x) &
    (for n be Nat holds K0.n=integral'(M,F0.n)) &
    K0 is convergent &
    integral+(M,f)=lim K0 by AS,Defintegral1;
   deffunc PFAB(Nat) = (F0.$1)|(A\/B);
   consider FAB be Functional_Sequence of X,ExtREAL such that
AS03:for n be Element of NAT holds FAB.n=PFAB(n) from SEQFUNC:sch 1;
   deffunc PK(Nat) = integral'(M,FAB.$1);
   consider KAB be ExtREAL_sequence such that
aAS04:for n be Element of NAT holds KAB.n = PK(n) from FUNCT_2:sch 4;
AS04:now let n be Nat; n in NAT by ORDINAL1:def 13;
       hence KAB.n = PK(n) by aAS04;
     end;
AS1:(for n be Nat holds FAB.n is_simple_func_in S &
      dom(FAB.n) = dom(f|(A\/B))) &
   (for n be Nat holds for x be Element of X st x in dom(f|(A\/B))
        holds (FAB.n).x = (F0.n).x) &
   (for n be Nat holds FAB.n is nonnegative) &
   (for n,m be Nat st n <=m holds
      for x be Element of X st x in dom(f|(A\/B))
          holds (FAB.n).x <= (FAB.m).x ) &
   (for x be Element of X st  x in dom(f|(A\/B)) holds
        FAB#x is convergent & lim(FAB#x) = f|(A\/B).x)
   proof
    thus
J2: now let n be Nat;
ww:      n in NAT by ORDINAL1:def 13;
      then
Y3:  FAB.n=(F0.n)|(A \/ B) by AS03;
     F0.n is_simple_func_in S by AS02;
     hence FAB.n is_simple_func_in S by Y3,Cor5;
     thus dom(FAB.n) = dom(F0.n|(A\/B))  by AS03,ww
                    .= dom(F0.n) /\ (A \/ B) by FUNCT_1:68
                    .= dom f /\ (A\/B) by AS02
                    .= dom(f|(A\/B)) by FUNCT_1:68;
    end;
    thus
J3: now let n be Nat, x be Element of X;
     assume 
      x in dom(f|(A\/B)); then
  Y4:x in dom(FAB.n) by J2;
  n in NAT by ORDINAL1:def 13;
  then FAB.n=F0.n|(A \/ B) by AS03;
     hence (FAB.n).x = (F0.n).x by Y4,FUNCT_1:68;
    end;
    hereby let n be Nat;
      reconsider n1=n as Element of NAT by ORDINAL1:def 13;
     F0.n is nonnegative by AS02; then
     F0.n1|(A\/B) is nonnegative by LL0;
     hence FAB.n is nonnegative by AS03;
    end;
    hereby let n ,m be Nat such that 
  Y8: n <= m;
     now let x be Element of X;
      assume 
   Y9: x in dom(f|(A\/B)); then
      x in dom f /\ (A\/B) by FUNCT_1:68; then
  Y11:x in dom f by XBOOLE_0:def 3;
      (FAB.n).x = (F0.n).x & (FAB.m).x = (F0.m).x by J3,Y9;
      hence (FAB.n).x <= (FAB.m).x by Y11,Y8,AS02;
     end;
     hence 
      for x be Element of X st x in dom(f|(A\/B)) holds (FAB.n).x <= (FAB.m).x;
    end;
    hereby let x be Element of X;
     assume 
 Y12: x in dom(f|(A\/B)); then
     x in dom f /\ (A\/B) by FUNCT_1:68; then
 Y14:x in dom f by XBOOLE_0:def 3;
 Y13:now let n be Element of NAT;
      thus (FAB#x).n = (FAB.n).x by DefHx
                    .=(F0.n).x by J3,Y12
                    .=(F0#x).n by DefHx;
     end; then
     FAB#x=F0#x by FUNCT_2:113;
     hence FAB#x is convergent by AS02,Y14;
     thus lim(FAB#x) = lim(F0#x) by Y13,FUNCT_2:113
                    .= f.x by AS02,Y14
                    .= f|(A\/B).x by Y12, FUNCT_1:68;
    end;
   end;
   for n,m be Nat st n<=m holds KAB.n <= KAB.m
   proof
    let n,m be Nat;
    assume Q:n<=m;
    reconsider n,m as Element of NAT by ORDINAL1:def 13;
P12:FAB.n is_simple_func_in S & FAB.m is_simple_func_in S &
    dom(FAB.n) = dom (f|(A\/B)) & dom(FAB.m) = dom(f|(A\/B)) &
    FAB.n is nonnegative & FAB.m is nonnegative by AS1;
P11:KAB.m = integral'(M,FAB.m) by AS04;
N0: for x be set st x in dom(FAB.m - FAB.n) holds (FAB.n).x <= (FAB.m).x
    proof
     let x be set;
     assume x in dom(FAB.m - FAB.n); then
     x in (dom(FAB.m) /\ dom(FAB.n)) \ ((FAB.m)"{+infty}/\(FAB.n)"{+infty}
         \/(FAB.m)"{-infty}/\(FAB.n)"{-infty}) by MESFUNC1:def 4; then
     x in dom(FAB.m) /\ dom(FAB.n) by XBOOLE_0:def 4;
     hence thesis by AS1,P12,Q;
    end; then
N1: integral'(M,FAB.n|dom(FAB.m - FAB.n)) 
      <= integral'(M,FAB.m|dom(FAB.m - FAB.n)) by P12,Cor9a;
    dom(FAB.m - FAB.n) = dom(FAB.m) /\ dom(FAB.n) by P12,N0,Diff1; then
    FAB.m|dom(FAB.m - FAB.n) = FAB.m &
    FAB.n|dom(FAB.m - FAB.n) = FAB.n by GRFUNC_1:64,P12;
    hence thesis by AS04,P11,N1;
   end; then
   KAB is convergent by Cor12a; then
I1:integral+(M,f|(A\/B)) = lim KAB by Defintegral1,AS1,AS04,X0,A1,X2;
   set DA = E /\ A;
   set DB = E /\ B;
   DA c= E & DB c= E by XBOOLE_1:17; then
B4:f is_measurable_on DA & f is_measurable_on DB by V2,MESFUNC1:34;
E1:DA = dom(f|A) & DB = dom(f|B) by V1,FUNCT_1:68; then
   DA c= dom f & DB c= dom f by RELAT_1:89; then
P1:dom f /\ DA = DA & dom f /\ DB  = DB by XBOOLE_1:28; then
P2:dom(f|DA) = dom(f|A) & dom(f|DB) = dom(f|B) by E1,RELAT_1:90;
   for x be set st x in dom(f|DA) holds (f|DA).x = (f|A).x
   proof
    let x be set;
    assume P3:x in dom(f|DA); then
    f|A.x = f.x by P2,FUNCT_1:70;
    hence thesis by P3,FUNCT_1:70;
   end; then
   f|DA = f|A by P2,FUNCT_1:9; then
E2:f|A is_measurable_on DA by P1,B4,Cor18;
   for x be set st x in dom(f|DB) holds (f|DB).x = (f|B).x
   proof
    let x be set;
    assume P3:x in dom(f|DB); then
    f|B.x = f.x by P2,FUNCT_1:70;
    hence thesis by P3,FUNCT_1:70;
   end; then
   f|DB = f|B by P2,FUNCT_1:9; then
E4:f|B is_measurable_on DB by P1,B4,Cor18;
AS12:f|A is nonnegative & f|B is nonnegative by AS,LL0;
   deffunc PFA(Nat) = F0.$1|A;
   consider FA be Functional_Sequence of X,ExtREAL such that
AS5a:for n be Element of NAT holds FA.n=PFA(n) from SEQFUNC:sch 1;
   deffunc PFB(Nat) = F0.$1|B;
   consider FB be Functional_Sequence of X,ExtREAL such that
AS5b:for n be Element of NAT holds FB.n=PFB(n) from SEQFUNC:sch 1;
C1:for n be Nat holds
    FA.n is_simple_func_in S & FB.n is_simple_func_in S &
    dom(FA.n) = dom(f|A) & dom(FB.n) = dom(f|B)
   proof
    let n be Nat;
    reconsider n1=n as Element of NAT by ORDINAL1:def 13;
 P1:F0.n is_simple_func_in S & F0.n is nonnegative by AS02;
 P2:FA.n1 = F0.n1|A & FB.n1=F0.n1|B by AS5a,AS5b;
    hence FA.n is_simple_func_in S & FB.n is_simple_func_in S by Cor5,P1;
    dom(FA.n)=dom(F0.n) /\ A & dom(FB.n) = dom(F0.n) /\ B by P2,FUNCT_1:68;
    hence dom(FA.n) = dom(f|A) & dom(FB.n) = dom(f|B) by E1,V1,AS02;
   end;
C2:for n be Nat holds FA.n is nonnegative & FB.n is nonnegative
   proof
    let n be Nat;
    reconsider n as Element of NAT by ORDINAL1:def 13;
    F0.n is nonnegative by AS02; then
    F0.n|A is nonnegative & F0.n|B is nonnegative by LL0;
    hence thesis by AS5a,AS5b;
   end;
C3a:for n,m be Nat st n <=m holds
     for x be Element of X st x in dom(f|A) holds (FA.n).x <= (FA.m).x
    proof
     let n,m be Nat;
     assume A:n<=m;
     let x be Element of X;
     assume B:x in dom(f|A); then
     x in dom f /\ A by FUNCT_1:68; then
P0:  x in dom f by XBOOLE_0:def 3;
     reconsider n,m as Element of NAT by ORDINAL1:def 13;
     dom(F0.n|A) = dom(FA.n) & dom(F0.m|A) = dom(FA.m) by AS5a; then
T2:  dom(F0.n|A) = dom(f|A) & dom(F0.m|A) = dom(f|A) by C1;
     (FA.n).x =(F0.n|A).x & (FA.m).x =(F0.m|A).x by AS5a; then
     (FA.n).x =(F0.n).x & (FA.m).x = (F0.m).x by B,T2,FUNCT_1:68;
     hence thesis by A,P0,AS02;
    end;
C3b:for n,m be Nat st n <=m holds
     for x be Element of X st x in dom(f|B) holds (FB.n).x <= (FB.m).x
    proof
     let n,m be Nat;
     assume A:n<=m;
     let x be Element of X;
     assume B:x in dom(f|B); then
     x in dom f /\ B by FUNCT_1:68; then
P0:  x in dom f by XBOOLE_0:def 3;
     reconsider n,m as Element of NAT by ORDINAL1:def 13;
     dom(F0.n|B) = dom(FB.n) & dom(F0.m|B) = dom(FB.m) by AS5b; then
T2:  dom(F0.n|B) = dom(f|B) & dom(F0.m|B) = dom(f|B) by C1;
     (FB.n).x =(F0.n|B).x & (FB.m).x =(F0.m|B).x by AS5b; then
     (FB.n).x =(F0.n).x & (FB.m).x = (F0.m).x by B,T2,FUNCT_1:68;
     hence thesis by A,P0,AS02;
    end;
C4a:for x be Element of X st x in dom(f|A) holds
     FA#x is convergent & lim(FA#x) = f|A.x
    proof
     let x be Element of X;
     assume A0:x in dom(f|A); then
     x in dom f /\ A by FUNCT_1:68; then
P0:  x in dom f by XBOOLE_0:def 3;
     now let n be Element of NAT;
 QQ2: dom(F0.n|A) = dom (FA.n) by AS5a .=dom(f|A) by C1;
      (FA#x).n = (FA.n).x by DefHx; then
      (FA#x).n = (F0.n|A).x by AS5a; then
      (FA#x).n = (F0.n).x by A0,QQ2,FUNCT_1:68;
      hence (FA#x).n = (F0#x).n by DefHx;
     end; then
Q2:  FA#x = F0#x by FUNCT_2:113;
     F0#x is convergent & lim(F0#x)=f.x by AS02,P0;
     hence thesis by Q2,A0,FUNCT_1:68;
    end;
C4b:for x be Element of X st x in dom(f|B) holds
     FB#x is convergent & lim(FB#x) = f|B.x
    proof
     let x be Element of X;
     assume A0:x in dom(f|B); then
     x in dom f /\ B by FUNCT_1:68; then
P0:  x in dom f by XBOOLE_0:def 3;
     now let n be Element of NAT;
 QQ2: dom(F0.n|B) = dom(FB.n) by AS5b .=dom(f|B) by C1;
      thus (FB#x).n = (FB.n).x by DefHx
                   .=(F0.n|B).x by AS5b
                   .=(F0.n).x by A0,QQ2,FUNCT_1:68
                   .=(F0#x).n by DefHx;
     end; then
Q2:  FB#x=F0#x by FUNCT_2:113;
     F0#x is convergent & lim(F0#x)=f.x by AS02,P0;
     hence thesis by Q2,A0,FUNCT_1:68;
    end;
    deffunc PKA(Nat) = integral'(M,FA.$1);
    consider KA be ExtREAL_sequence such that
aAS6a:for n be Element of NAT holds KA.n = PKA(n) from FUNCT_2:sch 4;
AS6a:now let n be Nat; n in NAT by ORDINAL1:def 13;
       hence KA.n = PKA(n) by aAS6a;
     end;
    deffunc PKB(Nat) = integral'(M,FB.$1);
    consider KB be ExtREAL_sequence such that
aAS6b:for n be Element of NAT holds KB.n = PKB(n) from FUNCT_2:sch 4;
AS6b:now let n be Nat; n in NAT by ORDINAL1:def 13;
       hence KB.n = PKB(n) by aAS6b;
     end;
    for n,m be Nat st n<=m holds KA.n <= KA.m & KB.n <= KB.m
    proof
     let n,m be Nat;
     assume Q:n<=m;
P12: FA.n is_simple_func_in S & FA.m is_simple_func_in S &
     FB.n is_simple_func_in S & FB.m is_simple_func_in S &
     dom(FA.n) = dom(f|A) & dom(FA.m) = dom(f|A) &
     dom(FB.n) = dom(f|B) & dom(FB.m) = dom(f|B) by C1;
P14: FA.n is nonnegative & FA.m is nonnegative &
     FB.n is nonnegative & FB.m is nonnegative by C2;
P11: KA.m = integral'(M,FA.m) & KB.m = integral'(M,FB.m) by AS6a,AS6b;
N0:  for x be set st x in dom(FA.m - FA.n) holds (FA.n).x <= (FA.m).x 
     proof
      let x be set;
      assume x in dom(FA.m - FA.n); then
      x in (dom(FA.m) /\ dom(FA.n))
       \( ((FA.m)"{+infty}/\(FA.n)"{+infty})
        \/((FA.m)"{-infty}/\(FA.n)"{-infty}) ) by MESFUNC1:def 4; then
      x in dom(FA.m) /\ dom(FA.n) by XBOOLE_0:def 4;
      hence thesis by C3a,P12,Q;
     end; then
N1:  integral'(M,FA.n|dom(FA.m - FA.n)) <= integral'(M,FA.m|dom(FA.m - FA.n))
       by P12,P14,Cor9a;
     dom(FA.m - FA.n) = dom(FA.m) /\ dom(FA.n) by P12,P14,N0,Diff1; then
     FA.m|dom(FA.m - FA.n) = FA.m & FA.n|dom(FA.m - FA.n) = FA.n 
       by GRFUNC_1:64,P12;
     hence KA.n <= KA.m by AS6a,P11,N1;
N0:  for x be set st x in dom(FB.m - FB.n) holds (FB.n).x <= (FB.m).x
     proof
      let x be set;
      assume x in dom(FB.m - FB.n); then
      x in (dom(FB.m) /\ dom(FB.n)) 
       \( ((FB.m)"{+infty}/\(FB.n)"{+infty})
        \/((FB.m)"{-infty}/\(FB.n)"{-infty}) ) by MESFUNC1:def 4; then
      x in dom(FB.m) /\ dom(FB.n) by XBOOLE_0:def 4;
      hence thesis by C3b,P12,Q;
     end; then
N1:  integral'(M,FB.n|dom(FB.m - FB.n)) <= integral'(M,FB.m|dom(FB.m - FB.n))
       by P12,P14,Cor9a;
     dom(FB.m - FB.n) = dom(FB.m) /\ dom(FB.n) by P12,P14,N0,Diff1; then
     FB.m|dom(FB.m - FB.n) = FB.m & FB.n|dom(FB.m - FB.n) = FB.n 
       by GRFUNC_1:64,P12;
     hence thesis by AS6b,P11,N1;
    end; then
P2: (for n,m be Nat st n<=m holds KA.n <= KA.m) &
    (for n,m be Nat st n<=m holds KB.n <= KB.m); then
    KA is convergent & KB is convergent by Cor12a; then
I2: integral+(M,f|A) =lim KA & integral+(M,f|B) =lim KB 
     by Defintegral1,E1,E2,E4,AS12,C1,C2,C3a,C4a,AS6a,C3b,C4b,AS6b;
C6: for n be Nat holds KAB.n=KA.n + KB.n
    proof
     let n be Nat;
     reconsider n as Element of NAT by ORDINAL1:def 13;
P1:  F0.n is_simple_func_in S & F0.n is nonnegative by AS02;
     FA.n=F0.n|A & FB.n=F0.n|B by AS5a,AS5b; then
P5:  integral'(M,F0.n|(A\/B)) = integral'(M,FA.n)+integral'(M,FB.n)
       by P1,AS,Th1;
P6:  KAB.n =integral'(M,FAB.n) by AS04
          .=integral'(M,F0.n|(A\/B)) by AS03;
     KA.n = integral'(M,FA.n) by AS6a;
     hence thesis by P5,P6,AS6b;
    end;
    for n be set holds
     (n in dom KA implies -infty < KA.n) & (n in dom KB implies -infty < KB.n)
    proof
     let n be set;
     hereby assume n in dom KA; then
      reconsider n1 = n as Element of NAT;
 P11: KA.n1 = integral'(M,FA.n1) & FA.n1 is_simple_func_in S by C1,AS6a;
      FA.n1 is nonnegative by C2;
      hence -infty < KA.n by P11,Cor9;
     end;
     assume n in dom KB; then
     reconsider n1 = n as Element of NAT;
 P11:KB.n1 = integral'(M,FB.n1) & FB.n1 is_simple_func_in S by C1,AS6b;
     FB.n1 is nonnegative by C2;
     hence -infty < KB.n by P11,Cor9;
    end; then
    KA is without-infty & KB is without-infty by WMI01;
    hence thesis by I1,I2,Cor16,P2,C6;
end;

theorem LM018e:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    A be Element of S st
 (ex E be Element of S st E = dom f & f is_measurable_on E ) &
 f is nonnegative & M.A = 0 holds
   integral+(M,f|A) = 0
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       A be Element of S;
   assume
AS: (ex E be Element of S st E = dom f & f is_measurable_on E ) &
    f is nonnegative & M.A = 0;
   consider E be Element of S such that
BS00:E = dom f & f is_measurable_on E by AS;
   set C = E/\A;
P0:C c= E by XBOOLE_1:17; then
B4:f is_measurable_on C by BS00,MESFUNC1:34;

P1:dom f /\ C = C by P0,BS00,XBOOLE_1:28; then
   dom(f|C) = C by FUNCT_1:68; then
P2:dom(f|C) = dom(f|A) by BS00,FUNCT_1:68;
   for x be set st x in dom(f|A) holds f|A.x = f|C.x
   proof
    let x be set;
    assume P3:x in dom(f|A); then
    (f|A).x = f.x by FUNCT_1:70;
    hence thesis by P2,P3,FUNCT_1:70;
   end; then
   f|A = f|C by P2,FUNCT_1:9; then
X1:f|A is_measurable_on C by P1,B4,Cor18;
   consider F0 be Functional_Sequence of X,ExtREAL,
            K0 be ExtREAL_sequence such that
W1: for n be Nat holds F0.n is_simple_func_in S & dom(F0.n) = dom f and
W2: for n be Nat holds F0.n is nonnegative and
W3: for n,m be Nat st n <=m holds
      for x be Element of X st x in dom f holds (F0.n).x <= (F0.m).x and
W4: for x be Element of X st x in dom f holds
      F0#x is convergent & lim(F0#x) = f.x and
    (for n be Nat holds K0.n=integral'(M,F0.n)) &
    K0 is convergent &
    integral+(M,f)=lim K0 by AS,Defintegral1;
   deffunc PFA(Nat) = (F0.$1)|A;
   consider FA be Functional_Sequence of X,ExtREAL such that
AS5:for n be Element of NAT holds FA.n=PFA(n) from SEQFUNC:sch 1;
   deffunc PK(Nat) = integral'(M,FA.$1);
   consider KA be ExtREAL_sequence such that
aAS6:for n be Element of NAT holds KA.n = PK(n) from FUNCT_2:sch 4;
AS6:now let n be Nat; n in NAT by ORDINAL1:def 13;
      hence KA.n = PK(n) by aAS6;
    end;
AS11:C = dom(f|A) by BS00,FUNCT_1:68;
AS12:f|A is nonnegative by AS,LL0;
C1:for n be Nat holds FA.n is_simple_func_in S & dom(FA.n) = dom(f|A)
   proof
    let n be Nat;
    reconsider n1=n as Element of NAT by ORDINAL1:def 13;
    F0.n1 is_simple_func_in S & FA.n1=F0.n1|A by W1,AS5;
    hence FA.n is_simple_func_in S by Cor5;
    dom(FA.n1)=dom(F0.n1|A) by AS5; then
    dom(FA.n)=dom(F0.n) /\ A by FUNCT_1:68;
    hence thesis by AS11,BS00,W1;
   end;
C2:for n be Nat holds FA.n is nonnegative
   proof
    let n be Nat;
    reconsider n as Element of NAT by ORDINAL1:def 13;
    F0.n is nonnegative by W2; then
    F0.n|A is nonnegative by LL0;
    hence thesis by AS5;
   end;
C3:for n,m be Nat st n <=m holds
     for x be Element of X st x in dom(f|A) holds (FA.n).x <= (FA.m).x
   proof
    let n,m be Nat;
    assume A:n<=m;
    reconsider n,m as Element of NAT by ORDINAL1:def 13;
    let x be Element of X;
    assume B:x in dom(f|A); then
    x in dom f /\ A by FUNCT_1:68; then
P0: x in dom f by XBOOLE_0:def 3;
    dom(F0.n|A) = dom(FA.n) & dom(F0.m|A) = dom(FA.m) by AS5; then
T2: dom(F0.n|A) = dom(f|A) & dom(F0.m|A) = dom(f|A) by C1;
    (FA.n).x =(F0.n|A).x & (FA.m).x =(F0.m|A).x by AS5; then
    (FA.n).x = (F0.n).x & (FA.m).x = (F0.m).x by B,T2,FUNCT_1:68;
    hence thesis by A,P0,W3;
   end;
C4:for x be Element of X st x in dom(f|A) holds
     FA#x is convergent & lim(FA#x) = f|A.x
   proof
    let x be Element of X;
    assume A0:x in dom(f|A); then
    x in dom f /\ A by FUNCT_1:68; then
    x in dom f by XBOOLE_0:def 3; then
Q2: F0#x is convergent & lim(F0#x)=f.x by W4;
    now let n be Element of NAT;
QQ2: dom(F0.n|A) = dom(FA.n) by AS5 .=dom(f|A) by C1;
     thus (FA#x).n = (FA.n).x by DefHx
                  .=(F0.n|A).x by AS5
                  .=(F0.n).x by A0,QQ2,FUNCT_1:68
                  .=(F0#x).n by DefHx;
    end; then
    FA#x = F0#x by FUNCT_2:113;
    hence thesis by Q2,A0,FUNCT_1:68;
   end;
   for n be Nat holds KA.n =R_EAL 0
   proof
    let n be Nat;
    reconsider n as Element of NAT by ORDINAL1:def 13;
    F0.n is_simple_func_in S & F0.n is nonnegative by W1,W2; then
    integral'(M,F0.n|A) = R_EAL 0 by Cor11a,AS; then
    integral'(M,FA.n) = R_EAL 0 by AS5;
    hence thesis by AS6;
   end; then
   KA is convergent & lim KA = R_EAL 0 by Cor14;
   hence thesis by AS11,X1,AS12,Defintegral1,C1,C2,C3,C4,AS6;
end;

theorem LM018f:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    A,B be Element of S st
  (ex E be Element of S st E = dom f & f is_measurable_on E ) &
  f is nonnegative & A c= B holds
     integral+(M,f|A) <= integral+(M,f|B)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       A,B be Element of S;
   assume
A1:(ex E be Element of S st E = dom f & f is_measurable_on E ) &
   f is nonnegative & A c= B;
   set A' = A /\ B;
   set B' = B \ A;
P1:A' misses B' by XBOOLE_1:89;
P4:0<= integral+(M,f|(A'\/B')) & 0<= integral+(M,f|A') &
   0<= integral+(M,f|B') &
   integral+(M,f|(A'\/B')) =integral+(M,f|A')+integral+(M,f|B')
     by LM018d,LM018d1,P1,A1;
   A' = A & A'\/B' = B by A1,XBOOLE_1:28,51;
   hence thesis by P4,SUPINF_2:20;
end;

theorem LM018g: 
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    E,A be Element of S st
 f is nonnegative & E = dom f & f is_measurable_on E & M.A =0 holds
  integral+(M,f|(E\A)) = integral+(M,f)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       E,A be Element of S such that
A1:f is nonnegative & E = dom f & f is_measurable_on E & M.A =0;
   set B = E\A;
   A misses B by XBOOLE_1:79; then
P4:integral+(M,f|(A\/B))
      =integral+(M,f|A)+integral+(M,f|B) by LM018d,A1;
   f|(A\/B) =f
   proof
    A \/ B = A \/ E by XBOOLE_1:39; then
    E c= A \/ B by XBOOLE_1:7; then
P5: dom f = dom f /\ (A\/B) by A1,XBOOLE_1:28 .= dom(f|(A\/B)) by FUNCT_1:68;
    for x be set st x in dom(f|(A\/B)) holds (f|(A\/B)).x = f.x by FUNCT_1:70;
    hence thesis by P5,FUNCT_1:9;
   end; then
   integral+(M,f) = 0.+ integral+(M,f|B) by P4,A1,LM018e;
   hence thesis by SUPINF_2:18;
end;

theorem LM018h: 
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
 (ex E be Element of S st E = dom f & E= dom g &
   f is_measurable_on E & g is_measurable_on E) &
 f is nonnegative & g is nonnegative &
 (for x be Element of X st x in dom g holds g.x <= f.x)
holds
  integral+(M,g) <= integral+(M,f)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL such that
A1:(ex A be Element of S st A = dom f & A= dom g
      & f is_measurable_on A & g is_measurable_on A) &
   f is nonnegative & g is nonnegative &
   (for x be Element of X st x in dom g holds g.x <= f.x);
   consider A be Element of S such that
A2: A = dom f & A= dom g & f is_measurable_on A & g is_measurable_on A by A1;
   consider F be Functional_Sequence of X,ExtREAL,
            K be ExtREAL_sequence such that
A3: (for n be Nat holds F.n is_simple_func_in S & dom(F.n) = dom f) &
    (for n be Nat holds F.n is nonnegative) &
    (for n,m be Nat st n <=m holds
      for x be Element of X st x in dom f holds (F.n).x <= (F.m).x ) &
    (for x be Element of X st x in dom f holds
      (F#x) is convergent & lim(F#x) = f.x) &
    (for n be Nat holds K.n=integral'(M,F.n)) &
    K is convergent &
    integral+(M,f)=lim(K) by A1,Defintegral1;
   consider G be Functional_Sequence of X,ExtREAL,
            L be ExtREAL_sequence such that
A4: (for n be Nat holds G.n is_simple_func_in S & dom(G.n) = dom g) &
    (for n be Nat holds G.n is nonnegative) &
    (for n,m be Nat st n <=m holds
      for x be Element of X st x in dom g holds (G.n).x <= (G.m).x ) &
    (for x be Element of X st  x in dom g holds
      G#x is convergent & lim(G#x) = g.x) &
    (for n be Nat holds L.n=integral'(M,G.n)) &
    L is convergent & integral+(M,g)=lim L by A1,Defintegral1;
A5:for x be Element of X st x in A holds lim(G#x)=sup rng(G#x)
   proof
    let x be Element of X;
    assume AS:x in A; 
    now let n,m be Nat;
     assume PS1: n<=m;
     (G#x).n=(G.n).x & (G#x).m=(G.m).x by DefHx;
     hence (G#x).n <= (G#x).m by AS,A2,A4,PS1;
    end;
    hence lim(G#x)=sup rng(G#x) by Cor12a;
   end;
A6:for n0 be Nat holds K is convergent & sup rng K = lim K & L.n0 <= lim K
   proof
    let n0 be Nat;
    set gg = G.n0;
B1: dom gg = A & gg is_simple_func_in S & gg is nonnegative by A4,A2;
    for x be Element of X st x in dom gg holds
     F#x is convergent & gg.x <= lim(F#x)
    proof
     let x be Element of X such that 
 B3: x in dom gg;
     gg.x =(G#x).n0 & (G#x).n0 <= sup rng (G#x) by DefHx,Cor12c; then
     gg.x <= lim(G#x) & g.x <= f.x by A1,A2,A5,B3,B1; then
     gg.x <= g.x & g.x <= lim(F#x) by A3,A4,B3,B1,A2;
     hence thesis by XXREAL_0:2,A3,A2,B3,B1;
    end; then
    consider GG be ExtREAL_sequence such that
B5:  (for n be Nat holds GG.n = integral'(M,F.n)) &
     GG is convergent & sup rng GG =lim GG &
     integral'(M,gg) <= lim GG by LM016,A3,A2,B1;
    now let n be Element of NAT;
     GG.n = integral'(M,F.n) by B5;
     hence GG.n = K.n by A3;
    end; then
    GG=K by FUNCT_2:113;
    hence K is convergent & sup rng K =lim K & L.n0 <= lim K by A4,B5;
   end;
   for n0 be Nat holds L is convergent & sup rng L=lim L
   proof
    let n0 be Nat;
    set ff = G.n0;
C1: dom ff = A & ff is_simple_func_in S & ff is nonnegative by A4,A2;
    for x be Element of X st x in dom ff holds
     G#x is convergent & ff.x <= lim(G#x)
    proof
     let x be Element of X such that
 C2: x in dom ff;
     ff.x =(G#x).n0 & (G#x).n0 <= sup rng (G#x) by DefHx,Cor12c;
     hence thesis by A4,A2,A5,C1,C2;
    end; then
    consider FF be ExtREAL_sequence such that
P1:  (for n be Nat holds FF.n = integral'(M,G.n)) &
     FF is convergent & sup rng FF = lim FF &
     integral'(M,ff) <= lim FF by LM016,A4,A2,C1;
    now let n be Element of NAT;
     thus FF.n = integral'(M,G.n) by P1 .=L.n by A4;
    end; then
    FF=L by FUNCT_2:113;
    hence L is convergent & sup rng L=lim L by P1;
   end;
   hence thesis by A3,A4,A6,Cor12d;
end;

theorem LM018i:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    c be Real st
  0 <= c &
  (ex A be Element of S st A = dom f & f is_measurable_on A) &
  f is nonnegative holds
   integral+(M,c(#)f) = R_EAL c * integral+(M,f)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       c be Real such that
A1:0 <= c & (ex A be Element of S st A = dom f & f is_measurable_on A)&
   f is nonnegative;
   consider F1 be Functional_Sequence of X,ExtREAL,
            K1 be ExtREAL_sequence such that
A2: (for n be Nat holds F1.n is_simple_func_in S & dom(F1.n) = dom f) &
    (for n be Nat holds F1.n is nonnegative) &
    (for n,m be Nat st n <=m holds
         for x be Element of X st x in dom f holds (F1.n).x <= (F1.m).x ) &
    (for x be Element of X st x in dom f holds
         F1#x is convergent & lim(F1#x) = f.x) &
    (for n be Nat holds K1.n=integral'(M,F1.n)) &
    K1 is convergent & integral+(M,f)=lim K1 by A1,Defintegral1;
   now let n1 be set;
    assume n1 in dom K1; then
    reconsider n = n1 as Element of NAT;
    K1.n = integral'(M,F1.n) & F1.n is_simple_func_in S &
    F1.n is nonnegative by A2;
    hence -infty < K1.n1 by Cor9;
   end; then
A3:K1 is without-infty by WMI01;
A4:for n,m be Nat st n<=m holds K1.n <= K1.m
   proof
    let n,m be Nat;
    assume Q:n<=m;
P11:K1.n = integral'(M,F1.n) & K1.m = integral'(M,F1.m) &
    F1.n is_simple_func_in S & F1.m is_simple_func_in S &
    dom(F1.n) = dom f & dom(F1.m) = dom f &
    F1.n is nonnegative & F1.m is nonnegative by A2;
 N0:for x be set st x in dom(F1.m - F1.n) holds (F1.n).x <= (F1.m).x
    proof
     let x be set;
     assume x in dom(F1.m - F1.n); then
     x in (dom(F1.m) /\ dom(F1.n)
       \ (((F1.m)"{+infty}/\(F1.n)"{+infty})
         \/((F1.m)"{-infty}/\(F1.n)"{-infty}))) by MESFUNC1:def 4; then
     x in dom(F1.m) /\ dom(F1.n) by XBOOLE_0:def 4;
     hence (F1.n).x <= (F1.m).x by A2,P11,Q;
    end; then
    dom(F1.m - F1.n) = dom(F1.m)/\dom(F1.n) by P11,Diff1; then
    F1.n|dom(F1.m - F1.n) = F1.n &
    F1.m|dom(F1.m - F1.n) = F1.m by GRFUNC_1:64,P11; 
    hence K1.n <= K1.m by P11,N0,Cor9a;
   end;
   consider A be Element of S such that
A5:A = dom f & f is_measurable_on A by A1;
A6:A = dom(c(#)f) & c(#)f is_measurable_on A by A5,MESFUNC1:41,def 6;
A7:c(#)f is nonnegative by A1,LL6;
   deffunc PF(Nat) = c(#)(F1.$1);
   consider F be Functional_Sequence of X,ExtREAL such that
A8: for n be Element of NAT holds F.n=PF(n) from SEQFUNC:sch 1;
   deffunc PK(Nat) = integral'(M,F.$1);
   consider K be ExtREAL_sequence such that
aA9: for n be Element of NAT holds K.n = PK(n) from FUNCT_2:sch 4;
A9: now let n be Nat; n in NAT by ORDINAL1:def 13;
      hence K.n = PK(n) by aA9;
    end;
C1:for n be Nat holds F.n is_simple_func_in S & dom(F.n) = dom(c(#)f)
   proof
    let n be Nat;
    reconsider n1=n as Element of NAT by ORDINAL1:def 13;
 P1:F1.n is_simple_func_in S & F1.n is nonnegative by A2;
 P3:F.n1=c(#)(F1.n1) by A8;
    hence F.n is_simple_func_in S by P1,LL5;
    thus dom(F.n) = dom(F1.n) by P3,MESFUNC1:def 6
                 .=A by A5,A2
                 .=dom(c(#)f) by A5,MESFUNC1:def 6;
   end;
C2:for n be Nat holds F.n is nonnegative
   proof
    let n be Nat;
    reconsider n as Element of NAT by ORDINAL1:def 13;
    F1.n is nonnegative by A2; then
    c(#)(F1.n) is nonnegative by A1,LL6;
    hence thesis by A8;
   end;
C3:for n,m be Nat st n <=m holds
    for x be Element of X st x in dom (c(#)f) holds (F.n).x <= (F.m).x
   proof
    let n,m be Nat;
    assume A:n<=m;
    let x be Element of X;
    assume B:x in dom(c(#)f);
    reconsider n,m as Element of NAT by ORDINAL1:def 13;
    dom(c(#)(F1.n)) = dom(F.n) & dom(c(#)(F1.m)) = dom(F.m) by A8; then
T2: dom(c(#)(F1.n)) = dom(c(#)f) & dom(c(#)(F1.m)) = dom(c(#)f) by C1;
    (F.n).x =(c(#)(F1.n)).x & (F.m).x =(c(#)(F1.m)).x by A8; then
T6: (F.n).x =R_EAL(c)*(F1.n).x & (F.m).x =R_EAL(c)*(F1.m).x 
      by B,T2,MESFUNC1:def 6;
    (F1.n).x <= (F1.m).x by A,A2,B,A6,A5;
    hence thesis by T6,A1,EXTREAL1:42;
   end;
C4:for x be Element of X st x in dom(c(#)f) holds
      F#x is convergent & lim(F#x) = (c(#)f).x
   proof
    let x be Element of X;
    assume A0: x in dom(c(#)f);
    Q1:now let n be Nat;
    reconsider n1=n as Element of NAT by ORDINAL1:def 13;
 QQ2:dom(c(#)(F1.n1)) = dom (F.n1) by A8 .=dom(c(#)f) by C1;
     thus (F#x).n = (F.n).x by DefHx
                 .= (c(#)(F1.n1)).x by A8
                 .= R_EAL c * (F1.n).x by A0,QQ2,MESFUNC1:def 6
                 .= R_EAL c * (F1#x).n by DefHx;
    end;
    now let n1 be set;
     assume n1 in dom(F1#x); then
     reconsider n=n1 as Element of NAT;
     F1.n is nonnegative by A2; then
 Q3: F1.n is without-infty by WMI02;
     (F1#x).n = (F1.n).x by DefHx;
     hence -infty < (F1#x).n1 by Q3,Def0003;
    end; then
 Q2:F1#x is without-infty by WMI01;
 S1:now let n,m be Nat;
     assume AA: n <=m;
P1a: (F1#x).n = (F1.n).x by DefHx;
     (F1#x).m = (F1.m).x by DefHx;
     hence (F1#x).n<= (F1#x).m by A0,AA,A2,P1a,A5,A6;
    end;
    R_EAL c *lim(F1#x) = R_EAL c * f.x by A0,A5,A6,A2
          .=(c(#)f).x by A0,MESFUNC1:def 6;
    hence thesis by Cor17b,A1,Q1,Q2,S1;
   end;
   for n be Nat holds K.n=R_EAL(c)*(K1.n)
   proof
    let n be Nat;
    reconsider n1=n as Element of NAT by ORDINAL1:def 13;
 P1:F1.n is_simple_func_in S & F1.n is nonnegative by A2;
 P3:F.n1=c(#)(F1.n1) by A8;
    thus K.n=integral'(M,F.n1) by A9
           .= R_EAL c * integral'(M,F1.n) by P1,P3,A1,Th1b
           .= R_EAL c * K1.n by A2;
   end; then
   K is convergent & lim K = R_EAL c * lim K1 by Cor17b,A1,A3,A4;
   hence thesis by Defintegral1,A2,A6,A7,A9,C1,C2,C3,C4;
end;

theorem LM018j:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL st
 (ex A be Element of S st A = dom f & f is_measurable_on A) &
 (for x be Element of X st x in dom f holds 0= f.x) holds
   integral+(M,f) = 0
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL such that
A1:(ex A be Element of S st A = dom f & f is_measurable_on A )&
   (for x be Element of X st x in dom f holds 0 = f.x);
   (for x be set st x in dom f holds 0 <= f.x) by A1; then
A2:f is nonnegative by SUPINF_2:71;
P1:dom(0(#)f) =dom f by MESFUNC1:def 6;
   now let x be set;
    assume A:x in dom(0(#)f);
    hence (0(#)f).x = R_EAL 0 * f.x by MESFUNC1:def 6
                   .= 0 by EXTREAL1:22
                   .= f.x by A,P1,A1;
   end;
   hence integral+(M,f) = integral+(M,0(#)f) by P1,FUNCT_1:9
                       .= R_EAL 0 * integral+(M,f) by LM018i,A1,A2
                       .= 0 by EXTREAL1:22;
end;

begin :: Integral of measurable function

definition
  let X be non empty set;
  let S be SigmaField of X;
  let M be sigma_Measure of S;
  let f be PartFunc of X,ExtREAL;
  func Integral(M,f) -> Element of ExtREAL equals
  integral+(M,max+f)-integral+(M,max-f);
  coherence;
end;

theorem LM022b:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL st
 (ex A be Element of S st A = dom f & f is_measurable_on A) &
 f is nonnegative holds
    Integral(M,f) =integral+(M,f)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL;
   assume
AS0:(ex A be Element of S st A = dom f & f is_measurable_on A) &
    f is nonnegative; then
   consider A be Element of S such that
AS1:A = dom f & f is_measurable_on A;
P1:dom f = dom max+ f by MESFUNC2:def 2;
P2:now let x be set;
    assume A:x in dom f;
  B:0 <= f.x by AS0,SUPINF_2:70;
    thus max+f.x = max(f.x,0) by A,P1,MESFUNC2:def 2
                .=f.x by XXREAL_0:def 9,B;
   end; then
L1:f = max+ f by FUNCT_1:9,P1;
P3:dom f = dom max-f by MESFUNC2:def 3;
P4:now let x be Element of X;
    assume B:x in dom max- f; then
    max+f.x=f.x by P2,P3;
    hence max-f.x=0 by B,P3,MESFUNC2:21;
   end;
B2:dom f=dom (max- f) by MESFUNC2:def 3;
   max- f is_measurable_on A by AS1,MESFUNC2:28;
   hence Integral(M,f) =integral+(M,f) - R_EAL 0 by L1,LM018j,P4,B2,AS1
                         .=integral+(M,f) by MEASURE6:21;
end;

theorem
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL st
 f is_simple_func_in S & f is nonnegative holds
  Integral(M,f) = integral+(M,f) & Integral(M,f) = integral'(M,f)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL;
   assume
AS0:f is_simple_func_in S & f is nonnegative;
   reconsider A=dom f as Element of S by AS0,Cor10;
   f is_measurable_on A by AS0,MESFUNC2:37;
   hence Integral(M,f) =integral+(M,f) by AS0,LM022b;
   hence thesis by LM018a,AS0;
end;

theorem
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL st
 (ex A be Element of S st A = dom f & f is_measurable_on A) &
 f is nonnegative holds
   0 <= Integral(M,f)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL;
   assume
A1:(ex A be Element of S st A = dom f & f is_measurable_on A) &
   f is nonnegative; then
   0 <= integral+(M,f) by LM018c;
   hence 0 <= Integral(M,f) by A1,LM022b;
end;

theorem 
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    A,B be Element of S st
 (ex E be Element of S st E = dom f & f is_measurable_on E ) &
 f is nonnegative & A misses B
holds
   Integral(M,f|(A\/B)) = Integral(M,f|A)+Integral(M,f|B)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       A,B be Element of S;
   assume that
A1: (ex E be Element of S st E = dom f & f is_measurable_on E ) and
A2: f is nonnegative & A misses B;
   consider E be Element of S such that
B0: E = dom f & f is_measurable_on E by A1;
B1:ex C be Element of S st C = dom(f|(A\/B)) & f|(A\/B) is_measurable_on C
   proof
    reconsider C = E/\(A\/B) as Element of S;
    take C;
    thus dom(f|(A\/B)) = C by B0,FUNCT_1:68;
    C c= dom f by B0,XBOOLE_1:17; then
B15:f is_measurable_on C & C = dom f /\ C by B0,XBOOLE_1:28,MESFUNC1:34; then
B14:f|C is_measurable_on C by Cor18;
B16:dom(f|(A\/B)) = C by B0,FUNCT_1:68 .= dom(f|C) by B15,FUNCT_1:68;
    for x be set st x in dom(f|(A\/B)) holds (f|(A\/B)).x = (f|C).x
    proof
     let x be set;
     assume 
B17:  x in dom(f|(A\/B)); then
     (f|(A\/B)).x = f.x by FUNCT_1:70;
     hence thesis by B16,B17,FUNCT_1:70;
    end;
    hence f|(A\/B) is_measurable_on C by B14,B16,FUNCT_1:9;
   end;
P1:integral+(M,f|(A\/B)) = integral+(M,f|A)+integral+(M,f|B)
     by LM018d,A1,A2;
   f|(A\/B) is nonnegative by A2,LL0; then
P2:Integral(M,f|(A\/B))=integral+(M,f|(A\/B)) by B1,LM022b;
B3:ex C be Element of S st C = dom(f|A) & f|A is_measurable_on C
   proof
    take C=E/\A;
    thus dom(f|A) = C by B0,FUNCT_1:68;
B31:C c= dom f by B0,XBOOLE_1:17; then
B32:f is_measurable_on C by B0,MESFUNC1:34;
B35:C = dom f /\ C by B31,XBOOLE_1:28;
B36:dom(f|A) = C by B0,FUNCT_1:68 .= dom(f|C) by B35,FUNCT_1:68;
    for x be set st x in dom(f|A) holds (f|A).x = (f|C).x
    proof
     let x be set;
     assume B37:x in dom(f|A); then
     (f|A).x = f.x by FUNCT_1:70;
     hence thesis by B36,B37,FUNCT_1:70;
    end; then
    f|C = f|A by B36,FUNCT_1:9;
    hence f|A is_measurable_on C by B35,B32,Cor18;
   end;
   f|A is nonnegative by A2,LL0; then
P3:Integral(M,f|A)=integral+(M,f|A) by LM022b,B3;
B4:ex C be Element of S st C = dom(f|B) & f|B is_measurable_on C
   proof
    take C=E/\B;
    thus dom(f|B) = C by B0,FUNCT_1:68;
B41:C c= dom f by B0,XBOOLE_1:17; then
B42:f is_measurable_on C by B0,MESFUNC1:34;
B45:C = dom f /\ C by B41,XBOOLE_1:28;
B46:dom(f|B) = C by B0,FUNCT_1:68 .= dom(f|C) by B45,FUNCT_1:68;
    for x be set st x in dom(f|B) holds (f|B).x = (f|C).x
    proof
     let x be set;
     assume B47:x in dom(f|B); then
     (f|B).x = f.x by FUNCT_1:70;
     hence thesis by B46,B47,FUNCT_1:70;
    end; then
    f|C = f|B by B46,FUNCT_1:9;
    hence f|B is_measurable_on C by B45,B42,Cor18;
   end;
   f|B is nonnegative by A2,LL0;
   hence thesis by P1,P2,P3,B4,LM022b;
end;

theorem 
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    A be Element of S st
(ex E be Element of S st E = dom f & f is_measurable_on E ) &
f is nonnegative holds
  0<= Integral(M,f|A)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       A be Element of S;
   assume that
A1: (ex E be Element of S st E = dom f & f is_measurable_on E) and
A2: f is nonnegative;
   consider E be Element of S such that
B0: E = dom f & f is_measurable_on E by A1;
B1:ex C be Element of S st C = dom(f|A) & f|A is_measurable_on C
   proof
    take C = E /\ A;
    thus dom(f|A) = C by B0,FUNCT_1:68;
    C c= E by XBOOLE_1:17; then
B10:f is_measurable_on C & C = dom f /\ C by B0,MESFUNC1:34,XBOOLE_1:28; then
B11:f|C is_measurable_on C by Cor18;
B12:dom(f|A) = C by B0,FUNCT_1:68 .= dom(f|C) by B10,FUNCT_1:68;
    for x be set st x in dom(f|A) holds (f|A).x = (f|C).x
    proof
     let x be set;
     assume B13:x in dom(f|A); then
     (f|A).x = f.x by FUNCT_1:68;
     hence thesis by B12,B13,FUNCT_1:68;
    end;
    hence f|A is_measurable_on C by B11,B12,FUNCT_1:9;
   end;
B2:f|A is nonnegative by A2,LL0; then
   0<= integral+(M,f|A) by LM018c,B1;
   hence thesis by LM022b,B2,B1;
end;

theorem 
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    A,B be Element of S st
 (ex E be Element of S st E = dom f & f is_measurable_on E ) &
 f is nonnegative & A c= B
holds
  Integral(M,f|A) <= Integral(M,f|B)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       A,B be Element of S;
   assume that
A1: (ex E be Element of S st E = dom f & f is_measurable_on E) and
A2: f is nonnegative and
A3: A c= B;
   consider E be Element of S such that
B0:E = dom f & f is_measurable_on E by A1;
B1:ex C be Element of S st C = dom(f|A) & f|A is_measurable_on C
   proof
    take C = E /\ A;
    thus dom(f|A) = C by B0,FUNCT_1:68;
    C c= E by XBOOLE_1:17; then
B10:f is_measurable_on C & C = dom f /\ C by B0,MESFUNC1:34,XBOOLE_1:28; then
B11:f|C is_measurable_on C by Cor18;
B12:dom(f|A) = C by B0,FUNCT_1:68 .= dom(f|C) by B10,FUNCT_1:68;
    for x be set st x in dom(f|A) holds (f|A).x = (f|C).x
    proof
     let x be set;
     assume B13:x in dom(f|A); then
     (f|A).x = f.x by FUNCT_1:68;
     hence thesis by B12,B13,FUNCT_1:68;
    end;
    hence f|A is_measurable_on C by B11,B12,FUNCT_1:9;
   end;
B2:f|A is nonnegative by A2,LL0;
B3:ex C be Element of S st C = dom(f|B) & f|B is_measurable_on C
   proof
    take C = E /\ B;
    thus dom(f|B) = C by B0,FUNCT_1:68;
    C c= E by XBOOLE_1:17; then
B10:f is_measurable_on C & C = dom f /\ C by B0,MESFUNC1:34,XBOOLE_1:28; then
B11:f|C is_measurable_on C by Cor18;
B12:dom(f|B) = C by B0,FUNCT_1:68 .= dom(f|C) by B10,FUNCT_1:68;
    for x be set st x in dom(f|B) holds (f|B).x = (f|C).x
    proof
     let x be set;
     assume B13:x in dom(f|B); then
     (f|B).x = f.x by FUNCT_1:68;
     hence thesis by B12,B13,FUNCT_1:68;
    end;
    hence f|B is_measurable_on C by B11,B12,FUNCT_1:9;
   end;
B4:f|B is nonnegative by A2,LL0;   
   integral+(M,f|A) <= integral+(M,f|B) by LM018f,A1,A2,A3; then
   Integral(M,f|A) <= integral+(M,f|B) by LM022b,B2,B1;
   hence thesis by LM022b,B3,B4;
end;

theorem 
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    A be Element of S st
 (ex E be Element of S st E = dom f & f is_measurable_on E) & M.A = 0
holds
    Integral(M,f|A)=0
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       A be Element of S such that
AS1:(ex E be Element of S st E = dom f & f is_measurable_on E) &
    M.A = 0;
   consider E be Element of S such that
AS2:E = dom f & f is_measurable_on E by AS1;
   reconsider C = E /\ A as Element of S;
A2:dom f=dom (max+ f) & dom f=dom (max- f) by MESFUNC2:def 2,def 3;
A3:max+f is nonnegative & max-f is nonnegative by MAX;
   max+ f is_measurable_on E by AS2,MESFUNC2:27; then
PP:integral+(M,(max+ f)|A)=0 by LM018e,A3,AS1,A2,AS2;
QQ:max- f is_measurable_on E by AS2,MESFUNC2:28;
   Integral(M,f|A)
    = integral+(M,(max+ f)|A) - integral+(M,max-(f|A)) by LM022g01
   .= integral+(M,(max+ f)|A) - integral+(M,(max- f)|A) by LM022g01
   .= 0.- 0. by PP,QQ,LM018e,A3,AS1,A2,AS2;
   hence thesis by MEASURE6:21;
end;

theorem LM022i:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    E,A be Element of S st
 E = dom f & f is_measurable_on E & M.A =0 holds
   Integral(M,f|(E\A)) = Integral(M,f)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       E,A be Element of S such that
A1:E = dom f & f is_measurable_on E & M.A =0;
   set B = E\A;
A2:dom f=dom(max+f) & dom f=dom(max-f) by MESFUNC2:def 2,def 3;
A3:max+f is nonnegative & max-f is nonnegative by MAX;
A4:max+f is_measurable_on E & max-f is_measurable_on E by A1,MESFUNC2:27,28;
   Integral(M,f|B)
    = integral+(M,(max+f)|B) -integral+(M,max-(f|B)) by LM022g01
   .= integral+(M,(max+f)|B) -integral+(M,(max-f)|B) by LM022g01
   .=integral+(M,max+f) -integral+(M,(max-f)|B) by LM018g,A1,A2,A3,A4;
   hence thesis by LM018g,A1,A2,A3,A4;
end;

definition 
let X be non empty set;
let S be SigmaField of X;
let M be sigma_Measure of S;
let f be PartFunc of X,ExtREAL;
pred f is_integrable_on M means :Defintegrable:
  (ex A be Element of S st A = dom f & f is_measurable_on A ) & 
  integral+(M,max+ f) < +infty & 
  integral+(M,max- f) < +infty;
end;

theorem LM022a:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL st 
 f is_integrable_on M holds
   0 <= integral+(M,max+f) & 0 <= integral+(M,max-f) &
   -infty < Integral(M,f) & Integral(M,f) < +infty 
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL such that
AS:f is_integrable_on M;
   consider A be Element of S such that
A1:A = dom f & f is_measurable_on A by Defintegrable,AS;
A2:dom f=dom(max+f) & dom f=dom(max-f) by MESFUNC2:def 2,def 3;
A3:max+f is nonnegative & max-f is nonnegative by MAX;
A4:max+f is_measurable_on A & max-f is_measurable_on A 
    by A1,MESFUNC2:27,28; then
A6:-infty <> integral+(M,max+f) & -infty <> integral+(M,max-f) 
     by LM018c,A1,A2,A3;
A7:integral+(M,max+f) <> +infty & integral+(M,max-f) <> +infty 
     by AS,Defintegrable; then
   reconsider maxf1=integral+(M,max+f) as Real by A6,EXTREAL1:1;
   reconsider maxf2=integral+(M,max-f) as Real by A6,A7,EXTREAL1:1;
   integral+(M,max+f)-integral+(M,max-f) = maxf1-maxf2 by SUPINF_2:5;
   hence thesis by A1,A2,A3,A4,LM018c,SUPINF_1:17,18;
end;

theorem LM022g:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    A be Element of S st
f is_integrable_on M holds
  integral+(M,max+(f|A)) <= integral+(M,max+ f) &
  integral+(M,max-(f|A)) <= integral+(M,max- f) &
  f|A is_integrable_on M
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       A be Element of S;
   assume 
AS: f is_integrable_on M; then
   consider E be Element of S such that
A2: E = dom f & f is_measurable_on E by Defintegrable;
A4:max+f is_measurable_on E & max-f is_measurable_on E by A2,MESFUNC2:27,28;
A5:dom f = dom(max+f) & dom f=dom(max-f) by MESFUNC2:def 2,def 3;
A3:max+f is nonnegative & max-f is nonnegative by MAX;
C0:E/\A c= E by XBOOLE_1:17;
   max+f|(E/\A) = (max+f|E)|A & max-f|(E/\A) = (max-f|E)|A by RELAT_1:100; then
   max+f|(E/\A) = (max+f)|A & max+f|E = max+f &
   max-f|(E/\A) = (max-f)|A & max-f|E = max-f by GRFUNC_1:64,A5,A2; then
C2:integral+(M,max+f|A) <= integral+(M,max+f) &
   integral+(M,max-f|A) <= integral+(M,max-f) by C0,LM018f,A2,A3,A4,A5;
P1:E /\ A = dom(f|A) by A2,FUNCT_1:68;
   dom f /\ (E/\A) = E/\A & f is_measurable_on E/\A
     by C0,A2,MESFUNC1:34,XBOOLE_1:28; then
   f|(E/\A) is_measurable_on E/\A by Cor18; then
   f|E|A is_measurable_on E/\A by RELAT_1:100; then
P2:f|A is_measurable_on E/\A by GRFUNC_1:64,A2;
E1:integral+(M,max+f) < +infty & integral+(M,max-f) < +infty
     by Defintegrable,AS;
   integral+(M,max+(f|A)) <= integral+(M,max+f) & 
   integral+(M,max-(f|A)) <= integral+(M,max-f) by C2,LM022g01; then
   integral+(M,max+(f|A)) < +infty &
   integral+(M,max-(f|A)) < +infty by E1,XXREAL_0:2;
   hence thesis by LM022g01,C2,P1,P2,Defintegrable;
end;

theorem LM022j:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    A,B be Element of S st
 f is_integrable_on M & A misses B holds
  Integral(M,f|(A\/B)) = Integral(M,f|A) + Integral(M,f|B)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       A,B be Element of S;
   assume
AS: f is_integrable_on M & A misses B; then
   consider E be Element of S such that
A1: E = dom f & f is_measurable_on E by Defintegrable;
   set AB = E/\(A\/B);
Q2:integral+(M,max+(f|A)) <= integral+(M,max+f) &
   integral+(M,max-(f|A)) <= integral+(M,max-f) &
   f|A is_integrable_on M &
   integral+(M,max+(f|B)) <= integral+(M,max+f) &
   integral+(M,max-(f|B)) <= integral+(M,max-f) &
   f|B is_integrable_on M by AS,LM022g; then
R2:(ex C be Element of S st C = dom(f|A) & f|A is_measurable_on C) &
   integral+(M,max+(f|A)) < +infty & integral+(M,max-(f|A)) < +infty &
   (ex C be Element of S st C = dom(f|B) & f|B is_measurable_on C) &
   integral+(M,max+(f|B)) < +infty & integral+(M,max-(f|B)) < +infty 
     by Defintegrable;
T2:0 <= integral+(M,max+(f|A)) & 0 <= integral+(M,max-(f|A)) &
   0 <= integral+(M,max+(f|B)) & 0 <= integral+(M,max-(f|B)) by Q2,LM022a;
Q5:Integral(M,f|(A\/B))
    = Integral(M,(f|E)|(A\/B)) by GRFUNC_1:64,A1
   .= Integral(M,f|AB) by RELAT_1:100
   .= integral+(M,max+f|AB) - integral+(M,max-(f|AB)) by LM022g01
   .= integral+(M,max+f|AB) - integral+(M,max-f|AB) by LM022g01;
A2:dom f= dom(max+f) & dom f = dom(max-f) by MESFUNC2:def 2,def 3;
A3:max+f is nonnegative & max-f is nonnegative by MAX;
   max+f is_measurable_on E & max-f is_measurable_on E 
     by A1,MESFUNC2:27,28; then
Q6:integral+(M,max+f|(A\/B)) = integral+(M,max+f|A) + integral+(M,max+f|B) &
   integral+(M,max-f|(A\/B)) = integral+(M,max-f|A) + integral+(M,max-f|B)
     by LM018d,A1,A2,A3,AS;
   max+f|(A\/B) = max+f|E|(A\/B) & max-f|(A\/B) = max-f|E|(A\/B)
     by A2,A1,GRFUNC_1:64; then
R6:max+f|(A\/B) = max+f|AB & max-f|(A\/B) = max-f|AB by RELAT_1:100;
Q9:max+(f|A)=max+f|A & max-(f|A)=max-f|A &
   max+(f|B)=max+f|B & max-(f|B)=max-f|B by LM022g01;
   reconsider f1 = integral+(M,max+(f|A)) as Real by R2,T2,EXTREAL1:1;
   reconsider f2 = integral+(M,max-(f|A)) as Real by R2,T2,EXTREAL1:1;
   reconsider g1 = integral+(M,max+(f|B)) as Real by R2,T2,EXTREAL1:1;
   reconsider g2 = integral+(M,max-(f|B)) as Real by R2,T2,EXTREAL1:1;
   integral+(M,max+(f|A)) + integral+(M,max+(f|B)) = f1+g1 &
   integral+(M,max-(f|A)) + integral+(M,max-(f|B)) = f2+g2 by SUPINF_2:1; then
   Integral(M,f|(A\/B)) = f1+g1-(f2+g2) by Q5,Q6,R6,Q9,SUPINF_2:5; then
Q1:Integral(M,f|(A\/B)) = (f1-f2)+(g1-g2);
   integral+(M,max+(f|A))-integral+(M,max-(f|A)) = f1-f2 &
   integral+(M,max+(f|B))-integral+(M,max-(f|B)) = g1-g2 by SUPINF_2:5;
   hence thesis by Q1,SUPINF_2:1;
end;

theorem
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    A,B be Element of S st
 f is_integrable_on M & B = (dom f)\A
holds
   f|A is_integrable_on M & Integral(M,f) = Integral(M,f|A)+Integral(M,f|B)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       A,B be Element of S such that
AS:f is_integrable_on M & B = dom f \ A;
C1:A misses B by AS,XBOOLE_1:79;
   A \/ B = A\/dom f by AS,XBOOLE_1:39; then
   dom f c= A \/ B by XBOOLE_1:7; then
C4:dom f /\ (A \/ B) = dom f by XBOOLE_1:28;
   f|(A\/B) = f|(dom f)|(A\/B) by GRFUNC_1:64
    .= f|(dom f /\(A\/B)) by RELAT_1:100 .= f by C4,GRFUNC_1:64;
   hence thesis by C1,AS,LM022j,LM022g;
end;

theorem LM022l:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL st 
 (ex A be Element of S st A = dom f & f is_measurable_on A )
   holds
    f is_integrable_on M iff |.f.| is_integrable_on M
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL;
   assume
A1:ex A be Element of S st A = dom f & f is_measurable_on A; then
   consider A be Element of S such that
A2: A = dom f & f is_measurable_on A;
A3:dom|.f.| = dom max+|.f.| & dom |.f.| = dom max-|.f.| &
   dom f= dom max+f & dom f = dom max-f by MESFUNC2:def 2,def 3;
A4:A = dom|.f.| & |.f.| is_measurable_on A
      by A2,MESFUNC1:def 10,MESFUNC2:29; then
A5:max-|.f.| is_measurable_on A by MESFUNC2:28;
A6:max+f is_measurable_on A & max-f is_measurable_on A by A2,MESFUNC2:27,28;
A7:max+f is nonnegative by MAX; then
A8:-infty <> integral+(M,max+f) by A3,A2,LM018c,A6;
P8:|.f.| = max+f + max-f by MESFUNC2:26;
   hereby assume
AS0:f is_integrable_on M;
 P2:now let x be set;
     assume
  PA: x in dom |.f.|; then
     (|.f.|).x =|. f.x .| by MESFUNC1:def 10; then
  B: 0 <= (|.f.|).x by EXTREAL2:51;
     (max+|.f.|).x = max((|.f.|).x,0) by PA,A3,MESFUNC2:def 2;
     hence (max+|.f.|).x = (|.f.|).x by XXREAL_0:def 9,B;
    end; then
 L1:|.f.| = max+|.f.| by FUNCT_1:9,A3;
    now let x be Element of X;
     assume
   B: x in dom max-|.f.|; then
     (max+|.f.|).x=(|.f.|).x by P2,A3;
     hence (max-|.f.|).x=0 by B,A3,MESFUNC2:21;
    end; then
 P7:integral+(M,max-|.f.|)=0 by LM018j,A5,A3,A4;
N1: integral+(M,max+f) < +infty & integral+(M,max-f) < +infty
     by AS0,Defintegrable;
    max-f is nonnegative by MAX; then
    integral+(M,max+f + max-f) =integral+(M,max+f)+integral+(M,max-f)
         by LM018,A3,A7,A6,A2; then
    integral+(M,max+f + max-f) <> +infty by SUPINF_2:8,N1; then
    integral+(M,max+|.f.|) < +infty by L1,P8,SUPINF_1:24;
    hence |.f.| is_integrable_on M by A4,P7,Defintegrable;
   end;
   assume
AS0:|.f.| is_integrable_on M;
    now let x be set;
     assume x in dom |.f.|; then
     (|.f.|).x =|. f.x .| by MESFUNC1:def 10;
     hence 0 <= (|.f.|).x by EXTREAL2:51;
    end; then
QQ1:|.f.| is nonnegative by SUPINF_2:71;
EB3:max-f is nonnegative by MAX; then
EB6:-infty <> integral+(M,max-f) by LM018c,A3,A2,A6;
P9: integral+(M,max+f + max-f) = integral+(M,max+f) + integral+(M,max-f)
        by LM018,A3,A6,A7,EB3,A2;
    0 <= integral+(M,max+|.f.|) & 0 <= integral+(M,max-|.f.|) &
    -infty < Integral(M,|.f.|) & Integral(M,|.f.|) < +infty by LM022a,AS0; then
    integral+(M,max+f + max-f) < +infty by P8,LM022b,A4,QQ1; then
    integral+(M,max+f) <>+infty & integral+(M,max-f) <> +infty
      by EXTREAL2:23,EB6,A8,P9; then
    integral+(M,max+f) < +infty & integral+(M,max-f) < +infty by SUPINF_1:24;
    hence f is_integrable_on M by A1,Defintegrable;
end;

theorem
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL st
 f is_integrable_on M holds
    |. Integral(M,f) .| <= Integral(M,|.f.|)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL such that
A1:f is_integrable_on M;
   consider A be Element of S such that
A2: A = dom f & f is_measurable_on A by Defintegrable,A1;
A3:0 <= integral+(M,max+f) & 0 <= integral+(M,max-f) &
   -infty < Integral(M,f) & Integral(M,f) < +infty &
   integral+(M,max+f) <> +infty & integral+(M,max-f) <> +infty
     by LM022a,A1,Defintegrable;
A4:A = dom |.f.| & |.f.| is_measurable_on A by A2,MESFUNC1:def 10,MESFUNC2:29;
   now let x be set;
    assume x in dom (|.f.|); then
    (|.f.|).x =|. f.x .| by MESFUNC1:def 10;
    hence 0 <= (|.f.|).x by EXTREAL2:51;
   end; then
A5:|.f.| is nonnegative by SUPINF_2:71;
A7:dom f=dom max+f & dom f = dom max-f by MESFUNC2:def 2,def 3;
A8:max+f is nonnegative & max-f is nonnegative by MAX;
A9:max+f is_measurable_on A & max-f is_measurable_on A
     by A2,MESFUNC2:27,28; then
A10:0 <= integral+(M,max+f) & 0 <= integral+(M,max-f) by LM018c,A8,A7,A2;
   |.f.| = max+f + max-f by MESFUNC2:26; then
P1:Integral(M,|.f.|) = integral+(M,max+f + max-f) by LM022b,A4,A5
    .= integral+(M,max+f)+integral+(M,max-f) by LM018,A7,A8,A9,A2;
   |.integral+(M,max+f)-integral+(M,max-f).|
    <= |.integral+(M,max+f).| + |.integral+(M,max-f).| by EXTREAL2:69,A3; then
   |.Integral(M,f).| <= integral+(M,max+f) + |.integral+(M,max-f).|
       by EXTREAL1:def 3,A10;
   hence thesis by P1,EXTREAL1:def 3,A10;
end;

theorem LM022n:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
 ( ex A be Element of S st A = dom f & f is_measurable_on A ) &
 dom f = dom g & g is_integrable_on M &
 ( for x be Element of X st x in dom f holds |.f.x .| <= g.x )
holds
   f is_integrable_on M & Integral(M,|.f.|) <= Integral(M,g)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL;
   assume    
AS: ( ex A be Element of S st A = dom f & f is_measurable_on A ) &
    dom f = dom g & g is_integrable_on M &
    ( for x be Element of X st x in dom f holds |. f.x .| <= g.x );
   consider A be Element of S such that
A1:A = dom f & f is_measurable_on A by AS;
   consider AA be Element of S such that
A2:AA = dom g & g is_measurable_on AA by Defintegrable,AS;
A3:A = dom |.f.| & |.f.| is_measurable_on A by A1,MESFUNC1:def 10,MESFUNC2:29;
   now let x be set;
    assume x in dom g; then
    |. f.x .| <= g.x by AS;
    hence 0 <= g.x by EXTREAL2:51;
   end; then
A4:g is nonnegative by SUPINF_2:71;
   now let x be set;
    assume x in dom |.f.|; then
    (|.f.|).x =|. f.x .| by MESFUNC1:def 10;
    hence 0 <= (|.f.|).x by EXTREAL2:51;
   end; then
A5:|.f.| is nonnegative by SUPINF_2:71;
P1:dom |.f.| = dom max+|.f.| & dom |.f.| = dom max-(|.f.|) &
   dom g = dom max+ g by MESFUNC2:def 2,def 3;
P2:now let x be set;
    assume PA:x in dom |.f.|; then
    (|.f.|).x =|. f.x .| by MESFUNC1:def 10; then
B:  0 <= (|.f.|).x by EXTREAL2:51;
    thus (max+|.f.|).x = max((|.f.|).x,0) by PA,P1,MESFUNC2:def 2
                      .=(|.f.|).x by XXREAL_0:def 9,B;
   end; then
L1:|.f.| = max+|.f.| by FUNCT_1:9,P1;
   now let x be set;
    assume PA:x in dom g;
B:  0 <= g.x by A4,SUPINF_2:70;
    thus max+g.x = max(g.x,0) by PA, P1,MESFUNC2:def 2
                .=g.x by XXREAL_0:def 9,B;
   end; then
XL1:g = max+g by FUNCT_1:9,P1;
P4:now let x be Element of X;
    assume B:x in dom max-(|.f.|); then
    max+(|.f.|).x=(|.f.|).x by P1,P2;
    hence max-(|.f.|).x=0 by B,P1,MESFUNC2:21;
   end;
   max-|.f.| is_measurable_on A by A3,MESFUNC2:28; then
P7:integral+(M,max-|.f.|) = 0 by LM018j,P4,P1,A3;
   for x be Element of X st x in dom |.f.| holds (|.f.|).x <= g.x
   proof
    let x be Element of X;
    assume AA: x in dom |.f.|; then
    (|.f.|).x =|. f.x .| by MESFUNC1:def 10;
    hence (|.f.|).x <= g.x by AS,AA,A3,A1;
   end; then
T1:integral+(M,|.f.|) <= integral+(M,g) by LM018h,AS,A1,A2,A4,A3,A5;
   integral+(M,max+g) < +infty by AS,Defintegrable; then
   integral+(M,max+(|.f.|)) < +infty by L1,T1,XXREAL_0:2,XL1; then
   |.f.| is_integrable_on M by A3,P7,Defintegrable;
   hence f is_integrable_on M by LM022l,AS;
   Integral(M,g) =integral+(M,g) by LM022b,A2,A4;
   hence thesis by LM022b,A3,A5,T1;
end;

theorem CorJ:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL, r be Real st 
 dom f in S & 0 <= r & dom f <> {} &
 (for x be set st x in dom f holds f.x = r) holds
   integral(X,S,M,f) = R_EAL(r) * M.(dom f)
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f be PartFunc of X,ExtREAL;
   let r be Real;
   assume that
A0:dom f in S and
A1:0 <= r and
A2:dom f <> {} and
A3:for x be set st x in dom f holds f.x = r;
   reconsider Df = dom f as Element of S by A0;
B1:f is_simple_func_in S by A0,A3,CorJa;
   for x be set st x in dom f holds 0 <= f.x by A3,A1; then
   consider F be Finite_Sep_Sequence of S,
            a,v be FinSequence of ExtREAL such that
A4: F,a are_Re-presentation_of f &
    dom v = dom F &
    (for n be Nat st n in dom v holds v.n = a.n*(M*F).n) &
    integral(X,S,M,f) = Sum v by A2,B1,MESFUNC4:4;
A5:dom f = union rng F & dom F = dom a &
   for n be Nat st n in dom F
    for x be set st x in F.n holds f.x = a.n by A4,MESFUNC3:def 1;
A7:dom v = dom(M*F) by A4,MESFUNC3:8;
   for n be Nat st n in dom v holds v.n = R_EAL r * (M*F).n
   proof
    let n be Nat;
    assume
 B1: n in dom v; then
    F.n in rng F by A4,FUNCT_1:12; then
 B3:F.n c= union rng F by ZFMISC_1:92;
 B4:v.n = a.n*(M*F).n by B1,A4;
    per cases;
    suppose F.n = {}; then
     M.(F.n) = 0 by MEASURE1:def 11; then
 B5: (M*F).n = 0 by B1,A4,FUNCT_1:23; then
     v.n = 0 by B4,EXTREAL1:22;
     hence v.n = R_EAL r * (M*F).n by B5,EXTREAL1:22;
    end;
    suppose F.n <> {}; then
     consider x be set such that
B7:  x in F.n by XBOOLE_0:def 1;
     a.n = f.x by A4,B1,B7,MESFUNC3:def 1;
     hence v.n = R_EAL r * (M*F).n by B4,A3,A5,B3,B7;
    end;
   end; then
   integral(X,S,M,f) = R_EAL r * Sum(M*F) by A4,A7,MESFUNC3:10
                    .= R_EAL r * M.(union rng F) by MESFUNC3:9;
   hence thesis by A4,MESFUNC3:def 1;
end;

theorem CorJ1:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL, r be Real st 
 dom f in S & 0 <= r & 
 (for x be set st x in dom f holds f.x = r) holds
   integral'(M,f) = R_EAL(r) * M.(dom f)
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f be PartFunc of X,ExtREAL;
   let r be Real;
   assume that
A0:dom f in S and
A1:0 <= r and
A3:for x be set st x in dom f holds f.x = r;
   per cases;
   suppose E:dom f = {}; then
 D1:integral'(M,f) = 0 by Defintegral';
    M.(dom f) = 0 by E,MEASURE1:def 11;
    hence thesis by D1,EXTREAL1:22;
   end;
   suppose D:dom f <> {}; then
    integral'(M,f) = integral(X,S,M,f) by Defintegral';
    hence thesis by A0,A1,A3,D,CorJ;
   end;
end;

theorem LM022u:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL st 
f is_integrable_on M holds
  f"{+infty} in S & f"{-infty} in S & M.(f"{+infty})=0 & M.(f"{-infty})=0 &
  f"{+infty} \/ f"{-infty} in S & M.(f"{+infty} \/ f"{-infty})=0
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f be PartFunc of X,ExtREAL;
   assume 
A0:f is_integrable_on M; then
A1:(ex A be Element of S st A = dom f & f is_measurable_on A) &
   integral+(M,max+f) < +infty &
   integral+(M,max-f) < +infty by Defintegrable;
   consider A be Element of S such that
A2: A = dom f & f is_measurable_on A by A0,Defintegrable;
A3:A /\ eq_dom(f,+infty) is_measurable_on S &
   A /\ eq_dom(f,-infty) is_measurable_on S by A2,MESFUNC1:37,38;
   for x be set holds
    ( x in eq_dom(f,+infty) implies x in A ) &
    ( x in eq_dom(f,-infty) implies x in A ) by A2,MESFUNC1:def 16; then
A4:eq_dom(f,+infty) c= A & eq_dom(f,-infty) c= A by TARSKI:def 3; then
   A /\ eq_dom(f,+infty) = eq_dom(f,+infty) &
   A /\ eq_dom(f,-infty) = eq_dom(f,-infty) by XBOOLE_1:28; then
Q1:f"{+infty} is_measurable_on S & f"{-infty} is_measurable_on S by A3,CorG;
   hence
P1:f"{+infty} in S & f"{-infty} in S by MESFUNC1:def 11;
   reconsider B1 = f"{+infty} as Element of S by Q1,MESFUNC1:def 11;
   reconsider B2 = f"{-infty} as Element of S by Q1,MESFUNC1:def 11;
B5:max+f is nonnegative & max-f is nonnegative by MAX;
   set C1 = A \ B1;
   set C2 = A \ B2;
B3:A = dom max+f & A = dom max-f by A2,MESFUNC2:def 2,def 3; then
B7:B1 c= dom max+f & B2 c= dom(max-f) by A4,CorG; then
   B1\/C1 = A & B2\/C2 = A by B3,XBOOLE_1:45; then
B8:dom(max+f|(B1\/C1)) = dom max+f /\ dom max+f & 
   dom(max-f|(B2\/C2)) = dom max-f /\ dom max-f by B3,FUNCT_1:68;
   for x be Element of X holds
    ( x in dom(max+f|(B1\/C1)) implies max+f|(B1\/C1).x = max+f.x ) &
    ( x in dom(max-f|(B2\/C2)) implies max-f|(B2\/C2).x = max-f.x )
      by FUNCT_1:70; then
B9:max+f|(B1\/C1) = max+f & max-f|(B2\/C2) = max-f by B8,PARTFUN1:34;
B4:max+f is_measurable_on A & max-f is_measurable_on A
     by A2,MESFUNC2:27,28; then
B4b:max+f is_measurable_on B1 & max-f is_measurable_on B2 by B3,B7,MESFUNC1:34;
   B1 = dom max+f /\ B1 & B2 = dom(max-f) /\ B2 by B7,XBOOLE_1:28; then
B4a:max+f|B1 is_measurable_on B1 & max-f|B2 is_measurable_on B2 by B4b,Cor18;
   C1 c= dom max+f & B1 misses C1 &
   C2 c= dom(max-f) & B2 misses C2 by B3,XBOOLE_1:106; then
   0 <= integral+(M,max+f|C1) & 0 <= integral+(M,max+f|B1) &
   integral+(M,max+f) = integral+(M,max+f|B1) + integral+(M,max+f|C1) &
   0 <= integral+(M,max-f|C2) & 0 <= integral+(M,max-f|B2) &
   integral+(M,max-f) = integral+(M,max-f|B2) + integral+(M,max-f|C2)   
     by B3,B4,B5,B9,LM018d,LM018d1; then
B11:integral+(M,max+f|B1) <= integral+(M,max+f) &
   integral+(M,max-f|B2) <= integral+(M,max-f) by CorI;
   thus
P2:now assume
B1: M.(f"{+infty}) <> 0; then
B2: 0 < M.(f"{+infty}) by P1,CorH;
B12:for r be Real st 0 < r holds R_EAL r * M.B1 <= integral+(M,max+f)
    proof
     let r be Real; assume 
  C0:0 < r;
     defpred P[set] means $1 in dom(max+f|B1);
     deffunc F(set) = R_EAL r;
  C1:for x be set st P[x] holds F(x) in ExtREAL;
     consider g be PartFunc of X,ExtREAL such that
  C2: (for x be set holds x in dom g iff x in X & P[x]) &
      (for x be set st x in dom g holds g.x = F(x)) from PARTFUN1:sch 3(C1);
     dom g = X /\ dom(max+f|B1) by C2,XBOOLE_0:def 3; then
  C5:dom g = dom(max+f|B1) by XBOOLE_1:28;
     dom(max+f|B1) = dom max+f /\ B1 by FUNCT_1:68; then
  C6:dom(max+f|B1) = B1 by B7,XBOOLE_1:28;
  C8:g is_measurable_on B1
     proof
      dom chi(B1,X) = X by FUNCT_3:def 3; then
   G3:B1 = dom chi(B1,X) /\ B1 by XBOOLE_1:28; then
   G1:B1 = dom(chi(B1,X)|B1) by FUNCT_1:68; then
   H0:dom g = dom(r(#)chi(B1,X)|B1) by C5,C6,MESFUNC1:def 6;
      for x be Element of X st x in dom g holds g.x = (r(#)(chi(B1,X)|B1)).x
      proof
       let x be Element of X;
       assume 
   H1: x in dom g; then
       x in dom(chi(B1,X)|B1) by C5,C6,G3,FUNCT_1:68; then
       x in dom(r(#)(chi(B1,X)|B1)) by MESFUNC1:def 6; then
   H4: (r(#)(chi(B1,X)|B1)).x = R_EAL r * (chi(B1,X)|B1).x by MESFUNC1:def 6
                           .= R_EAL r * chi(B1,X).x by H1,C5,C6,G1,FUNCT_1:70;
       chi(B1,X).x = 1 by H1,C5,C6,FUNCT_3:def 3; then
       (r(#)(chi(B1,X)|B1)).x = R_EAL r by H4,EXTREAL2:4;
       hence thesis by H1,C2;
      end; then
   G2:g = r(#)(chi(B1,X)|B1) by H0,PARTFUN1:34;
      chi(B1,X) is_measurable_on B1 by MESFUNC2:32; then
      chi(B1,X)|B1 is_measurable_on B1 by G3,Cor18;
      hence g is_measurable_on B1 by G1,G2,MESFUNC1:41;
     end;
     for x be set st x in dom g holds 0 <= g.x by C2,C0; then
  C9:g is nonnegative by SUPINF_2:71;
 C10:for x be Element of X st x in dom g holds g.x <= max+f|B1.x
     proof
      let x be Element of X;
      assume 
   F6:x in dom g; then
   F3:x in dom f & f.x in {+infty} by C5,C6,FUNCT_1:def 13; then
   F5:f.x = +infty by TARSKI:def 1; then
   F4:max(f.x,0) = f.x by XXREAL_0:def 9;
      x in dom max+f by F3,MESFUNC2:def 2; then
      max+f.x = +infty by F4,F5,MESFUNC2:def 2; then
      max+f|B1.x = +infty by F6,C5,C6,FUNCT_1:72;
      hence thesis by SUPINF_1:20;
     end;
     max+f|B1 is nonnegative by B5,LL0; then
     integral+(M,g) <= integral+(M,max+f|B1) 
       by B4a,C5,C6,C8,C9,C10,LM018h; then
  D1:integral+(M,g) <= integral+(M,max+f) by B11,SUPINF_1:29;
  D2:integral'(M,g) = R_EAL r * M.(dom g) by C2,C0,C6,C5,CorJ1;
     g is_simple_func_in S by C2,C6,C5,CorJa;
     hence thesis by D1,C5,C6,C9,D2,LM018a;
    end;
    per cases;
    suppose
  E1:M.B1 = +infty;
     R_EAL 1 * M.B1 <= integral+(M,max+f) by B12;
     hence contradiction by A1,E1,EXTREAL2:4;
    end;
    suppose
  E2:M.B1 <> +infty;
     R_EAL 1 * M.B1 <= integral+(M,max+f) by B12; then
  E3:0 < integral+(M,max+f) by B2,EXTREAL2:4;
     reconsider MB = M.B1 as Real by E2,EXTREAL1:1,B2;
     reconsider I = integral+(M,max+ f) as Real by A1,EXTREAL1:1,E3;
     0 < 2 * I by E3,XREAL_1:131; then
     (2 * I) / MB > 0 by B2,XREAL_1:141; then
  E6:R_EAL(2*I/MB) * M.B1 <= integral+(M,max+f) by B12;
     R_EAL(2*I/MB) * M.B1 = 2*I/MB * MB by EXTREAL1:13; then
     2 * I <= I by B1,E6,XCMPLX_1:88;
     hence contradiction by E3,XREAL_1:157;
    end;
   end;
   thus
P4:now assume 
B1: M.(f"{-infty}) <> 0;
B2':0 <= M.(f"{-infty}) by P1,CorH;
B12:for r be Real st 0 < r holds R_EAL r * M.B2 <= integral+(M,max-f)
    proof
     let r be Real;
     assume 
  C0: 0 < r;
     defpred P[set] means $1 in dom(max-f|B2);
     deffunc F(set) = R_EAL r;
  C1:for x be set st P[x] holds F(x) in ExtREAL;
     consider g be PartFunc of X,ExtREAL such that
  C2: (for x be set holds x in dom g iff x in X & P[x]) &
      (for x be set st x in dom g holds g.x = F(x)) from PARTFUN1:sch 3(C1);
     dom g = X /\ dom(max-f|B2) by C2,XBOOLE_0:def 3; then
  C5:dom g = dom(max- f|B2) by XBOOLE_1:28;
     dom(max-f|B2) = dom max-f /\ B2 by FUNCT_1:68; then
  C6:dom(max-f|B2) = B2 by B7,XBOOLE_1:28;
  C8:g is_measurable_on B2
     proof
      dom chi(B2,X) = X by FUNCT_3:def 3; then
   G3:B2 = dom chi(B2,X) /\ B2 by XBOOLE_1:28; then
   G1:B2 = dom(chi(B2,X)|B2) by FUNCT_1:68; then
   H0:dom g = dom(r(#)chi(B2,X)|B2) by C5,C6,MESFUNC1:def 6;
      for x be Element of X st x in dom g holds g.x = (r(#)(chi(B2,X)|B2)).x
      proof
       let x be Element of X;
       assume 
   H1: x in dom g; then
       x in dom(r(#)(chi(B2,X)|B2)) by C5,C6,G1,MESFUNC1:def 6; then
   H4: (r(#)(chi(B2,X)|B2)).x = R_EAL r * (chi(B2,X)|B2).x by MESFUNC1:def 6
                           .= R_EAL r * chi(B2,X).x by H1,C5,C6,G1,FUNCT_1:70;
       chi(B2,X).x = 1 by H1,C5,C6,FUNCT_3:def 3; then
       (r(#)(chi(B2,X)|B2)).x = R_EAL r by H4,EXTREAL2:4;
       hence thesis by H1,C2;
      end; then
   G2:g = r(#)(chi(B2,X)|B2) by H0,PARTFUN1:34;
      chi(B2,X) is_measurable_on B2 by MESFUNC2:32; then
      chi(B2,X)|B2 is_measurable_on B2 by G3,Cor18;
      hence g is_measurable_on B2 by G1,G2,MESFUNC1:41;
     end;
     for x be set st x in dom g holds 0 <= g.x by C0,C2; then
  C9:g is nonnegative by SUPINF_2:71;
 C10:for x be Element of X st x in dom g holds g.x <= (max-f|B2).x
     proof
      let x be Element of X;
      assume 
   F6:x in dom g; then
   F3:x in dom f & f.x in {-infty} by C5,C6,FUNCT_1:def 13; then
   F5:-f.x = +infty by TARSKI:def 1,SUPINF_2:4; then
   F4:max(-f.x,0) = -f.x by XXREAL_0:def 9;
      x in dom max-f by F3,MESFUNC2:def 3; then
      max-f.x = +infty by F4,F5,MESFUNC2:def 3; then
      (max-f|B2).x = +infty by F6,C5,C6,FUNCT_1:72;
      hence thesis by SUPINF_1:20;
     end;
     max-f|B2 is nonnegative by B5,LL0; then
     integral+(M,g) <= integral+(M,max-f|B2) 
       by B4a,C5,C6,C8,C9,C10,LM018h; then
  D1:integral+(M,g) <= integral+(M,max-f) by B11,SUPINF_1:29;
  D2:integral'(M,g) = R_EAL r * M.(dom g) by C0,C2,C6,C5,CorJ1;
     g is_simple_func_in S by C2,C5,C6,CorJa;
     hence thesis by D1,C5,C6,C9,D2,LM018a;
    end;
    per cases;
    suppose
  E1:M.B2 = +infty;
     R_EAL 1 * M.B2 <= integral+(M,max-f) by B12;
     hence contradiction by A1,E1,EXTREAL2:4;
    end;
    suppose
  E2:M.B2 <> +infty;
     R_EAL 1 * M.B2 <= integral+(M,max-f) by B12; then
  E3:0 < integral+(M,max-f) by B2',B1,EXTREAL2:4;
     reconsider MB = M.B2 as Real by E2,EXTREAL1:1,B2';
     reconsider I = integral+(M,max-f) as Real by A1,EXTREAL1:1,E3;
     0 < 2*I by E3,XREAL_1:131; then
     (2*I)/MB > 0 by B2',B1,XREAL_1:141; then
  E6:R_EAL(2*I/MB) * M.B2 <= integral+(M,max-f) by B12;
     R_EAL(2*I/MB) * M.B2 = 2*I/MB * MB by EXTREAL1:13; then
     2 * I <= I by B1,E6,XCMPLX_1:88;
     hence contradiction by E3,XREAL_1:157;
    end;
   end;
   thus f"{+infty} \/ f"{-infty} in S by P1,PROB_1:41;
   reconsider B1 as measure_zero of M by P2,MEASURE1:def 13;
   thus M.(f"{+infty} \/ f"{-infty}) = M.(B1 \/ B2)
          .= 0 by P4,MEASURE1:71;
end;

theorem LM022y1:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
f is_integrable_on M & g is_integrable_on M &
f is nonnegative & g is nonnegative holds
   f+g is_integrable_on M
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL;
   assume
AS: f is_integrable_on M & g is_integrable_on M & 
    f is nonnegative & g is nonnegative; then
   consider A be Element of S such that
A1: A = dom f & f is_measurable_on A by Defintegrable;
   consider B be Element of S such that
A2: B = dom g & g is_measurable_on B by Defintegrable,AS;
   dom f /\ dom g c= dom f & dom f /\ dom g c= dom g by XBOOLE_1:17; then
   f is_measurable_on A/\B & g is_measurable_on A/\B by A1,A2,MESFUNC1:34; then
A3:A/\B = dom(f+g) & f+g is_measurable_on A/\B by AS,A2,A1,MESFUNC2'7a;
A4:dom(f+g) = dom max+(f+g) & dom f = dom max+f & dom g = dom max+g
     by MESFUNC2:def 2;
P2:now let x be set; assume
 PA: x in dom(f+g); then
DD0:(f+g).x =f.x+g.x by MESFUNC1:def 3;
    0 <= f.x & 0 <= g.x by AS,SUPINF_2:70; then
  B:0 <= (f+g).x by EXTREAL2:7,DD0;
    max+(f+g).x = max((f+g).x,0) by PA,A4,MESFUNC2:def 2;
    hence max+(f+g).x =(f+g).x by XXREAL_0:def 9,B;
   end; then
L1:f+g = max+(f+g) by FUNCT_1:9,A4;
   now let x be set; assume
 PA:x in dom g;
  B:0 <= g.x by AS,SUPINF_2:70;
    (max+g).x = max(g.x,0) by PA,A4,MESFUNC2:def 2;
    hence (max+g).x = g.x by XXREAL_0:def 9,B;
   end; then
A6:g = max+g by FUNCT_1:9,A4;
   now let x be set; assume
 PA:x in dom f;
  B:0 <= f.x by AS,SUPINF_2:70;
    (max+f).x = max(f.x,0) by PA,A4,MESFUNC2:def 2;
    hence (max+f).x = f.x by XXREAL_0:def 9,B;
   end; then
A7:f = max+f by FUNCT_1:9,A4;
P4:now let x be Element of X;
    assume x in dom max-(f+g); then 
  B:x in dom(f+g) by MESFUNC2:def 3; then
    max+(f+g).x=(f+g).x by P2;
    hence max-(f+g).x=0 by B,MESFUNC2:21;
   end;
B2:dom(f+g)=dom(max-(f+g)) by MESFUNC2:def 3;
   max-(f+g) is_measurable_on A/\B by A3,MESFUNC2:28; then
A8:integral+(M,max-(f+g))=0 by LM018j,P4,B2,A3;
   consider C be Element of S such that
PP: C = dom(f+g) &
    integral+(M,f+g) = integral+(M,f|C) + integral+(M,g|C) by LM018b,A2,A1,AS;
   C c= A & C c= B by PP,A3,XBOOLE_1:17; then
   integral+(M,f|C) <= integral+(M,f|A) &
   integral+(M,g|C) <= integral+(M,g|B) by A1,A2,LM018f,AS; then
PQ:integral+(M,f|C) <= integral+(M,max+f) &
   integral+(M,g|C) <= integral+(M,max+g) by A1,A2,A6,A7,GRFUNC_1:64;
ZQ1:integral+(M,max+f) < +infty &
   integral+(M,max+g) < +infty by AS,Defintegrable; then
   integral+(M,max+f) + integral+(M,max+g) <> +infty by SUPINF_2:8; then
X: integral+(M,max+f) + integral+(M,max+g) < +infty by SUPINF_1:24;
   integral+(M,max+(f+g)) <= integral+(M,max+f) + integral+(M,max+g)
        by PP,L1,PQ,ZQ1,SUPINF_2:14; then
   integral+(M,max+(f+g)) < +infty by X,SUPINF_1:24;
   hence thesis by A3,Defintegrable,A8;
end;

theorem LM022y2:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st 
f is_integrable_on M & g is_integrable_on M holds dom (f+g) in S
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL;
   assume f is_integrable_on M & g is_integrable_on M; then
   f"{+infty} in S & f"{-infty} in S & g"{+infty} in S & g"{-infty} in S &
   (ex E1 be Element of S st E1=dom f & f is_measurable_on E1) &
   (ex E2 be Element of S st E2=dom g & g is_measurable_on E2)
     by Defintegrable,LM022u;
   hence dom(f+g) in S by LM022y2a;
end;

LM022y0:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
 (ex E0 be Element of S st dom(f+g) = E0 & f+g is_measurable_on E0) &
 f is_integrable_on M & g is_integrable_on M 
 holds f+g is_integrable_on M
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL;
   assume 
AS:(ex E0 be Element of S st dom(f+g) = E0 & f+g is_measurable_on E0) &
   f is_integrable_on M & g is_integrable_on M; then
   consider E be Element of S such that
A1: dom(f+g) = E & f+g is_measurable_on E;
A2:dom(f+g)
    = (dom f /\ dom g)\(f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty})
      by MESFUNC1:def 3; then
   dom(f+g) c= dom f /\ dom g by XBOOLE_1:36; then
A3:dom(f+g) c= dom f & dom(f+g) c= dom g by XBOOLE_1:18; then
A4:E c= dom |.f.| & E c= dom |.g.| by MESFUNC1:def 10,A1;
   consider E1 be Element of S such that
A5: E1=dom f & f is_measurable_on E1 by Defintegrable,AS;
   consider E2 be Element of S such that
A6: E2=dom g & g is_measurable_on E2 by Defintegrable,AS;
   |.f.| is_integrable_on M & |.g.| is_integrable_on M
     by AS,A5,A6,LM022l; then
A7:(|.f.|)|E is_integrable_on M & (|.g.|)|E is_integrable_on M by LM022g;
   f is_measurable_on E & g is_measurable_on E by A5,A6,A1,A3,MESFUNC1:34; then
A8:|.f.| is_measurable_on E & |.g.| is_measurable_on E by A1,A3,MESFUNC2:29;
   |.f.| is nonnegative & |.g.| is nonnegative by MAX; then
A9:(|.f.|)|E is nonnegative & (|.g.|)|E is nonnegative by LL0; then
A10:(|.f.|)|E + (|.g.|)|E is_integrable_on M by A7,LM022y1;
   dom |.f.| /\ E = E & dom |.g.| /\ E = E by A4,XBOOLE_1:28; then
A11:E = dom((|.f.|)|E) & (|.f.|)|E is_measurable_on E &
   E = dom((|.g.|)|E) & (|.g.|)|E is_measurable_on E 
     by A8,FUNCT_1:68,Cor18; then
A12:dom((|.f.|)|E) /\ dom((|.g.|)|E) = E /\ E;
   for x be set st x in dom f holds f.x in ExtREAL; then
   reconsider ff=f as Function of dom f, ExtREAL by FUNCT_2:5;
   for x be set st x in dom g holds g.x in ExtREAL; then
   reconsider gg=g as Function of dom g, ExtREAL by FUNCT_2:5;
A13:now let x be Element of X;
    assume
 A0: x in dom(f+g); then
    x in dom |.f.| & x in dom |.g.| by A1,A4; then
PP1:x in dom ff & x in dom gg by MESFUNC1:def 10;
PP2:x in dom((|.f.|)|E+(|.g.|)|E) by A0,A1,A9,A12,CorMES2'2'1;
    not ((f.x = +infty & g.x = -infty) or (f.x = -infty & g.x = +infty))
    proof
     assume
 Q1:  (f.x = +infty & g.x = -infty) or (f.x = -infty & g.x = +infty);
     per cases by Q1;
     suppose f.x = +infty & g.x = -infty; then
      ff.x in {+infty} & gg.x in {-infty} by TARSKI:def 1; then
      x in ff"{+infty} & x in gg"{-infty} by PP1,FUNCT_2:46; then
  QQ4:x in (ff"{+infty} /\ gg"{-infty}) by XBOOLE_0:def 3;
      ff"{+infty} /\ gg"{-infty}
       c= ff"{+infty}/\gg"{-infty} \/ ff"{-infty}/\gg"{+infty} by XBOOLE_1:7;
      hence contradiction by A0,A2,QQ4,XBOOLE_0:def 4;
     end;
     suppose f.x = -infty & g.x = +infty; then
      ff.x in {-infty} & gg.x in {+infty} by TARSKI:def 1; then
      x in ff"{-infty} & x in gg"{+infty} by PP1,FUNCT_2:46; then
  QQ4:x in (ff"{-infty} /\ gg"{+infty}) by XBOOLE_0:def 3;
      ff"{-infty} /\ gg"{+infty}
       c= ff"{+infty}/\gg"{-infty} \/ ff"{-infty}/\gg"{+infty} by XBOOLE_1:7;
      hence contradiction by A0,A2,QQ4,XBOOLE_0:def 4;
     end;
    end; then
 A7:|.f.x+g.x.| <= |.f.x.| +|.g.x.| by EXTREAL2:61;
    |.f.x.| +|.g.x.|
      = (|.f.|).x +|.g.x.| by MESFUNC1:def 10,A1,A0,A4
     .= (|.f.|).x +(|.g.|).x by MESFUNC1:def 10,A1,A0,A4
     .= ((|.f.|)|E).x +(|.g.|).x by A0,A1,A11,FUNCT_1:68
     .= ((|.f.|)|E).x +((|.g.|)|E).x by A0,A1,A11,FUNCT_1:68
     .= ( (|.f.|)|E+(|.g.|)|E ).x by PP2,MESFUNC1:def 3;
    hence |.(f+g).x.| <= ((|.f.|)|E+(|.g.|)|E).x by A0,MESFUNC1:def 3,A7;
   end;
   dom((|.f.|)|E+(|.g.|)|E) = E by A12,CorMES2'2'1,A9;
   hence f+g is_integrable_on M by A10,A13,A1,LM022n;
end;

theorem LM022y:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
 f is_integrable_on M & g is_integrable_on M holds
   f+g is_integrable_on M
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL such that
AS:f is_integrable_on M & g is_integrable_on M;
   (ex E1 be Element of S st E1=dom f & f is_measurable_on E1) &
   (ex E2 be Element of S st E2=dom g & g is_measurable_on E2) 
     by Defintegrable,AS; then
   ex K0 be Element of S st K0 = dom(f+g) & f+g is_measurable_on K0
     by LM022y3a;
   hence thesis by AS,LM022y0;
end;

LM022z1:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
 f is_integrable_on M & g is_integrable_on M & dom f = dom g holds
  ex E,NFG,NFPG be Element of S st
    E c= dom f & NFG c= dom f & E = dom f \ NFG &
    f|E is_finite & E = dom(f|E) & f|E is_measurable_on E &
    f|E is_integrable_on M & Integral(M,f)=Integral(M,f|E) &
    E c= dom g & NFG c= dom g & E = dom g \ NFG &
    g|E is_finite & E = dom(g|E) & g|E is_measurable_on E &
    g|E is_integrable_on M & Integral(M,g)=Integral(M,g|E) &
    E c= dom(f+g) & NFPG c= dom(f+g) & E = dom(f+g) \ NFPG &
    M.NFG = 0 &M.NFPG = 0 & E = dom((f+g)|E) &
    (f+g)|E is_measurable_on E & (f+g)|E is_integrable_on M &
    (f+g)|E = f|E + g|E &
    Integral(M,(f+g)|E)=Integral(M,f|E)+Integral(M,g|E)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL;
   assume that
AS: f is_integrable_on M & g is_integrable_on M & dom f = dom g;
   consider E0 be Element of S such that
A0: E0=dom f & f is_measurable_on E0 by Defintegrable,AS;
A1:ex Gf be Element of S st
    Gf=dom g & g is_measurable_on Gf by Defintegrable,AS;
   reconsider NF = f"{+infty} \/ f"{-infty} as Element of S by LM022u,AS;
   reconsider NG = g"{+infty} \/ g"{-infty} as Element of S by LM022u,AS;
   set NFG= NF \/ NG;
   M.NF = 0 & M.NG = 0 by LM022u,AS; then
   NF is measure_zero of M & NG is measure_zero of M by MEASURE1:def 13; then
A3:NFG is measure_zero of M by MEASURE1:70; then
A4:M.NFG=0 by MEASURE1:def 13;
   set E = E0 \ NFG;
   for x be set holds
    ( x in f"{+infty} implies x in dom f ) &
    ( x in f"{-infty} implies x in dom f ) &
    ( x in g"{+infty} implies x in dom g ) & 
    ( x in g"{-infty} implies x in dom g ) by FUNCT_1:def 13; then
    f"{+infty} c= dom f & f"{-infty} c= dom f &
    g"{+infty} c= dom g & g"{-infty} c= dom g by TARSKI:def 3; then
    NF c= dom g & NG c= dom g by AS,XBOOLE_1:8; then
A5:NF \/ NG c= dom g by XBOOLE_1:8;
A2:dom(f+g)
    = (dom f /\ dom g)\( f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty} )
      by MESFUNC1:def 3;
   set f1=f|E;
   set g1=g|E;
   f"{-infty} c= NF & g"{-infty} c= NG & f"{-infty}/\g"{+infty} c= f"{-infty} &
   f"{+infty}/\g"{-infty} c= g"{-infty} by XBOOLE_1:7,17; then
   f"{-infty}/\g"{+infty} c= NF & f"{+infty}/\g"{-infty} c= NG
     by XBOOLE_1:1; then
   f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty} c= NF \/ NG 
     by XBOOLE_1:13; then
A7:E c= dom(f+g) by AS,A0,XBOOLE_1:34,A2; then
A8:(f+g)|E = f1+g1 & dom(f1+g1)=E by LM022w;
   reconsider DFPG = dom(f+g) as Element of S by LM022y2,AS;
   set NFPG=DFPG \ E;
   DFPG c= E0 by AS,A0,A2,XBOOLE_1:36; then
   DFPG \ (E0 \ NFG) c=E0 \ (E0 \ NFG) by XBOOLE_1:33; then
LL:NFPG c= E0 /\ NFG by XBOOLE_1:48;
   E0 /\ NFG c= NFG by XBOOLE_1:17; then
   NFPG c= NFG by LL,XBOOLE_1:1; then
   NFPG is measure_zero of M by A3,MEASURE1:69; then
A11:M.NFPG=0 by MEASURE1:def 13;
   DFPG \ NFPG = DFPG /\ E by XBOOLE_1:48; then
Z12:E= DFPG \ NFPG by A7,XBOOLE_1:28;
A13:NFPG c= dom(f+g) by XBOOLE_1:36;
A6:E c= dom f & E c= dom g by AS,A0,XBOOLE_1:36; then
B14:f is_measurable_on E & g is_measurable_on E by A0,A1,MESFUNC1:34;
C14:dom f1 = dom f /\ E & dom g1 = dom g /\ E by FUNCT_1:68; then
A14f:E = dom f1 & E = dom g1 by A6,XBOOLE_1:28; then 
D14:f1 is_measurable_on E & g1 is_measurable_on E by B14,C14,Cor18;
A16f:
   for x be set st x in dom f1 holds f1.x < +infty & -infty < f1.x
   proof
    let x be set;
    assume
 L1: x in dom f1;
    for x be set st x in dom f holds f.x in ExtREAL; then
    reconsider ff=f as Function of dom f, ExtREAL by FUNCT_2:5;
    x in dom f /\ E by L1,FUNCT_1:68; then
 L5:x in dom f & x in E by XBOOLE_0:def 3; then
 L8:not x in NFG by XBOOLE_0:def 4;
 L2:now assume f1.x = +infty; then
     f.x = +infty by FUNCT_1:68,L1; then
     ff.x in {+infty} by TARSKI:def 1; then
  PP:x in ff"{+infty} by L5,FUNCT_2:46;
     f"{+infty} c= NF by XBOOLE_1:7;
     hence contradiction by L8,PP,XBOOLE_0:def 2;
    end;
    now assume f1.x = -infty; then
     f.x = -infty by FUNCT_1:68,L1; then
     ff.x in {-infty} by TARSKI:def 1; then
  PP:x in ff"{-infty} by L5,FUNCT_2:46;
     f"{-infty} c= NF by XBOOLE_1:7;
     hence contradiction by L8,PP,XBOOLE_0:def 2;
    end;
    hence thesis by L2,SUPINF_1:23,SUPINF_1:24;
   end; then
   (for x be set st x in dom f1 holds -infty < f1.x) &
   (for x be set st x in dom f1 holds f1.x < +infty); then
A17f:f1 is without-infty & f1 is without+infty by WMI01,WPI01;
A16g:for x be set st x in dom g1 holds -infty < g1.x & g1.x < +infty
   proof
    let x be set;
    assume
 L1: x in dom g1;
    for x be set st x in dom g holds g.x in ExtREAL; then
    reconsider gg=g as Function of dom g, ExtREAL by FUNCT_2:5;
    x in dom g /\ E by L1,FUNCT_1:68; then
 L5:x in dom g & x in E by XBOOLE_0:def 3; then
 L8:not x in NFG by XBOOLE_0:def 4;
 L2:now assume g1.x = +infty; then
     g.x = +infty by FUNCT_1:68,L1; then
     gg.x in {+infty} by TARSKI:def 1; then
  PP:x in gg"{+infty} by L5,FUNCT_2:46;
     g"{+infty} c= NG by XBOOLE_1:7;
     hence contradiction by L8,PP,XBOOLE_0:def 2;
    end;
    now assume g1.x = -infty; then
     g.x = -infty by FUNCT_1:68,L1; then
     gg.x in {-infty} by TARSKI:def 1; then
  PP:x in gg"{-infty} by L5,FUNCT_2:46;
     g"{-infty} c= NG by XBOOLE_1:7;
     hence contradiction by L8,PP,XBOOLE_0:def 2;
    end;
    hence thesis by L2,SUPINF_1:23,SUPINF_1:24;
   end; then
   (for x be set st x in dom g1 holds -infty < g1.x) &
   (for x be set st x in dom g1 holds g1.x < +infty); then
A17g:g1 is without-infty & g1 is without+infty by WMI01,WPI01;
   now let x be Element of X;
    assume x in dom f1; then
 P1:-infty < f1.x & f1.x < +infty by A16f;
    -(+infty) = -infty by SUPINF_2:def 3;
    hence |.f1.x.| < +infty by P1,EXTREAL2:59;
   end; then
S1:f1 is_finite by MESFUNC2:def 1;
   now let x be Element of X;
    assume x in dom g1; then
 P1:-infty < g1.x & g1.x < +infty by A16g;
    -(+infty) = -infty by SUPINF_2:def 3;
    hence |.g1.x.| < +infty by P1,EXTREAL2:59;
   end; then
S2:g1 is_finite by MESFUNC2:def 1; then
S3:E =dom(f1+g1) & f1+g1 is_measurable_on E by A7,LM022w,S1,MESFUNC2:7,D14;
S4:f1 is_integrable_on M & g1 is_integrable_on M by AS,LM022g; then
S5:f1+g1 is_integrable_on M by LM022y;
   (for x be set st x in dom f1 holds -infty < f1.x) &
   (for x be set st x in dom g1 holds -infty < g1.x) by A16f,A16g; then
N1:f1 is without-infty & g1 is without-infty by WMI01; then
   dom(max+(f1+g1) + max-f1) = dom f1 /\ dom g1 &
   dom(max-(f1+g1) + max+f1) = dom f1 /\ dom g1 by LM019a; then
N2:dom(max+(f1+g1) + max-f1) = E & dom(max-g1) = E &
   dom(max-(f1+g1) + max+f1) = E & dom(max+g1) = E 
     by A14f,MESFUNC2:def 2,def 3;
N3:max+(f1+g1) + max-f1 is nonnegative & max-(f1+g1) + max+f1 is nonnegative
      by N1,LM019a;
XX:dom(max-f1)= dom f1 & dom(max+f1)= dom f1 &
   dom(max-g1)= dom g1 & dom(max+g1)= dom g1 &
   dom(max+(f1+g1))= dom(f1+g1) & dom(max-(f1+g1))= dom(f1+g1)
     by MESFUNC2:def 2,def 3; 
N4:max+f1 is nonnegative & max-f1 is nonnegative & 
   max-g1 is nonnegative & max+g1 is nonnegative &
   max+(f1+g1) is nonnegative & max-(f1+g1) is nonnegative by MAX;
N5:max+(f1+g1) is_measurable_on E & max-(f1+g1) is_measurable_on E
     by S3,MESFUNC2:27,28;
N6:max-f1 is_measurable_on E by A14f,D14,MESFUNC2:28;
N7:max+(f1+g1) + max-f1 + max-g1 = max-(f1+g1) + max+f1 + max+g1
     by A17f,A17g,LM019d;
N8:max+g1 is_measurable_on E & max+f1 is_measurable_on E by D14,MESFUNC2:27;
   dom f1 /\ dom g1 = E by A14f; then
   max-(f1+g1) + max+f1 is_measurable_on E by D14,N1,LM019c; then
N9:integral+(M,max-(f1+g1)+max+f1+max+g1)
     =integral+(M,max-(f1+g1) + max+f1) + integral+(M,max+g1)
        by LM018,N3,N8,N4,N2
    .=integral+(M,max-(f1+g1)) + integral+(M,max+f1) + integral+(M,max+g1)
        by LM018,N5,N4,N8,XX,A8,A14f;
P1:0 <= integral+(M,max+f1) & integral+(M,max+f1) <> +infty &
   0 <= integral+(M,max-f1) & integral+(M,max-f1) <> +infty &
   0 <= integral+(M,max+g1) & integral+(M,max+g1) <> +infty &
   0 <= integral+(M,max-g1) & integral+(M,max-g1) <> +infty &
   0 <= integral+(M,max+(f1+g1)) & integral+(M,max+(f1+g1)) <> +infty & 
   0 <= integral+(M,max-(f1+g1)) & integral+(M,max-(f1+g1)) <> +infty 
        by S4,S5,Defintegrable,LM022a; then
P2:-integral+(M,max-f1) <> -infty & -integral+(M,max-f1) <> +infty &
   -integral+(M,max-(f1+g1)) <> +infty & -integral+(M,max-(f1+g1)) <> -infty
         by EXTREAL1:4;
   max+(f1+g1) + max-f1 is_measurable_on E & max-g1 is_measurable_on E
    by A14f,D14,N1,LM019b,MESFUNC2:28; then
   integral+(M,max+(f1+g1)+max-f1+max-g1)
    =integral+(M,max+(f1+g1)+max-f1) + integral+(M,max-g1)
       by LM018,N3,N4,N2
   .=integral+(M,max+(f1+g1)) + integral+(M,max-f1) + integral+(M,max-g1)
       by LM018,N5,N6,N4,XX,A8,A14f; then
   integral+(M,max+(f1+g1))+integral+(M,max-f1)+
    (integral+(M,max-g1) -integral+(M,max-g1) )
    = integral+(M,max-(f1+g1))+integral+(M,max+f1)
       + integral+(M,max+g1) - integral+(M,max-g1) 
          by N9,N7,P1,EXTREAL1:11; then 
   integral+(M,max+(f1+g1))+integral+(M,max-f1)+
    (integral+(M,max-g1) -integral+(M,max-g1) )
    = integral+(M,max-(f1+g1))+integral+(M,max+f1)
       + (integral+(M,max+g1) -integral+(M,max-g1)) by P1,EXTREAL1:11; then
   integral+(M,max+(f1+g1))+integral+(M,max-f1)+ 0.
    = integral+(M,max-(f1+g1))+integral+(M,max+f1)
       + (integral+(M,max+g1) -integral+(M,max-g1)) by EXTREAL1:9; then
   integral+(M,max+(f1+g1))+integral+(M,max-f1)
    = integral+(M,max-(f1+g1))+integral+(M,max+f1)+(integral+(M,max+g1)
       - integral+(M,max-g1)) by SUPINF_2:18; then
   -integral+(M,max-(f1+g1))+ integral+(M,max+(f1+g1))+integral+(M,max-f1)
    = -integral+(M,max-(f1+g1))+ ( integral+(M,max-(f1+g1))
        +integral+(M,max+f1)+(integral+(M,max+g1) -integral+(M,max-g1)) ) 
         by P1,EXTREAL1:8; then
   -integral+(M,max-(f1+g1))+ integral+(M,max+(f1+g1))+ integral+(M,max-f1)
    = -integral+(M,max-(f1+g1))+(integral+(M,max-(f1+g1))
        +(integral+(M,max+f1)+(integral+(M,max+g1)-integral+(M,max-g1))))
         by P1,EXTREAL1:8; then
   -integral+(M,max-(f1+g1))+ integral+(M,max+(f1+g1))+ integral+(M,max-f1)
    = -integral+(M,max-(f1+g1))+integral+(M,max-(f1+g1))
        +(integral+(M,max+f1)+(integral+(M,max+g1)-integral+(M,max-g1))) 
         by P2,P1,EXTREAL1:8; then
   -integral+(M,max-(f1+g1))+ integral+(M,max+(f1+g1))+ integral+(M,max-f1)
    = R_EAL 0 + (integral+(M,max+f1) + (integral+(M,max+g1)
       -integral+(M,max-g1))) by EXTREAL1:9; then
   -integral+(M,max-(f1+g1)) + integral+(M,max+(f1+g1))+ integral+(M,max-f1)
    = integral+(M,max+f1) + (integral+(M,max+g1) -integral+(M,max-g1))
       by SUPINF_2:18; then
   -integral+(M,max-f1)+ integral+(M,max-f1) 
     +(-integral+(M,max-(f1+g1)) + integral+(M,max+(f1+g1)))
    = -integral+(M,max-f1)+(integral+(M,max+f1)
     +(integral+(M,max+g1)-integral+(M,max-g1))) by P1,P2,EXTREAL1:8; then
   -integral+(M,max-f1) + integral+(M,max-f1)
     + (-integral+(M,max-(f1+g1)) + integral+(M,max+(f1+g1)))
    = -integral+(M,max-f1)+ integral+(M,max+f1)+(integral+(M,max+g1)
     -integral+(M,max-g1)) by P1,P2,EXTREAL1:8; then
   R_EAL 0 + (-integral+(M,max-(f1+g1)) + integral+(M,max+(f1+g1)))
    = (-integral+(M,max-f1)+integral+(M,max+f1))+(integral+(M,max+g1)
     -integral+(M,max-g1)) by EXTREAL1:9; then
X7:Integral(M,(f1+g1))=Integral(M,f1)+Integral(M,g1) by SUPINF_2:18;
X9:Integral(M,f) =Integral(M,f1) by A0,A4,LM022i;
   Integral(M,g) =Integral(M,g1) by AS,A0,A1,A4,LM022i;
   hence thesis by A6,A5,AS,A0,A4,S1,S2,A14f,S4,X9,D14,
     A7,A13,A8,S3,Z12,A11,S5,X7;
end;

LM022z:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL  st
 (f is_integrable_on M & g is_integrable_on M & dom f = dom g) holds
   ( f+g is_integrable_on M & Integral(M,f+g) =Integral(M,f)+Integral(M,g) )
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL;
   assume       
A0: f is_integrable_on M & g is_integrable_on M & dom f = dom g;
   hence
R1:f+g is_integrable_on M by LM022y;
R2:ex E,NFG,NFPG be Element of S st
    E c= dom f & NFG c= dom f &
    E = (dom f) \ NFG &
    f|E is_finite & E = dom(f|E) & f|E is_measurable_on E &
    f|E is_integrable_on M &
    Integral(M,f)=Integral(M,f|E) &
    E c= dom g & NFG c= dom g & E = dom g \ NFG &
    g|E is_finite & E = dom(g|E) & g|E is_measurable_on E &
    g|E is_integrable_on M &
    Integral(M,g)=Integral(M,g|E) &
    E c= dom(f+g) & NFPG c= dom(f+g) & E = dom(f+g) \ NFPG &
    M.NFG = 0 &M.NFPG = 0 &
    E = dom((f+g)|E) & (f+g)|E is_measurable_on E &
    (f+g)|E is_integrable_on M & (f+g)|E = f|E + g|E &
    Integral(M,(f+g)|E)=Integral(M,f|E)+Integral(M,g|E) by A0,LM022z1;
   ex K0 be Element of S st
    K0 = dom(f+g) & f+g is_measurable_on K0 by R1,Defintegrable;
   hence thesis by LM022i,R2;
end;

theorem
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
 f is_integrable_on M & g is_integrable_on M holds
  ex E be Element of S st 
    E = dom f /\ dom g & Integral(M,f+g)=Integral(M,f|E)+Integral(M,g|E)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL;
   assume
AS:f is_integrable_on M & g is_integrable_on M; then
   consider A be Element of S such that
A1: A = dom f & f is_measurable_on A by Defintegrable;
   consider B be Element of S such that
A2: B = dom g & g is_measurable_on B by AS,Defintegrable;
   set E = A /\ B;
   set f1 = f|E;
   set g1 = g|E;
A3:f1 is_integrable_on M & g1 is_integrable_on M by AS,LM022g;
A4:dom f1 = dom f /\ (A/\B) by FUNCT_1:68
         .= A /\ A /\ B by A1,XBOOLE_1:16;
A5:dom g1 = dom g /\ (A/\B) by FUNCT_1:68
         .= B /\ B /\ A by A2,XBOOLE_1:16; then
A6:Integral(M,f1+g1) = Integral(M,f1) + Integral(M,g1) by A3,A4,LM022z;
   take E = A /\ B;
   thus E = dom f /\ dom g by A1,A2;
A7:f1"{-infty} = E /\ (f"{-infty}) &
   g1"{-infty} = E /\ (g"{-infty}) &
   f1"{+infty} = E /\ (f"{+infty}) &
   g1"{+infty} = E /\ (g"{+infty}) by FUNCT_1:139; then
A8:f1"{-infty} /\ g1"{+infty}
     = f"{-infty} /\ E /\ E /\ g"{+infty} by XBOOLE_1:16
    .= f"{-infty} /\ (E /\ E) /\ g"{+infty} by XBOOLE_1:16
    .= E /\ (f"{-infty} /\ g"{+infty}) by XBOOLE_1:16;
   f1"{+infty} /\ g1"{-infty}
     = f"{+infty} /\ E /\ E /\ g"{-infty} by A7,XBOOLE_1:16
    .= f"{+infty} /\ (E /\ E) /\ g"{-infty} by XBOOLE_1:16
    .= E /\ (f"{+infty} /\ g"{-infty}) by XBOOLE_1:16; then
A9:f1"{-infty}/\g1"{+infty} \/ f1"{+infty}/\g1"{-infty}
     = E /\ (f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty})
       by A8,XBOOLE_1:23;
B1:dom(f1+g1) = (dom f1 /\ dom g1)
        \ (f1"{-infty}/\g1"{+infty} \/ f1"{+infty}/\g1"{-infty})
             by MESFUNC1:def 3
   .= E \ (f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty})
         by A4,A5,A9,XBOOLE_1:47
   .= dom(f+g) by A1,A2,MESFUNC1:def 3;
   for x be set st x in dom(f1+g1) holds (f1+g1).x = (f+g).x
   proof
    let x be set;
    assume C1:x in dom(f1+g1); then
    x in (dom f1 /\ dom g1)
        \ (f1"{-infty} /\ g1"{+infty} \/ f1"{+infty} /\g1"{-infty})
          by MESFUNC1:def 3; then
    x in dom f1 /\ dom g1 by XBOOLE_0:def 4; then
 C2:x in dom f1 & x in dom g1 by XBOOLE_0:def 3;
    (f1+g1).x = f1.x + g1.x by C1,MESFUNC1:def 3
             .= f.x + g1.x by C2,FUNCT_1:70
             .= f.x + g.x by C2,FUNCT_1:70;
    hence thesis by C1,B1,MESFUNC1:def 3;
   end;
   hence thesis by A6,B1,FUNCT_1:9;
end;

theorem LM024:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    c be Real st 
 f is_integrable_on M holds
  c(#)f is_integrable_on M &
  Integral(M,c(#)f) = R_EAL c * Integral(M,f)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       c be Real such that
AS:f is_integrable_on M;
   consider A be Element of S such that
P1: A = dom f & f is_measurable_on A by AS,Defintegrable;
R4:c(#)f is_measurable_on A & dom(c(#)f) =A by MESFUNC1:def 6,LM0252,P1;
O1:0 <= integral+(M,max+f) & integral+(M,max+f) <>+infty &
   0 <= integral+(M,max-f) & integral+(M,max-f) <>+infty
     by Defintegrable,LM022a,AS; then
   reconsider I1 = integral+(M,max+f) as Real by EXTREAL1:1;
   reconsider I2 = integral+(M,max-f) as Real by O1,EXTREAL1:1;
R7:max+f is nonnegative & max-f is nonnegative by MAX;
R8:dom(max+f) = A & dom(max-f) = A by MESFUNC2:def 2,def 3,P1;
R10:max+f is_measurable_on A & max-f is_measurable_on A by P1,MESFUNC2:27,28;
   per cases;
   suppose CA1:0 <= c; then
R1: max+(c(#)f)=c(#)max+f & max-(c(#)f)=c(#)max-f by LM020;
X0: integral+(M,c(#)max+f) = R_EAL c * integral+(M,max+f) &
    integral+(M,c(#)max-f) = R_EAL c * integral+(M,max-f)
      by CA1,R8,R7,R10,LM018i;
    c*I1 is Real & c*I2 is Real; then
    R_EAL c * integral+(M,max+f) is Real &
    R_EAL c * integral+(M,max-f) is Real by EXTREAL1:13; then
    integral+(M,max+(c(#)f)) < +infty &
    R_EAL c * integral+(M,max-f) < +infty by R1,X0,SUPINF_1:31;
    hence c(#)f is_integrable_on M by R4,X0,R1,Defintegrable;
    thus Integral(M,c(#)f)
       =integral+(M,c(#)max+f) -integral+(M,max-(c(#)f)) by CA1,LM020
      .=integral+(M,c(#)max+f) -integral+(M,c(#)max-f) by CA1,LM020
      .=R_EAL c * integral+(M,max+f) - integral+(M,c(#)max-f)
         by LM018i,R7,R8,R10,CA1
      .=R_EAL c * integral+(M,max+f) - R_EAL c *integral+(M,max-f)
         by LM018i,R7,R8,R10,CA1
      .=R_EAL c * Integral(M,f) by O1,EXTREAL1:30;
   end;
   suppose c < 0; then
PP: -c > 0 by XREAL_1:60;
    -(-c)=c; then
    consider a be Real such that
R0:  c =-a & a > 0 by PP;
R1: max+(c(#)f)=a(#)max-f & max-(c(#)f)=a(#)max+f by R0,LM021;
X0: integral+(M,a(#)max-f) = R_EAL a * integral+(M,max-f) &
    integral+(M,a(#)max+f) = R_EAL a * integral+(M,max+f)
      by R0,R8,R7,R10,LM018i;
    a*I2 is Real & a*I1 is Real; then
    R_EAL a *integral+(M,max-f) is Real &
    R_EAL(a)*integral+(M,max+f) is Real by EXTREAL1:13; then
    integral+(M,max+(c(#)f)) < +infty &
    R_EAL(a)*integral+(M,max+f) < +infty by R1,X0,SUPINF_1:31;
    hence c(#)f is_integrable_on M by R4,R1,X0,Defintegrable;
RS: -R_EAL a = -a by SUPINF_2:3;
    thus Integral(M,c(#)f)
       =integral+(M,a(#)max-f) -integral+(M,max-(c(#)f)) by R0,LM021
      .=R_EAL a * integral+(M,max-f) -integral+(M,a(#)max+f)
         by LM018i,R7,R8,R10,R0,R1
      .=R_EAL a * integral+(M,max-f)-R_EAL a * integral+(M,max+f)
         by LM018i,R7,R8,R10,R0
      .=R_EAL a * (integral+(M,max-f)-integral+(M,max+f)) by O1,EXTREAL1:30
      .=R_EAL a * (-(integral+(M,max+f)-integral+(M,max-f))) by EXTREAL2:15
      .=-(R_EAL a * (integral+(M,max+f)-integral+(M,max-f))) by EXTREAL1:26
      .= R_EAL c * Integral(M,f) by RS,R0,EXTREAL1:26;
   end;
end;

definition
let X be non empty set;
let S be SigmaField of X;
let M be sigma_Measure of S;
let f be PartFunc of X,ExtREAL;
let B be Element of S;
func Integral_on(M,B,f) -> Element of ExtREAL equals
 Integral(M,f|B);
coherence;
end;

theorem
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL,
    B be Element of S st
 f is_integrable_on M & g is_integrable_on M & B c= dom(f+g) 
holds
   f+g is_integrable_on M &
   Integral_on(M,B,f+g) = Integral_on(M,B,f) + Integral_on(M,B,g)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL,
       B be Element of S such that
A1:f is_integrable_on M & g is_integrable_on M & B c= dom(f+g);
   dom(f+g) = (dom f /\ dom g) \
    (f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty}) by MESFUNC1:def 3;then
   dom(f+g) c= dom f /\ dom g & 
   dom f /\ dom g c= dom f & dom f /\ dom g c= dom g by XBOOLE_1:17,36; then
   dom(f+g) c= dom f & dom(f+g) c= dom g by XBOOLE_1:1; then
A3:B c= dom f & B c= dom g by A1,XBOOLE_1:1;
   dom(f|B) = dom f /\ B & dom(g|B) = dom g /\ B by FUNCT_1:68; then
P5:f|B is_integrable_on M & dom(f|B) = B &
   g|B is_integrable_on M & dom(g|B) = B by A3,LM022g,A1,XBOOLE_1:28;
   Integral_on(M,B,f+g) =Integral(M,f|B+g|B) by A1,LM022w;
   hence thesis by A1,LM022y,P5,LM022z;
end;

theorem
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    c be Real,
    B be Element of S st
 f is_integrable_on M & f is_measurable_on B holds
  f|B is_integrable_on M &
  Integral_on(M,B,c(#)f) = R_EAL c * Integral_on(M,B,f)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       c be Real,
       B be Element of S such that
A1:f is_integrable_on M & f is_measurable_on B;
A2:ex A be Element of S st
    A = dom f & f is_measurable_on A by A1,Defintegrable;
   reconsider BF=dom f /\ B as Element of S by A2,FINSUB_1:def 2;
A31D: f|B is_integrable_on M by LM022g,A1;
   dom((c(#)f)|B) = dom(c(#)f) /\ B by FUNCT_1:68; then
   dom((c(#)f)|B) = dom f /\ B by MESFUNC1:def 6; then
   dom((c(#)f)|B) = dom(f|B) by FUNCT_1:68; then
P1:dom((c(#)f)|B) = dom(c(#)(f|B)) by MESFUNC1:def 6;
   for x be set st x in dom((c(#)f)|B) holds (c(#)f)|B.x = (c(#)(f|B)).x
   proof
    let x be set;
    assume
C1: x in dom ((c(#)f)|B); then
    x in dom (c(#)f) /\ B by FUNCT_1:68; then
C5: x in dom f /\ B & x in dom (c(#)f) by XBOOLE_0:def 3,MESFUNC1:def 6; then
C4: x in dom (f|B) by FUNCT_1:68; then
C6: x in dom (c(#)(f|B)) by MESFUNC1:def 6;
    (c(#)f)|B.x= (c(#)f).x by C1,FUNCT_1:70; then
    (c(#)f)|B.x= R_EAL c * f.x by C5,MESFUNC1:def 6; then
    (c(#)f)|B.x= R_EAL c * f|B.x by C4,FUNCT_1:70;
    hence (c(#)f)|B.x= (c(#)(f|B)).x by C6,MESFUNC1:def 6;
   end; then
   (c(#)f)|B = c(#)(f|B) by P1,FUNCT_1:9;
   hence thesis by A31D,LM024;
end;
