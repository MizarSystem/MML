:: Integral of Measurable Function
::  by Noboru Endou and Yasunari Shidama
::
:: Received May 24, 2006
:: Copyright (c) 2006 Association of Mizar Users

environ

 vocabularies PARTFUN1, SUPINF_1, MEASURE1, FINSEQ_1, RELAT_1, FUNCT_1, RAT_1,
      ORDINAL2, COMPLEX1, INT_1, SEQ_1, SEQ_2, NAT_1, MEASURE6, BOOLE, ARYTM,
      ARYTM_1, MESFUNC1, TARSKI, ARYTM_3, SQUARE_1, RLVECT_1, GROUP_1, PROB_1,
      PROB_2, INTEGRA1, MESFUNC2, MESFUNC3, SEQFUNC, TDGROUP, MESFUNC5,
      SUPINF_2, FINSET_1, MEMBERED, SETLIM_1, FUNCT_3, RFUNCT_3;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0,
      XXREAL_0, XREAL_0, REAL_1, BINARITH, RELAT_1, FUNCT_1, FINSEQ_1,
      RELSET_1, FINSET_1, PARTFUN1, FUNCT_2, INT_1, NAT_1, NAT_D, RAT_1,
      MEMBERED, SQUARE_1, SUPINF_1, SUPINF_2, MEASURE1, EXTREAL1, EXTREAL2,
      MESFUNC1, MESFUNC2, MESFUNC3, FINSEQOP, CONVFUN1, RFUNCT_3, PRE_CIRC,
      ABSVALUE, PROB_1, PROB_2, MEASURE6, NEWTON, LIMFUNC1, SEQFUNC, SETLIM_1;
 constructors WELLORD2, REAL_1, SQUARE_1, NAT_1, NAT_D, RAT_1, FINSEQOP,
      LIMFUNC1, SEQFUNC, NEWTON, RFUNCT_3, MEASURE6, EXTREAL1, MESFUNC1,
      EXTREAL2, BINARITH, PRE_CIRC, MESFUNC2, CONVFUN1, KURATO_2, MESFUNC3,
      SETLIM_1, VALUED_1, PARTFUN1, SEQ_1;
 registrations XBOOLE_0, SUBSET_1, ORDINAL1, RELSET_1, PARTFUN1, FINSET_1,
      NUMBERS, XXREAL_0, XREAL_0, NAT_1, INT_1, RAT_1, MEMBERED, COMPLEX1,
      FINSEQ_1, SEQ_1, MEASURE1, PRE_CIRC, VALUED_0, VALUED_1, FUNCT_2;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, XBOOLE_0, SUPINF_2, FINSEQ_1, CARD_3, MEASURE6, VALUED_1;
 theorems MEASURE1, MEASURE2, TARSKI, SUPINF_1, PARTFUN1, FUNCT_1, FUNCT_2,
      SEQFUNC, MEASURE6, NAT_1, REAL_1, SUPINF_2, RELSET_1, EXTREAL1, INT_1,
      EXTREAL2, MESFUNC1, FINSEQ_1, XREAL_0, PROB_1, PROB_2, XBOOLE_0, NAT_2,
      XBOOLE_1, XCMPLX_1, MESFUNC2, CONVFUN1, MESFUNC3, NEWTON, XREAL_1,
      COMPLEX1, MEASURE5, PREPOWER, TOPREALA, XXREAL_0, MESFUNC4, ZFMISC_1,
      PRE_CIRC, KURATO_2, SETLIM_1, BINARITH, FINSEQ_3, REAL_2, RAT_1, HAHNBAN,
      FUNCT_3, RELAT_1, FINSUB_1, GRFUNC_1, FINSEQ_2, ORDINAL1, NAT_D, NUMBERS;
 schemes FUNCT_1, FUNCT_2, FINSEQ_1, FINSEQ_2, NAT_1, SEQFUNC, PARTFUN1,
      KURATO_2;

begin :: Lemmas for extended real numbers

theorem Th1:
  for x,y be R_eal holds |.x-y.| = |.y-x.|
proof
   let x,y be R_eal;
    |.y-x.| = |.-(x-y).| by EXTREAL2:15;
   hence thesis by EXTREAL2:66;
end;

theorem Th2:
  for x,y be R_eal holds y-x <= |.x-y.|
proof
   let x,y be R_eal;
    x-y <= |.x-y.| & -|.x-y.| <= (x-y) by EXTREAL2:57;
   then x-y <= |.x-y.| & -(x-y) <= |.x-y.| by EXTREAL2:47;
   hence y-x <= |.x-y.| by EXTREAL2:15;
end;

theorem Th3:
for x,y be R_eal, e be real number st |.x-y.| < e &
  not ((x = +infty & y = +infty) or (x = -infty & y = -infty))
  holds x <> +infty & x <> -infty & y <> +infty & y <>-infty
proof
   let x,y be R_eal, e be real number;
   assume A1:|.x-y.| < e;
    x-y <= |.x-y.| & y-x <= |.x-y.| by Th2,EXTREAL2:57;
   then x-y < R_EAL e & y-x < e by A1,XXREAL_0:2;
   hence thesis by EXTREAL2:29;
end;

theorem Th4:
for x,y be R_eal st
 (for e be real number st 0 < e holds x < y + R_EAL e) holds x <= y
proof
   let x,y be R_eal;
   assume
A1: for e be real number st 0 < e holds x < y + R_EAL e;
   per cases;
   suppose
A2: y = +infty or y=-infty;
    per cases by A2;
    suppose y=+infty;
     hence x<= y by XXREAL_0:4;
    end;
    suppose
A3:  y=-infty;
      x < y+ R_EAL(1) by A1;
     hence x <= y by A3,SUPINF_2:def 2;
    end;
   end;
   suppose
A4: y <> +infty & y <>-infty;
    per cases;
    suppose
A5:  x = +infty;
      x < y+ R_EAL(1) by A1;
     hence x <= y by A5,XXREAL_0:4;
    end;
    suppose
A6:  x <> +infty;
      now assume
A7:   x <> -infty;
      assume
A8:    not x <= y;
       y <= +infty & -infty <= x by XXREAL_0:4,5;
      then y < +infty & -infty < x by A4,A7,XXREAL_0:1;
then A9:   0 < x- y by A8,EXTREAL1:41;
      reconsider x1=x as Real by A6,A7,XXREAL_0:14;
      reconsider y1=y as Real by A4,XXREAL_0:14;
A10:   x-y=x1-y1 by SUPINF_2:5;
      then x < y + R_EAL(x1-y1) by A1,A9;
      then x < y + x-y by A10,EXTREAL1:11;
      then x < x +(y-y) by A4,EXTREAL1:11;
      then x < x +0. by EXTREAL1:9;
      hence contradiction by SUPINF_2:18;
     end;
     hence x <= y by XXREAL_0:5;
    end;
   end;
end;

theorem Th5:
for x,y,t being R_eal holds
  t <> -infty & t <> +infty & x < y implies x + t < y + t
proof
   let x,y,t be R_eal;
   assume
A1: t <> -infty & t <> +infty & x < y;
then A2:x + t <= y + t by SUPINF_2:14;
A3:t-t = 0 by EXTREAL1:9;
    now assume x + t = y + t;
    then x + (t -t) = y + t-t by A1,EXTREAL1:11;
    then x+ R_EAL 0 = y + (t-t) by A1,A3,EXTREAL1:11;
    then x=y+ R_EAL 0 by A3,SUPINF_2:18;
    hence contradiction by A1,SUPINF_2:18;
   end;
   hence thesis by A2,XXREAL_0:1;
end;

theorem
 for x,y,t being R_eal holds
  t <> -infty & t <> +infty & x < y implies x - t < y - t
proof
   let x,y,t be R_eal;
   assume
A1: t <> -infty & t <> +infty & x < y;
then A2:x - t <= y - t by SUPINF_2:15;
A3:t-t = 0 by EXTREAL1:9;
    now assume x - t = y - t;
    then x - (t -t) = y - t+t by A1,EXTREAL2:41;
    then x- R_EAL 0 = y - (t-t) by A1,A3,EXTREAL2:41;
    then x=y+ R_EAL 0 by A3,EXTREAL1:24,SUPINF_2:18;
    hence contradiction by A1,SUPINF_2:18;
   end;
   hence thesis by A2,XXREAL_0:1;
end;

theorem Th7:
for a,b be real number holds
R_EAL a + R_EAL b = a+b & -R_EAL a = -a
proof
   let a,b be real number;
   reconsider a'=a,b'=b,c'=a+b as Real by XREAL_0:def 1;
    a' = R_EAL a & b' = R_EAL b & c' = R_EAL(a+b);
   hence R_EAL a + R_EAL b = a+b by SUPINF_2:1;
    -a' = R_EAL(-a);
   hence -R_EAL a = -a by SUPINF_2:3;
end;

theorem Th8:
for n be Nat, p be R_eal st 0 <= p & p < n holds
 ex k be Nat st 1 <= k & k <= 2|^n*n & (k-1)/(2|^n) <= p & p < k/(2|^n)
proof
   let n be Nat;
   let p be R_eal;
   assume
A1: 0 <= p & p < n;
   then p < R_EAL n;
   then reconsider p1 = p as Real by A1,MEASURE5:16,SUPINF_2:19;
   reconsider n as Element of NAT by ORDINAL1:def 13;
   set k = [\ p1*(2|^n)+1 /];
A2:0 <= p1 & 0 < 2|^n by A1,PREPOWER:13;
A3:0 <= p1*(2|^n) by A1,XREAL_1:129;
A4:p1*(2|^n) + 1 - 1 = p1*(2|^n);
then A5:0 < k by A3,INT_1:def 4;
   then reconsider k as Element of NAT by INT_1:16;
A6:0+1 <= k by A5,NAT_1:13;
A7:now assume
A8: k > 2|^n * n;
     k <= p1*(2|^n)+1 by INT_1:def 4;
then A9: 2|^n * n < p1*(2|^n)+1 by A8,XXREAL_0:2;
     p1*(2|^n) < 2|^n * n by A1,A2,XREAL_1:70;
then A10: p1*(2|^n) +1 < 2|^n * n +1 by XREAL_1:8;
    reconsider N = 2|^n * n as Integer;
     [\ N /] = N by INT_1:47;
    hence contradiction by A8,A9,A10,TOPREALA:2;
   end;
   take k;
    p1*(2|^n) < k & k <= p1*(2|^n)+1 by A4,INT_1:def 4;
   then p1 < k/(2|^n) & k-1 <= p1*(2|^n) by A2,XREAL_1:22,83;
   hence thesis by A2,A6,A7,XREAL_1:81;
end;

theorem Th9:
for n,k be Nat, p be R_eal st
 1 <= k & k <= 2|^n*n & n <= p & (k-1)/(2|^n) <= p
  holds k/(2|^n) <= p
proof
   let n,k be Nat;
   let p be R_eal;
   assume
A1:1 <= k & k <= 2|^n*n & n <= p & (k-1)/(2|^n) <= p;
A2:0 < 2|^n by PREPOWER:13;
   assume p < k/(2|^n);
    then n < k/(2|^n) by A1,XXREAL_0:2;
   hence contradiction by A1,A2,XREAL_1:81;
end;

theorem Th10:
for x,y,w,z being R_eal st -infty < w holds
  x < y & w < z implies x + w < y + z
proof
   let x,y,w,z be R_eal;
   assume
A1: -infty < w;
   assume
A2: x < y & w <z;
   per cases;
   suppose
A3: y = +infty;
then A4: y + z = +infty by A1,A2,SUPINF_2:def 2;
     z <= +infty by XXREAL_0:4;
    then x + w <> +infty by A2,A3,SUPINF_2:8;
    hence thesis by A4,XXREAL_0:4;
   end;
   suppose
A5: y <> +infty;
      z <= +infty by XXREAL_0:4;
     then w in REAL by A1,A2,XXREAL_0:14;
then A6:  x + w < y + w by A2,MEASURE6:12;
      -infty <= x by XXREAL_0:5;
     then y in REAL by A2,A5,XXREAL_0:14;
     then y + w < y + z by A2,MEASURE6:12;
    hence thesis by A6,XXREAL_0:2;
   end;
end;

theorem Th11:
for x,y,k being R_eal st 0 <= k holds
 k*max(x,y) = max(k*x,k*y) & k*min(x,y) = min(k*x,k*y)
proof
   let x,y,k be R_eal;
   assume
A1: 0 <= k;
    now
   per cases by XXREAL_0:16;
    suppose
A2:  max(x,y) = x;
     then y <= x by XXREAL_0:def 9;
     then k*y <= k*x by A1,EXTREAL1:42;
     hence k*max(x,y) = max(k*x,k*y) by A2,XXREAL_0:def 9;
    end;
    suppose
A3:  max(x,y) = y;
     then x <= y by XXREAL_0:def 9;
     then k*x <= k*y by A1,EXTREAL1:42;
     hence k*max(x,y) = max(k*x,k*y) by A3,XXREAL_0:def 9;
    end;
   end;
   hence k*max(x,y) = max(k*x,k*y);
   per cases by XXREAL_0:15;
   suppose
A4: min(x,y) = x;
    then x <= y by XXREAL_0:def 8;
    then k*x <= k*y by A1,EXTREAL1:42;
    hence k*min(x,y) = min(k*x,k*y) by A4,XXREAL_0:def 8;
   end;
   suppose
A5: min(x,y) = y;
    then y <= x by XXREAL_0:def 8;
    then k*y <= k*x by A1,EXTREAL1:42;
    hence k*min(x,y) = min(k*x,k*y) by A5,XXREAL_0:def 8;
   end;
end;

theorem
 for x,y,k being R_eal st k <= 0 holds
 k*min(x,y) = max(k*x,k*y) & k*max(x,y) = min(k*x,k*y)
proof
   let x,y,k be R_eal;
   assume
A1: k <= 0;
   hereby per cases by XXREAL_0:16;
    suppose max(x,y) = x;
then A2:  y <= x by XXREAL_0:def 9;
     then k*x <= k*y by A1,EXTREAL1:43;
     then max(k*x,k*y) = k*y by XXREAL_0:def 9;
     hence k*min(x,y) = max(k*x,k*y) by A2,XXREAL_0:def 8;
    end;
    suppose max(x,y) = y;
then A3:  x <= y by XXREAL_0:def 9;
     then k*y <= k*x by A1,EXTREAL1:43;
     then max(k*x,k*y) = k*x by XXREAL_0:def 9;
     hence k*min(x,y) = max(k*x,k*y) by A3,XXREAL_0:def 8;
    end;
   end;
   per cases by XXREAL_0:15;
   suppose min(x,y) = x;
then A4: x <= y by XXREAL_0:def 8;
    then k*y <= k*x by A1,EXTREAL1:43;
    then min(k*x,k*y) = k*y by XXREAL_0:def 8;
    hence k*max(x,y) = min(k*x,k*y) by A4,XXREAL_0:def 9;
   end;
   suppose min(x,y) = y;
then A5: y <= x by XXREAL_0:def 8;
    then k*x <= k*y by A1,EXTREAL1:43;
    then min(k*y,k*x) = k*x by XXREAL_0:def 8;
    hence k*max(x,y) = min(k*x,k*y) by A5,XXREAL_0:def 9;
   end;
end;

theorem Th13:
  for x,y,z being R_eal holds
    0 <= x & 0 <= z & z + x <= y implies z <= y
proof
   let x,y,z be R_eal;
   assume
A1:0 <= x & 0 <= z & z + x <= y;
   per cases by A1,SUPINF_2:2;
   suppose A2:x in REAL;
then A3: x < +infty & -infty < x by XXREAL_0:9,12;
     (z + x) - x = z & y - 0. = y by A2,MEASURE6:8,21;
    hence thesis by A1,A3,SUPINF_2:15;
   end;
   suppose x = +infty;
    then z+x = +infty by A1,SUPINF_2:def 2;
    then +infty = y by A1,XXREAL_0:4;
    hence thesis by XXREAL_0:4;
   end;
end;

begin :: Lemmas for partial function of non empty set,extended real numbers

definition let IT be set;
  attr IT is nonpositive means :Def1:
  for x being R_eal holds x in IT implies x <= 0;
end;

definition let R be Relation;
  attr R is nonpositive means :Def2:
  rng R is nonpositive;
end;

theorem Th14:
for X being set, F being PartFunc of X,ExtREAL holds
   F is nonpositive iff for n being set holds F.n <= 0.
proof
   let X be set,
       F be PartFunc of X,ExtREAL;
   hereby assume F is nonpositive;
then A1: rng F is nonpositive by Def2;
    let n be set;
    per cases;
    suppose n in dom F;
     then F.n in rng F by FUNCT_1:def 5;
     hence F.n <= 0. by A1,Def1;
    end;
    suppose not n in dom F;
     hence F.n <= 0. by FUNCT_1:def 4;
    end;
   end;
   assume
A2:for n being set holds F.n <= 0.;
   let y be R_eal;
   assume y in rng F;
   then ex x being set st x in dom F & y = F.x by FUNCT_1:def 5;
   hence thesis by A2;
  end;

theorem Th15:
for X being set, F being PartFunc of X,ExtREAL st
  for n being set st n in dom F holds F.n <= 0. holds F is nonpositive
proof
   let X be set,
       F be PartFunc of X,ExtREAL such that
A1:for n being set st n in dom F holds F.n <= 0.;
   let y be R_eal;
   assume y in rng F;
   then consider x being set such that
A2: x in dom F & y = F.x by FUNCT_1:def 5;
   thus thesis by A1,A2;
end;

definition let R be Relation;
  attr R is without-infty means :Def3:
  not -infty in rng R;

  attr R is without+infty means :Def4:
  not +infty in rng R;
end;

definition
  let X be non empty set, f be PartFunc of X,ExtREAL;
  redefine attr f is without-infty means :Def5:
  for x being set holds -infty < f.x;
  compatibility
proof
   hereby assume f is without-infty;
then A1: not -infty in rng f by Def3;
    hereby let x be set;
     per cases;
     suppose x in dom f;
      then f.x <> -infty by A1,FUNCT_1:def 5;
      hence -infty < f.x by XXREAL_0:6;
     end;
     suppose not x in dom f;
      then 0 = f.x by FUNCT_1:def 4;
      hence -infty < f.x;
     end;
    end;
   end;
   assume
A2: for x being set holds -infty < f.x;
    now assume -infty in rng f;
    then consider x be set such that
A3:  x in dom f & f.x = -infty by FUNCT_1:def 5;
    thus contradiction by A2,A3;
   end;
   hence f is without-infty by Def3;
end;

  redefine attr f is without+infty means :Def6:
  for x being set holds f.x < +infty;
  compatibility
proof
   hereby assume f is without+infty;
then A4: not +infty in rng f by Def4;
    hereby let x be set;
     per cases;
     suppose x in dom f;
      then f.x <> +infty by A4,FUNCT_1:def 5;
      hence f.x < +infty by XXREAL_0:4;
     end;
     suppose not x in dom f;
      then 0 = f.x by FUNCT_1:def 4;
      hence f.x < +infty;
     end;
    end;
   end;
   assume
A5: for x being set holds f.x < +infty;
    now assume +infty in rng f;
    then consider x be set such that
A6:  x in dom f & f.x = +infty by FUNCT_1:def 5;
    thus contradiction by A5,A6;
   end;
   hence f is without+infty by Def4;
end;
end;

theorem Th16:
for X be non empty set, f be PartFunc of X,ExtREAL holds
 (for x be set st x in dom f holds -infty < f.x) iff f is without-infty
proof
   let X be non empty set,
       f be PartFunc of X,ExtREAL;
   hereby assume A1:for x be set st x in dom f holds -infty < f.x;
     now let x be set;
     per cases;
     suppose x in dom f;
      hence -infty < f.x by A1;
     end;
     suppose not x in dom f;
      then f.x = 0 by FUNCT_1:def 4;
      hence -infty < f.x;
     end;
    end;
    hence f is without-infty by Def5;
   end;
   assume f is without-infty;
   hence thesis by Def5;
end;

theorem Th17:
for X be non empty set, f be PartFunc of X,ExtREAL holds
 (for x be set st x in dom f holds f.x < +infty) iff f is without+infty
proof
   let X be non empty set,
       f be PartFunc of X,ExtREAL;
   hereby assume A1:for x be set st x in dom f holds f.x < +infty;
     now let x be set;
     per cases;
     suppose x in dom f;
      hence f.x < +infty by A1;
     end;
     suppose not x in dom f;
      then f.x = 0 by FUNCT_1:def 4;
      hence f.x < +infty;
     end;
    end;
    hence f is without+infty by Def6;
   end;
   assume f is without+infty;
   hence thesis by Def6;
end;

theorem Th18:
for X be non empty set, f be PartFunc of X,ExtREAL st
 f is nonnegative holds f is without-infty
proof
   let X be non empty set, f be PartFunc of X,ExtREAL;
   assume f is nonnegative;
   then for x be set holds -infty < f.x by SUPINF_2:70;
   hence f is without-infty by Def5;
end;

theorem Th19:
for X be non empty set, f be PartFunc of X,ExtREAL st
 f is nonpositive holds f is without+infty
proof
   let X be non empty set, f be PartFunc of X,ExtREAL;
   assume f is nonpositive;
   then for x be set holds f.x < +infty by Th14;
   hence f is without+infty by Def6;
end;

registration let X be non empty set;
  cluster nonnegative -> without-infty PartFunc of X,ExtREAL;
  coherence by Th18;
  cluster nonpositive -> without+infty PartFunc of X,ExtREAL;
  coherence by Th19;
end;

theorem Th20:
for X be non empty set,
    S be SigmaField of X,
    f be PartFunc of X,ExtREAL st
  f is_simple_func_in S holds f is without+infty & f is without-infty
proof
   let X be non empty set,
       S be SigmaField of X,
       f be PartFunc of X,ExtREAL;
   assume A1:f is_simple_func_in S;
   hereby assume not f is without+infty;
    then +infty in rng f by Def4;
    then f"{+infty} <> {} by FUNCT_1:142;
    then consider x be set such that
A2: x in f"{+infty} by XBOOLE_0:def 1;
A3:x in dom f & f.x in {+infty} by A2,FUNCT_1:def 13;
    then f.x = +infty by TARSKI:def 1;
then A4:|. f.x .| = +infty by EXTREAL2:67;
     f is finite by A1,MESFUNC2:def 5;
    hence contradiction by A3,A4,MESFUNC2:def 1;
   end;
   hereby assume not f is without-infty;
    then -infty in rng f by Def3;
    then f"{-infty} <> {} by FUNCT_1:142;
    then consider x be set such that
A5: x in f"{-infty} by XBOOLE_0:def 1;
A6:x in dom f & f.x in {-infty} by A5,FUNCT_1:def 13;
    then f.x = -infty by TARSKI:def 1;
then A7:|. f.x .| = +infty by EXTREAL2:67;
     f is finite by A1,MESFUNC2:def 5;
    hence contradiction by A6,A7,MESFUNC2:def 1;
   end;
end;

theorem Th21:
for X be non empty set, Y be set, f be PartFunc of X,ExtREAL st
 f is nonnegative holds f|Y is nonnegative
proof
   let X be non empty set,
       Y be set,
       f be PartFunc of X,ExtREAL;
   assume
A1: f is nonnegative;
    now let x be set;
    assume A2:x in dom(f|Y);
    then (f|Y).x = f.x by FUNCT_1:70;
    hence 0 <= (f|Y).x by A1,A2,SUPINF_2:58;
   end;
   hence f|Y is nonnegative by SUPINF_2:71;
end;

theorem Th22:
for X be non empty set, f,g be PartFunc of X,ExtREAL st
 f is without-infty & g is without-infty holds
   dom(f+g)=dom f /\ dom g
proof
   let X be non empty set;
   let f,g be PartFunc of X,ExtREAL;
   assume f is without-infty & g is without-infty;
   then not -infty in rng f & not -infty in rng g by Def3;
   then f"{-infty} = {} & g"{-infty} = {} by FUNCT_1:142;
   then f"{+infty} /\ g"{-infty} \/ f"{-infty} /\ g"{+infty} = {};
   then dom(f+g) = (dom f /\ dom g)\{} by MESFUNC1:def 3;
   hence dom(f+g)=dom f /\ dom g;
end;

theorem
 for X be non empty set, f,g be PartFunc of X,ExtREAL st
 f is without-infty & g is without+infty holds
   dom(f-g)=dom f /\ dom g
proof
   let X be non empty set;
   let f,g be PartFunc of X,ExtREAL;
   assume f is without-infty & g is without+infty;
   then not -infty in rng f & not +infty in rng g by Def3,Def4;
   then f"{-infty} = {} & g"{+infty} = {} by FUNCT_1:142;
   then f"{+infty} /\ g"{+infty} \/ f"{-infty} /\ g"{-infty} = {};
   then dom(f-g) = (dom f /\ dom g)\{} by MESFUNC1:def 4;
   hence thesis;
end;

theorem Th24:
for X be non empty set, S be SigmaField of X, f,g be PartFunc of X,ExtREAL,
    F be Function of RAT,S, r be Real, A be Element of S st
f is without-infty & g is without-infty &
(for p be Rational holds
  F.p = A /\ less_dom(f,R_EAL p) /\ (A /\ less_dom(g,R_EAL (r-p))))
holds A /\ less_dom(f+g,R_EAL r) = union rng F
proof
   let X be non empty set;
   let S be SigmaField of X;
   let f,g be PartFunc of X,ExtREAL;
   let F be Function of RAT,S;
   let r be Real;
   let A be Element of S;
   assume that
A1:f is without-infty and
A2:g is without-infty and
A3: for p be Rational holds
     F.p = A /\ less_dom(f,R_EAL p) /\ (A /\ less_dom(g,R_EAL(r-p)));
A4:dom(f+g) = dom f /\ dom g by A1,A2,Th22;
A5:A /\ less_dom(f+g,R_EAL r) c= union rng F
   proof
    let x be set;
    assume
A6: x in A /\ less_dom(f+g,R_EAL r);
then A7: x in A & x in less_dom(f+g,R_EAL r) by XBOOLE_0:def 3;
then A8: x in dom(f+g) & ex y being R_eal st y = (f+g).x & y < R_EAL r
      by MESFUNC1:def 12;
then A9: x in dom f & x in dom g by A4,XBOOLE_0:def 3;
    reconsider x as Element of X by A6;
A10: f.x + g.x < R_EAL r by A8,MESFUNC1:def 3;
A11:-infty < f.x & -infty < g.x by A1,A2,Def5;
then A12:f.x < R_EAL r - g.x & f.x <> +infty & g.x <> +infty by A10,EXTREAL2:27
;
    then reconsider f1 = f.x, g1 = g.x as Real by A11,XXREAL_0:14;
     R_EAL r - g.x = r - g1 by SUPINF_2:5;
    then consider p being Rational such that
A13: f1 < p & p < r - g1 by A12,RAT_1:22;
     not p <= f1 & not r - p <= g1 by A13,REAL_2:165;
    then x in less_dom(f,R_EAL p) & x in less_dom(g,R_EAL(r-p))
      by A9,MESFUNC1:def 12;
    then x in A /\ less_dom(f,R_EAL p) & x in A /\ less_dom(g,R_EAL(r-p))
      by A7,XBOOLE_0:def 3;
then A14:x in A /\ less_dom(f,R_EAL p)/\(A /\ less_dom(g,R_EAL(r-p)))
      by XBOOLE_0:def 3;
     p in RAT by RAT_1:def 2;
    then p in dom F by FUNCT_2:def 1;
    then x in F.p & F.p in rng F by A3,A14,FUNCT_1:def 5;
    hence thesis by TARSKI:def 4;
   end;
    union rng F c= A /\ less_dom(f+g,R_EAL r)
   proof
    let x be set;
    assume x in union rng F;
    then consider Y being set such that
A15: x in Y & Y in rng F by TARSKI:def 4;
    consider p being set such that
A16: p in dom F & Y = F.p by A15,FUNCT_1:def 5;
    reconsider p as Rational by A16,RAT_1:def 2;
A17: x in A /\ less_dom(f,R_EAL p)/\(A /\ less_dom(g,R_EAL (r-p)))
      by A3,A15,A16;
    then x in A /\ less_dom(f,R_EAL p) & x in A /\ less_dom(g,R_EAL (r-p))
      by XBOOLE_0:def 3;
then A18:x in A & x in less_dom(f,R_EAL p) & x in less_dom(g,R_EAL (r-p))
      by XBOOLE_0:def 3;
    reconsider x as Element of X by A17;
    consider y1 being R_eal such that
A19: y1 = f.x & y1 < R_EAL p by A18,MESFUNC1:def 12;
    consider y2 being R_eal such that
A20: y2 = g.x & y2 < R_EAL(r-p) by A18,MESFUNC1:def 12;
A21: -infty < y1 & -infty < y2 by A1,A2,A19,A20,Def5;
     y1 <> +infty & y2 <> +infty by A19,A20,XXREAL_0:4;
    then reconsider f1 = y1,g1 = y2 as Real by A21,XXREAL_0:14;
     f1 < p & p < r- g1 by A19,A20,REAL_2:165;
    then f1 < r - g1 by XXREAL_0:2;
then A22:f1 + g1 < r by XREAL_1:22;
     x in dom f & x in dom g by A18,MESFUNC1:def 12;
then A23:x in dom (f+g) by A4,XBOOLE_0:def 3;
    then (f+g).x = f.x + g.x by MESFUNC1:def 3
           .= f1+g1 by A19,A20,SUPINF_2:1;
    then x in less_dom(f+g,R_EAL r) by A22,A23,MESFUNC1:def 12;
    hence thesis by A18,XBOOLE_0:def 3;
   end;
   hence thesis by A5,XBOOLE_0:def 10;
end;

definition
  let X be non empty set;
  let f be PartFunc of X,REAL;
  func R_EAL f -> PartFunc of X,ExtREAL equals  f;
  coherence by NUMBERS:31,PARTFUN1:31;
end;

theorem Th25:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
 f is nonnegative & g is nonnegative holds f+g is nonnegative
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f,g be PartFunc of X,ExtREAL;
   assume
A1:f is nonnegative & g is nonnegative;
    for x be set st x in dom(f+g) holds 0 <= (f+g).x
   proof
    let x be set;
    assume
A2: x in dom(f+g);
A3: 0 <= f.x & 0 <= g.x by A1,SUPINF_2:70;
    then g.x <= f.x + g.x by SUPINF_2:20;
    hence 0 <= (f+g).x by A2,A3,MESFUNC1:def 3;
   end;
   hence f+g is nonnegative by SUPINF_2:71;
end;

theorem Th26:
for X be non empty set,
    f be PartFunc of X,ExtREAL,c be Real st f is nonnegative holds
  (0 <= c implies c(#)f is nonnegative) &
  (c <= 0 implies c(#)f is nonpositive)
proof
   let X be non empty set;
   let f be PartFunc of X,ExtREAL;
   let c be Real;
   assume
A1:f is nonnegative;
   hereby assume
A2: 0 <= c;
    set g = c(#)f;
     for x be set st x in dom g holds 0 <= g.x
    proof
     let x be set;
     assume
A3:  x in dom g;
      0 <= f.x by A1,SUPINF_2:70;
     then 0 <= (R_EAL c)*f.x by A2,EXTREAL2:45;
     hence 0 <= (c(#)f).x by A3,MESFUNC1:def 6;
    end;
    hence c(#)f is nonnegative by SUPINF_2:71;
   end;
   assume
A4: c <= 0;
   set g = c(#)f;
    now let x be set;
    assume
A5: x in dom g;
     0 <= f.x by A1,SUPINF_2:70;
    then (R_EAL c)*f.x <= 0 by A4,EXTREAL2:46;
    hence g.x <= 0 by A5,MESFUNC1:def 6;
   end;
   hence c(#)f is nonpositive by Th15;
end;

theorem Th27:
for X be non empty set, f,g be PartFunc of X,ExtREAL st
 (for x be set st x in dom f /\ dom g holds
   g.x <= f.x & -infty < g.x & f.x < +infty) holds
 f-g is nonnegative
proof
   let X be non empty set,
       f,g be PartFunc of X,ExtREAL;
   assume
A1:for x be set st x in dom f /\ dom g holds
     g.x <= f.x & -infty < g.x & f.x < +infty;
    now let x be set;
    assume A2:x in dom(f-g);
     dom(f-g)
     = (dom f /\ dom g)\(f"{+infty}/\g"{+infty} \/ f"{-infty}/\g"{-infty})
       by MESFUNC1:def 4;
    then dom(f-g) c= dom f /\ dom g by XBOOLE_1:36;
then A3: g.x <= f.x & -infty < g.x & f.x < +infty by A1,A2;
    then -infty < f.x & g.x < +infty by XXREAL_0:2;
    then 0 <= f.x - g.x by A3,EXTREAL1:40;
    hence 0 <= (f-g).x by A2,MESFUNC1:def 4;
   end;
   hence f-g is nonnegative by SUPINF_2:71;
end;

Lm1:
for X be non empty set, S be SigmaField of X, M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL holds
 max+f is nonnegative & max-f is nonnegative & |.f.| is nonnegative
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f be PartFunc of X,ExtREAL;
    ( for x be set st x in dom max+ f holds 0<= (max+f).x ) &
   ( for x be set st x in dom max- f holds 0<= (max-f).x ) by MESFUNC2:14,15;
   hence max+f is nonnegative & max-f is nonnegative by SUPINF_2:71;
    now let x be set;
    assume x in dom |.f.|;
    then (|.f.|).x =|. f.x .| by MESFUNC1:def 10;
    hence 0 <= (|.f.|).x by EXTREAL2:51;
   end;
   hence |.f.| is nonnegative by SUPINF_2:71;
end;

theorem Th28:
for X be non empty set,
    f,g be PartFunc of X,ExtREAL st
 f is nonnegative & g is nonnegative holds
  dom(f+g)=dom f /\ dom g & f+g is nonnegative
proof
   let X be non empty set,
       f,g be PartFunc of X,ExtREAL;
   assume
A1:  f is nonnegative & g is nonnegative;
   then (for x being set st x in dom f holds -infty < f.x) &
   for x being set st x in dom g holds -infty < g.x by SUPINF_2:70;
   then f is without-infty & g is without-infty by Th16;
   hence A2:dom(f+g)=dom f /\ dom g by Th22;
    now let x be set;
    assume A3: x in dom f /\ dom g;
     0 <= f.x & 0 <= g.x by A1,SUPINF_2:70;
    then 0 <= f.x +g.x by EXTREAL2:7;
    hence 0 <= (f+g).x by A2,A3,MESFUNC1:def 3;
   end;
   hence f+g is nonnegative by A2,SUPINF_2:71;
end;

theorem Th29:
for X be non empty set, f,g,h be PartFunc of X,ExtREAL st
 f is nonnegative & g is nonnegative & h is nonnegative holds
   dom(f+g+h) = dom f /\ dom g /\ dom h & f+g+h is nonnegative &
   for x be set st x in dom f /\ dom g /\ dom h holds (f+g+h).x=f.x+g.x+h.x
proof
   let X be non empty set;
   let f,g,h be PartFunc of X,ExtREAL;
   assume
A1: f is nonnegative & g is nonnegative & h is nonnegative;
then A2:f+g is nonnegative by Th28;
then A3:dom(f+g+h) = dom(f+g) /\ dom h by A1,Th28;
   hence dom(f+g+h) = dom f /\ dom g /\ dom h by A1,Th28;
   thus f+g+h is nonnegative by A1,A2,Th28;
   hereby let x be set;
    assume x in dom f /\ dom g /\ dom h;
then A4: x in dom(f+g) /\ dom h by A1,Th28;
then A5: x in dom(f+g) by XBOOLE_0:def 3;
    thus (f+g+h).x = (f+g).x + h.x by A3,A4,MESFUNC1:def 3
                  .= f.x + g.x + h.x by A5,MESFUNC1:def 3;
   end;
end;

theorem Th30:
for X be non empty set,
    f,g being PartFunc of X,ExtREAL st
 f is without-infty & g is without-infty holds
   dom(max+(f+g) + max- f) = dom f /\ dom g &
   dom(max-(f+g) + max+ f) = dom f /\ dom g &
   dom(max+(f+g) + max- f + max- g) = dom f /\ dom g &
   dom(max-(f+g) + max+ f + max+ g) = dom f /\ dom g &
   max+(f+g) + max-f is nonnegative & max-(f+g) + max+f is nonnegative
proof
   let X be non empty set;
   let f,g be PartFunc of X,ExtREAL;
   assume that
A1: f is without-infty and
A2: g is without-infty;
 dom(f+g) = dom f /\ dom g by A1,A2,Th22;
then A3:dom(max+(f+g)) = dom f /\ dom g &
   dom(max-(f+g)) = dom f /\ dom g by MESFUNC2:def 2,def 3;
    for x be set holds (x in dom max+(f+g) implies -infty < (max+(f+g)).x) &
   (x in dom max-(f+g) implies -infty < (max-(f+g)).x)
     by MESFUNC2:14,15;
then A4:max+(f+g) is without-infty & max-(f+g) is without-infty by Th16;
A5:dom(max+ f) = dom f & dom(max- f) = dom f &
   dom(max+ g) = dom g & dom(max- g) = dom g by MESFUNC2:def 2,def 3;
    for x be set holds (x in dom(max- f) implies -infty < (max- f).x) &
   (x in dom(max+ f) implies -infty < (max+ f).x) &
   (x in dom(max+ g) implies -infty < (max+ g).x) &
   (x in dom(max- g) implies -infty < (max- g).x)
     by MESFUNC2:14,15;
then A6:max+f is without-infty & max-f is without-infty &
   max+g is without-infty & max-g is without-infty by Th16;
then dom(max+(f+g) + max- f) = dom(max+(f+g)) /\ dom(max- f) &
   dom(max-(f+g) + max+ f) = dom(max-(f+g)) /\ dom(max+ f)
      by A4,Th22;
then A7:dom(max+(f+g) + max- f) = dom g /\ (dom f /\ dom f) &
   dom(max-(f+g) + max+ f) = dom g /\ (dom f /\ dom f) by A3,A5,XBOOLE_1:16;
   hence dom(max+(f+g) + max- f) = dom f /\ dom g &
         dom(max-(f+g) + max+ f) = dom f /\ dom g;
A8:for x be set holds
    ( x in dom(max+(f+g) + max-f) implies 0 <= (max+(f+g) + max-f).x ) &
    ( x in dom(max-(f+g) + max+f) implies 0 <= (max-(f+g) + max+f).x )
   proof
    let x be set;
    hereby assume
A9:  x in dom(max+(f+g) + max- f);
     then 0 <= (max+(f+g)).x & 0 <= (max- f).x by MESFUNC2:14,15;
     then 0 <= (max+(f+g)).x + (max- f).x by EXTREAL2:7;
     hence 0 <= (max+(f+g) + max- f).x by A9,MESFUNC1:def 3;
    end;
    assume
A10:  x in dom(max-(f+g) + max+ f);
    then 0 <= (max-(f+g)).x & 0 <= (max+ f).x by MESFUNC2:14,15;
    then 0 <= (max-(f+g)).x + (max+ f).x by EXTREAL2:7;
    hence 0 <= (max-(f+g) + max+ f).x by A10,MESFUNC1:def 3;
   end;
   then for x be set holds
    (x in dom(max+(f+g) + max-f) implies -infty < (max+(f+g) + max-f).x ) &
    (x in dom(max-(f+g) + max+f) implies -infty < (max-(f+g) + max+f).x );
then A11:max+(f+g) + max-f is without-infty &
   max-(f+g) + max+f is without-infty by Th16;
   then dom(max+(f+g) + max-f + max-g)
    = dom f /\ dom g /\ dom g by A5,A6,A7,Th22
   .= dom f /\ (dom g /\ dom g) by XBOOLE_1:16;
   hence dom(max+(f+g) + max- f + max- g) = dom f /\ dom g;
    dom(max-(f+g) + max+ f + max+ g)
    = dom f /\ dom g /\ dom g by A5,A6,A7,A11,Th22;
   then dom(max-(f+g) + max+f + max+g) = dom f /\ (dom g /\ dom g) by
XBOOLE_1:16;
   hence dom(max-(f+g) + max+f + max+g) = dom f /\ dom g;
   thus thesis by A8,SUPINF_2:71;
end;

theorem Th31:
for X being non empty set, f,g being PartFunc of X,ExtREAL st
 f is without-infty & f is without+infty &
 g is without-infty & g is without+infty holds
 max+(f+g) + max- f + max- g = max-(f+g) + max+ f + max+ g
proof
   let X be non empty set,
       f,g be PartFunc of X,ExtREAL;
   assume
A1:  f is without-infty & f is without+infty &
    g is without-infty & g is without+infty;
   then dom(max+(f+g) + max- f + max- g) = dom f /\ dom g by Th30;
then A2:dom(max+(f+g) + max- f + max- g) = dom(max-(f+g) + max+ f + max+ g)
     by A1,Th30;
A3:dom(max+(f+g))= dom (f+g) & dom(max-(f+g))= dom (f+g) &
   dom max+ f = dom f & dom max- f = dom f &
   dom max+ g = dom g & dom max- g = dom g by MESFUNC2:def 2,def 3;
    ( for x be set st x in dom max+(f+g) holds 0 <= (max+(f+g)).x ) &
   ( for x be set st x in dom max-(f+g) holds 0 <= (max-(f+g)).x ) &
   ( for x be set st x in dom max+ f holds 0<= (max+f).x ) &
   ( for x be set st x in dom max- f holds 0<= (max-f).x ) &
   ( for x be set st x in dom max+ g holds 0<= (max+g).x ) &
   for x be set st x in dom max- g holds 0<= (max-g).x by MESFUNC2:14,15;
then A4:max+(f+g) is nonnegative & max-(f+g) is nonnegative &
   max+f is nonnegative & max-f is nonnegative & max+g is nonnegative &
   max-g is nonnegative by SUPINF_2:71;
then A5:dom(max+(f+g) + max- f + max- g)
    = dom(f+g) /\ dom f /\ dom g by A3,Th29;
then A6:dom(max+(f+g) + max- f + max- g)
    = dom(f+g) /\ (dom f /\ dom g) by XBOOLE_1:16;
    for x be set st x in dom(max+(f+g) + max- f + max- g) holds
    (max+(f+g) + max- f + max- g).x = (max-(f+g) + max+ f + max+ g).x
   proof
    let x be set;
    assume
A7: x in dom(max+(f+g) + max-f + max-g);
then A8:(max-(f+g)+ max+f + max+g).x
      =(max-(f+g)).x +(max+f).x +(max+g).x by A3,A4,A5,Th29;
A9: dom(f+g) = dom f /\ dom g by A1,Th22;
then A10: (max+(f+g)).x = max((f+g).x,0) by A3,A6,A7,MESFUNC2:def 2
                 .=max((f.x+g.x),0) by A6,A7,A9,MESFUNC1:def 3;
A11: (max-(f+g)).x = max(-(f+g).x,0) by A3,A6,A7,A9,MESFUNC2:def 3
                 .=max(-(f.x+g.x),0) by A6,A7,A9,MESFUNC1:def 3;
A12: x in dom f & x in dom g by A6,A7,A9,XBOOLE_0:def 3;
then A13: (max+f).x = max(f.x,0) & (max-f).x = max(-f.x,0) &
    (max+g).x = max(g.x,0) & (max-g).x = max(-g.x,0)
      by A3,MESFUNC2:def 2,def 3;
A14: f.x <> +infty & f.x <> -infty &
    g.x <> +infty & g.x <> -infty by A1,A12,Th16,Th17;
then A15: -f.x <> +infty & -f.x <> -infty &
    -g.x <> +infty & -g.x <> -infty by EXTREAL1:4;
A16: dom(max+(f+g) + max-f + max-g)
     = dom max+(f+g) /\ dom max-f /\ dom max-g by A4,Th29;
     now per cases;
     suppose
   A17:0 <= f.x;
      then -f.x <= 0 by EXTREAL1:25;
   then A18:max-f.x =0 by A13,XXREAL_0:def 9;
      per cases;
      suppose
   A19:0 <= g.x;
    then A20:0 <= f.x + g.x by A17,EXTREAL2:7;
       then - g.x <= 0 & -(f.x + g.x) <= 0 by A19,EXTREAL1:25;
    then A21:(max-g).x=0 & (max-(f+g)).x=0 by A11,A13,XXREAL_0:def 9;
    then A22:(max+(f+g)).x + (max-f).x + (max-g).x
         =f.x + g.x + R_EAL 0 + R_EAL 0 by A10,A18,A20,XXREAL_0:def 9
        .= f.x + g.x + R_EAL 0 by SUPINF_2:18
        .= f.x + g.x by SUPINF_2:18;
        (max+g).x = g.x by A13,A19,XXREAL_0:def 9;
       then (max-(f+g)).x + (max+f).x + (max+g).x
         = R_EAL 0 +f.x + g.x by A13,A17,A21,XXREAL_0:def 9;
       hence (max+(f+g)).x + (max-f).x + (max-g).x
         =(max-(f+g)).x + (max+f).x + (max+g).x by A22,SUPINF_2:18;
      end;
      suppose
   A23:g.x < 0;
       then 0 <= -g.x by EXTREAL1:25;
    then A24:(max-g).x= -g.x & (max+g).x = 0 by A13,A23,XXREAL_0:def 9;
       per cases;
       suppose
   A25:0 <= f.x + g.x;
        then -(f.x + g.x) <= 0 by EXTREAL1:25;
        then (max-(f+g)).x=0 by A11,XXREAL_0:def 9;
     then A26:(max-(f+g)).x + (max+f).x + (max+g).x
          = R_EAL 0 + f.x + R_EAL 0 by A13,A17,A24,XXREAL_0:def 9;
         (max+(f+g)).x + (max-f).x + (max-g).x
          =f.x+g.x + R_EAL 0 + -g.x by A10,A18,A24,A25,XXREAL_0:def 9
         .=f.x + g.x - g.x by SUPINF_2:18
         .=f.x +(g.x - g.x) by A14,EXTREAL1:11
         .=f.x + R_EAL 0 by EXTREAL1:9;
        hence (max+(f+g)).x+ (max-f).x + (max-g).x
          =(max-(f+g)).x + (max+f).x + (max+g).x by A26,SUPINF_2:18;
       end;
       suppose
   A27:f.x + g.x < 0;
        then 0 <= -(f.x + g.x) by EXTREAL1:25;
     then A28:(max-(f+g)).x = -(f.x + g.x) by A11,XXREAL_0:def 9;
         (max+(f+g)).x + (max-f).x + (max-g).x
          =R_EAL 0 + R_EAL 0 + -g.x by A10,A18,A24,A27,XXREAL_0:def 9;
     then A29:(max+(f+g)).x + (max-f).x + (max-g).x
          =R_EAL 0 + -g.x by SUPINF_2:18;
         (max-(f+g)).x + (max+f).x + (max+g).x
          = -(f.x + g.x) +f.x + R_EAL 0 by A13,A17,A24,A28,XXREAL_0:def 9
         .= -(f.x + g.x) +f.x by SUPINF_2:18
         .=-g.x - f.x + f.x by EXTREAL2:14
         .=-g.x + (-f.x +f.x) by A15,EXTREAL1:8;
        hence (max+(f+g)).x + (max-f).x + (max-g).x
          =(max-(f+g)).x + (max+f).x + (max+g).x by A29,EXTREAL1:9;
       end;
      end;
     end;
     suppose
   A30:f.x < 0;
      then 0 <= -f.x by EXTREAL1:25;
   then A31:(max-f).x = -f.x by A13,XXREAL_0:def 9;
      per cases;
      suppose
   A32:0 <= g.x;
       then -g.x <= 0 by EXTREAL1:25;
    then A33:(max-g).x = 0 by A13,XXREAL_0:def 9;
    A34:(max+g).x = g.x by A13,A32,XXREAL_0:def 9;
       per cases;
       suppose A35:0 <= f.x + g.x;
        then -(f.x + g.x) <= 0 by EXTREAL1:25;
     then A36:(max-(f+g)).x = 0 by A11,XXREAL_0:def 9;
     A37:(max+(f+g)).x + (max-f).x + (max-g).x
          =f.x + g.x+ -f.x + R_EAL 0 by A10,A31,A33,A35,XXREAL_0:def 9
         .=f.x + g.x+ -f.x by SUPINF_2:18
         .=g.x + (f.x+ -f.x) by A14,EXTREAL1:8
         .=g.x + R_EAL 0 by EXTREAL1:9;
         (max-(f+g)).x + (max+f).x + (max+g).x
          = R_EAL 0 + R_EAL 0 + g.x by A13,A30,A34,A36,XXREAL_0:def 9;
        hence (max+(f+g)).x + (max-f).x + (max-g).x
          =(max-(f+g)).x + (max+f).x + (max+g).x by A37,SUPINF_2:18;
       end;
       suppose A38:f.x + g.x < 0;
        then 0 <= -(f.x + g.x) by EXTREAL1:25;
     then A39:(max-(f+g)).x = -(f.x + g.x) by A11,XXREAL_0:def 9;
     A40:(max+(f+g)).x + (max-f).x + (max-g).x
          =R_EAL 0 + -f.x + R_EAL 0 by A10,A31,A33,A38,XXREAL_0:def 9
         .=R_EAL 0 + -f.x by SUPINF_2:18;
         (max-(f+g)).x + (max+f).x + (max+g).x
          = -(f.x + g.x) + R_EAL 0 + g.x by A13,A30,A34,A39,XXREAL_0:def 9
         .= -(f.x + g.x) +g.x by SUPINF_2:18
         .=-f.x - g.x +g.x by EXTREAL2:14
         .=-f.x + (-g.x +g.x) by A15,EXTREAL1:8;
        hence (max+(f+g)).x + (max-f).x + (max-g).x
          =(max-(f+g)).x + (max+f).x + (max+g).x by A40,EXTREAL1:9;
       end;
      end;
      suppose A41:g.x < 0;
    then A42:f.x + g.x <= 0 by A30,EXTREAL2:9;
        0 <= -g.x by A41,EXTREAL1:25;
    then A43:(max-g).x=-g.x by A13,XXREAL_0:def 9;
        0 <= -(f.x + g.x) by A42,EXTREAL1:25;
    then A44:(max-(f+g)).x=-(f.x + g.x) by A11,XXREAL_0:def 9;
    A45:(max+g).x = 0 by A13,A41,XXREAL_0:def 9;
    A46:(max+(f+g)).x+ (max-f).x + (max-g).x
         =R_EAL 0 + -f.x + -g.x by A10,A31,A42,A43,XXREAL_0:def 9
        .=-f.x -g.x by SUPINF_2:18;
        (max-(f+g)).x + (max+f).x + (max+g).x
         = -(f.x + g.x) + R_EAL 0 + R_EAL 0 by A13,A30,A44,A45,XXREAL_0:def 9
        .= -(f.x + g.x) + R_EAL 0 by SUPINF_2:18
        .= -(f.x + g.x) by SUPINF_2:18;
       hence (max+(f+g)).x+ (max-f).x + (max-g).x
         =(max-(f+g)).x + (max+f).x + (max+g).x by A46,EXTREAL2:14;
      end;
     end;
    end;
    hence thesis by A4,A7,A8,A16,Th29;
   end;
   hence thesis by A2,FUNCT_1:9;
end;

theorem Th32:
for C being non empty set, f being PartFunc of C,ExtREAL, c be Real st
 0 <= c holds max+(c(#)f) = c(#)max+f & max-(c(#)f) = c(#)max-f
proof
   let C be non empty set;
   let f be PartFunc of C,ExtREAL;
   let c be Real;
   assume
A1: 0 <= c;
A2:dom max+(c(#)f) = dom(c(#)f) by MESFUNC2:def 2
                   .= dom f by MESFUNC1:def 6
                   .= dom max+f by MESFUNC2:def 2
                   .= dom(c(#)max+f) by MESFUNC1:def 6;
A3:dom(max-(c(#)f)) = dom(c(#)f) by MESFUNC2:def 3
                   .= dom f by MESFUNC1:def 6
                   .= dom max-f by MESFUNC2:def 3
                   .= dom(c(#)max-f) by MESFUNC1:def 6;
    for x be Element of C st x in dom max+(c(#)f) holds
    (max+(c(#)f)).x = (c(#)max+f).x
   proof
    let x be Element of C;
    assume
 A4: x in dom max+(c(#)f);
 then A5:x in dom(c(#)f) by MESFUNC2:def 2;
    then x in dom f by MESFUNC1:def 6;
 then A6:x in dom max+ f by MESFUNC2:def 2;
 A7:(max+(c(#)f)).x = max((c(#)f).x,0) by A4,MESFUNC2:def 2
                   .= max(R_EAL c * f.x,0) by A5,MESFUNC1:def 6;
     (c(#)max+f).x = R_EAL c * max+f.x by A2,A4,MESFUNC1:def 6
                   .= R_EAL c * max(f.x,R_EAL 0) by A6,MESFUNC2:def 2
                   .= max(R_EAL c * f.x,R_EAL c * R_EAL 0) by A1,Th11;
    hence thesis by A7,EXTREAL1:22;
   end;
   hence max+(c(#)f) = c(#)max+f by A2,PARTFUN1:34;
    for x be Element of C st x in dom max-(c(#)f) holds
    (max-(c(#)f)).x = (c(#)max-f).x
   proof
    let x be Element of C;
    assume
 A8: x in dom max-(c(#)f);
 then A9:x in dom(c(#)f) by MESFUNC2:def 3;
    then x in dom f by MESFUNC1:def 6;
 then A10:x in dom max- f by MESFUNC2:def 3;
 A11:(max-(c(#)f)).x = max(-(c(#)f).x,0) by A8,MESFUNC2:def 3
                   .= max(-(R_EAL c)*f.x,0) by A9,MESFUNC1:def 6;
     (c(#)max-f).x = R_EAL c * max-f.x by A3,A8,MESFUNC1:def 6
                 .= R_EAL c * max(-f.x,R_EAL 0) by A10,MESFUNC2:def 3
                 .= max(R_EAL c * (-f.x),R_EAL c * R_EAL 0) by A1,Th11
                 .= max(-(R_EAL c)*f.x,R_EAL c * R_EAL 0) by EXTREAL1:26;
    hence thesis by A11,EXTREAL1:22;
   end;
   hence max-(c(#)f) = c(#)max-f by A3,PARTFUN1:34;
end;

theorem Th33:
for C being non empty set, f being PartFunc of C,ExtREAL, c be Real st
 0 <= c holds max+((-c)(#)f) = c(#)max-f & max-((-c)(#)f) = c(#)max+f
proof
   let C be non empty set;
   let f be PartFunc of C,ExtREAL;
   let c be Real;
   assume
A1: 0 <= c;
A2:dom max+((-c)(#)f) = dom((-c)(#)f) & dom max-((-c)(#)f) = dom((-c)(#)f)
     by MESFUNC2:def 2,def 3;
   then dom max+((-c)(#)f) = dom f & dom max-((-c)(#)f) = dom f
     by MESFUNC1:def 6;
then A3:dom max+((-c)(#)f) = dom max-f & dom max-((-c)(#)f) = dom max+f
     by MESFUNC2:def 2,def 3;
then A4:dom max+((-c)(#)f) = dom(c(#)max-f) & dom max-((-c)(#)f) = dom(c(#)max+
f)
     by MESFUNC1:def 6;
A5:- R_EAL c = -c by SUPINF_2:3;
    for x be Element of C st x in dom max+((-c)(#)f) holds
    max+((-c)(#)f).x = (c(#)max-f).x
   proof
    let x be Element of C;
    assume
 A6: x in dom max+((-c)(#)f);
 then A7:(max+((-c)(#)f)).x = max(((-c)(#)f).x,0) by MESFUNC2:def 2
                      .= max(R_EAL(-c)*f.x,0) by A2,A6,MESFUNC1:def 6
                      .= max(-(R_EAL c * f.x),0) by A5,EXTREAL1:26;
     (c(#)max-f).x = R_EAL c * max-f.x by A4,A6,MESFUNC1:def 6
                   .= R_EAL c * max(-f.x,R_EAL 0) by A3,A6,MESFUNC2:def 3
                   .= max(R_EAL c * (-f.x),R_EAL c * R_EAL 0) by A1,Th11
                   .= max(-(R_EAL c * f.x),R_EAL c * R_EAL 0) by EXTREAL1:26;
    hence thesis by A7,EXTREAL1:22;
   end;
   hence max+((-c)(#)f) = c(#)max-f by A4,PARTFUN1:34;
    for x be Element of C st x in dom max-((-c)(#)f) holds
     max-((-c)(#)f).x = (c(#)max+f).x
   proof
    let x be Element of C;
    assume
 A8: x in dom max-((-c)(#)f);
 then A9: max-((-c)(#)f).x = max(-((-c)(#)f).x,0) by MESFUNC2:def 3
                     .= max(-(R_EAL(-c))*f.x,0) by A2,A8,MESFUNC1:def 6
                     .= max((-(-(R_EAL c)))*f.x,0) by A5,EXTREAL1:26;
     (c(#)max+f).x = R_EAL c * max+f.x by A4,A8,MESFUNC1:def 6
                   .= R_EAL c * max(f.x,R_EAL 0) by A3,A8,MESFUNC2:def 2
                   .= max(R_EAL c * f.x,R_EAL c * R_EAL 0) by A1,Th11;
    hence thesis by A9,EXTREAL1:22;
   end;
   hence max-((-c)(#)f) = c(#)max+f by A4,PARTFUN1:34;
end;

theorem Th34:
for X be non empty set, f be PartFunc of X,ExtREAL, A be set holds
 max+(f|A)=max+f|A & max-(f|A)=max-f|A
proof
   let X be non empty set;
   let f be PartFunc of X,ExtREAL;
   let A be set;
A1:dom max+(f|A) = dom(f|A) by MESFUNC2:def 2
                .= dom f /\ A by FUNCT_1:68
                .= dom max+f /\ A by MESFUNC2:def 2
                .= dom(max+f|A) by FUNCT_1:68;
    for x being Element of X st x in dom max+(f|A) holds
     (max+(f|A)).x = (max+f|A).x
   proof
    let x be Element of X;
    assume
A2:  x in dom max+(f|A);
    then x in dom max+f /\ A by A1,FUNCT_1:68;
then A3: x in dom max+f & x in A by XBOOLE_0:def 3;
A4: (max+(f|A)).x = max((f|A).x,0) by A2,MESFUNC2:def 2
                 .= max(f.x,0) by A3,FUNCT_1:72;
     (max+f|A).x = (max+f).x by A1,A2,FUNCT_1:70;
    hence thesis by A3,A4,MESFUNC2:def 2;
   end;
   hence max+(f|A) = max+f|A by A1,PARTFUN1:34;
A5:dom max-(f|A) = dom(f|A) by MESFUNC2:def 3
                .= dom f /\ A by FUNCT_1:68
                .= dom max-f /\ A by MESFUNC2:def 3
                .= dom(max-f|A) by FUNCT_1:68;
    for x being Element of X st x in dom max-(f|A) holds
     (max-(f|A)).x = (max-f|A).x
   proof
    let x be Element of X;
    assume
A6:  x in dom max-(f|A);
    then x in dom max-f /\ A by A5,FUNCT_1:68;
then A7: x in dom max-f & x in A by XBOOLE_0:def 3;
A8: (max-(f|A)).x = max(-(f|A).x,0) by A6,MESFUNC2:def 3
                 .= max(-f.x,0) by A7,FUNCT_1:72;
     (max-f|A).x = (max-f).x by A5,A6,FUNCT_1:70;
    hence thesis by A7,A8,MESFUNC2:def 3;
   end;
   hence max-(f|A)=max-f|A by A5,PARTFUN1:34;
end;

theorem Th35:
for X be non empty set, f,g be PartFunc of X,ExtREAL, B be set st
 B c= dom(f+g) holds dom((f+g)|B) =B & dom(f|B+g|B)=B & (f+g)|B = f|B+g|B
proof
   let X be non empty set,
       f,g be PartFunc of X,ExtREAL,
       B be set such that
A1:B c= dom(f+g);
    dom(f+g) =
    (dom f /\ dom g)\(f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty})
      by MESFUNC1:def 3;
   then dom(f+g) c= dom f /\ dom g by XBOOLE_1:36;
   then B c= dom f /\ dom g by A1,XBOOLE_1:1;
then A2:B c= dom f & B c= dom g by XBOOLE_1:18;
    for x be set st x in dom(f|B) holds (f|B).x in ExtREAL;
   then reconsider fb = f|B as Function of dom(f|B),ExtREAL by FUNCT_2:5;
    for x be set st x in dom f holds f.x in ExtREAL;
   then reconsider ff = f as Function of dom f,ExtREAL by FUNCT_2:5;
    for x be set st x in dom(g|B) holds (g|B).x in ExtREAL;
   then reconsider gb = g|B as Function of dom (g|B), ExtREAL by FUNCT_2:5;
    for x be set st x in dom g holds g.x in ExtREAL;
   then reconsider gg = g as Function of dom g,ExtREAL by FUNCT_2:5;
    dom((f+g)|B) =dom(f+g) /\ B & dom(f|B) = dom f /\ B & dom(g|B) = dom g /\ B
     by FUNCT_1:68;
then A3:dom((f+g)|B) = B & dom(f|B) = B & dom(g|B) = B by A1,A2,XBOOLE_1:28;
A4:(f|B)"{+infty}=f"{+infty} /\ B
   proof
     now let x be set;
     assume
   x in (f|B)"{+infty};
  then A5:x in dom fb & fb.x in {+infty} by FUNCT_2:46;
     then x in dom f /\ B & (f|B).x in {+infty} by FUNCT_1:68;
     then x in dom f & x in B & f.x in {+infty} by A5,FUNCT_1:68,XBOOLE_0:def 3
;
     then x in ff"{+infty} & x in B by FUNCT_2:46;
     hence x in f"{+infty} /\ B by XBOOLE_0:def 3;
    end;
 then A6:(f|B)"{+infty} c= f"{+infty} /\ B by TARSKI:def 3;
     now let x be set;
     assume x in f"{+infty} /\ B;
     then x in f"{+infty} & x in B by XBOOLE_0:def 3;
     then x in dom ff & ff.x in {+infty} & x in B by FUNCT_2:46;
     then x in dom ff /\ B & ff.x in {+infty} by XBOOLE_0:def 3;
     then x in dom(ff|B) & ff.x in {+infty} by FUNCT_1:68;
     then x in dom fb & fb.x in {+infty} by FUNCT_1:68;
     hence x in (f|B)"{+infty} by FUNCT_2:46;
    end;
    then f"{+infty} /\ B c= (f|B)"{+infty} by TARSKI:def 3;
    hence thesis by A6,XBOOLE_0:def 10;
   end;
A7:(f|B)"{-infty}=f"{-infty} /\B
   proof
     now let x be set;
     assume x in (f|B)"{-infty};
  then A8:x in dom fb & fb.x in {-infty} by FUNCT_2:46;
     then x in dom f /\ B & (f|B).x in {-infty} by FUNCT_1:68;
     then x in dom f & x in B & f.x in {-infty} by A8,FUNCT_1:68,XBOOLE_0:def 3
;
     then x in ff"{-infty} & x in B by FUNCT_2:46;
     hence x in f"{-infty} /\ B by XBOOLE_0:def 3;
    end;
 then A9:(f|B)"{-infty} c= f"{-infty} /\ B by TARSKI:def 3;
     now let x be set;
     assume x in f"{-infty} /\ B;
     then x in f"{-infty} & x in B by XBOOLE_0:def 3;
     then x in dom ff & ff.x in {-infty} & x in B by FUNCT_2:46;
     then x in dom ff /\ B & ff.x in {-infty} by XBOOLE_0:def 3;
     then x in dom(ff|B) & ff.x in {-infty} by FUNCT_1:68;
     then x in dom fb & fb.x in {-infty} by FUNCT_1:68;
     hence x in (f|B)"{-infty} by FUNCT_2:46;
    end;
    then f"{-infty} /\ B c= (f|B)"{-infty} by TARSKI:def 3;
    hence thesis by A9,XBOOLE_0:def 10;
   end;
A10:(g|B)"{+infty}=g"{+infty} /\ B
   proof
     now let x be set;
     assume x in (g|B)"{+infty};
  then A11: x in dom gb & gb.x in {+infty} by FUNCT_2:46;
     then x in dom g /\ B & (g|B).x in {+infty} by FUNCT_1:68;
     then x in dom g & x in B & g.x in {+infty} by A11,FUNCT_1:68
,XBOOLE_0:def 3;
     then x in gg"{+infty} & x in B by FUNCT_2:46;
     hence x in g"{+infty} /\ B by XBOOLE_0:def 3;
    end;
 then A12:(g|B)"{+infty} c= g"{+infty} /\ B by TARSKI:def 3;
     now let x be set;
     assume x in g"{+infty} /\ B;
     then x in g"{+infty} & x in B by XBOOLE_0:def 3;
     then x in dom gg & gg.x in {+infty} & x in B by FUNCT_2:46;
     then x in dom gg /\ B & gg.x in {+infty} by XBOOLE_0:def 3;
     then x in dom(gg|B) & gg.x in {+infty} by FUNCT_1:68;
     then x in dom gb & gb.x in {+infty} by FUNCT_1:68;
     hence x in (g|B)"{+infty} by FUNCT_2:46;
    end;
    then g"{+infty} /\ B c= (g|B)"{+infty} by TARSKI:def 3;
    hence thesis by A12,XBOOLE_0:def 10;
   end;
A13:(g|B)"{-infty}=g"{-infty} /\B
   proof
     now let x be set;
     assume x in (g|B)"{-infty};
  then A14:x in dom gb & gb.x in {-infty} by FUNCT_2:46;
     then x in dom g /\ B & (g|B).x in {-infty} by FUNCT_1:68;
     then x in dom g & x in B & g.x in {-infty} by A14,FUNCT_1:68
,XBOOLE_0:def 3;
     then x in gg"{-infty} & x in B by FUNCT_2:46;
     hence x in g"{-infty} /\ B by XBOOLE_0:def 3;
    end;
 then A15:(g|B)"{-infty} c= g"{-infty} /\ B by TARSKI:def 3;
     now let x be set;
     assume x in g"{-infty} /\ B;
     then x in g"{-infty} & x in B by XBOOLE_0:def 3;
     then x in dom gg & gg.x in {-infty} & x in B by FUNCT_2:46;
     then x in dom gg /\ B & gg.x in {-infty} by XBOOLE_0:def 3;
     then x in dom(gg|B) & gg.x in {-infty} by FUNCT_1:68;
     then x in dom gb & gb.x in {-infty} by FUNCT_1:68;
     hence x in (g|B)"{-infty} by FUNCT_2:46;
    end;
    then g"{-infty} /\ B c= (g|B)"{-infty} by TARSKI:def 3;
    hence thesis by A15,XBOOLE_0:def 10;
   end;
A16:(f|B)"{-infty} /\ (g|B)"{+infty}
    = f"{-infty} /\B /\ g"{+infty} /\B  by A7,A10,XBOOLE_1:16
   .= f"{-infty} /\ g"{+infty} /\ B /\B  by XBOOLE_1:16
   .= f"{-infty} /\ g"{+infty} /\ (B /\ B) by XBOOLE_1:16;
    (f|B)"{+infty} /\ (g|B)"{-infty}
    = f"{+infty} /\B /\ g"{-infty} /\ B by A4,A13,XBOOLE_1:16
   .= f"{+infty} /\ g"{-infty} /\ B /\ B  by XBOOLE_1:16
   .= f"{+infty} /\ g"{-infty} /\ (B /\ B) by XBOOLE_1:16;
then A17:(f|B)"{-infty} /\ (g|B)"{+infty} \/ (f|B)"{+infty} /\ (g|B)"{-infty}
    =(f"{-infty}/\g"{+infty} \/ (f"{+infty}/\g"{-infty})) /\ B
      by A16,XBOOLE_1:23;
    dom(f|B) /\ dom(g|B) = dom f /\ B /\ dom(g|B) by FUNCT_1:68
    .= dom f /\ B /\ (dom g /\ B) by FUNCT_1:68
    .= dom f /\ B /\ dom g /\ B by XBOOLE_1:16
    .= dom f /\ dom g /\ B /\ B by XBOOLE_1:16
    .= dom f /\ dom g /\ (B /\ B) by XBOOLE_1:16;
then A18:dom(f|B+g|B) = (dom f /\ dom g /\ B)
      \((f|B)"{-infty} /\ (g|B)"{+infty} \/ ((f|B)"{+infty} /\ (g|B)"{-infty}))
         by MESFUNC1:def 3
   .=((dom f /\ dom g) \ ((f"{-infty} /\ g"{+infty})
      \/ (f"{+infty} /\ g"{-infty}))) /\ B by A17,XBOOLE_1:50
   .=dom(f+g) /\ B by MESFUNC1:def 3
   .=B by A1,XBOOLE_1:28;
    now let x be set;
    assume A19: x in dom ((f+g)|B);
    hence ((f+g)|B).x = (f+g).x by FUNCT_1:68
               .=f.x+g.x by A1,A3,A19,MESFUNC1:def 3
               .=(f|B).x + g.x by A3,A19,FUNCT_1:68
               .=(f|B).x + (g|B).x by A3,A19,FUNCT_1:68
               .= ((f|B)+(g|B)).x by A3,A18,A19,MESFUNC1:def 3;
   end;
   hence thesis by A3,A18,FUNCT_1:9;
end;

theorem Th36:
for X be non empty set, f be PartFunc of X,ExtREAL, a be R_eal holds
 eq_dom(f,a) = f"{a}
proof
   let X be non empty set;
   let f be PartFunc of X,ExtREAL;
   let a be R_eal;
    now let x be set;
    assume x in eq_dom(f,a);
then A1: x in dom f & ex y being R_eal st y = f.x & a=y by MESFUNC1:def 16;
    then f.x in {a} by TARSKI:def 1;
    hence x in f"{a} by A1,FUNCT_1:def 13;
   end;
then A2:eq_dom(f,a) c= f"{a} by TARSKI:def 3;
    now let x be set;
    assume x in f"{a};
then A3: x in dom f & f.x in {a} by FUNCT_1:def 13;
    then f.x = a by TARSKI:def 1;
    hence x in eq_dom(f,a) by A3,MESFUNC1:def 16;
   end;
   then f"{a} c= eq_dom(f,a) by TARSKI:def 3;
   hence thesis by A2,XBOOLE_0:def 10;
end;

begin :: Lemmas for measurable function and simple valued function

theorem Th37:
for X be non empty set,
    S be SigmaField of X,
    f,g be PartFunc of X,ExtREAL,
    A be Element of S st
 f is without-infty & g is without-infty &
 f is_measurable_on A & g is_measurable_on A holds
   f+g is_measurable_on A
proof
   let X be non empty set,
    S be SigmaField of X,
    f,g be PartFunc of X,ExtREAL,
    A be Element of S;
   assume that
A1: f is without-infty & g is without-infty and
A2: f is_measurable_on A & g is_measurable_on A;
    for r be real number holds A /\ less_dom(f+g,R_EAL r) is_measurable_on S
   proof
    let r be real number;
    reconsider r as Real by XREAL_0:def 1;
    consider F being Function of RAT,S such that
A3:  for p being Rational holds
      F.p = A /\ less_dom(f,R_EAL p) /\ (A /\ less_dom(g,R_EAL(r-p)))
         by A2,MESFUNC2:6;
A4: A /\ less_dom(f+g,R_EAL r) = union rng F by A1,A3,Th24;
     ex G be Function of NAT,S st rng F = rng G by MESFUNC1:5,MESFUNC2:5;
    then rng F is N_Sub_set_fam of X by MEASURE1:52;
    then union rng F in S by MEASURE1:def 9;
    hence thesis by A4,MESFUNC1:def 11;
   end;
   hence thesis by MESFUNC1:def 17;
end;

Lm2:
for X be non empty set,
    S be SigmaField of X,
    f,g be PartFunc of X,ExtREAL,
    A be Element of S st
  f is nonnegative & g is nonnegative &
  f is_measurable_on A & g is_measurable_on A holds
    dom(f+g) = dom f /\ dom g & f+g is_measurable_on A
proof
   let X be non empty set,
    S be SigmaField of X,
    f,g be PartFunc of X,ExtREAL,
    A be Element of S;
   assume that
A1: f is nonnegative & g is nonnegative and
A2: f is_measurable_on A & g is_measurable_on A;
    f is without-infty & g is without-infty by A1,Th18;
   hence thesis by A2,Th22,Th37;
end;

theorem
 for X be non empty set, S be SigmaField of X,
M be sigma_Measure of S, f be PartFunc of X,ExtREAL st
  f is_simple_func_in S & dom f = {} holds
ex F be Finite_Sep_Sequence of S, a,x be FinSequence of ExtREAL st
  F,a are_Re-presentation_of f & a.1 = 0 &
  (for n be Nat st 2 <= n & n in dom a holds 0 < a.n & a.n < +infty) &
  dom x = dom F &
  (for n be Nat st n in dom x holds x.n = a.n*(M*F).n) &
  Sum x = 0
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f be PartFunc of X,ExtREAL;
   assume
A1: f is_simple_func_in S & dom f = {};
   then for x be set st x in dom f holds 0 <= f.x;
   then consider F be Finite_Sep_Sequence of S,
            a be FinSequence of ExtREAL such that
A2: F,a are_Re-presentation_of f &
    a.1 = 0 &
    for n be Nat st 2 <= n & n in dom a holds 0 < a.n & a.n < +infty
     by A1,MESFUNC3:14;
   deffunc F(Nat) = a.$1*(M*F).$1;
   consider x be FinSequence of ExtREAL such that
A3: len x = len F and
A4: for n be Nat st n in Seg len F holds x.n = F(n)
from FINSEQ_2:sch 1;
take F,a,x;
A5:dom x = Seg len F by A3,FINSEQ_1:def 3;
then A6:dom x = dom F by FINSEQ_1:def 3;
   consider sumx be Function of NAT,ExtREAL such that
A7: Sum x = sumx.(len x) & sumx.0 = 0 &
    for i be  Element of NAT st i < len x holds sumx.(i+1)=sumx.i+x.(i+1)
     by CONVFUN1:def 5;
   defpred P[Nat] means $1 <= len x implies sumx.$1 = 0;
A8:union rng F = {} by A1,A2,MESFUNC3:def 1;
A9:for n be Nat st n in dom F holds F.n = {}
   proof
    let n be Nat;
    assume n in dom F;
then A10: F.n in rng F by FUNCT_1:12;
    assume F.n <> {};
    then ex v be set st v in F.n by XBOOLE_0:def 1;
    hence contradiction by A8,A10,TARSKI:def 4;
   end;
A11:P[0] by A7;
A12:for i be Nat st P[i] holds P[i+1]
   proof
    let i be Nat;
    assume
A13:  P[i];
    assume
    A14:  i+1 <= len x;
    reconsider i as Element of NAT by ORDINAL1:def 13;
     1 <= i+1 by NAT_1:11;
then A15: i+1 in dom x by A14,FINSEQ_3:27;
     i < len x by A14,NAT_1:13;
then A16: sumx.(i+1) = sumx.i + x.(i+1) by A7;
     F.(i+1) = {} by A6,A9,A15;
    then M.(F.(i+1)) = 0 by MEASURE1:def 11;
then A17: (M*F).(i+1) = 0 by A6,A15,FUNCT_1:23;
     x.(i+1) = a.(i+1)*(M*F).(i+1) by A4,A5,A15
           .= 0 by A17,EXTREAL1:22;
    hence thesis by A13,A14,A16,NAT_1:13,SUPINF_2:18;
   end;
    for i be Nat holds P[i] from NAT_1:sch 2(A11,A12);
   hence thesis by A2,A4,A5,A7,FINSEQ_1:def 3;
end;

theorem Th39:
for X be non empty set, S be SigmaField of X,
    f be PartFunc of X,ExtREAL, A be Element of S, r,s be Real st
f is_measurable_on A & A c= dom f holds
 A /\ great_eq_dom(f,R_EAL r) /\ less_dom(f,R_EAL s) is_measurable_on S
proof
   let X be non empty set;
   let S be SigmaField of X;
   let f be PartFunc of X,ExtREAL;
   let A be Element of S;
   let r,s be Real;
   assume
A1: f is_measurable_on A & A c= dom f;
   then A /\ less_dom(f,R_EAL s) is_measurable_on S by MESFUNC1:def 17;
then A2:A /\ less_dom(f,R_EAL s) in S by MESFUNC1:def 11;
    A /\ great_eq_dom(f,R_EAL r) is_measurable_on S by A1,MESFUNC1:31;
   then A /\ great_eq_dom(f,R_EAL r) in S by MESFUNC1:def 11;
then A3:A /\ great_eq_dom(f,R_EAL r) /\ (A /\ less_dom(f,R_EAL s)) in S
     by A2,FINSUB_1:def 2;
    A /\ great_eq_dom(f,R_EAL r) /\ (A /\ less_dom(f,R_EAL s))
    = A /\ great_eq_dom(f,R_EAL r) /\ A /\ less_dom(f,R_EAL s) by XBOOLE_1:16
   .= great_eq_dom(f,R_EAL r) /\ (A/\A) /\ less_dom(f,R_EAL s) by XBOOLE_1:16;
   hence thesis by A3,MESFUNC1:def 11;
end;

theorem Th40:
for X be non empty set, S be SigmaField of X,
    M be sigma_Measure of S, f be PartFunc of X,ExtREAL, A be Element of S st
 f is_simple_func_in S holds f|A is_simple_func_in S
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f be PartFunc of X,ExtREAL;
   let A be Element of S;
   assume
A1:f is_simple_func_in S;
   then consider F be Finite_Sep_Sequence of S such that
A2: dom f = union rng F and
A3: for n be Nat, x,y being Element of X st
     n in dom F & x in F.n & y in F.n holds f.x = f.y by MESFUNC2:def 5;
A4:f is finite by A1,MESFUNC2:def 5;
   set g = f|A;
    now let x be Element of X;
    assume
A5: x in dom g;
    then x in dom f /\ A by FUNCT_1:68;
    then x in dom f by XBOOLE_0:def 3;
    then |. f.x .| < +infty by A4,MESFUNC2:def 1;
    hence |. g.x .| < +infty by A5,FUNCT_1:70;
   end;
then A6:g is finite by MESFUNC2:def 1;
   deffunc FA(Nat) = F.$1 /\ A;
   consider G be FinSequence such that
A7: len G = len F &
    for n be Nat st n in Seg len F holds G.n = FA(n)
    from FINSEQ_1:sch 2;
    rng G c= S
   proof
    let P be set;
    assume P in rng G;
    then consider k be Nat such that
A8: k in dom G & P = G.k by FINSEQ_2:11;
A9:k in Seg len F by A7,A8,FINSEQ_1:def 3;
then A10:G.k = F.k /\ A by A7;
     k in dom F by A9,FINSEQ_1:def 3;
    then F.k in rng F & rng F c= S by FUNCT_1:12;
    hence thesis by A8,A10,FINSUB_1:def 2;
   end;
   then reconsider G as FinSequence of S by FINSEQ_1:def 4;
    for i,j be Nat st i in dom G & j in dom G & i <> j holds G.i misses G.j
   proof
    let i,j be Nat;
    assume
A11: i in dom G & j in dom G & i <> j;
then A12:i in Seg len F & j in Seg len F by A7,FINSEQ_1:def 3;
    then i in dom F & j in dom F by FINSEQ_1:def 3;
then A13:F.i misses F.j by A11,MESFUNC3:4;
     G.i = F.i /\ A & G.j = F.j /\ A by A7,A12;
    then G.i /\ G.j = F.i /\ A /\ F.j /\ A by XBOOLE_1:16
              .= F.i /\ F.j /\ A /\ A by XBOOLE_1:16
              .= {} /\ A /\ A by A13,XBOOLE_0:def 7;
    hence thesis by XBOOLE_0:def 7;
   end;
   then reconsider G as Finite_Sep_Sequence of S by MESFUNC3:4;
    for v be set st v in dom(f|A) holds v in union rng G
   proof
    let v be set;
    assume v in dom(f|A);
    then v in dom f /\ A by FUNCT_1:68;
then A14:v in dom f & v in A by XBOOLE_0:def 3;
    then consider W be set such that
A15: v in W & W in rng F by A2,TARSKI:def 4;
    consider k be Nat such that
A16: k in dom F & W = F.k by A15,FINSEQ_2:11;
A17:k in Seg len F by A16,FINSEQ_1:def 3;
    then G.k = F.k /\ A by A7;
then A18:v in G.k by A14,A15,A16,XBOOLE_0:def 3;
     k in dom G by A7,A17,FINSEQ_1:def 3;
    then G.k in rng G by FUNCT_1:12;
    hence thesis by A18,TARSKI:def 4;
   end;
then A19:dom(f|A) c= union rng G by TARSKI:def 3;
    for v be set st v in union rng G holds v in dom(f|A)
   proof
    let v be set;
    assume v in union rng G;
    then consider W be set such that
A20: v in W & W in rng G by TARSKI:def 4;
    consider k be Nat such that
A21: k in dom G & W = G.k by A20,FINSEQ_2:11;
A22:k in Seg(len F) by A7,A21,FINSEQ_1:def 3;
    then G.k = F.k /\ A by A7;
then A23:v in F.k & v in A by A20,A21,XBOOLE_0:def 3;
     k in dom F by A22,FINSEQ_1:def 3;
    then F.k in rng F by FUNCT_1:12;
    then v in union rng F by A23,TARSKI:def 4;
    then v in dom f /\ A by A2,A23,XBOOLE_0:def 3;
    hence thesis by FUNCT_1:68;
   end;
   then union rng G c= dom(f|A) by TARSKI:def 3;
then A24:dom(f|A) = union rng G by A19,XBOOLE_0:def 10;
    for n be Nat, x,y be Element of X st
    n in dom G & x in G.n & y in G.n holds (f|A).x = (f|A).y
   proof
    let n be Nat;
    let x,y be Element of X;
    assume
A25: n in dom G & x in G.n & y in G.n;
    then G.n in rng G by FUNCT_1:12;
then A26:x in dom(f|A) & y in dom(f|A) by A24,A25,TARSKI:def 4;
A27:n in Seg(len F) by A7,A25,FINSEQ_1:def 3;
    then G.n = F.n /\ A by A7;
then A28:x in F.n & y in F.n by A25,XBOOLE_0:def 3;
     n in dom F by A27,FINSEQ_1:def 3;
    then f.x = f.y by A3,A28;
    then (f|A).x = f.y by A26,FUNCT_1:70;
    hence thesis by A26,FUNCT_1:70;
   end;
   hence f|A is_simple_func_in S by A6,A24,MESFUNC2:def 5;
end;

theorem Th41:
for X be non empty set, S be SigmaField of X, A be Element of S,
    F be Finite_Sep_Sequence of S, G be FinSequence st
 dom F = dom G & (for n be Nat st n in dom F holds G.n = F.n /\ A) holds
  G is Finite_Sep_Sequence of S
proof
   let X be non empty set;
   let S be SigmaField of X;
   let A be Element of S;
   let F be Finite_Sep_Sequence of S;
   let G be FinSequence;
   assume that
A1:dom F = dom G and
A2:for n be Nat st n in dom F holds G.n = F.n /\ A;
    rng G c= S
   proof
    let v be set;
    assume v in rng G;
    then consider k be set such that
A3:  k in dom G & v = G.k by FUNCT_1:def 5;
     k is Nat by A3,ORDINAL1:def 13;
    then A4: G.k = F.k /\ A by A1,A2,A3;
     F.k in rng F by A1,A3,FUNCT_1:12;
    hence thesis by A3,A4,FINSUB_1:def 2;
   end;
   then reconsider G as FinSequence of S by FINSEQ_1:def 4;
    now let i,j be Nat;
    assume
A5:  i in dom G & j in dom G & i <> j;
then A6: G.i = F.i /\ A & G.j = F.j /\ A by A1,A2;
     F.i misses F.j by A5,PROB_2:def 3;
    hence G.i misses G.j by A6,XBOOLE_1:76;
   end;
   hence thesis by MESFUNC3:4;
end;

theorem Th42:
for X be non empty set, S be SigmaField of X, f be PartFunc of X,ExtREAL,
    A be Element of S, F,G be Finite_Sep_Sequence of S,
    a be FinSequence of ExtREAL st
 dom F = dom G &
 (for n be Nat st n in dom F holds G.n = F.n /\ A) &
 F,a are_Re-presentation_of f
holds
   G,a are_Re-presentation_of f|A
proof
   let X be non empty set;
   let S be SigmaField of X;
   let f be PartFunc of X,ExtREAL;
   let A be Element of S;
   let F,G be Finite_Sep_Sequence of S;
   let a be FinSequence of ExtREAL;
   assume that
A1: dom F = dom G and
A2: for n be Nat st n in dom F holds G.n = F.n /\ A and
A3: F,a are_Re-presentation_of f;
    now let v be set;
    assume v in dom(f|A);
    then v in dom f /\ A by FUNCT_1:68;
then A4: v in dom f & v in A by XBOOLE_0:def 3;
    then v in union rng F by A3,MESFUNC3:def 1;
    then consider C be set such that
A5:  v in C & C in rng F by TARSKI:def 4;
    consider k be Nat such that
A6:  k in dom F & C = F.k by A5,FINSEQ_2:11;
A7: G.k = F.k /\ A by A2,A6;
A8: G.k in rng G by A1,A6,FUNCT_1:12;
     v in F.k /\ A by A4,A5,A6,XBOOLE_0:def 3;
    hence v in union rng G by A7,A8,TARSKI:def 4;
   end;
then A9:dom(f|A) c= union rng G by TARSKI:def 3;
    now let v be set;
    assume v in union rng G;
    then consider C be set such that
A10: v in C & C in rng G by TARSKI:def 4;
    consider k be set such that
A11: k in dom G & C = G.k by A10,FUNCT_1:def 5;
     k is Nat by A11,ORDINAL1:def 13;
    then G.k = F.k /\ A by A1,A2,A11;
then A12:v in F.k & v in A by A10,A11,XBOOLE_0:def 3;
     F.k in rng F by A1,A11,FUNCT_1:12;
    then v in union rng F by A12,TARSKI:def 4;
    then v in dom f by A3,MESFUNC3:def 1;
    then v in dom f /\ A by A12,XBOOLE_0:def 3;
    hence v in dom(f|A) by FUNCT_1:68;
   end;
then A13: union rng G c= dom(f|A) by TARSKI:def 3;
then A14:dom(f|A) = union rng G by A9,XBOOLE_0:def 10;
A15:dom G = dom a by A1,A3,MESFUNC3:def 1;
    for k be Nat st k in dom G for x be set st x in G.k holds (f|A).x = a.k
   proof
    let k be Nat;
    assume
A16: k in dom G;
    let x be set;
    assume
A17: x in G.k;
     G.k in rng G by A16,FUNCT_1:12;
    then x in union rng G by A17,TARSKI:def 4;
then A18:(f|A).x = f.x by A13,FUNCT_1:70;
     for k be Nat st k in dom G for x be set st x in G.k holds f.x = a.k
    proof
     let k be Nat;
     assume
A19:   k in dom G;
     let x be set;
     assume x in G.k;
     then x in F.k /\ A by A1,A2,A19;
     then x in F.k by XBOOLE_0:def 3;
     hence f.x = a.k by A1,A3,A19,MESFUNC3:def 1;
    end;
    hence (f|A).x = a.k by A16,A17,A18;
   end;
   hence G,a are_Re-presentation_of f|A by A14,A15,MESFUNC3:def 1;
end;

theorem Th43:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL st
f is_simple_func_in S holds dom f is Element of S
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f be PartFunc of X,ExtREAL;
   assume f is_simple_func_in S;
   then ex F be Finite_Sep_Sequence of S st
    dom f = union rng F &
    for n being Nat,x,y being Element of X st
     n in dom F & x in F.n & y in F.n holds f.x = f.y by MESFUNC2:def 5;
   hence thesis by MESFUNC2:34;
end;

Lm3:
for Y being set, p be FinSequence holds
 (for i be Nat st i in dom p holds p.i in Y) implies p is FinSequence of Y
proof let Y be set;
   let p be FinSequence;
   assume
A1: for i be Nat st i in dom p holds p.i in Y;
   let b be set;
   assume b in rng p;
   then ex i be Nat st i in dom p & p.i = b by FINSEQ_2:11;
   hence thesis by A1;
end;

Lm4:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
f is_simple_func_in S & dom f <> {} & g is_simple_func_in S & dom g = dom f
 holds
    f+g is_simple_func_in S & dom(f+g) <> {}
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL such that
A1:f is_simple_func_in S and
A2:dom f <> {} and
A3:g is_simple_func_in S and
A4:dom g = dom f;
   consider F be Finite_Sep_Sequence of S,
      a be FinSequence of ExtREAL such that
A5: F,a are_Re-presentation_of f by A1,MESFUNC3:12;
   consider G be Finite_Sep_Sequence of S,
      b be FinSequence of ExtREAL such that
A6: G,b are_Re-presentation_of g by A3,MESFUNC3:12;
A7:dom f = union rng F & dom g = union rng G by A5,A6,MESFUNC3:def 1;
   set la = len F;
   set lb = len G;
   deffunc FG1(Nat) = F.(($1-'1) div lb + 1) /\ G.(($1-'1) mod lb + 1);
   consider FG be FinSequence such that
A8:len FG = la*lb and
A9:for k be Nat st k in Seg (la*lb) holds FG.k=FG1(k)
from FINSEQ_1:sch 2;
    now let k be Nat;
    assume k in dom FG;
then A10:k in Seg(la*lb) by A8,FINSEQ_1:def 3;
then A11:FG.k = F.((k-'1) div lb + 1) /\ G.((k-'1) mod lb + 1) by A9;
    set i=(k-'1) div lb + 1;
    set j=(k-'1) mod lb + 1;
A12:i >= 0+1 & j >= 0+1 by XREAL_1:8;
    reconsider la'=la,lb'=lb as natural number;
A13:1 <= k & k <= la*lb by A10,FINSEQ_1:3;
then A14:lb <> 0;
then A15:lb > 0;
    then lb >= 0+1 by NAT_1:13;
    then lb' divides (la'*lb') & 1 <= la*lb & 1 <= lb
      by A13,NAT_D:def 3,XXREAL_0:2;
then A16:((la'*lb') -' 1) div lb' = ((la'*lb') div lb') - 1 by NAT_2:17;
     (k -' 1) <= (la*lb -' 1) by A13,BINARITH:60;
    then (k -' 1) div lb <= (la*lb -' 1) div lb by NAT_2:26;
    then (k -' 1) div lb + 1 <= la*lb div lb by A16,XREAL_1:21;
    then i <= la by A14,NAT_D:18;
    then i in Seg la by A12;
    then i in dom F by FINSEQ_1:def 3;
then A17:F.i in rng F by FUNCT_1:12;
     (k -' 1) mod lb < lb by A15,NAT_D:1;
    then j <= lb by NAT_1:13;
    then j in dom G by A12,FINSEQ_3:27;
    then G.j in rng G by FUNCT_1:12;
    then F.i is_measurable M & G.j is_measurable M by A17,MEASURE1:def 12;
    then F.i /\ G.j is_measurable M by MEASURE1:66;
    hence FG.k in S by A11,MEASURE1:def 12;
   end;
   then reconsider FG as FinSequence of S by Lm3;
    for k,l be Nat st k in dom FG & l in dom FG & k <> l holds FG.k misses FG.l
   proof
    let k,l be Nat;
    assume
A18: k in dom FG & l in dom FG & k <> l;
    set i=(k-'1) div lb + 1;
    set j=(k-'1) mod lb + 1;
    set n=(l-'1) div lb + 1;
    set m=(l-'1) mod lb + 1;
A19:k in Seg(la*lb) & l in Seg (la*lb) by A8,A18,FINSEQ_1:def 3;
then A20:FG.k = F.i /\ G.j by A9;
A21:i >= 0+1 & j >= 0+1 & n >= 0+1 & m >= 0+1 by XREAL_1:8;
    reconsider la'=la,lb'=lb as natural number;
A22:1 <= k & k <= la*lb & 1 <= l & l <= la*lb by A19,FINSEQ_1:3;
then A23:lb <> 0;
then A24:lb > 0;
    then lb >= 0+1 by NAT_1:13;
    then lb' divides (la'*lb') & 1 <= la*lb & 1 <= lb
       by A22,NAT_D:def 3,XXREAL_0:2;
then A25:((la'*lb') -' 1) div lb' = ((la'*lb') div lb') - 1 by NAT_2:17;
     (k -' 1) <= (la*lb -' 1) & (l -' 1) <= (la*lb -' 1) by A22,BINARITH:60
;
    then (k -' 1) div lb <= (la*lb div lb) - 1 &
    (l -' 1) div lb <= (la*lb div lb) - 1 by A25,NAT_2:26;
    then (k -' 1) div lb + 1 <= la*lb div lb &
    (l -' 1) div lb + 1 <= la*lb div lb by XREAL_1:21;
    then i <= la & n <= la by A23,NAT_D:18;
    then i in Seg la & n in Seg la by A21;
then A26:i in dom F & n in dom F by FINSEQ_1:def 3;
     (k -' 1) mod lb < lb & (l -' 1) mod lb < lb by A24,NAT_D:1;
    then j <= lb & m <= lb by NAT_1:13;
then A27:j in dom G & m in dom G by A21,FINSEQ_3:27;
A28:now assume A29:i=n & j=m;
      (k-'1)+1=(i-1)*lb+(j-1)+1 & (l-'1)+1=(n-1)*lb+(m-1)+1
       by A24,NAT_D:2;
     then k - 1 + 1 = (i-1)*lb + j & l - 1 + 1 = (n-1)*lb+m by A22,BINARITH:50
;
     hence contradiction by A18,A29;
    end;
    per cases by A28;
    suppose i <> n;
then A30: F.i misses F.n by A26,MESFUNC3:4;
      FG.k /\ FG.l= F.i /\ G.j /\ (F.n /\ G.m) by A9,A19,A20;
     then FG.k /\ FG.l= F.i /\ (G.j /\ (F.n /\ G.m)) by XBOOLE_1:16;
     then FG.k /\ FG.l= F.i /\ (F.n /\ (G.j /\ G.m)) by XBOOLE_1:16;
     then FG.k /\ FG.l= F.i /\ F.n /\ (G.j /\ G.m) by XBOOLE_1:16;
     then FG.k /\ FG.l= {} /\ (G.j /\ G.m) by A30,XBOOLE_0:def 7;
     hence FG.k misses FG.l by XBOOLE_0:def 7;
    end;
    suppose j <> m;
then A31: G.j misses G.m by A27,MESFUNC3:4;
      FG.k /\ FG.l= F.i /\ G.j /\ (F.n /\ G.m) by A9,A19,A20;
     then FG.k /\ FG.l= F.i /\ (G.j /\ (F.n /\ G.m)) by XBOOLE_1:16;
     then FG.k /\ FG.l= F.i /\ (F.n /\ (G.j /\ G.m)) by XBOOLE_1:16;
     then FG.k /\ FG.l= F.i /\ F.n /\ (G.j /\ G.m) by XBOOLE_1:16;
     then FG.k /\ FG.l= F.i /\ F.n /\ {} by A31,XBOOLE_0:def 7;
     hence FG.k misses FG.l by XBOOLE_0:def 7;
    end;
   end;
   then reconsider FG as Finite_Sep_Sequence of S by MESFUNC3:4;
A32:dom f = union rng FG
   proof
     now let z be set;
     assume A33:z in dom f;
     then consider Y be set such that
A34:  z in Y & Y in rng F by A7,TARSKI:def 4;
     consider i be Nat such that
A35:  i in dom F & Y = F.i by A34,FINSEQ_2:11;
      i in Seg len F by A35,FINSEQ_1:def 3;
then A36: 1 <= i & i <= la by FINSEQ_1:3;
     then consider i' being Nat such that
A37:  i = 1 + i' by NAT_1:10;
     consider Z be set such that
A38:  z in Z & Z in rng G by A4,A7,A33,TARSKI:def 4;
     consider j be Nat such that
A39:  j in dom G & Z = G.j by A38,FINSEQ_2:11;
      i'*lb + j is Nat;
     then reconsider k=(i-1)*lb+j as Element of NAT by A37;
      j in Seg len G by A39,FINSEQ_1:def 3;
then A40: 1 <= j & j <= lb by FINSEQ_1:3;
     then consider j' being Nat such that
A41:  j = 1 + j' by NAT_1:10;
      i'*lb >= 0;
     then k >= 0 + j by A37,XREAL_1:8;
then A42: k >= 1 by A40,XXREAL_0:2;
then A43: k -' 1 = k - 1 by BINARITH:50 .= i'*lb + j' by A37,A41;
      j' < lb by A40,A41,NAT_1:13;
then A44: i = (k-'1) div lb +1 & j = (k-'1) mod lb +1
       by A37,A41,A43,NAT_D:def 1,def 2;
      i-1 <= la-1 by A36,XREAL_1:11;
     then (i-1)*lb <= (la - 1)*lb by XREAL_1:66;
then A45: k <= (la - 1) * lb + j by XREAL_1:8;
      (la - 1) * lb + j <= (la - 1) * lb + lb by A40,XREAL_1:8;
     then k <= la*lb by A45,XXREAL_0:2;
then A46: k in Seg (la*lb) by A42;
      z in F.i /\ G.j by A34,A35,A38,A39,XBOOLE_0:def 3;
then A47: z in FG.k by A9,A44,A46;
      k in dom FG by A8,A46,FINSEQ_1:def 3;
     then FG.k in rng FG by FUNCT_1:def 5;
     hence z in union rng FG by A47,TARSKI:def 4;
    end;
    hence dom f c= union rng FG by TARSKI:def 3;
    let z be set;
    assume z in union rng FG;
    then consider Y be set such that
A48: z in Y & Y in rng FG by TARSKI:def 4;
    consider k be Nat such that
A49: k in dom FG & Y = FG.k by A48,FINSEQ_2:11;
    set i=(k-'1) div lb +1;
    set j=(k-'1) mod lb +1;
     k in Seg (la*lb) by A8,A49,FINSEQ_1:def 3;
    then FG.k=F.i /\ G.j by A9;
then A50:z in F.i by A48,A49,XBOOLE_0:def 3;
    reconsider la'=la,lb'=lb as natural number;
     k in Seg len FG by A49,FINSEQ_1:def 3;
then A51:1 <= k & k <= la*lb by A8,FINSEQ_1:3;
then A52:lb <> 0;
    then lb > 0;
    then lb >= 0+1 by NAT_1:13;
    then lb' divides (la'*lb') & 1 <= la*lb & 1 <= lb
      by A51,NAT_D:def 3,XXREAL_0:2;
then A53:((la'*lb') -' 1) div lb' = ((la'*lb') div lb') - 1 by NAT_2:17;
     (k -' 1) <= (la*lb -' 1) by A51,BINARITH:60;
    then (k -' 1) div lb <= (la*lb div lb) - 1 by A53,NAT_2:26;
then A54:i <= la*lb div lb by XREAL_1:21;
A55:i >= 0+1 & j >= 0+1 by NAT_1:13;
     la*lb div lb = la by A52,NAT_D:18;
    then i in Seg la by A54,A55;
    then i in dom F by FINSEQ_1:def 3;
    then F.i in rng F by FUNCT_1:def 5;
    hence thesis by A7,A50,TARSKI:def 4;
   end;
A56:g is finite by A3,MESFUNC2:def 5;
then A57:dom(f+g) = dom f /\ dom g by MESFUNC2:2;
    now let x be Element of X;
    assume
A58:x in dom(f+g);
then A59:|. (f+g).x .| = |. f.x + g.x .| by MESFUNC1:def 3;
     f is finite by A1,MESFUNC2:def 5;
then A60:|. f.x .| < +infty & |. g.x .| < +infty
       by A4,A56,A57,A58,MESFUNC2:def 1;
    then not (f.x = +infty or f.x = -infty) &
    not (g.x = +infty or g.x = -infty) by EXTREAL2:67;
then A61:|. (f+g).x .| <= |. f.x .| + |. g.x .| by A59,EXTREAL2:61;
     |. f.x .| + |. g.x .| <> +infty by A60,SUPINF_2:8;
    then |. f.x .| + |. g.x .| < +infty by XXREAL_0:4;
    hence |. (f+g).x .| < +infty by A61,XXREAL_0:2;
   end;
then A62:f+g is finite by MESFUNC2:def 1;
    for k being Nat,x,y being Element of X st
     k in dom FG & x in FG.k & y in FG.k holds (f+g).x = (f+g).y
   proof
    let k be Nat;
    let x,y be Element of X;
    assume
A63:k in dom FG & x in FG.k & y in FG.k;
    set i=(k-'1) div lb + 1;
    set j=(k-'1) mod lb + 1;
     k in Seg (la*lb) by A8,A63,FINSEQ_1:def 3;
    then FG.k = F.( (k-'1) div lb + 1 ) /\ G.( (k-'1) mod lb  + 1 ) by A9;
then A64:x in F.i & x in G.j & y in F.i & y in G.j by A63,XBOOLE_0:def 3;
     k in Seg len FG by A63,FINSEQ_1:def 3;
then A65:1 <= k & k <= la*lb by A8,FINSEQ_1:3;
A66:i >= 0+1 & j >= 0+1 by XREAL_1:8;
A67:lb <> 0 by A65;
then A68:lb > 0;
    then lb >= 0+1 by NAT_1:13;
    then lb divides (la*lb) & 1 <= la*lb & 1 <= lb
       by A65,NAT_D:def 3,XXREAL_0:2;
then A69:((la*lb) -' 1) div lb = ((la*lb) div lb) - 1 by NAT_2:17;
     (k -' 1) <= (la*lb -' 1) by A65,BINARITH:60;
    then (k -' 1) div lb <= (la*lb div lb) - 1 by A69,NAT_2:26;
    then (k -' 1) div lb + 1 <= la*lb div lb by XREAL_1:21;
    then i <= la by A67,NAT_D:18;
    then i in Seg la by A66;
then A70:i in dom F by FINSEQ_1:def 3;
then A71:f.x=a.i by A5,A64,MESFUNC3:def 1;
     (k -' 1) mod lb < lb by A68,NAT_D:1;
    then j <= lb by NAT_1:13;
    then j in Seg lb by A66;
then A72:j in dom G by FINSEQ_1:def 3;
A73:f.y=a.i by A5,A64,A70,MESFUNC3:def 1;
     FG.k in rng FG by A63,FUNCT_1:def 5;
then A74:x in dom (f+g) & y in dom(f+g) by A4,A32,A57,A63,TARSKI:def 4;
    then (f+g).x= f.x+g.x by MESFUNC1:def 3;
    then (f+g).x= a.i+b.j by A6,A64,A71,A72,MESFUNC3:def 1;
    then (f+g).x= f.y+g.y by A6,A64,A72,A73,MESFUNC3:def 1;
    hence (f+g).x= (f+g).y by A74,MESFUNC1:def 3;
   end;
   hence f+g is_simple_func_in S by A4,A32,A57,A62,MESFUNC2:def 5;
   thus dom (f+g) <> {} by A2,A4,A57;
end;

theorem Th44:
for X be non empty set, S be SigmaField of X,
f,g be PartFunc of X,ExtREAL st
 f is_simple_func_in S & g is_simple_func_in S holds
   f+g is_simple_func_in S
proof
   let X be non empty set;
   let S be SigmaField of X;
   let f,g be PartFunc of X,ExtREAL;
   assume
A1: f is_simple_func_in S & g is_simple_func_in S;
   per cases;
   suppose
A2: dom(f+g) = {};
    then for x be Element of X st x in dom(f+g) holds
     |. (f+g).x .| < +infty;
 then A3:f+g is finite by MESFUNC2:def 1;
     ex F being Finite_Sep_Sequence of S st
     (dom(f+g) = union rng F &
      for n being Nat,x,y being Element of X st
       n in dom F & x in F.n & y in F.n holds (f+g).x = (f+g).y)
    proof
     reconsider EMPTY = {} as Element of S by PROB_1:42;
     deffunc F(Nat) = EMPTY;
     consider F being FinSequence of S such that
  A4: len F = 1 &
      for n be Nat st n in Seg 1 holds F.n = F(n)
from FINSEQ_2:sch 1;
      now let i,j be Nat;
      assume
   A5: i in dom F & j in dom F & i <> j;
      then i in Seg len F & j in Seg len F by FINSEQ_1:def 3;
      then i = 1 & j = 1 by A4,FINSEQ_1:4,TARSKI:def 1;
      hence F.i misses F.j by A5;
     end;
     then reconsider F as Finite_Sep_Sequence of S by MESFUNC3:4;
     take F;
      F = <* F.1 *> by A4,FINSEQ_1:57;
  then A6:rng F = {F.1} by FINSEQ_1:56;
      1 in Seg 1;
     then union rng F = union bool {} by A4,A6,ZFMISC_1:1;
     hence dom(f+g) = union rng F by A2,ZFMISC_1:99;
     hereby let n be Nat;
      let x,y be Element of X;
      assume
   A7: n in dom F & x in F.n & y in F.n;
      then n in Seg 1 by A4,FINSEQ_1:def 3;
      hence (f+g).x = (f+g).y by A4,A7;
     end;
    end;
    hence (f+g) is_simple_func_in S by A3,MESFUNC2:def 5;
   end;
   suppose
A8: dom(f+g) <> {};
    reconsider F = dom f as Element of S by A1,Th43;
    reconsider G = dom g as Element of S by A1,Th43;
     f is without+infty & g is without+infty by A1,Th20;
    then not +infty in rng f & not +infty in rng g by Def4;
then A9: f"{+infty} = {} & g"{+infty} = {} by FUNCT_1:142;
then A10: (dom f /\ dom g)\(f"{+infty}/\g"{-infty} \/ f"{-infty}/\g"{+infty})
      = dom f /\ dom g;
then A11:dom(f+g) = dom f /\ dom g by MESFUNC1:def 3;
     dom f is Element of S & dom g is Element of S by A1,Th43;
    then dom(f+g) in S by A11,FINSUB_1:def 2;
then A12: f|dom(f+g) is_simple_func_in S & g|dom(f+g) is_simple_func_in S by A1
,Th40
;
     dom(f|dom(f+g)) = dom f /\ dom(f+g) &
    dom(g|dom(f+g)) = dom g /\ dom(f+g) by FUNCT_1:68;
then A13: dom(f|dom(f+g)) = dom f /\ dom f /\ dom g &
    dom(g|dom(f+g)) = dom g /\ dom g /\ dom f by A11,XBOOLE_1:16;
    then dom(f|dom(f+g)) = dom(f+g) & dom(g|dom(f+g)) = dom(f+g)
      by A10,MESFUNC1:def 3;
then A14: f|dom(f+g) + g|dom(f+g) is_simple_func_in S by A8,A12,Lm4;
A15:(f|dom(f+g))"{+infty} = dom(f+g) /\ f"{+infty} &
    (g|dom(f+g))"{+infty} = dom(f+g) /\ g"{+infty} by FUNCT_1:139;
A16: dom(f|dom(f+g) + g|dom(f+g))
     = (dom(f|dom(f+g)) /\ dom(g|dom(f+g))) \
       (((f|dom(f+g))"{+infty} /\ (g|dom(f+g))"{-infty}) \/
     ((f|dom(f+g))"{-infty} /\ (g|dom(f+g))"{+infty})) by MESFUNC1:def 3
    .= dom(f+g) by A9,A10,A13,A15,MESFUNC1:def 3;
     for x be Element of X st x in dom(f|dom(f+g) + g|dom(f+g)) holds
      (f|dom(f+g) + g|dom(f+g)).x = (f+g).x
    proof
     let x be Element of X;
     assume
 A17: x in dom(f|dom(f+g) + g|dom(f+g));
     then (f|dom(f+g) + g|dom(f+g)).x
       = (f|dom(f+g)).x + (g|dom(f+g)).x by MESFUNC1:def 3
      .= f.x + (g|dom(f+g)).x by A16,A17,FUNCT_1:72
      .= f.x + g.x by A16,A17,FUNCT_1:72;
     hence thesis by A16,A17,MESFUNC1:def 3;
    end;
    hence f+g is_simple_func_in S by A14,A16,PARTFUN1:34;
   end;
end;

theorem Th45:
for X be non empty set, S be SigmaField of X, M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,c be Real st
 f is_simple_func_in S holds c(#)f is_simple_func_in S
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f be PartFunc of X,ExtREAL;
   let c be Real;
   assume
A1: f is_simple_func_in S;
   set g = c(#)f;
A2:dom g = dom f by MESFUNC1:def 6;
A3:f is finite by A1,MESFUNC2:def 5;
    now let x be Element of X;
    assume
A4:  x in dom g;
    then |. f.x .| < +infty by A2,A3,MESFUNC2:def 1;
    then f.x <> -infty & f.x <> +infty by EXTREAL2:67;
    then R_EAL c * f.x <> +infty & R_EAL c * f.x <> -infty by EXTREAL1:27,28;
    then g.x <> +infty & g.x <> -infty by A4,MESFUNC1:def 6;
    then -infty < g.x & g.x < +infty by XXREAL_0:4,6;
    then -(+infty) < g.x & g.x < +infty by SUPINF_2:def 3;
    hence |. g.x .| < +infty by EXTREAL2:59;
   end;
then A5:g is finite by MESFUNC2:def 1;
   consider G be Finite_Sep_Sequence of S such that
A6:dom f = union rng G and
A7:for n be Nat, x,y be Element of X st
     n in dom G & x in G.n & y in G.n holds f.x = f.y by A1,MESFUNC2:def 5;
    now let n be Nat;
    let x,y be Element of X; assume
A8:n in dom G & x in G.n & y in G.n;
    then G.n in rng G by FUNCT_1:12;
    then x in dom g & y in dom g by A2,A6,A8,TARSKI:def 4;
    then g.x = (R_EAL c)*f.x & g.y = (R_EAL c)*f.y by MESFUNC1:def 6;
    hence g.x = g.y by A7,A8;
   end;
   hence g is_simple_func_in S by A2,A5,A6,MESFUNC2:def 5;
end;

theorem Th46:
for X be non empty set, S be SigmaField of X, M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
 f is_simple_func_in S & g is_simple_func_in S &
 (for x be set st x in dom(f-g) holds g.x <= f.x) holds
 f-g is nonnegative
proof
   let X be non empty set,
      S be SigmaField of X,
      M be sigma_Measure of S,
      f,g be PartFunc of X,ExtREAL such that
A1:f is_simple_func_in S and
A2:g is_simple_func_in S and
A3:for x be set st x in dom(f-g) holds g.x <= f.x;
    f is without+infty & g is without-infty by A1,A2,Th20;
   then not +infty in rng f & not -infty in rng g by Def3,Def4;
then A4:f"{+infty} = {} & g"{-infty} = {} by FUNCT_1:142;
   then (dom f /\ dom g) \ (f"{+infty}/\g"{+infty} \/ f"{-infty}/\g"{-infty})
     = dom f /\ dom g;
then A5:dom (f-g) = dom f /\ dom g by MESFUNC1:def 4;
    for x be set st x in dom f /\ dom g holds
     g.x <= f.x & -infty < g.x & f.x < +infty
   proof
    let x be set;
    assume
A6: x in dom f /\ dom g;
    hence g.x <= f.x by A3,A5;
A7:x in dom f & x in dom g by A6,XBOOLE_0:def 3;
    then not g.x in {-infty} by A4,FUNCT_1:def 13;
    then not g.x = -infty by TARSKI:def 1;
    hence -infty < g.x by XXREAL_0:6;
     not f.x in {+infty} by A4,A7,FUNCT_1:def 13;
    then not f.x = +infty by TARSKI:def 1;
    hence f.x < +infty by XXREAL_0:4;
   end;
   hence f-g is nonnegative by Th27;
end;

theorem Th47:
for X be non empty set, S be SigmaField of X, M be sigma_Measure of S,
    A be Element of S, c be R_eal st
 c <> +infty & c <> -infty holds
   ex f be PartFunc of X,ExtREAL st
    f is_simple_func_in S & dom f = A &
    (for x be set st x in A holds f.x=c)
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let A be Element of S;
   let c be R_eal;
   assume
A1:c <> +infty & c <> -infty;
   defpred P[set] means $1 in A;
   deffunc F(set) = c;
A2:for x be set st P[x] holds F(x) in ExtREAL;
   consider f be PartFunc of X,ExtREAL such that
A3: (for x be set holds x in dom f iff x in X & P[x]) &
    (for x be set st x in dom f holds f.x = F(x)) from PARTFUN1:sch 3(A2);
   take f;
    -infty < c & c < +infty by A1,XXREAL_0:4,6;
   then -(+infty) < c & c < +infty by SUPINF_2:def 3;
   then |.c.| < +infty by EXTREAL2:59;
   then for x be Element of X st x in dom f holds |. f.x .| < +infty by A3;
then A4:f is finite by MESFUNC2:def 1;
A5:dom f c= A
   proof
    let x be set;
    assume x in dom f;
    hence x in A by A3;
   end;
A6:A c= dom f
   proof
    let x be set;
    assume x in A;
    hence thesis by A3;
   end;
    ex F being Finite_Sep_Sequence of S st
    (dom f = union rng F &
     for n being Nat, x,y being Element of X st
      n in dom F & x in F.n & y in F.n holds f.x = f.y)
   proof
    set F = <* dom f *>;
A7:rng F = {dom f} by FINSEQ_1:55;
then A8: rng F = {A} by A5,A6,XBOOLE_0:def 10;
     rng F c= S
    proof
     let z be set;
     assume z in rng F;
     then z = A by A8,TARSKI:def 1;
     hence thesis;
    end;
    then reconsider F as FinSequence of S by FINSEQ_1:def 4;
     now let i,j be Nat; assume
A9:   i in dom F & j in dom F & i <> j;
      dom F = Seg 1 by FINSEQ_1:55;
     then i = 1 & j = 1 by A9,FINSEQ_1:4,TARSKI:def 1;
     hence F.i misses F.j by A9;
    end;
    then reconsider F as Finite_Sep_Sequence of S by MESFUNC3:4;
    take F;
    thus dom f = union rng F by A7,ZFMISC_1:31;
    hereby let n be Nat;
     let x,y be Element of X;
     assume
A10:  n in dom F & x in F.n & y in F.n;
      dom F = Seg 1 by FINSEQ_1:55;
     then n = 1 by A10,FINSEQ_1:4,TARSKI:def 1;
then A11: x in dom f & y in dom f by A10,FINSEQ_1:57;
     then f.x = c by A3;
     hence f.x = f.y by A3,A11;
    end;
   end;
   hence f is_simple_func_in S by A4,MESFUNC2:def 5;
   thus dom f = A by A5,A6,XBOOLE_0:def 10;
   thus for x be set st x in A holds f.x = c by A3,A6;
end;

theorem Th48:
for X be non empty set, S be SigmaField of X, M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL, B,BF be Element of S st
 f is_measurable_on B & BF = dom f /\ B holds f|B is_measurable_on BF
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       B,BF be Element of S such that
A1:f is_measurable_on B & BF = dom f /\ B;
    now let r be real number;
     now let x be set;
      (x in dom(f|B) & ex y being R_eal st y=f|B.x & y < R_EAL r) iff
     x in dom f /\ B & ex y being R_eal st y=f|B.x & y < R_EAL r
            by FUNCT_1:68;
 then A2: x in BF & x in less_dom(f|B,R_EAL r) iff
     x in B & x in dom f & ex y being R_eal st y=(f|B).x & y < R_EAL r
        by A1,MESFUNC1:def 12,XBOOLE_0:def 3;
      x in B & x in dom f implies
      ((ex y being R_eal st y=f.x & y < R_EAL r) iff
      (ex y being R_eal st y=(f|B).x & y < R_EAL r))
     proof
      assume
A3:    x in B & x in dom f;
      then (ex y being R_eal st y=f.x & y < R_EAL r) implies
        f.x=(f|B).x by FUNCT_1:72;
      hence (ex y being R_eal st y=f.x & y < R_EAL r) implies
              ex y being R_eal st y=(f|B).x & y < R_EAL r;
      assume
A4:    ex y being R_eal st y=(f|B).x & y < R_EAL r;
       f.x=(f|B).x by A3,FUNCT_1:72;
      hence ex y being R_eal st y=f.x & y < R_EAL r by A4;
     end;
     then x in BF /\ less_dom(f|B,R_EAL r) iff
     x in B & x in less_dom(f,R_EAL r) by A2,MESFUNC1:def 12,XBOOLE_0:def 3;
     hence x in BF /\ less_dom(f|B,R_EAL r)
           iff x in B /\ less_dom(f,R_EAL r) by XBOOLE_0:def 3;
    end;
    then BF /\ less_dom(f|B,R_EAL r) c= B /\ less_dom(f,R_EAL r) &
    B /\ less_dom(f,R_EAL r) c= BF /\ less_dom(f|B,R_EAL r) by TARSKI:def 3;
    then BF /\ less_dom(f|B,R_EAL r) = B /\ less_dom(f,R_EAL r) by
XBOOLE_0:def 10;
    hence BF /\ less_dom(f|B,R_EAL r) is_measurable_on S by A1,MESFUNC1:def 17;
   end;
   hence thesis by MESFUNC1:def 17;
end;

theorem Th49:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    A be Element of S,
    f,g being PartFunc of X,ExtREAL st
 A c= dom f &
 f is_measurable_on A & g is_measurable_on A &
 f is without-infty & g is without-infty holds
   max+(f+g) + max-f is_measurable_on A
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       A be Element of S,
       f,g be PartFunc of X,ExtREAL;
   assume that
A1:A c= dom f and
A2:f is_measurable_on A & g is_measurable_on A and
A3:f is without-infty & g is without-infty;
    f+g is_measurable_on A by A2,A3,Th37;
then A4:max+(f+g) is_measurable_on A by MESFUNC2:27;
A5:max-f is_measurable_on A by A1,A2,MESFUNC2:28;
    max+(f+g) is nonnegative & max-f is nonnegative by Lm1;
   hence thesis by A4,A5,Lm2;
end;

theorem Th50:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    A be Element of S,
    f,g being PartFunc of X,ExtREAL st
 A c= dom f /\ dom g &
 f is_measurable_on A & g is_measurable_on A &
 f is without-infty & g is without-infty holds
   max-(f+g) + max+f is_measurable_on A
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       A be Element of S,
       f,g be PartFunc of X,ExtREAL;
   assume that
A1:A c= dom f /\ dom g and
A2:f is_measurable_on A & g is_measurable_on A and
A3:f is without-infty & g is without-infty;
    f+g is_measurable_on A & dom(f+g) = dom f /\ dom g
     by A2,A3,Th22,Th37;
then A4:max-(f+g) is_measurable_on A by A1,MESFUNC2:28;
A5:max+ f is_measurable_on A by A2,MESFUNC2:27;
    max-(f+g) is nonnegative & max+f is nonnegative by Lm1;
   hence thesis by A4,A5,Lm2;
end;

theorem Th51:
for X be non empty set, S being SigmaField of X,
    M being sigma_Measure of S, A being set st
 A in S holds 0 <= M.A
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let A be set;
   assume A in S;
   then reconsider A as Element of S;
   reconsider E = {} as Element of S by PROB_1:42;
    E c= A by XBOOLE_1:2;
   then M.E <= M.A by MEASURE1:62;
   hence thesis by MEASURE1:def 11;
end;

Lm5:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL, r be Real st
 dom f in S &
 (for x be set st x in dom f holds f.x = r)
 holds
   f is_simple_func_in S
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f be PartFunc of X,ExtREAL;
   let r be Real;
   assume that
A1:dom f in S and
A2:for x be set st x in dom f holds f.x = r;
   reconsider Df = dom f as Element of S by A1;
    now let x be Element of X;
    assume x in dom f;
    then f.x = R_EAL r by A2;
    then -infty < f.x & f.x < +infty by XXREAL_0:9,12;
    then -(+infty) < f.x & f.x < +infty by SUPINF_2:def 3;
    hence |. f.x .| < +infty by EXTREAL2:59;
   end;
then A3:f is finite by MESFUNC2:def 1;
    ex F being Finite_Sep_Sequence of S st (dom f = union rng F &
    for n being Nat,x,y being Element of X st
     n in dom F & x in F.n & y in F.n holds f.x = f.y)
   proof
    deffunc F(Nat) = Df;
    consider F be FinSequence of S such that
 A4: len F = 1 &
     for n be Nat st n in Seg 1 holds F.n = F(n)
     from FINSEQ_2:sch 1;
     now let i,j be Nat;
     assume A5:i in dom F & j in dom F & i <> j;
     then i in Seg(len F) & j in Seg(len F) by FINSEQ_1:def 3;
     then i = 1 & j = 1 by A4,FINSEQ_1:4,TARSKI:def 1;
     hence F.i misses F.j by A5;
    end;
    then reconsider F as Finite_Sep_Sequence of S by MESFUNC3:4;
    take F;
     1 in Seg 1;
 then A6:F.1 = Df by A4;
     F = <* F.1 *> by A4,FINSEQ_1:57;
    then rng F = {F.1} by FINSEQ_1:55;
    hence dom f = union rng F by A6,ZFMISC_1:31;
    hereby let n be Nat;
     let x,y be Element of X;
     assume
 A7:  n in dom F & x in F.n & y in F.n;
     then n in Seg 1 by A4,FINSEQ_1:def 3;
     then F.n = Df by A4;
     then f.x = R_EAL r & f.y = R_EAL r by A2,A7;
     hence f.x = f.y;
    end;
   end;
   hence thesis by A3,MESFUNC2:def 5;
end;

theorem Th52:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
(ex E1 be Element of S st E1=dom f & f is_measurable_on E1) &
(ex E2 be Element of S st E2=dom g & g is_measurable_on E2) &
f"{+infty} in S & f"{-infty} in S &
g"{+infty} in S & g"{-infty} in S holds
   dom(f+g) in S
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL;
   assume
A1:(ex E1 be Element of S st E1=dom f & f is_measurable_on E1) &
    (ex E2 be Element of S st E2=dom g & g is_measurable_on E2) &
    f"{+infty} in S & f"{-infty} in S & g"{+infty} in S & g"{-infty} in S;
   then consider E1 be Element of S such that
A2: E1=dom f & f is_measurable_on E1;
   consider E2 be Element of S such that
A3: E2=dom g & g is_measurable_on E2 by A1;
A4:dom(f+g)
    = (E1 /\ E2) \(f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty})
     by A2,A3,MESFUNC1:def 3;
    f"{-infty} is_measurable M & g"{+infty} is_measurable M &
   f"{+infty} is_measurable M & g"{-infty} is_measurable M
     by A1,MEASURE1:def 12;
   then f"{-infty} /\ g"{+infty} is_measurable M &
   f"{+infty} /\ g"{-infty} is_measurable M by MEASURE1:66;
   then f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty} is_measurable M
     by MEASURE1:66;
then A5:X \ (f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty})
      is_measurable M by MEASURE1:66;
A6:E1/\E2/\(X \ (f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty}))
    = (E1/\E2/\X) \ (f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty})
       by XBOOLE_1:49
   .=(E1/\E2) \ (f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty})
       by XBOOLE_1:28;
    E1/\E2 is_measurable M by MEASURE1:def 12;
   then E1/\E2/\(X \ (f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty}))
     is_measurable M by A5,MEASURE1:66;
   hence thesis by A4,A6,MEASURE1:def 12;
end;

Lm6:
for X be non empty set,
    S be SigmaField of X,
    A be Element of S,
    f be PartFunc of X,ExtREAL,
    r be real number holds
 A /\ less_dom(f,R_EAL r) = less_dom(f|A,R_EAL r)
proof
   let X be non empty set;
   let S be SigmaField of X;
   let A be Element of S;
   let f be PartFunc of X,ExtREAL;
   let r be real number;
    now let v be set;
    assume v in A /\ less_dom(f,R_EAL r);
then A1: v in A & v in less_dom(f,R_EAL r) by XBOOLE_0:def 3;
    then v in dom f &
    ex w be R_eal st w = f.v & w < R_EAL r by MESFUNC1:def 12;
    then v in A /\ dom f by A1,XBOOLE_0:def 3;
then A2: v in dom(f|A) by FUNCT_1:68;
    consider w be R_eal such that
A3:  w = f.v & w < R_EAL r by A1,MESFUNC1:def 12;
     w = (f|A).v by A1,A3,FUNCT_1:72;
    hence v in less_dom(f|A,R_EAL r) by A2,A3,MESFUNC1:def 12;
   end;
   hence A /\ less_dom(f,R_EAL r) c= less_dom(f|A,R_EAL r) by TARSKI:def 3;
   let v be set; assume
A4: v in less_dom(f|A,R_EAL r);
then A5:v in dom(f|A) by MESFUNC1:def 12;
   consider w being R_eal such that
A6: w = (f|A).v & w < R_EAL r by A4,MESFUNC1:def 12;
    v in dom f /\ A & w = f.v by A5,A6,FUNCT_1:68;
   then v in dom f & v in A & ex w be R_eal st w=f.v & w<R_EAL r
     by A6,XBOOLE_0:def 3;
   then v in A & v in less_dom(f,R_EAL r) by MESFUNC1:def 12;
   hence thesis by XBOOLE_0:def 3;
end;

Lm7:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    A be Element of S,
    f be PartFunc of X,ExtREAL holds
 f|A is_measurable_on A iff f is_measurable_on A
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let A be Element of S;
   let f be PartFunc of X,ExtREAL;
    now assume
A1: f|A is_measurable_on A;
     now let r be real number;
      A /\ less_dom(f|A,R_EAL r) is_measurable_on S by A1,MESFUNC1:def 17;
     then A /\ (A /\ less_dom(f,R_EAL r)) is_measurable_on S by Lm6;
     then A /\ A /\ less_dom(f,R_EAL r) is_measurable_on S by XBOOLE_1:16;
     hence A /\ less_dom(f,R_EAL r) is_measurable_on S;
    end;
    hence f is_measurable_on A by MESFUNC1:def 17;
   end;
   hence f|A is_measurable_on A implies f is_measurable_on A;
   assume
A2: f is_measurable_on A;
    now let r be real number;
     A/\A /\ less_dom(f,R_EAL r) is_measurable_on S by A2,MESFUNC1:def 17;
    then A /\ (A /\ less_dom(f,R_EAL r)) is_measurable_on S by XBOOLE_1:16;
    hence A /\ less_dom(f|A,R_EAL r) is_measurable_on S by Lm6;
   end;
   hence thesis by MESFUNC1:def 17;
end;

Lm8:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
(ex E1 be Element of S st E1=dom f & f is_measurable_on E1) &
(ex E2 be Element of S st E2=dom g & g is_measurable_on E2) &
dom f = dom g
  holds
   ex DFPG be Element of S st DFPG=dom(f+g) & f+g is_measurable_on DFPG
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL such that
A1:(ex E1 be Element of S st E1=dom f & f is_measurable_on E1) &
   (ex E2 be Element of S st E2=dom g & g is_measurable_on E2) &
   dom f = dom g;
   consider E0 be Element of S such that
A2: E0=dom f & f is_measurable_on E0 by A1;
A3:E0 /\ eq_dom(f,+infty) is_measurable_on S &
   E0 /\ eq_dom(f,-infty) is_measurable_on S by A2,MESFUNC1:37,38;
    now let v be set;
    assume v in f"{+infty};
    then v in dom f & f.v in {+infty} by FUNCT_1:def 13;
    then v in dom f & f.v = +infty by TARSKI:def 1;
    then v in E0 & v in eq_dom(f,+infty) by A2,MESFUNC1:def 16;
    hence v in E0 /\ eq_dom(f,+infty) by XBOOLE_0:def 3;
   end;
then A4:f"{+infty} c= E0 /\ eq_dom(f,+infty) by TARSKI:def 3;
    now let v be set;
    assume v in E0 /\ eq_dom(f,+infty);
then A5: v in E0 & v in eq_dom(f,+infty) by XBOOLE_0:def 3;
then A6: v in dom f by MESFUNC1:def 16;
     ex w be R_eal st w = f.v & +infty = w by A5,MESFUNC1:def 16;
    then f.v in {+infty} by TARSKI:def 1;
    hence v in f"{+infty} by A6,FUNCT_1:def 13;
   end;
   then E0 /\ eq_dom(f,+infty) c= f"{+infty} by TARSKI:def 3;
   then f"{+infty} = E0 /\ eq_dom(f,+infty) by A4,XBOOLE_0:def 10;
then A7:f"{+infty} in S by A3,MESFUNC1:def 11;
    now let v be set;
    assume v in f"{-infty};
    then v in dom f & f.v in {-infty} by FUNCT_1:def 13;
    then v in dom f & f.v = -infty by TARSKI:def 1;
    then v in E0 & v in eq_dom(f,-infty) by A2,MESFUNC1:def 16;
    hence v in E0 /\ eq_dom(f,-infty) by XBOOLE_0:def 3;
   end;
then A8:f"{-infty} c= E0 /\ eq_dom(f,-infty) by TARSKI:def 3;
    now let v be set;
    assume v in E0 /\ eq_dom(f,-infty);
then A9: v in E0 & v in eq_dom(f,-infty) by XBOOLE_0:def 3;
then A10: v in dom f by MESFUNC1:def 16;
     ex w be R_eal st w = f.v & -infty = w by A9,MESFUNC1:def 16;
    then f.v in {-infty} by TARSKI:def 1;
    hence v in f"{-infty} by A10,FUNCT_1:def 13;
   end;
   then E0 /\ eq_dom(f,-infty) c= f"{-infty} by TARSKI:def 3;
   then f"{-infty} = E0 /\ eq_dom(f,-infty) by A8,XBOOLE_0:def 10;
then A11:f"{-infty} in S by A3,MESFUNC1:def 11;
   consider Gf be Element of S such that
A12:Gf=dom g & g is_measurable_on Gf by A1;
    Gf /\ eq_dom(g,+infty) is_measurable_on S &
   Gf /\ eq_dom(g,-infty) is_measurable_on S by A12,MESFUNC1:37,38;
then A13:Gf /\ eq_dom(g,+infty) in S & Gf /\ eq_dom(g,-infty) in S
     by MESFUNC1:def 11;
    now let v be set;
    assume v in g"{+infty};
    then v in dom g & g.v in {+infty} by FUNCT_1:def 13;
    then v in dom g & g.v = +infty by TARSKI:def 1;
    then v in Gf & v in eq_dom(g,+infty) by A12,MESFUNC1:def 16;
    hence v in Gf /\ eq_dom(g,+infty) by XBOOLE_0:def 3;
   end;
then A14:g"{+infty} c= Gf /\ eq_dom(g,+infty) by TARSKI:def 3;
    now let v be set;
    assume v in Gf /\ eq_dom(g,+infty);
then A15: v in Gf & v in eq_dom(g,+infty) by XBOOLE_0:def 3;
then A16:  v in dom g by MESFUNC1:def 16;
     ex w be R_eal st w = g.v & +infty = w by A15,MESFUNC1:def 16;
    then g.v in {+infty} by TARSKI:def 1;
    hence v in g"{+infty} by A16,FUNCT_1:def 13;
   end;
   then Gf /\ eq_dom(g,+infty) c= g"{+infty} by TARSKI:def 3;
then A17:g"{+infty} in S by A13,A14,XBOOLE_0:def 10;
    now let v be set;
    assume v in g"{-infty};
    then v in dom g & g.v in {-infty} by FUNCT_1:def 13;
    then v in dom g & g.v = -infty by TARSKI:def 1;
    then v in Gf & v in eq_dom(g,-infty) by A12,MESFUNC1:def 16;
    hence v in Gf /\ eq_dom(g,-infty) by XBOOLE_0:def 3;
   end;
then A18:g"{-infty} c= Gf /\ eq_dom(g,-infty) by TARSKI:def 3;
    now let v be set;
    assume v in Gf /\ eq_dom(g,-infty);
then A19: v in Gf & v in eq_dom(g,-infty) by XBOOLE_0:def 3;
then A20: v in dom g by MESFUNC1:def 16;
     ex w be R_eal st w = g.v & -infty = w by A19,MESFUNC1:def 16;
    then g.v in {-infty} by TARSKI:def 1;
    hence v in g"{-infty} by A20,FUNCT_1:def 13;
   end;
   then Gf /\ eq_dom(g,-infty) c= g"{-infty} by TARSKI:def 3;
then A21:g"{-infty} in S by A13,A18,XBOOLE_0:def 10;
   reconsider NF=f"{+infty}\/f"{-infty} as Element of S by A7,A11,PROB_1:41;
   reconsider NG=g"{+infty}\/g"{-infty} as Element of S by A17,A21,PROB_1:41;
   reconsider NFG = NF \/ NG as Element of S;
   reconsider E = E0 \ NFG as Element of S;
   set f1=f|E;
   set g1=g|E;
A22:dom(f+g) = (dom f /\ dom g)
    \(f"{-infty} /\ g"{+infty} \/ f"{+infty} /\ g"{-infty}) by MESFUNC1:def 3;
    f"{-infty} /\ g"{+infty} c= f"{-infty} & f"{-infty} c= NF &
   f"{+infty} /\ g"{-infty} c= g"{-infty} & g"{-infty} c= NG
     by XBOOLE_1:7,17;
   then f"{-infty}/\g"{+infty} c= NF & f"{+infty}/\g"{-infty} c= NG
     by XBOOLE_1:1;
   then f"{-infty} /\ g"{+infty} \/ f"{+infty} /\ g"{-infty} c= NF \/ NG
     by XBOOLE_1:13;
then A23:E c= dom(f+g) by A1,A2,A22,XBOOLE_1:34;
then A24:(f+g)|E = f1+g1 & dom(f1+g1)=E by Th35;
   reconsider DFPG=dom(f+g) as Element of S by A1,A7,A11,A17,A21,Th52;
   set NFPG=DFPG \ E;
A25:E c= dom f & E c= dom g by A1,A2,XBOOLE_1:36;
then A26:f is_measurable_on E & g is_measurable_on E by A1,MESFUNC1:34;
    dom f /\ E = E & dom g /\ E = E by A25,XBOOLE_1:28;
then A27:f1 is_measurable_on E & g1 is_measurable_on E by A26,Th48;
A28:now let x be set;
    assume A29:x in dom f1;
    then x in dom f /\ E by FUNCT_1:68;
 then A30:x in dom f & x in E by XBOOLE_0:def 3;
 then A31:not x in NFG by XBOOLE_0:def 4;
     for x be set st x in dom f holds f.x in ExtREAL;
    then reconsider ff=f as Function of dom f,ExtREAL by FUNCT_2:5;
 A32:now assume f1.x = +infty;
     then f.x = +infty by A29,FUNCT_1:68;
     then f.x in {+infty} by TARSKI:def 1;
  then A33:x in ff"{+infty} by A30,FUNCT_2:46;
      f"{+infty} c= NF by XBOOLE_1:7;
     hence contradiction by A31,A33,XBOOLE_0:def 2;
    end;
     now assume f1.x = -infty;
     then f.x = -infty by A29,FUNCT_1:68;
     then ff.x in {-infty} by TARSKI:def 1;
  then A34:x in ff"{-infty} by A30,FUNCT_2:46;
      f"{-infty} c= NF by XBOOLE_1:7;
     hence contradiction by A31,A34,XBOOLE_0:def 2;
    end;
    hence -infty < f1.x & f1.x < +infty by A32,XXREAL_0:4,6;
   end;
A35:now let x be set;
    assume A36: x in dom g1;
    then x in dom g /\ E by FUNCT_1:68;
 then A37:x in dom g & x in E by XBOOLE_0:def 3;
 then A38:not x in NFG by XBOOLE_0:def 4;
     for x be set st x in dom g holds g.x in ExtREAL;
    then reconsider gg=g as Function of dom g, ExtREAL by FUNCT_2:5;
 A39:now assume g1.x = +infty;
     then g.x = +infty by A36,FUNCT_1:68;
     then gg.x in {+infty} by TARSKI:def 1;
  then A40:x in gg"{+infty} by A37,FUNCT_2:46;
      g"{+infty} c= NG by XBOOLE_1:7;
     hence contradiction by A38,A40,XBOOLE_0:def 2;
    end;
     now assume g1.x = -infty;
     then g.x = -infty by A36,FUNCT_1:68;
     then gg.x in {-infty} by TARSKI:def 1;
  then A41:x in gg"{-infty} by A37,FUNCT_2:46;
      g"{-infty} c= NG by XBOOLE_1:7;
     hence contradiction by A38,A41,XBOOLE_0:def 2;
    end;
    hence -infty < g1.x & g1.x < +infty by A39,XXREAL_0:4,6;
   end;
    now let x be Element of X;
    assume x in dom f1;
 then A42:-infty < f1.x & f1.x < +infty by A28;
     -(+infty) = -infty by SUPINF_2:def 3;
    hence |.f1.x.| < +infty by A42,EXTREAL2:59;
   end;
then A43:f1 is finite by MESFUNC2:def 1;
    now let x be Element of X;
    assume x in dom g1;
 then A44:-infty < g1.x & g1.x < +infty by A35;
     -(+infty) = -infty by SUPINF_2:def 3;
    hence |.g1.x.| < +infty by A44,EXTREAL2:59;
   end;
   then g1 is finite by MESFUNC2:def 1;
then A45:E =dom(f1+g1) & f1+g1 is_measurable_on E by A23,A27,A43,Th35,
MESFUNC2:7;
A46:for r being real number holds
    DFPG /\ less_dom(f+g,R_EAL r)
      = E/\less_dom(f1+g1,R_EAL r) \/ f"{-infty}/\(DFPG \ g"{+infty})
       \/ g"{-infty}/\(DFPG \ f"{+infty})
   proof
    let r be real number;
    set SL= DFPG/\less_dom(f+g,R_EAL r);
    set SR= E/\less_dom(f1+g1,R_EAL r)
         \/ f"{-infty}/\(DFPG \ g"{+infty}) \/ g"{-infty}/\(DFPG \ f"{+infty});
  A47:now let x be set;
     assume x in SL;
  then A48:x in DFPG & x in less_dom(f+g,R_EAL r) by XBOOLE_0:def 3;
     then x in (dom f /\ dom g)\(f"{-infty}/\g"{+infty}
          \/ f"{+infty}/\g"{-infty}) by MESFUNC1:def 3;
 then A49:not x in f"{+infty}/\g"{-infty} \/ f"{-infty}/\g"{+infty} &
     x in dom f /\ dom g by XBOOLE_0:def 4;
 then A50:x in dom f & x in dom g by XBOOLE_0:def 3;
 then A51:(x in f"{+infty} iff f.x in {+infty}) &
     (x in f"{-infty} iff f.x in {-infty}) &
     (x in g"{+infty} iff g.x in {+infty}) &
     (x in g"{-infty} iff g.x in {-infty}) by FUNCT_1:def 13;
  then A52:(x in f"{+infty} iff f.x=+infty) & (x in f"{-infty} iff f.x=-infty)
&
     (x in g"{+infty} iff g.x=+infty) & (x in g"{-infty} iff g.x=-infty)
        by TARSKI:def 1;
  A53:ex y be R_eal st y=(f+g).x & y < R_EAL r by A48,MESFUNC1:def 12;
  then A54:f.x + g.x < R_EAL r by A48,MESFUNC1:def 3;
 A55:not x in (f"{+infty}/\g"{-infty}) &
     not x in (f"{-infty}/\g"{+infty}) by A49,XBOOLE_0:def 2;
     per cases;
     suppose f.x =-infty;
      then x in f"{-infty} & x in DFPG \ g"{+infty}
        by A48,A51,A55,TARSKI:def 1,XBOOLE_0:def 3,def 4;
      then x in f"{-infty} /\ (DFPG \ g"{+infty}) by XBOOLE_0:def 3;
      then x in E /\ less_dom(f1+g1,R_EAL r)
        \/ f"{-infty} /\ (DFPG \ g"{+infty}) by XBOOLE_0:def 2;
      hence x in SR by XBOOLE_0:def 2;
     end;
     suppose A56:f.x <>-infty;
      per cases;
      suppose g.x = -infty;
       then x in g"{-infty} & x in DFPG \ f"{+infty}
           by A48,A51,A55,TARSKI:def 1,XBOOLE_0:def 3,def 4;
       then x in g"{-infty}/\(DFPG \ f"{+infty}) by XBOOLE_0:def 3;
       hence x in SR by XBOOLE_0:def 2;
      end;
      suppose g.x <> -infty;
       then not (x in f"{-infty}\/f"{+infty} or x in g"{-infty} or x in g"{
+infty})
              by A52,A54,A56,EXTREAL2:27,XBOOLE_0:def 2;
       then not (x in f"{-infty} \/ f"{+infty} \/ g"{-infty} or x in g"{+infty
})
          by XBOOLE_0:def 2;
       then not x in f"{-infty} \/ f"{+infty} \/ g"{-infty} \/ g"{+infty}
          by XBOOLE_0:def 2;
       then not x in NFG by XBOOLE_1:4;
    then A57:x in E by A2,A50,XBOOLE_0:def 4;
       then (f1+g1).x = (f+g).x by A24,FUNCT_1:68;
       then x in less_dom(f1+g1,R_EAL r) by A45,A53,A57,MESFUNC1:def 12;
       then x in E/\less_dom(f1+g1,R_EAL r) by A57,XBOOLE_0:def 3;
       then x in E/\less_dom(f1+g1,R_EAL r) \/ f"{-infty}/\(DFPG \ g"{+infty})
         by XBOOLE_0:def 2;
       hence x in SR by XBOOLE_0:def 2;
      end;
     end;
    end;
     now let x be set;
     assume x in SR;
  then A58:x in E /\ less_dom(f1+g1,R_EAL r) \/ f"{-infty} /\ (DFPG\ g"{+infty}
) or
     x in g"{-infty} /\ (DFPG \ f"{+infty}) by XBOOLE_0:def 2;
     per cases by A58,XBOOLE_0:def 2;
     suppose x in E /\ less_dom(f1+g1,R_EAL r);
  then A59:x in E & x in less_dom(f1+g1,R_EAL r) by XBOOLE_0:def 3;
      then ex y be R_eal st y=(f1+g1).x & y < R_EAL r by MESFUNC1:def 12;
      then (f+g).x < R_EAL r by A24,A59,FUNCT_1:68;
      then x in less_dom(f+g,R_EAL r) by A23,A59,MESFUNC1:def 12;
      hence x in SL by A23,A59,XBOOLE_0:def 3;
     end;
     suppose
   A60:x in f"{-infty} /\ (DFPG \ g"{+infty}) or
      x in g"{-infty} /\ (DFPG \ f"{+infty});
      per cases by A60;
      suppose x in f"{-infty} /\ (DFPG \ g"{+infty});
  then A61:x in f"{-infty} & x in DFPG \ g"{+infty} by XBOOLE_0:def 3;
   then A62:x in DFPG by XBOOLE_0:def 4;
       then x in (dom f /\ dom g)
              \ (f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty})
                by MESFUNC1:def 3;
       then x in dom f /\ dom g by XBOOLE_0:def 4;
       then x in dom f & x in dom g by XBOOLE_0:def 3;
       then (x in f"{+infty} iff f.x in {+infty}) &
       (x in f"{-infty} iff f.x in {-infty}) &
       (x in g"{+infty} iff g.x in {+infty}) &
       (x in g"{-infty} iff g.x in {-infty}) by FUNCT_1:def 13;
       then (x in f"{+infty} iff f.x=+infty) & (x in f"{-infty} iff f.x=-infty
) &
       (x in g"{+infty} iff g.x=+infty) & (x in g"{-infty} iff g.x=-infty)
         by TARSKI:def 1;
   then A63:f.x + g.x = -infty by A61,SUPINF_2:def 2,XBOOLE_0:def 4;
        R_EAL r in REAL by XREAL_0:def 1;
       then -infty < R_EAL r by XXREAL_0:12;
       then (f+g).x < R_EAL r by A62,A63,MESFUNC1:def 3;
       then x in less_dom(f+g,R_EAL r) by A62,MESFUNC1:def 12;
       hence x in DFPG /\ less_dom(f+g,R_EAL r) by A62,XBOOLE_0:def 3;
      end;
      suppose x in g"{-infty}/\(DFPG \ f"{+infty});
  then A64:x in g"{-infty} & x in DFPG \ f"{+infty} by XBOOLE_0:def 3;
   then A65:x in DFPG by XBOOLE_0:def 4;
       then x in (dom f /\ dom g)
            \(f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty})
               by MESFUNC1:def 3;
       then x in dom f /\ dom g by XBOOLE_0:def 4;
       then x in dom f & x in dom g by XBOOLE_0:def 3;
       then (x in f"{+infty} iff f.x in {+infty}) &
       (x in f"{-infty} iff f.x in {-infty}) &
       (x in g"{+infty} iff g.x in {+infty}) &
       (x in g"{-infty} iff g.x in {-infty}) by FUNCT_1:def 13;
       then (x in f"{+infty} iff f.x=+infty) & (x in f"{-infty} iff f.x=-infty
) &
       (x in g"{+infty} iff g.x=+infty) & (x in g"{-infty} iff g.x=-infty)
          by TARSKI:def 1;
   then A66:f.x + g.x = -infty & x in DFPG by A64,SUPINF_2:def 2,XBOOLE_0:def 4
;
        R_EAL r in REAL by XREAL_0:def 1;
       then -infty < R_EAL r by XXREAL_0:12;
       then (f+g).x < R_EAL r & x in DFPG by A66,MESFUNC1:def 3;
       then x in less_dom(f+g,R_EAL r) by MESFUNC1:def 12;
       hence x in DFPG /\ less_dom(f+g,R_EAL r) by A65,XBOOLE_0:def 3;
      end;
     end;
    end;
    hence SL=SR by A47,TARSKI:2;
   end;
    now let r be real number;
A67:DFPG /\ less_dom(f+g,R_EAL r)
     = E /\ less_dom(f1+g1,R_EAL r) \/ f"{-infty}/\(DFPG \ g"{+infty})
      \/ g"{-infty}/\(DFPG \ f"{+infty}) by A46;
     DFPG \ g"{+infty} in S & DFPG \ f"{+infty} in S by A7,A17,PROB_1:44;
    then f"{-infty}/\(DFPG \ g"{+infty}) in S &
    g"{-infty}/\(DFPG \ f"{+infty}) in S by A11,A21,FINSUB_1:def 2;
then A68:f"{-infty}/\(DFPG \ g"{+infty}) \/ g"{-infty}/\(DFPG \ f"{+infty}) in
S
       by PROB_1:41;
     E/\less_dom(f1+g1,R_EAL r) is_measurable_on S by A45,MESFUNC1:def 17;
    then E/\less_dom(f1+g1,R_EAL r) in S by MESFUNC1:def 11;
    then E/\less_dom(f1+g1,R_EAL r) \/ (f"{-infty}/\(DFPG \ g"{+infty})
     \/ g"{-infty}/\(DFPG \ f"{+infty})) in S by A68,PROB_1:41;
    then DFPG /\ less_dom(f+g,R_EAL r) in S by A67,XBOOLE_1:4;
    hence DFPG /\ less_dom(f+g,R_EAL r) is_measurable_on S by MESFUNC1:def 11;
   end;
   then f+g is_measurable_on DFPG by MESFUNC1:def 17;
   hence thesis;
end;

theorem Th53:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
 (ex E1 be Element of S st E1=dom f & f is_measurable_on E1) &
 (ex E2 be Element of S st E2=dom g & g is_measurable_on E2)
  holds
   ex E be Element of S st E=dom(f+g) & (f+g) is_measurable_on E
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL;
   assume that
A1:(ex E1 be Element of S st E1=dom f & f is_measurable_on E1) and
A2:ex E2 be Element of S st E2=dom g & g is_measurable_on E2;
   consider E1 being Element of S such that
A3: E1 = dom f & f is_measurable_on E1 by A1;
   consider E2 being Element of S such that
A4: E2 = dom g & g is_measurable_on E2 by A2;
   set E3 = E1 /\ E2;
   set f1 = f|E3;
   set g1 = g|E3;
A5:E3 c= E1 & E3 c= E2 by XBOOLE_1:17;
   then f is_measurable_on E3 & g is_measurable_on E3 by A3,A4,MESFUNC1:34;
then A6:f1 is_measurable_on E3 & g1 is_measurable_on E3 by Lm7;
    dom f1 = dom f /\ E3 & dom g1 = dom g /\ E3 by FUNCT_1:68;
then A7:dom f1 = E3 & dom g1 = E3 by A3,A4,A5,XBOOLE_1:28;
   then consider E be Element of S such that
A8: E = dom(f1+g1) &
    (f1+g1) is_measurable_on E by A6,Lm8;
A9:dom(f1+g1) = (dom f1 /\ dom g1) \
     (f1"{-infty}/\g1"{+infty} \/ f1"{+infty}/\g1"{-infty}) by MESFUNC1:def 3;
A10:f1"{-infty} = E3 /\ f"{-infty} & f1"{+infty} = E3 /\ f"{+infty} &
   g1"{-infty} = E3 /\ g"{-infty} & g1"{+infty} = E3 /\ g"{+infty}
     by FUNCT_1:139;
then A11:f1"{-infty} /\ g1"{+infty}
    = f"{-infty} /\ (E3 /\ (E3 /\ g"{+infty})) by XBOOLE_1:16
   .= f"{-infty} /\ (E3 /\ E3 /\ g"{+infty}) by XBOOLE_1:16
   .= f"{-infty} /\ g"{+infty} /\ E3 by XBOOLE_1:16;
    f1"{+infty} /\ g1"{-infty}
    = f"{+infty} /\ (E3 /\ (E3 /\ g"{-infty})) by A10,XBOOLE_1:16
   .= f"{+infty} /\ (E3 /\ E3 /\ g"{-infty}) by XBOOLE_1:16
   .= f"{+infty} /\ g"{-infty} /\ E3 by XBOOLE_1:16;
then A12:f1"{-infty}/\g1"{+infty} \/ f1"{+infty}/\g1"{-infty}
    = E3 /\ (f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty})
       by A11,XBOOLE_1:23;
A13:dom(f+g) = (dom f /\ dom g) \
    (f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty}) by MESFUNC1:def 3;
then A14:dom(f+g) = E by A3,A4,A7,A8,A9,A12,XBOOLE_1:47;
   take E;
A15:dom((f+g)|E) = dom(f+g) /\ E by FUNCT_1:68;
    now let v be Element of X; assume
A16:  v in dom((f+g)|E);
    then v in dom(f+g) /\ E by FUNCT_1:68;
then A17: v in dom(f+g) & v in E by XBOOLE_0:def 3;
A18: E c= E3 by A7,A8,A9,XBOOLE_1:36;
A19: ((f+g)|E).v = (f+g).v by A16,FUNCT_1:70
               .= f.v + g.v by A17,MESFUNC1:def 3;
     (f1+g1).v = f1.v + g1.v by A8,A14,A15,A16,MESFUNC1:def 3
             .= f.v + g1.v by A7,A17,A18,FUNCT_1:70;
    hence ((f+g)|E).v = (f1+g1).v by A7,A17,A18,A19,FUNCT_1:70;
   end;
   then (f+g)|E = f1+g1 by A8,A14,A15,PARTFUN1:34;
   hence thesis by A3,A4,A7,A8,A9,A12,A13,Lm7,XBOOLE_1:47;
end;

theorem Th54:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    A,B be Element of S st
 dom f = A holds f is_measurable_on B iff f is_measurable_on (A/\B)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       A,B be Element of S such that
A1:dom f = A;
A2:now let r be real number;
     now let x be set;
      x in A /\ less_dom(f,R_EAL r) iff x in A & x in less_dom(f,R_EAL r)
       by XBOOLE_0:def 3;
     hence x in A /\ less_dom(f,R_EAL r) iff x in less_dom(f,R_EAL r)
       by A1,MESFUNC1:def 12;
    end;
    then A /\ less_dom(f,R_EAL r) c= less_dom(f,R_EAL r) &
    less_dom(f,R_EAL r) c= A /\ less_dom(f,R_EAL r) by TARSKI:def 3;
    hence A /\ less_dom(f,R_EAL r) = less_dom(f,R_EAL r) by XBOOLE_0:def 10;
   end;
   hereby assume
 A3:f is_measurable_on B;
     now let r be real number;
      A /\ B /\ less_dom(f,R_EAL r)
       = B /\ (A /\ less_dom(f,R_EAL r)) by XBOOLE_1:16
      .= B /\ less_dom(f,R_EAL r) by A2;
     hence A/\B/\less_dom(f,R_EAL r) is_measurable_on S by A3,MESFUNC1:def 17;
    end;
    hence f is_measurable_on A/\B by MESFUNC1:def 17;
   end;
   assume
 A4:f is_measurable_on (A/\B);
    now let r be real number;
     A /\ B /\ less_dom(f,R_EAL r)
      = B /\ (A /\ less_dom(f,R_EAL r)) by XBOOLE_1:16
     .=B /\ less_dom(f,R_EAL r) by A2;
    hence B /\ less_dom(f,R_EAL r) is_measurable_on S by A4,MESFUNC1:def 17;
   end;
   hence f is_measurable_on B by MESFUNC1:def 17;
end;

theorem Th55:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL st
 ( ex A be Element of S st dom f = A )
 for c be Real, B be Element of S st f is_measurable_on B holds
   c(#)f is_measurable_on B
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL such that
A1:ex A be Element of S st A = dom f;
   let c be Real, B be Element of S such that
A2: f is_measurable_on B;
   consider A be Element of S such that
A3: A = dom f by A1;
A4:f is_measurable_on A /\ B by A2,A3,Th54;
    A /\ B c= dom f by A3,XBOOLE_1:17;
then A5:c(#)f is_measurable_on A /\ B by A4,MESFUNC1:41;
    dom(c(#)f) = A by A3,MESFUNC1:def 6;
   hence c(#)f is_measurable_on B by A5,Th54;
end;

begin :: Sequence of extended real numbers

definition
  mode ExtREAL_sequence is Function of NAT, ExtREAL;
end;

definition let seq be ExtREAL_sequence;
  attr seq is convergent_to_finite_number means :Def8:
  ex g be real number st
  for p be real number st 0<p
   ex n be Nat st
    for m be Nat st n<=m holds |.seq.m-R_EAL(g).| < p;
end;

definition let seq be ExtREAL_sequence;
  attr seq is convergent_to_+infty means :Def9:
  for g be real number st 0 < g
  ex n be Nat st for m be Nat st n<=m holds g <= seq.m;
end;

definition let seq be ExtREAL_sequence;
  attr seq is convergent_to_-infty means :Def10:
  for g be real number st g < 0
   ex n be Nat st for m be Nat st n<=m holds seq.m <= g;
end;

theorem Th56:
for seq be ExtREAL_sequence st seq is convergent_to_+infty holds
  not seq is convergent_to_-infty & not seq is convergent_to_finite_number
proof
   let seq be ExtREAL_sequence;
   assume
A1: seq is convergent_to_+infty;
   hereby assume seq is convergent_to_-infty;
    then consider n1 being Nat such that
A2:  for m be Nat st n1 <= m holds seq.m <= R_EAL -1 by Def10;
    consider n2 being Nat such that
A3:  for m be Nat st n2 <= m holds R_EAL 1 <= seq.m by A1,Def9;
    reconsider n1,n2 as Element of NAT by ORDINAL1:def 13;
    set m = max(n1,n2);
     m >= n1 & m >= n2 by XXREAL_0:25;
    then seq.m <= R_EAL -1 & R_EAL 1 <= seq.m by A2,A3;
    hence contradiction;
   end;
   assume seq is convergent_to_finite_number;
   then consider g being real number such that
A4: for p be real number st 0 < p
     ex n be Nat st
      for m be Nat st n<=m holds |.seq.m-R_EAL g.| < p by Def8;
   per cases;
   suppose
A5:g > 0;
    then consider n1 be Nat such that
A6: for m be Nat st n1 <= m holds |.seq.m-R_EAL g.| < R_EAL(g) by A4;
A7:now let m be Nat;
     assume n1 <= m;
     then |.seq.m-R_EAL g.| < R_EAL g by A6;
     then seq.m - R_EAL g < R_EAL g by EXTREAL2:58;
     then seq.m < R_EAL g + R_EAL g by EXTREAL2:29;
     then seq.m < R_EAL(g+g) by Th7;
     hence seq.m < R_EAL(2*g);
    end;
     g+g > 0 by A5,XREAL_1:36;
    then consider n2 be Nat such that
    A8: for m be Nat st n2 <= m holds R_EAL(2*g) <= seq.m by A1,Def9;
    reconsider n1,n2 as Element of NAT by ORDINAL1:def 13;
    set m = max(n1,n2);
     n1 <= m & n2 <= m by XXREAL_0:25;
    then seq.m < R_EAL(2*g) & R_EAL(2*g) <= seq.m by A7,A8;
    hence contradiction;
   end;
   suppose
A9:g = 0;
    consider n1 be Nat such that
    A10: for m be Nat st n1 <= m holds |. seq.m-R_EAL g .| < R_EAL 1 by A4;
    consider n2 be Nat such that
    A11: for m be Nat st n2 <= m holds R_EAL 1 <= seq.m by A1,Def9;
    reconsider n1,n2 as Element of NAT by ORDINAL1:def 13;
    set m = max(n1,n2);
A12:n1 <= m & n2 <= m by XXREAL_0:25;
    then |. seq.m-R_EAL g .| < R_EAL 1 by A10;
    then seq.m - R_EAL g < R_EAL 1 by EXTREAL2:58;
    then seq.m < R_EAL 1 + R_EAL g by EXTREAL2:29;
    then seq.m < R_EAL 1 by A9,SUPINF_2:18;
    hence contradiction by A11,A12;
   end;
   suppose
A13:g < 0;
    set g1 = -g;
     g1 > 0 by A13,XREAL_1:60;
    then consider n1 be Nat such that
A14: for m be Nat st n1 <= m holds |.seq.m-R_EAL g.| < R_EAL g1 by A4;
A15:now let m be Element of NAT;
     assume n1 <= m;
     then |.seq.m-R_EAL g.| < R_EAL g1 by A14;
     then seq.m - R_EAL g < R_EAL g1 by EXTREAL2:58;
     then seq.m < R_EAL g1 + R_EAL g by EXTREAL2:29;
     then seq.m < R_EAL(g1+g) by Th7;
     hence seq.m < 0;
    end;
    consider n2 be Nat such that
A16: for m be Nat st n2 <= m holds 1 <= seq.m by A1,Def9;
    reconsider n1,n2 as Element of NAT by ORDINAL1:def 13;
    set m = max(n1,n2);
     n1 <= m & n2 <= m by XXREAL_0:25;
    then seq.m < 0 & 1 <= seq.m by A15,A16;
    hence contradiction;
   end;
end;

theorem Th57:
for seq be ExtREAL_sequence st seq is convergent_to_-infty holds
  not seq is convergent_to_+infty & not seq is convergent_to_finite_number
proof
   let seq be ExtREAL_sequence;
   assume
A1: seq is convergent_to_-infty;
   hereby assume seq is convergent_to_+infty;
    then consider n1 being Nat such that
A2:  for m be Nat st n1 <= m holds R_EAL 1 <= seq.m by Def9;
    consider n2 being Nat such that
A3:  for m be Nat st n2 <= m holds seq.m <= R_EAL -1 by A1,Def10;
    reconsider n1,n2 as Element of NAT by ORDINAL1:def 13;
    set m = max(n1,n2);
     m >= n1 & m >= n2 by XXREAL_0:25;
    then seq.m <= R_EAL -1 & R_EAL 1 <= seq.m by A2,A3;
    hence contradiction;
   end;
   assume seq is convergent_to_finite_number;
   then consider g being real number such that
A4: for p be real number st 0 < p
     ex n be Nat st
      for m be Nat st n<=m holds |.seq.m-R_EAL g.| < p by Def8;
   reconsider g1 = -g as Real by XREAL_0:def 1;
   per cases;
   suppose
A5:g > 0;
then A6: g1 < -0 by XREAL_1:26;
    consider n1 be Nat such that
A7: for m be Nat st n1 <= m holds |.seq.m-R_EAL g.| < R_EAL(g) by A4,A5;
A8:now let m be Element of NAT;
     assume n1 <= m;
     then |.seq.m-R_EAL g.| < R_EAL g by A7;
     then -R_EAL g < seq.m - R_EAL g by EXTREAL2:58;
     then -R_EAL g + R_EAL g < seq.m by EXTREAL2:28;
     hence 0 < seq.m by EXTREAL1:9;
    end;
    consider n2 be Nat such that
A9: for m be Nat st n2 <= m holds seq.m <= R_EAL g1 by A1,A6,Def10;
    reconsider n1,n2 as Element of NAT by ORDINAL1:def 13;
    set m = max(n1,n2);
     n1 <= m & n2 <= m by XXREAL_0:25;
    then seq.m <= R_EAL g1 & 0 < seq.m by A8,A9;
    hence contradiction by A6;
   end;
   suppose
A10:g = 0;
    consider n1 be Nat such that
A11: for m be Nat st n1 <= m holds |. seq.m-R_EAL g .| < R_EAL 1 by A4;
    consider n2 be Nat such that
A12: for m be Nat st n2 <= m holds seq.m <= R_EAL -1 by A1,Def10;
    reconsider n1,n2 as Element of NAT by ORDINAL1:def 13;
    set m = max(n1,n2);
A13:n1 <= m & n2 <= m by XXREAL_0:25;
    then |. seq.m-R_EAL g .| < R_EAL 1 by A11;
    then -R_EAL 1 < seq.m - R_EAL g by EXTREAL2:58;
    then -R_EAL 1 + R_EAL g < seq.m by EXTREAL2:28;
    then -R_EAL 1 < seq.m by A10,SUPINF_2:18;
    then R_EAL -1 < seq.m by Th7;
    hence contradiction by A12,A13;
   end;
   suppose
A14:g < 0;
    then g1 > 0 by XREAL_1:60;
    then consider n1 be Nat such that
A15: for m be Nat st n1 <= m holds |.seq.m-R_EAL g.| < R_EAL g1 by A4;
A16:now let m be Element of NAT;
     assume n1 <= m;
     then |.seq.m-R_EAL g.| < R_EAL g1 by A15;
     then -R_EAL g1 < seq.m - R_EAL g by EXTREAL2:58;
     then -R_EAL g1 + R_EAL g < seq.m by EXTREAL2:28;
     then R_EAL -g1 + R_EAL g < seq.m by Th7;
     then R_EAL(-g1+g) < seq.m by Th7;
     hence R_EAL(2*g) < seq.m;
    end;
     2*g < 0 by A14,XREAL_1:134;
    then consider n2 be Nat such that
A17: for m be Nat st n2 <= m holds seq.m <= R_EAL(2*g) by A1,Def10;
    reconsider n1,n2 as Element of NAT by ORDINAL1:def 13;
    set m = max(n1,n2);
     n1 <= m & n2 <= m by XXREAL_0:25;
    then seq.m <= R_EAL(2*g) & R_EAL(2*g) < seq.m by A16,A17;
    hence contradiction;
   end;
end;

definition let seq be ExtREAL_sequence;
  attr seq is convergent means :Def11:
    seq is convergent_to_finite_number or
    seq is convergent_to_+infty or
    seq is convergent_to_-infty;
end;

definition let seq be ExtREAL_sequence;
assume A1:seq is convergent;
func lim seq -> R_eal means :Def12:
( ex g be real number st it = g &
  (for p be real number st 0<p
    ex n be Nat st
     for m be Nat st n<=m holds |.seq.m-it.| < p) &
   seq is convergent_to_finite_number )  or
( it=+infty & seq is convergent_to_+infty )  or
( it=-infty & seq is convergent_to_-infty );
existence
proof
   defpred P[R_eal] means
   ( ex g be real number st $1=g &
      (for p be real number st 0<p
        ex n be Nat st for m be Nat st n<=m holds |.seq.m-$1.| < p) &
      seq is convergent_to_finite_number ) or
   ( $1=+infty & seq is convergent_to_+infty ) or
   ( $1=-infty & seq is convergent_to_-infty );
   per cases by A1,Def11;
   suppose
A2: seq is convergent_to_finite_number;
    then ex g be real number st
     for p be real number st 0<p
      ex n be Nat st
       for m be Nat st n<=m holds |.seq.m-R_EAL g.| < p by Def8;
    hence ex s be R_eal st P[s] by A2;
   end;
   suppose seq is convergent_to_+infty or seq is convergent_to_-infty;
    hence ex s be R_eal st P[s];
   end;
end;
uniqueness
proof
   defpred P[R_eal] means
   ( ex g be real number st
      $1= g &
      (for p be real number st 0 < p
       ex n be Nat st
        for m be Nat st n<=m holds |.seq.m-$1.| < p) &
       seq is convergent_to_finite_number )
or ( $1=+infty & seq is convergent_to_+infty )
or ( $1=-infty & seq is convergent_to_-infty );
   given g1,g2 be R_eal such that
A3: P[g1] and
A4: P[g2] and
A5: g1<>g2;
   per cases by A1,Def11;
   suppose
A6: seq is convergent_to_finite_number;
    then consider g being real number such that
A7:  g1 = g &
     (for p be real number st 0 < p
       ex n be Nat st
        for m be Nat st n <= m holds |.seq.m-g1.| < p) &
     seq is convergent_to_finite_number by A3,Th56,Th57;
    consider h being real number such that
A8:  g2 = h &
     (for p be real number st 0 < p
       ex n be Nat st
        for m be Nat st n <= m holds |.seq.m-g2.| < p) &
     seq is convergent_to_finite_number by A4,A6,Th56,Th57;
     g - h <> 0 by A5,A7,A8;
    then |.g-h.| > 0 by COMPLEX1:133;
then A9:|.g-h.|/2 > 0 by XREAL_1:141;
    then consider n1 being Nat such that
A10: for m be Nat st n1 <= m holds |.seq.m-g1.| < R_EAL(|.g-h.|/2) by A7;
    consider n2 being Nat such that
A11: for m be Nat st n2 <= m holds |.seq.m-g2.| < R_EAL(|.g-h.|/2) by A8,A9;
    reconsider n1,n2 as Element of NAT by ORDINAL1:def 13;
    set m = max(n1,n2);
     n1 <= m & n2 <= m by XXREAL_0:25;
then A12: |.seq.m-g1.| < R_EAL(|.g-h.|/2) & |.seq.m-g2.| < R_EAL(|.g-h.|/2)
      by A10,A11;
    reconsider g,h as Real by XREAL_0:def 1;
A13: -seq.m + g1 = -(seq.m - g1) by EXTREAL2:15;
    then |.-seq.m+g1.| < R_EAL(|.g-h.|/2) by A12,EXTREAL2:66;
then A14: -R_EAL(|.g-h.|/2) < -seq.m+g1 & -seq.m+g1 < R_EAL(|.g-h.|/2) &
    -R_EAL(|.g-h.|/2) < seq.m-g2 & seq.m-g2 < R_EAL(|.g-h.|/2)
        by A12,EXTREAL2:58;
then A15: -seq.m + g1 in REAL & seq.m-g2 in REAL by MEASURE5:16;
    reconsider w = seq.m - g2 as Real by A14,MEASURE5:16;
     |.w.| in REAL by XREAL_0:def 1;
    then |.seq.m-g2.| in REAL by EXTREAL2:49;
    then |.seq.m-g1.| + |.seq.m-g2.| < R_EAL(|.g-h.|/2) + |.seq.m-g2.| &
    R_EAL(|.g-h.|/2) + |.seq.m-g2.| < R_EAL(|.g-h.|/2) + R_EAL(|.g-h.|/2)
        by A12,MEASURE6:12;
then A16:|.seq.m-g1.| + |.seq.m-g2.| < R_EAL(|.g-h.|/2) + R_EAL(|.g-h.|/2)
        by XXREAL_0:2;
A17: -seq.m + g1 <> +infty & -seq.m + g1 <> -infty &
    seq.m - g2 <> +infty & seq.m - g2 <> -infty &
    seq.m - g2 < +infty & -infty < seq.m - g2 by A15,XXREAL_0:9,12;
then A18: seq.m <> +infty & seq.m <> -infty by A8,EXTREAL2:25,26;
     |.g1-g2.| = |.g1 + R_EAL 0 - g2.| by SUPINF_2:18
             .= |.g1 + (seq.m + -seq.m) - g2.| by EXTREAL1:9
             .= |.-seq.m + g1 + seq.m - g2.| by A7,A18,EXTREAL1:8
             .= |.-seq.m + g1 +(seq.m - g2).| by A8,A17,EXTREAL1:11;
    then |.g1-g2.| <= |.-seq.m + g1.| + |.seq.m - g2.| by A17,EXTREAL2:61;
then A19:|.g1-g2.| <= |.seq.m-g1.| + |.seq.m-g2.| by A13,EXTREAL2:66;
A20:R_EAL(|.g-h.|/2) + R_EAL(|.g-h.|/2) = |.g-h.|/2 + |.g-h.|/2 by Th7;
     g-h = g1-g2 by A7,A8,SUPINF_2:5;
    hence contradiction by A16,A19,A20,EXTREAL2:49;
   end;
   suppose seq is convergent_to_+infty or seq is convergent_to_-infty;
    hence contradiction by A3,A4,A5,Th56,Th57;
   end;
end;
end;

theorem Th58:
for seq be ExtREAL_sequence, r be real number st
 (for n be Nat holds seq.n = r) holds
   seq is convergent_to_finite_number & lim seq = r
proof
   let seq be ExtREAL_sequence;
   let r be real number;
   assume
A1: for n be Nat holds seq.n = r;
A2:now let p be real number;
    assume
A3:   0 < p;
     now let m be Nat such that 1 <= m;
      seq.m = R_EAL r by A1;
     then seq.m - R_EAL r = 0 by EXTREAL1:9;
     hence |. seq.m - R_EAL r .| < p by A3,EXTREAL2:53;
    end;
    hence ex n be Nat st for m be Nat st n<=m holds |.seq.m-R_EAL r.| < p;
   end;
   hence A4: seq is convergent_to_finite_number by Def8;
   then seq is convergent by Def11;
   hence thesis by A2,A4,Def12;
end;

theorem Th59:
for F be FinSequence of ExtREAL st
(for n be Nat st n in dom F holds 0 <= F.n) holds
  0 <= Sum F
proof
   let F be FinSequence of ExtREAL;
   assume
A1:for n be Nat st n in dom F holds 0 <= F.n;
   consider sumf be Function of NAT,ExtREAL such that
A2: Sum F = sumf.(len F) & sumf.0 = 0 &
    for n be Element of NAT st n < len F holds sumf.(n+1) = sumf.n + F.(n+1)
     by CONVFUN1:def 5;
   defpred P[Nat] means $1 <= len F implies 0 <= sumf.($1);
A3:P[0] by A2;
A4:for n be Nat st P[n] holds P[n+1]
   proof
   let n be Nat;
    assume A5:P[n];
    assume
A6:  n+1 <= len F;
    reconsider n as Element of NAT by ORDINAL1:def 13;
     n < len F by A6,NAT_1:13;
then A7: sumf.(n+1) = sumf.n + F.(n+1) by A2;
     1 <= n+1 by NAT_1:11;
    then n+1 in Seg len F by A6;
    then n+1 in dom F by FINSEQ_1:def 3;
    then 0 <= sumf.n & 0 <= F.(n+1) by A1,A5,A6,NAT_1:13;
    hence thesis by A7,EXTREAL2:7;
   end;
    for n be Nat holds P[n] from NAT_1:sch 2(A3,A4);
   hence thesis by A2;
end;

theorem Th60:
for L be ExtREAL_sequence st
  (for n,m be Nat st n <=m holds L.n <= L.m)
 holds L is convergent & lim L = sup rng L
proof
   let L be ExtREAL_sequence;
   assume
A1:for n,m be Nat st n <=m holds L.n <= L.m;
A2:now let n be Nat;
A3: sup rng L is majorant of rng L by SUPINF_1:def 16;
    reconsider n1=n as Element of NAT by ORDINAL1:def 13;

     dom L = NAT by FUNCT_2:def 1;
    then L.n1 in rng L by FUNCT_1:def 5;
    hence L.n <= sup rng L by A3,SUPINF_1:def 9;
   end;
   per cases;
   suppose
A4:not ex k0 be Nat st -infty < L.k0;
A5: for k being Nat holds L.k=-infty by A4,XXREAL_0:6;
     now let K be real number such that K < 0;
      now let n be Nat such that 0 <= n;
       L.n=-infty by A5;
      hence L.n <= R_EAL K by XXREAL_0:5;
     end;
     hence ex N0 be Nat st for n be Nat st N0<=n holds L.n <= K;
    end;
then A6: L is convergent_to_-infty by Def10;
then A7: L is convergent by Def11;
     now let x be ext-real number;
     assume x in rng L;
     then consider n be set such that
A8:  n in dom L and
A9:  x=L.n by FUNCT_1:def 5;
      n is Nat by A8,ORDINAL1:def 13;
     hence x <= -infty by A5,A9;
    end;
then A10:-infty is majorant of rng L by SUPINF_1:def 9;
     for y be ext-real number st y is majorant of rng L holds -infty <= y
      by XXREAL_0:5;
    then -infty =sup rng L by A10,SUPINF_1:def 16;
    hence L is convergent & lim L = sup rng L by A6,A7,Def12;
   end;
   suppose ex k0 be Nat st -infty < L.k0;
    then consider k0 be Nat such that
A11: -infty < L.k0;
    reconsider k0 as Element of NAT by ORDINAL1:def 13;
    per cases;
    suppose ex K be real number st for n be Nat holds L.n < K;
     then consider K be real number such that
A12:  for n be Nat holds L.n < K;
A13: sup rng L <> -infty by A2,A11;
      now let x be ext-real number;
      assume x in rng L;
      then consider z be set such that
A14:   z in dom L and
A15:   x=L.z by FUNCT_1:def 5;
       z is Nat by A14,ORDINAL1:def 13;
      hence x <= R_EAL K by A12,A15;
     end;
     then R_EAL K is majorant of rng L by SUPINF_1:def 9;
then A16:  sup rng L <= R_EAL K by SUPINF_1:def 16;
      R_EAL K is Real by XREAL_0:def 1;
then A17: sup rng L <>+infty by A16,XXREAL_0:9;
A18:for p be real number st 0<p
      ex N0 be Nat st
       for m be Nat st N0<=m holds |. L.m - sup rng L .| < p
     proof
      let p be real number;
      assume
A19:  0 < p;
      reconsider e=p as Real by XREAL_0:def 1;
       sup rng L is Real by A13,A17,XXREAL_0:14;
      then consider y being ext-real number such that
A20:    y in rng L & sup rng L - R_EAL e < y by A19,MEASURE6:33;
      consider x be set such that
A21:    x in dom L & y=L.x by A20,FUNCT_1:def 5;
      reconsider N1=x as Element of NAT by A21;
      set N0=max(N1,k0);
      take N0;
      hereby let n be Nat;
       assume
A22:     N0 <= n;
        N1<=N0 by XXREAL_0:25;
       then N1 <=n by A22,XXREAL_0:2;
       then L.N1 <= L.n by A1;
then A23:   sup rng L - R_EAL e < L.n by A20,A21,XXREAL_0:2;
A24:   L.n <= sup rng L by A2;
        sup rng L + R_EAL 0 <= sup rng L + R_EAL e by A19,SUPINF_2:14;
then A25:   sup rng L <= sup rng L + R_EAL e by SUPINF_2:18;
        now assume
A26:    sup rng L = sup rng L + R_EAL e;
         R_EAL e + sup rng L + -sup rng L
          =R_EAL e + (sup rng L + -sup rng L) by A13,A17,EXTREAL1:8
         .=R_EAL e + R_EAL 0 by EXTREAL1:9
         .=R_EAL e by SUPINF_2:18;
        hence contradiction by A19,A26,EXTREAL1:9;
       end;
       then sup rng L < sup rng L + R_EAL e by A25,XXREAL_0:1;
then A27:   L.n < sup rng L + R_EAL e by A24,XXREAL_0:2;
        sup rng L < L.n + R_EAL e by A23,EXTREAL2:29;
       then sup rng L - L.n < R_EAL e by EXTREAL2:30;
       then -R_EAL e < - (sup rng L - L.n) by SUPINF_2:17;
then A28:   -R_EAL e < L.n -sup rng L by EXTREAL2:15;
        L.n -sup rng L < R_EAL e by A27,EXTREAL2:30;
       hence |.L.n-sup rng L .| < p by A28,EXTREAL2:59;
      end;
     end;
     reconsider h=sup rng L as real number by A13,A17,EXTREAL1:1;
A29:R_EAL h = sup rng L;
then A30:  L is convergent_to_finite_number by A18,Def8;
     hence L is convergent by Def11;
     hence lim L = sup rng L by A18,A29,A30,Def12;
    end;
    suppose
A31:not (ex K be real number st 0 < K & for n be Nat holds L.n < K);
      now let K be real number;
      assume 0 < K;
      then consider N0 be Nat such that
A32:  R_EAL K <= L.N0 by A31;
       now let n be Nat;
       assume N0 <=n;
       then L.N0 <= L.n by A1;
       hence R_EAL K <= L.n by A32,XXREAL_0:2;
      end;
      hence ex N0 be Nat st for n be Nat st N0<=n holds K <= L.n;
     end;
then A33:  L is convergent_to_+infty by Def9;
     hence A34:   L is convergent by Def11;
      now assume
A35:  sup rng L <> +infty;
       L.k0 <= sup(rng L) by A2;
      then reconsider h=sup rng L as Real by A11,A35,XXREAL_0:14;
      set K=max(0,h);
       0 <=K by XXREAL_0:25;
then A36:  0 < K + 1 by XREAL_1:31;
       h <=K by XXREAL_0:25;
then A37:  h+0 < K+1 by XREAL_1:10;
      consider N0 be Nat such that
A38:  R_EAL(K+1) <= L.N0 by A31,A36;
       sup rng L < L.N0 by A37,A38,XXREAL_0:2;
      hence contradiction by A2;
     end;
     hence lim L = sup rng L by A33,A34,Def12;
    end;
   end;
end;

theorem Th61:
 for L,G be ExtREAL_sequence st
  (for n be Nat holds L.n <= G.n)
 holds sup rng L <= sup rng G
proof
   let L,G be ExtREAL_sequence;
   assume
A1:for n be Nat holds L.n <= G.n;
A2:now let n be Element of NAT;
A3: sup rng G is majorant of rng G by SUPINF_1:def 16;
     dom G = NAT by FUNCT_2:def 1;
    then G.n in rng G by FUNCT_1:def 5;
then A4: G.n <= sup rng G by A3,SUPINF_1:def 9;
     L.n <= G.n by A1;
    hence L.n <= sup rng G by A4,XXREAL_0:2;
   end;
    now let x be ext-real number;
    assume x in rng L;
    then ex z be set st z in dom L & x=L.z by FUNCT_1:def 5;
    hence x <= sup rng G by A2;
   end;
   then sup rng G is majorant of rng L by SUPINF_1:def 9;
   hence thesis by SUPINF_1:def 16;
end;

theorem Th62:
for L be ExtREAL_sequence holds
 for n be Nat holds L.n <= sup rng L
proof
   let L be ExtREAL_sequence;
   let n be Nat;
A1:sup rng L is majorant of rng L by SUPINF_1:def 16;
reconsider n as Element of NAT by ORDINAL1:def 13;
    dom L = NAT by FUNCT_2:def 1;
   then L.n in rng L by FUNCT_1:def 5;
   hence thesis by A1,SUPINF_1:def 9;
end;

theorem Th63:
for L be ExtREAL_sequence, K be R_eal st
  (for n be Nat holds L.n <= K) holds sup rng L <= K
proof
   let L be ExtREAL_sequence, K be R_eal;
   assume
A1: for n be Nat holds L.n <= K;
    now let x be ext-real number;
    assume x in rng L;
    then consider z be set such that
A2: z in dom L and
A3: x=L.z by FUNCT_1:def 5;
     z is Nat by A2,ORDINAL1:def 13;
    hence x <= K by A1,A3;
   end;
   then K is majorant of rng L by SUPINF_1:def 9;
   hence thesis by SUPINF_1:def 16;
end;

theorem
 for L be ExtREAL_sequence, K be R_eal st
  K <> +infty & (for n be Nat holds L.n <= K) holds
   sup rng L < +infty
proof
   let L be ExtREAL_sequence, K be R_eal;
   assume that
A1: K <> +infty and
A2: for n be Nat holds L.n <= K;
    now let x be ext-real number;
    assume x in rng L;
    then consider z be set such that
A3: z in dom L and
A4: x=L.z by FUNCT_1:def 5;
     z is Nat by A3,ORDINAL1:def 13;
    hence x <= K by A2,A4;
   end;
   then K is majorant of rng L by SUPINF_1:def 9;
then A5:sup rng L <= K by SUPINF_1:def 16;
    K < +infty by A1,XXREAL_0:4;
   hence thesis by A5,XXREAL_0:2;
end;

theorem Th65:
for L be ExtREAL_sequence st L is without-infty holds
   sup rng L <> +infty
   iff
   ex K be real number st 0 <K & for n be Nat holds L.n <= K
proof
   let L be ExtREAL_sequence;
   assume
A1:L is without-infty;
A2:dom L = NAT by FUNCT_2:def 1;
   then L.1 in rng L by FUNCT_1:12;
then A3:-infty < L.1 & L.1 <= sup rng L by A1,Def5,SUPINF_1:76;
A4:now assume
     sup rng L <> +infty;
    then not sup rng L in {-infty,+infty} by A3,TARSKI:def 2;
    then sup rng L in REAL by XBOOLE_0:def 2,XXREAL_0:def 4;
    then reconsider S = sup rng L as real number by XREAL_0:def 1;
    set K = max(S,1);
 A5:S <= K & 1 <= K by XXREAL_0:25;
  now let n be Nat;
    n in NAT by ORDINAL1:def 13;
   then L.n in rng L by A2,FUNCT_1:12;
     then L.n <= sup rng L by SUPINF_1:76;
     hence L.n <= R_EAL K by A5,XXREAL_0:2;
    end;
    hence ex K be real number st
      0 <K & for n be Nat holds L.n <= R_EAL K by A5;
   end;
    now assume
     ex K be real number st 0 <K & for n be Nat holds L.n <= K;
    then consider K being real number such that
 A6: 0 < K & for n be Nat holds L.n <= K;
     now let w be ext-real number;
     assume w in rng L;
     then consider v be set such that
A7:  v in dom L and
A8:  w = L.v by FUNCT_1:def 5;
      v is Nat by A7,ORDINAL1:def 13;
     hence w <= R_EAL K by A6,A8;
    end;
    then R_EAL K is majorant of rng L by SUPINF_1:def 9;
 then A9:sup rng L <= R_EAL K by SUPINF_1:def 16;
     K in REAL by XREAL_0:def 1;
    hence sup rng L <> +infty by A9,XXREAL_0:9;
   end;
   hence thesis by A4;
end;

theorem Th66:
for L be ExtREAL_sequence,c be R_eal st
 (for n be Nat holds L.n=c) holds
   L is convergent & lim L = c & lim L = sup rng L
proof
   let L be ExtREAL_sequence;
   let c be R_eal;
   assume
A1: for n be Nat holds L.n = c;
A2:c in REAL or c in {-infty,+infty} by XBOOLE_0:def 2,XXREAL_0:def 4;
    now let v be ext-real number;
    assume v in rng L;
    then consider n be set such that
A3: n in dom L and
A4: v = L.n by FUNCT_1:def 5;
     n is Nat by A3,ORDINAL1:def 13;
    hence v <= c by A1,A4;
   end;
then A5:c is majorant of rng L by SUPINF_1:def 9;
    1 in NAT & dom L = NAT by FUNCT_2:def 1;
then A6:L.1 = c & L.1 in rng L by A1,FUNCT_1:12;
   per cases by A2,TARSKI:def 2;
   suppose c in REAL;
    then reconsider rc = c as real number by XREAL_0:def 1;
A7: now let p be real number;
     assume
A8:  0 < p;
      now let m be Nat such that 0 <= m;
       L.m - R_EAL rc = L.m + -L.m by A1;
      then L.m - R_EAL rc = 0 by EXTREAL1:9;
      hence |. L.m - R_EAL rc .| < p by A8,EXTREAL2:53;
     end;
     hence ex n be Nat st for m be Nat st n <= m holds |. L.m-R_EAL rc .| < p;
    end;
then A9: L is convergent_to_finite_number by Def8;
    hence L is convergent by Def11;
    hence lim L = c by A7,A9,Def12;
    hence lim L = sup rng L by A5,A6,SUPINF_1:80;
   end;
   suppose A10:c = -infty;
     for p be real number st p < 0
     ex n be Nat st for m be Nat st n<=m holds L.m <= p
    proof
     let p be real number such that p < 0;
A11: p in REAL by XREAL_0:def 1;
     take 0;
      now let m be Nat such that 0 <= m;
       L.m = -infty by A1,A10;
      hence L.m < p by A11,XXREAL_0:12;
     end;
     hence thesis;
    end;
then A12: L is convergent_to_-infty by Def10;
    hence L is convergent by Def11;
    hence lim(L) = c by A10,A12,Def12;
    hence lim L = sup rng L by A5,A6,SUPINF_1:80;
   end;
   suppose A13:c = +infty;
     for p be real number st 0 < p
     ex n be Nat st for m be Nat st n <= m holds p <= L.m
    proof
     let p be real number such that 0 < p;
A14: p in REAL by XREAL_0:def 1;
     take 0;
      now let m be Nat such that 0 <= m;
       L.m = +infty by A1,A13;
      hence p < L.m by A14,XXREAL_0:9;
     end;
     hence thesis;
    end;
then A15: L is convergent_to_+infty by Def9;
    hence L is convergent by Def11;
    hence lim L = c by A13,A15,Def12;
    hence lim L = sup rng L by A5,A6,SUPINF_1:80;
   end;
end;

Lm9:
for J be ExtREAL_sequence st J is without-infty holds
  sup rng J in REAL or sup rng J = +infty
proof
   let J be ExtREAL_sequence;
   assume
A1:J is without-infty;
    dom J = NAT by FUNCT_2:def 1;
   then J.1 in rng J & -infty < J.1 by A1,Def5,FUNCT_1:12;
   then not -infty is majorant of rng J by SUPINF_1:def 9;
   then sup rng J <> -infty by SUPINF_1:def 16;
   hence sup rng J in REAL or sup rng J = +infty by XXREAL_0:14;
end;

theorem Th67:
for J,K,L be ExtREAL_sequence st
 (for n,m be Nat st n <=m holds J.n <= J.m) &
 (for n,m be Nat st n <=m holds K.n <= K.m) &
 J is without-infty & K is without-infty &
 (for n be Nat holds J.n+K.n =L.n ) holds
   L is convergent & lim L= sup rng L &
   lim L = lim J + lim K &
   sup rng L = sup rng K + sup rng J
proof
   let J,K,L be ExtREAL_sequence;
   assume that
A1: for n,m be Nat st n <=m holds J.n <= J.m and
A2: for n,m be Nat st n <=m holds K.n <= K.m and
A3: J is without-infty & K is without-infty and
A4: for n be Nat holds J.n+K.n =L.n;
A5:dom J = NAT & dom K = NAT & dom L = NAT by FUNCT_2:def 1;
A6:now per cases by A3,Lm9;
    suppose
A7:  sup rng J in REAL;
     then reconsider SJ = sup rng J as real number by XREAL_0:def 1;
     per cases by A3,Lm9;
     suppose
A8:   sup rng K in REAL;
      then reconsider SK = sup rng K as real number by XREAL_0:def 1;
 A9:  R_EAL(SJ+SK) = R_EAL SJ + R_EAL SK by Th7;
 A10:  now let p be real number;
       assume 0 < p;
 then A11:   0 < p/2 by XREAL_1:217;
       then consider SJ' be ext-real number such that
 A12:    SJ' in rng J & sup rng J - R_EAL(p/2) < SJ' by A7,MEASURE6:33;
       consider nj be set such that
 A13:    nj in dom J & SJ' = J.nj by A12,FUNCT_1:def 5;
       reconsider nj as Element of NAT by A13;
       consider SK' be ext-real number such that
 A14:    SK' in rng K & sup rng K - R_EAL(p/2) < SK' by A8,A11,MEASURE6:33;
       consider nk be set such that
 A15:    nk in dom K & SK' = K.nk by A14,FUNCT_1:def 5;
       reconsider nk as Element of NAT by A15;
        p/2 + p/2 = p;
 then A16:  R_EAL p = R_EAL(p/2) + R_EAL(p/2) by Th7;
       reconsider n = max(nj,nk) as Nat;
       take n;
       hereby let m be Nat; assume
    A17: n <= m;
        reconsider m1=m as Element of NAT by ORDINAL1:def 13;
         R_EAL SJ in REAL & R_EAL SK in REAL &
        R_EAL(SJ+SK) in REAL by XREAL_0:def 1;
    then A18: R_EAL SJ < +infty & -infty < R_EAL SJ &
        R_EAL SK < +infty & -infty < R_EAL SK &
        R_EAL(SJ+SK) < +infty & -infty < R_EAL(SJ+SK) by XXREAL_0:9,12;
         J.m1 in rng J & K.m1 in rng K by A5,FUNCT_1:12;
    then A19: J.m <= R_EAL SJ & K.m <= R_EAL SK by SUPINF_1:76;
    then A20: -infty < J.m & J.m < +infty &
        -infty < K.m & K.m < +infty by A3,A18,Def5,XXREAL_0:2;
    then A21: -infty <> J.m + K.m & J.m + K.m <> +infty by SUPINF_2:8,9;
         L.m = J.m + K.m by A4;
        then L.m <= R_EAL(SJ+SK) & L.m < +infty
          by A9,A18,A19,A21,SUPINF_2:14,XXREAL_0:4;
    then A22: 0 <= R_EAL(SJ+SK) - L.m by A18,EXTREAL1:40;
    A23: -(L.m - R_EAL(SJ+SK)) = R_EAL(SJ+SK) - L.m by EXTREAL2:15;
        then L.m - R_EAL(SJ+SK) <= 0 by A22,EXTREAL1:25;
    then A24:|. L.m - R_EAL(SJ+SK) .| = R_EAL(SJ+SK) - L.m by A23,EXTREAL2:55;
    A25: R_EAL SJ - J.m + (R_EAL SK - K.m)
         = R_EAL SJ - J.m + R_EAL SK - K.m by A20,EXTREAL1:11
        .= R_EAL SK + R_EAL SJ - J.m - K.m by EXTREAL1:11
        .= R_EAL(SK+SJ) - (J.m + K.m) by A9,A18,A19,EXTREAL2:40
        .= R_EAL(SK+SJ) - L.m by A4;
        reconsider SJ', SK' as R_eal by XXREAL_0:def 1;
         R_EAL SJ < R_EAL(p/2) + SJ' & R_EAL(p/2) <> -infty &
        R_EAL SK < R_EAL(p/2) + SK' & R_EAL(p/2) <> -infty
          by A12,A14,EXTREAL2:29;
    then A26:R_EAL SJ - SJ' < R_EAL(p/2) & R_EAL SK - SK' < R_EAL(p/2)
          by EXTREAL2:30;
         nk <= n by XXREAL_0:25;
        then nk <= m by A17,XXREAL_0:2;
        then K.nk <= K.m by A2;
        then -infty < SK' & SK' < +infty by A3,A15,A20,Def5,XXREAL_0:2;
        then reconsider s0 = SK' as Real by XXREAL_0:14;
        reconsider s1 = R_EAL SK as Real by XREAL_0:def 1;
         R_EAL SK - SK' = s1 - s0 by SUPINF_2:5;
     then A27:(R_EAL SJ - SJ') + (R_EAL SK - SK') < R_EAL(p/2) + (R_EAL SK -
SK')
          by A26,MEASURE6:12;
        R_EAL(p/2) + (R_EAL SK - SK') < R_EAL(p/2) + R_EAL(p/2)
          by A26,MEASURE6:12;
    then A28:(R_EAL SJ - SJ') + (R_EAL SK - SK') < R_EAL(p/2) + R_EAL(p/2)
          by A27,XXREAL_0:2;
         -infty < SJ' & -infty < SK' by A3,A13,A15,Def5;
        then R_EAL SJ - SJ' <>+infty & R_EAL SK - SK' <>+infty by SUPINF_2:10;
     then A29:R_EAL SJ - SJ' < +infty & R_EAL SK - SK' < +infty by XXREAL_0:4;
         nj <= n & nk <=n by XXREAL_0:25;
        then nj <= m & nk <= m by A17,XXREAL_0:2;
        then SJ' <= J.m & SK' <= K.m by A1,A2,A13,A15;
        then R_EAL SJ - J.m <= R_EAL SJ - SJ' &
        R_EAL SK - K.m <= R_EAL SK - SK' by SUPINF_2:15;
        then (R_EAL SJ - J.m) + (R_EAL SK - K.m)
          <= (R_EAL SJ - SJ') + (R_EAL SK - SK') by A29,SUPINF_2:14;
        hence |. L.m - R_EAL(SJ+SK) .| < p by A16,A24,A25,A28,XXREAL_0:2;
       end;
      end;
   then A30:L is convergent_to_finite_number by Def8;
      hence L is convergent by Def11;
      hence lim L = sup rng J + sup rng K by A9,A10,A30,Def12;
     end;
     suppose
A31:   sup rng K = +infty;
       for p be real number st 0 < p
       ex n be Nat st for m be Nat st n<=m holds p <= L.m
      proof
       let p be real number;
       assume 0 < p;
       then 0 < p/2 by XREAL_1:217;
       then consider j be ext-real number such that
 A32:    j in rng J & sup rng J - R_EAL(p/2) < j by A7,MEASURE6:33;
       consider n1 being set such that
 A33:    n1 in dom J & j = J.n1 by A32,FUNCT_1:def 5;
       reconsider n1 as Element of NAT by A33;
       reconsider supj = sup rng J as Real by A7;
       reconsider p'2 = p/2, p' = p as Real by XREAL_0:def 1;
 A34:   supj - p'2 = sup rng J - R_EAL(p/2) by SUPINF_2:5;
 then A35:  p' - (supj - p'2) = R_EAL p - (sup rng J - R_EAL(p/2))
         by SUPINF_2:5;
       then R_EAL p - (sup rng J - R_EAL(p/2)) < sup rng K by A31,XXREAL_0:9;
       then consider k being R_eal such that
 A36:   k in rng K & R_EAL p - (sup rng J - R_EAL(p/2)) < k by HAHNBAN:8;
       consider n2 being set such that
 A37:   n2 in dom K & k = K.n2 by A36,FUNCT_1:def 5;
       reconsider n2 as Element of NAT by A37;
       set n = max(n1,n2);
        n1 <= n & n2 <= n by XXREAL_0:25;
       then J.n1 <= J.n & K.n2 <= K.n by A1,A2;
 then A38:  sup rng J - R_EAL(p/2) < J.n &
       R_EAL p - (sup rng J - R_EAL(p/2)) < K.n by A32,A33,A36,A37,XXREAL_0:2;
 A39:  -infty < R_EAL p - (sup rng J - R_EAL(p/2)) by A35,XXREAL_0:12;
 A40:  sup rng J is majorant of rng J by SUPINF_1:def 16;
        J.n in rng J by A5,FUNCT_1:12;
 then A41:  J.n <= sup rng J & sup rng J < +infty
         by A7,A40,SUPINF_1:def 9,XXREAL_0:9;
 then A42:  -infty < J.n & J.n < +infty by A3,Def5,XXREAL_0:2;
        p' = (p' - (supj - p'2)) + (supj - p'2);
       then R_EAL p - (sup rng J - R_EAL(p/2)) + (sup rng J - R_EAL(p/2)) = p'
        by A34,A35,SUPINF_2:1;
 then A43:  R_EAL p < J.n + K.n by A38,A39,Th10;
        now let m be Nat;
        assume n <= m;
  then A44:   J.n <= J.m & K.n <= K.m by A1,A2;
        reconsider m1=m as Element of NAT by ORDINAL1:def 13;
         J.m1 in rng J by A5,FUNCT_1:12;
        then J.m <= sup rng J by A40,SUPINF_1:def 9;
        then J.n + K.n <= J.m + K.m by A41,A42,A44,SUPINF_2:14;
        then J.n + K.n <= L.m by A4;
        hence p <= L.m by A43,XXREAL_0:2;
       end;
       hence ex n be Nat st for m be Nat st n <= m holds p <= L.m;
      end;
   then A45:L is convergent_to_+infty by Def9;
      hence L is convergent by Def11;
   then A46:lim L = +infty by A45,Def12;
       -infty < sup rng J by A7,XXREAL_0:12;
      hence lim L = sup rng J + sup rng K by A31,A46,SUPINF_2:def 2;
     end;
    end;
    suppose
A47:  sup rng J = +infty;
      now let p be real number;
      assume 0 < p;
 then A48:  0 < p/2 by XREAL_1:217;
      per cases by A3,Lm9;
      suppose
  A49:  sup rng K in REAL;
       then consider k be ext-real number such that
  A50:   k in rng K & sup rng K - R_EAL(p/2) < k by A48,MEASURE6:33;
       consider n1 being set such that
  A51:   n1 in dom K & k = K.n1 by A50,FUNCT_1:def 5;
       reconsider n1 as Element of NAT by A51;
       reconsider supk = sup rng K as Real by A49;
       reconsider p'2 = p/2, p' = p as Real by XREAL_0:def 1;
  A52:  supk - p'2 = sup rng K - R_EAL(p/2) by SUPINF_2:5;
  then A53:  p' - (supk - p'2) = R_EAL p - (sup rng K - R_EAL(p/2))
         by SUPINF_2:5;
       then R_EAL p - (sup rng K - R_EAL(p/2)) < sup rng J by A47,XXREAL_0:9;
       then consider j being R_eal such that
  A54:   j in rng J & R_EAL p - (sup rng K - R_EAL(p/2)) < j by HAHNBAN:8;
       consider n2 being set such that
  A55:  n2 in dom J & j = J.n2 by A54,FUNCT_1:def 5;
       reconsider n2 as Element of NAT by A55;
       set n = max(n1,n2);
        n1 <= n & n2 <= n by XXREAL_0:25;
       then J.n2 <= J.n & K.n1 <= K.n by A1,A2;
 then A56:  sup rng K - R_EAL(p/2) < K.n &
       R_EAL p - (sup rng K - R_EAL(p/2)) < J.n by A50,A51,A54,A55,XXREAL_0:2;
 A57:  -infty < R_EAL p - (sup rng K - R_EAL(p/2)) by A53,XXREAL_0:12;
 A58:  sup rng K is majorant of rng K by SUPINF_1:def 16;
        K.n in rng K by A5,FUNCT_1:12;
 then A59:  K.n <= sup rng K & sup rng K < +infty
         by A49,A58,SUPINF_1:def 9,XXREAL_0:9;
 then A60:  -infty < K.n & K.n < +infty by A3,Def5,XXREAL_0:2;
        p' = p' - (supk - p'2) + (supk - p'2);
       then R_EAL p - (sup rng K - R_EAL(p/2)) + (sup rng K - R_EAL(p/2)) = p'
        by A52,A53,SUPINF_2:1;
 then A61:  R_EAL p < J.n + K.n by A56,A57,Th10;
        now let m be Nat;
        assume n <= m;
  then A62:   J.n <= J.m & K.n <= K.m by A1,A2;
        reconsider m1=m as Element of NAT by ORDINAL1:def 13;
         K.m1 in rng K by A5,FUNCT_1:12;
        then K.m <= sup rng K by A58,SUPINF_1:def 9;
        then J.n + K.n <= J.m + K.m by A59,A60,A62,SUPINF_2:14;
        then J.n + K.n <= L.m by A4;
        hence p <= L.m by A61,XXREAL_0:2;
       end;
       hence ex n be Nat st for m be Nat st n <= m holds p <= L.m;
      end;
      suppose
        sup rng K = +infty;
       then consider n1 be Nat such that
  A63:   R_EAL(p/2) < K.n1 by A3,A48,Th65;
       consider n2 be Nat such that
  A64:   R_EAL(p/2) < J.n2 by A3,A47,A48,Th65;
        reconsider n1,n2 as Element of NAT by ORDINAL1:def 13;
       set n = max(n1,n2);
        n1 <= n & n2 <= n by XXREAL_0:25;
       then J.n2 <= J.n & K.n1 <= K.n by A1,A2;
  then A65:  R_EAL(p/2) < J.n & R_EAL(p/2) < K.n by A63,A64,XXREAL_0:2;
       -infty < R_EAL(p/2) by XXREAL_0:12;
  then A66:  R_EAL(p/2) + R_EAL(p/2) < J.n + K.n by A65,Th10;
        p/2 + p/2 = p;
       then R_EAL p < J.n + K.n by A66,Th7;
  then A67:  R_EAL p < L.n by A4;
        now let m be Nat;
        assume n <= m;
  then A68:   J.n <= J.m & K.n <= K.m by A1,A2;
         -infty < J.n & -infty < K.n by A3,Def5;
        then J.n + K.n <= J.m + K.m by A68,SUPINF_2:14;
        then J.n + K.n <= L.m by A4;
        then L.n <= L.m by A4;
        hence p <= L.m by A67,XXREAL_0:2;
       end;
       hence ex n be Nat st for m be Nat st n <= m holds p <= L.m;
      end;
     end;
  then A69:L is convergent_to_+infty by Def9;
     hence L is convergent by Def11;
  then A70:lim L = +infty by A69,Def12;
      K.0 in rng K by A5,FUNCT_1:12;
     then -infty < K.0 & K.0 <= sup rng K by A3,Def5,SUPINF_1:76;
     hence lim L = sup rng J + sup rng K by A47,A70,SUPINF_2:def 2;
    end;
   end;
   hence L is convergent;
A71:now let n,m be Nat;
    assume n <= m;
 then A72:J.n <= J.m & K.n <= K.m by A1,A2;
     -infty < J.n & -infty < J.m &
    -infty < K.n & -infty < K.m by A3,Def5;
    then J.n + K.n <= J.m + K.m by A72,SUPINF_2:14;
    then L.n <= J.m + K.m by A4;
    hence L.n <= L.m by A4;
   end;
   hence lim L= sup rng L by Th60;
    lim J = sup rng J & lim K = sup rng K by A1,A2,Th60;
   hence lim L = lim J + lim K &
   sup rng L= sup rng K + sup rng J by A6,A71,Th60;
end;

theorem Th68:
for L,K be ExtREAL_sequence,c be Real st
  0 <= c & L is without-infty &
  (for n be Nat holds K.n = R_EAL c * L.n ) holds
    sup rng K = R_EAL c * sup rng L & K is without-infty
proof
   let L,K be ExtREAL_sequence;
   let c be Real;
   assume that
A1: 0 <= c and
A2: L is without-infty and
A3: for n be Nat holds K.n = R_EAL c * L.n;
A4:-infty < R_EAL c & R_EAL c < +infty by XXREAL_0:9,12;
    now
   per cases by A2,Lm9;
    suppose
A5:  sup rng L in REAL;
then A6: -infty < sup rng L & sup rng L < +infty by XXREAL_0:9,12;
      now let x be ext-real number;
      assume x in rng K;
      then consider m be set such that
A7:    m in dom K & x = K.m by FUNCT_1:def 5;
A8:   dom K = NAT & dom L = NAT by FUNCT_2:def 1;
      reconsider m as Element of NAT by A7;
A9:   x = R_EAL c * L.m by A3,A7;
A10:   sup rng L is majorant of rng L by SUPINF_1:def 16;
       L.m in rng L by A8,FUNCT_1:12;
      then L.m <= sup rng L by A10,SUPINF_1:def 9;
      hence x <= R_EAL c*sup rng L by A1,A9,EXTREAL1:42;
     end;
then A11:  R_EAL c * sup rng L is majorant of rng K by SUPINF_1:def 9;
      for y being ext-real number st y is majorant of rng K
      holds R_EAL c * sup rng L <= y
     proof let y be ext-real number;
      assume
A12:   y is majorant of rng K;
       reconsider y as R_eal by XXREAL_0:def 1;
A13:   dom L = NAT & dom K = NAT by FUNCT_2:def 1;
      per cases;
      suppose
   A14: R_EAL c = 0;
   then A15: R_EAL c * sup rng L = 0 by EXTREAL1:22;
        K.1 in rng K by A13,FUNCT_1:12;
       then K.1 = R_EAL c * L.1 & K.1 <= y by A3,A12,SUPINF_1:def 9;
       hence thesis by A14,A15,EXTREAL1:22;
      end;
      suppose
   A16: R_EAL c <> 0;
        now let x be ext-real number;
        assume x in rng L;
        then consider n be set such that
     A17: n in dom L & x = L.n by FUNCT_1:def 5;
        reconsider n as Element of NAT by A17;
         K.n = R_EAL c * L.n & K.n in rng K by A3,A13,FUNCT_1:12;
        then R_EAL c * L.n <= y by A12,SUPINF_1:def 9;
        then R_EAL c * L.n / R_EAL c <= y/R_EAL c by A1,A16,EXTREAL1:48;
        hence x <= y / R_EAL c by A4,A16,A17,EXTREAL2:19;
       end;
       then y/R_EAL c is majorant of rng L by SUPINF_1:def 9;
   then A18:sup rng L <= y/R_EAL c by SUPINF_1:def 16;
   A19: now assume
     A20:y = -infty;
         K.1 in rng K by A13,FUNCT_1:12;
        then K.1 <= -infty by A12,A20,SUPINF_1:def 9;
        then K.1 = -infty by XXREAL_0:6;
     then A21:R_EAL c * L.1 = -infty by A3;
         L.1 in rng L by A13,FUNCT_1:12;
        then L.1 <= sup rng L by SUPINF_1:76;
        then -infty < L.1 & L.1 < +infty by A2,A6,Def5,XXREAL_0:2;
        hence contradiction by A21,EXTREAL1:28;
       end;
       per cases by A19,XXREAL_0:14;
       suppose y = +infty;
        hence thesis by XXREAL_0:4;
       end;
       suppose y in REAL;
        then reconsider ry = y as Real;
        reconsider sl = sup rng L as Real by A5;
         y/R_EAL c = ry/c by A16,EXTREAL1:32;
        then sl*c <= ry by A1,A16,A18,XREAL_1:85;
        hence thesis by EXTREAL1:13;
       end;
      end;
     end;
     hence sup rng K = R_EAL c * sup rng L by A11,SUPINF_1:def 16;
    end;
    suppose
A22:  sup rng L = +infty;
     per cases;
     suppose
A23:   c = 0;
       now let n be Nat;
        K.n = R_EAL c * L.n by A3;
       hence K.n = R_EAL 0 by A23,EXTREAL1:22;
      end;
      then lim K = sup rng K & lim K = R_EAL 0 by Th66;
      hence sup rng K = R_EAL c * sup rng L by A23,EXTREAL1:22;
     end;
     suppose
A24:  c <> 0;
then A25:   R_EAL c * sup rng L = +infty by A1,A22,EXTREAL1:def 1;
       now let n be set;
        -infty < L.n by A2,Def5;
then A26:    -infty*R_EAL c < R_EAL c * L.n by A1,A24,EXTREAL1:44;
       per cases;
       suppose n in dom K;
        then reconsider n1=n as Element of NAT;
         -infty*R_EAL c = -infty by A1,A24,EXTREAL1:def 1;
        then -infty < K.n1 by A3,A26;
        hence -infty < K.n;
       end;
       suppose not n in dom K;
        then K.n = 0 by FUNCT_1:def 4;
        hence -infty < K.n;
       end;
      end;
then A27:   K is without-infty by Def5;
       now let k be real number;
       assume 0 < k;
       then 0 < k/c by A1,A24,XREAL_1:141;
       then consider n be Nat such that
  A28:   R_EAL(k/c) < L.n by A2,A22,Th65;
  A29:  R_EAL(k/c) * R_EAL c < R_EAL c * L.n by A1,A24,A28,EXTREAL1:44;
       reconsider k1 = k as Real by XREAL_0:def 1;
        R_EAL(k/c)*R_EAL c = k1/c * c by EXTREAL1:13
         .= k1/(c/c) by XCMPLX_1:83
         .= k by A24,XCMPLX_1:51;
       then R_EAL k < K.n by A3,A29;
       hence ex n be Nat st not K.n <= k;
      end;
      hence sup rng K = R_EAL c * sup rng L by A25,A27,Th65;
     end;
    end;
   end;
   hence sup rng K = R_EAL c * sup rng L;
    now let n be set;
 A30:-infty < L.n by A2,Def5;
     L.n = +infty implies R_EAL c * L.n <> -infty by A1,EXTREAL2:45;
 then A31:-infty <> R_EAL c * L.n by A30,EXTREAL1:28;
    per cases;
    suppose n in dom K;
     then reconsider n1=n as Element of NAT;
      K.n1 <> -infty by A3,A31;
     hence -infty < K.n by XXREAL_0:6;
    end;
    suppose not n in dom K;
     then K.n = 0 by FUNCT_1:def 4;
     hence -infty < K.n;
    end;
   end;
   hence K is without-infty by Def5;
end;

theorem Th69:
for L,K be ExtREAL_sequence, c be Real st
  0 <=c &
  (for n,m be Nat st n <=m holds L.n <= L.m) &
  (for n be Nat holds K.n = R_EAL c * L.n) &
  L is without-infty holds
    ( (for n,m be Nat st n <=m holds K.n <= K.m) &
      K is without-infty &
      K is convergent & lim K = sup rng K & lim K = R_EAL c * lim L )
proof
   let L,K be ExtREAL_sequence, c be Real;
   assume that
A1:0 <= c and
A2:(for n,m be Nat st n <=m holds L.n <= L.m) and
A3:(for n be Nat holds K.n = R_EAL c * L.n ) and
A4:L is without-infty;
   thus
A5:now let n,m be Nat;
    assume n <= m;
    then L.n <= L.m by A2;
    then R_EAL c * L.n <= R_EAL c * L.m by A1,EXTREAL1:42;
    then K.n <= R_EAL c * L.m by A3;
    hence K.n <= K.m by A3;
   end;
   thus K is without-infty by A1,A3,A4,Th68;
   thus K is convergent & lim K= sup rng K by A5,Th60;
    sup rng K = lim K & sup rng L = lim L by A2,A5,Th60;
   hence lim K = R_EAL c * lim L by A1,A3,A4,Th68;
end;

begin :: Sequence of extended real valued functions

definition
let X be non empty set,
    H be Functional_Sequence of X,ExtREAL,
    x be Element of X;
func H#x -> ExtREAL_sequence means :Def13:
for n be Nat holds it.n = (H.n).x;
existence
proof
   deffunc U(Nat) = (H.$1).x;
   consider f be Function of NAT,ExtREAL such that
A1: for n be Element of NAT holds f.n = U(n) from FUNCT_2:sch 4;
take f;
let n be Nat;
 n in NAT by ORDINAL1:def 13;
hence thesis by A1;
end;
uniqueness
proof
   let S1,S2 be ExtREAL_sequence such that
A2: (for n be Nat holds S1.n = (H.n).x) & for n be Nat holds S2.n = (H.n).x;
    now let n be Element of NAT;
     S1.n = (H.n).x & S2.n = (H.n).x by A2;
    hence S1.n = S2.n;
   end;
   hence thesis by FUNCT_2:113;
end;
end;

definition let D1,D2 be set, F be Function of NAT,PFuncs(D1,D2), n be Nat;
redefine func F.n -> PartFunc of D1,D2;
coherence
proof
    n in NAT by ORDINAL1:def 13;
   then n in dom F by FUNCT_2:def 1;
   then F.n in rng F by FUNCT_1:def 5;
   hence thesis by PARTFUN1:120;
end;
end;

theorem Th70:
for X be non empty set, S be SigmaField of X,
    f be PartFunc of X,ExtREAL st
 (ex A be Element of S st A = dom f & f is_measurable_on A) &
 f is nonnegative
holds
  ex F be Functional_Sequence of X,ExtREAL st
   (for n be Nat holds F.n is_simple_func_in S & dom(F.n) = dom f) &
   (for n be Nat holds F.n is nonnegative) &
   (for n,m be Nat st n <=m holds
     for x be Element of X st x in dom f holds (F.n).x <= (F.m).x ) &
   (for x be Element of X st x in dom f holds
     (F#x) is convergent & lim(F#x) = f.x)
proof
   let X be non empty set,
       S be SigmaField of X,
       f be PartFunc of X,ExtREAL such that
A1: ex A be Element of S st A = dom f & f is_measurable_on A and
A2: f is nonnegative;
   consider A be Element of S such that
A3:A = dom f & f is_measurable_on A by A1;
   defpred PF[Nat,PartFunc of X,ExtREAL] means
    dom $2 = dom f &
    (for x be Element of X st x in dom f holds
      ( for k be Nat st
         1 <= k & k <= 2|^$1*$1 &
         (k-1)/(2|^$1) <= f.x &
         f.x < k/(2|^$1) holds $2.x= (k-1)/(2|^$1) ) &
      ( $1 <= f.x implies $2.x= $1 ));
A4:for n be Element of NAT
    ex y being Element of PFuncs(X,ExtREAL) st PF[n,y]
   proof
    let n be Element of NAT;
    defpred PP[set,set] means
    ( for k be Nat st
       1 <= k & k <= 2|^n*n &
       (k-1)/(2|^n) <= f.$1 &
       f.$1 < k/(2|^n) holds $2=(k-1)/(2|^n)) &
    ( n <= f.$1 implies $2 = n);
A5:for x be set st x in dom f ex y be set st PP[x,y]
    proof
     let x be set; assume x in dom f;
     per cases;
     suppose
A6:   f.x < n;
       0 <= f.x by A2,SUPINF_2:70;
      then consider k be Nat such that
A7:   1 <= k & k <= 2|^n*n &
       (k-1)/(2|^n) <= f.x & f.x < k/(2|^n) by A6,Th8;
      take y = (k-1)/(2|^n);
       now let k1 be Nat; assume
A8:   1 <= k1 & k1 <= 2|^n*n & (k1-1)/(2|^n) <= f.x & f.x < k1/(2|^n);
A9:   now assume k < k1;
        then k+1 <= k1 by NAT_1:13;
        then k <= k1 - 1 by XREAL_1:21;
        then k/(2|^n) <= (k1-1)/(2|^n) by XREAL_1:74;
        hence contradiction by A7,A8,XXREAL_0:2;
       end;
        now assume k1 < k;
        then k1+1 <= k by NAT_1:13;
        then k1 <= k - 1 by XREAL_1:21;
        then k1/(2|^n) <= (k-1)/(2|^n) by XREAL_1:74;
        hence contradiction by A7,A8,XXREAL_0:2;
       end;
       hence y=(k1-1)/(2|^n) by A9,XXREAL_0:1;
      end;
      hence PP[x,y] by A6;
     end;
     suppose
A10:   n <= f.x;
      reconsider y = n as Real;
      take y;
      thus PP[x,y] by A10,Th9;
     end;
    end;
A11:for x,y1,y2 be set st x in dom f & PP[x,y1] & PP[x,y2] holds y1 = y2
    proof
     let x,y1,y2 be set;
     assume
A12:  x in dom f & PP[x,y1] & PP[x,y2];
      now assume
A13:  f.x < R_EAL n;
       0 <= f.x by A2,SUPINF_2:70;
      then consider k be Nat such that
A14:   1 <= k & k <= 2|^n*n &
       (k-1)/(2|^n) <= f.x & f.x < k/(2|^n) by A13,Th8;
       y1 = (k-1)/(2|^n) by A12,A14;
      hence y1 = y2 by A12,A14;
     end;
     hence y1 = y2 by A12;
    end;
    consider fn be Function such that
A15:  dom fn = dom f &
     for x be set st x in dom f holds PP[x,fn.x] from FUNCT_1:sch 2(A11,A5);
     rng fn c= ExtREAL
    proof
      now let w be set;
      assume w in rng fn;
      then consider v be set such that
A16:   v in dom fn & w = fn.v by FUNCT_1:def 5;
      per cases;
      suppose R_EAL n <= f.v;
       then fn.v = R_EAL n by A15,A16;
       hence w in ExtREAL by A16;
      end;
      suppose
A17:   f.v < R_EAL n;
        0 <= f.v by A2,SUPINF_2:70;
       then consider k be Nat such that
A18:    1 <= k & k <= 2|^n*n &
        (k-1)/(2|^n) <= f.v & f.v < k/(2|^n) by A17,Th8;
        fn.v = (k-1)/(2|^n) by A15,A16,A18;
       hence w in ExtREAL by A16,XXREAL_0:def 1;
      end;
     end;
     hence rng fn c= ExtREAL by TARSKI:def 3;
    end;
    then reconsider fn as PartFunc of dom f,ExtREAL by A15,PARTFUN1:25;
    reconsider fn as PartFunc of X,ExtREAL by A15,PARTFUN1:28;
    reconsider y = fn as Element of PFuncs(X,ExtREAL) by PARTFUN1:119;
    take y;
    thus thesis by A15;
   end;
    ex F be Function of NAT,PFuncs(X,ExtREAL) st
    for n be Element of NAT holds PF[n,F.n] from FUNCT_2:sch 3(A4);
   then consider F being Function of NAT,PFuncs(X,ExtREAL) such that
A19: for n be Element of NAT holds dom (F.n) = dom f and
A20: for n be Element of NAT, x be Element of X st x in dom f holds
     ( for k be Nat st
        1 <= k & k <= 2|^n*n &
        (k-1)/(2|^n) <= f.x & f.x < k/(2|^n) holds
         (F.n).x=(k-1)/(2|^n) ) &
     ( n <= f.x implies (F.n).x= n );
A21: now let n be Nat;  n in NAT by ORDINAL1:def 13;
      hence dom (F.n) = dom f by A19;
    end;
   dom F = NAT & rng F c= PFuncs(X,ExtREAL) by FUNCT_2:def 1;
   then reconsider F as Functional_Sequence of X,ExtREAL by SEQFUNC:def 1;
A22:for n be Nat holds F.n is_simple_func_in S
   proof
    let n be Nat;
    reconsider n as Element of NAT by ORDINAL1:def 13;
     now let x be Element of X;
     assume x in dom(F.n);
then A23:x in dom f by A21;
     per cases;
     suppose R_EAL n <= f.x;
      then (F.n).x = R_EAL n by A20,A23;
      then - +infty < (F.n).x & (F.n).x < +infty by SUPINF_2:def 3,XXREAL_0:9;
      hence |. (F.n).x .| < +infty by EXTREAL2:59;
     end;
     suppose
A24:   f.x < R_EAL n;
A25:   0 <= f.x & R_EAL n < +infty by A2,SUPINF_2:70,XXREAL_0:9;
      then reconsider y = f.x as Real by A24,XXREAL_0:14;
      set k = [\ 2|^n*y /] + 1;
A26:   0 < 2|^n by PREPOWER:13;
then A27:   0 <= 2|^n*y & 2|^n*y < 2|^n*n by A24,A25,XREAL_1:70,129;
       [\ 2|^n*y /] <= 2|^n*y & 2|^n*y -1 < [\ 2|^n*y /] by INT_1:def 4;
then A28:   2|^n*y < k & [\ 2|^n*y /] < 2|^n*n by A27,XREAL_1:21,XXREAL_0:2;
then A29: 0+1 <=k & k <= 2|^n*n by A27,INT_1:20;
      reconsider k as Element of NAT by A27,A28,INT_1:16;
       k - 1 <= 2|^n*y & 2|^n*y < k by INT_1:52,def 4;
      then (k-1)/(2|^n) <= y & y < k/(2|^n) by A26,XREAL_1:81,83;
      then (F.n).x = (k-1)/(2|^n) by A20,A23,A29;
      then -infty < (F.n).x & (F.n).x < +infty by XXREAL_0:9,12;
      then - +infty < (F.n).x & (F.n).x < +infty by SUPINF_2:def 3;
      hence |. (F.n).x .| < +infty by EXTREAL2:59;
     end;
    end;
then A30:F.n is finite by MESFUNC2:def 1;
    reconsider N = 2|^n as Element of NAT;
    defpred PG[Nat,set] means
    ( $1 <= N*n implies
       $2 = A /\ great_eq_dom(f,R_EAL (($1-1)/(2|^n)))
           /\ less_dom(f,R_EAL ($1/(2|^n))) ) &
    ( $1 = N*n + 1 implies
       $2 = A /\ great_eq_dom(f,R_EAL (n)) );
A31: now let k be Nat;
       assume  k in Seg(N*n+1);
       reconsider k1=k as Element of NAT by ORDINAL1:def 13;
     per cases;
     suppose
A32: k <> N*n+1;
      set B = A /\ great_eq_dom(f,R_EAL ((k1-1)/(2|^n)))
              /\ less_dom(f,R_EAL (k1/(2|^n)));
       B is_measurable_on S by A3,Th39;
      then reconsider B as Element of S by MESFUNC1:def 11;
      take B;
      thus PG[k,B] by A32;
     end;
     suppose
A33:  k = N*n+1;
      set B = A /\ great_eq_dom(f,R_EAL n);
       B is_measurable_on S by A3,MESFUNC1:31;
      then reconsider B as Element of S by MESFUNC1:def 11;
      take B;
      thus PG[k,B] by A33,NAT_1:13;
     end;
    end;
    consider G be FinSequence of S such that
A34:  dom G = Seg(N*n +1) &
     for k be Nat st k in Seg(N*n +1) holds PG[k,G.k]
     from FINSEQ_1:sch 5(A31);
A35:len G = 2|^n*n +1 by A34,FINSEQ_1:def 3;
A36:now let k be Nat; assume
A37:1 <= k & k <= 2|^n*n;
A38:  k in NAT by ORDINAL1:def 13;
      k <= N*n + 1 by A37,NAT_1:12;
     then k in Seg(N*n+1) by A37,A38;
     hence G.k= A /\ great_eq_dom(f,R_EAL((k-1)/(2|^n)))
           /\ less_dom(f,R_EAL(k/(2|^n))) by A34,A37;
    end;
G is disjoint_valued
    proof
      now let x,y be set;
      assume
A39:  x <> y;
      per cases;
      suppose not x in dom G or not y in dom G;
       then G.x = {} or G.y = {} by FUNCT_1:def 4;
       hence G.x misses G.y by XBOOLE_1:65;
      end;
      suppose
A40:  x in dom G & y in dom G;
       then reconsider x1=x,y1=y as Element of NAT;
        x1 in Seg len G & y1 in Seg len G by A40,FINSEQ_1:def 3;
then A41:  1 <= x1 & x1 <= 2|^n*n + 1 &
       1 <= y1 & y1 <= 2|^n*n + 1 by A35,FINSEQ_1:3;
        now
       per cases by A39,REAL_1:def 5;
        case
A42:    x1 < y1;
         hereby assume
A43:     y1 = 2|^n*n+1;
then A44:       x1 <= N*n by A42,NAT_1:13;
then A45:     G.x = A /\ great_eq_dom(f,R_EAL((x1-1)/(2|^n)))
                 /\ less_dom(f,R_EAL(x1/(2|^n))) by A36,A41;
A46:     G.y = A /\ great_eq_dom(f,R_EAL n) by A34,A40,A43;
           now assume ex v be set st v in G.x /\ G.y;
           then consider v be set such that
A47:       v in G.x /\ G.y;
            v in G.x & v in G.y by A47,XBOOLE_0:def 3;
then A48:      v in less_dom(f,R_EAL(x1/(2|^n))) &
           v in great_eq_dom(f,R_EAL n) by A45,A46,XBOOLE_0:def 3;
then A49:      ex z1 be R_eal st
            z1 = f.v & z1 < R_EAL(x1/(2|^n)) by MESFUNC1:def 12;
A50:      ex z2 be R_eal st
            z2 = f.v & R_EAL n <= z2 by A48,MESFUNC1:def 15;
A51:        0 < 2|^n by PREPOWER:13;
            R_EAL n < R_EAL(x1/(2|^n)) by A49,A50,XXREAL_0:2;
           hence contradiction by A44,A51,XREAL_1:81;
          end;
          then G.x /\ G.y = {} by XBOOLE_0:def 1;
          hence G.x misses G.y by XBOOLE_0:def 7;
         end;
         assume y1 <> 2|^n*n+1;
         then y1 < N*n+1 by A41,REAL_1:def 5;
then A52:    y1 <= N*n by NAT_1:13;
         then x1 <= 2|^n*n by A42,XXREAL_0:2;
then A53:    G.x = A /\ great_eq_dom(f,R_EAL((x1-1)/(2|^n)))
                 /\ less_dom(f,R_EAL(x1/(2|^n))) &
         G.y = A /\ great_eq_dom(f,R_EAL((y1-1)/(2|^n)))
                 /\ less_dom(f,R_EAL(y1/(2|^n))) by A36,A41,A52;
          now assume ex v be set st v in G.x /\ G.y;
          then consider v be set such that
A54:      v in G.x /\ G.y;
           v in G.x & v in G.y by A54,XBOOLE_0:def 3;
then A55:    v in less_dom(f,R_EAL(x1/(2|^n))) &
          v in A /\ great_eq_dom(f,R_EAL((y1-1)/(2|^n)))
            by A53,XBOOLE_0:def 3;
then A56:     v in less_dom(f,R_EAL(x1/(2|^n))) &
          v in great_eq_dom(f,R_EAL((y1-1)/(2|^n))) by XBOOLE_0:def 3;
A57:     ex z1 be R_eal st z1 = f.v & z1 < R_EAL(x1/(2|^n))
           by A55,MESFUNC1:def 12;
     ex z2 be R_eal st z2 = f.v & R_EAL((y1-1)/(2|^n)) <= z2
           by A56,MESFUNC1:def 15;
          then R_EAL((y1-1)/(2|^n)) < R_EAL(x1/(2|^n))
            by A57,XXREAL_0:2;
          then y1-1 < x1 by XREAL_1:74;
          then y1 < x1 + 1 by XREAL_1:21;
          hence contradiction by A42,NAT_1:13;
         end;
         then G.x /\ G.y = {} by XBOOLE_0:def 1;
         hence G.x misses G.y by XBOOLE_0:def 7;
        end;
        case
A58:    y1 < x1;
         hereby assume x1 <> 2|^n*n+1;
          then x1 < N*n+1 by A41,REAL_1:def 5;
then A59:     x1 <= N*n by NAT_1:13;
          then y1 <= 2|^n*n by A58,XXREAL_0:2;
then A60:     G.y = A /\ great_eq_dom(f,R_EAL((y1-1)/(2|^n)))
                  /\ less_dom(f,R_EAL(y1/(2|^n))) &
          G.x = A /\ great_eq_dom(f,R_EAL((x1-1)/(2|^n)))
                  /\ less_dom(f,R_EAL(x1/(2|^n))) by A36,A41,A59;
           now assume ex v be set st v in G.x /\ G.y;
           then consider v be set such that
A61:       v in G.x /\ G.y;
            v in G.x & v in G.y by A61,XBOOLE_0:def 3;
then A62:     v in less_dom(f,R_EAL(y1/(2|^n))) &
           v in A /\ great_eq_dom(f,R_EAL((x1-1)/(2|^n)))
             by A60,XBOOLE_0:def 3;
then A63:      v in less_dom(f,R_EAL(y1/(2|^n))) &
           v in great_eq_dom(f,R_EAL((x1-1)/(2|^n))) by XBOOLE_0:def 3;
           consider z1 be R_eal such that
A64:       z1 = f.v & z1 < R_EAL(y1/(2|^n)) by A62,MESFUNC1:def 12;
           consider z2 be R_eal such that
A65:       z2 = f.v & R_EAL((x1-1)/(2|^n)) <= z2 by A63,MESFUNC1:def 15;
            R_EAL((x1-1)/(2|^n)) < R_EAL(y1/(2|^n))
             by A64,A65,XXREAL_0:2;
           then x1-1 < y1 by XREAL_1:74;
           then x1 < y1 + 1 by XREAL_1:21;
           hence contradiction by A58,NAT_1:13;
          end;
          then G.x /\ G.y = {} by XBOOLE_0:def 1;
          hence G.x misses G.y by XBOOLE_0:def 7;
         end;
         assume
A66:     x1 = 2|^n*n+1;
then A67:      y1 <= N*n by A58,NAT_1:13;
then A68:    G.y = A /\ great_eq_dom(f,R_EAL((y1-1)/(2|^n)))
                /\ less_dom(f,R_EAL(y1/(2|^n))) by A36,A41;
A69:    G.x = A /\ great_eq_dom(f,R_EAL n) by A34,A40,A66;
          now assume ex v be set st v in G.x /\ G.y;
          then consider v be set such that
A70:      v in G.x /\ G.y;
           v in G.x & v in G.y by A70,XBOOLE_0:def 3;
then A71:     v in less_dom(f,R_EAL(y1/(2|^n))) &
          v in great_eq_dom(f,R_EAL n) by A68,A69,XBOOLE_0:def 3;
          then consider z1 be R_eal such that
A72:      z1 = f.v & z1 < R_EAL(y1/(2|^n)) by MESFUNC1:def 12;
          consider z2 be R_eal such that
A73:      z2 = f.v & R_EAL n <= z2 by A71,MESFUNC1:def 15;
A74:       0 < 2|^n by PREPOWER:13;
           R_EAL n < R_EAL(y1/(2|^n)) by A72,A73,XXREAL_0:2;
          hence contradiction by A67,A74,XREAL_1:81;
         end;
         then G.x /\ G.y = {} by XBOOLE_0:def 1;
         hence G.x misses G.y by XBOOLE_0:def 7;
        end;
       end;
       hence G.x misses G.y;
      end;
     end;
     hence thesis by PROB_2:def 3;
    end;
    then reconsider G as Finite_Sep_Sequence of S;
     union rng G = dom f
    proof
      for v be set st v in union rng G holds v in dom f
     proof
      let v be set;
      assume v in union rng G;
      then consider B be set such that
A75:    v in B & B in rng G by TARSKI:def 4;
      consider m be set such that
A76:    m in dom G & B = G.m by A75,FUNCT_1:def 5;
      reconsider m as Element of NAT by A76;
A77:   1 <= m & m <= N*n+1 by A34,A76,FINSEQ_1:3;
       now per cases;
       suppose m = N*n+1;
        then B = A /\ great_eq_dom(f,R_EAL n) by A34,A76;
        hence v in A by A75,XBOOLE_0:def 3;
       end;
       suppose m <> N*n+1;
        then m < N*n+1 by A77,REAL_1:def 5;
        then m <= N*n by NAT_1:13;
        then B = A /\ great_eq_dom(f,R_EAL ((m-1)/(2|^n)))
              /\ less_dom(f,R_EAL (m/(2|^n))) by A34,A76;
        then v in A /\ great_eq_dom(f,R_EAL ((m-1)/(2|^n))) by A75,
XBOOLE_0:def 3;
        hence v in A by XBOOLE_0:def 3;
       end;
      end;
      hence v in dom f by A3;
     end;
then A78:  union rng G c= dom f by TARSKI:def 3;
      for v be set st v in dom f holds v in union rng G
     proof
      let v be set; assume
A79:    v in dom f;
       ex B being set st v in B & B in rng G
      proof
       per cases;
       suppose
A80:     f.v < R_EAL n;
         0 <= f.v by A2,SUPINF_2:70;
        then consider k be Nat such that
A81:      1 <= k & k <= 2|^n*n &
         (k-1)/(2|^n) <= f.v & f.v < k/(2|^n) by A80,Th8;
         v in great_eq_dom(f,R_EAL((k-1)/(2|^n)))
           by A79,A81,MESFUNC1:def 15;
then A82:     v in A /\ great_eq_dom(f,R_EAL((k-1)/(2|^n)))
           by A3,A79,XBOOLE_0:def 3;
         v in less_dom(f,R_EAL (k/(2|^n))) by A79,A81,MESFUNC1:def 12;
then A83:     v in A /\ great_eq_dom(f,R_EAL((k-1)/(2|^n)))
               /\ less_dom(f,R_EAL (k/(2|^n))) by A82,XBOOLE_0:def 3;
A84:     k in NAT by ORDINAL1:def 13;
         N*n <= N*n + 1 by NAT_1:11;
        then k <= N*n + 1 by A81,XXREAL_0:2;
then A85:     k in Seg(N*n+1) by A81,A84;
        take B = G.k;
        thus thesis by A34,A81,A83,A85,FUNCT_1:12;
       end;
       suppose R_EAL n <= f.v;
        then v in great_eq_dom(f,R_EAL n) by A79,MESFUNC1:def 15;
then A86:     v in A /\ great_eq_dom(f,R_EAL n) by A3,A79,XBOOLE_0:def 3;
        set k = N*n+1;
        take B = G.k;
         1 <= k by NAT_1:11;
        then k in Seg(N*n+1);
        hence thesis by A34,A86,FUNCT_1:12;
       end;
      end;
      hence v in union rng G by TARSKI:def 4;
     end;
     then dom f c= union rng G by TARSKI:def 3;
     hence thesis by A78,XBOOLE_0:def 10;
    end;
then A87:dom (F.n) = union rng G by A21;
     for k be Nat, x,y be Element of X st
     k in dom G & x in G.k & y in G.k holds (F.n).x =(F.n).y
    proof
     let k be Nat, x,y be Element of X;
     assume
A88: k in dom G & x in G.k & y in G.k;
then A89:1 <= k & k <= N*n+1 by A34,FINSEQ_1:3;

      now per cases;
      suppose k = N*n + 1;
       then G.k = A /\ great_eq_dom(f,R_EAL n) by A34,A88;
then A90:  x in A & x in great_eq_dom(f,R_EAL n) &
       y in A & y in great_eq_dom(f,R_EAL n) by A88,XBOOLE_0:def 3;
       then ex z1 be R_eal st z1 = f.x & R_EAL n <= z1 by MESFUNC1:def 15;
then A91:  (F.n).x = R_EAL n by A3,A20,A90;
        ex z2 be R_eal st z2 = f.y & R_EAL n <= z2 by A90,MESFUNC1:def 15;
       hence (F.n).x = (F.n).y by A3,A20,A90,A91;
      end;
      suppose k <> N*n + 1;
       then k < N*n+1 by A89,REAL_1:def 5;
then A92:  k <= N*n by NAT_1:13;
       then G.k = A /\ great_eq_dom(f,R_EAL ((k-1)/(2|^n)))
               /\ less_dom(f,R_EAL (k/(2|^n))) by A34,A88;
then A93:  x in A /\ great_eq_dom(f,R_EAL ((k-1)/(2|^n))) &
       x in less_dom(f,R_EAL (k/(2|^n))) &
       y in A /\ great_eq_dom(f,R_EAL ((k-1)/(2|^n))) &
       y in less_dom(f,R_EAL (k/(2|^n))) by A88,XBOOLE_0:def 3;
then A94:  x in A & x in great_eq_dom(f,R_EAL ((k-1)/(2|^n))) &
       y in A & y in great_eq_dom(f,R_EAL ((k-1)/(2|^n))) by XBOOLE_0:def 3;
A95:  ex z1 be R_eal st z1 = f.x & z1 < R_EAL (k/(2|^n))
         by A93,MESFUNC1:def 12;
A96:  ex z3 be R_eal st z3 = f.y & z3 < R_EAL(k/(2|^n))
         by A93,MESFUNC1:def 12;
A97:  ex z4 be R_eal st z4 = f.y & R_EAL((k-1)/(2|^n)) <= z4
         by A94,MESFUNC1:def 15;
        ex z2 be R_eal st z2 = f.x & R_EAL ((k-1)/(2|^n)) <= z2
         by A94,MESFUNC1:def 15;
       then (F.n).x = R_EAL((k-1)/(2|^n)) by A3,A20,A89,A92,A94,A95;
       hence (F.n).x = (F.n).y by A3,A20,A89,A92,A94,A96,A97;
      end;
     end;
     hence (F.n).x =(F.n). y;
    end;
    hence thesis by A30,A87,MESFUNC2:def 5;
   end;
A98:for n be Nat holds F.n is nonnegative
   proof
    let n be Nat;
    reconsider n as Element of NAT by ORDINAL1:def 13;
     now let x be set;
     assume x in dom (F.n);
then A99: x in dom f by A21;
     per cases;
     suppose n <= f.x;
      hence 0 <= (F.n).x by A20,A99;
     end;
     suppose
A100:  f.x < n;
       0 <= f.x by A2,SUPINF_2:70;
      then consider k be Nat such that
A101:   1 <= k & k <= 2|^n*n &
       (k-1)/(2|^n) <= f.x & f.x < k/(2|^n) by A100,Th8;
A102:  (F.n).x = (k-1)/(2|^n) by A20,A99,A101;
       0 <= k-1 & 2|^n > 0 by A101,PREPOWER:13,XREAL_1:50;
      hence 0 <= (F.n).x by A102,XREAL_1:138;
     end;
    end;
    hence thesis by SUPINF_2:71;
   end;
A103:for n,m be Nat st n <=m holds
    for x be Element of X st x in dom f holds (F.n).x <= (F.m).x
   proof
    let n,m be Nat such that
A104: n <=m;
     reconsider n,m as Element of NAT by ORDINAL1:def 13;
    let x be Element of X such that
A105: x in dom f;
    per cases;
    suppose
A106: R_EAL m <= f.x;
then A107: (F.m).x = R_EAL m by A20,A105;
      R_EAL n <= f.x by A104,A106,XXREAL_0:2;
     hence thesis by A20,A104,A105,A107;
    end;
    suppose
A108: f.x < R_EAL m;
A109: 0 <= f.x by A2,SUPINF_2:70;
     then consider M be Nat such that
A110:  1 <= M & M <= 2|^m*m &
      (M-1)/(2|^m) <= f.x & f.x < M/(2|^m) by A108,Th8;
      reconsider M as Element of NAT by ORDINAL1:def 13;
A111:  (F.m).x = (M-1)/(2|^m) by A20,A105,A110;
     per cases;
     suppose
A112:  R_EAL n <= f.x;
then A113:  (F.n).x = R_EAL n by A20,A105;
A114:  n < M/(2|^m) by A110,A112,XXREAL_0:2;
A115:   0 < 2|^m by PREPOWER:13;
then A116:  2|^m*n < M by A114,XREAL_1:81;
      reconsider M1 = 2|^m as Element of NAT;
       M1*n+1 <= M by A116,NAT_1:13;
      then M1*n <= M-1 by XREAL_1:21;
      hence thesis by A111,A113,A115,XREAL_1:79;
     end;
     suppose
       f.x < R_EAL n;
      then consider N1 be Nat such that
A117:   1 <= N1 & N1 <= 2|^n*n &
       (N1-1)/(2|^n) <= f.x & f.x < N1/(2|^n) by A109,Th8;
       reconsider N1 as Element of NAT by ORDINAL1:def 13;
A118:  (F.n).x = (N1-1)/(2|^n) by A20,A105,A117;
      consider k being Nat such that
      A119:   m = n + k by A104,NAT_1:10;
      reconsider k as Element of NAT by ORDINAL1:def 13;
A120:  2|^n > 0 & 2|^k > 0 & 2|^(n+k) > 0 by PREPOWER:13;
      reconsider K = 2|^k as Element of NAT;
       R_EAL((N1-1)/(2|^n)) < R_EAL(M/(2|^(n+k)))
        by A110,A117,A119,XXREAL_0:2;
      then (N1-1)/(2|^n) < M/(2|^n*2|^k) by NEWTON:13;
      then (N1-1)/(2|^n) < M/(2|^k)/(2|^n) by XCMPLX_1:79;
      then N1-1 < M/(2|^k) by XREAL_1:74;
      then K*(N1-1) < M by A120,XREAL_1:81;
      then K*(N1-1)+1 <= M by INT_1:20;
      then K*(N1-1) <= M-1 by XREAL_1:21;
      then K*(N1-1)/(2|^(n+k)) <= (M-1)/(2|^(n+k)) by XREAL_1:74;
      then K*(N1-1)/(2|^n*2|^k) <= (M-1)/(2|^(n+k)) by NEWTON:13;
      hence thesis by A111,A118,A119,A120,XCMPLX_1:92;
     end;
    end;
   end;
    now let x be Element of X such that
A121:x in dom f;
    per cases;
    suppose
A122:  |.f.x.| =+infty;
      now assume -f.x = +infty;
      then f.x < 0 by EXTREAL1:25;
      hence contradiction by A2,SUPINF_2:70;
     end;
then A123:  f.x = +infty by A122,EXTREAL2:50;
      for g be real number st 0 < g
      ex n be Nat st for m be Nat st n <= m holds g <= (F#x).m
     proof
      let g be real number;
      assume 0 < g;
      then reconsider n = [/ g \] as Nat by INT_1:80;
A124:  g <= n by INT_1:def 5;
       for m be Nat st n <= m holds g <= (F#x).m
      proof
       let m be Nat;
       assume n <= m;
then A125:   g <= m by A124,XXREAL_0:2;
       reconsider m as Element of NAT by ORDINAL1:def 13;
       reconsider g1 = g as Real by XREAL_0:def 1;
        R_EAL m <= f.x by A123,XXREAL_0:4;
       then (F.m).x = R_EAL m by A20,A121;
       hence thesis by A125,Def13;
      end;
      hence thesis;
     end;
then A126:  F#x is convergent_to_+infty by Def9;
     then F#x is convergent by Def11;
     hence F#x is convergent & lim(F#x) = f.x by A123,A126,Def12;
    end;
    suppose
      |.f.x.| <> +infty;
     then f.x <> +infty & f.x <> -infty by EXTREAL2:67;
     then reconsider g=f.x as Real by XXREAL_0:14;
A127:  f.x=R_EAL g;
A128:  for p be real number st 0 <p
      ex k be Nat st for j be Nat st j>=k holds |.(F#x).j - f.x.| < p
     proof
      let p be real number; assume
   0 < p;
      then consider N1 be Element of NAT such that
A129:   1/(2|^N1) <= p by PREPOWER:106;
A130:   for k be Nat st k >= N1 holds 1/(2|^k) <= p
      proof
       let k be Nat;
       assume k >= N1;
       then consider i being Nat such that
A131:    k = N1 + i by NAT_1:10;
A132:   2|^N1 > 0 & 2|^i >= 1 by PREPOWER:19;
       then (2|^N1)*(2|^i) >= 2|^N1 by XREAL_1:153;
       then 2|^k >= 2|^N1 by A131,NEWTON:13;
       then (2|^k)" <= (2|^N1)" by A132,XREAL_1:87;
       then 1/(2|^k) <= (2|^N1)" by XCMPLX_1:217;
       then 1/(2|^k) <= 1/(2|^N1) by XCMPLX_1:217;
       hence thesis by A129,XXREAL_0:2;
      end;
A133:  0 <= g by A2,SUPINF_2:70;
      set N2 = [/g\] + 1;
A134: g <= [/g\] & [/g\] < [/g\] + 1 by INT_1:def 5,XREAL_1:31;
then A135:  g < N2 by XXREAL_0:2;
      reconsider N2 as Element of NAT by A133,A134,INT_1:16;
A136:   for N be Nat st N >= N2 holds |.(F#x).N - f.x.| < 1/(2|^N)
      proof
       let N be Nat;
       assume
A137:    N >= N2;
       reconsider N as Element of NAT by ORDINAL1:def 13;
        0 <= f.x & f.x < N by A2,A135,A137,SUPINF_2:70,XXREAL_0:2;
       then consider m be Nat such that
A138:    1 <= m & m <= 2|^N*N &
        (m-1)/(2|^N) <= f.x & f.x < m/(2|^N) by Th8;
        reconsider m as Element of NAT by ORDINAL1:def 13;
A139:   (F#x).N = (F.N).x by Def13 .= (m-1)/(2|^N) by A20,A121,A138;
        -((F#x).N - f.x) = f.x - (F#x).N by EXTREAL2:15;
then A140:   |.(F#x).N - f.x.| = |.f.x - (F#x).N .| by EXTREAL2:66;
        R_EAL(m/(2|^N)) - R_EAL((m-1)/(2|^N))
        = m/(2|^N) - (m-1)/(2|^N) by SUPINF_2:5
       .= m/(2|^N) + -((m-1)/(2|^N))
       .= m/(2|^N) + (-(m-1))/(2|^N) by XCMPLX_1:188
       .= (m + -(m-1))/(2|^N) by XCMPLX_1:63;
then A141:   f.x - (F#x).N < 1/(2|^N) by A138,A139,MEASURE6:12;
        2|^N > 0 by PREPOWER:13;
       then (2|^N)" > 0 by XREAL_1:124;
       then 1/(2|^N) > 0 by XCMPLX_1:217;
then A142:   -R_EAL(1/(2|^N)) < 0 by EXTREAL1:25;
        0 <= f.x - (F#x).N by A138,A139,MEASURE5:2;
       hence thesis by A140,A141,A142,EXTREAL2:59;
      end;
      reconsider k=max(N2,N1) as Element of NAT;
       now let j be Nat; assume
A143:    j >= k;
        k >= N1 & k >= N2 by XXREAL_0:25;
       then j >= N1 & j >= N2 by A143,XXREAL_0:2;
then   1/(2|^j) <= p & |.(F#x).j - f.x.| < R_EAL(1/(2|^j)) by A130,A136;
       hence |.(F#x).j - f.x.| < p by XXREAL_0:2;
      end;
      hence ex k be Nat st for j be Nat st j >= k holds |.(F#x).j - f.x.| < p;
     end;
then A144:  F#x is convergent_to_finite_number by A127,Def8;
     then F#x is convergent by Def11;
     hence F#x is convergent & lim(F#x) = f.x by A127,A128,A144,Def12;
    end;
   end;
   hence thesis by A21,A22,A98,A103;
end;

begin :: Integral of non negative simple valued function

definition
  let X be non empty set;
  let S be SigmaField of X;
  let M be sigma_Measure of S;
  let f be PartFunc of X,ExtREAL;
  func integral'(M,f) -> Element of ExtREAL equals :Def14:
  integral(X,S,M,f) if dom f <> {} otherwise 0.;
  correctness;
end;

theorem Th71:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
 f is_simple_func_in S & g is_simple_func_in S &
 f is nonnegative & g is nonnegative holds
   dom(f+g) = dom f /\ dom g &
   integral'(M,f+g) = integral'(M,f|dom(f+g)) + integral'(M,g|dom(f+g))
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f,g be PartFunc of X,ExtREAL;
   assume that
A1:f is_simple_func_in S & g is_simple_func_in S and
A2:f is nonnegative & g is nonnegative;
A3:f+g is_simple_func_in S by A1,Th44;
    f is without-infty & g is without-infty by A2,Th18;
   then not -infty in rng f & not -infty in rng g by Def3;
then A4:f"{-infty} = {} & g"{-infty} = {} by FUNCT_1:142;
then A5:(dom f /\ dom g) \ (f"{-infty} /\ g"{+infty} \/ f"{+infty} /\ g"{-infty
})
    = dom f /\ dom g;
   hence A6: dom(f+g) = dom f /\ dom g by MESFUNC1:def 3;
    dom(f+g) is Element of S by A3,Th43;
then A7:f|dom(f+g) is_simple_func_in S &
   g|dom(f+g) is_simple_func_in S by A1,Th40;
    f|dom(f+g) is nonnegative & g|dom(f+g) is nonnegative by A2,Th21;
then A8:for x be set holds
    ( x in dom(f|dom(f+g)) implies 0 <= (f|dom(f+g)).x ) &
    ( x in dom(g|dom(f+g)) implies 0 <= (g|dom(f+g)).x ) by SUPINF_2:70;
    dom(f|dom(f+g)) = dom f /\ dom(f+g) &
   dom(g|dom(f+g)) = dom g /\ dom(f+g) by FUNCT_1:68;
then A9:dom(f|dom(f+g)) = dom f /\ dom f /\ dom g &
   dom(g|dom(f+g)) = dom g /\ dom g /\ dom f by A6,XBOOLE_1:16;
   per cases;
   suppose
A10: dom(f+g) = {};
then A11: integral'(M,f+g) = 0 by Def14;
     dom(f|dom(f+g)) = dom f /\ dom(f+g) &
    dom(g|dom(f+g)) = dom g /\ dom(f+g) by FUNCT_1:68;
    then integral'(M,f|dom(f+g)) = 0 & integral'(M,g|dom(f+g)) = 0
      by A10,Def14;
    hence integral'(M,f+g) = integral'(M,f|dom(f+g)) + integral'(M,g|dom(f+g))
      by A11,SUPINF_2:18;
   end;
   suppose
A12: dom(f+g) <> {};
then A13: integral(X,S,M,(f|dom(f+g) + g|dom(f+g)))
     = integral(X,S,M,f|dom(f+g))+integral(X,S,M,g|dom(f+g))
        by A6,A7,A8,A9,MESFUNC4:5;
A14:(f|dom(f+g))"{-infty} = dom(f+g) /\ f"{-infty} by FUNCT_1:139 .= {} by A4;
     (g|dom(f+g))"{-infty} = dom(f+g) /\ g"{-infty} by FUNCT_1:139
     .= {} by A4;
    then (dom(f|dom(f+g)) /\ dom(g|dom(f+g))) \
     ( (f|dom(f+g))"{-infty} /\ (g|dom(f+g))"{+infty} \/
      (f|dom(f+g))"{+infty} /\ (g|dom(f+g))"{-infty} )
     = dom(f+g) by A5,A9,A14,MESFUNC1:def 3;
then A15: dom(f|dom(f+g) + g|dom(f+g)) = dom(f+g) by MESFUNC1:def 3;
     for x be Element of X st x in dom(f|dom(f+g) + g|dom(f+g)) holds
     (f|dom(f+g) + g|dom(f+g)).x = (f+g).x
    proof
     let x be Element of X;
     assume
A16:  x in dom(f|dom(f+g) + g|dom(f+g));
     then (f|dom(f+g) + g|dom(f+g)).x
       = (f|dom(f+g)).x + (g|dom(f+g)).x by MESFUNC1:def 3
      .= f.x + (g|dom(f+g)).x by A15,A16,FUNCT_1:72
      .= f.x + g.x by A15,A16,FUNCT_1:72;
     hence thesis by A15,A16,MESFUNC1:def 3;
    end;
then A17: integral(X,S,M,f+g)
     = integral(X,S,M,f|dom(f+g)) + integral(X,S,M,g|dom(f+g))
       by A13,A15,PARTFUN1:34;
A18: integral(X,S,M,f|dom(f+g)) = integral'(M,f|dom(f+g))
      by A6,A9,A12,Def14;
     integral(X,S,M,g|dom(f+g)) = integral'(M,g|dom(f+g))
      by A6,A9,A12,Def14;
    hence integral'(M,f+g) = integral'(M,f|dom(f+g)) + integral'(M,g|dom(f+g))
        by A12,A17,A18,Def14;
   end;
end;

theorem Th72:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,c be Real st
 f is_simple_func_in S & f is nonnegative & 0 <= c holds
   integral'(M,c(#)f) = R_EAL(c)*integral'(M,f)
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f be PartFunc of X,ExtREAL;
   let c be Real;
   assume that
A1: f is_simple_func_in S and
A2: f is nonnegative and
A3: 0 <= c;
   set g = c(#)f;
A4:dom g = dom f by MESFUNC1:def 6;
A5:for x be set st x in dom f holds 0 <= f.x by A2,SUPINF_2:70;
A6:for x be set st x in dom g holds g.x = (R_EAL c)*f.x by MESFUNC1:def 6;
A7:0 <= R_EAL c & R_EAL c < +infty by A3,XXREAL_0:9;
   per cases;
   suppose
A8: dom g = {};
    then integral'(M,f) = 0 by A4,Def14;
    then (R_EAL c)*integral'(M,f) = 0 by EXTREAL1:22;
    hence integral'(M,g) = R_EAL(c)*integral'(M,f) by A8,Def14;
   end;
   suppose
A9: dom g <> {};
    then integral'(M,f) = integral(X,S,M,f) by A4,Def14;
    then integral(X,S,M,g) = (R_EAL c)*integral'(M,f)
      by A1,A4,A5,A6,A7,A9,MESFUNC4:6;
    hence integral'(M,g) = R_EAL(c)*integral'(M,f) by A9,Def14;
   end;
end;

theorem Th73:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    A,B be Element of S st
f is_simple_func_in S & f is nonnegative & A misses B holds
  integral'(M,f|(A\/B)) = integral'(M,f|A) + integral'(M,f|B)
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f be PartFunc of X,ExtREAL;
   let A,B be Element of S;
   assume that
A1:f is_simple_func_in S and
A2:f is nonnegative and
A3:A misses B;
   set g = f|(A\/B);
   set g1 = f|A;
   set g2 = f|B;
A4:g is_simple_func_in S by A1,Th40;
    g is nonnegative by A2,Th21;
then A5:for x be set st x in dom g holds 0 <= g.x by SUPINF_2:70;
   then consider G be Finite_Sep_Sequence of S,
            b be FinSequence of ExtREAL such that
A6: G,b are_Re-presentation_of g &
    b.1 = 0 &
    for n be Nat st 2 <= n & n in dom b holds 0 < b.n & b.n < +infty
     by A4,MESFUNC3:14;
   deffunc Fy(Nat) = b.$1*(M*G).$1;
   consider y be FinSequence of ExtREAL such that
A7: len y = len G &
for j be Nat st j in Seg len G holds y.j = Fy(j)
from FINSEQ_2:sch 1;
A8:dom y = Seg len y by FINSEQ_1:def 3
        .= dom G by A7,FINSEQ_1:def 3;

A9:f|A is_simple_func_in S by A1,Th40;
A10:f|B is_simple_func_in S by A1,Th40;
   deffunc G1(Nat) = G.$1 /\ A;
   consider G1 be FinSequence such that
A11: len G1 = len G &
    for k be Nat st k in Seg len G holds G1.k = G1(k)
      from FINSEQ_1:sch 2;
A12:dom G = Seg len G1 by A11,FINSEQ_1:def 3 .= dom G1 by FINSEQ_1:def 3;
A13:for k be Nat st k in dom G holds G1.k = G.k /\ A
   proof
    let k be Nat;
    assume k in dom G;
    then k in Seg len G by FINSEQ_1:def 3;
    hence thesis by A11;
   end;
   then reconsider G1 as Finite_Sep_Sequence of S by A12,Th41;
A14:dom(g|A) = dom g /\ A by FUNCT_1:68
           .= dom f /\ (A\/B) /\ A by FUNCT_1:68
           .= dom f /\ ((A\/B) /\ A) by XBOOLE_1:16
           .= dom f /\ A by XBOOLE_1:21
           .= dom g1 by FUNCT_1:68;
    for x be set st x in dom(g|A) holds (g|A).x = g1.x
   proof
    let x be set; assume
A15:  x in dom(g|A);
    then x in dom g /\ A by FUNCT_1:68;
then A16: x in dom g by XBOOLE_0:def 3;
     (g|A).x = g.x by A15,FUNCT_1:70 .= f.x by A16,FUNCT_1:70;
    hence (g|A).x = g1.x by A14,A15,FUNCT_1:70;
   end;
   then g|A = g1 by A14,FUNCT_1:9;
then A17:G1,b are_Re-presentation_of g1 by A6,A12,A13,Th42;
   deffunc G2(Nat) = G.$1 /\ B;
   consider G2 be FinSequence such that
A18: len G2 = len G &
    for k be Nat st k in Seg len G holds G2.k = G2(k)
      from FINSEQ_1:sch 2;
A19:dom G = Seg len G2 by A18,FINSEQ_1:def 3 .= dom G2 by FINSEQ_1:def 3;
A20:for k be Nat st k in dom G holds G2.k = G.k /\ B
   proof
    let k be Nat;
    assume k in dom G;
    then k in Seg len G by FINSEQ_1:def 3;
    hence G2.k = G.k /\ B by A18;
   end;
   then reconsider G2 as Finite_Sep_Sequence of S by A19,Th41;
A21:dom(g|B) = dom g /\ B by FUNCT_1:68
           .= dom f /\ (A\/B) /\ B by FUNCT_1:68
           .= dom f /\ ((A\/B) /\ B) by XBOOLE_1:16
           .= dom f /\ B by XBOOLE_1:21
           .= dom g2 by FUNCT_1:68;
    for x be set st x in dom(g|B) holds (g|B).x = g2.x
   proof
    let x be set; assume
A22:  x in dom(g|B);
    then x in dom g /\ B by FUNCT_1:68;
then A23: x in dom g by XBOOLE_0:def 3;
     (g|B).x = g.x by A22,FUNCT_1:70 .= f.x by A23,FUNCT_1:70;
    hence (g|B).x = g2.x by A21,A22,FUNCT_1:70;
   end;
   then g|B = g2 by A21,FUNCT_1:9;
then A24:G2,b are_Re-presentation_of g2 by A6,A19,A20,Th42;
   per cases;
   suppose
A25: dom(f|(A\/B)) = {};
then A26: integral'(M,g) = 0 by Def14;
     A c= A\/B by XBOOLE_1:7;
    then dom f /\ A c= dom f /\ (A\/B) by XBOOLE_1:26;
    then dom(f|A) c= dom f /\ (A\/B) by FUNCT_1:68;
    then dom(f|A) c= dom(f|(A\/B)) by FUNCT_1:68;
then dom(f|A) = {} by A25,XBOOLE_1:3;
then A27: integral'(M,g1) = 0 by Def14;
     B c= A\/B by XBOOLE_1:7;
    then dom f /\ B c= dom f /\ (A\/B) by XBOOLE_1:26;
    then dom(f|B) c= dom f /\ (A\/B) by FUNCT_1:68;
    then dom(f|B) c= dom(f|(A\/B)) by FUNCT_1:68;
    then dom(f|B) = {} by A25,XBOOLE_1:3;
    then integral'(M,g2) = 0 by Def14;
    hence integral'(M,g) = integral'(M,g1) + integral'(M,g2)
      by A26,A27,SUPINF_2:18;
   end;
   suppose
A28: dom(f|(A\/B)) <> {};
     for n be Nat st n in dom y holds y.n = b.n*(M*G).n
    proof
     let n be Nat;
     assume n in dom y;
     then n in Seg len y by FINSEQ_1:def 3;
     hence thesis by A7;
    end;
    then integral(X,S,M,g) = Sum y by A4,A5,A6,A8,A28,MESFUNC4:3;
then A29: integral'(M,g) = Sum y by A28,Def14;
    per cases;
    suppose
A30:  dom(f|A) = {};
then A31:  integral'(M,g1) = 0 by Def14;
A32:  dom(f|(A\/B)) = dom f /\ (A\/B) by FUNCT_1:68
       .= dom f /\ A \/ dom f /\ B by XBOOLE_1:23
       .= dom(f|A) \/ dom f /\ B by FUNCT_1:68
       .= dom(f|B) by A30,FUNCT_1:68;
      now let x be set;
      assume
A33:    x in dom g;
      then g.x = f.x by FUNCT_1:70;
      hence g.x = g2.x by A32,A33,FUNCT_1:70;
     end;
     then g = g2 by A32,FUNCT_1:9;
     hence thesis by A31,SUPINF_2:18;
    end;
    suppose
A34:  dom(f|A) <> {};
     per cases;
      suppose
A35:    dom(f|B) = {};
then A36:    integral'(M,g2) = 0 by Def14;
A37:    dom(f|(A\/B)) = dom f /\ (A\/B) by FUNCT_1:68
       .= (dom f /\ B) \/ (dom f /\ A) by XBOOLE_1:23
       .= dom(f|B) \/ (dom f /\ A) by FUNCT_1:68
       .= dom(f|A) by A35,FUNCT_1:68;
        now let x be set;
        assume
A38:      x in dom g;
        then g.x = f.x by FUNCT_1:70;
        hence g.x = g1.x by A37,A38,FUNCT_1:70;
       end;
       then g = g1 by A37,FUNCT_1:9;
       hence integral'(M,g)=integral'(M,g1)+integral'(M,g2) by A36,SUPINF_2:18;
      end;
      suppose
A39:    dom(f|B) <> {};
        g1 is nonnegative by A2,Th21;
then A40:    for x be set st x in dom g1 holds 0 <= g1.x by SUPINF_2:70;
        g2 is nonnegative by A2,Th21;
then A41:    for x be set st x in dom g2 holds 0 <= g2.x by SUPINF_2:70;
       deffunc Fy1(Nat) = b.$1*(M*G1).$1;
       consider y1 be FinSequence of ExtREAL such that
A42:     len y1 = len G1 &
        for j be Nat st j in Seg(len G1) holds y1.j = Fy1(j)
          from FINSEQ_2:sch 1;
A43:    dom y1 = Seg len G1 by A42,FINSEQ_1:def 3 .= dom G1 by FINSEQ_1:def 3;
        now let k be Nat;
        assume k in dom y1;
        then k in Seg len G1 by A43,FINSEQ_1:def 3;
        hence y1.k = b.k*(M*G1).k by A42;
       end;
       then integral(X,S,M,g1) = Sum y1 by A9,A17,A34,A40,A43,MESFUNC4:3;
then A44:    integral'(M,g1) = Sum y1 by A34,Def14;
       deffunc Fy2(Nat) = b.$1*(M*G2).$1;
       consider y2 be FinSequence of ExtREAL such that
A45:     len y2 = len G2 &
        for j be Nat st j in Seg len G2 holds y2.j = Fy2(j)
          from FINSEQ_2:sch 1;
A46:    dom y2 = Seg len G2 by A45,FINSEQ_1:def 3 .= dom G2 by FINSEQ_1:def 3;
        now let k be Nat;
        assume k in dom y2;
        then k in Seg len G2 by A46,FINSEQ_1:def 3;
        hence y2.k = b.k*(M*G2).k by A45;
       end;
       then integral(X,S,M,g2) = Sum y2 by A10,A24,A39,A41,A46,MESFUNC4:3;
then A47:    integral'(M,g2) = Sum y2 by A39,Def14;
A48:    for k be Nat st k in dom y1 holds 0 <= y1.k
       proof
        let k be Nat;
        assume
A49:     k in dom y1;
        then k in Seg len G1 by A42,FINSEQ_1:def 3;
then A50:    y1.k = b.k * (M*G1).k by A42;
A51:   dom b = dom G by A6,MESFUNC3:def 1
             .= Seg len y1 by A11,A42,FINSEQ_1:def 3
             .= dom y1 by FINSEQ_1:def 3;
         k in Seg len y1 by A49,FINSEQ_1:def 3;
then A52:    1 <= k & k <= len y1 by FINSEQ_1:3;
A53:   now per cases;
         suppose k = 1;
          hence 0 <= b.k by A6;
         end;
         suppose k <> 1;
          then 1 < k by A52,REAL_1:def 5;
          then 1+1 <= k by NAT_1:13;
          hence 0 <= b.k by A6,A49,A51;
         end;
        end;
         k in Seg(len G1) by A42,A49,FINSEQ_1:def 3;
then A54:    k in dom G1 by FINSEQ_1:def 3;
then A55:    (M*G1).k = M.(G1.k) by FUNCT_1:23;
         G1.k in rng G1 & rng G1 c= S by A54,FUNCT_1:12;
        then reconsider G1k = G1.k as Element of S;
         M is nonnegative by MEASURE1:def 11;
        then 0 <= M.G1k by SUPINF_2:58;
        hence 0 <= y1.k by A50,A53,A55,EXTREAL2:45;
       end;
A56:    for k be Nat st k in dom y2 holds 0 <= y2.k
       proof
        let k be Nat;
        assume
A57:     k in dom y2;
        then k in Seg len G2 by A45,FINSEQ_1:def 3;
then A58:    y2.k = b.k * (M*G2).k by A45;
A59:   dom b = dom G by A6,MESFUNC3:def 1
             .= Seg len y2 by A18,A45,FINSEQ_1:def 3
             .= dom y2 by FINSEQ_1:def 3;
         k in Seg len y2 by A57,FINSEQ_1:def 3;
then A60:    1 <= k & k <= len y2 by FINSEQ_1:3;
A61:   now per cases;
         suppose k = 1;
          hence 0 <= b.k by A6;
         end;
         suppose k <> 1;
          then 1 < k by A60,REAL_1:def 5;
          then 1+1 <= k by NAT_1:13;
          hence 0 <= b.k by A6,A57,A59;
         end;
        end;
         k in Seg len G2 by A45,A57,FINSEQ_1:def 3;
then A62:    k in dom G2 by FINSEQ_1:def 3;
then A63:    (M*G2).k = M.(G2.k) by FUNCT_1:23;
         G2.k in rng G2 & rng G2 c= S by A62,FUNCT_1:12;
        then reconsider G2k = G2.k as Element of S;
         M is nonnegative by MEASURE1:def 11;
        then 0 <= M.G2k by SUPINF_2:58;
        hence 0 <= y2.k by A58,A61,A63,EXTREAL2:45;
       end;
A64:    dom y1 = Seg len y2 by A11,A18,A42,A45,FINSEQ_1:def 3
             .= dom y2 by FINSEQ_1:def 3;
        y = y1 + y2
       proof
A65:   for x be set st x in dom y1 holds not y1.x in {-infty}
        proof
         let x be set;
         assume
A66:      x in dom y1;
         then reconsider x as Element of NAT;
          0 <= y1.x by A48,A66;
         hence thesis by TARSKI:def 1;
        end;
         for x be set holds not x in y1"{-infty}
        proof
         let x be set;
          not (x in dom y1 & y1.x in {-infty}) by A65;
         hence thesis by FUNCT_1:def 13;
        end;
then A67:    y1"{-infty} = {} by XBOOLE_0:def 1;
A68:   for x be set st x in dom y2 holds not y2.x in {-infty}
        proof
         let x be set;
         assume
A69:      x in dom y2;
         then reconsider x as Element of NAT;
          0 <= y2.x by A56,A69;
         hence thesis by TARSKI:def 1;
        end;
         for x be set holds not x in y2"{-infty}
        proof
         let x be set;
          not (x in dom y2 & y2.x in {-infty}) by A68;
         hence thesis by FUNCT_1:def 13;
        end;
then A70:   y2"{-infty} = {} by XBOOLE_0:def 1;
A71:    dom y = Seg len G /\ Seg len G by A8,FINSEQ_1:def 3
             .= dom y1 /\ Seg len G2 by A11,A18,A42,FINSEQ_1:def 3
             .= dom y1 /\ dom y2 by A45,FINSEQ_1:def 3;
then A72:     dom y = (dom y1 /\ dom y2)\(
                 y1"{-infty}/\y2"{+infty}\/y1"{+infty}/\y2"{-infty})
                   by A67,A70;
         for n be Element of NAT st n in dom y holds y.n = y1.n + y2.n
        proof
         let n be Element of NAT; assume
A73:     n in dom y;
then A74:     n in Seg len G by A7,FINSEQ_1:def 3;
then A75:     n in dom G by FINSEQ_1:def 3;
          n in dom y1 & n in dom y2 by A71,A73,XBOOLE_0:def 3;
then A76:     n in Seg len G1 & n in Seg len G2 by A42,A45,FINSEQ_1:def 3;
then A77:     n in dom G1 & n in dom G2 by FINSEQ_1:def 3;
A78:     G2.n = G.n /\ B by A20,A75;
          now let v be set; assume
A79:      v in G.n;
           G.n in rng G by A75,FUNCT_1:12;
          then v in union rng G by A79,TARSKI:def 4;
          then v in dom g by A6,MESFUNC3:def 1;
          then v in dom f /\ (A\/B) by FUNCT_1:68;
          hence v in A\/B by XBOOLE_0:def 3;
         end;
         then G.n c= A \/ B by TARSKI:def 3;
then A80:     G.n = G.n /\ (A\/B) by XBOOLE_1:28
            .= G.n /\ A \/ G.n /\ B by XBOOLE_1:23
            .= G1.n \/ G2.n by A13,A75,A78;
          G1.n in rng G1 & rng G1 c= S by A77,FUNCT_1:12;
         then reconsider G1n = G1.n as Element of S;
          G2.n in rng G2 & rng G2 c= S by A77,FUNCT_1:12;
         then reconsider G2n = G2.n as Element of S;
A81:     now assume G1.n meets G2.n;
          then consider v be set such that
A82:        v in G1.n & v in G2.n by XBOOLE_0:3;
           v in G.n /\ A & v in G.n /\ B by A13,A20,A75,A82;
          then v in A & v in B by XBOOLE_0:def 3;
          hence contradiction by A3,XBOOLE_0:3;
         end;
A83:     (M*G).n = M.(G.n) by A75,FUNCT_1:23
                .= M.G1n + M.G2n by A80,A81,MEASURE1:61
                .= (M*G1).n + M.(G2.n) by A77,FUNCT_1:23
                .= (M*G1).n + (M*G2).n by A77,FUNCT_1:23;
          M is nonnegative by MEASURE1:def 11;
         then 0 <= M.G1n & 0 <= M.G2n by MEASURE1:def 4;
         then (0 = (M*G1).n or 0 < (M*G1).n) &
         (0 = (M*G2).n or 0 < (M*G2).n) by A77,FUNCT_1:23;
         then b.n*(M*G).n = b.n*(M*G1).n + b.n*(M*G2).n by A83,EXTREAL1:29;
         then y.n = b.n*(M*G1).n + b.n*(M*G2).n by A7,A74;
         then y.n = y1.n + b.n*(M*G2).n by A42,A76;
         hence y.n = y1.n + y2.n by A45,A76;
        end;
        hence thesis by A72,MESFUNC1:def 3;
       end;
       hence integral'(M,g)=integral'(M,g1)+integral'(M,g2)
        by A29,A44,A47,A48,A56,A64,MESFUNC4:1;
      end;
    end;
   end;
end;

theorem Th74:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL st
f is_simple_func_in S & f is nonnegative holds 0 <= integral'(M,f)
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f be PartFunc of X,ExtREAL;
   assume that
A1:f is_simple_func_in S and
A2:f is nonnegative;
A3:for x be set st x in dom f holds 0 <= f.x by A2,SUPINF_2:70;
   per cases;
   suppose dom f = {};
    hence 0 <= integral'(M,f) by Def14;
   end;
   suppose
A4: dom f <> {};
then A5: integral'(M,f) = integral(X,S,M,f) by Def14;
    consider F be Finite_Sep_Sequence of S,
             a,x be FinSequence of ExtREAL such that
A6:  F,a are_Re-presentation_of f &
     dom x = dom F &
     (for n be Nat st n in dom x holds x.n=a.n*(M*F).n) &
     integral(X,S,M,f)=Sum x by A1,A3,A4,MESFUNC4:4;
     for n be Nat st n in dom x holds 0 <= x.n
    proof
     let n be Nat; assume
A7:  n in dom x;
     per cases;
     suppose F.n = {};
      then M.(F.n) = 0 by MEASURE1:def 11;
      then (M*F).n = 0 by A6,A7,FUNCT_1:23;
      then a.n*(M*F).n = 0 by EXTREAL1:22;
      hence 0 <= x.n by A6,A7;
     end;
     suppose F.n <> {};
      then consider v be set such that
A8:    v in F.n by XBOOLE_0:def 1;
       f.v = a.n by A6,A7,A8,MESFUNC3:def 1;
then A9:   0 <= a.n by A2,SUPINF_2:70;
       F.n in rng F & rng F c= S by A6,A7,FUNCT_1:12;
      then reconsider Fn=F.n as Element of S;
       M is nonnegative by MEASURE1:def 11;
      then 0 <= M.Fn by MEASURE1:def 4;
      then 0 <= (M*F).n by A6,A7,FUNCT_1:23;
      then 0 <= a.n*(M*F).n by A9,EXTREAL2:45;
      hence 0 <= x.n by A6,A7;
     end;
    end;
    hence 0 <= integral'(M,f) by A5,A6,Th59;
   end;
end;

Lm10:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL
  st f is_simple_func_in S & dom f <> {} & f is nonnegative &
     g is_simple_func_in S & dom g = dom f & g is nonnegative &
     (for x be set st x in dom f holds g.x <= f.x)
 holds
    f-g is_simple_func_in S & dom (f-g) <> {} &
    f-g is nonnegative &
    integral(X,S,M,f)=integral(X,S,M,f-g)+integral(X,S,M,g)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL such that
A1:f is_simple_func_in S and
A2:dom f <> {} and
A3:f is nonnegative and
A4:g is_simple_func_in S and
A5:dom g = dom f and
A6:g is nonnegative and
A7:for x be set st x in dom f holds g.x <= f.x;
A8:for x be set st x in dom f holds 0 <= f.x by A3,SUPINF_2:70;
   then consider F be Finite_Sep_Sequence of S,
   a,x be FinSequence of ExtREAL such that
A9: F,a are_Re-presentation_of f and
     dom x = dom F and
     (for n be Nat st n in dom x holds x.n=a.n*(M*F).n) and
     integral(X,S,M,f)=Sum(x) by A1,A2,MESFUNC4:4;
A10:for x be set st x in dom g holds 0 <= g.x by A6,SUPINF_2:70;
   then consider G be Finite_Sep_Sequence of S,
   b,y be FinSequence of ExtREAL such that
A11: G,b are_Re-presentation_of g and
     dom y = dom G and
    (for n be Nat st n in dom y holds y.n=b.n*(M*G).n) and
    integral(X,S,M,g)=Sum(y) by A2,A4,A5,MESFUNC4:4;
   set la = len a;
   set lb = len b;
A12:dom f = union rng F & dom g = union rng G &
   dom F = dom a & dom G = dom b by A9,A11,MESFUNC3:def 1;
   deffunc FG1(Nat) =
   F.(($1-'1) div lb + 1) /\ G.(($1-'1) mod lb + 1);
   consider FG be FinSequence such that
A13:len FG = la*lb and
A14:for k be Nat st k in Seg (la*lb) holds FG.k=FG1(k)
from FINSEQ_1:sch 2;
    FG is FinSequence of S
   proof
A15:now let k be Element of NAT;
     assume k in dom FG;
then A16: k in Seg (la*lb) by A13,FINSEQ_1:def 3;
then A17:  FG.k = F.((k -' 1) div lb + 1) /\ G.((k-'1) mod lb + 1) by A14;
     set i=(k -' 1) div lb + 1;
     set j=(k -' 1) mod lb + 1;
A18:  i >= 0+1 & j >= 0+1 by XREAL_1:8;
A19:  1 <= k & k <= la*lb by A16,FINSEQ_1:3;
then A20:  lb <> 0;
then A21:  lb > 0;
     then lb >= 0+1 by NAT_1:13;
     then lb divides la*lb & 1 <= la*lb & 1 <= lb
       by A19,NAT_D:def 3,XXREAL_0:2;
then A22:  ((la*lb) -' 1) div lb = ((la*lb) div lb) - 1 by NAT_2:17;
      (k -' 1) <= (la*lb -' 1) by A19,BINARITH:60;
     then (k -' 1) div lb <= (la*lb -' 1) div lb by NAT_2:26;
then  (k -' 1) div lb + 1 <= la*lb div lb by A22,XREAL_1:21;
then A23: i <= la by A20,NAT_D:18;
      (k -' 1) mod lb < lb by A21,NAT_D:1;
     then j <= lb by NAT_1:13;
     then i in Seg la & j in Seg lb by A18,A23;
     then i in dom F & j in dom G by A12,FINSEQ_1:def 3;
     then F.i in rng F & G.j in rng G by FUNCT_1:12;
     then F.i is_measurable M & G.j is_measurable M by MEASURE1:def 12;
     then F.i /\ G.j is_measurable M by MEASURE1:66;
     hence FG.k in S by A17,MEASURE1:def 12;
    end;
    let b be set; assume b in rng FG;
    then ex a be set st a in dom FG & b = FG.a by FUNCT_1:def 5;
    hence thesis by A15;
   end;
   then reconsider FG as FinSequence of S;
    for k,l be Nat st k in dom FG & l in dom FG & k <> l holds
    FG.k misses FG.l
   proof
    let k,l be Nat;
    assume
A24: k in dom FG & l in dom FG & k <> l;
    set i=(k-'1) div lb + 1;
    set j=(k-'1) mod lb + 1;
    set n=(l-'1) div lb + 1;
    set m=(l-'1) mod lb + 1;
A25:k in Seg (la*lb) & l in Seg (la*lb) by A13,A24,FINSEQ_1:def 3;
then A26: 1 <= k & k <= la*lb by FINSEQ_1:3;
then A27: lb <> 0;
then A28: lb > 0;
    then lb >= 0+1 by NAT_1:13;
    then lb divides la*lb & 1 <= la*lb & 1 <= lb
       by A26,NAT_D:def 3,XXREAL_0:2;
then A29: ((la*lb) -' 1) div lb = ((la*lb) div lb) - 1 by NAT_2:17;
     k -' 1 <= la*lb -' 1 by A26,BINARITH:60;
    then (k -' 1) div lb <= (la*lb div lb) - 1 by A29,NAT_2:26;
    then (k -' 1) div lb + 1 <= la*lb div lb by XREAL_1:21;
then A30:i <= la by A27,NAT_D:18;
A31: i >= 0+1 & j >= 0+1 & n >= 0+1 & m >= 0+1 by XREAL_1:8;
     (k -' 1) mod lb < lb & (l -' 1) mod lb < lb by A28,NAT_D:1;
    then j <= lb & m <= lb by NAT_1:13;
    then i in Seg la & j in Seg lb & m in Seg lb by A30,A31;
then A32:i in dom F & j in dom G & m in dom G by A12,FINSEQ_1:def 3;
A33: 1 <= l & l <= la*lb by A25,FINSEQ_1:3;
    then l -' 1 <= la*lb -' 1 by BINARITH:60;
    then (l -' 1) div lb <= (la*lb div lb) - 1 by A29,NAT_2:26;
    then (l -' 1) div lb + 1 <= la*lb div lb by XREAL_1:21;
    then n <= la by A27,NAT_D:18;
    then n in Seg la by A31;
then A34:n in dom F by A12,FINSEQ_1:def 3;
A35: now assume A36:i=n & j=m;
      (k-'1)+1=(i-1)*lb+(j-1)+1 & (l-'1)+1=(n-1)*lb+(m-1)+1 by A28,NAT_D:2;
     then k - 1 + 1 = (i-1)*lb + j & l - 1 + 1 = (n-1)*lb+m by A26,A33,
BINARITH:50;
     hence contradiction by A24,A36;
    end;
     FG.k = F.i /\ G.j by A14,A25;
then A37: FG.k /\ FG.l= F.i /\ G.j /\ (F.n /\ G.m) by A14,A25
               .= F.i /\ (G.j /\ (F.n /\ G.m)) by XBOOLE_1:16
               .= F.i /\ (F.n /\ (G.j /\ G.m)) by XBOOLE_1:16
               .= F.i /\ F.n /\ (G.j /\ G.m) by XBOOLE_1:16;
    per cases by A35;
    suppose i <> n;
     then F.i misses F.n by A32,A34,MESFUNC3:4;
     then FG.k /\ FG.l= {} /\ (G.j /\ G.m) by A37,XBOOLE_0:def 7;
     hence FG.k misses FG.l by XBOOLE_0:def 7;
    end;
    suppose j <> m;
     then G.j misses G.m by A32,MESFUNC3:4;
     then FG.k /\ FG.l= (F.i /\ F.n) /\ {} by A37,XBOOLE_0:def 7;
     hence FG.k misses FG.l by XBOOLE_0:def 7;
    end;
   end;
   then reconsider FG as Finite_Sep_Sequence of S by MESFUNC3:4;
   defpred PA[Nat,set] means
    (F.(($1 -' 1) div lb + 1) = {} implies $2 = 0) &
    (F.(($1 -' 1) div lb + 1) <> {} implies $2 = a.(($1 -' 1) div lb + 1));
A38:for k be Nat st k in Seg len FG
    ex v be Element of ExtREAL st PA[k,v]
   proof
    let k be Nat;
    assume k in Seg len FG;
    per cases;
    suppose
A39:  F.((k-'1) div lb + 1)={};
     take v = 0.;
     thus PA[k,v] by A39;
    end;
    suppose
A40:  F.((k-'1) div lb + 1)<>{};
     take v = a.((k-'1) div lb + 1);
     thus PA[k,v] by A40;
    end;
   end;
   consider a1 be FinSequence of ExtREAL such that
A41:dom a1 = Seg len FG &
for k be Nat st k in Seg len FG holds PA[k,a1.k]
from FINSEQ_1:sch 5(A38);
   deffunc X1(Nat) = a1.$1*(M*FG).$1;
   consider x1 be FinSequence of ExtREAL such that
A42: len x1 = len FG &
    for k be Nat st k in Seg len FG holds x1.k=X1(k)
     from FINSEQ_2:sch 1;
A43:dom x1 = dom FG by A42,FINSEQ_3:31;
A44:for n be Nat st n in dom x1 holds x1.n=a1.n*(M*FG).n
   proof
    let n be Nat;
    assume n in dom x1;
    then n in Seg len x1 by FINSEQ_1:def 3;
    hence x1.n = a1.n*(M*FG).n by A42;
   end;
   defpred PB[Nat,set] means
    (G.(($1 -' 1) mod lb + 1) = {} implies $2 = 0) &
    (G.(($1 -' 1) mod lb + 1) <> {} implies $2 = b.(($1 -' 1) mod lb + 1));
A45:for k be Nat st k in Seg len FG
    ex v be Element of ExtREAL st PB[k,v]
   proof
    let k be Nat;
    assume k in Seg len FG;
    per cases;
    suppose
A46:  G.((k-'1) mod lb + 1)={};
     take v = R_EAL 0;
     thus PB[k,v] by A46;
    end;
    suppose
A47:  G.((k-'1) mod lb + 1)<>{};
     take v = b.((k-'1) mod lb + 1);
     thus PB[k,v] by A47;
    end;
   end;
   consider b1 be FinSequence of ExtREAL such that
A48:dom b1 = Seg len FG &
for k be Nat st k in Seg len FG holds PB[k,b1.k]
from FINSEQ_1:sch 5(A45);
   deffunc Y1(Nat) = b1.$1*(M*FG).$1;
   consider y1 be FinSequence of ExtREAL such that
A49: len y1 = len FG &
for k be Nat st k in Seg len FG holds y1.k=Y1(k)
from FINSEQ_2:sch 1;
A50:dom y1 = dom FG by A49,FINSEQ_3:31;
A51:now let n be Nat;
    assume n in dom y1;
    then n in Seg len y1 by FINSEQ_1:def 3;
    hence y1.n = b1.n*(M*FG).n by A49;
   end;
   deffunc C1(Nat) = a1.$1-b1.$1;
   consider c1 be FinSequence of ExtREAL such that
A52:len c1 = len FG and
A53:for k be Nat st k in Seg len FG holds c1.k=C1(k)
from FINSEQ_2:sch 1;
   deffunc Z1(Nat) = c1.$1*(M*FG).$1;
   consider z1 be FinSequence of ExtREAL such that
A54: len z1 = len FG  &
for k be Nat st k in Seg len FG holds z1.k=Z1(k)
from FINSEQ_2:sch 1;
A55:dom z1 = dom FG by A54,FINSEQ_3:31;
A56:for n be Nat st n in dom z1 holds z1.n=c1.n*(M*FG).n
   proof
    let n be Nat;
    assume n in dom z1;
    then n in Seg len z1 by FINSEQ_1:def 3;
    hence z1.n = c1.n*(M*FG).n by A54;
   end;
A57:dom f = union rng FG
   proof
    thus dom f c= union rng FG
    proof
     let z be set;
     assume A58:z in dom f;
     then consider Y be set such that
A59:  z in Y & Y in rng F by A12,TARSKI:def 4;
     consider i be Nat such that
A60:  i in dom F & Y = F.i by A59,FINSEQ_2:11;
     consider Z be set such that
A61:  z in Z & Z in rng G by A5,A12,A58,TARSKI:def 4;
     consider j be Nat such that
A62:  j in dom G & Z = G.j by A61,FINSEQ_2:11;
      i in Seg len a & j in Seg len b by A12,A60,A62,FINSEQ_1:def 3;
then A63: 1 <= i & i <= la & 1 <= j & j <= lb by FINSEQ_1:3;
     then consider i' being Nat such that
A64: i = 1 + i' by NAT_1:10;
      i'*lb + j is Nat;
     then reconsider k=(i-1)*lb+j as Element of NAT by A64;
     consider j' being Nat such that
A65:  j = 1 + j' by A63,NAT_1:10;
      i'*lb >= 0;
     then k >= 0 + j by A64,XREAL_1:8;
then A66:  k >= 1 by A63,XXREAL_0:2;
     then k -' 1 = k - 1 by BINARITH:50;
then A67: k -' 1 = i'*lb + j' by A64,A65;
A68: j' < lb by A63,A65,NAT_1:13;
then A69:  i = (k-'1) div lb +1 by A64,A67,NAT_D:def 1;
A70:  j = (k-'1) mod lb +1 by A65,A67,A68,NAT_D:def 2;
      i-1 <= la-1 by A63,XREAL_1:11;
     then (i-1)*lb <= (la - 1)*lb by XREAL_1:66;
then A71:  k <= (la - 1) * lb + j by XREAL_1:8;
  (la - 1) * lb + j <= (la - 1) * lb + lb by A63,XREAL_1:8;
     then k <= la*lb by A71,XXREAL_0:2;
then A72:  k in Seg (la*lb) by A66;
     then k in dom FG by A13,FINSEQ_1:def 3;
then A73:  FG.k in rng FG by FUNCT_1:def 5;
      z in F.i /\ G.j by A59,A60,A61,A62,XBOOLE_0:def 3;
     then z in FG.k by A14,A69,A70,A72;
     hence thesis by A73,TARSKI:def 4;
    end;
    let z be set;
    assume z in union rng FG;
    then consider Y be set such that
A74:  z in Y & Y in rng FG by TARSKI:def 4;
    consider k be Nat such that
A75:  k in dom FG & Y = FG.k by A74,FINSEQ_2:11;
    set i=(k-'1) div lb +1;
    set j=(k-'1) mod lb +1;
     k in Seg (la*lb) by A13,A75,FINSEQ_1:def 3;
    then FG.k=F.i /\ G.j by A14;
then A76: z in F.i by A74,A75,XBOOLE_0:def 3;
     k in Seg len FG by A75,FINSEQ_1:def 3;
then A77: 1 <= k & k <= la*lb by A13,FINSEQ_1:3;
A78: i >= 0+1 & j >= 0+1 by XREAL_1:8;
A79: lb <> 0 by A77;
    then lb > 0;
    then lb >= 0+1 by NAT_1:13;
    then lb divides la*lb & 1 <= la*lb & 1 <= lb
      by A77,NAT_D:def 3,XXREAL_0:2;
then A80: ((la*lb) -' 1) div lb = ((la*lb) div lb) - 1 by NAT_2:17;
     (k -' 1) <= (la*lb -' 1) by A77,BINARITH:60;
    then (k -' 1) div lb <= (la*lb div lb) - 1 by A80,NAT_2:26;
then A81: i <= la*lb div lb by XREAL_1:21;
     la*lb div lb = la by A79,NAT_D:18;
    then i in Seg la by A78,A81;
    then i in dom F by A12,FINSEQ_1:def 3;
    then F.i in rng F by FUNCT_1:def 5;
    hence thesis by A12,A76,TARSKI:def 4;
   end;
    g is finite by A4,MESFUNC2:def 5;
then A82:dom(f-g) = dom f /\ dom g by MESFUNC2:2;
A83:f-g is finite
    proof
      now let x be Element of X;
      assume A84: x in dom (f-g);
  then A85: |. (f-g).x .| = |. f.x - g.x .| by MESFUNC1:def 4;
       f is finite & g is finite by A1,A4,MESFUNC2:def 5;
  then A86: |. f.x .| < +infty & |. g.x .| < +infty
         by A5,A82,A84,MESFUNC2:def 1;
      then not (f.x = +infty or f.x = -infty) &
      not (g.x = +infty or g.x = -infty) by EXTREAL2:67;
  then A87: |. (f-g).x .| <= |. f.x .| + |. g.x .| by A85,EXTREAL2:69;
       |. f.x .| + |. g.x .| <> +infty by A86,SUPINF_2:8;
      then |. f.x .| + |. g.x .| < +infty by XXREAL_0:4;
      hence |. (f-g).x .| < +infty by A87,XXREAL_0:2;
     end;
     hence thesis by MESFUNC2:def 1;
    end;
     for k being Nat,x,y being Element of X st
      k in dom FG & x in FG.k & y in FG.k holds (f-g).x = (f-g).y
    proof
     let k be Nat;
     let x,y be Element of X;
     assume
   A88: k in dom FG & x in FG.k & y in FG.k;
     then k in Seg (la*lb) by A13,FINSEQ_1:def 3;
then A89:  FG.k = F.( (k-'1) div lb + 1 ) /\ G.( (k-'1) mod lb + 1 ) by A14;
     set i=(k-'1) div lb + 1;
     set j=(k-'1) mod lb + 1;
      k in Seg len FG by A88,FINSEQ_1:def 3;
then A90:  1 <= k & k <= la*lb by A13,FINSEQ_1:3;
A91:  i >= 0+1 & j >= 0+1 by XREAL_1:8;
A92:  lb <> 0 by A90;
then A93:  lb > 0;
     then lb >= 0+1 by NAT_1:13;
     then lb divides la*lb & 1 <= la*lb & 1 <= lb
       by A90,NAT_D:def 3,XXREAL_0:2;
then A94:  ((la*lb) -' 1) div lb = ((la*lb) div lb) - 1 by NAT_2:17;
      (k -' 1) <= (la*lb -' 1) by A90,BINARITH:60;
     then (k -' 1) div lb <= (la*lb div lb) - 1 by A94,NAT_2:26;
     then (k -' 1) div lb + 1 <= la*lb div lb by XREAL_1:21;
then A95: i <= la by A92,NAT_D:18;
      (k -' 1) mod lb < lb by A93,NAT_D:1;
     then j <= lb by NAT_1:13;
     then i in Seg la & j in Seg lb by A91,A95;
then A96: i in dom F & j in dom G by A12,FINSEQ_1:def 3;
A97:  x in F.i & x in G.j & y in F.i & y in G.j by A88,A89,XBOOLE_0:def 3;
then A98:  f.x=a.i & f.y=a.i by A9,A96,MESFUNC3:def 1;
      FG.k in rng FG by A88,FUNCT_1:def 5;
then A99:  x in dom (f-g) & y in dom (f-g) by A5,A57,A82,A88,TARSKI:def 4;
     then (f-g).x= f.x-g.x by MESFUNC1:def 4
           .= a.i-b.j by A11,A96,A97,A98,MESFUNC3:def 1
           .= f.y-g.y by A11,A96,A97,A98,MESFUNC3:def 1;
     hence (f-g).x= (f-g).y by A99,MESFUNC1:def 4;
    end;
    hence A100:f-g is_simple_func_in S by A5,A57,A82,A83,MESFUNC2:def 5;
    thus dom (f-g) <> {} by A2,A5,A82;
A101:for x be set st x in dom (f-g) holds 0 <= (f-g).x
    proof
     let x be set;
     assume A102:x in dom (f-g);
      g is finite by A4,MESFUNC2:def 5;
     then |. g.x .| < +infty by A5,A82,A102,MESFUNC2:def 1;
then A103:  g.x < +infty & -infty < f.x by A3,EXTREAL2:58,SUPINF_2:70;
      g.x <= f.x by A5,A7,A82,A102;
     then 0 <= f.x - g.x by A103,EXTREAL1:40;
     hence 0 <= (f - g).x by A102,MESFUNC1:def 4;
    end;
    hence f-g is nonnegative by SUPINF_2:71;
A104:for k be Nat st k in dom FG
     for x be set st x in FG.k holds f.x=a1.k
    proof
     let k be Nat;
     assume
A105:   k in dom FG;
     let x be set;
     assume
A106:  x in FG.k;
      k in Seg (la*lb) by A13,A105,FINSEQ_1:def 3;
then A107:  FG.k = F.((k-'1) div lb + 1) /\ G.((k-'1) mod lb + 1) by A14;
     set i=(k-'1) div lb + 1;
     set j=(k-'1) mod lb + 1;
      k in Seg len FG by A105,FINSEQ_1:def 3;
then A108:  1 <= k & k <= la*lb by A13,FINSEQ_1:3;
A109:  i >= 0+1 & j >= 0+1 by XREAL_1:8;
A110:  lb <> 0 by A108;
     then lb > 0;
     then lb >= 0+1 by NAT_1:13;
     then lb divides la*lb & 1 <= la*lb & 1 <= lb
      by A108,NAT_D:def 3,XXREAL_0:2;
then A111:  ((la*lb) -' 1) div lb = ((la*lb) div lb) - 1 by NAT_2:17;
      (k -' 1) <= (la*lb -' 1) by A108,BINARITH:60;
     then (k -' 1) div lb <= (la*lb -' 1) div lb by NAT_2:26;
then A112:  i <= la*lb div lb by A111,XREAL_1:21;
      la*lb div lb = la by A110,NAT_D:18;
     then i in Seg la by A109,A112;
then A113: i in dom F by A12,FINSEQ_1:def 3;
A114:  k in Seg len FG by A105,FINSEQ_1:def 3;
A115:  x in F.i by A106,A107,XBOOLE_0:def 3;
     then f.x = a.i by A9,A113,MESFUNC3:def 1;
     hence f.x = a1.k by A41,A114,A115;
    end;
     dom FG = dom a1 by A41,FINSEQ_1:def 3;
    then FG,a1 are_Re-presentation_of f by A57,A104,MESFUNC3:def 1;
then A116: integral(X,S,M,f)=Sum x1 by A1,A2,A8,A43,A44,MESFUNC4:3;
A117:for k be Nat st k in dom FG
     for x be set st x in FG.k holds g.x=b1.k
    proof
     let k be Nat;
     assume A118: k in dom FG;
     let x be set;
     assume
  A119: x in FG.k;
      k in Seg (la*lb) by A13,A118,FINSEQ_1:def 3;
 then A120: FG.k = F.( (k-'1) div lb + 1 ) /\ G.( (k-'1) mod lb + 1 ) by A14;
     set i=(k-'1) div lb + 1;
     set j=(k-'1) mod lb + 1;
      k in Seg len FG by A118,FINSEQ_1:def 3;
then A121:  1 <= k & k <= la*lb by A13,FINSEQ_1:3;
A122:  i >= 0+1 & j >= 0+1 by XREAL_1:8;
      lb <> 0 by A121;
     then lb > 0;
     then (k -' 1) mod lb < lb by NAT_D:1;
     then j <= lb by NAT_1:13;
     then j in Seg lb by A122;
then A123: j in dom G by A12,FINSEQ_1:def 3;
 A124: x in G.j by A119,A120,XBOOLE_0:def 3;
 A125: k in Seg len FG by A118,FINSEQ_1:def 3;
     thus g.x=b.j by A11,A123,A124,MESFUNC3:def 1
                  .=b1.k by A48,A124,A125;
    end;
     dom FG = dom b1 by A48,FINSEQ_1:def 3;
    then FG,b1 are_Re-presentation_of g by A5,A57,A117,MESFUNC3:def 1;
then A126: integral(X,S,M,g)=Sum y1 by A2,A4,A5,A10,A50,A51,MESFUNC4:3;
A127:for k be Nat st k in dom FG
     for x be set st x in FG.k holds (f-g).x=c1.k
    proof
     let k be Nat;
     assume
   A128: k in dom FG;
     let x be set;
     assume
A129:   x in FG.k;
      FG.k in rng FG by A128,FUNCT_1:def 5;
     then x in dom (f-g) by A5,A57,A82,A129,TARSKI:def 4;
 then A130: (f-g).x= f.x-g.x by MESFUNC1:def 4;
      k in Seg len FG by A128,FINSEQ_1:def 3;
     then a1.k-b1.k = c1.k by A53;
     then a1.k-g.x = c1.k by A117,A128,A129;
     hence (f-g).x =c1.k by A104,A128,A129,A130;
    end;
     dom FG = dom c1 by A52,FINSEQ_3:31;
    then FG,c1 are_Re-presentation_of (f-g) by A5,A57,A82,A127,MESFUNC3:def 1;
then A131: integral(X,S,M,f-g)=Sum z1
              by A2,A5,A55,A56,A82,A100,A101,MESFUNC4:3;
A132: for i be Nat st i in dom y1 holds 0 <= y1.i
    proof
     let i be Nat;
     assume A133: i in dom y1;
then A134:  y1.i=b1.i*(M*FG).i by A51;
A135:  i in Seg len FG by A49,A133,FINSEQ_1:def 3;
     set i' = (i -' 1) mod lb + 1;
A136:  1 <= i & i <= la*lb by A13,A135,FINSEQ_1:3;
A137:  i' >= 0+1 by XREAL_1:8;
      lb <> 0 by A136;
then  lb > 0;
     then (i -' 1) mod lb < lb by NAT_D:1;
     then i' <= lb by NAT_1:13;
     then i' in Seg lb by A137;
then A138:  i' in dom G by A12,FINSEQ_1:def 3;
     per cases;
     suppose A139:G.i' <> {};
      then consider x' be set such that
A140:   x' in G.i' by XBOOLE_0:def 1;
       g.x' = b.i' by A11,A138,A140,MESFUNC3:def 1 .= b1.i by A48,A135,A139
;
then A141:  0 <= b1.i by A6,SUPINF_2:70;
       FG.i in rng FG by A50,A133,FUNCT_1:12;
      then reconsider FGi = FG.i as Element of S;
      reconsider EMPTY = {} as Element of S by MEASURE1:21;
       EMPTY c= FGi by XBOOLE_1:2;
then A142:  M.({}) <= M.FGi by MEASURE1:62;
       M.{} = 0 by MEASURE1:def 11;
      then 0 <= (M*FG).i by A50,A133,A142,FUNCT_1:23;
      hence 0 <= y1.i by A134,A141,EXTREAL2:45;
     end;
     suppose
A143:   G.i' = {};
       FG.i = F.((i-'1) div lb + 1) /\ G.i' by A13,A14,A135;
      then M.(FG.i) = 0 by A143,MEASURE1:def 11;
      then (M*FG).i = 0 by A50,A133,FUNCT_1:23;
      hence 0 <= y1.i by A134,EXTREAL1:22;
     end;
    end;
A144: for i be Nat st i in dom x1 holds 0 <= z1.i
    proof
     let i be Nat;
     assume
A145:    i in dom x1;
then A146:  (M*FG).i = M.(FG.i) by A43,FUNCT_1:23;
      FG.i in rng FG & rng FG c= S by A43,A145,FUNCT_1:12;
     then reconsider V = FG.i as Element of S;
     reconsider EMPTY = {} as Element of S by PROB_1:42;
A147:  i in Seg len FG by A42,A145,FINSEQ_1:def 3;
      EMPTY c= V by XBOOLE_1:2;
     then M.EMPTY <= M.V by MEASURE1:62;
then A148:  0 <= (M*FG).i by A146,MEASURE1:def 11;
     per cases;
     suppose FG.i <> {};
      then consider x be set such that
A149:    x in FG.i by XBOOLE_0:def 1;
       FG.i in rng FG by A43,A145,FUNCT_1:12;
then A150:   x in union rng FG by A149,TARSKI:def 4;
      then g.x <= f.x by A7,A57;
      then g.x <= a1.i by A43,A104,A145,A149;
then A151:   b1.i <= a1.i by A43,A117,A145,A149;
A152:  g is finite by A4,MESFUNC2:def 5;
      reconsider x as Element of X by A57,A150;
       |. g.x .| < +infty by A5,A57,A150,A152,MESFUNC2:def 1;
      then |. b1.i .| < +infty by A43,A117,A145,A149;
      then -(+infty) < b1.i & b1.i < +infty by EXTREAL2:58;
      then b1.i <> -infty & b1.i <> +infty by SUPINF_2:def 3;
      then 0 <= a1.i - b1.i by A151,MEASURE5:1;
      then 0 <= c1.i by A53,A147;
      then 0 <= c1.i*(M*FG).i by A148,EXTREAL2:45;
      hence thesis by A43,A55,A56,A145;
     end;
     suppose FG.i = {};
      then (M*FG).i = 0 by A146,MEASURE1:def 11;
      then c1.i * (M*FG).i = 0 by EXTREAL1:22;
      hence thesis by A43,A55,A56,A145;
     end;
    end;
     x1=z1+y1
    proof
      not -infty in rng y1
     proof
      assume -infty in rng y1;
      then consider i be set such that
A153:   i in dom y1 and
A154:   y1.i = -infty by FUNCT_1:def 5;
       i is Nat by A153,ORDINAL1:def 13;
      hence contradiction by A132,A153,A154;
     end;
then A155:  z1"{+infty} /\ y1"{-infty}
       = z1"{+infty} /\ {} by FUNCT_1:142
       .={};
      not -infty in rng z1
     proof
      assume -infty in rng z1;
      then consider i be set such that
A156:   i in dom z1 and
A157:   z1.i = -infty by FUNCT_1:def 5;
       i is Nat by A156,ORDINAL1:def 13;
      hence contradiction by A43,A55,A144,A156,A157;
     end;
then  z1"{-infty} /\ y1"{+infty}
       ={} /\ y1"{+infty} by FUNCT_1:142 .={};
then A158:  dom (z1+y1)
      =(dom z1 /\ dom y1) \ ({} \/ {}) by A155,MESFUNC1:def 3
     .=dom x1 by A42,A50,A55,FINSEQ_3:31;
A159:  for k be Nat  st k in dom x1 holds x1.k = (z1+y1).k
     proof
      let k be Nat;
      assume
A160:     k in dom x1;
then A161:   k in Seg len FG by A42,FINSEQ_1:def 3;
      set p=(k-'1) div lb + 1;
      set q=(k-'1) mod lb + 1;
A162:   1 <= k & k <= la*lb by A13,A161,FINSEQ_1:3;
A163:   p >= 0+1 & q >= 0+1 by XREAL_1:8;
A164:   lb <> 0 by A162;
then A165:   lb > 0;
      then lb >= 0+1 by NAT_1:13;
      then lb divides la*lb & 1 <= la*lb & 1 <= lb
        by A162,NAT_D:def 3,XXREAL_0:2;
then A166:   ((la*lb) -' 1) div lb = ((la*lb) div lb) - 1 by NAT_2:17;
       (k -' 1) <= (la*lb -' 1) by A162,BINARITH:60;
      then (k -' 1) div lb <= (la*lb div lb) - 1 by A166,NAT_2:26;
      then p <= la*lb div lb by XREAL_1:21;
      then p <= la by A164,NAT_D:18;
then A167:  p in Seg la by A163;
       (k -' 1) mod lb < lb by A165,NAT_D:1;
      then q <= lb by NAT_1:13;
      then q in Seg lb by A163;
then A168:   p in dom F & q in dom G by A12,A167,FINSEQ_1:def 3;
A169:   (c1.k+b1.k)*(M*FG).k = c1.k*(M*FG).k + b1.k*(M*FG).k
      proof
       per cases;
       suppose FG.k <> {};
        then F.p /\ G.q <> {} by A13,A14,A161;
        then consider v be set such that
A170:      v in F.p /\ G.q by XBOOLE_0:def 1;
A171:     v in F.p & v in G.q by A170,XBOOLE_0:def 3;
then A172:     a.p = f.v & b.q = g.v by A9,A11,A168,MESFUNC3:def 1;
A173:    F.p <> {} & G.q <> {} by A170;
         F.p in rng F & G.q in rng G by A168,FUNCT_1:12;
then A174:     v in dom f & v in dom g by A12,A171,TARSKI:def 4;
         0 <= b.q by A6,A172,SUPINF_2:70;
then A175:     0 = b1.k or 0 < b1.k by A48,A161,A173;
        reconsider v as Element of X by A174;
         g is finite by A4,MESFUNC2:def 5;
        then |. g.v .| < +infty by A174,MESFUNC2:def 1;
        then |. b1.k .| < +infty by A48,A161,A172,A173;
        then -(+infty) < b1.k & b1.k < +infty by EXTREAL2:58;
then A176:     -infty < b1.k & b1.k < +infty by SUPINF_2:def 3;
         b.q <= a.p by A7,A172,A174;
        then b1.k <= a.p by A48,A161,A173;
        then b1.k <= a1.k by A41,A161,A173;
        then 0 <= a1.k - b1.k by A176,MEASURE5:1;
        then 0 = c1.k or 0 < c1.k by A53,A161;
        hence thesis by A175,EXTREAL1:29;
       end;
       suppose FG.k = {};
        then M.(FG.k) = 0 by MEASURE1:def 11;
        then (M*FG).k = 0 by A43,A160,FUNCT_1:23;
        hence thesis by EXTREAL1:29;
       end;
      end;
A177:   a1.k <> +infty & a1.k <> -infty & b1.k <> +infty & b1.k <> -infty
      proof
        now per cases;
        suppose
A178:      F.p <> {};
         then consider v be set such that
A179:       v in F.p by XBOOLE_0:def 1;
          a1.k = a.p by A41,A161,A178;
then A180:      a1.k = f.v by A9,A168,A179,MESFUNC3:def 1;
          F.p in rng F by A168,FUNCT_1:12;
then A181:      v in dom f by A12,A179,TARSKI:def 4;
         then reconsider v as Element of X;
          f is finite by A1,MESFUNC2:def 5;
         then |. f.v .| < +infty by A181,MESFUNC2:def 1;
         then -(+infty) < a1.k & a1.k < +infty by A180,EXTREAL2:58;
         hence a1.k <> +infty & -infty <> a1.k by SUPINF_2:def 3;
        end;
        suppose F.p = {};
         hence a1.k <> +infty & -infty <> a1.k by A41,A161;
        end;
       end;
       hence +infty <> a1.k & a1.k <> -infty;
        now per cases;
        suppose
      A182:G.q <> {};
         then consider v be set such that
A183:       v in G.q by XBOOLE_0:def 1;
          b1.k = b.q by A48,A161,A182;
then A184:      b1.k = g.v by A11,A168,A183,MESFUNC3:def 1;
          G.q in rng G by A168,FUNCT_1:12;
then A185:      v in dom g by A12,A183,TARSKI:def 4;
         then reconsider v as Element of X;
          g is finite by A4,MESFUNC2:def 5;
         then |. g.v .| < +infty by A185,MESFUNC2:def 1;
         then -(+infty) < b1.k & b1.k < +infty by A184,EXTREAL2:58;
         hence b1.k <> +infty & -infty <> b1.k by SUPINF_2:def 3;
        end;
        suppose G.q = {};
         hence b1.k <> +infty & -infty <> b1.k by A48,A161;
        end;
       end;
       hence +infty <> b1.k & b1.k <> -infty;
      end;
       c1.k = a1.k - b1.k by A53,A161;
      then c1.k + b1.k = a1.k - (b1.k - b1.k) by A177,EXTREAL2:41
               .= a1.k + 0. by EXTREAL1:9,24
               .= a1.k by SUPINF_2:18;
      hence x1.k = (c1.k+b1.k)*(M*FG).k by A44,A160
              .= z1.k + b1.k*(M*FG).k by A43,A55,A56,A160,A169
              .= z1.k + y1.k by A43,A50,A51,A160
              .= (z1+y1).k by A158,A160,MESFUNC1:def 3;
     end;
      dom(z1+y1) = Seg len x1 by A158,FINSEQ_1:def 3;
     then z1+y1 is FinSequence by FINSEQ_1:def 2;
     hence thesis by A158,A159,FINSEQ_1:17;
    end;
   hence thesis by A43,A50,A55,A116,A126,A131,A132,A144,MESFUNC4:1;
end;

theorem Th75:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
  f is_simple_func_in S & f is nonnegative &
  g is_simple_func_in S & g is nonnegative &
  (for x be set st x in dom(f-g) holds g.x <= f.x)
 holds
  dom (f-g) = dom f /\ dom g &
  integral'(M,f|dom(f-g))=integral'(M,f-g)+integral'(M,g|dom(f-g))
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL such that
A1:f is_simple_func_in S and
A2:f is nonnegative and
A3:g is_simple_func_in S and
A4:g is nonnegative and
A5:for x be set st x in dom(f-g) holds g.x <= f.x;
    f is without+infty & g is without-infty by A1,A3,Th20;
   then not +infty in rng f & not -infty in rng g by Def3,Def4;
then A6:f"{+infty} = {} & g"{-infty} = {} by FUNCT_1:142;
then A7:(dom f /\ dom g)
    \((f"{+infty} /\ g"{+infty})\/(f"{-infty} /\ g"{-infty}))
     = dom f /\ dom g;
   hence A8:dom(f-g) = dom f /\ dom g by MESFUNC1:def 4;
    (-1)(#)g is_simple_func_in S by A3,Th45;
   then -g is_simple_func_in S by MESFUNC2:11;
   then f+(-g) is_simple_func_in S by A1,Th44;
then f-g is_simple_func_in S by MESFUNC2:9;
   then dom(f-g) is Element of S by Th43;
then A9:f|dom(f-g) is_simple_func_in S &
   g|dom(f-g) is_simple_func_in S by A1,A3,Th40;
A10:f|dom(f-g) is nonnegative & g|dom(f-g) is nonnegative by A2,A4,Th21;
    dom(f|dom(f-g)) = dom f /\ dom(f-g) &
   dom(g|dom(f-g)) = dom g /\ dom(f-g) by FUNCT_1:68;
then A11:dom(f|dom(f-g)) = dom f /\ dom f /\ dom g &
   dom(g|dom(f-g)) = dom g /\ dom g /\ dom f by A8,XBOOLE_1:16;
A12:for x be set st x in dom(f|dom(f-g)) holds
    (g|dom(f-g)).x <= (f|dom(f-g)).x
   proof
    let x be set;
    assume
A13: x in dom(f|dom(f-g));
    then g.x <= f.x by A5,A8,A11;
    then (g|dom(f-g)).x <= f.x by A8,A11,A13,FUNCT_1:72;
    hence (g|dom(f-g)).x <= (f|dom(f-g)).x by A8,A11,A13,FUNCT_1:72;
   end;
   thus integral'(M,f|dom(f-g))=integral'(M,f-g)+integral'(M,g|dom(f-g))
   proof
    per cases;
    suppose
A14:  dom(f-g) = {};
then A15:  integral'(M,f-g) = 0 by Def14;
      dom(f|dom(f-g)) = dom f /\ dom(f-g) by FUNCT_1:68;
then A16:  integral'(M,f|dom(f-g)) = 0 by A14,Def14;
      dom(g|dom(f-g)) = dom g /\ dom(f-g) by FUNCT_1:68;
     then integral'(M,g|dom(f-g)) = 0 by A14,Def14;
     hence integral'(M,f|dom(f-g))
       = integral'(M,f-g) + integral'(M,g|dom(f-g)) by A15,A16,SUPINF_2:18;
    end;
    suppose
A17:  dom(f-g) <> {};
then A18:  integral(X,S,M,f|dom(f-g))
      = integral(X,S,M,(f|dom(f-g) - g|dom(f-g)))
        +integral(X,S,M,g|dom(f-g)) by A8,A9,A10,A11,A12,Lm10;
      (f|dom(f-g))"{+infty} = dom(f-g) /\ f"{+infty} &
     (g|dom(f-g))"{-infty} = dom(f-g) /\ g"{-infty} by FUNCT_1:139;
     then (dom(f|dom(f-g)) /\ dom(g|dom(f-g))) \
      ( ((f|dom(f-g))"{+infty} /\ (g|dom(f-g))"{+infty}) \/
       ((f|dom(f-g))"{-infty} /\ (g|dom(f-g))"{-infty}) )
      = dom(f-g) by A6,A7,A11,MESFUNC1:def 4;
then A19:  dom(f|dom(f-g) - g|dom(f-g)) = dom(f-g) by MESFUNC1:def 4;
      for x be Element of X st x in dom(f|dom(f-g) - g|dom(f-g)) holds
      (f|dom(f-g) - g|dom(f-g)).x = (f-g).x
     proof
      let x be Element of X;
      assume
A20:   x in dom(f|dom(f-g) - g|dom(f-g));
      then (f|dom(f-g) - g|dom(f-g)).x
        = (f|dom(f-g)).x - (g|dom(f-g)).x by MESFUNC1:def 4
       .= f.x - (g|dom(f-g)).x by A19,A20,FUNCT_1:72
       .= f.x - g.x by A19,A20,FUNCT_1:72;
      hence thesis by A19,A20,MESFUNC1:def 4;
     end;
then A21:  integral(X,S,M,f|dom(f-g))
      = integral(X,S,M,f-g) + integral(X,S,M,g|dom(f-g))
        by A18,A19,PARTFUN1:34;
A22:  integral(X,S,M,f|dom(f-g)) = integral'(M,f|dom(f-g))
       by A8,A11,A17,Def14;
      integral(X,S,M,g|dom(f-g)) = integral'(M,g|dom(f-g))
       by A8,A11,A17,Def14;
     hence integral'(M,f|dom(f-g))
      = integral'(M,f-g) + integral'(M,g|dom(f-g))
         by A17,A21,A22,Def14;
    end;
   end;
end;

theorem Th76:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
f is_simple_func_in S &
g is_simple_func_in S &
f is nonnegative & g is nonnegative &
(for x be set st x in dom(f-g) holds g.x <= f.x)
holds
  integral'(M,g|dom(f-g)) <= integral'(M,f|dom(f-g))
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f,g be PartFunc of X,ExtREAL;
   assume that
A1:f is_simple_func_in S & g is_simple_func_in S and
A2:f is nonnegative & g is nonnegative and
A3:(for x be set st x in dom(f-g) holds g.x <= f.x);
    dom f is Element of S & dom g is Element of S by A1,Th43;
   then dom f /\ dom g in S by FINSUB_1:def 2;
   then dom(f-g) in S by A1,A2,A3,Th75;
then A4:f|dom(f-g) is_simple_func_in S &
   g|dom(f-g) is_simple_func_in S by A1,Th40;
A5:g|dom(f-g) is nonnegative by A2,Th21;
    (-1)(#)g is_simple_func_in S by A1,Th45;
   then -g is_simple_func_in S by MESFUNC2:11;
   then f+(-g) is_simple_func_in S by A1,Th44;
then A6:f-g is_simple_func_in S by MESFUNC2:9;
A7:integral'(M,f|dom(f-g)) = integral'(M,f-g)+integral'(M,g|dom(f-g))
     by A1,A2,A3,Th75;
    now assume integral'(M,f|dom(f-g)) <> +infty;
     f-g is nonnegative by A1,A3,Th46;
    then 0 <= integral'(M,f-g) & 0 <= integral'(M,g|dom(f-g)) by A4,A5,A6,Th74
;
    hence integral'(M,g|dom(f-g)) <= integral'(M,f|dom(f-g)) by A7,SUPINF_2:20;
   end;
   hence thesis by XXREAL_0:4;
end;

theorem Th77:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    c be R_eal st
 0 <= c & f  is_simple_func_in S &
 (for x be set st x in dom f holds f.x=c)
holds
   integral'(M,f) = c*(M.(dom f))
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f be PartFunc of X,ExtREAL;
   let c be R_eal;
   assume that
A1:0 <= c and
A2:f is_simple_func_in S and
A3:for x be set st x in dom f holds f.x = c;
   reconsider A = dom f as Element of S by A2,Th43;
A4:for x be set st x in dom f holds 0 <= f.x by A1,A3;
   per cases;
   suppose
A5: dom f = {};
then A6: integral'(M,f) = 0 by Def14;
     M.A = 0 by A5,MEASURE1:def 11;
    hence thesis by A6,EXTREAL1:22;
   end;
   suppose
A7: dom f <> {};
    set F = <* dom f *>;
A8: rng F = {A} by FINSEQ_1:55;
     rng F c= S
    proof
     let z be set;
     assume z in rng F;
     then z = A by A8,TARSKI:def 1;
     hence z in S;
    end;
    then reconsider F as FinSequence of S by FINSEQ_1:def 4;
     for i,j be Nat st i in dom F & j in dom F & i <> j holds
     F.i misses F.j
    proof
     let i,j be Nat;
     assume
A9:   i in dom F & j in dom F & i <> j;
      dom F = {1} by FINSEQ_1:4,55;
     then i = 1 & j = 1 by A9,TARSKI:def 1;
     hence thesis by A9;
    end;
    then reconsider F as Finite_Sep_Sequence of S by MESFUNC3:4;
    reconsider a = <* c *> as FinSequence of ExtREAL;
A10: dom f = union rng F by A8,ZFMISC_1:31;
A11: dom F = Seg 1 by FINSEQ_1:55 .= dom a by FINSEQ_1:55;
     for n be Nat st n in dom F
     for x be set st x in F.n holds f.x = a.n
    proof
     let n be Nat; assume
A12:   n in dom F;
     let x be set; assume
A13:  x in F.n;
      n in {1} by A12,FINSEQ_1:4,55;
then A14: n = 1 by TARSKI:def 1;
     then x in dom f by A13,FINSEQ_1:57;
     then f.x = c by A3;
     hence f.x = a.n by A14,FINSEQ_1:57;
    end;
then A15: F,a are_Re-presentation_of f by A10,A11,MESFUNC3:def 1;
    set x = <* c * M.A *>;
    reconsider x as FinSequence of ExtREAL;
A16:dom x = Seg 1 by FINSEQ_1:55 .= dom F by FINSEQ_1:55;
     for n be Nat st n in dom x holds x.n = a.n*(M*F).n
    proof
     let n be Nat;
     assume
A17:  n in dom x;
     then n in {1} by FINSEQ_1:4,55;
then A18: n = 1 by TARSKI:def 1;
then A19: x.n = c*M.A by FINSEQ_1:57;
      (M*F).n = M.(F.n) by A16,A17,FUNCT_1:23
            .= M.A by A18,FINSEQ_1:57;
     hence thesis by A18,A19,FINSEQ_1:57;
    end;
    then integral(X,S,M,f) = Sum x by A2,A4,A7,A15,A16,MESFUNC4:3;
then A20:integral'(M,f) = Sum x by A7,Def14;
A21: 1 = len x by FINSEQ_1:57;
     for n be Nat st n in dom x holds x.n = c*M.A
    proof
     let n be Nat;
     assume n in dom x;
     then n in {1} by FINSEQ_1:4,55;
     then n = 1 by TARSKI:def 1;
     hence x.n = c*M.A by FINSEQ_1:57;
    end;
    then Sum x = R_EAL 1 *(c*M.A) by A21,MESFUNC3:18;
    hence thesis by A20,EXTREAL2:4;
   end;
end;

theorem Th78:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL st
 f is_simple_func_in S & f is nonnegative holds
  integral'(M,f|eq_dom(f,R_EAL 0)) = 0
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f be PartFunc of X,ExtREAL;
   assume that
A1:f is_simple_func_in S and
A2:f is nonnegative;
   set A = dom f;
   set g = f|(A /\ eq_dom(f,R_EAL 0));
A3:g is_simple_func_in S
   proof
A4: f is finite by A1,MESFUNC2:def 5;
     for x be Element of X st x in dom g holds |. g.x .| < +infty
    proof
     let x be Element of X;
     assume
A5:  x in dom g;
     then x in dom f /\ (A/\eq_dom(f,R_EAL 0)) by FUNCT_1:68;
then A6: x in dom f by XBOOLE_0:def 3;
      g.x = f.x by A5,FUNCT_1:70;
     hence thesis by A4,A6,MESFUNC2:def 1;
    end;
then A7: g is finite by MESFUNC2:def 1;
     ex G be Finite_Sep_Sequence of S st
     (dom g = union rng G &
      for n be Nat, x,y be Element of X st
        n in dom G & x in G.n & y in G.n holds g.x = g.y)
    proof
     consider F be Finite_Sep_Sequence of S such that
A8:   dom f = union rng F &
      for n be Nat, x,y be Element of X st
       n in dom F & x in F.n & y in F.n holds f.x = f.y by A1,MESFUNC2:def 5;
     deffunc G(Nat) = F.$1 /\ (A/\eq_dom(f,R_EAL 0));
     reconsider A as Element of S by A8,MESFUNC2:34;
      f is_measurable_on A by A1,MESFUNC2:37;
     then A /\ less_dom(f,R_EAL 0) is_measurable_on S by MESFUNC1:def 17;
     then A /\ less_dom(f,R_EAL 0) in S by MESFUNC1:def 11;
     then A\(A /\ less_dom(f,R_EAL 0)) in S by PROB_1:44;
     then reconsider A1 = A/\great_eq_dom(f,0.) as Element of S by MESFUNC1:18;
      f is_measurable_on A1 by A1,MESFUNC2:37;
     then (A/\great_eq_dom(f,R_EAL 0))/\less_eq_dom(f,R_EAL 0)
is_measurable_on S
       by MESFUNC1:32;
     then A /\ eq_dom(f,R_EAL 0) is_measurable_on S by MESFUNC1:22;
     then reconsider A2 = A /\ eq_dom(f,R_EAL 0) as Element of S by
MESFUNC1:def 11;
     consider G be FinSequence such that
A9:  len G = len F &
for n be Nat st n in Seg len F holds G.n = G(n)
from FINSEQ_1:sch 2;
A10: dom G = Seg len F by A9,FINSEQ_1:def 3;
then A11: dom G = dom F by FINSEQ_1:def 3;
      dom F = Seg len F by FINSEQ_1:def 3;
     then for i be Nat st i in dom F holds G.i = F.i /\ A2 by A9;
     then reconsider G as Finite_Sep_Sequence of S by A11,Th41;
     take G;
      for i be Nat st i in dom G holds G.i = A2 /\ F.i by A9,A10;
then A12: union rng G = A2 /\ dom f by A8,A11,MESFUNC3:6
                .= dom g by FUNCT_1:68;
      for i be Nat, x,y be Element of X st
      i in dom G & x in G.i & y in G.i holds g.x = g.y
     proof
      let i be Nat;
      let x,y be Element of X;
      assume
A13:   i in dom G & x in G.i & y in G.i;
      then G.i = F.i /\ A2 by A9,A10;
then A14:  x in F.i & y in F.i by A13,XBOOLE_0:def 3;
       G.i in rng G by A13,FUNCT_1:12;
      then x in dom g & y in dom g by A12,A13,TARSKI:def 4;
      then g.x = f.x & g.y = f.y by FUNCT_1:70;
      hence thesis by A8,A11,A13,A14;
     end;
     hence thesis by A12;
    end;
    hence thesis by A7,MESFUNC2:def 5;
   end;
A15:for x be set st x in dom g holds 0 <= g.x
   proof
    let x be set;
    assume
A16: x in dom g;
     0 <= f.x by A2,SUPINF_2:70;
    hence thesis by A16,FUNCT_1:70;
   end;
    for x be set st x in eq_dom(f,R_EAL 0) holds x in A by MESFUNC1:def 16;
   then eq_dom(f,R_EAL 0) c= A by TARSKI:def 3;
then A17:f|(A/\eq_dom(f,R_EAL 0)) = f|eq_dom(f,R_EAL 0) by XBOOLE_1:28;
    now assume
A18: dom g <> {};
    then consider F be Finite_Sep_Sequence of S,
             a,x be FinSequence of ExtREAL such that
A19:  F,a are_Re-presentation_of g &
     a.1 =0 &
     (for n be Nat st 2 <= n & n in dom a holds
        0 < a.n & a.n < +infty ) &
     dom x = dom F &
     (for n be Nat st n in dom x holds x.n=a.n*(M*F).n) &
     integral(X,S,M,g)=Sum(x) by A3,A15,MESFUNC3:def 2;
A20: for x be set st x in dom g holds g.x = 0
    proof
     let x be set;
     assume
A21:  x in dom g;
     then x in dom f /\ (A /\ eq_dom(f,R_EAL 0)) by FUNCT_1:68;
     then x in dom f & x in A /\ eq_dom(f,R_EAL 0) by XBOOLE_0:def 3;
     then x in eq_dom(f,R_EAL 0) by XBOOLE_0:def 3;
     then ex z be R_eal st z = f.x & 0 = z by MESFUNC1:def 16;
     hence thesis by A21,FUNCT_1:70;
    end;
A22: for n be Nat st n in dom F holds a.n = 0 or F.n = {}
    proof
     let n be Nat;
     assume
A23:   n in dom F;
      now assume F.n <> {};
      then consider x be set such that
A24:    x in F.n by XBOOLE_0:def 1;
       F.n in rng F by A23,FUNCT_1:12;
      then x in union rng F by A24,TARSKI:def 4;
      then x in dom g by A19,MESFUNC3:def 1;
      then g.x = 0 by A20;
      hence thesis by A19,A23,A24,MESFUNC3:def 1;
     end;
     hence thesis;
    end;
A25: for n be Nat st n in dom x holds x.n = 0
    proof
     let n be Nat;
     assume
A26:   n in dom x;
     per cases by A19,A22,A26;
     suppose a.n = 0;
      then a.n*(M*F).n = 0 by EXTREAL1:22;
      hence thesis by A19,A26;
     end;
     suppose
       F.n = {};
      then M.(F.n) = 0 by MEASURE1:def 11;
      then (M*F).n = 0 by A19,A26,FUNCT_1:23;
      then a.n*(M*F).n = 0 by EXTREAL1:22;
      hence thesis by A19,A26;
     end;
    end;
     Sum x = 0
    proof
     consider sumx be Function of NAT,ExtREAL such that
A27:   Sum x = sumx.(len x) & sumx.0 = 0 &
      for i be Element of NAT st i < len x holds sumx.(i+1)=sumx.i + x.(i+1)
       by CONVFUN1:def 5;
      now assume x <> {};
      defpred P[Nat] means
       $1 <= len x implies sumx.$1 = 0;
A28:   P[0] by A27;
A29:   for k be Nat st P[k] holds P[k+1]
      proof
       let k be Nat;
       assume
A30:     P[k];
       assume
A31:     k+1 <= len x;
        reconsider k as Element of NAT by ORDINAL1:def 13;
        k < len x by A31,NAT_1:13;
then A32:    sumx.(k+1) = sumx.k + x.(k+1) by A27;
        1 <= k+1 by NAT_1:11;
       then k+1 in Seg(len x) by A31;
       then k+1 in dom x by FINSEQ_1:def 3;
       then x.(k+1) = 0 by A25;
       hence thesis by A30,A31,A32,NAT_1:13,SUPINF_2:18;
      end;
       for i be Nat holds P[i] from NAT_1:sch 2(A28,A29);
      hence thesis by A27;
     end;
     hence thesis by A27,FINSEQ_1:25;
    end;
    hence thesis by A17,A18,A19,Def14;
   end;
   hence thesis by A17,Def14;
end;

theorem Th79:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    B be Element of S,
    f be PartFunc of X,ExtREAL st
 f is_simple_func_in S & M.B=0 & f is nonnegative holds
  integral'(M,f|B) = 0
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let B be Element of S;
   let f be PartFunc of X,ExtREAL;
   assume that
A1: f is_simple_func_in S and
A2: M.B=0 and
A3: f is nonnegative;
   set A = dom f;
   set g = f|(A/\B);
A4:g is_simple_func_in S
   proof
A5:f is finite by A1,MESFUNC2:def 5;
     for x be Element of X st x in dom g holds |. g.x .| < +infty
    proof
     let x be Element of X;
     assume
A6:  x in dom g;
     then x in dom f /\ (A/\B) by FUNCT_1:68;
then A7: x in dom f by XBOOLE_0:def 3;
      g.x = f.x by A6,FUNCT_1:70;
     hence thesis by A5,A7,MESFUNC2:def 1;
    end;
then A8: g is finite by MESFUNC2:def 1;
     ex G be Finite_Sep_Sequence of S st
     (dom g = union rng G &
      for n be Nat, x,y be Element of X st
        n in dom G & x in G.n & y in G.n holds g.x = g.y)
    proof
     consider F be Finite_Sep_Sequence of S such that
A9:  dom f = union rng F &
      for n be Nat, x,y be Element of X st
       n in dom F & x in F.n & y in F.n holds f.x = f.y by A1,MESFUNC2:def 5;
     deffunc G(Nat) = F.$1 /\ (A/\ B);
     reconsider A as Element of S by A9,MESFUNC2:34;
     reconsider A2 = A/\B as Element of S;
     consider G be FinSequence such that
A10:  len G = len F &
for n be Nat st n in Seg len F holds G.n = G(n)
from FINSEQ_1:sch 2;
A11: dom G = Seg len F by A10,FINSEQ_1:def 3;
then A12: dom G = dom F by FINSEQ_1:def 3;
      dom F = Seg len F by FINSEQ_1:def 3;
     then for i be Nat st i in dom F holds G.i = F.i /\ A2 by A10;
     then reconsider G as Finite_Sep_Sequence of S by A12,Th41;
     take G;
      for i be Nat st i in dom G holds G.i = A2 /\ F.i by A10,A11;
then A13: union rng G = A2 /\ dom f by A9,A12,MESFUNC3:6
                .= dom g by FUNCT_1:68;
      for i be Nat, x,y be Element of X st
      i in dom G & x in G.i & y in G.i holds g.x = g.y
     proof
      let i be Nat;
      let x,y be Element of X;
      assume
A14:   i in dom G & x in G.i & y in G.i;
      then G.i = F.i /\ A2 by A10,A11;
then A15:  x in F.i & y in F.i by A14,XBOOLE_0:def 3;
       G.i in rng G by A14,FUNCT_1:12;
      then x in dom g & y in dom g by A13,A14,TARSKI:def 4;
      then g.x = f.x & g.y = f.y by FUNCT_1:70;
      hence thesis by A9,A12,A14,A15;
     end;
     hence thesis by A13;
    end;
    hence thesis by A8,MESFUNC2:def 5;
   end;
A16:for x be set st x in dom g holds 0 <= g.x
   proof
    let x be set;
    assume
A17: x in dom g;
     0 <= f.x by A3,SUPINF_2:70;
    hence thesis by A17,FUNCT_1:70;
   end;
    dom(f|(A/\B)) = A/\(A/\B) by FUNCT_1:68;
then A18:dom(f|(A/\B)) = (A/\A)/\B by XBOOLE_1:16;
then A19:dom(f|(A/\B)) = dom(f|B) by FUNCT_1:68;
    for x be set st x in dom(f|(A/\B)) holds (f|(A/\B)).x = (f|B).x
   proof
    let x be set;
    assume
 A20:x in dom(f|(A/\B));
    then (f|(A/\B)).x = f.x by FUNCT_1:70;
    hence thesis by A19,A20,FUNCT_1:70;
   end;
then A21:f|(A/\B) = f|B by A19,FUNCT_1:9;
    now per cases;
    suppose dom g = {};
     hence thesis by A19,Def14;
    end;
    suppose
A22:  dom g <> {};
     then consider F be Finite_Sep_Sequence of S,
              a,x be FinSequence of ExtREAL such that
A23:   F,a are_Re-presentation_of g &
      a.1 =0 &
      (for n be Nat st 2 <= n & n in dom a holds
         0 < a.n & a.n < +infty ) &
      dom x = dom F &
      (for n be Nat st n in dom x holds x.n=a.n*(M*F).n) &
      integral(X,S,M,g)=Sum(x) by A4,A16,MESFUNC3:def 2;
A24:  for n be Nat st n in dom F holds M.(F.n) = 0
     proof
      let n be Nat;
      assume
A25:    n in dom F;
       reconsider BB=B as measure_zero of M by A2,MEASURE1:def 13;
   A26:union rng F = dom g by A23,MESFUNC3:def 1;
   A27:F.n c= union rng F
       proof
         for v be set st v in F.n holds v in union rng F
        proof
         let v be set;
         assume
  A28:    v in F.n;
          F.n in rng F by A25,FUNCT_1:12;
         hence v in union rng F by A28,TARSKI:def 4;
        end;
        hence thesis by TARSKI:def 3;
       end;
        F.n in rng F & rng F c= S by A25,FUNCT_1:12;
       then reconsider FF=F.n as Element of S;
        dom g c= B by A18,XBOOLE_1:17;
       then FF c= BB by A26,A27,XBOOLE_1:1;
       then F.n is measure_zero of M by MEASURE1:69;
      hence M.(F.n) =0 by MEASURE1:def 13;
     end;
A29:  for n be Nat st n in dom x holds x.n = 0
     proof
      let n be Nat;
      assume
A30:    n in dom x;
      then M.(F.n) = 0 by A23,A24;
      then (M*F).n = 0 by A23,A30,FUNCT_1:23;
      then a.n*(M*F).n = 0 by EXTREAL1:22;
      hence thesis by A23,A30;
     end;
      Sum(x) = 0
     proof
      consider sumx be Function of NAT,ExtREAL such that
A31:    Sum(x) = sumx.(len x) & sumx.0 = 0 &
       for i be Element of NAT st i < len x holds sumx.(i+1)=sumx.i + x.(i+1)
        by CONVFUN1:def 5;
       now assume x <> {};
       defpred P[Nat] means
        $1 <= len x implies sumx.$1 = 0;
A32:    P[0] by A31;
A33:    for k be Nat st P[k] holds P[k+1]
       proof
        let k be Nat;
        assume
A34:      P[k];
        assume
A35:      k+1 <= len x;
         reconsider k as Element of NAT by ORDINAL1:def 13;
         k < len x by A35,NAT_1:13;
then A36:     sumx.(k+1) = sumx.k + x.(k+1) by A31;
         1 <= k+1 by NAT_1:11;
        then k+1 in Seg(len x) by A35;
        then k+1 in dom x by FINSEQ_1:def 3;
        then x.(k+1) = 0 by A29;
        hence thesis by A34,A35,A36,NAT_1:13,SUPINF_2:18;
       end;
        for i be Nat holds P[i] from NAT_1:sch 2(A32,A33);
       hence thesis by A31;
      end;
      hence thesis by A31,FINSEQ_1:25;
     end;
     hence thesis by A21,A22,A23,Def14;
    end;
   end;
   hence thesis;
end;

theorem Th80:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    g be PartFunc of X,ExtREAL,
    F be Functional_Sequence of X,ExtREAL,
    L be ExtREAL_sequence st
g is_simple_func_in S &
(for x be set st x in dom g holds 0 < g.x) &
(for n be Nat holds F.n is_simple_func_in S) &
(for n be Nat holds dom(F.n) = dom g) &
(for n be Nat holds F.n is nonnegative) &
(for n,m be Nat st n <=m holds
   for x be Element of X st x in dom g holds (F.n).x <= (F.m).x ) &
(for x be Element of X st x in dom g holds
   (F#x) is convergent & g.x <= lim(F#x) ) &
(for n be Nat holds L.n = integral'(M,F.n))
holds
    L is convergent &
    integral'(M,g) <= lim(L)
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let g be PartFunc of X,ExtREAL;
   let F be Functional_Sequence of X,ExtREAL;
   let L be ExtREAL_sequence;
   assume that
A1:g is_simple_func_in S and
A2:for x be set st x in dom g holds 0 < g.x and
A3:for n be Nat holds F.n is_simple_func_in S and
A4:for n be Nat holds dom(F.n) = dom g and
A5:for n be Nat holds F.n is nonnegative and
A6:for n,m be Nat st n <= m holds
    for x be Element of X st x in dom g holds (F.n).x <= (F.m).x and
A7:for x be Element of X st x in dom g holds
    (F#x) is convergent & g.x <= lim(F#x) and
A8:for n be Nat holds L.n = integral'(M,F.n);
   per cases;
   suppose
A9: dom g = {};
     now let n be Nat;
      dom(F.n) = {} & F.n is_simple_func_in S by A3,A4,A9;
     then integral'(M,F.n) = 0 by Def14;
     hence L.n = R_EAL 0 by A8;
    end;
then A10: L is convergent_to_finite_number & lim L = R_EAL 0 by Th58;
    hence L is convergent by Def11;
    thus integral'(M,g) <= lim L by A9,A10,Def14;
   end;
   suppose
A11: dom g <> {};
     for v be set st v in dom g holds 0 <= g.v by A2;
    then consider G be Finite_Sep_Sequence of S,
             a be FinSequence of ExtREAL such that
A12:  G,a are_Re-presentation_of g &
     a.1 = 0 &
     for n be Nat st 2 <= n & n in dom a holds
      0 < a.n & a.n < +infty by A1,MESFUNC3:14;
A13: len a <> 0
    proof
     assume len a = 0;
then A14:  dom a = Seg 0 by FINSEQ_1:def 3;
A15:  rng G = {}
     proof
      assume rng G <> {};
      then consider y be set such that
A16:    y in rng G by XBOOLE_0:def 1;
       ex x be set st x in dom G & y=G.x by A16,FUNCT_1:def 5;
      hence contradiction by A12,A14,FINSEQ_1:4,MESFUNC3:def 1;
     end;
       union rng G <> {} by A11,A12,MESFUNC3:def 1;
      then consider x be set such that
A17:   x in union rng G by XBOOLE_0:def 1;
       ex Y be set st x in Y & Y in rng G by A17,TARSKI:def 4;
      hence contradiction by A15;
    end;
    defpred PP1[Nat,set] means $2 = a.$1;
A18: for k be Nat st k in Seg len a
     ex x be Element of REAL st PP1[k,x]
    proof
     let k be Nat;
     assume k in Seg len a;
then A19:  1 <= k & k <= len a & k in dom a by FINSEQ_1:3,def 3;
     per cases;
     suppose A20:k = 1;
      take x = 0;
      thus x = a.k by A12,A20;
     end;
     suppose k <> 1;
      then k > 1 by A19,REAL_1:def 5;
      then k >= 1+1 by NAT_1:13;
      then 0 < a.k & a.k < +infty by A12,A19;
      then reconsider x = a.k as Element of REAL by XXREAL_0:14;
      take x;
      thus x = a.k;
     end;
    end;
    consider a1 be FinSequence of REAL such that
A21: dom a1 = Seg len a &
for k be Nat st k in Seg len a holds PP1[k,a1.k]
from FINSEQ_1:sch 5(A18);
A22:2 <= len a
    proof
     assume not (2<=len a);
     then len a = 1 by A13,NAT_1:23;
     then dom a = {1} by FINSEQ_1:4,def 3;
then A23:  dom G = {1} by A12,MESFUNC3:def 1;
then A24:  1 in dom G by TARSKI:def 1;
A25:  dom g = union rng G by A12,MESFUNC3:def 1
          .=union{G.1} by A23,FUNCT_1:14
          .= G.1 by ZFMISC_1:31;
     then consider x be set such that
A26:   x in G.1 by A11,XBOOLE_0:def 1;
      g.x=0 by A12,A24,A26,MESFUNC3:def 1;
     hence contradiction by A2,A25,A26;
    end;
then A27: 2 in dom a1 by A21;
    then a1.2 = a.2 & 2 in dom a by A21,FINSEQ_1:def 3;
then A28: a1.2 <> a.1 by A12;
     1 <= len a by A22,XXREAL_0:2;
    then 1 in Seg len a;
    then a.1=a1.1 by A21;
    then not a1.2 in {a1.1} & a1.2 in rng a1 by A27,A28,FUNCT_1:12,TARSKI:def 1
;
    then reconsider RINGA = (rng a1)\{a1.1} as finite non empty real-membered
set
      by XBOOLE_0:def 4;
    set alpha = R_EAL(min RINGA);
    set beta = R_EAL(max RINGA);
    reconsider beta1=max RINGA as Real by XREAL_0:def 1;
     min RINGA in RINGA by PRE_CIRC:def 4;
then A29: min RINGA in rng a1 & not min RINGA in {a1.1} by XBOOLE_0:def 4;
    then consider i be set such that
A30:  i in dom a1 & min RINGA = a1.i by FUNCT_1:def 5;
    reconsider i as Element of NAT by A30;
A31: i <> 1 by A29,A30,TARSKI:def 1;
     i in Seg len a1 by A30,FINSEQ_1:def 3;
    then 1 <= i by FINSEQ_1:3;
    then 1 < i by A31,REAL_1:def 5;
then A32: 1+1 <= i by NAT_1:13;
A33:g is finite by A1,MESFUNC2:def 5;
A34: for n be Nat holds dom(g - F.n) = dom g
    proof
     let n be Nat;
A35: dom(F.n) = dom g by A4;
      g is without+infty & g is without-infty by A1,Th20;
     then not +infty in rng g & not -infty in rng g by Def3,Def4;
then A36: g"{+infty} = {} & g"{-infty} = {} by FUNCT_1:142;
      dom(g - F.n) = (dom(F.n) /\ dom g)\
              ( (F.n)"{+infty} /\ g"{+infty} \/
               (F.n)"{-infty} /\ g"{-infty} ) by MESFUNC1:def 4;
     hence dom(g - F.n) = dom g by A35,A36;
    end;
A37: for e be R_eal st R_EAL 0 < e & e < alpha holds
     ex H be SetSequence of X, MF be ExtREAL_sequence st
      (for n be Nat holds H.n = less_dom(g-(F.n),e)) &
      (for n,m be Nat st n <= m holds H.n c= H.m) &
      (for n be Nat holds H.n c= dom g) &
      (for n be Nat holds MF.n = M.(H.n)) &
      M.(dom g) = sup rng MF &
      (for n be Nat holds H.n in S)
    proof
     let e be R_eal;
     assume
A38:  R_EAL 0 < e & e < alpha;
     deffunc FFH(Nat) = less_dom(g-(F.$1),e);
     consider H be SetSequence of X such that
A39: for n be Element of NAT holds H.n = FFH(n)
     from KURATO_2:sch 1;
A40: now let n be Nat;  n in NAT by ORDINAL1:def 13;
       hence H.n = FFH(n) by A39;
     end;
A41: for n,m be Nat st n <= m holds H.n c= H.m
     proof
      let n,m be Nat;
      assume
A42:   n <= m;
       now let x be set;
       assume x in H.n;
then A43:   x in less_dom(g-F.n,e) by A40;
then A44:   x in dom(g-F.n) &
       (ex y be R_eal st y = (g-F.n).x & y < e) by MESFUNC1:def 12;
then A45:  (g-F.n).x = g.x - (F.n).x by MESFUNC1:def 4;
A46:  ex y1 be R_eal st y1 = (g-F.n).x & y1 < e by A43,MESFUNC1:def 12;
A47:   dom(g-F.n) = dom g & dom(g-F.m) = dom g by A34;
then A48:   (g-F.m).x = g.x - (F.m).x by A44,MESFUNC1:def 4;
A49:   (F.n).x <= (F.m).x by A6,A42,A44,A47;
        |. g.x .| < +infty by A33,A44,A47,MESFUNC2:def 1;
       then g.x <> -infty & g.x <> +infty by EXTREAL2:67;
       then (g-F.m).x <= (g-F.n).x by A45,A48,A49,SUPINF_2:15;
       then (g-F.m).x < e by A46,XXREAL_0:2;
       then x in less_dom((g-F.m),e) by A44,A47,MESFUNC1:def 12;
       hence x in H.m by A40;
      end;
      hence H.n c= H.m by TARSKI:def 3;
     end;
      now let x be set;
      assume x in lim_inf H;
      then x in Union inferior_setsequence H by SETLIM_1:def 4;
      then consider V be set such that
A50:    x in V & V in rng inferior_setsequence H by TARSKI:def 4;
      consider n be set such that
A51:    n in dom inferior_setsequence H & V = (inferior_setsequence H).n
        by A50,FUNCT_1:def 5;
      reconsider n as Element of NAT by A51;
       x in H.(n+0) by A50,A51,SETLIM_1:24;
      then x in less_dom(g-F.n,e) by A40;
      then x in dom(g-F.n) by MESFUNC1:def 12;
      hence x in dom g by A34;
     end;
then A52:  lim_inf H c= dom g by TARSKI:def 3;
      dom g c= lim_inf H
     proof
       now let x be set;
       assume
A53:     x in dom g;
       then reconsider x1=x as Element of X;
A54:    F#x1 is convergent & g.x <= lim(F#x1) by A7,A53;
A55:    now assume
A56:     F#x1 is convergent_to_-infty;
        reconsider E = e as Real by A38,MEASURE5:16;
A57:     -E < -0 by A38,XREAL_1:26;
        then consider N be Nat such that
A58:      for m be Nat st N <= m holds (F#x1).m <= R_EAL(-E) by A56,Def10;
         F.N is nonnegative by A5;
        then (F#x1).N < 0 & 0 <= (F.N).x by A57,A58,SUPINF_2:70;
        hence contradiction by Def13;
       end;
        now per cases by A54,A55,Def11;
        suppose
A59:      F#x1 is convergent_to_finite_number;
          ( ex limFx be real number st lim(F#x1) = limFx &
           (for p be real number st 0<p ex n be Nat st
             for m be Nat st n<=m holds |. (F#x1).m - lim(F#x1).| < p) &
           F#x1 is convergent_to_finite_number ) or
         ( lim(F#x1) = +infty & F#x1 is convergent_to_+infty ) or
         lim(F#x1) = -infty & F#x1 is convergent_to_-infty by A54,Def12;
         then consider limFx be real number such that
A60:      lim(F#x1) = limFx &
          (for p be real number st 0 < p
            ex n be Nat st
             for m be Nat st n <= m holds
               |. (F#x1).m - lim(F#x1) .| < p) by A59,Th56,Th57;
          ex N be Nat st for m be Nat st N <= m holds x1 in less_dom(g-F.m,e)
         proof
          reconsider E = e as Real by A38,MEASURE5:16;
A61:      0 < E/2 by A38,XREAL_1:217;
          set E2 = E/2;
           g.x <= lim(F#x1) by A7,A53;
          then g.x - R_EAL(E/2) <= lim(F#x1)-0. by A61,SUPINF_2:15;
then A62:       g.x - R_EAL(E/2) <= lim(F#x1) by EXTREAL1:24,SUPINF_2:18;
          consider N be Nat such that
A63:       for m be Nat st N <= m holds
            |. (F#x1).m - lim(F#x1) .| < R_EAL(E/2) by A60,A61;
A64:      for n be Nat holds 0 <= lim(F#x1) - (F#x1).n
          proof
           let n be Nat;
            now let e be real number;
            assume 0 < e;
            then consider N0 be Nat such that
A65:         for m be Nat st N0 <= m holds
              |. (F#x1).m - lim(F#x1) .| < R_EAL e by A60;
            reconsider N0,n1=n as Element of NAT by ORDINAL1:def 13;
            set m=max(N0,n1);
             N0 <= m by XXREAL_0:25;
then A66:        |. (F#x1).m - lim(F#x1) .| < R_EAL e by A65;
             (F#x1).m - lim(F#x1) <= |. (F#x1).m - lim(F#x1) .|
              by EXTREAL2:57;
            then (F#x1).m - lim(F#x1) < R_EAL e by A66,XXREAL_0:2;
then A67:        (F#x1).m < R_EAL e + lim(F#x1) by A60,EXTREAL2:29;
             n <= m by XXREAL_0:25;
            then (F.n).x1 <= (F.m).x1 by A6,A53;
            then (F.n).x1 <= (F#x1).m by Def13;
            then (F#x1).n <= (F#x1).m by Def13;
            hence (F#x1).n < lim(F#x1) + R_EAL e by A67,XXREAL_0:2;
           end;
then A68:        (F#x1).n <= lim(F#x1) by Th4;
            -infty <= lim(F#x1) & lim(F#x1) <= +infty by XXREAL_0:4,5;
then A69:        -infty < lim(F#x1) & lim(F#x1) < +infty by A60,XXREAL_0:1;
           then (F#x1).n < +infty by A68,XXREAL_0:2;
           hence 0 <= lim(F#x1) - (F#x1).n by A68,A69,EXTREAL1:40;
          end;
A70:      now let n be Nat;
A71:        0 <= lim(F#x1) - (F#x1).n by A64;
           thus |. (F#x1).n - lim(F#x1) .| = |. lim(F#x1)- (F#x1).n .| by Th1
           .= lim(F#x1) - (F#x1).n by A71,EXTREAL1:def 3;
          end;
A72:       now let m be Nat; assume
A73:        N <= m;
           then |. (F#x1).m - lim(F#x1) .| < R_EAL(E/2) by A63;
           then (F#x1).m <> +infty & (F#x1).m <> -infty by A60,Th3;
then A74:      (F.m).x <> +infty & (F.m).x <> -infty by Def13;
            |. (F#x1).m - lim(F#x1) .| = lim(F#x1) - (F#x1).m by A70;
           then lim(F#x1) - (F#x1).m < R_EAL(E/2) by A63,A73;
           then lim(F#x1) - (F.m).x1 < R_EAL(E/2) by Def13;
           hence lim(F#x1) < (F.m).x + R_EAL(E/2) by A74,EXTREAL2:29;
          end;
           now let m be Nat such that
A75:       N <= m;
A76:       x1 in dom(g - F.m) by A34,A53;
A77:       g.x <= lim(F#x1)+R_EAL(E/2) by A62,EXTREAL2:33;
            lim(F#x1) < (F.m).x + R_EAL(E/2) by A72,A75;
           then lim(F#x1) + R_EAL(E/2) < (F.m).x + R_EAL(E/2)+ R_EAL(E/2)
            by Th5;
           then g.x < (F.m).x1 +R_EAL(E/2) +R_EAL(E/2) by A77,XXREAL_0:2;
           then g.x < (F.m).x1 +(R_EAL(E/2) +R_EAL(E/2) ) by EXTREAL1:8;
           then g.x < (F.m).x1 +R_EAL(E/2+E/2) by Th7;
           then g.x - (F.m).x1 < e by EXTREAL2:30;
           then (g-F.m).x1 < e by A76,MESFUNC1:def 4;
           hence x1 in less_dom(g-F.m,e) by A76,MESFUNC1:def 12;
          end;
          hence thesis;
         end;
         then consider N be Nat such that
A78:      for m be Nat st N <= m holds x in less_dom(g-F.m,e);
         reconsider N as Element of NAT by ORDINAL1:def 13;
          now let k be Element of NAT;
           N+k >= N by NAT_1:11;
          then x in less_dom(g-F.(N+k),e) by A78;
          hence x in H.(N+k) by A40;
         end;
then A79:      x in (inferior_setsequence H).N by SETLIM_1:24;
          dom inferior_setsequence H = NAT by FUNCT_2:def 1;
         hence ex N be Nat st
               N in dom inferior_setsequence H &
               x in (inferior_setsequence H).N by A79;
        end;
        suppose
A80:     F#x1 is convergent_to_+infty;
          ex N be Nat st for m be Nat st N <=m holds g.x1 - (F.m).x1 < e
         proof
           e in REAL by A38,MEASURE5:16;
then A81:      e <> +infty & e <> -infty by XXREAL_0:9,12;
          per cases;
          suppose
A82:        g.x1-e <= 0;
           consider N be Nat such that
A83:        for m be Nat st N <= m holds R_EAL 1 <= (F#x1).m
               by A80,Def9;
            now let m be Nat;
            assume N <=m;
            then g.x1-e < (F#x1).m by A82,A83;
            then g.x1 < (F#x1).m+e  by A81,EXTREAL2:29;
            then g.x1 - (F#x1).m < e by A81,EXTREAL2:30;
            hence g.x1 - (F.m).x1 < e by Def13;
           end;
           hence ex N be Nat st for m be Nat st N <=m holds g.x1-(F.m).x1 < e;
          end;
          suppose
A84:        0 < g.x1-e;
            |. g.x .| < +infty by A33,A53,MESFUNC2:def 1;
           then g.x <> -infty & g.x <> +infty by EXTREAL2:67;
           then reconsider gx1=g.x as Real by XXREAL_0:14;
           reconsider e1=e as Real by A38,MEASURE5:16;
            g.x-e = gx1-e1 by SUPINF_2:5;
           then reconsider ee=g.x1-e as Real;
A85:       0 < ee+1 & ee < ee+1 by A84,XREAL_1:31;
A86:       R_EAL ee < R_EAL(ee+1) by XREAL_1:31;
           consider N be Nat such that
A87:        for m be Nat st N <= m holds
             R_EAL(ee+1) <= (F#x1).m by A80,A85,Def9;
            now let m be Nat;
            assume N <=m;
            then R_EAL(ee+1) <= (F#x1).m by A87;
            then R_EAL ee < (F#x1).m by A86,XXREAL_0:2;
            then g.x1 < (F#x1).m+e by A81,EXTREAL2:29;
            then g.x1 - (F#x1).m < e by A81,EXTREAL2:30;
            hence g.x1 - (F.m).x1 < e by Def13;
           end;
           hence ex N be Nat st for m be Nat st N <=m holds g.x1-(F.m).x1 < e;
          end;
         end;
         then consider N be Nat such that
A88:       for m be Nat st N <=m holds g.x1 - (F.m).x1 < e;
          reconsider N as Element of NAT by ORDINAL1:def 13;
A89:     now let m be Nat;
          assume N <= m;
then A90:      g.x1 - (F.m).x1 < e  by A88;
           x1 in dom(g - F.m) by A34,A53;
          then x1 in dom(g-F.m) & (g-F.m).x1 < e by A90,MESFUNC1:def 4;
          hence x1 in less_dom(g-F.m,e) by MESFUNC1:def 12;
         end;
          now let k be Element of NAT;
           N+k >= N by NAT_1:11;
          then x in less_dom((g-F.(N+k)),e) by A89;
          hence x in H.(N+k) by A40;
         end;
then A91:      x in (inferior_setsequence H).N by SETLIM_1:24;
          dom inferior_setsequence H = NAT by FUNCT_2:def 1;
         hence ex N be Nat st N in dom inferior_setsequence H &
                             x in (inferior_setsequence H).N by A91;
        end;
       end;
       then consider N be Nat such that
A92:     N in dom inferior_setsequence H & x in (inferior_setsequence H).N;
        (inferior_setsequence H).N in rng inferior_setsequence H
          by A92,FUNCT_1:12;
       then x in Union inferior_setsequence H by A92,TARSKI:def 4;
       hence x in lim_inf H by SETLIM_1:def 4;
      end;
      hence dom g c= lim_inf H by TARSKI:def 3;
     end;
then A93: lim_inf H = dom g by A52,XBOOLE_0:def 10;
A94: for n be Nat holds H.n c= dom g
     proof
      let n be Nat;
       now let x be set;
       assume x in H.n;
       then x in less_dom(g-(F.n),e) by A40;
       then x in dom(g-F.n) by MESFUNC1:def 12;
       hence x in dom g by A34;
      end;
      hence thesis by TARSKI:def 3;
     end;
      for n,m be Element of NAT st n <= m holds H.n c= H.m by A41;
     then A95: H is non-decreasing by PROB_1:def 7;
A96: Union H c= dom g
     proof
      let x be set;
      assume x in Union H;
      then consider n be Element of NAT such that
A97:    x in H.n by PROB_1:25;
       H.n c= dom g by A94;
      hence x in dom g by A97;
     end;
A98: lim_inf H c= lim_sup H by KURATO_2:9;
     deffunc U(Nat) = M.(H.$1);
     consider MF be ExtREAL_sequence such that
A99: for n be Element of NAT holds MF.n = U(n) from FUNCT_2:sch 4;
A100: now let n be Nat;  n in NAT by ORDINAL1:def 13;
       hence MF.n = U(n) by A99;
     end;
A101:  M.(dom g) = sup rng MF & (for n be Element of NAT holds H.n in S)
     proof
A102:    now let x be set;
         assume x in NAT;
         then reconsider n=x as Element of NAT;
A103:     F.n is_simple_func_in S by A3;
         then consider GF being Finite_Sep_Sequence of S such that
A104:     dom(F.n) = union rng GF &
         for m being Nat,x,y being Element of X st
         m in dom GF & x in GF.m & y in GF.m holds (F.n).x = (F.n).y
         by MESFUNC2:def 5;
         reconsider DGH=union rng GF as Element of S by MESFUNC2:34;
A105:     g is finite & F.n is finite by A1,A103,MESFUNC2:def 5;
A106:    g is_measurable_on DGH by A1,MESFUNC2:37;
         reconsider E = e as Real by A38,MEASURE5:16;
          F.n is_measurable_on DGH by A103,MESFUNC2:37;
         then g-F.n is_measurable_on DGH by A104,A105,A106,MESFUNC2:13;
         then DGH /\ less_dom(g-F.n,R_EAL E) is_measurable_on S
         by MESFUNC1:def 17;
then A107:      DGH /\ less_dom(g-F.n,R_EAL E) in S by MESFUNC1:def 11;
A108:      less_dom(g-F.n,R_EAL E) c= dom(g-F.n)
         proof
           let x be set;
           assume x in less_dom(g-F.n,R_EAL E);
           hence x in dom(g-F.n) by MESFUNC1:def 12;
         end;
          dom(F.n) = dom g by A4;
         then DGH /\ less_dom(g-F.n,R_EAL E) = dom(g-F.n) /\ less_dom(g-F.n,
R_EAL E)
         by A34,A104;
         then DGH /\ less_dom(g-F.n,R_EAL E) = less_dom(g-F.n,R_EAL E)
         by A108,XBOOLE_1:28;
         hence H.x in S by A40,A107;
       end;
        dom H = NAT by FUNCT_2:def 1;
       then reconsider HH= H as Function of NAT,S by A102,FUNCT_2:5;
A109:  for n being Element of NAT holds HH.n c= HH.(n+1) by A41,NAT_1:11;
        lim_sup H = Union H by A95,SETLIM_1:65;
then A110:    M.(union rng H) = M.(dom g) by A93,A96,A98,XBOOLE_0:def 10;
A111:    dom MF = NAT & dom H = NAT by FUNCT_2:def 1;
        rng HH c= S by RELSET_1:12;
then A112:    rng H c= dom M by FUNCT_2:def 1;
A113:    for x be set holds x in dom MF iff x in dom H & H.x in dom M
       proof
         let x be set;
          now assume
A114:        x in dom MF;
           then H.x in rng H by A111,FUNCT_1:12;
           hence x in dom H & H.x in dom M by A111,A112,A114;
         end;
         hence thesis by A111;
       end;
        for x be set st x in dom MF holds MF.x = M.(H.x) by A99;
       then M*H =MF by A113,FUNCT_1:20;
       hence thesis by A102,A109,A110,MEASURE2:27;
     end;
      now let n be Nat;  n in NAT by ORDINAL1:def 13;
       hence H.n in S by A101;
     end;
     hence thesis by A40,A41,A94,A100,A101;
   end;
A115: i in dom a & a.i = R_EAL(a1.i) by A21,A30,FINSEQ_1:def 3;
then A116: 0 < alpha by A12,A30,A32;
A117: for x be set st x in dom g holds alpha <= g.x & g.x <= beta
    proof
     let x be set;
     assume
A118:  x in dom g;
     then x in union rng G by A12,MESFUNC3:def 1;
     then consider Y be set such that
A119:  x in Y & Y in rng G by TARSKI:def 4;
     consider k be set such that
A120:  k in dom G & Y = G.k by A119,FUNCT_1:def 5;
     reconsider k as Element of NAT by A120;
      k in dom a by A12,A120,MESFUNC3:def 1;
then A121: k in Seg len a by FINSEQ_1:def 3;
then A122:  a1.k in rng a1 by A21,FUNCT_1:12;
      now assume A123:a1.k=a1.1;
       1 <= len a by A22,XXREAL_0:2;
then A124:   1 in dom a1 by A21;
       a.k=a1.k by A21,A121;
then A125:   a.k=a.1 by A21,A123,A124;
       g.x = a.k by A12,A119,A120,MESFUNC3:def 1;
      hence contradiction by A2,A12,A118,A125;
     end;
     then not a1.k in {a1.1} by TARSKI:def 1;
then A126:  a1.k in RINGA by A122,XBOOLE_0:def 4;
      g.x = a.k by A12,A119,A120,MESFUNC3:def 1 .= a1.k by A21,A121;
     hence thesis by A126,PRE_CIRC:def 1,def 4;
    end;
    per cases;
    suppose
A127:  M.(dom g) <> +infty;
     reconsider DG=dom g as Element of S by A1,Th43;
A128: M.{} = 0 by MEASURE1:def 11;
      {} is_measurable M by MEASURE1:66;
then A129: {} in S by MEASURE1:def 12;
      {} c= dom g & dom g is Element of S by A1,Th43,XBOOLE_1:2;
then A130:M.(dom g) <> -infty & M.(dom g) <> +infty by A127,A128,A129,
MEASURE1:62;
      for x be set st x in dom g holds 0 <= g.x by A2;
then A131: g is nonnegative by SUPINF_2:71;
A132: 0 < beta
     proof
      consider x be set such that
A133:     x in dom g by A11,XBOOLE_0:def 1;
       alpha <= g.x & g.x <= beta by A117,A133;
      hence 0 < beta by A12,A30,A32,A115;
     end;
A134: integral'(M,g) <= beta*(M.DG)
     proof
      consider GP be PartFunc of X,ExtREAL such that
A135:  GP is_simple_func_in S & dom GP = DG &
       for x be set st x in DG holds GP.x = beta by Th47;
       for x be set st x in dom GP holds 0 <= GP.x by A132,A135;
then A136:GP is nonnegative by SUPINF_2:71;
A137:for x be set st x in dom(GP-g) holds g.x <= GP.x
      proof
       let x be set;
       assume x in dom(GP-g);
       then x in (dom GP /\ dom g)\
            (GP"{+infty}/\g"{+infty} \/ GP"{-infty}/\g"{-infty})
               by MESFUNC1:def 4;
then A138:    x in dom GP /\ dom g by XBOOLE_0:def 4;
       then GP.x = beta by A135;
       hence g.x <= GP.x by A117,A135,A138;
      end;
   then A139:integral'(M,g|dom(GP-g)) <= integral'(M,GP|dom(GP-g))
         by A1,A131,A135,A136,Th76;
       dom(GP-g) = dom GP /\ dom g by A1,A131,A135,A136,A137,Th75;
      then g|dom(GP-g) = g & GP|dom(GP-g) = GP by A135,GRFUNC_1:64;
      hence thesis by A132,A135,A139,Th77;
     end;
     reconsider MG=M.(dom g) as Real by A130,XXREAL_0:14;
      beta*(M.DG)=beta1*MG by EXTREAL1:13;
then A140: integral'(M,g) <> +infty & integral'(M,g) <> -infty
      by A1,A131,A134,Th74,XXREAL_0:9;
A141:  for e be R_eal st 0 < e & e < alpha holds
       ex N0 be Nat st
        for n be Nat st N0<= n holds
         integral'(M,g) - e*(beta + M.(dom g)) < integral'(M,F.n)
     proof
      let e be R_eal;
      assume
A142:   0 < e & e < alpha;
then A143: e <> -infty & e <> +infty by XXREAL_0:4;

A144: for x being set st x in dom g
       holds g.x <> -infty & g.x <> +infty by A33,EXTREAL2:67,MESFUNC2:def 1;
      consider H be SetSequence of X, MF be ExtREAL_sequence such that
A145:   for n be Nat holds H.n = less_dom(g-F.n,e) and
A146:   for n,m be Nat st n <= m holds H.n c= H.m and
A147:  for n be Nat holds H.n c= dom g and
A148:   for n be Nat holds MF.n = M.(H.n) and
A149:   M.(dom g) = sup rng MF and
A150:   for n be Nat holds H.n in S by A37,A142;
       sup rng MF is Real by A130,A149,XXREAL_0:14;
      then consider y being ext-real number such that
A151:    y in rng MF & sup rng MF - e < y by A142,MEASURE6:33;
      consider N0 be set such that
A152:    N0 in dom MF & y=MF.N0 by A151,FUNCT_1:def 5;
      reconsider N0 as Element of NAT by A152;
      reconsider B0=H.N0 as Element of S by A150;
       M.(dom g) -e < M.(H.N0) by A148,A149,A151,A152;
      then M.(dom g) < M.(H.N0) + e by A143,EXTREAL2:29;
then A153:   M.(dom g) - M.(H.N0) < e by A143,EXTREAL2:30;
A154: B0 c= DG by A147;
then A155: M.B0 <= M.DG by MEASURE1:62;
       M.DG < +infty by A127,XXREAL_0:4;
      then M.B0 < +infty by A155,XXREAL_0:2;
then A156:  M.(DG \ B0) = M.DG - M.B0 by A154,MEASURE1:63;
      take N0;
A157: now let n be Nat;
       assume
A158:   N0 <= n;
       reconsider BN=H.n as Element of S by A150;
        H.N0 c= H.n by A146,A158;
       then DG \ BN c= DG \ B0 by XBOOLE_1:34;
       then M.(DG \ BN) <= M.(DG \ B0) by MEASURE1:62;
       hence M.((dom g) \ H.n) <e by A153,A156,XXREAL_0:2;
      end;
       now let n be Nat;
       assume
A159:  N0 <= n;
A160:  H.n c= DG by A147;
       then DG =DG \/ H.n by XBOOLE_1:12;
then A161: DG =(DG \ H.n) \/ H.n by XBOOLE_1:39;
        H.n is Element of S by A150;
       then H.n is_measurable M by MEASURE1:def 12;
then A162: X \ H.n is_measurable M by MEASURE1:66;
        DG is_measurable M by MEASURE1:def 12;
then A163: DG /\ (X \ H.n) is_measurable M by A162,MEASURE1:66;
        DG /\ (X \ H.n) =(DG /\ X) \ H.n  by XBOOLE_1:49;
       then DG /\ (X \ H.n) = DG \ H.n by XBOOLE_1:28;
       then reconsider A=DG \ H.n as Element of S by A163,MEASURE1:def 12;
       reconsider B=H.n as Element of S by A150;
A164: dom(F.n) = dom g by A4;
then A165:    dom(F.n) = (A \/ B) /\ dom(F.n) by A161;
        for x be set st x in dom(F.n) holds (F.n).x=(F.n).x;
then A166:  F.n = (F.n)|(A \/ B) by A165,FUNCT_1:68;
A167: F.n is nonnegative by A5;
then A168: (F.n)|A is nonnegative & (F.n)|B is nonnegative by Th21;
A169: A misses B by XBOOLE_1:79;
A170: F.n is_simple_func_in S by A3;
then A171:  integral'(M,F.n) = integral'(M,(F.n) |A) + integral'(M,(F.n)|B)
               by A166,A167,A169,Th73;
A172:(F.n)|A is_simple_func_in S & (F.n)|B is_simple_func_in S
         by A170,Th40;
       then 0 <= integral'(M,(F.n)|A) & 0 <= integral'(M,(F.n)|B)
         by A168,Th74;
then A173:  integral'(M,(F.n)|B) <= integral'(M,F.n) by A171,SUPINF_2:20;
        dom((F.n)|B) = dom(F.n) /\ B by FUNCT_1:68;
then A174:dom((F.n)|B) = B by A160,A164,XBOOLE_1:28;
       consider EP be PartFunc of X,ExtREAL such that
A175:   EP is_simple_func_in S & dom EP= B &
        for x be set st x in B holds EP.x = e by A143,Th47;
A176:  integral'(M,EP) = e * M.B by A142,A175,Th77;
A177:    M.(dom g) < +infty by A127,XXREAL_0:4;
A178:    {} c= B by XBOOLE_1:2;
then A179:    0 <= M.B by A128,A129,MEASURE1:62;
A180:    M.B <= M.DG by A160,MEASURE1:62;
       then M.B < +infty by A177,XXREAL_0:2;
       then e * M.B < e * +infty by A142,A143,EXTREAL1:44;
then A181: integral'(M,EP) <> +infty & integral'(M,EP) <> -infty
         by A142,A176,A179,EXTREAL2:45,XXREAL_0:4;
A182: g|B is_simple_func_in S & g|A is_simple_func_in S by A1,Th40;
A183: A c= dom g by A161,XBOOLE_1:7;
then A184: dom(g|A) = A & dom(g|B) = B by A160,RELAT_1:91;
A185: g|A is nonnegative & g|B is nonnegative by A131,Th21;
then A186: integral'(M,g|A) <> -infty by A182,Th74;
A187: (F.n)|B+EP is_simple_func_in S by A172,A175,Th44;
        for x be set st x in dom EP holds 0 <= EP.x by A142,A175;
then A188: EP is nonnegative by SUPINF_2:71;
then A189: dom((F.n)|B+EP)
          = dom((F.n)|B) /\ dom EP by A168,A172,A175,Th71
         .= B by A174,A175;
A190: (F.n)|B+EP is nonnegative by A168,A188,Th25;
A191:    for x be set st
        x in dom( ((F.n)|B+EP) - g|B ) holds (g|B).x <= ((F.n)|B+EP).x
       proof
        let x be set;
        assume x in dom( ((F.n)|B+EP) - g|B );
        then x in (dom ((F.n)|B+EP) /\ dom(g|B)) \
          ( ((F.n)|B+EP)"{+infty} /\ (g|B)"{+infty}
             \/((F.n)|B+EP)"{-infty} /\ (g|B)"{-infty} )
               by MESFUNC1:def 4;
then A192:     x in dom((F.n)|B+EP) /\ dom(g|B) by XBOOLE_0:def 4;
then A193:     x in dom((F.n)|B+EP) by XBOOLE_0:def 3;
        then ((F.n)|B+EP).x =((F.n)|B).x + EP.x by MESFUNC1:def 3;
        then ((F.n)|B+EP).x =(F.n).x + EP.x by A174,A189,A193,FUNCT_1:68;
then A194:     ((F.n)|B+EP).x =(F.n).x + e by A175,A189,A193;
A195:     (g|B).x = g.x by A184,A189,A192,FUNCT_1:68;
        set f=g-(F.n);
A196:     dom f = dom g by A34;
         x in less_dom(g-F.n,e)  by A145,A189,A193;
then A197:     x in dom f & ex y being R_eal st y=f.x & y < e by
MESFUNC1:def 12;
then A198:    g.x - (F.n).x <= e by MESFUNC1:def 4;
         g.x <>-infty & g.x <>+infty by A144,A196,A197;
        hence thesis by A143,A194,A195,A198,EXTREAL2:33;
       end;
then A199:    dom( ((F.n)|B+EP) - g|B ) = dom((F.n)|B+EP) /\ dom(g|B)
          by A182,A185,A187,A190,Th75;
        dom((F.n)|B+EP) = dom((F.n)|B) /\ dom EP
          by A168,A172,A175,A188,Th71;
       then (F.n)|B+EP = ((F.n)|B+EP)|dom( ((F.n)|B+EP) - g|B ) &
       g|B = (g|B)|dom( ((F.n)|B+EP) - g|B )
         by A174,A175,A184,A199,GRFUNC_1:64;
then A200:  integral'(M,g|B) <= integral'(M,(F.n)|B+EP)
          by A182,A185,A187,A190,A191,Th76;
        integral'(M,(F.n)|B+EP) = integral'(M,((F.n)|B)|B) + integral'(M,EP|B)
          by A168,A172,A175,A188,A189,Th71
       .= integral'(M,((F.n)|B)|B) + integral'(M,EP) by A175,GRFUNC_1:64
       .= integral'(M,(F.n)|B) + integral'(M,EP) by FUNCT_1:82;
then A201:  integral'(M,g|B) - integral'(M,EP) <= integral'(M,(F.n)|B)
          by A181,A200,EXTREAL2:34;
A202:    dom g = (A \/ B) /\ dom g by A161;
        for x be set st x in dom g holds g.x=g.x;
       then g=g|(A\/B) by A202,FUNCT_1:68;
then A203:  integral'(M,g) = integral'(M,g|A) +integral'(M,g|B) by A1,A131,A169
,Th73
;
       consider GP be PartFunc of X,ExtREAL such that
A204:   GP is_simple_func_in S & dom GP = A &
        for x be set st x in A holds GP.x = beta by Th47;
        for x be set st x in dom GP holds 0 <= GP.x by A132,A204;
then A205: GP is nonnegative by SUPINF_2:71;
A206:    for x be set st x in dom(GP-(g|A)) holds (g|A).x <= GP.x
       proof
        let x be set;
        assume x in dom(GP-(g|A));
        then x in (dom GP /\ dom(g|A))
          \((GP"{+infty}/\(g|A)"{+infty}) \/ (GP"{-infty}/\(g|A)"{-infty}))
           by MESFUNC1:def 4;
then A207:     x in (dom GP /\ dom(g|A)) by XBOOLE_0:def 4;
then A208:     x in dom GP by XBOOLE_0:def 3;
A209:     (g|A).x=g.x by A184,A204,A207,FUNCT_1:70;
         x in dom g /\ A by A184,A204,A207,FUNCT_1:68;
        then x in dom g by XBOOLE_0:def 3;
        then g.x <= beta by A117;
        hence (g|A).x <= GP.x by A204,A208,A209;
       end;
       then dom(GP-(g|A)) = dom GP /\dom(g|A) by A182,A185,A204,A205,Th75;
       then (g|A)|dom(GP-(g|A)) = g|A & GP|dom(GP-(g|A)) = GP
          by A184,A204,GRFUNC_1:64;
then A210:  integral'(M,g|A) <= integral'(M,GP) by A182,A185,A204,A205,A206,
Th76;
A211:  integral'(M,GP) = beta*(M.A) by A132,A204,Th77;
A212:  M.A <> +infty by A177,A183,MEASURE1:62;
        M.A <= M.DG by A183,MEASURE1:62;
       then M.A < +infty by A177,XXREAL_0:2;
       then beta*(M.A) < beta*(+infty) by A132,EXTREAL1:44;
then A213:  beta*(M.A) <> +infty by A132,EXTREAL1:def 1;
        {} c= A by XBOOLE_1:2;
then A214:  0 <= M.A by A128,A129,MEASURE1:62;
then A215: beta*(M.A) <> -infty by A132,EXTREAL2:45;
        M.(DG \ H.n ) < e by A157,A159;
then A216:  beta*(M.A) < beta*e by A132,EXTREAL1:44;
       then integral'(M,(g|A)) < beta*e by A210,A211,XXREAL_0:2;
then A217:  integral'(M,(g|A)) <> +infty by XXREAL_0:4;
        e <> +infty & e <> -infty by A142,XXREAL_0:4;
       then reconsider ee=e as Real by XXREAL_0:14;
A218: beta*e = beta1*ee by EXTREAL1:13;
A219:   M.B <>-infty by A128,A129,A178,MEASURE1:62;
        M.(dom g) <> -infty by A128,A129,A178,A180,MEASURE1:62;
then A220:    M.(dom g) is Real by A127,XXREAL_0:14;
       reconsider XSMg = integral'(M,g) as Real by A140,XXREAL_0:14;
       reconsider XSMgA =integral'(M,g|A) as Real by A186,A217,XXREAL_0:14;
       reconsider
          XSMGP =integral'(M,GP) as Real by A211,A213,A215,XXREAL_0:14;
       reconsider MA=M.A as Real by A212,A214,XXREAL_0:14;
       reconsider betae=beta*e as Real by A218;
       reconsider XSMEP = integral'(M,EP) as Real by A181,XXREAL_0:14;
        M.B is Real by A177,A180,A219,XXREAL_0:14;
       then consider MB,MG be Real such that
A221:     MB=M.B & MG=M.(dom g) & MB<=MG by A180,A220;
A222:    XSMEP = e*(M.B) by A142,A175,Th77 .=ee*MB by A221,EXTREAL1:13;
A223:  integral'(M,g) - integral'(M,g|A) = integral'(M,g|B)
            by A186,A203,A217,EXTREAL2:11;
A224:  integral'(M,g) -integral'(M,GP) <= integral'(M,g) - integral'(M,g|A)
            by A186,A210,A211,A213,A215,A217,SUPINF_2:15;
        XSMg - XSMgA =integral'(M,g) - integral'(M,g|A) by SUPINF_2:5
                   .= integral'(M,g|B) by A186,A203,A217,EXTREAL2:11;
       then reconsider XSMgB = integral'(M,g|B) as Real;
A225:    XSMg - betae < XSMg -XSMGP by A211,A216,REAL_1:92;
        integral'(M,g) -integral'(M,GP) = XSMg -XSMGP by SUPINF_2:5;
       then XSMg-betae < XSMgB by A223,A224,A225,XXREAL_0:2;
then A226:   XSMg-betae- ee*MB < XSMgB - XSMEP by A222,REAL_1:92;
A227:   betae=ee*beta1 by EXTREAL1:13;
        ee*MB <= ee*MG by A142,A221,XREAL_1:66;
       then XSMg-betae-ee*MG <= XSMg-betae- ee*MB by XREAL_1:15;
then A228:   XSMg-ee*(beta1+MG) < XSMgB - XSMEP by A226,A227,XXREAL_0:2;
        beta1+MG = beta + M.(dom g) by A221,SUPINF_2:1;
       then ee*(beta1+MG) =e*(beta + M.(dom g)) by EXTREAL1:13;
then A229:   XSMg-ee*(beta1+MG) = integral'(M,g)-e*(beta + M.(dom g)) by
SUPINF_2:5;
        XSMgB - XSMEP =integral'(M,g|B) - integral'(M,EP) by SUPINF_2:5;
       then integral'(M,g) - e*(beta+M.(dom g)) < integral'(M,(F.n)|B)
            by A201,A228,A229,XXREAL_0:2;
       hence integral'(M,g) - e*(beta + M.(dom g)) < integral'(M,F.n)
            by A173,XXREAL_0:2;
      end;
      hence thesis;
     end;
A230:  for e be R_eal st 0 < e & e < alpha holds
      ex N0 be Nat st for n be Nat st N0<= n holds
       integral'(M,g) - e*(beta + M.(dom g)) < L.n
     proof
      let e be R_eal;
      assume 0 < e & e < alpha;
      then consider N0 be Nat such that
A231:     for n be Nat st N0<= n holds
        integral'(M,g) - e*(beta + M.(dom g)) < integral'(M,F.n) by A141;
       now let n be Nat;
       assume N0 <=n;
       then integral'(M,g)-e*(beta + M.(dom g)) < integral'(M,F.n) by A231;
       hence integral'(M,g) - e*(beta + M.(dom g)) < L.n by A8;
      end;
      hence thesis;
     end;
A232: for e1 be R_eal st 0 < e1
      ex e be R_eal st 0 < e & e < alpha & e*(beta + M.(dom g)) <= e1
     proof
      let e1 be R_eal; assume
A233:    0 < e1;
      reconsider ralpha=alpha as Real by XXREAL_0:14;
      reconsider rdomg=M.(dom g) as Real by A130,XXREAL_0:14;
       {} c= DG by XBOOLE_1:2;
then A234:  0 <= rdomg by A128,A129,MEASURE1:62;
      per cases;
      suppose
A235:   e1 = +infty;
       set z=ralpha/2;
A236:  0 < z & z < alpha by A116,XREAL_1:217,218;
        R_EAL z*(beta+M.(dom g)) <= +infty by XXREAL_0:4;
       hence ex e be R_eal st 0 < e & e < alpha &
              e*(beta+M.(dom g)) <= e1 by A235,A236;
      end;
      suppose e1 <> +infty;
       then reconsider re1=e1 as Real by A233,XXREAL_0:14;
A237:  0+0 < beta1+rdomg by A132,A234,XREAL_1:10;
       set x=re1/(beta1 + rdomg);
       set y=ralpha/2;

A238:  y < ralpha by A116,XREAL_1:218;
       set z=min(x,y);
A239:  now per cases by XXREAL_0:15;
        suppose min(x,y)=x;
         hence 0 < z by A233,A237,XREAL_1:141;
        end;
        suppose min(x,y)=y;
         hence 0 < z by A116,XREAL_1:217;
        end;
       end;
        z <= y by XXREAL_0:17;
then A240:  z < ralpha by A238,XXREAL_0:2;
        z <=x by XXREAL_0:17;
       then z*(beta1 + rdomg) <= re1/(beta1 + rdomg)*(beta1 + rdomg)
         by A237,XREAL_1:66;
then A241:  z*(beta1 + rdomg) <= re1 by A237,XCMPLX_1:88;
        R_EAL(beta1 + rdomg) =R_EAL beta1 + R_EAL rdomg by SUPINF_2:1;
       then z*(beta1 + rdomg) = R_EAL z*(beta+M.(dom g)) by EXTREAL1:38;
       hence ex e be R_eal st 0 < e &
              e < alpha & e*(beta+M.(dom g)) <= e1 by A239,A240,A241;
      end;
     end;
A242: for e1 be R_eal st 0 < e1 holds
      ex N0 be Nat st
       for n be Nat st N0<= n holds integral'(M,g) - e1 < L.n
     proof
      let e1 be R_eal;
      assume 0 < e1;
      then consider e be R_eal such that
A243: 0 < e & e < alpha & e*(beta + M.(dom g)) <= e1 by A232;
      consider N0 be Nat such that
A244:    for n be Nat st N0<= n holds
        integral'(M,g) - e*(beta + M.(dom g)) < L.n by A230,A243;
      take N0;
       now let n be Nat;
       assume N0 <= n;
then A245:    integral'(M,g) - e*(beta + M.(dom g)) < L.n by A244;
        integral'(M,g) - e1 <= integral'(M,g) - e*(beta + M.(dom g))
         by A140,A243,SUPINF_2:15;
       hence integral'(M,g) - e1 < L.n by A245,XXREAL_0:2;
      end;
      hence thesis;
     end;
A246: for n,m be Nat st n<=m holds L.n <= L.m
     proof
      let n,m be Nat;
      assume
A247:    n <=m;
A248:  dom (F.n) = dom g & dom (F.m) = dom g by A4;
A249:  F.n is nonnegative & F.m is nonnegative by A5;
A250:  F.n is_simple_func_in S & F.m is_simple_func_in S by A3;
A251:   for x be set st x in dom(F.m - F.n) holds (F.n).x <= (F.m).x
      proof
       let x be set;
       assume x in dom(F.m - F.n);
       then x in (dom(F.m) /\ dom(F.n)) \ (
        ((F.m)"{+infty}/\(F.n)"{+infty})\/((F.m)"{-infty}/\(F.n)"{-infty}) )
          by MESFUNC1:def 4;
       then x in dom(F.m) /\ dom(F.n) by XBOOLE_0:def 4;
       hence thesis by A6,A247,A248;
      end;
then A252:   integral'(M,(F.n)|dom(F.m - F.n)) <= integral'(M,(F.m)|dom(F.m - F
.n))
         by A249,A250,Th76;
       dom(F.m - F.n) = dom(F.m) /\ dom(F.n) by A249,A250,A251,Th75;
      then (F.m)|dom(F.m - F.n) = F.m & (F.n)|dom(F.m - F.n) = F.n
         by A248,GRFUNC_1:64;
      then L.n <= integral'(M,(F.m) ) by A8,A252;
      hence L.n <= L.m by A8;
     end;
A253: for n be Nat holds 0 <= L.n
     proof
      let n be Nat;
A254:  F.n is nonnegative by A5;
       F.n is_simple_func_in S by A3;
      then 0 <= integral'(M,F.n) by A254,Th74;
      hence 0 <= L.n by A8;
     end;
     per cases;
     suppose ex K be real number st 0 < K & for n be Nat holds L.n < K;
      then consider K be real number such that
        0 < K and
A255:   for n be Nat holds L.n < K;
A256:  for n be Nat holds L.n <= sup rng L
      proof
       let n be Nat;
A257:    sup rng L is majorant of rng L by SUPINF_1:def 16;
       reconsider n as Element of NAT by ORDINAL1:def 13;
        dom L = NAT by FUNCT_2:def 1;
       then L.n in rng L by FUNCT_1:def 5;
       hence thesis by A257,SUPINF_1:def 9;
      end;
      then L.1 <= sup rng L;
then A258: sup rng L <> -infty by A253;
       R_EAL K is majorant of rng L
      proof
        now let x be ext-real number;
        assume x in rng L;
        then consider z be set such that
A259:     z in dom L and
A260:     x=L.z by FUNCT_1:def 5;
         z is Nat by A259,ORDINAL1:def 13;
        hence x <= R_EAL K by A255,A260;
       end;
       hence thesis by SUPINF_1:def 9;
      end;
then A261:   sup rng L <= R_EAL K by SUPINF_1:def 16;
       R_EAL K is Real by XREAL_0:def 1;
then A262:  sup rng L <>+infty by A261,XXREAL_0:9;
A263: for p be real number st 0<p
       ex N0 be Nat st for m be Nat st N0<=m holds |.(L.m)-sup(rng L) .| < p
      proof
       let p be real number;
       assume
A264:   0 < p;
        sup rng L is Real by A258,A262,XXREAL_0:14;
       then consider y being ext-real number such that
A265:     y in rng L & sup rng L - R_EAL p < y by A264,MEASURE6:33;
       consider x be set such that
A266:     x in dom L & y=L.x by A265,FUNCT_1:def 5;
       reconsider N0=x as Element of NAT by A266;
       take N0;
       let n be Nat;
       assume N0 <= n;
       then L.N0 <= L.n by A246;
then A267:   sup rng L -R_EAL p < L.n by A265,A266,XXREAL_0:2;
A268:   L.n <= sup rng L by A256;
        sup rng L + 0. <= sup rng L + R_EAL p by A264,SUPINF_2:14;
then A269:   sup rng L <= sup rng L + R_EAL p by SUPINF_2:18;
        sup rng L <> sup rng L + R_EAL p
       proof
        assume
A270:     sup rng L = sup rng L + R_EAL p;
         R_EAL p +sup rng L + -sup rng L
          =R_EAL p +(sup rng L + -sup rng L) by A258,A262,EXTREAL1:8
         .=R_EAL p + R_EAL 0 by EXTREAL1:9
         .=R_EAL p by SUPINF_2:18;
        hence contradiction by A264,A270,EXTREAL1:9;
       end;
       then sup rng L < sup rng L + R_EAL p by A269,XXREAL_0:1;
       then L.n < sup rng L + R_EAL p by A268,XXREAL_0:2;
then A271:   L.n -sup rng L < R_EAL p by EXTREAL2:30;
        sup rng L < L.n + R_EAL p by A267,EXTREAL2:29;
       then sup rng L - L.n < R_EAL p by EXTREAL2:30;
       then -R_EAL p < - (sup rng L - L.n) by SUPINF_2:17;
       then -R_EAL p < L.n -sup rng L by EXTREAL2:15;
       hence thesis by A271,EXTREAL2:59;
      end;
      reconsider h=sup rng L as real number by A258,A262,EXTREAL1:1;
A272: R_EAL h=sup rng L;
then A273:   L is convergent_to_finite_number by A263,Def8;
      hence L is convergent by Def11;
then A274:   lim L = sup rng L by A263,A272,A273,Def12;
       now let e be real number;
       assume 0 < e;
       then consider N0 be Nat such that
A275:     for n be Nat st N0<= n holds integral'(M,g) - R_EAL e < L.n by A242;
A276:    integral'(M,g) - R_EAL e < L.N0 by A275;
        L.N0 <= sup rng L by A256;
       then integral'(M,g) - R_EAL e < sup rng L by A276,XXREAL_0:2;
       hence integral'(M,g) < lim L+ R_EAL e by A274,EXTREAL2:29;
      end;
      hence integral'(M,g) <= lim L by Th4;
     end;
     suppose
A277:  not (ex K be real number st 0 < K & for n be Nat holds L.n < K);
       now let K be real number;
       assume 0 < K;
       then consider N0 be Nat such that
A278:   R_EAL K <= L.N0 by A277;
        now let n be Nat;
        assume N0 <=n;
        then L.N0 <= L.n by A246;
        hence R_EAL K <= L.n by A278,XXREAL_0:2;
       end;
       hence ex N0 be Nat st for n be Nat st N0<=n holds K <= L.n;
      end;
then A279:   L is convergent_to_+infty by Def9;
      hence L is convergent by Def11;
      then lim L = +infty by A279,Def12;
      hence integral'(M,g) <= lim L by XXREAL_0:4;
     end;
    end;
    suppose
A280:  M.(dom g) = +infty;
     reconsider DG=dom g as Element of S by A1,Th43;
A281:  for e be R_eal st 0 < e & e < alpha holds
      for n be Nat holds (alpha - e)*M.less_dom(g-F.n,e) <= integral'(M,F.n)
     proof
      let e be R_eal;
      assume
A282:   0 < e & e < alpha;
then A283: e <> -infty & e <> +infty by XXREAL_0:4;
      then e <+infty by XXREAL_0:4;
then A284: 0<= alpha-e by A282,EXTREAL1:40;
      consider H be SetSequence of X, MF be ExtREAL_sequence such that
A285:   for n be Nat holds H.n = less_dom(g-(F.n),e) and
        for n,m be Nat st n <= m holds H.n c= H.m and
A286:  for n be Nat holds H.n c= dom g and
        for n be Nat holds MF.n = M.(H.n) and
        M.(dom g) = sup(rng MF) and
A287:   for n be Nat holds H.n in S by A37,A282;
       now let n be Nat;
        H.n c= DG by A286;
then A288:  DG =DG \/ H.n by XBOOLE_1:12
         .=(DG \ H.n) \/ H.n by XBOOLE_1:39;
        H.n is Element of S by A287;
       then H.n is_measurable M by MEASURE1:def 12;
then A289: X \ H.n is_measurable M by MEASURE1:66;
        DG is_measurable M by MEASURE1:def 12;
then A290: DG /\ (X \ H.n) is_measurable M by A289,MEASURE1:66;
        DG /\ (X \ H.n) =(DG /\ X) \ H.n  by XBOOLE_1:49
                      .= DG \ H.n by XBOOLE_1:28;
       then reconsider A=DG \ H.n as Element of S by A290,MEASURE1:def 12;
       reconsider B=H.n as Element of S by A287;
A291: dom(F.n) = dom g by A4;
then A292: B c= dom(F.n) by A288,XBOOLE_1:7;
A293:    dom (F.n) = (A \/ B) /\ dom(F.n) by A288,A291;
        for x be set st x in dom(F.n) holds (F.n).x=(F.n).x;
then A294:  F.n = (F.n)|(A \/ B) by A293,FUNCT_1:68;
A295: F.n is nonnegative by A5;
A296: A misses B by XBOOLE_1:79;
A297: F.n is_simple_func_in S by A3;
then A298:  integral'(M,F.n) =integral'(M,(F.n)|A) + integral'(M,(F.n)|B)
                        by A294,A295,A296,Th73;
A299: (F.n)|A is nonnegative & (F.n)|B is nonnegative by A295,Th21;
A300:(F.n)|A is_simple_func_in S &
       (F.n)|B is_simple_func_in S by A297,Th40;
       then 0 <= integral'(M,(F.n)|A) & 0 <= integral'(M,(F.n)|B)
         by A299,Th74;
then A301:  integral'(M,(F.n)|B) <= integral'(M,F.n) by A298,SUPINF_2:20;
A302:dom((F.n)|B) =dom(F.n) /\ B by FUNCT_1:68
                   .= B by A292,XBOOLE_1:28;
        alpha - e <> +infty by A282,SUPINF_2:10;
       then consider EP be PartFunc of X,ExtREAL such that
A303:   EP is_simple_func_in S &
        dom EP= B &
        for x be set st x in B holds EP.x = alpha- e by A284,Th47;
        for x be set st x in dom EP holds 0 <= EP.x by A284,A303;
then A304: EP is nonnegative by SUPINF_2:71;
A305:  for x be set st x in dom((F.n)|B - EP) holds EP.x <= ((F.n)|B) .x
       proof
        let x be set;
        assume x in dom((F.n)|B - EP);
        then x in (dom((F.n)|B) /\ dom EP)\
         ( (((F.n)|B)"{+infty} /\ EP"{+infty})
           \/ (((F.n)|B)"{-infty} /\ EP"{-infty}) ) by MESFUNC1:def 4;
then A306:     x in dom((F.n)|B) /\ dom EP by XBOOLE_0:def 4;
then A307:     x in dom((F.n)|B) by XBOOLE_0:def 3;
then A308:     ((F.n)|B).x =(F.n).x by FUNCT_1:68;
        set f=g-F.n;
A309:     dom f = dom g by A34;
         x in less_dom(g-F.n,e) by A285,A302,A307;
then A310:     x in dom f & ex y being R_eal st y=f.x & y < e by
MESFUNC1:def 12;
then A311:    g.x - (F.n).x <= e by MESFUNC1:def 4;
         for x be set st x in dom g holds g.x <> -infty & g.x <> +infty
           by A33,EXTREAL2:67,MESFUNC2:def 1;
        then g.x <> -infty & g.x <> +infty by A309,A310;
        then g.x <= (F.n).x + e by A283,A311,EXTREAL2:33;
then A312:     g.x-e <= (F.n).x by A283,EXTREAL2:34;
         alpha <= g.x by A117,A309,A310;
        then alpha-e <= g.x-e by A283,SUPINF_2:15;
        then alpha -e <= (F.n).x by A312,XXREAL_0:2;
        hence thesis by A302,A303,A306,A308;
       end;
then A313:    integral'(M,EP|dom((F.n)|B - EP))
        <= integral'(M,((F.n)|B)|dom((F.n)|B - EP) )
           by A299,A300,A303,A304,Th76;
A314:  integral'(M,EP) = (alpha-e)* (M.B) by A284,A303,Th77;
        dom((F.n)|B - EP) = dom((F.n)|B) /\ dom EP
          by A299,A300,A303,A304,A305,Th75;
       then EP|dom((F.n)|B - EP) = EP &
       ((F.n)|B)|dom((F.n)|B - EP) = (F.n)|B by A302,A303,GRFUNC_1:64;
       then integral'(M,EP) <= integral'(M,F.n) by A301,A313,XXREAL_0:2;
       hence (alpha-e)* M.less_dom(g-F.n,e) <= integral'(M,F.n) by A285,A314;
      end;
      hence thesis;
     end;
      for y be real number st 0 < y
      ex n be Nat st for m be Nat st n<=m holds y <= L.m
     proof
      let y be real number;
      assume 0 < y;
      set e=alpha/R_EAL 2;
      reconsider ralpha=alpha as Real by XXREAL_0:14;
      set a2=ralpha/2;
A315:   e =a2 by EXTREAL1:39;
then A316: 0 < e by A116,XREAL_1:217;
A317: e < alpha by A116,A315,XREAL_1:218;
A318:  alpha - e = ralpha - a2 by A315,SUPINF_2:5;
A319:  a2-a2 < ralpha - a2  by A116,XREAL_1:16;
then A320: 0 < alpha - e by A315,SUPINF_2:5;
      reconsider y1=y as Real by XREAL_0:def 1;
       y =(ralpha - a2) * (y1/(ralpha - a2)) by A319,XCMPLX_1:88;
      then y =R_EAL(ralpha - a2) *R_EAL(y1/(ralpha - a2)) by EXTREAL1:38;
then A321: (alpha - e)*(R_EAL y / (alpha - e)) = y by A318,A319,EXTREAL1:39;
       y/(ralpha - a2) =R_EAL y1/ R_EAL(ralpha - a2) by A319,EXTREAL1:39
                    .=R_EAL y/ (alpha - e) by A315,SUPINF_2:5;
then A322:  R_EAL y / (alpha - e) < +infty by XXREAL_0:9;
      consider H be SetSequence of X, MF be ExtREAL_sequence such that
A323:   for n be Nat holds H.n = less_dom(g-(F.n),e) and
A324:   for n,m be Nat st n <= m holds H.n c= H.m and
        ( for n be Nat holds H.n c= dom g ) and
A325:   for n be Nat holds MF.n = M.(H.n) and
A326:   M.(dom g) = sup rng MF and
A327:   for n be Nat holds H.n in S by A37,A316,A317;
       ex z be ext-real number st z in rng MF & (R_EAL y/ (alpha - e)) <= z
      proof
       assume not (ex z be ext-real number st z in rng MF &
                  (R_EAL y / (alpha - e)) <= z);
       then for z be ext-real number st z in rng MF holds
        z <= (R_EAL y / (alpha - e));
       then R_EAL y / (alpha - e) is majorant of rng MF by SUPINF_1:def 9;
       hence contradiction by A280,A322,A326,SUPINF_1:def 16;
      end;
      then consider z be R_eal such that
A328:   z in rng MF & R_EAL y / (alpha - e) <= z;
      consider x be set such that
A329:   x in dom MF & z=MF.x by A328,FUNCT_1:def 5;
      reconsider N0=x as Element of NAT by A329;
      take N0;
      thus for m be Nat st N0 <= m holds y <= L.m
      proof
       let m be Nat;
       assume
A330:    N0 <= m;
        R_EAL y / (alpha - e) <= M.(H.N0) by A325,A328,A329;
then A331:   R_EAL y <= (alpha - e)*M.(H.N0) by A320,A321,EXTREAL1:42;
A332:   H.N0 c= H.m by A324,A330;
        H.N0 in S & H.m in S by A327;
       then M.(H.N0) <= M.(H.m) by A332,MEASURE1:62;
       then (alpha - e)*M.(H.N0) <= (alpha - e)*M.(H.m) by A320,EXTREAL1:42;
       then R_EAL y <= (alpha - e)*M.(H.m) by A331,XXREAL_0:2;
then A333:   R_EAL y <= (alpha - e)*M.less_dom(g-(F.m),e) by A323;
        (alpha - e)*M.less_dom(g-(F.m),e) <= integral'(M,F.m)
         by A281,A316,A317;
       then R_EAL y <= integral'(M,F.m) by A333,XXREAL_0:2;
       hence y <= L.m by A8;
      end;
     end;
then A334:  L is convergent_to_+infty by Def9;
     hence L is convergent by Def11;
     then ( ex g be real number st lim L = g &
       (for p be real number st 0<p
         ex n be Nat st for m be Nat st n<=m holds |. L.m- lim L .| < p) &
       L is convergent_to_finite_number ) or
     ( lim L = +infty & L is convergent_to_+infty ) or
     ( lim L = -infty & L is convergent_to_-infty ) by Def12;
     hence integral'(M,g) <= lim L by A334,Th56,XXREAL_0:4;
    end;
   end;
end;

theorem Th81:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    g be PartFunc of X,ExtREAL,
    F be Functional_Sequence of X,ExtREAL st
g is_simple_func_in S & g is nonnegative &
(for n be Nat holds F.n is_simple_func_in S) &
(for n be Nat holds dom(F.n) = dom g) &
(for n be Nat holds F.n is nonnegative) &
(for n,m be Nat st n <=m holds
   for x be Element of X st x in dom g holds (F.n).x <= (F.m).x ) &
(for x be Element of X st x in dom g holds
   (F#x) is convergent & g.x <= lim(F#x) ) holds
  ex G be ExtREAL_sequence st
    (for n be Nat holds G.n = integral'(M,F.n)) &
    G is convergent & sup(rng G)=lim (G) &
    integral'(M,g) <= lim(G)
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let g be PartFunc of X,ExtREAL;
   let F be Functional_Sequence of X,ExtREAL;
   assume that
A1:g is_simple_func_in S and
A2:g is nonnegative and
A3:for n be Nat holds F.n is_simple_func_in S and
A4:for n be Nat holds dom(F.n) = dom g and
A5:for n be Nat holds F.n is nonnegative and
A6:for n,m be Nat st n <= m holds
     for x be Element of X st x in dom g holds (F.n).x <= (F.m).x and
A7:for x be Element of X st x in dom g holds
     F#x is convergent & g.x <= lim(F#x);
   reconsider DG=dom g as Element of S by A1,Th43;
   set E0 = eq_dom(g,R_EAL 0);
   set E' = dom g \ E0;
    for x be set st x in E0 holds x in dom g by MESFUNC1:def 16;
then A8:E0 c= DG by TARSKI:def 3;
then A9:DG =DG \/ E0 by XBOOLE_1:12 .=(DG \ E0) \/ E0 by XBOOLE_1:39;
    g is_measurable_on DG by A1,MESFUNC2:37;
   then DG/\great_eq_dom(g,R_EAL 0) is_measurable_on S by MESFUNC1:31;
   then reconsider GG= DG/\great_eq_dom(g,R_EAL 0) as Element of S
     by MESFUNC1:def 11;
    g is_measurable_on GG by A1,MESFUNC2:37;
   then GG/\less_eq_dom(g,R_EAL 0) is_measurable_on S by MESFUNC1:32;
   then DG /\ eq_dom(g,R_EAL 0) is_measurable_on S by MESFUNC1:22;
then A10:eq_dom(g,0.) is_measurable_on S by A8,XBOOLE_1:28;
   then E0 is Element of S by MESFUNC1:def 11;
   then E0 is_measurable M by MEASURE1:def 12;
then A11:X \ E0 is_measurable M by MEASURE1:66;
    DG is_measurable M by MEASURE1:def 12;
then A12:DG /\ (X \ E0) is_measurable M by A11,MEASURE1:66;
    DG /\ (X \ E0) =(DG /\ X) \ E0 by XBOOLE_1:49
                 .= DG \ E0 by XBOOLE_1:28;
   then reconsider E' as Element of S by A12,MEASURE1:def 12;
   reconsider E0 as Element of S by A10,MESFUNC1:def 11;
A13:E0 misses E' by XBOOLE_1:79;
   thus ex G be ExtREAL_sequence st
    (for n be Nat holds G.n = integral'(M,F.n)) &
    G is convergent &sup rng G = lim G &
    integral'(M,g) <= lim G
   proof
A14: E' c= dom g by A9,XBOOLE_1:7;
A15: dom(g|E') =dom g /\ E' by FUNCT_1:68 .= E' by A14,XBOOLE_1:28;
A16:g|E' is_simple_func_in S by A1,Th40;
A17:for x be set st x in dom(g|E') holds 0 < (g|E').x
    proof
     let x be set;
     assume
A18:   x in dom(g|E');
     then x in DG & not x in E0 by A15,XBOOLE_0:def 4;
     then g.x <> 0 by MESFUNC1:def 16;
     then 0 < g.x by A2,SUPINF_2:70;
     hence 0 < (g|E').x by A18,FUNCT_1:70;
    end;
    deffunc W(Nat) = (F.$1)|E';
    consider F' be Functional_Sequence of X,ExtREAL such that
A19: for n be Nat holds F'.n=W(n) from SEQFUNC:sch 1;
A20: for n being Nat holds F'.n=W(n) by A19;
A21:for n be Nat holds dom(F.n|E') = dom(g|E') & dom(F'.n) = dom(g|E')
    proof
     let n be Nat;
      dom(F.n) = E' \/ E0 by A4,A9;
then A22: E' c= dom(F.n) by XBOOLE_1:7;
     thus dom(F.n|E') =dom(F.n) /\ E' by FUNCT_1:68
                     .=dom(g|E') by A15,A22,XBOOLE_1:28;
     hence dom(F'.n) = dom(g|E') by A20;
    end;
A23:for n,m be Nat st n <= m holds
     for x be Element of X st x in dom(g|E') holds
      (F.n|E').x <= (F.m|E').x & (F'.n).x <= (F'.m).x
    proof
     let n,m be Nat;
     assume
A24:   n<=m;
     thus for x be Element of X st x in dom(g|E') holds
       (F.n|E').x <= (F.m|E').x & (F'.n).x <= (F'.m).x
     proof
      let x be Element of X;
      assume
A25:   x in dom(g|E');
then A26:  x in dom(F.m|E') & x in dom(F.n|E') by A21;
       (F.n).x <= (F.m).x by A6,A14,A15,A24,A25;
      then (F.n|E').x <= (F.m).x by A26,FUNCT_1:70;
      hence (F.n|E').x <= (F.m|E').x by A26,FUNCT_1:70;
      then (F'.n).x <= (F.m|E').x by A20;
      hence (F'.n).x <= (F'.m).x by A20;
     end;
    end;
A27:for n be Nat holds (F.n)|E' is_simple_func_in S & F'.n is_simple_func_in S
    proof
     let n be Nat;
      F.n is_simple_func_in S by A3;
     hence F.n|E' is_simple_func_in S by Th40;
     hence F'.n is_simple_func_in S by A20;
    end;
A28:for x be Element of X st x in dom(g|E') holds
     F'#x is convergent & (g|E').x <= lim(F'#x)
    proof
     let x be Element of X;
     assume
A29:  x in dom(g|E');
     then x in dom g /\ E' by FUNCT_1:68;
then A30: x in dom g by XBOOLE_0:def 3;
A31: (F'#x)=(F#x)
     proof
       now let n be Element of NAT;
A32:     x in dom(F.n|E') by A21,A29;
       thus (F'#x).n = (F'.n).x by Def13
                    .= (F.n|E').x by A20
                    .= (F.n).x by A32,FUNCT_1:70
                    .= (F#x).n by Def13;
      end;
      hence thesis by FUNCT_2:113;
     end;
      F#x is convergent & g.x <= lim(F#x) by A7,A30;
     hence thesis by A29,A31,FUNCT_1:70;
    end;
A33: dom g = (E0 \/ E') /\ dom g by A9;
     for x be set st x in dom g holds g.x=g.x;
    then g|(E0\/E') = g by A33,FUNCT_1:68;
then A34: integral'(M,g) = integral'(M,g|E0) + integral'(M,g|E') by A1,A2,A13,
Th73;
     integral'(M,g|E0) = 0 by A1,A2,Th78;
then A35: integral'(M,g) = integral'(M,g|E') by A34,SUPINF_2:18;
    deffunc U(Nat) = integral'(M,F.$1);
    consider G be ExtREAL_sequence such that
A36: for n be Element of NAT holds G.n = U(n) from FUNCT_2:sch 4;
A37: now let n be Nat;  n in NAT by ORDINAL1:def 13;
       hence G.n = U(n) by A36;
     end;
A38: for n,m be Nat st n <=m holds G.n <= G.m
    proof
     let n,m be Nat;
     assume A39:n<=m;
A40: F.n is_simple_func_in S & F.m is_simple_func_in S by A3;
A41: dom(F.n) = dom g & dom(F.m) = dom g by A4;
A42: F.n is nonnegative & F.m is nonnegative by A5;
A43: for x be set st x in dom (F.m - F.n) holds (F.n).x <= (F.m).x
     proof
      let x be set;
      assume x in dom(F.m - F.n);
      then x in (dom(F.m) /\ dom(F.n))
        \ ( (F.m)"{+infty}/\(F.n)"{+infty} \/ (F.m)"{-infty}/\(F.n)"{-infty} )
         by MESFUNC1:def 4;
      then x in dom(F.m) /\ dom(F.n) by XBOOLE_0:def 4;
      hence thesis by A6,A39,A41;
     end;
     then dom(F.m - F.n) = dom(F.m) /\ dom(F.n) by A40,A42,Th75;
then A44:  F.n|dom(F.m - F.n) = F.n & F.m|dom(F.m - F.n) = F.m by A41,
GRFUNC_1:64;
      G.n = integral'(M,F.n) & G.m = integral'(M,F.m) by A37;
     hence thesis by A40,A42,A43,A44,Th76;
    end;
    take G;
A45:G is convergent & lim G = sup rng G by A38,Th60;
A46:for n be Nat holds F'.n is nonnegative
    proof
     let n be Nat;
      F.n is nonnegative by A5;
     then F.n|E' is nonnegative by Th21;
     hence F'.n is nonnegative by A20;
    end;
    deffunc V(Nat) = integral'(M,(F.$1)|E');
    consider L be ExtREAL_sequence such that
A47: for n be Element of NAT holds L.n = V(n) from FUNCT_2:sch 4;
A48: now let n be Nat;  n in NAT by ORDINAL1:def 13;
      hence L.n=V(n) by A47;
    end;
A49:for n,m be Nat st n <= m holds
     for x be Element of X st x in dom(g|E') holds (F'.n).x <= (F'.m).x
       by A23;
A50:for n be Nat holds L.n = integral'(M,F'.n)
    proof
     let n be Nat;
     thus L.n = integral'(M,F.n|E') by A48
             .=integral'(M,F'.n) by A20;
    end;
then A51:L is convergent & integral'(M,g|E') <= lim L
      by A16,A17,A21,A27,A28,A46,A49,Th80;
     for n,m be Nat st n <= m holds L.n <= L.m
    proof
     let n,m be Nat;
     assume A52:n<=m;
A53: F'.n is_simple_func_in S & F'.m is_simple_func_in S by A27;
A54: dom(F'.n) = dom(g|E') & dom(F'.m) =dom(g|E') by A21;
A55: F'.n is nonnegative & F'.m is nonnegative by A46;
A56:  for x be set st x in dom(F'.m - F'.n) holds (F'.n).x <= (F'.m).x
     proof
      let x be set;
      assume x in dom(F'.m - F'.n);
      then x in (dom(F'.m) /\ dom(F'.n)
        \(((F'.m)"{+infty}/\(F'.n)"{+infty})
          \/((F'.m)"{-infty}/\(F'.n)"{-infty}))) by MESFUNC1:def 4;
      then x in dom(F'.m) /\ dom(F'.n) by XBOOLE_0:def 4;
      hence thesis by A23,A52,A54;
     end;
     then dom(F'.m - F'.n) = dom(F'.m) /\ dom(F'.n) by A53,A55,Th75;
then A57:  (F'.n)|dom(F'.m - F'.n) = F'.n &
     (F'.m)|dom(F'.m - F'.n) = F'.m by A54,GRFUNC_1:64;
      L.n = integral'(M,F'.n) &
     L.m = integral'(M,F'.m) by A50;
     hence thesis by A53,A55,A56,A57,Th76;
    end;
then A58:L is convergent & lim L = sup rng L by Th60;
     for n be Nat holds L.n <= G.n
    proof
     let n be Nat;
 A59:  G.n = integral'(M,F.n) by A37;
A60:   dom(F.n) = E' \/ E0 by A4,A9;
A61:F.n is_simple_func_in S by A3;
      F.n is nonnegative by A5;
then A62:(F.n|E0) is nonnegative & F.n|E' is nonnegative by Th21;
      F.n|E0 is_simple_func_in S & F.n|E' is_simple_func_in S
      by A61,Th40;
then A63: 0 <= integral'(M,F.n|E0) & 0 <= integral'(M,F.n|E') by A62,Th74;
 A64: dom(F.n) = (E0 \/ E') /\ dom (F.n) by A60;
      for x be set st x in dom(F.n) holds (F.n).x=(F.n).x;
then A65:  F.n = F.n|(E0 \/ E') by A64,FUNCT_1:68;
then A66:  F.n|(E0 \/ E') is nonnegative by A5;
      F.n is_simple_func_in S by A3;
     then integral'(M,F.n) =integral'(M,F.n|E0) + integral'(M,F.n|E')
      by A13,A65,A66,Th73;
     then integral'(M,F.n|E') <= integral'(M,F.n) by A63,SUPINF_2:20;
     hence L.n <= G.n by A48,A59;
    end;
    then sup rng L <= sup rng G by Th61;
    hence thesis by A35,A37,A45,A51,A58,XXREAL_0:2;
   end;
end;

theorem Th82:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    A be Element of S,
    F,G be Functional_Sequence of X,ExtREAL,
    K,L be ExtREAL_sequence st
(for n be Nat holds F.n is_simple_func_in S & dom(F.n)=A ) &
(for n be Nat holds F.n is nonnegative) &
(for n,m be Nat st n <=m holds
   for x be Element of X st x in A holds (F.n).x <= (F.m).x ) &
(for n be Nat holds G.n is_simple_func_in S & dom(G.n)=A) &
(for n be Nat holds G.n is nonnegative) &
(for n,m be Nat st n <=m holds
   for x be Element of X st x in A holds (G.n).x <= (G.m).x ) &
(for x be Element of X st x in A holds
     F#x is convergent & G#x is convergent & lim(F#x) = lim(G#x)) &
(for n be Nat holds K.n=integral'(M,F.n) & L.n=integral'(M,G.n))
holds
  K is convergent & L is convergent & lim K = lim L
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       A be Element of S,
       F,G be Functional_Sequence of X,ExtREAL,
       K,L be ExtREAL_sequence such that
A1:for n be Nat holds F.n is_simple_func_in S & dom(F.n)=A and
A2:for n be Nat holds F.n is nonnegative and
A3:for n,m be Nat st n <=m holds for x be Element of X st x in A holds
       (F.n).x <= (F.m).x and
A4:for n be Nat holds G.n is_simple_func_in S & dom(G.n)=A and
A5:for n be Nat holds G.n is nonnegative and
A6:for n,m be Nat st n <=m holds for x be Element of X st x in A holds
       (G.n).x <= (G.m).x and
A7:for x be Element of X st x in A holds
     F#x is convergent & G#x is convergent & lim(F#x) = lim(G#x) and
A8:for n be Nat holds K.n=integral'(M,F.n) & L.n=integral'(M,G.n);
A9:for n0 be Nat holds K is convergent & sup rng K = lim K & L.n0 <= lim K
   proof
    let n0 be Nat;
    reconsider g=G.n0 as PartFunc of X,ExtREAL;
A10:dom g = A by A4;
for x be Element of X st x in dom g holds
     F#x is convergent & g.x <= lim(F#x)
    proof
     let x be Element of X;
     assume x in dom g;
then A11: x in A by A4;
      now let n,m be Nat;
      assume A12: n<=m;
       (G#x).n=(G.n).x & (G#x).m=(G.m).x by Def13;
      hence (G#x).n <= (G#x).m by A6,A11,A12;
     end;
then A13:  lim(G#x)=sup rng(G#x) by Th60;
A14:  g.x=(G#x).n0 by Def13;
      (G#x).n0 <= sup rng(G#x) by Th62;
     hence thesis by A7,A11,A13,A14;
    end;
    then g is_simple_func_in S &
    g is nonnegative &
    (for n be Nat holds F.n is_simple_func_in S) &
    (for n be Nat holds dom(F.n) = dom g) &
    (for n be Nat holds F.n is nonnegative) &
    (for n,m be Nat st n <=m holds
     for x be Element of X st x in dom g holds (F.n).x <= (F.m).x ) &
    (for x be Element of X st x in dom g holds
      F#x is convergent & g.x <= lim(F#x) ) by A1,A2,A3,A4,A5,A10;
    then consider GG be ExtREAL_sequence such that
A15:  (for n be Nat holds GG.n = integral'(M,F.n)) &
     GG is convergent & sup rng GG = lim GG &
     integral'(M,g) <= lim GG by Th81;
     GG=K
    proof
      now let n be Element of NAT;
       GG.n = integral'(M,F.n) by A15;
      hence GG.n = K.n by A8;
     end;
     hence thesis by FUNCT_2:113;
    end;
    hence K is convergent & sup rng K = lim K & L.n0 <= lim K by A8,A15;
   end;
A16:for n0 be Nat holds
    L is convergent & sup rng L=lim L & K.n0 <= lim L
   proof
    let n0 be Nat;
    reconsider f=F.n0 as PartFunc of X,ExtREAL;
A17:dom f = A by A1;
for x be Element of X st x in dom f holds
     G#x is convergent & f.x <= lim(G#x)
    proof
     let x be Element of X;
     assume x in dom f;
then A18: x in A by A1;
A19:  f.x=(F#x).n0 by Def13;
      now let n,m be Nat; assume
A20:  n<=m;
       (F#x).n=(F.n).x & (F#x).m=(F.m).x by Def13;
      hence (F#x).n <= (F#x).m by A3,A18,A20;
     end;
then A21:  lim(F#x)=sup rng(F#x) by Th60;
      (F#x).n0 <= sup rng (F#x) by Th62;
     hence thesis by A7,A18,A19,A21;
    end;
    then f is_simple_func_in S & f is nonnegative &
    (for n be Nat holds G.n is_simple_func_in S) &
    (for n be Nat holds dom(G.n) = dom f) &
    (for n be Nat holds G.n is nonnegative) &
    (for n,m be Nat st n <=m holds
      for x be Element of X st x in dom f holds (G.n).x <= (G.m).x ) &
    (for x be Element of X st x in dom f holds
      G#x is convergent & f.x <= lim(G#x)) by A1,A2,A4,A5,A6,A17;
    then consider FF be ExtREAL_sequence such that
A22:  (for n be Nat holds FF.n = integral'(M,G.n)) &
     FF is convergent & sup rng FF = lim FF &
     integral'(M,f) <= lim FF by Th81;
     now let n be Element of NAT;
      FF.n = integral'(M,G.n) by A22;
     hence FF.n = L.n by A8;
    end;
    then FF=L by FUNCT_2:113;
    hence L is convergent & sup rng L = lim L & K.n0 <= lim L by A8,A22;
   end;
   hence K is convergent & L is convergent by A9;
    lim L <= lim K & lim K <= lim L by A9,A16,Th63;
   hence thesis by XXREAL_0:1;
end;

definition
let X be non empty set;
let S be SigmaField of X;
let M be sigma_Measure of S;
let f be PartFunc of X,ExtREAL;
  assume that
A1:ex A be Element of S st A = dom f & f is_measurable_on A and
A2:f is nonnegative;
func integral+(M,f) -> Element of ExtREAL means :Def15:
ex F be Functional_Sequence of X,ExtREAL,
   K be ExtREAL_sequence st
    (for n be Nat holds F.n is_simple_func_in S & dom(F.n) = dom f) &
    (for n be Nat holds F.n is nonnegative) &
    (for n,m be Nat st n <=m holds
      for x be Element of X st x in dom f holds (F.n).x <= (F.m).x ) &
    (for x be Element of X st  x in dom f holds
      F#x is convergent & lim(F#x) = f.x) &
    (for n be Nat holds K.n=integral'(M,F.n)) &
    K is convergent &
    it=lim K;
existence
proof
   consider F be Functional_Sequence of X,ExtREAL such that
A3: (for n be Nat holds F.n is_simple_func_in S & dom(F.n) = dom f) &
    (for n be Nat holds F.n is nonnegative) &
    (for n,m be Nat st n <=m holds
      for x be Element of X st x in dom f holds (F.n).x <= (F.m).x ) &
    (for x be Element of X st x in dom f holds
      F#x is convergent & lim(F#x) = f.x) by A1,A2,Th70;
   reconsider g=F.0 as PartFunc of X,ExtREAL;
A4:for x be Element of X st x in dom f holds
    F#x is convergent & g.x <= lim(F#x)
   proof
    let x be Element of X such that
 A5:x in dom f;
 now let n,m be Nat;
     assume
  A6:n<=m;
      (F#x).n=(F.n).x & (F#x).m = (F.m).x by Def13;
     hence (F#x).n <= (F#x).m by A3,A5,A6;
    end;
 then A7:F#x is convergent & lim(F#x)=sup rng(F#x) by Th60;
     g.x=(F#x).0 by Def13;
    hence thesis by A7,Th62;
   end;
    dom g = dom f by A3;
   then g is_simple_func_in S & g is nonnegative &
   (for n be Nat holds F.n is_simple_func_in S) &
   (for n be Nat holds dom(F.n) = dom g) &
   (for n be Nat holds F.n is nonnegative) &
   (for n,m be Nat st n <=m holds
     for x be Element of X st x in dom g holds (F.n).x <= (F.m).x ) &
   for x be Element of X st x in dom g holds
     F#x is convergent & g.x <= lim(F#x) by A3,A4;
   then ex G be ExtREAL_sequence st
    (for n be Nat holds G.n = integral'(M,F.n)) &
    G is convergent & sup rng G=lim G &
    integral'(M,g) <= lim G by Th81;
   then consider G be ExtREAL_sequence such that
 A8:(for n be Nat holds G.n = integral'(M,F.n)) &
    G is convergent &
    integral'(M,g) <= lim G;
   take lim G;
   thus thesis by A3,A8;
end;
uniqueness
proof
   let s1,s2 be Element of ExtREAL such that
A9:ex F1 be Functional_Sequence of X,ExtREAL,
      K1 be ExtREAL_sequence st
    (for n be Nat holds F1.n is_simple_func_in S & dom(F1.n) = dom f) &
    (for n be Nat holds F1.n is nonnegative) &
    (for n,m be Nat st n <=m holds
      for x be Element of X st x in dom f holds (F1.n).x <= (F1.m).x ) &
    (for x be Element of X st x in dom f holds
      F1#x is convergent & lim(F1#x) = f.x) &
    (for n be Nat holds K1.n=integral'(M,F1.n)) &
    K1 is convergent & s1=lim K1 and
A10:ex F2 be Functional_Sequence of X,ExtREAL,
      K2 be ExtREAL_sequence st
    (for n be Nat holds F2.n is_simple_func_in S & dom(F2.n) = dom f) &
    (for n be Nat holds F2.n is nonnegative) &
    (for n,m be Nat st n <=m holds
      for x be Element of X st x in dom f holds (F2.n).x <= (F2.m).x ) &
    (for x be Element of X st x in dom f holds
      F2#x is convergent & lim(F2#x) = f.x) &
    (for n be Nat holds K2.n=integral'(M,F2.n)) &
    K2 is convergent & s2=lim K2;
   consider F1 be Functional_Sequence of X,ExtREAL,
            K1 be ExtREAL_sequence such that
A11: (for n be Nat holds F1.n is_simple_func_in S & dom(F1.n) = dom f) &
    (for n be Nat holds F1.n is nonnegative) &
    (for n,m be Nat st n <=m holds
      for x be Element of X st x in dom f holds (F1.n).x <= (F1.m).x ) &
    (for x be Element of X st x in dom f holds
      F1#x is convergent & lim(F1#x) = f.x) &
    (for n be Nat holds K1.n=integral'(M,F1.n)) &
    K1 is convergent & s1=lim(K1) by A9;
   consider F2 be Functional_Sequence of X,ExtREAL,
            K2 be ExtREAL_sequence such that
A12: (for n be Nat holds F2.n is_simple_func_in S & dom(F2.n) = dom f) &
    (for n be Nat holds F2.n is nonnegative) &
    (for n,m be Nat st n <=m holds
      for x be Element of X st x in dom f holds (F2.n).x <= (F2.m).x ) &
    (for x be Element of X st x in dom f holds
      (F2#x) is convergent & lim(F2#x) = f.x) &
    (for n be Nat holds K2.n=integral'(M,F2.n)) &
    K2 is convergent & s2=lim K2 by A10;
    for x be Element of X st x in dom f holds
    F1#x is convergent & F2#x is convergent & lim(F1#x) = lim(F2#x)
   proof
    let x be Element of X; assume
 A13:x in dom f;
    then lim(F1#x) = f.x by A11 .= lim(F2#x) by A12,A13;
    hence thesis by A11,A12,A13;
   end;
   hence s1=s2 by A1,A11,A12,Th82;
end;
end;

theorem Th83:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL st
 f is_simple_func_in S & f is nonnegative holds
   integral+(M,f) =integral'(M,f)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL such that
A1:f is_simple_func_in S & f is nonnegative;
    ex GF be Finite_Sep_Sequence of S st
    dom f = union rng GF &
     for n being Nat,x,y being Element of X st
      n in dom GF & x in GF.n & y in GF.n holds f.x = f.y
       by A1,MESFUNC2:def 5;
   then reconsider A=dom f as Element of S by MESFUNC2:34;
A2:f is_measurable_on A by A1,MESFUNC2:37;
   deffunc PF(Nat) = f;
   consider F be Functional_Sequence of X,ExtREAL such that
A3: for n be Nat holds F.n=PF(n) from SEQFUNC:sch 1;
A4: for n being Nat holds F.n=PF(n) by A3;
   deffunc PK(Nat) = integral'(M,(F.$1));
   consider K be Function of NAT,ExtREAL such that
A5: for n be Element of NAT holds K.n = PK(n) from FUNCT_2:sch 4;
A6: now let n be Nat;  n in NAT by ORDINAL1:def 13;
      hence K.n=PK(n) by A5;
    end;
A7:for n be Nat holds F.n is_simple_func_in S & dom(F.n) = dom f by A1,A4;
A8:for n be Nat holds F.n is nonnegative by A1,A4;
A9:for n,m be Nat st n <=m holds
    for x be Element of X st x in dom f holds (F.n).x <= (F.m).x
   proof
    let n,m be Nat;
    assume n<=m;
    let x be Element of X;
    assume x in dom f;
     (F.n).x=f.x by A4;
    hence (F.n).x <=(F.m).x by A4;
   end;
A10:for x be Element of X st x in dom f holds
     F#x is convergent & lim(F#x) = f.x
   proof
    let x be Element of X;
    assume x in dom f;
     now let n be Nat;
     thus (F#x).n = (F.n).x by Def13 .=f.x by A4;
    end;
    hence F#x is convergent & lim(F#x) = f.x by Th66;
   end;
    for n be Nat holds K.n=integral'(M,f)
   proof
    let n be Nat;
    thus K.n=integral'(M,F.n) by A6
           .=integral'(M,f) by A4;
   end;
   then K is convergent & lim K=integral'(M,f) by Th66;
   hence integral+(M,f)=integral'(M,f)
      by A1,A2,A6,A7,A8,A9,A10,Def15;
end;

Lm11:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
 ( ex A be Element of S st
    A = dom f & A = dom g & f is_measurable_on A & g is_measurable_on A ) &
 f is nonnegative & g is nonnegative
 holds integral+(M,f+g) = integral+(M,f) + integral+(M,g)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL such that
A1: ex A be Element of S st
     A = dom f & A = dom g & f is_measurable_on A & g is_measurable_on A and
A2: f is nonnegative & g is nonnegative;
   consider F1 be Functional_Sequence of X,ExtREAL,
            K1 be ExtREAL_sequence such that
A3: (for n be Nat holds F1.n is_simple_func_in S & dom(F1.n) = dom f) &
    (for n be Nat holds F1.n is nonnegative) &
    (for n,m be Nat st n <=m holds
      for x be Element of X st x in dom f holds (F1.n).x <= (F1.m).x ) &
    (for x be Element of X st x in dom f holds
      F1#x is convergent & lim(F1#x) = f.x) &
    (for n be Nat holds K1.n=integral'(M,F1.n)) &
    K1 is convergent &
    integral+(M,f)=lim K1 by A1,A2,Def15;
   consider F2 be Functional_Sequence of X,ExtREAL,
            K2 be ExtREAL_sequence such that
A4: (for n be Nat holds F2.n is_simple_func_in S & dom(F2.n) = dom g) &
    (for n be Nat holds F2.n is nonnegative) &
    (for n,m be Nat st n <=m holds
      for x be Element of X st x in dom g holds (F2.n).x <= (F2.m).x ) &
    (for x be Element of X st x in dom g holds
      F2#x is convergent & lim(F2#x) = g.x) &
    (for n be Nat holds K2.n=integral'(M,F2.n)) &
    K2 is convergent &
    integral+(M,g)=lim K2 by A1,A2,Def15;
    now let n be set;
    assume n in dom K1;
    then reconsider n1 = n as Element of NAT;
     K1.n1 = integral'(M,F1.n1) & F1.n1 is_simple_func_in S &
    F1.n1 is nonnegative by A3;
    hence -infty < K1.n by Th74;
   end;
then A5:K1 is without-infty by Th16;
A6:for n,m be Nat st n<=m holds K1.n <= K1.m
   proof
    let n,m be Nat such that A7:n<=m;
A8: K1.n = integral'(M,F1.n) & K1.m = integral'(M,F1.m) & dom(F1.n) = dom f &
    F1.n is_simple_func_in S & F1.m is_simple_func_in S & dom(F1.m) = dom f &
    F1.n is nonnegative & F1.m is nonnegative by A3;
A9: now let x be set;
     assume x in dom(F1.m - F1.n);
     then x in (dom(F1.m) /\ dom(F1.n)) \
      ( (F1.m)"{+infty}/\(F1.n)"{+infty} \/ (F1.m)"{-infty}/\(F1.n)"{-infty} )
        by MESFUNC1:def 4;
     then x in dom(F1.m) /\ dom(F1.n) by XBOOLE_0:def 4;
     hence (F1.n).x <= (F1.m).x by A3,A7,A8;
    end;
    then dom(F1.m - F1.n) = dom(F1.m) /\ dom(F1.n) by A8,Th75;
    then (F1.n)|dom(F1.m - F1.n) = F1.n & (F1.m)|dom(F1.m - F1.n) = F1.m
       by A8,GRFUNC_1:64;
    hence K1.n <= K1.m by A8,A9,Th76;
   end;
    now let n be set;
    assume n in dom K2;
    then reconsider n1=n as Element of NAT;
     K2.n1 = integral'(M,F2.n1) & F2.n1 is_simple_func_in S &
    F2.n1 is nonnegative by A4;
    hence -infty < K2.n by Th74;
   end;
then A10:K2 is without-infty by Th16;
A11:for n,m be Nat st n<=m holds K2.n <= K2.m
   proof
    let n,m be Nat such that A12:n<=m;
A13: K2.n = integral'(M,F2.n) & K2.m = integral'(M,F2.m) & dom(F2.n) = dom g &
    F2.n is_simple_func_in S & F2.m is_simple_func_in S & dom(F2.m) = dom g &
    F2.n is nonnegative & F2.m is nonnegative by A4;
A14: now let x be set;
     assume x in dom(F2.m - F2.n);
     then x in (dom(F2.m) /\ dom(F2.n)) \
      ( ((F2.m)"{+infty}/\(F2.n)"{+infty})
         \/((F2.m)"{-infty}/\(F2.n)"{-infty}) ) by MESFUNC1:def 4;
     then x in dom(F2.m) /\ dom(F2.n) by XBOOLE_0:def 4;
     hence (F2.n).x <= (F2.m).x by A4,A12,A13;
    end;
    then dom(F2.m - F2.n) = dom(F2.m) /\ dom(F2.n) by A13,Th75;
    then F2.n|dom(F2.m - F2.n) = F2.n & F2.m|dom(F2.m - F2.n) = F2.m
      by A13,GRFUNC_1:64;
    hence K2.n <= K2.m by A13,A14,Th76;
   end;
   consider A be Element of S such that
A15: A = dom f & A = dom g &
    f is_measurable_on A & g is_measurable_on A by A1;
A16:f is without-infty & g is without-infty by A2,Th18;
then A17:f+g is_measurable_on A by A15,Th37;
    A =dom f /\ dom g by A15;
then A18:A =dom(f+g) by A16,Th22;
A19:f+g is nonnegative by A2,Th25;
   deffunc PF(Nat) = F1.$1+F2.$1;
   consider F be Functional_Sequence of X,ExtREAL such that
A20: for n be Nat holds F.n=PF(n) from SEQFUNC:sch 1;
A21: for n being Nat holds F.n=PF(n) by A20;
   deffunc PK(Nat) = integral'(M,F.$1);
   consider K be ExtREAL_sequence such that
A22: for n be Element of NAT holds K.n = PK(n) from FUNCT_2:sch 4;
A23: now let n be Nat;  n in NAT by ORDINAL1:def 13;
      hence K.n = PK(n) by A22;
    end;
A24:for n be Nat holds
    F.n is_simple_func_in S & dom(F.n) = dom(f+g) & F.n is nonnegative
   proof
    let n be Nat;
 A25:dom(F1.n)=dom f & F1.n is_simple_func_in S & F1.n is nonnegative by A3;
 A26:dom(F2.n)=dom g & F2.n is_simple_func_in S & F2.n is nonnegative by A4;
 A27:F.n=F1.n+F2.n by A21;
    hence F.n is_simple_func_in S by A25,A26,Th44;
     dom(F.n)= dom(F1.n) /\ dom(F2.n) by A25,A26,A27,Th71;
    hence dom(F.n) = dom(f+g) by A16,A25,A26,Th22;
     F1.n is nonnegative & F2.n is nonnegative & F.n=F1.n+F2.n by A3,A4,A21;
    hence F.n is nonnegative by Th25;
   end;
A28:for n,m be Nat st n <=m holds
    for x be Element of X st x in dom (f+g) holds (F.n).x <= (F.m).x
   proof
    let n,m be Nat;
    assume A29:n<=m;
    let x be Element of X;
    assume A30:x in dom (f+g);
     dom(F1.n+F2.n) = dom(F.n) & dom(F1.m+F2.m) = dom(F.m) by A21;
then A31: dom(F1.n+F2.n) = dom(f+g) & dom(F1.m+F2.m) = dom(f+g) by A24;
     (F.n).x =(F1.n+F2.n).x & (F.m).x =(F1.m+F2.m).x by A21;
then A32: (F.n).x = (F1.n).x+(F2.n).x &
    (F.m).x = (F1.m).x+(F2.m).x by A30,A31,MESFUNC1:def 3;
     F1.n is nonnegative & F2.n is nonnegative by A3,A4;
    then F1.n is without-infty & F2.n is without-infty by Th18;
then A33:-infty < (F1.n).x & -infty < (F2.n).x by Def5;
     (F1.n).x <= (F1.m).x & (F2.n).x <= (F2.m).x by A3,A4,A15,A18,A29,A30;
    hence (F.n).x <= (F.m).x by A32,A33,SUPINF_2:14;
   end;
A34:for x be Element of X st x in dom(f+g) holds
    F#x is convergent & lim(F#x) = (f+g).x
   proof
    let x be Element of X;
    assume A35:x in dom(f+g);
 A36:now let n be Nat;
 A37:dom(F1.n+F2.n) = dom(F.n) by A21 .=dom(f+g)  by A24;
      (F#x).n = (F.n).x by Def13;
     then (F#x).n = (F1.n+F2.n).x by A21;
     then (F#x).n = (F1.n).x+(F2.n).x by A35,A37,MESFUNC1:def 3;
     then (F#x).n = (F1#x).n+(F2.n).x by Def13;
     hence (F#x).n = (F1#x).n+(F2#x).n by Def13;
    end;
     now let n be set;
     hereby assume n in dom(F1#x);
      then reconsider n1=n as Element of NAT;
       F1.n1 is nonnegative by A3;
   then A38:F1.n1 is without-infty by Th18;
       (F1#x).n1 = (F1.n1).x by Def13;
      hence -infty < (F1#x).n by A38,Def5;
     end;
     assume n in dom(F2#x);
     then reconsider n1=n as Element of NAT;
      F2.n1 is nonnegative by A4;
  then A39:F2.n1 is without-infty by Th18;
      (F2#x).n1 = (F2.n1).x by Def13;
     hence -infty < (F2#x).n by A39,Def5;
    end;
 then A40:F1#x is without-infty & F2#x is without-infty by Th16;
 A41:now let n,m be Nat;
     assume A42:n <=m;
      (F1#x).n = (F1.n).x & (F1#x).m = (F1.m).x by Def13;
     hence (F1#x).n<= (F1#x).m by A3,A15,A18,A35,A42;
    end;
 A43:now let n,m be Nat;
     assume A44:n <=m;
      (F2#x).n = (F2.n).x & (F2#x).m = (F2.m).x by Def13;
     hence (F2#x).n<= (F2#x).m by A4,A15,A18,A35,A44;
    end;
     lim(F1#x) + lim(F2#x) =f.x + lim(F2#x) by A3,A15,A18,A35;
    then lim(F1#x) + lim(F2#x) =f.x + g.x by A4,A15,A18,A35;
    then lim(F1#x) + lim(F2#x) =(f+g).x by A35,MESFUNC1:def 3;
    hence thesis by A36,A40,A41,A43,Th67;
   end;
    for n be Nat holds K.n=K1.n+K2.n
   proof
    let n be Nat;
 A45:F1.n is_simple_func_in S & F1.n is nonnegative by A3;
 A46:F2.n is_simple_func_in S & F2.n is nonnegative by A4;
 A47:F.n=F1.n+F2.n by A21;
 A48:dom(F1.n) =dom f by A3
             .= dom(F2.n) by A4,A15;
 A49:K2.n=integral'(M,F2.n) by A4;
A50: dom(F.n) = dom(F1.n) /\ dom(F2.n) by A45,A46,A47,Th71;
     K.n=integral'(M,F.n) by A23;
    then K.n=integral'(M,F1.n|dom(F1.n)) + integral'(M,F2.n|dom(F2.n))
         by A45,A46,A47,A48,A50,Th71;
    then K.n=integral'(M,F1.n) + integral'(M,F2.n|dom(F2.n)) by GRFUNC_1:64;
    then K.n=integral'(M,F1.n) + integral'(M,F2.n) by GRFUNC_1:64;
    hence K.n= K1.n +K2.n by A3,A49;
   end;
   then K is convergent & lim K=lim K1+lim K2 by A5,A6,A10,A11,Th67;
   hence thesis by A3,A4,A17,A18,A19,A23,A24,A28,A34,Def15;
end;

theorem Th84:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
 ( ex A be Element of S st A = dom f & f is_measurable_on A ) &
 ( ex B be Element of S st B = dom g & g is_measurable_on B ) &
 f is nonnegative & g is nonnegative
 holds
   ex C be Element of S st
    C = dom(f+g) &
    integral+(M,f+g) = integral+(M,f|C) + integral+(M,g|C)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL;
   assume that
A1: ( ex A be Element of S st A = dom f & f is_measurable_on A ) and
A2: ( ex B be Element of S st B = dom g & g is_measurable_on B ) and
A3: f is nonnegative & g is nonnegative;
   consider A be Element of S such that
A4: A = dom f & f is_measurable_on A by A1;
   consider B be Element of S such that
A5: B = dom g & g is_measurable_on B by A2;
   set f1 = f|(dom f /\ dom g);
   set g1 = g|(dom f /\ dom g);
   take C = A /\ B;
    f is without-infty & g is without-infty by A3,Th18;
then A6:C = dom(f+g) by A4,A5,Th22;
    dom f1 = dom f /\ (dom f /\ dom g) & dom g1 = dom g /\ (dom f /\ dom g)
     by FUNCT_1:68;
then A7:dom f1 = dom f /\ dom f /\ dom g & dom g1 = dom g /\ dom g /\ dom f
     by XBOOLE_1:16;
    C c= dom f & C c= dom g by A4,A5,XBOOLE_1:17;
   then f is_measurable_on C & C = dom f /\ C &
   g is_measurable_on C & C = dom g /\ C by A4,A5,MESFUNC1:34,XBOOLE_1:28;
then A8:f|C is_measurable_on C & g|C is_measurable_on C by Th48;
A9:f1 is nonnegative & g1 is nonnegative by A3,Th21;
then A10:integral+(M,f1+g1) = integral+(M,f1) + integral+(M,g1) by A4,A5,A7,A8,
Lm11;
    f1 is without-infty & g1 is without-infty by A9,Th18;
then A11:dom(f1+g1) = dom f1 /\ dom g1 & dom(f1+g1) = C /\ C by A4,A5,A7,Th22;
    for x be set st x in dom(f1+g1) holds (f1+g1).x = (f+g).x
   proof
    let x be set;
    assume
 A12:x in dom(f1+g1);
 then A13:x in dom f1 & x in dom g1 by A11,XBOOLE_0:def 3;
     (f1+g1).x = f1.x + g1.x by A12,MESFUNC1:def 3
             .= f.x + g1.x by A13,FUNCT_1:70
             .= f.x + g.x by A13,FUNCT_1:70;
    hence (f1+g1).x = (f+g).x by A6,A11,A12,MESFUNC1:def 3;
   end;
   hence thesis by A4,A5,A6,A10,A11,FUNCT_1:9;
end;

theorem Th85:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL st
 (ex A be Element of S st A = dom f & f is_measurable_on A) &
 f is nonnegative holds
   0 <= integral+(M,f)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL;
   assume (ex A be Element of S st A = dom f & f is_measurable_on A  )&
          f is nonnegative;
   then consider F1 be Functional_Sequence of X,ExtREAL,
            K1 be ExtREAL_sequence such that
A1:(for n be Nat holds F1.n is_simple_func_in S & dom(F1.n) = dom f) &
    (for n be Nat holds F1.n is nonnegative) &
    (for n,m be Nat st n <=m holds
       for x be Element of X st x in dom f holds (F1.n).x <= (F1.m).x ) &
    (for x be Element of X st x in dom f holds
       F1#x is convergent & lim(F1#x) = f.x) &
       (for n be Nat holds K1.n=integral'(M,F1.n)) &
    K1 is convergent &
    integral+(M,f)=lim K1 by Def15;
A2:for n be Nat holds 0 <= K1.n
   proof
    let n be Nat;
     K1.n = integral'(M,F1.n) & F1.n is_simple_func_in S &
    F1.n is nonnegative by A1;
    hence 0<= K1.n by Th74;
   end;
    for n,m be Nat st n<=m holds K1.n <= K1.m
   proof
    let n,m be Nat;
    assume A3:n<=m;
A4:F1.n is_simple_func_in S & F1.m is_simple_func_in S &
    dom(F1.n) = dom f & dom(F1.m) = dom f &
    F1.n is nonnegative & F1.m is nonnegative &
    K1.m = integral'(M,F1.m) by A1;
A5: for x be set st x in dom(F1.m - F1.n) holds (F1.n).x <= (F1.m).x
    proof
     let x be set;
     assume x in dom(F1.m - F1.n);
     then x in (dom(F1.m) /\ dom(F1.n))\
      (((F1.m)"{+infty}/\(F1.n)"{+infty})
        \/((F1.m)"{-infty}/\(F1.n)"{-infty})) by MESFUNC1:def 4;
     then x in dom(F1.m) /\ dom(F1.n) by XBOOLE_0:def 4;
     hence thesis by A1,A3,A4;
    end;
then A6: integral'(M,F1.n|dom(F1.m - F1.n)) <= integral'(M,F1.m|dom(F1.m - F1.n
))
       by A4,Th76;
     dom(F1.m - F1.n) = dom(F1.m) /\ dom(F1.n) by A4,A5,Th75;
    then F1.n|dom(F1.m - F1.n) = F1.n & F1.m|dom(F1.m - F1.n) = F1.m
      by A4,GRFUNC_1:64;
    hence K1.n <= K1.m by A1,A4,A6;
   end;
   then lim K1 =sup rng K1 by Th60;
   then K1.0 <= lim K1 by Th62;
   hence thesis by A1,A2;
end;

theorem Th86:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    A be Element of S st
  (ex E be Element of S st E = dom f & f is_measurable_on E ) &
  f is nonnegative holds
    0<= integral+(M,f|A)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       A be Element of S;
   assume
A1: (ex E be Element of S st E = dom f & f is_measurable_on E) &
    f is nonnegative;
   then consider E be Element of S such that
A2: E = dom f & f is_measurable_on E;
   set C = E/\A;
A3:C c= E by XBOOLE_1:17;
then A4:f is_measurable_on C by A2,MESFUNC1:34;
A5:C = dom(f|A) by A2,FUNCT_1:68;
    dom f /\ C = C by A2,A3,XBOOLE_1:28;
then A6:f|C is_measurable_on C by A4,Th48;
A7:dom(f|A) = C by A2,FUNCT_1:68 .= dom f /\ C by A2,A3,XBOOLE_1:28
     .= dom(f|C) by FUNCT_1:68;
    for x be set st x in dom(f|A) holds (f|A).x = (f|C).x
   proof
    let x be set;
    assume A8:x in dom(f|A);
    then (f|A).x = f.x by FUNCT_1:70;
    hence thesis by A7,A8,FUNCT_1:70;
   end;
then A9:f|A is_measurable_on C by A6,A7,FUNCT_1:9;
    f|A is nonnegative by A1,Th21;
   hence thesis by A5,A9,Th85;
end;

theorem Th87:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    A,B be Element of S st
  (ex E be Element of S st E = dom f & f is_measurable_on E) &
  f is nonnegative & A misses B
 holds
    integral+(M,f|(A\/B)) = integral+(M,f|A)+integral+(M,f|B)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       A,B be Element of S;
   assume
A1: (ex E be Element of S st E = dom f & f is_measurable_on E) &
    f is nonnegative & A misses B;
   then consider E be Element of S such that
A2: E = dom f and
A3: f is_measurable_on E;
   set C = E/\(A\/B);
A4:dom(f|(A\/B)) = C by A2,FUNCT_1:68;
A5:C c= dom f by A2,XBOOLE_1:17;
then A6:C = dom f /\ C by XBOOLE_1:28;
then A7:dom(f|(A\/B)) = dom(f|C) by A4,FUNCT_1:68;
    for x be set st x in dom(f|(A\/B)) holds f|(A\/B).x = f|C.x
   proof
    let x be set;
    assume
A8:  x in dom(f|(A\/B));
    then f|(A\/B).x = f.x by FUNCT_1:70;
    hence thesis by A7,A8,FUNCT_1:70;
   end;
then A9:f|(A\/B) = f|C by A7,FUNCT_1:9;
    f is_measurable_on C by A2,A3,A5,MESFUNC1:34;
then A10:f|(A\/B) is_measurable_on C by A6,A9,Th48;
A11:f|(A\/B) is nonnegative by A1,Th21;
   consider F0 be Functional_Sequence of X,ExtREAL,
            K0 be ExtREAL_sequence such that
A12:(for n be Nat holds F0.n is_simple_func_in S & dom(F0.n) = dom f) &
    (for n be Nat holds F0.n is nonnegative) &
    (for n,m be Nat st n <=m holds
      for x be Element of X st x in dom f holds (F0.n).x <= (F0.m).x ) &
    (for x be Element of X st  x in dom f holds
      F0#x is convergent & lim(F0#x) = f.x) &
    (for n be Nat holds K0.n=integral'(M,F0.n)) &
    K0 is convergent &
    integral+(M,f)=lim K0 by A1,Def15;
   deffunc PFAB(Nat) = (F0.$1)|(A\/B);
   consider FAB be Functional_Sequence of X,ExtREAL such that
A13:for n be Nat holds FAB.n=PFAB(n) from SEQFUNC:sch 1;
   deffunc PK(Nat) = integral'(M,FAB.$1);
   consider KAB be ExtREAL_sequence such that
A14:for n be Element of NAT holds KAB.n = PK(n) from FUNCT_2:sch 4;
A15:now let n be Nat;  n in NAT by ORDINAL1:def 13;
       hence KAB.n = PK(n) by A14;
     end;
A16:(for n be Nat holds FAB.n is_simple_func_in S &
      dom(FAB.n) = dom(f|(A\/B))) &
   (for n be Nat holds for x be Element of X st x in dom(f|(A\/B))
        holds (FAB.n).x = (F0.n).x) &
   (for n be Nat holds FAB.n is nonnegative) &
   (for n,m be Nat st n <=m holds
      for x be Element of X st x in dom(f|(A\/B))
          holds (FAB.n).x <= (FAB.m).x ) &
   (for x be Element of X st  x in dom(f|(A\/B)) holds
        FAB#x is convergent & lim(FAB#x) = f|(A\/B).x)
   proof
    thus
A17: now let n be Nat;
A18:  FAB.n=(F0.n)|(A \/ B) by A13;
      F0.n is_simple_func_in S by A12;
     hence FAB.n is_simple_func_in S by A18,Th40;
     thus dom(FAB.n) = dom(F0.n|(A\/B))  by A13
                    .= dom(F0.n) /\ (A \/ B) by FUNCT_1:68
                    .= dom f /\ (A\/B) by A12
                    .= dom(f|(A\/B)) by FUNCT_1:68;
    end;
    thus
A19: now let n be Nat, x be Element of X;
     assume
       x in dom(f|(A\/B));
  then A20:x in dom(FAB.n) by A17;
   FAB.n=F0.n|(A \/ B) by A13;
     hence (FAB.n).x = (F0.n).x by A20,FUNCT_1:68;
    end;
    hereby let n be Nat;
      reconsider n1=n as Element of NAT by ORDINAL1:def 13;
      F0.n is nonnegative by A12;
     then F0.n1|(A\/B) is nonnegative by Th21;
     hence FAB.n is nonnegative by A13;
    end;
    hereby let n ,m be Nat such that
  A21: n <= m;
      now let x be Element of X;
      assume
   A22: x in dom(f|(A\/B));
      then x in dom f /\ (A\/B) by FUNCT_1:68;
  then A23:x in dom f by XBOOLE_0:def 3;
       (FAB.n).x = (F0.n).x & (FAB.m).x = (F0.m).x by A19,A22;
      hence (FAB.n).x <= (FAB.m).x by A12,A21,A23;
     end;
     hence for x be Element of X st x in dom(f|(A\/B)) holds (FAB.n).x <= (FAB.
m).x;
    end;
    hereby let x be Element of X;
     assume
 A24: x in dom(f|(A\/B));
     then x in dom f /\ (A\/B) by FUNCT_1:68;
 then A25:x in dom f by XBOOLE_0:def 3;
 A26:now let n be Element of NAT;
      thus (FAB#x).n = (FAB.n).x by Def13
                    .=(F0.n).x by A19,A24
                    .=(F0#x).n by Def13;
     end;
     then FAB#x=F0#x by FUNCT_2:113;
     hence FAB#x is convergent by A12,A25;
     thus lim(FAB#x) = lim(F0#x) by A26,FUNCT_2:113
                    .= f.x by A12,A25
                    .= f|(A\/B).x by A24,FUNCT_1:68;
    end;
   end;
    for n,m be Nat st n<=m holds KAB.n <= KAB.m
   proof
    let n,m be Nat;
    assume A27:n<=m;
    reconsider n,m as Element of NAT by ORDINAL1:def 13;
A28:FAB.n is_simple_func_in S & FAB.m is_simple_func_in S &
    dom(FAB.n) = dom (f|(A\/B)) & dom(FAB.m) = dom(f|(A\/B)) &
    FAB.n is nonnegative & FAB.m is nonnegative by A16;
A29:KAB.m = integral'(M,FAB.m) by A15;
A30: for x be set st x in dom(FAB.m - FAB.n) holds (FAB.n).x <= (FAB.m).x
    proof
     let x be set;
     assume x in dom(FAB.m - FAB.n);
     then x in (dom(FAB.m) /\ dom(FAB.n)) \ ((FAB.m)"{+infty}/\(FAB.n)"{+infty
}
         \/(FAB.m)"{-infty}/\(FAB.n)"{-infty}) by MESFUNC1:def 4;
     then x in dom(FAB.m) /\ dom(FAB.n) by XBOOLE_0:def 4;
     hence thesis by A16,A27,A28;
    end;
then A31: integral'(M,FAB.n|dom(FAB.m - FAB.n))
      <= integral'(M,FAB.m|dom(FAB.m - FAB.n)) by A28,Th76;
     dom(FAB.m - FAB.n) = dom(FAB.m) /\ dom(FAB.n) by A28,A30,Th75;
    then FAB.m|dom(FAB.m - FAB.n) = FAB.m &
    FAB.n|dom(FAB.m - FAB.n) = FAB.n by A28,GRFUNC_1:64;
    hence thesis by A15,A29,A31;
   end;
   then KAB is convergent by Th60;
then A32:integral+(M,f|(A\/B)) = lim KAB by A4,A10,A11,A15,A16,Def15;
   set DA = E /\ A;
   set DB = E /\ B;
    DA c= E & DB c= E by XBOOLE_1:17;
then A33:f is_measurable_on DA & f is_measurable_on DB by A3,MESFUNC1:34;
A34:DA = dom(f|A) & DB = dom(f|B) by A2,FUNCT_1:68;
   then DA c= dom f & DB c= dom f by RELAT_1:89;
then A35:dom f /\ DA = DA & dom f /\ DB  = DB by XBOOLE_1:28;
then A36:dom(f|DA) = dom(f|A) & dom(f|DB) = dom(f|B) by A34,RELAT_1:90;
    for x be set st x in dom(f|DA) holds (f|DA).x = (f|A).x
   proof
    let x be set;
    assume A37:x in dom(f|DA);
    then f|A.x = f.x by A36,FUNCT_1:70;
    hence thesis by A37,FUNCT_1:70;
   end;
   then f|DA = f|A by A36,FUNCT_1:9;
then A38:f|A is_measurable_on DA by A33,A35,Th48;
    for x be set st x in dom(f|DB) holds (f|DB).x = (f|B).x
   proof
    let x be set;
    assume A39:x in dom(f|DB);
    then f|B.x = f.x by A36,FUNCT_1:70;
    hence thesis by A39,FUNCT_1:70;
   end;
   then f|DB = f|B by A36,FUNCT_1:9;
then A40:f|B is_measurable_on DB by A33,A35,Th48;
A41:f|A is nonnegative & f|B is nonnegative by A1,Th21;
   deffunc PFA(Nat) = F0.$1|A;
   consider FA be Functional_Sequence of X,ExtREAL such that
A42:for n be Nat holds FA.n=PFA(n) from SEQFUNC:sch 1;
   deffunc PFB(Nat) = F0.$1|B;
   consider FB be Functional_Sequence of X,ExtREAL such that
A43:for n be Nat holds FB.n=PFB(n) from SEQFUNC:sch 1;
A44:for n be Nat holds
    FA.n is_simple_func_in S & FB.n is_simple_func_in S &
    dom(FA.n) = dom(f|A) & dom(FB.n) = dom(f|B)
   proof
    let n be Nat;
    reconsider n1=n as Element of NAT by ORDINAL1:def 13;
 A45:F0.n is_simple_func_in S & F0.n is nonnegative by A12;
 A46:FA.n1 = F0.n1|A & FB.n1=F0.n1|B by A42,A43;
    hence FA.n is_simple_func_in S & FB.n is_simple_func_in S by A45,Th40;
     dom(FA.n)=dom(F0.n) /\ A & dom(FB.n) = dom(F0.n) /\ B by A46,FUNCT_1:68;
    hence dom(FA.n) = dom(f|A) & dom(FB.n) = dom(f|B) by A2,A12,A34;
   end;
A47:for n be Nat holds FA.n is nonnegative & FB.n is nonnegative
   proof
    let n be Nat;
    reconsider n as Element of NAT by ORDINAL1:def 13;
     F0.n is nonnegative by A12;
    then F0.n|A is nonnegative & F0.n|B is nonnegative by Th21;
    hence thesis by A42,A43;
   end;
A48:for n,m be Nat st n <=m holds
     for x be Element of X st x in dom(f|A) holds (FA.n).x <= (FA.m).x
    proof
     let n,m be Nat;
     assume A49:n<=m;
     let x be Element of X;
     assume A50:x in dom(f|A);
     then x in dom f /\ A by FUNCT_1:68;
then A51:  x in dom f by XBOOLE_0:def 3;
     reconsider n,m as Element of NAT by ORDINAL1:def 13;
      dom(F0.n|A) = dom(FA.n) & dom(F0.m|A) = dom(FA.m) by A42;
then A52:  dom(F0.n|A) = dom(f|A) & dom(F0.m|A) = dom(f|A) by A44;
      (FA.n).x =(F0.n|A).x & (FA.m).x =(F0.m|A).x by A42;
     then (FA.n).x =(F0.n).x & (FA.m).x = (F0.m).x by A50,A52,FUNCT_1:68;
     hence thesis by A12,A49,A51;
    end;
A53:for n,m be Nat st n <=m holds
     for x be Element of X st x in dom(f|B) holds (FB.n).x <= (FB.m).x
    proof
     let n,m be Nat;
     assume A54:n<=m;
     let x be Element of X;
     assume A55:x in dom(f|B);
     then x in dom f /\ B by FUNCT_1:68;
then A56:  x in dom f by XBOOLE_0:def 3;
     reconsider n,m as Element of NAT by ORDINAL1:def 13;
      dom(F0.n|B) = dom(FB.n) & dom(F0.m|B) = dom(FB.m) by A43;
then A57:  dom(F0.n|B) = dom(f|B) & dom(F0.m|B) = dom(f|B) by A44;
      (FB.n).x =(F0.n|B).x & (FB.m).x =(F0.m|B).x by A43;
     then (FB.n).x =(F0.n).x & (FB.m).x = (F0.m).x by A55,A57,FUNCT_1:68;
     hence thesis by A12,A54,A56;
    end;
A58:for x be Element of X st x in dom(f|A) holds
     FA#x is convergent & lim(FA#x) = f|A.x
    proof
     let x be Element of X;
     assume A59:x in dom(f|A);
     then x in dom f /\ A by FUNCT_1:68;
then A60:  x in dom f by XBOOLE_0:def 3;
      now let n be Element of NAT;
 A61: dom(F0.n|A) = dom (FA.n) by A42 .=dom(f|A) by A44;
       (FA#x).n = (FA.n).x by Def13;
      then (FA#x).n = (F0.n|A).x by A42;
      then (FA#x).n = (F0.n).x by A59,A61,FUNCT_1:68;
      hence (FA#x).n = (F0#x).n by Def13;
     end;
then A62:  FA#x = F0#x by FUNCT_2:113;
      F0#x is convergent & lim(F0#x)=f.x by A12,A60;
     hence thesis by A59,A62,FUNCT_1:68;
    end;
A63:for x be Element of X st x in dom(f|B) holds
     FB#x is convergent & lim(FB#x) = f|B.x
    proof
     let x be Element of X;
     assume A64:x in dom(f|B);
     then x in dom f /\ B by FUNCT_1:68;
then A65:  x in dom f by XBOOLE_0:def 3;
      now let n be Element of NAT;
 A66: dom(F0.n|B) = dom(FB.n) by A43 .=dom(f|B) by A44;
      thus (FB#x).n = (FB.n).x by Def13
                   .=(F0.n|B).x by A43
                   .=(F0.n).x by A64,A66,FUNCT_1:68
                   .=(F0#x).n by Def13;
     end;
then A67:  FB#x=F0#x by FUNCT_2:113;
      F0#x is convergent & lim(F0#x)=f.x by A12,A65;
     hence thesis by A64,A67,FUNCT_1:68;
    end;
    deffunc PKA(Nat) = integral'(M,FA.$1);
    consider KA be ExtREAL_sequence such that
A68:for n be Element of NAT holds KA.n = PKA(n) from FUNCT_2:sch 4;
A69:now let n be Nat;  n in NAT by ORDINAL1:def 13;
       hence KA.n = PKA(n) by A68;
     end;
    deffunc PKB(Nat) = integral'(M,FB.$1);
    consider KB be ExtREAL_sequence such that
A70:for n be Element of NAT holds KB.n = PKB(n) from FUNCT_2:sch 4;
A71:now let n be Nat;  n in NAT by ORDINAL1:def 13;
       hence KB.n = PKB(n) by A70;
     end;
     for n,m be Nat st n<=m holds KA.n <= KA.m & KB.n <= KB.m
    proof
     let n,m be Nat;
     assume A72:n<=m;
A73: FA.n is_simple_func_in S & FA.m is_simple_func_in S &
     FB.n is_simple_func_in S & FB.m is_simple_func_in S &
     dom(FA.n) = dom(f|A) & dom(FA.m) = dom(f|A) &
     dom(FB.n) = dom(f|B) & dom(FB.m) = dom(f|B) by A44;
A74: FA.n is nonnegative & FA.m is nonnegative &
     FB.n is nonnegative & FB.m is nonnegative by A47;
A75: KA.m = integral'(M,FA.m) & KB.m = integral'(M,FB.m) by A69,A71;
A76:  for x be set st x in dom(FA.m - FA.n) holds (FA.n).x <= (FA.m).x
     proof
      let x be set;
      assume x in dom(FA.m - FA.n);
      then x in (dom(FA.m) /\ dom(FA.n))
       \( ((FA.m)"{+infty}/\(FA.n)"{+infty})
        \/((FA.m)"{-infty}/\(FA.n)"{-infty}) ) by MESFUNC1:def 4;
      then x in dom(FA.m) /\ dom(FA.n) by XBOOLE_0:def 4;
      hence thesis by A48,A72,A73;
     end;
then A77:  integral'(M,FA.n|dom(FA.m - FA.n)) <= integral'(M,FA.m|dom(FA.m - FA
.n))
       by A73,A74,Th76;
      dom(FA.m - FA.n) = dom(FA.m) /\ dom(FA.n) by A73,A74,A76,Th75;
     then FA.m|dom(FA.m - FA.n) = FA.m & FA.n|dom(FA.m - FA.n) = FA.n
       by A73,GRFUNC_1:64;
     hence KA.n <= KA.m by A69,A75,A77;
A78:  for x be set st x in dom(FB.m - FB.n) holds (FB.n).x <= (FB.m).x
     proof
      let x be set;
      assume x in dom(FB.m - FB.n);
      then x in (dom(FB.m) /\ dom(FB.n))
       \( ((FB.m)"{+infty}/\(FB.n)"{+infty})
        \/((FB.m)"{-infty}/\(FB.n)"{-infty}) ) by MESFUNC1:def 4;
      then x in dom(FB.m) /\ dom(FB.n) by XBOOLE_0:def 4;
      hence thesis by A53,A72,A73;
     end;
then A79:  integral'(M,FB.n|dom(FB.m - FB.n)) <= integral'(M,FB.m|dom(FB.m - FB
.n))
       by A73,A74,Th76;
      dom(FB.m - FB.n) = dom(FB.m) /\ dom(FB.n) by A73,A74,A78,Th75;
     then FB.m|dom(FB.m - FB.n) = FB.m & FB.n|dom(FB.m - FB.n) = FB.n
       by A73,GRFUNC_1:64;
     hence thesis by A71,A75,A79;
    end;
then A80: (for n,m be Nat st n<=m holds KA.n <= KA.m) &
    (for n,m be Nat st n<=m holds KB.n <= KB.m);
    then KA is convergent & KB is convergent by Th60;
then A81: integral+(M,f|A) =lim KA & integral+(M,f|B) =lim KB
     by A34,A38,A40,A41,A44,A47,A48,A53,A58,A63,A69,A71,Def15;
A82: for n be Nat holds KAB.n=KA.n + KB.n
    proof
     let n be Nat;
     reconsider n as Element of NAT by ORDINAL1:def 13;
A83:  F0.n is_simple_func_in S & F0.n is nonnegative by A12;
      FA.n=F0.n|A & FB.n=F0.n|B by A42,A43;
then A84:  integral'(M,F0.n|(A\/B)) = integral'(M,FA.n)+integral'(M,FB.n)
       by A1,A83,Th73;
A85:  KAB.n =integral'(M,FAB.n) by A15
          .=integral'(M,F0.n|(A\/B)) by A13;
      KA.n = integral'(M,FA.n) by A69;
     hence thesis by A71,A84,A85;
    end;
     for n be set holds
     (n in dom KA implies -infty < KA.n) & (n in dom KB implies -infty < KB.n)
    proof
     let n be set;
     hereby assume n in dom KA;
      then reconsider n1 = n as Element of NAT;
 A86: KA.n1 = integral'(M,FA.n1) & FA.n1 is_simple_func_in S by A44,A69;
       FA.n1 is nonnegative by A47;
      hence -infty < KA.n by A86,Th74;
     end;
     assume n in dom KB;
     then reconsider n1 = n as Element of NAT;
 A87:KB.n1 = integral'(M,FB.n1) & FB.n1 is_simple_func_in S by A44,A71;
      FB.n1 is nonnegative by A47;
     hence -infty < KB.n by A87,Th74;
    end;
    then KA is without-infty & KB is without-infty by Th16;
    hence thesis by A32,A80,A81,A82,Th67;
end;

theorem Th88:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    A be Element of S st
 (ex E be Element of S st E = dom f & f is_measurable_on E ) &
 f is nonnegative & M.A = 0 holds
   integral+(M,f|A) = 0
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       A be Element of S;
   assume
A1: (ex E be Element of S st E = dom f & f is_measurable_on E ) &
    f is nonnegative & M.A = 0;
   then consider E be Element of S such that
A2:E = dom f & f is_measurable_on E;
   set C = E/\A;
A3:C c= E by XBOOLE_1:17;
then A4:f is_measurable_on C by A2,MESFUNC1:34;

A5:dom f /\ C = C by A2,A3,XBOOLE_1:28;
   then dom(f|C) = C by FUNCT_1:68;
then A6:dom(f|C) = dom(f|A) by A2,FUNCT_1:68;
    for x be set st x in dom(f|A) holds f|A.x = f|C.x
   proof
    let x be set;
    assume A7:x in dom(f|A);
    then (f|A).x = f.x by FUNCT_1:70;
    hence thesis by A6,A7,FUNCT_1:70;
   end;
   then f|A = f|C by A6,FUNCT_1:9;
then A8:f|A is_measurable_on C by A4,A5,Th48;
   consider F0 be Functional_Sequence of X,ExtREAL,
            K0 be ExtREAL_sequence such that
A9: for n be Nat holds F0.n is_simple_func_in S & dom(F0.n) = dom f and
A10: for n be Nat holds F0.n is nonnegative and
A11: for n,m be Nat st n <=m holds
      for x be Element of X st x in dom f holds (F0.n).x <= (F0.m).x and
A12: for x be Element of X st x in dom f holds
      F0#x is convergent & lim(F0#x) = f.x and
     (for n be Nat holds K0.n=integral'(M,F0.n)) &
    K0 is convergent &
    integral+(M,f)=lim K0 by A1,Def15;
   deffunc PFA(Nat) = (F0.$1)|A;
   consider FA be Functional_Sequence of X,ExtREAL such that
A13:for n be Nat holds FA.n=PFA(n) from SEQFUNC:sch 1;
   deffunc PK(Nat) = integral'(M,FA.$1);
   consider KA be ExtREAL_sequence such that
A14:for n be Element of NAT holds KA.n = PK(n) from FUNCT_2:sch 4;
A15:now let n be Nat;  n in NAT by ORDINAL1:def 13;
      hence KA.n = PK(n) by A14;
    end;
A16:C = dom(f|A) by A2,FUNCT_1:68;
A17:f|A is nonnegative by A1,Th21;
A18:for n be Nat holds FA.n is_simple_func_in S & dom(FA.n) = dom(f|A)
   proof
    let n be Nat;
    reconsider n1=n as Element of NAT by ORDINAL1:def 13;
     F0.n1 is_simple_func_in S & FA.n1=F0.n1|A by A9,A13;
    hence FA.n is_simple_func_in S by Th40;
     dom(FA.n1)=dom(F0.n1|A) by A13;
    then dom(FA.n)=dom(F0.n) /\ A by FUNCT_1:68;
    hence thesis by A2,A9,A16;
   end;
A19:for n be Nat holds FA.n is nonnegative
   proof
    let n be Nat;
    reconsider n as Element of NAT by ORDINAL1:def 13;
     F0.n is nonnegative by A10;
    then F0.n|A is nonnegative by Th21;
    hence thesis by A13;
   end;
A20:for n,m be Nat st n <=m holds
     for x be Element of X st x in dom(f|A) holds (FA.n).x <= (FA.m).x
   proof
    let n,m be Nat;
    assume A21:n<=m;
    reconsider n,m as Element of NAT by ORDINAL1:def 13;
    let x be Element of X;
    assume A22:x in dom(f|A);
    then x in dom f /\ A by FUNCT_1:68;
then A23: x in dom f by XBOOLE_0:def 3;
     dom(F0.n|A) = dom(FA.n) & dom(F0.m|A) = dom(FA.m) by A13;
then A24: dom(F0.n|A) = dom(f|A) & dom(F0.m|A) = dom(f|A) by A18;
     (FA.n).x =(F0.n|A).x & (FA.m).x =(F0.m|A).x by A13;
    then (FA.n).x = (F0.n).x & (FA.m).x = (F0.m).x by A22,A24,FUNCT_1:68;
    hence thesis by A11,A21,A23;
   end;
A25:for x be Element of X st x in dom(f|A) holds
     FA#x is convergent & lim(FA#x) = f|A.x
   proof
    let x be Element of X;
    assume A26:x in dom(f|A);
    then x in dom f /\ A by FUNCT_1:68;
    then x in dom f by XBOOLE_0:def 3;
then A27: F0#x is convergent & lim(F0#x)=f.x by A12;
     now let n be Element of NAT;
A28: dom(F0.n|A) = dom(FA.n) by A13 .=dom(f|A) by A18;
     thus (FA#x).n = (FA.n).x by Def13
                  .=(F0.n|A).x by A13
                  .=(F0.n).x by A26,A28,FUNCT_1:68
                  .=(F0#x).n by Def13;
    end;
    then FA#x = F0#x by FUNCT_2:113;
    hence thesis by A26,A27,FUNCT_1:68;
   end;
    for n be Nat holds KA.n =R_EAL 0
   proof
    let n be Nat;
    reconsider n as Element of NAT by ORDINAL1:def 13;
     F0.n is_simple_func_in S & F0.n is nonnegative by A9,A10;
    then integral'(M,F0.n|A) = R_EAL 0 by A1,Th79;
    then integral'(M,FA.n) = R_EAL 0 by A13;
    hence thesis by A15;
   end;
   then KA is convergent & lim KA = R_EAL 0 by Th66;
   hence thesis by A8,A15,A16,A17,A18,A19,A20,A25,Def15;
end;

theorem Th89:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    A,B be Element of S st
  (ex E be Element of S st E = dom f & f is_measurable_on E ) &
  f is nonnegative & A c= B holds
     integral+(M,f|A) <= integral+(M,f|B)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       A,B be Element of S;
   assume
A1:(ex E be Element of S st E = dom f & f is_measurable_on E ) &
   f is nonnegative & A c= B;
   set A' = A /\ B;
   set B' = B \ A;
A' misses B' by XBOOLE_1:89;
then A2:0<= integral+(M,f|(A'\/B')) & 0<= integral+(M,f|A') &
   0<= integral+(M,f|B') &
   integral+(M,f|(A'\/B')) =integral+(M,f|A')+integral+(M,f|B')
     by A1,Th86,Th87;
    A' = A & A'\/B' = B by A1,XBOOLE_1:28,51;
   hence thesis by A2,SUPINF_2:20;
end;

theorem Th90:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    E,A be Element of S st
 f is nonnegative & E = dom f & f is_measurable_on E & M.A =0 holds
  integral+(M,f|(E\A)) = integral+(M,f)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       E,A be Element of S such that
A1:f is nonnegative & E = dom f & f is_measurable_on E & M.A =0;
   set B = E\A;
    A misses B by XBOOLE_1:79;
then A2:integral+(M,f|(A\/B))
      =integral+(M,f|A)+integral+(M,f|B) by A1,Th87;
    f|(A\/B) =f
   proof
     A \/ B = A \/ E by XBOOLE_1:39;
    then E c= A \/ B by XBOOLE_1:7;
then A3: dom f = dom f /\ (A\/B) by A1,XBOOLE_1:28 .= dom(f|(A\/B)) by
FUNCT_1:68;
     for x be set st x in dom(f|(A\/B)) holds (f|(A\/B)).x = f.x by FUNCT_1:70;
    hence thesis by A3,FUNCT_1:9;
   end;
   then integral+(M,f) = 0.+ integral+(M,f|B) by A1,A2,Th88;
   hence thesis by SUPINF_2:18;
end;

theorem Th91:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
 (ex E be Element of S st E = dom f & E= dom g &
   f is_measurable_on E & g is_measurable_on E) &
 f is nonnegative & g is nonnegative &
 (for x be Element of X st x in dom g holds g.x <= f.x)
holds
  integral+(M,g) <= integral+(M,f)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL such that
A1:(ex A be Element of S st A = dom f & A= dom g
      & f is_measurable_on A & g is_measurable_on A) &
   f is nonnegative & g is nonnegative &
   (for x be Element of X st x in dom g holds g.x <= f.x);
   consider A be Element of S such that
A2: A = dom f & A= dom g & f is_measurable_on A & g is_measurable_on A by A1;
   consider F be Functional_Sequence of X,ExtREAL,
            K be ExtREAL_sequence such that
A3: (for n be Nat holds F.n is_simple_func_in S & dom(F.n) = dom f) &
    (for n be Nat holds F.n is nonnegative) &
    (for n,m be Nat st n <=m holds
      for x be Element of X st x in dom f holds (F.n).x <= (F.m).x ) &
    (for x be Element of X st x in dom f holds
      (F#x) is convergent & lim(F#x) = f.x) &
    (for n be Nat holds K.n=integral'(M,F.n)) &
    K is convergent &
    integral+(M,f)=lim(K) by A1,Def15;
   consider G be Functional_Sequence of X,ExtREAL,
            L be ExtREAL_sequence such that
A4: (for n be Nat holds G.n is_simple_func_in S & dom(G.n) = dom g) &
    (for n be Nat holds G.n is nonnegative) &
    (for n,m be Nat st n <=m holds
      for x be Element of X st x in dom g holds (G.n).x <= (G.m).x ) &
    (for x be Element of X st  x in dom g holds
      G#x is convergent & lim(G#x) = g.x) &
    (for n be Nat holds L.n=integral'(M,G.n)) &
    L is convergent & integral+(M,g)=lim L by A1,Def15;
A5:for x be Element of X st x in A holds lim(G#x)=sup rng(G#x)
   proof
    let x be Element of X;
    assume A6:x in A;
     now let n,m be Nat;
     assume A7: n<=m;
      (G#x).n=(G.n).x & (G#x).m=(G.m).x by Def13;
     hence (G#x).n <= (G#x).m by A2,A4,A6,A7;
    end;
    hence lim(G#x)=sup rng(G#x) by Th60;
   end;
A8:for n0 be Nat holds K is convergent & sup rng K = lim K & L.n0 <= lim K
   proof
    let n0 be Nat;
    set gg = G.n0;
A9: dom gg = A & gg is_simple_func_in S & gg is nonnegative by A2,A4;
     for x be Element of X st x in dom gg holds
     F#x is convergent & gg.x <= lim(F#x)
    proof
     let x be Element of X such that
 A10: x in dom gg;
      gg.x =(G#x).n0 & (G#x).n0 <= sup rng (G#x) by Def13,Th62;
     then gg.x <= lim(G#x) & g.x <= f.x by A1,A2,A5,A9,A10;
     then gg.x <= g.x & g.x <= lim(F#x) by A2,A3,A4,A9,A10;
     hence thesis by A2,A3,A9,A10,XXREAL_0:2;
    end;
    then consider GG be ExtREAL_sequence such that
A11:  (for n be Nat holds GG.n = integral'(M,F.n)) &
     GG is convergent & sup rng GG =lim GG &
     integral'(M,gg) <= lim GG by A2,A3,A9,Th81;
     now let n be Element of NAT;
      GG.n = integral'(M,F.n) by A11;
     hence GG.n = K.n by A3;
    end;
    then GG=K by FUNCT_2:113;
    hence K is convergent & sup rng K =lim K & L.n0 <= lim K by A4,A11;
   end;
    for n0 be Nat holds L is convergent & sup rng L=lim L
   proof
    let n0 be Nat;
    set ff = G.n0;
A12: dom ff = A & ff is_simple_func_in S & ff is nonnegative by A2,A4;
     for x be Element of X st x in dom ff holds
     G#x is convergent & ff.x <= lim(G#x)
    proof
     let x be Element of X such that
 A13: x in dom ff;
      ff.x =(G#x).n0 & (G#x).n0 <= sup rng (G#x) by Def13,Th62;
     hence thesis by A2,A4,A5,A12,A13;
    end;
    then consider FF be ExtREAL_sequence such that
A14:  (for n be Nat holds FF.n = integral'(M,G.n)) &
     FF is convergent & sup rng FF = lim FF &
     integral'(M,ff) <= lim FF by A2,A4,A12,Th81;
     now let n be Element of NAT;
     thus FF.n = integral'(M,G.n) by A14 .=L.n by A4;
    end;
    then FF=L by FUNCT_2:113;
    hence L is convergent & sup rng L=lim L by A14;
   end;
   hence thesis by A3,A4,A8,Th63;
end;

theorem Th92:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    c be Real st
  0 <= c &
  (ex A be Element of S st A = dom f & f is_measurable_on A) &
  f is nonnegative holds
   integral+(M,c(#)f) = R_EAL c * integral+(M,f)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       c be Real such that
A1:0 <= c & (ex A be Element of S st A = dom f & f is_measurable_on A)&
   f is nonnegative;
   consider F1 be Functional_Sequence of X,ExtREAL,
            K1 be ExtREAL_sequence such that
A2: (for n be Nat holds F1.n is_simple_func_in S & dom(F1.n) = dom f) &
    (for n be Nat holds F1.n is nonnegative) &
    (for n,m be Nat st n <=m holds
         for x be Element of X st x in dom f holds (F1.n).x <= (F1.m).x ) &
    (for x be Element of X st x in dom f holds
         F1#x is convergent & lim(F1#x) = f.x) &
    (for n be Nat holds K1.n=integral'(M,F1.n)) &
    K1 is convergent & integral+(M,f)=lim K1 by A1,Def15;
    now let n1 be set;
    assume n1 in dom K1;
    then reconsider n = n1 as Element of NAT;
     K1.n = integral'(M,F1.n) & F1.n is_simple_func_in S &
    F1.n is nonnegative by A2;
    hence -infty < K1.n1 by Th74;
   end;
then A3:K1 is without-infty by Th16;
A4:for n,m be Nat st n<=m holds K1.n <= K1.m
   proof
    let n,m be Nat;
    assume A5:n<=m;
A6:K1.n = integral'(M,F1.n) & K1.m = integral'(M,F1.m) &
    F1.n is_simple_func_in S & F1.m is_simple_func_in S &
    dom(F1.n) = dom f & dom(F1.m) = dom f &
    F1.n is nonnegative & F1.m is nonnegative by A2;
 A7:for x be set st x in dom(F1.m - F1.n) holds (F1.n).x <= (F1.m).x
    proof
     let x be set;
     assume x in dom(F1.m - F1.n);
     then x in (dom(F1.m) /\ dom(F1.n)
       \ (((F1.m)"{+infty}/\(F1.n)"{+infty})
         \/((F1.m)"{-infty}/\(F1.n)"{-infty}))) by MESFUNC1:def 4;
     then x in dom(F1.m) /\ dom(F1.n) by XBOOLE_0:def 4;
     hence (F1.n).x <= (F1.m).x by A2,A5,A6;
    end;
    then dom(F1.m - F1.n) = dom(F1.m)/\dom(F1.n) by A6,Th75;
    then F1.n|dom(F1.m - F1.n) = F1.n &
    F1.m|dom(F1.m - F1.n) = F1.m by A6,GRFUNC_1:64;
    hence K1.n <= K1.m by A6,A7,Th76;
   end;
   consider A be Element of S such that
A8:A = dom f & f is_measurable_on A by A1;
A9:A = dom(c(#)f) & c(#)f is_measurable_on A by A8,MESFUNC1:41,def 6;
A10:c(#)f is nonnegative by A1,Th26;
   deffunc PF(Nat) = c(#)(F1.$1);
   consider F be Functional_Sequence of X,ExtREAL such that
A11: for n be Nat holds F.n=PF(n) from SEQFUNC:sch 1;
   deffunc PK(Nat) = integral'(M,F.$1);
   consider K be ExtREAL_sequence such that
A12: for n be Element of NAT holds K.n = PK(n) from FUNCT_2:sch 4;
A13: now let n be Nat;  n in NAT by ORDINAL1:def 13;
      hence K.n = PK(n) by A12;
    end;
A14:for n be Nat holds F.n is_simple_func_in S & dom(F.n) = dom(c(#)f)
   proof
    let n be Nat;
    reconsider n1=n as Element of NAT by ORDINAL1:def 13;
 A15:F1.n is_simple_func_in S & F1.n is nonnegative by A2;
 A16:F.n1=c(#)(F1.n1) by A11;
    hence F.n is_simple_func_in S by A15,Th45;
    thus dom(F.n) = dom(F1.n) by A16,MESFUNC1:def 6
                 .=A by A2,A8
                 .=dom(c(#)f) by A8,MESFUNC1:def 6;
   end;
A17:for n be Nat holds F.n is nonnegative
   proof
    let n be Nat;
    reconsider n as Element of NAT by ORDINAL1:def 13;
     F1.n is nonnegative by A2;
    then c(#)(F1.n) is nonnegative by A1,Th26;
    hence thesis by A11;
   end;
A18:for n,m be Nat st n <=m holds
    for x be Element of X st x in dom (c(#)f) holds (F.n).x <= (F.m).x
   proof
    let n,m be Nat;
    assume A19:n<=m;
    let x be Element of X;
    assume A20:x in dom(c(#)f);
    reconsider n,m as Element of NAT by ORDINAL1:def 13;
     dom(c(#)(F1.n)) = dom(F.n) & dom(c(#)(F1.m)) = dom(F.m) by A11;
then A21: dom(c(#)(F1.n)) = dom(c(#)f) & dom(c(#)(F1.m)) = dom(c(#)f) by A14;
     (F.n).x =(c(#)(F1.n)).x & (F.m).x =(c(#)(F1.m)).x by A11;
then A22: (F.n).x =R_EAL(c)*(F1.n).x & (F.m).x =R_EAL(c)*(F1.m).x
      by A20,A21,MESFUNC1:def 6;
     (F1.n).x <= (F1.m).x by A2,A8,A9,A19,A20;
    hence thesis by A1,A22,EXTREAL1:42;
   end;
A23:for x be Element of X st x in dom(c(#)f) holds
      F#x is convergent & lim(F#x) = (c(#)f).x
   proof
    let x be Element of X;
    assume A24: x in dom(c(#)f);
    A25:now let n be Nat;
    reconsider n1=n as Element of NAT by ORDINAL1:def 13;
 A26:dom(c(#)(F1.n1)) = dom (F.n1) by A11 .=dom(c(#)f) by A14;
     thus (F#x).n = (F.n).x by Def13
                 .= (c(#)(F1.n1)).x by A11
                 .= R_EAL c * (F1.n).x by A24,A26,MESFUNC1:def 6
                 .= R_EAL c * (F1#x).n by Def13;
    end;
     now let n1 be set;
     assume n1 in dom(F1#x);
     then reconsider n=n1 as Element of NAT;
      F1.n is nonnegative by A2;
 then A27: F1.n is without-infty by Th18;
      (F1#x).n = (F1.n).x by Def13;
     hence -infty < (F1#x).n1 by A27,Def5;
    end;
 then A28:F1#x is without-infty by Th16;
 A29:now let n,m be Nat;
     assume A30: n <=m;
A31: (F1#x).n = (F1.n).x by Def13;
      (F1#x).m = (F1.m).x by Def13;
     hence (F1#x).n<= (F1#x).m by A2,A8,A9,A24,A30,A31;
    end;
     R_EAL c *lim(F1#x) = R_EAL c * f.x by A2,A8,A9,A24
          .=(c(#)f).x by A24,MESFUNC1:def 6;
    hence thesis by A1,A25,A28,A29,Th69;
   end;
    for n be Nat holds K.n=R_EAL(c)*(K1.n)
   proof
    let n be Nat;
    reconsider n1=n as Element of NAT by ORDINAL1:def 13;
 A32:F1.n is_simple_func_in S & F1.n is nonnegative by A2;
 A33:F.n1=c(#)(F1.n1) by A11;
    thus K.n=integral'(M,F.n1) by A13
           .= R_EAL c * integral'(M,F1.n) by A1,A32,A33,Th72
           .= R_EAL c * K1.n by A2;
   end;
   then K is convergent & lim K = R_EAL c * lim K1 by A1,A3,A4,Th69;
   hence thesis by A2,A9,A10,A13,A14,A17,A18,A23,Def15;
end;

theorem Th93:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL st
 (ex A be Element of S st A = dom f & f is_measurable_on A) &
 (for x be Element of X st x in dom f holds 0= f.x) holds
   integral+(M,f) = 0
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL such that
A1:(ex A be Element of S st A = dom f & f is_measurable_on A )&
   (for x be Element of X st x in dom f holds 0 = f.x);
    (for x be set st x in dom f holds 0 <= f.x) by A1;
then A2:f is nonnegative by SUPINF_2:71;
A3:dom(0(#)f) =dom f by MESFUNC1:def 6;
    now let x be set;
    assume A4:x in dom(0(#)f);
    hence (0(#)f).x = R_EAL 0 * f.x by MESFUNC1:def 6
                   .= 0 by EXTREAL1:22
                   .= f.x by A1,A3,A4;
   end;
   hence integral+(M,f) = integral+(M,0(#)f) by A3,FUNCT_1:9
                       .= R_EAL 0 * integral+(M,f) by A1,A2,Th92
                       .= 0 by EXTREAL1:22;
end;

begin :: Integral of measurable function

definition
  let X be non empty set;
  let S be SigmaField of X;
  let M be sigma_Measure of S;
  let f be PartFunc of X,ExtREAL;
  func Integral(M,f) -> Element of ExtREAL equals
   integral+(M,max+f)-integral+(M,max-f);
  coherence;
end;

theorem Th94:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL st
 (ex A be Element of S st A = dom f & f is_measurable_on A) &
 f is nonnegative holds
    Integral(M,f) =integral+(M,f)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL;
   assume
A1:(ex A be Element of S st A = dom f & f is_measurable_on A) &
    f is nonnegative;
   then consider A be Element of S such that
A2:A = dom f & f is_measurable_on A;
A3:dom f = dom max+ f by MESFUNC2:def 2;
A4:now let x be set;
    assume A5:x in dom f;
  A6:0 <= f.x by A1,SUPINF_2:70;
    thus max+f.x = max(f.x,0) by A3,A5,MESFUNC2:def 2
                .=f.x by A6,XXREAL_0:def 9;
   end;
then A7:f = max+ f by A3,FUNCT_1:9;
A8:dom f = dom max-f by MESFUNC2:def 3;
A9:now let x be Element of X;
    assume x in dom max- f;
    then max+f.x=f.x by A4,A8;
    hence max-f.x=0 by MESFUNC2:21;
   end;
A10:dom f=dom (max- f) by MESFUNC2:def 3;
    max- f is_measurable_on A by A2,MESFUNC2:28;
   hence Integral(M,f) =integral+(M,f) - R_EAL 0 by A2,A7,A9,A10,Th93
                         .=integral+(M,f) by MEASURE6:21;
end;

theorem
 for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL st
 f is_simple_func_in S & f is nonnegative holds
  Integral(M,f) = integral+(M,f) & Integral(M,f) = integral'(M,f)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL;
   assume
A1:f is_simple_func_in S & f is nonnegative;
   then reconsider A=dom f as Element of S by Th43;
    f is_measurable_on A by A1,MESFUNC2:37;
   hence Integral(M,f) =integral+(M,f) by A1,Th94;
   hence thesis by A1,Th83;
end;

theorem
 for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL st
 (ex A be Element of S st A = dom f & f is_measurable_on A) &
 f is nonnegative holds
   0 <= Integral(M,f)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL;
   assume
A1:(ex A be Element of S st A = dom f & f is_measurable_on A) &
   f is nonnegative;
   then 0 <= integral+(M,f) by Th85;
   hence 0 <= Integral(M,f) by A1,Th94;
end;

theorem
 for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    A,B be Element of S st
 (ex E be Element of S st E = dom f & f is_measurable_on E ) &
 f is nonnegative & A misses B
holds
   Integral(M,f|(A\/B)) = Integral(M,f|A)+Integral(M,f|B)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       A,B be Element of S;
   assume that
A1: (ex E be Element of S st E = dom f & f is_measurable_on E ) and
A2: f is nonnegative & A misses B;
   consider E be Element of S such that
A3: E = dom f & f is_measurable_on E by A1;
A4:ex C be Element of S st C = dom(f|(A\/B)) & f|(A\/B) is_measurable_on C
   proof
    reconsider C = E/\(A\/B) as Element of S;
    take C;
    thus dom(f|(A\/B)) = C by A3,FUNCT_1:68;
     C c= dom f by A3,XBOOLE_1:17;
then A5:f is_measurable_on C & C = dom f /\ C by A3,MESFUNC1:34,XBOOLE_1:28;
then A6:f|C is_measurable_on C by Th48;
A7:dom(f|(A\/B)) = C by A3,FUNCT_1:68 .= dom(f|C) by A5,FUNCT_1:68;
     for x be set st x in dom(f|(A\/B)) holds (f|(A\/B)).x = (f|C).x
    proof
     let x be set;
     assume
A8:  x in dom(f|(A\/B));
     then (f|(A\/B)).x = f.x by FUNCT_1:70;
     hence thesis by A7,A8,FUNCT_1:70;
    end;
    hence f|(A\/B) is_measurable_on C by A6,A7,FUNCT_1:9;
   end;
A9:integral+(M,f|(A\/B)) = integral+(M,f|A)+integral+(M,f|B)
     by A1,A2,Th87;
    f|(A\/B) is nonnegative by A2,Th21;
then A10:Integral(M,f|(A\/B))=integral+(M,f|(A\/B)) by A4,Th94;
A11:ex C be Element of S st C = dom(f|A) & f|A is_measurable_on C
   proof
    take C=E/\A;
    thus dom(f|A) = C by A3,FUNCT_1:68;
A12:C c= dom f by A3,XBOOLE_1:17;
then A13:f is_measurable_on C by A3,MESFUNC1:34;
A14:C = dom f /\ C by A12,XBOOLE_1:28;
A15:dom(f|A) = C by A3,FUNCT_1:68 .= dom(f|C) by A14,FUNCT_1:68;
     for x be set st x in dom(f|A) holds (f|A).x = (f|C).x
    proof
     let x be set;
     assume A16:x in dom(f|A);
     then (f|A).x = f.x by FUNCT_1:70;
     hence thesis by A15,A16,FUNCT_1:70;
    end;
    then f|C = f|A by A15,FUNCT_1:9;
    hence f|A is_measurable_on C by A13,A14,Th48;
   end;
    f|A is nonnegative by A2,Th21;
then A17:Integral(M,f|A)=integral+(M,f|A) by A11,Th94;
A18:ex C be Element of S st C = dom(f|B) & f|B is_measurable_on C
   proof
    take C=E/\B;
    thus dom(f|B) = C by A3,FUNCT_1:68;
A19:C c= dom f by A3,XBOOLE_1:17;
then A20:f is_measurable_on C by A3,MESFUNC1:34;
A21:C = dom f /\ C by A19,XBOOLE_1:28;
A22:dom(f|B) = C by A3,FUNCT_1:68 .= dom(f|C) by A21,FUNCT_1:68;
     for x be set st x in dom(f|B) holds (f|B).x = (f|C).x
    proof
     let x be set;
     assume A23:x in dom(f|B);
     then (f|B).x = f.x by FUNCT_1:70;
     hence thesis by A22,A23,FUNCT_1:70;
    end;
    then f|C = f|B by A22,FUNCT_1:9;
    hence f|B is_measurable_on C by A20,A21,Th48;
   end;
    f|B is nonnegative by A2,Th21;
   hence thesis by A9,A10,A17,A18,Th94;
end;

theorem
 for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    A be Element of S st
(ex E be Element of S st E = dom f & f is_measurable_on E ) &
f is nonnegative holds
  0<= Integral(M,f|A)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       A be Element of S;
   assume that
A1: (ex E be Element of S st E = dom f & f is_measurable_on E) and
A2: f is nonnegative;
   consider E be Element of S such that
A3: E = dom f & f is_measurable_on E by A1;
A4:ex C be Element of S st C = dom(f|A) & f|A is_measurable_on C
   proof
    take C = E /\ A;
    thus dom(f|A) = C by A3,FUNCT_1:68;
     C c= E by XBOOLE_1:17;
then A5:f is_measurable_on C & C = dom f /\ C by A3,MESFUNC1:34,XBOOLE_1:28;
then A6:f|C is_measurable_on C by Th48;
A7:dom(f|A) = C by A3,FUNCT_1:68 .= dom(f|C) by A5,FUNCT_1:68;
     for x be set st x in dom(f|A) holds (f|A).x = (f|C).x
    proof
     let x be set;
     assume A8:x in dom(f|A);
     then (f|A).x = f.x by FUNCT_1:68;
     hence thesis by A7,A8,FUNCT_1:68;
    end;
    hence f|A is_measurable_on C by A6,A7,FUNCT_1:9;
   end;
A9:f|A is nonnegative by A2,Th21;
   then 0<= integral+(M,f|A) by A4,Th85;
   hence thesis by A4,A9,Th94;
end;

theorem
 for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    A,B be Element of S st
 (ex E be Element of S st E = dom f & f is_measurable_on E ) &
 f is nonnegative & A c= B
holds
  Integral(M,f|A) <= Integral(M,f|B)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       A,B be Element of S;
   assume that
A1: (ex E be Element of S st E = dom f & f is_measurable_on E) and
A2: f is nonnegative and
A3: A c= B;
   consider E be Element of S such that
A4:E = dom f & f is_measurable_on E by A1;
A5:ex C be Element of S st C = dom(f|A) & f|A is_measurable_on C
   proof
    take C = E /\ A;
    thus dom(f|A) = C by A4,FUNCT_1:68;
     C c= E by XBOOLE_1:17;
then A6:f is_measurable_on C & C = dom f /\ C by A4,MESFUNC1:34,XBOOLE_1:28;
then A7:f|C is_measurable_on C by Th48;
A8:dom(f|A) = C by A4,FUNCT_1:68 .= dom(f|C) by A6,FUNCT_1:68;
     for x be set st x in dom(f|A) holds (f|A).x = (f|C).x
    proof
     let x be set;
     assume A9:x in dom(f|A);
     then (f|A).x = f.x by FUNCT_1:68;
     hence thesis by A8,A9,FUNCT_1:68;
    end;
    hence f|A is_measurable_on C by A7,A8,FUNCT_1:9;
   end;
A10:f|A is nonnegative by A2,Th21;
A11:ex C be Element of S st C = dom(f|B) & f|B is_measurable_on C
   proof
    take C = E /\ B;
    thus dom(f|B) = C by A4,FUNCT_1:68;
     C c= E by XBOOLE_1:17;
then A12:f is_measurable_on C & C = dom f /\ C by A4,MESFUNC1:34,XBOOLE_1:28;
then A13:f|C is_measurable_on C by Th48;
A14:dom(f|B) = C by A4,FUNCT_1:68 .= dom(f|C) by A12,FUNCT_1:68;
     for x be set st x in dom(f|B) holds (f|B).x = (f|C).x
    proof
     let x be set;
     assume A15:x in dom(f|B);
     then (f|B).x = f.x by FUNCT_1:68;
     hence thesis by A14,A15,FUNCT_1:68;
    end;
    hence f|B is_measurable_on C by A13,A14,FUNCT_1:9;
   end;
A16:f|B is nonnegative by A2,Th21;
    integral+(M,f|A) <= integral+(M,f|B) by A1,A2,A3,Th89;
   then Integral(M,f|A) <= integral+(M,f|B) by A5,A10,Th94;
   hence thesis by A11,A16,Th94;
end;

theorem
 for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    A be Element of S st
 (ex E be Element of S st E = dom f & f is_measurable_on E) & M.A = 0
holds
    Integral(M,f|A)=0
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       A be Element of S such that
A1:(ex E be Element of S st E = dom f & f is_measurable_on E) &
    M.A = 0;
   consider E be Element of S such that
A2:E = dom f & f is_measurable_on E by A1;
   reconsider C = E /\ A as Element of S;
A3:dom f=dom (max+ f) & dom f=dom (max- f) by MESFUNC2:def 2,def 3;
A4:max+f is nonnegative & max-f is nonnegative by Lm1;
    max+ f is_measurable_on E by A2,MESFUNC2:27;
then A5:integral+(M,(max+ f)|A)=0 by A1,A2,A3,A4,Th88;
A6:max- f is_measurable_on E by A2,MESFUNC2:28;
    Integral(M,f|A)
    = integral+(M,(max+ f)|A) - integral+(M,max-(f|A)) by Th34
   .= integral+(M,(max+ f)|A) - integral+(M,(max- f)|A) by Th34
   .= 0.- 0. by A1,A2,A3,A4,A5,A6,Th88;
   hence thesis by MEASURE6:21;
end;

theorem Th101:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    E,A be Element of S st
 E = dom f & f is_measurable_on E & M.A =0 holds
   Integral(M,f|(E\A)) = Integral(M,f)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       E,A be Element of S such that
A1:E = dom f & f is_measurable_on E & M.A =0;
   set B = E\A;
A2:dom f=dom(max+f) & dom f=dom(max-f) by MESFUNC2:def 2,def 3;
A3:max+f is nonnegative & max-f is nonnegative by Lm1;
A4:max+f is_measurable_on E & max-f is_measurable_on E by A1,MESFUNC2:27,28;
    Integral(M,f|B)
    = integral+(M,(max+f)|B) -integral+(M,max-(f|B)) by Th34
   .= integral+(M,(max+f)|B) -integral+(M,(max-f)|B) by Th34
   .=integral+(M,max+f) -integral+(M,(max-f)|B) by A1,A2,A3,A4,Th90;
   hence thesis by A1,A2,A3,A4,Th90;
end;

definition
let X be non empty set;
let S be SigmaField of X;
let M be sigma_Measure of S;
let f be PartFunc of X,ExtREAL;
pred f is_integrable_on M means :Def17:
  (ex A be Element of S st A = dom f & f is_measurable_on A ) &
  integral+(M,max+ f) < +infty &
  integral+(M,max- f) < +infty;
end;

theorem Th102:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL st
 f is_integrable_on M holds
   0 <= integral+(M,max+f) & 0 <= integral+(M,max-f) &
   -infty < Integral(M,f) & Integral(M,f) < +infty
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL such that
A1:f is_integrable_on M;
   consider A be Element of S such that
A2:A = dom f & f is_measurable_on A by A1,Def17;
A3:dom f=dom(max+f) & dom f=dom(max-f) by MESFUNC2:def 2,def 3;
A4:max+f is nonnegative & max-f is nonnegative by Lm1;
A5:max+f is_measurable_on A & max-f is_measurable_on A
    by A2,MESFUNC2:27,28;
then A6:-infty <> integral+(M,max+f) & -infty <> integral+(M,max-f)
     by A2,A3,A4,Th85;
A7:integral+(M,max+f) <> +infty & integral+(M,max-f) <> +infty
     by A1,Def17;
   then reconsider maxf1=integral+(M,max+f) as Real by A6,XXREAL_0:14;
   reconsider maxf2=integral+(M,max-f) as Real by A6,A7,XXREAL_0:14;
    integral+(M,max+f)-integral+(M,max-f) = maxf1-maxf2 by SUPINF_2:5;
   hence thesis by A2,A3,A4,A5,Th85,XXREAL_0:9,12;
end;

theorem Th103:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    A be Element of S st
f is_integrable_on M holds
  integral+(M,max+(f|A)) <= integral+(M,max+ f) &
  integral+(M,max-(f|A)) <= integral+(M,max- f) &
  f|A is_integrable_on M
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       A be Element of S;
   assume
A1: f is_integrable_on M;
   then consider E be Element of S such that
A2: E = dom f & f is_measurable_on E by Def17;
A3:max+f is_measurable_on E & max-f is_measurable_on E by A2,MESFUNC2:27,28;
A4:dom f = dom(max+f) & dom f=dom(max-f) by MESFUNC2:def 2,def 3;
A5:max+f is nonnegative & max-f is nonnegative by Lm1;
A6:E/\A c= E by XBOOLE_1:17;
    max+f|(E/\A) = (max+f|E)|A & max-f|(E/\A) = (max-f|E)|A by RELAT_1:100;
   then max+f|(E/\A) = (max+f)|A & max+f|E = max+f &
   max-f|(E/\A) = (max-f)|A & max-f|E = max-f by A2,A4,GRFUNC_1:64;
then A7:integral+(M,max+f|A) <= integral+(M,max+f) &
   integral+(M,max-f|A) <= integral+(M,max-f) by A2,A3,A4,A5,A6,Th89;
A8:E /\ A = dom(f|A) by A2,FUNCT_1:68;
    dom f /\ (E/\A) = E/\A & f is_measurable_on E/\A
     by A2,A6,MESFUNC1:34,XBOOLE_1:28;
   then f|(E/\A) is_measurable_on E/\A by Th48;
   then f|E|A is_measurable_on E/\A by RELAT_1:100;
then A9:f|A is_measurable_on E/\A by A2,GRFUNC_1:64;
A10:integral+(M,max+f) < +infty & integral+(M,max-f) < +infty
     by A1,Def17;
    integral+(M,max+(f|A)) <= integral+(M,max+f) &
   integral+(M,max-(f|A)) <= integral+(M,max-f) by A7,Th34;
   then integral+(M,max+(f|A)) < +infty &
   integral+(M,max-(f|A)) < +infty by A10,XXREAL_0:2;
   hence thesis by A7,A8,A9,Def17,Th34;
end;

theorem Th104:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    A,B be Element of S st
 f is_integrable_on M & A misses B holds
  Integral(M,f|(A\/B)) = Integral(M,f|A) + Integral(M,f|B)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       A,B be Element of S;
   assume
A1: f is_integrable_on M & A misses B;
   then consider E be Element of S such that
A2: E = dom f & f is_measurable_on E by Def17;
   set AB = E/\(A\/B);
A3:integral+(M,max+(f|A)) <= integral+(M,max+f) &
   integral+(M,max-(f|A)) <= integral+(M,max-f) &
   f|A is_integrable_on M &
   integral+(M,max+(f|B)) <= integral+(M,max+f) &
   integral+(M,max-(f|B)) <= integral+(M,max-f) &
   f|B is_integrable_on M by A1,Th103;
then A4:(ex C be Element of S st C = dom(f|A) & f|A is_measurable_on C) &
   integral+(M,max+(f|A)) < +infty & integral+(M,max-(f|A)) < +infty &
   (ex C be Element of S st C = dom(f|B) & f|B is_measurable_on C) &
   integral+(M,max+(f|B)) < +infty & integral+(M,max-(f|B)) < +infty
     by Def17;
A5:0 <= integral+(M,max+(f|A)) & 0 <= integral+(M,max-(f|A)) &
   0 <= integral+(M,max+(f|B)) & 0 <= integral+(M,max-(f|B)) by A3,Th102;
A6:Integral(M,f|(A\/B))
    = Integral(M,(f|E)|(A\/B)) by A2,GRFUNC_1:64
   .= Integral(M,f|AB) by RELAT_1:100
   .= integral+(M,max+f|AB) - integral+(M,max-(f|AB)) by Th34
   .= integral+(M,max+f|AB) - integral+(M,max-f|AB) by Th34;
A7:dom f= dom(max+f) & dom f = dom(max-f) by MESFUNC2:def 2,def 3;
A8:max+f is nonnegative & max-f is nonnegative by Lm1;
    max+f is_measurable_on E & max-f is_measurable_on E
     by A2,MESFUNC2:27,28;
then A9:integral+(M,max+f|(A\/B)) = integral+(M,max+f|A) + integral+(M,max+f|B)
&
   integral+(M,max-f|(A\/B)) = integral+(M,max-f|A) + integral+(M,max-f|B)
     by A1,A2,A7,A8,Th87;
    max+f|(A\/B) = max+f|E|(A\/B) & max-f|(A\/B) = max-f|E|(A\/B)
     by A2,A7,GRFUNC_1:64;
then A10:max+f|(A\/B) = max+f|AB & max-f|(A\/B) = max-f|AB by RELAT_1:100;
A11:max+(f|A)=max+f|A & max-(f|A)=max-f|A &
   max+(f|B)=max+f|B & max-(f|B)=max-f|B by Th34;
   reconsider f1 = integral+(M,max+(f|A)) as Real by A4,A5,XXREAL_0:14;
   reconsider f2 = integral+(M,max-(f|A)) as Real by A4,A5,XXREAL_0:14;
   reconsider g1 = integral+(M,max+(f|B)) as Real by A4,A5,XXREAL_0:14;
   reconsider g2 = integral+(M,max-(f|B)) as Real by A4,A5,XXREAL_0:14;
    integral+(M,max+(f|A)) + integral+(M,max+(f|B)) = f1+g1 &
   integral+(M,max-(f|A)) + integral+(M,max-(f|B)) = f2+g2 by SUPINF_2:1;
   then Integral(M,f|(A\/B)) = f1+g1-(f2+g2) by A6,A9,A10,A11,SUPINF_2:5;
then A12:Integral(M,f|(A\/B)) = (f1-f2)+(g1-g2);
    integral+(M,max+(f|A))-integral+(M,max-(f|A)) = f1-f2 &
   integral+(M,max+(f|B))-integral+(M,max-(f|B)) = g1-g2 by SUPINF_2:5;
   hence thesis by A12,SUPINF_2:1;
end;

theorem
 for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    A,B be Element of S st
 f is_integrable_on M & B = (dom f)\A
holds
   f|A is_integrable_on M & Integral(M,f) = Integral(M,f|A)+Integral(M,f|B)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       A,B be Element of S such that
A1:f is_integrable_on M & B = dom f \ A;
A2:A misses B by A1,XBOOLE_1:79;
    A \/ B = A\/dom f by A1,XBOOLE_1:39;
   then dom f c= A \/ B by XBOOLE_1:7;
then A3:dom f /\ (A \/ B) = dom f by XBOOLE_1:28;
    f|(A\/B) = f|(dom f)|(A\/B) by GRFUNC_1:64
    .= f|(dom f /\(A\/B)) by RELAT_1:100 .= f by A3,GRFUNC_1:64;
   hence thesis by A1,A2,Th103,Th104;
end;

theorem Th106:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL st
 (ex A be Element of S st A = dom f & f is_measurable_on A )
   holds
    f is_integrable_on M iff |.f.| is_integrable_on M
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL;
   assume
A1:ex A be Element of S st A = dom f & f is_measurable_on A;
   then consider A be Element of S such that
A2: A = dom f & f is_measurable_on A;
A3:dom|.f.| = dom max+|.f.| & dom |.f.| = dom max-|.f.| &
   dom f= dom max+f & dom f = dom max-f by MESFUNC2:def 2,def 3;
A4:A = dom|.f.| & |.f.| is_measurable_on A
      by A2,MESFUNC1:def 10,MESFUNC2:29;
then A5:max-|.f.| is_measurable_on A by MESFUNC2:28;
A6:max+f is_measurable_on A & max-f is_measurable_on A by A2,MESFUNC2:27,28;
A7:max+f is nonnegative by Lm1;
then A8:-infty <> integral+(M,max+f) by A2,A3,A6,Th85;
A9:|.f.| = max+f + max-f by MESFUNC2:26;
   hereby assume
A10:f is_integrable_on M;
 A11:now let x be set;
     assume
  A12: x in dom |.f.|;
     then (|.f.|).x =|. f.x .| by MESFUNC1:def 10;
  then A13: 0 <= (|.f.|).x by EXTREAL2:51;
      (max+|.f.|).x = max((|.f.|).x,0) by A3,A12,MESFUNC2:def 2;
     hence (max+|.f.|).x = (|.f.|).x by A13,XXREAL_0:def 9;
    end;
 then A14:|.f.| = max+|.f.| by A3,FUNCT_1:9;
     now let x be Element of X;
     assume
     x in dom max-|.f.|;
     then (max+|.f.|).x=(|.f.|).x by A3,A11;
     hence (max-|.f.|).x=0 by MESFUNC2:21;
    end;
 then A15:integral+(M,max-|.f.|)=0 by A3,A4,A5,Th93;
A16: integral+(M,max+f) < +infty & integral+(M,max-f) < +infty
     by A10,Def17;
     max-f is nonnegative by Lm1;
    then integral+(M,max+f + max-f) =integral+(M,max+f)+integral+(M,max-f)
         by A2,A3,A6,A7,Lm11;
    then integral+(M,max+f + max-f) <> +infty by A16,SUPINF_2:8;
    then integral+(M,max+|.f.|) < +infty by A9,A14,XXREAL_0:4;
    hence |.f.| is_integrable_on M by A4,A15,Def17;
   end;
   assume
A17:|.f.| is_integrable_on M;
     now let x be set;
     assume x in dom |.f.|;
     then (|.f.|).x =|. f.x .| by MESFUNC1:def 10;
     hence 0 <= (|.f.|).x by EXTREAL2:51;
    end;
then A18:|.f.| is nonnegative by SUPINF_2:71;
A19:max-f is nonnegative by Lm1;
then A20:-infty <> integral+(M,max-f) by A2,A3,A6,Th85;
A21: integral+(M,max+f + max-f) = integral+(M,max+f) + integral+(M,max-f)
        by A2,A3,A6,A7,A19,Lm11;
     0 <= integral+(M,max+|.f.|) & 0 <= integral+(M,max-|.f.|) &
    -infty < Integral(M,|.f.|) & Integral(M,|.f.|) < +infty by A17,Th102;
    then integral+(M,max+f + max-f) < +infty by A4,A9,A18,Th94;
    then integral+(M,max+f) <>+infty & integral+(M,max-f) <> +infty
      by A8,A20,A21,EXTREAL2:23;
    then integral+(M,max+f) < +infty & integral+(M,max-f) < +infty by
XXREAL_0:4;
    hence f is_integrable_on M by A1,Def17;
end;

theorem
 for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL st
 f is_integrable_on M holds
    |. Integral(M,f) .| <= Integral(M,|.f.|)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL such that
A1:f is_integrable_on M;
   consider A be Element of S such that
A2: A = dom f & f is_measurable_on A by A1,Def17;
A3:0 <= integral+(M,max+f) & 0 <= integral+(M,max-f) &
   -infty < Integral(M,f) & Integral(M,f) < +infty &
   integral+(M,max+f) <> +infty & integral+(M,max-f) <> +infty
     by A1,Def17,Th102;
A4:A = dom |.f.| & |.f.| is_measurable_on A by A2,MESFUNC1:def 10,MESFUNC2:29;
    now let x be set;
    assume x in dom (|.f.|);
    then (|.f.|).x =|. f.x .| by MESFUNC1:def 10;
    hence 0 <= (|.f.|).x by EXTREAL2:51;
   end;
then A5:|.f.| is nonnegative by SUPINF_2:71;
A6:dom f=dom max+f & dom f = dom max-f by MESFUNC2:def 2,def 3;
A7:max+f is nonnegative & max-f is nonnegative by Lm1;
A8:max+f is_measurable_on A & max-f is_measurable_on A
     by A2,MESFUNC2:27,28;
then A9:0 <= integral+(M,max+f) & 0 <= integral+(M,max-f) by A2,A6,A7,Th85;
    |.f.| = max+f + max-f by MESFUNC2:26;
then A10:Integral(M,|.f.|) = integral+(M,max+f + max-f) by A4,A5,Th94
    .= integral+(M,max+f)+integral+(M,max-f) by A2,A6,A7,A8,Lm11;
    |.integral+(M,max+f)-integral+(M,max-f).|
    <= |.integral+(M,max+f).| + |.integral+(M,max-f).| by A3,EXTREAL2:69;
   then |.Integral(M,f).| <= integral+(M,max+f) + |.integral+(M,max-f).|
       by A9,EXTREAL1:def 3;
   hence thesis by A9,A10,EXTREAL1:def 3;
end;

theorem Th108:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
 ( ex A be Element of S st A = dom f & f is_measurable_on A ) &
 dom f = dom g & g is_integrable_on M &
 ( for x be Element of X st x in dom f holds |.f.x .| <= g.x )
holds
   f is_integrable_on M & Integral(M,|.f.|) <= Integral(M,g)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL;
   assume
A1: ( ex A be Element of S st A = dom f & f is_measurable_on A ) &
    dom f = dom g & g is_integrable_on M &
    ( for x be Element of X st x in dom f holds |. f.x .| <= g.x );
   then consider A be Element of S such that
A2:A = dom f & f is_measurable_on A;
   consider AA be Element of S such that
A3:AA = dom g & g is_measurable_on AA by A1,Def17;
A4:A = dom |.f.| & |.f.| is_measurable_on A by A2,MESFUNC1:def 10,MESFUNC2:29;
    now let x be set;
    assume x in dom g;
    then |. f.x .| <= g.x by A1;
    hence 0 <= g.x by EXTREAL2:51;
   end;
then A5:g is nonnegative by SUPINF_2:71;
    now let x be set;
    assume x in dom |.f.|;
    then (|.f.|).x =|. f.x .| by MESFUNC1:def 10;
    hence 0 <= (|.f.|).x by EXTREAL2:51;
   end;
then A6:|.f.| is nonnegative by SUPINF_2:71;
A7:dom |.f.| = dom max+|.f.| & dom |.f.| = dom max-(|.f.|) &
   dom g = dom max+ g by MESFUNC2:def 2,def 3;
A8:now let x be set;
    assume A9:x in dom |.f.|;
    then (|.f.|).x =|. f.x .| by MESFUNC1:def 10;
then A10:  0 <= (|.f.|).x by EXTREAL2:51;
    thus (max+|.f.|).x = max((|.f.|).x,0) by A7,A9,MESFUNC2:def 2
                      .=(|.f.|).x by A10,XXREAL_0:def 9;
   end;
then A11:|.f.| = max+|.f.| by A7,FUNCT_1:9;
    now let x be set;
    assume A12:x in dom g;
A13:  0 <= g.x by A5,SUPINF_2:70;
    thus max+g.x = max(g.x,0) by A7,A12,MESFUNC2:def 2
                .=g.x by A13,XXREAL_0:def 9;
   end;
then A14:g = max+g by A7,FUNCT_1:9;
A15:now let x be Element of X;
    assume x in dom max-(|.f.|);
    then max+(|.f.|).x=(|.f.|).x by A7,A8;
    hence max-(|.f.|).x=0 by MESFUNC2:21;
   end;
    max-|.f.| is_measurable_on A by A4,MESFUNC2:28;
then A16:integral+(M,max-|.f.|) = 0 by A4,A7,A15,Th93;
    for x be Element of X st x in dom |.f.| holds (|.f.|).x <= g.x
   proof
    let x be Element of X;
    assume A17: x in dom |.f.|;
    then (|.f.|).x =|. f.x .| by MESFUNC1:def 10;
    hence (|.f.|).x <= g.x by A1,A2,A4,A17;
   end;
then A18:integral+(M,|.f.|) <= integral+(M,g) by A1,A2,A3,A4,A5,A6,Th91;
    integral+(M,max+g) < +infty by A1,Def17;
   then integral+(M,max+(|.f.|)) < +infty by A11,A14,A18,XXREAL_0:2;
   then |.f.| is_integrable_on M by A4,A16,Def17;
   hence f is_integrable_on M by A1,Th106;
    Integral(M,g) =integral+(M,g) by A3,A5,Th94;
   hence thesis by A4,A6,A18,Th94;
end;

theorem Th109:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL, r be Real st
 dom f in S & 0 <= r & dom f <> {} &
 (for x be set st x in dom f holds f.x = r) holds
   integral(X,S,M,f) = R_EAL(r) * M.(dom f)
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f be PartFunc of X,ExtREAL;
   let r be Real;
   assume that
A1:dom f in S and
A2:0 <= r and
A3:dom f <> {} and
A4:for x be set st x in dom f holds f.x = r;
   reconsider Df = dom f as Element of S by A1;
A5:f is_simple_func_in S by A1,A4,Lm5;
    for x be set st x in dom f holds 0 <= f.x by A2,A4;
   then consider F be Finite_Sep_Sequence of S,
            a,v be FinSequence of ExtREAL such that
A6: F,a are_Re-presentation_of f &
    dom v = dom F &
    (for n be Nat st n in dom v holds v.n = a.n*(M*F).n) &
    integral(X,S,M,f) = Sum v by A3,A5,MESFUNC4:4;
A7:dom f = union rng F & dom F = dom a &
   for n be Nat st n in dom F
    for x be set st x in F.n holds f.x = a.n by A6,MESFUNC3:def 1;
A8:dom v = dom(M*F) by A6,MESFUNC3:8;
    for n be Nat st n in dom v holds v.n = R_EAL r * (M*F).n
   proof
    let n be Nat;
    assume
 A9: n in dom v;
    then F.n in rng F by A6,FUNCT_1:12;
 then A10:F.n c= union rng F by ZFMISC_1:92;
 A11:v.n = a.n*(M*F).n by A6,A9;
    per cases;
    suppose F.n = {};
     then M.(F.n) = 0 by MEASURE1:def 11;
 then A12: (M*F).n = 0 by A6,A9,FUNCT_1:23;
     then v.n = 0 by A11,EXTREAL1:22;
     hence v.n = R_EAL r * (M*F).n by A12,EXTREAL1:22;
    end;
    suppose F.n <> {};
     then consider x be set such that
A13:  x in F.n by XBOOLE_0:def 1;
      a.n = f.x by A6,A9,A13,MESFUNC3:def 1;
     hence v.n = R_EAL r * (M*F).n by A4,A7,A10,A11,A13;
    end;
   end;
   then integral(X,S,M,f) = R_EAL r * Sum(M*F) by A6,A8,MESFUNC3:10
                    .= R_EAL r * M.(union rng F) by MESFUNC3:9;
   hence thesis by A6,MESFUNC3:def 1;
end;

theorem Th110:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL, r be Real st
 dom f in S & 0 <= r &
 (for x be set st x in dom f holds f.x = r) holds
   integral'(M,f) = R_EAL(r) * M.(dom f)
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f be PartFunc of X,ExtREAL;
   let r be Real;
   assume that
A1:dom f in S and
A2:0 <= r and
A3:for x be set st x in dom f holds f.x = r;
   per cases;
   suppose A4:dom f = {};
 then A5:integral'(M,f) = 0 by Def14;
     M.(dom f) = 0 by A4,MEASURE1:def 11;
    hence thesis by A5,EXTREAL1:22;
   end;
   suppose A6:dom f <> {};
    then integral'(M,f) = integral(X,S,M,f) by Def14;
    hence thesis by A1,A2,A3,A6,Th109;
   end;
end;

theorem Th111:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL st
f is_integrable_on M holds
  f"{+infty} in S & f"{-infty} in S & M.(f"{+infty})=0 & M.(f"{-infty})=0 &
  f"{+infty} \/ f"{-infty} in S & M.(f"{+infty} \/ f"{-infty})=0
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let f be PartFunc of X,ExtREAL;
   assume
A1:f is_integrable_on M;
then A2:(ex A be Element of S st A = dom f & f is_measurable_on A) &
   integral+(M,max+f) < +infty &
   integral+(M,max-f) < +infty by Def17;
   consider A be Element of S such that
A3: A = dom f & f is_measurable_on A by A1,Def17;
A4:A /\ eq_dom(f,+infty) is_measurable_on S &
   A /\ eq_dom(f,-infty) is_measurable_on S by A3,MESFUNC1:37,38;
    for x be set holds
    ( x in eq_dom(f,+infty) implies x in A ) &
    ( x in eq_dom(f,-infty) implies x in A ) by A3,MESFUNC1:def 16;
then A5:eq_dom(f,+infty) c= A & eq_dom(f,-infty) c= A by TARSKI:def 3;
   then A /\ eq_dom(f,+infty) = eq_dom(f,+infty) &
   A /\ eq_dom(f,-infty) = eq_dom(f,-infty) by XBOOLE_1:28;
then A6:f"{+infty} is_measurable_on S & f"{-infty} is_measurable_on S by A4,
Th36;
   hence A7:f"{+infty} in S & f"{-infty} in S by MESFUNC1:def 11;
   reconsider B1 = f"{+infty} as Element of S by A6,MESFUNC1:def 11;
   reconsider B2 = f"{-infty} as Element of S by A6,MESFUNC1:def 11;
A8:max+f is nonnegative & max-f is nonnegative by Lm1;
   set C1 = A \ B1;
   set C2 = A \ B2;
A9:A = dom max+f & A = dom max-f by A3,MESFUNC2:def 2,def 3;
then A10:B1 c= dom max+f & B2 c= dom(max-f) by A5,Th36;
   then B1\/C1 = A & B2\/C2 = A by A9,XBOOLE_1:45;
then A11:dom(max+f|(B1\/C1)) = dom max+f /\ dom max+f &
   dom(max-f|(B2\/C2)) = dom max-f /\ dom max-f by A9,FUNCT_1:68;
    for x be Element of X holds
    ( x in dom(max+f|(B1\/C1)) implies max+f|(B1\/C1).x = max+f.x ) &
    ( x in dom(max-f|(B2\/C2)) implies max-f|(B2\/C2).x = max-f.x )
      by FUNCT_1:70;
then A12:max+f|(B1\/C1) = max+f & max-f|(B2\/C2) = max-f by A11,PARTFUN1:34;
A13:max+f is_measurable_on A & max-f is_measurable_on A
     by A3,MESFUNC2:27,28;
then A14:max+f is_measurable_on B1 & max-f is_measurable_on B2 by A9,A10,
MESFUNC1:34;
    B1 = dom max+f /\ B1 & B2 = dom(max-f) /\ B2 by A10,XBOOLE_1:28;
then A15:max+f|B1 is_measurable_on B1 & max-f|B2 is_measurable_on B2 by A14,
Th48;
    C1 c= dom max+f & B1 misses C1 &
   C2 c= dom(max-f) & B2 misses C2 by A9,XBOOLE_1:106;
   then 0 <= integral+(M,max+f|C1) & 0 <= integral+(M,max+f|B1) &
   integral+(M,max+f) = integral+(M,max+f|B1) + integral+(M,max+f|C1) &
   0 <= integral+(M,max-f|C2) & 0 <= integral+(M,max-f|B2) &
   integral+(M,max-f) = integral+(M,max-f|B2) + integral+(M,max-f|C2)
     by A8,A9,A12,A13,Th86,Th87;
then A16:integral+(M,max+f|B1) <= integral+(M,max+f) &
   integral+(M,max-f|B2) <= integral+(M,max-f) by Th13;
   thus
A17:now assume
A18: M.(f"{+infty}) <> 0;
then A19: 0 < M.(f"{+infty}) by A7,Th51;
A20:for r be Real st 0 < r holds R_EAL r * M.B1 <= integral+(M,max+f)
    proof
     let r be Real; assume
  A21:0 < r;
     defpred P[set] means $1 in dom(max+f|B1);
     deffunc F(set) = R_EAL r;
  A22:for x be set st P[x] holds F(x) in ExtREAL;
     consider g be PartFunc of X,ExtREAL such that
  A23: (for x be set holds x in dom g iff x in X & P[x]) &
      (for x be set st x in dom g holds g.x = F(x)) from PARTFUN1:sch 3(A22);
      dom g = X /\ dom(max+f|B1) by A23,XBOOLE_0:def 3;
  then A24:dom g = dom(max+f|B1) by XBOOLE_1:28;
      dom(max+f|B1) = dom max+f /\ B1 by FUNCT_1:68;
  then A25:dom(max+f|B1) = B1 by A10,XBOOLE_1:28;
  A26:g is_measurable_on B1
     proof
       dom chi(B1,X) = X by FUNCT_3:def 3;
   then A27:B1 = dom chi(B1,X) /\ B1 by XBOOLE_1:28;
   then A28:B1 = dom(chi(B1,X)|B1) by FUNCT_1:68;
   then A29:dom g = dom(r(#)chi(B1,X)|B1) by A24,A25,MESFUNC1:def 6;
       for x be Element of X st x in dom g holds g.x = (r(#)(chi(B1,X)|B1)).x
      proof
       let x be Element of X;
       assume
   A30: x in dom g;
       then x in dom(chi(B1,X)|B1) by A24,A25,A27,FUNCT_1:68;
       then x in dom(r(#)(chi(B1,X)|B1)) by MESFUNC1:def 6;
   then A31: (r(#)(chi(B1,X)|B1)).x = R_EAL r * (chi(B1,X)|B1).x by
MESFUNC1:def 6
                           .= R_EAL r * chi(B1,X).x by A24,A25,A28,A30,
FUNCT_1:70;
        chi(B1,X).x = 1 by A24,A25,A30,FUNCT_3:def 3;
       then (r(#)(chi(B1,X)|B1)).x = R_EAL r by A31,EXTREAL2:4;
       hence thesis by A23,A30;
      end;
   then A32:g = r(#)(chi(B1,X)|B1) by A29,PARTFUN1:34;
       chi(B1,X) is_measurable_on B1 by MESFUNC2:32;
      then chi(B1,X)|B1 is_measurable_on B1 by A27,Th48;
      hence g is_measurable_on B1 by A28,A32,MESFUNC1:41;
     end;
      for x be set st x in dom g holds 0 <= g.x by A21,A23;
  then A33:g is nonnegative by SUPINF_2:71;
 A34:for x be Element of X st x in dom g holds g.x <= max+f|B1.x
     proof
      let x be Element of X;
      assume
   A35:x in dom g;
   then A36:x in dom f & f.x in {+infty} by A24,A25,FUNCT_1:def 13;
   then A37:f.x = +infty by TARSKI:def 1;
   then A38:max(f.x,0) = f.x by XXREAL_0:def 9;
       x in dom max+f by A36,MESFUNC2:def 2;
      then max+f.x = +infty by A37,A38,MESFUNC2:def 2;
      then max+f|B1.x = +infty by A24,A25,A35,FUNCT_1:72;
      hence thesis by XXREAL_0:4;
     end;
      max+f|B1 is nonnegative by A8,Th21;
     then integral+(M,g) <= integral+(M,max+f|B1)
       by A15,A24,A25,A26,A33,A34,Th91;
  then A39:integral+(M,g) <= integral+(M,max+f) by A16,XXREAL_0:2;
  A40:integral'(M,g) = R_EAL r * M.(dom g) by A21,A23,A24,A25,Th110;
      g is_simple_func_in S by A23,A24,A25,Lm5;
     hence thesis by A24,A25,A33,A39,A40,Th83;
    end;
    per cases;
    suppose
  A41:M.B1 = +infty;
      R_EAL 1 * M.B1 <= integral+(M,max+f) by A20;
     hence contradiction by A2,A41,EXTREAL2:4;
    end;
    suppose
  A42:M.B1 <> +infty;
      R_EAL 1 * M.B1 <= integral+(M,max+f) by A20;
  then A43:0 < integral+(M,max+f) by A19,EXTREAL2:4;
     reconsider MB = M.B1 as Real by A19,A42,XXREAL_0:14;
     reconsider I = integral+(M,max+ f) as Real by A2,A43,XXREAL_0:14;
      0 < 2 * I by A43,XREAL_1:131;
     then (2 * I) / MB > 0 by A19,XREAL_1:141;
  then A44:R_EAL(2*I/MB) * M.B1 <= integral+(M,max+f) by A20;
      R_EAL(2*I/MB) * M.B1 = 2*I/MB * MB by EXTREAL1:13;
     then 2 * I <= I by A18,A44,XCMPLX_1:88;
     hence contradiction by A43,XREAL_1:157;
    end;
   end;
   thus
A45:now assume
A46: M.(f"{-infty}) <> 0;
A47:0 <= M.(f"{-infty}) by A7,Th51;
A48:for r be Real st 0 < r holds R_EAL r * M.B2 <= integral+(M,max-f)
    proof
     let r be Real;
     assume
  A49: 0 < r;
     defpred P[set] means $1 in dom(max-f|B2);
     deffunc F(set) = R_EAL r;
  A50:for x be set st P[x] holds F(x) in ExtREAL;
     consider g be PartFunc of X,ExtREAL such that
  A51: (for x be set holds x in dom g iff x in X & P[x]) &
      (for x be set st x in dom g holds g.x = F(x)) from PARTFUN1:sch 3(A50);
      dom g = X /\ dom(max-f|B2) by A51,XBOOLE_0:def 3;
  then A52:dom g = dom(max- f|B2) by XBOOLE_1:28;
      dom(max-f|B2) = dom max-f /\ B2 by FUNCT_1:68;
  then A53:dom(max-f|B2) = B2 by A10,XBOOLE_1:28;
  A54:g is_measurable_on B2
     proof
       dom chi(B2,X) = X by FUNCT_3:def 3;
   then A55:B2 = dom chi(B2,X) /\ B2 by XBOOLE_1:28;
   then A56:B2 = dom(chi(B2,X)|B2) by FUNCT_1:68;
   then A57:dom g = dom(r(#)chi(B2,X)|B2) by A52,A53,MESFUNC1:def 6;
       for x be Element of X st x in dom g holds g.x = (r(#)(chi(B2,X)|B2)).x
      proof
       let x be Element of X;
       assume
   A58: x in dom g;
       then x in dom(r(#)(chi(B2,X)|B2)) by A52,A53,A56,MESFUNC1:def 6;
   then A59: (r(#)(chi(B2,X)|B2)).x = R_EAL r * (chi(B2,X)|B2).x by
MESFUNC1:def 6
                           .= R_EAL r * chi(B2,X).x by A52,A53,A56,A58,
FUNCT_1:70;
        chi(B2,X).x = 1 by A52,A53,A58,FUNCT_3:def 3;
       then (r(#)(chi(B2,X)|B2)).x = R_EAL r by A59,EXTREAL2:4;
       hence thesis by A51,A58;
      end;
   then A60:g = r(#)(chi(B2,X)|B2) by A57,PARTFUN1:34;
       chi(B2,X) is_measurable_on B2 by MESFUNC2:32;
      then chi(B2,X)|B2 is_measurable_on B2 by A55,Th48;
      hence g is_measurable_on B2 by A56,A60,MESFUNC1:41;
     end;
      for x be set st x in dom g holds 0 <= g.x by A49,A51;
  then A61:g is nonnegative by SUPINF_2:71;
 A62:for x be Element of X st x in dom g holds g.x <= (max-f|B2).x
     proof
      let x be Element of X;
      assume
   A63:x in dom g;
   then A64:x in dom f & f.x in {-infty} by A52,A53,FUNCT_1:def 13;
   then A65:-f.x = +infty by SUPINF_2:4,TARSKI:def 1;
   then A66:max(-f.x,0) = -f.x by XXREAL_0:def 9;
       x in dom max-f by A64,MESFUNC2:def 3;
      then max-f.x = +infty by A65,A66,MESFUNC2:def 3;
      then (max-f|B2).x = +infty by A52,A53,A63,FUNCT_1:72;
      hence thesis by XXREAL_0:4;
     end;
      max-f|B2 is nonnegative by A8,Th21;
     then integral+(M,g) <= integral+(M,max-f|B2)
       by A15,A52,A53,A54,A61,A62,Th91;
  then A67:integral+(M,g) <= integral+(M,max-f) by A16,XXREAL_0:2;
  A68:integral'(M,g) = R_EAL r * M.(dom g) by A49,A51,A52,A53,Th110;
      g is_simple_func_in S by A51,A52,A53,Lm5;
     hence thesis by A52,A53,A61,A67,A68,Th83;
    end;
    per cases;
    suppose
  A69:M.B2 = +infty;
      R_EAL 1 * M.B2 <= integral+(M,max-f) by A48;
     hence contradiction by A2,A69,EXTREAL2:4;
    end;
    suppose
  A70:M.B2 <> +infty;
      R_EAL 1 * M.B2 <= integral+(M,max-f) by A48;
  then A71:0 < integral+(M,max-f) by A46,A47,EXTREAL2:4;
     reconsider MB = M.B2 as Real by A47,A70,XXREAL_0:14;
     reconsider I = integral+(M,max-f) as Real by A2,A71,XXREAL_0:14;
      0 < 2*I by A71,XREAL_1:131;
     then (2*I)/MB > 0 by A46,A47,XREAL_1:141;
  then A72:R_EAL(2*I/MB) * M.B2 <= integral+(M,max-f) by A48;
      R_EAL(2*I/MB) * M.B2 = 2*I/MB * MB by EXTREAL1:13;
     then 2 * I <= I by A46,A72,XCMPLX_1:88;
     hence contradiction by A71,XREAL_1:157;
    end;
   end;
   thus f"{+infty} \/ f"{-infty} in S by A7,PROB_1:41;
   reconsider B1 as measure_zero of M by A17,MEASURE1:def 13;
   thus M.(f"{+infty} \/ f"{-infty}) = M.(B1 \/ B2)
          .= 0 by A45,MEASURE1:71;
end;

theorem Th112:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
f is_integrable_on M & g is_integrable_on M &
f is nonnegative & g is nonnegative holds
   f+g is_integrable_on M
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL;
   assume
A1: f is_integrable_on M & g is_integrable_on M &
    f is nonnegative & g is nonnegative;
   then consider A be Element of S such that
A2: A = dom f & f is_measurable_on A by Def17;
   consider B be Element of S such that
A3: B = dom g & g is_measurable_on B by A1,Def17;
    dom f /\ dom g c= dom f & dom f /\ dom g c= dom g by XBOOLE_1:17;
   then f is_measurable_on A/\B & g is_measurable_on A/\B by A2,A3,MESFUNC1:34
;
then A4:A/\B = dom(f+g) & f+g is_measurable_on A/\B by A1,A2,A3,Lm2;
A5:dom(f+g) = dom max+(f+g) & dom f = dom max+f & dom g = dom max+g
     by MESFUNC2:def 2;
A6:now let x be set; assume
 A7: x in dom(f+g);
then A8:(f+g).x =f.x+g.x by MESFUNC1:def 3;
     0 <= f.x & 0 <= g.x by A1,SUPINF_2:70;
  then A9:0 <= (f+g).x by A8,EXTREAL2:7;
     max+(f+g).x = max((f+g).x,0) by A5,A7,MESFUNC2:def 2;
    hence max+(f+g).x =(f+g).x by A9,XXREAL_0:def 9;
   end;
then A10:f+g = max+(f+g) by A5,FUNCT_1:9;
    now let x be set; assume
 A11:x in dom g;
  A12:0 <= g.x by A1,SUPINF_2:70;
     (max+g).x = max(g.x,0) by A5,A11,MESFUNC2:def 2;
    hence (max+g).x = g.x by A12,XXREAL_0:def 9;
   end;
then A13:g = max+g by A5,FUNCT_1:9;
    now let x be set; assume
 A14:x in dom f;
  A15:0 <= f.x by A1,SUPINF_2:70;
     (max+f).x = max(f.x,0) by A5,A14,MESFUNC2:def 2;
    hence (max+f).x = f.x by A15,XXREAL_0:def 9;
   end;
then A16:f = max+f by A5,FUNCT_1:9;
A17:now let x be Element of X;
    assume x in dom max-(f+g);
  then x in dom(f+g) by MESFUNC2:def 3;
    then max+(f+g).x=(f+g).x by A6;
    hence max-(f+g).x=0 by MESFUNC2:21;
   end;
A18:dom(f+g)=dom(max-(f+g)) by MESFUNC2:def 3;
    max-(f+g) is_measurable_on A/\B by A4,MESFUNC2:28;
then A19:integral+(M,max-(f+g))=0 by A4,A17,A18,Th93;
   consider C be Element of S such that
A20: C = dom(f+g) &
    integral+(M,f+g) = integral+(M,f|C) + integral+(M,g|C) by A1,A2,A3,Th84;
    C c= A & C c= B by A4,A20,XBOOLE_1:17;
   then integral+(M,f|C) <= integral+(M,f|A) &
   integral+(M,g|C) <= integral+(M,g|B) by A1,A2,A3,Th89;
then A21:integral+(M,f|C) <= integral+(M,max+f) &
   integral+(M,g|C) <= integral+(M,max+g) by A2,A3,A13,A16,GRFUNC_1:64;
A22:integral+(M,max+f) < +infty &
   integral+(M,max+g) < +infty by A1,Def17;
   then integral+(M,max+f) + integral+(M,max+g) <> +infty by SUPINF_2:8;
then A23: integral+(M,max+f) + integral+(M,max+g) < +infty by XXREAL_0:4;
    integral+(M,max+(f+g)) <= integral+(M,max+f) + integral+(M,max+g)
        by A10,A20,A21,A22,SUPINF_2:14;
   then integral+(M,max+(f+g)) < +infty by A23,XXREAL_0:4;
   hence thesis by A4,A19,Def17;
end;

theorem Th113:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
f is_integrable_on M & g is_integrable_on M holds dom (f+g) in S
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL;
   assume f is_integrable_on M & g is_integrable_on M;
   then f"{+infty} in S & f"{-infty} in S & g"{+infty} in S & g"{-infty} in S
&
   (ex E1 be Element of S st E1=dom f & f is_measurable_on E1) &
   (ex E2 be Element of S st E2=dom g & g is_measurable_on E2)
     by Def17,Th111;
   hence dom(f+g) in S by Th52;
end;

Lm12:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
 (ex E0 be Element of S st dom(f+g) = E0 & f+g is_measurable_on E0) &
 f is_integrable_on M & g is_integrable_on M
 holds f+g is_integrable_on M
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL;
   assume
A1:(ex E0 be Element of S st dom(f+g) = E0 & f+g is_measurable_on E0) &
   f is_integrable_on M & g is_integrable_on M;
   then consider E be Element of S such that
A2: dom(f+g) = E & f+g is_measurable_on E;
A3:dom(f+g)
    = (dom f /\ dom g)\(f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty})
      by MESFUNC1:def 3;
   then dom(f+g) c= dom f /\ dom g by XBOOLE_1:36;
then A4:dom(f+g) c= dom f & dom(f+g) c= dom g by XBOOLE_1:18;
then A5:E c= dom |.f.| & E c= dom |.g.| by A2,MESFUNC1:def 10;
   consider E1 be Element of S such that
A6: E1=dom f & f is_measurable_on E1 by A1,Def17;
   consider E2 be Element of S such that
A7: E2=dom g & g is_measurable_on E2 by A1,Def17;
    |.f.| is_integrable_on M & |.g.| is_integrable_on M
     by A1,A6,A7,Th106;
then A8:(|.f.|)|E is_integrable_on M & (|.g.|)|E is_integrable_on M by Th103;
    f is_measurable_on E & g is_measurable_on E by A2,A4,A6,A7,MESFUNC1:34;
then A9:|.f.| is_measurable_on E & |.g.| is_measurable_on E by A2,A4,
MESFUNC2:29;
    |.f.| is nonnegative & |.g.| is nonnegative by Lm1;
then A10:(|.f.|)|E is nonnegative & (|.g.|)|E is nonnegative by Th21;
then A11:(|.f.|)|E + (|.g.|)|E is_integrable_on M by A8,Th112;
    dom |.f.| /\ E = E & dom |.g.| /\ E = E by A5,XBOOLE_1:28;
then A12:E = dom((|.f.|)|E) & (|.f.|)|E is_measurable_on E &
   E = dom((|.g.|)|E) & (|.g.|)|E is_measurable_on E
     by A9,Th48,FUNCT_1:68;
then A13:dom((|.f.|)|E) /\ dom((|.g.|)|E) = E /\ E;
    for x be set st x in dom f holds f.x in ExtREAL;
   then reconsider ff=f as Function of dom f, ExtREAL by FUNCT_2:5;
    for x be set st x in dom g holds g.x in ExtREAL;
   then reconsider gg=g as Function of dom g, ExtREAL by FUNCT_2:5;
A14:now let x be Element of X;
    assume
 A15: x in dom(f+g);
    then x in dom |.f.| & x in dom |.g.| by A2,A5;
then A16:x in dom ff & x in dom gg by MESFUNC1:def 10;
A17:x in dom((|.f.|)|E+(|.g.|)|E) by A2,A10,A13,A15,Th28;
     not ((f.x = +infty & g.x = -infty) or (f.x = -infty & g.x = +infty))
    proof
     assume
 A18:  (f.x = +infty & g.x = -infty) or (f.x = -infty & g.x = +infty);
     per cases by A18;
     suppose f.x = +infty & g.x = -infty;
      then ff.x in {+infty} & gg.x in {-infty} by TARSKI:def 1;
      then x in ff"{+infty} & x in gg"{-infty} by A16,FUNCT_2:46;
  then A19:x in (ff"{+infty} /\ gg"{-infty}) by XBOOLE_0:def 3;
       ff"{+infty} /\ gg"{-infty}
       c= ff"{+infty}/\gg"{-infty} \/ ff"{-infty}/\gg"{+infty} by XBOOLE_1:7;
      hence contradiction by A3,A15,A19,XBOOLE_0:def 4;
     end;
     suppose f.x = -infty & g.x = +infty;
      then ff.x in {-infty} & gg.x in {+infty} by TARSKI:def 1;
      then x in ff"{-infty} & x in gg"{+infty} by A16,FUNCT_2:46;
  then A20:x in (ff"{-infty} /\ gg"{+infty}) by XBOOLE_0:def 3;
       ff"{-infty} /\ gg"{+infty}
       c= ff"{+infty}/\gg"{-infty} \/ ff"{-infty}/\gg"{+infty} by XBOOLE_1:7;
      hence contradiction by A3,A15,A20,XBOOLE_0:def 4;
     end;
    end;
 then A21:|.f.x+g.x.| <= |.f.x.| +|.g.x.| by EXTREAL2:61;
     |.f.x.| +|.g.x.|
      = (|.f.|).x +|.g.x.| by A2,A5,A15,MESFUNC1:def 10
     .= (|.f.|).x +(|.g.|).x by A2,A5,A15,MESFUNC1:def 10
     .= ((|.f.|)|E).x +(|.g.|).x by A2,A12,A15,FUNCT_1:68
     .= ((|.f.|)|E).x +((|.g.|)|E).x by A2,A12,A15,FUNCT_1:68
     .= ( (|.f.|)|E+(|.g.|)|E ).x by A17,MESFUNC1:def 3;
    hence |.(f+g).x.| <= ((|.f.|)|E+(|.g.|)|E).x by A15,A21,MESFUNC1:def 3;
   end;
    dom((|.f.|)|E+(|.g.|)|E) = E by A10,A13,Th28;
   hence f+g is_integrable_on M by A2,A11,A14,Th108;
end;

theorem Th114:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
 f is_integrable_on M & g is_integrable_on M holds
   f+g is_integrable_on M
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL such that
A1:f is_integrable_on M & g is_integrable_on M;
    (ex E1 be Element of S st E1=dom f & f is_measurable_on E1) &
   (ex E2 be Element of S st E2=dom g & g is_measurable_on E2)
     by A1,Def17;
   then ex K0 be Element of S st K0 = dom(f+g) & f+g is_measurable_on K0
     by Th53;
   hence thesis by A1,Lm12;
end;

Lm13:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
 f is_integrable_on M & g is_integrable_on M & dom f = dom g holds
  ex E,NFG,NFPG be Element of S st
    E c= dom f & NFG c= dom f & E = dom f \ NFG &
    f|E is finite & E = dom(f|E) & f|E is_measurable_on E &
    f|E is_integrable_on M & Integral(M,f)=Integral(M,f|E) &
    E c= dom g & NFG c= dom g & E = dom g \ NFG &
    g|E is finite & E = dom(g|E) & g|E is_measurable_on E &
    g|E is_integrable_on M & Integral(M,g)=Integral(M,g|E) &
    E c= dom(f+g) & NFPG c= dom(f+g) & E = dom(f+g) \ NFPG &
    M.NFG = 0 &M.NFPG = 0 & E = dom((f+g)|E) &
    (f+g)|E is_measurable_on E & (f+g)|E is_integrable_on M &
    (f+g)|E = f|E + g|E &
    Integral(M,(f+g)|E)=Integral(M,f|E)+Integral(M,g|E)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL;
   assume that
A1: f is_integrable_on M & g is_integrable_on M & dom f = dom g;
   consider E0 be Element of S such that
A2: E0=dom f & f is_measurable_on E0 by A1,Def17;
A3:ex Gf be Element of S st
    Gf=dom g & g is_measurable_on Gf by A1,Def17;
   reconsider NF = f"{+infty} \/ f"{-infty} as Element of S by A1,Th111;
   reconsider NG = g"{+infty} \/ g"{-infty} as Element of S by A1,Th111;
   set NFG= NF \/ NG;
    M.NF = 0 & M.NG = 0 by A1,Th111;
   then NF is measure_zero of M & NG is measure_zero of M by MEASURE1:def 13;
then A4:NFG is measure_zero of M by MEASURE1:70;
then A5:M.NFG=0 by MEASURE1:def 13;
   set E = E0 \ NFG;
    for x be set holds
    ( x in f"{+infty} implies x in dom f ) &
    ( x in f"{-infty} implies x in dom f ) &
    ( x in g"{+infty} implies x in dom g ) &
    ( x in g"{-infty} implies x in dom g ) by FUNCT_1:def 13;
    then f"{+infty} c= dom f & f"{-infty} c= dom f &
    g"{+infty} c= dom g & g"{-infty} c= dom g by TARSKI:def 3;
    then NF c= dom g & NG c= dom g by A1,XBOOLE_1:8;
then A6:NF \/ NG c= dom g by XBOOLE_1:8;
A7:dom(f+g)
    = (dom f /\ dom g)\( f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty} )
      by MESFUNC1:def 3;
   set f1=f|E;
   set g1=g|E;
    f"{-infty} c= NF & g"{-infty} c= NG & f"{-infty}/\g"{+infty} c= f"{-infty}
&
   f"{+infty}/\g"{-infty} c= g"{-infty} by XBOOLE_1:7,17;
   then f"{-infty}/\g"{+infty} c= NF & f"{+infty}/\g"{-infty} c= NG
     by XBOOLE_1:1;
   then f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty} c= NF \/ NG
     by XBOOLE_1:13;
then A8:E c= dom(f+g) by A1,A2,A7,XBOOLE_1:34;
then A9:(f+g)|E = f1+g1 & dom(f1+g1)=E by Th35;
   reconsider DFPG = dom(f+g) as Element of S by A1,Th113;
   set NFPG=DFPG \ E;
    DFPG c= E0 by A1,A2,A7,XBOOLE_1:36;
   then DFPG \ (E0 \ NFG) c=E0 \ (E0 \ NFG) by XBOOLE_1:33;
then A10:NFPG c= E0 /\ NFG by XBOOLE_1:48;
    E0 /\ NFG c= NFG by XBOOLE_1:17;
   then NFPG c= NFG by A10,XBOOLE_1:1;
   then NFPG is measure_zero of M by A4,MEASURE1:69;
then A11:M.NFPG=0 by MEASURE1:def 13;
    DFPG \ NFPG = DFPG /\ E by XBOOLE_1:48;
then A12:E= DFPG \ NFPG by A8,XBOOLE_1:28;
A13:NFPG c= dom(f+g) by XBOOLE_1:36;
A14:E c= dom f & E c= dom g by A1,A2,XBOOLE_1:36;
then A15:f is_measurable_on E & g is_measurable_on E by A2,A3,MESFUNC1:34;
A16:dom f1 = dom f /\ E & dom g1 = dom g /\ E by FUNCT_1:68;
then A17:E = dom f1 & E = dom g1 by A14,XBOOLE_1:28;
then A18:f1 is_measurable_on E & g1 is_measurable_on E by A15,A16,Th48;
A19:
   for x be set st x in dom f1 holds f1.x < +infty & -infty < f1.x
   proof
    let x be set;
    assume
 A20: x in dom f1;
     for x be set st x in dom f holds f.x in ExtREAL;
    then reconsider ff=f as Function of dom f, ExtREAL by FUNCT_2:5;
     x in dom f /\ E by A20,FUNCT_1:68;
 then A21:x in dom f & x in E by XBOOLE_0:def 3;
 then A22:not x in NFG by XBOOLE_0:def 4;
 A23:now assume f1.x = +infty;
     then f.x = +infty by A20,FUNCT_1:68;
     then ff.x in {+infty} by TARSKI:def 1;
  then A24:x in ff"{+infty} by A21,FUNCT_2:46;
      f"{+infty} c= NF by XBOOLE_1:7;
     hence contradiction by A22,A24,XBOOLE_0:def 2;
    end;
     now assume f1.x = -infty;
     then f.x = -infty by A20,FUNCT_1:68;
     then ff.x in {-infty} by TARSKI:def 1;
  then A25:x in ff"{-infty} by A21,FUNCT_2:46;
      f"{-infty} c= NF by XBOOLE_1:7;
     hence contradiction by A22,A25,XBOOLE_0:def 2;
    end;
    hence thesis by A23,XXREAL_0:4,6;
   end;
   then (for x be set st x in dom f1 holds -infty < f1.x) &
   (for x be set st x in dom f1 holds f1.x < +infty);
then A26:f1 is without-infty & f1 is without+infty by Th16,Th17;
A27:for x be set st x in dom g1 holds -infty < g1.x & g1.x < +infty
   proof
    let x be set;
    assume
 A28: x in dom g1;
     for x be set st x in dom g holds g.x in ExtREAL;
    then reconsider gg=g as Function of dom g, ExtREAL by FUNCT_2:5;
     x in dom g /\ E by A28,FUNCT_1:68;
 then A29:x in dom g & x in E by XBOOLE_0:def 3;
 then A30:not x in NFG by XBOOLE_0:def 4;
 A31:now assume g1.x = +infty;
     then g.x = +infty by A28,FUNCT_1:68;
     then gg.x in {+infty} by TARSKI:def 1;
  then A32:x in gg"{+infty} by A29,FUNCT_2:46;
      g"{+infty} c= NG by XBOOLE_1:7;
     hence contradiction by A30,A32,XBOOLE_0:def 2;
    end;
     now assume g1.x = -infty;
     then g.x = -infty by A28,FUNCT_1:68;
     then gg.x in {-infty} by TARSKI:def 1;
  then A33:x in gg"{-infty} by A29,FUNCT_2:46;
      g"{-infty} c= NG by XBOOLE_1:7;
     hence contradiction by A30,A33,XBOOLE_0:def 2;
    end;
    hence thesis by A31,XXREAL_0:4,6;
   end;
   then (for x be set st x in dom g1 holds -infty < g1.x) &
   (for x be set st x in dom g1 holds g1.x < +infty);
then A34:g1 is without-infty & g1 is without+infty by Th16,Th17;
    now let x be Element of X;
    assume x in dom f1;
 then A35:-infty < f1.x & f1.x < +infty by A19;
     -(+infty) = -infty by SUPINF_2:def 3;
    hence |.f1.x.| < +infty by A35,EXTREAL2:59;
   end;
then A36:f1 is finite by MESFUNC2:def 1;
    now let x be Element of X;
    assume x in dom g1;
 then A37:-infty < g1.x & g1.x < +infty by A27;
     -(+infty) = -infty by SUPINF_2:def 3;
    hence |.g1.x.| < +infty by A37,EXTREAL2:59;
   end;
then A38:g1 is finite by MESFUNC2:def 1;
then A39:E =dom(f1+g1) & f1+g1 is_measurable_on E by A8,A18,A36,Th35,MESFUNC2:7
;
A40:f1 is_integrable_on M & g1 is_integrable_on M by A1,Th103;
then A41:f1+g1 is_integrable_on M by Th114;
    (for x be set st x in dom f1 holds -infty < f1.x) &
   (for x be set st x in dom g1 holds -infty < g1.x) by A19,A27;
then A42:f1 is without-infty & g1 is without-infty by Th16;
   then dom(max+(f1+g1) + max-f1) = dom f1 /\ dom g1 &
   dom(max-(f1+g1) + max+f1) = dom f1 /\ dom g1 by Th30;
then A43:dom(max+(f1+g1) + max-f1) = E & dom(max-g1) = E &
   dom(max-(f1+g1) + max+f1) = E & dom(max+g1) = E
     by A17,MESFUNC2:def 2,def 3;
A44:max+(f1+g1) + max-f1 is nonnegative & max-(f1+g1) + max+f1 is nonnegative
      by A42,Th30;
A45:dom(max-f1)= dom f1 & dom(max+f1)= dom f1 &
   dom(max-g1)= dom g1 & dom(max+g1)= dom g1 &
   dom(max+(f1+g1))= dom(f1+g1) & dom(max-(f1+g1))= dom(f1+g1)
     by MESFUNC2:def 2,def 3;
A46:max+f1 is nonnegative & max-f1 is nonnegative &
   max-g1 is nonnegative & max+g1 is nonnegative &
   max+(f1+g1) is nonnegative & max-(f1+g1) is nonnegative by Lm1;
A47:max+(f1+g1) is_measurable_on E & max-(f1+g1) is_measurable_on E
     by A39,MESFUNC2:27,28;
A48:max-f1 is_measurable_on E by A17,A18,MESFUNC2:28;
A49:max+(f1+g1) + max-f1 + max-g1 = max-(f1+g1) + max+f1 + max+g1
     by A26,A34,Th31;
A50:max+g1 is_measurable_on E & max+f1 is_measurable_on E by A18,MESFUNC2:27;
    dom f1 /\ dom g1 = E by A17;
   then max-(f1+g1) + max+f1 is_measurable_on E by A18,A42,Th50;
then A51:integral+(M,max-(f1+g1)+max+f1+max+g1)
     =integral+(M,max-(f1+g1) + max+f1) + integral+(M,max+g1)
        by A43,A44,A46,A50,Lm11
    .=integral+(M,max-(f1+g1)) + integral+(M,max+f1) + integral+(M,max+g1)
        by A9,A17,A45,A46,A47,A50,Lm11;
A52:0 <= integral+(M,max+f1) & integral+(M,max+f1) <> +infty &
   0 <= integral+(M,max-f1) & integral+(M,max-f1) <> +infty &
   0 <= integral+(M,max+g1) & integral+(M,max+g1) <> +infty &
   0 <= integral+(M,max-g1) & integral+(M,max-g1) <> +infty &
   0 <= integral+(M,max+(f1+g1)) & integral+(M,max+(f1+g1)) <> +infty &
   0 <= integral+(M,max-(f1+g1)) & integral+(M,max-(f1+g1)) <> +infty
        by A40,A41,Def17,Th102;
then A53:-integral+(M,max-f1) <> -infty & -integral+(M,max-f1) <> +infty &
   -integral+(M,max-(f1+g1)) <> +infty & -integral+(M,max-(f1+g1)) <> -infty
         by EXTREAL1:4;
    max+(f1+g1) + max-f1 is_measurable_on E & max-g1 is_measurable_on E
    by A17,A18,A42,Th49,MESFUNC2:28;
   then integral+(M,max+(f1+g1)+max-f1+max-g1)
    =integral+(M,max+(f1+g1)+max-f1) + integral+(M,max-g1)
       by A43,A44,A46,Lm11
   .=integral+(M,max+(f1+g1)) + integral+(M,max-f1) + integral+(M,max-g1)
       by A9,A17,A45,A46,A47,A48,Lm11;
   then integral+(M,max+(f1+g1))+integral+(M,max-f1)+
    (integral+(M,max-g1) -integral+(M,max-g1) )
    = integral+(M,max-(f1+g1))+integral+(M,max+f1)
       + integral+(M,max+g1) - integral+(M,max-g1)
          by A49,A51,A52,EXTREAL1:11;
   then integral+(M,max+(f1+g1))+integral+(M,max-f1)+
    (integral+(M,max-g1) -integral+(M,max-g1) )
    = integral+(M,max-(f1+g1))+integral+(M,max+f1)
       + (integral+(M,max+g1) -integral+(M,max-g1)) by A52,EXTREAL1:11;
   then integral+(M,max+(f1+g1))+integral+(M,max-f1)+ 0.
    = integral+(M,max-(f1+g1))+integral+(M,max+f1)
       + (integral+(M,max+g1) -integral+(M,max-g1)) by EXTREAL1:9;
   then integral+(M,max+(f1+g1))+integral+(M,max-f1)
    = integral+(M,max-(f1+g1))+integral+(M,max+f1)+(integral+(M,max+g1)
       - integral+(M,max-g1)) by SUPINF_2:18;
   then -integral+(M,max-(f1+g1))+ integral+(M,max+(f1+g1))+integral+(M,max-f1
)
    = -integral+(M,max-(f1+g1))+ ( integral+(M,max-(f1+g1))
        +integral+(M,max+f1)+(integral+(M,max+g1) -integral+(M,max-g1)) )
         by A52,EXTREAL1:8;
   then -integral+(M,max-(f1+g1))+ integral+(M,max+(f1+g1))+ integral+(M,max-
f1)
    = -integral+(M,max-(f1+g1))+(integral+(M,max-(f1+g1))
        +(integral+(M,max+f1)+(integral+(M,max+g1)-integral+(M,max-g1))))
         by A52,EXTREAL1:8;
   then -integral+(M,max-(f1+g1))+ integral+(M,max+(f1+g1))+ integral+(M,max-
f1)
    = -integral+(M,max-(f1+g1))+integral+(M,max-(f1+g1))
        +(integral+(M,max+f1)+(integral+(M,max+g1)-integral+(M,max-g1)))
         by A52,A53,EXTREAL1:8;
   then -integral+(M,max-(f1+g1))+ integral+(M,max+(f1+g1))+ integral+(M,max-
f1)
    = R_EAL 0 + (integral+(M,max+f1) + (integral+(M,max+g1)
       -integral+(M,max-g1))) by EXTREAL1:9;
   then -integral+(M,max-(f1+g1)) + integral+(M,max+(f1+g1))+ integral+(M,max-
f1)
    = integral+(M,max+f1) + (integral+(M,max+g1) -integral+(M,max-g1))
       by SUPINF_2:18;
   then -integral+(M,max-f1)+ integral+(M,max-f1)
     +(-integral+(M,max-(f1+g1)) + integral+(M,max+(f1+g1)))
    = -integral+(M,max-f1)+(integral+(M,max+f1)
     +(integral+(M,max+g1)-integral+(M,max-g1))) by A52,A53,EXTREAL1:8;
   then -integral+(M,max-f1) + integral+(M,max-f1)
     + (-integral+(M,max-(f1+g1)) + integral+(M,max+(f1+g1)))
    = -integral+(M,max-f1)+ integral+(M,max+f1)+(integral+(M,max+g1)
     -integral+(M,max-g1)) by A52,A53,EXTREAL1:8;
   then R_EAL 0 + (-integral+(M,max-(f1+g1)) + integral+(M,max+(f1+g1)))
    = (-integral+(M,max-f1)+integral+(M,max+f1))+(integral+(M,max+g1)
     -integral+(M,max-g1)) by EXTREAL1:9;
then A54:Integral(M,(f1+g1))=Integral(M,f1)+Integral(M,g1) by SUPINF_2:18;
A55:Integral(M,f) =Integral(M,f1) by A2,A5,Th101;
    Integral(M,g) =Integral(M,g1) by A1,A2,A3,A5,Th101;
   hence thesis by A1,A2,A5,A6,A8,A9,A11,A12,A13,A14,A17,A18,A36,A38,A39,A40
,A41,A54,A55;
end;

Lm14:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL  st
 (f is_integrable_on M & g is_integrable_on M & dom f = dom g) holds
   ( f+g is_integrable_on M & Integral(M,f+g) =Integral(M,f)+Integral(M,g) )
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL;
   assume
A1: f is_integrable_on M & g is_integrable_on M & dom f = dom g;
   hence A2:f+g is_integrable_on M by Th114;
A3:ex E,NFG,NFPG be Element of S st
    E c= dom f & NFG c= dom f &
    E = (dom f) \ NFG &
    f|E is finite & E = dom(f|E) & f|E is_measurable_on E &
    f|E is_integrable_on M &
    Integral(M,f)=Integral(M,f|E) &
    E c= dom g & NFG c= dom g & E = dom g \ NFG &
    g|E is finite & E = dom(g|E) & g|E is_measurable_on E &
    g|E is_integrable_on M &
    Integral(M,g)=Integral(M,g|E) &
    E c= dom(f+g) & NFPG c= dom(f+g) & E = dom(f+g) \ NFPG &
    M.NFG = 0 &M.NFPG = 0 &
    E = dom((f+g)|E) & (f+g)|E is_measurable_on E &
    (f+g)|E is_integrable_on M & (f+g)|E = f|E + g|E &
    Integral(M,(f+g)|E)=Integral(M,f|E)+Integral(M,g|E) by A1,Lm13;
    ex K0 be Element of S st
    K0 = dom(f+g) & f+g is_measurable_on K0 by A2,Def17;
   hence thesis by A3,Th101;
end;

theorem
 for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL st
 f is_integrable_on M & g is_integrable_on M holds
  ex E be Element of S st
    E = dom f /\ dom g & Integral(M,f+g)=Integral(M,f|E)+Integral(M,g|E)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL;
   assume
A1:f is_integrable_on M & g is_integrable_on M;
   then consider A be Element of S such that
A2: A = dom f & f is_measurable_on A by Def17;
   consider B be Element of S such that
A3: B = dom g & g is_measurable_on B by A1,Def17;
   set E = A /\ B;
   set f1 = f|E;
   set g1 = g|E;
A4:f1 is_integrable_on M & g1 is_integrable_on M by A1,Th103;
A5:dom f1 = dom f /\ (A/\B) by FUNCT_1:68
         .= A /\ A /\ B by A2,XBOOLE_1:16;
A6:dom g1 = dom g /\ (A/\B) by FUNCT_1:68
         .= B /\ B /\ A by A3,XBOOLE_1:16;
then A7:Integral(M,f1+g1) = Integral(M,f1) + Integral(M,g1) by A4,A5,Lm14;
   take E = A /\ B;
   thus E = dom f /\ dom g by A2,A3;
A8:f1"{-infty} = E /\ (f"{-infty}) &
   g1"{-infty} = E /\ (g"{-infty}) &
   f1"{+infty} = E /\ (f"{+infty}) &
   g1"{+infty} = E /\ (g"{+infty}) by FUNCT_1:139;
then A9:f1"{-infty} /\ g1"{+infty}
     = f"{-infty} /\ E /\ E /\ g"{+infty} by XBOOLE_1:16
    .= f"{-infty} /\ (E /\ E) /\ g"{+infty} by XBOOLE_1:16
    .= E /\ (f"{-infty} /\ g"{+infty}) by XBOOLE_1:16;
    f1"{+infty} /\ g1"{-infty}
     = f"{+infty} /\ E /\ E /\ g"{-infty} by A8,XBOOLE_1:16
    .= f"{+infty} /\ (E /\ E) /\ g"{-infty} by XBOOLE_1:16
    .= E /\ (f"{+infty} /\ g"{-infty}) by XBOOLE_1:16;
then A10:f1"{-infty}/\g1"{+infty} \/ f1"{+infty}/\g1"{-infty}
     = E /\ (f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty})
       by A9,XBOOLE_1:23;
A11:dom(f1+g1) = (dom f1 /\ dom g1)
        \ (f1"{-infty}/\g1"{+infty} \/ f1"{+infty}/\g1"{-infty})
             by MESFUNC1:def 3
   .= E \ (f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty})
         by A5,A6,A10,XBOOLE_1:47
   .= dom(f+g) by A2,A3,MESFUNC1:def 3;
    for x be set st x in dom(f1+g1) holds (f1+g1).x = (f+g).x
   proof
    let x be set;
    assume A12:x in dom(f1+g1);
    then x in (dom f1 /\ dom g1)
        \ (f1"{-infty} /\ g1"{+infty} \/ f1"{+infty} /\g1"{-infty})
          by MESFUNC1:def 3;
    then x in dom f1 /\ dom g1 by XBOOLE_0:def 4;
 then A13:x in dom f1 & x in dom g1 by XBOOLE_0:def 3;
     (f1+g1).x = f1.x + g1.x by A12,MESFUNC1:def 3
             .= f.x + g1.x by A13,FUNCT_1:70
             .= f.x + g.x by A13,FUNCT_1:70;
    hence thesis by A11,A12,MESFUNC1:def 3;
   end;
   hence thesis by A7,A11,FUNCT_1:9;
end;

theorem Th116:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    c be Real st
 f is_integrable_on M holds
  c(#)f is_integrable_on M &
  Integral(M,c(#)f) = R_EAL c * Integral(M,f)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       c be Real such that
A1:f is_integrable_on M;
   consider A be Element of S such that
A2: A = dom f & f is_measurable_on A by A1,Def17;
A3:c(#)f is_measurable_on A & dom(c(#)f) =A by A2,Th55,MESFUNC1:def 6;
A4:0 <= integral+(M,max+f) & integral+(M,max+f) <>+infty &
   0 <= integral+(M,max-f) & integral+(M,max-f) <>+infty
     by A1,Def17,Th102;
   then reconsider I1 = integral+(M,max+f) as Real by XXREAL_0:14;
   reconsider I2 = integral+(M,max-f) as Real by A4,XXREAL_0:14;
A5:max+f is nonnegative & max-f is nonnegative by Lm1;
A6:dom(max+f) = A & dom(max-f) = A by A2,MESFUNC2:def 2,def 3;
A7:max+f is_measurable_on A & max-f is_measurable_on A by A2,MESFUNC2:27,28;
   per cases;
   suppose A8:0 <= c;
then A9: max+(c(#)f)=c(#)max+f & max-(c(#)f)=c(#)max-f by Th32;
A10: integral+(M,c(#)max+f) = R_EAL c * integral+(M,max+f) &
    integral+(M,c(#)max-f) = R_EAL c * integral+(M,max-f)
      by A5,A6,A7,A8,Th92;
     c*I1 is Real & c*I2 is Real;
    then R_EAL c * integral+(M,max+f) is Real &
    R_EAL c * integral+(M,max-f) is Real by EXTREAL1:13;
    then integral+(M,max+(c(#)f)) < +infty &
    R_EAL c * integral+(M,max-f) < +infty by A9,A10,XXREAL_0:9;
    hence c(#)f is_integrable_on M by A3,A9,A10,Def17;
    thus Integral(M,c(#)f)
       =integral+(M,c(#)max+f) -integral+(M,max-(c(#)f)) by A8,Th32
      .=integral+(M,c(#)max+f) -integral+(M,c(#)max-f) by A8,Th32
      .=R_EAL c * integral+(M,max+f) - integral+(M,c(#)max-f)
         by A5,A6,A7,A8,Th92
      .=R_EAL c * integral+(M,max+f) - R_EAL c *integral+(M,max-f)
         by A5,A6,A7,A8,Th92
      .=R_EAL c * Integral(M,f) by A4,EXTREAL1:30;
   end;
   suppose c < 0;
then A11: -c > 0 by XREAL_1:60;
     -(-c)=c;
    then consider a be Real such that
A12:  c =-a & a > 0 by A11;
A13: max+(c(#)f)=a(#)max-f & max-(c(#)f)=a(#)max+f by A12,Th33;
A14: integral+(M,a(#)max-f) = R_EAL a * integral+(M,max-f) &
    integral+(M,a(#)max+f) = R_EAL a * integral+(M,max+f)
      by A5,A6,A7,A12,Th92;
     a*I2 is Real & a*I1 is Real;
    then R_EAL a *integral+(M,max-f) is Real &
    R_EAL(a)*integral+(M,max+f) is Real by EXTREAL1:13;
    then integral+(M,max+(c(#)f)) < +infty &
    R_EAL(a)*integral+(M,max+f) < +infty by A13,A14,XXREAL_0:9;
    hence c(#)f is_integrable_on M by A3,A13,A14,Def17;
A15: -R_EAL a = -a by SUPINF_2:3;
    thus Integral(M,c(#)f)
       =R_EAL a * integral+(M,max-f) -integral+(M,a(#)max+f)
         by A5,A6,A7,A12,A13,Th92
      .=R_EAL a * integral+(M,max-f)-R_EAL a * integral+(M,max+f)
         by A5,A6,A7,A12,Th92
      .=R_EAL a * (integral+(M,max-f)-integral+(M,max+f)) by A4,EXTREAL1:30
      .=R_EAL a * (-(integral+(M,max+f)-integral+(M,max-f))) by EXTREAL2:15
      .=-(R_EAL a * (integral+(M,max+f)-integral+(M,max-f))) by EXTREAL1:26
      .= R_EAL c * Integral(M,f) by A12,A15,EXTREAL1:26;
   end;
end;

definition
let X be non empty set;
let S be SigmaField of X;
let M be sigma_Measure of S;
let f be PartFunc of X,ExtREAL;
let B be Element of S;
func Integral_on(M,B,f) -> Element of ExtREAL equals
  Integral(M,f|B);
coherence;
end;

theorem
 for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL,
    B be Element of S st
 f is_integrable_on M & g is_integrable_on M & B c= dom(f+g)
holds
   f+g is_integrable_on M &
   Integral_on(M,B,f+g) = Integral_on(M,B,f) + Integral_on(M,B,g)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL,
       B be Element of S such that
A1:f is_integrable_on M & g is_integrable_on M & B c= dom(f+g);
    dom(f+g) = (dom f /\ dom g) \
    (f"{-infty}/\g"{+infty} \/ f"{+infty}/\g"{-infty}) by MESFUNC1:def 3;
   then dom(f+g) c= dom f /\ dom g &
   dom f /\ dom g c= dom f & dom f /\ dom g c= dom g by XBOOLE_1:17,36;
   then dom(f+g) c= dom f & dom(f+g) c= dom g by XBOOLE_1:1;
then A2:B c= dom f & B c= dom g by A1,XBOOLE_1:1;
    dom(f|B) = dom f /\ B & dom(g|B) = dom g /\ B by FUNCT_1:68;
then A3:f|B is_integrable_on M & dom(f|B) = B &
   g|B is_integrable_on M & dom(g|B) = B by A1,A2,Th103,XBOOLE_1:28;
    Integral_on(M,B,f+g) =Integral(M,f|B+g|B) by A1,Th35;
   hence thesis by A1,A3,Lm14,Th114;
end;

theorem
 for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f be PartFunc of X,ExtREAL,
    c be Real,
    B be Element of S st
 f is_integrable_on M & f is_measurable_on B holds
  f|B is_integrable_on M &
  Integral_on(M,B,c(#)f) = R_EAL c * Integral_on(M,B,f)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f be PartFunc of X,ExtREAL,
       c be Real,
       B be Element of S such that
A1:f is_integrable_on M & f is_measurable_on B;
ex A be Element of S st
    A = dom f & f is_measurable_on A by A1,Def17;
   then reconsider BF=dom f /\ B as Element of S by FINSUB_1:def 2;
A2: f|B is_integrable_on M by A1,Th103;
    dom((c(#)f)|B) = dom(c(#)f) /\ B by FUNCT_1:68;
   then dom((c(#)f)|B) = dom f /\ B by MESFUNC1:def 6;
   then dom((c(#)f)|B) = dom(f|B) by FUNCT_1:68;
then A3:dom((c(#)f)|B) = dom(c(#)(f|B)) by MESFUNC1:def 6;
    for x be set st x in dom((c(#)f)|B) holds (c(#)f)|B.x = (c(#)(f|B)).x
   proof
    let x be set;
    assume
A4: x in dom ((c(#)f)|B);
    then x in dom (c(#)f) /\ B by FUNCT_1:68;
then A5: x in dom f /\ B & x in dom (c(#)f) by MESFUNC1:def 6,XBOOLE_0:def 3;
then A6: x in dom (f|B) by FUNCT_1:68;
then A7: x in dom (c(#)(f|B)) by MESFUNC1:def 6;
     (c(#)f)|B.x= (c(#)f).x by A4,FUNCT_1:70;
    then (c(#)f)|B.x= R_EAL c * f.x by A5,MESFUNC1:def 6;
    then (c(#)f)|B.x= R_EAL c * f|B.x by A6,FUNCT_1:70;
    hence (c(#)f)|B.x= (c(#)(f|B)).x by A7,MESFUNC1:def 6;
   end;
   then (c(#)f)|B = c(#)(f|B) by A3,FUNCT_1:9;
   hence thesis by A2,Th116;
end;
