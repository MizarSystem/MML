:: Complex Banach Space of Bounded Complex Sequences
::  by Noboru Endou
::
:: Received March 18, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies PRE_TOPC, NORMSP_1, RLVECT_1, FUNCT_1, ARYTM, ARYTM_1, ARYTM_3,
      RELAT_1, ABSVALUE, ORDINAL2, LATTICES, RLSUB_1, SEQ_1, FUNCT_2, FUNCOP_1,
      SEQ_2, SEQM_3, BHSP_3, RSSPACE, RSSPACE3, LOPBAN_1, CSSPACE4, CLVECT_1,
      COMSEQ_1, COMPLEX1, CSSPACE, CFUNCT_1, CLOPBAN1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, MCART_1, RELSET_1, PRE_TOPC,
      DOMAIN_1, FUNCOP_1, STRUCT_0, XREAL_0, ORDINAL1, NUMBERS, REAL_1, NAT_1,
      FUNCT_1, FUNCT_2, INTEGRA2, PSCOMP_1, RLVECT_1, COMPLEX1, COMSEQ_1,
      CFUNCT_1, COMSEQ_2, NORMSP_1, SEQ_1, SEQ_2, SEQM_3, CLVECT_1, SEQ_4,
      PARTFUN1, CSSPACE, LOPBAN_1, CSSPACE3, CLOPBAN1;
 constructors REAL_1, INTEGRA2, NAT_1, PSCOMP_1, DOMAIN_1, RAT_1, PARTFUN1,
      LOPBAN_1, COMSEQ_1, COMPLEX1, COMSEQ_2, CSSPACE3, CLOPBAN1, BINOP_2;
 registrations SUBSET_1, RELSET_1, STRUCT_0, RELAT_1, ORDINAL2, FUNCT_2, SEQ_1,
      XREAL_0, MEMBERED, COMSEQ_1, CLVECT_1, CSSPACE3, COMPLEX1, SEQM_3;
 requirements SUBSET, REAL, BOOLE, NUMERALS, ARITHM;
 definitions TARSKI, XBOOLE_0, SEQ_4;
 theorems RELAT_1, AXIOMS, TARSKI, ABSVALUE, ZFMISC_1, REAL_1, XBOOLE_0, NAT_1,
      REAL_2, FUNCOP_1, SEQ_1, SEQ_2, SEQM_3, SEQ_4, FUNCT_1, FUNCT_2,
      RLVECT_1, XREAL_0, XCMPLX_0, PSCOMP_1, INTEGRA2, RSSPACE2, CSSPACE,
      COMSEQ_2, COMPLEX1, CLVECT_1, COMSEQ_1, CSSPACE3, COMSEQ_3, CLOPBAN1,
      XCMPLX_1;
 schemes SEQ_1, FUNCT_2, XBOOLE_0, COMSEQ_1;

begin :: Complex Banach space of bounded complex sequences

Lm1:
 for rseq be Real_Sequence
 for K be real number st (for n be Nat holds rseq.n <= K)
 holds sup rng rseq <= K
 proof
   let rseq be Real_Sequence;
   let K be real number such that
   A1:  for n be Nat holds rseq.n <= K;
      now let s be real number such that
    A2: s in rng rseq;
       NAT= dom rseq by FUNCT_2:def 1;
    then rseq is Function of NAT, rng rseq by FUNCT_2:3;
    then consider n be set such that
    A3: n in NAT & rseq.n=s by A2,FUNCT_2:17;
    thus s <=K by A1,A3;
   end;
   hence thesis by SEQ_4:62;
end;

Lm2:
for rseq be Real_Sequence st rseq is bounded holds
   for n be Nat holds rseq.n <= sup rng rseq
proof
   let rseq be Real_Sequence such that
A1:rseq is bounded;
   let n be Nat;
      rng rseq is bounded by A1,PSCOMP_1:7;
then A2:  rng rseq is bounded_above by SEQ_4:def 3;
      NAT = dom rseq by FUNCT_2:def 1;
     then rseq.n in rng rseq by FUNCT_1:12;
   hence thesis by A2,SEQ_4:def 4;
end;

definition
func the_set_of_BoundedComplexSequences -> Subset of
     Linear_Space_of_ComplexSequences means :Def1:
   for x being set holds x in it iff
    (x in the_set_of_ComplexSequences & seq_id x is bounded);
existence
proof
   defpred P[set] means seq_id $1 is bounded;
   consider IT being set such that
A1:for x being set holds x in IT
    iff x in the_set_of_ComplexSequences & P[x] from XBOOLE_0:sch 1;
    IT is Subset of the_set_of_ComplexSequences
   proof
      for x be set st x in IT holds x in the_set_of_ComplexSequences by A1;
     hence thesis by TARSKI:def 3;
   end;
   hence thesis by A1,CSSPACE:def 7;
end;
uniqueness
proof
   let X1,X2 be Subset of Linear_Space_of_ComplexSequences;
   assume that
A2: for x being set holds x in X1 iff
     (x in the_set_of_ComplexSequences & seq_id(x) is bounded) and
A3: for x being set holds x in X2 iff
     (x in the_set_of_ComplexSequences & seq_id(x) is bounded);
    thus X1 c= X2
    proof
      let x be set;
      assume x in X1;
      then x in the_set_of_ComplexSequences & seq_id(x) is bounded by A2;
      hence thesis by A3;
    end;
    let x be set;
    assume x in X2;
    then x in the_set_of_ComplexSequences & seq_id(x) is bounded by A3;
    hence thesis by A2;
  end;
end;

Lm3:
for seq1,seq2 be Complex_Sequence st
 seq1 is bounded & seq2 is bounded holds
  seq1 + seq2 is bounded
proof
   let seq1,seq2 be Complex_Sequence;
   assume
A1:seq1 is bounded & seq2 is bounded;
   then consider r1 being Real such that
A2:0 < r1 & for n be Nat holds |.seq1.n.| < r1 by COMSEQ_2:8;
   consider r2 being Real such that
A3:0 < r2 & for n be Nat holds |.seq2.n.| < r2 by A1,COMSEQ_2:8;
    for n be Nat holds |.(seq1+seq2).n.| < r1+r2
   proof
     let n be Nat;
      |.(seq1+seq2).n.| = |.(seq1.n + seq2.n).| by COMSEQ_1:def 4;
then A4:  |.(seq1+seq2).n.| <= |.seq1.n.| + |.seq2.n.| by COMPLEX1:142;
      |.seq1.n.| < r1 & |.seq2.n.| < r2 by A2,A3;
     then |.seq1.n.| + |.seq2.n.| < r1 + r2 by REAL_1:67;
     hence thesis by A4,AXIOMS:22;
   end;
   hence thesis by COMSEQ_2:def 3;
end;

Lm4:
for c be Complex, seq be Complex_Sequence st seq is bounded holds
 c(#)seq is bounded
proof
   let c be Complex;
   let seq be Complex_Sequence such that
A1:seq is bounded;
   consider r be Real such that
A2:0 < r & for n be Nat holds |.seq.n.| < r by A1,COMSEQ_2:8;
    ex r1 be Real st for n be Nat holds |.(c(#)seq).n.| < r1
   proof
      now per cases;
       case A3:c = 0c;
A4:      for n be Nat holds |.(c(#)seq).n.| < 1
         proof
           let n be Nat;
            (c(#)seq).n = c * seq.n by COMSEQ_1:def 7
             .= 0 by A3;
           hence thesis by COMPLEX1:130;
         end;
         take r1 = 1;
         thus thesis by A4;
       end;
       case A5:c <> 0c;
A6:      for n be Nat holds |.(c(#)seq).n.| < |.c.|*r
         proof
           let n be Nat;
A7:        |.c.| > 0 & r > |.seq.n.| by A2,A5,COMPLEX1:133;
            |.(c(#)seq).n.| = |.c * seq.n.| by COMSEQ_1:def 7
            .= |.c.|*|.seq.n.| by COMPLEX1:151;
           hence thesis by A7,REAL_1:70;
         end;
         take r1 = |.c.|*r;
         thus thesis by A6;
       end;
     end;
     hence thesis;
   end;
   hence thesis by COMSEQ_2:def 3;
end;

registration
  cluster the_set_of_BoundedComplexSequences -> non empty;
coherence
proof
    seq_id CZeroseq is bounded
   proof
     reconsider seq=seq_id CZeroseq as Complex_Sequence;
      for n being Nat holds |.seq.n.| < 1 by CSSPACE:def 6,COMPLEX1:130;
     hence thesis by COMSEQ_2:8;
   end;
   hence thesis by Def1;
end;
  cluster the_set_of_BoundedComplexSequences -> lineary-closed;
  coherence
proof
   set W = the_set_of_BoundedComplexSequences;
A1:for v,u be VECTOR of Linear_Space_of_ComplexSequences st
    v in the_set_of_BoundedComplexSequences &
    u in the_set_of_BoundedComplexSequences
      holds v + u in the_set_of_BoundedComplexSequences
   proof
     let v,u be VECTOR of Linear_Space_of_ComplexSequences such that
A2:  v in W & u in W;
      seq_id(v+u) is bounded
     proof
A3:    seq_id v is bounded by A2,Def1;
A4:    seq_id u is bounded by A2,Def1;

        seq_id(v+u)=seq_id(seq_id v+seq_id u) by CSSPACE:4,def 7
                 .=seq_id v + seq_id u by CSSPACE:3;

       hence thesis by A3,A4,Lm3;
     end;
     hence v+u in W by Def1,CSSPACE:def 7;
   end;
    for c be Complex, v be VECTOR of Linear_Space_of_ComplexSequences st
    v in W holds c * v in W
   proof
     let c be Complex;
     let v be VECTOR of Linear_Space_of_ComplexSequences such that
A5: v in W;
      seq_id(c*v) is bounded
     proof
A6: seq_id v is bounded by A5,Def1;
        seq_id(c*v) =seq_id(c(#)seq_id(v)) by CSSPACE:5,def 7
                  .=c(#)seq_id(v) by CSSPACE:3;
       hence thesis by A6,Lm4;
     end;
     hence c*v in W by Def1,CSSPACE:def 7;
   end;
   hence thesis by A1,CLVECT_1:def 3;
end;
end;

canceled;
 Lm5:
CLSStruct (# the_set_of_BoundedComplexSequences,
  Zero_(the_set_of_BoundedComplexSequences,Linear_Space_of_ComplexSequences),
  Add_(the_set_of_BoundedComplexSequences,Linear_Space_of_ComplexSequences),
  Mult_(the_set_of_BoundedComplexSequences,Linear_Space_of_ComplexSequences) #)
is Subspace of Linear_Space_of_ComplexSequences by CSSPACE:13;

registration
cluster CLSStruct (# the_set_of_BoundedComplexSequences,
  Zero_(the_set_of_BoundedComplexSequences,Linear_Space_of_ComplexSequences),
  Add_(the_set_of_BoundedComplexSequences,Linear_Space_of_ComplexSequences),
  Mult_(the_set_of_BoundedComplexSequences,Linear_Space_of_ComplexSequences) #)
   -> Abelian add-associative right_zeroed right_complementable
        ComplexLinearSpace-like;
  coherence by CSSPACE:13;
end;

Lm6:
CLSStruct (# the_set_of_BoundedComplexSequences,
  Zero_(the_set_of_BoundedComplexSequences,Linear_Space_of_ComplexSequences),
  Add_(the_set_of_BoundedComplexSequences,Linear_Space_of_ComplexSequences),
  Mult_(the_set_of_BoundedComplexSequences,Linear_Space_of_ComplexSequences) #)
  is Abelian add-associative right_zeroed
    right_complementable ComplexLinearSpace-like;

Lm7:
ex NORM be Function of the_set_of_BoundedComplexSequences,REAL st
  for x be set st x in the_set_of_BoundedComplexSequences holds
    NORM.x = sup rng |.seq_id(x).|
proof
  deffunc F(set) = sup rng |.seq_id($1).|;
A1:for z be set st z in the_set_of_BoundedComplexSequences
   holds F(z) in REAL;
      ex f being Function of the_set_of_BoundedComplexSequences,REAL st
     for x being set st x in the_set_of_BoundedComplexSequences holds
       f.x = F(x) from FUNCT_2:sch 2(A1);
   hence thesis;
end;

definition
  func Complex_linfty_norm ->
  Function of the_set_of_BoundedComplexSequences, REAL means :Def2:
  for x be set st x in the_set_of_BoundedComplexSequences holds
    it.x = sup rng |.seq_id x.|;
existence by Lm7;
uniqueness
proof
   let NORM1,NORM2
   be Function of the_set_of_BoundedComplexSequences, REAL such that
A1:for x be set st x in the_set_of_BoundedComplexSequences holds
     NORM1.x = sup rng |.seq_id x.| and
A2:for x be set st x in the_set_of_BoundedComplexSequences holds
     NORM2.x = sup rng |.seq_id x.|;
A3:dom NORM1 = the_set_of_BoundedComplexSequences &
     dom NORM2 = the_set_of_BoundedComplexSequences by FUNCT_2:def 1;
      for z be set st z in the_set_of_BoundedComplexSequences holds
       NORM1.z = NORM2.z
   proof
     let z be set such that
A4:  z in the_set_of_BoundedComplexSequences;
      NORM1.z = sup rng |.seq_id(z).| by A1,A4;
     hence thesis by A2,A4;
  end;
  hence thesis by A3,FUNCT_1:9;
end;
end;

Lm8:
for seq be Complex_Sequence st (for n be Nat holds seq.n=0c) holds
      seq is bounded & sup rng |.seq.| = 0
proof
   let seq be Complex_Sequence such that
A1:for n be Nat holds seq.n=0c;
    for n be Nat holds |.seq.n.| < 1 by A1,COMPLEX1:130;
   hence seq is bounded by COMSEQ_2:def 3;
    NAT = dom |.seq.| by FUNCT_2:def 1;
then A2:|.seq.| is Function of NAT, rng |.seq.| by FUNCT_2:3;
    rng |.seq.| = {0}
   proof
      rng |.seq.| c= {0}
     proof
       let y be set;
       assume y in rng |.seq.|;
       then consider n be set such that
A3:    n in NAT & (|.seq.|).n=y by A2,FUNCT_2:17;
       reconsider n as Nat by A3;
        |.seq.| .n = |. seq.n .| by COMSEQ_1:def 14 .= |.0c.| by A1;
       hence y in {0} by A3,COMPLEX1:130,TARSKI:def 1;
     end;
     hence thesis by ZFMISC_1:39;
   end;
   hence thesis by SEQ_4:22;
end;

Lm9:
for seq be Complex_Sequence st seq is bounded holds
 |.seq.| is bounded
proof
   let seq be Complex_Sequence;
   assume seq is bounded;
   then consider r be Real such that
A1:for n be Nat holds |.seq.n.| < r by COMSEQ_2:def 3;
A2:|. seq.0 .| < r & 0 <= |. seq.0 .| by A1,COMPLEX1:132;
    for n be Nat holds abs (|.seq.| .n) < r
   proof
     let n be Nat;
      |.seq.| .n = |.seq.n.| by COMSEQ_1:def 14;
     then 0 <= |.seq.| .n by COMPLEX1:132;
     then abs(|.seq.| .n) = |.seq.| .n by ABSVALUE:def 1
      .= |.seq.n.| by COMSEQ_1:def 14;
     hence thesis by A1;
   end;
   hence thesis by A2,SEQ_2:15;
end;

Lm10:
for seq be Complex_Sequence st |.seq.| is bounded holds
 seq is bounded
proof
   let seq be Complex_Sequence;
   assume |.seq.| is bounded;
   then consider r be real number such that
A1:0 < r & for n be Nat holds abs(|.seq.| .n)<r by SEQ_2:15;
   reconsider r as Real by XREAL_0:def 1;
    for n be Nat holds |.seq.n.| < r
   proof
     let n be Nat;
      |.seq.| .n = |.seq.n.| by COMSEQ_1:def 14;
     then |.seq.| .n >= 0 by COMPLEX1:132;
     then abs(|.seq.| .n) = |.seq.| .n by ABSVALUE:def 1
      .= |.seq.n.| by COMSEQ_1:def 14;
     hence thesis by A1;
   end;
   hence thesis by COMSEQ_2:def 3;
end;

Lm11:
for seq be Complex_Sequence st
 ( seq is bounded & sup rng |.seq.| = 0 ) holds
  for n be Nat holds seq.n = 0c
proof
   let seq be Complex_Sequence such that
A1:seq is bounded and
A2:sup rng |.seq.| = 0;
   let n be Nat;

A3:|.seq.| is bounded by A1,Lm9;
    0 <= |.seq.n.| by COMPLEX1:132;
   then 0 <= |.seq.| .n by COMSEQ_1:def 14;
   then (|.seq.|).n =0 by A2,A3,Lm2;
   then |.seq.n.| = 0 by COMSEQ_1:def 14;
   hence thesis by COMPLEX1:131;
end;

theorem
 for seq be Complex_Sequence holds
 ( seq is bounded & sup rng |.seq.| = 0 ) iff
   for n be Nat holds seq.n = 0c by Lm8,Lm11;

registration
cluster CNORMSTR (# the_set_of_BoundedComplexSequences,
  Zero_(the_set_of_BoundedComplexSequences,Linear_Space_of_ComplexSequences),
  Add_(the_set_of_BoundedComplexSequences,Linear_Space_of_ComplexSequences),
  Mult_(the_set_of_BoundedComplexSequences,Linear_Space_of_ComplexSequences),
  Complex_linfty_norm #) -> Abelian add-associative right_zeroed
    right_complementable ComplexLinearSpace-like;
  coherence by Lm6,CSSPACE3:4;
end;

definition
func Complex_linfty_Space -> non empty CNORMSTR equals :Def3:
 CNORMSTR (# the_set_of_BoundedComplexSequences,
  Zero_(the_set_of_BoundedComplexSequences,Linear_Space_of_ComplexSequences),
  Add_(the_set_of_BoundedComplexSequences,Linear_Space_of_ComplexSequences),
  Mult_(the_set_of_BoundedComplexSequences,Linear_Space_of_ComplexSequences),
  Complex_linfty_norm #);
  coherence;
end;


theorem Th3:
the carrier of Complex_linfty_Space = the_set_of_BoundedComplexSequences &
( for x be set holds
   x is VECTOR of Complex_linfty_Space
    iff x is Complex_Sequence & seq_id x is bounded ) &
0.Complex_linfty_Space = CZeroseq &
( for u be VECTOR of Complex_linfty_Space holds u = seq_id u ) &
( for u,v be VECTOR of Complex_linfty_Space holds u+v =seq_id(u)+seq_id(v) ) &
( for c be Complex, u be VECTOR of Complex_linfty_Space holds
  c*u =c(#)seq_id(u) ) &
( for u be VECTOR of Complex_linfty_Space holds
   -u = -seq_id u & seq_id(-u) = -seq_id u ) &
( for u,v be VECTOR of Complex_linfty_Space holds u-v =seq_id(u)-seq_id(v) ) &
( for v be VECTOR of Complex_linfty_Space holds seq_id v is bounded ) &
( for v be VECTOR of Complex_linfty_Space holds
   ||.v.|| = sup rng |.seq_id v.| )
proof
   set l1 =Complex_linfty_Space;
A1:for x be set holds x is Element of l1
     iff x is Complex_Sequence & seq_id x is bounded
   proof
     let x be set;
      x in the_set_of_ComplexSequences iff x is Complex_Sequence
      by CSSPACE:def 1;
     hence thesis by Def1,Def3;
   end;
A2:for u be VECTOR of l1 holds u = seq_id u
   proof
     let u be VECTOR of l1;
        u is VECTOR of Linear_Space_of_ComplexSequences
     by Def3,Lm5,CLVECT_1:30;
     hence thesis by CSSPACE:def 2,def 7;
   end;
A3:for u,v be VECTOR of l1 holds u+v =seq_id u+seq_id v
   proof
     let u,v be VECTOR of l1;
     reconsider u1=u, v1 = v as VECTOR of Linear_Space_of_ComplexSequences
      by Def3,Lm5,CLVECT_1:30;
     set L1=Linear_Space_of_ComplexSequences;
     set W = the_set_of_BoundedComplexSequences;
      dom (the add of L1)
       = [:the carrier of L1,the carrier of L1:] by FUNCT_2:def 1;
then A4:  dom ((the add of Linear_Space_of_ComplexSequences) | [:W,W:])
       =[:W,W:] by RELAT_1:91;
      u+v =(the add of l1).[u,v] by RLVECT_1:def 3
        .=((the add of Linear_Space_of_ComplexSequences)|[:W,W:]).[u,v]
         by Def3,CSSPACE:def 8
        .=(the add of Linear_Space_of_ComplexSequences).[u,v]
         by A4,Def3,FUNCT_1:70
        .=u1+v1 by RLVECT_1:def 3;
     hence thesis by CSSPACE:4,def 7;
   end;

A5:for c be Complex, u be VECTOR of l1 holds c*u =c(#)seq_id u
   proof
     let c be Complex;
     let u be VECTOR of l1;
     reconsider u1=u as VECTOR of Linear_Space_of_ComplexSequences
      by Def3,Lm5,CLVECT_1:30;
     set L1=Linear_Space_of_ComplexSequences;
     set W = the_set_of_BoundedComplexSequences;
A6:  [:COMPLEX,W:] c= [:COMPLEX,the carrier of L1:] by ZFMISC_1:119;
      dom (the Mult of L1)
        = [:COMPLEX,the carrier of L1:] by FUNCT_2:def 1;
then A7:  dom ((the Mult of Linear_Space_of_ComplexSequences) | [:COMPLEX,W:])
       =[:COMPLEX,W:] by A6,RELAT_1:91;
      c*u =(the Mult of l1).[c,u] by CLVECT_1:def 1
        .=((the Mult of Linear_Space_of_ComplexSequences)|[:COMPLEX,W:]).[c,u]
           by Def3,CSSPACE:def 9
        .=(the Mult of Linear_Space_of_ComplexSequences).[c,u]
           by A7,Def3,FUNCT_1:70
        .=c*u1 by CLVECT_1:def 1;
     hence thesis by CSSPACE:5,def 7;
   end;

A8:for u be VECTOR of l1 holds -u =-seq_id u & seq_id(-u)=-seq_id u
   proof
     let u be VECTOR of l1;
         -u = (-1r)*u by Def3,CLVECT_1:4
        .= (-1r)(#)seq_id u by A5
        .= -seq_id u by COMSEQ_1:14;
     hence thesis by A2;
   end;
A9:for u,v be VECTOR of l1 holds u-v =seq_id u-seq_id v
   proof
     let u,v be VECTOR of l1;
     thus u-v = u+ -v by RLVECT_1:def 11
             .= seq_id u+seq_id(-v) by A3
             .= seq_id u+(-seq_id v) by A8
             .= seq_id u-seq_id v by COMSEQ_1:def 10;
   end;

A10:0.l1 = CZeroseq
   proof
     set W = the_set_of_BoundedComplexSequences;
     thus 0.l1 = Zero_(W,Linear_Space_of_ComplexSequences)
                  by Def3,RLVECT_1:def 2
              .= 0.Linear_Space_of_ComplexSequences by CSSPACE:def 10
              .= CZeroseq by CSSPACE:def 7,RLVECT_1:def 2;
   end;
    for v be VECTOR of l1 holds ||.v.|| = sup rng |.seq_id v.|
   proof
     let v be VECTOR of l1;
     thus ||.v.|| = (the norm of l1).v by CLVECT_1:def 9
                 .= sup rng |.seq_id v.| by Def2,Def3;
   end;
   hence thesis by A1,A2,A3,A5,A8,A9,A10,Def3;
end;

theorem Th4:
for x, y being Point of Complex_linfty_Space, c be Complex holds
  ( ||.x.|| = 0 iff x = 0.Complex_linfty_Space ) &
  0 <= ||.x.|| &
  ||.x+y.|| <= ||.x.|| + ||.y.|| &
  ||. c*x .|| = |.c.| * ||.x.||
proof
   let x, y be Point of Complex_linfty_Space;
   let c be Complex;
A1:now assume
A2:  ||.x.|| = 0;
A3:  ||.x.|| = sup rng |.seq_id x.| by Th3;
      seq_id x is bounded by Th3;
then A4:  for n be Nat holds 0c = (seq_id x).n by A2,A3,Lm11;
      x in the_set_of_ComplexSequences by Def1,Def3;
     hence x=0.Complex_linfty_Space by A4,Th3,CSSPACE:def 6;
   end;

A5:now assume
      x=0.Complex_linfty_Space;
then A6:  for n be Nat holds (seq_id x).n=0c by Th3,CSSPACE:def 6;
     thus ||.x.|| = sup rng |.seq_id x.| by Th3 .= 0 by A6,Lm8;
   end;

A7:seq_id x is bounded by Def1,Def3;
A8:0 <= sup rng |.seq_id x.|
   proof
A9:  |.seq_id x.| is bounded by A7,Lm9;
      |.seq_id x.| .1 =|.(seq_id x).1 .| by COMSEQ_1:def 14;
     then 0<= |.seq_id x.| .1 by COMPLEX1:132;
     hence 0 <= sup rng |.seq_id x.| by A9,Lm2;
   end;
A10:||.y.|| = sup rng |.seq_id y.| by Th3;
A11:sup rng |.seq_id(x+y).| = ||.x + y.|| by Th3;
A12:for n be Nat holds |.seq_id(x+y).| .n
       = |.(((seq_id x).n) + ((seq_id y).n)).|
   proof
     let n be Nat;
        (|.seq_id(x+y).|).n
         = (|.(seq_id(seq_id x+seq_id y)).|).n by Th3
        .= (|.(seq_id x+seq_id y).|).n by CSSPACE:3
        .= |.((seq_id x+seq_id y).n).| by COMSEQ_1:def 14
        .= |.(((seq_id x).n)+((seq_id y).n)).| by COMSEQ_1:def 4;
     hence thesis;
   end;
A13:for n be Nat holds (|.seq_id(x+y).|).n
        <= (|.seq_id x.|).n + (|.seq_id y.|).n
   proof
     let n be Nat;
      |.(((seq_id x).n)+ ((seq_id y).n)).|
       <= |.((seq_id x).n).| + |.((seq_id y).n).| by COMPLEX1:142;
     then (|.(seq_id(x+y)).|).n
       <= |.((seq_id x).n).| + |.((seq_id y).n).| by A12;
     then (|.seq_id(x+y).|).n
        <= (|.seq_id x.|).n + |.((seq_id y).n).| by COMSEQ_1:def 14;
     hence thesis by COMSEQ_1:def 14;
   end;
A14:for n being Nat holds
     (|.(seq_id(x+y)).|).n <= ((|.seq_id x.|) + (|.seq_id y.|)).n
   proof
     let n be Nat;
        (|.seq_id x.|).n + (|.seq_id y.|).n
        =((|.seq_id x.|) + (|.seq_id y.|)).n by SEQ_1:11;
     hence thesis by A13;
   end;
    seq_id x is bounded by Def1,Def3;
then A15:|.seq_id x.| is bounded by Lm9;
    seq_id y is bounded by Def1,Def3;
then A16:|.seq_id y.| is bounded by Lm9;
A17:sup rng |.seq_id(x+y).|
     <= sup rng |.seq_id x.| + sup rng |.seq_id y.|
   proof
      now let n be Nat;
A18:   (|.seq_id(x+y).|).n
         <= (|.seq_id x.| + |.seq_id y.|).n by A14;
A19:   (|.seq_id x.| + |.seq_id y.|).n
         = (|.seq_id x.|).n + (|.seq_id y.|).n by SEQ_1:11;
A20:   (|.seq_id x.|).n <=sup rng |.seq_id x.| by A15,Lm2;
        (|.seq_id y.|).n <=sup rng |.seq_id y.| by A16,Lm2;
       then ((|.seq_id x.|) + (|.seq_id y.|)).n
         <= sup rng |.seq_id x.| + sup rng |.seq_id y.|
           by A19,A20,REAL_1:55;
       hence (|.seq_id(x+y).|).n
         <= sup rng |.seq_id x.| + sup rng |.seq_id y.|
          by A18,AXIOMS:22;
     end;
     hence thesis by Lm1;
   end;

A21:for n be Nat holds
     (|.(c(#)seq_id x).|).n =|.c.|*(|.seq_id x.|).n
   proof
     let n be Nat;
      (|.c(#)seq_id x.|).n
       =|.((c(#)seq_id x).n).| by COMSEQ_1:def 14
      .=|.(c*((seq_id x).n)).| by COMSEQ_1:def 7
      .=|.c.|*(|.((seq_id x).n).|) by COMPLEX1:151
      .=|.c.|*(|.(seq_id x).| .n) by COMSEQ_1:def 14;
     hence thesis;
   end;
    seq_id x is bounded by Def1,Def3;
   then |.seq_id x.| is bounded by Lm9;
   then rng |.seq_id x.| is bounded by PSCOMP_1:7;
then A22:rng |.seq_id x.| is bounded_above by SEQ_4:def 3;
A23:0 <= |.c.| by COMPLEX1:132;
    ||. c*x .|| = sup rng |.seq_id(c*x).| by Th3
      .=sup rng |.(seq_id(c(#)seq_id x)).| by Th3
      .=sup(rng |.(c(#)seq_id x).|) by CSSPACE:3
      .=sup(rng (|.c.| (#) (|.seq_id x.|))) by A21,SEQ_1:13
      .=sup(|.c.|*rng |.seq_id x.|) by INTEGRA2:17
      .=|.c.|*sup rng |.seq_id x.| by A22,A23,INTEGRA2:13
      .=|.c.|*||.x.|| by Th3;
   hence thesis by A1,A5,A8,A10,A11,A17,Th3;
end;

registration
  cluster Complex_linfty_Space -> ComplexNormSpace-like ComplexLinearSpace-like
       Abelian add-associative right_zeroed right_complementable;
  coherence by Def3,Th4,CLVECT_1:def 10;
end;

reserve CNS for non empty ComplexNormSpace;
reserve seq for sequence of CNS;

Lm12:
for seq1,seq2,seq3 be Complex_Sequence holds
 seq1 = seq2 - seq3 iff
  for n be Nat holds seq1.n = seq2.n - seq3.n
proof
   let seq1,seq2,seq3 be Complex_Sequence;
A1:seq1 = seq2 - seq3 implies
    for n be Nat holds seq1.n = seq2.n - seq3.n
   proof
     assume seq1 = seq2 - seq3;
then A2:  seq1 = seq2 + -seq3 by COMSEQ_1:def 10;
      now let n be Nat;
        seq1.n = seq2.n + (-seq3).n by A2,COMSEQ_1:def 4
             .= seq2.n + -seq3.n by COMSEQ_1:def 9;
       hence seq1.n = seq2.n - seq3.n;
     end;
     hence thesis;
   end;
    (for n be Nat holds seq1.n = seq2.n - seq3.n) implies
    seq1 = seq2 - seq3
   proof
     assume
A3:  for n be Nat holds seq1.n = seq2.n - seq3.n;

      for x be set st x in NAT holds seq1.x = (seq2-seq3).x
     proof
       let x be set;
       assume x in NAT;
       then reconsider x as Nat;
        seq1.x = seq2.x - seq3.x by A3
             .= seq2.x + -seq3.x
             .= seq2.x + (-seq3).x by COMSEQ_1:def 9
             .= (seq2 + (-seq3)).x by COMSEQ_1:def 4;
       hence thesis by COMSEQ_1:def 10;
     end;
     hence thesis by FUNCT_2:18;
   end;
   hence thesis by A1;

end;

theorem Th5:
for vseq be sequence of Complex_linfty_Space st
  vseq is Cauchy_sequence_by_Norm holds vseq is convergent
proof
   let vseq be sequence of Complex_linfty_Space such that
A1:vseq is Cauchy_sequence_by_Norm;

   defpred P[set,set] means
    ex i be Nat st $1=i &
    ex cseqi be Complex_Sequence st
      (for n be Nat holds cseqi.n=(seq_id(vseq.n)).i) &
        cseqi is convergent & $2 = lim cseqi;
A2:for x be set st x in NAT ex y be set st y in COMPLEX & P[x,y]
   proof
     let x be set;
     assume x in NAT;
     then reconsider i=x as Nat;
     deffunc F(Nat) = (seq_id(vseq.$1)).i;
     consider cseqi be Complex_Sequence such that
A3:  for n be Nat holds cseqi.n = F(n) from COMSEQ_1:sch 1;
A4:  cseqi is convergent
     proof
        now
         let e be Real such that
A5:      e > 0;
         thus ex k be Nat st
          for m be Nat st k <= m holds |.(cseqi.m -cseqi.k).| < e
         proof
           consider k be Nat such that
A6:        for n, m be Nat st n >= k & m >= k holds
            ||.(vseq.n) - (vseq.m).|| < e by A1,A5,CSSPACE3:10;
            take k;
            let m be Nat such that
A7:         k<=m;
             ||.(vseq.m) - (vseq.k).||
             =sup rng |.(seq_id((vseq.m) - (vseq.k))).| by Th3;
then A8:         sup rng |.(seq_id((vseq.m) - (vseq.k))).| < e by A6,A7;
             seq_id((vseq.m)-(vseq.k)) is bounded by Def1,Def3;
            then |.(seq_id((vseq.m)-(vseq.k))).| is bounded by Lm9;
then A9:         |.(seq_id((vseq.m) - (vseq.k))).| .i
              <= sup rng |.(seq_id((vseq.m) - (vseq.k))).| by Lm2;
             seq_id((vseq.m) - (vseq.k))
              = seq_id(seq_id(vseq.m)-seq_id(vseq.k)) by Th3
             .= seq_id(vseq.m)-seq_id(vseq.k) by CSSPACE:3
             .= seq_id(vseq.m)+-seq_id(vseq.k) by COMSEQ_1:def 10;
            then (seq_id((vseq.m) - (vseq.k))).i
              = (seq_id(vseq.m)).i+(-seq_id(vseq.k)).i by COMSEQ_1:def 4
             .=(seq_id(vseq.m)).i+(-(seq_id(vseq.k)).i) by COMSEQ_1:def 9
             .=(seq_id(vseq.m)).i-(seq_id(vseq.k)).i
             .=cseqi.m -(seq_id(vseq.k)).i by A3
             .=cseqi.m - cseqi.k by A3;
            then |.(cseqi.m-cseqi.k).| = |.(seq_id((vseq.m)-(vseq.k))).| .i
              by COMSEQ_1:def 14;
            hence thesis by A8,A9,AXIOMS:22;
         end;
       end;
       hence thesis by COMSEQ_3:46;
     end;
     take y = lim cseqi;
     thus thesis by A3,A4;
   end;
   consider f be Function of NAT,COMPLEX such that
A10:for x be set st x in NAT holds P[x,f.x] from FUNCT_2:sch 1(A2);
   reconsider tseq=f as Complex_Sequence;
A11:now
     let i be Nat;
     reconsider x=i as set;
        ex i0 be Nat st x=i0 & ex cseqi be Complex_Sequence st
       ( for n be Nat holds cseqi.n=(seq_id(vseq.n)).i0 ) &
      cseqi is convergent & f.x=lim cseqi by A10;
      hence ex cseqi be Complex_Sequence st
       ( for n be Nat holds cseqi.n=(seq_id(vseq.n)).i ) &
       cseqi is convergent & tseq.i=lim cseqi;
   end;
A12:for e be Real st e >0
   ex k be Nat st
    for n be Nat st n >= k holds
     |.(seq_id tseq-seq_id(vseq.n)).| is bounded &
       sup rng |.(seq_id tseq-seq_id(vseq.n)).| <= e
   proof
     let e be Real such that
A13: e > 0;
     consider k be Nat such that
A14: for n, m be Nat st n >= k & m >= k holds
      ||.(vseq.n) - (vseq.m).|| < e by A1,A13,CSSPACE3:10;
A15: for m,n be Nat st n >= k & m >= k holds
      |.seq_id((vseq.n) - (vseq.m)).| is bounded &
        sup rng |.seq_id((vseq.n) - (vseq.m)).| < e
     proof
       let m,n be Nat such that
A16:   n >= k & m >= k;
        ||.(vseq.n) - (vseq.m).|| < e by A14,A16;
then A17:   (the norm of Complex_linfty_Space).((vseq.n) - (vseq.m))
         < e by CLVECT_1:def 9;
        seq_id((vseq.n) - (vseq.m)) is bounded by Def1,Def3;
       hence thesis by A17,Def2,Def3,Lm9;
     end;
A18: for n be Nat for i be Nat holds
      for rseq be Real_Sequence st
       ( for m be Nat holds rseq.m=|.(seq_id(vseq.m-vseq.n)).| .i ) holds
        ( rseq is convergent &
          lim rseq = |.((seq_id tseq -seq_id(vseq.n))).| .i )
     proof
       let n be Nat;
A19:   for m,k be Nat holds
       seq_id((vseq.m) - (vseq.k)) = seq_id(vseq.m)-seq_id(vseq.k)
       proof
         let m,k be Nat;
            seq_id((vseq.m) - (vseq.k))
            = seq_id(seq_id(vseq.m)-seq_id(vseq.k)) by Th3;
         hence thesis by CSSPACE:3;
       end;
        now let i be Nat;
           consider cseqi be Complex_Sequence such that
    A20:   ( for n be Nat holds cseqi.n=(seq_id(vseq.n)).i ) &
             cseqi is convergent & tseq.i=lim cseqi by A11;
          now let rseq be Real_Sequence such that
A21:       for m be Nat holds rseq.m=|.(seq_id(vseq.m-vseq.n)).| .i;
A22:       now let m be Nat;
A23:         seq_id(vseq.m - vseq.n)
                   = seq_id(vseq.m)-seq_id(vseq.n) by A19;
             thus rseq.m=|.(seq_id(vseq.m-vseq.n)).| .i by A21
                     .=|.((seq_id(vseq.m-vseq.n)).i).| by COMSEQ_1:def 14
                     .=|.((seq_id(vseq.m)).i
                       -(seq_id(vseq.n)).i).| by A23,Lm12
                     .= |.(cseqi.m -(seq_id(vseq.n)).i) .| by A20;
           end;
            |.(tseq.i-(seq_id(vseq.n)).i).|
                   = |.((tseq-(seq_id(vseq.n))).i).| by Lm12
                  .= |.((seq_id tseq-(seq_id(vseq.n))).i).| by CSSPACE:3
                  .= |.((seq_id tseq -seq_id(vseq.n))).| .i by COMSEQ_1:def 14;
           hence rseq is convergent &
            lim rseq = |.(seq_id tseq -seq_id(vseq.n)).| .i
             by A20,A22,CSSPACE3:1;
         end;
         hence for rseq be Real_Sequence st
          ( for m be Nat holds rseq.m=|.(seq_id(vseq.m-vseq.n)).| .i ) holds
            rseq is convergent
            & lim rseq = |.(seq_id tseq -seq_id(vseq.n)).| .i;
       end;
       hence thesis;
     end;
      for n be Nat st n >= k holds
      |.(seq_id tseq-seq_id(vseq.n)).| is bounded &
     sup rng |.(seq_id tseq-seq_id(vseq.n)).| <= e
     proof
       let n be Nat such that
A24:   n >= k;
A25:   for i be Nat holds |.((seq_id tseq -seq_id(vseq.n))).| .i <= e
       proof
         let i be Nat;
         deffunc F(Nat)= |.(seq_id((vseq.$1) - (vseq.n))).| .i;
         consider rseq be Real_Sequence such that
A26:     for m be Nat holds rseq.m = F(m) from SEQ_1:sch 1;
A27:     rseq is convergent by A18,A26;
A28:     lim rseq = |.(seq_id tseq-seq_id(vseq.n)).| .i by A18,A26;
          for m be Nat st m >= k holds rseq.m <= e
         proof
           let m be Nat;
           assume m >= k;
then A29:       |.(seq_id((vseq.m) - (vseq.n))).| is bounded &
             sup rng |. seq_id((vseq.m) - (vseq.n)).| <= e
               by A15,A24;
then A30:       |.(seq_id((vseq.m) - (vseq.n))).| .i
             <=sup rng |.seq_id((vseq.m) - (vseq.n)).| by Lm2;
              rseq.m = |.(seq_id((vseq.m) - (vseq.n))).| .i by A26;
           hence thesis by A29,A30,AXIOMS:22;
         end;
         hence thesis by A27,A28,RSSPACE2:6;
       end;
        |.(seq_id tseq-seq_id(vseq.n)).| is bounded
       proof
A31:     0 + 0 < 1 + e by A13,REAL_1:67;
A32:     0 + e < 1 + e by REAL_1:67;
          now let i be Nat;
          |.((seq_id tseq -seq_id(vseq.n))).| .i <= e by A25;
           then |.(((seq_id tseq -seq_id(vseq.n))).i).| <= e by COMSEQ_1:def 14
;
           hence |.(((seq_id tseq -seq_id(vseq.n))).i).|
                <e+1 by A32,AXIOMS:22;
         end;
         then seq_id tseq -seq_id(vseq.n) is bounded by A31,COMSEQ_2:8;
         hence thesis by Lm9;
       end;
       hence thesis by A25,Lm1;
     end;
     hence thesis;
   end;
A33:seq_id tseq is bounded
   proof
     consider m be Nat such that
A34: for n be Nat st n >= m holds
     |.(seq_id tseq -seq_id(vseq.n)).| is bounded &
     sup rng |.(seq_id tseq -seq_id(vseq.n)).| <= 1 by A12;
A35: |.(seq_id tseq -seq_id(vseq.m)).| is bounded by A34;
      seq_id(vseq.m) is bounded by Def1,Def3;
then A36: |.seq_id (vseq.m).| is bounded by Lm9;
     set a=|.(seq_id tseq -seq_id(vseq.m)).|;
     set b=|.seq_id(vseq.m).|;
     set d=|.seq_id tseq.|;
A37: a + b is bounded by A35,A36,SEQM_3:71;
A38: for i be Nat holds d.i <= (a+b).i
     proof
       let i be Nat;
A39:   a.i = |.((seq_id tseq -seq_id(vseq.m)).i).| by COMSEQ_1:def 14
          .= |.((seq_id tseq+-seq_id(vseq.m)).i).| by COMSEQ_1:def 10
          .= |.((seq_id tseq).i+(-seq_id(vseq.m)).i).| by COMSEQ_1:def 4
          .= |.((seq_id tseq).i+(-(seq_id(vseq.m)).i)).| by COMSEQ_1:def 9
          .= |.((seq_id tseq).i-(seq_id(vseq.m)).i).|;
A40:   b.i=|.((seq_id(vseq.m)).i).| by COMSEQ_1:def 14;
        d.i=|.((seq_id tseq).i).| by COMSEQ_1:def 14;
       then d.i-b.i <= a.i by A39,A40,COMPLEX1:145;
       then d.i-b.i+b.i<= a.i + b.i by AXIOMS:24;
       hence thesis by SEQ_1:11;
     end;
      d is bounded
     proof
       reconsider r=sup rng (a+b)+1 as real number;
        0 <= sup rng(a+b)
       proof
A41:     (a+b).1 =a.1 + b.1 by SEQ_1:11;
          a.1=|.((seq_id tseq -seq_id(vseq.m)).1).| by COMSEQ_1:def 14;
         then A42:    0<= a.1 by COMPLEX1:132;
          b.1=|.( (seq_id(vseq.m)).1 ).| by COMSEQ_1:def 14;
         then 0<= b.1 by COMPLEX1:132;
         then 0 + 0 <= a.1+ b.1 by A42,REAL_1:55;
         hence 0<= sup rng(a+b) by A37,A41,Lm2;
       end;
then A43:   0+0 < sup( rng(a+b) )+1 by REAL_1:67;
A44:   sup( rng(a+b) ) +0 < sup( rng(a+b) )+1 by REAL_1:67;
        now let i be Nat;
A45:     d.i <= (a+b).i by A38;
          (a+b).i <= sup rng (a+b) by A37,Lm2;
then A46:     d.i <= sup rng (a+b) by A45,AXIOMS:22;
          d.i=|.((seq_id tseq).i).| by COMSEQ_1:def 14;
         hence |.((seq_id tseq).i).| <r by A44,A46,AXIOMS:22;
       end;
       then seq_id tseq is bounded by A43,COMSEQ_2:8;
       hence thesis by Lm9;
     end;
     hence thesis by Lm10;
   end;
A47:tseq in the_set_of_ComplexSequences by CSSPACE:def 1;
   then reconsider tv=tseq as Point of Complex_linfty_Space by A33,Def1,Def3;

    ex tv be Point of Complex_linfty_Space st
    for e1 be Real st e1 > 0
     ex m be Nat st
      for n be Nat st n >= m holds ||.(vseq.n) - tv.|| < e1
   proof
     take tv;
     let e1 be Real such that
A48: e1 > 0;
     set e=e1/2;
A49: e > 0 by A48,SEQ_2:3;
A50: e < e1 by A48,SEQ_2:4;
     consider m be Nat such that
A51: for n be Nat st n >= m holds
      |.(seq_id tseq-seq_id(vseq.n)).| is bounded &
     sup rng |.(seq_id tseq-seq_id(vseq.n)).| <= e by A12,A49;
      now let n be Nat such that
A52:   n >= m;
       reconsider u=tseq as VECTOR of Complex_linfty_Space
         by A33,A47,Def1,Def3;
A53:   sup rng( |.(seq_id tseq-seq_id(vseq.n)).|) <= e by A51,A52;
       reconsider v=vseq.n as VECTOR of Complex_linfty_Space;
        seq_id(u-v) = u-v by Th3;
       then sup rng |.seq_id(u-v).|
         = sup rng |.(seq_id tseq-seq_id(vseq.n)).| by Th3;
then A54:   (the norm of Complex_linfty_Space).(u-v) <= e by A53,Def2,Def3;
        ||.(vseq.n) - tv.|| =||.(vseq.n) +-tv.|| by RLVECT_1:def 11
        .=||.-(tv-(vseq.n)).|| by RLVECT_1:47
        .=||.tv-(vseq.n).|| by CLVECT_1:104;
       then ||.(vseq.n) - tv.|| <= e by A54,CLVECT_1:def 9;
       hence ||.(vseq.n) - tv.|| < e1 by A50,AXIOMS:22;
     end;
     hence thesis;
   end;
   hence thesis by CLVECT_1:def 15;
end;

theorem
 Complex_linfty_Space is ComplexBanachSpace by Th5,CLOPBAN1:def 14;

begin  :: Another example of complex Banach space

definition
  let X be non empty set;
  let Y be ComplexNormSpace;
  let IT be Function of X, the carrier of Y;
  attr IT is bounded means :Def4:
  ex K being Real st 0 <= K &
   for x being Element of X holds
   ||. IT.x .|| <= K;
end;

theorem Th7:
for X be non empty set, Y be ComplexNormSpace,
 f be Function of X,the carrier of Y st
  (for x be Element of X holds f.x=0.Y) holds f is bounded
proof
   let X be non empty set;
   let Y be ComplexNormSpace;
   let f be Function of X,the carrier of Y such that
A1:for x be Element of X holds f.x=0.Y;
A2:now
     let x be Element of X;
     thus ||. f.x .|| = ||. 0.Y .|| by A1
         .=0 by CLVECT_1:103;
   end;
   take 0;
   thus thesis by A2;
end;

registration
  let X be non empty set;
  let Y be ComplexNormSpace;
  cluster bounded Function of X,the carrier of Y;
existence
proof
  set f=X --> 0.Y;
  reconsider f as Function of X,the carrier of Y by FUNCOP_1:58;
  take f;
     for x be Element of X holds f.x =0.Y by FUNCOP_1:13;
  hence thesis by Th7;
end;
end;

definition
  let X be non empty set;
  let Y be ComplexNormSpace;
  func ComplexBoundedFunctions(X,Y) ->
    Subset of ComplexVectSpace(X,Y) means :Def5:
   for x being set holds x in it
     iff
   x is bounded Function of X,the carrier of Y;
existence
proof
   defpred P[set] means $1 is bounded Function of X,the carrier of Y;
   consider IT being set such that
A1:for x being set holds x in IT
    iff x in Funcs(X,the carrier of Y)  & P[x] from XBOOLE_0:sch 1;
A2:IT is Subset of Funcs(X,the carrier of Y)
   proof
      for x be set st x in IT holds x in Funcs(X,the carrier of Y) by A1;
     hence thesis by TARSKI:def 3;
   end;
A3:the carrier of ComplexVectSpace(X,Y) = Funcs(X,the carrier of Y)
   proof
      ComplexVectSpace(X,Y) = CLSStruct(#Funcs(X,the carrier of Y),
              (FuncZero(X,Y)),FuncAdd(X,Y),FuncExtMult(X,Y)#)
              by CLOPBAN1:def 2;
     hence thesis;
   end;
   take IT;
   thus IT is Subset of ComplexVectSpace(X,Y) by A2,A3;
   let x be set;
   thus x in IT implies x is bounded Function of X,the carrier of Y by A1;
   assume
A4:x is bounded Function of X,the carrier of Y;
   then x in Funcs(X,the carrier of Y) by FUNCT_2:11;
   hence thesis by A1,A4;
end;
uniqueness
proof
   let X1,X2 be Subset of ComplexVectSpace(X,Y);
   assume that
A5:for x being set holds x in X1 iff
     x is bounded Function of X,the carrier of Y and
A6:for x being set holds x in X2 iff
     x is bounded Function of X,the carrier of Y;
    for x being set st x in X1 holds x in X2
   proof
     let x be set;
     assume x in X1;
     then x is bounded Function of X,the carrier of Y by A5;
     hence thesis by A6;
   end;
then A7:X1 c= X2 by TARSKI:def 3;
    for x being set st x in X2 holds x in X1
   proof
     let x be set;
     assume x in X2;
     then x is bounded Function of X,the carrier of Y by A6;
     hence thesis by A5;
   end;
   then X2 c= X1 by TARSKI:def 3;
   hence thesis by A7,XBOOLE_0:def 10;
end;
end;

registration
  let X be non empty set;
  let Y be ComplexNormSpace;
  cluster ComplexBoundedFunctions(X,Y) -> non empty;
coherence
proof
   set f=X --> 0.Y;
   reconsider f as  Function of X,the carrier of Y by FUNCOP_1:58;
    f is bounded
   proof
      for x be Element of X holds f.x =0.Y by FUNCOP_1:13;
     hence thesis by Th7;
   end;
   hence thesis by Def5;
end;
end;

theorem Th8:
for X be non empty set for Y be ComplexNormSpace holds
  ComplexBoundedFunctions(X,Y) is lineary-closed
proof
   let X be non empty set;
   let Y be ComplexNormSpace;
   set W = ComplexBoundedFunctions(X,Y);
A1:ComplexVectSpace(X,Y)
    = CLSStruct(#Funcs(X,the carrier of Y),
       (FuncZero(X,Y)),FuncAdd(X,Y),FuncExtMult(X,Y)#) by CLOPBAN1:def 2;
A2:for v,u be VECTOR of ComplexVectSpace(X,Y)  st
    v in W & u in W holds v + u in W
   proof
     let v,u be VECTOR of ComplexVectSpace(X,Y)  such that
A3:  v in W & u in W;
     reconsider f=v+u as Function of X,the carrier of Y by A1,FUNCT_2:121;
      f is bounded
     proof
       reconsider u1=u as bounded Function of X, the carrier of Y by A3,Def5;
       reconsider v1=v as bounded Function of X, the carrier of Y by A3,Def5;
       consider K1 be Real such that
A4:     0 <= K1 &
        for x be Element of X holds ||. u1.x .||   <= K1 by Def4;
       consider K2 be Real such that
A5:     0 <= K2 &
        for x be Element of X holds ||. v1.x .|| <= K2 by Def4;
       take K3=K1+K2;
A6:    0 + 0 <= K1+K2 by A4,A5,REAL_1:55;
        now let x be Element of X;
A7:      ||. f.x .|| =||. v1.x+u1.x .|| by CLOPBAN1:12;
A8:      ||. u1.x+v1.x .|| <= ||. u1.x .||+ ||. v1.x .|| by CLVECT_1:def 10;
A9:      ||. u1.x .|| <= K1 by A4;
          ||. v1.x .|| <= K2 by A5;
         then ||. u1.x .|| + ||. v1.x .|| <= K1 +K2 by A9,REAL_1:55;
         hence ||. f.x .|| <= K3 by A7,A8,AXIOMS:22;
       end;
       hence thesis by A6;
     end;
     hence thesis by Def5;
   end;

    for c be Complex for v be VECTOR of ComplexVectSpace(X,Y) st
     v in W holds c * v in W
   proof
     let c be Complex;
     let v be VECTOR of ComplexVectSpace(X,Y) such that
A10: v in W;
     reconsider f=c*v as Function of X,the carrier of Y by A1,FUNCT_2:121;  f
is bounded
     proof
       reconsider v1=v as bounded Function of X,
       the carrier of Y by A10,Def5;
       consider K be Real such that
A11:    0 <= K &
        for x be Element of X holds ||. v1.x .|| <= K by Def4;
       take K1=|.c.|*K;
A12:   0 <= K & 0 <=|.c.| by A11,COMPLEX1:132;
        now let x be Element of X;
A13:     ||. f.x .|| =||. c*v1.x .|| by CLOPBAN1:13;
A14:     ||. c*v1.x .|| = |.c.|* ||. v1.x .|| by CLVECT_1:def 10;
A15:     ||. v1.x .|| <= K by A11;
          0 <=|.c.| by COMPLEX1:132;
         hence ||. f.x .|| <= |.c.|* K by A13,A14,A15,AXIOMS:25;
       end;
       hence thesis by A12,REAL_2:121;
     end;
     hence thesis by Def5;
   end;
   hence thesis by A2,CLVECT_1:def 3;
end;

theorem Th9:
for X be non empty set for Y be ComplexNormSpace holds
 CLSStruct (# ComplexBoundedFunctions(X,Y),
        Zero_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
         Add_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
        Mult_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)) #)
is Subspace of ComplexVectSpace(X,Y)
proof
   let X be non empty set;
   let Y be ComplexNormSpace;
    ComplexBoundedFunctions(X,Y) is lineary-closed by Th8;
   hence thesis by CSSPACE:13;
end;

registration
  let X be non empty set;
  let Y be ComplexNormSpace;
cluster CLSStruct (# ComplexBoundedFunctions(X,Y),
    Zero_(ComplexBoundedFunctions(X,Y), ComplexVectSpace(X,Y)),
    Add_(ComplexBoundedFunctions(X,Y), ComplexVectSpace(X,Y)),
    Mult_(ComplexBoundedFunctions(X,Y), ComplexVectSpace(X,Y)) #)
 -> Abelian add-associative right_zeroed right_complementable
        ComplexLinearSpace-like;
  coherence by Th9;
end;

canceled;

definition
  let X be non empty set;
  let Y be ComplexNormSpace;
  func C_VectorSpace_of_BoundedFunctions(X,Y) -> ComplexLinearSpace equals
  :Def6:
CLSStruct (# ComplexBoundedFunctions(X,Y),
        Zero_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
         Add_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
        Mult_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)) #);
  coherence;
end;

registration
  let X be non empty set;
  let Y be ComplexNormSpace;
  cluster C_VectorSpace_of_BoundedFunctions(X,Y) -> strict;
  coherence
proof
    C_VectorSpace_of_BoundedFunctions(X,Y)=
    CLSStruct (# ComplexBoundedFunctions(X,Y),
        Zero_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
         Add_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
        Mult_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)) #) by Def6;
   hence thesis;
end;
end;

theorem Th11:
for X be non empty set for  Y be ComplexNormSpace,
 f,g,h be VECTOR of C_VectorSpace_of_BoundedFunctions(X,Y),
  f',g',h' be bounded Function of X,the carrier of Y st
    f'=f & g'=g & h'=h holds
     (h = f+g iff (for x be Element of X holds (h'.x = f'.x + g'.x)) )
proof
   let X be non empty set;
   let Y be ComplexNormSpace;
   let f,g,h be VECTOR of C_VectorSpace_of_BoundedFunctions(X,Y);
   let f',g',h' be bounded Function of X,the carrier of Y such that
A1:f'=f and
A2:g'=g and
A3:h'=h;
    C_VectorSpace_of_BoundedFunctions(X,Y)=
    CLSStruct (# ComplexBoundedFunctions(X,Y),
      Zero_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
      Add_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
      Mult_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y))#) by Def6;
then A4:C_VectorSpace_of_BoundedFunctions(X,Y) is Subspace of ComplexVectSpace(
X,Y)
     by Th9;
   then reconsider f1=f as VECTOR of ComplexVectSpace(X,Y) by CLVECT_1:30;
   reconsider g1=g as VECTOR of ComplexVectSpace(X,Y) by A4,CLVECT_1:30;
   reconsider h1=h as VECTOR of ComplexVectSpace(X,Y) by A4,CLVECT_1:30;
   reconsider f1'=f', g1'=g', h1'=h' as Element
            of Funcs(X, the carrier of Y) by FUNCT_2:11;
A5:now assume
A6:  h = f+g;
     let x be Element of X;
      h1=f1+g1 by A4,A6,CLVECT_1:33;
     hence h'.x=f'.x+g'.x by A1,A2,A3,CLOPBAN1:12;
   end;
    now assume
      for x be Element of X holds h'.x=f'.x+g'.x;
     then h1=f1+g1 by A1,A2,A3,CLOPBAN1:12;
     hence h =f+g by A4,CLVECT_1:33;
   end;
   hence thesis by A5;
end;

theorem Th12:
for X be non empty set for  Y be ComplexNormSpace,
 f,h be VECTOR of C_VectorSpace_of_BoundedFunctions(X,Y),
  f',h' be bounded Function of X,the carrier of Y st f'=f & h'=h
for c be Complex holds
      h = c*f iff for x be Element of X holds h'.x = c*f'.x
proof
   let X be non empty set;
   let Y be ComplexNormSpace;
   let f,h be VECTOR of C_VectorSpace_of_BoundedFunctions(X,Y);
   let f',h' be bounded Function of X,the carrier of Y such that
A1:f'=f and
A2:h'=h;
   let c be Complex;
    C_VectorSpace_of_BoundedFunctions(X,Y)
    = CLSStruct (# ComplexBoundedFunctions(X,Y),
       Zero_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
       Add_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
       Mult_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)) #)
         by Def6;
then A3:C_VectorSpace_of_BoundedFunctions(X,Y) is Subspace of ComplexVectSpace(
X,Y)
     by Th9;
   then reconsider f1=f, h1=h as VECTOR of ComplexVectSpace(X,Y) by CLVECT_1:30
;
   reconsider f1'=f', h1'=h' as Element of Funcs(X, the carrier of Y)
     by FUNCT_2:11;
A4:now assume
A5:  h = c*f;
     let x be Element of X;
      h1=c*f1 by A3,A5,CLVECT_1:34;
     hence h'.x=c*f'.x by A1,A2,CLOPBAN1:13;
   end;
    now assume
      for x be Element of X holds h'.x=c*f'.x;
     then h1=c*f1 by A1,A2,CLOPBAN1:13;
     hence h =c*f by A3,CLVECT_1:34;
   end;
   hence thesis by A4;
 end;

theorem Th13:
for X be non empty set, Y be ComplexNormSpace holds
 0.C_VectorSpace_of_BoundedFunctions(X,Y) = (X -->0.Y)
proof
   let X be non empty set;
   let Y be ComplexNormSpace;
    C_VectorSpace_of_BoundedFunctions(X,Y)
    = CLSStruct (# ComplexBoundedFunctions(X,Y),
        Zero_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
        Add_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
        Mult_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)) #)
          by Def6;
then A1:C_VectorSpace_of_BoundedFunctions(X,Y) is Subspace of ComplexVectSpace(
X,Y)
     by Th9;
    0.ComplexVectSpace(X,Y) = (X -->0.Y) by CLOPBAN1:14;
   hence thesis by A1,CLVECT_1:31;
end;

definition
   let X be non empty set;
   let Y be ComplexNormSpace;
   let f be set such that
A1:f in ComplexBoundedFunctions(X,Y);
  func modetrans(f,X,Y)
   -> bounded Function of X,the carrier of Y equals:Def7:
    f;
  coherence by A1,Def5;
end;

definition
   let X be non empty set;
   let Y be ComplexNormSpace;
   let u be Function of X,the carrier of Y;
   func PreNorms(u) -> non empty Subset of REAL equals :Def8:
     {||.u.t.|| where t is Element of X : not contradiction  };
coherence
proof
A1:now let x be set;
      now assume
        x in {||.u.t.|| where t is Element of X: not contradiction };
       then consider t be Element of X such that
A2:    x=||.u.t.||;
       thus x in REAL by A2;
     end;
     hence x in {||.u.t.|| where t is Element of X: not contradiction }
       implies x in REAL;
   end;
   consider x be Element of X;
    ||.u.x.|| in {||.u.t.|| where t is Element of X: not contradiction };
   hence thesis by A1,TARSKI:def 3;
end;
end;

theorem Th14:
for X be non empty set for Y be ComplexNormSpace,
 g be bounded Function of X,the carrier of Y holds
  PreNorms(g) is non empty bounded_above
proof
   let X be non empty set;
   let Y be ComplexNormSpace;
   let g be bounded Function of X,the carrier of Y;
    PreNorms(g) is bounded_above
   proof
     consider K be Real such that
      0 <= K and
A1:  for x be Element of X holds ||. g.x .|| <= K by Def4;
A2:  now let r be Real such that
A3:    r in PreNorms(g);
        r in {||.g.t.|| where t is Element of X: not contradiction }
          by A3,Def8;
       then consider t be Element of X such that
A4:    r=||.g.t.||;
       thus r <=K by A1,A4;
     end;
     take K;
     thus thesis by A2;
   end;
   hence thesis;
end;

theorem
 for X be non empty set for Y be ComplexNormSpace,
 g be Function of X,the carrier of Y holds
  g is bounded iff PreNorms(g) is bounded_above
proof
   let X be non empty set; let Y be ComplexNormSpace;
   let g be Function of X,the carrier of Y;

    now assume
A1:  PreNorms(g) is bounded_above;
     reconsider K=sup PreNorms(g) as Real;
A2:  0 <= K
     proof
A3:    now let r be Real such that
A4:      r in PreNorms(g);
          r in {||.g.t.|| where t is Element of X : not contradiction  }
           by A4,Def8;
         then consider t be Element of X such that
A5:      r=||.g.t.||;
         thus 0 <= r by A5,CLVECT_1:106;
       end;
       consider r0 be set such that
A6:    r0 in PreNorms(g) by XBOOLE_0:def 1;
       reconsider r0 as Real by A6;
        0 <= r0 by A3,A6;
       hence thesis by A1,A6,SEQ_4:def 4;
     end;
A7:  now let t be Element of X;
        ||.g.t.|| in
         {||.g.s.|| where s is Element of X : not contradiction };
       then ||.g.t.|| in PreNorms(g) by Def8;
       hence ||.g.t.|| <= K by A1,SEQ_4:def 4;
     end;
     take K;
     thus g is bounded by A2,A7,Def4;
   end;
   hence thesis by Th14;
end;

theorem Th16:
for X be non empty set, Y be ComplexNormSpace
  ex NORM be Function of ComplexBoundedFunctions(X,Y),REAL st
    for f be set st f in ComplexBoundedFunctions(X,Y) holds
      NORM.f = sup PreNorms(modetrans(f,X,Y))
proof
   let X be non empty set;
   let Y be ComplexNormSpace;
   deffunc F(set) = sup PreNorms(modetrans($1,X,Y));
A1:for z be set st z in ComplexBoundedFunctions(X,Y) holds F(z) in REAL;
      ex f being Function of ComplexBoundedFunctions(X,Y),REAL st
       for x being set st x in ComplexBoundedFunctions(X,Y) holds
       f.x = F(x) from FUNCT_2:sch 2(A1);
   hence thesis;
end;

definition
   let X be non empty set;
   let Y be ComplexNormSpace;
   func ComplexBoundedFunctionsNorm(X,Y)
     -> Function of ComplexBoundedFunctions(X,Y), REAL means :Def9:
  for x be set st x in ComplexBoundedFunctions(X,Y) holds
    it.x = sup PreNorms(modetrans(x,X,Y));
existence by Th16;
uniqueness
proof
   let NORM1,NORM2 be Function of ComplexBoundedFunctions(X,Y), REAL such that
A1: (for x be set st x in ComplexBoundedFunctions(X,Y) holds
      NORM1.x = sup PreNorms(modetrans(x,X,Y))) and
A2: (for x be set st x in ComplexBoundedFunctions(X,Y) holds
      NORM2.x = sup PreNorms(modetrans(x,X,Y)));
A3:dom NORM1 = ComplexBoundedFunctions(X,Y) &
   dom NORM2 = ComplexBoundedFunctions(X,Y) by FUNCT_2:def 1;
    for z be set st z in ComplexBoundedFunctions(X,Y) holds NORM1.z = NORM2.z
   proof
     let z be set such that
A4:  z in ComplexBoundedFunctions(X,Y);
      NORM1.z = sup PreNorms(modetrans(z,X,Y)) by A1,A4;
     hence thesis by A2,A4;
  end;
  hence thesis by A3,FUNCT_1:9;
end;
end;

theorem Th17:
for X be non empty set, Y be ComplexNormSpace,
 f be bounded Function of X,the carrier of Y holds modetrans(f,X,Y)=f
proof
   let X be non empty set;
   let Y be ComplexNormSpace;
   let f be bounded Function of X,the carrier of Y;
    f in ComplexBoundedFunctions(X,Y) by Def5;
   hence thesis by Def7;
end;

theorem Th18:
for X be non empty set, Y be ComplexNormSpace,
 f be bounded Function of X,the carrier of Y holds
   ComplexBoundedFunctionsNorm(X,Y).f = sup PreNorms(f)
proof
   let X be non empty set;
   let Y be ComplexNormSpace;
   let f be bounded Function of X,the carrier of Y;
A1:f in ComplexBoundedFunctions(X,Y) by Def5;
   reconsider f'=f as set;
   thus ComplexBoundedFunctionsNorm(X,Y).f
         = sup PreNorms(modetrans(f',X,Y)) by A1,Def9
        .= sup PreNorms(f) by Th17;
end;

definition
  let X be non empty set;
  let Y be ComplexNormSpace;
func C_NormSpace_of_BoundedFunctions(X,Y) -> non empty CNORMSTR equals :Def10:
  CNORMSTR (# ComplexBoundedFunctions(X,Y),
    Zero_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
    Add_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
    Mult_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
          ComplexBoundedFunctionsNorm(X,Y) #);
coherence;
end;

theorem Th19:
for X be non empty set, Y be ComplexNormSpace holds
  (X --> 0.Y) = 0.C_NormSpace_of_BoundedFunctions(X,Y)
proof
   let X be non empty set;
   let Y be ComplexNormSpace;
A1:C_NormSpace_of_BoundedFunctions(X,Y)
     = CNORMSTR (# ComplexBoundedFunctions(X,Y),
         Zero_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
         Add_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
         Mult_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
           ComplexBoundedFunctionsNorm(X,Y) #) by Def10;
A2:C_VectorSpace_of_BoundedFunctions(X,Y)
     = CLSStruct (# ComplexBoundedFunctions(X,Y),
         Zero_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
         Add_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
         Mult_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)) #) by Def6;
    (X --> 0.Y)
     =0.C_VectorSpace_of_BoundedFunctions(X,Y) by Th13
    .=Zero_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y))
        by A2,RLVECT_1:def 2
    .=0.C_NormSpace_of_BoundedFunctions(X,Y) by A1,RLVECT_1:def 2;
   hence thesis;
end;

theorem Th20:
for X be non empty set, Y be ComplexNormSpace,
 f being Point of C_NormSpace_of_BoundedFunctions(X,Y),
  g be bounded Function of X,the carrier of Y st g=f holds
    for t be Element of X holds ||.g.t.|| <= ||.f.||
proof
   let X be non empty set;
   let Y be ComplexNormSpace;
   let f being Point of C_NormSpace_of_BoundedFunctions(X,Y);
   let g be bounded Function of X,the carrier of Y such that
A1:g=f;
A2:C_NormSpace_of_BoundedFunctions(X,Y)
     = CNORMSTR (# ComplexBoundedFunctions(X,Y),
         Zero_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
         Add_(ComplexBoundedFunctions(X,Y), ComplexVectSpace(X,Y)),
         Mult_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
           ComplexBoundedFunctionsNorm(X,Y) #) by Def10;
A3:PreNorms(g) is non empty bounded_above by Th14;
    now let t be Element of X;
      ||.g.t.|| in
        {||.g.s.|| where s is Element of X: not contradiction};
     then ||.g.t.|| in PreNorms(g) by Def8;
     then ||.g.t.|| <= sup PreNorms(g) by A3,SEQ_4:def 4;
     then ||.g.t.|| <= ComplexBoundedFunctionsNorm(X,Y).g by Th18;
     hence ||.g.t.|| <= ||.f.|| by A1,A2,CLVECT_1:def 9;
   end;
   hence thesis;
end;

theorem
 for X be non empty set, Y be ComplexNormSpace,
 f being Point of C_NormSpace_of_BoundedFunctions(X,Y) holds
  0 <= ||.f.||
proof
   let X be non empty set;
   let Y be ComplexNormSpace;
   let f being Point of C_NormSpace_of_BoundedFunctions(X,Y);
A1:C_NormSpace_of_BoundedFunctions(X,Y)
    = CNORMSTR (# ComplexBoundedFunctions(X,Y),
         Zero_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
         Add_(ComplexBoundedFunctions(X,Y), ComplexVectSpace(X,Y)),
         Mult_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
           ComplexBoundedFunctionsNorm(X,Y) #) by Def10;
   then reconsider g=f as bounded Function of X,the carrier of Y by Def5;
A2:now let r be Real such that
A3:  r in PreNorms(g);
      r in {||.g.t.|| where t is Element of X: not contradiction }
       by A3,Def8;
     then consider t be Element of X such that
A4:  r=||.g.t.||;
     thus 0 <= r by A4,CLVECT_1:106;
   end;
A5:PreNorms(g) is non empty bounded_above by Th14;
   consider r0 be set such that
A6:r0 in PreNorms(g) by XBOOLE_0:def 1;
   reconsider r0 as Real by A6;
    0 <= r0 by A2,A6;
then A7:0 <=sup PreNorms(g) by A5,A6,SEQ_4:def 4;
    ComplexBoundedFunctionsNorm(X,Y).f = sup PreNorms(g) by Th18;
   hence thesis by A1,A7,CLVECT_1:def 9;
end;

theorem Th22:
for X be non empty set, Y be ComplexNormSpace,
 f being Point of C_NormSpace_of_BoundedFunctions(X,Y) st
  f = 0.C_NormSpace_of_BoundedFunctions(X,Y) holds 0 = ||.f.||
proof
   let X be non empty set;
   let Y be ComplexNormSpace;
   let f being Point of C_NormSpace_of_BoundedFunctions(X,Y) such that
A1: f = 0.C_NormSpace_of_BoundedFunctions(X,Y);
A2:C_NormSpace_of_BoundedFunctions(X,Y)
     = CNORMSTR (# ComplexBoundedFunctions(X,Y),
         Zero_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
         Add_(ComplexBoundedFunctions(X,Y), ComplexVectSpace(X,Y)),
         Mult_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
           ComplexBoundedFunctionsNorm(X,Y) #) by Def10;
   thus ||.f.|| = 0
   proof
     set z = X --> 0.Y;
     reconsider z as Function of X, the carrier of Y by FUNCOP_1:58;
     reconsider g=f as bounded Function of X,
     the carrier of Y by A2,Def5;
A3:  z=g by A1,Th19;
A4:  now let r be Real such that
A5:    r in PreNorms(g);
        r in {||.g.t.|| where t is Element of X: not contradiction}
         by A5,Def8;
       then consider t be Element of X such that
A6:    r=||.g.t.||;
        ||.g.t.|| = ||.0.Y.|| by A3,FUNCOP_1:13 .= 0 by CLVECT_1:def 10;
       hence 0 <= r & r <=0 by A6;
     end;
A7:  PreNorms(g) is non empty bounded_above by Th14;
     consider r0 be set such that
A8:  r0 in PreNorms(g) by XBOOLE_0:def 1;
     reconsider r0 as Real by A8;
A9:  0 <= r0 & r0<=0 by A4,A8;
      (for s be real number st s in PreNorms(g) holds s <= 0)
      implies sup PreNorms(g) <= 0 by SEQ_4:62;
     then sup PreNorms(g) = 0 by A4,A7,A8,A9,SEQ_4:def 4;
     then ComplexBoundedFunctionsNorm(X,Y).f=0 by Th18;
     hence thesis by A2,CLVECT_1:def 9;
   end;
end;

theorem Th23:
for X be non empty set, Y be ComplexNormSpace,
 f,g,h be Point of C_NormSpace_of_BoundedFunctions(X,Y),
  f',g',h' be bounded Function of X,the carrier of Y st
   f'=f & g'=g & h'=h holds
     ( h = f+g iff (for x be Element of X holds (h'.x = f'.x + g'.x)) )
proof
   let X be non empty set;let Y be ComplexNormSpace;
   let f,g,h be Point of C_NormSpace_of_BoundedFunctions(X,Y);
   let f',g',h' be bounded Function of X,the carrier of Y such that
A1:f'=f and
A2:g'=g and
A3:h'=h;
A4:C_NormSpace_of_BoundedFunctions(X,Y)
    = CNORMSTR (# ComplexBoundedFunctions(X,Y),
       Zero_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
       Add_(ComplexBoundedFunctions(X,Y), ComplexVectSpace(X,Y)),
       Mult_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
           ComplexBoundedFunctionsNorm(X,Y) #) by Def10;
A5:C_VectorSpace_of_BoundedFunctions(X,Y)
    = CLSStruct (# ComplexBoundedFunctions(X,Y),
       Zero_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
       Add_(ComplexBoundedFunctions(X,Y), ComplexVectSpace(X,Y)),
       Mult_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)) #)
         by Def6;
   then reconsider f1=f, g1=g, h1=h as VECTOR of
     C_VectorSpace_of_BoundedFunctions(X,Y) by A4;
    h=f+g iff h1=f1+g1
   proof
A6:  now
       assume h=f+g;
       hence h1=Add_(ComplexBoundedFunctions(X,Y),
         ComplexVectSpace(X,Y)).[f,g] by A4,RLVECT_1:def 3
          .=f1+g1 by A5,RLVECT_1:def 3;
     end;
      now
       assume h1=f1+g1;
       hence h=Add_(ComplexBoundedFunctions(X,Y),
         ComplexVectSpace(X,Y))
         .[f1,g1] by A5,RLVECT_1:def 3
      .=f+g by A4,RLVECT_1:def 3;
     end;
     hence thesis by A6;
   end;
   hence thesis by A1,A2,A3,Th11;
end;

theorem Th24:
for X be non empty set, Y be ComplexNormSpace,
 f,h be Point of C_NormSpace_of_BoundedFunctions(X,Y),
  f',h' be bounded Function of X,the carrier of Y st f'=f & h'=h
   for c be Complex holds
      h = c*f iff for x be Element of X holds h'.x = c*f'.x
proof
   let X be non empty set;
   let Y be ComplexNormSpace;
   let f,h be Point of C_NormSpace_of_BoundedFunctions(X,Y);
   let f',h' be bounded Function of X,the carrier of Y such that
A1:f'=f and
A2:h'=h;
   let c be Complex;
A3:C_NormSpace_of_BoundedFunctions(X,Y)
    = CNORMSTR (# ComplexBoundedFunctions(X,Y),
        Zero_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
        Add_(ComplexBoundedFunctions(X,Y), ComplexVectSpace(X,Y)),
        Mult_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
           ComplexBoundedFunctionsNorm(X,Y) #) by Def10;
A4:C_VectorSpace_of_BoundedFunctions(X,Y)
    = CLSStruct (# ComplexBoundedFunctions(X,Y),
        Zero_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
        Add_(ComplexBoundedFunctions(X,Y), ComplexVectSpace(X,Y)),
        Mult_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)) #)
          by Def6;
   then reconsider f1=f as VECTOR of C_VectorSpace_of_BoundedFunctions(X,Y) by
A3;
   reconsider h1=h as VECTOR of C_VectorSpace_of_BoundedFunctions(X,Y)
     by A3,A4;
    h=c*f iff h1=c*f1
   proof
A5:  now
       assume h=c*f;
       hence h1=Mult_(ComplexBoundedFunctions(X,Y),
         ComplexVectSpace(X,Y)).[c,f] by A3,CLVECT_1:def 1
          .=c*f1 by A4,CLVECT_1:def 1;
     end;
      now
       assume h1=c*f1;
       hence h=Mult_(ComplexBoundedFunctions(X,Y),
         ComplexVectSpace(X,Y))
         .[c,f1] by A4,CLVECT_1:def 1
      .=c*f by A3,CLVECT_1:def 1;
     end;
     hence thesis by A5;
   end;
   hence thesis by A1,A2,Th12;
end;

theorem Th25:
for X be non empty set, Y be ComplexNormSpace,
 f,g being Point of C_NormSpace_of_BoundedFunctions(X,Y),
  c be Complex holds
   ( ||.f.|| = 0 iff
     f = 0.C_NormSpace_of_BoundedFunctions(X,Y) ) &
     ||.c*f.|| = |.c.| * ||.f.|| &
     ||.f+g.|| <= ||.f.|| + ||.g.||
proof
   let X be non empty set;
   let Y be ComplexNormSpace;
   let f, g being Point of C_NormSpace_of_BoundedFunctions(X,Y);
   let c be Complex;
A1:C_NormSpace_of_BoundedFunctions(X,Y)
    = CNORMSTR (# ComplexBoundedFunctions(X,Y),
        Zero_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
        Add_(ComplexBoundedFunctions(X,Y), ComplexVectSpace(X,Y)),
        Mult_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
           ComplexBoundedFunctionsNorm(X,Y) #) by Def10;
A2:C_VectorSpace_of_BoundedFunctions(X,Y)
    = CLSStruct (# ComplexBoundedFunctions(X,Y),
        Zero_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
        Add_(ComplexBoundedFunctions(X,Y), ComplexVectSpace(X,Y)),
        Mult_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)) #) by Def6;
A3:now assume
A4:  ||.f.|| = 0;
     reconsider g=f as bounded Function of X,the carrier of Y by A1,Def5;
     set z = X --> 0.Y;
     reconsider z as Function of X, the carrier of Y by FUNCOP_1:58;
      now let t be Element of X;
        ||.g.t.|| <= ||.f.||  by Th20;
       then ||.g.t.|| = 0 by A4,CLVECT_1:106;
       hence g.t =0.Y by CLVECT_1:def 10
                .=z.t by FUNCOP_1:13;
     end;
     then g=z by FUNCT_2:113;
     hence f=0.C_NormSpace_of_BoundedFunctions(X,Y) by Th19;
   end;

A5:now assume
A6:  f = 0.C_NormSpace_of_BoundedFunctions(X,Y);
     thus ||.f.|| = 0
     proof
       set z = X --> 0.Y;
       reconsider z as Function of X, the carrier of Y by FUNCOP_1:58;
       reconsider g=f as bounded Function of X,the carrier of Y by A1,Def5;
A7:    z=g by A6,Th19;
A8:    now let r be Real such that
A9:      r in PreNorms(g);
          r in {||.g.t.|| where t is Element of X:not contradiction }
           by A9,Def8;
         then consider t be Element of X such that
A10:     r=||.g.t.||;
          ||.g.t.|| = ||.0.Y.|| by A7,FUNCOP_1:13
                  .= 0 by CLVECT_1:def 10;
         hence 0 <= r & r <=0 by A10;
       end;
A11:   PreNorms(g) is non empty bounded_above by Th14;
       consider r0 be set such that
A12:   r0 in PreNorms(g) by XBOOLE_0:def 1;
       reconsider r0 as Real by A12;
A13:   0<=r0 by A8,A12;
        (for s be real number st s in PreNorms(g) holds s <= 0)
         implies sup PreNorms(g) <= 0 by SEQ_4:62;
       then sup PreNorms(g) = 0 by A8,A11,A12,A13,SEQ_4:def 4;
       then ComplexBoundedFunctionsNorm(X,Y).f =0 by Th18;
       hence thesis by A1,CLVECT_1:def 9;
     end;
   end;

A14:||.c*f.|| = |.c.| * ||.f.||
   proof
     reconsider f1=f, h1=c*f as bounded Function of X, the carrier of Y
       by A1,Def5;
A15: now let t be Element of  X;
A16: ||.h1.t.||= ||.c*f1.t.|| by Th24;
A17: ||.c*f1.t.|| =|.c.|*||.f1.t.|| by CLVECT_1:def 10;
A18: ||.f1.t.||<= ||.f.|| by Th20;
      0<= |.c.| by COMPLEX1:132;
     hence ||.h1.t.|| <= |.c.|*||.f.|| by A16,A17,A18,AXIOMS:25;
   end;
A19:now let r be Real such that
A20: r in PreNorms(h1);
      r in {||.h1.t.|| where t is Element of  X: not contradiction  }
       by A20,Def8;
     then consider t be Element of X such that
A21: r=||.h1.t.||;
     thus r <= |.c.|*||.f.|| by A15,A21;
   end;

A22:
   (for s be real number st s in PreNorms(h1) holds s <= |.c.|*||.f.|| )
     implies sup PreNorms(h1) <=  |.c.|*||.f.|| by SEQ_4:62;
    ComplexBoundedFunctionsNorm(X,Y).(c*f)
    = sup PreNorms(h1) by Th18;
then A23:||.c*f.|| <= |.c.|*||.f.|| by A1,A19,A22,CLVECT_1:def 9;
    now per cases;
     case
A24:   c <> 0c;
A25:   now let t be Element of X;
          h1.t=c*f1.t by Th24;
then A26:     c"*h1.t =( c"* c)*f1.t by CLVECT_1:def 2
               .=1r*f1.t by A24,COMPLEX1:def 7,XCMPLX_0:def 7
               .=f1.t by CLVECT_1:def 2;
A27:     ||.c"*h1.t.|| =|.c".|*||.h1.t.|| by CLVECT_1:def 10;
A28:     ||.h1.t.||<= ||.c*f.|| by Th20;
A29:     0<= |.c".| by COMPLEX1:132;
          |.c".| = |.1r/c.| by COMPLEX1:def 7,XCMPLX_1:217
             .= 1/|.c.| by COMPLEX1:134,153
             .= 1*(|.c.|)" by XCMPLX_0:def 9
             .= |.c.|";
         hence ||.f1.t.|| <= |.c.|"*||.c*f.|| by A26,A27,A28,A29,AXIOMS:25;
       end;
A30:   now let r be Real such that
A31:     r in PreNorms(f1);
          r in {||.f1.t.|| where t is Element of  X: not contradiction }
            by A31,Def8;
         then consider t be Element of X such that
A32:     r=||.f1.t.||;
         thus r <= |.c.|"*||.c*f.|| by A25,A32;
       end;
A33:   |.c.| <>0 by A24,COMPLEX1:133;
A34:   (for s be real number st s in PreNorms(f1) holds s <= |.c.|"*||.c*f.|| )
         implies sup PreNorms(f1) <= |.c.|"*||.c*f.|| by SEQ_4:62;
        ComplexBoundedFunctionsNorm(X,Y).(f) = sup PreNorms(f1) by Th18;
then A35:   ||.f.|| <=|.c.|"*||.c*f.|| by A1,A30,A34,CLVECT_1:def 9;
        0 <= |.c.| by COMPLEX1:132;
       then |.c.|*||.f.|| <= |.c.|*(|.c.|"*||.c*f.||) by A35,AXIOMS:25;
       then |.c.|*||.f.|| <=(|.c.|*|.c.|")*||.c*f.||;
       then |.c.|*||.f.|| <=1*||.c*f.|| by A33,XCMPLX_0:def 7;
       hence |.c.|* ||.f.|| <=||.c*f.||;
     end;
     case
A36:   c = 0c;
       reconsider fz=f as VECTOR of C_VectorSpace_of_BoundedFunctions(X,Y)
         by A1,A2;
   c*f =Mult_(ComplexBoundedFunctions(X,Y),
            ComplexVectSpace(X,Y)).[c,f] by A1,CLVECT_1:def 1
        .=c*fz by A2,CLVECT_1:def 1
        .=0.C_VectorSpace_of_BoundedFunctions(X,Y) by A36,CLVECT_1:2
        .=Zero_(ComplexBoundedFunctions(X,Y),
          ComplexVectSpace(X,Y)) by A2,RLVECT_1:def 2
        .=0.C_NormSpace_of_BoundedFunctions(X,Y) by A1,RLVECT_1:def 2;
       hence |.c.|* ||.f.|| =||.c*f.|| by A36,Th22,COMPLEX1:130;
       end;
     end;
     hence thesis by A23,AXIOMS:21;
   end;
    ||.f+g.|| <= ||.f.|| + ||.g.||
   proof
     reconsider f1=f, g1=g, h1=f+g as
        bounded Function of X,the carrier of Y by A1,Def5;
A37: now let t be Element of X;
A38:   ||.h1.t.||= ||.f1.t+g1.t.|| by Th23;
A39:   ||.f1.t+g1.t.|| <=||.f1.t.||+||.g1.t.|| by CLVECT_1:def 10;
A40:   ||.f1.t.||<= ||.f.|| by Th20;
        ||.g1.t.||<= ||.g.|| by Th20;
       then ||.f1.t.||+||.g1.t.|| <= ||.f.|| + ||.g.|| by A40,REAL_1:55;
       hence ||.h1.t.|| <= ||.f.|| + ||.g.|| by A38,A39,AXIOMS:22;
     end;
A41: now let r be Real such that
A42:   r in PreNorms(h1);
        r in {||.h1.t.|| where t is Element of X: not contradiction}
         by A42,Def8;
       then consider t be Element of X such that
A43:   r=||.h1.t.||;
       thus r <= ||.f.|| + ||.g.|| by A37,A43;
     end;
A44: (for s be real number st s in PreNorms(h1) holds s <= ||.f.|| + ||.g.||)
       implies sup PreNorms(h1) <= ||.f.|| + ||.g.|| by SEQ_4:62;
      ComplexBoundedFunctionsNorm(X,Y).(f+g) = sup PreNorms(h1) by Th18;
     hence ||.f+g.|| <=||.f.|| + ||.g.|| by A1,A41,A44,CLVECT_1:def 9;
   end;
   hence thesis by A3,A5,A14;
end;

theorem Th26:
for X be non empty set, Y be ComplexNormSpace holds
 C_NormSpace_of_BoundedFunctions(X,Y) is ComplexNormSpace-like
proof
   let X be non empty set;
   let Y be ComplexNormSpace;
    for x, y being Point of C_NormSpace_of_BoundedFunctions(X,Y)
   for c be Complex holds
    ( ||.x.|| = 0 iff
      x = 0.C_NormSpace_of_BoundedFunctions(X,Y) ) &
    ||.c*x.|| = |.c.| * ||.x.|| &
    ||.x+y.|| <= ||.x.|| +  ||.y.|| by Th25;
   hence thesis by CLVECT_1:def 10;
end;

theorem Th27:
for X be non empty set, Y be ComplexNormSpace holds
  C_NormSpace_of_BoundedFunctions(X,Y) is ComplexNormSpace
proof
   let X be non empty set;
   let Y be ComplexNormSpace;
A1:C_NormSpace_of_BoundedFunctions(X,Y)
    = CNORMSTR (# ComplexBoundedFunctions(X,Y),
        Zero_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
        Add_(ComplexBoundedFunctions(X,Y), ComplexVectSpace(X,Y)),
        Mult_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
           ComplexBoundedFunctionsNorm(X,Y) #) by Def10;
    CLSStruct (# ComplexBoundedFunctions(X,Y),
        Zero_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
        Add_(ComplexBoundedFunctions(X,Y), ComplexVectSpace(X,Y)),
        Mult_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)) #)
          is ComplexLinearSpace;
   hence thesis by A1,Th26,CSSPACE3:4;
end;

registration
   let X be non empty set;
   let Y be ComplexNormSpace;
   cluster C_NormSpace_of_BoundedFunctions(X,Y) ->
           ComplexNormSpace-like ComplexLinearSpace-like
           Abelian add-associative right_zeroed right_complementable;
   coherence by Th27;
end;

theorem Th28:
for X be non empty set for Y be ComplexNormSpace,
 f,g,h be Point of C_NormSpace_of_BoundedFunctions(X,Y),
 f',g',h' be bounded Function of X,the carrier of Y st
  f'=f & g'=g & h'=h holds
   (h = f-g iff (for x be Element of X holds (h'.x = f'.x - g'.x)) )
proof
   let X be non empty set;
   let Y be ComplexNormSpace;
   let f,g,h be Point of C_NormSpace_of_BoundedFunctions(X,Y);
   let f',g',h' be bounded Function of X,the carrier of Y such that
A1:f'=f and
A2:g'=g and
A3:h'=h;
A4:now assume h=f-g;
     then h+g=f-(g-g) by RLVECT_1:43;
     then h+g=f-0.C_NormSpace_of_BoundedFunctions(X,Y) by RLVECT_1:28;
then A5:  h+g=f by RLVECT_1:26;
      now let x be Element of  X;
        f'.x=h'.x +  g'.x by A1,A2,A3,A5,Th23;
       then f'.x-g'.x=h'.x + (g'.x-g'.x) by RLVECT_1:42;
       then f'.x-g'.x=h'.x + 0.Y by RLVECT_1:28;
       hence f'.x-g'.x=h'.x by RLVECT_1:10;
     end;
     hence for x be Element of X holds (h'.x = f'.x - g'.x);
   end;
    now assume
A6:  for x be Element of X holds (h'.x = f'.x - g'.x);
        now let x be Element of  X;
          h'.x = f'.x - g'.x by A6;
       then h'.x + g'.x= f'.x - (g'.x- g'.x) by RLVECT_1:43;
       then h'.x + g'.x= f'.x - 0.Y by RLVECT_1:28;
       hence h'.x + g'.x= f'.x by RLVECT_1:26;
     end;
     then f=h+g by A1,A2,A3,Th23;
     then f-g=h+(g-g) by RLVECT_1:42;
     then f-g=h+0.C_NormSpace_of_BoundedFunctions(X,Y) by RLVECT_1:28;
     hence f-g=h by RLVECT_1:10;
   end;
   hence thesis by A4;
end;

Lm13:
for e be Real, seq be Real_Sequence st ( seq is convergent &
 ex k be Nat st (for i be Nat st k <= i holds seq.i <=e ) ) holds
  lim seq <=e
proof
   let e be Real;
   let seq be Real_Sequence such that
A1:seq is convergent and
A2:ex k be Nat st (for i be Nat st k <= i holds seq.i <=e );
   reconsider cseq = NAT --> e as Real_Sequence by FUNCOP_1:57;
A3:for n be Nat holds cseq.n=e by FUNCOP_1:13;
A5:cseq is convergent by SEQ_4:39;
   consider k be Nat such that
A6:for i be Nat st k <= i holds seq.i <=e by A2;
A7:(seq ^\k) is convergent by A1,SEQ_4:33;
A8:lim (seq ^\k)=lim seq by A1,SEQ_4:33;
A9:now let i be Nat;
A10: (seq ^\k).i = seq.(k+i) by SEQM_3:def 9;
        k <= k+i by NAT_1:29;
     then seq.(k+i) <=e by A6;
     hence (seq ^\k) .i <= cseq.i by A3,A10;
  end;
     lim cseq = cseq.0 by SEQ_4:41 .= e by A3;
  hence thesis by A5,A7,A8,A9,SEQ_2:32;
end;

theorem Th29:
for X be non empty set, Y be ComplexNormSpace st Y is complete
 for seq be sequence of C_NormSpace_of_BoundedFunctions(X,Y) st
  seq is Cauchy_sequence_by_Norm holds seq is convergent
proof
   let X be non empty set;
   let Y be ComplexNormSpace such that
A1:Y is complete;
   let vseq be sequence of C_NormSpace_of_BoundedFunctions(X,Y)
    such that
A2:vseq is Cauchy_sequence_by_Norm;
A3:||.vseq.|| is convergent
   proof
      now let e1 be real number such that
A4:    e1 >0;
       reconsider e =e1 as Real by XREAL_0:def 1;
       consider k be Nat such that
A5:    for n, m be Nat st n >= k & m >= k holds
        ||.(vseq.n) - (vseq.m).|| < e by A2,A4,CSSPACE3:10;
A6:    now
         let m be Nat such that
A7:      m >= k;
A8:      abs( ||.vseq.m.||- ||.vseq.k.|| )
           <= ||.(vseq.m) - (vseq.k).|| by CLVECT_1:111;
A9:      ||.vseq.k.||= ||.vseq.||.k by CLVECT_1:def 16;
A10:     ||.vseq.m.||= ||.vseq.||.m by CLVECT_1:def 16;
          ||.(vseq.m) - (vseq.k).|| <e by A5,A7;
         hence abs( ||.vseq.||.m - ||.vseq.||.k ) <e1
           by A8,A9,A10,AXIOMS:22;
       end;
       take k;
       thus for m be Nat st m >= k holds
             abs(||.vseq.||.m - ||.vseq.||.k ) < e1 by A6;
     end;
     hence ||.vseq.|| is convergent by SEQ_4:58;
   end;
   defpred P[set, set] means ex xseq be sequence of Y st
    (for n be Nat holds xseq.n=modetrans((vseq.n),X,Y).$1)
    & xseq is convergent & $2= lim xseq;
A11:C_NormSpace_of_BoundedFunctions(X,Y)
    = CNORMSTR (# ComplexBoundedFunctions(X,Y),
        Zero_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
        Add_(ComplexBoundedFunctions(X,Y), ComplexVectSpace(X,Y)),
        Mult_(ComplexBoundedFunctions(X,Y),ComplexVectSpace(X,Y)),
            ComplexBoundedFunctionsNorm(X,Y) #) by Def10;
A12:for x be Element of X ex y be Element of Y st  P[x,y]
   proof
     let x be Element of X;
     deffunc F(Nat) = modetrans((vseq.$1),X,Y).x;
     consider xseq be sequence of Y such that
A13:  for n be Nat holds xseq.n = F(n) from FUNCT_2:sch 4;
A14: for m,k be Nat holds
       ||.xseq.m-xseq.k.|| <= ||.vseq.m - vseq.k.||
     proof
       let m,k be Nat;
        vseq.m is bounded Function of X,the carrier of Y by A11,Def5;
then A15:   modetrans((vseq.m),X,Y)=vseq.m by Th17;
        vseq.k is bounded Function of X,the carrier of Y by A11,Def5;
then A16:   modetrans((vseq.k),X,Y)=vseq.k by Th17;
       reconsider h1=vseq.m-vseq.k
         as bounded Function of X,the carrier of Y by A11,Def5;
A17:   xseq.m = modetrans((vseq.m),X,Y).x by A13;
        xseq.k = modetrans((vseq.k),X,Y).x by A13;
       then xseq.m - xseq.k = h1.x by A15,A16,A17,Th28;
       hence thesis by Th20;
     end;
A18: xseq is convergent
     proof
        now
         let e be Real such that
A19:     e > 0;
         thus ex k be Nat st for n, m be Nat st n >= k & m >= k holds
           ||.xseq.n -xseq.m.|| < e
         proof
           consider k be Nat such that
A20:        for n, m be Nat st n >= k & m >= k holds
             ||.(vseq.n) - (vseq.m).|| < e by A2,A19,CSSPACE3:10;
           take k;
           thus for n, m be Nat st ( n >= k & m >= k )
                  holds ||.xseq.n-xseq.m.|| < e
           proof
             let n,m be Nat such that
A21:         n >=k & m >= k;
A22:         ||.xseq.n-xseq.m.|| <= ||.(vseq.n) - (vseq.m).|| by A14;
              ||.(vseq.n) - (vseq.m).|| < e by A20,A21;
             hence ||.xseq.n-xseq.m.|| < e by A22,AXIOMS:22;
           end;
         end;
       end;
       then xseq is Cauchy_sequence_by_Norm by CSSPACE3:10;
       hence thesis by A1,CLOPBAN1:def 14;
     end;
     take y = lim xseq;
     thus thesis by A13,A18;
   end;
   consider f be Function of  X,the carrier of Y such that
A23:for x be Element of X holds P[x,f.x] from FUNCT_2:sch 3(A12);
   reconsider tseq=f as Function of X,the carrier of Y;

A24:tseq is bounded
   proof
A25: now
       let x be Element of X;
       consider xseq be sequence of Y such that
A26:   (for n be Nat holds xseq.n=modetrans((vseq.n),X,Y).x)
       & xseq is convergent
       & tseq.x = lim xseq by A23;
A27:   for m be Nat holds ||.xseq.m.|| <= ||.vseq.m.||
       proof
         let m be Nat;
          vseq.m is bounded Function of X,the carrier of Y by A11,Def5;
then A28:     modetrans((vseq.m),X,Y)=vseq.m by Th17;
         reconsider h1=vseq.m as bounded Function of X,the carrier of Y
          by A11,Def5;
          xseq.m =modetrans((vseq.m),X,Y).x by A26;
         hence thesis by A28,Th20;
       end;
A29:   ||.xseq.|| is convergent by A26,CLOPBAN1:45;
A30:   ||.tseq.x.|| = lim ||.xseq.|| by A26,CLOPBAN1:45;
        for n be Nat holds ||.xseq.||.n  <=(||.vseq.||).n
       proof
         let n be Nat;
A31:     ||.xseq.||.n = ||.(xseq.n).|| by CLVECT_1:def 16;
          ||.(xseq.n).|| <= ||.vseq.n.|| by A27;
         hence thesis by A31,CLVECT_1:def 16;
       end;
       hence ||.tseq.x.|| <= lim (||.vseq.|| ) by A3,A29,A30,SEQ_2:32;
     end;
     take K= lim (||.vseq.|| );
      K >=0
     proof
        now let n be Nat;
          ||.vseq.n.|| >=0 by CLVECT_1:106;
         hence ||.vseq.||.n >=0 by CLVECT_1:def 16;
       end;
       hence thesis by A3,SEQ_2:31;
     end;
     hence thesis by A25;
   end;

A32:for e be Real st
    e > 0 ex k be Nat st for n be Nat st n >= k holds
     for x be Element of X holds
      ||.modetrans((vseq.n),X,Y).x - tseq.x.|| <= e
   proof
     let e be Real such that
A33: e > 0;
     consider k be Nat such that
A34: for n, m be Nat st ( n >= k & m >= k ) holds
      ||.(vseq.n) - (vseq.m).|| < e by A2,A33,CSSPACE3:10;
A35: now let n be Nat such that
A36:   n >= k;
        now let x be Element of X;
         consider xseq be sequence of Y such that
A37:      (for n be Nat holds  xseq.n=modetrans((vseq.n),X,Y).x)
          & xseq is convergent
          & tseq.x = lim xseq by A23;
A38:     for m,k be Nat holds
          ||.xseq.m-xseq.k.|| <= ||.vseq.m - vseq.k.||
         proof
           let m,k be Nat;
            vseq.m is bounded Function of X,the carrier of Y by A11,Def5;
then A39:       modetrans((vseq.m),X,Y)=vseq.m by Th17;
            vseq.k is bounded Function of X,the carrier of Y by A11,Def5;
then A40:       modetrans((vseq.k),X,Y)=vseq.k by Th17;
           reconsider h1=vseq.m-vseq.k
             as bounded Function of X,the carrier of Y by A11,Def5;
A41:       xseq.m =modetrans((vseq.m),X,Y).x by A37;
            xseq.k =modetrans((vseq.k),X,Y).x by A37;
           then xseq.m - xseq.k =h1.x by A39,A40,A41,Th28;
           hence thesis by Th20;
         end;
A42:     for m be Nat st m >=k holds ||.xseq.n-xseq.m.|| <= e
         proof
           let m be Nat such that
A43:       m >=k;
A44:       ||.xseq.n-xseq.m.|| <= ||.vseq.n - vseq.m.|| by A38;
            ||.vseq.n - vseq.m.|| <e by A34,A36,A43;
           hence thesis by A44,AXIOMS:22;
         end;
          ||.xseq.n-tseq.x.|| <= e
         proof
           deffunc F(Nat) = ||.xseq.$1 - xseq.n.||;
           consider rseq be Real_Sequence such that
A45:        for m be Nat holds rseq.m = F(m) from SEQ_1:sch 1;
A46:       rseq is convergent & lim rseq = ||.tseq.x-xseq.n.||
           proof
A47:         xseq - xseq.n is convergent by A37,CLVECT_1:117;
A48:         lim (xseq-xseq.n)= tseq.x - xseq.n by A37,CLVECT_1:123;
              rseq = ||.xseq - xseq.n.||
             proof
                now let x be set such that
A49:             x in NAT;
                 reconsider k=x as Nat by A49;
                 thus rseq.x = ||.xseq.k - xseq.n.|| by A45
                            .= ||.(xseq - xseq.n).k.|| by CLVECT_1:def 13
                            .= ||.(xseq - xseq.n).||.x by CLVECT_1:def 16;
               end;
               hence thesis by SEQ_1:8;
             end;
             hence thesis by A47,A48,CLOPBAN1:22;
           end;
            for m be Nat st m >= k holds rseq.m <= e
           proof
             let m be Nat such that
A50:         m >=k;
              rseq.m = ||.xseq.m-xseq.n.|| by A45
                   .= ||.xseq.n-xseq.m.|| by CLVECT_1:109;
             hence thesis by A42,A50;
           end;
           then lim rseq <= e by A46,Lm13;
           hence thesis by A46,CLVECT_1:109;
         end;
         hence ||.modetrans((vseq.n),X,Y).x - tseq.x.|| <= e by A37;
       end;
       hence for x be Element of X holds
          ||.modetrans((vseq.n),X,Y).x - tseq.x.|| <= e;
     end;
     take k;
     thus thesis by A35;
   end;
   reconsider tseq as bounded Function of X,the carrier of Y by A24;
   reconsider tv=tseq as Point of
    C_NormSpace_of_BoundedFunctions(X,Y) by A11,Def5;

A51:for e be Real st
    e > 0 ex k be Nat st for n be Nat st
     n >= k holds ||.vseq.n - tv.|| <= e
   proof
     let e be Real such that
A52: e > 0;
     consider k be Nat such that
A53: for n be Nat st n >= k holds
       for x be Element of X holds
         ||.modetrans((vseq.n),X,Y).x - tseq.x.|| <= e by A32,A52;
      now let n be Nat such that
A54:   n >= k;
       set f1=modetrans((vseq.n),X,Y);
       set g1=tseq;
       reconsider h1=vseq.n-tv
        as bounded Function of X,the carrier of Y by A11,Def5;
A55:   now
         let t be Element of  X;
          vseq.n is bounded Function of X,the carrier of Y by A11,Def5;
         then modetrans((vseq.n),X,Y)=vseq.n by Th17;
         then ||.h1.t.||= ||.f1.t-g1.t.|| by Th28;
         hence ||.h1.t.|| <=e by A53,A54;
       end;
A56:   now let r be Real such that
A57:     r in PreNorms(h1);
          r in {||.h1.t.|| where t is Element of X:not contradiction }
           by A57,Def8;
         then consider t be Element of X such that
A58:     r=||.h1.t.||;
         thus r <=e by A55,A58;
       end;
A59:   (for s be real number st s in PreNorms(h1) holds s <= e)
         implies sup PreNorms(h1) <=e by SEQ_4:62;
        ComplexBoundedFunctionsNorm(X,Y).(vseq.n-tv)
        = sup PreNorms(h1) by Th18;
       hence ||.vseq.n-tv.|| <=e by A11,A56,A59,CLVECT_1:def 9;
     end;
     hence thesis;
   end;
    for e be Real st e > 0 ex m be Nat st for n be Nat st
     n >= m holds ||.(vseq.n) - tv.|| < e
   proof
     let e be Real such that
A60: e > 0;
A61: e/2 > 0 by A60,SEQ_2:3;
A62: e/2<e by A60,SEQ_2:4;
     consider m be Nat such that
A63: for n be Nat st n >= m holds ||.(vseq.n) - tv.|| <= e/2 by A51,A61;
      now let n be Nat such that
A64:   n >= m;
        ||.(vseq.n) - tv.|| <= e/2 by A63,A64;
       hence ||.(vseq.n) - tv.|| < e by A62,AXIOMS:22;
     end;
     hence thesis;
   end;
   hence thesis by CLVECT_1:def 15;
end;

theorem Th30:
for X be non empty set, Y be ComplexBanachSpace holds
 C_NormSpace_of_BoundedFunctions(X,Y) is ComplexBanachSpace
proof
   let X be non empty set;
   let Y be ComplexBanachSpace;
    for seq be sequence of C_NormSpace_of_BoundedFunctions(X,Y)
      st seq is Cauchy_sequence_by_Norm holds seq is convergent by Th29;
    hence thesis by CLOPBAN1:def 14;
end;

registration
  let X be non empty set;
  let Y be ComplexBanachSpace;
  cluster C_NormSpace_of_BoundedFunctions (X,Y) -> complete;
  coherence by Th30;
end;

begin :: Some properties of complex sequences

theorem
 for seq1,seq2 be Complex_Sequence st
 seq1 is bounded & seq2 is bounded holds
  seq1 + seq2 is bounded by Lm3;

theorem
 for c be Complex, seq be Complex_Sequence st seq is bounded holds
 c(#)seq is bounded by Lm4;

theorem
 for seq be Complex_Sequence holds seq is bounded iff
 |.seq.| is bounded by Lm9,Lm10;

theorem
 for seq1,seq2,seq3 be Complex_Sequence holds
 seq1 = seq2 - seq3 iff
  ( for n be Nat holds seq1.n = seq2.n - seq3.n ) by Lm12;
