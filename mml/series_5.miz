:: On the Partial Product and Partial Sum of Series and Related Basic
:: Inequalities
::  by Fuguo Ge and Xiquan Liang
::
:: Received November 23, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies ARYTM, ARYTM_1, ARYTM_3, ASYMPT_0, SQUARE_1, GROUP_1, ABSVALUE,
      FUNCT_1, POWER, SEQ_1, SERIES_3, SERIES_1;
 notations ORDINAL1, XCMPLX_0, XXREAL_0, XREAL_0, NAT_1, SQUARE_1, NEWTON,
      COMPLEX1, SEQ_1, POWER, SERIES_1, SERIES_3;
 constructors NAT_1, SQUARE_1, REAL_1, BINOP_2, SIN_COS, SERIES_1, SEQ_1,
      SERIES_3, XXREAL_0;
 registrations XREAL_0, SQUARE_1, NEWTON, MEMBERED, XCMPLX_0, INT_1, SERIES_3,
      XXREAL_0;
 requirements REAL, SUBSET, BOOLE, NUMERALS, ARITHM;
 definitions SQUARE_1;
 theorems XCMPLX_1, SQUARE_1, SIN_COS2, NEWTON, POWER, XREAL_1, PREPOWER,
      SERIES_1, SERIES_3, COMPLEX1, ABSVALUE, XXREAL_0;
 schemes NAT_1, BINOM;

begin

 reserve a,b,c,d,e,f for positive (real number),
         m,u,w,x,y,z for real number,
         n,k for Nat,
         s,s1,s2,s3,s4,s5 for Real_Sequence;

Lm2:x|^3-y|^3=(x-y)*(x^2+x*y+y^2)
proof
  (x-y)*(x^2+x*y+y^2) = x*x^2+x*x*y+x*y^2-(y*x^2+y*x*y+y*y^2)
  .=x*x|^2+x*x*y+x*y^2-(y*x^2+y*x*y+y*y^2) by NEWTON:100
  .=x|^(2+1)+x*x*y+x*y^2-(y*x^2+y*x*y+y*y^2) by NEWTON:11
  .=x|^3+x*x*y+x*y^2-(y*x^2+y*x*y+y*y^2)
  .=x|^3+x*(y*y)-y*x*y-y*y^2
  .=x|^3-y|^2*y by NEWTON:100
  .=x|^3-y|^(2+1) by NEWTON:11;
  hence thesis;
end;

Lm3:2/((1/a)+(1/b))=2*(a*b)/(a+b)
proof
  1/((1/a)+(1/b)) = 1/((1*b+1*a)/(a*b)) by XCMPLX_1:117
  .=((a*b)*1)/(b+a) by XCMPLX_1:78
  .=(a*b)/(b+a);then
  (2*1)/((1/a)+(1/b))=2*((a*b)/(b+a)) by XCMPLX_1:75;
  hence thesis by XCMPLX_1:75;
end;

Lm4:(1/x)*(1/y)*(1/z)=1/(x*y*z)
proof
  (1/x)*(1/y)*(1/z) = 1/(x*y)*(1/z) by XCMPLX_1:103
     .=1/((x*y)*z) by XCMPLX_1:103;
  hence thesis;
end;

Lm5:
  for b being real number holds (a/c) to_power -b=(c/a) to_power b
proof
  let b be real number;
  (a/c) to_power (-b) = (1/(a/c)) to_power b by POWER:37
    .=(c/a*1) to_power b by XCMPLX_1:81
    .=(c/a) to_power b;
  hence thesis;
end;

Lm6:((sqrt(a*b))^2+(sqrt(c*d))^2)*((sqrt(a*c))^2+(sqrt(b*d))^2)
    >=b*c*(a+d)^2
proof
A3:a*d*b^2>=0 & a*d*c^2>=0 by XREAL_1:129;
A4:((sqrt(a*b))^2+(sqrt(c*d))^2)*((sqrt(a*c))^2+(sqrt(b*d))^2)
     =(a*b+(sqrt(c*d))^2)*((sqrt(a*c))^2+(sqrt(b*d))^2) by SQUARE_1:def 4
    .=(a*b+c*d)*((sqrt(a*c))^2+(sqrt(b*d))^2) by SQUARE_1:def 4
    .=(a*b+c*d)*(a*c+(sqrt(b*d))^2) by SQUARE_1:def 4
    .=(a*b+c*d)*(a*c+b*d) by SQUARE_1:def 4
    .=(a^2)*b*c+a*b^2*d+c^2*d*a+b*c*(d^2);
   a*b^2*d+c^2*d*a>=2*sqrt((a*b^2*d)*(c^2*d*a)) by A3,SIN_COS2:1;then
   (a*b^2*d+c^2*d*a)+((a^2)*b*c+b*c*(d^2))>=
     2*sqrt(a^2*b^2*c^2*d^2)+((a^2)*b*c+b*c*(d^2)) by XREAL_1:9;then
   a*b^2*d+c^2*d*a+(a^2)*b*c+b*c*(d^2)>=
     2*sqrt(a^2*b^2*c^2*d^2)+(a^2)*b*c+b*c*(d^2);then
   a*b^2*d+c^2*d*a+(a^2)*b*c+b*c*(d^2)>=
     2*sqrt(((a*b)*(c*d))^2)+(a^2)*b*c+b*c*(d^2);then
   a*b^2*d+c^2*d*a+(a^2)*b*c+b*c*(d^2)>=
     2*(a*b*c*d)+(a^2)*b*c+b*c*(d^2) by SQUARE_1:89;
   hence thesis by A4;
end;

Lm8:(x+y+z)^2=x^2+y^2+z^2+2*x*y+2*y*z+2*z*x;

Lm13:abs x<1 implies x^2<1
proof
  assume abs x<1; then
  (abs x)^2<1 by SQUARE_1:78,SQUARE_1:59;
  hence thesis by COMPLEX1:161;
end;

Lm15:x^2<1 implies abs x<1
proof
  assume
A1:x^2<1;
  x^2>=0 by XREAL_1:65;then
  sqrt(x^2)<1 by A1,SQUARE_1:95,SQUARE_1:83;
  hence thesis by COMPLEX1:158;
end;

Lm21: 2*a^2*sqrt(b*c)*2*b^2*sqrt(a*c)*2*c^2*sqrt(a*b) = (2*a*b*c)|^3
proof
  2*a^2*sqrt(b*c)*2*b^2*sqrt(a*c)*2*c^2*sqrt(a*b)
   =2^2*2*a^2*b^2*c^2*sqrt(b*c)*sqrt(a*c)*sqrt(a*b)
  .=2|^2*2*a^2*b^2*c^2*sqrt(b*c)*sqrt(a*c)*sqrt(a*b) by NEWTON:100
  .=2|^(2+1)*a^2*b^2*c^2*sqrt(b*c)*sqrt(a*c)*sqrt(a*b) by NEWTON:11
  .=2|^3*a^2*b^2*c^2*sqrt(b*c)*sqrt(a*c)*sqrt(a*b)
  .=2|^3*a^2*b^2*c^2*(sqrt(b*c)*sqrt(a*c))*sqrt(a*b)
  .=2|^3*a^2*b^2*c^2*sqrt((b*c)*(a*c))*sqrt(a*b) by SQUARE_1:97
  .=2|^3*a^2*b^2*c^2*sqrt(b*c*a*c)*sqrt(a*b)
  .=2|^3*a^2*b^2*c^2*(sqrt(b*c*a*c)*sqrt(a*b))
  .=2|^3*a^2*b^2*c^2*sqrt((b*c*a*c)*(a*b)) by SQUARE_1:97
  .=2|^3*a^2*b^2*c^2*sqrt((a*b*c)^2)
  .=2|^3*a^2*b^2*c^2*(a*b*c) by SQUARE_1:89
  .=2|^3*(a^2*a)*(b^2*b)*(c^2*c)
  .=2|^3*(a|^2*a)*(b^2*b)*(c^2*c) by NEWTON:100
  .=2|^3*(a|^2*a)*(b|^2*b)*(c^2*c) by NEWTON:100
  .=2|^3*(a|^2*a)*(b|^2*b)*(c|^2*c) by NEWTON:100
  .=2|^3*a|^(2+1)*(b|^2*b)*(c|^2*c) by NEWTON:11
  .=2|^3*a|^3*b|^3*(c|^2*c) by NEWTON:11
  .=2|^3*a|^3*b|^3*c|^(2+1) by NEWTON:11
  .=(2*a)|^3*b|^3*c|^3 by NEWTON:12
  .=(2*a*b)|^3*c|^3 by NEWTON:12
  .=(2*a*b*c)|^3 by NEWTON:12;
  hence thesis;
end;

Lm22:sqrt(a^2+a*b+b^2)=(1/2)*sqrt(4*(a^2+b^2)+4*a*b)
proof
   a^2+a*b>0 by XREAL_1:36;then
B1:a^2+a*b+b^2>0 by XREAL_1:36;
   a^2+b^2>0 by XREAL_1:36;then
A4:4*(a^2+b^2)>0 by XREAL_1:131;
A5:4*(a^2+b^2)+4*a*b>0 by A4,XREAL_1:36;then
   sqrt(4*(a^2+b^2)+4*a*b)>0 by SQUARE_1:93;then
A6:(1/2)*sqrt(4*(a^2+b^2)+4*a*b)>0 by XREAL_1:131;
   ((1/2)*sqrt(4*(a^2+b^2)+4*a*b))^2
   =(1/2)*(1/2)*(sqrt(4*(a^2+b^2)+4*a*b))^2
   .=(1/4)*(4*(a^2+b^2)+4*a*b) by A5,SQUARE_1:def 4
   .=(sqrt(a^2+a*b+b^2))^2 by B1,SQUARE_1:def 4;then
A7:sqrt((sqrt(a^2+a*b+b^2))^2)
    =(1/2)*sqrt(4*(a^2+b^2)+4*a*b) by A6,SQUARE_1:89;
   sqrt(a^2+a*b+b^2)>0 by B1,SQUARE_1:93;
   hence thesis by SQUARE_1:89,A7;
end;

Lm23:sqrt(a^2+a*b+b^2)>=(1/2)*sqrt(3)*(a+b)
proof
   a^2+b^2>=2*a*b by SERIES_3:6;then
A2:a^2+b^2+(3*(a^2+b^2)+4*a*b)>=2*a*b+(3*(a^2+b^2)+4*a*b) by XREAL_1:8;
   a^2+b^2>0 by XREAL_1:36;then
A3:3*(a^2+b^2)>0 by XREAL_1:131;
   3*(a^2+b^2)+6*a*b>0 by A3,XREAL_1:36;then
   sqrt(4*(a^2+b^2)+4*a*b)>=sqrt(3*(a^2+b^2)+6*a*b) by A2,SQUARE_1:94;then
A4:(1/2)*sqrt(4*(a^2+b^2)+4*a*b)>=(1/2)*sqrt(3*(a^2+b^2)+6*a*b)
     by XREAL_1:66;
A5:sqrt(3*(a^2+b^2)+6*a*b) = sqrt(3*(a+b)^2);
   sqrt(3*(a^2+b^2)+6*a*b) = sqrt(3)*sqrt((a+b)^2) by SQUARE_1:97,A5
     .=sqrt(3)*(a+b) by SQUARE_1:89;
   hence thesis by A4,Lm22;
end;

Lm24:sqrt((a^2+a*b+b^2)/3)<=sqrt((a^2+b^2)/2)
proof
   (a^2+b^2)/2>=a*b by SERIES_3:7;then
   (a^2+b^2)/2+(a^2+b^2)>=a*b+(a^2+b^2) by XREAL_1:8;then
A1:(a^2+b^2+(a^2+b^2)/2)/3>=(a^2+a*b+b^2)/3 by XREAL_1:74;
   a^2+a*b>0 by XREAL_1:36;then
   a^2+a*b+b^2>0 by XREAL_1:36;then
   (a^2+a*b+b^2)/3>0 by XREAL_1:141;
   hence thesis by A1,SQUARE_1:94;
end;

Lm26:((b*c)/a)^2+((c*a)/b)^2>=2*c^2
proof
  ((b*c)/a)^2+((c*a)/b)^2>=2*((b*c)/a)*((c*a)/b) by SERIES_3:6;then
  ((b*c)/a)^2+((c*a)/b)^2>=2*(((b*c)/a)*((c*a)/b));then
  ((b*c)/a)^2+((c*a)/b)^2>=2*(((b*c)*(c*a))/(a*b)) by XCMPLX_1:77;then
  ((b*c)/a)^2+((c*a)/b)^2>=2*(((b*a)*(c*c))/((a*b)*1));then
  ((b*c)/a)^2+((c*a)/b)^2>=2*((c*c)/1) by XCMPLX_1:92;
  hence thesis;
end;

Lm27:((b*c)/a)*((c*a)/b)=c^2
proof
  ((b*c)/a)*((c*a)/b) = ((b*c)*(c*a))/(a*b) by XCMPLX_1:77
   .=((c*c)*(a*b))/(a*b)
   .=(c*c)*((a*b)/(a*b)) by XCMPLX_1:75
   .=(c*c)*1 by XCMPLX_1:60;
  hence thesis;
end;

Lm28:2*((b*c)/a)*((c*a)/b)+2*((b*c)/a)*((a*b)/c)+2*((c*a)/b)*((a*b)/c)
    =2*(a^2+b^2+c^2)
proof
  2*((b*c)/a)*((c*a)/b)+2*((b*c)/a)*((a*b)/c)+2*((c*a)/b)*((a*b)/c)
   =2*(((b*c)/a)*((c*a)/b))+2*((b*c)/a)*((a*b)/c)+2*((c*a)/b)*((a*b)/c)
  .=2*c^2+2*(((b*c)/a)*((a*b)/c))+2*((c*a)/b)*((a*b)/c) by Lm27
  .=2*c^2+2*b^2+2*(((c*a)/b)*((a*b)/c)) by Lm27
  .=2*c^2+2*b^2+2*a^2 by Lm27
  .=2*(c^2+b^2+a^2);
  hence thesis;
end;

Lm29:((b*c)/a+(c*a)/b+(a*b)/c)^2=((c*a)/b)^2+((a*b)/c)^2+((b*c)/a)^2+
     2*(a^2+b^2+c^2)
proof
  ((b*c)/a+(c*a)/b+(a*b)/c)^2
  =((b*c)/a)^2+((c*a)/b)^2+((a*b)/c)^2+(2*((b*c)/a)*((c*a)/b)+2*((b*c)/a)
    *((a*b)/c)+2*((c*a)/b)*((a*b)/c))
  .=((b*c)/a)^2+((c*a)/b)^2+((a*b)/c)^2+2*(a^2+b^2+c^2) by Lm28;
  hence thesis;
end;

Lm30:a+b+c = 1 implies 1/a-1=(b+c)/a
proof
  assume a+b+c =1;then
  1/a-1 = (a+(b+c))/a-1
   .=a/a+(b+c)/a-1 by XCMPLX_1:63
   .=1+(b+c)/a-1 by XCMPLX_1:60;
  hence thesis;
end;

Lm31:((2*sqrt(b*c))/a)*((2*sqrt(a*c))/b)*((2*sqrt(a*b))/c) = 8
proof
A2:b*a*c^2>0 by XREAL_1:131;
   ((2*sqrt(b*c))/a)*((2*sqrt(a*c))/b)*((2*sqrt(a*b))/c)
   =(((2*sqrt(b*c))*(2*sqrt(a*c)))/(a*b))*((2*sqrt(a*b))/c) by XCMPLX_1:77
  .=((4*(sqrt(b*c)*sqrt(a*c)))/(a*b))*((2*sqrt(a*b))/c)
  .=((4*sqrt((b*c)*(a*c)))/(a*b))*((2*sqrt(a*b))/c) by SQUARE_1:97
  .=((4*sqrt(b*a*c^2))*(2*sqrt(a*b)))/((a*b)*c) by XCMPLX_1:77
  .=(8*(sqrt(b*a*c^2)*sqrt(a*b)))/(a*b*c)
  .=(8*sqrt((b*a*c^2)*(a*b)))/(a*b*c) by A2,SQUARE_1:97
  .=(8*sqrt((a*b*c)^2))/(a*b*c)
  .=(8*(a*b*c))/(a*b*c) by SQUARE_1:89
  .=8*((a*b*c)/(a*b*c)) by XCMPLX_1:75
  .=8*1 by XCMPLX_1:60;
  hence thesis;
end;

Lm32:a+b+c = 1 implies 1+1/a=2+(b+c)/a
proof
  assume a+b+c =1;then
  1+1/a = 1+(a+(b+c))/a
   .=1+(a/a+(b+c)/a) by XCMPLX_1:63
   .=1+a/a+(b+c)/a
   .=1+1+(b+c)/a by XCMPLX_1:60;
  hence thesis;
end;

Lm33:(1+sqrt(a*c)/b)*(sqrt(a*b)/c)=sqrt(a*b)/c+a/sqrt(b*c)
proof
A3:sqrt(b*c)>0 by SQUARE_1:93;
   (1+sqrt(a*c)/b)*(sqrt(a*b)/c)
   =1*(sqrt(a*b)/c)+(sqrt(a*c)/b)*(sqrt(a*b)/c)
  .=sqrt(a*b)/c+(sqrt(a*c)*sqrt(a*b))/(b*c) by XCMPLX_1:77
  .=sqrt(a*b)/c+(sqrt((a*c)*(a*b)))/(b*c) by SQUARE_1:97
  .=sqrt(a*b)/c+(sqrt((a^2)*(c*b)))/(b*c)
  .=sqrt(a*b)/c+(sqrt(a^2)*sqrt(c*b))/(b*c) by SQUARE_1:97
  .=sqrt(a*b)/c+(sqrt(a^2)*sqrt(c*b))/(sqrt(b*c))^2 by SQUARE_1:def 4
  .=sqrt(a*b)/c+(sqrt(a^2)/sqrt(b*c))*(sqrt(c*b)/sqrt(b*c)) by XCMPLX_1:77
  .=sqrt(a*b)/c+(sqrt(a^2)/sqrt(b*c))*(1) by A3,XCMPLX_1:60
  .=sqrt(a*b)/c+a/sqrt(b*c) by SQUARE_1:89;
  hence thesis;
end;

Lm34:(sqrt(b*c)/a+c/sqrt(b*a))*(sqrt(a*b)/c) =b/sqrt(a*c)+1
proof
A3:sqrt(a*c)>0 & sqrt(a*b)>0 by SQUARE_1:93;
   (sqrt(b*c)/a+c/sqrt(b*a))*(sqrt(a*b)/c)
    =(sqrt(b*c)/a)*(sqrt(a*b)/c)+(c/sqrt(b*a))*(sqrt(a*b)/c)
   .=((sqrt(b*c)*sqrt(a*b))/(a*c))+(c/sqrt(b*a))*(sqrt(a*b)/c) by XCMPLX_1:77
   .=((sqrt((b*c)*(a*b)))/(a*c))+(c/sqrt(b*a))*(sqrt(a*b)/c) by SQUARE_1:97
   .=((sqrt((a*c)*b^2))/(a*c))+(c/sqrt(b*a))*(sqrt(a*b)/c)
   .=((sqrt(a*c)*sqrt(b^2))/(a*c))+(c/sqrt(b*a))*(sqrt(a*b)/c)
       by SQUARE_1:97
   .=(sqrt(b^2)*sqrt(a*c))/(sqrt(a*c))^2+(c/sqrt(b*a))*(sqrt(a*b)/c)
     by SQUARE_1:def 4
   .=(sqrt(b^2)/sqrt(a*c))*(sqrt(a*c)/sqrt(a*c))+(c/sqrt(b*a))*(sqrt(a*b)/c)
       by XCMPLX_1:77
   .=(sqrt(b^2)/sqrt(a*c))*1+(c/sqrt(b*a))*(sqrt(a*b)/c) by A3,XCMPLX_1:60
   .=b/sqrt(a*c)+(c/sqrt(b*a))*(sqrt(a*b)/c) by SQUARE_1:89
   .=b/sqrt(a*c)+(c/c)*(sqrt(a*b)/sqrt(b*a)) by XCMPLX_1:86
   .=b/sqrt(a*c)+1*(sqrt(a*b)/sqrt(b*a)) by XCMPLX_1:60
   .=b/sqrt(a*c)+1 by A3,XCMPLX_1:60;
   hence thesis;
end;

Lm35:(1+sqrt(b*c)/a)*(1+sqrt(a*c)/b)*(1+sqrt(a*b)/c)
    =2+sqrt(a*c)/b+b/sqrt(a*c)+sqrt(a*b)/c+c/sqrt(b*a)+a/sqrt(b*c)+sqrt(b*c)/a
proof
A3:sqrt(b*a)>0 & sqrt(a*c)>0 & sqrt(b*c)>0 by SQUARE_1:93;
   (1+sqrt(b*c)/a)*(1+sqrt(a*c)/b)*(1+sqrt(a*b)/c)
   =(1*1+1*(sqrt(a*c)/b)+(sqrt(b*c)/a)*1+(sqrt(b*c)/a)*(sqrt(a*c)/b))*
     (1+sqrt(a*b)/c)
  .=(1+sqrt(a*c)/b+sqrt(b*c)/a+(sqrt(b*c)*sqrt(a*c))/(a*b))*
     (1+sqrt(a*b)/c) by XCMPLX_1:77
  .=(1+sqrt(a*c)/b+sqrt(b*c)/a+(sqrt((b*c)*(a*c)))/(a*b) )*
     (1+sqrt(a*b)/c) by SQUARE_1:97
  .=(1+sqrt(a*c)/b+sqrt(b*c)/a+(sqrt(b*a*c^2))/(a*b) )*
     (1+sqrt(a*b)/c)
  .=(1+sqrt(a*c)/b+sqrt(b*c)/a+(sqrt(b*a)*sqrt(c^2))/(a*b) )*
     (1+sqrt(a*b)/c) by SQUARE_1:97
  .=(1+sqrt(a*c)/b+sqrt(b*c)/a+(sqrt(b*a)*c)/(a*b) )*
     (1+sqrt(a*b)/c) by SQUARE_1:89
  .=(1+sqrt(a*c)/b+sqrt(b*c)/a+(sqrt(b*a)*c)/(sqrt(a*b))^2)*
     (1+sqrt(a*b)/c) by SQUARE_1:def 4
  .=(1+sqrt(a*c)/b+sqrt(b*c)/a+((sqrt(b*a)/sqrt(b*a))*(c/sqrt(b*a))))*
     (1+sqrt(a*b)/c) by XCMPLX_1:77
  .=(1+sqrt(a*c)/b+sqrt(b*c)/a+(1*(c/sqrt(b*a))))*
     (1+sqrt(a*b)/c) by A3,XCMPLX_1:60
  .=1+sqrt(a*c)/b+(1+sqrt(a*c)/b)*(sqrt(a*b)/c)+sqrt(b*c)/a+c/sqrt(b*a)
    +(sqrt(b*c)/a+c/sqrt(b*a))*(sqrt(a*b)/c)
  .=1+sqrt(a*c)/b+(sqrt(a*b)/c+a/sqrt(b*c))+sqrt(b*c)/a+c/sqrt(b*a)
    +(sqrt(b*c)/a+c/sqrt(b*a))*(sqrt(a*b)/c) by Lm33
  .=1+sqrt(a*c)/b+sqrt(a*b)/c+a/sqrt(b*c)+sqrt(b*c)/a+c/sqrt(b*a)
    +(b/sqrt(a*c)+1) by Lm34
  .=2+sqrt(a*c)/b+b/sqrt(a*c)+sqrt(a*b)/c+c/sqrt(b*a)+a/sqrt(b*c)+sqrt(b*c)/a;
  hence thesis;
end;

Lm36:sqrt(a*c)/b+b/sqrt(a*c)+sqrt(a*b)/c+c/sqrt(b*a)+a/sqrt(b*c)+sqrt(b*c)/a
     >=6
proof
A2:sqrt(b*a)>0 & sqrt(a*c)>0 & sqrt(b*c)>0 by SQUARE_1:93;then
A3:sqrt(a*c)/b+b/sqrt(a*c)>=2 by SERIES_3:3;
   sqrt(a*b)/c+c/sqrt(b*a)>=2 by A2,SERIES_3:3;then
A4:sqrt(a*c)/b+b/sqrt(a*c)+(sqrt(a*b)/c+c/sqrt(b*a))>=2+2
     by A3,XREAL_1:9;
   a/sqrt(b*c)+sqrt(b*c)/a>=2 by A2,SERIES_3:3;then
   sqrt(a*c)/b+b/sqrt(a*c)+sqrt(a*b)/c+c/sqrt(b*a)+(a/sqrt(b*c)+sqrt(b*c)/a)
     >=4+2 by A4,XREAL_1:9;
   hence thesis;
end;

theorem
  (a+b)*(1/a+1/b)>=4
proof
  a/b+b/a>=2*sqrt((a/b)*(b/a)) by SIN_COS2:1;then
  a/b+b/a>=2*sqrt((a*b)/(b*a)) by XCMPLX_1:77;then
  a/b+b/a>=2*sqrt((a/b*b)/a) by XCMPLX_1:84;then
  a/b+b/a>=2*sqrt(a/a) by XCMPLX_1:88;then
  a/b+b/a>=2*1 by XCMPLX_1:60,SQUARE_1:83;then
  (a/b+b/a)+2>=2+2 by XREAL_1:9;then
  a/b+b/a+1+1>=4;then
  a*(1/b)+b/a+1+1>=4 by XCMPLX_1:100;then
  a*(1/b)+b*(1/a)+1+1>=4 by XCMPLX_1:100;then
  a*(1/b)+b*(1/a)+a*(1/a)+1>=4 by XCMPLX_1:107;then
  a*(1/b)+b*(1/a)+a*(1/a)+b*(1/b)>=4 by XCMPLX_1:107;
  hence thesis;
end;

theorem
  a|^4+b|^4>=a|^3*b+a*b|^3
proof
A1:(a-b)^2>=0 by XREAL_1:65;
   a^2+a*b>0 by XREAL_1:36;then
   a^2+a*b+b^2>0 by XREAL_1:36;then
   ((a-b)*(a-b))*(a^2+a*b+b^2)>=0 by A1,XREAL_1:129;then
   (a-b)*((a-b)*(a^2+a*b+b^2))>=0;then
   (a-b)*(a|^3-b|^3)>=0 by Lm2;then
   a|^3*a-a|^3*b-b|^3*(a-b)>=0;then
   a|^(3+1)-a|^3*b-b|^3*(a-b)>=0 by NEWTON:11;then
   a|^4-a|^3*b-(b|^3*a-b|^3*b)>=0;then
   a|^4-a|^3*b-(b|^3*a-b|^(3+1))>=0 by NEWTON:11;then
   a|^4-a|^3*b-b|^3*a+b|^(3+1)>=0;then
   a|^4-a|^3*b-b|^3*a+b|^4>=0;then
   (a|^4+b|^4-a|^3*b-b|^3*a)+(a|^3*b+b|^3*a)>=0+(a|^3*b+b|^3*a) by XREAL_1:9;
   hence thesis;
end;

theorem Th1:
  a<b implies 1<(b+c)/(a+c)
proof
  assume a<b; then
  a+c<b+c by XREAL_1:10;
  hence thesis by XREAL_1:189;
end;

theorem Th2:
  a<b implies a/b<sqrt(a/b)
proof
   assume
A1:a<b;
   b^2>0;then
   b|^2>0 by NEWTON:100;then
   b|^2*b>0 by XREAL_1:131;then
A2:b|^(2+1)>0 by NEWTON:11;
   a-b<0 by A1,XREAL_1:51;then
   (a*b)*(a-b)<0 by XREAL_1:134;then
   a^2*b-a*b^2+(a*b^2)<0+(a*b^2) by XREAL_1:10;then
   (a^2*b)/(b|^(2+1))<(a*b^2)/(b|^(2+1)) by A2,XREAL_1:76;then
   (a^2*b)/(b|^2*b)<(a*b^2)/(b|^(2+1)) by NEWTON:11;then
   (a^2*b)/(b|^2*b)<(a*b^2)/(b|^2*b) by NEWTON:11;then
   (a^2*b)/(b^2*b)<(a*b^2)/(b|^2*b) by NEWTON:100;then
   (a^2*b)/(b^2*b)<(a*b^2)/(b^2*b) by NEWTON:100;then
   a^2/b^2<(a*b^2)/(b*b^2) by XCMPLX_1:92;then
   a^2/b^2<a/b by XCMPLX_1:92;then
   (a/b)^2<a/b by XCMPLX_1:77;then
   sqrt((a/b)^2)<sqrt(a/b) by SQUARE_1:95;
   hence thesis by SQUARE_1:89;
end;

theorem Th3:
  a<b implies sqrt(a/b)<(b+sqrt((a^2+b^2)/2))/(a+sqrt((a^2+b^2)/2))
proof
   assume
A1:a<b;
   a^2+b^2>0 by XREAL_1:36;then
   (a^2+b^2)/2>0 by XREAL_1:141;then
   sqrt((a^2+b^2)/2)>0 by SQUARE_1:93;then
A3:1<(b+sqrt((a^2+b^2)/2))/(a+sqrt((a^2+b^2)/2)) by A1,Th1;
   a/b<1 by A1,XREAL_1:191;then
   sqrt(a/b)<1 by SQUARE_1:83,95;
   hence thesis by A3,XXREAL_0:2;
end;

theorem
  a<b implies a/b<(b+sqrt((a^2+b^2)/2))/(a+sqrt((a^2+b^2)/2))
proof
   assume
A1:a<b;then
A2:a/b<sqrt(a/b) by Th2;
   sqrt(a/b)<(b+sqrt((a^2+b^2)/2))/(a+sqrt((a^2+b^2)/2)) by A1,Th3;
   hence thesis by A2,XXREAL_0:2;
end;

theorem Th5:
  2/((1/a)+(1/b))<=sqrt(a*b)
proof
A5:sqrt(a*b)>0 by SQUARE_1:93;then
A6:2*sqrt(a*b)>0 by XREAL_1:131;
   a+b>=2*sqrt(a*b) by SIN_COS2:1;then
   (2*sqrt(a*b))/(a+b)<=1 by A6,XREAL_1:185;then
   ((2*sqrt(a*b))/(a+b))*sqrt(a*b)<=1*sqrt(a*b) by A5,XREAL_1:66;then
   (2*sqrt(a*b))/((a+b)/sqrt(a*b))<=sqrt(a*b) by XCMPLX_1:83;then
   ((2*sqrt(a*b))*sqrt(a*b))/(a+b)<=sqrt(a*b) by XCMPLX_1:78;then
   (2*(sqrt(a*b))^2)/(a+b)<=sqrt(a*b);then
   2*(a*b)/(a+b)<=sqrt(a*b) by SQUARE_1:def 4;
   hence thesis by Lm3;
end;

theorem Th6:
  (a+b)/2<=sqrt((a^2+b^2)/2)
proof
A3:a^2+b^2>=0 by XREAL_1:35;
A4:sqrt(2)>0 by SQUARE_1:93;
   a^2+b^2>=2*a*b by SERIES_3:6;then
   (a^2+b^2)+(a^2+b^2)>=2*a*b+(a^2+b^2) by XREAL_1:9;then
   sqrt(2*(a^2+b^2))>=sqrt((a+b)^2) by SQUARE_1:94;then
   sqrt(2*(a^2+b^2))>=(a+b) by SQUARE_1:89;then
   (sqrt(2*(a^2+b^2)))/sqrt(2*2)>=(a+b)/2 by SQUARE_1:85,XREAL_1:74;then
   (sqrt(2*(a^2+b^2)))/(sqrt(2)*sqrt(2))>=(a+b)/2 by SQUARE_1:97;then
   (sqrt(2)*sqrt(a^2+b^2))/(sqrt(2)*sqrt(2))>=(a+b)/2 by A3,SQUARE_1:97;then
   (sqrt(2)/sqrt(2)*sqrt(a^2+b^2))/sqrt(2)>=(a+b)/2 by XCMPLX_1:84;then
   (1*sqrt(a^2+b^2))/sqrt(2)>=(a+b)/2 by A4,XCMPLX_1:60;
   hence thesis by A3,SQUARE_1:99;
end;

theorem
  x+y<=sqrt(2*(x^2+y^2))
proof
   x^2+y^2>=2*x*y by SERIES_3:6;then
A3:x^2+y^2+(x^2+y^2)>=2*x*y+(x^2+y^2) by XREAL_1:8;then
A1:2*(x^2+y^2)>=(x+y)^2;
B1:(x+y)^2>=0 by XREAL_1:65;
A4:sqrt(2*(x^2+y^2))>=sqrt((x+y)^2) by A3,B1,SQUARE_1:94;
   per cases;
   suppose x+y>0;
   hence thesis by A4,SQUARE_1:89;
   end;
   suppose x+y=0;
   hence thesis by A1,SQUARE_1:94,SQUARE_1:82;
   end;
   suppose x+y<0;
   hence thesis by A3,B1,SQUARE_1:def 4;
   end;
end;

theorem Th7:
  2/((1/a)+(1/b))<=(a+b)/2
proof
A1:2/((1/a)+(1/b))<=sqrt(a*b) by Th5;
   (a+b)/2>=sqrt(a*b) by SERIES_3:2;
   hence thesis by A1,XXREAL_0:2;
end;

theorem
  sqrt(a*b)<=sqrt((a^2+b^2)/2)
proof
A1:(a+b)/2<=sqrt((a^2+b^2)/2) by Th6;
   (a+b)/2>=sqrt(a*b) by SERIES_3:2;
   hence thesis by A1,XXREAL_0:2;
end;

theorem
  2/((1/a)+(1/b))<=sqrt((a^2+b^2)/2)
proof
A1:(a+b)/2<=sqrt((a^2+b^2)/2) by Th6;
   2/((1/a)+(1/b))<=(a+b)/2 by Th7;
   hence thesis by A1,XXREAL_0:2;
end;

theorem
  abs x<1 & abs y<1 implies abs((x+y)/(1+x*y))<=1
proof
   assume
A1:abs(x)<1 & abs(y)<1;
   per cases;
   suppose B2:1+x*y<>0;
   x^2<1 & y^2<1 by A1,Lm13;then
   x^2-x^2<1-x^2 & y^2-y^2<1-y^2 by XREAL_1:16;then
   0<(1-x^2)*(1-y^2) by XREAL_1:131;then
   0+(x^2+y^2)<1-y^2-x^2+x^2*y^2+(x^2+y^2) by XREAL_1:10;then
A2:x^2+y^2+2*x*y<1+x^2*y^2+2*x*y by XREAL_1:10;
A3:(1+x*y)^2>0 by B2,SQUARE_1:74;then
   (x+y)^2/(1+x*y)^2<(x*y+1)^2/(1+x*y)^2 by A2,XREAL_1:76;then
   (x+y)^2/(1+x*y)^2<1 by A3,XCMPLX_1:60;then
   ((x+y)/(1+x*y))^2<1 by XCMPLX_1:77;
   hence thesis by Lm15;
  end;
  suppose 1+x*y=0;then
    abs((x+y)/(1+x*y))= abs(0) by XCMPLX_1:49
       .=0 by ABSVALUE:7;
  hence thesis;
  end;
end;

theorem
  abs(x+y)/(1+abs(x+y))<=abs(x)/(1+abs x)+abs(y)/(1+abs y)
proof
   per cases;
   suppose abs(x+y)=0;
   hence thesis;
   end;
   suppose
A3:abs(x+y)>0;
   abs(x+y)<=abs(x)+abs(y) by COMPLEX1:142;then
   1/(abs(x)+abs(y))<=1/abs(x+y) by A3,XREAL_1:120;then
   1/(abs(x)+abs(y))+1<=1/abs(x+y)+1 by XREAL_1:9;then
A5:1/(1/abs(x+y)+1)<=1/(1/(abs(x)+abs(y))+1) by XREAL_1:120;
A6:0<abs(x)+abs(y) by A3,COMPLEX1:142;
A7:1/(1/(abs(x)+abs(y))+1)
  =(1*(abs(x)+abs(y)))/((1/(abs(x)+abs(y))+1)*(abs(x)+abs(y)))
    by A6,XCMPLX_1:92
   .=(abs(x)+abs(y))/(1/(abs(x)+abs(y))*(abs(x)+abs(y))+(abs(x)+abs(y)))
 .=(abs(x)+abs(y))/(1+(abs(x)+abs(y))) by A6,XCMPLX_1:88
 .=abs(x)/(1+abs(x)+abs(y))+abs(y)/(1+abs(x)+abs(y)) by XCMPLX_1:63;
   1+abs(x)>=1 & 1+abs(y)>=1 by XREAL_1:33;then
   1+abs(x)+abs(y)>=1+abs(y) & 1+abs(y)+abs(x)>=1+abs(x) by XREAL_1:8;then
   abs(x)/(1+abs(x)+abs(y))<=abs(x)/(1+abs(x)) &
    abs(y)/(1+abs(x)+abs(y))<=abs(y)/(1+abs(y)) by XREAL_1:120;then
A12:abs(x)/(1+abs(x)+abs(y))+abs(y)/(1+abs(x)+abs(y))<=
    abs(x)/(1+abs(x))+abs(y)/(1+abs(y)) by XREAL_1:9;
    abs(x+y)/(1+abs(x+y))
   =(abs(x+y)/abs(x+y))/((1+abs(x+y))/abs(x+y)) by A3,XCMPLX_1:55
  .=1/((1+abs(x+y))/abs(x+y)) by A3,XCMPLX_1:60
  .=1/(1/abs(x+y)+abs(x+y)/abs(x+y)) by XCMPLX_1:63
  .=1/(1/abs(x+y)+1) by A3,XCMPLX_1:60;
   hence thesis by A5,A7,A12,XXREAL_0:2;
   end;
end;

theorem
  a/(a+b+d)+b/(a+b+c)+c/(b+c+d)+d/(a+c+d)>1
proof
   c+(a+b+d)>0+(a+b+d) by XREAL_1:10;then
A3:a/(a+b+c+d)<a/(a+b+d) by XREAL_1:78;
   d+(a+b+c)>0+(a+b+c) by XREAL_1:10;then
A5:b/(a+b+c+d)<b/(a+b+c) by XREAL_1:78;
   (b+c+d)+a>0+(b+c+d) by XREAL_1:10;then
A7:c/(b+c+d)>c/(a+b+c+d) by XREAL_1:78;
   b+(a+c+d)>0+(a+c+d) by XREAL_1:10;then
A9:d/(a+c+d)>d/(a+b+c+d) by XREAL_1:78;
   a/(a+b+c+d)+b/(a+b+c+d)<a/(a+b+d)+b/(a+b+c) by A3,A5,XREAL_1:10;then
   (a+b)/(a+b+c+d)<a/(a+b+d)+b/(a+b+c) by XCMPLX_1:63;then
   (a+b)/(a+b+c+d)+c/(a+b+c+d)<(a/(a+b+d)+b/(a+b+c))+c/(b+c+d)
     by A7,XREAL_1:10;then
   (a+b+c)/(a+b+c+d)<(a/(a+b+d)+b/(a+b+c))+c/(b+c+d) by XCMPLX_1:63;then
   (a+b+c)/(a+b+c+d)+d/(a+b+c+d)<(a/(a+b+d)+b/(a+b+c))+c/(b+c+d)+d/(a+c+d)
     by A9,XREAL_1:10;then
   (a+b+c+d)/(a+b+c+d)<(a/(a+b+d)+b/(a+b+c))+c/(b+c+d)+d/(a+c+d)
     by XCMPLX_1:63;
   hence thesis by XCMPLX_1:60;
end;

theorem
  a/(a+b+d)+b/(a+b+c)+c/(b+c+d)+d/(a+c+d)<2
proof
   a+b+d>a+b by XREAL_1:31;then
A3:a/(a+b)>a/(a+b+d) by XREAL_1:78;
   a+b+c>a+b by XREAL_1:31;then
A4:b/(a+b)>b/(a+b+c) by XREAL_1:78;
   b+(c+d)>c+d by XREAL_1:31;then
A6:c/(c+d)>c/(b+c+d) by XREAL_1:78;
   a+(c+d)>c+d by XREAL_1:31;then
A7:d/(c+d)>d/(a+c+d) by XREAL_1:78;
   a/(a+b)+b/(a+b)>a/(a+b+d)+b/(a+b+c) by A3,A4,XREAL_1:10;then
   (a+b)/(a+b)>a/(a+b+d)+b/(a+b+c) by XCMPLX_1:63;then
   1>a/(a+b+d)+b/(a+b+c) by XCMPLX_1:60;then
   1+c/(c+d)>a/(a+b+d)+b/(a+b+c)+c/(b+c+d) by A6,XREAL_1:10;then
   (1+c/(c+d))+d/(c+d)>(a/(a+b+d)+b/(a+b+c)+c/(b+c+d))+d/(a+c+d)
     by A7,XREAL_1:10;then
   1+(c/(c+d)+d/(c+d))>a/(a+b+d)+b/(a+b+c)+c/(b+c+d)+d/(a+c+d);then
   1+(c+d)/(c+d)>a/(a+b+d)+b/(a+b+c)+c/(b+c+d)+d/(a+c+d)
     by XCMPLX_1:63;then
   1+1>a/(a+b+d)+b/(a+b+c)+c/(b+c+d)+d/(a+c+d) by XCMPLX_1:60;
   hence thesis;
end;

theorem
  a+b>c & b+c>a & a+c>b implies 1/(a+b-c)+1/(b+c-a)+1/(c+a-b)>=9/(a+b+c)
proof
  assume a+b>c & b+c>a & a+c>b;then
A2:a+b-c>0 & b+c-a>0 & a+c-b>0 by XREAL_1:52;
   set d=a+b-c;
   set e=b+c-a;
   set f=c+a-b;
A3:1/d>0 & 1/e>0 & 1/f>0 by A2,XREAL_1:141;then
   (1/d)*(1/e)>0 by XREAL_1:131;then
   (1*1)/(d*e)>0 by XCMPLX_1:77;then
   ((1*1)/(d*e))*(1/f)>0 by A3,XREAL_1:131;then
A4:1/(d*e*f)>0 by XCMPLX_1:77;then
   3-root(1/(d*e*f))>=0 by POWER:8;then
A5:3*(3-root((1/(d*e*f))))>=0 by XREAL_1:129;
A6:d+e+f>=3*(3-root(d*e*f)) by A2,SERIES_3:15;
   d*e>0 by A2,XREAL_1:131;then
B1:d*e*f>0 by A2,XREAL_1:131;then
   3-root(d*e*f)>=0 by POWER:8;then
A7:3*(3-root(d*e*f))>=0 by XREAL_1:129;
   1/d+1/e+1/f>=3*(3-root((1/d)*(1/e)*(1/f))) by A3,SERIES_3:15;then
A8:1/d+1/e+1/f>=3*(3-root((1/(d*e*f)))) by Lm4;
   (d+e+f)*(1/d+1/e+1/f)>=(3*(3-root(d*e*f)))*(3*(3-root((1/(d*e*f)))))
     by A5,A6,A7,A8,XREAL_1:68;then
   (d+e+f)*(1/d+1/e+1/f)>=9*((3-root(d*e*f))*(3-root((1/(d*e*f))))); then
   (d+e+f)*(1/d+1/e+1/f)>=9*(3-root((d*e*f)*(1/(d*e*f))))
     by A4,B1,POWER:12;then
   (d+e+f)*(1/d+1/e+1/f)>=9*(3-root((d*e*f)/((d*e*f)/1))) by XCMPLX_1:80;
   then (d+e+f)*(1/d+1/e+1/f)>=9*(3-root(1)) by B1,XCMPLX_1:60;then
   (d+e+f)*(1/d+1/e+1/f)>=9*1 by POWER:7;
   hence thesis by XREAL_1:81;
end;

theorem
  sqrt((a+b)*(c+d))>=sqrt(a*c)+sqrt(b*d)
proof
  a*d+b*c>=2*sqrt((a*d)*(b*c)) by SIN_COS2:1;then
  (a*d+b*c)+(a*c+b*d)>=2*sqrt((a*d)*(b*c))+(a*c+b*d) by XREAL_1:9;then
  (a+b)*(c+d)>=2*sqrt((a*d)*(b*c))+a*c+b*d;then
  (a+b)*(c+d)>=2*sqrt((a*d)*(b*c))+(sqrt(a*c))^2+b*d by SQUARE_1:def 4;then
A2:(a+b)*(c+d)>=2*sqrt((a*d)*(b*c))+(sqrt(a*c))^2+(sqrt(b*d))^2
    by SQUARE_1:def 4;
   (sqrt((a+b)*(c+d)))^2>=2*sqrt((a*c)*(b*d))+(sqrt(a*c))^2+(sqrt(b*d))^2
     by A2,SQUARE_1:def 4;then
A5:(sqrt((a+b)*(c+d)))^2>=(sqrt(a*c))^2+2*(sqrt(a*c)*sqrt(b*d))+(sqrt(b*d))^2
    by SQUARE_1:97;
   (sqrt(a*c)+sqrt(b*d))^2>=0 by XREAL_1:65;then
A6:sqrt((sqrt((a+b)*(c+d)))^2)>=sqrt((sqrt(a*c)+sqrt(b*d))^2)
     by A5,SQUARE_1:94;
A7:sqrt((a+b)*(c+d))>0 by SQUARE_1:93;
   sqrt(a*c)>0 & sqrt(b*d)>0 by SQUARE_1:93;then
   sqrt(a*c)+sqrt(b*d)>0 by XREAL_1:36;then
   sqrt((sqrt((a+b)*(c+d)))^2)>=sqrt(a*c)+sqrt(b*d) by A6,SQUARE_1:89;
   hence thesis by A7,SQUARE_1:89;
end;

theorem
  (a*b+c*d)*(a*c+b*d)>=4*a*b*c*d
proof
A2:(a*b+c*d)*(a*c+b*d)
    =((sqrt(a*b))^2+c*d)*(a*c+b*d) by SQUARE_1:def 4
   .=((sqrt(a*b))^2+(sqrt(c*d))^2)*(a*c+b*d) by SQUARE_1:def 4
   .=((sqrt(a*b))^2+(sqrt(c*d))^2)*((sqrt(a*c))^2+b*d) by SQUARE_1:def 4
   .=((sqrt(a*b))^2+(sqrt(c*d))^2)*((sqrt(a*c))^2+(sqrt(b*d))^2)
     by SQUARE_1:def 4;
A3:(a*b+c*d)*(a*c+b*d)>=b*c*(a+d)^2 by A2,Lm6;
A4:a+d>=2*sqrt(a*d) by SIN_COS2:1;
   sqrt(a*d)>0 by SQUARE_1:93;then
   2*sqrt(a*d)>0 by XREAL_1:131;then
   (a+d)^2>=(2*sqrt(a*d))^2 by A4,SQUARE_1:77;then
   (a+d)^2>=2^2*(sqrt(a*d))^2;then
   (a+d)^2>=2*2*(a*d) by SQUARE_1:def 4;then
   (a+d)^2*(b*c)>=4*a*d*(b*c) by XREAL_1:66;
   hence thesis by A3,XXREAL_0:2;
end;

theorem Th12:
  a/b+b/c+c/a>=3
proof
  a/b+b/c+c/a>=3*(3-root((a/b)*(b/c)*(c/a))) by SERIES_3:15;then
  a/b+b/c+c/a>=3*(3-root(((a*b)/(b*c))*(c/a))) by XCMPLX_1:77;then
  a/b+b/c+c/a>=3*(3-root((a/c)*(c/a))) by XCMPLX_1:92;then
  a/b+b/c+c/a>=3*(3-root(((a*c)/(c*a)))) by XCMPLX_1:77;then
  a/b+b/c+c/a>=3*(3-root(1)) by XCMPLX_1:60;then
  a/b+b/c+c/a>=3*1 by POWER:7;
  hence thesis;
end;

theorem
  a*b+b*c+c*a=1 implies a+b+c>=sqrt 3
proof
   assume
A1:a*b+b*c+c*a=1;
A3:a^2+b^2+c^2>=1 by A1,SERIES_3:10;
   a^2+b^2+c^2+2>=1+2 by A3,XREAL_1:8;then
   sqrt((a+b+c)^2)>=sqrt(3) by A1,SQUARE_1:94;
   hence thesis by SQUARE_1:89;
end;

theorem
  (b+c-a)/a+(c+a-b)/b+(a+b-c)/c>=3
proof
A1:(b+c-a)/a+(c+a-b)/b+(a+b-c)/c
  =(b+c)/a-a/a+(c+a-b)/b+(a+b-c)/c by XCMPLX_1:121
 .=(b+c)/a-1+(c+a-b)/b+(a+b-c)/c by XCMPLX_1:60
 .=(b+c)/a-1+((c+a)/b-b/b)+(a+b-c)/c by XCMPLX_1:121
 .=(b+c)/a-1+((c+a)/b-1)+(a+b-c)/c by XCMPLX_1:60
 .=(b+c)/a-1+(c+a)/b-1+((a+b)/c-c/c) by XCMPLX_1:121
 .=(b+c)/a-1+(c+a)/b-1+((a+b)/c-1) by XCMPLX_1:60
 .=(b+c)/a+(c+a)/b+(a+b)/c-3
 .=b/a+c/a+(c+a)/b+(a+b)/c-3 by XCMPLX_1:63
 .=b/a+c/a+(c/b+a/b)+(a+b)/c-3 by XCMPLX_1:63
 .=b/a+c/a+(c/b+a/b)+(a/c+b/c)-3 by XCMPLX_1:63
 .=a/b+b/c+c/a+b/a+c/b+a/c-3;
A2:a/b+b/c+c/a>=3 by Th12;
   b/a+c/b+a/c>=3 by Th12;then
   (a/b+b/c+c/a)+(b/a+c/b+a/c)>=3+3 by A2,XREAL_1:9;then
   (a/b+b/c+c/a+b/a+c/b+a/c)-3>=6-3 by XREAL_1:11;
   hence thesis by A1;
end;

theorem
  (a+1/a)*(b+1/b)>=(sqrt(a*b)+1/sqrt(a*b))^2
proof
A2:sqrt(a*b)>0 by SQUARE_1:93;
A4:(a+1/a)*(b+1/b)=a*b+a*(1/b)+(1/a)*b+(1/a)*(1/b)
  .=a*b+(a*1)/b+(1/a)*b+(1/a)*(1/b) by XCMPLX_1:75
  .=a*b+(a*1)/b+(b*1)/a+(1/a)*(1/b) by XCMPLX_1:75
  .=a*b+(a*1)/b+(b*1)/a+(1*1)/(a*b) by XCMPLX_1:77
  .=a*b+a/b+b/a+1/(a*b);
A5:(sqrt(a*b)+1/sqrt(a*b))^2
 =(sqrt(a*b))^2+2*sqrt(a*b)*(1/sqrt(a*b))+(1/sqrt(a*b))^2
.=a*b+2*sqrt(a*b)*(1/sqrt(a*b))+(1/sqrt(a*b))^2 by SQUARE_1:def 4
.=a*b+2*sqrt(a*b)*(1/sqrt(a*b))+1^2/(sqrt(a*b))^2 by XCMPLX_1:77
.=a*b+2*(sqrt(a*b)*(1/sqrt(a*b)))+1/(a*b) by SQUARE_1:def 4
.=a*b+2*((sqrt(a*b)*1)/sqrt(a*b))+1/(a*b) by XCMPLX_1:75
.=a*b+2*(1)+1/(a*b) by A2,XCMPLX_1:60
.=a*b+2+1/(a*b);
  a/b+b/a>=2*sqrt((a/b)*(b/a)) by SIN_COS2:1;then
  a/b+b/a>=2*sqrt((a*b)/(b*a)) by XCMPLX_1:77;then
  a/b+b/a>=2*1 by XCMPLX_1:60,SQUARE_1:83;then
  (a/b+b/a)+(a*b+1/(a*b))>=2+(a*b+1/(a*b)) by XREAL_1:8;
  hence thesis by A4,A5;
end;

theorem
  (b*c)/a+(a*c)/b+(a*b)/c>=a+b+c
proof
A2:(b*c)/a+(a*c)/b = c*(b/a)+(a*c)/b by XCMPLX_1:75
  .=c*(b/a)+c*(a/b) by XCMPLX_1:75
  .=c*(b/a+a/b);
A3:(a*c)/b+(a*b)/c = a*(c/b)+(a*b)/c by XCMPLX_1:75
  .=a*(c/b)+a*(b/c) by XCMPLX_1:75
  .=a*(c/b+b/c);
A4: (a*b)/c+(b*c)/a =b*(a/c)+(b*c)/a by XCMPLX_1:75
      .=b*(a/c)+b*(c/a) by XCMPLX_1:75
      .=b*(a/c+c/a);
    b/a+a/b>=2 by SERIES_3:3;then
A5: c*(b/a+a/b)>=c*2 by XREAL_1:66;
    c/b+b/c>=2 by SERIES_3:3;then
A6: a*(c/b+b/c)>=a*2 by XREAL_1:66;
    a/c+c/a>=2 by SERIES_3:3;then
    b*(a/c+c/a)>=b*2 by XREAL_1:66;then
    a*(c/b+b/c)+b*(a/c+c/a)>=a*2+b*2 by A6,XREAL_1:9;then
A7: a*(c/b+b/c)+b*(a/c+c/a)+c*(b/a+a/b)>=a*2+b*2+c*2 by A5,XREAL_1:9;
    (2*((b*c)/a+(a*c)/b+(a*b)/c))/2>=(2*(a+b+c))/2 by A2,A3,A4,A7,XREAL_1:74;
    hence thesis;
end;

theorem
  x>y & y>z implies x^2*y+y^2*z+z^2*x>x*y^2+y*z^2+z*x^2
proof
   assume
A1:x>y & y>z;
A3:x-y>0 & y-z>0 by A1,XREAL_1:52;
   x>z by A1,XXREAL_0:2;then
A4:x-z>0 by XREAL_1:52;
   (x-y)*(y-z)>0 by A3,XREAL_1:131;then
   (x-y)*(y-z)*(x-z)>0 by A4,XREAL_1:131;then
   (x^2*y+y^2*z+z^2*x)-(x*y^2+y*z^2+z*x^2)>0;
   hence thesis by XREAL_1:49;
end;

theorem
  a>b & b>c implies b/(a-b)>c/(a-c)
proof
   assume
A1:a>b & b>c;then
A2:a-b>0 & b-c>0 by XREAL_1:52;
A4:b/(a-b)>c/(a-b) by A1,A2,XREAL_1:76;
   b*(-1)<c*(-1) by A1,XREAL_1:71;then
   -b+a<-c+a by XREAL_1:10;then
   c/(a-b)>c/(a-c) by A2,XREAL_1:78;
   hence thesis by A4,XXREAL_0:2;
end;

theorem
  b>a & c>d implies c/(c+a)>d/(d+b)
proof
  assume b>a & c>d;then
  b*c>a*d by XREAL_1:98;then
  b*c+c*d>a*d+c*d by XREAL_1:10;then
  c*(b+d)>d*(a+c);
  hence thesis by XREAL_1:108;
end;

theorem
  m*x+z*y<=sqrt(m^2+z^2)*sqrt(x^2+y^2)
proof
   m^2>=0 & z^2>=0 & x^2>=0 & y^2>=0 by XREAL_1:65;then
A3:m^2+z^2>=0 & x^2+y^2>=0 by XREAL_1:35;
A4:(sqrt(m^2+z^2)*sqrt(x^2+y^2))^2
    =(sqrt(m^2+z^2))^2*(sqrt(x^2+y^2))^2
   .=(m^2+z^2)*(sqrt(x^2+y^2))^2 by A3,SQUARE_1:def 4
   .=(m^2+z^2)*(x^2+y^2) by A3,SQUARE_1:def 4
   .=m^2*x^2+(m*y)^2+(z*x)^2+z^2*y^2;
   (m*y)^2+(z*x)^2>=2*(m*y)*(z*x) by SERIES_3:6;then
A5:(m*y)^2+(z*x)^2+(m^2*x^2+z^2*y^2)>=2*m*x*y*z+(m^2*x^2+z^2*y^2)
     by XREAL_1:8;
   (m*x+z*y)^2>=0 by XREAL_1:65;then
B3:sqrt((sqrt(m^2+z^2)*sqrt(x^2+y^2))^2)>=sqrt((m*x+z*y)^2)
     by A4,A5,SQUARE_1:94;
B1:sqrt(m^2+z^2)>=0 & sqrt(x^2+y^2)>=0 by A3,SQUARE_1:def 4;then
   sqrt(m^2+z^2)*sqrt(x^2+y^2)>=0 by XREAL_1:129;then
A7:sqrt(m^2+z^2)*sqrt(x^2+y^2)>=sqrt((m*x+z*y)^2) by B3,SQUARE_1:89;
   per cases;
   suppose m*x+z*y>0;
   hence thesis by A7,SQUARE_1:89;
   end;
   suppose m*x+z*y=0;
   hence thesis by B1,XREAL_1:129;
   end;
   suppose m*x+z*y<0;
   hence thesis by B1,XREAL_1:129;
   end;
end;

theorem Th15:
  (m*x+u*y+w*z)^2<=(m^2+u^2+w^2)*(x^2+y^2+z^2)
proof
A4:(m*y)^2+(u*x)^2>=2*(m*y)*(u*x) by SERIES_3:6;
A5: m^2*z^2+w^2*x^2=(m*z)^2+(w*x)^2;
A6:m^2*z^2+w^2*x^2>=2*(m*z)*(w*x) by A5,SERIES_3:6;
A7:  u^2*z^2+w^2*y^2=(u*z)^2+(w*y)^2;
A8:  u^2*z^2+w^2*y^2>=2*(u*z)*(w*y) by A7,SERIES_3:6;
     m^2*y^2+u^2*x^2+(m^2*z^2+w^2*x^2)>=2*m*y*u*x+2*m*z*w*x
       by A4,A6,XREAL_1:9;then
     m^2*y^2+u^2*x^2+m^2*z^2+w^2*x^2+(u^2*z^2+w^2*y^2)>=2*m*y*u*x+2*m*z*w*x+
       2*u*z*w*y by A8,XREAL_1:9;then
    m^2*y^2+u^2*x^2+m^2*z^2+w^2*x^2+(u^2*z^2)+(w^2*y^2)+((m*x)^2+(u*y)^2+
      (w*z)^2)>=2*m*y*u*x+2*m*z*w*x+2*u*z*w*y+((m*x)^2+(u*y)^2+(w*z)^2)
      by XREAL_1:8;
    hence thesis;
end;

theorem
  (9*a*b*c)/(a^2+b^2+c^2)<=a+b+c
proof
A1: (a^2+b^2+c^2)*(a+b+c)
  =a*a^2+a*b^2+a*c^2+b*a^2+b*b^2+b*c^2+(c*a^2)+(c*b^2)+(c*c^2)
  .=a*a|^2+a*b^2+a*c^2+b*a^2+b*b^2+b*c^2+(c*a^2)+(c*b^2)+(c*c^2) by NEWTON:100
  .=a|^(2+1)+a*b^2+a*c^2+b*a^2+b*b^2+b*c^2+(c*a^2)+(c*b^2)+(c*c^2)
    by NEWTON:11
  .=a|^3+a*b^2+a*c^2+b*a^2+b*b|^2+b*c^2+(c*a^2)+(c*b^2)+(c*c^2) by NEWTON:100
  .=a|^3+a*b^2+a*c^2+b*a^2+b|^(2+1)+b*c^2+(c*a^2)+(c*b^2)+(c*c^2)
    by NEWTON:11
  .=a|^3+a*b^2+a*c^2+b*a^2+b|^3+b*c^2+(c*a^2)+(c*b^2)+(c*c|^2) by NEWTON:100
  .=a|^3+a*b^2+a*c^2+b*a^2+b|^3+b*c^2+(c*a^2)+(c*b^2)+(c|^(2+1)) by NEWTON:11
  .=a|^3+a*b^2+a*c^2+b*a^2+b|^3+b*c^2+(c*a^2)+(c*b^2)+(c|^3)
  .=a|^3+b|^3+c|^3+a*b^2+a*c^2+b*a^2+b*c^2+(c*a^2)+(c*b^2);
A3:b+c>=2*sqrt(b*c) & a+c>=2*sqrt(a*c) & a+b>=2*sqrt(a*b) by SIN_COS2:1;
A5:a^2*(b+c)>=a^2*(2*sqrt(b*c)) & b^2*(a+c)>=b^2*(2*sqrt(a*c)) &
   c^2*(a+b)>=c^2*(2*sqrt(a*b)) by A3,XREAL_1:66;then
   a^2*(b+c)+b^2*(a+c)>=a^2*(2*sqrt(b*c))+b^2*(2*sqrt(a*c))
     by XREAL_1:9;then
A6:a^2*(b+c)+b^2*(a+c)+c^2*(a+b)>=2*a^2*sqrt(b*c)+2*b^2*sqrt(a*c)+
     2*c^2*sqrt(a*b) by A5,XREAL_1:9;
   sqrt(b*c)>0 & sqrt(a*c)>0 & sqrt(a*b)>0 by SQUARE_1:93;then
   a^2*sqrt(b*c)>0 & b^2*sqrt(a*c)>0 & c^2*sqrt(a*b)>0
     by XREAL_1:131;then
A8:2*(a^2*sqrt(b*c))>0 & 2*(b^2*sqrt(a*c))>0 & 2*(c^2*sqrt(a*b))>0
     by XREAL_1:131;
   2*a^2*sqrt(b*c)+2*b^2*sqrt(a*c)+2*c^2*sqrt(a*b)>=
     3*(3-root((2*a^2*sqrt(b*c))
     *(2*b^2*sqrt(a*c))*(2*c^2*sqrt(a*b)))) by A8,SERIES_3:15;then
  2*a^2*sqrt(b*c)+2*b^2*sqrt(a*c)+2*c^2*sqrt(a*b)>=
     3*(3-root(2*a^2*sqrt(b*c)
     *2*b^2*sqrt(a*c)*2*c^2*sqrt(a*b)));then
  2*a^2*sqrt(b*c)+2*b^2*sqrt(a*c)+2*c^2*sqrt(a*b)>=
     3*(3-root((2*a*b*c)|^3)) by Lm21;then
   2*a^2*sqrt(b*c)+2*b^2*sqrt(a*c)+2*c^2*sqrt(a*b)>=
     3*(2*a*b*c) by POWER:5;then
A9:a*b^2+a*c^2+b*a^2+b*c^2+(c*a^2)+(c*b^2)>=6*a*b*c by A6,XXREAL_0:2;
  a|^3+b|^3+c|^3>=3*a*b*c by SERIES_3:12;then
A10:(a|^3+b|^3+c|^3)+(a*b^2+a*c^2+b*a^2+b*c^2+(c*a^2)+(c*b^2))>=3*a*b*c
    +6*a*b*c by A9,XREAL_1:9;
  a^2+b^2>0 by XREAL_1:36;then
A11:a^2+b^2+c^2>0 by XREAL_1:36;then
  ((a+b+c)*(a^2+b^2+c^2))/(a^2+b^2+c^2)>=(9*a*b*c)/(a^2+b^2+c^2)
    by A10,A1,XREAL_1:74;then
  (a+b+c)/((a^2+b^2+c^2)/(a^2+b^2+c^2))>=(9*a*b*c)/(a^2+b^2+c^2)
    by XCMPLX_1:78;then
  (a+b+c)/1>=(9*a*b*c)/(a^2+b^2+c^2) by A11,XCMPLX_1:60;
  hence thesis;
end;

theorem
  a+b+c<=sqrt((a^2+a*b+b^2)/3)+sqrt((b^2+b*c+c^2)/3)+sqrt((c^2+c*a+a^2)/3)
proof
B1:sqrt(a^2+a*b+b^2)>=(1/2)*sqrt(3)*(a+b) by Lm23;
   sqrt(b^2+b*c+c^2)>=(1/2)*sqrt(3)*(b+c) by Lm23;then
B2:sqrt(a^2+a*b+b^2)+sqrt(b^2+b*c+c^2)>=(1/2)*sqrt(3)*(a+b)+(1/2)*sqrt(3)*(b+c)
     by B1,XREAL_1:9;
B3:sqrt(3)>0 by SQUARE_1:93;
   sqrt(c^2+c*a+a^2)>=(1/2)*sqrt(3)*(c+a) by Lm23;then
   sqrt(a^2+a*b+b^2)+sqrt(b^2+b*c+c^2)+sqrt(c^2+c*a+a^2)>=(1/2)*sqrt(3)*(a+b)+
     (1/2)*sqrt(3)*(b+c)+(1/2)*sqrt(3)*(c+a) by B2,XREAL_1:9;then
   (sqrt(a^2+a*b+b^2)+sqrt(b^2+b*c+c^2)+sqrt(c^2+c*a+a^2))/sqrt(3)>=((a+b+c)*
     sqrt(3))/sqrt(3) by B3,XREAL_1:74;then
   (sqrt(a^2+a*b+b^2)+sqrt(b^2+b*c+c^2)+sqrt(c^2+c*a+a^2))/sqrt(3)>=(a+b+c)*
     (sqrt(3)/sqrt(3)) by XCMPLX_1:75;then
   (sqrt(a^2+a*b+b^2)+sqrt(b^2+b*c+c^2)+sqrt(c^2+c*a+a^2))/sqrt(3)>=(a+b+c)*1
     by B3,XCMPLX_1:60;then
   (sqrt(a^2+a*b+b^2)+sqrt(b^2+b*c+c^2))/sqrt(3)+sqrt(c^2+c*a+a^2)/sqrt(3)
     >=a+b+c by XCMPLX_1:63;then
B4:sqrt(a^2+a*b+b^2)/sqrt(3)+sqrt(b^2+b*c+c^2)/sqrt(3)+sqrt(c^2+c*a+a^2)/
     sqrt(3)>=a+b+c by XCMPLX_1:63;
   a^2+a*b>0 & b^2+b*c>0 & c^2+c*a>0 by XREAL_1:36;then
A3:a^2+a*b+b^2>0 & b^2+b*c+c^2>0 & c^2+c*a+a^2>0 by XREAL_1:36;then
   sqrt((a^2+a*b+b^2)/3)+sqrt(b^2+b*c+c^2)/sqrt(3)+sqrt(c^2+c*a+a^2)/
     sqrt(3)>=a+b+c by B4,SQUARE_1:99;then
   sqrt((a^2+a*b+b^2)/3)+sqrt((b^2+b*c+c^2)/3)+sqrt(c^2+c*a+a^2)/
     sqrt(3)>=a+b+c by A3,SQUARE_1:99;
   hence thesis by A3,SQUARE_1:99;
end;

theorem
  sqrt((a^2+a*b+b^2)/3)+sqrt((b^2+b*c+c^2)/3)+sqrt((c^2+c*a+a^2)/3)
  <=sqrt((a^2+b^2)/2)+sqrt((b^2+c^2)/2)+sqrt((c^2+a^2)/2)
proof
A1:sqrt((a^2+a*b+b^2)/3)<=sqrt((a^2+b^2)/2) by Lm24;
   sqrt((b^2+b*c+c^2)/3)<=sqrt((b^2+c^2)/2) by Lm24;then
A2:sqrt((a^2+a*b+b^2)/3)+sqrt((b^2+b*c+c^2)/3)<=sqrt((a^2+b^2)/2)+sqrt
     ((b^2+c^2)/2) by A1,XREAL_1:9;
   sqrt((c^2+c*a+a^2)/3)<=sqrt((c^2+a^2)/2) by Lm24;
   hence thesis by A2,XREAL_1:9;
end;

theorem
  sqrt((a^2+b^2)/2)+sqrt((b^2+c^2)/2)+sqrt((c^2+a^2)/2)
  <=sqrt(3*(a^2+b^2+c^2))
proof
   a^2+b^2>0 & b^2+c^2>0 & c^2+a^2>0 by XREAL_1:36;then
B1:(a^2+b^2)/2>0 & (b^2+c^2)/2>0 & (c^2+a^2)/2>0 by XREAL_1:141;then
A3:(sqrt((a^2+b^2)/2))^2=(a^2+b^2)/2 & (sqrt((b^2+c^2)/2))^2=(b^2+c^2)/2
     & (sqrt((c^2+a^2)/2))^2=(c^2+a^2)/2 by SQUARE_1:def 4;
A2:(1*sqrt((a^2+b^2)/2)+1*sqrt((b^2+c^2)/2)+1*sqrt((c^2+a^2)/2))^2
     <=(1^2+1^2+1^2)*((sqrt((a^2+b^2)/2))^2+(sqrt((b^2+c^2)/2))^2+
     (sqrt((c^2+a^2)/2))^2) by Th15;
A5:sqrt((a^2+b^2)/2)>0 & sqrt((b^2+c^2)/2)>0 & sqrt((c^2+a^2)/2)>0
     by B1,SQUARE_1:93;then
   sqrt((a^2+b^2)/2)+sqrt((b^2+c^2)/2)>0 by XREAL_1:36;then
A6:sqrt((a^2+b^2)/2)+sqrt((b^2+c^2)/2)+sqrt((c^2+a^2)/2)>0
     by A5,XREAL_1:36;
   (sqrt((a^2+b^2)/2)+sqrt((b^2+c^2)/2)+sqrt((c^2+a^2)/2))^2>=0
     by XREAL_1:65;then
   sqrt((sqrt((a^2+b^2)/2)+sqrt((b^2+c^2)/2)+sqrt((c^2+a^2)/2))^2)<=
     sqrt(3*(a^2+b^2+c^2)) by A2,A3,SQUARE_1:94;
   hence thesis by A6,SQUARE_1:89;
end;

theorem
  sqrt(3*(a^2+b^2+c^2))<=(b*c)/a+(c*a)/b+(a*b)/c
proof
A1:((b*c)/a)^2+((c*a)/b)^2>=2*c^2 by Lm26;
A2:((b*c)/a)^2+((a*b)/c)^2>=2*b^2 by Lm26;
   ((c*a)/b)^2+((a*b)/c)^2>=2*a^2 by Lm26;then
   (((c*a)/b)^2+((a*b)/c)^2)+(((b*c)/a)^2+((a*b)/c)^2)>=2*a^2+2*b^2
     by A2,XREAL_1:9;then
   ((c*a)/b)^2+((a*b)/c)^2+((b*c)/a)^2+((a*b)/c)^2+(((b*c)/a)^2+((c*a)/b)^2)>=
     2*a^2+2*b^2+2*c^2 by A1,XREAL_1:9;then
   ((((c*a)/b)^2+((a*b)/c)^2+((b*c)/a)^2)*2)/2>=((a^2+b^2+c^2)*2)/2
     by XREAL_1:74;then
   ((c*a)/b)^2+((a*b)/c)^2+((b*c)/a)^2+2*(a^2+b^2+c^2)>=(a^2+b^2+c^2)+
     2*(a^2+b^2+c^2) by XREAL_1:8;then
A3:((b*c)/a+(c*a)/b+(a*b)/c)^2>=3*(a^2+b^2+c^2) by Lm29;
   a^2+b^2>0 by XREAL_1:36;then
   a^2+b^2+c^2>0 by XREAL_1:36;then
A5:3*(a^2+b^2+c^2)>0 by XREAL_1:131;
   sqrt(((b*c)/a+(c*a)/b+(a*b)/c)^2)>=sqrt(3*(a^2+b^2+c^2))
     by A3,A5,SQUARE_1:94;
   hence thesis by SQUARE_1:89;
end;

theorem
  a+b=1 implies (1/a^2-1)*(1/b^2-1)>=9
proof
  assume
A1:a+b=1;
A3:(1/a^2-1)*(1/b^2-1)
  =(1-1*a^2)/a^2*(1/b^2-1) by XCMPLX_1:127
 .=(1-1*a^2)/a^2*((1-1*b^2)/b^2) by XCMPLX_1:127
 .=((1-a)*(1+a)*((1-b)*(1+b)))/(a^2*b^2) by XCMPLX_1:77;
A6:(1/a^2-1)*(1/b^2-1)
  =((a*b)*((1+a)*(1+b)))/((a*b)*(a*b)) by A3,A1
 .=((a*b)/(a*b))*(((1+a)*(1+b))/(a*b)) by XCMPLX_1:77
 .=1*(((1+a)*(1+b))/(a*b)) by XCMPLX_1:60
 .=(1+(a+b)+a*b)/(a*b)
 .=2/(a*b)+(a*b)/(a*b) by A1,XCMPLX_1:63
 .=2/(a*b)+1 by XCMPLX_1:60;
A7:1>=2*sqrt(a*b) by A1,SIN_COS2:1;
   sqrt(a*b)>0 by SQUARE_1:93;then
   2*sqrt(a*b)>0 by XREAL_1:131;then
   1>=(2*sqrt(a*b))^2 by A7,SQUARE_1:59,77;then
   1>=2*2*(sqrt(a*b))^2;then
   1>=4*(a*b) by SQUARE_1:def 4;then
   8/1<=8/(4*a*b) by XREAL_1:120;then
   8<=8/(4*(a*b));then
   8<=8/4/(a*b) by XCMPLX_1:79;then
   8+1<=2/(a*b)+1 by XREAL_1:9;
   hence thesis by A6;
end;

theorem
  a+b=1 implies a*b+1/(a*b)>=17/4
proof
   assume a+b=1; then
A4:1>=2*sqrt(a*b) by SIN_COS2:1;
   sqrt(a*b)>0 by SQUARE_1:93;then
   2*sqrt(a*b)>0 by XREAL_1:131;then
   1>=(2*sqrt(a*b))^2 by A4,SQUARE_1:77,SQUARE_1:59;then
   1>=2*2*(sqrt(a*b))^2;then
   1>=4*(a*b) by SQUARE_1:def 4;then
A5:1/4>=((a*b)*4)/4 by XREAL_1:74;
A6:(1-a*b)^2>=0 by XREAL_1:65;
A7: 1/(a*b)+a*b
   =(1+(a*b)*(a*b))/(a*b) by XCMPLX_1:114
  .=((1^2-2*1*(a*b)+(a*b)^2)+2*(a*b))/(a*b)
  .=(1-a*b)^2/(a*b)+(2*(a*b))/(a*b) by XCMPLX_1:63
  .=(1-a*b)^2/(a*b)+2*((a*b)/(a*b)) by XCMPLX_1:75
  .=(1-a*b)^2/(a*b)+2*1 by XCMPLX_1:60
  .=(1-a*b)^2/(a*b)+2;
  (1-a*b)^2/(1/4)<=(1-a*b)^2/(a*b) by A5,A6,XREAL_1:120;then
  ((1-a*b)^2*4)/1<=(1-a*b)^2/(a*b) by XCMPLX_1:78;then
A8:(1-a*b)^2*4+2<=(1-a*b)^2/(a*b)+2 by XREAL_1:9;
  1/4*(-1)<=a*b*(-1) by A5,XREAL_1:67;then
  -(1/4)+1<=-(a*b)+1 by XREAL_1:9;then
  (3/4)^2<=(1-(a*b))^2 by SQUARE_1:77;then
  (9/4^2)/(1/4)<=((1-(a*b))^2)/(1/4) by XREAL_1:74;then
  (9/4^2)/(1/4)<=(((1-(a*b))^2)*4)/1 by XCMPLX_1:78;then
  9/4+8/4<=((1-(a*b))^2)*4+2 by XREAL_1:9;
  hence thesis by A7,A8,XXREAL_0:2;
end;

theorem
  a+b+c = 1 implies 1/a+1/b+1/c>=9
proof
   assume
B1:a+b+c = 1;
A1:a+b+c>=3*(3-root(a*b*c)) by SERIES_3:15;
   3-root(a*b*c)>=0 by POWER:8;then
A2:3*(3-root(a*b*c))>=0 by XREAL_1:129;
   1/a+1/b+1/c>=3*(3-root((1/a)*(1/b)*(1/c))) by SERIES_3:15;then
A4:1/a+1/b+1/c>=3*(3-root((1/(a*b*c)))) by Lm4;
   3-root((1/(a*b*c)))>=0 by POWER:8;then
   3*(3-root((1/(a*b*c))))>=0 by XREAL_1:129;then
   (a+b+c)*(1/a+1/b+1/c)>=(3*(3-root(a*b*c)))*(3*(3-root((1/(a*b*c)))))
     by A1,A2,A4,XREAL_1:68;then
   (1/a+1/b+1/c)>=9*((3-root(a*b*c))*(3-root((1/(a*b*c))))) by B1;then
   (1/a+1/b+1/c)>=9*(3-root((a*b*c)*(1/(a*b*c))))
     by POWER:12;then
   (1/a+1/b+1/c)>=9*(3-root((a*b*c)/((a*b*c)/1))) by XCMPLX_1:80;then
   (1/a+1/b+1/c)>=9*(3-root(1)) by XCMPLX_1:60;then
   (1/a+1/b+1/c)>=9*1 by POWER:7;
   hence thesis;
end;

theorem
  a+b+c = 1 implies (1/a-1)*(1/b-1)*(1/c-1)>=8
proof
   assume
A1:a+b+c =1;then
A2: (1/a-1)*(1/b-1)*(1/c-1)
   =((b+c)/a)*(1/b-1)*(1/c-1) by Lm30
  .=((b+c)/a)*((a+c)/b)*(1/c-1) by A1,Lm30
  .=((b+c)/a)*((a+c)/b)*((a+b)/c+c/c-1) by A1,XCMPLX_1:63
  .=((b+c)/a)*((a+c)/b)*((a+b)/c+1-1) by XCMPLX_1:60
  .=((b+c)/a)*((a+c)/b)*((a+b)/c);
  b+c>=2*sqrt(b*c) & a+c>=2*sqrt(a*c) & a+b>=2*sqrt(a*b) by SIN_COS2:1;then
A3:(b+c)/a>=(2*sqrt(b*c))/a & (a+c)/b>=(2*sqrt(a*c))/b &
    (a+b)/c>=(2*sqrt(a*b))/c by XREAL_1:74;
   sqrt(b*c)>0 & sqrt(a*c)>0 & sqrt(a*b)>0 by SQUARE_1:93;then
   2*sqrt(b*c)>0 & 2*sqrt(a*c)>0 & 2*sqrt(a*b)>0 by XREAL_1:131;then
A4:(2*sqrt(b*c))/a>0 & (2*sqrt(a*c))/b>0 & (2*sqrt(a*b))/c>0
     by XREAL_1:141;then
A5:((b+c)/a)*((a+c)/b)>=((2*sqrt(b*c))/a)*((2*sqrt(a*c))/b)
     by A3,XREAL_1:68;
   ((2*sqrt(b*c))/a)*((2*sqrt(a*c))/b)>0 by A4,XREAL_1:131;then
  (1/a-1)*(1/b-1)*(1/c-1)>=((2*sqrt(b*c))/a)*((2*sqrt(a*c))/b)*
    ((2*sqrt(a*b))/c) by A2,A5,A3,A4,XREAL_1:68;
  hence thesis by Lm31;
end;

theorem
  a+b+c = 1 implies (1+1/a)*(1+1/b)*(1+1/c)>=64
proof
   assume
A1:a+b+c = 1;then
A2:(1+1/a)*(1+1/b)*(1+1/c)
    =(2+(b+c)/a)*(1+1/b)*(1+1/c) by Lm32
   .=(2+(b+c)/a)*(2+(a+c)/b)*(1+1/c) by A1,Lm32
   .=(2+(b+c)/a)*(2+(a+c)/b)*(1+((a+b)/c+c/c)) by A1,XCMPLX_1:63
   .=(2+(b+c)/a)*(2+(a+c)/b)*(1+(a+b)/c+1) by XCMPLX_1:60
   .=(2+(b+c)/a)*(2+(a+c)/b)*(2+(a+b)/c);
   b+c>=2*sqrt(b*c) & a+c>=2*sqrt(a*c) & a+b>=2*sqrt(a*b) by SIN_COS2:1;then
   (b+c)/a>=(2*sqrt(b*c))/a & (a+c)/b>=(2*sqrt(a*c))/b &
     (a+b)/c>=(2*sqrt(a*b))/c by XREAL_1:74;then
   2+(b+c)/a>=2+(2*sqrt(b*c))/a & 2+(a+c)/b>=2+(2*sqrt(a*c))/b &
     2+(a+b)/c>=2+(2*sqrt(a*b))/c by XREAL_1:8;then
A3:2+(b+c)/a>=2+2*(sqrt(b*c)/a) & 2+(a+c)/b>=2+2*(sqrt(a*c)/b) &
     2+(a+b)/c>=2+2*(sqrt(a*b)/c) by XCMPLX_1:75;
   sqrt(b*c)>0 & sqrt(a*c)>0 & sqrt(a*b)>0 by SQUARE_1:93;then
   sqrt(b*c)/a>0 & sqrt(a*c)/b>0 & sqrt(a*b)/c>0 by XREAL_1:141;then
   2*(sqrt(b*c)/a)>0 & 2*(sqrt(a*c)/b)>0 & 2*(sqrt(a*b)/c)>0
     by XREAL_1:131;then
A4:2+2*(sqrt(b*c)/a)>0 & 2+2*(sqrt(a*c)/b)>0 & 2+2*(sqrt(a*b)/c)>0
      by XREAL_1:36;then
A5:(2+(b+c)/a)*(2+(a+c)/b)>=(2*(1+sqrt(b*c)/a))*(2*(1+sqrt(a*c)/b))
     by A3,XREAL_1:68;
   (2*(1+sqrt(b*c)/a))*(2*(1+sqrt(a*c)/b))>0 by A4,XREAL_1:131;then
   (2+(b+c)/a)*(2+(a+c)/b)*(2+(a+b)/c)>=(2*(1+sqrt(b*c)/a))*(2*(1+sqrt(a*c)/b))
     *(2*(1+sqrt(a*b)/c)) by A3,A4,A5,XREAL_1:68;then
   (1+1/a)*(1+1/b)*(1+1/c)>=8*((1+sqrt(b*c)/a)*(1+sqrt(a*c)/b)
     *(1+sqrt(a*b)/c)) by A2;then
A7:(1+1/a)*(1+1/b)*(1+1/c)>=8*
   (2+sqrt(a*c)/b+b/sqrt(a*c)+sqrt(a*b)/c+c/sqrt(b*a)+a/sqrt(b*c)+sqrt(b*c)/a)
      by Lm35;
   sqrt(a*c)/b+b/sqrt(a*c)+sqrt(a*b)/c+c/sqrt(b*a)+a/sqrt(b*c)+sqrt(b*c)/a
     >=6 by Lm36;then
   2+(sqrt(a*c)/b+b/sqrt(a*c)+sqrt(a*b)/c+c/sqrt(b*a)+a/sqrt(b*c)+sqrt(b*c)/a)
     >=6+2 by XREAL_1:8;then
   8*(2+sqrt(a*c)/b+b/sqrt(a*c)+sqrt(a*b)/c+c/sqrt(b*a)+a/sqrt(b*c)+
     sqrt(b*c)/a)>=8*8 by XREAL_1:66;
   hence thesis by A7,XXREAL_0:2;
end;

theorem
  x+y+z=1 implies x^2+y^2+z^2>=1/3
proof
   assume x+y+z=1;then
A3:x^2+y^2+z^2+2*x*y+2*y*z+2*z*x=1 by SQUARE_1:59,Lm8;
A4:(x^2+y^2)/2>=x*y & (y^2+z^2)/2>=y*z & (x^2+z^2)/2>=x*z by SERIES_3:7;then
   (x^2+y^2)/2+(y^2+z^2)/2>=y*z+x*y by XREAL_1:9;then
   (x^2+y^2)/2+(y^2+z^2)/2+(x^2+z^2)/2>=y*z+x*y+x*z by A4,XREAL_1:9;then
   -((x^2+y^2)/2+(y^2+z^2)/2+(x^2+z^2)/2)<=-(y*z+x*y+x*z) by XREAL_1:26;then
   (-((x^2+y^2)/2+(y^2+z^2)/2+(x^2+z^2)/2))*2<=(-(y*z+x*y+x*z))*2
     by XREAL_1:66;then
   -((x^2+y^2)/2+(y^2+z^2)/2+(x^2+z^2)/2)*2+1<=-(y*z+x*y+x*z)*2+1
     by XREAL_1:8;then
   1-(x^2+y^2+z^2)*2<=1-(y*z+x*y+x*z)*2;then
   1<=(x^2+y^2+z^2)+(x^2+y^2+z^2)*2 by A3,XREAL_1:22;then
   1/3<=((x^2+y^2+z^2)*3)/3 by XREAL_1:74;
   hence thesis;
end;

theorem
  x+y+z=1 implies x*y+y*z+z*x<=1/3
proof
   assume x+y+z=1; then
A2:1^2=x^2+y^2+z^2+2*x*y+2*y*z+2*z*x by Lm8;
   x^2+y^2+z^2>=x*y+y*z+x*z by SERIES_3:10;then
   (x^2+y^2+z^2)+2*(x*y+y*z+z*x)>=(x*y+y*z+x*z)+2*(x*y+y*z+z*x)
     by XREAL_1:9;then
   1/3>=(3*(x*y+y*z+z*x))/3 by A2,XREAL_1:74;
   hence thesis;
end;

theorem
  a>b & b>c implies (a to_power (2*a))*(b to_power (2*b))*(c to_power (2*c)) >
  (a to_power (b+c))*(b to_power (a+c))*(c to_power (a+b))
proof
  assume
A1:a>b & b>c;then
A2:a-b>0 & b-c>0 by XREAL_1:52;
A3:a>c by A1,XXREAL_0:2;then
A4:a-c>0 by XREAL_1:52;
A5:a/b>1 & b/c>1 by A1,XREAL_1:189;
A6:a/c>1 by A3,XREAL_1:189;
A7:(a/b) to_power (a-b) > 1 & (b/c) to_power (b-c) > 1 &
   (a/c) to_power (a-c) > 1  by A2,A4,A5,A6,POWER:40;then
   ((a/b) to_power (a-b))*((b/c) to_power (b-c)) > 1 by XREAL_1:163;then
   ((a/b) to_power (a-b))*((b/c) to_power (b-c)) *((a/c) to_power -(c-a))
   >1 by A7,XREAL_1:163;then
   ((a/b) to_power (a-b))*((b/c) to_power (b-c)) *((c/a) to_power (c-a))
   >1 by Lm5;then
   (a to_power (a-b)/b to_power (a-b))*((b/c) to_power (b-c)) *((c/a)
     to_power (c-a)) >1 by POWER:36;then
   (a to_power (a-b)/b to_power (a-b))*(b to_power (b-c)/c to_power (b-c))
     *((c/a) to_power (c-a)) >1 by POWER:36;then
   (a to_power (a-b)/b to_power (a-b))*(b to_power (b-c)/c to_power (b-c))
     *(c to_power (c-a)/a to_power (c-a)) >1 by POWER:36;then
((a to_power (a-b))*(b to_power (b-c)))/((c to_power (b-c))*(b to_power (a-b)))
     *(c to_power (c-a)/a to_power (c-a)) >1 by XCMPLX_1:77;then
((a to_power (a-b))/(c to_power (b-c)))*((b to_power (b-c))/(b to_power (a-b)))
     *(c to_power (c-a)/a to_power (c-a)) >1 by XCMPLX_1:77;then
    ((a to_power (a-b))/(c to_power (b-c)))*(b to_power ((b-c)-(a-b)))
     *(c to_power (c-a)/a to_power (c-a)) >1 by POWER:34;then
    ((a to_power (a-b))/(c to_power (b-c)))*(b to_power (2*b-a-c))
     *(c to_power (c-a)/a to_power (c-a)) >1;then
    ((a to_power (a-b))/(c to_power (b-c)))*(c to_power (c-a)/a to_power (c-a))
     *(b to_power (2*b-a-c)) >1;then
    ((a to_power (a-b))/a to_power (c-a))*(c to_power (c-a)/(c to_power (b-c)))
     *(b to_power (2*b-a-c)) >1 by XCMPLX_1:86;then
    ((a to_power ((a-b)-(c-a))))*(c to_power (c-a)/(c to_power (b-c)))
     *(b to_power (2*b-a-c)) >1 by POWER:34;then
    ((a to_power (2*a-b-c)))*(c to_power ((c-a)-(b-c)))
     *(b to_power (2*b-a-c)) >1 by POWER:34;then
    ((a to_power (2*a-(b+c))))*(c to_power (2*c-(a+b)))
     *(b to_power (2*b-(a+c))) >1;then
    (a to_power (2*a)/a to_power (b+c))*(c to_power (2*c-(a+b)))
     *(b to_power (2*b-(a+c))) >1 by POWER:34;then
    (a to_power (2*a)/a to_power (b+c))*(c to_power (2*c)/c to_power (a+b))
     *(b to_power (2*b-(a+c))) >1 by POWER:34;then
    ((a to_power (2*a))*((c to_power (2*c)))/((a to_power (b+c))*
    (c to_power (a+b))))*(b to_power (2*b-(a+c))) >1 by XCMPLX_1:77;then
    ((a to_power (2*a))*((c to_power (2*c)))/((a to_power (b+c))*
    (c to_power (a+b))))*
    (b to_power (2*b)/b to_power (a+c)) >1 by POWER:34;then
A8: ((a to_power (2*a))*(c to_power (2*c))*(b to_power (2*b)))/
    ((a to_power (b+c))*
    (c to_power (a+b))*(b to_power (a+c))) >1 by XCMPLX_1:77;
A9:a to_power (b+c)>0 & b to_power (c+a)>0 & c to_power (a+b)>0
     by POWER:39;then
   (a to_power (b+c))*(c to_power (a+b))>0 by XREAL_1:131;then
A10:(a to_power (b+c))*(c to_power (a+b))*(b to_power (a+c))>0
     by A9,XREAL_1:131;
   set d=(a to_power (2*a))*(b to_power (2*b))*(c to_power (2*c));
   set e=(a to_power (b+c))*(b to_power (a+c))*(c to_power (a+b));
   (d/e)*e>1*e by A8,A10,XREAL_1:70;
   hence thesis by A10,XCMPLX_1:88;
end;

theorem
  n>=1 implies a|^(n+1)+b|^(n+1)>=a|^n*b+a*b|^n
proof
  assume
A1:n>=1;
   per cases;
   suppose
A2:a-b>0;then
   a-b+b>0+b by XREAL_1:10;then
   a|^n>b|^n by A1,PREPOWER:18;then
   a|^n-b|^n>0 by XREAL_1:52;then
   (a-b)*(a|^n-b|^n)>0 by A2,XREAL_1:131;then
   a*(a|^n)-a*(b|^n)-b*(a|^n)+b*(b|^n)>0;then
   a|^(n+1)-a*(b|^n)-b*(a|^n)+b*(b|^n)>0 by NEWTON:11;then
   a|^(n+1)-a*(b|^n)-b*(a|^n)+b|^(n+1)>0 by NEWTON:11;then
   (a|^(n+1)+b|^(n+1)-a*(b|^n)-b*(a|^n))+(a*(b|^n)+b*(a|^n))>
     0+(a*(b|^n)+b*(a|^n)) by XREAL_1:10;
     hence thesis;
   end;
   suppose A4:a-b=0;then
A5:a|^(n+1)=b|^(n+1);
   a|^n*b+a*b|^n
  =a|^(n+1)+(a|^n*a) by A4,NEWTON:11
 .=a|^(n+1)+a|^(n+1) by NEWTON:11
 .=2*(a|^(n+1));
  hence thesis by A5;
 end;
   suppose
A6:a-b<0;then
   (a-b)+b<0+b by XREAL_1:10;then
   a|^n<b|^n by A1,PREPOWER:18;then
   a|^n-b|^n<0 by XREAL_1:51;then
   (a-b)*(a|^n-b|^n)>0 by A6,XREAL_1:132;then
   a*(a|^n)-a*(b|^n)-b*(a|^n)+b*(b|^n)>0;then
   a|^(n+1)-a*(b|^n)-b*(a|^n)+b*(b|^n)>0 by NEWTON:11;then
   a|^(n+1)-a*(b|^n)-b*(a|^n)+b|^(n+1)>0 by NEWTON:11;then
   (a|^(n+1)+b|^(n+1)-a*(b|^n)-b*(a|^n))+(a*(b|^n)+b*(a|^n))>
     0+(a*(b|^n)+b*(a|^n)) by XREAL_1:10;
   hence thesis;
   end;
end;

theorem
  a^2+b^2=c^2 & n>=3 implies a|^(n+2)+b|^(n+2)<c|^(n+2)
proof
A3:b^2>0 & a^2>0;
   assume
A1:a^2+b^2=c^2 & n>=3;then
   c^2-a^2+a^2>0+a^2 by XREAL_1:10;then
   sqrt(c^2)>sqrt(a^2) by SQUARE_1:95;then
   c>sqrt(a^2) by SQUARE_1:89;then
A4:c>a by SQUARE_1:89;
B2:n>=1 by A1,XXREAL_0:2;then
B3:c|^n>a|^n by A4,PREPOWER:18;
   c^2-b^2+b^2>0+b^2 by A1,XREAL_1:10;then
   sqrt(c^2)>sqrt(b^2) by SQUARE_1:95;then
   c>sqrt(b^2) by SQUARE_1:89;then
   c>b by SQUARE_1:89;then
B4:c|^n>b|^n by B2,PREPOWER:18;
A7:a|^2>0 & b|^2>0 by A3,NEWTON:100;then
   c|^n*a|^2>a|^n*a|^2 by B3,XREAL_1:70;then
A8:c|^n*a|^2>a|^(n+2) by NEWTON:13;
   c|^n*b|^2>b|^n*b|^2 by B4,A7,XREAL_1:70;then
   c|^n*b|^2>b|^(n+2) by NEWTON:13;then
   c|^n*a|^2+c|^n*b|^2>a|^(n+2)+b|^(n+2) by A8,XREAL_1:10;then
   c|^n*(a|^2+b|^2)>a|^(n+2)+b|^(n+2);then
   c|^n*(a^2+b|^2)>a|^(n+2)+b|^(n+2) by NEWTON:100;then
   c|^n*c^2>a|^(n+2)+b|^(n+2) by A1,NEWTON:100;then
   c|^n*c|^2>a|^(n+2)+b|^(n+2) by NEWTON:100;
   hence thesis by NEWTON:13;
end;

theorem
  n>=1 implies (1+1/(n+1))|^n<(1+1/n)|^(n+1)
proof
  assume
A1:n>=1;
   n+1>n+0 by XREAL_1:10;then
   1/(n+1)<1/n by A1,XREAL_1:78;then
A2:1/(n+1)+1<1/n+1 by XREAL_1:10;
A4:(1+1/(n+1))|^n<(1+1/n)|^n by A1,A2,PREPOWER:18;
A5:1/n>0 by A1,XREAL_1:141;
A7:(1+1/n)|^(n+1)-(1+1/n)|^n
  =(1+1/n)|^n*(1+1/n)-(1+1/n)|^n by NEWTON:11
 .=(1+1/n)|^n*(1/n);
   (1+1/n)|^n>0 by PREPOWER:13;then
   (1+1/n)|^(n+1)-(1+1/n)|^n>0 by A7,A5,XREAL_1:131;then
   (1+1/n)|^(n+1)-(1+1/n)|^n+(1+1/n)|^n>0+(1+1/n)|^n by XREAL_1:10;
   hence thesis by A4,XXREAL_0:2;
end;

theorem
  n>=1 & k>=1 implies (a|^k+b|^k)*(a|^n+b|^n)<=2*(a|^(k+n)+b|^(k+n))
proof
  assume
B1:n>=1 & k>=1;
A1:(a|^k+b|^k)*(a|^n+b|^n)-2*(a|^(k+n)+b|^(k+n))
  =a|^k*a|^n+a|^k*b|^n+b|^k*a|^n+b|^k*b|^n-2*a|^(k+n)-2*(b|^(k+n))
 .=a|^(k+n)+a|^k*b|^n+b|^k*a|^n+b|^k*b|^n-2*a|^(k+n)-2*(b|^(k+n)) by NEWTON:13
 .=a|^(k+n)+a|^k*b|^n+b|^k*a|^n+b|^(k+n)-2*a|^(k+n)-2*(b|^(k+n)) by NEWTON:13
 .=a|^k*b|^n+b|^k*a|^n-a|^(k+n)-(b|^(k+n))
 .=a|^k*b|^n+b|^k*a|^n-a|^k*a|^n-(b|^(k+n)) by NEWTON:13
 .=a|^k*b|^n+b|^k*a|^n-a|^k*a|^n-b|^k*b|^n by NEWTON:13
 .=(a|^n-b|^n)*(b|^k-a|^k);
   per cases;
   suppose a-b>0;then
   a-b+b>0+b by XREAL_1:10;then
A3:a|^n>b|^n & a|^k>b|^k by B1,PREPOWER:18;then
A4:a|^n-b|^n>0 by XREAL_1:52;
   b|^k-a|^k<0 by A3,XREAL_1:51;then
   ((a|^k+b|^k)*(a|^n+b|^n))-(2*(a|^(k+n)+b|^(k+n)))<0 by A1,A4,XREAL_1:134;
   hence thesis by XREAL_1:50;
   end;
   suppose a-b=0;then
   a|^n=b|^n & a|^k=b|^k;
   hence thesis by A1;
   end;
   suppose a-b<0;then
   a-b+b<0+b by XREAL_1:10;then
A5:a|^n<b|^n & a|^k<b|^k by B1,PREPOWER:18;then
A6:a|^n-b|^n<0 by XREAL_1:51;
   b|^k-a|^k>0 by A5,XREAL_1:52;then
   ((a|^k+b|^k)*(a|^n+b|^n))-(2*(a|^(k+n)+b|^(k+n)))<0
     by A1,A6,XREAL_1:134;
   hence thesis by XREAL_1:50;
   end;
end;

theorem
  (for n holds s.n=1/sqrt(n+1)) implies
    for n holds (Partial_Sums s).n<2*sqrt(n+1)
proof
  assume
A1:for n holds s.n=1/sqrt(n+1);
   defpred X[Nat] means (Partial_Sums s).$1<2*sqrt($1+1);
   (Partial_Sums s).0 = s.0 by SERIES_1:def 1
 .=1/sqrt(0+1) by A1
 .=1 by SQUARE_1:83;then
A2:X[0] by SQUARE_1:83;
A3:for n st X[n] holds X[n+1]
   proof let n;
   assume (Partial_Sums s).n<2*sqrt(n+1);then
B1:(Partial_Sums s).n+1/sqrt(n+2)<2*sqrt(n+1)+1/sqrt(n+2) by XREAL_1:10;
A10:sqrt(n+2)>0 by SQUARE_1:93;
   4*n^2+12*n+8<4*n^2+12*n+9 by XREAL_1:10;then
   sqrt(4*((n+2)*(n+1)))<sqrt(((2*n)+3)^2) by SQUARE_1:95;then
   2*sqrt((n+2)*(n+1))<sqrt(((2*n)+3)^2) by SQUARE_1:85,97;then
   2*sqrt((n+2)*(n+1))<2*n+3 by SQUARE_1:89;then
   2*sqrt((n+2)*(n+1))+1<2*n+3+1 by XREAL_1:10;then
   2*(sqrt(n+2)*sqrt(n+1))+1<2*(n+2) by SQUARE_1:97;then
   2*(sqrt(n+2)*sqrt(n+1))+1<2*sqrt((n+2)^2) by SQUARE_1:89;then
   (2*(sqrt(n+2)*sqrt(n+1))+1)/sqrt(n+2)<(2*sqrt((n+2)*(n+2)))/sqrt(n+2)
     by A10,XREAL_1:76;then
   (2*(sqrt(n+2)*sqrt(n+1))+1)/sqrt(n+2)<(2*(sqrt(n+2)*sqrt(n+2)))/sqrt(n+2)
     by SQUARE_1:97;then
   (2*(sqrt(n+2)*sqrt(n+1))+1)/sqrt(n+2)<(2*sqrt(n+2))*sqrt(n+2)/sqrt(n+2);
     then
   (2*(sqrt(n+2)*sqrt(n+1))+1)/sqrt(n+2)<2*sqrt(n+2) by A10,XCMPLX_1:90;then
   (2*sqrt(n+1))*sqrt(n+2)/sqrt(n+2)+1/sqrt(n+2)<2*sqrt(n+2) by XCMPLX_1:63;
     then
   (2*sqrt(n+1))+1/sqrt(n+2)<2*sqrt(n+2) by A10,XCMPLX_1:90;then
   (Partial_Sums s).n+1/sqrt((n+1)+1)<2*sqrt(n+2) by B1,XXREAL_0:2;then
   (Partial_Sums s).n+s.(n+1)<2*sqrt(n+2) by A1;
   hence thesis by SERIES_1:def 1;
   end;
   for n holds X[n] from NAT_1:sch 1(A2,A3);
   hence thesis;
end;

theorem Th4:
  (for n holds s.n=1/((n+1)^2)) implies
    for n holds (Partial_Sums s).n<=2-1/(n+1)
proof
   assume
A1:for n holds s.n=1/((n+1)^2);
   defpred X[Nat] means (Partial_Sums s).$1<=2-1/($1+1);
   (Partial_Sums s).0=s.0 by SERIES_1:def 1
      .=1/((0+1)^2) by A1
      .=1;then
A2:X[0];
A3:for n st X[n] holds X[n+1]
   proof let n;
   assume (Partial_Sums s).n<=2-1/(n+1);then
A5:(Partial_Sums s).n+1/((n+2)^2)<=2-1/(n+1)+1/((n+2)^2) by XREAL_1:9;
A8:(n+1)*((n+2)^2)>0 by XREAL_1:131;
   n^2+3*n+3>n^2+3*n+2 by XREAL_1:10;then
   (n^2+3*n+3)/((n+1)*((n+2)^2))>((n+2)*(n+1))/(((n+2)^2)*(n+1))
     by A8,XREAL_1:76;then
   (n^2+3*n+3)/((n+1)*((n+2)^2))>(n+2)/((n+2)*(n+2))by XCMPLX_1:92;then
   (n^2+3*n+3)/((n+1)*((n+2)^2))>((n+2)/(n+2))/(n+2) by XCMPLX_1:79;then
   (n^2+3*n+3)/((n+1)*((n+2)^2))>1/(n+2) by XCMPLX_1:60;then
   (-1)*((n^2+3*n+3)/((n+1)*((n+2)^2)))<(-1)*(1/(n+2))
     by XREAL_1:71;then
   -(n^2+3*n+3)/((n+1)*((n+2)^2))+2<-1/(n+2)+2 by XREAL_1:10;then
A9:2-(n^2+3*n+3)/((n+1)*((n+2)^2))<2-1/(n+2);
   (Partial_Sums s).(n+1) =(Partial_Sums s).n+s.(n+1) by SERIES_1:def 1
 .=(Partial_Sums s).n+1/((n+1+1)^2) by A1;then
   (Partial_Sums s).(n+1)<=2-(1/(n+1)-1/((n+2)^2)) by A5;then
   (Partial_Sums s).(n+1)<=2-(1*((n+2)^2)-1*(n+1))/((n+1)*((n+2)^2))
     by XCMPLX_1:131;
   hence thesis by A9,XXREAL_0:2;
   end;
   for n holds X[n] from NAT_1:sch 1(A2,A3);
   hence thesis;
end;

theorem
  (for n holds s.n=1/((n+1)^2)) implies (Partial_Sums s).n<2
proof
  assume for n holds s.n=1/((n+1)^2);then
A2:(Partial_Sums s).n<=2-1/(n+1) by Th4;
   -(1/(n+1))+2<0+2 by XREAL_1:10;
   hence thesis by A2,XXREAL_0:2;
end;

theorem Th9:
  (for n holds s.n<1) implies
    for n holds Partial_Sums(s).n<n+1
proof
   assume
A1:for n holds s.n<1;
   defpred X[Nat] means Partial_Sums(s).$1<$1+1;
A2:Partial_Sums(s).0 = s.0 by SERIES_1:def 1;
A3:X[0] by A1,A2;
A4:for n st X[n] holds X[n+1]
   proof let n;
   assume
A5:Partial_Sums(s).n<n+1;
A6:Partial_Sums(s).(n+1) = Partial_Sums(s).n+s.(n+1) by SERIES_1:def 1;
   s.(n+1)<1 by A1;
   hence thesis by A5,XREAL_1:10,A6;
   end;
   for n holds X[n] from NAT_1:sch 1(A3,A4);
   hence thesis;
end;

theorem
  (for n holds s.n>0 & s.n<1) implies
    for n holds Partial_Product(s).n>=Partial_Sums(s).n-n
proof
  assume
A1:for n holds s.n>0 & s.n<1;
   defpred X[Nat] means Partial_Product(s).$1>=Partial_Sums(s).$1-$1;
   Partial_Sums(s).0-0 = s.0 by SERIES_1:def 1;then
A3:X[0] by SERIES_3:def 1;
A4:for n st X[n] holds X[n+1]
   proof let n; assume
A5:Partial_Product(s).n>=Partial_Sums(s).n-n;
   Partial_Sums(s).n<n+1 by A1,Th9;then
A6:Partial_Sums(s).n-(n+1)<0 by XREAL_1:51;
   s.(n+1)<1 by A1;then
   s.(n+1)*(Partial_Sums(s).n-n-1)>1*(Partial_Sums(s).n-n-1)
     by A6,XREAL_1:71;then
A7:s.(n+1)*Partial_Sums(s).n-s.(n+1)*n-s.(n+1)+s.(n+1)>
   Partial_Sums(s).n-n-1+s.(n+1) by XREAL_1:10;
   s.(n+1)>0 by A1;then
   Partial_Product(s).n*s.(n+1)>=(Partial_Sums(s).n-n)*s.(n+1)
     by A5,XREAL_1:66;then
   Partial_Product(s).n*s.(n+1)>Partial_Sums(s).n+s.(n+1)-(n+1)
     by A7,XXREAL_0:2;then
   Partial_Product(s).(n+1)>Partial_Sums(s).n+s.(n+1)-(n+1)
     by SERIES_3:def 1;
   hence thesis by SERIES_1:def 1;
   end;
   for n holds X[n] from NAT_1:sch 1(A3,A4);
   hence thesis;
end;

theorem Th11:
  (for n holds s.n>0 & s1.n=1/s.n) implies
    for n holds Partial_Sums(s1).n>0
proof
  assume
A1:for n holds s.n>0 & s1.n=1/s.n;
   defpred X[Nat] means Partial_Sums(s1).$1>0;
A3:s.0>0 by A1;
B1:Partial_Sums(s1).0=s1.0 by SERIES_1:def 1;
   s1.0=1/s.0 by A1;then
A4:X[0] by B1,A3,XREAL_1:141;
A5:for n st X[n] holds X[n+1]
   proof let n; assume
A6:Partial_Sums(s1).n>0;
A7:Partial_Sums(s1).(n+1)=Partial_Sums(s1).n+s1.(n+1) by SERIES_1:def 1;
A8:s1.(n+1)=1/s.(n+1) by A1;
   s.(n+1)>0 by A1;then
   s1.(n+1)>0 by A8,XREAL_1:141;
   hence thesis by A7,A6,XREAL_1:36;
   end;
   for n holds X[n] from NAT_1:sch 1(A4,A5);
   hence thesis;
end;

theorem
  (for n holds s.n>0 & s1.n=1/s.n) implies
    for n holds Partial_Sums(s).n*Partial_Sums(s1).n>=(n+1)^2
proof
   assume
A1:for n holds s.n>0 & s1.n=1/s.n;
   defpred X[Nat] means Partial_Sums(s).$1*Partial_Sums(s1).$1>=($1+1)^2;
A2:s.0>0 by A1;
A3:Partial_Sums(s).0*Partial_Sums(s1).0
      =s.0*Partial_Sums(s1).0 by SERIES_1:def 1
     .=s.0*s1.0 by SERIES_1:def 1
     .=s.0*(1/s.0) by A1
     .=s.0/(s.0/1) by XCMPLX_1:80
     .=1 by A2,XCMPLX_1:60;
A4:X[0] by A3;
A5:for n st X[n] holds X[n+1]
   proof let n; assume
A6:Partial_Sums(s).n*Partial_Sums(s1).n>=(n+1)^2;
   set x=Partial_Sums(s).n;
   set y=Partial_Sums(s1).n;
A8:s.(n+1)>0 by A1;
A9:Partial_Sums(s).(n+1)*Partial_Sums(s1).(n+1)
  =(x+s.(n+1))*Partial_Sums(s1).(n+1) by SERIES_1:def 1
 .=(x+s.(n+1))*(y+s1.(n+1)) by SERIES_1:def 1
 .=x*y+x*(s1.(n+1))+(s.(n+1))*y+(s.(n+1))*(s1.(n+1))
 .=x*y+x*(1/s.(n+1))+(s.(n+1))*y+(s.(n+1))*(s1.(n+1)) by A1
 .=x*y+x*(1/s.(n+1))+(s.(n+1))*y+(s.(n+1))*(1/s.(n+1)) by A1
 .=x*y+x*(1/s.(n+1))+(s.(n+1))*y+((s.(n+1))*1)/s.(n+1) by XCMPLX_1:75
 .=x*y+x*(1/s.(n+1))+(s.(n+1))*y+1 by A8,XCMPLX_1:60
 .=x*y+x/(s.(n+1)/1)+(s.(n+1))*y+1 by XCMPLX_1:80
 .=x*y+x/s.(n+1)+(s.(n+1))*y+1;
A10:x>0 by A1,SERIES_3:33;
A11:y>0 by A1,Th11;
A12:x/s.(n+1)>0 by A8,A10,XREAL_1:141;
    y*s.(n+1)>0 by A8,A11,XREAL_1:131;then
    x/s.(n+1)+y*s.(n+1)>=2*sqrt((y*s.(n+1))*(x/s.(n+1)))
      by A12,SIN_COS2:1;then
    x/s.(n+1)+y*s.(n+1)>=2*sqrt(x/((1*s.(n+1))/(y*s.(n+1))))
      by XCMPLX_1:82;then
    x/s.(n+1)+y*s.(n+1)>=2*sqrt(x/(1/y)) by A8,XCMPLX_1:92;then
A14:x/s.(n+1)+y*s.(n+1)>=2*sqrt(y*(x/1)) by XCMPLX_1:82;
    sqrt(x*y)>=sqrt((n+1)^2) by A6,SQUARE_1:94;then
    sqrt(x*y)>=n+1 by SQUARE_1:89;then
    2*sqrt(x*y)>=2*(n+1) by XREAL_1:66;then
    x/s.(n+1)+y*s.(n+1)>=2*n+2 by A14,XXREAL_0:2;then
A16:(x/s.(n+1)+y*s.(n+1))+(n^2+2*n+2)>=(2*n+2)+(n^2+2*n+2)
      by XREAL_1:9;
    x*y+(x/s.(n+1)+y*s.(n+1)+1)>=(n+1)^2+(x/s.(n+1)+y*s.(n+1)+1)
      by A6,XREAL_1:9;
    hence thesis by A9,A16,XXREAL_0:2;
   end;
   for n holds X[n] from NAT_1:sch 1(A4,A5);
   hence thesis;
end;

theorem
  (for n st n>=1 holds s.n=sqrt(n) & s.0=0) implies
    for n st n>=1 holds Partial_Sums(s).n<1/6*(4*n+3)*sqrt(n)
proof
   assume
A1:for n st n>=1 holds s.n=sqrt(n) & s.0=0;
   defpred X[Nat] means Partial_Sums(s).$1<1/6*(4*$1+3)*sqrt($1);
A2:Partial_Sums(s).(1+0)
    =Partial_Sums(s).0 + s.(1+0) by SERIES_1:def 1
   .=s.0 + s.1 by SERIES_1:def 1
   .=0+s.1 by A1
   .=1 by A1,SQUARE_1:83;
A3:X[1] by A2,SQUARE_1:83;
A4:for n st n>=1 & X[n] holds X[n+1]
   proof let n; assume
A5:n>=1 & Partial_Sums(s).n<1/6*(4*n+3)*sqrt(n);
   n+1>=1+1 by A5,XREAL_1:9;then
   n+1>=1 by XXREAL_0:2;then
A7:s.(n+1)=sqrt(n+1) by A1;
   0+(16*n^2+8*n)<1+(16*n^2+8*n) by XREAL_1:10;then
A8:(16*n+8)*n<1*(16*n^2+8*n+1);
   (4*(4*n+1)+4)/((4*n+1)^2)<1/n by A5,A8,XREAL_1:108;then
A9:(4*(4*n+1))/((4*n+1)*(4*n+1))+4/((4*n+1)^2)<1/n by XCMPLX_1:63;
   4/(4*n+1)+4/((4*n+1)^2)<1/n by A9,XCMPLX_1:92;then
   1+(4/(4*n+1)+4/((4*n+1)^2))<1+1/n by XREAL_1:10;then
   1+4/(4*n+1)+4/((4*n+1)^2)<1+1/n;then
   1+4/(4*n+1)+2^2/((4*n+1)^2)<1+1/n;then
   1+2*2/(4*n+1)+(2/(4*n+1))^2<1+1/n by XCMPLX_1:77;then
   1+2*(2/(4*n+1))+(2/(4*n+1))^2<1+1/n by XCMPLX_1:75;then
A11:(2/(4*n+1)+1)^2<(1+n*1)/n by A5,XCMPLX_1:114;
A12:sqrt((2/(4*n+1)+1)^2)<sqrt((1+n)/n) by A11,SQUARE_1:95;
    2/(4*n+1)+1<sqrt((1+n)/n) by A12,SQUARE_1:89;then
A13:(2+(4*n+1)*1)/(4*n+1)<sqrt((1+n)/n) by XCMPLX_1:114;
A14:(4*n+3)/(4*n+1)<sqrt(1+n)/sqrt(n) by A13,SQUARE_1:99;
    sqrt(n)>0 by A5,SQUARE_1:93;then
    (4*n+3)*(sqrt(n))<(sqrt(1+n))*(4*n+1) by A14,XREAL_1:104;then
    (1/6)*((4*n+3)*(sqrt(n)))<(1/6)*((sqrt(1+n))*(4*n+1))
      by XREAL_1:70;then
    Partial_Sums(s).n<(1/6)*sqrt(1+n)*((4*n)+7)-sqrt(1+n) by A5,XXREAL_0:2;then
    Partial_Sums(s).n+sqrt(1+n)<(1/6)*sqrt(1+n)*((4*n)+7)-sqrt(1+n)+sqrt(1+n)
      by XREAL_1:10; then
    Partial_Sums(s).(n+1)<(1/6)*(4*(n+1)+3)*sqrt(1+n) by A7,SERIES_1:def 1;
    hence thesis;
    end;
    for n st n>=1 holds X[n] from BINOM:sch 1(A3,A4);
    hence thesis;
end;

theorem
  (for n st n>=1 holds s.n=sqrt(n) & s.0=0) implies
  for n st n>=1 holds Partial_Sums(s).n>(2/3)*n*sqrt(n)
proof
   assume
A1:for n st n>=1 holds s.n=sqrt(n) & s.0=0;
   defpred X[Nat] means Partial_Sums(s).$1>(2/3)*$1*sqrt($1);
A2:Partial_Sums(s).(1+0)
    =Partial_Sums(s).0 + s.(1+0) by SERIES_1:def 1
   .=s.0 + s.1 by SERIES_1:def 1
   .=0+s.1 by A1
   .=1 by A1,SQUARE_1:83;
A3:X[1] by A2,SQUARE_1:83;
A4:for n st n>=1 & X[n] holds X[n+1]
   proof let n; assume
A5:n>=1 & Partial_Sums(s).n>(2/3)*n*sqrt(n);
   3*n>=3*1 by A5,XREAL_1:66;then
   3*n>1 by XXREAL_0:2;then
   1-3*n<0 by XREAL_1:51;then
   1+(n-4*n)+(4*n^2-4*n^2)+4*n|^3<0+4*n|^3 by XREAL_1:10;then
   (4*(n|^3)-4*n^2+n)+(4*n^2-4*n+1)<4*n|^3;then
   (4*(n|^(2+1))-4*n^2+n)+(4*n^2-4*n+1)<4*n|^3;then
   (4*(n|^2*n)-4*(n*n)+n)+(4*n^2-4*n+1)<4*n|^3 by NEWTON:11;then
   (4*n|^2-4*n+1)*n+(4*n^2-4*n+1)*1<4*n|^3;then
   (4*n^2-4*n+1)*n+(4*n^2-4*n+1)*1<4*n|^3 by NEWTON:100;then
   (4*n^2-4*n+1)*(n+1)<4*n|^(2+1);then
   (4*n^2-4*n+1)*(n+1)<4*(n|^2*n) by NEWTON:11;then
   (4*n^2-4*n+1)*(n+1)<4*n|^2*n;then
A7:(2*n-1)^2*(n+1)<4*n^2*n by NEWTON:100;
A9:2*n>=2*1 by A5,XREAL_1:66;then
   2*n>1 by XXREAL_0:2;then
A10:2*n-1>0 by XREAL_1:52;then
   (2*n-1)^2>0 by SQUARE_1:74;then
   (n+1)/n<(2*n)^2/(2*n-1)^2  by A7,A5,XREAL_1:108;then
A11:(n+1)/n<((2*n)/(2*n-1))^2 by XCMPLX_1:77;
A12:sqrt((n+1)/n)<sqrt(((2*n)/(2*n-1))^2) by A11,SQUARE_1:95;
   (2*n)/(2*n-1)>0 by A9,A10,XREAL_1:141;then
   sqrt((n+1)/n)<(2*n)/(2*n-1) by A12,SQUARE_1:89;then
A13:sqrt(n+1)/sqrt(n)<(2*n)/(2*n-1) by SQUARE_1:99;
    sqrt(n)>0 by A5,SQUARE_1:93;then
    sqrt(n+1)*(2*n-1)<(2*n)*sqrt(n) by A10,A13,XREAL_1:104;then
    (1/3)*(sqrt(n+1)*(2*n-1))<(1/3)*((2*n)*sqrt(n)) by XREAL_1:70;then
    Partial_Sums(s).n>((2/3)*(n+1))*sqrt(n+1)-sqrt(n+1) by A5,XXREAL_0:2;then
A14:Partial_Sums(s).n+sqrt(n+1)>((2/3)*(n+1))*sqrt(n+1)-sqrt(n+1)+sqrt(n+1)
      by XREAL_1:10;
    n+1>=1+1 by A5,XREAL_1:9;then
    n+1>=1 by XXREAL_0:2;then
    Partial_Sums(s).n+s.(n+1)>((2/3)*(n+1))*sqrt(n+1) by A1,A14;
    hence thesis by SERIES_1:def 1;
    end;
    for n st n>=1 holds X[n] from BINOM:sch 1(A3,A4);
    hence thesis;
   end;

theorem
  (for n st n>=1 holds s.n=1+1/(2*n+1) & s.0=1) implies
    for n st n>=1 holds Partial_Product(s).n>(1/2)*sqrt(2*n+3)
proof
  assume
A1:for n st n>=1 holds s.n=1+1/(2*n+1) & s.0=1;
   defpred X[Nat] means Partial_Product(s).$1>(1/2)*sqrt(2*$1+3);
A2:Partial_Product(s).(1+0)
    =Partial_Product(s).0 * s.(1+0) by SERIES_3:def 1
   .=s.0 * s.1 by SERIES_3:def 1
   .=1*s.1 by A1
   .=1+1/(2*1+1) by A1
   .=4/3;
A3:(1/2)*sqrt(2*1+3)=(1/2)*sqrt(5) .=sqrt(5)/2;
   sqrt(16/9)>sqrt(5/4) by SQUARE_1:95;then
   sqrt(4^2)/sqrt(3^2)>sqrt(5/4) by SQUARE_1:99;then
   4/sqrt(3^2)>sqrt(5/4) by SQUARE_1:89;then
   4/3>sqrt(5/4) by SQUARE_1:89;then
   4/3>sqrt(5)/sqrt(2^2) by SQUARE_1:99;then
B1:X[1] by A3,A2,SQUARE_1:89;
A4:for n st n>=1 & X[n] holds X[n+1]
   proof let n; assume
A5:n>=1 & Partial_Product(s).n>(1/2)*sqrt(2*n+3);
   Partial_Product(s).n*(1+1/(2*n+3))>((1/2)*sqrt(2*n+3))*(1+1/(2*n+3))
     by A5,XREAL_1:70;then
   Partial_Product(s).n*(1+1/(2*n+3))>((1/2)*sqrt(2*n+3))*1+
     ((1/2)*sqrt(2*n+3))*(1/(2*n+3));then
   Partial_Product(s).n*(1+1/(2*n+3))>(1/2)*sqrt(2*n+3)+
     ((1/2)*sqrt(2*n+3))/(2*n+3) by XCMPLX_1:75;then
   Partial_Product(s).n*(1+1/(2*n+3))>(1/2)*sqrt(2*n+3)+
     (1/2)*(sqrt(2*n+3)/(2*n+3)) by XCMPLX_1:75;then
   Partial_Product(s).n*(1+1/(2*n+3))>(1/2)*sqrt(2*n+3)+
     (1/2)*(sqrt(2*n+3)/sqrt((2*n+3)^2)) by SQUARE_1:89;then
   Partial_Product(s).n*(1+1/(2*n+3))>(1/2)*sqrt(2*n+3)+
     (1/2)*((sqrt(2*n+3)*1)/(sqrt(2*n+3)*sqrt(2*n+3)))
        by SQUARE_1:97;then
A7:Partial_Product(s).n*(1+1/(2*n+3))>(1/2)*sqrt(2*n+3)+
     (1/2)*((sqrt(2*n+3)/sqrt(2*n+3))*(1/sqrt(2*n+3))) by XCMPLX_1:77;
A8:sqrt(2*n+3)>0 by SQUARE_1:93;then
A9:Partial_Product(s).n*(1+1/(2*n+3))>(1/2)*sqrt(2*n+3)+
     (1/2)*(1*(1/sqrt(2*n+3))) by A7,XCMPLX_1:60;
    1/(2*n+3)+(2*n+5)>0+(2*n+5) by XREAL_1:10;then
    1/(2*n+3)+2+(2*n+3)>2*n+5;then
    1^2/(sqrt(2*n+3))^2+2+(2*n+3)>2*n+5 by SQUARE_1:def 4;then
    (1/(sqrt(2*n+3)))^2+2+(2*n+3)>2*n+5 by XCMPLX_1:77;then
    (1/sqrt(2*n+3))^2+2*1+(sqrt(2*n+3))^2>2*n+5 by SQUARE_1:def 4;then
(1/sqrt(2*n+3))^2+2*(sqrt(2*n+3)*(1/sqrt(2*n+3)))+(sqrt(2*n+3))^2>2*n+5
      by A8,XCMPLX_1:107;then
A12:sqrt((1/sqrt(2*n+3)+sqrt(2*n+3))^2)>sqrt(2*n+5) by SQUARE_1:95;
    1/sqrt(2*n+3)>0 by A8,XREAL_1:141;then
    1/sqrt(2*n+3)+sqrt(2*n+3)>0 by A8,XREAL_1:36;then
    sqrt(2*n+3)+1/sqrt(2*n+3)>sqrt(2*n+5) by A12,SQUARE_1:89;then
    (1/2)*(sqrt(2*n+3)+1/sqrt(2*n+3))>(1/2)*sqrt(2*n+5) by XREAL_1:70;then
A13:Partial_Product(s).n*(1+1/(2*(n+1)+1))>(1/2)*sqrt(2*n+5) by A9,XXREAL_0:2;
    n+1>=1+1 by A5,XREAL_1:9;then
    n+1>=1 by XXREAL_0:2;then
    Partial_Product(s).n*s.(n+1)>(1/2)*sqrt(2*n+5) by A1,A13;
    hence thesis by SERIES_3:def 1;
   end;
   for n st n>=1 holds X[n] from BINOM:sch 1(B1,A4);
   hence thesis;
end;

theorem
  (for n st n>=1 holds s.n=sqrt(n*(n+1)) & s.0=0) implies
    for n st n>=1 holds Partial_Sums(s).n>(n*(n+1))/2
proof
   assume
A1:for n st n>=1 holds s.n=sqrt(n*(n+1)) & s.0=0;
   defpred X[Nat] means Partial_Sums(s).$1>($1*($1+1))/2;
A2:Partial_Sums(s).1 = Partial_Sums(s).(0+1)
   .=Partial_Sums(s).0+s.1 by SERIES_1:def 1
   .=s.0+s.1 by SERIES_1:def 1
   .=0+s.1 by A1
   .=sqrt(1*(1+1)) by A1
   .=sqrt 2;then
A4:(Partial_Sums(s).1)^2 = 2 by SQUARE_1:def 4;
   sqrt((Partial_Sums(s).1)^2)>sqrt(((1*(1+1))/2)^2) by A4,SQUARE_1:95;then
A8:sqrt((Partial_Sums(s).1)^2)>((1*(1+1))/2) by SQUARE_1:89;
   Partial_Sums(s).1>0 by A2,SQUARE_1:93;then
A9:X[1] by A8,SQUARE_1:89;
A10:for n st n>=1 & X[n] holds X[n+1]
    proof let n; assume
A11:n>=1 & Partial_Sums(s).n>(n*(n+1))/2;
A12:n+2>n+1 by XREAL_1:10;
A14:(n+2)*(n+1)>(n+1)^2 by A12,XREAL_1:70;
    sqrt((n+2)*(n+1))>sqrt((n+1)^2) by A14,SQUARE_1:95;then
A15:sqrt((n+2)*(n+1))>n+1 by SQUARE_1:89;
    n+1>=1+1 by A11,XREAL_1:9;then
    n+1>=1 by XXREAL_0:2;then
A17:s.(n+1)=sqrt((n+1)*(n+1+1)) by A1;
      Partial_Sums(s).n+(sqrt((n+1)*(n+2))-((n+1)*(n+2))/2)>(n*(n+1))/2
      +(sqrt((n+1)*(n+2))-((n+1)*(n+2))/2) by A11,XREAL_1:10;then
A19:Partial_Sums(s).n+sqrt((n+1)*(n+2))-((n+1)*(n+2))/2>
      sqrt((n+1)*(n+2))-(n+1);
    Partial_Sums(s).n+sqrt((n+1)*(n+2))-((n+1)*(n+2))/2>0
      by A19,A15,XREAL_1:52;then
    Partial_Sums(s).n+sqrt((n+1)*(n+2))-((n+1)*(n+2))/2+((n+1)*(n+2))/2>0
      +((n+1)*(n+2))/2 by XREAL_1:10;
    hence thesis by A17,SERIES_1:def 1;
    end;
    for n st n>=1 holds X[n] from BINOM:sch 1(A9,A10);
    hence thesis;
end;
