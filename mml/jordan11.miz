:: Preparing the Internal Approximations of Simple Closed Curves
::  by Andrzej Trybulec
::
:: Received May 21, 2002
:: Copyright (c) 2002 Association of Mizar Users
 
environ

 vocabularies NUMBERS, SUBSET_1, TOPREAL2, EUCLID, XXREAL_0, CARD_1, TREES_1,
      JORDAN8, RLTOPSP1, RELAT_1, JORDAN1A, XBOOLE_0, JORDAN6, TOPREAL1,
      JORDAN9, TARSKI, PRE_TOPC, GOBOARD9, JORDAN1H, ARYTM_3, FINSEQ_1,
      MATRIX_1, JORDAN1E, CONNSP_1, PSCOMP_1, INT_1, MCART_1, FUNCT_1, SEQ_4,
      SEQ_1, RCOMP_1, STRUCT_0, REAL_1, ZFMISC_1, ORDINAL2, RELAT_2, XREAL_0,
      ORDINAL1, XXREAL_2, XXREAL_1, JORDAN10, JORDAN2C, SETFAM_1, GOBOARD5,
      ARYTM_1, NEWTON, METRIC_1, JORDAN11, MEASURE5;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, SETFAM_1, ORDINAL1, NUMBERS,
      XXREAL_0, XREAL_0, REAL_1, INT_1, NAT_1, NAT_D, NEWTON, XXREAL_2, SEQ_4,
      RELSET_1, FUNCT_2, FINSEQ_1, MATRIX_1, SEQ_1, RCOMP_1, STRUCT_0,
      PRE_TOPC, METRIC_1, METRIC_6, CONNSP_1, COMPTS_1, RLTOPSP1, EUCLID,
      TOPREAL1, TOPREAL2, GOBOARD5, GOBOARD9, PSCOMP_1, JORDAN2C, JORDAN6,
      JORDAN8, JORDAN9, JORDAN10, JORDAN1A, JORDAN1E, JORDAN1H, TOPREAL6,
      JORDAN1K;
 constructors SETFAM_1, REAL_1, NAT_1, NAT_D, RCOMP_1, NEWTON, BINARITH,
      TOPS_1, CONNSP_1, TBSP_1, JORDAN1, PSCOMP_1, GOBOARD9, JORDAN5C, JORDAN6,
      JORDAN2C, TOPREAL6, JCT_MISC, JORDAN1K, JORDAN8, JORDAN9, JORDAN10,
      JORDAN1A, JORDAN1E, JORDAN1H, SEQ_1, XXREAL_2, SEQ_4, CONVEX1;
 registrations SUBSET_1, ORDINAL1, RELSET_1, NUMBERS, XXREAL_0, XREAL_0, NAT_1,
      INT_1, MEMBERED, STRUCT_0, COMPTS_1, TBSP_1, EUCLID, TOPREAL1, TOPREAL2,
      SPPOL_2, PSCOMP_1, TOPREAL5, SPRECT_2, JORDAN6, JORDAN2C, TOPREAL6,
      JORDAN8, JORDAN10, VALUED_0, XXREAL_2, JORDAN, RLTOPSP1, MEASURE6;
 requirements NUMERALS, REAL, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, XBOOLE_0, JORDAN1H, PSCOMP_1, SUBSET_1;
 theorems SEQ_4, JORDAN8, NEWTON, NAT_1, GOBOARD7, GOBOARD5, XBOOLE_0,
      XBOOLE_1, JORDAN2C, SUBSET_1, GOBOARD9, ZFMISC_1, EUCLID, GOBOARD1,
      JORDAN6, PSCOMP_1, SPRECT_1, TARSKI, TOPREAL3, SETFAM_1, RCOMP_1,
      RELAT_1, FUNCT_2, TOPREAL1, PRE_TOPC, COMPTS_1, JORDAN1A, ENUMSET1,
      FUNCT_1, XREAL_0, JCT_MISC, JORDAN10, GOBRD14, JORDAN1B, JORDAN1C,
      JORDAN1F, JORDAN1G, JORDAN1H, JORDAN1I, JORDAN1J, PREPOWER, XREAL_1,
      JORDAN1K, METRIC_6, WEIERSTR, JORDAN9, TBSP_1, XXREAL_0, JORDAN1, INT_1,
      MATRIX_1, XXREAL_2, NAT_D, RLTOPSP1;
 schemes DOMAIN_1, FUNCT_2;

begin

reserve i,j,k,n for Element of NAT,
  C for being_simple_closed_curve Subset of TOP-REAL 2;

Lm1: i > 0 & 1 <= j & j <= width Gauge(C,i) & k <= j & 1 <= k & k <= width
Gauge(C,i) & LSeg(Gauge(C,i)*(Center Gauge(C,i),j),Gauge(C,i)*(Center Gauge(C,i
),k)) /\ Upper_Arc L~Cage(C,i) = {Gauge(C,i)*(Center Gauge(C,i),j)} & LSeg(
Gauge(C,i)*(Center Gauge(C,i),j),Gauge(C,i)*(Center Gauge(C,i),k)) /\ Lower_Arc
L~Cage(C,i) = {Gauge(C,i)*(Center Gauge(C,i),k)} implies LSeg(Gauge(C,i)*(
Center Gauge(C,i),j),Gauge(C,i)*(Center Gauge(C,i),k)) c= Cl RightComp Cage(C,i
)
proof
  assume that
A1: i > 0 and
A2: 1 <= j and
A3: j <= width Gauge(C,i) and
A4: k <= j and
A5: 1 <= k and
A6: k <= width Gauge(C,i) and
A7: LSeg(Gauge(C,i)*(Center Gauge(C,i),j),Gauge(C,i)*(Center Gauge(C,i),
  k)) /\ Upper_Arc L~Cage(C,i) = {Gauge(C,i)*(Center Gauge(C,i),j)} and
A8: LSeg(Gauge(C,i)*(Center Gauge(C,i),j),Gauge(C,i)*(Center Gauge(C,i),
  k)) /\ Lower_Arc L~Cage(C,i) = {Gauge(C,i)*(Center Gauge(C,i),k)};
  set p = Gauge(C,i)*(Center Gauge(C,i),j), q = Gauge(C,i)*(Center Gauge(C,i),
  k), S = RightComp Cage(C,i);
A9: {q} c= Lower_Arc L~Cage(C,i) by A8,XBOOLE_1:17;
A10: X-SpanStart(C,i) = Center Gauge(C,i) by JORDAN1B:17;
  then
A11: 1 < Center Gauge(C,i) by JORDAN1H:58,XXREAL_0:2;
A12: Center Gauge(C,i) < len Gauge(C,i) by A10,JORDAN1H:58;
  then
A13: [Center Gauge(C,i),j] in Indices Gauge(C,i) by A2,A3,A11,MATRIX_1:37;
  p in {p} by TARSKI:def 1;
  then p in Upper_Arc L~Cage(C,i) by A7,XBOOLE_0:def 4;
  then
A14: p in L~Upper_Seq(C,i) by A1,JORDAN1G:63;
A15: [Center Gauge(C,i),k] in Indices Gauge(C,i) by A5,A6,A11,A12,MATRIX_1:37;
  q in {q} by TARSKI:def 1;
  then q in Lower_Arc L~Cage(C,i) by A8,XBOOLE_0:def 4;
  then q in L~Lower_Seq(C,i) by A1,JORDAN1G:64;
  then j <> k by A2,A6,A11,A12,A14,JORDAN1J:57;
  then
A16: p <> q by A13,A15,JORDAN1H:32;
  set A = LSeg(p,q) \ {p,q};
  LSeg(p,q) /\ L~Cage(C,i) = LSeg(p,q) /\ (Upper_Arc L~Cage(C,i) \/
  Lower_Arc L~Cage(C,i)) by JORDAN6:65
    .= LSeg(p,q) /\ Upper_Arc L~Cage(C,i) \/ LSeg(p,q) /\ Lower_Arc L~Cage(C
  ,i) by XBOOLE_1:23
    .= {p,q} by A7,A8,ENUMSET1:41;
  then A misses L~Cage(C,i) by XBOOLE_1:90;
  then
A17: A c= (L~Cage(C,i))` by SUBSET_1:43;
A18: C c= S by JORDAN10:11;
  LSeg(q,p) meets Upper_Arc C by A1,A3,A4,A5,A7,A8,A11,A12,JORDAN1J:61;
  then
A19: LSeg(q,p) meets C by JORDAN6:76,XBOOLE_1:63;
  {p} c= Upper_Arc L~Cage(C,i) by A7,XBOOLE_1:17;
  then {p} \/ {q} c= Upper_Arc L~Cage(C,i) \/ Lower_Arc L~Cage(C,i) by A9,
XBOOLE_1:13;
  then {p} \/ {q} c= L~Cage(C,i) by JORDAN6:65;
  then
A20: {p,q} c= L~Cage(C,i) by ENUMSET1:41;
  L~Cage(C,i) misses C by JORDAN10:5;
  then {p,q} misses C by A20,XBOOLE_1:63;
  then
A21: A meets C by A19,XBOOLE_1:84;
  S is_a_component_of (L~Cage(C,i))` by GOBOARD9:def 2;
  then A c= S by A17,A21,A18,GOBOARD9:6,JORDAN1:52;
  hence thesis by A16,JORDAN1H:8;
end;

Lm2: ex n st n is_sufficiently_large_for C
proof
  set s = (W-bound C + E-bound C)/2, e = Gauge(C,1)*(X-SpanStart(C,1),len
  Gauge(C,1)), f = Gauge(C,1)*(X-SpanStart(C,1),1);
A1: len Gauge(C,1) = width Gauge(C,1) by JORDAN8:def 1;
A2: X-SpanStart(C,1) = Center Gauge(C,1) by JORDAN1B:17;
  then X-SpanStart(C,1) = (len Gauge(C,1)) div 2 + 1 by JORDAN1A:def 1;
  then
A3: 1 <= X-SpanStart(C,1) by NAT_1:11;
  len Gauge(C,1) >= 4 by JORDAN8:13;
  then
A4: 1 < len Gauge(C,1) by XXREAL_0:2;
  then
A5: f`1 = s by A2,JORDAN1A:59;
  then
A6: f in Vertical_Line s by JORDAN1A:17;
  0 < len Gauge(C,1) by JORDAN8:13;
  then len Gauge(C,1) qua Integer div 2 < len Gauge(C,1) by INT_1:83;
  then (len Gauge(C,1)) div 2 + 1 <= len Gauge(C,1) by NAT_1:13;
  then X-SpanStart(C,1) <= len Gauge(C,1) by A2,JORDAN1A:def 1;
  then
A7: f`2 < e`2 by A3,A4,A1,GOBOARD5:5;
  set e1 = proj2.e, f1 = proj2.f;
A8: proj2.e = e`2 by PSCOMP_1:def 29;
  4 <= len Gauge (C,1) by JORDAN8:13;
  then
A9: 1 <= len Gauge (C,1) by XXREAL_0:2;
  set AA = LSeg(e,f) /\ Upper_Arc C, BB = LSeg(e,f) /\ Lower_Arc C;
  deffunc F(Element of NAT)= lower_bound(proj2.:(LSeg(f,e) /\ Upper_Arc L~Cage
  (C,$1+1)));
  consider Es being Real_Sequence such that
A10: for i being Element of NAT holds Es.i = F(i) from FUNCT_2:sch 4;
A11: BB is compact;
  reconsider A = proj2.:AA, B = proj2.:BB as compact Subset of REAL by A11
,JCT_MISC:24;
  deffunc G(Element of NAT)=|[s,Es.$1]|;
  consider E being Function of NAT, the carrier of TOP-REAL 2 such that
A12: for i being Element of NAT holds E.i = G(i) from FUNCT_2:sch 4;
A13: e`1 = s by A2,A4,JORDAN1A:59;
  then e in Vertical_Line s by JORDAN1A:17;
  then
A14: LSeg(e,f) c= Vertical_Line s by A6,JORDAN1A:23;
A15: A misses B
  proof
    assume A meets B;
    then consider z being set such that
A16: z in A and
A17: z in B by XBOOLE_0:3;
    reconsider z as Real by A16;
    consider p being Point of TOP-REAL 2 such that
A18: p in AA and
A19: z = proj2.p by A16,FUNCT_2:116;
A20: p in Upper_Arc C by A18,XBOOLE_0:def 4;
    consider q being Point of TOP-REAL 2 such that
A21: q in BB and
A22: z = proj2.q by A17,FUNCT_2:116;
A23: p`2 = proj2.q by A19,A22,PSCOMP_1:def 29
      .= q`2 by PSCOMP_1:def 29;
A24: q in Lower_Arc C by A21,XBOOLE_0:def 4;
A25: q in LSeg(e,f) by A21,XBOOLE_0:def 4;
A26: p in LSeg(e,f) by A18,XBOOLE_0:def 4;
    then p`1 = s by A14,JORDAN6:34
      .= q`1 by A14,A25,JORDAN6:34;
    then p = q by A23,TOPREAL3:11;
    then p in Upper_Arc C /\ Lower_Arc C by A20,A24,XBOOLE_0:def 4;
    then
A27: p in {W-min C,E-max C } by JORDAN6:65;
    per cases by A27,TARSKI:def 2;
    suppose
A28:  p = W-min C;
A29:  (W-min C)`1 = W-bound C by EUCLID:56;
      (W-min C)`1 = s by A14,A26,A28,JORDAN6:34;
      hence contradiction by A29,SPRECT_1:33;
    end;
    suppose
A30:  p = E-max C;
A31:  (E-max C)`1 = E-bound C by EUCLID:56;
      (E-max C)`1 = s by A14,A26,A30,JORDAN6:34;
      hence contradiction by A31,SPRECT_1:33;
    end;
  end;
  deffunc H(Element of NAT) =upper_bound(proj2.:(LSeg(f,E.$1) /\ Lower_Arc L~
  Cage(C,$1+1)));
  consider Fs being Real_Sequence such that
A32: for i being Element of NAT holds Fs.i = H(i) from FUNCT_2:sch 4;
  deffunc I(Element of NAT)=|[s,Fs.$1]|;
  consider F being Function of NAT, the carrier of TOP-REAL 2 such that
A33: for i being Element of NAT holds F.i = I(i) from FUNCT_2:sch 4;
  deffunc F(Element of NAT)=proj2.:LSeg(E.$1,F.$1);
  consider S being Function of NAT, bool REAL such that
A34: for i being Element of NAT holds S.i = F(i) from FUNCT_2:sch 4;
A35: for i holds E.i in Upper_Arc L~Cage(C,i+1)
  proof
    let i;
    set p = E.i;
A36: i+1 >= 1 by NAT_1:11;
    reconsider DD = LSeg(f,e) /\ Upper_Arc L~Cage(C,i+1) as compact Subset of
    TOP-REAL 2;
    reconsider D = proj2.:DD as compact Subset of REAL by JCT_MISC:24;
    DD c= the carrier of TOP-REAL 2;
    then DD c= dom proj2 by FUNCT_2:def 1;
    then
A37: dom proj2 /\ DD = DD by XBOOLE_1:28;
A38: X-SpanStart(C,i+1) = Center Gauge(C,i+1) by JORDAN1B:17;
    then
    LSeg(Gauge(C,i+1)*(X-SpanStart(C,i+1),1), Gauge(C,i+1)*(X-SpanStart(C
    ,i+1),len Gauge(C,i+1))) meets Upper_Arc L~Cage(C,i+1) by JORDAN1B:34;
    then LSeg(f,e) meets Upper_Arc L~Cage(C,i+1) by A2,A38,A36,JORDAN1A:65
,XBOOLE_1:63;
    then DD <> {} by XBOOLE_0:def 7;
    then dom proj2 meets DD by A37,XBOOLE_0:def 7;
    then
A39: D <> {} by RELAT_1:151;
    Es.i = lower_bound D by A10;
    then consider dd being Point of TOP-REAL 2 such that
A40: dd in DD and
A41: Es.i = proj2.dd by A39,FUNCT_2:116,RCOMP_1:32;
A42: dd in LSeg(e,f) by A40,XBOOLE_0:def 4;
A43: E.i = |[s,Es.i]| by A12;
    then p`2 = Es.i by EUCLID:56;
    then
A44: dd`2 = p`2 by A41,PSCOMP_1:def 29;
    p`1 = s by A43,EUCLID:56;
    then
A45: dd`1 = p`1 by A14,A42,JORDAN6:34;
    dd in Upper_Arc L~Cage(C,i+1) by A40,XBOOLE_0:def 4;
    hence thesis by A45,A44,TOPREAL3:11;
  end;
A46: for i holds F.i in Lower_Arc L~Cage(C,i+1)
  proof
    let i;
    set p = F.i;
A47: X-SpanStart(C,i+1) = Center Gauge(C,i+1) by JORDAN1B:17;
    reconsider DD = LSeg(f,E.i) /\ Lower_Arc L~Cage(C,i+1) as compact Subset
    of TOP-REAL 2;
    reconsider D = proj2.:DD as compact Subset of REAL by JCT_MISC:24;
A48: E.i in Upper_Arc L~Cage(C,i+1) by A35;
    DD c= the carrier of TOP-REAL 2;
    then DD c= dom proj2 by FUNCT_2:def 1;
    then
A49: dom proj2 /\ DD = DD by XBOOLE_1:28;
A50: E.i = |[s,Es.i]| by A12;
    then
A51: (E.i)`1 = s by EUCLID:56;
    then E.i in Vertical_Line s by JORDAN1A:17;
    then
A52: LSeg(E.i,f) c= Vertical_Line s by A6,JORDAN1A:23;
    (E.i)`2 = Es.i by A50,EUCLID:56
      .= lower_bound(proj2.:(LSeg(f,e) /\ Upper_Arc L~Cage(C,i+1))) by A10;
    then ex j st 1 <= j & j <= width Gauge(C,i+1) & E.i = Gauge(C,i+1)*(
    X-SpanStart(C,i+1),j) by A2,A1,A47,A51,JORDAN1F:13;
    then LSeg(f,E.i) meets Lower_Arc L~Cage(C,i+1) by A2,A47,A48,JORDAN1J:62;
    then DD <> {} by XBOOLE_0:def 7;
    then dom proj2 meets DD by A49,XBOOLE_0:def 7;
    then
A53: D <> {} by RELAT_1:151;
    Fs.i = upper_bound D by A32;
    then consider dd being Point of TOP-REAL 2 such that
A54: dd in DD and
A55: Fs.i = proj2.dd by A53,FUNCT_2:116,RCOMP_1:32;
A56: dd in Lower_Arc L~Cage(C,i+1) by A54,XBOOLE_0:def 4;
A57: F.i = |[s,Fs.i]| by A33;
    then p`2 = Fs.i by EUCLID:56;
    then
A58: dd`2 = p`2 by A55,PSCOMP_1:def 29;
A59: dd in LSeg(E.i,f) by A54,XBOOLE_0:def 4;
    p`1 = s by A57,EUCLID:56;
    then dd`1 = p`1 by A59,A52,JORDAN6:34;
    hence thesis by A56,A58,TOPREAL3:11;
  end;
A60: for i being Element of NAT holds S.i qua Subset of REAL is interval &
  S.i meets A & S.i meets B
  proof
    let i be Element of NAT;
    reconsider DD = LSeg(e,f) /\ Upper_Arc L~Cage(C,i+1) as compact Subset of
    TOP-REAL 2;
    reconsider D = proj2.:DD as compact Subset of REAL by JCT_MISC:24;
A61: X-SpanStart(C,i+1) = Center Gauge(C,i+1) by JORDAN1B:17;
    DD c= the carrier of TOP-REAL 2;
    then DD c= dom proj2 by FUNCT_2:def 1;
    then
A62: dom proj2 /\ DD = DD by XBOOLE_1:28;
A63: 1 <= i+1 by NAT_1:11;
    LSeg(Gauge(C,i+1)*(Center Gauge(C,i+1),1), Gauge(C,i+1)*(Center
Gauge(C,i+1),len Gauge(C,i+1))) meets Upper_Arc L~Cage(C,i+1) by JORDAN1B:34;
    then LSeg(f,e) meets Upper_Arc L~Cage(C,i+1) by A2,A63,JORDAN1A:65
,XBOOLE_1:63;
    then DD <> {} by XBOOLE_0:def 7;
    then dom proj2 meets DD by A62,XBOOLE_0:def 7;
    then
A64: D <> {} by RELAT_1:151;
    Es.i = lower_bound D by A10;
    then consider dd being Point of TOP-REAL 2 such that
A65: dd in DD and
A66: Es.i = proj2.dd by A64,FUNCT_2:116,RCOMP_1:32;
A67: dd in LSeg(f,e) by A65,XBOOLE_0:def 4;
    reconsider DD = LSeg(f,E.i) /\ Lower_Arc L~Cage(C,i+1) as compact Subset
    of TOP-REAL 2;
    reconsider D = proj2.:DD as compact Subset of REAL by JCT_MISC:24;
    DD c= the carrier of TOP-REAL 2;
    then DD c= dom proj2 by FUNCT_2:def 1;
    then
A68: dom proj2 /\ DD = DD by XBOOLE_1:28;
A69: E.i = |[s,Es.i]| by A12;
    then
A70: (E.i)`1 = s by EUCLID:56;
A71: F.i = |[s,Fs.i]| by A33;
    then
A72: (F.i)`1 = s by EUCLID:56;
A73: (F.i)`2 =Fs.i by A71,EUCLID:56
      .= upper_bound(proj2.:(LSeg(f,E.i) /\ Lower_Arc L~Cage(C,i+1)))by A32;
    (E.i)`2 = Es.i by A69,EUCLID:56
      .= lower_bound(proj2.:(LSeg(f,e) /\ Upper_Arc L~Cage(C,i+1))) by A10;
    then consider j such that
A74: 1 <= j and
A75: j <= width Gauge(C,i+1) and
A76: E.i = Gauge(C,i+1)*(X-SpanStart(C,i+1),j) by A2,A1,A70,A61,JORDAN1F:13;
A77: E.i in Upper_Arc L~Cage(C,i+1) by A35;
    then consider k such that
A78: 1 <= k and
A79: k <= width Gauge(C,i+1) and
A80: F.i = Gauge(C,i+1)*(Center Gauge(C,i+1),k) by A2,A61,A72,A74,A75,A76,A73,
JORDAN1I:30;
    (E.i)`2 = Es.i by A69,EUCLID:56
      .= lower_bound(proj2.:(LSeg(f,e) /\ Upper_Arc L~Cage(C,i+1))) by A10;
    then ex j st 1 <= j & j <= width Gauge(C,i+1) & E.i = Gauge(C,i+1)*(
    X-SpanStart(C,i+1),j) by A2,A1,A70,A61,JORDAN1F:13;
    then LSeg(f,E.i) meets Lower_Arc L~Cage(C,i+1) by A2,A61,A77,JORDAN1J:62;
    then DD <> {} by XBOOLE_0:def 7;
    then dom proj2 meets DD by A68,XBOOLE_0:def 7;
    then
A81: D <> {} by RELAT_1:151;
A82: (E.i)`2 = Es.i by A69,EUCLID:56
      .= dd`2 by A66,PSCOMP_1:def 29;
    for p being real number st p in D holds p <= (E.i)`2
    proof
      let p be real number;
      assume p in D;
      then consider x being set such that
      x in dom proj2 and
A83:  x in DD and
A84:  p = proj2.x by FUNCT_1:def 12;
A85:  f`2 <= (E.i)`2 by A7,A67,A82,TOPREAL1:10;
      reconsider x as Point of TOP-REAL 2 by A83;
      x in LSeg(f,E.i) by A83,XBOOLE_0:def 4;
      then x`2 <= (E.i)`2 by A85,TOPREAL1:10;
      hence thesis by A84,PSCOMP_1:def 29;
    end;
    then
A86: upper_bound D <= (E.i)`2 by A81,SEQ_4:62;
    dd`1 = (E.i)`1 by A14,A70,A67,JORDAN6:34;
    then
A87: E.i in LSeg(e,f) by A67,A82,TOPREAL3:11;
    Fs.i = upper_bound D by A32;
    then consider dd being Point of TOP-REAL 2 such that
A88: dd in DD and
A89: Fs.i = proj2.dd by A81,FUNCT_2:116,RCOMP_1:32;
A90: (F.i)`2 = Fs.i by A71,EUCLID:56
      .= dd`2 by A89,PSCOMP_1:def 29;
A91: dd in LSeg(E.i,f) by A88,XBOOLE_0:def 4;
    E.i in Vertical_Line s by A70,JORDAN1A:17;
    then LSeg(E.i,f) c= Vertical_Line s by A6,JORDAN1A:23;
    then dd`1 = (F.i)`1 by A72,A91,JORDAN6:34;
    then
A92: F.i in LSeg(E.i,f) by A91,A90,TOPREAL3:11;
    f in LSeg(e,f) by RLTOPSP1:69;
    then LSeg(f,E.i) c= LSeg(e,f) by A87,TOPREAL1:12;
    then
A93: LSeg(E.i,F.i) c= LSeg(e,f) by A87,A92,TOPREAL1:12;
A94: for x be set holds x in LSeg(E.i,F.i) /\ Upper_Arc L~Cage(C,i+1)
    implies x = E.i
    proof
      let x be set;
      reconsider DD = LSeg(f,e) /\ Upper_Arc L~Cage(C,i+1) as compact Subset
      of TOP-REAL 2;
      reconsider D = proj2.:DD as compact Subset of REAL by JCT_MISC:24;
      assume
A95:  x in LSeg(E.i,F.i) /\ Upper_Arc L~Cage(C,i+1);
      then reconsider p = x as Point of TOP-REAL 2;
A96:  p in LSeg(E.i,F.i) by A95,XBOOLE_0:def 4;
      p in Upper_Arc L~Cage(C,i+1) by A95,XBOOLE_0:def 4;
      then p in DD by A93,A96,XBOOLE_0:def 4;
      then proj2.p in D by FUNCT_2:43;
      then
A97:  p`2 in D by PSCOMP_1:def 29;
      E.i = |[s,Es.i]| by A12;
      then
A98:  (E.i)`2 = Es.i by EUCLID:56
        .= lower_bound D by A10;
      D is bounded by RCOMP_1:28;
      then
A99:  (E.i)`2 <= p`2 by A98,A97,SEQ_4:def 5;
      p`2 <= (E.i)`2 by A73,A86,A96,TOPREAL1:10;
      then
A100: p`2 = (E.i)`2 by A99,XXREAL_0:1;
      p`1 = (E.i)`1 by A70,A72,A96,GOBOARD7:5;
      hence thesis by A100,TOPREAL3:11;
    end;
A101: Gauge(C,i+1)*(Center Gauge(C,i+1),j) in LSeg(Gauge(C,i+1)*(Center
    Gauge(C,i+1),k), Gauge(C,i+1)*(Center Gauge(C,i+1),j)) by RLTOPSP1:69;
A102: LSeg(Gauge(C,i+1)*(Center Gauge(C,i+1),k), Gauge(C,i+1)*(Center
Gauge(C,i+1),j)) /\ Upper_Arc L~Cage(C,i+1) = {Gauge(C,i+1)*(Center Gauge(C,i+1
    ),j)}
    proof
      thus LSeg(Gauge(C,i+1)*(Center Gauge(C,i+1),k), Gauge(C,i+1)*(Center
Gauge(C,i+1),j)) /\ Upper_Arc L~Cage(C,i+1) c= {Gauge(C,i+1)*(Center Gauge(C,i+
      1),j)}
      proof
        let x be set;
        assume x in LSeg(Gauge(C,i+1)*(Center Gauge(C,i+1),k), Gauge(C,i+1)
        *(Center Gauge(C,i+1),j)) /\ Upper_Arc L~Cage(C,i+1);
        then x = Gauge(C,i+1)*(Center Gauge(C,i+1),j) by A61,A76,A80,A94;
        hence thesis by TARSKI:def 1;
      end;
      let x be set;
      assume x in {Gauge(C,i+1)*(Center Gauge(C,i+1),j)};
      then x = Gauge(C,i+1)*(Center Gauge(C,i+1),j) by TARSKI:def 1;
      hence thesis by A61,A77,A76,A101,XBOOLE_0:def 4;
    end;
    E.i in LSeg(E.i,f) by RLTOPSP1:69;
    then
A103: LSeg(E.i,F.i) c= LSeg(E.i,f) by A92,TOPREAL1:12;
A104: for x be set holds x in LSeg(E.i,F.i) /\ Lower_Arc L~Cage(C,i+1)
    implies x = F.i
    proof
      let x be set;
      reconsider EE = LSeg(f,E.i) /\ Lower_Arc L~Cage(C,i+1) as compact Subset
      of TOP-REAL 2;
      reconsider E0 = proj2.:EE as compact Subset of REAL by JCT_MISC:24;
      assume
A105: x in LSeg(E.i,F.i) /\ Lower_Arc L~Cage(C,i+1);
      then reconsider p = x as Point of TOP-REAL 2;
A106: p in LSeg(E.i,F.i) by A105,XBOOLE_0:def 4;
      p in Lower_Arc L~Cage(C,i+1) by A105,XBOOLE_0:def 4;
      then p in EE by A103,A106,XBOOLE_0:def 4;
      then proj2.p in E0 by FUNCT_2:43;
      then
A107: p`2 in E0 by PSCOMP_1:def 29;
      F.i = |[s,Fs.i]| by A33;
      then
A108: (F.i)`2 = Fs.i by EUCLID:56
        .= upper_bound E0 by A32;
      E0 is bounded by RCOMP_1:28;
      then
A109: (F.i)`2 >= p`2 by A108,A107,SEQ_4:def 4;
      p`2 >= (F.i)`2 by A73,A86,A106,TOPREAL1:10;
      then
A110: p`2 = (F.i)`2 by A109,XXREAL_0:1;
      p`1 = (F.i)`1 by A70,A72,A106,GOBOARD7:5;
      hence thesis by A110,TOPREAL3:11;
    end;
A111: F.i in Lower_Arc L~Cage(C,i+1) by A46;
A112: S.i = proj2.:LSeg(E.i,F.i) by A34;
    hence S.i is interval by JCT_MISC:15;
A113: Center Gauge(C,i+1) <= len Gauge(C,i+1) by JORDAN1B:14;
A114: Gauge(C,i+1)*(Center Gauge(C,i+1),k) in LSeg(Gauge(C,i+1)*(Center
    Gauge(C,i+1),k), Gauge(C,i+1)*(Center Gauge(C,i+1),j)) by RLTOPSP1:69;
A115: LSeg(Gauge(C,i+1)*(Center Gauge(C,i+1),k), Gauge(C,i+1)*(Center
Gauge(C,i+1),j)) /\ Lower_Arc L~Cage(C,i+1) = {Gauge(C,i+1)*(Center Gauge(C,i+1
    ),k)}
    proof
      thus LSeg(Gauge(C,i+1)*(Center Gauge(C,i+1),k), Gauge(C,i+1)*(Center
Gauge(C,i+1),j)) /\ Lower_Arc L~Cage(C,i+1) c= {Gauge(C,i+1)*(Center Gauge(C,i+
      1),k)}
      proof
        let x be set;
        assume x in LSeg(Gauge(C,i+1)*(Center Gauge(C,i+1),k), Gauge(C,i+1)
        *(Center Gauge(C,i+1),j)) /\ Lower_Arc L~Cage(C,i+1);
        then x = Gauge(C,i+1)*(Center Gauge(C,i+1),k) by A61,A76,A80,A104;
        hence thesis by TARSKI:def 1;
      end;
      let x be set;
      assume x in {Gauge(C,i+1)*(Center Gauge(C,i+1),k)};
      then x = Gauge(C,i+1)*(Center Gauge(C,i+1),k) by TARSKI:def 1;
      hence thesis by A80,A111,A114,XBOOLE_0:def 4;
    end;
    1 <= Center Gauge(C,i+1) by JORDAN1B:12;
    then
A116: k <= j by A61,A74,A76,A73,A79,A80,A113,A86,GOBOARD5:5;
    then LSeg(E.i,F.i) meets AA by A61,A93,A75,A76,A78,A80,A102,A115,
JORDAN1J:64,XBOOLE_1:77;
    hence S.i meets A by A112,JORDAN1A:24;
    LSeg(E.i,F.i) meets BB by A61,A93,A75,A76,A78,A80,A116,A102,A115,
JORDAN1J:63,XBOOLE_1:77;
    hence thesis by A112,JORDAN1A:24;
  end;
  proj2.f = f`2 by PSCOMP_1:def 29;
  then
A117: proj2.:LSeg(f,e) = [.f1,e1.] by A7,A8,SPRECT_1:61;
  then
A118: B c= [.f1,e1.] by RELAT_1:156,XBOOLE_1:17;
  A c= [.f1,e1.] by A117,RELAT_1:156,XBOOLE_1:17;
  then consider r being real number such that
A119: r in [.f1,e1.] and
A120: not r in A \/ B and
A121: for i being Element of NAT ex k being Element of NAT st i <= k & r
  in S.k by A15,A118,A60,JCT_MISC:21;
  reconsider r as Real by XREAL_0:def 1;
  set p = |[s,r]|;
A122: p`1 = s by EUCLID:56;
  for Y being set st Y in BDD-Family C holds p in Y
  proof
    let Y be set;
A123: BDD-Family C = { Cl BDD L~Cage(C,k1) where k1 is Element of NAT :
    not contradiction } by JORDAN10:def 2;
    assume Y in BDD-Family C;
    then consider k1 being Element of NAT such that
A124: Y = Cl BDD L~Cage(C,k1) by A123;
    consider k0 being Element of NAT such that
A125: k1 <= k0 and
A126: r in S.k0 by A121;
A127: proj2.(F.k0) = (F.k0)`2 by PSCOMP_1:def 29;
    reconsider EE = LSeg(f,E.k0) /\ Lower_Arc L~Cage(C,k0+1) as compact Subset
    of TOP-REAL 2;
    reconsider CC = LSeg(f,E.k0) /\ Lower_Arc L~Cage(C,k0+1) as compact Subset
    of TOP-REAL 2;
    reconsider W = proj2.:CC as compact Subset of REAL by JCT_MISC:24;
A128: Center Gauge(C,k0+1) <= len Gauge(C,k0+1) by JORDAN1B:14;
    reconsider E0 = proj2.:EE as compact Subset of REAL by JCT_MISC:24;
    CC c= the carrier of TOP-REAL 2;
    then CC c= dom proj2 by FUNCT_2:def 1;
    then
A129: dom proj2 /\ CC = CC by XBOOLE_1:28;
A130: RightComp Cage(C,k0+1) c= RightComp Cage(C,k0) by JORDAN1H:56,NAT_1:11;
    RightComp Cage(C,k0) c= RightComp Cage(C,k1) by A125,JORDAN1H:56;
    then RightComp Cage(C,k0+1) c= RightComp Cage(C,k1) by A130,XBOOLE_1:1;
    then
A131: Cl RightComp Cage(C,k0+1) c= Cl RightComp Cage(C,k1) by PRE_TOPC:49;
A132: E.k0 in Upper_Arc L~Cage(C,k0+1) by A35;
A133: 1+0 <= k0+1 by NAT_1:11;
A134: E.k0 in Upper_Arc L~Cage(C,k0+1) by A35;
A135: X-SpanStart(C,k0+1) = Center Gauge(C,k0+1) by JORDAN1B:17;
    reconsider DD = LSeg(f,E.k0) /\ Lower_Arc L~Cage(C,k0+1) as compact Subset
    of TOP-REAL 2;
A136: proj2.(E.k0) = (E.k0)`2 by PSCOMP_1:def 29;
    reconsider D = proj2.:DD as compact Subset of REAL by JCT_MISC:24;
A137: Fs.k0 = upper_bound D by A32;
    DD c= the carrier of TOP-REAL 2;
    then DD c= dom proj2 by FUNCT_2:def 1;
    then
A138: dom proj2 /\ DD = DD by XBOOLE_1:28;
A139: E.k0 = |[s,Es.k0]| by A12;
    then
A140: (E.k0)`1 = s by EUCLID:56;
    (E.k0)`2 = Es.k0 by A139,EUCLID:56
      .= lower_bound(proj2.:(LSeg(f,e) /\ Upper_Arc L~Cage(C,k0+1))) by A10;
    then ex j st 1 <= j & j <= width Gauge(C,k0+1) & E.k0 = Gauge (C,k0+1)
    *(X-SpanStart(C,k0+1),j) by A2,A1,A140,A135,JORDAN1F:13;
    then
A141: LSeg(f,E.k0) meets Lower_Arc L~Cage(C,k0+1) by A2,A135,A134,JORDAN1J:62;
    then DD <> {} by XBOOLE_0:def 7;
    then dom proj2 meets DD by A138,XBOOLE_0:def 7;
    then D <> {} by RELAT_1:151;
    then consider dd being Point of TOP-REAL 2 such that
A142: dd in DD and
A143: Fs.k0 = proj2.dd by A137,FUNCT_2:116,RCOMP_1:32;
A144: dd in LSeg(E.k0,f) by A142,XBOOLE_0:def 4;
    reconsider DD = LSeg(f,e) /\ Upper_Arc L~Cage(C,k0+1) as compact Subset of
    TOP-REAL 2;
    reconsider D = proj2.:DD as compact Subset of REAL by JCT_MISC:24;
    DD c= the carrier of TOP-REAL 2;
    then DD c= dom proj2 by FUNCT_2:def 1;
    then
A145: dom proj2 /\ DD = DD by XBOOLE_1:28;
    LSeg(Gauge(C,k0+1)*(Center Gauge(C,k0+1),1), Gauge(C,k0+1)*(Center
Gauge(C,k0+1),len Gauge(C,k0+1))) meets Upper_Arc L~Cage(C,k0+1) by JORDAN1B:34
    ;
    then LSeg(f,e) meets Upper_Arc L~Cage(C,k0+1) by A2,A133,JORDAN1A:65
,XBOOLE_1:63;
    then DD <> {} by XBOOLE_0:def 7;
    then dom proj2 meets DD by A145,XBOOLE_0:def 7;
    then
A146: D <> {} by RELAT_1:151;
A147: F.k0 = |[s,Fs.k0]| by A33;
    then
A148: (F.k0)`1 = s by EUCLID:56;
A149: (F.k0)`2 = Fs.k0 by A147,EUCLID:56
      .= dd`2 by A143,PSCOMP_1:def 29;
    E.k0 in Vertical_Line s by A140,JORDAN1A:17;
    then LSeg(E.k0,f) c= Vertical_Line s by A6,JORDAN1A:23;
    then dd`1 = (F.k0)`1 by A148,A144,JORDAN6:34;
    then
A150: F.k0 in LSeg(E.k0,f) by A144,A149,TOPREAL3:11;
    Es.k0 = lower_bound D by A10;
    then consider dd being Point of TOP-REAL 2 such that
A151: dd in DD and
A152: Es.k0 = proj2.dd by A146,FUNCT_2:116,RCOMP_1:32;
A153: dd in LSeg(f,e) by A151,XBOOLE_0:def 4;
A154: (E.k0)`2 = Es.k0 by A139,EUCLID:56
      .= dd`2 by A152,PSCOMP_1:def 29;
    then
A155: f`2 <= (E.k0)`2 by A7,A153,TOPREAL1:10;
    then
A156: (F.k0)`2 <= (E.k0)`2 by A144,A149,TOPREAL1:10;
    r in proj2.:LSeg(E.k0,F.k0) by A34,A126;
    then r in [.proj2.(F.k0),proj2.(E.k0).] by A136,A127,A156,SPRECT_1:61;
    then
A157: p in LSeg(E.k0,F.k0) by A140,A148,JORDAN1A:21;
A158: F.k0 in Lower_Arc L~Cage(C,k0+1) by A46;
A159: f in LSeg(f,e) by RLTOPSP1:69;
A160: E.k0 in LSeg(f,E.k0) by RLTOPSP1:69;
    then
A161: LSeg(E.k0,F.k0) c= LSeg(f,E.k0) by A150,TOPREAL1:12;
    for x being set holds x in LSeg(E.k0,F.k0) /\ Lower_Arc L~Cage(C,k0+
    1) iff x = F.k0
    proof
      let x be set;
      thus x in LSeg(E.k0,F.k0) /\ Lower_Arc L~Cage(C,k0+1) implies x = F.k0
      proof
        F.k0 = |[s,Fs.k0]| by A33;
        then
A162:   (F.k0)`2 = Fs.k0 by EUCLID:56
          .= upper_bound E0 by A32;
        assume
A163:   x in LSeg(E.k0,F.k0) /\ Lower_Arc L~Cage(C,k0+1);
        then reconsider p = x as Point of TOP-REAL 2;
A164:   p in LSeg(E.k0,F.k0) by A163,XBOOLE_0:def 4;
        then
A165:   p`2 >= (F.k0)`2 by A156,TOPREAL1:10;
        p in Lower_Arc L~Cage(C,k0+1) by A163,XBOOLE_0:def 4;
        then p in EE by A161,A164,XBOOLE_0:def 4;
        then proj2.p in E0 by FUNCT_2:43;
        then
A166:   p`2 in E0 by PSCOMP_1:def 29;
        E0 is bounded by RCOMP_1:28;
        then (F.k0)`2 >= p`2 by A162,A166,SEQ_4:def 4;
        then
A167:   p`2 = (F.k0)`2 by A165,XXREAL_0:1;
        p`1 = (F.k0)`1 by A140,A148,A164,GOBOARD7:5;
        hence thesis by A167,TOPREAL3:11;
      end;
      assume
A168: x = F.k0;
      then x in LSeg(E.k0,F.k0) by RLTOPSP1:69;
      hence thesis by A158,A168,XBOOLE_0:def 4;
    end;
    then
A169: LSeg(E.k0,F.k0) /\ Lower_Arc L~Cage(C,k0+1) = {F.k0} by TARSKI:def 1;
A170: for p being real number st p in W holds p <= (E.k0)`2
    proof
      let p be real number;
      assume p in W;
      then consider x being set such that
      x in dom proj2 and
A171: x in CC and
A172: p = proj2.x by FUNCT_1:def 12;
      reconsider x as Point of TOP-REAL 2 by A171;
      x in LSeg(f,E.k0) by A171,XBOOLE_0:def 4;
      then x`2 <= (E.k0)`2 by A155,TOPREAL1:10;
      hence thesis by A172,PSCOMP_1:def 29;
    end;
    CC <> {} by A141,XBOOLE_0:def 7;
    then dom proj2 meets CC by A129,XBOOLE_0:def 7;
    then W <> {} by RELAT_1:151;
    then
A173: upper_bound W <= (E.k0)`2 by A170,SEQ_4:62;
    dd`1 = (E.k0)`1 by A14,A140,A153,JORDAN6:34;
    then E.k0 in LSeg(f,e) by A153,A154,TOPREAL3:11;
    then LSeg(f,E.k0) c= LSeg(e,f) by A159,TOPREAL1:12;
    then
A174: LSeg(E.k0,F.k0) c= LSeg(e,f) by A150,A160,TOPREAL1:12;
    for x being set holds x in LSeg(E.k0,F.k0) /\ Upper_Arc L~Cage(C,k0+
    1) iff x = E.k0
    proof
      let x be set;
      thus x in LSeg(E.k0,F.k0) /\ Upper_Arc L~Cage(C,k0+1) implies x = E.k0
      proof
        E.k0 = |[s,Es.k0]| by A12;
        then
A175:   (E.k0)`2 = Es.k0 by EUCLID:56
          .= lower_bound D by A10;
        assume
A176:   x in LSeg(E.k0,F.k0) /\ Upper_Arc L~Cage(C,k0+1);
        then reconsider p = x as Point of TOP-REAL 2;
A177:   p in LSeg(E.k0,F.k0) by A176,XBOOLE_0:def 4;
        then
A178:   p`2 <= (E.k0)`2 by A156,TOPREAL1:10;
        p in Upper_Arc L~Cage(C,k0+1) by A176,XBOOLE_0:def 4;
        then p in DD by A174,A177,XBOOLE_0:def 4;
        then proj2.p in D by FUNCT_2:43;
        then
A179:   p`2 in D by PSCOMP_1:def 29;
        D is bounded by RCOMP_1:28;
        then (E.k0)`2 <= p`2 by A175,A179,SEQ_4:def 5;
        then
A180:   p`2 = (E.k0)`2 by A178,XXREAL_0:1;
        p`1 = (E.k0)`1 by A140,A148,A177,GOBOARD7:5;
        hence thesis by A180,TOPREAL3:11;
      end;
      assume
A181: x = E.k0;
      then x in LSeg(E.k0,F.k0) by RLTOPSP1:69;
      hence thesis by A132,A181,XBOOLE_0:def 4;
    end;
    then
A182: LSeg(E.k0,F.k0) /\ Upper_Arc L~Cage(C,k0+1) = {E.k0} by TARSKI:def 1;
    (E.k0)`2 = Es.k0 by A139,EUCLID:56
      .= lower_bound(proj2.:(LSeg(f,e) /\ Upper_Arc L~Cage(C,k0+1))) by A10;
    then consider j such that
A183: 1 <= j and
A184: j <= width Gauge(C,k0+1) and
A185: E.k0 = Gauge(C,k0+1)*(X-SpanStart(C,k0+1),j) by A2,A1,A140,A135,
JORDAN1F:13;
A186: (F.k0)`2 =Fs.k0 by A147,EUCLID:56
      .= upper_bound(proj2.:(LSeg(f,E.k0) /\ Lower_Arc L~Cage(C,k0+1)))by A32;
    then consider k such that
A187: 1 <= k and
A188: k <= width Gauge(C,k0+1) and
A189: F.k0 = Gauge(C,k0+1)*(X-SpanStart(C,k0+1),k) by A2,A148,A135,A183,A184
,A185,A132,JORDAN1I:30;
    1 <= Center Gauge(C,k0+1) by JORDAN1B:12;
    then k <= j by A135,A183,A185,A186,A188,A189,A128,A173,GOBOARD5:5;
    then LSeg(E.k0,F.k0) c= Cl RightComp Cage(C,k0+1) by A135,A183,A184,A185
,A187,A188,A189,A182,A169,Lm1;
    then p in Cl RightComp Cage(C,k0+1) by A157;
    then p in Cl RightComp Cage(C,k1) by A131;
    hence thesis by A124,GOBRD14:47;
  end;
  then
A190: p in meet BDD-Family C by SETFAM_1:def 1;
A191: p in LSeg(e,f) by A5,A13,A119,JORDAN1A:21;
A192: now
    assume p in C;
    then p in Lower_Arc C \/ Upper_Arc C by JORDAN6:65;
    then p in (Lower_Arc C \/ Upper_Arc C) /\ LSeg(e,f) by A191,XBOOLE_0:def 4;
    then p in AA \/ BB by XBOOLE_1:23;
    then proj2.p in proj2.:(AA \/ BB) by FUNCT_2:43;
    then r in proj2.:(AA \/ BB) by PSCOMP_1:128;
    hence contradiction by A120,RELAT_1:153;
  end;
  meet BDD-Family C = C \/ BDD C by JORDAN10:21;
  then p in BDD C by A192,A190,XBOOLE_0:def 3;
  then consider n,i,j such that
A193: 1 < i and
A194: i < len Gauge(C,n) and
A195: 1 < j and
A196: j < width Gauge(C,n) and
A197: p`1 <> (Gauge(C,n)*(i,j))`1 and
A198: p in cell(Gauge(C,n),i,j) and
A199: cell(Gauge(C,n),i,j) c= BDD C by JORDAN1C:35;
  take n,j;
  thus j < width Gauge(C,n) by A196;
A200: X-SpanStart(C,n) = Center Gauge(C,n) by JORDAN1B:17;
A201: len Gauge(C,n) = width Gauge(C,n) by JORDAN8:def 1;
A202: X-SpanStart(C,n) <= len Gauge(C,n) by JORDAN1H:58;
A203: 1 <= X-SpanStart(C,n) by JORDAN1H:58,XXREAL_0:2;
  n > 0 by A194,A196,A199,JORDAN1B:44;
  then (Gauge(C,n)*(X-SpanStart(C,n),j))`1 = s by A2,A5,A195,A196,A200,A9,A201,
JORDAN1A:57;
  hence thesis by A122,A193,A194,A195,A196,A197,A198,A199,A203,A202,JORDAN1B:23
;
end;

definition
  let C;
  func ApproxIndex C -> Element of NAT means
  :Def1:
  it
is_sufficiently_large_for C & for j st j is_sufficiently_large_for C holds j >=
  it;
  existence
  proof
    defpred P[Element of NAT] means $1 is_sufficiently_large_for C;
    set X = {i : P[i]};
A1: X is Subset of NAT from DOMAIN_1:sch 7;
    consider i such that
A2: i is_sufficiently_large_for C by Lm2;
    i in X by A2;
    then reconsider X as non empty Subset of NAT by A1;
    take min X;
    min X in X by XXREAL_2:def 7;
    then ex i st i = min X & i is_sufficiently_large_for C;
    hence min X is_sufficiently_large_for C;
    let j;
    assume j is_sufficiently_large_for C;
    then j in X;
    hence thesis by XXREAL_2:def 7;
  end;
  uniqueness
  proof
    let it1,it2 be Element of NAT such that
A3: it1 is_sufficiently_large_for C and
A4: for j st j is_sufficiently_large_for C holds j >= it1 and
A5: it2 is_sufficiently_large_for C and
A6: for j st j is_sufficiently_large_for C holds j >= it2;
A7: it2 <= it1 by A3,A6;
    it1 <= it2 by A4,A5;
    hence thesis by A7,XXREAL_0:1;
  end;
end;

theorem Th1:
  ApproxIndex C >= 1
proof
  ApproxIndex C is_sufficiently_large_for C by Def1;
  hence thesis by JORDAN1H:60;
end;

definition
  let C;
  func Y-InitStart C -> Element of NAT means
  :Def2:
  it < width Gauge(C,
ApproxIndex C) & cell(Gauge(C,ApproxIndex C),X-SpanStart(C,ApproxIndex C)-'1,it
  ) c= BDD C & for j st j < width Gauge(C,ApproxIndex C) & cell(Gauge(C,
  ApproxIndex C),X-SpanStart(C,ApproxIndex C)-'1,j) c= BDD C holds j >= it;
  existence
  proof
    set n = ApproxIndex C;
    defpred P[Element of NAT] means $1 < width Gauge(C,n) & cell(Gauge(C,n),
    X-SpanStart(C,n)-'1,$1) c= BDD C;
    set X = { i: P[i] };
A1: X is Subset of NAT from DOMAIN_1:sch 7;
    n is_sufficiently_large_for C by Def1;
    then consider j such that
A2: P[j] by JORDAN1H:def 3;
    j in X by A2;
    then reconsider X as non empty Subset of NAT by A1;
    take min X;
    min X in X by XXREAL_2:def 7;
    then ex i st i = min X & P[i];
    hence P[min X];
    let i;
    assume P[i];
    then i in X;
    hence thesis by XXREAL_2:def 7;
  end;
  uniqueness
  proof
    let it1,it2 be Element of NAT;
    assume that
A3: it1 < width Gauge(C,ApproxIndex C) and
A4: cell(Gauge(C,ApproxIndex C),X-SpanStart(C,ApproxIndex C)-'1,it1)
    c= BDD C and
A5: for j st j < width Gauge(C,ApproxIndex C) & cell(Gauge(C,
    ApproxIndex C),X-SpanStart(C,ApproxIndex C)-'1,j) c= BDD C holds j >= it1
    and
A6: it2 < width Gauge(C,ApproxIndex C) and
A7: cell(Gauge(C,ApproxIndex C),X-SpanStart(C,ApproxIndex C)-'1,it2)
    c= BDD C and
A8: for j st j < width Gauge(C,ApproxIndex C) & cell(Gauge(C,
    ApproxIndex C),X-SpanStart(C,ApproxIndex C)-'1,j) c= BDD C holds j >= it2;
A9: it2 <= it1 by A3,A4,A8;
    it1 <= it2 by A5,A6,A7;
    hence thesis by A9,XXREAL_0:1;
  end;
end;

theorem Th2:
  Y-InitStart C > 1
proof
  set m = ApproxIndex C;
A1: X-SpanStart(C,m)-'1 <= len Gauge(C,m) by JORDAN1H:59;
  assume
A2: Y-InitStart C <= 1;
  per cases by A2,NAT_1:26;
  suppose
A3: Y-InitStart C = 0;
A4: cell(Gauge(C,m),X-SpanStart(C,m)-'1,0) c= UBD C by A1,JORDAN1A:70;
    0 <= width Gauge(C,m);
    then
A5: cell(Gauge(C,m),X-SpanStart(C,m)-'1,0) is non empty by A1,JORDAN1A:45;
    cell(Gauge(C,m),X-SpanStart(C,m)-'1,0) c= BDD C by A3,Def2;
    hence contradiction by A5,A4,JORDAN2C:28,XBOOLE_1:68;
  end;
  suppose
    Y-InitStart C = 1;
    then
A6: cell(Gauge(C,m),X-SpanStart(C,m)-'1,1) c= BDD C by Def2;
    set i1 = X-SpanStart(C,m);
A7: i1-'1 <= i1 by NAT_D:44;
    i1 < len Gauge(C,m) by JORDAN1H:58;
    then
A8: i1-'1 < len Gauge(C,m) by A7,XXREAL_0:2;
    BDD C c= C` by JORDAN2C:29;
    then
A9: cell(Gauge(C,m),X-SpanStart(C,m)-'1,1) c= C` by A6,XBOOLE_1:1;
    UBD C is_outside_component_of C by JORDAN2C:76;
    then
A10: UBD C is_a_component_of C` by JORDAN2C:def 4;
A11: width Gauge(C,m) <> 0 by GOBOARD1:def 5;
    then
A12: 0+1 <= width Gauge(C,m) by NAT_1:14;
    then
A13: cell(Gauge(C,m),X-SpanStart(C,m)-'1,1) is non empty by A1,JORDAN1A:45;
    1 <= i1-'1 by JORDAN1H:59;
    then
    cell(Gauge(C,m),i1-'1,0) /\ cell(Gauge(C,m),i1-'1,0+1) = LSeg(Gauge(C
    ,m)*(i1-'1,0+1),Gauge(C,m)*(i1-'1+1,0+1)) by A11,A8,GOBOARD5:27;
    then
A14: cell(Gauge(C,m),i1-'1,0) meets cell(Gauge(C,m),i1-'1,0+1) by
XBOOLE_0:def 7;
    cell(Gauge(C,m),X-SpanStart(C,m)-'1,0) c= UBD C by A1,JORDAN1A:70;
    then cell(Gauge(C,m),X-SpanStart(C,m)-'1,1) c= UBD C by A12,A8,A14,A10,A9,
GOBOARD9:6,JORDAN1A:46;
    hence contradiction by A6,A13,JORDAN2C:28,XBOOLE_1:68;
  end;
end;

theorem Th3:
  Y-InitStart C + 1 < width Gauge(C,ApproxIndex C)
proof
  set m = ApproxIndex C;
A1: X-SpanStart(C,m)-'1 <= len Gauge(C,m) by JORDAN1H:59;
  assume Y-InitStart C + 1 >= width Gauge(C,m);
  then
A2: Y-InitStart C + 1 > width Gauge(C,m) or Y-InitStart C + 1 = width Gauge(
  C,m) by XXREAL_0:1;
A3: Y-InitStart C < width Gauge(C,m) or Y-InitStart C = width Gauge(C,m) by
Def2;
  per cases by A2,A3,NAT_1:13;
  suppose
    Y-InitStart C = width Gauge(C,m);
    hence contradiction by Def2;
  end;
  suppose
    Y-InitStart C + 1 = width Gauge(C,m);
    then Y-InitStart C = width Gauge(C,m)-'1 by NAT_D:34;
    then
A4: cell(Gauge(C,m),X-SpanStart(C,m)-'1,width Gauge(C,m)-'1) c= BDD C by Def2;
    BDD C c= C` by JORDAN2C:29;
    then
A5: cell(Gauge(C,m),X-SpanStart(C,m)-'1,width Gauge(C,m)-'1) c= C` by A4,
XBOOLE_1:1;
A6: cell(Gauge(C,m),X-SpanStart(C,m)-'1,width Gauge(C,m)) c= UBD C by A1,
JORDAN1A:71;
    set i1 = X-SpanStart(C,m);
A7: i1-'1 <= i1 by NAT_D:44;
    i1 < len Gauge(C,m) by JORDAN1H:58;
    then
A8: i1-'1 < len Gauge(C,m) by A7,XXREAL_0:2;
    UBD C is_outside_component_of C by JORDAN2C:76;
    then
A9: UBD C is_a_component_of C` by JORDAN2C:def 4;
    width Gauge(C,m)-'1 <= width Gauge(C,m) by NAT_D:44;
    then
A10: cell(Gauge(C,m),X-SpanStart(C,m)-'1,width Gauge(C,m)-'1) is non empty
    by A1,JORDAN1A:45;
A11: width Gauge(C,m)-1 < width Gauge(C,m) by XREAL_1:148;
A12: 1 <= i1-'1 by JORDAN1H:59;
A13: width Gauge(C,m) <> 0 by GOBOARD1:def 5;
    then width Gauge(C,m)-'1+1 = width Gauge(C,m) by NAT_1:14,XREAL_1:237;
    then cell(Gauge(C,m),i1-'1,width Gauge(C,m)) /\ cell(Gauge(C,m),i1-'1,
width Gauge(C,m)-'1) = LSeg(Gauge(C,m)*(i1-'1,width Gauge(C,m)), Gauge(C,m)*(i1
    -'1+1,width Gauge(C,m))) by A8,A11,A12,GOBOARD5:27;
    then
A14: cell(Gauge(C,m),i1-'1,width Gauge(C,m)) meets cell(Gauge(C,m),i1-'1,
    width Gauge(C,m)-'1) by XBOOLE_0:def 7;
    width Gauge(C,m)-'1 < width Gauge(C,m) by A13,A11,NAT_1:14,XREAL_1:235;
    then cell(Gauge(C,m),X-SpanStart(C,m)-'1,width Gauge(C,m)-'1) c= UBD C by
A6,A8,A14,A9,A5,GOBOARD9:6,JORDAN1A:46;
    hence contradiction by A4,A10,JORDAN2C:28,XBOOLE_1:68;
  end;
end;

definition
  let C,n;
  assume n is_sufficiently_large_for C;
  then
A1: n >= ApproxIndex C by Def1;
  set i1 = X-SpanStart(C,n);
  func Y-SpanStart(C,n) -> Element of NAT means
  :Def3:
  it <= width Gauge(C,n)
  & (for k st it <= k & k <= 2|^(n-'ApproxIndex C)*(Y-InitStart C-'2)+2 holds
cell(Gauge(C,n),X-SpanStart(C,n)-'1,k) c= BDD C) & for j st j <= width Gauge(C,
  n) & for k st j <= k & k <= 2|^(n-'ApproxIndex C)*(Y-InitStart C-'2)+2 holds
  cell(Gauge(C,n),X-SpanStart(C,n)-'1,k) c= BDD C holds j >= it;
  existence
  proof
    set m = ApproxIndex C;
    defpred P[Element of NAT] means $1 <= width Gauge(C,n) & for k st $1 <= k
& k <= 2|^(n-'m)*(Y-InitStart C-'2)+2 holds cell(Gauge(C,n),i1-'1,k) c= BDD C;
    set X = { i: P[i]};
    set j0 = 2|^(n-'m)*(Y-InitStart C-'2)+2;
A2: X is Subset of NAT from DOMAIN_1:sch 7;
A3: Y-InitStart C+1 < width Gauge(C,m) by Th3;
    then Y-InitStart C + 1 < 2|^m + (2+1) by JORDAN1A:49;
    then Y-InitStart C + 1 < 2|^m + 2 + 1;
    then
A4: Y-InitStart C < 2|^m + 2 by XREAL_1:8;
A5: 1 < Y-InitStart C by Th2;
    then 1+1 <= Y-InitStart C by NAT_1:13;
    then Y-InitStart C-'2 < 2|^m by A4,NAT_D:54;
    then 2|^(n-'m)*(Y-InitStart C-'2) <= 2|^(n-'m)*(2|^m) by XREAL_1:66;
    then 2|^(n-'m)*(Y-InitStart C-'2) <= 2|^((n-'m)+m) by NEWTON:13;
    then
A6: 2|^(n-'m)*(Y-InitStart C-'2) <= 2|^n by A1,XREAL_1:237;
A7: now
      2|^(m-'1) <= 2|^m by NAT_D:35,PREPOWER:107;
      then
A8:   2|^(m-'1) + 2 <= 2|^m + 2 by XREAL_1:8;
      len Gauge(C,m) = 2|^m + (2 + 1) by JORDAN8:def 1
        .= 2|^m + 2 + 1;
      then
A9:   X-SpanStart(C,m) < len Gauge(C,m) by A8,NAT_1:13;
A10:  Y-InitStart C >= 1+1 by A5,NAT_1:13;
      let j;
      assume that
A11:  j0 <= j and
A12:  j <= 2|^(n-'m)*(Y-InitStart C-'2)+2;
A13:  m >= 1 by Th1;
      1 <= 2|^(m-'1) by PREPOWER:19;
      then
A14:  2+1 <= X-SpanStart(C,m) by XREAL_1:8;
A15:  cell(Gauge(C,m),X-SpanStart(C,m)-'1,Y-InitStart C) c= BDD C by Def2;
      j = j0 by A11,A12,XXREAL_0:1;
      then
A16:  j = 2|^(n-'m)*(Y-InitStart C-2)+2 by A10,XREAL_1:235;
      (n-'m)+(m-'1) = (n-'m)+(m-1) by Th1,XREAL_1:235
        .= (n-'m)+ m-1
        .= n-1 by A1,XREAL_1:237
        .= n-'1 by A1,A13,XREAL_1:235,XXREAL_0:2;
      then i1 = 2|^(n-'m)*(X-SpanStart(C,m)-2)+2 by NEWTON:13;
      then cell(Gauge(C,n),i1-'1,j) c= cell(Gauge(C,m),X-SpanStart(C,m)-'1,
      Y-InitStart C) by A1,A3,A14,A9,A16,Th2,JORDAN1A:69;
      hence cell(Gauge(C,n),i1-'1,j) c= BDD C by A15,XBOOLE_1:1;
    end;
    2|^n <= 2|^n + 1 by NAT_1:11;
    then 2|^(n-'m)*(Y-InitStart C-'2) <= 2|^n + 1 by A6,XXREAL_0:2;
    then j0 <= 2|^n + 1+2 by XREAL_1:8;
    then j0 <= 2|^n + (1+2);
    then j0 <= len Gauge(C,n) by JORDAN8:def 1;
    then j0 <= width Gauge(C,n) by JORDAN8:def 1;
    then j0 in X by A7;
    then reconsider X as non empty Subset of NAT by A2;
    take min X;
    min X in X by XXREAL_2:def 7;
    then ex j st j = min X & P[j];
    hence P[min X];
    let j;
    assume P[j];
    then j in X;
    hence thesis by XXREAL_2:def 7;
  end;
  uniqueness
  proof
    let it1,it2 be Element of NAT;
    defpred definiens[Element of NAT] means $1 <= width Gauge(C,n) & (for i st
$1 <= i & i <= 2|^(n-'ApproxIndex C)*(Y-InitStart C-'2)+2 holds cell(Gauge(C,n)
,X-SpanStart(C,n)-'1,i) c= BDD C) & for j st j <= width Gauge(C,n) & for i st j
<= i & i <= 2|^(n-'ApproxIndex C)*(Y-InitStart C-'2)+2 holds cell(Gauge(C,n),
    X-SpanStart(C,n)-'1,i) c= BDD C holds j >= $1;
    assume that
A17: definiens[it1] and
A18: definiens[it2];
A19: it2 <= it1 by A17,A18;
    it1 <= it2 by A17,A18;
    hence thesis by A19,XXREAL_0:1;
  end;
end;

theorem Th4:
  n is_sufficiently_large_for C implies X-SpanStart(C,n) = 2|^(n-'
  ApproxIndex C)*(X-SpanStart(C,ApproxIndex C)-2)+2
proof
  set m = ApproxIndex C;
A1: m >= 1 by Th1;
  assume n is_sufficiently_large_for C;
  then
A2: n >=ApproxIndex C by Def1;
  (n-'m)+(m-'1) = (n-'m)+(m-1) by Th1,XREAL_1:235
    .= (n-'m)+ m-1
    .= n-1 by A2,XREAL_1:237
    .= n-'1 by A2,A1,XREAL_1:235,XXREAL_0:2;
  hence thesis by NEWTON:13;
end;

theorem Th5:
  n is_sufficiently_large_for C implies Y-SpanStart(C,n) <= 2|^(n-'
  ApproxIndex C)*(Y-InitStart C-'2)+2
proof
  set m = ApproxIndex C;
A1: X-SpanStart(C,m) > 2 by JORDAN1H:58;
  set j0 = 2|^(n-'m)*(Y-InitStart C-'2)+2;
  set i1 = X-SpanStart(C,n);
  assume
A2: n is_sufficiently_large_for C;
  then
A3: n >=ApproxIndex C by Def1;
A4: 1 < Y-InitStart C by Th2;
  then 1+1 <= Y-InitStart C by NAT_1:13;
  then
A5: Y-InitStart C-'2 = Y-InitStart C-2 by XREAL_1:235;
A6: Y-InitStart C+1 < width Gauge(C,m) by Th3;
A7: now
    m-'1 <= m by NAT_D:44;
    then
A8: 2|^(m-'1) <= 2|^m by PREPOWER:107;
    len Gauge(C,m) = 2|^m + 3 by JORDAN8:def 1;
    then
A9: X-SpanStart(C,m) < len Gauge(C,m) by A8,XREAL_1:10;
A10: 2+1 <= X-SpanStart(C,m) by A1,NAT_1:13;
A11: i1 = 2|^(n-'m)*(X-SpanStart(C,m)-2)+2 by A2,Th4;
    let j;
    assume that
A12: j0 <= j and
A13: j <= 2|^(n-'m)*(Y-InitStart C-'2)+2;
A14: cell(Gauge(C,m),X-SpanStart(C,m)-'1,Y-InitStart C) c= BDD C by Def2;
    j = j0 by A12,A13,XXREAL_0:1;
    then cell(Gauge(C,n),i1-'1,j) c= cell(Gauge(C,m),X-SpanStart(C,m)-'1,
    Y-InitStart C) by A3,A6,A5,A10,A9,A11,Th2,JORDAN1A:69;
    hence cell(Gauge(C,n),i1-'1,j) c= BDD C by A14,XBOOLE_1:1;
  end;
  Y-InitStart C < Y-InitStart C +1 by XREAL_1:31;
  then Y-InitStart C < width Gauge(C,m) by Th3,XXREAL_0:2;
  then j0 <= width Gauge(C,n) by A3,A4,A5,JORDAN1A:53;
  hence thesis by A2,A7,Def3;
end;

theorem Th6:
  n is_sufficiently_large_for C implies cell(Gauge(C,n),X-SpanStart
  (C,n)-'1,Y-SpanStart(C,n)) c= BDD C
proof
  assume
A1: n is_sufficiently_large_for C;
  then Y-SpanStart(C,n) <= 2|^(n-'ApproxIndex C)*(Y-InitStart C-'2)+2 by Th5;
  hence thesis by A1,Def3;
end;

theorem Th7:
  n is_sufficiently_large_for C implies 1 < Y-SpanStart(C,n) &
  Y-SpanStart(C,n) <= width Gauge(C,n)
proof
  assume
A1: n is_sufficiently_large_for C;
  thus 1 < Y-SpanStart(C,n)
  proof
A2: X-SpanStart(C,n)-'1 <= len Gauge(C,n) by JORDAN1H:59;
    assume
A3: Y-SpanStart(C,n) <= 1;
    per cases by A3,NAT_1:26;
    suppose
A4:   Y-SpanStart(C,n) = 0;
A5:   cell(Gauge(C,n),X-SpanStart(C,n)-'1,0) c= UBD C by A2,JORDAN1A:70;
      0 <= width Gauge(C,n);
      then
A6:   cell(Gauge(C,n),X-SpanStart(C,n)-'1,0) is non empty by A2,JORDAN1A:45;
      cell(Gauge(C,n),X-SpanStart(C,n)-'1,0) c= BDD C by A1,A4,Th6;
      hence contradiction by A6,A5,JORDAN2C:28,XBOOLE_1:68;
    end;
    suppose
      Y-SpanStart(C,n) = 1;
      then
A7:   cell(Gauge(C,n),X-SpanStart(C,n)-'1,1) c= BDD C by A1,Th6;
      set i1 = X-SpanStart(C,n);
A8:   i1-'1 <= i1 by NAT_D:44;
      i1 < len Gauge(C,n) by JORDAN1H:58;
      then
A9:   i1-'1 < len Gauge(C,n) by A8,XXREAL_0:2;
      BDD C c= C` by JORDAN2C:29;
      then
A10:  cell(Gauge(C,n),X-SpanStart(C,n)-'1,1) c= C` by A7,XBOOLE_1:1;
      UBD C is_outside_component_of C by JORDAN2C:76;
      then
A11:  UBD C is_a_component_of C` by JORDAN2C:def 4;
A12:  width Gauge(C,n) <> 0 by GOBOARD1:def 5;
      then
A13:  0+1 <= width Gauge(C,n) by NAT_1:14;
      then
A14:  cell(Gauge(C,n),X-SpanStart(C,n)-'1,1) is non empty by A2,JORDAN1A:45;
      1 <= i1-'1 by JORDAN1H:59;
      then
      cell(Gauge(C,n),i1-'1,0) /\ cell(Gauge(C,n),i1-'1,0+1) = LSeg(Gauge
      (C,n)*(i1-'1,0+1),Gauge(C,n)*(i1-'1+1,0+1)) by A12,A9,GOBOARD5:27;
      then
A15:  cell(Gauge(C,n),i1-'1,0) meets cell(Gauge(C,n),i1-'1,0+1) by
XBOOLE_0:def 7;
      cell(Gauge(C,n),X-SpanStart(C,n)-'1,0) c= UBD C by A2,JORDAN1A:70;
      then cell(Gauge(C,n),X-SpanStart(C,n)-'1,1) c= UBD C by A13,A9,A15,A11
,A10,GOBOARD9:6,JORDAN1A:46;
      hence contradiction by A7,A14,JORDAN2C:28,XBOOLE_1:68;
    end;
  end;
  thus thesis by A1,Def3;
end;

theorem
  n is_sufficiently_large_for C implies [X-SpanStart(C,n),Y-SpanStart(C,
  n)] in Indices Gauge(C,n)
proof
A1: X-SpanStart(C,n) < len Gauge(C,n) by JORDAN1H:58;
  1+1 < X-SpanStart(C,n) by JORDAN1H:58;
  then
A2: 1 < X-SpanStart(C,n) by NAT_1:13;
  assume
A3: n is_sufficiently_large_for C;
  then
A4: Y-SpanStart(C,n) <= width Gauge(C,n) by Th7;
  1 < Y-SpanStart(C,n) by A3,Th7;
  hence thesis by A2,A1,A4,MATRIX_1:37;
end;

theorem
  n is_sufficiently_large_for C implies [X-SpanStart(C,n)-'1,Y-SpanStart
  (C,n)] in Indices Gauge(C,n)
proof
A1: 1 <= X-SpanStart(C,n)-'1 by JORDAN1H:59;
A2: X-SpanStart(C,n)-'1 < len Gauge(C,n) by JORDAN1H:59;
  assume
A3: n is_sufficiently_large_for C;
  then
A4: Y-SpanStart(C,n) <= width Gauge(C,n) by Th7;
  1 < Y-SpanStart(C,n) by A3,Th7;
  hence thesis by A1,A2,A4,MATRIX_1:37;
end;

theorem
  n is_sufficiently_large_for C implies cell(Gauge(C,n),X-SpanStart(C,n)
  -'1,Y-SpanStart(C,n)-'1) meets C
proof
  set i1 = X-SpanStart(C,n);
A1: Y-SpanStart(C,n)-1 < Y-SpanStart(C,n) by XREAL_1:148;
  assume
A2: n is_sufficiently_large_for C;
  then
A3: 1 < Y-SpanStart(C,n) by Th7;
  assume
A4: cell(Gauge(C,n),X-SpanStart(C,n)-'1,Y-SpanStart(C,n)-'1) misses C;
A5: for k st Y-SpanStart(C,n)-'1 <= k & k <= 2|^(n-'ApproxIndex C)*(
  Y-InitStart C-'2)+2 holds cell(Gauge(C,n),i1-'1,k) c= BDD C
  proof
    let k such that
A6: Y-SpanStart(C,n)-'1 <= k and
A7: k <= 2|^(n-'ApproxIndex C)*(Y-InitStart C-'2)+2;
    per cases by A6,XXREAL_0:1;
    suppose
A8:   Y-SpanStart(C,n)-'1 = k;
      1 < Y-SpanStart(C,n) by A2,Th7;
      then
A9:   k+1 = Y-SpanStart(C,n) by A8,XREAL_1:237;
A10:  cell(Gauge(C,n),i1-'1,k) c= C` by A4,A8,SUBSET_1:43;
A11:  k < k+1 by XREAL_1:31;
      Y-SpanStart(C,n) <= width Gauge(C,n) by A2,Th7;
      then
A12:  k < width Gauge(C,n) by A9,A11,XXREAL_0:2;
      set W = {B where B is Subset of TOP-REAL 2: B is_inside_component_of C};
A13:  i1-'1 <= i1 by NAT_D:44;
      i1 < len Gauge(C,n) by JORDAN1H:58;
      then
A14:  i1-'1 < len Gauge(C,n) by A13,XXREAL_0:2;
A15:  BDD C = union W by JORDAN2C:def 5;
      1+1 < X-SpanStart(C,n) by JORDAN1H:58;
      then 1 <= i1-1 by XREAL_1:21;
      then 1 <= i1-'1 by NAT_D:39;
      then
      cell(Gauge(C,n),i1-'1,k) /\ cell(Gauge(C,n),i1-'1,k+1) = LSeg(Gauge
      (C,n)*(i1-'1,k+1),Gauge(C,n)*(i1-'1+1,k+1)) by A14,A12,GOBOARD5:27;
      then cell(Gauge(C,n),i1-'1,k) meets cell(Gauge(C,n),i1-'1,k+1) by
XBOOLE_0:def 7;
      then cell(Gauge(C,n),i1-'1,k) meets BDD C by A2,A9,Th6,XBOOLE_1:63;
      then consider e being set such that
A16:  e in W and
A17:  cell(Gauge(C,n),i1-'1,k) meets e by A15,ZFMISC_1:98;
      consider B being Subset of TOP-REAL 2 such that
A18:  e = B and
A19:  B is_inside_component_of C by A16;
A20:  B c= BDD C by A15,A16,A18,ZFMISC_1:92;
      B is_a_component_of C` by A19,JORDAN2C:def 3;
      then cell(Gauge(C,n),i1-'1,k) c= B by A10,A14,A12,A17,A18,GOBOARD9:6
,JORDAN1A:46;
      hence thesis by A20,XBOOLE_1:1;
    end;
    suppose
      Y-SpanStart(C,n)-'1 < k;
      then Y-SpanStart(C,n) < k+1 by NAT_D:55;
      then Y-SpanStart(C,n) <= k by NAT_1:13;
      hence thesis by A2,A7,Def3;
    end;
  end;
  Y-SpanStart(C,n) <= width Gauge(C,n) by A2,Def3;
  then Y-SpanStart(C,n)-'1 <= width Gauge(C,n) by NAT_D:44;
  then Y-SpanStart(C,n)-'1 >=Y-SpanStart(C,n) by A2,A5,Def3;
  hence contradiction by A3,A1,XREAL_1:235;
end;

theorem
  n is_sufficiently_large_for C implies cell(Gauge(C,n),X-SpanStart(C,n)
  -'1,Y-SpanStart(C,n)) misses C
proof
  assume n is_sufficiently_large_for C;
  then cell(Gauge(C,n),X-SpanStart(C,n)-'1,Y-SpanStart(C,n)) c= BDD C by Th6;
  hence thesis by JORDAN1A:15,XBOOLE_1:63;
end;
 
:: Moved from JORDAN1K, AK 20.02.2006

begin :: BDD & UBD

reserve p,q for Point of TOP-REAL 2,
  D for Simple_closed_curve;

theorem Th12:
  UBD C meets UBD D
proof
  reconsider A = C as bounded Subset of Euclid 2 by JORDAN2C:def 2;
  consider r1 being Real, x1 being Point of Euclid 2 such that
  0 < r1 and
A1: A c= Ball(x1,r1) by METRIC_6:def 10;
  reconsider B = D as bounded Subset of Euclid 2 by JORDAN2C:def 2;
  consider r2 being Real, x2 being Point of Euclid 2 such that
  0 < r2 and
A2: B c= Ball(x2,r2) by METRIC_6:def 10;
  reconsider C9 = Ball(x1,r1)`, D9 = Ball(x2,r2)` as connected Subset of
  TOP-REAL 2 by JORDAN1K:37;
  consider x3 being Point of Euclid 2, r3 being Real such that
A3: Ball(x1,r1) \/ Ball(x2,r2) c= Ball(x3,r3) by WEIERSTR:1;
A4: now
    assume D9 is Bounded;
    then D9 is bounded Subset of Euclid 2 by JORDAN2C:def 2;
    hence contradiction by JORDAN1K:8;
  end;
A5: now
    assume C9 is Bounded;
    then C9 is bounded Subset of Euclid 2 by JORDAN2C:def 2;
    hence contradiction by JORDAN1K:8;
  end;
  Ball(x3,r3)` c= (Ball(x1,r1) \/ Ball(x2,r2))` by A3,SUBSET_1:31;
  then
A6: Ball(x3,r3)` c= Ball(x1,r1)` /\ Ball(x2,r2)` by XBOOLE_1:53;
  then
A7: Ball(x3,r3)` c= Ball(x1,r1)` by XBOOLE_1:18;
A8: Ball(x3,r3)` c= Ball(x2,r2)` by A6,XBOOLE_1:18;
  Ball(x2,r2)` c= B` by A2,SUBSET_1:31;
  then Ball(x2,r2)` misses B by SUBSET_1:43;
  then D9 c= UBD D by A4,JORDAN2C:133;
  then
A9: Ball(x3,r3)` c= UBD D by A8,XBOOLE_1:1;
  Ball(x1,r1)` c= A` by A1,SUBSET_1:31;
  then Ball(x1,r1)` misses A by SUBSET_1:43;
  then C9 c= UBD C by A5,JORDAN2C:133;
  then
A10: Ball(x3,r3)` c= UBD C by A7,XBOOLE_1:1;
  Ball(x3,r3)` <> {}Euclid 2 by JORDAN1K:8;
  hence thesis by A10,A9,XBOOLE_1:68;
end;

theorem Th13:
  q in UBD C & p in BDD C implies dist(q,C) < dist(q,p)
proof
  assume that
A1: q in UBD C and
A2: p in BDD C and
A3: dist(q,C) >= dist(q,p);
A4: UBD C is_a_component_of C` by JORDAN2C:132;
  now
    assume LSeg(p,q) meets C;
    then consider x being set such that
A5: x in LSeg(p,q) and
A6: x in C by XBOOLE_0:3;
    reconsider x as Point of TOP-REAL 2 by A5;
A7: dist(q,C) <= dist(q,x) by A6,JORDAN1K:50;
    C misses BDD C by JORDAN1A:15;
    then x <> p by A2,A6,XBOOLE_0:3;
    hence contradiction by A3,A5,A7,JORDAN1K:30,XXREAL_0:2;
  end;
  then
A8: LSeg(p,q) c= C` by SUBSET_1:43;
  q in LSeg(p,q) by RLTOPSP1:69;
  then
A9: LSeg(p,q) meets UBD C by A1,XBOOLE_0:3;
A10: BDD C = union{B where B is Subset of TOP-REAL 2: B
  is_inside_component_of C} by JORDAN2C:def 5;
  then consider X being set such that
A11: p in X and
A12: X in {B where B is Subset of TOP-REAL 2: B is_inside_component_of C
  } by A2,TARSKI:def 4;
  consider B being Subset of TOP-REAL 2 such that
A13: X = B and
A14: B is_inside_component_of C by A12;
  B c= BDD C by A10,A12,A13,ZFMISC_1:92;
  then
A15: B misses UBD C by JORDAN2C:28,XBOOLE_1:63;
  p in LSeg(p,q) by RLTOPSP1:69;
  then
A16: LSeg(p,q) meets B by A11,A13,XBOOLE_0:3;
  B is_a_component_of C` by A14,JORDAN2C:def 3;
  then UBD C = B by A8,A4,A9,A16,JORDAN9:3;
  hence contradiction by A15,XBOOLE_1:66;
end;

theorem Th14:
  not p in BDD C implies dist(p,C) <= dist(p,BDD C)
proof
  per cases;
  suppose
    p in C;
    then dist(p,C) = 0 by JORDAN1K:45;
    hence thesis by JORDAN1K:44;
  end;
  suppose
A1: not p in C;
    assume that
A2: not p in BDD C and
A3: dist(p,C) > dist(p,BDD C);
A4: ex q st q in BDD C & dist(p,q) < dist(p,C) by A3,JORDAN1K:48;
    p in C` by A1,SUBSET_1:50;
    then p in (BDD C) \/ (UBD C) by JORDAN2C:31;
    then p in UBD C by A2,XBOOLE_0:def 3;
    hence contradiction by A4,Th13;
  end;
end;

theorem Th15:
  not(C c= BDD D & D c= BDD C)
proof
  assume that
A1: C c= BDD D and
A2: D c= BDD C;
  UBD C meets UBD D by Th12;
  then consider e being set such that
A3: e in UBD C and
A4: e in UBD D by XBOOLE_0:3;
  reconsider p = e as Point of TOP-REAL 2 by A3;
  UBD D misses BDD D by JORDAN2C:28;
  then
A5: not p in BDD D by A4,XBOOLE_0:3;
  UBD C misses BDD C by JORDAN2C:28;
  then
A6: not p in BDD C by A3,XBOOLE_0:3;
  then dist(p,C) <= dist(p, BDD C) by Th14;
  then dist(p, BDD D) < dist(p, BDD C) by A1,A5,JORDAN1K:51,XXREAL_0:2;
  then dist(p, BDD D) < dist(p,D) by A2,A6,JORDAN1K:51,XXREAL_0:2;
  hence contradiction by A5,Th14;
end;

theorem Th16:
  C c= BDD D implies D c= UBD C
proof
  assume
A1: C c= BDD D;
  assume
A2: not D c= UBD C;
  C misses D by A1,JORDAN1A:15,XBOOLE_1:63;
  then D c= BDD C by A2,JORDAN1K:19;
  hence contradiction by A1,Th15;
end;

theorem
  L~Cage(C,n) c= UBD C
proof
  C c= BDD L~Cage(C,n) by JORDAN10:12;
  hence thesis by Th16;
end;
