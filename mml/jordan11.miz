:: Preparing the Internal Approximations of Simple Closed Curves
::  by Andrzej Trybulec
::
:: Received May 21, 2002
:: Copyright (c) 2002 Association of Mizar Users

environ

 vocabularies JORDAN1A, JORDAN11, JORDAN8, EUCLID, GROUP_1, FINSEQ_1, ARYTM_1,
      SEQ_1, BOOLE, GOBOARD5, SETFAM_1, SQUARE_1, FUNCT_1, PSCOMP_1, RCOMP_1,
      RELAT_2, JORDAN2C, JORDAN6, JORDAN9, PRE_TOPC, RELAT_1, TARSKI, ORDINAL2,
      CONNSP_1, COMPTS_1, MCART_1, TOPREAL1, TOPREAL2, GOBOARD9, ARYTM_3,
      SUBSET_1, LATTICES, NAT_1, INT_1, JORDAN10, SEQ_2, FUNCT_5, TREES_1,
      MATRIX_1, JORDAN1H, JORDAN1E, ARYTM, METRIC_1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, SETFAM_1, ORDINAL1, NUMBERS,
      XCMPLX_0, XXREAL_0, XREAL_0, REAL_1, INT_1, NAT_1, NAT_D, BINARITH,
      NEWTON, SEQ_4, RELSET_1, FUNCT_2, FINSEQ_1, MATRIX_1, SEQ_1, RCOMP_1,
      STRUCT_0, PRE_TOPC, METRIC_1, METRIC_6, CONNSP_1, COMPTS_1, EUCLID,
      TOPREAL1, TOPREAL2, GOBOARD5, GOBOARD9, PSCOMP_1, JORDAN2C, JORDAN6,
      JCT_MISC, JORDAN8, JORDAN9, JORDAN10, JORDAN1A, JORDAN1E, JORDAN1H,
      TOPREAL6, JORDAN1K;
 constructors SETFAM_1, REAL_1, NAT_1, NAT_D, RCOMP_1, NEWTON, BINARITH,
      TOPS_1, CONNSP_1, TBSP_1, JORDAN1, PSCOMP_1, GOBOARD9, JORDAN5C, JORDAN6,
      JORDAN2C, TOPREAL6, JCT_MISC, JORDAN1K, JORDAN8, JORDAN9, JORDAN10,
      JORDAN1A, JORDAN1E, JORDAN1H;
 registrations XBOOLE_0, SUBSET_1, ORDINAL1, RELSET_1, NUMBERS, XXREAL_0,
      XREAL_0, NAT_1, INT_1, MEMBERED, STRUCT_0, COMPTS_1, TBSP_1, EUCLID,
      TOPREAL1, TOPREAL2, SPPOL_2, PSCOMP_1, TOPREAL5, SPRECT_2, JORDAN6,
      JORDAN2C, TOPREAL6, JORDAN8, JORDAN10;
 requirements NUMERALS, REAL, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, XBOOLE_0, JORDAN1H, PSCOMP_1, SUBSET_1;
 theorems SEQ_4, JORDAN8, NEWTON, NAT_1, GOBOARD7, GOBOARD5, XBOOLE_0,
      XBOOLE_1, JORDAN2C, SUBSET_1, GOBOARD9, ZFMISC_1, EUCLID, GOBOARD1,
      BINARITH, JORDAN6, PSCOMP_1, SPRECT_1, TARSKI, TOPREAL3, SETFAM_1,
      RCOMP_1, RELAT_1, FUNCT_2, TOPREAL1, PRE_TOPC, COMPTS_1, JORDAN1A,
      ENUMSET1, FUNCT_1, XREAL_0, JCT_MISC, JORDAN10, GOBRD14, JORDAN1B,
      JORDAN1C, JORDAN1F, JORDAN1G, JORDAN1H, JORDAN1I, JORDAN1J, PREPOWER,
      XREAL_1, JORDAN1K, METRIC_6, WEIERSTR, JORDAN9, TBSP_1, XXREAL_0,
      JORDAN1, INT_1;
 schemes DOMAIN_1, FUNCT_2;

begin

reserve i,j,k,n for Element of NAT,
        C for being_simple_closed_curve Subset of TOP-REAL 2;

Lm1:
 i > 0 &
 1 <= j & j <= width Gauge(C,i) & k <= j &
 1 <= k & k <= width Gauge(C,i) &
 LSeg(Gauge(C,i)*(Center Gauge(C,i),j),Gauge(C,i)*(Center Gauge(C,i),k))
    /\ Upper_Arc L~Cage(C,i) = {Gauge(C,i)*(Center Gauge(C,i),j)} &
 LSeg(Gauge(C,i)*(Center Gauge(C,i),j),Gauge(C,i)*(Center Gauge(C,i),k))
    /\ Lower_Arc L~Cage(C,i) = {Gauge(C,i)*(Center Gauge(C,i),k)}
 implies
 LSeg(Gauge(C,i)*(Center Gauge(C,i),j),Gauge(C,i)*(Center Gauge(C,i),k))
       c= Cl RightComp Cage(C,i)
proof assume that
A1: i > 0 and
A2: 1 <= j and
A3: j <= width Gauge(C,i) and
A4: k <= j and
A5: 1 <= k and
A6: k <= width Gauge(C,i) and
A7: LSeg(Gauge(C,i)*(Center Gauge(C,i),j),Gauge(C,i)*(Center Gauge(C,i),k))
    /\ Upper_Arc L~Cage(C,i) = {Gauge(C,i)*(Center Gauge(C,i),j)} and
A8: LSeg(Gauge(C,i)*(Center Gauge(C,i),j),Gauge(C,i)*(Center Gauge(C,i),k))
    /\ Lower_Arc L~Cage(C,i) = {Gauge(C,i)*(Center Gauge(C,i),k)};
  set p = Gauge(C,i)*(Center Gauge(C,i),j),
      q = Gauge(C,i)*(Center Gauge(C,i),k),
      S = RightComp Cage(C,i);
       X-SpanStart(C,i) = Center Gauge(C,i) by JORDAN1B:17;
   then 2 <= Center Gauge(C,i) & Center Gauge(C,i) < len Gauge(C,i)
       by JORDAN1H:58;
   then A9: 1 < Center Gauge(C,i) & Center Gauge(C,i) < len Gauge(C,i)
       by XXREAL_0:2;
        q in {q} by TARSKI:def 1;
    then q in Lower_Arc L~Cage(C,i) by A8,XBOOLE_0:def 3;
    then A10: q in L~Lower_Seq(C,i) by A1,JORDAN1G:64;
        p in {p} by TARSKI:def 1;
    then p in Upper_Arc L~Cage(C,i) by A7,XBOOLE_0:def 3;
    then p in L~Upper_Seq(C,i) by A1,JORDAN1G:63;
    then A11:  j <> k by A2,A6,A9,A10,JORDAN1J:57;
A12: [Center Gauge(C,i),j] in Indices Gauge(C,i) by A2,A3,A9,GOBOARD7:10;
       [Center Gauge(C,i),k] in Indices Gauge(C,i)
       by A5,A6,A9,GOBOARD7:10;
   then A13: p <> q by A11,A12,JORDAN1H:32;
   set A = LSeg(p,q) \ {p,q};
A14: A is connected by JORDAN1:52;
A15: S is_a_component_of (L~Cage(C,i))` by GOBOARD9:def 2;
       LSeg(p,q) /\ L~Cage(C,i)
       = LSeg(p,q) /\ (Upper_Arc L~Cage(C,i) \/ Lower_Arc L~Cage(C,i))
           by JORDAN6:65
      .= LSeg(p,q) /\ Upper_Arc L~Cage(C,i) \/
         LSeg(p,q) /\ Lower_Arc L~Cage(C,i) by XBOOLE_1:23
      .= {p,q} by A7,A8,ENUMSET1:41;
   then A misses L~Cage(C,i) by XBOOLE_1:90;
   then A16: A c= (L~Cage(C,i))` by SUBSET_1:43;
A17: LSeg(q,p) meets Upper_Arc C by A1,A3,A4,A5,A7,A8,A9,JORDAN1J:61;
        Upper_Arc C c= C by JORDAN6:76;
    then A18: LSeg(q,p) meets C by A17,XBOOLE_1:63;
A19: {p} c= Upper_Arc L~Cage(C,i) by A7,XBOOLE_1:17;
        {q} c= Lower_Arc L~Cage(C,i) by A8,XBOOLE_1:17;
    then {p} \/ {q} c= Upper_Arc L~Cage(C,i) \/ Lower_Arc L~Cage(C,i)
       by A19,XBOOLE_1:13;
    then {p} \/ {q} c= L~Cage(C,i) by JORDAN6:65;
    then A20: {p,q} c= L~Cage(C,i) by ENUMSET1:41;
        L~Cage(C,i) misses C by JORDAN10:5;
    then {p,q} misses C by A20,XBOOLE_1:63;
    then A21: A meets C by A18,XBOOLE_1:84;
      C c= S by JORDAN10:11;
  then A c= S by A14,A15,A16,A21,GOBOARD9:6;
 hence LSeg(p,q) c= Cl S by A13,JORDAN1H:8;
end;

Lm2:
 ex n st n is_sufficiently_large_for C
proof
  set s = (W-bound C + E-bound C)/2,
      e = Gauge(C,1)*(X-SpanStart(C,1),len Gauge(C,1)),
      f = Gauge(C,1)*(X-SpanStart(C,1),1);
A1: X-SpanStart(C,1) = Center Gauge(C,1) by JORDAN1B:17;
  then X-SpanStart(C,1) = (len Gauge(C,1)) div 2 + 1 by JORDAN1A:def 1;
  then A2: 1 <= X-SpanStart(C,1) by NAT_1:11;
    len Gauge(C,1) >= 4 by JORDAN8:13;
then A3: 1 < len Gauge(C,1) by XXREAL_0:2;
then A4: f`1 = s by A1,JORDAN1A:59;
then A5: f in Vertical_Line s by JORDAN1A:17;
A6: e`1 = s by A1,A3,JORDAN1A:59;
  then e in Vertical_Line s by JORDAN1A:17;
  then A7: LSeg(e,f) c= Vertical_Line s by A5,JORDAN1A:23;
A8: len Gauge(C,1) = width Gauge(C,1) by JORDAN8:def 1;
      0 < len Gauge(C,1) by JORDAN8:13;
  then len Gauge(C,1) qua Integer div 2 < len Gauge(C,1) by INT_1:83;
  then (len Gauge(C,1)) div 2 + 1 <= len Gauge(C,1) by NAT_1:13;
  then X-SpanStart(C,1) <= len Gauge(C,1) by A1,JORDAN1A:def 1;
  then A9: f`2 < e`2 by A2,A3,A8,GOBOARD5:5;
A10: proj2.f = f`2 & proj2.e = e`2 by PSCOMP_1:def 29;
deffunc F(Element of NAT)=inf(proj2.:(LSeg(f,e) /\ Upper_Arc L~Cage(C,$1+1)));
  consider Es being Real_Sequence such that
A11: for i being Element of NAT holds Es.i =
       F(i) from FUNCT_2:sch 4;
deffunc G(Element of NAT)=|[s,Es.$1]|;
       consider E being Function of NAT, the carrier of TOP-REAL 2 such that
A12: for i being Element of NAT holds E.i = G(i) from FUNCT_2:sch 4;
deffunc H(Element of NAT)
=sup(proj2.:(LSeg(f,E.$1) /\ Lower_Arc L~Cage(C,$1+1)));
consider Fs being Real_Sequence such that
A13: for i being Element of NAT holds Fs.i =
       H(i) from FUNCT_2:sch 4;
deffunc I(Element of NAT)=|[s,Fs.$1]|;
       consider F being Function of NAT, the carrier of TOP-REAL 2 such that
A14: for i being Element of NAT holds F.i = I(i) from FUNCT_2:sch 4;
  set e1 = proj2.e, f1 = proj2.f;
A15: for i holds E.i in Upper_Arc L~Cage(C,i+1)
   proof let i;
     set p = E.i;
A16:  X-SpanStart(C,i+1) = Center Gauge(C,i+1) by JORDAN1B:17;
     then A17:  LSeg(Gauge(C,i+1)*(X-SpanStart(C,i+1),1),
           Gauge(C,i+1)*(X-SpanStart(C,i+1),len Gauge(C,i+1)))
          meets Upper_Arc L~Cage(C,i+1) by JORDAN1B:34;
         i+1 >= 1 by NAT_1:11;
     then LSeg(Gauge(C,i+1)*(X-SpanStart(C,i+1),1),
           Gauge(C,i+1)*(X-SpanStart(C,i+1),len Gauge(C,i+1)))
                 c= LSeg(f,e) by A1,A16,JORDAN1A:65;
     then A18:  LSeg(f,e) meets Upper_Arc L~Cage(C,i+1) by A17,XBOOLE_1:63;
         E.i = |[s,Es.i]| by A12;
     then A19:    p`1 = s & p`2 = Es.i by EUCLID:56;
     reconsider DD = LSeg(f,e) /\ Upper_Arc L~Cage(C,i+1)
         as compact Subset of TOP-REAL 2 by PSCOMP_1:64;
     reconsider D = proj2.:DD as compact Subset of REAL by JCT_MISC:24;
          DD c= the carrier of TOP-REAL 2;
     then DD c= dom proj2 by FUNCT_2:def 1;
     then A20:    dom proj2 /\ DD = DD by XBOOLE_1:28;
         DD <> {} by A18,XBOOLE_0:def 7;
     then dom proj2 meets DD by A20,XBOOLE_0:def 7;
     then A21:    D <> {} by RELAT_1:151;
         Es.i = inf D by A11;
     then Es.i in D by A21,RCOMP_1:32;
     then consider dd being Point of TOP-REAL 2 such that
A22:   dd in DD and
A23:   Es.i = proj2.dd by FUNCT_2:116;
A24:  dd in Upper_Arc L~Cage(C,i+1) by A22,XBOOLE_0:def 3;
         dd in LSeg(e,f) by A22,XBOOLE_0:def 3;
     then A25:   dd`1 = p`1 by A7,A19,JORDAN6:34;
         dd`2 = p`2 by A19,A23,PSCOMP_1:def 29;
    hence E.i in Upper_Arc L~Cage(C,i+1) by A24,A25,TOPREAL3:11;
   end;
A26: for i holds F.i in Lower_Arc L~Cage(C,i+1)
   proof let i;
     set p = F.i;
A27:  X-SpanStart(C,i+1) = Center Gauge(C,i+1) by JORDAN1B:17;
A28: E.i = |[s,Es.i]| by A12;
     then A29: (E.i)`1 = s by EUCLID:56;
         (E.i)`2 = Es.i by A28,EUCLID:56
          .= inf(proj2.:(LSeg(f,e) /\ Upper_Arc L~Cage(C,i+1))) by A11;
     then consider j such that
A30:    1 <= j and
A31:    j <= width Gauge(C,i+1) and
A32:    E.i = Gauge(C,i+1)*(X-SpanStart(C,i+1),j)
      by A1,A8,A27,A29,JORDAN1F:13;
         E.i in Upper_Arc L~Cage(C,i+1) by A15;
     then A33:   LSeg(f,E.i)
          meets Lower_Arc L~Cage(C,i+1) by A1,A27,A30,A31,A32,JORDAN1J:62;
         F.i = |[s,Fs.i]| by A14;
     then A34:    p`1 = s & p`2 = Fs.i by EUCLID:56;
     reconsider DD = LSeg(f,E.i) /\ Lower_Arc L~Cage(C,i+1)
           as compact Subset of TOP-REAL 2 by PSCOMP_1:64;
     reconsider D = proj2.:DD as compact Subset of REAL by JCT_MISC:24;
         DD c= the carrier of TOP-REAL 2;
     then DD c= dom proj2 by FUNCT_2:def 1;
     then A35:    dom proj2 /\ DD = DD by XBOOLE_1:28;
         DD <> {} by A33,XBOOLE_0:def 7;
     then dom proj2 meets DD by A35,XBOOLE_0:def 7;
     then A36:    D <> {} by RELAT_1:151;
         Fs.i = sup D by A13;
     then Fs.i in D by A36,RCOMP_1:32;
     then consider dd being Point of TOP-REAL 2 such that
A37:   dd in DD and
A38:   Fs.i = proj2.dd by FUNCT_2:116;
A39:  dd in Lower_Arc L~Cage(C,i+1) by A37,XBOOLE_0:def 3;
A40:  dd in LSeg(E.i,f) by A37,XBOOLE_0:def 3;
         E.i in Vertical_Line s by A29,JORDAN1A:17;
     then LSeg(E.i,f) c= Vertical_Line s by A5,JORDAN1A:23;
     then A41:   dd`1 = p`1 by A34,A40,JORDAN6:34;
         dd`2 = p`2 by A34,A38,PSCOMP_1:def 29;
    hence F.i in Lower_Arc L~Cage(C,i+1) by A39,A41,TOPREAL3:11;
   end;
A42: proj2.:LSeg(f,e) = [.f1,e1.] by A9,A10,SPRECT_1:61;
deffunc F(Element of NAT)=proj2.:LSeg(E.$1,F.$1);
consider S being Function of NAT, bool REAL such that
A43: for i being Element of NAT holds S.i = F(i) from FUNCT_2:sch 4;
  set AA = LSeg(e,f) /\ Upper_Arc C,
      BB = LSeg(e,f) /\ Lower_Arc C;
      AA is compact & BB is compact by COMPTS_1:20;
  then reconsider A = proj2.:AA, B = proj2.:BB as compact Subset of REAL
      by JCT_MISC:24;
A44: A misses B
   proof assume A meets B;
    then consider z being set such that
A45:   z in A and
A46:   z in B by XBOOLE_0:3;
     reconsider z as Real by A45;
     consider p being Point of TOP-REAL 2 such that
A47:    p in AA and
A48:    z = proj2.p by A45,FUNCT_2:116;
     consider q being Point of TOP-REAL 2 such that
A49:    q in BB and
A50:    z = proj2.q by A46,FUNCT_2:116;
A51:   q in LSeg(e,f) by A49,XBOOLE_0:def 3;
A52:  p in LSeg(e,f) by A47,XBOOLE_0:def 3;
     then A53:    p`1 = s by A7,JORDAN6:34 .= q`1 by A7,A51,JORDAN6:34;
         p`2 = proj2.q by A48,A50,PSCOMP_1:def 29 .= q`2 by PSCOMP_1:def 29;
     then A54:      p = q by A53,TOPREAL3:11;
A55:   p in Upper_Arc C by A47,XBOOLE_0:def 3;
         q in Lower_Arc C by A49,XBOOLE_0:def 3;
     then p in Upper_Arc C /\ Lower_Arc C by A54,A55,XBOOLE_0:def 3;
     then A56:    p in {W-min C,E-max C } by JORDAN6:65;
     per cases by A56,TARSKI:def 2;
     suppose p = W-min C;
      then A57:     (W-min C)`1 = s by A7,A52,JORDAN6:34;
           (W-min C)`1 = W-bound C by EUCLID:56;
      hence contradiction by A57,SPRECT_1:33;
      end;
      suppose p = E-max C;
       then A58:      (E-max C)`1 = s by A7,A52,JORDAN6:34;
           (E-max C)`1 = E-bound C by EUCLID:56;
      hence contradiction by A58,SPRECT_1:33;
   end;
   end;
      AA c= LSeg(e,f) by XBOOLE_1:17;
  then A59: A c= [.f1,e1.] by A42,RELAT_1:156;
      BB c= LSeg(e,f) by XBOOLE_1:17;
  then A60: B c= [.f1,e1.] by A42,RELAT_1:156;
      for i being Element of NAT holds S.i qua Subset of REAL is connected &
   S.i meets A & S.i meets B
   proof let i be Element of NAT;
A61:   S.i = proj2.:LSeg(E.i,F.i) by A43;
    hence S.i is connected by JCT_MISC:15;
    reconsider DD = LSeg(e,f) /\ Upper_Arc L~Cage(C,i+1)
         as compact Subset of TOP-REAL 2 by PSCOMP_1:64;
    reconsider D = proj2.:DD as compact Subset of REAL by JCT_MISC:24;
A62: E.i = |[s,Es.i]| by A12;
    then A63: (E.i)`1 = s by EUCLID:56;
A64:  X-SpanStart(C,i+1) = Center Gauge(C,i+1) by JORDAN1B:17;
A65: F.i = |[s,Fs.i]| by A14;
     then A66: (F.i)`1 = s by EUCLID:56;
A67:  LSeg(Gauge(C,i+1)*(Center Gauge(C,i+1),1),
           Gauge(C,i+1)*(Center Gauge(C,i+1),len Gauge(C,i+1)))
          meets Upper_Arc L~Cage(C,i+1) by JORDAN1B:34;
         1 <= i+1 by NAT_1:11;
     then LSeg(Gauge(C,i+1)*(Center Gauge(C,i+1),1),
           Gauge(C,i+1)*(Center Gauge(C,i+1),len Gauge(C,i+1))) c=
      LSeg(Gauge(C,1)*(Center Gauge(C,1),1),
           Gauge(C,1)*(Center Gauge(C,1),len Gauge(C,1))) by JORDAN1A:65;
     then A68:  LSeg(f,e) meets Upper_Arc L~Cage(C,i+1) by A1,A67,XBOOLE_1:63;
         DD c= the carrier of TOP-REAL 2;
     then DD c= dom proj2 by FUNCT_2:def 1;
     then A69:    dom proj2 /\ DD = DD by XBOOLE_1:28;
         DD <> {} by A68,XBOOLE_0:def 7;
     then dom proj2 meets DD by A69,XBOOLE_0:def 7;
     then A70:    D <> {} by RELAT_1:151;
         Es.i = inf D by A11;
     then Es.i in D by A70,RCOMP_1:32;
     then consider dd being Point of TOP-REAL 2 such that
A71:   dd in DD and
A72:  Es.i = proj2.dd by FUNCT_2:116;
A73:  dd in LSeg(f,e) by A71,XBOOLE_0:def 3;
     then A74:   dd`1 = (E.i)`1 by A7,A63,JORDAN6:34;
     A75: (E.i)`2 = Es.i by A62,EUCLID:56
            .= dd`2 by A72,PSCOMP_1:def 29;
     then A76:    E.i in LSeg(e,f) by A73,A74,TOPREAL3:11;
         f in LSeg(e,f) by TOPREAL1:6;
     then A77:    LSeg(f,E.i) c= LSeg(e,f) by A76,TOPREAL1:12;
     reconsider DD = LSeg(f,E.i) /\ Lower_Arc L~Cage(C,i+1)
         as compact Subset of TOP-REAL 2 by PSCOMP_1:64;
     reconsider D = proj2.:DD as compact Subset of REAL by JCT_MISC:24;
       (E.i)`2 = Es.i by A62,EUCLID:56
          .= inf(proj2.:(LSeg(f,e) /\ Upper_Arc L~Cage(C,i+1))) by A11;
     then consider j such that
A78:    1 <= j and
A79:    j <= width Gauge(C,i+1) and
A80:    E.i = Gauge(C,i+1)*(X-SpanStart(C,i+1),j)
        by A1,A8,A63,A64,JORDAN1F:13;
A81:  E.i in Upper_Arc L~Cage(C,i+1) by A15;
     then A82:   LSeg(f,E.i)
          meets Lower_Arc L~Cage(C,i+1) by A1,A64,A78,A79,A80,JORDAN1J:62;
         DD c= the carrier of TOP-REAL 2;
     then DD c= dom proj2 by FUNCT_2:def 1;
     then A83:    dom proj2 /\ DD = DD by XBOOLE_1:28;
         DD <> {} by A82,XBOOLE_0:def 7;
     then dom proj2 meets DD by A83,XBOOLE_0:def 7;
     then A84:    D <> {} by RELAT_1:151;
         Fs.i = sup D by A13;
     then Fs.i in D by A84,RCOMP_1:32;
     then consider dd being Point of TOP-REAL 2 such that
A85:  dd in DD and
A86:  Fs.i = proj2.dd by FUNCT_2:116;
A87:  dd in LSeg(E.i,f) by A85,XBOOLE_0:def 3;
         E.i in Vertical_Line s by A63,JORDAN1A:17;
     then LSeg(E.i,f) c= Vertical_Line s by A5,JORDAN1A:23;
then A88:  dd`1 = (F.i)`1 by A66,A87,JORDAN6:34;
         (F.i)`2 = Fs.i by A65,EUCLID:56
            .= dd`2 by A86,PSCOMP_1:def 29;
then A89: F.i in LSeg(E.i,f) by A87,A88,TOPREAL3:11;
then A90:    LSeg(E.i,F.i) c= LSeg(e,f) by A76,A77,TOPREAL1:12;
         E.i in LSeg(E.i,f) by TOPREAL1:6;
then A91: LSeg(E.i,F.i) c= LSeg(E.i,f) by A89,TOPREAL1:12;
         (E.i)`2 = Es.i by A62,EUCLID:56
          .= inf(proj2.:(LSeg(f,e) /\ Upper_Arc L~Cage(C,i+1))) by A11;
     then consider j such that
A92:   1 <= j and
A93:   j <= width Gauge(C,i+1) and
A94:   E.i = Gauge(C,i+1)*(X-SpanStart(C,i+1),j)
      by A1,A8,A63,A64,JORDAN1F:13;
A95:  (F.i)`2 =Fs.i by A65,EUCLID:56
       .= sup(proj2.:(LSeg(f,E.i) /\ Lower_Arc L~Cage(C,i+1)))by A13;
     then consider k such that
A96:  1 <= k and
A97:  k <= width Gauge(C,i+1) and
A98:  F.i = Gauge(C,i+1)*(Center Gauge(C,i+1),k)
                     by A1,A64,A66,A81,A92,A93,A94,JORDAN1I:30;
A99:  F.i in Lower_Arc L~Cage(C,i+1) by A26;
     A100: 1 <= Center Gauge(C,i+1) by JORDAN1B:12;
     A101: Center Gauge(C,i+1) <= len Gauge(C,i+1) by JORDAN1B:14;
        for p being real number st p in D holds p <= (E.i)`2
     proof let p be real number;
      assume p in D;
       then consider x being set such that
            x in dom proj2 and
A102:    x in DD and
A103:    p = proj2.x by FUNCT_1:def 12;
      reconsider x as Point of TOP-REAL 2 by A102;
A104:    f`2 <= (E.i)`2 by A9,A73,A75,TOPREAL1:10;
           x in LSeg(f,E.i) by A102,XBOOLE_0:def 3;
       then x`2 <= (E.i)`2 by A104,TOPREAL1:10;
      hence p <= (E.i)`2 by A103,PSCOMP_1:def 29;
     end;
    then A105: sup D <= (E.i)`2 by A84,SEQ_4:62;
then A106:  k <= j by A64,A92,A94,A95,A97,A98,A100,A101,GOBOARD5:5;
A107: for x be set holds
      x in LSeg(E.i,F.i) /\ Upper_Arc L~Cage(C,i+1) implies x = E.i
      proof
       reconsider DD = LSeg(f,e) /\ Upper_Arc L~Cage(C,i+1)
         as compact Subset of TOP-REAL 2 by PSCOMP_1:64;
       reconsider D = proj2.:DD as compact Subset of REAL by JCT_MISC:24;
       let x be set;
       assume
A108:      x in LSeg(E.i,F.i) /\ Upper_Arc L~Cage(C,i+1);
        then reconsider p = x as Point of TOP-REAL 2;
A109:      p in LSeg(E.i,F.i) by A108,XBOOLE_0:def 3;
        then A110:      p`1 = (E.i)`1 by A63,A66,GOBOARD7:5;
A111:      p`2 <= (E.i)`2 by A95,A105,A109,TOPREAL1:10;
            E.i = |[s,Es.i]| by A12;
then A112:       (E.i)`2 = Es.i by EUCLID:56
         .= inf D by A11;
            D is bounded by RCOMP_1:28;
then A113:     D is bounded_below by SEQ_4:def 3;
            p in Upper_Arc L~Cage(C,i+1) by A108,XBOOLE_0:def 3;
        then p in DD by A90,A109,XBOOLE_0:def 3;
        then proj2.p in D by FUNCT_2:43;
        then p`2 in D by PSCOMP_1:def 29;
        then (E.i)`2 <= p`2 by A112,A113,SEQ_4:def 5;
        then p`2 = (E.i)`2 by A111,XXREAL_0:1;
       hence x = E.i by A110,TOPREAL3:11;
      end;
     A114: for x be set holds
      x in LSeg(E.i,F.i) /\ Lower_Arc L~Cage(C,i+1) implies x = F.i
      proof
       reconsider EE = LSeg(f,E.i) /\ Lower_Arc L~Cage(C,i+1)
        as compact Subset of TOP-REAL 2 by PSCOMP_1:64;
       reconsider E0 = proj2.:EE as compact Subset of REAL by JCT_MISC:24;
       let x be set;
       assume
A115:      x in LSeg(E.i,F.i) /\ Lower_Arc L~Cage(C,i+1);
        then reconsider p = x as Point of TOP-REAL 2;
A116:      p in LSeg(E.i,F.i) by A115,XBOOLE_0:def 3;
then A117:      p`1 = (F.i)`1 by A63,A66,GOBOARD7:5;
A118:      p`2 >= (F.i)`2 by A95,A105,A116,TOPREAL1:10;
            F.i = |[s,Fs.i]| by A14;
then A119:         (F.i)`2 = Fs.i by EUCLID:56
           .= sup E0 by A13;
              E0 is bounded by RCOMP_1:28;
then A120:       E0 is bounded_above by SEQ_4:def 3;
              p in Lower_Arc L~Cage(C,i+1) by A115,XBOOLE_0:def 3;
          then p in EE by A91,A116,XBOOLE_0:def 3;
          then proj2.p in E0 by FUNCT_2:43;
          then p`2 in E0 by PSCOMP_1:def 29;
          then (F.i)`2 >= p`2 by A119,A120,SEQ_4:def 4;
          then p`2 = (F.i)`2 by A118,XXREAL_0:1;
         hence x = F.i by A117,TOPREAL3:11;
        end;
A121: Gauge(C,i+1)*(Center Gauge(C,i+1),j) in
       LSeg(Gauge(C,i+1)*(Center Gauge(C,i+1),k),
            Gauge(C,i+1)*(Center Gauge(C,i+1),j)) &
      Gauge(C,i+1)*(Center Gauge(C,i+1),k) in
       LSeg(Gauge(C,i+1)*(Center Gauge(C,i+1),k),
            Gauge(C,i+1)*(Center Gauge(C,i+1),j)) by TOPREAL1:6;
A122: LSeg(Gauge(C,i+1)*(Center Gauge(C,i+1),k),
       Gauge(C,i+1)*(Center Gauge(C,i+1),j)) /\ Upper_Arc L~Cage(C,i+1) =
       {Gauge(C,i+1)*(Center Gauge(C,i+1),j)}
   proof
    thus LSeg(Gauge(C,i+1)*(Center Gauge(C,i+1),k),
     Gauge(C,i+1)*(Center Gauge(C,i+1),j)) /\ Upper_Arc L~Cage(C,i+1) c=
      {Gauge(C,i+1)*(Center Gauge(C,i+1),j)}
    proof
     let x be set;
     assume x in LSeg(Gauge(C,i+1)*(Center Gauge(C,i+1),k),
      Gauge(C,i+1)*(Center Gauge(C,i+1),j)) /\ Upper_Arc L~Cage(C,i+1);
     then x = Gauge(C,i+1)*(Center Gauge(C,i+1),j) by A64,A94,A98,A107;
     hence x in {Gauge(C,i+1)*(Center Gauge(C,i+1),j)} by TARSKI:def 1;
    end;
     let x be set;
     assume x in {Gauge(C,i+1)*(Center Gauge(C,i+1),j)};
     then x = Gauge(C,i+1)*(Center Gauge(C,i+1),j) by TARSKI:def 1;
     hence x in LSeg(Gauge(C,i+1)*(Center Gauge(C,i+1),k),
      Gauge(C,i+1)*(Center Gauge(C,i+1),j)) /\ Upper_Arc L~Cage(C,i+1)
       by A64,A81,A94,A121,XBOOLE_0:def 3;
   end;
   A123: LSeg(Gauge(C,i+1)*(Center Gauge(C,i+1),k),
       Gauge(C,i+1)*(Center Gauge(C,i+1),j)) /\ Lower_Arc L~Cage(C,i+1) =
       {Gauge(C,i+1)*(Center Gauge(C,i+1),k)}
   proof
    thus LSeg(Gauge(C,i+1)*(Center Gauge(C,i+1),k),
     Gauge(C,i+1)*(Center Gauge(C,i+1),j)) /\ Lower_Arc L~Cage(C,i+1) c=
      {Gauge(C,i+1)*(Center Gauge(C,i+1),k)}
    proof
     let x be set;
     assume x in LSeg(Gauge(C,i+1)*(Center Gauge(C,i+1),k),
      Gauge(C,i+1)*(Center Gauge(C,i+1),j)) /\ Lower_Arc L~Cage(C,i+1);
     then x = Gauge(C,i+1)*(Center Gauge(C,i+1),k) by A64,A94,A98,A114;
     hence x in {Gauge(C,i+1)*(Center Gauge(C,i+1),k)} by TARSKI:def 1;
    end;
     let x be set;
     assume x in {Gauge(C,i+1)*(Center Gauge(C,i+1),k)};
     then x = Gauge(C,i+1)*(Center Gauge(C,i+1),k) by TARSKI:def 1;
     hence thesis by A98,A99,A121,XBOOLE_0:def 3;
   end;
     then LSeg(F.i,E.i) meets Upper_Arc C
      by A64,A93,A94,A96,A98,A106,A122,JORDAN1J:64;
     then LSeg(E.i,F.i) meets AA by A90,XBOOLE_1:77;
    hence S.i meets A by A61,JORDAN1A:24;
         LSeg(E.i,F.i) meets Lower_Arc C
      by A64,A93,A94,A96,A98,A106,A122,A123,JORDAN1J:63;
     then LSeg(E.i,F.i) meets BB by A90,XBOOLE_1:77;
    hence S.i meets B by A61,JORDAN1A:24;
   end;
  then consider r being real number such that
A124: r in [.f1,e1.] and
A125: not r in A \/ B and
A126: for i being Element of NAT ex k being Element of NAT st i <= k & r in S.k
        by A44,A59,A60,JCT_MISC:21;
  reconsider r as Real by XREAL_0:def 1;
  set p = |[s,r]|;
A127: p in LSeg(e,f) by A4,A6,A124,JORDAN1A:21;
A128: p`1 = s by EUCLID:56;
A129: now assume p in C;
      then p in Lower_Arc C \/ Upper_Arc C by JORDAN6:65;
      then p in (Lower_Arc C \/ Upper_Arc C) /\ LSeg(e,f) by A127,
XBOOLE_0:def 3;
      then p in AA \/ BB by XBOOLE_1:23;
      then proj2.p in proj2.:(AA \/ BB) by FUNCT_2:43;
      then r in proj2.:(AA \/ BB) by PSCOMP_1:128;
     hence contradiction by A125,RELAT_1:153;
  end;
A130: meet BDD-Family C = C \/ BDD C by JORDAN10:21;
      for Y being set st Y in BDD-Family C holds p in Y
   proof let Y be set;
A131: BDD-Family C
 = { Cl BDD L~Cage(C,k1) where k1 is Element of NAT : not contradiction
}
     by JORDAN10:def 2;
    assume Y in BDD-Family C;
     then consider k1 being Element of NAT such that
A132:    Y = Cl BDD L~Cage(C,k1) by A131;
     consider k0 being Element of NAT such that
A133:   k1 <= k0 and
A134:   r in S.k0 by A126;
A135: E.k0 = |[s,Es.k0]| by A12;
    then A136: (E.k0)`1 = s by EUCLID:56;
A137: F.k0 = |[s,Fs.k0]| by A14;
     then A138: (F.k0)`1 = s by EUCLID:56;
A139: proj2.(E.k0) = (E.k0)`2 & proj2.(F.k0) = (F.k0)`2 by PSCOMP_1:def 29;
    reconsider DD = LSeg(f,E.k0) /\ Lower_Arc L~Cage(C,k0+1)
         as compact Subset of TOP-REAL 2 by PSCOMP_1:64;
    reconsider D = proj2.:DD as compact Subset of REAL by JCT_MISC:24;
A140:  X-SpanStart(C,k0+1) = Center Gauge(C,k0+1) by JORDAN1B:17;
          (E.k0)`2 = Es.k0 by A135,EUCLID:56
          .= inf(proj2.:(LSeg(f,e) /\ Upper_Arc L~Cage(C,k0+1))) by A11;
      then consider j such that
A141:    1 <= j and
A142:    j <= width Gauge(C,k0+1) and
A143:    E.k0 = Gauge(C,k0+1)*(X-SpanStart(C,k0+1),j)
                     by A1,A8,A136,A140,JORDAN1F:13;
          E.k0 in Upper_Arc L~Cage(C,k0+1) by A15;
      then A144:   LSeg(f,E.k0)
          meets Lower_Arc L~Cage(C,k0+1) by A1,A140,A141,A142,A143,JORDAN1J:62;
        DD c= the carrier of TOP-REAL 2;
    then DD c= dom proj2 by FUNCT_2:def 1;
    then A145:  dom proj2 /\ DD = DD by XBOOLE_1:28;
        DD <> {} by A144,XBOOLE_0:def 7;
    then dom proj2 meets DD by A145,XBOOLE_0:def 7;
    then A146:  D <> {} by RELAT_1:151;
        Fs.k0 = sup D by A13;
    then Fs.k0 in D by A146,RCOMP_1:32;
    then consider dd being Point of TOP-REAL 2 such that
A147:  dd in DD and
A148:  Fs.k0 = proj2.dd by FUNCT_2:116;
A149: dd in LSeg(E.k0,f) by A147,XBOOLE_0:def 3;
        E.k0 in Vertical_Line s by A136,JORDAN1A:17;
    then LSeg(E.k0,f) c= Vertical_Line s by A5,JORDAN1A:23;
    then A150:  dd`1 = (F.k0)`1 by A138,A149,JORDAN6:34;
    A151: (F.k0)`2 = Fs.k0 by A137,EUCLID:56
            .= dd`2 by A148,PSCOMP_1:def 29;
    then A152:  F.k0 in LSeg(E.k0,f) by A149,A150,TOPREAL3:11;
    reconsider DD = LSeg(f,e) /\ Upper_Arc L~Cage(C,k0+1)
         as compact Subset of TOP-REAL 2 by PSCOMP_1:64;
    reconsider D = proj2.:DD as compact Subset of REAL by JCT_MISC:24;
A153:  LSeg(Gauge(C,k0+1)*(Center Gauge(C,k0+1),1),
           Gauge(C,k0+1)*(Center Gauge(C,k0+1),len Gauge(C,k0+1)))
          meets Upper_Arc L~Cage(C,k0+1) by JORDAN1B:34;
          1+0 <= k0+1 by NAT_1:11;
      then LSeg(Gauge(C,k0+1)*(Center Gauge(C,k0+1),1),
           Gauge(C,k0+1)*(Center Gauge(C,k0+1),len Gauge(C,k0+1))) c=
      LSeg(Gauge(C,1)*(Center Gauge(C,1),1),
           Gauge(C,1)*(Center Gauge(C,1),len Gauge(C,1))) by JORDAN1A:65;
      then A154:  LSeg(f,e) meets Upper_Arc L~Cage(C,k0+1) by A1,A153,
XBOOLE_1:63;
        DD c= the carrier of TOP-REAL 2;
    then DD c= dom proj2 by FUNCT_2:def 1;
    then A155:  dom proj2 /\ DD = DD by XBOOLE_1:28;
        DD <> {} by A154,XBOOLE_0:def 7;
    then dom proj2 meets DD by A155,XBOOLE_0:def 7;
    then A156:  D <> {} by RELAT_1:151;
        Es.k0 = inf D by A11;
    then Es.k0 in D by A156,RCOMP_1:32;
    then consider dd being Point of TOP-REAL 2 such that
A157:  dd in DD and
A158:  Es.k0 = proj2.dd by FUNCT_2:116;
A159: dd in LSeg(f,e) by A157,XBOOLE_0:def 3;
    then A160:  dd`1 = (E.k0)`1 by A7,A136,JORDAN6:34;
    A161: (E.k0)`2 = Es.k0 by A135,EUCLID:56
            .= dd`2 by A158,PSCOMP_1:def 29;
    then A162:    E.k0 in LSeg(f,e) by A159,A160,TOPREAL3:11;
    A163:   f`2 <= (E.k0)`2 by A9,A159,A161,TOPREAL1:10;
    then A164: (F.k0)`2 <= (E.k0)`2 by A149,A151,TOPREAL1:10;
        r in proj2.:LSeg(E.k0,F.k0) by A43,A134;
    then r in [.proj2.(F.k0),proj2.(E.k0).] by A139,A164,SPRECT_1:61;
    then A165:    p in LSeg(E.k0,F.k0) by A136,A138,JORDAN1A:21;
        (E.k0)`2 = Es.k0 by A135,EUCLID:56
          .= inf(proj2.:(LSeg(f,e) /\ Upper_Arc L~Cage(C,k0+1))) by A11;
    then consider j such that
A166:  1 <= j and
A167:  j <= width Gauge(C,k0+1) and
A168:  E.k0 = Gauge(C,k0+1)*(X-SpanStart(C,k0+1),j)
      by A1,A8,A136,A140,JORDAN1F:13;
A169:   E.k0 in Upper_Arc L~Cage(C,k0+1) by A15;
A170:   F.k0 in Lower_Arc L~Cage(C,k0+1) by A26;
A171: (F.k0)`2 =Fs.k0 by A137,EUCLID:56
       .= sup(proj2.:(LSeg(f,E.k0) /\ Lower_Arc L~Cage(C,k0+1)))by A13;
    then consider k such that
A172:  1 <= k and
A173:  k <= width Gauge(C,k0+1) and
A174:  F.k0 = Gauge(C,k0+1)*(X-SpanStart(C,k0+1),k)
            by A1,A138,A140,A166,A167,A168,A169,JORDAN1I:30;
    reconsider CC = LSeg(f,E.k0) /\ Lower_Arc L~Cage(C,k0+1)
         as compact Subset of TOP-REAL 2 by PSCOMP_1:64;
    reconsider W = proj2.:CC as compact Subset of REAL by JCT_MISC:24;
         CC c= the carrier of TOP-REAL 2;
     then CC c= dom proj2 by FUNCT_2:def 1;
     then A175:    dom proj2 /\ CC = CC by XBOOLE_1:28;
         CC <> {} by A144,XBOOLE_0:def 7;
     then dom proj2 meets CC by A175,XBOOLE_0:def 7;
     then A176:    W <> {} by RELAT_1:151;
A177: 1 <= Center Gauge(C,k0+1) by JORDAN1B:12;
A178: Center Gauge(C,k0+1) <= len Gauge(C,k0+1) by JORDAN1B:14;
        for p being real number st p in W holds p <= (E.k0)`2
     proof let p be real number;
      assume p in W;
       then consider x being set such that
            x in dom proj2 and
A179:    x in CC and
A180:    p = proj2.x by FUNCT_1:def 12;
      reconsider x as Point of TOP-REAL 2 by A179;
           x in LSeg(f,E.k0) by A179,XBOOLE_0:def 3;
       then x`2 <= (E.k0)`2 by A163,TOPREAL1:10;
      hence p <= (E.k0)`2 by A180,PSCOMP_1:def 29;
     end;
    then sup W <= (E.k0)`2 by A176,SEQ_4:62;
    then A181:  k <= j by A140,A166,A168,A171,A173,A174,A177,A178,GOBOARD5:5;
    A182: E.k0 in LSeg(f,E.k0) by TOPREAL1:6;
    then A183: LSeg(E.k0,F.k0) c= LSeg(f,E.k0) by A152,TOPREAL1:12;
        f in LSeg(f,e) by TOPREAL1:6;
    then LSeg(f,E.k0) c= LSeg(e,f) by A162,TOPREAL1:12;
    then A184: LSeg(E.k0,F.k0) c= LSeg(e,f) by A152,A182,TOPREAL1:12;
       for x being set holds
    x in LSeg(E.k0,F.k0) /\ Upper_Arc L~Cage(C,k0+1) iff x = E.k0
    proof let x be set;
     thus x in LSeg(E.k0,F.k0) /\ Upper_Arc L~Cage(C,k0+1) implies x = E.k0
      proof assume
A185:      x in LSeg(E.k0,F.k0) /\ Upper_Arc L~Cage(C,k0+1);
        then reconsider p = x as Point of TOP-REAL 2;
A186:      p in LSeg(E.k0,F.k0) by A185,XBOOLE_0:def 3;
        then A187:      p`1 = (E.k0)`1 by A136,A138,GOBOARD7:5;
A188:      p`2 <= (E.k0)`2 by A164,A186,TOPREAL1:10;
            E.k0 = |[s,Es.k0]| by A12;
        then A189:       (E.k0)`2 = Es.k0 by EUCLID:56
         .= inf D by A11;
            D is bounded by RCOMP_1:28;
        then A190:     D is bounded_below by SEQ_4:def 3;
            p in Upper_Arc L~Cage(C,k0+1) by A185,XBOOLE_0:def 3;
        then p in DD by A184,A186,XBOOLE_0:def 3;
        then proj2.p in D by FUNCT_2:43;
        then p`2 in D by PSCOMP_1:def 29;
        then (E.k0)`2 <= p`2 by A189,A190,SEQ_4:def 5;
        then p`2 = (E.k0)`2 by A188,XXREAL_0:1;
       hence x = E.k0 by A187,TOPREAL3:11;
      end;
     assume
A191:     x = E.k0;
      then x in LSeg(E.k0,F.k0) by TOPREAL1:6;
     hence x in LSeg(E.k0,F.k0) /\ Upper_Arc L~Cage(C,k0+1)
       by A169,A191,XBOOLE_0:def 3;
    end;
   then A192: LSeg(E.k0,F.k0) /\ Upper_Arc L~Cage(C,k0+1) = {E.k0} by
TARSKI:def 1;
   reconsider EE = LSeg(f,E.k0) /\ Lower_Arc L~Cage(C,k0+1)
        as compact Subset of TOP-REAL 2 by PSCOMP_1:64;
   reconsider E0 = proj2.:EE as compact Subset of REAL by JCT_MISC:24;
       for x being set holds
    x in LSeg(E.k0,F.k0) /\ Lower_Arc L~Cage(C,k0+1) iff x = F.k0
    proof let x be set;
     thus x in LSeg(E.k0,F.k0) /\ Lower_Arc L~Cage(C,k0+1) implies x = F.k0
      proof assume
A193:      x in LSeg(E.k0,F.k0) /\ Lower_Arc L~Cage(C,k0+1);
        then reconsider p = x as Point of TOP-REAL 2;
A194:      p in LSeg(E.k0,F.k0) by A193,XBOOLE_0:def 3;
        then A195:      p`1 = (F.k0)`1 by A136,A138,GOBOARD7:5;
A196:      p`2 >= (F.k0)`2 by A164,A194,TOPREAL1:10;
            F.k0 = |[s,Fs.k0]| by A14;
          then A197:         (F.k0)`2 = Fs.k0 by EUCLID:56
           .= sup E0 by A13;
              E0 is bounded by RCOMP_1:28;
          then A198:       E0 is bounded_above by SEQ_4:def 3;
              p in Lower_Arc L~Cage(C,k0+1) by A193,XBOOLE_0:def 3;
          then p in EE by A183,A194,XBOOLE_0:def 3;
          then proj2.p in E0 by FUNCT_2:43;
          then p`2 in E0 by PSCOMP_1:def 29;
          then (F.k0)`2 >= p`2 by A197,A198,SEQ_4:def 4;
          then p`2 = (F.k0)`2 by A196,XXREAL_0:1;
         hence x = F.k0 by A195,TOPREAL3:11;
        end;
       assume
A199:       x = F.k0;
        then x in LSeg(E.k0,F.k0) by TOPREAL1:6;
       hence x in LSeg(E.k0,F.k0) /\ Lower_Arc L~Cage(C,k0+1)
           by A170,A199,XBOOLE_0:def 3;
      end;
     then LSeg(E.k0,F.k0) /\ Lower_Arc L~Cage(C,k0+1) = {F.k0} by TARSKI:def 1
;
     then LSeg(E.k0,F.k0) c= Cl RightComp Cage(C,k0+1)
            by A140,A166,A167,A168,A172,A173,A174,A181,A192,Lm1;
     then A200:   p in Cl RightComp Cage(C,k0+1) by A165;
         k0 <= k0+1 by NAT_1:11;
     then A201:  RightComp Cage(C,k0+1) c= RightComp Cage(C,k0) by JORDAN1H:56;
         RightComp Cage(C,k0) c= RightComp Cage(C,k1) by A133,JORDAN1H:56;
     then RightComp Cage(C,k0+1) c= RightComp Cage(C,k1) by A201,XBOOLE_1:1;
     then Cl RightComp Cage(C,k0+1) c= Cl RightComp Cage(C,k1) by PRE_TOPC:49;
     then p in Cl RightComp Cage(C,k1) by A200;
    hence p in Y by A132,GOBRD14:47;
   end;
  then p in meet BDD-Family C by SETFAM_1:def 1;
  then p in BDD C by A129,A130,XBOOLE_0:def 2;
  then consider n,i,j such that
A202: 1 < i and
A203: i < len Gauge(C,n) and
A204: 1 < j and
A205: j < width Gauge(C,n) and
A206: p`1 <> (Gauge(C,n)*(i,j))`1 and
A207: p in cell(Gauge(C,n),i,j) and
A208: cell(Gauge(C,n),i,j) c= BDD C by JORDAN1C:35;
 take n,j;
 thus j < width Gauge(C,n) by A205;
A209: n > 0 by A203,A205,A208,JORDAN1B:44;
A210:  X-SpanStart(C,n) = Center Gauge(C,n) by JORDAN1B:17;
        2 <= X-SpanStart(C,n) by JORDAN1H:58;
    then A211: 1 <= X-SpanStart(C,n) & X-SpanStart(C,n) <= len Gauge(C,n)
              by JORDAN1H:58,XXREAL_0:2;
     4 <= len Gauge (C,1) by JORDAN8:13;
 then A212: 1 <= len Gauge (C,1) by XXREAL_0:2;
    len Gauge(C,n) = width Gauge(C,n) by JORDAN8:def 1;
    then (Gauge(C,n)*(X-SpanStart(C,n),j))`1
          = s by A1,A4,A204,A205,A209,A210,A212,JORDAN1A:57;
 hence cell(Gauge(C,n),X-SpanStart(C,n)-'1,j) c= BDD C
          by A128,A202,A203,A204,A205,A206,A207,A208,A211,JORDAN1B:23;
end;

definition let C;
 func ApproxIndex C -> Element of NAT means
:Def1: it is_sufficiently_large_for C &
  for j st j is_sufficiently_large_for C holds j >= it;
 existence
  proof
    defpred P[Element of NAT] means $1 is_sufficiently_large_for C;
    set X = {i : P[i]};
    consider i such that
A1:    i is_sufficiently_large_for C by Lm2;
A2:  i in X by A1;
        X is Subset of NAT from DOMAIN_1:sch 7;
    then reconsider X as non empty Subset of NAT by A2;
   take min X;
        min X in X by SEQ_4:def 6;
    then ex i st i = min X & i is_sufficiently_large_for C;
   hence min X is_sufficiently_large_for C;
   let j;
   assume j is_sufficiently_large_for C;
    then j in X;
   hence j >= min X by SEQ_4:def 6;
  end;
 uniqueness
  proof let it1,it2 be Element of NAT such that
A3: it1 is_sufficiently_large_for C and
A4: for j st j is_sufficiently_large_for C holds j >= it1 and
A5: it2 is_sufficiently_large_for C and
A6: for j st j is_sufficiently_large_for C holds j >= it2;
        it1 <= it2 & it2 <= it1 by A3,A4,A5,A6;
   hence it1 = it2 by XXREAL_0:1;
  end;
end;

theorem Th1:
 ApproxIndex C >= 1
proof
      ApproxIndex C is_sufficiently_large_for C by Def1;
 hence thesis by JORDAN1H:60;
end;

definition let C;
 func Y-InitStart C -> Element of NAT means
:Def2: it < width Gauge(C,ApproxIndex C) &
    cell(Gauge(C,ApproxIndex C),X-SpanStart(C,ApproxIndex C)-'1,it) c= BDD C &
  for j st j < width Gauge(C,ApproxIndex C) &
       cell(Gauge(C,ApproxIndex C),X-SpanStart(C,ApproxIndex C)-'1,j) c= BDD C
    holds j >= it;
 existence
  proof set n = ApproxIndex C;
    defpred P[Element of NAT] means
      $1 < width Gauge(C,n) &
      cell(Gauge(C,n),X-SpanStart(C,n)-'1,$1) c= BDD C;
        n is_sufficiently_large_for C by Def1;
    then consider j such that
A1:   P[j] by JORDAN1H:def 3;
    set X = { i: P[i] };
A2:  j in X by A1;
        X is Subset of NAT from DOMAIN_1:sch 7;
    then reconsider X as non empty Subset of NAT by A2;
   take min X;
        min X in X by SEQ_4:def 6;
    then ex i st i = min X & P[i];
   hence P[min X];
   let i;
   assume P[i];
    then i in X;
   hence thesis by SEQ_4:def 6;
  end;
 uniqueness
  proof let it1,it2 be Element of NAT;
   assume
       it1 < width Gauge(C,ApproxIndex C) &
    cell(Gauge(C,ApproxIndex C),X-SpanStart(C,ApproxIndex C)-'1,it1) c= BDD C &
  (for j st j < width Gauge(C,ApproxIndex C) &
       cell(Gauge(C,ApproxIndex C),X-SpanStart(C,ApproxIndex C)-'1,j) c= BDD C
    holds j >= it1) &
   it2 < width Gauge(C,ApproxIndex C) &
    cell(Gauge(C,ApproxIndex C),X-SpanStart(C,ApproxIndex C)-'1,it2) c= BDD C &
  for j st j < width Gauge(C,ApproxIndex C) &
       cell(Gauge(C,ApproxIndex C),X-SpanStart(C,ApproxIndex C)-'1,j) c= BDD C
    holds j >= it2;
    then it1 <= it2 & it2 <= it1;
   hence thesis by XXREAL_0:1;
  end;
end;

theorem Th2:
 Y-InitStart C > 1
proof set m = ApproxIndex C;
A1: X-SpanStart(C,m)-'1 <= len Gauge(C,m) by JORDAN1H:59;
 assume
A2: Y-InitStart C <= 1;
  per cases by A2,NAT_1:26;
  suppose Y-InitStart C = 0;
    then A3: cell(Gauge(C,m),X-SpanStart(C,m)-'1,0) c= BDD C by Def2;
      0 <= width Gauge(C,m);
  then A4: cell(Gauge(C,m),X-SpanStart(C,m)-'1,0) is non empty by A1,
JORDAN1A:45;
      cell(Gauge(C,m),X-SpanStart(C,m)-'1,0) c= UBD C by A1,JORDAN1A:70;
  then UBD C meets BDD C by A3,A4,XBOOLE_1:68;
 hence contradiction by JORDAN2C:28;
  end;
  suppose Y-InitStart C = 1;
   then A5:  cell(Gauge(C,m),X-SpanStart(C,m)-'1,1) c= BDD C by Def2;
A6:  width Gauge(C,m) <> 0 by GOBOARD1:def 5;
  then A7: 0+1 <= width Gauge(C,m) by NAT_1:14;
  then A8: cell(Gauge(C,m),X-SpanStart(C,m)-'1,1) is non empty by A1,
JORDAN1A:45;
A9: cell(Gauge(C,m),X-SpanStart(C,m)-'1,0) c= UBD C by A1,JORDAN1A:70;
   set i1 = X-SpanStart(C,m);
A10: i1 < len Gauge(C,m) by JORDAN1H:58;
        i1-'1 <= i1 by BINARITH:62;
   then A11: i1-'1 < len Gauge(C,m) by A10,XXREAL_0:2;
A12:  0 < width Gauge(C,m) by A6;
       1 <= i1-'1 by JORDAN1H:59;
   then cell(Gauge(C,m),i1-'1,0) /\ cell(Gauge(C,m),i1-'1,0+1)
    = LSeg(Gauge(C,m)*(i1-'1,0+1),Gauge(C,m)*(i1-'1+1,0+1))
              by A11,A12,GOBOARD5:27;
    then A13:   cell(Gauge(C,m),i1-'1,0) meets cell(Gauge(C,m),i1-'1,0+1) by
XBOOLE_0:def 7;
    UBD C is_outside_component_of C by JORDAN2C:76;
   then A14:  UBD C is_a_component_of C` by JORDAN2C:def 4;
A15:  cell(Gauge(C,m),X-SpanStart(C,m)-'1,1) is connected by A7,A11,JORDAN1A:46
;
      BDD C c= C` by JORDAN2C:29;
  then cell(Gauge(C,m),X-SpanStart(C,m)-'1,1) c= C` by A5,XBOOLE_1:1;
  then cell(Gauge(C,m),X-SpanStart(C,m)-'1,1) c= UBD C by A9,A13,A14,A15,
GOBOARD9:6;
  then UBD C meets BDD C by A5,A8,XBOOLE_1:68;
 hence contradiction by JORDAN2C:28;
end;
end;

theorem Th3:
 Y-InitStart C + 1 < width Gauge(C,ApproxIndex C)
proof set m = ApproxIndex C;
A1: X-SpanStart(C,m)-'1 <= len Gauge(C,m) by JORDAN1H:59;
 assume Y-InitStart C + 1 >= width Gauge(C,m);
  then A2: Y-InitStart C + 1 > width Gauge(C,m) or
    Y-InitStart C + 1 = width Gauge(C,m) by XXREAL_0:1;
A3: Y-InitStart C < width Gauge(C,m) or
    Y-InitStart C = width Gauge(C,m) by Def2;
  per cases by A2,A3,NAT_1:13;
  suppose Y-InitStart C = width Gauge(C,m);
 hence contradiction by Def2;
  end;
  suppose Y-InitStart C + 1 = width Gauge(C,m);
   then Y-InitStart C = width Gauge(C,m)-'1 by BINARITH:39;
   then A4:  cell(Gauge(C,m),X-SpanStart(C,m)-'1,width Gauge(C,m)-'1) c= BDD C
by Def2;
      width Gauge(C,m)-'1 <= width Gauge(C,m) by BINARITH:62;
  then A5: cell(Gauge(C,m),X-SpanStart(C,m)-'1,width Gauge(C,m)-'1) is non
empty
                                         by A1,JORDAN1A:45;
A6: cell(Gauge(C,m),X-SpanStart(C,m)-'1,width Gauge(C,m)) c= UBD C by A1,
JORDAN1A:71;
   set i1 = X-SpanStart(C,m);
A7: i1 < len Gauge(C,m) by JORDAN1H:58;
      width Gauge(C,m) <> 0 by GOBOARD1:def 5;
then A8: 0+1 <= width Gauge(C,m) by NAT_1:14;
        i1-'1 <= i1 by BINARITH:62;
then A9: i1-'1 < len Gauge(C,m) by A7,XXREAL_0:2;
A10:   width Gauge(C,m)-1 < width Gauge(C,m) by XREAL_1:148;
    then A11: width Gauge(C,m)-'1 < width Gauge(C,m) by A8,BINARITH:50;
A12: width Gauge(C,m)-'1+1 = width Gauge(C,m) by A8,BINARITH:53;
       1 <= i1-'1 by JORDAN1H:59;
   then cell(Gauge(C,m),i1-'1,width Gauge(C,m)) /\
        cell(Gauge(C,m),i1-'1,width Gauge(C,m)-'1)
    = LSeg(Gauge(C,m)*(i1-'1,width Gauge(C,m)),
           Gauge(C,m)*(i1-'1+1,width Gauge(C,m)))
              by A9,A10,A12,GOBOARD5:27;
    then A13:   cell(Gauge(C,m),i1-'1,width Gauge(C,m)) meets
                cell(Gauge(C,m),i1-'1,width Gauge(C,m)-'1) by XBOOLE_0:def 7;
    UBD C is_outside_component_of C by JORDAN2C:76;
   then A14:  UBD C is_a_component_of C` by JORDAN2C:def 4;
A15:  cell(Gauge(C,m),X-SpanStart(C,m)-'1,width Gauge(C,m)-'1) is connected
                              by A9,A11,JORDAN1A:46;
      BDD C c= C` by JORDAN2C:29;
  then cell(Gauge(C,m),X-SpanStart(C,m)-'1,width Gauge(C,m)-'1) c= C`
                               by A4,XBOOLE_1:1;
  then cell(Gauge(C,m),X-SpanStart(C,m)-'1,width Gauge(C,m)-'1)
                        c= UBD C by A6,A13,A14,A15,GOBOARD9:6;
  then UBD C meets BDD C by A4,A5,XBOOLE_1:68;
 hence contradiction by JORDAN2C:28;
end;
end;

definition
  let C,n such that
A1: n is_sufficiently_large_for C;
A2: n >= ApproxIndex C by A1,Def1;
    set i1 = X-SpanStart(C,n);
 func Y-SpanStart(C,n) -> Element of NAT means
:Def3: it <= width Gauge(C,n) &
   (for k st it <= k & k <= 2|^(n-'ApproxIndex C)*(Y-InitStart C-'2)+2
     holds cell(Gauge(C,n),X-SpanStart(C,n)-'1,k) c= BDD C) &
    for j st j <= width Gauge(C,n) &
     for k st j <= k & k <= 2|^(n-'ApproxIndex C)*(Y-InitStart C-'2)+2
       holds cell(Gauge(C,n),X-SpanStart(C,n)-'1,k) c= BDD C
    holds j >= it;
 existence
  proof
    set m = ApproxIndex C;
    defpred P[Element of NAT] means
     $1 <= width Gauge(C,n) &
     for k st $1 <= k & k <= 2|^(n-'m)*(Y-InitStart C-'2)+2
      holds cell(Gauge(C,n),i1-'1,k) c= BDD C;
    set X = { i: P[i]};
A3:    1 < Y-InitStart C by Th2;
A4:    Y-InitStart C+1 < width Gauge(C,m) by Th3;
    set j0 = 2|^(n-'m)*(Y-InitStart C-'2)+2;
        Y-InitStart C + 1 < 2|^m + (2+1) by A4,JORDAN1A:49;
    then Y-InitStart C + 1 < 2|^m + 2 + 1;
    then A5:  Y-InitStart C < 2|^m + 2 by XREAL_1:8;
        1+1 <= Y-InitStart C by A3,NAT_1:13;
    then Y-InitStart C-'2 < 2|^m by A5,BINARITH:72;
    then 2|^(n-'m)*(Y-InitStart C-'2) <= 2|^(n-'m)*(2|^m) by XREAL_1:66;
    then 2|^(n-'m)*(Y-InitStart C-'2) <= 2|^((n-'m)+m) by NEWTON:13;
    then A6:  2|^(n-'m)*(Y-InitStart C-'2) <= 2|^n by A2,BINARITH:53;
        2|^n <= 2|^n + 1 by NAT_1:11;
    then 2|^(n-'m)*(Y-InitStart C-'2) <= 2|^n + 1 by A6,XXREAL_0:2;
    then j0 <= 2|^n + 1+2 by XREAL_1:8;
    then j0 <= 2|^n + (1+2);
    then j0 <= len Gauge(C,n) by JORDAN8:def 1;
    then A7:  j0 <= width Gauge(C,n) by JORDAN8:def 1;
        now let j;
     assume j0 <= j & j <= 2|^(n-'m)*(Y-InitStart C-'2)+2;
      then A8:     j = j0 by XXREAL_0:1;
          1 <= 2|^(m-'1) by PREPOWER:19;
      then A9:    2+1 <= X-SpanStart(C,m) by XREAL_1:8;
A10:   len Gauge(C,m) = 2|^m + (2 + 1) by JORDAN8:def 1
           .= 2|^m + 2 + 1;
          m-'1 <= m by BINARITH:52;
      then 2|^(m-'1) <= 2|^m by PREPOWER:107;
      then 2|^(m-'1) + 2 <= 2|^m + 2 by XREAL_1:8;
      then A11:    X-SpanStart(C,m) < len Gauge(C,m) by A10,NAT_1:13;
A12:   m >= 1 by Th1;
      then A13:   n >= 1 by A2,XXREAL_0:2;
          (n-'m)+(m-'1) = (n-'m)+(m-1) by A12,BINARITH:50
        .= (n-'m)+ m-1
        .= n-1 by A2,BINARITH:53
        .= n-'1 by A13,BINARITH:50;
      then A14:    i1 = 2|^(n-'m)*(X-SpanStart(C,m)-2)+2 by NEWTON:13;
          Y-InitStart C >= 1+1 by A3,NAT_1:13;
      then j = 2|^(n-'m)*(Y-InitStart C-2)+2 by A8,BINARITH:50;
then A15:    cell(Gauge(C,n),i1-'1,j) c=
       cell(Gauge(C,m),X-SpanStart(C,m)-'1,Y-InitStart C)
          by A2,A3,A4,A9,A11,A14,JORDAN1A:69;
          cell(Gauge(C,m),X-SpanStart(C,m)-'1,Y-InitStart C) c= BDD C by Def2;
     hence cell(Gauge(C,n),i1-'1,j) c= BDD C by A15,XBOOLE_1:1;
    end;
    then A16:  j0 in X by A7;
        X is Subset of NAT from DOMAIN_1:sch 7;
    then reconsider X as non empty Subset of NAT by A16;
   take min X;
        min X in X by SEQ_4:def 6;
    then ex j st j = min X & P[j];
   hence P[min X];
   let j;
   assume P[j];
    then j in X;
   hence thesis by SEQ_4:def 6;
  end;
 uniqueness
  proof
    defpred definiens[Element of NAT] means
   $1 <= width Gauge(C,n) &
  (for i st $1 <= i & i <= 2|^(n-'ApproxIndex C)*(Y-InitStart C-'2)+2
   holds cell(Gauge(C,n),X-SpanStart(C,n)-'1,i) c= BDD C) &
  for j st j <= width Gauge(C,n) &
    for i st j <= i & i <= 2|^(n-'ApproxIndex C)*(Y-InitStart C-'2)+2
       holds cell(Gauge(C,n),X-SpanStart(C,n)-'1,i) c= BDD C
    holds j >= $1;
   let it1,it2 be Element of NAT;
   assume definiens[it1] & definiens[it2];
    then it1 <= it2 & it2 <= it1;
   hence thesis by XXREAL_0:1;
  end;
end;

theorem Th4:
 n is_sufficiently_large_for C implies
 X-SpanStart(C,n) = 2|^(n-'ApproxIndex C)*(X-SpanStart(C,ApproxIndex C)-2)+2
 proof
   set i1 = X-SpanStart(C,n), m = ApproxIndex C;
 assume n is_sufficiently_large_for C;
  then A1: n >=ApproxIndex C by Def1;
A2:   m >= 1 by Th1;
      then A3:   n >= 1 by A1,XXREAL_0:2;
          (n-'m)+(m-'1) = (n-'m)+(m-1) by A2,BINARITH:50
        .= (n-'m)+ m-1
        .= n-1 by A1,BINARITH:53
        .= n-'1 by A3,BINARITH:50;
   hence i1 = 2|^(n-'m)*(X-SpanStart(C,m)-2)+2 by NEWTON:13;
  end;

theorem Th5:
 n is_sufficiently_large_for C implies
  Y-SpanStart(C,n) <= 2|^(n-'ApproxIndex C)*(Y-InitStart C-'2)+2
proof set m = ApproxIndex C;
A1: X-SpanStart(C,m) > 2 by JORDAN1H:58;
 assume
A2: n is_sufficiently_large_for C;
  then A3: n >=ApproxIndex C by Def1;
  set i1 = X-SpanStart(C,n);
    set j0 = 2|^(n-'m)*(Y-InitStart C-'2)+2;
A4: Y-InitStart C+1 < width Gauge(C,m) by Th3;
A5: 1 < Y-InitStart C by Th2;
   then 1+1 <= Y-InitStart C by NAT_1:13;
   then A6: Y-InitStart C-'2 = Y-InitStart C-2 by BINARITH:50;
       Y-InitStart C < Y-InitStart C +1 by XREAL_1:31;
   then Y-InitStart C < width Gauge(C,m) by A4,XXREAL_0:2;
   then A7:  j0 <= width Gauge(C,n) by A3,A5,A6,JORDAN1A:53;
        now let j;
     assume j0 <= j & j <= 2|^(n-'m)*(Y-InitStart C-'2)+2;
      then A8:     j = j0 by XXREAL_0:1;
A9:   cell(Gauge(C,m),X-SpanStart(C,m)-'1,Y-InitStart C) c= BDD C by Def2;
A10:   2+1 <= X-SpanStart(C,m) by A1,NAT_1:13;
A11:   len Gauge(C,m) = 2|^m + 3 by JORDAN8:def 1;
         m-'1 <= m by BINARITH:62;
     then 2|^(m-'1) <= 2|^m by PREPOWER:107;
     then A12:    X-SpanStart(C,m) < len Gauge(C,m) by A11,XREAL_1:10;
         i1 = 2|^(n-'m)*(X-SpanStart(C,m)-2)+2 by A2,Th4;
     then cell(Gauge(C,n),i1-'1,j) c=
       cell(Gauge(C,m),X-SpanStart(C,m)-'1,Y-InitStart C)
        by A3,A4,A5,A6,A8,A10,A12,JORDAN1A:69;
     hence cell(Gauge(C,n),i1-'1,j) c= BDD C by A9,XBOOLE_1:1;
    end;
 hence Y-SpanStart(C,n) <= 2|^(n-'ApproxIndex C)*(Y-InitStart C-'2)+2
    by A2,A7,Def3;
end;

theorem Th6:
 n is_sufficiently_large_for C
  implies cell(Gauge(C,n),X-SpanStart(C,n)-'1,Y-SpanStart(C,n)) c= BDD C
proof assume
A1: n is_sufficiently_large_for C;
 then Y-SpanStart(C,n) <= 2|^(n-'ApproxIndex C)*(Y-InitStart C-'2)+2 by Th5;
 hence thesis by A1,Def3;
end;

theorem Th7:
 n is_sufficiently_large_for C implies
 1 < Y-SpanStart(C,n) & Y-SpanStart(C,n) <= width Gauge(C,n)
proof
 assume
A1: n is_sufficiently_large_for C;
 thus 1 < Y-SpanStart(C,n)
  proof
  A2: X-SpanStart(C,n)-'1 <= len Gauge(C,n) by JORDAN1H:59;
   assume
  A3: Y-SpanStart(C,n) <= 1;
    per cases by A3,NAT_1:26;
    suppose Y-SpanStart(C,n) = 0;
    then A4: cell(Gauge(C,n),X-SpanStart(C,n)-'1,0) c= BDD C by A1,Th6;
        0 <= width Gauge(C,n);
    then A5: cell(Gauge(C,n),X-SpanStart(C,n)-'1,0) is non empty by A2,
JORDAN1A:45;
        cell(Gauge(C,n),X-SpanStart(C,n)-'1,0) c= UBD C by A2,JORDAN1A:70;
    then UBD C meets BDD C by A4,A5,XBOOLE_1:68;
   hence contradiction by JORDAN2C:28;
    end;
    suppose Y-SpanStart(C,n) = 1;
     then A6:  cell(Gauge(C,n),X-SpanStart(C,n)-'1,1) c= BDD C by A1,Th6;
A7:    width Gauge(C,n) <> 0 by GOBOARD1:def 5;
    then A8: 0+1 <= width Gauge(C,n) by NAT_1:14;
    then A9: cell(Gauge(C,n),X-SpanStart(C,n)-'1,1) is non empty by A2,
JORDAN1A:45;
  A10: cell(Gauge(C,n),X-SpanStart(C,n)-'1,0) c= UBD C by A2,JORDAN1A:70;
     set i1 = X-SpanStart(C,n);
  A11: i1 < len Gauge(C,n) by JORDAN1H:58;
          i1-'1 <= i1 by BINARITH:62;
     then A12: i1-'1 < len Gauge(C,n) by A11,XXREAL_0:2;
  A13:  0 < width Gauge(C,n) by A7;
         1 <= i1-'1 by JORDAN1H:59;
     then cell(Gauge(C,n),i1-'1,0) /\ cell(Gauge(C,n),i1-'1,0+1)
      = LSeg(Gauge(C,n)*(i1-'1,0+1),Gauge(C,n)*(i1-'1+1,0+1))
                by A12,A13,GOBOARD5:27;
      then A14:   cell(Gauge(C,n),i1-'1,0) meets cell(Gauge(C,n),i1-'1,0+1)
      by XBOOLE_0:def 7;
      UBD C is_outside_component_of C by JORDAN2C:76;
     then A15:  UBD C is_a_component_of C` by JORDAN2C:def 4;
A16:  cell(Gauge(C,n),X-SpanStart(C,n)-'1,1) is connected by A8,A12,JORDAN1A:46
;
        BDD C c= C` by JORDAN2C:29;
    then cell(Gauge(C,n),X-SpanStart(C,n)-'1,1) c= C` by A6,XBOOLE_1:1;
    then cell(Gauge(C,n),X-SpanStart(C,n)-'1,1) c= UBD C by A10,A14,A15,A16,
GOBOARD9:6;
    then UBD C meets BDD C by A6,A9,XBOOLE_1:68;
   hence contradiction by JORDAN2C:28;
  end;
  end;
 thus Y-SpanStart(C,n) <= width Gauge(C,n) by A1,Def3;
end;

theorem
     n is_sufficiently_large_for C implies
 [X-SpanStart(C,n),Y-SpanStart(C,n)] in Indices Gauge(C,n)
proof
 assume
A1: n is_sufficiently_large_for C;
       1+1 < X-SpanStart(C,n) by JORDAN1H:58;
then A2: 1 < X-SpanStart(C,n) & X-SpanStart(C,n) < len Gauge(C,n) by
JORDAN1H:58,NAT_1:13;
      1 < Y-SpanStart(C,n) & Y-SpanStart(C,n) <= width Gauge(C,n) by A1,Th7;
 hence thesis by A2,GOBOARD7:10;
end;

theorem
     n is_sufficiently_large_for C implies
 [X-SpanStart(C,n)-'1,Y-SpanStart(C,n)] in Indices Gauge(C,n)
proof
 assume
A1: n is_sufficiently_large_for C;
A2: 1 <= X-SpanStart(C,n)-'1 & X-SpanStart(C,n)-'1 < len Gauge(C,n) by
JORDAN1H:59;
      1 < Y-SpanStart(C,n) & Y-SpanStart(C,n) <= width Gauge(C,n) by A1,Th7;
 hence thesis by A2,GOBOARD7:10;
end;

theorem
     n is_sufficiently_large_for C
  implies cell(Gauge(C,n),X-SpanStart(C,n)-'1,Y-SpanStart(C,n)-'1) meets C
proof
 assume
A1: n is_sufficiently_large_for C;
  set i1 = X-SpanStart(C,n);
A2: Y-SpanStart(C,n) <= width Gauge(C,n) by A1,Def3;
 assume A3: cell(Gauge(C,n),X-SpanStart(C,n)-'1,Y-SpanStart(C,n)-'1) misses C;
A4: Y-SpanStart(C,n)-'1 <= width Gauge(C,n) by A2,BINARITH:62;
A5: 1 < Y-SpanStart(C,n) by A1,Th7;
      for k st Y-SpanStart(C,n)-'1 <= k &
     k <= 2|^(n-'ApproxIndex C)*(Y-InitStart C-'2)+2
    holds cell(Gauge(C,n),i1-'1,k) c= BDD C
   proof let k such that
A6:  Y-SpanStart(C,n)-'1 <= k and
A7:  k <= 2|^(n-'ApproxIndex C)*(Y-InitStart C-'2)+2;
    per cases by A6,XXREAL_0:1;
    suppose
A8:   Y-SpanStart(C,n)-'1 = k;
     then A9:    cell(Gauge(C,n),i1-'1,k) c= C` by A3,SUBSET_1:43;
A10: i1 < len Gauge(C,n) by JORDAN1H:58;
        i1-'1 <= i1 by BINARITH:62;
   then A11: i1-'1 < len Gauge(C,n) by A10,XXREAL_0:2;
A12:  1 < Y-SpanStart(C,n) & Y-SpanStart(C,n) <= width Gauge(C,n) by A1,Th7;
    then A13:   k+1 = Y-SpanStart(C,n) by A8,BINARITH:53;
        k < k+1 by XREAL_1:31;
    then A14:  k < width Gauge(C,n) by A12,A13,XXREAL_0:2;
       1+1 < X-SpanStart(C,n) by JORDAN1H:58;
   then 1 <= i1-1 by XREAL_1:21;
   then 1 <= i1-'1 by BINARITH:57;
   then cell(Gauge(C,n),i1-'1,k) /\ cell(Gauge(C,n),i1-'1,k+1)
    = LSeg(Gauge(C,n)*(i1-'1,k+1),Gauge(C,n)*(i1-'1+1,k+1))
              by A11,A14,GOBOARD5:27;
    then A15:   cell(Gauge(C,n),i1-'1,k) meets cell(Gauge(C,n),i1-'1,k+1) by
XBOOLE_0:def 7;
        cell(Gauge(C,n),i1-'1,k+1) c= BDD C by A1,A13,Th6;
    then A16:  cell(Gauge(C,n),i1-'1,k) meets BDD C by A15,XBOOLE_1:63;
    set W = {B where B is Subset of TOP-REAL 2: B is_inside_component_of C};
A17:  BDD C = union W by JORDAN2C:def 5;
    then consider e being set such that
A18: e in W and
A19: cell(Gauge(C,n),i1-'1,k) meets e by A16,ZFMISC_1:98;
    consider B being Subset of TOP-REAL 2 such that
A20: e = B and
A21: B is_inside_component_of C by A18;
A22:   cell(Gauge(C,n),i1-'1,k) is connected by A11,A14,JORDAN1A:46;
         B is_a_component_of C` by A21,JORDAN2C:def 3;
     then A23:    cell(Gauge(C,n),i1-'1,k) c= B by A9,A19,A20,A22,GOBOARD9:6;
         B c= BDD C by A17,A18,A20,ZFMISC_1:92;
    hence cell(Gauge(C,n),i1-'1,k) c= BDD C by A23,XBOOLE_1:1;
    end;
    suppose Y-SpanStart(C,n)-'1 < k;
     then Y-SpanStart(C,n) < k+1 by BINARITH:73;
     then Y-SpanStart(C,n) <= k by NAT_1:13;
    hence cell(Gauge(C,n),i1-'1,k) c= BDD C by A1,A7,Def3;
   end;
   end;
  then A24:  Y-SpanStart(C,n)-'1 >=Y-SpanStart(C,n) by A1,A4,Def3;
       Y-SpanStart(C,n)-1 < Y-SpanStart(C,n) by XREAL_1:148;
 hence contradiction by A5,A24,BINARITH:50;
end;

theorem
     n is_sufficiently_large_for C
 implies cell(Gauge(C,n),X-SpanStart(C,n)-'1,Y-SpanStart(C,n)) misses C
proof assume n is_sufficiently_large_for C;
  then A1: cell(Gauge(C,n),X-SpanStart(C,n)-'1,Y-SpanStart(C,n)) c= BDD C by
Th6;
      BDD C misses C by JORDAN1A:15;
 hence thesis by A1,XBOOLE_1:63;
end;

:: Moved from JORDAN1K, AK 20.02.2006

begin :: BDD & UBD

reserve p,q for Point of TOP-REAL 2,
  D for Simple_closed_curve;

theorem Th12:
 UBD C meets UBD D
proof
  reconsider A = C as bounded Subset of Euclid 2 by JORDAN2C:def 2;
  consider r1 being Real, x1 being Point of Euclid 2 such that
       0 < r1 and
A1: A c= Ball(x1,r1) by METRIC_6:def 10;
  reconsider B = D as bounded Subset of Euclid 2 by JORDAN2C:def 2;
  consider r2 being Real, x2 being Point of Euclid 2 such that
        0 < r2 and
A2: B c= Ball(x2,r2) by METRIC_6:def 10;
  consider x3 being Point of Euclid 2, r3 being Real such that
A3: Ball(x1,r1) \/ Ball(x2,r2) c= Ball(x3,r3) by WEIERSTR:1;
        Ball(x3,r3)` c= (Ball(x1,r1) \/ Ball(x2,r2))` by A3,SUBSET_1:31;
   then A4:  Ball(x3,r3)` c= Ball(x1,r1)` /\ Ball(x2,r2)` by XBOOLE_1:53;
   then A5:  Ball(x3,r3)` c= Ball(x1,r1)` by XBOOLE_1:18;
A6: Ball(x3,r3)` c= Ball(x2,r2)` by A4,XBOOLE_1:18;
   reconsider C' = Ball(x1,r1)`, D' = Ball(x2,r2)`
       as connected Subset of TOP-REAL 2 by JORDAN1K:37;
     A7: Ball(x1,r1) is bounded by TBSP_1:19;
A8: now assume C' is Bounded;
     then C' is bounded Subset of Euclid 2 by JORDAN2C:def 2;
    hence contradiction by A7,JORDAN1K:8;
   end;
        Ball(x1,r1)` c= A` by A1,SUBSET_1:31;
   then Ball(x1,r1)` misses A by SUBSET_1:43;
   then C' c= UBD C by A8,JORDAN2C:133;
   then A9: Ball(x3,r3)` c= UBD C by A5,XBOOLE_1:1;
     A10: Ball(x2,r2) is bounded by TBSP_1:19;
A11: now assume D' is Bounded;
     then D' is bounded Subset of Euclid 2 by JORDAN2C:def 2;
    hence contradiction by A10,JORDAN1K:8;
   end;
        Ball(x2,r2)` c= B` by A2,SUBSET_1:31;
   then Ball(x2,r2)` misses B by SUBSET_1:43;
   then D' c= UBD D by A11,JORDAN2C:133;
   then A12: Ball(x3,r3)` c= UBD D by A6,XBOOLE_1:1;
       Ball(x3,r3) is bounded by TBSP_1:19;
  then Ball(x3,r3)` <> {}Euclid 2 by JORDAN1K:8;
 hence thesis by A9,A12,XBOOLE_1:68;
end;

theorem Th13:
 q in UBD C & p in BDD C implies dist(q,C) < dist(q,p)
proof assume that
A1: q in UBD C and
A2: p in BDD C and
A3: dist(q,C) >= dist(q,p);
       now assume LSeg(p,q) meets C;
    then consider x being set such that
A4:  x in LSeg(p,q) and
A5:  x in C by XBOOLE_0:3;
    reconsider x as Point of TOP-REAL 2 by A4;
A6:  dist(q,C) <= dist(q,x) by A5,JORDAN1K:50;
         C misses BDD C by JORDAN1A:15;
    then x <> p by A2,A5,XBOOLE_0:3;
    then dist(q,x) < dist(q,p) by A4,JORDAN1K:30;
   hence contradiction by A3,A6,XXREAL_0:2;
  end;
  then A7: LSeg(p,q) c= C` by SUBSET_1:43;
A8: UBD C is_a_component_of C` by JORDAN2C:132;
        q in LSeg(p,q) by TOPREAL1:6;
   then A9: LSeg(p,q) meets UBD C by A1,XBOOLE_0:3;
A10:
  BDD C = union{B where B is Subset of TOP-REAL 2: B is_inside_component_of C}
        by JORDAN2C:def 5;
  then consider X being set such that
A11: p in X and
A12: X in {B where B is Subset of TOP-REAL 2: B is_inside_component_of C}
         by A2,TARSKI:def 4;
  consider B being Subset of TOP-REAL 2 such that
A13: X = B and
A14: B is_inside_component_of C by A12;
A15: B is_a_component_of C` by A14,JORDAN2C:def 3;
       p in LSeg(p,q) by TOPREAL1:6;
  then LSeg(p,q) meets B by A11,A13,XBOOLE_0:3;
  then A16:  UBD C = B by A7,A8,A9,A15,JORDAN9:3;
A17: B c= BDD C by A10,A12,A13,ZFMISC_1:92;
       BDD C misses UBD C by JORDAN2C:28;
  then B misses UBD C by A17,XBOOLE_1:63;
 hence contradiction by A16,XBOOLE_1:66;
end;

registration let C;
 cluster BDD C -> non empty;
 coherence
  proof
   set n = ApproxIndex C,
       i = X-SpanStart(C,n)-'1, j = Y-SpanStart(C,n),
       B = cell(Gauge(C,n),i,j);
A1:  n is_sufficiently_large_for C by Def1;
    then A2:   B c= BDD C by Th6;
     i<=len Gauge(C,n) & j<=width Gauge(C,n) by A1,Def3,JORDAN1H:59;
   then B <> {} by JORDAN1A:45;
   hence thesis by A2,XBOOLE_1:3;
  end;
end;

theorem Th14:
 not p in BDD C implies dist(p,C) <= dist(p,BDD C)
 proof
  per cases;
  suppose p in C;
    then dist(p,C) = 0 by JORDAN1K:45;
   hence thesis by JORDAN1K:44;
  end;
  suppose not p in C;
   then p in C` by SUBSET_1:50;
   then A1:  p in (BDD C) \/ (UBD C) by JORDAN2C:31;
   assume that
A2: not p in BDD C and
A3: dist(p,C) > dist(p,BDD C);
A4:  p in UBD C by A1,A2,XBOOLE_0:def 2;
        ex q st q in BDD C & dist(p,q) < dist(p,C) by A3,JORDAN1K:48;
  hence contradiction by A4,Th13;
 end;
 end;

theorem Th15:
 not(C c= BDD D & D c= BDD C)
 proof assume that
A1: C c= BDD D and
A2: D c= BDD C;
        UBD C meets UBD D by Th12;
   then consider e being set such that
A3: e in UBD C and
A4: e in UBD D by XBOOLE_0:3;
   reconsider p = e as Point of TOP-REAL 2 by A3;
        UBD C misses BDD C by JORDAN2C:28;
   then A5:  not p in BDD C by A3,XBOOLE_0:3;
   then A6:   dist(p,BDD C) < dist(p,D) by A2,JORDAN1K:51;
        UBD D misses BDD D by JORDAN2C:28;
   then A7:  not p in BDD D by A4,XBOOLE_0:3;
   then A8:  dist(p,BDD D) < dist(p,C) by A1,JORDAN1K:51;
        dist(p,C) <= dist(p, BDD C) by A5,Th14;
   then dist(p, BDD D) < dist(p, BDD C) by A8,XXREAL_0:2;
   then dist(p, BDD D) < dist(p,D) by A6,XXREAL_0:2;
  hence contradiction by A7,Th14;
 end;

theorem Th16:
  C c= BDD D implies D c= UBD C
proof assume
A1: C c= BDD D;
       D misses BDD D by JORDAN1A:15;
  then A2: C misses D by A1,XBOOLE_1:63;
 assume not D c= UBD C;
  then D c= BDD C by A2,JORDAN1K:19;
 hence contradiction by A1,Th15;
end;

theorem
   L~Cage(C,n) c= UBD C
proof
   C c= BDD L~Cage(C,n) by JORDAN10:12;
  hence thesis by Th16;
end;

