:: Yoneda Embedding
::  by Miros{\l}aw Wojciechowski
::
:: Received June 12, 1997
:: Copyright (c) 1997 Association of Mizar Users
 
environ

 vocabularies CAT_1, ENS_1, FUNCT_1, GRAPH_1, RELAT_1, SUBSET_1, MCART_1,
      NATTRA_1, STRUCT_0, XBOOLE_0, PZFMISC1, TARSKI, OPPCAT_1, CAT_2, FUNCT_2,
      YONEDA_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, MCART_1, RELAT_1, FUNCT_1, BINOP_1,
      FUNCT_2, STRUCT_0, GRAPH_1, CAT_1, CAT_2, OPPCAT_1, ENS_1, NATTRA_1;
 constructors PARTFUN1, DOMAIN_1, NATTRA_1, ENS_1, RELSET_1;
 registrations XBOOLE_0, FUNCT_1, RELSET_1, ENS_1, STRUCT_0, FUNCT_2;
 requirements SUBSET, BOOLE;
 definitions TARSKI, BINOP_1, GRAPH_1;
 theorems ZFMISC_1, MCART_1, FUNCT_1, FUNCT_2, CAT_1, CAT_2, OPPCAT_1, ENS_1,
      NATTRA_1, ISOCAT_1, RELAT_1;
 schemes FUNCT_2;

begin

reserve y for set;
reserve A for Category,
  a,o for Object of A;
reserve f for Morphism of A;

definition
  let A;
  func EnsHom(A) -> Category equals
  Ens(Hom(A));
  correctness;
end;

theorem Th1:
  for f,g being Function, m1,m2 being Morphism of EnsHom A st cod
  m1 = dom m2 & [[dom m1,cod m1],f] = m1 & [[dom m2,cod m2],g] = m2 holds [[dom
  m1,cod m2],g*f] = m2*m1
proof
  let f,g be Function;
  let m1,m2 be Morphism of EnsHom A such that
A1: cod m1 = dom m2 and
A2: [[dom m1,cod m1],f] = m1 and
A3: [[dom m2,cod m2],g] = m2;
A4: EnsHom A = CatStr(# Hom A,Maps Hom A,fDom Hom A,fCod Hom A,fComp Hom A,
    fId Hom A #) by ENS_1:def 14;
  then reconsider m19=m1 as Element of Maps Hom A;
  reconsider m29=m2 as Element of Maps Hom A by A4;
A5: cod m19= m1`1`2 by ENS_1:def 5
    .=[dom m1,cod m1]`2 by A2,MCART_1:7
    .=dom m2 by A1,MCART_1:7
    .=[dom m2,cod m2]`1 by MCART_1:7
    .=m2`1`1 by A3,MCART_1:7
    .= dom m29 by ENS_1:def 4;
  [[dom m19,cod m19],m19`2] =m1 by ENS_1:8;
  then
A6: m19`2=f by A2,ZFMISC_1:33;
  [[dom m29,cod m29],m29`2] =m2 by ENS_1:8;
  then
A7: m29`2= g by A3,ZFMISC_1:33;
A8: cod m2 =[dom m2,cod m2]`2 by MCART_1:7
    .=m2`1`2 by A3,MCART_1:7
    .= cod m29 by ENS_1:def 5;
A9: dom m19= m1`1`1 by ENS_1:def 4
    .=[dom m1,cod m1]`1 by A2,MCART_1:7
    .=dom m1 by MCART_1:7;
  [m2,m1] in dom(the Comp of EnsHom A) by A1,CAT_1:40;
  then m2*m1 = (fComp Hom A).(m29,m19) by A4,CAT_1:def 4
    .= m29*m19 by A5,ENS_1:def 12
    .= [[dom m1,cod m2],g*f] by A5,A9,A8,A7,A6,ENS_1:def 7;
  hence thesis;
end;

definition
  let A,a;
  func <|a,?> -> Functor of A,EnsHom(A) equals
  hom?-(a);
  coherence by ENS_1:49;
end;

canceled;

theorem Th3:
  for f being Morphism of A holds <|cod f,?>
  is_naturally_transformable_to <|dom f,?>
proof
  let f;
  set F1=<|cod f,?> ,F2=<|dom f,?>;
  set B = EnsHom(A);
  deffunc F(Element of A) = [[Hom(cod f,$1),Hom(dom f,$1)],hom(
  f,$1)];
A1: for a being Object of A holds [[Hom(cod f,a),Hom(dom f,a)],hom(f,a)] in
  Hom(F1.a,F2.a)
  proof
    let a be Object of A;
A2: EnsHom A = CatStr(# Hom A,Maps Hom A,fDom Hom A,fCod Hom A, fComp Hom
      A,fId Hom A #) by ENS_1:def 14;
    then reconsider m = [[Hom(cod f,a),Hom(dom f,a)],hom(f,a)] as Morphism of
    EnsHom A by ENS_1:48;
    reconsider m9=m as Element of Maps Hom A by ENS_1:48;
A3: cod(m)= (fCod Hom A).m by A2
      .= cod m9 by ENS_1:def 11
      .= m`1`2 by ENS_1:def 5
      .= [Hom(cod f,a),Hom(dom f,a)]`2 by MCART_1:7
      .= Hom(dom f,a) by MCART_1:7
      .= (Obj (hom?-(Hom A,dom f))).a by ENS_1:60
      .= (hom?-(Hom A,dom f)).a by CAT_1:def 20
      .= F2.a by ENS_1:def 26;
    dom(m)=(fDom Hom A).m by A2
      .= dom m9 by ENS_1:def 10
      .= m`1`1 by ENS_1:def 4
      .= [Hom(cod f,a),Hom(dom f,a)]`1 by MCART_1:7
      .= Hom(cod f,a) by MCART_1:7
      .= (Obj (hom?-(Hom A,cod f))).a by ENS_1:60
      .= (hom?-(Hom A,cod f)).a by CAT_1:def 20
      .=F1.a by ENS_1:def 26;
    hence thesis by A3,CAT_1:18;
  end;
A4: for a being Element of A holds F(a) in the carrier' of B
  proof
    let a;
    [[Hom(cod f,a),Hom(dom f,a)],hom(f,a)] in Hom(F1.a,F2.a) by A1;
    hence thesis;
  end;
  consider t being Function of the carrier of A, the carrier' of B such that
A5: for o being Element of A holds t.o = F(o) from
  FUNCT_2:sch 8(A4);
A6: for a being Object of A holds t.a is Morphism of F1.a,F2.a
  proof
    let a;
    [[Hom(cod f,a),Hom(dom f,a)],hom(f,a)] in Hom(F1.a,F2.a) by A1;
    then [[Hom(cod f,a),Hom(dom f,a)],hom(f,a)] is Morphism of F1.a,F2.a by
    CAT_1:def 7;
    hence thesis by A5;
  end;
  for a being Object of A holds Hom(F1.a,F2.a) <> {} by A1;
  then
A7: F1 is_transformable_to F2 by NATTRA_1:def 2;
  then reconsider t as transformation of F1,F2 by A6,NATTRA_1:def 3;
  for a,b being Object of A st Hom(a,b) <> {} for g being Morphism of a,b
  holds t.b*F1.g = F2.g*t.a
  proof
    let a,b be Object of A such that
A8: Hom(a,b) <> {};
A9: Hom(F1.a,F1.b)<>{} by A8,CAT_1:126;
    let g be Morphism of a,b;
A10: dom g =a by A8,CAT_1:23;
A11: rng hom(cod f,g) c= dom hom(f,b)
    proof
A12:  cod g =b by A8,CAT_1:23;
      per cases;
      suppose
A13:    Hom(dom f,b) = {};
A14:    rng hom(cod f,g) c= Hom(cod f,cod g) by RELAT_1:def 19;
        Hom(cod f,b) = {} by A13,ENS_1:42;
        hence thesis by A12,A14,FUNCT_2:def 1;
      end;
      suppose
A15:    Hom(dom f,b) <> {};
        cod g = b by A8,CAT_1:23;
        then
A16:    rng hom(cod f,g) c= Hom(cod f,cod g) & Hom(cod f,cod g)=dom hom(f
        ,b) by A15,FUNCT_2:def 1,RELAT_1:def 19;
        let e be set;
        assume e in rng hom(cod f,g);
        hence thesis by A16;
      end;
    end;
A17: rng hom(f,a) c= dom hom(dom f,g)
    proof
A18:  dom g =a by A8,CAT_1:23;
      per cases;
      suppose
A19:    Hom(dom f,cod g) = {};
A20:    rng hom(f,a) c= Hom(dom f,a) by RELAT_1:def 19;
        Hom(dom f,dom g) = {} by A19,ENS_1:42;
        hence thesis by A18,A20,FUNCT_2:def 1;
      end;
      suppose
A21:    Hom(dom f,cod g) <> {};
        let e be set;
        assume
A22:    e in rng hom(f,a);
        rng hom(f,a) c= Hom(dom f,a) & Hom(dom f,a) = dom hom(dom f,g) by
        A18,A21, FUNCT_2:def 1,RELAT_1:def 19;
        hence thesis by A22;
      end;
    end;
A23: dom (hom(f,b)*hom(cod f,g)) = dom(hom(dom f,g)*hom(f,a))
    proof
      per cases;
      suppose
A24:    Hom(cod f,dom g)= {};
        dom (hom(f,b)*hom(cod f,g))=dom (hom(cod f,g)) by A11,RELAT_1:46
          .=Hom(cod f,dom g) by A24,FUNCT_2:def 1
          .=dom (hom(f,a)) by A10,A24,FUNCT_2:def 1
          .=dom(hom(dom f,g)*hom(f,a)) by A17,RELAT_1:46;
        hence thesis;
      end;
      suppose
A25:    Hom(cod f,dom g) <> {};
        then
A26:    Hom(cod f,cod g) <> {} by ENS_1:42;
A27:    Hom(dom f,a) <> {} by A10,A25,ENS_1:42;
        dom (hom(f,b)*hom(cod f,g))=dom (hom(cod f,g)) by A11,RELAT_1:46
          .=Hom(cod f,dom g) by A26,FUNCT_2:def 1
          .=Hom(cod f,a) by A8,CAT_1:23
          .=dom (hom(f,a)) by A27,FUNCT_2:def 1
          .=dom(hom(dom f,g)*hom(f,a)) by A17,RELAT_1:46;
        hence thesis;
      end;
    end;
A28: for x being set st x in dom (hom(f,b)*hom(cod f,g)) holds (hom(f,b)*
    hom(cod f,g)).x = (hom(dom f,g)*hom(f,a)).x
    proof
      let x be set such that
A29:  x in dom (hom(f,b)*hom(cod f,g));
      per cases;
      suppose
A30:    Hom(cod f,dom g) <> {};
A31:    x in dom hom(cod f,g) by A29,FUNCT_1:21;
        Hom(cod f,cod g) <> {} by A30,ENS_1:42;
        then
A32:    x in Hom(cod f,dom g) by A31,FUNCT_2:def 1;
        then reconsider x as Morphism of A;
A33:    dom(g) = cod(x) & dom(x) = cod(f) by A32,CAT_1:18;
A34:    dom(g) = cod(x) by A32,CAT_1:18;
        then
A35:    cod(g*x)=cod(g) by CAT_1:42
          .=b by A8,CAT_1:23;
A36:    hom(f,a).x =x*f by A10,A32,ENS_1:def 21;
        then reconsider h=hom(f,a).x as Morphism of A;
A37:    dom(x) = cod(f) by A32,CAT_1:18;
        then
A38:    dom(x*f) =dom f by CAT_1:42;
        dom(g*x) =dom x by A34,CAT_1:42
          .=cod f by A32,CAT_1:18;
        then
A39:    g*x in Hom(cod f,b) by A35,CAT_1:18;
        cod(x*f)=cod(x) by A37,CAT_1:42
          .=dom g by A32,CAT_1:18;
        then
A40:    hom(f,a).x in Hom(dom f,dom g) by A36,A38,CAT_1:18;
        (hom(f,b)*hom(cod f,g)).x = hom(f,b).(hom(cod f,g).x) by A29,
        FUNCT_1:22
          .=hom(f,b).(g*x) by A32,ENS_1:def 20
          .=(g*x)*f by A39,ENS_1:def 21
          .= g*(x*f) by A33,CAT_1:43
          .= g*(h) by A10,A32,ENS_1:def 21
          .= hom(dom f,g).(hom(f,a).x) by A40,ENS_1:def 20
          .= (hom(dom f,g)*hom(f,a)).x by A23,A29,FUNCT_1:22;
        hence thesis;
      end;
      suppose
A41:    Hom(cod f,dom g) = {};
        x in dom hom(cod f,g) by A29,FUNCT_1:21;
        hence thesis by A41,FUNCT_2:def 1;
      end;
    end;
A42: Hom(F2.a,F2.b)<>{} by A8,CAT_1:126;
A43: cod g =b by A8,CAT_1:23;
    reconsider f4 = t.a as Morphism of EnsHom A;
A44: t.a = (t qua Function of the carrier of A, the carrier' of B).a by A7
    ,NATTRA_1:def 5
      .= [[Hom(cod f,a),Hom(dom f,a)],hom(f,a)] by A5;
    then reconsider f49=f4 as Element of Maps Hom A by ENS_1:48;
A45: Hom(F1.a,F2.a)<>{} by A1;
    reconsider f1=t.b as Morphism of EnsHom A;
A46: t.b= (t qua Function of the carrier of A, the carrier' of B).b by A7,
    NATTRA_1:def 5
      .= [[Hom(cod f,b),Hom(dom f,b)],hom(f,b)] by A5;
    then reconsider f19=f1 as Element of Maps Hom A by ENS_1:48;
A47: EnsHom A = CatStr(# Hom A,Maps Hom A,fDom Hom A,fCod Hom A, fComp Hom
      A,fId Hom A #) by ENS_1:def 14;
    then
A48: cod(f1) = (fCod Hom A).f1 .= cod f19 by ENS_1:def 11
      .= f1`1`2 by ENS_1:def 5
      .= [Hom(cod f,b),Hom(dom f,b)]`2 by A46,MCART_1:7
      .= Hom(dom f,b) by MCART_1:7;
A49: dom(f4) = (fDom Hom A).f4 by A47
      .= dom f49 by ENS_1:def 10
      .= f4`1`1 by ENS_1:def 4
      .= [Hom(cod f,a),Hom(dom f,a)]`1 by A44,MCART_1:7
      .=Hom(cod f,a) by MCART_1:7;
A50: cod(f4) = (fCod Hom A).f4 by A47
      .= cod f49 by ENS_1:def 11
      .= f4`1`2 by ENS_1:def 5
      .= [Hom(cod f,a),Hom(dom f,a)]`2 by A44,MCART_1:7
      .= Hom(dom f,a) by MCART_1:7;
    reconsider f2=F1.g as Morphism of EnsHom A;
A51: f2 = (hom?-(cod f)).g by A8,NATTRA_1:def 1
      .=[[Hom(cod f,dom g),Hom(cod f,cod g)],hom(cod f,g)] by ENS_1:def 22;
    then reconsider f29=f2 as Element of Maps Hom A by ENS_1:47;
A52: dom(f2) = (fDom Hom A).f2 by A47
      .= dom f29 by ENS_1:def 10
      .= f2`1`1 by ENS_1:def 4
      .= [Hom(cod f,dom g),Hom(cod f,cod g)]`1 by A51,MCART_1:7
      .= Hom(cod f,dom g) by MCART_1:7;
A53: cod(f2) = (fCod Hom A).f2 by A47
      .= cod f29 by ENS_1:def 11
      .= f2`1`2 by ENS_1:def 5
      .= [Hom(cod f,dom g),Hom(cod f,cod g)]`2 by A51,MCART_1:7
      .= Hom(cod f,cod g) by MCART_1:7;
A54: dom(f1) =(fDom Hom A).f1 by A47
      .= dom f19 by ENS_1:def 10
      .= f1`1`1 by ENS_1:def 4
      .=[Hom(cod f,b),Hom(dom f,b)]`1 by A46,MCART_1:7
      .=Hom(cod f,b) by MCART_1:7;
    then
A55: cod f2 = dom f1 by A8,A53,CAT_1:23;
    reconsider f3 = F2.g as Morphism of EnsHom A;
A56: f3= (hom?-(dom f)).g by A8,NATTRA_1:def 1
      .=[[Hom(dom f,dom g),Hom(dom f,cod g)],hom(dom f,g)] by ENS_1:def 22;
    then reconsider f39=f3 as Element of Maps Hom A by ENS_1:47;
A57: cod(f3) = (fCod Hom A).f3 by A47
      .= cod f39 by ENS_1:def 11
      .= f3`1`2 by ENS_1:def 5
      .= [Hom(dom f,dom g),Hom(dom f,cod g)]`2 by A56,MCART_1:7
      .= Hom(dom f,cod g) by MCART_1:7;
A58: dom(f3) =(fDom Hom A).f3 by A47
      .= dom f39 by ENS_1:def 10
      .= f3`1`1 by ENS_1:def 4
      .= [Hom(dom f,dom g),Hom(dom f,cod g)]`1 by A56,MCART_1:7
      .= Hom(dom f,dom g) by MCART_1:7;
    then
A59: cod f4 = dom f3 by A8,A50,CAT_1:23;
    Hom(F1.b,F2.b)<>{} by A1;
    then t.b*F1.g = f1*f2 by A9,CAT_1:def 13
      .= [[Hom(cod f,dom g),Hom(dom f,b)],hom(f,b)*hom(cod f,g)] by A46,A54,
    A48,A51,A52,A53,A55,Th1
      .= [[ Hom(cod f,a),Hom(dom f,cod g)],hom(dom f,g)*hom(f,a)] by A10,A43
    ,A23,A28,FUNCT_1:9
      .= f3*f4 by A56,A58,A57,A44,A49,A50,A59,Th1
      .= F2.g*t.a by A42,A45,CAT_1:def 13;
    hence thesis;
  end;
  hence thesis by A7,NATTRA_1:def 7;
end;

definition
  let A,f;
  func <|f,?> -> natural_transformation of <|cod f,?>, <|dom f,?> means
  :Def3:
  for o be Object of A holds it.o = [[Hom(cod f,o),Hom(dom f,o)],hom(f,id o)];
  existence
  proof
    set B=EnsHom A;
    deffunc F(Element of A) = [[Hom(cod f,$1),Hom(dom f,$1)],
    hom(f,id $1)];
    set F1=<|cod f,?> , F2=<|dom f,?>;
A1: for o being Object of A holds [[Hom(cod f,o),Hom(dom f,o)],hom(f,id o)
    ] in Hom(F1.o,F2.o)
    proof
      let o be Object of A;
A2:   EnsHom A = CatStr(# Hom A,Maps Hom A,fDom Hom A,fCod Hom A, fComp
        Hom A,fId Hom A #) by ENS_1:def 14;
A3:   hom(f,id o)=hom(f,o) by ENS_1:53;
      then reconsider
      m = [[Hom(cod f,o),Hom(dom f,o)],hom(f,id o)] as Morphism of
      EnsHom A by A2,ENS_1:48;
      reconsider m9=m as Element of Maps Hom A by A3,ENS_1:48;
A4:   cod(m) = (fCod Hom A).m by A2
        .= cod m9 by ENS_1:def 11
        .= m`1`2 by ENS_1:def 5
        .= [Hom(cod f,o),Hom(dom f,o)]`2 by MCART_1:7
        .= Hom(dom f,o) by MCART_1:7
        .= (Obj (hom?-(Hom A,dom f))).o by ENS_1:60
        .= (hom?-(Hom A,dom f)).o by CAT_1:def 20
        .=F2.o by ENS_1:def 26;
      dom(m) = (fDom Hom A).m by A2
        .= dom m9 by ENS_1:def 10
        .= m`1`1 by ENS_1:def 4
        .= [Hom(cod f,o),Hom(dom f,o)]`1 by MCART_1:7
        .= Hom(cod f,o) by MCART_1:7
        .= (Obj (hom?-(Hom A,cod f))).o by ENS_1:60
        .= (hom?-(Hom A,cod f)).o by CAT_1:def 20
        .= F1.o by ENS_1:def 26;
      hence thesis by A4,CAT_1:18;
    end;
A5: for o being Element of A holds F(o) in the carrier' of EnsHom(A)
    proof
      let o;
      [[Hom(cod f,o),Hom(dom f,o)],hom(f,id o)] in Hom(F1.o,F2.o) by A1;
      hence thesis;
    end;
    consider t being Function of the carrier of A, the carrier' of EnsHom(A)
    such that
A6: for o being Element of A holds t.o = F(o) from
    FUNCT_2:sch 8(A5);
A7: for o being Object of A holds t.o is Morphism of F1.o,F2.o
    proof
      let o;
      [[Hom(cod f,o),Hom(dom f,o)],hom(f,id o)] in Hom(F1.o,F2.o) by A1;
      then [[Hom(cod f,o),Hom(dom f,o)],hom(f,id o)] is Morphism of F1.o,F2.o
      by CAT_1:def 7;
      hence thesis by A6;
    end;
    for o being Object of A holds Hom(F1.o,F2.o) <> {} by A1;
    then
A8: F1 is_transformable_to F2 by NATTRA_1:def 2;
    then reconsider t as transformation of F1,F2 by A7,NATTRA_1:def 3;
A9: for a,b being Object of A st Hom(a,b) <> {} for g being Morphism of a
    ,b holds t.b*F1.g = F2.g*t.a
    proof
      let a,b be Object of A such that
A10:  Hom(a,b) <> {};
A11:  Hom(F1.a,F1.b)<>{} by A10,CAT_1:126;
      let g be Morphism of a,b;
A12:  dom g =a by A10,CAT_1:23;
A13:  rng hom(cod f,g) c= dom hom(f,b)
      proof
A14:    cod g =b by A10,CAT_1:23;
        per cases;
        suppose
A15:      Hom(dom f,b) = {};
A16:      rng hom(cod f,g) c= Hom(cod f,cod g) by RELAT_1:def 19;
          Hom(cod f,b) = {} by A15,ENS_1:42;
          hence thesis by A14,A16,FUNCT_2:def 1;
        end;
        suppose
A17:      Hom(dom f,b) <> {};
          cod g =b by A10,CAT_1:23;
          then
A18:      rng hom(cod f,g) c= Hom(cod f,cod g) & Hom(cod f,cod g)=dom hom
          (f,b) by A17,FUNCT_2:def 1,RELAT_1:def 19;
          let e be set;
          assume e in rng hom(cod f,g);
          hence thesis by A18;
        end;
      end;
A19:  rng hom(f,a) c= dom hom(dom f,g)
      proof
A20:    dom g =a by A10,CAT_1:23;
        per cases;
        suppose
A21:      Hom(dom f,cod g) = {};
A22:      rng hom(f,a) c= Hom(dom f,a) by RELAT_1:def 19;
          Hom(dom f,dom g) = {} by A21,ENS_1:42;
          hence thesis by A20,A22,FUNCT_2:def 1;
        end;
        suppose
A23:      Hom(dom f,cod g) <> {};
          let e be set;
          assume
A24:      e in rng hom(f,a);
          rng hom(f,a) c= Hom(dom f,a) & Hom(dom f,a)=dom hom(dom f,g) by
          A20,A23, FUNCT_2:def 1,RELAT_1:def 19;
          hence thesis by A24;
        end;
      end;
A25:  dom (hom(f,b)*hom(cod f,g)) = dom(hom(dom f,g)*hom(f,a))
      proof
        per cases;
        suppose
A26:      Hom(cod f,dom g)= {};
          dom (hom(f,b)*hom(cod f,g)) = dom (hom(cod f,g)) by A13,
          RELAT_1:46
            .=Hom(cod f,a) by A12,A26,FUNCT_2:def 1
            .=dom (hom(f,a)) by A12,A26,FUNCT_2:def 1
            .=dom(hom(dom f,g)*hom(f,a)) by A19,RELAT_1:46;
          hence thesis;
        end;
        suppose
A27:      Hom(cod f,dom g) <> {};
          then
A28:      Hom(cod f,cod g) <> {} by ENS_1:42;
A29:      Hom(dom f,a) <> {} by A12,A27,ENS_1:42;
          dom (hom(f,b)*hom(cod f,g)) = dom (hom(cod f,g)) by A13,
          RELAT_1:46
            .=Hom(cod f,a) by A12,A28,FUNCT_2:def 1
            .=dom (hom(f,a)) by A29,FUNCT_2:def 1
            .=dom(hom(dom f,g)*hom(f,a)) by A19,RELAT_1:46;
          hence thesis;
        end;
      end;
A30:  for x being set st x in dom (hom(f,b)*hom(cod f,g)) holds (hom(f,b)
      *hom(cod f,g)).x = (hom(dom f,g)*hom(f,a)).x
      proof
        let x be set such that
A31:    x in dom (hom(f,b)*hom(cod f,g));
        per cases;
        suppose
A32:      Hom(cod f,dom g) <> {};
A33:      x in dom hom(cod f,g) by A31,FUNCT_1:21;
          Hom(cod f,cod g) <> {} by A32,ENS_1:42;
          then
A34:      x in Hom(cod f,dom g) by A33,FUNCT_2:def 1;
          then reconsider x as Morphism of A;
A35:      dom(g) = cod(x) by A34,CAT_1:18;
          cod g =b by A10,CAT_1:23;
          then
A36:      cod(g*x)=b by A35,CAT_1:42;
          dom(g*x) =dom x by A35,CAT_1:42
            .=cod f by A34,CAT_1:18;
          then
A37:      g*x in Hom(cod f,b) by A36,CAT_1:18;
A38:      dom(x) = cod(f) by A34,CAT_1:18;
          then
A39:      dom(x*f) =dom f by CAT_1:42;
A40:      hom(f,a).x =x*f by A12,A34,ENS_1:def 21;
          then reconsider h=hom(f,a).x as Morphism of A;
A41:      dom(g) = cod(x) & dom(x) = cod(f) by A34,CAT_1:18;
          cod(x*f)=cod(x) by A38,CAT_1:42
            .=dom g by A34,CAT_1:18;
          then
A42:      hom(f,a).x in Hom(dom f,dom g) by A40,A39,CAT_1:18;
          (hom(f,b)*hom(cod f,g)).x = hom(f,b).(hom(cod f,g).x) by A31,
          FUNCT_1:22
            .=hom(f,b).(g*x) by A34,ENS_1:def 20
            .=(g*x)*f by A37,ENS_1:def 21
            .= g*(x*f) by A41,CAT_1:43
            .= g*(h) by A12,A34,ENS_1:def 21
            .= hom(dom f,g).(hom(f,a).x) by A42,ENS_1:def 20
            .= (hom(dom f,g)*hom(f,a)).x by A25,A31,FUNCT_1:22;
          hence thesis;
        end;
        suppose
A43:      Hom(cod f,dom g) = {};
          x in dom hom(cod f,g) by A31,FUNCT_1:21;
          hence thesis by A43,FUNCT_2:def 1;
        end;
      end;
A44:  Hom(F2.a,F2.b)<>{} by A10,CAT_1:126;
A45:  cod g =b by A10,CAT_1:23;
      reconsider f4=t.a as Morphism of EnsHom A;
A46:  t.a = (t qua Function of the carrier of A, the carrier' of B).a by
      A8,NATTRA_1:def 5
        .= [[Hom(cod f,a),Hom(dom f,a)],hom(f,id a)] by A6
        .= [[Hom(cod f,a),Hom(dom f,a)],hom(f,a)] by ENS_1:53;
      then reconsider f49=f4 as Element of Maps Hom A by ENS_1:48;
A47:  Hom(F1.a,F2.a)<>{} by A1;
      reconsider f1=t.b as Morphism of EnsHom A;
A48:  t.b = (t qua Function of the carrier of A, the carrier' of B).b by
      A8,NATTRA_1:def 5
        .= [[Hom(cod f,b),Hom(dom f,b)],hom(f,id b)] by A6
        .=[[Hom(cod f,b),Hom(dom f,b)],hom(f,b)] by ENS_1:53;
      then reconsider f19=f1 as Element of Maps Hom A by ENS_1:48;
A49:  EnsHom A = CatStr(# Hom A,Maps Hom A,fDom Hom A,fCod Hom A, fComp
        Hom A,fId Hom A #) by ENS_1:def 14;
      then
A50:  cod(f1) = (fCod Hom A).f1 .= cod f19 by ENS_1:def 11
        .= f1`1`2 by ENS_1:def 5
        .= [Hom(cod f,b),Hom(dom f,b)]`2 by A48,MCART_1:7
        .= Hom(dom f,b) by MCART_1:7;
A51:  dom(f4) =(fDom Hom A).f4 by A49
        .= dom f49 by ENS_1:def 10
        .= f4`1`1 by ENS_1:def 4
        .=[Hom(cod f,a),Hom(dom f,a)]`1 by A46,MCART_1:7
        .=Hom(cod f,a) by MCART_1:7;
A52:  cod(f4) = (fCod Hom A).f4 by A49
        .= cod f49 by ENS_1:def 11
        .= f4`1`2 by ENS_1:def 5
        .= [Hom(cod f,a),Hom(dom f,a)]`2 by A46,MCART_1:7
        .= Hom(dom f,a) by MCART_1:7;
      reconsider f2=F1.g as Morphism of EnsHom A;
A53:  f2 = (hom?-(cod f)).g by A10,NATTRA_1:def 1
        .=[[Hom(cod f,dom g),Hom(cod f,cod g)],hom(cod f,g)] by ENS_1:def 22;
      then reconsider f29=f2 as Element of Maps Hom A by ENS_1:47;
A54:  dom(f2) =(fDom Hom A).f2 by A49
        .= dom f29 by ENS_1:def 10
        .= f2`1`1 by ENS_1:def 4
        .=[Hom(cod f,dom g),Hom(cod f,cod g)]`1 by A53,MCART_1:7
        .=Hom(cod f,dom g) by MCART_1:7;
A55:  cod(f2) = (fCod Hom A).f2 by A49
        .= cod f29 by ENS_1:def 11
        .= f2`1`2 by ENS_1:def 5
        .= [Hom(cod f,dom g),Hom(cod f,cod g)]`2 by A53,MCART_1:7
        .= Hom(cod f,cod g) by MCART_1:7;
A56:  dom(f1) = (fDom Hom A).f1 by A49
        .= dom f19 by ENS_1:def 10
        .= f1`1`1 by ENS_1:def 4
        .= [Hom(cod f,b),Hom(dom f,b)]`1 by A48,MCART_1:7
        .= Hom(cod f,b) by MCART_1:7;
      then
A57:  cod f2 = dom f1 by A10,A55,CAT_1:23;
      reconsider f3=F2.g as Morphism of EnsHom A;
A58:  f3 = (hom?-(dom f)).g by A10,NATTRA_1:def 1
        .=[[Hom(dom f,dom g),Hom(dom f,cod g)],hom(dom f,g)] by ENS_1:def 22;
      then reconsider f39=f3 as Element of Maps Hom A by ENS_1:47;
A59:  cod(f3) = (fCod Hom A).f3 by A49
        .= cod f39 by ENS_1:def 11
        .= f3`1`2 by ENS_1:def 5
        .= [Hom(dom f,dom g),Hom(dom f,cod g)]`2 by A58,MCART_1:7
        .= Hom(dom f,cod g) by MCART_1:7;
A60:  dom(f3) =(fDom Hom A).f3 by A49
        .= dom f39 by ENS_1:def 10
        .= f3`1`1 by ENS_1:def 4
        .=[Hom(dom f,dom g),Hom(dom f,cod g)]`1 by A58,MCART_1:7
        .=Hom(dom f,dom g) by MCART_1:7;
      then
A61:  cod f4 = dom f3 by A10,A52,CAT_1:23;
      Hom(F1.b,F2.b)<>{} by A1;
      then t.b*F1.g = f1*f2 by A11,CAT_1:def 13
        .=[[Hom(cod f,dom g),Hom(dom f,b)],hom(f,b)*hom(cod f,g)] by A48,A56
      ,A50,A53,A54,A55,A57,Th1
        .=[[ Hom(cod f,a),Hom(dom f,cod g)],hom(dom f,g)*hom(f,a)] by A12,
      A45,A25,A30,FUNCT_1:9
        .=f3*f4 by A58,A60,A59,A46,A51,A52,A61,Th1
        .= F2.g*t.a by A44,A47,CAT_1:def 13;
      hence thesis;
    end;
    F1 is_naturally_transformable_to F2 by Th3;
    then reconsider t as natural_transformation of F1,F2 by
    A9,NATTRA_1:def 8;
    for o be Element of A holds t.o= [[Hom(cod f,o),Hom( dom f,o)],hom(f,id o)]
    proof
      let o;
      t.o =(t qua Function).o by A8,NATTRA_1:def 5
        .= [[Hom(cod f,o),Hom(dom f,o)],hom(f,id o)] by A6;
      hence thesis;
    end;
    hence thesis;
  end;
  uniqueness
  proof
    for a being Object of A holds [[Hom(cod f,a),Hom(dom f,a)],hom(f,a)]
    in Hom(<|cod f,?>.a,<|dom f,?>.a)
    proof
      let a be Object of A;
A62:  EnsHom A = CatStr(# Hom A,Maps Hom A,fDom Hom A,fCod Hom A, fComp
        Hom A,fId Hom A #) by ENS_1:def 14;
      then reconsider
      m = [[Hom(cod f,a),Hom(dom f,a)],hom(f,a)] as Morphism of
      EnsHom A by ENS_1:48;
      reconsider m9=m as Element of Maps Hom A by ENS_1:48;
A63:  cod(m) = (fCod Hom A).m by A62
        .= cod m9 by ENS_1:def 11
        .= m`1`2 by ENS_1:def 5
        .= [Hom(cod f,a),Hom(dom f,a)]`2 by MCART_1:7
        .= Hom(dom f,a) by MCART_1:7
        .= (Obj (hom?-(Hom A,dom f))).a by ENS_1:60
        .= (hom?-(Hom A,dom f)).a by CAT_1:def 20
        .=<|dom f,?>.a by ENS_1:def 26;
      dom(m) =(fDom Hom A).m by A62
        .= dom m9 by ENS_1:def 10
        .= m`1`1 by ENS_1:def 4
        .=[Hom(cod f,a),Hom(dom f,a)]`1 by MCART_1:7
        .=Hom(cod f,a) by MCART_1:7
        .= (Obj (hom?-(Hom A,cod f))).a by ENS_1:60
        .= (hom?-(Hom A,cod f)).a by CAT_1:def 20
        .=<|cod f,?>.a by ENS_1:def 26;
      hence thesis by A63,CAT_1:18;
    end;
    then for a being Object of A holds Hom(<|cod f,?>.a,<|dom f,?>.a) <> {};
    then
A64: <|cod f,?> is_transformable_to <|dom f,?> by NATTRA_1:def 2;
    let h1,h2 be natural_transformation of <|cod f,?>, <|dom f,?> such that
A65: for o be Object of A holds h1.o = [[Hom(cod f,o),Hom(dom f,o)],
    hom(f,id o)] and
A66: for o be Object of A holds h2.o =[[Hom(cod f,o),Hom(dom f,o)],hom
    (f,id o)];
    now
      let o;
      thus h1.o =[[Hom(cod f,o),Hom(dom f,o)],hom(f,id o)] by A65
        .= h2.o by A66;
    end;
    hence thesis by A64,NATTRA_1:20;
  end;
end;

theorem Th4:
  for f being Element of the carrier' of A holds [[<|cod f,?>,<|dom
  f,?>],<|f,?>] is Element of the carrier' of Functors(A,EnsHom(A))
proof
  let f be Element of the carrier' of A;
  <|cod f,?> is_naturally_transformable_to <|dom f,?> by Th3;
  then [[<|cod f,?>,<|dom f,?>],<|f,?>] in NatTrans(A,EnsHom(A)) by
  NATTRA_1
  :def 16;
  hence thesis by NATTRA_1:def 18;
end;

definition
  let A;
  func Yoneda(A) -> Contravariant_Functor of A,Functors(A,EnsHom(A)) means
  :
  Def4: for
 f being Morphism of A holds it.f = [[<|cod f,?>,<|dom f,?>],<|f,?>];
  existence
  proof
    deffunc F(Element of the carrier' of A) = [[<|cod $1,?>,<|dom $1,?>],<|$1
    ,?>];
A1: for f being Element of the carrier' of A holds F(f) in the carrier' of
    Functors(A,EnsHom(A))
    proof
      let f;
      [[<|cod f,?>,<|dom f,?>],<|f,?>] is Morphism of Functors(A,EnsHom(A)
      ) by Th4;
      hence thesis;
    end;
    consider F being Function of the carrier' of A, the carrier' of Functors(A
    ,EnsHom(A)) such that
A2: for f being Element of the carrier' of A holds F.f = F(f) from
    FUNCT_2:sch 8(A1);
A3: for f,g being Morphism of A st dom g = cod f holds F.(g*f) = (F.f)*(F .g)
    proof
      let f,g be Morphism of A;
      reconsider t1=<|g,?> as natural_transformation of <|cod g,?>,<|dom g,?>;
      assume
A4:   dom g = cod f;
      then reconsider
      t=<|f,?> as natural_transformation of <|dom g,?>,<|dom f,?>;
A5:   F.g =[[<|cod g,?>,<|dom g,?>],t1] by A2;
A6:   dom(g*f) = dom f by A4,CAT_1:42;
      then reconsider
      tt=t`*`t1 as natural_transformation of <|cod(g*f),?>,<|dom(g*
      f),?> by A4, CAT_1:42;
A7:   cod(g*f) = cod g by A4,CAT_1:42;
      for o being Object of A holds (<|g*f,?>).o =tt.o
      proof
        set F1=<|cod f,?> , F2=<|dom f,?>;
        let o be Object of A;
        reconsider f1=t.o as Morphism of EnsHom(A);
        reconsider f2=t1.o as Morphism of EnsHom(A);
A8:     f2 = [[Hom(cod g,o),Hom(dom g,o)],hom(g,id o)] by Def3;
        for a being Object of A holds [[Hom(cod g,a),Hom(dom g,a)],hom(g,
        a)] in Hom(<|cod g,?>.a,<|dom g,?>.a)
        proof
          let a be Object of A;
A9:       EnsHom A = CatStr(# Hom A,Maps Hom A,fDom Hom A, fCod Hom A,
            fComp Hom A,fId Hom A #) by ENS_1:def 14;
          then reconsider
          m = [[Hom(cod g,a),Hom(dom g,a)],hom(g,a)] as Morphism of
          EnsHom A by ENS_1:48;
          reconsider m9=m as Element of Maps Hom A by ENS_1:48;
A10:      cod(m) = (fCod Hom A).m by A9
            .= cod m9 by ENS_1:def 11
            .= m`1`2 by ENS_1:def 5
            .= [Hom(cod g,a),Hom(dom g,a)]`2 by MCART_1:7
            .= Hom(dom g,a) by MCART_1:7
            .= (Obj (hom?-(Hom A,dom g))).a by ENS_1:60
            .= (hom?-(Hom A,dom g)).a by CAT_1:def 20
            .= <|dom g,?>.a by ENS_1:def 26;
          dom(m) = (fDom Hom A).m by A9
            .= dom m9 by ENS_1:def 10
            .= m`1`1 by ENS_1:def 4
            .= [Hom(cod g,a),Hom(dom g,a)]`1 by MCART_1:7
            .= Hom(cod g,a) by MCART_1:7
            .= (Obj (hom?-(Hom A,cod g))).a by ENS_1:60
            .= (hom?-(Hom A,cod g)).a by CAT_1:def 20
            .=<|cod g,?>.a by ENS_1:def 26;
          hence thesis by A10,CAT_1:18;
        end;
        then
A11:    Hom(<|cod g,?>.o,<|dom g,?>.o)<> {};
        for a being Object of A holds [[Hom(cod f,a),Hom(dom f,a)],hom(f,
        a)] in Hom(F1.a,F2.a)
        proof
          let a be Object of A;
A12:      EnsHom A = CatStr(# Hom A,Maps Hom A,fDom Hom A, fCod Hom A,
            fComp Hom A,fId Hom A #) by ENS_1:def 14;
          then reconsider
          m = [[Hom(cod f,a),Hom(dom f,a)],hom(f, a)] as Morphism
          of EnsHom A by ENS_1:48;
          reconsider m9=m as Element of Maps Hom A by ENS_1:48;
A13:      cod(m) = (fCod Hom A).m by A12
            .= cod m9 by ENS_1:def 11
            .= m`1`2 by ENS_1:def 5
            .= [Hom(cod f,a),Hom(dom f,a)]`2 by MCART_1:7
            .= Hom(dom f,a) by MCART_1:7
            .= (Obj (hom?-(Hom A,dom f))).a by ENS_1:60
            .= (hom?-(Hom A,dom f)).a by CAT_1:def 20
            .= F2.a by ENS_1:def 26;
          dom(m) = (fDom Hom A).m by A12
            .= dom m9 by ENS_1:def 10
            .= m`1`1 by ENS_1:def 4
            .= [Hom(cod f,a),Hom(dom f,a)]`1 by MCART_1:7
            .= Hom(cod f,a) by MCART_1:7
            .= (Obj (hom?-(Hom A,cod f))).a by ENS_1:60
            .= (hom?-(Hom A,cod f)).a by CAT_1:def 20
            .=F1.a by ENS_1:def 26;
          hence thesis by A13,CAT_1:18;
        end;
        then
A14:    Hom(F1.o,F2.o)<> {};
A15:    (<|g*f,?>).o = [[Hom(cod(g),o),Hom(dom(g*f),o)], hom(g*f,id o)]
        by A7,Def3
          .= [[Hom(cod g,o),Hom(dom f,o)],hom(g*f,o)] by A6,ENS_1:53;
A16:    t.o = [[Hom(cod f,o),Hom(dom f,o)],hom(f,id o)] by A4,Def3;
A17:    EnsHom A = CatStr(# Hom A,Maps Hom A,fDom Hom A,fCod Hom A, fComp
          Hom A,fId Hom A #) by ENS_1:def 14;
        then reconsider f19=f1 as Element of Maps Hom A;
        reconsider f29=f2 as Element of Maps Hom A by A17;
A18:    cod(f2) = (fCod Hom A).f2 by A17
          .= cod f29 by ENS_1:def 11
          .= f2`1`2 by ENS_1:def 5
          .= [Hom(cod g,o),Hom(dom g,o)]`2 by A8,MCART_1:7
          .= Hom(dom g,o) by MCART_1:7;
A19:    cod(f1) = (fCod Hom A).f1 by A17
          .= cod f19 by ENS_1:def 11
          .= f1`1`2 by ENS_1:def 5
          .= [Hom(cod f,o),Hom(dom f,o)]`2 by A16,MCART_1:7
          .= Hom(dom f,o) by MCART_1:7;
A20:    dom(f1) =(fDom Hom A).f1 by A17
          .= dom f19 by ENS_1:def 10
          .= f1`1`1 by ENS_1:def 4
          .=[Hom(cod f,o),Hom(dom f,o)]`1 by A16,MCART_1:7
          .=Hom(cod f,o) by MCART_1:7;
A21:    dom(f2) = (fDom Hom A).f2 by A17
          .= dom f29 by ENS_1:def 10
          .= f2`1`1 by ENS_1:def 4
          .= [Hom(cod g,o),Hom(dom g,o)]`1 by A8,MCART_1:7
          .= Hom(cod g,o) by MCART_1:7;
        <|dom g,?> is_naturally_transformable_to <|dom f,?> & <|cod g,?>
        is_naturally_transformable_to <|dom g,?> by A4,Th3;
        then (tt).o = (t.o)*(t1.o) by A6,A7,NATTRA_1:27
          .= f1 * f2 by A4,A14,A11,CAT_1:def 13
          .= [[Hom(cod g,o),Hom(dom f,o)], hom(f,id o)*hom(g,id o)] by A4,
        A16,A8,A20,A18,A21,A19,Th1
          .= [[Hom(cod g,o),Hom(dom f,o)],hom(f,o)*hom(g,id o)] by ENS_1:53
          .= [[Hom(cod g,o),Hom(dom f,o)],hom(f,o)*hom(g,o)] by ENS_1:53
          .= [[Hom(cod g,o),Hom(dom f,o)],hom(g*f,o)] by A4,ENS_1:46;
        hence thesis by A15;
      end;
      then
A22:  (<|g*f,?> )=tt by Th3,ISOCAT_1:31;
A23:  F.f =[[<|dom g,?>,<|dom f,?>],t] by A2,A4;
      then
A24:  [F.f,F.g] in dom (the Comp of Functors(A,EnsHom(A))) by A5,
      NATTRA_1:41;
      then consider F9,F19,F29 being Functor of A,EnsHom(A), t9 being
natural_transformation of F9,F19, t19 being natural_transformation of F19,F29
      such that
A25:  F.g=[[F9,F19],t9] and
A26:  F.f = [[F19,F29],t19] and
A27:  (the Comp of Functors(A,EnsHom(A))).(F.f,F.g) = [[F9,F29],t19
      `*`t9] by NATTRA_1:def 18;
A28:  <|f,?> = t19 by A23,A26,ZFMISC_1:33;
      [F19,F29] = [<|dom g,?>,<|dom f,?>] by A23,A26,ZFMISC_1:33;
      then
A29:  <|dom f,?> = F29 by ZFMISC_1:33;
      [F9,F19] = [<|cod g,?>,<|dom g,?>] by A5,A25,ZFMISC_1:33;
      then
A30:  <|cod g,?> = F9 & <|dom g,?> = F19 by ZFMISC_1:33;
      <|g,?> = t9 by A5,A25,ZFMISC_1:33;
      then F.f*F.g = [[<|cod g,?>,<|dom f,?>],t`*`t1] by A24,A27,
      A28,A30,
      A29,CAT_1:def 4;
      hence thesis by A2,A6,A7,A22;
    end;
A31: for f being Morphism of A holds F.(id dom f) = id cod (F.f) & F.(id
    cod f) = id dom (F.f)
    proof
      let f;
      set g =F.f;
      set X= dom <|id dom f,?>;
A32:  dom <|id dom f,?> = the carrier of A by FUNCT_2:def 1
        .= dom id <|dom f,?> by FUNCT_2:def 1;
A33:  F.(id dom f) =[[<|cod (id dom f),?>,<|dom (id dom f),?>],<|(id dom
      f),?>] by A2
        .=[[<|dom f,?>,<| dom (id dom f),?>],<|(id dom f),?>] by CAT_1:44
        .=[[<|dom f,?>,<|dom f,?>],<|id dom f,?>] by CAT_1:44;
A34:  g = [[<|cod f,?>,<|dom f,?>],<|f,?>] by A2;
      then cod g =<|dom f,?> by NATTRA_1:39;
      then
A35:  [[<|dom f,?>,<|dom f,?>],id <|dom f,?>] = id cod (F.f) by
      NATTRA_1:def 18;
A36:  for y st y in X holds (<|id dom f,?>).y =(id <|dom f,?>).y
      proof
        let y;
        assume y in X;
        then reconsider z=y as Object of A by FUNCT_2:def 1;
        reconsider zz= id z as Morphism of z,z;
A37:    Hom(z,z) <> {} by CAT_1:56;
        <|cod id dom f,?> is_transformable_to <|dom f,?> by CAT_1:44;
        then
A38:    <|cod id dom f,?> is_transformable_to <|dom id dom f,?> by
        CAT_1:44;
        (<|id dom f,?>).z = [[Hom(cod id dom f,z),Hom(dom id dom f,z)] ,
        hom(id dom f,id z)] by Def3
          .= [[Hom(dom f,z),Hom(dom id dom f,z)], hom(id dom f,id z)] by
        CAT_1:44
          .= [[Hom(dom f,z),Hom(dom f,z)], hom(id dom f,id z)] by CAT_1:44
          .= [[Hom(dom f,z),Hom(dom f,z)], hom(dom f,id z)] by ENS_1:53
          .= [[Hom(dom f,z),Hom(dom f,cod id z)], hom(dom f,id z)] by CAT_1:
        44
          .= [[Hom(dom f,dom id z),Hom(dom f,cod id z)], hom(dom f,id z)] by
        CAT_1:44
          .= (<|dom f,?> qua Function ).id z by ENS_1:def 22
          .= (<|dom f,?>).zz by A37,NATTRA_1:def 1
          .= id (<|dom f,?>.z) by NATTRA_1:15
          .= (id <|dom f,?> ).z by NATTRA_1:21
          .= (id <|dom f,?> qua Function).z by NATTRA_1:def 5;
        hence thesis by A38,NATTRA_1:def 5;
      end;
      set X= dom <|id cod f,?>;
A39:  for y st y in X holds (<|id cod f,?>).y =(id <|cod f,?>).y
      proof
        let y;
        assume y in X;
        then reconsider z=y as Object of A by FUNCT_2:def 1;
A40:    Hom(z,z) <> {} by CAT_1:56;
        <|cod id cod f,?> is_transformable_to <|cod f,?> by CAT_1:44;
        then
A41:    <|cod id cod f,?> is_transformable_to <|dom id cod f,?> by
        CAT_1:44;
        (<|id cod f,?>).z = [[Hom(cod id cod f,z),Hom(dom id cod f,z)],
        hom(id cod f,id z)] by Def3
          .= [[Hom(cod f,z),Hom(dom id cod f,z)], hom(id cod f,id z)] by
        CAT_1:44
          .= [[Hom(cod f,z),Hom(cod f,z)], hom(id cod f,id z)] by CAT_1:44
          .= [[Hom(cod f,z),Hom(cod f,z)], hom(cod f,id z)] by ENS_1:53
          .= [[Hom(cod f,z),Hom(cod f,cod id z)], hom(cod f,id z)] by CAT_1:
        44
          .= [[Hom(cod f,dom id z),Hom(cod f,cod id z)], hom(cod f,id z)] by
        CAT_1:44
          .= (<|cod f,?> qua Function).id z by ENS_1:def 22
          .= (<|cod f,?>).id z by A40,NATTRA_1:def 1
          .= id (<|cod f,?>.z) by NATTRA_1:15
          .= (id <|cod f,?>).z by NATTRA_1:21
          .= (id <|cod f,?> qua Function).z by NATTRA_1:def 5;
        hence thesis by A41,NATTRA_1:def 5;
      end;
      dom <|id cod f,?> = the carrier of A by FUNCT_2:def 1
        .= dom id <|cod f,?> by FUNCT_2:def 1;
      then
A42:  <|id cod f,?> =id <|cod f,?> by A39,FUNCT_1:9;
A43:  F.(id cod f) = [[<|cod (id cod f),?>,<|dom (id cod f),?>], <|(id
      cod f),?>] by A2
        .= [[<|cod f,?>,<| dom (id cod f),?>],<|(id cod f),?>] by CAT_1:44
        .= [[<|cod f,?>,<|cod f,?>],<|id cod f,?>] by CAT_1:44;
      dom g =<|cod f,?> by A34,NATTRA_1:39;
      hence thesis by A33,A35,A32,A36,A43,A42,FUNCT_1:9,NATTRA_1:def 18;
    end;
    for c being Object of A ex d being Object of Functors(A,EnsHom(A)) st
    F.(id c)= id d
    proof
      let c be Object of A;
      set X= dom <|id c,?>;
      <|c,?> in Funct(A,EnsHom(A)) by CAT_2:def 2;
      then reconsider d=<|c,?> as Object of Functors(A,EnsHom(A)) by
      NATTRA_1:
      def 18;
      take d;
A44:  for y st y in X holds (<|id c,?>).y =(id <|c,?>).y
      proof
        let y;
        assume y in X;
        then reconsider z=y as Object of A by FUNCT_2:def 1;
        reconsider zz= id z as Morphism of z,z;
A45:    Hom(z,z) <> {} by CAT_1:56;
        <|cod id c,?> is_transformable_to <|c,?> by CAT_1:44;
        then
A46:    <|cod id c,?> is_transformable_to <|dom id c,?> by
        CAT_1:44;
        (<|id c,?>).z = [[Hom(cod id c,z),Hom(dom id c,z)],hom(id c,id z)
        ] by Def3
          .= [[Hom(c,z),Hom(dom id c,z)],hom(id c,id z)] by CAT_1:44
          .= [[Hom(c,z),Hom(c,z)],hom(id c,id z)] by CAT_1:44
          .= [[Hom(c,z),Hom(c,z)],hom(c,id z)] by ENS_1:53
          .= [[Hom(c,z),Hom(c,cod id z)],hom(c,id z)] by CAT_1:44
          .= [[Hom(c,dom id z),Hom(c,cod id z)],hom(c,id z)] by CAT_1:44
          .=(<|c,?> qua Function ).id z by ENS_1:def 22
          .=(<|c,?>).zz by A45,NATTRA_1:def 1
          .= id (<|c,?>.z) by NATTRA_1:15
          .=(id <|c,?> ).z by NATTRA_1:21
          .=(id <|c,?> qua Function).z by NATTRA_1:def 5;
        hence thesis by A46,NATTRA_1:def 5;
      end;
      dom <|id c,?> = the carrier of A by FUNCT_2:def 1
        .= dom id <|c,?> by FUNCT_2:def 1;
      then
A47:  <|id c,?>= id <|c,?> by A44,FUNCT_1:9;
      F.(id c) = [[<|cod id c,?>,<|dom id c,?>],<|id c,?>] by A2
        .=[[<|c,?>,<|dom id c,?>],<|id c,?>] by CAT_1:44
        .= [[<|c,?>,<|c,?>],id <|c,?>] by A47,CAT_1:44
        .=id d by NATTRA_1:def 18;
      hence thesis;
    end;
    then reconsider F as Contravariant_Functor of A,Functors(A,EnsHom(A)) by
    A31
    ,A3,OPPCAT_1:def 7;
    for f being Element of the carrier' of A holds F.f = [[<|cod f,?>,<|
    dom f,?>],<|f,?>] by A2;
    hence thesis;
  end;
  uniqueness
  proof
    let h1,h2 be Contravariant_Functor of A,Functors(A,EnsHom(A)) such that
A48: for f holds h1.f = [[<|cod f,?>,<|dom f,?>],<|f,?>] and
A49: for f holds h2.f = [[<|cod f,?>,<|dom f,?>],<|f,?>];
    now
      let f;
      thus h1.f = [[<|cod f,?>,<|dom f,?>],<|f,?>] by A48
        .= h2.f by A49;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

definition
  let A,B be Category;
  let F be Contravariant_Functor of A,B;
  let c be Object of A;
  func F.c -> Object of B equals
  (Obj F).c;
  correctness;
end;

theorem
  for F being Functor of A,Functors(A,EnsHom(A)) st Obj F is one-to-one
  & F is faithful holds F is one-to-one
proof
  let F be Functor of A,Functors(A,EnsHom(A));
  assume
A1: Obj F is one-to-one;
  assume
A2: F is faithful;
  for x1,x2 be set st x1 in dom F & x2 in dom F & F.x1 = F.x2 holds x1 = x2
  proof
    let x1,x2 be set;
    assume that
A3: x1 in dom F & x2 in dom F and
A4: F.x1 = F.x2;
    reconsider m1=x1,m2=x2 as Morphism of A by A3,FUNCT_2:def 1;
    set o1=dom m1,o2=cod m1;
    set o3=dom m2,o4=cod m2;
    reconsider m19=m1 as Morphism of o1,o2 by CAT_1:22;
    reconsider m29=m2 as Morphism of o3,o4 by CAT_1:22;
A5: Hom(o1,o2) <> {} by CAT_1:19;
    then
A6: Hom(F.o1,F.o2) <> {} by CAT_1:126;
A7: (Obj F).o4=(F).o4 by CAT_1:def 20;
A8: (Obj F).o3 = F.o3 by CAT_1:def 20;
A9: Hom(o3,o4) <> {} by CAT_1:19;
    then
A10: Hom(F.o3,F.o4) <> {} by CAT_1:126;
A11: F.m19= F.m2 by A4,A5,NATTRA_1:def 1
      .= F.m29 by A9,NATTRA_1:def 1;
    (Obj F).o1 = F.o1 by CAT_1:def 20
      .=dom (F.m29) by A11,A6,CAT_1:23
      .= (Obj F).o3 by A10,A8,CAT_1:23;
    then
A12: m2 is Morphism of dom m2,cod m2 & o1=o3 by A1,
    CAT_1:22,FUNCT_2:25;
    (Obj F).o2 = F.o2 by CAT_1:def 20
      .=cod(F.m29) by A11,A6,CAT_1:23
      .=(Obj F).o4 by A10,A7,CAT_1:23;
    then m1 is Morphism of dom m1,cod m1 & m2 is Morphism of o1,o2 by A1,A12,
    CAT_1:22,FUNCT_2:25;
    hence thesis by A2,A4,A5,CAT_1:def 24;
  end;
  hence thesis by FUNCT_1:def 8;
end;

definition
  let C,D be Category;
  let T be Contravariant_Functor of C,D;
  attr T is faithful means
  :Def6:
  for c,c9 being Object of C st Hom(c,c9) <>
  {} for f1,f2 being Morphism of c,c9 holds T.f1 = T.f2 implies f1 = f2;
end;

theorem Th6:
  for F being Contravariant_Functor of A,Functors(A,EnsHom(A)) st
  Obj F is one-to-one & F is faithful holds F is one-to-one
proof
  let F be Contravariant_Functor of A,Functors(A,EnsHom(A));
  assume
A1: Obj F is one-to-one;
  assume
A2: F is faithful;
  for x1,x2 be set st x1 in dom F & x2 in dom F & F.x1 = F.x2 holds x1 = x2
  proof
    let x1,x2 be set;
    assume that
A3: x1 in dom F & x2 in dom F and
A4: F.x1 = F.x2;
    reconsider m1=x1,m2=x2 as Morphism of A by A3,FUNCT_2:def 1;
    set o1=dom m1,o2=cod m1;
    set o3=dom m2,o4=cod m2;
    reconsider m29=m2 as Morphism of o3,o4 by CAT_1:22;
    (Obj F).o1 = cod (F.m29) by A4,OPPCAT_1:33
      .= (Obj F).o3 by OPPCAT_1:33;
    then
A5: m2 is Morphism of dom m2,cod m2 & o1=o3 by A1,
    CAT_1:22,FUNCT_2:25;
A6: m1 is Morphism of dom m1,cod m1 & Hom(o1,o2) <> {} by CAT_1:19,22;
    (Obj F).o2 =dom(F.m29) by A4,OPPCAT_1:33
      .=(Obj F).o4 by OPPCAT_1:33;
    then m2 is Morphism of o1,o2 by A1,A5,FUNCT_2:25;
    hence thesis by A2,A4,A6,Def6;
  end;
  hence thesis by FUNCT_1:def 8;
end;

registration
  let A;
  cluster Yoneda A -> faithful;
coherence
proof
  set F = Yoneda A;
  for o1,o2 being Object of A st Hom(o1,o2) <> {} for x1,x2 being Morphism
  of o1,o2 holds F.x1 = F.x2 implies x1 = x2
  proof
    let o1,o2 be Object of A;
    assume
A1: Hom(o1,o2) <> {};
    let x1,x2 be Morphism of o1,o2;
A2: x2 in Hom(o1,o2) by A1,CAT_1:def 7;
    assume F.x1 = F.x2;
    then [[<|cod x1,?>,<|dom x1,?>],<|x1,?>]=F.x2 by Def4;
    then [[<|cod x1,?>,<|dom x1,?>],<|x1,?>]= [[<|cod x2,?>,<|dom x2,?>],<|x2
    ,?>] by Def4;
    then
    [<|cod x1,?>,<|dom x1,?>,<|x1,?>]= [[<|cod x2,?>,<|dom x2,?>],<|x2,?>
    ] by MCART_1:def 3;
    then
    [<|cod x1,?>,<|dom x1,?>,<|x1,?>] = [<|cod x2,?>,<|dom x2,?>,<|x2,?>]
    by MCART_1:def 3;
    then
A3: <|x1,?>= <|x2,?> by MCART_1:28;
A4: x1 in Hom(o1,o2) by A1,CAT_1:def 7;
    then
A5: dom x1 = o1 by CAT_1:18
      .= dom x2 by A2,CAT_1:18;
    cod x1 = o2 by A4,CAT_1:18
      .= cod x2 by A2,CAT_1:18;
    then [[Hom(cod x1,o2),Hom(dom x1,o2)],hom(x1,id o2)]= <|x2,?>.o2 by
    A5,A3,Def3;
    then
    [[Hom(cod x1,o2),Hom(dom x1,o2)],hom(x1,id o2)]= [[Hom(cod x2,o2),Hom
    (dom x2,o2)],hom(x2,id o2)] by Def3;
    then
    [[Hom(o2,o2),Hom(dom x1,o2)],hom(x1,id o2)]= [[Hom(cod x2,o2),Hom(dom
    x2,o2)],hom(x2,id o2)] by A4,CAT_1:18;
    then
    [[Hom(o2,o2),Hom(o1,o2)],hom(x1,id o2)]= [[Hom(cod x2,o2),Hom(dom x2,
    o2)],hom(x2,id o2)] by A4,CAT_1:18;
    then
    [[Hom(o2,o2),Hom(o1,o2)],hom(x1,id o2)]= [[Hom(o2,o2),Hom(dom x2,o2)]
    ,hom(x2,id o2)] by A2,CAT_1:18;
    then
    [[Hom(o2,o2),Hom(o1,o2)],hom(x1,id o2)]= [[Hom(o2,o2),Hom(o1,o2)],hom
    (x2,id o2)] by A2,CAT_1:18;
    then [[Hom(o2,o2),Hom(o1,o2)],hom(x1,id o2)]= [Hom(o2,o2),Hom(o1,o2),hom(
    x2,id o2)] by MCART_1:def 3;
    then
A6: [Hom(o2,o2),Hom(o1,o2),hom(x1,id o2)]= [Hom(o2,o2),Hom(o1,o2),hom(x2,
    id o2)] by MCART_1:def 3;
    reconsider dd=(id o2 ) as Morphism of A;
A7: Hom(o2,o2) <> {} by CAT_1:56;
    then
A8: (id o2)*dd =(id o2)*(id o2) by CAT_1:def 13;
    id o2 in Hom(o2,o2) by CAT_1:55;
    then id o2 in Hom(cod x2,o2) by A2,CAT_1:18;
    then id o2 in Hom(cod x2,dom id o2) by CAT_1:44;
    then
A9: hom(x2,id o2).dd =(id o2)*(id o2)*(x2 qua Morphism of A) by A8,
    ENS_1:def 24
      .=(id o2)*(x2 qua Morphism of A) by CAT_1:59
      .=(id o2)*x2 by A1,A7,CAT_1:def 13
      .=x2 by A1,CAT_1:57;
    id o2 in Hom(o2,o2) by CAT_1:55;
    then id o2 in Hom(cod x1,o2) by A4,CAT_1:18;
    then id o2 in Hom(cod x1,dom id o2) by CAT_1:44;
    then hom(x1,id o2).dd =(id o2)*(id o2)*(x1 qua Morphism of A) by A8,
    ENS_1:def 24
      .=(id o2)*(x1 qua Morphism of A) by CAT_1:59
      .=(id o2)*x1 by A1,A7,CAT_1:def 13
      .=x1 by A1,CAT_1:57;
    hence thesis by A9,A6,MCART_1:28;
  end;
  hence thesis by Def6;
end;
end;

registration
  let A;
  cluster Yoneda A -> one-to-one;
coherence
proof
  set F = Yoneda A;
  for x1,x2 be set st x1 in dom (Obj F) & x2 in dom (Obj F) & (Obj F).x1 =
  (Obj F).x2 holds x1 = x2
  proof
    let x1,x2 be set;
    assume that
A1: x1 in dom (Obj F) & x2 in dom (Obj F) and
A2: (Obj F).x1 = (Obj F).x2;
    reconsider c =x1,c1=x2 as Object of A by A1,FUNCT_2:def 1;
    reconsider f=id c1 as Morphism of c1,c1;
A3: Hom(c1,c1) <> {} by CAT_1:56;
    <|c1,?> in Funct(A,EnsHom(A)) by CAT_2:def 2;
    then reconsider d1=<|c1,?> as Object of Functors(A,EnsHom(A)) by
    NATTRA_1:
    def 18;
    <|c,?> in Funct(A,EnsHom(A)) by CAT_2:def 2;
    then reconsider d=<|c,?> as Object of Functors(A,EnsHom(A)) by
    NATTRA_1:def
    18;
    F.(id c1)= id d1
    proof
      set X= dom <|id c1,?>;
A4:   for y st y in X holds (<|id c1,?>).y =(id <|c1,?>).y
      proof
        let y;
        assume y in X;
        then reconsider z=y as Object of A by FUNCT_2:def 1;
        reconsider zz= id z as Morphism of z,z;
A5:     Hom(z,z) <> {} by CAT_1:56;
        <|cod id c1,?> is_transformable_to <|c1,?> by CAT_1:44;
        then
A6:     <|cod id c1,?> is_transformable_to <|dom id c1,?> by CAT_1:
        44;
        (<|id c1,?>).z = [[Hom(cod id c1,z),Hom(dom id c1,z)],hom(id c1,
        id z)] by Def3
          .= [[Hom(c1,z),Hom(dom id c1,z)],hom(id c1,id z)] by CAT_1:44
          .= [[Hom(c1,z),Hom(c1,z)],hom(id c1,id z)] by CAT_1:44
          .= [[Hom(c1,z),Hom(c1,z)],hom(c1,id z)] by ENS_1:53
          .= [[Hom(c1,z),Hom(c1,cod id z)],hom(c1,id z)] by CAT_1:44
          .= [[Hom(c1,dom id z),Hom(c1,cod id z)],hom(c1,id z)] by CAT_1:44
          .= (<|c1,?> qua Function ).id z by ENS_1:def 22
          .= (<|c1,?>).zz by A5,NATTRA_1:def 1
          .= id (<|c1,?>.z) by NATTRA_1:15
          .= (id <|c1,?> ).z by NATTRA_1:21
          .= (id <|c1,?> qua Function).z by NATTRA_1:def 5;
        hence thesis by A6,NATTRA_1:def 5;
      end;
      dom <|id c1,?> = the carrier of A by FUNCT_2:def 1
        .= dom id <|c1,?> by FUNCT_2:def 1;
      then
A7:   <|id c1,?>= id <|c1,?> by A4,FUNCT_1:9;
      F.(id c1) = [[<|cod id c1,?>,<|dom id c1,?>],<|id c1,?>] by Def4
        .= [[<|c1,?>,<|dom id c1,?>],<|id c1,?>] by CAT_1:44
        .= [[<|c1,?>,<|c1,?>],id <|c1,?>] by A7,CAT_1:44
        .=id d1 by NATTRA_1:def 18;
      hence thesis;
    end;
    then
A8: (Obj F).c1 = d1 by OPPCAT_1:31;
    F.(id c)= id d
    proof
      set X= dom <|id c,?>;
A9:   for y st y in X holds (<|id c,?>).y =(id <|c,?>).y
      proof
        let y;
        assume y in X;
        then reconsider z=y as Object of A by FUNCT_2:def 1;
        reconsider zz= id z as Morphism of z,z;
A10:    Hom(z,z) <> {} by CAT_1:56;
        <|cod id c,?> is_transformable_to <|c,?> by CAT_1:44;
        then
A11:    <|cod id c,?> is_transformable_to <|dom id c,?> by
        CAT_1:44;
        (<|id c,?>).z = [[Hom(cod id c,z),Hom(dom id c,z)],hom(id c,id z)
        ] by Def3
          .= [[Hom(c,z),Hom(dom id c,z)],hom(id c,id z)] by CAT_1:44
          .= [[Hom(c,z),Hom(c,z)],hom(id c,id z)] by CAT_1:44
          .= [[Hom(c,z),Hom(c,z)],hom(c,id z)] by ENS_1:53
          .= [[Hom(c,z),Hom(c,cod id z)],hom(c,id z)] by CAT_1:44
          .= [[Hom(c,dom id z),Hom(c,cod id z)],hom(c,id z)] by CAT_1:44
          .=(<|c,?> qua Function ).id z by ENS_1:def 22
          .=(<|c,?>).zz by A10,NATTRA_1:def 1
          .= id (<|c,?>.z) by NATTRA_1:15
          .=(id <|c,?> ).z by NATTRA_1:21
          .=(id <|c,?> qua Function).z by NATTRA_1:def 5;
        hence thesis by A11,NATTRA_1:def 5;
      end;
      dom <|id c,?> = the carrier of A by FUNCT_2:def 1
        .= dom id <|c,?> by FUNCT_2:def 1;
      then
A12:  <|id c,?>= id <|c,?> by A9,FUNCT_1:9;
      F.(id c) = [[<|cod id c,?>,<|dom id c,?>],<|id c,?>] by Def4
        .= [[<|c,?>,<|dom id c,?>],<|id c,?>] by CAT_1:44
        .= [[<|c,?>,<|c,?>],id <|c,?>] by A12,CAT_1:44
        .=id d by NATTRA_1:def 18;
      hence thesis;
    end;
    then (Obj F).c = d by OPPCAT_1:31;
    then [[Hom(c,dom f),Hom(c,cod f)],hom(c,f)]= (hom?-(c1)).f by A2,A8,
    ENS_1:def 22;
    then
    [[Hom(c,dom f),Hom(c,cod f)],hom(c,f)]= [[Hom(c1,dom f),Hom(c1,cod f)
    ],hom(c1,f)] by ENS_1:def 22;
    then
    [Hom(c,dom f),Hom(c,cod f),hom(c,f)]= [[Hom(c1,dom f),Hom(c1,cod f)],
    hom(c1,f)] by MCART_1:def 3;
    then [Hom(c,dom f),Hom(c,cod f),hom(c,f)]= [Hom(c1,dom f),Hom(c1,cod f),
    hom(c1,f)] by MCART_1:def 3;
    then Hom(c,dom f)=Hom(c1,dom f) by MCART_1:28;
    then Hom(c,dom f)=Hom(c1,c1) by A3,CAT_1:23;
    then
A13: Hom(c,c1)=Hom(c1,c1) by A3,CAT_1:23;
    id c1 in Hom(c1,c1) by CAT_1:55;
    then reconsider h=id c1 as Morphism of c,c1 by A13,CAT_1:def 7;
    dom h = c by A3,A13,CAT_1:23;
    hence thesis by A3,CAT_1:23;
  end;
  then Obj F is one-to-one by FUNCT_1:def 8;
  hence thesis by Th6;
end;
end;

definition
  let C,D be Category;
  let T be Contravariant_Functor of C,D;
  attr T is full means
  :Def7:
  for c,c9 being Object of C st Hom(T.c9,T.c) <> {}
  for g being Morphism of T.c9,T.c holds Hom(c,c9) <> {} &
  ex f being Morphism of c,c9 st g = T.f;
end;

registration
  let A;
  cluster Yoneda A -> full;
coherence
proof
  set F = Yoneda A;
  for c,c9 being Object of A st Hom(F.c9,F.c) <> {} for g being Morphism
  of F.c9,F.c holds Hom(c,c9) <> {} & ex f being Morphism of c,c9 st g = F.f
  proof
    let c,c9 be Object of A;
    assume
A1: Hom(F.c9,F.c) <> {};
A2: <|c9,?>.c9 = (hom?-(Hom A,c9)).c9 by ENS_1:def 26
      .= (Obj (hom?-(Hom A,c9))).c9 by CAT_1:def 20
      .= Hom(c9,c9) by ENS_1:60;
    let g be Morphism of F.c9,F.c;
    g in the carrier' of Functors(A,EnsHom A);
    then g in NatTrans(A,EnsHom A) by NATTRA_1:def 18;
    then consider
    F1,F2 being Functor of A,EnsHom A, t being natural_transformation
    of F1,F2 such that
A3: g = [[F1,F2],t] and
A4: F1 is_naturally_transformable_to F2 by NATTRA_1:def 16;
A5: dom g =F1 by A3,NATTRA_1:39;
    <|c9,?> in Funct(A,EnsHom(A)) by CAT_2:def 2;
    then reconsider d1=<|c9,?> as Object of Functors(A,EnsHom(A)) by
    NATTRA_1:
    def 18;
    <|c,?> in Funct(A,EnsHom(A)) by CAT_2:def 2;
    then reconsider d=<|c,?> as Object of Functors(A,EnsHom(A)) by NATTRA_1:
    def 18;
A6: F.(id c)= id d
    proof
      set X= dom <|id c,?>;
A7:   for y st y in X holds (<|id c,?>).y =(id <|c,?>).y
      proof
        let y;
        assume y in X;
        then reconsider z=y as Object of A by FUNCT_2:def 1;
        reconsider zz= id z as Morphism of z,z;
A8:     Hom(z,z) <> {} by CAT_1:56;
        <|cod id c,?> is_transformable_to <|c,?> by CAT_1:44;
        then
A9:     <|cod id c,?> is_transformable_to <|dom id c,?> by
        CAT_1:44;
        (<|id c,?>).z = [[Hom(cod id c,z),Hom(dom id c,z)],hom(id c,id z)
        ] by Def3
          .= [[Hom(c,z),Hom(dom id c,z)],hom(id c,id z)] by CAT_1:44
          .= [[Hom(c,z),Hom(c,z)],hom(id c,id z)] by CAT_1:44
          .= [[Hom(c,z),Hom(c,z)],hom(c,id z)] by ENS_1:53
          .= [[Hom(c,z),Hom(c,cod id z)],hom(c,id z)] by CAT_1:44
          .= [[Hom(c,dom id z),Hom(c,cod id z)],hom(c,id z)] by CAT_1:44
          .=(<|c,?> qua Function ).id z by ENS_1:def 22
          .=(<|c,?>).zz by A8,NATTRA_1:def 1
          .= id (<|c,?>.z) by NATTRA_1:15
          .=(id <|c,?> ).z by NATTRA_1:21
          .=(id <|c,?> qua Function).z by NATTRA_1:def 5;
        hence thesis by A9,NATTRA_1:def 5;
      end;
      dom <|id c,?> = the carrier of A by FUNCT_2:def 1
        .= dom id <|c,?> by FUNCT_2:def 1;
      then
A10:  <|id c,?>= id <|c,?> by A7,FUNCT_1:9;
      F.(id c) = [[<|cod id c,?>,<|dom id c,?>],<|id c,?>] by Def4
        .=[[<|c,?>,<|dom id c,?>],<|id c,?>] by CAT_1:44
        .= [[<|c,?>,<|c,?>],id <|c,?>] by A10,CAT_1:44
        .=id d by NATTRA_1:def 18;
      hence thesis;
    end;
    then
A11: F.c = d by OPPCAT_1:31;
A12: F.(id c9)= id d1
    proof
      set X= dom <|id c9,?>;
A13:  for y st y in X holds (<|id c9,?>).y =(id <|c9,?>).y
      proof
        let y;
        assume y in X;
        then reconsider z=y as Object of A by FUNCT_2:def 1;
        reconsider zz= id z as Morphism of z,z;
A14:    Hom(z,z) <> {} by CAT_1:56;
        <|cod id c9,?> is_transformable_to <|c9,?> by CAT_1:44;
        then
A15:    <|cod id c9,?> is_transformable_to <|dom id c9,?> by CAT_1:
        44;
        (<|id c9,?>).z = [[Hom(cod id c9,z),Hom(dom id c9,z)],hom(id c9,
        id z)] by Def3
          .= [[Hom(c9,z),Hom(dom id c9,z)],hom(id c9,id z)] by CAT_1:44
          .= [[Hom(c9,z),Hom(c9,z)],hom(id c9,id z)] by CAT_1:44
          .= [[Hom(c9,z),Hom(c9,z)],hom(c9,id z)] by ENS_1:53
          .= [[Hom(c9,z),Hom(c9,cod id z)],hom(c9,id z)] by CAT_1:44
          .=[[Hom(c9,dom id z),Hom(c9,cod id z)],hom(c9,id z)] by CAT_1:44
          .=(<|c9,?> qua Function ).id z by ENS_1:def 22
          .=(<|c9,?>).zz by A14,NATTRA_1:def 1
          .= id (<|c9,?>.z) by NATTRA_1:15
          .=(id <|c9,?> ).z by NATTRA_1:21
          .=(id <|c9,?> qua Function).z by NATTRA_1:def 5;
        hence thesis by A15,NATTRA_1:def 5;
      end;
      dom <|id c9,?> = the carrier of A by FUNCT_2:def 1
        .= dom id <|c9,?> by FUNCT_2:def 1;
      then
A16:  <|id c9,?>= id <|c9,?> by A13,FUNCT_1:9;
      F.(id c9) = [[<|cod id c9,?>,<|dom id c9,?>],<|id c9,?>] by Def4
        .=[[<|c9,?>,<|dom id c9,?>],<|id c9,?>] by CAT_1:44
        .= [[<|c9,?>,<|c9,?>],id <|c9,?>] by A16,CAT_1:44
        .=id d1 by NATTRA_1:def 18;
      hence thesis;
    end;
    then
A17: (Obj F).c9 = d1 by OPPCAT_1:31;
A18: cod g = F2 by A3,NATTRA_1:39;
    then
A19: F2=F.c by A1,CAT_1:23;
A20: F.c9= d1 by A12,OPPCAT_1:31;
A21: <|c,?>.c9 = (hom?-(Hom A,c)).c9 by ENS_1:def 26
      .= (Obj (hom?-(Hom A,c))).c9 by CAT_1:def 20
      .= Hom(c,c9) by ENS_1:60;
A22: dom g =F.c9 & cod g= F.c by A1,CAT_1:23;
    then reconsider t as natural_transformation of <|c9,?>,<|c,?> by A5,A18,
    A12,
    A11,OPPCAT_1:31;
    (Obj F).c = d by A6,OPPCAT_1:31;
    then <|c9,?> is_transformable_to <|c,?> by A4,A5,A18,A22,A20,NATTRA_1:
    def 7;
    then Hom(<|c9,?>.c9,<|c,?>.c9) <> {} by NATTRA_1:def 2;
    then
A23: (t.c9) in Hom(<|c9,?>.c9,<|c,?>.c9) by CAT_1:def 7;
    then
A24: cod (t.c9) =<|c,?>.c9 by CAT_1:18;
A25: EnsHom A = CatStr(# Hom A,Maps Hom A,fDom Hom A,fCod Hom A,fComp Hom
      A,fId Hom A #) by ENS_1:def 14;
    then reconsider t1=t.c9 as Element of Maps Hom A;
A26: cod (t.c9) = (fCod Hom A).(t.c9) by A25
      .= cod t1 by ENS_1:def 11;
    t1 in Maps (dom t1,cod t1) by ENS_1:19;
    then
A27: t1`2 in Funcs(dom t1,cod t1) by ENS_1:20;
A28: dom (t.c9) =<|c9,?>.c9 by A23,CAT_1:18;
    then (the Source of EnsHom A).(t.c9) <> {} by A2,CAT_1:56;
    then dom t1 <> {} by A25,ENS_1:def 10;
    then
A29: cod t1 <> {} by A27;
    then
A30: cod (t.c9) <> {} by A25,ENS_1:def 11;
    hence Hom(c,c9) <> {} by A21,A23,CAT_1:18;
    dom (t.c9) =(fDom Hom A).(t.c9) by A25
      .= dom t1 by ENS_1:def 10;
    then
A31: t1`2 is Function of <|c9,?>.c9,<|c,?>.c9 by A28,A24,A27,A26,
    FUNCT_2:121;
    then
A32: dom t1`2 = Hom(c9,c9) by A2,A24,A30,FUNCT_2:def 1;
    then id c9 in dom t1`2 by CAT_1:55;
    then
A33: t1`2.id c9 in rng t1`2 by FUNCT_1:def 5;
    rng t1`2 c= Hom(c,c9) by A21,A31,RELAT_1:def 19;
    then t1`2.id c9 in Hom(c,c9) by A33;
    then reconsider f = t1`2.id c9 as Morphism of c,c9 by CAT_1:def 7;
A34: <|c,?>.c9 <> {} by A24,A25,A29,ENS_1:def 11;
    then
A35: dom f = c by A21,CAT_1:23;
    take f;
A36: cod f = c9 by A21,A34,CAT_1:23;
A37: F1=F.c9 by A1,A5,CAT_1:23;
    then
A38: <|c9,?> is_transformable_to <|c,?> by A4,
    A19,A17,A11,NATTRA_1:def 7;
    for a being Object of A holds <|f,?>.a =t.a
    proof
      let a be Object of A;
A39:  <|c,?>.a = (hom?-(Hom A,c)).a by ENS_1:def 26
        .= (Obj (hom?-(Hom A,c))).a by CAT_1:def 20
        .= Hom(c,a) by ENS_1:60;
      reconsider ta1=t.a as Element of Maps Hom A by A25;
A40:  <|c9,?>.a = (hom?-(Hom A,c9)).a by ENS_1:def 26
        .= (Obj (hom?-(Hom A,c9))).a by CAT_1:def 20
        .= Hom(c9,a) by ENS_1:60;
      ta1 in Maps (dom ta1,cod ta1) by ENS_1:19;
      then
A41:  ta1`2 in Funcs(dom ta1,cod ta1) by ENS_1:20;
      then
A42:  ta1`2 is Function of dom ta1,cod ta1 by FUNCT_2:121;
      set X=dom ta1`2;
A43:  dom (t.a) =(fDom Hom A).(t.a) by A25
        .= dom ta1 by ENS_1:def 10;
A44:  Hom(<|c9,?>.a,<|c,?>.a) <> {} by A38,NATTRA_1:def 2;
      then
A45:  dom(t.a) =<|c9,?>.a by CAT_1:23;
A46:  cod (t.a) =<|c,?>.a by A44,CAT_1:23;
A47:  cod (t.a) = (fCod Hom A).(t.a) by A25
        .= cod ta1 by ENS_1:def 11;
      then
A48:  ta1 =[[<|c9,?>.a,<|c,?>.a],ta1`2] by A45,A46,A43,ENS_1:8;
A49:  ta1`2 is Function of dom(t.a),cod(t.a) by A43,A47,A41,FUNCT_2:121;
A50:  dom ta1`2 = Hom(c9,a)
      proof
        per cases;
        suppose
          Hom(c9,a) = {};
          hence thesis by A45,A43,A40,A42,FUNCT_2:def 1;
        end;
        suppose
          Hom(c9,a) <> {};
          then Hom(dom f,a) <> {} by A36,ENS_1:42;
          hence thesis by A35,A45,A46,A40,A39,A49,FUNCT_2:def 1;
        end;
      end;
A51:  for x be set st x in X holds hom(f,id a).x = ta1`2.x
      proof
        reconsider t1=t.c9 as Element of Maps Hom A by A25;
        let x be set;
A52:    f in Hom(c,cod f) by A35,CAT_1:18;
        assume
A53:    x in X;
        then reconsider y = x as Morphism of cod f,a by A36,A50,CAT_1:def 7;
        reconsider h=y as Morphism of c9,a by A21,A24,A30,CAT_1:23;
A54:    dom h = c9 by A50,A53,CAT_1:23;
        reconsider tc9=<|c9,?>.h as Element of Maps Hom A by A25;
A55:    cod h =a by A50,A53,CAT_1:23;
        reconsider tch=<|c,?>.h as Element of Maps Hom A by A25;
A56:    [[Hom(c,dom h),Hom(c,cod h)],hom(c,h)] =(<|c,?> qua Function ).h
        by ENS_1:def 22
          .=(<|c,?>).h by A50,A53,NATTRA_1:def 1;
A57:    [[Hom(c9,dom h),Hom(c9,cod h)],hom(c9,h)] =(<|c9,?> qua Function
        ).h by ENS_1:def 22
          .=(<|c9,?>).h by A50,A53,NATTRA_1:def 1;
A58:    cod (<|c9,?>.h) = (fCod Hom A).(<|c9,?>.h) by A25
          .= cod tc9 by ENS_1:def 11;
        then
A59:    cod(<|c9,?>.h) =(tc9)`1`2 by ENS_1:def 5
          .= [Hom(c9,dom h),Hom(c9,cod h)]`2 by A57,MCART_1:7
          .=Hom(c9,cod h) by MCART_1:7;
        then
A60:    [(t.a),(<|c9,?>.h)] in dom(the Comp of EnsHom A) by A45,A40,A55,
        CAT_1:40;
        tc9 in Maps (dom tc9,cod tc9) by ENS_1:19;
        then
A61:    tc9`2 in Funcs(dom tc9,cod tc9) by ENS_1:20;
A62:    dom (<|c9,?>.h) =(fDom Hom A).(<|c9,?>.h) by A25
          .= dom tc9 by ENS_1:def 10;
        then dom (<|c9,?>.h) = (tc9)`1`1 by ENS_1:def 4
          .= [Hom(c9,dom h),Hom(c9,cod h)]`1 by A57,MCART_1:7
          .=Hom(c9,dom h) by MCART_1:7;
        then tc9`2 is Function of Hom(c9,dom h),Hom(c9,cod h) by A62,A58,
        A59,A61,FUNCT_2:121;
        then
A63:    dom tc9`2 = Hom(c9,c9) by A50,A53,A54,A55,FUNCT_2:def 1;
A64:    dom y =cod f by A36,A50,A53,CAT_1:23;
        tch =[[dom tch,cod tch],tch`2] by ENS_1:8;
        then [Hom(c,dom h),Hom(c,cod h),hom(c,h)]= [[dom tch,cod tch],tch`2]
        by A56,MCART_1:def 3;
        then [Hom(c,dom h),Hom(c,cod h),hom(c,h)]= [dom tch,cod tch,tch`2] by
        MCART_1:def 3;
        then
A65:    hom(c,h)=tch`2 by MCART_1:28;
        tc9 =[[dom tc9,cod tc9],tc9`2] by ENS_1:8;
        then [dom tc9,cod tc9,tc9`2] = [[Hom(c9,dom h),Hom(c9,cod h)],hom(c9,
        h)] by A57,MCART_1:def 3;
        then [dom tc9,cod tc9,tc9`2] = [Hom(c9,dom h),Hom(c9,cod h),hom(c9,h)
        ] by MCART_1:def 3;
        then
A66:    tc9`2=hom(c9,h) by MCART_1:28;
A67:    Hom(<|c9,?>.c9,<|c,?>.c9) <> {} by A38,NATTRA_1:def 2;
        then (t.c9) in Hom(<|c9,?>.c9,<|c,?>.c9) by CAT_1:def 7;
        then
A68:    cod (t.c9) = <|c,?>.c9 by CAT_1:18;
        dom (<|c,?>.h) =(fDom Hom A).(<|c,?>.h) by A25
          .= dom tch by ENS_1:def 10;
        then dom (<|c,?>.h) = tch`1`1 by ENS_1:def 4
          .=[Hom(c,dom h),Hom(c,cod h)]`1 by A56,MCART_1:7
          .=Hom(c,dom h) by MCART_1:7;
        then
A69:    [(<|c,?>.h),(t.c9)] in dom(the Comp of EnsHom A) by A21,A54,A68,
        CAT_1:40;
        cod (t.c9) = (fCod Hom A).(t.c9) by A25
          .= cod t1 by ENS_1:def 11;
        then
A70:    cod t1=[Hom(c,dom h),Hom(c,cod h)]`1 by A21,A54,A68,
        MCART_1:
        7
          .= tch`1`1 by A56,MCART_1:7
          .=dom tch by ENS_1:def 4;
        Hom(<|c,?>.c9,<|c,?>.a ) <> {} by A50,A53,CAT_1:126;
        then
A71:    (<|c,?>.h)*(t.c9) =(<|c,?>.h)*(t.c9 qua Morphism of EnsHom A) by
        A67,CAT_1:def 13
          .= (fComp Hom A).(tch,t1) by A25,A69,CAT_1:def 4
          .= tch*t1 by A70,ENS_1:def 12
          .= [[dom t1,cod tch],tch`2*t1`2] by A70,ENS_1:def 7;
A72:    cod tc9=(tc9)`1`2 by ENS_1:def 5
          .= [Hom(c9,dom h),Hom(c9,cod h)]`2 by A57,MCART_1:7
          .= dom ta1 by A45,A43,A40,A55,MCART_1:7;
        Hom(<|c9,?>.c9,<|c9,?>.a ) <> {} by A50,A53,CAT_1:126;
        then
A73:    (t.a)*(<|c9,?>.h) = (t.a qua Morphism of EnsHom A)*(<|c9,?>.h)
        by A44,CAT_1:def 13
          .= (fComp Hom A).(ta1,tc9) by A25,A60,CAT_1:def 4
          .= ta1*tc9 by A72,ENS_1:def 12
          .= [[dom tc9,cod ta1],ta1`2*tc9`2] by A72,ENS_1:def 7;
        (t.a)*(<|c9,?>.h) =(<|c,?>.h)*(t.c9) by A4,A37,A19,A17,A11,A50,
        A53,NATTRA_1:def 8;
        then [dom tc9,cod ta1,(ta1`2*tc9`2)] = [[dom t1,cod tch],(tch`2*t1`2)
        ] by A73,A71,MCART_1:def 3;
        then [dom tc9,cod ta1,(ta1`2*tc9`2)] = [dom t1,cod tch,(tch`2*t1`2)]
        by MCART_1:def 3;
        then
A74:    ta1`2*tc9`2=tch`2*t1`2 by MCART_1:28;
A75:    id c9 in Hom(c9,cod f) by A36,CAT_1:55;
        hom(f,id a).x =hom(f,a).y by ENS_1:53
          .= y * f by A36,A50,A53,ENS_1:def 21
          .= tch`2.(t1`2.id c9) by A64,A65,A52,ENS_1:def 20
          .= (ta1`2*tc9`2).(id c9) by A32,A74,CAT_1:55,FUNCT_1:23
          .= ta1`2.(hom(c9,h).id c9) by A66,A63,CAT_1:55,FUNCT_1:23
          .= ta1`2.(y * id c9) by A64,A75,ENS_1:def 20
          .= ta1`2.x by A36,A64,CAT_1:47;
        hence thesis;
      end;
      dom hom(f,a) = Hom(cod f,a)
      proof
        per cases;
        suppose
          Hom(cod f,a) = {};
          hence thesis by FUNCT_2:def 1;
        end;
        suppose
          Hom(cod f,a) <> {};
          then Hom(c,a) <> {} by A35,ENS_1:42;
          hence thesis by A35,FUNCT_2:def 1;
        end;
      end;
      then dom ta1`2 = dom hom(f,id a) by A36,A50,ENS_1:53;
      then hom(f,id a)=ta1`2 by A51,FUNCT_1:9;
      hence thesis by A35,A36,A40,A39,A48,Def3;
    end;
    then <|f,?> = t by A4,A37,A19,A17,A11,A35,A36,ISOCAT_1:31;
    hence thesis by A3,A5,A18,A22,A17,A11,A35,A36,Def4;
  end;
  hence thesis by Def7;
end;
end;
