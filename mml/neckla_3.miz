:: The Class of Series-Parallel Graphs, {III}
::  by Krzysztof Retel
::
:: Received February 3, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies NECKLA_3, NECKLA_2, NECKLACE, ORDERS_1, RELAT_1, FINSET_1,
      FUNCT_1, BOOLE, YELLOW_0, SUBSET_1, INCPROJ, RELAT_2, CLASSES2, CAT_1,
      REALSET1, REWRITE1, CARD_1, FINSEQ_1, ARYTM_1, FINSEQ_5, MSUALG_5,
      TARSKI, EQREL_1, WELLORD1, FUNCT_4, FUNCOP_1, SEQM_3, ARYTM;
 notations TARSKI, XBOOLE_0, ENUMSET1, SUBSET_1, ZFMISC_1, RELAT_1, RELAT_2,
      RELSET_1, FUNCT_1, PARTFUN1, FUNCT_2, FINSET_1, CARD_1, NUMBERS,
      XCMPLX_0, XXREAL_0, NAT_1, REALSET1, DOMAIN_1, STRUCT_0, ORDERS_2,
      WAYBEL_1, FUNCOP_1, CLASSES2, NECKLACE, WAYBEL_0, FUNCT_4, REWRITE1,
      FINSEQ_1, FINSEQ_5, EQREL_1, MSUALG_5, YELLOW_0, NECKLA_2, WELLORD1;
 constructors XXREAL_0, NAT_1, BINOP_2, EQREL_1, CLASSES1, TOLER_1, CLASSES2,
      FINSEQ_5, REWRITE1, REALSET2, MSUALG_5, ORDERS_3, WAYBEL_1, NECKLACE,
      NECKLA_2, RELSET_1;
 registrations XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, PARTFUN1, FUNCT_2,
      FUNCOP_1, FINSET_1, XXREAL_0, XREAL_0, NAT_1, CARD_1, FINSEQ_1, REALSET1,
      STRUCT_0, ORDERS_2, YELLOW_0, ORDERS_4, NECKLACE, NECKLA_2, ORDINAL1,
      FUNCT_4, RELSET_1, ZFMISC_1;
 requirements BOOLE, SUBSET, REAL, NUMERALS, ARITHM;
 definitions XBOOLE_0, TARSKI, NECKLACE, RELAT_2, FUNCOP_1, WELLORD1, STRUCT_0;
 theorems RELSET_1, NECKLACE, XBOOLE_0, WAYBEL_0, YELLOW_0, FUNCT_1, FUNCT_2,
      ORDERS_2, RELAT_2, SUBSET_1, ZFMISC_1, XBOOLE_1, TARSKI, MCART_1,
      CLASSES2, CLASSES1, NECKLA_2, FUNCOP_1, ENUMSET1, RELAT_1, SYSREL,
      PARTFUN1, GROUP_6, CARD_2, REWRITE1, FINSEQ_1, FINSEQ_3, FINSEQ_5, NAT_1,
      EQREL_1, MSUALG_6, MSUALG_5, CARD_1, NAT_2, FUNCT_4, FUNCT_3, WAYBEL_1,
      YELLOW_6, REALSET1, XREAL_1, XXREAL_0, ORDINAL1;
 schemes NAT_1;

begin :: Preliminaries

reserve A,B,a,b,c,d,e,f,g,h for set;

theorem Th1:
  (id A)|B = id A /\ [:B,B:]
proof
  thus (id A)|B c= id A /\ [:B,B:]
  proof
    let a be set;
    assume
A1: a in (id A)|B;
    (id A)|B is Relation of B,A by RELSET_1:33;
    then consider x,y being set such that
A2: a = [x,y] & x in B & y in A by A1,RELSET_1:6;
A3: [x,y] in id A by A1,A2,RELAT_1:def 11;
    then x in A & x = y by RELAT_1:def 10;
    then [x,y] in [:B,B:] by A2,ZFMISC_1:106;
    hence thesis by A2,A3,XBOOLE_0:def 4;
  end;
  let a be set;
  assume a in id A /\ [:B,B:];
  then
A4: a in id A & a in [:B,B:] by XBOOLE_0:def 4;
  then consider x1,y1 being set such that
A5: x1 in B & y1 in B & a = [x1,y1] by ZFMISC_1:def 2;
  thus thesis by A4,A5,RELAT_1:def 11;
end;

theorem
  id {a,b,c,d} = {[a,a],[b,b],[c,c],[d,d]}
proof
  set X = {a,b,c,d};
  thus id X c= {[a,a],[b,b],[c,c],[d,d]}
  proof
    let x be set;
    assume
A1: x in id X;
    then consider x1,y1 being set such that
A2: x = [x1,y1] & x1 in X & y1 in X by RELSET_1:6;
A3: x1 = y1 by A1,A2,RELAT_1:def 10;
    per cases by A2,ENUMSET1:def 2;
    suppose x1 = a;
      hence thesis by A2,A3,ENUMSET1:def 2;
    end;
    suppose x1 = b;
      hence thesis by A2,A3,ENUMSET1:def 2;
    end;
    suppose x1 = c;
      hence thesis by A2,A3,ENUMSET1:def 2;
    end;
    suppose x1 = d;
      hence thesis by A2,A3,ENUMSET1:def 2;
    end;
  end;
  let x be set;
  assume
A4: x in {[a,a],[b,b],[c,c],[d,d]};
  per cases by A4,ENUMSET1:def 2;
  suppose
A5: x = [a,a];
    a in X by ENUMSET1:def 2;
    hence thesis by A5,RELAT_1:def 10;
  end;
  suppose
A6: x = [b,b];
    b in X by ENUMSET1:def 2;
    hence thesis by A6,RELAT_1:def 10;
  end;
  suppose
A7: x = [c,c];
    c in X by ENUMSET1:def 2;
    hence thesis by A7,RELAT_1:def 10;
  end;
  suppose
A8: x = [d,d];
    d in X by ENUMSET1:def 2;
    hence thesis by A8,RELAT_1:def 10;
  end;
end;

theorem Th3:
  [:{a,b,c,d},{e,f,g,h}:] = {[a,e],[a,f],[b,e],[b,f],[a,g],[a,h],[b,g],[b,h]}
  \/ {[c,e],[c,f],[d,e],[d,f],[c,g],[c,h],[d,g],[d,h]}
proof
  set X1 = {a,b,c,d}, Y1 = {e,f,g,h}, X11 = {a,b}, X12 = {c,d}, Y11 = {e,f},
  Y12 = {g,h};
A1: [:X11,Y11:] = {[a,e],[a,f],[b,e],[b,f]} &
  [:X11,Y12:] = {[a,g],[a,h],[b,g],[b,h]} &
  [:X12,Y11:] = {[c,e],[c,f],[d,e],[d,f]} &
  [:X12,Y12:] = {[c,g],[c,h],[d,g],[d,h]} by MCART_1:25;
  X1 = X11 \/ X12 & Y1 = Y11 \/ Y12 by ENUMSET1:45;
  then [:X1,Y1:] = [:X11,Y11:] \/ [:X11,Y12:] \/ [:X12,Y11:] \/ [:X12,Y12:]
  by ZFMISC_1:121;
  then [:X1,Y1:] = {[a,e],[a,f],[b,e],[b,f],[a,g],[a,h],[b,g],[b,h]}
  \/ {[c,e],[c,f],[d,e],[d,f]} \/ {[c,g],[c,h],[d,g],[d,h]} by A1,ENUMSET1:65
    .= {[a,e],[a,f],[b,e],[b,f],[a,g],[a,h],[b,g],[b,h]}
  \/ ({[c,e],[c,f],[d,e],[d,f]} \/ {[c,g],[c,h],[d,g],[d,h]}) by XBOOLE_1:4
    .= {[a,e],[a,f],[b,e],[b,f],[a,g],[a,h],[b,g],[b,h]}
  \/ {[c,e],[c,f],[d,e],[d,f],[c,g],[c,h],[d,g],[d,h]} by ENUMSET1:65;
  hence thesis;
end;

registration
  let X,Y be trivial set;
  cluster -> trivial Relation of X,Y;
  correctness
  proof
    let R be Relation of X,Y;
    assume not thesis;
    then consider a1,a2 be set such that
A1: a1 in R & a2 in R & a1 <> a2 by REALSET1:14;
A2: (X is empty or ex x be set st X = {x}) &
    (Y is empty or ex y be set st Y = {y}) by REALSET1:def 4;
    per cases by A2;
    suppose X is empty & Y is empty;
      hence thesis by A1;
    end;
    suppose X is empty & ex x be set st Y = {x};
      hence thesis by A1;
    end;
    suppose ex x be set st X = {x} & Y is empty;
      hence thesis by A1;
    end;
    suppose
A3:   ex x be set st X = {x} & ex y be set st Y = {y};
      then consider x be set such that
A4:   X = {x};
      consider y be set such that
A5:   Y = {y} by A3;
      consider x1,x2 be set such that
A6:   a1 = [x1,x2] & x1 in X & x2 in Y by A1,RELSET_1:6;
A7:   x1 = x & x2 = y by A4,A5,A6,TARSKI:def 1;
      consider y1,y2 be set such that
A8:   a2 = [y1,y2] & y1 in X & y2 in Y by A1,RELSET_1:6;
      y1 = x & y2 = y by A4,A5,A8,TARSKI:def 1;
      hence thesis by A1,A6,A7,A8;
    end;
  end;
end;

theorem Th4:
  for X be trivial set, R be Relation of X st R is non empty
  holds ex x be set st R = {[x,x]}
proof
  let X be trivial set;
  let R be Relation of X such that
A1: R is non empty;
  consider x be set such that
A2: x in R by A1,XBOOLE_0:def 1;
  consider y,z be set such that
A3: x = [y,z] & y in X & z in X by A2,RELSET_1:6;
  consider a be set such that
A4: X = {a} by A3,REALSET1:def 4;
A5: y = a & z = a by A3,A4,TARSKI:def 1;
  R = {[a,a]}
  proof
    thus R c= {[a,a]}
    proof
      let r be set such that
A6:   r in R;
      consider y,z be set such that
A7:   r = [y,z] & y in X & z in X by A6,RELSET_1:6;
      y = a & z = a by A4,A7,TARSKI:def 1;
      hence thesis by A7,TARSKI:def 1;
    end;
    let z be set;
    assume z in {[a,a]};
    hence thesis by A2,A3,A5,TARSKI:def 1;
  end;
  hence thesis;
end;

registration
  let X be trivial set;
  cluster -> trivial reflexive symmetric transitive
    strongly_connected Relation of X;
  correctness
  proof
    let R be Relation of X,X;
A1: R is_reflexive_in field R
    proof
      per cases;
      suppose
A2:     R is empty;
        let x be set;
        assume x in field R;
        hence thesis by A2,RELAT_1:63;
      end;
      suppose R is non empty;
        then consider z be set such that
A3:     R = {[z,z]} by Th4;
A4:     dom R = {z} & rng R = {z} by A3,RELAT_1:23;
        let x be set;
        assume x in field R;
        then x in dom R \/ rng R by RELAT_1:def 6;
        then x = z by A4,TARSKI:def 1;
        hence thesis by A3,TARSKI:def 1;
      end;
    end;
A5: R is_symmetric_in field R
    proof
      per cases;
      suppose
A6:     R is empty;
        let x,y be set;
        assume x in field R & y in field R & [x,y] in R;
        hence thesis by A6;
      end;
      suppose R is non empty;
        then consider a be set such that
A7:     R = {[a,a]} by Th4;
        let x,y be set;
        assume x in field R & y in field R & [x,y] in R;
        then [x,y] = [a,a] by A7,TARSKI:def 1;
        then x = a & y = a by ZFMISC_1:33;
        hence thesis by A7,TARSKI:def 1;
      end;
    end;
A8: R is_transitive_in field R
    proof
      per cases;
      suppose
A9:     R is empty;
        let x,y,z be set;
        assume x in field R & y in field R & z in field R &
        [x,y] in R & [y,z] in R;
        hence thesis by A9;
      end;
      suppose R is non empty;
        then consider a be set such that
A10:    R = {[a,a]} by Th4;
        let x,y,z be set;
        assume x in field R & y in field R & z in field R
        & [x,y] in R & [y,z] in R;
        then [x,y] = [a,a] & [y,z] = [a,a] by A10,TARSKI:def 1;
        then x = a & y = a & z = a by ZFMISC_1:33;
        hence thesis by A10,TARSKI:def 1;
      end;
    end;
    R is_strongly_connected_in field R
    proof
      per cases;
      suppose
A11:    R is empty;
        let x,y be set;
        assume x in field R & y in field R;
        hence thesis by A11,RELAT_1:63;
      end;
      suppose R is non empty;
        then consider a be set such that
A12:    R = {[a,a]} by Th4;
A13:    dom R = {a} & rng R = {a} by A12,RELAT_1:23;
        let x,y be set;
        assume x in field R & y in field R;
        then x in dom R \/ rng R & y in dom R \/ rng R by RELAT_1:def 6;
        then x = a & y = a by A13,TARSKI:def 1;
        hence thesis by A12,TARSKI:def 1;
      end;
    end;
    hence thesis by A1,A5,A8,RELAT_2:def 9,def 11,def 15,def 16;
  end;
end;

theorem Th5:
  for X be non empty trivial set, R be Relation of X holds R is_symmetric_in X
proof
  let X be non empty trivial set;
  let R be Relation of X;
  consider x be set such that
A1: X = {x} by REALSET1:def 4;
  let a,b be set;
  assume
A2: a in X & b in X & [a,b] in R;
  then a = x & b = x by A1,TARSKI:def 1;
  hence thesis by A2;
end;

registration
  cluster non empty strict finite irreflexive symmetric RelStr;
  correctness
  proof
    set X = {0,1}, r = {[0,1],[1,0]};
    0 in X & 1 in X by TARSKI:def 2;
    then
A1: [0,1] in [:X,X:] & [1,0] in [:X,X:] by ZFMISC_1:def 2;
    r c= [:X,X:]
    proof
      let x be set;
      assume x in r;
      hence thesis by A1,TARSKI:def 2;
    end;
    then reconsider r as Relation of X,X ;
    take R = RelStr (#X,r#);
    for x,y be set st x in X & y in X & [x,y] in r holds [y,x] in r
    proof
      let x,y be set;
      assume
A2:   x in X & y in X & [x,y] in r;
      per cases by A2,TARSKI:def 2;
      suppose [x,y] = [0,1];
        then x = 0 & y = 1 by ZFMISC_1:33;
        hence thesis by TARSKI:def 2;
      end;
      suppose [x,y] = [1,0];
        then x = 1 & y = 0 by ZFMISC_1:33;
        hence thesis by TARSKI:def 2;
      end;
    end;
    then
A3: r is_symmetric_in X by RELAT_2:def 3;
    for x being set st x in the carrier of R
    holds not [x,x] in the InternalRel of R
    proof
      let x be set;
      assume x in the carrier of R;
      then
A4:   x = 0 or x = 1 by TARSKI:def 2;
A5:   not [0,0] in r
      proof
        assume [0,0] in r;
        then [0,0] = [0,1] or [0,0] = [1,0] by TARSKI:def 2;
        hence contradiction by ZFMISC_1:33;
      end;
      not [1,1] in r
      proof
        assume [1,1] in r;
        then [1,1] = [0,1] or [1,1] = [1,0] by TARSKI:def 2;
        hence contradiction by ZFMISC_1:33;
      end;
      hence thesis by A4,A5;
    end;
    hence thesis by A3,NECKLACE:def 4,def 6;
  end;
end;

registration
  let L be irreflexive RelStr;
  cluster -> irreflexive (full SubRelStr of L);
  correctness
  proof
    let S be full SubRelStr of L;
A1: the carrier of S c= the carrier of L &
    the InternalRel of S = (the InternalRel of L) |_2 the carrier of S
    by YELLOW_0:def 13,def 14;
    let x be set;
    assume x in the carrier of S;
    then not [x,x] in the InternalRel of L by A1,NECKLACE:def 6;
    hence thesis by A1,XBOOLE_0:def 4;
  end;
end;

registration
  let L be symmetric RelStr;
  cluster -> symmetric (full SubRelStr of L);
  correctness
  proof
    let S be full SubRelStr of L;
A1: the carrier of S c= the carrier of L &
    the InternalRel of S = (the InternalRel of L) |_2 the carrier of S
    by YELLOW_0:def 13,def 14;
    let x,y be set;
    assume
A2: x in the carrier of S & y in the carrier of S &
    [x,y] in the InternalRel of S;
    then
A3: x in the carrier of L & y in the carrier of L &
    [x,y] in the InternalRel of L by A1,XBOOLE_0:def 4;
    the InternalRel of L is_symmetric_in the carrier of L by NECKLACE:def 4;
    then
A4: [y,x] in the InternalRel of L by A3,RELAT_2:def 3;
    [y,x] in [:the carrier of S,the carrier of S:] by A2,ZFMISC_1:106;
    hence thesis by A1,A4,XBOOLE_0:def 4;
  end;
end;

theorem Th6:
  for R be irreflexive symmetric RelStr st card (the carrier of R) = 2
  holds ex a,b be set st the carrier of R = {a,b} &
  (the InternalRel of R = {[a,b],[b,a]} or the InternalRel of R = {})
proof
  let R be irreflexive symmetric RelStr;
  assume
A1: card (the carrier of R) = 2;
  then reconsider X = the carrier of R as finite set;
  consider a,b be set such that
A2: a <> b & X = {a,b} by A1,CARD_2:79;
  set Q = the InternalRel of R;
A3: the InternalRel of R c= {[a,b],[b,a]}
  proof
    let x be set;
    assume
A4: x in the InternalRel of R;
    then consider x1,x2 be set such that
A5: x = [x1,x2] & x1 in X & x2 in X by RELSET_1:6;
A6: (x1 = a or x1 = b) & (x2 = a or x2 = b) by A2,A5,TARSKI:def 2;
    per cases by A5,A6;
    suppose
A7:   x = [a,a];
      a in the carrier of R by A2,TARSKI:def 2;
      hence thesis by A4,A7,NECKLACE:def 6;
    end;
    suppose x = [a,b];
      hence thesis by TARSKI:def 2;
    end;
    suppose x = [b,a];
      hence thesis by TARSKI:def 2;
    end;
    suppose
A8:   x = [b,b];
      b in the carrier of R by A2,TARSKI:def 2;
      hence thesis by A4,A8,NECKLACE:def 6;
    end;
  end;
  per cases by A3,ZFMISC_1:42;
  suppose Q = {};
    hence thesis by A2;
  end;
  suppose
A9: Q = {[a,b]};
    then
A10: [a,b] in Q by TARSKI:def 1;
A11: a in X & b in X by A2,TARSKI:def 2;
    Q is_symmetric_in X by NECKLACE:def 4;
    then [b,a] in Q by A10,A11,RELAT_2:def 3;
    then [b,a] = [a,b] by A9,TARSKI:def 1;
    hence thesis by A2,ZFMISC_1:33;
  end;
  suppose
A12: Q = {[b,a]};
    then
A13: [b,a] in Q by TARSKI:def 1;
A14: a in X & b in X by A2,TARSKI:def 2;
    Q is_symmetric_in X by NECKLACE:def 4;
    then [a,b] in Q by A13,A14,RELAT_2:def 3;
    then [b,a] = [a,b] by A12,TARSKI:def 1;
    hence thesis by A2,ZFMISC_1:33;
  end;
  suppose Q = {[a,b],[b,a]};
    hence thesis by A2;
  end;
end;

begin :: Some facts about operations 'union_of' and 'sum_of'

registration
  let R be non empty RelStr,S be RelStr;
  cluster union_of(R,S) -> non empty;
  correctness
  proof
    (the carrier of R) \/ (the carrier of S) is non empty;
    then the carrier of union_of(R,S) is non empty by NECKLA_2:def 2;
    hence thesis;
  end;
  cluster sum_of(R,S) -> non empty;
  correctness
  proof
    (the carrier of R) \/ (the carrier of S) is non empty;
    then the carrier of sum_of(R,S) is non empty by NECKLA_2:def 3;
    hence thesis;
  end;
end;

registration
  let R be RelStr, S be non empty RelStr;
  cluster union_of(R,S) -> non empty;
  correctness
  proof
    (the carrier of R) \/ (the carrier of S) is non empty;
    then the carrier of union_of(R,S) is non empty by NECKLA_2:def 2;
    hence thesis;
  end;
  cluster sum_of(R,S) -> non empty;
  correctness
  proof
    (the carrier of R) \/ (the carrier of S) is non empty;
    then the carrier of sum_of(R,S) is non empty by NECKLA_2:def 3;
    hence thesis;
  end;
end;

registration
  let R,S be finite RelStr;
  cluster union_of(R,S) -> finite;
  correctness
  proof
    (the carrier of R) \/ the carrier of S is finite;
    then the carrier of union_of(R,S) is finite by NECKLA_2:def 2;
    hence thesis;
  end;
  cluster sum_of(R,S) -> finite;
  correctness
  proof
    (the carrier of R) \/ the carrier of S is finite;
    then the carrier of sum_of(R,S) is finite by NECKLA_2:def 3;
    hence thesis;
  end;
end;

registration
  let R,S be symmetric RelStr;
  cluster union_of(R,S) -> symmetric;
  correctness
  proof
    set U = union_of(R,S), cU = the carrier of U, IU = the InternalRel of U,
    cR = the carrier of R, cS = the carrier of S;
    let x,y be set;
    assume x in cU & y in cU & [x,y] in IU;
    then
A1: [x,y] in (the InternalRel of R) \/ the InternalRel of S by NECKLA_2:def 2;
    per cases by A1,XBOOLE_0:def 3;
    suppose
A2:   [x,y] in the InternalRel of R;
A3:   the InternalRel of R is_symmetric_in cR by NECKLACE:def 4;
      x in cR & y in cR by A2,ZFMISC_1:106;
      then [y,x] in the InternalRel of R by A2,A3,RELAT_2:def 3;
      then [y,x] in (the InternalRel of R) \/ the InternalRel of S
      by XBOOLE_0:def 3;
      hence thesis by NECKLA_2:def 2;
    end;
    suppose
A4:   [x,y] in the InternalRel of S;
A5:   the InternalRel of S is_symmetric_in cS by NECKLACE:def 4;
      x in cS & y in cS by A4,ZFMISC_1:106;
      then [y,x] in the InternalRel of S by A4,A5,RELAT_2:def 3;
      then [y,x] in (the InternalRel of R) \/ the InternalRel of S
      by XBOOLE_0:def 3;
      hence thesis by NECKLA_2:def 2;
    end;
  end;
  cluster sum_of(R,S) -> symmetric;
  correctness
  proof
    set SU = sum_of(R,S), cSU = the carrier of sum_of(R,S),
    ISU = the InternalRel of SU, cR = the carrier of R,
    IR = the InternalRel of R, cS = the carrier of S,
    IS = the InternalRel of S;
A6: IR is_symmetric_in cR by NECKLACE:def 4;
A7: IS is_symmetric_in cS by NECKLACE:def 4;
    the InternalRel of sum_of(R,S) is_symmetric_in cSU
    proof
      let x,y be set;
      assume x in cSU & y in cSU & [x,y] in ISU;
      then [x,y] in IR \/ IS \/ [:cR,cS:] \/ [:cS,cR:] by NECKLA_2:def 3;
      then [x,y] in IR \/ IS \/ [:cR,cS:] or [x,y] in [:cS,cR:]
      by XBOOLE_0:def 3;
      then
A8:   [x,y] in IR \/ IS or [x,y] in [:cR,cS:] or [x,y] in [:cS, cR:]
      by XBOOLE_0:def 3;
      per cases by A8,XBOOLE_0:def 3;
      suppose
A9:     [x,y] in IR;
        then x in cR & y in cR by ZFMISC_1:106;
        then [y,x] in IR by A6,A9,RELAT_2:def 3;
        then [y,x] in IR \/ IS by XBOOLE_0:def 3;
        then [y,x] in IR \/ IS \/ [:cR,cS:] by XBOOLE_0:def 3;
        then [y,x] in IR \/ IS \/ [:cR,cS:] \/ [:cS,cR:] by XBOOLE_0:def 3;
        hence thesis by NECKLA_2:def 3;
      end;
      suppose
A10:    [x,y] in IS;
        then x in cS & y in cS by ZFMISC_1:106;
        then [y,x] in IS by A7,A10,RELAT_2:def 3;
        then [y,x] in IR \/ IS by XBOOLE_0:def 3;
        then [y,x] in IR \/ IS \/ [:cR,cS:] by XBOOLE_0:def 3;
        then [y,x] in IR \/ IS \/ [:cR,cS:] \/ [:cS,cR:] by XBOOLE_0:def 3;
        hence thesis by NECKLA_2:def 3;
      end;
      suppose [x,y] in [:cR,cS:];
        then x in cR & y in cS by ZFMISC_1:106;
        then [y,x] in [:cS,cR:] by ZFMISC_1:106;
        then [y,x] in [:cR,cS:] \/ [:cS,cR:] by XBOOLE_0:def 3;
        then [y,x] in IS \/ ([:cR,cS:] \/ [:cS,cR:]) by XBOOLE_0:def 3;
        then [y,x] in IS \/ [:cR,cS:] \/ [:cS,cR:] by XBOOLE_1:4;
        then [y,x] in IR \/ (IS \/ [:cR,cS:] \/ [:cS,cR:]) by XBOOLE_0:def 3;
        then [y,x] in IR \/ (IS \/ ([:cR,cS:] \/ [:cS,cR:])) by XBOOLE_1:4;
        then [y,x] in IR \/ IS \/ ([:cR,cS:] \/ [:cS,cR:]) by XBOOLE_1:4;
        then [y,x] in IR \/ IS \/ [:cR,cS:] \/ [:cS,cR:] by XBOOLE_1:4;
        hence thesis by NECKLA_2:def 3;
      end;
      suppose [x,y] in [:cS,cR:];
        then x in cS & y in cR by ZFMISC_1:106;
        then [y,x] in [:cR,cS:] by ZFMISC_1:106;
        then [y,x] in [:cR,cS:] \/ [:cS,cR:] by XBOOLE_0:def 3;
        then [y,x] in IS \/ ([:cR,cS:] \/ [:cS,cR:]) by XBOOLE_0:def 3;
        then [y,x] in IS \/ [:cR,cS:] \/ [:cS,cR:] by XBOOLE_1:4;
        then [y,x] in IR \/ (IS \/ [:cR,cS:] \/ [:cS,cR:]) by XBOOLE_0:def 3;
        then [y,x] in IR \/ (IS \/ ([:cR,cS:] \/ [:cS,cR:])) by XBOOLE_1:4;
        then [y,x] in IR \/ IS \/ ([:cR,cS:] \/ [:cS,cR:]) by XBOOLE_1:4;
        then [y,x] in IR \/ IS \/ [:cR,cS:] \/ [:cS,cR:] by XBOOLE_1:4;
        hence thesis by NECKLA_2:def 3;
      end;
    end;
    hence thesis by NECKLACE:def 4;
  end;
end;

registration
  let R,S be irreflexive RelStr;
  cluster union_of(R,S) -> irreflexive;
  correctness
  proof
    set U = union_of(R,S), cU = the carrier of U, IU = the InternalRel of U,
    cR = the carrier of R, cS = the carrier of S;
    for x be set st x in cU holds not [x,x] in IU
    proof
      let x be set such that x in cU;
      assume not thesis;
      then
A1:   [x,x] in (the InternalRel of R) \/ the InternalRel of S
      by NECKLA_2:def 2;
      per cases by A1,XBOOLE_0:def 3;
      suppose
A2:     [x,x] in the InternalRel of R;
        then x in cR by ZFMISC_1:106;
        hence thesis by A2,NECKLACE:def 6;
      end;
      suppose
A3:     [x,x] in the InternalRel of S;
        then x in cS by ZFMISC_1:106;
        hence thesis by A3,NECKLACE:def 6;
      end;
    end;
    hence thesis by NECKLACE:def 6;
  end;
end;

theorem
  for R,S be irreflexive RelStr st the carrier of R misses the carrier of S
  holds sum_of(R,S) is irreflexive
proof
  let R,S be irreflexive RelStr such that
A1: the carrier of R misses the carrier of S;
  for x be set st x in the carrier of sum_of(R,S)
  holds not [x,x] in the InternalRel of sum_of(R,S)
  proof
    let x be set;
    set IR = the InternalRel of R, IS = the InternalRel of S,
    RS = [:the carrier of R,the carrier of S:],
    SR = [:the carrier of S,the carrier of R:];
    assume x in the carrier of sum_of(R,S);
    assume not thesis;
    then [x,x] in IR \/ IS \/ RS \/ SR by NECKLA_2:def 3;
    then [x,x] in IR \/ IS \/ RS or [x,x] in SR by XBOOLE_0:def 3;
    then
A2: [x,x] in IR \/ IS or [x,x] in RS or [x,x] in SR by XBOOLE_0:def 3;
    per cases by A2,XBOOLE_0:def 3;
    suppose
A3:   [x,x] in IR;
      then x in the carrier of R by ZFMISC_1:106;
      hence thesis by A3,NECKLACE:def 6;
    end;
    suppose
A4:   [x,x] in IS;
      then x in the carrier of S by ZFMISC_1:106;
      hence thesis by A4,NECKLACE:def 6;
    end;
    suppose [x,x] in RS;
      then x in the carrier of R & x in the carrier of S by ZFMISC_1:106;
      hence thesis by A1,XBOOLE_0:3;
    end;
    suppose [x,x] in SR;
      then x in the carrier of S & x in the carrier of R by ZFMISC_1:106;
      hence thesis by A1,XBOOLE_0:3;
    end;
  end;
  hence thesis by NECKLACE:def 6;
end;

theorem Th8:
  for R1,R2 being RelStr holds
  union_of(R1,R2) = union_of(R2,R1) & sum_of(R1,R2) = sum_of(R2,R1)
proof
  let R1,R2 be RelStr;
  set U1 = union_of(R1,R2), S1 = sum_of(R1,R2);
A1: the carrier of U1 = (the carrier of R2) \/ the carrier of R1
  by NECKLA_2:def 2;
A2: the InternalRel of U1 = (the InternalRel of R2) \/ the InternalRel of R1
  by NECKLA_2:def 2;
A3: the carrier of S1 = (the carrier of R2) \/ the carrier of R1
  by NECKLA_2:def 3;
  the InternalRel of S1 = (the InternalRel of R1) \/ (the InternalRel of R2 )
  \/ [:the carrier of R1, the carrier of R2:]
  \/ [:the carrier of R2, the carrier of R1:] by NECKLA_2:def 3
    .= (the InternalRel of R2) \/ (the InternalRel of R1)
  \/ [:the carrier of R2, the carrier of R1:]
  \/ [:the carrier of R1, the carrier of R2:] by XBOOLE_1:4;
  hence thesis by A1,A2,A3,NECKLA_2:def 2,def 3;
end;

theorem Th9:
  for G being irreflexive RelStr, G1,G2 being RelStr st
  ( G = union_of(G1,G2) or G = sum_of(G1,G2) )
  holds G1 is irreflexive & G2 is irreflexive
proof
  let G be irreflexive RelStr, G1,G2 be RelStr;
  assume
A1: G = union_of(G1,G2) or G = sum_of(G1,G2);
  per cases by A1;
  suppose
A2: G = union_of(G1,G2);
    assume
A3: not thesis;
    thus thesis
    proof
      per cases by A3;
      suppose not G1 is irreflexive;
        then consider x being set such that
A4:     x in the carrier of G1 and
A5:     [x,x] in the InternalRel of G1 by NECKLACE:def 6;
        x in (the carrier of G1) \/ the carrier of G2 by A4,XBOOLE_0:def 3;
        then
A6:     x in the carrier of G by A2,NECKLA_2:def 2;
        [x,x] in (the InternalRel of G1) \/ the InternalRel of G2
        by A5,XBOOLE_0:def 3;
        then [x,x] in the InternalRel of G by A2,NECKLA_2:def 2;
        hence thesis by A6,NECKLACE:def 6;
      end;
      suppose not G2 is irreflexive;
        then consider x being set such that
A7:     x in the carrier of G2 and
A8:     [x,x] in the InternalRel of G2 by NECKLACE:def 6;
        x in (the carrier of G1) \/ the carrier of G2 by A7,XBOOLE_0:def 3;
        then
A9:     x in the carrier of G by A2,NECKLA_2:def 2;
        [x,x] in (the InternalRel of G1) \/ the InternalRel of G2
        by A8,XBOOLE_0:def 3;
        then [x,x] in the InternalRel of G by A2,NECKLA_2:def 2;
        hence thesis by A9,NECKLACE:def 6;
      end;
    end;
  end;
  suppose
A10: G = sum_of(G1,G2);
    assume
A11: not thesis;
    thus thesis
    proof
      per cases by A11;
      suppose not G1 is irreflexive;
        then consider x being set such that
A12:    x in the carrier of G1 and
A13:    [x,x] in the InternalRel of G1 by NECKLACE:def 6;
        x in (the carrier of G1) \/ the carrier of G2 by A12,XBOOLE_0:def 3;
        then
A14:    x in the carrier of G by A10,NECKLA_2:def 3;
        [x,x] in (the InternalRel of G1) \/ the InternalRel of G2
        by A13,XBOOLE_0:def 3;
        then [x,x] in (the InternalRel of G1) \/ (the InternalRel of G2)
        \/ [:the carrier of G1,the carrier of G2:] by XBOOLE_0:def 3;
        then [x,x] in (the InternalRel of G1) \/ (the InternalRel of G2)
        \/ [:the carrier of G1,the carrier of G2:]
        \/ [:the carrier of G2,the carrier of G1:] by XBOOLE_0:def 3;
        then [x,x] in the InternalRel of G by A10,NECKLA_2:def 3;
        hence thesis by A14,NECKLACE:def 6;
      end;
      suppose not G2 is irreflexive;
        then consider x being set such that
A15:    x in the carrier of G2 and
A16:    [x,x] in the InternalRel of G2 by NECKLACE:def 6;
        x in (the carrier of G1) \/ the carrier of G2 by A15,XBOOLE_0:def 3;
        then
A17:    x in the carrier of G by A10,NECKLA_2:def 3;
        [x,x] in (the InternalRel of G1) \/ the InternalRel of G2
        by A16,XBOOLE_0:def 3;
        then [x,x] in (the InternalRel of G1) \/ (the InternalRel of G2)
        \/ [:the carrier of G1,the carrier of G2:] by XBOOLE_0:def 3;
        then [x,x] in (the InternalRel of G1) \/ (the InternalRel of G2)
        \/ [:the carrier of G1,the carrier of G2:]
        \/ [:the carrier of G2,the carrier of G1:] by XBOOLE_0:def 3;
        then [x,x] in the InternalRel of G by A10,NECKLA_2:def 3;
        hence thesis by A17,NECKLACE:def 6;
      end;
    end;
  end;
end;

theorem Th10:
  for G being non empty RelStr, H1,H2 being RelStr
  st the carrier of H1 misses the carrier of H2 &
  ( the RelStr of G = union_of(H1,H2) or the RelStr of G = sum_of(H1,H2) )
  holds H1 is full SubRelStr of G & H2 is full SubRelStr of G
proof
  let G be non empty RelStr;
  let H1,H2 be RelStr;
  assume that
A1: the carrier of H1 misses the carrier of H2 and
A2: the RelStr of G = union_of(H1,H2) or the RelStr of G = sum_of(H1,H2);
  set cH1 = the carrier of H1, cH2 = the carrier of H2,
  IH1 = the InternalRel of H1, IH2 = the InternalRel of H2,
  H1H2 = [:cH1,cH2:], H2H1 = [:cH2,cH1:];
  per cases by A2;
  suppose
A3: the RelStr of G = union_of(H1,H2);
    then
A4: the InternalRel of G = IH1 \/ IH2 by NECKLA_2:def 2;
    then
A5: IH1 c= the InternalRel of G by XBOOLE_1:7;
A6: the carrier of G = (the carrier of H1) \/ (the carrier of H2)
    by A3,NECKLA_2:def 2;
    then
A7: the carrier of H1 c= the carrier of G by XBOOLE_1:7;
A8: the carrier of H2 c= the carrier of G by A6,XBOOLE_1:7;
A9: the InternalRel of H2 c= the InternalRel of G by A4,XBOOLE_1:7;
A10: IH1 = (the InternalRel of G)|_2 cH1
    proof
      thus IH1 c= (the InternalRel of G)|_2 cH1
      proof
        let a be set;
        assume
A11:    a in IH1;
        the InternalRel of G = IH1 \/ IH2 by A3,NECKLA_2:def 2;
        then IH1 c= the InternalRel of G by XBOOLE_1:7;
        hence thesis by A11,XBOOLE_0:def 4;
      end;
      let a be set;
      assume a in (the InternalRel of G)|_2 cH1;
      then
A12:  a in the InternalRel of G & a in [:cH1,cH1:] by XBOOLE_0:def 4;
      then
A13:  a in IH1 \/ IH2 by A3,NECKLA_2:def 2;
      per cases by A13,XBOOLE_0:def 3;
      suppose a in IH1;
        hence thesis;
      end;
      suppose a in IH2;
        then consider x,y being set such that
A14:    a = [x,y] & x in cH2 & y in cH2 by RELSET_1:6;
        consider x1,y1 being set such that
A15:    x1 in cH1 & y1 in cH1 & a = [x1,y1] by A12,ZFMISC_1:def 2;
        x in cH1 & x in cH2 by A14,A15,ZFMISC_1:33;
        hence thesis by A1,XBOOLE_0:3;
      end;
    end;
    IH2 = (the InternalRel of G)|_2 cH2
    proof
      thus IH2 c= (the InternalRel of G)|_2 cH2
      proof
        let a be set;
        assume
A16:    a in IH2;
        the InternalRel of G = IH1 \/ IH2 by A3,NECKLA_2:def 2;
        then IH2 c= the InternalRel of G by XBOOLE_1:7;
        hence thesis by A16,XBOOLE_0:def 4;
      end;
      let a be set;
      assume a in (the InternalRel of G)|_2 cH2;
      then
A17:  a in the InternalRel of G & a in [:cH2,cH2:] by XBOOLE_0:def 4;
      then
A18:  a in IH1 \/ IH2 by A3,NECKLA_2:def 2;
      per cases by A18,XBOOLE_0:def 3;
      suppose a in IH1;
        then consider x,y being set such that
A19:    a = [x,y] & x in cH1 & y in cH1 by RELSET_1:6;
        consider x1,y1 being set such that
A20:    x1 in cH2 & y1 in cH2 & a = [x1,y1] by A17,ZFMISC_1:def 2;
        x = x1 & y = y1 by A19,A20,ZFMISC_1:33;
        then cH1 /\ cH2 <> {} by A19,A20,XBOOLE_0:def 4;
        hence thesis by A1,XBOOLE_0:def 7;
      end;
      suppose a in IH2;
        hence thesis;
      end;
    end;
    hence thesis by A5,A7,A8,A9,A10,YELLOW_0:def 13,def 14;
  end;
  suppose
A21: the RelStr of G = sum_of(H1,H2);
    then
A22: the InternalRel of G = IH1 \/ IH2 \/ [:cH1, cH2:] \/ [:cH2, cH1:]
    by NECKLA_2:def 3;
    then
A23: IH1 \/ IH2 \/ [:cH1, cH2:] c= the InternalRel of G by XBOOLE_1:7;
    IH1 c= IH1 \/ (IH2 \/ [:cH1, cH2:]) by XBOOLE_1:7;
    then IH1 c= IH1 \/ IH2 \/ [:cH1, cH2:] by XBOOLE_1:4;
    then
A24: IH1 c= the InternalRel of G by A23,XBOOLE_1:1;
A25: the carrier of G = (the carrier of H1) \/ (the carrier of H2)
    by A21,NECKLA_2:def 3;
    then
A26: the carrier of H1 c= the carrier of G by XBOOLE_1:7;
A27: the carrier of H2 c= the carrier of G by A25,XBOOLE_1:7;
    IH2 c= IH1 \/ (IH2 \/ [:cH1, cH2:]) by XBOOLE_1:7,10;
    then IH2 c= IH1 \/ IH2 \/ [:cH1, cH2:] by XBOOLE_1:4;
    then
A28: IH2 c= IH1 \/ IH2 \/ [:cH1, cH2:] \/ [:cH2, cH1:] by XBOOLE_1:10;
A29: IH1 = (the InternalRel of G)|_2 cH1
    proof
      thus IH1 c= (the InternalRel of G)|_2 cH1
      proof
        let a be set;
        assume
A30:    a in IH1;
        the InternalRel of G = IH1 \/ IH2 \/ H1H2 \/ H2H1 by A21,NECKLA_2:def 3
          .= IH1 \/ (IH2 \/ H1H2 \/ H2H1) by XBOOLE_1:113;
        then IH1 c= the InternalRel of G by XBOOLE_1:7;
        hence thesis by A30,XBOOLE_0:def 4;
      end;
      let a be set;
      assume a in (the InternalRel of G)|_2 cH1;
      then
A31:  a in the InternalRel of G & a in [:cH1,cH1:] by XBOOLE_0:def 4;
      then a in IH1 \/ IH2 \/ H1H2 \/ H2H1 by A21,NECKLA_2:def 3;
      then a in IH1 \/ (IH2 \/ H1H2 \/ H2H1) by XBOOLE_1:113;
      then a in IH1 or a in IH2 \/ H1H2 \/ H2H1 by XBOOLE_0:def 3;
      then a in IH1 or a in IH2 \/ (H1H2 \/ H2H1) by XBOOLE_1:4;
      then
A32:  a in IH1 or a in IH2 or a in H1H2 \/ H2H1 by XBOOLE_0:def 3;
      per cases by A32,XBOOLE_0:def 3;
      suppose a in IH1;
        hence thesis;
      end;
      suppose a in IH2;
        then consider x,y being set such that
A33:    a = [x,y] & x in cH2 & y in cH2 by RELSET_1:6;
        consider x1,y1 being set such that
A34:    x1 in cH1 & y1 in cH1 & a = [x1,y1] by A31,ZFMISC_1:def 2;
        x = x1 & y = y1 by A33,A34,ZFMISC_1:33;
        then cH1 /\ cH2 <> {} by A33,A34,XBOOLE_0:def 4;
        hence thesis by A1,XBOOLE_0:def 7;
      end;
      suppose a in H1H2;
        then consider x,y being set such that
A35:    x in cH1 & y in cH2 & a = [x,y] by ZFMISC_1:def 2;
        consider x1,y1 being set such that
A36:    x1 in cH1 & y1 in cH1 & a = [x1,y1] by A31,ZFMISC_1:def 2;
        x = x1 & y = y1 by A35,A36,ZFMISC_1:33;
        then cH1 /\ cH2 <> {} by A35,A36,XBOOLE_0:def 4;
        hence thesis by A1,XBOOLE_0:def 7;
      end;
      suppose a in H2H1;
        then consider x,y being set such that
A37:    x in cH2 & y in cH1 & a = [x,y] by ZFMISC_1:def 2;
        consider x1,y1 being set such that
A38:    x1 in cH1 & y1 in cH1 & a = [x1,y1] by A31,ZFMISC_1:def 2;
        x = x1 & y = y1 by A37,A38,ZFMISC_1:33;
        then cH1 /\ cH2 <> {} by A37,A38,XBOOLE_0:def 4;
        hence thesis by A1,XBOOLE_0:def 7;
      end;
    end;
    IH2 = (the InternalRel of G)|_2 cH2
    proof
      thus IH2 c= (the InternalRel of G)|_2 cH2
      proof
        let a be set;
        assume
A39:    a in IH2;

the InternalRel of G = IH1 \/ IH2 \/ H1H2 \/ H2H1 by A21,NECKLA_2:def 3;

then the InternalRel of G = IH2 \/ (IH1 \/ H1H2 \/ H2H1) by XBOOLE_1:113;
        then IH2 c= the InternalRel of G by XBOOLE_1:7;
        hence thesis by A39,XBOOLE_0:def 4;
      end;
      let a be set;
      assume a in (the InternalRel of G)|_2 cH2;
      then
A40:  a in the InternalRel of G & a in [:cH2,cH2:] by XBOOLE_0:def 4;
      then a in IH1 \/ IH2 \/ H1H2 \/ H2H1 by A21,NECKLA_2:def 3;
      then a in IH1 \/ (IH2 \/ H1H2 \/ H2H1) by XBOOLE_1:113;
      then a in IH1 or a in IH2 \/ H1H2 \/ H2H1 by XBOOLE_0:def 3;
      then a in IH1 or a in IH2 \/ (H1H2 \/ H2H1) by XBOOLE_1:4;
      then
A41:  a in IH1 or a in IH2 or a in H1H2 \/ H2H1 by XBOOLE_0:def 3;
      per cases by A41,XBOOLE_0:def 3;
      suppose a in IH1;
        then consider x,y being set such that
A42:    a = [x,y] & x in cH1 & y in cH1 by RELSET_1:6;
        consider x1,y1 being set such that
A43:    x1 in cH2 & y1 in cH2 & a = [x1,y1] by A40,ZFMISC_1:def 2;
        x = x1 & y = y1 by A42,A43,ZFMISC_1:33;
        then cH1 /\ cH2 <> {} by A42,A43,XBOOLE_0:def 4;
        hence thesis by A1,XBOOLE_0:def 7;
      end;
      suppose a in IH2;
        hence thesis;
      end;
      suppose a in H1H2;
        then consider x,y being set such that
A44:    x in cH1 & y in cH2 & a = [x,y] by ZFMISC_1:def 2;
        consider x1,y1 being set such that
A45:    x1 in cH2 & y1 in cH2 & a = [x1,y1] by A40,ZFMISC_1:def 2;
        x = x1 & y = y1 by A44,A45,ZFMISC_1:33;
        then cH1 /\ cH2 <> {} by A44,A45,XBOOLE_0:def 4;
        hence thesis by A1,XBOOLE_0:def 7;
      end;
      suppose a in H2H1;
        then consider x,y being set such that
A46:    x in cH2 & y in cH1 & a = [x,y] by ZFMISC_1:def 2;
        consider x1,y1 being set such that
A47:    x1 in cH2 & y1 in cH2 & a = [x1,y1] by A40,ZFMISC_1:def 2;
        x = x1 & y = y1 by A46,A47,ZFMISC_1:33;
        then cH1 /\ cH2 <> {} by A46,A47,XBOOLE_0:def 4;
        hence thesis by A1,XBOOLE_0:def 7;
      end;
    end;
    hence thesis by A22,A24,A26,A27,A28,A29,YELLOW_0:def 13,def 14;
  end;
end;

begin :: Theorems relating to the complement of RelStr

theorem Th11:
  the InternalRel of ComplRelStr Necklace 4
  = {[0,2],[2,0],[0,3],[3,0],[1,3],[3,1]}
proof
  set N4 = Necklace 4, cN4 = the carrier of N4, CmpN4 = ComplRelStr N4;
A1: the carrier of Necklace 4 = {0,1,2,3} by NECKLACE:2,21;
  thus the InternalRel of CmpN4 c= {[0,2],[2,0],[0,3],[3,0],[1,3],[3,1]}
  proof
    let x be set;
    assume x in the InternalRel of CmpN4;
    then
A2: x in (the InternalRel of N4)` \ id cN4 by NECKLACE:def 9;
    then
A3: x in (the InternalRel of N4)` & not x in id cN4 by XBOOLE_0:def 5;
    then
A4: x in [:cN4,cN4:] \ (the InternalRel of N4) & not x in id cN4
    by SUBSET_1:def 5;
    consider a1,b1 being set such that
A5: a1 in cN4 & b1 in cN4 & x = [a1,b1] by A2,ZFMISC_1:def 2;
    per cases by A1,A5,ENUMSET1:def 2;
    suppose a1 = 0 & b1 = 0;
      hence thesis by A3,A5,RELAT_1:def 10;
    end;
    suppose a1 = 0 & b1 = 1;
      then x in the InternalRel of N4 by A5,ENUMSET1:def 4,NECKLA_2:2;
      hence thesis by A4,XBOOLE_0:def 5;
    end;
    suppose a1 = 0 & b1 = 2;
      hence thesis by A5,ENUMSET1:def 4;
    end;
    suppose a1 = 0 & b1 = 3;
      hence thesis by A5,ENUMSET1:def 4;
    end;
    suppose a1 = 1 & b1 = 0;
      then x in the InternalRel of N4 by A5,ENUMSET1:def 4,NECKLA_2:2;
      hence thesis by A4,XBOOLE_0:def 5;
    end;
    suppose a1 = 2 & b1 = 0;
      hence thesis by A5,ENUMSET1:def 4;
    end;
    suppose a1 = 3 & b1 = 0;
      hence thesis by A5,ENUMSET1:def 4;
    end;
    suppose a1 = 1 & b1 = 1;
      hence thesis by A3,A5,RELAT_1:def 10;
    end;
    suppose a1 = 1 & b1 = 2;
      then x in the InternalRel of N4 by A5,ENUMSET1:def 4,NECKLA_2:2;
      hence thesis by A4,XBOOLE_0:def 5;
    end;
    suppose a1 = 1 & b1 = 3;
      hence thesis by A5,ENUMSET1:def 4;
    end;
    suppose a1 = 2 & b1 = 2;
      hence thesis by A3,A5,RELAT_1:def 10;
    end;
    suppose a1 = 2 & b1 = 1;
      then x in the InternalRel of N4 by A5,ENUMSET1:def 4,NECKLA_2:2;
      hence thesis by A4,XBOOLE_0:def 5;
    end;
    suppose a1 = 2 & b1 = 3;
      then x in the InternalRel of N4 by A5,ENUMSET1:def 4,NECKLA_2:2;
      hence thesis by A4,XBOOLE_0:def 5;
    end;
    suppose a1 = 3 & b1 = 3;
      hence thesis by A3,A5,RELAT_1:def 10;
    end;
    suppose a1 = 3 & b1 = 1;
      hence thesis by A5,ENUMSET1:def 4;
    end;
    suppose a1 = 3 & b1 = 2;
      then x in the InternalRel of N4 by A5,ENUMSET1:def 4,NECKLA_2:2;
      hence thesis by A4,XBOOLE_0:def 5;
    end;
  end;
  let a be set;
  assume
A6: a in {[0,2],[2,0],[0,3],[3,0],[1,3],[3,1]};
  per cases by A6,ENUMSET1:def 4;
  suppose
A7: a = [0,2];
A8: not a in the InternalRel of N4
    proof
      assume
A9:   not thesis;
      per cases by A9,ENUMSET1:def 4,NECKLA_2:2;
      suppose a = [0,1];
        hence contradiction by A7,ZFMISC_1:33;
      end;
      suppose a = [1,0];
        hence contradiction by A7,ZFMISC_1:33;
      end;
      suppose a = [1,2];
        hence contradiction by A7,ZFMISC_1:33;
      end;
      suppose a = [2,1];
        hence contradiction by A7,ZFMISC_1:33;
      end;
      suppose a = [2,3];
        hence contradiction by A7,ZFMISC_1:33;
      end;
      suppose a = [3,2];
        hence contradiction by A7,ZFMISC_1:33;
      end;
    end;
    0 in cN4 & 2 in cN4 by A1,ENUMSET1:def 2;
    then a in [:cN4,cN4:] & not a in (the InternalRel of N4) & not a in id cN4
    by A7,A8,RELAT_1:def 10,ZFMISC_1:106;
    then a in [:cN4,cN4:]\ (the InternalRel of N4) & not a in id cN4
    by XBOOLE_0:def 5;
    then a in (the InternalRel of N4)` & not a in id cN4 by SUBSET_1:def 5;
    then a in (the InternalRel of N4)` \ id cN4 by XBOOLE_0:def 5;
    hence thesis by NECKLACE:def 9;
  end;
  suppose
A10: a = [2,0];
A11: not a in the InternalRel of N4
    proof
      assume
A12:  not thesis;
      per cases by A12,ENUMSET1:def 4,NECKLA_2:2;
      suppose a = [0,1];
        hence contradiction by A10,ZFMISC_1:33;
      end;
      suppose a = [1,0];
        hence contradiction by A10,ZFMISC_1:33;
      end;
      suppose a = [1,2];
        hence contradiction by A10,ZFMISC_1:33;
      end;
      suppose a = [2,1];
        hence contradiction by A10,ZFMISC_1:33;
      end;
      suppose a = [2,3];
        hence contradiction by A10,ZFMISC_1:33;
      end;
      suppose a = [3,2];
        hence contradiction by A10,ZFMISC_1:33;
      end;
    end;
    0 in cN4 & 2 in cN4 by A1,ENUMSET1:def 2;
    then a in [:cN4,cN4:] & not a in (the InternalRel of N4) & not a in id cN4
    by A10,A11,RELAT_1:def 10,ZFMISC_1:106;
    then a in [:cN4,cN4:]\ (the InternalRel of N4) & not a in id cN4
    by XBOOLE_0:def 5;
    then a in (the InternalRel of N4)` & not a in id cN4 by SUBSET_1:def 5;
    then a in (the InternalRel of N4)` \ id cN4 by XBOOLE_0:def 5;
    hence thesis by NECKLACE:def 9;
  end;
  suppose
A13: a = [0,3];
A14: not a in the InternalRel of N4
    proof
      assume
A15:  not thesis;
      per cases by A15,ENUMSET1:def 4,NECKLA_2:2;
      suppose a = [0,1];
        hence contradiction by A13,ZFMISC_1:33;
      end;
      suppose a = [1,0];
        hence contradiction by A13,ZFMISC_1:33;
      end;
      suppose a = [1,2];
        hence contradiction by A13,ZFMISC_1:33;
      end;
      suppose a = [2,1];
        hence contradiction by A13,ZFMISC_1:33;
      end;
      suppose a = [2,3];
        hence contradiction by A13,ZFMISC_1:33;
      end;
      suppose a = [3,2];
        hence contradiction by A13,ZFMISC_1:33;
      end;
    end;
    0 in cN4 & 3 in cN4 by A1,ENUMSET1:def 2;
    then a in [:cN4,cN4:] & not a in (the InternalRel of N4) & not a in id cN4
    by A13,A14,RELAT_1:def 10,ZFMISC_1:106;
    then a in [:cN4,cN4:]\ (the InternalRel of N4) & not a in id cN4
    by XBOOLE_0:def 5;
    then a in (the InternalRel of N4)` & not a in id cN4 by SUBSET_1:def 5;
    then a in (the InternalRel of N4)` \ id cN4 by XBOOLE_0:def 5;
    hence thesis by NECKLACE:def 9;
  end;
  suppose
A16: a = [3,0];
A17: not a in the InternalRel of N4
    proof
      assume
A18:  not thesis;
      per cases by A18,ENUMSET1:def 4,NECKLA_2:2;
      suppose a = [0,1];
        hence contradiction by A16,ZFMISC_1:33;
      end;
      suppose a = [1,0];
        hence contradiction by A16,ZFMISC_1:33;
      end;
      suppose a = [1,2];
        hence contradiction by A16,ZFMISC_1:33;
      end;
      suppose a = [2,1];
        hence contradiction by A16,ZFMISC_1:33;
      end;
      suppose a = [2,3];
        hence contradiction by A16,ZFMISC_1:33;
      end;
      suppose a = [3,2];
        hence contradiction by A16,ZFMISC_1:33;
      end;
    end;
    0 in cN4 & 3 in cN4 by A1,ENUMSET1:def 2;
    then a in [:cN4,cN4:] & not a in (the InternalRel of N4) & not a in id cN4
    by A16,A17,RELAT_1:def 10,ZFMISC_1:106;
    then a in [:cN4,cN4:]\ (the InternalRel of N4) & not a in id cN4
    by XBOOLE_0:def 5;
    then a in (the InternalRel of N4)` & not a in id cN4 by SUBSET_1:def 5;
    then a in (the InternalRel of N4)` \ id cN4 by XBOOLE_0:def 5;
    hence thesis by NECKLACE:def 9;
  end;
  suppose
A19: a = [1,3];
A20: not a in the InternalRel of N4
    proof
      assume
A21:  not thesis;
      per cases by A21,ENUMSET1:def 4,NECKLA_2:2;
      suppose a = [0,1];
        hence contradiction by A19,ZFMISC_1:33;
      end;
      suppose a = [1,0];
        hence contradiction by A19,ZFMISC_1:33;
      end;
      suppose a = [1,2];
        hence contradiction by A19,ZFMISC_1:33;
      end;
      suppose a = [2,1];
        hence contradiction by A19,ZFMISC_1:33;
      end;
      suppose a = [2,3];
        hence contradiction by A19,ZFMISC_1:33;
      end;
      suppose a = [3,2];
        hence contradiction by A19,ZFMISC_1:33;
      end;
    end;
    1 in cN4 & 3 in cN4 by A1,ENUMSET1:def 2;
    then a in [:cN4,cN4:] & not a in (the InternalRel of N4) & not a in id cN4
    by A19,A20,RELAT_1:def 10,ZFMISC_1:106;
    then a in [:cN4,cN4:]\ (the InternalRel of N4) & not a in id cN4
    by XBOOLE_0:def 5;
    then a in (the InternalRel of N4)` & not a in id cN4 by SUBSET_1:def 5;
    then a in (the InternalRel of N4)` \ id cN4 by XBOOLE_0:def 5;
    hence thesis by NECKLACE:def 9;
  end;
  suppose
A22: a = [3,1];
A23: not a in the InternalRel of N4
    proof
      assume
A24:  not thesis;
      per cases by A24,ENUMSET1:def 4,NECKLA_2:2;
      suppose a = [0,1];
        hence contradiction by A22,ZFMISC_1:33;
      end;
      suppose a = [1,0];
        hence contradiction by A22,ZFMISC_1:33;
      end;
      suppose a = [1,2];
        hence contradiction by A22,ZFMISC_1:33;
      end;
      suppose a = [2,1];
        hence contradiction by A22,ZFMISC_1:33;
      end;
      suppose a = [2,3];
        hence contradiction by A22,ZFMISC_1:33;
      end;
      suppose a = [3,2];
        hence contradiction by A22,ZFMISC_1:33;
      end;
    end;
    1 in cN4 & 3 in cN4 by A1,ENUMSET1:def 2;
    then a in [:cN4,cN4:] & not a in (the InternalRel of N4) & not a in id cN4
    by A22,A23,RELAT_1:def 10,ZFMISC_1:106;
    then a in [:cN4,cN4:]\ (the InternalRel of N4) & not a in id cN4
    by XBOOLE_0:def 5;
    then a in (the InternalRel of N4)` & not a in id cN4 by SUBSET_1:def 5;
    then a in (the InternalRel of N4)` \ id cN4 by XBOOLE_0:def 5;
    hence thesis by NECKLACE:def 9;
  end;
end;

registration
  let R be RelStr;
  cluster ComplRelStr R -> irreflexive;
  correctness
  proof
    set R1 = ComplRelStr R;
    for x being set st x in the carrier of R1
    holds not [x,x] in the InternalRel of R1
    proof
      let x be set;
      assume x in the carrier of R1;
      then
A1:   x in the carrier of R by NECKLACE:def 9;
      not [x,x] in the InternalRel of R1
      proof
        assume [x,x] in the InternalRel of R1;
        then [x,x] in (the InternalRel of R)` \ id (the carrier of R)
        by NECKLACE:def 9;
        then [x,x] in (the InternalRel of R)` &
        not [x,x] in id (the carrier of R) by XBOOLE_0:def 5;
        hence contradiction by A1,RELAT_1:def 10;
      end;
      hence thesis;
    end;
    hence thesis by NECKLACE:def 6;
  end;
end;

registration
  let R be symmetric RelStr;
  cluster ComplRelStr R -> symmetric;
  correctness
  proof
    set S = ComplRelStr R;
    let x,y be set;
    assume
A1: x in the carrier of S & y in the carrier of S &
    [x,y] in the InternalRel of S;
    per cases;
    suppose x = y;
      hence thesis by A1;
    end;
    suppose
A2:   x <> y;
A3:   x in the carrier of R & y in the carrier of R by A1,NECKLACE:def 9;
      [x,y] in (the InternalRel of R)` \ id (the carrier of R)
      by A1,NECKLACE:def 9;
      then [x,y] in (the InternalRel of R)` &
      not [x,y] in id (the carrier of R) by XBOOLE_0:def 5;
      then [x,y] in [:the carrier of R,the carrier of R:] \ (the
      InternalRel of R) by SUBSET_1:def 5;
      then
A4:   [x,y] in [:the carrier of R,the carrier of R:] &
      not [x,y] in the InternalRel of R by XBOOLE_0:def 5;
      the InternalRel of R is_symmetric_in the carrier of R by NECKLACE:def 4;
      then [y,x] in [:the carrier of R,the carrier of R:] &
      not [y,x] in the InternalRel of R by A3,A4,RELAT_2:def 3,ZFMISC_1:106;
      then [y,x] in [:the carrier of R,the carrier of R:] \ (the
      InternalRel of R) by XBOOLE_0:def 5;
      then [y,x] in (the InternalRel of R)` &
      not [y,x] in id (the carrier of R) by A2,RELAT_1:def 10,SUBSET_1:def 5;
      then [y,x] in (the InternalRel of R)` \ id (the carrier of R)
      by XBOOLE_0:def 5;
      hence thesis by NECKLACE:def 9;
    end;
  end;
end;

theorem Th12:
  for R be RelStr holds
  the InternalRel of R misses the InternalRel of ComplRelStr R
proof
  let R be RelStr;
  assume not thesis;
  then (the InternalRel of R) /\ the InternalRel of ComplRelStr R <> {}
  by XBOOLE_0:def 7;
  then consider a being set such that
A1: a in (the InternalRel of R) /\ the InternalRel of ComplRelStr R
  by XBOOLE_0:def 1;
  a in the InternalRel of ComplRelStr R by A1,XBOOLE_0:def 4;
  then a in (the InternalRel of R)` \ id (the carrier of R) by NECKLACE:def 9;
  then a in (the InternalRel of R)` & not a in id (the carrier of R)
  by XBOOLE_0:def 5;
  then a in [:the carrier of R,the carrier of R:] \ (the InternalRel of R)
  by SUBSET_1:def 5;
  then not a in the InternalRel of R by XBOOLE_0:def 5;
  hence thesis by A1,XBOOLE_0:def 4;
end;

theorem Th13:
  for R being RelStr holds
  id the carrier of R misses the InternalRel of ComplRelStr R
proof
  let R be RelStr;
  assume not thesis;
  then id (the carrier of R) /\ the InternalRel of ComplRelStr R <> {}
  by XBOOLE_0:def 7;
  then consider a being set such that
A1: a in id (the carrier of R) /\ the InternalRel of ComplRelStr R
  by XBOOLE_0:def 1;
A2: a in id (the carrier of R) & a in the InternalRel of ComplRelStr R
  by A1,XBOOLE_0:def 4;
  then a in (the InternalRel of R)` \ id the carrier of R by NECKLACE:def 9;
  hence contradiction by A2,XBOOLE_0:def 5;
end;

theorem Th14:
  for G being RelStr holds
  [:the carrier of G,the carrier of G:] = id (the carrier of G) \/
  (the InternalRel of G) \/ (the InternalRel of ComplRelStr G)
proof
  let G be RelStr;
  set idcG = id the carrier of G, IG = the InternalRel of G,
  ICmpG = the InternalRel of ComplRelStr G, cG = the carrier of G;
  thus [:cG,cG:] c= idcG \/ IG \/ ICmpG
  proof
    let a be set;
    assume
A1: a in [:cG,cG:];
    then consider x,y being set such that
A2: x in cG & y in cG & a = [x,y] by ZFMISC_1:def 2;
    per cases;
    suppose
A3:   x = y;
      [x,x] in id cG by A2,RELAT_1:def 10;
      then a in id cG \/ IG by A2,A3,XBOOLE_0:def 3;
      hence thesis by XBOOLE_0:def 3;
    end;
    suppose x <> y;
      then
A4:   not a in id cG by A2,RELAT_1:def 10;
      thus thesis
      proof
        per cases;
        suppose a in IG;
          then a in id cG \/ IG by XBOOLE_0:def 3;
          hence thesis by XBOOLE_0:def 3;
        end;
        suppose not a in IG;
          then a in [:cG,cG:] \ IG by A1,XBOOLE_0:def 5;
          then a in IG` by SUBSET_1:def 5;
          then a in IG` \ id cG by A4,XBOOLE_0:def 5;
          then a in ICmpG by NECKLACE:def 9;
          then a in IG \/ ICmpG by XBOOLE_0:def 3;
          then a in id cG \/ (IG \/ ICmpG) by XBOOLE_0:def 3;
          hence thesis by XBOOLE_1:4;
        end;
      end;
    end;
  end;
  let a be set;
  assume a in idcG \/ IG \/ ICmpG;
  then
A5: a in id cG \/ IG or a in ICmpG by XBOOLE_0:def 3;
  per cases by A5,XBOOLE_0:def 3;
  suppose a in id cG;
    then consider x,y being set such that
A6: a = [x,y] & x in cG & y in cG by RELSET_1:6;
    thus thesis by A6,ZFMISC_1:106;
  end;
  suppose a in IG;
    then consider x,y being set such that
A7: a = [x,y] & x in cG & y in cG by RELSET_1:6;
    thus thesis by A7,ZFMISC_1:106;
  end;
  suppose a in ICmpG;
    then a in IG` \ id cG by NECKLACE:def 9;
    hence thesis;
  end;
end;

theorem Th15:
  for G being strict irreflexive RelStr st G is trivial
  holds ComplRelStr G = G
proof
  let G be strict irreflexive RelStr;
  set CG = ComplRelStr G;
  assume G is trivial;
  then
A1: the carrier of G is trivial;
  per cases by A1,REALSET1:def 4;
  suppose
A2: the carrier of G is empty;
    then the InternalRel of G c= [:{},{}:];
    then
A3: the InternalRel of G c= {};
A4: the InternalRel of G = {} by A3;
    the InternalRel of CG = (the InternalRel of G)` \
    id (the carrier of G) by NECKLACE:def 9;
    then the InternalRel of CG = ([:{},{}:] \ {}) \ id {}
    by A2;
    then the InternalRel of CG = ({} \ {}) \ id {};
    hence thesis by A4,NECKLACE:def 9;
  end;
  suppose ex x being set st the carrier of G = {x};
    then consider x being set such that
A5: the carrier of G = {x};
    the InternalRel of G c= [:{x},{x}:] by A5;
    then the InternalRel of G c= {[x,x]} by ZFMISC_1:35;
    then
A6: the InternalRel of G = {} or the InternalRel of G = {[x,x]}
    by ZFMISC_1:39;
A7: the InternalRel of G <> {[x,x]}
    proof
      assume not thesis;
      then
A8:   [x,x] in the InternalRel of G by TARSKI:def 1;
      x in the carrier of G by A5,TARSKI:def 1;
      hence contradiction by A8,NECKLACE:def 6;
    end;
A9: the carrier of CG = {x} by A5,NECKLACE:def 9;
    the InternalRel of CG = (the InternalRel of G)` \
    id (the carrier of G) by NECKLACE:def 9;
    then the InternalRel of CG = ([:{x},{x}:] \ {}) \ id {x}
    by A5,A6,A7,SUBSET_1:def 5;
    then the InternalRel of CG = {[x,x]} \ id {x} by ZFMISC_1:35;
    then the InternalRel of CG = {[x,x]} \ {[x,x]} by SYSREL:30;
    hence thesis by A5,A6,A7,A9,XBOOLE_1:37;
  end;
end;

theorem Th16:
  for G being strict irreflexive RelStr holds ComplRelStr (ComplRelStr G) = G
proof
  let G be strict irreflexive RelStr;
  set CCmpG = ComplRelStr (ComplRelStr G), CmpG = ComplRelStr G,
  cG = the carrier of G, IG = the InternalRel of G,
  ICmpG = the InternalRel of CmpG, ICCmpG = the InternalRel of CCmpG;
A1: cG = the carrier of CmpG by NECKLACE:def 9
    .= the carrier of CCmpG by NECKLACE:def 9;
A2: cG = the carrier of CmpG by NECKLACE:def 9;
A3: id cG misses IG
  proof
    assume not thesis;
    then id cG /\ IG <> {} by XBOOLE_0:def 7;
    then consider a being set such that
A4: a in id cG /\ IG by XBOOLE_0:def 1;
A5: a in id cG & a in IG by A4,XBOOLE_0:def 4;
    consider x,y being set such that
A6: a = [x,y] & x in cG & y in cG by A4,RELSET_1:6;
    x = y by A5,A6,RELAT_1:def 10;
    hence contradiction by A5,A6,NECKLACE:def 6;
  end;
  ICCmpG = (ICmpG)` \ id the carrier of CmpG by NECKLACE:def 9
    .= [:the carrier of CmpG,the carrier of CmpG:] \ ICmpG \
  id the carrier of CmpG by SUBSET_1:def 5
    .= ([:cG,cG:] \ (IG` \ id cG)) \ id cG by A2,NECKLACE:def 9
    .= (([:cG,cG:] \ IG`) \/ ([:cG,cG:] /\ id cG)) \ id cG by XBOOLE_1:52
    .= ( ([:cG,cG:] \ IG`) \/ id cG ) \ id cG by XBOOLE_1:28
    .= ([:cG,cG:] \ IG`) \ id cG by XBOOLE_1:40
    .= ([:cG,cG:] \ ([:cG,cG:] \ IG)) \ id cG by SUBSET_1:def 5
    .= ([:cG,cG:] /\ IG) \ id cG by XBOOLE_1:48
    .= IG \ id cG by XBOOLE_1:28
    .= IG by A3,XBOOLE_1:83;
  hence thesis by A1;
end;

theorem Th17:
  for G1,G2 being RelStr st the carrier of G1 misses the carrier of G2
  holds ComplRelStr union_of(G1,G2) = sum_of(ComplRelStr G1, ComplRelStr G2)
proof
  let G1,G2 be RelStr;
  assume
A1: the carrier of G1 misses the carrier of G2;
A2: the carrier of ComplRelStr union_of(G1,G2) = the carrier of union_of(G1,G2)
  by NECKLACE:def 9
    .= (the carrier of G1) \/ the carrier of G2 by NECKLA_2:def 2;
A3: the carrier of sum_of(ComplRelStr G1, ComplRelStr G2) =
  (the carrier of ComplRelStr G1) \/ (the carrier of ComplRelStr G2)
  by NECKLA_2:def 3
    .= (the carrier of G1) \/ the carrier of ComplRelStr G2 by NECKLACE:def 9
    .= (the carrier of G1) \/ the carrier of G2 by NECKLACE:def 9;
  set P = the InternalRel of ComplRelStr union_of(G1,G2),
  R = the InternalRel of sum_of(ComplRelStr G1,ComplRelStr G2),
  X1 = the InternalRel of ComplRelStr G1,
  X2 = the InternalRel of ComplRelStr G2,
  X3 = [:the carrier of ComplRelStr G1,the carrier of ComplRelStr G2:],
  X4 = [:the carrier of ComplRelStr G2,the carrier of ComplRelStr G1:],
  X5 = [:the carrier of G1, the carrier of G1:],
  X6 = [:the carrier of G2, the carrier of G2:],
  X7 = [:the carrier of G1, the carrier of G2:],
  X8 = [:the carrier of G2, the carrier of G1:];
  for a,b being set holds [a,b] in P iff [a,b] in R
  proof
    let a,b be set;
    set x = [a,b];
    thus x in P implies x in R
    proof
      assume x in P;
      then x in (the InternalRel of union_of(G1,G2))` \
      id the carrier of union_of(G1,G2) by NECKLACE:def 9;
      then
A4:   x in (the InternalRel of union_of(G1,G2))` &
      not x in id the carrier of union_of(G1,G2) by XBOOLE_0:def 5;
      then x in [:the carrier of union_of(G1,G2),the carrier of union_of(G1,
      G2):] \ (the InternalRel of union_of(G1,G2)) by SUBSET_1:def 5;
      then
A5:   x in [:the carrier of union_of(G1,G2),the carrier of union_of( G1,G2) :]
      & not x in (the InternalRel of union_of(G1,G2)) by XBOOLE_0:def 5;
A6:   the carrier of G1 = the carrier of ComplRelStr G1 &
      the carrier of G2 = the carrier of ComplRelStr G2 by NECKLACE:def 9;
      x in [:(the carrier of G1) \/ the carrier of G2,
      the carrier of union_of(G1,G2):] by A5,NECKLA_2:def 2;
      then x in [:(the carrier of G1) \/ the carrier of G2,
      (the carrier of G1) \/ the carrier of G2:] by NECKLA_2:def 2;
      then x in X5 \/ X3 \/ X4 \/ X6 by A6,ZFMISC_1:121;
      then x in X5 \/ (X3 \/ X4 \/ X6) by XBOOLE_1:113;
      then x in X5 or x in (X3 \/ X4 \/ X6) by XBOOLE_0:def 3;
      then x in X5 or x in X3 \/ (X4 \/ X6) by XBOOLE_1:4;
      then
A7:   x in X5 or x in X3 or x in X4 \/ X6 by XBOOLE_0:def 3;
      not x in (the InternalRel of G1) \/ the InternalRel of G2
      by A5,NECKLA_2:def 2;
      then
A8:   not x in the InternalRel of G1 & not x in the InternalRel of G2
      by XBOOLE_0:def 3;
A9:   not x in id ((the carrier of G1) \/ the carrier of G2)
      by A4,NECKLA_2:def 2;
A10:  not x in id the carrier of G1 & not x in id the carrier of G2
      proof
        assume not thesis;
        then x in id (the carrier of G1) \/ id (the carrier of G2)
        by XBOOLE_0:def 3;
        hence contradiction by A9,SYSREL:32;
      end;
      per cases by A7,XBOOLE_0:def 3;
      suppose x in [:the carrier of G1,the carrier of G1:];
        then x in [:the carrier of G1,the carrier of G1:] \ the InternalRel
        of G1 by A8,XBOOLE_0:def 5;
        then x in (the InternalRel of G1)` by SUBSET_1:def 5;
        then x in (the InternalRel of G1)` \ id the carrier of G1
        by A10,XBOOLE_0:def 5;
        then x in X1 by NECKLACE:def 9;
        then x in X1 \/ (X2 \/ X3 \/ X4) by XBOOLE_0:def 3;
        then x in X1 \/ X2 \/ X3 \/ X4 by XBOOLE_1:113;
        hence thesis by NECKLA_2:def 3;
      end;
      suppose x in X3;
        then x in X2 \/ X3 by XBOOLE_0:def 3;
        then x in X2 \/ X3 \/ X4 by XBOOLE_0:def 3;
        then x in X1 \/ (X2 \/ X3 \/ X4) by XBOOLE_0:def 3;
        then x in X1 \/ X2 \/ X3 \/ X4 by XBOOLE_1:113;
        hence thesis by NECKLA_2:def 3;
      end;
      suppose x in X4;
        then x in X3 \/ X4 by XBOOLE_0:def 3;
        then x in X2 \/ (X3 \/ X4) by XBOOLE_0:def 3;
        then x in X2 \/ X3 \/ X4 by XBOOLE_1:4;
        then x in X1 \/ (X2 \/ X3 \/ X4) by XBOOLE_0:def 3;
        then x in X1 \/ X2 \/ X3 \/ X4 by XBOOLE_1:113;
        hence thesis by NECKLA_2:def 3;
      end;
      suppose x in [:the carrier of G2,the carrier of G2:];
        then x in [:the carrier of G2,the carrier of G2:] \ the InternalRel
        of G2 by A8,XBOOLE_0:def 5;
        then x in (the InternalRel of G2)` by SUBSET_1:def 5;
        then x in (the InternalRel of G2)` \ id the carrier of G2
        by A10,XBOOLE_0:def 5;
        then x in the InternalRel of ComplRelStr G2 by NECKLACE:def 9;
        then x in (the InternalRel of ComplRelStr G1) \/
        (the InternalRel of ComplRelStr G2) by XBOOLE_0:def 3;
        then x in (the InternalRel of ComplRelStr G1) \/
        (the InternalRel of ComplRelStr G2) \/
        [:the carrier of ComplRelStr G1, the carrier of ComplRelStr G2:]
        by XBOOLE_0:def 3;
        then x in (the InternalRel of ComplRelStr G1) \/
        (the InternalRel of ComplRelStr G2) \/
        [:the carrier of ComplRelStr G1, the carrier of ComplRelStr G2:] \/
        [:the carrier of ComplRelStr G2, the carrier of ComplRelStr G1:]
        by XBOOLE_0:def 3;
        hence thesis by NECKLA_2:def 3;
      end;
    end;
    assume x in R;
    then x in X1 \/ X2 \/ X3 \/ X4 by NECKLA_2:def 3;
    then x in X1 \/ X2 \/ X3 or x in X4 by XBOOLE_0:def 3;
    then
A11: x in X1 \/ X2 or x in X3 or x in X4 by XBOOLE_0:def 3;
    per cases by A11,XBOOLE_0:def 3;
    suppose x in X1;
      then
A12:  x in (the InternalRel of G1)` \ id the carrier of G1 by NECKLACE:def 9;
      then x in (the InternalRel of G1)` &
      not x in id the carrier of G1 by XBOOLE_0:def 5;
      then x in [:the carrier of G1,the carrier of G1:] \ the InternalRel of
      G1 by SUBSET_1:def 5;
      then
A13:  x in [:the carrier of G1,the carrier of G1:] &
      not x in the InternalRel of G1 by XBOOLE_0:def 5;
      x in X5 \/ X7 by A12,XBOOLE_0:def 3;
      then x in X5 \/ X7 \/ X8 by XBOOLE_0:def 3;
      then x in X5 \/ X7 \/ X8 \/ X6 by XBOOLE_0:def 3;
      then x in [:(the carrier of G1) \/ the carrier of G2,
      (the carrier of G1) \/ the carrier of G2:] by ZFMISC_1:121;
      then x in [:(the carrier of G1) \/ the carrier of G2,
      the carrier of union_of(G1,G2):] by NECKLA_2:def 2;
      then
A14:  x in [:the carrier of union_of(G1,G2),
      the carrier of union_of(G1,G2):] by NECKLA_2:def 2;
      not x in the InternalRel of union_of(G1,G2)
      proof
        assume not thesis;
        then x in (the InternalRel of G1) \/ the InternalRel of G2
        by NECKLA_2:def 2;
        then x in the InternalRel of G2 by A13,XBOOLE_0:def 3;
        then [:the carrier of G1,the carrier of G1:] /\
        [:the carrier of G2,the carrier of G2:] is non empty
        by A12,XBOOLE_0:def 4;
        then [:the carrier of G1,the carrier of G1:] meets
        [:the carrier of G2,the carrier of G2:] by XBOOLE_0:def 7;
        hence contradiction by A1,ZFMISC_1:127;
      end;
      then x in [:the carrier of union_of(G1,G2),the carrier of union_of(G1,
      G2):] \ (the InternalRel of union_of(G1,G2)) by A14,XBOOLE_0:def 5;
      then
A15:  x in (the InternalRel of union_of(G1,G2))` by SUBSET_1:def 5;
      not x in id the carrier of union_of(G1,G2)
      proof
        assume not thesis;
        then x in id ((the carrier of G1) \/ the carrier of G2) by
NECKLA_2:def 2;
        then
A16:    x in (id the carrier of G1) \/ id the carrier of G2 by SYSREL:32;
        thus thesis
        proof
          per cases by A16,XBOOLE_0:def 3;
          suppose x in id the carrier of G1;
            hence contradiction by A12,XBOOLE_0:def 5;
          end;
          suppose x in id the carrier of G2;
            then [:the carrier of G1,the carrier of G1:] /\
            [:the carrier of G2,the carrier of G2:] is non empty
            by A12,XBOOLE_0:def 4;
            then [:the carrier of G1,the carrier of G1:] meets
            [:the carrier of G2,the carrier of G2:] by XBOOLE_0:def 7;
            hence contradiction by A1,ZFMISC_1:127;
          end;
        end;
      end;
      then x in (the InternalRel of union_of(G1,G2))` \
      id the carrier of union_of(G1,G2) by A15,XBOOLE_0:def 5;
      hence thesis by NECKLACE:def 9;
    end;
    suppose x in X2;
      then
A17:  x in (the InternalRel of G2)` \ id the carrier of G2 by NECKLACE:def 9;
      then x in (the InternalRel of G2)` &
      not x in id the carrier of G2 by XBOOLE_0:def 5;
      then x in [:the carrier of G2,the carrier of G2:] \ the InternalRel of
      G2 by SUBSET_1:def 5;
      then
A18:  x in [:the carrier of G2,the carrier of G2:] &
      not x in the InternalRel of G2 by XBOOLE_0:def 5;
      x in X8 \/ X6 by A17,XBOOLE_0:def 3;
      then x in X7 \/ (X8 \/ X6) by XBOOLE_0:def 3;
      then x in X7 \/ X8 \/ X6 by XBOOLE_1:4;
      then x in X5 \/ (X7 \/ X8 \/ X6) by XBOOLE_0:def 3;
      then x in X5 \/ X7 \/ X8 \/ X6 by XBOOLE_1:113;
      then x in [:(the carrier of G1) \/ the carrier of G2,
      (the carrier of G1) \/ the carrier of G2:] by ZFMISC_1:121;
      then x in [:(the carrier of G1) \/ the carrier of G2,
      the carrier of union_of(G1,G2):] by NECKLA_2:def 2;
      then
A19:  x in [:the carrier of union_of(G1,G2),
      the carrier of union_of(G1,G2):] by NECKLA_2:def 2;
      not x in the InternalRel of union_of(G1,G2)
      proof
        assume not thesis;
        then x in (the InternalRel of G1) \/ the InternalRel of G2
        by NECKLA_2:def 2;
        then x in the InternalRel of G1 by A18,XBOOLE_0:def 3;
        then [:the carrier of G1,the carrier of G1:] /\
        [:the carrier of G2,the carrier of G2:] is non empty
        by A17,XBOOLE_0:def 4;
        then [:the carrier of G1,the carrier of G1:] meets
        [:the carrier of G2,the carrier of G2:] by XBOOLE_0:def 7;
        hence contradiction by A1,ZFMISC_1:127;
      end;
      then x in [:the carrier of union_of(G1,G2),the carrier of union_of(G1
      ,G2):] \ (the InternalRel of union_of(G1,G2)) by A19,XBOOLE_0:def 5;
      then
A20:  x in (the InternalRel of union_of(G1,G2))` by SUBSET_1:def 5;
      not x in id the carrier of union_of(G1,G2)
      proof
        assume not thesis;
        then x in id ((the carrier of G1) \/ the carrier of G2) by
NECKLA_2:def 2;
        then
A21:    x in (id the carrier of G1) \/ id the carrier of G2 by SYSREL:32;
        per cases by A21,XBOOLE_0:def 3;
        suppose x in id the carrier of G2;
          hence contradiction by A17,XBOOLE_0:def 5;
        end;
        suppose x in id the carrier of G1;
          then [:the carrier of G1,the carrier of G1:] /\
          [:the carrier of G2,the carrier of G2:] is non empty
          by A17,XBOOLE_0:def 4;
          then [:the carrier of G1,the carrier of G1:] meets
          [:the carrier of G2,the carrier of G2:] by XBOOLE_0:def 7;
          hence contradiction by A1,ZFMISC_1:127;
        end;
      end;
      then x in (the InternalRel of union_of(G1,G2))` \
      id the carrier of union_of(G1,G2) by A20,XBOOLE_0:def 5;
      hence thesis by NECKLACE:def 9;
    end;
    suppose x in X3;
      then
A22:  x in [:the carrier of G1,the carrier of ComplRelStr G2:]
      by NECKLACE:def 9;
      then
A23:  x in [:the carrier of G1,the carrier of G2:] by NECKLACE:def 9;
      then x in X7 \/ X8 by XBOOLE_0:def 3;
      then x in X5 \/ (X7 \/ X8) by XBOOLE_0:def 3;
      then x in X5 \/ X7 \/ X8 by XBOOLE_1:4;
      then x in X5 \/ X7 \/ X8 \/ X6 by XBOOLE_0:def 3;
      then x in [:(the carrier of G1) \/ the carrier of G2,
      (the carrier of G1) \/ the carrier of G2:] by ZFMISC_1:121;
      then x in [:(the carrier of G1) \/ the carrier of G2,
      the carrier of union_of(G1,G2):] by NECKLA_2:def 2;
      then
A24:  x in [:the carrier of union_of(G1,G2),
      the carrier of union_of(G1,G2):] by NECKLA_2:def 2;
      not x in the InternalRel of union_of(G1,G2)
      proof
        assume not thesis;
        then
A25:    x in (the InternalRel of G1) \/ the InternalRel of G2
        by NECKLA_2:def 2;
        per cases by A25,XBOOLE_0:def 3;
        suppose x in the InternalRel of G1;
          then
A26:      a in the carrier of G1 & b in the carrier of G1 by ZFMISC_1:106;
          b in the carrier of G2 by A23,ZFMISC_1:106;
          then b in (the carrier of G1) /\ the carrier of G2
          by A26,XBOOLE_0:def 4;
          hence contradiction by A1,XBOOLE_0:def 7;
        end;
        suppose x in the InternalRel of G2;
          then
A27:      a in the carrier of G2 & b in the carrier of G2 by ZFMISC_1:106;
          a in the carrier of G1 by A22,ZFMISC_1:106;
          then a in (the carrier of G1) /\ the carrier of G2
          by A27,XBOOLE_0:def 4;
          hence contradiction by A1,XBOOLE_0:def 7;
        end;
      end;
      then x in [:the carrier of union_of(G1,G2),the carrier of union_of(G1
      ,G2):] \ (the InternalRel of union_of(G1,G2)) by A24,XBOOLE_0:def 5;
      then
A28:  x in (the InternalRel of union_of(G1,G2))` by SUBSET_1:def 5;
      not x in id the carrier of union_of(G1,G2)
      proof
        assume not thesis;
        then x in id ((the carrier of G1) \/ the carrier of G2) by
NECKLA_2:def 2;
        then
A29:    x in (id the carrier of G1) \/ id the carrier of G2 by SYSREL:32;
        per cases by A29,XBOOLE_0:def 3;
        suppose x in id the carrier of G1;
          then
A30:      a in the carrier of G1 & b in the carrier of G1 by ZFMISC_1:106;
          b in the carrier of G2 by A23,ZFMISC_1:106;
          then b in (the carrier of G1) /\ the carrier of G2
          by A30,XBOOLE_0:def 4;
          hence contradiction by A1,XBOOLE_0:def 7;
        end;
        suppose x in id the carrier of G2;
          then
A31:      a in the carrier of G2 & b in the carrier of G2 by ZFMISC_1:106;
          a in the carrier of G1 by A22,ZFMISC_1:106;
          then a in (the carrier of G1) /\ the carrier of G2
          by A31,XBOOLE_0:def 4;
          hence contradiction by A1,XBOOLE_0:def 7;
        end;
      end;
      then x in (the InternalRel of union_of(G1,G2))` \
      id the carrier of union_of(G1,G2) by A28,XBOOLE_0:def 5;
      hence thesis by NECKLACE:def 9;
    end;
    suppose x in X4;
      then
A32:  x in [:the carrier of G2,the carrier of ComplRelStr G1:]
      by NECKLACE:def 9;
      then
A33:  x in [:the carrier of G2,the carrier of G1:] by NECKLACE:def 9;
      then x in X7 \/ X8 by XBOOLE_0:def 3;
      then x in X5 \/ (X7 \/ X8) by XBOOLE_0:def 3;
      then x in X5 \/ X7 \/ X8 by XBOOLE_1:4;
      then x in X5 \/ X7 \/ X8 \/ X6 by XBOOLE_0:def 3;
      then x in [:(the carrier of G1) \/ the carrier of G2,
      (the carrier of G1) \/ the carrier of G2:] by ZFMISC_1:121;
      then x in [:(the carrier of G1) \/ the carrier of G2,
      the carrier of union_of(G1,G2):] by NECKLA_2:def 2;
      then
A34:  x in [:the carrier of union_of(G1,G2),
      the carrier of union_of(G1,G2):] by NECKLA_2:def 2;
      not x in the InternalRel of union_of(G1,G2)
      proof
        assume not thesis;
        then
A35:    x in (the InternalRel of G1) \/ the InternalRel of G2
        by NECKLA_2:def 2;
        per cases by A35,XBOOLE_0:def 3;
        suppose x in the InternalRel of G1;
          then
A36:      a in the carrier of G1 & b in the carrier of G1 by ZFMISC_1:106;
          a in the carrier of G2 by A32,ZFMISC_1:106;
          then a in (the carrier of G1) /\ the carrier of G2
          by A36,XBOOLE_0:def 4;
          hence contradiction by A1,XBOOLE_0:def 7;
        end;
        suppose x in the InternalRel of G2;
          then
A37:      a in the carrier of G2 & b in the carrier of G2 by ZFMISC_1:106;
          b in the carrier of G1 by A33,ZFMISC_1:106;
          then b in (the carrier of G1) /\ the carrier of G2
          by A37,XBOOLE_0:def 4;
          hence contradiction by A1,XBOOLE_0:def 7;
        end;
      end;
      then x in [:the carrier of union_of(G1,G2),the carrier of union_of(G1
      ,G2):] \ (the InternalRel of union_of(G1,G2)) by A34,XBOOLE_0:def 5;
      then
A38:  x in (the InternalRel of union_of(G1,G2))` by SUBSET_1:def 5;
      not x in id the carrier of union_of(G1,G2)
      proof
        assume not thesis;
        then x in id ((the carrier of G1) \/ the carrier of G2) by
NECKLA_2:def 2;
        then
A39:    x in (id the carrier of G1) \/ id the carrier of G2 by SYSREL:32;
        per cases by A39,XBOOLE_0:def 3;
        suppose x in id the carrier of G1;
          then
A40:      a in the carrier of G1 & b in the carrier of G1 by ZFMISC_1:106;
          a in the carrier of G2 by A32,ZFMISC_1:106;
          then a in (the carrier of G1) /\ the carrier of G2
          by A40,XBOOLE_0:def 4;
          hence contradiction by A1,XBOOLE_0:def 7;
        end;
        suppose x in id the carrier of G2;
          then
A41:      a in the carrier of G2 & b in the carrier of G2 by ZFMISC_1:106;
          b in the carrier of G1 by A33,ZFMISC_1:106;
          then b in (the carrier of G1) /\ the carrier of G2
          by A41,XBOOLE_0:def 4;
          hence contradiction by A1,XBOOLE_0:def 7;
        end;
      end;
      then x in (the InternalRel of union_of(G1,G2))` \
      id the carrier of union_of(G1,G2) by A38,XBOOLE_0:def 5;
      hence thesis by NECKLACE:def 9;
    end;
  end;
  hence thesis by A2,A3,RELAT_1:def 2;
end;

theorem Th18:
  for G1,G2 being RelStr st the carrier of G1 misses the carrier of G2
  holds ComplRelStr sum_of(G1,G2) = union_of(ComplRelStr G1, ComplRelStr G2)
proof
  let G1,G2 be RelStr;
  assume
A1: the carrier of G1 misses the carrier of G2;
A2: the carrier of ComplRelStr sum_of(G1,G2) = the carrier of sum_of(G1,G2)
  by NECKLACE:def 9
    .= (the carrier of G1) \/ the carrier of G2 by NECKLA_2:def 3;
A3: the carrier of union_of(ComplRelStr G1, ComplRelStr G2) =
  (the carrier of ComplRelStr G1) \/ (the carrier of ComplRelStr G2)
  by NECKLA_2:def 2
    .= (the carrier of G1) \/ the carrier of ComplRelStr G2 by NECKLACE:def 9
    .= (the carrier of G1) \/ the carrier of G2 by NECKLACE:def 9;
  set P = the InternalRel of ComplRelStr sum_of(G1,G2),
  R = the InternalRel of union_of(ComplRelStr G1,ComplRelStr G2),
  X1 = the InternalRel of ComplRelStr G1,
  X2 = the InternalRel of ComplRelStr G2,
  X5 = [:the carrier of G1, the carrier of G1:],
  X6 = [:the carrier of G2, the carrier of G2:],
  X7 = [:the carrier of G1, the carrier of G2:],
  X8 = [:the carrier of G2, the carrier of G1:];
A4: [:the carrier of sum_of(G1,G2),the carrier of sum_of(G1,G2):] =
  [:(the carrier of G1) \/ the carrier of G2,
  the carrier of sum_of(G1,G2):] by NECKLA_2:def 3
    .= [:(the carrier of G1) \/ the carrier of G2,
  (the carrier of G1) \/ the carrier of G2:] by NECKLA_2:def 3
    .= X5 \/ X7 \/ X8 \/ X6 by ZFMISC_1:121;
  for a,b being set holds [a,b] in P iff [a,b] in R
  proof
    let a,b be set;
    set x = [a,b];
    thus x in P implies x in R
    proof
      assume x in P;
      then
A5:   x in (the InternalRel of sum_of(G1,G2))` \
      id (the carrier of sum_of(G1,G2)) by NECKLACE:def 9;
      then
A6:   x in (the InternalRel of sum_of(G1,G2))` &
      not x in id (the carrier of sum_of(G1,G2)) by XBOOLE_0:def 5;
      then x in [:the carrier of sum_of(G1,G2),the carrier of sum_of(G1,G2)
      :] \ (the InternalRel of sum_of(G1,G2)) by SUBSET_1:def 5;
      then x in [:the carrier of sum_of(G1,G2),the carrier of sum_of(G1,G2) :]
      & not x in the InternalRel of sum_of(G1,G2) by XBOOLE_0:def 5;
      then
A7:   not x in (the InternalRel of G1) \/ (the InternalRel of G2) \/
      [:the carrier of G1,the carrier of G2:] \/
      [:the carrier of G2,the carrier of G1:] by NECKLA_2:def 3;
A8:   not x in the InternalRel of G1 & not x in the InternalRel of G2 &
      not x in [:the carrier of G1,the carrier of G2:] &
      not x in [:the carrier of G2,the carrier of G1:]
      proof
        assume not thesis;
        then x in (the InternalRel of G1) \/ the InternalRel of G2 or
        x in [:the carrier of G1,the carrier of G2:] or
        x in [:the carrier of G2,the carrier of G1:] by XBOOLE_0:def 3;
        then x in (the InternalRel of G1) \/ (the InternalRel of G2) \/
        [:the carrier of G1,the carrier of G2:] or
        x in [:the carrier of G2,the carrier of G1:] by XBOOLE_0:def 3;
        hence contradiction by A7,XBOOLE_0:def 3;
      end;
      not x in id ((the carrier of G1) \/ the carrier of G2)
      by A6,NECKLA_2:def 3;
      then not x in id (the carrier of G1) \/ id the carrier of G2
      by SYSREL:32;
      then
A9:   not x in id the carrier of G1 & not x in id the carrier of G2
      by XBOOLE_0:def 3;
      x in X5 \/ X7 \/ X8 or x in X6 by A4,A5,XBOOLE_0:def 3;
      then
A10:  x in X5 \/ X7 or x in X8 or x in X6 by XBOOLE_0:def 3;
      per cases by A10,XBOOLE_0:def 3;
      suppose x in X5;
        then x in X5 \ (the InternalRel of G1) by A8,XBOOLE_0:def 5;
        then x in (the InternalRel of G1)` by SUBSET_1:def 5;
        then x in (the InternalRel of G1)` \ id the carrier of G1 by A9,
XBOOLE_0:def 5;
        then x in X1 by NECKLACE:def 9;
        then x in X1 \/ X2 by XBOOLE_0:def 3;
        hence thesis by NECKLA_2:def 2;
      end;
      suppose x in X7;
        hence thesis by A8;
      end;
      suppose x in X8;
        hence thesis by A8;
      end;
      suppose x in X6;
        then x in X6 \ (the InternalRel of G2) by A8,XBOOLE_0:def 5;
        then x in (the InternalRel of G2)` by SUBSET_1:def 5;
        then x in (the InternalRel of G2)` \ id the carrier of G2 by A9,
XBOOLE_0:def 5;
        then x in X2 by NECKLACE:def 9;
        then x in X1 \/ X2 by XBOOLE_0:def 3;
        hence thesis by NECKLA_2:def 2;
      end;
    end;
    assume x in R;
    then
A11: x in (the InternalRel of ComplRelStr G1) \/
    the InternalRel of ComplRelStr G2 by NECKLA_2:def 2;
    per cases by A11,XBOOLE_0:def 3;
    suppose x in the InternalRel of ComplRelStr G1;
      then
A12:  x in (the InternalRel of G1)` \ id the carrier of G1 by NECKLACE:def 9;
      then
A13:  x in (the InternalRel of G1)` & not x in id the carrier of G1
      by XBOOLE_0:def 5;
      then x in [:the carrier of G1,the carrier of G1:] \ the InternalRel
      of G1 & not x in id the carrier of G1 by SUBSET_1:def 5;
      then
A14:  x in [:the carrier of G1,the carrier of G1:] &
      not x in the InternalRel of G1 & not x in id the carrier of G1
      by XBOOLE_0:def 5;
A15:  not x in id (the carrier of sum_of(G1,G2))
      proof
        assume not thesis;
        then x in id ((the carrier of G1) \/ the carrier of G2) by
NECKLA_2:def 3;
        then x in id (the carrier of G1) \/ id the carrier of G2 by SYSREL:32;
        then x in id the carrier of G2 by A13,XBOOLE_0:def 3;
        then
A16:    a in the carrier of G2 & b in the carrier of G2 by ZFMISC_1:106;
        a in the carrier of G1 by A12,ZFMISC_1:106;
        then (the carrier of G1) /\ the carrier of G2 is non empty
        by A16,XBOOLE_0:def 4;
        hence contradiction by A1,XBOOLE_0:def 7;
      end;
A17:  not x in the InternalRel of sum_of(G1,G2)
      proof
        assume not thesis;
        then x in (the InternalRel of G1) \/ (the InternalRel of G2) \/
        X7 \/ X8 by NECKLA_2:def 3;
        then x in (the InternalRel of G1) \/ (the InternalRel of G2) \/ X7
        or x in X8 by XBOOLE_0:def 3;
        then
A18:    x in (the InternalRel of G1) \/ the InternalRel of G2 or
        x in X7 or x in X8 by XBOOLE_0:def 3;
        per cases by A14,A18,XBOOLE_0:def 3;
        suppose x in the InternalRel of G2;
          then
A19:      a in the carrier of G2 & b in the carrier of G2 by ZFMISC_1:106;
          a in the carrier of G1 by A12,ZFMISC_1:106;
          then (the carrier of G1) /\ the carrier of G2 is non empty
          by A19,XBOOLE_0:def 4;
          hence contradiction by A1,XBOOLE_0:def 7;
        end;
        suppose x in X7;
          then
A20:      a in the carrier of G1 & b in the carrier of G2 by ZFMISC_1:106;
          b in the carrier of G1 by A12,ZFMISC_1:106;
          then (the carrier of G1) /\ the carrier of G2 is non empty
          by A20,XBOOLE_0:def 4;
          hence contradiction by A1,XBOOLE_0:def 7;
        end;
        suppose x in X8;
          then
A21:      a in the carrier of G2 & b in the carrier of G1 by ZFMISC_1:106;
          a in the carrier of G1 by A12,ZFMISC_1:106;
          then (the carrier of G1) /\ the carrier of G2 is non empty
          by A21,XBOOLE_0:def 4;
          hence contradiction by A1,XBOOLE_0:def 7;
        end;
      end;
      x in X5 \/ (X7 \/ X8 \/ X6) by A12,XBOOLE_0:def 3;
      then x in [:the carrier of sum_of(G1,G2),the carrier of sum_of(G1,G2) :]
      by A4,XBOOLE_1:113;
      then x in [:the carrier of sum_of(G1,G2),the carrier of sum_of(G1,G2)
      :] \ (the InternalRel of sum_of(G1,G2)) by A17,XBOOLE_0:def 5;
      then x in (the InternalRel of sum_of(G1,G2))` &
      not x in id (the carrier of sum_of(G1,G2)) by A15,SUBSET_1:def 5;
      then x in (the InternalRel of sum_of(G1,G2))` \
      id (the carrier of sum_of(G1,G2)) by XBOOLE_0:def 5;
      hence thesis by NECKLACE:def 9;
    end;
    suppose x in the InternalRel of ComplRelStr G2;
      then
A22:  x in (the InternalRel of G2)` \ id the carrier of G2 by NECKLACE:def 9;
      then
A23:  x in (the InternalRel of G2)` & not x in id the carrier of G2
      by XBOOLE_0:def 5;
      then x in [:the carrier of G2,the carrier of G2:] \ the InternalRel
      of G2 & not x in id the carrier of G2 by SUBSET_1:def 5;
      then
A24:  x in [:the carrier of G2,the carrier of G2:] &
      not x in the InternalRel of G2 & not x in id the carrier of G2
      by XBOOLE_0:def 5;
A25:  not x in id (the carrier of sum_of(G1,G2))
      proof
        assume not thesis;
        then x in id ((the carrier of G1) \/ the carrier of G2) by
NECKLA_2:def 3;
        then x in id (the carrier of G1) \/ id the carrier of G2 by SYSREL:32;
        then x in id the carrier of G1 by A23,XBOOLE_0:def 3;
        then
A26:    a in the carrier of G1 & b in the carrier of G1 by ZFMISC_1:106;
        b in the carrier of G2 by A22,ZFMISC_1:106;
        then (the carrier of G1) /\ the carrier of G2 is non empty
        by A26,XBOOLE_0:def 4;
        hence contradiction by A1,XBOOLE_0:def 7;
      end;
A27:  not x in the InternalRel of sum_of(G1,G2)
      proof
        assume not thesis;
        then x in (the InternalRel of G1) \/ (the InternalRel of G2) \/
        X7 \/ X8 by NECKLA_2:def 3;
        then x in (the InternalRel of G1) \/ (the InternalRel of G2) \/ X7
        or x in X8 by XBOOLE_0:def 3;
        then
A28:    x in (the InternalRel of G1) \/ the InternalRel of G2 or
        x in X7 or x in X8 by XBOOLE_0:def 3;
        per cases by A24,A28,XBOOLE_0:def 3;
        suppose x in the InternalRel of G1;
          then
A29:      a in the carrier of G1 & b in the carrier of G1 by ZFMISC_1:106;
          a in the carrier of G2 by A22,ZFMISC_1:106;
          then (the carrier of G1) /\ the carrier of G2 is non empty
          by A29,XBOOLE_0:def 4;
          hence contradiction by A1,XBOOLE_0:def 7;
        end;
        suppose x in X7;
          then
A30:      a in the carrier of G1 & b in the carrier of G2 by ZFMISC_1:106;
          a in the carrier of G2 by A22,ZFMISC_1:106;
          then (the carrier of G1) /\ the carrier of G2 is non empty
          by A30,XBOOLE_0:def 4;
          hence contradiction by A1,XBOOLE_0:def 7;
        end;
        suppose x in X8;
          then
A31:      a in the carrier of G2 & b in the carrier of G1 by ZFMISC_1:106;
          b in the carrier of G2 by A22,ZFMISC_1:106;
          then (the carrier of G1) /\ the carrier of G2 is non empty
          by A31,XBOOLE_0:def 4;
          hence contradiction by A1,XBOOLE_0:def 7;
        end;
      end;
      x in [:the carrier of sum_of(G1,G2),the carrier of sum_of(G1,G2):]
      by A4,A22,XBOOLE_0:def 3;
      then x in [:the carrier of sum_of(G1,G2),the carrier of sum_of(G1,G2
      ):] \ (the InternalRel of sum_of(G1,G2)) by A27,XBOOLE_0:def 5;
      then x in (the InternalRel of sum_of(G1,G2))` &
      not x in id (the carrier of sum_of(G1,G2)) by A25,SUBSET_1:def 5;
      then x in (the InternalRel of sum_of(G1,G2))` \
      id (the carrier of sum_of(G1,G2)) by XBOOLE_0:def 5;
      hence thesis by NECKLACE:def 9;
    end;
  end;
  hence thesis by A2,A3,RELAT_1:def 2;
end;

theorem
  for G being RelStr, H being full SubRelStr of G
  holds the InternalRel of ComplRelStr H =
  (the InternalRel of ComplRelStr G)|_2 the carrier of ComplRelStr H
proof
  let G be RelStr, H be full SubRelStr of G;
  set IH = the InternalRel of H, ICmpH = the InternalRel of ComplRelStr H,
  cH = the carrier of H, IG = the InternalRel of G, cG = the carrier of G,
  ICmpG = the InternalRel of ComplRelStr G;
A1: ICmpH = IH` \ id cH by NECKLACE:def 9
    .= [:cH,cH:] \ IH \ id cH by SUBSET_1:def 5;
A2: ICmpG = IG` \ id cG by NECKLACE:def 9
    .= [:cG,cG:] \ IG \ id cG by SUBSET_1:def 5;
A3: cH c= cG by YELLOW_0:def 13;
  ICmpG |_2 the carrier of ComplRelStr H = ICmpG |_2 cH by NECKLACE:def 9
    .= ([:cG,cG:] \ IG) /\ [:cH,cH:] \ id cG /\ [:cH,cH:] by A2,XBOOLE_1:50
    .= ([:cG,cG:] /\ [:cH,cH:]) \ (IG /\ [:cH,cH:]) \ (id cG /\ [:cH,cH:])
  by XBOOLE_1:50
    .= ([:cG,cG:] /\ [:cH,cH:]) \ (IG /\ [:cH,cH:]) \ (id cG)|cH by Th1
    .= ([:cG,cG:] /\ [:cH,cH:]) \ IG|_2 cH \ id cH by A3,FUNCT_3:1
    .= ([:cG,cG:] /\ [:cH,cH:]) \ IH \ id cH by YELLOW_0:def 14
    .= [:cG /\ cH, cG /\ cH:] \ IH \ id cH by ZFMISC_1:123
    .= [:cH, cG /\ cH:] \ IH \ id cH by A3,XBOOLE_1:28
    .= ICmpH by A1,A3,XBOOLE_1:28;
  hence thesis;
end;

theorem Th20:
  for G being non empty irreflexive RelStr, x being Element of G,
  x' being Element of ComplRelStr G st x = x' holds
  ComplRelStr (subrelstr ([#]G \ {x})) = subrelstr ([#](ComplRelStr G) \ {x'})
proof
  let G be non empty irreflexive RelStr, x be Element of G,
  x' be Element of ComplRelStr G;
  assume
A1: x = x';
  set R = subrelstr ([#]G \ {x}), cR = the carrier of R,
  cG = the carrier of G;
A2: the carrier of ComplRelStr (subrelstr ([#]G \ {x}))
  = the carrier of (subrelstr ([#]G \ {x})) by NECKLACE:def 9
    .= (the carrier of G) \ {x} by YELLOW_0:def 15
    .= [#](ComplRelStr G) \ {x'} by A1,NECKLACE:def 9
    .= the carrier of subrelstr ([#](ComplRelStr G) \ {x'}) by YELLOW_0:def 15;
A3: [#]ComplRelStr G = cG by NECKLACE:def 9;
A4: [:(cG \ {x}),(cG \ {x}):] = [:cR,([#]G \ {x}):] by YELLOW_0:def 15
    .= [:cR,cR:] by YELLOW_0:def 15;
A5: cR c= cG by YELLOW_0:def 13;
A6: [:cR,cR:] = [:[#]G,([#]G \ {x}):] \ [:{x},([#]G \ {x}):]
  by A4,ZFMISC_1:125
    .= [:[#]G,[#]G:] \ [:[#]G,{x}:] \ [:{x},([#]G\{x}):] by ZFMISC_1:125
    .= [:cG,cG:] \ [:cG,{x}:] \ ([:{x},cG:] \ [:{x},{x}:]) by ZFMISC_1:125
    .= ( ([:cG,cG:] \ [:cG,{x}:]) \ [:{x},cG:] ) \/
  ([:cG,cG:] \ [:cG,{x}:]) /\ [:{x},{x}:] by XBOOLE_1:52
    .= ([:cG,cG:] \ ([:cG,{x}:] \/ [:{x},cG:]) ) \/
  ([:cG,cG:] \ [:cG,{x}:]) /\ [:{x},{x}:] by XBOOLE_1:41;
A7: the InternalRel of ComplRelStr R =
  (the InternalRel of R)` \ id cR by NECKLACE:def 9
    .= [:cR,cR:] \ (the InternalRel of R) \ id cR by SUBSET_1:def 5
    .= [:cR,cR:] \ ( (the InternalRel of G)|_2 cR ) \id cR by YELLOW_0:def 14
    .= ( ([:cR,cR:] \ (the InternalRel of G)) \/ ([:cR,cR:] \ [:cR,cR:]) )
  \ id cR by XBOOLE_1:54
    .= ( ([:cR,cR:] \ (the InternalRel of G)) \/ {}) \ id cR by XBOOLE_1:37
    .= ([:cR,cR:] \ (the InternalRel of G)) \ id cR;
A8: the InternalRel of subrelstr ([#](ComplRelStr G) \ {x'})
  = (the InternalRel of ComplRelStr G)|_2
  the carrier of subrelstr ([#](ComplRelStr G) \ {x'}) by YELLOW_0:def 14

.= (the InternalRel of ComplRelStr G)|_2 (cG \ {x}) by A1,A3,YELLOW_0:def 15
    .= ((the InternalRel of G)` \ id cG )
  /\ [:(cG \ {x}),(cG \ {x}):] by NECKLACE:def 9
    .= ( [:cR,cR:] /\ (the InternalRel of G)` ) \ id cG by A4,XBOOLE_1:49
    .= ( [:cR,cR:] /\ ([:cG,cG:] \ the InternalRel of G) ) \ id cG
  by SUBSET_1:def 5

.= ( ([:cR,cR:] /\ [:cG,cG:]) \ the InternalRel of G) \ id cG by XBOOLE_1:49
    .= ([:cR,cR:] \ (the InternalRel of G)) \ id cG by A5,XBOOLE_1:28
,ZFMISC_1:119;
  the InternalRel of subrelstr ([#](ComplRelStr G) \ {x'})
  = the InternalRel of ComplRelStr R
  proof
    thus the InternalRel of subrelstr ([#](ComplRelStr G) \ {x'})
    c= the InternalRel of ComplRelStr R
    proof
      let a be set;
      assume a in the InternalRel of subrelstr ([#](ComplRelStr G) \ {x'});
      then
A9:   a in ([:cR,cR:] \ (the InternalRel of G)) & not a in id cG
      by A8,XBOOLE_0:def 5;
      not a in id cR
      proof
        assume
A10:    not thesis;
        then consider x2,y2 being set such that
A11:    a = [x2,y2] & x2 in cR & y2 in cR by RELSET_1:6;
A12:    x2 in cG \ {x} & y2 in cG \ {x} by A11,YELLOW_0:def 15;
        x2 = y2 by A10,A11,RELAT_1:def 10;
        hence contradiction by A9,A11,A12,RELAT_1:def 10;
      end;
      hence thesis by A7,A9,XBOOLE_0:def 5;
    end;
    let a be set;
    assume
A13: a in the InternalRel of ComplRelStr R;
    then a in ([:cR,cR:] \ (the InternalRel of G)) & not a in id cR
    by A7,XBOOLE_0:def 5;
    then not a in id (cG \ {x}) by YELLOW_0:def 15;
    then
A14: not a in (id cG \ id {x}) by SYSREL:32;
    per cases by A14,XBOOLE_0:def 5;
    suppose not a in id cG;

then a in ([:cR,cR:] \ (the InternalRel of G)) & not a in id cG by A7,A13,
XBOOLE_0:def 5;
      hence thesis by A8,XBOOLE_0:def 5;
    end;
    suppose a in id {x};
      then
A15:  a in {[x,x]} by SYSREL:30;
      thus thesis
      proof
        per cases by A6,A7,A13,XBOOLE_0:def 3;
        suppose a in [:cG,cG:] \ ([:cG,{x}:] \/ [:{x},cG:]);
          then a in [:cG,cG:] & not a in ([:cG,{x}:] \/ [:{x},cG:])
          by XBOOLE_0:def 5;
          then
A16:      a in [:cG,cG:] & not a in [:cG,{x}:] & not a in [:{x}, cG:]
          by XBOOLE_0:def 3;
          x in {x} & x in cG by TARSKI:def 1;
          then [x,x] in [:{x},cG:] by ZFMISC_1:106;
          hence thesis by A15,A16,TARSKI:def 1;
        end;
        suppose a in ([:cG,cG:] \ [:cG,{x}:]) /\ [:{x},{x}:];
          then a in [:cG,cG:] \ [:cG,{x}:] by XBOOLE_0:def 4;
          then
A17:      a in [:cG,cG:] & not a in [:cG,{x}:] by XBOOLE_0:def 5;
          x in {x} & x in cG by TARSKI:def 1;
          then [x,x] in [:cG,{x}:] by ZFMISC_1:106;
          hence thesis by A15,A17,TARSKI:def 1;
        end;
      end;
    end;
  end;
  hence thesis by A2;
end;

begin :: Another facts relating to operation 'embeds'

registration
  cluster trivial strict -> N-free (non empty RelStr);
  correctness
  proof
    let R be non empty RelStr;
    set Y = Necklace 4;
    assume R is trivial strict;
    then consider y be set such that
A1: the carrier of R = {y} by GROUP_6:def 2;
    assume not R is N-free;
    then R embeds Y by NECKLA_2:def 1;
    then consider f being Function of Y,R such that
A2: f is one-to-one & for x,y being Element of Y holds
    [x,y] in the InternalRel of Y iff [f.x,f.y] in the InternalRel of R
    by NECKLACE:def 2;
A3: dom f = the carrier of Y by FUNCT_2:def 1
      .= {0,1,2,3} by NECKLACE:2,21;
    then
A4: 0 in dom f by ENUMSET1:def 2;
A5: 1 in dom f by A3,ENUMSET1:def 2;
A6: f.0 in {y} by A1,A4,PARTFUN1:27;
    f.1 in {y} by A1,A5,PARTFUN1:27;
    then f.0 = y & f.1 = y by A6,TARSKI:def 1;
    hence contradiction by A2,A4,A5,FUNCT_1:def 8;
  end;
end;

theorem
  for R being reflexive antisymmetric RelStr, S being RelStr holds
  (ex f being Function of R,S st for x,y being Element of R holds
  [x,y] in the InternalRel of R iff [f.x,f.y] in the InternalRel of S)
  iff S embeds R
proof
  let R being reflexive antisymmetric RelStr, S being RelStr;
A1: now
    assume
A2: S embeds R;
    then consider f being Function of R,S such that f is one-to-one and
A3: for x,y being Element of R holds
    [x,y] in the InternalRel of R iff [f.x,f.y] in the InternalRel of S
    by NECKLACE:def 2;
    take f;
    thus thesis by A2,A3;
  end;
  now
    assume ex f being Function of R,S st for x,y being Element of R holds
    [x,y] in the InternalRel of R iff [f.x,f.y] in the InternalRel of S;
    then consider f being Function of R,S such that
A4: for x,y being Element of R holds
    [x,y] in the InternalRel of R iff [f.x,f.y] in the InternalRel of S;
    for x1,x2 being set st x1 in dom f & x2 in dom f & f.x1 = f.x2 holds
    x1 = x2
    proof
      let x1,x2 be set;
      assume
A5:   x1 in dom f & x2 in dom f & f.x1 = f.x2;
      then reconsider x1,x2 as Element of R;
A6:   the InternalRel of R is_reflexive_in the carrier of R by ORDERS_2:def 4;
      then [x1,x1] in the InternalRel of R by A5,RELAT_2:def 1;
      then [f.x1,f.x2] in the InternalRel of S by A4,A5;
      then [x1,x2] in the InternalRel of R by A4;
      then
A7:   x1 <= x2 by ORDERS_2:def 9;
      [x2,x2] in the InternalRel of R by A5,A6,RELAT_2:def 1;
      then [f.x2,f.x1] in the InternalRel of S by A4,A5;
      then [x2,x1] in the InternalRel of R by A4;
      then x2 <= x1 by ORDERS_2:def 9;
      hence thesis by A7,ORDERS_2:25;
    end;
    then f is one-to-one by FUNCT_1:def 8;
    hence S embeds R by A4,NECKLACE:def 2;
  end;
  hence thesis by A1;
end;

theorem Th22:
  for G being non empty RelStr, H being non empty full SubRelStr of G
  holds G embeds H
proof
  let G be non empty RelStr;
  let H be non empty full SubRelStr of G;
  reconsider f = id the carrier of H as Function of the carrier of H,
  the carrier of H;
A1: the carrier of H c= the carrier of G by YELLOW_0:def 13;
A2: dom f = the carrier of H by FUNCT_1:34;
  for x being set st x in the carrier of H holds f.x in the carrier of G
  proof
    let x be set;
    assume x in the carrier of H;
    then f.x in the carrier of H by FUNCT_1:34;
    hence thesis by A1;
  end;
  then reconsider f = id the carrier of H as Function of the carrier of H,
  the carrier of G by A2,FUNCT_2:5;
  reconsider f as Function of H,G;
  for x,y being Element of H holds
  [x,y] in the InternalRel of H iff [f.x,f.y] in the InternalRel of G
  proof
    let x,y be Element of H;
    set IH = the InternalRel of H, IG = the InternalRel of G,
    cH = the carrier of H;
A3: f.x = x & f.y =y by FUNCT_1:34;
    thus [x,y] in IH implies [f.x,f.y] in IG
    proof
      assume [x,y] in IH;
      then [x,y] in IG |_2 cH by YELLOW_0:def 14;
      hence thesis by A3,XBOOLE_0:def 4;
    end;
    assume [f.x,f.y] in IG;
    then [x,y] in IG |_2 cH by A3,XBOOLE_0:def 4;
    hence thesis by YELLOW_0:def 14;
  end;
  hence thesis by NECKLACE:def 2;
end;

theorem Th23:
  for G being non empty RelStr, H being non empty full SubRelStr of G
  st G is N-free holds H is N-free
proof
  let G be non empty RelStr, H be non empty full SubRelStr of G;
  assume
A1: G is N-free;
  assume not thesis;
  then
A2: H embeds Necklace 4 by NECKLA_2:def 1;
  G embeds H by Th22;
  then G embeds Necklace 4 by A2,NECKLACE:13;
  hence contradiction by A1,NECKLA_2:def 1;
end;

theorem Th24:
  for G being non empty irreflexive RelStr holds
  G embeds Necklace 4 iff ComplRelStr G embeds Necklace 4
proof
  let G be non empty irreflexive RelStr;
  set N4 = Necklace 4, CmpN4 = ComplRelStr Necklace 4, CmpG = ComplRelStr G;
A1: the carrier of Necklace 4 = {0,1,2,3} by NECKLACE:2,21;
A2: the carrier of CmpN4 = the carrier of N4 by NECKLACE:def 9;
A3: the carrier of CmpG = the carrier of G by NECKLACE:def 9;
  thus G embeds N4 implies CmpG embeds N4
  proof
    assume G embeds Necklace 4;
    then consider f being Function of N4,G such that
A4: f is one-to-one & for x,y being Element of N4 holds
    [x,y] in the InternalRel of N4 iff [f.x,f.y] in the InternalRel of G
    by NECKLACE:def 2;
    CmpN4,N4 are_isomorphic by NECKLACE:30,WAYBEL_1:7;
    then consider g being Function of CmpN4,N4 such that
A5: g is isomorphic by WAYBEL_1:def 8;
A6: g is one-to-one monotone & ex g1 being Function of N4,CmpN4 st g1 = g" &
    g1 is monotone by A5,WAYBEL_0:def 38;
    reconsider h = f*g as Function of CmpN4,G;
A7: dom h = the carrier of CmpN4 by FUNCT_2:def 1;
A8: h is one-to-one by A4,A6;
A9: for x,y being Element of CmpN4 holds
    [x,y] in the InternalRel of CmpN4 iff [h.x,h.y] in the InternalRel of G
    proof
      let x,y be Element of CmpN4;
      thus [x,y] in the InternalRel of CmpN4 implies
      [h.x,h.y] in the InternalRel of G
      proof
        assume [x,y] in the InternalRel of CmpN4;
        then x <= y by ORDERS_2:def 9;
        then g.x <= g.y by A6,WAYBEL_1:def 2;
        then [g.x,g.y] in the InternalRel of N4 by ORDERS_2:def 9;
        then [f.(g.x),f.(g.y)] in the InternalRel of G by A4;
        then [(f*g).x,f.(g.y)] in the InternalRel of G by FUNCT_2:21;
        hence thesis by FUNCT_2:21;
      end;
      assume [h.x,h.y] in the InternalRel of G;
      then [f.(g.x),h.y] in the InternalRel of G by FUNCT_2:21;
      then [f.(g.x),f.(g.y)] in the InternalRel of G by FUNCT_2:21;
      then [g.x,g.y] in the InternalRel of N4 by A4;
      then g.x <= g.y by ORDERS_2:def 9;
      then x <= y by A5,WAYBEL_0:66;
      hence thesis by ORDERS_2:def 9;
    end;
A10: 0 in the carrier of CmpN4 & 1 in the carrier of CmpN4 &
    2 in the carrier of CmpN4 & 3 in the carrier of CmpN4
    by A1,A2,ENUMSET1:def 2;
    [0,2] in the InternalRel of CmpN4 & [2,0] in the InternalRel of CmpN4 &
    [0,3] in the InternalRel of CmpN4 & [3,0] in the InternalRel of CmpN4 &
    [1,3] in the InternalRel of CmpN4 & [3,1] in the InternalRel of CmpN4
    by Th11,ENUMSET1:def 4;
    then
A11: [h.0,h.2] in the InternalRel of G & [h.2,h.0] in the InternalRel of G &
    [h.0,h.3] in the InternalRel of G & [h.3,h.0] in the InternalRel of G &
    [h.1,h.3] in the InternalRel of G &
    [h.3,h.1] in the InternalRel of G by A9,A10;
A12: [h.0,h.1] in the InternalRel of CmpG
    proof
      assume
A13:  not thesis;
      [h.0,h.1] in the InternalRel of G
      proof
        assume
A14:    not thesis;
        h.0 in the carrier of G & h.1 in the carrier of G by A10,FUNCT_2:7;

then [h.0,h.1] in [:the carrier of G,the carrier of G:] by ZFMISC_1:106;
        then [h.0,h.1] in id (the carrier of G) \/ (the InternalRel of G)
        \/ (the InternalRel of CmpG) by Th14;
        then [h.0,h.1] in id (the carrier of G) \/ (the InternalRel of G) or
        [h.0,h.1] in the InternalRel of CmpG by XBOOLE_0:def 3;
        then [h.0,h.1] in id (the carrier of G) by A13,A14,XBOOLE_0:def 3;
        then h.0 = h.1 by RELAT_1:def 10;
        hence contradiction by A7,A8,A10,FUNCT_1:def 8;
      end;
      then
A15:  [0,1] in the InternalRel of CmpN4 by A9,A10;
      [0,1] in the InternalRel of N4 by ENUMSET1:def 4,NECKLA_2:2;
      then [0,1] in (the InternalRel of N4) /\ (the InternalRel of CmpN4)
      by A15,XBOOLE_0:def 4;

then the InternalRel of N4 meets the InternalRel of CmpN4 by XBOOLE_0:def 7;
      hence thesis by Th12;
    end;
A16: [h.1,h.0] in the InternalRel of CmpG
    proof
      assume
A17:  not thesis;
      [h.1,h.0] in the InternalRel of G
      proof
        assume
A18:    not thesis;
        h.0 in the carrier of G & h.1 in the carrier of G by A10,FUNCT_2:7;

then [h.1,h.0] in [:the carrier of G,the carrier of G:] by ZFMISC_1:106;
        then [h.1,h.0] in id (the carrier of G) \/ (the InternalRel of G)
        \/ (the InternalRel of CmpG) by Th14;
        then [h.1,h.0] in id (the carrier of G) \/ (the InternalRel of G) or
        [h.1,h.0] in the InternalRel of CmpG by XBOOLE_0:def 3;
        then [h.1,h.0] in id (the carrier of G) by A17,A18,XBOOLE_0:def 3;
        then h.0 = h.1 by RELAT_1:def 10;
        hence contradiction by A7,A8,A10,FUNCT_1:def 8;
      end;
      then
A19:  [1,0] in the InternalRel of CmpN4 by A9,A10;
      [1,0] in the InternalRel of N4 by ENUMSET1:def 4,NECKLA_2:2;
      then [1,0] in (the InternalRel of N4) /\ (the InternalRel of CmpN4)
      by A19,XBOOLE_0:def 4;

then the InternalRel of N4 meets the InternalRel of CmpN4 by XBOOLE_0:def 7;
      hence thesis by Th12;
    end;
A20: [h.1,h.2] in the InternalRel of CmpG
    proof
      assume
A21:  not thesis;
      [h.1,h.2] in the InternalRel of G
      proof
        assume
A22:    not thesis;
        h.1 in the carrier of G & h.2 in the carrier of G by A10,FUNCT_2:7;

then [h.1,h.2] in [:the carrier of G,the carrier of G:] by ZFMISC_1:106;
        then [h.1,h.2] in id (the carrier of G) \/ (the InternalRel of G)
        \/ (the InternalRel of CmpG) by Th14;
        then [h.1,h.2] in id (the carrier of G) \/ (the InternalRel of G) or
        [h.1,h.2] in the InternalRel of CmpG by XBOOLE_0:def 3;
        then [h.1,h.2] in id (the carrier of G) by A21,A22,XBOOLE_0:def 3;
        then h.1 = h.2 by RELAT_1:def 10;
        hence contradiction by A7,A8,A10,FUNCT_1:def 8;
      end;
      then
A23:  [1,2] in the InternalRel of CmpN4 by A9,A10;
      [1,2] in the InternalRel of N4 by ENUMSET1:def 4,NECKLA_2:2;
      then [1,2] in (the InternalRel of N4) /\ (the InternalRel of CmpN4)
      by A23,XBOOLE_0:def 4;

then the InternalRel of N4 meets the InternalRel of CmpN4 by XBOOLE_0:def 7;
      hence thesis by Th12;
    end;
A24: [h.2,h.1] in the InternalRel of CmpG
    proof
      assume
A25:  not thesis;
      [h.2,h.1] in the InternalRel of G
      proof
        assume
A26:    not thesis;
        h.1 in the carrier of G & h.2 in the carrier of G by A10,FUNCT_2:7;

then [h.2,h.1] in [:the carrier of G,the carrier of G:] by ZFMISC_1:106;
        then [h.2,h.1] in id (the carrier of G) \/ (the InternalRel of G)
        \/ (the InternalRel of CmpG) by Th14;
        then [h.2,h.1] in id (the carrier of G) \/ (the InternalRel of G) or
        [h.2,h.1] in the InternalRel of CmpG by XBOOLE_0:def 3;
        then [h.2,h.1] in id (the carrier of G) by A25,A26,XBOOLE_0:def 3;
        then h.1 = h.2 by RELAT_1:def 10;
        hence contradiction by A7,A8,A10,FUNCT_1:def 8;
      end;
      then
A27:  [2,1] in the InternalRel of CmpN4 by A9,A10;
      [2,1] in the InternalRel of N4 by ENUMSET1:def 4,NECKLA_2:2;
      then [2,1] in (the InternalRel of N4) /\ (the InternalRel of CmpN4)
      by A27,XBOOLE_0:def 4;

then the InternalRel of N4 meets the InternalRel of CmpN4 by XBOOLE_0:def 7;
      hence thesis by Th12;
    end;
A28: [h.2,h.3] in the InternalRel of CmpG
    proof
      assume
A29:  not thesis;
      [h.2,h.3] in the InternalRel of G
      proof
        assume
A30:    not thesis;
        h.2 in the carrier of G & h.3 in the carrier of G by A10,FUNCT_2:7;

then [h.2,h.3] in [:the carrier of G,the carrier of G:] by ZFMISC_1:106;
        then [h.2,h.3] in id (the carrier of G) \/ (the InternalRel of G)
        \/ (the InternalRel of CmpG) by Th14;
        then [h.2,h.3] in id (the carrier of G) \/ (the InternalRel of G) or
        [h.2,h.3] in the InternalRel of CmpG by XBOOLE_0:def 3;
        then [h.2,h.3] in id (the carrier of G) by A29,A30,XBOOLE_0:def 3;
        then h.2 = h.3 by RELAT_1:def 10;
        hence contradiction by A7,A8,A10,FUNCT_1:def 8;
      end;
      then
A31:  [2,3] in the InternalRel of CmpN4 by A9,A10;
      [2,3] in the InternalRel of N4 by ENUMSET1:def 4,NECKLA_2:2;
      then [2,3] in (the InternalRel of N4) /\ (the InternalRel of CmpN4)
      by A31,XBOOLE_0:def 4;

then the InternalRel of N4 meets the InternalRel of CmpN4 by XBOOLE_0:def 7;
      hence thesis by Th12;
    end;
A32: [h.3,h.2] in the InternalRel of CmpG
    proof
      assume
A33:  not thesis;
      [h.3,h.2] in the InternalRel of G
      proof
        assume
A34:    not thesis;
        h.2 in the carrier of G & h.3 in the carrier of G by A10,FUNCT_2:7;

then [h.3,h.2] in [:the carrier of G,the carrier of G:] by ZFMISC_1:106;
        then [h.3,h.2] in id (the carrier of G) \/ (the InternalRel of G)
        \/ (the InternalRel of CmpG) by Th14;
        then [h.3,h.2] in id (the carrier of G) \/ (the InternalRel of G) or
        [h.3,h.2] in the InternalRel of CmpG by XBOOLE_0:def 3;
        then [h.3,h.2] in id (the carrier of G) by A33,A34,XBOOLE_0:def 3;
        then h.2 = h.3 by RELAT_1:def 10;
        hence contradiction by A7,A8,A10,FUNCT_1:def 8;
      end;
      then
A35:  [3,2] in the InternalRel of CmpN4 by A9,A10;
      [3,2] in the InternalRel of N4 by ENUMSET1:def 4,NECKLA_2:2;
      then [3,2] in (the InternalRel of N4) /\ (the InternalRel of CmpN4)
      by A35,XBOOLE_0:def 4;

then the InternalRel of N4 meets the InternalRel of CmpN4 by XBOOLE_0:def 7;
      hence thesis by Th12;
    end;
    for x,y being Element of N4 holds
    [x,y] in the InternalRel of N4 iff [h.x,h.y] in the InternalRel of CmpG
    proof
      let x,y be Element of N4;
      thus [x,y] in the InternalRel of N4 implies
      [h.x,h.y] in the InternalRel of CmpG
      proof
        assume
A36:    [x,y] in the InternalRel of N4;
        per cases by A36,ENUMSET1:def 4,NECKLA_2:2;
        suppose [x,y] = [0,1];
          then x = 0 & y = 1 by ZFMISC_1:33;
          hence thesis by A12;
        end;
        suppose [x,y] = [1,0];
          then x = 1 & y = 0 by ZFMISC_1:33;
          hence thesis by A16;
        end;
        suppose [x,y] = [1,2];
          then x = 1 & y = 2 by ZFMISC_1:33;
          hence thesis by A20;
        end;
        suppose [x,y] = [2,1];
          then x = 2 & y = 1 by ZFMISC_1:33;
          hence thesis by A24;
        end;
        suppose [x,y] = [2,3];
          then x = 2 & y = 3 by ZFMISC_1:33;
          hence thesis by A28;
        end;
        suppose [x,y] = [3,2];
          then x = 3 & y = 2 by ZFMISC_1:33;
          hence thesis by A32;
        end;
      end;
      assume
A37:  [h.x,h.y] in the InternalRel of CmpG;
      per cases by A1,ENUMSET1:def 2;
      suppose
A38:    x = 0 & y = 0;
        then h.0 in the carrier of CmpG by A37,ZFMISC_1:106;
        hence thesis by A37,A38,NECKLACE:def 6;
      end;
      suppose x = 0 & y = 1;
        hence thesis by ENUMSET1:def 4,NECKLA_2:2;
      end;
      suppose x = 0 & y = 2;
        then [h.0,h.2] in (the InternalRel of G) /\ the InternalRel of CmpG
        by A11,A37,XBOOLE_0:def 4;
        then the InternalRel of G meets the InternalRel of CmpG
        by XBOOLE_0:def 7;
        hence thesis by Th12;
      end;
      suppose x = 0 & y = 3;
        then [h.0,h.3] in (the InternalRel of G) /\ the InternalRel of CmpG
        by A11,A37,XBOOLE_0:def 4;
        then the InternalRel of G meets the InternalRel of CmpG
        by XBOOLE_0:def 7;
        hence thesis by Th12;
      end;
      suppose x = 1 & y = 0;
        hence thesis by ENUMSET1:def 4,NECKLA_2:2;
      end;
      suppose
A39:    x = 1 & y = 1;
        then h.1 in the carrier of CmpG by A37,ZFMISC_1:106;
        hence thesis by A37,A39,NECKLACE:def 6;
      end;
      suppose x = 1 & y = 2;
        hence thesis by ENUMSET1:def 4,NECKLA_2:2;
      end;
      suppose x = 1 & y = 3;
        then [h.1,h.3] in (the InternalRel of G) /\ the InternalRel of CmpG
        by A11,A37,XBOOLE_0:def 4;
        then the InternalRel of G meets the InternalRel of CmpG
        by XBOOLE_0:def 7;
        hence thesis by Th12;
      end;
      suppose x = 2 & y = 0;
        then [h.2,h.0] in (the InternalRel of G) /\ the InternalRel of CmpG
        by A11,A37,XBOOLE_0:def 4;
        then the InternalRel of G meets the InternalRel of CmpG
        by XBOOLE_0:def 7;
        hence thesis by Th12;
      end;
      suppose x = 2 & y = 1;
        hence thesis by ENUMSET1:def 4,NECKLA_2:2;
      end;
      suppose
A40:    x = 2 & y = 2;
        then h.2 in the carrier of CmpG by A37,ZFMISC_1:106;
        hence thesis by A37,A40,NECKLACE:def 6;
      end;
      suppose x = 2 & y = 3;
        hence thesis by ENUMSET1:def 4,NECKLA_2:2;
      end;
      suppose x = 3 & y = 0;
        then [h.3,h.0] in (the InternalRel of G) /\ the InternalRel of CmpG
        by A11,A37,XBOOLE_0:def 4;
        then the InternalRel of G meets the InternalRel of CmpG
        by XBOOLE_0:def 7;
        hence thesis by Th12;
      end;
      suppose x = 3 & y = 1;
        then [h.3,h.1] in (the InternalRel of G) /\ the InternalRel of CmpG
        by A11,A37,XBOOLE_0:def 4;
        then the InternalRel of G meets the InternalRel of CmpG
        by XBOOLE_0:def 7;
        hence thesis by Th12;
      end;
      suppose x = 3 & y = 2;
        hence thesis by ENUMSET1:def 4,NECKLA_2:2;
      end;
      suppose
A41:    x = 3 & y = 3;
        then h.3 in the carrier of CmpG by A37,ZFMISC_1:106;
        hence thesis by A37,A41,NECKLACE:def 6;
      end;
    end;
    hence thesis by A2,A3,A8,NECKLACE:def 2;
  end;
  assume CmpG embeds N4;
  then consider f being Function of N4, CmpG such that
A42: f is one-to-one & for x,y being Element of N4
  holds [x,y] in the InternalRel of N4 iff
  [f.x,f.y] in the InternalRel of CmpG by NECKLACE:def 2;
A43: dom f = the carrier of N4 by FUNCT_2:def 1;
A44: 0 in the carrier of N4 & 1 in the carrier of N4 &
  2 in the carrier of N4 & 3 in the carrier of N4 by A1,ENUMSET1:def 2;
  [0,1] in the InternalRel of N4 & [1,0] in the InternalRel of N4 &
  [1,2] in the InternalRel of N4 & [2,1] in the InternalRel of N4 &
  [2,3] in the InternalRel of N4 & [3,2] in the InternalRel of N4
  by ENUMSET1:def 4,NECKLA_2:2;
  then
A45: [f.0,f.1] in the InternalRel of CmpG &
  [f.1,f.0] in the InternalRel of CmpG &
  [f.1,f.2] in the InternalRel of CmpG &
  [f.2,f.1] in the InternalRel of CmpG &
  [f.2,f.3] in the InternalRel of CmpG &
  [f.3,f.2] in the InternalRel of CmpG by A42,A44;
A46: [f.0,f.2] in the InternalRel of G
  proof
    assume
A47: not thesis;
    [f.0,f.2] in the InternalRel of CmpG
    proof
      assume
A48:  not thesis;
      f.0 in the carrier of CmpG & f.2 in the carrier of CmpG by A44,FUNCT_2:7;
      then [f.0,f.2] in [:the carrier of G,the carrier of G:]
      by A3,ZFMISC_1:106;
      then [f.0,f.2] in id (the carrier of G) \/ (the InternalRel of G)
      \/ (the InternalRel of CmpG) by Th14;
      then [f.0,f.2] in id (the carrier of G) \/ (the InternalRel of G) or
      [f.0,f.2] in the InternalRel of CmpG by XBOOLE_0:def 3;
      then [f.0,f.2] in id (the carrier of G) by A47,A48,XBOOLE_0:def 3;
      then f.0 = f.2 by RELAT_1:def 10;
      hence contradiction by A42,A43,A44,FUNCT_1:def 8;
    end;
    then
A49: [0,2] in the InternalRel of N4 by A42,A44;
    [0,2] in the InternalRel of CmpN4 by Th11,ENUMSET1:def 4;
    then [0,2] in (the InternalRel of N4) /\ (the InternalRel of CmpN4)
    by A49,XBOOLE_0:def 4;

then the InternalRel of N4 meets the InternalRel of CmpN4 by XBOOLE_0:def 7;
    hence thesis by Th12;
  end;
A50: [f.2,f.0] in the InternalRel of G
  proof
    assume
A51: not [f.2,f.0] in the InternalRel of G;
    [f.2,f.0] in the InternalRel of CmpG
    proof
      assume
A52:  not thesis;
      f.0 in the carrier of CmpG & f.2 in the carrier of CmpG by A44,FUNCT_2:7;
      then [f.2,f.0] in [:the carrier of G,the carrier of G:]
      by A3,ZFMISC_1:106;
      then [f.2,f.0] in id (the carrier of G) \/ (the InternalRel of G)
      \/ (the InternalRel of CmpG) by Th14;
      then [f.2,f.0] in id (the carrier of G) \/ (the InternalRel of G) or
      [f.2,f.0] in the InternalRel of CmpG by XBOOLE_0:def 3;
      then [f.2,f.0] in id (the carrier of G) by A51,A52,XBOOLE_0:def 3;
      then f.0 = f.2 by RELAT_1:def 10;
      hence contradiction by A42,A43,A44,FUNCT_1:def 8;
    end;
    then
A53: [2,0] in the InternalRel of N4 by A42,A44;
    [2,0] in the InternalRel of CmpN4 by Th11,ENUMSET1:def 4;
    then [2,0] in (the InternalRel of N4) /\ (the InternalRel of CmpN4)
    by A53,XBOOLE_0:def 4;

then the InternalRel of N4 meets the InternalRel of CmpN4 by XBOOLE_0:def 7;
    hence thesis by Th12;
  end;
A54: [f.0,f.3] in the InternalRel of G
  proof
    assume
A55: not [f.0,f.3] in the InternalRel of G;
    [f.0,f.3] in the InternalRel of CmpG
    proof
      assume
A56:  not thesis;
      f.0 in the carrier of CmpG & f.3 in the carrier of CmpG by A44,FUNCT_2:7;
      then [f.0,f.3] in [:the carrier of G,the carrier of G:]
      by A3,ZFMISC_1:106;
      then [f.0,f.3] in id (the carrier of G) \/ (the InternalRel of G)
      \/ (the InternalRel of CmpG) by Th14;
      then [f.0,f.3] in id (the carrier of G) \/ (the InternalRel of G) or
      [f.0,f.3] in the InternalRel of CmpG by XBOOLE_0:def 3;
      then [f.0,f.3] in id (the carrier of G) by A55,A56,XBOOLE_0:def 3;
      then f.0 = f.3 by RELAT_1:def 10;
      hence contradiction by A42,A43,A44,FUNCT_1:def 8;
    end;
    then
A57: [0,3] in the InternalRel of N4 by A42,A44;
    [0,3] in the InternalRel of CmpN4 by Th11,ENUMSET1:def 4;
    then [0,3] in (the InternalRel of N4) /\ (the InternalRel of CmpN4)
    by A57,XBOOLE_0:def 4;

then the InternalRel of N4 meets the InternalRel of CmpN4 by XBOOLE_0:def 7;
    hence thesis by Th12;
  end;
A58: [f.3,f.0] in the InternalRel of G
  proof
    assume
A59: not [f.3,f.0] in the InternalRel of G;
    [f.3,f.0] in the InternalRel of CmpG
    proof
      assume
A60:  not thesis;
      f.0 in the carrier of CmpG & f.3 in the carrier of CmpG by A44,FUNCT_2:7;
      then [f.3,f.0] in [:the carrier of G,the carrier of G:]
      by A3,ZFMISC_1:106;
      then [f.3,f.0] in id (the carrier of G) \/ (the InternalRel of G)
      \/ (the InternalRel of CmpG) by Th14;
      then [f.3,f.0] in id (the carrier of G) \/ (the InternalRel of G) or
      [f.3,f.0] in the InternalRel of CmpG by XBOOLE_0:def 3;
      then [f.3,f.0] in id (the carrier of G) by A59,A60,XBOOLE_0:def 3;
      then f.0 = f.3 by RELAT_1:def 10;
      hence contradiction by A42,A43,A44,FUNCT_1:def 8;
    end;
    then
A61: [3,0] in the InternalRel of N4 by A42,A44;
    [3,0] in the InternalRel of CmpN4 by Th11,ENUMSET1:def 4;
    then [3,0] in (the InternalRel of N4) /\ (the InternalRel of CmpN4)
    by A61,XBOOLE_0:def 4;

then the InternalRel of N4 meets the InternalRel of CmpN4 by XBOOLE_0:def 7;
    hence thesis by Th12;
  end;
A62: [f.1,f.3] in the InternalRel of G
  proof
    assume
A63: not [f.1,f.3] in the InternalRel of G;
    [f.1,f.3] in the InternalRel of CmpG
    proof
      assume
A64:  not thesis;
      f.1 in the carrier of CmpG & f.3 in the carrier of CmpG by A44,FUNCT_2:7;
      then [f.1,f.3] in [:the carrier of G,the carrier of G:]
      by A3,ZFMISC_1:106;
      then [f.1,f.3] in id (the carrier of G) \/ (the InternalRel of G)
      \/ (the InternalRel of CmpG) by Th14;
      then [f.1,f.3] in id (the carrier of G) \/ (the InternalRel of G) or
      [f.1,f.3] in the InternalRel of CmpG by XBOOLE_0:def 3;
      then [f.1,f.3] in id (the carrier of G) by A63,A64,XBOOLE_0:def 3;
      then f.1 = f.3 by RELAT_1:def 10;
      hence contradiction by A42,A43,A44,FUNCT_1:def 8;
    end;
    then
A65: [1,3] in the InternalRel of N4 by A42,A44;
    [1,3] in the InternalRel of CmpN4 by Th11,ENUMSET1:def 4;
    then [1,3] in (the InternalRel of N4) /\ (the InternalRel of CmpN4)
    by A65,XBOOLE_0:def 4;

then the InternalRel of N4 meets the InternalRel of CmpN4 by XBOOLE_0:def 7;
    hence thesis by Th12;
  end;
A66: [f.3,f.1] in the InternalRel of G
  proof
    assume
A67: not [f.3,f.1] in the InternalRel of G;
    [f.3,f.1] in the InternalRel of CmpG
    proof
      assume
A68:  not thesis;
      f.1 in the carrier of CmpG & f.3 in the carrier of CmpG by A44,FUNCT_2:7;
      then [f.3,f.1] in [:the carrier of G,the carrier of G:]
      by A3,ZFMISC_1:106;
      then [f.3,f.1] in id (the carrier of G) \/ (the InternalRel of G)
      \/ (the InternalRel of CmpG) by Th14;
      then [f.3,f.1] in id (the carrier of G) \/ (the InternalRel of G) or
      [f.3,f.1] in the InternalRel of CmpG by XBOOLE_0:def 3;
      then [f.3,f.1] in id (the carrier of G) by A67,A68,XBOOLE_0:def 3;
      then f.1 = f.3 by RELAT_1:def 10;
      hence contradiction by A42,A43,A44,FUNCT_1:def 8;
    end;
    then
A69: [3,1] in the InternalRel of N4 by A42,A44;
    [3,1] in the InternalRel of CmpN4 by Th11,ENUMSET1:def 4;
    then [3,1] in (the InternalRel of N4) /\ (the InternalRel of CmpN4)
    by A69,XBOOLE_0:def 4;

then the InternalRel of N4 meets the InternalRel of CmpN4 by XBOOLE_0:def 7;
    hence thesis by Th12;
  end;
A70: for x,y being Element of CmpN4 holds
  [x,y] in the InternalRel of CmpN4 iff [f.x,f.y] in the InternalRel of G
  proof
    let x,y be Element of CmpN4;
    thus [x,y] in the InternalRel of CmpN4 implies
    [f.x,f.y] in the InternalRel of G
    proof
      assume
A71:  [x,y] in the InternalRel of CmpN4;
      per cases by A71,Th11,ENUMSET1:def 4;
      suppose [x,y] = [0,2];
        then x = 0 & y = 2 by ZFMISC_1:33;
        hence thesis by A46;
      end;
      suppose [x,y] = [2,0];
        then x = 2 & y = 0 by ZFMISC_1:33;
        hence thesis by A50;
      end;
      suppose [x,y] = [0,3];
        then x = 0 & y = 3 by ZFMISC_1:33;
        hence thesis by A54;
      end;
      suppose [x,y] = [3,0];
        then x = 3 & y = 0 by ZFMISC_1:33;
        hence thesis by A58;
      end;
      suppose [x,y] = [1,3];
        then x = 1 & y = 3 by ZFMISC_1:33;
        hence thesis by A62;
      end;
      suppose [x,y] = [3,1];
        then x = 3 & y = 1 by ZFMISC_1:33;
        hence thesis by A66;
      end;
    end;
    assume
A72: [f.x,f.y] in the InternalRel of G;
A73: the carrier of N4 = the carrier of CmpN4 by NECKLACE:def 9;
    per cases by A1,A73,ENUMSET1:def 2;
    suppose
A74:  x = 0 & y = 0;
      then f.0 in the carrier of G by A72,ZFMISC_1:106;
      hence thesis by A72,A74,NECKLACE:def 6;
    end;
    suppose x = 0 & y = 1;
      then [f.0,f.1] in (the InternalRel of G) /\ the InternalRel of CmpG
      by A45,A72,XBOOLE_0:def 4;
      then the InternalRel of G meets the InternalRel of CmpG
      by XBOOLE_0:def 7;
      hence thesis by Th12;
    end;
    suppose x = 0 & y = 2;
      hence thesis by Th11,ENUMSET1:def 4;
    end;
    suppose x = 0 & y = 3;
      hence thesis by Th11,ENUMSET1:def 4;
    end;
    suppose x = 1 & y = 0;
      then [f.1,f.0] in (the InternalRel of G) /\ the InternalRel of CmpG
      by A45,A72,XBOOLE_0:def 4;
      then the InternalRel of G meets the InternalRel of CmpG
      by XBOOLE_0:def 7;
      hence thesis by Th12;
    end;
    suppose x = 2 & y = 0;
      hence thesis by Th11,ENUMSET1:def 4;
    end;
    suppose x = 3 & y = 0;
      hence thesis by Th11,ENUMSET1:def 4;
    end;
    suppose
A75:  x = 1 & y = 1;
      then f.1 in the carrier of G by A72,ZFMISC_1:106;
      hence thesis by A72,A75,NECKLACE:def 6;
    end;
    suppose x = 1 & y = 2;
      then [f.1,f.2] in (the InternalRel of G) /\ the InternalRel of CmpG
      by A45,A72,XBOOLE_0:def 4;
      then the InternalRel of G meets the InternalRel of CmpG
      by XBOOLE_0:def 7;
      hence thesis by Th12;
    end;
    suppose x = 1 & y = 3;
      hence thesis by Th11,ENUMSET1:def 4;
    end;
    suppose x = 2 & y = 1;
      then [f.2,f.1] in (the InternalRel of G) /\ the InternalRel of CmpG
      by A45,A72,XBOOLE_0:def 4;
      then the InternalRel of G meets the InternalRel of CmpG
      by XBOOLE_0:def 7;
      hence thesis by Th12;
    end;
    suppose
A76:  x = 2 & y = 2;
      then f.2 in the carrier of G by A72,ZFMISC_1:106;
      hence thesis by A72,A76,NECKLACE:def 6;
    end;
    suppose x = 2 & y = 3;
      then [f.2,f.3] in (the InternalRel of G) /\ the InternalRel of CmpG
      by A45,A72,XBOOLE_0:def 4;
      then the InternalRel of G meets the InternalRel of CmpG
      by XBOOLE_0:def 7;
      hence thesis by Th12;
    end;
    suppose x = 3 & y = 1;
      hence thesis by Th11,ENUMSET1:def 4;
    end;
    suppose x = 3 & y = 2;
      then [f.3,f.2] in (the InternalRel of G) /\ the InternalRel of CmpG
      by A45,A72,XBOOLE_0:def 4;
      then the InternalRel of G meets the InternalRel of CmpG
      by XBOOLE_0:def 7;
      hence thesis by Th12;
    end;
    suppose
A77:  x = 3 & y = 3;
      then f.3 in the carrier of G by A72,ZFMISC_1:106;
      hence thesis by A72,A77,NECKLACE:def 6;
    end;
  end;
  reconsider f as Function of CmpN4,G by A2,A3;
  consider g being Function of N4,CmpN4 such that
A78: g is isomorphic by NECKLACE:30,WAYBEL_1:def 8;
A79: g is one-to-one monotone & ex g1 being Function of CmpN4,N4 st g1 = g" &
  g1 is monotone by A78,WAYBEL_0:def 38;
  reconsider h = f*g as Function of N4,G;
A80: h is one-to-one by A42,A79;
  for x,y being Element of N4 holds
  [x,y] in the InternalRel of N4 iff [h.x,h.y] in the InternalRel of G
  proof
    let x,y be Element of N4;
    thus [x,y] in the InternalRel of N4 implies
    [h.x,h.y] in the InternalRel of G
    proof
      assume [x,y] in the InternalRel of N4;
      then x <= y by ORDERS_2:def 9;
      then g.x <= g.y by A79,WAYBEL_1:def 2;
      then [g.x,g.y] in the InternalRel of CmpN4 by ORDERS_2:def 9;
      then [f.(g.x),f.(g.y)] in the InternalRel of G by A70;
      then [(f*g).x,f.(g.y)] in the InternalRel of G by FUNCT_2:21;
      hence thesis by FUNCT_2:21;
    end;
    assume [h.x,h.y] in the InternalRel of G;
    then [f.(g.x),h.y] in the InternalRel of G by FUNCT_2:21;
    then [f.(g.x),f.(g.y)] in the InternalRel of G by FUNCT_2:21;
    then [g.x,g.y] in the InternalRel of CmpN4 by A70;
    then g.x <= g.y by ORDERS_2:def 9;
    then x <= y by A78,WAYBEL_0:66;
    hence thesis by ORDERS_2:def 9;
  end;
  hence thesis by A80,NECKLACE:def 2;
end;

theorem Th25:
  for G being non empty irreflexive RelStr holds
  G is N-free iff ComplRelStr G is N-free
proof
  let G be non empty irreflexive RelStr;
  thus G is N-free implies ComplRelStr G is N-free
  proof
    assume
A1: G is N-free;
    assume not thesis;
    then ComplRelStr G embeds Necklace 4 by NECKLA_2:def 1;
    then G embeds Necklace 4 by Th24;
    hence contradiction by A1,NECKLA_2:def 1;
  end;
  assume
A2: ComplRelStr G is N-free;
  assume not thesis;
  then G embeds Necklace 4 by NECKLA_2:def 1;
  then ComplRelStr G embeds Necklace 4 by Th24;
  hence contradiction by A2,NECKLA_2:def 1;
end;

begin :: Connected Graphs

definition
  let R be RelStr;
  mode path of R is RedSequence of the InternalRel of R;
end;

definition
  let R be RelStr;
  attr R is path-connected means
  :Def1:
  for x,y being set st x in the carrier of R &
  y in the carrier of R & x <> y holds
  the InternalRel of R reduces x,y or the InternalRel of R reduces y,x;
end;

registration
  cluster empty -> path-connected RelStr;
  correctness
  proof
    let R be RelStr;
    assume
A1: R is empty;
    let x,y be set;
    assume x in the carrier of R & y in the carrier of R & x <> y;
    hence thesis by A1;
  end;
end;

registration
  cluster connected -> path-connected (non empty RelStr);
  correctness
  proof
    let R be non empty RelStr;
    set cR = the carrier of R, IR = the InternalRel of R;
    assume
A1: R is connected;
    for x,y being set st x in the carrier of R & y in the carrier of R & x
    <> y holds (the InternalRel of R) reduces x,y
    or (the InternalRel of R) reduces y,x
    proof
      let x,y being set such that
A2:   x in cR & y in cR & x <> y;
      reconsider x,y as Element of R by A2;
A3:   x <= y or y <= x by A1,WAYBEL_0:def 29;
      per cases by A3,ORDERS_2:def 9;
      suppose [x,y] in IR;
        then
A4:     <*x,y*> is RedSequence of IR by REWRITE1:8;
        len <*x,y*> = 2 & <*x,y*>.1 = x & <*x,y*>.2 = y by FINSEQ_1:61;
        hence thesis by A4,REWRITE1:def 3;
      end;
      suppose [y,x] in IR;
        then
A5:     <*y,x*> is RedSequence of IR by REWRITE1:8;
        len <*y,x*> = 2 & <*y,x*>.1 = y & <*y,x*>.2 = x by FINSEQ_1:61;
        hence thesis by A5,REWRITE1:def 3;
      end;
    end;
    hence thesis by Def1;
  end;
end;

theorem Th26:
  for R being non empty transitive reflexive RelStr, x,y being Element of R
  holds the InternalRel of R reduces x,y implies [x,y] in the InternalRel of R
proof
  let R be non empty transitive reflexive RelStr;
  let x,y be Element of R;
  set cR = the carrier of R, IR = the InternalRel of R;
  assume
A1: IR reduces x,y;
A2: IR is_reflexive_in cR by ORDERS_2:def 4;
A3: IR is_transitive_in cR by ORDERS_2:def 5;
  consider p being RedSequence of IR such that
A4: p.1 = x & p.len p = y by A1,REWRITE1:def 3;
  reconsider p as FinSequence;
A5: len p > 0 &
  for i being Nat st i in dom p & i+1 in dom p holds [p.i, p.(i+1)] in IR
  by REWRITE1:def 2;
  then 0 + 1 <= len p by NAT_1:13;
  then
A6: len p in dom p by FINSEQ_3:27;
  defpred P[Nat] means $1 in dom p implies [p.1,p.$1] in IR;
A7: P[1] by A2,A4,RELAT_2:def 1;
A8: for k be non empty Nat st P[k] holds P[k+1]
  proof
    let k be non empty Nat such that
A9: P[k];
    assume
A10: k+1 in dom p;
A11: k <= k+1 by NAT_1:11;
A12: 0 + 1 <= k by NAT_1:13;
    k+1 <= len p by A10,FINSEQ_3:27;
    then
A13: 1 <= k & k <= len p by A11,A12,XXREAL_0:2;
    then k in dom p by FINSEQ_3:27;
    then [p.k,p.(k+1)] in IR by A10,REWRITE1:def 2;
    then p.1 in cR & p.k in cR & p.(k+1) in cR & [p.1,p.k] in IR &
    [p.k,p.(k+1)] in IR by A9,A13,FINSEQ_3:27,ZFMISC_1:106;
    hence thesis by A3,RELAT_2:def 8;
  end;
  for k be non empty Nat holds P[k] from NAT_1:sch 10(A7,A8);
  hence thesis by A4,A5,A6;
end;

registration
  cluster path-connected -> connected (non empty transitive reflexive RelStr);
  correctness
  proof
    let R be non empty transitive reflexive RelStr;
    assume
A1: R is path-connected;
    set IR = the InternalRel of R;
    for x,y being Element of R holds x <= y or y <= x
    proof
      let x,y being Element of R;
      per cases;
      suppose x = y;
        hence thesis;
      end;
      suppose x <> y;
        then IR reduces x,y or IR reduces y,x by A1,Def1;
        then [x,y] in IR or [y,x] in IR by Th26;
        hence thesis by ORDERS_2:def 9;
      end;
    end;
    hence thesis by WAYBEL_0:def 29;
  end;
end;

theorem Th27:
  for R be symmetric RelStr,x,y being set
  st x in the carrier of R & y in the carrier of R holds
  the InternalRel of R reduces x,y implies the InternalRel of R reduces y,x
proof
  let R be symmetric RelStr;
  set cR = the carrier of R, IR = the InternalRel of R;
  let x,y be set such that x in cR & y in cR;
  assume IR reduces x,y;
  then consider p being RedSequence of IR such that
A1: p.1 = x & p.len p = y by REWRITE1:def 3;
  reconsider p as FinSequence;
A2: IR = IR~ by RELAT_2:30;
A3: (Rev p).len p = x & (Rev p).1 = y by A1,FINSEQ_5:65;
  IR reduces y,x
  proof
    reconsider q = Rev p as RedSequence of IR by A2,REWRITE1:10;
    q.1 = y & q.len q = x by A3,FINSEQ_5:def 3;
    hence thesis by REWRITE1:def 3;
  end;
  hence thesis;
end;

definition
  let R be symmetric RelStr;
  redefine attr R is path-connected means
  :Def2:
  for x,y being set st x in the carrier of R &
  y in the carrier of R & x <> y holds (the InternalRel of R) reduces x,y;
  compatibility
  proof
    set IR = the InternalRel of R, cR = the carrier of R;
    thus R is path-connected implies
    for x,y being set st x in the carrier of R & y in the carrier of R & x <> y
    holds (the InternalRel of R) reduces x,y
    proof
      assume
A1:   R is path-connected;
      let x,y being set such that
A2:   x in cR & y in cR & x <> y;
      per cases by A1,A2,Def1;
      suppose IR reduces x,y;
        hence thesis;
      end;
      suppose IR reduces y,x;
        hence thesis by A2,Th27;
      end;
    end;
    assume for x,y be set st x in the carrier of R
    & y in the carrier of R & x <> y holds IR reduces x,y;
    then for x,y being set st x in the carrier of R
    & y in the carrier of R & x <> y holds (the InternalRel of R) reduces x,y
    or (the InternalRel of R) reduces y,x;
    hence thesis by Def1;
  end;
end;

definition
  let R be RelStr;
  let x be Element of R;
  func component x -> Subset of R equals

  Class(EqCl the InternalRel of R, x);
  coherence;
end;

registration
  let R be non empty RelStr;
  let x be Element of R;
  cluster component x -> non empty;
  correctness by EQREL_1:28;
end;

canceled;

theorem Th29:
  for R being RelStr, x being Element of R,
  y be set st y in component x holds [x,y] in EqCl the InternalRel of R
proof
  let R be RelStr;
  let x be Element of R;
  let y be set;
  set IR = the InternalRel of R;
  assume y in component x;
  then [y,x] in EqCl IR by EQREL_1:27;
  hence thesis by EQREL_1:12;
end;

theorem Th30:
  for R being RelStr, x being Element of R, A being set
  holds A = component x iff for y being set
  holds y in A iff [x,y] in EqCl the InternalRel of R
proof
  let R be RelStr;
  let x being Element of R;
  let A be set;
  set IR = the InternalRel of R;
A1: A = component x implies
  for y being set holds [x,y] in EqCl IR implies y in A
  proof
    assume
A2: A = component x;
    let y be set;
    assume [x,y] in EqCl IR;
    then [y,x] in EqCl IR by EQREL_1:12;
    hence thesis by A2,EQREL_1:27;
  end;
  (for y being set holds y in A iff [x,y] in EqCl (the InternalRel of R))
  implies A = component x
  proof
    assume
A3: for y being set holds y in A iff [x,y] in EqCl (the InternalRel of R);
A4: A c= component x
    proof
      let a be set;
      assume a in A;
      then [x,a] in EqCl IR by A3;
      then [a,x] in EqCl IR by EQREL_1:12;
      hence thesis by EQREL_1:27;
    end;
    component x c= A
    proof
      let a be set;
      assume a in component x;
      then [a,x] in EqCl IR by EQREL_1:27;
      then [x,a] in EqCl IR by EQREL_1:12;
      hence thesis by A3;
    end;
    hence thesis by A4,XBOOLE_0:def 10;
  end;
  hence thesis by A1,Th29;
end;

theorem Th31:
  for R be non empty irreflexive symmetric RelStr holds
  R is not path-connected implies
  ex G1,G2 being non empty strict irreflexive symmetric RelStr
  st the carrier of G1 misses the carrier of G2 &
  the RelStr of R = union_of(G1,G2)
proof
  let R be non empty irreflexive symmetric RelStr;
  set cR = the carrier of R, IR = the InternalRel of R;
  assume R is not path-connected;
  then consider x,y being set such that
A1: x in cR & y in cR & x <> y & not IR reduces x,y by Def2;
  reconsider x,y as Element of R by A1;
  set A1 = component x, A2 = (the carrier of R) \ A1;
  reconsider A2 as Subset of R;
  set G1 = subrelstr A1, G2 = subrelstr A2;
A2: the carrier of G1 = A1 & the carrier of G2 = A2 by YELLOW_0:def 15;
  then
A3: (the carrier of G1) misses (the carrier of G2) by XBOOLE_1:79;
  IR = IR~ by RELAT_2:30;
  then not IR \/ IR~ reduces x,y by A1;
  then not x,y are_convertible_wrt IR by REWRITE1:def 4;
  then not [x,y] in EqCl IR by MSUALG_6:41;
  then y in cR & not y in A1 by Th30;
  then the carrier of G2 is non empty by A2,XBOOLE_0:def 5;
  then
A4: G1 is non empty strict RelStr &
  G2 is non empty strict RelStr by A2;
A5: cR = A1 \/ A2
  proof
    thus cR c= A1 \/ A2
    proof
      let a be set;
      assume
A6:   a in cR;
      assume not thesis;
      then not a in A1 & not a in A2 by XBOOLE_0:def 3;
      hence contradiction by A6,XBOOLE_0:def 5;
    end;
    thus thesis;
  end;
  then
A7: cR = the carrier of union_of(G1,G2) by A2,NECKLA_2:def 2;
A8: the InternalRel of G1 misses the InternalRel of G2
  proof
    set IG1 = the InternalRel of G1, IG2 = the InternalRel of G2;
    assume not thesis;
    then IG1 /\ IG2 <> {} by XBOOLE_0:def 7;
    then consider a be set such that
A9: a in IG1 /\ IG2 by XBOOLE_0:def 1;
A10: a in IG1 & a in IG2 by A9,XBOOLE_0:def 4;
    then consider c1,c2 be set such that
A11: a = [c1,c2] & c1 in A1 & c2 in A1 by A2,RELSET_1:6;
    consider g1,g2 be set such that
A12: a = [g1,g2] & g1 in A2 & g2 in A2 by A2,A10,RELSET_1:6;
    c1 in A1 & c1 in A2 by A11,A12,ZFMISC_1:33;
    then c1 in A1 /\ A2 by XBOOLE_0:def 4;
    hence contradiction by A2,A3,XBOOLE_0:def 7;
  end;
A13: the InternalRel of G2 = IR \ (the InternalRel of G1)
  proof
    set IG1 = the InternalRel of G1, IG2 = the InternalRel of G2;
    thus IG2 c= IR \ IG1
    proof
      let a be set;
      assume
A14:  a in IG2;
      then consider g1,g2 be set such that
A15:  a = [g1,g2] & g1 in A2 & g2 in A2 by A2,RELSET_1:6;
      reconsider g1,g2 as Element of G2 by A15,YELLOW_0:def 15;
A16:  g1 <= g2 by A14,A15,ORDERS_2:def 9;
      reconsider u1 = g1, u2 = g2 as Element of R by A15;
      u1 <= u2 by A16,YELLOW_0:60;
      then
A17:  a in IR by A15,ORDERS_2:def 9;
      not a in IG1
      proof
        assume not thesis;
        then a in IG1 /\ IG2 by A14,XBOOLE_0:def 4;
        hence contradiction by A8,XBOOLE_0:def 7;
      end;
      hence thesis by A17,XBOOLE_0:def 5;
    end;
    let a be set;
    assume a in IR \ IG1;
    then
A18: a in IR & not a in IG1 by XBOOLE_0:def 5;
    then consider c1,c2 be set such that
A19: a = [c1,c2] & c1 in cR & c2 in cR by RELSET_1:6;
    reconsider c1,c2 as Element of R by A19;
A20: c1 <= c2 by A18,A19,ORDERS_2:def 9;
    per cases by A5,XBOOLE_0:def 3;
    suppose
A21:  c1 in A1 & c2 in A1;
      then reconsider d1 = c1 as Element of G1 by YELLOW_0:def 15;
      reconsider d2 = c2 as Element of G1 by A21,YELLOW_0:def 15;
      d1 <= d2 by A2,A20,YELLOW_0:61;
      hence thesis by A18,A19,ORDERS_2:def 9;
    end;
    suppose c1 in A1 & c2 in A2;
      then a in [:A1,A2:] by A19,ZFMISC_1:def 2;
      then
A22:  a in [:A1,A2:] /\ IR by A18,XBOOLE_0:def 4;
      [:A1,A2:] misses IR
      proof
        assume not thesis;
        then [:A1,A2:] /\ IR <> {} by XBOOLE_0:def 7;
        then consider b be set such that
A23:    b in [:A1,A2:] /\ IR by XBOOLE_0:def 1;
A24:    b in [:A1,A2:] & b in IR by A23,XBOOLE_0:def 4;
        then consider b1,b2 be set such that
A25:    b1 in A1 & b2 in A2 & b = [b1,b2] by ZFMISC_1:def 2;
        reconsider b1 as Element of R by A25;
        reconsider b2 as Element of R by A25;
A26:    b1 in cR & b2 in cR & IR c= EqCl IR by MSUALG_5:def 1;
        [x,b1] in EqCl IR by A25,Th30;
        then [x,b2] in EqCl IR by A24,A25,A26,EQREL_1:13;
        then b2 in A1 by Th30;
        then b2 in A1 /\ A2 by A25,XBOOLE_0:def 4;
        hence thesis by A2,A3,XBOOLE_0:def 7;
      end;
      hence thesis by A22,XBOOLE_0:def 7;
    end;
    suppose c1 in A2 & c2 in A1;
      then a in [:A2,A1:] by A19,ZFMISC_1:def 2;
      then
A27:  a in [:A2,A1:] /\ IR by A18,XBOOLE_0:def 4;
      [:A2,A1:] misses IR
      proof
        assume not thesis;
        then [:A2,A1:] /\ IR <> {} by XBOOLE_0:def 7;
        then consider b be set such that
A28:    b in [:A2,A1:] /\ IR by XBOOLE_0:def 1;
A29:    b in [:A2,A1:] & b in IR by A28,XBOOLE_0:def 4;
        then consider b1,b2 be set such that
A30:    b1 in A2 & b2 in A1 & b = [b1,b2] by ZFMISC_1:def 2;
        reconsider b1 as Element of R by A30;
        reconsider b2 as Element of R by A30;
        b1 in cR & b2 in cR & IR c= EqCl IR by MSUALG_5:def 1;
        then
A31:    [b2,b1] in EqCl IR by A29,A30,EQREL_1:12;
        [x,b2] in EqCl IR by A30,Th30;
        then [x,b1] in EqCl IR by A31,EQREL_1:13;
        then b1 in A1 by Th30;
        then b1 in A1 /\ A2 by A30,XBOOLE_0:def 4;
        hence thesis by A2,A3,XBOOLE_0:def 7;
      end;
      hence thesis by A27,XBOOLE_0:def 7;
    end;
    suppose
A32:  c1 in A2 & c2 in A2;
      then reconsider d1 = c1 as Element of G2 by YELLOW_0:def 15;
      reconsider d2 = c2 as Element of G2 by A32,YELLOW_0:def 15;
      d1 <= d2 by A2,A20,A32,YELLOW_0:61;
      hence thesis by A19,ORDERS_2:def 9;
    end;
  end;
  IR = (the InternalRel of G1) \/ (the InternalRel of G2)
  proof
    set IG1 = the InternalRel of G1, IG2 = the InternalRel of G2;
    thus IR c= IG1 \/ IG2
    proof
      let a be set;
      assume
A33:  a in IR;
      assume not thesis;
      then not a in IG1 & not a in IG2 by XBOOLE_0:def 3;
      hence contradiction by A13,A33,XBOOLE_0:def 5;
    end;
    let a be set;
    assume
A34: a in IG1 \/ IG2;
    per cases by A34,XBOOLE_0:def 3;
    suppose
A35:  a in IG1;
      then consider v,w be set such that
A36:  a = [v,w] & v in A1 & w in A1 by A2,RELSET_1:6;
      reconsider v,w as Element of G1 by A36,YELLOW_0:def 15;
A37:  v <= w by A35,A36,ORDERS_2:def 9;
      reconsider u1 = v, u2 = w as Element of R by A36;
      u1 <= u2 by A37,YELLOW_0:60;
      hence thesis by A36,ORDERS_2:def 9;
    end;
    suppose
A38:  a in IG2;
      then consider v,w be set such that
A39:  a = [v,w] & v in A2 & w in A2 by A2,RELSET_1:6;
      reconsider v,w as Element of G2 by A39,YELLOW_0:def 15;
A40:  v <= w by A38,A39,ORDERS_2:def 9;
      reconsider u1 = v, u2 = w as Element of R by A39;
      u1 <= u2 by A40,YELLOW_0:60;
      hence thesis by A39,ORDERS_2:def 9;
    end;
  end;
  then IR = the InternalRel of union_of(G1,G2) by NECKLA_2:def 2;
  hence thesis by A3,A4,A7;
end;

theorem Th32:
  for R be non empty irreflexive symmetric RelStr holds
  ComplRelStr R is not path-connected implies
  ex G1,G2 being non empty strict irreflexive symmetric RelStr
  st the carrier of G1 misses the carrier of G2 &
  the RelStr of R = sum_of(G1,G2)
proof
  let R be non empty irreflexive symmetric RelStr;
  set cR = the carrier of R, IR = the InternalRel of R, CR = ComplRelStr R,
  ICR = the InternalRel of ComplRelStr R, cCR = the carrier of ComplRelStr R;
  assume CR is not path-connected;
  then consider x,y being set such that
A1: x in cCR & y in cCR & x <> y & not ICR reduces x,y by Def2;
  reconsider x,y as Element of CR by A1;
  set A1 = component x, A2 = (the carrier of R) \ A1;
  reconsider A1 as Subset of R by NECKLACE:def 9;
  reconsider A2 as Subset of R;
  set G1 = subrelstr A1, G2 = subrelstr A2;
A2: the carrier of G1 = A1 & the carrier of G2 = A2 by YELLOW_0:def 15;
  then
A3: (the carrier of G1) misses (the carrier of G2) by XBOOLE_1:79;
  ICR = ICR~ by RELAT_2:30;
  then not ICR \/ ICR~ reduces x,y by A1;
  then not x,y are_convertible_wrt ICR by REWRITE1:def 4;
  then not [x,y] in EqCl ICR by MSUALG_6:41;
  then y in cR & not y in A1 by A1,Th30,NECKLACE:def 9;
  then the carrier of G2 is non empty by A2,XBOOLE_0:def 5;
  then
A4: G1 is non empty strict RelStr &
  G2 is non empty strict RelStr by A2;
A5: cR = A1 \/ A2
  proof
    thus cR c= A1 \/ A2
    proof
      let a be set;
      assume
A6:   a in cR;
      assume not thesis;
      then not a in A1 & not a in A2 by XBOOLE_0:def 3;
      hence contradiction by A6,XBOOLE_0:def 5;
    end;
    let a be set;
    assume
A7: a in A1 \/ A2;
    per cases by A7,XBOOLE_0:def 3;
    suppose a in A1;
      hence thesis;
    end;
    suppose a in A2;
      hence thesis;
    end;
  end;
  then
A8: cR = the carrier of sum_of(G1,G2) by A2,NECKLA_2:def 3;
  set IG1 = the InternalRel of G1, IG2 = the InternalRel of G2,
  G1G2 = [:the carrier of G1,the carrier of G2:],
  G2G1 = [:the carrier of G2,the carrier of G1:];
A9: the InternalRel of G1 misses the InternalRel of G2
  proof
    assume not thesis;
    then IG1 /\ IG2 <> {} by XBOOLE_0:def 7;
    then consider a be set such that
A10: a in IG1 /\ IG2 by XBOOLE_0:def 1;
A11: a in IG1 & a in IG2 by A10,XBOOLE_0:def 4;
    then consider c1,c2 be set such that
A12: a = [c1,c2] & c1 in A1 & c2 in A1 by A2,RELSET_1:6;
    consider g1,g2 be set such that
A13: a = [g1,g2] & g1 in A2 & g2 in A2 by A2,A11,RELSET_1:6;
    c1 in A1 & c1 in A2 by A12,A13,ZFMISC_1:33;
    then c1 in A1 /\ A2 by XBOOLE_0:def 4;
    hence contradiction by A2,A3,XBOOLE_0:def 7;
  end;
A14: the InternalRel of G2 = IR \ IG1 \ G1G2 \ G2G1
  proof
    thus IG2 c= IR \ IG1 \ G1G2 \ G2G1
    proof
      let a be set;
      assume
A15:  a in IG2;
      then consider g1,g2 be set such that
A16:  a = [g1,g2] & g1 in A2 & g2 in A2 by A2,RELSET_1:6;
      reconsider g1,g2 as Element of G2 by A16,YELLOW_0:def 15;
A17:  g1 <= g2 by A15,A16,ORDERS_2:def 9;
      reconsider u1 = g1, u2 = g2 as Element of R by A16;
      u1 <= u2 by A17,YELLOW_0:60;
      then
A18:  a in IR by A16,ORDERS_2:def 9;
A19:  not a in IG1
      proof
        assume not thesis;
        then a in IG1 /\ IG2 by A15,XBOOLE_0:def 4;
        hence contradiction by A9,XBOOLE_0:def 7;
      end;
A20:  not a in G1G2
      proof
        assume a in G1G2;
        then g1 in A1 & g2 in A2 by A2,A16,ZFMISC_1:106;
        then g1 in A1 /\ A2 by A16,XBOOLE_0:def 4;
        hence thesis by A2,A3,XBOOLE_0:def 7;
      end;
A21:  not a in G2G1
      proof
        assume a in G2G1;
        then g1 in A2 & g2 in A1 by A2,A16,ZFMISC_1:106;
        then g2 in A1 /\ A2 by A16,XBOOLE_0:def 4;
        hence thesis by A2,A3,XBOOLE_0:def 7;
      end;
      a in IR \ IG1 by A18,A19,XBOOLE_0:def 5;
      then a in IR \ IG1 \ G1G2 by A20,XBOOLE_0:def 5;
      hence thesis by A21,XBOOLE_0:def 5;
    end;
    let a be set;
    assume a in IR \ IG1 \ G1G2 \ G2G1;
    then
A22: a in IR \ IG1 \ G1G2 & not a in G2G1 by XBOOLE_0:def 5;
    then
A23: a in IR \ IG1 & not a in G1G2 & not a in G2G1 by XBOOLE_0:def 5;
    then
A24: a in IR & not a in IG1 & not a in G1G2 & not a in G2G1 by XBOOLE_0:def 5;
    then consider c1,c2 be set such that
A25: a = [c1,c2] & c1 in cR & c2 in cR by RELSET_1:6;
    reconsider c1,c2 as Element of R by A25;
A26: c1 <= c2 by A24,A25,ORDERS_2:def 9;
    per cases by A5,XBOOLE_0:def 3;
    suppose
A27:  c1 in A1 & c2 in A1;
      then reconsider d1 = c1 as Element of G1 by YELLOW_0:def 15;
      reconsider d2 = c2 as Element of G1 by A27,YELLOW_0:def 15;
      d1 <= d2 by A2,A26,YELLOW_0:61;
      hence thesis by A24,A25,ORDERS_2:def 9;
    end;
    suppose c1 in A1 & c2 in A2;
      hence thesis by A2,A23,A25,ZFMISC_1:106;
    end;
    suppose c1 in A2 & c2 in A1;
      hence thesis by A2,A22,A25,ZFMISC_1:106;
    end;
    suppose
A28:  c1 in A2 & c2 in A2;
      then reconsider d1 = c1,d2 = c2 as Element of G2 by YELLOW_0:def 15;
      d1 <= d2 by A2,A26,A28,YELLOW_0:61;
      hence thesis by A25,ORDERS_2:def 9;
    end;
  end;
  IR = (the InternalRel of G1) \/ (the InternalRel of G2)
  \/ [:the carrier of G1,the carrier of G2:]
  \/ [:the carrier of G2,the carrier of G1:]
  proof
    set G1G2 = [:the carrier of G1,the carrier of G2:],
    G2G1 = [:the carrier of G2,the carrier of G1:];
    thus IR c= IG1 \/ IG2 \/ G1G2 \/ G2G1
    proof
      let a be set;
      assume
A29:  a in IR;
      assume not thesis;
      then not a in (IG1 \/ IG2 \/ G1G2) & not a in G2G1 by XBOOLE_0:def 3;
      then not a in (IG1 \/ IG2) & not a in G1G2 & not a in G2G1
      by XBOOLE_0:def 3;
      then
A30:  not a in IG1 & not a in IG2 & not a in G1G2 & not a in G2G1
      by XBOOLE_0:def 3;
      then not a in IR \ IG1 \ G1G2 or a in G2G1 by A14,XBOOLE_0:def 5;
      then not a in IR \ IG1 or a in G1G2 or a in G2G1 by XBOOLE_0:def 5;
      hence thesis by A29,A30,XBOOLE_0:def 5;
    end;
    let a be set such that
A31: a in IG1 \/ IG2 \/ G1G2 \/ G2G1;
    a in IG1 \/ IG2 \/ G1G2 or a in G2G1 by A31,XBOOLE_0:def 3;
    then
A32: a in IG1 \/ IG2 or a in G1G2 or a in G2G1 by XBOOLE_0:def 3;
    per cases by A32,XBOOLE_0:def 3;
    suppose
A33:  a in IG1;
      then consider v,w be set such that
A34:  a = [v,w] & v in A1 & w in A1 by A2,RELSET_1:6;
      reconsider v,w as Element of G1 by A34,YELLOW_0:def 15;
A35:  v <= w by A33,A34,ORDERS_2:def 9;
      reconsider u1 = v, u2 = w as Element of R by A34;
      u1 <= u2 by A35,YELLOW_0:60;
      hence thesis by A34,ORDERS_2:def 9;
    end;
    suppose
A36:  a in IG2;
      then consider v,w be set such that
A37:  a = [v,w] & v in A2 & w in A2 by A2,RELSET_1:6;
      reconsider v,w as Element of G2 by A37,YELLOW_0:def 15;
A38:  v <= w by A36,A37,ORDERS_2:def 9;
      reconsider u1 = v, u2 = w as Element of R by A37;
      u1 <= u2 by A38,YELLOW_0:60;
      hence thesis by A37,ORDERS_2:def 9;
    end;
    suppose
A39:  a in G1G2;
      then consider v,w be set such that
A40:  a = [v,w] by RELAT_1:def 1;
A41:  v in A1 & w in A2 by A2,A39,A40,ZFMISC_1:106;
      then reconsider v,w as Element of CR by NECKLACE:def 9;
A42:  v <> w
      proof
        assume
A43:    v = w;
        A1 /\ A2 = {} by A2,A3,XBOOLE_0:def 7;
        hence thesis by A41,A43,XBOOLE_0:def 4;
      end;
      assume
A44:  not thesis;
A45:  not a in id cR by A40,A42,RELAT_1:def 10;
      [v,w] in [:cR,cR:] by A41,ZFMISC_1:106;
      then a in [:cR,cR:] \ IR by A40,A44,XBOOLE_0:def 5;
      then a in IR` by SUBSET_1:def 5;
      then a in IR` \ id cR by A45,XBOOLE_0:def 5;
      then [v,w] in ICR by A40,NECKLACE:def 9;
      then v,w are_convertible_wrt ICR by REWRITE1:30;
      then
A46:  [v,w] in EqCl ICR by MSUALG_6:41;
      [x,v] in EqCl ICR by A41,Th30;
      then [x,w] in EqCl ICR by A46,EQREL_1:13;
      then w in component x by Th30;
      then w in A1 /\ A2 by A41,XBOOLE_0:def 4;
      hence thesis by A2,A3,XBOOLE_0:def 7;
    end;
    suppose
A47:  a in G2G1;
      then consider v,w be set such that
A48:  a = [v,w] by RELAT_1:def 1;
A49:  v in A2 & w in A1 by A2,A47,A48,ZFMISC_1:106;
      then reconsider v,w as Element of CR by NECKLACE:def 9;
A50:  v <> w
      proof
        assume
A51:    v = w;
        A1 /\ A2 = {} by A2,A3,XBOOLE_0:def 7;
        hence thesis by A49,A51,XBOOLE_0:def 4;
      end;
      assume
A52:  not thesis;
A53:  not a in id cR by A48,A50,RELAT_1:def 10;
      [v,w] in [:cR,cR:] by A49,ZFMISC_1:106;
      then a in [:cR,cR:] \ IR by A48,A52,XBOOLE_0:def 5;
      then a in IR` by SUBSET_1:def 5;
      then a in IR` \ id cR by A53,XBOOLE_0:def 5;
      then [v,w] in ICR by A48,NECKLACE:def 9;
      then v,w are_convertible_wrt ICR by REWRITE1:30;
      then [v,w] in EqCl ICR by MSUALG_6:41;
      then
A54:  [w,v] in EqCl ICR by EQREL_1:12;
      [x,w] in EqCl ICR by A49,Th30;
      then [x,v] in EqCl ICR by A54,EQREL_1:13;
      then v in component x by Th30;
      then v in A1 /\ A2 by A49,XBOOLE_0:def 4;
      hence thesis by A2,A3,XBOOLE_0:def 7;
    end;
  end;
  then IR = the InternalRel of sum_of(G1,G2) by NECKLA_2:def 3;
  hence thesis by A3,A4,A8;
end;

Lm1: for X being non empty finite set, A,B being non empty set
st X = A \/ B & A misses B holds card A in card X
proof
  let X be non empty finite set;
  let A,B be non empty set;
  set n = card X;
  assume
A1: X = A \/ B & A misses B;
  then
A2: card A c= n & card B c= n by CARD_1:27,XBOOLE_1:7;
  then reconsider A as finite set;
  reconsider B as finite set by A2;
A3: n = card A + card B by A1,CARD_2:53;
A4: card A >= 1 & card B >= 1 by NAT_1:14;
A5: card A < n
  proof
    assume not thesis;
    then card A + card B >= n+1 by A4,XREAL_1:9;
    hence thesis by A3,NAT_1:13;
  end;
  thus thesis by A5,NAT_1:45;
end;

theorem
  for G being irreflexive RelStr st G in fin_RelStr_sp
  holds ComplRelStr G in fin_RelStr_sp
proof
  let G be irreflexive RelStr;
  assume
A1: G in fin_RelStr_sp;
  defpred P[Nat] means for G being irreflexive RelStr st
  card the carrier of G = $1 & G in fin_RelStr_sp
  holds ComplRelStr G in fin_RelStr_sp;
A2: for k being Nat st for n being Nat st n < k holds P[n] holds P[k]
  proof
    let k be Nat such that
A3: for n being Nat st n < k holds P[n];
    let G be irreflexive RelStr;
    assume that
A4: card the carrier of G = k and
A5: G in fin_RelStr_sp;
    per cases by A5,NECKLA_2:6;
    suppose G is strict non empty trivial RelStr;
      hence thesis by A5,Th15;
    end;
    suppose ex G1,G2 being strict RelStr st
      (the carrier of G1) misses (the carrier of G2) &
      G1 in fin_RelStr_sp & G2 in fin_RelStr_sp &
      (G = union_of(G1,G2) or G = sum_of(G1,G2) );
      then consider G1,G2 being strict RelStr such that
A6:   (the carrier of G1) misses the carrier of G2 &
      G1 in fin_RelStr_sp & G2 in fin_RelStr_sp and
A7:   G = union_of(G1,G2) or G = sum_of(G1,G2);
A8:   G1 is non empty finite by A6,NECKLA_2:4;
      then the carrier of G1 is non empty finite;
      then reconsider n1 = card the carrier of G1 as Nat;
A11:  G2 is non empty finite by A6,NECKLA_2:4;
      then the carrier of G2 is non empty finite;
      then reconsider n2 = card the carrier of G2 as Nat;
      thus thesis
      proof
        per cases by A7;
        suppose
A14:      G = union_of(G1,G2);
          then reconsider G1 as irreflexive RelStr by Th9;
          reconsider G2 as irreflexive RelStr by A14,Th9;
A15:      the carrier of G = (the carrier of G1) \/ the carrier of G2 by A14,
NECKLA_2:def 2;
          reconsider cG1 = the carrier of G1 as non empty finite set by A8;
          reconsider cG2 = the carrier of G2 as non empty finite set by A11;
A16:      card the carrier of G = card cG1 + card cG2 by A6,A15,CARD_2:53;
          card cG1 = n1;
          then n1 <> 0;
          then
A17:      n1 > 0;
          card cG2 = n2;
          then n2 <> 0;
          then
A18:      n2 > 0;
          n1 < k
          proof
            assume not thesis;
            then k+0 < n2 + n1 by A18,XREAL_1:10;
            hence thesis by A4,A16;
          end;
          then
A19:      ComplRelStr G1 in fin_RelStr_sp by A3,A6;
          n2 < k
          proof
            assume not thesis;
            then k+0 < n1 + n2 by A17,XREAL_1:10;
            hence thesis by A4,A16;
          end;
          then
A20:      ComplRelStr G2 in fin_RelStr_sp by A3,A6;
A21:      ComplRelStr G = sum_of(ComplRelStr G1,ComplRelStr G2) by A6,A14,Th17;
          the carrier of ComplRelStr G1 = the carrier of G1 &
          the carrier of ComplRelStr G2 = the carrier of G2 by NECKLACE:def 9;
          hence thesis by A6,A19,A20,A21,NECKLA_2:def 5;
        end;
        suppose
A22:      G = sum_of(G1,G2);
          then reconsider G1 as irreflexive RelStr by Th9;
          reconsider G2 as irreflexive RelStr by A22,Th9;
A23:      the carrier of G = (the carrier of G1) \/ the carrier of G2 by A22,
NECKLA_2:def 3;
          reconsider cG1 = the carrier of G1 as non empty finite set by A8;
          reconsider cG2 = the carrier of G2 as non empty finite set by A11;
A24:      card the carrier of G = card cG1 + card cG2 by A6,A23,CARD_2:53;
          card cG1 = n1;
          then n1 <> 0;
          then
A25:      n1 > 0;
          card cG2 = n2;
          then n2 <> 0;
          then
A26:      n2 > 0;
          n1 < k
          proof
            assume not thesis;
            then k+0 < n2 + n1 by A26,XREAL_1:10;
            hence thesis by A4,A24;
          end;
          then
A27:      ComplRelStr G1 in fin_RelStr_sp by A3,A6;
          n2 < k
          proof
            assume not thesis;
            then k+0 < n1 + n2 by A25,XREAL_1:10;
            hence thesis by A4,A24;
          end;
          then
A28:      ComplRelStr G2 in fin_RelStr_sp by A3,A6;
A29:      ComplRelStr G = union_of(ComplRelStr G1,ComplRelStr G2)
          by A6,A22,Th18;
          the carrier of ComplRelStr G1 = the carrier of G1 &
          the carrier of ComplRelStr G2 = the carrier of G2 by NECKLACE:def 9;
          hence thesis by A6,A27,A28,A29,NECKLA_2:def 5;
        end;
      end;
    end;
  end;
A30: for k being Nat holds P[k] from NAT_1:sch 4(A2);
  G is finite by A1,NECKLA_2:4;
  then the carrier of G is finite;
  then card the carrier of G is Nat;
  hence thesis by A1,A30;
end;

theorem Th34:
  for R be irreflexive symmetric RelStr
  st card (the carrier of R) = 2 & the carrier of R in FinSETS
  holds the RelStr of R in fin_RelStr_sp
proof
  let R be irreflexive symmetric RelStr;
  assume
A1: card (the carrier of R) = 2 & the carrier of R in FinSETS;
  then consider a,b being set such that
A2: the carrier of R = {a,b} &
  (the InternalRel of R = {[a,b],[b,a]} or the InternalRel of R = {}) by Th6;
  set A = {a}, B = {b};
A3: A c= the carrier of R
  proof
    let x be set;
    assume x in A;
    then x = a by TARSKI:def 1;
    hence thesis by A2,TARSKI:def 2;
  end;
  then reconsider A as Subset of R;
A4: B c= the carrier of R
  proof
    let x be set;
    assume x in B;
    then x = b by TARSKI:def 1;
    hence thesis by A2,TARSKI:def 2;
  end;
  then reconsider B as Subset of R;
  set H1 = subrelstr A, H2 = subrelstr B;
  H1 is non empty by YELLOW_0:def 15;
  then reconsider H1 as non empty strict irreflexive symmetric RelStr;
A5: the carrier of H1 = A by YELLOW_0:def 15;
  the carrier of H1 in FinSETS
  proof
    the carrier of H1 c= the carrier of R by A3,YELLOW_0:def 15;
    hence thesis by A1,CLASSES1:6,CLASSES2:def 2;
  end;
  then
A6: H1 in fin_RelStr_sp by A5,NECKLA_2:def 5;
  H2 is non empty by YELLOW_0:def 15;
  then reconsider H2 as non empty strict irreflexive symmetric RelStr;
A7: the carrier of H2 = B by YELLOW_0:def 15;
  the carrier of H2 in FinSETS
  proof
    the carrier of H2 c= the carrier of R by A4,YELLOW_0:def 15;
    hence thesis by A1,CLASSES1:6,CLASSES2:def 2;
  end;
  then
A8: H2 in fin_RelStr_sp by A7,NECKLA_2:def 5;
  a <> b
  proof
    assume not thesis;
    then the carrier of R = {a} by A2,ENUMSET1:69;
    hence thesis by A1,CARD_1:50;
  end;
  then
A9: A misses B by ZFMISC_1:17;
  then
A10: (the carrier of H1) misses (the carrier of H2) by A7,YELLOW_0:def 15;
A11: the carrier of R =
  (the carrier of H1) \/ (the carrier of H2) by A2,A5,A7,ENUMSET1:41;
  the InternalRel of H1 c= [:{a},{a}:] by A5;
  then the InternalRel of H1 c= {[a,a]} by ZFMISC_1:35;
  then
A12: the InternalRel of H1 = {} or the InternalRel of H1 = {[a,a]}
  by ZFMISC_1:39;
A13: the InternalRel of H1 = {}
  proof
    assume
A14: not thesis;
    a in A by TARSKI:def 1;
    then a in the carrier of H1 by YELLOW_0:def 15;
    then not [a,a] in the InternalRel of H1 by NECKLACE:def 6;
    hence thesis by A12,A14,TARSKI:def 1;
  end;
  the InternalRel of H2 c= [:{b},{b}:] by A7;
  then the InternalRel of H2 c= {[b,b]} by ZFMISC_1:35;
  then
A15: the InternalRel of H2 = {} or the InternalRel of H2 = {[b,b]}
  by ZFMISC_1:39;
A16: the InternalRel of H2 = {}
  proof
    assume
A17: not thesis;
    b in B by TARSKI:def 1;
    then b in the carrier of H2 by YELLOW_0:def 15;
    then not [b,b] in the InternalRel of H2 by NECKLACE:def 6;
    hence thesis by A15,A17,TARSKI:def 1;
  end;
  per cases by A2;
  suppose
A18: the InternalRel of R = {[a,b],[b,a]};
    set S = sum_of(H1,H2);
    the InternalRel of S = (the InternalRel of H1) \/ (the InternalRel of
    H2) \/ [:A,B:] \/ [:B,A:] by A5,A7,NECKLA_2:def 3;
    then the InternalRel of S = {[a,b]} \/ [:{b},{a}:] by A13,A16,ZFMISC_1:35;
    then the InternalRel of S = {[a,b]} \/ {[b,a]} by ZFMISC_1:35;
    then
A19: the InternalRel of S = the InternalRel of R by A18,ENUMSET1:41;
    the carrier of S = the carrier of R by A11,NECKLA_2:def 3;
    hence thesis by A5,A6,A7,A8,A9,A19,NECKLA_2:def 5;
  end;
  suppose
A20: the InternalRel of R = {};
    set U = union_of(H1,H2);
A21: the InternalRel of U = (the InternalRel of H1) \/ (the InternalRel of H2
    ) by NECKLA_2:def 2;
    the carrier of U = the carrier of R by A11,NECKLA_2:def 2;
    hence thesis by A6,A8,A10,A13,A16,A20,A21,NECKLA_2:def 5;
  end;
end;

theorem
  for R be RelStr st R in fin_RelStr_sp holds R is symmetric
proof
  let X be RelStr;
  assume
A1: X in fin_RelStr_sp;
  per cases;
  suppose X is trivial;
    then
A2: the carrier of X is trivial;
    thus thesis
    proof
      per cases by A2,REALSET1:def 4;
      suppose
A3:     the carrier of X is empty;
        let a,b be set;
        assume a in the carrier of X & b in the carrier of X &
        [a,b] in the InternalRel of X;
        hence thesis by A3;
      end;
      suppose ex x being set st the carrier of X = {x};
        then consider x being set such that
A4:     the carrier of X = {x};
A5:     [:the carrier of X,the carrier of X:] = {[x,x]} by A4,ZFMISC_1:35;
        thus thesis
        proof
          per cases by A5,ZFMISC_1:39;
          suppose
A6:         the InternalRel of X = {};
            let a,b be set;
            assume a in the carrier of X & b in the carrier of X &
            [a,b] in the InternalRel of X;
            hence thesis by A6;
          end;
          suppose
A7:         the InternalRel of X = {[x,x]};
            let a,b be set;
            assume
A8:         a in the carrier of X & b in the carrier of X &
            [a,b] in the InternalRel of X;
            then [a,b] = [x,x] by A7,TARSKI:def 1;
            then a = x & b = x by ZFMISC_1:33;
            hence thesis by A8;
          end;
        end;
      end;
    end;
  end;
  suppose
A9: not X is trivial;
    defpred P[Nat] means
    for X be non empty RelStr st not X is trivial & X in fin_RelStr_sp holds
    card (the carrier of X) c= $1 implies X is symmetric;
A10: P[0];
A14: for k being Nat st P[k] holds P[k+1]
    proof
      let k be Nat such that
A15:  P[k];
      let Y be non empty RelStr such that
A16:  not Y is trivial & Y in fin_RelStr_sp;
      reconsider k1=k as Element of NAT by ORDINAL1:def 13;
      set cY = the carrier of Y;
      consider R be strict RelStr such that
A17:  Y = R & the carrier of R in FinSETS by A16,NECKLA_2:def 4;
      reconsider cY as finite set by A17;
      consider H1,H2 be strict RelStr such that
A18:  the carrier of H1 misses the carrier of H2 &
      H1 in fin_RelStr_sp & H2 in fin_RelStr_sp and
A19:  (Y = union_of(H1,H2) or Y = sum_of(H1,H2)) by A16,NECKLA_2:6;
      set cH1 = the carrier of H1, cH2 = the carrier of H2;
A20:  card cY = card ((cH1) \/ cH2) by A19,NECKLA_2:def 2,def 3;
      consider R1 be strict RelStr such that
A21:  H1 = R1 & the carrier of R1 in FinSETS by A18,NECKLA_2:def 4;
      reconsider cH1 as finite set by A21;
      consider R2 be strict RelStr such that
A22:  H2 = R2 & the carrier of R2 in FinSETS by A18,NECKLA_2:def 4;
      reconsider cH2 as finite set by A22;
A23:  card cY = card cH1 + card cH2 by A18,A20,CARD_2:53;
      assume card (the carrier of Y) c= k+1;
      then card cY c= card (k1+1) by CARD_1:def 5;
      then card cY <= card (k1+1) by NAT_1:40;
      then
A24:  card cY <= k+1 by CARD_1:def 5;
      H1 is non empty & H2 is non empty by A18,NECKLA_2:4;
      then cH1 is non empty & cH2 is non empty;
      then card cH1 <> 0 & card cH2 <> 0;
      then
A25:  card cH1 >= 1 & card cH2 >= 1 by NAT_1:14;
      per cases by A23,A24,A25,NAT_1:8,XXREAL_0:1;
      suppose card cY <= k;
        then card cY <= card k1 by CARD_1:def 5;
        then card (the carrier of Y) c= card k by NAT_1:40;
        then card (the carrier of Y) c= k1 by CARD_1:def 5;
        hence thesis by A15,A16;
      end;
      suppose
A26:    card cY = k+1 & k = 0;
        consider x be set;
        card cY = card {x} by A26,CARD_1:50;
        then cY,{x} are_equipotent by CARD_1:21;
        then consider y be set such that
A27:    cY = {y} by CARD_1:48;
        thus thesis by A16,A27;
      end;
      suppose
A28:    card cH1 + card cH2 = k+1 & k > 0 & card cH1 = 1 & card cH2 = 1;
        then consider x be set such that
A29:    cH1 = {x} by CARD_2:60;
        consider y be set such that
A30:    cH2 = {y} by A28,CARD_2:60;
A31:    the InternalRel of H1 is_symmetric_in cH1 by A29,Th5;
        the InternalRel of H2 is_symmetric_in cH2 by A30,Th5;
        then reconsider H2 as symmetric RelStr by NECKLACE:def 4;
        reconsider H1 as symmetric RelStr by A31,NECKLACE:def 4;
        union_of(H1,H2) is symmetric & sum_of(H1,H2) is symmetric;
        hence thesis by A19;
      end;
      suppose
A32:    card cH1 + card cH2 = k+1 & k > 0 & card cH1 = 1 & card cH2 > 1;
        then consider x be set such that
A33:    cH1 = {x} by CARD_2:60;
        the InternalRel of H1 is_symmetric_in cH1 by A33,Th5;
        then reconsider H1 as symmetric RelStr by NECKLACE:def 4;
        card cH2 is non trivial by A32,NAT_2:30;
        then card cH2 >= 2 by NAT_2:31;
        then cH2 is non empty non trivial by REALSET1:13;
        then H2 is non empty non trivial;
        then reconsider H2 as symmetric RelStr by A15,A18,A32;
        union_of(H1,H2) is symmetric & sum_of(H1,H2) is symmetric;
        hence thesis by A19;
      end;
      suppose
A34:    card cH1 + card cH2 = k+1 & k > 0 & card cH1 > 1 & card cH2 = 1;
        then consider x be set such that
A35:    cH2 = {x} by CARD_2:60;
        the InternalRel of H2 is_symmetric_in cH2 by A35,Th5;
        then reconsider H2 as symmetric RelStr by NECKLACE:def 4;
        card cH1 is non trivial by A34,NAT_2:30;
        then card cH1 >= 2 by NAT_2:31;
        then cH1 is non empty non trivial by REALSET1:13;
        then H1 is non empty non trivial;
        then reconsider H1 as symmetric RelStr by A15,A18,A34;
        union_of(H1,H2) is symmetric & sum_of(H1,H2) is symmetric;
        hence thesis by A19;
      end;
      suppose
A36:    card cH1 + card cH2 = k+1 & k > 0 & card cH1 > 1 & card cH2 > 1;
        card cH1 < k+1
        proof
          assume not thesis;
          then card cH1 + card cH2 >= (k+1)+1 by A25,XREAL_1:9;
          hence thesis by A36,NAT_1:13;
        end;
        then card cH1 <= k by NAT_1:13;
        then card cH1 <= card k1 by CARD_1:def 5;
        then card cH1 c= card k by NAT_1:40;
        then
A37:    card cH1 c= k1 by CARD_1:def 5;
        card cH1 is non trivial by A36,NAT_2:30;
        then card cH1 >= 2 by NAT_2:31;
        then cH1 is non empty non trivial by REALSET1:13;
        then H1 is non empty non trivial;
        then reconsider H1 as symmetric RelStr by A15,A18,A37;
        card cH2 < k+1
        proof
          assume not thesis;
          then card cH1 + card cH2 >= (k+1)+1 by A25,XREAL_1:9;
          hence thesis by A36,NAT_1:13;
        end;
        then card cH2 <= k by NAT_1:13;
        then card cH2 <= card k1 by CARD_1:def 5;
        then card cH2 c= card k by NAT_1:40;
        then
A38:    card cH2 c= k1 by CARD_1:def 5;
        card cH2 is non trivial by A36,NAT_2:30;
        then card cH2 >= 2 by NAT_2:31;
        then cH2 is non empty non trivial by REALSET1:13;
        then H2 is non empty non trivial;
        then reconsider H2 as symmetric RelStr by A15,A18,A38;
        union_of(H1,H2) is symmetric & sum_of(H1,H2) is symmetric;
        hence thesis by A19;
      end;
    end;
A39: for k being Nat holds P[k] from NAT_1:sch 2(A10,A14);
    consider R be strict RelStr such that
A40: X = R & the carrier of R in FinSETS by A1,NECKLA_2:def 4;
    reconsider X as non empty RelStr by A1,NECKLA_2:4;
    the carrier of X is finite by A40;
    then card the carrier of X is Nat;
    hence thesis by A1,A9,A39;
  end;
end;

theorem Th36:
  for G being RelStr, H1,H2 being non empty RelStr,
  x being Element of H1, y being Element of H2
  st G = union_of(H1,H2) & the carrier of H1 misses the carrier of H2
  holds not [x,y] in the InternalRel of G
proof
  let G be RelStr;
  let H1,H2 be non empty RelStr;
  let x be Element of H1;
  let y be Element of H2;
  assume
A1: G = union_of(H1,H2) & (the carrier of H1) misses (the carrier of H2);
  assume not thesis;
  then
A2: [x,y] in (the InternalRel of H1) \/ the InternalRel of H2
  by A1,NECKLA_2:def 2;
  per cases by A2,XBOOLE_0:def 3;
  suppose [x,y] in (the InternalRel of H1);
    then x in the carrier of H1 & y in the carrier of H1 by ZFMISC_1:106;
    then y in (the carrier of H1) /\ the carrier of H2 by XBOOLE_0:def 4;
    hence thesis by A1,XBOOLE_0:def 7;
  end;
  suppose [x,y] in (the InternalRel of H2);
    then x in the carrier of H2 & y in the carrier of H2 by ZFMISC_1:106;
    then x in (the carrier of H1) /\ the carrier of H2 by XBOOLE_0:def 4;
    hence thesis by A1,XBOOLE_0:def 7;
  end;
end;

theorem
  for G being RelStr, H1,H2 being non empty RelStr,
  x being Element of H1, y being Element of H2
  st G = sum_of(H1,H2) holds not [x,y] in the InternalRel of ComplRelStr G
proof
  let G being RelStr,H1,H2 being non empty RelStr, x being Element of H1,
  y being Element of H2;
  assume
A1: G = sum_of(H1,H2);
  set cH1 = the carrier of H1, cH2 = the carrier of H2,
  IH1 = the InternalRel of H1, IH2 = the InternalRel of H2;
  [x,y] in [:cH1,cH2:] \/ [:cH2,cH1:] by XBOOLE_0:def 3;
  then [x,y] in IH2 \/ ([:cH1,cH2:] \/ [:cH2,cH1:]) by XBOOLE_0:def 3;
  then [x,y] in IH1 \/ (IH2 \/ ([:cH1,cH2:] \/ [:cH2,cH1:])) by XBOOLE_0:def 3;
  then [x,y] in IH1 \/ (IH2 \/ [:cH1,cH2:] \/ [:cH2,cH1:]) by XBOOLE_1:4;
  then [x,y] in IH1 \/ IH2 \/ [:cH1,cH2:] \/ [:cH2,cH1:] by XBOOLE_1:113;
  then
A2: [x,y] in the InternalRel of G by A1,NECKLA_2:def 3;
  not [x,y] in the InternalRel of ComplRelStr G
  proof
    assume not thesis;
    then [x,y] in (the InternalRel of G) /\ the InternalRel of ComplRelStr G
    by A2,XBOOLE_0:def 4;
    then (the InternalRel of G) meets the InternalRel of ComplRelStr G
    by XBOOLE_0:def 7;
    hence contradiction by Th12;
  end;
  hence thesis;
end;

theorem Th38:
  for G being non empty symmetric RelStr, x being Element of G,
  R1,R2 being non empty RelStr st the carrier of R1 misses the carrier of R2
  & subrelstr ([#]G \ {x}) = union_of(R1,R2) & G is path-connected
  holds ex b being Element of R1 st [b,x] in the InternalRel of G
proof
  let G be non empty symmetric RelStr;
  let x be Element of G;
  let R1,R2 be non empty RelStr;
  assume that
A1: the carrier of R1 misses the carrier of R2 and
A2: subrelstr ([#]G \ {x}) = union_of(R1,R2) and
A3: G is path-connected;
  set R = subrelstr ([#]G \ {x}), A = the carrier of R;
A4: A = [#]G \ {x} by YELLOW_0:def 15;
  reconsider A as Subset of G by YELLOW_0:def 15;
  consider a being Element of R1;
  the carrier of R1 c= (the carrier of R1) \/ the carrier of R2 by XBOOLE_1:7;
  then
A5: the carrier of R1 c= the carrier of R by A2,NECKLA_2:def 2;
A6: the carrier of R = A;
  then the carrier of R1 c= the carrier of G by A5,XBOOLE_1:1;
  then
A7: a in the carrier of G by TARSKI:def 3;
  x <> a
  proof
    assume not thesis;
    then x in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 3;
    then x in (the carrier of G) \ {x} by A2,A4,NECKLA_2:def 2;
    then not x in {x} by XBOOLE_0:def 5;
    hence thesis by TARSKI:def 1;
  end;
  then (the InternalRel of G) reduces x,a by A3,A7,Def2;
  then consider p being FinSequence such that
A8: len p > 0 & p.1 = x & p.len p = a and
A9: for i being Element of NAT st i in dom p & i+1 in dom p
  holds [p.i, p.(i+1)] in (the InternalRel of G) by REWRITE1:12;
  defpred P[Nat] means p.$1 in the carrier of R1 & $1 in dom p &
  for k being Nat st k > $1 holds k in dom p implies p.k in the carrier of R1;
A10: ex k being Nat st P[k]
  proof
    P[len p] by A8,CARD_1:47,FINSEQ_3:27,FINSEQ_5:6;
    hence thesis;
  end;
  ex n0 being Nat st P[n0] &
  for n being Nat st P[n] holds n >= n0 from NAT_1:sch 5(A10);
  then consider n0 being Element of NAT such that
A11: P[n0] and
A12: for n being Nat st P[n] holds n >= n0;
  n0 <> 0
  proof
    assume not thesis;
    then 0 in Seg (len p) by A11,FINSEQ_1:def 3;
    hence contradiction by FINSEQ_1:3;
  end;
  then consider k0 being Nat such that
A13: n0 = k0 + 1 by NAT_1:6;
A14: n0 <> 1
  proof
    assume not thesis;
    then x in the carrier of G & not x in {x} by A4,A5,A8,A11,XBOOLE_0:def 5;
    hence contradiction by TARSKI:def 1;
  end;
A15: k0 >= 1
  proof
    assume not thesis;
    then k0 = 0 by NAT_1:26;
    hence contradiction by A13,A14;
  end;
  k0 <= len p
  proof
A16: k0 <= n0 by A13,XREAL_1:31;
    n0 in Seg (len p) by A11,FINSEQ_1:def 3;
    then n0 <= len p by FINSEQ_1:3;
    hence thesis by A16,XXREAL_0:2;
  end;
  then
A17: k0 in dom p by A15,FINSEQ_3:27;
A18: k0 < n0 by A13,NAT_1:13;
A19: for k being Nat st k > k0
  holds k in dom p implies p.k in the carrier of R1
  proof
    assume not thesis;
    then consider k being Nat such that
A20: k > k0 & k in dom p & not p.k in the carrier of R1;
    k > n0
    proof
      per cases by XXREAL_0:1;
      suppose k < n0;
        hence thesis by A13,A20,NAT_1:13;
      end;
      suppose n0 < k;
        hence thesis;
      end;
      suppose n0 = k;
        hence thesis by A11,A20;
      end;
    end;
    hence contradiction by A11,A20;
  end;
A21: [p.k0,p.(k0+1)] in the InternalRel of G by A9,A11,A13,A17;
  then
A22: p.k0 in the carrier of G & p.n0 in the carrier of G by A13,ZFMISC_1:106;
A23: the carrier of G = (the carrier of R) \/ {x}
  proof
    thus the carrier of G c= (the carrier of R) \/ {x}
    proof
      let a be set;
      assume
A24:  a in the carrier of G;
      per cases;
      suppose a = x;
        then a in {x} by TARSKI:def 1;
        hence thesis by XBOOLE_0:def 3;
      end;
      suppose a <> x;
        then a in the carrier of G & not a in {x} by A24,TARSKI:def 1;
        then a in A by A4,XBOOLE_0:def 5;
        hence thesis by XBOOLE_0:def 3;
      end;
    end;
    let a be set;
    assume
A25: a in (the carrier of R) \/ {x};
    per cases by A25,XBOOLE_0:def 3;
    suppose a in the carrier of R;
      hence thesis by A4;
    end;
    suppose a in {x};
      hence thesis;
    end;
  end;
  thus thesis
  proof
    per cases by A22,A23,XBOOLE_0:def 3;
    suppose
A26:  p.k0 in the carrier of R;
      then p.k0 in (the carrier of R1) \/ the carrier of R2
      by A2,NECKLA_2:def 2;
      then
A27:  p.k0 in the carrier of R1 or
      p.k0 in the carrier of R2 by XBOOLE_0:def 3;
      set u = p.k0, v = p.n0;
      [u,v] in [:the carrier of R,the carrier of R:]
      by A5,A11,A26,ZFMISC_1:106;
      then
A28:  [u,v] in (the InternalRel of G)|_2 the carrier of R
      by A13,A21,XBOOLE_0:def 4;
      reconsider u as Element of R2 by A12,A17,A18,A19,A27;
      reconsider v as Element of R1 by A11;
      not [u,v] in the InternalRel of R
      proof
        assume
A29:    not thesis;
A30:    v in the carrier of R1;
        u in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 3;
        then
A31:    u in the carrier of R by A2,NECKLA_2:def 2;
        the InternalRel of R is_symmetric_in the carrier of R
        by NECKLACE:def 4;
        then [v,u] in the InternalRel of R by A5,A29,A30,A31,RELAT_2:def 3;
        hence thesis by A1,A2,Th36;
      end;
      hence thesis by A28,YELLOW_0:def 14;
    end;
    suppose p.k0 in {x};
      then
A32:  p.k0 = x by TARSKI:def 1;
      set b = p.n0;
      reconsider b as Element of R1 by A11;
A33:  b in the carrier of R by A5,TARSKI:def 3;
      (the InternalRel of G) is_symmetric_in the carrier of G
      by NECKLACE:def 4;
      then [b,x] in (the InternalRel of G) by A6,A13,A21,A32,A33,RELAT_2:def 3;
      hence thesis;
    end;
  end;
end;

theorem Th39:
  for G being non empty symmetric irreflexive RelStr,
  a,b,c,d being Element of G, Z being Subset of G st Z = {a,b,c,d}
  & a,b,c,d are_mutually_different & [a,b] in the InternalRel of G
  & [b,c] in the InternalRel of G & [c,d] in the InternalRel of G
  & not [a,c] in the InternalRel of G & not [a,d] in the InternalRel of G
  & not [b,d] in the InternalRel of G holds subrelstr Z embeds Necklace 4
proof
  let G be non empty symmetric irreflexive symmetric RelStr;
  let a,b,c,d be Element of G;
  let Z be Subset of G;
  assume that
A1: Z = {a,b,c,d} and
A2: a,b,c,d are_mutually_different and
A3: [a,b] in the InternalRel of G and
A4: [b,c] in the InternalRel of G and
A5: [c,d] in the InternalRel of G and
A6: not [a,c] in the InternalRel of G and
A7: not [a,d] in the InternalRel of G and
A8: not [b,d] in the InternalRel of G;
  set H = subrelstr Z, N4 = Necklace 4, IH = the InternalRel of H,
  cH = the carrier of H, IG = the InternalRel of G,
  X = {[a,a],[a,b],[b,a],[b,b],[a,c],[a,d],[b,c],[b,d]},
  Y = {[c,a],[c,b],[d,a],[d,b],[c,c],[c,d],[d,c],[d,d]};
A9: the InternalRel of H = {[a,b],[b,a],[b,c],[c,b],[c,d],[d,c]}
  proof
    thus the InternalRel of H c= {[a,b],[b,a],[b,c],[c,b],[c,d],[d,c]}
    proof
      let x be set;
      assume x in IH;
      then
A10:  x in IG |_2 cH by YELLOW_0:def 14;
      then
A11:  x in IG & x in [:cH,cH:] by XBOOLE_0:def 4;
      the carrier of H = Z by YELLOW_0:def 15;
      then
A12:  x in X \/ Y by A1,A11,Th3;
      per cases by A12,XBOOLE_0:def 3;
      suppose
A13:    x in X;
        thus thesis
        proof
          per cases by A13,ENUMSET1:def 6;
          suppose
A14:        x = [a,a];
            not [a,a] in IG by NECKLACE:def 6;
            hence thesis by A10,A14,XBOOLE_0:def 4;
          end;
          suppose x =[a,b];
            hence thesis by ENUMSET1:def 4;
          end;
          suppose x =[b,a];
            hence thesis by ENUMSET1:def 4;
          end;
          suppose
A15:        x =[b,b];
            not [b,b] in IG by NECKLACE:def 6;
            hence thesis by A10,A15,XBOOLE_0:def 4;
          end;
          suppose x = [a,c];
            hence thesis by A6,A10,XBOOLE_0:def 4;
          end;
          suppose x = [a,d];
            hence thesis by A7,A10,XBOOLE_0:def 4;
          end;
          suppose x = [b,c];
            hence thesis by ENUMSET1:def 4;
          end;
          suppose x =[b,d];
            hence thesis by A8,A10,XBOOLE_0:def 4;
          end;
        end;
      end;
      suppose
A16:    x in Y;
A17:    IG is_symmetric_in the carrier of G by NECKLACE:def 4;
        thus thesis
        proof
          per cases by A16,ENUMSET1:def 6;
          suppose x = [c,a];
            hence thesis by A6,A11,A17,RELAT_2:def 3;
          end;
          suppose x = [c,b];
            hence thesis by ENUMSET1:def 4;
          end;
          suppose x = [d,a];
            hence thesis by A7,A11,A17,RELAT_2:def 3;
          end;
          suppose x = [d,b];
            hence thesis by A8,A11,A17,RELAT_2:def 3;
          end;
          suppose
A18:        x = [c,c];
            not [c,c] in IG by NECKLACE:def 6;
            hence thesis by A10,A18,XBOOLE_0:def 4;
          end;
          suppose x = [c,d];
            hence thesis by ENUMSET1:def 4;
          end;
          suppose x = [d,c];
            hence thesis by ENUMSET1:def 4;
          end;
          suppose
A19:        x = [d,d];
            not [d,d] in IG by NECKLACE:def 6;
            hence thesis by A10,A19,XBOOLE_0:def 4;
          end;
        end;
      end;
    end;
    let x be set;
    assume
A20: x in {[a,b],[b,a],[b,c],[c,b],[c,d],[d,c]};
    per cases by A20,ENUMSET1:def 4;
    suppose
A21:  x = [a,b];
      a in Z & b in Z by A1,ENUMSET1:def 2;
      then a in cH & b in cH by YELLOW_0:def 15;
      then [a,b] in [:cH,cH:] by ZFMISC_1:106;
      then x in IG |_2 cH by A3,A21,XBOOLE_0:def 4;
      hence thesis by YELLOW_0:def 14;
    end;
    suppose
A22:  x = [b,a];
      IG is_symmetric_in the carrier of G by NECKLACE:def 4;
      then
A23:  [b,a] in IG by A3,RELAT_2:def 3;
      b in Z & a in Z by A1,ENUMSET1:def 2;
      then b in cH & a in cH by YELLOW_0:def 15;
      then [b,a] in [:cH,cH:] by ZFMISC_1:106;
      then x in IG |_2 cH by A22,A23,XBOOLE_0:def 4;
      hence thesis by YELLOW_0:def 14;
    end;
    suppose
A24:  x = [b,c];
      b in Z & c in Z by A1,ENUMSET1:def 2;
      then b in cH & c in cH by YELLOW_0:def 15;
      then [b,c] in [:cH,cH:] by ZFMISC_1:106;
      then x in IG |_2 cH by A4,A24,XBOOLE_0:def 4;
      hence thesis by YELLOW_0:def 14;
    end;
    suppose
A25:  x = [c,b];
      IG is_symmetric_in the carrier of G by NECKLACE:def 4;
      then
A26:  [c,b] in IG by A4,RELAT_2:def 3;
      b in Z & c in Z by A1,ENUMSET1:def 2;
      then b in cH & c in cH by YELLOW_0:def 15;
      then [c,b] in [:cH,cH:] by ZFMISC_1:106;
      then x in IG |_2 cH by A25,A26,XBOOLE_0:def 4;
      hence thesis by YELLOW_0:def 14;
    end;
    suppose
A27:  x = [c,d];
      c in Z & d in Z by A1,ENUMSET1:def 2;
      then c in cH & d in cH by YELLOW_0:def 15;
      then [c,d] in [:cH,cH:] by ZFMISC_1:106;
      then x in IG |_2 cH by A5,A27,XBOOLE_0:def 4;
      hence thesis by YELLOW_0:def 14;
    end;
    suppose
A28:  x = [d,c];
      IG is_symmetric_in the carrier of G by NECKLACE:def 4;
      then
A29:  [d,c] in IG by A5,RELAT_2:def 3;
      c in Z & d in Z by A1,ENUMSET1:def 2;
      then c in cH & d in cH by YELLOW_0:def 15;
      then [d,c] in [:cH,cH:] by ZFMISC_1:106;
      then x in IG |_2 cH by A28,A29,XBOOLE_0:def 4;
      hence thesis by YELLOW_0:def 14;
    end;
  end;
A30: a <> b & a <> c & a <> d & b <> c & b <> d & c <> d by A2,ZFMISC_1:def 6;
  set g = (0,1) --> (a,b), h = (2,3) --> (c,d), f = g +* h;
  dom f = dom g \/ dom h by FUNCT_4:def 1
    .= {0,1} \/ dom h by FUNCT_4:65
    .= {0,1} \/ {2,3} by FUNCT_4:65
    .= {0,1,2,3} by ENUMSET1:45;
  then
A31: dom f = the carrier of N4 by NECKLACE:2,21;
A32: rng g = {a,b} by FUNCT_4:67;
A33: rng h = {c,d} by FUNCT_4:67;
A34: the carrier of H is non empty by A1,YELLOW_0:def 15;
A35: dom g misses dom h
  proof
    assume not thesis;
    then consider x being set such that
A36: x in dom g & x in dom h by XBOOLE_0:3;
    (x=0 or x=1) & (x=2 or x=3) by A36,TARSKI:def 2;
    hence contradiction;
  end;
A37: rng f = the carrier of H
  proof
    rng f = rng g \/ rng h by A35,NECKLACE:7;
    then rng f = {a,b,c,d} by A32,A33,ENUMSET1:45;
    hence thesis by A1,YELLOW_0:def 15;
  end;
  then reconsider f as Function of N4,H by A31,FUNCT_2:def 1,RELSET_1:11;
A38: g is one-to-one
  proof
A39: g = (0 .--> a) +* (1 .-->b) by FUNCT_4:def 4;
    rng (0 .--> a) misses rng (1 .--> b)
    proof
      assume rng (0 .--> a) meets rng (1 .--> b);
      then consider x being set such that
A40:  x in rng (0 .--> a) & x in rng (1 .--> b) by XBOOLE_0:3;
A41:  rng (0 .--> a) = {a} by FUNCOP_1:14;
A42:  rng (1 .--> b) = {b} by FUNCOP_1:14;
      x = a by A40,A41,TARSKI:def 1;
      hence contradiction by A30,A40,A42,TARSKI:def 1;
    end;
    hence thesis by A39,FUNCT_4:98;
  end;
A43: h is one-to-one
  proof
A44: h = (2 .--> c) +* (3 .--> d) by FUNCT_4:def 4;
    rng (2 .--> c) misses rng (3 .--> d)
    proof
      assume rng (2 .--> c) meets rng (3 .--> d);
      then consider x being set such that
A45:  x in rng (2 .--> c) & x in rng (3 .--> d) by XBOOLE_0:3;
A46:  rng (2 .--> c) = {c} by FUNCOP_1:14;
A47:  rng (3 .--> d) = {d} by FUNCOP_1:14;
      x = c by A45,A46,TARSKI:def 1;
      hence contradiction by A30,A45,A47,TARSKI:def 1;
    end;
    hence thesis by A44,FUNCT_4:98;
  end;
A48: rng g misses rng h
  proof
    assume not thesis;
    then consider x being set such that
A49: x in rng g & x in rng h by XBOOLE_0:3;
    (x = a or x = b) & (x = c or x = d) by A32,A33,A49,TARSKI:def 2;
    hence contradiction by A2,ZFMISC_1:def 6;
  end;
  then
A50: f is one-to-one by A38,A43,FUNCT_4:98;
  for x,y being Element of N4 holds
  [x,y] in the InternalRel of N4 iff [f.x,f.y] in the InternalRel of H
  proof
    let x,y being Element of N4;
    thus [x,y] in the InternalRel of N4 implies
    [f.x,f.y] in the InternalRel of H
    proof
      assume
A51:  [x,y] in the InternalRel of N4;
      per cases by A51,ENUMSET1:def 4,NECKLA_2:2;
      suppose [x,y] = [0,1];
        then
A52:    x = 0 & y = 1 by ZFMISC_1:33;
        then x in {0,1} & y in {0,1} by TARSKI:def 2;
        then
A53:    x in dom g & y in dom g by FUNCT_4:65;
        then
A54:    f.x = g.0 by A35,A52,FUNCT_4:17
          .= a by FUNCT_4:66;
        f.y = g.1 by A35,A52,A53,FUNCT_4:17
          .= b by FUNCT_4:66;
        hence thesis by A9,A54,ENUMSET1:def 4;
      end;
      suppose [x,y] = [1,0];
        then
A55:    x = 1 & y = 0 by ZFMISC_1:33;
        then x in {0,1} & y in {0,1} by TARSKI:def 2;
        then
A56:    x in dom g & y in dom g by FUNCT_4:65;
        then
A57:    f.x = g.1 by A35,A55,FUNCT_4:17
          .= b by FUNCT_4:66;
        f.y = g.0 by A35,A55,A56,FUNCT_4:17
          .= a by FUNCT_4:66;
        hence thesis by A9,A57,ENUMSET1:def 4;
      end;
      suppose [x,y] = [1,2];
        then
A58:    x = 1 & y = 2 by ZFMISC_1:33;
        then x in {0,1} & y in {2,3} by TARSKI:def 2;
        then
A59:    x in dom g & y in dom h by FUNCT_4:65;
        then
A60:    f.x = g.1 by A35,A58,FUNCT_4:17
          .= b by FUNCT_4:66;
        g +* h = h +* g by A35,FUNCT_4:36;
        then f.y = h.2 by A35,A58,A59,FUNCT_4:17
          .= c by FUNCT_4:66;
        hence thesis by A9,A60,ENUMSET1:def 4;
      end;
      suppose [x,y] = [2,1];
        then
A61:    x = 2 & y = 1 by ZFMISC_1:33;
        then x in {2,3} & y in {0,1} by TARSKI:def 2;
        then
A62:    x in dom h & y in dom g by FUNCT_4:65;
        then
A63:    f.y = g.1 by A35,A61,FUNCT_4:17
          .= b by FUNCT_4:66;
        g +* h = h +* g by A35,FUNCT_4:36;
        then f.x = h.2 by A35,A61,A62,FUNCT_4:17
          .= c by FUNCT_4:66;
        hence thesis by A9,A63,ENUMSET1:def 4;
      end;
      suppose [x,y] = [2,3];
        then
A64:    x = 2 & y = 3 by ZFMISC_1:33;
        then x in {2,3} & y in {2,3} by TARSKI:def 2;
        then
A65:    x in dom h & y in dom h by FUNCT_4:65;
A66:    g +* h = h +* g by A35,FUNCT_4:36;
        then
A67:    f.x = h.2 by A35,A64,A65,FUNCT_4:17
          .= c by FUNCT_4:66;
        f.y = h.3 by A35,A64,A65,A66,FUNCT_4:17
          .= d by FUNCT_4:66;
        hence thesis by A9,A67,ENUMSET1:def 4;
      end;
      suppose [x,y] = [3,2];
        then
A68:    x = 3 & y = 2 by ZFMISC_1:33;
        then x in {3,2} & y in {3,2} by TARSKI:def 2;
        then
A69:    x in dom h & y in dom h by FUNCT_4:65;
A70:    g +* h = h +* g by A35,FUNCT_4:36;
        then
A71:    f.x = h.3 by A35,A68,A69,FUNCT_4:17
          .= d by FUNCT_4:66;
        f.y = h.2 by A35,A68,A69,A70,FUNCT_4:17
          .= c by FUNCT_4:66;
        hence thesis by A9,A71,ENUMSET1:def 4;
      end;
    end;
    thus [f.x,f.y] in the InternalRel of H implies
    [x,y] in the InternalRel of N4
    proof
      assume
A72:  [f.x,f.y] in the InternalRel of H;
A73:  dom g = {0,1} & rng g = {a,b} by FUNCT_4:65,67;

then reconsider g as Function of {0,1},{a,b} by RELSET_1:11;
      reconsider G = g" as Function of {a,b},{0,1} by A32,A38,FUNCT_2:31;
A74:  dom h = {2,3} & rng h = {c,d} by FUNCT_4:65,67;

then reconsider h as Function of {2,3},{c,d} by RELSET_1:11;
      reconsider Hh = h" as Function of {c,d},{2,3} by A33,A43,FUNCT_2:31;
      reconsider F=f" as Function of the carrier of H,the carrier of N4
      by A37,A50,FUNCT_2:31;
A75:  dom G = {a,b} & rng G = {0,1} by A38,A73,FUNCT_1:55;
A76:  dom Hh = {c,d} & rng Hh = {2,3} by A43,A74,FUNCT_1:55;
      then G +* Hh = Hh +* G by A32,A33,A48,A75,FUNCT_4:36;
      then
A77:  F = G +* Hh & F = Hh +* G by A35,A38,A43,A48,NECKLACE:8;
A78:  G = (a,b) --> (0,1) & Hh = (c,d) --> (2,3) by A30,NECKLACE:11;
      per cases by A9,A72,ENUMSET1:def 4;
      suppose [f.x,f.y] = [a,b];
        then
A79:    f.x = a & f.y = b by ZFMISC_1:33;
        then
A80:    f.x in {a,b} & f.y in {a,b} by TARSKI:def 2;
A81:    f.x in dom G & f.y in dom G by A75,A79,TARSKI:def 2;
        F.(f.x) = G.a by A32,A33,A48,A75,A76,A77,A79,A80,FUNCT_4:17
          .= 0 by A30,A78,FUNCT_4:66;
        then
A82:    x = 0 by A34,A50,FUNCT_2:32;
A83:    F.(f.y) = G.b by A32,A33,A48,A75,A76,A77,A79,A81,FUNCT_4:17
          .= 1 by A78,FUNCT_4:66;
        F.(f.y) = y by A34,A50,FUNCT_2:32;
        hence thesis by A82,A83,ENUMSET1:def 4,NECKLA_2:2;
      end;
      suppose [f.x,f.y] = [b,a];
        then
A84:    f.x = b & f.y = a by ZFMISC_1:33;
        then
A85:    f.x in {a,b} & f.y in {a,b} by TARSKI:def 2;
A86:    f.x in dom G & f.y in dom G by A75,A84,TARSKI:def 2;
        F.(f.y) = G.a by A32,A33,A48,A75,A76,A77,A84,A85,FUNCT_4:17
          .= 0 by A30,A78,FUNCT_4:66;
        then
A87:    y = 0 by A34,A50,FUNCT_2:32;
A88:    F.(f.x) = G.b by A32,A33,A48,A75,A76,A77,A84,A86,FUNCT_4:17
          .= 1 by A78,FUNCT_4:66;
        F.(f.x) = x by A34,A50,FUNCT_2:32;
        hence thesis by A87,A88,ENUMSET1:def 4,NECKLA_2:2;
      end;
      suppose [f.x,f.y] = [b,c];
        then
A89:    f.x = b & f.y = c by ZFMISC_1:33;
        then
A90:    f.x in dom G & f.y in dom Hh by A75,A76,TARSKI:def 2;
        then F.(f.x) = G.b by A32,A33,A48,A75,A76,A77,A89,FUNCT_4:17
          .= 1 by A78,FUNCT_4:66;
        then
A91:    x = 1 by A34,A50,FUNCT_2:32;
A92:    F.(f.y) = Hh.c by A32,A33,A48,A75,A76,A77,A89,A90,FUNCT_4:17
          .= 2 by A30,A78,FUNCT_4:66;
        F.(f.y) = y by A34,A50,FUNCT_2:32;
        hence thesis by A91,A92,ENUMSET1:def 4,NECKLA_2:2;
      end;
      suppose [f.x,f.y] = [c,b];
        then
A93:    f.x = c & f.y = b by ZFMISC_1:33;
        then
A94:    f.x in dom Hh & f.y in dom G by A75,A76,TARSKI:def 2;
        then F.(f.y) = G.b by A32,A33,A48,A75,A76,A77,A93,FUNCT_4:17
          .= 1 by A78,FUNCT_4:66;
        then
A95:    y = 1 by A34,A50,FUNCT_2:32;
A96:    F.(f.x) = Hh.c by A32,A33,A48,A75,A76,A77,A93,A94,FUNCT_4:17
          .= 2 by A30,A78,FUNCT_4:66;
        F.(f.x) = x by A34,A50,FUNCT_2:32;
        hence thesis by A95,A96,ENUMSET1:def 4,NECKLA_2:2;
      end;
      suppose [f.x,f.y] = [c,d];
        then
A97:    f.x = c & f.y = d by ZFMISC_1:33;
        then
A98:    f.x in {c,d} & f.y in {c,d} by TARSKI:def 2;
A99:    f.x in dom Hh & f.y in dom Hh by A76,A97,TARSKI:def 2;
        F.(f.x) = Hh.c by A32,A33,A48,A75,A76,A77,A97,A98,FUNCT_4:17
          .= 2 by A30,A78,FUNCT_4:66;
        then
A100:   x = 2 by A34,A50,FUNCT_2:32;
A101:   F.(f.y) = Hh.d by A32,A33,A48,A75,A76,A77,A97,A99,FUNCT_4:17
          .= 3 by A78,FUNCT_4:66;
        F.(f.y) = y by A34,A50,FUNCT_2:32;
        hence thesis by A100,A101,ENUMSET1:def 4,NECKLA_2:2;
      end;
      suppose [f.x,f.y] = [d,c];
        then
A102:   f.x = d & f.y = c by ZFMISC_1:33;
        then
A103:   f.x in {c,d} & f.y in {c,d} by TARSKI:def 2;
A104:   f.x in dom Hh & f.y in dom Hh by A76,A102,TARSKI:def 2;
        F.(f.y) = Hh.c by A32,A33,A48,A75,A76,A77,A102,A103,FUNCT_4:17
          .= 2 by A30,A78,FUNCT_4:66;
        then
A105:   y = 2 by A34,A50,FUNCT_2:32;
A106:   F.(f.x) = Hh.d by A32,A33,A48,A75,A76,A77,A102,A104,FUNCT_4:17
          .= 3 by A78,FUNCT_4:66;
        F.(f.x) = x by A34,A50,FUNCT_2:32;
        hence thesis by A105,A106,ENUMSET1:def 4,NECKLA_2:2;
      end;
    end;
  end;
  hence thesis by A50,NECKLACE:def 2;
end;

theorem Th40:
  for G being non empty irreflexive symmetric RelStr, x being Element of G,
  R1,R2 being non empty RelStr st the carrier of R1 misses the carrier of R2
  & subrelstr ([#]G \ {x}) = union_of(R1,R2) & G is non trivial
  & G is path-connected & ComplRelStr G is path-connected
  holds G embeds Necklace 4
proof
  let G be non empty irreflexive symmetric RelStr, x be Element of G,
  R1,R2 be non empty RelStr;
  assume that
A1: the carrier of R1 misses the carrier of R2 and
A2: subrelstr ([#]G \ {x}) = union_of(R1,R2) and
A3: G is non trivial & G is path-connected & ComplRelStr G is path-connected;
  set A = (the carrier of G) \ {x}, X = {x};
  reconsider X as Subset of G;
  set H = subrelstr X;
  reconsider A as Subset of G;
  set R = subrelstr A;
  R is non empty by A3,YELLOW_0:def 15;
  then reconsider R as non empty irreflexive symmetric RelStr;
A4: the carrier of G = (the carrier of R) \/ {x}
  proof
    thus the carrier of G c= (the carrier of R) \/ {x}
    proof
      let a be set;
      assume
A5:   a in the carrier of G;
      per cases;
      suppose a = x;
        then a in {x} by TARSKI:def 1;
        hence thesis by XBOOLE_0:def 3;
      end;
      suppose a <> x;
        then not a in {x} by TARSKI:def 1;
        then a in (the carrier of G) \ {x} by A5,XBOOLE_0:def 5;
        then a in the carrier of R by YELLOW_0:def 15;
        hence thesis by XBOOLE_0:def 3;
      end;
    end;
    let a be set;
    assume
A6: a in (the carrier of R) \/ {x};
    per cases by A6,XBOOLE_0:def 3;
    suppose a in the carrier of R;
      then a in (the carrier of G) \ {x} by YELLOW_0:def 15;
      hence thesis;
    end;
    suppose a in {x};
      hence thesis;
    end;
  end;
A7: (the carrier of H) misses the carrier of R
  proof
    assume not thesis;
    then (the carrier of H) /\ (the carrier of R) <> {} by XBOOLE_0:def 7;
    then X /\ (the carrier of R) <> {} by YELLOW_0:def 15;
    then X /\ A <> {} by YELLOW_0:def 15;
    then consider a being set such that
A8: a in X /\ A by XBOOLE_0:def 1;
    a in X & a in A by A8,XBOOLE_0:def 4;
    hence contradiction by XBOOLE_0:def 5;
  end;
  H is non empty by YELLOW_0:def 15;
  then reconsider H as non empty irreflexive symmetric RelStr;
  reconsider X1 = {y where y is Element of R1:
  [y,x] in (the InternalRel of G)}, Y1 = {y where y is Element of R1:
  not [y,x] in the InternalRel of G}, X2 = {y where y is Element of R2:
  [y,x] in (the InternalRel of G)}, Y2 = {y where y is Element of R2:
  not [y,x] in the InternalRel of G} as set;
A9: X1 misses Y1
  proof
    assume not thesis;
    then consider a being set such that
A10: a in X1 & a in Y1 by XBOOLE_0:3;
    consider y1 being Element of R1 such that
A11: y1 = a & [y1,x] in the InternalRel of G by A10;
    consider y2 being Element of R1 such that
A12: y2 = a & not [y2,x] in the InternalRel of G by A10;
    thus contradiction by A11,A12;
  end;
A13: X2 misses Y2
  proof
    assume not thesis;
    then consider a being set such that
A14: a in X2 & a in Y2 by XBOOLE_0:3;
    consider y1 being Element of R2 such that
A15: y1 = a & [y1,x] in the InternalRel of G by A14;
    consider y2 being Element of R2 such that
A16: y2 = a & not [y2,x] in the InternalRel of G by A14;
    thus contradiction by A15,A16;
  end;
A17: R = subrelstr ([#]G \ {x});
A18: the carrier of R1 = X1 \/ Y1
  proof
    thus the carrier of R1 c= X1 \/ Y1
    proof
      let a be set;
      assume
A19:  a in the carrier of R1;
      per cases;
      suppose [a,x] in the InternalRel of G;
        then a in X1 by A19;
        hence thesis by XBOOLE_0:def 3;
      end;
      suppose not [a,x] in the InternalRel of G;
        then a in Y1 by A19;
        hence thesis by XBOOLE_0:def 3;
      end;
    end;
    let a be set such that
A20: a in X1 \/ Y1;
    per cases by A20,XBOOLE_0:def 3;
    suppose a in X1;
      then consider y being Element of R1 such that
A21:  a = y & [y,x] in (the InternalRel of G);
      thus thesis by A21;
    end;
    suppose a in Y1;
      then consider y being Element of R1 such that
A22:  a = y & not [y,x] in the InternalRel of G;
      thus thesis by A22;
    end;
  end;
A23: the carrier of R2 = X2 \/ Y2
  proof
    thus the carrier of R2 c= X2 \/ Y2
    proof
      let a be set;
      assume
A24:  a in the carrier of R2;
      per cases;
      suppose [a,x] in the InternalRel of G;
        then a in X2 by A24;
        hence thesis by XBOOLE_0:def 3;
      end;
      suppose not [a,x] in the InternalRel of G;
        then a in Y2 by A24;
        hence thesis by XBOOLE_0:def 3;
      end;
    end;
    let a be set such that
A25: a in X2 \/ Y2;
    per cases by A25,XBOOLE_0:def 3;
    suppose a in X2;
      then consider y being Element of R2 such that
A26:  a = y & [y,x] in (the InternalRel of G);
      thus thesis by A26;
    end;
    suppose a in Y2;
      then consider y being Element of R2 such that
A27:  a = y & not [y,x] in the InternalRel of G;
      thus thesis by A27;
    end;
  end;
A28: R = union_of(R2,R1) by A2,Th8;
  consider a being Element of R1 such that
A29: [a,x] in the InternalRel of G by A1,A2,A3,Th38;
A30: a in X1 by A29;
  consider b being Element of R2 such that
A31: [b,x] in the InternalRel of G by A1,A3,A17,A28,Th38;
A32: b in X2 by A31;
A33: Y1 \/ Y2 is non empty
  proof
    assume not thesis;
    then
A34: Y1 is empty & Y2 is empty;
A35: for a being Element of R holds [a,x] in the InternalRel of G
    proof
      let a be Element of R;
A36:  the carrier of R = (the carrier of R1) \/ the carrier of R2
      by A2,NECKLA_2:def 2;
      per cases by A36,XBOOLE_0:def 3;
      suppose a in the carrier of R1;
        then consider y being Element of R1 such that
A37:    a = y & [y,x] in the InternalRel of G by A18,A34;
        thus thesis by A37;
      end;
      suppose a in the carrier of R2;
        then consider y being Element of R2 such that
A38:    a = y & [y,x] in the InternalRel of G by A23,A34;
        thus thesis by A38;
      end;
    end;
    not ComplRelStr G is path-connected
    proof
      assume
A39:  not thesis;
A40:  a <> x
      proof
        assume not thesis;
        then x in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 3;
        then
A41:    x in the carrier of R by A2,NECKLA_2:def 2;
        x in {x} by TARSKI:def 1;
        then x in the carrier of H by YELLOW_0:def 15;
        then x in (the carrier of R) /\ the carrier of H by A41,XBOOLE_0:def 4;
        hence contradiction by A7,XBOOLE_0:def 7;
      end;
      a is Element of ComplRelStr G
      proof
        a in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 3;
        then
A42:    a in the carrier of R by A2,NECKLA_2:def 2;
        the carrier of R c= the carrier of G by A4,XBOOLE_1:7;
        hence thesis by A42,NECKLACE:def 9;
      end;
      then a is Element of ComplRelStr G &
      x is Element of ComplRelStr G by NECKLACE:def 9;
      then (the InternalRel of ComplRelStr G) reduces x,a by A39,A40,Def2;
      then consider p being FinSequence such that
A43:  len p > 0 & p.1 = x & p.len p = a and
A44:  for i being Element of NAT st i in dom p & i+1 in dom p holds
      [p.i, p.(i+1)] in (the InternalRel of ComplRelStr G) by REWRITE1:12;
A45:  0 + 1 <= len p by A43,NAT_1:13;
      then len p > 1 by A40,A43,XXREAL_0:1;
      then 1+1 <= len p by NAT_1:13;
      then 1 in dom p & 2 in dom p by A45,FINSEQ_3:27;
      then
A46:  [p.1,p.(1+1)] in (the InternalRel of ComplRelStr G) by A44;
      then p.2 in the carrier of ComplRelStr G by ZFMISC_1:106;
      then
A47:  p.2 in the carrier of G by NECKLACE:def 9;
A48:  p.2 <> x
      proof
        assume
A49:    not thesis;
        [x,x] in id the carrier of G by RELAT_1:def 10;
        then [x,x] in (the InternalRel of ComplRelStr G) /\ id the carrier
        of G by A43,A46,A49,XBOOLE_0:def 4;
        then (the InternalRel of ComplRelStr G) meets id the carrier of G
        by XBOOLE_0:def 7;
        hence contradiction by Th13;
      end;
A50:  p.2 in the carrier of R
      proof
        assume not thesis;
        then p.2 in {x} by A4,A47,XBOOLE_0:def 3;
        hence thesis by A48,TARSKI:def 1;
      end;
A51:  p.1 in the carrier of ComplRelStr G &
      p.(1+1) in the carrier of ComplRelStr G by A46,ZFMISC_1:106;
      the InternalRel of ComplRelStr G is_symmetric_in
      the carrier of ComplRelStr G by NECKLACE:def 4;
      then
A52:  [p.(1+1),p.1] in the InternalRel of ComplRelStr G
      by A46,A51,RELAT_2:def 3;
      [p.2,x] in the InternalRel of G by A35,A50;
      then [p.2,x] in (the InternalRel of ComplRelStr G) /\
      the InternalRel of G by A43,A52,XBOOLE_0:def 4;
      then (the InternalRel of ComplRelStr G) meets the InternalRel of G
      by XBOOLE_0:def 7;
      hence thesis by Th12;
    end;
    hence thesis by A3;
  end;
  thus thesis
  proof
    per cases by A33;
    suppose
A53:  Y1 is non empty;
      ex b being Element of Y1, c being Element of X1
      st [b,c] in the InternalRel of G
      proof
        consider b being Element of Y1;
        a in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 3;
        then
A54:    a in the carrier of R by A2,NECKLA_2:def 2;
A55:    the carrier of R c= the carrier of G by A4,XBOOLE_1:7;
        then reconsider a as Element of G by A54;
        b in Y1 by A53;
        then consider y being Element of R1 such that
A56:    y = b & not [y,x] in the InternalRel of G;
        b in (the carrier of R1) \/ the carrier of R2 by A56,XBOOLE_0:def 3;
        then b in the carrier of R by A2,NECKLA_2:def 2;
        then reconsider b as Element of G by A55;
        a <> b
        proof
          assume not thesis;
          then a in X1 & a in Y1 by A29,A53;
          then a in X1 /\ Y1 by XBOOLE_0:def 4;
          hence contradiction by A9,XBOOLE_0:def 7;
        end;
        then (the InternalRel of G) reduces a,b by A3,Def2;
        then consider p being FinSequence such that
A57:    len p > 0 & p.1 = a & p.len p = b and
A58:    for i being Element of NAT st i in dom p & i+1 in dom p
        holds [p.i, p.(i+1)] in (the InternalRel of G) by REWRITE1:12;
        defpred P[Nat] means p.$1 in Y1 & $1 in dom p &
        for k being Nat st k > $1 holds k in dom p implies p.k in Y1;
A59:    ex k being Nat st P[k]
        proof
          P[len p]
          proof
A60:        len p in dom p by A57,CARD_1:47,FINSEQ_5:6;
            for k being Nat st k > len p holds k in dom p implies p.k in Y1
            proof
              let k be Nat such that
A61:          k > len p;
              assume k in dom p;
              then k in Seg (len p) by FINSEQ_1:def 3;
              hence thesis by A61,FINSEQ_1:3;
            end;
            hence thesis by A53,A57,A60;
          end;
          hence thesis;
        end;
        ex n0 being Nat st P[n0] &
        for n being Nat st P[n] holds n >= n0 from NAT_1:sch 5(A59);
        then consider n0 being Nat such that
A62:    P[n0] and
A63:    for n being Nat st P[n] holds n >= n0;
        n0 <> 0
        proof
          assume not thesis;
          then 0 in Seg (len p) by A62,FINSEQ_1:def 3;
          hence contradiction by FINSEQ_1:3;
        end;
        then consider k0 being Nat such that
A64:    n0 = k0 + 1 by NAT_1:6;
A65:    n0 <> 1
        proof
          assume not thesis;
          then a in X1 & a in Y1 by A29,A57,A62;
          then X1 /\ Y1 is non empty by XBOOLE_0:def 4;
          hence contradiction by A9,XBOOLE_0:def 7;
        end;
A66:    k0 in dom p
        proof
A67:      k0 >= 1
          proof
            assume not thesis;
            then k0 = 0 by NAT_1:26;
            hence contradiction by A64,A65;
          end;
          k0 <= len p
          proof
A68:        k0 <= k0 + 1 by XREAL_1:31;
            n0 in Seg (len p) by A62,FINSEQ_1:def 3;
            then n0 <= len p by FINSEQ_1:3;
            hence thesis by A64,A68,XXREAL_0:2;
          end;
          hence thesis by A67,FINSEQ_3:27;
        end;
        k0 < n0 by A64,NAT_1:13;
        then
A69:    not P[k0] by A63;
A70:    for k being Nat st k > k0 holds k in dom p implies p.k in Y1
        proof
          assume not thesis;
          then consider k being Nat such that
A71:      k > k0 & k in dom p & not p.k in Y1;
          k > n0
          proof
            per cases by XXREAL_0:1;
            suppose k < n0;
              hence thesis by A64,A71,NAT_1:13;
            end;
            suppose n0 < k;
              hence thesis;
            end;
            suppose n0 = k;
              hence thesis by A62,A71;
            end;
          end;
          hence contradiction by A62,A71;
        end;
A72:    [p.k0,p.(k0+1)] in the InternalRel of G by A58,A62,A64,A66;
A73:    [p.n0,p.k0] in the InternalRel of G
        proof
A74:      the InternalRel of G is_symmetric_in the carrier of G
          by NECKLACE:def 4;
          p.k0 in the carrier of G & p.n0 in the carrier of G by A64,A72,
ZFMISC_1:106;
          hence thesis by A64,A72,A74,RELAT_2:def 3;
        end;
        p.k0 in the carrier of G by A72,ZFMISC_1:106;
        then p.k0 in the carrier of R or p.k0 in {x} by A4,XBOOLE_0:def 3;
        then
A75:    p.k0 in (the carrier of R1) \/ the carrier of R2 or p.k0 in {x}
        by A2,NECKLA_2:def 2;
        thus thesis
        proof
          per cases by A64,A72,A75,XBOOLE_0:def 3,ZFMISC_1:106;
          suppose
A76:        p.k0 in the carrier of R1 & p.n0 in the carrier of G;

then reconsider m=p.k0 as Element of X1 by A18,A66,A69,A70,XBOOLE_0:def 3;
            reconsider l=p.n0 as Element of Y1 by A62;
A77:        [m,l] in the InternalRel of G by A58,A62,A64,A66;
A78:        the InternalRel of G is_symmetric_in the carrier of G
            by NECKLACE:def 4;
            l in the carrier of R1 by A18,A62,XBOOLE_0:def 3;

then l in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 3;
            then
A79:        l in the carrier of R by A2,NECKLA_2:def 2;

m in (the carrier of R1) \/ the carrier of R2 by A76,XBOOLE_0:def 3;
            then
A80:        m in the carrier of R by A2,NECKLA_2:def 2;
            the carrier of R c= the carrier of G by A4,XBOOLE_1:7;

then [l,m] in the InternalRel of G by A77,A78,A79,A80,RELAT_2:def 3;
            hence thesis;
          end;
          suppose p.k0 in the carrier of R2 & p.n0 in the carrier of G;
            then reconsider m=p.k0 as Element of R2;
            reconsider l=p.n0 as Element of R1 by A18,A62,XBOOLE_0:def 3;
            l in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 3;
            then
A81:        l in the carrier of R by A2,NECKLA_2:def 2;
            m in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 3;
            then m in the carrier of R by A2,NECKLA_2:def 2;
            then [l,m] in [:the carrier of R,the carrier of R:]
            by A81,ZFMISC_1:106;
            then [l,m] in (the InternalRel of G)|_2 the carrier of R
            by A73,XBOOLE_0:def 4;
            then [l,m] in the InternalRel of R by YELLOW_0:def 14;
            hence thesis by A1,A2,Th36;
          end;
          suppose
A82:        p.k0 in {x} & p.n0 in the carrier of G;
            consider y1 being Element of R1 such that
A83:        p.n0 = y1 & not [y1,x] in the InternalRel of G by A62;
            thus thesis by A73,A82,A83,TARSKI:def 1;
          end;
        end;
      end;
      then consider u being Element of Y1, v being Element of X1 such that
A84:  [u,v] in the InternalRel of G;
      consider w being Element of X2;
      set Z = {u,v,x,w};
      Z c= the carrier of G
      proof
        let q be set such that
A85:    q in Z;
A86:    q = u or q = v or q = x or q = w by A85,ENUMSET1:def 2;
        u in the carrier of R1 by A18,A53,XBOOLE_0:def 3;
        then u in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 3;
        then
A87:    u in the carrier of R by A2,NECKLA_2:def 2;
A88:    the carrier of R c= the carrier of G by A4,XBOOLE_1:7;
        v in X1 by A30;
        then consider y1 being Element of R1 such that
A89:    y1 = v & [y1,x] in the InternalRel of G;
        v in (the carrier of R1) \/ the carrier of R2 by A89,XBOOLE_0:def 3;
        then
A90:    v in the carrier of R by A2,NECKLA_2:def 2;
        w in X2 by A32;
        then consider y2 being Element of R2 such that
A91:    y2 = w & [y2,x] in the InternalRel of G;
        w in (the carrier of R1) \/ the carrier of R2 by A91,XBOOLE_0:def 3;
        then w in the carrier of R by A2,NECKLA_2:def 2;
        hence thesis by A86,A87,A88,A90;
      end;
      then reconsider Z as Subset of G;
A92:  u in Y1 by A53;
A93:  v in X1 by A30;
A94:  w in X2 by A32;
      reconsider u,v as Element of G by A84,ZFMISC_1:106;
      w in X2 by A32;
      then consider y being Element of R2 such that
A95:  y = w & [y,x] in the InternalRel of G;
      reconsider w as Element of G by A95,ZFMISC_1:106;
      u <> v & v <> x & x <> u & w <> u & w <> v & w <> x
      proof
A96:    u <> v
        proof
          assume
A97:      not thesis;
          consider y1 being Element of R1 such that
A98:      u = y1 & not [y1,x] in the InternalRel of G by A92;
          consider y2 being Element of R1 such that
A99:      v = y2 & [y2,x] in the InternalRel of G by A93;
          thus contradiction by A97,A98,A99;
        end;
A100:   v <> x
        proof
          assume
A101:     not thesis;
          consider y1 being Element of R1 such that
A102:     v = y1 & [y1,x] in the InternalRel of G by A93;
          x in (the carrier of R1) \/ the carrier of R2 by A101,A102,
XBOOLE_0:def 3;
          then x in the carrier of R by A2,NECKLA_2:def 2;
          then x in (the carrier of G) \ {x} by YELLOW_0:def 15;
          then not x in {x} by XBOOLE_0:def 5;
          hence contradiction by TARSKI:def 1;
        end;
A103:   x <> u
        proof
          assume
A104:     not thesis;
          consider y1 being Element of R1 such that
A105:     u = y1 & not [y1,x] in the InternalRel of G by A92;
          x in (the carrier of R1) \/ (the carrier of R2) by A104,A105,
XBOOLE_0:def 3;
          then x in the carrier of R by A2,NECKLA_2:def 2;
          then x in (the carrier of G) \ {x} by YELLOW_0:def 15;
          then not x in {x} by XBOOLE_0:def 5;
          hence contradiction by TARSKI:def 1;
        end;
A106:   w <> u
        proof
          assume
A107:     not thesis;
          consider y1 being Element of R2 such that
A108:     w = y1 & [y1,x] in the InternalRel of G by A94;
          consider y2 being Element of R1 such that
A109:     u = y2 & not [y2,x] in the InternalRel of G by A92;
          thus contradiction by A107,A108,A109;
        end;
A110:   w <> v
        proof
          assume
A111:     not thesis;
          consider y1 being Element of R2 such that
A112:     w = y1 & [y1,x] in the InternalRel of G by A94;
          consider y2 being Element of R1 such that
A113:     v = y2 & [y2,x] in the InternalRel of G by A93;
          y1 in (the carrier of R1) /\ (the carrier of R2)
          by A111,A112,A113,XBOOLE_0:def 4;
          hence contradiction by A1,XBOOLE_0:def 7;
        end;
        w <> x
        proof
          assume
A114:     not thesis;
          consider y1 being Element of R2 such that
A115:     w = y1 & [y1,x] in the InternalRel of G by A94;
          x in (the carrier of R1) \/ the carrier of R2 by A114,A115,
XBOOLE_0:def 3;
          then x in the carrier of R by A2,NECKLA_2:def 2;
          then x in (the carrier of G) \ {x} by YELLOW_0:def 15;
          then not x in {x} by XBOOLE_0:def 5;
          hence contradiction by TARSKI:def 1;
        end;
        hence thesis by A96,A100,A103,A106,A110;
      end;
      then
A116: u,v,x,w are_mutually_different by ZFMISC_1:def 6;
A117: [v,x] in the InternalRel of G
      proof
        consider y1 being Element of R1 such that
A118:   v = y1 & [y1,x] in the InternalRel of G by A93;
        thus thesis by A118;
      end;
A119: [x,w] in the InternalRel of G
      proof
        consider y1 being Element of R2 such that
A120:   w = y1 & [y1,x] in the InternalRel of G by A94;
        the InternalRel of G is_symmetric_in the carrier of G
        by NECKLACE:def 4;
        hence thesis by A120,RELAT_2:def 3;
      end;
A121: not [u,x] in the InternalRel of G
      proof
        consider y1 being Element of R1 such that
A122:   u = y1 & not [y1,x] in the InternalRel of G by A92;
        thus thesis by A122;
      end;
A123: not [u,w] in the InternalRel of G
      proof
        assume
A124:   not thesis;
        consider y1 being Element of R1 such that
A125:   u = y1 & not [y1,x] in the InternalRel of G by A92;
        consider y2 being Element of R2 such that
A126:   w = y2 & [y2,x] in the InternalRel of G by A94;
        u in (the carrier of R1) \/ the carrier of R2 by A125,XBOOLE_0:def 3;
        then reconsider u as Element of R by A2,NECKLA_2:def 2;
        w in (the carrier of R1) \/ the carrier of R2 by A126,XBOOLE_0:def 3;
        then reconsider w as Element of R by A2,NECKLA_2:def 2;

[u,w] in (the InternalRel of G)|_2 the carrier of R by A124,XBOOLE_0:def 4;
        then [u,w] in the InternalRel of R by YELLOW_0:def 14;
        then
A127:   [u,w] in (the InternalRel of R1) \/ (the InternalRel of R2 )
        by A2,NECKLA_2:def 2;
        per cases by A127,XBOOLE_0:def 3;
        suppose [u,w] in the InternalRel of R1;
          then u in the carrier of R1 & w in the carrier of R1 by ZFMISC_1:106;

then w in (the carrier of R1) /\ the carrier of R2 by A126,XBOOLE_0:def 4;
          hence contradiction by A1,XBOOLE_0:def 7;
        end;
        suppose [u,w] in the InternalRel of R2;
          then u in the carrier of R2 & w in the carrier of R2 by ZFMISC_1:106;

then u in (the carrier of R1) /\ the carrier of R2 by A125,XBOOLE_0:def 4;
          hence contradiction by A1,XBOOLE_0:def 7;
        end;
      end;
      not [v,w] in the InternalRel of G
      proof
        assume
A128:   not thesis;
        consider y1 being Element of R1 such that
A129:   v = y1 & [y1,x] in the InternalRel of G by A93;
        consider y2 being Element of R2 such that
A130:   w = y2 & [y2,x] in the InternalRel of G by A94;
        v in (the carrier of R1) \/ the carrier of R2 by A129,XBOOLE_0:def 3;
        then reconsider v as Element of R by A2,NECKLA_2:def 2;
        w in (the carrier of R1) \/ the carrier of R2 by A130,XBOOLE_0:def 3;
        then reconsider w as Element of R by A2,NECKLA_2:def 2;

[v,w] in (the InternalRel of G)|_2 the carrier of R by A128,XBOOLE_0:def 4;
        then [v,w] in the InternalRel of R by YELLOW_0:def 14;
        then
A131:   [v,w] in (the InternalRel of R1) \/ (the InternalRel of R2 )
        by A2,NECKLA_2:def 2;
        per cases by A131,XBOOLE_0:def 3;
        suppose [v,w] in the InternalRel of R1;
          then v in the carrier of R1 & w in the carrier of R1 by ZFMISC_1:106;

then w in (the carrier of R1) /\ the carrier of R2 by A130,XBOOLE_0:def 4;
          hence contradiction by A1,XBOOLE_0:def 7;
        end;
        suppose [v,w] in the InternalRel of R2;
          then v in the carrier of R2 & w in the carrier of R2 by ZFMISC_1:106;

then v in (the carrier of R1) /\ the carrier of R2 by A129,XBOOLE_0:def 4;
          hence contradiction by A1,XBOOLE_0:def 7;
        end;
      end;
      then
A132: subrelstr Z embeds Necklace 4 by A84,A116,A117,A119,A121,A123,Th39;
      subrelstr Z is non empty by YELLOW_0:def 15;
      then reconsider H = subrelstr Z as non empty full SubRelStr of G;
      G embeds Necklace 4
      proof
        assume not thesis;
        then G is N-free by NECKLA_2:def 1;
        then H is N-free by Th23;
        hence thesis by A132,NECKLA_2:def 1;
      end;
      hence thesis;
    end;
    suppose
A133: Y2 is non empty;
      ex c being Element of Y2, d being Element of X2
      st [c,d] in the InternalRel of G
      proof
        consider c being Element of Y2;
        b in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 3;
        then
A134:   b in the carrier of R by A2,NECKLA_2:def 2;
A135:   the carrier of R c= the carrier of G by A4,XBOOLE_1:7;
        then reconsider b as Element of G by A134;
        c in Y2 by A133;
        then consider y being Element of R2 such that
A136:   y = c & not [y,x] in the InternalRel of G;
        c in (the carrier of R1) \/ the carrier of R2 by A136,XBOOLE_0:def 3;
        then c in the carrier of R by A2,NECKLA_2:def 2;
        then reconsider c as Element of G by A135;
        b <> c
        proof
          assume not thesis;
          then c in X2 & c in Y2 by A31,A133;
          then c in X2 /\ Y2 by XBOOLE_0:def 4;
          hence contradiction by A13,XBOOLE_0:def 7;
        end;
        then (the InternalRel of G) reduces b,c by A3,Def2;
        then consider p being FinSequence such that
A137:   len p > 0 & p.1 = b & p.len p = c and
A138:   for i being Element of NAT st i in dom p & i+1 in dom p
        holds [p.i, p.(i+1)] in (the InternalRel of G) by REWRITE1:12;
        defpred P[Nat] means p.$1 in Y2 & $1 in dom p &
        for k being Nat st k > $1 holds k in dom p implies p.k in Y2;
A139:   ex k being Nat st P[k]
        proof
          P[len p]
          proof
A140:       len p in dom p by A137,CARD_1:47,FINSEQ_5:6;
            for k being Nat st k > len p holds k in dom p implies p.k in Y2
            proof
              let k be Nat such that
A141:         k > len p;
              assume k in dom p;
              then k in Seg (len p) by FINSEQ_1:def 3;
              hence thesis by A141,FINSEQ_1:3;
            end;
            hence thesis by A133,A137,A140;
          end;
          hence thesis;
        end;
        ex n0 being Nat st P[n0] &
        for n being Nat st P[n] holds n >= n0 from NAT_1:sch 5(A139);
        then consider n0 being Nat such that
A142:   P[n0] and
A143:   for n being Nat st P[n] holds n >= n0;
        n0 <> 0
        proof
          assume not thesis;
          then 0 in Seg (len p) by A142,FINSEQ_1:def 3;
          hence contradiction by FINSEQ_1:3;
        end;
        then consider k0 being Nat such that
A144:   n0 = k0 + 1 by NAT_1:6;
A145:   n0 <> 1
        proof
          assume not thesis;
          then b in X2 & b in Y2 by A31,A137,A142;
          then X2 /\ Y2 is non empty by XBOOLE_0:def 4;
          hence contradiction by A13,XBOOLE_0:def 7;
        end;
A146:   k0 in dom p
        proof
A147:     k0 >= 1
          proof
            assume not thesis;
            then k0 = 0 by NAT_1:26;
            hence contradiction by A144,A145;
          end;
          k0 <= len p
          proof
A148:       k0 <= k0 + 1 by XREAL_1:31;
            n0 in Seg (len p) by A142,FINSEQ_1:def 3;
            then n0 <= len p by FINSEQ_1:3;
            hence thesis by A144,A148,XXREAL_0:2;
          end;
          then k0 in Seg (len p) by A147,FINSEQ_1:3;
          hence thesis by FINSEQ_1:def 3;
        end;
        k0 < n0 by A144,NAT_1:13;
        then
A149:   not P[k0] by A143;
A150:   for k being Nat st k > k0 holds k in dom p implies p.k in Y2
        proof
          assume not thesis;
          then consider k being Nat such that
A151:     k > k0 & k in dom p & not p.k in Y2;
          k > n0
          proof
            per cases by XXREAL_0:1;
            suppose k < n0;
              hence thesis by A144,A151,NAT_1:13;
            end;
            suppose n0 < k;
              hence thesis;
            end;
            suppose n0 = k;
              hence thesis by A142,A151;
            end;
          end;
          hence contradiction by A142,A151;
        end;
A152:   [p.k0,p.(k0+1)] in the InternalRel of G by A138,A142,A144,A146;
A153:   [p.n0,p.k0] in the InternalRel of G
        proof
A154:     the InternalRel of G is_symmetric_in the carrier of G
          by NECKLACE:def 4;
          p.k0 in the carrier of G & p.n0 in the carrier of G by A144,A152,
ZFMISC_1:106;
          hence thesis by A144,A152,A154,RELAT_2:def 3;
        end;
        p.k0 in the carrier of G by A152,ZFMISC_1:106;
        then p.k0 in the carrier of R or p.k0 in {x} by A4,XBOOLE_0:def 3;
        then
A155:   p.k0 in (the carrier of R1) \/ the carrier of R2 or p.k0 in {x}
        by A2,NECKLA_2:def 2;
        thus thesis
        proof
          per cases by A144,A152,A155,XBOOLE_0:def 3,ZFMISC_1:106;
          suppose p.k0 in the carrier of R2 & p.n0 in the carrier of G;
            then reconsider m=p.k0 as Element of X2 by A23,A146,A149,A150,
XBOOLE_0:def 3;
            reconsider l=p.n0 as Element of Y2 by A142;
            [m,l] in the InternalRel of G by A138,A142,A144,A146;
            hence thesis by A153;
          end;
          suppose p.k0 in the carrier of R1 & p.n0 in the carrier of G;
            then reconsider m=p.k0 as Element of R1;
            reconsider l=p.n0 as Element of R2 by A23,A142,XBOOLE_0:def 3;
            l in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 3;
            then
A156:       l in the carrier of R by A2,NECKLA_2:def 2;
            m in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 3;
            then
A157:       m in the carrier of R by A2,NECKLA_2:def 2;

then [l,m] in [:the carrier of R,the carrier of R:] by A156,ZFMISC_1:106;
            then [l,m] in (the InternalRel of G)|_2 the carrier of R
            by A153,XBOOLE_0:def 4;
            then
A158:       [l,m] in the InternalRel of R by YELLOW_0:def 14;
            the InternalRel of R is_symmetric_in the carrier of R by
NECKLACE:def 4;
            then [m,l] in the InternalRel of R by A156,A157,A158,RELAT_2:def 3;
            hence thesis by A1,A2,Th36;
          end;
          suppose
A159:       p.k0 in {x} & p.n0 in the carrier of G;
            consider y1 being Element of R2 such that
A160:       p.n0 = y1 & not [y1,x] in the InternalRel of G by A142;
            thus thesis by A153,A159,A160,TARSKI:def 1;
          end;
        end;
      end;
      then consider u being Element of Y2, v being Element of X2 such that
A161: [u,v] in the InternalRel of G;
      consider w being Element of X1;
      set Z = {u,v,x,w};
      Z c= the carrier of G
      proof
        let q be set such that
A162:   q in Z;
A163:   q = u or q = v or q = x or q = w by A162,ENUMSET1:def 2;
        u in the carrier of R2 by A23,A133,XBOOLE_0:def 3;
        then u in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 3;
        then
A164:   u in the carrier of R by A2,NECKLA_2:def 2;
A165:   the carrier of R c= the carrier of G by A4,XBOOLE_1:7;
        v in X2 by A32;
        then consider y1 being Element of R2 such that
A166:   y1 = v & [y1,x] in the InternalRel of G;
        v in (the carrier of R1) \/ the carrier of R2 by A166,XBOOLE_0:def 3;
        then
A167:   v in the carrier of R by A2,NECKLA_2:def 2;
        w in X1 by A30;
        then consider y2 being Element of R1 such that
A168:   y2 = w & [y2,x] in the InternalRel of G;
        w in (the carrier of R1) \/ the carrier of R2 by A168,XBOOLE_0:def 3;
        then w in the carrier of R by A2,NECKLA_2:def 2;
        hence thesis by A163,A164,A165,A167;
      end;
      then reconsider Z as Subset of G;
A169: u in Y2 by A133;
A170: v in X2 by A32;
A171: w in X1 by A30;
      reconsider u,v as Element of G by A161,ZFMISC_1:106;
      w in X1 by A30;
      then consider y being Element of R1 such that
A172: y = w & [y,x] in the InternalRel of G;
      reconsider w as Element of G by A172,ZFMISC_1:106;
      u <> v & v <> x & x <> u & w <> u & w <> v & w <> x
      proof
A173:   u <> v
        proof
          assume
A174:     not thesis;
          consider y1 being Element of R2 such that
A175:     u = y1 & not [y1,x] in the InternalRel of G by A169;
          consider y2 being Element of R2 such that
A176:     v = y2 & [y2,x] in the InternalRel of G by A170;
          thus contradiction by A174,A175,A176;
        end;
A177:   v <> x
        proof
          assume
A178:     not thesis;
          consider y1 being Element of R2 such that
A179:     v = y1 & [y1,x] in the InternalRel of G by A170;
          x in (the carrier of R1) \/ the carrier of R2 by A178,A179,
XBOOLE_0:def 3;
          then x in the carrier of R by A2,NECKLA_2:def 2;
          then x in (the carrier of G) \ {x} by YELLOW_0:def 15;
          then not x in {x} by XBOOLE_0:def 5;
          hence contradiction by TARSKI:def 1;
        end;
A180:   x <> u
        proof
          assume
A181:     not thesis;
          consider y1 being Element of R2 such that
A182:     u = y1 & not [y1,x] in the InternalRel of G by A169;
          x in (the carrier of R1) \/ (the carrier of R2) by A181,A182,
XBOOLE_0:def 3;
          then x in the carrier of R by A2,NECKLA_2:def 2;
          then x in (the carrier of G) \ {x} by YELLOW_0:def 15;
          then not x in {x} by XBOOLE_0:def 5;
          hence contradiction by TARSKI:def 1;
        end;
A183:   w <> u
        proof
          assume
A184:     not thesis;
          consider y1 being Element of R1 such that
A185:     w = y1 & [y1,x] in the InternalRel of G by A171;
          consider y2 being Element of R2 such that
A186:     u = y2 & not [y2,x] in the InternalRel of G by A169;
          thus contradiction by A184,A185,A186;
        end;
A187:   w <> v
        proof
          assume
A188:     not thesis;
          consider y1 being Element of R1 such that
A189:     w = y1 & [y1,x] in the InternalRel of G by A171;
          consider y2 being Element of R2 such that
A190:     v = y2 & [y2,x] in the InternalRel of G by A170;
          y1 in (the carrier of R1) /\ (the carrier of R2)
          by A188,A189,A190,XBOOLE_0:def 4;
          hence contradiction by A1,XBOOLE_0:def 7;
        end;
        w <> x
        proof
          assume
A191:     not thesis;
          consider y1 being Element of R1 such that
A192:     w = y1 & [y1,x] in the InternalRel of G by A171;
          x in (the carrier of R1) \/ the carrier of R2 by A191,A192,
XBOOLE_0:def 3;
          then x in the carrier of R by A2,NECKLA_2:def 2;
          then x in (the carrier of G) \ {x} by YELLOW_0:def 15;
          then not x in {x} by XBOOLE_0:def 5;
          hence contradiction by TARSKI:def 1;
        end;
        hence thesis by A173,A177,A180,A183,A187;
      end;
      then
A193: u,v,x,w are_mutually_different by ZFMISC_1:def 6;
A194: [v,x] in the InternalRel of G
      proof
        consider y1 being Element of R2 such that
A195:   v = y1 & [y1,x] in the InternalRel of G by A170;
        thus thesis by A195;
      end;
A196: [x,w] in the InternalRel of G
      proof
        consider y1 being Element of R1 such that
A197:   w = y1 & [y1,x] in the InternalRel of G by A171;
        the InternalRel of G is_symmetric_in the carrier of G
        by NECKLACE:def 4;
        hence thesis by A197,RELAT_2:def 3;
      end;
A198: not [u,x] in the InternalRel of G
      proof
        consider y1 being Element of R2 such that
A199:   u = y1 & not [y1,x] in the InternalRel of G by A169;
        thus thesis by A199;
      end;
A200: not [u,w] in the InternalRel of G
      proof
        assume
A201:   not thesis;
        consider y1 being Element of R2 such that
A202:   u = y1 & not [y1,x] in the InternalRel of G by A169;
        consider y2 being Element of R1 such that
A203:   w = y2 & [y2,x] in the InternalRel of G by A171;
        u in (the carrier of R1) \/ the carrier of R2 by A202,XBOOLE_0:def 3;
        then reconsider u as Element of R by A2,NECKLA_2:def 2;
        w in (the carrier of R1) \/ the carrier of R2 by A203,XBOOLE_0:def 3;
        then reconsider w as Element of R by A2,NECKLA_2:def 2;

[u,w] in (the InternalRel of G)|_2 the carrier of R by A201,XBOOLE_0:def 4;
        then [u,w] in the InternalRel of R by YELLOW_0:def 14;
        then
A204:   [u,w] in (the InternalRel of R1) \/ (the InternalRel of R2 )
        by A2,NECKLA_2:def 2;
        per cases by A204,XBOOLE_0:def 3;
        suppose [u,w] in the InternalRel of R1;
          then u in the carrier of R1 & w in the carrier of R1 by ZFMISC_1:106;

then u in (the carrier of R1) /\ the carrier of R2 by A202,XBOOLE_0:def 4;
          hence contradiction by A1,XBOOLE_0:def 7;
        end;
        suppose [u,w] in the InternalRel of R2;
          then u in the carrier of R2 & w in the carrier of R2 by ZFMISC_1:106;

then w in (the carrier of R1) /\ the carrier of R2 by A203,XBOOLE_0:def 4;
          hence contradiction by A1,XBOOLE_0:def 7;
        end;
      end;
      not [v,w] in the InternalRel of G
      proof
        assume
A205:   not thesis;
        consider y1 being Element of R2 such that
A206:   v = y1 & [y1,x] in the InternalRel of G by A170;
        consider y2 being Element of R1 such that
A207:   w = y2 & [y2,x] in the InternalRel of G by A171;
        v in (the carrier of R1) \/ the carrier of R2 by A206,XBOOLE_0:def 3;
        then reconsider v as Element of R by A2,NECKLA_2:def 2;
        w in (the carrier of R1) \/ the carrier of R2 by A207,XBOOLE_0:def 3;
        then reconsider w as Element of R by A2,NECKLA_2:def 2;

[v,w] in (the InternalRel of G)|_2 the carrier of R by A205,XBOOLE_0:def 4;
        then [v,w] in the InternalRel of R by YELLOW_0:def 14;
        then
A208:   [v,w] in (the InternalRel of R1) \/ (the InternalRel of R2 )
        by A2,NECKLA_2:def 2;
        per cases by A208,XBOOLE_0:def 3;
        suppose [v,w] in the InternalRel of R1;
          then v in the carrier of R1 & w in the carrier of R1 by ZFMISC_1:106;

then v in (the carrier of R1) /\ the carrier of R2 by A206,XBOOLE_0:def 4;
          hence contradiction by A1,XBOOLE_0:def 7;
        end;
        suppose [v,w] in the InternalRel of R2;
          then v in the carrier of R2 & w in the carrier of R2 by ZFMISC_1:106;

then w in (the carrier of R1) /\ the carrier of R2 by A207,XBOOLE_0:def 4;
          hence contradiction by A1,XBOOLE_0:def 7;
        end;
      end;
      then
A209: subrelstr Z embeds Necklace 4 by A161,A193,A194,A196,A198,A200,Th39;
      subrelstr Z is non empty by YELLOW_0:def 15;
      then reconsider H = subrelstr Z as non empty full SubRelStr of G;
      G embeds Necklace 4
      proof
        assume not thesis;
        then G is N-free by NECKLA_2:def 1;
        then H is N-free by Th23;
        hence thesis by A209,NECKLA_2:def 1;
      end;
      hence thesis;
    end;
  end;
end;

theorem
  for G being non empty strict finite irreflexive symmetric RelStr
  st G is N-free & the carrier of G in FinSETS
  holds the RelStr of G in fin_RelStr_sp
proof
  defpred P[Nat] means
  for G be non empty strict finite irreflexive symmetric RelStr
  st G is N-free & card the carrier of G = $1 & the carrier of G in FinSETS
  holds (the RelStr of G) in fin_RelStr_sp;
A1: for n be Nat st for k be Nat st k < n holds P[k] holds P[n]
  proof
    let n be Nat such that
A2: for k be Nat st k < n holds P[k];
    let G be non empty strict finite irreflexive symmetric RelStr;
    set CG = ComplRelStr G;
    assume
A3: G is N-free & card (the carrier of G) = n & (the carrier of G) in FinSETS;
    per cases;
    suppose G is trivial;
      then reconsider G as non empty trivial RelStr;
      (the RelStr of G) in fin_RelStr_sp by A3,NECKLA_2:def 5;
      hence thesis;
    end;
    suppose G is not path-connected & G is non trivial;
      then consider G1,G2 being non empty strict irreflexive symmetric RelStr
      such that
A4:   (the carrier of G1) misses (the carrier of G2) and
A5:   the RelStr of G = union_of(G1,G2) by Th31;
      set cG1 = the carrier of G1, cG2 = the carrier of G2,
      R = the RelStr of G, cR = the carrier of R;
A6:   cR = cG1 \/ cG2 by A5,NECKLA_2:def 2;
      then
A7:   card cG1 in card cR by A4,Lm1;
      then
A8:   cG1 is finite by CARD_2:68;
      reconsider cG1 as finite set by A7,CARD_2:68;
      reconsider G1 as non empty strict finite irreflexive symmetric RelStr
      by A8;
      reconsider cR as finite set;
A9:   card cG1 < card cR by A7,NAT_1:45;
      G1 is full SubRelStr of G by A4,A5,Th10;
      then
A10:  G1 is N-free by A3,Th23;
      the carrier of G1 in FinSETS
      by A3,A6,CLASSES1:6,CLASSES2:def 2,XBOOLE_1:7;
      then
A11:  G1 in fin_RelStr_sp by A2,A3,A9,A10;
A12:  card cG2 in card cR by A4,A6,Lm1;
      then
A13:  cG2 is finite by CARD_2:68;
      reconsider cG2 as finite set by A12,CARD_2:68;
      reconsider G2 as non empty strict finite irreflexive symmetric RelStr
      by A13;
A14:  card cG2 < card cR by A12,NAT_1:45;
      G2 is full SubRelStr of G by A4,A5,Th10;
      then
A15:  G2 is N-free by A3,Th23;
      the carrier of G2 in FinSETS
      by A3,A6,CLASSES1:6,CLASSES2:def 2,XBOOLE_1:7;
      then G2 in fin_RelStr_sp by A2,A3,A14,A15;
      hence thesis by A4,A5,A11,NECKLA_2:def 5;
    end;
    suppose CG is not path-connected & G is non trivial;
      then consider G1,G2 being non empty strict irreflexive symmetric RelStr
      such that
A16:  (the carrier of G1) misses (the carrier of G2) and
A17:  (the RelStr of G) = sum_of(G1,G2) by Th32;
      set cG1 = the carrier of G1, cG2 = the carrier of G2,
      R = the RelStr of G, cR = the carrier of R;
A18:  cR = cG1 \/ cG2 by A17,NECKLA_2:def 3;
      then
A19:  card cG1 in card cR by A16,Lm1;
      then
A20:  cG1 is finite by CARD_2:68;
      reconsider cG1 as finite set by A19,CARD_2:68;
      reconsider G1 as non empty strict finite irreflexive symmetric RelStr
      by A20;
      reconsider cR as finite set;
A21:  card cG1 < card cR by A19,NAT_1:45;
      G1 is full SubRelStr of G by A16,A17,Th10;
      then
A22:  G1 is N-free by A3,Th23;
      the carrier of G1 in FinSETS
      by A3,A18,CLASSES1:6,CLASSES2:def 2,XBOOLE_1:7;
      then
A23:  G1 in fin_RelStr_sp by A2,A3,A21,A22;
A24:  card cG2 in card cR by A16,A18,Lm1;
      then
A25:  cG2 is finite by CARD_2:68;
      reconsider cG2 as finite set by A24,CARD_2:68;
      reconsider G2 as non empty strict finite irreflexive symmetric RelStr
      by A25;
A26:  card cG2 < card cR by A24,NAT_1:45;
      G2 is full SubRelStr of G by A16,A17,Th10;
      then
A27:  G2 is N-free by A3,Th23;
      the carrier of G2 in FinSETS
      by A3,A18,CLASSES1:6,CLASSES2:def 2,XBOOLE_1:7;
      then G2 in fin_RelStr_sp by A2,A3,A26,A27;
      hence thesis by A16,A17,A23,NECKLA_2:def 5;
    end;
    suppose
A28:  G is non trivial & G is path-connected & CG is path-connected;
      consider x be set such that
A29:  x in the carrier of G by XBOOLE_0:def 1;
      reconsider x as Element of G by A29;
      set A = (the carrier of G) \ {x};
A30:  A c= the carrier of G;
      reconsider A as Subset of G;
      set R = subrelstr A;
      R is non empty by A28,YELLOW_0:def 15;
      then reconsider R as non empty finite irreflexive symmetric RelStr;
A31:  card the carrier of R < n
      proof
        card A = card (the carrier of G) - card {x} by CARD_2:63;
        then
A32:    card A = n - 1 by A3,CARD_2:60;
        n - 1 < n - 1 + 1 by XREAL_1:31;
        hence thesis by A32,YELLOW_0:def 15;
      end;
A33:  R is N-free by A3,Th23;
      the carrier of R in FinSETS
      proof
        the carrier of R c= the carrier of G by A30,YELLOW_0:def 15;
        hence thesis by A3,CLASSES1:6,CLASSES2:def 2;
      end;
      then
A34:  R in fin_RelStr_sp by A2,A31,A33;
      thus thesis
      proof
        per cases by A34,NECKLA_2:6;
        suppose R is trivial RelStr;
          then the carrier of R is trivial;
          then
A35:      A is trivial by YELLOW_0:def 15;
          the carrier of R is non empty;
          then A is non empty by YELLOW_0:def 15;
          then consider a be set such that
A36:      A = {a} by A35,REALSET1:def 4;
A37:      {a} \/ {x} = (the carrier of G) \/ {x} by A36,XBOOLE_1:39;
A38:      (the carrier of G) \/ {x} = the carrier of G
          proof
            thus (the carrier of G) \/ {x} c= the carrier of G
            proof
              let c be set;
              assume c in (the carrier of G) \/ {x};
              then c in the carrier of G or c in {x} by XBOOLE_0:def 3;
              hence thesis;
            end;
            let c be set;
            assume c in the carrier of G;
            hence thesis by XBOOLE_0:def 3;
          end;
          then
A39:      the carrier of G = {a,x} by A37,ENUMSET1:41;
          a <> x by A28,A37,A38;
          then card (the carrier of G) = 2 by A39,CARD_2:76;
          hence thesis by A3,Th34;
        end;
        suppose ex R1,R2 being strict RelStr st
          (the carrier of R1) misses (the carrier of R2) &
          R1 in fin_RelStr_sp & R2 in fin_RelStr_sp &
          (R = union_of(R1,R2) or R = sum_of(R1,R2) );
          then consider R1,R2 being strict RelStr such that
A40:      (the carrier of R1) misses (the carrier of R2) and
A41:      R1 in fin_RelStr_sp & R2 in fin_RelStr_sp &
          (R = union_of(R1,R2) or R = sum_of(R1,R2));
          thus thesis
          proof
            per cases by A41;
            suppose
A42:          R = union_of(R1,R2);
              R1 is SubRelStr of R by A40,A41,Th10;

then reconsider R1 as non empty SubRelStr of G by A41,NECKLA_2:4,YELLOW_6:16;
              R2 is SubRelStr of R by A40,A41,Th10;

then reconsider R2 as non empty SubRelStr of G by A41,NECKLA_2:4,YELLOW_6:16;
              subrelstr ([#]G \ {x}) = union_of(R1,R2) by A42;
              then G embeds Necklace 4 by A28,A40,Th40;
              hence thesis by A3,NECKLA_2:def 1;
            end;
            suppose R = sum_of(R1,R2);
              then
A43:          ComplRelStr R = union_of(ComplRelStr R1, ComplRelStr R2)
              by A40,Th18;

reconsider G' = ComplRelStr G as non empty irreflexive symmetric RelStr;
A44:          ComplRelStr R1 is non empty
              proof
                assume not thesis;
                then the carrier of ComplRelStr R1 is empty;
                then the carrier of R1 is empty;
                then R1 is empty;
                hence contradiction by A41,NECKLA_2:4;
              end;
A45:          ComplRelStr R2 is non empty
              proof
                assume not thesis;
                then the carrier of ComplRelStr R2 is empty;
                then the carrier of R2 is empty;
                then R2 is empty;
                hence contradiction by A41,NECKLA_2:4;
              end;
              reconsider R11 = ComplRelStr R1 as non empty RelStr by A44;
              reconsider R22 = ComplRelStr R2 as non empty RelStr by A45;
              reconsider x' = x as Element of G' by NECKLACE:def 9;
A46:          the carrier of R11 = the carrier of R1 &
              the carrier of R22 = the carrier of R2 by NECKLACE:def 9;
A47:          G'
              is path-connected & ComplRelStr G' is path-connected by A28,Th16;
A48:          G' is non trivial by A28,NECKLACE:def 9;
              ComplRelStr R = ComplRelStr (subrelstr ([#]G \ {x}))
                .= subrelstr ([#](G') \ {x'}) by Th20;
              then
A50:          G' embeds Necklace 4 by A40,A43,A46,A47,A48,Th40;
              G' is N-free by A3,Th25;
              hence thesis by A50,NECKLA_2:def 1;
            end;
          end;
        end;
      end;
    end;
  end;
A51: for k be Nat holds P[k] from NAT_1:sch 4(A1);
  let R be non empty strict finite irreflexive symmetric RelStr;
  card the carrier of R is Nat;
  hence thesis by A51;
end;

