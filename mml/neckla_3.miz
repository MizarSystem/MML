:: The Class of Series-Parallel Graphs, {III}
::  by Krzysztof Retel
:: 
:: Received February 3, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabulary NECKLA_3, NECKLA_2, NECKLACE, ORDERS_1, RELAT_1, FINSET_1, FUNCT_1,
      PRE_TOPC, BOOLE, YELLOW_0, SUBSET_1, INCPROJ, RELAT_2, CLASSES2, CAT_1,
      REALSET1, REWRITE1, CARD_1, FINSEQ_1, ARYTM_1, FINSEQ_5, MSUALG_5,
      TARSKI, EQREL_1, WELLORD1, FUNCT_4, FUNCOP_1, SEQM_3;
 notation TARSKI, SETWISEO, GROUP_2, XBOOLE_0, ENUMSET1, SUBSET_1, ZFMISC_1,
      RELAT_1, RELAT_2, RELSET_1, ORDINAL1, FINSET_1, ARYTM_1, ARYTM_0,
      XCMPLX_0, REAL_1, XREAL_0, NAT_1, CARD_1, STRUCT_0, LATTICE3, ORDERS_3,
      ORDERS_1, WAYBEL_1, PRE_TOPC, SETFAM_1, CQC_LANG, FUNCT_1, INCPROJ,
      PROB_1, FINSUB_1, CLASSES2, MCART_1, NECKLACE, WAYBEL_0, FUNCOP_1,
      REALSET1, FUNCT_4, GROUP_1, PARTFUN1, REWRITE1, FINSEQ_1, FINSEQ_5,
      EQREL_1, MSUALG_5, YELLOW_0, NECKLA_2, WELLORD1, FUNCT_2;
 constructors STRUCT_0, ORDERS_1, ORDERS_2, CLASSES2, CQC_LANG, NECKLA_2,
      ORDERS_3, ENUMSET1, NECKLACE, NAT_1, ARYTM_0, RELAT_1, FUNCT_1, XREAL_0,
      WAYBEL_0, WAYBEL_1, RELAT_2, ARYTM_1, REAL_1, ORDINAL3, SETFAM_1, CARD_1,
      YELLOW13, FINSET_1, FINSUB_1, SETWISEO, GROUP_2, CLASSES1, SEQM_3,
      GROUP_1, PROB_1, SQUARE_1, INCPROJ, REALSET1, LATTICE3, YELLOW21, CARD_2,
      FINSOP_1, FUNCT_4, BVFUNC24, REWRITE1, FINSEQ_5, MSUALG_5, YELLOW_0,
      AMI_1, WELLORD1, TOLER_1;
 clusters RELSET_1, ORDERS_1, STRUCT_0, FINSET_1, ARYTM_3, RELAT_1, PARTFUN1,
      FINSEQ_5, FUNCT_1, NAT_1, FINSUB_1, FUNCT_2, ORDINAL2, ORDINAL1,
      ORDERS_3, SUBSET_1, XBOOLE_0, WAYBEL_0, CLASSES1, CLASSES2, CQC_LANG,
      CARD_1, YELLOW13, BINARITH, PRELAMB, ZFMISC_1, NECKLA_2, NECKLACE,
      YELLOW_0, WAYBEL11, REALSET1, TOPREAL8, ORDERS_4, XREAL_0, INT_1,
      FINSEQ_1, FUNCOP_1, FUNCT_7, AMISTD_1, FUNCT_4, WAYBEL35;
 requirements BOOLE, SUBSET, REAL, NUMERALS, ARITHM;
 definitions RELSET_1, XBOOLE_0, TARSKI, NECKLACE, RELAT_2, FUNCT_1;
 theorems RELSET_1, NECKLACE, XBOOLE_0, STRUCT_0, WAYBEL_0, YELLOW_0, FUNCT_1,
      PRE_TOPC, FUNCT_2, ORDERS_1, RELAT_2, SUBSET_1, ZFMISC_1, XBOOLE_1,
      TARSKI, MCART_1, CLASSES2, CLASSES1, NECKLA_2, CQC_LANG, ENUMSET1,
      RELAT_1, SYSREL, INCPROJ, GROUP_1, PARTFUN1, GROUP_6, REALSET1, CARD_2,
      CARD_4, REWRITE1, FINSEQ_1, FINSEQ_3, SCMFSA_7, FINSEQ_5, NAT_1, AXIOMS,
      XCMPLX_1, EQREL_1, MSUALG_6, MSUALG_5, WELLORD1, CARD_1, REAL_1, SPPOL_1,
      GRAPH_5, RLVECT_1, NAT_2, TOLER_1, GROUP_3, CQC_THE1, FUNCT_4, KNASTER,
      FUNCT_3, WAYBEL_1, YELLOW_6;
 schemes BINARITH, NAT_1;

begin :: Preliminaries

reserve A,B,a,b,c,d,e,f,g,h for set;

theorem Th1:
  (id A)|B = id A /\ [:B,B:]
proof
 thus (id A)|B c= id A /\ [:B,B:]
  proof
   let a be set;
   assume A1: a in (id A)|B;
     (id A)|B is Relation of B,A by RELSET_1:33;
   then consider x,y being set such that
   A3: a = [x,y] & x in B & y in A by RELSET_1:6,A1;
   A4: [x,y] in id A by RELAT_1:def 11,A3,A1;
   then x in A & x = y by RELAT_1:def 10;
   then [x,y] in [:B,B:] by ZFMISC_1:106,A3;
   hence thesis by XBOOLE_0:def 3,A3,A4;
  end;
 let a be set;
 assume a in id A /\ [:B,B:];
  then A8: a in id A & a in [:B,B:] by XBOOLE_0:def 3;
  then consider x1,y1 being set such that
  A9: x1 in B & y1 in B & a = [x1,y1] by ZFMISC_1:def 2;
 thus thesis by RELAT_1:def 11,A8,A9;
end;

theorem
  id {a,b,c,d} = {[a,a],[b,b],[c,c],[d,d]}
proof
 set X = {a,b,c,d};
 thus id X c= {[a,a],[b,b],[c,c],[d,d]}
  proof
   let x be set;
   assume A1: x in id X;
   then consider x1,y1 being set such that
   A2: x = [x1,y1] & x1 in X & y1 in X  by RELSET_1:6;
   A3: x1 = y1 by A1,A2,RELAT_1:def 10;
   per cases by A2,ENUMSET1:18;
   suppose x1 = a;
   hence thesis by A2,A3,ENUMSET1:19;
   end;
   suppose x1 = b;
   hence thesis by A2,A3,ENUMSET1:19;
   end;
   suppose x1 = c;
   hence thesis by A2,A3,ENUMSET1:19;
   end;
   suppose x1 = d;
   hence thesis by A2,A3,ENUMSET1:19;
 end;
 end;
 let x be set;
 assume A8: x in {[a,a],[b,b],[c,c],[d,d]};
 per cases by A8,ENUMSET1:18;
 suppose A9: x = [a,a];
    a in X by ENUMSET1:19;
 hence thesis by RELAT_1:def 10,A9;
 end;
 suppose A11: x = [b,b];
    b in X by ENUMSET1:19;
 hence thesis by RELAT_1:def 10,A11;
 end;
 suppose A13: x = [c,c];
    c in X by ENUMSET1:19;
 hence thesis by RELAT_1:def 10,A13;
 end;
 suppose A15: x = [d,d];
    d in X by ENUMSET1:19;
 hence thesis by RELAT_1:def 10,A15;
end;
end;

theorem Th3:
 [:{a,b,c,d},{e,f,g,h}:] = {[a,e],[a,f],[b,e],[b,f],[a,g],[a,h],[b,g],[b,h]}
   \/ {[c,e],[c,f],[d,e],[d,f],[c,g],[c,h],[d,g],[d,h]}
proof
 set X1 = {a,b,c,d},
     Y1 = {e,f,g,h},
     X11 = {a,b},
     X12 = {c,d},
     Y11 = {e,f},
     Y12 = {g,h};
 A1: [:X11,Y11:] = {[a,e],[a,f],[b,e],[b,f]} &
     [:X11,Y12:] = {[a,g],[a,h],[b,g],[b,h]} &
     [:X12,Y11:] = {[c,e],[c,f],[d,e],[d,f]} &
     [:X12,Y12:] = {[c,g],[c,h],[d,g],[d,h]} by MCART_1:25;
   X1 = X11 \/ X12 & Y1 = Y11 \/ Y12 by ENUMSET1:45;
 then [:X1,Y1:] = [:X11,Y11:] \/ [:X11,Y12:] \/ [:X12,Y11:] \/ [:X12,Y12:]
             by ZFMISC_1:121;
 then [:X1,Y1:] = {[a,e],[a,f],[b,e],[b,f],[a,g],[a,h],[b,g],[b,h]}
   \/ {[c,e],[c,f],[d,e],[d,f]} \/ {[c,g],[c,h],[d,g],[d,h]} by ENUMSET1:65,A1
   .= {[a,e],[a,f],[b,e],[b,f],[a,g],[a,h],[b,g],[b,h]}
   \/ ({[c,e],[c,f],[d,e],[d,f]} \/ {[c,g],[c,h],[d,g],[d,h]}) by XBOOLE_1:4
   .= {[a,e],[a,f],[b,e],[b,f],[a,g],[a,h],[b,g],[b,h]}
   \/ {[c,e],[c,f],[d,e],[d,f],[c,g],[c,h],[d,g],[d,h]} by ENUMSET1:65;
 hence thesis;
end;

registration
  let X,Y be trivial set;
  cluster -> trivial Relation of X,Y;
  correctness
   proof
    let R be Relation of X,Y;
    assume not thesis;
    then consider a1,a2 be set such that
    A2: a1 in R & a2 in R & a1 <> a2 by SPPOL_1:3;
    A3: (X is empty or ex x be set st X = {x}) &
       (Y is empty or ex y be set st Y = {y}) by REALSET1:def 12;
    per cases by A3;
    suppose X is empty & Y is empty;
    hence thesis by A2,RELSET_1:26;
    end;
    suppose X is empty & ex x be set st Y = {x};
    hence thesis by A2,RELSET_1:26;
    end;
    suppose ex x be set st X = {x} & Y is empty;
    hence thesis by A2,RELSET_1:27;
    end;
    suppose A7: ex x be set st X = {x} & ex y be set st Y = {y};
     then consider x be set such that A8: X = {x};
     consider y be set such that A9: Y = {y} by A7;
     consider x1,x2 be set such that
     A10: a1 = [x1,x2] & x1 in X & x2 in Y by A2,RELSET_1:6;
     A11: x1 = x & x2 = y by A8,A9,A10,TARSKI:def 1;
     consider y1,y2 be set such that
     A12: a2 = [y1,y2] & y1 in X & y2 in Y by A2,RELSET_1:6;
       y1 = x & y2 = y by A8,A9,A12,TARSKI:def 1;
    hence thesis by A2,A11,A10,A12;
   end;
   end;
end;

theorem Th4:
  for X be trivial set, R be Relation of X st R is non empty
   holds ex x be set st R = {[x,x]}
proof
 let X be trivial set;
 let R be Relation of X such that A1: R is non empty;
 consider x be set such that A2: x in R by XBOOLE_0:def 1,A1;
 consider y,z be set such that
 A3: x = [y,z] & y in X & z in X by A2,RELSET_1:6;
 consider a be set such that A4: X = {a} by A3,REALSET1:def 12;
 A5: y = a & z = a by A3,A4,TARSKI:def 1;
   R = {[a,a]}
  proof
   thus R c= {[a,a]}
    proof
     let r be set such that A7: r in R;
     consider y,z be set such that
     A8: r = [y,z] & y in X & z in X by RELSET_1:6,A7;
       y = a & z = a by A4,TARSKI:def 1,A8;
     hence thesis by A8,TARSKI:def 1;
    end;
   let z be set;
   assume z in {[a,a]};
   hence thesis by A2,A3,A5,TARSKI:def 1;
  end;
 hence thesis;
end;

registration
 let X be trivial set;
 cluster -> trivial reflexive symmetric transitive
                    strongly_connected Relation of X;
 correctness
  proof
   let R be Relation of X,X;
   A1: R is_reflexive_in field R
    proof
     per cases;
     suppose A2: R is empty;
      let x be set;
      assume x in field R;
     hence thesis by A2,TOLER_1:1;
     end;
     suppose R is non empty;
      then consider z be set such that
      A5: R = {[z,z]} by Th4;
      A6: dom R = {z} & rng R = {z} by A5,RELAT_1:23;
      let x be set;
      assume x in field R;
      then x in dom R \/ rng R by RELAT_1:def 6;
      then x = z by A6,TARSKI:def 1;
     hence thesis by TARSKI:def 1,A5;
    end;
    end;
   A10: R is_symmetric_in field R
    proof
     per cases;
     suppose A11: R is empty;
      let x,y be set;
      assume x in field R & y in field R & [x,y] in R;
     hence thesis by A11;
     end;
     suppose R is non empty;
      then consider a be set such that A14: R = {[a,a]} by Th4;
      let x,y be set;
      assume x in field R & y in field R & [x,y] in R;
      then [x,y] = [a,a]  by A14,TARSKI:def 1;
      then x = a & y = a by ZFMISC_1:33;
     hence thesis by TARSKI:def 1,A14;
    end;
    end;
    A18: R is_transitive_in field R
    proof
     per cases;
     suppose A19: R is empty;
      let x,y,z be set;
      assume x in field R & y in field R & z in field R &
       [x,y] in R & [y,z] in R;
     hence thesis by A19;
     end;
     suppose R is non empty;
      then consider a be set such that A22: R = {[a,a]} by Th4;
      let x,y,z be set;
      assume x in field R & y in field R & z in field R
       & [x,y] in R & [y,z] in R;
      then [x,y] = [a,a] & [y,z] = [a,a] by A22,TARSKI:def 1;
      then x = a & y = a & z = a by ZFMISC_1:33;
     hence thesis by TARSKI:def 1,A22;
    end;
    end;
     R is_strongly_connected_in field R
    proof
     per cases;
     suppose A28: R is empty;
      let x,y be set;
      assume x in field R & y in field R;
     hence thesis by A28,TOLER_1:1;
     end;
     suppose R is non empty;
      then consider a be set such that A31: R = {[a,a]} by Th4;
      A32: dom R = {a} & rng R = {a} by A31,RELAT_1:23;
      let x,y be set;
      assume x in field R & y in field R;
      then x in dom R \/ rng R & y in dom R \/ rng R by RELAT_1:def 6;
      then x = a & y = a by TARSKI:def 1,A32;
     hence thesis by A31,TARSKI:def 1;
    end;
    end;
   hence thesis by A1,A10,A18,RELAT_2:def 9,def 11,def 15,def 16;
  end;
end;

theorem Th5: for X be non empty trivial set, R be Relation of X
  holds R is_symmetric_in X
proof
 let X be non empty trivial set;
 let R be Relation of X;
 consider x be set such that A1: X = {x} by REALSET1:def 12;
 let a,b be set;
 assume A2: a in X & b in X & [a,b] in R;
 then a = x & b = x by TARSKI:def 1,A1;
 hence thesis by A2;
end;

registration
  cluster non empty strict finite irreflexive symmetric RelStr;
  correctness
   proof
    set X = {0,1},
        r = {[0,1],[1,0]};
      0 in X & 1 in X by TARSKI:def 2;
    then A2: [0,1] in [:X,X:] & [1,0] in [:X,X:] by ZFMISC_1:def 2;
      r c= [:X,X:]
     proof
      let x be set; assume x in r;
      hence thesis by A2,TARSKI:def 2;
     end;
    then reconsider r as Relation of X,X by RELSET_1:def 1;
    take R = RelStr (#X,r#);
      for x,y be set st x in X & y in X & [x,y] in r
      holds [y,x] in r
      proof
       let x,y be set;
       assume A6: x in X & y in X & [x,y] in r;
       per cases by TARSKI:def 2,A6;
       suppose [x,y] = [0,1];
        then x = 0 & y = 1 by ZFMISC_1:33;
       hence thesis by TARSKI:def 2;
       end;
       suppose [x,y] = [1,0];
        then x = 1 & y = 0 by ZFMISC_1:33;
       hence thesis by TARSKI:def 2;
      end;
      end;
    then A11: r is_symmetric_in X by RELAT_2:def 3;
      for x being set st x in the carrier of R
     holds not [x,x] in the InternalRel of R
      proof
       let x be set;
       assume x in the carrier of R;
       then A14: x = 0 or x = 1 by TARSKI:def 2;
       A15: not [0,0] in r
        proof
         assume [0,0] in r;
         then [0,0] = [0,1] or [0,0] = [1,0] by TARSKI:def 2;
         hence contradiction by ZFMISC_1:33;
        end;
         not [1,1] in r
        proof
         assume [1,1] in r;
         then [1,1] = [0,1] or [1,1] = [1,0] by TARSKI:def 2;
         hence contradiction by ZFMISC_1:33;
        end;
       hence thesis by A14,A15;
      end;
    hence thesis by GROUP_1:def 13,NECKLACE:def 6,def 4,A11;
   end;
end;

registration
 let L be irreflexive RelStr;
 cluster -> irreflexive (full SubRelStr of L);
 correctness
  proof
   let S be full SubRelStr of L;
   A1: the carrier of S c= the carrier of L &
       the InternalRel of S = (the InternalRel of L) |_2 the carrier of S
           by YELLOW_0:def 13,def 14;
   then A2: the InternalRel of S = (the InternalRel of L) /\
       [:the carrier of S, the carrier of S:] by WELLORD1:def 6;
   let x be set;
   assume x in the carrier of S;
   then not [x,x] in the InternalRel of L by NECKLACE:def 6,A1;
   hence thesis by A2,XBOOLE_0:def 3;
  end;
end;

registration
 let L be symmetric RelStr;
 cluster -> symmetric (full SubRelStr of L);
 correctness
  proof
   let S be full SubRelStr of L;
   A1: the carrier of S c= the carrier of L &
       the InternalRel of S = (the InternalRel of L) |_2 the carrier of S
           by YELLOW_0:def 13,def 14;
   then A2: the InternalRel of S = (the InternalRel of L) /\
       [:the carrier of S, the carrier of S:] by WELLORD1:def 6;
   let x,y be set;
   assume A3: x in the carrier of S & y in the carrier of S &
             [x,y] in the InternalRel of S;
   then A4: x in the carrier of L & y in the carrier of L &
   [x,y] in the InternalRel of L by A1,A2,XBOOLE_0:def 3;
     the InternalRel of L is_symmetric_in the carrier of L by NECKLACE:def 4;
   then A6: [y,x] in the InternalRel of L by RELAT_2:def 3,A4;
     [y,x] in [:the carrier of S,the carrier of S:] by ZFMISC_1:106,A3;
   hence thesis by XBOOLE_0:def 3,A6,A2;
  end;
end;

theorem Th6:
  for R be irreflexive symmetric RelStr st Card (the carrier of R) = 2
  holds ex a,b be set st the carrier of R = {a,b} &
  (the InternalRel of R = {[a,b],[b,a]} or the InternalRel of R = {})
proof
 let R be irreflexive symmetric RelStr;
 assume A1: Card (the carrier of R) = 2;
 then reconsider X = the carrier of R as finite set by CARD_4:1;
 consider a,b be set such that
 A2: a <> b & X = {a,b} by GROUP_3:166,A1;
 set Q = the InternalRel of R;
 A3: the InternalRel of R c= {[a,b],[b,a]}
  proof
   let x be set;
   assume A4: x in the InternalRel of R;
   then consider x1,x2 be set such that
   A5: x = [x1,x2] & x1 in X & x2 in X by RELSET_1:6;
   A6: (x1 = a or x1 = b) & (x2 = a or x2 = b) by TARSKI:def 2,A2,A5;
   per cases by A5,A6;
   suppose A7: x = [a,a];
      a in the carrier of R by TARSKI:def 2,A2;
   hence thesis by A4,A7,NECKLACE:def 6;
   end;
   suppose x = [a,b];
   hence thesis by TARSKI:def 2;
   end;
   suppose x = [b,a];
   hence thesis by TARSKI:def 2;
   end;
   suppose A11: x = [b,b];
      b in the carrier of R by TARSKI:def 2,A2;
   hence thesis by A4,A11,NECKLACE:def 6;
  end;
  end;
 per cases by A3,ZFMISC_1:42;
 suppose Q = {};
 hence thesis by A2;
 end;
 suppose A14: Q = {[a,b]};
  then A15: [a,b] in Q by TARSKI:def 1;
  A16: a in X & b in X by A2,TARSKI:def 2;
    Q is_symmetric_in X by NECKLACE:def 4;
  then [b,a] in Q by RELAT_2:def 3,A15,A16;
  then [b,a] = [a,b] by TARSKI:def 1,A14;
 hence thesis by A2,ZFMISC_1:33;
 end;
 suppose A20: Q = {[b,a]};
  then A21: [b,a] in Q by TARSKI:def 1;
  A22: a in X & b in X by A2,TARSKI:def 2;
    Q is_symmetric_in X by NECKLACE:def 4;
  then [a,b] in Q by RELAT_2:def 3,A21,A22;
  then [b,a] = [a,b] by TARSKI:def 1,A20;
 hence thesis by A2,ZFMISC_1:33;
 end;
 suppose Q = {[a,b],[b,a]};
 hence thesis by A2;
end;
end;

begin :: Some facts about operations 'union_of' and 'sum_of'

registration
 let R be non empty RelStr,S be RelStr;
 cluster union_of(R,S) -> non empty;
 correctness
  proof
     (the carrier of R) \/ (the carrier of S) is non empty;
   then the carrier of union_of(R,S) is non empty by NECKLA_2:def 2;
   hence thesis by STRUCT_0:def 1;
  end;
 cluster sum_of(R,S) -> non empty;
 correctness
  proof
     (the carrier of R) \/ (the carrier of S) is non empty;
   then the carrier of sum_of(R,S) is non empty by NECKLA_2:def 3;
   hence thesis by STRUCT_0:def 1;
  end;
end;

registration
 let R be RelStr, S be non empty RelStr;
 cluster union_of(R,S) -> non empty;
 correctness
  proof
     (the carrier of R) \/ (the carrier of S) is non empty;
   then the carrier of union_of(R,S) is non empty by NECKLA_2:def 2;
   hence thesis by STRUCT_0:def 1;
  end;
 cluster sum_of(R,S) -> non empty;
 correctness
  proof
     (the carrier of R) \/ (the carrier of S) is non empty;
   then the carrier of sum_of(R,S) is non empty by NECKLA_2:def 3;
   hence thesis by STRUCT_0:def 1;
  end;
end;

registration
 let R,S be finite RelStr;
 cluster union_of(R,S) -> finite;
 correctness
  proof
     (the carrier of R) \/ the carrier of S is finite;
   then the carrier of union_of(R,S) is finite by NECKLA_2:def 2;
   hence thesis by GROUP_1:def 13;
  end;
 cluster sum_of(R,S) -> finite;
 correctness
  proof
   (the carrier of R) \/ the carrier of S is finite;
   then the carrier of sum_of(R,S) is finite by NECKLA_2:def 3;
   hence thesis by GROUP_1:def 13;
  end;
end;

registration
  let R,S be symmetric RelStr;
  cluster union_of(R,S) -> symmetric;
  correctness
   proof
    set U = union_of(R,S),
        c_U = the carrier of U,
        I_U = the InternalRel of U,
        c_R = the carrier of R,
        c_S = the carrier of S;
    let x,y be set;
    assume x in c_U & y in c_U & [x,y] in I_U;
    then A2: [x,y] in (the InternalRel of R) \/ the InternalRel of S
              by NECKLA_2:def 2;
     per cases by XBOOLE_0:def 2,A2;
     suppose A3: [x,y] in the InternalRel of R;
      A4: the InternalRel of R is_symmetric_in c_R by NECKLACE:def 4;
        x in c_R & y in c_R by ZFMISC_1:106,A3;
      then [y,x] in the InternalRel of R by RELAT_2:def 3,A3,A4;
      then [y,x] in (the InternalRel of R) \/ the InternalRel of S
                                          by XBOOLE_0:def 2;
     hence thesis by NECKLA_2:def 2;
     end;
     suppose A8: [x,y] in the InternalRel of S;
      A9: the InternalRel of S is_symmetric_in c_S by NECKLACE:def 4;
        x in c_S & y in c_S by ZFMISC_1:106,A8;
      then [y,x] in the InternalRel of S by RELAT_2:def 3,A8,A9;
      then [y,x] in (the InternalRel of R) \/ the InternalRel of S
                                          by XBOOLE_0:def 2;
     hence thesis by NECKLA_2:def 2;
   end;
   end;
  cluster sum_of(R,S) -> symmetric;
  correctness
   proof
    set SU = sum_of(R,S),
        c_SU = the carrier of sum_of(R,S),
        I_SU = the InternalRel of SU,
        c_R = the carrier of R,
        I_R = the InternalRel of R,
        c_S = the carrier of S,
        I_S = the InternalRel of S;
    A13: I_R is_symmetric_in c_R by NECKLACE:def 4;
    A14: I_S is_symmetric_in c_S by NECKLACE:def 4;
      the InternalRel of sum_of(R,S) is_symmetric_in c_SU
    proof
     let x,y be set;
     assume x in c_SU & y in c_SU & [x,y] in I_SU;
     then [x,y] in I_R \/ I_S \/ [:c_R,c_S:] \/ [:c_S,c_R:]
                                                by NECKLA_2:def 3;
     then [x,y] in I_R \/ I_S \/ [:c_R,c_S:] or [x,y] in [:c_S,c_R:]
          by XBOOLE_0:def 2;
     then A19: [x,y] in I_R \/ I_S or [x,y] in [:c_R,c_S:] or [x,y] in [:c_S,
c_R:]
          by XBOOLE_0:def 2;
     per cases by A19,XBOOLE_0:def 2;
     suppose A20: [x,y] in I_R;
      then x in c_R & y in c_R by ZFMISC_1:106;
      then [y,x] in I_R by A20,RELAT_2:def 3,A13;
      then [y,x] in I_R \/ I_S by XBOOLE_0:def 2;
      then [y,x] in I_R \/ I_S \/ [:c_R,c_S:] by XBOOLE_0:def 2;
      then [y,x] in I_R \/ I_S \/ [:c_R,c_S:] \/ [:c_S,c_R:] by XBOOLE_0:
def 2;
     hence thesis by NECKLA_2:def 3;
     end;
     suppose A26: [x,y] in I_S;
      then x in c_S & y in c_S by ZFMISC_1:106;
      then [y,x] in I_S by A26,RELAT_2:def 3,A14;
      then [y,x] in I_R \/ I_S by XBOOLE_0:def 2;
      then [y,x] in I_R \/ I_S \/ [:c_R,c_S:] by XBOOLE_0:def 2;
      then [y,x] in I_R \/ I_S \/ [:c_R,c_S:] \/ [:c_S,c_R:] by XBOOLE_0:
def 2;
     hence thesis by NECKLA_2:def 3;
     end;
     suppose [x,y] in [:c_R,c_S:];
      then x in c_R & y in c_S by ZFMISC_1:106;
      then [y,x] in [:c_S,c_R:] by ZFMISC_1:106;
      then [y,x] in [:c_R,c_S:] \/ [:c_S,c_R:] by XBOOLE_0:def 2;
      then [y,x] in I_S \/ ([:c_R,c_S:] \/ [:c_S,c_R:]) by XBOOLE_0:def 2;
      then [y,x] in I_S \/ [:c_R,c_S:] \/ [:c_S,c_R:] by XBOOLE_1:4;
      then [y,x] in I_R \/ (I_S \/ [:c_R,c_S:] \/ [:c_S,c_R:]) by XBOOLE_0
:def 2;
      then [y,x] in I_R \/ (I_S \/ ([:c_R,c_S:] \/ [:c_S,c_R:])) by
XBOOLE_1:4;
      then [y,x] in I_R \/ I_S \/ ([:c_R,c_S:] \/ [:c_S,c_R:]) by XBOOLE_1:4;
      then [y,x] in I_R \/ I_S \/ [:c_R,c_S:] \/ [:c_S,c_R:] by XBOOLE_1:4;
     hence thesis by NECKLA_2:def 3;
     end;
     suppose [x,y] in [:c_S,c_R:];
      then x in c_S & y in c_R by ZFMISC_1:106;
      then [y,x] in [:c_R,c_S:] by ZFMISC_1:106;
      then [y,x] in [:c_R,c_S:] \/ [:c_S,c_R:] by XBOOLE_0:def 2;
      then [y,x] in I_S \/ ([:c_R,c_S:] \/ [:c_S,c_R:]) by XBOOLE_0:def 2;
      then [y,x] in I_S \/ [:c_R,c_S:] \/ [:c_S,c_R:] by XBOOLE_1:4;
      then [y,x] in I_R \/ (I_S \/ [:c_R,c_S:] \/ [:c_S,c_R:]) by XBOOLE_0
:def 2;
     then [y,x] in I_R \/ (I_S \/ ([:c_R,c_S:] \/ [:c_S,c_R:])) by XBOOLE_1:4;
      then [y,x] in I_R \/ I_S \/ ([:c_R,c_S:] \/ [:c_S,c_R:]) by XBOOLE_1:4;
      then [y,x] in I_R \/ I_S \/ [:c_R,c_S:] \/ [:c_S,c_R:] by XBOOLE_1:4;
     hence thesis by NECKLA_2:def 3;
    end;
    end;
    hence thesis by NECKLACE:def 4;
   end;
end;

registration
  let R,S be irreflexive RelStr;
  cluster union_of(R,S) -> irreflexive;
  correctness
   proof
    set U = union_of(R,S),
        c_U = the carrier of U,
        I_U = the InternalRel of U,
        c_R = the carrier of R,
        c_S = the carrier of S;
      for x be set st x in c_U holds not [x,x] in I_U
     proof
      let x be set such that x in c_U;
      assume not thesis;
      then A3: [x,x] in (the InternalRel of R) \/ the InternalRel of S
                                                          by NECKLA_2:def 2;
      per cases by A3,XBOOLE_0:def 2;
      suppose A4: [x,x] in the InternalRel of R;
       then x in c_R by ZFMISC_1:106;
      hence thesis by A4,NECKLACE:def 6;
      end;
      suppose A6: [x,x] in the InternalRel of S;
       then x in c_S by ZFMISC_1:106;
      hence thesis by A6,NECKLACE:def 6;
      end;
      end;
    hence thesis by NECKLACE:def 6;
   end;
end;

theorem
    for R,S be irreflexive RelStr st the carrier of R misses the carrier of S
 holds sum_of(R,S) is irreflexive
proof
 let R,S be irreflexive RelStr such that
 A1: the carrier of R misses the carrier of S;
    for x be set st x in the carrier of sum_of(R,S)
    holds not [x,x] in the InternalRel of sum_of(R,S)
   proof
    let x be set;
    set I_R = the InternalRel of R,
        I_S = the InternalRel of S,
        RS = [:the carrier of R,the carrier of S:],
        SR = [:the carrier of S,the carrier of R:];
    assume x in the carrier of sum_of(R,S);
    assume not thesis;
    then [x,x] in I_R \/ I_S \/ RS \/ SR by NECKLA_2:def 3;
    then [x,x] in I_R \/ I_S \/ RS or [x,x] in SR by XBOOLE_0:def 2;
    then A6: [x,x] in I_R \/ I_S or [x,x] in RS or [x,x] in SR by XBOOLE_0:def
2;
    per cases by A6,XBOOLE_0:def 2;
    suppose A7: [x,x] in I_R;
     then x in the carrier of R by ZFMISC_1:106;
    hence thesis by A7,NECKLACE:def 6;
    end;
    suppose A9: [x,x] in I_S;
     then x in the carrier of S by ZFMISC_1:106;
    hence thesis by A9,NECKLACE:def 6;
    end;
    suppose [x,x] in RS;
     then x in the carrier of R & x in the carrier of S by ZFMISC_1:106;
    hence thesis by XBOOLE_0:3,A1;
    end;
    suppose [x,x] in SR;
     then x in the carrier of S & x in the carrier of R by ZFMISC_1:106;
    hence thesis by XBOOLE_0:3,A1;
   end;
   end;
  hence thesis by NECKLACE:def 6;
end;

theorem Th8:
  for R1,R2 being RelStr holds
  union_of(R1,R2) = union_of(R2,R1) & sum_of(R1,R2) = sum_of(R2,R1)
proof
 let R1,R2 be RelStr;
 set U1 = union_of(R1,R2),
     S1 = sum_of(R1,R2);
 A1: the carrier of U1 = (the carrier of R2) \/ the carrier of R1
     by NECKLA_2:def 2;
 A2: the InternalRel of U1 = (the InternalRel of R2) \/ the InternalRel of R1
     by NECKLA_2:def 2;
  A3: the carrier of S1 = (the carrier of R2) \/ the carrier of R1
     by NECKLA_2:def 3;
   the InternalRel of S1 = (the InternalRel of R1) \/ (the InternalRel of R2)
          \/ [:the carrier of R1, the carrier of R2:]
          \/ [:the carrier of R2, the carrier of R1:] by NECKLA_2:def 3
     .=  (the InternalRel of R2) \/ (the InternalRel of R1)
          \/ [:the carrier of R2, the carrier of R1:]
          \/ [:the carrier of R1, the carrier of R2:] by XBOOLE_1:4;
 hence thesis by A1,A2,A3,NECKLA_2:def 2,def 3;
end;

theorem Th9:
  for G being irreflexive RelStr, G1,G2 being RelStr st
   ( G = union_of(G1,G2) or G = sum_of(G1,G2) )
   holds G1 is irreflexive & G2 is irreflexive
proof
 let G be irreflexive RelStr, G1,G2 be RelStr;
 assume A1: G = union_of(G1,G2) or G = sum_of(G1,G2);
 per cases by A1;
 suppose A2: G = union_of(G1,G2);
 assume A3: not thesis;
 thus thesis
  proof
   per cases by A3;
   suppose not G1 is irreflexive;
    then consider x being set such that A5: x in the carrier of G1 and
         A6: [x,x] in the InternalRel of G1 by NECKLACE:def 6;
      x in (the carrier of G1) \/ the carrier of G2 by XBOOLE_0:def 2,A5;
    then A8: x in the carrier of G by NECKLA_2:def 2,A2;
      [x,x] in (the InternalRel of G1) \/ the InternalRel of G2
                by XBOOLE_0:def 2,A6;
    then [x,x] in the InternalRel of G by NECKLA_2:def 2,A2;
   hence thesis by A8,NECKLACE:def 6;
   end;
   suppose not G2 is irreflexive;
    then consider x being set such that A12: x in the carrier of G2 and
         A13: [x,x] in the InternalRel of G2 by NECKLACE:def 6;
      x in (the carrier of G1) \/ the carrier of G2 by XBOOLE_0:def 2,A12;
    then A15: x in the carrier of G by NECKLA_2:def 2,A2;
      [x,x] in (the InternalRel of G1) \/ the InternalRel of G2
                by XBOOLE_0:def 2,A13;
    then [x,x] in the InternalRel of G by NECKLA_2:def 2,A2;
   hence thesis by A15,NECKLACE:def 6;
  end;
  end;
 end;
 suppose A18: G = sum_of(G1,G2);
 assume A19: not thesis;
 thus thesis
  proof
   per cases by A19;
   suppose not G1 is irreflexive;
    then consider x being set such that A21: x in the carrier of G1 and
         A22: [x,x] in the InternalRel of G1 by NECKLACE:def 6;
      x in (the carrier of G1) \/ the carrier of G2 by XBOOLE_0:def 2,A21;
    then A24: x in the carrier of G by NECKLA_2:def 3,A18;
      [x,x] in (the InternalRel of G1) \/ the InternalRel of G2
                by XBOOLE_0:def 2,A22;
    then [x,x] in (the InternalRel of G1) \/ (the InternalRel of G2)
                 \/ [:the carrier of G1,the carrier of G2:] by XBOOLE_0:def 2;
    then [x,x] in (the InternalRel of G1) \/ (the InternalRel of G2)
                 \/ [:the carrier of G1,the carrier of G2:]
                 \/ [:the carrier of G2,the carrier of G1:] by XBOOLE_0:def 2;
    then [x,x] in the InternalRel of G by NECKLA_2:def 3,A18;
   hence thesis by A24,NECKLACE:def 6;
   end;
   suppose not G2 is irreflexive;
    then consider x being set such that A30: x in the carrier of G2 and
         A31: [x,x] in the InternalRel of G2 by NECKLACE:def 6;
      x in (the carrier of G1) \/ the carrier of G2 by XBOOLE_0:def 2,A30;
    then A33: x in the carrier of G by NECKLA_2:def 3,A18;
      [x,x] in (the InternalRel of G1) \/ the InternalRel of G2
                by XBOOLE_0:def 2,A31;
    then [x,x] in (the InternalRel of G1) \/ (the InternalRel of G2)
                 \/ [:the carrier of G1,the carrier of G2:] by XBOOLE_0:def 2;
    then [x,x] in (the InternalRel of G1) \/ (the InternalRel of G2)
                 \/ [:the carrier of G1,the carrier of G2:]
                 \/ [:the carrier of G2,the carrier of G1:] by XBOOLE_0:def 2;
    then [x,x] in the InternalRel of G by NECKLA_2:def 3,A18;
   hence thesis by A33,NECKLACE:def 6;
  end;
  end;
end;
end;

theorem Th10:
  for G being non empty RelStr, H1,H2 being RelStr
  st the carrier of H1 misses the carrier of H2 &
   ( the RelStr of G = union_of(H1,H2) or the RelStr of G = sum_of(H1,H2) )
  holds H1 is full SubRelStr of G & H2 is full SubRelStr of G
proof
 let G be non empty RelStr;
 let H1,H2 be RelStr;
 assume that A1: the carrier of H1 misses the carrier of H2 and
A2:  the RelStr of G = union_of(H1,H2) or the RelStr of G = sum_of(H1,H2);
 set cH1 = the carrier of H1,
     cH2 = the carrier of H2,
     IH1 = the InternalRel of H1,
     IH2 = the InternalRel of H2,
     H1H2 = [:cH1,cH2:],
     H2H1 = [:cH2,cH1:];
 per cases by A2;
 suppose A3: the RelStr of G = union_of(H1,H2); then
X5: the InternalRel of G = IH1 \/ IH2 by NECKLA_2:def 2;
    then X4: IH1 c= the InternalRel of G by XBOOLE_1:7;
X6: the carrier of G = (the carrier of H1) \/ (the carrier of H2)
      by A3,NECKLA_2:def 2;then
X2: the carrier of H1 c= the carrier of G by XBOOLE_1:7;
X3: the carrier of H2 c= the carrier of G by X6,XBOOLE_1:7;
x1: the InternalRel of H2 c= the InternalRel of G by X5,XBOOLE_1:7;
  A4: IH1 = (the InternalRel of G)|_2 cH1
   proof
    thus IH1 c= (the InternalRel of G)|_2 cH1
     proof
      let a be set;
      assume A5: a in IH1;
        the InternalRel of G = IH1 \/ IH2 by A3,NECKLA_2:def 2;
      then IH1 c= the InternalRel of G by XBOOLE_1:7;
       then a in (the InternalRel of G) /\ [:cH1,cH1:] by A5,XBOOLE_0:def 3;
      hence thesis by WELLORD1:def 6;
     end;
    let a be set;
    assume a in (the InternalRel of G)|_2 cH1;
    then a in (the InternalRel of G) /\ [:cH1,cH1:] by WELLORD1:def 6;
    then A11: a in the InternalRel of G & a in [:cH1,cH1:] by XBOOLE_0:def 3;
    then A12: a in IH1 \/ IH2 by NECKLA_2:def 2,A3;
      per cases by A12,XBOOLE_0:def 2;
      suppose a in IH1;
      hence thesis;
      end;
      suppose a in IH2;
       then consider x,y being set such that
       A15: a = [x,y] & x in cH2 & y in cH2 by RELSET_1:6;
       consider x1,y1 being set such that
       A16: x1 in cH1 & y1 in cH1 & a = [x1,y1] by A11,ZFMISC_1:def 2;
        x in cH1 & x in cH2 by A15,A16,ZFMISC_1:33;
      hence thesis by A1,XBOOLE_0:3;
     end;
   end;
    IH2 = (the InternalRel of G)|_2 cH2
   proof
    thus IH2 c= (the InternalRel of G)|_2 cH2
     proof
      let a be set;
      assume A20: a in IH2;
        the InternalRel of G = IH1 \/ IH2 by NECKLA_2:def 2,A3;
      then IH2 c= the InternalRel of G by XBOOLE_1:7;
       then a in (the InternalRel of G) /\ [:cH2,cH2:] by A20,XBOOLE_0:def 3;
      hence thesis by WELLORD1:def 6;
     end;
    let a be set;
    assume a in (the InternalRel of G)|_2 cH2;
    then a in (the InternalRel of G) /\ [:cH2,cH2:] by WELLORD1:def 6;
    then A26: a in the InternalRel of G & a in [:cH2,cH2:] by XBOOLE_0:def 3;
    then A27: a in IH1 \/ IH2 by NECKLA_2:def 2,A3;
      per cases by A27,XBOOLE_0:def 2;
      suppose a in IH1;
       then consider x,y being set such that
       A29: a = [x,y] & x in cH1 & y in cH1 by RELSET_1:6;
       consider x1,y1 being set such that
       A30: x1 in cH2 & y1 in cH2 & a = [x1,y1] by A26,ZFMISC_1:def 2;
        x = x1 & y = y1 by A29,A30,ZFMISC_1:33;
       then cH1 /\ cH2 <> {} by A29,A30,XBOOLE_0:def 3;
      hence thesis by A1,XBOOLE_0:def 7;
      end;
      suppose a in IH2;
      hence thesis;
     end;
   end;
 hence thesis by x1,A4,YELLOW_0:def 14,X2,X3,X4,YELLOW_0:def 13;
 end;
 suppose A34: the RelStr of G = sum_of(H1,H2);then
X5: the InternalRel of G = IH1 \/ IH2 \/ [:cH1, cH2:] \/ [:cH2, cH1:]
      by NECKLA_2:def 3; then
Y1: IH1 \/ IH2 \/ [:cH1, cH2:] c= the InternalRel of G by XBOOLE_1:7;
    IH1 c= IH1 \/ (IH2 \/ [:cH1, cH2:]) by XBOOLE_1:7;then
    IH1 c= IH1 \/ IH2 \/ [:cH1, cH2:] by XBOOLE_1:4; then
    X4: IH1 c= the InternalRel of G by Y1,XBOOLE_1:1;
X6: the carrier of G = (the carrier of H1) \/ (the carrier of H2)
      by A34,NECKLA_2:def 3;then
X2: the carrier of H1 c= the carrier of G by XBOOLE_1:7;
X3: the carrier of H2 c= the carrier of G by X6,XBOOLE_1:7;
    IH2 c= IH2 \/ [:cH1, cH2:] by XBOOLE_1:7;then
    IH2 c= IH1 \/ (IH2 \/ [:cH1, cH2:]) by XBOOLE_1:10;then
    IH2 c= IH1 \/ IH2 \/ [:cH1, cH2:] by XBOOLE_1:4;then
x1: IH2 c= IH1 \/ IH2 \/ [:cH1, cH2:] \/ [:cH2, cH1:] by XBOOLE_1:10;
  A35: IH1 = (the InternalRel of G)|_2 cH1
  proof
   thus IH1 c= (the InternalRel of G)|_2 cH1
    proof
     let a be set;
     assume A36: a in IH1;
     the InternalRel of G = IH1 \/ IH2 \/ H1H2 \/ H2H1 by NECKLA_2:def 3,A34
                     .= IH1 \/ (IH2 \/ H1H2 \/ H2H1) by XBOOLE_1:113;
     then IH1 c= the InternalRel of G by XBOOLE_1:7;
      then a in (the InternalRel of G) /\ [:cH1,cH1:] by A36,XBOOLE_0:def 3;
     hence thesis by WELLORD1:def 6;
    end;
   let a be set;
   assume a in (the InternalRel of G)|_2 cH1;
   then a in (the InternalRel of G) /\ [:cH1,cH1:] by WELLORD1:def 6;
   then A42: a in the InternalRel of G & a in [:cH1,cH1:] by XBOOLE_0:def 3;
   then a in IH1 \/ IH2 \/ H1H2 \/ H2H1 by NECKLA_2:def 3,A34;
   then a in IH1 \/ (IH2 \/ H1H2 \/ H2H1) by XBOOLE_1:113;
   then a in IH1 or a in IH2 \/ H1H2 \/ H2H1 by XBOOLE_0:def 2;
   then a in IH1 or a in IH2 \/ (H1H2 \/ H2H1) by XBOOLE_1:4;
   then A47: a in IH1 or a in IH2 or a in H1H2 \/ H2H1 by XBOOLE_0:def 2;
     per cases by A47,XBOOLE_0:def 2;
     suppose a in IH1;
     hence thesis;
     end;
     suppose a in IH2;
      then consider x,y being set such that
      A50: a = [x,y] & x in cH2 & y in cH2 by RELSET_1:6;
      consider x1,y1 being set such that
      A51: x1 in cH1 & y1 in cH1 & a = [x1,y1] by ZFMISC_1:def 2,A42;
       x = x1 & y = y1 by A50,A51,ZFMISC_1:33;
      then cH1 /\ cH2 <> {} by A50,A51,XBOOLE_0:def 3;
     hence thesis by A1,XBOOLE_0:def 7;
     end;
     suppose a in H1H2;
      then consider x,y being set such that
      A55: x in cH1 & y in cH2 & a = [x,y] by ZFMISC_1:def 2;
      consider x1,y1 being set such that
      A56: x1 in cH1 & y1 in cH1 & a = [x1,y1] by ZFMISC_1:def 2,A42;
       x = x1 & y = y1 by A55,A56,ZFMISC_1:33;
      then cH1 /\ cH2 <> {} by A55,A56,XBOOLE_0:def 3;
     hence thesis by A1,XBOOLE_0:def 7;
     end;
     suppose a in H2H1;
      then consider x,y being set such that
      A60: x in cH2 & y in cH1 & a = [x,y] by ZFMISC_1:def 2;
      consider x1,y1 being set such that
      A61: x1 in cH1 & y1 in cH1 & a = [x1,y1] by ZFMISC_1:def 2,A42;
       x = x1 & y = y1 by A60,A61,ZFMISC_1:33;
      then cH1 /\ cH2 <> {} by A60,A61,XBOOLE_0:def 3;
     hence thesis by A1,XBOOLE_0:def 7;
    end;
  end;
    IH2 = (the InternalRel of G)|_2 cH2
  proof
   thus IH2 c= (the InternalRel of G)|_2 cH2
    proof
     let a be set;
     assume A65: a in IH2;
       the InternalRel of G = IH1 \/ IH2 \/ H1H2 \/ H2H1 by NECKLA_2:def 3,
A34;
     then the InternalRel of G = IH2 \/ (IH1 \/ H1H2 \/ H2H1) by XBOOLE_1:113;
     then IH2 c= the InternalRel of G by XBOOLE_1:7;
      then a in (the InternalRel of G) /\ [:cH2,cH2:] by A65,XBOOLE_0:def 3;
     hence thesis by WELLORD1:def 6;
    end;
   let a be set;
   assume a in (the InternalRel of G)|_2 cH2;
   then a in (the InternalRel of G) /\ [:cH2,cH2:] by WELLORD1:def 6;
   then A72: a in the InternalRel of G & a in [:cH2,cH2:] by XBOOLE_0:def 3;
    then a in IH1 \/ IH2 \/ H1H2 \/ H2H1 by A34,NECKLA_2:def 3;
   then a in IH1 \/ (IH2 \/ H1H2 \/ H2H1) by XBOOLE_1:113;
   then a in IH1 or a in IH2 \/ H1H2 \/ H2H1 by XBOOLE_0:def 2;
   then a in IH1 or a in IH2 \/ (H1H2 \/ H2H1) by XBOOLE_1:4;
   then A77: a in IH1 or a in IH2 or a in H1H2 \/ H2H1 by XBOOLE_0:def 2;
     per cases by A77,XBOOLE_0:def 2;
     suppose a in IH1;
      then consider x,y being set such that
      A79: a = [x,y] & x in cH1 & y in cH1 by RELSET_1:6;
      consider x1,y1 being set such that
      A80: x1 in cH2 & y1 in cH2 & a = [x1,y1] by A72,ZFMISC_1:def 2;
       x = x1 & y = y1 by A79,A80,ZFMISC_1:33;
      then cH1 /\ cH2 <> {} by A79,A80,XBOOLE_0:def 3;
     hence thesis by A1,XBOOLE_0:def 7;
     end;
     suppose a in IH2;
     hence thesis;
     end;
     suppose a in H1H2;
      then consider x,y being set such that
      A85: x in cH1 & y in cH2 & a = [x,y] by ZFMISC_1:def 2;
      consider x1,y1 being set such that
      A86: x1 in cH2 & y1 in cH2 & a = [x1,y1] by A72,ZFMISC_1:def 2;
       x = x1 & y = y1 by A85,A86,ZFMISC_1:33;
      then cH1 /\ cH2 <> {} by A85,A86,XBOOLE_0:def 3;
     hence thesis by A1,XBOOLE_0:def 7;
     end;
     suppose a in H2H1;
      then consider x,y being set such that
      A90: x in cH2 & y in cH1 & a = [x,y] by ZFMISC_1:def 2;
      consider x1,y1 being set such that
      A91: x1 in cH2 & y1 in cH2 & a = [x1,y1] by A72,ZFMISC_1:def 2;
       x = x1 & y = y1 by A90,A91,ZFMISC_1:33;
      then cH1 /\ cH2 <> {} by A90,A91,XBOOLE_0:def 3;
     hence thesis by A1,XBOOLE_0:def 7;
    end;
  end;
 hence thesis by x1,A35,YELLOW_0:def 14,X5,X2,X3,X4,YELLOW_0:def 13;
end;
end;

begin :: Theorems relating to the complement of RelStr

theorem Th11:
 the InternalRel of ComplRelStr Necklace 4
         = {[0,2],[2,0],[0,3],[3,0],[1,3],[3,1]}
proof
 set N4 = Necklace 4,
     cN4 = the carrier of N4,
     CmpN4 = ComplRelStr N4;
A1: the carrier of Necklace 4 = {0,1,2,3} by NECKLACE:2,21;
 thus the InternalRel of CmpN4 c= {[0,2],[2,0],[0,3],[3,0],[1,3],[3,1]}
  proof
   let x be set;
   assume x in the InternalRel of CmpN4;
   then x in (the InternalRel of N4)` \ id cN4 by NECKLACE:def 9;
   then A5: x in (the InternalRel of N4)` & not x in id cN4 by XBOOLE_0:def 4;
   then A6: x in [:cN4,cN4:] \ (the InternalRel of N4) & not x in id cN4
             by SUBSET_1:def 5;
   then x in [:cN4,cN4:] & not x in (the InternalRel of N4) &
            not x in id cN4 by XBOOLE_0:def 4;
   then consider a1,b1 being set such that
   A8: a1 in cN4 & b1 in cN4 & x = [a1,b1] by ZFMISC_1:def 2;
   per cases by A8,ENUMSET1:18,A1;
   suppose a1 = 0 & b1 = 0;
   hence thesis by A5,A8,RELAT_1:def 10;
   end;
   suppose a1 = 0 & b1 = 1;
    then x in the InternalRel of N4 by ENUMSET1:29,NECKLA_2:2,A8;
   hence thesis by A6,XBOOLE_0:def 4;
   end;
   suppose a1 = 0 & b1 = 2;
   hence thesis by ENUMSET1:29,A8;
   end;
   suppose a1 = 0 & b1 = 3;
   hence thesis by ENUMSET1:29,A8;
   end;
   suppose a1 = 1 & b1 = 0;
    then x in the InternalRel of N4 by ENUMSET1:29,NECKLA_2:2,A8;
   hence thesis by A6,XBOOLE_0:def 4;
   end;
   suppose a1 = 2 & b1 = 0;
   hence thesis by ENUMSET1:29,A8;
   end;
   suppose a1 = 3 & b1 = 0;
   hence thesis by ENUMSET1:29,A8;
   end;
   suppose a1 = 1 & b1 = 1;
   hence thesis by A5,A8,RELAT_1:def 10;
   end;
   suppose a1 = 1 & b1 = 2;
    then x in the InternalRel of N4 by ENUMSET1:29,NECKLA_2:2,A8;
   hence thesis by A6,XBOOLE_0:def 4;
   end;
   suppose a1 = 1 & b1 = 3;
   hence thesis by ENUMSET1:29,A8;
   end;
   suppose a1 = 2 & b1 = 2;
   hence thesis by A5,A8,RELAT_1:def 10;
   end;
   suppose a1 = 2 & b1 = 1;
    then x in the InternalRel of N4 by ENUMSET1:29,NECKLA_2:2,A8;
   hence thesis by A6,XBOOLE_0:def 4;
   end;
   suppose a1 = 2 & b1 = 3;
    then x in the InternalRel of N4 by ENUMSET1:29,NECKLA_2:2,A8;
   hence thesis by A6,XBOOLE_0:def 4;
   end;
   suppose a1 = 3 & b1 = 3;
   hence thesis by A5,A8,RELAT_1:def 10;
   end;
   suppose a1 = 3 & b1 = 1;
   hence thesis by ENUMSET1:29,A8;
   end;
   suppose a1 = 3 & b1 = 2;
    then x in the InternalRel of N4 by ENUMSET1:29,NECKLA_2:2,A8;
   hence thesis by A6,XBOOLE_0:def 4;
  end;
  end;
 let a be set;
 assume A31: a in {[0,2],[2,0],[0,3],[3,0],[1,3],[3,1]};
 per cases by A31,ENUMSET1:28;
 suppose A32: a = [0,2];
  A39: not a in the InternalRel of N4
   proof
    assume A40: not thesis;
     per cases by A40,ENUMSET1:28,NECKLA_2:2;
     suppose a = [0,1];
     hence contradiction by A32,ZFMISC_1:33;
     end;
     suppose a = [1,0];
     hence contradiction by A32,ZFMISC_1:33;
     end;
     suppose a = [1,2];
     hence contradiction by A32,ZFMISC_1:33;
     end;
     suppose a = [2,1];
     hence contradiction by A32,ZFMISC_1:33;
     end;
     suppose a = [2,3];
     hence contradiction by A32,ZFMISC_1:33;
     end;
     suppose a = [3,2];
     hence contradiction by A32,ZFMISC_1:33;
    end;
   end;
    0 in cN4 & 2 in cN4 by A1,ENUMSET1:19;
  then a in [:cN4,cN4:] & not a in (the InternalRel of N4) & not a in id
cN4
            by ZFMISC_1:106,RELAT_1:def 10,A39,A32;
  then a in [:cN4,cN4:]\ (the InternalRel of N4) & not a in id cN4
            by XBOOLE_0:def 4;
  then a in (the InternalRel of N4)` & not a in id cN4 by SUBSET_1:def 5;
  then a in (the InternalRel of N4)` \ id cN4 by XBOOLE_0:def 4;
 hence thesis by NECKLACE:def 9;
 end;
 suppose A52: a = [2,0];
  A59: not a in the InternalRel of N4
   proof
    assume A60: not thesis;
     per cases by A60,ENUMSET1:28,NECKLA_2:2;
     suppose a = [0,1];
     hence contradiction by A52,ZFMISC_1:33;
     end;
     suppose a = [1,0];
     hence contradiction by A52,ZFMISC_1:33;
     end;
     suppose a = [1,2];
     hence contradiction by A52,ZFMISC_1:33;
     end;
     suppose a = [2,1];
     hence contradiction by A52,ZFMISC_1:33;
     end;
     suppose a = [2,3];
     hence contradiction by A52,ZFMISC_1:33;
     end;
     suppose a = [3,2];
     hence contradiction by A52,ZFMISC_1:33;
    end;
   end;
    0 in cN4 & 2 in cN4 by A1,ENUMSET1:19;
  then a in [:cN4,cN4:] & not a in (the InternalRel of N4) & not a in id
cN4
            by ZFMISC_1:106,RELAT_1:def 10,A59,A52;
  then a in [:cN4,cN4:]\ (the InternalRel of N4) & not a in id cN4
            by XBOOLE_0:def 4;
  then a in (the InternalRel of N4)` & not a in id cN4 by SUBSET_1:def 5;
  then a in (the InternalRel of N4)` \ id cN4 by XBOOLE_0:def 4;
 hence thesis by NECKLACE:def 9;
 end;
 suppose A72: a = [0,3];
  A79: not a in the InternalRel of N4
   proof
    assume A80: not thesis;
     per cases by A80,ENUMSET1:28,NECKLA_2:2;
     suppose a = [0,1];
     hence contradiction by A72,ZFMISC_1:33;
     end;
     suppose a = [1,0];
     hence contradiction by A72,ZFMISC_1:33;
     end;
     suppose a = [1,2];
     hence contradiction by A72,ZFMISC_1:33;
     end;
     suppose a = [2,1];
     hence contradiction by A72,ZFMISC_1:33;
     end;
     suppose a = [2,3];
     hence contradiction by A72,ZFMISC_1:33;
     end;
     suppose a = [3,2];
     hence contradiction by A72,ZFMISC_1:33;
    end;
   end;
    0 in cN4 & 3 in cN4 by A1,ENUMSET1:19;
  then a in [:cN4,cN4:] & not a in (the InternalRel of N4) & not a in id
cN4
            by ZFMISC_1:106,RELAT_1:def 10,A79,A72;
  then a in [:cN4,cN4:]\ (the InternalRel of N4) & not a in id cN4
            by XBOOLE_0:def 4;
  then a in (the InternalRel of N4)` & not a in id cN4 by SUBSET_1:def 5;
  then a in (the InternalRel of N4)` \ id cN4 by XBOOLE_0:def 4;
 hence thesis by NECKLACE:def 9;
 end;
 suppose A92: a = [3,0];
  A99: not a in the InternalRel of N4
   proof
    assume A100: not thesis;
     per cases by A100,ENUMSET1:28,NECKLA_2:2;
     suppose a = [0,1];
     hence contradiction by A92,ZFMISC_1:33;
     end;
     suppose a = [1,0];
     hence contradiction by A92,ZFMISC_1:33;
     end;
     suppose a = [1,2];
     hence contradiction by A92,ZFMISC_1:33;
     end;
     suppose a = [2,1];
     hence contradiction by A92,ZFMISC_1:33;
     end;
     suppose a = [2,3];
     hence contradiction by A92,ZFMISC_1:33;
     end;
     suppose a = [3,2];
     hence contradiction by A92,ZFMISC_1:33;
    end;
   end;
    0 in cN4 & 3 in cN4 by A1,ENUMSET1:19;
  then a in [:cN4,cN4:] & not a in (the InternalRel of N4) & not a in id
cN4
            by ZFMISC_1:106,RELAT_1:def 10,A99,A92;
  then a in [:cN4,cN4:]\ (the InternalRel of N4) & not a in id cN4
            by XBOOLE_0:def 4;
  then a in (the InternalRel of N4)` & not a in id cN4 by SUBSET_1:def 5;
  then a in (the InternalRel of N4)` \ id cN4 by XBOOLE_0:def 4;
 hence thesis by NECKLACE:def 9;
 end;
 suppose A112: a = [1,3];
  A119: not a in the InternalRel of N4
   proof
    assume A120: not thesis;
     per cases by A120,ENUMSET1:28,NECKLA_2:2;
     suppose a = [0,1];
     hence contradiction by A112,ZFMISC_1:33;
     end;
     suppose a = [1,0];
     hence contradiction by A112,ZFMISC_1:33;
     end;
     suppose a = [1,2];
     hence contradiction by A112,ZFMISC_1:33;
     end;
     suppose a = [2,1];
     hence contradiction by A112,ZFMISC_1:33;
     end;
     suppose a = [2,3];
     hence contradiction by A112,ZFMISC_1:33;
     end;
     suppose a = [3,2];
     hence contradiction by A112,ZFMISC_1:33;
    end;
   end;
    1 in cN4 & 3 in cN4 by A1,ENUMSET1:19;
  then a in [:cN4,cN4:] & not a in (the InternalRel of N4) & not a in id
cN4
            by ZFMISC_1:106,RELAT_1:def 10,A119,A112;
  then a in [:cN4,cN4:]\ (the InternalRel of N4) & not a in id cN4
            by XBOOLE_0:def 4;
  then a in (the InternalRel of N4)` & not a in id cN4 by SUBSET_1:def 5;
  then a in (the InternalRel of N4)` \ id cN4 by XBOOLE_0:def 4;
 hence thesis by NECKLACE:def 9;
 end;
 suppose A132: a = [3,1];
  A139: not a in the InternalRel of N4
   proof
    assume A140: not thesis;
     per cases by A140,ENUMSET1:28,NECKLA_2:2;
     suppose a = [0,1];
     hence contradiction by A132,ZFMISC_1:33;
     end;
     suppose a = [1,0];
     hence contradiction by A132,ZFMISC_1:33;
     end;
     suppose a = [1,2];
     hence contradiction by A132,ZFMISC_1:33;
     end;
     suppose a = [2,1];
     hence contradiction by A132,ZFMISC_1:33;
     end;
     suppose a = [2,3];
     hence contradiction by A132,ZFMISC_1:33;
     end;
     suppose a = [3,2];
     hence contradiction by A132,ZFMISC_1:33;
    end;
   end;
    1 in cN4 & 3 in cN4 by A1,ENUMSET1:19;
  then a in [:cN4,cN4:] & not a in (the InternalRel of N4) & not a in id
cN4
            by ZFMISC_1:106,RELAT_1:def 10,A139,A132;
  then a in [:cN4,cN4:]\ (the InternalRel of N4) & not a in id cN4
            by XBOOLE_0:def 4;
  then a in (the InternalRel of N4)` & not a in id cN4 by SUBSET_1:def 5;
  then a in (the InternalRel of N4)` \ id cN4 by XBOOLE_0:def 4;
 hence thesis by NECKLACE:def 9;
end;
end;

registration
  let R be RelStr;
  cluster ComplRelStr R -> irreflexive;
  correctness
   proof
    set R1 = ComplRelStr R;
      for x being set st x in the carrier of R1
     holds not [x,x] in the InternalRel of R1
      proof
       let x be set;
       assume x in the carrier of R1;
       then A3: x in the carrier of R by NECKLACE:def 9;
         not [x,x] in the InternalRel of R1
        proof
         assume [x,x] in the InternalRel of R1;
         then [x,x] in (the InternalRel of R)` \ id (the carrier of R)
              by NECKLACE:def 9;
         then [x,x] in (the InternalRel of R)` &
            not [x,x] in id (the carrier of R) by XBOOLE_0:def 4;
         hence contradiction by RELAT_1:def 10,A3;
        end;
       hence thesis;
      end;
    hence thesis by NECKLACE:def 6;
   end;
end;

registration
  let R be symmetric RelStr;
  cluster ComplRelStr R -> symmetric;
  correctness
   proof
     set S = ComplRelStr R;
     let x,y be set;
     assume A1: x in the carrier of S & y in the carrier of S &
       [x,y] in the InternalRel of S;
     per cases;
     suppose x = y;
     hence thesis by A1;
     end;
     suppose A3: x <> y;
      A4: x in the carrier of R & y in the carrier of R by A1,NECKLACE:def 9;
        [x,y] in (the InternalRel of R)` \ id (the carrier of R)
               by NECKLACE:def 9,A1;
      then [x,y] in (the InternalRel of R)` &
      not [x,y] in id (the carrier of R) by XBOOLE_0:def 4;
      then [x,y] in [:the carrier of R,the carrier of R:] \ (the
InternalRel of R) by SUBSET_1:def 5;
      then A8: [x,y] in [:the carrier of R,the carrier of R:] &
      not [x,y] in the InternalRel of R by XBOOLE_0:def 4;
        the InternalRel of R is_symmetric_in the carrier of R by NECKLACE:
def 4;
      then [y,x] in [:the carrier of R,the carrier of R:] &
      not [y,x] in the InternalRel of R by ZFMISC_1:106,RELAT_2:def 3,A8,A4;
      then [y,x] in [:the carrier of R,the carrier of R:] \ (the
InternalRel of R)
               by XBOOLE_0:def 4;
      then [y,x] in (the InternalRel of R)` &
      not [y,x] in id (the carrier of R)  by RELAT_1:def 10,A3,SUBSET_1:def 5;
      then [y,x] in (the InternalRel of R)` \ id (the carrier of R)
                     by XBOOLE_0:def 4;
     hence thesis by NECKLACE:def 9;
   end;
   end;
end;

theorem Th12:  for R be RelStr holds
  the InternalRel of R misses the InternalRel of ComplRelStr R
proof
 let R be RelStr;
 assume not thesis;
 then (the InternalRel of R) /\ the InternalRel of ComplRelStr R <> {}
       by XBOOLE_0:def 7;
 then consider a being set such that
 A3: a in (the InternalRel of R) /\ the InternalRel of ComplRelStr R
       by XBOOLE_0:def 1;
   a in the InternalRel of ComplRelStr R by A3,XBOOLE_0:def 3;
 then a in (the InternalRel of R)` \ id (the carrier of R)
            by NECKLACE:def 9;
 then a in (the InternalRel of R)` & not a in id (the carrier of R)
            by XBOOLE_0:def 4;
 then a in [:the carrier of R,the carrier of R:] \ (the InternalRel of R)
        by SUBSET_1:def 5;
 then not a in the InternalRel of R by XBOOLE_0:def 4;
 hence thesis by A3,XBOOLE_0:def 3;
end;

theorem Th13: for R being RelStr holds
  id the carrier of R misses the InternalRel of ComplRelStr R
proof
 let R be RelStr;
 assume not thesis;
  then id (the carrier of R) /\ the InternalRel of ComplRelStr R <> {}
      by XBOOLE_0:def 7;
 then consider a being set such that
 A3: a in id (the carrier of R) /\ the InternalRel of ComplRelStr R
          by XBOOLE_0:def 1;
 A4: a in id (the carrier of R) & a in the InternalRel of ComplRelStr R
          by A3,XBOOLE_0:def 3;
 then a in (the InternalRel of R)` \ id the carrier of R by NECKLACE:def 9;
 hence contradiction by A4,XBOOLE_0:def 4;
end;

theorem Th14:
 for G being RelStr holds
  [:the carrier of G,the carrier of G:] = id (the carrier of G) \/
       (the InternalRel of G) \/ (the InternalRel of ComplRelStr G)
proof
 let G be RelStr;
 set idcG = id the carrier of G,
     IG = the InternalRel of G,
     ICmpG = the InternalRel of ComplRelStr G,
     cG = the carrier of G;
 thus [:cG,cG:] c= idcG \/ IG \/ ICmpG
 proof
 let a be set;
 assume A1: a in [:cG,cG:];
 then consider x,y being set such that
 A2: x in cG & y in cG & a = [x,y] by ZFMISC_1:def 2;
 per cases;
 suppose A3: x = y;
   [x,x] in id cG by A2,RELAT_1:def 10;
  then a in id cG \/ IG by A2,A3,XBOOLE_0:def 2;
 hence thesis by XBOOLE_0:def 2;
 end;
 suppose x <> y;
  then A7: not a in id cG by RELAT_1:def 10,A2;
 thus thesis
  proof
   per cases;
   suppose a in IG;
    then a in id cG \/ IG by XBOOLE_0:def 2;
   hence thesis by XBOOLE_0:def 2;
   end;
   suppose not a in IG;
    then a in [:cG,cG:] \ IG by XBOOLE_0:def 4,A1;
    then a in IG` by SUBSET_1:def 5;
    then a in IG` \ id cG by A7,XBOOLE_0:def 4;
    then a in ICmpG by NECKLACE:def 9;
    then a in IG \/ ICmpG by XBOOLE_0:def 2;
    then a in id cG \/ (IG \/ ICmpG) by XBOOLE_0:def 2;
   hence thesis by XBOOLE_1:4;
  end;
  end;
 end;
 end;
   let a be set;
   assume a in idcG \/ IG \/ ICmpG;
   then A18: a in id cG \/ IG or a in ICmpG by XBOOLE_0:def 2;
   per cases by A18,XBOOLE_0:def 2;
   suppose a in id cG;
    then consider x,y being set such that
    A20: a = [x,y] & x in cG & y in cG by RELSET_1:6;
   thus thesis by A20,ZFMISC_1:106;
   end;
   suppose a in IG;
    then consider x,y being set such that
    A22: a = [x,y] & x in cG & y in cG by RELSET_1:6;
   thus thesis by A22,ZFMISC_1:106;
   end;
   suppose a in ICmpG;
    then a in IG` \ id cG by NECKLACE:def 9;
   hence thesis;
end;
end;

theorem Th15:
  for G being strict irreflexive RelStr st G is trivial
   holds ComplRelStr G = G
proof
 let G be strict irreflexive RelStr;
 set CG = ComplRelStr G;
 assume G is trivial;
 then A2: the carrier of G is trivial by REALSET1:def 13;
 per cases by A2,REALSET1:def 12;
 suppose A3: the carrier of G is empty;
  then the InternalRel of G c= [:{},{}:];
  then A5: the InternalRel of G c= {} by ZFMISC_1:113;
    {} c= the InternalRel of G by XBOOLE_1:2;
  then A7: the InternalRel of G = {} by XBOOLE_0:def 10,A5;
    the InternalRel of CG = (the InternalRel of G)` \
   id (the carrier of G) by NECKLACE:def 9;
  then the InternalRel of CG = ([:{},{}:] \ {}) \ id {}
      by A7,A3,SUBSET_1:def 5;
  then the InternalRel of CG = ({} \ {}) \ id {} by ZFMISC_1:113;
 hence thesis by A7,NECKLACE:def 9;
 end;
 suppose ex x being set st the carrier of G = {x};
  then consider x being set such that
  A12: the carrier of G = {x};
    the InternalRel of G c= [:{x},{x}:] by A12;
  then the InternalRel of G c= {[x,x]} by ZFMISC_1:35;
  then A15: the InternalRel of G = {} or the InternalRel of G = {[x,x]}
     by ZFMISC_1:39;
  A16: the InternalRel of G <> {[x,x]}
   proof
    assume not thesis;
    then A18: [x,x] in the InternalRel of G by TARSKI:def 1;
      x in the carrier of G by A12,TARSKI:def 1;
    hence contradiction by NECKLACE:def 6,A18;
   end;
  A20: the carrier of CG = {x} by A12,NECKLACE:def 9;
    the InternalRel of CG = (the InternalRel of G)` \
         id (the carrier of G) by NECKLACE:def 9;
  then the InternalRel of CG = ([:{x},{x}:] \ {}) \ id {x}
         by A12,A15,A16,SUBSET_1:def 5;
  then the InternalRel of CG = {[x,x]} \ id {x} by ZFMISC_1:35;
  then the InternalRel of CG = {[x,x]} \ {[x,x]} by SYSREL:30;
 hence thesis by A12,A15,A16,A20,XBOOLE_1:37;
end;
end;

theorem Th16:
  for G being strict irreflexive RelStr
   holds ComplRelStr (ComplRelStr G) = G
proof
 let G be strict irreflexive RelStr;
 set CCmpG = ComplRelStr (ComplRelStr G),
      CmpG = ComplRelStr G,
        cG = the carrier of G,
        IG = the InternalRel of G,
     ICmpG = the InternalRel of CmpG,
    ICCmpG = the InternalRel of CCmpG;
 A1: cG = the carrier of CmpG by NECKLACE:def 9
   .= the carrier of CCmpG by NECKLACE:def 9;
 A2: cG = the carrier of CmpG by NECKLACE:def 9;
 A3: id cG misses IG
  proof
   assume not thesis;
   then id cG /\ IG <> {} by XBOOLE_0:def 7;
   then consider a being set such that
   A6: a in id cG /\ IG by XBOOLE_0:def 1;
   A7: a in id cG & a in IG by XBOOLE_0:def 3,A6;
   consider x,y being set such that
   A8: a = [x,y] & x in cG & y in cG by A6,RELSET_1:6;
    x = y by A7,A8,RELAT_1:def 10;
   hence contradiction by A7,A8,NECKLACE:def 6;
  end;
   ICCmpG = (ICmpG)` \ id the carrier of CmpG by NECKLACE:def 9
       .= [:the carrier of CmpG,the carrier of CmpG:] \ ICmpG \
           id the carrier of CmpG by SUBSET_1:def 5
       .= ([:cG,cG:] \ (IG` \ id cG)) \ id cG by NECKLACE:def 9,A2
       .= (([:cG,cG:] \ IG`) \/ ([:cG,cG:] /\ id cG)) \ id cG by XBOOLE_1:52
       .= ( ([:cG,cG:] \ IG`) \/ id cG ) \ id cG by XBOOLE_1:28
       .= ([:cG,cG:] \ IG`) \ id cG by XBOOLE_1:40
       .= ([:cG,cG:] \ ([:cG,cG:] \ IG)) \ id cG by SUBSET_1:def 5
       .= ([:cG,cG:] /\ IG) \ id cG by XBOOLE_1:48
       .= IG \ id cG by XBOOLE_1:28
       .= IG by A3,XBOOLE_1:83;
 hence thesis by A1;
end;

theorem Th17:
  for G1,G2 being RelStr st the carrier of G1 misses the carrier of G2
   holds ComplRelStr union_of(G1,G2) = sum_of(ComplRelStr G1, ComplRelStr G2)
 proof
  let G1,G2 be RelStr;
  assume A1: the carrier of G1 misses the carrier of G2;
A2: the carrier of ComplRelStr union_of(G1,G2) = the carrier of union_of(G1,G2)
                   by NECKLACE:def 9
     .= (the carrier of G1) \/ the carrier of G2 by NECKLA_2:def 2;
  A3: the carrier of sum_of(ComplRelStr G1, ComplRelStr G2) =
      (the carrier of ComplRelStr G1) \/ (the carrier of ComplRelStr G2)
                      by NECKLA_2:def 3
     .= (the carrier of G1) \/ the carrier of ComplRelStr G2 by NECKLACE:def 9
     .= (the carrier of G1) \/ the carrier of G2 by NECKLACE:def 9;
  set P = the InternalRel of ComplRelStr union_of(G1,G2),
      R = the InternalRel of sum_of(ComplRelStr G1,ComplRelStr G2),
      X1 = the InternalRel of ComplRelStr G1,
      X2 = the InternalRel of ComplRelStr G2,
      X3 = [:the carrier of ComplRelStr G1,the carrier of ComplRelStr G2:],
      X4 = [:the carrier of ComplRelStr G2,the carrier of ComplRelStr G1:],
      X5 = [:the carrier of G1, the carrier of G1:],
      X6 = [:the carrier of G2, the carrier of G2:],
      X7 = [:the carrier of G1, the carrier of G2:],
      X8 = [:the carrier of G2, the carrier of G1:];
    for a,b being set holds [a,b] in P iff [a,b] in R
  proof
   let a,b be set;
   set x = [a,b];
   thus x in P implies x in R
    proof
     assume x in P;
     then x in (the InternalRel of union_of(G1,G2))` \
      id the carrier of union_of(G1,G2) by NECKLACE:def 9;
     then A7: x in (the InternalRel of union_of(G1,G2))` &
      not x in id the carrier of union_of(G1,G2) by XBOOLE_0:def 4;
     then x in [:the carrier of union_of(G1,G2),the carrier of union_of(G1,
G2):]
       \ (the InternalRel of union_of(G1,G2)) by SUBSET_1:def 5;
 then A9: x in [:the carrier of union_of(G1,G2),the carrier of union_of(G1,G2)
:]
     & not x in (the InternalRel of union_of(G1,G2)) by XBOOLE_0:def 4;
     A10: the carrier of G1 = the carrier of ComplRelStr G1 &
         the carrier of G2 = the carrier of ComplRelStr G2 by NECKLACE:def 9;
       x in [:(the carrier of G1) \/ the carrier of G2,
            the carrier of union_of(G1,G2):] by A9,NECKLA_2:def 2;
     then x in [:(the carrier of G1) \/ the carrier of G2,
            (the carrier of G1) \/ the carrier of G2:] by NECKLA_2:def 2;
     then x in X5 \/ X3 \/ X4 \/ X6 by A10,ZFMISC_1:121;
     then x in X5 \/ (X3 \/ X4 \/ X6) by XBOOLE_1:113;
     then x in X5 or x in (X3 \/ X4 \/ X6) by XBOOLE_0:def 2;
     then x in X5 or x in X3 \/ (X4 \/ X6) by XBOOLE_1:4;
     then A17: x in X5 or x in X3 or x in X4 \/ X6 by XBOOLE_0:def 2;
       not x in (the InternalRel of G1) \/ the InternalRel of G2
          by NECKLA_2:def 2,A9;
     then A19: not x in the InternalRel of G1 & not x in the InternalRel of G2
          by XBOOLE_0:def 2;
     A20: not x in id ((the carrier of G1) \/ the carrier of G2)
          by A7,NECKLA_2:def 2;
     A21: not x in id the carrier of G1 & not x in id the carrier of G2
      proof
       assume not thesis;
       then x in id (the carrier of G1) \/ id (the carrier of G2)
            by XBOOLE_0:def 2;
       hence contradiction by A20,SYSREL:32;
      end;
     per cases by A17,XBOOLE_0:def 2;
     suppose x in [:the carrier of G1,the carrier of G1:];
      then x in [:the carrier of G1,the carrier of G1:] \ the InternalRel
of G1
         by A19,XBOOLE_0:def 4;
      then x in (the InternalRel of G1)` by SUBSET_1:def 5;
      then x in (the InternalRel of G1)` \ id the carrier of G1
           by XBOOLE_0:def 4,A21;
      then x in X1 by NECKLACE:def 9;
      then x in X1 \/ (X2 \/ X3 \/ X4) by XBOOLE_0:def 2;
      then x in X1 \/ X2 \/ X3 \/ X4 by XBOOLE_1:113;
     hence thesis by NECKLA_2:def 3;
     end;
     suppose x in X3;
      then x in X2 \/ X3 by XBOOLE_0:def 2;
       then x in X2 \/ X3 \/ X4 by XBOOLE_0:def 2;
       then x in X1 \/ (X2 \/ X3 \/ X4) by XBOOLE_0:def 2;
       then x in X1 \/ X2 \/ X3 \/ X4 by XBOOLE_1:113;
     hence thesis by NECKLA_2:def 3;
     end;
     suppose x in X4;
      then x in X3 \/ X4 by XBOOLE_0:def 2;
      then x in X2 \/ (X3 \/ X4) by XBOOLE_0:def 2;
      then x in X2 \/ X3 \/ X4 by XBOOLE_1:4;
      then x in X1 \/ (X2 \/ X3 \/ X4) by XBOOLE_0:def 2;
      then x in X1 \/ X2 \/ X3 \/ X4 by XBOOLE_1:113;
     hence thesis by NECKLA_2:def 3;
     end;
     suppose x in [:the carrier of G2,the carrier of G2:];
      then x in [:the carrier of G2,the carrier of G2:] \ the InternalRel
of G2
         by A19,XBOOLE_0:def 4;
      then x in (the InternalRel of G2)` by SUBSET_1:def 5;
      then x in (the InternalRel of G2)` \ id the carrier of G2
           by XBOOLE_0:def 4,A21;
      then x in the InternalRel of ComplRelStr G2 by NECKLACE:def 9;
      then x in (the InternalRel of ComplRelStr G1) \/
               (the InternalRel of ComplRelStr G2) by XBOOLE_0:def 2;
      then x in (the InternalRel of ComplRelStr G1) \/
                (the InternalRel of ComplRelStr G2) \/
        [:the carrier of ComplRelStr G1, the carrier of ComplRelStr G2:]
         by XBOOLE_0:def 2;
      then x in (the InternalRel of ComplRelStr G1) \/
        (the InternalRel of ComplRelStr G2) \/
        [:the carrier of ComplRelStr G1, the carrier of ComplRelStr G2:] \/
        [:the carrier of ComplRelStr G2, the carrier of ComplRelStr G1:]
        by XBOOLE_0:def 2;
     hence thesis by NECKLA_2:def 3;
    end;
    end;
   assume x in R;
   then x in X1 \/ X2 \/ X3 \/ X4 by NECKLA_2:def 3;
   then x in X1 \/ X2 \/ X3 or x in X4 by XBOOLE_0:def 2;
   then A53: x in X1 \/ X2 or x in X3 or x in X4 by XBOOLE_0:def 2;
   per cases by A53,XBOOLE_0:def 2;
   suppose x in X1;
    then A55: x in (the InternalRel of G1)` \ id the carrier of G1 by NECKLACE:
def 9;
    then x in (the InternalRel of G1)` &
         not x in id the carrier of G1 by XBOOLE_0:def 4;
    then x in [:the carrier of G1,the carrier of G1:] \ the InternalRel of
G1
      by SUBSET_1:def 5;
    then A58: x in [:the carrier of G1,the carrier of G1:] &
    not x in the InternalRel of G1 by XBOOLE_0:def 4;
    then x in X5 \/ X7 by XBOOLE_0:def 2;
    then x in X5 \/ X7 \/ X8 by XBOOLE_0:def 2;
    then x in X5 \/ X7 \/ X8 \/ X6 by XBOOLE_0:def 2;
    then x in [:(the carrier of G1) \/ the carrier of G2,
             (the carrier of G1) \/ the carrier of G2:] by ZFMISC_1:121;
    then x in [:(the carrier of G1) \/ the carrier of G2,
             the carrier of union_of(G1,G2):] by NECKLA_2:def 2;
    then A64: x in [:the carrier of union_of(G1,G2),
             the carrier of union_of(G1,G2):] by NECKLA_2:def 2;
      not x in the InternalRel of union_of(G1,G2)
     proof
      assume not thesis;
      then x in (the InternalRel of G1) \/ the InternalRel of G2
           by NECKLA_2:def 2;
      then x in the InternalRel of G2 by A58,XBOOLE_0:def 2;
      then [:the carrier of G1,the carrier of G1:] /\
           [:the carrier of G2,the carrier of G2:] is non empty
           by A58,XBOOLE_0:def 3;
      then [:the carrier of G1,the carrier of G1:] meets
           [:the carrier of G2,the carrier of G2:]
           by XBOOLE_0:def 7;
      hence contradiction by ZFMISC_1:127,A1;
     end;
    then x in [:the carrier of union_of(G1,G2),the carrier of union_of(G1,
G2):]
         \ (the InternalRel of union_of(G1,G2)) by XBOOLE_0:def 4,A64;
    then A72: x in (the InternalRel of union_of(G1,G2))`  by SUBSET_1:def 5;
      not x in id the carrier of union_of(G1,G2)
     proof
      assume not thesis;
      then x in id ((the carrier of G1) \/ the carrier of G2) by NECKLA_2:
def 2;
      then A76: x in (id the carrier of G1) \/ id the carrier of G2 by SYSREL:
32;
      thus thesis
       proof
        per cases by A76,XBOOLE_0:def 2;
        suppose x in id the carrier of G1;
        hence contradiction by A55,XBOOLE_0:def 4;
        end;
        suppose x in id the carrier of G2;
         then [:the carrier of G1,the carrier of G1:] /\
              [:the carrier of G2,the carrier of G2:] is non empty
              by A58,XBOOLE_0:def 3;
         then [:the carrier of G1,the carrier of G1:] meets
              [:the carrier of G2,the carrier of G2:] by XBOOLE_0:def 7;
         hence contradiction by ZFMISC_1:127,A1;
       end;
       end;
     end;
    then x in (the InternalRel of union_of(G1,G2))` \
              id the carrier of union_of(G1,G2) by XBOOLE_0:def 4,A72;
   hence thesis by NECKLACE:def 9;
   end;
   suppose x in X2;
    then A83: x in (the InternalRel of G2)` \ id the carrier of G2 by NECKLACE:
def 9;
    then x in (the InternalRel of G2)` &
         not x in id the carrier of G2 by XBOOLE_0:def 4;
    then x in [:the carrier of G2,the carrier of G2:] \ the InternalRel of
G2
         by SUBSET_1:def 5;
    then A86: x in [:the carrier of G2,the carrier of G2:] &
    not x in the InternalRel of G2 by XBOOLE_0:def 4;
    then x in X8 \/ X6 by XBOOLE_0:def 2;
    then x in X7 \/ (X8 \/ X6) by XBOOLE_0:def 2;
    then x in X7 \/ X8 \/ X6 by XBOOLE_1:4;
    then x in X5 \/ (X7 \/ X8 \/ X6) by XBOOLE_0:def 2;
    then x in X5 \/ X7 \/ X8 \/ X6 by XBOOLE_1:113;
    then x in [:(the carrier of G1) \/ the carrier of G2,
           (the carrier of G1) \/ the carrier of G2:] by ZFMISC_1:121;
    then x in [:(the carrier of G1) \/ the carrier of G2,
             the carrier of union_of(G1,G2):] by NECKLA_2:def 2;
    then A94: x in [:the carrier of union_of(G1,G2),
             the carrier of union_of(G1,G2):] by NECKLA_2:def 2;
      not x in the InternalRel of union_of(G1,G2)
     proof
      assume not thesis;
      then x in (the InternalRel of G1) \/ the InternalRel of G2
           by NECKLA_2:def 2;
      then x in the InternalRel of G1 by A86,XBOOLE_0:def 2;
      then [:the carrier of G1,the carrier of G1:] /\
           [:the carrier of G2,the carrier of G2:] is non empty
           by A86,XBOOLE_0:def 3;
      then [:the carrier of G1,the carrier of G1:] meets
           [:the carrier of G2,the carrier of G2:]
           by XBOOLE_0:def 7;
      hence contradiction by ZFMISC_1:127,A1;
     end;
    then x in [:the carrier of union_of(G1,G2),the carrier of union_of(G1
,G2):]
         \ (the InternalRel of union_of(G1,G2)) by XBOOLE_0:def 4,A94;
    then A102: x in (the InternalRel of union_of(G1,G2))` by SUBSET_1:def 5;
      not x in id the carrier of union_of(G1,G2)
     proof
      assume not thesis;
      then x in id ((the carrier of G1) \/ the carrier of G2) by NECKLA_2
:def 2;
      then A106: x in (id the carrier of G1) \/ id the carrier of G2 by SYSREL:
32;
        per cases by A106,XBOOLE_0:def 2;
        suppose x in id the carrier of G2;
        hence contradiction by A83,XBOOLE_0:def 4;
        end;
        suppose x in id the carrier of G1;
         then [:the carrier of G1,the carrier of G1:] /\
              [:the carrier of G2,the carrier of G2:] is non empty
              by A86,XBOOLE_0:def 3;
         then [:the carrier of G1,the carrier of G1:] meets
              [:the carrier of G2,the carrier of G2:] by XBOOLE_0:def 7;
         hence contradiction by ZFMISC_1:127,A1;
       end;
     end;
    then x in (the InternalRel of union_of(G1,G2))` \
              id the carrier of union_of(G1,G2) by XBOOLE_0:def 4,A102;
   hence thesis by NECKLACE:def 9;
   end;
   suppose x in X3;
    then A113: x in [:the carrier of G1,the carrier of ComplRelStr G2:]
         by NECKLACE:def 9;
    then A114: x in [:the carrier of G1,the carrier of G2:] by NECKLACE:def 9;
    then x in X7 \/ X8 by XBOOLE_0:def 2;
    then x in X5 \/ (X7 \/ X8) by XBOOLE_0:def 2;
    then x in X5 \/ X7 \/ X8 by XBOOLE_1:4;
    then x in X5 \/ X7 \/ X8 \/ X6 by XBOOLE_0:def 2;
    then x in [:(the carrier of G1) \/ the carrier of G2,
           (the carrier of G1) \/ the carrier of G2:] by ZFMISC_1:121;
    then x in [:(the carrier of G1) \/ the carrier of G2,
             the carrier of union_of(G1,G2):] by NECKLA_2:def 2;
    then A121: x in [:the carrier of union_of(G1,G2),
             the carrier of union_of(G1,G2):] by NECKLA_2:def 2;
      not x in the InternalRel of union_of(G1,G2)
     proof
      assume not thesis;
      then A124: x in (the InternalRel of G1) \/ the InternalRel of G2
           by NECKLA_2:def 2;
        per cases by A124,XBOOLE_0:def 2;
        suppose x in the InternalRel of G1;
         then A126: a in the carrier of G1 & b in the carrier of G1
              by ZFMISC_1:106;
           b in the carrier of G2 by A114,ZFMISC_1:106;
         then b in (the carrier of G1) /\ the carrier of G2
              by A126,XBOOLE_0:def 3;
        hence contradiction by A1,XBOOLE_0:def 7;
        end;
        suppose x in the InternalRel of G2;
         then A130: a in the carrier of G2 & b in the carrier of G2
              by ZFMISC_1:106;
           a in the carrier of G1 by A113,ZFMISC_1:106;
         then a in (the carrier of G1) /\ the carrier of G2
              by A130,XBOOLE_0:def 3;
        hence contradiction by A1,XBOOLE_0:def 7;
       end;
     end;
    then x in [:the carrier of union_of(G1,G2),the carrier of union_of(G1
,G2):]
         \ (the InternalRel of union_of(G1,G2)) by XBOOLE_0:def 4,A121;
    then A134: x in (the InternalRel of union_of(G1,G2))` by SUBSET_1:def 5;
      not x in id the carrier of union_of(G1,G2)
     proof
      assume not thesis;
      then x in id ((the carrier of G1) \/ the carrier of G2) by NECKLA_2
:def 2;
      then A138: x in (id the carrier of G1) \/ id the carrier of G2 by SYSREL:
32;
        per cases by A138,XBOOLE_0:def 2;
        suppose x in id the carrier of G1;
         then A140: a in the carrier of G1 & b in the carrier of G1
                    by ZFMISC_1:106;
           b in the carrier of G2 by A114,ZFMISC_1:106;
         then b in (the carrier of G1) /\ the carrier of G2
              by A140,XBOOLE_0:def 3;
        hence contradiction by A1,XBOOLE_0:def 7;
        end;
        suppose x in id the carrier of G2;
         then A144: a in the carrier of G2 & b in the carrier of G2
                    by ZFMISC_1:106;
           a in the carrier of G1 by A113,ZFMISC_1:106;
         then a in (the carrier of G1) /\ the carrier of G2
              by A144,XBOOLE_0:def 3;
        hence contradiction by A1,XBOOLE_0:def 7;
       end;
     end;
    then x in (the InternalRel of union_of(G1,G2))` \
              id the carrier of union_of(G1,G2) by XBOOLE_0:def 4,A134;
   hence thesis by NECKLACE:def 9;
   end;
   suppose x in X4;
    then A149: x in [:the carrier of G2,the carrier of ComplRelStr G1:]
         by NECKLACE:def 9;
    then A150: x in [:the carrier of G2,the carrier of G1:] by NECKLACE:def 9;
    then x in X7 \/ X8 by XBOOLE_0:def 2;
    then x in X5 \/ (X7 \/ X8) by XBOOLE_0:def 2;
    then x in X5 \/ X7 \/ X8 by XBOOLE_1:4;
    then x in X5 \/ X7 \/ X8 \/ X6 by XBOOLE_0:def 2;
    then x in [:(the carrier of G1) \/ the carrier of G2,
           (the carrier of G1) \/ the carrier of G2:] by ZFMISC_1:121;
    then x in [:(the carrier of G1) \/ the carrier of G2,
             the carrier of union_of(G1,G2):] by NECKLA_2:def 2;
    then A157: x in [:the carrier of union_of(G1,G2),
             the carrier of union_of(G1,G2):] by NECKLA_2:def 2;
      not x in the InternalRel of union_of(G1,G2)
     proof
      assume not thesis;
      then A160: x in (the InternalRel of G1) \/ the InternalRel of G2
           by NECKLA_2:def 2;
        per cases by A160,XBOOLE_0:def 2;
        suppose x in the InternalRel of G1;
         then A162: a in the carrier of G1 & b in the carrier of G1
              by ZFMISC_1:106;
           a in the carrier of G2 by A149,ZFMISC_1:106;
         then a in (the carrier of G1) /\ the carrier of G2
              by A162,XBOOLE_0:def 3;
        hence contradiction by A1,XBOOLE_0:def 7;
        end;
        suppose x in the InternalRel of G2;
         then A166: a in the carrier of G2 & b in the carrier of G2
              by ZFMISC_1:106;
           b in the carrier of G1 by A150,ZFMISC_1:106;
         then b in (the carrier of G1) /\ the carrier of G2
              by A166,XBOOLE_0:def 3;
        hence contradiction by A1,XBOOLE_0:def 7;
       end;
     end;
    then x in [:the carrier of union_of(G1,G2),the carrier of union_of(G1
,G2):]
         \ (the InternalRel of union_of(G1,G2)) by XBOOLE_0:def 4,A157;
    then A170: x in (the InternalRel of union_of(G1,G2))` by SUBSET_1:def 5;
      not x in id the carrier of union_of(G1,G2)
     proof
      assume not thesis;
      then x in id ((the carrier of G1) \/ the carrier of G2) by NECKLA_2
:def 2;
      then A174: x in (id the carrier of G1) \/ id the carrier of G2 by SYSREL:
32;
        per cases by A174,XBOOLE_0:def 2;
        suppose x in id the carrier of G1;
         then A176: a in the carrier of G1 & b in the carrier of G1
                    by ZFMISC_1:106;
           a in the carrier of G2 by A149,ZFMISC_1:106;
         then a in (the carrier of G1) /\ the carrier of G2
              by A176,XBOOLE_0:def 3;
        hence contradiction by A1,XBOOLE_0:def 7;
        end;
        suppose x in id the carrier of G2;
         then A180: a in the carrier of G2 & b in the carrier of G2
                    by ZFMISC_1:106;
           b in the carrier of G1 by A150,ZFMISC_1:106;
         then b in (the carrier of G1) /\ the carrier of G2
              by A180,XBOOLE_0:def 3;
        hence contradiction by A1,XBOOLE_0:def 7;
       end;
     end;
    then x in (the InternalRel of union_of(G1,G2))` \
              id the carrier of union_of(G1,G2) by XBOOLE_0:def 4,A170;
   hence thesis by NECKLACE:def 9;
  end;
  end;
  hence thesis by A2,A3,RELAT_1:def 2;
 end;

theorem Th18:
  for G1,G2 being RelStr st the carrier of G1 misses the carrier of G2
   holds ComplRelStr sum_of(G1,G2) = union_of(ComplRelStr G1, ComplRelStr G2)
proof
 let G1,G2 be RelStr;
 assume A1: the carrier of G1 misses the carrier of G2;
A2: the carrier of ComplRelStr sum_of(G1,G2) = the carrier of sum_of(G1,G2)
                   by NECKLACE:def 9
     .= (the carrier of G1) \/ the carrier of G2 by NECKLA_2:def 3;
 A3: the carrier of union_of(ComplRelStr G1, ComplRelStr G2) =
        (the carrier of ComplRelStr G1) \/ (the carrier of ComplRelStr G2)
                      by NECKLA_2:def 2
     .= (the carrier of G1) \/ the carrier of ComplRelStr G2 by NECKLACE:def 9
     .= (the carrier of G1) \/ the carrier of G2 by NECKLACE:def 9;
 set P = the InternalRel of ComplRelStr sum_of(G1,G2),
     R = the InternalRel of union_of(ComplRelStr G1,ComplRelStr G2),
     X1 = the InternalRel of ComplRelStr G1,
     X2 = the InternalRel of ComplRelStr G2,
     X5 = [:the carrier of G1, the carrier of G1:],
     X6 = [:the carrier of G2, the carrier of G2:],
     X7 = [:the carrier of G1, the carrier of G2:],
     X8 = [:the carrier of G2, the carrier of G1:];
 A4: [:the carrier of sum_of(G1,G2),the carrier of sum_of(G1,G2):] =
          [:(the carrier of G1) \/ the carrier of G2,
             the carrier of sum_of(G1,G2):] by NECKLA_2:def 3
       .= [:(the carrier of G1) \/ the carrier of G2,
            (the carrier of G1) \/ the carrier of G2:] by NECKLA_2:def 3
       .= X5 \/ X7 \/ X8 \/ X6 by ZFMISC_1:121;
   for a,b being set holds [a,b] in P iff [a,b] in R
  proof
   let a,b be set;
   set x = [a,b];
   thus x in P implies x in R
   proof
    assume x in P;
    then x in (the InternalRel of sum_of(G1,G2))` \
               id (the carrier of sum_of(G1,G2)) by NECKLACE:def 9;
    then A8: x in (the InternalRel of sum_of(G1,G2))` &
             not x in id (the carrier of sum_of(G1,G2)) by XBOOLE_0:def 4;
    then x in [:the carrier of sum_of(G1,G2),the carrier of sum_of(G1,G2)
:] \
              (the InternalRel of sum_of(G1,G2)) by SUBSET_1:def 5;
    then A10: x in [:the carrier of sum_of(G1,G2),the carrier of sum_of(G1,G2)
:]
       & not x in the InternalRel of sum_of(G1,G2) by XBOOLE_0:def 4;
    then A11: not x in (the InternalRel of G1) \/ (the InternalRel of G2) \/
             [:the carrier of G1,the carrier of G2:] \/
             [:the carrier of G2,the carrier of G1:] by NECKLA_2:def 3;
    A12: not x in the InternalRel of G1 & not x in the InternalRel of G2 &
     not x in [:the carrier of G1,the carrier of G2:] &
     not x in [:the carrier of G2,the carrier of G1:]
      proof
       assume not thesis;
       then x in (the InternalRel of G1) \/ the InternalRel of G2 or
            x in [:the carrier of G1,the carrier of G2:] or
            x in [:the carrier of G2,the carrier of G1:] by XBOOLE_0:def 2;
       then x in (the InternalRel of G1) \/ (the InternalRel of G2) \/
                 [:the carrier of G1,the carrier of G2:] or
            x in [:the carrier of G2,the carrier of G1:] by XBOOLE_0:def 2;
       hence contradiction by A11,XBOOLE_0:def 2;
      end;
      not x in id ((the carrier of G1) \/ the carrier of G2)
          by NECKLA_2:def 3,A8;
    then not x in id (the carrier of G1) \/ id the carrier of G2
              by SYSREL:32;
    then A18: not x in id the carrier of G1 & not x in id the carrier of G2
              by XBOOLE_0:def 2;
      x in X5 \/ X7 \/ X8 or x in X6 by XBOOLE_0:def 2,A10,A4;
    then A20: x in X5 \/ X7 or x in X8 or x in X6 by XBOOLE_0:def 2;
    per cases by A20,XBOOLE_0:def 2;
    suppose x in X5;
     then x in X5 \ (the InternalRel of G1) by XBOOLE_0:def 4,A12;
     then x in (the InternalRel of G1)` by SUBSET_1:def 5;
     then x in (the InternalRel of G1)` \ id the carrier of G1 by XBOOLE_0
:def 4,A18;
     then x in X1 by NECKLACE:def 9;
     then x in X1 \/ X2 by XBOOLE_0:def 2;
    hence thesis by NECKLA_2:def 2;
    end;
    suppose x in X7;
    hence thesis by A12;
    end;
    suppose x in X8;
    hence thesis by A12;
    end;
    suppose x in X6;
     then x in X6 \ (the InternalRel of G2) by XBOOLE_0:def 4,A12;
     then x in (the InternalRel of G2)` by SUBSET_1:def 5;
     then x in (the InternalRel of G2)` \ id the carrier of G2 by XBOOLE_0
:def 4,A18;
     then x in X2 by NECKLACE:def 9;
     then x in X1 \/ X2 by XBOOLE_0:def 2;
    hence thesis by NECKLA_2:def 2;
   end;
   end;
   assume x in R;
    then A36: x in (the InternalRel of ComplRelStr G1) \/
                   the InternalRel of ComplRelStr G2 by NECKLA_2:def 2;
    per cases by A36,XBOOLE_0:def 2;
    suppose x in the InternalRel of ComplRelStr G1;
     then x in (the InternalRel of G1)` \ id the carrier of G1
                by NECKLACE:def 9;
     then A39: x in (the InternalRel of G1)` & not x in id the carrier of G1
                by XBOOLE_0:def 4;
     then x in [:the carrier of G1,the carrier of G1:] \ the InternalRel
of G1
             & not x in id the carrier of G1 by SUBSET_1:def 5;
     then A41: x in [:the carrier of G1,the carrier of G1:] &
      not x in the InternalRel of G1 & not x in id the carrier of G1
          by XBOOLE_0:def 4;
     A42: not x in id (the carrier of sum_of(G1,G2))
      proof
       assume not thesis;
       then x in id ((the carrier of G1) \/ the carrier of G2) by NECKLA_2
:def 3;
       then x in id (the carrier of G1) \/ id the carrier of G2 by SYSREL:
32;
       then x in id the carrier of G2 by A39,XBOOLE_0:def 2;
       then A47: a in the carrier of G2 & b in the carrier of G2 by ZFMISC_1:
106;
         a in the carrier of G1 by A41,ZFMISC_1:106;
       then (the carrier of G1) /\ the carrier of G2 is non empty
            by XBOOLE_0:def 3,A47;
       hence contradiction by A1,XBOOLE_0:def 7;
      end;
     A50: not x in the InternalRel of sum_of(G1,G2)
      proof
       assume not thesis;
       then x in (the InternalRel of G1) \/ (the InternalRel of G2) \/
                 X7 \/ X8 by NECKLA_2:def 3;
       then x in (the InternalRel of G1) \/ (the InternalRel of G2) \/ X7
            or x in X8 by XBOOLE_0:def 2;
       then A54: x in (the InternalRel of G1) \/ the InternalRel of G2 or
            x in X7 or x in X8 by XBOOLE_0:def 2;
         per cases by A41,A54,XBOOLE_0:def 2;
         suppose x in the InternalRel of G2;
          then A56: a in the carrier of G2 & b in the carrier of G2 by ZFMISC_1
:106;
            a in the carrier of G1  by A41,ZFMISC_1:106;
          then (the carrier of G1) /\ the carrier of G2 is non empty
               by XBOOLE_0:def 3,A56;
         hence contradiction by A1,XBOOLE_0:def 7;
         end;
         suppose x in X7;
          then A60: a in the carrier of G1 & b in the carrier of G2 by ZFMISC_1
:106;
            b in the carrier of G1 by A41,ZFMISC_1:106;
          then (the carrier of G1) /\ the carrier of G2 is non empty
                by A60,XBOOLE_0:def 3;
         hence contradiction by A1,XBOOLE_0:def 7;
         end;
         suppose x in X8;
          then A64: a in the carrier of G2 & b in the carrier of G1 by ZFMISC_1
:106;
            a in the carrier of G1 by A41,ZFMISC_1:106;
          then (the carrier of G1) /\ the carrier of G2 is non empty
                by A64,XBOOLE_0:def 3;
         hence contradiction by A1,XBOOLE_0:def 7;
        end;
      end;
       x in X5 \/ (X7 \/ X8 \/ X6) by A41,XBOOLE_0:def 2;
     then x in [:the carrier of sum_of(G1,G2),the carrier of sum_of(G1,G2)
:]
               by XBOOLE_1:113,A4;
     then x in [:the carrier of sum_of(G1,G2),the carrier of sum_of(G1,G2)
:] \
           (the InternalRel of sum_of(G1,G2)) by A50,XBOOLE_0:def 4;
     then x in (the InternalRel of  sum_of(G1,G2))` &
      not x in id (the carrier of sum_of(G1,G2)) by SUBSET_1:def 5,A42;
     then x in (the InternalRel of sum_of(G1,G2))` \
                    id (the carrier of sum_of(G1,G2)) by XBOOLE_0:def 4;
    hence thesis by NECKLACE:def 9;
    end;
    suppose x in the InternalRel of ComplRelStr G2;
     then x in (the InternalRel of G2)` \ id the carrier of G2
                by NECKLACE:def 9;
     then A74: x in (the InternalRel of G2)` & not x in id the carrier of G2
                by XBOOLE_0:def 4;
     then x in [:the carrier of G2,the carrier of G2:] \ the InternalRel
of G2
             & not x in id the carrier of G2 by SUBSET_1:def 5;
     then A76: x in [:the carrier of G2,the carrier of G2:] &
      not x in the InternalRel of G2 & not x in id the carrier of G2
          by XBOOLE_0:def 4;
     A77: not x in id (the carrier of sum_of(G1,G2))
      proof
       assume not thesis;
       then x in id ((the carrier of G1) \/ the carrier of G2) by NECKLA_2
:def 3;
       then x in id (the carrier of G1) \/ id the carrier of G2 by SYSREL:
32;
       then x in id the carrier of G1 by A74,XBOOLE_0:def 2;
       then A82: a in the carrier of G1 & b in the carrier of G1 by ZFMISC_1:
106;
         b in the carrier of G2 by A76,ZFMISC_1:106;
       then (the carrier of G1) /\ the carrier of G2 is non empty
            by XBOOLE_0:def 3,A82;
       hence contradiction by A1,XBOOLE_0:def 7;
      end;
     A85: not x in the InternalRel of sum_of(G1,G2)
      proof
       assume not thesis;
       then x in (the InternalRel of G1) \/ (the InternalRel of G2) \/
                 X7 \/ X8 by NECKLA_2:def 3;
       then x in (the InternalRel of G1) \/ (the InternalRel of G2) \/ X7
            or x in X8 by XBOOLE_0:def 2;
       then A89: x in (the InternalRel of G1) \/ the InternalRel of G2 or
            x in X7 or x in X8 by XBOOLE_0:def 2;
         per cases by A76,A89,XBOOLE_0:def 2;
         suppose x in the InternalRel of G1;
          then A91: a in the carrier of G1 & b in the carrier of G1 by ZFMISC_1
:106;
            a in the carrier of G2  by A76,ZFMISC_1:106;
          then (the carrier of G1) /\ the carrier of G2 is non empty
               by XBOOLE_0:def 3,A91;
         hence contradiction by A1,XBOOLE_0:def 7;
         end;
         suppose x in X7;
          then A95: a in the carrier of G1 & b in the carrier of G2 by ZFMISC_1
:106;
            a in the carrier of G2 by A76,ZFMISC_1:106;
          then (the carrier of G1) /\ the carrier of G2 is non empty
                by A95,XBOOLE_0:def 3;
         hence contradiction by A1,XBOOLE_0:def 7;
         end;
         suppose x in X8;
          then A99: a in the carrier of G2 & b in the carrier of G1 by
ZFMISC_1:106;
            b in the carrier of G2 by A76,ZFMISC_1:106;
          then (the carrier of G1) /\ the carrier of G2 is non empty
                by A99,XBOOLE_0:def 3;
         hence contradiction by A1,XBOOLE_0:def 7;
        end;
      end;
       x in [:the carrier of sum_of(G1,G2),the carrier of sum_of(G1,G2):]
               by XBOOLE_0:def 2,A76,A4;
     then x in [:the carrier of sum_of(G1,G2),the carrier of sum_of(G1,G2
):] \
           (the InternalRel of sum_of(G1,G2)) by A85,XBOOLE_0:def 4;
     then x in (the InternalRel of  sum_of(G1,G2))` &
      not x in id (the carrier of sum_of(G1,G2)) by SUBSET_1:def 5,A77;
     then x in (the InternalRel of sum_of(G1,G2))` \
                    id (the carrier of sum_of(G1,G2)) by XBOOLE_0:def 4;
    hence thesis by NECKLACE:def 9;
  end;
  end;
 hence thesis by A2,A3,RELAT_1:def 2;
end;

theorem
    for G being RelStr, H being full SubRelStr of G
  holds  the InternalRel of ComplRelStr H =
     (the InternalRel of ComplRelStr G)|_2 the carrier of ComplRelStr H
proof
 let G be RelStr, H be full SubRelStr of G;
 set IH = the InternalRel of H,
     ICmpH = the InternalRel of ComplRelStr H,
     cH = the carrier of H,
     IG = the InternalRel of G,
     cG = the carrier of G,
     ICmpG = the InternalRel of ComplRelStr G;
 A1: ICmpH = IH` \ id cH by NECKLACE:def 9
          .= [:cH,cH:] \ IH \ id cH by SUBSET_1:def 5;
 A2: ICmpG = IG` \ id cG by NECKLACE:def 9
          .= [:cG,cG:] \ IG \ id cG by SUBSET_1:def 5;
 A3: cH c= cG by YELLOW_0:def 13;
   ICmpG |_2 the carrier of ComplRelStr H =
           ICmpG |_2 cH by NECKLACE:def 9
  .=  ([:cG,cG:] \ IG \ id cG) /\ [:cH,cH:] by A2,WELLORD1:def 6
  .= ([:cG,cG:] \ IG) /\ [:cH,cH:] \ id cG /\ [:cH,cH:] by XBOOLE_1:50
  .= ([:cG,cG:] /\ [:cH,cH:]) \ (IG /\ [:cH,cH:]) \ (id cG /\ [:cH,cH:])
                by XBOOLE_1:50
  .= ([:cG,cG:] /\ [:cH,cH:]) \ (IG /\ [:cH,cH:]) \ (id cG)|cH by Th1
  .= ([:cG,cG:] /\ [:cH,cH:]) \ (IG /\ [:cH,cH:]) \ id cH by FUNCT_3:1,A3
  .= ([:cG,cG:] /\ [:cH,cH:]) \ IG|_2 cH \ id cH by WELLORD1:def 6
  .= ([:cG,cG:] /\ [:cH,cH:]) \ IH \ id cH by YELLOW_0:def 14
  .= [:cG /\ cH, cG /\ cH:] \ IH \ id cH by ZFMISC_1:123
  .= [:cH, cG /\ cH:] \ IH \ id cH by XBOOLE_1:28,A3
  .= ICmpH by A1,XBOOLE_1:28,A3;
 hence thesis;
end;

theorem Th20:
 for G being non empty irreflexive RelStr, x being Element of the carrier of G,
  x' being Element of the carrier of ComplRelStr G st x = x'  holds
 ComplRelStr (subrelstr ([#]G \ {x})) = subrelstr ([#](ComplRelStr G) \ {x'})
proof
 let G be non empty irreflexive RelStr, x be Element of the carrier of G,
     x' be Element of the carrier of ComplRelStr G;
 assume A1: x = x';
 set R = subrelstr ([#]G \ {x}),
    cR = the carrier of R,
    cG = the carrier of G;
 A2: the carrier of ComplRelStr (subrelstr ([#]G \ {x}))
     = the carrier of (subrelstr ([#]G \ {x})) by NECKLACE:def 9
    .= [#]G \ {x} by YELLOW_0:def 15
    .= (the carrier of G) \ {x} by PRE_TOPC:def 3
    .= (the carrier of ComplRelStr G) \ {x} by NECKLACE:def 9
    .= [#](ComplRelStr G) \ {x'} by PRE_TOPC:def 3,A1
    .= the carrier of subrelstr ([#](ComplRelStr G) \ {x'}) by YELLOW_0:def 15;
 A3: [#]G = the carrier of G by PRE_TOPC:def 3;
 A4: [#]ComplRelStr G = the carrier of ComplRelStr G by PRE_TOPC:def 3
                        .= cG by NECKLACE:def 9;
 A5: [:(cG \ {x}),(cG \ {x}):] = [:cR,([#]G \ {x}):] by YELLOW_0:def 15,A3
          .= [:cR,cR:] by YELLOW_0:def 15;
    cR c= cG by YELLOW_0:def 13;
 then A7: [:cR,cR:] c= [:cG,cG:] by ZFMISC_1:119;
 A8: [:cR,cR:] = [:[#]G,([#]G \ {x}):] \ [:{x},([#]G \ {x}):]
                   by A5,A3,ZFMISC_1:125
    .= [:[#]G,[#]G:] \ [:[#]G,{x}:] \ [:{x},([#]G\{x}):] by ZFMISC_1:125
    .= [:cG,cG:] \ [:cG,{x}:] \ ([:{x},cG:] \ [:{x},{x}:]) by A3,ZFMISC_1:125
    .= ( ([:cG,cG:] \ [:cG,{x}:]) \ [:{x},cG:] ) \/
        ([:cG,cG:] \ [:cG,{x}:]) /\ [:{x},{x}:] by XBOOLE_1:52
    .= ([:cG,cG:] \ ([:cG,{x}:] \/ [:{x},cG:]) ) \/
            ([:cG,cG:] \ [:cG,{x}:]) /\ [:{x},{x}:] by XBOOLE_1:41;
A9: the InternalRel of ComplRelStr R =
     (the InternalRel of R)` \ id cR by NECKLACE:def 9
     .= [:cR,cR:] \ (the InternalRel of R) \ id cR by SUBSET_1:def 5
     .= [:cR,cR:] \ ( (the InternalRel of G)|_2 cR ) \id cR
           by YELLOW_0:def 14
     .= [:cR,cR:] \ ( (the InternalRel of G) /\ [:cR,cR:] ) \id cR
           by WELLORD1:def 6
     .= ( ([:cR,cR:] \ (the InternalRel of G)) \/ ([:cR,cR:] \ [:cR,cR:]) )
          \ id cR by XBOOLE_1:54
     .= ( ([:cR,cR:] \ (the InternalRel of G)) \/ {}) \ id cR by XBOOLE_1:37
     .= ([:cR,cR:] \ (the InternalRel of G)) \ id cR;
A10: the InternalRel of subrelstr ([#](ComplRelStr G) \ {x'})
   = (the InternalRel of ComplRelStr G)|_2
       the carrier of subrelstr ([#](ComplRelStr G) \ {x'}) by YELLOW_0:def 14
  .= (the InternalRel of ComplRelStr G)|_2 (cG \ {x}) by A4,A1,YELLOW_0:def 15
  .= (the InternalRel of ComplRelStr G) /\ [:(cG \ {x}),(cG \ {x}):]
      by WELLORD1:def 6
  .= ((the InternalRel of G)` \ id cG )
     /\ [:(cG \ {x}),(cG \ {x}):] by NECKLACE:def 9
  .= ( [:cR,cR:] /\ (the InternalRel of G)` ) \ id cG by XBOOLE_1:49,A5
  .= ( [:cR,cR:] /\ ([:cG,cG:] \ the InternalRel of G) ) \ id cG
       by SUBSET_1:def 5
  .= ( ([:cR,cR:] /\ [:cG,cG:]) \ the InternalRel of G) \ id cG by XBOOLE_1:49
  .= ([:cR,cR:] \ (the InternalRel of G)) \ id cG by A7,XBOOLE_1:28;
   the InternalRel of subrelstr ([#](ComplRelStr G) \ {x'})
     = the InternalRel of ComplRelStr R
  proof
   thus the InternalRel of subrelstr ([#](ComplRelStr G) \ {x'})
      c= the InternalRel of ComplRelStr R
    proof
     let a be set;
     assume a in the InternalRel of subrelstr ([#](ComplRelStr G) \ {x'});
     then A13: a in ([:cR,cR:] \ (the InternalRel of G)) & not a in id cG
            by A10,XBOOLE_0:def 4;
      not a in id cR
      proof
       assume A15: not thesis;
       then consider x2,y2 being set such that
       A16: a = [x2,y2] & x2 in cR & y2 in cR  by RELSET_1:6;
         x2 in cG \ {x} & y2 in cG \ {x} by A3,YELLOW_0:def 15,A16;
       then A18: x2 in cG & y2 in cG by XBOOLE_0:def 4;
        x2 = y2 by A15,A16,RELAT_1:def 10;
       hence contradiction by A13,A16,A18,RELAT_1:def 10;
      end;
     hence thesis by A9,A13,XBOOLE_0:def 4;
    end;
   let a be set;
   assume A20: a in the InternalRel of ComplRelStr R;
   then A21: a in ([:cR,cR:] \ (the InternalRel of G)) & not a in id cR
        by A9,XBOOLE_0:def 4;
    then not a in id (cG \ {x}) by A3,YELLOW_0:def 15;
   then A23: not a in (id cG \ id {x}) by SYSREL:32;
   per cases by A23,XBOOLE_0:def 4;
   suppose not a in id cG;
    then a in ([:cR,cR:] \ (the InternalRel of G)) & not a in id cG by A9,
A20,XBOOLE_0:def 4;
   hence thesis by A10,XBOOLE_0:def 4;
   end;
   suppose a in id {x};
    then A27: a in {[x,x]} by SYSREL:30;
    A28: a in [:cR,cR:] by A21,XBOOLE_0:def 4;
   thus thesis
    proof
     per cases by A8,A28,XBOOLE_0:def 2;
     suppose a in [:cG,cG:] \ ([:cG,{x}:] \/ [:{x},cG:]);
      then a in [:cG,cG:] & not a in ([:cG,{x}:] \/ [:{x},cG:])
                by XBOOLE_0:def 4;
      then A31: a in [:cG,cG:] & not a in [:cG,{x}:] & not a in [:{x},cG:]
               by XBOOLE_0:def 2;
        x in {x} & x in cG by TARSKI:def 1;
      then [x,x] in [:{x},cG:] by ZFMISC_1:106;
     hence thesis by A27,A31,TARSKI:def 1;
     end;
     suppose a in ([:cG,cG:] \ [:cG,{x}:]) /\ [:{x},{x}:];
      then a in [:cG,cG:] \ [:cG,{x}:] by XBOOLE_0:def 3;
      then A36: a in [:cG,cG:] & not a in [:cG,{x}:] by XBOOLE_0:def 4;
        x in {x} & x in cG by TARSKI:def 1;
      then [x,x] in [:cG,{x}:] by ZFMISC_1:106;
     hence thesis by A27,A36,TARSKI:def 1;
    end;
    end;
  end;
  end;
 hence thesis by A2;
end;

begin :: Another facts relating to operation 'embeds'

registration
  cluster trivial strict -> N-free (non empty RelStr);
  correctness
   proof
    let R be non empty RelStr;
    set Y = Necklace 4;
    assume R is trivial strict;
    then consider y be set such that
    A2: the carrier of R = {y} by GROUP_6:def 2;
    assume not R is N-free;
    then R embeds Y by NECKLA_2:def 1;
    then consider f being map of Y,R such that A5: f is  one-to-one &
    for x,y being Element of the carrier of Y holds
    [x,y] in the InternalRel of Y iff [f.x,f.y] in the InternalRel of R
          by NECKLACE:def 2;
    A6: dom f = the carrier of Y by FUNCT_2:def 1
             .= {0,1,2,3} by NECKLACE:2,21;
    then A7: 0 in dom f by ENUMSET1:19;
    A8: 1 in dom f by A6,ENUMSET1:19;
    A9: f.0 in {y} by PARTFUN1:27,A7,A2;
      f.1 in {y} by PARTFUN1:27,A8,A2;
    then f.0 = y & f.1 = y by A9,TARSKI:def 1;
    hence contradiction by A5,FUNCT_1:def 8,A7,A8;
   end;
end;

theorem
     for R being reflexive antisymmetric RelStr, S being RelStr  holds
    (ex f being map of R,S st
    for x,y being Element of the carrier of R holds
    [x,y] in the InternalRel of R iff [f.x,f.y] in the InternalRel of S)
     iff S embeds R
    proof
      let R being reflexive antisymmetric RelStr, S being RelStr;
      A1: now assume A2: S embeds R;
           then consider f being map of R,S such that f is one-to-one and
           A3: for x,y being Element of the carrier of R holds
           [x,y] in the InternalRel of R iff [f.x,f.y] in the InternalRel of S
              by NECKLACE:def 2;
           take f;
           thus thesis by A2,A3;
         end;
        now assume ex f being map of R,S st
          for x,y being Element of the carrier of R holds
           [x,y] in the InternalRel of R iff [f.x,f.y] in the InternalRel of S;
          then consider f being map of R,S such that A6:
           for x,y being Element of the carrier of R holds
           [x,y] in the InternalRel of R iff [f.x,f.y] in the InternalRel of S;
            for x1,x2 being set st x1 in dom f & x2 in dom f & f.x1 = f.x2
holds
          x1 = x2
           proof
             let x1,x2 be set;
             assume A8: x1 in dom f & x2 in dom f & f.x1 = f.x2;
             then reconsider x1,x2 as Element of the carrier of R;
             A9: the InternalRel of R is_reflexive_in the carrier of R
             by ORDERS_1:def 4;
             then [x1,x1] in the InternalRel of R by RELAT_2:def 1,A8;
             then [f.x1,f.x2] in the InternalRel of S by A8,A6;
             then [x1,x2] in the InternalRel of R by A6;
             then A13: x1 <= x2 by ORDERS_1:def 9;
               [x2,x2] in the InternalRel of R by RELAT_2:def 1,A9,A8;
             then [f.x2,f.x1] in the InternalRel of S by A8,A6;
             then [x2,x1] in the InternalRel of R by A6;
             then x2 <= x1 by ORDERS_1:def 9;
             hence thesis by A13,ORDERS_1:25;
           end;
          then f is one-to-one by FUNCT_1:def 8;
          hence S embeds R by NECKLACE:def 2,A6;
         end;
       hence thesis by A1;
    end;

theorem Th22:
 for G being non empty RelStr, H being non empty full SubRelStr of G
  holds G embeds H
proof
 let G be non empty RelStr;
 let H be non empty full SubRelStr of G;
 reconsider f = id the carrier of H as Function of the carrier of H,
   the carrier of H;
 A1: the carrier of H c= the carrier of G by YELLOW_0:def 13;
 A2: dom f = the carrier of H by FUNCT_1:34;
   for x being set st x in the carrier of H holds f.x in the carrier of G
  proof
   let x be set;
   assume x in the carrier of H;
   then f.x in the carrier of H by FUNCT_1:34;
   hence thesis by A1;
  end;
 then reconsider f = id the carrier of H as Function of the carrier of H,
   the carrier of G by FUNCT_2:5,A2;
 reconsider f as map of H,G;
   for x,y being Element of the carrier of H holds
 [x,y] in the InternalRel of H iff [f.x,f.y] in the InternalRel of G
  proof
   let x,y be Element of the carrier of H;
   set IH = the InternalRel of H,
       IG = the InternalRel of G,
       cH = the carrier of H;
   A7: f.x = x & f.y =y by FUNCT_1:34;
   thus [x,y] in IH implies [f.x,f.y] in IG
    proof
     assume [x,y] in IH;
     then [x,y] in IG |_2 cH by YELLOW_0:def 14;
     then [x,y] in IG /\ [:cH,cH:] by WELLORD1:def 6;
     hence thesis by A7,XBOOLE_0:def 3;
    end;
   assume A11: [f.x,f.y] in IG;
     [x,y] in [:cH,cH:] by ZFMISC_1:106;
   then [x,y] in IG /\ [:cH,cH:] by A7,A11,XBOOLE_0:def 3;
   then [x,y] in IG |_2 cH by WELLORD1:def 6;
   hence [x,y] in IH by YELLOW_0:def 14;
  end;
 hence thesis by NECKLACE:def 2;
end;

theorem Th23:
  for G being non empty RelStr, H being non empty full SubRelStr of G
  st G is N-free holds H is N-free
proof
 let G be non empty RelStr, H be non empty full SubRelStr of G;
 assume A1: G is N-free;
 assume not thesis;
 then A3: H embeds Necklace 4 by NECKLA_2:def 1;
   G embeds H by Th22;
 then G embeds Necklace 4 by NECKLACE:13,A3;
 hence contradiction by A1,NECKLA_2:def 1;
end;

theorem Th24:
  for G being non empty irreflexive RelStr holds
  G embeds Necklace 4 iff ComplRelStr G embeds Necklace 4
proof
 let G be non empty irreflexive RelStr;
 set N4 = Necklace 4,
     CmpN4 = ComplRelStr Necklace 4,
     CmpG = ComplRelStr G;
A1: the carrier of Necklace 4 = {0,1,2,3} by NECKLACE:2,21;
 A2: the carrier of CmpN4 = the carrier of N4 by NECKLACE:def 9;
 A3: the carrier of CmpG = the carrier of G by NECKLACE:def 9;
 thus G embeds N4 implies CmpG embeds N4
 proof
 assume G embeds Necklace 4;
 then consider f being map of N4,G such that
 A5: f is one-to-one & for x,y being Element of N4 holds
  [x,y] in the InternalRel of N4 iff [f.x,f.y] in the InternalRel of G
        by NECKLACE:def 2;
   CmpN4,N4 are_isomorphic by NECKLACE:30,WAYBEL_1:7;
 then consider g being map of CmpN4,N4 such that
 A7: g is isomorphic by WAYBEL_1:def 8;
 A8: g is one-to-one monotone & ex g1 being map of N4,CmpN4 st g1 = g" &
     g1 is monotone by A7,WAYBEL_0:def 38;
 reconsider h = f*g as map of CmpN4,G;
 A9: dom h = the carrier of CmpN4 by FUNCT_2:def 1;
 A10: h is one-to-one by FUNCT_1:46,A5,A8;
 A11: for x,y being Element of CmpN4 holds
  [x,y] in the InternalRel of CmpN4 iff [h.x,h.y] in the InternalRel of G
 proof
   let x,y be Element of the carrier of CmpN4;
   thus [x,y] in the InternalRel of CmpN4 implies
        [h.x,h.y] in the InternalRel of G
    proof
     assume [x,y] in the InternalRel of CmpN4;
     then x <= y by ORDERS_1:def 9;
     then g.x <= g.y by WAYBEL_1:def 2,A8;
     then [g.x,g.y] in the InternalRel of N4 by ORDERS_1:def 9;
     then [f.(g.x),f.(g.y)] in the InternalRel of G by A5;
     then [(f*g).x,f.(g.y)] in the InternalRel of G by FUNCT_2:21;
     hence thesis by FUNCT_2:21;
    end;
   assume [h.x,h.y] in the InternalRel of G;
   then [f.(g.x),h.y] in the InternalRel of G by FUNCT_2:21;
   then [f.(g.x),f.(g.y)] in the InternalRel of G by FUNCT_2:21;
   then [g.x,g.y] in the InternalRel of N4 by A5;
   then g.x <= g.y by ORDERS_1:def 9;
   then x <= y by WAYBEL_0:66,A7;
   hence thesis by ORDERS_1:def 9;
  end;
 A24: 0 in the carrier of CmpN4 & 1 in the carrier of CmpN4 &
      2 in the carrier of CmpN4 & 3 in the carrier of CmpN4
                          by ENUMSET1:19,A1,A2;
   [0,2] in the InternalRel of CmpN4 & [2,0] in the InternalRel of CmpN4 &
      [0,3] in the InternalRel of CmpN4 & [3,0] in the InternalRel of CmpN4 &
      [1,3] in the InternalRel of CmpN4 & [3,1] in the InternalRel of CmpN4
                   by Th11,ENUMSET1:29;
 then A26: [h.0,h.2] in the InternalRel of G &
           [h.2,h.0] in the InternalRel of G &
           [h.0,h.3] in the InternalRel of G &
           [h.3,h.0] in the InternalRel of G &
           [h.1,h.3] in the InternalRel of G &
           [h.3,h.1] in the InternalRel of G by A11,A24;

A27: [h.0,h.1] in the InternalRel of CmpG
  proof
   assume A28: not thesis;
     [h.0,h.1] in the InternalRel of G
   proof
    assume A30: not thesis;
      h.0 in the carrier of G & h.1 in the carrier of G by FUNCT_2:7,A24;
    then [h.0,h.1] in [:the carrier of G,the carrier of G:] by ZFMISC_1:
106;
    then [h.0,h.1] in id (the carrier of G) \/ (the InternalRel of G)
            \/ (the InternalRel of CmpG) by Th14;
    then [h.0,h.1] in id (the carrier of G) \/ (the InternalRel of G) or
         [h.0,h.1] in the InternalRel of CmpG by XBOOLE_0:def 2;
    then [h.0,h.1] in id (the carrier of G) by A28,A30,XBOOLE_0:def 2;
    then h.0 = h.1 by RELAT_1:def 10;
    hence contradiction by A9,A10,A24,FUNCT_1:def 8;
   end;
   then A37: [0,1] in the InternalRel of CmpN4 by A11,A24;
     [0,1] in the InternalRel of N4 by ENUMSET1:29,NECKLA_2:2;
   then [0,1] in (the InternalRel of N4) /\ (the InternalRel of CmpN4)
              by XBOOLE_0:def 3,A37;
   then the InternalRel of N4 meets the InternalRel of CmpN4 by XBOOLE_0:
def 7;
   hence thesis by Th12;
  end;
A41: [h.1,h.0] in the InternalRel of CmpG
  proof
   assume A42: not thesis;
     [h.1,h.0] in the InternalRel of G
   proof
    assume A44: not thesis;
      h.0 in the carrier of G & h.1 in the carrier of G by FUNCT_2:7,A24;
    then [h.1,h.0] in [:the carrier of G,the carrier of G:] by ZFMISC_1:
106;
    then [h.1,h.0] in id (the carrier of G) \/ (the InternalRel of G)
            \/ (the InternalRel of CmpG) by Th14;
    then [h.1,h.0] in id (the carrier of G) \/ (the InternalRel of G) or
         [h.1,h.0] in the InternalRel of CmpG by XBOOLE_0:def 2;
    then [h.1,h.0] in id (the carrier of G) by A42,A44,XBOOLE_0:def 2;
    then h.0 = h.1 by RELAT_1:def 10;
    hence contradiction by A9,A10,A24,FUNCT_1:def 8;
   end;
   then A51: [1,0] in the InternalRel of CmpN4 by A11,A24;
     [1,0] in the InternalRel of N4 by ENUMSET1:29,NECKLA_2:2;
   then [1,0] in (the InternalRel of N4) /\ (the InternalRel of CmpN4)
              by XBOOLE_0:def 3,A51;
   then the InternalRel of N4 meets the InternalRel of CmpN4 by XBOOLE_0:
def 7;
   hence thesis by Th12;
  end;
A55: [h.1,h.2] in the InternalRel of CmpG
  proof
   assume A56: not thesis;
     [h.1,h.2] in the InternalRel of G
   proof
    assume A58: not thesis;
      h.1 in the carrier of G & h.2 in the carrier of G by FUNCT_2:7,A24;
    then [h.1,h.2] in [:the carrier of G,the carrier of G:] by ZFMISC_1:
106;
    then [h.1,h.2] in id (the carrier of G) \/ (the InternalRel of G)
            \/ (the InternalRel of CmpG) by Th14;
    then [h.1,h.2] in id (the carrier of G) \/ (the InternalRel of G) or
         [h.1,h.2] in the InternalRel of CmpG by XBOOLE_0:def 2;
    then [h.1,h.2] in id (the carrier of G) by A56,A58,XBOOLE_0:def 2;
    then h.1 = h.2 by RELAT_1:def 10;
    hence contradiction by A9,A10,A24,FUNCT_1:def 8;
   end;
   then A65: [1,2] in the InternalRel of CmpN4 by A11,A24;
     [1,2] in the InternalRel of N4 by ENUMSET1:29,NECKLA_2:2;
   then [1,2] in (the InternalRel of N4) /\ (the InternalRel of CmpN4)
              by XBOOLE_0:def 3,A65;
   then the InternalRel of N4 meets the InternalRel of CmpN4 by XBOOLE_0:
def 7;
   hence thesis by Th12;
  end;
A69: [h.2,h.1] in the InternalRel of CmpG
  proof
   assume A70: not thesis;
     [h.2,h.1] in the InternalRel of G
   proof
    assume A72: not thesis;
      h.1 in the carrier of G & h.2 in the carrier of G by FUNCT_2:7,A24;
    then [h.2,h.1] in [:the carrier of G,the carrier of G:] by ZFMISC_1:
106;
    then [h.2,h.1] in id (the carrier of G) \/ (the InternalRel of G)
            \/ (the InternalRel of CmpG) by Th14;
    then [h.2,h.1] in id (the carrier of G) \/ (the InternalRel of G) or
         [h.2,h.1] in the InternalRel of CmpG by XBOOLE_0:def 2;
    then [h.2,h.1] in id (the carrier of G) by A70,A72,XBOOLE_0:def 2;
    then h.1 = h.2 by RELAT_1:def 10;
    hence contradiction by A9,A10,A24,FUNCT_1:def 8;
   end;
   then A79: [2,1] in the InternalRel of CmpN4 by A11,A24;
     [2,1] in the InternalRel of N4 by ENUMSET1:29,NECKLA_2:2;
   then [2,1] in (the InternalRel of N4) /\ (the InternalRel of CmpN4)
              by XBOOLE_0:def 3,A79;
   then the InternalRel of N4 meets the InternalRel of CmpN4 by XBOOLE_0:
def 7;
   hence thesis by Th12;
  end;
A83: [h.2,h.3] in the InternalRel of CmpG
  proof
   assume A84: not thesis;
     [h.2,h.3] in the InternalRel of G
   proof
    assume A86: not thesis;
      h.2 in the carrier of G & h.3 in the carrier of G by FUNCT_2:7,A24;
    then [h.2,h.3] in [:the carrier of G,the carrier of G:] by ZFMISC_1:
106;
    then [h.2,h.3] in id (the carrier of G) \/ (the InternalRel of G)
            \/ (the InternalRel of CmpG) by Th14;
    then [h.2,h.3] in id (the carrier of G) \/ (the InternalRel of G) or
         [h.2,h.3] in the InternalRel of CmpG by XBOOLE_0:def 2;
    then [h.2,h.3] in id (the carrier of G) by A84,A86,XBOOLE_0:def 2;
    then h.2 = h.3 by RELAT_1:def 10;
    hence contradiction by A9,A10,A24,FUNCT_1:def 8;
   end;
   then A93: [2,3] in the InternalRel of CmpN4 by A11,A24;
     [2,3] in the InternalRel of N4 by ENUMSET1:29,NECKLA_2:2;
   then [2,3] in (the InternalRel of N4) /\ (the InternalRel of CmpN4)
              by XBOOLE_0:def 3,A93;
   then the InternalRel of N4 meets the InternalRel of CmpN4 by XBOOLE_0:
def 7;
   hence thesis by Th12;
  end;
A97: [h.3,h.2] in the InternalRel of CmpG
  proof
   assume A98: not thesis;
     [h.3,h.2] in the InternalRel of G
   proof
    assume A100: not thesis;
      h.2 in the carrier of G & h.3 in the carrier of G by FUNCT_2:7,A24;
    then [h.3,h.2] in [:the carrier of G,the carrier of G:] by ZFMISC_1:
106;
    then [h.3,h.2] in id (the carrier of G) \/ (the InternalRel of G)
            \/ (the InternalRel of CmpG) by Th14;
    then [h.3,h.2] in id (the carrier of G) \/ (the InternalRel of G) or
         [h.3,h.2] in the InternalRel of CmpG by XBOOLE_0:def 2;
    then [h.3,h.2] in id (the carrier of G) by A98,A100,XBOOLE_0:def 2;
    then h.2 = h.3 by RELAT_1:def 10;
    hence contradiction by A9,A10,A24,FUNCT_1:def 8;
   end;
   then A107: [3,2] in the InternalRel of CmpN4 by A11,A24;
     [3,2] in the InternalRel of N4 by ENUMSET1:29,NECKLA_2:2;
   then [3,2] in (the InternalRel of N4) /\ (the InternalRel of CmpN4)
              by XBOOLE_0:def 3,A107;
   then the InternalRel of N4 meets the InternalRel of CmpN4 by XBOOLE_0:
def 7;
   hence thesis by Th12;
  end;

   for x,y being Element of N4 holds
  [x,y] in the InternalRel of N4 iff [h.x,h.y] in the InternalRel of CmpG
 proof
  let x,y be Element of N4;
  thus [x,y] in the InternalRel of N4 implies
       [h.x,h.y] in the InternalRel of CmpG
  proof
   assume A112: [x,y] in the InternalRel of N4;
   per cases by A112,ENUMSET1:28,NECKLA_2:2;
   suppose [x,y] = [0,1];
    then x = 0 & y = 1 by ZFMISC_1:33;
   hence thesis by A27;
   end;
   suppose [x,y] = [1,0];
    then x = 1 & y = 0 by ZFMISC_1:33;
   hence thesis by A41;
   end;
   suppose [x,y] = [1,2];
    then x = 1 & y = 2 by ZFMISC_1:33;
   hence thesis by A55;
   end;
   suppose [x,y] = [2,1];
    then x = 2 & y = 1 by ZFMISC_1:33;
   hence thesis by A69;
   end;
   suppose [x,y] = [2,3];
    then x = 2 & y = 3 by ZFMISC_1:33;
   hence thesis by A83;
   end;
   suppose [x,y] = [3,2];
    then x = 3 & y = 2 by ZFMISC_1:33;
   hence thesis by A97;
  end;
  end;
  assume A125: [h.x,h.y] in the InternalRel of CmpG;
  per cases by A1,ENUMSET1:18;
  suppose A126: x = 0 & y = 0;
   then h.0 in the carrier of CmpG by A125,ZFMISC_1:106;
  hence thesis by A125,A126,NECKLACE:def 6;
  end;
  suppose x = 0 & y = 1;
  hence thesis by NECKLA_2:2,ENUMSET1:29;
  end;
  suppose x = 0 & y = 2;
    then [h.0,h.2] in (the InternalRel of G) /\ the InternalRel of CmpG
        by A26,A125,XBOOLE_0:def 3;
   then the InternalRel of G meets the InternalRel of CmpG
        by XBOOLE_0:def 7;
  hence thesis by Th12;
  end;
  suppose x = 0 & y = 3;
    then [h.0,h.3] in (the InternalRel of G) /\ the InternalRel of CmpG
        by A26,A125,XBOOLE_0:def 3;
   then the InternalRel of G meets the InternalRel of CmpG
        by XBOOLE_0:def 7;
  hence thesis by Th12;
  end;
  suppose x = 1 & y = 0;
  hence thesis by NECKLA_2:2,ENUMSET1:29;
  end;
  suppose A136:  x = 1 & y = 1;
   then h.1 in the carrier of CmpG by A125,ZFMISC_1:106;
  hence thesis by A125,A136,NECKLACE:def 6;
  end;
  suppose x = 1 & y = 2;
  hence thesis by NECKLA_2:2,ENUMSET1:29;
  end;
  suppose x = 1 & y = 3;
    then [h.1,h.3] in (the InternalRel of G) /\ the InternalRel of CmpG
        by A26,A125,XBOOLE_0:def 3;
   then the InternalRel of G meets the InternalRel of CmpG
        by XBOOLE_0:def 7;
  hence thesis by Th12;
  end;
  suppose x = 2 & y = 0;
    then [h.2,h.0] in (the InternalRel of G) /\ the InternalRel of CmpG
        by A26,A125,XBOOLE_0:def 3;
   then the InternalRel of G meets the InternalRel of CmpG
        by XBOOLE_0:def 7;
  hence thesis by Th12;
  end;
  suppose x = 2 & y = 1;
  hence thesis by NECKLA_2:2,ENUMSET1:29;
  end;
  suppose A146: x = 2 & y = 2;
   then h.2 in the carrier of CmpG by A125,ZFMISC_1:106;
  hence thesis by A125,A146,NECKLACE:def 6;
  end;
  suppose x = 2 & y = 3;
  hence thesis by NECKLA_2:2,ENUMSET1:29;
  end;
  suppose x = 3 & y = 0;
    then [h.3,h.0] in (the InternalRel of G) /\ the InternalRel of CmpG
        by A26,A125,XBOOLE_0:def 3;
   then the InternalRel of G meets the InternalRel of CmpG
        by XBOOLE_0:def 7;
  hence thesis by Th12;
  end;
  suppose x = 3 & y = 1;
    then [h.3,h.1] in (the InternalRel of G) /\ the InternalRel of CmpG
        by A26,A125,XBOOLE_0:def 3;
   then the InternalRel of G meets the InternalRel of CmpG
        by XBOOLE_0:def 7;
  hence thesis by Th12;
  end;
  suppose x = 3 & y = 2;
  hence thesis by NECKLA_2:2,ENUMSET1:29;
  end;
  suppose A156: x = 3 & y = 3;
   then h.3 in the carrier of CmpG by A125,ZFMISC_1:106;
  hence thesis by A125,A156,NECKLACE:def 6;
 end;
 end;
 hence thesis by A2,A3,A10,NECKLACE:def 2;
 end;
 assume CmpG embeds N4;
   then consider f being map of N4, CmpG such that
   A159: f is one-to-one & for x,y being Element of the carrier of N4
    holds  [x,y] in the InternalRel of N4 iff
           [f.x,f.y] in the InternalRel of CmpG by NECKLACE:def 2;
   A160: dom f = the carrier of N4 by FUNCT_2:def 1;
   A161: 0 in the carrier of N4 & 1 in the carrier of N4 &
       2 in the carrier of N4 & 3 in the carrier of N4 by ENUMSET1:19,A1;
     [0,1] in the InternalRel of N4 & [1,0] in the InternalRel of N4 &
       [1,2] in the InternalRel of N4 & [2,1] in the InternalRel of N4 &
       [2,3] in the InternalRel of N4 & [3,2] in the InternalRel of N4
       by NECKLA_2:2,ENUMSET1:29;
   then A163: [f.0,f.1] in the InternalRel of CmpG &
            [f.1,f.0] in the InternalRel of CmpG &
            [f.1,f.2] in the InternalRel of CmpG &
            [f.2,f.1] in the InternalRel of CmpG &
            [f.2,f.3] in the InternalRel of CmpG &
            [f.3,f.2] in the InternalRel of CmpG by A159,A161;
A164: [f.0,f.2] in the InternalRel of G
    proof
     assume A165: not thesis;
       [f.0,f.2] in the InternalRel of CmpG
      proof
       assume A167: not thesis;
         f.0 in the carrier of CmpG & f.2 in the carrier of CmpG by
FUNCT_2:7,A161;
       then [f.0,f.2] in [:the carrier of G,the carrier of G:]
            by ZFMISC_1:106,A3;
       then [f.0,f.2] in id (the carrier of G) \/ (the InternalRel of G)
            \/ (the InternalRel of CmpG) by Th14;
       then [f.0,f.2] in id (the carrier of G) \/ (the InternalRel of G) or
            [f.0,f.2] in the InternalRel of CmpG by XBOOLE_0:def 2;
       then [f.0,f.2] in id (the carrier of G) by A165,A167,XBOOLE_0:def 2;
       then f.0 = f.2 by RELAT_1:def 10;
       hence contradiction by A159,A160,A161,FUNCT_1:def 8;
      end;
     then A174: [0,2] in the InternalRel of N4 by A159,A161;
       [0,2] in the InternalRel of CmpN4 by ENUMSET1:29,Th11;
     then [0,2] in (the InternalRel of N4) /\ (the InternalRel of CmpN4)
              by XBOOLE_0:def 3,A174;
     then the InternalRel of N4 meets the InternalRel of CmpN4 by
XBOOLE_0:def 7;
     hence thesis by Th12;
    end;
A178: [f.2,f.0] in the InternalRel of G
   proof
    assume A179: not [f.2,f.0] in the InternalRel of G;
      [f.2,f.0] in the InternalRel of CmpG
     proof
      assume A181: not thesis;
        f.0 in the carrier of CmpG & f.2 in the carrier of CmpG by FUNCT_2:
7,A161;
      then [f.2,f.0] in [:the carrier of G,the carrier of G:]
            by ZFMISC_1:106,A3;
      then [f.2,f.0] in id (the carrier of G) \/ (the InternalRel of G)
            \/ (the InternalRel of CmpG) by Th14;
      then [f.2,f.0] in id (the carrier of G) \/ (the InternalRel of G) or
            [f.2,f.0] in the InternalRel of CmpG by XBOOLE_0:def 2;
      then [f.2,f.0] in id (the carrier of G) by A179,A181,XBOOLE_0:def 2;
      then f.0 = f.2 by RELAT_1:def 10;
      hence contradiction by A159,A160,A161,FUNCT_1:def 8;
     end;
    then A188: [2,0] in the InternalRel of N4 by A159,A161;
      [2,0] in the InternalRel of CmpN4 by ENUMSET1:29,Th11;
    then [2,0] in (the InternalRel of N4) /\ (the InternalRel of CmpN4)
              by XBOOLE_0:def 3,A188;
    then the InternalRel of N4 meets the InternalRel of CmpN4 by XBOOLE_0
:def 7;
    hence thesis by Th12;
   end;
A192: [f.0,f.3] in the InternalRel of G
   proof
    assume A193: not [f.0,f.3] in the InternalRel of G;
      [f.0,f.3] in the InternalRel of CmpG
     proof
      assume A195: not thesis;
        f.0 in the carrier of CmpG & f.3 in the carrier of CmpG by FUNCT_2:
7,A161;
      then [f.0,f.3] in [:the carrier of G,the carrier of G:]
            by ZFMISC_1:106,A3;
      then [f.0,f.3] in id (the carrier of G) \/ (the InternalRel of G)
            \/ (the InternalRel of CmpG) by Th14;
      then [f.0,f.3] in id (the carrier of G) \/ (the InternalRel of G)
or
            [f.0,f.3] in the InternalRel of CmpG by XBOOLE_0:def 2;
      then [f.0,f.3] in id (the carrier of G) by A193,A195,XBOOLE_0:def 2;
      then f.0 = f.3 by RELAT_1:def 10;
      hence contradiction by A159,A160,A161,FUNCT_1:def 8;
     end;
    then A202: [0,3] in the InternalRel of N4 by A159,A161;
      [0,3] in the InternalRel of CmpN4 by ENUMSET1:29,Th11;
    then [0,3] in (the InternalRel of N4) /\ (the InternalRel of CmpN4)
              by XBOOLE_0:def 3,A202;
    then the InternalRel of N4 meets the InternalRel of CmpN4 by XBOOLE_0
:def 7;
    hence thesis by Th12;
   end;
A206: [f.3,f.0] in the InternalRel of G
   proof
    assume A207: not [f.3,f.0] in the InternalRel of G;
      [f.3,f.0] in the InternalRel of CmpG
     proof
      assume A209: not thesis;
        f.0 in the carrier of CmpG & f.3 in the carrier of CmpG by FUNCT_2:
7,A161;
      then [f.3,f.0] in [:the carrier of G,the carrier of G:]
            by ZFMISC_1:106,A3;
      then [f.3,f.0] in id (the carrier of G) \/ (the InternalRel of G)
            \/ (the InternalRel of CmpG) by Th14;
      then [f.3,f.0] in id (the carrier of G) \/ (the InternalRel of G)
or
            [f.3,f.0] in the InternalRel of CmpG by XBOOLE_0:def 2;
      then [f.3,f.0] in id (the carrier of G) by A207,A209,XBOOLE_0:def 2;
      then f.0 = f.3 by RELAT_1:def 10;
      hence contradiction by A159,A160,A161,FUNCT_1:def 8;
     end;
    then A216: [3,0] in the InternalRel of N4 by A159,A161;
      [3,0] in the InternalRel of CmpN4 by ENUMSET1:29,Th11;
    then [3,0] in (the InternalRel of N4) /\ (the InternalRel of CmpN4)
              by XBOOLE_0:def 3,A216;
    then the InternalRel of N4 meets the InternalRel of CmpN4 by XBOOLE_0
:def 7;
    hence thesis by Th12;
   end;
A220: [f.1,f.3] in the InternalRel of G
   proof
    assume A221: not [f.1,f.3] in the InternalRel of G;
      [f.1,f.3] in the InternalRel of CmpG
     proof
      assume A223: not thesis;
        f.1 in the carrier of CmpG & f.3 in the carrier of CmpG by FUNCT_2
:7,A161;
      then [f.1,f.3] in [:the carrier of G,the carrier of G:]
            by ZFMISC_1:106,A3;
      then [f.1,f.3] in id (the carrier of G) \/ (the InternalRel of G)
            \/ (the InternalRel of CmpG) by Th14;
      then [f.1,f.3] in id (the carrier of G) \/ (the InternalRel of G)
or
            [f.1,f.3] in the InternalRel of CmpG by XBOOLE_0:def 2;
      then [f.1,f.3] in id (the carrier of G) by A221,A223,XBOOLE_0:def 2;
      then f.1 = f.3 by RELAT_1:def 10;
      hence contradiction by A159,A160,A161,FUNCT_1:def 8;
     end;
    then A230: [1,3] in the InternalRel of N4 by A159,A161;
      [1,3] in the InternalRel of CmpN4 by ENUMSET1:29,Th11;
    then [1,3] in (the InternalRel of N4) /\ (the InternalRel of CmpN4)
              by XBOOLE_0:def 3,A230;
    then the InternalRel of N4 meets the InternalRel of CmpN4 by XBOOLE_0
:def 7;
    hence thesis by Th12;
   end;
A234: [f.3,f.1] in the InternalRel of G
   proof
    assume A235: not [f.3,f.1] in the InternalRel of G;
      [f.3,f.1] in the InternalRel of CmpG
     proof
      assume A237: not thesis;
        f.1 in the carrier of CmpG & f.3 in the carrier of CmpG by FUNCT_2
:7,A161;
      then [f.3,f.1] in [:the carrier of G,the carrier of G:]
            by ZFMISC_1:106,A3;
      then [f.3,f.1] in id (the carrier of G) \/ (the InternalRel of G)
            \/ (the InternalRel of CmpG) by Th14;
      then [f.3,f.1] in id (the carrier of G) \/ (the InternalRel of G)
or
            [f.3,f.1] in the InternalRel of CmpG by XBOOLE_0:def 2;
      then [f.3,f.1] in id (the carrier of G) by A235,A237,XBOOLE_0:def 2;
      then f.1 = f.3 by RELAT_1:def 10;
      hence contradiction by A159,A160,A161,FUNCT_1:def 8;
     end;
    then A244: [3,1] in the InternalRel of N4 by A159,A161;
      [3,1] in the InternalRel of CmpN4 by ENUMSET1:29,Th11;
    then [3,1] in (the InternalRel of N4) /\ (the InternalRel of CmpN4)
              by XBOOLE_0:def 3,A244;
    then the InternalRel of N4 meets the InternalRel of CmpN4 by XBOOLE_0
:def 7;
    hence thesis by Th12;
   end;

A248:  for x,y being Element of the carrier of CmpN4 holds
  [x,y] in the InternalRel of CmpN4 iff [f.x,f.y] in the InternalRel of G
   proof
    let x,y be Element of the carrier of CmpN4;
    thus [x,y] in the InternalRel of CmpN4 implies
         [f.x,f.y] in the InternalRel of G
     proof
      assume A249: [x,y] in the InternalRel of CmpN4;
      per cases by A249,ENUMSET1:28,Th11;
      suppose [x,y] = [0,2];
       then x = 0 & y = 2 by ZFMISC_1:33;
      hence thesis by A164;
      end;
      suppose [x,y] = [2,0];
       then x = 2 & y = 0 by ZFMISC_1:33;
      hence thesis by A178;
      end;
      suppose [x,y] = [0,3];
       then x = 0 & y = 3 by ZFMISC_1:33;
      hence thesis by A192;
      end;
      suppose [x,y] = [3,0];
       then x = 3 & y = 0 by ZFMISC_1:33;
      hence thesis by A206;
      end;
      suppose [x,y] = [1,3];
       then x = 1 & y = 3 by ZFMISC_1:33;
      hence thesis by A220;
      end;
      suppose [x,y] = [3,1];
       then x = 3 & y = 1 by ZFMISC_1:33;
      hence thesis by A234;
     end;
     end;
    assume A262: [f.x,f.y] in the InternalRel of G;
    A263: the carrier of N4 = the carrier of CmpN4 by NECKLACE:def 9;
    per cases by A1,ENUMSET1:18,A263;
    suppose A264: x = 0 & y = 0;
     then f.0 in the carrier of G by A262,ZFMISC_1:106;
    hence thesis by A262,A264,NECKLACE:def 6;
    end;
    suppose x = 0 & y = 1;
      then [f.0,f.1] in (the InternalRel of G) /\ the InternalRel of CmpG
          by A163,A262,XBOOLE_0:def 3;
     then the InternalRel of G meets the InternalRel of CmpG
          by XBOOLE_0:def 7;
    hence thesis by Th12;
    end;
    suppose x = 0 & y = 2;
    hence thesis by Th11,ENUMSET1:29;
    end;
    suppose x = 0 & y = 3;
    hence thesis by Th11,ENUMSET1:29;
    end;
    suppose x = 1 & y = 0;
      then [f.1,f.0] in (the InternalRel of G) /\ the InternalRel of CmpG
          by A163,A262,XBOOLE_0:def 3;
     then the InternalRel of G meets the InternalRel of CmpG
          by XBOOLE_0:def 7;
    hence thesis by Th12;
    end;
    suppose x = 2 & y = 0;
    hence thesis by Th11,ENUMSET1:29;
    end;
    suppose x = 3 & y = 0;
    hence thesis by Th11,ENUMSET1:29;
    end;
    suppose A276: x = 1 & y = 1;
     then f.1 in the carrier of G by A262,ZFMISC_1:106;
    hence thesis by A262,A276,NECKLACE:def 6;
    end;
    suppose x = 1 & y = 2;
      then [f.1,f.2] in (the InternalRel of G) /\ the InternalRel of CmpG
          by A163,A262,XBOOLE_0:def 3;
     then the InternalRel of G meets the InternalRel of CmpG
          by XBOOLE_0:def 7;
    hence thesis by Th12;
    end;
    suppose x = 1 & y = 3;
    hence thesis by Th11,ENUMSET1:29;
    end;
    suppose x = 2 & y = 1;
      then [f.2,f.1] in (the InternalRel of G) /\ the InternalRel of CmpG
          by A163,A262,XBOOLE_0:def 3;
     then the InternalRel of G meets the InternalRel of CmpG
          by XBOOLE_0:def 7;
    hence thesis by Th12;
    end;
    suppose A285: x = 2 & y = 2;
     then f.2 in the carrier of G by A262,ZFMISC_1:106;
    hence thesis by A262,A285,NECKLACE:def 6;
    end;
    suppose x = 2 & y = 3;
      then [f.2,f.3] in (the InternalRel of G) /\ the InternalRel of CmpG
          by A163,A262,XBOOLE_0:def 3;
     then the InternalRel of G meets the InternalRel of CmpG
          by XBOOLE_0:def 7;
    hence thesis by Th12;
    end;
    suppose x = 3 & y = 1;
    hence thesis by Th11,ENUMSET1:29;
    end;
    suppose x = 3 & y = 2;
      then [f.3,f.2] in (the InternalRel of G) /\ the InternalRel of CmpG
          by A163,A262,XBOOLE_0:def 3;
     then the InternalRel of G meets the InternalRel of CmpG
          by XBOOLE_0:def 7;
    hence thesis by Th12;
    end;
    suppose A294: x = 3 & y = 3;
     then f.3 in the carrier of G by A262,ZFMISC_1:106;
    hence thesis by A262,A294,NECKLACE:def 6;
   end;
   end;
   reconsider f as map of CmpN4,G by A2,A3;
   consider g being map of N4,CmpN4 such that
   A296: g is isomorphic by NECKLACE:30,WAYBEL_1:def 8;
   A297: g is one-to-one monotone & ex g1 being map of CmpN4,N4 st g1 = g" &
             g1 is monotone by A296,WAYBEL_0:def 38;
   reconsider h = f*g as map of N4,G;
   A298: h is one-to-one by FUNCT_1:46,A159,A297;
    for x,y being Element of N4 holds
      [x,y] in the InternalRel of N4 iff [h.x,h.y] in the InternalRel of G
   proof
    let x,y be Element of N4;
    thus [x,y] in the InternalRel of N4 implies
               [h.x,h.y] in the InternalRel of G
    proof
     assume [x,y] in the InternalRel of N4;
     then x <= y by ORDERS_1:def 9;
     then g.x <= g.y by WAYBEL_1:def 2,A297;
     then [g.x,g.y] in the InternalRel of CmpN4 by ORDERS_1:def 9;
     then [f.(g.x),f.(g.y)] in the InternalRel of G by A248;
     then [(f*g).x,f.(g.y)] in the InternalRel of G by FUNCT_2:21;
     hence thesis by FUNCT_2:21;
    end;
    assume [h.x,h.y] in the InternalRel of G;
    then [f.(g.x),h.y] in the InternalRel of G by FUNCT_2:21;
    then [f.(g.x),f.(g.y)] in the InternalRel of G by FUNCT_2:21;
    then [g.x,g.y] in the InternalRel of CmpN4 by A248;
    then g.x <= g.y by ORDERS_1:def 9;
    then x <= y by WAYBEL_0:66,A296;
    hence thesis by ORDERS_1:def 9;
   end;
 hence thesis by A298,NECKLACE:def 2;
end;

theorem Th25:
  for G being non empty irreflexive RelStr holds
   G is N-free iff ComplRelStr G is N-free
proof
 let G be non empty irreflexive RelStr;
 thus G is N-free implies ComplRelStr G is N-free
  proof
   assume A1: G is N-free;
   assume not thesis;
   then ComplRelStr G embeds Necklace 4 by NECKLA_2:def 1;
   then G embeds Necklace 4 by Th24;
   hence contradiction by A1,NECKLA_2:def 1;
  end;
 assume A5: ComplRelStr G is N-free;
 assume not thesis;
 then G embeds Necklace 4 by NECKLA_2:def 1;
 then ComplRelStr G embeds Necklace 4 by Th24;
 hence contradiction by A5,NECKLA_2:def 1;
end;

begin :: Connected Graphs

definition
  let R be RelStr;
  mode path of R is RedSequence of the InternalRel of R;
end;

definition
  let R be RelStr;
  attr R is path-connected means
:Def1: for x,y being set st x in the carrier of R &
   y in the carrier of R & x <> y   holds
   the InternalRel of R reduces x,y or the InternalRel of R reduces y,x;
end;

registration
  cluster empty -> path-connected RelStr;
  correctness
   proof
    let R be RelStr;
    assume A1: R is empty;
    let x,y be set;
    assume x in the carrier of R & y in the carrier of R & x <> y;
    hence thesis by A1,STRUCT_0:def 1;
   end;
end;

registration
  cluster connected -> path-connected (non empty RelStr);
  correctness
   proof
    let R be non empty RelStr;
    set c_R = the carrier of R,
        I_R = the InternalRel of R;
    assume A1: R is connected;
      for x,y being set st x in the carrier of R & y in the carrier of R & x
<> y
    holds (the InternalRel of R) reduces x,y
          or (the InternalRel of R) reduces y,x
     proof
      let x,y being set such that A3: x in c_R & y in c_R & x <> y;
      reconsider x,y as Element of R by A3;
      A4: x <= y or y <= x by WAYBEL_0:def 29,A1;
      per cases by A4,ORDERS_1:def 9;
      suppose [x,y] in I_R;
       then A6: <*x,y*> is RedSequence of I_R by REWRITE1:8;
         len <*x,y*> = 2 & <*x,y*>.1 = x & <*x,y*>.2 = y by FINSEQ_1:61;
      hence thesis by REWRITE1:def 3,A6;
      end;
      suppose [y,x] in I_R;
       then A9: <*y,x*> is RedSequence of I_R by REWRITE1:8;
         len <*y,x*> = 2 & <*y,x*>.1 = y & <*y,x*>.2 = x by FINSEQ_1:61;
      hence thesis by REWRITE1:def 3,A9;
     end;
     end;
    hence thesis by Def1;
   end;
end;

theorem Th26:
  for R being non empty transitive reflexive RelStr, x,y being Element of R
   holds the InternalRel of R reduces x,y
         implies [x,y] in the InternalRel of R
proof
  let R be non empty transitive reflexive RelStr;
  let x,y be Element of R;
  set c_R = the carrier of R,
      I_R = the InternalRel of R;
  assume A1: I_R reduces x,y;
  A2: I_R is_reflexive_in c_R by ORDERS_1:def 4;
  A3: I_R is_transitive_in c_R by ORDERS_1:def 5;
  consider p being RedSequence of I_R such that
  A4: p.1 = x & p.len p = y by REWRITE1:def 3,A1;
  reconsider p as FinSequence;
  A5: len p > 0 &
  for i being Nat st i in dom p & i+1 in dom p holds [p.i, p.(i+1)] in I_R
        by REWRITE1:def 2;
  per cases;
  suppose x = y;
  hence thesis by A2,RELAT_2:def 1;
  end;
  suppose x <> y;
     0 + 1 <= len p by SCMFSA_7:28,A5;
   then A8: len p in dom p by FINSEQ_3:27;
   defpred P[Nat] means $1 in dom p implies [p.1,p.$1] in I_R;
   A9: P[1] by A4,A2,RELAT_2:def 1;
   A10: for k be non empty Nat st P[k] holds P[k+1]
     proof
      let k be non empty Nat such that A11: P[k];
      assume A12: k+1 in dom p;
      A13: k <=  k+1 by NAT_1:29;
        0 < k by NAT_1:19;
      then A15: 0 + 1 <= k by SCMFSA_7:28;
        k+1 <= len p by A12,FINSEQ_3:27;
      then A17: 1 <= k & k <= len p by A15,A13,AXIOMS:22;
      then k in dom p by FINSEQ_3:27;
      then [p.k,p.(k+1)] in I_R by REWRITE1:def 2,A12;
      then p.1 in c_R & p.k in c_R & p.(k+1) in c_R & [p.1,p.k] in I_R &
      [p.k,p.(k+1)] in I_R by A11,A17,FINSEQ_3:27,ZFMISC_1:106;
      hence thesis by A3,RELAT_2:def 8;
     end;
     for k be non empty Nat holds P[k] from BINARITH:sch 1(A9,A10);
  hence thesis by A8,A4,A5;
 end;
end;

registration
  cluster path-connected -> connected (non empty transitive reflexive RelStr);
  correctness
   proof
    let R be non empty transitive reflexive RelStr;
    assume A1: R is path-connected;
    set I_R = the InternalRel of R;
      for x,y being Element of R holds x <= y or y <= x
     proof
      let x,y being Element of R;
      per cases;
      suppose x = y;
      hence thesis;
      end;
      suppose x <> y;
       then I_R reduces x,y or I_R reduces y,x by Def1,A1;
       then [x,y] in I_R or [y,x] in I_R by Th26;
      hence thesis by ORDERS_1:def 9;
     end;
     end;
    hence thesis by WAYBEL_0:def 29;
   end;
end;

theorem Th27: for R be symmetric RelStr,x,y being set
 st x in the carrier of R & y in the carrier of R holds
 the InternalRel of R reduces x,y implies the InternalRel of R reduces y,x
  proof
   let R be symmetric RelStr;
   set c_R = the carrier of R,
       I_R = the InternalRel of R;
   let x,y be set such that x in c_R & y in c_R;
   assume I_R reduces x,y;
   then consider p being RedSequence of I_R such that
   A2: p.1 = x & p.len p = y by REWRITE1:def 3;
   reconsider p as FinSequence;
   A3: I_R = I_R~ by RELAT_2:30;
   A4: (Rev p).len p = x & (Rev p).1 = y by A2,FINSEQ_5:65;
     I_R reduces y,x
    proof
     reconsider q = Rev p as RedSequence of I_R by A3,REWRITE1:10;
       q.1 = y & q.len q = x by FINSEQ_5:def 3,A4;
     hence thesis by REWRITE1:def 3;
    end;
   hence thesis;
  end;

definition
  let R be symmetric RelStr;
  redefine attr R is path-connected means
:Def2:  for x,y being set st x in the carrier of R &
  y in the carrier of R & x <> y holds (the InternalRel of R) reduces x,y;
  compatibility
   proof
    set I_R = the InternalRel of R,
        c_R = the carrier of R;
    thus R is path-connected implies
    for x,y being set st x in the carrier of R & y in the carrier of R & x <> y
    holds (the InternalRel of R) reduces x,y
     proof
      assume A1: R is path-connected;
      let x,y being set such that A2: x in c_R & y in c_R & x <> y;
      per cases by A1,A2,Def1;
      suppose I_R reduces x,y;
      hence thesis;
      end;
      suppose I_R reduces y,x;
      hence thesis by Th27,A2;
     end;
     end;
    assume for x,y be set st x in the carrier of R
    & y in the carrier of R & x <> y holds I_R reduces x,y;
    then for x,y being set st x in the carrier of R
    & y in the carrier of R & x <> y
    holds (the InternalRel of R) reduces x,y
    or (the InternalRel of R) reduces y,x;
    hence R is path-connected by Def1;
   end;
end;

definition
  let R be RelStr;
  let x be Element of R;
  func component x -> Subset of R equals
:Def3: Class(EqCl the InternalRel of R, x);
  coherence;
end;

theorem Th28:  for R being non empty RelStr, x being Element of R
  holds x in component x
proof
 let R be non empty RelStr;
 let x be Element of R;
   x in Class(EqCl the InternalRel of R, x) by EQREL_1:28;
 hence thesis by Def3;
end;

registration
  let R be non empty RelStr;
  let x be Element of R;
  cluster component x -> non empty;
  correctness by Th28;
end;

theorem Th29: for R being  RelStr, x being Element of R,
  y be set st y in component x holds [x,y] in EqCl the InternalRel of R
proof
 let R be RelStr;
 let x be Element of R;
 let y be set;
 set  I_R = the InternalRel of R,
      CL = Class(EqCl I_R, x);
 assume y in component x;
 then y in CL by Def3;
 then [y,x] in EqCl I_R by EQREL_1:27;
 hence thesis by EQREL_1:12;
end;

theorem Th30: for R being RelStr, x being Element of R, A being set
  holds A = component x iff for y being set
  holds y in A iff [x,y] in EqCl the InternalRel of R
proof
 let R be RelStr;
 let x being Element of R;
 let A be set;
 set I_R = the InternalRel of R,
     CL = Class(EqCl I_R, x);
 A1: A = component x implies
     for y being set holds [x,y] in EqCl I_R implies y in A
   proof
    assume A2: A = component x;
    let y be set;
    assume [x,y] in EqCl I_R;
    then [y,x] in EqCl I_R by EQREL_1:12;
    then y in CL by EQREL_1:27;
    hence thesis by A2,Def3;
   end;
   (for y being set holds y in A iff [x,y] in EqCl (the InternalRel of R))
    implies A = component  x
    proof
     assume A7: for y being set holds
          y in A iff [x,y] in EqCl (the InternalRel of R);
     A8: A c= component x
      proof
       let a be set;
       assume a in A;
       then [x,a] in EqCl I_R by A7;
       then [a,x] in EqCl I_R by EQREL_1:12;
       then a in CL by EQREL_1:27;
       hence thesis by Def3;
      end;
       component x c= A
      proof
       let a be set;
       assume a in component x;
       then a in CL by Def3;
       then [a,x] in EqCl I_R by EQREL_1:27;
       then [x,a] in EqCl I_R by EQREL_1:12;
       hence thesis by A7;
      end;
     hence thesis by XBOOLE_0:def 10,A8;
    end;
  hence thesis by A1,Th29;
end;

theorem Th31:
 for R be non empty irreflexive symmetric RelStr holds
  R is not path-connected implies
  ex G1,G2 being non empty strict irreflexive symmetric RelStr
  st the carrier of G1 misses the carrier of G2 &
  the RelStr of R = union_of(G1,G2)
proof
 let R be non empty irreflexive symmetric RelStr;
 set c_R = the carrier of R,
     I_R = the InternalRel of R;
 assume R is not path-connected;
 then consider x,y being set such that
 A2: x in c_R & y in c_R & x <> y & not I_R reduces x,y by Def2;
 reconsider x,y as Element of R by A2;
 set A1 = component x,
     A2 = (the carrier of R) \ A1;
 reconsider A2 as Subset of R by XBOOLE_1:36;
 set G1 = subrelstr A1,
     G2 = subrelstr A2;
 A3: the carrier of G1 = A1 & the carrier of G2 = A2 by YELLOW_0:def 15;
 then A4: (the carrier of G1) misses (the carrier of G2) by XBOOLE_1:79;
   I_R = I_R~ by RELAT_2:30;
 then not I_R \/ I_R~ reduces x,y by A2;
 then not x,y  are_convertible_wrt I_R by REWRITE1:def 4;
 then not [x,y] in EqCl I_R by MSUALG_6:41;
 then y in c_R & not y in A1 by Th30;
 then the carrier of G2 is non empty by A3,XBOOLE_0:def 4;
 then A11: G1 is non empty strict RelStr &
   G2 is non empty strict RelStr by STRUCT_0:def 1,A3;
 A12: c_R = A1 \/ A2
  proof
   thus c_R c= A1 \/ A2
    proof
     let a be set;
     assume A13: a in c_R;
     assume not thesis;
     then not a in A1 & not a in A2 by XBOOLE_0:def 2;
     hence contradiction by XBOOLE_0:def 4,A13;
    end;
    thus thesis;
  end;
 then A16: c_R = the carrier of union_of(G1,G2) by A3,NECKLA_2:def 2;
 A17: the InternalRel of G1 misses the InternalRel of G2
  proof
   set I_G1 = the InternalRel of G1,
       I_G2 = the InternalRel of G2;
   assume not thesis;
   then I_G1 /\ I_G2 <> {} by XBOOLE_0:def 7;
   then consider a be set such that
   A20: a in I_G1 /\ I_G2 by XBOOLE_0:def 1;
   A21: a in I_G1 & a in I_G2 by A20,XBOOLE_0:def 3;
   then consider c1,c2 be set such that
   A22: a = [c1,c2] & c1 in A1 & c2 in A1 by RELSET_1:6,A3;
   consider g1,g2 be set such that
   A23: a = [g1,g2] & g1 in A2 & g2 in A2  by RELSET_1:6,A3,A21;
     c1 in A1 & c1 in A2 by A22,A23,ZFMISC_1:33;
   then c1 in A1 /\ A2 by XBOOLE_0:def 3;
   hence contradiction by A3,A4,XBOOLE_0:def 7;
  end;
 A26: the InternalRel of G2 = I_R \ (the InternalRel of G1)
  proof
   set I_G1 = the InternalRel of G1,
       I_G2 = the InternalRel of G2;
   thus I_G2 c= I_R \ I_G1
    proof
     let a be set;
     assume A27: a in I_G2;
     then consider g1,g2 be set such that
     A28: a = [g1,g2] & g1 in A2 & g2 in A2 by RELSET_1:6,A3;
     reconsider g1,g2 as Element of G2 by A28,YELLOW_0:def 15;
     A29: g1 <= g2 by ORDERS_1:def 9,A27,A28;
     reconsider u1 = g1, u2 = g2 as Element of R by A28;
       u1 <= u2 by A29,YELLOW_0:60;
     then A31: a in I_R by A28,ORDERS_1:def 9;
       not a in I_G1
      proof
       assume not thesis;
       then a in I_G1 /\ I_G2 by A27,XBOOLE_0:def 3;
       hence contradiction by A17,XBOOLE_0:def 7;
      end;
     hence thesis by XBOOLE_0:def 4,A31;
    end;
   let a be set;
   assume a in I_R \ I_G1;
    then A36: a in I_R & not a in I_G1 by XBOOLE_0:def 4;
    then consider c1,c2 be set such that
    A37: a = [c1,c2] & c1 in c_R & c2 in c_R by RELSET_1:6;
    reconsider c1,c2 as Element of R by A37;
    A38: c1 <= c2 by ORDERS_1:def 9,A36,A37;
    per cases by A12,XBOOLE_0:def 2;
    suppose A39: c1 in A1 & c2 in A1;
     then reconsider d1 = c1 as Element of G1 by YELLOW_0:def 15;
     reconsider d2 = c2 as Element of G1 by A39,YELLOW_0:def 15;
       d1 <= d2 by A3,A38,YELLOW_0:61;
    hence thesis by A36,A37,ORDERS_1:def 9;
    end;
    suppose A41: c1 in A1 & c2 in A2;
       a in [:A1,A2:] by A41,A37,ZFMISC_1:def 2;
     then A43: a in [:A1,A2:] /\ I_R by A36,XBOOLE_0:def 3;
       [:A1,A2:] misses I_R
      proof
       assume not thesis;
       then [:A1,A2:] /\ I_R <> {} by XBOOLE_0:def 7;
       then consider b be set such that
       A47: b in [:A1,A2:] /\ I_R by XBOOLE_0:def 1;
       A48: b in [:A1,A2:] & b in I_R by XBOOLE_0:def 3,A47;
       then consider b1,b2 be set such that
       A49: b1 in A1 & b2 in A2 &  b = [b1,b2] by ZFMISC_1:def 2;
       reconsider b1 as Element of R by A49;
       reconsider b2 as Element of R by A49;
       A50: b1 in c_R & b2 in c_R & I_R c= EqCl I_R by MSUALG_5:def 1;
         [x,b1] in EqCl I_R by Th30,A49;
       then [x,b2] in EqCl I_R by A48,A49,A50,EQREL_1:13;
       then b2 in A1 by Th30;
       then b2 in A1 /\ A2 by A49,XBOOLE_0:def 3;
       hence thesis by XBOOLE_0:def 7,A3,A4;
      end;
    hence thesis by A43,XBOOLE_0:def 7;
    end;
    suppose A55: c1 in A2 & c2 in A1;
       a in [:A2,A1:] by A55,A37,ZFMISC_1:def 2;
     then A57: a in [:A2,A1:] /\ I_R by A36,XBOOLE_0:def 3;
       [:A2,A1:] misses I_R
      proof
       assume not thesis;
       then [:A2,A1:] /\ I_R <> {} by XBOOLE_0:def 7;
       then consider b be set such that
       A61: b in [:A2,A1:] /\ I_R by XBOOLE_0:def 1;
       A62: b in [:A2,A1:] & b in I_R by XBOOLE_0:def 3,A61;
       then consider b1,b2 be set such that
       A63: b1 in A2 & b2 in A1 &  b = [b1,b2] by ZFMISC_1:def 2;
       reconsider b1 as Element of R by A63;
       reconsider b2 as Element of R by A63;
         b1 in c_R & b2 in c_R & I_R c= EqCl I_R by MSUALG_5:def 1;
       then A65:  [b2,b1] in EqCl I_R by EQREL_1:12,A62,A63;
         [x,b2] in EqCl I_R by Th30,A63;
       then [x,b1] in EqCl I_R by EQREL_1:13,A65;
       then b1 in A1 by Th30;
       then b1 in A1 /\ A2 by A63,XBOOLE_0:def 3;
       hence thesis by XBOOLE_0:def 7,A3,A4;
      end;
    hence thesis by A57,XBOOLE_0:def 7;
    end;
    suppose A70: c1 in A2 & c2 in A2;
     then reconsider d1 = c1 as Element of G2 by YELLOW_0:def 15;
     reconsider d2 = c2 as Element of G2 by A70,YELLOW_0:def 15;
       d1 <= d2 by A3,A38,A70,YELLOW_0:61;
    hence thesis by A37,ORDERS_1:def 9;
  end;
  end;
   I_R = (the InternalRel of G1) \/ (the InternalRel of G2)
  proof
   set I_G1 = the InternalRel of G1,
       I_G2 = the InternalRel of G2;
   thus I_R c= I_G1 \/ I_G2
    proof
     let a be set;
     assume A73: a in I_R;
     assume not thesis;
     then not a in I_G1 & not a in I_G2 by XBOOLE_0:def 2;
     hence contradiction by XBOOLE_0:def 4,A26,A73;
    end;
    let a be set;
    assume A76: a in I_G1 \/ I_G2;
    per cases by A76,XBOOLE_0:def 2;
    suppose A77: a in I_G1;
     then consider v,w be set such that
     A78: a = [v,w] & v in A1 & w in A1 by RELSET_1:6,A3;
     reconsider v,w as Element of G1 by A78,YELLOW_0:def 15;
     A79: v <= w by A78,A77,ORDERS_1:def 9;
     reconsider u1 = v, u2 = w as Element of R by A78;
       u1 <= u2 by A79,YELLOW_0:60;
    hence thesis by A78,ORDERS_1:def 9;
    end;
    suppose A81: a in I_G2;
     then consider v,w be set such that
     A82: a = [v,w] & v in A2 & w in A2 by RELSET_1:6,A3;
     reconsider v,w as Element of G2 by A82,YELLOW_0:def 15;
     A83: v <= w by A82,A81,ORDERS_1:def 9;
     reconsider u1 = v, u2 = w as Element of R by A82;
       u1 <= u2 by A83,YELLOW_0:60;
    hence thesis by A82,ORDERS_1:def 9;
  end;
  end;
 then I_R = the InternalRel of union_of(G1,G2) by NECKLA_2:def 2;
 hence thesis by A4,A11,A16;
end;

theorem Th32:
 for R be non empty irreflexive symmetric RelStr holds
  ComplRelStr R is not path-connected implies
  ex G1,G2 being non empty strict irreflexive symmetric RelStr
  st the carrier of G1 misses the carrier of G2 &
  the RelStr of R = sum_of(G1,G2)
proof
 let R be non empty irreflexive symmetric RelStr;
 set c_R = the carrier of R,
     I_R = the InternalRel of R,
     CR = ComplRelStr R,
     I_CR = the InternalRel of ComplRelStr R,
     c_CR = the carrier of ComplRelStr R;
 assume CR is not path-connected;
 then consider x,y being set such that
 A2: x in c_CR & y in c_CR & x <> y & not I_CR reduces x,y by Def2;
 reconsider x,y as Element of CR by A2;
 set A1 = component x,
     A2 = (the carrier of R) \ A1;
 reconsider A1 as Subset of R by NECKLACE:def 9;
 reconsider A2 as Subset of R by XBOOLE_1:36;
 set G1 = subrelstr A1,
     G2 = subrelstr A2;
 A3: the carrier of G1 = A1 & the carrier of G2 = A2 by YELLOW_0:def 15;
 then A4: (the carrier of G1) misses (the carrier of G2) by XBOOLE_1:79;
   I_CR = I_CR~ by RELAT_2:30;
 then not I_CR \/ I_CR~ reduces x,y by A2;
 then not x,y are_convertible_wrt I_CR by REWRITE1:def 4;
 then not [x,y] in EqCl I_CR by MSUALG_6:41;
 then y in c_R & not y in A1 by A2,Th30,NECKLACE:def 9;
 then the carrier of G2 is non empty by XBOOLE_0:def 4,A3;
 then A11: G1 is non empty strict RelStr &
     G2 is non empty strict RelStr by A3,STRUCT_0:def 1;
 A12: c_R = A1 \/ A2
  proof
   thus c_R c= A1 \/ A2
    proof
     let a be set;
     assume A13: a in c_R;
     assume not thesis;
     then not a in A1 & not a in A2 by XBOOLE_0:def 2;
     hence contradiction by XBOOLE_0:def 4,A13;
    end;
   let a be set;
   assume A16: a in A1 \/ A2;
   per cases by A16,XBOOLE_0:def 2;
   suppose a in A1;
   hence thesis;
   end;
   suppose a in A2;
   hence thesis;
  end;
  end;
 then A19: c_R = the carrier of sum_of(G1,G2) by A3,NECKLA_2:def 3;
 set I_G1 = the InternalRel of G1,
     I_G2 = the InternalRel of G2,
     G1G2 = [:the carrier of G1,the carrier of G2:],
     G2G1 = [:the carrier of G2,the carrier of G1:];
 A20: the InternalRel of G1 misses the InternalRel of G2
  proof
   assume not thesis;
   then I_G1 /\ I_G2 <> {} by XBOOLE_0:def 7;
   then consider a be set such that
   A23: a in I_G1 /\ I_G2 by XBOOLE_0:def 1;
   A24: a in I_G1 & a in I_G2 by A23,XBOOLE_0:def 3;
   then consider c1,c2 be set such that
   A25: a = [c1,c2] & c1 in A1 & c2 in A1 by RELSET_1:6,A3;
   consider g1,g2 be set such that
   A26: a = [g1,g2] & g1 in A2 & g2 in A2  by RELSET_1:6,A3,A24;
     c1 in A1 & c1 in A2 by A25,A26,ZFMISC_1:33;
   then c1 in A1 /\ A2 by XBOOLE_0:def 3;
   hence contradiction by A3,A4,XBOOLE_0:def 7;
  end;
 A29: the InternalRel of G2 = I_R \ I_G1 \ G1G2 \ G2G1
  proof
   thus I_G2 c= I_R \ I_G1 \ G1G2 \ G2G1
    proof
     let a be set;
     assume A30: a in I_G2;
     then consider g1,g2 be set such that
     A31: a = [g1,g2] & g1 in A2 & g2 in A2 by RELSET_1:6,A3;
     reconsider g1,g2 as Element of G2 by A31,YELLOW_0:def 15;
     A32: g1 <= g2 by ORDERS_1:def 9,A30,A31;
     reconsider u1 = g1, u2 = g2 as Element of R by A31;
       u1 <= u2 by A32,YELLOW_0:60;
     then A34: a in I_R by A31,ORDERS_1:def 9;
     A35: not a in I_G1
      proof
       assume not thesis;
       then a in I_G1 /\ I_G2 by A30,XBOOLE_0:def 3;
       hence contradiction by A20,XBOOLE_0:def 7;
      end;
     A38: not a in G1G2
        proof
         assume a in G1G2;
         then g1 in A1 & g2 in A2 by ZFMISC_1:106,A3,A31;
         then g1 in A1 /\ A2 by XBOOLE_0:def 3,A31;
         hence thesis by XBOOLE_0:def 7,A4,A3;
        end;
     A42: not a in G2G1
        proof
         assume a in G2G1;
         then g1 in A2 & g2 in A1 by ZFMISC_1:106,A3,A31;
         then g2 in A1 /\ A2 by XBOOLE_0:def 3,A31;
         hence thesis by XBOOLE_0:def 7,A4,A3;
        end;
       a in I_R \ I_G1 by XBOOLE_0:def 4,A34,A35;
     then a in I_R \ I_G1 \ G1G2 by XBOOLE_0:def 4,A38;
     hence thesis by XBOOLE_0:def 4,A42;
    end;
   let a be set;
   assume a in I_R \ I_G1 \ G1G2 \ G2G1;
    then A49: a in I_R \ I_G1 \ G1G2 & not a in G2G1 by XBOOLE_0:def 4;
    then A50: a in I_R \ I_G1 & not a in G1G2 & not a in G2G1 by XBOOLE_0:def 4
;
    then A51: a in I_R & not a in I_G1 & not a in G1G2 & not a in G2G1
                                        by XBOOLE_0:def 4;
    then consider c1,c2 be set such that
    A52: a = [c1,c2] & c1 in c_R & c2 in c_R by RELSET_1:6;
    reconsider c1,c2 as Element of R by A52;
    A53: c1 <= c2 by ORDERS_1:def 9,A51,A52;
    per cases by A12,XBOOLE_0:def 2;
    suppose A54: c1 in A1 & c2 in A1;
     then reconsider d1 = c1 as Element of G1 by YELLOW_0:def 15;
     reconsider d2 = c2 as Element of G1 by A54,YELLOW_0:def 15;
       d1 <= d2 by A3,A53,YELLOW_0:61;
    hence thesis by A51,A52,ORDERS_1:def 9;
    end;
    suppose c1 in A1 & c2 in A2;
    hence thesis by A3,A50,A52,ZFMISC_1:106;
    end;
    suppose c1 in A2 & c2 in A1;
    hence thesis by A3,A49,A52,ZFMISC_1:106;
    end;
    suppose A58: c1 in A2 & c2 in A2;
     then reconsider d1 = c1,d2 = c2 as Element of G2 by YELLOW_0:def 15;
       d1 <= d2 by A3,A53,A58,YELLOW_0:61;
    hence thesis by A52,ORDERS_1:def 9;
  end;
  end;
   I_R = (the InternalRel of G1) \/ (the InternalRel of G2)
       \/ [:the carrier of G1,the carrier of G2:]
       \/ [:the carrier of G2,the carrier of G1:]
 proof
  set G1_G2 = [:the carrier of G1,the carrier of G2:],
      G2_G1 = [:the carrier of G2,the carrier of G1:];
  thus I_R c= I_G1 \/ I_G2 \/ G1_G2 \/ G2_G1
   proof
    let a be set;
    assume A61: a in I_R;
    assume not thesis;
    then not a in (I_G1 \/ I_G2 \/ G1_G2) & not a in G2_G1 by XBOOLE_0:def 2;
    then not a in (I_G1 \/ I_G2) & not a in G1_G2 & not a in G2_G1
                   by XBOOLE_0:def 2;
    then A65: not a in I_G1 & not a in I_G2 & not a in G1_G2 & not a in G2_G1
                    by XBOOLE_0:def 2;
    then not a in I_R \ I_G1 \ G1G2 or a in G2G1 by A29,XBOOLE_0:def 4;
    then not a in I_R \ I_G1 or a in G1G2 or a in G2G1 by XBOOLE_0:def 4;
    hence thesis by A65,A61,XBOOLE_0:def 4;
   end;
  let a be set such that
  A68: a in I_G1 \/ I_G2 \/ G1_G2 \/ G2_G1;
    a in I_G1 \/ I_G2 \/ G1_G2 or a in G2_G1 by A68,XBOOLE_0:def 2;
  then A70: a in I_G1 \/ I_G2 or a in G1_G2 or a in G2_G1 by XBOOLE_0:def 2;
  per cases by A70,XBOOLE_0:def 2;
  suppose A71: a in I_G1;
   then consider v,w be set such that
   A72: a = [v,w] & v in A1 & w in A1 by RELSET_1:6,A3;
   reconsider v,w as Element of G1 by A72,YELLOW_0:def 15;
   A73: v <= w by A72,A71,ORDERS_1:def 9;
   reconsider u1 = v, u2 = w as Element of R by A72;
     u1 <= u2 by A73,YELLOW_0:60;
  hence thesis by A72,ORDERS_1:def 9;
  end;
  suppose A75: a in I_G2;
   then consider v,w be set such that
   A76: a = [v,w] & v in A2 & w in A2 by RELSET_1:6,A3;
   reconsider v,w as Element of G2 by A76,YELLOW_0:def 15;
   A77: v <= w by A76,A75,ORDERS_1:def 9;
   reconsider u1 = v, u2 = w as Element of R by A76;
     u1 <= u2 by A77,YELLOW_0:60;
  hence thesis by A76,ORDERS_1:def 9;
  end;
  suppose A79: a in G1_G2;
   then consider v,w be set such that
   A80: a = [v,w] by ZFMISC_1:102;
   A81: v in A1 & w in A2 by A80,A79,ZFMISC_1:106,A3;
   then reconsider v,w as Element of CR by NECKLACE:def 9;
   A82: v <> w
      proof
       assume A83: v = w;
         A1 /\ A2 = {} by A4,A3,XBOOLE_0:def 7;
       hence thesis by A81,A83,XBOOLE_0:def 3;
      end;
   assume A85: not thesis;
   A86: not a in id c_R by RELAT_1:def 10,A82,A80;
     [v,w] in [:c_R,c_R:] by ZFMISC_1:106,A81;
   then a in [:c_R,c_R:] \ I_R by A80,A85,XBOOLE_0:def 4;
   then a in I_R` by SUBSET_1:def 5;
   then a in I_R` \ id c_R by XBOOLE_0:def 4,A86;
   then [v,w] in I_CR by A80,NECKLACE:def 9;
   then v,w are_convertible_wrt I_CR by REWRITE1:30;
   then A93: [v,w] in EqCl I_CR by MSUALG_6:41;
     [x,v] in EqCl I_CR by A81,Th30;
   then [x,w] in EqCl I_CR by A93,EQREL_1:13;
   then w in component x by Th30;
   then w in A1 /\ A2 by A81,XBOOLE_0:def 3;
  hence thesis by A3,A4,XBOOLE_0:def 7;
  end;
  suppose A98: a in G2_G1;
   then consider v,w be set such that
   A99: a = [v,w] by ZFMISC_1:102;
   A100: v in A2 & w in A1 by A99,A98,ZFMISC_1:106,A3;
   then reconsider v,w as Element of CR by NECKLACE:def 9;
   A101: v <> w
      proof
       assume A102: v = w;
         A1 /\ A2 = {} by A4,A3,XBOOLE_0:def 7;
       hence thesis by A100,A102,XBOOLE_0:def 3;
      end;
   assume A104: not thesis;
   A105: not a in id c_R by RELAT_1:def 10,A101,A99;
     [v,w] in [:c_R,c_R:] by ZFMISC_1:106,A100;
   then a in [:c_R,c_R:] \ I_R by A99,A104,XBOOLE_0:def 4;
   then a in I_R` by SUBSET_1:def 5;
   then a in I_R` \ id c_R by XBOOLE_0:def 4,A105;
   then [v,w] in I_CR by A99,NECKLACE:def 9;
   then v,w are_convertible_wrt I_CR by REWRITE1:30;
    then [v,w] in EqCl I_CR by MSUALG_6:41;
   then A113: [w,v] in EqCl I_CR by EQREL_1:12;
     [x,w] in EqCl I_CR by A100,Th30;
   then [x,v] in EqCl I_CR by A113,EQREL_1:13;
   then v in component x by Th30;
   then v in A1 /\ A2 by A100,XBOOLE_0:def 3;
  hence thesis by A3,A4,XBOOLE_0:def 7;
 end;
 end;
 then I_R = the InternalRel of sum_of(G1,G2) by NECKLA_2:def 3;
 hence thesis by A4,A11,A19;
end;

Lm1:
  for X being non empty finite set, A,B being non empty set
  st X = A \/ B & A misses B holds Card A <` Card X & Card B <` Card X
proof
 let X be non empty finite set;
 let A,B be non empty set;
 set n = Card X;
 assume A1: X = A \/ B & A misses B;
 then A c= X & B c= X by XBOOLE_1:7;
 then A3: Card A <=` n & Card B <=` n by CARD_1:27;
 then reconsider A as finite set by CARD_2:68;
 reconsider B as finite set by CARD_2:68,A3;
 A4: n = card A + card B by CARD_2:53,A1;
   card A <> 0 & card B <> 0 by CARD_4:17;
 then A6: card A >= 1 & card B >= 1 by RLVECT_1:99;
 A7: card A < n
  proof
   assume not thesis;
   then card A + card B >= n+1 by A6,REAL_1:55;
   hence thesis by A4,NAT_1:38;
  end;
   card B < n
  proof
   assume not thesis;
   then card A + card B >= n+1 by A6,REAL_1:55;
   hence thesis by A4,NAT_1:38;
  end;
 hence thesis by A7,CARD_2:58;
end;

theorem
    for G being irreflexive RelStr st G in fin_RelStr_sp
  holds ComplRelStr G in fin_RelStr_sp
proof
 let G be irreflexive RelStr;
 assume A1: G in fin_RelStr_sp;
 defpred P[Nat] means for G being irreflexive RelStr st
   Card the carrier of G = $1 & G in fin_RelStr_sp
        holds ComplRelStr G in fin_RelStr_sp;
 A2: for k being Nat st for n being Nat st n < k holds P[n] holds P[k]
  proof
   let k be Nat such that
   A3: for n being Nat st n < k holds P[n];
   let G be irreflexive RelStr;
   assume that A4: Card the carrier of G = k and
               A5: G in fin_RelStr_sp;
   per cases by A5,NECKLA_2:6;
   suppose G is strict non empty trivial RelStr;
   hence thesis by A5,Th15;
   end;
   suppose ex G1,G2 being strict RelStr st
     (the carrier of G1) misses (the carrier of G2) &
     G1 in fin_RelStr_sp & G2 in fin_RelStr_sp &
     (G = union_of(G1,G2) or G = sum_of(G1,G2) );
    then consider G1,G2 being strict RelStr such that
    A8: (the carrier of G1) misses the carrier of G2 &
        G1 in fin_RelStr_sp & G2 in fin_RelStr_sp and
    A9: G = union_of(G1,G2) or G = sum_of(G1,G2);
    A10: G1 is non empty finite by NECKLA_2:4,A8;
    then the carrier of G1 is non empty finite
             by GROUP_1:def 13,STRUCT_0:def 1;
    then consider n1 being Nat such that
    A12: Card the carrier of G1 = card n1 by CARD_4:4;
    A13: Card the carrier of G1 = n1 by FINSEQ_1:78,A12;
    A14: G2 is non empty finite by NECKLA_2:4,A8;
    then the carrier of G2 is non empty finite
             by GROUP_1:def 13,STRUCT_0:def 1;
    then consider n2 being Nat such that
    A16: Card the carrier of G2 = card n2 by CARD_4:4;
    A17: Card the carrier of G2 = n2 by FINSEQ_1:78,A16;
   thus thesis
    proof
     per cases by A9;
     suppose A18: G = union_of(G1,G2);
      then reconsider G1 as irreflexive RelStr by Th9;
      reconsider G2 as irreflexive RelStr by Th9,A18;
      A19: the carrier of G = (the carrier of G1) \/ the carrier of G2 by A18,
NECKLA_2:def 2;
      reconsider cG1 = the carrier of G1 as non empty finite set by A10,GROUP_1
:def 13,STRUCT_0:def 1;
      reconsider cG2 = the carrier of G2 as non empty finite set by A14,GROUP_1
:def 13,STRUCT_0:def 1;
      A20: Card the carrier of G = card cG1 + card cG2 by A8,A19,CARD_2:53;
        card cG1 = n1 by A12,FINSEQ_1:78;
      then n1 <> 0 by CARD_4:17;
      then A23: n1 > 0 by NAT_1:19;
        card cG2 = n2 by A16,FINSEQ_1:78;
      then n2 <> 0 by CARD_4:17;
      then A26: n2 > 0 by NAT_1:19;
        n1 < k
       proof
        assume not thesis;
        then k+0 < n2 + n1 by A26,REAL_1:67;
        hence thesis by A4,A12,A17,A20,FINSEQ_1:78;
       end;
      then A30: ComplRelStr G1 in fin_RelStr_sp by A13,A8,A3;
        n2 < k
       proof
        assume not thesis;
        then k+0 < n1 + n2 by A23,REAL_1:67;
        hence thesis by A4,A12,A17,A20,FINSEQ_1:78;
       end;
      then A34: ComplRelStr G2 in fin_RelStr_sp by A17,A8,A3;
      A35: ComplRelStr G = sum_of(ComplRelStr G1,ComplRelStr G2) by Th17,A8,A18
;
        the carrier of ComplRelStr G1 = the carrier of G1 &
      the carrier of ComplRelStr G2 = the carrier of G2 by NECKLACE:def 9;
     hence thesis by NECKLA_2:def 5,A30,A34,A8,A35;
     end;
     suppose A37: G = sum_of(G1,G2);
      then reconsider G1 as irreflexive RelStr by Th9;
      reconsider G2 as irreflexive RelStr by Th9,A37;
      A38: the carrier of G = (the carrier of G1) \/ the carrier of G2 by A37,
NECKLA_2:def 3;
      reconsider cG1 = the carrier of G1 as non empty finite set by A10,GROUP_1
:def 13,STRUCT_0:def 1;
      reconsider cG2 = the carrier of G2 as non empty finite set by A14,GROUP_1
:def 13,STRUCT_0:def 1;
      A39: Card the carrier of G = card cG1 + card cG2 by A8,A38,CARD_2:53;
        card cG1 = n1 by A12,FINSEQ_1:78;
      then n1 <> 0 by CARD_4:17;
      then A42: n1 > 0 by NAT_1:19;
        card cG2 = n2 by A16,FINSEQ_1:78;
      then n2 <> 0 by CARD_4:17;
      then A45: n2 > 0 by NAT_1:19;
        n1 < k
       proof
        assume not thesis;
        then k+0 < n2 + n1 by A45,REAL_1:67;
        hence thesis by A4,A12,A17,A39,FINSEQ_1:78;
       end;
      then A49: ComplRelStr G1 in fin_RelStr_sp by A13,A8,A3;
        n2 < k
       proof
        assume not thesis;
        then k+0 < n1 + n2 by A42,REAL_1:67;
        hence thesis by A4,A12,A17,A39,FINSEQ_1:78;
       end;
      then A53: ComplRelStr G2 in fin_RelStr_sp by A17,A8,A3;
      A54: ComplRelStr G = union_of(ComplRelStr G1,ComplRelStr G2)
           by Th18,A8,A37;

       the carrier of ComplRelStr G1 = the carrier of G1 &
      the carrier of ComplRelStr G2 = the carrier of G2 by NECKLACE:def 9;
     hence thesis by A8,A49,A53,A54,NECKLA_2:def 5;
    end;
    end;
  end;
  end;
 A56: for k being Nat holds P[k] from NAT_1:sch 4(A2);
   G is finite by NECKLA_2:4,A1;
 then the carrier of G is finite by GROUP_1:def 13;
 then consider n being Nat such that
 A59: Card the carrier of G = card n by CARD_4:4;
 thus thesis by A56,A1,A59;
end;

theorem Th34:
  for R be irreflexive symmetric RelStr
   st Card (the carrier of R) = 2 & the carrier of R in FinSETS
   holds the RelStr of R in fin_RelStr_sp
proof
 let R be irreflexive symmetric RelStr;
 assume A1: Card (the carrier of R) = 2 & the carrier of R in FinSETS;
 then consider a,b being set such that
 A2: the carrier of R = {a,b} &
 (the InternalRel of R = {[a,b],[b,a]} or the InternalRel of R = {})
       by Th6;
 set A = {a},
     B = {b};
 A3: A c= the carrier of R
  proof
   let x be set;
   assume x in A;
   then x = a by TARSKI:def 1;
   hence thesis by TARSKI:def 2,A2;
  end;
 then reconsider A as Subset of the carrier of R;
 A6: B c= the carrier of R
  proof
   let x be set;
   assume x in B;
   then x = b by TARSKI:def 1;
   hence thesis by TARSKI:def 2,A2;
  end;
 then reconsider B as Subset of the carrier of R;
 set H1 = subrelstr A,
     H2 = subrelstr B;
    H1 is non empty
  proof
     assume not thesis;
     then the carrier of H1 is empty by STRUCT_0:def 1;
     hence thesis by YELLOW_0:def 15;
  end;
 then reconsider H1 as non empty strict irreflexive symmetric RelStr;
 A12: the carrier of H1 = A by YELLOW_0:def 15;
 then A13: the carrier of H1 is non empty trivial by REALSET1:def 12;
   the carrier of H1 in FinSETS
  proof
     the carrier of H1 c= the carrier of R by YELLOW_0:def 15,A3;
   hence thesis by A1,CLASSES1:6,CLASSES2:def 2;
  end;
 then A16: H1 in fin_RelStr_sp by NECKLA_2:def 5,A13;
   H2 is non empty
  proof
     assume not thesis;
     then the carrier of H2 is empty by STRUCT_0:def 1;
     hence thesis by YELLOW_0:def 15;
  end;
 then reconsider H2 as non empty strict irreflexive symmetric RelStr;
 A20: the carrier of H2 = B by YELLOW_0:def 15;
 then A21: the carrier of H2 is non empty trivial by REALSET1:def 12;
   the carrier of H2 in FinSETS
  proof
     the carrier of H2 c= the carrier of R by YELLOW_0:def 15,A6;
   hence thesis by A1,CLASSES1:6,CLASSES2:def 2;
  end;
 then A24: H2 in fin_RelStr_sp by NECKLA_2:def 5,A21;
    a <> b
    proof
     assume not thesis;
     then the carrier of R = {a} by ENUMSET1:69,A2;
     hence thesis by A1,CARD_1:50,CARD_2:20;
    end; then
 A25: A misses B by ZFMISC_1:17;
 then A34: (the carrier of H1) misses (the carrier of H2) by A20,YELLOW_0:def
15;
 A35: the carrier of R =
   (the carrier of H1) \/ (the carrier of H2) by A12,A20,ENUMSET1:41,A2;
   the InternalRel of H1 c= [:{a},{a}:] by A12;
 then the InternalRel of H1 c= {[a,a]} by ZFMISC_1:35;
 then A38: the InternalRel of H1 = {} or the InternalRel of H1 = {[a,a]}
      by ZFMISC_1:39;
 A39: the InternalRel of H1 = {}
  proof
   assume A40: not thesis;
     a in A by TARSKI:def 1;
   then a in the carrier of H1 by YELLOW_0:def 15;
   then not [a,a] in the InternalRel of H1 by NECKLACE:def 6;
   hence thesis by A38,A40,TARSKI:def 1;
  end;
   the InternalRel of H2 c= [:{b},{b}:] by A20;
 then the InternalRel of H2 c= {[b,b]} by ZFMISC_1:35;
 then A46: the InternalRel of H2 = {} or the InternalRel of H2 = {[b,b]}
      by ZFMISC_1:39;
 A47: the InternalRel of H2 = {}
  proof
   assume A48: not thesis;
     b in B by TARSKI:def 1;
   then b in the carrier of H2 by YELLOW_0:def 15;
   then not [b,b] in the InternalRel of H2 by NECKLACE:def 6;
   hence thesis by A46,A48,TARSKI:def 1;
  end;
 per cases by A2;
 suppose A52: the InternalRel of R = {[a,b],[b,a]};
  set S = sum_of(H1,H2);
    the InternalRel of S = (the InternalRel of H1) \/ (the InternalRel of
H2) \/ [:A,B:] \/ [:B,A:] by A12,A20,NECKLA_2:def 3;
  then the InternalRel of S = {[a,b]} \/ [:{b},{a}:] by ZFMISC_1:35,A39,A47;
  then the InternalRel of S = {[a,b]} \/ {[b,a]} by ZFMISC_1:35;
  then A56: the InternalRel of S = the InternalRel of R by A52,ENUMSET1:41;
    the carrier of S = the carrier of R by A35,NECKLA_2:def 3;
 hence thesis by A12,A16,A20,A24,A25,A56,NECKLA_2:def 5;
 end;
 suppose A58: the InternalRel of R = {};
  set U = union_of(H1,H2);
  A59: the InternalRel of U = (the InternalRel of H1) \/ (the InternalRel of H2
)
      by NECKLA_2:def 2;
    the carrier of U = the carrier of R by A35,NECKLA_2:def 2;
 hence thesis by A16,A24,A34,A39,A47,A58,A59,NECKLA_2:def 5;
end;
end;

theorem
    for R be RelStr st R in fin_RelStr_sp holds R is symmetric
proof
 let X be RelStr;
 assume A1: X in fin_RelStr_sp;
 per cases;
 suppose X is trivial;
  then A3: the carrier of X is trivial by REALSET1:def 13;
thus thesis
proof
 per cases by A3,REALSET1:def 12;
 suppose A4: the carrier of X is empty;
  let a,b be set;
  assume a in the carrier of X & b in the carrier of X &
             [a,b] in the InternalRel of X;
 hence thesis by A4;
 end;
 suppose ex x being set st the carrier of X = {x};
  then consider x being set such that A7: the carrier of X = {x};
  A8: [:the carrier of X,the carrier of X:] = {[x,x]} by ZFMISC_1:35,A7;
 thus thesis
  proof
   per cases by A8,ZFMISC_1:39;
   suppose A9: the InternalRel of X = {};
    let a,b be set;
    assume a in the carrier of X & b in the carrier of X &
               [a,b] in the InternalRel of X;
   hence thesis by A9;
   end;
   suppose A11: the InternalRel of X = {[x,x]};
    let a,b be set;
    assume A12: a in the carrier of X & b in the carrier of X &
               [a,b] in the InternalRel of X;
    then [a,b] = [x,x] by TARSKI:def 1,A11;
    then a = x & b = x by ZFMISC_1:33;
   hence thesis by A12;
  end;
  end;
end;
end;
 end;
 suppose A15: not X is trivial;
 defpred P[Nat] means
  for X be non empty RelStr st not X is trivial & X in fin_RelStr_sp holds
  Card (the carrier of X) <=` $1  implies X is symmetric;
 A16: P[0]
  proof
   let X be non empty RelStr such that
   A17: not X is trivial & X in fin_RelStr_sp;
   consider R be strict RelStr such that
   A18: X = R & the carrier of R in FinSETS by NECKLA_2:def 4,A17;
   set cX = the carrier of X;
   reconsider cX as finite set by A18,NECKLA_2:3;
   A19: not cX is trivial by A17,REALSET1:def 13;
   assume A20: Card (the carrier of X) <=` 0;
   assume not thesis;
   per cases by CARD_2:57,19,A20;
   suppose A21: card cX < 0;
      card cX >= 2 by SPPOL_1:2,A19;
   hence thesis by A21,AXIOMS:22;
   end;
   suppose card cX = 0;
   hence thesis by GRAPH_5:5;
  end;
  end;
 A24: for k being Nat st P[k] holds P[k+1]
 proof
  let k be Nat such that A25: P[k];
  let Y be non empty RelStr such that
  A26: not Y is trivial & Y in fin_RelStr_sp;
  set cY = the carrier of Y;
  consider R be strict RelStr such that
  A27: Y = R & the carrier of R in FinSETS by NECKLA_2:def 4,A26;
  reconsider cY as finite set by A27,NECKLA_2:3;
  consider H1,H2 be strict RelStr such that
  A28:  the carrier of H1 misses the carrier of H2 &
       H1 in fin_RelStr_sp & H2 in fin_RelStr_sp and
  A29: (Y = union_of(H1,H2) or Y = sum_of(H1,H2)) by A26,NECKLA_2:6;
  set cH1 = the carrier of H1,
      cH2 = the carrier of H2;
  A30: Card cY = Card ((cH1) \/ cH2) by NECKLA_2:def 2,def 3,A29;
  consider R1 be strict RelStr such that
  A31: H1 = R1 & the carrier of R1 in FinSETS by NECKLA_2:def 4,A28;
  reconsider cH1 as finite set by A31,NECKLA_2:3;
  consider R2 be strict RelStr such that
  A32: H2 = R2 & the carrier of R2 in FinSETS by NECKLA_2:def 4,A28;
  reconsider cH2 as finite set by A32,NECKLA_2:3;
  A33: card cY = card cH1 + card cH2 by CARD_2:53,A28,A30;
  assume Card (the carrier of Y) <=` k+1;
  then Card cY <=` Card (k+1) by CARD_1:66;
  then card cY <= card (k+1) by CARD_2:57;
  then A37:  card cY <= k+1 by CARD_1:66;
  A38: H1 is non empty & H2 is non empty by A28,NECKLA_2:4;
  then cH1 is non empty & cH2 is non empty by STRUCT_0:def 1;
  then card cH1 <> 0 & card cH2 <> 0 by CARD_4:17;
  then A41: card cH1 >= 1 & card cH2 >= 1 by RLVECT_1:99;
  per cases by A33,A37,A41,NAT_1:19,26,REAL_1:def 5;
  suppose card cY <= k;
   then card cY <= card k by CARD_1:66;
   then Card (the carrier of Y) <=` Card k by CARD_2:57;
   then Card (the carrier of Y) <=` k by CARD_1:66;
  hence thesis by A25,A26;
  end;
  suppose A46: card cY = k+1 & k = 0;
   consider x be set;
     card cY = card {x} by A46,CARD_1:79;
   then cY,{x} are_equipotent by CARD_1:21;
   then consider y be set such that A49: cY = {y} by CARD_1:48;
     not cY is trivial by A26,REALSET1:def 13;
  hence thesis by REALSET1:def 12,A49;
  end;
  suppose A51: card cH1 + card cH2 = k+1 & k > 0 & card cH1 = 1 & card cH2 = 1;
   then consider x be set such that
   A52: cH1 = {x} by CARD_2:60;
   consider y be set such that A53: cH2 = {y} by CARD_2:60,A51;
     cH1 is non empty trivial by A52,REALSET1:def 12;
   then A55: the InternalRel of H1 is_symmetric_in cH1 by Th5;
     cH2 is non empty trivial by A53,REALSET1:def 12;
   then the InternalRel of H2 is_symmetric_in cH2 by Th5;
   then reconsider H2 as symmetric RelStr by NECKLACE:def 4;
   reconsider H1 as symmetric RelStr by A55,NECKLACE:def 4;
     union_of(H1,H2) is symmetric & sum_of(H1,H2) is symmetric;
  hence thesis by A29;
  end;
  suppose A59:card cH1 + card cH2 = k+1 & k > 0 & card cH1 = 1 & card cH2 > 1;
   then consider x be set such that
   A60: cH1 = {x} by CARD_2:60;
     cH1 is non empty trivial set by A60,REALSET1:def 12;
   then the InternalRel of H1 is_symmetric_in cH1 by Th5;
   then reconsider H1 as symmetric RelStr by NECKLACE:def 4;
     card cH2 < k+1 by A59,NAT_1:38;
   then card cH2 <= k by NAT_1:38;
   then card cH2 <= card k by CARD_1:66;
   then Card cH2 <=` Card k by CARD_2:57;
   then A67: Card cH2 <=` k by CARD_1:66;
     card cH2 <> 0 by A59;
   then card cH2 is non trivial by A59,NAT_2:30;
   then card cH2 >= 2 by NAT_2:31;
   then cH2 is non empty non trivial by A38,SPPOL_1:2,STRUCT_0:def 1;
   then H2 is non empty non trivial by REALSET1:def 13,STRUCT_0:def 1;
   then reconsider H2 as symmetric RelStr by A25,A28,A67;
     union_of(H1,H2) is symmetric & sum_of(H1,H2) is symmetric;
  hence thesis by A29;
  end;
  suppose A74: card cH1 + card cH2 = k+1 & k > 0 & card cH1 > 1 & card cH2 = 1;
   then consider x be set such that
   A75: cH2 = {x} by CARD_2:60;
     cH2 is non empty trivial set by A75,REALSET1:def 12;
   then the InternalRel of H2 is_symmetric_in cH2 by Th5;
   then reconsider H2 as symmetric RelStr by NECKLACE:def 4;
     card cH1 < k+1 by A74,NAT_1:38;
   then card cH1 <= k by NAT_1:38;
   then card cH1 <= card k by CARD_1:66;
   then Card cH1 <=` Card k by CARD_2:57;
   then A82: Card cH1 <=` k by CARD_1:66;
     card cH1 <> 0 by A74;
   then card cH1 is non trivial by A74,NAT_2:30;
   then card cH1 >= 2 by NAT_2:31;
   then cH1 is non empty non trivial by A38,SPPOL_1:2,STRUCT_0:def 1;
   then H1 is non empty non trivial by REALSET1:def 13,STRUCT_0:def 1;
   then reconsider H1 as symmetric RelStr by A25,A28,A82;
     union_of(H1,H2) is symmetric & sum_of(H1,H2) is symmetric;
  hence thesis by A29;
  end;
  suppose A89: card cH1 + card cH2 = k+1 & k > 0 & card cH1 > 1 & card cH2 > 1;
     card cH1 < k+1
     proof
      assume not thesis;
      then card cH1 + card cH2 >= (k+1)+1 by A41,REAL_1:55;
      hence thesis by A89,NAT_1:38;
     end;
   then card cH1 <= k by NAT_1:38;
   then card cH1 <= card k by CARD_1:66;
   then Card cH1 <=` Card k by CARD_2:57;
   then A98: Card cH1 <=` k by CARD_1:66;
     card cH1 <> 0 by A89;
   then card cH1 is non trivial by A89,NAT_2:30;
   then card cH1 >= 2 by NAT_2:31;
   then cH1 is non empty non trivial by A38,SPPOL_1:2,STRUCT_0:def 1;
   then H1 is non empty non trivial by REALSET1:def 13,STRUCT_0:def 1;
   then reconsider H1 as symmetric RelStr by A25,A28,A98;
     card cH2 < k+1
     proof
      assume not thesis;
      then card cH1 + card cH2 >= (k+1)+1 by A41,REAL_1:55;
      hence thesis by A89,NAT_1:38;
     end;
   then card cH2 <= k by NAT_1:38;
   then card cH2 <= card k by CARD_1:66;
   then Card cH2 <=` Card k by CARD_2:57;
   then A112: Card cH2 <=` k by CARD_1:66;
     card cH2 <> 0 by A89;
   then card cH2 is non trivial by A89,NAT_2:30;
   then card cH2 >= 2 by NAT_2:31;
   then cH2 is non empty non trivial by A38,SPPOL_1:2,STRUCT_0:def 1;
   then H2 is non empty non trivial by REALSET1:def 13,STRUCT_0:def 1;
   then reconsider H2 as symmetric RelStr by A25,A28,A112;
     union_of(H1,H2) is symmetric & sum_of(H1,H2) is symmetric;
  hence thesis by A29;
 end;
 end;
 A119: for k being Nat holds P[k] from NAT_1:sch 1(A16,A24);
 consider R be strict RelStr such that
 A120: X = R & the carrier of R in FinSETS by NECKLA_2:def 4,A1;
 reconsider X as non empty RelStr by NECKLA_2:4,A1;
   the carrier of X is finite by NECKLA_2:3,A120;
 then consider n be Nat such that
 A122: Card (the carrier of X) = Card n by CARD_4:4;
  Card n = n by CARD_1:66;
 hence thesis by A1,A15,A119,A122;
end;
end;

theorem Th36:   for G being RelStr, H1,H2 being non empty RelStr,
  x being Element of the carrier of H1, y being Element of the carrier of H2
  st G = union_of(H1,H2) & the carrier of H1 misses the carrier of H2
   holds not [x,y] in the InternalRel of G
proof
 let G be RelStr;
 let H1,H2 be non empty RelStr;
 let x be Element of the carrier of H1;
 let y be Element of the carrier of H2;
 assume A1: G = union_of(H1,H2) &
    (the carrier of H1) misses (the carrier of H2);
 assume not thesis;
 then A3: [x,y] in (the InternalRel of H1) \/ the InternalRel of H2
      by NECKLA_2:def 2,A1;
 per cases by A3,XBOOLE_0:def 2;
 suppose [x,y] in (the InternalRel of H1);
  then x in the carrier of H1 & y in the carrier of H1 by ZFMISC_1:106;
   then y in (the carrier of H1) /\ the carrier of H2 by XBOOLE_0:def 3;
 hence thesis by A1,XBOOLE_0:def 7;
 end;
 suppose [x,y] in (the InternalRel of H2);
  then x in the carrier of H2 & y in the carrier of H2 by ZFMISC_1:106;
   then x in (the carrier of H1) /\ the carrier of H2 by XBOOLE_0:def 3;
 hence thesis by A1,XBOOLE_0:def 7;
end;
end;

theorem    for G being RelStr, H1,H2 being non empty RelStr,
  x being Element of the carrier of H1, y being Element of the carrier of H2
  st G = sum_of(H1,H2) holds not [x,y] in the InternalRel of ComplRelStr G
proof
 let G being RelStr,H1,H2 being non empty RelStr,
  x being Element of the carrier of H1,
           y being Element of the carrier of H2;
 assume A1: G = sum_of(H1,H2);
 set cH1 = the carrier of H1,
     cH2 = the carrier of H2,
     IH1 = the InternalRel of H1,
     IH2 = the InternalRel of H2;
   [x,y] in [:the carrier of H1,the carrier of H2:] by ZFMISC_1:106;
 then [x,y] in [:cH1,cH2:] \/ [:cH2,cH1:] by XBOOLE_0:def 2;
 then [x,y] in IH2 \/ ([:cH1,cH2:] \/ [:cH2,cH1:])  by XBOOLE_0:def 2;
 then [x,y] in IH1 \/  (IH2 \/ ([:cH1,cH2:] \/ [:cH2,cH1:])) by XBOOLE_0:
def 2;
 then [x,y] in IH1 \/  (IH2 \/ [:cH1,cH2:] \/ [:cH2,cH1:]) by XBOOLE_1:4;
 then [x,y] in IH1 \/ IH2 \/ [:cH1,cH2:] \/ [:cH2,cH1:] by XBOOLE_1:113;
 then A8: [x,y] in the InternalRel of G by A1,NECKLA_2:def 3;
   not [x,y] in the InternalRel of ComplRelStr G
  proof
   assume not thesis;
   then [x,y] in (the InternalRel of G) /\ the InternalRel of ComplRelStr G
        by XBOOLE_0:def 3,A8;
   then (the InternalRel of G) meets the InternalRel of ComplRelStr G
        by XBOOLE_0:def 7;
   hence contradiction by Th12;
  end;
 hence thesis;
end;

theorem Th38:
 for G being non empty symmetric RelStr, x being Element of the carrier of G,
 R1,R2 being non empty RelStr st the carrier of R1 misses the carrier of R2
 &  subrelstr ([#]G \ {x}) = union_of(R1,R2) & G is path-connected
 holds ex b being Element of the carrier of R1 st [b,x] in the InternalRel of G
proof
 let G be non empty symmetric RelStr;
 let x be Element of the carrier of G;
 let R1,R2 be non empty RelStr;
 assume that A1: the carrier of R1 misses the carrier of R2 and
        A2: subrelstr ([#]G \ {x}) = union_of(R1,R2) and
        A3: G is path-connected;
 set R = subrelstr ([#]G \ {x}),
     A = the carrier of R;
 A4: A = [#]G \ {x} by YELLOW_0:def 15;
 then A5: A = (the carrier of G) \ {x} by PRE_TOPC:12;
 reconsider A as Subset of the carrier of G by YELLOW_0:def 15;
 consider a being Element of the carrier of R1;
   the carrier of R1 c= (the carrier of R1) \/ the carrier of R2
                      by XBOOLE_1:7;
 then A7: the carrier of R1 c= the carrier of R by NECKLA_2:def 2,A2;
 A8: the carrier of R = A;
 then the carrier of R1 c= the carrier of G by A7,XBOOLE_1:1;
 then A10: a in the carrier of G by TARSKI:def 3;
   x <> a
  proof
   assume not thesis;
   then x in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 2;
   then x in (the carrier of G) \ {x} by A5,A2,NECKLA_2:def 2;
   then not x in {x} by XBOOLE_0:def 4;
   hence thesis by TARSKI:def 1;
  end;
 then (the InternalRel of G) reduces x,a by Def2,A3,A10;
 then consider p being FinSequence such that
 A17: len p > 0 & p.1 = x & p.len p = a and
 A18: for i being Nat st i in dom p & i+1 in dom p
           holds [p.i, p.(i+1)] in (the InternalRel of G) by REWRITE1:12;
 defpred P[Nat] means p.$1 in the carrier of R1 & $1 in dom p &
    for k being Nat st k > $1 holds
        k in dom p implies p.k in the carrier of R1;
 A19: ex k being Nat st P[k]
  proof
     P[len p]
    proof
     len p in dom p
     proof
       p <> {} by A17,FINSEQ_1:25;
       hence thesis by SCMFSA_7:12;
     end;
     hence thesis by A17,FINSEQ_3:27;
    end;
   hence thesis;
  end;
    ex n0 being Nat st P[n0] &
     for n being Nat st P[n] holds n >= n0 from NAT_1:sch 5(A19);
  then consider n0 being Nat such that
  A28: P[n0] and
  A29: for n being Nat st P[n] holds n >= n0;
    n0 <> 0
   proof
    assume not thesis;
     then 0 in Seg (len p) by A28,FINSEQ_1:def 3;
    hence contradiction by FINSEQ_1:3;
   end;
  then consider k0 being Nat such that
  A33: n0 = k0 + 1 by NAT_1:22;
  A34: n0 <> 1
   proof
    assume A35: not thesis;
    x in the carrier of G & not x in {x} by A4,A7,A17,A28,A35,XBOOLE_0:def 4;
    hence contradiction by TARSKI:def 1;
   end;
    A39: k0 >= 1
    proof
     assume not thesis;
     then k0 = 0 by CQC_THE1:2;
     hence contradiction by A34,A33;
    end;
    k0 <= len p
    proof
     A43: k0 <= n0 by A33,REAL_1:69;
       n0 in Seg (len p) by A28,FINSEQ_1:def 3;
     then n0 <= len p by FINSEQ_1:3;
     hence thesis by AXIOMS:22,A43;
    end;then
  A38: k0 in dom p by A39,FINSEQ_3:27;
  A47: k0 < n0 by A33,NAT_1:38;
  A49: for k being Nat st k > k0
   holds k in dom p implies p.k in the carrier of R1
   proof
    assume not thesis;
    then consider k being Nat such that A51: k > k0
           & k in dom p & not p.k in the carrier of R1;
      k > n0
    proof
      per cases by REAL_1:def 5;
      suppose k < n0;
      hence thesis by A33,A51,SCMFSA_7:28;
      end;
      suppose n0 < k;
      hence thesis;
      end;
      suppose n0 = k;
      hence thesis by A28,A51;
     end;
    end;
    hence contradiction by A28,A51;
   end;
  A56: [p.k0,p.(k0+1)] in the InternalRel of G by A18,A28,A33,A38;
  then A57: p.k0 in the carrier of G & p.n0 in the carrier of G
          by ZFMISC_1:106,A33;
  A58: the carrier of G = (the carrier of R) \/ {x}
    proof
     thus the carrier of G c= (the carrier of R) \/ {x}
      proof
       let a be set;
       assume A59: a in the carrier of G;
         per cases;
         suppose a = x;
          then a in {x} by TARSKI:def 1;
         hence thesis by XBOOLE_0:def 2;
         end;
         suppose a <> x;
          then a in the carrier of G & not a in {x} by A59,TARSKI:def 1;
          then a in A by A5,XBOOLE_0:def 4;
         hence thesis by XBOOLE_0:def 2;
        end;
      end;
     let a be set;
     assume A65: a in (the carrier of R) \/ {x};
        per cases by A65,XBOOLE_0:def 2;
        suppose a in the carrier of R;
        hence thesis by A4;
        end;
        suppose a in {x};
        hence thesis;
       end;
    end;
  thus thesis
   proof
    per cases by A58,A57,XBOOLE_0:def 2;
    suppose A68: p.k0 in the carrier of R;
     then p.k0 in (the carrier of R1) \/ the carrier of R2
             by NECKLA_2:def 2,A2;
     then A70: p.k0 in the carrier of R1 or
          p.k0 in the carrier of R2 by XBOOLE_0:def 2;
     set u = p.k0,
         v = p.n0;
       [u,v] in [:the carrier of R,the carrier of R:]
             by A7,A28,A68,ZFMISC_1:106;
     then A72: [u,v] in (the InternalRel of G)|_2 the carrier of R
             by WELLORD1:16,A56,A33;
     reconsider u as Element of the carrier of R2 by A29,A38,A47,A49,A70;
     reconsider v as Element of the carrier of R1 by A28;
      not [u,v] in the InternalRel of R
       proof
        assume A74: not thesis;
        A75: v in the carrier of R1;
          u in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 2;
        then A77: u in the carrier of R by NECKLA_2:def 2,A2;
          the InternalRel of R is_symmetric_in the carrier of R
            by NECKLACE:def 4;
        then [v,u] in the InternalRel of R by A7,A74,A75,A77,RELAT_2:def 3;
        hence thesis by A1,A2,Th36;
       end;
    hence thesis by A72,YELLOW_0:def 14;
    end;
    suppose p.k0 in {x};
     then A81: p.k0 = x by TARSKI:def 1;
     set b = p.n0;
     reconsider b as Element of the carrier of R1 by A28;
     A82: b in the carrier of R by A7,TARSKI:def 3;
       (the InternalRel of G) is_symmetric_in the carrier of G
       by NECKLACE:def 4;
     then [b,x] in (the InternalRel of G) by A8,A33,A56,A81,A82,RELAT_2:def 3;
     hence thesis;
    end;
    end;
end;

theorem Th39:
  for G being non empty symmetric irreflexive RelStr,
  a,b,c,d being Element of the carrier of G,
  Z being Subset of the carrier of G st Z = {a,b,c,d}
  & a,b,c,d are_mutually_different & [a,b] in the InternalRel of G
  & [b,c] in the InternalRel of G  & [c,d] in the InternalRel of G
  & not [a,c] in the InternalRel of G
  & not [a,d] in the InternalRel of G
  & not [b,d] in the InternalRel of G
    holds subrelstr Z embeds Necklace 4
proof
 let G be non empty symmetric irreflexive symmetric RelStr;
 let a,b,c,d be Element of the carrier of G;
 let Z be Subset of the carrier of G;
 assume that A1: Z = {a,b,c,d} and
   A2: a,b,c,d are_mutually_different and
   A3: [a,b] in the InternalRel of G and
   A4: [b,c] in the InternalRel of G and
   A5: [c,d] in the InternalRel of G and
   A6: not [a,c] in the InternalRel of G and
   A7: not [a,d] in the InternalRel of G and
   A8: not [b,d] in the InternalRel of G;
 set H = subrelstr Z,
     N4 = Necklace 4,
     IH = the InternalRel of H,
     cH = the carrier of H,
     IG = the InternalRel of G,
     X = {[a,a],[a,b],[b,a],[b,b],[a,c],[a,d],[b,c],[b,d]},
     Y = {[c,a],[c,b],[d,a],[d,b],[c,c],[c,d],[d,c],[d,d]};
A9:  the InternalRel of H = {[a,b],[b,a],[b,c],[c,b],[c,d],[d,c]}
  proof
   thus the InternalRel of H c= {[a,b],[b,a],[b,c],[c,b],[c,d],[d,c]}
    proof
     let x be set;
     assume x in IH;
     then x in IG |_2 cH by YELLOW_0:def 14;
     then A12: x in IG /\ [:cH,cH:] by WELLORD1:def 6;
     then A13: x in IG & x in [:cH,cH:] by XBOOLE_0:def 3;
      the carrier of H = Z by YELLOW_0:def 15;
     then A15: x in X \/ Y by A1,A13,Th3;
     per cases by A15,XBOOLE_0:def 2;
     suppose A16: x in X;
     thus thesis
      proof
       per cases by A16,ENUMSET1:38;
       suppose A17: x = [a,a];
         not [a,a] in IG by NECKLACE:def 6;
       hence thesis by A12,A17,XBOOLE_0:def 3;
       end;
       suppose x =[a,b];
       hence thesis by ENUMSET1:29;
       end;
       suppose x =[b,a];
       hence thesis by ENUMSET1:29;
       end;
       suppose A21: x =[b,b];
         not [b,b] in IG by NECKLACE:def 6;
       hence thesis by A12,A21,XBOOLE_0:def 3;
       end;
       suppose x = [a,c];
       hence thesis by A6,A12,XBOOLE_0:def 3;
       end;
       suppose x = [a,d];
       hence thesis by A7,A12,XBOOLE_0:def 3;
       end;
       suppose x = [b,c];
       hence thesis by ENUMSET1:29;
       end;
       suppose x =[b,d];
       hence thesis by A8,A12,XBOOLE_0:def 3;
      end;
      end;
     end;
     suppose A27: x in Y;
BA:  IG is_symmetric_in the carrier of G by NECKLACE:def 4;
     thus thesis
      proof
       per cases by A27,ENUMSET1:38;
       suppose x = [c,a];
       hence thesis by A6,A13,RELAT_2:def 3,BA;
       end;
       suppose x = [c,b];
       hence thesis by ENUMSET1:29;
       end;
       suppose x = [d,a];
       hence thesis by A7,A13,RELAT_2:def 3,BA;
       end;
       suppose x = [d,b];
       hence thesis by A8,A13,RELAT_2:def 3,BA;
       end;
       suppose A35: x = [c,c];
         not [c,c] in IG by NECKLACE:def 6;
       hence thesis by A12,A35,XBOOLE_0:def 3;
       end;
       suppose x = [c,d];
       hence thesis by ENUMSET1:29;
       end;
       suppose x = [d,c];
       hence thesis by ENUMSET1:29;
       end;
       suppose A39: x = [d,d];
         not [d,d] in IG by NECKLACE:def 6;
       hence thesis by A12,A39,XBOOLE_0:def 3;
      end;
      end;
    end;
    end;
   let x be set;
   assume A41: x in {[a,b],[b,a],[b,c],[c,b],[c,d],[d,c]};
   per cases by A41,ENUMSET1:28;
   suppose A42: x = [a,b];
      a in Z & b in Z by A1,ENUMSET1:19;
    then a in cH & b in cH by YELLOW_0:def 15;
    then [a,b] in [:cH,cH:] by ZFMISC_1:106;
    then x in IG /\ [:cH,cH:] by XBOOLE_0:def 3,A3,A42;
    then x in IG |_2 cH by WELLORD1:def 6;
   hence thesis by YELLOW_0:def 14;
   end;
   suppose A48: x = [b,a];
      IG is_symmetric_in the carrier of G by NECKLACE:def 4;
    then A50: [b,a] in IG by RELAT_2:def 3,A3;
      b in Z & a in Z by A1,ENUMSET1:19;
    then b in cH & a in cH by YELLOW_0:def 15;
    then [b,a] in [:cH,cH:] by ZFMISC_1:106;
    then x in IG /\ [:cH,cH:] by A48,A50,XBOOLE_0:def 3;
    then x in IG |_2 cH by WELLORD1:def 6;
   hence thesis by YELLOW_0:def 14;
   end;
   suppose A56: x = [b,c];
      b in Z & c in Z by A1,ENUMSET1:19;
    then b in cH & c in cH by YELLOW_0:def 15;
    then [b,c] in [:cH,cH:] by ZFMISC_1:106;
    then x in IG /\ [:cH,cH:] by XBOOLE_0:def 3,A4,A56;
    then x in IG |_2 cH by WELLORD1:def 6;
   hence thesis by YELLOW_0:def 14;
   end;
   suppose A62: x = [c,b];
      IG is_symmetric_in the carrier of G by NECKLACE:def 4;
    then A64: [c,b] in IG by RELAT_2:def 3,A4;
      b in Z & c in Z by A1,ENUMSET1:19;
    then b in cH & c in cH by YELLOW_0:def 15;
    then [c,b] in [:cH,cH:] by ZFMISC_1:106;
    then x in IG /\ [:cH,cH:] by A62,A64,XBOOLE_0:def 3;
    then x in IG |_2 cH by WELLORD1:def 6;
   hence thesis by YELLOW_0:def 14;
   end;
   suppose A70: x = [c,d];
      c in Z & d in Z by A1,ENUMSET1:19;
    then c in cH & d in cH by YELLOW_0:def 15;
    then [c,d] in [:cH,cH:] by ZFMISC_1:106;
    then x in IG /\ [:cH,cH:] by XBOOLE_0:def 3,A5,A70;
    then x in IG |_2 cH by WELLORD1:def 6;
   hence thesis by YELLOW_0:def 14;
   end;
   suppose A76: x = [d,c];
      IG is_symmetric_in the carrier of G by NECKLACE:def 4;
    then A78: [d,c] in IG by RELAT_2:def 3,A5;
      c in Z & d in Z by A1,ENUMSET1:19;
    then c in cH & d in cH by YELLOW_0:def 15;
    then [d,c] in [:cH,cH:] by ZFMISC_1:106;
    then x in IG /\ [:cH,cH:] by A76,A78,XBOOLE_0:def 3;
    then x in IG |_2 cH by WELLORD1:def 6;
   hence thesis by YELLOW_0:def 14;
  end;
  end;
 A84: a <> b & a <> c & a <> d & b <> c & b <> d & c <> d by A2,INCPROJ:def 6;
 set  g = (0,1) --> (a,b), h = (2,3) --> (c,d),
      f = g +* h;
   dom f = dom g \/ dom h by FUNCT_4:def 1
           .= {0,1} \/ dom h by FUNCT_4:65
           .= {0,1} \/ {2,3} by FUNCT_4:65
           .= {0,1,2,3} by ENUMSET1:45;
 then A86: dom f = the carrier of N4 by NECKLACE:2,21;
 A87:  rng g = {a,b} by FUNCT_4:67;
 A88:  rng h = {c,d} by FUNCT_4:67;
 A89: the carrier of H is non empty
  proof
     a in {a,b,c,d} by ENUMSET1:19;
   hence thesis by A1,YELLOW_0:def 15;
  end;
 A91: dom g misses dom h
   proof
    assume not thesis;
    then consider x being set such that
    A93: x in dom g & x in dom h by XBOOLE_0:3;
      (x=0 or x=1) & (x=2 or x=3) by A93,TARSKI:def 2;
    hence contradiction;
   end;
 A95: rng f = the carrier of H
  proof
    rng f = rng g \/ rng h by NECKLACE:7,A91;
    then rng f = {a,b,c,d} by A87,A88,ENUMSET1:45;
   hence thesis by YELLOW_0:def 15,A1;
  end;
 then reconsider f as map of N4,H by A86,A89,FUNCT_2:def 1,RELSET_1:11;
 A98: g is one-to-one
    proof
     A99: g = ({0} --> a) +* ({1} -->b) by FUNCT_4:def 4;
     A100: {0} --> a = 0 .--> a by CQC_LANG:def 2;
     A101: {1} --> b = 1 .--> b by CQC_LANG:def 2;
         rng (0 .--> a) misses rng (1 .--> b)
       proof
        assume rng (0 .--> a) meets rng (1 .--> b);
        then consider x being set such that
        A104: x in rng (0 .--> a) & x in rng (1 .--> b) by XBOOLE_0:3;
        A105: rng (0 .--> a) = {a} by CQC_LANG:5;
        A106: rng (1 .--> b) = {b} by CQC_LANG:5;
          x = a by A104,A105,TARSKI:def 1;
        hence contradiction by A84,A104,A106,TARSKI:def 1;
       end;
       hence thesis by A99,A100,A101,KNASTER:1;
      end;
 A108: h is one-to-one
  proof
   A109: h = ({2} --> c) +* ({3} --> d) by FUNCT_4:def 4;
   A110: {2} --> c = 2 .--> c by CQC_LANG:def 2;
   A111: {3} --> d = 3 .--> d by CQC_LANG:def 2;
     rng (2 .--> c) misses rng (3 .--> d)
    proof
     assume rng (2 .--> c) meets rng (3 .--> d);
     then consider x being set such that
     A114: x in rng (2 .--> c) & x in rng (3 .--> d) by XBOOLE_0:3;
     A115: rng (2 .--> c) = {c} by CQC_LANG:5;
     A116: rng (3 .--> d) = {d} by CQC_LANG:5;
       x = c by A114,A115,TARSKI:def 1;
     hence contradiction by A84,A114,A116,TARSKI:def 1;
    end;
   hence thesis by A109,A110,A111,KNASTER:1;
  end;
 A118: rng g misses rng h
   proof
    assume not thesis;
    then consider x being set such that
    A120: x in rng g & x in rng h by XBOOLE_0:3;
      (x = a or x = b) & (x = c or x = d) by A120,A87,A88,TARSKI:def 2;
    hence contradiction by A2,INCPROJ:def 6;
   end;
 then A122: f is one-to-one by KNASTER:1,A98,A108;
   for x,y being Element of the carrier of N4 holds
  [x,y] in the InternalRel of N4 iff [f.x,f.y] in the InternalRel of H
 proof
  let x,y being Element of the carrier of N4;
  thus [x,y] in the InternalRel of N4 implies
       [f.x,f.y] in the InternalRel of H
   proof
    assume A124: [x,y] in the InternalRel of N4;
    per cases by A124,NECKLA_2:2,ENUMSET1:28;
    suppose [x,y] = [0,1];
     then A126: x = 0 & y = 1 by ZFMISC_1:33;
     then x in {0,1} & y in {0,1} by TARSKI:def 2;
     then A128: x in dom g & y in dom g by FUNCT_4:65;
     then A129: f.x = g.0 by A91,FUNCT_4:17,A126
             .= a by FUNCT_4:66;
       f.y = g.1 by A91,FUNCT_4:17,A126,A128
        .= b by FUNCT_4:66;
    hence thesis by A9,ENUMSET1:29,A129;
    end;
    suppose [x,y] = [1,0];
     then A132: x = 1 & y = 0 by ZFMISC_1:33;
     then x in {0,1} & y in {0,1} by TARSKI:def 2;
     then A134: x in dom g & y in dom g by FUNCT_4:65;
     then A135: f.x = g.1 by A91,FUNCT_4:17,A132
             .= b by FUNCT_4:66;
       f.y = g.0 by A91,FUNCT_4:17,A132,A134
        .= a by FUNCT_4:66;
    hence thesis by A9,ENUMSET1:29,A135;
    end;
    suppose [x,y] = [1,2];
     then A138: x = 1 & y = 2 by ZFMISC_1:33;
     then x in {0,1} & y in {2,3} by TARSKI:def 2;
     then A140:x in dom g & y in dom h by FUNCT_4:65;
     then A141: f.x = g.1 by A91,FUNCT_4:17,A138
                 .= b by FUNCT_4:66;
       g +* h = h +* g by FUNCT_4:36,A91;
     then f.y = h.2 by A91,FUNCT_4:17,A138,A140
        .= c by FUNCT_4:66;
    hence thesis by A9,ENUMSET1:29,A141;
    end;
    suppose [x,y] = [2,1];
     then A145: x = 2 & y = 1 by ZFMISC_1:33;
     then x in {2,3} & y in {0,1} by TARSKI:def 2;
     then A147:x in dom h & y in dom g by FUNCT_4:65;
     then A148: f.y = g.1 by A91,FUNCT_4:17,A145
                 .= b by FUNCT_4:66;
       g +* h = h +* g by FUNCT_4:36,A91;
     then f.x = h.2 by A91,FUNCT_4:17,A145,A147
        .= c by FUNCT_4:66;
    hence thesis by A9,ENUMSET1:29,A148;
    end;
    suppose [x,y] = [2,3];
     then A152: x = 2 & y = 3 by ZFMISC_1:33;
     then x in {2,3} & y in {2,3} by TARSKI:def 2;
     then A154: x in dom h & y in dom h by FUNCT_4:65;
     A155: g +* h = h +* g by FUNCT_4:36,A91;
     then A156: f.x = h.2 by A91,FUNCT_4:17,A152,A154
                 .= c by FUNCT_4:66;
       f.y = h.3 by A91,FUNCT_4:17,A152,A154,A155
        .= d by FUNCT_4:66;
    hence thesis by A9,ENUMSET1:29,A156;
    end;
    suppose [x,y] = [3,2];
     then A159: x = 3 & y = 2 by ZFMISC_1:33;
     then x in {3,2} & y in {3,2} by TARSKI:def 2;
     then A161: x in dom h & y in dom h by FUNCT_4:65;
     A162: g +* h = h +* g by FUNCT_4:36,A91;
     then A163: f.x = h.3 by A91,FUNCT_4:17,A159,A161
                 .= d by FUNCT_4:66;
       f.y = h.2 by A91,FUNCT_4:17,A159,A161,A162
        .= c by FUNCT_4:66;
    hence thesis by A9,ENUMSET1:29,A163;
   end;
   end;
  thus [f.x,f.y] in the InternalRel of H implies
       [x,y] in the InternalRel of N4
   proof
    assume A165: [f.x,f.y] in the InternalRel of H;
    A166: dom g = {0,1} & rng g = {a,b} by FUNCT_4:65,67;
    then reconsider g as Function of {0,1},{a,b} by FUNCT_2:def 1,RELSET_1:11;
    reconsider G = g" as Function of {a,b},{0,1} by A87,A98,FUNCT_2:31;
    A167: dom h = {2,3} & rng h = {c,d} by FUNCT_4:65,67;
    then reconsider h as Function of {2,3},{c,d} by FUNCT_2:def 1,RELSET_1:11;
    reconsider Hh = h" as Function of {c,d},{2,3} by A88,A108,FUNCT_2:31;
    reconsider F=f" as Function of the carrier of H,the carrier of N4
         by A122,FUNCT_2:31,A95;
    A168: dom G = {a,b} & rng G = {0,1} by FUNCT_1:55,A166,A98;
    A169: dom Hh = {c,d} & rng Hh = {2,3} by FUNCT_1:55,A167,A108;
     then G +* Hh = Hh +* G by A87,A88,A118,A168,FUNCT_4:36;
    then A171: F = G +* Hh  & F = Hh +* G by NECKLACE:8,A98,A108,A91,A118;
    A172: G = (a,b) --> (0,1) & Hh = (c,d) --> (2,3) by NECKLACE:11,A84;
    per cases by A165,ENUMSET1:28,A9;
    suppose [f.x,f.y] = [a,b];
     then A174: f.x = a & f.y = b by ZFMISC_1:33;
     then A175: f.x in {a,b} & f.y in {a,b} by TARSKI:def 2;
     A176: f.x in dom G & f.y in dom G by A168,A174,TARSKI:def 2;
      F.(f.x) = G.a by A87,A88,A118,A168,A169,A171,A174,A175,FUNCT_4:17
                     .= 0 by FUNCT_4:66,A84,A172;
     then A178: x = 0 by A89,A122,FUNCT_2:32;
     A179: F.(f.y) = G.b by A87,A88,A118,A168,A169,A171,A174,A176,FUNCT_4:17
            .= 1 by FUNCT_4:66,A84,A172;
      F.(f.y) = y by A122,FUNCT_2:32,A89;
    hence thesis by A178,A179,ENUMSET1:29,NECKLA_2:2;
    end;
    suppose [f.x,f.y] = [b,a];
     then A182: f.x = b & f.y = a by ZFMISC_1:33;
     then A183: f.x in {a,b} & f.y in {a,b} by TARSKI:def 2;
     A184: f.x in dom G & f.y in dom G by A168,A182,TARSKI:def 2;
      F.(f.y) = G.a by A87,A88,A118,A168,A169,A171,A182,A183,FUNCT_4:17
                     .= 0 by FUNCT_4:66,A84,A172;
     then A186: y = 0 by A89,A122,FUNCT_2:32;
     A187: F.(f.x) = G.b by A87,A88,A118,A168,A169,A171,A182,A184,FUNCT_4:17
            .= 1 by FUNCT_4:66,A84,A172;
      F.(f.x) = x by A122,FUNCT_2:32,A89;
    hence thesis by A186,A187,ENUMSET1:29,NECKLA_2:2;
    end;
    suppose [f.x,f.y] = [b,c];
     then A190: f.x = b & f.y = c by ZFMISC_1:33;
     then A191: f.x in dom G & f.y in dom Hh by A168,A169,TARSKI:def 2;
     then F.(f.x) = G.b by A87,A88,A118,A168,A169,A171,A190,FUNCT_4:17
                     .= 1 by FUNCT_4:66,A84,A172;
     then A193: x = 1 by A89,A122,FUNCT_2:32;
     A194: F.(f.y) = Hh.c by A87,A88,A118,A168,A169,A171,A190,A191,FUNCT_4:17
                .= 2 by FUNCT_4:66,A84,A172;
      F.(f.y) = y by A122,FUNCT_2:32,A89;
    hence thesis by A193,A194,ENUMSET1:29,NECKLA_2:2;
    end;
    suppose [f.x,f.y] = [c,b];
     then A197: f.x = c & f.y = b by ZFMISC_1:33;
     then A198: f.x in dom Hh & f.y in dom G by A168,A169,TARSKI:def 2;
     then F.(f.y) = G.b by A87,A88,A118,A168,A169,A171,A197,FUNCT_4:17
                     .= 1 by FUNCT_4:66,A84,A172;
     then A200: y = 1 by A89,A122,FUNCT_2:32;
     A201: F.(f.x) = Hh.c by A87,A88,A118,A168,A169,A171,A197,A198,FUNCT_4:17
                .= 2 by FUNCT_4:66,A84,A172;
      F.(f.x) = x by A122,FUNCT_2:32,A89;
    hence thesis by A200,A201,ENUMSET1:29,NECKLA_2:2;
    end;
    suppose [f.x,f.y] = [c,d];
     then A204: f.x = c & f.y = d by ZFMISC_1:33;
     then A205: f.x in {c,d} & f.y in {c,d} by TARSKI:def 2;
     A206: f.x in dom Hh & f.y in dom Hh by A169,A204,TARSKI:def 2;
      F.(f.x) = Hh.c by A87,A88,A118,A168,A169,A171,A204,A205,FUNCT_4:17
                     .= 2 by FUNCT_4:66,A84,A172;
     then A208: x = 2 by A89,A122,FUNCT_2:32;
     A209: F.(f.y) = Hh.d by A87,A88,A118,A168,A169,A171,A204,A206,FUNCT_4:17
            .= 3 by FUNCT_4:66,A84,A172;
      F.(f.y) = y by A122,FUNCT_2:32,A89;
    hence thesis by A208,A209,ENUMSET1:29,NECKLA_2:2;
    end;
    suppose [f.x,f.y] = [d,c];
     then A212: f.x = d & f.y = c by ZFMISC_1:33;
     then A213: f.x in {c,d} & f.y in {c,d} by TARSKI:def 2;
     A214: f.x in dom Hh & f.y in dom Hh by A169,A212,TARSKI:def 2;
      F.(f.y) = Hh.c by A87,A88,A118,A168,A169,A171,A212,A213,FUNCT_4:17
                     .= 2 by FUNCT_4:66,A84,A172;
     then A216: y = 2 by A89,A122,FUNCT_2:32;
     A217: F.(f.x) = Hh.d by A87,A88,A118,A168,A169,A171,A212,A214,FUNCT_4:17
            .= 3 by FUNCT_4:66,A84,A172;
      F.(f.x) = x by A122,FUNCT_2:32,A89;
    hence thesis by A216,A217,ENUMSET1:29,NECKLA_2:2;
   end;
   end;
 end;
 hence thesis by NECKLACE:def 2,A122;
end;

theorem Th40: for G being non empty irreflexive symmetric RelStr,
  x being Element of the carrier of G,
  R1,R2 being non empty RelStr
  st the carrier of R1 misses the carrier of R2
   & subrelstr ([#]G \ {x}) = union_of(R1,R2) & G is non trivial
   & G is path-connected & ComplRelStr G is path-connected
   holds G embeds Necklace 4
proof
 let G be non empty irreflexive symmetric RelStr,
     x be Element of the carrier of G,
     R1,R2 be non empty RelStr;
 assume that A1: the carrier of R1 misses the carrier of R2 and
              A2: subrelstr ([#]G \ {x}) = union_of(R1,R2) and
  A3: G is non trivial & G is path-connected & ComplRelStr G is path-connected;
 set A = (the carrier of G) \ {x},
     X = {x};
 reconsider X as Subset of the carrier of G;
 set H = subrelstr X;
   A c= the carrier of G
   proof
    let a be set;
    assume a in A;
    hence thesis by XBOOLE_0:def 4;
   end;
 then reconsider A as Subset of the carrier of G;
 set R = subrelstr A;
   R is non empty
  proof
   assume not thesis;
   then the carrier of R is empty by STRUCT_0:def 1;
   then A is empty by YELLOW_0:def 15;
   then A10: the carrier of G = {x} by ZFMISC_1:66;
    not the carrier of G is trivial by A3,REALSET1:def 13;
   hence thesis by A10,REALSET1:def 12;
  end;
 then reconsider R as non empty irreflexive symmetric RelStr;
 A12: the carrier of G = (the carrier of R) \/ {x}
   proof
    thus the carrier of G c= (the carrier of R) \/ {x}
    proof
     let a be set;
     assume A13: a in the carrier of G;
      per cases;
      suppose a = x;
      then a in {x} by TARSKI:def 1;
      hence thesis by XBOOLE_0:def 2;
      end;
      suppose a <> x;
      then not a in {x} by TARSKI:def 1;
       then a in (the carrier of G) \ {x} by A13,XBOOLE_0:def 4;
      then a in the carrier of R by YELLOW_0:def 15;
      hence thesis by XBOOLE_0:def 2;
     end;
    end;
    let a be set;
    assume A20: a in (the carrier of R) \/ {x};
     per cases by A20,XBOOLE_0:def 2;
     suppose a in the carrier of R;
     then a in (the carrier of G) \ {x} by YELLOW_0:def 15;
     hence thesis by XBOOLE_0:def 4;
     end;
     suppose a in {x};
     hence thesis;
    end;
   end;
 A24: (the carrier of H) misses the carrier of R
  proof
   assume not thesis;
   then (the carrier of H) /\ (the carrier of R) <> {} by XBOOLE_0:def 7;
   then X /\ (the carrier of R) <> {} by YELLOW_0:def 15;
   then X /\ A <> {} by YELLOW_0:def 15;
   then consider a being set such that A29: a in X /\ A by XBOOLE_0:def 1;
    a in X & a in A by A29,XBOOLE_0:def 3;
   hence contradiction by XBOOLE_0:def 4;
  end;
   H is non empty
  proof
   assume not thesis;
   then the carrier of H is empty by STRUCT_0:def 1;
   hence contradiction by YELLOW_0:def 15;
  end;
 then reconsider H as non empty irreflexive symmetric RelStr;
 A34: R = union_of(R1,R2) by A2,PRE_TOPC:def 3;
 reconsider X1 = {y where y is Element of the carrier of R1:
                   [y,x] in (the InternalRel of G)},
            Y1 = {y where y is Element of the carrier of R1:
                   not [y,x] in the InternalRel of G},
            X2 = {y where y is Element of the carrier of R2:
                   [y,x] in (the InternalRel of G)},
            Y2 = {y where y is Element of the carrier of R2:
                   not [y,x] in the InternalRel of G} as set;
 A35: X1 misses Y1
  proof
   assume not thesis;
   then consider a being set such that
   A39: a in X1 & a in Y1 by XBOOLE_0:3;
   consider y1 being Element of the carrier of R1
   such that A40: y1 = a & [y1,x] in the InternalRel of G by A39;
   consider y2 being Element of the carrier of R1
   such that A41: y2 = a & not [y2,x] in the InternalRel of G by A39;
   thus contradiction by A40,A41;
  end;
 A42: X2 misses Y2
  proof
   assume not thesis;
   then consider a being set such that
   A46: a in X2 & a in Y2 by XBOOLE_0:3;
   consider y1 being Element of the carrier of R2
   such that A47: y1 = a & [y1,x] in the InternalRel of G by A46;
   consider y2 being Element of the carrier of R2
   such that A48: y2 = a & not [y2,x] in the InternalRel of G by A46;
   thus contradiction by A47,A48;
  end;
 A49: R = subrelstr ([#]G \ {x}) by PRE_TOPC:12;
 A50: the carrier of R1 = X1 \/ Y1
   proof
     thus the carrier of R1 c= X1 \/ Y1
     proof
      let a be set;
      assume A51: a in the carrier of R1;
       per cases;
       suppose [a,x] in the InternalRel of G;
       then a in X1 by A51;
       hence thesis by XBOOLE_0:def 2;
       end;
       suppose not [a,x] in the InternalRel of G;
       then a in Y1 by A51;
       hence thesis by XBOOLE_0:def 2;
      end;
     end;
     let a be set such that A56: a in X1 \/ Y1;
      per cases by A56,XBOOLE_0:def 2;
      suppose a in X1;
      then consider y being Element of the carrier of R1
      such that A58: a = y & [y,x] in (the InternalRel of G);
      thus thesis by A58;
      end;
      suppose a in Y1;
      then consider y being Element of the carrier of R1
      such that A60: a = y & not [y,x] in the InternalRel of G;
      thus thesis by A60;
     end;
    end;
 A61: the carrier of R2 = X2 \/ Y2
    proof
     thus the carrier of R2 c= X2 \/ Y2
     proof
      let a be set;
      assume A62: a in the carrier of R2;
       per cases;
       suppose [a,x] in the InternalRel of G;
       then a in X2 by A62;
       hence thesis by XBOOLE_0:def 2;
       end;
       suppose not [a,x] in the InternalRel of G;
       then a in Y2 by A62;
       hence thesis by XBOOLE_0:def 2;
      end;
     end;
     let a be set such that A67: a in X2 \/ Y2;
      per cases by A67,XBOOLE_0:def 2;
      suppose a in X2;
      then consider y being Element of the carrier of R2
      such that A69: a = y & [y,x] in (the InternalRel of G);
      thus thesis by A69;
      end;
      suppose a in Y2;
      then consider y being Element of the carrier of R2
      such that A71: a = y & not [y,x] in the InternalRel of G;
      thus thesis by A71;
     end;
    end;
 A72: R = union_of(R2,R1) by Th8,A34;
 consider a being Element of the carrier of R1 such that
 A73: [a,x] in the InternalRel of G by A1,A2,A3,Th38;
 A74: a in X1 by A73;
 consider b being Element of the carrier of R2 such that
 A75: [b,x] in the InternalRel of G by A3,A1,Th38,A72,A49;
 A76: b in X2 by A75;
 A77: Y1 \/ Y2 is non empty
  proof
   assume not thesis;then
   A79: Y1 is empty & Y2 is empty by XBOOLE_1:15;
   A87: for a being Element of the carrier of R
    holds [a,x] in the InternalRel of G
     proof
      let a be Element of the carrier of R;
      A88: the carrier of R = (the carrier of R1) \/ the carrier of R2
           by NECKLA_2:def 2,A34;
        per cases by A88,XBOOLE_0:def 2;
        suppose a in the carrier of R1;
        then consider y being Element of the carrier of R1
        such that A90: a = y & [y,x] in the InternalRel of G by A79,A50;
        thus thesis by A90;
        end;
        suppose a in the carrier of R2;
        then consider y being Element of the carrier of R2
        such that A92: a = y & [y,x] in the InternalRel of G by A79,A61;
        thus thesis by A92;
       end;
      end;
    not ComplRelStr G is path-connected
    proof
     assume A94: not thesis;
     A95: a <> x
      proof
       assume not thesis;
       then x in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 2;
       then A98: x in the carrier of R by NECKLA_2:def 2,A34;
         x in {x} by TARSKI:def 1;
       then x in the carrier of H by YELLOW_0:def 15;
       then x in (the carrier of R) /\ the carrier of H by A98,XBOOLE_0:def 3;
       hence contradiction by A24,XBOOLE_0:def 7;
      end;
       a is Element of the carrier of ComplRelStr G
      proof
         a in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 2;
       then A104: a in the carrier of R by NECKLA_2:def 2,A34;
        the carrier of R c= the carrier of G by XBOOLE_1:7,A12;
       hence thesis by A104,NECKLACE:def 9;
      end;
     then a is Element of the carrier of ComplRelStr G &
         x is Element of the carrier of ComplRelStr G by NECKLACE:def 9;
     then (the InternalRel of ComplRelStr G) reduces x,a by A94,A95,Def2;
     then consider p being FinSequence such that
     A108: len p > 0 & p.1 = x & p.len p = a and
     A109: for i being Nat st i in dom p & i+1 in dom p holds
         [p.i, p.(i+1)] in (the InternalRel of ComplRelStr G) by REWRITE1:12;
     A110: 0 + 1 <= len p by SCMFSA_7:28,A108;
      then len p > 1 by A95,A108,REAL_1:def 5;
     then 1+1 <= len p by SCMFSA_7:28;
      then 1 in dom p & 2 in dom p by A110,FINSEQ_3:27;
     then A114: [p.1,p.(1+1)] in (the InternalRel of ComplRelStr G) by A109;
     then p.2 in the carrier of ComplRelStr G by ZFMISC_1:106;
     then A116: p.2 in the carrier of G by NECKLACE:def 9;
     A117: p.2 <> x
      proof
       assume A118: not thesis;
         [x,x] in id the carrier of G by RELAT_1:def 10;
       then [x,x] in (the InternalRel of ComplRelStr G) /\ id the carrier
of G
             by A108,A114,A118,XBOOLE_0:def 3;
       then (the InternalRel of ComplRelStr G) meets id the carrier of G
            by XBOOLE_0:def 7;
       hence contradiction by Th13;
      end;
     A122: p.2 in the carrier of R
      proof
       assume not thesis;
       then p.2 in {x} by A12,A116,XBOOLE_0:def 2;
       hence thesis by A117,TARSKI:def 1;
      end;
     A125: p.1 in the carrier of ComplRelStr G &
          p.(1+1) in the carrier of ComplRelStr G by A114,ZFMISC_1:106;
       the InternalRel of ComplRelStr G is_symmetric_in
                        the carrier of ComplRelStr G by NECKLACE:def 4;
     then A127: [p.(1+1),p.1] in the InternalRel of ComplRelStr G
           by A114,A125,RELAT_2:def 3;
       [p.2,x] in the InternalRel of G by A87,A122;
     then [p.2,x] in (the InternalRel of ComplRelStr G) /\
                      the InternalRel of G by A108,A127,XBOOLE_0:def 3;
     then (the InternalRel of ComplRelStr G) meets the InternalRel of G
          by XBOOLE_0:def 7;
     hence thesis by Th12;
    end;
   hence thesis by A3;
  end;
 thus thesis
 proof
  per cases by A77;
  suppose A131: Y1 is non empty;
    ex b being Element of Y1, c being Element of X1
       st [b,c] in the InternalRel of G
   proof
    consider b being Element of Y1;
      a in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 2;
    then A134: a in the carrier of R by NECKLA_2:def 2,A34;
    A135: the carrier of R c= the carrier of G by XBOOLE_1:7,A12;
    then reconsider a as Element of G by A134;
      b in Y1 by A131;
    then consider y being Element of the carrier of R1
    such that A137: y = b & not [y,x] in the InternalRel of G;
      b in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 2,A137;
    then b in the carrier of R by NECKLA_2:def 2,A34;
    then reconsider b as Element of G by A135;
      a <> b
     proof
      assume not thesis;
       then a in X1 & a in Y1 by A73,A131;
      then a in X1 /\ Y1 by XBOOLE_0:def 3;
      hence contradiction by A35,XBOOLE_0:def 7;
     end;
    then (the InternalRel of G) reduces a,b by Def2,A3;
    then consider p being FinSequence such that
    A145: len p > 0 & p.1 = a & p.len p = b and
    A146: for i being Nat st i in dom p & i+1 in dom p
        holds [p.i, p.(i+1)] in (the InternalRel of G) by REWRITE1:12;
    defpred P[Nat] means p.$1 in Y1 & $1 in dom p &
      for k being Nat st k > $1 holds
          k in dom p implies p.k in Y1;
    A147: ex k being Nat st P[k]
     proof
        P[len p]
      proof
       A149: len p in dom p
       proof
         p <> {} by A145,FINSEQ_1:25;
        hence thesis by SCMFSA_7:12;
       end;
         for k being Nat st k > len p holds
           k in dom p implies p.k in Y1
        proof
         let k be Nat such that A152: k > len p;
         assume k in dom p;
         then k in Seg (len p) by FINSEQ_1:def 3;
         hence thesis by A152,FINSEQ_1:3;
        end;
      hence thesis by A131,A145,A149;
     end;
    hence thesis;
    end;
      ex n0 being Nat st P[n0] &
     for n being Nat st P[n] holds n >= n0 from NAT_1:sch 5(A147);
    then consider n0 being Nat such that
    A156: P[n0] and
    A157: for n being Nat st P[n] holds n >= n0;
      n0 <> 0
     proof
      assume not thesis;
      then 0 in Seg (len p) by A156,FINSEQ_1:def 3;
      hence contradiction by FINSEQ_1:3;
     end;
   then consider k0 being Nat such that
   A161: n0 = k0 + 1 by NAT_1:22;
   A162: n0 <> 1
    proof
     assume not thesis;
     then a in X1 & a in Y1 by A145,A156,A73;
     then X1 /\ Y1 is non empty by XBOOLE_0:def 3;
     hence contradiction by A35,XBOOLE_0:def 7;
    end;
   A166: k0 in dom p
    proof
     A167: k0 >= 1
     proof
      assume not thesis;
      then k0 = 0 by CQC_THE1:2;
      hence contradiction by A161,A162;
     end;
       k0 <= len p
      proof
       A171: k0 <= k0 + 1 by REAL_1:69;
         n0 in Seg (len p) by A156,FINSEQ_1:def 3;
       then n0 <= len p by FINSEQ_1:3;
       hence thesis by A161,A171,AXIOMS:22;
      end;
     hence thesis by A167,FINSEQ_3:27;
    end;
     k0 < n0 by A161,NAT_1:38;
   then A176: not P[k0] by A157;
   A178: for k being Nat st k > k0
         holds  k in dom p implies p.k in Y1
    proof
     assume not thesis;
     then consider k being Nat such that A180: k > k0
            & k in dom p & not p.k in Y1;
      k > n0
      proof
        per cases by REAL_1:def 5;
        suppose A182: k < n0;
        thus thesis by A161,A180,A182,SCMFSA_7:28;
        end;
        suppose n0 < k;
        hence thesis;
        end;
        suppose n0 = k;
        hence thesis by A156,A180;
       end;
      end;
     hence contradiction by A156,A180;
    end;
   A186: [p.k0,p.(k0+1)] in the InternalRel of G by A146,A156,A161,A166;
   A187: [p.n0,p.k0] in the InternalRel of G
     proof
      A188: the InternalRel of G is_symmetric_in the carrier of G
              by NECKLACE:def 4;
       p.k0 in the carrier of G & p.n0 in the carrier of G  by A161,A186,
ZFMISC_1:106;
      hence thesis by A161,A186,A188,RELAT_2:def 3;
     end;
    p.k0 in the carrier of G by A186,ZFMISC_1:106;
    then p.k0 in the carrier of R or p.k0 in  {x} by A12,XBOOLE_0:def 2;
   then A192: p.k0 in (the carrier of R1) \/ the carrier of R2 or p.k0 in {x}
             by NECKLA_2:def 2,A34;
   thus thesis
    proof
     per cases by A161,A186,A192,XBOOLE_0:def 2,ZFMISC_1:106;
     suppose A193: p.k0 in the carrier of R1 & p.n0 in the carrier of G;
     then reconsider m=p.k0 as Element of X1 by A50,A166,A176,A178,XBOOLE_0:def
2;
     reconsider l=p.n0 as Element of Y1 by A156;
     A194: [m,l] in the InternalRel of G by A146,A156,A161,A166;
     A195: the InternalRel of G is_symmetric_in the carrier of G
     by NECKLACE:def 4;
       l in the carrier of R1 by A50,A156,XBOOLE_0:def 2;
     then l in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 2;
     then A198: l in the carrier of R by NECKLA_2:def 2,A34;
       m in (the carrier of R1) \/ the carrier of R2 by A193,XBOOLE_0:def 2;
     then A200: m in the carrier of R by NECKLA_2:def 2,A34;
      the carrier of R c= the carrier of G by XBOOLE_1:7,A12;
      then [l,m] in the InternalRel of G by A194,A195,A198,A200,RELAT_2:
def 3;
     hence thesis;
     end;
     suppose p.k0 in the carrier of R2 & p.n0 in the carrier of G;
     then reconsider m=p.k0 as Element of the carrier of R2;
     reconsider l=p.n0 as Element of the carrier of R1 by A50,A156,XBOOLE_0:def
2;
       l in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 2;
     then A205: l in the carrier of R by NECKLA_2:def 2,A34;
       m in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 2;
     then m in the carrier of R by NECKLA_2:def 2,A34;
      then [l,m] in [:the carrier of R,the carrier of R:]
          by A205,ZFMISC_1:106;
     then [l,m] in (the InternalRel of G)|_2 the carrier of R
          by WELLORD1:16,A187;
     then [l,m] in the InternalRel of R by YELLOW_0:def 14;
     hence thesis by A1,A34,Th36;
     end;
     suppose A211: p.k0 in {x} & p.n0 in the carrier of G;
     consider y1 being Element of the carrier of R1 such that
     A212: p.n0 = y1 & not [y1,x] in the InternalRel of G by A156;
     thus thesis by A187,A211,A212,TARSKI:def 1;
    end;
    end;
   end;
  then consider u being Element of Y1, v being Element of X1
  such that A213: [u,v] in the InternalRel of G;
  consider w being Element of X2;
  set Z = {u,v,x,w};
    Z c= the carrier of G
   proof
    let q be set such that A215: q in Z;
    A216: q = u or q = v or q = x or q = w by A215, ENUMSET1:18;
      u in the carrier of R1 by A50,A131,XBOOLE_0:def 2;
    then u in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 2;
    then A219: u in the carrier of R by NECKLA_2:def 2,A34;
    A220: the carrier of R c= the carrier of G by A12,XBOOLE_1:7;
      v in X1 by A74;
    then consider y1 being Element of the carrier of R1 such that
    A222: y1 = v & [y1,x] in the InternalRel of G;
      v in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 2,A222;
    then A224: v in the carrier of R by NECKLA_2:def 2,A34;
      w in X2 by A76;
    then consider y2 being Element of the carrier of R2 such that
    A226: y2 = w & [y2,x] in the InternalRel of G;
      w in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 2,A226;
    then w in the carrier of R by NECKLA_2:def 2,A34;
    hence thesis by A216,A219,A220,A224;
   end;
   then reconsider Z as Subset of the carrier of G;
  A229: u in Y1 by A131;
  A230: v in X1 by A74;
  A231: w in X2 by A76;
  reconsider u,v as Element of the carrier of G by A213,ZFMISC_1:106;
    w in X2 by A76;
  then consider y being Element of the carrier of R2 such that
  A233: y = w & [y,x] in the InternalRel of G;
  reconsider w as Element of the carrier of G by A233,ZFMISC_1:106;
    u <> v & v <> x & x <> u & w <> u & w <> v & w <> x
   proof
    A235: u <> v
    proof
     assume A236: not thesis;
     consider y1 being Element of the carrier of R1 such that
     A237: u = y1 & not [y1,x] in the InternalRel of G by A229;
     consider y2 being Element of the carrier of R1 such that
     A238: v = y2 & [y2,x] in the InternalRel of G by A230;
     thus contradiction by A236,A237,A238;
    end;
   A239: v <> x
     proof
      assume A240: not thesis;
      consider y1 being Element of the carrier of R1 such that
      A241: v = y1 & [y1,x] in the InternalRel of G by A230;
        x in (the carrier of R1) \/ the carrier of R2 by A240,A241,
XBOOLE_0:def 2;
      then x in the carrier of R by A34,NECKLA_2:def 2;
      then x in (the carrier of G) \ {x} by YELLOW_0:def 15;
      then not x in {x} by XBOOLE_0:def 4;
      hence contradiction by TARSKI:def 1;
     end;
    A246: x <> u
      proof
       assume A247: not thesis;
       consider y1 being Element of the carrier of R1 such that
       A248: u = y1 & not [y1,x] in the InternalRel of G by A229;
         x in (the carrier of R1) \/ (the carrier of R2) by A247,A248,
XBOOLE_0:def 2;
       then x in the carrier of R by A34,NECKLA_2:def 2;
       then x in (the carrier of G) \ {x} by YELLOW_0:def 15;
       then not x in {x} by XBOOLE_0:def 4;
       hence contradiction by TARSKI:def 1;
      end;
    A253: w <> u
      proof
       assume A254: not thesis;
       consider y1 being Element of the carrier of R2 such that
       A255: w = y1 & [y1,x] in the InternalRel of G by A231;
       consider y2 being Element of the carrier of R1 such that
       A256: u = y2 & not [y2,x] in the InternalRel of G by A229;
       thus contradiction by A254,A255,A256;
      end;
    A257: w <> v
      proof
       assume A258: not thesis;
       consider y1 being Element of the carrier of R2 such that
       A259: w = y1 & [y1,x] in the InternalRel of G by A231;
       consider y2 being Element of the carrier of R1 such that
       A260: v = y2 & [y2,x] in the InternalRel of G by A230;
        y1 in (the carrier of R1) /\ (the carrier of R2)
       by A258,A259,A260,XBOOLE_0:def 3;
       hence contradiction by A1,XBOOLE_0:def 7;
      end;
      w <> x
     proof
      assume A263: not thesis;
      consider y1 being Element of the carrier of R2 such that
      A264: w = y1 & [y1,x] in the InternalRel of G by A231;
        x in (the carrier of R1) \/ the carrier of R2 by A263,A264,
XBOOLE_0:def 2;
      then x in the carrier of R by A34,NECKLA_2:def 2;
      then x in (the carrier of G) \ {x} by YELLOW_0:def 15;
      then not x in {x} by XBOOLE_0:def 4;
      hence contradiction by TARSKI:def 1;
     end;
     hence thesis by A235,A239,A246,A253,A257;
    end;
  then A269: u,v,x,w are_mutually_different by INCPROJ:def 6;
  A270: [v,x] in the InternalRel of G
   proof
    consider y1 being Element of the carrier of R1 such that
    A271: v = y1 & [y1,x] in the InternalRel of G by A230;
    thus thesis by A271;
   end;
  A272: [x,w] in the InternalRel of G
   proof
    consider y1 being Element of the carrier of R2 such that
    A273: w = y1 & [y1,x] in the InternalRel of G by A231;
     the InternalRel of G is_symmetric_in the carrier of G
            by NECKLACE:def 4;
    hence thesis by A273,RELAT_2:def 3;
   end;
  A275: not [u,x] in the InternalRel of G
   proof
    consider y1 being Element of the carrier of R1 such that
    A276: u = y1 & not [y1,x] in the InternalRel of G by A229;
    thus thesis by A276;
   end;
  A277: not [u,w] in the InternalRel of G
   proof
    assume A278: not thesis;
    consider y1 being Element of the carrier of R1 such that
    A279: u = y1 & not [y1,x] in the InternalRel of G by A229;
    consider y2 being Element of the carrier of R2 such that
    A280: w = y2 & [y2,x] in the InternalRel of G by A231;
      u in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 2,A279;
    then reconsider u as Element of the carrier of R by A34,NECKLA_2:def 2;
      w in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 2,A280;
    then reconsider w as Element of the carrier of R by A34,NECKLA_2:def 2;
      [u,w] in [:the carrier of R,the carrier of R:] by ZFMISC_1:106;
    then [u,w] in (the InternalRel of G) /\
                 [:the carrier of R,the carrier of R:] by A278,XBOOLE_0:def 3;
    then [u,w] in (the InternalRel of G)|_2 the carrier of R
                    by WELLORD1:def 6;
    then [u,w] in the InternalRel of R by YELLOW_0:def 14;
    then A287: [u,w] in (the InternalRel of R1) \/ (the InternalRel of R2)
                 by NECKLA_2:def 2,A34;
      per cases by A287,XBOOLE_0:def 2;
      suppose [u,w] in the InternalRel of R1;
      then u in the carrier of R1 & w in the carrier of R1 by ZFMISC_1:106;
      then w in (the carrier of R1) /\ the carrier of R2 by A280
      ,XBOOLE_0:def 3;
      hence contradiction by A1,XBOOLE_0:def 7;
      end;
      suppose [u,w] in the InternalRel of R2;
      then u in the carrier of R2 & w in the carrier of R2 by ZFMISC_1:106;
      then u in (the carrier of R1) /\ the carrier of R2 by A279
,XBOOLE_0:def 3;
      hence contradiction by A1,XBOOLE_0:def 7;
     end;
    end;
    not [v,w] in the InternalRel of G
   proof
    assume A295: not thesis;
    consider y1 being Element of the carrier of R1 such that
    A296: v = y1 & [y1,x] in the InternalRel of G by A230;
    consider y2 being Element of the carrier of R2 such that
    A297: w = y2 & [y2,x] in the InternalRel of G by A231;
      v in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 2,A296;
    then reconsider v as Element of the carrier of R by A34,NECKLA_2:def 2;
      w in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 2,A297;
    then reconsider w as Element of the carrier of R by A34,NECKLA_2:def 2;
      [v,w] in [:the carrier of R,the carrier of R:] by ZFMISC_1:106;
    then [v,w] in (the InternalRel of G) /\
              [:the carrier of R,the carrier of R:] by A295,XBOOLE_0:def 3;
    then [v,w] in (the InternalRel of G)|_2 the carrier of R by WELLORD1:
def 6;
    then [v,w] in the InternalRel of R by YELLOW_0:def 14;
    then A304: [v,w] in (the InternalRel of R1) \/ (the InternalRel of R2)
              by NECKLA_2:def 2,A34;
      per cases by A304,XBOOLE_0:def 2;
      suppose [v,w] in the InternalRel of R1;
      then v in the carrier of R1 & w in the carrier of R1 by ZFMISC_1:106;
      then w in (the carrier of R1) /\ the carrier of R2 by A297
,XBOOLE_0:def 3;
      hence contradiction by A1,XBOOLE_0:def 7;
      end;
      suppose [v,w] in the InternalRel of R2;
      then v in the carrier of R2 & w in the carrier of R2 by ZFMISC_1:106;
      then v in (the carrier of R1) /\ the carrier of R2 by A296
,XBOOLE_0:def 3;
      hence contradiction by A1,XBOOLE_0:def 7;
     end;
    end;
  then A311: subrelstr Z embeds Necklace 4 by Th39,A213,A269,A270,A272,A275,
A277;
    subrelstr Z is non empty
   proof
      the carrier of subrelstr Z = Z by YELLOW_0:def 15;
    then u in the carrier of subrelstr Z by ENUMSET1:19;
    hence thesis by STRUCT_0:def 1;
   end;
  then reconsider H = subrelstr Z as non empty full SubRelStr of G;
    G embeds Necklace 4
   proof
    assume not thesis;
    then G is N-free by NECKLA_2:def 1;
    then H is N-free by Th23;
    hence thesis by A311,NECKLA_2:def 1;
   end;
  hence thesis;
  end;
  suppose A319: Y2 is non empty;
    ex c being Element of Y2, d being Element of X2
       st [c,d] in the InternalRel of G
   proof
    consider c being Element of Y2;
      b in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 2;
    then A322: b in the carrier of R by NECKLA_2:def 2,A34;
    A323: the carrier of R c= the carrier of G by XBOOLE_1:7,A12;
    then reconsider b as Element of G by A322;
      c in Y2 by A319;
    then consider y being Element of the carrier of R2
    such that A325: y = c & not [y,x] in the InternalRel of G;
      c in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 2,A325;
    then c in the carrier of R by NECKLA_2:def 2,A34;
    then reconsider c as Element of G by A323;
      b <> c
     proof
      assume not thesis;
       then c in X2 & c in Y2 by A75,A319;
      then c in X2 /\ Y2 by XBOOLE_0:def 3;
      hence contradiction by A42,XBOOLE_0:def 7;
     end;
    then (the InternalRel of G) reduces b,c by Def2,A3;
    then consider p being FinSequence such that
    A333: len p > 0 & p.1 = b & p.len p = c and
    A334: for i being Nat st i in dom p & i+1 in dom p
    holds [p.i, p.(i+1)] in (the InternalRel of G) by REWRITE1:12;
    defpred P[Nat] means p.$1 in Y2 & $1 in dom p &
     for k being Nat st k > $1 holds k in dom p implies p.k in Y2;
    A335: ex k being Nat st P[k]
     proof
        P[len p]
      proof
       A337: len p in dom p
       proof
         p <> {} by A333,FINSEQ_1:25;
        hence thesis by SCMFSA_7:12;
       end;
        for k being Nat st k > len p holds k in dom p implies p.k in Y2
       proof
        let k be Nat such that A340: k > len p;
        assume k in dom p;
        then k in Seg (len p) by FINSEQ_1:def 3;
        hence thesis by A340,FINSEQ_1:3;
       end;
       hence thesis by A319,A333,A337;
      end;
      hence thesis;
     end;
      ex n0 being Nat st P[n0] &
       for n being Nat st P[n] holds n >= n0 from NAT_1:sch 5(A335);
    then consider n0 being Nat such that
    A344: P[n0] and
    A345: for n being Nat st P[n] holds n >= n0;
      n0 <> 0
     proof
      assume not thesis;
      then 0 in Seg (len p) by A344,FINSEQ_1:def 3;
      hence contradiction by FINSEQ_1:3;
     end;
    then consider k0 being Nat such that
    A349: n0 = k0 + 1 by NAT_1:22;
    A350: n0 <> 1
     proof
      assume not thesis;
      then b in X2 & b in Y2 by A333,A344,A75;
      then X2 /\ Y2 is non empty by XBOOLE_0:def 3;
      hence contradiction by A42,XBOOLE_0:def 7;
     end;
    A354: k0 in dom p
     proof
      A355: k0 >= 1
      proof
       assume not thesis;
       then k0 = 0 by CQC_THE1:2;
       hence contradiction by A349,A350;
      end;
        k0 <= len p
      proof
       A359: k0 <= k0 + 1 by REAL_1:69;
         n0 in Seg (len p) by A344,FINSEQ_1:def 3;
       then n0 <= len p by FINSEQ_1:3;
       hence thesis by A349,A359,AXIOMS:22;
      end;
      then k0 in Seg (len p) by FINSEQ_1:3,A355;
      hence thesis by FINSEQ_1:def 3;
     end;
      k0 < n0 by A349,NAT_1:38;
    then A364: not P[k0] by A345;
    A366: for k being Nat st k > k0 holds  k in dom p implies p.k in Y2
     proof
      assume not thesis;
      then consider k being Nat such that A368: k > k0
      & k in dom p & not p.k in Y2;
       k > n0
      proof
         per cases by REAL_1:def 5;
         suppose k < n0;
         hence thesis by A349,A368,SCMFSA_7:28;
         end;
         suppose n0 < k;
         hence thesis;
         end;
         suppose n0 = k;
         hence thesis by A344,A368;
        end;
       end;
      hence contradiction by A344,A368;
     end;
    A374: [p.k0,p.(k0+1)] in the InternalRel of G by A334,A344,A349,A354;
    A375: [p.n0,p.k0] in the InternalRel of G
     proof
      A376: the InternalRel of G is_symmetric_in the carrier of G
            by NECKLACE:def 4;
       p.k0 in the carrier of G & p.n0 in the carrier of G by A349,A374,
ZFMISC_1:106;
      hence thesis by A349,A374,A376,RELAT_2:def 3;
     end;
     p.k0 in the carrier of G by A374,ZFMISC_1:106;
     then p.k0 in the carrier of R or p.k0 in  {x} by A12,XBOOLE_0:def 2;
    then A380: p.k0 in (the carrier of R1) \/ the carrier of R2 or p.k0 in {x}
             by NECKLA_2:def 2,A34;
    thus thesis
     proof
      per cases by A349,A374,A380,XBOOLE_0:def 2,ZFMISC_1:106;
      suppose p.k0 in the carrier of R2 & p.n0 in the carrier of G;
      then reconsider m=p.k0 as Element of X2 by A61,A354,A364,A366,XBOOLE_0:
def 2;
      reconsider l=p.n0 as Element of Y2 by A344;
        [m,l] in the InternalRel of G by A334,A344,A349,A354;
      hence thesis by A375;
      end;
      suppose p.k0 in the carrier of R1 & p.n0 in the carrier of G;
      then reconsider m=p.k0 as Element of the carrier of R1;
      reconsider l=p.n0 as Element of the carrier of R2 by A61,A344,XBOOLE_0:
def 2;
        l in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 2;
      then A385: l in the carrier of R by NECKLA_2:def 2,A34;
        m in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 2;
      then A387: m in the carrier of R by NECKLA_2:def 2,A34;
       then [l,m] in [:the carrier of R,the carrier of R:] by A385,
ZFMISC_1:106;
      then [l,m] in (the InternalRel of G)|_2 the carrier of R
                       by WELLORD1:16,A375;
      then A390: [l,m] in the InternalRel of R by YELLOW_0:def 14;
        the InternalRel of R is_symmetric_in the carrier of R by NECKLACE:
def 4;
      then [m,l] in the InternalRel of R by RELAT_2:def 3,A385,A387,A390;
      hence thesis by A1,A34,Th36;
      end;
      suppose A393: p.k0 in {x} & p.n0 in the carrier of G;
      consider y1 being Element of the carrier of R2 such that
      A394: p.n0 = y1 & not [y1,x] in the InternalRel of G by A344;
      thus thesis by A375,A393,A394,TARSKI:def 1;
     end;
     end;
    end;
  then consider u being Element of Y2, v being Element of X2
  such that A395: [u,v] in the InternalRel of G;
  consider w being Element of X1;
  set Z = {u,v,x,w};
    Z c= the carrier of G
   proof
    let q be set such that A397: q in Z;
    A398: q = u or q = v or q = x or q = w by A397, ENUMSET1:18;
      u in the carrier of R2 by A61,A319,XBOOLE_0:def 2;
    then u in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 2;
    then A401: u in the carrier of R by NECKLA_2:def 2,A34;
    A402: the carrier of R c= the carrier of G by A12,XBOOLE_1:7;
      v in X2 by A76;
    then consider y1 being Element of the carrier of R2 such that
    A404: y1 = v & [y1,x] in the InternalRel of G;
      v in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 2,A404;
    then A406: v in the carrier of R by NECKLA_2:def 2,A34;
      w in X1 by A74;
    then consider y2 being Element of the carrier of R1 such that
    A408: y2 = w & [y2,x] in the InternalRel of G;
      w in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 2,A408;
    then w in the carrier of R by NECKLA_2:def 2,A34;
    hence thesis by A398,A401,A402,A406;
   end;
  then reconsider Z as Subset of the carrier of G;
  A411: u in Y2 by A319;
  A412: v in X2 by A76;
  A413: w in X1 by A74;
  reconsider u,v as Element of the carrier of G by A395,ZFMISC_1:106;
    w in X1 by A74;
  then consider y being Element of the carrier of R1 such that
  A415: y = w  & [y,x] in the InternalRel of G;
  reconsider w as Element of the carrier of G by A415,ZFMISC_1:106;
    u <> v & v <> x & x <> u & w <> u & w <> v & w <> x
   proof
    A417: u <> v
    proof
     assume A418: not thesis;
     consider y1 being Element of the carrier of R2 such that
     A419: u = y1 & not [y1,x] in the InternalRel of G by A411;
     consider y2 being Element of the carrier of R2 such that
     A420: v = y2 & [y2,x] in the InternalRel of G by A412;
     thus contradiction by A418,A419,A420;
    end;
    A421: v <> x
     proof
      assume A422: not thesis;
      consider y1 being Element of the carrier of R2 such that
      A423: v = y1 & [y1,x] in the InternalRel of G by A412;
        x in (the carrier of R1) \/ the carrier of R2 by A422,A423,
XBOOLE_0:def 2;
      then x in the carrier of R by A34,NECKLA_2:def 2;
      then x in (the carrier of G) \ {x} by YELLOW_0:def 15;
      then not x in {x} by XBOOLE_0:def 4;
      hence contradiction by TARSKI:def 1;
     end;
    A428: x <> u
     proof
      assume A429: not thesis;
      consider y1 being Element of the carrier of R2 such that
      A430: u = y1 & not [y1,x] in the InternalRel of G by A411;
        x in (the carrier of R1) \/ (the carrier of R2) by A429,A430,
XBOOLE_0:def 2;
      then x in the carrier of R by A34,NECKLA_2:def 2;
      then x in (the carrier of G) \ {x} by YELLOW_0:def 15;
      then not x in {x} by XBOOLE_0:def 4;
      hence contradiction by TARSKI:def 1;
     end;
    A435: w <> u
     proof
      assume A436: not thesis;
      consider y1 being Element of the carrier of R1 such that
      A437: w = y1 & [y1,x] in the InternalRel of G by A413;
      consider y2 being Element of the carrier of R2 such that
      A438: u = y2 & not [y2,x] in the InternalRel of G by A411;
      thus contradiction by A436,A437,A438;
     end;
    A439: w <> v
     proof
      assume A440: not thesis;
      consider y1 being Element of the carrier of R1 such that
      A441: w = y1 & [y1,x] in the InternalRel of G by A413;
      consider y2 being Element of the carrier of R2 such that
      A442: v = y2 & [y2,x] in the InternalRel of G by A412;
       y1 in (the carrier of R1) /\ (the carrier of R2)
           by A440,A441,A442,XBOOLE_0:def 3;
      hence contradiction by A1,XBOOLE_0:def 7;
     end;
      w <> x
     proof
      assume A445: not thesis;
      consider y1 being Element of the carrier of R1 such that
      A446: w = y1 & [y1,x] in the InternalRel of G by A413;
        x in (the carrier of R1) \/ the carrier of R2 by A445,A446,
XBOOLE_0:def 2;
      then x in the carrier of R by A34,NECKLA_2:def 2;
      then x in (the carrier of G) \ {x} by YELLOW_0:def 15;
      then not x in {x} by XBOOLE_0:def 4;
      hence contradiction by TARSKI:def 1;
     end;
     hence thesis by A417,A421,A428,A435,A439;
    end;
  then A451: u,v,x,w are_mutually_different by INCPROJ:def 6;
  A452: [v,x] in the InternalRel of G
   proof
    consider y1 being Element of the carrier of R2 such that
    A453: v = y1 & [y1,x] in the InternalRel of G by A412;
    thus thesis by A453;
   end;
  A454: [x,w] in the InternalRel of G
   proof
    consider y1 being Element of the carrier of R1 such that
    A455: w = y1 & [y1,x] in the InternalRel of G by A413;
     the InternalRel of G is_symmetric_in the carrier of G
    by NECKLACE:def 4;
    hence thesis by A455,RELAT_2:def 3;
   end;
  A457: not [u,x] in the InternalRel of G
   proof
    consider y1 being Element of the carrier of R2 such that
    A458: u = y1 & not [y1,x] in the InternalRel of G by A411;
    thus thesis by A458;
   end;
  A459: not [u,w] in the InternalRel of G
   proof
    assume A460: not thesis;
    consider y1 being Element of the carrier of R2 such that
    A461: u = y1 & not [y1,x] in the InternalRel of G by A411;
    consider y2 being Element of the carrier of R1 such that
    A462: w = y2 & [y2,x] in the InternalRel of G by A413;
    u in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 2,A461;
    then reconsider u as Element of the carrier of R by A34,NECKLA_2:def 2;
      w in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 2,A462;
    then reconsider w as Element of the carrier of R by A34,NECKLA_2:def 2;
      [u,w] in [:the carrier of R,the carrier of R:] by ZFMISC_1:106;
    then [u,w] in (the InternalRel of G) /\ [:the carrier of R,the
carrier of R:]
             by A460,XBOOLE_0:def 3;
    then [u,w] in (the InternalRel of G)|_2 the carrier of R
                    by WELLORD1:def 6;
    then [u,w] in the InternalRel of R by YELLOW_0:def 14;
    then A469: [u,w] in (the InternalRel of R1) \/ (the InternalRel of R2)
                 by NECKLA_2:def 2,A34;
      per cases by A469,XBOOLE_0:def 2;
      suppose [u,w] in the InternalRel of R1;
      then u in the carrier of R1 & w in the carrier of R1 by ZFMISC_1:106;
      then u in (the carrier of R1) /\ the carrier of R2 by A461
,XBOOLE_0:def 3;
      hence contradiction by A1,XBOOLE_0:def 7;
      end;
      suppose [u,w] in the InternalRel of R2;
      then u in the carrier of R2 & w in the carrier of R2  by ZFMISC_1:106;
      then w in (the carrier of R1) /\ the carrier of R2 by A462
,XBOOLE_0:def 3;
      hence contradiction by A1,XBOOLE_0:def 7;
     end;
    end;
    not [v,w] in the InternalRel of G
   proof
    assume A477: not thesis;
    consider y1 being Element of the carrier of R2 such that
    A478: v = y1 & [y1,x] in the InternalRel of G by A412;
    consider y2 being Element of the carrier of R1 such that
    A479: w = y2 & [y2,x] in the InternalRel of G by A413;
      v in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 2,A478;
    then reconsider v as Element of the carrier of R by A34,NECKLA_2:def 2;
      w in (the carrier of R1) \/ the carrier of R2 by XBOOLE_0:def 2,A479;
    then reconsider w as Element of the carrier of R by A34,NECKLA_2:def 2;
      [v,w] in [:the carrier of R,the carrier of R:] by ZFMISC_1:106;
    then [v,w] in (the InternalRel of G) /\
           [:the carrier of R,the carrier of R:] by A477,XBOOLE_0:def 3;
    then [v,w] in (the InternalRel of G)|_2 the carrier of R by WELLORD1:
def 6;
    then [v,w] in the InternalRel of R by YELLOW_0:def 14;
    then A486: [v,w] in (the InternalRel of R1) \/ (the InternalRel of R2)
             by NECKLA_2:def 2,A34;
      per cases by A486,XBOOLE_0:def 2;
      suppose [v,w] in the InternalRel of R1;
      then v in the carrier of R1 & w in the carrier of R1 by ZFMISC_1:106;
      then v in (the carrier of R1) /\ the carrier of R2 by A478,XBOOLE_0
:def 3;
      hence contradiction by A1,XBOOLE_0:def 7;
      end;
      suppose [v,w] in the InternalRel of R2;
      then v in the carrier of R2 & w in the carrier of R2 by ZFMISC_1:106;
      then w in (the carrier of R1) /\ the carrier of R2 by A479,XBOOLE_0
:def 3;
      hence contradiction by A1,XBOOLE_0:def 7;
     end;
    end;
  then A493: subrelstr Z embeds Necklace 4 by Th39,A395,A451,A452,A454,A457,
A459;
    subrelstr Z is non empty
   proof
      the carrier of subrelstr Z = Z by YELLOW_0:def 15;
    then u in the carrier of subrelstr Z by ENUMSET1:19;
    hence thesis by STRUCT_0:def 1;
   end;
  then reconsider H = subrelstr Z as non empty full SubRelStr of G;
    G embeds Necklace 4
   proof
    assume not thesis;
    then G is N-free by NECKLA_2:def 1;
    then H is N-free by Th23;
    hence thesis by A493,NECKLA_2:def 1;
   end;
  hence thesis;
 end;
 end;
end;

theorem
     for G being non empty strict finite irreflexive symmetric RelStr
  st G is N-free & the carrier of G in FinSETS
     holds the RelStr of G in fin_RelStr_sp
proof
 defpred P[Nat] means
 for G be non empty strict finite irreflexive symmetric RelStr
 st G is N-free & Card the carrier of G = $1 & the carrier of G in FinSETS
  holds (the RelStr of G) in fin_RelStr_sp;
A1: for n be Nat st for k be Nat st k < n holds P[k] holds P[n]
 proof
  let n be Nat such that A2: for k be Nat st k < n holds P[k];
  let G be non empty strict finite irreflexive symmetric RelStr;
  set C_G = ComplRelStr G;
  assume A3: G is N-free & Card (the carrier of G) = n &
             (the carrier of G) in FinSETS;
  per cases;
  suppose G is trivial;
   then reconsider G as non empty trivial RelStr;
     (the RelStr of G) in fin_RelStr_sp by A3,NECKLA_2:def 5;
  hence thesis;
  end;
  suppose G is not path-connected & G is non trivial;
   then consider G1,G2 being non empty strict irreflexive symmetric RelStr
   such that A7: (the carrier of G1) misses (the carrier of G2) and
   A8: the RelStr of G = union_of(G1,G2) by Th31;
   set cG1 = the carrier of G1,
       cG2 = the carrier of G2,
       R = the RelStr of G,
       cR = the carrier of R;
   A10: cR = cG1 \/ cG2  by A8,NECKLA_2:def 2;
   then A11: Card cG1 <` Card cR by Lm1,A7;
   then A12: cG1 is finite by CARD_2:68;
   reconsider cG1 as finite set by A11,CARD_2:68;
   reconsider G1 as non empty strict finite irreflexive symmetric RelStr
              by GROUP_1:def 13,A12;
   reconsider cR as finite set;
   A13: card cG1 < card cR by CARD_2:58,A11;
     G1 is full SubRelStr of G by A7,A8,Th10;
   then A20: G1 is N-free by Th23,A3;
     the carrier of G1 in FinSETS
    proof
      cG1 c= the carrier of G by A10,XBOOLE_1:7;
     hence thesis by A3,CLASSES1:6,CLASSES2:def 2;
    end;
   then A23: G1 in fin_RelStr_sp by A2,A3,A13,A20;
   A24: Card cG2 <` Card cR by Lm1,A7,A10;
   then A25: cG2 is finite by CARD_2:68;
   reconsider cG2 as finite set by A24,CARD_2:68;
   reconsider G2 as non empty strict finite irreflexive symmetric RelStr
              by GROUP_1:def 13,A25;
   A26: card cG2 < card cR by CARD_2:58,A24;
     G2 is full SubRelStr of G by A7,A8,Th10;
   then A33: G2 is N-free by Th23,A3;
     the carrier of G2 in FinSETS
    proof
      cG2 c= the carrier of G by A10,XBOOLE_1:7;
     hence thesis by A3,CLASSES1:6,CLASSES2:def 2;
    end;
   then G2 in fin_RelStr_sp by A2,A3,A26,A33;
  hence thesis by A7,A8,A23,NECKLA_2:def 5;
  end;
  suppose C_G is not path-connected & G is non trivial;
   then consider G1,G2 being non empty strict irreflexive symmetric RelStr
   such that A38: (the carrier of G1) misses (the carrier of G2) and
   A39: (the RelStr of G) = sum_of(G1,G2) by Th32;
   set cG1 = the carrier of G1,
       cG2 = the carrier of G2,
       R = the RelStr of G,
       cR = the carrier of R,
       IG1 = the InternalRel of G1,
       IG2 = the InternalRel of G2,
       G1G2 = [:the carrier of G1,the carrier of G2:],
       G2G1 = [:the carrier of G2,the carrier of G1:],
       IG = the InternalRel of G;
   A43: cR = cG1 \/ cG2  by A39,NECKLA_2:def 3;
   then A44: Card cG1 <` Card cR by Lm1,A38;
   then A45: cG1 is finite by CARD_2:68;
   reconsider cG1 as finite set by A44,CARD_2:68;
   reconsider G1 as non empty strict finite irreflexive symmetric RelStr
              by GROUP_1:def 13,A45;
   reconsider cR as finite set;
   A46: card cG1 < card cR by CARD_2:58,A44;
     G1 is full SubRelStr of G by A38,A39,Th10;
   then A53: G1 is N-free by Th23,A3;
     the carrier of G1 in FinSETS
    proof
      cG1 c= the carrier of G by A43,XBOOLE_1:7;
     hence thesis by A3,CLASSES1:6,CLASSES2:def 2;
    end;
   then A56: G1 in fin_RelStr_sp by A2,A3,A46,A53;
   A57: Card cG2 <` Card cR by Lm1,A38,A43;
   then A58: cG2 is finite by CARD_2:68;
   reconsider cG2 as finite set by A57,CARD_2:68;
   reconsider G2 as non empty strict finite irreflexive symmetric RelStr
              by GROUP_1:def 13,A58;
   A59: card cG2 < card cR by CARD_2:58,A57;
     G2 is full SubRelStr of G by A38,A39,Th10;
   then A66: G2 is N-free by Th23,A3;
     the carrier of G2 in FinSETS
    proof
      cG2 c= the carrier of G by A43,XBOOLE_1:7;
     hence thesis by A3,CLASSES1:6,CLASSES2:def 2;
    end;
   then G2 in fin_RelStr_sp by A2,A3,A59,A66;
  hence thesis by A38,A39,A56,NECKLA_2:def 5;
  end;
  suppose A70: G is non trivial & G is path-connected & C_G is path-connected;
   consider x be set such that A71: x in the carrier of G by XBOOLE_0:def 1;
   reconsider x as Element of the carrier of G by A71;
   set A = (the carrier of G) \ {x},
       X = {x};
   reconsider X as Subset of the carrier of G;
   A72: A c= the carrier of G
    proof
     let a be set;
     assume a in A;
     hence thesis by XBOOLE_0:def 4;
    end;
   then reconsider A as Subset of the carrier of G;
   set R = subrelstr A;
     R is non empty
    proof
     assume not thesis;
     then the carrier of R is empty by STRUCT_0:def 1;
     then A is empty by YELLOW_0:def 15;
     then A78: the carrier of G = {x} by ZFMISC_1:66;
      not the carrier of G is trivial by A70,REALSET1:def 13;
     hence thesis by A78,REALSET1:def 12;
    end;
   then reconsider R as non empty finite irreflexive symmetric RelStr;
   A80: card the carrier of R < n
    proof
       card A = card (the carrier of G) - card {x} by CARD_2:63;
     then A82: card A = n - 1 by CARD_2:60,A3;
       n - 1 < n - 1 + 1 by REAL_1:69;
     then n - 1 < n by XCMPLX_1:27;
     hence thesis by A82,YELLOW_0:def 15;
    end;
   A85: R is N-free by Th23,A3;
     the carrier of R in FinSETS
    proof
      the carrier of R c= the carrier of G by YELLOW_0:def 15,A72;
     hence thesis by A3,CLASSES1:6,CLASSES2:def 2;
     end;
   then A88: R in fin_RelStr_sp by A2,A80,A85;
thus thesis
proof
 per cases by A88,NECKLA_2:6;
 suppose R is trivial RelStr;
  then the carrier of R is trivial by REALSET1:def 13;
  then A94: A is trivial by YELLOW_0:def 15;
    the carrier of R is non empty;
  then A is non empty by YELLOW_0:def 15;
  then consider a be set such that A97: A = {a} by A94,REALSET1:def 12;
   A98: {a} \/ {x} = (the carrier of G) \/ {x} by A97,XBOOLE_1:39;
   A99: (the carrier of G) \/ {x} = the carrier of G
    proof
     thus (the carrier of G) \/ {x} c= the carrier of G
      proof
       let c be set;
       assume c in (the carrier of G) \/ {x};
       then c in the carrier of G or c in {x} by XBOOLE_0:def 2;
       hence thesis;
      end;
     let c be set;
     assume c in the carrier of G;
     hence thesis by XBOOLE_0:def 2;
    end;
  then A103: the carrier of G = {a,x} by A98,ENUMSET1:41;
    a <> x
   proof
    assume a = x;
     then the carrier of G is trivial by A98,A99,REALSET1:def 12;
    hence contradiction by REALSET1:def 13,A70;
   end;
  then Card (the carrier of G) = 2 by CARD_2:76,A103;
  hence thesis by A3,Th34;
 end;
 suppose ex R1,R2 being strict RelStr st
  (the carrier of R1) misses (the carrier of R2) &
  R1 in fin_RelStr_sp & R2 in fin_RelStr_sp &
  (R = union_of(R1,R2) or R = sum_of(R1,R2) );
  then consider R1,R2 being strict RelStr such that
  A109: (the carrier of R1) misses (the carrier of R2) and
  A110: R1 in fin_RelStr_sp & R2 in fin_RelStr_sp &
  (R = union_of(R1,R2) or R = sum_of(R1,R2));
 thus thesis
  proof
   per cases by A110;
   suppose A111: R = union_of(R1,R2);
     R1 is SubRelStr of R by Th10,A110,A109;
   then reconsider R1 as non empty SubRelStr of G by YELLOW_6:16,A110,NECKLA_2:
4;
     R2 is SubRelStr of R by Th10,A110,A109;
   then reconsider R2 as non empty SubRelStr of G by YELLOW_6:16,A110,NECKLA_2:
4;
     subrelstr ([#]G \ {x}) = union_of(R1,R2) by A111,PRE_TOPC:def 3;
   then G embeds Necklace 4 by A109,A70,Th40;
   hence thesis by A3,NECKLA_2:def 1;
   end;
   suppose R = sum_of(R1,R2);
    then A125: ComplRelStr R = union_of(ComplRelStr R1, ComplRelStr R2)
              by Th18,A109;
    reconsider G' = ComplRelStr G as non empty irreflexive symmetric RelStr;
    A126: ComplRelStr R1 is non empty
     proof
      assume not thesis;
      then the carrier of ComplRelStr R1 is empty by STRUCT_0:def 1;
      then the carrier of R1 is empty by NECKLACE:def 9;
      then R1 is empty by STRUCT_0:def 1;
      hence contradiction by NECKLA_2:4,A110;
     end;
    A131: ComplRelStr R2 is non empty
     proof
      assume not thesis;
      then the carrier of ComplRelStr R2 is empty by STRUCT_0:def 1;
      then the carrier of R2 is empty by NECKLACE:def 9;
      then R2 is empty by STRUCT_0:def 1;
      hence contradiction by NECKLA_2:4,A110;
     end;
    reconsider R11 = ComplRelStr R1 as non empty RelStr by A126;
    reconsider R22 = ComplRelStr R2 as non empty RelStr by A131;
    reconsider x' = x as Element of the carrier of G'
              by NECKLACE:def 9;
    A136: the carrier of R11 = the carrier of R1 &
    the carrier of R22 = the carrier of R2 by NECKLACE:def 9;
    A137: G' is path-connected & ComplRelStr G' is path-connected by A70,Th16;
    A138: G' is non trivial
     proof
      assume not thesis;
      then consider x1 being set such that
      A140: the carrier of G' = {x1} by GROUP_6:def 2;
       the carrier of G = {x1} by A140,NECKLACE:def 9;
      hence contradiction by A70,GROUP_6:def 2;
     end;
     ComplRelStr R = ComplRelStr (subrelstr ([#]G \ {x})) by PRE_TOPC:def 3
                 .= subrelstr ([#](G') \ {x'}) by Th20;
    then A143: G' embeds Necklace 4 by A109,A125,A136,A137,A138,Th40;
     G' is N-free by A3,Th25;
    hence thesis by A143,NECKLA_2:def 1;
  end;
  end;
 end;
 end;
end;
end;
A145: for k be Nat holds P[k] from NAT_1:sch 4(A1);
 let R be non empty strict finite irreflexive symmetric RelStr;
 assume A146: R is N-free;
 consider n be Nat such that
A147: Card (the carrier of R) = Card n by CARD_4:4;
   Card n = n by CARD_1:66;
 hence thesis by A145,A146,A147;
end;
