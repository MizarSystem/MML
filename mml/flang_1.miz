:: Formal Languages -- Concatenation and Closure
::  by Micha{\l} Trybulec
::
:: Received March 9, 2007
:: Copyright (c) 2007 Association of Mizar Users

environ

 vocabularies FUNCT_1, BOOLE, FINSEQ_1, RELAT_1, TARSKI, ALGSEQ_1, AFINSQ_1,
      MEASURE6, GROUP_1, SETFAM_1, FLANG_1, ARYTM;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, NUMBERS, XCMPLX_0, NAT_1,
      DOMAIN_1, FUNCT_1, RELSET_1, FUNCT_2, SETFAM_1, XXREAL_0, AFINSQ_1,
      CATALAN2;
 constructors XXREAL_0, XREAL_0, NAT_1, CATALAN2;
 registrations XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, ORDINAL1, XXREAL_0,
      XREAL_0, NAT_1, AFINSQ_1, CARD_1;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;
 definitions TARSKI;
 theorems AFINSQ_1, ISOCAT_2, NAT_1, ORDINAL1, REAL_1, SUBSET_1, TARSKI,
      XBOOLE_0, XBOOLE_1, XREAL_1, ZFMISC_1;
 schemes CARD_FIL, DOMAIN_1, NAT_1, RECDEF_1, SUBSET_1;

begin

 reserve E, x, y, z, X, Y, Z for set;
 reserve A, B, C, D for Subset of E^omega;
 reserve a, a1, a2, b, c, c1, c2, d, ab, bc for Element of E^omega;
 reserve e for Element of E;
 reserve i, j, k, l, n, n1, n2, m for Nat;

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: Preliminaries - Redefinitions
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

definition
  let E, a, b;
  redefine func a ^ b -> Element of E^omega;
  coherence
  proof
     a ^ b is XFinSequence of E;
    hence thesis by AFINSQ_1:def 8;
  end;
end;

definition
  let E;
  redefine func <%>E -> Element of E^omega;
  coherence by AFINSQ_1:def 8;
end;

definition
  let E be non empty set;
  let e be Element of E;
  redefine func <%e%> -> Element of E^omega;
  coherence
  proof
     {e} c= E;
    then rng <%e%> c= E by AFINSQ_1:36;
    hence thesis by AFINSQ_1:def 8;
  end;
end;

definition
  let E, a;
  redefine func {a} -> Subset of E^omega;
  coherence by SUBSET_1:55;
end;

definition
  let E;
  let f be Function of NAT, bool E;
  let n;
  redefine func f.n -> Subset of E;
  coherence
  proof
  n is Element of NAT by ORDINAL1:def 13;
    then f.n in rng f by ISOCAT_2:4;
    hence thesis;
  end;
end;

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: Preliminaries - Numbers:
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

theorem Th1:
n1 > n or n2 > n implies n1 + n2 > n
proof
  assume
A1:  n1 > n or n2 > n;
A2:
  n1 > n & n2 >= 0 implies n1 + n2 > n + 0 by XREAL_1:10;

  n1 >= 0 & n2 > n implies n1 + n2 > n + 0 by XREAL_1:10;
  hence thesis by A1,A2;
end;

theorem Th2:
n1 + n <= n2 + 1 & n > 0 implies n1 <= n2
proof
  assume
A1:  n1 + n <= n2 + 1 & n > 0;
  then 1 + 0 <= n by NAT_1:13;
  hence thesis by A1,XREAL_1:10;
end;

theorem Th3:
n1 + n2 = 1 iff (n1 = 1 & n2 = 0) or (n1 = 0 & n2 = 1)
proof
  thus n1 + n2 = 1 implies (n1 = 1 & n2 = 0) or (n1 = 0 & n2 = 1)
  proof
    assume
A1:    n1 + n2 = 1;
     now
      assume
A2:     n1 <= 1 & n2 <= 1;
A3:   n1 = 1 & n2 = 1 implies contradiction by A1;
       n1 = 0 & n2 = 0 implies contradiction by A1;
      hence (n1 = 1 & n2 = 0) or (n1 = 0 & n2 = 1) by A2,A3,NAT_1:26;
    end;
    hence thesis by A1,Th1;
  end;
  thus thesis;
end;

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: Preliminaries - Sequences:
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

theorem Th4:
a ^ b = <%x%> iff (a = <%>E & b = <%x%>) or (b = <%>E & a = <%x%>)
proof
  thus a ^ b = <%x%> implies (a = <%>E & b = <%x%>) or (b = <%>E & a = <%x%>)
  proof
    assume
A1:    a ^ b = <%x%>;
    then len (a ^ b) = 1 by AFINSQ_1:38;
    then len a + len b = 1 by AFINSQ_1:20;
    then (len a = 1 & len b = 0) or (len a = 0 & len b = 1) by Th3;
    then (len a = 1 & b = <%>E) or (a = <%>E & len b = 1) by AFINSQ_1:18;
    hence thesis by A1,AFINSQ_1:32;
  end;
  assume (a = <%>E & b = <%x%>) or (b = <%>E & a = <%x%>);
  hence thesis by AFINSQ_1:32;
end;

theorem Th5:
for p, q being XFinSequence holds
  a = p ^ q implies
    p is Element of E^omega &
    q is Element of E^omega
proof
  let p, q be XFinSequence;
  assume a = p ^ q;
  then p is XFinSequence of E &
       q is XFinSequence of E by AFINSQ_1:34;
  hence thesis by AFINSQ_1:def 8;
end;

theorem Th6:
<%x%> is Element of E^omega implies x in E
proof
  assume <%x%> is Element of E^omega;
  then rng <%x%> c= E by ORDINAL1:def 8;
  then {x} c= E by AFINSQ_1:36;
  hence thesis by ZFMISC_1:37;
end;

theorem Th7:
len b = n + 1 implies ex c, e st len c = n & b = c ^ <%e%>
proof
  assume
A1: len b = n + 1;
  then b <> {} by AFINSQ_1:18;
  then consider c' being XFinSequence, x such that
A2: b = c' ^ <%x%> by AFINSQ_1:44;
  reconsider c = c' as Element of E^omega by A2,Th5;
A3:
  n + 1 = len c + len <%x%> by A1,A2,AFINSQ_1:20
       .= len c + 1 by AFINSQ_1:37;
   <%x%> is Element of E^omega by A2,Th5;
  then reconsider e = x as Element of E by Th6;
  take c, e;
  thus thesis by A2,A3;
end;

theorem Th8:
for p being XFinSequence st p <> {}
  ex q being XFinSequence, x st p = <%x%> ^ q
proof
  defpred P[Nat] means
    for p being XFinSequence st len p = $1 & p <> {}
      ex q being XFinSequence, x st p = <%x%> ^ q;
A1:
  P[0] by AFINSQ_1:18;
A2:
  now
    let n;
    assume
A3:   P[n];
    thus P[n + 1]
    proof
      let p be XFinSequence such that
A4:     len p = n + 1 & p <> {};
      consider q being XFinSequence, x such that
A5:     p = q ^ <%x%> by A4,AFINSQ_1:44;
A6:   n + 1 = len q + len <%x%> by A4,A5,AFINSQ_1:20
           .= len q + 1 by AFINSQ_1:37;
      per cases;
      suppose
A7:    q = {};
        then p = <%x%> by A5,AFINSQ_1:32
              .= <%x%> ^ q by A7,AFINSQ_1:32;
        hence ex s being XFinSequence, z st p = <%z%> ^ s;
      end;
      suppose q <> {};
        then consider r being XFinSequence, y such that
A8:       q = <%y%> ^ r by A3,A6;
         p = <%y%> ^ (r ^ <%x%>) by A5,A8,AFINSQ_1:30;
        hence ex s being XFinSequence, z st p = <%z%> ^ s;
      end;
    end;
  end;
A9:
  for n holds P[n] from NAT_1:sch 2(A1, A2);
  let p be XFinSequence;
  assume
A10:  p <> {};
   len p = len p;
  hence thesis by A9,A10;
end;

theorem
 len b = n + 1 implies ex c, e st len c = n & b = <%e%> ^ c
proof
  assume
A1: len b = n + 1;
  then b <> {} by AFINSQ_1:18;
  then consider c' being XFinSequence, x such that
A2: b = <%x%> ^ c' by Th8;
  reconsider c = c' as Element of E^omega by A2,Th5;
A3:
  n + 1 = len c + len <%x%> by A1,A2,AFINSQ_1:20
       .= len c + 1 by AFINSQ_1:37;
   <%x%> is Element of E^omega by A2,Th5;
  then reconsider e = x as Element of E by Th6;
  take c, e;
  thus thesis by A2,A3;
end;

theorem Th10:
for p being XFinSequence st len p = i + j
  ex q1, q2 being XFinSequence st len q1 = i & len q2 = j & p = q1 ^ q2
proof
  defpred P[Nat] means
    for p being XFinSequence, i, j st len p = $1 & len p = i + j
      ex q1, q2 being XFinSequence st len q1 = i & len q2 = j & p = q1 ^ q2;
A1:
  P[0]
  proof
    let p be XFinSequence;
    let i, j;
    assume
A2:   len p = 0 & len p = i + j;
    then A3:   i = 0 & j = 0 by NAT_1:7;
A4: p = {} by A2,AFINSQ_1:18;
A5: len {} = i & len {} = j by A3,AFINSQ_1:18;
     p = {} ^ {} by A4,AFINSQ_1:32;
    hence thesis by A5;
  end;
A6:
  now
    let n;
    assume
A7:   P[n];
    thus P[n + 1]
    proof
      let p be XFinSequence;
      let i, j;
      assume
A8:     len p = n + 1 & len p = i + j;
      per cases;
      suppose
A9:     j = 0;
        take q1 = p;
        take q2 = {};
        thus len q1 = i & len q2 = j & p = q1 ^ q2
          by A8,A9,AFINSQ_1:18,32;
      end;
      suppose j > 0;
        then consider k such that
A10:      j = k + 1 by NAT_1:6;
A11:    n = i + k by A8,A10;
         p <> {} by A8,AFINSQ_1:18;
        then consider q being XFinSequence, x such that
A12:      p = q ^ <%x%> by AFINSQ_1:44;
         n + 1 = len q + len <%x%> by A8,A12,AFINSQ_1:20
             .= len q + 1 by AFINSQ_1:37;
        then consider q1, q2 being XFinSequence such that
A13:      len q1 = i & len q2 = k & q = q1 ^ q2 by A7,A11;
A14:    p = q1 ^ (q2 ^ <%x%>) by A12,A13,AFINSQ_1:30;
         len (q2 ^ <%x%>) = len q2 + len <%x%> by AFINSQ_1:20
                        .= j by A10,A13,AFINSQ_1:37;
        hence ex q1, q2 being XFinSequence st
          len q1 = i & len q2 = j & p = q1 ^ q2 by A13,A14;
      end;
    end;
  end;
   for n holds P[n] from NAT_1:sch 2(A1, A6);
  hence thesis;
end;

theorem
 len b = n + m implies ex c1, c2 st len c1 = n & len c2 = m & b = c1 ^ c2
proof
  assume
 len b = n + m;
  then consider c1', c2' being XFinSequence such that
A1: len c1' = n & len c2' = m & b = c1' ^ c2' by Th10;
  reconsider c1 = c1' as Element of E^omega by A1,Th5;
  reconsider c2 = c2' as Element of E^omega by A1,Th5;
  take c1, c2;
  thus thesis by A1;
end;

theorem Th12:
a ^ a = a implies a = {}
proof
  assume
  a ^ a = a;
  then len a + len a = len a by AFINSQ_1:20;
  hence thesis by AFINSQ_1:18;
end;

begin

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: Concatenation of Languages
:: Definition of ^^
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

definition
  let E, A, B;
  func A ^^ B -> Subset of E^omega means :Def1:
    x in it iff ex a, b st a in A & b in B & x = a ^ b;
  existence
  proof
    defpred P[set] means ex a, b st a in A & b in B & $1 = a ^ b;
    consider C such that
A1:    x in C iff x in E^omega & P[x] from SUBSET_1:sch 1;
    take C;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let C1, C2 be Subset of E^omega such that
A2:   x in C1 iff ex a, b st a in A & b in B & x = a ^ b and
A3:   x in C2 iff ex a, b st a in A & b in B & x = a ^ b;
     now
      let x;
      thus x in C1 implies x in C2
      proof
        assume x in C1;
        then ex a, b st a in A & b in B & x = a ^ b by A2;
        hence x in C2 by A3;
      end;
      assume x in C2;
      then ex a, b st a in A & b in B & x = a ^ b by A3;
      hence x in C1 by A2;
    end;
    hence thesis by TARSKI:2;
  end;
end;

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: Concatenation of Languages
:: Properties of ^^
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

theorem Th13:
A ^^ B = {} iff A = {} or B = {}
proof
  thus A ^^ B = {} implies A = {} or B = {}
  proof
    assume that
A1:   A ^^ B = {} and
A2:   A <> {} & B <> {};
    consider a such that
A3:   a in A by A2,SUBSET_1:10;
    consider b such that
A4:   b in B by A2,SUBSET_1:10;
     a ^ b in A ^^ B by A3,A4,Def1;
    hence contradiction by A1;
  end;
  assume
A5:  A = {} or B = {};
   not ex x st x in A ^^ B
  proof
    given x such that
A6:   x in A ^^ B;
     ex a, b st a in A & b in B & x = a ^ b by A6,Def1;
    hence contradiction by A5;
  end;
  hence thesis by XBOOLE_0:def 1;
end;

theorem Th14:
A ^^ {<%>E} = A & {<%>E} ^^ A = A
proof
A1:
  x in {<%>E} ^^ A implies x in A
  proof
    assume x in {<%>E} ^^ A;
    then consider d, a such that
A2:   d in {<%>E} & a in A & x = d ^ a by Def1;
     x = <%>E ^ a by A2,TARSKI:def 1;
    hence thesis by A2,AFINSQ_1:32;
  end;
A3:
  x in A ^^ {<%>E} implies x in A
  proof
    assume x in A ^^ {<%>E};
    then consider a, d such that
A4:   a in A & d in {<%>E} & x = a ^ d by Def1;
     x = a ^ <%>E by A4,TARSKI:def 1;
    hence thesis by A4,AFINSQ_1:32;
  end;
A5:
  x in A implies x in {<%>E} ^^ A
  proof
    assume
A6:   x in A;
     ex d, a st d in {<%>E} & a in A & x = d ^ a
    proof
      consider d such that
A7:     d = <%>E;
      reconsider a = x as Element of E^omega by A6;
      take d, a;
      thus thesis by A6,A7,AFINSQ_1:32,TARSKI:def 1;
    end;
    hence thesis by Def1;
  end;

  x in A implies x in A ^^ {<%>E}
  proof
    assume
A8:   x in A;
     ex a, d st a in A & d in {<%>E} & x = a ^ d
    proof
      set d = <%>E;
      reconsider a = x as Element of E^omega by A8;
      take a, d;
      thus thesis by A8,AFINSQ_1:32,TARSKI:def 1;
    end;
    hence thesis by Def1;
  end;
  hence thesis by A1,A3,A5,TARSKI:2;
end;

theorem Th15:
A ^^ B = {<%>E} iff A = {<%>E} & B = {<%>E}
proof
  thus A ^^ B = {<%>E} implies A = {<%>E} & B = {<%>E}
  proof
    assume that
A1:   A ^^ B = {<%>E} and
A2:   (A <> {<%>E} or B <> {<%>E});
     <%>E in A ^^ B by A1,TARSKI:def 1;
    then consider a, b such that
A3:   a in A & b in B & <%>E = a ^ b by Def1;
  now
      let x;
      assume
A4:     (x in A or x in B) & x <> <%>E;
A5:   now
        assume
A6:      x in A;
        then reconsider xa = x as Element of E^omega;
A7:    xa ^ b in A ^^ B by A3,A6,Def1;
         xa ^ b <> <%>E by A4,AFINSQ_1:33;
        hence contradiction by A1,A7,TARSKI:def 1;
      end;
   now
        assume
A8:      x in B;
        then reconsider xb = x as Element of E^omega;
A9:    a ^ xb in A ^^ B by A3,A8,Def1;
         a ^ xb <> <%>E by A4,AFINSQ_1:33;
        hence contradiction by A1,A9,TARSKI:def 1;
      end;
      hence contradiction by A4,A5;
    end;
    then (x in A iff x = <%>E) & (x in B iff x = <%>E) by A3;
    hence contradiction by A2,TARSKI:def 1;
  end;
  assume A = {<%>E} & B = {<%>E};
  hence thesis by Th14;
end;

theorem Th16:
<%>E in A ^^ B iff <%>E in A & <%>E in B
proof
  thus <%>E in A ^^ B implies <%>E in A & <%>E in B
  proof
    assume <%>E in A ^^ B;
    then consider a, b such that
A1:    a in A & b in B & a ^ b = <%>E by Def1;
    thus thesis by A1,AFINSQ_1:33;
  end;
  assume <%>E in A & <%>E in B;
  then <%>E ^ <%>E in A ^^ B by Def1;
  hence thesis by AFINSQ_1:32;
end;

theorem Th17:
<%>E in B implies A c= A ^^ B & A c= B ^^ A
proof
  assume
A1:  <%>E in B;
  thus A c= A ^^ B
  proof
    let x;
    assume
A2:   x in A;
    then reconsider xa = x as Element of E^omega;
     xa ^ <%>E in A ^^ B by A1,A2,Def1;
    hence x in A ^^ B by AFINSQ_1:32;
  end;
  thus A c= B ^^ A
  proof
    let x;
    assume
A3:   x in A;
    then reconsider xa = x as Element of E^omega;
     <%>E ^ xa in B ^^ A by A1,A3,Def1;
    hence x in B ^^ A by AFINSQ_1:32;
  end;
end;

theorem Th18:
A c= C & B c= D implies A ^^ B c= C ^^ D
proof
  assume
A1:  A c= C & B c= D;
  thus thesis
  proof
    let x;
    assume x in A ^^ B;
    then consider a, b such that
A2:    a in A & b in B & x = a ^ b by Def1;
    thus x in C ^^ D by A1,A2,Def1;
  end;
end;

theorem Th19:
(A ^^ B) ^^ C = A ^^ (B ^^ C)
proof
   now
    let x;
    thus x in (A ^^ B) ^^ C implies x in A ^^ (B ^^ C)
    proof
      assume x in (A ^^ B) ^^ C;
      then consider ab, c such that
A1:     ab in (A ^^ B) & c in C & x = ab ^ c by Def1;
      consider a, b such that
A2:     a in A & b in B & ab = a ^ b by A1,Def1;
A3:   x = a ^ (b ^ c) by A1,A2,AFINSQ_1:30;
       b ^ c in B ^^ C by A1,A2,Def1;
      hence thesis by A2,A3,Def1;
    end;
    assume x in A ^^ (B ^^ C);
    then consider a, bc such that
A4:   a in A & bc in (B ^^ C) & x = a ^ bc by Def1;
    consider b, c such that
A5:   b in B & c in C & bc = b ^ c by A4,Def1;
A6: x = (a ^ b) ^ c by A4,A5,AFINSQ_1:30;
     a ^ b in A ^^ B by A4,A5,Def1;
    hence x in (A ^^ B) ^^ C by A5,A6,Def1;
  end;
  hence thesis by TARSKI:2;
end;

theorem Th20:
A ^^ (B /\ C) c= (A ^^ B) /\ (A ^^ C) &
(B /\ C) ^^ A c= (B ^^ A) /\ (C ^^ A)
proof
  thus A ^^ (B /\ C) c= (A ^^ B) /\ (A ^^ C)
  proof
    let x;
    assume x in A ^^ (B /\ C);
    then consider a, bc such that
A1:   a in A & bc in B /\ C & x = a ^ bc by Def1;
     bc in B & bc in C by A1,XBOOLE_0:def 3;
    then x in A ^^ B & x in A ^^ C by A1,Def1;
    hence x in (A ^^ B) /\ (A ^^ C) by XBOOLE_0:def 3;
  end;
  thus (B /\ C) ^^ A c= (B ^^ A) /\ (C ^^ A)
  proof
    let x;
    assume x in (B /\ C) ^^ A;
    then consider bc, a such that
A2:   bc in B /\ C & a in A & x = bc ^ a by Def1;
     bc in B & bc in C by A2,XBOOLE_0:def 3;
    then x in B ^^ A & x in C ^^ A by A2,Def1;
    hence x in (B ^^ A) /\ (C ^^ A) by XBOOLE_0:def 3;
  end;
end;

theorem Th21:
A ^^ B \/ A ^^ C = A ^^ (B \/ C) &
B ^^ A \/ C ^^ A = (B \/ C) ^^ A
proof
A1:
  A ^^ (B \/ C) c= (A ^^ B) \/ (A ^^ C)
  proof
    let x;
    assume x in A ^^ (B \/ C);
    then consider a, bc such that
A2:   a in A & bc in B \/ C & x = a ^ bc by Def1;
     bc in B or bc in C by A2,XBOOLE_0:def 2;
    then x in A ^^ B or x in A ^^ C by A2,Def1;
    hence x in (A ^^ B) \/ (A ^^ C) by XBOOLE_0:def 2;
  end;
A3:
  (B \/ C) ^^ A c= (B ^^ A) \/ (C ^^ A)
  proof
    let x;
    assume x in (B \/ C) ^^ A;
    then consider bc, a such that
A4:   bc in B \/ C & a in A & x = bc ^ a by Def1;
     bc in B or bc in C by A4,XBOOLE_0:def 2;
    then x in B ^^ A or x in C ^^ A by A4,Def1;
    hence x in (B ^^ A) \/ (C ^^ A) by XBOOLE_0:def 2;
  end;
A5:
  now
     B c= B \/ C & C c= B \/ C by XBOOLE_1:7;
    then A ^^ B c= A ^^ (B \/ C) & A ^^ C c= A ^^ (B \/ C) by Th18;
    hence (A ^^ B) \/ (A ^^ C) c= A ^^ (B \/ C) by XBOOLE_1:8;
  end;

  now
     B c= B \/ C & C c= B \/ C by XBOOLE_1:7;
    then B ^^ A c= (B \/ C) ^^ A & C ^^ A c= (B \/ C) ^^ A by Th18;
    hence (B ^^ A) \/ (C ^^ A) c= (B \/ C) ^^ A by XBOOLE_1:8;
  end;
  hence thesis by A1,A3,A5,XBOOLE_0:def 10;
end;

theorem Th22:
(A ^^ B) \ (A ^^ C) c= A ^^ (B \ C) &
(B ^^ A) \ (C ^^ A) c= (B \ C) ^^ A
proof
  thus (A ^^ B) \ (A ^^ C) c= A ^^ (B \ C)
  proof
    let x;
    assume x in (A ^^ B) \ (A ^^ C);
    then A1:   x in A ^^ B & not x in A ^^ C by XBOOLE_0:def 4;
    then consider a, b such that
A2:   a in A & b in B & x = a ^ b by Def1;
 now
      assume not b in C;
      then b in B \ C by A2,XBOOLE_0:def 4;
      hence x in A ^^ (B \ C) by A2,Def1;
    end;
    hence x in A ^^ (B \ C) by A1,A2,Def1;
  end;
  thus (B ^^ A) \ (C ^^ A) c= (B \ C) ^^ A
  proof
    let x;
    assume x in (B ^^ A) \ (C ^^ A);
    then A3:   x in B ^^ A & not x in C ^^ A by XBOOLE_0:def 4;
    then consider b, a such that
A4:   b in B & a in A & x = b ^ a by Def1;
 now
      assume not b in C;
      then b in B \ C by A4,XBOOLE_0:def 4;
      hence x in (B \ C) ^^ A by A4,Def1;
    end;
    hence x in (B \ C) ^^ A by A3,A4,Def1;
  end;
end;

theorem
 (A ^^ B) \+\ (A ^^ C) c= A ^^ (B \+\ C) &
(B ^^ A) \+\ (C ^^ A) c= (B \+\ C) ^^ A
proof
A1:
  now
A2: A ^^ (B /\ C) c= (A ^^ B) /\ (A ^^ C) by Th20;
A3: (A ^^ (B \/ C)) \ (A ^^ (B /\ C)) c=
      A ^^ ((B \/ C) \ (B /\ C)) by Th22;
     (A ^^ B) \+\ (A ^^ C)
      = ((A ^^ B) \/ (A ^^ C)) \ ((A ^^ B) /\ (A ^^ C)) by XBOOLE_1:101
     .= (A ^^ (B \/ C)) \ ((A ^^ B) /\ (A ^^ C)) by Th21;
    then (A ^^ B) \+\ (A ^^ C) c=
           (A ^^ (B \/ C)) \ (A ^^ (B /\ C)) by A2,XBOOLE_1:34;
    then (A ^^ B) \+\ (A ^^ C) c= A ^^ ((B \/ C) \ (B /\ C)) by A3,XBOOLE_1:1
;
    hence (A ^^ B) \+\ (A ^^ C) c= A ^^ (B \+\ C) by XBOOLE_1:101;
  end;

  now
A4: (B /\ C) ^^ A c= (B ^^ A) /\ (C ^^ A) by Th20;
A5: ((B \/ C) ^^ A) \ ((B /\ C) ^^ A) c=
      ((B \/ C) \ (B /\ C)) ^^ A by Th22;
     (B ^^ A) \+\ (C ^^ A)
      = ((B ^^ A) \/ (C ^^ A)) \ ((B ^^ A) /\ (C ^^ A)) by XBOOLE_1:101
     .= ((B \/ C) ^^ A) \ ((B ^^ A) /\ (C ^^ A)) by Th21;
    then (B ^^ A) \+\ (C ^^ A) c=
           ((B \/ C) ^^ A) \ ((B /\ C) ^^ A) by A4,XBOOLE_1:34;
    then (B ^^ A) \+\ (C ^^ A) c= ((B \/ C) \ (B /\ C)) ^^ A by A5,XBOOLE_1:1
;
    hence (B ^^ A) \+\ (C ^^ A) c= (B \+\ C) ^^ A by XBOOLE_1:101;
  end;
  hence thesis by A1;
end;

begin

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: Concatenation of a Language with Itself
:: Definition of |^
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

definition
  let E, A, n;
  func A |^ n -> Subset of E^omega means :Def2:
    ex concat being Function of NAT, bool (E^omega) st
      it = concat.n &
      concat.0 = {<%>E} &
      for i holds concat.(i + 1) = concat.i ^^ A;
  existence
  proof
    defpred P[set, set, set] means
      ex B, C st C = $3 & B = $2 & C = B ^^ A;
A1:  for i being Element of NAT
      for x being Element of bool (E^omega)
        ex y being Element of bool (E^omega) st P[i, x, y]
    proof
      let i be Element of NAT;
      let x be Element of bool (E^omega);
      take x ^^ A;
      thus thesis;
    end;
    consider f being Function of NAT, bool (E^omega) such that
A2:    f.0 = {<%>E} & for i being Element of NAT holds P[i, f.i, f.(i + 1)]
                                                        from RECDEF_1:sch 2(A1)
;
    consider C being Subset of E^omega such that
A3:    C = f.n;
    take C;
 now
      let i;
      reconsider j = i as Element of NAT by ORDINAL1:def 13;
      consider B, C such that
A4:    C = f.(j + 1) & B = f.j & C = B ^^ A by A2;
      thus f.(i + 1) = f.i ^^ A by A4;
    end;
    hence thesis by A2,A3;
  end;
  uniqueness
  proof
    let C1, C2 be Subset of E^omega;
    given f1 being Function of NAT, bool (E^omega) such that
A5:   C1 = f1.n &
      f1.0 = {<%>E} &
      for i holds f1.(i + 1) = f1.i ^^ A;
    given f2 being Function of NAT, bool (E^omega) such that
A6:   C2 = f2.n &
      f2.0 = {<%>E} &
      for i holds f2.(i + 1) = f2.i ^^ A;
    defpred P[Nat] means f1.$1 = f2.$1;
A7: P[0] by A5,A6;
A8: now
      let k;
      assume
A9:    P[k];
       f2.(k + 1) = f2.k ^^ A by A6;
      hence P[k + 1] by A5,A9;
    end;
     for k holds P[k] from NAT_1:sch 2(A7, A8);
    hence thesis by A5,A6;
  end;
end;

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: Concatenation of a Language with Itself
:: Properties of |^
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

theorem Th24:
A |^ (n + 1) = (A |^ n) ^^ A
proof
  consider concat being Function of NAT, bool (E^omega) such that
A1:  A |^ n = concat.n &
    concat.0 = {<%>E} &
    for i holds concat.(i + 1) = concat.i ^^ A by Def2;
   concat.(n + 1) = (A |^ n) ^^ A by A1;
  hence thesis by A1,Def2;
end;

theorem Th25:
A |^ 0 = {<%>E}
proof
  consider concat being Function of NAT, bool (E^omega) such that
A1:  A |^ 0 = concat.0 &
    concat.0 = {<%>E} &
    for i holds concat.(i + 1) = concat.i ^^ A by Def2;
  thus thesis by A1;
end;

theorem Th26:
A |^ 1 = A
proof
  consider concat being Function of NAT, bool (E^omega) such that
A1:  A |^ 1 = concat.1 &
    concat.0 = {<%>E} &
    for i holds concat.(i + 1) = concat.i ^^ A by Def2;
  thus A |^ 1 = concat.(0 + 1) by A1
             .= {<%>E} ^^ A by A1
             .= A by Th14;
end;

theorem Th27:
A |^ 2 = A ^^ A
proof
  thus A |^ 2 = A |^ (1 + 1)
             .= (A |^ 1) ^^ A by Th24
             .= A ^^ A  by Th26;
end;

theorem Th28:
A |^ n = {} iff n > 0 & A = {}
proof
  thus A |^ n = {} implies n > 0 & A = {}
  proof
    assume that
A1:   A |^ n = {} and
A2:   n <= 0 or A <> {};
A3:  now
      assume
A4:     A <> {};
      defpred P[Nat] means A |^ $1 <> {};
A5:    P[0]
      proof
         A |^ 0 = {<%>E} by Th25;
        hence thesis;
      end;
A6:    now
        let n;
        assume
       P[n];
        then consider a1 such that
A7:       a1 in A |^ n by SUBSET_1:10;
        consider a2 such that
A8:       a2 in A by A4,SUBSET_1:10;
         a1 ^ a2 in A |^ n ^^ A by A7,A8,Def1;
        hence P[n + 1] by Th24;
      end;
       for n holds P[n] from NAT_1:sch 2(A5, A6);
      hence contradiction by A1;
    end;
     now
      assume n <= 0;
      then n = 0;
      then A |^ n = {<%>E} by Th25;
      hence contradiction by A1;
    end;
    hence thesis by A2,A3;
  end;
  assume
A9: n > 0 & A = {};
  then A10: n >= 0 + 1 by NAT_1:13;
  defpred P[Nat] means A |^ $1 = {};
A11:
  P[1] by A9,Th26;
A12:
  now
    let m be Nat;
    assume 1 <= m & P[m];
     {}(E^omega) |^ (m + 1) = {}(E^omega) |^ m ^^ {}(E^omega) by Th24
                          .= {} by Th13;
    hence P[m + 1] by A9;
  end;
A13:
  for m being Nat st m >= 1 holds P[m] from NAT_1:sch 8(A11, A12);
   for n st n >= 1 holds P[n]
  proof
    let n;
    reconsider m = n as Element of NAT by ORDINAL1:def 13;
     m >= 1 implies P[m] by A13;
    hence thesis;
  end;
  hence thesis by A10;
end;

theorem Th29:
{<%>E} |^ n = {<%>E}
proof
  defpred P[Nat] means {<%>E} |^ $1 = {<%>E};
A1:
  P[0] by Th25;
A2:
  now
    let n;
    assume
A3:   P[n];
     {<%>E} |^ (n + 1) = ({<%>E} |^ n) ^^ {<%>E} by Th24
                     .= {<%>E} by A3,Th14;
    hence P[n + 1];
  end;
   for n holds P[n] from NAT_1:sch 2(A1, A2);
  hence thesis;
end;

theorem
 A |^ n = {<%>E} iff n = 0 or A = {<%>E}
proof
  thus A |^ n = {<%>E} implies n = 0 or A = {<%>E}
  proof
    assume
A1:    A |^ n = {<%>E};
     now
      assume n > 0;
      then consider m such that
A2:      m + 1 = n by NAT_1:6;
       A |^ n = (A |^ m) ^^ A by A2,Th24;
      hence A = {<%>E} by A1,Th15;
    end;
    hence thesis;
  end;
  assume n = 0 or A = {<%>E};
  hence thesis by Th25,Th29;
end;

theorem Th31:
<%>E in A implies <%>E in A |^ n
proof
  assume
A1:  <%>E in A;
  defpred P[Nat] means <%>E in A |^ $1;
A2:
  P[0]
  proof
     A |^ 0 = {<%>E} by Th25;
    hence thesis by TARSKI:def 1;
  end;
A3:
  now
    let n;
    assume P[n];
    then <%>E in (A |^ n) ^^ A by A1,Th16;
    hence P[n + 1] by Th24;
  end;
   for n holds P[n] from NAT_1:sch 2(A2, A3);
  hence thesis;
end;

theorem
 <%>E in A |^ n & n > 0 implies <%>E in A
proof
  assume
A1:  <%>E in A |^ n & n > 0;
  then consider m such that
A2:  m + 1 = n by NAT_1:6;
   A |^ n = (A |^ m) ^^ A by A2,Th24;
  then consider a1, a2 such that
A3:  a1 in A |^ m & a2 in A & a1 ^ a2 = <%>E by A1,Def1;
  thus thesis by A3,AFINSQ_1:33;
end;

theorem Th33:
(A |^ n) ^^ A = A ^^ (A |^ n)
proof
  defpred P[Nat] means (A |^ $1) ^^ A = A ^^ (A |^ $1);
A1:
  P[0]
  proof
    thus (A |^ 0) ^^ A = {<%>E} ^^ A by Th25
                      .= A by Th14
                      .= A ^^ {<%>E} by Th14
                      .= A ^^ (A |^ 0) by Th25;
  end;
A2:
  now
    let n;
    assume
A3:   P[n];
     (A |^ (n + 1)) ^^ A = ((A |^ n) ^^ A) ^^ A by Th24
                       .= A ^^ ((A |^ n) ^^ A) by A3,Th19
                       .= A ^^ (A |^ (n + 1)) by Th24;
    hence P[n + 1];
  end;
   for n holds P[n] from NAT_1:sch 2(A1, A2);
  hence thesis;
end;

theorem Th34:
A |^ (m + n) = (A |^ m) ^^ (A |^ n)
proof
  defpred P[Nat] means
    for m, n st m + n <= $1 holds A |^ (m + n) = (A |^ m) ^^ (A |^ n);
A1:
  P[0]
  proof
    let m, n;
    assume m + n <= 0;
    then A2:   m + n = 0;
    then A3:   m = 0 & n = 0 by NAT_1:7;
    thus A |^ (m + n) = (A |^ 0) ^^ {<%>E} by A2,Th14
                     .= (A |^ m) ^^ (A |^ n) by A3,Th25;
  end;
A4:
  now
    let l;
    assume
A5:   P[l];
     now
      let m, n be Nat;
      assume
A6:     m + n <= l + 1;
A7:   now
        assume
A8:     m + n = l + 1;
A9:   now
          assume
A10:      m = 0;
          thus A |^ (m + n) = {<%>E} ^^ (A |^ (l + 1)) by A8,Th14
                           .= (A |^ m) ^^ (A |^ n) by A8,A10,Th25;
        end;
A11:   now
          assume
A12:      n = 0;
          thus A |^ (m + n) = (A |^ (l + 1)) ^^ {<%>E} by A8,Th14
                           .= (A |^ m) ^^ (A |^ n) by A8,A12,Th25;
        end;
   now
          assume
A13:     m > 0 & n > 0;
          then consider k such that
A14:     k + 1 = m by NAT_1:6;
A15:    k + 1 <= l by A8,A13,A14,Th2;
           A |^ (m + n) = A |^ (k + n + 1) by A14
                      .= (A |^ (k + n)) ^^ A by Th24
                      .= A ^^ (A |^ (k + n)) by Th33
                      .= A ^^ ((A |^ k) ^^ (A |^ n)) by A5,A8,A14
                      .= (A ^^ (A |^ k)) ^^ (A |^ n) by Th19
                      .= ((A |^ k) ^^ A) ^^ (A |^ n) by Th33
                      .= ((A |^ k) ^^ (A |^ 1)) ^^ (A |^ n) by Th26
                      .= (A |^ m) ^^ (A |^ n) by A5,A14,A15;
          hence A |^ (m + n) = (A |^ m) ^^ (A |^ n);
        end;
        hence A |^ (m + n) = (A |^ m) ^^ (A |^ n) by A9,A11;
      end;
   now
        assume m + n < l + 1;
        then m + n <= l by NAT_1:13;
        hence A |^ (m + n) = (A |^ m) ^^ (A |^ n) by A5;
      end;
      hence A |^ (m + n) = (A |^ m) ^^ (A |^ n) by A6,A7,REAL_1:def 5;
    end;
    hence P[l + 1];
  end;
   for l holds P[l] from NAT_1:sch 2(A1, A4);
  hence thesis;
end;

theorem
 (A |^ m) |^ n = A |^ (m * n)
proof
  defpred P[Nat] means
    for m, n st m + n <= $1 holds (A |^ m) |^ n = A |^ (m * n);
A1:
  P[0]
  proof
    let m, n;
    assume m + n <= 0;
    then m + n = 0;
    then A2:   m = 0 & n = 0 by NAT_1:7;
    hence (A |^ m) |^ n = {<%>E} by Th25
                       .= A |^ (m * n) by A2,Th25;
  end;
A3:
  now
    let l;
    assume
A4:   P[l];
     now
      let m, n;
      assume
A5:     m + n <= l + 1;
A6:   now
        assume
A7:     m + n = l + 1;
A8:   now
          assume
A9:      m = 0;
          hence (A |^ m) |^ n = {<%>E} |^ n by Th25
                            .= {<%>E} by Th29
                            .= A |^ (m * n) by A9,Th25;
        end;
A10:   now
          assume
A11:      n = 0;
          hence (A |^ m) |^ n = {<%>E} by Th25
                             .= A |^ (m * n) by A11,Th25;
        end;
   now
          assume m > 0 & n > 0;
          then consider k such that
A12:     k + 1 = n by NAT_1:6;
A13:    m + k <= l by A7,A12;
           (A |^ m) |^ n = ((A |^ m) |^ k) ^^ (A |^ m) by A12,Th24
                       .= (A |^ (m * k)) ^^ (A |^ m) by A4,A13
                       .= A |^ (m * k + m) by Th34
                       .= A |^ (m * n) by A12;
          hence (A |^ m) |^ n = A |^ (m * n);
        end;
        hence (A |^ m) |^ n = A |^ (m * n) by A8,A10;
      end;
   now
        assume m + n < l + 1;
        then m + n <= l by NAT_1:13;
        hence (A |^ m) |^ n = A |^ (m * n) by A4;
      end;
      hence (A |^ m) |^ n = A |^ (m * n) by A5,A6,REAL_1:def 5;
    end;
    hence P[l + 1];
  end;
A14:
  for l holds P[l] from NAT_1:sch 2(A1, A3);
   now
    let m, n;
    reconsider l = m + n as Nat;
     m + n <= l;
    hence (A |^ m) |^ n = A |^ (m * n) by A14;
  end;
  hence thesis;
end;

theorem Th36:
<%>E in A & n > 0 implies A c= A |^ n
proof
  assume that
A1: <%>E in A and
A2: n > 0;
  consider m such that
A3:  m + 1 = n by A2,NAT_1:6;
   <%>E in A |^ m by A1,Th31;
  then A c= A |^ m ^^ A by Th17;
  hence thesis by A3,Th24;
end;

theorem
 <%>E in A & m > n implies A |^ n c= A |^ m
proof
  assume that
A1: <%>E in A and
A2: m > n;
  consider k such that
A3:  n + k = m by A2,NAT_1:10;
   <%>E in A |^ k by A1,Th31;
  then A |^ n c= A |^ k ^^ A |^ n by Th17;
  hence thesis by A3,Th34;
end;

theorem Th38:
A c= B implies A |^ n c= B |^ n
proof
  assume
A1:  A c= B;
 defpred P[Nat] means A |^ $1 c= B |^ $1;
A2:
  P[0]
  proof
     A |^ 0 = {<%>E} & B |^ 0 = {<%>E} by Th25;
    hence thesis;
  end;
A3:
  now
    let n;
    assume
A4:   P[n];
     (A |^ n) ^^ A = A |^ (n + 1) & (B |^ n) ^^ B = B |^ (n + 1) by Th24;
    hence P[n + 1] by A1,A4,Th18;
  end;
   for n holds P[n] from NAT_1:sch 2(A2, A3);
  hence thesis;
end;

theorem
 (A |^ n) \/ (B |^ n) c= (A \/ B) |^ n
proof
   A c= A \/ B & B c= A \/ B by XBOOLE_1:7;
  then A |^ n c= (A \/ B) |^ n & B |^ n c= (A \/ B) |^ n by Th38;
  hence thesis by XBOOLE_1:8;
end;

theorem
 (A /\ B) |^ n c= (A |^ n) /\ (B |^ n)
proof
   A /\ B c= A & A /\ B c= B by XBOOLE_1:17;
  then (A /\ B) |^ n c= A |^ n & (A /\ B) |^ n c= B |^ n by Th38;
  hence thesis by XBOOLE_1:19;
end;

theorem Th41:
a in C |^ m & b in C |^ n implies a ^ b in C |^ (m + n)
proof
  assume a in C |^ m & b in C |^ n;
  then a ^ b in (C |^ m) ^^ (C |^ n) by Def1;
  hence thesis by Th34;
end;

begin

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: Kleene Closure
:: Definition of *
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

definition
  let E, A;
  func A* -> Subset of E^omega equals
     union { B : ex n st B = A |^ n };
  coherence
  proof
    defpred P[set] means ex n st $1 = A |^ n;
    reconsider M = { B : P[B] } as Subset-Family of E^omega
                           from DOMAIN_1:sch 7;
     union M is Subset of E^omega;
    hence thesis;
  end;
end;

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: Kleene Closure
:: Properties of *
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

theorem Th42:
x in A* iff ex n st x in A |^ n
proof
  thus x in A* implies ex n st x in A |^ n
  proof
    assume
    x in A*;
    then consider X such that
A1:   x in X and
A2:   X in { B : ex k st B = A |^ k } by TARSKI:def 4;
    defpred P[set] means ex k st $1 = A |^ k;
A3: X in { B : P[B] } by A2;
     P[X] from CARD_FIL:sch 1(A3);
    hence thesis by A1;
  end;
  given n such that
A4:  x in A |^ n;
  defpred P[set] means ex k st $1 = A |^ k;
  consider B such that
A5:  x in B & P[B] by A4;
  reconsider A = { C : P[C] } as Subset-Family of E^omega from DOMAIN_1:sch 7;
   B in A by A5;
  hence thesis by A5,TARSKI:def 4;
end;

theorem Th43:
A |^ n c= A*
proof
   (A |^ n) in { B : ex k st B = A |^ k };
  hence thesis by ZFMISC_1:92;
end;

theorem Th44:
A c= A*
proof
   A = A |^ 1 by Th26;
  hence thesis by Th43;
end;

theorem
 A ^^ A c= A*
proof
   A ^^ A = A |^ 2 by Th27;
  hence thesis by Th43;
end;

theorem Th46:
a in C* & b in C* implies a ^ b in C*
proof
  assume
A1:  a in C* & b in C*;
  then consider k such that
A2: a in C |^ k by Th42;
  consider l such that
A3: b in C |^ l by A1,Th42;
   a ^ b in C |^ (k + l) by A2,A3,Th41;
  hence thesis by Th42;
end;

theorem Th47:
A c= C* & B c= C* implies A ^^ B c= C*
proof
  assume
A1:  A c= C* & B c= C*;
  thus thesis
  proof
    let x;
    assume x in A ^^ B;
    then consider a, b such that
A2:    a in A & b in B & x = a ^ b by Def1;
    thus x in C* by A1,A2,Th46;
  end;
end;

theorem
 A* = {<%>E} iff A = {} or A = {<%>E}
proof
  thus A* = {<%>E} implies A = {} or A = {<%>E}
  proof
    assume that
A1:   A* = {<%>E} and
A2:   A <> {} & A <> {<%>E};
    consider x such that
A3:   x in A & x <> <%>E by A2,ZFMISC_1:41;
    reconsider a = x as Element of E^omega by A3;
     A c= A* by Th44;
    hence contradiction by A1,A3,TARSKI:def 1;
  end;
A4:
  now
    assume
A5:   A = {};
A6: now
      let x;
      assume x in A*;
      then consider n such that
A7:     x in A |^ n by Th42;
A8:   n = 0 implies x in {<%>E} by A7,Th25;
       n > 0 implies contradiction by A5,A7,Th28;
      hence x in {<%>E} by A8;
    end;
     now
      let x;
      assume x in {<%>E};
      then x in A |^ 0 by Th25;
      hence x in A* by Th42;
    end;
    hence A* = {<%>E} by A6,TARSKI:2;
  end;
   now
    assume
A9:   A = {<%>E};
A10: A* c= {<%>E}
    proof
      let x;
      assume x in A*;
      then consider n such that
A11:     x in A |^ n by Th42;
      thus x in {<%>E} by A9,A11,Th29;
    end;
     {<%>E} c= A*
    proof
      let x;
      assume x in {<%>E};
      then x in A |^ 0 by Th25;
      hence x in A* by Th42;
    end;
    hence A* = {<%>E} by A10,XBOOLE_0:def 10;
  end;
  hence thesis by A4;
end;

theorem Th49:
<%>E in A*
proof
   {<%>E} = A |^ 0 by Th25;
  then <%>E in A |^ 0 by TARSKI:def 1;
  hence thesis by Th42;
end;

theorem
 A* = {x} implies x = <%>E
proof
  assume
A1:  A* = {x} & x <> <%>E;
  then A2:  x in A* by ZFMISC_1:37;
  reconsider a = x as Element of E^omega by A1,ZFMISC_1:37;
A3:
  a ^ a in A* by A2,Th46;
   a ^ a <> x by A1,Th12;
  hence thesis by A1,A3,TARSKI:def 1;
end;

theorem Th51:
x in A |^ (m + 1) implies x in (A*) ^^ A & x in A ^^ (A*)
proof
  assume x in A |^ (m + 1);
  then A1:  x in (A |^ m) ^^ A by Th24;
  then consider a, b such that
A2:  a in A |^ m & b in A & x = a ^ b by Def1;
   a in A* & b in A by A2,Th42;
  hence x in (A*) ^^ A by A2,Def1;
   x in A ^^ (A |^ m) by A1,Th33;
  then consider a, b such that
A3:  a in A & b in A |^ m & x = a ^ b by Def1;
   a in A & b in A* by A3,Th42;
  hence x in A ^^ (A*) by A3,Def1;
end;

theorem
 A |^ (m + 1) c= (A*) ^^ A & A |^ (m + 1) c= A ^^ (A*)
proof
  thus A |^ (m + 1) c= (A*) ^^ A
  proof
    let x;
    assume x in A |^ (m + 1);
    hence x in (A*) ^^ A by Th51;
  end;
  let x;
  assume x in A |^ (m + 1);
  hence x in A ^^ (A*) by Th51;
end;

theorem Th53:
x in (A*) ^^ A or x in A ^^ (A*) implies x in A*
proof
A1:
  now
    let x;
    assume x in (A*) ^^ A;
    then consider a, b such that
A2:   a in A* & b in A & x = a ^ b by Def1;
    consider n such that
A3:   a in A |^ n by A2,Th42;
     b in A |^ 1 by A2,Th26;
    then a ^ b in A |^ (n + 1) by A3,Th41;
    hence x in A* by A2,Th42;
  end;

  now
    let x;
    assume x in A ^^ (A*);
    then consider a, b such that
A4:   a in A & b in A* & x = a ^ b by Def1;
    consider n such that
A5:   b in A |^ n by A4,Th42;
     a in A |^ 1 by A4,Th26;
    then a ^ b in A |^ (n + 1) by A5,Th41;
    hence x in A* by A4,Th42;
  end;
  hence thesis by A1;
end;

theorem
 (A*) ^^ A c= A* & A ^^ (A*) c= A*
proof
  thus (A*) ^^ A c= A*
  proof
    let x;
    assume x in (A*) ^^ A;
    hence x in A* by Th53;
  end;
  thus A ^^ (A*) c= A*
  proof
    let x;
    assume x in A^^ (A*);
    hence x in A* by Th53;
  end;
end;

theorem Th55:
<%>E in A implies A* = (A*) ^^ A & A* = A ^^ (A*)
proof
  assume
A1: <%>E in A;
A2:
  <%>E in A* by Th49;
A3:
  for x holds
    (x in (A*) ^^ A implies x in A*) &
    (x in A ^^ (A*) implies x in A*) by Th53;
   now
    let x;
    assume x in A*;
    then consider n such that
A4:   x in A |^ n by Th42;
A5: now
      assume n = 0;
      then x in {<%>E} by A4,Th25;
      then x = <%>E by TARSKI:def 1;
      hence x in (A*) ^^ A & x in A ^^ (A*) by A1,A2,Th16;
    end;
A6:
    now
      assume n > 0;
      then consider m such that
A7:      m + 1 = n by NAT_1:6;
      thus x in (A*) ^^ A by A4,A7,Th51;
    end;

    now
      assume n > 0;
      then consider m such that
A8:     m + 1 = n by NAT_1:6;
      thus x in A ^^ (A*) by A4,A8,Th51;
    end;
    hence x in (A*) ^^ A & x in A ^^ (A*) by A5,A6;
  end;
  then (x in A* implies x in (A*) ^^ A) &
       (x in A* implies x in A ^^ (A*));
  hence thesis by A3,TARSKI:2;
end;

theorem
 <%>E in A implies A* = (A*) ^^ (A |^ n) & A* = (A |^ n) ^^ (A*)
proof
  assume
A1: <%>E in A;
  defpred P[Nat] means A* = (A*) ^^ (A |^ $1) & A* = (A |^ $1) ^^ (A*);
A2:
  P[0]
  proof
    thus (A*) ^^ (A |^ 0) = (A*) ^^ {<%>E} by Th25
                         .= A* by Th14;
    thus (A |^ 0) ^^ (A*) = {<%>E} ^^ (A*) by Th25
                         .= A* by Th14;
  end;
A3:
  now
    let n;
    assume
A4:   P[n];
A5: (A*) ^^ (A |^ (n + 1)) = (A*) ^^ ((A |^ n) ^^ A) by Th24
                          .= (A*) ^^ A by A4,Th19
                          .= A* by A1,Th55;
 (A |^ (n + 1)) ^^ (A*) = ((A |^ n) ^^ A) ^^ (A*) by Th24
                          .= (A ^^ (A |^ n)) ^^ (A*) by Th33
                          .= A ^^ (A*) by A4,Th19
                          .= A* by A1,Th55;
    hence P[n + 1] by A5;
  end;
   for n holds P[n] from NAT_1:sch 2(A2, A3);
  hence thesis;
end;

theorem Th57:
A* = {<%>E} \/ A ^^ (A*) & A* = {<%>E} \/ (A*) ^^ A
proof
A1:
  now
    let x;
    assume x in A*;
    then consider n such that
A2:   x in A |^ n by Th42;
A3: n = 0 implies x in {<%>E} by A2,Th25;
     now
      assume n > 0;
      then consider m such that
A4:     m + 1 = n by NAT_1:6;
      thus x in (A*) ^^ A by A2,A4,Th51;
    end;
    hence x in {<%>E} \/ (A*) ^^ A by A3,XBOOLE_0:def 2;
  end;
A5:
  now
    let x;
    assume x in A*;
    then consider n such that
A6:   x in A |^ n by Th42;
A7: n = 0 implies x in {<%>E} by A6,Th25;
     now
      assume n > 0;
      then consider m such that
A8:     m + 1 = n by NAT_1:6;
      thus x in A ^^ (A*) by A6,A8,Th51;
    end;
    hence x in {<%>E} \/ A ^^ (A*) by A7,XBOOLE_0:def 2;
  end;
A9:
  now
    let x;
    assume x in {<%>E} \/ A ^^ (A*);
    then x in {<%>E} or x in A ^^ (A*) by XBOOLE_0:def 2;
    then x = <%>E or x in A* by Th53,TARSKI:def 1;
    hence x in A* by Th49;
  end;

  now
    let x;
    assume x in {<%>E} \/ (A*) ^^ A;
    then x in {<%>E} or x in (A*) ^^ A by XBOOLE_0:def 2;
    then x = <%>E or x in A* by Th53,TARSKI:def 1;
    hence x in A* by Th49;
  end;
  hence thesis by A1,A5,A9,TARSKI:2;
end;

theorem Th58:
A ^^ (A*) = (A*) ^^ A
proof
A1:
  A* = {<%>E} \/ A ^^ (A*) & A* = {<%>E} \/ (A*) ^^ A by Th57;
   now per cases;
    suppose <%>E in A;
      then A* = A ^^ (A*) & A* = (A*) ^^ A by Th55;
      hence A ^^ (A*) = (A*) ^^ A;
    end;
    suppose not <%>E in A;
      then not <%>E in A ^^ (A*) & not <%>E in (A*) ^^ A by Th16;
      then {<%>E} misses A ^^ (A*) & {<%>E} misses (A*) ^^ A by ZFMISC_1:56;
      hence A ^^ (A*) = (A*) ^^ A by A1,XBOOLE_1:71;
    end;
  end;
  hence thesis;
end;

theorem
 (A |^ n) ^^ (A*) = (A*) ^^ (A |^ n)
proof
  defpred P[Nat] means (A |^ $1) ^^ (A*) = (A*) ^^ (A |^ $1);
A1:
  P[0]
  proof
    thus (A |^ 0) ^^ (A*) = {<%>E} ^^ (A*) by Th25
                         .= A* by Th14
                         .= (A*) ^^ {<%>E} by Th14
                         .= (A*) ^^ (A |^ 0) by Th25;
  end;
A2:
  now
    let n;
    assume
A3:   P[n];
     (A |^ (n + 1)) ^^ (A*) = (A |^ n ^^ A) ^^ (A*) by Th24
                          .= A |^ n ^^ (A ^^ (A*)) by Th19
                          .= A |^ n ^^ ((A*) ^^ A) by Th58
                          .= ((A*) ^^ A |^ n) ^^ A by A3,Th19
                          .= (A*) ^^ (A |^ n ^^ A) by Th19
                          .= (A*) ^^ A |^ (n + 1) by Th24;
    hence P[n + 1];
  end;
   for n holds P[n] from NAT_1:sch 2(A1, A2);
  hence thesis;
end;

theorem Th60:
A c= B* implies A |^ n c= B*
proof
  assume
A1:  A c= B*;
  defpred P[Nat] means A |^ $1 c= B*;
A2:
  P[0]
  proof
     <%>E in B* by Th49;
    then {<%>E} c= B* by ZFMISC_1:37;
    hence thesis by Th25;
  end;
A3:
  now
    let n;
    assume P[n];
    then (A |^ n) ^^ A c= B*  by A1,Th47;
    hence P[n + 1] by Th24;
  end;
   for n holds P[n] from NAT_1:sch 2(A2, A3);
  hence thesis;
end;

theorem Th61:
A c= B* implies A* c= B*
proof
  assume
A1:  A c= B*;
  thus thesis
  proof
    let x;
    assume
   x in A*;
    then consider n such that
A2:   x in A |^ n by Th42;
     A |^ n c= B* by A1,Th60;
    hence x in B* by A2;
  end;
end;

theorem Th62:
A c= B implies A* c= B*
proof
  assume
A1:  A c= B;
   B c= B* by Th44;
  then A c= B* by A1,XBOOLE_1:1;
  hence thesis by Th61;
end;

theorem Th63:
(A*)* = A*
proof
A1:
  (A*)* c= A* by Th61;
   A* c= (A*)* by Th44;
  hence thesis by A1,XBOOLE_0:def 10;
end;

theorem
 (A*) ^^ (A*) = A*
proof
A1:
  (A*) ^^ (A*) c= A* by Th47;
   <%>E in A* by Th49;
  then A* c= (A*) ^^ (A*) by Th17;
  hence thesis by A1,XBOOLE_0:def 10;
end;

theorem
 (A |^ n)* c= A*
proof
   A |^ n c= A* by Th43;
  hence thesis by Th61;
end;

theorem Th66:
(A*) |^ n c= A*
proof
   (A*) |^ n c= (A*)* by Th43;
  hence thesis by Th63;
end;

theorem
 n > 0 implies (A*) |^ n = A*
proof
   <%>E in A* by Th49;
  then A1:  n > 0 implies A* c= (A*) |^ n by Th36;
   (A*) |^ n c= A* by Th66;
  hence thesis by A1,XBOOLE_0:def 10;
end;

theorem Th68:
A c= B* implies B* = (B \/ A)*
proof
  assume
A1:  A c= B*;
  defpred P[Nat] means (B \/ A) |^ $1 c= B*;
A2:
  P[0]
  proof
A3: (B \/ A) |^ 0 = {<%>E} by Th25;
     <%>E in B* by Th49;
    hence thesis by A3,ZFMISC_1:37;
  end;
A4:
  now
    let n;
    assume
A5:   P[n];
A6: (B \/ A) |^ (n + 1) = (B \/ A) |^ n ^^ (B \/ A) by Th24;
     B c= B* by Th44;
    then B \/ A c= B* by A1,XBOOLE_1:8;
    hence P[n + 1] by A5,A6,Th47;
  end;
A7:
  for n holds P[n] from NAT_1:sch 2(A2, A4);
A8:
  (B \/ A)* c= B*
  proof
    let x;
    assume x in (B \/ A)*;
    then consider n such that
A9:   x in (B \/ A) |^ n by Th42;
     (B \/ A) |^ n c= B* by A7;
    hence x in B* by A9;
  end;
   B c= B \/ A by XBOOLE_1:7;
  then B* c= (B \/ A)* by Th62;
  hence thesis by A8,XBOOLE_0:def 10;
end;

theorem Th69:
a in A* implies A* = (A \/ {a})*
proof
  assume a in A*;
  then {a} c= A* by ZFMISC_1:37;
  hence thesis by Th68;
end;

theorem
 A* = (A \ {<%>E})*
proof
A1:
  <%>E in A* by Th49;
   <%>E in (A \ {<%>E})* by Th49;
  hence (A \ {<%>E})* = ((A \ {<%>E}) \/ {<%>E})* by Th69
                     .= (A \/ {<%>E})* by XBOOLE_1:39
                     .= A* by A1,Th69;
end;

theorem
 (A*) \/ (B*) c= (A \/ B)*
proof
   A c= A \/ B & B c= A \/ B by XBOOLE_1:7;
  then A* c= (A \/ B)* & B* c= (A \/ B)* by Th62;
  hence thesis by XBOOLE_1:8;
end;

theorem
 (A /\ B)* c= (A*) /\ (B*)
proof
   A /\ B c= A & A /\ B c= B by XBOOLE_1:17;
  then (A /\ B)* c= A* & (A /\ B)* c= B* by Th62;
  hence thesis by XBOOLE_1:19;
end;

theorem Th73:
<%x%> in A* iff <%x%> in A
proof
  thus <%x%> in A* implies <%x%> in A
  proof
    assume
A1:    <%x%> in A* & not <%x%> in A;
    defpred P[Nat] means <%x%> in A |^ $1;
    consider n such that
A2:   P[n] by A1,Th42;
    reconsider m = n as Element of NAT by ORDINAL1:def 13;
A3: ex i being Nat st P[i]
    proof
      take m;
      thus thesis by A2;
    end;
     ex n1 being Nat st P[n1] &
      for n2 being Nat st P[n2] holds n1 <= n2 from NAT_1:sch 5(A3);
    then consider n1 being Nat such that
A4:   P[n1] and
A5:   for n2 being Nat st P[n2] holds n1 <= n2;
A6:  now
      assume n1 > 1;
      then consider n2 such that
A7:     n2 + 1 = n1 by NAT_1:6;
       <%x%> in (A |^ n2) ^^ A by A4,A7,Th24;
      then consider a, b such that
A8:      a in (A |^ n2) & b in A & <%x%> = a ^ b by Def1;
       now
        assume
A9:        (a = <%x%> & b = <%>E);
        reconsider n2 as Element of NAT by ORDINAL1:def 13;
         ex i being Element of NAT st P[i] & n1 > i
        proof
          take n2;
          thus thesis by A7,A8,A9,NAT_1:13;
        end;
        hence contradiction by A5;
      end;
      hence <%x%> in A by A8,Th4;
    end;
A10:  n1 = 1 implies <%x%> in A by A4,Th26;
     now
      assume n1 = 0;
      then <%x%> in {<%>E} by A4,Th25;
      hence contradiction by TARSKI:def 1;
    end;
    hence contradiction by A1,A6,A10,NAT_1:26;
  end;
  assume <%x%> in A;
  then <%x%> in A |^ 1 by Th26;
  hence thesis by Th42;
end;

begin

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: Lexemes as a Subset of E^omega
:: Definition of Lex
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

definition
  let E;
  func Lex(E) -> Subset of E^omega means :Def4:
    x in it iff ex e st e in E & x = <%e%>;
  existence
  proof
    defpred P[set] means ex e st e in E & $1 = <%e%>;
    consider C such that
A1:    x in C iff x in E^omega & P[x] from SUBSET_1:sch 1;
    take C;
     x in C iff ex e st e in E & x = <%e%>
    proof
      thus x in C implies ex e st e in E & x = <%e%> by A1;
      given e such that
A2:      e in E & x = <%e%>;
       {e} c= E by A2,ZFMISC_1:37;
      then rng <%e%> c= E by AFINSQ_1:36;
      then <%e%> is XFinSequence of E by ORDINAL1:def 8;
      then <%e%> is Element of E^omega by AFINSQ_1:def 8;
      hence thesis by A1,A2;
    end;
    hence thesis;
  end;
  uniqueness
  proof
    let C1, C2 be Subset of E^omega such that
A3:   x in C1 iff ex e st e in E & x = <%e%> and
A4:   x in C2 iff ex e st e in E & x = <%e%>;
     now
      let x;
      thus x in C1 implies x in C2
      proof
        assume x in C1;
        then ex e st e in E & x = <%e%> by A3;
        hence x in C2 by A4;
      end;
      assume x in C2;
      then ex e st e in E & x = <%e%> by A4;
      hence x in C1 by A3;
    end;
    hence thesis by TARSKI:2;
  end;
end;

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: Lexemes as a Subset of E^omega
:: Properties of Lex
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

theorem Th74:
a in Lex(E) |^ len a
proof
  defpred P[Nat] means
    for a holds len a = $1 implies a in Lex(E) |^ $1;
A1:
  P[0]
  proof
    let a;
    assume len a = 0;
    then a = <%>E by AFINSQ_1:18;
    then a in {<%>E} by TARSKI:def 1;
    hence thesis by Th25;
  end;
A2:
  now
    let n;
    assume
A3:   P[n];
     now
      let b;
      assume len b = n + 1;
      then consider c, e such that
A4:     len c = n & b = c ^ <%e%> by Th7;
A5:   c in Lex(E) |^ n by A3,A4;
       <%e%> is Element of E^omega by A4,Th5;
      then e in E by Th6;
      then <%e%> in Lex(E) by Def4;
      then <%e%> in Lex(E) |^ 1 by Th26;
      hence b in Lex(E) |^ (n + 1) by A4,A5,Th41;
    end;
    hence P[n + 1];
  end;
   for n holds P[n] from NAT_1:sch 2(A1, A2);
  hence thesis;
end;

theorem
 Lex(E)* = E^omega
proof
A1:
  now
    let x;
    assume x in E^omega;
    then reconsider a = x as Element of E^omega;
     a in Lex(E) |^ len a by Th74;
    hence x in Lex(E)* by Th42;
  end;

  for x st x in Lex(E)* holds x in E^omega;
  hence thesis by A1,TARSKI:2;
end;

theorem
 A* = E^omega implies Lex(E) c= A
proof
  assume
A1:  A* = E^omega;
  thus thesis
  proof
    let x;
    assume
A2:   x in Lex(E);
    then consider e such that
A3:   e in E & x = <%e%> by Def4;
    thus x in A by A1,A2,A3,Th73;
  end;
end;
