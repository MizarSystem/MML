:: Real Exponents and Logarithms
::  by Konrad Raczkowski and Andrzej N\c{e}dzusiak
::
:: Received October 1, 1990
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies NUMBERS, XREAL_0, ORDINAL1, SUBSET_1, INT_1, RAT_1, RELAT_1,
      ARYTM_1, NEWTON, ARYTM_3, CARD_1, PREPOWER, FUNCT_1, XXREAL_0, NAT_1,
      REAL_1, SEQ_1, SEQ_2, ORDINAL2, SQUARE_1, XXREAL_2, SEQ_4, COMPLEX1,
      VALUED_0, POWER, ABIAN;
 notations SUBSET_1, NEWTON, ORDINAL1, XCMPLX_0, XREAL_0, XXREAL_0, COMPLEX1,
      INT_1, RAT_1, REAL_1, NUMBERS, NAT_1, SEQ_1, SEQ_2, SEQM_3, SQUARE_1,
      SEQ_4, PREPOWER, ABIAN;
 constructors REAL_1, SQUARE_1, NAT_1, SEQ_2, SEQM_3, SEQ_4, NEWTON, PREPOWER,
      SEQ_1, XXREAL_2, RELSET_1, ABIAN;
 registrations NUMBERS, XXREAL_0, XREAL_0, SQUARE_1, NAT_1, INT_1, RAT_1,
      MEMBERED, NEWTON, PREPOWER, XBOOLE_0, SEQ_4, ABIAN;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions SQUARE_1, XCMPLX_0, INT_1, ABIAN;
 theorems ABSVALUE, NAT_1, SEQ_2, SEQM_3, INT_1, SEQ_4, NEWTON, PREPOWER,
      TARSKI, FUNCT_2, XREAL_0, XCMPLX_1, XREAL_1, XXREAL_0, ABIAN;
 schemes SEQ_1;

begin

reserve x for set;
reserve a, b, c, d, e for real number;
reserve m, n, m1, m2 for Element of NAT;
reserve k, l for Integer;
reserve p for Rational;

Lm1:
    now
      let n be Nat;
      assume n is odd;
      then ex m st n=2*m+1 by ABIAN:9;
      then n>=0+1 by XREAL_1:8;
      hence 1 <= n;
    end;
    
theorem Th1:
  n is even implies (-a) |^ n = a |^ n
proof
  given m such that
A1: n=2*m;
  thus
  (-a) |^ n = ((-a) |^ (1+1)) |^ m by A1,NEWTON:14
    .= ((-a) |^ (0+1) * (-a) |^ (0+1)) |^ m by NEWTON:13
    .= ((-a) |^ 0 * (-a) * (-a) |^ (0+1)) |^ m by NEWTON:11
    .= ((-a) |^ 0 * (-a) * ((-a) |^ 0*(-a))) |^ m by NEWTON:11
    .= ((-a) GeoSeq.0 * (-a) * ((-a) |^ 0 * (-a))) |^ m by PREPOWER:def 1
    .= ((-a) GeoSeq.0 * (-a) * ((-a) GeoSeq.0 * (-a))) |^ m by PREPOWER:def 1
    .= ((-a) GeoSeq.0 * (-a) * (1 * (-a))) |^ m by PREPOWER:4
    .= (1*(-a) * (1*(-a))) |^ m by PREPOWER:4
    .= (a * a) |^ m
    .= ((a GeoSeq.0*a) * (1*a)) |^ m by PREPOWER:4
    .= ((a GeoSeq.0*a) * (a GeoSeq.0*a)) |^ m by PREPOWER:4
    .= ((a GeoSeq.0*a) * (a |^ 0*a)) |^ m by PREPOWER:def 1
    .= ((a |^ 0*a) * (a |^ 0*a)) |^ m by PREPOWER:def 1
    .= ((a |^ 0*a) * a |^ (0+1)) |^ m by NEWTON:11
    .= (a |^ (0+1) * a |^ (0+1)) |^ m by NEWTON:11
    .= (a |^ (1+1)) |^ m by NEWTON:13
    .= a |^ n by A1,NEWTON:14;
end;

theorem Th2:
  n is odd implies (-a) |^ n = - a |^ n
proof
  assume n is odd;
  then consider m such that
A1: n=2*m+1 by ABIAN:9;
  thus
  (-a) |^ n = (-a) |^ (2*m) * (-a) by A1,NEWTON:11
    .= a |^ (2*m) * (-a) by Th1
    .= -a |^ (2*m) * a
    .= -a |^ n by A1,NEWTON:11;
end;

theorem Th3:
  a>=0 or n is even implies a |^ n >= 0
proof
  assume
A1: a>=0 or n is even;
A2: now
    let a,n such that
A3: a>=0;
    now per cases by A3;
      suppose a>0;
        hence a |^ n >= 0 by PREPOWER:13;
      end;
      suppose
A6:     a=0;
        now per cases by NAT_1:6;
          suppose
A8:         n=0;
            a |^ n = a GeoSeq.n by PREPOWER:def 1
              .= 1 by A8,PREPOWER:4;
            hence a |^ n >= 0;
          end;
          suppose
A10:        ex m being Nat st n = m+1;
            consider m being Nat such that
A11:        n = m + 1 by A10;
            a |^ n = a |^ m * a by A11,NEWTON:11
              .= 0 by A6;
            hence a |^ n >= 0;
          end;
        end;
        hence a |^ n >= 0;
      end;
    end;
    hence a |^ n >= 0;
  end;
  now
    assume
A14: n is even;
     now per cases;
      suppose a>=0;
        hence thesis by A2;
      end;
      suppose a<0;
        then (-a) |^ n >= 0 by A2;
        hence thesis by A14,Th1;
      end;
    end;
    hence thesis;
  end;
  hence thesis by A1,A2;
end;

definition
  let n be Nat;
  let a be real number;
  func n-root a -> real number equals
  :Def1:
  n -Root a if a>=0 & n>=1,
  -n -Root (-a) if a<0 & n is odd;
  consistency;
  coherence;
end;

definition
  let n be Nat;
  let a be Real;
  redefine func n-root a -> Real;
  coherence by XREAL_0:def 1;
end;

canceled;

theorem
  for n being Nat st n>=1 & a>=0 or n is odd holds
  (n-root a) |^ n = a & n-root (a |^ n) = a
proof
  let n be Nat;
  assume
A1: n>=1 & a>=0 or n is odd;
A2: now
    assume that
A3: n>=1 and
A4: a>=0;
A5: n-root a = n -Root a by A3,A4,Def1;
A6: now per cases by A4;
      suppose a>0;
        hence a |^ n >= 0 by PREPOWER:13;
      end;
      suppose a=0;
        hence a |^ n >= 0;
      end;
    end;
    n-root (a |^ n) = n -Root (a |^ n) by A3,A6,Def1;
    hence thesis by A3,A4,A5,PREPOWER:28;
  end;
A10: now
    assume
B1: n is odd;
    then
A11: ex m st n=2*m+1 by ABIAN:9;
A12: n>=1 by B1,Lm1;
A13: now per cases;
      suppose a>=0;
        hence thesis by A2,A12;
      end;
      suppose
A15:    a<0;
A16:    -a>0 by A15,XREAL_1:60;
        thus
        (n-root a) |^ n = (- n -Root (-a)) |^ n by A11,A15,Def1
          .= -(n -Root (-a)) |^ n by A11,Th2
          .= -(-a) by A12,A15,PREPOWER:28
          .= a;
A17:    (-a) |^ n > 0 by A16,PREPOWER:13;
A18:    -a |^ n > 0 by A11,A17,Th2;
A19:    a |^ n < 0 by A18;
        thus n-root (a |^ n) = - n -Root (-a |^ n) by A11,A19,Def1
          .= - n -Root ((-a) |^ n) by A11,Th2
          .= -(-a) by A12,A15,PREPOWER:28
          .= a;
      end;
    end;
    thus thesis by A13;
  end;
  thus thesis by A1,A2,A10;
end;

theorem Th6:
  for n being Nat st n>=1 holds n-root 0 = 0
proof
  let n be Nat;
  assume
A1: n>=1;
  thus n-root 0 = n -Root 0 by A1,Def1
    .= 0 by A1,PREPOWER:def 3;
end;

theorem
  n>=1 implies n-root 1 = 1
proof
  assume
A1: n>=1;
  thus n-root 1 = n -Root 1 by A1,Def1
    .= 1 by A1,PREPOWER:29;
end;

theorem Th8:
  a>=0 & n>=1 implies n-root a >= 0
proof
  assume that
A1: a>=0 and
A2: n>=1;
A3: now per cases by A1;
    suppose
A4:   a>0;
A5:   n-root a = n -Root a by A1,A2,Def1;
      thus thesis by A2,A4,A5,PREPOWER:def 3;
    end;
    suppose
A6:   a=0;
A7:   n-root a = n -Root a by A1,A2,Def1
        .= 0 by A2,A6,PREPOWER:def 3;
      thus thesis by A7;
    end;
  end;
  thus thesis by A3;
end;

theorem
  n is odd implies n-root (-1) = -1
proof
  assume
A1: n is odd;
   then
A2: n>=1 by Lm1;
  thus
  n-root (-1) = - n -Root (-(-1)) by A1,Def1
    .= - 1 by A2,PREPOWER:29;
end;

theorem
  1-root a = a
proof
A1: now per cases;
    suppose
A2:   a>=0;
      thus 1-root a = 1 -Root a by A2,Def1
        .= a by A2,PREPOWER:30;
    end;
    suppose
A3:   a<0;
A4:   1 = 2*0 + 1;
      thus 1-root a = - 1 -Root (-a) by A3,A4,Def1
        .= -(-a) by A3,PREPOWER:30
        .= a;
    end;
  end;
  thus thesis by A1;
end;

theorem Th11:
  n is odd implies n-root a = - n-root (-a)
proof
  assume
A0: n is odd;
    then
A1: ex m st n = 2*m + 1 by ABIAN:9;
A2: n>=1 by A0,Lm1;
A3: now per cases;
    suppose
A4:   a<0;
      thus
      n-root a = - n -Root (-a) by A1,A4,Def1
        .= - n-root (-a) by A2,A4,Def1;
    end;
    suppose
A5:   a=0;
      thus n-root a = 0 by A2,A5,Th6
        .= - n-root (-a) by A2,A5,Th6;
    end;
    suppose
A6:   a>0;
A7:   -a<-0 by A6,XREAL_1:26;
      thus - n-root (-a) = -(- n -Root (-(-a))) by A1,A7,Def1
        .= n-root a by A2,A6,Def1;
    end;
  end;
  thus thesis by A3;
end;

theorem Th12:
  n>=1 & a>=0 & b>=0 or n is odd implies
  n-root (a*b) = n-root a * n-root b
proof
  assume
A1: n>=1 & a>=0 & b>=0 or n is odd;
A2: now
    let a,b,n;
    assume that
A3: n>=1 and
A4: a>=0 and
A5: b>=0;
    thus n-root (a*b) = n -Root (a*b) by A3,A4,A5,Def1
      .= n -Root a * n -Root b by A3,A4,A5,PREPOWER:31
      .= n-root a * n -Root b by A3,A4,Def1
      .= n-root a * n-root b by A3,A5,Def1;
  end;
A6: now
    assume
A7: n is odd;
    then
A8: n>=1 by Lm1;
A9: now per cases;
      suppose
A10:    a>=0 & b>=0;
        thus thesis by A2,A8,A10;
      end;
      suppose
A11:    a<0 & b<0;
        thus
        n-root (a*b) = n -Root ((-a)*(-b)) by A8,A11,Def1
          .= (-(-n -Root (-a))) * n -Root (-b) by A8,A11,PREPOWER:31
          .= (-(n-root a)) * (-(-n -Root (-b))) by A7,A11,Def1
          .= (-(n-root a)) * (-(n-root b)) by A7,A11,Def1
          .= n-root a * n-root b;
      end;
      suppose
A12:    a>=0 & b<0;
        thus
        n-root (a*b) = -n-root (-a*b) by A7,Th11
          .= -n-root (a*(-b))
          .= -(n-root a * n-root (-b)) by A2,A8,A12
          .= n-root a * (-n-root (-b))
          .= n-root a * n-root b by A7,Th11;
      end;
      suppose
A13:    a<0 & b>=0;
        thus
        n-root (a*b) = -n-root (-a*b) by A7,Th11
          .= -n-root ((-a)*b)
          .= -(n-root (-a) * n-root b) by A2,A8,A13
          .= (-n-root (-a)) * n-root b
          .= n-root a * n-root b by A7,Th11;
      end;
    end;
    thus thesis by A9;
  end;
  thus thesis by A1,A2,A6;
end;

theorem Th13:
  a>0 & n>=1 or a<>0 & n is odd implies n-root (1/a) = 1/(n-root a)
proof
  assume
A1: a>0 & n>=1 or a<>0 & n is odd;
A2: now
    let a,n;
    assume
A3: a>0 & n>=1;
    thus n-root (1/a) = n -Root (1/a) by A3,Def1
      .= 1/(n -Root a) by A3,PREPOWER:32
      .= 1/(n-root a) by A3,Def1;
  end;
A4: now
    assume that
A5: a<>0 and
A6: n is odd;
A7: n>=1 by A6,Lm1;
A8: now per cases by A5;
      suppose
A9:     a>0;
        thus thesis by A2,A7,A9;
      end;
      suppose
A10:    a<0;
A11:    -a>0 by A10,XREAL_1:60;
        thus
        1/(n-root a) = 1/(-(n-root (-a))) by A6,Th11
          .= - 1/(n-root (-a)) by XCMPLX_1:189
          .= - n-root (1/(-a)) by A2,A7,A11
          .= - n-root (-1/a) by XCMPLX_1:189
          .= n-root (1/a) by A6,Th11;
      end;
    end;
    thus thesis by A8;
  end;
  thus thesis by A1,A2,A4;
end;

theorem
  a>=0 & b>0 & n>=1 or b<>0 & n is odd implies
  n-root (a/b) = n-root a / n-root b
proof
  assume
A1: a>=0 & b>0 & n>=1 or b<>0 & n is odd;
A2: now per cases by A1;
    suppose
A3:   a>=0 & b>0 & n>=1;
      thus n-root (a/b) = n -Root (a/b) by A3,Def1
        .= n -Root a / n -Root b by A3,PREPOWER:33
        .= n-root a / n -Root b by A3,Def1
        .= n-root a / n-root b by A3,Def1;
    end;
    suppose
A4:   b<>0 & n is odd;
      thus n-root (a/b) = n-root (a*(1/b))
        .= n-root a * n-root (1/b) by A4,Th12
        .= n-root a * (1 / n-root b) by A4,Th13
        .= n-root a / n-root b;
    end;
  end;
  thus thesis by A2;
end;

theorem
  a>=0 & n>=1 & m>=1 or n is odd & m is odd implies
  n-root (m-root a) = (n*m)-root a
proof
  assume
A1: a>=0 & n>=1 & m>=1 or n is odd & m is odd;
A2: now
    let a,n,m;
    assume that
A3: a>=0 and
A4: n>=1 and
A5: m>=1;
A6: n*m>= 1 by A4,A5,XREAL_1:161;
A7: m-root a >= 0 by A3,A5,Th8;
A8: m -Root a >= 0 by A3,A5,A7,Def1;
    thus
    n-root (m-root a) = n-root (m -Root a) by A3,A5,Def1
      .= n -Root (m -Root a) by A4,A8,Def1
      .= (n*m) -Root a by A3,A4,A5,PREPOWER:34
      .= (n*m)-root a by A3,A6,Def1;
  end;
A9: now
    assume n is odd;
    then consider m1 such that
A10: n=2*m1+1 by ABIAN:9;
    assume m is odd;
    then consider m2 such that
A11: m=2*m2+1 by ABIAN:9;
A12: n>=0+1 by A10,XREAL_1:8;
A13: m>=0+1 by A11,XREAL_1:8;
A14: n*m >= 1 by A12,A13,XREAL_1:161;
A15: n*m = 2*(m1*(2*m2)+m1+m2) + 1 by A10,A11;
A16: now per cases;
      suppose
A17:    a>=0;
        thus thesis by A2,A12,A13,A17;
      end;
      suppose
A18:    a<0;
A19:    m-root (-a) >= 0 by A13,A18,Th8;
A20:    m -Root (-a) >= 0 by A13,A18,A19,Def1;
        thus
        n-root (m-root a) = n-root (-m -Root (-a)) by A11,A18,Def1
          .= -n-root (-(-m -Root (-a))) by A10,Th11
          .= -n -Root (m -Root (-a)) by A12,A20,Def1
          .= -(n*m) -Root (-a) by A12,A13,A18,PREPOWER:34
          .= -(n*m)-root (-a) by A14,A18,Def1
          .= (n*m)-root a by A15,Th11;
      end;
    end;
    thus thesis by A16;
  end;
  thus thesis by A1,A2,A9;
end;

theorem
  a>=0 & n>=1 & m>=1 or n is odd & m is odd implies
  n-root a * m-root a = (n*m)-root (a |^ (n+m))
proof
  assume
A1: a>=0 & n>=1 & m>=1 or n is odd & m is odd;
A2: now
    let a,n,m;
    assume that
A3: a>=0 and
A4: n>=1 and
A5: m>=1;
A6: n*m>= 1 & a |^ (n+m) >=0 by A3,A4,A5,Th3,XREAL_1:161;
    thus
    n-root a * m-root a = n-root a* m -Root a by A3,A5,Def1
      .= n -Root a * m -Root a by A3,A4,Def1
      .= (n*m) -Root (a |^ (n+m)) by A3,A4,A5,PREPOWER:35
      .= (n*m)-root (a |^ (n+m)) by A6,Def1;
  end;
A7: now
    assume n is odd;
    then consider m1 such that
A8: n=2*m1+1 by ABIAN:9;
    assume m is odd;
    then consider m2 such that
A9: m=2*m2+1 by ABIAN:9;
A10: n>=0+1 & m>=0+1 by A8,A9,XREAL_1:8;
A11: n*m >= 1 by A10,XREAL_1:161;
A12: n+m = 2*(m1+(1+m2)) by A8,A9;
A13: now per cases;
      suppose
A14:    a>=0;
        thus thesis by A2,A10,A14;
      end;
      suppose
A15:    a<0;
A16:    a |^ (n+m) >= 0 by A12,Th3;
        thus
        n-root a * m-root a = n-root a * (-m -Root (-a)) by A9,A15,Def1
          .= (-n -Root (-a)) * (-m -Root (-a)) by A8,A15,Def1
          .= n -Root (-a) * m -Root (-a)
          .= (n*m) -Root ((-a) |^ (n+m))by A10,A15,PREPOWER:35
          .= (n*m) -Root (a |^ (n+m)) by A12,Th1
          .= (n*m)-root (a |^ (n+m)) by A11,A16,Def1;
      end;
    end;
    thus thesis by A13;
  end;
  thus thesis by A1,A2,A7;
end;

theorem
  a<=b & (0<=a & n>=1 or n is odd) implies n-root a <= n-root b
proof
  assume that
A1: a<=b and
A2: 0<=a & n>=1 or n is odd;
A3: now
    let a,b,n;
    assume that
A4: 0<=a & n>=1 and
A5: a<=b;
A6: n -Root a <= n -Root b by A4,A5,PREPOWER:36;
A7: n -Root a <= n-root b by A4,A5,A6,Def1;
    thus n-root a <= n-root b by A4,A7,Def1;
  end;
A8: now
    assume
A9: n is odd;
    then
A10: n>=1 by Lm1;
A11: now per cases;
      suppose
A12:    a>=0;
        thus thesis by A1,A3,A10,A12;
      end;
      suppose
A13:    a<0;
A14:    now per cases;
          suppose
A15:        b>=0;
A16:        n-root b >= 0 by A10,A15,Th8;
A17:        n-root (-a) >= 0 by A10,A13,Th8;
A18:        - n-root (-a) <= -0 by A17;
            thus thesis by A9,A16,A18,Th11;
          end;
          suppose
A19:        b<0;
A20:        -a>=-b by A1,XREAL_1:26;
A21:        n-root (-a) >= n-root (-b) by A3,A10,A19,A20;
A22:        - n-root (-a) <= - n-root (-b) by A21,XREAL_1:26;
A23:        n-root a <= - n-root (-b) by A9,A22,Th11;
            thus thesis by A9,A23,Th11;
          end;
        end;
        thus thesis by A14;
      end;
    end;
    thus thesis by A11;
  end;
  thus thesis by A1,A2,A3,A8;
end;

theorem
  a<b & (a>=0 & n>=1 or n is odd) implies n-root a < n-root b
proof
  assume that
A1: a<b and
A2: 0<=a & n>=1 or n is odd;
A3: now
    let a,b,n;
    assume that
A4: 0<=a & n>=1 and
A5: a<b;
A6: n -Root a < n -Root b by A4,A5,PREPOWER:37;
A7: n -Root a < n-root b by A4,A5,A6,Def1;
    thus n-root a < n-root b by A4,A7,Def1;
  end;
A8: now
    assume
A9: n is odd;
    then
A10: n>=1 by Lm1;
A11: now per cases;
      suppose
A12:    a>=0;
        thus thesis by A1,A3,A10,A12;
      end;
      suppose
A13:    a<0;
A14:    -a>0 by A13,XREAL_1:60;
A15:    now per cases;
          suppose
A16:        b>=0;
A17:        n-root b >= 0 by A10,A16,Th8;
A18:        n -Root (-a) > 0 by A10,A14,PREPOWER:def 3;
A19:        - n -Root (-a) < -0 by A18,XREAL_1:26;
            thus thesis by A9,A13,A17,A19,Def1;
          end;
          suppose
A20:        b<0;
A21:        -a>-b by A1,XREAL_1:26;
A22:        n-root (-a) > n-root (-b) by A3,A10,A20,A21;
A23:        - n-root (-a) < - n-root (-b) by A22,XREAL_1:26;
A24:        n-root a < - n-root (-b) by A9,A23,Th11;
            thus thesis by A9,A24,Th11;
          end;
        end;
        thus thesis by A15;
      end;
    end;
    thus thesis by A11;
  end;
  thus thesis by A1,A2,A3,A8;
end;

theorem Th19:
  a>=1 & n>=1 implies n-root a >= 1 & a >= n-root a
proof
  assume that
A1: a>=1 and
A2: n>=1;
  A3: n -Root a >= 1 & a >= n -Root a by A1,A2,PREPOWER:38;
  thus thesis by A2,A3,Def1;
end;

theorem
  a<=-1 & n is odd implies n-root a <= -1 & a <= n-root a
proof
  assume that
A1: a<=-1 and
A2: n is odd;
A3: n>=1 & -a>=1 by A1,A2,XREAL_1:27,Lm1;
A4: n-root (-a) >= 1 by A3,Th19;
A5: -a >= n-root (-a) by A3,Th19;
A6: - n-root (-a) <= -1 by A4,XREAL_1:26;
A7: a <= - n-root (-a) by A5,XREAL_1:27;
  thus thesis by A2,A6,A7,Th11;
end;

theorem Th21:
  a>=0 & a<1 & n>=1 implies a <= n-root a & n-root a < 1
proof
  assume that
A1: a>=0 and
A2: a<1 and
A3: n>=1;
  a <= n -Root a & n -Root a < 1 by A1,A2,A3,PREPOWER:39;
  hence thesis by A1,A3,Def1;
end;

theorem
  a>-1 & a<=0 & n is odd implies a >= n-root a & n-root a > -1
proof
  assume that
A1: a>-1 and
A2: a<=0;
  assume n is odd;
  then
A3: ex m st n=2*m+1 by ABIAN:9;
A4: n>=1 & -a<1 by A1,A3,XREAL_1:27,Lm1;
A5: n-root (-a) < 1 by A2,A4,Th21;
A6: -a <= n-root (-a) by A2,A4,Th21;
A7: - n-root (-a) > -1 by A5,XREAL_1:26;
A8: a >= - n-root (-a) by A6,XREAL_1:28;
  thus thesis by A3,A7,A8,Th11;
end;

theorem Th23:
  a>0 & n>=1 implies n-root a - 1 <= (a-1)/n
proof
  assume
A1: a>0 & n>=1;
A2: n -Root a - 1 <= (a-1)/n by A1,PREPOWER:40;
  thus thesis by A1,A2,Def1;
end;

theorem Th24:
  for s being Real_Sequence, a st a > 0 &
  (for n st n>=1 holds s.n = n-root a)
  holds s is convergent & lim s = 1
proof
  let s be Real_Sequence, a;
  assume that
A1: a > 0 and
A2: for n st n>=1 holds s.n = n-root a;
A3: now
    let n be Element of NAT;
    assume
A4: n>=1;
    thus s.n = n-root a by A2,A4
      .= n -Root a by A1,A4,Def1;
  end;
  thus thesis by A1,A3,PREPOWER:42;
end;

definition
  let a,b be real number;
  func a to_power b -> real number means
  :Def2:
  it = a #R b if a > 0,
  it = 0 if a = 0 & b > 0,
  ex k st k = b & it = a #Z k if b is Integer;
  consistency
  proof
    let IT be real number;
    thus a > 0 & a = 0 & b > 0 implies
    (IT = a #R b iff IT = 0);
    thus a > 0 & b is Integer implies
    (IT = a #R b iff ex k st k = b & IT = a #Z k)
    proof
      assume
A1:   a > 0;
      assume
A2:   b is Integer;
      reconsider b as Integer by A2;
A3:   a #R b = a #Q b by A1,PREPOWER:88
        .= a #Z b by A1,PREPOWER:113;
      thus thesis by A3;
    end;
A4: a = 0 & b > 0 & b is Integer implies
    (IT = 0 iff ex k st k = b & IT = a #Z k)
    proof
      assume
A5:   a = 0;
      assume that
A6:   b > 0 and
A7:   b is Integer;
A8:   b in NAT by A6,A7,INT_1:16;
      reconsider b as Nat by A8;
A9:   a #Z b = a |^ b by PREPOWER:46
        .= 0 by A5,A6,NAT_1:14,NEWTON:16;
      thus thesis by A9;
    end;
    thus thesis by A4;
  end;
  existence
  proof
A10: now
      assume
A11:  b is Integer;
      reconsider k = b as Integer by A11;
      take c = a #Z k;
      thus ex k st k = b & c = a #Z k;
    end;
    thus thesis by A10;
  end;
  uniqueness;
end;

definition
  let a,b be Real;
  redefine func a to_power b -> Real;
  coherence by XREAL_0:def 1;
end;

canceled 4;

theorem Th29:
  a to_power 0 = 1
proof
  thus a to_power 0 = a #Z 0 by Def2
    .= 1 by PREPOWER:44;
end;

theorem Th30:
  a to_power 1 = a
proof
  thus a to_power 1 = a #Z 1 by Def2
    .= a by PREPOWER:45;
end;

theorem Th31:
  1 to_power a = 1
proof
A1: 1 #R a <> 0 by PREPOWER:95;
  thus
  1 to_power a = (1/1) #R a by Def2
    .= (1 #R a) / (1 #R a) by PREPOWER:94
    .= 1 by A1,XCMPLX_1:60;
end;

theorem Th32:
  a > 0 implies a to_power (b+c) = a to_power b * a to_power c
proof
  assume
A1: a > 0;
A2: a #R (b+c) = a #R b * a #R c by A1,PREPOWER:89;
A3: a #R (b+c) = a #R b * a to_power c by A1,A2,Def2;
A4: a #R (b+c) = a to_power b * a to_power c by A1,A3,Def2;
  thus thesis by A1,A4,Def2;
end;

theorem Th33:
  a > 0 implies a to_power (-c) = 1 / a to_power c
proof
  assume
A1: a > 0;
A2: a #R (-c) = 1 / a #R c by A1,PREPOWER:90;
A3: a #R (-c) = 1 / a to_power c by A1,A2,Def2;
  thus thesis by A1,A3,Def2;
end;

theorem Th34:
  a > 0 implies a to_power (b-c) = a to_power b / a to_power c
proof
  assume
A1: a > 0;
A2: a #R (b-c) = a #R b / a #R c by A1,PREPOWER:91;
A3: a #R (b-c) = a #R b / a to_power c by A1,A2,Def2;
A4: a #R (b-c) = a to_power b / a to_power c by A1,A3,Def2;
  thus thesis by A1,A4,Def2;
end;

theorem
  a>0 & b>0 implies (a*b) to_power c = a to_power c*b to_power c
proof
  assume that
A1: a > 0 and
A2: b > 0;
A3: a * b > 0 by A1,A2,XREAL_1:131;
A4: (a * b) #R c = a #R c * b #R c by A1,A2,PREPOWER:92;
A5: (a * b) #R c = a #R c * b to_power c by A2,A4,Def2;
A6: (a * b) #R c = a to_power c * b to_power c by A1,A5,Def2;
  thus thesis by A3,A6,Def2;
end;

theorem Th36:
  a>0 & b>0 implies (a/b) to_power c = a to_power c/b to_power c
proof
  assume that
A1: a > 0 and
A2: b > 0;
A3: a / b > 0 by A1,A2,XREAL_1:141;
A4: (a / b) #R c = a #R c / b #R c by A1,A2,PREPOWER:94;
A5: (a / b) #R c = a #R c / b to_power c by A2,A4,Def2;
A6: (a / b) #R c = a to_power c / b to_power c by A1,A5,Def2;
  thus thesis by A3,A6,Def2;
end;

theorem Th37:
  a>0 implies (1/a) to_power b = a to_power (-b)
proof
  assume
A1: a>0;
  thus (1/a) to_power b = (1/a) #R b by A1,Def2
    .= 1/a #R b by A1,PREPOWER:93
    .= 1/a to_power b by A1,Def2
    .= a to_power (-b) by A1,Th33;
end;

theorem Th38:
  a > 0 implies a to_power b to_power c = a to_power (b * c)
proof
  assume
A1: a > 0;
A2: a #R b > 0 by A1,PREPOWER:95;
A3: a #R b #R c = a #R (b * c) by A1,PREPOWER:105;
A4: a #R b #R c = a to_power (b * c) by A1,A3,Def2;
A5: a #R b to_power c = a to_power (b * c) by A2,A4,Def2;
  thus thesis by A1,A5,Def2;
end;

theorem Th39:
  a > 0 implies a to_power b > 0
proof
  assume
A1: a > 0;
A2: a #R b > 0 by A1,PREPOWER:95;
  thus thesis by A1,A2,Def2;
end;

theorem Th40:
  a > 1 & b > 0 implies a to_power b > 1
proof
  assume that
A1: a > 1 and
A2: b > 0;
A3: a #R b > 1 by A1,A2,PREPOWER:100;
  thus thesis by A1,A3,Def2;
end;

theorem Th41:
  a > 1 & b < 0 implies a to_power b < 1
proof
  assume that
A1: a > 1 and
A2: b < 0;
A3: a #R b < 1 by A1,A2,PREPOWER:102;
  thus thesis by A1,A3,Def2;
end;

theorem
  a > 0 & a < b & c > 0 implies a to_power c < b to_power c
proof
  assume that
A1: a > 0 and
A2: a < b and
A3: c > 0;
A4: a to_power c > 0 by A1,Th39;
A5: a to_power c <> 0 by A1,Th39;
A6: a/a<b/a by A1,A2,XREAL_1:76;
A7: 1<b/a by A1,A6,XCMPLX_1:60;
A8: (b/a) to_power c > 1 by A3,A7,Th40;
A9: b to_power c / a to_power c > 1 by A1,A2,A8,Th36;
A10: b to_power c/a to_power c*a to_power c>1*a to_power c by A4,A9,
  XREAL_1:70;
  thus thesis by A5,A10,XCMPLX_1:88;
end;

theorem
  a > 0 & a < b & c < 0 implies a to_power c > b to_power c
proof
  assume that
A1: a > 0 and
A2: a < b and
A3: c < 0;
A4: a to_power c > 0 by A1,Th39;
A5: a to_power c <> 0 by A1,Th39;
A6: a/a<b/a by A1,A2,XREAL_1:76;
A7: 1<b/a by A1,A6,XCMPLX_1:60;
A8: (b/a) to_power c < 1 by A3,A7,Th41;
A9: b to_power c / a to_power c < 1 by A1,A2,A8,Th36;
A10: b to_power c/a to_power c*a to_power c <1*a to_power c by A4,A9,
  XREAL_1:70;
  thus thesis by A5,A10,XCMPLX_1:88;
end;

theorem Th44:
  a < b & c > 1 implies c to_power a < c to_power b
proof
  assume that
A1: a < b and
A2: c > 1;
A3: c to_power a > 0 by A2,Th39;
A4: c to_power a <> 0 by A2,Th39;
A5: b-a>0 by A1,XREAL_1:52;
A6: c to_power (b-a) > 1 by A2,A5,Th40;
A7: c to_power b / c to_power a > 1 by A2,A6,Th34;
A8: c to_power b/c to_power a*c to_power a > 1*c to_power a by A3,A7,XREAL_1:70
  ;
  thus thesis by A4,A8,XCMPLX_1:88;
end;

theorem Th45:
  a < b & c > 0 & c < 1 implies c to_power a > c to_power b
proof
  assume that
A1: a < b and
A2: c > 0 and
A3: c < 1;
A4: (1/c) to_power a > 0 by A2,Th39;
A5: (1/c) to_power a <> 0 by A2,Th39;
A6: c to_power a > 0 by A2,Th39;
A7: c/c <1/c by A2,A3,XREAL_1:76;
A8: 1<1/c by A2,A7,XCMPLX_1:60;
A9: b-a>0 by A1,XREAL_1:52;
A10: (1/c) to_power (b-a) > 1 by A8,A9,Th40;
A11: (1/c) to_power b / (1/c) to_power a > 1 by A2,A10,Th34;
A12: (1/c) to_power b/(1/c) to_power a*(1/c) to_power a >
  1*(1/c) to_power a by A4,A11,XREAL_1:70;
A13: (1/c) to_power b > (1/c) to_power a by A5,A12,XCMPLX_1:88;
A14: 1 to_power b/c to_power b > (1/c) to_power a by A2,A13,Th36;
A15: 1 / c to_power b > (1/c) to_power a by A14,Th31;
A16: 1 / c to_power b > 1 to_power a/c to_power a by A2,A15,Th36;
A17: 1 / c to_power b > 1 / c to_power a by A16,Th31;
  thus thesis by A6,A17,XREAL_1:93;
end;

registration
  let a be real number, n be Nat;
  identify a to_power n with a |^ n;
  compatibility
  proof
    thus a to_power n = a #Z n by Def2
      .= a |^ n by PREPOWER:46;
  end;
end;

theorem
  for n be Nat holds a to_power n = a |^ n;

canceled 3;

theorem
  a to_power k = a #Z k by Def2;

theorem Th51:
  a>0 implies a to_power p = a #Q p
proof
  assume
A1: a>0;
  thus a to_power p = a #R p by A1,Def2
    .= a #Q p by A1,PREPOWER:88;
end;

theorem Th52:
  a>=0 & n>=1 implies a to_power (1/n) = n-root a
proof
  assume that
A1: a>=0 and
A2: n>=1;
  reconsider p=n" as Rational;
A3: now per cases by A1;
    suppose
A4:   a>0;
      thus a to_power (1/n) = a #Q p by A4,Th51
        .= n -Root a by A2,PREPOWER:61;
    end;
    suppose
A5:   a=0;
      thus a to_power (1/n) = 0 by A2,A5,Def2
        .= n -Root a
      by A2,A5,PREPOWER:def 3;
    end;
  end;
  thus thesis by A1,A2,A3,Def1;
end;

theorem Th53:
  a to_power 2 = a^2
proof
A1: now per cases;
    suppose
A2:   a>0;
      thus
      a to_power 2 = a to_power (1+1)
        .= a to_power 1 * a to_power 1 by A2,Th32
        .= a to_power 1 * a by Th30
        .= a^2 by Th30;
    end;
    suppose
A3:   a=0;
      thus thesis by A3,Def2;
    end;
    suppose
A4:   a<0;
      reconsider l=1 as Integer;
      thus
      a to_power 2 = a #Z (l+l) by Def2
        .= a #Z l * a #Z l by A4,PREPOWER:54
        .= a * a #Z l by PREPOWER:45
        .= a^2 by PREPOWER:45;
    end;
  end;
  thus thesis by A1;
end;

theorem Th54:
  a<>0 & k is even implies (-a) to_power k = a to_power k
proof
  assume
A1: a<>0;
  given l such that
A2: k=2*l;
  reconsider l1=2 as Integer;
A3: now per cases by A1;
    suppose
A4:   a>0;
      thus
      a to_power k = (a to_power 2) to_power l by A2,A4,Th38
        .= (a^2) to_power l by Th53
        .= ((-a)^2) to_power l
        .= ((-a) to_power 2) to_power l by Th53
        .= ((-a) #Z l1) to_power l by Def2
        .= ((-a) #Z l1) #Z l by Def2
        .= (-a) #Z (l1*l) by PREPOWER:55
        .= (-a) to_power k by A2,Def2;
    end;
    suppose
A5:   a<0;
A6:   -a>0 by A5,XREAL_1:60;
      thus (-a) to_power k = ((-a) to_power 2) to_power l by A2,A6,Th38
        .= ((-a)^2) to_power l by Th53
        .= (a^2) to_power l
        .= (a to_power 2) to_power l by Th53
        .= (a #Z l1) to_power l by Def2
        .= (a #Z l1) #Z l by Def2
        .= a #Z (l1*l) by PREPOWER:55
        .= a to_power k by A2,Def2;
    end;
  end;
  thus thesis by A3;
end;

theorem
  a<>0 & k is odd implies (-a) to_power k = -(a to_power k)
proof
  assume
A1: a<>0;
  assume k is odd;
  then consider l such that
A2: k=2*l + 1 by ABIAN:1;
  reconsider l1=1 as Integer;
A3: now per cases by A1;
    suppose
A4:   a>0;
A5:   -a<>-0 by A4;
A6:   a to_power k = a to_power (2*l) * a to_power 1 by A2,A4,Th32
        .= a to_power (2*l) * a by Th30
        .= (-a) to_power (2*l) * a by A1,Th54
        .= - (-a) to_power (2*l) * (-a)
        .= - (-a) #Z (2*l) * (-a) by Def2
        .= - (-a) #Z (2*l) * (-a) #Z l1 by PREPOWER:45
        .= - (-a) #Z k by A2,A5,PREPOWER:54
        .= - (-a) to_power k by Def2;
      thus thesis by A6;
    end;
    suppose
A7:   a<0;
A8:   -a>0 by A7,XREAL_1:60;
      thus (-a) to_power k = (-a) to_power (2*l) * (-a) to_power 1 by A2,A8,
      Th32
        .= (-a) to_power (2*l) * (-a) by Th30
        .= a to_power (2*l) * (-a) by A1,Th54
        .= - a to_power (2*l) * a
        .= - a #Z (2*l) * a by Def2
        .= - a #Z (2*l) * a #Z l1 by PREPOWER:45
        .= - a #Z k by A2,A7,PREPOWER:54
        .= - a to_power k by Def2;
    end;
  end;
  thus thesis by A3;
end;

theorem
  -1 < a implies (1 + a) to_power n >= 1 + n * a by PREPOWER:24;

theorem Th57:
  a>0 & a<>1 & c <>d implies a to_power c <> a to_power d
proof
  assume that
A1: a>0 and
A2: a<>1 and
A3: c <>d;
A4: now per cases by A3,XXREAL_0:1;
    suppose
A5:   c <d;
A6:   now per cases by A2,XXREAL_0:1;
        suppose
A7:       a<1;
          thus thesis by A1,A5,A7,Th45;
        end;
        suppose
A8:       a>1;
          thus thesis by A5,A8,Th44;
        end;
      end;
      thus thesis by A6;
    end;
    suppose
A9:   c>d;
A10:  now per cases by A2,XXREAL_0:1;
        suppose
A11:      a<1;
          thus thesis by A1,A9,A11,Th45;
        end;
        suppose
A12:      a>1;
          thus thesis by A9,A12,Th44;
        end;
      end;
      thus thesis by A10;
    end;
  end;
  thus thesis by A4;
end;

definition
  let a,b be real number;
  assume that
A1: a>0 and
A2: a<>1 and
A3: b>0;
  func log(a,b) -> real number means
  :Def3:
  a to_power it = b;
  existence
  proof
    reconsider a1=a, b1=b as Real by XREAL_0:def 1;
    set X = {c where c is Real: a to_power c <= b};
A4: for x holds x in X implies x in REAL
    proof
      let x;
      assume
A5:   x in X;
A6:   ex c be Real st x = c & a to_power c <= b by A5;
      thus thesis by A6;
    end;
    reconsider X as Subset of REAL by A4,TARSKI:def 3;
A7: ex d st d in X
    proof
A8:   now
        let a,b be real number such that
A9:     a>1 and
A10:    b>0;
        reconsider a1=a, b1=b as Real by XREAL_0:def 1;
        set e = a1-1;
        consider n such that
A11:    n>1/(b*e) by SEQ_4:10;
A12:    e>0-1 by A9,XREAL_1:11;
A13:    (1 + e) to_power n >= 1 + n * e by A12,PREPOWER:24;
A14:    1 + n * e >= 0 + n * e by XREAL_1:8;
A15:    (1 + e) to_power n >= n * e by A13,A14,XXREAL_0:2;
A16:    e>1-1 by A9,XREAL_1:11;
A17:    n*e>1/(b*e)*e by A11,A16,XREAL_1:70;
A18:    n*e>=1/b by A16,A17,XCMPLX_1:93;
A19:    a to_power n >= 1/b by A15,A18,XXREAL_0:2;
A20:    1/a to_power n <= 1/(1/b) by A10,A19,XREAL_1:87;
A21:    a1 to_power (-n) <= b1 by A9,A20,Th33;
        thus ex d st a to_power d <= b by A21;
      end;
A22:  now per cases by A2,XXREAL_0:1;
        suppose
A23:      a>1;
          thus ex d st a to_power d <= b by A3,A8,A23;
        end;
        suppose
A24:      a<1;
A25:      1/a >1/1 by A1,A24,XREAL_1:90;
          consider d such that
A26:      (1/a) to_power d <= b by A3,A8,A25;
A27:      a1 to_power (-d) <= b1 by A1,A26,Th37;
          thus ex e st a to_power e <= b by A27;
        end;
      end;
      consider d such that
A28:  a to_power d <= b by A22;
      take d;
A29:  d is Real by XREAL_0:def 1;
      thus thesis by A28,A29;
    end;
A30: now per cases by A2,XXREAL_0:1;
      suppose
A31:    a>1;
A32:    X is bounded_above
        proof consider n such that
A33:      n > (b1-1)/(a1-1) by SEQ_4:10;
A34:      a-1>1-1 by A31,XREAL_1:11;
A35:      n*(a-1) > b-1 by A33,A34,XREAL_1:79;
A36:      1 + n*(a-1) > 1+(b-1) by A35,XREAL_1:8;
A37:      a-1>0-1 by A1,XREAL_1:11;
A38:      (1+(a1-1)) to_power n >= 1 + n*(a1-1) by A37,PREPOWER:24;
A39:      a to_power n > b by A36,A38,XXREAL_0:2;
A40:      now
            let c be real number;
            assume
A41:        c in X;
A42:        ex d be Real st d=c & a to_power d <= b by A41;
A43:        a1 to_power c <= a1 to_power n by A39,A42,XXREAL_0:2;
            thus c <=n by A31,A43,Th44;
          end;
          thus thesis by A40,SEQ_4:def 1;
        end;
        take d = upper_bound X;
A44:    not b < a to_power d
        proof
          assume
A45:      a to_power d > b;
          consider e be real number such that
A46:      b<e and
A47:      e<a to_power d by A45,XREAL_1:7;
          reconsider e as Real by XREAL_0:def 1;
          consider n such that
A48:      n > (a1-1)/(e/b1-1) by SEQ_4:10;
          reconsider m = max(1,n) as Element of NAT by XXREAL_0:16;
A49:      n<=m by XXREAL_0:25;
A50:      (a-1)/(e/b-1) < m by A48,A49,XXREAL_0:2;
A51:      e/b>1 by A3,A46,XREAL_1:189;
A52:      e/b-1>1-1 by A51,XREAL_1:11;
A53:      (a-1)<m*(e/b-1) by A50,A52,XREAL_1:79;
A54:      1<=m by XXREAL_0:25;
A55:      (a-1)/m<e/b-1 by A53,A54,XREAL_1:85;
A56:      (a-1)/m+1<e/b-1+1 by A55,XREAL_1:8;
A57:      m-root a1 - 1 <= (a1-1)/m by A1,Th23,XXREAL_0:25;
A58:      m-root a - 1 + 1 <= (a-1)/m + 1 by A57,XREAL_1:8;
A59:      m-root a <= e/b by A56,A58,XXREAL_0:2;
A60:      a1 to_power (1/m) <= e/b1 by A1,A59,Th52,XXREAL_0:25;
A61:      a to_power (1/m) * b <= e by A3,A60,XREAL_1:85;
          consider c be real number such that
A62:      c in X and
A63:      d-1/m < c by A7,A32,A54,SEQ_4:def 4;
          reconsider c as Real by XREAL_0:def 1;
A64:      ex g being Real st g=c & a to_power g <= b by A62;
A65:      a1 to_power (1/m) >= 0 by A1,Th39;
A66:      a to_power c * a to_power (1/m) <= a to_power (1/m)*b by A64,A65,
          XREAL_1:66;
A67:      a to_power c * a to_power (1/m) <= e by A61,A66,XXREAL_0:2;
A68:      a1 to_power (c + 1/m) <= e by A1,A67,Th32;
A69:      d<c + 1/m by A63,XREAL_1:21;
A70:      a1 to_power d <= a1 to_power (c + 1/m) by A31,A69,Th44;
          thus contradiction by A47,A68,A70,XXREAL_0:2;
        end;
A71:    not a to_power d < b
        proof
          assume
A72:      a to_power d < b;
          consider e being real number such that
A73:      a to_power d<e and
A74:      e<b by A72,XREAL_1:7;
          reconsider e as Real by XREAL_0:def 1;
A75:      a1 to_power d > 0 by A1,Th39;
A76:      b/e>1 by A73,A74,A75,XREAL_1:189;
A77:      b/e-1>1-1 by A76,XREAL_1:11;
          deffunc F(Element of NAT) = $1-root a;
          consider s being Real_Sequence such that
A78:      for n holds s.n = F(n) from SEQ_1:sch 1;
A79:      for n st n>=1 holds s.n = n-root a1 by A78;
A80:      s is convergent & lim s = 1 by A1,A79,Th24;
          consider n such that
A81:      for m st m>=n holds abs(s.m - 1)<b/e-1
          by A77,A80,SEQ_2:def 7;
          reconsider m = max(1,n) as Element of NAT by XXREAL_0:16;
A82:      abs(s.m - 1)<b/e-1 by A81,XXREAL_0:25;
A83:      abs(m-root a - 1)<b/e-1 by A78,A82;
A84:      m-root a - 1<=abs(m-root a - 1) by ABSVALUE:11;
A85:      m-root a - 1<b/e-1 by A83,A84,XXREAL_0:2;
A86:      m-root a < b/e by A85,XREAL_1:11;
A87:      a1 to_power (1/m) < b1/e by A1,A86,Th52,XXREAL_0:25;
A88:      a
 to_power d * a to_power (1/m) < b/e * a to_power d by A75,A87,XREAL_1:70;
A89:      a to_power (d + 1/m) < b * a to_power d / e by A1,A88,Th32;
A90:      a to_power d / e < 1 by A73,A75,XREAL_1:191;
A91:      a to_power d / e * b < 1*b by A3,A90,XREAL_1:70;
A92:      a to_power (d + 1/m) <= b by A89,A91,XXREAL_0:2;
A93:      d + 1/m in X by A92;
A94:      m>=1 & d + 1/m <= d by A32,A93,SEQ_4:def 4,XXREAL_0:25;
          thus contradiction by A94,XREAL_1:31;
        end;
        thus b = a to_power d by A44,A71,XXREAL_0:1;
      end;
      suppose
A95:    a<1;
A96:    X is bounded_below
        proof consider n such that
A97:      n > (b1-1)/(1/a1-1) by SEQ_4:10;
A98:      1/a >1/1 by A1,A95,XREAL_1:90;
A99:      1/a-1>1-1 by A98,XREAL_1:11;
A100:     n*(1/a-1) > b-1 by A97,A99,XREAL_1:79;
A101:     1 + n*(1/a-1) > 1+(b-1) by A100,XREAL_1:8;
A102:     1/a-1>0-1 by A1,XREAL_1:11;
A103:     (1+(1/a1-1)) to_power n >= 1 + n*(1/a1-1) by A102,PREPOWER:24;
A104:     (1/a) to_power n > b by A101,A103,XXREAL_0:2;
A105:     a1 to_power (-n) > b1 by A1,A104,Th37;
A106:     now
            let c be real number;
            assume
A107:       c in X;
A108:       ex d be Real st d=c & a to_power d <= b by A107;
A109:       a1 to_power c <= a1 to_power (-n) by A105,A108,XXREAL_0:2;
            thus c>=-n by A1,A95,A109,Th45;
          end;
          thus thesis by A106,SEQ_4:def 2;
        end;
        take d = lower_bound X;
A110:   not b < a to_power d
        proof
          assume
A111:     a to_power d > b;
          consider e being real number such that
A112:     b<e and
A113:     e<a to_power d by A111,XREAL_1:7;
          reconsider e as Real by XREAL_0:def 1;
          consider n such that
A114:     n > (1/a1-1)/(e/b1-1) by SEQ_4:10;
          reconsider m = max(1,n) as Element of NAT by XXREAL_0:16;
A115:     n<=m by XXREAL_0:25;
A116:     (1/a-1)/(e/b-1) < m by A114,A115,XXREAL_0:2;
A117:     e/b>1 by A3,A112,XREAL_1:189;
A118:     e/b-1>1-1 by A117,XREAL_1:11;
A119:     (1/a-1)<m*(e/b-1) by A116,A118,XREAL_1:79;
A120:     1<=m by XXREAL_0:25;
A121:     (1/a-1)/m<e/b-1 by A119,A120,XREAL_1:85;
A122:     (1/a-1)/m+1<e/b-1+1 by A121,XREAL_1:8;
A123:     m-root (1/a1) - 1 <= (1/a1-1)/m by A1,Th23,XXREAL_0:25;
A124:     m-root (1/a) - 1 + 1 <= (1/a-1)/m + 1 by A123,XREAL_1:8;
A125:     m-root (1/a) <= e/b by A122,A124,XXREAL_0:2;
A126:     (1/a1) to_power (1/m) <= e/b1 by A1,A125,Th52,XXREAL_0:25;
A127:     (1/a) to_power (1/m) * b <= e by A3,A126,XREAL_1:85;
A128:     a1 to_power (-1/m) * b1 <= e by A1,A127,Th37;
          consider c be real number such that
A129:     c in X and
A130:     c <d+1/m by A7,A96,A120,SEQ_4:def 5;
          reconsider c as Real by XREAL_0:def 1;
A131:     ex g being Real st g=c & a to_power g <= b by A129;
A132:     a1 to_power (-1/m) >= 0 by A1,Th39;
A133:     a to_power c*a to_power (-1/m) <= a to_power (-1/m)*b by A131,A132,
          XREAL_1:66;
A134:     a to_power c * a to_power (-1/m) <= e by A128,A133,XXREAL_0:2;
A135:     a1 to_power (c + -1/m) <= e by A1,A134,Th32;
A136:     d>c - 1/m by A130,XREAL_1:21;
A137:     a1 to_power d <= a1 to_power (c - 1/m) by A1,A95,A136,Th45;
          thus contradiction by A113,A135,A137,XXREAL_0:2;
        end;
A138:   not a to_power d < b
        proof
          assume
A139:     a to_power d < b;
          consider e being real number such that
A140:     a to_power d<e and
A141:     e<b by A139,XREAL_1:7;
          reconsider e as Real by XREAL_0:def 1;
A142:     a1 to_power d > 0 by A1,Th39;
A143:     b/e>1 by A140,A141,A142,XREAL_1:189;
A144:     b/e-1>1-1 by A143,XREAL_1:11;
          deffunc F(Element of NAT) = $1-root (1/a);
          consider s being Real_Sequence such that
A145:     for n holds s.n = F(n) from SEQ_1:sch 1;
A146:     for n st n>=1 holds s.n = n-root (1/a1) by A145;
A147:     s is convergent & lim s = 1 by A1,A146,Th24;
          consider n such that
A148:     for m st m>=n holds abs(s.m - 1)<b/e-1
          by A144,A147,SEQ_2:def 7;
          reconsider m = max(1,n) as Element of NAT by XXREAL_0:16;
A149:     abs(s.m - 1)<b/e-1 by A148,XXREAL_0:25;
A150:     abs(m-root (1/a) - 1)<b/e-1 by A145,A149;
A151:     m-root (1/a) - 1<=abs(m-root (1/a) - 1) by ABSVALUE:11;
A152:     m-root (1/a) - 1<b/e-1 by A150,A151,XXREAL_0:2;
A153:     m-root (1/a) < b/e by A152,XREAL_1:11;
A154:     (1/a1) to_power (1/m) < b1/e by A1,A153,Th52,XXREAL_0:25;
A155:     a to_power d*(1/a) to_power (1/m)<b/e*a to_power d by A142,A154,
          XREAL_1:70;
A156:     a1 to_power d*a1 to_power (-1/m)<b1/e*a1 to_power d by A1,A155,
          Th37;
A157:     a to_power (d - 1/m) < b * a to_power d / e by A1,A156,Th32;
A158:     a to_power d / e < 1 by A140,A142,XREAL_1:191;
A159:     a to_power d / e * b < 1*b by A3,A158,XREAL_1:70;
A160:     a to_power (d - 1/m) < b by A157,A159,XXREAL_0:2;
A161:     d - 1/m in X by A160;
A162:     m>=1 & d - 1/m >= d by A96,A161,SEQ_4:def 5,XXREAL_0:25;
          thus contradiction by A162,XREAL_1:46;
        end;
        thus b = a to_power d by A110,A138,XXREAL_0:1;
      end;
    end;
    thus thesis by A30;
  end;
  uniqueness by A1,A2,Th57;
end;

definition
  let a,b be Real;
  redefine func log(a,b) -> Real;
  coherence by XREAL_0:def 1;
end;

canceled;

theorem
  a>0 & a<>1 implies log(a,1) = 0
proof
  assume
A1: a>0 & a<>1;
A2: a to_power 0 = 1 by Th29;
  thus thesis by A1,A2,Def3;
end;

theorem
  a>0 & a<>1 implies log(a,a) = 1
proof
  assume
A1: a>0 & a<>1;
A2: a to_power 1 = a by Th30;
  thus thesis by A1,A2,Def3;
end;

theorem
  a>0 & a<>1 & b>0 & c>0 implies log(a,b) + log(a,c) = log(a,b*c)
proof
  assume that
A1: a>0 and
A2: a<>1 and
A3: b>0 and
A4: c>0;
A5: a to_power (log(a,b) + log(a,c))
  = a to_power log(a,b) * a to_power log(a,c) by A1,Th32
    .= b * a to_power log(a,c) by A1,A2,A3,Def3
    .= b * c by A1,A2,A4,Def3;
A6: b*c>0 by A3,A4,XREAL_1:131;
  thus thesis by A1,A2,A5,A6,Def3;
end;

theorem
  a>0 & a<>1 & b>0 & c>0 implies log(a,b) - log(a,c) = log(a,b/c)
proof
  assume that
A1: a>0 and
A2: a<>1 and
A3: b>0 and
A4: c>0;
A5: a to_power (log(a,b) - log(a,c))
  = a to_power log(a,b) / a to_power log(a,c) by A1,Th34
    .= b / a to_power log(a,c) by A1,A2,A3,Def3
    .= b / c by A1,A2,A4,Def3;
A6: b/c>0 by A3,A4,XREAL_1:141;
  thus thesis by A1,A2,A5,A6,Def3;
end;

theorem
  a>0 & a<>1 & b>0 implies log(a,b to_power c) = c * log(a,b)
proof
  assume that
A1: a>0 and
A2: a<>1 and
A3: b>0;
A4: b to_power c > 0 by A3,Th39;
A5: a to_power (c*log(a,b)) = (a to_power log(a,b)) to_power c by A1,Th38
    .= b to_power c by A1,A2,A3,Def3;
  thus thesis by A1,A2,A4,A5,Def3;
end;

theorem
  a>0 & a<>1 & b>0 & b<>1 & c>0 implies log(a,c) = log(a,b)*log(b,c)
proof
  assume that
A1: a>0 and
A2: a<>1 and
A3: b>0 and
A4: b<>1 and
A5: c>0;
A6: a to_power (log(a,b)*log(b,c))
  = a to_power log(a,b) to_power log(b,c) by A1,Th38
    .= b to_power log(b,c) by A1,A2,A3,Def3
    .= c by A3,A4,A5,Def3;
  thus thesis by A1,A2,A5,A6,Def3;
end;

theorem
  a>1 & b>0 & c>b implies log(a,c) > log(a,b)
proof
  assume that
A1: a>1 and
A2: b>0 and
A3: c>b and
A4: log(a,c) <= log(a,b);
A5: a to_power log(a,b) = b by A1,A2,Def3;
A6: a to_power log(a,c) = c by A1,A2,A3,Def3;
A7: now per cases by A4,XXREAL_0:1;
    suppose
A8:   log(a,c)<log(a,b);
      thus contradiction by A1,A3,A5,A6,A8,Th44;
    end;
    suppose
A9:   log(a,c) = log(a,b);
      thus contradiction by A1,A2,A3,A6,A9,Def3;
    end;
  end;
  thus contradiction by A7;
end;

theorem
  a>0 & a<1 & b>0 & c>b implies log(a,c) < log(a,b)
proof
  assume that
A1: a>0 & a<1 and
A2: b>0 and
A3: c>b and
A4: log(a,c) >= log(a,b);
A5: a to_power log(a,b) = b by A1,A2,Def3;
A6: a to_power log(a,c) = c by A1,A2,A3,Def3;
A7: now per cases by A4,XXREAL_0:1;
    suppose
A8:   log(a,c)>log(a,b);
      thus contradiction by A1,A3,A5,A6,A8,Th45;
    end;
    suppose
A9:   log(a,c) = log(a,b);
      thus contradiction by A1,A2,A3,A6,A9,Def3;
    end;
  end;
  thus contradiction by A7;
end;

theorem
  for s being Real_Sequence st
  for n holds s.n = (1 + 1/(n+1)) to_power (n+1 )
  holds s is convergent
proof
  let s be Real_Sequence such that
A1: for n holds s.n = (1 + 1/(n+1)) to_power (n+1);
A2: now
    let n be Element of NAT;
A3: (1+1/(n+1)) to_power (n+1) > 0 by Th39;
A4: s.(n+1)/s.n =(1 + 1/(n+1+1)) to_power (n+1+1) / s.n by A1
      .=(1 + 1/(n+1+1)) to_power (n+1+1)/(1+1/(n+1)) to_power (n+1)*1 by A1
      .=(1 + 1/(n+1+1)) to_power (n+1+1)/(1 + 1/(n+1)) to_power (n+1) *
    ((1+1/(n+1))/(1+1/(n+1))) by XCMPLX_1:60
      .=(1+1/(n+1)) * (1 + 1/(n+1+1)) to_power (n+1+1) /
    ((1 + 1/(n+1)) to_power (n+1) * (1+1/(n+1))) by XCMPLX_1:77
      .=(1+1/(n+1)) * (1 + 1/(n+1+1)) to_power (n+1+1) /
    ((1 + 1/(n+1)) to_power (n+1) * (1+1/(n+1)) to_power 1) by Th30
      .=(1+1/(n+1)) * (1 + 1/(n+1+1)) to_power (n+1+1) /
    (1 + 1/(n+1)) to_power (n+1+1) by Th32
      .=(1+1/(n+1)) * ((1 + 1/(n+1+1)) to_power (n+1+1) /
    (1 + 1/(n+1)) to_power (n+1+1))
      .=(1+1/(n+1)) * ((1 + 1/(n+1+1))/(1 + 1/(n+1))) to_power (n+1+1)
    by Th36;
A5: (1 + 1/(n+1+1))/(1 + 1/(n+1))
    = ((1*(n+1+1) + 1)/(n+1+1))/(1 + 1/(n+1)) by XCMPLX_1:114
      .= ((n+1+1+1)/(n+1+1))/((1*(n+1) + 1)/(n+1)) by XCMPLX_1:114
      .= ((n+(1+1)+1)*(n+1))/((n+2)*(n+2)) by XCMPLX_1:85
      .= (n*n+n*2+2*n+3+1-1)/((n+2)*(n+2))
      .= ((n+2)*(n+2))/((n+2)*(n+2)) - 1/((n+2)*(n+2))
      .= 1 - 1/((n+2)*(n+2)) by XCMPLX_1:6,60;
A6: n+1+1>0+1 by XREAL_1:8;
A7: (n+2)*(n+2)>1 by A6,XREAL_1:163;
A8: 1/((n+2)*(n+2))<1 by A7,XREAL_1:214;
A9: - 1/((n+2)*(n+2)) > -1 by A8,XREAL_1:26;
A10: (1 + -1/((n+2)*(n+2))) to_power (n+1+1) >= 1 + (n+1+1)*(-1/((n+2)*(n+2)
    )) by A9,PREPOWER:24;
A11: (1 - 1/((n+2)*(n+2))) to_power (n+1+1) >= 1 - (n+2)*1/((n+2)*(n+2))
    by A10;
A12: (1 - 1/((n+2)*(n+2))) to_power (n+1+1) >= 1 - ((n+2)/(n+2)*1)/(n+2)
    by A11,XCMPLX_1:84;
A13: (1 - 1/((n+2)*(n+2))) to_power (n+1+1) >= 1 - (1*1)/(n+2)
    by A12,XCMPLX_1:60;
A14: s.(n+1)/s.n >= (1 + 1/(n+1)) * (1 - 1/(n+2)) by A4,A5,A13,XREAL_1:66;
A15: s.(n+1)/s.n >= ((1*(n+1) + 1)/(n+1)) * (1 - 1/(n+2)) by A14,
    XCMPLX_1:114;
A16: s.(n+1)/s.n >= ((n+2)/(n+1)) * ((1*(n+2) - 1)/(n+2)) by A15,
    XCMPLX_1:128;
A17: s.(n+1)/s.n >= ((n+1)*(n+2))/((n+1)*(n+2)) by A16,XCMPLX_1:77;
A18: s.(n+1)/s.n >= 1 by A17,XCMPLX_1:6,60;
A19: s.n>0 by A1,A3;
    thus s.(n+1)>=s.n by A18,A19,XREAL_1:193;
  end;
A20: s is non-decreasing by A2,SEQM_3:def 13;
A21: now
    let n;
A22: 2*(n+1)>0 by XREAL_1:131;
A23: 2*(n+1)<>0;
A24: s.(n+(n+1)) = (1 + 1/(2*n+(1+1))) to_power (2*n+1+1) by A1
      .= ((1 + 1/(2*(n+1))) to_power (n+1)) to_power 2 by Th38;
A25: 2*(n+1)+1>0+1 by A22,XREAL_1:8;
A26: 1/(2*(n+1)+1)<1 by A25,XREAL_1:214;
A27: - 1/(2*(n+1)+1)> -1 by A26,XREAL_1:26;
A28: - 1/(2*(n+1)+1) + 1 > -1 + 1 by A27,XREAL_1:8;
A29: (1 + 1/(2*(n+1))) to_power (n+1)
    = (1/(1/(1 + 1/(2*(n+1))))) to_power (n+1)
      .= (1/(1 + 1/(2*(n+1)))) to_power (-(n+1)) by Th37
      .= (1/((1*(2*(n+1)) + 1)/(2*(n+1)))) to_power (-(n+1)) by A23,
    XCMPLX_1:114
      .= ((2*(n+1)+1-1)/(2*(n+1)+1)) to_power (-(n+1)) by XCMPLX_1:78
      .= ((2*(n+1)+1)/(2*(n+1)+1) - 1/(2*(n+1)+1)) to_power (-(n+1))
      .= (1 - 1/(2*(n+1)+1)) to_power (-(n+1)) by XCMPLX_1:60
      .= 1 / (1 - 1/(2*(n+1)+1)) to_power (n+1) by A28,Th33;
    A30: (
1 + - 1/(2*(n+1)+1)) to_power (n+1) >= 1 + (n+1)*(- 1/(2*(n+1)+1)) by A27,
    PREPOWER:24;
A31: (1 - 1/(2*(n+1)+1)) to_power (n+1) >= 1-(n+1)/(2*(n+1)+1) by A30;
    A32: (
1 - 1/(2*(n+1)+1)) to_power (n+1) >= (1*(2*(n+1)+1)-(n+1))/(2*(n +1 )+1)
    by A31,XCMPLX_1:128;
A33: now
      assume
A34:  (2*(n+1)-n)/(2*(n+1)+1) < 1/2;
A35:  (2*(n+1)-n)*2 < (2*(n+1)+1)*1 by A34,XREAL_1:104;
A36:  2*(n+1)+(-n + (2*(n+1)-n)) < 2*(n+1)+1 by A35;
      thus contradiction by A36,XREAL_1:8;
    end;
A37: (1 - 1/(2*(n+1)+1)) to_power (n+1) >= 1/2 by A32,A33,XXREAL_0:2;
A38: (1 + 1/(2*(n+1))) to_power (n+1) <= 1/(1/2) by A29,A37,XREAL_1:87;
A39: (1 + 1/(2*(n+1))) to_power (n+1) > 0 by Th39;
A40: ((1 + 1/(2*(n+1))) to_power (n+1))^2 <= 2*2 by A38,A39,XREAL_1:68;
A41: s.(n+(n+1)) <= 2*2 by A24,A40,Th53;
A42: s.n<=s.(n+(n+1)) by A20,SEQM_3:11;
A43: s.n <= 2*2 by A41,A42,XXREAL_0:2;
    thus s.n < 2*2 + 1 by A43,XXREAL_0:2;
  end;
A44: s is bounded_above by A21,SEQ_2:def 3;
  thus thesis by A20,A44;
end;

definition
  func number_e -> real number means
  for s being Real_Sequence st
  for n holds s.n = (1 + 1/(n+1)) to_power (n+1)
  holds it = lim s;
  existence
  proof
    deffunc F(Element of NAT) = (1 + 1/($1+1)) to_power ($1+1);
    consider s1 being Real_Sequence such that
A1: for n holds s1.n = F(n) from SEQ_1:sch 1;
    take lim s1;
    let s be Real_Sequence such that
A2: for n holds s.n = (1 + 1/(n+1)) to_power (n+1);
A3: now
      let n be Element of NAT;
      thus s1.n = (1 + 1/(n+1)) to_power (n+1) by A1
        .= s.n by A2;
    end;
    thus thesis by A3,FUNCT_2:113;
  end;
  uniqueness
  proof
    let a,b;
    assume that
A4: for s being Real_Sequence st
    for n holds s.n = (1 + 1/(n+1)) to_power (n+1)
    holds a = lim s and
A5: for s being Real_Sequence st
    for n holds s.n = (1 + 1/(n+1)) to_power (n+1)
    holds b = lim s;
    deffunc F(Element of NAT) = (1 + 1/($1+1)) to_power ($1+1);
    consider s1 being Real_Sequence such that
A6: for n holds s1.n = F(n) from SEQ_1:sch 1;
A7: lim s1 = a by A4,A6;
    thus thesis by A5,A6,A7;
  end;
end;

definition
  redefine func number_e -> Real;
  coherence by XREAL_0:def 1;
end;

theorem Th68:
  2 to_power 2 = 4
proof
  thus 2 to_power 2 = 2^2 by Th53
    .= 4;
end;

theorem Th69:
  2 to_power 3 = 8
proof
  thus 2 to_power 3 = 2 to_power (2+1)
    .= (2 to_power 2)*(2 to_power 1) by Th32
    .= 4*2 by Th30,Th68
    .= 8;
end;

theorem
  2 to_power 4 = 16
proof
  thus 2 to_power 4 = 2 to_power (2+2)
    .= (2 to_power 2)*(2 to_power 2) by Th32
    .= 16 by Th68;
end;

theorem
  2 to_power 5 = 32
proof
  thus 2 to_power 5 = 2 to_power (3+2)
    .= (2 to_power 3)*(2 to_power 2) by Th32
    .= 32 by Th68,Th69;
end;

theorem
  2 to_power 6 = 64
proof
  thus 2 to_power 6 = 2 to_power (3+3)
    .= 2 to_power 3 * 2 to_power 3 by Th32
    .= 64 by Th69;
end;


