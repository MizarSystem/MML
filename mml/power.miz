:: Real Exponents and Logarithms
::  by Konrad Raczkowski and Andrzej N\c{e}dzusiak
::
:: Received October 1, 1990
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies ARYTM, INT_1, RAT_1, ARYTM_1, PREPOWER, FUNCT_1, ARYTM_3, SEQ_1,
      SEQ_2, ORDINAL2, GROUP_1, RELAT_1, SQUARE_1, SEQ_4, ABSVALUE, PROB_1,
      POWER;
 notations SUBSET_1, NEWTON, ORDINAL1, XCMPLX_0, XREAL_0, XXREAL_0, COMPLEX1,
      INT_1, RAT_1, REAL_1, NUMBERS, NAT_1, SEQ_1, SEQ_2, SEQM_3, SQUARE_1,
      SEQ_4, PREPOWER;
 constructors REAL_1, SQUARE_1, NAT_1, SEQ_2, SEQM_3, SEQ_4, NEWTON, PREPOWER,
      SEQ_1, XXREAL_2;
 registrations NUMBERS, XXREAL_0, XREAL_0, SQUARE_1, NAT_1, INT_1, RAT_1,
      MEMBERED, NEWTON, PREPOWER, XBOOLE_0, SEQ_4;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions SQUARE_1;
 theorems ABSVALUE, NAT_1, SEQ_2, SEQM_3, INT_1, SQUARE_1, SEQ_4,
      REAL_2, NEWTON, PREPOWER, TARSKI, FUNCT_2, XREAL_0, XCMPLX_1,
      XREAL_1, XXREAL_0, ORDINAL1;
 schemes SEQ_1;

begin

reserve x for set;
reserve a, b, c, d, e for real number;
reserve m, n, m1, m2 for Element of NAT;
reserve k, l for Integer;
reserve p for Rational;

theorem Th1:
  (ex m st n=2*m) implies (-a) |^ n = a |^ n
proof
  given m such that
A1: n=2*m; thus
  (-a) |^ n = ((-a) |^ (1+1)) |^ m by A1,NEWTON:14
    .= ((-a) |^ (0+1) * (-a) |^ (0+1)) |^ m by NEWTON:13
    .= ((-a) |^ 0 * (-a) * (-a) |^ (0+1)) |^ m by NEWTON:11
    .= ((-a) |^ 0 * (-a) * ((-a) |^ 0*(-a))) |^ m by NEWTON:11
    .= ((-a) GeoSeq.0 * (-a) * ((-a) |^ 0 * (-a))) |^ m by PREPOWER:def 1
    .= ((-a) GeoSeq.0 * (-a) * ((-a) GeoSeq.0 * (-a))) |^ m by PREPOWER:def 1
    .= ((-a) GeoSeq.0 * (-a) * (1 * (-a))) |^ m by PREPOWER:4
    .= (1*(-a) * (1*(-a))) |^ m by PREPOWER:4
    .= (a * a) |^ m
    .= ((a GeoSeq.0*a) * (1*a)) |^ m by PREPOWER:4
    .= ((a GeoSeq.0*a) * (a GeoSeq.0*a)) |^ m by PREPOWER:4
    .= ((a GeoSeq.0*a) * (a |^ 0*a)) |^ m by PREPOWER:def 1
    .= ((a |^ 0*a) * (a |^ 0*a)) |^ m by PREPOWER:def 1
    .= ((a |^ 0*a) * a |^ (0+1)) |^ m by NEWTON:11
    .= (a |^ (0+1) * a |^ (0+1)) |^ m by NEWTON:11
    .= (a |^ (1+1)) |^ m by NEWTON:13
    .= a |^ n by A1,NEWTON:14;
end;

theorem Th2:
  (ex m st n=2*m+1) implies (-a) |^ n = - a |^ n
proof
  given m such that
A1: n=2*m+1; thus
  (-a) |^ n = (-a) |^ (2*m) * (-a) by A1,NEWTON:11
    .= a |^ (2*m) * (-a) by Th1
    .= -a |^ (2*m) * a
    .= -a |^ n by A1,NEWTON:11;
end;

theorem Th3:
  a>=0 or (ex m st n=2*m) implies a |^ n >= 0
proof
  assume
A1: a>=0 or (ex m st n=2*m);
A2: now
    let a,n such that
A3: a>=0;
    now per cases by A3;
      suppose a>0;
        hence a |^ n >= 0 by PREPOWER:13;
      end;
      suppose
A4:     a=0;
        now per cases by NAT_1:6;
          suppose
A5:         n=0;
            a |^ n = a GeoSeq.n by PREPOWER:def 1
              .= 1 by A5,PREPOWER:4;
            hence a |^ n >= 0;
          end;
          suppose ex m being Nat st n = m+1;
            then consider m being Nat such that
A6:         n = m + 1;
            a |^ n = a |^ m * a by A6,NEWTON:11
              .= 0 by A4;
            hence a |^ n >= 0;
          end;
        end;
        hence a |^ n >= 0;
      end;
    end;
    hence a |^ n >= 0;
  end;
  now
    assume
A7: ex m st n=2*m;
    now per cases;
      suppose a>=0;
        hence a |^ n >= 0 by A2;
      end;
      suppose a<0;
        then -a>=0;
        then (-a) |^ n >= 0 by A2;
        hence a |^ n >= 0 by A7,Th1;
      end;
    end;
    hence a |^ n >= 0;
  end;
  hence thesis by A1,A2;
end;

definition
  let n be Nat;
  let a be real number;
  func n-root a -> real number equals
  :Def1:
  n -Root a if a>=0 & n>=1,
  - n -Root (-a) if a<0 & ex m st n=2*m+1;
  consistency;
  coherence;
end;

definition
  let n;
  let a be Real;
  redefine func n-root a -> Real;
  coherence by XREAL_0:def 1;
end;

canceled;

theorem
  for n being Nat st n>=1 & a>=0 or ex m st n=2*m+1 holds
  (n-root a) |^ n = a & n-root (a |^ n) = a
proof
  let n be Nat;
  assume
A1: n>=1 & a>=0 or ex m st n=2*m+1;
A2: now
    assume
A3: n>=1 & a>=0;
    then
A4: n-root a = n -Root a by Def1;
    now per cases by A3;
      suppose a>0;
        hence a |^ n >= 0 by PREPOWER:13;
      end;
      suppose a=0;
        hence a |^ n >= 0;
      end;
    end;
    then n-root (a |^ n) = n -Root (a |^ n) by A3,Def1;
    hence (n-root a) |^ n = a & n-root (a |^ n) = a by A3,A4,PREPOWER:28;
  end;
  now
    assume
A5: ex m st n=2*m+1;
    then consider m such that
A6: n = 2*m + 1;
A7: n>=0+1 by A6,XREAL_1:8;
    now per cases;
      suppose a>=0;
        hence (n-root a) |^ n = a & n-root (a |^ n) = a by A2,A7;
      end;
      suppose
A8:     a<0;
        then
A9:     -a>0 by XREAL_1:60; thus
        (n-root a) |^ n = (- n -Root (-a)) |^ n by A5,A8,Def1
          .= -(n -Root (-a)) |^ n by A5,Th2
          .= -(-a) by A7,A9,PREPOWER:28
          .= a;
        (-a) |^ n > 0 by A9,PREPOWER:13;
        then -a |^ n > 0 by A5,Th2;
        then a |^ n < 0;
        hence n-root (a |^ n) = - n -Root (-a |^ n) by A5,Def1
          .= - n -Root ((-a) |^ n) by A5,Th2
          .= -(-a) by A7,A9,PREPOWER:28
          .= a;
      end;
    end;
    hence (n-root a) |^ n = a & n-root (a |^ n) = a;
  end;
  hence thesis by A1,A2;
end;

theorem Th6:
  for n being Nat st n>=1 holds n-root 0 = 0
proof
  let n be Nat;
  assume
A1: n>=1;
  hence n-root 0 = n -Root 0 by Def1
    .= 0 by A1,PREPOWER:def 3;
end;

theorem
  n>=1 implies n-root 1 = 1
proof
  assume
A1: n>=1;
  hence n-root 1 = n -Root 1 by Def1
    .= 1 by A1,PREPOWER:29;
end;

theorem Th8:
  a>=0 & n>=1 implies n-root a >= 0
proof
  assume
A1: a>=0 & n>=1;
  now per cases by A1;
    suppose
A2:   a>0;
      n-root a = n -Root a by A1,Def1;
      hence n-root a >= 0 by A1,A2,PREPOWER:def 3;
    end;
    suppose
A3:   a=0;
      n-root a = n -Root a by A1,Def1
        .= 0 by A1,A3,PREPOWER:def 3;
      hence n -root a >= 0;
    end;
  end;
  hence thesis;
end;

theorem
  (ex m st n=2*m+1) implies n-root (-1) = -1
proof
  assume
A1: ex m st n=2*m+1;
  then consider m such that
A2: n = 2*m + 1;
A3: n>=0+1 by A2,XREAL_1:8; thus
  n-root (-1) = - n -Root (-(-1)) by A1,Def1
    .= - 1 by A3,PREPOWER:29;
end;

theorem
  1-root a = a
proof
  now per cases;
    suppose
A1:   a>=0;
      hence 1-root a = 1 -Root a by Def1
        .= a by A1,PREPOWER:30;
    end;
    suppose
A2:   a<0;
      then
A3:   -a>=0;
      1 = 2*0 + 1;
      hence 1-root a = - 1 -Root (-a) by A2,Def1
        .= -(-a) by A3,PREPOWER:30
        .= a;
    end;
  end;
  hence thesis;
end;

theorem Th11:
  (ex m st n = 2*m + 1) implies n-root a = - n-root (-a)
proof
  assume
A1: (ex m st n = 2*m + 1);
  then consider m such that
A2: n = 2*m + 1;
A3: n>=0+1 by A2,XREAL_1:8;
  now per cases;
    suppose
A4:   a<0;
      then
A5:   -a>=0; thus
      n-root a = - n -Root (-a) by A1,A4,Def1
        .= - n-root (-a) by A3,A5,Def1;
    end;
    suppose
A6:   a=0;
      hence n-root a = 0 by A3,Th6
        .= - n-root (-a) by A3,A6,Th6;
    end;
    suppose
A7:   a>0;
      then -a<-0 by XREAL_1:26;
      hence - n-root (-a) = -(- n -Root (-(-a))) by A1,Def1
        .= n-root a by A3,A7,Def1;
    end;
  end;
  hence thesis;
end;

theorem Th12:
  n>=1 & a>=0 & b>=0 or (ex m st n=2*m+1) implies
  n-root (a*b) = n-root a * n-root b
proof
  assume
A1: n>=1 & a>=0 & b>=0 or ex m st n=2*m+1;
A2: now
    let a,b,n;
    assume
A3: n>=1 & a>=0 & b>=0;
    thus n-root (a*b) = n -Root (a*b) by A3,Def1
      .= n -Root a * n -Root b by A3,PREPOWER:31
      .= n-root a * n -Root b by A3,Def1
      .= n-root a * n-root b by A3,Def1;
  end;
  now
    assume
A4: ex m st n=2*m+1;
    then consider m such that
A5: n = 2*m + 1;
A6: n>=0+1 by A5,XREAL_1:8;
    now per cases;
      suppose a>=0 & b>=0;
        hence n-root (a*b) = n-root a * n-root b by A2,A6;
      end;
      suppose
A7:     a<0 & b<0;
        then
A8:     a*b>=0;
A9:     -a>=0 by A7;
A10:    -b>=0 by A7; thus
        n-root (a*b) = n -Root ((-a)*(-b)) by A6,A8,Def1
          .= (-(-n -Root (-a))) * n -Root (-b) by A6,A9,A10,PREPOWER:31
          .= (-(n-root a)) * (-(-n -Root (-b))) by A4,A7,Def1
          .= (-(n-root a)) * (-(n-root b)) by A4,A7,Def1
          .= n-root a * n-root b;
      end;
      suppose
A11:    a>=0 & b<0; thus
        n-root (a*b) = -n-root (-a*b) by A4,Th11
          .= -n-root (a*(-b))
          .= -(n-root a * n-root (-b)) by A2,A6,A11
          .= n-root a * (-n-root (-b))
          .= n-root a * n-root b by A4,Th11;
      end;
      suppose
A13:    a<0 & b>=0; thus
        n-root (a*b) = -n-root (-a*b) by A4,Th11
          .= -n-root ((-a)*b)
          .= -(n-root (-a) * n-root b) by A2,A6,A13
          .= (-n-root (-a)) * n-root b
          .= n-root a * n-root b by A4,Th11;
      end;
    end;
    hence n-root (a*b) = n-root a * n-root b;
  end;
  hence thesis by A1,A2;
end;

theorem Th13:
  a>0 & n>=1 or (a<>0 & ex m st n = 2*m+1) implies n-root (1/a) = 1/(n-root a)
proof
  assume
A1: a>0 & n>=1 or a<>0 & ex m st n = 2*m+1;
A2: now
    let a,n;
    assume
A3: a>0 & n>=1;
    thus n-root (1/a) = n -Root (1/a) by A3,Def1
      .= 1/(n -Root a) by A3,PREPOWER:32
      .= 1/(n-root a) by A3,Def1;
  end;
  now
    assume
A4: a<>0 & ex m st n = 2*m+1;
    then consider m such that
A5: n = 2*m + 1;
A6: n>=0+1 by A5,XREAL_1:8;
    now per cases by A4;
      suppose a>0;
        hence n-root (1/a) = 1/(n-root a) by A2,A6;
      end;
      suppose a<0;
        then
A7:     -a>0 by XREAL_1:60; thus
        1/(n-root a) = 1/(-(n-root (-a))) by A4,Th11
          .= - 1/(n-root (-a)) by XCMPLX_1:189
          .= - n-root (1/(-a)) by A2,A6,A7
          .= - n-root (-1/a) by XCMPLX_1:189
          .= n-root (1/a) by A4,Th11;
      end;
    end;
    hence n-root (1/a) = 1/(n-root a);
  end;
  hence thesis by A1,A2;
end;

theorem
  (a>=0 & b>0 & n>=1) or (b<>0 & ex m st n=2*m+1) implies
  n-root (a/b) = n-root a / n-root b
proof
  assume
A1: (a>=0 & b>0 & n>=1) or (b<>0 & ex m st n=2*m+1);
  now per cases by A1;
    suppose
A2:   a>=0 & b>0 & n>=1;
      thus n-root (a/b) = n -Root (a/b) by A2,Def1
        .= n -Root a / n -Root b by A2,PREPOWER:33
        .= n-root a / n -Root b by A2,Def1
        .= n-root a / n-root b by A2,Def1;
    end;
    suppose
A3:   b<>0 & ex m st n=2*m+1;
      thus n-root (a/b) = n-root (a*(1/b)) by XCMPLX_1:100
        .= n-root a * n-root (1/b) by A3,Th12
        .= n-root a * (1 / n-root b) by A3,Th13
        .= n-root a / n-root b by XCMPLX_1:100;
    end;
  end;
  hence thesis;
end;

theorem
  (a>=0 & n>=1 & m>=1) or (ex m1,m2 st n=2*m1+1 & m=2*m2+1) implies
  n-root (m-root a) = (n*m)-root a
proof
  assume
A1: (a>=0 & n>=1 & m>=1) or (ex m1,m2 st n=2*m1+1 & m=2*m2+1);
A2: now
    let a,n,m;
    assume
A3: a>=0 & n>=1 & m>=1;
    then
A4: n*m>= 1 by REAL_2:138;
    m-root a >= 0 by A3,Th8;
    then
A5: m -Root a >= 0 by A3,Def1; thus
    n-root (m-root a) = n-root (m -Root a) by A3,Def1
      .= n -Root (m -Root a) by A3,A5,Def1
      .= (n*m) -Root a by A3,PREPOWER:34
      .= (n*m)-root a by A3,A4,Def1;
  end;
  now
    given m1,m2 such that
A6: n=2*m1+1 & m=2*m2+1;
A7: n>=0+1 by A6,XREAL_1:8;
A8: m>=0+1 by A6,XREAL_1:8;
    then
A9: n*m >= 1 by A7,REAL_2:138;
A10: n*m = 2*(m1*(2*m2)+m1+m2) + 1 by A6;
    now per cases;
      suppose a>=0;
        hence n-root (m-root a) = (n*m)-root a by A2,A7,A8;
      end;
      suppose
A11:    a<0;
        then
A12:    -a>=0;
        then m-root (-a) >= 0 by A8,Th8;
        then
A13:    m -Root (-a) >= 0 by A8,A12,Def1; thus
        n-root (m-root a) = n-root (-m -Root (-a)) by A6,A11,Def1
          .= -n-root (-(-m -Root (-a))) by A6,Th11
          .= -n -Root (m -Root (-a)) by A7,A13,Def1
          .= -(n*m) -Root (-a) by A7,A8,A12,PREPOWER:34
          .= -(n*m)-root (-a) by A9,A12,Def1
          .= (n*m)-root a by A10,Th11;
      end;
    end;
    hence n-root (m-root a) = (n*m)-root a;
  end;
  hence thesis by A1,A2;
end;

theorem
  (a>=0 & n>=1 & m>=1) or (ex m1,m2 st n=2*m1+1 & m=2*m2+1) implies
  n-root a * m-root a = (n*m)-root (a |^ (n+m))
proof
  assume
A1: (a>=0 & n>=1 & m>=1) or (ex m1,m2 st n=2*m1+1 & m=2*m2+1);
A2: now
    let a,n,m;
    assume
A3: a>=0 & n>=1 & m>=1;
    then
A4: n*m>= 1 by REAL_2:138;
A5: a |^ (n+m) >=0 by A3,Th3; thus
    n-root a * m-root a = n-root a* m -Root a by A3,Def1
      .= n -Root a * m -Root a by A3,Def1
      .= (n*m) -Root (a |^ (n+m)) by A3,PREPOWER:35
      .= (n*m)-root (a |^ (n+m)) by A4,A5,Def1;
  end;
  now
    given m1,m2 such that
A6: n=2*m1+1 & m=2*m2+1;
A7: n>=0+1 by A6,XREAL_1:8;
A8: m>=0+1 by A6,XREAL_1:8;
    then
A9: n*m >= 1 by A7,REAL_2:138;
A10: n+m = 2*(m1+(1+m2)) by A6;
    now per cases;
      suppose a>=0;
        hence n-root a * m-root a = (n*m)-root (a |^ (n+m)) by A2,A7,A8;
      end;
      suppose
A11:    a<0;
        then
A12:    -a>=0;
A13:    a |^ (n+m) >= 0 by A10,Th3; thus
        n-root a * m-root a = n-root a * (-m -Root (-a)) by A6,A11,Def1
          .= (-n -Root (-a)) * (-m -Root (-a)) by A6,A11,Def1
          .= n -Root (-a) * m -Root (-a)
          .= (n*m) -Root ((-a) |^ (n+m))by A7,A8,A12,PREPOWER:35
          .= (n*m) -Root (a |^ (n+m)) by A10,Th1
          .= (n*m)-root (a |^ (n+m)) by A9,A13,Def1;
      end;
    end;
    hence n-root a * m-root a = (n*m)-root (a |^ (n+m));
  end;
  hence thesis by A1,A2;
end;

theorem
  a<=b & (0<=a & n>=1 or ex m st n=2*m+1) implies n-root a <= n-root b
proof
  assume that
A1: a<=b and
A2: 0<=a & n>=1 or ex m st n=2*m+1;
A3: now
    let a,b,n;
    assume
A4: 0<=a & n>=1 & a<=b;
    then n -Root a <= n -Root b by PREPOWER:36;
    then n -Root a <= n-root b by A4,Def1;
    hence n-root a <= n-root b by A4,Def1;
  end;
  now
    assume
A5: ex m st n=2*m+1;
    then consider m such that
A6: n = 2*m + 1;
A7: n>=0+1 by A6,XREAL_1:8;
    now per cases;
      suppose a>=0;
        hence n-root a <= n-root b by A1,A3,A7;
      end;
      suppose a<0;
        then
A8:     -a>=0;
        now per cases;
          suppose b>=0;
            then
A9:         n-root b >= 0 by A7,Th8;
            n-root (-a) >= 0 by A7,A8,Th8;
            then - n-root (-a) <= -0;
            hence n-root a <= n-root b by A5,A9,Th11;
          end;
          suppose b<0;
            then
A10:        -b>=0;
            -a>=-b by A1,XREAL_1:26;
            then n-root (-a) >= n-root (-b) by A3,A7,A10;
            then - n-root (-a) <= - n-root (-b) by XREAL_1:26;
            then n-root a <= - n-root (-b) by A5,Th11;
            hence n-root a <= n-root b by A5,Th11;
          end;
        end;
        hence n-root a <= n-root b;
      end;
    end;
    hence n-root a <= n-root b;
  end;
  hence thesis by A1,A2,A3;
end;

theorem
  a<b & (a>=0 & n>=1 or ex m st n=2*m+1) implies n-root a < n-root b
proof
  assume that
A1: a<b and
A2: 0<=a & n>=1 or ex m st n=2*m+1;
A3: now
    let a,b,n;
    assume
A4: 0<=a & n>=1 & a<b;
    then n -Root a < n -Root b by PREPOWER:37;
    then n -Root a < n-root b by A4,Def1;
    hence n-root a < n-root b by A4,Def1;
  end;
  now
    assume
A5: ex m st n=2*m+1;
    then consider m such that
A6: n = 2*m + 1;
A7: n>=0+1 by A6,XREAL_1:8;
    now per cases;
      suppose a>=0;
        hence n-root a < n-root b by A1,A3,A7;
      end;
      suppose
A8:     a<0;
        then
A9:     -a>0 by XREAL_1:60;
        now per cases;
          suppose b>=0;
            then
A10:        n-root b >= 0 by A7,Th8;
            n -Root (-a) > 0 by A7,A9,PREPOWER:def 3;
            then - n -Root (-a) < -0 by XREAL_1:26;
            hence n-root a < n-root b by A5,A8,A10,Def1;
          end;
          suppose b<0;
            then
A11:        -b>=0;
            -a>-b by A1,XREAL_1:26;
            then n-root (-a) > n-root (-b) by A3,A7,A11;
            then - n-root (-a) < - n-root (-b) by XREAL_1:26;
            then n-root a < - n-root (-b) by A5,Th11;
            hence n-root a < n-root b by A5,Th11;
          end;
        end;
        hence n-root a < n-root b;
      end;
    end;
    hence n-root a < n-root b;
  end;
  hence thesis by A1,A2,A3;
end;

theorem Th19:
  a>=1 & n>=1 implies n-root a >= 1 & a >= n-root a
proof
  assume
A1: a>=1 & n>=1;
  then n -Root a >= 1 & a >= n -Root a by PREPOWER:38;
  hence n-root a >= 1 & a >= n-root a by A1,Def1;
end;

theorem
  a<=-1 & (ex m st n=2*m+1) implies n-root a <= -1 & a <= n-root a
proof
  assume
A1: a<=-1 & (ex m st n=2*m+1);
  then consider m such that
A2: n = 2*m + 1;
A3: n>=0+1 by A2,XREAL_1:8;
  -a>=1 by A1,REAL_2:109;
  then n-root (-a) >= 1 & -a >= n-root (-a) by A3,Th19;
  then - n-root (-a) <= -1 & a <= - n-root (-a) by REAL_2:109,XREAL_1:26;
  hence thesis by A1,Th11;
end;

theorem Th21:
  a>=0 & a<1 & n>=1 implies a <= n-root a & n-root a < 1
proof
  assume
A1: a>=0 & a<1 & n>=1;
  then a <= n -Root a & n -Root a < 1 by PREPOWER:39;
  hence a <= n-root a & n -root a < 1 by A1,Def1;
end;

theorem
  a>-1 & a<=0 & (ex m st n=2*m+1) implies a >= n-root a & n-root a > -1
proof
  assume
A1: a>-1 & a<=0 & (ex m st n=2*m+1);
  then consider m such that
A2: n = 2*m + 1;
A3: n>=0+1 by A2,XREAL_1:8;
A4: -a<1 by A1,REAL_2:109;
  -a>=-0 by A1;
  then n-root (-a) < 1 & -a <= n-root (-a) by A3,A4,Th21;
  then - n-root (-a) > -1 & a >= - n-root (-a) by REAL_2:110,XREAL_1:26;
  hence thesis by A1,Th11;
end;

theorem Th23:
  a>0 & n>=1 implies n-root a - 1 <= (a-1)/n
proof
  assume
A1: a>0 & n>=1;
  then n -Root a - 1 <= (a-1)/n by PREPOWER:40;
  hence thesis by A1,Def1;
end;

theorem Th24:
  for s being Real_Sequence, a st a > 0 & (for n st n>=1 holds s.n = n-root a)
  holds s is convergent & lim s = 1
proof
  let s be Real_Sequence, a;
  assume that
A1: a > 0 and
A2: for n st n>=1 holds s.n = n-root a;
  now
    let n;
    assume
A3: n>=1;
    hence s.n = n-root a by A2
      .= n -Root a by A1,A3,Def1;
  end;
  hence thesis by A1,PREPOWER:42;
end;

definition
  let a,b be real number;
  func a to_power b -> real number means
  :Def2:
  it = a #R b if a > 0,
  it = 0 if a = 0 & b > 0,
  ex k st k = b & it = a #Z k if b is Integer;
  consistency
   proof  let IT be real number;
    thus a > 0 & a = 0 & b > 0 implies
     (IT = a #R b iff IT = 0);
    thus a > 0 & b is Integer implies
     (IT = a #R b iff ex k st k = b & IT = a #Z k)
     proof
      assume
Z1:    a > 0;
      assume b is Integer;
       then reconsider b as Integer;
       a #R b = a #Q b by Z1,PREPOWER:88
          .= a #Z b by Z1,PREPOWER:113;
      hence thesis;
     end;
     a = 0 & b > 0 & b is Integer implies
      (IT = 0 iff ex k st k = b & IT = a #Z k)
     proof assume
Z1:    a = 0;
      assume
Z2:    b > 0 & b is Integer;
       then b in NAT by INT_1:16;
       then reconsider b as Nat;
        a #Z b = a |^ b by PREPOWER:46
              .= 0 by Z1,NEWTON:16,Z2,NAT_1:14;
      hence thesis;
     end;
    hence thesis;
   end;
  existence
  proof now assume b is Integer;
      then reconsider k = b as Integer;
      take c = a #Z k;
      thus ex k st k = b & c = a #Z k;
    end;
    hence thesis;
  end;
  uniqueness;
end;

definition
  let a,b be Real;
  redefine func a to_power b -> Real;
  coherence by XREAL_0:def 1;
end;

canceled 4;

theorem Th29:
  a to_power 0 = 1
proof
 thus a to_power 0 = a #Z 0 by Def2
     .= 1 by PREPOWER:44;
end;

theorem Th30:
  a to_power 1 = a
proof
 thus a to_power 1 = a #Z 1 by Def2
     .= a by PREPOWER:45;
end;

theorem Th31:
  1 to_power a = 1
proof
A1: 1 #R a <> 0 by PREPOWER:95; thus
  1 to_power a = (1/1) #R a by Def2
    .= (1 #R a) / (1 #R a) by PREPOWER:94
    .= 1 by A1,XCMPLX_1:60;
end;

theorem Th32:
  a > 0 implies a to_power (b+c) = a to_power b * a to_power c
proof
  assume
A1: a > 0;
  then a #R (b+c) = a #R b * a #R c by PREPOWER:89;
  then a #R (b+c) = a #R b * a to_power c by A1,Def2;
  then a #R (b+c) = a to_power b * a to_power c by A1,Def2;
  hence thesis by A1,Def2;
end;

theorem Th33:
  a > 0 implies a to_power (-c) = 1 / a to_power c
proof
  assume
A1: a > 0;
  then a #R (-c) = 1 / a #R c by PREPOWER:90;
  then a #R (-c) = 1 / a to_power c by A1,Def2;
  hence thesis by A1,Def2;
end;

theorem Th34:
  a > 0 implies a to_power (b-c) = a to_power b / a to_power c
proof
  assume
A1: a > 0;
  then a #R (b-c) = a #R b / a #R c by PREPOWER:91;
  then a #R (b-c) = a #R b / a to_power c by A1,Def2;
  then a #R (b-c) = a to_power b / a to_power c by A1,Def2;
  hence thesis by A1,Def2;
end;

theorem
  a>0 & b>0 implies (a*b) to_power c = a to_power c*b to_power c
proof
  assume
A1: a > 0 & b > 0;
  then
A2: a * b > 0 by XREAL_1:131;
  (a * b) #R c = a #R c * b #R c by A1,PREPOWER:92;
  then (a * b) #R c = a #R c * b to_power c by A1,Def2;
  then (a * b) #R c = a to_power c * b to_power c by A1,Def2;
  hence thesis by A2,Def2;
end;

theorem Th36:
  a>0 & b>0 implies (a/b) to_power c = a to_power c/b to_power c
proof
  assume
A1: a > 0 & b > 0;
  then
A2: a / b > 0 by XREAL_1:141;
  (a / b) #R c = a #R c / b #R c by A1,PREPOWER:94;
  then (a / b) #R c = a #R c / b to_power c by A1,Def2;
  then (a / b) #R c = a to_power c / b to_power c by A1,Def2;
  hence thesis by A2,Def2;
end;

theorem Th37:
  a>0 implies (1/a) to_power b = a to_power (-b)
proof
  assume
A1: a>0;
  then 1/a>0 by REAL_2:127;
  hence (1/a) to_power b = (1/a) #R b by Def2
    .= 1/a #R b by A1,PREPOWER:93
    .= 1/a to_power b by A1,Def2
    .= a to_power (-b) by A1,Th33;
end;

theorem Th38:
  a > 0 implies a to_power b to_power c = a to_power (b * c)
proof
  assume
A1: a > 0;
  then
A2: a #R b > 0 by PREPOWER:95;
  a #R b #R c = a #R (b * c) by A1,PREPOWER:105;
  then a #R b #R c = a to_power (b * c) by A1,Def2;
  then a #R b to_power c = a to_power (b * c) by A2,Def2;
  hence thesis by A1,Def2;
end;

theorem Th39:
  a > 0 implies a to_power b > 0
proof
  assume
A1: a > 0;
  then a #R b > 0 by PREPOWER:95;
  hence thesis by A1,Def2;
end;

theorem Th40:
  a > 1 & b > 0 implies a to_power b > 1
proof
  assume
A1: a > 1 & b > 0;
  then a #R b > 1 by PREPOWER:100;
  hence thesis by A1,Def2;
end;

theorem Th41:
  a > 1 & b < 0 implies a to_power b < 1
proof
  assume
A1: a > 1 & b < 0;
  then a #R b < 1 by PREPOWER:102;
  hence thesis by A1,Def2;
end;

theorem
  a > 0 & a < b & c > 0 implies a to_power c < b to_power c
proof
  assume
A1: a > 0 & a < b & c > 0;
  then
A2: a to_power c > 0 by Th39;
A3: a to_power c <> 0 by A1,Th39;
  a/a<b/a by A1,XREAL_1:76;
  then 1<b/a by A1,XCMPLX_1:60;
  then (b/a) to_power c > 1 by A1,Th40;
  then b to_power c / a to_power c > 1 by A1,Th36;
  then b to_power c/a to_power c*a to_power c>1*a to_power c by A2,XREAL_1:70;
  hence thesis by A3,XCMPLX_1:88;
end;

theorem
  a > 0 & a < b & c < 0 implies a to_power c > b to_power c
proof
  assume
A1: a > 0 & a < b & c < 0;
  then
A2: a to_power c > 0 by Th39;
A3: a to_power c <> 0 by A1,Th39;
  a/a<b/a by A1,XREAL_1:76;
  then 1<b/a by A1,XCMPLX_1:60;
  then (b/a) to_power c < 1 by A1,Th41;
  then b to_power c / a to_power c < 1 by A1,Th36;
  then b to_power c/a to_power c*a to_power c <1*a to_power c by A2,XREAL_1:70;
  hence thesis by A3,XCMPLX_1:88;
end;

theorem Th44:
  a < b & c > 1 implies c to_power a < c to_power b
proof
  assume
A1: a < b & c > 1;
  then
A2: c to_power a > 0 by Th39;
A3: c to_power a <> 0 by A1,Th39;
  b-a>0 by A1,XREAL_1:52;
  then c to_power (b-a) > 1 by A1,Th40;
  then c to_power b / c to_power a > 1 by A1,Th34;

then c to_power b/c to_power a*c to_power a > 1*c to_power a by A2,XREAL_1:70;
  hence thesis by A3,XCMPLX_1:88;
end;

theorem Th45:
  a < b & c > 0 & c < 1 implies c to_power a > c to_power b
proof
  assume
A1: a < b & c > 0 & c < 1;
  then
A2: (1/c)>0 by XREAL_1:141;
  then
A3: (1/c) to_power a > 0 by Th39;
A4: (1/c) to_power a <> 0 by A2,Th39;
  c to_power a > 0 by A1,Th39;
  then
A5: 1/c to_power a > 0 by REAL_2:127;
  c/c <1/c by A1,XREAL_1:76;
  then
A6: 1<1/c by A1,XCMPLX_1:60;
  b-a>0 by A1,XREAL_1:52;
  then (1/c) to_power (b-a) > 1 by A6,Th40;
  then (1/c) to_power b / (1/c) to_power a > 1 by A1,Th34,XREAL_1:141;
  then (1/c) to_power b/(1/c) to_power a*(1/c) to_power a >
  1*(1/c) to_power a by A3,XREAL_1:70;
  then (1/c) to_power b > (1/c) to_power a by A4,XCMPLX_1:88;
  then 1 to_power b/c to_power b > (1/c) to_power a by A1,Th36;
  then 1 / c to_power b > (1/c) to_power a by Th31;
  then 1 / c to_power b > 1 to_power a/c to_power a by A1,Th36;
  then 1 / c to_power b > 1 / c to_power a by Th31;
  hence thesis by A5,REAL_2:154;
end;

registration let a be real number, n be Nat;
 identify a to_power n with a |^ n;
 compatibility
  proof
   thus a to_power n = a #Z n by Def2
        .= a |^ n by PREPOWER:46;
  end;
end;

theorem
  for n be Nat holds a to_power n = a |^ n;

canceled 3;

theorem
  a to_power k = a #Z k by Def2;

theorem Th51:
  a>0 implies a to_power p = a #Q p
proof
  assume
A1: a>0;
  hence a to_power p = a #R p by Def2
    .= a #Q p by A1,PREPOWER:88;
end;

theorem Th52:
  a>=0 & n>=1 implies a to_power (1/n) = n-root a
proof
  assume
A1: a>=0 & n>=1;
A2: 1/n = n" by XCMPLX_1:217;
  reconsider p=n" as Rational;
A3: 1/n>0 by A1,A2,XREAL_1:124;
  now per cases by A1;
    suppose
A4:   a>0;
      hence a to_power (1/n) = a #Q p by A2,Th51
        .= n -Root a by A1,A4,PREPOWER:61;
    end;
    suppose
A5:   a=0;
      hence a to_power (1/n) = 0 by A3,Def2
        .= n -Root a
      by A1,A5,PREPOWER:def 3;
    end;
  end;
  hence a to_power (1/n) = n-root a by A1,Def1;
end;

theorem Th53:
  a to_power 2 = a^2
proof
  now per cases;
    suppose
A1:   a>0; thus
      a to_power 2 = a to_power (1+1)
        .= a to_power 1 * a to_power 1 by A1,Th32
        .= a to_power 1 * a by Th30
        .= a^2 by Th30;
    end;
    suppose a=0;
      hence a to_power 2 = a^2 by Def2;
    end;
    suppose
A2:   a<0;
      reconsider l=1 as Integer; thus
      a to_power 2 = a #Z (l+l) by Def2
        .= a #Z l * a #Z l by A2,PREPOWER:54
        .= a * a #Z l by PREPOWER:45
        .= a^2 by PREPOWER:45;
    end;
  end;
  hence thesis;
end;

theorem Th54:
  a<>0 & (ex l st k = 2*l) implies (-a) to_power k = a to_power k
proof
  assume
A1: a<>0;
  given l such that
A2: k=2*l;
  reconsider l1=2 as Integer;
  now per cases by A1;
    suppose
A3:   a>0;
     thus
      a to_power k = (a to_power 2) to_power l by A2,A3,Th38
        .= (a^2) to_power l by Th53
        .= ((-a)^2) to_power l
        .= ((-a) to_power 2) to_power l by Th53
        .= ((-a) #Z l1) to_power l by Def2
        .= ((-a) #Z l1) #Z l by Def2
        .= (-a) #Z (l1*l) by PREPOWER:55
        .= (-a) to_power k by A2,Def2;
    end;
    suppose a<0;
      then -a>0 by XREAL_1:60;
      hence (-a) to_power k = ((-a) to_power 2) to_power l by A2,Th38
        .= ((-a)^2) to_power l by Th53
        .= (a^2) to_power l
        .= (a to_power 2) to_power l by Th53
        .= (a #Z l1) to_power l by Def2
        .= (a #Z l1) #Z l by Def2
        .= a #Z (l1*l) by PREPOWER:55
        .= a to_power k by A2,Def2;
    end;
  end;
  hence thesis;
end;

theorem
  a<>0 & (ex l st k = 2*l + 1) implies (-a) to_power k = -(a to_power k)
proof
  assume
A1: a<>0;
  given l such that
A2: k=2*l + 1;
  reconsider l1=1 as Integer;
  now per cases by A1;
    suppose
A3:   a>0;
A5:   -a<>-0 by A3;
      a to_power k = a to_power (2*l) * a to_power 1 by A2,A3,Th32
        .= a to_power (2*l) * a by Th30
        .= (-a) to_power (2*l) * a by A1,Th54
        .= - (-a) to_power (2*l) * (-a)
        .= - (-a) #Z (2*l) * (-a) by Def2
        .= - (-a) #Z (2*l) * (-a) #Z l1 by PREPOWER:45
        .= - (-a) #Z k by A2,A5,PREPOWER:54
        .= - (-a) to_power k by Def2;
      hence (-a) to_power k = - a to_power k;
    end;
    suppose
A6:   a<0;
      then -a>0 by XREAL_1:60;
      hence (-a) to_power k = (-a) to_power (2*l) * (-a) to_power 1 by A2,Th32
        .= (-a) to_power (2*l) * (-a) by Th30
        .= a to_power (2*l) * (-a) by A1,Th54
        .= - a to_power (2*l) * a
        .= - a #Z (2*l) * a by Def2
        .= - a #Z (2*l) * a #Z l1 by PREPOWER:45
        .= - a #Z k by A2,A6,PREPOWER:54
        .= - a to_power k by Def2;
    end;
  end;
  hence thesis;
end;

theorem
  -1 < a implies (1 + a) to_power n >= 1 + n * a by PREPOWER:24;

theorem Th57:
  a>0 & a<>1 & c <>d implies a to_power c <> a to_power d
proof
  assume
A1: a>0 & a<>1 & c <>d;
  now per cases by A1,XXREAL_0:1;
    suppose
A2:   c <d;
      now per cases by A1,XXREAL_0:1;
        suppose a<1;
          hence a to_power c <> a to_power d by A1,A2,Th45;
        end;
        suppose a>1;
          hence a to_power c <> a to_power d by A2,Th44;
        end;
      end;
      hence a to_power c <> a to_power d;
    end;
    suppose
A3:   c>d;
      now per cases by A1,XXREAL_0:1;
        suppose a<1;
          hence a to_power c <> a to_power d by A1,A3,Th45;
        end;
        suppose a>1;
          hence a to_power c <> a to_power d by A3,Th44;
        end;
      end;
      hence a to_power c <> a to_power d;
    end;
  end;
  hence thesis;
end;

definition
  let a,b be real number;
  assume that
A1: a>0 & a<>1 and
A2: b>0;
  func log(a,b) -> real number means
  :Def3:
  a to_power it = b;
  existence
  proof
    reconsider a1=a, b1=b as Real by XREAL_0:def 1;
    set X = {c where c is Real: a to_power c <= b};
    X is Subset of REAL
    proof
      for x holds x in X implies x in REAL
      proof
        let x;
        assume x in X;
        then ex c be Real st x = c & a to_power c <= b;
        hence thesis;
      end;
      hence thesis by TARSKI:def 3;
    end;
    then reconsider X as Subset of REAL;
A3: ex d st d in X
    proof
A4:   now
        let a,b be real number such that
A5:     a>1 & b>0;
        reconsider a1=a, b1=b as Real by XREAL_0:def 1;
        set e = a1-1;
        consider n such that
A6:     n>1/(b*e) by SEQ_4:10;
        e>0-1 by A5,XREAL_1:11;
        then
A7:     (1 + e) to_power n >= 1 + n * e by PREPOWER:24;
        1 + n * e >= 0 + n * e by XREAL_1:8;
        then
A8:     (1 + e) to_power n >= n * e by A7,XXREAL_0:2;
A9:     e>1-1 by A5,XREAL_1:11;
        then n*e>1/(b*e)*e by A6,XREAL_1:70;
        then n*e>=1/b by A9,XCMPLX_1:93;
        then
A10:    a to_power n >= 1/b by A8,XXREAL_0:2;
        1/b > 0 by A5,REAL_2:127;
        then 1/a to_power n <= 1/(1/b) by A10,REAL_2:152;
        then 1/a to_power n <= b by XCMPLX_1:56;
        then a1 to_power (-n) <= b1 by A5,Th33;
        hence ex d st a to_power d <= b;
      end;
      now per cases by A1,XXREAL_0:1;
        suppose a>1;
          hence ex d st a to_power d <= b by A2,A4;
        end;
        suppose a<1;
          then 1/a >1/1 by A1,REAL_2:151;
          then consider d such that
A11:      (1/a) to_power d <= b by A2,A4;
          a1 to_power (-d) <= b1 by A1,A11,Th37;
          hence ex e st a to_power e <= b;
        end;
      end;
      then consider d such that
A12:  a to_power d <= b;
      take d;
      d is Real by XREAL_0:def 1;
      hence d in X by A12;
    end;
    now per cases by A1,XXREAL_0:1;
      suppose
A13:    a>1;
A14:    X is bounded_above
        proof consider n such that
A15:      n > (b1-1)/(a1-1) by SEQ_4:10;
          a-1>1-1 by A13,XREAL_1:11;
          then n*(a-1) > b-1 by A15,REAL_2:177;
          then
A16:      1 + n*(a-1) > 1+(b-1) by XREAL_1:8;
          a-1>0-1 by A1,XREAL_1:11;
          then (1+(a1-1)) to_power n >= 1 + n*(a1-1) by PREPOWER:24;
          then
A17:      a to_power n > b by A16,XXREAL_0:2;
          now
            let c be real number;
            assume c in X;
            then ex d be Real st d=c & a to_power d <= b;
            then a1 to_power c <= a1 to_power n by A17,XXREAL_0:2;
            hence c <=n by A13,Th44;
          end;
          hence thesis by SEQ_4:def 1;
        end;
        take d = upper_bound X;
A18:    not b < a to_power d
        proof
          assume a to_power d > b;
          then consider e be real number such that
A19:      b<e & e<a to_power d by XREAL_1:7;
          reconsider e as Real by XREAL_0:def 1;
          consider n such that
A20:      n > (a1-1)/(e/b1-1) by SEQ_4:10;
          reconsider m = max(1,n) as Element of NAT by XXREAL_0:16;
          n<=m by XXREAL_0:25;
          then
A21:      (a-1)/(e/b-1) < m by A20,XXREAL_0:2;
          e/b>1 by A2,A19,REAL_2:142;
          then e/b-1>1-1 by XREAL_1:11;
          then
A22:      (a-1)<m*(e/b-1) by A21,REAL_2:177;
A23:      1<=m by XXREAL_0:25;
          then (a-1)/m<e/b-1 by A22,REAL_2:178;
          then
A24:      (a-1)/m+1<e/b-1+1 by XREAL_1:8;
          m-root a1 - 1 <= (a1-1)/m by A1,Th23,XXREAL_0:25;
          then m-root a - 1 + 1 <= (a-1)/m + 1 by XREAL_1:8;
          then m-root a <= e/b by A24,XXREAL_0:2;
          then a1 to_power (1/m) <= e/b1 by A1,Th52,XXREAL_0:25;
          then
A25:      a to_power (1/m) * b <= e by A2,REAL_2:178;
          1/m>0 by A23,REAL_2:127;
          then consider c be real number such that
A26:      c in X & d-1/m < c by A3,A14,SEQ_4:def 4;
          reconsider c as Real by XREAL_0:def 1;
A27:      ex g being Real st g=c & a to_power g <= b by A26;
          a1 to_power (1/m) >= 0 by A1,Th39;

then a to_power c * a to_power (1/m) <= a to_power (1/m)*b by A27,XREAL_1:66;
          then a to_power c * a to_power (1/m) <= e by A25,XXREAL_0:2;
          then
A28:      a1 to_power (c + 1/m) <= e by A1,Th32;
          d<c + 1/m by A26,XREAL_1:21;
          then a1 to_power d <= a1 to_power (c + 1/m) by A13,Th44;
          hence contradiction by A19,A28,XXREAL_0:2;
        end;
        not a to_power d < b
        proof
          assume a to_power d < b;
          then consider e being real number such that
A29:      a to_power d<e & e<b by XREAL_1:7;
          reconsider e as Real by XREAL_0:def 1;
A30:      a1 to_power d > 0 by A1,Th39;
          then b/e>1 by A29,REAL_2:142;
          then
A31:      b/e-1>1-1 by XREAL_1:11;
          deffunc F(Element of NAT) = $1-root a;
          consider s being Real_Sequence such that
A32:      for n holds s.n = F(n) from SEQ_1:sch 1;
          for n st n>=1 holds s.n = n-root a1 by A32;
          then s is convergent & lim s = 1 by A1,Th24;
          then consider n such that
A33:      for m st m>=n holds abs(s.m - 1)<b/e-1 by A31,SEQ_2:def 7;
          reconsider m = max(1,n) as Element of NAT by XXREAL_0:16;
A34:      m>=n & m>=1 by XXREAL_0:25;
          abs(s.m - 1)<b/e-1 by A33,XXREAL_0:25;
          then
A35:      abs(m-root a - 1)<b/e-1 by A32;
          m-root a - 1<=abs(m-root a - 1) by ABSVALUE:11;
          then m-root a - 1<b/e-1 by A35,XXREAL_0:2;
          then m-root a < b/e by XREAL_1:11;
          then a1 to_power (1/m) < b1/e by A1,Th52,XXREAL_0:25;

then a to_power d * a to_power (1/m) < b/e * a to_power d by A30,XREAL_1:70;
          then a1 to_power (d + 1/m) < b1/e * a1 to_power d by A1,Th32;
          then a to_power (d + 1/m) < b * a to_power d / e by XCMPLX_1:75;
          then
A36:      a to_power (d + 1/m) < a to_power d / e * b by XCMPLX_1:75;
          a to_power d / e < 1 by A29,A30,REAL_2:142;
          then a to_power d / e * b < 1*b by A2,XREAL_1:70;
          then a to_power (d + 1/m) <= b by A36,XXREAL_0:2;
          then d + 1/m in X;
          then d + 1/m <= d by A14,SEQ_4:def 4;
          then 1/m <= 0 by REAL_2:174;
          hence contradiction by A34,REAL_2:127;
        end;
        hence b = a to_power d by A18,XXREAL_0:1;
      end;
      suppose
A37:    a<1;
A38:    X is bounded_below
        proof consider n such that
A39:      n > (b1-1)/(1/a1-1) by SEQ_4:10;
          1/a >1/1 by A1,A37,REAL_2:151;
          then 1/a-1>1-1 by XREAL_1:11;
          then n*(1/a-1) > b-1 by A39,REAL_2:177;
          then
A40:      1 + n*(1/a-1) > 1+(b-1) by XREAL_1:8;
          1/a>0 by A1,REAL_2:127;
          then 1/a-1>0-1 by XREAL_1:11;
          then (1+(1/a1-1)) to_power n >= 1 + n*(1/a1-1) by PREPOWER:24;
          then (1/a) to_power n > b by A40,XXREAL_0:2;
          then
A41:      a1 to_power (-n) > b1 by A1,Th37;
          now
            let c be real number;
            assume c in X;
            then ex d be Real st d=c & a to_power d <= b;
            then a1 to_power c <= a1 to_power (-n) by A41,XXREAL_0:2;
            hence c>=-n by A1,A37,Th45;
          end;
          hence thesis by SEQ_4:def 2;
        end;
        take d = lower_bound X;
A42:    not b < a to_power d
        proof
          assume a to_power d > b;
          then consider e being real number such that
A43:      b<e & e<a to_power d by XREAL_1:7;
          reconsider e as Real by XREAL_0:def 1;
          consider n such that
A44:      n > (1/a1-1)/(e/b1-1) by SEQ_4:10;
          reconsider m = max(1,n) as Element of NAT by XXREAL_0:16;
          n<=m by XXREAL_0:25;
          then
A45:      (1/a-1)/(e/b-1) < m by A44,XXREAL_0:2;
          e/b>1 by A2,A43,REAL_2:142;
          then e/b-1>1-1 by XREAL_1:11;
          then
A46:      (1/a-1)<m*(e/b-1) by A45,REAL_2:177;
A47:      1<=m by XXREAL_0:25;
          then (1/a-1)/m<e/b-1 by A46,REAL_2:178;
          then
A48:      (1/a-1)/m+1<e/b-1+1 by XREAL_1:8;
A49:      1/a>0 by A1,REAL_2:127;
          then m-root (1/a1) - 1 <= (1/a1-1)/m by Th23,XXREAL_0:25;
          then m-root (1/a) - 1 + 1 <= (1/a-1)/m + 1 by XREAL_1:8;
          then m-root (1/a) <= e/b by A48,XXREAL_0:2;
          then (1/a1) to_power (1/m) <= e/b1 by A49,Th52,XXREAL_0:25;
          then (1/a) to_power (1/m) * b <= e by A2,REAL_2:178;
          then
A50:      a1 to_power (-1/m) * b1 <= e by A1,Th37;
          1/m>0 by A47,REAL_2:127;
          then consider c be real number such that
A51:      c in X & c <d+1/m by A3,A38,SEQ_4:def 5;
          reconsider c as Real by XREAL_0:def 1;
A52:      ex g being Real st g=c & a to_power g <= b by A51;
          a1 to_power (-1/m) >= 0 by A1,Th39;

then a to_power c*a to_power (-1/m) <= a to_power (-1/m)*b by A52,XREAL_1:66;
          then a to_power c * a to_power (-1/m) <= e by A50,XXREAL_0:2;
          then
A53:      a1 to_power (c + -1/m) <= e by A1,Th32;
          d>c - 1/m by A51,XREAL_1:21;
          then a1 to_power d <= a1 to_power (c - 1/m) by A1,A37,Th45;
          hence contradiction by A43,A53,XXREAL_0:2;
        end;
        not a to_power d < b
        proof
          assume a to_power d < b;
          then consider e being real number such that
A54:      a to_power d<e & e<b by XREAL_1:7;
          reconsider e as Real by XREAL_0:def 1;
A55:      a1 to_power d > 0 by A1,Th39;
          then b/e>1 by A54,REAL_2:142;
          then
A56:      b/e-1>1-1 by XREAL_1:11;
          deffunc F(Element of NAT) = $1-root (1/a);
          consider s being Real_Sequence such that
A57:      for n holds s.n = F(n) from SEQ_1:sch 1;
A58:      1/a>0 by A1,REAL_2:127;
          for n st n>=1 holds s.n = n-root (1/a1) by A57;
          then s is convergent & lim s = 1 by A58,Th24;
          then consider n such that
A59:      for m st m>=n holds abs(s.m - 1)<b/e-1 by A56,SEQ_2:def 7;
          reconsider m = max(1,n) as Element of NAT by XXREAL_0:16;
A60:      m>=n & m>=1 by XXREAL_0:25;
          abs(s.m - 1)<b/e-1 by A59,XXREAL_0:25;
          then
A61:      abs(m-root (1/a) - 1)<b/e-1 by A57;
          m-root (1/a) - 1<=abs(m-root (1/a) - 1) by ABSVALUE:11;
          then m-root (1/a) - 1<b/e-1 by A61,XXREAL_0:2;
          then m-root (1/a) < b/e by XREAL_1:11;
          then (1/a1) to_power (1/m) < b1/e by A58,Th52,XXREAL_0:25;

then a to_power d*(1/a) to_power (1/m)<b/e*a to_power d by A55,XREAL_1:70;
          then a1 to_power d*a1 to_power (-1/m)<b1/e*a1 to_power d by A1,Th37;
          then a1 to_power (d +- 1/m) < b1/e * a1 to_power d by A1,Th32;
          then a to_power (d - 1/m) < b * a to_power d / e by XCMPLX_1:75;
          then
A62:      a to_power (d - 1/m) < a to_power d / e * b by XCMPLX_1:75;
          a to_power d / e < 1 by A54,A55,REAL_2:142;
          then a to_power d / e * b < 1*b by A2,XREAL_1:70;
          then a to_power (d - 1/m) < b by A62,XXREAL_0:2;
          then d - 1/m in X;
          then d - 1/m >= d by A38,SEQ_4:def 5;
          then 1/m <= 0 by REAL_2:174;
          hence contradiction by A60,REAL_2:127;
        end;
        hence b = a to_power d by A42,XXREAL_0:1;
      end;
    end;
    hence thesis;
  end;
  uniqueness by A1,Th57;
end;

definition
  let a,b be Real;
  redefine func log(a,b) -> Real;
  coherence by XREAL_0:def 1;
end;

canceled;

theorem
  a>0 & a<>1 implies log(a,1) = 0
proof
  assume
A1: a>0 & a<>1;
  a to_power 0 = 1 by Th29;
  hence thesis by A1,Def3;
end;

theorem
  a>0 & a<>1 implies log(a,a) = 1
proof
  assume
A1: a>0 & a<>1;
  a to_power 1 = a by Th30;
  hence thesis by A1,Def3;
end;

theorem
  a>0 & a<>1 & b>0 & c>0 implies log(a,b) + log(a,c) = log(a,b*c)
proof
  assume
A1: a>0 & a<>1 & b>0 & c>0;
  then
A2: a to_power (log(a,b) + log(a,c))
  = a to_power log(a,b) * a to_power log(a,c) by Th32
    .= b * a to_power log(a,c) by A1,Def3
    .= b * c by A1,Def3;
  b*c>0 by A1,REAL_2:122;
  hence thesis by A1,A2,Def3;
end;

theorem
  a>0 & a<>1 & b>0 & c>0 implies log(a,b) - log(a,c) = log(a,b/c)
proof
  assume
A1: a>0 & a<>1 & b>0 & c>0;
  then
A2: a to_power (log(a,b) - log(a,c))
  = a to_power log(a,b) / a to_power log(a,c) by Th34
    .= b / a to_power log(a,c) by A1,Def3
    .= b / c by A1,Def3;
  b/c>0 by A1,REAL_2:127;
  hence thesis by A1,A2,Def3;
end;

theorem
  a>0 & a<>1 & b>0 implies log(a,b to_power c) = c * log(a,b)
proof
  assume
A1: a>0 & a<>1 & b>0;
  then
A2: b to_power c > 0 by Th39;
  a to_power (c*log(a,b)) = (a to_power log(a,b)) to_power c by A1,Th38
    .= b to_power c by A1,Def3;
  hence thesis by A1,A2,Def3;
end;

theorem
  a>0 & a<>1 & b>0 & b<>1 & c>0 implies log(a,c) = log(a,b)*log(b,c)
proof
  assume
A1: a>0 & a<>1 & b>0 & b<>1 & c>0;
  then a to_power (log(a,b)*log(b,c))
  = a to_power log(a,b) to_power log(b,c) by Th38
    .= b to_power log(b,c) by A1,Def3
    .= c by A1,Def3;
  hence thesis by A1,Def3;
end;

theorem
  a>1 & b>0 & c>b implies log(a,c) > log(a,b)
proof
  assume that
A1: a>1 & b>0 & c>b and
A2: log(a,c) <= log(a,b);
A3: a to_power log(a,b) = b by A1,Def3;
A4: a to_power log(a,c) = c by A1,Def3;
  now per cases by A2,XXREAL_0:1;
    suppose log(a,c)<log(a,b);
      hence contradiction by A1,A3,A4,Th44;
    end;
    suppose log(a,c) = log(a,b);
      hence contradiction by A1,A4,Def3;
    end;
  end;
  hence contradiction;
end;

theorem
  a>0 & a<1 & b>0 & c>b implies log(a,c) < log(a,b)
proof
  assume that
A1: a>0 & a<1 & b>0 & c>b and
A2: log(a,c) >= log(a,b);
A3: a to_power log(a,b) = b by A1,Def3;
A4: a to_power log(a,c) = c by A1,Def3;
  now per cases by A2,XXREAL_0:1;
    suppose log(a,c)>log(a,b);
      hence contradiction by A1,A3,A4,Th45;
    end;
    suppose log(a,c) = log(a,b);
      hence contradiction by A1,A4,Def3;
    end;
  end;
  hence contradiction;
end;

theorem
  for s being Real_Sequence st for n holds s.n = (1 + 1/(n+1)) to_power (n+1 )
  holds s is convergent
proof
  let s be Real_Sequence such that
A1: for n holds s.n = (1 + 1/(n+1)) to_power (n+1);
  now
    let n;
A2: (1+1/(n+1)) to_power (n+1) > 0 by Th39;
A3: s.(n+1)/s.n =(1 + 1/(n+1+1)) to_power (n+1+1) / s.n by A1
      .=(1 + 1/(n+1+1)) to_power (n+1+1)/(1+1/(n+1)) to_power (n+1)*1 by A1
      .=(1 + 1/(n+1+1)) to_power (n+1+1)/(1 + 1/(n+1)) to_power (n+1) *
    ((1+1/(n+1))/(1+1/(n+1))) by XCMPLX_1:60
      .=(1+1/(n+1)) * (1 + 1/(n+1+1)) to_power (n+1+1) /
    ((1 + 1/(n+1)) to_power (n+1) * (1+1/(n+1))) by XCMPLX_1:77
      .=(1+1/(n+1)) * (1 + 1/(n+1+1)) to_power (n+1+1) /
    ((1 + 1/(n+1)) to_power (n+1) * (1+1/(n+1)) to_power 1) by Th30
      .=(1+1/(n+1)) * (1 + 1/(n+1+1)) to_power (n+1+1) /
    (1 + 1/(n+1)) to_power (n+1+1) by Th32
      .=(1+1/(n+1)) * ((1 + 1/(n+1+1)) to_power (n+1+1) /
    (1 + 1/(n+1)) to_power (n+1+1)) by XCMPLX_1:75
      .=(1+1/(n+1)) * ((1 + 1/(n+1+1))/(1 + 1/(n+1))) to_power (n+1+1)
    by Th36;
A4: (1 + 1/(n+1+1))/(1 + 1/(n+1))
    = ((1*(n+1+1) + 1)/(n+1+1))/(1 + 1/(n+1)) by XCMPLX_1:114
      .= ((n+1+1+1)/(n+1+1))/((1*(n+1) + 1)/(n+1)) by XCMPLX_1:114
      .= ((n+(1+1)+1)*(n+1))/((n+2)*(n+2)) by XCMPLX_1:85
      .= (n*n+n*2+2*n+3+1-1)/((n+2)*(n+2))
      .= ((n+2)*(n+2))/((n+2)*(n+2)) - 1/((n+2)*(n+2)) by XCMPLX_1:121
      .= 1 - 1/((n+2)*(n+2)) by XCMPLX_1:6,60;
    n+1+1>0+1 by XREAL_1:8;
    then (n+2)*(n+2)>1 by REAL_2:137;
    then 1/((n+2)*(n+2))<1 by SQUARE_1:2;
    then - 1/((n+2)*(n+2)) > -1 by XREAL_1:26;

then (1 + -1/((n+2)*(n+2))) to_power (n+1+1) >= 1 + (n+1+1)*(-1/((n+2)*(n+2)
    )) by PREPOWER:24;
    then (1 - 1/((n+2)*(n+2))) to_power (n+1+1) >= 1 - (n+2)*(1/((n+2)*(n+2)));
    then (1 - 1/((n+2)*(n+2))) to_power (n+1+1) >= 1 - (n+2)*1/((n+2)*(n+2))
    by XCMPLX_1:75;
    then (1 - 1/((n+2)*(n+2))) to_power (n+1+1) >= 1 - ((n+2)/(n+2)*1)/(n+2)
    by XCMPLX_1:84;
    then (1 - 1/((n+2)*(n+2))) to_power (n+1+1) >= 1 - (1*1)/(n+2)
    by XCMPLX_1:60;
    then s.(n+1)/s.n >= (1 + 1/(n+1)) * (1 - 1/(n+2)) by A3,A4,XREAL_1:66;
    then s.(n+1)/s.n >= ((1*(n+1) + 1)/(n+1)) * (1 - 1/(n+2)) by XCMPLX_1:114;
    then s.(n+1)/s.n >= ((n+2)/(n+1)) * ((1*(n+2) - 1)/(n+2)) by XCMPLX_1:128;
    then s.(n+1)/s.n >= ((n+1)*(n+2))/((n+1)*(n+2)) by XCMPLX_1:77;
    then
A5: s.(n+1)/s.n >= 1 by XCMPLX_1:6,60;
    s.n>0 by A1,A2;
    hence s.(n+1)>=s.n by A5,REAL_2:118;
  end;
  then
A6: s is non-decreasing by SEQM_3:def 13;
  now
    let n;
A7: 2*(n+1)>0 by REAL_2:122;
A8: 1/(1+1/(2*(n+1)))>0 by REAL_2:127;
A9: 2*(n+1)<>0;
A10: s.(n+(n+1)) = (1 + 1/(2*n+(1+1))) to_power (2*n+1+1) by A1
      .= ((1 + 1/(2*(n+1))) to_power (n+1)) to_power 2 by Th38;
    2*(n+1)+1>0+1 by A7,XREAL_1:8;
    then 1/(2*(n+1)+1)<1 by SQUARE_1:2;
    then
A11: - 1/(2*(n+1)+1)> -1 by XREAL_1:26;
    then
A12: - 1/(2*(n+1)+1) + 1 > -1 + 1 by XREAL_1:8;
A13: (1 + 1/(2*(n+1))) to_power (n+1)
    = (1/(1/(1 + 1/(2*(n+1))))) to_power (n+1) by XCMPLX_1:56
      .= (1/(1 + 1/(2*(n+1)))) to_power (-(n+1)) by A8,Th37
      .= (1/((1*(2*(n+1)) + 1)/(2*(n+1)))) to_power (-(n+1)) by A9,XCMPLX_1:114
      .= ((2*(n+1)+1-1)/(2*(n+1)+1)) to_power (-(n+1)) by XCMPLX_1:78
      .= ((2*(n+1)+1)/(2*(n+1)+1) - 1/(2*(n+1)+1)) to_power (-(n+1))
    by XCMPLX_1:121
      .= (1 - 1/(2*(n+1)+1)) to_power (-(n+1)) by XCMPLX_1:60
      .= 1 / (1 - 1/(2*(n+1)+1)) to_power (n+1) by A12,Th33;
    (1 + - 1/(2*(n+1)+1)) to_power (n+1) >= 1 + (n+1)*(- 1/(2*(n+1)+1)) by A11,
    PREPOWER:24;
    then (1 - 1/(2*(n+1)+1)) to_power (n+1) >= 1-(n+1)*(1/(2*(n+1)+1));
    then (1 - 1/(2*(n+1)+1)) to_power (n+1) >= 1-(n+1)/(2*(n+1)+1) by
    XCMPLX_1:100;
    then
A14: (1 - 1/(2*(n+1)+1)) to_power (n+1) >= (1*(2*(n+1)+1)-(n+1))/(2*(n +1 )+1)
    by XCMPLX_1:128;
    now
      assume (2*(n+1)-n)/(2*(n+1)+1) < 1/2;
      then (2*(n+1)-n)*2 < (2*(n+1)+1)*1 by REAL_2:187;
      then 2*(n+1)+(-n + (2*(n+1)-n)) < 2*(n+1)+1;
      hence contradiction by XREAL_1:8;
    end;
    then (1 - 1/(2*(n+1)+1)) to_power (n+1) >= 1/2 by A14,XXREAL_0:2;
    then
A15: (1 + 1/(2*(n+1))) to_power (n+1) <= 1/(1/2) by A13,REAL_2:152;
    (1 + 1/(2*(n+1))) to_power (n+1) > 0 by Th39;
    then ((1 + 1/(2*(n+1))) to_power (n+1))^2 <= 2*2 by A15,XREAL_1:68;
    then
A16: s.(n+(n+1)) <= 2*2 by A10,Th53;
    s.n<=s.(n+(n+1)) by A6,SEQM_3:11;
    then s.n <= 2*2 & 2*2 + 0 < 2*2 + 1 by A16,XXREAL_0:2;
    hence s.n < 2*2 + 1 by XXREAL_0:2;
  end;
  then s is bounded_above by SEQ_2:def 3;
  hence thesis by A6;
end;

definition
  func number_e -> real number means
  for s being Real_Sequence st for n holds s.n = (1 + 1/(n+1)) to_power (n+1 )
  holds it = lim s;
  existence
  proof
    deffunc F(Element of NAT) = (1 + 1/($1+1)) to_power ($1+1);
    consider s1 being Real_Sequence such that
A1: for n holds s1.n = F(n) from SEQ_1:sch 1;
    take lim s1;
    let s be Real_Sequence such that
A2: for n holds s.n = (1 + 1/(n+1)) to_power (n+1);
    now
      let n; thus s1.n = (1 + 1/(n+1)) to_power (n+1) by A1
        .= s.n by A2;
    end;
    hence thesis by FUNCT_2:113;
  end;
  uniqueness
  proof
    let a,b;
    assume that
A3: for s being Real_Sequence st
    for n holds s.n = (1 + 1/(n+1)) to_power (n+1) holds a = lim s and
A4: for s being Real_Sequence st
    for n holds s.n = (1 + 1/(n+1)) to_power (n+1) holds b = lim s;
    deffunc F(Element of NAT) = (1 + 1/($1+1)) to_power ($1+1);
    consider s1 being Real_Sequence such that
A5: for n holds s1.n = F(n) from SEQ_1:sch 1;
    lim s1 = a by A3,A5;
    hence thesis by A4,A5;
  end;
end;

definition
  redefine func number_e -> Real;
  coherence by XREAL_0:def 1;
end;

