:: The {N}agata-Smirnov Theorem. {P}art {II}
::  by Karol P\c{a}k
::
:: Received July 22, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies SQUARE_1, TARSKI, BOOLE, SUBSET_1, RELAT_1, FUNCT_1, FUNCT_3,
      SETFAM_1, RELAT_2, GROUP_1, NAT_1, NAGATA_2, ORDINAL2, PRE_TOPC,
      METRIC_1, ARYTM_1, PCOMPS_1, PCOMPS_2, ARYTM, ABSVALUE, NAGATA_1,
      TOPMETR, TMAP_1, PSCOMP_1, SEQ_2, SGRAPH1, FINSET_1, PROB_1, URYSOHN1,
      SUPINF_2, RLVECT_1, SERIES_1, SEQ_1, SEQM_3, ARYTM_3, PREPOWER, FINSEQ_1,
      TDGROUP, SEQFUNC, CARD_1, FUNCT_2, FINSOP_1, BINOP_1, SETWISEO, COMPTS_1,
      RLVECT_3, COH_SP, PARTFUN1, WELLORD1, NATTRA_1, FUNCT_7, VALUED_0;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, SETFAM_1, SETWISEO, BORSUK_1,
      RELAT_1, RELAT_2, ORDINAL1, XCMPLX_0, XXREAL_0, XREAL_0, FUNCT_1, REAL_1,
      RELSET_1, PARTFUN1, VALUED_0, VALUED_1, SEQ_1, FUNCT_2, FUNCT_3,
      DOMAIN_1, BINOP_1, PRE_TOPC, NAT_1, NAT_D, CARD_1, NUMBERS, FINSET_1,
      COMPLEX1, STRUCT_0, FINSEQ_1, PCOMPS_1, PCOMPS_2, NEWTON, TOPS_2,
      METRIC_1, PROB_1, URYSOHN1, SEQFUNC, PREPOWER, TOPMETR, PSCOMP_1, TMAP_1,
      NAGATA_1, SEQ_2, SEQM_3, SEQ_4, SERIES_1, FINSOP_1, COMPTS_1, CANTOR_1,
      FRAENKEL, WELLORD1, FUNCT_7;
 constructors SETFAM_1, WELLORD1, FUNCT_3, SETWISEO, REAL_1, SQUARE_1, NAT_1,
      NAT_D, SEQM_3, PROB_1, FINSOP_1, NEWTON, PREPOWER, SERIES_1, FUNCT_7,
      TOPS_2, COMPTS_1, TMAP_1, CANTOR_1, PCOMPS_2, PSCOMP_1, URYSOHN3,
      NAGATA_1, SEQ_1, BORSUK_4, XXREAL_2;
 registrations XBOOLE_0, SUBSET_1, ORDINAL1, RELSET_1, FINSET_1, FRAENKEL,
      NUMBERS, XXREAL_0, XREAL_0, NAT_1, MEMBERED, FINSEQ_1, STRUCT_0, TOPS_1,
      METRIC_1, BORSUK_1, TOPMETR, PSCOMP_1, TOPREAL6, VALUED_0, VALUED_1,
      FUNCT_2, BORSUK_4, BORSUK_6;
 requirements BOOLE, SUBSET, NUMERALS, REAL, ARITHM;
 definitions FUNCT_2, TARSKI, TOPS_2, XBOOLE_0, BINOP_1, SUBSET_1, STRUCT_0,
      PRE_TOPC;
 theorems REAL_1, NAT_1, NEWTON, FUNCT_2, PCOMPS_1, PCOMPS_2, NAGATA_1,
      SUBSET_1, METRIC_6, FUNCT_3, FUNCT_1, METRIC_1, XCMPLX_1, ABSVALUE,
      TOPMETR, TMAP_1, BORSUK_1, XBOOLE_1, TARSKI, SETFAM_1, XBOOLE_0, TOPS_1,
      TOPS_2, PRE_TOPC, FINSEQ_1, CARD_4, TOPREAL6, ZFMISC_1,
      BINOP_1, REAL_2, SEQ_2, SEQ_4, SEQ_1, SERIES_1, PREPOWER, SEQM_3,
      SEQFUNC, PARTFUN1, JGRAPH_2, URYSOHN1, FINSEQ_3, FINSEQ_4, FINSOP_1,
      CANTOR_1, SETWISEO, PROB_1, URYSOHN3, WELLORD1, WELLORD2, TBSP_1,
      RELAT_1, RELAT_2, YELLOW_9, FUNCT_7, RSSPACE2, RSSPACE, BHSP_5, XREAL_1,
      COMPLEX1, XXREAL_0, ORDINAL1, NAT_D, VALUED_1, CARD_1, CARD_2;
 schemes FUNCT_2, NAT_1, SEQFUNC, FRAENKEL, BINOP_1, PCOMPS_2;

begin

reserve i, k, m, n for Element of NAT,
  r, s for Real,
  rn for real number,
  x, y, z, X for set,
  T, T1, T2 for non empty TopSpace,
  p, q for Point of T,
  A, B, C for Subset of T,
  A' for non empty Subset of T,
  pq for Element of [:the carrier of T,the carrier of T:],
  pq' for Point of [:T,T:],
  pmet,pmet1 for Function of [:the carrier of T,the carrier of T:],REAL,
  pmet',pmet1' for RealMap of [:T,T:],
  f,f1 for RealMap of T,
  FS2 for Functional_Sequence of [:the carrier of T,the carrier of T:],REAL,
  seq for Real_Sequence;

theorem Th1:
  for i st i>0 ex n,m st i=(2|^n)*(2*m+1)
proof
  let i such that
A1: i>0;
  defpred N[Element of NAT] means
  for k st 1<=k & k<=$1 ex n,m st k=(2|^n)*(2*m+1);
A2: N[0];
A3: for i st N[i] holds N[i + 1]
  proof
    let i such that
A4: N[i];
    let k such that
A5: 1<=k & k <=i+1;
    now per cases by A5,REAL_1:def 5;
      suppose
A6:     k=i+1 & i=0;
        set m=0;
        k=1*(m*2+1) & 1=2|^0 by A6,NEWTON:9;
        hence ex n,m st k=(2|^n)*(2*m+1);
      end;
      suppose
A7:     k=i+1 & i>0;
        per cases by NAT_D:12;
        suppose k mod 2=1;
          then consider m being Nat such that
A8:       k = 2 * m + 1 & 1<2 by NAT_D:def 2;
          reconsider m as Element of NAT by ORDINAL1:def 13;
          1=2|^0 by NEWTON:9;
          then k=(2|^0)*(2*m+1) by A8;
          hence ex n,m st k=(2|^n)*(2*m+1);
        end;
        suppose k mod 2=0;
          then consider j being Nat such that
A9:       k = 2 * j + 0 & 0<2 by NAT_D:def 2;
          reconsider j as Element of NAT by ORDINAL1:def 13;
          j<>0 by A7,A9;
          then
A10:      j>=1 by NAT_1:14;
          j<=i
          proof
            assume j>i;
            then j+j>i+1 by NAT_1:14,XREAL_1:10;
            hence thesis by A7,A9;
          end;
          then consider n,m such that
A11:      j=(2|^n)*(2*m+1) by A4,A10;
          k=(2*(2|^n))*(2*m+1) by A9,A11;
          then k=(2|^(n+1))*(2*m+1) by NEWTON:11;
          hence ex n,m st k=(2|^n)*(2*m+1);
        end;
      end;
      suppose k<i+1;
        then k<=i by NAT_1:13;
        hence ex n,m st k=(2|^n)*(2*m+1) by A4,A5;
      end;
    end;
    hence thesis;
  end;
A12: for n holds N[n] from NAT_1:sch 1(A2,A3);
  i>=1 by A1,NAT_1:14;
  hence thesis by A12;
end;

definition
  func PairFunc -> Function of [:NAT,NAT:],NAT means
  :Def1:
  for n,m holds it.[n,m] = (2|^n)*(2*m+1)-1;
  existence
  proof
    deffunc N(Element of NAT,Element of NAT) = In((2|^$1)*(2*$2+1)-1,NAT);
A1: for n,m be Element of NAT holds (2|^n)*(2*m+1)-1 in NAT
    proof
      let n,m be Element of NAT;
      0 < 2|^n & 0< 2*m+1 by NEWTON:102;
      then 0<((2|^n)*(2*m+1)) by REAL_2:122;
      then ((2|^n)*(2*m+1))-1 is Element of NAT by NAT_1:20;
      hence thesis;
    end;
    consider NN being Function of [:NAT,NAT:],NAT such that
A2: for n,m being Element of NAT holds NN.(n,m) = N(n,m) from BINOP_1:sch 4;
    take NN;
    let n,m;
    NN.(n,m) = In((2|^n)*(2*m+1)-1,NAT) by A2
      .= (2|^n)*(2*m+1)-1 by A1,FUNCT_7:def 1;
    hence thesis;
  end;
  uniqueness
  proof
    let N1,N2 be Function of [:NAT,NAT:],NAT;
    assume that
A3: for n,m holds N1.[n,m] = (2|^n)*(2*m+1)-1 and
A4: for n,m holds N2.[n,m] = (2|^n)*(2*m+1)-1;
    now
      let n,m be Element of NAT;
      N1.[n,m] = (2|^n)*(2*m+1)-1 & N2.[n,m] = (2|^n)*(2*m+1)-1 by A3,A4;
      hence N1.(n,m)=N2.(n,m);
    end;
    hence thesis by BINOP_1:2;
  end;
end;

theorem Th2:
  PairFunc is bijective
proof
  thus PairFunc is one-to-one
  proof
    now
      let nm1,nm2 be set;
      assume
A1:   nm1 in [:NAT,NAT:] & nm2 in [:NAT,NAT:] & PairFunc.nm1=PairFunc.nm2;
      then consider n1,m1 be set such that
A2:   n1 in NAT & m1 in NAT & [n1,m1]=nm1 by ZFMISC_1:def 2;
      consider n2,m2 be set such that
A3:   n2 in NAT & m2 in NAT & [n2,m2]=nm2 by A1,ZFMISC_1:def 2;
      reconsider n1,n2,m1,m2 as Element of NAT by A2,A3;
      (2|^n1)*(2*m1+1)-1=PairFunc.nm1 & (2|^n2)*(2*m2+1)-1=PairFunc.nm2
      by A2,A3,Def1;
      then n1=n2 & m1=m2 by A1,CARD_4:52;
      hence nm1=nm2 by A2,A3;
    end;
    hence thesis by FUNCT_2:25;
  end;
  thus PairFunc is onto
  proof
    now
      let i be set;
      assume i in NAT;
      then reconsider i'=i as Element of NAT;
      consider n, m be Element of NAT such that
A4:   i'+1=(2|^n) *(2*m+1) by Th1;
      i'-0=((2|^n) *(2*m+1))-1 & dom PairFunc = [:NAT,NAT:]
      by A4,FUNCT_2:def 1;
      then [n,m] in dom PairFunc & i= PairFunc.[n,m] by Def1;
      hence i in rng PairFunc by FUNCT_1:def 5;
    end;
    then NAT c= rng PairFunc & rng PairFunc c= NAT by TARSKI:def 3;
    hence NAT=rng PairFunc by XBOOLE_0:def 10;
  end;
end;

definition
  let X be set,f be Function of [:X,X:],REAL,x be Element of X;
  func dist(f,x) -> Function of X,REAL means
  :Def2:
  for y be Element of X holds it.y = f.(x,y);
  existence
  proof
    deffunc D(set)=f.(x,$1);
A1: for y being set st y in X holds D(y) in REAL
    proof
      let y be set;
      assume y in X;
      f.[x,y] in REAL;
      hence D(y) in REAL;
    end;
    consider DIST be Function of X,REAL such that
A2: for y being set st y in X holds DIST.y =D(y) from FUNCT_2:sch 2(A1);
    now per cases;
      suppose
A3:     X is empty;
        let y be Element of X;
        not [x,y] in dom f & not y in dom DIST by A3,ZFMISC_1:113;
        then f.[x,y]={} & DIST.y={} by FUNCT_1:def 4;
        hence f.(x,y)=DIST.y;
      end;
      suppose X is non empty;
        hence for y be Element of X holds DIST.y = f.(x,y) by A2;
      end;
    end;
    hence thesis;
  end;
  uniqueness
  proof
    let D1,D2 be Function of X,REAL;
    assume that
A4: for y be Element of X holds D1.y=f.(x,y) and
A5: for y be Element of X holds D2.y=f.(x,y);
    now
      let y be Element of X;
      D1.y=f.(x,y) & D2.y=f.(x,y) by A4,A5;
      hence D1.y=D2.y;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

theorem Th3:
  for D be Subset of [:T1,T2:] st D is open
  for x1 be Point of T1, x2 be Point of T2
  for X1 be Subset of T1,X2 be Subset of T2 holds
  (X1=pr1(the carrier of T1,the carrier of T2).:(D/\[:the carrier of T1,{x2}:])
  implies X1 is open) &
  (X2=pr2(the carrier of T1,the carrier of T2).:(D/\[:{x1},the carrier of T2:])
  implies X2 is open)
proof
  let D be Subset of [:T1,T2:] such that
A1: D is open;
  let x1 be Point of T1, x2 be Point of T2;
  set cT1 =the carrier of T1;
  set cT2=the carrier of T2;
  let X1 be Subset of T1,X2 be Subset of T2;
  consider FA be Subset-Family of [:T1,T2:] such that
A2: D=union FA & for B be set st B in FA ex B1 being Subset of T1,
  B2 being Subset of T2 st B = [:B1,B2:] & B1 is open & B2 is open by A1,
  BORSUK_1:45;
  thus X1=pr1(cT1,cT2).:(D/\[:cT1,{x2}:]) implies X1 is open
  proof
    assume
A3: X1=pr1(cT1,cT2).:(D/\[:cT1,{x2}:]);
    for t be set holds t in X1 iff
    ex U be Subset of T1 st U is open & U c= X1 & t in U
    proof
      let t be set;
      t in X1 implies ex U be Subset of T1 st U is open & U c= X1 & t in U
      proof
        assume t in X1;
        then consider tx be set such that
A4:     tx in dom pr1(cT1,cT2) & tx in (D/\[:cT1,{x2}:])
        & t=pr1(cT1,cT2).tx by A3,FUNCT_1:def 12;
        consider tx1,tx2 be set such that
A5:     tx1 in cT1 & tx2 in cT2 & tx=[tx1,tx2] by A4,ZFMISC_1:def 2;
        tx in D by A4,XBOOLE_0:def 3;
        then consider tX be set such that
A6:     tx in tX & tX in FA by A2,TARSKI:def 4;
        consider tX1 be Subset of T1,tX2 be Subset of T2 such that
A7:     tX=[:tX1,tX2:] & tX1 is open & tX2 is open by A2,A6;
        take tX1;
        thus tX1 is open by A7;
A8:     tx1 in tX1& pr1(cT1,cT2).(tx1,tx2)=tx1
        by A5,A6,A7,FUNCT_3:def 5,ZFMISC_1:106;
        thus tX1 c=X1
        proof
          let a be set such that
A9:       a in tX1;
          tx in [:cT1,{x2}:] by A4,XBOOLE_0:def 3;
          then tx2=x2 & tx2 in tX2 by A5,A6,A7,ZFMISC_1:106,129;
          then [a,x2] in [:tX1,tX2:] & [a,x2] in [:cT1,cT2:]
          by A9,ZFMISC_1:106;
          then
A10:      [a,x2] in union FA& [a,x2] in [:cT1,{x2}:]& [a,x2] in dom
          pr1(cT1,cT2) by A6,A7,A9,FUNCT_3:def 5,TARSKI:def 4,ZFMISC_1:129;
          then [a,x2] in D/\[:cT1,{x2}:] by A2,XBOOLE_0:def 3;
          then pr1(cT1,cT2).(a,x2) in pr1(cT1,cT2).:(D/\[:cT1,{x2}:])
          & pr1(cT1,cT2).(a,x2)=a by A9,A10,FUNCT_1:def 12,FUNCT_3:def 5;
          hence thesis by A3;
        end;
        thus t in tX1 by A4,A5,A8;
      end;
      hence thesis;
    end;
    hence thesis by TOPS_1:57;
  end;
  thus X2= pr2(cT1,cT2).:(D/\[:{x1},cT2:]) implies X2 is open
  proof
    assume
A11: X2=pr2(cT1,cT2).:(D/\[:{x1},cT2:]);
    for t be set holds t in X2 iff
    ex U be Subset of T2 st U is open & U c= X2 & t in U
    proof
      let t be set;
      t in X2 implies ex U be Subset of T2 st U is open & U c= X2 & t in U
      proof
        assume t in X2;
        then consider tx be set such that
A12:    tx in dom pr2(cT1,cT2) & tx in (D/\[:{x1},cT2:])
        & t=pr2(cT1,cT2).tx by A11,FUNCT_1:def 12;
        consider tx1,tx2 be set such that
A13:    tx1 in cT1 & tx2 in cT2 & tx=[tx1,tx2] by A12,ZFMISC_1:def 2;
        tx in D by A12,XBOOLE_0:def 3;
        then consider tX be set such that
A14:    tx in tX & tX in FA by A2,TARSKI:def 4;
        consider tX1 be Subset of T1,tX2 be Subset of T2 such that
A15:    tX=[:tX1,tX2:] & tX1 is open & tX2 is open by A2,A14;
A16:    tx2 in tX2& pr2(cT1,cT2).(tx1,tx2)=tx2
        by A13,A14,A15,FUNCT_3:def 6,ZFMISC_1:106;
        tX2 c=X2
        proof
          let a be set such that
A17:      a in tX2;
          tx in [:{x1},cT2:] by A12,XBOOLE_0:def 3;
          then tx1=x1 & tx1 in tX1 by A13,A14,A15,ZFMISC_1:106,128;
          then [x1,a] in [:tX1,tX2:] & [x1,a] in [:cT1,cT2:]
          by A17,ZFMISC_1:106;
          then
A18:      [x1,a] in union FA& [x1,a] in [:{x1},cT2:]& [x1,a] in dom
          pr2(cT1,cT2) by A14,A15,A17,FUNCT_3:def 6,TARSKI:def 4,ZFMISC_1:128;
          then [x1,a] in D/\[:{x1},cT2:] by A2,XBOOLE_0:def 3;
          then pr2(cT1,cT2).(x1,a) in pr2(cT1,cT2).:(D/\[:{x1},cT2:])
          & pr2(cT1,cT2).(x1,a)=a by A17,A18,FUNCT_1:def 12,FUNCT_3:def 6;
          hence thesis by A11;
        end;
        hence thesis by A12,A13,A15,A16;
      end;
      hence thesis;
    end;
    hence thesis by TOPS_1:57;
  end;
end;

theorem Th4:
  for pmet st for pmet' st pmet=pmet' holds pmet' is continuous
  for x be Point of T holds dist(pmet,x) is continuous
proof
  set cT=the carrier of T;
  let pmet such that
A1: for pmet' st pmet=pmet' holds pmet' is continuous;
  let x be Point of T;
  [:cT,cT:]= the carrier of [:T,T:] by BORSUK_1:def 5;
  then reconsider pmet'=pmet as RealMap of [:T,T:];
  reconsider pmetR=pmet' as Function of [:T,T:],R^1 by TOPMETR:24;
  reconsider distx=dist(pmet,x) as Function of T,R^1 by TOPMETR:24;
  pmet' is continuous by A1;
  then
A2: pmetR is continuous by TOPREAL6:83;
  now
    let t be Point of T;
    for R being Subset of R^1 st R is open & distx.t in R
    ex U being Subset of T st U is open & t in U & distx.:U c= R
    proof
      let R be Subset of R^1 such that
A3:   R is open & distx.t in R;
      reconsider xt=[x,t] as Point of [:T,T:] by BORSUK_1:def 5;
      pmetR is_continuous_at xt & distx.t=pmet.(x,t) & pmet.(x,t)=pmet.xt
      by A2,Def2,TMAP_1:55;
      then consider XU be Subset of [:T,T:] such that
A4:   XU is open & xt in XU & pmetR.:XU c= R by A3,TMAP_1:48;
      set U=pr2(cT,cT).:(XU/\[:{x},cT:]);
      [x,t] in XU & [x,t] in [:{x},cT:] by A4,ZFMISC_1:128;
      then [x,t] in XU/\[:{x},cT:] & dom pr2(cT,cT)=[:cT,cT:] & [x,t] in
      [:cT,cT:] by FUNCT_3:def 6,XBOOLE_0:def 3;
      then pr2(cT,cT).(x,t) in pr2(cT,cT).:(XU/\[:{x},cT:]) by FUNCT_1:def 12;
      then
A5:   t in U & U is open by A4,Th3,FUNCT_3:def 6;
      distx.:U c= R
      proof
        let du be set such that
A6:     du in distx.:U;
        consider u be set such that
A7:     u in dom distx & u in U & distx.u=du by A6,FUNCT_1:def 12;
        reconsider u as Point of T by A7;
        consider xu be set such that
A8:     xu in dom pr2(cT,cT)& xu in (XU/\[:{x},cT:]) & pr2(cT,cT).xu=u by A7,
        FUNCT_1:def 12;
        consider x',u' be set such that
A9:     x' in cT & u' in cT & xu=[x',u'] by A8,ZFMISC_1:def 2;
        reconsider x',u' as Point of T by A9;
        [x',u'] in [:{x},cT:] & pr2(cT,cT).(x',u') = u' by A8,A9,FUNCT_3:def 6
        ,XBOOLE_0:def 3;
        then x'=x & u'=u by A8,A9,ZFMISC_1:128;
        then pmet.(x',u')=du & [x',u'] in XU & pmet'.[x',u']=pmet.(x',u')
        & dom pmetR=the carrier of [:T,T:] by A7,A8,A9,Def2,FUNCT_2:def 1
        ,XBOOLE_0:def 3;
        then du in pmetR.:XU by FUNCT_1:def 12;
        hence thesis by A4;
      end;
      hence thesis by A5;
    end;
    hence distx is_continuous_at t by TMAP_1:48;
  end;
  then distx is continuous by TMAP_1:55;
  hence thesis by TOPREAL6:83;
end;

definition
  let X be non empty set,f be Function of [:X,X:],REAL,A be Subset of X;
  func inf(f,A) -> Function of X,REAL means
  :Def3:
  for x be Element of X holds it.x = inf((dist(f,x)).:A);
  existence
  proof
    deffunc I(Element of X)=inf((dist(f,$1)).:A);
    consider INF be Function of X,REAL such that
A1: for x being Element of X holds INF.x = I(x) from FUNCT_2:sch 4;
    take INF;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let I1,I2 be Function of X,REAL;
    assume that
A2: for x be Element of X holds I1.x=inf((dist(f,x)).:A) and
A3: for x be Element of X holds I2.x=inf((dist(f,x)).:A);
    now
      let x be Element of X;
      I1.x=inf((dist(f,x)).:A) & I2.x=inf((dist(f,x)).:A) by A2,A3;
      hence I1.x=I2.x;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

Lm1: for X be non empty set,f be Function of [:X,X:],REAL st
f is_a_pseudometric_of X holds f is bounded_below &
for A be non empty Subset of X,x be Element of X holds
dist(f,x).:A is non empty bounded_below
proof
  let X be non empty set,f be Function of [:X,X:],REAL such that
A1: f is_a_pseudometric_of X;
A2: now
    let xy be set;
    assume xy in dom f;
    then consider x,y such that
A3: x in X & y in X & [x,y] = xy by ZFMISC_1:def 2;
    reconsider x,y as Element of X by A3;
    f.(x,y) >=0 by A1,NAGATA_1:29;
    then f.xy >=0 & 0>-1 by A3;
    hence f.xy>-1;
  end;
  now
    let A be non empty Subset of X,x be Element of X;
    consider a be set such that
A4: a in A by XBOOLE_0:def 1;
    reconsider a as Element of X by A4;
    dom dist(f,x)=X by FUNCT_2:def 1;
    then
A5: dist(f,x).a in dist(f,x).:A by A4,FUNCT_1:def 12;
    now
      let rn;
      assume rn in dist(f,x).:A;
      then consider y such that
A6:   y in dom dist(f,x) & y in A & rn = dist(f,x).y by FUNCT_1:def 12;
      reconsider y as Element of X by A6;
      f.(x,y)>=0 & rn=f.(x,y) by A1,A6,Def2,NAGATA_1:29;
      hence rn>=0;
    end;
    hence dist(f,x).:A is non empty bounded_below by A5,SEQ_4:def 2;
  end;
  hence thesis by A2,SEQ_2:def 2;
end;

theorem Th5:
  for X be non empty set,f be Function of [:X,X:],REAL st
  f is_a_pseudometric_of X for A be non empty Subset of X,x be Element of X
  holds inf(f,A).x>=0
proof
  let X be non empty set,f be Function of [:X,X:],REAL such that
A1: f is_a_pseudometric_of X;
  let A be non empty Subset of X,x be Element of X;
  consider a be set such that
A2: a in A by XBOOLE_0:def 1;
  a in X & X=dom dist(f,x) by A2,FUNCT_2:def 1;
  then
A3: dist(f,x).a in (dist(f,x)).:A by A2,FUNCT_1:def 12;
  now
    let rn;
    assume rn in (dist(f,x)).:A;
    then consider y such that
A4: y in dom dist(f,x) & y in A & rn = dist(f,x).y by FUNCT_1:def 12;
    dist(f,x).y=f.(x,y) by A4,Def2;
    hence rn>=0 by A1,A4,NAGATA_1:29;
  end;
  then inf ((dist(f,x)).:A)>=0 & f is bounded_below by A1,A3,Lm1,SEQ_4:60;
  hence thesis by Def3;
end;

theorem Th6:
  for X be non empty set,f be Function of [:X,X:],REAL st
  f is_a_pseudometric_of X for A be Subset of X,x be Element of X
  holds x in A implies inf(f,A).x=0
proof
  let X be non empty set,f be Function of [:X,X:],REAL such that
A1: f is_a_pseudometric_of X;
  let A be Subset of X,x be Element of X;
  assume
A2: x in A;
  then reconsider A as non empty Subset of X;
  f is Reflexive by A1,NAGATA_1:def 10;
  then f.(x,x)=0 & X=dom dist(f,x) by FUNCT_2:def 1,METRIC_1:def 3;
  then dist(f,x).x=0 & x in dom dist(f,x) by Def2;
  then 0 in dist(f,x).:A & dist(f,x).:A is non empty bounded_below
  by A1,A2,Lm1,FUNCT_1:def 12;
  then inf(dist(f,x).:A)<=0 & f is bounded_below by A1,Lm1,SEQ_4:def 5;
  then inf(f,A).x <=0 & inf(f,A).x >=0 by A1,Def3,Th5;
  hence thesis;
end;

theorem Th7:
  for pmet st pmet is_a_pseudometric_of the carrier of T
  for x,y be Point of T,A be non empty Subset of T holds
  abs(inf(pmet,A).x-inf(pmet,A).y)<=pmet.(x,y)
proof
  let pmet such that
A1: pmet is_a_pseudometric_of the carrier of T;
  let x,y be Point of T,A be non empty Subset of T;
  for x1,y1 be Point of T holds inf(pmet,A).y1-inf(pmet,A).x1>=-pmet.(x1,y1)
  proof
    let x1,y1 be Point of T;
A2: dist(pmet,x1).:A is non empty bounded_below & dist(pmet,y1).:A is
    non empty bounded_below & pmet is bounded_below by A1,Lm1;
    for rn st rn in dist(pmet,y1).:A holds
    rn>=inf(dist(pmet,x1).:A)-pmet.(x1,y1)
    proof
      let rn;
      assume rn in dist(pmet,y1).:A;
      then consider z such that
A3:   z in dom dist(pmet,y1) & z in A & dist(pmet,y1).z=rn by FUNCT_1:def 12;
      reconsider z as Point of T by A3;
      dom dist(pmet,x1)= the carrier of T by FUNCT_2:def 1;
      then pmet is triangle & dist(pmet,x1).z in dist(pmet,x1).:A &

dist(pmet,x1).z= pmet.(x1,z) by A1,A3,Def2,FUNCT_1:def 12,NAGATA_1:def 10;
      then pmet.(x1,y1)+pmet.(y1,z)>=pmet.(x1,z)
      & pmet.(x1,z)>=inf(dist(pmet,x1).:A) by A2,METRIC_1:def 6,SEQ_4:def 5;
      then pmet.(x1,y1)+pmet.(y1,z)>=inf(dist(pmet,x1).:A)+0 by XXREAL_0:2;
      then pmet.(y1,z)-0>=inf(dist(pmet,x1).:A)-pmet.(x1,y1) by XREAL_1:23;
      hence thesis by A3,Def2;
    end;
    then inf(dist(pmet,y1).:A)-0>=inf(dist(pmet,x1).:A)-pmet.(x1,y1) by A2,
    SEQ_4:60;
    then inf(dist(pmet,y1).:A)-inf(dist(pmet,x1).:A)>=0-pmet.(x1,y1) &
    inf(dist(pmet,y1).:A)=inf(pmet,A).y1 by Def3,REAL_2:169;
    hence thesis by Def3;
  end;
  then inf(pmet,A).y-inf(pmet,A).x>=-pmet.(x,y) &
  inf(pmet,A).x-inf(pmet,A).y>=-pmet.(y,x)
  & pmet is symmetric by A1,NAGATA_1:def 10;
  then -(inf(pmet,A).x-inf(pmet,A).y)>=-pmet.(x,y) &
  inf(pmet,A).x-inf(pmet,A).y>=-pmet.(x,y) by METRIC_1:def 5;
  then (inf(pmet,A).x-inf(pmet,A).y)<= pmet.(x,y) &
  (inf(pmet,A).x-inf(pmet,A).y)>=-pmet.(x,y) by XREAL_1:26;
  hence thesis by ABSVALUE:12;
end;

theorem Th8:
  for pmet st pmet is_a_pseudometric_of the carrier of T &
  for p holds dist(pmet,p) is continuous
  for A be non empty Subset of T holds inf(pmet,A) is continuous
proof
  let pmet such that
A1: pmet is_a_pseudometric_of the carrier of T and
A2: for p holds dist(pmet,p) is continuous;
  let A be non empty Subset of T;
  reconsider infR=inf(pmet,A) as Function of T,R^1 by TOPMETR:24;
  now
    let t be Point of T;
    reconsider dR=dist(pmet,t) as Function of T,R^1 by TOPMETR:24;
    for R being Subset of R^1 st R is open & infR.t in R
    ex U being Subset of T st U is open & t in U & infR.:U c= R
    proof
      let R be Subset of R^1 such that
A3:   R is open & infR.t in R;
      reconsider infRt=infR qua real-valued Function.t,
      dRt=dR qua real-valued Function.t as Point of RealSpace
      by METRIC_1:def 14;
      consider r being real number such that
A4:   r>0 & Ball(infRt,r) c= R by A3,TOPMETR:22,def 7;
      reconsider dB=Ball(dRt,r) as Subset of R^1 by METRIC_1:def 14,TOPMETR:24;
      abs(dR.t-dR.t)=0 & dist(pmet,t) is continuous by A2,ABSVALUE:7;
      then dist(dRt,dRt)<r & dR is continuous by A4,TOPMETR:15,TOPREAL6:83;
      then dRt in dB & dB is open & dR is_continuous_at t by METRIC_1:12
      ,TMAP_1:55,TOPMETR:21,def 7;
      then consider B be Subset of T such that
A5:   B is open & t in B & dR.:B c=dB by TMAP_1:48;
      infR.:B c= R
      proof
        let Ib be set;
        assume Ib in infR.:B;
        then consider b be set such that
A6:     b in dom infR & b in B & infR.b=Ib by FUNCT_1:def 12;
        reconsider b as Point of T by A6;
        reconsider infRb=infR qua real-valued Function.b,
        dRb=dR qua real-valued Function.b as Point of RealSpace by
        METRIC_1:def 14;
        dom dR=the carrier of T by FUNCT_2:def 1;
        then dR.b in dR.:B & dR.t=pmet.(t,t) by A6,Def2,FUNCT_1:def 12;
        then dR.b in dB & dR.t=0 by A1,A5,NAGATA_1:28;
        then dist(dRt,dRb)<r & dist(dRt,dRb)=abs(0-dR.b) & pmet.(t,b)>=0 by A1,
        METRIC_1:12,NAGATA_1:29,TOPMETR:15;
        then abs(-(0-dR.b))<r & dR.b>=0 by Def2,COMPLEX1:138;
        then dR.b<r by ABSVALUE:def 1;
        then abs(inf(pmet,A).t-inf(pmet,A).b)<=pmet.(t,b)& dist(infRt,infRb)=
        abs(inf(pmet,A).t-inf(pmet,A).b) & pmet.(t,b) < r by A1,Def2,Th7,
        TOPMETR:15;
        then dist(infRt,infRb)<r by XXREAL_0:2;
        then infRb in Ball(infRt,r) by METRIC_1:12;
        hence thesis by A4,A6;
      end;
      hence thesis by A5;
    end;
    hence infR is_continuous_at t by TMAP_1:48;
  end;
  then infR is continuous by TMAP_1:55;
  hence thesis by TOPREAL6:83;
end;

theorem Th9:
  for f be Function of [:X,X:],REAL st f is_metric_of X holds
  f is_a_pseudometric_of X
proof
  let f be Function of [:X,X:],REAL such that
A1: f is_metric_of X;
  for a,b,c be Element of X holds
  f.(a,a)=0 & f.(a,b)=f.(b,a) & f.(a,c)<=f.(a,b)+f.(b,c) by A1,PCOMPS_1:def 7;
  then f is Reflexive symmetric triangle by METRIC_1:def 3,def 5,def 6;
  hence thesis by NAGATA_1:def 10;
end;

theorem Th10:
  for pmet st pmet is_metric_of the carrier of T &
  (for A be non empty Subset of T holds
  Cl A={p where p is Point of T:inf(pmet,A).p=0}) holds T is metrizable
proof
  let pmet such that
A1: pmet is_metric_of the carrier of T and
A2: for A be non empty Subset of T holds
  Cl A={p where p is Point of T:inf(pmet,A).p=0};
  set PM=SpaceMetr(the carrier of T,pmet);
A3: for x,y be Element of PM holds pmet.(x,y)=dist(x,y)
  proof
    let x,y be Element of PM;
    PM=MetrStruct(#the carrier of T,pmet#) by A1,PCOMPS_1:def 8;
    hence thesis by METRIC_1:def 1;
  end;
A4: the topology of T c=Family_open_set PM
  proof
    let A be set;
    assume
A5: A in the topology of T;
    then reconsider AT=A as Subset of T;
    reconsider AP=A as Subset of PM by A1,A5,PCOMPS_2:8;
    per cases;
    suppose AT` is empty;
      then AT`=([#]T)`& [#]T= the carrier of T by XBOOLE_1:37;
      then AT=the carrier of T by TOPS_1:21;
      then AT=the carrier of PM by A1,PCOMPS_2:8;
      hence A in Family_open_set PM by PCOMPS_1:34;
    end;
    suppose
A6:   AT` is non empty;
      for xP be Element of PM st xP in AP holds ex r st r>0 & Ball(xP,r) c= AP
      proof
        let xP be Element of PM such that
A7:     xP in AP;
        reconsider xT=xP as Element of T by A1,PCOMPS_2:8;
        AT is open by A5,PRE_TOPC:def 5;
        then AT` is closed;
        then AT`=Cl AT` by PRE_TOPC:52;
        then
A8:     AT`={p where p is Point of T:inf(pmet,AT`).p=0} by A2,A6;
A9:     inf(pmet,AT`).xT>0
        proof
          assume inf(pmet,AT`).xT<=0;
          then inf(pmet,AT`).xT=0 by A1,A6,Th5,Th9;
          then xT in AT` & xT in AT by A7,A8;
          then AT` meets AT by XBOOLE_0:3;
          hence thesis by XBOOLE_1:79;
        end;
        take r=inf(pmet,AT`).xT;
        Ball(xP,r) c= AP
        proof
          let y;
          assume
A10:      y in Ball(xP,r) & not y in AP;
          then reconsider yP=y as Point of PM;
          reconsider yT=yP as Point of T by A1,PCOMPS_2:8;
A11:      dist(xP,yP)<r by A10,METRIC_1:12;
          yT in AT` & dom dist(pmet,xT) = the carrier of T by A10,FUNCT_2:def 1
          ,XBOOLE_0:def 4;
          then dist(pmet,xT).yT in dist(pmet,xT).:AT` &
          pmet is_a_pseudometric_of the carrier of T by A1,Th9,FUNCT_1:def 12;
          then dist(pmet,xT).yT in dist(pmet,xT).:AT`& dist(pmet,xT).:AT` is
          non empty bounded_below & pmet is bounded_below by A6,Lm1;
          then inf(dist(pmet,xT).:AT`)<=dist(pmet,xT).yT
          & dist(pmet,xT).yT=pmet.(xT,yT) & inf(dist(pmet,xT).:AT`)=
          inf(pmet,AT`).xT by Def2,Def3,SEQ_4:def 5;
          hence thesis by A3,A11;
        end;
        hence thesis by A9;
      end;
      hence A in Family_open_set PM by PCOMPS_1:def 5;
    end;
  end;
  Family_open_set PM c= the topology of T
  proof
    let A be set;
    assume
A12: A in Family_open_set PM;
    then reconsider AT=A as Subset of T by A1,PCOMPS_2:8;
    per cases;
    suppose AT` is empty;
      then AT`=([#]T)`& [#]T= the carrier of T by XBOOLE_1:37;
      then AT=the carrier of T by TOPS_1:21;
      hence A in the topology of T by PRE_TOPC:def 1;
    end;
    suppose
A13:  AT` is non empty;
      for x holds x in AT iff
      ex U be Subset of T st U is open & U c= AT & x in U
      proof
        let x;
        x in AT implies ex U be Subset of T st U is open & U c= AT & x in U
        proof
          assume
A14:      x in AT;
          then reconsider xT=x as Element of T;
          reconsider xP=x as Element of PM by A1,A14,PCOMPS_2:8;
          consider r such that
A15:      r>0 & Ball(xP,r) c= AT by A12,A14,PCOMPS_1:def 5;
          pmet.(xT,xT)=0 by A1,PCOMPS_1:def 7;
          then
A16:      dist(xP,xP)<r by A3,A15;
          reconsider B=Ball(xP,r) as Subset of T by A1,PCOMPS_2:8;
          AT`c=B` & ex y st y in AT` by A13,A15,SUBSET_1:31,XBOOLE_0:def 1;
          then consider b be set such that
A17:      b in B`;
          reconsider b as Point of T by A17;
          set Inf={p where p is Point of T:inf(pmet,B`).p=0};
          B`=Inf
          proof
            thus B`c=Inf
            proof
              let y such that
A18:          y in B`;
              inf(pmet,B`).y=0 by A1,A18,Th6,Th9;
              hence y in Inf by A18;
            end;
            let y;
            assume y in Inf;
            then consider yT be Point of T such that
A19:        y=yT & inf(pmet,B`).yT=0;
            reconsider yP=yT as Point of PM by A1,PCOMPS_2:8;
            assume not y in B`;
            then
A20:        yT in B by A19,XBOOLE_0:def 4;
            Ball(xP,r) in Family_open_set PM by PCOMPS_1:33;
            then consider s such that
A21:        s>0& Ball(yP,s)c=Ball(xP,r) by A20,PCOMPS_1:def 5;
            b in B` & dom dist(pmet,yT)=the carrier of T by A17,FUNCT_2:def 1;
            then dist(pmet,yT).b in dist(pmet,yT).:B` &

pmet is_a_pseudometric_of the carrier of T by A1,Th9,FUNCT_1:def 12;
            then
A22:        dist(pmet,yT).:B` is non empty bounded_below
            & pmet is bounded_below by A17,Lm1;
            inf(dist(pmet,yT).:B`)=0 by A19,Def3;
            then consider rn such that
A23:        rn in dist(pmet,yT).:B`& rn<0+s by A21,A22,SEQ_4:def 5;
            consider z such that
A24:        z in dom dist(pmet,yT)& z in B`&
            rn =dist(pmet,yT).z by A23,FUNCT_1:def 12;
            reconsider zT=z as Point of T by A24;
            reconsider zP=z as Point of PM by A1,A24,PCOMPS_2:8;
            pmet.(yT,zT)<s by A23,A24,Def2;
            then dist(yP,zP)<s by A3;
            then zP in Ball(yP,s) by METRIC_1:12;
            then B` meets B by A21,A24,XBOOLE_0:3;
            hence thesis by XBOOLE_1:79;
          end;
          then B`=Cl B` by A2,A17;
          then B is open & xP in B by A16,METRIC_1:12,TOPS_1:30;
          hence thesis by A15;
        end;
        hence thesis;
      end;
      then AT is open by TOPS_1:57;
      hence A in the topology of T by PRE_TOPC:def 5;
    end;
  end;
  then the topology of T =Family_open_set PM by A4,XBOOLE_0:def 10;
  hence thesis by A1,PCOMPS_1:def 9;
end;

theorem Th11:
  for FS2 st (for n ex pmet st FS2.n=pmet &
  pmet is_a_pseudometric_of the carrier of T)
  & for pq holds FS2#pq is summable
  for pmet st for pq holds pmet.pq=Sum(FS2#pq)
  holds pmet is_a_pseudometric_of the carrier of T
proof
  set cT=the carrier of T;
  let FS2 such that
A1: for n ex pmet st FS2.n=pmet & pmet is_a_pseudometric_of cT and
A2: for pq holds FS2#pq is summable;
  let pmet such that
A3: for pq holds pmet.pq=Sum(FS2#pq);
  now
    let a,b,c be Point of T;
    thus pmet.(a,a)=0
    proof
      set aa=[a,a];
      set F=FS2#aa;
      now
        let n;
        consider pmet1 such that
A4:     FS2.n=pmet1 & pmet1 is_a_pseudometric_of cT by A1;
        pmet1.(a,a)=0 by A4,NAGATA_1:28;
        hence F.n=0*F.n by A4,SEQFUNC:def 11;
      end;
      then F=0(#)F & F is summable by A2,SEQ_1:13;
      then Sum(F)=0*Sum(F) by SERIES_1:13;
      hence thesis by A3;
    end;
    thus pmet.(a,c)<=pmet.(a,b)+pmet.(c,b)
    proof
      set ab=[a,b],cb=[c,b],ac=[a,c];
      set Fac=FS2#ac;
      set Fab=FS2#ab;
      set Fcb=FS2#cb;
      Fab is summable & Fcb is summable by A2;
      then
A5:   Fab+Fcb is summable & Sum(Fab+Fcb)=Sum(Fab)+Sum(Fcb) by SERIES_1:10;
      now
        let n;
        consider pmet1 such that
A6:     FS2.n=pmet1 & pmet1 is_a_pseudometric_of cT by A1;
        pmet1.(a,c)<=pmet1.(a,b)+pmet1.(c,b) & 0<=pmet1.(a,c) & pmet1.(c,b)=
        pmet1.cb & pmet1.(a,c)=pmet1.ac by A6,NAGATA_1:28,29;
        then 0<=FS2.n.ac & FS2.n.ac<=(FS2.n.ab+FS2.n.cb) & FS2.n.ac=
        Fac.n & FS2.n.ab=Fab.n by A6,SEQFUNC:def 11;
        then 0<=Fac.n & Fac.n<=(Fab.n+Fcb.n) by SEQFUNC:def 11;
        hence 0<=Fac.n & Fac.n<=(Fab+Fcb).n by SEQ_1:11;
      end;
      then Sum(Fac)<=Sum(Fab+Fcb)&Sum(Fab)=pmet.ab&Sum(Fcb)=pmet.cb &
      pmet.ab=pmet.(a,b)& pmet.cb=pmet.(c,b) by A3,A5,SERIES_1:24;
      hence thesis by A3,A5;
    end;
  end;
  hence thesis by NAGATA_1:28;
end;

theorem Th12:
  for n,seq st for m st m<=n holds seq.m<=r
  for m st m<=n holds Partial_Sums(seq).m <= r * (m+1)
proof
  let n,seq such that
A1: for m st m<=n holds seq.m<=r;
  defpred P[Element of NAT] means $1<=n implies Partial_Sums(seq).$1<=r*($1+1);
A2: P[0]
  proof
    Partial_Sums(seq).0=seq.0& 0<=n by SERIES_1:def 1;
    hence thesis by A1;
  end;
A3: for m st P[m] holds P[m+1]
  proof
    let m such that
A4: P[m];
    assume m+1<=n;
    then Partial_Sums(seq).m <= r*(m+1)&seq.(m+1)<=r & Partial_Sums(seq).(m+1)
    = Partial_Sums(seq).m+seq.(m+1) by A1,A4,NAT_1:13,SERIES_1:def 1;
    then Partial_Sums(seq).(m+1)<=(r*(m+1)) + r by XREAL_1:9;
    hence thesis;
  end;
  for m holds P[m] from NAT_1:sch 1(A2,A3);
  hence thesis;
end;

theorem Th13:
  for k holds abs(Partial_Sums(seq).k)<=Partial_Sums(abs(seq)).k
proof
  set PS=Partial_Sums(seq),absPS=Partial_Sums(abs(seq));
  defpred P[Element of NAT] means abs(PS.$1)<=absPS.$1;
A1: P[0]
  proof
    absPS.0=(abs seq).0 & (abs seq).0=abs(seq.0) by SEQ_1:16,SERIES_1:def 1;
    hence thesis by SERIES_1:def 1;
  end;
A2: for k st P[k] holds P[k+1]
  proof
    let k such that
A3: P[k];
    PS.(k+1)=PS.k+seq.(k+1) by SERIES_1:def 1;
    then abs(PS.(k+1))<=abs(PS.k)+abs(seq.(k+1))
    & abs(PS.k)+abs(seq.(k+1)) <=absPS.k+abs(seq.(k+1))
    & (abs seq).(k+1)= abs(seq.(k+1)) by A3,COMPLEX1:142,SEQ_1:16,XREAL_1:9;
    then abs(PS.(k+1))<=absPS.k+(abs seq).(k+1) by XXREAL_0:2;
    hence thesis by SERIES_1:def 1;
  end;
  for k holds P[k] from NAT_1:sch 1 (A1,A2);
  hence thesis;
end;

theorem Th14:
  for FS1 being Functional_Sequence of the carrier of T,REAL
  st (for n ex f st FS1.n=f & f is continuous & for p holds f.p>=0)
  & (ex seq st seq is summable & for n,p holds (FS1#p).n<=seq.n)
  for f st for p holds f.p=Sum(FS1#p) holds f is continuous
proof
  let FS1 be Functional_Sequence of the carrier of T,REAL such that
A1: for n ex f st FS1.n=f & f is continuous & for p holds f.p>=0 and
A2: ex seq st seq is summable & for n,p holds (FS1#p).n<=seq.n;
  let f such that
A3: for p holds f.p=Sum(FS1#p);
  reconsider fR=f as Function of T,R^1 by TOPMETR:24;
  now
    let p;
    for R being Subset of R^1 st R is open & fR.p in R
    ex U being Subset of T st U is open & p in U & fR.:U c= R
    proof
      let R be Subset of R^1 such that
A4:   R is open & fR.p in R;
      reconsider fRp=fR qua real-valued Function.p as Point of RealSpace
      by METRIC_1:def 14;
      consider rn such that
A5:   rn>0 & Ball(fRp,rn) c= R by A4,TOPMETR:22,def 7;
      set r2=rn/2,r4=rn/4;
      reconsider r2,r4 as Real;
      consider seq such that
A6:   seq is summable & for n,q holds (FS1#q).n<=seq.n by A2;
      Partial_Sums(seq) is convergent & r4>0 by A5,A6,SEQ_2:3,SERIES_1:def 2;
      then consider n such that
A7:   for m st n<=m holds abs(Partial_Sums(seq).m-lim Partial_Sums(seq))<r4
      by SEQ_2:def 7;
A8:   for k,q holds (FS1#q).k>=0
      proof
        let k,q;
        consider f1 such that
A9:     FS1.k=f1 & f1 is continuous & for q holds f1.q>=0 by A1;
        FS1.k.q=(FS1#q).k by SEQFUNC:def 11;
        hence (FS1#q).k>=0 by A9;
      end;
A10:  for k holds (seq^\(n+1)).k>=0
      proof
        let k;
        0<=(FS1#p).(n+1+k) & (FS1#q).(n+1+k)<=seq.(n+1+k) &
        seq.(n+1+k)=(seq^\(n+1)).k by A6,A8,SEQM_3:def 9;
        hence thesis;
      end;
      abs(Partial_Sums(seq).n-lim Partial_Sums(seq))<r4 & lim Partial_Sums(
      seq ) =Sum(seq) & Sum(seq)=Partial_Sums(seq).n + Sum(seq^\(n+1))
      by A6,A7,SERIES_1:18,def 3;
      then abs(-Sum(seq^\(n+1)))<r4 & seq^\(n+1) is summable by A6,SERIES_1:15;
      then abs(Sum(seq^\(n+1)))<r4 & Sum(seq^\(n+1))>=0 by A10,COMPLEX1:138
      ,SERIES_1:21;
      then
A11:  Sum(seq^\(n+1))<r4 by ABSVALUE:def 1;
A12:  for q holds FS1#q is summable & 0<=Sum((FS1#q)^\(n+1))
      & Sum((FS1#q)^\(n+1))<r4
      proof
        let q;
        set F=FS1#q;
A13:    for k holds 0<=F.k & F.k<=seq.k by A6,A8;
        then
A14:    F is summable by A6,SERIES_1:24;
        F^\(n+1) is summable & seq^\(n+1) is summable &
        for k holds 0<=(F^\(n+1)).k & (F^\(n+1)).k<=(seq^\(n+1)).k
        proof
          now
            let k;
            0<=F.(n+1+k) & F.(n+1+k)<=seq.(n+1+k) & seq.(n+1+k)=
            (seq^\(n+1)).k & F.(n+1+k)=(F^\(n+1)).k by A6,A8,SEQM_3:def 9;
            hence 0<=(F^\(n+1)).k & (F^\(n+1)).k<=(seq^\(n+1)).k;
          end;
          hence thesis by A6,A14,SERIES_1:15;
        end;
        then 0<=Sum(F^\(n+1)) & Sum(F^\(n+1))<=Sum(seq^\(n+1)) by SERIES_1:21
        ,24;
        hence thesis by A6,A11,A13,SERIES_1:24,XXREAL_0:2;
      end;
      defpred Sn[set,set] means ex SS being Subset of T st SS = $2 &
      SS is open & p in SS & for f1 st FS1.$1=f1
      for f1p be Point of RealSpace st f1p=f1.p holds
      f1.:SS c= Ball(f1p,r2/(n+1));
A15:  for k being set st k in {0}\/Seg(n)
      ex U be set st U in bool the carrier of T & Sn[k,U]
      proof
        let k be set;
        assume k in {0}\/Seg(n);
        then k in Seg(n) or k in {0} by XBOOLE_0:def 2;
        then reconsider k as Element of NAT by TARSKI:def 1;
        consider f1 such that
A16:    FS1.k=f1 & f1 is continuous & for p holds f1.p>=0 by A1;
        reconsider f1'=f1 as Function of T,R^1 by TOPMETR:24;
        reconsider f1p=f1' qua real-valued Function.p as Point of RealSpace
        by METRIC_1:def 14;
        set B=Ball(f1p,r2/(n+1));
        reconsider B as Subset of R^1 by METRIC_1:def 14,TOPMETR:24;
        dist(f1p,f1p)=0 & r2>0 & n+1<>0 & n+1>=0 by A5,METRIC_1:1,SEQ_2:3;
        then dist(f1p,f1p)<r2/(n+1) & f1' is continuous by A16,REAL_2:127
        ,TOPREAL6:83;
        then f1p in B & B is open & f1' is_continuous_at p by METRIC_1:12
        ,TMAP_1:55,TOPMETR:21,def 7;
        then consider U be Subset of T such that
A17:    U is open & p in U & f1'.:U c= B by TMAP_1:48;
        for f1 st FS1.k=f1 for f1p be Point of RealSpace st f1p=f1.p holds
        f1.:U c= Ball(f1p,r2/(n+1)) by A16,A17;
        hence thesis by A17;
      end;
      consider FSn be Function of ({0}\/Seg(n)),bool the carrier of T such that
A18:  for k being set st k in {0}\/Seg(n) holds Sn[k,FSn.k]
      from FUNCT_2:sch 1(A15);
      reconsider RNG=rng FSn as Subset-Family of T;
A19:  RNG is open
      proof
        let Q be Subset of T;
        assume Q in RNG;
        then consider x such that
A20:    x in dom FSn & FSn.x=Q by FUNCT_1:def 5;
        consider SS being Subset of T such that
A21:    SS = FSn.x & SS is open & p in SS & for f1 st FS1.x=f1
        for f1p be Point of RealSpace st f1p=f1.p holds
        f1.:SS c= Ball(f1p,r2/(n+1)) by A18,A20;
        thus thesis by A20,A21;
      end;
      0 in {0} by TARSKI:def 1;
      then 0 in {0}\/Seg(n) by XBOOLE_0:def 2;
      then reconsider RNG as non empty Subset-Family of T by FUNCT_2:6;
A22:  p in meet RNG
      proof
        now
          let Fx be set;
          assume Fx in RNG;
          then consider x such that
A23:      x in dom FSn & FSn.x=Fx by FUNCT_1:def 5;
          consider SS being Subset of T such that
A24:      SS = FSn.x & SS is open & p in SS & for f1 st FS1.x=f1
          for f1p be Point of RealSpace st f1p=f1.p holds
          f1.:SS c= Ball(f1p,r2/(n+1)) by A18,A23;
          thus p in Fx by A23,A24;
        end;
        hence thesis by SETFAM_1:def 1;
      end;
      fR.:(meet RNG)c=R
      proof
        let fRq be set;
        assume fRq in fR.:(meet RNG);
        then consider q be set such that
A25:    q in dom fR & q in meet RNG & fRq=fR.q by FUNCT_1:def 12;
        reconsider q as Point of T by A25;
        reconsider fRq=fR qua real-valued Function.q as Point of RealSpace
        by METRIC_1:def 14;
        set sp=FS1#p, sq=FS1#q, spn=sp^\(n+1), sqn=sq^\(n+1);
        set PSp=Partial_Sums(sp),PSq=Partial_Sums(sq),PSpq=Partial_Sums(sp-sq);
        set absPSpq=Partial_Sums(abs(sp-sq));
        0<=Sum(spn)&0<=Sum(sqn) by A12;
        then abs(Sum(spn))=Sum(spn) & abs(Sum(sqn))=Sum(sqn) & Sum(spn)<r4
        & Sum(sqn)<r4 by A12,ABSVALUE:def 1;
        then
A26:    abs(Sum(spn))+abs(Sum(sqn))<r4+r4 by XREAL_1:10;
A27:    for k st k<=n holds (abs(sp-sq)).k <=r2/(n+1)
        proof
          let k such that
A28:      k<=n;
          k=0 or k>=1 by NAT_1:14;
          then k in {0} or k in Seg n by A28,FINSEQ_1:3,TARSKI:def 1;
          then
A29:      k in {0}\/Seg n by XBOOLE_0:def 2;
          consider f1 such that
A30:      FS1.k=f1 & f1 is continuous & for p holds f1.p>=0 by A1;
          reconsider f1p=f1.p,f1q=f1.q as Point of RealSpace
          by METRIC_1:def 14;
          FSn.k in RNG & q in meet RNG by A25,A29,FUNCT_2:6;
          then q in FSn.k & dom f1=the carrier of T by FUNCT_2:def 1
          ,SETFAM_1:def 1;
          then
A31:      f1q in f1.:(FSn.k) by FUNCT_1:def 12;
          consider SS being Subset of T such that
A32:      SS = FSn.k & SS is open & p in SS & for f1 st FS1.k=f1
          for f1p be Point of RealSpace st f1p=f1.p holds
          f1.:SS c= Ball(f1p,r2/(n+1)) by A18,A29;
          f1.:(FSn.k) c= Ball(f1p,r2/(n+1)) by A30,A32;
          then dist(f1p,f1q)<r2/(n+1) by A31,METRIC_1:12;
          then abs(f1.p-f1.q)<r2/(n+1) & f1.p=sp.k & f1.q=sq.k & dom (sp-sq)=
          NAT by A30,FUNCT_2:def 1,SEQFUNC:def 11,TOPMETR:15;
          then abs(sp.k-sq.k)<r2/(n+1) & (sp-sq).k=sp.k-sq.k by VALUED_1:13;
          hence thesis by SEQ_1:16;
        end;
        PSp-PSq=PSpq & dom (PSp-PSq)= NAT by SEQ_1:3,SERIES_1:9;
        then abs(PSp.n-PSq.n)=abs(PSpq.n) & abs(PSpq.n)<=absPSpq.n & absPSpq.n
        <=(r2/(n+1))*(n+1) by A27,Th12,Th13,VALUED_1:13;
        then abs(PSp.n-PSq.n)<=(r2/(n+1))*(n+1) by XXREAL_0:2;
        then
A33:    abs(PSp.n-PSq.n)<=r2 by XCMPLX_1:88;
        sp is summable & sq is summable by A12;
        then Sum(sp)=PSp.n+Sum(spn) & Sum(sq)=PSq.n+Sum(sqn) & fRp=Sum(sp) &
        fRq=Sum(sq) by A3,SERIES_1:18;
        then abs(fR.p-fR.q) =abs((PSp.n-PSq.n)+(Sum(spn)-Sum(sqn)));
        then
A34:    abs(fR.p-fR.q)<=abs(PSp.n-PSq.n)+abs(Sum(spn)-Sum(sqn)) &

abs(Sum(spn)-Sum(sqn))<=abs(Sum(spn))+abs(Sum(sqn)) by COMPLEX1:142,143;
        then abs(Sum(spn)-Sum(sqn))<r2 by A26,XXREAL_0:2;
        then abs(PSp.n-PSq.n)+abs(Sum(spn)-Sum(sqn))<r2+r2 by A33,XREAL_1:10;
        then abs(fR.p-fR.q)<rn by A34,XXREAL_0:2;
        then dist(fRp,fRq)<rn by TOPMETR:15;
        then fRq in Ball(fRp,rn) by METRIC_1:12;
        hence thesis by A5,A25;
      end;
      hence thesis by A19,A22,TOPS_2:27;
    end;
    hence fR is_continuous_at p by TMAP_1:48;
  end;
  then fR is continuous by TMAP_1:55;
  hence thesis by TOPREAL6:83;
end;

theorem Th15:
  for s, FS2 st for n ex pmet st FS2.n=pmet &
  pmet is_a_pseudometric_of the carrier of T & (for pq holds pmet.pq<=s) &
  for pmet' st pmet=pmet' holds pmet' is continuous
  for pmet st for pq holds pmet.pq=Sum((1/2)GeoSeq(#)(FS2#pq))
  holds pmet is_a_pseudometric_of the carrier of T &
  for pmet' st pmet=pmet' holds pmet' is continuous
proof
  set cT=the carrier of T,cTT= the carrier of [:T,T:];
  set Geo=(1/2)GeoSeq;
  let s, FS2 such that
A1: for n ex pmet st
  FS2.n=pmet & pmet is_a_pseudometric_of cT & (for pq holds pmet.pq<=s)
  & for pmet' st pmet=pmet' holds pmet' is continuous;
  let pmet such that
A2: for pq holds pmet.pq=Sum(Geo(#)(FS2#pq));
  deffunc GF(Nat)=(Geo.$1)(#)(FS2.$1);
  consider GeoF be Functional_Sequence of [:cT,cT:],REAL such that
A3: for n be Nat holds GeoF.n = GF(n) from SEQFUNC:sch 1;
A4: for n ex pmet1 st GeoF.n=pmet1 & pmet1 is_a_pseudometric_of cT
  proof
    let n;
    consider pmet1 such that
A5: FS2.n=pmet1 & pmet1 is_a_pseudometric_of cT & (for pq holds pmet1.pq<=s)
    & for pmet1' st pmet1=pmet1' holds pmet1' is continuous by A1;
    deffunc F(Element of cT,Element of cT)=(Geo.n)*pmet1.($1,$2);
    consider GF be Function of [:cT,cT:],REAL such that
A6: for p for q holds GF.(p,q)=F(p,q) from BINOP_1:sch 4;
A7: GeoF.n=GF
    proof
A8:   (GeoF.n)=(Geo.n)(#)FS2.n by A3;
      then
A9:   dom (FS2.n)=dom (GeoF.n)& dom pmet1=[:cT,cT:]& dom GF=
      [:cT,cT:] by FUNCT_2:def 1,VALUED_1:def 5;
      now
        let x,y;
        assume
A10:    [x,y] in dom (GeoF.n);
        then reconsider x'=x,y'=y as Point of T by ZFMISC_1:106;
        (GeoF.n).(x,y)=(Geo.n)*pmet1.[x,y] & GF.(x',y')=
        (Geo.n)*pmet1.(x',y') by A5,A6,A8,A10,VALUED_1:def 5;
        hence (GeoF.n).(x,y)=GF.(x,y);
      end;
      hence thesis by A5,A9,BINOP_1:32;
    end;
    GF is_a_pseudometric_of cT
    proof
      now
        let a,b,c be Point of T;
        thus GF.(a,a)=0
        proof
          GF.(a,a)=(Geo.n)*pmet1.(a,a) & pmet1.(a,a)=0 by A5,A6,NAGATA_1:28;
          hence thesis;
        end;
        thus GF.(a,c)<=GF.(a,b)+GF.(c,b)
        proof
          (1/2)|^n>0 by NEWTON:102;
          then pmet1.(a,c)<=pmet1.(a,b)+pmet1.(c,b) & ((1/2)GeoSeq.n)>0
          by A5,NAGATA_1:28,PREPOWER:def 1;
          then pmet1.(a,c)*((1/2)GeoSeq.n)<=(pmet1.(a,b)+
          pmet1.(c,b))*(Geo.n) by XREAL_1:66;
          then GF.(a,c)<=(Geo.n)*pmet1.(a,b)+(Geo.n)*pmet1.(c,b)&
          (Geo.n)*pmet1.(a,b)=GF.(a,b) & (Geo.n)*pmet1.(c,b)=GF.(c,b) by A6;
          hence thesis;
        end;
      end;
      hence thesis by NAGATA_1:28;
    end;
    hence thesis by A7;
  end;
  set SGeo=s(#)(Geo);
A11: for pq,k holds 0<=(GeoF#pq).k & (GeoF#pq).k<=SGeo.k
  proof
    let pq,k;
    consider pmet1 such that
A12: FS2.k=pmet1 & pmet1 is_a_pseudometric_of cT & (for pq holds pmet1.pq<=s)
    & for pmet1' st pmet1=pmet1' holds pmet1' is continuous by A1;
    consider x,y such that
A13: x in cT & y in cT & [x,y]=pq by ZFMISC_1:def 2;
    reconsider x,y as Point of T by A13;
    dom (FS2.k)=[:cT,cT:] by A12,FUNCT_2:def 1;
    then dom ((Geo.k)(#)(FS2.k))=[:cT,cT:] by VALUED_1:def 5;
    then
A14: (Geo.k)*(FS2.k).pq=((Geo.k)(#)(FS2.k)).pq by VALUED_1:def 5
      .=GeoF.k.pq by A3
      .=(GeoF#pq).k by SEQFUNC:def 11;
    (1/2)|^k>0 by NEWTON:102;
    then 0<=pmet1.(x,y)& pmet1.pq<=s& pmet1.(x,y)=pmet1.pq &Geo.k>0 by A12,A13,
    NAGATA_1:29,PREPOWER:def 1;
    then 0<=(Geo.k)*(pmet1.pq) &(Geo.k)*(pmet1.pq)<=(Geo.k)*s by XREAL_1:66;
    hence thesis by A12,A14,SEQ_1:13;
  end;
  0<(1/2)& (1/2)<1;
  then abs(1/2)<1 by ABSVALUE:def 1;
  then
A15: Geo is summable by SERIES_1:28;
A16: for pq holds GeoF#pq is summable
  proof
    let pq;
    for k holds 0<=(GeoF#pq).k & (GeoF#pq).k<=SGeo.k & SGeo is summable
    by A11,A15,SERIES_1:13;
    hence thesis by SERIES_1:24;
  end;
A17: for pq holds pmet.pq=Sum(GeoF#pq)
  proof
    let pq;
    now
      let z;
      assume z in NAT;
      then reconsider k=z as Element of NAT;
      consider pmet1 such that
A18:  FS2.k=pmet1 & pmet1 is_a_pseudometric_of cT & (for pq holds pmet1.pq<=s)
      & for pmet1' st pmet1=pmet1' holds pmet1' is continuous by A1;
      dom (FS2.k)=[:cT,cT:] by A18,FUNCT_2:def 1;
      then dom ((Geo.k)(#)(FS2.k))=[:cT,cT:] by VALUED_1:def 5;
      then (Geo.k)*(FS2.k).pq=((Geo.k)(#)(FS2.k)).pq by VALUED_1:def 5
        .=GeoF.k.pq by A3
        .=(GeoF#pq).k by SEQFUNC:def 11;
      then (GeoF#pq).k=(Geo.k)*(FS2#pq).k by SEQFUNC:def 11
        .=(Geo(#)(FS2#pq)).k by SEQ_1:12;
      hence (Geo(#)(FS2#pq)).z=(GeoF#pq).z;
    end;
    then Geo(#)(FS2#pq)=(GeoF#pq) by SEQ_1:8;
    hence thesis by A2;
  end;
  reconsider GeoF'=GeoF as Functional_Sequence of cTT,REAL by BORSUK_1:def 5;
A19: for pq,pq' st pq=pq' holds GeoF#pq=GeoF'#pq'
  proof
    let pq,pq' such that
A20: pq=pq';
    now
      let x such that
A21:  x in NAT;
      reconsider k=x as Element of NAT by A21;
      (GeoF#pq).k=GeoF.k.pq & (GeoF'#pq').k=GeoF'.k.pq' by SEQFUNC:def 11;
      hence (GeoF#pq).x=(GeoF'#pq').x by A20;
    end;
    hence thesis by SEQ_1:8;
  end;
A22: for n ex f be RealMap of [:T,T:] st (GeoF'.n=f & f is continuous &
  for pq' holds f.pq'>=0)
  proof
    let n;
    consider pmet1 such that
A23: FS2.n=pmet1 & pmet1 is_a_pseudometric_of cT & (for pq holds pmet1.pq<=s)
    & for pmet1' st pmet1=pmet1' holds pmet1' is continuous by A1;
    cTT=[:cT,cT:] by BORSUK_1:def 5;
    then reconsider pmet1'=pmet1 as RealMap of [:T,T:];
    reconsider pR=pmet1' as Function of [:T,T:],R^1 by TOPMETR:24;
    pmet1' is continuous by A23;
    then pR is continuous by TOPREAL6:83;
    then consider fR be Function of [:T,T:],R^1 such that
A24: (for pq' be Point of [:T,T:],rn st pR.pq'=rn holds fR.pq'=
    (Geo.n)*rn) & fR is continuous by JGRAPH_2:33;
    reconsider f=fR as RealMap of [:T,T:] by TOPMETR:24;
A25: (GeoF'.n)=(Geo.n)(#)FS2.n by A3;
    then
A26: dom (FS2.n)=dom (GeoF'.n)& dom pmet1=[:cT,cT:] & dom f=cTT
    & [:cT,cT:]=cTT by BORSUK_1:def 5,FUNCT_2:def 1,VALUED_1:def 5;
A27: now
      let pq' such that pq' in dom (GeoF'.n);
      (GeoF'.n).pq'=(Geo.n)*pmet1.pq' & f.pq'=(Geo.n)*pmet1.pq'
      by A23,A24,A25,A26,VALUED_1:def 5;
      hence (GeoF'.n).pq'=f.pq';
    end;
    take f;
    now
      let pq';
      reconsider pq=pq' as Element of [:cT,cT:] by BORSUK_1:def 5;
      GeoF'.n.pq'=(GeoF#pq).n & (GeoF#pq).n >=0 by A11,SEQFUNC:def 11;
      hence f.pq'>=0 by A23,A26,A27;
    end;
    hence thesis by A23,A24,A26,A27,PARTFUN1:34,TOPREAL6:83;
  end;
A28: ex seq st seq is summable & for n,pq' holds (GeoF'#pq').n<=seq.n
  proof
    take SGeo;
    thus SGeo is summable by A15,SERIES_1:13;
    now
      let n,pq';
      reconsider pq=pq' as Element of [:cT,cT:] by BORSUK_1:def 5;
      (GeoF'#pq').n =(GeoF#pq).n by A19;
      hence (GeoF'#pq').n <=SGeo.n by A11;
    end;
    hence thesis;
  end;
  now
    let pmet1' such that
A29: pmet=pmet1';
    for pq' holds pmet1'.pq'=Sum(GeoF'#pq')
    proof
      let pq';
      reconsider pq=pq' as Element of [:cT,cT:] by BORSUK_1:def 5;
      pmet.pq=Sum(GeoF#pq) & GeoF#pq=GeoF'#pq' by A17,A19;
      hence thesis by A29;
    end;
    hence pmet1' is continuous by A22,A28,Th14;
  end;
  hence thesis by A4,A16,A17,Th11;
end;

theorem Th16:
  for pmet st pmet is_a_pseudometric_of the carrier of T
  & for pmet' st pmet=pmet' holds pmet' is continuous
  for A be non empty Subset of T,p holds p in Cl A implies inf(pmet,A).p=0
proof
  let pmet such that
A1: pmet is_a_pseudometric_of the carrier of T and
A2: for pmet' st pmet=pmet' holds pmet' is continuous;
  let A be non empty Subset of T,p;
  set rn=0;
  reconsider Z={rn},infA=inf(pmet,A).:A as Subset of R^1 by TOPMETR:24;
  reconsider InfA=inf(pmet,A) as Function of T,R^1 by TOPMETR:24;
  assume
A3: p in Cl A;
  infA c= Z
  proof
    let infa be set;
    assume infa in infA;
    then consider a be set such that
A4: a in dom inf(pmet,A) & a in A & infa=inf(pmet,A).a by FUNCT_1:def 12;
    infa=0 by A1,A4,Th6;
    hence thesis by TARSKI:def 1;
  end;
  then
A5: Cl (infA) c= Cl Z by PRE_TOPC:49;
  for p holds dist(pmet,p) is continuous by A2,Th4;
  then inf(pmet,A) is continuous &rn is Point of R^1 by A1,Th8,TOPMETR:24;
  then InfA is continuous & Z is closed by PCOMPS_1:10,TOPREAL6:83;
  then InfA.:(Cl A) c= Cl(InfA.:A) & Z=Cl Z & dom inf(pmet,A)= the carrier of
  T by FUNCT_2:def 1,PRE_TOPC:52,TOPS_2:57;
  then InfA.:(Cl A)c=Z & inf(pmet,A).p in inf(pmet,A).:(Cl A)
  by A3,A5,FUNCT_1:def 12,XBOOLE_1:1;
  hence thesis by TARSKI:def 1;
end;

theorem Th17:
  for T st T is_T1 for s, FS2 st (for n ex pmet st FS2.n=pmet &
  pmet is_a_pseudometric_of the carrier of T & (for pq holds pmet.pq<=s) &
  for pmet' st pmet=pmet' holds pmet' is continuous)
  & for p,A' st not p in A' & A' is closed
  ex n st for pmet st FS2.n=pmet holds inf(pmet,A').p>0 holds
  (ex pmet st pmet is_metric_of the carrier of T &
  for pq holds pmet.pq=Sum((1/2)GeoSeq(#)(FS2#pq))) & T is metrizable
proof
  let T such that
A1: T is_T1;
  let s,FS2 such that
A2: for n ex pmet st FS2.n=pmet &
  pmet is_a_pseudometric_of the carrier of T & (for pq holds pmet.pq<=s) &
  for pmet' st pmet=pmet' holds pmet' is continuous and
A3: for p,A' st not p in A' & A' is closed
  ex n st for pmet st FS2.n=pmet holds inf(pmet,A').p>0;
  set cT=the carrier of T,Geo=(1/2)GeoSeq;
  deffunc pm(Element of cT,Element of cT)=Sum(Geo(#)(FS2#[$1,$2]));
  consider pmet such that
A4: for p,q holds pmet.(p,q)=pm(p,q) from BINOP_1:sch 4;
A5: for pq holds pmet.pq=Sum(Geo(#)(FS2#pq))
  proof
    let pq;
    consider p,q be set such that
A6: p in cT & q in cT & pq=[p,q] by ZFMISC_1:def 2;
    pmet.pq = pmet.(p,q) by A6;
    hence thesis by A4,A6;
  end;
A7: for pq,n holds 0<=(Geo(#)(FS2#pq)).n & (Geo(#)(FS2#pq)).n<=(s(#)Geo).n
  proof
    let pq,n;
    consider pmet1 such that
A8: FS2.n=pmet1 & pmet1 is_a_pseudometric_of cT & (for pq holds pmet1.pq<=s)
    & for pmet1' st pmet1=pmet1' holds pmet1' is continuous by A2;
    consider x,y such that
A9: x in cT & y in cT & [x,y]=pq by ZFMISC_1:def 2;
    reconsider x,y as Point of T by A9;
    (1/2)|^n>0 by NEWTON:102;
    then 0<=pmet1.(x,y)& pmet1.pq<=s & pmet1.(x,y)=pmet1.pq & Geo.n>0 &
    pmet1.pq=(FS2#pq).n by A8,A9,NAGATA_1:29,PREPOWER:def 1,SEQFUNC:def 11;
    then 0<=(Geo.n)*(FS2#pq).n &(Geo.n)*(FS2#pq).n<=(Geo.n)*s &
    (Geo.n)*s=(s(#)Geo).n by SEQ_1:13,XREAL_1:66;
    hence thesis by SEQ_1:12;
  end;
A10: for p,q holds pmet.(p,q)=0 implies p=q
  proof
    let p,q;
    assume
A11: pmet.(p,q)=0 & p<>q;
    set pq=[p,q];
    0<(1/2)& (1/2)<1;
    then abs(1/2)<1 by ABSVALUE:def 1;
    then Geo is summable by SERIES_1:28;
    then s(#)Geo is summable & pmet.pq=pmet.(p,q) &
    for n holds 0<=(Geo(#)(FS2#pq)).n &
    (Geo(#)(FS2#pq)).n<=(s(#)Geo).n by A7,SERIES_1:13;
    then
A12: Geo(#)(FS2#pq) is summable & Sum(Geo(#)(FS2#pq))=0
    & for n holds 0<=(Geo(#)(FS2#pq)).n by A5,A11,SERIES_1:24;
    set Q={q};
    Q is closed & not p in Q by A1,A11,TARSKI:def 1,URYSOHN1:27;
    then consider n such that
A13: for pmet1 st FS2.n=pmet1 holds inf(pmet1,Q).p>0 by A3;
    consider pmet1 such that
A14: FS2.n=pmet1 & pmet1 is_a_pseudometric_of cT & (for pq holds pmet1.pq<=s)
    & for pmet1' st pmet1=pmet1' holds pmet1' is continuous by A2;
    dom dist(pmet1,p)=cT& q in Q & pmet1 is bounded_below & inf(pmet1,Q).p> 0
    by A13,A14,Lm1,FUNCT_2:def 1,TARSKI:def 1;
    then dist(pmet1,p).q in dist(pmet1,p).:Q & inf(dist(pmet1,p).:Q)>0
    & dist(pmet1,p).:Q is non empty bounded_below
    by A14,Def3,Lm1,FUNCT_1:def 12;
    then
A15: dist(pmet1,p).q>0 by SEQ_4:def 5;
    (Geo(#)(FS2#pq)).n=0 & (1/2)|^n>0 by A12,NEWTON:102,RSSPACE:21;
    then Geo.n *(FS2#pq).n=0 & Geo.n<>0 by PREPOWER:def 1,SEQ_1:12;
    then (FS2#pq).n=0 & FS2.n.pq=pmet1.(p,q) & pmet1.(p,q)=dist(pmet1,p).q
    by A14,Def2,XCMPLX_1:6;
    hence thesis by A15,SEQFUNC:def 11;
  end;
  pmet is_a_pseudometric_of cT by A2,A5,Th15;
  then pmet is Reflexive discerning symmetric triangle by A10,METRIC_1:def 4
  ,NAGATA_1:def 10;
  then
A16: pmet is_metric_of cT by METRIC_6:3;
  hence ex pmet st pmet is_metric_of the carrier of T &
  for pq holds pmet.pq=Sum((1/2)GeoSeq(#)(FS2#pq)) by A5;
  for A be non empty Subset of T holds
  Cl A={p where p is Point of T:inf(pmet,A).p=0}
  proof
    let A be non empty Subset of T;
    set INF={p where p is Point of T:inf(pmet,A).p=0};
A17: Cl A c= INF
    proof
      let x;
      assume
A18:  x in Cl A;
      then reconsider p=x as Point of T;
      pmet is_a_pseudometric_of cT & for pmet' st pmet=pmet'
      holds pmet' is continuous by A2,A5,Th15;
      then inf(pmet,A).p=0 by A18,Th16;
      hence thesis;
    end;
    INF c= Cl A
    proof
      let x such that
A19:  x in INF;
      assume
A20:  not x in Cl A;
      consider p be Point of T such that
A21:  x=p & inf(pmet,A).p=0 by A19;
      A c= Cl A & ex y st y in A by PRE_TOPC:48,XBOOLE_0:def 1;
      then consider n such that
A22:  for pmet1 st FS2.n=pmet1 holds inf(pmet1,Cl A).p>0 by A3,A20,A21;
      consider pmet1 such that
A23:  FS2.n=pmet1 & pmet1 is_a_pseudometric_of cT & (for pq holds pmet1.pq<=s)
      & for pmet1' st pmet1=pmet1' holds pmet1' is continuous by A2;
      set r=Geo.n*inf(pmet1,Cl A).p;
      (1/2)|^n>0 by NEWTON:102;
      then Geo.n>0 & inf(pmet1,Cl A).p>0 by A22,A23,PREPOWER:def 1;
      then
A24:  r>0 by REAL_2:122;
      then
A25:  r/2>0 by SEQ_2:3;
A26:  pmet is_a_pseudometric_of cT by A2,A5,Th15;
      then dist(pmet,p).:A is non empty bounded_below by Lm1;
      then consider rn such that
A27:  rn in dist(pmet,p).:A & rn<inf(dist(pmet,p).:A)+r/2 by A25,SEQ_4:def 5;
      consider a be set such that
A28:  a in dom dist(pmet,p) & a in A & rn=dist(pmet,p).a by A27,FUNCT_1:def 12;
      reconsider a as Point of T by A28;
      reconsider pa=[p,a] as Element of [:cT,cT:];
      0<1/2 & 1/2<1;
      then abs(1/2)<1 by ABSVALUE:def 1;
      then Geo is summable by SERIES_1:28;
      then s(#)Geo is summable & for k holds 0<=(Geo(#)(FS2#pa)).k &
      (Geo(#)(FS2#pa)).k<=(s(#)Geo).k by A7,SERIES_1:13;
      then Geo(#)(FS2#pa) is summable & pmet.pa=pmet.(p,a)
      & for k holds 0<=(Geo(#)(FS2#pa)).k by SERIES_1:24;
      then (Geo(#)(FS2#pa)).n <=Sum(Geo(#)(FS2#pa)) & rn=pmet.pa &
      pmet is bounded_below by A26,A28,Def2,Lm1,RSSPACE2:4;
      then rn>=(Geo(#)(FS2#pa)).n & inf(dist(pmet,p).:A)=0 by A5,A21,Def3;
      then
A29:  (Geo(#)(FS2#pa)).n <r/2 by A27,XXREAL_0:2;
      A c= Cl A by PRE_TOPC:48;
      then dom dist(pmet1,p)=cT & a in Cl A & pmet1 is bounded_below
      by A23,A28,Lm1,FUNCT_2:def 1;
      then dist(pmet1,p).a in dist(pmet1,p).:(Cl A) & dist(pmet1,p).:(Cl A)
      is non empty bounded_below & inf(dist(pmet1,p).:(Cl A))=
      inf(pmet1,Cl A).p by A23,Def3,Lm1,FUNCT_1:def 12;
      then inf(pmet1,Cl A).p<=dist(pmet1,p).a & dist(pmet1,p).a=pmet1.(p,a)
      & (FS2#pa).n= pmet1.pa & (1/2)|^n>0 by A23,Def2,NEWTON:102,SEQFUNC:def 11
      ,SEQ_4:def 5;
      then inf(pmet1,Cl A).p<=(FS2#pa).n & Geo.n>0 by PREPOWER:def 1;
      then r<=Geo.n *(FS2#pa).n by XREAL_1:66;
      then r<=(Geo(#)(FS2#pa)).n by SEQ_1:12;
      then r<r/2 by A29,XXREAL_0:2;
      hence thesis by A24,XREAL_1:218;
    end;
    hence thesis by A17,XBOOLE_0:def 10;
  end;
  hence T is metrizable by A16,Th10;
end;

theorem Th18:
  for D being non empty set, p,q be FinSequence of D,B be BinOp of D st
  p is one-to-one & q is one-to-one & rng q c= rng p
  & B is commutative associative & (B has_a_unity or len q>=1 & len p>len q)
  holds ex r be FinSequence of D st r is one-to-one
  & rng r=rng p \rng q & B "**" p =B.(B "**" q,B "**" r)
proof
  let D be non empty set, p,q be FinSequence of D,B be BinOp of D such that
A1: p is one-to-one & q is one-to-one & rng q c= rng p and
A2: B is commutative associative & (B has_a_unity or len q>=1 & len p>len q);
  consider r be FinSequence such that
A3: rng r=rng p \rng q & r is one-to-one by FINSEQ_4:73;
  reconsider r as FinSequence of D by A3,FINSEQ_1:def 4;
  rng r misses rng q & rng (q^r)=rng q \/(rng p\ rng q ) by A3,FINSEQ_1:44
  ,XBOOLE_1:79;
  then
A4: q^r is one-to-one & rng (q^r)=rng p by A1,A3,FINSEQ_3:98,XBOOLE_1:45;
  then consider P be Permutation of dom p such that
A5: p*P=(q^r) & dom P = dom p & rng P = dom p by A1,BHSP_5:1;
  card (rng p)=len p & card (rng (q^r))=len (q^r) by A1,A4,FINSEQ_4:77;
  then len q < len q+ len r or B has_a_unity by A2,A4,FINSEQ_1:35;

then 1<=len r& 1<=len q & 1<=len p or B has_a_unity by A2,NAT_1:19,XXREAL_0:2;
  then B "**" (q ^ r) = B.(B "**" q,B "**" r) & B "**" p= B "**" (q^r) by A2,A5
  ,FINSOP_1:6,8;
  hence thesis by A3;
end;

registration
  let T1,T2 be TopSpace;
  let f be RealMap of [:T1,T2:];
  let t1 be Point of T1, t2 be Point of T2;
  cluster f.(t1,t2) -> real;
  coherence;
end;

:: NAGATA SMIRNOV THEOREM

Lm2: PairFunc is onto one-to-one by Th2;
then NAT = rng PairFunc by FUNCT_2:def 3;
then

Lm3: PairFunc" = PairFunc qua Function" by Lm2,TOPS_2:def 4;

theorem Th19:
  for T holds (T is_T3 & T is_T1 &
  ex Bn being FamilySequence of T st Bn is Basis_sigma_locally_finite)
  iff T is metrizable
proof
  let T;
  thus (T is_T3 & T is_T1 &
  ex Bn being FamilySequence of T st Bn is Basis_sigma_locally_finite)
  implies T is metrizable
  proof
    assume that
A1: T is_T3 and
A2: T is_T1 and
A3: ex Bn be FamilySequence of T st Bn is Basis_sigma_locally_finite;
    consider Bn be FamilySequence of T such that
A4: Bn is Basis_sigma_locally_finite by A3;
A5: Bn is sigma_locally_finite & Union Bn is Basis of T by A4,NAGATA_1:def 6;
    set cT = the carrier of T;
    set bcT = bool the carrier of T;
    set cTT=the carrier of [:T,T:];
    set Fun=Funcs(cTT,REAL);
    defpred NN[set,set,RealMap of [:T,T:]] means
    ex pmet st $3=pmet & $3 is continuous & pmet is_a_pseudometric_of cT &
    for p,q holds pmet.[p,q]<=1 & for p,q st
    ex A,B st A is open & B is open & A in Bn.$2 & B in Bn.$1 &
    p in A & Cl A c= B & not q in B holds pmet.[p,q] =1;
A6: for n,m ex pmet' st NN[n,m,pmet']
    proof
      let n,m;
A7:   Bn.n is locally_finite & Bn.m is locally_finite by A5,NAGATA_1:def 3;
      deffunc V(set)=union{Q where Q is Subset of T: Q in Bn.m & Cl Q c=$1};
A8:   for A be set st A in bcT holds V(A) in bcT
      proof
        let A be set such that A in bcT;
        set Um={Q where Q is Subset of T:Q in Bn.m & Cl Q c=A};
        now
          let uv be set such that
A9:       uv in V(A);
          consider v be set such that
A10:      uv in v & v in Um by A9,TARSKI:def 4;
          consider B such that
A11:      v=B & B in Bn.m & Cl B c=A by A10;
          thus uv in cT by A10,A11;
        end;
        then V(A) c= cT by TARSKI:def 3;
        hence thesis;
      end;
      consider Vm be Function of bcT,bcT such that
A12:  for A be set st A in bcT holds Vm.A=V(A) from FUNCT_2:sch 2(A8);
A13:  for A holds Cl (Vm.A) c=A
      proof
        let A;
        set VmA={Q where Q is Subset of T:Q in Bn.m & Cl Q c=A};
        VmA c= bcT
        proof
          let B be set;
          assume B in VmA;
          then ex C st B=C & C in Bn.m & Cl C c=A;
          hence thesis;
        end;
        then reconsider VmA as Subset-Family of T;
        VmA c=Bn.m
        proof
          let B be set;
          assume B in VmA;
          then ex C st B=C & C in Bn.m & Cl C c=A;
          hence thesis;
        end;
        then
A14:    Cl union VmA = union clf VmA by A7,PCOMPS_1:12,23;
        union clf VmA c= A
        proof
          let ClBu be set;
          assume ClBu in union clf VmA;
          then consider ClB be set such that
A15:      ClBu in ClB & ClB in clf VmA by TARSKI:def 4;
          reconsider ClB as Subset of T by A15;
          consider B such that
A16:      Cl B =ClB & B in VmA by A15,PCOMPS_1:def 3;
          ex C st B=C & C in Bn.m & Cl C c=A by A16;
          hence thesis by A15,A16;
        end;
        hence thesis by A12,A14;
      end;
      defpred F[set,set] means
      ex F be RealMap of T, S be Subset of T st S = $1 & F = $2 &
      F is continuous & (for p holds 0 <= F.p & F.p <= 1 &
      (p in S` implies F.p = 0) & (p in Cl (Vm.S) implies F.p = 1));
      set Bnn = Bn.n;
A17:  for A be set st A in Bnn ex f be set st f in Funcs(cT,REAL) & F[A,f]
      proof
        let A be set;
        assume A in Bnn;
        then A in Union Bn & Union Bn c= the topology of T
        by A5,CANTOR_1:def 2,PROB_1:25;
        then reconsider A as open Subset of T by PRE_TOPC:def 5;
        Cl (Vm.A) c=A & A` misses A by A13,XBOOLE_1:79;
        then A` misses Cl (Vm.A) & T is_T4 by A1,A2,A4,NAGATA_1:20,XBOOLE_1:63;
        then consider f be Function of T,R^1 such that
A18:    f is continuous & (for p holds 0 <= f.p & f.p <= 1 &
        (p in A` implies f.p=0)&(p in Cl (Vm.A) implies f.p=1))
        by URYSOHN3:23;
        reconsider f'=f as RealMap of T by TOPMETR:24;
A19:    ex F be RealMap of T, S be Subset of T st S = A & F = f' &
        F is continuous & (for p holds 0 <= F.p & F.p <= 1 &
        (p in S` implies F.p = 0) & (p in Cl (Vm.S) implies F.p = 1))
        proof
          take f', A;
          thus thesis by A18,TOPREAL6:83;
        end;
        f' in Funcs(cT,REAL) by FUNCT_2:11;
        hence thesis by A19;
      end;
      consider Fn be Function of Bnn,Funcs(cT,REAL) such that
A20:  for A be set st A in Bnn holds F[A,Fn.A] from FUNCT_2:sch 1(A17);
      deffunc s(Subset of T)={Q where Q is Subset of T: Q in Bn.n& Q meets $1};
      defpred S[set,Subset of T] means
      $1 in $2 & $2 is open & s($2) is finite;
A21:  for p being Element of cT
      ex A be Element of bcT st S[p,A] by A7,PCOMPS_1:def 2;
      consider Sx be Function of cT,bcT such that
A22:  for p be Element of cT holds S[p,Sx.p] from FUNCT_2:sch 3(A21);
      deffunc Fx(Element of T)=
      {Fn.A where A is Subset of T:A in Bn.n & A in s(Sx.$1)};
A23:  for p holds Fx(p) is finite
      proof
        let p;
        deffunc Fxx(Subset of T)=Fn.$1;
        set SFxx={Fxx(A) where A is Subset of T:A in s(Sx.p)};
A24:    s(Sx.p) is finite by A22;
A25:    SFxx is finite from FRAENKEL:sch 21(A24);
        Fx(p) c= SFxx
        proof
          let FA be set;
          assume FA in Fx(p);
          then ex A st FA=Fn.A & A in Bn.n & A in s(Sx.p);
          hence thesis;
        end;
        hence thesis by A25;
      end;
      defpred funcdist[RealMap of T,Function] means
      for p,q holds $2.(p,q)=abs($1.p-$1.q);
A26:  for f be Element of Funcs(cT,REAL)
      ex fxy be Element of Fun st funcdist[f,fxy]
      proof
        let f be Element of Funcs(cT,REAL);
        defpred fd[Element of T,Element of T,set] means $3=abs(f.$1-f.$2);
A27:    for x,y be Element of cT ex r be Element of REAL st fd[x,y,r];
        consider Fd be Function of [:cT,cT:],REAL such that
A28:    for x,y be Element of cT holds fd[x,y,Fd.(x,y)]
        from BINOP_1:sch 3(A27);
        [:cT,cT:]= cTT by BORSUK_1:def 5;
        then Fd in Fun & funcdist[f,Fd] by A28,FUNCT_2:11;
        hence thesis;
      end;
      consider Fdist be Function of Funcs(cT,REAL),Fun such that
A29:  for fd be Element of Funcs(cT,REAL) holds
      funcdist[fd,Fdist.fd] from FUNCT_2:sch 3(A26);
A30:  for p be Point of T,fd be Element of Funcs(cT, REAL) st
      fd in Fx(p) holds
      funcdist[fd,Fdist.fd] & Fdist.fd is continuous RealMap of [:T,T:]
      proof
        let p be Point of T,fd be Element of Funcs(cT,REAL);
        assume fd in Fx(p);
        then consider A be Subset of T such that
A31:    fd=Fn.A & A in Bn.n & A in s(Sx.p);
A32:    F[A,Fn.A] by A20,A31;
A33:    funcdist[fd,Fdist.fd] & Fdist.fd in Fun by A29;
        reconsider FD=Fdist.fd as RealMap of [:T,T:] by FUNCT_2:121;
        FD is continuous by A31,A32,A33,NAGATA_1:21;
        hence thesis by A29;
      end;
      deffunc RNG(set)={Fdist.fd where fd is RealMap of T:fd in $1};
      defpred gxy[set,Function] means
      $2 is one-to-one & ex p,q st [p,q]=$1 & rng $2=RNG(Fx(p)\/Fx(q));
A34:  for p,q holds RNG(Fx(p)\/Fx(q)) is finite & RNG(Fx(p)\/Fx(q))c=Fun
      proof
        let p,q;
A35:    RNG(Fx(p)\/Fx(q)) c=Fun
        proof
          let FDm be set;
          assume FDm in RNG(Fx(p)\/Fx(q));
          then consider fd be RealMap of T such that
A36:      FDm=Fdist.fd & fd in Fx(p)\/Fx(q);
          fd in Funcs(cT,REAL) by FUNCT_2:11;
          hence thesis by A36,FUNCT_2:7;
        end;
        deffunc FD(RealMap of T)=Fdist.$1;
        set RNG'={FD(fd) where fd is Element of Funcs(cT,REAL):
        fd in Fx(p)\/Fx(q)};
        Fx(p) is finite & Fx(q) is finite by A23;
        then
A37:    Fx(p)\/Fx(q) is finite;
A38:    RNG' is finite from FRAENKEL:sch 21(A37);
        RNG(Fx(p)\/Fx(q))c=RNG'
        proof
          let Fdistfd be set;
          assume Fdistfd in RNG(Fx(p)\/Fx(q));
          then consider fd be RealMap of T such that
A39:      Fdistfd =Fdist.fd & fd in Fx(p)\/Fx(q);
          fd in Funcs(cT,REAL) by FUNCT_2:11;
          hence thesis by A39;
        end;
        hence thesis by A35,A38;
      end;
A40:  for pq be Element of cTT ex G be Element of Fun* st gxy[pq,G]
      proof
        let pq be Element of cTT;
        pq in the carrier of [:T,T:];
        then pq in [:cT,cT:] by BORSUK_1:def 5;
        then consider p,q be set such that
A41:    p in cT & q in cT & pq=[p,q] by ZFMISC_1:def 2;
        reconsider p,q as Point of T by A41;
        RNG(Fx(p)\/Fx(q)) is finite by A34;
        then consider SF be FinSequence such that
A42:    rng SF =RNG(Fx(p)\/Fx(q)) & SF is one-to-one by FINSEQ_4:73;
        rng SF c=Fun by A34,A42;
        then reconsider SF as FinSequence of Fun by FINSEQ_1:def 4;
        SF in Fun* & gxy[pq,SF] by A41,A42,FINSEQ_1:def 11;
        hence thesis;
      end;
      consider SFdist be Function of cTT,Fun* such that
A43:  for pq be Element of cTT holds gxy[pq,SFdist.pq]
      from FUNCT_2:sch 3(A40);
      defpred add'[Element of Fun,Element of Fun,set] means $1+$2=$3;
A44:  for f,g be Element of Fun ex fg be Element of Fun st add'[f,g,fg]
      proof
        let f,g be Element of Fun;
        set f'=f,g'=g;
        f'+g' in Fun by FUNCT_2:11;
        hence thesis;
      end;
      consider ADD be BinOp of Funcs(the carrier of [:T,T:],REAL) such that
A45:  for f,g be Element of Fun holds add'[f,g,ADD.(f,g)]
      from BINOP_1:sch 3(A44);
A46:  for f1,f2 be RealMap of [:T,T:] holds ADD.(f1,f2)=f1+f2
      proof
        let f1,f2 be RealMap of [:T,T:];
        reconsider f1'=f1,f2'=f2 as Element of Fun by FUNCT_2:11;
        ADD.(f1',f2')=f1'+f2' by A45;
        hence thesis;
      end;
      defpred SFdist[set,set] means
      for FS be FinSequence of Fun st FS=SFdist.$1 holds $2= ADD "**" FS;
A47:  for pq be set st pq in cTT ex S be set st S in Fun & SFdist[pq,S]
      proof
        let pq be set;
        assume pq in cTT;
        then SFdist.pq in Fun* by FUNCT_2:7;
        then reconsider SF=SFdist.pq as FinSequence of Fun by FINSEQ_1:def 11;
        for FS be FinSequence of Funcs(cTT,REAL) st FS=SFdist.pq
        holds ADD "**" FS = ADD "**" SF;
        hence thesis;
      end;
      consider SumFdist be Function of cTT,Funcs(cTT,REAL) such that
A48:  for xy be set st xy in cTT holds
      SFdist[xy,SumFdist.xy] from FUNCT_2:sch 1(A47);
A49:  for pq' holds SumFdist.pq' is continuous RealMap of [:T,T:]
      proof
        let pq';
        reconsider SF=SFdist.pq' as FinSequence of Fun by FINSEQ_1:def 11;
        consider p,q such that
A50:    [p,q]=pq' & rng SF=RNG(Fx(p)\/Fx(q)) by A43;
        for n st 0<>n & n<=len SF holds SF.n is continuous RealMap of [:T,T:]
        proof
          let n;
          assume
A51:      0<>n & n<=len SF;
          then n>=1 by NAT_1:14;
          then n in dom SF by A51,FINSEQ_3:27;
          then SF.n in RNG(Fx(p)\/Fx(q)) by A50,FUNCT_1:def 5;
          then consider fd be RealMap of T such that
A52:      SF.n=Fdist.fd & fd in (Fx(p)\/Fx(q));
A53:      fd in Funcs(cT,REAL) by FUNCT_2:11;
          fd in Fx(p) or fd in Fx(q) by A52,XBOOLE_0:def 2;
          hence thesis by A30,A52,A53;
        end;
        then ADD"**"SF is continuous RealMap of [:T,T:] &
        SF=SFdist.pq' by A46,NAGATA_1:25;
        hence thesis by A48;
      end;
      reconsider SumFdist'=SumFdist as
      Function of cTT,Funcs(cTT,the carrier of R^1) by TOPMETR:24;
A54:  for pq' holds SumFdist'.pq' is continuous Function of [:T,T:],R^1
      proof
        let pq';

reconsider SF=SumFdist.pq' as Function of [:T,T:],R^1 by A49,TOPMETR:24;
        SumFdist.pq' is continuous RealMap of [:T,T:] by A49;
        then SF is continuous by TOPREAL6:83;
        hence thesis;
      end;
      defpred ss[set,set] means
      for x,y be Element of T st $1=[x,y] holds $2=[:Sx.x,Sx.y:];
A55:  for pq' be set st pq' in cTT ex SS be set st SS in bool cTT & ss[pq',SS]
      proof
        let pq' be set;
        assume pq' in cTT;
        then pq' in [:cT,cT:] by BORSUK_1:def 5;
        then consider p,q be set such that
A56:    p in cT & q in cT & pq'=[p,q] by ZFMISC_1:def 2;
        reconsider p,q as Point of T by A56;
        now
          let p1,q1 be Point of T;
          assume pq'=[p1,q1];
          then p1=p & q1=q by A56,ZFMISC_1:33;
          hence [:Sx.p,Sx.q:]=[:Sx.p1,Sx.q1:];
        end;
        hence thesis;
      end;
      consider SS be Function of cTT,bool cTT such that
A57:  for pq be set st pq in cTT holds ss[pq,SS.pq] from FUNCT_2:sch 1(A55);
A58:  for pq' holds pq' in SS.pq' & SS.pq' is open
      proof
        let pq' be Point of [:T,T:];
        pq' in cTT;
        then pq' in [:cT,cT:] by BORSUK_1:def 5;
        then consider p,q be set such that
A59:    p in cT & q in cT & pq'=[p,q] by ZFMISC_1:def 2;
        reconsider p,q as Point of T by A59;
        SS.pq'=[:Sx.p,Sx.q:] & p in Sx.p & q in Sx.q & Sx.p is open &
        Sx.q is open by A22,A57,A59;
        hence thesis by A59,BORSUK_1:46,ZFMISC_1:def 2;
      end;
A60:  for pq be Element of cTT, map' be Element of Fun st
      map' is_a_unity_wrt ADD holds map'.pq=0
      proof
        let pq be Element of cTT, map' be Element of Fun;
        assume map' is_a_unity_wrt ADD;
        then ADD.(map',map')=map' by BINOP_1:11;
        then (map'+map').pq=map'.pq by A45;
        then map'.pq+map'.pq=map'.pq by NAGATA_1:def 7;
        hence thesis;
      end;
A61:  for pq1,pq2 be Point of [:T,T:] holds
      (pq1 in SS.pq2 implies SumFdist.pq1.pq1 = SumFdist.pq2.pq1) &
      for SumFdist1,SumFdist2 be RealMap of [:T,T:] st
      SumFdist1=SumFdist.pq1 & SumFdist2 = SumFdist.pq2
      holds SumFdist1.pq1 >= SumFdist2.pq1
      proof
        let pq1,pq2 be Point of [:T,T:];
        deffunc No0(Element of T,Element of T)=
        {Fn.A where A is Subset of T:A in Bn.n &
        for FnA be RealMap of T st Fn.A=FnA holds (FnA.$1 > 0 or FnA.$2 > 0)};
A62:    for p,q,p1,q1 be Point of T st [p,q] in SS.[p1,q1] holds
        No0(p,q) c= Fx(p1)\/Fx(q1)
        proof
          let p,q,p1,q1 be Point of T such that
A63:      [p,q] in SS.[p1,q1];
          let no0 be set;
          assume no0 in No0(p,q);
          then consider A be Subset of T such that
A64:      no0=Fn.A & A in Bn.n & for FnA be RealMap of T st Fn.A=FnA holds
          (FnA.p > 0 or FnA.q > 0);
          Fn.A in Funcs(cT,REAL) by A64,FUNCT_2:7;
          then reconsider FnA=Fn.A as RealMap of T by FUNCT_2:121;
          reconsider pq1=[p1,q1] as Element of cTT by BORSUK_1:def 5;
A65:      FnA.p > 0 or FnA.q > 0 by A64;
          F[A,Fn.A] by A20,A64;
          then (not p in cT\A or not q in cT\A) & [:Sx.p1,Sx.q1:]=SS.pq1
          by A57,A65;
          then (p in A or q in A) & p in Sx.p1 & q in Sx.q1
          by A63,XBOOLE_0:def 4,ZFMISC_1:106;
          then A meets Sx.p1 or A meets Sx.q1 by XBOOLE_0:3;
          then A in s(Sx.p1) or A in s(Sx.q1) by A64;
          then FnA in Fx(p1) or FnA in Fx(q1) by A64;
          hence thesis by A64,XBOOLE_0:def 2;
        end;
A66:    for f be FinSequence of Funcs(cTT,REAL),p,q,p1,q1 be Point of T
        st rng f= RNG(Fx(p1)\/Fx(q1))\RNG(No0(p,q)) holds (ADD "**" f).[p,q]=0
        proof
          let f be FinSequence of Funcs(cTT,REAL),p,q,p1,q1 be Point of T
          such that
A67:      rng f= RNG(Fx(p1)\/Fx(q1))\RNG(No0(p,q));
          reconsider pq=[p,q] as Element of cTT by BORSUK_1:def 5;
          now per cases;
            suppose
A68:          len f = 0;
              ADD has_a_unity by A46,NAGATA_1:23;
              then ADD "**" f=the_unity_wrt ADD & ex f be Element of Fun st
              f is_a_unity_wrt ADD by A68,FINSOP_1:def 1,SETWISEO:def 2;
              then ADD"**"f is_a_unity_wrt ADD by BINOP_1:def 8;
              hence (ADD"**"f).pq=0 by A60;
            end;
            suppose
A69:          len f<>0;
              then len f >=1 by NAT_1:14;
              then consider F be Function of NAT,Fun such that
A70:          F.1 = f.1 & (for n st 0 <> n & n < len f holds
              F.(n + 1) = ADD.(F.n,f.(n + 1))) & ADD"**"f = F.(len f)
              by FINSOP_1:def 1;
              defpred R[Element of NAT] means
              $1<>0 & $1<=len f implies F.$1.pq=0;
A71:          R[0];
A72:          for k holds R[k] implies R[k+1]
              proof
                let k;
                assume
A73:            R[k];
                assume
A74:            k+1<>0 & k+1 <=len f;
                then
A75:            k+1 <=len f & k< len f & k+1>=1 by NAT_1:13,14;
                then k+1 in dom f by FINSEQ_3:27;
                then f.(k+1) in RNG(Fx(p1)\/Fx(q1))\RNG(No0(p,q)) by A67,
                FUNCT_1:def 5;
                then
A76:            f.(k+1) in RNG(Fx(p1)\/Fx(q1))
                & not f.(k+1) in RNG(No0(p,q)) by XBOOLE_0:def 4;
                then consider fd be RealMap of T such that
A77:            Fdist.fd =f.(k+1) & fd in Fx(p1)\/Fx(q1);
                fd in Funcs(cT,REAL) by FUNCT_2:11;
                then reconsider Fdistfd=Fdist.fd,Fk1=F.(k+1),
                Fk=F.k,fk1=f.(k+1) as RealMap of [:T,T:] by A77,FUNCT_2:7,121;
                fd.p=0 & fd.q=0
                proof
                  assume
A78:              fd.p<>0 or fd.q<>0;
                  per cases by A77,XBOOLE_0:def 2;
                  suppose fd in Fx(p1);
                    then consider A be Subset of T such that
A79:                fd=Fn.A & A in Bn.n & A in s(Sx.p1);
                    not fd in No0(p,q) by A76,A77;
                    then consider FnA be RealMap of T such that
A80:                Fn.A=FnA & not (FnA.p > 0 or FnA.q > 0) by A79;
                    F[A,Fn.A] by A20,A79;
                    hence contradiction by A78,A79,A80;
                  end;
                  suppose fd in Fx(q1);
                    then consider A be Subset of T such that
A81:                fd=Fn.A & A in Bn.n & A in s(Sx.q1);
                    not fd in No0(p,q) by A76,A77;
                    then consider FnA be RealMap of T such that
A82:                Fn.A=FnA & not (FnA.p > 0 or FnA.q > 0) by A81;
                    F[A,Fn.A] by A20,A81;
                    hence contradiction by A78,A81,A82;
                  end;
                end;
                then fd.p-fd.q=0 & fd in Funcs(cT,REAL) by FUNCT_2:11;
                then
A83:            abs(fd.p-fd.q)=0 & Fdistfd.(p,q)=abs(fd.p- fd.q) by A29,
                ABSVALUE:7;
                per cases;
                suppose k=0;
                  hence F.(k+1).pq=0 by A70,A77,A83;
                end;
                suppose k>0;
                  then Fk1=ADD.(Fk,fk1)& k>0 by A70,A75;
                  then Fk1=Fk+fk1 & Fk.pq=0 by A46,A73,A74,NAT_1:13;
                  then Fk1.pq=0+fk1.pq by NAGATA_1:def 7;
                  hence F.(k+1).pq=0 by A77,A83;
                end;
              end;
              for n holds R[n] from NAT_1:sch 1(A71,A72);
              hence (ADD"**"f).pq=0 by A69,A70;
            end;
          end;
          hence thesis;
        end;
        reconsider S1=SFdist.pq1,S2=SFdist.pq2 as FinSequence of Fun
        by FINSEQ_1:def 11;
        consider p1,q1 be Point of T such that
A84:    [p1,q1]=pq1 & rng S1=RNG(Fx(p1)\/Fx(q1)) by A43;
        consider p2,q2 be Point of T such that
A85:    [p2,q2]=pq2 & rng S2=RNG(Fx(p2)\/Fx(q2)) by A43;
A86:    RNG(No0(p1,q1))c=RNG(Fx(p1)\/Fx(q1))
        proof
          let FD be set such that
A87:      FD in RNG(No0(p1,q1));
          consider fd be RealMap of T such that
A88:      FD=Fdist.fd & fd in (No0(p1,q1)) by A87;
          p1 in Sx.p1 & q1 in Sx.q1 by A22;
          then [p1,q1] in [:Sx.p1,Sx.q1:] by ZFMISC_1:106;
          then [p1,q1] in SS.[p1,q1] by A57;
          then No0(p1,q1)c= Fx(p1)\/Fx(q1) by A62;
          hence thesis by A88;
        end;
        reconsider S1=SFdist.pq1,S2=SFdist.pq2 as FinSequence of Fun
        by FINSEQ_1:def 11;
        RNG(Fx(p1)\/Fx(q1)) is finite by A34;
        then
A89:    RNG(No0(p1,q1)) is finite by A86;
        then consider No be FinSequence such that
A90:    rng No =RNG(No0(p1,q1)) & No is one-to-one by FINSEQ_4:73;
        RNG(Fx(p1)\/Fx(q1))c=Fun by A34;
        then
A91:    RNG(No0(p1,q1)) c= Fun by A86,XBOOLE_1:1;
        then reconsider No as FinSequence of Fun by A90,FINSEQ_1:def 4;
A92:    ADD has_a_unity & ADD is commutative associative by A46,NAGATA_1:23;
        rng No c= rng S1 & S1 is one-to-one by A43,A84,A86,A90;
        then consider S1No be FinSequence of Fun such that
A93:    S1No is one-to-one & rng S1No=rng S1 \rng No &
        ADD"**"S1 =ADD.(ADD"**"No,ADD"**"S1No) by A90,A92,Th18;
        set RNiS2=RNG(No0(p1,q1))/\RNG(Fx(p2)\/Fx(q2));
        RNiS2 is finite by A89;
        then consider NoiS2 be FinSequence such that
A94:    rng NoiS2 =RNiS2 & NoiS2 is one-to-one by FINSEQ_4:73;
        RNiS2 c=RNG(No0(p1,q1)) by XBOOLE_1:17;
        then RNiS2 c= Fun by A91,XBOOLE_1:1;
        then reconsider NoiS2 as FinSequence of Fun by A94,FINSEQ_1:def 4;
        rng NoiS2 c= rng No & No is one-to-one by A90,A94,XBOOLE_1:17;
        then consider NoNoiS2 be FinSequence of Fun such that
A95:    NoNoiS2 is one-to-one & rng NoNoiS2=rng No \rng NoiS2 &
        ADD"**"No=ADD.(ADD"**"NoiS2,ADD"**"NoNoiS2) by A92,A94,Th18;
        rng NoiS2 c= rng S2 & S2 is one-to-one by A43,A85,A94,XBOOLE_1:17;
        then consider S2No be FinSequence of Fun such that
A96:    S2No is one-to-one & rng S2No=rng S2 \rng NoiS2 &
        ADD"**"S2=ADD.(ADD"**"NoiS2,ADD"**"S2No) by A92,A94,Th18;
        reconsider ANoNoiS2=ADD"**"NoNoiS2,ANo=ADD"**"No,AS1No=ADD"**"S1No,
        AS2No=ADD"**"S2No,ANoiS2=ADD"**"NoiS2,AS1=ADD"**"S1,AS2=ADD"**"S2
        as RealMap of [:T,T:] by FUNCT_2:121;
A97:    ANoNoiS2.(p1,q1)>=0
        proof
          set N=NoNoiS2;
          per cases;
          suppose
A98:        len N=0;
            ADD has_a_unity by A46,NAGATA_1:23;
            then ADD "**" N=the_unity_wrt ADD & ex f be Element of Fun st
            f is_a_unity_wrt ADD by A98,FINSOP_1:def 1,SETWISEO:def 2;
            then ADD"**"N is_a_unity_wrt ADD by BINOP_1:def 8;
            hence ANoNoiS2.(p1,q1)>=0 by A60,A84;
          end;
          suppose
A99:        len N<>0;
            then len N >=1 by NAT_1:14;
            then consider F be Function of NAT,Fun such that
A100:       F.1 = N.1 & (for n st 0 <> n & n < len N holds
            F.(n + 1) = ADD.(F.n,N.(n + 1))) & ADD "**" N=F.(len N)
            by FINSOP_1:def 1;
            defpred R[Element of NAT] means $1<>0 & $1<=len N implies
            for Fn be RealMap of [:T,T:] st Fn=F.$1 holds Fn.(p1,q1)>=0;
A101:       R[0];
A102:       for k holds R[k] implies R[k+1]
            proof
              let k;
              assume
A103:         R[k];
              assume
A104:         k+1<>0 & k+1 <=len N;
              then
A105:         k+1 <=len N & k< len N & k+1>=1 by NAT_1:13,14;
              then k+1 in dom N by FINSEQ_3:27;
              then N.(k+1) in rng No \rng NoiS2 by A95,FUNCT_1:def 5;
              then N.(k+1) in RNG(No0(p1,q1)) by A90,XBOOLE_0:def 4;
              then consider fd be RealMap of T such that
A106:         Fdist.fd =N.(k+1) & fd in No0(p1,q1);
              fd in Funcs(cT,REAL) by FUNCT_2:11;
              then reconsider Fdistfd=Fdist.fd,Fk1=F.(k+1),
              Fk=F.k,Nk1=N.(k+1) as RealMap of [:T,T:] by A106,FUNCT_2:7,121;
              fd in Funcs(cT,REAL) by FUNCT_2:11;
              then
A107:         abs(fd.p1-fd.q1)>=0 & Fdistfd.(p1,q1)=abs(fd. p1-fd.q1)
              by A29,COMPLEX1:132;
A108:         now per cases;
                suppose k=0;
                  hence Fk1.(p1,q1)>=0 by A100,A106,A107;
                end;
                suppose k>0;
                  then Fk1=ADD.(Fk,Nk1)& k>0 by A100,A105;
                  then Fk1=Fk+Nk1 & Fk.(p1,q1) >= 0& Nk1.(p1,q1) >= 0
                  by A46,A103,A104,A106,A107,NAT_1:13;
                  then 0+0<=Fk.(p1,q1)+Nk1.(p1,q1) &
                  Fk.(p1,q1)+Nk1.(p1,q1)=Fk1.(p1,q1)
                  by A84,NAGATA_1:def 7;
                  hence Fk1.(p1,q1) >=0;
                end;
              end;
              let Fn be RealMap of [:T,T:];
              assume Fn=F.(k+1);
              hence thesis by A108;
            end;
            for n holds R[n] from NAT_1:sch 1(A101,A102);
            hence ANoNoiS2.(p1,q1)>=0 by A99,A100;
          end;
        end;
        AS1=ANo+AS1No & ANo=ANoiS2+ANoNoiS2 & AS2=ANoiS2+AS2No & rng S2No=
        RNG(Fx(p2)\/Fx(q2))\RNG(No0(p1,q1))
        by A46,A85,A93,A94,A95,A96,XBOOLE_1:47;
        then
A109:   AS1.pq1=ANo.pq1+AS1No.pq1 & ANo.pq1=ANoiS2.pq1+ANoNoiS2. pq1 &
        AS2.pq1=ANoiS2.pq1+AS2No.pq1 & AS1No.pq1=0 & AS2No.pq1=0
        by A66,A84,A90,A93,NAGATA_1:def 7;
        thus pq1 in SS.pq2 implies SumFdist.pq1.pq1 = SumFdist.pq2.pq1
        proof
          assume
A110:     pq1 in SS.pq2;
          now
            let FD be set;
            assume FD in RNG (No0(p1,q1));
            then consider fd be RealMap of T such that
A111:       FD=Fdist.fd & fd in (No0(p1,q1));
            No0(p1,q1) c= Fx(p2)\/Fx(q2) by A62,A84,A85,A110;
            hence FD in RNG(Fx(p2)\/Fx(q2)) by A111;
          end;
          then RNG (No0(p1,q1)) c= RNG(Fx(p2)\/Fx(q2)) by TARSKI:def 3;
          then RNiS2=RNG(No0(p1,q1)) & rng NoNoiS2=RNG(No0(p1,q1))\RNiS2
          by A90,A94,A95,XBOOLE_1:28;
          then rng NoNoiS2={} by XBOOLE_1:37;
          then NoNoiS2={} by RELAT_1:64;
          then len NoNoiS2 =0 & ADD has_a_unity
           by A46,CARD_1:47,NAGATA_1:23;
          then ADD"**"NoNoiS2=the_unity_wrt ADD & ex f be Element of Fun st
          f is_a_unity_wrt ADD by FINSOP_1:def 1,SETWISEO:def 2;
          then ADD"**"NoNoiS2 is_a_unity_wrt ADD by BINOP_1:def 8;
          then AS1.pq1=AS2.pq1 & SumFdist.pq1=AS1 by A48,A60,A109;
          hence thesis by A48;
        end;
        now
          let SumFdist1,SumFdist2 be RealMap of [:T,T:];
          assume
A112:     SumFdist1=SumFdist.pq1 & SumFdist2 = SumFdist.pq2;
          then AS2.(p1,q1)<=AS1.(p1,q1) & SumFdist2=AS2
          by A48,A84,A97,A109,XREAL_1:9;
          hence SumFdist1.pq1 >= SumFdist2.pq1 by A48,A84,A112;
        end;
        hence thesis;
      end;
      cTT=[:cT,cT:] by BORSUK_1:def 5;
      then reconsider SumFTS = SumFdist' Toler as
      Function of [:cT,cT:],REAL by TOPMETR:24;
      reconsider SumFTS'=SumFdist' Toler as RealMap of [:T,T:] by TOPMETR:24;
      for p,q be Point of [:T,T:] st p in SS.q holds
      SumFdist'.p.p=SumFdist'.q.p by A61;
      then SumFdist' Toler is continuous by A54,A58,NAGATA_1:26;
      then
A113: SumFTS' is continuous by TOPREAL6:83;
      now
        let p,q,r be Point of T;
        thus SumFTS.(p,p)=0
        proof
          reconsider pp=[p,p] as Point of [:T,T:] by BORSUK_1:def 5;
          reconsider SF=SFdist.pp as FinSequence of Fun by FINSEQ_1:def 11;
          now per cases;
            suppose
A114:         len SF=0;
              ADD has_a_unity by A46,NAGATA_1:23;
              then ADD "**" SF=the_unity_wrt ADD & ex f be Element of Fun st
              f is_a_unity_wrt ADD by A114,FINSOP_1:def 1,SETWISEO:def 2;
              then ADD"**"SF is_a_unity_wrt ADD by BINOP_1:def 8;
              hence (ADD"**"SF).pp=0 by A60;
            end;
            suppose
A115:         len SF <>0;
              then len SF >=1 by NAT_1:14;
              then consider F be Function of NAT,Fun such that
A116:         F.1 = SF.1 & (for n st 0 <> n & n < len SF holds
              F.(n + 1) = ADD.(F.n,SF.(n + 1))) & ADD"**"SF = F.(len SF)
              by FINSOP_1:def 1;
              defpred R[Element of NAT] means
              $1<>0 & $1<=len SF implies F.$1.pp=0;
A117:         R[0];
A118:         for k holds R[k] implies R[k+1]
              proof
                let k;
                assume
A119:           R[k];
                assume
A120:           k+1<>0 & k+1 <=len SF;
                then
A121:           k+1 <=len SF & k< len SF & k+1>=1 by NAT_1:13,14;
                then
A122:           k+1 in dom SF by FINSEQ_3:27;
                consider x,y be Point of T such that
A123:           [x,y]=pp & rng SF=RNG(Fx(x)\/Fx(y)) by A43;
                SF.(k+1) in RNG(Fx(x)\/Fx(y)) by A122,A123,FUNCT_1:def 5;
                then consider fd be RealMap of T such that
A124:           Fdist.fd =SF.(k+1) & fd in Fx(x)\/Fx(y);
                fd in Funcs(cT,REAL) by FUNCT_2:11;
                then reconsider Fdistfd=Fdist.fd,Fk1=F.(k+1),Fk=F.k,
                SFk1=SF.(k+1) as RealMap of [:T,T:] by A124,FUNCT_2:7,121;
                fd in Funcs(cT,REAL) by FUNCT_2:11;
                then
A125:           abs(fd.p-fd.p)=0&Fdistfd.(p,p)=abs(fd.p-fd .p) by A29,
                ABSVALUE:7;
                per cases;
                suppose k=0;
                  hence F.(k+1).pp=0 by A116,A124,A125;
                end;
                suppose k>0;
                  then Fk1=ADD.(Fk,SFk1)& k>0 by A116,A121;
                  then Fk1=Fk+SFk1 & Fk.pp=0 by A46,A119,A120,NAT_1:13;
                  then Fk1.pp=0+SFk1.pp by NAGATA_1:def 7;
                  hence F.(k+1).pp=0 by A124,A125;
                end;
              end;
              for n holds R[n] from NAT_1:sch 1(A117,A118);
              hence (ADD"**"SF).pp=0 by A115,A116;
            end;
          end;
          then (ADD"**"SF).pp=0 & SumFdist.pp=ADD "**" SF by A48;
          hence thesis by NAGATA_1:def 8;
        end;
        thus SumFTS.(p,r)<=SumFTS.(p,q)+SumFTS.(r,q)
        proof
          reconsider pr=[p,r],pq=[p,q],rq=[r,q] as Point of [:T,T:]
          by BORSUK_1:def 5;
          reconsider SFpr=SFdist.pr as FinSequence of Fun by FINSEQ_1:def 11;
          reconsider ASFpr=ADD"**"SFpr as RealMap of [:T,T:] by FUNCT_2:121;
          reconsider SumFpr=SumFdist.pr,SumFpq=SumFdist.pq,
          SumFrq=SumFdist.rq as RealMap of [:T,T:] by FUNCT_2:121;
          now per cases;
            suppose
A126:         len SFpr = 0;
              ADD has_a_unity by A46,NAGATA_1:23;
              then ADD"**"SFpr=the_unity_wrt ADD & ex f being Element of Fun
              st f is_a_unity_wrt ADD by A126,FINSOP_1:def 1,SETWISEO:def 2;
              then ADD"**"SFpr is_a_unity_wrt ADD by BINOP_1:def 8;
              then ASFpr.pr=0 & ASFpr.pq=0 & ASFpr.rq=0 by A60;
              hence ASFpr.pr <= ASFpr.pq + ASFpr.rq;
            end;
            suppose
A127:         len SFpr <> 0;
              then len SFpr >=1 by NAT_1:14;
              then consider F be Function of NAT,Fun such that
A128:         F.1 = SFpr.1 & (for n st 0 <> n & n < len SFpr holds
              F.(n+1)=ADD.(F.n,SFpr.(n+1))) & ADD"**"SFpr=F.(len SFpr)
              by FINSOP_1:def 1;
              defpred T[Element of NAT] means $1<>0 & $1<=len SFpr implies
              for F' be RealMap of [:T,T:] st F'=F.$1 holds
              F'.pr<=F'.pq+F'.rq;
A129:         T[0];
A130:         for k holds T[k] implies T[k+1]
              proof
                let k;
                assume
A131:           T[k];
                assume
A132:           k+1<>0 & k+1 <=len SFpr;
                then
A133:           k+1<=len SFpr&k<len SFpr& k+1>=1 by NAT_1:13,14;
                then
A134:           k+1 in dom SFpr by FINSEQ_3:27;
                consider x,y be Point of T such that
A135:           [x,y]=pr & rng SFpr=RNG(Fx(x)\/Fx(y)) by A43;
                SFpr.(k+1) in RNG(Fx(x)\/Fx(y)) by A134,A135,FUNCT_1:def 5;
                then consider fd be RealMap of T such that
A136:           Fdist.fd =SFpr.(k+1) & fd in Fx(x)\/Fx(y);
                fd in Funcs(cT,REAL) by FUNCT_2:11;
                then reconsider Fdistfd=Fdist.fd, Fk1=F.(k+1), Fk=F.k,
                SFk1=SFpr.(k+1) as RealMap of [:T,T:] by A136,FUNCT_2:7,121;
                fd.p-fd.r=(fd.p-fd.q)+(fd.q-fd.r)
                & fd in Funcs(cT,REAL) by FUNCT_2:11;
                then
A137:           abs(fd.p-fd.r)<=abs(fd.p-fd.q)+ abs(fd.q- fd.r)
                & abs(fd.q-fd.r)=abs(-(fd.q-fd.r)) & Fdistfd.(p,r)=
                abs(fd.p-fd.r) & Fdistfd.(p,q)=abs(fd.p-fd.q) &
                Fdistfd.(r,q)=abs(fd.r-fd.q) by A29,COMPLEX1:138,142;
                let F' be RealMap of [:T,T:] such that
A138:           F'=F.(k+1);
                per cases;
                suppose k=0;
                  hence F'.pr<=F'.pq+F'.rq by A128,A136,A137,A138;
                end;
                suppose k>0;
                  then Fk1=ADD.(Fk,SFk1)& k>0 by A128,A133;
                  then Fk1=Fk+SFk1 & Fk.pr<=Fk.pq+Fk.rq& Fdistfd=SFk1
                  by A46,A131,A132,A136,NAT_1:13;
                  then Fk.pr+SFk1.pr<=(Fk.pq+Fk.rq)+(SFk1.pq+SFk1.rq) &
                  Fk1.pr=Fk.pr+SFk1.pr & Fk1.pq=Fk.pq+SFk1.pq
                  & Fk1.rq=Fk.rq+SFk1.rq by A137,NAGATA_1:def 7,XREAL_1:9;
                  hence F'.pr<=F'.pq+F'.rq by A138;
                end;
              end;
              for n holds T[n] from NAT_1:sch 1(A129,A130);
              hence ASFpr.pr <= ASFpr.pq + ASFpr.rq by A127,A128;
            end;
          end;
          then ASFpr.pr <= ASFpr.pq + ASFpr.rq & SumFpr=ADD "**" SFpr &
          SumFpr.pq <=SumFpq.pq & SumFpr.rq <=SumFrq.rq by A48,A61;
          then SumFpr.pr<=SumFpr.pq+SumFpr.rq & SumFpr.pq + SumFpr.rq <=
          SumFpq.pq+SumFrq.rq by XREAL_1:9;
          then SumFpr.pr<=SumFpq.pq+SumFrq.rq & SumFTS.pr = SumFpr.pr &
          SumFTS.pq = SumFpq.pq by NAGATA_1:def 8,XXREAL_0:2;
          hence thesis by NAGATA_1:def 8;
        end;
      end;
      then
A139: SumFTS is_a_pseudometric_of cT by NAGATA_1:28;
A140: for p,q st ex A,B st A is open & B is open & A in Bn.m
      & B in Bn.n & p in A & Cl A c= B & not q in B holds SumFTS'.[p,q] >= 1
      proof
        let p,q such that
A141:   ex A,B st A is open & B is open & A in Bn.m
        & B in Bn.n & p in A & Cl A c= B & not q in B;
        reconsider pq=[p,q] as Point of [:T,T:] by BORSUK_1:def 5;
        reconsider SF=SFdist.pq as FinSequence of Fun by FINSEQ_1:def 11;
        reconsider ASF=ADD"**"SF as RealMap of [:T,T:] by FUNCT_2:121;
        assume
A142:   SumFTS'.[p,q] < 1;
A143:   SumFTS.pq=SumFTS.(p,q) & SumFTS.pq=SumFdist.pq.pq &
        SumFdist.pq=ASF by A48,NAGATA_1:def 8;
        consider x,y be Point of T such that
A144:   [x,y]=pq & rng SF=RNG(Fx(x)\/Fx(y)) by A43;
        consider A,B such that
A145:   A is open & B is open & A in Bn.m & B in Bn.n
        & p in A & Cl A c= B & not q in B by A141;
        reconsider FnB=Fn.B as RealMap of T by A145,FUNCT_2:7,121;
        A in {Q where Q is Subset of T:Q in Bn.m & Cl Q c=B} by A145;
        then A c=V(B) by ZFMISC_1:92;
        then A c= Vm.B & Vm.B c= Cl(Vm.B) & q in cT\B by A12,A145,PRE_TOPC:48
        ,XBOOLE_0:def 4;
        then
A146:   p in Cl(Vm.B) & q in B` & Cl(Vm.B)c=B by A13,A145,TARSKI:def 3;
        then p in Sx.p & p in B by A22;
        then Sx.p meets B by XBOOLE_0:3;
        then B in s(Sx.p) & x=p by A144,A145,ZFMISC_1:33;
        then FnB in Fx(x) by A145;
        then FnB in Fx(x)\/Fx(y) by XBOOLE_0:def 2;
        then
A147:   Fdist.FnB in rng SF by A144;
        then SF<>{} by RELAT_1:60;
        then len SF<>0 by CARD_2:59;
        then len SF >=1 by NAT_1:14;
        then consider F be Function of NAT,Fun such that
A148:   F.1 = SF.1 & (for n st 0 <> n & n < len SF holds
        F.(n+1)=ADD.(F.n,SF.(n+1))) & ADD"**"SF=F.(len SF) by FINSOP_1:def 1;
A149:   for k st k <>0 & k <=len SF
        for f be RealMap of [:T,T:] st f=SF.k holds f.pq>=0
        proof
          let k such that
A150:     k <>0 & k <=len SF;
          let f be RealMap of [:T,T:] such that
A151:     f=SF.k;
          k >=1 & k <= len SF by A150,NAT_1:14;
          then k in dom SF by FINSEQ_3:27;
          then SF.k in RNG(Fx(x)\/Fx(y)) by A144,FUNCT_1:def 5;
          then consider fd be RealMap of T such that
A152:     Fdist.fd =SF.k & fd in Fx(x)\/Fx(y);
          Fdist.fd=f & fd in Funcs(cT,REAL) by A151,A152,FUNCT_2:11;
          then f.(p,q)=abs(fd.p-fd.q) by A29;
          hence thesis by COMPLEX1:132;
        end;
        defpred P[Element of NAT] means for k st k<>0 & k<=$1 & $1<=len SF
        for fk,Fn be RealMap of [:T,T:] st fk=SF.k & Fn=F.$1
        holds fk.pq<=Fn.pq;
A153:   P[0];
A154:   for i holds P[i] implies P[i+1]
        proof
          let i;
          assume
A155:     P[i];
          let k such that
A156:     k<>0 & k<=i+1 & i+1<=len SF;
          now
            let fk,Fn be RealMap of [:T,T:] such that
A157:       fk=SF.k & Fn=F.(i+1);
            reconsider Fi=F.i as RealMap of [:T,T:] by FUNCT_2:121;
            1<=i+1 by NAT_1:14;
            then i+1 in dom SF by A156,FINSEQ_3:27;
            then SF.(i+1) in rng SF by FUNCT_1:def 5;
            then reconsider SFi1=SF.(i+1) as RealMap of [:T,T:]
            by FUNCT_2:121;
            per cases by A156,REAL_1:def 5;
            suppose k<i+1;
              then k<=i & i<=len SF & i<i+1 by A156,NAT_1:13;
              then
A158:         fk.pq<=Fi.pq & i<>0 & i<len SF & i+1<>0
              by A155,A156,A157,XXREAL_0:2;
              then F.(i+1)=ADD.(F.i,SF.(i+1))&SFi1.pq>=0 by A148,A149,A156;
              then Fn =Fi+SFi1 & Fi.pq +0 <= Fi.pq + SFi1.pq by A46,A157,
              XREAL_1:9;
              then Fn.pq>=Fi.pq by NAGATA_1:def 7;
              hence fk.pq<=Fn.pq by A158,XXREAL_0:2;
            end;
            suppose
A159:         k=i+1;
              per cases;
              suppose i=0;
                hence fk.pq<=Fn.pq by A148,A157,A159;
              end;
              suppose
A160:           i<>0;
                i+0<i+1 by XREAL_1:10;
                then
A161:           i<len SF & 1<=i by A156,A160,NAT_1:14,XXREAL_0:2;
                then i in dom SF by FINSEQ_3:27;
                then SF.i in rng SF by FUNCT_1:def 5;
                then reconsider SFi=SF.i as RealMap of [:T,T:] by FUNCT_2:121;
                0<=SFi.pq & SFi.pq<=Fi.pq by A149,A155,A161;
                then F.(i+1)=ADD.(F.i,SF.(i+1)) & fk=SFi1 & 0<=Fi.pq
                by A148,A157,A159,A161;
                then Fn=Fi+fk&Fi.pq+fk.pq>=0+fk.pq by A46,A157,XREAL_1:9;
                hence fk.pq<=Fn.pq by NAGATA_1:def 7;
              end;
            end;
          end;
          hence for fk,Fn be RealMap of [:T,T:] st fk=SF.k & Fn=F.(i+1)
          holds fk.pq<=Fn.pq;
        end;
A162:   for i holds P[i] from NAT_1:sch 1(A153,A154);
        reconsider FdistFnB=Fdist.FnB as RealMap of [:T,T:]
        by A147,FUNCT_2:121;
        F[B,Fn.B] by A20,A145;
        then FnB in Funcs(cT,REAL) & FnB.p =1 & FnB.q=0 by A145,A146,FUNCT_2:7;
        then
A163:   FdistFnB.(p,q)=abs(1-0) & abs(1)=1 by A29,ABSVALUE:def 1;
        consider k be set such that
A164:   k in dom SF & SF.k=Fdist.FnB by A147,FUNCT_1:def 5;
A165:   k in Seg(len SF) by A164,FINSEQ_1:def 3;
        reconsider k as Element of NAT by A164;
        k<>0 & k <=len SF & FdistFnB = SF.k & ASF=F.(len SF) by A148,A164,A165,
        FINSEQ_1:3;
        hence thesis by A142,A143,A162,A163;
      end;
      take pmet'=min(1,SumFTS');
A166: min(1,SumFTS')=min(1,SumFTS) & min(1,SumFTS') is continuous &
      min(1,SumFTS) is_a_pseudometric_of cT
      by A113,A139,BORSUK_1:def 5,NAGATA_1:27,30;
A167: for p,q holds min(1,SumFTS').[p,q]<=1
      proof
        let p,q;
        cTT=[:cT,cT:] by BORSUK_1:def 5;
        then min(1,SumFTS').[p,q]=min(1,SumFTS'.[p,q]) by NAGATA_1:def 9;
        hence thesis by XXREAL_0:17;
      end;
      now
        let p,q;
        assume ex A,B st A is open & B is open & A in Bn.m & B in Bn.n &
        p in A & Cl A c= B & not q in B;
        then SumFTS'.[p,q]>=1 by A140;
        then 1 = min(1,SumFTS'.[p,q]) & [:cT,cT:]=cTT
        by BORSUK_1:def 5,XXREAL_0:def 8;
        hence 1=min(1,SumFTS').[p,q] by NAGATA_1:def 9;
      end;
      hence thesis by A166,A167;
    end;
    defpred N[set,set] means ex F be RealMap of [:T,T:] st F = $2 &
    for n,m st PairFunc".$1=[n,m] holds NN[n,m,F];
A168: for k be set st k in NAT ex f be set st f in Funcs(cTT,REAL) & N[k,f]
    proof
      let k be set such that
A169: k in NAT;
A170: PairFunc is onto one-to-one by Th2;
      then NAT =rng PairFunc by FUNCT_2:def 3;
      then consider nm be set such that
A171: nm in dom PairFunc & k=PairFunc.nm by A169,FUNCT_1:def 5;
      consider n,m be set such that
A172: n in NAT & m in NAT & nm=[n,m] by A171,ZFMISC_1:def 2;
      consider pmet' such that
A173: NN[n,m,pmet'] by A6,A172;
      take pmet';
      thus pmet' in Funcs(cTT,REAL) by FUNCT_2:11;
      take pm = pmet';
      thus pm = pmet';
      let n1,m1 be Element of NAT;
      assume PairFunc".k=[n1,m1];
      then [n1,m1]=[n,m] by A170,A171,A172,Lm3,FUNCT_1:54;
      then n1=n & m1=m by ZFMISC_1:33;
      hence thesis by A173;
    end;
    consider F be Function of NAT,Funcs(cTT,REAL) such that
A174: for n be set st n in NAT holds N[n,F.n] from FUNCT_2:sch 1(A168);
A175: dom F=NAT by FUNCT_2:def 1;
    now
      let n;
      F.n in Funcs(cTT,REAL) & [:cT,cT:]=cTT by BORSUK_1:def 5;
      hence F.n is PartFunc of [:cT,cT:],REAL by FUNCT_2:121;
    end;
    then reconsider F'=F as Functional_Sequence of[:cT,cT:],REAL
    by A175,SEQFUNC:1;
A176: for k ex pmet st F'.k=pmet & pmet is_a_pseudometric_of cT &
    (for pq holds pmet.pq<=1) &
    for pmet' st pmet=pmet' holds pmet' is continuous
    proof
      let k;
A177: PairFunc is onto one-to-one by Th2;
      then NAT =rng PairFunc by FUNCT_2:def 3;
      then consider nm be set such that
A178: nm in dom PairFunc & k=PairFunc.nm by FUNCT_1:def 5;
      consider n,m be set such that
A179: n in NAT & m in NAT & nm=[n,m] by A178,ZFMISC_1:def 2;
A180: [n,m]=PairFunc".k by A177,A178,A179,Lm3,FUNCT_1:54;
      consider Fk be RealMap of [:T,T:] such that
A181: Fk = F.k & for n,m st PairFunc".k=[n,m] holds NN[n,m,Fk] by A174;
      consider pmet such that
A182: Fk=pmet & Fk is continuous & pmet is_a_pseudometric_of cT &
      for p,q holds pmet.[p,q]<=1 & for p,q st
      ex A,B st A is open & B is open & A in Bn.m & B in Bn.n &
      p in A & Cl A c= B & not q in B holds pmet.[p,q] = 1 by A179,A180,A181;
      take pmet;
      thus F'.k=pmet & pmet is_a_pseudometric_of cT by A181,A182;
      thus for pq holds pmet.pq<=1
      proof
        let pq;
        consider p,q be set such that
A183:   p in cT & q in cT & pq=[p,q] by ZFMISC_1:def 2;
        thus thesis by A182,A183;
      end;
      thus thesis by A182;
    end;
    for p,A' st not p in A' & A' is closed
    ex k st for pmet st F'.k=pmet holds inf(pmet,A').p>0
    proof
      let p,A' such that
A184: not p in A' & A' is closed;
      set O=A'`;
      p in O & O is open by A184,XBOOLE_0:def 4;
      then consider U be Subset of T such that
A185: p in U & Cl U c=O & U in Union Bn by A1,A5,NAGATA_1:19;
      Union Bn c=the topology of T by A5,CANTOR_1:def 2;
      then reconsider U as open Subset of T by A185,PRE_TOPC:def 5;
      consider W be Subset of T such that
A186: p in W & Cl W c=U & W in Union Bn by A1,A5,A185,NAGATA_1:19;
      Union Bn c=the topology of T by A5,CANTOR_1:def 2;
      then reconsider W as open Subset of T by A186,PRE_TOPC:def 5;
      consider n such that
A187: U in Bn.n by A185,PROB_1:25;
      consider m such that
A188: W in Bn.m by A186,PROB_1:25;
      set k=PairFunc.[n,m];
      take k;
      reconsider Fk=F.k as RealMap of [:T,T:] by FUNCT_2:121;
      dom PairFunc = [:NAT,NAT:] by FUNCT_2:def 1;
      then PairFunc is one-to-one & [n,m] in dom PairFunc by Th2;
      then
A189: [n,m]=PairFunc".k by Lm3,FUNCT_1:54;
      consider G be RealMap of [:T,T:] such that
A190: G = F.k & for n,m st PairFunc".k=[n,m] holds NN[n,m,G] by A174;
      consider pmet such that
A191: G=pmet & G is continuous & pmet is_a_pseudometric_of cT &
      for p,q holds pmet.[p,q]<=1 & for p,q st
      ex A,B st A is open & B is open & A in Bn.m & B in Bn.n &
      p in A & Cl A c= B & not q in B holds pmet.[p,q] = 1 by A189,A190;
      consider a be set such that
A192: a in A' by XBOOLE_0:def 1;
      a in A' & cT=dom dist(pmet,p) by A192,FUNCT_2:def 1;
      then
A193: dist(pmet,p).a in (dist(pmet,p)).:A' by FUNCT_1:def 12;
      for rn st rn in (dist(pmet,p)).:A' holds rn>=1
      proof
        let rn;
        assume rn in (dist(pmet,p)).:A';
        then consider a be set such that
A194:   a in dom dist(pmet,p) & a in A' & rn=(dist(pmet,p)).a
        by FUNCT_1:def 12;
        reconsider a as Point of T by A194;
        U c= Cl U & Cl U c=O by A185,PRE_TOPC:48;
        then U c= O by XBOOLE_1:1;
        then U misses A' by SUBSET_1:43;
        then not a in U by A194,XBOOLE_0:3;
        then Fk.[p,a]=1 & pmet.(p,a)=dist(pmet,p).a
        by A186,A187,A188,A190,A191,Def2;
        hence thesis by A190,A191,A194;
      end;
      then inf ((dist(pmet,p)).:A') >= 1 & pmet is bounded_below
      by A191,A193,Lm1,SEQ_4:60;
      hence thesis by A190,A191,Def3;
    end;
    hence T is metrizable by A2,A176,Th17;
  end;
  thus thesis by NAGATA_1:15,16;
end;

theorem Th20:
  T is metrizable implies for FX be Subset-Family of T
  st FX is Cover of T & FX is open ex Un be FamilySequence of T
  st Union Un is open & Union Un is Cover of T &
  Union Un is_finer_than FX & Un is sigma_discrete
proof
  set cT=the carrier of T;
  assume T is metrizable;
  then consider metr be Function of [:cT,cT:],REAL such that
A1: metr is_metric_of cT and
A2: Family_open_set(SpaceMetr(cT,metr))=the topology of T by PCOMPS_1:def 9;
  set PM=SpaceMetr(cT,metr);
  set cPM= the carrier of PM;
  let FX be Subset-Family of T such that
A3: FX is Cover of T and
A4: FX is open;
  consider R be Relation such that
A5: R well_orders FX by WELLORD2:26;
  consider Mn be Relation such that
A6: Mn = R |_2 FX;
  set UB ={union {Ball(c,1/2) where c is Point of PM:
  c in V\PartUnion(V,Mn) & Ball(c,3/2) c= V} where V is Subset of PM:V in FX};
  UB c= bool the carrier of PM
  proof
    let x;
    assume x in UB;
    then consider V be Subset of PM such that
A7: x=union{Ball(c,1/2) where c is Point of PM:
    c in V\PartUnion(V,Mn) & Ball(c,3/2)c= V} and V in FX;
    x c= cPM
    proof
      let y;
      assume y in x;
      then consider W be set such that
A8:   y in W & W in {Ball(c,1/2) where c is Point of PM:
      c in V\PartUnion(V,Mn)& Ball(c,3/2)c=V} by A7,TARSKI:def 4;
      ex c be Point of PM st W=Ball(c,1/2) & c in V\PartUnion(V,Mn)&
      Ball(c,3/2) c= V by A8;
      hence y in cPM by A8;
    end;
    hence thesis;
  end;
  then reconsider UB as Subset-Family of PM;
  deffunc F1(Point of PM,Nat)=Ball($1,1/(2|^($2+1)));
  defpred Q1[Point of PM,Subset of PM,Nat] means
  $1 in $2\PartUnion($2,Mn) & Ball($1,3/(2|^($3+1)))c= $2;
  defpred P1[set] means $1 in FX;
  consider Un be Function of NAT,bool bool cPM such that
A9: Un.0 = UB & for n holds Un.(n+1)=
  {union{F1(c,n) where c is Point of PM:Q1[c,V,n]
  & not c in union{union (Un.k): k <= n}}
  where V is Subset of PM: P1[V]} from PCOMPS_2:sch 3;
  cT= cPM by A1,PCOMPS_2:8;
  then reconsider Un'=Un as FamilySequence of T;
  take Un';
  thus Union Un' is open
  proof
    let A;
    assume A in Union Un';
    then consider n such that
A10: A in Un.n by PROB_1:25;
    per cases;
    suppose n=0;
      then consider V be Subset of PM such that
A11:  A=union{Ball(c,1/2) where c is Point of PM:
      c in V\PartUnion(V,Mn) & Ball(c,3/2) c= V} and V in FX by A9,A10;
      set BALL={Ball(c,1/2) where c is Point of PM:
      c in V\PartUnion(V,Mn) & Ball(c,3/2) c= V};
      BALL c= bool the carrier of PM
      proof
        let x;
        assume x in BALL;
        then ex c be Point of PM st x=Ball(c,1/2) & c in V\PartUnion(V,Mn )&
        Ball(c,3/2) c= V;
        hence thesis;
      end;
      then reconsider BALL as Subset-Family of PM;
      BALL c= Family_open_set PM
      proof
        let x;
        assume x in BALL;
        then ex c be Point of PM st x=Ball(c,1/2) & c in V\PartUnion(V,Mn )&
        Ball(c,3/2) c= V;
        hence x in Family_open_set PM by PCOMPS_1:33;
      end;
      then A in the topology of T by A2,A11,PCOMPS_1:36;
      hence A is open by PRE_TOPC:def 5;
    end;
    suppose n>0;
      then consider m being Nat such that
A12:  n=m+1 by NAT_1:6;
      reconsider m as Element of NAT by ORDINAL1:def 13;
      A in {union{F1(c,m) where c is Point of PM:Q1[c,V,m]&
      not c in union{union (Un.k): k <= m}}
      where V is Subset of PM: P1[V]} by A9,A10,A12;
      then consider V be Subset of PM such that
A13:  A=union{F1(c,m) where c is Point of PM:Q1[c,V,m]&
      not c in union{union (Un.k): k <= m}} & P1[V];
      set BALL={F1(c,m) where c is Point of PM:Q1[c,V,m]&
      not c in union{union (Un.k): k <= m}};
      BALL c= bool the carrier of PM
      proof
        let x;
        assume x in BALL;
        then ex c be Point of PM st x=F1(c,m) & Q1[c,V,m] &
        not c in union{union (Un.k): k <= m};
        hence thesis;
      end;
      then reconsider BALL as Subset-Family of PM;
      BALL c= Family_open_set PM
      proof
        let x;
        assume x in BALL;
        then ex c be Point of PM st x=F1(c,m) & Q1[c,V,m] &
        not c in union{union (Un.k): k <= m};
        hence x in Family_open_set PM by PCOMPS_1:33;
      end;
      then A in the topology of T by A2,A13,PCOMPS_1:36;
      hence A is open by PRE_TOPC:def 5;
    end;
  end;
A14: Mn well_orders FX by A5,A6,PCOMPS_2:5;
  thus Union Un' is Cover of T
  proof
    [#]T c= union Union Un'
    proof
      let x such that
A15:  x in [#]T;
      defpred P2[set] means x in $1;
      ex G be Subset of T st x in G & G in FX by A3,A15,PCOMPS_1:5;
      then
A16:  ex G be set st G in FX & P2[G];
      consider X such that
A17:  X in FX & P2[X] & for Y be set st
      Y in FX & P2[Y] holds [X,Y] in Mn from PCOMPS_2:sch 1(A14,A16);
      reconsider X as Subset of T by A17;
      X is open by A4,A17,TOPS_2:def 1;
      then
A18:  X in Family_open_set PM by A2,PRE_TOPC:def 5;
      reconsider X as Subset of PM by A1,PCOMPS_2:8;
      reconsider x'=x as Element of PM by A1,A15,PCOMPS_2:8;
      consider r such that
A19:  r>0 & Ball(x',r) c= X by A17,A18,PCOMPS_1:def 5;
      defpred P3[Nat] means 3/(2 |^ $1) <= r;
      ex k st P3[k] by A19,PREPOWER:106;
      then
A20:  ex k be Nat st P3[k];
      consider k be Nat such that
A21:  P3[k] & for i be Nat st P3[i] holds k <= i from NAT_1:sch 5(A20);
A22:  2|^(k+1)=2|^k*2 & 2|^k > 0 by NEWTON:11,PREPOWER:13;
      then 2|^(k+1) >= 2|^k by REAL_2:146;
      then 3/2 |^(k+1) <= 3/2|^k by A22,REAL_2:201;
      then
A23:  3/2 |^(k+1) <= r by A21,XXREAL_0:2;
      assume
A24:  not x in union Union Un';
A25:  for V be set,n be Element of NAT st V in Un'.n holds not x in V
      proof
        let V be set,n be Element of NAT;
        assume V in Un'.n;
        then V in Union Un by PROB_1:25;
        hence thesis by A24,TARSKI:def 4;
      end;
A26:  for n holds not x in union (Un'.n)
      proof
        let n;
        assume x in union (Un'.n);
        then consider V be set such that
A27:    x in V & V in Un'.n by TARSKI:def 4;
        thus contradiction by A25,A27;
      end;
      set W=union{F1(y,k) where y is Point of PM:Q1[y,X,k]
      & not y in union{union(Un.i):i <= k}};
A28:  x in W
      proof
        set A=Ball(x',1/(2|^(k+1)));
        0 < 2|^(k+1) by PREPOWER:13;
        then 0 < 1/(2|^(k+1)) by REAL_2:127;
        then
A29:    x' in A by TBSP_1:16;
        not x' in PartUnion(X,Mn)
        proof
          assume x' in PartUnion(X,Mn);
          then x' in union (Mn-Seg(X)) by PCOMPS_2:def 1;
          then consider M be set such that
A30:      x' in M & M in Mn-Seg(X) by TARSKI:def 4;
A31:      M <> X & [M,X] in Mn by A30,WELLORD1:def 1;
          then M in field Mn by RELAT_1:30;
          then
A32:      M in FX by A5,A6,PCOMPS_2:5;
          then [X,M] in Mn& Mn is_antisymmetric_in FX by A14,A17,A30,
          WELLORD1:def 5;
          hence contradiction by A17,A31,A32,RELAT_2:def 4;
        end;
        then
A33:    x' in X\PartUnion(X,Mn) by A17,XBOOLE_0:def 4;
        Ball(x',3/(2|^(k+1))) c= Ball(x',r) by A23,PCOMPS_1:1;
        then
A34:    Ball(x',3/(2|^(k+1))) c= X by A19,XBOOLE_1:1;
        not x' in union{union(Un'.i):i <= k}
        proof
          assume x' in union {union(Un'.i):i <= k};
          then consider D be set such that
A35:      x' in D & D in {union(Un'.i):i <= k} by TARSKI:def 4;
          consider i such that
A36:      D=union (Un'.i) & i <= k by A35;
          thus contradiction by A26,A35,A36;
        end;
        then A in {F1(y,k) where y is Point of PM:Q1[y,X,k] &
        not y in union {union(Un'.i): i <= k}} by A33,A34;
        hence thesis by A29,TARSKI:def 4;
      end;
A37:  k in NAT by ORDINAL1:def 13;
      W in {union{F1(y,k)where y is Point of PM: Q1[y,V,k] &
      not y in union {union(Un.q) where q is Element of NAT: q <= k}}
      where V is Subset of PM: V in FX} by A17;
      then W in Un'.(k+1) by A9,A37;
      hence thesis by A25,A28;
    end;
    then [#]T = union Union Un' by XBOOLE_0:def 10;
    hence thesis by SETFAM_1:60;
  end;
  thus Union Un' is_finer_than FX
  proof
    for X be set st X in Union Un' ex Y be set st Y in FX & X c= Y
    proof
      let X be set;
      assume X in Union Un';
      then consider n such that
A38:  X in Un.n by PROB_1:25;
      per cases;
      suppose n=0;
        then consider V be Subset of PM such that
A39:    X=union{Ball(c,1/2) where c is Point of PM:
        c in V\PartUnion(V,Mn) & Ball(c,3/2) c= V} & V in FX by A9,A38;
        set BALL={Ball(c,1/2) where c is Point of PM:
        c in V\PartUnion(V,Mn) & Ball(c,3/2) c= V};
        BALL c= bool the carrier of PM
        proof
          let x;
          assume x in BALL;
          then ex c be Point of PM st x=Ball(c,1/2) &
          c in V\PartUnion(V,Mn) & Ball(c,3/2) c= V;
          hence thesis;
        end;
        then reconsider BALL as Subset-Family of PM;
        for W be set st W in BALL holds W c= V
        proof
          let W be set;
          assume W in BALL;
          then consider c be Element of PM such that
A40:      W=Ball(c,1/2) & c in V\PartUnion(V,Mn) & Ball(c,3/2) c= V;
          Ball(c,1/2) c= Ball(c,3/2) by PCOMPS_1:1;
          hence thesis by A40,XBOOLE_1:1;
        end;
        then X c= V & V in FX by A39,ZFMISC_1:94;
        hence thesis;
      end;
      suppose n>0;
        then consider m being Nat such that
A41:    n=m+1 by NAT_1:6;
        reconsider m as Element of NAT by ORDINAL1:def 13;
        X in {union{F1(c,m) where c is Point of PM:Q1[c,V,m] &
        not c in union{union (Un.k): k <= m}}
        where V is Subset of PM: P1[V]} by A9,A38,A41;
        then consider V be Subset of PM such that
A42:    X=union{F1(c,m) where c is Point of PM:Q1[c,V,m] &
        not c in union{union (Un.k): k <= m}} & P1[V];
        set BALL={F1(c,m) where c is Point of PM:Q1[c,V,m] &
        not c in union{union (Un.k): k <= m}};
        BALL c= bool the carrier of PM
        proof
          let x;
          assume x in BALL;
          then ex c be Point of PM st x=F1(c,m) & Q1[c,V,m] &
          not c in union{union (Un.k): k <= m};
          hence thesis;
        end;
        then reconsider BALL as Subset-Family of PM;
        for W be set st W in BALL holds W c= V
        proof
          let W be set;
          assume W in BALL;
          then consider c be Element of PM such that
A43:      W=F1(c,m) & Q1[c,V,m] & not c in union{union (Un.k): k <= m};
          0 < 2|^(m+1) by PREPOWER:13;
          then 1/(2|^(m+1)) < 3/(2|^(m+1)) by REAL_1:73;
          then F1(c,m)c=Ball(c,3/(2|^(m+1))) by PCOMPS_1:1;
          hence thesis by A43,XBOOLE_1:1;
        end;
        then X c= V & V in FX by A42,ZFMISC_1:94;
        hence thesis;
      end;
    end;
    hence thesis by SETFAM_1:def 2;
  end;
  thus Un' is sigma_discrete
  proof
    for n holds Un'.n is discrete
    proof
      let n;
      for p ex O be open Subset of T st p in O &
      (for A,B st A in Un'.n & B in Un'.n holds
      O meets A & O meets B implies A=B)
      proof
        let p;
        reconsider p'=p as Point of PM by A1,PCOMPS_2:8;
        set O=Ball(p',1/(2|^(n+2)));
        0 < 2|^(n+2) by PREPOWER:13;
        then
A44:    0< 1/(2|^(n+2)) by REAL_2:127;
        then p' in O & O in Family_open_set PM by PCOMPS_1:33,TBSP_1:16;
        then reconsider O as open Subset of T by A2,PRE_TOPC:def 5;
        take O;
        now
          let A,B such that
A45:      A in Un'.n & B in Un'.n;
          assume
A46:      O meets A & O meets B;
          then consider a be set such that
A47:      a in O & a in A by XBOOLE_0:3;
          consider b be set such that
A48:      b in O & b in B by A46,XBOOLE_0:3;
          reconsider a,b as Point of PM by A47,A48;
          dist(p',a)<1/(2|^(n+2)) & dist(p',b)<1/(2|^(n+2)) &
          dist(p',a)=dist(a,p') by A47,A48,METRIC_1:12;
          then dist(a,p')+dist(p',b)<1/(2|^(n+2))+1/(2|^(n+2)) &
          dist(a,b)<=dist(a,p')+dist(p',b) by METRIC_1:4,XREAL_1:10;
          then dist(a,b)<1/(2|^(n+2))+1/(2|^(n+2)) & 2|^(n+1+1)=2|^(n+1)*2
          by NEWTON:11,XXREAL_0:2;
          then dist(a,b)<2*(1/(2*2|^(n+1)));
          then dist(a,b)<(2*1) /(2*2|^(n+1)) by XCMPLX_1:75;
          then
A49:      dist(a,b)<(2/2*1)/2|^(n+1) by XCMPLX_1:84;
          now per cases;
            suppose
A50:          n=0;
              then consider V be Subset of PM such that
A51:          A=union {Ball(c,1/2) where c is Point of PM:
              c in V\PartUnion(V,Mn) & Ball(c,3/2) c= V} & V in FX by A9,A45;
              consider Ba be set such that
A52:          a in Ba & Ba in {Ball(c,1/2) where c is Point of PM:
              c in V\PartUnion(V,Mn) & Ball(c,3/2) c= V}
              by A47,A51,TARSKI:def 4;
              consider ca be Point of PM such that
A53:          Ba=Ball(ca,1/2) & ca in V\PartUnion(V,Mn) &
              Ball(ca,3/2) c= V by A52;
              consider W be Subset of PM such that
A54:          B=union {Ball(c,1/2) where c is Point of PM:
              c in W\PartUnion(W,Mn) & Ball(c,3/2) c= W} &
              W in FX by A9,A45,A50;
              consider Bb be set such that
A55:          b in Bb & Bb in {Ball(c,1/2) where c is Point of PM:
              c in W\PartUnion(W,Mn) & Ball(c,3/2) c= W}
              by A48,A54,TARSKI:def 4;
              consider cb be Point of PM such that
A56:          Bb=Ball(cb,1/2) & cb in W\PartUnion(W,Mn) &
              Ball(cb,3/2) c= W by A55;
              dist(ca,a)<1/2 & dist(a,b)<1/2 by A49,A50,A52,A53,METRIC_1:12
              ,NEWTON:10;
              then dist(ca,a)+dist(a,b)<1/2+1/2 & dist(ca,b)<=dist(ca,a)+
              dist(a,b) by METRIC_1:4,XREAL_1:10;
              then dist(ca,b)<1 & dist(cb,b)<1/2 & dist(cb,b)=dist(b,cb)
              by A55,A56,METRIC_1:12,XXREAL_0:2;
              then dist(ca,b)+dist(b,cb)<1+1/2 & dist(ca,cb)<=dist(ca,b)+
              dist(b,cb) by METRIC_1:4,XREAL_1:10;
              then dist(ca,cb)<3/2 & dist(ca,cb)=dist(cb,ca) by XXREAL_0:2;
              then
A57:          ca in Ball(cb,3/2)&cb in Ball(ca,3/2) by METRIC_1:12;
              V=W
              proof
                assume
A58:            V<>W;
                Mn is_connected_in FX by A14,WELLORD1:def 5;
                then [V,W] in Mn or [W,V] in Mn by A51,A54,A58,RELAT_2:def 6;
                then V in Mn-Seg(W) or W in Mn-Seg(V)by A58,WELLORD1:def 1;
                then cb in union(Mn-Seg(W)) or ca in union(Mn-Seg(V)) by A53
                ,A56,A57,TARSKI:def 4;
                then cb in PartUnion(W,Mn) or ca in PartUnion(V,Mn)
                by PCOMPS_2:def 1;
                hence thesis by A53,A56,XBOOLE_0:def 4;
              end;
              hence A=B by A51,A54;
            end;
            suppose n>0;
              then consider m being Nat such that
A59:          n=m+1 by NAT_1:6;
              reconsider m as Element of NAT by ORDINAL1:def 13;
              A in {union{F1(c,m) where c is Point of PM:Q1[c,V,m]
              & not c in union{union (Un.k): k <= m}}
              where V is Subset of PM: P1[V]} by A9,A45,A59;
              then consider V be Subset of PM such that
A60:          A=union{F1(c,m) where c is Point of PM:Q1[c,V,m]
              & not c in union{union(Un.k): k <= m}} & P1[V];
              consider Ba be set such that
A61:          a in Ba&Ba in {F1(c,m) where c is Point of PM:Q1[c,V,m]
              & not c in union{union(Un.k): k <= m}} by A47,A60,TARSKI:def 4;
              consider ca be Point of PM such that
A62:          Ba=F1(ca,m) & Q1[ca,V,m] &
              not ca in union{union(Un.k):k<=m} by A61;
              B in {union{F1(c,m) where c is Point of PM:Q1[c,W,m]
              & not c in union{union (Un.k): k <= m}}
              where W is Subset of PM:P1[W]} by A9,A45,A59;
              then consider W be Subset of PM such that
A63:          B=union{F1(c,m) where c is Point of PM:Q1[c,W,m]
              & not c in union{union(Un.k): k <= m}} & P1[W];
              consider Bb be set such that
A64:          b in Bb&Bb in {F1(c,m) where c is Point of PM:Q1[c,W,m]
              & not c in union{union(Un.k): k <= m}} by A48,A63,TARSKI:def 4;
              consider cb be Point of PM such that
A65:          Bb=F1(cb,m) & Q1[cb,W,m] &
              not cb in union{union(Un.k):k<=m} by A64;
              set r=1/(2|^n);
A66:          2|^(n+1)=2|^n*2 & 2|^n>0 by NEWTON:11,PREPOWER:13;
              then 2|^(n+1)>=2|^n by REAL_2:146;
              then 1/2|^(n+1) <= r by A66,REAL_2:201;
              then dist(ca,a)<r & dist(a,b)<r
              by A49,A59,A61,A62,METRIC_1:12,XXREAL_0:2;
              then dist(ca,a)+dist(a,b)<r+r & dist(ca,b)<=dist(ca,a)+
              dist(a,b) by METRIC_1:4,XREAL_1:10;
              then dist(ca,b)<r+r & dist(cb,b)<r & dist(cb,b)=dist(b,cb)
              by A59,A64,A65,METRIC_1:12,XXREAL_0:2;
              then dist(ca,b)+dist(b,cb)<(r+r)+r & dist(ca,cb)<=dist(ca,b)+
              dist(b,cb) by METRIC_1:4,XREAL_1:10;
              then dist(ca,cb)<3*r &dist(ca,cb)=dist(cb,ca) &3*r=(3*1)/2|^n
              by XCMPLX_1:75,XXREAL_0:2;
              then
A67:          ca in Ball(cb,3/2|^(m+1))&cb in Ball(ca,3/2|^( m+1))
              by A59,METRIC_1:12;
              V=W
              proof
                assume
A68:            V<>W;
                Mn is_connected_in FX by A14,WELLORD1:def 5;
                then [V,W] in Mn or [W,V] in Mn by A60,A63,A68,RELAT_2:def 6;
                then V in Mn-Seg(W) or W in Mn-Seg(V)by A68,WELLORD1:def 1;
                then cb in union(Mn-Seg(W)) or ca in union(Mn-Seg(V)) by A62
                ,A65,A67,TARSKI:def 4;
                then cb in PartUnion(W,Mn) or ca in PartUnion(V,Mn)
                by PCOMPS_2:def 1;
                hence thesis by A62,A65,XBOOLE_0:def 4;
              end;
              hence A=B by A60,A63;
            end;
          end;
          hence A=B;
        end;
        hence thesis by A44,TBSP_1:16;
      end;
      hence thesis by NAGATA_1:def 1;
    end;
    hence thesis by NAGATA_1:def 2;
  end;
end;

theorem Th21:
  for T st T is metrizable
  ex Un be FamilySequence of T st Un is Basis_sigma_discrete
proof
  let T such that
A1: T is metrizable;
  consider metr be Function of
  [:the carrier of T,the carrier of T:],REAL such that
A2: metr is_metric_of the carrier of T &
  Family_open_set(SpaceMetr (the carrier of T,metr)) =
  the topology of T by A1,PCOMPS_1:def 9;
  set PM = SpaceMetr(the carrier of T,metr);
  set TPM = TopSpaceMetr PM;
A3: TPM = TopStruct (#the carrier of PM,Family_open_set PM#)
  by PCOMPS_1:def 6;
  then
A4: [#]T=[#]TPM by A2,PCOMPS_2:8;
  deffunc BALL(set)={Ball(x, 1/(2 |^ (In($1,NAT)))) where x is Point of PM:
  x is Point of PM};
A5: for k be set st k in NAT holds BALL(k) in bool bool the carrier of T
  proof
    let k be set;
    assume k in NAT;
    then reconsider k as Element of NAT;
    BALL(k) c= bool the carrier of T
    proof
      let B be set such that
A6:   B in BALL(k);
      In(k,NAT) = k by FUNCT_7:def 1;
      then consider x be Point of PM such that
A7:   B=Ball(x,1/(2|^k)) & x is Point of PM by A6;
      B in Family_open_set PM by A7,PCOMPS_1:33;
      hence thesis by A2;
    end;
    hence thesis;
  end;
  consider FB be FamilySequence of T such that
A8: for k be set st k in NAT holds FB.k= BALL(k) from FUNCT_2:sch 2(A5);
  set bbcT=bool bool the carrier of T;
  defpred U[set,set] means ex FS be FamilySequence of T st $2 = FS &
  Union FS is open & Union FS is Cover of T &
  Union FS is_finer_than FB.$1 & FS is sigma_discrete;
A9: for n be Element of NAT ex Un be Element of Funcs(NAT,bbcT) st U[n,Un]
  proof
    let n be Element of NAT;
A10: n = In(n,NAT) by FUNCT_7:def 1;
    [#]TPM = union (FB.n)
    proof
      thus [#]TPM c= union (FB.n)
      proof
        let x;
        assume x in [#]TPM;
        then reconsider x'=x as Element of PM by A3;
        2|^n > 0 by NEWTON:102;
        then 0<1/(2|^n) by REAL_2:127;
        then x' in Ball(x',1/(2|^ n)) &
        Ball(x',1/(2|^ n)) in BALL(n) by A10,TBSP_1:16;
        then x' in Ball(x',1/(2|^ n))&Ball(x',1/(2|^ n)) in FB.n by A8;
        hence thesis by TARSKI:def 4;
      end;
      thus union (FB.n) c= [#]TPM
      proof
        given x such that
A11:    x in union (FB.n) & not x in [#]TPM;
        consider A be set such that
A12:    x in A & A in (FB.n) by A11,TARSKI:def 4;
        x in A & A in BALL(n) by A8,A12;
        then consider y be Point of PM such that
A13:    A=Ball(y,1/(2|^n)) & y is Point of PM by A10;
        thus thesis by A3,A11,A12,A13;
      end;
    end;
    then
A14: (FB.n) is Cover of T by A4,SETFAM_1:60;
    now
      let U be Subset of T;
      assume U in (FB.n);
      then U in BALL(n) by A8;
      then ex x be Point of PM st
      U = Ball(x,1/(2|^ n)) & x is Point of PM by A10;
      then U in the topology of T by A2,PCOMPS_1:33;
      hence U is open by PRE_TOPC:def 5;
    end;
    then FB.n is open by TOPS_2:def 1;
    then consider Un be FamilySequence of T such that
A15: Union Un is open & Union Un is Cover of T &
    Union Un is_finer_than FB.n & Un is sigma_discrete by A1,A14,Th20;
    Un in Funcs(NAT,bbcT) by FUNCT_2:11;
    hence thesis by A15;
  end;
  consider Un be Function of NAT,Funcs(NAT,bbcT) such that
A16: for n be Element of NAT holds U[n,Un.n] from FUNCT_2:sch 3(A9);
  defpred X[set,set] means for n,m st PairFunc.[n,m]=$1
  for Unn be FamilySequence of T st Unn=Un.n holds $2=Unn.m;
A17: for k be set st k in NAT ex Ux be set st Ux in bbcT & X[k,Ux]
  proof
    let k be set;
    assume k in NAT;
    then reconsider k'=k as Element of NAT;
A18: PairFunc is onto one-to-one by Th2;
    then NAT =rng PairFunc by FUNCT_2:def 3;
    then consider nm be set such that
A19: nm in dom PairFunc & k'=PairFunc.nm by FUNCT_1:def 5;
    consider n,m be set such that
A20: n in NAT & m in NAT & nm=[n,m] by A19,ZFMISC_1:def 2;
    reconsider Unn=Un.n as FamilySequence of T by A20,FUNCT_2:7,121;
    take Ux=Unn.m;
    now
      let n1,m1 be Element of NAT such that
A21:  PairFunc.[n1,m1]=k;
      now
        let Unn be FamilySequence of T such that
A22:    Unn=Un.n1;
        [n,m]=[n1,m1] by A18,A19,A20,A21,FUNCT_2:25;
        then n1=n & m=m1 by ZFMISC_1:33;
        hence Ux=Unn.m1 by A22;
      end;
      hence for Unn be FamilySequence of T st Unn=Un.n1 holds Ux=Unn.m1;
    end;
    hence Ux in bbcT & X[k,Ux];
  end;
  consider UX be Function of NAT,bbcT such that
A23: for k be set st k in NAT holds X[k,UX.k] from FUNCT_2:sch 1(A17);
A24: UX is sigma_discrete
  proof
    for k holds UX.k is discrete
    proof
      let k;
      PairFunc is onto by Th2;
      then NAT = rng PairFunc by FUNCT_2:def 3;
      then consider nm be set such that
A25:  nm in dom PairFunc & k=PairFunc.nm by FUNCT_1:def 5;
      consider n,m be set such that
A26:  n in NAT & m in NAT & nm=[n,m] by A25,ZFMISC_1:def 2;
      consider FS be FamilySequence of T such that
A27:  Un.n = FS & Union FS is open & Union FS is Cover of T &
      Union FS is_finer_than FB.n & FS is sigma_discrete by A16,A26;
      FS.m is discrete by A26,A27,NAGATA_1:def 2;
      hence thesis by A23,A25,A26,A27;
    end;
    hence thesis by NAGATA_1:def 2;
  end;
A28: Union UX c= the topology of T
  proof
    let UXk be set;
    assume UXk in Union UX;
    then consider k such that
A29: UXk in UX.k by PROB_1:25;
    reconsider UXk'=UXk as Subset of T by A29;
    PairFunc is onto by Th2;
    then NAT =rng PairFunc by FUNCT_2:def 3;
    then consider nm be set such that
A30: nm in dom PairFunc & k=PairFunc.nm by FUNCT_1:def 5;
    consider n,m be set such that
A31: n in NAT & m in NAT & nm=[n,m] by A30,ZFMISC_1:def 2;
    reconsider Unn=Un.n as FamilySequence of T by A31,FUNCT_2:7,121;
    consider FS be FamilySequence of T such that
A32: Un.n = FS & Union FS is open & Union FS is Cover of T &
    Union FS is_finer_than FB.n & FS is sigma_discrete by A16,A31;
    UXk in Unn.m by A23,A29,A30,A31;
    then UXk in Union Unn & Union Unn is open by A31,A32,PROB_1:25;
    then UXk' is open by TOPS_2:def 1;
    hence thesis by PRE_TOPC:def 5;
  end;
  for A st A is open for p st p in A ex B st B in Union UX & p in B & B c= A
  proof
    let A such that
A33: A is open;
    let p such that
A34: p in A;
A35: A in Family_open_set PM by A2,A33,PRE_TOPC:def 5;
    then reconsider p as Element of PM by A34;
    consider r such that
A36: r>0 & Ball(p,r) c=A by A34,A35,PCOMPS_1:def 5;
    consider n such that
A37: 1/(2|^n)<=r by A36,PREPOWER:106;
    consider Unn1 be FamilySequence of T such that
A38: Un.(n+1) = Unn1 & Union Unn1 is open & Union Unn1 is Cover of T &
    Union Unn1 is_finer_than FB.(n+1) & Unn1 is sigma_discrete by A16;
    union (Union Unn1) = [#]T by A38,SETFAM_1:60;
    then consider B be set such that
A39: p in B & B in (Union Unn1) by TARSKI:def 4;
    reconsider B as Subset of PM by A2,A39,PCOMPS_2:8;
    consider B1 be set such that
A40: B1 in FB.(n+1) & B c= B1 by A38,A39,SETFAM_1:def 2;
A41: n + 1 = In(n+1,NAT) by FUNCT_7:def 1;
    B1 in BALL(n+1) by A8,A40;
    then consider q be Point of PM such that
A42: B1=Ball(q,(1/(2|^(n+1)))) & q is Element of PM by A41;
    consider k such that
A43: B in Unn1.k by A39,PROB_1:25;
A44: UX.(PairFunc.[n+1,k])=Unn1.k by A23,A38;
    B c= Ball(p,r)
    proof
      now
        let s be Element of PM;
        assume s in B;
        then
A45:    dist(q,s)<1/(2|^(n+1)) by A40,A42,METRIC_1:12;
A46:    dist(p,s)<=dist(q,p)+dist(q,s) by METRIC_1:4;
        dist(q,p)<1/(2|^(n+1)) by A39,A40,A42,METRIC_1:12;
        then dist(q,p)+dist(q,s)<1/(2|^(n+1))+1/(2|^(n+1)) by A45,XREAL_1:10;
        then dist(p,s)<2*(1/(2|^(n+1))) by A46,XXREAL_0:2;
        then dist(p,s)<(1/(2|^n * 2))*2 by NEWTON:11;
        then dist(p,s)<(1/(2|^n)) by XCMPLX_1:93;
        then dist(p,s)<r by A37,XXREAL_0:2;
        hence s in Ball(p,r) by METRIC_1:12;
      end;
      hence thesis by SUBSET_1:7;
    end;
    then B in Union UX&p in B& B c=A by A36,A39,A43,A44,PROB_1:25,XBOOLE_1:1;
    hence thesis;
  end;
  then Union UX is Basis of T by A28,YELLOW_9:32;
  then UX is Basis_sigma_discrete by A24,NAGATA_1:def 5;
  hence thesis;
end;

:: BING's THEOREM

theorem
  for T holds (T is_T3 & T is_T1 &
  ex Bn being FamilySequence of T st Bn is Basis_sigma_discrete)
  iff T is metrizable
proof
  let T;
  now
    assume
A1: T is_T3 & T is_T1 & ex Bn be FamilySequence of T st
    Bn is Basis_sigma_discrete;
    then consider Bn be FamilySequence of T such that
A2: Bn is Basis_sigma_discrete;
A3: Bn is sigma_discrete & Union Bn is Basis of T by A2,NAGATA_1:def 5;
    then Bn is sigma_locally_finite by NAGATA_1:12;
    then Bn is Basis_sigma_locally_finite by A3,NAGATA_1:def 6;
    hence T is metrizable by A1,Th19;
  end;
  hence thesis by Th21,NAGATA_1:15;
end;

