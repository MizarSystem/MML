:: Basic Operations on Extended Real Numbers
::  by Andrzej Trybulec
::
:: Received September 23, 2008
:: Copyright (c) 2008 Association of Mizar Users

environ

 vocabularies SUPINF_1, ARYTM, ARYTM_1, RELAT_1, ASYMPT_0, ZF_LANG,
  ARYTM_3, XCMPLX_0, XREAL_0, SQUARE_1;
 notations ORDINAL1, SUBSET_1,
   XCMPLX_0, XXREAL_0, XREAL_0, NUMBERS;
 constructors XBOOLE_0, ORDINAL1, XXREAL_0, XCMPLX_0, XREAL_0, NUMBERS,
   XXREAL_2;
 registrations XXREAL_0, XREAL_0, ORDINAL1, XBOOLE_0, XCMPLX_0, REAL_1, NAT_1,
   NUMBERS;
 requirements BOOLE, SUBSET, NUMERALS, ARITHM, REAL;
 definitions XCMPLX_0, XXREAL_0;
 theorems XXREAL_0, XREAL_0, XCMPLX_1, XREAL_1;

begin

reserve x,y,z,w for ext-real number,
        r,s for real number;

definition
  let x,y;
  redefine pred x <= y means
:Def1: ex p,q being Element of REAL st p = x & q = y & p <= q
   if x in REAL & y in REAL
  otherwise x = -infty or y = +infty;
  consistency;
  compatibility
  proof
    thus x in REAL & y in REAL implies
    (x <= y iff ex p,q being Element of REAL st p = x & q = y & p <= q);
    thus not(x in REAL & y in REAL) implies
    (x <= y iff x = -infty or y = +infty)
    proof
      assume
A3:   not(x in REAL & y in REAL);
      per cases by A3;
      suppose
A4:    not x in REAL;
        thus x <= y implies x = -infty or y = +infty
        proof
          assume
A5:       x <= y;
          assume x <> -infty;
          then x = +infty by A4,XXREAL_0:14;
          hence thesis by A5,XXREAL_0:4;
        end;
        thus thesis by XXREAL_0:3,5;
      end;
      suppose
A6:    not y in REAL;
        thus x <= y implies x = -infty or y = +infty
        proof
          assume
A7:       x <= y;
          assume
A8:       x <> -infty;
          assume y <> +infty;
          then y = -infty by A6,XXREAL_0:14;
          hence thesis by A7,A8,XXREAL_0:6;
        end;
        thus thesis by XXREAL_0:3,5;
      end;
    end;
  end;
end;

registration
 cluster non real positive (ext-real number);
 existence
  proof
   take +infty;
   thus thesis;
  end;
 cluster non real negative (ext-real number);
 existence
  proof
   take -infty;
   thus thesis;
  end;
end;

theorem Theorem1:
 for x being non real positive (ext-real number) holds x = +infty
 proof let x be non real positive (ext-real number);
   not x in REAL & x <> -infty;
  hence thesis by XXREAL_0:14;
 end;

theorem Theorem2:
 for x being non real negative (ext-real number) holds x = -infty
 proof let x be non real negative (ext-real number);
   not x in REAL & x <> +infty;
  hence thesis by XXREAL_0:14;
 end;

registration
 cluster non real non negative -> positive (ext-real number);
 coherence;
 cluster non real non positive -> negative (ext-real number);
 coherence;
end;

theorem Th23:
  0 < x implies ex y being real number st 0 < y & y < x
proof
  assume
A1: 0 < x;
  per cases by XXREAL_0:14,A1;
  suppose x in REAL;
    then reconsider z = x as real number;
    consider t being real number such that
A3: 0 < t & t < z by A1,XREAL_1:7;
    take t;
    thus thesis by A3;
  end;
  suppose
A4: x = +infty;
    consider z being real number such that
A5: 0 < z by XREAL_1:3;
    take z;
    thus thesis by A4,A5,XXREAL_0:4;
  end;
end;

begin :: addition

definition let x,y;
  func x + y -> ext-real number means :Def3:
    ex a,b being complex number st x = a & y = b & it = a + b
      if x is real & y is real,
    it = +infty if x = +infty & y <> -infty or y = +infty & x <> -infty,
    it = -infty if x = -infty & y <> +infty or y = -infty & x <> +infty
    otherwise it = 0;
 existence
  proof
   thus x is real & y is real implies
      ex c being ext-real number, a, b being complex number
       st x = a & y = b & c = a + b
     proof assume x is real & y is real;
       then reconsider a = x, b = y as real number;
      take c = a+b, a, b;
      thus thesis;
     end;
   thus thesis;
  end;
 uniqueness;
 consistency;
 commutativity;
end;

definition let x;
  func -x -> ext-real number means :Def5:
    ex a being complex number st x = a & it = -a if x is real,
    it = -infty if x = +infty
    otherwise it = +infty;
 existence
  proof
   thus x is real implies
    ex c being ext-real number, a being complex number st
     x = a & c = -a
     proof assume x is real;
       then reconsider a = x as real number;
      take -a, a;
      thus thesis;
     end;
   thus thesis;
  end;
 uniqueness;
 consistency;
 involutiveness
  proof
    let y,x be ext-real number such that
A1: x is real implies ex a being complex number st x = a & y = - a and
A2: x = +infty implies y = -infty and
A3: x is not real & x <> +infty implies y = +infty;
    thus y is real implies ex a being complex number st y = a & x = - a
    proof
      assume y is real;
      then consider a being complex number such that
A4:   x = a and
A5:   y = - a by A1,A2,A3;
      take -a;
      thus thesis by A4,A5;
    end;
     x in REAL implies x is real;
    hence y = +infty implies x = -infty by A1,A2,XXREAL_0:14;
    thus thesis by A1,A3;
  end;
end;

definition let x,y;
 func x - y -> ext-real number equals x + -y;
 coherence;
end;

registration let x,y be real number, a,b be complex number;
 identify x+y with a+b when x = a, y = b;
 compatibility by Def3;
end;

registration let x be real number, a be complex number;
 identify -x with -a when x = a;
 compatibility by Def5;
end;

registration :: for ext-real numbers
  let r be real number;
  cluster -r -> real;
  coherence;
end;

registration :: for ext-real numbers
  let r,s be real number;
  cluster r+s -> real;
  coherence;
  cluster r-s -> real;
  coherence;
end;

registration let x be real number, y be non real ext-real number;
 cluster x+y -> non real number;
 coherence
proof
 let z be number such that
Z1: z = x+y;
  not y in REAL;
  then y = -infty or y = +infty by XXREAL_0:14;
 hence thesis by Z1,Def3;
end;
end;

registration let x,y be non real positive (ext-real number);
 cluster x+y -> non real;
 coherence
  proof
    x = +infty & y = +infty by Theorem1;
   hence thesis by Def3;
  end;
end;

registration let x,y be non real negative (ext-real number);
 cluster x+y -> non real;
 coherence
  proof
    x = -infty & y = -infty by Theorem2;
   hence thesis by Def3;
  end;
end;

registration
 let x be non real negative (ext-real number),
     y be non real positive (ext-real number);
 cluster x+y -> zero;
 coherence
  proof
    x = -infty & y = +infty by Theorem1,Theorem2;
   hence thesis by Def3;
  end;
end;

registration let x,y be real number, a,b be complex number;
 identify x-y with a-b when x = a, y = b;
 compatibility;
end;

theorem Tx3:
 x + 0 = x
 proof
  per cases by XXREAL_0:14;
  suppose x in REAL;
    then reconsider r=x as real number;
    r + 0 = r;
   hence thesis;
  end;
  suppose x = -infty or x = +infty;
   hence thesis by Def3;
  end;
 end;

theorem Tx4A:
 --infty = +infty by Def5;

theorem Tx4B:
 -+infty = -infty by Def5;

Lm5C:
 +infty + +infty = +infty by Def3;

Lm5D:
 -infty + -infty = -infty by Def3;

theorem Tx4:
 x + -x = 0
proof
 per cases by XXREAL_0:14;
 suppose x = -infty;
  hence thesis by Tx4A;
 end;
 suppose x in REAL;
   then reconsider x as real number;
   reconsider y=-x as real number;
   x + y = 0;
  hence thesis;
 end;
 suppose x = +infty;
  hence thesis by Tx4B;
 end;
end;

theorem
 x+y = 0 implies x = -y
proof
 assume
Z: x+y = 0;
 per cases by XXREAL_0:14;
 suppose x in REAL;
  then reconsider x as real number;
   x+y is real by Z;
  then reconsider y as real number;
  x+y = 0 by Z;
  then x = -y;
 hence thesis;
 end;
 suppose
S: x = -infty;
  then y = +infty by Z,Def3;
 hence thesis by S,Def5;
 end;
 suppose
S: x = +infty;
  then y = -infty by Z,Def3;
 hence thesis by S,Def5;
 end;
end;

Lm2: for x  holds -x in REAL iff x in REAL
proof
  let x;
  hereby
    assume -x in REAL;
    then reconsider a = -x as real number;
    -a in REAL by XREAL_0:def 1;
    hence x in REAL;
  end;
  assume x in REAL;
  then reconsider a = x as real number;
  reconsider c = a as complex number;
  -a is real;
  hence thesis by XREAL_0:def 1;
end;

Lm4: -(+infty + -infty) = +infty - +infty by Def5
  .= --infty - +infty by Def5;

Lm3x: -+infty = -infty by Def5;

Lm5: x in REAL implies -(x + +infty) = -+infty + -x
proof
  assume
A1: x in REAL;
    x + +infty = +infty by A1,Def3;
  hence thesis by Tx4B,A1,Def3;
end;

Lm6: x in REAL implies -(x + -infty) = --infty + -x
proof
  assume
A1: x in REAL;
    x + -infty = -infty by A1,Def3;
  hence thesis by Tx4A,A1,Def3;
end;

theorem Th9:
 -(x+y) = -x + -y
proof per cases by XXREAL_0:14;
  suppose
A1: x = +infty & y = +infty;
    hence -(x + y) = -+infty by Def3
      .= -x +- y by A1,Lm3x,Def3;
  end;
  suppose x = +infty & y = -infty;
    hence thesis by Lm4;
  end;
  suppose x = +infty & y in REAL;
    hence thesis by Lm5;
  end;
  suppose x = -infty & y = +infty;
    hence thesis by Lm4;
  end;
  suppose
A2: x = -infty & y = -infty;
    hence -(x + y) = --infty by Def3
      .= -x +- y by A2,Tx4A,Def3;
  end;
  suppose x = -infty & y in REAL;
    hence thesis by Lm6;
  end;
  suppose x in REAL & y = +infty;
    hence thesis by Lm5;
  end;
  suppose x in REAL & y = -infty;
    hence thesis by Lm6;
  end;
  suppose x in REAL & y in REAL;
    then reconsider a=x, b=y as real number;
     -(x + y) = -(a + b)
      .= -a + - b;
    hence thesis;
  end;
end;

reserve f,g for ext-real number;

theorem ThM3:  :: MEMBER_1:3
  -f = -g implies f = g
  proof
    assume
A1: -f = -g;
    per cases by XXREAL_0:14;
    suppose
A2:   f in REAL;
      then
A3:   ex a being complex number st f = a & -f = -a
      by Def5;
      now
        assume not g in REAL;
        then g = +infty or g = -infty by XXREAL_0:14;
        hence contradiction by A1,A2,Def5;
      end;
      then ex a being complex number st g = a & -g = -a
      by Def5;
      hence thesis by A1,A3;
    end;
    suppose f = +infty;
      hence thesis by A1,Tx4A;
    end;
    suppose f = -infty;
      then - -g = -infty by A1;
      hence thesis by A1;
    end;
  end;

LTh8:
  x + y = +infty implies x = +infty or y = +infty
proof
  assume
A1: x + y = +infty;
  assume not (x = +infty or y = +infty);
  then
A3: (x in REAL & y in REAL) or (x in REAL & y = -infty) or
  (x = -infty & y in REAL) or (x = -infty & y = -infty) by XXREAL_0:14;
  thus thesis by A1,A3,Def3;
end;

LTh9:
 x + y = -infty implies (x = -infty or y = -infty)
proof
  assume
A1: x + y = -infty;
  assume not (x = -infty or y = -infty);
  then
A3: (x in REAL & y in REAL) or (x in REAL & y = +infty) or
  (x = +infty & y in REAL) or (x = +infty & y = +infty) by XXREAL_0:14;
  thus thesis by A1,A3,Def3;
end;

theorem Th4:  :: MEMBER_1:4
  r+f = r+g implies f = g
  proof
    assume
A1: r+f = r+g;
    per cases by XXREAL_0:14;
    suppose
A3:   f in REAL;
      then consider a, b being complex number such that
A4:   r = a & f = b and
A5:   r+f = a+b by Def3;
      now
        assume not g in REAL;
        then g = +infty or g = -infty by XXREAL_0:14;
        hence contradiction by A1,A3;
      end;
      then ex c, d being complex number st r = c & g = d & r+g = c+d
      by Def3;
      hence thesis by A1,A4,A5;
    end;
    suppose
A6:   f = +infty;
      then r+f = +infty by Def3;
      hence thesis by A1,A6,LTh8;
    end;
    suppose
A7:   f = -infty;
      then r+f = -infty by Def3;
      hence thesis by A1,A7,LTh9;
    end;
  end;

theorem :: MEMBER_1:5
  r-f = r-g implies f = g
  proof
    assume r-f = r-g;
    then -f = -g by Th4;
    hence thesis by ThM3;
  end;

theorem Th16:
  x <> +infty implies +infty - x = +infty & x - +infty = -infty
proof
  assume
A1: x <> +infty;
  then -x <> -infty by Tx4A;
  hence +infty - x = +infty by Def3;
  - +infty = -infty by Def5;
  hence thesis by A1,Def3;
end;

theorem Th29:
 x <> -infty implies -infty - x = -infty & x - -infty = +infty
proof
  assume
A1: x <> -infty;
  now
    assume -x = +infty;
    then --x = -infty by Def5;
    hence contradiction by A1;
  end;
  hence -infty - x = -infty by Def3;
  thus thesis by A1,Def3,Tx4A;
end;

theorem Th21:
  x - 0 = x
proof
  per cases by XXREAL_0:14;
  suppose x in REAL;
    then reconsider a = x as real number;
    x - 0 = a - 0
      .= x;
    hence thesis;
  end;
  suppose x = -infty;
    hence thesis by Th29;
  end;
  suppose x = +infty;
    hence thesis by Th16;
  end;
end;

theorem
  x + y = +infty implies x = +infty or y = +infty by LTh8;

theorem
 x + y = -infty implies (x = -infty or y = -infty) by LTh9;

theorem Th32:
  x - y = +infty implies (x = +infty or y = -infty)
proof
  assume
A1: x - y = +infty;
  assume not (x = +infty or y = -infty);
  then
A3: (x in REAL & y in REAL) or (x in REAL & y = +infty) or
  (x = -infty & y in REAL) or (x = -infty & y = +infty) by XXREAL_0:14;
  thus thesis by A1,A3,Th16,Th29;
end;

theorem Th33:
 x - y = -infty implies (x = -infty or y = +infty)
proof
  assume
A1: x - y = -infty;
  assume not (x = -infty or y = +infty);
  then
A3: (x in REAL & y in REAL) or (x in REAL & y = -infty) or
  (x = +infty & y in REAL) or (x = +infty & y = -infty) by XXREAL_0:14;
  thus thesis by A1,A3,Th16,Th29;
end;

theorem Th34:
  (not ((x = +infty & y = -infty) or (x = -infty & y = +infty)) &
  x + y in REAL) implies x in REAL & y in REAL
proof
  assume
A1: (not ((x = +infty & y = -infty) or (x = -infty & y = +infty
  )) & x + y in REAL);
  assume
A2: not (x in REAL & y in REAL);
  (x in REAL or x = -infty or x = +infty) &
  (y in REAL or y = -infty or y = +infty) by XXREAL_0:14;
  hence thesis by A1,A2;
end;

theorem Th35:
  (not ((x = +infty & y = +infty) or (x = -infty & y = -infty)) &
  x - y in REAL) implies (x in REAL & y in REAL)
proof
  assume
A1: (not ((x = +infty & y = +infty) or (x = -infty & y = -infty
  )) & x - y in REAL);
  assume
A2: not (x in REAL & y in REAL);
  (x in REAL or x = -infty or x = +infty) &
  (y in REAL or y = -infty or y = +infty) by XXREAL_0:14;
  hence thesis by A1,A2,Th16,Th29;
end;

theorem Th44:
  x is real implies y - x + x = y & y + x - x = y
proof
  assume
A1:  x is real;
  per cases by A1,XXREAL_0:14;
  suppose x in REAL & y in REAL;
    then reconsider a = x, b = y as real number;
A3: y - x + x = (b - a) + a
      .= y;
    (y + x) - x = b + a - a
      .= y;
    hence thesis by A3;
  end;
  suppose
A4: x in REAL & y = -infty;
    then y - x = -infty & y + x = -infty by Def3;
    hence thesis by A4;
  end;
  suppose
A5: x in REAL & y = +infty;
    then y - x = +infty & y + x = +infty by Def3;
    hence thesis by A5;
  end;
end;

theorem Th58:
  (x = +infty iff -x = -infty) & (x = -infty iff -x = +infty)
proof
  -x = +infty implies x = -infty
  proof
    assume -x = +infty;
    then --x = -infty by Def5;
    hence thesis;
  end;
  hence thesis by Tx4A;
end;

theorem
  z is real implies x = x + z - z
proof
  assume that
A1: z is real;
  per cases by XXREAL_0:14;
  suppose
A4: x = +infty;
    then x + z = +infty by A1,Def3;
    hence thesis by A1,A4,Th16;
  end;
  suppose
A5: x = -infty;
    then x + z = -infty by A1,Def3;
    hence thesis by A1,A5,Th29;
  end;
  suppose x is Element of REAL;
    then reconsider a = x, c = z as real number by A1;
    x + z - z = a + c - c;
    hence thesis;
  end;
end;

Lm3: -+infty = -infty by Def5;

Lm6: x in REAL implies -(x + -infty) = --infty + -x
proof
  assume
A1: x in REAL;
A2: --infty = +infty by Th58;
    x + -infty = -infty by A1,Def3;
  hence thesis by A2,A1,Def3;
end;

canceled;

theorem Th14:
  -(x + y) = -y - x
proof per cases by XXREAL_0:14;
  suppose
A1: x = +infty & y = +infty;
    hence -(x + y) = -+infty by Def3
      .= -y - x by A1,Lm3,Def3;
  end;
  suppose x = +infty & y = -infty;
    hence thesis by Lm4;
  end;
  suppose x = +infty & y in REAL;
    hence thesis by Lm5;
  end;
  suppose x = -infty & y = +infty;
    hence thesis by Lm4;
  end;
  suppose
A2: x = -infty & y = -infty;
    hence -(x + y) = --infty by Def3
      .= -y - x by A2,Tx4A,Def3;
  end;
  suppose x = -infty & y in REAL;
    hence thesis by Lm6;
  end;
  suppose x in REAL & y = +infty;
    hence thesis by Lm5;
  end;
  suppose x in REAL & y = -infty;
    hence thesis by Lm6;
  end;
  suppose x in REAL & y in REAL;
    then reconsider a=x, b=y as real number;
     -(x + y) = -(a + b)
      .= -a + - b;
    hence thesis;
  end;
end;

theorem
  -(x - y) = -x + y & -(x - y) = y - x
proof
  -(x - y) = -(-y) - x by Th14;
  hence thesis;
end;

theorem Th30:
  -(-x + y) = x - y & -(-x + y) = x + -y
proof
  -(-x + y) = -(-x) - y by Th14
    .= x - y;
  hence thesis;
end;

theorem
  0<= y & y < +infty implies z = z + y - y
proof
  assume 0 <= y & y < +infty;
  then y in REAL by XXREAL_0:14;
  hence thesis by Th44;
end;

theorem Th59:
  not (x = +infty & y = -infty) & not (x = -infty & y = +infty) &
  not ((y = +infty & z = -infty) or (y = -infty & z = +infty)) &
  not ((x = +infty & z = -infty) or (x = -infty & z = +infty)) implies
  x + y + z = x + (y + z)
proof
  assume that
A1: not (x = +infty & y = -infty or x = -infty & y = +infty) and
A2: not ((y = +infty & z = -infty) or (y = -infty & z = +infty)) and
A3: not ((x = +infty & z = -infty) or (x = -infty & z = +infty));
  per cases by A1,A2,A3,XXREAL_0:14;
    suppose x in REAL & y in REAL & z in REAL;
      then reconsider A = x, B = y, C = z as real number;
      A + B + C = x + y + z & A + (B + C) = x + (y + z);
      hence thesis;
    end;
    suppose
A4:   x in REAL & y = +infty & z in REAL;
      then x + y = +infty & y + z = +infty by Def3;
      hence thesis by A4;
    end;
    suppose
A5:   x in REAL & y = -infty & z in REAL;
      then x + y = -infty & y + z = -infty by Def3;
      hence thesis by A5;
    end;
    suppose
A6:   x = -infty & y in REAL & z in REAL;
      then x + y = -infty by Def3;
      then x + y + z = -infty by A6,Def3;
      hence thesis by A6,Def3;
    end;
    suppose
A8:   x = +infty & y in REAL & z in REAL;
      then x + y = +infty by Def3;
      then x + y + z = +infty by A8,Def3;
      hence thesis by A8,Def3;
    end;
    suppose
A10:  x in REAL & y in REAL & z = +infty;
      then y + z = +infty by Def3;
      then x + (y + z) = +infty by A10,Def3;
      hence thesis by A10,Def3;
    end;
    suppose
A12:  x in REAL & y in REAL & z = -infty;
      then y + z = -infty by Def3;
      then x + (y + z) = -infty by A12,Def3;
      hence thesis by A12,Def3;
    end;
    suppose
A14:  x = +infty & y = +infty & z in REAL;
      then x + y = +infty by Def3;
      then
A15:  x + y + z = +infty by A14,Def3;
      y + z <> -infty by A14,Def3;
      hence thesis by A14,A15,Def3;
    end;
    suppose
A16:  x in REAL & y = -infty & z = -infty;
      then
A17:  x + y = -infty by Def3;
      then x + y + z = -infty by A16,Def3;
      hence thesis by A16,A17;
    end;
    suppose
A18:  x = -infty & y = -infty & z in REAL;
      then
A19:  x + y = -infty by Def3;
      then x + y + z = -infty by A18,Def3;
      hence thesis by A18,A19;
    end;
    suppose x = -infty & y in REAL & z = -infty or
      x = -infty & y = -infty & z = -infty or
      x = +infty & y in REAL & z = +infty or
      x = +infty & y = +infty & z = +infty;
      hence thesis;
    end;
    suppose
A20:  x in REAL & y = +infty & z = +infty;
      then x + y = +infty & y + z = +infty by Def3;
      hence thesis by A20;
    end;
end;

theorem Th60:
  not (x = +infty & y = -infty) & not (x = -infty & y = +infty) &
  not (y = +infty & z = +infty) & not (y = -infty & z = -infty) &
  not (x = +infty & z = +infty) & not (x = -infty & z = -infty) implies
  x + y - z = x + (y - z)
proof
  assume
A1: not (x = +infty & y = -infty) & not (x = -infty & y = +infty) &
  not (y = +infty & z = +infty) & not (y = -infty & z = -infty) &
  not (x = +infty & z = +infty) & not (x = -infty & z = -infty);
  then
A2: not (y = +infty & -z = -infty) & not (y = -infty & -z = +infty) &
  not (x = +infty & -z = -infty) & not (x = -infty & -z = +infty) by Th58;
  thus thesis by A1,A2,Th59;
end;

theorem  :: extension of REAL_1:27
  not (x = +infty & y = +infty) & not (x = -infty & y = -infty) &
  not (y = +infty & z = -infty) & not (y = -infty & z = +infty) &
  not (x = +infty & z = +infty) & not (x = -infty & z = -infty)
  implies x - y - z = x - (y + z)
proof
  assume that
A1: not(x=+infty & y=+infty) and
A2: not(x=-infty & y=-infty) and
A3: not (y = +infty & z = -infty) and
A4: not (y = -infty & z = +infty) and
A5: not (x = +infty & z = +infty) and
A6: not (x = -infty & z = -infty);
  per cases;
  suppose
A7: x = +infty;
    then x - y = +infty by A1,Th16;
    then
A8: x - y - z = +infty by A5,A7,Th16;
    y + z <> +infty by A1,A5,A7,LTh8;
    hence thesis by A7,A8,Th16;
  end;
  suppose
A9: x = -infty;
    then x - y = -infty by A2,Th29;
    then
A10: x - y - z = -infty by A6,A9,Th29;
    y + z <> -infty by A2,A6,A9,LTh9;
    hence thesis by A9,A10,Th29;
  end;
  suppose
A11: x <> +infty & x <> -infty;
    then x in REAL by XXREAL_0:14;
    then reconsider a = x as real number;
    per cases;
      suppose
A12:    y = +infty;
        then x - y = -infty & y + z = +infty
         by A3,A11,Th16,Def3;
        hence thesis by A3,A12,Th29;
      end;
      suppose
A13:    y = -infty;
        then x - y = +infty & y + z = -infty
         by A4,A11,Th29,Def3;
        hence thesis by A4,A13,Th16;
      end;
      suppose y <> +infty & y <> -infty;
    then y in REAL by XXREAL_0:14;
        then reconsider b = y as real number;
A14:    x - y = a - b;
        per cases;
          suppose z = +infty;
            then x - y - z = -infty & y + z = +infty
             by A14,Th16,Def3;
            hence thesis by A11,Th16;
          end;
          suppose z = -infty;
            then x - y - z = +infty & y + z = -infty
             by A14,Th29,Def3;
            hence thesis by A11,Th29;
          end;
          suppose z <> +infty & z <> -infty;
    then z in REAL by XXREAL_0:14;
            then reconsider c = z as real number;
A15:        x - y - z = a - b - c & y + z = b + c;
             x - (y + z) = a - (b + c);
            hence thesis by A15;
          end;
      end;
  end;
end;

theorem  Th109:
  not (x = +infty & y = +infty) & not (x = -infty & y = -infty) &
  not (y = +infty & z = +infty) & not (y = -infty & z = -infty) &
  not (x = +infty & z = -infty) & not (x = -infty & z = +infty)
  implies x - y + z = x - (y - z)
proof
  assume that
A1: not(x=+infty & y=+infty) and
A2: not(x=-infty & y=-infty) and
A3: not (y = +infty & z = +infty) and
A4: not (y = -infty & z = -infty) and
A5: not (x = +infty & z = -infty) and
A6: not (x = -infty & z = +infty);
  per cases;
  suppose
A7: x = +infty;
    then x - y = +infty by A1,Th16;
    then
A8: x - y + z = +infty by A5,A7,Def3;
    y - z <> +infty by A1,A5,A7,Th32;
    hence thesis by A7,A8,Th16;
  end;
  suppose
A9: x = -infty;
    then x - y = -infty by A2,Th29;
    then
A10: x - y + z = -infty by A6,A9,Def3;
    y - z <> -infty by A2,A6,A9,Th33;
    hence thesis by A9,A10,Th29;
  end;
  suppose
A11: x <> +infty & x <> -infty;
    then x in REAL by XXREAL_0:14;
    then reconsider a = x as real number;
    per cases;
      suppose
A12:    y = +infty;
        then x - y = -infty & y - z = +infty by A3,A11,Th16;
        hence thesis by A3,A12,Def3;
      end;
      suppose
A13:    y = -infty;
        then x - y = +infty & y - z = -infty by A4,A11,Th29;
        hence thesis by A4,A13,Def3;
      end;
      suppose y <> +infty & y <> -infty;
    then y in REAL by XXREAL_0:14;
        then reconsider b = y as real number;
A14:    x - y = a - b;
        per cases;
          suppose z = +infty;
            then x - y + z = +infty & y - z = -infty
             by A14,Th16,Def3;
            hence thesis by A11,Th29;
          end;
          suppose z = -infty;
            then x - y + z = -infty & y - z = +infty
             by A14,Th29,Def3;
            hence thesis by A11,Th16;
          end;
          suppose z <> +infty & z <> -infty;
            then z in REAL by XXREAL_0:14;
            then reconsider c = z as real number;
A15:        x - y + z = a - b + c & y - z = b - c;
            x - (y - z) = a - (b - c);
            hence thesis by A15;
          end;
      end;
  end;
end;

theorem Th22:
  z is real implies (z + x) - (z + y) = x - y
proof
  assume
A1: z is real;
  per cases by A1,XXREAL_0:14;
  suppose
S:  x = -infty;
   per cases by XXREAL_0:14;
   suppose
SS:    y = -infty;
    (z + -infty) - (z + -infty) = -infty - (z + -infty)
     by A1,Def3
       .= -infty - -infty by A1,Def3;
   hence thesis by S,SS;
   end;
   suppose
SS:   y = +infty;
    (z + -infty) - (z + +infty) = -infty - (z + +infty)
     by A1,Def3
       .= -infty - +infty by A1,Def3;
   hence thesis by S,SS;
   end;
   suppose
SS:   y in REAL;
    then consider a,b being complex number such that
TT:   z = a & y = b & z+y = a + b by A1,Def3;
     reconsider a,b as real number by A1,SS,TT;
YY:  a+b in REAL by XREAL_0:def 1;
    (z + -infty) - (z + y) = -infty - (z + y) by A1,Def3
       .= -infty by YY,Def3,TT
       .= -infty - y by SS,Def3;
   hence thesis by S;
   end;
  end;
  suppose
S: y = +infty;
   per cases by XXREAL_0:14;
   suppose
SS:    x = -infty;
    (z + -infty) - (z + +infty) = z + -infty - +infty
     by A1,Def3
               .= -infty - +infty by A1,Def3;
   hence thesis by S,SS;
   end;
   suppose
SS:   x = +infty;
    (z + +infty) - (z + +infty) = z + +infty - +infty
     by A1,Def3
               .= +infty - +infty by A1,Def3;
   hence thesis by S,SS;
   end;
   suppose
SS:   x in REAL;
    then consider a,b being complex number such that
TT:  z = a & x = b & z+x = a + b by A1,Def3;
A:  - +infty = -infty by Def5;
    reconsider a,b as real number by A1,SS,TT;
VV: a+b in REAL by XREAL_0:def 1;
    (z + x) - (z + +infty) = z + x - +infty by A1,Def3
       .= z + x + -infty by Def5
       .= -infty by VV,TT,Def3
       .= x + - +infty by SS,A,Def3;
   hence thesis by S;
   end;
  end;
  suppose x in REAL & y in REAL & z in REAL;
    then reconsider a = x, b = y, c = z as real number;
    (z + x) - (z + y) = (a + c) - (c + b)
      .= a - b
      .= x - y;
    hence thesis;
  end;
  suppose
A4: x = +infty & y in REAL & z in REAL;
    then reconsider c = z, b = y as real number;
A5: z + y = c + b;
    z + x = +infty & x - y = +infty by A4,Def3;
    hence thesis by A5,Th16;
  end;
  suppose
A6: x in REAL & y = -infty & z in REAL;
    then reconsider c = z, a = x as real number;
A7: z + x = c + a;
    z + y = -infty by A6,Def3;
    then (z + x) - (z + y) = +infty by A7,Th29
      .= x - y by A6,Th29;
    hence thesis;
  end;
  suppose
A8: x = +infty & y = -infty & z in REAL;
    then z + y = -infty & not z + x = -infty by Def3;
    then (z + x) - (z + y) = +infty by Th29
      .= x - y by A8,Th29;
    hence thesis;
  end;
end;

theorem
  y is real implies (z - y) + (y - x) = z - x
proof
  assume
Z:  y is real;
  thus (z - y) + (y - x) = (z - y) - (x - y) by Th30
      .= z - x by Z,Th22;
end;

begin :: addition & order

Lm5E: x <= y implies x + z <= y + z
proof
 assume
Z: x <= y;
 per cases by XXREAL_0:14;
 suppose
T: x in REAL & y in REAL;
  per cases by XXREAL_0:14;
  suppose
S:  z = -infty;
    -infty <= y + z by XXREAL_0:5;
  hence thesis by S,T,Def3;
  end;
  suppose z in REAL;
   then reconsider x, y, z as real number by T;
   x + z <= y + z by Z,XREAL_1:8;
  hence thesis;
  end;
  suppose
S:  z = +infty;
   x + z <= +infty by XXREAL_0:3;
  hence thesis by S,T,Def3;
  end;
 end;
 suppose
T: x in REAL & y = +infty;
 per cases by XXREAL_0:14;
 suppose
S: z = -infty;
  -infty <= y+z by XXREAL_0:5;
 hence thesis by S,T,Def3;
 end;
 suppose
S:  z in REAL;
   x + z <= +infty by XXREAL_0:3;
 hence thesis by S,T,Def3;
 end;
 suppose z = +infty;
 hence thesis by T,Lm5C,XXREAL_0:3;
 end;
 end;
 suppose
T: x = -infty & y in REAL;
 per cases by XXREAL_0:14;
 suppose z = -infty;
 hence thesis by T,Lm5D,XXREAL_0:5;
 end;
 suppose
S:  z in REAL;
   -infty <= y + z by XXREAL_0:5;
 hence thesis by S,T,Def3;
 end;
 suppose
S: z = +infty;
   x + z <= +infty by XXREAL_0:3;
 hence thesis by S,T,Def3;
 end;
 end;
 suppose
T: x = -infty & y = +infty;
 per cases by XXREAL_0:14;
 suppose z = -infty;
 hence thesis by T,Lm5D;
 end;
 suppose
S:  z in REAL;
   -infty <= y + z by XXREAL_0:5;
 hence thesis by S,T,Def3;
 end;
 suppose z = +infty;
 hence thesis by T,Lm5C;
 end;
 end;
 suppose x = +infty;
  hence thesis by Z,XXREAL_0:4;
 end;
 suppose y = -infty;
  hence thesis by Z,XXREAL_0:6;
 end;
end;

Lm16: x >= 0 & y > 0 implies x + y > 0
proof
  assume x >= 0;
  then x + y >= 0 + y by Lm5E;
  hence thesis by Tx3;
end;

Lm17: x <= 0 & y < 0 implies x + y < 0
proof
  assume x <= 0;
  then x + y <= 0 + y by Lm5E;
  hence thesis by Tx3;
end;

registration
  let x,y be non negative (ext-real number);
  cluster x + y -> non negative;
  coherence
  proof
    per cases;
    suppose x = 0;
      hence x + y >= 0 by Tx3;
    end;
    suppose x > 0;
      hence x+y >= 0 by Lm16;
    end;
  end;
end;

registration
  let x,y be non positive (ext-real number);
  cluster x + y -> non positive;
  coherence
  proof
    per cases;
    suppose x = 0;
      hence x + y <= 0 by Tx3;
    end;
    suppose x < 0;
      hence x + y <= 0 by Lm17;
    end;
  end;
end;

registration
  let x be positive (ext-real number);
  let y be non negative (ext-real number);
  cluster x + y -> positive;
  coherence by Lm16;
  cluster y + x -> positive;
  coherence;
end;

registration
  let x be negative (ext-real number);
  let y be non positive (ext-real number);
  cluster x + y -> negative;
  coherence by Lm17;
  cluster y + x -> negative;
  coherence;
end;

registration
  let x be non positive (ext-real number);
  cluster -x -> non negative;
  coherence
  proof
    assume -x < 0;
    then -x - -x < 0;
    hence contradiction by Tx4;
  end;
end;

registration
  let x be non negative (ext-real number);
  cluster -x -> non positive;
  coherence
  proof
    assume -x > 0;
    then -x - -x > 0;
    hence contradiction by Tx4;
  end;
end;

registration
  let x be positive (ext-real number);
  cluster -x -> negative;
  coherence
  proof
    assume -x >= 0;
    then -x- -x > 0;
    hence contradiction by Tx4;
  end;
end;

registration
  let x be negative (ext-real number);
  cluster -x -> positive;
  coherence
  proof
    assume -x <= 0;
    then -x- -x < 0;
    hence contradiction by Tx4;
  end;
end;

registration
  let x be non negative (ext-real number), y be non positive (ext-real number);
  cluster x - y -> non negative;
  coherence;
  cluster y - x -> non positive;
  coherence;
end;

registration
  let x be positive (ext-real number);
  let y be non positive (ext-real number);
  cluster x - y -> positive;
  coherence;
  cluster y - x -> negative;
  coherence;
end;

registration
  let x be negative (ext-real number);
  let y be non negative (ext-real number);
  cluster x - y -> negative;
  coherence;
  cluster y - x -> positive;
  coherence;
end;

Lm3: for x,y  st x <= y holds - y <= - x
proof
  let x,y;
  assume
A1: x <= y;
  per cases;
  case that
A2: -y in REAL and
A3: -x in REAL;
    y in REAL by A2,Lm2;
    then consider b being complex number such that
A4: y = b and
A5: -y = - b by Def5;
    x in REAL by A3,Lm2;
    then consider a being complex number such that
A6: x = a and
A7: -x = - a by Def5;
    x in REAL & y in REAL by A2,A3,Lm2;
    then reconsider a,b as real number by A4,A6;
    reconsider mb=-b, ma=-a as Element of REAL by XREAL_0:def 1;
    take mb,ma;
    thus mb = -y & ma = -x by A5,A7;
    thus thesis by A1,A4,A6,XREAL_1:26;
  end;
  case not(-y in REAL & -x in REAL);
    then not(y in REAL & x in REAL) by Lm2;
    then x = -infty or y = +infty by A1,Def1;
    hence thesis by Def5;
  end;
end;

theorem
  x <= y implies x + z <= y + z by Lm5E;

canceled;

theorem Th36:
  x <= y & z <= w implies x + z <= y + w
proof
  assume
A1: x <= y & z <= w;
  per cases;
  suppose
S: x = +infty & z = -infty;
    then
A:   y = +infty by A1,XXREAL_0:4;
    w <> -infty implies +infty + w = +infty by Def3;
   hence thesis by A,S;
  end;
  suppose
S: x = -infty & z = +infty;
    then
A:   w = +infty by A1,XXREAL_0:4;
    y <> -infty implies +infty + y = +infty by Def3;
   hence thesis by A,S;
  end;
  suppose
S: y = +infty & w = -infty;
    then
A:   z = -infty by A1,XXREAL_0:6;
    x <> +infty implies -infty + x = -infty by Def3;
   hence thesis by A,S;
  end;
  suppose
S: y = -infty & w = +infty;
    then
A:   x = -infty by A1,XXREAL_0:6;
    z <> +infty implies -infty + z = -infty by Def3;
   hence thesis by A,S;
  end;
  suppose
S: not ((x = +infty & z = -infty) or (x = -infty & z = +infty)) &
   not ((y = +infty & w = -infty) or (y = -infty & w = +infty));
  reconsider a = x + z, b = y + w as Element of ExtREAL by XXREAL_0:def 1;
A2: (a in REAL & b in REAL) or (a in REAL & b = +infty) or
  (a in REAL & b = -infty) or
  (a = +infty & b in REAL) or (a = +infty & b = +infty) or (a = +infty & b =
  -infty) or
  (a = -infty & b in REAL) or (a = -infty & b = +infty) or (a = -infty & b =
  -infty) by XXREAL_0:14;
A3: (a in REAL & b in REAL) implies a <= b
  proof
    assume a in REAL & b in REAL;
    then x in REAL & y in REAL & z in REAL & w in REAL by Th34,S;
    then consider Ox,Oy,Os,Ot being real number such that
A4: Ox = x & Oy = y & Os = z & Ot = w & Ox <= Oy & Os <= Ot by A1;
A5: Ox + Os <= Os + Oy by A4,XREAL_1:8;
    Os + Oy <= Ot + Oy by A4,XREAL_1:8;
    hence thesis by A5,A4,XXREAL_0:2;
  end;
A7: a in REAL & b = -infty implies a <= b
  proof
    assume a in REAL & b = -infty;
    then y = -infty or w = -infty by LTh9;
    then x = -infty or z = -infty by A1,XXREAL_0:6;
    then a = -infty by Def3,S;
    hence thesis by XXREAL_0:5;
  end;
A8: (a = +infty & b in REAL) implies a <= b
  proof
    assume a = +infty & b in REAL;
    then x = +infty or z = +infty by LTh8;
    then y = +infty or w = +infty by A1,XXREAL_0:4;
    then b = +infty by Def3,S;
    hence thesis by XXREAL_0:4;
  end;
  not(a = +infty & b = -infty)
  proof
    assume
A9: a = +infty & b = -infty;
    x = +infty or z = +infty by A9,LTh8;
    hence thesis by A1,A9,S,XXREAL_0:4,LTh9;
  end;
  hence thesis by A2,A3,A7,A8,XXREAL_0:4,5;
 end;
end;

theorem Th37:
  x <= y & z <= w implies x - w <= y - z
proof
  assume
A1: x <= y & z <= w;
   then -w <= -z by Lm3;
  hence thesis by A1,Th36;
end;

theorem Th38:
   x <= y iff - y <= - x
proof
  thus x <= y implies - y <= - x by Lm3;
  - y <= - x implies --x <= --y by Lm3;
  hence thesis;
end;

canceled;

theorem Th28:
  0 <= z implies x <= x + z
proof
  assume 0 <= z;
   then x + 0 <= x + z by Th36;
  hence thesis by Tx3;
end;

theorem
 x <= y implies y-x >= 0
 proof
  assume x <= y;
   then -y <= -x by Lm3;
   then -y +y <= y + -x by Th36;
  hence thesis by Tx4;
 end;

theorem
  (z = -infty & y = +infty implies x <= 0) &
  (z = +infty & y = -infty implies x <= 0) implies
  (x - y <= z implies x <= z + y)
proof assume that
Z1: z = -infty & y = +infty implies x <= 0 and
Z2: z = +infty & y = -infty implies x <= 0 and
Z3: x - y <= z;
 per cases by XXREAL_0:14;
 suppose
S:  z = -infty;
   per cases by S,Th33,Z3,XXREAL_0:6;
   suppose x = -infty;
   hence thesis by XXREAL_0:5;
   end;
   suppose y = +infty;
   hence thesis by Z1,S;
   end;
 end;
 suppose
S:  z = +infty;
  per cases;
  suppose y = -infty;
   hence thesis by Z2,S;
  end;
  suppose y <> -infty;
    then z + y = +infty by S,Def3;
   hence thesis by XXREAL_0:3;
  end;
 end;
 suppose
S: z in REAL;
  per cases by Z3,S,XXREAL_0:13;
  suppose
SS:  x-y in REAL;
   per cases by SS,Th35;
   suppose y = +infty;
    then z + y = +infty by S,Def3;
   hence thesis by XXREAL_0:3;
   end;
   suppose x = -infty;
   hence thesis by XXREAL_0:5;
   end;
   suppose that
S1: x in REAL and
S2: y in REAL;
    reconsider a = x, b = y, c = z as Element of REAL by S,S1,S2;
    a - b <= c by Z3;
    then a <= b + c by XREAL_1:22;
   hence thesis;
   end;
  end;
  suppose
SS:  x-y = -infty;
   per cases by SS,Th33;
   suppose x = -infty;
   hence thesis by XXREAL_0:5;
   end;
   suppose y = +infty;
    then z + y = +infty by S,Def3;
   hence thesis by XXREAL_0:3;
   end;
  end;
 end;
end;

theorem
  (x = +infty & y = +infty implies 0 <= z) &
  (x = -infty & y = -infty implies 0 <= z) implies
  (x <= z + y implies x - y <= z)
proof assume that
Z1: x = +infty & y = +infty implies 0 <= z and
Z2: x = -infty & y = -infty implies 0 <= z and
Z3: x <= z + y;
 per cases by XXREAL_0:14;
 suppose
S:  x = +infty;
   per cases by S,LTh8,Z3,XXREAL_0:4;
   suppose z = +infty;
   hence thesis by XXREAL_0:3;
   end;
   suppose y = +infty;
   hence thesis by Z1,S,Tx4;
   end;
 end;
 suppose
S:  x = -infty;
  per cases;
  suppose y = -infty;
   hence thesis by Z2,S,Tx4;
  end;
  suppose --y <> -infty;
    then -y <> +infty by Def5;
    then x - y = -infty by S,Def3;
   hence thesis by XXREAL_0:5;
  end;
 end;
 suppose
S: x in REAL;
  per cases by Z3,S,XXREAL_0:10;
  suppose
SS:  z + y in REAL;
   per cases by SS,Th34;
   suppose y = +infty;
    then x - y = -infty by S,Def3,Tx4A;
   hence thesis by XXREAL_0:5;
   end;
   suppose z = +infty;
   hence thesis by XXREAL_0:3;
   end;
   suppose that
S1: z in REAL and
S2: y in REAL;
    reconsider a = x, b = y, c = z as Element of REAL by S,S1,S2;
    a <= b + c by Z3;
    then a - b <= c by XREAL_1:22;
   hence thesis;
   end;
  end;
  suppose
SS:  z + y = +infty;
   per cases by SS,LTh8;
   suppose z = +infty;
   hence thesis by XXREAL_0:3;
   end;
   suppose y = +infty;
    then x - y = -infty by S,Def3,Tx4A;
   hence thesis by XXREAL_0:5;
   end;
  end;
 end;
end;

canceled;

theorem Th47:
  z in REAL & x < y implies x + z < y + z & x - z < y - z
proof
  assume
A1: z in REAL & x < y;
  then
A2: x + z <= y + z & x - z <= y - z by Th36;
A3: x + z <> y + z
  proof
    assume x + z = y + z;
    then x = (y + z) - z by A1,Th44
      .= y by A1,Th44;
    hence thesis by A1;
  end;
  x - z <> y - z
  proof
    assume x - z = y - z;
    then x = (y - z) + z by A1,Th44
      .= y by A1,Th44;
    hence thesis by A1;
  end;
  hence thesis by A2,A3,XXREAL_0:1;
end;

theorem
  0<= x & 0<= y & 0<= z implies (x + y) + z = x + (y + z)
proof
  assume
Z: 0<= x & 0<= y & 0<= z;
  per cases by Z,XXREAL_0:14;
  suppose
A3: x in REAL & y in REAL & z in REAL;
    consider a,b,c,d,e being real number such that
A4: x = a & y = b & z = c & x + y = d & y + z = e by A3;
    (x + y) + z = (a + b) + c by A4
      .= a + (b + c)
      .= x + (y + z) by A4;
    hence thesis;
  end;
  suppose
A5: x = +infty;
    (x + y) + z = +infty + z by A5,Def3,Z
      .= +infty by Def3,Z
      .= +infty + (y + z) by Z,Def3;
    hence thesis by A5;
  end;
  suppose
A7: y = +infty;
    then (x + y) + z = +infty + z by Def3,Z
      .= +infty by Def3,Z
      .= x + (+infty) by Def3,Z
      .= x + (+infty + z) by Def3,Z;
    hence thesis by A7;
  end;
  suppose
A8: z = +infty;
    (x + y) + z = +infty by A8,Def3,Z
      .= x + (+infty) by Def3,Z
      .= x + (y + (+infty)) by Def3,Z;
    hence thesis by A8;
  end;
end;

theorem MEA2:
  x is real implies (y + x <= z iff y <= z - x)
proof
  assume
A1: x is real;
  hereby
    assume
A3: y + x <= z;
    (y + x) - x = y
    proof
      per cases by XXREAL_0:14;
      suppose y in REAL;
      then reconsider a = x, b = y as Element of REAL by A1,XXREAL_0:14;
        (y + x) - x = (b + a) - a
          .= y;
        hence thesis;
      end;
      suppose
A6:     y = -infty or y = +infty;
        per cases by A6;
          suppose
A7:         y = -infty;
            hence (y + x) - x = -infty - x by A1,Def3
              .= y by A1,A7,Th29;
          end;
          suppose
A8:         y = +infty;
            hence (y + x) - x = +infty - x by A1,Def3
              .= y by A1,A8,Th16;
          end;
      end;
    end;
    hence y <= z - x by A3,Th37;
  end;
  assume
A9: y <= z - x;
  (z - x) + x = z
  proof
    per cases by XXREAL_0:14;
    suppose z in REAL;
      then reconsider a = x, b = z as real number by A1;
      thus (z - x) + x = (b - a) + a
        .= z;
    end;
    suppose
A12:  z = -infty;
      hence (z - x) + x = -infty + x by A1,Th29
        .= z by A1,A12,Def3;
    end;
    suppose
A13:  z = +infty;
      hence (z - x) + x = +infty + x by A1,Th16
        .= z by A1,A13,Def3;
    end;
  end;
  hence thesis by A9,Th36;
end;

theorem Th19:
   0 < x & x < y implies 0 < y - x
proof
  assume
A1: 0 < x & x < y;
  then
A2: x in REAL by XXREAL_0:48;
B3: 0 + x < y by A1,Tx3;
  0 <> y - x
  proof
    assume 0= y - x;
    then x = (y - x) + x by Tx3
      .= y by A2,Th44;
    hence thesis by A1;
  end;
  hence thesis by B3,A2,MEA2;
end;

theorem
  0 <= x & 0 <= z & z + x < y implies z < y - x
proof
  assume
A1: 0 <= x & 0 <= z & z + x < y;
A2: x in REAL
  proof
    assume
A3: not x in REAL;
    x <> +infty
    proof
      assume x = +infty;
      then +infty < y by A1,Def3;
      hence thesis by XXREAL_0:4;
    end;
    hence thesis by A1,A3,XXREAL_0:10;
  end;
  then
A5: z <= y - x by A1,MEA2;
  z <> y - x by A1,A2,Th44;
  hence thesis by A5,XXREAL_0:1;
end;

canceled;

theorem
 0 <= x & 0 <= z & z + x < y implies z <= y
proof
  assume
A1: 0 <= x & 0 <= z & z + x < y;
    x in REAL
  proof
    assume
A3: not x in REAL;
    x <> +infty
    proof
      assume x = +infty;
      then +infty < y by A1,Def3;
      hence thesis by XXREAL_0:4;
    end;
    hence thesis by A1,A3,XXREAL_0:10;
  end;
  then (z + x) - x = z & y - 0 = y by Th44,Th21;
  hence thesis by A1,Th37;
end;

canceled;

theorem Th43:
  0 <= x & x < z implies ex y being real number st
  0 < y & x + y < z
proof
  assume
A1: 0 <= x & x < z;
  per cases by A1;
  suppose
S:  0 < x;
  then
A2: x in REAL by A1,XXREAL_0:48;
  0 < z - x by A1,Th19,S;
  then consider y being real number such that
A3: 0 < y & y < z - x by Th23;
  take y;
A4: x + y <= x + (z - x) by A3,Th36;
A5: x + (z - x) = z by A2,Th44;
    x + y <> z by A2,A3,Th44;
  hence thesis by A3,A4,A5,XXREAL_0:1;
  end;
  suppose
A2: 0 = x;
    consider y being real number such that
A3: 0 < y & y < z by A1,Th23;
    take y;
    thus thesis by A2,A3,Tx3;
  end;
end;

theorem
  0 < x implies ex y being real number st 0 < y & y + y < x
proof
  assume 0 < x;
  then consider x1 being real number such that
A1: 0 < x1 & x1 < x by Th23;
  consider x2 being real number such that
A2: 0 < x2 & x1 + x2 < x by A1,Th43;
  take y = min(x1,x2);
  per cases;
  suppose
A3: x1 <= x2;
    then
A4: y = x1 by XXREAL_0:def 9;
    thus 0 < y by A1,A3,XXREAL_0:def 9;
    y + y <= x1 + x2 & x1 + x2 <= x by A2,A3,A4,Th36;
    hence thesis by A2,XXREAL_0:2;
  end;
  suppose
A5: x2 <= x1;
    then
A6: y = x2 by XXREAL_0:def 9;
    thus 0 < y by A2,A5,XXREAL_0:def 9;
    y + y <= x1 + x2 & x1 + x2 <= x by A2,A5,A6,Th36;
    hence thesis by A2,XXREAL_0:2;
  end;
end;

theorem Th64:
  x < y & x < +infty & -infty < y implies 0 < y- x
proof
  assume that
A1: x < y and
A2: x < +infty & -infty < y;
  per cases;
  suppose y = +infty;
    hence thesis by A2,Th16;
  end;
  suppose
A3: y <> +infty;
    per cases;
      suppose x = -infty;
        hence thesis by A2,Th29;
      end;
      suppose
A4:     x <> -infty;
        x in REAL & y in REAL by A2,A3,A4,XXREAL_0:14;
        then reconsider a = x, b = y as real number;
        b - a > 0 by A1,XREAL_1:52;
        hence thesis;
      end;
  end;
end;

canceled 5;

theorem
  not ((x = +infty & y = -infty) or (x = -infty & y = +infty)) &
  x + y < z implies x <> +infty & y <> +infty & z <> -infty & x < z - y
proof
  assume that
A1: not ((x=+infty & y=-infty) or (x=-infty & y=+infty)) and
A2: x + y < z;
  per cases;
  suppose
A3: z = +infty;
    then x<>+infty & y<>+infty & z<>-infty by A1,A2,Def3;
    then x < +infty & z - y = +infty by A3,XXREAL_0:4,Th16;
    hence thesis by A1,A2,A3,Def3;
  end;
  suppose
A4: z <> +infty;
    x + y < +infty by A2,XXREAL_0:2,4;
    then
A5: x <> +infty & y <> +infty by A1,Def3;
A6: -infty <= x + y by XXREAL_0:5;
    then z in REAL by A2,A4,XXREAL_0:14;
    then reconsider c = z as real number;
    per cases;
      suppose
A7:     y = -infty;
        then x < +infty by A1,XXREAL_0:4;
        hence thesis by A2,A6,A7,Th29;
      end;
      suppose y <> -infty;
        then y in REAL by A5,XXREAL_0:14;
        then reconsider b = y as real number;
        per cases;
          suppose
A8:         x = -infty;
X:          z - y = c - b;
            thus x <> +infty by A8;
            thus y <> +infty by X;
            thus z <> -infty by X;
             c-b in REAL by XREAL_0:def 1;
            hence thesis by A8,XXREAL_0:12;
          end;
          suppose x <> -infty;
            then x in REAL by A5,XXREAL_0:14;
            then reconsider a = x as real number;
A9:         x + y = a + b & z = c;
            a < c - b by A2,A9,XREAL_1:22;
            hence thesis;
          end;
      end;
  end;
end;

theorem
  not ((z = +infty & y = +infty) or (z = -infty & y = -infty)) &
  x < z - y implies x <> +infty & y <> +infty & z <> -infty & x + y < z
proof
  assume that
A1: not ((z=+infty & y=+infty) or (z=-infty & y=-infty)) and
A2: x < z - y;
  per cases;
  suppose
A3: x = -infty;
    then
A4: x<>+infty & y<>+infty & z<>-infty by A1,A2,Th16,Th29;
    -infty <= z by XXREAL_0:5;
    then -infty < z & x + y = -infty by A3,A4,XXREAL_0:1,Def3;
    hence thesis by A3;
  end;
  suppose
A5: x <> -infty;
    -infty <= x by XXREAL_0:5;
    then
A6: z <> -infty & y <> +infty by A1,Th16,Th29,A2;
A7: z - y <= +infty by XXREAL_0:4;
    then x in REAL by A2,A5,XXREAL_0:14;
    then reconsider a = x as real number;
    per cases;
      suppose
A8:     y = -infty;
        then z <> -infty & -infty <= z by A1,XXREAL_0:5;
        then -infty < z by XXREAL_0:1;
        hence thesis by A2,A7,A8,Def3;
      end;
      suppose y <> -infty;
        then y in REAL by A6,XXREAL_0:14;
        then reconsider b = y as real number;
        per cases;
          suppose
A9:         z = +infty;
             a+b in REAL by XREAL_0:def 1;
            hence thesis by A9,XXREAL_0:9;
          end;
          suppose z <> +infty;
            then z in REAL by A6,XXREAL_0:14;
            then reconsider c = z as real number;
            x = a & z - y = c - b;
            then a + b < c by A2,XREAL_1:22;
            hence thesis;
          end;
      end;
  end;
end;

theorem
  not ((x = +infty & y = +infty) or (x = -infty & y = -infty)) &
  x - y < z implies x <> +infty & y <> -infty & z <> -infty & x < z + y
proof
  assume that
A1: not ((x=+infty & y=+infty) or (x=-infty & y=-infty)) and
A2: x - y < z;
  per cases;
  suppose
A3: z = +infty;
    then x<>+infty & y<>-infty & z<>-infty by A1,A2,Th16,Th29;
    then x < +infty & z + y = +infty by A3,XXREAL_0:4,Def3;
    hence thesis by A3;
  end;
  suppose
A4: z <> +infty;
    x - y < +infty by A2,XXREAL_0:2,4;
    then
A5: x <> +infty & y <> -infty by A1,Th16,Th29;
A6: -infty <= x - y by XXREAL_0:5;
    then z in REAL by A2,A4,XXREAL_0:14;
    then reconsider c = z as real number;
    per cases;
      suppose
A7:     y = +infty;
        then x < +infty by A1,XXREAL_0:4;
        hence thesis by A2,A6,A7,Def3;
      end;
      suppose y <> +infty;
    then y in REAL by A5,XXREAL_0:14;
        then reconsider b = y as real number;
        per cases;
          suppose
A8:         x = -infty;
             c+b in REAL by XREAL_0:def 1;
            hence thesis by A8,XXREAL_0:12;
          end;
          suppose x <> -infty;
            then x in REAL by A5,XXREAL_0:14;
            then reconsider a = x as real number;
A9:         x - y = a - b & z = c;
            a < c + b by A2,A9,XREAL_1:21;
            hence thesis;
          end;
      end;
  end;
end;

theorem
  not ((z = +infty & y = -infty) or (z = -infty & y = +infty)) &
  x < z + y implies x <> +infty & y <> -infty & z <> -infty & x - y < z
proof
  assume that
A1: not ((z=+infty & y=-infty) or (z=-infty & y=+infty)) and
A2: x < z + y;
  per cases;
  suppose
A3: x = -infty;
    then
A4: x<>+infty & y<>-infty & z<>-infty by A1,A2,Def3;
    -infty <= z by XXREAL_0:5;
    then -infty < z & x - y = -infty by A3,A4,XXREAL_0:1,Th29;
    hence thesis by A1,A2,A3,Def3;
  end;
  suppose
A5: x <> -infty;
    -infty <= x by XXREAL_0:5;
    then
A6: z <> -infty & y <> -infty by A1,Def3,A2;
A7: z + y <= +infty by XXREAL_0:4;
    then x in REAL by A2,A5,XXREAL_0:14;
    then reconsider a = x as real number;
    per cases;
      suppose
A8:     y = +infty;
        then z <> -infty & -infty <= z by A1,XXREAL_0:5;
        then -infty < z by XXREAL_0:1;
        hence thesis by A2,A7,A8,Th16;
      end;
      suppose y <> +infty;
        then y in REAL by A6,XXREAL_0:14;
        then reconsider b = y as real number;
        per cases;
          suppose
A9:         z = +infty;
             a-b in REAL by XREAL_0:def 1;
            hence thesis by A9,XXREAL_0:9;
          end;
          suppose z <> +infty;
            then z in REAL by A6,XXREAL_0:14;
            then reconsider c = z as real number;
A10:        x = a & z + y = c + b;
            a - b < c by A2,A10,XREAL_1:21;
            hence thesis;
          end;
      end;
  end;
end;

theorem
  not ((x = +infty & y = -infty) or (x = -infty & y = +infty) or
  (y = +infty & z = +infty) or (y = -infty & z = -infty)) &
  x + y <= z implies y <> +infty & x <= z - y
proof
  assume that
A1: not ((x=+infty & y=-infty) or (x=-infty & y=+infty) or
  (y = +infty & z = +infty) or (y = -infty & z = -infty)) and
A2: x + y <= z;
  thus y <> +infty
  proof
    assume
A3: y = +infty;
    then x + y = +infty by A1,Def3;
    hence contradiction by A1,A2,A3,XXREAL_0:4;
  end;
  per cases;
  suppose z = +infty;
    then z - y = +infty by A1,Th16;
    hence thesis by XXREAL_0:4;
  end;
  suppose
A4: z <> +infty;
    then x + y < +infty by A2,XXREAL_0:2,4;
    then
A5: x <> +infty & y <> +infty by A1,Def3;
    per cases;
      suppose x = -infty;
        hence thesis by XXREAL_0:5;
      end;
      suppose x <> -infty;
        then x in REAL by A5,XXREAL_0:14;
        then reconsider a = x as real number;
        per cases;
          suppose y = -infty;
            then z - y = +infty by A1,Th29;
            hence thesis by XXREAL_0:4;
          end;
          suppose y <> -infty;
        then y in REAL by A5,XXREAL_0:14;
            then reconsider b = y as real number;
A6:         x + y = a + b;
             a+b in REAL by XREAL_0:def 1;
            then z <> -infty by A2,XXREAL_0:12;
            then z in REAL by A4,XXREAL_0:14;
            then reconsider c = z as real number;
            z = c;
            then consider p,q being real number such that
A7:         p = x + y & q = z & p <= q by A2,A6;
            a <= c - b by A6,A7,XREAL_1:21;
            hence thesis;
          end;
      end;
  end;
end;

theorem
  not (x = +infty & y = -infty) & not (x = -infty & y = +infty) &
  not (y = +infty & z = +infty) & x <= z - y implies y <> +infty & x + y <= z
proof
  assume that
A1: not (x = +infty & y = -infty) and
A2: not (x = -infty & y = +infty) and
A3: not (y = +infty & z = +infty) and
A4: x <= z - y;
   thus
A5: y <> +infty
  proof
    assume
A6: y = +infty;
    then z - y = -infty by A3,Th16;
    hence contradiction by A2,A4,A6,XXREAL_0:6;
  end;
  per cases by A5;
  suppose y = -infty;
    then x + y = -infty by A1,Def3;
    hence thesis by XXREAL_0:5;
  end;
  suppose
A7: y <> +infty & y <> -infty;
    - +infty = -infty & - -infty = +infty by Def5;
    then -y <> -infty & -y <> +infty by A7;
    then
      z - y + y = z + (-y + y) by A7,Th59
      .= z + 0 by Tx4
      .= z by Tx3;
    hence thesis by A4,Th36;
  end;
end;

theorem
  not ((x = +infty & y = +infty) or (x = -infty & y = -infty) or
  (y = +infty & z = -infty) or (y = -infty & z = +infty)) &
  x - y <= z implies y <> -infty
proof
  assume that
A1: not ((x=+infty & y=+infty) or (x=-infty & y=-infty) or
  (y = +infty & z = -infty) or (y = -infty & z = +infty)) and
A2: x - y <= z;
  thus y <> -infty
  proof
    assume
A3: y = -infty;
    then x - y = +infty by A1,Th29;
    hence contradiction by A1,A2,A3,XXREAL_0:4;
  end;
    thus thesis;
end;

theorem
  not (x = -infty & y = -infty) &
  not (y = -infty & z = +infty) & x <= z + y implies y <> -infty
proof
  assume that
A2: not(x=-infty & y=-infty) and
A3: not(y = -infty & z = +infty) and
A4: x <= z + y; thus
 y <> -infty
  proof
    assume
A6: y = -infty;
    then z + y = -infty by A3,Def3;
    hence contradiction by A2,A4,A6,XXREAL_0:6;
  end;
    thus thesis;
end;

theorem
  (x <= -y implies y <= -x) & (-x <= y implies -y <= x)
proof
  x <= -y implies --y <= -x by Th38;
  hence x <= -y implies y <= -x;
  -x <= y implies -y <= --x by Th38;
  hence thesis;
end;

theorem
  (for e be real number st 0 < e holds x < y + e) implies x <= y
proof
  assume
A1: for e be real number st 0 < e holds x < y + e;
  per cases;
  suppose
A2: y = +infty or y=-infty;
    per cases by A2;
    suppose y=+infty;
      hence thesis by XXREAL_0:4;
    end;
    suppose
A3:   y=-infty;
      x < y + 1 by A1;
      hence thesis by A3,Def3;
    end;
  end;
  suppose
A4: y <> +infty & y <>-infty;
    per cases;
    suppose
A5:   x = +infty;
      x < y+ 1 by A1;
      hence thesis by A5,XXREAL_0:4;
    end;
    suppose
A6:   x <> +infty;
      now
        assume
A7:     x <> -infty;
        assume
A8:     not x <= y;
        y <= +infty & -infty <= x by XXREAL_0:4,5;
        then y < +infty & -infty < x by A4,A7,XXREAL_0:1;
        then
A9:     0 < x- y by A8,Th64;
        x in REAL by A6,A7,XXREAL_0:14;
        then reconsider x1=x as real number;
        y in REAL by A4,XXREAL_0:14;
        then reconsider y1=y as real number;
        x < y + (x1-y1) by A1,A9;
        then x < y + x-y by Th60;
        then x < x +(y-y) by A4,Th60;
        then x < x +0 by Tx4;
        hence contradiction by Tx3;
      end;
      hence thesis by XXREAL_0:5;
    end;
  end;
end;

reserve t for ext-real number;

theorem
  t <> -infty & t <> +infty & x < y implies x + t < y + t
proof
  assume
A1: t <> -infty & t <> +infty & x < y;
  then
A2: x + t <= y + t by Th36;
A3: t-t = 0 by Tx4;
  now
    assume x + t = y + t;
    then x + (t -t) = y + t-t by A1,Th60;
    then x+ 0 = y + (t-t) by A1,A3,Th60;
    then x=y+ 0 by A3,Tx3;
    hence contradiction by A1,Tx3;
  end;
  hence thesis by A2,XXREAL_0:1;
end;

theorem
  t <> -infty & t <> +infty & x < y implies x - t < y - t
proof
  assume
A1: t <> -infty & t <> +infty & x < y;
  then
A2: x - t <= y - t by Th37;
A3: t-t = 0 by Tx4;
  now
    assume x - t = y - t;
    then x - (t -t) = y - t+t by A1,Th109;
    then x - 0 = y - (t-t) by A1,A3,Th109;
    then x= y+ 0 by A3,Tx3;
    hence contradiction by A1,Tx3;
  end;
  hence thesis by A2,XXREAL_0:1;
end;

theorem
  -infty < w & x < y & w < z implies x + w < y + z
proof
  assume
A1: -infty < w;
  assume
A2: x < y & w <z;
  per cases;
  suppose
A3: y = +infty;
    then
A4: y + z = +infty by A1,A2,Def3;
    z <= +infty by XXREAL_0:4;
    hence thesis by A2,A3,A4,XXREAL_0:4,LTh8;
  end;
  suppose
A5: y <> +infty;
    z <= +infty by XXREAL_0:4;
    then w in REAL by A1,A2,XXREAL_0:14;
    then
A6: x + w < y + w by A2,Th47;
    -infty <= x by XXREAL_0:5;
    then y in REAL by A2,A5,XXREAL_0:14;
    then y + w < y + z by A2,Th47;
    hence thesis by A6,XXREAL_0:2;
  end;
end;

theorem
   0 <= x & z + x <= y implies z <= y
proof
 assume 0 <= x;
  then
A1: z <= z + x by Th28;
 assume z + x <= y;
 hence thesis by A1,XXREAL_0:2;
end;

begin :: multiplication

definition let x,y be ext-real number;
  func x * y -> ext-real number means :Def4:
    ex a,b being complex number st x = a & y = b & it = a * b
      if x is real & y is real,
    it = +infty if (x is not real or y is not real) &
     (x is positive & y is positive or x is negative & y is negative),
    it = -infty if (x is not real or y is not real) &
     (x is positive & y is negative or x is negative & y is positive)
    otherwise it = 0;
 existence
  proof
   thus x is real & y is real implies
      ex c being ext-real number, a, b being complex number
       st x = a & y = b & c = a * b
     proof assume x is real & y is real;
       then reconsider a = x, b = y as real number;
      take c = a*b, a, b;
      thus thesis;
     end;
   thus thesis;
  end;
 uniqueness;
 consistency;
 commutativity;
end;

registration let x,y be real number, a,b be complex number;
 identify x*y with a*b when x = a, y = b;
 compatibility by Def4;
end;

definition let x be ext-real number;
  func x" -> ext-real number means :Def6:
    ex a being complex number st x = a & it = a" if x is real
     otherwise it = 0;
 existence
  proof
   thus x is real implies
    ex c being ext-real number, a being complex number st
     x = a & c = a"
     proof assume x is real;
       then reconsider a = x as real number;
      take a", a;
      thus thesis;
     end;
   thus thesis;
  end;
 uniqueness;
 consistency;
end;

registration let x be real number, a be complex number;
 identify x" with a" when x = a;
 compatibility by Def6;
end;

definition let x,y be ext-real number;
 func x / y -> ext-real number equals x * y";
 coherence;
end;

registration let x,y be real number, a,b be complex number;
 identify x/y with a/b when x = a, y = b;
 compatibility
  proof
   reconsider y1=y" as real number;
   assume x = a & y = b;
   hence a / b = x * y1
       .= x / y;
  end;
end;

Tx1:
 x * 0 = 0
 proof
  per cases by XXREAL_0:14;
  suppose x in REAL;
    then ex a,b being complex number st x = a & 0 = b & x * 0 = a * b by Def4;
   hence thesis;
  end;
  suppose x = +infty;
   hence thesis by Def4;
  end;
  suppose x = -infty;
   hence thesis by Def4;
  end;
 end;

registration
  let x be positive (ext-real number), y be negative (ext-real number);
  cluster x*y -> negative;
  coherence
  proof
   per cases;
   suppose x in REAL & y in REAL;
     then reconsider x,y as real number;
     x*y < 0;
    hence thesis;
   end;
   suppose not x in REAL or not y in REAL;
     then x is not real or y is not real by XREAL_0:def 1;
    hence thesis by Def4;
   end;
  end;
end;

registration
  let x,y be negative (ext-real number);
  cluster x*y -> positive;
  coherence
  proof
   per cases;
   suppose x in REAL & y in REAL;
     then reconsider x,y as real number;
     x*y > 0;
    hence thesis;
   end;
   suppose not x in REAL or not y in REAL;
     then x is not real or y is not real by XREAL_0:def 1;
    hence thesis by Def4;
   end;
  end;
end;

registration
  let x,y be positive (ext-real number);
  cluster x*y -> positive;
  coherence
  proof
   per cases;
   suppose x in REAL & y in REAL;
     then reconsider x,y as real number;
     x*y > 0;
    hence thesis;
   end;
   suppose not x in REAL or not y in REAL;
     then x is not real or y is not real by XREAL_0:def 1;
    hence thesis by Def4;
   end;
  end;
end;

registration
  let x be non positive (ext-real number), y be non negative (ext-real number);
  cluster x*y -> non positive;
  coherence
  proof
   per cases;
   suppose x = 0 or y = 0;
     hence x * y <= 0 by Tx1;
   end;
   suppose x < 0 & y > 0;
     hence x * y <= 0;
   end;
  end;
end;

registration
  let x,y be non positive (ext-real number);
  cluster x*y -> non negative;
  coherence
  proof
    per cases;
    suppose x = 0 or y = 0;
      hence x * y >= 0 by Tx1;
    end;
    suppose x < 0 & y < 0;
      hence x * y >= 0;
    end;
  end;
end;

registration
  let x,y be non negative (ext-real number);
  cluster x*y -> non negative;
  coherence
  proof
    per cases;
    suppose x = 0 or y = 0;
      hence x * y >= 0 by Tx1;
    end;
    suppose x > 0 & y > 0;
      hence x * y >= 0;
    end;
  end;
end;

registration
  let x be non positive (ext-real number);
  cluster x" -> non positive;
  coherence
  proof
   per cases;
    suppose x is real;
     then reconsider x as real number;
     x" is non positive;
   hence thesis;
    end;
    suppose x is not real;
     hence thesis by Def6;
    end;
  end;
end;

registration
  let x be non negative (ext-real number);
  cluster x" -> non negative;
  coherence
  proof
   per cases;
    suppose x is real;
     then reconsider x as real number;
     x" is non negative;
   hence thesis;
    end;
    suppose x is not real;
     hence thesis by Def6;
    end;
  end;
end;

registration
  let x be non negative (ext-real number), y be non positive (ext-real number);
  cluster x/y -> non positive;
  coherence;
  cluster y/x -> non positive;
  coherence;
end;

registration
  let x,y be non negative (ext-real number);
  cluster x/y -> non negative;
  coherence;
end;

registration
  let x,y be non positive (ext-real number);
  cluster x/y -> non negative;
  coherence;
end;

LemC:
 x is not real & x*y = 0 implies y = 0
 proof assume that
Z1: x is not real and
Z2: x*y = 0;
   not(x is positive & y is positive or x is negative & y is negative)
                                  by Z2;
  hence thesis by Z1,Z2;
 end;

registration let x,y be non zero (ext-real number);
 cluster x*y -> non zero;
 coherence
proof
 per cases;
 suppose x is real & y is real;
  then reconsider r=x, s=y as real number;
 assume x*y is zero;
  then r*s = 0;
 hence contradiction;
 end;
 suppose x is not real or y is not real;
 hence thesis by LemC;
 end;
end;
end;

registration
 let x be zero (ext-real number), y be ext-real number;
 cluster x*y -> zero (ext-real number);
 coherence by Tx1;
end;

LmZ: x = 0 implies x*(y*z) = (x*y)*z
 proof
  assume
Z: x = 0;
  hence x*(y*z) = 0
     .= (x*y)*z by Z;
 end;

LmZ1: y = 0 implies x*(y*z) = (x*y)*z
 proof
  assume
Z: y = 0;
  hence x*(y*z) = 0
     .= (x*y)*z by Z;
 end;

LmA: y is not real implies x*(y*z) = (x*y)*z
 proof
  assume y is not real;
   then
pc: not y in REAL;
  assume
C: not thesis;
   then
ppc: x <> 0 & z <> 0 by LmZ;
  per cases by pc,XXREAL_0:14;
  suppose
S:  y = -infty;
   per cases by ppc;
   suppose
A:  x is positive & z is positive;
    then x*(-infty*z) = x*-infty by Def4
        .= -infty by A,Def4
        .= -infty*z by A,Def4
        .= (x*-infty)*z by A,Def4;
   hence thesis by S,C;
   end;
   suppose
A:  x is positive & z is negative;
    then x*(-infty*z) = x*+infty by Def4
        .= +infty by A,Def4
        .= -infty*z by A,Def4
        .= (x*-infty)*z by A,Def4;
   hence thesis by S,C;
   end;
   suppose
A:  x is negative & z is positive;
    then x*(-infty*z) = x*-infty by Def4
        .= +infty by A,Def4
        .= +infty*z by A,Def4
        .= (x*-infty)*z by A,Def4;
   hence thesis by S,C;
   end;
   suppose
A:  x is negative & z is negative;
    then x*(-infty*z) = x*+infty by Def4
        .= -infty by A,Def4
        .= +infty*z by A,Def4
        .= (x*-infty)*z by A,Def4;
   hence thesis by S,C;
   end;
  end;
  suppose
S:  y = +infty;
   per cases by ppc;
   suppose
A:  x is positive & z is positive;
    then x*(+infty*z) = x*+infty by Def4
        .= +infty by A,Def4
        .= +infty*z by A,Def4
        .= (x*+infty)*z by A,Def4;
   hence thesis by S,C;
   end;
   suppose
A:  x is positive & z is negative;
    then x*(+infty*z) = x*-infty by Def4
        .= -infty by A,Def4
        .= +infty*z by A,Def4
        .= (x*+infty)*z by A,Def4;
   hence thesis by S,C;
   end;
   suppose
A:  x is negative & z is positive;
    then x*(+infty*z) = x*+infty by Def4
        .= -infty by A,Def4
        .= -infty*z by A,Def4
        .= (x*+infty)*z by A,Def4;
   hence thesis by S,C;
   end;
   suppose
A:  x is negative & z is negative;
    then x*(+infty*z) = x*-infty by Def4
        .= +infty by A,Def4
        .= -infty*z by A,Def4
        .= (x*+infty)*z by A,Def4;
   hence thesis by S,C;
   end;
  end;
 end;

LmB: x is not real implies x*(y*z) = (x*y)*z
 proof assume x is not real;
   then
pc: not x in REAL;
  assume
C: not thesis;
   then
ppc: y <> 0 & z <> 0 by LmZ,LmZ1;
  per cases by pc,XXREAL_0:14;
  suppose
S:  x = -infty;
   per cases by ppc;
   suppose
A:  y is positive & z is positive;
    then -infty*(y*z) = -infty by Def4
        .= -infty*z by A,Def4
        .= (-infty*y)*z by A,Def4;
   hence thesis by S,C;
   end;
   suppose
A:  y is positive & z is negative;
    then -infty*(y*z) = +infty by Def4
        .= -infty*z by A,Def4
        .= (-infty*y)*z by A,Def4;
   hence thesis by S,C;
   end;
   suppose
A:  y is negative & z is positive;
    then -infty*(y*z) = +infty by Def4
        .= +infty*z by A,Def4
        .= (-infty*y)*z by A,Def4;
   hence thesis by S,C;
   end;
   suppose
A:  y is negative & z is negative;
    then -infty*(y*z) = -infty by Def4
        .= +infty*z by A,Def4
        .= (-infty*y)*z by A,Def4;
   hence thesis by S,C;
   end;
  end;
  suppose
S:  x = +infty;
   per cases by ppc;
   suppose
A:  y is positive & z is positive;
    then +infty*(y*z) = +infty by Def4
        .= +infty*z by A,Def4
        .= (+infty*y)*z by A,Def4;
   hence thesis by S,C;
   end;
   suppose
A:  y is positive & z is negative;
    then +infty*(y*z) = -infty by Def4
        .= +infty*z by A,Def4
        .= (+infty*y)*z by A,Def4;
   hence thesis by S,C;
   end;
   suppose
A:  y is negative & z is positive;
    then +infty*(y*z) = -infty by Def4
        .= -infty*z by A,Def4
        .= (+infty*y)*z by A,Def4;
   hence thesis by S,C;
   end;
   suppose
A:  y is negative & z is negative;
    then +infty*(y*z) = +infty by Def4
        .= -infty*z by A,Def4
        .= (+infty*y)*z by A,Def4;
   hence thesis by S,C;
   end;
  end;
 end;

theorem Th6:
 x*(y*z) = (x*y)*z
proof
 per cases;
 suppose x is real & y is real & z is real;
   then reconsider r=x, s=y, t=z as real number;
   reconsider rs = r*s, sx = s*t as real number;
   thus x*(y*z)
        = r*sx
       .=  rs*t
       .= (x*y)*z;
 end;
 suppose x is not real or z is not real;
  hence thesis by LmB;
 end;
 suppose y is not real;
  hence thesis by LmA;
 end;
end;

registration :: for ext-real numbers
  let r be real number;
  cluster r" -> real;
  coherence;
end;

registration :: for ext-real numbers
  let r,s be real number;
  cluster r*s -> real;
  coherence;
  cluster r/s -> real;
  coherence;
end;

registration
  cluster -infty" -> zero;
  coherence by Def6;
  cluster +infty" -> zero;
  coherence by Def6;
end;

theorem :: MEMBER_1:2
  (f*g)" = f"*g"
  proof
    per cases by XXREAL_0:14;
    suppose f in REAL & g in REAL;
      then reconsider f1 = f, g1 = g as real number;
      consider a being complex number such that
A1:   f1 = a & f" = a" by Def6;
      consider b being complex number such that
A2:   g1 = b & g" = b" by Def6;
      ex a,b being complex number st f" = a & g" = b & f"*g" = a * b
      by A1,A2,Def4;
      then f"*g" = (f1*g1)" by A1,A2,XCMPLX_1:205;
      hence thesis;
    end;
    suppose
A3:   f = +infty;
      g is positive or g is negative or g = 0;
      then (f*g)" = +infty" or (f*g)" = -infty" or (f*g)" = 0"
      by A3,Def4;
      hence thesis by A3;
    end;
    suppose
A4:   f = -infty;
      g is positive or g is negative or g = 0;
      then (f*g)" = +infty" or (f*g)" = -infty" or (f*g)" = 0"
      by A4,Def4;
      hence thesis by A4;
    end;
    suppose
A5:   g = +infty;
      f is positive or f is negative or f = 0;
      then (f*g)" = +infty" or (f*g)" = -infty" or (f*g)" = 0"
      by A5,Def4;
      hence thesis by A5;
    end;
    suppose
A6:   g = -infty;
      f is positive or f is negative or f = 0;
      then (f*g)" = +infty" or (f*g)" = -infty" or (f*g)" = 0"
      by A6,Def4;
      hence thesis by A6;
    end;
  end;

theorem :: MEMBER_1:6
  r <> 0 & r*f = r*g implies f = g
  proof
    assume that
A1: r <> 0 and
A2: r*f = r*g;
A3: r is positive or r is negative by A1;
    per cases by XXREAL_0:14;
    suppose
A4:   f in REAL;
      then consider a, b being complex number such that
A5:   r = a & f = b and
A6:   r*f = a*b by Def4;
      now
        assume not g in REAL;
        then g = +infty or g = -infty by XXREAL_0:14;
        hence contradiction by A3,A2,A4,Def4;
      end;
      then ex c, d being complex number st r = c & g = d & r*g = c*d
      by Def4;
      hence thesis by A1,A2,A5,A6,XCMPLX_1:5;
    end;
    suppose
A7:   f = +infty;
      assume f <> g;
      then g in REAL or g = -infty by A7,XXREAL_0:14;
      hence thesis by A3,A2,A7,Def4;
    end;
    suppose
A9:   f = -infty;
      assume f <> g;
      then g in REAL or g = +infty by A9,XXREAL_0:14;
      hence thesis by A3,A2,A9,Def4;
    end;
  end;

theorem Th61:
  x <> +infty & x <> -infty & x * y = +infty implies y = +infty or y = -infty
proof
  assume
A1: x <> +infty & x <> -infty & x * y = +infty;
  assume y <> +infty & y <> -infty;
  then x in REAL & y in REAL by A1,XXREAL_0:14;
  then reconsider a=x, b=y as real number;
  x * y = a * b;
  hence contradiction by A1;
end;

theorem Th62:
  x <> +infty & x <> -infty & x * y = -infty implies y = +infty or y = -infty
proof
  assume
A1: x <> +infty & x <> -infty & x * y = -infty;
  assume y <> +infty & y <> -infty;
  then x in REAL & y in REAL by A1,XXREAL_0:14;
  then reconsider a=x, b=y as real number;
  x * y = a * b;
  hence contradiction by A1;
end;

LM1:
 x*y in REAL implies x in REAL & y in REAL or x*y = 0
proof assume
Z1: x*y in REAL;
 assume not x in REAL or not y in REAL;
  then
Z2: x is not real or y is not real by XREAL_0:def 1;
 assume
Z3: x*y <> 0;
 per cases by Z3;
 suppose
  x is positive & y is positive or x is negative & y is negative;
 hence contradiction by Z1,Def4,Z2;
 end;
 suppose
  x is positive & y is negative or x is negative & y is positive;
 hence contradiction by Z1,Def4,Z2;
 end;
end;

theorem Th65:
  x <= y & 0 <= z implies x*z <= y*z
proof
  assume that
A1: x <= y and
A2: 0 <= z;
 per cases by A2;
 suppose z = 0;
  hence thesis;
 end;
 suppose
A3: z > 0;
  per cases;
  suppose
S:   x = 0;
     then z*y >= 0 by A1,A2;
    hence thesis by S;
  end;
  suppose
SS: x <> 0;
  per cases;
  case that
C1: x*z in REAL and
C2: y*z in REAL;
   y*z = 0 implies y = 0 by A3;
   then reconsider x,y,z as Element of REAL by C1,LM1,A3,C2,SS;
   reconsider p = x*z, q = y*z as Element of REAL by XREAL_0:def 1;
   take p, q;
   thus thesis by A1,A2,XREAL_1:66;
  end;
  case
C: not x*z in REAL or not y*z in REAL;
  per cases by C;
  suppose
C: not x*z in REAL;
  per cases by C,XXREAL_0:14;
  suppose x*z = -infty;
   hence thesis;
  end;
  suppose
S:  x*z = +infty;
A:  x <> -infty by S,A3;
pc: not x in REAL or not y in REAL or not z in REAL by C,XREAL_0:def 1;
   per cases by pc, XXREAL_0:14,A;
   suppose y = +infty;
   hence thesis by A3,Def4;
   end;
   suppose x = +infty;
    then y = +infty by A1,XXREAL_0:4;
   hence thesis by A3,Def4;
   end;
   suppose y = -infty;
    then x = -infty by A1,XXREAL_0:6;
   hence thesis by A3,Def4;
   end;
   suppose that
S1:  not z in REAL and
     x in REAL & y in REAL;
G:  z = +infty by A3,XXREAL_0:14,S1;
    per cases by SS;
    suppose x < 0;
    hence thesis by G,Def4;
    end;
    suppose 0 < x;
     hence thesis by G,Def4,A1;
    end;
   end;
   end;
  end;
  suppose
C: not y*z in REAL;
pc: not x in REAL or not y in REAL or not z in REAL by C,XREAL_0:def 1;
   per cases by pc, XXREAL_0:14;
   suppose x = -infty;
   hence thesis by A3,Def4;
   end;
   suppose y = +infty;
   hence thesis by A3,Def4;
   end;
   suppose x = +infty;
    then y = +infty by A1,XXREAL_0:4;
   hence thesis by A3,Def4;
   end;
   suppose y = -infty;
    then x = -infty by A1,XXREAL_0:6;
   hence thesis by A3,Def4;
   end;
   suppose that
S1:  not z in REAL and
     x in REAL & y in REAL;
G:  z = +infty by A3,XXREAL_0:14,S1;
    per cases by SS;
    suppose x < 0;
    hence thesis by G,Def4;
    end;
    suppose 0 < x;
     hence thesis by G,Def4,A1;
    end;
  end;
  end;
 end;
 end;
end;
end;

theorem Th67:
  x < y & 0 < z & z <> +infty implies x*z < y*z
proof
  assume that
A1: x < y and
A2: 0 < z and
A3: z <> +infty;
X: x*z <= y*z by A1,A2,Th65;
  now assume
Z:  x*z = y*z;
K: x <> +infty by A1,XXREAL_0:3;
L: y <> -infty by A1,XXREAL_0:5;
   per cases by K,L,XXREAL_0:14;
   suppose
S:   x in REAL & y in REAL;
    z in REAL by A3,A2,XXREAL_0:14;
    then reconsider x,y,z as real number by S;
     x*z < y*z by A1,A2,XREAL_1:70;
   hence contradiction by Z;
   end;
   suppose
U:   y = +infty;
    then y*z = +infty by A2,Def4;
    then x = +infty or x = -infty by Th61,A3,Z,A2;
   hence contradiction by A1,U,A2,Z;
   end;
   suppose
U:  x = -infty;
    then x*z = -infty by A2,Def4;
    then y = +infty or y = -infty by Th62,A3,Z,A2;
   hence contradiction by A1,U,A2,Z;
   end;
  end;
 hence thesis by X,XXREAL_0:1;
end;

theorem
 x*y in REAL implies x in REAL & y in REAL or x*y = 0 by LM1;

theorem
 +infty" = 0;

theorem
 -infty" = 0;

theorem
  x/+infty = 0;

theorem
  x/-infty = 0;

theorem Th89:
  x <> -infty & x <> +infty & x <> 0 implies x / x = 1
proof
  assume x <> -infty & x <> +infty;
   then x in REAL by XXREAL_0:14;
   then reconsider a=x as real number;
  assume x <> 0;
   then a/a =1 by XCMPLX_1:60;
  hence thesis;
end;

theorem
 x <= y & 0 < z implies x/z <= y/z by Th65;

theorem
  x < y & 0 < z & z <> +infty implies x/z < y/z
proof
  assume that
A2: x < y and
A3: 0 < z and
pc: z <> +infty;
  per cases by pc,XXREAL_0:14;
  suppose z = -infty;
   hence thesis by A3;
  end;
  suppose z in REAL;
   then reconsider z as real number;
   z" > 0 by A3;
  hence thesis by A2,Th67;
  end;
end;

theorem Th79:
 1*x = x
proof
 per cases by XXREAL_0:14;
 suppose x in REAL;
   then reconsider x, y=1 as real number;
   y*x = x;
  hence thesis;
 end;
 suppose x = -infty or x = +infty;
  hence thesis by Def4;
 end;
end;

theorem Th80:
 y" = 0 implies y = +infty or y = -infty or y = 0
 proof assume
Z: y" = 0;
  assume y <> +infty & y <> -infty;
   then y in REAL by XXREAL_0:14;
   then reconsider y as real number;
  y"" = 0 by Z;
  hence thesis;
 end;

theorem Th81:
   0 < y & y < +infty implies +infty / y = +infty
proof
  assume
A1: 0<y & y<+infty;
   y" <> 0 by A1,Th80;
  hence thesis by Def4,A1;
end;

theorem Th82:
  y < 0 & -infty < y implies -infty / y = +infty
proof
  assume
A1: y<0 & -infty<y;
   y" <> 0 by A1,Th80;
  hence thesis by Def4,A1;
end;

theorem Th83:
  y < 0 & -infty < y implies +infty / y = -infty
proof
  assume
A1: y<0 & -infty<y;
   y" <> 0 by A1,Th80;
  hence thesis by Def4,A1;
end;

theorem Th84:
  0 < y & y < +infty implies -infty / y = -infty
proof
  assume
A1: 0<y & y<+infty;
   y" <> 0 by A1,Th80;
  hence thesis by Def4,A1;
end;

theorem  :: extension of REAL_1:34
  x <> +infty & x <> -infty & x <> 0 implies x*(1/x) = 1 & (1/x)*x = 1
proof
  assume
A1: x <> +infty & x <> -infty & x <> 0;
  then x in REAL by XXREAL_0:14;
  then reconsider a = x as real number;
  x * (1 / x) = a * (1/a)
    .= 1 by A1,XCMPLX_1:107;
  hence thesis;
end;

theorem
  -infty < y & y < +infty & y <> 0 implies x * y / y = x & x * (y / y) = x
proof
  assume that
A1: -infty < y and
A2: y < +infty and
A3: y <> 0;
  reconsider b = y as Element of REAL by A1,A2,XXREAL_0:14;
A4: x * y / y = x
  proof
    per cases;
    suppose
A5:   x = +infty;
      per cases by A3;
        suppose
A6:       0 < y;
          then x * y = +infty by A5,Def4;
          hence thesis by A2,A5,A6,Th81;
        end;
        suppose
A7:       y < 0;
          then x * y = -infty by A5,Def4;
          hence thesis by A1,A5,A7,Th82;
        end;
    end;
    suppose
A8:   x = -infty;
      per cases by A3;
        suppose
A9:       0 <y;
          then x * y = -infty by A8,Def4;
          hence thesis by A2,A8,A9,Th84;
        end;
        suppose
A10:      y < 0;
          then x * y = +infty by A8,Def4;
          hence thesis by A1,A8,A10,Th83;
        end;
    end;
    suppose x <> +infty & x <> -infty;
      then x in REAL by XXREAL_0:14;
      then reconsider a = x as real number;
      (x * y)/y = (a * b)/b
        .= a by A3,XCMPLX_1:90;
      hence thesis;
    end;
  end;
  y / y = 1 by A1,A2,A3,Th89;
  hence thesis by A4,Th79;
end;

theorem
  x = +infty or x = -infty implies for y holds x * y <> 1
proof
  assume
A1: x = +infty or x = -infty;
  given y such that
A2: x * y = 1;
  per cases;
  suppose y = 0;
    hence contradiction by A2;
  end;
  suppose 0 < y;
   hence contradiction by A1,A2,Def4;
  end;
  suppose y < 0;
   hence contradiction by A1,A2,Def4;
  end;
end;

theorem
  x * y <> +infty & x * y <> -infty implies x in REAL or y in REAL
proof
  assume
A1: x * y <> +infty & x * y <> -infty;
  assume
A2: not (x in REAL or y in REAL);
  per cases by A2,XXREAL_0:14;
  suppose x = +infty & y = +infty;
    hence contradiction by A1,Def4;
  end;
  suppose x = +infty & y = -infty;
    hence contradiction by A1,Def4;
  end;
  suppose x = -infty & y = +infty;
    hence contradiction by A1,Def4;
  end;
  suppose x = -infty & y = -infty;
    hence contradiction by A1,Def4;
  end;
end;

begin :: distributivity

theorem Th7:
 (-1)*x = -x
proof
 per cases by XXREAL_0:14;
 suppose x in REAL;
   then reconsider x, y=-1 as real number;
   y*x = -x;
  hence thesis;
 end;
 suppose
S: x = -infty;
  hence (-1)*x = +infty by Def4
       .= -x by S,Def5;
 end;
 suppose
S: x = +infty;
  hence (-1)*x = -infty by Def4
       .= -x by S,Def5;
 end;
end;

theorem Th8o:
 - x*y = (-x)*y
proof
 thus - x*y = (-1)*(x*y) by Th7
    .= (-1)*x*y by Th6
    .= (-x)*y by Th7;
end;

theorem Th11:
 y = -z implies x*(y+z) = x*y +x*z
proof
 assume
Z: y = -z;
 hence x*(y+z) = x*0 by Tx4
       .= x*y - x*y by Tx4
       .= x*y + x*(-y) by Th8o
       .= x*y + x*z by Z;
end;

theorem Th12:
 2*x = x+x
 proof
  per cases by XXREAL_0:14;
  suppose x in REAL;
    then reconsider x as real number;
    2*x = x+x;
   hence thesis;
  end;
  suppose
S:  x = -infty;
   hence 2*x = -infty by Def4
       .= x+x by S,Def3;
  end;
  suppose
S:  x = +infty;
   hence 2*x = +infty by Def4
       .= x+x by S,Def3;
  end;
 end;

Lm12C:
 x*(y+y) = x*y+x*y
  proof
   thus x*(y+y) = x*(2*y) by Th12
      .= 2*(x*y) by Th6
      .= x*y+x*y by Th12;
  end;

LmX:
 x*(0+z) = x*0 +x*z
 proof
  thus x*(0+z) = x*z by Tx3
      .= x*0 +x*z by Tx3;
 end;

LmY:
 0*(y+z) = 0*y +0*z;

Lm12A:
 x is real & y is real implies x*(y+z) = x*y +x*z
  proof
   assume
Z:  x is real & y is real;
    then reconsider r=x, s=y as real number;
V:  x*y = r*s;
   per cases by XXREAL_0:14;
   suppose z in REAL;
    then reconsider t=z as real number;
    reconsider u=s+t, v=r*s, w=r*t as real number;
    r*u = v + w;
   hence thesis;
   end;
   suppose
S:  z = -infty;
   then
T:  y+z = -infty by Def3,Z;
   per cases;
   suppose x is zero;
   hence thesis by LmY;
   end;
   suppose
SS:  x is positive;
   hence x*(y+z) = -infty by T,Def4
      .= x*y + -infty by V,Def3
      .= x*y +x*z by SS,S,Def4;
   end;
   suppose
SS:  x is negative;
   hence x*(y+z) = +infty by T,Def4
      .= x*y + +infty by V,Def3
      .= x*y +x*z by SS,S,Def4;
   end;
   end;
   suppose
S:  z = +infty;
   then
T:  y+z = +infty by Def3,Z;
   per cases;
   suppose x is zero;
   hence thesis by LmY;
   end;
   suppose
SS:  x is positive;
   hence x*(y+z) = +infty by T,Def4
      .= x*y + +infty by V,Def3
      .= x*y +x*z by SS,S,Def4;
   end;
   suppose
SS:  x is negative;
   hence x*(y+z) = -infty by T,Def4
      .= x*y + -infty by V,Def3
      .= x*y +x*z by SS,S,Def4;
   end;
   end;
  end;

Lm12B:
 x is real & y is not real implies x*(y+z) = x*y +x*z
  proof assume that
Z1: x is real and
Z2: y is not real;
   per cases;
   suppose z is real;
    hence thesis by Z1,Lm12A;
   end;
   suppose z is not real;
    then
pc: not y in REAL & not z in REAL by Z2;
   per cases by pc,XXREAL_0:14;
   suppose y = -infty & z = -infty;
   hence thesis by Lm12C;
   end;
   suppose y = -infty & z = +infty;
   hence thesis by Th11,Tx4A;
   end;
   suppose y = +infty & z = -infty;
   hence thesis by Th11,Tx4B;
   end;
   suppose y = +infty & z = +infty;
   hence thesis by Lm12C;
   end;
   end;
  end;

theorem Th106:
 x is real implies x*(y+z) = x*y +x*z
proof assume
Z: x is real;
 per cases;
 suppose y is real & z is real;
 hence thesis by Z,Lm12A;
 end;
 suppose y is not real or z is not real;
 hence thesis by Z,Lm12B;
 end;
end;

theorem Th14:
 y>=0 & z>=0 implies x*(y+z) = x*y +x*z
proof assume
Z: y>=0 & z>=0;
 per cases by Z;
 suppose y = 0 or z = 0;
  hence thesis by LmX;
 end;
 suppose
T:  y>0 & z>0;
 per cases;
 suppose x is real;
 hence thesis by Th106;
 end;
 suppose x is not real;
  then
pc: not x in REAL;
 per cases by pc,XXREAL_0:14;
 suppose
DD: x = -infty;
 hence x*(y+z) = -infty by T,Def4
     .= -infty + x*z by DD,T,Def3
     .= x*y + x*z by DD,T,Def4;
 end;
 suppose
DD: x = +infty;
 hence x*(y+z) = +infty by T,Def4
     .= +infty + x*z by DD,T,Def3
     .= x*y + x*z by DD,T,Def4;
 end;
 end;
 end;
end;

canceled;

theorem
 y<=0 & z<=0 implies x*(y+z) = x*y +x*z
proof assume
Z: y<=0 & z<=0;
 thus x*(y+z) = --(x*(y+z))
     .= -(x*-(y+z)) by Th8o
     .= -(x*(-y+-z)) by Th9
     .= -(x*(-y)+x*(-z)) by Z,Th14
     .= -(-x*y+x*(-z)) by Th8o
     .= -(-x*y+-x*z) by Th8o
     .= --(x*y +x*z) by Th9
     .= x*y +x*z;
end;

theorem
 x*(0+z) = x*0 +x*z by LmX;

theorem :: MEMBER_1:1
  (-f)" = -f"
  proof
    per cases by XXREAL_0:14;
    suppose
A1:   f in REAL;
      then reconsider g = f as real number;
      consider a being complex number such that
A2:   f = a and
A3:   f" = a" by A1,Def6;
A4:   (-a)" = -a" by XCMPLX_1:224;
A5:   -f = -g;
      ex m being complex number st -f = m & -f" = m"
      proof
        take -a;
        thus -f = -a by A2,A5;
        thus thesis by A3,A5,A4,Def5;
      end;
      hence thesis by A5,Def6;
    end;
    suppose
A6:   f = +infty;
      hence (-f)" = (-infty)" by Def5
      .= -f" by A6;
    end;
    suppose
A7:   f = -infty;
      hence (-f)" = (+infty)" by Def5
      .= -f" by A7;
    end;
  end;

theorem
  x is real implies x * (y - z) = x * y - x * z
proof
  assume x is real;
  then x * (y - z) = x * y + x * (-z) by Th106
     .= x * y + -(x * z) by Th8o;
  hence thesis;
end;

theorem Th66:
  x <= y & z <= 0 implies y*z <= x*z
proof
  assume that
A1: x <= y and
A2: z <= 0;
B: -x*z = x*(-z) by Th8o;
C: -y*z = y*(-z) by Th8o;
  x*(-z) <= y*(-z) by A1,Th65,A2;
 hence thesis by B,C,Th38;
end;

theorem Th68:
  x < y & z < 0 & z <> -infty implies y*z < x*z
proof
  assume that
A1: x < y and
A2: z < 0 and
A3: z <> -infty;
B: -x*z = x*(-z) by Th8o;
C: -y*z = y*(-z) by Th8o;
  x*(-z) < y*(-z) by A1,Th67,Tx4A,A3,ThM3,A2;
 hence thesis by Th38,B,C;
end;

theorem
 x <= y & z < 0 implies y/z <= x/z by Th66;

theorem
  x < y & z < 0 & z <> -infty implies y/z < x/z
proof
  assume that
A2: x < y and
A3: 0 > z and
pc: z <> -infty;
  per cases by pc,XXREAL_0:14;
  suppose z = +infty;
   hence thesis by A3;
  end;
  suppose z in REAL;
   then reconsider z as real number;
   z" < 0 by A3;
  hence thesis by A2,Th68;
  end;
end;

theorem
  x/2 + x/2 = x
proof
   x/2 + x/2 = (x+x)/2 by Th106
      .= 2*x/2 by Th12
      .= x*(2/2) by Th6
      .= x by Th79;
  hence thesis;
end;

