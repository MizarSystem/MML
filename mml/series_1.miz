:: Series
::  by Konrad Raczkowski and Andrzej N\c{e}dzusiak
::
:: Received October 15, 1990
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies ARYTM, SEQ_1, FUNCT_1, POWER, SEQ_2, ORDINAL2, ARYTM_3, ARYTM_1,
      ABSVALUE, INT_1, SEQM_3, SUPINF_2, RLVECT_1, PROB_1, LATTICES, PREPOWER,
      SERIES_1, GROUP_1, FUNCOP_1;
 notations SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0, COMPLEX1, REAL_1,
      NAT_1, VALUED_1, SEQ_1, SEQ_2, FUNCT_2, SEQM_3, INT_1, NEWTON, PREPOWER,
      POWER, FUNCOP_1, XXREAL_0;
 constructors FUNCOP_1, XXREAL_0, REAL_1, NAT_1, SEQ_2, SEQM_3, NEWTON,
      PREPOWER, POWER, PARTFUN1, SEQ_1, VALUED_1;
 registrations ORDINAL1, RELSET_1, NUMBERS, XXREAL_0, XREAL_0, NAT_1, INT_1,
      MEMBERED, SEQM_3, NEWTON, XBOOLE_0, VALUED_0, VALUED_1, FUNCT_2, POWER;
 requirements REAL, NUMERALS, SUBSET, ARITHM, BOOLE;
 definitions VALUED_1;
 theorems SEQ_1, SEQ_2, SEQM_3, SEQ_4, REAL_1, REAL_2, NAT_1, RFUNCT_2,
      ABSVALUE, INT_1, PREPOWER, POWER, SCHEME1, FUNCT_2, NEWTON, XCMPLX_1,
      FUNCOP_1, XREAL_1, COMPLEX1, XXREAL_0, ORDINAL1;
 schemes NAT_1, SEQ_1, FUNCT_2;

begin

reserve n,m,k for Element of NAT;
reserve a,p,r for real number;
reserve s,s1,s2,s3 for Real_Sequence;

Lm1: 1/1=1 & 1/(-1)=-1;

theorem Th1:
  0<a & a<1 & (for n holds s.n=a to_power (n+1)) implies
  s is convergent & lim s = 0
proof
  assume that
A1: 0<a & a<1 and
A2: for n holds s.n=a to_power (n+1);
  set r = 1/a - 1;
  1/a > 1 by A1,Lm1,REAL_2:151;
  then
A3: r > 0 by XREAL_1:52;
A4: for p be real number st 0 < p ex m st for n st m<=n holds abs(s.n - 0) < p
  proof
    let p be real number;
    assume
A5: 0<p;
    then
A6: 1/p > 0 by REAL_2:127;
A7: 1/(p * r) = 1/p/r by XCMPLX_1:79;
    [\1/(p * r)/] is Element of NAT
    proof
      p * r > 0 by A3,A5,REAL_2:122;
      then
A8:   1/(p * r) > 0 by REAL_2:127;
      1/(p * r) <= [\1/(p * r)/] + 1 by INT_1:52;
      hence [\1/(p * r)/] is Element of NAT by A8,INT_1:16,20;
    end;
    then consider m such that
A9: m = [\1/(p * r)/];
    take m;
    now
      let n such that
A10:  m<=n;
      [\1/(p * r)/] > 1/(p * r) -1 by INT_1:def 4;
      then n > 1/(p * r) - 1 by A9,A10,XXREAL_0:2;
      then n + 1 > 1/p/r by A7,XREAL_1:21;
      then
A11:  (n+1) * r > 1/p by A3,REAL_2:177;
      a to_power (n+1) > 0 by A1,POWER:39;
      then
A12:  abs(a to_power (n+1)) = a to_power (n+1) by ABSVALUE:def 1;
A13:  1 + (n+1) * r <= (1 + r) to_power (n + 1) by A3,POWER:56;
A14:  (1+r) to_power (n+1) = 1 to_power (n+1) / a to_power (n+1) by A1,POWER:36
        .= 1 / a to_power (n+1) by POWER:31;
      0 + (n+1) * r < 1 + (n+1) * r by XREAL_1:8;
      then 1/p < 1 + (n+1) * r by A11,XXREAL_0:2;
      then 1/p < 1/ a to_power (n+1) by A13,A14,XXREAL_0:2;
      then abs(a to_power (n+1)) < p by A6,A12,REAL_2:154;
      hence abs(s.n - 0) < p by A2;
    end;
    hence thesis;
  end;
  hence s is convergent by SEQ_2:def 6;
  hence lim s = 0 by A4,SEQ_2:def 7;
end;

theorem Th2:
  abs(a) to_power n = abs(a to_power n)
proof
  per cases;
  suppose
A1: a <> 0;
    then
A2: abs(a) > 0 by COMPLEX1:133;
    now per cases by A1;
      suppose
A3:     a>0;
        then
A4:     a to_power n = abs(a) to_power n by ABSVALUE:def 1;
        a to_power n > 0 by A3,POWER:39;
        hence abs(a) to_power n = abs(a to_power n) by A4,ABSVALUE:def 1;
      end;
      suppose
A5:     a < 0;
A6:     ex k being Element of NAT st n = 2 * k or n = 2 * k + 1 by SCHEME1:1;
        reconsider m=n as Integer;
        now per cases by A6;
          suppose
A7:         ex k being Integer st m = 2 * k;
A8:         abs(a) to_power n = (-a) to_power m by A5,ABSVALUE:def 1
              .= a to_power m by A1,A7,POWER:54;
            abs(a) to_power n > 0 by A2,POWER:39;
            hence abs(a) to_power n = abs(a to_power n) by A8,ABSVALUE:def 1;
          end;
          suppose
A9:         ex k being Integer st m = 2 * k + 1;
A10:        abs(a) to_power n = (-a) to_power m by A5,ABSVALUE:def 1
              .= - a to_power m by A1,A9,POWER:55;
            abs(a) to_power n > 0 by A2,POWER:39;
            hence abs(a) to_power n = abs(
            - a to_power n ) by A10,ABSVALUE:def 1
              .= abs( a to_power n ) by COMPLEX1:138;
          end;
        end;
        hence abs(a) to_power n = abs(a to_power n);
      end;
    end;
    hence thesis;
  end;
  suppose
A11: a = 0;
    per cases;
    suppose
S:   n = 0;
       a to_power n = 1 by S,NEWTON:9;
      hence thesis by A11,COMPLEX1:130,134;
    end;
    suppose n > 0;
      then abs(a) to_power n = 0 by A11,COMPLEX1:130,POWER:def 2;
      hence thesis by A11,COMPLEX1:130;
    end;
  end;
end;

theorem Th3:
  abs(a)<1 & (for n holds s.n=a to_power (n+1)) implies
  s is convergent & lim s = 0
proof
  assume that
A1: abs(a) < 1 and
A2: for n holds s.n=a to_power (n+1);
  now per cases;
    suppose abs(a) = 0;
      then
A3:   a = 0 by ABSVALUE:7;
A4:   now
        let n;
        a to_power (n+1) = 0 by A3,POWER:def 2;
        hence s.n = 0 by A2;
      end;
      then
A5:   s is constant by SEQM_3:def 6;
      s.0 = 0 by A4;
      hence s is convergent & lim s = 0 by A5,SEQ_4:39,41;
    end;
    suppose not abs(a) = 0;
      then
A6:   abs(a) > 0 by COMPLEX1:132;
      deffunc U(Element of NAT) = abs(a) to_power ($1+1);
      consider s1 such that
A7:   for n holds s1.n = U(n) from SEQ_1:sch 1;
A8:   s1 is convergent & lim s1 = 0 by A1,A6,A7,Th1;
      now
        let n;
        thus s1.n = abs(a) to_power (n+1) by A7
          .= abs(a to_power (n+1)) by Th2
          .= abs(s.n) by A2;
      end;
      then abs(s) is convergent & lim abs(s) = 0 by A8,SEQ_1:16;
      hence s is convergent & lim s = 0 by SEQ_4:28;
    end;
  end;
  hence thesis;
end;

definition
  let s;
  func Partial_Sums(s) -> Real_Sequence means
  :Def1:
  it.0=s.0 & for n holds it.(n+1) = it.n + s.(n+1);
  existence
  proof
    deffunc U(Nat,Real) = $2 + s.($1+1);
    consider f being Function of NAT,REAL such that
A1: f.0 = s.0 & for n being Nat holds f.(n+1) = U(n,f.n)
    from NAT_1:sch 12;
    take f;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let s1,s2;
    assume that
A2: s1.0=s.0 & for n holds s1.(n+1)=s1.n + s.(n+1) and
A3: s2.0=s.0 & for n holds s2.(n+1)=s2.n + s.(n+1);
    defpred X[Element of NAT] means s1.$1 = s2.$1;
A4: X[0] by A2,A3;
A5: for k st X[k] holds X[k+1]
    proof
      let k;
      assume s1.k =s2.k;
      hence s1.(k+1) = s2.k + s.(k+1) by A2
        .= s2.(k+1) by A3;
    end;
    for n holds X[n] from NAT_1:sch 1(A4,A5);
    hence s1 = s2 by FUNCT_2:113;
  end;
end;

definition
  let s;
  attr s is summable means
  :Def2:
  Partial_Sums(s) is convergent;
  func Sum(s) -> Real equals

  lim Partial_Sums(s);
  correctness;
end;

canceled 3;

theorem Th7:
  s is summable implies s is convergent & lim s = 0
proof
  assume s is summable;
  then
A1: Partial_Sums(s) is convergent by Def2;
  then
A2: Partial_Sums(s) ^\1 is convergent &
  lim(Partial_Sums(s) ^\1) = lim(Partial_Sums(s)) by SEQ_4:33;
  then
A3: lim(Partial_Sums(s) ^\1 - Partial_Sums(s))
  = lim(Partial_Sums(s)) - lim(Partial_Sums(s)) by A1,SEQ_2:26
    .= 0;
A4: s ^\1 = Partial_Sums(s) ^\1 - Partial_Sums(s)
  proof
    now
      let n;
      (Partial_Sums(s)).(n+1) = (Partial_Sums(s)).n + s.(n+1) by Def1;
      then (Partial_Sums(s)).(n+1) = (Partial_Sums(s)).n + (s ^\1).n
      by SEQM_3:def 9;
      hence (Partial_Sums(s) ^\1).n = (Partial_Sums(s)).n + (s ^\1).n
      by SEQM_3:def 9;
    end;
    then
A5: (Partial_Sums(s) ^\1) = Partial_Sums(s) + s ^\1 by SEQ_1:11;
    s ^\1 + (Partial_Sums(s) - Partial_Sums(s)) = s ^\1
    proof
      now
        let n;
        thus (s ^\1 + (Partial_Sums(s) - Partial_Sums(s))).n
        = (s ^\1).n + (Partial_Sums(s) +- Partial_Sums(s)).n by SEQ_1:11

.= (s ^\1).n + ((Partial_Sums(s)).n + (-Partial_Sums(s)).n) by SEQ_1:11

.= (s ^\1).n + ((Partial_Sums(s)).n +- (Partial_Sums(s)).n) by SEQ_1:14
          .= (s ^\1).n;
      end;
      hence thesis by FUNCT_2:113;
    end;
    hence thesis by A5,SEQ_1:39;
  end;
  then
A6: s ^\1 is convergent by A1,A2,SEQ_2:25;
  hence s is convergent by SEQ_4:35;
  thus thesis by A3,A4,A6,SEQ_4:36;
end;

theorem Th8:
  Partial_Sums(s1) + Partial_Sums(s2) = Partial_Sums(s1+s2)
proof
A1: (Partial_Sums(s1) + Partial_Sums(s2)).0
  = Partial_Sums(s1).0 + Partial_Sums(s2).0 by SEQ_1:11
    .= s1.0 + Partial_Sums(s2).0 by Def1
    .= s1.0 + s2.0 by Def1
    .= (s1+s2).0 by SEQ_1:11;
  now
    let n; thus (Partial_Sums(s1) + Partial_Sums(s2)).(n+1)
    = Partial_Sums(s1).(n+1) + Partial_Sums(s2).(n+1) by SEQ_1:11
      .= Partial_Sums(s1).n + s1.(n+1) + Partial_Sums(s2).(n+1) by Def1
      .= Partial_Sums(s1).n+s1.(n+1)+(s2.(n+1)+Partial_Sums(s2).n) by Def1
      .= Partial_Sums(s1).n+(s1.(n+1)+s2.(n+1))+Partial_Sums(s2).n
      .= Partial_Sums(s1).n+(s1+s2).(n+1)+Partial_Sums(s2).n by SEQ_1:11
      .= Partial_Sums(s1).n+Partial_Sums(s2).n+(s1+s2).(n+1)
      .= (Partial_Sums(s1)+Partial_Sums(s2)).n+(s1+s2).(n+1) by SEQ_1:11;
  end;
  hence thesis by A1,Def1;
end;

theorem Th9:
  Partial_Sums(s1) - Partial_Sums(s2) = Partial_Sums(s1-s2)
proof
A1: (Partial_Sums(s1) - Partial_Sums(s2)).0
  = Partial_Sums(s1).0 - Partial_Sums(s2).0 by RFUNCT_2:6
    .= s1.0 - Partial_Sums(s2).0 by Def1
    .= s1.0 - s2.0 by Def1
    .= (s1-s2).0 by RFUNCT_2:6;
  now
    let n; thus (Partial_Sums(s1) - Partial_Sums(s2)).(n+1)
    = Partial_Sums(s1).(n+1) - Partial_Sums(s2).(n+1) by RFUNCT_2:6
      .= (Partial_Sums(s1).n + s1.(n+1)) - Partial_Sums(s2).(n+1) by Def1
      .= (Partial_Sums(s1).n+s1.(n+1))-(s2.(n+1)+Partial_Sums(s2).n) by Def1
      .= Partial_Sums(s1).n+(s1.(n+1)-s2.(n+1))-Partial_Sums(s2).n
      .= (s1-s2).(n+1)+Partial_Sums(s1).n-Partial_Sums(s2).n by RFUNCT_2:6
      .= (s1-s2).(n+1)+(Partial_Sums(s1).n-Partial_Sums(s2).n)
      .= (Partial_Sums(s1)-Partial_Sums(s2)).n+(s1-s2).(n+1) by RFUNCT_2:6;
  end;
  hence thesis by A1,Def1;
end;

theorem
  s1 is summable & s2 is summable implies
  s1+s2 is summable & Sum(s1+s2) = Sum(s1) + Sum(s2)
proof
  assume
A1: s1 is summable & s2 is summable;
  then
A2: Partial_Sums(s1) is convergent by Def2;
A3: Partial_Sums(s2) is convergent by A1,Def2;
  then Partial_Sums(s1) + Partial_Sums(s2) is convergent by A2,SEQ_2:19;
  then Partial_Sums(s1+s2) is convergent by Th8;
  hence s1+s2 is summable by Def2;
  thus Sum(s1+s2) =lim (Partial_Sums(s1) + Partial_Sums(s2)) by Th8
    .=Sum(s1) + Sum(s2) by A2,A3,SEQ_2:20;
end;

theorem
  s1 is summable & s2 is summable implies
  s1-s2 is summable & Sum(s1-s2) = Sum(s1) - Sum(s2)
proof
  assume
A1: s1 is summable & s2 is summable;
  then
A2: Partial_Sums(s1) is convergent by Def2;
A3: Partial_Sums(s2) is convergent by A1,Def2;
  then Partial_Sums(s1) - Partial_Sums(s2) is convergent by A2,SEQ_2:25;
  then Partial_Sums(s1-s2) is convergent by Th9;
  hence s1-s2 is summable by Def2;
  thus Sum(s1-s2) =lim (Partial_Sums(s1) - Partial_Sums(s2)) by Th9
    .=Sum(s1) - Sum(s2) by A2,A3,SEQ_2:26;
end;

theorem Th12:
  Partial_Sums(r(#)s) = r(#)Partial_Sums(s)
proof
A1: (r(#)Partial_Sums(s)).0 = r*Partial_Sums(s).0 by SEQ_1:13
    .= r*s.0 by Def1
    .= (r(#)s).0 by SEQ_1:13;
  now
    let n; thus (r(#)Partial_Sums(s)).(n+1)
    = r*Partial_Sums(s).(n+1) by SEQ_1:13
      .= r*(Partial_Sums(s).n + s.(n+1)) by Def1
      .= r*Partial_Sums(s).n + r*s.(n+1)
      .= (r(#)Partial_Sums(s)).n + r*s.(n+1) by SEQ_1:13
      .= (r(#)Partial_Sums(s)).n + (r(#)s).(n+1) by SEQ_1:13;
  end;
  hence thesis by A1,Def1;
end;

theorem Th13:
  s is summable implies r(#)s is summable & Sum(r(#)s) =r*Sum(s)
proof
  assume s is summable;
  then
A1: Partial_Sums(s) is convergent by Def2;
  then r(#)Partial_Sums(s) is convergent by SEQ_2:21;
  then Partial_Sums(r(#)s) is convergent by Th12;
  hence r(#)s is summable by Def2;
  thus Sum(r(#)s) =lim (r(#)Partial_Sums(s)) by Th12
    .=r*Sum(s) by A1,SEQ_2:22;
end;

theorem Th14:
  for s,s1 st for n holds s1.n=s.0 holds
  Partial_Sums(s^\1) = (Partial_Sums(s)^\1) - s1
proof
  let s,s1;
  assume
A1: for n holds s1.n=s.0;
A2: ((Partial_Sums(s)^\1) - s1).0 = (Partial_Sums(s)^\1).0 - s1.0
  by RFUNCT_2:6
    .= (Partial_Sums(s)^\1).0 - s.0 by A1
    .= Partial_Sums(s).(0+1) - s.0 by SEQM_3:def 9
    .= Partial_Sums(s).0 + s.(0+1) - s.0 by Def1
    .= s.(0+1) + s.0 - s.0 by Def1
    .= (s^\1).0 by SEQM_3:def 9;
  now
    let k; thus ((Partial_Sums(s)^\1) - s1).(k+1)
    = (Partial_Sums(s)^\1).(k+1) - s1.(k+1) by RFUNCT_2:6
      .= (Partial_Sums(s)^\1).(k+1) - s.0 by A1
      .= Partial_Sums(s).(k+1+1) - s.0 by SEQM_3:def 9
      .= s.(k+1+1) + Partial_Sums(s).(k+1) - s.0 by Def1
      .= s.(k+1+1) + Partial_Sums(s).(k+1) - s1.k by A1
      .= s.(k+1+1) + (Partial_Sums(s).(k+1) - s1.k)
      .= s.(k+1+1) + ((Partial_Sums(s)^\1).k - s1.k) by SEQM_3:def 9
      .= s.(k+1+1) + ((Partial_Sums(s)^\1) - s1).k by RFUNCT_2:6
      .= ((Partial_Sums(s)^\1) - s1).k + (s^\1).(k+1) by SEQM_3:def 9;
  end;
  hence thesis by A2,Def1;
end;

theorem Th15:
  s is summable implies for n holds s^\n is summable
proof
  defpred X[Element of NAT] means s^\$1 is summable;
  assume s is summable;
  then
A1: X[0] by SEQM_3:34;
A2: for n st X[n] holds X[n+1]
  proof
    let n;
    assume s^\n is summable;
    then Partial_Sums(s^\n) is convergent by Def2;
    then
A3: Partial_Sums(s^\n)^\1 is convergent by SEQ_4:33;
A4: s^\(n+1)=(s^\n)^\1 by SEQM_3:36;
    reconsider s1 = NAT --> (s^\n).0 as Real_Sequence;
A5: for k holds s1.k = (s^\n).0 by FUNCOP_1:13;
A6: s1 is convergent by SEQ_4:39;
    Partial_Sums(s^\n^\1) = (Partial_Sums(s^\n)^\1) - s1 by A5,Th14;
    then Partial_Sums(s^\n^\1) is convergent by A3,A6,SEQ_2:25;
    hence thesis by A4,Def2;
  end;
  thus for n holds X[n] from NAT_1:sch 1(A1,A2);
end;

theorem Th16:
  (ex n st s^\n is summable) implies s is summable
proof
  given n such that
A1: s^\n is summable;
  s^\n^\1 is summable by A1,Th15;
  then
A2: Partial_Sums(s^\n^\1) is convergent by Def2;
  reconsider s1 = NAT --> Partial_Sums(s).n as Real_Sequence;
A3: s1 is convergent by SEQ_4:39;
  for k holds (Partial_Sums(s)^\(n+1)).k = Partial_Sums(s^\(n+1)).k + s1.k
  proof
A4: Partial_Sums(s^\(n+1)).0 = (s^\(n+1)).0 by Def1
      .= s.(0+(n+1)) by SEQM_3:def 9
      .= s.(n+1);
    defpred X[Element of NAT] means
    (Partial_Sums(s)^\(n+1)).$1 = Partial_Sums(s^\(n+1)).$1 + s1.$1;
    (Partial_Sums(s)^\(n+1)).0 = Partial_Sums(s).(0+(n+1)) by SEQM_3:def 9
      .= s.(n+1) + Partial_Sums(s).n by Def1
      .= Partial_Sums(s^\(n+1)).0 + s1.0 by A4,FUNCOP_1:13;
    then
A5: X[0];
A6: for k st X[k] holds X[k+1]
    proof
      let k;
      assume
A7:   (Partial_Sums(s)^\(n+1)).k = Partial_Sums(s^\(n+1)).k + s1.k;
      Partial_Sums(s^\(n+1)).(k+1) + s1.(k+1)
      = s1.(k+1) + (Partial_Sums(s^\(n+1)).k + (s^\(n+1)).(k+1)) by Def1
        .= s1.(k+1) + Partial_Sums(s^\(n+1)).k + (s^\(n+1)).(k+1)
        .= Partial_Sums(s).n + Partial_Sums(s^\(n+1)).k + (s^\(n+1)).(k+1) by
      FUNCOP_1:13
        .= (Partial_Sums(s)^\(n+1)).k + (s^\(n+1)).(k+1) by A7,FUNCOP_1:13
        .= Partial_Sums(s).(k+(n+1)) + (s^\(n+1)).(k+1) by SEQM_3:def 9
        .= Partial_Sums(s).(k+(n+1)) + s.(k+1+(n+1)) by SEQM_3:def 9
        .= Partial_Sums(s).(k+(n+1)+1) by Def1
        .= Partial_Sums(s).(k+1+(n+1))
        .= (Partial_Sums(s)^\(n+1)).(k+1) by SEQM_3:def 9;
      hence (Partial_Sums(s)^\(
      n+1)).(k+1) = Partial_Sums(s^\(n+1)).(k+1) + s1.(k+1);
    end;
    thus for k holds X[k] from NAT_1:sch 1(A5,A6);
  end;
  then Partial_Sums(s)^\(n+1) = Partial_Sums(s^\(n+1)) + s1 by SEQ_1:11
    .= Partial_Sums((s^\n)^\1) + s1 by SEQM_3:36;
  then Partial_Sums(s)^\(n+1) is convergent by A2,A3,SEQ_2:19;
  then Partial_Sums(s) is convergent by SEQ_4:35;
  hence thesis by Def2;
end;

theorem Th17:
  (for n holds s1.n<=s2.n) implies
  for n holds Partial_Sums(s1).n<=Partial_Sums(s2).n
proof
  assume
A1: for n holds s1.n<=s2.n;
A2: Partial_Sums(s2).0 = s2.0 by Def1;
  defpred X[Element of NAT] means Partial_Sums(s1).$1 <= Partial_Sums(s2).$1;
  Partial_Sums(s1).0 = s1.0 by Def1;
  then
A3: X[0] by A1,A2;
A4: for n st X[n] holds X[n+1]
  proof
    let n such that
A5: Partial_Sums(s1).n <= Partial_Sums(s2).n;
A6: Partial_Sums(s1).(n+1) = Partial_Sums(s1).n + s1.(n+1) by Def1;
A7: Partial_Sums(s2).(n+1) = Partial_Sums(s2).n + s2.(n+1) by Def1;
    s1.(n+1)<=s2.(n+1) by A1;
    hence Partial_Sums(s1).(n+1) <= Partial_Sums(s2).(n+1) by A5,A6,A7,
    XREAL_1:9;
  end;
  thus for n holds X[n] from NAT_1:sch 1(A3,A4);
end;

theorem
  s is summable implies for n holds Sum(s) = Partial_Sums(s).n + Sum(s^\(n+1 )
  )
proof
  assume
A1: s is summable;
  defpred X[Element of NAT] means Sum(s) = Partial_Sums(s).$1 + Sum(s^\($1+1));
A2: X[0]
  proof
    reconsider s1 = NAT --> s.0 as Real_Sequence;
A3: for k holds s1.k= s.0 by FUNCOP_1:13;
A4: Partial_Sums(s) is convergent by A1,Def2;
    then
A5: Partial_Sums(s)^\1 is convergent by SEQ_4:33;
A6: s1 is convergent by SEQ_4:39;
    Partial_Sums(s^\1) = (Partial_Sums(s)^\1) - s1 by A3,Th14;
    then lim Partial_Sums(s^\1) = lim (Partial_Sums(s)^\1) - lim s1 by A5,A6,
    SEQ_2:26
      .= lim Partial_Sums(s) - lim s1 by A4,SEQ_4:33
      .= Sum(s) - s1.0 by SEQ_4:41
      .= Sum(s) - s.0 by FUNCOP_1:13;
    then Sum(s) = Sum(s^\1) +(-(-s.0));
    hence thesis by Def1;
  end;
A7: for n st X[n] holds X[n+1]
  proof
    let n;
    assume
A8: Sum(s) = Partial_Sums(s).n + Sum(s^\(n+1));
    reconsider s1 = NAT --> (s^\(n+1)).0 as Real_Sequence;
A9: for k holds s1.k= (s^\(n+1)).0 by FUNCOP_1:13;
A10: s1 is convergent by SEQ_4:39;
    s^\(n+1) is summable by A1,Th15;
    then
A11: Partial_Sums(s^\(n+1)) is convergent by Def2;
    then
A12: Partial_Sums(s^\(n+1))^\1 is convergent by SEQ_4:33;
A13: Partial_Sums(s^\(n+1)^\1) = (Partial_Sums(s^\(n+1))^\1) - s1 by A9,Th14;
    lim Partial_Sums(s^\(n+1+1)) = lim Partial_Sums(s^\(n+1)^\1) by SEQM_3:36
      .= lim (Partial_Sums(s^\(n+1))^\1) - lim s1 by A10,A12,A13,SEQ_2:26
      .= lim Partial_Sums(s^\(n+1)) - lim s1 by A11,SEQ_4:33
      .= Sum(s^\(n+1)) - s1.0 by SEQ_4:41
      .= Sum(s^\(n+1)) - (s^\(n+1)).0 by FUNCOP_1:13;
    then Sum(s^\(n+1+1)) = Sum(s) - (Partial_Sums(s).n + (s^\(n+1)).0) by A8
      .= Sum(s) - (Partial_Sums(s).n + s.(0+(n+1))) by SEQM_3:def 9
      .= Sum(s) - Partial_Sums(s).(n+1) by Def1;
    hence Sum(s) = Partial_Sums(s).(n+1) + Sum(s^\(n+1+1));
  end;
  thus for n holds X[n] from NAT_1:sch 1(A2,A7);
end;

theorem Th19:
  (for n holds 0<=s.n) implies Partial_Sums(s) is non-decreasing
proof
  assume
A1: for n holds 0<=s.n;
  now
    let n;
    0<=s.(n+1) by A1;
    then 0 + Partial_Sums(s).n <= s.(n+1) + Partial_Sums(s).n by XREAL_1:8;
    hence Partial_Sums(s).n <= Partial_Sums(s).(n+1) by Def1;
  end;
  hence thesis by SEQM_3:def 13;
end;

theorem Th20:
  (for n holds 0<=s.n) implies
  (Partial_Sums(s) is bounded_above iff s is summable)
proof
  assume for n holds 0<=s.n;
  then
A1: Partial_Sums(s) is non-decreasing by Th19;
  thus Partial_Sums(s) is bounded_above implies s is summable
  proof
    assume Partial_Sums(s) is bounded_above;
    then Partial_Sums(s) is convergent by A1,SEQ_4:51;
    hence thesis by Def2;
  end;
  assume s is summable;
  then Partial_Sums(s) is convergent by Def2;
  then Partial_Sums(s) is bounded by SEQ_2:27;
  hence thesis by SEQ_2:def 5;
end;

theorem
  s is summable & (for n holds 0<=s.n) implies 0<=Sum(s)
proof
  assume that
A1: s is summable and
A2: for n holds 0<=s.n;
A3: Partial_Sums(s) is non-decreasing by A2,Th19;
A4: Partial_Sums(s) is convergent by A1,Def2;
  now
    let n;
A5: Partial_Sums(s).0<=Partial_Sums(s).n by A3,SEQM_3:21;
    0<=s.0 by A2;
    hence 0<=Partial_Sums(s).n by A5,Def1;
  end;
  hence thesis by A4,SEQ_2:31;
end;

theorem Th22:
  (for n holds 0<=s2.n) & s1 is summable &
  (ex m st for n st m<=n holds s2.n<=s1.n) implies s2 is summable
proof
  assume that
A1: for n holds 0<=s2.n and
A2: s1 is summable;
  given m such that
A3: for n st m<=n holds s2.n<=s1.n;
A4: now
    let n;
A5: (s1^\m).n = s1.(n+m) by SEQM_3:def 9;
    0<= s2.(n+m) by A1;
    hence 0<=(s1^\m).n by A3,A5,NAT_1:11;
  end;
  s1^\m is summable by A2,Th15;
  then Partial_Sums(s1^\m) is bounded_above by A4,Th20;
  then consider r being real number such that
A6: for n holds Partial_Sums(s1^\m).n<r by SEQ_2:def 3;
A7: now
    let n;
    s2.(n+m)<=s1.(n+m) by A3,NAT_1:12;
    then (s2^\m).n<=s1.(n+m) by SEQM_3:def 9;
    hence (s2^\m).n<=(s1^\m).n by SEQM_3:def 9;
  end;
A8: now
    let n;
    (s2^\m).n = s2.(n+m) by SEQM_3:def 9;
    hence 0<=(s2^\m).n by A1;
  end;
  now
    let n;
    Partial_Sums(s2^\m).n <= Partial_Sums(s1^\m).n by A7,Th17;
    hence Partial_Sums(s2^\m).n<r by A6,XXREAL_0:2;
  end;
  then Partial_Sums(s2^\m) is bounded_above by SEQ_2:def 3;
  then s2^\m is summable by A8,Th20;
  hence thesis by Th16;
end;

canceled;

theorem
  (for n holds 0<=s1.n & s1.n<=s2.n) & s2 is summable implies
  s1 is summable & Sum(s1)<=Sum(s2)
proof
  assume that
A1: for n holds 0<=s1.n & s1.n<=s2.n and
A2: s2 is summable;
  for n holds 0<=n implies s1.n<=s2.n by A1;
  hence s1 is summable by A1,A2,Th22;
  then
A3: Partial_Sums(s1) is convergent by Def2;
A4: Partial_Sums(s2) is convergent by A2,Def2;
  for n holds Partial_Sums(s1).n<=Partial_Sums(s2).n by A1,Th17;
  hence thesis by A3,A4,SEQ_2:32;
end;

theorem Th25:
  s is summable iff for r st 0<r ex n st
  for m st n<=m holds abs(Partial_Sums(s).m - Partial_Sums(s).n)<r
proof
  thus s is summable implies for r st 0<r ex n st
  for m st n<=m holds abs(Partial_Sums(s).m - Partial_Sums(s).n)<r
  proof
    assume s is summable;
    then Partial_Sums(s) is convergent by Def2;
    hence thesis by SEQ_4:58;
  end;
  assume for r st 0<r ex n st
  for m st n<=m holds abs(Partial_Sums(s).m - Partial_Sums(s).n)<r;
  then Partial_Sums(s) is convergent by SEQ_4:58;
  hence thesis by Def2;
end;

theorem Th26:
  a <> 1 implies Partial_Sums(a GeoSeq).n = (1 - a to_power (n+1))/(1-a)
proof
  assume a<>1;
  then
A1: 1-a <> 0;
  defpred X[Element of NAT] means
  Partial_Sums(a GeoSeq).$1 = (1-a to_power ($1+1))/(1-a);
  Partial_Sums(a GeoSeq).0 = a GeoSeq.0 by Def1
    .= 1 by PREPOWER:4
    .= (1-a)/(1-a) by A1,XCMPLX_1:60
    .= (1-a to_power (0+1))/(1-a) by POWER:30;
  then
A2: X[0];
A3: for n st X[n] holds X[n+1]
  proof
    let n;
    assume
A4: Partial_Sums(a GeoSeq).n = (1 - a to_power (n+1))/(1-a);
    thus Partial_Sums(a GeoSeq).(n+1)
    = (1 - a to_power (n+1))/(1-a) + a GeoSeq.(n+1) by A4,Def1
      .= (1 - a to_power (n+1))/(1-a) + a to_power (n+1) * 1 by PREPOWER:def 1
      .= (1-a to_power (n+1))/(1-a)+a to_power (n+1)*((1-a)/(1-a)) by A1,
    XCMPLX_1:60
      .= (1-a to_power (n+1))/(1-a)+(a to_power (n+1)*(1-a))/(1-a)
    by XCMPLX_1:75
      .= (1-a to_power (n+1) + (a to_power (n+1)-a |^ (n+1)*a))/(1-a)
      by XCMPLX_1:63
      .= (1-a to_power (n+1) + (a to_power (n+1)-a |^ (n+1+1)))/(1-a)
    by NEWTON:11
      .= (1 - a to_power (n+1+1))/(1-a);
  end;
  for n holds X[n] from NAT_1:sch 1(A2,A3);
  hence thesis;
end;

theorem
  a <> 1 & (for n holds s.(n+1) = a * s.n) implies
  for n holds Partial_Sums(s).n = s.0 * (1 - a to_power (n+1))/(1-a)
proof
  assume
A1: a <> 1 & (for n holds s.(n+1) = a * s.n);
  defpred X[Element of NAT] means s.$1 = s.0 * a GeoSeq.$1;
  a GeoSeq.0 = 1 by PREPOWER:4;
  then
A2: X[0];
A3: for n st X[n] holds X[n+1]
  proof
    let n;
    assume s.n = s.0 * a GeoSeq.n;
    then s.(n+1) = a * (s.0 * a GeoSeq.n) by A1
      .= s.0 * (a GeoSeq.n * a)
      .= s.0 * a GeoSeq.(n+1) by PREPOWER:4;
    hence thesis;
  end;
  for n holds X[n] from NAT_1:sch 1(A2,A3);
  then s = s.0 (#) a GeoSeq by SEQ_1:13;
  then
A4: Partial_Sums(s) = s.0 (#) Partial_Sums(a GeoSeq) by Th12;
  now
    let n;
    thus Partial_Sums(s).n = s.0 * Partial_Sums(a GeoSeq).n by A4,SEQ_1:13
      .= s.0 * ((1 - a to_power (n+1))/(1-a)) by A1,Th26
      .= s.0 * (1 - a to_power (n+1))/(1-a) by XCMPLX_1:75;
  end;
  hence thesis;
end;

theorem Th28:
  abs(a)<1 implies a GeoSeq is summable & Sum(a GeoSeq) = 1/(1-a)
proof
  assume
A1: abs(a)<1;
  deffunc U(Element of NAT) = a to_power ($1+1);
  consider s such that
A2: for n holds s.n = U(n) from SEQ_1:sch 1;
A3: s is convergent & lim s = 0 by A1,A2,Th3;
A4: now
    let r be real number such that
A5: r>0;
    a<1 by A1,SEQ_2:9;
    then
A6: 1-a>0 by XREAL_1:52;
    then r*(1-a)>0*r by A5,XREAL_1:70;
    then consider m such that
A7: for n st n>=m holds abs(s.n - 0)<r*(1-a) by A3,SEQ_2:def 7;
    take m;
A8: a<>1 by A1,SEQ_2:9;
A9: 1-a<>0 by A1,SEQ_2:9;
    let n such that
A10: n>=m;
A11: abs(Partial_Sums(a GeoSeq).n - 1/(1-a))
    = abs((1 - a to_power (n+1))/(1-a) - 1/(1-a)) by A8,Th26
      .= abs((1 +- a to_power (n+1) - 1)/(1-a)) by XCMPLX_1:121
      .= abs(- a to_power (n+1)/(1-a)) by XCMPLX_1:188
      .= abs(a to_power (n+1)/(1-a)) by COMPLEX1:138
      .= abs(a to_power (n+1))/abs((1-a)) by COMPLEX1:153
      .= abs(a to_power (n+1))/(1-a) by A6,ABSVALUE:def 1;
    abs(s.n - 0)<r*(1-a) by A7,A10;
    then abs(a to_power (n+1) - 0)<r*(1-a) by A2;
    then abs(a to_power (n+1))/(1-a)<r*(1-a)/(1-a) by A6,REAL_1:73;
    hence abs(Partial_Sums(a GeoSeq).n - 1/(1-a)) < r by A9,A11,XCMPLX_1:90;
  end;
  then
A12: Partial_Sums(a GeoSeq) is convergent by SEQ_2:def 6;
  hence a GeoSeq is summable by Def2;
  thus thesis by A4,A12,SEQ_2:def 7;
end;

theorem
  abs(a) < 1 & (for n holds s.(n+1) = a * s.n) implies
  s is summable & Sum(s) = s.0/(1-a)
proof
  assume
A1: abs(a) < 1 & (for n holds s.(n+1) = a * s.n);
  defpred X[Element of NAT] means s.$1 = s.0 * a GeoSeq.$1;
  a GeoSeq.0 = 1 by PREPOWER:4;
  then
A2: X[0];
A3: for n st X[n] holds X[n+1]
  proof
    let n;
    assume s.n = s.0 * a GeoSeq.n;
    then s.(n+1) = a * (s.0 * a GeoSeq.n) by A1
      .= s.0 * (a GeoSeq.n * a)
      .= s.0 * a GeoSeq.(n+1) by PREPOWER:4;
    hence thesis;
  end;
  for n holds X[n] from NAT_1:sch 1(A2,A3);
  then s = s.0 (#) a GeoSeq by SEQ_1:13;
  then
A4: Partial_Sums(s) = s.0 (#) Partial_Sums(a GeoSeq) by Th12;
A5: a GeoSeq is summable & Sum(a GeoSeq) = 1/(1-a) by A1,Th28;
  then
A6: Partial_Sums(a GeoSeq) is convergent by Def2;
  then
A7: Partial_Sums(s) is convergent by A4,SEQ_2:21;
A8: lim Partial_Sums(s) = s.0 * (1/(1-a)) by A4,A5,A6,SEQ_2:22
    .= s.0*1/(1-a) by XCMPLX_1:75
    .= s.0/(1-a);
  thus s is summable by A7,Def2;
  thus thesis by A8;
end;

theorem Th30:
  (for n holds s.n>0 & s1.n=s.(n+1)/s.n) & s1 is convergent & lim s1 < 1
  implies s is summable
proof
  assume that
A1: (for n holds s.n>0 & s1.n=s.(n+1)/s.n) and
A2: s1 is convergent and
A3: lim s1 < 1;
  set r = (1 - lim s1)/2;
  0 + lim s1 < 1 by A3;
  then 0 < 1 - lim s1 by XREAL_1:22;
  then
A4: r > 0 by XREAL_1:217;
A5: r + lim s1 = 1 - r;
  consider m such that
A6: for n st m <= n holds abs(s1.n - lim s1) < r by A2,A4,SEQ_2:def 7;
  set s2 = (s.m * (1-r) to_power (-m)) (#) (1-r) GeoSeq;
A7: now
    let n;
A8: s.n > 0 by A1;
    s.(n+1) > 0 by A1;
    then s.(n+1)/s.n > 0 by A8,XREAL_1:141;
    hence s1.n >= 0 by A1;
  end;
  then
A9: lim s1 >= 0 by A2,PREPOWER:2;
A10: s2 is summable
  proof
    1 - lim s1 > 0 by A3,XREAL_1:52;
    then r > 0 by XREAL_1:217;
    then
A11: 1 - r < 1 - 0 by REAL_2:105;
    1 - lim s1 <= 1 - 0 by A9,REAL_2:106;
    then 1 - lim s1 < 2 * 2 by XXREAL_0:2;
    then r < 2 * 2 / 2 by REAL_1:73;
    then r < 1 + 1;
    then r - 1 < 1 by XREAL_1:21;
    then - (r - 1) > - 1 by XREAL_1:26;
    then abs(1-r) < 1 by A11,SEQ_2:9;
    then (1-r) GeoSeq is summable by Th28;
    hence thesis by Th13;
  end;
A12: 1 - r > 0
  proof
    -1 < lim s1 by A9,XXREAL_0:2;
    then - lim s1 < 1 by REAL_2:109;
    then 1 + -lim s1 < 1 + 1 by XREAL_1:8;
    then (1 - lim s1)/2 < 2/2 by REAL_1:73;
    hence thesis by XREAL_1:52;
  end;
  defpred X[Element of NAT] means s.(m+$1) <= s2.(m+$1);
A13: X[0]
  proof
    s2.(m+0) = (s.m * (1-r) to_power (-m)) * (1-r) GeoSeq.m by SEQ_1:13
      .= (s.m * (1-r) to_power (-m)) * (1-r) |^ m by PREPOWER:def 1
      .= s.m * ((1-r) to_power (-m) * (1-r) to_power m)
      .= s.m * (1-r) to_power (-m + m) by A12,POWER:32
      .= s.m * 1 by POWER:29
      .= s.(m+0);
    hence thesis;
  end;
A14: for k holds X[k] implies X[k+1]
  proof
    let k such that
A15: s.(m+k) <= s2.(m+k);
A16: s1.(m+k) >= 0 by A7;
A17: s2.(m+k) >= 0 by A1,A15;
A18: s.(m+k) <> 0 by A1;
A19: now
      abs(s1.(m+k) - lim s1) < r by A6,NAT_1:11;
      then s1.(m+k) - lim s1 < r by SEQ_2:9;
      hence s1.(m+k) <= 1 - r by A5,XREAL_1:21;
    end;
    s.(m+(k+1)) = s.(m+k+1) / s.(m+k) * s.(m+k) by A18,XCMPLX_1:88
      .= s1.(m+k) * s.(m+k) by A1;
    then
A20: s.(m+(k+1)) <= s1.(m+k) * s2.(m+k) by A15,A16,XREAL_1:66;
A21: s1.(m+k) * s2.(m+k) <= (1-r) * s2.(m+k) by A17,A19,XREAL_1:66;
    set X = (s.m * (1-r) to_power (-m));
    (1-r) * s2.(m+k) = (1-r) * (X * (1-r) GeoSeq.(m+k)) by SEQ_1:13
      .= X * ((1-r) GeoSeq.(m+k) * (1-r))
      .= X * (1-r) GeoSeq.(m+k+1) by PREPOWER:4
      .= s2.(m+(k+1)) by SEQ_1:13;
    hence thesis by A20,A21,XXREAL_0:2;
  end;
A22: for k holds X[k] from NAT_1:sch 1(A13,A14);
A23: now
    let n;
    assume m <= n;
    then consider k be Nat such that
A24: n = m + k by NAT_1:10;
    reconsider k as Element of NAT by ORDINAL1:def 13;
    n = m+k by A24;
    hence s.n <= s2.n by A22;
  end;
  for n holds 0 <= s.n by A1;
  hence thesis by A10,A23,Th22;
end;

theorem
  (for n holds s.n>0) & (ex m st for n st n>=m holds s.(n+1)/s.n >= 1)
  implies s is not summable
proof
  assume that
A1: (for n holds s.n>0) and
A2: (ex m st for n st n>=m holds s.(n+1)/s.n >= 1);
  consider m such that
A3: for n st n>=m holds s.(n+1)/s.n>=1 by A2;
  defpred X[Element of NAT] means s.(m+$1)>=s.m;
A4: X[0];
A5: for k st X[k] holds X[k+1]
  proof
    let k such that
A6: s.(m+k)>=s.m;
A7: s.(m+k)>0 by A1;
    s.(m+k+1)/s.(m+k)>=1 by A3,NAT_1:11;
    then s.(m+k+1)>=s.(m+k) by A7,REAL_2:118;
    hence thesis by A6,XXREAL_0:2;
  end;
A8: for k holds X[k] from NAT_1:sch 1(A4,A5);
A9: s.m>0 by A1;
  for k ex n st n>=k & not abs(s.n - 0)<s.m
  proof
    let k;
    take n = m + k;
    s.n>=s.m by A8;
    hence thesis by NAT_1:11,SEQ_2:9;
  end;
  then not lim s = 0 or s is not convergent by A9,SEQ_2:def 7;
  hence thesis by Th7;
end;

theorem Th32:
  (for n holds s.n>=0 & s1.n = n-root (s.n)) & s1 is convergent & lim s1 < 1
  implies s is summable
proof
  assume that
A1: (for n holds s.n>=0 & s1.n = n-root (s.n)) and
A2: s1 is convergent and
A3: lim s1 < 1;
  set r = (1 - lim s1)/2;
  0 + lim s1 < 1 by A3;
  then 0 < 1 - lim s1 by XREAL_1:22;
  then
A4: r > 0 by XREAL_1:217;
A5: r + lim s1 = 1 - r;
A6: (s1^\1) is convergent & lim (s1^\1) = lim s1 by A2,SEQ_4:33;
  now
    let n;
A7: s.(n+1) >= 0 by A1;
    (s1^\1).n = s1.(n+1) by SEQM_3:def 9
      .= (n+1)-root (s.(n+1)) by A1;
    hence (s1^\1).n >= 0 by A7,NAT_1:11,POWER:8;
  end;
  then
A8: lim s1 >= 0 by A6,PREPOWER:2;
A9: 1 - r > 0
  proof
    -1 < lim s1 by A8,XXREAL_0:2;
    then - lim s1 < 1 by REAL_2:109;
    then 1 + -lim s1 < 1 + 1 by XREAL_1:8;
    then (1 - lim s1)/2 < 2/2 by REAL_1:73;
    hence thesis by XREAL_1:52;
  end;
  consider m such that
A10: for n st m <= n holds abs(s1.n - lim s1) < r by A2,A4,SEQ_2:def 7;
A11: for n st m+1<=n holds s.n <= (1-r) to_power n
  proof
    let n;
    assume
A12: m+1<=n;
    then
A13: m<=n by NAT_1:13;
     1<=m+1 by NAT_1:11;
    then
A15: 1 <= n by A12,XXREAL_0:2;
A16: s.n >= 0 by A1;
    abs(s1.n - lim s1) < r by A10,A13;
    then s1.n - lim s1 < r by SEQ_2:9;
    then s1.n < 1 - r by A5,XREAL_1:21;
    then
A17: n-root (s.n) < 1 - r by A1;
    now per cases;
      suppose s.n = 0;
        hence s.n < (1 - r) to_power n by A9,POWER:39;
      end;
      suppose s.n <> 0;
        then n -Root (s.n) > 0 by A15,A16,PREPOWER:def 3;
        then n-root (s.n) > 0 by A15,A16,POWER:def 1;
        then (n-root (s.n)) to_power n < (1-r) to_power n by A12,A17,POWER:42;
        then (n-root (s.n)) |^ n < (1-r) to_power n;
        hence s.n < (1-r) to_power n by A15,A16,POWER:5;
      end;
    end;
    hence thesis;
  end;
  set s2 = (1-r) GeoSeq;
A18: s2 is summable
  proof
    1 - lim s1 > 0 by A3,XREAL_1:52;
    then r > 0 by XREAL_1:217;
    then
A19: 1 - r < 1 - 0 by REAL_2:105;
    1 - lim s1 <= 1 - 0 by A8,REAL_2:106;
    then 1 - lim s1 < 2 * 2 by XXREAL_0:2;
    then r < 2 * 2 / 2 by REAL_1:73;
    then r < 1 + 1;
    then r - 1 < 1 by XREAL_1:21;
    then - (r - 1) > - 1 by XREAL_1:26;
    then abs(1-r) < 1 by A19,SEQ_2:9;
    hence thesis by Th28;
  end;
  for n st m+1 <= n holds s.n <= s2.n
  proof
    let n;
    s2.n = (1-r) to_power n by PREPOWER:def 1;
    hence thesis by A11;
  end;
  hence thesis by A1,A18,Th22;
end;

theorem Th33:
  (for n holds s.n>=0 & s1.n = n-root (s.n)) &
  (ex m st for n st m<=n holds s1.n>=1) implies s is not summable
proof
  assume that
A1: (for n holds s.n>=0 & s1.n = n-root (s.n)) and
A2: (ex m st for n st m<=n holds s1.n>=1);
  consider m such that
A3: for n st m<=n holds s1.n>=1 by A2;
A4: for n st m+1<=n holds s.n>=1
  proof
    let n such that
A5: m+1<=n;
    m<=m+1 by NAT_1:11;
    then
A6: m<=n by A5,XXREAL_0:2;
    1<=1+m by NAT_1:11;
    then
A8: 1<=n by A5,XXREAL_0:2;
    s1.n >= 1 by A3,A6;
    then
A9: n-root (s.n) >= 1 by A1;
A10: s.n >= 0 by A1;
    now per cases by A9,REAL_1:def 5;
      suppose n-root (s.n) = 1;
        then s.n = 1 |^ n by A8,A10,POWER:5;
        hence s.n>=1 by NEWTON:15;
      end;
      suppose n-root (s.n) > 1;
        then (n-root (s.n)) to_power n > 1 by A5,POWER:40;
        then (n-root (s.n)) |^ n > 1;
        hence s.n >= 1 by A8,A10,POWER:5;
      end;
    end;
    hence thesis;
  end;
  for k ex n st k<=n & not abs(s.n - 0) < 1
  proof
    let k;
    take n = m + 1 + k;
    not s.n < 1 by A4,NAT_1:11;
    hence thesis by NAT_1:11,SEQ_2:9;
  end;
  then s is not convergent or not lim s = 0 by SEQ_2:def 7;
  hence thesis by Th7;
end;

theorem
  (for n holds s.n>=0 & s1.n = n-root (s.n)) & s1 is convergent & lim s1 > 1
  implies s is not summable
proof
  assume that
A1: (for n holds s.n>=0 & s1.n = n-root (s.n)) and
A2: s1 is convergent & lim s1 > 1;
  set r = lim s1 - 1;
  r > 0 by A2,XREAL_1:52;
  then consider m such that
A3: for n st m<=n holds abs(s1.n - lim s1) < r by A2,SEQ_2:def 7;
  for n st m<=n holds s1.n >= 1
  proof
    let n;
    assume m<=n;
    then abs(s1.n - lim s1) < r by A3;
    then s1.n - lim s1 > - r by SEQ_2:9;
    then s1.n - lim s1 + lim s1 > - r + lim s1 by XREAL_1:8;
    hence thesis;
  end;
  hence thesis by A1,Th33;
end;

registration
  let k, n be Nat;
  cluster k to_power n -> natural;
  coherence;
end;

definition
  let k, n be Nat;
  redefine func k to_power n -> Element of NAT;
  coherence by ORDINAL1:def 13;
end;

theorem Th35:
  s is non-increasing & (for n holds s.n>=0 &
  s1.n = 2 to_power n * s.(2 to_power n)) implies
  (s is summable iff s1 is summable)
proof
  assume that
A1: s is non-increasing and
A2: for n holds s.n>=0 & s1.n = 2 to_power n * s.(2 to_power n);
A3: now
    let n;
    s.(2 to_power n) >= 0 by A2;
    then 2 to_power n * s.(2 to_power n) >= 0;
    hence s1.n >= 0 by A2;
  end;
  thus s is summable implies s1 is summable
  proof
    assume
A4: s is summable;
    defpred Y[Element of NAT] means
    Partial_Sums(s1).$1 <= 2 * Partial_Sums(s).(2 to_power $1);
A5: Y[0]
    proof
A6:   2 to_power 0 = 0+1 by POWER:29;
A7:   Partial_Sums(s1).0 = s1.0 by Def1
        .= 1 * s.1 by A2,A6
        .= s.1;
A8:   2 * Partial_Sums(s).(2 to_power 0)
      = 2 * (Partial_Sums(s).0 + s.1) by A6,Def1
        .= 2*(s.0 + s.1) by Def1
        .= 2*s.0 + 2*s.1;
      s.1>=0 by A2;
      then
A9:   s.1 + s.1 >= s.1 + 0 by XREAL_1:9;
      s.0>=0 by A2;
      then s.0 + s.0 >= 0 + 0;
      hence thesis by A7,A8,A9,XREAL_1:9;
    end;
A10: for n st Y[n] holds Y[n+1]
    proof
      let n;
      assume Partial_Sums(s1).n <= 2 * Partial_Sums(s).(2 to_power n);
      then Partial_Sums(s1).n + s1.(n+1) <=
      2 * Partial_Sums(s).(2 to_power n) + s1.(n+1) by XREAL_1:8;
      then
A11:  Partial_Sums(s1).(n+1) <=
      2 * Partial_Sums(s).(2 to_power n) + s1.(n+1) by Def1;
      deffunc U(Element of NAT) = Partial_Sums(s).(2 to_power n + $1) -
      Partial_Sums(s).(2 to_power n);
      consider a being Real_Sequence such that
A12:  for m holds a.m = U(m) from SEQ_1:sch 1;
      defpred X[Element of NAT] means
      $1 > 2 to_power n or $1 * s.(2 to_power (n+1)) <= a.$1;
A13:  X[0]
      proof
        a.0 = Partial_Sums(s).(2 to_power n + 0) -
        Partial_Sums(s).(2 to_power n) by A12
          .= 0;
        hence thesis;
      end;
A14:  for m st X[m] holds X[m+1]
      proof
        let m;
        assume
A15:    m > 2 to_power n or m * s.(2 to_power (n+1)) <= a.m;
        now per cases by A15;
          suppose m > 2 to_power n;
            hence thesis by NAT_1:13;
          end;
          suppose
A16:        m * s.(2 to_power (n+1)) <= a.m;
            now per cases;
              suppose m<2 to_power n;
                then m+1<=2 to_power n by NAT_1:13;

then 2 to_power n + (m+1) <= 2 to_power n + 2 to_power n by XREAL_1:9;
                then 2 to_power n + m + 1 <= 2 * 2 to_power n;

then 2 to_power n + m + 1 <= 2 to_power 1 * 2 to_power n by POWER:30;
                then 2 to_power n + m + 1 <= 2 to_power (n+1) by POWER:32;

then s.(2 to_power n + m + 1) >= s.(2 to_power (n+1)) by A1,SEQM_3:14;
                then m * s.(2 to_power (n+1)) + 1 * s.(2 to_power (n+1)) <=
                a.m + s.(2 to_power n + m + 1) by A16,XREAL_1:9;
                then (m + 1) * s.(2 to_power (n+1)) <=

Partial_Sums(s).(2 to_power n + m) - Partial_Sums(s).(2 to_power n) +
                s.(2 to_power n + m + 1) by A12;
                then (m + 1) * s.(2 to_power (n+1)) <=
                Partial_Sums(s).(2 to_power n + m) + s.(2 to_power n + m + 1) -
                Partial_Sums(s).(2 to_power n);
                then (m + 1) * s.(2 to_power (n+1)) <=
                Partial_Sums(s).(2 to_power n + (m + 1)) -
                Partial_Sums(s).(2 to_power n) by Def1;
                hence thesis by A12;
              end;
              suppose m>=2 to_power n;
                hence thesis by NAT_1:13;
              end;
            end;
            hence thesis;
          end;
        end;
        hence thesis;
      end;
      for m holds X[m] from NAT_1:sch 1(A13,A14);
      then 2*(2 to_power n * s.(2 to_power (n+1))) <= 2*a.(2 to_power n)
      by XREAL_1:66;
      then 2*2 to_power n * s.(2 to_power (n+1)) <= 2*a.(2 to_power n);

then 2 to_power 1 * 2 to_power n * s.(2 to_power (n+1)) <= 2*a.(2 to_power n)
      by POWER:30;
      then 2 to_power (n+1) * s.(2 to_power (n+1)) <= 2*a.(2 to_power n) by
      POWER:32;
      then s1.(n+1) <= 2*a.(2 to_power n) by A2;
      then
A17:  s1.(n+1) <= 2*(Partial_Sums(s).(2 to_power n + 2 to_power n) -
      Partial_Sums(s).(2 to_power n)) by A12;
A18:  2 to_power n + 2 to_power n = 2 * 2 to_power n
        .= 2 to_power 1 * 2 to_power n by POWER:30
        .= 2 to_power (n+1) by POWER:32;
      2 * Partial_Sums(s).(2 to_power n) + s1.(n+1) <=
      2 * Partial_Sums(s).(2 to_power n) +
      (2*(Partial_Sums(s).(2 to_power n + 2 to_power n) -
      Partial_Sums(s).(2 to_power n))) by A17,XREAL_1:9;
      hence Partial_Sums(s1).(n+1) <= 2 * Partial_Sums(s).(2 to_power (n+1))
      by A11,A18,XXREAL_0:2;
    end;
A19: for n holds Y[n] from NAT_1:sch 1(A5,A10);
    Partial_Sums(s) is bounded_above by A2,A4,Th20;
    then consider r be real number such that
A20: for n holds Partial_Sums(s).n<r by SEQ_2:def 3;
    now
      let n;
A21:  2 * Partial_Sums(s).(2 to_power n) < 2*r by A20,XREAL_1:70;
      Partial_Sums(s1).n <= 2 * Partial_Sums(s).(2 to_power n) by A19;
      hence Partial_Sums(s1).n < 2*r by A21,XXREAL_0:2;
    end;
    then Partial_Sums(s1) is bounded_above by SEQ_2:def 3;
    hence thesis by A3,Th20;
  end;
  assume
A22: s1 is summable;
  defpred Y[Element of NAT] means
  Partial_Sums(s).(2 to_power ($1+1)) <= Partial_Sums(s1).$1 + s.0 + s.2;
A23: Y[0]
  proof
A24: Partial_Sums(s).(2 to_power (0+1)) = Partial_Sums(s).(1+1) by POWER:30
      .= Partial_Sums(s).(0+1)+s.2 by Def1
      .= Partial_Sums(s).0+s.1+s.2 by Def1
      .= s.0+s.1+s.2 by Def1;
A25: 2 to_power 0 = 1 by POWER:29;
    Partial_Sums(s1).0 + s.0 + s.2 = s1.0 + s.0 + s.2 by Def1
      .= 2 to_power 0 * s.(2 to_power 0) + s.0 + s.2 by A2
      .= s.0 + s.1 + s.2 by A25;
    hence thesis by A24;
  end;
A26: for n st Y[n] holds Y[n+1]
  proof
    let n;
    assume Partial_Sums(s).(2 to_power (n+1)) <= Partial_Sums(s1).n + s.0+s.2;
    then Partial_Sums(s).(2 to_power (n+1)) + s1.(n+1) <= s1.(n+1) +
    (Partial_Sums(s1).n + s.0+s.2) by XREAL_1:9;
    then Partial_Sums(s).(2 to_power (n+1)) + s1.(n+1) <=
    Partial_Sums(s1).n + s1.(n+1) + s.0+s.2;
    then
A27: Partial_Sums(s).(2 to_power (n+1)) + s1.(n+1) <=
    Partial_Sums(s1).(n+1) + s.0+s.2 by Def1;
    defpred X[Element of NAT] means Partial_Sums(s).(2 to_power (n+1) + $1) -
    Partial_Sums(s).(2 to_power (n+1)) <= $1 * s.(2 to_power (n+1));
A28: X[0];
A29: for m st X[m] holds X[m+1]
    proof
      let m;
      assume
A30:  Partial_Sums(s).(2 to_power (n+1) + m) -
      Partial_Sums(s).(2 to_power (n+1)) <= m * s.(2 to_power (n+1));
      2 to_power (n+1) + (m+1) >= 2 to_power (n+1) by NAT_1:11;

then s.(2 to_power (n+1) + (m+1)) <= s.(2 to_power (n+1)) by A1,SEQM_3:14;
      then Partial_Sums(s).(2 to_power (n+1) + m) -
      Partial_Sums(s).(2 to_power (n+1)) +
      s.(2 to_power (n+1) + m+1) <= m * s.(2 to_power (n+1)) +
      s.(2 to_power (n+1)) by A30,XREAL_1:9;

then Partial_Sums(s).(2 to_power (n+1) + m) + s.(2 to_power (n+1) + m+1) -
      Partial_Sums(s).(2 to_power (n+1)) <= m * s.(2 to_power (n+1)) +
      s.(2 to_power (n+1));
      hence Partial_Sums(s).(2 to_power (n+1) + (m + 1)) -
      Partial_Sums(s).(2 to_power (n+1)) <=
      (m + 1) * s.(2 to_power (n+1)) by Def1;
    end;
    for m holds X[m] from NAT_1:sch 1(A28,A29);
    then Partial_Sums(s).(2 to_power (n+1) + 2 to_power (n+1)) -
    Partial_Sums(s).(2 to_power (n+1))<=
    2 to_power (n+1) * s.(2 to_power (n+1));
    then
A31: Partial_Sums(s).(2 to_power (n+1) + 2 to_power (n+1)) -
    Partial_Sums(s).(2 to_power (n+1)) <= s1.(n+1) by A2;
    2 to_power (n+1) + 2 to_power (n+1) = 2 * 2 to_power (n+1)
      .= 2 to_power 1 * 2 to_power (n+1) by POWER:30
      .= 2 to_power (n+1+1) by POWER:32;
    then Partial_Sums(s).(2 to_power (n+1+1)) -
    Partial_Sums(s).(2 to_power (n+1)) + Partial_Sums(s).(2 to_power (n+1)) <=
    Partial_Sums(s).(2 to_power (n+1)) + s1.(n+1) by A31,XREAL_1:9;
    hence Partial_Sums(s).(2 to_power (n+1+1)) <=
    Partial_Sums(s1).(n+1) + s.0+s.2 by A27,XXREAL_0:2;
  end;
A32: for n holds Y[n] from NAT_1:sch 1(A23,A26);
A33: Partial_Sums(s) is non-decreasing by A2,Th19;
  Partial_Sums(s1) is bounded_above by A3,A22,Th20;
  then consider r be real number such that
A34: for n holds Partial_Sums(s1).n<r by SEQ_2:def 3;
  now
    let n;
A35: (1+1) to_power n >= 1 + n*1 by POWER:56;
    1+n>=n by NAT_1:11;
    then 2 to_power n >= n by A35,XXREAL_0:2;
    then 2 to_power n + 2 to_power n>= n+n by XREAL_1:9;
    then 2 * 2 to_power n >= n+n;
    then 2 to_power 1 * 2 to_power n >= n+n by POWER:30;
    then
A36: 2 to_power (n+1) >= n+n by POWER:32;
    n+n>=n by NAT_1:11;
    then 2 to_power (n+1) >= n by A36,XXREAL_0:2;
    then
A37: Partial_Sums(s).(2 to_power (n+1)) >= Partial_Sums(s).n by A33,SEQM_3:12;
    Partial_Sums(s).(2 to_power (n+1)) <= Partial_Sums(s1).n+s.0+s.2 by A32;
    then
A38: Partial_Sums(s).n <= Partial_Sums(s1).n+s.0+s.2 by A37,XXREAL_0:2;
    Partial_Sums(s1).n < r by A34;
    then Partial_Sums(s1).n + (s.0 + s.2) < r + (s.0+s.2) by XREAL_1:8;
    hence Partial_Sums(s).n < r + (s.0+s.2) by A38,XXREAL_0:2;
  end;
  then Partial_Sums(s) is bounded_above by SEQ_2:def 3;
  hence thesis by A2,Th20;
end;

theorem
  p>1 & (for n st n>=1 holds s.n = 1/n to_power p) implies s is summable
proof
  assume that
A1: p>1 and
A2: for n st n>=1 holds s.n = 1/n to_power p;
  defpred X[Element of NAT,real number] means
  ($1=0 & $2=1) or ($1>=1 & $2=1/$1 to_power p);
A3: for n ex r being Real st X[n,r]
  proof
    let n;
    n=0 or n>=1
    proof
      assume
A4:   n<>0 & n<1;
      then n>0;
      then n>=0+1 by NAT_1:13;
      hence contradiction by A4;
    end;
    hence thesis;
  end;
  consider s1 such that
A5: for n holds X[n,s1.n] from FUNCT_2:sch 3(A3);
A6: s1.0 = 1 by A5;
  now
    let n;
    now per cases by NAT_1:6;
      suppose
A7:     n=0;
A8:     s1.(n+1) = 1/(n+1) to_power p by A5;
        (n+1) #R p >= 1 by A1,A7,PREPOWER:99;
        then (n+1) to_power p >= 1 by POWER:def 2;
        hence s1.(n+1)<=s1.n by A6,A7,A8,REAL_2:164;
      end;
      suppose ex m be Nat st n=m+1;
        then consider m be Nat such that
A9:     n = m+1;
A10:    n to_power p > 0 by A9,POWER:39;
A11:    n/(n+1)>0 by A9,XREAL_1:141;
A12:    s1.(n+1)/s1.n = (1/(n+1) to_power p)/s1.n by A5
          .= (1/(n+1) to_power p)/(1/n to_power p) by A5,A9
          .= (1/(n+1) to_power p) * n to_power p by XCMPLX_1:101
          .= n to_power p / (n+1) to_power p by XCMPLX_1:100
          .= (n/(n+1)) to_power p by A9,POWER:36
          .= (n/(n+1)) #R p by A11,POWER:def 2;
        1/n to_power p > 0 by A10,REAL_2:127;
        then
A13:    s1.n>0 by A5;
        n<=n+1 by NAT_1:11;
        then n/(n+1)<=1 by A9,REAL_2:143;
        then s1.(n+1)/s1.n <= (n/(n+1)) #R 0 by A1,A11,A12,PREPOWER:98;
        then s1.(n+1)/s1.n <= 1 by A9,PREPOWER:85,XREAL_1:141;
        hence s1.(n+1)<=s1.n by A13,REAL_2:118;
      end;
    end;
    hence s1.(n+1)<=s1.n;
  end;
  then
A14: s1 is non-increasing by SEQM_3:def 14;
  deffunc V(Element of NAT) = 2 to_power $1 * s1.(2 to_power $1);
  consider s2 such that
A15: for n holds s2.n = V(n) from SEQ_1:sch 1;
A16: now
    let n;
    now per cases by NAT_1:6;
      suppose n=0;
        hence s1.n >= 0 by A5;
      end;
      suppose ex m be Nat st n=m+1;
        then consider m be Nat such that
A17:    n = m+1;
        n to_power p > 0 by A17,POWER:39;
        then 1/n to_power p >= 0;
        hence s1.n>=0 by A5;
      end;
    end;
    hence s1.n>=0 & s2.n = 2 to_power n * s1.(2 to_power n) by A15;
  end;
  deffunc U(Element of NAT) = $1-root (s2.$1);
  consider s3 such that
A18: for n holds s3.n = U(n) from SEQ_1:sch 1;
A19: now
    let n;
A20: 2 to_power n > 0 by POWER:39; thus
A21: s2.n = 2 to_power n * s1.(2 to_power n) by A15
      .= 2 to_power n * (1/(2 to_power n) to_power p) by A5,A20
      .= 2 to_power n * (1/2 to_power (n*p)) by POWER:38
      .= 2 to_power n * 2 to_power (-n*p) by POWER:33
      .= 2 to_power (n+-n*p) by POWER:32
      .= 2 to_power ((1-p)*n);
    hence s2.n>=0 by POWER:39;
    s2.n = 2 to_power (1-p) to_power n by A21,POWER:38;
    hence s3.n = n-root (2 to_power (1-p) to_power n) by A18;
  end;
A22: now
    let n;
A23: n+1>=0+1 by XREAL_1:8;
A24: 2 to_power (1-p) <> 0 & 2 to_power (1-p) >= 0 by POWER:39; thus
    (s3^\1).n = s3.(n+1) by SEQM_3:def 9
      .= (n+1)-root (2 to_power (1-p) to_power (n+1)) by A19
      .= 2 to_power (1-p) by A23,A24,POWER:5;
  end;
  then
A25: s3^\1 is constant by SEQM_3:def 6;
  then
A26: s3 is convergent by SEQ_4:35,39;
  lim (s3^\1) = (s3^\1).0 by A25,SEQ_4:41
    .= 2 to_power (1-p) by A22;
  then
A27: lim s3 = 2 to_power (1-p) by A25,SEQ_4:36,39;
  1-p<0 & 2>1 by A1,REAL_2:105;
  then lim s3 < 1 by A27,POWER:41;
  then s2 is summable by A18,A19,A26,Th32;
  then s1 is summable by A14,A16,Th35;
  then
A28: s1^\1 is summable by Th15;
A29: now
    let n;
A30: n+1>=0+1 by XREAL_1:8;
A31: (s^\1).n = s.(n+1) by SEQM_3:def 9
      .= 1/(n+1) to_power p by A2,A30
      .= s1.(n+1) by A5
      .= (s1^\1).n by SEQM_3:def 9;
    s1.(n+1)>=0 by A16;
    hence (s^\1).n>=0 by A31,SEQM_3:def 9;
  end;
  now
    let n;
    assume n>=0;
A32: n+1>=0+1 by XREAL_1:8;
    (s^\1).n = s.(n+1) by SEQM_3:def 9
      .= 1/(n+1) to_power p by A2,A32
      .= s1.(n+1) by A5
      .= (s1^\1).n by SEQM_3:def 9;
    hence (s1^\1).n>=(s^\1).n;
  end;
  then s^\1 is summable by A28,A29,Th22;
  hence thesis by Th16;
end;

theorem
  p<=1 & (for n st n>=1 holds s.n=1/n to_power p) implies s is not summable
proof
  assume that
A1: p<=1 and
A2: for n st n>=1 holds s.n = 1/n to_power p;
  now per cases;
    suppose p<0;
      then
A3:   -p>=0;
      now
        assume s is convergent & lim s=0;
        then consider m such that
A4:     for n st n>= m holds abs(s.n-0)<1 by SEQ_2:def 7;
        consider k such that
A5:     k>m by SEQ_4:10;
        now
          let n such that
A6:       n>=k;
A7:       n>0 by A5,A6;
          then
A8:       n>=0+1 by NAT_1:13;
          n>=m by A5,A6,XXREAL_0:2;
          then abs(s.n-0)<1 by A4;
          then abs(1/n to_power p)<1 by A2,A8;
          then abs(n to_power (-p))<1 by A7,POWER:33;
          then
A9:       n to_power (-p)<1 by ABSVALUE:def 1;
          n #R (-p) >= 1 by A3,A8,PREPOWER:99;
          hence contradiction by A7,A9,POWER:def 2;
        end;
        hence contradiction;
      end;
      hence s is not summable by Th7;
    end;
    suppose
A10:  p>=0;
      defpred X[Element of NAT,real number] means
      ($1=0 & $2=1) or ($1>=1 & $2=1/$1 to_power p);
A11:  for n ex r being Real st X[n,r]
      proof
        let n;
        n=0 or n>=1
        proof
          assume
A12:      n<>0 & n<1;
          then n>0;
          then n>=0+1 by NAT_1:13;
          hence contradiction by A12;
        end;
        hence thesis;
      end;
      consider s1 such that
A13:  for n holds X[n,s1.n] from FUNCT_2:sch 3(A11);
A14:  s1.0 = 1 by A13;
      now
        let n;
        now per cases by NAT_1:6;
          suppose
A15:        n=0;
A16:        s1.(n+1) = 1/(n+1) to_power p by A13;
            (n+1) #R p >= 1 by A10,A15,PREPOWER:99;
            then (n+1) to_power p >= 1 by POWER:def 2;
            hence s1.(n+1)<=s1.n by A14,A15,A16,REAL_2:164;
          end;
          suppose ex m be Nat st n=m+1;
            then consider m be Nat such that
A17:        n = m+1;
A18:        n to_power p > 0 by A17,POWER:39;
A19:        n/(n+1)>0 by A17,XREAL_1:141;
A20:        s1.(n+1)/s1.n = (1/(n+1) to_power p)/s1.n by A13
              .= (1/(n+1) to_power p)/(1/n to_power p) by A13,A17
              .= (1/(n+1) to_power p) * n to_power p by XCMPLX_1:101
              .= n to_power p / (n+1) to_power p by XCMPLX_1:100
              .= (n/(n+1)) to_power p by A17,POWER:36
              .= (n/(n+1)) #R p by A19,POWER:def 2;
            1/n to_power p > 0 by A18,REAL_2:127;
            then
A21:        s1.n>0 by A13;
            n<=n+1 by NAT_1:11;
            then n/(n+1)<=1 by A17,REAL_2:143;
            then s1.(n+1)/s1.n <= (n/(n+1)) #R 0 by A10,A19,A20,PREPOWER:98;
            then s1.(n+1)/s1.n <= 1 by A17,PREPOWER:85,XREAL_1:141;
            hence s1.(n+1)<=s1 .n by A21,REAL_2:118;
          end;
        end;
        hence s1.(n+1)<=s1.n;
      end;
      then
A22:  s1 is non-increasing by SEQM_3:def 14;
      deffunc U(Element of NAT) = 2 to_power $1 * s1.(2 to_power $1);
      consider s2 such that
A23:  for n holds s2.n = U(n) from SEQ_1:sch 1;
A24:  now
        let n;
        now per cases by NAT_1:6;
          suppose n=0;
            hence s1.n >= 0 by A13;
          end;
          suppose ex m be Nat st n=m+1;
            then consider m be Nat such that
A25:        n = m+1;
            n to_power p > 0 by A25,POWER:39;
            then 1/n to_power p >= 0;
            hence s1.n>=0 by A13;
          end;
        end;
        hence s1.n>=0 & s2.n = 2 to_power n * s1.(2 to_power n) by A23;
      end;
      now
        assume s2 is convergent & lim s2=0;
        then consider m such that
A26:    for n st n>=m holds abs(s2.n-0)<1/2 by SEQ_2:def 7;
        now
          let n;
          assume n>=m;
          then abs(s2.n-0)<1/2 by A26;
          then
A27:      abs(2 to_power n * s1.(2 to_power n))<1/2 by A23;
          2 to_power n >= 1 by PREPOWER:19;
then abs(2 to_power n * (1/(2 to_power n) to_power p))<1/2 by A13,A27;
          then abs(2 to_power n * (1/2 to_power (n*p)))<1/2 by POWER:38;
          then abs(2 to_power n * 2 to_power (-n*p))<1/2 by POWER:33;
          then abs(2 to_power (n+-n*p))<1/2 by POWER:32;
          then 2 to_power (n-n*p)<1/2 by ABSVALUE:def 1;
          then 2 to_power (n-n*p)*2<1/2*2 by XREAL_1:70;
          then 2 to_power (n-n*p)*2 to_power 1<1 by POWER:30;
          then
A28:      2 to_power (n-n*p+1)<1 by POWER:32;
          1-p>=0 by A1,XREAL_1:50;
          then n*(1-p)>=0;
          then n - n*p + 1 >= 0+0;
          then 2 #R (n-n*p+1) >= 1 by PREPOWER:99;
          hence contradiction by A28,POWER:def 2;
        end;
        hence contradiction;
      end;
      then not s2 is summable by Th7;
      then
A29:  not s1 is summable by A22,A24,Th35;
      now
        let n;
        assume
A30:    n>=1;
        then s.n = 1/n to_power p by A2
          .= s1.n by A13,A30;
        hence s.n>=s1.n;
      end;
      hence s is not summable by A24,A29,Th22;
    end;
  end;
  hence thesis;
end;

definition
  let s;
  canceled;
  attr s is absolutely_summable means
  :Def5:
  abs(s) is summable;
end;

canceled;

theorem Th39:
  for n,m st n<=m holds abs(Partial_Sums(s).m - Partial_Sums(s).n) <=
  abs(Partial_Sums(abs(s)).m - Partial_Sums(abs(s)).n)
proof
  let n,m such that
A1: n<=m;
  set s1=Partial_Sums(s);
  set s2=Partial_Sums(abs(s));
  defpred X[Element of NAT] means
  abs(s1.(n+$1) - s1.n) <= abs(s2.(n+$1) - s2.n);
A2: X[0];
  now
    let k;
    abs(s.k)>=0 by COMPLEX1:132;
    hence abs(s).k >= 0 by SEQ_1:16;
  end;
  then
A3: s2 is non-decreasing by Th19;
A4: for k st X[k] holds X[k+1]
  proof
    let k;
    assume
A5: abs(s1.(n+k) - s1.n) <= abs(s2.(n+k) - s2.n);
    abs(s1.(n+(k+1)) - s1.n) = abs(s.(n+k+1) + s1.(n+k) - s1.n) by Def1
      .= abs(s.(n+k+1) + (s1.(n+k) - s1.n));
    then
A6: abs(s1.(n+(k+1))-s1.n) <= abs(s.(n+k+1))+abs(s1.(n+k) - s1.n)
    by COMPLEX1:142;
    abs(s.(n+k+1)) + abs(s1.(n+k) - s1.n) <=
    abs(s.(n+k+1)) + abs(s2.(n+k) - s2.n) by A5,XREAL_1:9;
    then
A7: abs(s1.(n+(k+1))-s1.n) <= abs(s.(n+k+1))+abs(s2.(n+k)-s2.n)
    by A6,XXREAL_0:2;
    s2.(n+k)>=s2.n by A3,SEQM_3:11;
    then
A8: s2.(n+k) - s2.n >= 0 by XREAL_1:50;
    then
A9: abs(s1.(n+(k+1))-s1.n) <= abs(s.(n+k+1))+(s2.(n+k)-s2.n) by A7,
    ABSVALUE:def 1;
    abs(s.(n+k+1)) >= 0 by COMPLEX1:132;
    then
A10: abs(s.(n+k+1))+(s2.(n+k)-s2.n) >= 0+0 by A8;
    abs(s2.(n+(k+1)) - s2.n) = abs(s2.(n+k) + (abs(s)).(n+k+1) - s2.n) by Def1
      .= abs(abs(s.(n+k+1)) + s2.(n+k) - s2.n) by SEQ_1:16
      .= abs(abs(s.(n+k+1)) + (s2.(n+k) - s2.n));
    hence abs(s1.(n+(k+1))-s1.n) <= abs(s2.(n+(k+1))-s2.n)
    by A9,A10,ABSVALUE:def 1;
  end;
A11: for k holds X[k] from NAT_1:sch 1(A2,A4);
  reconsider u=n, v=m as Integer;
  reconsider k = v-u as Element of NAT by A1,INT_1:18;
  n+k = m;
  hence thesis by A11;
end;

theorem
  s is absolutely_summable implies s is summable
proof
  assume s is absolutely_summable;
  then
A1: abs(s) is summable by Def5;
  now
    let r;
    assume 0<r;
    then consider n such that
A2: for m st n<=m holds
    abs(Partial_Sums(abs(s)).m - Partial_Sums(abs(s)).n)<r by A1,Th25;
    take n;
    let m;
    assume
A3: n<=m;
    then
A4: abs(Partial_Sums(abs(s)).m - Partial_Sums(abs(s)).n)<r by A2;
    abs(Partial_Sums(s).m - Partial_Sums(s).n) <=
    abs(Partial_Sums(abs(s)).m - Partial_Sums(abs(s)).n) by A3,Th39;
    hence abs( Partial_Sums(s).m - Partial_Sums(s).n)<r by A4,XXREAL_0:2;
  end;
  hence thesis by Th25;
end;

theorem
  (for n holds 0<=s.n) & s is summable implies s is absolutely_summable
proof
  assume that
A1: for n holds 0<=s.n and
A2: s is summable;
A3: Partial_Sums(s) is convergent by A2,Def2;
  now
    let n;
    0<=s.n by A1;
    hence s.n = abs(s.n) by ABSVALUE:def 1;
  end;
  then Partial_Sums(abs(s)) is convergent by A3,SEQ_1:16;
  then abs(s) is summable by Def2;
  hence thesis by Def5;
end;

theorem
  (for n holds s.n<>0 & s1.n=abs(s).(n+1)/abs(s).n) &
  s1 is convergent & lim s1 < 1 implies s is absolutely_summable
proof
  assume that
A1: for n holds s.n<>0 & s1.n=abs(s).(n+1)/abs(s).n and
A2: s1 is convergent & lim s1 < 1;
  now
    let n;
A3: abs(s).n = abs(s.n) by SEQ_1:16;
    s.n <> 0 by A1;
    hence abs(s).n > 0 by A3,COMPLEX1:133;
    thus s1.n=abs(s).(n+1)/abs(s).n by A1;
  end;
  then abs(s) is summable by A2,Th30;
  hence thesis by Def5;
end;

theorem Th43:
  r>0 & (ex m st for n st n>=m holds abs(s.n)>=r) implies
  s is not convergent or lim s <> 0
proof
  assume
A1: r>0;
  given m such that
A2: for n st n>=m holds abs(s.n)>=r;
  now per cases;
    suppose s is not convergent;
      hence thesis;
    end;
    suppose
A3:   s is convergent;
      now
        assume lim s=0;
        then consider k such that
A4:     for n st n>=k holds abs(s.n-0)<r by A1,A3,SEQ_2:def 7;
        now
          let n such that
A5:       n>=m+k;
          m+k>=m by NAT_1:11;
          then
A6:       n>=m by A5,XXREAL_0:2;
          m+k>=k by NAT_1:11;
          then n>=k by A5,XXREAL_0:2;
          then abs(s.n-0)<r by A4;
          hence contradiction by A2,A6;
        end;
        hence contradiction;
      end;
      hence thesis;
    end;
  end;
  hence thesis;
end;

theorem
  (for n holds s.n<>0) &
  (ex m st for n st n>=m holds abs(s).(n+1)/abs(s).n >= 1)
  implies s is not summable
proof
  assume
A1: for n holds s.n<>0;
  given m such that
A2: for n st n>=m holds abs(s).(n+1)/abs(s).n >= 1;
  s.m <> 0 by A1;
  then
A3: abs(s.m) > 0 by COMPLEX1:133;
  now
    let n such that
A4: n>=m;
    defpred X[Element of NAT] means abs(s.(m+$1))>=abs(s.m);
A5: X[0];
A6: for k st X[k] holds X[k+1]
    proof
      let k such that
A7:   abs(s.(m+k))>=abs(s.m);
      abs(s).(m+k+1)/abs(s).(m+k) >= 1 by A2,NAT_1:11;
      then abs(s.(m+k+1))/abs(s).(m+k) >= 1 by SEQ_1:16;
      then
A8:   abs(s.(m+k+1))/abs(s.(m+k)) >= 1 by SEQ_1:16;
      s.(m+k) <> 0 by A1;
      then abs(s.(m+k)) > 0 by COMPLEX1:133;
      then abs(s.(m+k+1))>=abs(s.(m+k)) by A8,REAL_2:118;
      hence thesis by A7,XXREAL_0:2;
    end;
A9: for k holds X[k] from NAT_1:sch 1(A5,A6);
    consider k be Nat such that
A10: n=m+k by A4,NAT_1:10;
    reconsider k as Element of NAT by ORDINAL1:def 13;
    n=m+k by A10;
    hence abs(s.n)>=abs(s.m) by A9;
  end;
  then s is not convergent or lim s <> 0 by A3,Th43;
  hence thesis by Th7;
end;

theorem
  (for n holds s1.n = n-root (abs(s).n)) & s1 is convergent & lim s1 < 1
  implies s is absolutely_summable
proof
  assume that
A1: for n holds s1.n = n-root (abs(s).n) and
A2: s1 is convergent & lim s1 < 1;
  now
    let n;
    abs(s).n = abs(s.n) by SEQ_1:16;
    hence abs(s).n >= 0 by COMPLEX1:132;
    thus s1.n = n-root (abs(s).n) by A1;
  end;
  then abs(s) is summable by A2,Th32;
  hence thesis by Def5;
end;

theorem
  (for n holds s1.n = n-root(abs(s).n)) & (ex m st for n st m<=n holds s1.n >=
  1 ) implies s is not summable
proof
  assume
A1: for n holds s1.n = n-root (abs(s).n);
  given m such that
A2: for n st m<=n holds s1.n>=1;
  now
    let n such that
A3: n>=m+1;
    m+1>=m by NAT_1:11;
    then
A4: n>=m by A3,XXREAL_0:2;
A5: abs(s.n) >= 0 by COMPLEX1:132;
    s1.n = n-root (abs(s).n) by A1
      .= n-root abs(s.n) by SEQ_1:16;
    then
A6: n-root abs(s.n) |^ n >= 1 by A2,A4,PREPOWER:19;
    m+1>=1 by NAT_1:11;
    then n>=1 by A3,XXREAL_0:2;
    hence abs(s.n) >= 1 by A5,A6,POWER:5;
  end;
  then s is not convergent or lim s<>0 by Th43;
  hence thesis by Th7;
end;

theorem
  (for n holds s1.n = n-root (abs(s).n)) & s1 is convergent & lim s1 > 1
  implies s is not summable
proof
  assume that
A1: for n holds s1.n = n-root (abs(s).n) and
A2: s1 is convergent & lim s1 > 1;
  lim s1 - 1 > 0 by A2,XREAL_1:52;
  then consider m such that
A3: for n st n>=m holds abs(s1.n-lim s1)<lim s1-1 by A2,SEQ_2:def 7;
  now
    let n such that
A4: n>=m+1;
    m+1>=m by NAT_1:11;
    then
A5: n>=m by A4,XXREAL_0:2;
A6: abs(s.n) >= 0 by COMPLEX1:132;
    s1.n = n-root (abs(s).n) by A1
      .= n-root abs(s.n) by SEQ_1:16;
    then abs(n-root abs(s.n) - lim s1) < lim s1 - 1 by A3,A5;
    then -(lim s1 - 1) < n-root abs(s.n) - lim s1 by SEQ_2:9;
    then 1 - lim s1 + lim s1 < n-root abs(s.n) - lim s1 + lim s1 by XREAL_1:8;
    then
A7: n-root abs(s.n) |^ n >= 1 by PREPOWER:19;
    m+1>=1 by NAT_1:11;
    then n>=1 by A4,XXREAL_0:2;
    hence abs(s.n) >= 1 by A6,A7,POWER:5;
  end;
  then s is not convergent or lim s<>0 by Th43;
  hence thesis by Th7;
end;

begin :: Addenda

:: from BHSP_4, 2006.03.04, A.T.

definition
  let s;
  let n be Nat;
  func Sum(s, n) -> Real equals

  Partial_Sums(s).n;
  coherence;
end;

definition
  let s;
  let n,m be Nat;
  func Sum(s, n, m) -> Real equals

  Sum(s, n) - Sum(s, m);
  coherence;
end;

