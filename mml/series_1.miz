:: Series
::  by Konrad Raczkowski and Andrzej N\c{e}dzusiak
::
:: Received October 15, 1990
:: Copyright (c) 1990 Association of Mizar Users
 
environ

 vocabularies NUMBERS, SUBSET_1, XREAL_0, ORDINAL1, SEQ_1, ARYTM_3, CARD_1,
      FUNCT_1, POWER, SEQ_2, ORDINAL2, ARYTM_1, XXREAL_0, COMPLEX1, RELAT_1,
      INT_1, NAT_1, REAL_1, CARD_3, VALUED_1, FUNCOP_1, VALUED_0, XXREAL_2,
      PREPOWER, NEWTON, SERIES_1, ABIAN, MEMBERED, XBOOLE_0, IDEAL_1, XCMPLX_0;
 notations XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0, COMPLEX1,
      REAL_1, NAT_1, MEMBERED, RELAT_1, FUNCT_1, VALUED_0, VALUED_1, SEQ_1,
      SEQ_2, FUNCT_2, INT_1, NEWTON, ABIAN, PREPOWER, POWER, FUNCOP_1,
      XXREAL_0;
 constructors FUNCOP_1, XXREAL_0, REAL_1, NAT_1, SEQ_2, SEQM_3, NEWTON,
      PREPOWER, POWER, PARTFUN1, SEQ_1, VALUED_1, SEQ_4, RELSET_1, ABIAN,
      BINOP_2;
 registrations ORDINAL1, RELSET_1, NUMBERS, XXREAL_0, XREAL_0, NAT_1, INT_1,
      MEMBERED, NEWTON, XBOOLE_0, VALUED_0, VALUED_1, FUNCT_2, POWER, FUNCOP_1,
      SEQ_2, SEQ_4, XCMPLX_0;
 requirements REAL, NUMERALS, SUBSET, ARITHM, BOOLE;
 definitions VALUED_1, XCMPLX_0;
 theorems SEQ_1, SEQ_2, SEQM_3, SEQ_4, NAT_1, RFUNCT_2, ABSVALUE, INT_1,
      PREPOWER, POWER, FUNCT_2, NEWTON, XCMPLX_1, FUNCOP_1, XREAL_1, COMPLEX1,
      XXREAL_0, ORDINAL1, VALUED_0, VALUED_1, MEMBERED;
 schemes NAT_1, SEQ_1, FUNCT_2, RECDEF_1;

begin

reserve n,m,k for Element of NAT;
reserve a,p,r for real number;
reserve s,s1,s2,s3 for Real_Sequence;

Lm1: 1/1=1;

theorem Th1:
  0<a & a<1 & (for n holds s.n=a to_power (n+1)) implies
  s is convergent & lim s = 0
proof
  assume that
A1: 0<a and
A2: a<1 and
A3: for n holds s.n=a to_power (n+1);
  set r = 1/a - 1;
  1/a > 1 by A1,A2,Lm1,XREAL_1:90;
  then
A4: r > 0 by XREAL_1:52;
A5: for p be real number st 0 < p ex m st for n st m<=n holds abs(s.n - 0) < p
  proof
    let p be real number;
A6: 1/(p * r) <= [\1/(p * r)/] + 1 by INT_1:52;
    assume
A7: 0<p;
    then p * r > 0 by A4;
    then 1/(p * r) > 0;
    then [\1/(p * r)/] is Element of NAT by A6,INT_1:16,20;
    then consider m such that
A8: m = [\1/(p * r)/];
    take m;
A9: 1/(p * r) = 1/p/r by XCMPLX_1:79;
    now
A10:  [\1/(p * r)/] > 1/(p * r) -1 by INT_1:def 4;
      let n;
      assume m<=n;
      then n > 1/(p * r) - 1 by A8,A10,XXREAL_0:2;
      then n + 1 > 1/p/r by A9,XREAL_1:21;
      then
A11:  (n+1) * r > 1/p by A4,XREAL_1:79;
      0 + (n+1) * r < 1 + (n+1) * r by XREAL_1:8;
      then
A12:  1/p < 1 + (n+1) * r by A11,XXREAL_0:2;
A13:  (1+r) to_power (n+1) = 1 to_power (n+1) / a to_power (n+1) by A1,
      POWER:36
        .= 1 / a to_power (n+1) by POWER:31;
      a to_power (n+1) > 0 by A1,POWER:39;
      then
A14:  abs(a to_power (n+1)) = a to_power (n+1) by ABSVALUE:def 1;
      1 + (n+1) * r <= (1 + r) to_power (n + 1) by A4,POWER:56;
      then 1/p < 1/ a to_power (n+1) by A13,A12,XXREAL_0:2;
      then abs(a to_power (n+1)) < p by A7,A14,XREAL_1:93;
      hence abs(s.n - 0) < p by A3;
    end;
    hence thesis;
  end;
  hence s is convergent by SEQ_2:def 6;
  hence thesis by A5,SEQ_2:def 7;
end;

theorem Th2:
  for n being natural number holds abs(a) to_power n = abs(a to_power n)
proof
  let n be natural number;
  per cases;
  suppose
A1: a <> 0;
    then
A2: abs(a) > 0 by COMPLEX1:133;
    now
      per cases by A1;
      suppose a>0;
        then a to_power n = abs(a) to_power n & a to_power n > 0 by
        ABSVALUE:def 1,POWER:39;
        hence thesis by ABSVALUE:def 1;
      end;
      suppose
A3:     a < 0;
        reconsider m=n as Integer;
        now
          per cases;
          suppose
A5:         m is even;
A6:         abs(a) to_power n > 0 by A2,POWER:39;
            abs(a) to_power n = (-a) to_power m by A3,ABSVALUE:def 1
              .= a to_power m by A5,POWER:54;
            hence thesis by A6,ABSVALUE:def 1;
          end;
          suppose
A7:         m is odd;
A8:         abs(a) to_power n > 0 by A2,POWER:39;
            abs(a) to_power n = (-a) to_power m by A3,ABSVALUE:def 1
              .= - a to_power m by A7,POWER:55;
            hence abs(a) to_power n = abs( - a to_power n ) by A8,ABSVALUE
            :def 1
              .= abs( a to_power n ) by COMPLEX1:138;
          end;
        end;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
  suppose
A9: a = 0;
    per cases;
    suppose n = 0;
      then a to_power n = 1 by NEWTON:9;
      hence thesis by A9,COMPLEX1:130,134;
    end;
    suppose n > 0;
      then abs(a) to_power n = 0 by A9,COMPLEX1:130,POWER:def 2;
      hence thesis by A9,COMPLEX1:130;
    end;
  end;
end;

theorem Th3:
  abs(a)<1 & (for n holds s.n=a to_power (n+1)) implies s is
  convergent & lim s = 0
proof
  assume that
A1: abs(a) < 1 and
A2: for n holds s.n=a to_power (n+1);
  now
    per cases;
    suppose
      abs(a) = 0;
      then
A3:   a = 0 by ABSVALUE:7;
      now
        let n be Nat;
        n in NAT & a to_power (n+1) = 0 by A3,ORDINAL1:def 13,POWER:def 2;
        hence s.n = 0 by A2;
      end;
      then s is constant & s.0 = 0 by VALUED_0:def 18;
      hence thesis by SEQ_4:41;
    end;
    suppose
A4:   not abs(a) = 0;
      deffunc U(Element of NAT) = abs(a) to_power ($1+1);
      consider s1 such that
A5:   for n holds s1.n = U(n) from SEQ_1:sch 1;
A6:   now
        let n;
        thus s1.n = abs(a) to_power (n+1) by A5
          .= abs(a to_power (n+1)) by Th2
          .= abs(s.n) by A2;
      end;
A7:   abs(a) > 0 by A4,COMPLEX1:132;
      then lim s1 = 0 by A1,A5,Th1;
      then
A8:   lim abs(s) = 0 by A6,SEQ_1:16;
      s1 is convergent by A1,A7,A5,Th1;
      then abs(s) is convergent by A6,SEQ_1:16;
      hence thesis by A8,SEQ_4:28;
    end;
  end;
  hence thesis;
end;

definition
  let X be non empty add-closed complex-membered set;
  let s1, s2 be sequence of X;
  redefine func s1+s2 -> sequence of X;
  coherence
  proof
a1: dom(s1+s2) = dom s1 /\ dom s2 by VALUED_1:def 1
    .= NAT /\ dom s2 by FUNCT_2:def 1
    .= NAT /\ NAT by FUNCT_2:def 1;
    now
      let x be set such that
a2:   x in NAT;
a3:   s1.x in X & s2.x in X by a2,FUNCT_2:7;
      (s1+s2).x = s1.x + s2.x by a1,a2,VALUED_1:def 1;
      hence (s1+s2).x in X by a3,MEMBERED:def 25;
    end;
    hence thesis by a1,FUNCT_2:5;
  end;
end;

definition
  let X be complex-membered set;
  let s be sequence of X such that
a0: X is non empty add-closed;
  func Partial_Sums(s) -> sequence of X means
  :Def1:
  it.0 = s.0 & for n holds it.(n+1) = it.n + s.(n+1);
  existence
  proof
    reconsider X as non empty complex-membered set by a0;
    reconsider s as sequence of X;
    defpred P[Nat,Element of X,set] means $3 = $2 + s.($1+1);
a1: for n being Element of NAT for x being Element of X
    ex y being Element of X st P[n,x,y]
    proof
      let n be Element of NAT;
      let x be Element of X;
      reconsider y = x + s.(n+1) as Element of X by a0,MEMBERED:def 25;
      take y;
      thus P[n,x,y];
    end;
    ex f being Function of NAT,X st f.0 = s.0 &
    for n being Element of NAT holds P[n,f.n,f.(n+1)] from RECDEF_1:sch 2(a1);
    hence thesis;
  end;
  uniqueness
  proof
    let s1,s2 be sequence of X;
    assume that
A2: s1.0=s.0 and
A3: for n holds s1.(n+1)=s1.n + s.(n+1) and
A4: s2.0=s.0 and
A5: for n holds s2.(n+1)=s2.n + s.(n+1);
    defpred X[Element of NAT] means s1.$1 = s2.$1;
A6: for k st X[k] holds X[k+1]
    proof
      let k;
      assume s1.k =s2.k;
      hence s1.(k+1) = s2.k + s.(k+1) by A3
        .= s2.(k+1) by A5;
    end;
A7: X[0] by A2,A4;
    for n holds X[n] from NAT_1:sch 1(A7,A6);
    hence s1 = s2 by FUNCT_2:113;
  end;
end;

definition
  let s;
  attr s is summable means
  :Def2:
  Partial_Sums(s) is convergent;
  func Sum(s) -> Real equals
  lim Partial_Sums(s);
  correctness;
end;

canceled 3;

theorem Th7:
  s is summable implies s is convergent & lim s = 0
proof
  assume s is summable;
  then
A1: Partial_Sums(s) is convergent by Def2;
  now
    let n;
    (Partial_Sums(s)).(n+1) = (Partial_Sums(s)).n + s.(n+1) by Def1;
    then (Partial_Sums(s)).(n+1) = (Partial_Sums(s)).n + (s ^\1).n by
    NAT_1:
    def 3;
    hence (Partial_Sums(s) ^\1).n = (Partial_Sums(s)).n + (s ^\1).n by NAT_1
    :def 3;
  end;
  then
A2: (Partial_Sums(s) ^\1) = Partial_Sums(s) + s ^\1 by SEQ_1:11;
  now
    let n;
    thus (s ^\1 + (Partial_Sums(s) - Partial_Sums(s))).n = (s ^\1).n + (
    Partial_Sums(s) +- Partial_Sums(s)).n by SEQ_1:11
      .= (s ^\1).n + ((Partial_Sums(s)).n + (-Partial_Sums(s)).n) by SEQ_1:
    11
      .= (s ^\1).n + ((Partial_Sums(s)).n +- (Partial_Sums(s)).n) by SEQ_1:
    14
      .= (s ^\1).n;
  end;
  then s ^\1 + (Partial_Sums(s) - Partial_Sums(s)) = s ^\1 by
  FUNCT_2:113;
  then
A3: s ^\1 = Partial_Sums(s) ^\1 - Partial_Sums(s) by A2,SEQ_1:39;
  then
A4: s ^\1 is convergent by A1,SEQ_2:25;
  hence s is convergent by SEQ_4:35;
  lim(Partial_Sums(s) ^\1) = lim(Partial_Sums(s)) by A1,SEQ_4:33;
  then
  lim(Partial_Sums(s) ^\1 - Partial_Sums(s)) = lim(Partial_Sums(s)) - lim(
  Partial_Sums(s)) by A1,SEQ_2:26
    .= 0;
  hence thesis by A3,A4,SEQ_4:36;
end;

Th11a:
  for X being non empty add-closed (complex-membered set)
  for seq,seq1,seq2 being sequence of X holds
  seq = seq1 + seq2 iff for n holds seq.n =seq1.n + seq2.n
proof
  let X be non empty add-closed (complex-membered set);
  let seq,seq1,seq2 be sequence of X;
  thus seq = seq1 + seq2 implies for n holds seq.n =seq1.n + seq2.n
  proof
    assume
A1: seq = seq1 + seq2;
    let n;
    dom seq = NAT by FUNCT_2:def 1;
    hence thesis by A1,VALUED_1:def 1;
  end;
a1: dom seq = NAT by FUNCT_2:def 1;
  assume for n holds seq.n =seq1.n + seq2.n;
  then
A2: for n being set st n in dom seq holds seq.n = seq1.n + seq2.n by a1;
  dom seq = NAT /\ NAT by FUNCT_2:def 1
    .= NAT /\ dom seq2 by FUNCT_2:def 1
    .= dom seq1 /\ dom seq2 by FUNCT_2:def 1;
  hence thesis by A2,VALUED_1:def 1;
end;

theorem Th8:
  for X being non empty add-closed (complex-membered set)
  for s1,s2 being sequence of X holds
  Partial_Sums(s1) + Partial_Sums(s2) = Partial_Sums(s1+s2)
proof
  let X be non empty add-closed (complex-membered set);
  let s1,s2 be sequence of X;
A1: now
    let n;
    thus (Partial_Sums(s1) + Partial_Sums(s2)).(n+1) = Partial_Sums(s1).(n+1)
    + Partial_Sums(s2).(n+1) by Th11a
      .= Partial_Sums(s1).n + s1.(n+1) + Partial_Sums(s2).(n+1) by Def1
      .= Partial_Sums(s1).n+s1.(n+1)+(s2.(n+1)+Partial_Sums(s2).n) by Def1
      .= Partial_Sums(s1).n+(s1.(n+1)+s2.(n+1))+Partial_Sums(s2).n
      .= Partial_Sums(s1).n+(s1+s2).(n+1)+Partial_Sums(s2).n by Th11a
      .= Partial_Sums(s1).n+Partial_Sums(s2).n+(s1+s2).(n+1)
      .= (Partial_Sums(s1)+Partial_Sums(s2)).n+(s1+s2).(n+1) by Th11a;
  end;
  (Partial_Sums(s1) + Partial_Sums(s2)).0 = Partial_Sums(s1).0 +
  Partial_Sums(s2).0 by Th11a
    .= s1.0 + Partial_Sums(s2).0 by Def1
    .= s1.0 + s2.0 by Def1
    .= (s1+s2).0 by Th11a;
  hence thesis by A1,Def1;
end;

theorem Th9:
  Partial_Sums(s1) - Partial_Sums(s2) = Partial_Sums(s1-s2)
proof
A1: now
    let n;
    thus (Partial_Sums(s1) - Partial_Sums(s2)).(n+1) = Partial_Sums(s1).(n+1)
    - Partial_Sums(s2).(n+1) by RFUNCT_2:6
      .= (Partial_Sums(s1).n + s1.(n+1)) - Partial_Sums(s2).(n+1) by Def1
      .= (Partial_Sums(s1).n+s1.(n+1))-(s2.(n+1)+Partial_Sums(s2).n) by Def1
      .= Partial_Sums(s1).n+(s1.(n+1)-s2.(n+1))-Partial_Sums(s2).n
      .= (s1-s2).(n+1)+Partial_Sums(s1).n-Partial_Sums(s2).n by RFUNCT_2:6
      .= (s1-s2).(n+1)+(Partial_Sums(s1).n-Partial_Sums(s2).n)
      .= (Partial_Sums(s1)-Partial_Sums(s2)).n+(s1-s2).(n+1) by RFUNCT_2:6;
  end;
  (Partial_Sums(s1) - Partial_Sums(s2)).0 = Partial_Sums(s1).0 -
  Partial_Sums(s2).0 by RFUNCT_2:6
    .= s1.0 - Partial_Sums(s2).0 by Def1
    .= s1.0 - s2.0 by Def1
    .= (s1-s2).0 by RFUNCT_2:6;
  hence thesis by A1,Def1;
end;

theorem
  s1 is summable & s2 is summable implies s1+s2 is summable &
  Sum(s1+s2) = Sum(s1) + Sum(s2)
proof
  assume s1 is summable & s2 is summable;
  then
A1: Partial_Sums(s1) is convergent & Partial_Sums(s2) is convergent by
  Def2;
  then Partial_Sums(s1) + Partial_Sums(s2) is convergent by SEQ_2:19;
  then Partial_Sums(s1+s2) is convergent by Th8;
  hence s1+s2 is summable by Def2;
  thus Sum(s1+s2) =lim (Partial_Sums(s1) + Partial_Sums(s2)) by Th8
    .=Sum(s1) + Sum(s2) by A1,SEQ_2:20;
end;

theorem
  s1 is summable & s2 is summable implies s1-s2 is summable & Sum(s1-s2)
  = Sum(s1) - Sum(s2)
proof
  assume s1 is summable & s2 is summable;
  then
A1: Partial_Sums(s1) is convergent & Partial_Sums(s2) is convergent by
  Def2;
  then Partial_Sums(s1) - Partial_Sums(s2) is convergent by SEQ_2:25;
  then Partial_Sums(s1-s2) is convergent by Th9;
  hence s1-s2 is summable by Def2;
  thus Sum(s1-s2) =lim (Partial_Sums(s1) - Partial_Sums(s2)) by Th9
    .=Sum(s1) - Sum(s2) by A1,SEQ_2:26;
end;

theorem Th12:
  Partial_Sums(r(#)s) = r(#)Partial_Sums(s)
proof
A1: now
    let n;
    thus (r(#)Partial_Sums(s)).(n+1) = r*Partial_Sums(s).(n+1) by SEQ_1:13
      .= r*(Partial_Sums(s).n + s.(n+1)) by Def1
      .= r*Partial_Sums(s).n + r*s.(n+1)
      .= (r(#)Partial_Sums(s)).n + r*s.(n+1) by SEQ_1:13
      .= (r(#)Partial_Sums(s)).n + (r(#)s).(n+1) by SEQ_1:13;
  end;
  (r(#)Partial_Sums(s)).0 = r*Partial_Sums(s).0 by SEQ_1:13
    .= r*s.0 by Def1
    .= (r(#)s).0 by SEQ_1:13;
  hence thesis by A1,Def1;
end;

theorem Th13:
  s is summable implies r(#)s is summable & Sum(r(#)s) =r*Sum(s)
proof
  assume s is summable;
  then
A1: Partial_Sums(s) is convergent by Def2;
  then r(#)Partial_Sums(s) is convergent by SEQ_2:21;
  then Partial_Sums(r(#)s) is convergent by Th12;
  hence r(#)s is summable by Def2;
  thus Sum(r(#)s) =lim (r(#)Partial_Sums(s)) by Th12
    .=r*Sum(s) by A1,SEQ_2:22;
end;

theorem Th14:
  for s,s1 st for n holds s1.n=s.0 holds Partial_Sums(s^\1) = (
  Partial_Sums(s)^\1) - s1
proof
  let s,s1;
  assume
A1: for n holds s1.n=s.0;
A2: now
    let k;
    thus ((Partial_Sums(s)^\1) - s1).(k+1) = (Partial_Sums(s)^\1).(k+1) - s1.(
    k+1) by RFUNCT_2:6
      .= (Partial_Sums(s)^\1).(k+1) - s.0 by A1
      .= Partial_Sums(s).(k+1+1) - s.0 by NAT_1:def 3
      .= s.(k+1+1) + Partial_Sums(s).(k+1) - s.0 by Def1
      .= s.(k+1+1) + Partial_Sums(s).(k+1) - s1.k by A1
      .= s.(k+1+1) + (Partial_Sums(s).(k+1) - s1.k)
      .= s.(k+1+1) + ((Partial_Sums(s)^\1).k - s1.k) by NAT_1:def 3
      .= s.(k+1+1) + ((Partial_Sums(s)^\1) - s1).k by RFUNCT_2:6
      .= ((Partial_Sums(s)^\1) - s1).k + (s^\1).(k+1) by NAT_1:def 3;
  end;
  ((Partial_Sums(s)^\1) - s1).0 = (Partial_Sums(s)^\1).0 - s1.0 by
  RFUNCT_2:6
    .= (Partial_Sums(s)^\1).0 - s.0 by A1
    .= Partial_Sums(s).(0+1) - s.0 by NAT_1:def 3
    .= Partial_Sums(s).0 + s.(0+1) - s.0 by Def1
    .= s.(0+1) + s.0 - s.0 by Def1
    .= (s^\1).0 by NAT_1:def 3;
  hence thesis by A2,Def1;
end;

theorem Th15:
  s is summable implies for n holds s^\n is summable
proof
  defpred X[Element of NAT] means s^\$1 is summable;
A1: for n st X[n] holds X[n+1]
  proof
    let n;
    reconsider s1 = NAT --> (s^\n).0 as Real_Sequence;
    for k holds s1.k = (s^\n).0 by FUNCOP_1:13;
    then
A2: Partial_Sums(s^\n^\1) = (Partial_Sums(s^\n)^\1) - s1 by Th14;
    assume s^\n is summable;
    then Partial_Sums(s^\n) is convergent by Def2;
    then s^\(n+1)=(s^\n)^\1 & Partial_Sums(s^\n^\1) is convergent by A2,
    NAT_1:49 ,SEQ_2:25;
    hence thesis by Def2;
  end;
  assume s is summable;
  then
A3: X[0] by NAT_1:48;
  thus for n holds X[n] from NAT_1:sch 1(A3,A1);
end;

theorem Th16:
  (ex n st s^\n is summable) implies s is summable
proof
  given n such that
A1: s^\n is summable;
  reconsider s1 = NAT --> Partial_Sums(s).n as Real_Sequence;
  for k holds (Partial_Sums(s)^\(n+1)).k = Partial_Sums(s^\(n+1)).k + s1.k
  proof
    defpred X[Element of NAT] means (Partial_Sums(s)^\(n+1)).$1 = Partial_Sums
    (s^\(n+1)).$1 + s1.$1;
A2: Partial_Sums(s^\(n+1)).0 = (s^\(n+1)).0 by Def1
      .= s.(0+(n+1)) by NAT_1:def 3
      .= s.(n+1);
A3: for k st X[k] holds X[k+1]
    proof
      let k;
      assume
A4:   (Partial_Sums(s)^\(n+1)).k = Partial_Sums(s^\(n+1)).k + s1.k;
      Partial_Sums(s^\(n+1)).(k+1) + s1.(k+1) = s1.(k+1) + (Partial_Sums(
      s^\(n+1)).k + (s^\(n+1)).(k+1)) by Def1
        .= s1.(k+1) + Partial_Sums(s^\(n+1)).k + (s^\(n+1)).(k+1)
        .= Partial_Sums(s).n + Partial_Sums(s^\(n+1)).k + (s^\(n+1)).(k+1)
      by FUNCOP_1:13
        .= (Partial_Sums(s)^\(n+1)).k + (s^\(n+1)).(k+1) by A4,FUNCOP_1:13
        .= Partial_Sums(s).(k+(n+1)) + (s^\(n+1)).(k+1) by NAT_1:def 3
        .= Partial_Sums(s).(k+(n+1)) + s.(k+1+(n+1)) by NAT_1:def 3
        .= Partial_Sums(s).(k+(n+1)+1) by Def1
        .= Partial_Sums(s).(k+1+(n+1))
        .= (Partial_Sums(s)^\(n+1)).(k+1) by NAT_1:def 3;
      hence thesis;
    end;
    (Partial_Sums(s)^\(n+1)).0 = Partial_Sums(s).(0+(n+1)) by NAT_1:def 3
      .= s.(n+1) + Partial_Sums(s).n by Def1
      .= Partial_Sums(s^\(n+1)).0 + s1.0 by A2,FUNCOP_1:13;
    then
A5: X[0];
    thus for k holds X[k] from NAT_1:sch 1(A5,A3);
  end;
  then
A6: Partial_Sums(s)^\(n+1) = Partial_Sums(s^\(n+1)) + s1 by SEQ_1:11
    .= Partial_Sums((s^\n)^\1) + s1 by NAT_1:49;
  s^\n^\1 is summable by A1,Th15;
  then Partial_Sums(s^\n^\1) is convergent by Def2;
  then Partial_Sums(s)^\(n+1) is convergent by A6,SEQ_2:19;
  then Partial_Sums(s) is convergent by SEQ_4:35;
  hence thesis by Def2;
end;

theorem Th17:
  (for n holds s1.n<=s2.n) implies for n holds Partial_Sums(s1).n
  <=Partial_Sums(s2).n
proof
  defpred X[Element of NAT] means Partial_Sums(s1).$1 <= Partial_Sums(s2).$1;
  assume
A1: for n holds s1.n<=s2.n;
A2: for n st X[n] holds X[n+1]
  proof
    let n such that
A3: Partial_Sums(s1).n <= Partial_Sums(s2).n;
A4: s1.(n+1)<=s2.(n+1) by A1;
    Partial_Sums(s1).(n+1) = Partial_Sums(s1).n + s1.(n+1) & Partial_Sums(
    s2).(n +1) = Partial_Sums(s2).n + s2.(n+1) by Def1;
    hence thesis by A3,A4,XREAL_1:9;
  end;
  Partial_Sums(s2).0 = s2.0 & Partial_Sums(s1).0 = s1.0 by Def1;
  then
A5: X[0] by A1;
  thus for n holds X[n] from NAT_1:sch 1(A5,A2);
end;

theorem
  s is summable implies for n holds Sum(s) = Partial_Sums(s).n + Sum(s^\
  (n+1 ) )
proof
  defpred X[Element of NAT] means Sum(s) = Partial_Sums(s).$1 + Sum(s^\($1+1));
  assume
A1: s is summable;
A2: for n st X[n] holds X[n+1]
  proof
    let n;
    assume
A3: Sum(s) = Partial_Sums(s).n + Sum(s^\(n+1));
    reconsider s1 = NAT --> (s^\(n+1)).0 as Real_Sequence;
    for k holds s1.k= (s^\(n+1)).0 by FUNCOP_1:13;
    then
A4: Partial_Sums(s^\(n+1)^\1) = (Partial_Sums(s^\(n+1))^\1) - s1 by
    Th14;
    s^\(n+1) is summable by A1,Th15;
    then
A5: Partial_Sums(s^\(n+1)) is convergent by Def2;
    lim Partial_Sums(s^\(n+1+1)) = lim Partial_Sums(s^\(n+1)^\1) by NAT_1
    :49
      .= lim (Partial_Sums(s^\(n+1))^\1) - lim s1 by A5,A4,SEQ_2:26
      .= lim Partial_Sums(s^\(n+1)) - lim s1 by A5,SEQ_4:33
      .= Sum(s^\(n+1)) - s1.0 by SEQ_4:41
      .= Sum(s^\(n+1)) - (s^\(n+1)).0 by FUNCOP_1:13;
    then Sum(s^\(n+1+1)) = Sum(s) - (Partial_Sums(s).n + (s^\(n+1)).0) by A3
      .= Sum(s) - (Partial_Sums(s).n + s.(0+(n+1))) by NAT_1:def 3
      .= Sum(s) - Partial_Sums(s).(n+1) by Def1;
    hence thesis;
  end;
A6: X[0]
  proof
    reconsider s1 = NAT --> s.0 as Real_Sequence;
A7: Partial_Sums(s) is convergent by A1,Def2;
    for k holds s1.k= s.0 by FUNCOP_1:13;
    then Partial_Sums(s^\1) = (Partial_Sums(s)^\1) - s1 by Th14;
    then lim Partial_Sums(s^\1) = lim (Partial_Sums(s)^\1) - lim s1 by A7,
    SEQ_2:26
      .= lim Partial_Sums(s) - lim s1 by A7,SEQ_4:33
      .= Sum(s) - s1.0 by SEQ_4:41
      .= Sum(s) - s.0 by FUNCOP_1:13;
    then Sum(s) = Sum(s^\1) +(-(-s.0));
    hence thesis by Def1;
  end;
  thus for n holds X[n] from NAT_1:sch 1(A6,A2);
end;

theorem Th19:
  (for n holds 0<=s.n) implies Partial_Sums(s) is non-decreasing
proof
  assume
A1: for n holds 0<=s.n;
  now
    let n;
    0<=s.(n+1) by A1;
    then 0 + Partial_Sums(s).n <= s.(n+1) + Partial_Sums(s).n by
    XREAL_1:8;
    hence Partial_Sums(s).n <= Partial_Sums(s).(n+1) by Def1;
  end;
  hence thesis by SEQM_3:def 13;
end;

theorem Th20:
  (for n holds 0<=s.n) implies (Partial_Sums(s) is bounded_above
  iff s is summable)
proof
  assume for n holds 0<=s.n;
  then Partial_Sums(s) is non-decreasing by Th19;
  hence Partial_Sums(s) is bounded_above implies s is summable by Def2;
  assume s is summable;
  then Partial_Sums(s) is convergent by Def2;
  then Partial_Sums(s) is bounded by SEQ_2:27;
  hence thesis;
end;

theorem
  s is summable & (for n holds 0<=s.n) implies 0<=Sum(s)
proof
  assume that
A1: s is summable and
A2: for n holds 0<=s.n;
A3: now
    let n;
    Partial_Sums(s).0<=Partial_Sums(s).n & 0<=s.0 by A2,Th19,SEQM_3:21;
    hence 0<=Partial_Sums(s).n by Def1;
  end;
  Partial_Sums(s) is convergent by A1,Def2;
  hence thesis by A3,SEQ_2:31;
end;

theorem Th22:
  (for n holds 0<=s2.n) & s1 is summable & (ex m st for n st m<=n
  holds s2.n<=s1.n) implies s2 is summable
proof
  assume that
A1: for n holds 0<=s2.n and
A2: s1 is summable;
  given m such that
A3: for n st m<=n holds s2.n<=s1.n;
A4: now
    let n;
    (s1^\m).n = s1.(n+m) & 0<= s2.(n+m) by A1,NAT_1:def 3;
    hence 0<=(s1^\m).n by A3,NAT_1:11;
  end;
  s1^\m is summable by A2,Th15;
  then Partial_Sums(s1^\m) is bounded_above by A4,Th20;
  then consider r being real number such that
A5: for n holds Partial_Sums(s1^\m).n<r by SEQ_2:def 3;
A6: now
    let n;
    s2.(n+m)<=s1.(n+m) by A3,NAT_1:12;
    then (s2^\m).n<=s1.(n+m) by NAT_1:def 3;
    hence (s2^\m).n<=(s1^\m).n by NAT_1:def 3;
  end;
  now
    let n;
    Partial_Sums(s2^\m).n <= Partial_Sums(s1^\m).n by A6,Th17;
    hence Partial_Sums(s2^\m).n<r by A5,XXREAL_0:2;
  end;
  then
A7: Partial_Sums(s2^\m) is bounded_above by SEQ_2:def 3;
  now
    let n;
    (s2^\m).n = s2.(n+m) by NAT_1:def 3;
    hence 0<=(s2^\m).n by A1;
  end;
  then s2^\m is summable by A7,Th20;
  hence thesis by Th16;
end;

canceled;

theorem
  (for n holds 0<=s1.n & s1.n<=s2.n) & s2 is summable implies s1 is
  summable & Sum(s1)<=Sum(s2)
proof
  assume that
A1: for n holds 0<=s1.n & s1.n<=s2.n and
A2: s2 is summable;
  for n holds 0<=n implies s1.n<=s2.n by A1;
  hence s1 is summable by A1,A2,Th22;
  then
A3: Partial_Sums(s1) is convergent by Def2;
  Partial_Sums(s2) is convergent & for n holds Partial_Sums(s1).n<=
  Partial_Sums(s2).n by A1,A2,Def2,Th17;
  hence thesis by A3,SEQ_2:32;
end;

theorem Th25:
  s is summable iff for r st 0<r ex n st for m st n<=m holds abs(
  Partial_Sums(s).m - Partial_Sums(s).n)<r
proof
  thus s is summable implies for r st 0<r ex n st for m st n<=m holds abs(
  Partial_Sums(s).m - Partial_Sums(s).n)<r
  proof
    assume s is summable;
    then Partial_Sums(s) is convergent by Def2;
    hence thesis by SEQ_4:58;
  end;
  assume for r st 0<r ex n st for m st n<=m holds abs(Partial_Sums(s).m -
  Partial_Sums(s).n)<r;
  then Partial_Sums(s) is convergent by SEQ_4:58;
  hence thesis by Def2;
end;

theorem Th26:
  a <> 1 implies Partial_Sums(a GeoSeq).n = (1 - a to_power (n+1)) /(1-a)
proof
  defpred X[Element of NAT] means Partial_Sums(a GeoSeq).$1 = (1-a to_power (
  $1+1))/(1-a);
  assume a<>1;
  then
A1: 1-a <> 0;
A2: for n st X[n] holds X[n+1]
  proof
    let n;
    assume Partial_Sums(a GeoSeq).n = (1 - a to_power (n+1))/(1-a);
    hence Partial_Sums(a GeoSeq).(n+1) = (1 - a to_power (n+1))/(1-a) + a
    GeoSeq.(n+1) by Def1
      .= (1 - a to_power (n+1))/(1-a) + a to_power (n+1) * 1 by PREPOWER:def
    1
      .= (1-a to_power (n+1))/(1-a)+a to_power (n+1)*((1-a)/(1-a)) by A1,
    XCMPLX_1:60
      .= (1-a to_power (n+1))/(1-a)+(a to_power (n+1)*(1-a))/(1-a)
      .= (1-a to_power (n+1) + (a to_power (n+1)-a |^ (n+1)*a))/(1-a)
      .= (1-a to_power (n+1) + (a to_power (n+1)-a |^ (n+1+1)))/(1-a) by
    NEWTON:11
      .= (1 - a to_power (n+1+1))/(1-a);
  end;
  Partial_Sums(a GeoSeq).0 = a GeoSeq.0 by Def1
    .= 1 by PREPOWER:4
    .= (1-a)/(1-a) by A1,XCMPLX_1:60
    .= (1-a to_power (0+1))/(1-a) by POWER:30;
  then
A3: X[0];
  for n holds X[n] from NAT_1:sch 1(A3,A2);
  hence thesis;
end;

theorem
  a <> 1 & (for n holds s.(n+1) = a * s.n) implies for n holds
  Partial_Sums(s).n = s.0 * (1 - a to_power (n+1))/(1-a)
proof
  assume that
A1: a <> 1 and
A2: for n holds s.(n+1) = a * s.n;
  defpred X[Element of NAT] means s.$1 = s.0 * a GeoSeq.$1;
A3: for n st X[n] holds X[n+1]
  proof
    let n;
    assume s.n = s.0 * a GeoSeq.n;
    then s.(n+1) = a * (s.0 * a GeoSeq.n) by A2
      .= s.0 * (a GeoSeq.n * a)
      .= s.0 * a GeoSeq.(n+1) by PREPOWER:4;
    hence thesis;
  end;
  a GeoSeq.0 = 1 by PREPOWER:4;
  then
A4: X[0];
  for n holds X[n] from NAT_1:sch 1(A4,A3);
  then s = s.0 (#) a GeoSeq by SEQ_1:13;
  then
A5: Partial_Sums(s) = s.0 (#) Partial_Sums(a GeoSeq) by Th12;
  now
    let n;
    thus Partial_Sums(s).n = s.0 * Partial_Sums(a GeoSeq).n by A5,SEQ_1:13
      .= s.0 * ((1 - a to_power (n+1))/(1-a)) by A1,Th26
      .= s.0 * (1 - a to_power (n+1))/(1-a);
  end;
  hence thesis;
end;

theorem Th28:
  abs(a)<1 implies a GeoSeq is summable & Sum(a GeoSeq) = 1/(1-a)
proof
  deffunc U(Element of NAT) = a to_power ($1+1);
  consider s such that
A1: for n holds s.n = U(n) from SEQ_1:sch 1;
  assume
A2: abs(a)<1;
  then
A3: s is convergent & lim s = 0 by A1,Th3;
A4: now
    a<1 by A2,SEQ_2:9;
    then
A5: 1-a>0 by XREAL_1:52;
    let r be real number;
    assume r>0;
    then r*(1-a)>0*r by A5;
    then consider m such that
A6: for n st n>=m holds abs(s.n - 0)<r*(1-a) by A3,SEQ_2:def 7;
    take m;
    let n;
    assume n>=m;
    then abs(s.n - 0)<r*(1-a) by A6;
    then abs(a to_power (n+1) - 0)<r*(1-a) by A1;
    then
A7: abs(a to_power (n+1))/(1-a)<r*(1-a)/(1-a) by A5,XREAL_1:76;
    a<>1 by A2,SEQ_2:9;
    then
A8: abs(Partial_Sums(a GeoSeq).n - 1/(1-a)) = abs((1 - a to_power (n+1))/
    (1-a) - 1/(1-a)) by Th26
      .= abs((1 +- a to_power (n+1) - 1)/(1-a))
      .= abs(- a to_power (n+1)/(1-a))
      .= abs(a to_power (n+1)/(1-a)) by COMPLEX1:138
      .= abs(a to_power (n+1))/abs((1-a)) by COMPLEX1:153
      .= abs(a to_power (n+1))/(1-a) by A5,ABSVALUE:def 1;
    1-a<>0 by A2,SEQ_2:9;
    hence abs(Partial_Sums(a GeoSeq).n - 1/(1-a)) < r by A8,A7, XCMPLX_1:
    90;
  end;
  then
A9: Partial_Sums(a GeoSeq) is convergent by SEQ_2:def 6;
  hence a GeoSeq is summable by Def2;
  thus thesis by A4,A9,SEQ_2:def 7;
end;

theorem
  abs(a) < 1 & (for n holds s.(n+1) = a * s.n) implies s is summable &
  Sum(s) = s.0/(1-a)
proof
  assume that
A1: abs(a) < 1 and
A2: for n holds s.(n+1) = a * s.n;
  defpred X[Element of NAT] means s.$1 = s.0 * a GeoSeq.$1;
A3: for n st X[n] holds X[n+1]
  proof
    let n;
    assume s.n = s.0 * a GeoSeq.n;
    then s.(n+1) = a * (s.0 * a GeoSeq.n) by A2
      .= s.0 * (a GeoSeq.n * a)
      .= s.0 * a GeoSeq.(n+1) by PREPOWER:4;
    hence thesis;
  end;
  a GeoSeq.0 = 1 by PREPOWER:4;
  then
A4: X[0];
  for n holds X[n] from NAT_1:sch 1(A4,A3);
  then s = s.0 (#) a GeoSeq by SEQ_1:13;
  then
A5: Partial_Sums(s) = s.0 (#) Partial_Sums(a GeoSeq) by Th12;
  a GeoSeq is summable by A1,Th28;
  then
A6: Partial_Sums(a GeoSeq) is convergent by Def2;
  then Partial_Sums(s) is convergent by A5,SEQ_2:21;
  hence s is summable by Def2;
  Sum(a GeoSeq) = 1/(1-a) by A1,Th28;
  then lim Partial_Sums(s) = s.0 * (1/(1-a)) by A5,A6,SEQ_2:22
    .= s.0*1/(1-a)
    .= s.0/(1-a);
  hence thesis;
end;

theorem Th30:
  (for n holds s.n>0 & s1.n=s.(n+1)/s.n) & s1 is convergent & lim
  s1 < 1 implies s is summable
proof
  assume that
A1: for n holds s.n>0 & s1.n=s.(n+1)/s.n and
A2: s1 is convergent and
A3: lim s1 < 1;
  set r = (1 - lim s1)/2;
  0 + lim s1 < 1 by A3;
  then 0 < 1 - lim s1 by XREAL_1:22;
  then r > 0;
  then consider m such that
A4: for n st m <= n holds abs(s1.n - lim s1) < r by A2,SEQ_2:def 7;
  set s2 = (s.m * (1-r) to_power (-m)) (#) (1-r) GeoSeq;
  defpred X[Element of NAT] means s.(m+$1) <= s2.(m+$1);
A5: now
    let n;
    s.n > 0 & s.(n+1) > 0 by A1;
    then s.(n+1)/s.n > 0;
    hence s1.n >= 0 by A1;
  end;
  then
A6: lim s1 >= 0 by A2,PREPOWER:2;
  then 1 + -lim s1 < 1 + 1 by XREAL_1:8;
  then
A7: (1 - lim s1)/2 < 2/2 by XREAL_1:76;
A8: r + lim s1 = 1 - r;
A9: for k holds X[k] implies X[k+1]
  proof
    set X = (s.m * (1-r) to_power (-m));
    let k such that
A10: s.(m+k) <= s2.(m+k);
    abs(s1.(m+k) - lim s1) < r by A4,NAT_1:11;
    then s1.(m+k) - lim s1 < r by SEQ_2:9;
    then
A11: s1.(m+k) <= 1 - r by A8,XREAL_1:21;
    s2.(m+k) >= 0 by A1,A10;
    then
A12: s1.(m+k) * s2.(m+k) <= (1-r) * s2.(m+k) by A11,XREAL_1:66;
    s.(m+k) <> 0 by A1;
    then
A13: s.(m+(k+1)) = s.(m+k+1) / s.(m+k) * s.(m+k) by XCMPLX_1:88
      .= s1.(m+k) * s.(m+k) by A1;
    s1.(m+k) >= 0 by A5;
    then
A14: s.(m+(k+1)) <= s1.(m+k) * s2.(m+k) by A10,A13,XREAL_1:66;
    (1-r) * s2.(m+k) = (1-r) * (X * (1-r) GeoSeq.(m+k)) by SEQ_1:13
      .= X * ((1-r) GeoSeq.(m+k) * (1-r))
      .= X * (1-r) GeoSeq.(m+k+1) by PREPOWER:4
      .= s2.(m+(k+1)) by SEQ_1:13;
    hence thesis by A14,A12,XXREAL_0:2;
  end;
  s2.(m+0) = (s.m * (1-r) to_power (-m)) * (1-r) GeoSeq.m by SEQ_1:13
    .= (s.m * (1-r) to_power (-m)) * (1-r) |^ m by PREPOWER:def 1
    .= s.m * ((1-r) to_power (-m) * (1-r) to_power m)
    .= s.m * (1-r) to_power (-m + m) by A7,POWER:32,XREAL_1:52
    .= s.m * 1 by POWER:29
    .= s.(m+0);
  then
A15: X[0];
A16: for k holds X[k] from NAT_1:sch 1(A15,A9);
A17: now
    let n;
    assume m <= n;
    then consider k be Nat such that
A18: n = m + k by NAT_1:10;
    reconsider k as Element of NAT by ORDINAL1:def 13;
    n = m+k by A18;
    hence s.n <= s2.n by A16;
  end;
  1 - lim s1 <= 1 - 0 by A6,XREAL_1:8;
  then 1 - lim s1 < 2 * 2 by XXREAL_0:2;
  then r < 2 * 2 / 2 by XREAL_1:76;
  then r < 1 + 1;
  then r - 1 < 1 by XREAL_1:21;
  then
A19: - (r - 1) > - 1 by XREAL_1:26;
  1 - lim s1 > 0 by A3,XREAL_1:52;
  then r > 0;
  then 1 - r < 1 - 0 by XREAL_1:12;
  then abs(1-r) < 1 by A19,SEQ_2:9;
  then (1-r) GeoSeq is summable by Th28;
  then
A20: s2 is summable by Th13;
  for n holds 0 <= s.n by A1;
  hence thesis by A20,A17,Th22;
end;

theorem
  (for n holds s.n>0) & (ex m st for n st n>=m holds s.(n+1)/s.n >= 1)
  implies s is not summable
proof
  assume that
A1: for n holds s.n>0 and
A2: ex m st for n st n>=m holds s.(n+1)/s.n >= 1;
  consider m such that
A3: for n st n>=m holds s.(n+1)/s.n>=1 by A2;
  defpred X[Element of NAT] means s.(m+$1)>=s.m;
A4: for k st X[k] holds X[k+1]
  proof
    let k such that
A5: s.(m+k)>=s.m;
    s.(m+k)>0 & s.(m+k+1)/s.(m+k)>=1 by A1,A3,NAT_1:11;
    then s.(m+k+1)>=s.(m+k) by XREAL_1:193;
    hence thesis by A5,XXREAL_0:2;
  end;
A6: X[0];
A7: for k holds X[k] from NAT_1:sch 1(A6,A4);
A8: for k ex n st n>=k & not abs(s.n - 0)<s.m
  proof
    let k;
    take n = m + k;
    s.n>=s.m by A7;
    hence thesis by NAT_1:11,SEQ_2:9;
  end;
  s.m>0 by A1;
  then not lim s = 0 or s is not convergent by A8,SEQ_2:def 7;
  hence thesis by Th7;
end;

theorem Th32:
  (for n holds s.n>=0 & s1.n = n-root (s.n)) & s1 is convergent &
  lim s1 < 1 implies s is summable
proof
  assume that
A1: for n holds s.n>=0 & s1.n = n-root (s.n) and
A2: s1 is convergent and
A3: lim s1 < 1;
A4: now
    let n;
A5: (s1^\1).n = s1.(n+1) by NAT_1:def 3
      .= (n+1)-root (s.(n+1)) by A1;
    s.(n+1) >= 0 by A1;
    hence (s1^\1).n >= 0 by A5,NAT_1:11,POWER:8;
  end;
  set r = (1 - lim s1)/2;
  0 + lim s1 < 1 by A3;
  then 0 < 1 - lim s1 by XREAL_1:22;
  then r > 0;
  then consider m such that
A6: for n st m <= n holds abs(s1.n - lim s1) < r by A2,SEQ_2:def 7;
  lim (s1^\1) = lim s1 by A2,SEQ_4:33;
  then
A7: lim s1 >= 0 by A2,A4,PREPOWER:2;
  then 1 + -lim s1 < 1 + 1 by XREAL_1:8;
  then (1 - lim s1)/2 < 2/2 by XREAL_1:76;
  then
A8: 1 - r > 0 by XREAL_1:52;
  set s2 = (1-r) GeoSeq;
A9: r + lim s1 = 1 - r;
A10: for n st m+1<=n holds s.n <= (1-r) to_power n
  proof
    let n;
    assume
A11: m+1<=n;
    1<=m+1 by NAT_1:11;
    then
A12: 1 <= n by A11,XXREAL_0:2;
A13: s.n >= 0 by A1;
    m<=n by A11,NAT_1:13;
    then abs(s1.n - lim s1) < r by A6;
    then s1.n - lim s1 < r by SEQ_2:9;
    then s1.n < 1 - r by A9,XREAL_1:21;
    then
A14: n-root (s.n) < 1 - r by A1;
    now
      per cases;
      suppose
        s.n = 0;
        hence s.n < (1 - r) to_power n by A8,POWER:39;
      end;
      suppose
        s.n <> 0;
        then n -Root (s.n) > 0 by A12,A13,PREPOWER:def 3;
        then n-root (s.n) > 0 by A12,A13,POWER:def 1;
        then (n-root (s.n)) to_power n < (1-r) to_power n by A11,A14,
        POWER:42;
        hence s.n < (1-r) to_power n by A12,A13,POWER:5;
      end;
    end;
    hence thesis;
  end;
A15: for n st m+1 <= n holds s.n <= s2.n
  proof
    let n;
    s2.n = (1-r) to_power n by PREPOWER:def 1;
    hence thesis by A10;
  end;
  1 - lim s1 <= 1 - 0 by A7,XREAL_1:8;
  then 1 - lim s1 < 2 * 2 by XXREAL_0:2;
  then r < 2 * 2 / 2 by XREAL_1:76;
  then r < 1 + 1;
  then r - 1 < 1 by XREAL_1:21;
  then
A16: - (r - 1) > - 1 by XREAL_1:26;
  1 - lim s1 > 0 by A3,XREAL_1:52;
  then r > 0;
  then 1 - r < 1 - 0 by XREAL_1:12;
  then abs(1-r) < 1 by A16,SEQ_2:9;
  then s2 is summable by Th28;
  hence thesis by A1,A15,Th22;
end;

theorem Th33:
  (for n holds s.n>=0 & s1.n = n-root (s.n)) & (ex m st for n st m
  <=n holds s1.n>=1) implies s is not summable
proof
  assume that
A1: for n holds s.n>=0 & s1.n = n-root (s.n) and
A2: ex m st for n st m<=n holds s1.n>=1;
  consider m such that
A3: for n st m<=n holds s1.n>=1 by A2;
A4: for n st m+1<=n holds s.n>=1
  proof
    let n such that
A5: m+1<=n;
    1<=1+m by NAT_1:11;
    then
A6: 1<=n by A5,XXREAL_0:2;
A7: s.n >= 0 by A1;
    m<=m+1 by NAT_1:11;
    then m<=n by A5,XXREAL_0:2;
    then s1.n >= 1 by A3;
    then
A8: n-root (s.n) >= 1 by A1;
    now
      per cases by A8,XXREAL_0:1;
      suppose
        n-root (s.n) = 1;
        then s.n = 1 |^ n by A6,A7,POWER:5;
        hence thesis by NEWTON:15;
      end;
      suppose
        n-root (s.n) > 1;
        then (n-root (s.n)) to_power n > 1 by A5,POWER:40;
        hence thesis by A6,A7,POWER:5;
      end;
    end;
    hence thesis;
  end;
  for k ex n st k<=n & not abs(s.n - 0) < 1
  proof
    let k;
    take n = m + 1 + k;
    not s.n < 1 by A4,NAT_1:11;
    hence thesis by NAT_1:11,SEQ_2:9;
  end;
  then s is not convergent or not lim s = 0 by SEQ_2:def 7;
  hence thesis by Th7;
end;

theorem
  (for n holds s.n>=0 & s1.n = n-root (s.n)) & s1 is convergent & lim s1
  > 1 implies s is not summable
proof
  assume that
A1: for n holds s.n>=0 & s1.n = n-root (s.n) and
A2: s1 is convergent and
A3: lim s1 > 1;
  set r = lim s1 - 1;
  r > 0 by A3,XREAL_1:52;
  then consider m such that
A4: for n st m<=n holds abs(s1.n - lim s1) < r by A2,SEQ_2:def 7;
  for n st m<=n holds s1.n >= 1
  proof
    let n;
    assume m<=n;
    then abs(s1.n - lim s1) < r by A4;
    then s1.n - lim s1 > - r by SEQ_2:9;
    then s1.n - lim s1 + lim s1 > - r + lim s1 by XREAL_1:8;
    hence thesis;
  end;
  hence thesis by A1,Th33;
end;

registration
  let k, n be Nat;
  cluster k to_power n -> natural;
  coherence;
end;

definition
  let k, n be Nat;
  redefine func k to_power n -> Element of NAT;
  coherence by ORDINAL1:def 13;
end;

theorem Th35:
  s is non-increasing & (for n holds s.n>=0 & s1.n = 2 to_power n
  * s.(2 to_power n)) implies (s is summable iff s1 is summable)
proof
  assume that
A1: s is non-increasing and
A2: for n holds s.n>=0 & s1.n = 2 to_power n * s.(2 to_power n);
A3: Partial_Sums(s).(2 to_power (0+1)) = Partial_Sums(s).(1+1) by POWER:30
    .= Partial_Sums(s).(0+1)+s.2 by Def1
    .= Partial_Sums(s).0+s.1+s.2 by Def1
    .= s.0+s.1+s.2 by Def1;
A4: now
    let n;
    s.(2 to_power n) >= 0 by A2;
    then 2 to_power n * s.(2 to_power n) >= 0;
    hence s1.n >= 0 by A2;
  end;
  thus s is summable implies s1 is summable
  proof
    defpred Y[Element of NAT] means Partial_Sums(s1).$1 <= 2 * Partial_Sums(s)
    .(2 to_power $1);
A5: s.0>=0 by A2;
A6: for n st Y[n] holds Y[n+1]
    proof
      let n;
      deffunc U(Element of NAT) = Partial_Sums(s).(2 to_power n + $1) -
      Partial_Sums(s).(2 to_power n);
      consider a being Real_Sequence such that
A7:   for m holds a.m = U(m) from SEQ_1:sch 1;
      defpred X[Element of NAT] means $1 > 2 to_power n or $1 * s.(2 to_power
      (n+1)) <= a.$1;
A8:   for m st X[m] holds X[m+1]
      proof
        let m;
        assume
A9:     m > 2 to_power n or m * s.(2 to_power (n+1)) <= a.m;
        now
          per cases by A9;
          suppose
            m > 2 to_power n;
            hence thesis by NAT_1:13;
          end;
          suppose
A10:        m * s.(2 to_power (n+1)) <= a.m;
            now
              per cases;
              suppose
                m<2 to_power n;
                then m+1<=2 to_power n by NAT_1:13;
                then 2 to_power n + (m+1) <= 2 to_power n + 2 to_power n by
                XREAL_1:9;
                then 2 to_power n + m + 1 <= 2 * 2 to_power n;
                then 2 to_power n + m + 1 <= 2 to_power 1 * 2 to_power n by
                POWER:30;
                then 2 to_power n + m + 1 <= 2 to_power (n+1) by
                POWER:32;
                then s.(2 to_power n + m + 1) >= s.(2 to_power (n+1)) by A1,
                SEQM_3:14;
                then
                m * s.(2 to_power (n+1)) + 1 * s.(2 to_power (n+1)) <= a.
                m + s.(2 to_power n + m + 1) by A10,XREAL_1:9;
                then (m + 1) * s.(2 to_power (n+1)) <= Partial_Sums(s).(2
to_power n + m) - Partial_Sums(s).(2 to_power n) + s.(2 to_power n + m + 1) by
                A7;
                then (m + 1) * s.(2 to_power (n+1)) <= Partial_Sums(s).(2
to_power n + m) + s.(2 to_power n + m + 1) - Partial_Sums(s).(2 to_power n);
                then (m + 1) * s.(2 to_power (n+1)) <= Partial_Sums(s).(2
                to_power n + (m + 1)) - Partial_Sums(s).(2 to_power n) by Def1;
                hence thesis by A7;
              end;
              suppose
                m>=2 to_power n;
                hence thesis by NAT_1:13;
              end;
            end;
            hence thesis;
          end;
        end;
        hence thesis;
      end;
      a.0 = Partial_Sums(s).(2 to_power n + 0) - Partial_Sums(s).(2
      to_power n) by A7
        .= 0;
      then
A11:  X[0];
      for m holds X[m] from NAT_1:sch 1(A11,A8);
      then 2*(2 to_power n * s.(2 to_power (n+1))) <= 2*a.(2 to_power n) by
      XREAL_1:66;
      then 2*2 to_power n * s.(2 to_power (n+1)) <= 2*a.(2 to_power n);
      then 2 to_power 1 * 2 to_power n * s.(2 to_power (n+1)) <= 2*a.(2
      to_power n) by POWER:30;
      then 2 to_power (n+1) * s.(2 to_power (n+1)) <= 2*a.(2 to_power n) by
      POWER:32;
      then s1.(n+1) <= 2*a.(2 to_power n) by A2;
      then s1.(n+1) <= 2*(Partial_Sums(s).(2 to_power n + 2 to_power n) -
      Partial_Sums(s).(2 to_power n)) by A7;
      then
A12:  2 * Partial_Sums(s).(2 to_power n) + s1.(n+1) <= 2 * Partial_Sums(s
      ).(2 to_power n) + (2*(Partial_Sums(s).(2 to_power n + 2 to_power n) -
      Partial_Sums(s).(2 to_power n))) by XREAL_1:9;
      assume Partial_Sums(s1).n <= 2 * Partial_Sums(s).(2 to_power n);
      then
      Partial_Sums(s1).n + s1.(n+1) <= 2 * Partial_Sums(s).(2 to_power n)
      + s1.(n+1) by XREAL_1:8;
      then
A13:  Partial_Sums(s1).(n+1) <= 2 * Partial_Sums(s).(2 to_power n) + s1.(
      n+1) by Def1;
      2 to_power n + 2 to_power n = 2 * 2 to_power n
        .= 2 to_power 1 * 2 to_power n by POWER:30
        .= 2 to_power (n+1) by POWER:32;
      hence thesis by A13,A12,XXREAL_0:2;
    end;
A14: 2 to_power 0 = 0+1 by POWER:29;
    then
A15: 2 * Partial_Sums(s).(2 to_power 0) = 2 * (Partial_Sums(s).0 + s.1) by
    Def1
      .= 2*(s.0 + s.1) by Def1
      .= 2*s.0 + 2*s.1;
    assume s is summable;
    then Partial_Sums(s) is bounded_above by A2,Th20;
    then consider r be real number such that
A16: for n holds Partial_Sums(s).n<r by SEQ_2:def 3;
    s.1>=0 by A2;
    then
A17: s.1 + s.1 >= s.1 + 0 by XREAL_1:9;
    Partial_Sums(s1).0 = s1.0 by Def1
      .= 1 * s.1 by A2,A14
      .= s.1;
    then
A18: Y[0] by A15,A17,A5,XREAL_1:9;
A19: for n holds Y[n] from NAT_1:sch 1(A18,A6);
    now
      let n;
      2 * Partial_Sums(s).(2 to_power n) < 2*r & Partial_Sums(s1).n <= 2
      * Partial_Sums(s).(2 to_power n) by A19,A16,XREAL_1:70;
      hence Partial_Sums(s1).n < 2*r by XXREAL_0:2;
    end;
    then Partial_Sums(s1) is bounded_above by SEQ_2:def 3;
    hence thesis by A4,Th20;
  end;
  assume s1 is summable;
  then Partial_Sums(s1) is bounded_above by A4,Th20;
  then consider r be real number such that
A20: for n holds Partial_Sums(s1).n<r by SEQ_2:def 3;
A21: 2 to_power 0 = 1 by POWER:29;
  defpred Y[Element of NAT] means Partial_Sums(s).(2 to_power ($1+1)) <=
  Partial_Sums(s1).$1 + s.0 + s.2;
A22: for n st Y[n] holds Y[n+1]
  proof
    let n;
    defpred X[Element of NAT] means Partial_Sums(s).(2 to_power (n+1) + $1) -
    Partial_Sums(s).(2 to_power (n+1)) <= $1 * s.(2 to_power (n+1));
A23: for m st X[m] holds X[m+1]
    proof
      let m;
      2 to_power (n+1) + (m+1) >= 2 to_power (n+1) by NAT_1:11;
      then
A24:  s.(2 to_power (n+1) + (m+1)) <= s.(2 to_power (n+1)) by A1,
      SEQM_3:14;
      assume Partial_Sums(s).(2 to_power (n+1) + m) - Partial_Sums(s).(2
      to_power (n+1)) <= m * s.(2 to_power (n+1));
      then Partial_Sums(s).(2 to_power (n+1) + m) - Partial_Sums(s).(2
to_power (n+1)) + s.(2 to_power (n+1) + m+1) <= m * s.(2 to_power (n+1)) + s.(2
      to_power (n+1)) by A24,XREAL_1:9;
      then
      Partial_Sums(s).(2 to_power (n+1) + m) + s.(2 to_power (n+1) + m+1)
      - Partial_Sums(s).(2 to_power (n+1)) <= m * s.(2 to_power (n+1)) + s.(2
      to_power (n+1));
      hence thesis by Def1;
    end;
A25: X[0];
    for m holds X[m] from NAT_1:sch 1(A25,A23);
    then
    Partial_Sums(s).(2 to_power (n+1) + 2 to_power (n+1)) - Partial_Sums(
    s).(2 to_power (n+1))<= 2 to_power (n+1) * s.(2 to_power (n+1));
    then
A26: Partial_Sums(s).(2 to_power (n+1) + 2 to_power (n+1)) - Partial_Sums(
    s).(2 to_power (n+1)) <= s1.(n+1) by A2;
    assume Partial_Sums(s).(2 to_power (n+1)) <= Partial_Sums(s1).n + s.0+s. 2;
    then Partial_Sums(s).(2 to_power (n+1)) + s1.(n+1) <= s1.(n+1) + (
    Partial_Sums(s1).n + s.0+s.2) by XREAL_1:9;
    then
    Partial_Sums(s).(2 to_power (n+1)) + s1.(n+1) <= Partial_Sums(s1).n +
    s1.(n+1) + s.0+s.2;
    then
A27: Partial_Sums(s).(2 to_power (n+1)) + s1.(n+1) <= Partial_Sums(s1).(n+
    1) + s.0+s.2 by Def1;
    2 to_power (n+1) + 2 to_power (n+1) = 2 * 2 to_power (n+1)
      .= 2 to_power 1 * 2 to_power (n+1) by POWER:30
      .= 2 to_power (n+1+1) by POWER:32;
    then
    Partial_Sums(s).(2 to_power (n+1+1)) - Partial_Sums(s).(2 to_power (n
+1)) + Partial_Sums(s).(2 to_power (n+1)) <= Partial_Sums(s).(2 to_power (n+1))
    + s1.(n+1) by A26,XREAL_1:9;
    hence thesis by A27,XXREAL_0:2;
  end;
  Partial_Sums(s1).0 + s.0 + s.2 = s1.0 + s.0 + s.2 by Def1
    .= 2 to_power 0 * s.(2 to_power 0) + s.0 + s.2 by A2
    .= s.0 + s.1 + s.2 by A21;
  then
A28: Y[0] by A3;
A29: for n holds Y[n] from NAT_1:sch 1(A28,A22);
A30: Partial_Sums(s) is non-decreasing by A2,Th19;
  now
    let n;
    Partial_Sums(s1).n < r by A20;
    then
A31: Partial_Sums(s1).n + (s.0 + s.2) < r + (s.0+s.2) by XREAL_1:8;
    (1+1) to_power n >= 1 + n*1 & 1+n>=n by NAT_1:11,POWER:56;
    then 2 to_power n >= n by XXREAL_0:2;
    then 2 to_power n + 2 to_power n>= n+n by XREAL_1:9;
    then 2 * 2 to_power n >= n+n;
    then 2 to_power 1 * 2 to_power n >= n+n by POWER:30;
    then
A32: 2 to_power (n+1) >= n+n by POWER:32;
    n+n>=n by NAT_1:11;
    then 2 to_power (n+1) >= n by A32,XXREAL_0:2;
    then
A33: Partial_Sums(s).(2 to_power (n+1)) >= Partial_Sums(s).n by A30,
    SEQM_3:12;
    Partial_Sums(s).(2 to_power (n+1)) <= Partial_Sums(s1).n+s.0+s.2 by
    A29;
    then Partial_Sums(s).n <= Partial_Sums(s1).n+s.0+s.2 by A33,
    XXREAL_0:2;
    hence Partial_Sums(s).n < r + (s.0+s.2) by A31,XXREAL_0:2;
  end;
  then Partial_Sums(s) is bounded_above by SEQ_2:def 3;
  hence thesis by A2,Th20;
end;

theorem
  p>1 & (for n st n>=1 holds s.n = 1/n to_power p) implies s is summable
proof
  assume that
A1: p>1 and
A2: for n st n>=1 holds s.n = 1/n to_power p;
  defpred X[Element of NAT,real number] means ($1=0 & $2=1) or ($1>=1 & $2=1/
  $1 to_power p);
A3: for n ex r being Real st X[n,r]
  proof
    let n;
    n=0 or n>=1
    proof
      assume that
A4:   n<>0 and
A5:   n<1;
      n>=0+1 by A4,NAT_1:13;
      hence contradiction by A5;
    end;
    hence thesis;
  end;
  consider s1 such that
A6: for n holds X[n,s1.n] from FUNCT_2:sch 3(A3);
  deffunc V(Element of NAT) = 2 to_power $1 * s1.(2 to_power $1);
  consider s2 such that
A7: for n holds s2.n = V(n) from SEQ_1:sch 1;
A8: s1.0 = 1 by A6;
  now
    let n;
    now
      per cases by NAT_1:6;
      suppose
A9:     n=0;
        then (n+1) #R p >= 1 by A1,PREPOWER:99;
        then
A10:    (n+1) to_power p >= 1 by POWER:def 2;
        s1.(n+1) = 1/(n+1) to_power p by A6;
        hence s1.(n+1)<=s1.n by A8,A9,A10,XREAL_1:213;
      end;
      suppose
A11:    ex m be Nat st n=m+1;
        then n to_power p > 0 by POWER:39;
        then 1/n to_power p > 0;
        then
A12:    s1.n>0 by A6;
A13:    n/(n+1)<=1 by NAT_1:11,XREAL_1:185;
A14:    n/(n+1)>0 by A11;
        s1.(n+1)/s1.n = (1/(n+1) to_power p)/s1.n by A6
          .= (1/(n+1) to_power p)/(1/n to_power p) by A6,A11
          .= (1/(n+1) to_power p) * n to_power p
          .= n to_power p / (n+1) to_power p
          .= (n/(n+1)) to_power p by A11,POWER:36
          .= (n/(n+1)) #R p by A14,POWER:def 2;
        then s1.(n+1)/s1.n <= (n/(n+1)) #R 0 by A1,A14,A13,PREPOWER:98;
        then s1.(n+1)/s1.n <= 1 by A11,PREPOWER:85;
        hence s1.(n+1)<=s1.n by A12,XREAL_1:189;
      end;
    end;
    hence s1.(n+1)<=s1.n;
  end;
  then
A15: s1 is non-increasing by SEQM_3:def 14;
A16: now
    let n;
    assume n>=0;
A17: n+1>=0+1 by XREAL_1:8;
    (s^\1).n = s.(n+1) by NAT_1:def 3
      .= 1/(n+1) to_power p by A2,A17
      .= s1.(n+1) by A6
      .= (s1^\1).n by NAT_1:def 3;
    hence (s1^\1).n>=(s^\1).n;
  end;
  deffunc U(Element of NAT) = $1-root (s2.$1);
  consider s3 such that
A18: for n holds s3.n = U(n) from SEQ_1:sch 1;
A19: now
    let n;
A20: 2 to_power n > 0 by POWER:39;
    thus
A21: s2.n = 2 to_power n * s1.(2 to_power n) by A7
      .= 2 to_power n * (1/(2 to_power n) to_power p) by A6,A20
      .= 2 to_power n * (1/2 to_power (n*p)) by POWER:38
      .= 2 to_power n * 2 to_power (-n*p) by POWER:33
      .= 2 to_power (n+-n*p) by POWER:32
      .= 2 to_power ((1-p)*n);
    hence s2.n>=0 by POWER:39;
    s2.n = 2 to_power (1-p) to_power n by A21,POWER:38;
    hence s3.n = n-root (2 to_power (1-p) to_power n) by A18;
  end;
A22: now
    let n be Nat;
A23: n+1>=0+1 & 2 to_power (1-p) >= 0 by POWER:39,XREAL_1:8;
    thus (s3^\1).n = s3.(n+1) by NAT_1:def 3
      .= (n+1)-root (2 to_power (1-p) to_power (n+1)) by A19
      .= 2 to_power (1-p) by A23,POWER:5;
  end;
  then
A24: s3^\1 is constant by VALUED_0:def 18;
  then lim (s3^\1) = (s3^\1).0 by SEQ_4:41
    .= 2 to_power (1-p) by A22;
  then
A25: lim s3 = 2 to_power (1-p) by A24,SEQ_4:36;
A26: now
    let n;
    now
      per cases by NAT_1:6;
      suppose
        n=0;
        hence s1.n >= 0 by A6;
      end;
      suppose
        ex m be Nat st n=m+1;
        then n to_power p > 0 by POWER:39;
        then 1/n to_power p >= 0;
        hence s1.n>=0 by A6;
      end;
    end;
    hence s1.n>=0 & s2.n = 2 to_power n * s1.(2 to_power n) by A7;
  end;
A27: now
    let n;
A28: n+1>=0+1 by XREAL_1:8;
A29: s1.(n+1)>=0 by A26;
    (s^\1).n = s.(n+1) by NAT_1:def 3
      .= 1/(n+1) to_power p by A2,A28
      .= s1.(n+1) by A6
      .= (s1^\1).n by NAT_1:def 3;
    hence (s^\1).n>=0 by A29,NAT_1:def 3;
  end;
A30: s3 is convergent by A24,SEQ_4:35;
  1-p<0 by A1,XREAL_1:51;
  then lim s3 < 1 by A25,POWER:41;
  then s2 is summable by A18,A19,A30,Th32;
  then s1 is summable by A15,A26,Th35;
  then s1^\1 is summable by Th15;
  then s^\1 is summable by A27,A16,Th22;
  hence thesis by Th16;
end;

theorem
  p<=1 & (for n st n>=1 holds s.n=1/n to_power p) implies s is not summable
proof
  assume that
A1: p<=1 and
A2: for n st n>=1 holds s.n = 1/n to_power p;
  now
    per cases;
    suppose
A3:   p<0;
      now
        assume s is convergent & lim s=0;
        then consider m such that
A4:     for n st n>= m holds abs(s.n-0)<1 by SEQ_2:def 7;
        consider k such that
A5:     k>m by SEQ_4:10;
        now
          let n such that
A6:       n>=k;
A7:       n>0 by A5,A6;
          then
A8:       n>=0+1 by NAT_1:13;
          n>=m by A5,A6,XXREAL_0:2;
          then abs(s.n-0)<1 by A4;
          then abs(1/n to_power p)<1 by A2,A8;
          then abs(n to_power (-p))<1 by A7,POWER:33;
          then
A9:       n to_power (-p)<1 by ABSVALUE:def 1;
          n #R (-p) >= 1 by A3,A8,PREPOWER:99;
          hence contradiction by A7,A9,POWER:def 2;
        end;
        hence contradiction;
      end;
      hence thesis by Th7;
    end;
    suppose
A10:  p>=0;
      defpred X[Element of NAT,real number] means ($1=0 & $2=1) or ($1>=1 & $2
      =1/$1 to_power p);
A11:  for n ex r being Real st X[n,r]
      proof
        let n;
        n=0 or n>=1
        proof
          assume that
A12:      n<>0 and
A13:      n<1;
          n>=0+1 by A12,NAT_1:13;
          hence contradiction by A13;
        end;
        hence thesis;
      end;
      consider s1 such that
A14:  for n holds X[n,s1.n] from FUNCT_2:sch 3(A11);
A15:  s1.0 = 1 by A14;
      now
        let n;
        now
          per cases by NAT_1:6;
          suppose
A16:        n=0;
            then (n+1) #R p >= 1 by A10,PREPOWER:99;
            then
A17:        (n+1) to_power p >= 1 by POWER:def 2;
            s1.(n+1) = 1/(n+1) to_power p by A14;
            hence s1.(n+1)<=s1.n by A15,A16,A17,XREAL_1:213;
          end;
          suppose
A18:        ex m be Nat st n=m+1;
            then n to_power p > 0 by POWER:39;
            then 1/n to_power p > 0;
            then
A19:        s1.n>0 by A14;
A20:        n/(n+1)<=1 by NAT_1:11,XREAL_1:185;
A21:        n/(n+1)>0 by A18;
            s1.(n+1)/s1.n = (1/(n+1) to_power p)/s1.n by A14
              .= (1/(n+1) to_power p)/(1/n to_power p) by A14,A18
              .= (1/(n+1) to_power p) * n to_power p
              .= n to_power p / (n+1) to_power p
              .= (n/(n+1)) to_power p by A18,POWER:36
              .= (n/(n+1)) #R p by A21,POWER:def 2;
            then s1.(n+1)/s1.n <= (n/(n+1)) #R 0 by A10,A21,A20,PREPOWER:
            98;
            then s1.(n+1)/s1.n <= 1 by A18,PREPOWER:85;
            hence s1.(n+1)<=s1 .n by A19,XREAL_1:189;
          end;
        end;
        hence s1.(n+1)<=s1.n;
      end;
      then
A22:  s1 is non-increasing by SEQM_3:def 14;
A23:  now
        let n;
        assume
A24:    n>=1;
        then s.n = 1/n to_power p by A2
          .= s1.n by A14,A24;
        hence s.n>=s1.n;
      end;
      deffunc U(Element of NAT) = 2 to_power $1 * s1.(2 to_power $1);
      consider s2 such that
A25:  for n holds s2.n = U(n) from SEQ_1:sch 1;
A26:  now
        let n;
        now
          per cases by NAT_1:6;
          suppose
            n=0;
            hence s1.n >= 0 by A14;
          end;
          suppose
            ex m be Nat st n=m+1;
            then n to_power p > 0 by POWER:39;
            then 1/n to_power p >= 0;
            hence s1.n>=0 by A14;
          end;
        end;
        hence s1.n>=0 & s2.n = 2 to_power n * s1.(2 to_power n) by A25;
      end;
      now
        assume s2 is convergent & lim s2=0;
        then consider m such that
A27:    for n st n>=m holds abs(s2.n-0)<1/2 by SEQ_2:def 7;
        now
          let n;
          assume n>=m;
          then abs(s2.n-0)<1/2 by A27;
          then
A28:      abs(2 to_power n * s1.(2 to_power n))<1/2 by A25;
          2 to_power n >= 1 by PREPOWER:19;
          then abs(2 to_power n * (1/(2 to_power n) to_power p))<1/2 by A14,
          A28;
          then abs(2 to_power n * (1/2 to_power (n*p)))<1/2 by POWER:38;
          then abs(2 to_power n * 2 to_power (-n*p))<1/2 by POWER:33;
          then abs(2 to_power (n+-n*p))<1/2 by POWER:32;
          then 2 to_power (n-n*p)<1/2 by ABSVALUE:def 1;
          then 2 to_power (n-n*p)*2<1/2*2 by XREAL_1:70;
          then 2 to_power (n-n*p)*2 to_power 1<1 by POWER:30;
          then
A29:      2 to_power (n-n*p+1)<1 by POWER:32;
          1-p>=0 by A1,XREAL_1:50;
          then n*(1-p)>=0;
          then 2 #R (n-n*p+1) >= 1 by PREPOWER:99;
          hence contradiction by A29,POWER:def 2;
        end;
        hence contradiction;
      end;
      then not s2 is summable by Th7;
      then not s1 is summable by A22,A26,Th35;
      hence thesis by A26,A23,Th22;
    end;
  end;
  hence thesis;
end;

definition
  let s;
  canceled;
  attr s is absolutely_summable means
  :Def5:
  abs(s) is summable;
end;

canceled;

theorem Th39:
  for n,m st n<=m holds abs(Partial_Sums(s).m - Partial_Sums(s).n)
  <= abs(Partial_Sums(abs(s)).m - Partial_Sums(abs(s)).n)
proof
  let n,m such that
A1: n<=m;
  reconsider u=n, v=m as Integer;
  set s2=Partial_Sums(abs(s));
  set s1=Partial_Sums(s);
  defpred X[Element of NAT] means abs(s1.(n+$1) - s1.n) <= abs(s2.(n+$1) - s2.
  n);
  now
    let k;
    abs(s.k)>=0 by COMPLEX1:132;
    hence abs(s).k >= 0 by SEQ_1:16;
  end;
  then
A2: s2 is non-decreasing by Th19;
A3: for k st X[k] holds X[k+1]
  proof
    let k;
A4: abs(s.(n+k+1)) >= 0 by COMPLEX1:132;
    assume abs(s1.(n+k) - s1.n) <= abs(s2.(n+k) - s2.n);
    then
A5: abs(s.(n+k+1)) + abs(s1.(n+k) - s1.n) <= abs(s.(n+k+1)) + abs(s2.(n+k
    ) - s2.n) by XREAL_1:9;
A6: abs(s2.(n+(k+1)) - s2.n) = abs(s2.(n+k) + (abs(s)).(n+k+1) - s2.n) by
    Def1
      .= abs(abs(s.(n+k+1)) + s2.(n+k) - s2.n) by SEQ_1:16
      .= abs(abs(s.(n+k+1)) + (s2.(n+k) - s2.n));
    s2.(n+k)>=s2.n by A2,SEQM_3:11;
    then
A7: s2.(n+k) - s2.n >= 0 by XREAL_1:50;
    abs(s1.(n+(k+1)) - s1.n) = abs(s.(n+k+1) + s1.(n+k) - s1.n) by Def1
      .= abs(s.(n+k+1) + (s1.(n+k) - s1.n));
    then abs(s1.(n+(k+1))-s1.n) <= abs(s.(n+k+1))+abs(s1.(n+k) - s1.n) by
    COMPLEX1:142;
    then abs(s1.(n+(k+1))-s1.n) <= abs(s.(n+k+1))+abs(s2.(n+k)-s2.n) by
    A5
    ,XXREAL_0:2;
    then abs(s1.(n+(k+1))-s1.n) <= abs(s.(n+k+1))+(s2.(n+k)-s2.n) by A7,
    ABSVALUE:def 1;
    hence thesis by A7,A4,A6,ABSVALUE:def 1;
  end;
  reconsider k = v-u as Element of NAT by A1,INT_1:18;
A8: n+k = m;
A9: X[0];
  for k holds X[k] from NAT_1:sch 1(A9,A3);
  hence thesis by A8;
end;

theorem
  s is absolutely_summable implies s is summable
proof
  assume s is absolutely_summable;
  then
A1: abs(s) is summable by Def5;
  now
    let r;
    assume 0<r;
    then consider n such that
A2: for m st n<=m holds abs(Partial_Sums(abs(s)).m - Partial_Sums(abs(
    s)).n)<r by A1,Th25;
    take n;
    let m;
    assume
A3: n<=m;
    then
A4: abs(Partial_Sums(s).m - Partial_Sums(s).n) <= abs(Partial_Sums(abs(s))
    .m - Partial_Sums(abs(s)).n) by Th39;
    abs(Partial_Sums(abs(s)).m - Partial_Sums(abs(s)).n)<r by A2,A3;
    hence abs( Partial_Sums(s).m - Partial_Sums(s).n)<r by A4,XXREAL_0:2;
  end;
  hence thesis by Th25;
end;

theorem
  (for n holds 0<=s.n) & s is summable implies s is absolutely_summable
proof
  assume that
A1: for n holds 0<=s.n and
A2: s is summable;
A3: now
    let n;
    0<=s.n by A1;
    hence s.n = abs(s.n) by ABSVALUE:def 1;
  end;
  Partial_Sums(s) is convergent by A2,Def2;
  then Partial_Sums(abs(s)) is convergent by A3,SEQ_1:16;
  then abs(s) is summable by Def2;
  hence thesis by Def5;
end;

theorem
  (for n holds s.n<>0 & s1.n=abs(s).(n+1)/abs(s).n) & s1 is convergent &
  lim s1 < 1 implies s is absolutely_summable
proof
  assume that
A1: for n holds s.n<>0 & s1.n=abs(s).(n+1)/abs(s).n and
A2: s1 is convergent & lim s1 < 1;
  now
    let n;
    abs(s).n = abs(s.n) & s.n <> 0 by A1,SEQ_1:16;
    hence abs(s).n > 0 by COMPLEX1:133;
    thus s1.n=abs(s).(n+1)/abs(s).n by A1;
  end;
  then abs(s) is summable by A2,Th30;
  hence thesis by Def5;
end;

theorem Th43:
  r>0 & (ex m st for n st n>=m holds abs(s.n)>=r) implies s is not
  convergent or lim s <> 0
proof
  assume
A1: r>0;
  given m such that
A2: for n st n>=m holds abs(s.n)>=r;
  now
    per cases;
    suppose
      s is not convergent;
      hence thesis;
    end;
    suppose
A3:   s is convergent;
      now
        assume lim s=0;
        then consider k such that
A4:     for n st n>=k holds abs(s.n-0)<r by A1,A3,SEQ_2:def 7;
        now
          let n such that
A5:       n>=m+k;
          m+k>=k by NAT_1:11;
          then n>=k by A5,XXREAL_0:2;
          then
A6:       abs(s.n-0)<r by A4;
          m+k>=m by NAT_1:11;
          then n>=m by A5,XXREAL_0:2;
          hence contradiction by A2,A6;
        end;
        hence contradiction;
      end;
      hence thesis;
    end;
  end;
  hence thesis;
end;

theorem
  (for n holds s.n<>0) & (ex m st for n st n>=m holds abs(s).(n+1)/abs(s
  ).n >= 1) implies s is not summable
proof
  assume
A1: for n holds s.n<>0;
  given m such that
A2: for n st n>=m holds abs(s).(n+1)/abs(s).n >= 1;
A3: now
    defpred X[Element of NAT] means abs(s.(m+$1))>=abs(s.m);
A4: for k st X[k] holds X[k+1]
    proof
      let k such that
A5:   abs(s.(m+k))>=abs(s.m);
      abs(s).(m+k+1)/abs(s).(m+k) >= 1 by A2,NAT_1:11;
      then abs(s.(m+k+1))/abs(s).(m+k) >= 1 by SEQ_1:16;
      then
A6:   abs(s.(m+k+1))/abs(s.(m+k)) >= 1 by SEQ_1:16;
      s.(m+k) <> 0 by A1;
      then abs(s.(m+k)) > 0 by COMPLEX1:133;
      then abs(s.(m+k+1))>=abs(s.(m+k)) by A6,XREAL_1:193;
      hence thesis by A5,XXREAL_0:2;
    end;
    let n;
    assume n>=m;
    then consider k be Nat such that
A7: n=m+k by NAT_1:10;
    reconsider k as Element of NAT by ORDINAL1:def 13;
A8: n=m+k by A7;
A9: X[0];
    for k holds X[k] from NAT_1:sch 1(A9,A4);
    hence abs(s.n)>=abs(s.m) by A8;
  end;
  s.m <> 0 by A1;
  then abs(s.m) > 0 by COMPLEX1:133;
  then s is not convergent or lim s <> 0 by A3,Th43;
  hence thesis by Th7;
end;

theorem
  (for n holds s1.n = n-root (abs(s).n)) & s1 is convergent & lim s1 < 1
  implies s is absolutely_summable
proof
  assume that
A1: for n holds s1.n = n-root (abs(s).n) and
A2: s1 is convergent & lim s1 < 1;
  now
    let n;
    abs(s).n = abs(s.n) by SEQ_1:16;
    hence abs(s).n >= 0 by COMPLEX1:132;
    thus s1.n = n-root (abs(s).n) by A1;
  end;
  then abs(s) is summable by A2,Th32;
  hence thesis by Def5;
end;

theorem
  (for n holds s1.n = n-root(abs(s).n)) & (ex m st for n st m<=n holds
  s1.n >= 1 ) implies s is not summable
proof
  assume
A1: for n holds s1.n = n-root (abs(s).n);
  given m such that
A2: for n st m<=n holds s1.n>=1;
  now
    let n such that
A3: n>=m+1;
    m+1>=1 by NAT_1:11;
    then
A4: n>=1 by A3,XXREAL_0:2;
    m+1>=m by NAT_1:11;
    then
A5: n>=m by A3,XXREAL_0:2;
    s1.n = n-root (abs(s).n) by A1
      .= n-root abs(s.n) by SEQ_1:16;
    then abs(s.n) >= 0 & n-root abs(s.n) |^ n >= 1 by A2,A5,COMPLEX1:132 ,
    PREPOWER:19;
    hence abs(s.n) >= 1 by A4,POWER:5;
  end;
  then s is not convergent or lim s<>0 by Th43;
  hence thesis by Th7;
end;

theorem
  (for n holds s1.n = n-root (abs(s).n)) & s1 is convergent & lim s1 > 1
  implies s is not summable
proof
  assume that
A1: for n holds s1.n = n-root (abs(s).n) and
A2: s1 is convergent and
A3: lim s1 > 1;
  lim s1 - 1 > 0 by A3,XREAL_1:52;
  then consider m such that
A4: for n st n>=m holds abs(s1.n-lim s1)<lim s1-1 by A2,SEQ_2:def 7;
  now
    let n such that
A5: n>=m+1;
A6: s1.n = n-root (abs(s).n) by A1
      .= n-root abs(s.n) by SEQ_1:16;
    m+1>=m by NAT_1:11;
    then n>=m by A5,XXREAL_0:2;
    then abs(n-root abs(s.n) - lim s1) < lim s1 - 1 by A4,A6;
    then -(lim s1 - 1) < n-root abs(s.n) - lim s1 by SEQ_2:9;
    then 1 - lim s1 + lim s1 < n-root abs(s.n) - lim s1 + lim s1 by
    XREAL_1:8;
    then
A7: abs(s.n) >= 0 & n-root abs(s.n) |^ n >= 1 by COMPLEX1:132,
    PREPOWER:19;
    m+1>=1 by NAT_1:11;
    then n>=1 by A5,XXREAL_0:2;
    hence abs(s.n) >= 1 by A7,POWER:5;
  end;
  then s is not convergent or lim s<>0 by Th43;
  hence thesis by Th7;
end;

begin :: Addenda
:: from BHSP_4, 2006.03.04, A.T.

definition
  let s;
  let n be Nat;
  func Sum(s, n) -> Real equals
  Partial_Sums(s).n;
  coherence;
end;

definition
  let s;
  let n,m be Nat;
  func Sum(s, n, m) -> Real equals
  Sum(s, n) - Sum(s, m);
  coherence;
end;
