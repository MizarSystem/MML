:: Upper and Lower Sequence on the Cage, Upper and Lower Arcs
::  by Robert Milewski
::
:: Received April 5, 2002
:: Copyright (c) 2002 Association of Mizar Users

environ

 vocabularies JORDAN8, PSCOMP_1, BOOLE, JORDAN1A, JORDAN9, GOBOARD1, GOBOARD5,
      JORDAN6, SPRECT_2, JORDAN3, FINSEQ_5, JORDAN2C, SPPOL_2, GROUP_2,
      RFINSEQ, REALSET1, JORDAN1E, SQUARE_1, FINSEQ_4, TOPREAL2, CONNSP_1,
      COMPTS_1, TOPREAL1, SPPOL_1, FINSEQ_1, TREES_1, EUCLID, RELAT_1, MCART_1,
      MATRIX_1, GOBOARD9, PRE_TOPC, RELAT_2, SEQM_3, SUBSET_1, FUNCT_1,
      ARYTM_1, FINSEQ_6, CARD_1, GOBOARD2, GROUP_1, GRAPH_2, NAT_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, NUMBERS, XCMPLX_0, XXREAL_0, NAT_1,
      NAT_D, FUNCT_1, PARTFUN1, FINSEQ_1, FINSEQ_4, FINSEQ_5, RFINSEQ,
      MATRIX_1, FINSEQ_6, GRAPH_2, REALSET1, STRUCT_0, PRE_TOPC, TOPREAL2,
      CARD_1, NEWTON, BINARITH, CONNSP_1, COMPTS_1, EUCLID, PSCOMP_1, SPRECT_2,
      GOBOARD1, TOPREAL1, GOBOARD2, GOBOARD5, GOBOARD9, GOBRD13, SPPOL_1,
      SPPOL_2, JORDAN3, JORDAN8, JORDAN2C, JORDAN6, JORDAN9, JORDAN1A,
      JORDAN1E;
 constructors PARTFUN1, DOMAIN_1, REAL_1, SQUARE_1, NAT_1, FINSEQ_4, CARD_4,
      RFINSEQ, BINARITH, TOPS_1, CONNSP_1, REALSET2, MATRIX_2, GOBOARD2,
      JORDAN1, PSCOMP_1, GRAPH_2, GOBOARD9, JORDAN3, JORDAN5C, SPRECT_1,
      JORDAN6, JORDAN2C, JORDAN8, GOBRD13, JORDAN9, JORDAN1A, JORDAN1E, NAT_D,
      NEWTON;
 registrations SPRECT_1, TOPREAL6, JORDAN8, REVROT_1, INT_1, RELSET_1,
      SUBSET_1, PRE_TOPC, SPRECT_3, GOBOARD2, FINSEQ_1, FINSEQ_5, SPPOL_2,
      JORDAN1A, JORDAN1E, JORDAN1G, GOBRD11, XBOOLE_0, XREAL_0, JORDAN1,
      MEMBERED, ORDINAL2, XXREAL_0, TOPREAL5, ORDINAL1;
 requirements NUMERALS, BOOLE, SUBSET, REAL, ARITHM;
 definitions TARSKI, GOBOARD5, XBOOLE_0, PSCOMP_1;
 theorems BINARITH, JORDAN8, PSCOMP_1, JORDAN1A, NAT_1, REAL_1, GOBOARD5,
      FINSEQ_1, FINSEQ_2, JORDAN4, JORDAN6, SPRECT_2, FINSEQ_4, FINSEQ_5,
      FINSEQ_6, GOBOARD7, GOBOARD9, SPPOL_1, SPPOL_2, REVROT_1, TOPREAL1,
      SPRECT_3, JORDAN5B, JORDAN3, GOBRD13, INT_1, JORDAN9, JORDAN2C, SUBSET_1,
      CONNSP_1, ZFMISC_1, JGRAPH_1, GOBOARD1, SPRECT_1, TARSKI, TOPREAL3,
      FINSEQ_3, RELAT_1, COMPTS_1, FUNCT_1, TOPREAL5, JORDAN10, GRAPH_2,
      GOBOARD2, CARD_1, CARD_2, PRE_CIRC, ENUMSET1, SPRECT_4, JORDAN1B,
      SPRECT_5, JORDAN1E, PARTFUN2, JORDAN1F, TDLAT_1, JORDAN1G, GOBOARD3,
      JORDAN1H, TOPREAL8, GOBRD14, JORDAN1D, JORDAN1I, XBOOLE_0, XBOOLE_1,
      REALSET1, XREAL_1, XXREAL_0, EUCLID, PRE_TOPC, ORDINAL1;
 schemes NAT_1;

begin

  reserve n for Element of NAT;

  theorem Th1:
   for G be Go-board
   for i1,i2,j1,j2 be Element of NAT st
    1 <= j1 & j1 <= width G & 1 <= j2 & j2 <= width G &
    1 <= i1 & i1 < i2 & i2 <= len G holds
     G*(i1,j1)`1 < G*(i2,j2)`1
   proof
    let G be Go-board;
    let i1,i2,j1,j2 be Element of NAT;
    assume that
     A1: 1 <= j1 and
     A2: j1 <= width G and
     A3: 1 <= j2 and
     A4: j2 <= width G and
     A5: 1 <= i1 and
     A6: i1 < i2 and
     A7: i2 <= len G;
    A8: 1 <= i2 by A5,A6,XXREAL_0:2;
    then G*(i2,j1)`1 = G*(i2,1)`1 by A1,A2,A7,GOBOARD5:3
       .= G*(i2,j2)`1 by A3,A4,A7,A8,GOBOARD5:3;
    hence G*(i1,j1)`1 < G*(i2,j2)`1 by A1,A2,A5,A6,A7,GOBOARD5:4;
   end;

  theorem Th2:
   for G be Go-board
   for i1,i2,j1,j2 be Element of NAT st
    1 <= i1 & i1 <= len G & 1 <= i2 & i2 <= len G &
    1 <= j1 & j1 < j2 & j2 <= width G holds
     G*(i1,j1)`2 < G*(i2,j2)`2
   proof
    let G be Go-board;
    let i1,i2,j1,j2 be Element of NAT;
    assume that
     A1: 1 <= i1 and
     A2: i1 <= len G and
     A3: 1 <= i2 and
     A4: i2 <= len G and
     A5: 1 <= j1 and
     A6: j1 < j2 and
     A7: j2 <= width G;
    A8: 1 <= j2 by A5,A6,XXREAL_0:2;
    then G*(i1,j2)`2 = G*(1,j2)`2 by A1,A2,A7,GOBOARD5:2
       .= G*(i2,j2)`2 by A3,A4,A7,A8,GOBOARD5:2;
    hence G*(i1,j1)`2 < G*(i2,j2)`2 by A1,A2,A5,A6,A7,GOBOARD5:5;
   end;

  registration
   let f be non empty FinSequence;
   let g be FinSequence;
   cluster f^'g -> non empty;
   coherence
   proof
    f^'g = f^(2,len g)-cut g by GRAPH_2:def 2;
    hence thesis;
   end;
  end;

  theorem Th3:
   for C be compact connected non vertical non horizontal Subset of TOP-REAL 2
   for n be Element of NAT holds
    L~(Cage(C,n)-:E-max L~Cage(C,n)) /\ L~(Cage(C,n):-E-max L~Cage(C,n)) =
     {N-min L~Cage(C,n),E-max L~Cage(C,n)}
   proof
    let C be compact connected non vertical non horizontal
     Subset of TOP-REAL 2;
    let n be Element of NAT;
    set US = Cage(C,n)-:E-max L~Cage(C,n);
    set LS = Cage(C,n):-E-max L~Cage(C,n);
    set f=Cage(C,n);
    set pW=E-max L~Cage(C,n);
    set pN=N-min L~Cage(C,n);
    A1: pW in rng Cage(C,n) by SPRECT_2:50;
    A2: pW..Cage(C,n) <= pW..Cage(C,n);
        (f:-pW)/.len(f:-pW) = f/.len f by A1,FINSEQ_5:57
       .= f/.1 by FINSEQ_6:def 1
       .= pN by JORDAN9:34;
    then A3: pN in rng (Cage(C,n):-pW) by REVROT_1:3;
    A4: Cage(C,n)-:pW <> {} by A1,FINSEQ_5:50;
        (Cage(C,n):-pW)/.1 = pW by FINSEQ_5:56;
    then A5: E-max L~Cage(C,n) in rng (Cage(C,n):-E-max L~Cage(C,n))
                                                              by FINSEQ_6:46;
        (Cage(C,n)-:pW)/.1 = Cage(C,n)/.1 by A1,FINSEQ_5:47
       .= pN by JORDAN9:34;
    then A6: N-min L~Cage(C,n) in rng (Cage(C,n)-:E-max L~Cage(C,n))
                                                          by A4,FINSEQ_6:46;
        len(f-:pW) = pW..f by A1,FINSEQ_5:45;
    then (f-:pW)/.len (f-:pW) = pW by A1,FINSEQ_5:48;
    then A7: pW in rng (Cage(C,n)-:pW) by A4,REVROT_1:3;
        N-max L~f in L~f & pW`1 = E-bound L~f by EUCLID:56,SPRECT_1:13;
    then (N-max L~f)`1 <= pW`1 by PSCOMP_1:71;
    then A8: pN <> pW by SPRECT_2:55;
    then A9: Card {pN,pW} = 2 by CARD_2:76;
        {pN,pW} c= rng LS
    proof
     let x be set;
     assume x in {pN,pW};
     hence x in rng LS by A3,A5,TARSKI:def 2;
    end;
    then A10: Card {pN,pW} c= Card rng LS by CARD_1:27;
    Card rng LS c= Card dom LS by CARD_2:80;
    then Card rng LS c= len LS by PRE_CIRC:21;
    then 2 c= len LS by A9,A10,XBOOLE_1:1;
    then len LS >= 2 by CARD_1:56;
    then A11: rng LS c= L~LS by SPPOL_2:18;
    A12: E-max L~Cage(C,n) in rng LS by FINSEQ_6:66;
    A13: Card {pN,pW} = 2 by A8,CARD_2:76;
    {pN,pW} c= rng US
    proof
     let x be set;
     assume x in {pN,pW};
     hence x in rng US by A6,A7,TARSKI:def 2;
    end;
    then A14: Card {pN,pW} c= Card rng US by CARD_1:27;
    Card rng US c= Card dom US by CARD_2:80;
    then Card rng US c= len US by PRE_CIRC:21;
    then A15: 2 c= len US by A13,A14,XBOOLE_1:1;
    then A16: len US >= 2 by CARD_1:56;
    then A17: rng US c= L~US by SPPOL_2:18;
    len US <> 0 by A15,CARD_1:56;
    then A18: US is non empty by FINSEQ_1:25;
    A19: E-max L~Cage(C,n) in rng US by A1,A2,FINSEQ_5:49;
    US/.1 = Cage(C,n)/.1 by A1,FINSEQ_5:47
       .= N-min L~Cage(C,n) by JORDAN9:34;
    then A20: N-min L~Cage(C,n) in rng US by A18,FINSEQ_6:46;
        LS/.(len LS) = Cage(C,n)/.len Cage(C,n) by A1,FINSEQ_5:57
       .= Cage(C,n)/.1 by FINSEQ_6:def 1
       .= N-min L~Cage(C,n) by JORDAN9:34;
    then A21: N-min L~Cage(C,n) in rng LS by REVROT_1:3;
    thus L~US /\ L~LS c=
     {N-min L~Cage(C,n),E-max L~Cage(C,n)}
    proof
     let x be set;
     assume A22: x in L~US /\ L~LS;
     then A23: x in L~US & x in L~LS by XBOOLE_0:def 3;
     reconsider x1=x as Point of TOP-REAL 2 by A22;
     consider i1 be Element of NAT such that
      A24: 1 <= i1 & i1+1 <= len US and
      A25: x1 in LSeg(US,i1) by A23,SPPOL_2:13;
     consider i2 be Element of NAT such that
      A26: 1 <= i2 & i2+1 <= len LS and
      A27: x1 in LSeg(LS,i2) by A23,SPPOL_2:13;
     A28: LSeg(US,i1) = LSeg(f,i1) by A24,SPPOL_2:9;
     set i3=i2-'1;
     A29: i3+1 = i2 by A26,BINARITH:53;
     then A30: LSeg(LS,i2) = LSeg(f,i3+pW..f) by A1,SPPOL_2:10;
     A31: len US = pW..f by A1,FINSEQ_5:45;
     A32: len LS = len f-pW..f+1 by A1,FINSEQ_5:53;
     A33: i2-1 >= 1-1 by A26,XREAL_1:11;
         i2 < len f-pW..f+1 by A26,A32,NAT_1:13;
     then i2-1 < len f-pW..f by XREAL_1:21;
     then i2-1+pW..f < len f by XREAL_1:22;
     then A34: i3+pW..f < len f by A33,BINARITH:def 3;
     A35: pW..f >= 1 by A1,FINSEQ_4:31;
     i3+1 < len f-pW..f+1 by A26,A29,A32,NAT_1:13;
     then i3 < len f-pW..f by XREAL_1:9;
     then A36: i3+pW..f < len f by XREAL_1:22;
     A37: i1+1 < pW..f+1 by A24,A31,NAT_1:13;
     1+pW..f <= i3+1+pW..f by A26,A29,XREAL_1:9;
     then i1+1 < i3+pW..f+1 by A37,XXREAL_0:2;
     then A38: i1+1 <= i3+pW..f by NAT_1:13;
     A39: i3+pW..f+1 <= len f by A36,NAT_1:13;
     A40: len f > 4 by GOBOARD7:36;
     A41: pW..f-'1+1 = pW..f by A35,BINARITH:53;
     assume A42: not x in {N-min L~Cage(C,n),E-max L~Cage(C,n)};
     now per cases by A24,A38,REAL_1:def 5;
      suppose i1+1 < i3+pW..f & i1 > 1;
       then LSeg(f,i1) misses LSeg(f,i3+pW..f) by A36,GOBOARD5:def 4;
       then LSeg(f,i1) /\ LSeg(f,i3+pW..f) = {} by XBOOLE_0:def 7;
       hence contradiction by A25,A27,A28,A30,XBOOLE_0:def 3;
      end;
      suppose A43: i1 = 1;
       A44: i3+pW..f >= 0+2 by A16,A31,XREAL_1:9;
       now per cases by A44,REAL_1:def 5;
        suppose i3+pW..f > 2;
         then A45: i1+1 < i3+pW..f by A43;
             now per cases by A39,REAL_1:def 5;
          suppose i3+pW..f+1 < len f;
           then LSeg(f,i1) misses LSeg(f,i3+pW..f) by A45,GOBOARD5:def 4;
           then LSeg(f,i1) /\ LSeg(f,i3+pW..f) = {} by XBOOLE_0:def 7;
           hence contradiction by A25,A27,A28,A30,XBOOLE_0:def 3;
          end;
          suppose i3+pW..f+1 = len f;
           then i3+pW..f = len f-1;
           then i3+pW..f = len f-'1 by BINARITH:def 3;
           then LSeg(f,i1) /\ LSeg(f,i3+pW..f) = {f/.1} by A40,A43,REVROT_1:30
;
           then x1 in {f/.1} by A25,A27,A28,A30,XBOOLE_0:def 3;
           then x1 = f/.1 by TARSKI:def 1
              .= pN by JORDAN9:34;
           hence contradiction by A42,TARSKI:def 2;
         end;
         end;
         hence contradiction;
        end;
        suppose A46: i3+pW..f = 2;
         then A47: x1 in LSeg(f,1) /\ LSeg(f,1+1)
                                         by A25,A27,A28,A30,A43,XBOOLE_0:def 3;
         1 + 2 <= len f by A40,XXREAL_0:2;
         then x1 in {f/.(1+1)} by A47,TOPREAL1:def 8;
         then A48: x1 = f/.(1+1) by TARSKI:def 1;
         A49: 0+1 in dom LS by FINSEQ_5:6;
         0+pW..f >= i3+pW..f by A15,A31,A46,CARD_1:56;
         then i3 <= 0 by XREAL_1:8;
         then i3 = 0;
         then LS/.1 = x1 by A1,A46,A48,A49,FINSEQ_5:55;
         then x1 = pW by FINSEQ_5:56;
         hence contradiction by A42,TARSKI:def 2;
       end;
       end;
       hence contradiction;
      end;
      suppose A50: i1+1 = i3+pW..f;
       0+pW..f <= i3+pW..f by XREAL_1:9;
       then pW..f = i1+1 by A24,A31,A50,XXREAL_0:1;
       then i1 = pW..f-1;
       then A51: i1 = pW..f-'1 by BINARITH:def 3;
       i3+pW..f >= pW..f by NAT_1:11;
       then pW..f < len f by A34,XXREAL_0:2;
       then pW..f+1 <= len f by NAT_1:13;
       then pW..f-'1 + (1+1) <= len f by A41;
       then LSeg(f,i1) /\ LSeg(f,i3+(pW..f)) = {f/.(pW..f)}
                                          by A24,A41,A50,A51,TOPREAL1:def 8;
       then x1 in {f/.(pW..f)} by A25,A27,A28,A30,XBOOLE_0:def 3;
       then x1 = f/.(pW..f) by TARSKI:def 1
          .= pW by A1,FINSEQ_5:41;
       hence contradiction by A42,TARSKI:def 2;
     end;
     end;
     hence contradiction;
    end;
    thus {N-min L~Cage(C,n),E-max L~Cage(C,n)} c= L~US /\ L~LS
    proof
     let x be set;
     assume A52: x in {N-min L~Cage(C,n),E-max L~Cage(C,n)};
     per cases by A52,TARSKI:def 2;
      suppose x = N-min L~Cage(C,n);
       hence x in L~US /\ L~LS by A11,A17,A20,A21,XBOOLE_0:def 3;
      end;
      suppose x = E-max L~Cage(C,n);
       hence x in L~US /\ L~LS by A11,A12,A17,A19,XBOOLE_0:def 3;
    end;
    end;
   end;

  theorem Th4:
   for C be compact connected non vertical non horizontal
    Subset of TOP-REAL 2 holds
     Upper_Seq(C,n) = Rotate(Cage(C,n),E-max L~Cage(C,n)):-W-min L~Cage(C,n)
   proof
    let C be compact connected non vertical non horizontal
     Subset of TOP-REAL 2;
    set Nmi = N-min L~Cage(C,n);
    set Nma = N-max L~Cage(C,n);
    set Wmi = W-min L~Cage(C,n);
    set Wma = W-max L~Cage(C,n);
    set Ema = E-max L~Cage(C,n);
    set Emi = E-min L~Cage(C,n);
    set Sma = S-max L~Cage(C,n);
    set Smi = S-min L~Cage(C,n);
    set RotWmi = Rotate(Cage(C,n),Wmi);
    set RotEma = Rotate(Cage(C,n),Ema);
    A1: Cage(C,n)/.1 = Nmi by JORDAN9:34;
    A2: Nmi in rng Cage(C,n) by SPRECT_2:43;
    A3: Wmi in rng Cage(C,n) by SPRECT_2:47;
    A4: Ema in rng Cage(C,n) by SPRECT_2:50;
    A5: Nmi..Cage(C,n) < Nma..Cage(C,n) by A1,SPRECT_2:72;
    A6: Nma..Cage(C,n) <= Ema..Cage(C,n) by A1,SPRECT_2:74;
    then A7: Nmi..Cage(C,n) < Ema..Cage(C,n) by A5,XXREAL_0:2;
    A8: Ema..Cage(C,n) < Emi..Cage(C,n) by A1,SPRECT_2:75;
    A9: Emi..Cage(C,n) <= Sma..Cage(C,n) by A1,SPRECT_2:76;
    A10: Sma..Cage(C,n) < Smi..Cage(C,n) by A1,SPRECT_2:77;
    A11: Smi..Cage(C,n) <= Wmi..Cage(C,n) by A1,SPRECT_2:78;
        Ema..Cage(C,n) < Sma..Cage(C,n) by A8,A9,XXREAL_0:2;
    then Ema..Cage(C,n) < Smi..Cage(C,n) by A10,XXREAL_0:2;
    then A12: Ema..Cage(C,n) < Wmi..Cage(C,n) by A11,XXREAL_0:2;
    then A13: Ema in rng (Cage(C,n)-:Wmi) by A3,A4,FINSEQ_5:49;
    A14: Cage(C,n)-:Wmi <> {} by A3,FINSEQ_5:50;
    (Cage(C,n)-:Wmi)/.1 = Cage(C,n)/.1 by A3,FINSEQ_5:47
       .= Nmi by JORDAN9:34;
    then A15: Nmi in rng (Cage(C,n)-:Wmi) by A14,FINSEQ_6:46;
        len(Cage(C,n)-:Wmi) = Wmi..Cage(C,n) by A3,FINSEQ_5:45;
    then (Cage(C,n)-:Wmi)/.len (Cage(C,n)-:Wmi) = Wmi by A3,FINSEQ_5:48;
    then A16: Wmi in rng (Cage(C,n)-:Wmi) by A14,REVROT_1:3;
        Wma in L~Cage(C,n) & Nmi`2 = N-bound L~Cage(C,n)
                                                  by EUCLID:56,SPRECT_1:15;
    then Wma`2 <= Nmi`2 by PSCOMP_1:71;
    then A17: Nmi <> Wmi by SPRECT_2:61;
    then A18: Card {Nmi,Wmi} = 2 by CARD_2:76;
        {Nmi,Wmi} c= rng (Cage(C,n)-:Wmi)
    proof
     let x be set;
     assume x in {Nmi,Wmi};
     hence x in rng (Cage(C,n)-:Wmi) by A15,A16,TARSKI:def 2;
    end;
    then A19: Card {Nmi,Wmi} c= Card rng (Cage(C,n)-:Wmi) by CARD_1:27;
    Card rng (Cage(C,n)-:Wmi) c= Card dom (Cage(C,n)-:Wmi) by CARD_2:80;
    then Card rng (Cage(C,n)-:Wmi) c= len (Cage(C,n)-:Wmi) by PRE_CIRC:21;
    then 2 c= len (Cage(C,n)-:Wmi) by A18,A19,XBOOLE_1:1;
    then len (Cage(C,n)-:Wmi) >= 2 by CARD_1:56;
    then A20: rng (Cage(C,n)-:Wmi) c= L~(Cage(C,n)-:Wmi) by SPPOL_2:18;
    A21: Nmi..Cage(C,n) < Wmi..Cage(C,n) by A7,A12,XXREAL_0:2;
    then A22: Nmi in rng (Cage(C,n)-:Wmi) by A2,A3,FINSEQ_5:49;
    A23: Ema..(Cage(C,n)-:Wmi) <> 1
    proof
     assume A24: Ema..(Cage(C,n)-:Wmi) = 1;
     Nmi..(Cage(C,n)-:Wmi) = Nmi..Cage(C,n) by A2,A3,A21,SPRECT_5:3
        .= 1 by A1,FINSEQ_6:47;
     hence contradiction by A5,A6,A13,A22,A24,FINSEQ_5:10;
    end;
    then A25: Ema in rng (Cage(C,n)-:Wmi/^1) by A13,FINSEQ_6:83;
    A26: Nmi`1 < Nma`1 by SPRECT_2:55;
    Nma`1 <= (NE-corner L~Cage(C,n))`1 by PSCOMP_1:97;
    then Nma`1 <= E-bound L~Cage(C,n) by EUCLID:56;
    then A27: Nmi <> Ema by A26,EUCLID:56;
    not Ema in rng (Cage(C,n):-Wmi)
    proof
     assume A28: Ema in rng (Cage(C,n):-Wmi);
         (Cage(C,n):-Wmi)/.len(Cage(C,n):-Wmi) = Cage(C,n)/.len Cage(C,n)
                                                            by A3,FINSEQ_5:57
        .= Cage(C,n)/.1 by FINSEQ_6:def 1
        .= Nmi by JORDAN9:34;
     then A29: Nmi in rng (Cage(C,n):-Wmi) by REVROT_1:3;
         (Cage(C,n):-Wmi)/.1 = Wmi by FINSEQ_5:56;
     then A30: Wmi in rng (Cage(C,n):-Wmi) by FINSEQ_6:46;
         Wma in L~Cage(C,n) & Nmi`2 = N-bound L~Cage(C,n)
                                                  by EUCLID:56,SPRECT_1:15;
     then Wma`2 <= Nmi`2 by PSCOMP_1:71;
     then Nmi <> Wmi by SPRECT_2:61;
     then A31: Card {Nmi,Wmi} = 2 by CARD_2:76;
         {Nmi,Wmi} c= rng (Cage(C,n):-Wmi)
     proof
      let x be set;
      assume x in {Nmi,Wmi};
      hence x in rng (Cage(C,n):-Wmi) by A29,A30,TARSKI:def 2;
     end;
     then A32: Card {Nmi,Wmi} c= Card rng (Cage(C,n):-Wmi) by CARD_1:27;
     Card rng (Cage(C,n):-Wmi) c= Card dom (Cage(C,n):-Wmi) by CARD_2:80;
     then Card rng (Cage(C,n):-Wmi) c= len (Cage(C,n):-Wmi) by PRE_CIRC:21;
     then 2 c= len (Cage(C,n):-Wmi) by A31,A32,XBOOLE_1:1;
     then len (Cage(C,n):-Wmi) >= 2 by CARD_1:56;
     then rng (Cage(C,n):-Wmi) c= L~(Cage(C,n):-Wmi) by SPPOL_2:18;
     then Ema in L~(Cage(C,n)-:Wmi) /\ L~(Cage(C,n):-Wmi)
                                    by A13,A20,A28,XBOOLE_0:def 3;
     then Ema in {Nmi,Wmi} by JORDAN1G:25;
     then Ema = Wmi by A27,TARSKI:def 2;
     hence contradiction by TOPREAL5:25;
    end;
    then A33: Ema in rng (Cage(C,n)-:Wmi/^1) \ rng (Cage(C,n):-Wmi)
                                   by A25,XBOOLE_0:def 4;
    A34: Wmi in rng (Cage(C,n):-Ema) by A3,A4,A12,FINSEQ_6:67;
    A35: Card {Nmi,Ema} = 2 by A27,CARD_2:76;
        (Cage(C,n):-Ema)/.len(Cage(C,n):-Ema) = Cage(C,n)/.len Cage(C,n)
                                                            by A4,FINSEQ_5:57
       .= Cage(C,n)/.1 by FINSEQ_6:def 1
       .= Nmi by JORDAN9:34;
    then A36: Nmi in rng (Cage(C,n):-Ema) by REVROT_1:3;
        (Cage(C,n):-Ema)/.1 = Ema by FINSEQ_5:56;
    then A37: Ema in rng (Cage(C,n):-Ema) by FINSEQ_6:46;
        {Nmi,Ema} c= rng (Cage(C,n):-Ema)
    proof
     let x be set;
     assume x in {Nmi,Ema};
     hence x in rng (Cage(C,n):-Ema) by A36,A37,TARSKI:def 2;
    end;
    then A38: Card {Nmi,Ema} c= Card rng (Cage(C,n):-Ema) by CARD_1:27;
        Card rng (Cage(C,n):-Ema) c= Card dom (Cage(C,n):-Ema) by CARD_2:80;
    then Card rng (Cage(C,n):-Ema) c= len (Cage(C,n):-Ema) by PRE_CIRC:21;
    then 2 c= len (Cage(C,n):-Ema) by A35,A38,XBOOLE_1:1;
    then len (Cage(C,n):-Ema) >= 2 by CARD_1:56;
    then A39: rng (Cage(C,n):-Ema) c= L~(Cage(C,n):-Ema) by SPPOL_2:18;

        not Wmi in rng (Cage(C,n)-:Ema)
    proof
     assume A40: Wmi in rng (Cage(C,n)-:Ema);
     then A41: Cage(C,n)-:Ema is non empty by FINSEQ_1:27;
         (Cage(C,n)-:Ema)/.1 = Cage(C,n)/.1 by A4,FINSEQ_5:47
        .= Nmi by JORDAN9:34;
     then A42: Nmi in rng (Cage(C,n)-:Ema) by A41,FINSEQ_6:46;
         (Cage(C,n)-:Ema)/.len (Cage(C,n)-:Ema) =
           (Cage(C,n)-:Ema)/.(Ema..Cage(C,n)) by A4,FINSEQ_5:45
        .= Ema by A4,FINSEQ_5:48;
     then A43: Ema in rng (Cage(C,n)-:Ema) by A41,REVROT_1:3;
         {Nmi,Ema} c= rng (Cage(C,n)-:Ema)
     proof
      let x be set;
      assume x in {Nmi,Ema};
      hence x in rng (Cage(C,n)-:Ema) by A42,A43,TARSKI:def 2;
     end;
     then A44: Card {Nmi,Ema} c= Card rng (Cage(C,n)-:Ema) by CARD_1:27;
         Card rng (Cage(C,n)-:Ema) c= Card dom (Cage(C,n)-:Ema) by CARD_2:80;
     then Card rng (Cage(C,n)-:Ema) c= len (Cage(C,n)-:Ema) by PRE_CIRC:21;
     then 2 c= len (Cage(C,n)-:Ema) by A35,A44,XBOOLE_1:1;
     then len (Cage(C,n)-:Ema) >= 2 by CARD_1:56;
     then rng (Cage(C,n)-:Ema) c= L~(Cage(C,n)-:Ema) by SPPOL_2:18;
     then Wmi in L~(Cage(C,n)-:Ema) /\ L~(Cage(C,n):-Ema)
                                by A34,A39,A40,XBOOLE_0:def 3;
     then Wmi in {Nmi,Ema} by Th3;
     then Wmi = Ema by A17,TARSKI:def 2;
     hence contradiction by TOPREAL5:25;
    end;
    then A45: Wmi in rng Cage(C,n) \ rng(Cage(C,n)-:Ema) by A3,XBOOLE_0:def 4;
        RotWmi-:Ema = ((Cage(C,n):-Wmi)^((Cage(C,n)-:Wmi)/^1))-:Ema
                                                        by A3,FINSEQ_6:def 2
       .= (Cage(C,n):-Wmi)^(((Cage(C,n)-:Wmi)/^1)-:Ema) by A33,FINSEQ_6:72
       .= (Cage(C,n):-Wmi)^(((Cage(C,n)-:Wmi)-:Ema)/^1) by A13,A23,FINSEQ_6:65
       .= ((Cage(C,n):-Ema):-Wmi)^(((Cage(C,n)-:Wmi)-:Ema)/^1)
                                                         by A4,A45,FINSEQ_6:76
       .= ((Cage(C,n):-Ema):-Wmi)^((Cage(C,n)-:Ema)/^1) by A3,A13,FINSEQ_6:80
       .= ((Cage(C,n):-Ema)^((Cage(C,n)-:Ema)/^1)):-Wmi by A34,FINSEQ_6:69
       .= RotEma:-Wmi by A4,FINSEQ_6:def 2;
    hence Upper_Seq(C,n) = Rotate(Cage(C,n),E-max L~Cage(C,n)):-
                                        W-min L~Cage(C,n) by JORDAN1E:def 1;
   end;

  theorem
    for C be compact non vertical non horizontal Subset of TOP-REAL 2 holds
    W-min L~Cage(C,n) in rng Upper_Seq(C,n) &
    W-min L~Cage(C,n) in L~Upper_Seq(C,n)
   proof
    let C be compact non vertical non horizontal Subset of TOP-REAL 2;
    set p = W-min L~Cage(C,n);
    A1: Upper_Seq(C,n) = Rotate(Cage(C,n),p)-:E-max L~Cage(C,n)
                                                          by JORDAN1E:def 1;
    A2: p in rng Cage(C,n) by SPRECT_2:47;
        E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
    then E-max L~Cage(C,n) in rng Rotate(Cage(C,n),p) by A2,FINSEQ_6:96;
    then Upper_Seq(C,n)/.1 = Rotate(Cage(C,n),p)/.1 by A1,FINSEQ_5:47;
    then Upper_Seq(C,n)/.1 = p by A2,FINSEQ_6:98;
    hence A3: p in rng Upper_Seq(C,n) by FINSEQ_6:46;
        len Upper_Seq(C,n) >= 2 by TOPREAL1:def 10;
    then rng Upper_Seq(C,n) c= L~Upper_Seq(C,n) by SPPOL_2:18;
    hence p in L~Upper_Seq(C,n) by A3;
   end;

  theorem
   for C be compact connected non vertical non horizontal
     Subset of TOP-REAL 2 holds
    W-max L~Cage(C,n) in rng Upper_Seq(C,n) &
    W-max L~Cage(C,n) in L~Upper_Seq(C,n)
   proof
    let C be compact connected non vertical non horizontal
     Subset of TOP-REAL 2;
    set x = W-max L~Cage(C,n);
    set p = W-min L~Cage(C,n);
    set f = Rotate(Cage(C,n),E-max L~Cage(C,n));
        E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
    then A1: rng f = rng Cage(C,n) by FINSEQ_6:96;
    A2: x in rng Cage(C,n) by SPRECT_2:48;
    A3: p in rng Cage(C,n) by SPRECT_2:47;
    A4: Lower_Seq(C,n) = f-:W-min L~Cage(C,n) by JORDAN1G:26;
    A5: E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
        W-min L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:47;
    then W-min L~Cage(C,n) in rng f by A5,FINSEQ_6:96;
    then A6: Lower_Seq(C,n)/.1 = f/.1 by A4,FINSEQ_5:47;
    A7: Lower_Seq(C,n)/.1 = E-max L~Cage(C,n) by JORDAN1F:6;
    A8: L~Cage(C,n) = L~f by REVROT_1:33;
    then (W-min L~f)..f < (W-max L~f)..f by A6,A7,SPRECT_5:43;
    then x in rng(f:-p) by A1,A2,A3,A8,FINSEQ_6:67;
    hence A9: x in rng Upper_Seq(C,n) by Th4;
    len Upper_Seq(C,n) >= 2 by TOPREAL1:def 10;
    then rng Upper_Seq(C,n) c= L~Upper_Seq(C,n) by SPPOL_2:18;
    hence x in L~Upper_Seq(C,n) by A9;
   end;

  theorem Th7:
   for C be compact connected non vertical non horizontal
     Subset of TOP-REAL 2 holds
    N-min L~Cage(C,n) in rng Upper_Seq(C,n) &
    N-min L~Cage(C,n) in L~Upper_Seq(C,n)
   proof
    let C be compact connected non vertical non horizontal
     Subset of TOP-REAL 2;
    set x = N-min L~Cage(C,n);
    set p = W-min L~Cage(C,n);
    set f = Rotate(Cage(C,n),E-max L~Cage(C,n));
    E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
    then A1: rng f = rng Cage(C,n) by FINSEQ_6:96;
    A2: x in rng Cage(C,n) by SPRECT_2:43;
    A3: p in rng Cage(C,n) by SPRECT_2:47;
    A4: Lower_Seq(C,n) = f-:W-min L~Cage(C,n) by JORDAN1G:26;
    A5: E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
        W-min L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:47;
    then W-min L~Cage(C,n) in rng f by A5,FINSEQ_6:96;
    then A6: Lower_Seq(C,n)/.1 = f/.1 by A4,FINSEQ_5:47;
    A7: Lower_Seq(C,n)/.1 = E-max L~Cage(C,n) by JORDAN1F:6;
    A8: L~Cage(C,n) = L~f by REVROT_1:33;
    then A9: (W-min L~f)..f < (W-max L~f)..f by A6,A7,SPRECT_5:43;
        (W-max L~f)..f <= (N-min L~f)..f by A6,A7,A8,SPRECT_5:44;
    then p..f < x..f by A8,A9,XXREAL_0:2;
    then x in rng(f:-p) by A1,A2,A3,FINSEQ_6:67;
    hence A10: x in rng Upper_Seq(C,n) by Th4;
        len Upper_Seq(C,n) >= 2 by TOPREAL1:def 10;
    then rng Upper_Seq(C,n) c= L~Upper_Seq(C,n) by SPPOL_2:18;
    hence x in L~Upper_Seq(C,n) by A10;
   end;

  theorem
   for C be compact connected non vertical non horizontal
     Subset of TOP-REAL 2 holds
    N-max L~Cage(C,n) in rng Upper_Seq(C,n) &
    N-max L~Cage(C,n) in L~Upper_Seq(C,n)
   proof
    let C be compact connected non vertical non horizontal
     Subset of TOP-REAL 2;
    set x = N-max L~Cage(C,n);
    set p = W-min L~Cage(C,n);
    set f = Rotate(Cage(C,n),E-max L~Cage(C,n));
        E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
    then A1: rng f = rng Cage(C,n) by FINSEQ_6:96;
    A2: x in rng Cage(C,n) by SPRECT_2:44;
    A3: p in rng Cage(C,n) by SPRECT_2:47;
    A4: Lower_Seq(C,n) = f-:W-min L~Cage(C,n) by JORDAN1G:26;
    A5: E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
        W-min L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:47;
    then W-min L~Cage(C,n) in rng f by A5,FINSEQ_6:96;
    then A6: Lower_Seq(C,n)/.1 = f/.1 by A4,FINSEQ_5:47;
    A7: Lower_Seq(C,n)/.1 = E-max L~Cage(C,n) by JORDAN1F:6;
    A8: L~Cage(C,n) = L~f by REVROT_1:33;
    then A9: (W-min L~f)..f < (W-max L~f)..f by A6,A7,SPRECT_5:43;
    A10: (W-max L~f)..f <= (N-min L~f)..f by A6,A7,A8,SPRECT_5:44;
    per cases;
     suppose N-max L~f <> E-max L~f;
      then (N-min L~f)..f < (N-max L~f)..f by A6,A7,A8,SPRECT_5:45;
      then (W-max L~f)..f < (N-max L~f)..f by A10,XXREAL_0:2;
      then p..f < x..f by A8,A9,XXREAL_0:2;
      then x in rng(f:-p) by A1,A2,A3,FINSEQ_6:67;
      hence A11: x in rng Upper_Seq(C,n) by Th4;
          len Upper_Seq(C,n) >= 2 by TOPREAL1:def 10;
      then rng Upper_Seq(C,n) c= L~Upper_Seq(C,n) by SPPOL_2:18;
      hence x in L~Upper_Seq(C,n) by A11;
     end;
     suppose A12: N-max L~f = E-max L~f;
      A13: Upper_Seq(C,n) = Rotate(Cage(C,n),p)-:E-max L~Cage(C,n)
                                                          by JORDAN1E:def 1;
      A14: W-min L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:47;
          E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
      then A15: E-max L~Cage(C,n) in rng Rotate(Cage(C,n),W-min L~Cage(C,n))
                                                          by A14,FINSEQ_6:96;
          (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n)) <=
       (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n));
      hence A16: x in rng Upper_Seq(C,n) by A8,A12,A13,A15,FINSEQ_5:49;
          len Upper_Seq(C,n) >= 2 by TOPREAL1:def 10;
      then rng Upper_Seq(C,n) c= L~Upper_Seq(C,n) by SPPOL_2:18;
      hence x in L~Upper_Seq(C,n) by A16;
   end;
   end;

  theorem
       for C be compact non vertical non horizontal Subset of TOP-REAL 2 holds
    E-max L~Cage(C,n) in rng Upper_Seq(C,n) &
    E-max L~Cage(C,n) in L~Upper_Seq(C,n)
   proof
    let C be compact non vertical non horizontal Subset of TOP-REAL 2;
    set x = E-max L~Cage(C,n);
    set p = W-min L~Cage(C,n);
    A1: Upper_Seq(C,n) = Rotate(Cage(C,n),p)-:E-max L~Cage(C,n)
                                                          by JORDAN1E:def 1;
    A2: W-min L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:47;
        E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
    then A3: E-max L~Cage(C,n) in rng Rotate(Cage(C,n),W-min L~Cage(C,n))
                                                          by A2,FINSEQ_6:96;
        (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n)) <=
     (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n));
    hence A4: x in rng Upper_Seq(C,n) by A1,A3,FINSEQ_5:49;
        len Upper_Seq(C,n) >= 2 by TOPREAL1:def 10;
    then rng Upper_Seq(C,n) c= L~Upper_Seq(C,n) by SPPOL_2:18;
    hence x in L~Upper_Seq(C,n) by A4;
   end;

  theorem
       for C be compact non vertical non horizontal Subset of TOP-REAL 2 holds
    E-max L~Cage(C,n) in rng Lower_Seq(C,n) &
    E-max L~Cage(C,n) in L~Lower_Seq(C,n)
   proof
    let C be compact non vertical non horizontal Subset of TOP-REAL 2;
    set p = E-max L~Cage(C,n);
        Lower_Seq(C,n) = Rotate(Cage(C,n),W-min L~Cage(C,n)):-p by
JORDAN1E:def 2;
    hence A1: p in rng Lower_Seq(C,n) by FINSEQ_6:66;
        len Lower_Seq(C,n) >= 2 by TOPREAL1:def 10;
    then rng Lower_Seq(C,n) c= L~Lower_Seq(C,n) by SPPOL_2:18;
    hence p in L~Lower_Seq(C,n) by A1;
   end;

  theorem
       for C be compact non vertical non horizontal Subset of TOP-REAL 2 holds
    E-min L~Cage(C,n) in rng Lower_Seq(C,n) &
    E-min L~Cage(C,n) in L~Lower_Seq(C,n)
   proof
    let C be compact non vertical non horizontal Subset of TOP-REAL 2;
    set x = E-min L~Cage(C,n);
    set p = E-max L~Cage(C,n);
    set f = Rotate(Cage(C,n),W-min L~Cage(C,n));
        W-min L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:47;
    then A1: rng f = rng Cage(C,n) by FINSEQ_6:96;
    A2: x in rng Cage(C,n) by SPRECT_2:49;
    A3: p in rng Cage(C,n) by SPRECT_2:50;
    A4: Upper_Seq(C,n) = f-:E-max L~Cage(C,n) by JORDAN1E:def 1;
    A5: W-min L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:47;
        E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
    then E-max L~Cage(C,n) in rng f by A5,FINSEQ_6:96;
    then A6: Upper_Seq(C,n)/.1 = f/.1 by A4,FINSEQ_5:47;
    A7: Upper_Seq(C,n)/.1 = W-min L~Cage(C,n) by JORDAN1F:5;
    A8: L~Cage(C,n) = L~f by REVROT_1:33;
    then (E-max L~f)..f < (E-min L~f)..f by A6,A7,SPRECT_5:27;
    then x in rng(f:-p) by A1,A2,A3,A8,FINSEQ_6:67;
    hence A9: x in rng Lower_Seq(C,n) by JORDAN1E:def 2;
        len Lower_Seq(C,n) >= 2 by TOPREAL1:def 10;
    then rng Lower_Seq(C,n) c= L~Lower_Seq(C,n) by SPPOL_2:18;
    hence x in L~Lower_Seq(C,n) by A9;
   end;

  theorem Th12:
   for C be compact non vertical non horizontal Subset of TOP-REAL 2 holds
    S-max L~Cage(C,n) in rng Lower_Seq(C,n) &
    S-max L~Cage(C,n) in L~Lower_Seq(C,n)
   proof
    let C be compact non vertical non horizontal Subset of TOP-REAL 2;
    set x = S-max L~Cage(C,n);
    set p = E-max L~Cage(C,n);
    set f = Rotate(Cage(C,n),W-min L~Cage(C,n));
        W-min L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:47;
    then A1: rng f = rng Cage(C,n) by FINSEQ_6:96;
    A2: x in rng Cage(C,n) by SPRECT_2:46;
    A3: p in rng Cage(C,n) by SPRECT_2:50;
    A4: Upper_Seq(C,n) = f-:E-max L~Cage(C,n) by JORDAN1E:def 1;
    A5: W-min L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:47;
        E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
    then E-max L~Cage(C,n) in rng f by A5,FINSEQ_6:96;
    then A6: Upper_Seq(C,n)/.1 = f/.1 by A4,FINSEQ_5:47;
    A7: Upper_Seq(C,n)/.1 = W-min L~Cage(C,n) by JORDAN1F:5;
    A8: L~Cage(C,n) = L~f by REVROT_1:33;
    then A9: (E-max L~f)..f < (E-min L~f)..f by A6,A7,SPRECT_5:27;
        (E-min L~f)..f <= (S-max L~f)..f by A6,A7,A8,SPRECT_5:28;
    then p..f < x..f by A8,A9,XXREAL_0:2;
    then x in rng(f:-p) by A1,A2,A3,FINSEQ_6:67;
    hence A10: x in rng Lower_Seq(C,n) by JORDAN1E:def 2;
        len Lower_Seq(C,n) >= 2 by TOPREAL1:def 10;
    then rng Lower_Seq(C,n) c= L~Lower_Seq(C,n) by SPPOL_2:18;
    hence x in L~Lower_Seq(C,n) by A10;
   end;

  theorem
       for C be compact non vertical non horizontal Subset of TOP-REAL 2 holds
    S-min L~Cage(C,n) in rng Lower_Seq(C,n) &
    S-min L~Cage(C,n) in L~Lower_Seq(C,n)
   proof
    let C be compact non vertical non horizontal Subset of TOP-REAL 2;
    set x = S-min L~Cage(C,n);
    set p = E-max L~Cage(C,n);
    set f = Rotate(Cage(C,n),W-min L~Cage(C,n));
        W-min L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:47;
    then A1: rng f = rng Cage(C,n) by FINSEQ_6:96;
    A2: x in rng Cage(C,n) by SPRECT_2:45;
    A3: p in rng Cage(C,n) by SPRECT_2:50;
    A4: Upper_Seq(C,n) = f-:E-max L~Cage(C,n) by JORDAN1E:def 1;
    A5: W-min L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:47;
        E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
    then E-max L~Cage(C,n) in rng f by A5,FINSEQ_6:96;
    then A6: Upper_Seq(C,n)/.1 = f/.1 by A4,FINSEQ_5:47;
    A7: Upper_Seq(C,n)/.1 = W-min L~Cage(C,n) by JORDAN1F:5;
    A8: L~Cage(C,n) = L~f by REVROT_1:33;
    then A9: (E-max L~f)..f < (E-min L~f)..f by A6,A7,SPRECT_5:27;
    A10: (E-min L~f)..f <= (S-max L~f)..f by A6,A7,A8,SPRECT_5:28;
    per cases;
     suppose S-min L~f <> W-min L~f;
      then (S-max L~f)..f < (S-min L~f)..f by A6,A7,A8,SPRECT_5:29;
      then (E-min L~f)..f < (S-min L~f)..f by A10,XXREAL_0:2;
      then p..f < x..f by A8,A9,XXREAL_0:2;
      then x in rng(f:-p) by A1,A2,A3,FINSEQ_6:67;
      hence A11: x in rng Lower_Seq(C,n) by JORDAN1E:def 2;
          len Lower_Seq(C,n) >= 2 by TOPREAL1:def 10;
      then rng Lower_Seq(C,n) c= L~Lower_Seq(C,n) by SPPOL_2:18;
      hence x in L~Lower_Seq(C,n) by A11;
     end;
     suppose A12: S-min L~f = W-min L~f;
      Lower_Seq(C,n)/.(len Lower_Seq(C,n)) = W-min L~Cage(C,n) by
JORDAN1F:8;
      hence A13: x in rng Lower_Seq(C,n) by A8,A12,REVROT_1:3;
          len Lower_Seq(C,n) >= 2 by TOPREAL1:def 10;
      then rng Lower_Seq(C,n) c= L~Lower_Seq(C,n) by SPPOL_2:18;
      hence x in L~Lower_Seq(C,n) by A13;
   end;
   end;

  theorem
       for C be compact non vertical non horizontal Subset of TOP-REAL 2 holds
    W-min L~Cage(C,n) in rng Lower_Seq(C,n) &
    W-min L~Cage(C,n) in L~Lower_Seq(C,n)
   proof
    let C be compact non vertical non horizontal Subset of TOP-REAL 2;
    set p = W-min L~Cage(C,n);
        Lower_Seq(C,n)/.(len Lower_Seq(C,n)) = p by JORDAN1F:8;
    hence A1: p in rng Lower_Seq(C,n) by REVROT_1:3;
        len Lower_Seq(C,n) >= 2 by TOPREAL1:def 10;
    then rng Lower_Seq(C,n) c= L~Lower_Seq(C,n) by SPPOL_2:18;
    hence p in L~Lower_Seq(C,n) by A1;
   end;

  theorem Th15:
   for X,Y be non empty compact Subset of TOP-REAL 2 st
    X c= Y & N-min Y in X holds
     N-min X = N-min Y
   proof
    let X,Y be non empty compact Subset of TOP-REAL 2;
    assume that
     A1: X c= Y and
     A2: N-min Y in X;
    A3: N-bound X <= N-bound Y by A1,PSCOMP_1:129;
    A4: (N-min X)`2 = N-bound X & (N-min Y)`2 = N-bound Y by EUCLID:56;
        N-bound X >= (N-min Y)`2 by A2,PSCOMP_1:71;
    then A5: N-bound X = N-bound Y by A3,A4,XXREAL_0:1;
    then N-min Y in N-most X by A2,A4,SPRECT_2:14;
    then A6: (N-min X)`1 <= (N-min Y)`1 by PSCOMP_1:98;
        N-min X in X by SPRECT_1:13;
    then N-min X in N-most Y by A1,A4,A5,SPRECT_2:14;
    then (N-min X)`1 >= (N-min Y)`1 by PSCOMP_1:98;
    then (N-min X)`1 = (N-min Y)`1 by A6,XXREAL_0:1;
    hence N-min X = N-min Y by A4,A5,TOPREAL3:11;
   end;

  theorem Th16:
   for X,Y be non empty compact Subset of TOP-REAL 2 st
    X c= Y & N-max Y in X holds
     N-max X = N-max Y
   proof
    let X,Y be non empty compact Subset of TOP-REAL 2;
    assume that
     A1: X c= Y and
     A2: N-max Y in X;
    A3: N-bound X <= N-bound Y by A1,PSCOMP_1:129;
    A4: (N-max X)`2 = N-bound X & (N-max Y)`2 = N-bound Y by EUCLID:56;
        N-bound X >= (N-max Y)`2 by A2,PSCOMP_1:71;
    then A5: N-bound X = N-bound Y by A3,A4,XXREAL_0:1;
    then N-max Y in N-most X by A2,A4,SPRECT_2:14;
    then A6: (N-max X)`1 >= (N-max Y)`1 by PSCOMP_1:98;
        N-max X in X by SPRECT_1:13;
    then N-max X in N-most Y by A1,A4,A5,SPRECT_2:14;
    then (N-max X)`1 <= (N-max Y)`1 by PSCOMP_1:98;
    then (N-max X)`1 = (N-max Y)`1 by A6,XXREAL_0:1;
    hence N-max X = N-max Y by A4,A5,TOPREAL3:11;
   end;

  theorem Th17:
       for X,Y be non empty compact Subset of TOP-REAL 2 st
    X c= Y & E-min Y in X holds
     E-min X = E-min Y
   proof
    let X,Y be non empty compact Subset of TOP-REAL 2;
    assume that
     A1: X c= Y and
     A2: E-min Y in X;
    A3: E-bound X <= E-bound Y by A1,PSCOMP_1:130;
    A4: (E-min X)`1 = E-bound X & (E-min Y)`1 = E-bound Y by EUCLID:56;
        E-bound X >= (E-min Y)`1 by A2,PSCOMP_1:71;
    then A5: E-bound X = E-bound Y by A3,A4,XXREAL_0:1;
    then E-min Y in E-most X by A2,A4,SPRECT_2:17;
    then A6: (E-min X)`2 <= (E-min Y)`2 by PSCOMP_1:108;
        E-min X in X by SPRECT_1:16;
    then E-min X in E-most Y by A1,A4,A5,SPRECT_2:17;
    then (E-min X)`2 >= (E-min Y)`2 by PSCOMP_1:108;
    then (E-min X)`2 = (E-min Y)`2 by A6,XXREAL_0:1;
    hence E-min X = E-min Y by A4,A5,TOPREAL3:11;
   end;

  theorem Th18:
    for X,Y be non empty compact Subset of TOP-REAL 2 st
    X c= Y & E-max Y in X holds
     E-max X = E-max Y
   proof
    let X,Y be non empty compact Subset of TOP-REAL 2;
    assume that
     A1: X c= Y and
     A2: E-max Y in X;
    A3: E-bound X <= E-bound Y by A1,PSCOMP_1:130;
    A4: (E-max X)`1 = E-bound X & (E-max Y)`1 = E-bound Y by EUCLID:56;
        E-bound X >= (E-max Y)`1 by A2,PSCOMP_1:71;
    then A5: E-bound X = E-bound Y by A3,A4,XXREAL_0:1;
    then E-max Y in E-most X by A2,A4,SPRECT_2:17;
    then A6: (E-max X)`2 >= (E-max Y)`2 by PSCOMP_1:108;
        E-max X in X by SPRECT_1:16;
    then E-max X in E-most Y by A1,A4,A5,SPRECT_2:17;
    then (E-max X)`2 <= (E-max Y)`2 by PSCOMP_1:108;
    then (E-max X)`2 = (E-max Y)`2 by A6,XXREAL_0:1;
    hence E-max X = E-max Y by A4,A5,TOPREAL3:11;
   end;

  theorem Th19:
    for X,Y be non empty compact Subset of TOP-REAL 2 st
    X c= Y & S-min Y in X holds
     S-min X = S-min Y
   proof
    let X,Y be non empty compact Subset of TOP-REAL 2;
    assume that
     A1: X c= Y and
     A2: S-min Y in X;
    A3: S-bound X >= S-bound Y by A1,PSCOMP_1:131;
    A4: (S-min X)`2 = S-bound X & (S-min Y)`2 = S-bound Y by EUCLID:56;
        S-bound X <= (S-min Y)`2 by A2,PSCOMP_1:71;
    then A5: S-bound X = S-bound Y by A3,A4,XXREAL_0:1;
    then S-min Y in S-most X by A2,A4,SPRECT_2:15;
    then A6: (S-min X)`1 <= (S-min Y)`1 by PSCOMP_1:118;
        S-min X in X by SPRECT_1:14;
    then S-min X in S-most Y by A1,A4,A5,SPRECT_2:15;
    then (S-min X)`1 >= (S-min Y)`1 by PSCOMP_1:118;
    then (S-min X)`1 = (S-min Y)`1 by A6,XXREAL_0:1;
    hence S-min X = S-min Y by A4,A5,TOPREAL3:11;
   end;

  theorem Th20:
       for X,Y be non empty compact Subset of TOP-REAL 2 st
    X c= Y & S-max Y in X holds
     S-max X = S-max Y
   proof
    let X,Y be non empty compact Subset of TOP-REAL 2;
    assume that
     A1: X c= Y and
     A2: S-max Y in X;
    A3: S-bound X >= S-bound Y by A1,PSCOMP_1:131;
    A4: (S-max X)`2 = S-bound X & (S-max Y)`2 = S-bound Y by EUCLID:56;
        S-bound X <= (S-max Y)`2 by A2,PSCOMP_1:71;
    then A5: S-bound X = S-bound Y by A3,A4,XXREAL_0:1;
    then S-max Y in S-most X by A2,A4,SPRECT_2:15;
    then A6: (S-max X)`1 >= (S-max Y)`1 by PSCOMP_1:118;
        S-max X in X by SPRECT_1:14;
    then S-max X in S-most Y by A1,A4,A5,SPRECT_2:15;
    then (S-max X)`1 <= (S-max Y)`1 by PSCOMP_1:118;
    then (S-max X)`1 = (S-max Y)`1 by A6,XXREAL_0:1;
    hence S-max X = S-max Y by A4,A5,TOPREAL3:11;
   end;

  theorem Th21:
   for X,Y be non empty compact Subset of TOP-REAL 2 st
    X c= Y & W-min Y in X holds
     W-min X = W-min Y
   proof
    let X,Y be non empty compact Subset of TOP-REAL 2;
    assume that
     A1: X c= Y and
     A2: W-min Y in X;
    A3: W-bound X >= W-bound Y by A1,PSCOMP_1:132;
    A4: (W-min X)`1 = W-bound X & (W-min Y)`1 = W-bound Y by EUCLID:56;
        W-bound X <= (W-min Y)`1 by A2,PSCOMP_1:71;
    then A5: W-bound X = W-bound Y by A3,A4,XXREAL_0:1;
    then W-min Y in W-most X by A2,A4,SPRECT_2:16;
    then A6: (W-min X)`2 <= (W-min Y)`2 by PSCOMP_1:88;
        W-min X in X by SPRECT_1:15;
    then W-min X in W-most Y by A1,A4,A5,SPRECT_2:16;
    then (W-min X)`2 >= (W-min Y)`2 by PSCOMP_1:88;
    then (W-min X)`2 = (W-min Y)`2 by A6,XXREAL_0:1;
    hence W-min X = W-min Y by A4,A5,TOPREAL3:11;
   end;

  theorem Th22:
       for X,Y be non empty compact Subset of TOP-REAL 2 st
    X c= Y & W-max Y in X holds
     W-max X = W-max Y
   proof
    let X,Y be non empty compact Subset of TOP-REAL 2;
    assume that
     A1: X c= Y and
     A2: W-max Y in X;
    A3: W-bound X >= W-bound Y by A1,PSCOMP_1:132;
    A4: (W-max X)`1 = W-bound X & (W-max Y)`1 = W-bound Y by EUCLID:56;
        W-bound X <= (W-max Y)`1 by A2,PSCOMP_1:71;
    then A5: W-bound X = W-bound Y by A3,A4,XXREAL_0:1;
    then W-max Y in W-most X by A2,A4,SPRECT_2:16;
    then A6: (W-max X)`2 >= (W-max Y)`2 by PSCOMP_1:88;
        W-max X in X by SPRECT_1:15;
    then W-max X in W-most Y by A1,A4,A5,SPRECT_2:16;
    then (W-max X)`2 <= (W-max Y)`2 by PSCOMP_1:88;
    then (W-max X)`2 = (W-max Y)`2 by A6,XXREAL_0:1;
    hence W-max X = W-max Y by A4,A5,TOPREAL3:11;
   end;

  theorem Th23:
   for X,Y be non empty compact Subset of TOP-REAL 2 st
    N-bound X <= N-bound Y holds
     N-bound (X\/Y) = N-bound Y
   proof
    let X,Y be non empty compact Subset of TOP-REAL 2;
    assume N-bound X <= N-bound Y;
    then max(N-bound X,N-bound Y) = N-bound Y by XXREAL_0:def 9;
    hence N-bound (X\/Y) = N-bound Y by SPRECT_1:56;
   end;

  theorem Th24:
   for X,Y be non empty compact Subset of TOP-REAL 2 st
    E-bound X <= E-bound Y holds
     E-bound (X\/Y) = E-bound Y
   proof
    let X,Y be non empty compact Subset of TOP-REAL 2;
    assume E-bound X <= E-bound Y;
    then max(E-bound X,E-bound Y) = E-bound Y by XXREAL_0:def 9;
    hence E-bound (X\/Y) = E-bound Y by SPRECT_1:57;
   end;

  theorem Th25:
   for X,Y be non empty compact Subset of TOP-REAL 2 st
    S-bound X <= S-bound Y holds
     S-bound (X\/Y) = S-bound X
   proof
    let X,Y be non empty compact Subset of TOP-REAL 2;
    assume S-bound X <= S-bound Y;
    then min(S-bound X,S-bound Y) = S-bound X by XXREAL_0:def 8;
    hence S-bound (X\/Y) = S-bound X by SPRECT_1:55;
   end;

  theorem Th26:
   for X,Y be non empty compact Subset of TOP-REAL 2 st
    W-bound X <= W-bound Y holds
     W-bound (X\/Y) = W-bound X
   proof
    let X,Y be non empty compact Subset of TOP-REAL 2;
    assume W-bound X <= W-bound Y;
    then min(W-bound X,W-bound Y) = W-bound X by XXREAL_0:def 8;
    hence W-bound (X\/Y) = W-bound X by SPRECT_1:54;
   end;

  theorem
       for X,Y be non empty compact Subset of TOP-REAL 2 st
    N-bound X < N-bound Y holds
     N-min (X\/Y) = N-min Y
   proof
    let X,Y be non empty compact Subset of TOP-REAL 2;
    A1: X\/Y is compact by COMPTS_1:19;
    assume A2: N-bound X < N-bound Y;
    then A3: N-bound (X\/Y) = N-bound Y by Th23;
    A4: (N-min(X\/Y))`2 = N-bound (X\/Y) & (N-min Y)`2 = N-bound Y
                                                              by EUCLID:56;
    A5: Y c= X\/Y by XBOOLE_1:7;
        N-min Y in Y by SPRECT_1:13;
    then N-min Y in N-most(X\/Y) by A1,A3,A4,A5,SPRECT_2:14;
    then A6: (N-min(X\/Y))`1 <= (N-min Y)`1 by A1,PSCOMP_1:98;
    A7: N-min(X\/Y) in X\/Y by A1,SPRECT_1:13;
    per cases by A7,XBOOLE_0:def 2;
     suppose N-min(X\/Y) in Y;
      then N-min(X\/Y) in N-most Y by A3,A4,SPRECT_2:14;
      then (N-min(X\/Y))`1 >= (N-min Y)`1 by PSCOMP_1:98;
      then (N-min(X\/Y))`1 = (N-min Y)`1 by A6,XXREAL_0:1;
      hence N-min (X\/Y) = N-min Y by A3,A4,TOPREAL3:11;
     end;
     suppose N-min(X\/Y) in X;
      hence N-min (X\/Y) = N-min Y by A2,A3,A4,PSCOMP_1:71;
   end;
   end;

  theorem
       for X,Y be non empty compact Subset of TOP-REAL 2 st
    N-bound X < N-bound Y holds
     N-max (X\/Y) = N-max Y
   proof
    let X,Y be non empty compact Subset of TOP-REAL 2;
    A1: X\/Y is compact by COMPTS_1:19;
    assume A2: N-bound X < N-bound Y;
    then A3: N-bound (X\/Y) = N-bound Y by Th23;
    A4: (N-max(X\/Y))`2 = N-bound (X\/Y) & (N-max Y)`2 = N-bound Y
                                                              by EUCLID:56;
    A5: Y c= X\/Y by XBOOLE_1:7;
        N-max Y in Y by SPRECT_1:13;
    then N-max Y in N-most(X\/Y) by A1,A3,A4,A5,SPRECT_2:14;
    then A6: (N-max(X\/Y))`1 >= (N-max Y)`1 by A1,PSCOMP_1:98;
    A7: N-max(X\/Y) in X\/Y by A1,SPRECT_1:13;
    per cases by A7,XBOOLE_0:def 2;
     suppose N-max(X\/Y) in Y;
      then N-max(X\/Y) in N-most Y by A3,A4,SPRECT_2:14;
      then (N-max(X\/Y))`1 <= (N-max Y)`1 by PSCOMP_1:98;
      then (N-max(X\/Y))`1 = (N-max Y)`1 by A6,XXREAL_0:1;
      hence N-max (X\/Y) = N-max Y by A3,A4,TOPREAL3:11;
     end;
     suppose N-max(X\/Y) in X;
      hence N-max (X\/Y) = N-max Y by A2,A3,A4,PSCOMP_1:71;
   end;
   end;

  theorem
       for X,Y be non empty compact Subset of TOP-REAL 2 st
    E-bound X < E-bound Y holds
     E-min (X\/Y) = E-min Y
   proof
    let X,Y be non empty compact Subset of TOP-REAL 2;
    A1: X\/Y is compact by COMPTS_1:19;
    assume A2: E-bound X < E-bound Y;
    then A3: E-bound (X\/Y) = E-bound Y by Th24;
    A4: (E-min(X\/Y))`1 = E-bound (X\/Y) & (E-min Y)`1 = E-bound Y
                                                              by EUCLID:56;
    A5: Y c= X\/Y by XBOOLE_1:7;
        E-min Y in Y by SPRECT_1:16;
    then E-min Y in E-most(X\/Y) by A1,A3,A4,A5,SPRECT_2:17;
    then A6: (E-min(X\/Y))`2 <= (E-min Y)`2 by A1,PSCOMP_1:108;
    A7: E-min(X\/Y) in X\/Y by A1,SPRECT_1:16;
    per cases by A7,XBOOLE_0:def 2;
     suppose E-min(X\/Y) in Y;
      then E-min(X\/Y) in E-most Y by A3,A4,SPRECT_2:17;
      then (E-min(X\/Y))`2 >= (E-min Y)`2 by PSCOMP_1:108;
      then (E-min(X\/Y))`2 = (E-min Y)`2 by A6,XXREAL_0:1;
      hence E-min (X\/Y) = E-min Y by A3,A4,TOPREAL3:11;
     end;
     suppose E-min(X\/Y) in X;
      hence E-min (X\/Y) = E-min Y by A2,A3,A4,PSCOMP_1:71;
   end;
   end;

  theorem
       for X,Y be non empty compact Subset of TOP-REAL 2 st
    E-bound X < E-bound Y holds
     E-max (X\/Y) = E-max Y
   proof
    let X,Y be non empty compact Subset of TOP-REAL 2;
    A1: X\/Y is compact by COMPTS_1:19;
    assume A2: E-bound X < E-bound Y;
    then A3: E-bound (X\/Y) = E-bound Y by Th24;
    A4: (E-max(X\/Y))`1 = E-bound (X\/Y) & (E-max Y)`1 = E-bound Y
                                                              by EUCLID:56;
    A5: Y c= X\/Y by XBOOLE_1:7;
        E-max Y in Y by SPRECT_1:16;
    then E-max Y in E-most(X\/Y) by A1,A3,A4,A5,SPRECT_2:17;
    then A6: (E-max(X\/Y))`2 >= (E-max Y)`2 by A1,PSCOMP_1:108;
    A7: E-max(X\/Y) in X\/Y by A1,SPRECT_1:16;
    per cases by A7,XBOOLE_0:def 2;
     suppose E-max(X\/Y) in Y;
      then E-max(X\/Y) in E-most Y by A3,A4,SPRECT_2:17;
      then (E-max(X\/Y))`2 <= (E-max Y)`2 by PSCOMP_1:108;
      then (E-max(X\/Y))`2 = (E-max Y)`2 by A6,XXREAL_0:1;
      hence E-max (X\/Y) = E-max Y by A3,A4,TOPREAL3:11;
     end;
     suppose E-max(X\/Y) in X;
      hence E-max (X\/Y) = E-max Y by A2,A3,A4,PSCOMP_1:71;
   end;
   end;

  theorem
       for X,Y be non empty compact Subset of TOP-REAL 2 st
    S-bound X < S-bound Y holds
     S-min (X\/Y) = S-min X
   proof
    let X,Y be non empty compact Subset of TOP-REAL 2;
    A1: X\/Y is compact by COMPTS_1:19;
    assume A2: S-bound X < S-bound Y;
    then A3: S-bound (X\/Y) = S-bound X by Th25;
    A4: (S-min(X\/Y))`2 = S-bound (X\/Y) & (S-min X)`2 = S-bound X
                                                              by EUCLID:56;
    A5: X c= X\/Y by XBOOLE_1:7;
        S-min X in X by SPRECT_1:14;
    then S-min X in S-most(X\/Y) by A1,A3,A4,A5,SPRECT_2:15;
    then A6: (S-min(X\/Y))`1 <= (S-min X)`1 by A1,PSCOMP_1:118;
    A7: S-min(X\/Y) in X\/Y by A1,SPRECT_1:14;
    per cases by A7,XBOOLE_0:def 2;
     suppose S-min(X\/Y) in X;
      then S-min(X\/Y) in S-most X by A3,A4,SPRECT_2:15;
      then (S-min(X\/Y))`1 >= (S-min X)`1 by PSCOMP_1:118;
      then (S-min(X\/Y))`1 = (S-min X)`1 by A6,XXREAL_0:1;
      hence S-min (X\/Y) = S-min X by A3,A4,TOPREAL3:11;
     end;
     suppose S-min(X\/Y) in Y;
      hence S-min (X\/Y) = S-min X by A2,A3,A4,PSCOMP_1:71;
   end;
   end;

  theorem
       for X,Y be non empty compact Subset of TOP-REAL 2 st
    S-bound X < S-bound Y holds
     S-max (X\/Y) = S-max X
   proof
    let X,Y be non empty compact Subset of TOP-REAL 2;
    A1: X\/Y is compact by COMPTS_1:19;
    assume A2: S-bound X < S-bound Y;
    then A3: S-bound (X\/Y) = S-bound X by Th25;
    A4: (S-max(X\/Y))`2 = S-bound (X\/Y) & (S-max X)`2 = S-bound X
                                                              by EUCLID:56;
    A5: X c= X\/Y by XBOOLE_1:7;
        S-max X in X by SPRECT_1:14;
    then S-max X in S-most(X\/Y) by A1,A3,A4,A5,SPRECT_2:15;
    then A6: (S-max(X\/Y))`1 >= (S-max X)`1 by A1,PSCOMP_1:118;
    A7: S-max(X\/Y) in X\/Y by A1,SPRECT_1:14;
    per cases by A7,XBOOLE_0:def 2;
     suppose S-max(X\/Y) in X;
      then S-max(X\/Y) in S-most X by A3,A4,SPRECT_2:15;
      then (S-max(X\/Y))`1 <= (S-max X)`1 by PSCOMP_1:118;
      then (S-max(X\/Y))`1 = (S-max X)`1 by A6,XXREAL_0:1;
      hence S-max (X\/Y) = S-max X by A3,A4,TOPREAL3:11;
     end;
     suppose S-max(X\/Y) in Y;
      hence S-max (X\/Y) = S-max X by A2,A3,A4,PSCOMP_1:71;
   end;
   end;

  theorem Th33:
   for X,Y be non empty compact Subset of TOP-REAL 2 st
    W-bound X < W-bound Y holds
     W-min (X\/Y) = W-min X
   proof
    let X,Y be non empty compact Subset of TOP-REAL 2;
    A1: X\/Y is compact by COMPTS_1:19;
    assume A2: W-bound X < W-bound Y;
    then A3: W-bound (X\/Y) = W-bound X by Th26;
    A4: (W-min(X\/Y))`1 = W-bound (X\/Y) & (W-min X)`1 = W-bound X
                                                              by EUCLID:56;
    A5: X c= X\/Y by XBOOLE_1:7;
    W-min X in X by SPRECT_1:15;
    then W-min X in W-most(X\/Y) by A1,A3,A4,A5,SPRECT_2:16;
    then A6: (W-min(X\/Y))`2 <= (W-min X)`2 by A1,PSCOMP_1:88;
    A7: W-min(X\/Y) in X\/Y by A1,SPRECT_1:15;
    per cases by A7,XBOOLE_0:def 2;
     suppose W-min(X\/Y) in X;
      then W-min(X\/Y) in W-most X by A3,A4,SPRECT_2:16;
      then (W-min(X\/Y))`2 >= (W-min X)`2 by PSCOMP_1:88;
      then (W-min(X\/Y))`2 = (W-min X)`2 by A6,XXREAL_0:1;
      hence W-min (X\/Y) = W-min X by A3,A4,TOPREAL3:11;
     end;
     suppose W-min(X\/Y) in Y;
      hence W-min (X\/Y) = W-min X by A2,A3,A4,PSCOMP_1:71;
   end;
   end;

  theorem
    for X,Y be non empty compact Subset of TOP-REAL 2 st
    W-bound X < W-bound Y holds
     W-max (X\/Y) = W-max X
   proof
    let X,Y be non empty compact Subset of TOP-REAL 2;
    A1: X\/Y is compact by COMPTS_1:19;
    assume A2: W-bound X < W-bound Y;
    then A3: W-bound (X\/Y) = W-bound X by Th26;
    A4: (W-max(X\/Y))`1 = W-bound (X\/Y) & (W-max X)`1 = W-bound X
                                                              by EUCLID:56;
    A5: X c= X\/Y by XBOOLE_1:7;
        W-max X in X by SPRECT_1:15;
    then W-max X in W-most(X\/Y) by A1,A3,A4,A5,SPRECT_2:16;
    then A6: (W-max(X\/Y))`2 >= (W-max X)`2 by A1,PSCOMP_1:88;
    A7: W-max(X\/Y) in X\/Y by A1,SPRECT_1:15;
    per cases by A7,XBOOLE_0:def 2;
     suppose W-max(X\/Y) in X;
      then W-max(X\/Y) in W-most X by A3,A4,SPRECT_2:16;
      then (W-max(X\/Y))`2 <= (W-max X)`2 by PSCOMP_1:88;
      then (W-max(X\/Y))`2 = (W-max X)`2 by A6,XXREAL_0:1;
      hence W-max (X\/Y) = W-max X by A3,A4,TOPREAL3:11;
     end;
     suppose W-max(X\/Y) in Y;
      hence W-max (X\/Y) = W-max X by A2,A3,A4,PSCOMP_1:71;
   end;
   end;

  theorem Th35:
   for f be FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2 st f is_S-Seq & p in L~f holds
    L_Cut(f,p)/.len L_Cut(f,p) = f/.len f
   proof
    let f be FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2;
    assume that
A1: f is_S-Seq and
A2: p in L~f;
    len f > 0 by A1,TOPREAL1:def 10; then
    f <> {} by FINSEQ_1:25; then
A3: len f in dom f by FINSEQ_5:6;
    L_Cut(f,p) <> {} by A2,JORDAN1E:7;
    then len L_Cut(f,p) in dom L_Cut(f,p) by FINSEQ_5:6;
    hence L_Cut(f,p)/.len L_Cut(f,p) = L_Cut(f,p).len L_Cut(f,p)
                                                     by FINSEQ_4:def 4
       .= f.len f by A1,A2,JORDAN1B:5
       .= f/.len f by A3,FINSEQ_4:def 4;
   end;

  theorem Th36:
   for f be non constant standard special_circular_sequence
   for p,q be Point of TOP-REAL 2
   for g be connected Subset of TOP-REAL 2 st
    p in RightComp f & q in LeftComp f & p in g & q in g holds
     g meets L~f
   proof
    let f be non constant standard special_circular_sequence;
    let p,q be Point of TOP-REAL 2;
    let g be connected Subset of TOP-REAL 2;
    assume that
A1: p in RightComp f & q in LeftComp f and
A2: p in g & q in g;
    assume g misses L~f;
    then g c= (L~f)` by TDLAT_1:2;
    then reconsider A = g as Subset of (TOP-REAL 2)|(L~f)` by PRE_TOPC:29;
A3: A is connected by CONNSP_1:24;
    RightComp f is_a_component_of (L~f)` by GOBOARD9:def 2;
    then consider R being Subset of (TOP-REAL 2)|(L~f)` such that
A4: R = RightComp f and
A5: R is_a_component_of (TOP-REAL 2)|(L~f)` by CONNSP_1:def 6;
    LeftComp f is_a_component_of (L~f)` by GOBOARD9:def 1;
    then consider L being Subset of (TOP-REAL 2)|(L~f)` such that
A6: L = LeftComp f and
A7: L is_a_component_of (TOP-REAL 2)|(L~f)` by CONNSP_1:def 6;
    R /\ A <> {} & L /\ A <> {} by A1,A2,A4,A6,XBOOLE_0:def 3;
    then R meets A & L meets A by XBOOLE_0:def 7;
    then RightComp f = LeftComp f by A3,A4,A5,A6,A7,JORDAN2C:100;
    hence contradiction by SPRECT_4:7;
   end;

  registration
   cluster non constant standard s.c.c.
    (being_S-Seq FinSequence of TOP-REAL 2);
   existence
   proof
    consider C be Simple_closed_curve;
    consider n be Element of NAT;
A1: len Upper_Seq(C,n) >= 2 by TOPREAL1:def 10;
A2: Upper_Seq(C,n) is_sequence_on Gauge(C,n) by JORDAN1G:4;
    take Upper_Seq(C,n);
    thus thesis by A1,A2,JGRAPH_1:16,JORDAN8:8;
   end;
  end;

  theorem Th37:
   for f be S-Sequence_in_R2
   for p be Point of TOP-REAL 2 st p in rng f holds
    L_Cut(f,p) = mid(f,p..f,len f)
   proof
    let f be S-Sequence_in_R2;
    let p be Point of TOP-REAL 2;
    assume p in rng f;
    then consider i be Element of NAT such that
A1: i in dom f and
A2: f.i = p by FINSEQ_2:11;
A3: 0+1 <= i & i <= len f by A1,FINSEQ_3:27;
    len f >= 2 by TOPREAL1:def 10;
    then A4: len f >= 1 by XXREAL_0:2;
    per cases by A3,REAL_1:def 5;
     suppose i > 1;
      then A5: Index(p,f) + 1 = i by A2,A3,JORDAN3:45;
      then L_Cut(f,p) = mid(f,Index(p,f)+1,len f) by A2,JORDAN3:def 4;
      hence L_Cut(f,p) = mid(f,p..f,len f) by A1,A2,A5,FINSEQ_5:12;
     end;
     suppose A6: i = 1;
      thus L_Cut(f,p) = L_Cut(f,f/.i) by A1,A2,FINSEQ_4:def 4
         .= f by A6,JORDAN5B:30
         .= mid(f,1,len f) by A4,JORDAN3:29
         .= mid(f,p..f,len f) by A1,A2,A6,FINSEQ_5:12;
   end;
   end;

  theorem Th38:
   for M be Go-board
   for f be S-Sequence_in_R2 st f is_sequence_on M
   for p be Point of TOP-REAL 2 st p in rng f holds
    R_Cut(f,p) is_sequence_on M
   proof
    let M be Go-board;
    let f be S-Sequence_in_R2;
    assume A1: f is_sequence_on M;
    let p be Point of TOP-REAL 2;
    assume p in rng f;
    then R_Cut(f,p) = mid(f,1,p..f) by JORDAN1G:57;
    hence R_Cut(f,p) is_sequence_on M by A1,JORDAN1H:33;
   end;

  theorem Th39:
   for M be Go-board
   for f be S-Sequence_in_R2 st f is_sequence_on M
   for p be Point of TOP-REAL 2 st p in rng f holds
    L_Cut(f,p) is_sequence_on M
   proof
    let M be Go-board;
    let f be S-Sequence_in_R2;
    assume A1: f is_sequence_on M;
    let p be Point of TOP-REAL 2;
    assume p in rng f;
    then L_Cut(f,p) = mid(f,p..f,len f) by Th37;
    hence L_Cut(f,p) is_sequence_on M by A1,JORDAN1H:33;
   end;

  theorem Th40:
   for G be Go-board
   for f be FinSequence of TOP-REAL 2 st f is_sequence_on G
   for i,j be Element of NAT st 1 <= i & i <= len G & 1 <= j & j <= width G
    holds
    G*(i,j) in L~f implies G*(i,j) in rng f
   proof
    let G be Go-board;
    let f be FinSequence of TOP-REAL 2;
    assume A1: f is_sequence_on G;
    let i,j be Element of NAT;
    assume A2: 1 <= i & i <= len G & 1 <= j & j <= width G;
    assume G*(i,j) in L~f;
    then consider k be Element of NAT such that
     A3: 1 <= k and
     A4: k+1 <= len f and
     A5: G*(i,j) in LSeg(f/.k,f/.(k+1)) by SPPOL_2:14;
    consider i1,j1,i2,j2 be Element of NAT such that
     A6: [i1,j1] in Indices G and
     A7: f/.k = G*(i1,j1) and
     A8: [i2,j2] in Indices G and
     A9: f/.(k+1) = G*(i2,j2) and
     A10: i1 = i2 & j1+1 = j2 or i1+1 = i2 & j1 = j2 or
         i1 = i2+1 & j1 = j2 or i1 = i2 & j1 = j2+1 by A1,A3,A4,JORDAN8:6;
    A11: 1 <= i1 & i1 <= len G & 1 <= j1 & j1 <= width G by A6,GOBOARD5:1;
    A12: 1 <= i2 & i2 <= len G & 1 <= j2 & j2 <= width G by A8,GOBOARD5:1;
        k+1 >= 1 & k < len f by A4,NAT_1:11,13;
    then A13: k in dom f & k+1 in dom f by A3,A4,FINSEQ_3:27;
    per cases by A10;
     suppose A14: i1 = i2 & j1+1 = j2;
          j1 <= j1+1 by NAT_1:11;
      then G*(i1,j1)`1 <= G*(i1,j1+1)`1 & G*(i1,j1)`2 <= G*(i1,j1+1)`2
                                                by A11,A12,A14,JORDAN1A:39,
40;
      then G*(i1,j1)`1 <= G*(i,j)`1 & G*(i,j)`1 <= G*(i1,j1+1)`1 &
           G*(i1,j1)`2 <= G*(i,j)`2 & G*(i,j)`2 <= G*(i1,j1+1)`2
                                                by A5,A7,A9,A14,TOPREAL1:9,
10;
      then i1 <= i & i <= i1 & j1 <= j & j <= j1+1
                                             by A2,A11,A12,A14,Th1,Th2;
      then i = i1 & (j = j1 or j = j1+1) by XXREAL_0:1,NAT_1:9;
      hence G*(i,j) in rng f by A7,A9,A13,A14,PARTFUN2:4;
     end;
     suppose A15: i1+1 = i2 & j1 = j2;
          i1 <= i1+1 by NAT_1:11;
      then G*(i1,j1)`1 <= G*(i1+1,j1)`1 & G*(i1,j1)`2 <= G*(i1+1,j1)`2
                                                by A11,A12,A15,JORDAN1A:39,
40;
      then G*(i1,j1)`1 <= G*(i,j)`1 & G*(i,j)`1 <= G*(i1+1,j1)`1 &
           G*(i1,j1)`2 <= G*(i,j)`2 & G*(i,j)`2 <= G*(i1+1,j1)`2
                                                by A5,A7,A9,A15,TOPREAL1:9,
10;
      then j1 <= j & j <= j1 & i1 <= i & i <= i1+1
                                             by A2,A11,A12,A15,Th1,Th2;
      then j = j1 & (i = i1 or i = i1+1) by XXREAL_0:1,NAT_1:9;
      hence G*(i,j) in rng f by A7,A9,A13,A15,PARTFUN2:4;
     end;
     suppose A16: i1 = i2+1 & j1 = j2;
          i2 <= i2+1 by NAT_1:11;
      then G*(i2,j1)`1 <= G*(i2+1,j1)`1 & G*(i2,j1)`2 <= G*(i2+1,j1)`2
                                                by A11,A12,A16,JORDAN1A:39,
40;
      then G*(i2,j1)`1 <= G*(i,j)`1 & G*(i,j)`1 <= G*(i2+1,j1)`1 &
           G*(i2,j1)`2 <= G*(i,j)`2 & G*(i,j)`2 <= G*(i2+1,j1)`2
                                                by A5,A7,A9,A16,TOPREAL1:9,
10;
      then j1 <= j & j <= j1 & i2 <= i & i <= i2+1
                                             by A2,A11,A12,A16,Th1,Th2;
      then j = j1 & (i = i2 or i = i2+1) by XXREAL_0:1,NAT_1:9;
      hence G*(i,j) in rng f by A7,A9,A13,A16,PARTFUN2:4;
     end;
     suppose A17: i1 = i2 & j1 = j2+1;
          j2 <= j2+1 by NAT_1:11;
      then G*(i1,j2)`1 <= G*(i1,j2+1)`1 & G*(i1,j2)`2 <= G*(i1,j2+1)`2
                                                by A11,A12,A17,JORDAN1A:39,
40;
      then G*(i1,j2)`1 <= G*(i,j)`1 & G*(i,j)`1 <= G*(i1,j2+1)`1 &
           G*(i1,j2)`2 <= G*(i,j)`2 & G*(i,j)`2 <= G*(i1,j2+1)`2
                                                by A5,A7,A9,A17,TOPREAL1:9,
10;
      then i1 <= i & i <= i1 & j2 <= j & j <= j2+1
                                             by A2,A11,A12,A17,Th1,Th2;
      then i = i1 & (j = j2 or j = j2+1) by XXREAL_0:1,NAT_1:9;
      hence G*(i,j) in rng f by A7,A9,A13,A17,PARTFUN2:4;
   end;
   end;

  theorem
   for f be S-Sequence_in_R2
   for g be FinSequence of TOP-REAL 2 holds
    g is unfolded s.n.c. one-to-one & L~f /\ L~g = {f/.1} & f/.1 = g/.len g &
    (for i be Element of NAT st 1<=i & i+2 <= len f holds
    LSeg(f,i) /\ LSeg(f/.len f,g/.1) = {}) &
    (for i be Element of NAT st 2<=i & i+1 <= len g holds
     LSeg(g,i) /\ LSeg(f/.len f,g/.1) = {}) implies f^g is s.c.c.
   proof
    let f be S-Sequence_in_R2;
    let g be FinSequence of TOP-REAL 2;
    assume that
A1: g is unfolded s.n.c. one-to-one and
A2: L~f /\ L~g = {f/.1} and
A3: f/.1 = g/.len g and
A4: for i be Element of NAT st 1<=i & i+2<=len f holds
    LSeg(f,i) /\ LSeg(f/.len f,g/.1) = {} and
A5: for i be Element of NAT st 2<=i & i+1 <= len g holds
         LSeg(g,i) /\ LSeg(f/.len f,g/.1) = {};
    A6: now let i be Element of NAT;
     assume 1 <= i & i+2 <= len f;
     then LSeg(f,i) /\ LSeg(f/.len f,g/.1) = {} by A4;
     hence LSeg(f,i) misses LSeg(f/.len f,g/.1) by XBOOLE_0:def 7;
    end;
    A7: now let i be Element of NAT;
     assume 2 <= i & i+1 <= len g;
     then LSeg(g,i) /\ LSeg(f/.len f,g/.1) = {} by A5;
     hence LSeg(g,i) misses LSeg(f/.len f,g/.1) by XBOOLE_0:def 7;
    end;
    let i,j be Element of NAT such that
     A8: i+1 < j and
     A9: i > 1 & j < len (f^g) or j+1 < len (f^g);
    A10: j+1 <= len (f^g) by A9,NAT_1:13;
    per cases;
     suppose i = 0;
      then LSeg(f^g,i) = {} by TOPREAL1:def 5;
      then LSeg(f^g,i) /\ LSeg(f^g,j) = {};
      hence thesis by XBOOLE_0:def 7;
     end;
     suppose A11: i <> 0;
     A12: len(f^g) = len f + len g by FINSEQ_1:35;
     A13: 1 <= i by A11,NAT_1:14;
         i <= i+1 by NAT_1:11;
     then A14: i < j by A8,XXREAL_0:2;
         now per cases;
      suppose A15: j+1 <= len f;
           j <= j+1 by NAT_1:11;
       then i < j+1 by A14,XXREAL_0:2;
       then i < len f by A15,XXREAL_0:2;
       then i+1 <= len f by NAT_1:13;
       then A16: LSeg(f^g,i) = LSeg(f,i) by SPPOL_2:6;
       LSeg(f^g,j) = LSeg(f,j) by A15,SPPOL_2:6;
       hence thesis by A8,A16,TOPREAL1:def 9;
      end;
      suppose j+1 > len f;
       then A17: len f <= j by NAT_1:13;
       then reconsider j' = j - len f as Element of NAT by INT_1:18;
       A18: len f + j' = j;
          A19: j+1-len f <= len g by A10,A12,XREAL_1:22;
       then A20: j'+1 <= len g;
           now per cases;
        suppose A21: i <= len f;
             now per cases;
          suppose A22: i = len f;
           then len f +1+1 <= j by A8,NAT_1:13;
           then len f +(1+1) <= j;
           then A23: 1+1 <= j' by XREAL_1:21;
           then A24: 1 <= j' by XXREAL_0:2;
           then A25: LSeg(f^g,j) = LSeg(g,j') by A18,SPPOL_2:7;
               g is non empty by A20,A24,GOBOARD2:3,RELAT_1:60;
           then LSeg(f^g,i) = LSeg(f/.len f,g/.1) by A22,SPPOL_2:8;
           hence thesis by A7,A20,A23,A25;
          end;
          suppose i <> len f;
           then i < len f by A21,REAL_1:def 5;
           then i+1 <= len f by NAT_1:13;
           then A26: LSeg(f^g,i) = LSeg(f,i) by SPPOL_2:6;
               now per cases;
            suppose A27: j = len f;
             then i+1+1 <= len f by A8,NAT_1:13;
             then A28: i+(1+1) <= len f;
                 1 <= len g by A10,A12,A27,XREAL_1:8;
             then g is non empty by FINSEQ_3:27,RELAT_1:60;
             then LSeg(f^g,j) = LSeg(f/.len f,g/.1) by A27,SPPOL_2:8;
             hence thesis by A6,A13,A26,A28;
            end;
            suppose j <> len f;
             then len f < j by A17,REAL_1:def 5;
             then len f+1 <= j by NAT_1:13;
             then A29: 1 <= j' by XREAL_1:21;
             then A30: LSeg(f^g,len f+j') = LSeg(g,j') by SPPOL_2:7;
             then A31: LSeg(f^g,j) c= L~g by TOPREAL3:26;
                 LSeg(f^g,i) c= L~f by A26,TOPREAL3:26;
             then A32: LSeg(f^g,i) /\ LSeg(f^g,j) c= {f/.1} by A2,A31,
XBOOLE_1:27;
A33:          len f >= 2 by TOPREAL1:def 10;
                 now per cases by A32,ZFMISC_1:39;
              suppose LSeg(f^g,i) /\ LSeg(f^g,j) = {};
               hence thesis by XBOOLE_0:def 7;
              end;
              suppose LSeg(f^g,i) /\ LSeg(f^g,j) = {f/.1};
               then f/.1 in LSeg(f^g,i) /\ LSeg(f^g,j) by TARSKI:def 1;
               then A34: f/.1 in LSeg(f^g,i) & f/.1 in LSeg(f^g,j)
                                                             by XBOOLE_0:def 3;
                   j'+1 >= 1 & j' < len g by A20,NAT_1:11,13;
               then j' in dom g & j'+1 in dom g by A19,A29,FINSEQ_3:27;
               then j'+1=len g by A1,A3,A30,A34,GOBOARD2:7;
               hence thesis by A9,A12,A26,A33,A34,JORDAN5B:33;
             end;
             end;
             hence thesis;
           end;
           end;
           hence thesis;
         end;
         end;
         hence thesis;
        end;
        suppose A35: i > len f;
         then reconsider i' = i - len f as Element of NAT by INT_1:18;
             len f + 1 <= i by A35,NAT_1:13;
         then 1 <= i' by XREAL_1:21;
         then A36: LSeg(f^g,len f+i') = LSeg(g,i') by SPPOL_2:7;
             i+1-len f < j' by A8,XREAL_1:11;
         then A37: i'+1 < j';
             j <> len f by A8,A35,NAT_1:13;
         then len f < j by A17,REAL_1:def 5;
         then len f+1 <= j by NAT_1:13;
         then 1 <= j' by XREAL_1:21;
         then LSeg(f^g,len f+j') = LSeg(g,j') by SPPOL_2:7;
         hence thesis by A1,A36,A37,TOPREAL1:def 9;
       end;
       end;
       hence thesis;
     end;
     end;
     hence thesis;
   end;
   end;

  theorem
   for C be compact non vertical non horizontal non empty Subset of
TOP-REAL 2
   ex i be Element of NAT st 1 <= i & i+1 <= len Gauge(C,n) &
    W-min C in cell(Gauge(C,n),1,i) & W-min C <> Gauge(C,n)*(2,i)
   proof
    let C be compact non vertical non horizontal non empty
     Subset of TOP-REAL 2;
    set G = Gauge(C,n);
    A1: len G = width G by JORDAN8:def 1;
    defpred P[Element of NAT] means 1 <= $1 & $1 < len G &
     G*(2,$1)`2 < (W-min C)`2;
    A2: for k be Element of NAT st P[k] holds k <= len G;
    A3: len G = 2|^n+3 & len G = width G by JORDAN8:def 1;
    A4: len G >= 4 by JORDAN8:13;
    then A5: 1 < len G by XXREAL_0:2;
        (SW-corner C)`2 <= (W-min C)`2 by PSCOMP_1:87;
    then A6: S-bound C <= (W-min C)`2 by EUCLID:56;
    A7: 2 <= len G by A4,XXREAL_0:2;
    then G*(2,2)`2 = S-bound C by JORDAN8:16;
    then G*(2,1)`2 < S-bound C by A3,A7,GOBOARD5:5;
    then G*(2,1)`2 < (W-min C)`2 by A6,XXREAL_0:2;
    then A8: ex k be Element of NAT st P[k] by A5;
      ex i being Element of NAT st P[i] &
       for n st P[n] holds n <= i from NAT_1:sch 6
(A2,A8);
    then consider i being Element of NAT such that
     A9: 1 <= i & i < len G and
     A10: G*(2,i)`2 < (W-min C)`2 and
     A11: for n st P[n] holds n <= i;
    take i;
    thus 1 <= i & i+1 <= len G by A9,NAT_1:13;
    A12: LSeg(G*(1+1,i),G*(1+1,i+1)) c= cell(G,1,i) by A1,A5,A9,GOBOARD5:19;
    A13: i+1 <= len G by A9,NAT_1:13;
    A14: 1 <= i+1 by NAT_1:12;
        (W-min C)`1 = W-bound C by EUCLID:56;
    then A15: G*(2,i)`1 = (W-min C)`1 & (W-min C)`1 = G*(2,i+1)`1
                                                by A9,A13,A14,JORDAN8:14;
        now assume i+1 = len G;
     then len G-'1 = i by BINARITH:39;
     then A16: G*(2,i)`2 = N-bound C by A7,JORDAN8:17;
         (NW-corner C)`2 >= (W-min C)`2 by PSCOMP_1:87;
     hence contradiction by A10,A16,EUCLID:56;
    end;
    then i+1 < len G & i < i+1 by A13,NAT_1:13,REAL_1:def 5;
    then (W-min C)`2 <= G*(2,i+1)`2 by A11,A14;
    then W-min C in LSeg(G*(2,i),G*(2,i+1)) by A10,A15,GOBOARD7:8;
    hence W-min C in cell(G,1,i) by A12;
    thus W-min C <> G*(2,i) by A10;
   end;

  theorem Th43:
   for f be S-Sequence_in_R2
   for p be Point of TOP-REAL 2 st p in L~f & f.len f in L~R_Cut(f,p) holds
    f.len f = p
   proof
    let f be S-Sequence_in_R2;
    let p be Point of TOP-REAL 2;
    assume that
A1: p in L~f and
A2: f.len f in L~R_Cut(f,p);
A3: L~f = L~(Rev f) by SPPOL_2:22;
A4: (Rev f).1 = f.len f by FINSEQ_5:65;
A5: Rev f is_S-Seq by SPPOL_2:47;
    L_Cut(Rev f,p) = Rev R_Cut(f,p) by A1,JORDAN3:57;
    then (Rev f).1 in L~L_Cut(Rev f,p) by A2,A4,SPPOL_2:22;
    hence f.len f = p by A1,A3,A4,A5,JORDAN1E:11;
   end;

  theorem Th44:
   for f be FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2 holds
    R_Cut (f,p) <> {}
   proof
    let f be FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2;
    per cases;
    suppose p<>f.1;
    then R_Cut (f,p) = mid(f,1,Index(p,f))^<*p*> by JORDAN3:def 5;
    hence thesis;
    end;
    suppose p=f.1; then R_Cut (f,p) = <*p*> by JORDAN3:def 5;
    hence thesis;
   end;
   end;

theorem Th45:
 for f be FinSequence of TOP-REAL 2
 for p be Point of TOP-REAL 2 st p in L~f holds
  R_Cut(f,p)/.(len R_Cut(f,p)) = p
 proof
  let f be FinSequence of TOP-REAL 2;
  let p be Point of TOP-REAL 2;
  assume A1: p in L~f;
  R_Cut(f,p) is non empty by Th44;
  then len R_Cut(f,p) in dom R_Cut(f,p) by FINSEQ_5:6;
  hence R_Cut(f,p)/.len R_Cut(f,p) = R_Cut(f,p).len R_Cut(f,p)
                                                          by FINSEQ_4:def 4
     .= p by A1,JORDAN3:59;
 end;

  theorem Th46:
   for C be compact connected non vertical non horizontal Subset of TOP-REAL 2
   for p be Point of TOP-REAL 2 holds
    p in L~Upper_Seq(C,n) & p`1 = E-bound L~Cage(C,n) implies
     p = E-max L~Cage(C,n)
   proof
    let C be compact connected non vertical non horizontal
     Subset of TOP-REAL 2;
    let p be Point of TOP-REAL 2;
    set Ca = Cage(C,n);
    set US = Upper_Seq(C,n);
    set LS = Lower_Seq(C,n);
    set Wmin = W-min L~Ca;
    set Smax = S-max L~Ca;
    set Smin = S-min L~Ca;
    set Emin = E-min L~Ca;
    set Emax = E-max L~Ca;
    set Wbo = W-bound L~Cage(C,n);
    set Nbo = N-bound L~Cage(C,n);
    set Ebo = E-bound L~Cage(C,n);
    set Sbo = S-bound L~Cage(C,n);
    set NE = NE-corner L~Ca;
    assume that
     A1: p in L~Upper_Seq(C,n) and
     A2: p`1 = E-bound L~Cage(C,n) and
     A3: p <> E-max L~Cage(C,n);
    A4: 1 in dom US & 1 in dom LS by FINSEQ_5:6;
    US/.1 = Wmin by JORDAN1F:5;
    then A5: US.1 = Wmin by A4,FINSEQ_4:def 4;
    LS/.1 = Emax by JORDAN1F:6;
    then A6: LS.1 = Emax by A4,FINSEQ_4:def 4;
    Wbo <> Ebo by SPRECT_1:33;
    then p <> US.1 by A2,A5,EUCLID:56;
    then reconsider g = R_Cut(US,p) as being_S-Seq FinSequence of TOP-REAL 2
                                                            by A1,JORDAN3:70;
    A7: US is_in_the_area_of Ca by JORDAN1E:21;
    then <*p*> is_in_the_area_of Ca by A1,SPRECT_3:63;
    then A8: g is_in_the_area_of Ca by A1,A7,SPRECT_3:69;
    A9: (g/.1)`1 = (US/.1)`1 by A1,SPRECT_3:39
       .= Wmin`1 by JORDAN1F:5
       .= Wbo by EUCLID:56;
        len g in dom g by FINSEQ_5:6;
    then g/.len g = g.len g by FINSEQ_4:def 4
       .= p by A1,JORDAN3:59;
    then A10: g is_a_h.c._for Ca by A2,A8,A9,SPRECT_2:def 2;
        len Cage(C,n) > 4 by GOBOARD7:36;
    then len Cage(C,n) >= 2 by XXREAL_0:2;
    then A11: rng Cage(C,n) c= L~Cage(C,n) by SPPOL_2:18;
        now per cases;
     suppose A12: Emax <> NE;
      set h = Rev R_Cut(LS,Smax)^<*NE*>;
      A13: not NE in rng Cage(C,n)
      proof
       assume A14: NE in rng Cage(C,n);
       A15: NE`1 = E-bound L~Cage(C,n) & NE`2 = N-bound L~Cage(C,n)
                                            by EUCLID:56;
       then NE`2 >= S-bound L~Cage(C,n) by SPRECT_1:24;
       then NE in { p1 where p1 is Point of TOP-REAL 2 :
        p1`1 = E-bound L~Cage(C,n) & p1`2 <= N-bound L~Cage(C,n) &
        p1`2 >= S-bound L~Cage(C,n) } by A15;
       then NE in LSeg(SE-corner L~Cage(C,n), NE-corner L~Cage(C,n))
                                                              by SPRECT_1:25;
       then NE in LSeg(SE-corner L~Cage(C,n), NE-corner L~Cage(C,n)) /\
        L~Cage(C,n) by A11,A14,XBOOLE_0:def 3;
       then NE in E-most L~Cage(C,n);
       then A16: NE`2 <= (E-max L~Cage(C,n))`2 by PSCOMP_1:108;
           (E-max L~Cage(C,n))`2 <= NE`2 by PSCOMP_1:107;
       then A17: (E-max L~Cage(C,n))`2 = NE`2 by A16,XXREAL_0:1;
           (E-max L~Cage(C,n))`1 = NE`1 by PSCOMP_1:105;
       hence contradiction by A12,A17,TOPREAL3:11;
      end;
      Smax in rng LS by Th12;
      then R_Cut(LS,Smax) = mid(LS,1,Smax..LS) by JORDAN1G:57;
      then A18: rng R_Cut(LS,Smax) c= rng LS by JORDAN3:28;
      rng LS c= rng Ca by JORDAN1G:47;
      then rng R_Cut(LS,Smax) c= rng Ca by A18,XBOOLE_1:1;
      then not NE in rng R_Cut(LS,Smax) by A13;
      then rng R_Cut(LS,Smax) misses {NE} by ZFMISC_1:56;
      then rng R_Cut(LS,Smax) misses rng <*NE*> by FINSEQ_1:55;
      then A19: rng Rev R_Cut(LS,Smax) misses rng <*NE*> by FINSEQ_5:60;
      A20: Smax in L~LS by Th12;
      A21: Emin`2 < Emax`2 by SPRECT_2:57;
          Emin in L~Ca by SPRECT_1:16;
      then Sbo <= Emin`2 by PSCOMP_1:71;
      then A22: Smax <> LS.1 by A6,A21,EUCLID:56;
      then reconsider RCutLS = R_Cut(LS,Smax) as
                     being_S-Seq FinSequence of TOP-REAL 2 by A20,JORDAN3:70;
      A23: <*NE*> is one-to-one by FINSEQ_3:102;
      A24: Rev RCutLS is special by SPPOL_2:42;
      A25: <*NE*> is special by SPPOL_2:39;
      A26: Rev RCutLS/.len Rev RCutLS = Rev RCutLS/.len RCutLS
                                                           by FINSEQ_5:def 3
         .= RCutLS/.1 by FINSEQ_5:68
         .= LS/.1 by A20,SPRECT_3:39
         .= Emax by JORDAN1F:6;
      then (Rev RCutLS/.len Rev RCutLS)`1 = E-bound L~Ca by EUCLID:56
         .= NE`1 by EUCLID:56
         .= (<*NE*>/.1)`1 by FINSEQ_4:25;
      then A27: h is one-to-one special by A19,A23,A24,A25,FINSEQ_3:98
,GOBOARD2:13;
      A28: 2 <= len g by TOPREAL1:def 10;
      A29: len h = len Rev R_Cut(LS,Smax) + 1 by FINSEQ_2:19
         .= len R_Cut(LS,Smax) + 1 by FINSEQ_5:def 3
         .= Index(Smax,LS)+1+1 by A20,A22,JORDAN3:60;
      Index(Smax,LS)+1 >= 0+1 by XREAL_1:9;
      then A30: len h >= 1+1 by A29,XREAL_1:9;
      A31: LS is_in_the_area_of Ca by JORDAN1E:22;
      then <*Smax*> is_in_the_area_of Ca by A20,SPRECT_3:63;
      then R_Cut(LS,Smax) is_in_the_area_of Ca by A20,A31,SPRECT_3:69;
      then A32: Rev R_Cut(LS,Smax) is_in_the_area_of Ca by SPRECT_3:68;
          <*NE*> is_in_the_area_of Ca by SPRECT_2:29;
      then A33: h is_in_the_area_of Ca by A32,SPRECT_2:28;
          1 in dom Rev RCutLS by FINSEQ_5:6;
      then h/.1 = Rev RCutLS/.1 by FINSEQ_4:83
          .= R_Cut(LS,Smax)/.len R_Cut(LS,Smax) by FINSEQ_5:68
          .= Smax by A20,Th45;
      then A34: (h/.1)`2 = Sbo by EUCLID:56;
          (h/.len h)`2 = (h/.(len Rev R_Cut(LS,Smax)+1))`2 by FINSEQ_2:19
         .= NE`2 by FINSEQ_4:82
         .= Nbo by EUCLID:56;
      then h is_a_v.c._for Ca by A33,A34,SPRECT_2:def 3;
      then L~g meets L~h by A10,A27,A28,A30,SPRECT_2:33;
      then consider x be set such that
       A35: x in L~g and
       A36: x in L~h by XBOOLE_0:3;
      reconsider x as Point of TOP-REAL 2 by A35;
      A37: L~g c= L~US by A1,JORDAN3:76;
      then A38: x in L~US by A35;
      A39: L~h = L~Rev RCutLS \/ LSeg(Rev RCutLS/.len Rev RCutLS,NE)
                                                               by SPPOL_2:19;
      A40: L~RCutLS c= L~LS by A20,JORDAN3:76;
      A41: len US in dom US & len LS in dom LS by FINSEQ_5:6;
          now per cases by A36,A39,XBOOLE_0:def 2;
       suppose x in L~Rev RCutLS;
        then A42: x in L~RCutLS by SPPOL_2:22;
        then x in L~US /\ L~LS by A35,A37,A40,XBOOLE_0:def 3;
        then A43: x in {Wmin,Emax} by JORDAN1E:20;
            now per cases by A43,TARSKI:def 2;
         suppose x = Wmin;
          then LS/.len LS in L~R_Cut(LS,Smax) by A42,JORDAN1F:8;
          then LS.len LS in L~R_Cut(LS,Smax) by A41,FINSEQ_4:def 4;
          then LS.len LS = Smax by A20,Th43;
          then LS/.len LS = Smax by A41,FINSEQ_4:def 4;
          then A44: Wmin = Smax by JORDAN1F:8;
          A45: Smin`1 < Smax`1 by SPRECT_2:59;
              Smin in L~Ca by SPRECT_1:14;
          then Wbo <= Smin`1 by PSCOMP_1:71;
          hence contradiction by A44,A45,EUCLID:56;
         end;
         suppose x = Emax;
          then US/.len US in L~R_Cut(US,p) by A35,JORDAN1F:7;
          then US.len US in L~R_Cut(US,p) by A41,FINSEQ_4:def 4;
          then US.len US = p by A1,Th43;
          then US/.len US = p by A41,FINSEQ_4:def 4;
          hence contradiction by A3,JORDAN1F:7;
        end;
        end;
        hence contradiction;
       end;
       suppose A46: x in LSeg(Rev RCutLS/.len Rev RCutLS,NE);
        A47: Emax`1 = Ebo by EUCLID:56;
            NE`1 = Ebo by EUCLID:56;
        then A48: x`1 = Ebo by A26,A46,A47,GOBOARD7:5;
            Emax`2 <= NE`2 by PSCOMP_1:107;
        then A49: Emax`2 <= x`2 by A26,A46,TOPREAL1:10;
            L~Ca = L~US \/ L~LS by JORDAN1E:17;
        then L~US c= L~Ca by XBOOLE_1:7;
        then x in E-most L~Ca by A38,A48,SPRECT_2:17;
        then x`2 <= Emax`2 by PSCOMP_1:108;
        then x`2 = Emax`2 by A49,XXREAL_0:1;
        then x = Emax by A47,A48,TOPREAL3:11;
        then US/.len US in L~R_Cut(US,p) by A35,JORDAN1F:7;
        then US.len US in L~R_Cut(US,p) by A41,FINSEQ_4:def 4;
        then US.len US = p by A1,Th43;
        then US/.len US = p by A41,FINSEQ_4:def 4;
        hence contradiction by A3,JORDAN1F:7;
      end;
      end;
      hence contradiction;
     end;
     suppose A50: Emax = NE;
      set h = Rev R_Cut(LS,Smax);
      A51: Smax in L~LS by Th12;
      A52: Emin`2 < Emax`2 by SPRECT_2:57;
          Emin in L~Ca by SPRECT_1:16;
      then Sbo <= Emin`2 by PSCOMP_1:71;
      then Smax <> LS.1 by A6,A52,EUCLID:56;
      then reconsider RCutLS = R_Cut(LS,Smax) as
                     being_S-Seq FinSequence of TOP-REAL 2 by A51,JORDAN3:70;
      A53: Rev RCutLS is special by SPPOL_2:42;
      A54: Rev RCutLS/.len Rev RCutLS = Rev RCutLS/.len RCutLS
                                                           by FINSEQ_5:def 3
         .= RCutLS/.1 by FINSEQ_5:68
         .= LS/.1 by A51,SPRECT_3:39
         .= Emax by JORDAN1F:6;
      A55: 2 <= len g by TOPREAL1:def 10;
          len RCutLS >= 2 by TOPREAL1:def 10;
      then A56: len h >= 2 by FINSEQ_5:def 3;
      A57: LS is_in_the_area_of Ca by JORDAN1E:22;
      then <*Smax*> is_in_the_area_of Ca by A51,SPRECT_3:63;
      then R_Cut(LS,Smax) is_in_the_area_of Ca by A51,A57,SPRECT_3:69;
      then A58: h is_in_the_area_of Ca by SPRECT_3:68;
          1 in dom Rev RCutLS by FINSEQ_5:6;
      then h/.1 = R_Cut(LS,Smax)/.len R_Cut(LS,Smax) by FINSEQ_5:68
          .= Smax by A51,Th45;
      then A59: (h/.1)`2 = Sbo by EUCLID:56;
          (h/.len h)`2 = Nbo by A50,A54,EUCLID:56;
      then h is_a_v.c._for Ca by A58,A59,SPRECT_2:def 3;
      then L~g meets L~h by A10,A53,A55,A56,SPRECT_2:33;
      then consider x be set such that
       A60: x in L~g and
       A61: x in L~h by XBOOLE_0:3;
      reconsider x as Point of TOP-REAL 2 by A60;
      A62: L~g c= L~US by A1,JORDAN3:76;
      A63: L~RCutLS c= L~LS by A51,JORDAN3:76;
      A64: len US in dom US & len LS in dom LS by FINSEQ_5:6;
      A65: x in L~RCutLS by A61,SPPOL_2:22;
      then x in L~US /\ L~LS by A60,A62,A63,XBOOLE_0:def 3;
      then A66: x in {Wmin,Emax} by JORDAN1E:20;
          now per cases by A66,TARSKI:def 2;
       suppose x = Wmin;
        then LS/.len LS in L~R_Cut(LS,Smax) by A65,JORDAN1F:8;
        then LS.len LS in L~R_Cut(LS,Smax) by A64,FINSEQ_4:def 4;
        then LS.len LS = Smax by A51,Th43;
        then LS/.len LS = Smax by A64,FINSEQ_4:def 4;
        then A67: Wmin = Smax by JORDAN1F:8;
        A68: Smin`1 < Smax`1 by SPRECT_2:59;
            Smin in L~Ca by SPRECT_1:14;
        then Wbo <= Smin`1 by PSCOMP_1:71;
        hence contradiction by A67,A68,EUCLID:56;
       end;
       suppose x = Emax;
        then US/.len US in L~R_Cut(US,p) by A60,JORDAN1F:7;
        then US.len US in L~R_Cut(US,p) by A64,FINSEQ_4:def 4;
        then US.len US = p by A1,Th43;
        then US/.len US = p by A64,FINSEQ_4:def 4;
        hence contradiction by A3,JORDAN1F:7;
      end;
      end;
      hence contradiction;
    end;
    end;
    hence contradiction;
   end;

  theorem
   for C be compact connected non vertical non horizontal Subset of TOP-REAL 2
   for p be Point of TOP-REAL 2 holds
    p in L~Lower_Seq(C,n) & p`1 = W-bound L~Cage(C,n) implies
     p = W-min L~Cage(C,n)
   proof
    let C be compact connected non vertical non horizontal
     Subset of TOP-REAL 2;
    let p be Point of TOP-REAL 2;
    set Ca = Cage(C,n);
    set LS = Lower_Seq(C,n);
    set US = Upper_Seq(C,n);
    set Emax = E-max L~Ca;
    set Nmin = N-min L~Ca;
    set Nmax = N-max L~Ca;
    set Wmax = W-max L~Ca;
    set Wmin = W-min L~Ca;
    set Ebo = E-bound L~Cage(C,n);
    set Sbo = S-bound L~Cage(C,n);
    set Wbo = W-bound L~Cage(C,n);
    set Nbo = N-bound L~Cage(C,n);
    set SW = SW-corner L~Ca;
    assume that
     A1: p in L~Lower_Seq(C,n) and
     A2: p`1 = W-bound L~Cage(C,n) and
     A3: p <> W-min L~Cage(C,n);
    A4: 1 in dom LS & 1 in dom US by FINSEQ_5:6;
        LS/.1 = Emax by JORDAN1F:6;
    then A5: LS.1 = Emax by A4,FINSEQ_4:def 4;
        US/.1 = Wmin by JORDAN1F:5;
    then A6: US.1 = Wmin by A4,FINSEQ_4:def 4;
        Ebo <> Wbo by SPRECT_1:33;
    then p <> LS.1 by A2,A5,EUCLID:56;
    then reconsider g1 = R_Cut(LS,p) as being_S-Seq FinSequence of TOP-REAL 2
                                                            by A1,JORDAN3:70;
    reconsider g = Rev g1 as being_S-Seq FinSequence of TOP-REAL 2
                                                               by SPPOL_2:47;
    A7: L~g = L~g1 by SPPOL_2:22;
    A8: LS is_in_the_area_of Ca by JORDAN1E:22;
    then <*p*> is_in_the_area_of Ca by A1,SPRECT_3:63;
    then g1 is_in_the_area_of Ca by A1,A8,SPRECT_3:69;
    then A9: g is_in_the_area_of Ca by SPRECT_3:68;
    A10: g/.1 = g1/.len g1 by FINSEQ_5:68;
    A11: g/.len g = g/.len g1 by FINSEQ_5:def 3
       .= g1/.1 by FINSEQ_5:68;
    A12: (g1/.1)`1 = (LS/.1)`1 by A1,SPRECT_3:39
       .= Emax`1 by JORDAN1F:6
       .= Ebo by EUCLID:56;
        len g1 in dom g1 by FINSEQ_5:6;
    then g1/.len g1 = g1.len g1 by FINSEQ_4:def 4
       .= p by A1,JORDAN3:59;
    then A13: g is_a_h.c._for Ca by A2,A9,A10,A11,A12,SPRECT_2:def 2;
        len Cage(C,n) > 4 by GOBOARD7:36;
    then len Cage(C,n) >= 2 by XXREAL_0:2;
    then A14: rng Cage(C,n) c= L~Cage(C,n) by SPPOL_2:18;
        now per cases;
     suppose A15: Wmin <> SW;
      set h1 = Rev R_Cut(US,Nmin)^<*SW*>;
      A16: not SW in rng Cage(C,n)
      proof
       assume A17: SW in rng Cage(C,n);
       A18: SW`1 = W-bound L~Cage(C,n) & SW`2 = S-bound L~Cage(C,n)
                                         by EUCLID:56;
       then SW`2 <= N-bound L~Cage(C,n) by SPRECT_1:24;
       then SW in { p1 where p1 is Point of TOP-REAL 2 :
        p1`1 = W-bound L~Cage(C,n) & p1`2 <= N-bound L~Cage(C,n) &
        p1`2 >= S-bound L~Cage(C,n) } by A18;
       then SW in LSeg(SW-corner L~Cage(C,n), NW-corner L~Cage(C,n))
                                                              by SPRECT_1:28;
       then SW in LSeg(SW-corner L~Cage(C,n), NW-corner L~Cage(C,n)) /\
        L~Cage(C,n) by A14,A17,XBOOLE_0:def 3;
       then SW in W-most L~Cage(C,n);
       then A19: SW`2 >= (W-min L~Cage(C,n))`2 by PSCOMP_1:88;
           (W-min L~Cage(C,n))`2 >= SW`2 by PSCOMP_1:87;
       then A20: (W-min L~Cage(C,n))`2 = SW`2 by A19,XXREAL_0:1;
           (W-min L~Cage(C,n))`1 = SW`1 by PSCOMP_1:85;
       hence contradiction by A15,A20,TOPREAL3:11;
      end;
          Nmin in rng US by Th7;
      then R_Cut(US,Nmin) = mid(US,1,Nmin..US) by JORDAN1G:57;
      then A21: rng R_Cut(US,Nmin) c= rng US by JORDAN3:28;
          rng US c= rng Ca by JORDAN1G:47;
      then rng R_Cut(US,Nmin) c= rng Ca by A21,XBOOLE_1:1;
      then not SW in rng R_Cut(US,Nmin) by A16;
      then rng R_Cut(US,Nmin) misses {SW} by ZFMISC_1:56;
      then rng R_Cut(US,Nmin) misses rng <*SW*> by FINSEQ_1:55;
      then A22: rng Rev R_Cut(US,Nmin) misses rng <*SW*> by FINSEQ_5:60;
      A23: Nmin in L~US by Th7;
      A24: Wmax`2 > Wmin`2 by SPRECT_2:61;
          Wmax in L~Ca by SPRECT_1:15;
      then Nbo >= Wmax`2 by PSCOMP_1:71;
      then A25: Nmin <> US.1 by A6,A24,EUCLID:56;
      then reconsider RCutUS = R_Cut(US,Nmin) as
                     being_S-Seq FinSequence of TOP-REAL 2 by A23,JORDAN3:70;
      A26: <*SW*> is one-to-one by FINSEQ_3:102;
      A27: Rev RCutUS is special by SPPOL_2:42;
      A28: <*SW*> is special by SPPOL_2:39;
      A29: Rev RCutUS/.len Rev RCutUS = Rev RCutUS/.len RCutUS
                                                           by FINSEQ_5:def 3
         .= RCutUS/.1 by FINSEQ_5:68
         .= US/.1 by A23,SPRECT_3:39
         .= Wmin by JORDAN1F:5;
      then (Rev RCutUS/.len Rev RCutUS)`1 = W-bound L~Ca by EUCLID:56
         .= SW`1 by EUCLID:56
         .= (<*SW*>/.1)`1 by FINSEQ_4:25;
      then reconsider h1 as one-to-one special FinSequence of TOP-REAL 2
                                                  by A22,A26,A27,A28,
FINSEQ_3:98,GOBOARD2:13;
      set h = Rev h1;
      A30: h is special by SPPOL_2:42;
      A31: 2 <= len g by TOPREAL1:def 10;
      A32: len h1 = len Rev R_Cut(US,Nmin) + 1 by FINSEQ_2:19
         .= len R_Cut(US,Nmin) + 1 by FINSEQ_5:def 3
         .= Index(Nmin,US)+1+1 by A23,A25,JORDAN3:60;
      Index(Nmin,US)+1 >= 0+1 by XREAL_1:9;
      then len h1 >= 1+1 by A32,XREAL_1:9;
      then A33: len h >= 2 by FINSEQ_5:def 3;
      A34: US is_in_the_area_of Ca by JORDAN1E:21;
      then <*Nmin*> is_in_the_area_of Ca by A23,SPRECT_3:63;
      then R_Cut(US,Nmin) is_in_the_area_of Ca by A23,A34,SPRECT_3:69;
      then A35: Rev R_Cut(US,Nmin) is_in_the_area_of Ca by SPRECT_3:68;
          <*SW*> is_in_the_area_of Ca by SPRECT_2:32;
      then h1 is_in_the_area_of Ca by A35,SPRECT_2:28;
      then A36: h is_in_the_area_of Ca by SPRECT_3:68;
          1 in dom Rev RCutUS by FINSEQ_5:6;
      then h1/.1 = Rev RCutUS/.1 by FINSEQ_4:83
          .= R_Cut(US,Nmin)/.len R_Cut(US,Nmin) by FINSEQ_5:68
          .= Nmin by A23,Th45;
      then A37: (h1/.1)`2 = Nbo by EUCLID:56;
      A38: h/.len h = h/.len h1 by FINSEQ_5:def 3
         .= h1/.1 by FINSEQ_5:68;
      A39: h/.1 = h1/.len h1 by FINSEQ_5:68;
          (h1/.len h1)`2 = (h1/.(len Rev R_Cut(US,Nmin)+1))`2 by FINSEQ_2:19
         .= SW`2 by FINSEQ_4:82
         .= Sbo by EUCLID:56;
      then h is_a_v.c._for Ca by A36,A37,A38,A39,SPRECT_2:def 3;
      then L~g meets L~h by A13,A30,A31,A33,SPRECT_2:33;
      then consider x be set such that
       A40: x in L~g and
       A41: x in L~h by XBOOLE_0:3;
      reconsider x as Point of TOP-REAL 2 by A40;
      A42: L~g c= L~LS by A1,A7,JORDAN3:76;
      then A43: x in L~LS by A40;
          L~h = L~h1 by SPPOL_2:22;
      then A44: L~h = L~Rev RCutUS \/ LSeg(Rev RCutUS/.len Rev RCutUS,SW)
                                                               by SPPOL_2:19;
      A45: L~RCutUS c= L~US by A23,JORDAN3:76;
      A46: len LS in dom LS & len US in dom US by FINSEQ_5:6;
          now per cases by A41,A44,XBOOLE_0:def 2;
       suppose x in L~Rev RCutUS;
        then A47: x in L~RCutUS by SPPOL_2:22;
        then x in L~LS /\ L~US by A40,A42,A45,XBOOLE_0:def 3;
        then A48: x in {Emax,Wmin} by JORDAN1E:20;
            now per cases by A48,TARSKI:def 2;
         suppose x = Emax;
          then US/.len US in L~R_Cut(US,Nmin) by A47,JORDAN1F:7;
          then US.len US in L~R_Cut(US,Nmin) by A46,FINSEQ_4:def 4;
          then US.len US = Nmin by A23,Th43;
          then US/.len US = Nmin by A46,FINSEQ_4:def 4;
          then A49: Emax = Nmin by JORDAN1F:7;
          A50: Nmax`1 > Nmin`1 by SPRECT_2:55;
              Nmax in L~Ca by SPRECT_1:13;
          then Ebo >= Nmax`1 by PSCOMP_1:71;
          hence contradiction by A49,A50,EUCLID:56;
         end;
         suppose x = Wmin;
          then LS/.len LS in L~R_Cut(LS,p) by A7,A40,JORDAN1F:8;
          then LS.len LS in L~R_Cut(LS,p) by A46,FINSEQ_4:def 4;
          then LS.len LS = p by A1,Th43;
          then LS/.len LS = p by A46,FINSEQ_4:def 4;
          hence contradiction by A3,JORDAN1F:8;
        end;
        end;
        hence contradiction;
       end;
       suppose A51: x in LSeg(Rev RCutUS/.len Rev RCutUS,SW);
        A52: Wmin`1 = Wbo by EUCLID:56;
            SW`1 = Wbo by EUCLID:56;
        then A53: x`1 = Wbo by A29,A51,A52,GOBOARD7:5;
            Wmin`2 >= SW`2 by PSCOMP_1:87;
        then A54: Wmin`2 >= x`2 by A29,A51,TOPREAL1:10;
            L~Ca = L~LS \/ L~US by JORDAN1E:17;
        then L~LS c= L~Ca by XBOOLE_1:7;
        then x in W-most L~Ca by A43,A53,SPRECT_2:16;
        then x`2 >= Wmin`2 by PSCOMP_1:88;
        then x`2 = Wmin`2 by A54,XXREAL_0:1;
        then x = Wmin by A52,A53,TOPREAL3:11;
        then LS/.len LS in L~R_Cut(LS,p) by A7,A40,JORDAN1F:8;
        then LS.len LS in L~R_Cut(LS,p) by A46,FINSEQ_4:def 4;
        then LS.len LS = p by A1,Th43;
        then LS/.len LS = p by A46,FINSEQ_4:def 4;
        hence contradiction by A3,JORDAN1F:8;
      end;
      end;
      hence contradiction;
     end;
     suppose A55: Wmin = SW;
      set h = R_Cut(US,Nmin);
      A56: Nmin in L~US by Th7;
      A57: Wmax`2 > Wmin`2 by SPRECT_2:61;
          Wmax in L~Ca by SPRECT_1:15;
      then Nbo >= Wmax`2 by PSCOMP_1:71;
      then Nmin <> US.1 by A6,A57,EUCLID:56;
      then reconsider RCutUS = R_Cut(US,Nmin) as
                     being_S-Seq FinSequence of TOP-REAL 2 by A56,JORDAN3:70;
      A58: RCutUS/.1 = US/.1 by A56,SPRECT_3:39
         .= Wmin by JORDAN1F:5;
      A59: 2 <= len g by TOPREAL1:def 10;
      A60: len RCutUS >= 2 by TOPREAL1:def 10;
      A61: US is_in_the_area_of Ca by JORDAN1E:21;
      then <*Nmin*> is_in_the_area_of Ca by A56,SPRECT_3:63;
      then A62: R_Cut(US,Nmin) is_in_the_area_of Ca by A56,A61,SPRECT_3:69;
          R_Cut(US,Nmin)/.len R_Cut(US,Nmin) = Nmin by A56,Th45;
      then A63: (h/.len h)`2 = Nbo by EUCLID:56;
          (h/.1)`2 = Sbo by A55,A58,EUCLID:56;
      then h is_a_v.c._for Ca by A62,A63,SPRECT_2:def 3;
      then L~g meets L~h by A13,A59,A60,SPRECT_2:33;
      then consider x be set such that
       A64: x in L~g and
       A65: x in L~h by XBOOLE_0:3;
      reconsider x as Point of TOP-REAL 2 by A64;
      A66: L~g c= L~LS by A1,A7,JORDAN3:76;
      A67: L~RCutUS c= L~US by A56,JORDAN3:76;
      A68: len LS in dom LS & len US in dom US by FINSEQ_5:6;
          x in L~LS /\ L~US by A64,A65,A66,A67,XBOOLE_0:def 3;
      then A69: x in {Emax,Wmin} by JORDAN1E:20;
          now per cases by A69,TARSKI:def 2;
       suppose x = Emax;
        then US/.len US in L~R_Cut(US,Nmin) by A65,JORDAN1F:7;
        then US.len US in L~R_Cut(US,Nmin) by A68,FINSEQ_4:def 4;
        then US.len US = Nmin by A56,Th43;
        then US/.len US = Nmin by A68,FINSEQ_4:def 4;
        then A70: Emax = Nmin by JORDAN1F:7;
        A71: Nmax`1 > Nmin`1 by SPRECT_2:55;
            Nmax in L~Ca by SPRECT_1:13;
        then Ebo >= Nmax`1 by PSCOMP_1:71;
        hence contradiction by A70,A71,EUCLID:56;
       end;
       suppose x = Wmin;
        then LS/.len LS in L~R_Cut(LS,p) by A7,A64,JORDAN1F:8;
        then LS.len LS in L~R_Cut(LS,p) by A68,FINSEQ_4:def 4;
        then LS.len LS = p by A1,Th43;
        then LS/.len LS = p by A68,FINSEQ_4:def 4;
        hence contradiction by A3,JORDAN1F:8;
      end;
      end;
      hence contradiction;
    end;
    end;
    hence contradiction;
   end;

  theorem
   for G be Go-board
   for f,g be FinSequence of TOP-REAL 2
   for k be Element of NAT holds
    1 <= k & k < len f & f^g is_sequence_on G implies
     left_cell(f^g,k,G) = left_cell(f,k,G) &
     right_cell(f^g,k,G) = right_cell(f,k,G)
   proof
    let G be Go-board;
    let f,g be FinSequence of TOP-REAL 2;
    let k be Element of NAT;
    assume that
     A1: 1 <= k and
     A2: k < len f and
     A3: f^g is_sequence_on G;
    A4: (f^g)|len f = f by FINSEQ_5:26;
    A5: k+1 <= len f by A2,NAT_1:13;
        len f <= len f + len g by NAT_1:11;
    then len f <= len (f^g) by FINSEQ_1:35;
    then k+1 <= len (f^g) by A5,XXREAL_0:2;
    hence thesis by A1,A3,A4,A5,GOBRD13:32;
   end;

  theorem Th49:
   for D be set
   for f,g be FinSequence of D
   for i be Element of NAT st i <= len f holds
    (f^'g)|i = f|i
   proof
    let D be set;
    let f,g be FinSequence of D;
    let i be Element of NAT;
    assume A1: i <= len f;
    then A2: len(f|i) = i by FINSEQ_1:80;
    per cases;
     suppose A3: g <> {};
      then len g <> 0 by FINSEQ_1:25;
      then len g > 0;
      then len g >= 0+1 by NAT_1:13;
      then i+1 <= len f+len g by A1,XREAL_1:9;
      then i+1 <= len(f^'g)+1 by A3,GRAPH_2:13;
      then i <= len(f^'g) by XREAL_1:8;
      then A4: len((f^'g)|i) = i by FINSEQ_1:80;
      now let j be Element of NAT;
       assume A5: j in Seg i;
       then j <= i by FINSEQ_1:3;
       then A6: 1 <= j & j <= len f by A1,A5,XXREAL_0:2,FINSEQ_1:3;
       thus ((f^'g)|i).j = ((f^'g)|(Seg i)).j by FINSEQ_1:def 15
          .= (f^'g).j by A5,FUNCT_1:72
          .= f.j by A6,GRAPH_2:14
          .= (f|(Seg i)).j by A5,FUNCT_1:72
          .= (f|i).j by FINSEQ_1:def 15;
      end;
      hence (f^'g)|i = f|i by A2,A4,FINSEQ_2:10;
     end;
     suppose g = {};
      hence thesis by GRAPH_2:59;
   end;
   end;

  theorem Th50:
   for D be set
   for f,g be FinSequence of D holds
    (f^'g)|(len f) = f
   proof
    let D be set;
    let f,g be FinSequence of D;
    f|len f = f|(Seg len f) by FINSEQ_1:def 15;
    hence f = f|len f by FINSEQ_2:23
       .= (f^'g)|(len f) by Th49;
   end;

  theorem Th51:
   for G be Go-board
   for f,g be FinSequence of TOP-REAL 2
   for k be Element of NAT holds
    1 <= k & k < len f & f^'g is_sequence_on G implies
     left_cell(f^'g,k,G) = left_cell(f,k,G) &
     right_cell(f^'g,k,G) = right_cell(f,k,G)
   proof
    let G be Go-board;
    let f,g be FinSequence of TOP-REAL 2;
    let k be Element of NAT;
    assume that
     A1: 1 <= k and
     A2: k < len f and
     A3: f^'g is_sequence_on G;
    A4: (f^'g)|len f = f by Th50;
    A5: k+1 <= len f by A2,NAT_1:13;
        len f <= len (f^'g) by TOPREAL8:7;
    then k+1 <= len (f^'g) by A5,XXREAL_0:2;
    hence thesis by A1,A3,A4,A5,GOBRD13:32;
   end;

  theorem Th52:
   for G be Go-board
   for f be S-Sequence_in_R2
   for p be Point of TOP-REAL 2
   for k be Element of NAT
    st 1 <= k & k < p..f & f is_sequence_on G & p in rng f holds
    left_cell(R_Cut(f,p),k,G) = left_cell(f,k,G) &
    right_cell(R_Cut(f,p),k,G) = right_cell(f,k,G)
   proof
    let G be Go-board;
    let f be S-Sequence_in_R2;
    let p be Point of TOP-REAL 2;
    let k be Element of NAT;
    assume that
     A1: 1 <= k and
     A2: k < p..f and
     A3: f is_sequence_on G and
     A4: p in rng f;
        p..f >= 1 by A1,A2,XXREAL_0:2;
    then A5: f|(p..f) = mid(f,1,p..f) by JORDAN3:25
       .= R_Cut(f,p) by A4,JORDAN1G:57;
    A6: k+1 <= p..f by A2,NAT_1:13;
        p..f <= len f by A4,FINSEQ_4:31;
    then k+1 <= len f by A6,XXREAL_0:2;
    hence thesis by A1,A3,A5,A6,GOBRD13:32;
   end;

  theorem Th53:
   for G be Go-board
   for f be FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2
   for k be Element of NAT st 1 <= k & k < p..f & f is_sequence_on G holds
    left_cell(f-:p,k,G) = left_cell(f,k,G) &
    right_cell(f-:p,k,G) = right_cell(f,k,G)
   proof
    let G be Go-board;
    let f be FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2;
    let k be Element of NAT;
    assume that
     A1: 1 <= k and
     A2: k < p..f and
     A3: f is_sequence_on G;
    A4: f|(p..f) = f-:p by FINSEQ_5:def 1;
    A5: k+1 <= p..f by A2,NAT_1:13;
    per cases by TOPREAL8:4;
     suppose p in rng f;
      then p..f <= len f by FINSEQ_4:31;
      then k+1 <= len f by A5,XXREAL_0:2;
      hence thesis by A1,A3,A4,A5,GOBRD13:32;
     end;
     suppose p..f = 0;
      hence thesis by A2;
   end;
   end;

  theorem Th54:
   for f,g be FinSequence of TOP-REAL 2 st
    f is unfolded s.n.c. one-to-one & g is unfolded s.n.c. one-to-one &
     f/.len f = g/.1 & L~f /\ L~g = {g/.1} holds
      f^'g is s.n.c.
   proof
    let f,g be FinSequence of TOP-REAL 2;
    assume that
     A1: f is unfolded s.n.c. one-to-one and
     A2: g is unfolded s.n.c. one-to-one and
     A3: f/.len f = g/.1 and
     A4: L~f /\ L~g = {g/.1};
        now let i,j be Element of NAT;
     assume A5: i+1 < j;
         now per cases;
      suppose A6: j < len f;
       then A7: LSeg(f^'g,j) = LSeg(f,j) by TOPREAL8:28;
       i+1 < len f by A5,A6,XXREAL_0:2;
       then i < len f by NAT_1:13;
       then LSeg(f^'g,i) = LSeg(f,i) by TOPREAL8:28;
       hence LSeg(f^'g,i) misses LSeg(f^'g,j) by A1,A5,A7,TOPREAL1:def 9;
      end;
      suppose j >= len f;
       then consider k be Nat such that
        A8: j = len f + k by NAT_1:10;
        reconsider k as Element of NAT by ORDINAL1:def 13;
       A9: now assume f is empty;
        then len f = 0 by FINSEQ_1:25;
        then L~f = {} by TOPREAL1:28;
        hence contradiction by A4;
       end;
       A10: now assume g is trivial;
        then len g < 2 by REALSET1:13;
        then len g = 0 or len g = 1 by NAT_1:23;
        then L~g = {} by TOPREAL1:28;
        hence contradiction by A4;
       end;
       now per cases;
        suppose A11: i >= 1 & j+1 <= len(f^'g);
         then j+1 < len(f^'g)+1 by NAT_1:13;
         then len f+(k+1) < len f + len g by A8,A10,GRAPH_2:13;
         then A12: k+1 < len g by XREAL_1:9;
         then A13: LSeg(f^'g,len f+k) = LSeg(g,k+1) by A3,A9,A10,TOPREAL8:31;
         then A14: LSeg(f^'g,j) c= L~g by A8,TOPREAL3:26;
             now per cases;
          suppose A15: i < len f;
           then A16: LSeg(f^'g,i) = LSeg(f,i) by TOPREAL8:28;
           then LSeg(f^'g,i) c= L~f by TOPREAL3:26;
           then A17: LSeg(f^'g,i) /\ LSeg(f^'g,j) c= {g/.1} by A4,A14,
XBOOLE_1:27;
           assume LSeg(f^'g,i) meets LSeg(f^'g,j);
           then consider x be set such that
            A18: x in LSeg(f^'g,i) and
            A19: x in LSeg(f^'g,j) by XBOOLE_0:3;
           x in LSeg(f^'g,i) /\ LSeg(f^'g,j) by A18,A19,XBOOLE_0:def 3;
           then A20: x = g/.1 by A17,TARSKI:def 1;
           A21: i in dom f by A11,A15,FINSEQ_3:27;
           i+1 > 1 & i+1 <= len f by A11,A15,NAT_1:13;
           then i+1 in dom f by FINSEQ_3:27;
           then len f+0 < len f+k by A1,A3,A5,A8,A16,A18,A20,A21,GOBOARD2:7;
           then k > 0 by XREAL_1:9;
           then A22: k+1 > 0+1 by XREAL_1:8;
           len g >= 2 by A10,REALSET1:13;
           hence contradiction by A2,A8,A13,A19,A20,A22,JORDAN5B:33;
          end;
          suppose i >= len f;
           then consider l be Nat such that
            A23: i = len f + l by NAT_1:10;
            reconsider l as Element of NAT by ORDINAL1:def 13;
               len f+(l+1) < len f+k by A5,A8,A23;
           then l+1 < k by XREAL_1:9;
           then A24: l+1+1 < k+1 by XREAL_1:8;
           then l+1+1 < len g by A12,XXREAL_0:2;
           then l+1 < len g by NAT_1:13;
           then LSeg(f^'g,len f+l) = LSeg(g,l+1) by A3,A9,A10,TOPREAL8:31;
           hence LSeg(f^'g,i) misses LSeg(f^'g,j)
                                        by A2,A8,A13,A23,A24,TOPREAL1:def 9;
         end;
         end;
         hence LSeg(f^'g,i) misses LSeg(f^'g,j);
        end;
        suppose j+1 > len(f^'g);
         then LSeg(f^'g,j) = {} by TOPREAL1:def 5;
         hence LSeg(f^'g,i) misses LSeg(f^'g,j) by XBOOLE_1:65;
        end;
        suppose i < 1;
         then LSeg(f^'g,i) = {} by TOPREAL1:def 5;
         hence LSeg(f^'g,i) misses LSeg(f^'g,j) by XBOOLE_1:65;
       end;
       end;
       hence LSeg(f^'g,i) misses LSeg(f^'g,j);
     end;
     end;
     hence LSeg(f^'g,i) misses LSeg(f^'g,j);
    end;
    hence f^'g is s.n.c. by TOPREAL1:def 9;
   end;

  theorem Th55:
   for f,g be FinSequence of TOP-REAL 2 st
    f is one-to-one & g is one-to-one & rng f /\ rng g c= {g/.1} holds
     f^'g is one-to-one
   proof
    let f,g be FinSequence of TOP-REAL 2;
    assume that
     A1: f is one-to-one and
     A2: g is one-to-one and
     A3: rng f /\ rng g c= {g/.1};
    per cases;
     suppose A4: rng g <> {};
          now let i,j be Element of NAT;
       assume that
        A5: i in dom (f^'g) and
        A6: j in dom (f^'g) and
        A7: (f^'g)/.i = (f^'g)/.j;
       A8: 1 <= i & i <= len (f^'g) by A5,FINSEQ_3:27;
       A9: 1 <= j & j <= len (f^'g) by A6,FINSEQ_3:27;
           g <> {} by A4,FINSEQ_1:27;
       then len (f^'g)+1 = len f + len g by GRAPH_2:13;
       then A10: i < len f + len g & j < len f + len g by A8,A9,NAT_1:13;
       A11: len f = len f + 0;
       A12: 1 in dom g by A4,FINSEQ_3:34;
           now per cases;
        suppose A13: i <= len f & j <= len f;
         then A14: (f^'g)/.i = f/.i & (f^'g)/.j = f/.j by A8,A9,GRAPH_2:61;
             i in dom f & j in dom f by A8,A9,A13,FINSEQ_3:27;
         hence i = j by A1,A7,A14,PARTFUN2:17;
        end;
        suppose A15: i > len f & j > len f;
         then consider k be Nat such that
          A16: i = len f + k by NAT_1:10;
         consider l be Nat such that
          A17: j = len f + l by A15,NAT_1:10;
          reconsider k,l as Element of NAT by ORDINAL1:def 13;
         A18: k < len g & l < len g by A10,A16,A17,XREAL_1:9;
             k > 0 by A11,A15,A16,XREAL_1:9;
         then A19: k >= 0+1 by NAT_1:13;
         then A20: (f^'g)/.i = g/.(k+1) by A16,A18,GRAPH_2:62;
             l > 0 by A11,A15,A17,XREAL_1:9;
         then A21: l >= 0+1 by NAT_1:13;
         then A22: (f^'g)/.j = g/.(l+1) by A17,A18,GRAPH_2:62;
         A23: k+1 <= len g & l+1 <= len g by A18,NAT_1:13;
             k+1 > 1 & l+1 > 1 by A19,A21,NAT_1:13;
         then k+1 in dom g & l+1 in dom g by A23,FINSEQ_3:27;
         then k+1 = l+1 by A2,A7,A20,A22,PARTFUN2:17;
         hence i = j by A16,A17;
        end;
        suppose A24: i <= len f & j > len f;
         then A25: (f^'g)/.i = f/.i by A8,GRAPH_2:61;
             i in dom f by A8,A24,FINSEQ_3:27;
         then A26: (f^'g)/.i in rng f by A25,PARTFUN2:4;
         consider l be Nat such that
          A27: j = len f + l by A24,NAT_1:10;
          reconsider l as Element of NAT by ORDINAL1:def 13;
         A28: l < len g by A10,A27,XREAL_1:9;
             l > 0 by A11,A24,A27,XREAL_1:9;
         then A29: l >= 0+1 by NAT_1:13;
         then A30: (f^'g)/.j = g/.(l+1) by A27,A28,GRAPH_2:62;
         A31: l+1 <= len g by A28,NAT_1:13;
         A32: l+1 > 1 by A29,NAT_1:13;
         then A33: l+1 in dom g by A31,FINSEQ_3:27;
         then (f^'g)/.j in rng g by A30,PARTFUN2:4;
         then (f^'g)/.j in rng f /\ rng g by A7,A26,XBOOLE_0:def 3;
         then g/.(l+1) = g/.1 by A3,A30,TARSKI:def 1;
         hence i = j by A2,A12,A32,A33,PARTFUN2:17;
        end;
        suppose A34: j <= len f & i > len f;
         then A35: (f^'g)/.j = f/.j by A9,GRAPH_2:61;
             j in dom f by A9,A34,FINSEQ_3:27;
         then A36: (f^'g)/.j in rng f by A35,PARTFUN2:4;
         consider l be Nat such that
          A37: i = len f + l by A34,NAT_1:10;
          reconsider l as Element of NAT by ORDINAL1:def 13;
         A38: l < len g by A10,A37,XREAL_1:9;
             l > 0 by A11,A34,A37,XREAL_1:9;
         then A39: l >= 0+1 by NAT_1:13;
         then A40: (f^'g)/.i = g/.(l+1) by A37,A38,GRAPH_2:62;
         A41: l+1 <= len g by A38,NAT_1:13;
         A42: l+1 > 1 by A39,NAT_1:13;
         then A43: l+1 in dom g by A41,FINSEQ_3:27;
         then (f^'g)/.i in rng g by A40,PARTFUN2:4;
         then (f^'g)/.i in rng f /\ rng g by A7,A36,XBOOLE_0:def 3;
         then g/.(l+1) = g/.1 by A3,A40,TARSKI:def 1;
         hence i = j by A2,A12,A42,A43,PARTFUN2:17;
       end;
       end;
       hence i = j;
      end;
      hence f^'g is one-to-one by PARTFUN2:16;
     end;
     suppose rng g = {};
      then g = {} by FINSEQ_1:27;
      hence f^'g is one-to-one by A1,GRAPH_2:59;
   end;
   end;

  theorem Th56:
   for f be FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2 st f is_S-Seq & p in rng f & p <> f.1 holds
    Index(p,f)+1 = p..f
   proof
    let f be FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2;
    assume that
A1: f is_S-Seq and
A2: p in rng f and
A3: p <> f.1;
A4: 1 <= p..f & p..f <= len f by A2,FINSEQ_4:31;
    p..f <> 1 by A2,A3,FINSEQ_4:29;
    then A5: 1 < p..f by A4,REAL_1:def 5;
    f.(p..f) = p by A2,FINSEQ_4:29;
    hence Index(p,f)+1 = p..f by A1,A4,A5,JORDAN3:45;
   end;

  theorem Th57:
   for C be compact connected non vertical non horizontal Subset of TOP-REAL 2
   for i,j,k be Element of NAT st
    1 < i & i < len Gauge(C,n) &
    1 <= j & k <= width Gauge(C,n) &
    Gauge(C,n)*(i,k) in L~Upper_Seq(C,n) &
    Gauge(C,n)*(i,j) in L~Lower_Seq(C,n) holds j <> k
   proof
    let C be compact connected non vertical non horizontal
     Subset of TOP-REAL 2;
    let i,j,k be Element of NAT;
    assume that
A1: 1 < i & i < len Gauge(C,n) and
A2: 1 <= j & k <= width Gauge(C,n) and
A3: Gauge(C,n)*(i,k) in L~Upper_Seq(C,n) and
A4: Gauge(C,n)*(i,j) in L~Lower_Seq(C,n) and
A5: j = k;
A6: len Gauge(C,n) = width Gauge(C,n) by JORDAN8:def 1;
    Gauge(C,n)*(i,k) in L~Upper_Seq(C,n) /\ L~Lower_Seq(C,n)
                                                 by A3,A4,A5,XBOOLE_0:def 3;
    then A7: Gauge(C,n)*(i,k) in {W-min L~Cage(C,n),E-max L~Cage(C,n)}
                                                              by JORDAN1E:20;
    A8: [i,j] in Indices Gauge(C,n) by A1,A2,A5,GOBOARD7:10;
        len Gauge(C,n) >= 4 by JORDAN8:13;
    then A9: len Gauge(C,n) >= 1 by XXREAL_0:2;
    then A10: [len Gauge(C,n),j] in Indices Gauge(C,n) by A2,A5,GOBOARD7:10;
    A11: [1,j] in Indices Gauge(C,n) by A2,A5,A9,GOBOARD7:10;
    per cases by A7,TARSKI:def 2;
     suppose A12: Gauge(C,n)*(i,k) = W-min L~Cage(C,n);
          Gauge(C,n)*(1,j)`1 = W-bound L~Cage(C,n) by A2,A5,A6,JORDAN1A:94;
      then (W-min L~Cage(C,n))`1 <> W-bound L~Cage(C,n)
                                               by A1,A5,A8,A11,A12,JORDAN1G:7;
      hence contradiction by EUCLID:56;
     end;
     suppose A13: Gauge(C,n)*(i,k) = E-max L~Cage(C,n);
      Gauge(C,n)*(len Gauge(C,n),j)`1 = E-bound L~Cage(C,n)
                                                     by A2,A5,A6,JORDAN1A:92;
      then (E-max L~Cage(C,n))`1 <> E-bound L~Cage(C,n)
                                                by A1,A5,A8,A10,A13,JORDAN1G:7;
      hence contradiction by EUCLID:56;
   end;
   end;

  theorem Th58:
   for C be Simple_closed_curve
   for i,j,k be Element of NAT st
    1 < i & i < len Gauge(C,n) &
    1 <= j & j <= k & k <= width Gauge(C,n) &
    LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ L~Upper_Seq(C,n) =
     {Gauge(C,n)*(i,k)} &
    LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ L~Lower_Seq(C,n) =
     {Gauge(C,n)*(i,j)} holds
      LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Lower_Arc C
   proof
    let C be Simple_closed_curve;
    let i,j,k be Element of NAT;
    set Ga = Gauge(C,n);
    set US = Upper_Seq(C,n);
    set LS = Lower_Seq(C,n);
    set LA = Lower_Arc C;
    set Wmin = W-min L~Cage(C,n);
    set Emax = E-max L~Cage(C,n);
    set Wbo = W-bound L~Cage(C,n);
    set Ebo = E-bound L~Cage(C,n);
    set Gij = Ga*(i,j);
    set Gik = Ga*(i,k);
    assume that
A1: 1 < i & i < len Ga and
A2: 1 <= j & j <= k & k <= width Ga and
A3: LSeg(Gij,Gik) /\ L~US = {Gik} and
A4: LSeg(Gij,Gik) /\ L~LS = {Gij} and
A5: LSeg(Gij,Gik) misses LA;
    Gij in {Gij} by TARSKI:def 1;
    then A6: Gij in L~LS by A4,XBOOLE_0:def 3;
    Gik in {Gik} by TARSKI:def 1;
    then A7: Gik in L~US by A3,XBOOLE_0:def 3;
    then A8: j <> k by A1,A2,A6,Th57;
    A9: 1 <= j & j <= width Ga by A2,XXREAL_0:2;
    A10: 1 <= k & k <= width Ga by A2,XXREAL_0:2;
    A11: [i,j] in Indices Ga by A1,A9,GOBOARD7:10;
    A12: [i,k] in Indices Ga by A1,A10,GOBOARD7:10;
    A13: US is_sequence_on Ga by JORDAN1G:4;
    A14: LS is_sequence_on Ga by JORDAN1G:5;
    set go = R_Cut(US,Gik);
    set do = L_Cut(LS,Gij);
    A15: len Ga = width Ga by JORDAN8:def 1;
    A16: len US >= 3 by JORDAN1E:19;
    then len US >= 1 by XXREAL_0:2;
    then 1 in dom US by FINSEQ_3:27;
    then A17: US.1 = US/.1 by FINSEQ_4:def 4
       .= Wmin by JORDAN1F:5;
    A18: Wmin`1 = Wbo by EUCLID:56
       .= Ga*(1,k)`1 by A10,A15,JORDAN1A:94;
        len Ga >= 4 by JORDAN8:13;
    then A19: len Ga >= 1 by XXREAL_0:2;
    then A20: [1,k] in Indices Ga by A10,GOBOARD7:10;
    then A21: Gik <> US.1 by A1,A12,A17,A18,JORDAN1G:7;
    then reconsider go as being_S-Seq FinSequence of TOP-REAL 2
                                                            by A7,JORDAN3:70;
    A22: len LS >= 1+2 by JORDAN1E:19;
    then len LS >= 1 by XXREAL_0:2;
    then A23: 1 in dom LS & len LS in dom LS by FINSEQ_3:27;
    then A24: LS.len LS = LS/.len LS by FINSEQ_4:def 4
       .= Wmin by JORDAN1F:8;
    A25: Wmin`1 = Wbo by EUCLID:56
       .= Ga*(1,k)`1 by A10,A15,JORDAN1A:94;
    A26: [i,j] in Indices Ga by A1,A9,GOBOARD7:10;
    then A27: Gij <> LS.len LS by A1,A20,A24,A25,JORDAN1G:7;
    then reconsider do as being_S-Seq FinSequence of TOP-REAL 2
                                                            by A6,JORDAN3:69;
    A28: [len Ga,k] in Indices Ga by A10,A19,GOBOARD7:10;
    A29: LS.1 = LS/.1 by A23,FINSEQ_4:def 4
       .= Emax by JORDAN1F:6;
        Emax`1 = Ebo by EUCLID:56
       .= Ga*(len Ga,k)`1 by A10,A15,JORDAN1A:92;
    then A30: Gij <> LS.1 by A1,A26,A28,A29,JORDAN1G:7;
    A31: len go >= 1+1 by TOPREAL1:def 10;
    A32: Gik in rng US by A1,A7,A10,A13,Th40;
    then A33: go is_sequence_on Ga by A13,Th38;
    A34: len do >= 1+1 by TOPREAL1:def 10;
    A35: Gij in rng LS by A1,A6,A9,A14,Th40;
    then A36: do is_sequence_on Ga by A14,Th39;
    reconsider go as non constant s.c.c.
            (being_S-Seq FinSequence of TOP-REAL 2) by A31,A33,JGRAPH_1:16
,JORDAN8:8;
    reconsider do as non constant s.c.c.
            (being_S-Seq FinSequence of TOP-REAL 2) by A34,A36,JGRAPH_1:16
,JORDAN8:8;
    A37: len go > 1 by A31,NAT_1:13;
    then A38: len go in dom go by FINSEQ_3:27;
    then A39: go/.len go = go.len go by FINSEQ_4:def 4
       .= Gik by A7,JORDAN3:59;
        len do >= 1 by A34,XXREAL_0:2;
    then 1 in dom do by FINSEQ_3:27;
    then A40: do/.1 = do.1 by FINSEQ_4:def 4
       .= Gij by A6,JORDAN3:58;
    reconsider m = len go - 1 as Element of NAT by A38,FINSEQ_3:28;
    A41: m+1 = len go;
    then A42: len go-'1 = m by BINARITH:39;
    A43: LSeg(go,m) c= L~go by TOPREAL3:26;
    A44: L~go c= L~US by A7,JORDAN3:76;
    then LSeg(go,m) c= L~US by A43,XBOOLE_1:1;
    then A45: LSeg(go,m) /\ LSeg(Gik,Gij) c= {Gik} by A3,XBOOLE_1:26;
     m >= 1 by A31,XREAL_1:21;
    then A46: LSeg(go,m) = LSeg(go/.m,Gik) by A39,A41,TOPREAL1:def 5;
    {Gik} c= LSeg(go,m) /\ LSeg(Gik,Gij)
    proof
     let x be set;
     assume x in {Gik};
     then A47: x = Gik by TARSKI:def 1;
     A48: Gik in LSeg(go,m) by A46,TOPREAL1:6;
         Gik in LSeg(Gik,Gij) by TOPREAL1:6;
     hence x in LSeg(go,m) /\ LSeg(Gik,Gij) by A47,A48,XBOOLE_0:def 3;
    end;
    then A49: LSeg(go,m) /\ LSeg(Gik,Gij) = {Gik} by A45,XBOOLE_0:def 10;
    A50: LSeg(do,1) c= L~do by TOPREAL3:26;
    A51: L~do c= L~LS by A6,JORDAN3:77;
    then LSeg(do,1) c= L~LS by A50,XBOOLE_1:1;
    then A52: LSeg(do,1) /\ LSeg(Gik,Gij) c= {Gij} by A4,XBOOLE_1:26;
    A53: LSeg(do,1) = LSeg(Gij,do/.(1+1)) by A34,A40,TOPREAL1:def 5;
    {Gij} c= LSeg(do,1) /\ LSeg(Gik,Gij)
    proof
     let x be set;
     assume x in {Gij};
     then A54: x = Gij by TARSKI:def 1;
     A55: Gij in LSeg(do,1) by A53,TOPREAL1:6;
     Gij in LSeg(Gik,Gij) by TOPREAL1:6;
     hence x in LSeg(do,1) /\ LSeg(Gik,Gij) by A54,A55,XBOOLE_0:def 3;
    end;
    then A56: LSeg(Gik,Gij) /\ LSeg(do,1) = {Gij} by A52,XBOOLE_0:def 10;
    A57: go/.1 = US/.1 by A7,SPRECT_3:39
       .= Wmin by JORDAN1F:5;
    then A58: go/.1 = LS/.len LS by JORDAN1F:8
       .= do/.len do by A6,Th35;
    A59: rng go c= L~go & rng do c= L~do by A31,A34,SPPOL_2:18;
    A60: {go/.1} c= L~go /\ L~do
    proof
     let x be set;
     assume x in {go/.1};
     then x = go/.1 by TARSKI:def 1;
     then x in rng go & x in rng do by A58,FINSEQ_6:46,REVROT_1:3;
     hence x in L~go /\ L~do by A59,XBOOLE_0:def 3;
    end;
    A61: LS.1 = LS/.1 by A23,FINSEQ_4:def 4
       .= Emax by JORDAN1F:6;
    A62: [len Ga,j] in Indices Ga by A9,A19,GOBOARD7:10;
    L~go /\ L~do c= {go/.1}
    proof
     let x be set;
     assume x in L~go /\ L~do;
     then A63: x in L~go & x in L~do by XBOOLE_0:def 3;
     then x in L~US /\ L~LS by A44,A51,XBOOLE_0:def 3;
     then x in {Wmin,Emax} by JORDAN1E:20;
     then A64: x = Wmin or x = Emax by TARSKI:def 2;
         now assume x = Emax;
      then A65: Emax = Gij by A6,A61,A63,JORDAN1E:11;
          Ga*(len Ga,j)`1 = Ebo by A9,A15,JORDAN1A:92;
      then Emax`1 <> Ebo by A1,A11,A62,A65,JORDAN1G:7;
      hence contradiction by EUCLID:56;
     end;
     hence x in {go/.1} by A57,A64,TARSKI:def 1;
    end;
    then A66: L~go /\ L~do = {go/.1} by A60,XBOOLE_0:def 10;
    set W2 = go/.2;
    A67: 2 in dom go by A31,FINSEQ_3:27;
    A68: Gik..US >= 1 by A32,FINSEQ_4:31;
    A69: now assume Gik`1 = Wbo;
     then Ga*(1,k)`1 = Ga*(i,k)`1 by A10,A15,JORDAN1A:94;
     hence contradiction by A1,A12,A20,JORDAN1G:7;
    end;
    go = mid(US,1,Gik..US) by A32,JORDAN1G:57
       .= US|(Gik..US) by A68,JORDAN3:25;
    then A70: W2 = US/.2 by A67,FINSEQ_4:85;
    set pion = <*Gik,Gij*>;
    A71: now let n be Element of NAT;
     assume n in dom pion;
     then n in Seg 2 by FINSEQ_3:29;
     then n = 1 or n = 2 by FINSEQ_1:4,TARSKI:def 2;
     then pion/.n = Gik or pion/.n = Gij by FINSEQ_4:26;
     hence ex i,j be Element of NAT st [i,j] in Indices Ga & pion/.n = Ga*(i,j)
                                                                  by A11,A12;
    end;
    A72: Gik <> Gij by A8,A11,A12,GOBOARD1:21;
    A73: Gik`1 = Ga*(i,1)`1 by A1,A10,GOBOARD5:3
       .= Gij`1 by A1,A9,GOBOARD5:3;
    then LSeg(Gik,Gij) is vertical by SPPOL_1:37;
    then pion is_S-Seq by A72,JORDAN1B:8;
    then consider pion1 be FinSequence of TOP-REAL 2 such that
     A74: pion1 is_sequence_on Ga and
     A75: pion1 is_S-Seq and
     A76: L~pion = L~pion1 and
     A77: pion/.1 = pion1/.1 and
     A78: pion/.len pion = pion1/.len pion1 and
     A79: len pion <= len pion1 by A71,GOBOARD3:2;
    reconsider pion1 as being_S-Seq FinSequence of TOP-REAL 2 by A75;
    set godo = go^'pion1^'do;
        len Cage(C,n) > 4 by GOBOARD7:36;
    then A80: 1+1 <= len Cage(C,n) by XXREAL_0:2;
    then A81: 1+1 <= len Rotate(Cage(C,n),Wmin) by REVROT_1:14;
        len (go^'pion1) >= len go by TOPREAL8:7;
    then A82: len (go^'pion1) >= 1+1 by A31,XXREAL_0:2;
    then A83: len (go^'pion1) > 1+0 by NAT_1:13;
        len godo >= len (go^'pion1) by TOPREAL8:7;
    then A84: 1+1 <= len godo by A82,XXREAL_0:2;
    A85: US is_sequence_on Ga by JORDAN1G:4;
    A86: go/.len go = pion1/.1 by A39,A77,FINSEQ_4:26;
    then A87: go^'pion1 is_sequence_on Ga by A33,A74,TOPREAL8:12;
    A88: (go^'pion1)/.len (go^'pion1) = pion/.len pion by A78,GRAPH_2:58
       .= pion/.2 by FINSEQ_1:61
       .= do/.1 by A40,FINSEQ_4:26;
    then A89: godo is_sequence_on Ga by A36,A87,TOPREAL8:12;
        LSeg(pion1,1) c= L~<*Gik,Gij*> by A76,TOPREAL3:26;
    then LSeg(pion1,1) c= LSeg(Gik,Gij) by SPPOL_2:21;
    then A90: LSeg(go,len go-'1) /\ LSeg(pion1,1) c= {Gik} by A42,A49,
XBOOLE_1:27;
    A91: len pion1 >= 1+1 by A79,FINSEQ_1:61;
        {Gik} c= LSeg(go,m) /\ LSeg(pion1,1)
    proof
     let x be set;
     assume x in {Gik};
     then A92: x = Gik by TARSKI:def 1;
     A93: Gik in LSeg(go,m) by A46,TOPREAL1:6;
         Gik in LSeg(pion1,1) by A39,A86,A91,TOPREAL1:27;
     hence x in LSeg(go,m) /\ LSeg(pion1,1) by A92,A93,XBOOLE_0:def 3;
    end;
    then LSeg(go,len go-'1) /\ LSeg(pion1,1) = { go/.len go }
                                     by A39,A42,A90,XBOOLE_0:def 10;
    then A94: go^'pion1 is unfolded by A86,TOPREAL8:34;
        len pion1 >= 2+0 by A79,FINSEQ_1:61;
    then A95: len pion1-2 >= 0 by XREAL_1:21;
        len (go^'pion1)+1-1 = len go+len pion1-1 by GRAPH_2:13;
    then len (go^'pion1)-1 = len go + (len pion1-2)
       .= len go + (len pion1-'2) by A95,BINARITH:def 3;
    then A96: len (go^'pion1)-'1 = len go + (len pion1-'2)
                                                      by BINARITH:def 3;
    A97: len pion1-1 >= 1 by A91,XREAL_1:21;
    then A98: len pion1-1 >= 0;
    then A99: len pion1-'1 = len pion1-1 by BINARITH:def 3;
    A100: len pion1-'2+1 = len pion1-2+1 by A95,BINARITH:def 3
       .= len pion1-'1 by A98,BINARITH:def 3;
        len pion1-1+1 <= len pion1;
    then A101: len pion1-'1 < len pion1 by A99,NAT_1:13;
        LSeg(pion1,len pion1-'1) c= L~<*Gik,Gij*> by A76,TOPREAL3:26;
    then LSeg(pion1,len pion1-'1) c= LSeg(Gik,Gij) by SPPOL_2:21;
    then A102: LSeg(pion1,len pion1-'1) /\ LSeg(do,1) c= {Gij} by A56,
XBOOLE_1:27;
    {Gij} c= LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
    proof
     let x be set;
     assume x in {Gij};
     then A103: x = Gij by TARSKI:def 1;
     A104: Gij in LSeg(do,1) by A53,TOPREAL1:6;
      pion1/.(len pion1-'1+1) = pion/.2 by A78,A99,FINSEQ_1:61
        .= Gij by FINSEQ_4:26;
     then Gij in LSeg(pion1,len pion1-'1) by A97,A99,TOPREAL1:27;
     hence x in LSeg(pion1,len pion1-'1) /\ LSeg(do,1) by A103,A104,
XBOOLE_0:def 3;
    end;
    then LSeg(pion1,len pion1-'1) /\ LSeg(do,1) = {Gij} by A102,XBOOLE_0:def 10
;
    then A105: LSeg(go^'pion1,len go+(len pion1-'2)) /\ LSeg(do,1) =
       {(go^'pion1)/.len (go^'pion1)} by A40,A86,A88,A100,A101,TOPREAL8:31;
    A106: (go^'pion1) is non trivial by A82,REALSET1:13;
    A107: rng pion1 c= L~pion1 by A91,SPPOL_2:18;
    A108: {pion1/.1} c= L~go /\ L~pion1
    proof
     let x be set;
     assume x in {pion1/.1};
     then x = pion1/.1 by TARSKI:def 1;
     then x in rng go & x in rng pion1 by A86,FINSEQ_6:46,REVROT_1:3;
     hence x in L~go /\ L~pion1 by A59,A107,XBOOLE_0:def 3;
    end;
    L~go /\ L~pion1 c= {pion1/.1}
    proof
     let x be set;
     assume x in L~go /\ L~pion1;
     then x in L~go & x in L~pion1 by XBOOLE_0:def 3;
     then x in L~pion1 /\ L~US by A44,XBOOLE_0:def 3;
     hence x in {pion1/.1} by A3,A39,A76,A86,SPPOL_2:21;
    end;
    then A109: L~go /\ L~pion1 = {pion1/.1} by A108,XBOOLE_0:def 10;
    then A110: (go^'pion1) is s.n.c. by A86,Th54;
        rng go /\ rng pion1 c= {pion1/.1} by A59,A107,A109,XBOOLE_1:27;
    then A111: go^'pion1 is one-to-one by Th55;
    A112: pion/.len pion = pion/.2 by FINSEQ_1:61
       .= do/.1 by A40,FINSEQ_4:26;
    A113: {pion1/.len pion1} c= L~do /\ L~pion1
    proof
     let x be set;
     assume x in {pion1/.len pion1};
     then x = pion1/.len pion1 by TARSKI:def 1;
     then x in rng do & x in rng pion1 by A78,A112,FINSEQ_6:46,REVROT_1:3;
     hence x in L~do /\ L~pion1 by A59,A107,XBOOLE_0:def 3;
    end;
    L~do /\ L~pion1 c= {pion1/.len pion1}
    proof
     let x be set;
     assume x in L~do /\ L~pion1;
     then x in L~do & x in L~pion1 by XBOOLE_0:def 3;
     then x in L~pion1 /\ L~LS by A51,XBOOLE_0:def 3;
     hence x in {pion1/.len pion1} by A4,A40,A76,A78,A112,SPPOL_2:21;
    end;
    then A114: L~do /\ L~pion1 = {pion1/.len pion1} by A113,XBOOLE_0:def 10;
    A115: L~(go^'pion1) /\ L~do = (L~go \/ L~pion1) /\ L~do by A86,TOPREAL8:35
       .= {go/.1} \/ {do/.1} by A66,A78,A112,A114,XBOOLE_1:23
       .= {(go^'pion1)/.1} \/ {do/.1} by GRAPH_2:57
       .= {(go^'pion1)/.1,do/.1} by ENUMSET1:41;
        do/.len do = (go^'pion1)/.1 by A58,GRAPH_2:57;
    then reconsider godo as non constant standard special_circular_sequence
                                              by A84,A88,A89,A94,A96,A105,A106
,A110,A111,A115,JORDAN8:7,8,TOPREAL8:11,33,34;
    A116: LA is_an_arc_of E-max C,W-min C by JORDAN6:def 9;
    then A117: LA is connected by JORDAN6:11;
    A118: W-min C in LA & E-max C in LA by A116,TOPREAL1:4;
    set ff = Rotate(Cage(C,n),Wmin);
        Wmin in rng Cage(C,n) by SPRECT_2:47;
    then A119: ff/.1 = Wmin by FINSEQ_6:98;
    A120: L~ff = L~Cage(C,n) by REVROT_1:33;
    then A121: (W-max L~ff)..ff > 1 by A119,SPRECT_5:23;
        (W-max L~ff)..ff <= (N-min L~ff)..ff by A119,A120,SPRECT_5:24;
    then A122: (N-min L~ff)..ff > 1 by A121,XXREAL_0:2;
        (N-min L~ff)..ff < (N-max L~ff)..ff by A119,A120,SPRECT_5:25;
    then A123: (N-max L~ff)..ff > 1 by A122,XXREAL_0:2;
        (N-max L~ff)..ff <= (E-max L~ff)..ff by A119,A120,SPRECT_5:26;
    then A124: Emax..ff > 1 by A120,A123,XXREAL_0:2;
    A125: now assume A126: Gik..US <= 1;
         Gik..US >= 1 by A32,FINSEQ_4:31;
     then Gik..US = 1 by A126,XXREAL_0:1;
     then Gik = US/.1 by A32,FINSEQ_5:41;
     hence contradiction by A17,A21,JORDAN1F:5;
    end;
    A127: Cage(C,n) is_sequence_on Ga by JORDAN9:def 1;
    then A128: ff is_sequence_on Ga by REVROT_1:34;
    A129: right_cell(godo,1,Ga)\L~godo c= RightComp godo by A84,A89,JORDAN9:29;
    A130: L~godo = L~(go^'pion1) \/ L~do by A88,TOPREAL8:35
       .= L~go \/ L~pion1 \/ L~do by A86,TOPREAL8:35;
        L~Cage(C,n) = L~US \/ L~LS by JORDAN1E:17;
    then A131: L~US c= L~Cage(C,n) & L~LS c= L~Cage(C,n) by XBOOLE_1:7;
    then A132: L~go c= L~Cage(C,n) & L~do c= L~Cage(C,n) by A44,A51,XBOOLE_1:1;
    A133: W-min C in C by SPRECT_1:15;
    A134: L~pion = LSeg(Gik,Gij) by SPPOL_2:21;
    A135: now assume W-min C in L~godo;
     then A136: W-min C in L~go \/ L~pion1 or W-min C in L~do by A130,
XBOOLE_0:def 2;
     per cases by A136,XBOOLE_0:def 2;
      suppose W-min C in L~go;
       then C meets L~Cage(C,n) by A132,A133,XBOOLE_0:3;
       hence contradiction by JORDAN10:5;
      end;
      suppose W-min C in L~pion1;
       hence contradiction by A5,A76,A118,A134,XBOOLE_0:3;
      end;
      suppose W-min C in L~do;
       then C meets L~Cage(C,n) by A132,A133,XBOOLE_0:3;
       hence contradiction by JORDAN10:5;
    end;
    end;
    right_cell(Rotate(Cage(C,n),Wmin),1) =
          right_cell(ff,1,GoB ff) by A81,JORDAN1H:29
       .= right_cell(ff,1,GoB Cage(C,n)) by REVROT_1:28
       .= right_cell(ff,1,Ga) by JORDAN1H:52
       .= right_cell(ff-:Emax,1,Ga) by A124,A128,Th53
       .= right_cell(US,1,Ga) by JORDAN1E:def 1
       .= right_cell(R_Cut(US,Gik),1,Ga) by A32,A85,A125,Th52
       .= right_cell(go^'pion1,1,Ga) by A37,A87,Th51
       .= right_cell(godo,1,Ga) by A83,A89,Th51;
    then W-min C in right_cell(godo,1,Ga) by JORDAN1I:8;
    then A137: W-min C in right_cell(godo,1,Ga)\L~godo by A135,XBOOLE_0:def 4;
    A138: godo/.1 = (go^'pion1)/.1 by GRAPH_2:57
       .= Wmin by A57,GRAPH_2:57;
    A139: len US >= 2 by A16,XXREAL_0:2;
    A140: godo/.2 = (go^'pion1)/.2 by A82,GRAPH_2:61
       .= US/.2 by A31,A70,GRAPH_2:61
       .= (US^'LS)/.2 by A139,GRAPH_2:61
       .= Rotate(Cage(C,n),Wmin)/.2 by JORDAN1E:15;
    A141: L~go \/ L~do is compact by COMPTS_1:19;
    A142: L~go \/ L~do c= L~Cage(C,n) by A132,XBOOLE_1:8;
        Wmin in rng go by A57,FINSEQ_6:46;
    then Wmin in L~go \/ L~do by A59,XBOOLE_0:def 2;
    then A143: W-min (L~go \/ L~do) = Wmin by A141,A142,Th21;
    A144: (W-min (L~go \/ L~do))`1 = W-bound (L~go \/ L~do) & Wmin`1 = Wbo
                                                              by EUCLID:56;
        W-bound LSeg(Gik,Gij) = Gik`1 by A73,SPRECT_1:62;
    then A145: W-bound L~pion1 = Gik`1 by A76,SPPOL_2:21;
        Gik`1 >= Wbo by A7,A131,PSCOMP_1:71;
    then Gik`1 > Wbo by A69,REAL_1:def 5;
    then W-min (L~go\/L~do\/L~pion1) = W-min (L~go \/ L~do)
                                                  by A141,A143,A144,A145,Th33;
    then A146: W-min L~godo = Wmin by A130,A143,XBOOLE_1:4;
    A147: rng godo c= L~godo by A84,SPPOL_2:18;
        2 in dom godo by A84,FINSEQ_3:27;
    then A148: godo/.2 in rng godo by PARTFUN2:4;
        godo/.2 in W-most L~Cage(C,n) by A140,JORDAN1I:27;
    then (godo/.2)`1 = (W-min L~godo)`1 by A146,PSCOMP_1:88
       .= W-bound L~godo by EUCLID:56;
    then godo/.2 in W-most L~godo by A147,A148,SPRECT_2:16;
    then Rotate(godo,W-min L~godo)/.2 in W-most L~godo by A138,A146,FINSEQ_6:95
;
    then reconsider godo as clockwise_oriented non constant standard
                                    special_circular_sequence by JORDAN1I:27;
        len US in dom US by FINSEQ_5:6;
    then A149: US.len US = US/.len US by FINSEQ_4:def 4
       .= Emax by JORDAN1F:7;
    A150: E-max C in E-most C by PSCOMP_1:111;
    A151: east_halfline E-max C misses L~go
    proof
     assume east_halfline E-max C meets L~go;
     then consider p be set such that
      A152: p in east_halfline E-max C and
      A153: p in L~go by XBOOLE_0:3;
     reconsider p as Point of TOP-REAL 2 by A152;
         p in L~US by A44,A153;
     then p in east_halfline E-max C /\ L~Cage(C,n) by A131,A152,XBOOLE_0:def 3
;
     then A154: p`1 = Ebo by A150,JORDAN1A:104;
     then A155: p = Emax by A44,A153,Th46;
     then Emax = Gik by A7,A149,A153,Th43;
     then Gik`1 = Ga*(len Ga,k)`1 by A10,A15,A154,A155,JORDAN1A:92;
     hence contradiction by A1,A12,A28,JORDAN1G:7;
    end;
        now assume east_halfline E-max C meets L~godo;
     then A156: east_halfline E-max C meets (L~go \/ L~pion1) or
               east_halfline E-max C meets L~do by A130,XBOOLE_1:70;
     per cases by A156,XBOOLE_1:70;
      suppose east_halfline E-max C meets L~go;
       hence contradiction by A151;
      end;
      suppose east_halfline E-max C meets L~pion1;
       then consider p be set such that
        A157: p in east_halfline E-max C and
        A158: p in L~pion1 by XBOOLE_0:3;
       reconsider p as Point of TOP-REAL 2 by A157;
       A159: p`1 = Gik`1 by A73,A76,A134,A158,GOBOARD7:5;
           i+1 <= len Ga by A1,NAT_1:13;
       then A160: i+1-1 <= len Ga-1 by XREAL_1:11;
       A161: i <= len Ga-'1 by A160,BINARITH:def 3;
           len Ga-'1 <= len Ga by BINARITH:52;
       then p`1 <= Ga*(len Ga-'1,1)`1 by A1,A10,A15,A19,A159,A161,JORDAN1A:39;
       then p`1 <= E-bound C by A19,JORDAN8:15;
       then A162: p`1 <= (E-max C)`1 by EUCLID:56;
           p`1 >= (E-max C)`1 by A157,TOPREAL1:def 13;
       then A163: p`1 = (E-max C)`1 by A162,XXREAL_0:1;
           p`2 = (E-max C)`2 by A157,TOPREAL1:def 13;
       then p = E-max C by A163,TOPREAL3:11;
       hence contradiction by A5,A76,A118,A134,A158,XBOOLE_0:3;
      end;
      suppose east_halfline E-max C meets L~do;
       then consider p be set such that
        A164: p in east_halfline E-max C and
        A165: p in L~do by XBOOLE_0:3;
       reconsider p as Point of TOP-REAL 2 by A164;
           p in L~LS by A51,A165;
       then p in east_halfline E-max C /\ L~Cage(C,n) by A131,A164,
XBOOLE_0:def 3;
       then A166: p`1 = Ebo by A150,JORDAN1A:104;
       A167: (E-max C)`2 = p`2 by A164,TOPREAL1:def 13;
       set RC = Rotate(Cage(C,n),Emax);
       A168: E-max C in right_cell(RC,1) by JORDAN1I:9;
       A169: 1+1 <= len LS by A22,XXREAL_0:2;
           LS = RC-:Wmin by JORDAN1G:26;
       then A170: LSeg(LS,1) = LSeg(RC,1) by A169,SPPOL_2:9;
       A171: L~RC = L~Cage(C,n) by REVROT_1:33;
       A172: len RC = len Cage(C,n) by REVROT_1:14;
       A173: GoB RC = GoB Cage(C,n) by REVROT_1:28
          .= Ga by JORDAN1H:52;
       A174: Emax in rng Cage(C,n) by SPRECT_2:50;
       A175: RC is_sequence_on Ga by A127,REVROT_1:34;
       A176: RC/.1 = E-max L~RC by A171,A174,FINSEQ_6:98;
       then consider ii,jj be Element of NAT such that
        A177: [ii,jj+1] in Indices Ga and
        A178: [ii,jj] in Indices Ga and
        A179: RC/.1 = Ga*(ii,jj+1) and
        A180: RC/.(1+1) = Ga*(ii,jj) by A80,A172,A175,JORDAN1I:25;
       consider jj2 be Element of NAT such that
        A181: 1 <= jj2 & jj2 <= width Ga and
        A182: Emax = Ga*(len Ga,jj2) by JORDAN1D:29;
       A183: len Ga >= 4 by JORDAN8:13;
       then len Ga >= 1 by XXREAL_0:2;
       then [len Ga,jj2] in Indices Ga by A181,GOBOARD7:10;
       then A184: ii = len Ga by A171,A176,A177,A179,A182,GOBOARD1:21;
       A185: 1 <= ii & ii <= len Ga & 1 <= jj+1 & jj+1 <= width Ga
                                                           by A177,GOBOARD5:1;
       A186: 1 <= ii & ii <= len Ga & 1 <= jj & jj <= width Ga
                                                           by A178,GOBOARD5:1;
       A187: ii+1 <> ii;
       jj+1+1 <> jj;
       then A188: right_cell(RC,1) = cell(Ga,ii-'1,jj)
                          by A80,A172,A173,A177,A178,A179,A180,A187,
GOBOARD5:def 6;
       A189: ii-'1+1 = ii by A185,BINARITH:53;
       ii-1 >= 4-1 by A183,A184,XREAL_1:11;
       then A190: ii-1 >= 1 by XXREAL_0:2;
       A191: 1 <= ii-'1 by A190,BINARITH:def 3;
       A192: Ga*(ii-'1,jj)`1 <= (E-max C)`1 & (E-max C)`1 <= Ga*(ii-'1+1,
jj)`1 &
            Ga*(ii-'1,jj)`2 <= (E-max C)`2 & (E-max C)`2 <= Ga*(ii-'1,jj+1)`2
                                      by A168,A185,A186,A188,A189,A190,
JORDAN9:19;
       A193: ii-'1 < len Ga by A185,A189,NAT_1:13;
       then A194: Ga*(ii-'1,jj)`2 = Ga*(1,jj)`2 by A186,A191,GOBOARD5:2
          .= Ga*(ii,jj)`2 by A186,GOBOARD5:2;
       A195: Ga*(ii-'1,jj+1)`2 = Ga*(1,jj+1)`2 by A185,A191,A193,GOBOARD5:2
          .= Ga*(ii,jj+1)`2 by A185,GOBOARD5:2;
           Ga*(len Ga,jj)`1 = Ebo & Ebo = Ga*(len Ga,jj+1)`1
                                                by A15,A185,A186,JORDAN1A:92;
       then p in LSeg(RC/.1,RC/.(1+1))
                              by A166,A167,A179,A180,A184,A192,A194,A195,
GOBOARD7:8;
       then A196: p in LSeg(LS,1) by A80,A170,A172,TOPREAL1:def 5;
       A197: p in LSeg(do,Index(p,do)) by A165,JORDAN3:42;
       A198: do = mid(LS,Gij..LS,len LS) by A35,Th37;
       A199: 1<=Gij..LS & Gij..LS<=len LS by A35,FINSEQ_4:31;
           Gij..LS <> len LS by A27,A35,FINSEQ_4:29;
       then A200: Gij..LS < len LS by A199,REAL_1:def 5;
       A201: 1<=Index(p,do) & Index(p,do) < len do by A165,JORDAN3:41;
       A202: Index(Gij,LS)+1 = Gij..LS by A30,A35,Th56;
       consider t be Element of NAT such that
        A203: t in dom LS and
        A204: LS.t = Gij by A35,FINSEQ_2:11;
       A205: 1 <= t & t <= len LS by A203,FINSEQ_3:27;
       then 1 < t by A30,A204,REAL_1:def 5;
       then Index(Gij,LS)+1 = t by A204,A205,JORDAN3:45;
       then A206: len L_Cut(LS,Gij) = len LS-Index(Gij,LS)
                                                  by A6,A204,JORDAN3:61;
       set tt = Index(p,do)+(Gij..LS)-'1;
       A207: 1<=Index(Gij,LS) & 0+Index(Gij,LS) < len LS by A6,JORDAN3:41;
       then A208: len LS-Index(Gij,LS) > 0 by XREAL_1:22;
       Index(p,do) < len LS-'Index(Gij,LS) by A201,A206,BINARITH:def 3;
       then Index(p,do)+1 <= len LS-'Index(Gij,LS) by NAT_1:13;
       then Index(p,do) <= len LS-'Index(Gij,LS)-1 by XREAL_1:21;
       then Index(p,do) <= len LS-Index(Gij,LS)-1 by A208,BINARITH:def 3;
       then Index(p,do) <= len LS-Gij..LS by A202;
       then Index(p,do) <= len LS-'Gij..LS by BINARITH:def 3;
       then Index(p,do) < len LS-'(Gij..LS)+1 by NAT_1:13;
       then A209: LSeg(mid(LS,Gij..LS,len LS),Index(p,do)) =
            LSeg(LS,Index(p,do)+(Gij..LS)-'1) by A199,A200,A201,JORDAN4:31;
       A210: 1+1 <= Gij..LS by A202,A207,XREAL_1:9;
       then Index(p,do)+Gij..LS >= 1+1+1 by A201,XREAL_1:9;
       then A211: Index(p,do)+Gij..LS-1 >= 1+1+1-1 by XREAL_1:11;
       then A212: Index(p,do)+Gij..LS-1 >= 0;
       A213: tt >= 1+1 by A211,BINARITH:def 3;
       A214: 2 in dom LS by A169,FINSEQ_3:27;
           now per cases by A213,REAL_1:def 5;
        suppose tt > 1+1;
         then LSeg(LS,1) misses LSeg(LS,tt) by TOPREAL1:def 9;
         hence contradiction by A196,A197,A198,A209,XBOOLE_0:3;
        end;
        suppose A215: tt = 1+1;
         then LSeg(LS,1) /\ LSeg(LS,tt) = {LS/.2} by A22,TOPREAL1:def 8;
         then p in {LS/.2} by A196,A197,A198,A209,XBOOLE_0:def 3;
         then A216: p = LS/.2 by TARSKI:def 1;
         then A217: p..LS = 2 by A214,FINSEQ_5:44;
             1+1 = Index(p,do)+(Gij..LS)-1 by A212,A215,BINARITH:def 3;
         then 1+1+1 = Index(p,do)+(Gij..LS);
         then A218: Gij..LS = 2 by A201,A210,JORDAN1E:10;
             p in rng LS by A214,A216,PARTFUN2:4;
         then p = Gij by A35,A217,A218,FINSEQ_5:10;
         then Gij`1 = Ebo by A216,JORDAN1G:40;
         then Gij`1 = Ga*(len Ga,j)`1 by A9,A15,JORDAN1A:92;
         hence contradiction by A1,A11,A62,JORDAN1G:7;
       end;
       end;
       hence contradiction;
    end;
    end;
    then east_halfline E-max C c= (L~godo)` by SUBSET_1:43;
    then consider W be Subset of TOP-REAL 2 such that
     A219: W is_a_component_of (L~godo)` and
     A220: east_halfline E-max C c= W by GOBOARD9:5;
        east_halfline E-max C is not Bounded by JORDAN2C:129;
    then W is not Bounded by A220,JORDAN2C:16;
    then W is_outside_component_of L~godo by A219,JORDAN2C:def 4;
    then W c= UBD L~godo by JORDAN2C:27;
    then A221: east_halfline E-max C c= UBD L~godo by A220,XBOOLE_1:1;
        E-max C in east_halfline E-max C by TOPREAL1:45;
    then E-max C in UBD L~godo by A221;
    then E-max C in LeftComp godo by GOBRD14:46;
    then LA meets L~godo by A117,A118,A129,A137,Th36;
    then A222: LA meets (L~go \/ L~pion1) or LA meets L~do by A130,XBOOLE_1:70;
    A223: LA c= C by JORDAN6:76;
    per cases by A222,XBOOLE_1:70;
     suppose LA meets L~go;
      then LA meets L~Cage(C,n) by A132,XBOOLE_1:63;
      then C meets L~Cage(C,n) by A223,XBOOLE_1:63;
      hence contradiction by JORDAN10:5;
     end;
     suppose LA meets L~pion1;
      hence contradiction by A5,A76,A134;
     end;
     suppose LA meets L~do;
      then LA meets L~Cage(C,n) by A132,XBOOLE_1:63;
      then C meets L~Cage(C,n) by A223,XBOOLE_1:63;
      hence contradiction by JORDAN10:5;
   end;
   end;

  theorem Th59:
   for C be Simple_closed_curve
   for i,j,k be Element of NAT st
    1 < i & i < len Gauge(C,n) &
    1 <= j & j <= k & k <= width Gauge(C,n) &
    LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ L~Upper_Seq(C,n) =
     {Gauge(C,n)*(i,k)} &
    LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ L~Lower_Seq(C,n) =
     {Gauge(C,n)*(i,j)} holds
      LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Upper_Arc C
   proof
    let C be Simple_closed_curve;
    let i,j,k be Element of NAT;
    set Ga = Gauge(C,n);
    set US = Upper_Seq(C,n);
    set LS = Lower_Seq(C,n);
    set UA = Upper_Arc C;
    set Wmin = W-min L~Cage(C,n);
    set Emax = E-max L~Cage(C,n);
    set Wbo = W-bound L~Cage(C,n);
    set Ebo = E-bound L~Cage(C,n);
    set Gij = Ga*(i,j);
    set Gik = Ga*(i,k);
    assume that
     A1: 1 < i & i < len Ga and
     A2: 1 <= j & j <= k & k <= width Ga and
     A3: LSeg(Gij,Gik) /\ L~US = {Gik} and
     A4: LSeg(Gij,Gik) /\ L~LS = {Gij} and
     A5: LSeg(Gij,Gik) misses UA;
        Gij in {Gij} by TARSKI:def 1;
    then A6: Gij in L~LS by A4,XBOOLE_0:def 3;
        Gik in {Gik} by TARSKI:def 1;
    then A7: Gik in L~US by A3,XBOOLE_0:def 3;
    then A8: j <> k by A1,A2,A6,Th57;
    A9: 1 <= j & j <= width Ga by A2,XXREAL_0:2;
    A10: 1 <= k & k <= width Ga by A2,XXREAL_0:2;
    A11: [i,j] in Indices Ga by A1,A9,GOBOARD7:10;
    A12: [i,k] in Indices Ga by A1,A10,GOBOARD7:10;
    A13: US is_sequence_on Ga by JORDAN1G:4;
    A14: LS is_sequence_on Ga by JORDAN1G:5;
    set go = R_Cut(US,Gik);
    set do = L_Cut(LS,Gij);
    A15: len Ga = width Ga by JORDAN8:def 1;
    A16: len US >= 3 by JORDAN1E:19;
    then len US >= 1 by XXREAL_0:2;
    then 1 in dom US by FINSEQ_3:27;
    then A17: US.1 = US/.1 by FINSEQ_4:def 4
       .= Wmin by JORDAN1F:5;
    A18: Wmin`1 = Wbo by EUCLID:56
       .= Ga*(1,k)`1 by A10,A15,JORDAN1A:94;
        len Ga >= 4 by JORDAN8:13;
    then A19: len Ga >= 1 by XXREAL_0:2;
    then A20: [1,k] in Indices Ga by A10,GOBOARD7:10;
    then A21: Gik <> US.1 by A1,A12,A17,A18,JORDAN1G:7;
    then reconsider go as being_S-Seq FinSequence of TOP-REAL 2
                                                            by A7,JORDAN3:70;
    A22: len LS >= 1+2 by JORDAN1E:19;
    then len LS >= 1 by XXREAL_0:2;
    then A23: 1 in dom LS & len LS in dom LS by FINSEQ_3:27;
    then A24: LS.len LS = LS/.len LS by FINSEQ_4:def 4
       .= Wmin by JORDAN1F:8;
    A25: Wmin`1 = Wbo by EUCLID:56
       .= Ga*(1,k)`1 by A10,A15,JORDAN1A:94;
    A26: [i,j] in Indices Ga by A1,A9,GOBOARD7:10;
    then A27: Gij <> LS.len LS by A1,A20,A24,A25,JORDAN1G:7;
    then reconsider do as being_S-Seq FinSequence of TOP-REAL 2
                                                            by A6,JORDAN3:69;
    A28: [len Ga,k] in Indices Ga by A10,A19,GOBOARD7:10;
    A29: LS.1 = LS/.1 by A23,FINSEQ_4:def 4
       .= Emax by JORDAN1F:6;
        Emax`1 = Ebo by EUCLID:56
       .= Ga*(len Ga,k)`1 by A10,A15,JORDAN1A:92;
    then A30: Gij <> LS.1 by A1,A26,A28,A29,JORDAN1G:7;
    A31: len go >= 1+1 by TOPREAL1:def 10;
    A32: Gik in rng US by A1,A7,A10,A13,Th40;
    then A33: go is_sequence_on Ga by A13,Th38;
    A34: len do >= 1+1 by TOPREAL1:def 10;
    A35: Gij in rng LS by A1,A6,A9,A14,Th40;
    then A36: do is_sequence_on Ga by A14,Th39;
    reconsider go as non constant s.c.c.
            (being_S-Seq FinSequence of TOP-REAL 2) by A31,A33,JGRAPH_1:16
,JORDAN8:8;
    reconsider do as non constant s.c.c.
            (being_S-Seq FinSequence of TOP-REAL 2) by A34,A36,JGRAPH_1:16
,JORDAN8:8;
    A37: len go > 1 by A31,NAT_1:13;
    then A38: len go in dom go by FINSEQ_3:27;
    then A39: go/.len go = go.len go by FINSEQ_4:def 4
       .= Gik by A7,JORDAN3:59;
        len do >= 1 by A34,XXREAL_0:2;
    then 1 in dom do by FINSEQ_3:27;
    then A40: do/.1 = do.1 by FINSEQ_4:def 4
       .= Gij by A6,JORDAN3:58;
    reconsider m = len go - 1 as Element of NAT by A38,FINSEQ_3:28;
    A41: m+1 = len go;
    then A42: len go-'1 = m by BINARITH:39;
    A43: LSeg(go,m) c= L~go by TOPREAL3:26;
    A44: L~go c= L~US by A7,JORDAN3:76;
    then LSeg(go,m) c= L~US by A43,XBOOLE_1:1;
    then A45: LSeg(go,m) /\ LSeg(Gik,Gij) c= {Gik} by A3,XBOOLE_1:26;
        m >= 1 by A31,XREAL_1:21;
    then A46: LSeg(go,m) = LSeg(go/.m,Gik) by A39,A41,TOPREAL1:def 5;
        {Gik} c= LSeg(go,m) /\ LSeg(Gik,Gij)
    proof
     let x be set;
     assume x in {Gik};
     then A47: x = Gik by TARSKI:def 1;
     A48: Gik in LSeg(go,m) by A46,TOPREAL1:6;
         Gik in LSeg(Gik,Gij) by TOPREAL1:6;
     hence x in LSeg(go,m) /\ LSeg(Gik,Gij) by A47,A48,XBOOLE_0:def 3;
    end;
    then A49: LSeg(go,m) /\ LSeg(Gik,Gij) = {Gik} by A45,XBOOLE_0:def 10;
    A50: LSeg(do,1) c= L~do by TOPREAL3:26;
    A51: L~do c= L~LS by A6,JORDAN3:77;
    then LSeg(do,1) c= L~LS by A50,XBOOLE_1:1;
    then A52: LSeg(do,1) /\ LSeg(Gik,Gij) c= {Gij} by A4,XBOOLE_1:26;
    A53: LSeg(do,1) = LSeg(Gij,do/.(1+1)) by A34,A40,TOPREAL1:def 5;
        {Gij} c= LSeg(do,1) /\ LSeg(Gik,Gij)
    proof
     let x be set;
     assume x in {Gij};
     then A54: x = Gij by TARSKI:def 1;
     A55: Gij in LSeg(do,1) by A53,TOPREAL1:6;
         Gij in LSeg(Gik,Gij) by TOPREAL1:6;
     hence x in LSeg(do,1) /\ LSeg(Gik,Gij) by A54,A55,XBOOLE_0:def 3;
    end;
    then A56: LSeg(Gik,Gij) /\ LSeg(do,1) = {Gij} by A52,XBOOLE_0:def 10;
    A57: go/.1 = US/.1 by A7,SPRECT_3:39
       .= Wmin by JORDAN1F:5;
    then A58: go/.1 = LS/.len LS by JORDAN1F:8
       .= do/.len do by A6,Th35;
    A59: rng go c= L~go & rng do c= L~do by A31,A34,SPPOL_2:18;
    A60: {go/.1} c= L~go /\ L~do
    proof
     let x be set;
     assume x in {go/.1};
     then x = go/.1 by TARSKI:def 1;
     then x in rng go & x in rng do by A58,FINSEQ_6:46,REVROT_1:3;
     hence x in L~go /\ L~do by A59,XBOOLE_0:def 3;
    end;
    A61: LS.1 = LS/.1 by A23,FINSEQ_4:def 4
       .= Emax by JORDAN1F:6;
    A62: [len Ga,j] in Indices Ga by A9,A19,GOBOARD7:10;
        L~go /\ L~do c= {go/.1}
    proof
     let x be set;
     assume x in L~go /\ L~do;
     then A63: x in L~go & x in L~do by XBOOLE_0:def 3;
     then x in L~US /\ L~LS by A44,A51,XBOOLE_0:def 3;
     then x in {Wmin,Emax} by JORDAN1E:20;
     then A64: x = Wmin or x = Emax by TARSKI:def 2;
         now assume x = Emax;
      then A65: Emax = Gij by A6,A61,A63,JORDAN1E:11;
          Ga*(len Ga,j)`1 = Ebo by A9,A15,JORDAN1A:92;
      then Emax`1 <> Ebo by A1,A11,A62,A65,JORDAN1G:7;
      hence contradiction by EUCLID:56;
     end;
     hence x in {go/.1} by A57,A64,TARSKI:def 1;
    end;
    then A66: L~go /\ L~do = {go/.1} by A60,XBOOLE_0:def 10;
    set W2 = go/.2;
    A67: 2 in dom go by A31,FINSEQ_3:27;
    A68: Gik..US >= 1 by A32,FINSEQ_4:31;
    A69: now assume Gik`1 = Wbo;
     then Ga*(1,k)`1 = Ga*(i,k)`1 by A10,A15,JORDAN1A:94;
     hence contradiction by A1,A12,A20,JORDAN1G:7;
    end;
        go = mid(US,1,Gik..US) by A32,JORDAN1G:57
       .= US|(Gik..US) by A68,JORDAN3:25;
    then A70: W2 = US/.2 by A67,FINSEQ_4:85;
    set pion = <*Gik,Gij*>;
    A71: now let n be Element of NAT;
     assume n in dom pion;
     then n in Seg 2 by FINSEQ_3:29;
     then n = 1 or n = 2 by FINSEQ_1:4,TARSKI:def 2;
     then pion/.n = Gik or pion/.n = Gij by FINSEQ_4:26;
     hence ex i,j be Element of NAT st [i,j] in Indices Ga & pion/.n = Ga*(i,j)
                                                                  by A11,A12;
    end;
    A72: Gik <> Gij by A8,A11,A12,GOBOARD1:21;
    A73: Gik`1 = Ga*(i,1)`1 by A1,A10,GOBOARD5:3
       .= Gij`1 by A1,A9,GOBOARD5:3;
    then LSeg(Gik,Gij) is vertical by SPPOL_1:37;
    then pion is_S-Seq by A72,JORDAN1B:8;
    then consider pion1 be FinSequence of TOP-REAL 2 such that
     A74: pion1 is_sequence_on Ga and
     A75: pion1 is_S-Seq and
     A76: L~pion = L~pion1 and
     A77: pion/.1 = pion1/.1 and
     A78: pion/.len pion = pion1/.len pion1 and
     A79: len pion <= len pion1 by A71,GOBOARD3:2;
    reconsider pion1 as being_S-Seq FinSequence of TOP-REAL 2 by A75;
    set godo = go^'pion1^'do;
        len Cage(C,n) > 4 by GOBOARD7:36;
    then A80: 1+1 <= len Cage(C,n) by XXREAL_0:2;
    then A81: 1+1 <= len Rotate(Cage(C,n),Wmin) by REVROT_1:14;
        len (go^'pion1) >= len go by TOPREAL8:7;
    then A82: len (go^'pion1) >= 1+1 by A31,XXREAL_0:2;
    then A83: len (go^'pion1) > 1+0 by NAT_1:13;
        len godo >= len (go^'pion1) by TOPREAL8:7;
    then A84: 1+1 <= len godo by A82,XXREAL_0:2;
    A85: US is_sequence_on Ga by JORDAN1G:4;
    A86: go/.len go = pion1/.1 by A39,A77,FINSEQ_4:26;
    then A87: go^'pion1 is_sequence_on Ga by A33,A74,TOPREAL8:12;
    A88: (go^'pion1)/.len (go^'pion1) = pion/.len pion by A78,GRAPH_2:58
       .= pion/.2 by FINSEQ_1:61
       .= do/.1 by A40,FINSEQ_4:26;
    then A89: godo is_sequence_on Ga by A36,A87,TOPREAL8:12;
        LSeg(pion1,1) c= L~<*Gik,Gij*> by A76,TOPREAL3:26;
    then LSeg(pion1,1) c= LSeg(Gik,Gij) by SPPOL_2:21;
    then A90: LSeg(go,len go-'1) /\ LSeg(pion1,1) c= {Gik} by A42,A49,
XBOOLE_1:27;
    A91: len pion1 >= 1+1 by A79,FINSEQ_1:61;
        {Gik} c= LSeg(go,m) /\ LSeg(pion1,1)
    proof
     let x be set;
     assume x in {Gik};
     then A92: x = Gik by TARSKI:def 1;
     A93: Gik in LSeg(go,m) by A46,TOPREAL1:6;
         Gik in LSeg(pion1,1) by A39,A86,A91,TOPREAL1:27;
     hence x in LSeg(go,m) /\ LSeg(pion1,1) by A92,A93,XBOOLE_0:def 3;
    end;
    then LSeg(go,len go-'1) /\ LSeg(pion1,1) = { go/.len go }
                                            by A39,A42,A90,XBOOLE_0:def 10;
    then A94: go^'pion1 is unfolded by A86,TOPREAL8:34;
        len pion1 >= 2+0 by A79,FINSEQ_1:61;
    then A95: len pion1-2 >= 0 by XREAL_1:21;
        len (go^'pion1)+1-1 = len go+len pion1-1 by GRAPH_2:13;
    then len (go^'pion1)-1 = len go + (len pion1-2)
       .= len go + (len pion1-'2) by A95,BINARITH:def 3;
    then A96: len (go^'pion1)-'1 = len go + (len pion1-'2)
                                                      by BINARITH:def 3;
    A97: len pion1-1 >= 1 by A91,XREAL_1:21;
    then A98: len pion1-1 >= 0;
    then A99: len pion1-'1 = len pion1-1 by BINARITH:def 3;
    A100: len pion1-'2+1 = len pion1-2+1 by A95,BINARITH:def 3
       .= len pion1-'1 by A98,BINARITH:def 3;
        len pion1-1+1 <= len pion1;
    then A101: len pion1-'1 < len pion1 by A99,NAT_1:13;
        LSeg(pion1,len pion1-'1) c= L~<*Gik,Gij*> by A76,TOPREAL3:26;
    then LSeg(pion1,len pion1-'1) c= LSeg(Gik,Gij) by SPPOL_2:21;
    then A102: LSeg(pion1,len pion1-'1) /\ LSeg(do,1) c= {Gij} by A56,
XBOOLE_1:27;
        {Gij} c= LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
    proof
     let x be set;
     assume x in {Gij};
     then A103: x = Gij by TARSKI:def 1;
     A104: Gij in LSeg(do,1) by A53,TOPREAL1:6;
      pion1/.(len pion1-'1+1) = pion/.2 by A78,A99,FINSEQ_1:61
        .= Gij by FINSEQ_4:26;
     then Gij in LSeg(pion1,len pion1-'1) by A97,A99,TOPREAL1:27;
     hence x in LSeg(pion1,len pion1-'1) /\ LSeg(do,1) by A103,A104,
XBOOLE_0:def 3;
    end;
    then LSeg(pion1,len pion1-'1) /\ LSeg(do,1) = {Gij} by A102,XBOOLE_0:def 10
;
    then A105: LSeg(go^'pion1,len go+(len pion1-'2)) /\ LSeg(do,1) =
       {(go^'pion1)/.len (go^'pion1)} by A40,A86,A88,A100,A101,TOPREAL8:31;
    A106: (go^'pion1) is non trivial by A82,REALSET1:13;
    A107: rng pion1 c= L~pion1 by A91,SPPOL_2:18;
    A108: {pion1/.1} c= L~go /\ L~pion1
    proof
     let x be set;
     assume x in {pion1/.1};
     then x = pion1/.1 by TARSKI:def 1;
     then x in rng go & x in rng pion1 by A86,FINSEQ_6:46,REVROT_1:3;
     hence x in L~go /\ L~pion1 by A59,A107,XBOOLE_0:def 3;
    end;
        L~go /\ L~pion1 c= {pion1/.1}
    proof
     let x be set;
     assume x in L~go /\ L~pion1;
     then x in L~go & x in L~pion1 by XBOOLE_0:def 3;
     then x in L~pion1 /\ L~US by A44,XBOOLE_0:def 3;
     hence x in {pion1/.1} by A3,A39,A76,A86,SPPOL_2:21;
    end;
    then A109: L~go /\ L~pion1 = {pion1/.1} by A108,XBOOLE_0:def 10;
    then A110: (go^'pion1) is s.n.c. by A86,Th54;
        rng go /\ rng pion1 c= {pion1/.1} by A59,A107,A109,XBOOLE_1:27;
    then A111: go^'pion1 is one-to-one by Th55;
    A112: pion/.len pion = pion/.2 by FINSEQ_1:61
       .= do/.1 by A40,FINSEQ_4:26;
    A113: {pion1/.len pion1} c= L~do /\ L~pion1
    proof
     let x be set;
     assume x in {pion1/.len pion1};
     then x = pion1/.len pion1 by TARSKI:def 1;
     then x in rng do & x in rng pion1 by A78,A112,FINSEQ_6:46,REVROT_1:3;
     hence x in L~do /\ L~pion1 by A59,A107,XBOOLE_0:def 3;
    end;
        L~do /\ L~pion1 c= {pion1/.len pion1}
    proof
     let x be set;
     assume x in L~do /\ L~pion1;
     then x in L~do & x in L~pion1 by XBOOLE_0:def 3;
     then x in L~pion1 /\ L~LS by A51,XBOOLE_0:def 3;
     hence x in {pion1/.len pion1} by A4,A40,A76,A78,A112,SPPOL_2:21;
    end;
    then A114: L~do /\ L~pion1 = {pion1/.len pion1} by A113,XBOOLE_0:def 10;
    A115: L~(go^'pion1) /\ L~do = (L~go \/ L~pion1) /\ L~do by A86,TOPREAL8:35
       .= {go/.1} \/ {do/.1} by A66,A78,A112,A114,XBOOLE_1:23
       .= {(go^'pion1)/.1} \/ {do/.1} by GRAPH_2:57
       .= {(go^'pion1)/.1,do/.1} by ENUMSET1:41;
        do/.len do = (go^'pion1)/.1 by A58,GRAPH_2:57;
    then reconsider godo as non constant standard special_circular_sequence
                                              by A84,A88,A89,A94,A96,A105,A106
,A110,A111,A115,JORDAN8:7,8,TOPREAL8:11,33,34;
    A116: UA is_an_arc_of W-min C,E-max C by JORDAN6:def 8;
    then A117: UA is connected by JORDAN6:11;
    A118: W-min C in UA & E-max C in UA by A116,TOPREAL1:4;
    set ff = Rotate(Cage(C,n),Wmin);
        Wmin in rng Cage(C,n) by SPRECT_2:47;
    then A119: ff/.1 = Wmin by FINSEQ_6:98;
    A120: L~ff = L~Cage(C,n) by REVROT_1:33;
    then A121: (W-max L~ff)..ff > 1 by A119,SPRECT_5:23;
        (W-max L~ff)..ff <= (N-min L~ff)..ff by A119,A120,SPRECT_5:24;
    then A122: (N-min L~ff)..ff > 1 by A121,XXREAL_0:2;
        (N-min L~ff)..ff < (N-max L~ff)..ff by A119,A120,SPRECT_5:25;
    then A123: (N-max L~ff)..ff > 1 by A122,XXREAL_0:2;
        (N-max L~ff)..ff <= (E-max L~ff)..ff by A119,A120,SPRECT_5:26;
    then A124: Emax..ff > 1 by A120,A123,XXREAL_0:2;
    A125: now assume A126: Gik..US <= 1;
         Gik..US >= 1 by A32,FINSEQ_4:31;
     then Gik..US = 1 by A126,XXREAL_0:1;
     then Gik = US/.1 by A32,FINSEQ_5:41;
     hence contradiction by A17,A21,JORDAN1F:5;
    end;
    A127: Cage(C,n) is_sequence_on Ga by JORDAN9:def 1;
    then A128: ff is_sequence_on Ga by REVROT_1:34;
    A129: right_cell(godo,1,Ga)\L~godo c= RightComp godo by A84,A89,JORDAN9:29;
    A130: L~godo = L~(go^'pion1) \/ L~do by A88,TOPREAL8:35
       .= L~go \/ L~pion1 \/ L~do by A86,TOPREAL8:35;
        L~Cage(C,n) = L~US \/ L~LS by JORDAN1E:17;
    then A131: L~US c= L~Cage(C,n) & L~LS c= L~Cage(C,n) by XBOOLE_1:7;
    then A132: L~go c= L~Cage(C,n) & L~do c= L~Cage(C,n) by A44,A51,XBOOLE_1:1;
    A133: W-min C in C by SPRECT_1:15;
    A134: L~pion = LSeg(Gik,Gij) by SPPOL_2:21;
    A135: now assume W-min C in L~godo;
     then A136: W-min C in L~go \/ L~pion1 or W-min C in L~do by A130,
XBOOLE_0:def 2;
     per cases by A136,XBOOLE_0:def 2;
      suppose W-min C in L~go;
       then C meets L~Cage(C,n) by A132,A133,XBOOLE_0:3;
       hence contradiction by JORDAN10:5;
      end;
      suppose W-min C in L~pion1;
       hence contradiction by A5,A76,A118,A134,XBOOLE_0:3;
      end;
      suppose W-min C in L~do;
       then C meets L~Cage(C,n) by A132,A133,XBOOLE_0:3;
       hence contradiction by JORDAN10:5;
    end;
    end;
        right_cell(Rotate(Cage(C,n),Wmin),1) =
          right_cell(ff,1,GoB ff) by A81,JORDAN1H:29
       .= right_cell(ff,1,GoB Cage(C,n)) by REVROT_1:28
       .= right_cell(ff,1,Ga) by JORDAN1H:52
       .= right_cell(ff-:Emax,1,Ga) by A124,A128,Th53
       .= right_cell(US,1,Ga) by JORDAN1E:def 1
       .= right_cell(R_Cut(US,Gik),1,Ga) by A32,A85,A125,Th52
       .= right_cell(go^'pion1,1,Ga) by A37,A87,Th51
       .= right_cell(godo,1,Ga) by A83,A89,Th51;
    then W-min C in right_cell(godo,1,Ga) by JORDAN1I:8;
    then A137: W-min C in right_cell(godo,1,Ga)\L~godo by A135,XBOOLE_0:def 4;
    A138: godo/.1 = (go^'pion1)/.1 by GRAPH_2:57
       .= Wmin by A57,GRAPH_2:57;
    A139: len US >= 2 by A16,XXREAL_0:2;
    A140: godo/.2 = (go^'pion1)/.2 by A82,GRAPH_2:61
       .= US/.2 by A31,A70,GRAPH_2:61
       .= (US^'LS)/.2 by A139,GRAPH_2:61
       .= Rotate(Cage(C,n),Wmin)/.2 by JORDAN1E:15;
    A141: L~go \/ L~do is compact by COMPTS_1:19;
    A142: L~go \/ L~do c= L~Cage(C,n) by A132,XBOOLE_1:8;
        Wmin in rng go by A57,FINSEQ_6:46;
    then Wmin in L~go \/ L~do by A59,XBOOLE_0:def 2;
    then A143: W-min (L~go \/ L~do) = Wmin by A141,A142,Th21;
    A144: (W-min (L~go \/ L~do))`1 = W-bound (L~go \/ L~do) & Wmin`1 = Wbo
                                                              by EUCLID:56;
        W-bound LSeg(Gik,Gij) = Gik`1 by A73,SPRECT_1:62;
    then A145: W-bound L~pion1 = Gik`1 by A76,SPPOL_2:21;
        Gik`1 >= Wbo by A7,A131,PSCOMP_1:71;
    then Gik`1 > Wbo by A69,REAL_1:def 5;
    then W-min (L~go\/L~do\/L~pion1) = W-min (L~go \/ L~do)
                                                  by A141,A143,A144,A145,Th33;
    then A146: W-min L~godo = Wmin by A130,A143,XBOOLE_1:4;
    A147: rng godo c= L~godo by A84,SPPOL_2:18;
        2 in dom godo by A84,FINSEQ_3:27;
    then A148: godo/.2 in rng godo by PARTFUN2:4;
        godo/.2 in W-most L~Cage(C,n) by A140,JORDAN1I:27;
    then (godo/.2)`1 = (W-min L~godo)`1 by A146,PSCOMP_1:88
       .= W-bound L~godo by EUCLID:56;
    then godo/.2 in W-most L~godo by A147,A148,SPRECT_2:16;
    then Rotate(godo,W-min L~godo)/.2 in W-most L~godo by A138,A146,FINSEQ_6:95
;
    then reconsider godo as clockwise_oriented non constant standard
                                    special_circular_sequence by JORDAN1I:27;
        len US in dom US by FINSEQ_5:6;
    then A149: US.len US = US/.len US by FINSEQ_4:def 4
       .= Emax by JORDAN1F:7;
    A150: E-max C in E-most C by PSCOMP_1:111;
    A151: east_halfline E-max C misses L~go
    proof
     assume east_halfline E-max C meets L~go;
     then consider p be set such that
      A152: p in east_halfline E-max C and
      A153: p in L~go by XBOOLE_0:3;
     reconsider p as Point of TOP-REAL 2 by A152;
         p in L~US by A44,A153;
     then p in east_halfline E-max C /\ L~Cage(C,n) by A131,A152,XBOOLE_0:def 3
;
     then A154: p`1 = Ebo by A150,JORDAN1A:104;
     then A155: p = Emax by A44,A153,Th46;
     then Emax = Gik by A7,A149,A153,Th43;
     then Gik`1 = Ga*(len Ga,k)`1 by A10,A15,A154,A155,JORDAN1A:92;
     hence contradiction by A1,A12,A28,JORDAN1G:7;
    end;
        now assume east_halfline E-max C meets L~godo;
     then A156: east_halfline E-max C meets (L~go \/ L~pion1) or
               east_halfline E-max C meets L~do by A130,XBOOLE_1:70;
     per cases by A156,XBOOLE_1:70;
      suppose east_halfline E-max C meets L~go;
       hence contradiction by A151;
      end;
      suppose east_halfline E-max C meets L~pion1;
       then consider p be set such that
        A157: p in east_halfline E-max C and
        A158: p in L~pion1 by XBOOLE_0:3;
       reconsider p as Point of TOP-REAL 2 by A157;
       A159: p`1 = Gik`1 by A73,A76,A134,A158,GOBOARD7:5;
           i+1 <= len Ga by A1,NAT_1:13;
       then i+1-1 <= len Ga-1 by XREAL_1:11;
       then A160: i <= len Ga-'1 by BINARITH:def 3;
           len Ga-'1 <= len Ga by BINARITH:52;
       then p`1 <= Ga*(len Ga-'1,1)`1 by A1,A10,A15,A19,A159,A160,JORDAN1A:39;
       then p`1 <= E-bound C by A19,JORDAN8:15;
       then A161: p`1 <= (E-max C)`1 by EUCLID:56;
           p`1 >= (E-max C)`1 by A157,TOPREAL1:def 13;
       then A162: p`1 = (E-max C)`1 by A161,XXREAL_0:1;
           p`2 = (E-max C)`2 by A157,TOPREAL1:def 13;
       then p = E-max C by A162,TOPREAL3:11;
       hence contradiction by A5,A76,A118,A134,A158,XBOOLE_0:3;
      end;
      suppose east_halfline E-max C meets L~do;
       then consider p be set such that
        A163: p in east_halfline E-max C and
        A164: p in L~do by XBOOLE_0:3;
       reconsider p as Point of TOP-REAL 2 by A163;
           p in L~LS by A51,A164;
       then p in east_halfline E-max C /\ L~Cage(C,n) by A131,A163,
XBOOLE_0:def 3;
       then A165: p`1 = Ebo by A150,JORDAN1A:104;
       A166: (E-max C)`2 = p`2 by A163,TOPREAL1:def 13;
       set RC = Rotate(Cage(C,n),Emax);
       A167: E-max C in right_cell(RC,1) by JORDAN1I:9;
       A168: 1+1 <= len LS by A22,XXREAL_0:2;
           LS = RC-:Wmin by JORDAN1G:26;
       then A169: LSeg(LS,1) = LSeg(RC,1) by A168,SPPOL_2:9;
       A170: L~RC = L~Cage(C,n) by REVROT_1:33;
       A171: len RC = len Cage(C,n) by REVROT_1:14;
       A172: GoB RC = GoB Cage(C,n) by REVROT_1:28
          .= Ga by JORDAN1H:52;
       A173: Emax in rng Cage(C,n) by SPRECT_2:50;
       A174: RC is_sequence_on Ga by A127,REVROT_1:34;
       A175: RC/.1 = E-max L~RC by A170,A173,FINSEQ_6:98;
       then consider ii,jj be Element of NAT such that
        A176: [ii,jj+1] in Indices Ga and
        A177: [ii,jj] in Indices Ga and
        A178: RC/.1 = Ga*(ii,jj+1) and
        A179: RC/.(1+1) = Ga*(ii,jj) by A80,A171,A174,JORDAN1I:25;
       consider jj2 be Element of NAT such that
        A180: 1 <= jj2 & jj2 <= width Ga and
        A181: Emax = Ga*(len Ga,jj2) by JORDAN1D:29;
       A182: len Ga >= 4 by JORDAN8:13;
       then len Ga >= 1 by XXREAL_0:2;
       then [len Ga,jj2] in Indices Ga by A180,GOBOARD7:10;
       then A183: ii = len Ga by A170,A175,A176,A178,A181,GOBOARD1:21;
       A184: 1 <= ii & ii <= len Ga & 1 <= jj+1 & jj+1 <= width Ga
                                                           by A176,GOBOARD5:1;
       A185: 1 <= ii & ii <= len Ga & 1 <= jj & jj <= width Ga
                                                           by A177,GOBOARD5:1;
       A186: ii+1 <> ii;
       jj+1+1 <> jj;
       then A187: right_cell(RC,1) = cell(Ga,ii-'1,jj)
                          by A80,A171,A172,A176,A177,A178,A179,A186,
GOBOARD5:def 6;
       A188: ii-'1+1 = ii by A184,BINARITH:53;
       ii-1 >= 4-1 by A182,A183,XREAL_1:11;
       then A189: ii-1 >= 1 by XXREAL_0:2;
       A190: 1 <= ii-'1 by A189,BINARITH:def 3;
       A191: Ga*(ii-'1,jj)`1 <= (E-max C)`1 & (E-max C)`1 <= Ga*(ii-'1+1,
jj)`1 &
            Ga*(ii-'1,jj)`2 <= (E-max C)`2 & (E-max C)`2 <= Ga*(ii-'1,jj+1)`2
                                      by A167,A184,A185,A187,A188,A189,
JORDAN9:19;
       A192: ii-'1 < len Ga by A184,A188,NAT_1:13;
       then A193: Ga*(ii-'1,jj)`2 = Ga*(1,jj)`2 by A185,A190,GOBOARD5:2
          .= Ga*(ii,jj)`2 by A185,GOBOARD5:2;
       A194: Ga*(ii-'1,jj+1)`2 = Ga*(1,jj+1)`2 by A184,A190,A192,GOBOARD5:2
          .= Ga*(ii,jj+1)`2 by A184,GOBOARD5:2;
           Ga*(len Ga,jj)`1 = Ebo & Ebo = Ga*(len Ga,jj+1)`1
                                                by A15,A184,A185,JORDAN1A:92;
       then p in LSeg(RC/.1,RC/.(1+1))
                              by A165,A166,A178,A179,A183,A191,A193,A194,
GOBOARD7:8;
       then A195: p in LSeg(LS,1) by A80,A169,A171,TOPREAL1:def 5;
       A196: p in LSeg(do,Index(p,do)) by A164,JORDAN3:42;
       A197: do = mid(LS,Gij..LS,len LS) by A35,Th37;
       A198: 1<=Gij..LS & Gij..LS<=len LS by A35,FINSEQ_4:31;
           Gij..LS <> len LS by A27,A35,FINSEQ_4:29;
       then A199: Gij..LS < len LS by A198,REAL_1:def 5;
       A200: 1<=Index(p,do) & Index(p,do) < len do by A164,JORDAN3:41;
       A201: Index(Gij,LS)+1 = Gij..LS by A30,A35,Th56;
       consider t be Element of NAT such that
        A202: t in dom LS and
        A203: LS.t = Gij by A35,FINSEQ_2:11;
       A204: 1 <= t & t <= len LS by A202,FINSEQ_3:27;
       then 1 < t by A30,A203,REAL_1:def 5;
       then Index(Gij,LS)+1 = t by A203,A204,JORDAN3:45;
       then A205: len L_Cut(LS,Gij) = len LS-Index(Gij,LS)
                                                  by A6,A203,JORDAN3:61;
       set tt = Index(p,do)+(Gij..LS)-'1;
       A206: 1<=Index(Gij,LS) & 0+Index(Gij,LS) < len LS by A6,JORDAN3:41;
       then A207: len LS-Index(Gij,LS) > 0 by XREAL_1:22;
       Index(p,do) < len LS-'Index(Gij,LS) by A200,A205,BINARITH:def 3;
       then Index(p,do)+1 <= len LS-'Index(Gij,LS) by NAT_1:13;
       then Index(p,do) <= len LS-'Index(Gij,LS)-1 by XREAL_1:21;
       then Index(p,do) <= len LS-Index(Gij,LS)-1 by A207,BINARITH:def 3;
       then Index(p,do) <= len LS-Gij..LS by A201;
       then Index(p,do) <= len LS-'Gij..LS by BINARITH:def 3;
       then Index(p,do) < len LS-'(Gij..LS)+1 by NAT_1:13;
       then A208: LSeg(mid(LS,Gij..LS,len LS),Index(p,do)) =
            LSeg(LS,Index(p,do)+(Gij..LS)-'1) by A198,A199,A200,JORDAN4:31;
       A209: 1+1 <= Gij..LS by A201,A206,XREAL_1:9;
       then Index(p,do)+Gij..LS >= 1+1+1 by A200,XREAL_1:9;
       then A210: Index(p,do)+Gij..LS-1 >= 1+1+1-1 by XREAL_1:11;
       then A211: Index(p,do)+Gij..LS-1 >= 0;
       A212: tt >= 1+1 by A210,BINARITH:def 3;
       A213: 2 in dom LS by A168,FINSEQ_3:27;
           now per cases by A212,REAL_1:def 5;
        suppose tt > 1+1;
         then LSeg(LS,1) misses LSeg(LS,tt) by TOPREAL1:def 9;
         hence contradiction by A195,A196,A197,A208,XBOOLE_0:3;
        end;
        suppose A214: tt = 1+1;
         then LSeg(LS,1) /\ LSeg(LS,tt) = {LS/.2} by A22,TOPREAL1:def 8;
         then p in {LS/.2} by A195,A196,A197,A208,XBOOLE_0:def 3;
         then A215: p = LS/.2 by TARSKI:def 1;
         then A216: p..LS = 2 by A213,FINSEQ_5:44;
             1+1 = Index(p,do)+(Gij..LS)-1 by A211,A214,BINARITH:def 3;
         then 1+1+1 = Index(p,do)+(Gij..LS);
         then A217: Gij..LS = 2 by A200,A209,JORDAN1E:10;
             p in rng LS by A213,A215,PARTFUN2:4;
         then p = Gij by A35,A216,A217,FINSEQ_5:10;
         then Gij`1 = Ebo by A215,JORDAN1G:40;
         then Gij`1 = Ga*(len Ga,j)`1 by A9,A15,JORDAN1A:92;
         hence contradiction by A1,A11,A62,JORDAN1G:7;
       end;
       end;
       hence contradiction;
    end;
    end;
    then east_halfline E-max C c= (L~godo)` by SUBSET_1:43;
    then consider W be Subset of TOP-REAL 2 such that
     A218: W is_a_component_of (L~godo)` and
     A219: east_halfline E-max C c= W by GOBOARD9:5;
        east_halfline E-max C is not Bounded by JORDAN2C:129;
    then W is not Bounded by A219,JORDAN2C:16;
    then W is_outside_component_of L~godo by A218,JORDAN2C:def 4;
    then W c= UBD L~godo by JORDAN2C:27;
    then A220: east_halfline E-max C c= UBD L~godo by A219,XBOOLE_1:1;
        E-max C in east_halfline E-max C by TOPREAL1:45;
    then E-max C in UBD L~godo by A220;
    then E-max C in LeftComp godo by GOBRD14:46;
    then UA meets L~godo by A117,A118,A129,A137,Th36;
    then A221: UA meets (L~go \/ L~pion1) or UA meets L~do by A130,XBOOLE_1:70;
    A222: UA c= C by JORDAN6:76;
    per cases by A221,XBOOLE_1:70;
     suppose UA meets L~go;
      then UA meets L~Cage(C,n) by A132,XBOOLE_1:63;
      then C meets L~Cage(C,n) by A222,XBOOLE_1:63;
      hence contradiction by JORDAN10:5;
     end;
     suppose UA meets L~pion1;
      hence contradiction by A5,A76,A134;
     end;
     suppose UA meets L~do;
      then UA meets L~Cage(C,n) by A132,XBOOLE_1:63;
      then C meets L~Cage(C,n) by A222,XBOOLE_1:63;
      hence contradiction by JORDAN10:5;
   end;
   end;

  theorem Th60:
   for C be Simple_closed_curve
   for i,j,k be Element of NAT st
    1 < i & i < len Gauge(C,n) &
    1 <= j & j <= k & k <= width Gauge(C,n) & n > 0 &
    LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Upper_Arc L~Cage(C,n) =
     {Gauge(C,n)*(i,k)} &
    LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Lower_Arc L~Cage(C,n) =
     {Gauge(C,n)*(i,j)} holds
     LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Lower_Arc C
   proof
    let C be Simple_closed_curve;
    let i,j,k be Element of NAT;
    assume that
     A1: 1 < i & i < len Gauge(C,n) and
     A2: 1 <= j & j <= k & k <= width Gauge(C,n) and
     A3: n > 0 and
     A4: LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Upper_Arc L~Cage(C,n) =
      {Gauge(C,n)*(i,k)} and
     A5: LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Lower_Arc L~Cage(C,n) =
      {Gauge(C,n)*(i,j)};
    A6: L~Upper_Seq(C,n) = Upper_Arc L~Cage(C,n) by A3,JORDAN1G:63;
        L~Lower_Seq(C,n) = Lower_Arc L~Cage(C,n) by A3,JORDAN1G:64;
    hence LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Lower_Arc C
                                        by A1,A2,A4,A5,A6,Th58;
   end;

  theorem Th61:
   for C be Simple_closed_curve
   for i,j,k be Element of NAT st
    1 < i & i < len Gauge(C,n) &
    1 <= j & j <= k & k <= width Gauge(C,n) & n > 0 &
    LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Upper_Arc L~Cage(C,n) =
     {Gauge(C,n)*(i,k)} &
    LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Lower_Arc L~Cage(C,n) =
     {Gauge(C,n)*(i,j)} holds
      LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Upper_Arc C
   proof
    let C be Simple_closed_curve;
    let i,j,k be Element of NAT;
    assume that
     A1: 1 < i & i < len Gauge(C,n) and
     A2: 1 <= j & j <= k & k <= width Gauge(C,n) and
     A3: n > 0 and
     A4: LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Upper_Arc L~Cage(C,n) =
      {Gauge(C,n)*(i,k)} and
     A5: LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Lower_Arc L~Cage(C,n) =
      {Gauge(C,n)*(i,j)};
    A6: L~Upper_Seq(C,n) = Upper_Arc L~Cage(C,n) by A3,JORDAN1G:63;
        L~Lower_Seq(C,n) = Lower_Arc L~Cage(C,n) by A3,JORDAN1G:64;
    hence LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Upper_Arc C
                                        by A1,A2,A4,A5,A6,Th59;
   end;

  theorem
    for C be compact connected non vertical non horizontal Subset of
 TOP-REAL 2
   for j be Element of NAT holds
    Gauge(C,n+1)*(Center Gauge(C,n+1),j) in Upper_Arc L~Cage(C,n+1) &
    1 <= j & j <= width Gauge(C,n+1) implies
    LSeg(Gauge(C,1)*(Center Gauge(C,1),1),Gauge(C,n+1)*(Center Gauge(C,n+1),j))
     meets Lower_Arc L~Cage(C,n+1)
   proof
    let C be compact connected non vertical non horizontal
     Subset of TOP-REAL 2;
    let j be Element of NAT;
    assume that
     A1: Gauge(C,n+1)*(Center Gauge(C,n+1),j) in Upper_Arc L~Cage(C,n+1) and
     A2: 1 <= j & j <= width Gauge(C,n+1);
    set in1 = Center Gauge(C,n+1);
    A3: n+1 >= 0+1 by NAT_1:11;
    A4: 1 <= in1 by JORDAN1B:12;
    A5: in1 <= len Gauge(C,n+1) by JORDAN1B:14;
    A6: LSeg(Gauge(C,n+1)*(Center Gauge(C,n+1),1),
      Gauge(C,n+1)*(Center Gauge(C,n+1),j)) c=
     LSeg(Gauge(C,1)*(Center Gauge(C,1),1),
      Gauge(C,n+1)*(Center Gauge(C,n+1),j)) by A2,A3,JORDAN1A:66;
        Upper_Arc L~Cage(C,n+1) c= L~Cage(C,n+1) by JORDAN6:76;
    then LSeg(Gauge(C,n+1)*(in1,1),Gauge(C,n+1)*(in1,j))
     meets Lower_Arc L~Cage(C,n+1) by A1,A2,A4,A5,JORDAN1G:65;
    hence thesis by A6,XBOOLE_1:63;
   end;

  theorem
       for C be Simple_closed_curve
   for j,k be Element of NAT holds
    1 <= j & j <= k & k <= width Gauge(C,n+1) &
    LSeg(Gauge(C,n+1)*(Center Gauge(C,n+1),j),
         Gauge(C,n+1)*(Center Gauge(C,n+1),k)) /\ Upper_Arc L~Cage(C,n+1) =
     {Gauge(C,n+1)*(Center Gauge(C,n+1),k)} &
    LSeg(Gauge(C,n+1)*(Center Gauge(C,n+1),j),
         Gauge(C,n+1)*(Center Gauge(C,n+1),k)) /\ Lower_Arc L~Cage(C,n+1) =
     {Gauge(C,n+1)*(Center Gauge(C,n+1),j)} implies
     LSeg(Gauge(C,n+1)*(Center Gauge(C,n+1),j),
          Gauge(C,n+1)*(Center Gauge(C,n+1),k)) meets Lower_Arc C
   proof
    let C be Simple_closed_curve;
    let j,k be Element of NAT;
    assume that
     A1: 1 <= j & j <= k & k <= width Gauge(C,n+1) and
     A2: LSeg(Gauge(C,n+1)*(Center Gauge(C,n+1),j),
        Gauge(C,n+1)*(Center Gauge(C,n+1),k)) /\ Upper_Arc L~Cage(C,n+1) =
      {Gauge(C,n+1)*(Center Gauge(C,n+1),k)} and
     A3: LSeg(Gauge(C,n+1)*(Center Gauge(C,n+1),j),
        Gauge(C,n+1)*(Center Gauge(C,n+1),k)) /\ Lower_Arc L~Cage(C,n+1) =
      {Gauge(C,n+1)*(Center Gauge(C,n+1),j)};
    A4: len Gauge(C,n+1) >= 4 by JORDAN8:13;
    then A5: len Gauge(C,n+1) >= 3 by XXREAL_0:2;
        len Gauge(C,n+1) >= 2 by A4,XXREAL_0:2;
    then A6: 1 < Center Gauge(C,n+1) by JORDAN1B:15;
        Center Gauge(C,n+1) < len Gauge(C,n+1) by A5,JORDAN1B:16;
    hence LSeg(Gauge(C,n+1)*(Center Gauge(C,n+1),j),
          Gauge(C,n+1)*(Center Gauge(C,n+1),k)) meets Lower_Arc C
                                                   by A1,A2,A3,A6,Th60;
   end;

  theorem
   for C be Simple_closed_curve
   for j,k be Element of NAT holds
    1 <= j & j <= k & k <= width Gauge(C,n+1) &
    LSeg(Gauge(C,n+1)*(Center Gauge(C,n+1),j),
         Gauge(C,n+1)*(Center Gauge(C,n+1),k)) /\ Upper_Arc L~Cage(C,n+1) =
     {Gauge(C,n+1)*(Center Gauge(C,n+1),k)} &
    LSeg(Gauge(C,n+1)*(Center Gauge(C,n+1),j),
         Gauge(C,n+1)*(Center Gauge(C,n+1),k)) /\ Lower_Arc L~Cage(C,n+1) =
     {Gauge(C,n+1)*(Center Gauge(C,n+1),j)} implies
     LSeg(Gauge(C,n+1)*(Center Gauge(C,n+1),j),
          Gauge(C,n+1)*(Center Gauge(C,n+1),k)) meets Upper_Arc C
   proof
    let C be Simple_closed_curve;
    let j,k be Element of NAT;
    assume that
     A1: 1 <= j & j <= k & k <= width Gauge(C,n+1) and
     A2: LSeg(Gauge(C,n+1)*(Center Gauge(C,n+1),j),
        Gauge(C,n+1)*(Center Gauge(C,n+1),k)) /\ Upper_Arc L~Cage(C,n+1) =
      {Gauge(C,n+1)*(Center Gauge(C,n+1),k)} and
     A3: LSeg(Gauge(C,n+1)*(Center Gauge(C,n+1),j),
        Gauge(C,n+1)*(Center Gauge(C,n+1),k)) /\ Lower_Arc L~Cage(C,n+1) =
      {Gauge(C,n+1)*(Center Gauge(C,n+1),j)};
    A4: len Gauge(C,n+1) >= 4 by JORDAN8:13;
    then A5: len Gauge(C,n+1) >= 3 by XXREAL_0:2;
        len Gauge(C,n+1) >= 2 by A4,XXREAL_0:2;
    then A6: 1 < Center Gauge(C,n+1) by JORDAN1B:15;
        Center Gauge(C,n+1) < len Gauge(C,n+1) by A5,JORDAN1B:16;
    hence LSeg(Gauge(C,n+1)*(Center Gauge(C,n+1),j),
          Gauge(C,n+1)*(Center Gauge(C,n+1),k)) meets Upper_Arc C
                                                   by A1,A2,A3,A6,Th61;
   end;

:: Moved from JORDAN, AG 20.01.2006

theorem
  for X, Y being non empty compact Subset of TOP-REAL 2 st
  X c= Y & (W-min Y in X or W-max Y in X) holds W-bound X = W-bound Y
  proof
    let X, Y be non empty compact Subset of TOP-REAL 2;
    assume
A1: X c= Y & (W-min Y in X or W-max Y in X);
    (W-min X)`1 = W-bound X & (W-max X)`1 = W-bound X &
    (W-min Y)`1 = W-bound Y & (W-max Y)`1 = W-bound Y
    by EUCLID:56;
    hence thesis by A1,Th21,Th22;
  end;

theorem
  for X, Y being non empty compact Subset of TOP-REAL 2 st
  X c= Y & (E-min Y in X or E-max Y in X) holds E-bound X = E-bound Y
  proof
    let X, Y be non empty compact Subset of TOP-REAL 2;
    assume
A1: X c= Y & (E-min Y in X or E-max Y in X);
    (E-min X)`1 = E-bound X & (E-max X)`1 = E-bound X &
    (E-min Y)`1 = E-bound Y & (E-max Y)`1 = E-bound Y
    by EUCLID:56;
    hence thesis by A1,Th17,Th18;
  end;

theorem
  for X, Y being non empty compact Subset of TOP-REAL 2 st
  X c= Y & (N-min Y in X or N-max Y in X) holds N-bound X = N-bound Y
  proof
    let X, Y be non empty compact Subset of TOP-REAL 2;
    assume
A1: X c= Y & (N-min Y in X or N-max Y in X);
    (N-min X)`2 = N-bound X & (N-max X)`2 = N-bound X &
    (N-min Y)`2 = N-bound Y & (N-max Y)`2 = N-bound Y
    by EUCLID:56;
    hence thesis by A1,Th15,Th16;
  end;

theorem
  for X, Y being non empty compact Subset of TOP-REAL 2 st
  X c= Y & (S-min Y in X or S-max Y in X) holds S-bound X = S-bound Y
  proof
    let X, Y be non empty compact Subset of TOP-REAL 2;
    assume
A1: X c= Y & (S-min Y in X or S-max Y in X);
    (S-min X)`2 = S-bound X & (S-max X)`2 = S-bound X &
    (S-min Y)`2 = S-bound Y & (S-max Y)`2 = S-bound Y
    by EUCLID:56;
    hence thesis by A1,Th19,Th20;
  end;
