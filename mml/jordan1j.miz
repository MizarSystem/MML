:: Upper and Lower Sequence on the Cage, Upper and Lower Arcs
::  by Robert Milewski
::
:: Received April 5, 2002
:: Copyright (c) 2002 Association of Mizar Users

environ

 vocabularies JORDAN8, PSCOMP_1, BOOLE, JORDAN1A, JORDAN9, GOBOARD1, GOBOARD5,
      JORDAN6, SPRECT_2, JORDAN3, FINSEQ_5, JORDAN2C, SPPOL_2, GROUP_2,
      RFINSEQ, REALSET1, JORDAN1E, SQUARE_1, FINSEQ_4, TOPREAL2, CONNSP_1,
      COMPTS_1, TOPREAL1, SPPOL_1, FINSEQ_1, TREES_1, EUCLID, RELAT_1, MCART_1,
      MATRIX_1, GOBOARD9, PRE_TOPC, RELAT_2, SEQM_3, SUBSET_1, FUNCT_1,
      ARYTM_1, FINSEQ_6, CARD_1, GOBOARD2, GROUP_1, GRAPH_2, ARYTM;
 notations TARSKI, XBOOLE_0, SUBSET_1, CARD_1, NUMBERS, XCMPLX_0, XXREAL_0,
      NAT_1, NAT_D, FUNCT_1, PARTFUN1, FINSEQ_1, FINSEQ_4, FINSEQ_5, RFINSEQ,
      MATRIX_1, FINSEQ_6, GRAPH_2, REALSET1, STRUCT_0, PRE_TOPC, TOPREAL2,
      NEWTON, BINARITH, CONNSP_1, COMPTS_1,
      RLTOPSP1, EUCLID, PSCOMP_1, SPRECT_2,
      GOBOARD1, TOPREAL1, GOBOARD2, GOBOARD5, GOBOARD9, GOBRD13, SPPOL_1,
      SPPOL_2, JORDAN3, JORDAN8, JORDAN2C, JORDAN6, JORDAN9, JORDAN1A,
      JORDAN1E;
 constructors PARTFUN1, REAL_1, SQUARE_1, NAT_1, FINSEQ_4, NEWTON, REALSET1,
      RFINSEQ, BINARITH, TOPS_1, CONNSP_1, MATRIX_2, GOBOARD2, JORDAN1,
      PSCOMP_1, GRAPH_2, GOBOARD9, JORDAN3, JORDAN5C, JORDAN6, JORDAN2C,
      JORDAN8, GOBRD13, JORDAN9, JORDAN1A, JORDAN1E, NAT_D, RELSET_1,
      CONVEX1;
 registrations XBOOLE_0, ORDINAL1, RELSET_1, XXREAL_0, XREAL_0, NAT_1,
      MEMBERED, FINSEQ_1, REALSET1, FINSEQ_5, FINSEQ_6, STRUCT_0, PRE_TOPC,
      TOPREAL1, TOPREAL2, GOBOARD2, JORDAN1, SPPOL_2, GOBRD11, TOPREAL5,
      SPRECT_1, SPRECT_2, REVROT_1, TOPREAL6, JORDAN8, JORDAN1E, JORDAN1G,
      CARD_1, FUNCT_1, EUCLID;
 requirements NUMERALS, BOOLE, SUBSET, REAL, ARITHM;
 definitions TARSKI, GOBOARD5, XBOOLE_0, PSCOMP_1;
 theorems JORDAN8, PSCOMP_1, JORDAN1A, NAT_1, GOBOARD5, FINSEQ_1, FINSEQ_2,
      JORDAN4, JORDAN6, SPRECT_2, FINSEQ_4, FINSEQ_5, FINSEQ_6, GOBOARD7,
      GOBOARD9, SPPOL_1, SPPOL_2, REVROT_1, TOPREAL1, SPRECT_3, JORDAN5B,
      JORDAN3, GOBRD13, INT_1, JORDAN9, JORDAN2C, SUBSET_1, CONNSP_1, ZFMISC_1,
      JGRAPH_1, GOBOARD1, SPRECT_1, TARSKI, TOPREAL3, FINSEQ_3, RELAT_1,
      COMPTS_1, FUNCT_1, TOPREAL5, JORDAN10, GRAPH_2, GOBOARD2, CARD_1, CARD_2,
      PRE_CIRC, ENUMSET1, SPRECT_4, JORDAN1B, SPRECT_5, JORDAN1E, PARTFUN2,
      JORDAN1F, TDLAT_1, JORDAN1G, GOBOARD3, JORDAN1H, TOPREAL8, GOBRD14,
      JORDAN1D, JORDAN1I, XBOOLE_0, XBOOLE_1, REALSET1, XREAL_1, XXREAL_0,
      EUCLID, PRE_TOPC, ORDINAL1, PARTFUN1, MATRIX_1, XREAL_0, NAT_D, RLTOPSP1;
 schemes NAT_1;

begin

reserve n for Element of NAT;

theorem Th1:
  for G be Go-board for i1,i2,j1,j2 be Element of NAT st
  1 <= j1 & j1 <= width G & 1 <= j2 & j2 <= width G &
  1 <= i1 & i1 < i2 & i2 <= len G holds G*(i1,j1)`1 < G*(i2,j2)`1
proof
  let G be Go-board;
  let i1,i2,j1,j2 be Element of NAT;
  assume that
A1: 1 <= j1 and
A2: j1 <= width G and
A3: 1 <= j2 and
A4: j2 <= width G and
A5: 1 <= i1 and
A6: i1 < i2 and
A7: i2 <= len G;
A8: 1 <= i2 by A5,A6,XXREAL_0:2;
  then G*(i2,j1)`1 = G*(i2,1)`1 by A1,A2,A7,GOBOARD5:3
    .= G*(i2,j2)`1 by A3,A4,A7,A8,GOBOARD5:3;
  hence thesis by A1,A2,A5,A6,A7,GOBOARD5:4;
end;

theorem Th2:
  for G be Go-board for i1,i2,j1,j2 be Element of NAT st
  1 <= i1 & i1 <= len G & 1 <= i2 & i2 <= len G &
  1 <= j1 & j1 < j2 & j2 <= width G holds G*(i1,j1)`2 < G*(i2,j2)`2
proof
  let G be Go-board;
  let i1,i2,j1,j2 be Element of NAT;
  assume that
A1: 1 <= i1 and
A2: i1 <= len G and
A3: 1 <= i2 and
A4: i2 <= len G and
A5: 1 <= j1 and
A6: j1 < j2 and
A7: j2 <= width G;
A8: 1 <= j2 by A5,A6,XXREAL_0:2;
  then G*(i1,j2)`2 = G*(1,j2)`2 by A1,A2,A7,GOBOARD5:2
    .= G*(i2,j2)`2 by A3,A4,A7,A8,GOBOARD5:2;
  hence thesis by A1,A2,A5,A6,A7,GOBOARD5:5;
end;

registration
  let f be non empty FinSequence;
  let g be FinSequence;
  cluster f^'g -> non empty;
  coherence
  proof
    f^'g = f^(2,len g)-cut g by GRAPH_2:def 2;
    hence thesis;
  end;
end;

theorem Th3:
  for C be compact connected non vertical non horizontal Subset of TOP-REAL 2
  for n be Element of NAT holds
  L~(Cage(C,n)-:E-max L~Cage(C,n)) /\ L~(Cage(C,n):-E-max L~Cage(C,n)) =
  {N-min L~Cage(C,n),E-max L~Cage(C,n)}
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
  let n be Element of NAT;
  set US = Cage(C,n)-:E-max L~Cage(C,n);
  set LS = Cage(C,n):-E-max L~Cage(C,n);
  set f=Cage(C,n);
  set pW=E-max L~Cage(C,n);
  set pN=N-min L~Cage(C,n);
A1: pW in rng Cage(C,n) by SPRECT_2:50;
A2: pW..Cage(C,n) <= pW..Cage(C,n);
  (f:-pW)/.len(f:-pW) = f/.len f by A1,FINSEQ_5:57
    .= f/.1 by FINSEQ_6:def 1
    .= pN by JORDAN9:34;
  then
A3: pN in rng (Cage(C,n):-pW) by REVROT_1:3;
A4: Cage(C,n)-:pW <> {} by A1,FINSEQ_5:50;
  (Cage(C,n):-pW)/.1 = pW by FINSEQ_5:56;
  then
A5: E-max L~Cage(C,n) in rng (Cage(C,n):-E-max L~Cage(C,n)) by FINSEQ_6:46;
  (Cage(C,n)-:pW)/.1 = Cage(C,n)/.1 by A1,FINSEQ_5:47
    .= pN by JORDAN9:34;
  then
A6: N-min L~Cage(C,n) in rng (Cage(C,n)-:E-max L~Cage(C,n)) by A4,FINSEQ_6:46;
  len(f-:pW) = pW..f by A1,FINSEQ_5:45;
  then (f-:pW)/.len (f-:pW) = pW by A1,FINSEQ_5:48;
  then
A7: pW in rng (Cage(C,n)-:pW) by A4,REVROT_1:3;
  N-max L~f in L~f & pW`1 = E-bound L~f by EUCLID:56,SPRECT_1:13;
  then (N-max L~f)`1 <= pW`1 by PSCOMP_1:71;
  then
A8: pN <> pW by SPRECT_2:55;
  then
A9: card {pN,pW} = 2 by CARD_2:76;
  {pN,pW} c= rng LS
  proof
    let x be set;
    assume x in {pN,pW};
    hence thesis by A3,A5,TARSKI:def 2;
  end;
  then
A10: card {pN,pW} c= card rng LS by CARD_1:27;
  card rng LS c= card dom LS by CARD_2:80;
  then card rng LS c= len LS by CARD_1:104;
  then 2 c= len LS by A9,A10,XBOOLE_1:1;
  then len LS >= 2 by NAT_1:40;
  then
A11: rng LS c= L~LS by SPPOL_2:18;
A12: E-max L~Cage(C,n) in rng LS by FINSEQ_6:66;
A13: card {pN,pW} = 2 by A8,CARD_2:76;
  {pN,pW} c= rng US
  proof
    let x be set;
    assume x in {pN,pW};
    hence thesis by A6,A7,TARSKI:def 2;
  end;
  then
A14: card {pN,pW} c= card rng US by CARD_1:27;
  card rng US c= card dom US by CARD_2:80;
  then card rng US c= len US by CARD_1:104;
  then
A15: 2 c= len US by A13,A14,XBOOLE_1:1;
  then
A16: len US >= 2 by NAT_1:40;
  then
A17: rng US c= L~US by SPPOL_2:18;
  len US <> 0 by A15,NAT_1:40;
  then
A18: US is non empty;
A19: E-max L~Cage(C,n) in rng US by A1,A2,FINSEQ_5:49;
  US/.1 = Cage(C,n)/.1 by A1,FINSEQ_5:47
    .= N-min L~Cage(C,n) by JORDAN9:34;
  then
A20: N-min L~Cage(C,n) in rng US by A18,FINSEQ_6:46;
  LS/.(len LS) = Cage(C,n)/.len Cage(C,n) by A1,FINSEQ_5:57
    .= Cage(C,n)/.1 by FINSEQ_6:def 1
    .= N-min L~Cage(C,n) by JORDAN9:34;
  then
A21: N-min L~Cage(C,n) in rng LS by REVROT_1:3;
  thus L~US /\ L~LS c= {N-min L~Cage(C,n),E-max L~Cage(C,n)}
  proof
    let x be set;
    assume
A22: x in L~US /\ L~LS;
    then
A23: x in L~US & x in L~LS by XBOOLE_0:def 4;
    reconsider x1=x as Point of TOP-REAL 2 by A22;
    consider i1 be Element of NAT such that
A24: 1 <= i1 & i1+1 <= len US and
A25: x1 in LSeg(US,i1) by A23,SPPOL_2:13;
    consider i2 be Element of NAT such that
A26: 1 <= i2 & i2+1 <= len LS and
A27: x1 in LSeg(LS,i2) by A23,SPPOL_2:13;
A28: LSeg(US,i1) = LSeg(f,i1) by A24,SPPOL_2:9;
    set i3=i2-'1;
A29: i3+1 = i2 by A26,XREAL_1:237;
    then
A30: LSeg(LS,i2) = LSeg(f,i3+pW..f) by A1,SPPOL_2:10;
A31: len US = pW..f by A1,FINSEQ_5:45;
A32: len LS = len f-pW..f+1 by A1,FINSEQ_5:53;
A33: i2-1 >= 1-1 by A26,XREAL_1:11;
    i2 < len f-pW..f+1 by A26,A32,NAT_1:13;
    then i2-1 < len f-pW..f by XREAL_1:21;
    then i2-1+pW..f < len f by XREAL_1:22;
    then
A34: i3+pW..f < len f by A33,XREAL_0:def 2;
A35: pW..f >= 1 by A1,FINSEQ_4:31;
    i3+1 < len f-pW..f+1 by A26,A29,A32,NAT_1:13;
    then i3 < len f-pW..f by XREAL_1:9;
    then
A36: i3+pW..f < len f by XREAL_1:22;
A37: i1+1 < pW..f+1 by A24,A31,NAT_1:13;
    1+pW..f <= i3+1+pW..f by A26,A29,XREAL_1:9;
    then i1+1 < i3+pW..f+1 by A37,XXREAL_0:2;
    then
A38: i1+1 <= i3+pW..f by NAT_1:13;
A39: i3+pW..f+1 <= len f by A36,NAT_1:13;
A40: pW..f-'1+1 = pW..f by A35,XREAL_1:237;
    assume
A41: not x in {N-min L~Cage(C,n),E-max L~Cage(C,n)};
    now per cases by A24,A38,XXREAL_0:1;
      suppose i1+1 < i3+pW..f & i1 > 1;
        then LSeg(f,i1) misses LSeg(f,i3+pW..f) by A36,GOBOARD5:def 4;
        then LSeg(f,i1) /\ LSeg(f,i3+pW..f) = {} by XBOOLE_0:def 7;
        hence contradiction by A25,A27,A28,A30,XBOOLE_0:def 4;
      end;
      suppose
A42:    i1 = 1;
A43:    i3+pW..f >= 0+2 by A16,A31,XREAL_1:9;
        now per cases by A43,XXREAL_0:1;
          suppose i3+pW..f > 2;
            then
A44:        i1+1 < i3+pW..f by A42;
            now per cases by A39,XXREAL_0:1;
              suppose i3+pW..f+1 < len f;
                then LSeg(f,i1) misses LSeg(f,i3+pW..f) by A44,GOBOARD5:def 4;
                then LSeg(f,i1) /\ LSeg(f,i3+pW..f) = {} by XBOOLE_0:def 7;
                hence contradiction by A25,A27,A28,A30,XBOOLE_0:def 4;
              end;
              suppose i3+pW..f+1 = len f;
                then i3+pW..f = len f-1;
                then i3+pW..f = len f-'1 by XREAL_0:def 2;

then LSeg(f,i1) /\ LSeg(f,i3+pW..f) = {f/.1} by A42,GOBOARD7:36,REVROT_1:30;
                then x1 in {f/.1} by A25,A27,A28,A30,XBOOLE_0:def 4;
                then x1 = f/.1 by TARSKI:def 1
                  .= pN by JORDAN9:34;
                hence contradiction by A41,TARSKI:def 2;
              end;
            end;
            hence contradiction;
          end;
          suppose
A45:        i3+pW..f = 2;
            then
A46:        x1 in LSeg(f,1) /\ LSeg(f,1+1)
            by A25,A27,A28,A30,A42,XBOOLE_0:def 4;
            1 + 2 <= len f by GOBOARD7:36,XXREAL_0:2;
            then x1 in {f/.(1+1)} by A46,TOPREAL1:def 8;
            then
A47:        x1 = f/.(1+1) by TARSKI:def 1;
A48:        0+1 in dom LS by FINSEQ_5:6;
            0+pW..f >= i3+pW..f by A15,A31,A45,NAT_1:40;
            then i3 <= 0 by XREAL_1:8;
            then i3 = 0;
            then LS/.1 = x1 by A1,A45,A47,A48,FINSEQ_5:55;
            then x1 = pW by FINSEQ_5:56;
            hence contradiction by A41,TARSKI:def 2;
          end;
        end;
        hence contradiction;
      end;
      suppose
A49:    i1+1 = i3+pW..f;
        0+pW..f <= i3+pW..f by XREAL_1:9;
        then
A50:    pW..f = i1+1 by A24,A31,A49,XXREAL_0:1;
        i3+pW..f >= pW..f by NAT_1:11;
        then pW..f < len f by A34,XXREAL_0:2;
        then pW..f+1 <= len f by NAT_1:13;
        then pW..f-'1 + (1+1) <= len f by A40;
        then LSeg(f,i1) /\ LSeg(f,i3+(pW..f)) = {f/.(pW..f)}
        by A24,A40,A49,A50,TOPREAL1:def 8;
        then x1 in {f/.(pW..f)} by A25,A27,A28,A30,XBOOLE_0:def 4;
        then x1 = f/.(pW..f) by TARSKI:def 1
          .= pW by A1,FINSEQ_5:41;
        hence contradiction by A41,TARSKI:def 2;
      end;
    end;
    hence contradiction;
  end;
  thus {N-min L~Cage(C,n),E-max L~Cage(C,n)} c= L~US /\ L~LS
  proof
    let x be set;
    assume
A51: x in {N-min L~Cage(C,n),E-max L~Cage(C,n)};
    per cases by A51,TARSKI:def 2;
    suppose x = N-min L~Cage(C,n);
      hence thesis by A11,A17,A20,A21,XBOOLE_0:def 4;
    end;
    suppose x = E-max L~Cage(C,n);
      hence thesis by A11,A12,A17,A19,XBOOLE_0:def 4;
    end;
  end;
end;

theorem Th4:
  for C be compact connected non vertical non horizontal
  Subset of TOP-REAL 2 holds
  Upper_Seq(C,n) = Rotate(Cage(C,n),E-max L~Cage(C,n)):-W-min L~Cage(C,n)
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
  set Nmi = N-min L~Cage(C,n);
  set Nma = N-max L~Cage(C,n);
  set Wmi = W-min L~Cage(C,n);
  set Wma = W-max L~Cage(C,n);
  set Ema = E-max L~Cage(C,n);
  set Emi = E-min L~Cage(C,n);
  set Sma = S-max L~Cage(C,n);
  set Smi = S-min L~Cage(C,n);
  set RotWmi = Rotate(Cage(C,n),Wmi);
  set RotEma = Rotate(Cage(C,n),Ema);
A1: Cage(C,n)/.1 = Nmi by JORDAN9:34;
A2: Nmi in rng Cage(C,n) by SPRECT_2:43;
A3: Wmi in rng Cage(C,n) by SPRECT_2:47;
A4: Ema in rng Cage(C,n) by SPRECT_2:50;
A5: Nmi..Cage(C,n) < Nma..Cage(C,n) by A1,SPRECT_2:72;
A6: Nma..Cage(C,n) <= Ema..Cage(C,n) by A1,SPRECT_2:74;
A7: Nmi..Cage(C,n) < Ema..Cage(C,n) by A1,A5,SPRECT_2:74,XXREAL_0:2;
A8: Emi..Cage(C,n) <= Sma..Cage(C,n) by A1,SPRECT_2:76;
A9: Smi..Cage(C,n) <= Wmi..Cage(C,n) by A1,SPRECT_2:78;
  Ema..Cage(C,n) < Sma..Cage(C,n) by A1,A8,SPRECT_2:75,XXREAL_0:2;
  then
A10: Ema..Cage(C,n) < Smi..Cage(C,n) by A1,SPRECT_2:77,XXREAL_0:2;
  then
A11: Ema..Cage(C,n) < Wmi..Cage(C,n) by A1,SPRECT_2:78,XXREAL_0:2;
A12: Ema in rng (Cage(C,n)-:Wmi) by A3,A4,A9,A10,FINSEQ_5:49,XXREAL_0:2;
A13: Cage(C,n)-:Wmi <> {} by A3,FINSEQ_5:50;
  (Cage(C,n)-:Wmi)/.1 = Cage(C,n)/.1 by A3,FINSEQ_5:47
    .= Nmi by JORDAN9:34;
  then
A14: Nmi in rng (Cage(C,n)-:Wmi) by A13,FINSEQ_6:46;
  len(Cage(C,n)-:Wmi) = Wmi..Cage(C,n) by A3,FINSEQ_5:45;
  then (Cage(C,n)-:Wmi)/.len (Cage(C,n)-:Wmi) = Wmi by A3,FINSEQ_5:48;
  then
A15: Wmi in rng (Cage(C,n)-:Wmi) by A13,REVROT_1:3;
  Wma in L~Cage(C,n) & Nmi`2 = N-bound L~Cage(C,n) by EUCLID:56,SPRECT_1:15;
  then Wma`2 <= Nmi`2 by PSCOMP_1:71;
  then
A16: Nmi <> Wmi by SPRECT_2:61;
  then
A17: card {Nmi,Wmi} = 2 by CARD_2:76;
  {Nmi,Wmi} c= rng (Cage(C,n)-:Wmi)
  proof
    let x be set;
    assume x in {Nmi,Wmi};
    hence thesis by A14,A15,TARSKI:def 2;
  end;
  then
A18: card {Nmi,Wmi} c= card rng (Cage(C,n)-:Wmi) by CARD_1:27;
  card rng (Cage(C,n)-:Wmi) c= card dom (Cage(C,n)-:Wmi) by CARD_2:80;
  then card rng (Cage(C,n)-:Wmi) c= len (Cage(C,n)-:Wmi) by CARD_1:104;
  then 2 c= len (Cage(C,n)-:Wmi) by A17,A18,XBOOLE_1:1;
  then len (Cage(C,n)-:Wmi) >= 2 by NAT_1:40;
  then
A19: rng (Cage(C,n)-:Wmi) c= L~(Cage(C,n)-:Wmi) by SPPOL_2:18;
A20: Nmi in rng (Cage(C,n)-:Wmi) by A2,A3,A7,A11,FINSEQ_5:49,XXREAL_0:2;
A21: Ema..(Cage(C,n)-:Wmi) <> 1
  proof
    assume
A22: Ema..(Cage(C,n)-:Wmi) = 1;
    Nmi..(Cage(C,n)-:Wmi) = Nmi..Cage(C,n) by A2,A3,A7,A11,SPRECT_5:3
,XXREAL_0:2
      .= 1 by A1,FINSEQ_6:47;
    hence contradiction by A5,A6,A12,A20,A22,FINSEQ_5:10;
  end;
  then
A23: Ema in rng (Cage(C,n)-:Wmi/^1) by A12,FINSEQ_6:83;
A24: Nmi`1 < Nma`1 by SPRECT_2:55;
  Nma`1 <= (NE-corner L~Cage(C,n))`1 by PSCOMP_1:97;
  then Nma`1 <= E-bound L~Cage(C,n) by EUCLID:56;
  then
A25: Nmi <> Ema by A24,EUCLID:56;
  not Ema in rng (Cage(C,n):-Wmi)
  proof
    assume
A26: Ema in rng (Cage(C,n):-Wmi);
    (Cage(C,n):-Wmi)/.len(Cage(C,n):-Wmi) = Cage(C,n)/.len Cage(C,n)
    by A3,FINSEQ_5:57
      .= Cage(C,n)/.1 by FINSEQ_6:def 1
      .= Nmi by JORDAN9:34;
    then
A27: Nmi in rng (Cage(C,n):-Wmi) by REVROT_1:3;
    (Cage(C,n):-Wmi)/.1 = Wmi by FINSEQ_5:56;
    then
A28: Wmi in rng (Cage(C,n):-Wmi) by FINSEQ_6:46;
    Wma in L~Cage(C,n) & Nmi`2 = N-bound L~Cage(C,n) by EUCLID:56,SPRECT_1:15;
    then Wma`2 <= Nmi`2 by PSCOMP_1:71;
    then Nmi <> Wmi by SPRECT_2:61;
    then
A29: card {Nmi,Wmi} = 2 by CARD_2:76;
    {Nmi,Wmi} c= rng (Cage(C,n):-Wmi)
    proof
      let x be set;
      assume x in {Nmi,Wmi};
      hence thesis by A27,A28,TARSKI:def 2;
    end;
    then
A30: card {Nmi,Wmi} c= card rng (Cage(C,n):-Wmi) by CARD_1:27;
    card rng (Cage(C,n):-Wmi) c= card dom (Cage(C,n):-Wmi) by CARD_2:80;
    then card rng (Cage(C,n):-Wmi) c= len (Cage(C,n):-Wmi) by CARD_1:104;
    then 2 c= len (Cage(C,n):-Wmi) by A29,A30,XBOOLE_1:1;
    then len (Cage(C,n):-Wmi) >= 2 by NAT_1:40;
    then rng (Cage(C,n):-Wmi) c= L~(Cage(C,n):-Wmi) by SPPOL_2:18;
    then Ema in L~(Cage(C,n)-:Wmi) /\ L~(Cage(C,n):-Wmi)
    by A12,A19,A26,XBOOLE_0:def 4;
    then Ema in {Nmi,Wmi} by JORDAN1G:25;
    then Ema = Wmi by A25,TARSKI:def 2;
    hence contradiction by TOPREAL5:25;
  end;
  then
A31: Ema in rng (Cage(C,n)-:Wmi/^1) \ rng (Cage(C,n):-Wmi)
  by A23,XBOOLE_0:def 5;
A32: Wmi in rng (Cage(C,n):-Ema) by A3,A4,A9,A10,FINSEQ_6:67,XXREAL_0:2;
A33: card {Nmi,Ema} = 2 by A25,CARD_2:76;
  (Cage(C,n):-Ema)/.len(Cage(C,n):-Ema) = Cage(C,n)/.len Cage(C,n)
  by A4,FINSEQ_5:57
    .= Cage(C,n)/.1 by FINSEQ_6:def 1
    .= Nmi by JORDAN9:34;
  then
A34: Nmi in rng (Cage(C,n):-Ema) by REVROT_1:3;
  (Cage(C,n):-Ema)/.1 = Ema by FINSEQ_5:56;
  then
A35: Ema in rng (Cage(C,n):-Ema) by FINSEQ_6:46;
  {Nmi,Ema} c= rng (Cage(C,n):-Ema)
  proof
    let x be set;
    assume x in {Nmi,Ema};
    hence thesis by A34,A35,TARSKI:def 2;
  end;
  then
A36: card {Nmi,Ema} c= card rng (Cage(C,n):-Ema) by CARD_1:27;
  card rng (Cage(C,n):-Ema) c= card dom (Cage(C,n):-Ema) by CARD_2:80;
  then card rng (Cage(C,n):-Ema) c= len (Cage(C,n):-Ema) by CARD_1:104;
  then 2 c= len (Cage(C,n):-Ema) by A33,A36,XBOOLE_1:1;
  then len (Cage(C,n):-Ema) >= 2 by NAT_1:40;
  then
A37: rng (Cage(C,n):-Ema) c= L~(Cage(C,n):-Ema) by SPPOL_2:18;
  not Wmi in rng (Cage(C,n)-:Ema)
  proof
    assume
A38: Wmi in rng (Cage(C,n)-:Ema);
    then
A39: Cage(C,n)-:Ema is non empty by RELAT_1:60;
    (Cage(C,n)-:Ema)/.1 = Cage(C,n)/.1 by A4,FINSEQ_5:47
      .= Nmi by JORDAN9:34;
    then
A40: Nmi in rng (Cage(C,n)-:Ema) by A39,FINSEQ_6:46;
    (Cage(C,n)-:Ema)/.len (Cage(C,n)-:Ema) =
    (Cage(C,n)-:Ema)/.(Ema..Cage(C,n)) by A4,FINSEQ_5:45
      .= Ema by A4,FINSEQ_5:48;
    then
A41: Ema in rng (Cage(C,n)-:Ema) by A39,REVROT_1:3;
    {Nmi,Ema} c= rng (Cage(C,n)-:Ema)
    proof
      let x be set;
      assume x in {Nmi,Ema};
      hence thesis by A40,A41,TARSKI:def 2;
    end;
    then
A42: card {Nmi,Ema} c= card rng (Cage(C,n)-:Ema) by CARD_1:27;
    card rng (Cage(C,n)-:Ema) c= card dom (Cage(C,n)-:Ema) by CARD_2:80;
    then card rng (Cage(C,n)-:Ema) c= len (Cage(C,n)-:Ema) by CARD_1:104;
    then 2 c= len (Cage(C,n)-:Ema) by A33,A42,XBOOLE_1:1;
    then len (Cage(C,n)-:Ema) >= 2 by NAT_1:40;
    then rng (Cage(C,n)-:Ema) c= L~(Cage(C,n)-:Ema) by SPPOL_2:18;
    then Wmi in L~(Cage(C,n)-:Ema) /\ L~(Cage(C,n):-Ema)
    by A32,A37,A38,XBOOLE_0:def 4;
    then Wmi in {Nmi,Ema} by Th3;
    then Wmi = Ema by A16,TARSKI:def 2;
    hence contradiction by TOPREAL5:25;
  end;
  then
A43: Wmi in rng Cage(C,n) \ rng(Cage(C,n)-:Ema) by A3,XBOOLE_0:def 5;
  RotWmi-:Ema = ((Cage(C,n):-Wmi)^((Cage(C,n)-:Wmi)/^1))-:Ema
  by A3,FINSEQ_6:def 2
    .= (Cage(C,n):-Wmi)^(((Cage(C,n)-:Wmi)/^1)-:Ema) by A31,FINSEQ_6:72
    .= (Cage(C,n):-Wmi)^(((Cage(C,n)-:Wmi)-:Ema)/^1) by A12,A21,FINSEQ_6:65
    .= ((Cage(C,n):-Ema):-Wmi)^(((Cage(C,n)-:Wmi)-:Ema)/^1)
  by A43,FINSEQ_6:76,SPRECT_2:50
    .= ((Cage(C,n):-Ema):-Wmi)^((Cage(C,n)-:Ema)/^1) by A3,A12,FINSEQ_6:80
    .= ((Cage(C,n):-Ema)^((Cage(C,n)-:Ema)/^1)):-Wmi by A32,FINSEQ_6:69
    .= RotEma:-Wmi by A4,FINSEQ_6:def 2;
  hence thesis by JORDAN1E:def 1;
end;

theorem
  for C be compact non vertical non horizontal Subset of TOP-REAL 2 holds
  W-min L~Cage(C,n) in rng Upper_Seq(C,n) &
  W-min L~Cage(C,n) in L~Upper_Seq(C,n)
proof
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  set p = W-min L~Cage(C,n);
A1: Upper_Seq(C,n) = Rotate(Cage(C,n),p)-:E-max L~Cage(C,n) by JORDAN1E:def 1;
A2: p in rng Cage(C,n) by SPRECT_2:47;
  E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
  then E-max L~Cage(C,n) in rng Rotate(Cage(C,n),p) by FINSEQ_6:96,SPRECT_2:47;
  then Upper_Seq(C,n)/.1 = Rotate(Cage(C,n),p)/.1 by A1,FINSEQ_5:47;
  then Upper_Seq(C,n)/.1 = p by A2,FINSEQ_6:98;
  hence
A3: p in rng Upper_Seq(C,n) by FINSEQ_6:46;
  len Upper_Seq(C,n) >= 2 by TOPREAL1:def 10;
  then rng Upper_Seq(C,n) c= L~Upper_Seq(C,n) by SPPOL_2:18;
  hence thesis by A3;
end;

theorem
  for C be compact connected non vertical non horizontal
  Subset of TOP-REAL 2 holds W-max L~Cage(C,n) in rng Upper_Seq(C,n) &
  W-max L~Cage(C,n) in L~Upper_Seq(C,n)
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
  set x = W-max L~Cage(C,n);
  set p = W-min L~Cage(C,n);
  set f = Rotate(Cage(C,n),E-max L~Cage(C,n));
A1: rng f = rng Cage(C,n) by FINSEQ_6:96,SPRECT_2:50;
A2: x in rng Cage(C,n) by SPRECT_2:48;
A3: p in rng Cage(C,n) by SPRECT_2:47;
A4: Lower_Seq(C,n) = f-:W-min L~Cage(C,n) by JORDAN1G:26;
  W-min L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:47;
  then W-min L~Cage(C,n) in rng f by FINSEQ_6:96,SPRECT_2:50;
  then
A5: Lower_Seq(C,n)/.1 = f/.1 by A4,FINSEQ_5:47;
A6: L~Cage(C,n) = L~f by REVROT_1:33;
  then (W-min L~f)..f < (W-max L~f)..f by A5,JORDAN1F:6,SPRECT_5:43;
  then x in rng(f:-p) by A1,A2,A3,A6,FINSEQ_6:67;
  hence
A7: x in rng Upper_Seq(C,n) by Th4;
  len Upper_Seq(C,n) >= 2 by TOPREAL1:def 10;
  then rng Upper_Seq(C,n) c= L~Upper_Seq(C,n) by SPPOL_2:18;
  hence thesis by A7;
end;

theorem Th7:
  for C be compact connected non vertical non horizontal
  Subset of TOP-REAL 2 holds N-min L~Cage(C,n) in rng Upper_Seq(C,n) &
  N-min L~Cage(C,n) in L~Upper_Seq(C,n)
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
  set x = N-min L~Cage(C,n);
  set p = W-min L~Cage(C,n);
  set f = Rotate(Cage(C,n),E-max L~Cage(C,n));
A1: rng f = rng Cage(C,n) by FINSEQ_6:96,SPRECT_2:50;
A2: x in rng Cage(C,n) by SPRECT_2:43;
A3: p in rng Cage(C,n) by SPRECT_2:47;
A4: Lower_Seq(C,n) = f-:W-min L~Cage(C,n) by JORDAN1G:26;
  W-min L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:47;
  then W-min L~Cage(C,n) in rng f by FINSEQ_6:96,SPRECT_2:50;
  then
A5: Lower_Seq(C,n)/.1 = f/.1 by A4,FINSEQ_5:47;
A6: L~Cage(C,n) = L~f by REVROT_1:33;
  then
A7: (W-min L~f)..f < (W-max L~f)..f by A5,JORDAN1F:6,SPRECT_5:43;
  (W-max L~f)..f <= (N-min L~f)..f by A5,A6,JORDAN1F:6,SPRECT_5:44;
  then x in rng(f:-p) by A1,A2,A3,A6,A7,FINSEQ_6:67,XXREAL_0:2;
  hence
A8: x in rng Upper_Seq(C,n) by Th4;
  len Upper_Seq(C,n) >= 2 by TOPREAL1:def 10;
  then rng Upper_Seq(C,n) c= L~Upper_Seq(C,n) by SPPOL_2:18;
  hence thesis by A8;
end;

theorem
  for C be compact connected non vertical non horizontal
  Subset of TOP-REAL 2 holds N-max L~Cage(C,n) in rng Upper_Seq(C,n) &
  N-max L~Cage(C,n) in L~Upper_Seq(C,n)
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
  set x = N-max L~Cage(C,n);
  set p = W-min L~Cage(C,n);
  set f = Rotate(Cage(C,n),E-max L~Cage(C,n));
A1: rng f = rng Cage(C,n) by FINSEQ_6:96,SPRECT_2:50;
A2: x in rng Cage(C,n) by SPRECT_2:44;
A3: p in rng Cage(C,n) by SPRECT_2:47;
A4: Lower_Seq(C,n) = f-:W-min L~Cage(C,n) by JORDAN1G:26;
  W-min L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:47;
  then W-min L~Cage(C,n) in rng f by FINSEQ_6:96,SPRECT_2:50;
  then
A5: Lower_Seq(C,n)/.1 = f/.1 by A4,FINSEQ_5:47;
A6: Lower_Seq(C,n)/.1 = E-max L~Cage(C,n) by JORDAN1F:6;
A7: L~Cage(C,n) = L~f by REVROT_1:33;
  then
A8: (W-min L~f)..f < (W-max L~f)..f by A5,JORDAN1F:6,SPRECT_5:43;
A9: (W-max L~f)..f <= (N-min L~f)..f by A5,A7,JORDAN1F:6,SPRECT_5:44;
  per cases;
  suppose N-max L~f <> E-max L~f;
    then (W-max L~f)..f < (N-max L~f)..f by A5,A6,A7,A9,SPRECT_5:45,XXREAL_0:2;
    then x in rng(f:-p) by A1,A2,A3,A7,A8,FINSEQ_6:67,XXREAL_0:2;
    hence
A10: x in rng Upper_Seq(C,n) by Th4;
    len Upper_Seq(C,n) >= 2 by TOPREAL1:def 10;
    then rng Upper_Seq(C,n) c= L~Upper_Seq(C,n) by SPPOL_2:18;
    hence thesis by A10;
  end;
  suppose
A11: N-max L~f = E-max L~f;
A12: Upper_Seq(C,n) = Rotate(Cage(C,n),p)-:E-max L~Cage(C,n)
    by JORDAN1E:def 1;
    E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
    then
A13: E-max L~Cage(C,n) in rng Rotate(Cage(C,n),W-min L~Cage(C,n))
    by FINSEQ_6:96,SPRECT_2:47;
    (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n)) <=
    (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n));
    hence
A14: x in rng Upper_Seq(C,n) by A7,A11,A12,A13,FINSEQ_5:49;
    len Upper_Seq(C,n) >= 2 by TOPREAL1:def 10;
    then rng Upper_Seq(C,n) c= L~Upper_Seq(C,n) by SPPOL_2:18;
    hence thesis by A14;
  end;
end;

theorem
  for C be compact non vertical non horizontal Subset of TOP-REAL 2 holds
  E-max L~Cage(C,n) in rng Upper_Seq(C,n) &
  E-max L~Cage(C,n) in L~Upper_Seq(C,n)
proof
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  set x = E-max L~Cage(C,n);
  set p = W-min L~Cage(C,n);
A1: Upper_Seq(C,n) = Rotate(Cage(C,n),p)-:E-max L~Cage(C,n) by JORDAN1E:def 1;
  E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
  then
A2: E-max L~Cage(C,n) in rng Rotate(Cage(C,n),W-min L~Cage(C,n))
  by FINSEQ_6:96,SPRECT_2:47;
  (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n)) <=
  (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n));
  hence
A3: x in rng Upper_Seq(C,n) by A1,A2,FINSEQ_5:49;
  len Upper_Seq(C,n) >= 2 by TOPREAL1:def 10;
  then rng Upper_Seq(C,n) c= L~Upper_Seq(C,n) by SPPOL_2:18;
  hence thesis by A3;
end;

theorem
  for C be compact non vertical non horizontal Subset of TOP-REAL 2 holds
  E-max L~Cage(C,n) in rng Lower_Seq(C,n) &
  E-max L~Cage(C,n) in L~Lower_Seq(C,n)
proof
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  set p = E-max L~Cage(C,n);
  Lower_Seq(C,n) = Rotate(Cage(C,n),W-min L~Cage(C,n)):-p by JORDAN1E:def 2;
  hence
A1: p in rng Lower_Seq(C,n) by FINSEQ_6:66;
  len Lower_Seq(C,n) >= 2 by TOPREAL1:def 10;
  then rng Lower_Seq(C,n) c= L~Lower_Seq(C,n) by SPPOL_2:18;
  hence thesis by A1;
end;

theorem
  for C be compact non vertical non horizontal Subset of TOP-REAL 2 holds
  E-min L~Cage(C,n) in rng Lower_Seq(C,n) &
  E-min L~Cage(C,n) in L~Lower_Seq(C,n)
proof
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  set x = E-min L~Cage(C,n);
  set p = E-max L~Cage(C,n);
  set f = Rotate(Cage(C,n),W-min L~Cage(C,n));
A1: rng f = rng Cage(C,n) by FINSEQ_6:96,SPRECT_2:47;
A2: x in rng Cage(C,n) by SPRECT_2:49;
A3: p in rng Cage(C,n) by SPRECT_2:50;
A4: Upper_Seq(C,n) = f-:E-max L~Cage(C,n) by JORDAN1E:def 1;
  E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
  then E-max L~Cage(C,n) in rng f by FINSEQ_6:96,SPRECT_2:47;
  then
A5: Upper_Seq(C,n)/.1 = f/.1 by A4,FINSEQ_5:47;
A6: L~Cage(C,n) = L~f by REVROT_1:33;
  then (E-max L~f)..f < (E-min L~f)..f by A5,JORDAN1F:5,SPRECT_5:27;
  then x in rng(f:-p) by A1,A2,A3,A6,FINSEQ_6:67;
  hence
A7: x in rng Lower_Seq(C,n) by JORDAN1E:def 2;
  len Lower_Seq(C,n) >= 2 by TOPREAL1:def 10;
  then rng Lower_Seq(C,n) c= L~Lower_Seq(C,n) by SPPOL_2:18;
  hence thesis by A7;
end;

theorem Th12:
  for C be compact non vertical non horizontal Subset of TOP-REAL 2 holds
  S-max L~Cage(C,n) in rng Lower_Seq(C,n) &
  S-max L~Cage(C,n) in L~Lower_Seq(C,n)
proof
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  set x = S-max L~Cage(C,n);
  set p = E-max L~Cage(C,n);
  set f = Rotate(Cage(C,n),W-min L~Cage(C,n));
A1: rng f = rng Cage(C,n) by FINSEQ_6:96,SPRECT_2:47;
A2: x in rng Cage(C,n) by SPRECT_2:46;
A3: p in rng Cage(C,n) by SPRECT_2:50;
A4: Upper_Seq(C,n) = f-:E-max L~Cage(C,n) by JORDAN1E:def 1;
  E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
  then E-max L~Cage(C,n) in rng f by FINSEQ_6:96,SPRECT_2:47;
  then
A5: Upper_Seq(C,n)/.1 = f/.1 by A4,FINSEQ_5:47;
A6: L~Cage(C,n) = L~f by REVROT_1:33;
  then
A7: (E-max L~f)..f < (E-min L~f)..f by A5,JORDAN1F:5,SPRECT_5:27;
  (E-min L~f)..f <= (S-max L~f)..f by A5,A6,JORDAN1F:5,SPRECT_5:28;
  then x in rng(f:-p) by A1,A2,A3,A6,A7,FINSEQ_6:67,XXREAL_0:2;
  hence
A8: x in rng Lower_Seq(C,n) by JORDAN1E:def 2;
  len Lower_Seq(C,n) >= 2 by TOPREAL1:def 10;
  then rng Lower_Seq(C,n) c= L~Lower_Seq(C,n) by SPPOL_2:18;
  hence thesis by A8;
end;

theorem
  for C be compact non vertical non horizontal Subset of TOP-REAL 2 holds
  S-min L~Cage(C,n) in rng Lower_Seq(C,n) &
  S-min L~Cage(C,n) in L~Lower_Seq(C,n)
proof
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  set x = S-min L~Cage(C,n);
  set p = E-max L~Cage(C,n);
  set f = Rotate(Cage(C,n),W-min L~Cage(C,n));
A1: rng f = rng Cage(C,n) by FINSEQ_6:96,SPRECT_2:47;
A2: x in rng Cage(C,n) by SPRECT_2:45;
A3: p in rng Cage(C,n) by SPRECT_2:50;
A4: Upper_Seq(C,n) = f-:E-max L~Cage(C,n) by JORDAN1E:def 1;
  E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
  then E-max L~Cage(C,n) in rng f by FINSEQ_6:96,SPRECT_2:47;
  then
A5: Upper_Seq(C,n)/.1 = f/.1 by A4,FINSEQ_5:47;
A6: Upper_Seq(C,n)/.1 = W-min L~Cage(C,n) by JORDAN1F:5;
A7: L~Cage(C,n) = L~f by REVROT_1:33;
  then
A8: (E-max L~f)..f < (E-min L~f)..f by A5,JORDAN1F:5,SPRECT_5:27;
A9: (E-min L~f)..f <= (S-max L~f)..f by A5,A7,JORDAN1F:5,SPRECT_5:28;
  per cases;
  suppose S-min L~f <> W-min L~f;
    then (E-min L~f)..f < (S-min L~f)..f by A5,A6,A7,A9,SPRECT_5:29,XXREAL_0:2;
    then x in rng(f:-p) by A1,A2,A3,A7,A8,FINSEQ_6:67,XXREAL_0:2;
    hence
A10: x in rng Lower_Seq(C,n) by JORDAN1E:def 2;
    len Lower_Seq(C,n) >= 2 by TOPREAL1:def 10;
    then rng Lower_Seq(C,n) c= L~Lower_Seq(C,n) by SPPOL_2:18;
    hence thesis by A10;
  end;
  suppose
A11: S-min L~f = W-min L~f;
    Lower_Seq(C,n)/.(len Lower_Seq(C,n)) = W-min L~Cage(C,n) by JORDAN1F:8;
    hence
A12: x in rng Lower_Seq(C,n) by A7,A11,REVROT_1:3;
    len Lower_Seq(C,n) >= 2 by TOPREAL1:def 10;
    then rng Lower_Seq(C,n) c= L~Lower_Seq(C,n) by SPPOL_2:18;
    hence thesis by A12;
  end;
end;

theorem
  for C be compact non vertical non horizontal Subset of TOP-REAL 2 holds
  W-min L~Cage(C,n) in rng Lower_Seq(C,n) &
  W-min L~Cage(C,n) in L~Lower_Seq(C,n)
proof
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  set p = W-min L~Cage(C,n);
  Lower_Seq(C,n)/.(len Lower_Seq(C,n)) = p by JORDAN1F:8;
  hence
A1: p in rng Lower_Seq(C,n) by REVROT_1:3;
  len Lower_Seq(C,n) >= 2 by TOPREAL1:def 10;
  then rng Lower_Seq(C,n) c= L~Lower_Seq(C,n) by SPPOL_2:18;
  hence thesis by A1;
end;

theorem Th15:
  for X,Y be non empty compact Subset of TOP-REAL 2 st
  X c= Y & N-min Y in X holds N-min X = N-min Y
proof
  let X,Y be non empty compact Subset of TOP-REAL 2;
  assume that
A1: X c= Y and
A2: N-min Y in X;
A3: N-bound X <= N-bound Y by A1,PSCOMP_1:129;
A4: (N-min X)`2 = N-bound X & (N-min Y)`2 = N-bound Y by EUCLID:56;
A5: N-bound X >= (N-min Y)`2 by A2,PSCOMP_1:71;
  then
A6: N-bound X = N-bound Y by A3,A4,XXREAL_0:1;
  N-min Y in N-most X by A2,A3,A4,A5,SPRECT_2:14,XXREAL_0:1;
  then
A7: (N-min X)`1 <= (N-min Y)`1 by PSCOMP_1:98;
  N-min X in X by SPRECT_1:13;
  then N-min X in N-most Y by A1,A3,A4,A5,SPRECT_2:14,XXREAL_0:1;
  then (N-min X)`1 >= (N-min Y)`1 by PSCOMP_1:98;
  then (N-min X)`1 = (N-min Y)`1 by A7,XXREAL_0:1;
  hence thesis by A4,A6,TOPREAL3:11;
end;

theorem Th16:
  for X,Y be non empty compact Subset of TOP-REAL 2 st
  X c= Y & N-max Y in X holds N-max X = N-max Y
proof
  let X,Y be non empty compact Subset of TOP-REAL 2;
  assume that
A1: X c= Y and
A2: N-max Y in X;
A3: N-bound X <= N-bound Y by A1,PSCOMP_1:129;
A4: (N-max X)`2 = N-bound X & (N-max Y)`2 = N-bound Y by EUCLID:56;
A5: N-bound X >= (N-max Y)`2 by A2,PSCOMP_1:71;
  then
A6: N-bound X = N-bound Y by A3,A4,XXREAL_0:1;
  N-max Y in N-most X by A2,A3,A4,A5,SPRECT_2:14,XXREAL_0:1;
  then
A7: (N-max X)`1 >= (N-max Y)`1 by PSCOMP_1:98;
  N-max X in X by SPRECT_1:13;
  then N-max X in N-most Y by A1,A3,A4,A5,SPRECT_2:14,XXREAL_0:1;
  then (N-max X)`1 <= (N-max Y)`1 by PSCOMP_1:98;
  then (N-max X)`1 = (N-max Y)`1 by A7,XXREAL_0:1;
  hence thesis by A4,A6,TOPREAL3:11;
end;

theorem Th17:
  for X,Y be non empty compact Subset of TOP-REAL 2 st
  X c= Y & E-min Y in X holds E-min X = E-min Y
proof
  let X,Y be non empty compact Subset of TOP-REAL 2;
  assume that
A1: X c= Y and
A2: E-min Y in X;
A3: E-bound X <= E-bound Y by A1,PSCOMP_1:130;
A4: (E-min X)`1 = E-bound X & (E-min Y)`1 = E-bound Y by EUCLID:56;
A5: E-bound X >= (E-min Y)`1 by A2,PSCOMP_1:71;
  then
A6: E-bound X = E-bound Y by A3,A4,XXREAL_0:1;
  E-min Y in E-most X by A2,A3,A4,A5,SPRECT_2:17,XXREAL_0:1;
  then
A7: (E-min X)`2 <= (E-min Y)`2 by PSCOMP_1:108;
  E-min X in X by SPRECT_1:16;
  then E-min X in E-most Y by A1,A3,A4,A5,SPRECT_2:17,XXREAL_0:1;
  then (E-min X)`2 >= (E-min Y)`2 by PSCOMP_1:108;
  then (E-min X)`2 = (E-min Y)`2 by A7,XXREAL_0:1;
  hence thesis by A4,A6,TOPREAL3:11;
end;

theorem Th18:
  for X,Y be non empty compact Subset of TOP-REAL 2 st
  X c= Y & E-max Y in X holds E-max X = E-max Y
proof
  let X,Y be non empty compact Subset of TOP-REAL 2;
  assume that
A1: X c= Y and
A2: E-max Y in X;
A3: E-bound X <= E-bound Y by A1,PSCOMP_1:130;
A4: (E-max X)`1 = E-bound X & (E-max Y)`1 = E-bound Y by EUCLID:56;
A5: E-bound X >= (E-max Y)`1 by A2,PSCOMP_1:71;
  then
A6: E-bound X = E-bound Y by A3,A4,XXREAL_0:1;
  E-max Y in E-most X by A2,A3,A4,A5,SPRECT_2:17,XXREAL_0:1;
  then
A7: (E-max X)`2 >= (E-max Y)`2 by PSCOMP_1:108;
  E-max X in X by SPRECT_1:16;
  then E-max X in E-most Y by A1,A3,A4,A5,SPRECT_2:17,XXREAL_0:1;
  then (E-max X)`2 <= (E-max Y)`2 by PSCOMP_1:108;
  then (E-max X)`2 = (E-max Y)`2 by A7,XXREAL_0:1;
  hence thesis by A4,A6,TOPREAL3:11;
end;

theorem Th19:
  for X,Y be non empty compact Subset of TOP-REAL 2 st
  X c= Y & S-min Y in X holds S-min X = S-min Y
proof
  let X,Y be non empty compact Subset of TOP-REAL 2;
  assume that
A1: X c= Y and
A2: S-min Y in X;
A3: S-bound X >= S-bound Y by A1,PSCOMP_1:131;
A4: (S-min X)`2 = S-bound X & (S-min Y)`2 = S-bound Y by EUCLID:56;
A5: S-bound X <= (S-min Y)`2 by A2,PSCOMP_1:71;
  then
A6: S-bound X = S-bound Y by A3,A4,XXREAL_0:1;
  S-min Y in S-most X by A2,A3,A4,A5,SPRECT_2:15,XXREAL_0:1;
  then
A7: (S-min X)`1 <= (S-min Y)`1 by PSCOMP_1:118;
  S-min X in X by SPRECT_1:14;
  then S-min X in S-most Y by A1,A3,A4,A5,SPRECT_2:15,XXREAL_0:1;
  then (S-min X)`1 >= (S-min Y)`1 by PSCOMP_1:118;
  then (S-min X)`1 = (S-min Y)`1 by A7,XXREAL_0:1;
  hence thesis by A4,A6,TOPREAL3:11;
end;

theorem Th20:
  for X,Y be non empty compact Subset of TOP-REAL 2 st
  X c= Y & S-max Y in X holds S-max X = S-max Y
proof
  let X,Y be non empty compact Subset of TOP-REAL 2;
  assume that
A1: X c= Y and
A2: S-max Y in X;
A3: S-bound X >= S-bound Y by A1,PSCOMP_1:131;
A4: (S-max X)`2 = S-bound X & (S-max Y)`2 = S-bound Y by EUCLID:56;
A5: S-bound X <= (S-max Y)`2 by A2,PSCOMP_1:71;
  then
A6: S-bound X = S-bound Y by A3,A4,XXREAL_0:1;
  S-max Y in S-most X by A2,A3,A4,A5,SPRECT_2:15,XXREAL_0:1;
  then
A7: (S-max X)`1 >= (S-max Y)`1 by PSCOMP_1:118;
  S-max X in X by SPRECT_1:14;
  then S-max X in S-most Y by A1,A3,A4,A5,SPRECT_2:15,XXREAL_0:1;
  then (S-max X)`1 <= (S-max Y)`1 by PSCOMP_1:118;
  then (S-max X)`1 = (S-max Y)`1 by A7,XXREAL_0:1;
  hence thesis by A4,A6,TOPREAL3:11;
end;

theorem Th21:
  for X,Y be non empty compact Subset of TOP-REAL 2 st
  X c= Y & W-min Y in X holds W-min X = W-min Y
proof
  let X,Y be non empty compact Subset of TOP-REAL 2;
  assume that
A1: X c= Y and
A2: W-min Y in X;
A3: W-bound X >= W-bound Y by A1,PSCOMP_1:132;
A4: (W-min X)`1 = W-bound X & (W-min Y)`1 = W-bound Y by EUCLID:56;
A5: W-bound X <= (W-min Y)`1 by A2,PSCOMP_1:71;
  then
A6: W-bound X = W-bound Y by A3,A4,XXREAL_0:1;
  W-min Y in W-most X by A2,A3,A4,A5,SPRECT_2:16,XXREAL_0:1;
  then
A7: (W-min X)`2 <= (W-min Y)`2 by PSCOMP_1:88;
  W-min X in X by SPRECT_1:15;
  then W-min X in W-most Y by A1,A3,A4,A5,SPRECT_2:16,XXREAL_0:1;
  then (W-min X)`2 >= (W-min Y)`2 by PSCOMP_1:88;
  then (W-min X)`2 = (W-min Y)`2 by A7,XXREAL_0:1;
  hence thesis by A4,A6,TOPREAL3:11;
end;

theorem Th22:
  for X,Y be non empty compact Subset of TOP-REAL 2 st
  X c= Y & W-max Y in X holds W-max X = W-max Y
proof
  let X,Y be non empty compact Subset of TOP-REAL 2;
  assume that
A1: X c= Y and
A2: W-max Y in X;
A3: W-bound X >= W-bound Y by A1,PSCOMP_1:132;
A4: (W-max X)`1 = W-bound X & (W-max Y)`1 = W-bound Y by EUCLID:56;
A5: W-bound X <= (W-max Y)`1 by A2,PSCOMP_1:71;
  then
A6: W-bound X = W-bound Y by A3,A4,XXREAL_0:1;
  W-max Y in W-most X by A2,A3,A4,A5,SPRECT_2:16,XXREAL_0:1;
  then
A7: (W-max X)`2 >= (W-max Y)`2 by PSCOMP_1:88;
  W-max X in X by SPRECT_1:15;
  then W-max X in W-most Y by A1,A3,A4,A5,SPRECT_2:16,XXREAL_0:1;
  then (W-max X)`2 <= (W-max Y)`2 by PSCOMP_1:88;
  then (W-max X)`2 = (W-max Y)`2 by A7,XXREAL_0:1;
  hence thesis by A4,A6,TOPREAL3:11;
end;

theorem Th23:
  for X,Y be non empty compact Subset of TOP-REAL 2 st
  N-bound X <= N-bound Y holds N-bound (X\/Y) = N-bound Y
proof
  let X,Y be non empty compact Subset of TOP-REAL 2;
  assume N-bound X <= N-bound Y;
  then max(N-bound X,N-bound Y) = N-bound Y by XXREAL_0:def 10;
  hence thesis by SPRECT_1:56;
end;

theorem Th24:
  for X,Y be non empty compact Subset of TOP-REAL 2 st
  E-bound X <= E-bound Y holds E-bound (X\/Y) = E-bound Y
proof
  let X,Y be non empty compact Subset of TOP-REAL 2;
  assume E-bound X <= E-bound Y;
  then max(E-bound X,E-bound Y) = E-bound Y by XXREAL_0:def 10;
  hence thesis by SPRECT_1:57;
end;

theorem Th25:
  for X,Y be non empty compact Subset of TOP-REAL 2 st
  S-bound X <= S-bound Y holds S-bound (X\/Y) = S-bound X
proof
  let X,Y be non empty compact Subset of TOP-REAL 2;
  assume S-bound X <= S-bound Y;
  then min(S-bound X,S-bound Y) = S-bound X by XXREAL_0:def 9;
  hence thesis by SPRECT_1:55;
end;

theorem Th26:
  for X,Y be non empty compact Subset of TOP-REAL 2 st
  W-bound X <= W-bound Y holds W-bound (X\/Y) = W-bound X
proof
  let X,Y be non empty compact Subset of TOP-REAL 2;
  assume W-bound X <= W-bound Y;
  then min(W-bound X,W-bound Y) = W-bound X by XXREAL_0:def 9;
  hence thesis by SPRECT_1:54;
end;

theorem
  for X,Y be non empty compact Subset of TOP-REAL 2 st
  N-bound X < N-bound Y holds N-min (X\/Y) = N-min Y
proof
  let X,Y be non empty compact Subset of TOP-REAL 2;
A1: X\/Y is compact by COMPTS_1:19;
  assume
A2: N-bound X < N-bound Y;
  then
A3: N-bound (X\/Y) = N-bound Y by Th23;
A4: (N-min(X\/Y))`2 = N-bound (X\/Y) & (N-min Y)`2 = N-bound Y by EUCLID:56;
A5: Y c= X\/Y by XBOOLE_1:7;
  N-min Y in Y by SPRECT_1:13;
  then N-min Y in N-most(X\/Y) by A1,A3,A4,A5,SPRECT_2:14;
  then
A6: (N-min(X\/Y))`1 <= (N-min Y)`1 by A1,PSCOMP_1:98;
A7: N-min(X\/Y) in X\/Y by A1,SPRECT_1:13;
  per cases by A7,XBOOLE_0:def 3;
  suppose N-min(X\/Y) in Y;
    then N-min(X\/Y) in N-most Y by A2,A4,Th23,SPRECT_2:14;
    then (N-min(X\/Y))`1 >= (N-min Y)`1 by PSCOMP_1:98;
    then (N-min(X\/Y))`1 = (N-min Y)`1 by A6,XXREAL_0:1;
    hence thesis by A2,A4,Th23,TOPREAL3:11;
  end;
  suppose N-min(X\/Y) in X;
    hence thesis by A2,A3,A4,PSCOMP_1:71;
  end;
end;

theorem
  for X,Y be non empty compact Subset of TOP-REAL 2 st
  N-bound X < N-bound Y holds N-max (X\/Y) = N-max Y
proof
  let X,Y be non empty compact Subset of TOP-REAL 2;
A1: X\/Y is compact by COMPTS_1:19;
  assume
A2: N-bound X < N-bound Y;
  then
A3: N-bound (X\/Y) = N-bound Y by Th23;
A4: (N-max(X\/Y))`2 = N-bound (X\/Y) & (N-max Y)`2 = N-bound Y by EUCLID:56;
A5: Y c= X\/Y by XBOOLE_1:7;
  N-max Y in Y by SPRECT_1:13;
  then N-max Y in N-most(X\/Y) by A1,A3,A4,A5,SPRECT_2:14;
  then
A6: (N-max(X\/Y))`1 >= (N-max Y)`1 by A1,PSCOMP_1:98;
A7: N-max(X\/Y) in X\/Y by A1,SPRECT_1:13;
  per cases by A7,XBOOLE_0:def 3;
  suppose N-max(X\/Y) in Y;
    then N-max(X\/Y) in N-most Y by A2,A4,Th23,SPRECT_2:14;
    then (N-max(X\/Y))`1 <= (N-max Y)`1 by PSCOMP_1:98;
    then (N-max(X\/Y))`1 = (N-max Y)`1 by A6,XXREAL_0:1;
    hence thesis by A2,A4,Th23,TOPREAL3:11;
  end;
  suppose N-max(X\/Y) in X;
    hence thesis by A2,A3,A4,PSCOMP_1:71;
  end;
end;

theorem
  for X,Y be non empty compact Subset of TOP-REAL 2 st
  E-bound X < E-bound Y holds E-min (X\/Y) = E-min Y
proof
  let X,Y be non empty compact Subset of TOP-REAL 2;
A1: X\/Y is compact by COMPTS_1:19;
  assume
A2: E-bound X < E-bound Y;
  then
A3: E-bound (X\/Y) = E-bound Y by Th24;
A4: (E-min(X\/Y))`1 = E-bound (X\/Y) & (E-min Y)`1 = E-bound Y by EUCLID:56;
A5: Y c= X\/Y by XBOOLE_1:7;
  E-min Y in Y by SPRECT_1:16;
  then E-min Y in E-most(X\/Y) by A1,A3,A4,A5,SPRECT_2:17;
  then
A6: (E-min(X\/Y))`2 <= (E-min Y)`2 by A1,PSCOMP_1:108;
A7: E-min(X\/Y) in X\/Y by A1,SPRECT_1:16;
  per cases by A7,XBOOLE_0:def 3;
  suppose E-min(X\/Y) in Y;
    then E-min(X\/Y) in E-most Y by A2,A4,Th24,SPRECT_2:17;
    then (E-min(X\/Y))`2 >= (E-min Y)`2 by PSCOMP_1:108;
    then (E-min(X\/Y))`2 = (E-min Y)`2 by A6,XXREAL_0:1;
    hence thesis by A2,A4,Th24,TOPREAL3:11;
  end;
  suppose E-min(X\/Y) in X;
    hence thesis by A2,A3,A4,PSCOMP_1:71;
  end;
end;

theorem
  for X,Y be non empty compact Subset of TOP-REAL 2 st
  E-bound X < E-bound Y holds E-max (X\/Y) = E-max Y
proof
  let X,Y be non empty compact Subset of TOP-REAL 2;
A1: X\/Y is compact by COMPTS_1:19;
  assume
A2: E-bound X < E-bound Y;
  then
A3: E-bound (X\/Y) = E-bound Y by Th24;
A4: (E-max(X\/Y))`1 = E-bound (X\/Y) & (E-max Y)`1 = E-bound Y by EUCLID:56;
A5: Y c= X\/Y by XBOOLE_1:7;
  E-max Y in Y by SPRECT_1:16;
  then E-max Y in E-most(X\/Y) by A1,A3,A4,A5,SPRECT_2:17;
  then
A6: (E-max(X\/Y))`2 >= (E-max Y)`2 by A1,PSCOMP_1:108;
A7: E-max(X\/Y) in X\/Y by A1,SPRECT_1:16;
  per cases by A7,XBOOLE_0:def 3;
  suppose E-max(X\/Y) in Y;
    then E-max(X\/Y) in E-most Y by A2,A4,Th24,SPRECT_2:17;
    then (E-max(X\/Y))`2 <= (E-max Y)`2 by PSCOMP_1:108;
    then (E-max(X\/Y))`2 = (E-max Y)`2 by A6,XXREAL_0:1;
    hence thesis by A2,A4,Th24,TOPREAL3:11;
  end;
  suppose E-max(X\/Y) in X;
    hence thesis by A2,A3,A4,PSCOMP_1:71;
  end;
end;

theorem
  for X,Y be non empty compact Subset of TOP-REAL 2 st
  S-bound X < S-bound Y holds S-min (X\/Y) = S-min X
proof
  let X,Y be non empty compact Subset of TOP-REAL 2;
A1: X\/Y is compact by COMPTS_1:19;
  assume
A2: S-bound X < S-bound Y;
  then
A3: S-bound (X\/Y) = S-bound X by Th25;
A4: (S-min(X\/Y))`2 = S-bound (X\/Y) & (S-min X)`2 = S-bound X by EUCLID:56;
A5: X c= X\/Y by XBOOLE_1:7;
  S-min X in X by SPRECT_1:14;
  then S-min X in S-most(X\/Y) by A1,A3,A4,A5,SPRECT_2:15;
  then
A6: (S-min(X\/Y))`1 <= (S-min X)`1 by A1,PSCOMP_1:118;
A7: S-min(X\/Y) in X\/Y by A1,SPRECT_1:14;
  per cases by A7,XBOOLE_0:def 3;
  suppose S-min(X\/Y) in X;
    then S-min(X\/Y) in S-most X by A2,A4,Th25,SPRECT_2:15;
    then (S-min(X\/Y))`1 >= (S-min X)`1 by PSCOMP_1:118;
    then (S-min(X\/Y))`1 = (S-min X)`1 by A6,XXREAL_0:1;
    hence thesis by A2,A4,Th25,TOPREAL3:11;
  end;
  suppose S-min(X\/Y) in Y;
    hence thesis by A2,A3,A4,PSCOMP_1:71;
  end;
end;

theorem
  for X,Y be non empty compact Subset of TOP-REAL 2 st
  S-bound X < S-bound Y holds S-max (X\/Y) = S-max X
proof
  let X,Y be non empty compact Subset of TOP-REAL 2;
A1: X\/Y is compact by COMPTS_1:19;
  assume
A2: S-bound X < S-bound Y;
  then
A3: S-bound (X\/Y) = S-bound X by Th25;
A4: (S-max(X\/Y))`2 = S-bound (X\/Y) & (S-max X)`2 = S-bound X by EUCLID:56;
A5: X c= X\/Y by XBOOLE_1:7;
  S-max X in X by SPRECT_1:14;
  then S-max X in S-most(X\/Y) by A1,A3,A4,A5,SPRECT_2:15;
  then
A6: (S-max(X\/Y))`1 >= (S-max X)`1 by A1,PSCOMP_1:118;
A7: S-max(X\/Y) in X\/Y by A1,SPRECT_1:14;
  per cases by A7,XBOOLE_0:def 3;
  suppose S-max(X\/Y) in X;
    then S-max(X\/Y) in S-most X by A2,A4,Th25,SPRECT_2:15;
    then (S-max(X\/Y))`1 <= (S-max X)`1 by PSCOMP_1:118;
    then (S-max(X\/Y))`1 = (S-max X)`1 by A6,XXREAL_0:1;
    hence thesis by A2,A4,Th25,TOPREAL3:11;
  end;
  suppose S-max(X\/Y) in Y;
    hence thesis by A2,A3,A4,PSCOMP_1:71;
  end;
end;

theorem Th33:
  for X,Y be non empty compact Subset of TOP-REAL 2 st
  W-bound X < W-bound Y holds W-min (X\/Y) = W-min X
proof
  let X,Y be non empty compact Subset of TOP-REAL 2;
A1: X\/Y is compact by COMPTS_1:19;
  assume
A2: W-bound X < W-bound Y;
  then
A3: W-bound (X\/Y) = W-bound X by Th26;
A4: (W-min(X\/Y))`1 = W-bound (X\/Y) & (W-min X)`1 = W-bound X by EUCLID:56;
A5: X c= X\/Y by XBOOLE_1:7;
  W-min X in X by SPRECT_1:15;
  then W-min X in W-most(X\/Y) by A1,A3,A4,A5,SPRECT_2:16;
  then
A6: (W-min(X\/Y))`2 <= (W-min X)`2 by A1,PSCOMP_1:88;
A7: W-min(X\/Y) in X\/Y by A1,SPRECT_1:15;
  per cases by A7,XBOOLE_0:def 3;
  suppose W-min(X\/Y) in X;
    then W-min(X\/Y) in W-most X by A2,A4,Th26,SPRECT_2:16;
    then (W-min(X\/Y))`2 >= (W-min X)`2 by PSCOMP_1:88;
    then (W-min(X\/Y))`2 = (W-min X)`2 by A6,XXREAL_0:1;
    hence thesis by A2,A4,Th26,TOPREAL3:11;
  end;
  suppose W-min(X\/Y) in Y;
    hence thesis by A2,A3,A4,PSCOMP_1:71;
  end;
end;

theorem
  for X,Y be non empty compact Subset of TOP-REAL 2 st
  W-bound X < W-bound Y holds W-max (X\/Y) = W-max X
proof
  let X,Y be non empty compact Subset of TOP-REAL 2;
A1: X\/Y is compact by COMPTS_1:19;
  assume
A2: W-bound X < W-bound Y;
  then
A3: W-bound (X\/Y) = W-bound X by Th26;
A4: (W-max(X\/Y))`1 = W-bound (X\/Y) & (W-max X)`1 = W-bound X by EUCLID:56;
A5: X c= X\/Y by XBOOLE_1:7;
  W-max X in X by SPRECT_1:15;
  then W-max X in W-most(X\/Y) by A1,A3,A4,A5,SPRECT_2:16;
  then
A6: (W-max(X\/Y))`2 >= (W-max X)`2 by A1,PSCOMP_1:88;
A7: W-max(X\/Y) in X\/Y by A1,SPRECT_1:15;
  per cases by A7,XBOOLE_0:def 3;
  suppose W-max(X\/Y) in X;
    then W-max(X\/Y) in W-most X by A2,A4,Th26,SPRECT_2:16;
    then (W-max(X\/Y))`2 <= (W-max X)`2 by PSCOMP_1:88;
    then (W-max(X\/Y))`2 = (W-max X)`2 by A6,XXREAL_0:1;
    hence thesis by A2,A4,Th26,TOPREAL3:11;
  end;
  suppose W-max(X\/Y) in Y;
    hence thesis by A2,A3,A4,PSCOMP_1:71;
  end;
end;

theorem Th35:
  for f be FinSequence of TOP-REAL 2
  for p be Point of TOP-REAL 2 st f is being_S-Seq & p in L~f holds
  L_Cut(f,p)/.len L_Cut(f,p) = f/.len f
proof
  let f be FinSequence of TOP-REAL 2;
  let p be Point of TOP-REAL 2;
  assume that
A1: f is being_S-Seq and
A2: p in L~f;
  len f > 0 by A1;
  then f <> {};
  then
A3: len f in dom f by FINSEQ_5:6;
  L_Cut(f,p) <> {} by A2,JORDAN1E:7;
  then len L_Cut(f,p) in dom L_Cut(f,p) by FINSEQ_5:6;
  hence L_Cut(f,p)/.len L_Cut(f,p) = L_Cut(f,p).len L_Cut(f,p)
  by PARTFUN1:def 8
    .= f.len f by A1,A2,JORDAN1B:5
    .= f/.len f by A3,PARTFUN1:def 8;
end;

theorem Th36:
  for f be non constant standard special_circular_sequence
  for p,q be Point of TOP-REAL 2 for g be connected Subset of TOP-REAL 2 st
  p in RightComp f & q in LeftComp f & p in g & q in g holds g meets L~f
proof
  let f be non constant standard special_circular_sequence;
  let p,q be Point of TOP-REAL 2;
  let g be connected Subset of TOP-REAL 2;
  assume that
A1: p in RightComp f & q in LeftComp f and
A2: p in g & q in g;
  assume g misses L~f;
  then g c= (L~f)` by TDLAT_1:2;
  then reconsider A = g as Subset of (TOP-REAL 2)|(L~f)` by PRE_TOPC:29;
A3: A is connected by CONNSP_1:24;
  RightComp f is_a_component_of (L~f)` by GOBOARD9:def 2;
  then consider R being Subset of (TOP-REAL 2)|(L~f)` such that
A4: R = RightComp f and
A5: R is_a_component_of (TOP-REAL 2)|(L~f)` by CONNSP_1:def 6;
  LeftComp f is_a_component_of (L~f)` by GOBOARD9:def 1;
  then consider L being Subset of (TOP-REAL 2)|(L~f)` such that
A6: L = LeftComp f and
A7: L is_a_component_of (TOP-REAL 2)|(L~f)` by CONNSP_1:def 6;
  R /\ A <> {} & L /\ A <> {} by A1,A2,A4,A6,XBOOLE_0:def 4;
  then R meets A & L meets A by XBOOLE_0:def 7;
  hence contradiction by A3,A4,A5,A6,A7,JORDAN2C:100,SPRECT_4:7;
end;

registration
  cluster non constant standard s.c.c.
    (being_S-Seq FinSequence of TOP-REAL 2);
  existence
  proof
    consider C be Simple_closed_curve;
    consider n be Element of NAT;
A1: len Upper_Seq(C,n) >= 2 by TOPREAL1:def 10;
A2: Upper_Seq(C,n) is_sequence_on Gauge(C,n) by JORDAN1G:4;
    take Upper_Seq(C,n);
    thus thesis by A1,A2,JGRAPH_1:16,JORDAN8:8;
  end;
end;

theorem Th37:
  for f be S-Sequence_in_R2 for p be Point of TOP-REAL 2 st p in rng f holds
  L_Cut(f,p) = mid(f,p..f,len f)
proof
  let f be S-Sequence_in_R2;
  let p be Point of TOP-REAL 2;
  assume p in rng f;
  then consider i be Nat such that
A1: i in dom f and
A2: f.i = p by FINSEQ_2:11;
A3: 0+1 <= i & i <= len f by A1,FINSEQ_3:27;
A4: len f >= 2 by TOPREAL1:def 10;
  per cases by A3,XXREAL_0:1;
  suppose i > 1;
    then
A5: Index(p,f) + 1 = i by A2,A3,JORDAN3:45;
    then L_Cut(f,p) = mid(f,Index(p,f)+1,len f) by A2,JORDAN3:def 4;
    hence thesis by A1,A2,A5,FINSEQ_5:12;
  end;
  suppose
A6: i = 1;
    thus L_Cut(f,p) = L_Cut(f,f/.i) by A1,A2,PARTFUN1:def 8
      .= f by A6,JORDAN5B:30
      .= mid(f,1,len f) by A4,JORDAN3:29,XXREAL_0:2
      .= mid(f,p..f,len f) by A1,A2,A6,FINSEQ_5:12;
  end;
end;

theorem Th38:
  for M be Go-board for f be S-Sequence_in_R2 st f is_sequence_on M
  for p be Point of TOP-REAL 2 st p in rng f holds R_Cut(f,p) is_sequence_on M
proof
  let M be Go-board;
  let f be S-Sequence_in_R2;
  assume
A1: f is_sequence_on M;
  let p be Point of TOP-REAL 2;
  assume p in rng f;
  then R_Cut(f,p) = mid(f,1,p..f) by JORDAN1G:57;
  hence thesis by A1,JORDAN1H:33;
end;

theorem Th39:
  for M be Go-board for f be S-Sequence_in_R2 st f is_sequence_on M
  for p be Point of TOP-REAL 2 st p in rng f holds L_Cut(f,p) is_sequence_on M
proof
  let M be Go-board;
  let f be S-Sequence_in_R2;
  assume
A1: f is_sequence_on M;
  let p be Point of TOP-REAL 2;
  assume p in rng f;
  then L_Cut(f,p) = mid(f,p..f,len f) by Th37;
  hence thesis by A1,JORDAN1H:33;
end;

theorem Th40:
  for G be Go-board for f be FinSequence of TOP-REAL 2 st f is_sequence_on G
  for i,j be Element of NAT st 1 <= i & i <= len G & 1 <= j & j <= width G
  holds G*(i,j) in L~f implies G*(i,j) in rng f
proof
  let G be Go-board;
  let f be FinSequence of TOP-REAL 2;
  assume
A1: f is_sequence_on G;
  let i,j be Element of NAT;
  assume
A2: 1 <= i & i <= len G & 1 <= j & j <= width G;
  assume G*(i,j) in L~f;
  then consider k be Element of NAT such that
A3: 1 <= k and
A4: k+1 <= len f and
A5: G*(i,j) in LSeg(f/.k,f/.(k+1)) by SPPOL_2:14;
  consider i1,j1,i2,j2 be Element of NAT such that
A6: [i1,j1] in Indices G and
A7: f/.k = G*(i1,j1) and
A8: [i2,j2] in Indices G and
A9: f/.(k+1) = G*(i2,j2) and
A10: i1 = i2 & j1+1 = j2 or i1+1 = i2 & j1 = j2 or
  i1 = i2+1 & j1 = j2 or i1 = i2 & j1 = j2+1 by A1,A3,A4,JORDAN8:6;
A11: 1 <= i1 & i1 <= len G & 1 <= j1 & j1 <= width G by A6,MATRIX_1:39;
A12: 1 <= i2 & i2 <= len G & 1 <= j2 & j2 <= width G by A8,MATRIX_1:39;
  k+1 >= 1 & k < len f by A4,NAT_1:11,13;
  then
A13: k in dom f & k+1 in dom f by A3,A4,FINSEQ_3:27;
  per cases by A10;
  suppose
A14: i1 = i2 & j1+1 = j2;
    j1 <= j1+1 by NAT_1:11;
    then G*(i1,j1)`1 <= G*(i1,j1+1)`1 & G*(i1,j1)`2 <= G*(i1,j1+1)`2
    by A11,A12,A14,JORDAN1A:39,40;
    then G*(i1,j1)`1 <= G*(i,j)`1 & G*(i,j)`1 <= G*(i1,j1+1)`1 &
    G*(i1,j1)`2 <= G*(i,j)`2 & G*(i,j)`2 <= G*(i1,j1+1)`2
    by A5,A7,A9,A14,TOPREAL1:9,10;
    then i1 <= i & i <= i1 & j1 <= j & j <= j1+1 by A2,A11,A12,A14,Th1,Th2;
    then i = i1 & (j = j1 or j = j1+1) by NAT_1:9,XXREAL_0:1;
    hence thesis by A7,A9,A13,A14,PARTFUN2:4;
  end;
  suppose
A15: i1+1 = i2 & j1 = j2;
    i1 <= i1+1 by NAT_1:11;
    then G*(i1,j1)`1 <= G*(i1+1,j1)`1 & G*(i1,j1)`2 <= G*(i1+1,j1)`2
    by A11,A12,A15,JORDAN1A:39,40;
    then G*(i1,j1)`1 <= G*(i,j)`1 & G*(i,j)`1 <= G*(i1+1,j1)`1 &
    G*(i1,j1)`2 <= G*(i,j)`2 & G*(i,j)`2 <= G*(i1+1,j1)`2
    by A5,A7,A9,A15,TOPREAL1:9,10;
    then j1 <= j & j <= j1 & i1 <= i & i <= i1+1 by A2,A11,A12,A15,Th1,Th2;
    then j = j1 & (i = i1 or i = i1+1) by NAT_1:9,XXREAL_0:1;
    hence thesis by A7,A9,A13,A15,PARTFUN2:4;
  end;
  suppose
A16: i1 = i2+1 & j1 = j2;
    i2 <= i2+1 by NAT_1:11;
    then G*(i2,j1)`1 <= G*(i2+1,j1)`1 & G*(i2,j1)`2 <= G*(i2+1,j1)`2
    by A11,A12,A16,JORDAN1A:39,40;
    then G*(i2,j1)`1 <= G*(i,j)`1 & G*(i,j)`1 <= G*(i2+1,j1)`1 &
    G*(i2,j1)`2 <= G*(i,j)`2 & G*(i,j)`2 <= G*(i2+1,j1)`2
    by A5,A7,A9,A16,TOPREAL1:9,10;
    then j1 <= j & j <= j1 & i2 <= i & i <= i2+1 by A2,A11,A12,A16,Th1,Th2;
    then j = j1 & (i = i2 or i = i2+1) by NAT_1:9,XXREAL_0:1;
    hence thesis by A7,A9,A13,A16,PARTFUN2:4;
  end;
  suppose
A17: i1 = i2 & j1 = j2+1;
    j2 <= j2+1 by NAT_1:11;
    then G*(i1,j2)`1 <= G*(i1,j2+1)`1 & G*(i1,j2)`2 <= G*(i1,j2+1)`2
    by A11,A12,A17,JORDAN1A:39,40;
    then G*(i1,j2)`1 <= G*(i,j)`1 & G*(i,j)`1 <= G*(i1,j2+1)`1 &
    G*(i1,j2)`2 <= G*(i,j)`2 & G*(i,j)`2 <= G*(i1,j2+1)`2
    by A5,A7,A9,A17,TOPREAL1:9,10;
    then i1 <= i & i <= i1 & j2 <= j & j <= j2+1 by A2,A11,A12,A17,Th1,Th2;
    then i = i1 & (j = j2 or j = j2+1) by NAT_1:9,XXREAL_0:1;
    hence thesis by A7,A9,A13,A17,PARTFUN2:4;
  end;
end;

theorem
  for f be S-Sequence_in_R2 for g be FinSequence of TOP-REAL 2 holds
  g is unfolded s.n.c. one-to-one & L~f /\ L~g = {f/.1} & f/.1 = g/.len g &
  (for i be Element of NAT st 1<=i & i+2 <= len f holds
  LSeg(f,i) /\ LSeg(f/.len f,g/.1) = {}) &
  (for i be Element of NAT st 2<=i & i+1 <= len g holds
  LSeg(g,i) /\ LSeg(f/.len f,g/.1) = {}) implies f^g is s.c.c.
proof
  let f be S-Sequence_in_R2;
  let g be FinSequence of TOP-REAL 2;
  assume that
A1: g is unfolded s.n.c. one-to-one and
A2: L~f /\ L~g = {f/.1} and
A3: f/.1 = g/.len g and
A4: for i be Element of NAT st 1<=i & i+2<=len f holds
  LSeg(f,i) /\ LSeg(f/.len f,g/.1) = {} and
A5: for i be Element of NAT st 2<=i & i+1 <= len g holds
  LSeg(g,i) /\ LSeg(f/.len f,g/.1) = {};
A6: now
    let i be Element of NAT;
    assume 1 <= i & i+2 <= len f;
    then LSeg(f,i) /\ LSeg(f/.len f,g/.1) = {} by A4;
    hence LSeg(f,i) misses LSeg(f/.len f,g/.1) by XBOOLE_0:def 7;
  end;
A7: now
    let i be Element of NAT;
    assume 2 <= i & i+1 <= len g;
    then LSeg(g,i) /\ LSeg(f/.len f,g/.1) = {} by A5;
    hence LSeg(g,i) misses LSeg(f/.len f,g/.1) by XBOOLE_0:def 7;
  end;
  let i,j be Element of NAT such that
A8: i+1 < j and
A9: i > 1 & j < len (f^g) or j+1 < len (f^g);
A10: j+1 <= len (f^g) by A9,NAT_1:13;
  per cases;
  suppose i = 0;
    then LSeg(f^g,i) = {} by TOPREAL1:def 5;
    then LSeg(f^g,i) /\ LSeg(f^g,j) = {};
    hence thesis by XBOOLE_0:def 7;
  end;
  suppose
A11: i <> 0;
A12: len(f^g) = len f + len g by FINSEQ_1:35;
    i <= i+1 by NAT_1:11;
    then
A13: i < j by A8,XXREAL_0:2;
    now per cases;
      suppose
A14:    j+1 <= len f;
        j <= j+1 by NAT_1:11;
        then i < j+1 by A13,XXREAL_0:2;
        then i < len f by A14,XXREAL_0:2;
        then i+1 <= len f by NAT_1:13;
        then
A15:    LSeg(f^g,i) = LSeg(f,i) by SPPOL_2:6;
        LSeg(f^g,j) = LSeg(f,j) by A14,SPPOL_2:6;
        hence thesis by A8,A15,TOPREAL1:def 9;
      end;
      suppose j+1 > len f;
        then
A16:    len f <= j by NAT_1:13;
        then reconsider j' = j - len f as Element of NAT by INT_1:18;
A17:    len f + j' = j;
A18:    j+1-len f <= len g by A10,A12,XREAL_1:22;
        then
A19:    j'+1 <= len g;
        now per cases;
          suppose
A20:        i <= len f;
            now per cases;
              suppose
A21:            i = len f;
                then len f +1+1 <= j by A8,NAT_1:13;
                then len f +(1+1) <= j;
                then
A22:            1+1 <= j' by XREAL_1:21;
A24:            LSeg(f^g,j) = LSeg(g,j') by A17,A22,SPPOL_2:7,XXREAL_0:2;
                g is non empty by A19;
                then LSeg(f^g,i) = LSeg(f/.len f,g/.1) by A21,SPPOL_2:8;
                hence thesis by A7,A19,A22,A24;
              end;
              suppose i <> len f;
                then i < len f by A20,XXREAL_0:1;
                then i+1 <= len f by NAT_1:13;
                then
A25:            LSeg(f^g,i) = LSeg(f,i) by SPPOL_2:6;
                now per cases;
                  suppose
A26:                j = len f;
                    then i+1+1 <= len f by A8,NAT_1:13;
                    then
A27:                i+(1+1) <= len f;
                    1 <= len g by A10,A12,A26,XREAL_1:8;
                    then g is non empty;
                    then LSeg(f^g,j) = LSeg(f/.len f,g/.1) by A26,SPPOL_2:8;
                    hence thesis by A6,A11,A25,A27,NAT_1:14;
                  end;
                  suppose j <> len f;
                    then len f < j by A16,XXREAL_0:1;
                    then len f+1 <= j by NAT_1:13;
                    then
A28:                1 <= j' by XREAL_1:21;
                    then
A29:                LSeg(f^g,len f+j') = LSeg(g,j') by SPPOL_2:7;
                    then
A30:                LSeg(f^g,j) c= L~g by TOPREAL3:26;
                    LSeg(f^g,i) c= L~f by A25,TOPREAL3:26;
                    then
A31:                LSeg(f^g,i) /\ LSeg(f^g,j) c= {f/. 1} by A2,A30,XBOOLE_1:27
                    ;
A32:                len f >= 2 by TOPREAL1:def 10;
                    now per cases by A31,ZFMISC_1:39;
                      suppose LSeg(f^g,i) /\ LSeg(f^g,j) = {};
                        hence thesis by XBOOLE_0:def 7;
                      end;
                      suppose LSeg(f^g,i) /\ LSeg(f^g,j) = {f/.1};

then f/.1 in LSeg(f^g,i) /\ LSeg(f^g,j) by TARSKI:def 1;
                        then
A33:                    f/.1 in LSeg(f^g,i) & f/.1 in LSeg(f^g,j)
                        by XBOOLE_0:def 4;
                        j'+1 >= 1 & j' < len g by A19,NAT_1:11,13;

then j' in dom g & j'+1 in dom g by A18,A28,FINSEQ_3:27;
                        then j'+1=len g by A1,A3,A29,A33,GOBOARD2:7;
                        hence thesis by A9,A12,A25,A32,A33,JORDAN5B:33;
                      end;
                    end;
                    hence thesis;
                  end;
                end;
                hence thesis;
              end;
            end;
            hence thesis;
          end;
          suppose
A34:        i > len f;
            then reconsider i' = i - len f as Element of NAT by INT_1:18;
            len f + 1 <= i by A34,NAT_1:13;
            then 1 <= i' by XREAL_1:21;
            then
A35:        LSeg(f^g,len f+i') = LSeg(g,i') by SPPOL_2:7;
            i+1-len f < j' by A8,XREAL_1:11;
            then
A36:        i'+1 < j';
            j <> len f by A8,A34,NAT_1:13;
            then len f < j by A16,XXREAL_0:1;
            then len f+1 <= j by NAT_1:13;
            then 1 <= j' by XREAL_1:21;
            then LSeg(f^g,len f+j') = LSeg(g,j') by SPPOL_2:7;
            hence thesis by A1,A35,A36,TOPREAL1:def 9;
          end;
        end;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
end;

theorem
  for C be compact non vertical non horizontal non empty Subset of TOP-REAL 2
  ex i be Element of NAT st 1 <= i & i+1 <= len Gauge(C,n) &
  W-min C in cell(Gauge(C,n),1,i) & W-min C <> Gauge(C,n)*(2,i)
proof
  let C be compact non vertical non horizontal non empty Subset of TOP-REAL 2;
  set G = Gauge(C,n);
A1: len G = width G by JORDAN8:def 1;
  defpred P[Nat] means 1 <= $1 & $1 < len G & G*(2,$1)`2 < (W-min C)`2;
A2: for k be Nat st P[k] holds k <= len G;
A3: len G = 2|^n+3 & len G = width G by JORDAN8:def 1;
A4: len G >= 4 by JORDAN8:13;
  then
A5: 1 < len G by XXREAL_0:2;
  (SW-corner C)`2 <= (W-min C)`2 by PSCOMP_1:87;
  then
A6: S-bound C <= (W-min C)`2 by EUCLID:56;
A7: 2 <= len G by A4,XXREAL_0:2;
  then G*(2,2)`2 = S-bound C by JORDAN8:16;
  then G*(2,1)`2 < S-bound C by A3,A7,GOBOARD5:5;
  then G*(2,1)`2 < (W-min C)`2 by A6,XXREAL_0:2;
  then
A8: ex k be Nat st P[k] by A5;
  ex i being Nat st P[i] &
  for n be Nat st P[n] holds n <= i from NAT_1:sch 6(A2,A8);
  then consider i being Nat such that
A9: 1 <= i & i < len G and
A10: G*(2,i)`2 < (W-min C)`2 and
A11: for n be Nat st P[n] holds n <= i;
  reconsider i as Element of NAT by ORDINAL1:def 13;
  take i;
  thus 1 <= i & i+1 <= len G by A9,NAT_1:13;
A12: LSeg(G*(1+1,i),G*(1+1,i+1)) c= cell(G,1,i) by A1,A5,A9,GOBOARD5:19;
A13: i+1 <= len G by A9,NAT_1:13;
A14: 1 <= i+1 by NAT_1:12;
  (W-min C)`1 = W-bound C by EUCLID:56;
  then
A15: G*(2,i)`1 = (W-min C)`1 & (W-min C)`1 = G*(2,i+1)`1
  by A9,A13,JORDAN8:14,NAT_1:12;
  now
    assume i+1 = len G;
    then len G-'1 = i by NAT_D:34;
    then
A16: G*(2,i)`2 = N-bound C by A7,JORDAN8:17;
    (NW-corner C)`2 >= (W-min C)`2 by PSCOMP_1:87;
    hence contradiction by A10,A16,EUCLID:56;
  end;
  then i+1 < len G & i < i+1 by A13,NAT_1:13,XXREAL_0:1;
  then (W-min C)`2 <= G*(2,i+1)`2 by A11,A14;
  then W-min C in LSeg(G*(2,i),G*(2,i+1)) by A10,A15,GOBOARD7:8;
  hence W-min C in cell(G,1,i) by A12;
  thus thesis by A10;
end;

theorem Th43:
  for f be S-Sequence_in_R2
  for p be Point of TOP-REAL 2 st p in L~f & f.len f in L~R_Cut(f,p) holds
  f.len f = p
proof
  let f be S-Sequence_in_R2;
  let p be Point of TOP-REAL 2;
  assume that
A1: p in L~f and
A2: f.len f in L~R_Cut(f,p);
A3: L~f = L~(Rev f) by SPPOL_2:22;
A4: (Rev f).1 = f.len f by FINSEQ_5:65;
  L_Cut(Rev f,p) = Rev R_Cut(f,p) by A1,JORDAN3:57;
  then (Rev f).1 in L~L_Cut(Rev f,p) by A2,A4,SPPOL_2:22;
  hence thesis by A1,A3,A4,JORDAN1E:11,SPPOL_2:47;
end;

theorem Th44:
  for f be FinSequence of TOP-REAL 2 for p be Point of TOP-REAL 2 holds
  R_Cut (f,p) <> {}
proof
  let f be FinSequence of TOP-REAL 2;
  let p be Point of TOP-REAL 2;
  per cases;
  suppose p<>f.1;
    then R_Cut (f,p) = mid(f,1,Index(p,f))^<*p*> by JORDAN3:def 5;
    hence thesis;
  end;
  suppose p=f.1;
    then R_Cut (f,p) = <*p*> by JORDAN3:def 5;
    hence thesis;
  end;
end;

theorem Th45:
  for f be FinSequence of TOP-REAL 2
  for p be Point of TOP-REAL 2 st p in L~f holds
  R_Cut(f,p)/.(len R_Cut(f,p)) = p
proof
  let f be FinSequence of TOP-REAL 2;
  let p be Point of TOP-REAL 2;
  assume
A1: p in L~f;
  R_Cut(f,p) is non empty by Th44;
  then len R_Cut(f,p) in dom R_Cut(f,p) by FINSEQ_5:6;
  hence R_Cut(f,p)/.len R_Cut(f,p) = R_Cut(f,p).len R_Cut(f,p)
  by PARTFUN1:def 8
    .= p by A1,JORDAN3:59;
end;

theorem Th46:
  for C be compact connected non vertical non horizontal Subset of TOP-REAL 2
  for p be Point of TOP-REAL 2 holds
  p in L~Upper_Seq(C,n) & p`1 = E-bound L~Cage(C,n) implies
  p = E-max L~Cage(C,n)
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
  let p be Point of TOP-REAL 2;
  set Ca = Cage(C,n);
  set US = Upper_Seq(C,n);
  set LS = Lower_Seq(C,n);
  set Wmin = W-min L~Ca;
  set Smax = S-max L~Ca;
  set Smin = S-min L~Ca;
  set Emin = E-min L~Ca;
  set Emax = E-max L~Ca;
  set Wbo = W-bound L~Cage(C,n);
  set Nbo = N-bound L~Cage(C,n);
  set Ebo = E-bound L~Cage(C,n);
  set Sbo = S-bound L~Cage(C,n);
  set NE = NE-corner L~Ca;
  assume that
A1: p in L~Upper_Seq(C,n) and
A2: p`1 = E-bound L~Cage(C,n) and
A3: p <> E-max L~Cage(C,n);
A4: 1 in dom US & 1 in dom LS by FINSEQ_5:6;
  US/.1 = Wmin by JORDAN1F:5;
  then
A5: US.1 = Wmin by A4,PARTFUN1:def 8;
  LS/.1 = Emax by JORDAN1F:6;
  then
A6: LS.1 = Emax by A4,PARTFUN1:def 8;
  Wbo <> Ebo by SPRECT_1:33;
  then p <> US.1 by A2,A5,EUCLID:56;
  then reconsider g = R_Cut(US,p) as being_S-Seq FinSequence of TOP-REAL 2
  by A1,JORDAN3:70;
  <*p*> is_in_the_area_of Ca by A1,JORDAN1E:21,SPRECT_3:63;
  then
A7: g is_in_the_area_of Ca by A1,JORDAN1E:21,SPRECT_3:69;
A8: (g/.1)`1 = (US/.1)`1 by A1,SPRECT_3:39
    .= Wmin`1 by JORDAN1F:5
    .= Wbo by EUCLID:56;
  len g in dom g by FINSEQ_5:6;
  then g/.len g = g.len g by PARTFUN1:def 8
    .= p by A1,JORDAN3:59;
  then
A9: g is_a_h.c._for Ca by A2,A7,A8,SPRECT_2:def 2;
  len Cage(C,n) > 4 by GOBOARD7:36;
  then
A10: rng Cage(C,n) c= L~Cage(C,n) by SPPOL_2:18,XXREAL_0:2;
  now per cases;
    suppose
A11:  Emax <> NE;
      set h = Rev R_Cut(LS,Smax)^<*NE*>;
A12:  not NE in rng Cage(C,n)
      proof
        assume
A13:    NE in rng Cage(C,n);
A14:    NE`1 = E-bound L~Cage(C,n) & NE`2 = N-bound L~Cage(C,n) by EUCLID:56;
        then NE`2 >= S-bound L~Cage(C,n) by SPRECT_1:24;
        then NE in { p1 where p1 is Point of TOP-REAL 2 :
        p1`1 = E-bound L~Cage(C,n) & p1`2 <= N-bound L~Cage(C,n) &
        p1`2 >= S-bound L~Cage(C,n) } by A14;
        then NE in LSeg(SE-corner L~Cage(C,n), NE-corner L~Cage(C,n))
        by SPRECT_1:25;
        then NE in LSeg(SE-corner L~Cage(C,n), NE-corner L~Cage(C,n)) /\
        L~Cage(C,n) by A10,A13,XBOOLE_0:def 4;
        then
A15:    NE`2 <= (E-max L~Cage(C,n))`2 by PSCOMP_1:108;
        (E-max L~Cage(C,n))`2 <= NE`2 by PSCOMP_1:107;
        then
A16:    (E-max L~Cage(C,n))`2 = NE`2 by A15,XXREAL_0:1;
        (E-max L~Cage(C,n))`1 = NE`1 by PSCOMP_1:105;
        hence contradiction by A11,A16,TOPREAL3:11;
      end;
      Smax in rng LS by Th12;
      then R_Cut(LS,Smax) = mid(LS,1,Smax..LS) by JORDAN1G:57;
      then
A17:  rng R_Cut(LS,Smax) c= rng LS by JORDAN3:28;
      rng LS c= rng Ca by JORDAN1G:47;
      then rng R_Cut(LS,Smax) c= rng Ca by A17,XBOOLE_1:1;
      then not NE in rng R_Cut(LS,Smax) by A12;
      then rng R_Cut(LS,Smax) misses {NE} by ZFMISC_1:56;
      then rng R_Cut(LS,Smax) misses rng <*NE*> by FINSEQ_1:55;
      then
A18:  rng Rev R_Cut(LS,Smax) misses rng <*NE*> by FINSEQ_5:60;
A19:  Smax in L~LS by Th12;
A20:  Emin`2 < Emax`2 by SPRECT_2:57;
      Emin in L~Ca by SPRECT_1:16;
      then Sbo <= Emin`2 by PSCOMP_1:71;
      then
A21:  Smax <> LS.1 by A6,A20,EUCLID:56;
      then reconsider RCutLS = R_Cut(LS,Smax) as
      being_S-Seq FinSequence of TOP-REAL 2 by A19,JORDAN3:70;
A22:  <*NE*> is one-to-one by FINSEQ_3:102;
A23:  Rev RCutLS is special by SPPOL_2:42;
A24:  <*NE*> is special by SPPOL_2:39;
A25:  Rev RCutLS/.len Rev RCutLS = Rev RCutLS/.len RCutLS by FINSEQ_5:def 3
        .= RCutLS/.1 by FINSEQ_5:68
        .= LS/.1 by A19,SPRECT_3:39
        .= Emax by JORDAN1F:6;
      then (Rev RCutLS/.len Rev RCutLS)`1 = E-bound L~Ca by EUCLID:56
        .= NE`1 by EUCLID:56
        .= (<*NE*>/.1)`1 by FINSEQ_4:25;
      then
A26:  h is one-to-one special by A18,A22,A23,A24,FINSEQ_3:98,GOBOARD2:13;
A27:  2 <= len g by TOPREAL1:def 10;
A28:  len h = len Rev R_Cut(LS,Smax) + 1 by FINSEQ_2:19
        .= len R_Cut(LS,Smax) + 1 by FINSEQ_5:def 3
        .= Index(Smax,LS)+1+1 by A19,A21,JORDAN3:60;
      Index(Smax,LS)+1 >= 0+1 by XREAL_1:9;
      then
A29:  len h >= 1+1 by A28,XREAL_1:9;
      <*Smax*> is_in_the_area_of Ca by A19,JORDAN1E:22,SPRECT_3:63;
      then R_Cut(LS,Smax) is_in_the_area_of Ca by A19,JORDAN1E:22,SPRECT_3:69;
      then
A30:  Rev R_Cut(LS,Smax) is_in_the_area_of Ca by SPRECT_3:68;
      <*NE*> is_in_the_area_of Ca by SPRECT_2:29;
      then
A31:  h is_in_the_area_of Ca by A30,SPRECT_2:28;
      1 in dom Rev RCutLS by FINSEQ_5:6;
      then h/.1 = Rev RCutLS/.1 by FINSEQ_4:83
        .= R_Cut(LS,Smax)/.len R_Cut(LS,Smax) by FINSEQ_5:68
        .= Smax by A19,Th45;
      then
A32:  (h/.1)`2 = Sbo by EUCLID:56;
      (h/.len h)`2 = (h/.(len Rev R_Cut(LS,Smax)+1))`2 by FINSEQ_2:19
        .= NE`2 by FINSEQ_4:82
        .= Nbo by EUCLID:56;
      then h is_a_v.c._for Ca by A31,A32,SPRECT_2:def 3;
      then L~g meets L~h by A9,A26,A27,A29,SPRECT_2:33;
      then consider x be set such that
A33:  x in L~g and
A34:  x in L~h by XBOOLE_0:3;
      reconsider x as Point of TOP-REAL 2 by A33;
A35:  L~g c= L~US by A1,JORDAN3:76;
      then
A36:  x in L~US by A33;
A37:  L~h = L~Rev RCutLS \/ LSeg(Rev RCutLS/.len Rev RCutLS,NE) by SPPOL_2:19;
A38:  L~RCutLS c= L~LS by A19,JORDAN3:76;
A39:  len US in dom US & len LS in dom LS by FINSEQ_5:6;
      now per cases by A34,A37,XBOOLE_0:def 3;
        suppose x in L~Rev RCutLS;
          then
A40:      x in L~RCutLS by SPPOL_2:22;
          then x in L~US /\ L~LS by A33,A35,A38,XBOOLE_0:def 4;
          then
A41:      x in {Wmin,Emax} by JORDAN1E:20;
          now per cases by A41,TARSKI:def 2;
            suppose x = Wmin;
              then LS/.len LS in L~R_Cut(LS,Smax) by A40,JORDAN1F:8;
              then LS.len LS in L~R_Cut(LS,Smax) by A39,PARTFUN1:def 8;
              then LS.len LS = Smax by A19,Th43;
              then LS/.len LS = Smax by A39,PARTFUN1:def 8;
              then
A42:          Wmin = Smax by JORDAN1F:8;
A43:          Smin`1 < Smax`1 by SPRECT_2:59;
              Smin in L~Ca by SPRECT_1:14;
              then Wbo <= Smin`1 by PSCOMP_1:71;
              hence contradiction by A42,A43,EUCLID:56;
            end;
            suppose x = Emax;
              then US/.len US in L~R_Cut(US,p) by A33,JORDAN1F:7;
              then US.len US in L~R_Cut(US,p) by A39,PARTFUN1:def 8;
              then US.len US = p by A1,Th43;
              then US/.len US = p by A39,PARTFUN1:def 8;
              hence contradiction by A3,JORDAN1F:7;
            end;
          end;
          hence contradiction;
        end;
        suppose
A44:      x in LSeg(Rev RCutLS/.len Rev RCutLS,NE);
A45:      Emax`1 = Ebo by EUCLID:56;
          NE`1 = Ebo by EUCLID:56;
          then
A46:      x`1 = Ebo by A25,A44,A45,GOBOARD7:5;
          Emax`2 <= NE`2 by PSCOMP_1:107;
          then
A47:      Emax`2 <= x`2 by A25,A44,TOPREAL1:10;
          L~Ca = L~US \/ L~LS by JORDAN1E:17;
          then L~US c= L~Ca by XBOOLE_1:7;
          then x in E-most L~Ca by A36,A46,SPRECT_2:17;
          then x`2 <= Emax`2 by PSCOMP_1:108;
          then x`2 = Emax`2 by A47,XXREAL_0:1;
          then x = Emax by A45,A46,TOPREAL3:11;
          then US/.len US in L~R_Cut(US,p) by A33,JORDAN1F:7;
          then US.len US in L~R_Cut(US,p) by A39,PARTFUN1:def 8;
          then US.len US = p by A1,Th43;
          then US/.len US = p by A39,PARTFUN1:def 8;
          hence contradiction by A3,JORDAN1F:7;
        end;
      end;
      hence contradiction;
    end;
    suppose
A48:  Emax = NE;
      set h = Rev R_Cut(LS,Smax);
A49:  Smax in L~LS by Th12;
A50:  Emin`2 < Emax`2 by SPRECT_2:57;
      Emin in L~Ca by SPRECT_1:16;
      then Sbo <= Emin`2 by PSCOMP_1:71;
      then Smax <> LS.1 by A6,A50,EUCLID:56;
      then reconsider RCutLS = R_Cut(LS,Smax) as
      being_S-Seq FinSequence of TOP-REAL 2 by A49,JORDAN3:70;
A51:  Rev RCutLS is special by SPPOL_2:42;
A52:  Rev RCutLS/.len Rev RCutLS = Rev RCutLS/.len RCutLS by FINSEQ_5:def 3
        .= RCutLS/.1 by FINSEQ_5:68
        .= LS/.1 by A49,SPRECT_3:39
        .= Emax by JORDAN1F:6;
A53:  2 <= len g by TOPREAL1:def 10;
      len RCutLS >= 2 by TOPREAL1:def 10;
      then
A54:  len h >= 2 by FINSEQ_5:def 3;
      <*Smax*> is_in_the_area_of Ca by A49,JORDAN1E:22,SPRECT_3:63;
      then R_Cut(LS,Smax) is_in_the_area_of Ca by A49,JORDAN1E:22,SPRECT_3:69;
      then
A55:  h is_in_the_area_of Ca by SPRECT_3:68;
      1 in dom Rev RCutLS by FINSEQ_5:6;
      then h/.1 = R_Cut(LS,Smax)/.len R_Cut(LS,Smax) by FINSEQ_5:68
        .= Smax by A49,Th45;
      then
A56:  (h/.1)`2 = Sbo by EUCLID:56;
      (h/.len h)`2 = Nbo by A48,A52,EUCLID:56;
      then h is_a_v.c._for Ca by A55,A56,SPRECT_2:def 3;
      then L~g meets L~h by A9,A51,A53,A54,SPRECT_2:33;
      then consider x be set such that
A57:  x in L~g and
A58:  x in L~h by XBOOLE_0:3;
      reconsider x as Point of TOP-REAL 2 by A57;
A59:  L~g c= L~US by A1,JORDAN3:76;
A60:  L~RCutLS c= L~LS by A49,JORDAN3:76;
A61:  len US in dom US & len LS in dom LS by FINSEQ_5:6;
A62:  x in L~RCutLS by A58,SPPOL_2:22;
      then x in L~US /\ L~LS by A57,A59,A60,XBOOLE_0:def 4;
      then
A63:  x in {Wmin,Emax} by JORDAN1E:20;
      now per cases by A63,TARSKI:def 2;
        suppose x = Wmin;
          then LS/.len LS in L~R_Cut(LS,Smax) by A62,JORDAN1F:8;
          then LS.len LS in L~R_Cut(LS,Smax) by A61,PARTFUN1:def 8;
          then LS.len LS = Smax by A49,Th43;
          then LS/.len LS = Smax by A61,PARTFUN1:def 8;
          then
A64:      Wmin = Smax by JORDAN1F:8;
A65:      Smin`1 < Smax`1 by SPRECT_2:59;
          Smin in L~Ca by SPRECT_1:14;
          then Wbo <= Smin`1 by PSCOMP_1:71;
          hence contradiction by A64,A65,EUCLID:56;
        end;
        suppose x = Emax;
          then US/.len US in L~R_Cut(US,p) by A57,JORDAN1F:7;
          then US.len US in L~R_Cut(US,p) by A61,PARTFUN1:def 8;
          then US.len US = p by A1,Th43;
          then US/.len US = p by A61,PARTFUN1:def 8;
          hence contradiction by A3,JORDAN1F:7;
        end;
      end;
      hence contradiction;
    end;
  end;
  hence contradiction;
end;

theorem
  for C be compact connected non vertical non horizontal Subset of TOP-REAL 2
  for p be Point of TOP-REAL 2 holds
  p in L~Lower_Seq(C,n) & p`1 = W-bound L~Cage(C,n) implies
  p = W-min L~Cage(C,n)
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
  let p be Point of TOP-REAL 2;
  set Ca = Cage(C,n);
  set LS = Lower_Seq(C,n);
  set US = Upper_Seq(C,n);
  set Emax = E-max L~Ca;
  set Nmin = N-min L~Ca;
  set Nmax = N-max L~Ca;
  set Wmax = W-max L~Ca;
  set Wmin = W-min L~Ca;
  set Ebo = E-bound L~Cage(C,n);
  set Sbo = S-bound L~Cage(C,n);
  set Wbo = W-bound L~Cage(C,n);
  set Nbo = N-bound L~Cage(C,n);
  set SW = SW-corner L~Ca;
  assume that
A1: p in L~Lower_Seq(C,n) and
A2: p`1 = W-bound L~Cage(C,n) and
A3: p <> W-min L~Cage(C,n);
A4: 1 in dom LS & 1 in dom US by FINSEQ_5:6;
  LS/.1 = Emax by JORDAN1F:6;
  then
A5: LS.1 = Emax by A4,PARTFUN1:def 8;
  US/.1 = Wmin by JORDAN1F:5;
  then
A6: US.1 = Wmin by A4,PARTFUN1:def 8;
  Ebo <> Wbo by SPRECT_1:33;
  then p <> LS.1 by A2,A5,EUCLID:56;
  then reconsider g1 = R_Cut(LS,p) as being_S-Seq FinSequence of TOP-REAL 2
  by A1,JORDAN3:70;
  reconsider g = Rev g1 as being_S-Seq FinSequence of TOP-REAL 2
  by SPPOL_2:47;
A7: L~g = L~g1 by SPPOL_2:22;
  <*p*> is_in_the_area_of Ca by A1,JORDAN1E:22,SPRECT_3:63;
  then g1 is_in_the_area_of Ca by A1,JORDAN1E:22,SPRECT_3:69;
  then
A8: g is_in_the_area_of Ca by SPRECT_3:68;
A9: g/.1 = g1/.len g1 by FINSEQ_5:68;
A10: g/.len g = g/.len g1 by FINSEQ_5:def 3
    .= g1/.1 by FINSEQ_5:68;
A11: (g1/.1)`1 = (LS/.1)`1 by A1,SPRECT_3:39
    .= Emax`1 by JORDAN1F:6
    .= Ebo by EUCLID:56;
  len g1 in dom g1 by FINSEQ_5:6;
  then g1/.len g1 = g1.len g1 by PARTFUN1:def 8
    .= p by A1,JORDAN3:59;
  then
A12: g is_a_h.c._for Ca by A2,A8,A9,A10,A11,SPRECT_2:def 2;
  len Cage(C,n) > 4 by GOBOARD7:36;
  then
A13: rng Cage(C,n) c= L~Cage(C,n) by SPPOL_2:18,XXREAL_0:2;
  now per cases;
    suppose
A14:  Wmin <> SW;
      set h1 = Rev R_Cut(US,Nmin)^<*SW*>;
A15:  not SW in rng Cage(C,n)
      proof
        assume
A16:    SW in rng Cage(C,n);
A17:    SW`1 = W-bound L~Cage(C,n) & SW`2 = S-bound L~Cage(C,n) by EUCLID:56;
        then SW`2 <= N-bound L~Cage(C,n) by SPRECT_1:24;
        then SW in { p1 where p1 is Point of TOP-REAL 2 :
        p1`1 = W-bound L~Cage(C,n) & p1`2 <= N-bound L~Cage(C,n) &
        p1`2 >= S-bound L~Cage(C,n) } by A17;
        then SW in LSeg(SW-corner L~Cage(C,n), NW-corner L~Cage(C,n))
        by SPRECT_1:28;
        then SW in LSeg(SW-corner L~Cage(C,n), NW-corner L~Cage(C,n)) /\
        L~Cage(C,n) by A13,A16,XBOOLE_0:def 4;
        then
A18:    SW`2 >= (W-min L~Cage(C,n))`2 by PSCOMP_1:88;
        (W-min L~Cage(C,n))`2 >= SW`2 by PSCOMP_1:87;
        then
A19:    (W-min L~Cage(C,n))`2 = SW`2 by A18,XXREAL_0:1;
        (W-min L~Cage(C,n))`1 = SW`1 by PSCOMP_1:85;
        hence contradiction by A14,A19,TOPREAL3:11;
      end;
      Nmin in rng US by Th7;
      then R_Cut(US,Nmin) = mid(US,1,Nmin..US) by JORDAN1G:57;
      then
A20:  rng R_Cut(US,Nmin) c= rng US by JORDAN3:28;
      rng US c= rng Ca by JORDAN1G:47;
      then rng R_Cut(US,Nmin) c= rng Ca by A20,XBOOLE_1:1;
      then not SW in rng R_Cut(US,Nmin) by A15;
      then rng R_Cut(US,Nmin) misses {SW} by ZFMISC_1:56;
      then rng R_Cut(US,Nmin) misses rng <*SW*> by FINSEQ_1:55;
      then
A21:  rng Rev R_Cut(US,Nmin) misses rng <*SW*> by FINSEQ_5:60;
A22:  Nmin in L~US by Th7;
A23:  Wmax`2 > Wmin`2 by SPRECT_2:61;
      Wmax in L~Ca by SPRECT_1:15;
      then Nbo >= Wmax`2 by PSCOMP_1:71;
      then
A24:  Nmin <> US.1 by A6,A23,EUCLID:56;
      then reconsider RCutUS = R_Cut(US,Nmin) as
      being_S-Seq FinSequence of TOP-REAL 2 by A22,JORDAN3:70;
A25:  <*SW*> is one-to-one by FINSEQ_3:102;
A26:  Rev RCutUS is special by SPPOL_2:42;
A27:  <*SW*> is special by SPPOL_2:39;
A28:  Rev RCutUS/.len Rev RCutUS = Rev RCutUS/.len RCutUS by FINSEQ_5:def 3
        .= RCutUS/.1 by FINSEQ_5:68
        .= US/.1 by A22,SPRECT_3:39
        .= Wmin by JORDAN1F:5;
      then (Rev RCutUS/.len Rev RCutUS)`1 = W-bound L~Ca by EUCLID:56
        .= SW`1 by EUCLID:56
        .= (<*SW*>/.1)`1 by FINSEQ_4:25;
      then reconsider h1 as one-to-one special FinSequence of TOP-REAL 2
      by A21,A25,A26,A27,FINSEQ_3:98,GOBOARD2:13;
      set h = Rev h1;
A29:  h is special by SPPOL_2:42;
A30:  2 <= len g by TOPREAL1:def 10;
A31:  len h1 = len Rev R_Cut(US,Nmin) + 1 by FINSEQ_2:19
        .= len R_Cut(US,Nmin) + 1 by FINSEQ_5:def 3
        .= Index(Nmin,US)+1+1 by A22,A24,JORDAN3:60;
      Index(Nmin,US)+1 >= 0+1 by XREAL_1:9;
      then len h1 >= 1+1 by A31,XREAL_1:9;
      then
A32:  len h >= 2 by FINSEQ_5:def 3;
      <*Nmin*> is_in_the_area_of Ca by A22,JORDAN1E:21,SPRECT_3:63;
      then R_Cut(US,Nmin) is_in_the_area_of Ca by A22,JORDAN1E:21,SPRECT_3:69;
      then
A33:  Rev R_Cut(US,Nmin) is_in_the_area_of Ca by SPRECT_3:68;
      <*SW*> is_in_the_area_of Ca by SPRECT_2:32;
      then h1 is_in_the_area_of Ca by A33,SPRECT_2:28;
      then
A34:  h is_in_the_area_of Ca by SPRECT_3:68;
      1 in dom Rev RCutUS by FINSEQ_5:6;
      then h1/.1 = Rev RCutUS/.1 by FINSEQ_4:83
        .= R_Cut(US,Nmin)/.len R_Cut(US,Nmin) by FINSEQ_5:68
        .= Nmin by A22,Th45;
      then
A35:  (h1/.1)`2 = Nbo by EUCLID:56;
A36:  h/.len h = h/.len h1 by FINSEQ_5:def 3
        .= h1/.1 by FINSEQ_5:68;
A37:  h/.1 = h1/.len h1 by FINSEQ_5:68;
      (h1/.len h1)`2 = (h1/.(len Rev R_Cut(US,Nmin)+1))`2 by FINSEQ_2:19
        .= SW`2 by FINSEQ_4:82
        .= Sbo by EUCLID:56;
      then h is_a_v.c._for Ca by A34,A35,A36,A37,SPRECT_2:def 3;
      then L~g meets L~h by A12,A29,A30,A32,SPRECT_2:33;
      then consider x be set such that
A38:  x in L~g and
A39:  x in L~h by XBOOLE_0:3;
      reconsider x as Point of TOP-REAL 2 by A38;
A40:  L~g c= L~LS by A1,A7,JORDAN3:76;
      then
A41:  x in L~LS by A38;
      L~h = L~h1 by SPPOL_2:22;
      then
A42:  L~h = L~Rev RCutUS \/ LSeg(Rev RCutUS/.len Rev RCutUS,SW) by SPPOL_2:19;
A43:  L~RCutUS c= L~US by A22,JORDAN3:76;
A44:  len LS in dom LS & len US in dom US by FINSEQ_5:6;
      now per cases by A39,A42,XBOOLE_0:def 3;
        suppose x in L~Rev RCutUS;
          then
A45:      x in L~RCutUS by SPPOL_2:22;
          then x in L~LS /\ L~US by A38,A40,A43,XBOOLE_0:def 4;
          then
A46:      x in {Emax,Wmin} by JORDAN1E:20;
          now per cases by A46,TARSKI:def 2;
            suppose x = Emax;
              then US/.len US in L~R_Cut(US,Nmin) by A45,JORDAN1F:7;
              then US.len US in L~R_Cut(US,Nmin) by A44,PARTFUN1:def 8;
              then US.len US = Nmin by A22,Th43;
              then US/.len US = Nmin by A44,PARTFUN1:def 8;
              then
A47:          Emax = Nmin by JORDAN1F:7;
A48:          Nmax`1 > Nmin`1 by SPRECT_2:55;
              Nmax in L~Ca by SPRECT_1:13;
              then Ebo >= Nmax`1 by PSCOMP_1:71;
              hence contradiction by A47,A48,EUCLID:56;
            end;
            suppose x = Wmin;
              then LS/.len LS in L~R_Cut(LS,p) by A7,A38,JORDAN1F:8;
              then LS.len LS in L~R_Cut(LS,p) by A44,PARTFUN1:def 8;
              then LS.len LS = p by A1,Th43;
              then LS/.len LS = p by A44,PARTFUN1:def 8;
              hence contradiction by A3,JORDAN1F:8;
            end;
          end;
          hence contradiction;
        end;
        suppose
A49:      x in LSeg(Rev RCutUS/.len Rev RCutUS,SW);
A50:      Wmin`1 = Wbo by EUCLID:56;
          SW`1 = Wbo by EUCLID:56;
          then
A51:      x`1 = Wbo by A28,A49,A50,GOBOARD7:5;
          Wmin`2 >= SW`2 by PSCOMP_1:87;
          then
A52:      Wmin`2 >= x`2 by A28,A49,TOPREAL1:10;
          L~Ca = L~LS \/ L~US by JORDAN1E:17;
          then L~LS c= L~Ca by XBOOLE_1:7;
          then x in W-most L~Ca by A41,A51,SPRECT_2:16;
          then x`2 >= Wmin`2 by PSCOMP_1:88;
          then x`2 = Wmin`2 by A52,XXREAL_0:1;
          then x = Wmin by A50,A51,TOPREAL3:11;
          then LS/.len LS in L~R_Cut(LS,p) by A7,A38,JORDAN1F:8;
          then LS.len LS in L~R_Cut(LS,p) by A44,PARTFUN1:def 8;
          then LS.len LS = p by A1,Th43;
          then LS/.len LS = p by A44,PARTFUN1:def 8;
          hence contradiction by A3,JORDAN1F:8;
        end;
      end;
      hence contradiction;
    end;
    suppose
A53:  Wmin = SW;
      set h = R_Cut(US,Nmin);
A54:  Nmin in L~US by Th7;
A55:  Wmax`2 > Wmin`2 by SPRECT_2:61;
      Wmax in L~Ca by SPRECT_1:15;
      then Nbo >= Wmax`2 by PSCOMP_1:71;
      then Nmin <> US.1 by A6,A55,EUCLID:56;
      then reconsider RCutUS = R_Cut(US,Nmin) as
      being_S-Seq FinSequence of TOP-REAL 2 by A54,JORDAN3:70;
A56:  RCutUS/.1 = US/.1 by A54,SPRECT_3:39
        .= Wmin by JORDAN1F:5;
A57:  2 <= len g by TOPREAL1:def 10;
A58:  len RCutUS >= 2 by TOPREAL1:def 10;
      <*Nmin*> is_in_the_area_of Ca by A54,JORDAN1E:21,SPRECT_3:63;
      then
A59:  R_Cut(US,Nmin) is_in_the_area_of Ca by A54,JORDAN1E:21,SPRECT_3:69;
      R_Cut(US,Nmin)/.len R_Cut(US,Nmin) = Nmin by A54,Th45;
      then
A60:  (h/.len h)`2 = Nbo by EUCLID:56;
      (h/.1)`2 = Sbo by A53,A56,EUCLID:56;
      then h is_a_v.c._for Ca by A59,A60,SPRECT_2:def 3;
      then L~g meets L~h by A12,A57,A58,SPRECT_2:33;
      then consider x be set such that
A61:  x in L~g and
A62:  x in L~h by XBOOLE_0:3;
      reconsider x as Point of TOP-REAL 2 by A61;
A63:  L~g c= L~LS by A1,A7,JORDAN3:76;
A64:  L~RCutUS c= L~US by A54,JORDAN3:76;
A65:  len LS in dom LS & len US in dom US by FINSEQ_5:6;
      x in L~LS /\ L~US by A61,A62,A63,A64,XBOOLE_0:def 4;
      then
A66:  x in {Emax,Wmin} by JORDAN1E:20;
      now per cases by A66,TARSKI:def 2;
        suppose x = Emax;
          then US/.len US in L~R_Cut(US,Nmin) by A62,JORDAN1F:7;
          then US.len US in L~R_Cut(US,Nmin) by A65,PARTFUN1:def 8;
          then US.len US = Nmin by A54,Th43;
          then US/.len US = Nmin by A65,PARTFUN1:def 8;
          then
A67:      Emax = Nmin by JORDAN1F:7;
A68:      Nmax`1 > Nmin`1 by SPRECT_2:55;
          Nmax in L~Ca by SPRECT_1:13;
          then Ebo >= Nmax`1 by PSCOMP_1:71;
          hence contradiction by A67,A68,EUCLID:56;
        end;
        suppose x = Wmin;
          then LS/.len LS in L~R_Cut(LS,p) by A7,A61,JORDAN1F:8;
          then LS.len LS in L~R_Cut(LS,p) by A65,PARTFUN1:def 8;
          then LS.len LS = p by A1,Th43;
          then LS/.len LS = p by A65,PARTFUN1:def 8;
          hence contradiction by A3,JORDAN1F:8;
        end;
      end;
      hence contradiction;
    end;
  end;
  hence contradiction;
end;

theorem
  for G be Go-board for f,g be FinSequence of TOP-REAL 2
  for k be Element of NAT holds
  1 <= k & k < len f & f^g is_sequence_on G implies
  left_cell(f^g,k,G) = left_cell(f,k,G) &
  right_cell(f^g,k,G) = right_cell(f,k,G)
proof
  let G be Go-board;
  let f,g be FinSequence of TOP-REAL 2;
  let k be Element of NAT;
  assume that
A1: 1 <= k and
A2: k < len f and
A3: f^g is_sequence_on G;
A4: (f^g)|len f = f by FINSEQ_5:26;
A5: k+1 <= len f by A2,NAT_1:13;
  len f <= len f + len g by NAT_1:11;
  then len f <= len (f^g) by FINSEQ_1:35;
  then k+1 <= len (f^g) by A5,XXREAL_0:2;
  hence thesis by A1,A3,A4,A5,GOBRD13:32;
end;

theorem Th49:
  for D be set for f,g be FinSequence of D
  for i be Element of NAT st i <= len f holds (f^'g)|i = f|i
proof
  let D be set;
  let f,g be FinSequence of D;
  let i be Element of NAT;
  assume
A1: i <= len f;
  then
A2: len(f|i) = i by FINSEQ_1:80;
  per cases;
  suppose
A3: g <> {};
    then len g <> 0;
    then len g > 0;
    then len g >= 0+1 by NAT_1:13;
    then i+1 <= len f+len g by A1,XREAL_1:9;
    then i+1 <= len(f^'g)+1 by A3,GRAPH_2:13;
    then i <= len(f^'g) by XREAL_1:8;
    then
A4: len((f^'g)|i) = i by FINSEQ_1:80;
   then
X: dom ((f^'g)|i) = Seg i by FINSEQ_1:def 3;
    now
      let j be Nat;
A5:   j in NAT by ORDINAL1:def 13;
      assume
A6:   j in dom((f^'g)|i);
      then j <= i by X,FINSEQ_1:3;
      then
A7:   1 <= j & j <= len f by A1,A6,X,FINSEQ_1:3,XXREAL_0:2;
      thus ((f^'g)|i).j = ((f^'g)|(Seg i)).j by FINSEQ_1:def 15
        .= (f^'g).j by A6,X,FUNCT_1:72
        .= f.j by A5,A7,GRAPH_2:14
        .= (f|(Seg i)).j by A6,X,FUNCT_1:72
        .= (f|i).j by FINSEQ_1:def 15;
    end;
    hence thesis by A2,A4,FINSEQ_2:10;
  end;
  suppose g = {};
    hence thesis by GRAPH_2:59;
  end;
end;

theorem Th50:
  for D be set for f,g be FinSequence of D holds (f^'g)|(len f) = f
proof
  let D be set;
  let f,g be FinSequence of D;
  f|len f = f|(Seg len f) by FINSEQ_1:def 15;
  hence thesis by Th49,FINSEQ_2:23;
end;

theorem Th51:
  for G be Go-board for f,g be FinSequence of TOP-REAL 2
  for k be Element of NAT holds
  1 <= k & k < len f & f^'g is_sequence_on G implies
  left_cell(f^'g,k,G) = left_cell(f,k,G) &
  right_cell(f^'g,k,G) = right_cell(f,k,G)
proof
  let G be Go-board;
  let f,g be FinSequence of TOP-REAL 2;
  let k be Element of NAT;
  assume that
A1: 1 <= k and
A2: k < len f and
A3: f^'g is_sequence_on G;
A4: (f^'g)|len f = f by Th50;
A5: k+1 <= len f by A2,NAT_1:13;
  len f <= len (f^'g) by TOPREAL8:7;
  then k+1 <= len (f^'g) by A5,XXREAL_0:2;
  hence thesis by A1,A3,A4,A5,GOBRD13:32;
end;

theorem Th52:
  for G be Go-board for f be S-Sequence_in_R2 for p be Point of TOP-REAL 2
  for k be Element of NAT
  st 1 <= k & k < p..f & f is_sequence_on G & p in rng f holds
  left_cell(R_Cut(f,p),k,G) = left_cell(f,k,G) &
  right_cell(R_Cut(f,p),k,G) = right_cell(f,k,G)
proof
  let G be Go-board;
  let f be S-Sequence_in_R2;
  let p be Point of TOP-REAL 2;
  let k be Element of NAT;
  assume that
A1: 1 <= k and
A2: k < p..f and
A3: f is_sequence_on G and
A4: p in rng f;
A5: f|(p..f) = mid(f,1,p..f) by A1,A2,JORDAN3:25,XXREAL_0:2
    .= R_Cut(f,p) by A4,JORDAN1G:57;
A6: k+1 <= p..f by A2,NAT_1:13;
  p..f <= len f by A4,FINSEQ_4:31;
  then k+1 <= len f by A6,XXREAL_0:2;
  hence thesis by A1,A3,A5,A6,GOBRD13:32;
end;

theorem Th53:
  for G be Go-board for f be FinSequence of TOP-REAL 2
  for p be Point of TOP-REAL 2
  for k be Element of NAT st 1 <= k & k < p..f & f is_sequence_on G holds
  left_cell(f-:p,k,G) = left_cell(f,k,G) &
  right_cell(f-:p,k,G) = right_cell(f,k,G)
proof
  let G be Go-board;
  let f be FinSequence of TOP-REAL 2;
  let p be Point of TOP-REAL 2;
  let k be Element of NAT;
  assume that
A1: 1 <= k and
A2: k < p..f and
A3: f is_sequence_on G;
A4: f|(p..f) = f-:p by FINSEQ_5:def 1;
A5: k+1 <= p..f by A2,NAT_1:13;
  per cases by TOPREAL8:4;
  suppose p in rng f;
    then p..f <= len f by FINSEQ_4:31;
    then k+1 <= len f by A5,XXREAL_0:2;
    hence thesis by A1,A3,A4,A5,GOBRD13:32;
  end;
  suppose p..f = 0;
    hence thesis by A2;
  end;
end;

theorem Th54:
  for f,g be FinSequence of TOP-REAL 2 st
  f is unfolded s.n.c. one-to-one & g is unfolded s.n.c. one-to-one &
  f/.len f = g/.1 & L~f /\ L~g = {g/.1} holds f^'g is s.n.c.
proof
  let f,g be FinSequence of TOP-REAL 2;
  assume that
A1: f is unfolded s.n.c. one-to-one and
A2: g is unfolded s.n.c. one-to-one and
A3: f/.len f = g/.1 and
A4: L~f /\ L~g = {g/.1};
  now
    let i,j be Nat;
    assume
A5: i+1 < j;
A6: i in NAT & j in NAT by ORDINAL1:def 13;
    now per cases;
      suppose
A7:     j < len f;
        then
A8:     LSeg(f^'g,j) = LSeg(f,j) by A6,TOPREAL8:28;
        i+1 < len f by A5,A7,XXREAL_0:2;
        then i < len f by NAT_1:13;
        then LSeg(f^'g,i) = LSeg(f,i) by A6,TOPREAL8:28;
        hence LSeg(f^'g,i) misses LSeg(f^'g,j) by A1,A5,A8,TOPREAL1:def 9;
      end;
      suppose j >= len f;
        then consider k be Nat such that
A9:     j = len f + k by NAT_1:10;
        reconsider k as Element of NAT by ORDINAL1:def 13;
A10:    now
          assume f is empty;
          then len f = 0;
          then L~f = {} by TOPREAL1:28;
          hence contradiction by A4;
        end;
A11:    now
          assume g is trivial;
          then len g < 2 by REALSET1:13;
          then len g = 0 or len g = 1 by NAT_1:23;
          then L~g = {} by TOPREAL1:28;
          hence contradiction by A4;
        end;
        now per cases;
          suppose
A12:        i >= 1 & j+1 <= len(f^'g);
            then j+1 < len(f^'g)+1 by NAT_1:13;
            then len f+(k+1) < len f + len g by A9,A11,GRAPH_2:13;
            then
A13:        k+1 < len g by XREAL_1:9;
            then
A14:        LSeg(f^'g,len f+k) = LSeg(g,k+1) by A3,A10,A11,TOPREAL8:31;
            then
A15:        LSeg(f^'g,j) c= L~g by A9,TOPREAL3:26;
            now per cases;
              suppose
A16:            i < len f;
                then
A17:            LSeg(f^'g,i) = LSeg(f,i) by A6,TOPREAL8:28;
                then LSeg(f^'g,i) c= L~f by TOPREAL3:26;
                then
A18:            LSeg(f^'g,i) /\ LSeg(f^'g,j) c= {g/.1} by A4,A15,XBOOLE_1:27;
                assume LSeg(f^'g,i) meets LSeg(f^'g,j);
                then consider x be set such that
A19:            x in LSeg(f^'g,i) and
A20:            x in LSeg(f^'g,j) by XBOOLE_0:3;
                x in LSeg(f^'g,i) /\ LSeg(f^'g,j) by A19,A20,XBOOLE_0:def 4;
                then
A21:            x = g/.1 by A18,TARSKI:def 1;
A22:            i in dom f by A12,A16,FINSEQ_3:27;
                i+1 > 1 & i+1 <= len f by A12,A16,NAT_1:13;
                then i+1 in dom f by FINSEQ_3:27;

then len f+0 < len f+k by A1,A3,A5,A9,A17,A19,A21,A22,GOBOARD2:7;
                then k > 0;
                then
A23:            k+1 > 0+1 by XREAL_1:8;
                len g >= 2 by A11,REALSET1:13;
                hence contradiction by A2,A9,A14,A20,A21,A23,JORDAN5B:33;
              end;
              suppose i >= len f;
                then consider l be Nat such that
A24:            i = len f + l by NAT_1:10;
                reconsider l as Element of NAT by ORDINAL1:def 13;
                len f+(l+1) < len f+k by A5,A9,A24;
                then l+1 < k by XREAL_1:9;
                then
A25:            l+1+1 < k+1 by XREAL_1:8;
                then l+1+1 < len g by A13,XXREAL_0:2;
                then l+1 < len g by NAT_1:13;

then LSeg(f^'g,len f+l) = LSeg(g,l+1) by A3,A10,A11,TOPREAL8:31;
                hence LSeg(f^'g,i) misses LSeg(f^'g,j)
                by A2,A9,A14,A24,A25,TOPREAL1:def 9;
              end;
            end;
            hence LSeg(f^'g,i) misses LSeg(f^'g,j);
          end;
          suppose j+1 > len(f^'g);
            then LSeg(f^'g,j) = {} by TOPREAL1:def 5;
            hence LSeg(f^'g,i) misses LSeg(f^'g,j) by XBOOLE_1:65;
          end;
          suppose i < 1;
            then LSeg(f^'g,i) = {} by TOPREAL1:def 5;
            hence LSeg(f^'g,i) misses LSeg(f^'g,j) by XBOOLE_1:65;
          end;
        end;
        hence LSeg(f^'g,i) misses LSeg(f^'g,j);
      end;
    end;
    hence LSeg(f^'g,i) misses LSeg(f^'g,j);
  end;
  hence thesis by TOPREAL1:def 9;
end;

theorem Th55:
  for f,g be FinSequence of TOP-REAL 2 st
  f is one-to-one & g is one-to-one & rng f /\ rng g c= {g/.1} holds
  f^'g is one-to-one
proof
  let f,g be FinSequence of TOP-REAL 2;
  assume that
A1: f is one-to-one and
A2: g is one-to-one and
A3: rng f /\ rng g c= {g/.1};
  per cases;
  suppose
A4: rng g <> {};
    now
      let i,j be Element of NAT;
      assume that
A5:   i in dom (f^'g) and
A6:   j in dom (f^'g) and
A7:   (f^'g)/.i = (f^'g)/.j;
A8:   1 <= i & i <= len (f^'g) by A5,FINSEQ_3:27;
A9:   1 <= j & j <= len (f^'g) by A6,FINSEQ_3:27;
      g <> {} by A4,RELAT_1:60;
      then len (f^'g)+1 = len f + len g by GRAPH_2:13;
      then
A10:  i < len f + len g & j < len f + len g by A8,A9,NAT_1:13;
A11:  len f = len f + 0;
A12:  1 in dom g by A4,FINSEQ_3:34;
      now per cases;
        suppose
A13:      i <= len f & j <= len f;
          then
A14:      (f^'g)/.i = f/.i & (f^'g)/.j = f/.j by A8,A9,GRAPH_2:61;
          i in dom f & j in dom f by A8,A9,A13,FINSEQ_3:27;
          hence i = j by A1,A7,A14,PARTFUN2:17;
        end;
        suppose
A15:      i > len f & j > len f;
          then consider k be Nat such that
A16:      i = len f + k by NAT_1:10;
          consider l be Nat such that
A17:      j = len f + l by A15,NAT_1:10;
          reconsider k,l as Element of NAT by ORDINAL1:def 13;
A18:      k < len g & l < len g by A10,A16,A17,XREAL_1:9;
          k > 0 by A11,A15,A16;
          then
A19:      k >= 0+1 by NAT_1:13;
          then
A20:      (f^'g)/.i = g/.(k+1) by A16,A18,GRAPH_2:62;
          l > 0 by A11,A15,A17;
          then
A21:      l >= 0+1 by NAT_1:13;
          then
A22:      (f^'g)/.j = g/.(l+1) by A17,A18,GRAPH_2:62;
A23:      k+1 <= len g & l+1 <= len g by A18,NAT_1:13;
          k+1 > 1 & l+1 > 1 by A19,A21,NAT_1:13;
          then k+1 in dom g & l+1 in dom g by A23,FINSEQ_3:27;
          then k+1 = l+1 by A2,A7,A20,A22,PARTFUN2:17;
          hence i = j by A16,A17;
        end;
        suppose
A24:      i <= len f & j > len f;
          then
A25:      (f^'g)/.i = f/.i by A8,GRAPH_2:61;
          i in dom f by A8,A24,FINSEQ_3:27;
          then
A26:      (f^'g)/.i in rng f by A25,PARTFUN2:4;
          consider l be Nat such that
A27:      j = len f + l by A24,NAT_1:10;
          reconsider l as Element of NAT by ORDINAL1:def 13;
A28:      l < len g by A10,A27,XREAL_1:9;
          l > 0 by A11,A24,A27;
          then
A29:      l >= 0+1 by NAT_1:13;
          then
A30:      (f^'g)/.j = g/.(l+1) by A27,A28,GRAPH_2:62;
A31:      l+1 <= len g by A28,NAT_1:13;
A32:      l+1 > 1 by A29,NAT_1:13;
          then
A33:      l+1 in dom g by A31,FINSEQ_3:27;
          then (f^'g)/.j in rng g by A30,PARTFUN2:4;
          then (f^'g)/.j in rng f /\ rng g by A7,A26,XBOOLE_0:def 4;
          then g/.(l+1) = g/.1 by A3,A30,TARSKI:def 1;
          hence i = j by A2,A12,A32,A33,PARTFUN2:17;
        end;
        suppose
A34:      j <= len f & i > len f;
          then
A35:      (f^'g)/.j = f/.j by A9,GRAPH_2:61;
          j in dom f by A9,A34,FINSEQ_3:27;
          then
A36:      (f^'g)/.j in rng f by A35,PARTFUN2:4;
          consider l be Nat such that
A37:      i = len f + l by A34,NAT_1:10;
          reconsider l as Element of NAT by ORDINAL1:def 13;
A38:      l < len g by A10,A37,XREAL_1:9;
          l > 0 by A11,A34,A37;
          then
A39:      l >= 0+1 by NAT_1:13;
          then
A40:      (f^'g)/.i = g/.(l+1) by A37,A38,GRAPH_2:62;
A41:      l+1 <= len g by A38,NAT_1:13;
A42:      l+1 > 1 by A39,NAT_1:13;
          then
A43:      l+1 in dom g by A41,FINSEQ_3:27;
          then (f^'g)/.i in rng g by A40,PARTFUN2:4;
          then (f^'g)/.i in rng f /\ rng g by A7,A36,XBOOLE_0:def 4;
          then g/.(l+1) = g/.1 by A3,A40,TARSKI:def 1;
          hence i = j by A2,A12,A42,A43,PARTFUN2:17;
        end;
      end;
      hence i = j;
    end;
    hence thesis by PARTFUN2:16;
  end;
  suppose rng g = {};
    then g = {} by RELAT_1:64;
    hence thesis by A1,GRAPH_2:59;
  end;
end;

theorem Th56:
  for f be FinSequence of TOP-REAL 2
  for p be Point of TOP-REAL 2 st f is being_S-Seq & p in rng f & p <> f.1
  holds
  Index(p,f)+1 = p..f
proof
  let f be FinSequence of TOP-REAL 2;
  let p be Point of TOP-REAL 2;
  assume that
A1: f is being_S-Seq and
A2: p in rng f and
A3: p <> f.1;
A4: 1 <= p..f & p..f <= len f by A2,FINSEQ_4:31;
  p..f <> 1 by A2,A3,FINSEQ_4:29;
  then
A5: 1 < p..f by A4,XXREAL_0:1;
  f.(p..f) = p by A2,FINSEQ_4:29;
  hence thesis by A1,A4,A5,JORDAN3:45;
end;

theorem Th57:
  for C be compact connected non vertical non horizontal Subset of TOP-REAL 2
  for i,j,k be Element of NAT st 1 < i & i < len Gauge(C,n) &
  1 <= j & k <= width Gauge(C,n) & Gauge(C,n)*(i,k) in L~Upper_Seq(C,n) &
  Gauge(C,n)*(i,j) in L~Lower_Seq(C,n) holds j <> k
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
  let i,j,k be Element of NAT;
  assume that
A1: 1 < i & i < len Gauge(C,n) and
A2: 1 <= j & k <= width Gauge(C,n) and
A3: Gauge(C,n)*(i,k) in L~Upper_Seq(C,n) and
A4: Gauge(C,n)*(i,j) in L~Lower_Seq(C,n) and
A5: j = k;
A6: len Gauge(C,n) = width Gauge(C,n) by JORDAN8:def 1;
  Gauge(C,n)*(i,k) in L~Upper_Seq(C,n) /\ L~Lower_Seq(C,n)
  by A3,A4,A5,XBOOLE_0:def 4;
  then
A7: Gauge(C,n)*(i,k) in {W-min L~Cage(C,n),E-max L~Cage(C,n)} by JORDAN1E:20;
A8: [i,j] in Indices Gauge(C,n) by A1,A2,A5,MATRIX_1:37;
  len Gauge(C,n) >= 4 by JORDAN8:13;
  then
A9: len Gauge(C,n) >= 1 by XXREAL_0:2;
  then
A10: [len Gauge(C,n),j] in Indices Gauge(C,n) by A2,A5,MATRIX_1:37;
A11: [1,j] in Indices Gauge(C,n) by A2,A5,A9,MATRIX_1:37;
  per cases by A7,TARSKI:def 2;
  suppose
A12: Gauge(C,n)*(i,k) = W-min L~Cage(C,n);
    Gauge(C,n)*(1,j)`1 = W-bound L~Cage(C,n) by A2,A5,A6,JORDAN1A:94;
    then (W-min L~Cage(C,n))`1 <> W-bound L~Cage(C,n)
    by A1,A5,A8,A11,A12,JORDAN1G:7;
    hence contradiction by EUCLID:56;
  end;
  suppose
A13: Gauge(C,n)*(i,k) = E-max L~Cage(C,n);
    Gauge(C,n)*(len Gauge(C,n),j)`1 = E-bound L~Cage(C,n)
    by A2,A5,A6,JORDAN1A:92;
    then (E-max L~Cage(C,n))`1 <> E-bound L~Cage(C,n)
    by A1,A5,A8,A10,A13,JORDAN1G:7;
    hence contradiction by EUCLID:56;
  end;
end;

theorem Th58:
  for C be Simple_closed_curve for i,j,k be Element of NAT st
  1 < i & i < len Gauge(C,n) & 1 <= j & j <= k & k <= width Gauge(C,n) &
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ L~Upper_Seq(C,n) =
  {Gauge(C,n)*(i,k)} &
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ L~Lower_Seq(C,n) =
  {Gauge(C,n)*(i,j)} holds
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Lower_Arc C
proof
  let C be Simple_closed_curve;
  let i,j,k be Element of NAT;
  set Ga = Gauge(C,n);
  set US = Upper_Seq(C,n);
  set LS = Lower_Seq(C,n);
  set LA = Lower_Arc C;
  set Wmin = W-min L~Cage(C,n);
  set Emax = E-max L~Cage(C,n);
  set Wbo = W-bound L~Cage(C,n);
  set Ebo = E-bound L~Cage(C,n);
  set Gij = Ga*(i,j);
  set Gik = Ga*(i,k);
  assume that
A1: 1 < i & i < len Ga and
A2: 1 <= j & j <= k & k <= width Ga and
A3: LSeg(Gij,Gik) /\ L~US = {Gik} and
A4: LSeg(Gij,Gik) /\ L~LS = {Gij} and
A5: LSeg(Gij,Gik) misses LA;
  Gij in {Gij} by TARSKI:def 1;
  then
A6: Gij in L~LS by A4,XBOOLE_0:def 4;
  Gik in {Gik} by TARSKI:def 1;
  then
A7: Gik in L~US by A3,XBOOLE_0:def 4;
  then
A8: j <> k by A1,A2,A6,Th57;
A9: 1 <= j & j <= width Ga by A2,XXREAL_0:2;
A10: 1 <= k & k <= width Ga by A2,XXREAL_0:2;
A11: [i,j] in Indices Ga by A1,A9,MATRIX_1:37;
A12: [i,k] in Indices Ga by A1,A10,MATRIX_1:37;
  set go = R_Cut(US,Gik);
  set do = L_Cut(LS,Gij);
A13: len Ga = width Ga by JORDAN8:def 1;
A14: len US >= 3 by JORDAN1E:19;
  then len US >= 1 by XXREAL_0:2;
  then 1 in dom US by FINSEQ_3:27;
  then
A15: US.1 = US/.1 by PARTFUN1:def 8
    .= Wmin by JORDAN1F:5;
A16: Wmin`1 = Wbo by EUCLID:56
    .= Ga*(1,k)`1 by A10,A13,JORDAN1A:94;
  len Ga >= 4 by JORDAN8:13;
  then
A17: len Ga >= 1 by XXREAL_0:2;
  then
A18: [1,k] in Indices Ga by A10,MATRIX_1:37;
  then
A19: Gik <> US.1 by A1,A12,A15,A16,JORDAN1G:7;
  then reconsider go as being_S-Seq FinSequence of TOP-REAL 2
  by A7,JORDAN3:70;
A20: len LS >= 1+2 by JORDAN1E:19;
  then len LS >= 1 by XXREAL_0:2;
  then
A21: 1 in dom LS & len LS in dom LS by FINSEQ_3:27;
  then
A22: LS.len LS = LS/.len LS by PARTFUN1:def 8
    .= Wmin by JORDAN1F:8;
A23: Wmin`1 = Wbo by EUCLID:56
    .= Ga*(1,k)`1 by A10,A13,JORDAN1A:94;
A24: [i,j] in Indices Ga by A1,A9,MATRIX_1:37;
  then
A25: Gij <> LS.len LS by A1,A18,A22,A23,JORDAN1G:7;
  then reconsider do as being_S-Seq FinSequence of TOP-REAL 2
  by A6,JORDAN3:69;
A26: [len Ga,k] in Indices Ga by A10,A17,MATRIX_1:37;
A27: LS.1 = LS/.1 by A21,PARTFUN1:def 8
    .= Emax by JORDAN1F:6;
  Emax`1 = Ebo by EUCLID:56
    .= Ga*(len Ga,k)`1 by A10,A13,JORDAN1A:92;
  then
A28: Gij <> LS.1 by A1,A24,A26,A27,JORDAN1G:7;
A29: len go >= 1+1 by TOPREAL1:def 10;
A30: Gik in rng US by A1,A7,A10,Th40,JORDAN1G:4;
  then
A31: go is_sequence_on Ga by Th38,JORDAN1G:4;
A32: len do >= 1+1 by TOPREAL1:def 10;
A33: Gij in rng LS by A1,A6,A9,Th40,JORDAN1G:5;
  then
A34: do is_sequence_on Ga by Th39,JORDAN1G:5;
  reconsider go as non constant s.c.c.
  (being_S-Seq FinSequence of TOP-REAL 2) by A29,A31,JGRAPH_1:16,JORDAN8:8;
  reconsider do as non constant s.c.c.
  (being_S-Seq FinSequence of TOP-REAL 2) by A32,A34,JGRAPH_1:16,JORDAN8:8;
A35: len go > 1 by A29,NAT_1:13;
  then
A36: len go in dom go by FINSEQ_3:27;
  then
A37: go/.len go = go.len go by PARTFUN1:def 8
    .= Gik by A7,JORDAN3:59;
  len do >= 1 by A32,XXREAL_0:2;
  then 1 in dom do by FINSEQ_3:27;
  then
A38: do/.1 = do.1 by PARTFUN1:def 8
    .= Gij by A6,JORDAN3:58;
  reconsider m = len go - 1 as Element of NAT by A36,FINSEQ_3:28;
A39: m+1 = len go;
  then
A40: len go-'1 = m by NAT_D:34;
A41: LSeg(go,m) c= L~go by TOPREAL3:26;
A42: L~go c= L~US by A7,JORDAN3:76;
  then LSeg(go,m) c= L~US by A41,XBOOLE_1:1;
  then
A43: LSeg(go,m) /\ LSeg(Gik,Gij) c= {Gik} by A3,XBOOLE_1:26;
  m >= 1 by A29,XREAL_1:21;
  then
A44: LSeg(go,m) = LSeg(go/.m,Gik) by A37,A39,TOPREAL1:def 5;
  {Gik} c= LSeg(go,m) /\ LSeg(Gik,Gij)
  proof
    let x be set;
    assume x in {Gik};
    then
A45: x = Gik by TARSKI:def 1;
A46: Gik in LSeg(go,m) by A44,RLTOPSP1:69;
    Gik in LSeg(Gik,Gij) by RLTOPSP1:69;
    hence thesis by A45,A46,XBOOLE_0:def 4;
  end;
  then
A47: LSeg(go,m) /\ LSeg(Gik,Gij) = {Gik} by A43,XBOOLE_0:def 10;
A48: LSeg(do,1) c= L~do by TOPREAL3:26;
A49: L~do c= L~LS by A6,JORDAN3:77;
  then LSeg(do,1) c= L~LS by A48,XBOOLE_1:1;
  then
A50: LSeg(do,1) /\ LSeg(Gik,Gij) c= {Gij} by A4,XBOOLE_1:26;
A51: LSeg(do,1) = LSeg(Gij,do/.(1+1)) by A32,A38,TOPREAL1:def 5;
  {Gij} c= LSeg(do,1) /\ LSeg(Gik,Gij)
  proof
    let x be set;
    assume x in {Gij};
    then
A52: x = Gij by TARSKI:def 1;
A53: Gij in LSeg(do,1) by A51,RLTOPSP1:69;
    Gij in LSeg(Gik,Gij) by RLTOPSP1:69;
    hence thesis by A52,A53,XBOOLE_0:def 4;
  end;
  then
A54: LSeg(Gik,Gij) /\ LSeg(do,1) = {Gij} by A50,XBOOLE_0:def 10;
A55: go/.1 = US/.1 by A7,SPRECT_3:39
    .= Wmin by JORDAN1F:5;
  then
A56: go/.1 = LS/.len LS by JORDAN1F:8
    .= do/.len do by A6,Th35;
A57: rng go c= L~go & rng do c= L~do by A29,A32,SPPOL_2:18;
A58: {go/.1} c= L~go /\ L~do
  proof
    let x be set;
    assume x in {go/.1};
    then x = go/.1 by TARSKI:def 1;
    then x in rng go & x in rng do by A56,FINSEQ_6:46,REVROT_1:3;
    hence thesis by A57,XBOOLE_0:def 4;
  end;
A59: LS.1 = LS/.1 by A21,PARTFUN1:def 8
    .= Emax by JORDAN1F:6;
A60: [len Ga,j] in Indices Ga by A9,A17,MATRIX_1:37;
  L~go /\ L~do c= {go/.1}
  proof
    let x be set;
    assume x in L~go /\ L~do;
    then
A61: x in L~go & x in L~do by XBOOLE_0:def 4;
    then x in L~US /\ L~LS by A42,A49,XBOOLE_0:def 4;
    then x in {Wmin,Emax} by JORDAN1E:20;
    then
A62: x = Wmin or x = Emax by TARSKI:def 2;
    now
      assume x = Emax;
      then
A63:  Emax = Gij by A6,A59,A61,JORDAN1E:11;
      Ga*(len Ga,j)`1 = Ebo by A9,A13,JORDAN1A:92;
      then Emax`1 <> Ebo by A1,A11,A60,A63,JORDAN1G:7;
      hence contradiction by EUCLID:56;
    end;
    hence thesis by A55,A62,TARSKI:def 1;
  end;
  then
A64: L~go /\ L~do = {go/.1} by A58,XBOOLE_0:def 10;
  set W2 = go/.2;
A65: 2 in dom go by A29,FINSEQ_3:27;
A66: Gik..US >= 1 by A30,FINSEQ_4:31;
A67: now
    assume Gik`1 = Wbo;
    then Ga*(1,k)`1 = Ga*(i,k)`1 by A10,A13,JORDAN1A:94;
    hence contradiction by A1,A12,A18,JORDAN1G:7;
  end;
  go = mid(US,1,Gik..US) by A30,JORDAN1G:57
    .= US|(Gik..US) by A66,JORDAN3:25;
  then
A68: W2 = US/.2 by A65,FINSEQ_4:85;
  set pion = <*Gik,Gij*>;
A69: now
    let n be Element of NAT;
    assume n in dom pion;
    then n in Seg 2 by FINSEQ_3:29;
    then n = 1 or n = 2 by FINSEQ_1:4,TARSKI:def 2;
    then pion/.n = Gik or pion/.n = Gij by FINSEQ_4:26;
    hence ex i,j be Element of NAT st [i,j] in Indices Ga & pion/.n = Ga*(i,j)
    by A11,A12;
  end;
A70: Gik <> Gij by A8,A11,A12,GOBOARD1:21;
A71: Gik`1 = Ga*(i,1)`1 by A1,A10,GOBOARD5:3
    .= Gij`1 by A1,A9,GOBOARD5:3;
  then LSeg(Gik,Gij) is vertical by SPPOL_1:37;
  then pion is being_S-Seq by A70,JORDAN1B:8;
  then consider pion1 be FinSequence of TOP-REAL 2 such that
A72: pion1 is_sequence_on Ga and
A73: pion1 is being_S-Seq and
A74: L~pion = L~pion1 and
A75: pion/.1 = pion1/.1 and
A76: pion/.len pion = pion1/.len pion1 and
A77: len pion <= len pion1 by A69,GOBOARD3:2;
  reconsider pion1 as being_S-Seq FinSequence of TOP-REAL 2 by A73;
  set godo = go^'pion1^'do;
A78: 1+1 <= len Cage(C,n) by GOBOARD7:36,XXREAL_0:2;
  then
A79: 1+1 <= len Rotate(Cage(C,n),Wmin) by REVROT_1:14;
  len (go^'pion1) >= len go by TOPREAL8:7;
  then
A80: len (go^'pion1) >= 1+1 by A29,XXREAL_0:2;
  then
A81: len (go^'pion1) > 1+0 by NAT_1:13;
A82: len godo >= len (go^'pion1) by TOPREAL8:7;
  then
A83: 1+1 <= len godo by A80,XXREAL_0:2;
A84: US is_sequence_on Ga by JORDAN1G:4;
A85: go/.len go = pion1/.1 by A37,A75,FINSEQ_4:26;
  then
A86: go^'pion1 is_sequence_on Ga by A31,A72,TOPREAL8:12;
A87: (go^'pion1)/.len (go^'pion1) = pion/.len pion by A76,GRAPH_2:58
    .= pion/.2 by FINSEQ_1:61
    .= do/.1 by A38,FINSEQ_4:26;
  then
A88: godo is_sequence_on Ga by A34,A86,TOPREAL8:12;
  LSeg(pion1,1) c= L~<*Gik,Gij*> by A74,TOPREAL3:26;
  then LSeg(pion1,1) c= LSeg(Gik,Gij) by SPPOL_2:21;
  then
A89: LSeg(go,len go-'1) /\ LSeg(pion1,1) c= {Gik} by A40,A47,XBOOLE_1:27;
A90: len pion1 >= 1+1 by A77,FINSEQ_1:61;
  {Gik} c= LSeg(go,m) /\ LSeg(pion1,1)
  proof
    let x be set;
    assume x in {Gik};
    then
A91: x = Gik by TARSKI:def 1;
A92: Gik in LSeg(go,m) by A44,RLTOPSP1:69;
    Gik in LSeg(pion1,1) by A37,A85,A90,TOPREAL1:27;
    hence thesis by A91,A92,XBOOLE_0:def 4;
  end;
  then LSeg(go,len go-'1) /\ LSeg(pion1,1) = { go/.len go }
  by A37,A40,A89,XBOOLE_0:def 10;
  then
A93: go^'pion1 is unfolded by A85,TOPREAL8:34;
  len pion1 >= 2+0 by A77,FINSEQ_1:61;
  then
A94: len pion1-2 >= 0 by XREAL_1:21;
  len (go^'pion1)+1-1 = len go+len pion1-1 by GRAPH_2:13;
  then len (go^'pion1)-1 = len go + (len pion1-2)
    .= len go + (len pion1-'2) by A94,XREAL_0:def 2;
  then
A95: len (go^'pion1)-'1 = len go + (len pion1-'2) by XREAL_0:def 2;
A96: len pion1-1 >= 1 by A90,XREAL_1:21;
  then
A97: len pion1-'1 = len pion1-1 by XREAL_0:def 2;
A98: len pion1-'2+1 = len pion1-2+1 by A94,XREAL_0:def 2
    .= len pion1-'1 by A96,XREAL_0:def 2;
  len pion1-1+1 <= len pion1;
  then
A99: len pion1-'1 < len pion1 by A97,NAT_1:13;
  LSeg(pion1,len pion1-'1) c= L~<*Gik,Gij*> by A74,TOPREAL3:26;
  then LSeg(pion1,len pion1-'1) c= LSeg(Gik,Gij) by SPPOL_2:21;
  then
A100: LSeg(pion1,len pion1-'1) /\ LSeg(do,1) c= {Gij} by A54,XBOOLE_1:27;
  {Gij} c= LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
  proof
    let x be set;
    assume x in {Gij};
    then
A101: x = Gij by TARSKI:def 1;
A102: Gij in LSeg(do,1) by A51,RLTOPSP1:69;
    pion1/.(len pion1-'1+1) = pion/.2 by A76,A97,FINSEQ_1:61
      .= Gij by FINSEQ_4:26;
    then Gij in LSeg(pion1,len pion1-'1) by A96,A97,TOPREAL1:27;
    hence thesis by A101,A102,XBOOLE_0:def 4;
  end;
  then LSeg(pion1,len pion1-'1) /\ LSeg(do,1) = {Gij} by A100,XBOOLE_0:def 10;
  then
A103: LSeg(go^'pion1,len go+(len pion1-'2)) /\ LSeg(do,1) =
  {(go^'pion1)/.len (go^'pion1)} by A38,A85,A87,A98,A99,TOPREAL8:31;
A104: (go^'pion1) is non trivial by A80,REALSET1:13;
A105: rng pion1 c= L~pion1 by A90,SPPOL_2:18;
A106: {pion1/.1} c= L~go /\ L~pion1
  proof
    let x be set;
    assume x in {pion1/.1};
    then x = pion1/.1 by TARSKI:def 1;
    then x in rng go & x in rng pion1 by A85,FINSEQ_6:46,REVROT_1:3;
    hence thesis by A57,A105,XBOOLE_0:def 4;
  end;
  L~go /\ L~pion1 c= {pion1/.1}
  proof
    let x be set;
    assume x in L~go /\ L~pion1;
    then x in L~go & x in L~pion1 by XBOOLE_0:def 4;
    then x in L~pion1 /\ L~US by A42,XBOOLE_0:def 4;
    hence thesis by A3,A37,A74,A85,SPPOL_2:21;
  end;
  then
A107: L~go /\ L~pion1 = {pion1/.1} by A106,XBOOLE_0:def 10;
  then
A108: (go^'pion1) is s.n.c. by A85,Th54;
  rng go /\ rng pion1 c= {pion1/.1} by A57,A105,A107,XBOOLE_1:27;
  then
A109: go^'pion1 is one-to-one by Th55;
A110: pion/.len pion = pion/.2 by FINSEQ_1:61
    .= do/.1 by A38,FINSEQ_4:26;
A111: {pion1/.len pion1} c= L~do /\ L~pion1
  proof
    let x be set;
    assume x in {pion1/.len pion1};
    then x = pion1/.len pion1 by TARSKI:def 1;
    then x in rng do & x in rng pion1 by A76,A110,FINSEQ_6:46,REVROT_1:3;
    hence thesis by A57,A105,XBOOLE_0:def 4;
  end;
  L~do /\ L~pion1 c= {pion1/.len pion1}
  proof
    let x be set;
    assume x in L~do /\ L~pion1;
    then x in L~do & x in L~pion1 by XBOOLE_0:def 4;
    then x in L~pion1 /\ L~LS by A49,XBOOLE_0:def 4;
    hence thesis by A4,A38,A74,A76,A110,SPPOL_2:21;
  end;
  then
A112: L~do /\ L~pion1 = {pion1/.len pion1} by A111,XBOOLE_0:def 10;
A113: L~(go^'pion1) /\ L~do = (L~go \/ L~pion1) /\ L~do by A85,TOPREAL8:35
    .= {go/.1} \/ {do/.1} by A64,A76,A110,A112,XBOOLE_1:23
    .= {(go^'pion1)/.1} \/ {do/.1} by GRAPH_2:57
    .= {(go^'pion1)/.1,do/.1} by ENUMSET1:41;
  do/.len do = (go^'pion1)/.1 by A56,GRAPH_2:57;
  then reconsider godo as non constant standard special_circular_sequence

by A83,A87,A88,A93,A95,A103,A104,A108,A109,A113,JORDAN8:7,8,TOPREAL8:11,33,34;
A114: LA is_an_arc_of E-max C,W-min C by JORDAN6:def 9;
  then
A115: LA is connected by JORDAN6:11;
A116: W-min C in LA & E-max C in LA by A114,TOPREAL1:4;
  set ff = Rotate(Cage(C,n),Wmin);
  Wmin in rng Cage(C,n) by SPRECT_2:47;
  then
A117: ff/.1 = Wmin by FINSEQ_6:98;
A118: L~ff = L~Cage(C,n) by REVROT_1:33;
  then (W-max L~ff)..ff > 1 by A117,SPRECT_5:23;
  then (N-min L~ff)..ff > 1 by A117,A118,SPRECT_5:24,XXREAL_0:2;
  then (N-max L~ff)..ff > 1 by A117,A118,SPRECT_5:25,XXREAL_0:2;
  then
A119: Emax..ff > 1 by A117,A118,SPRECT_5:26,XXREAL_0:2;
A120: now
    assume
A121: Gik..US <= 1;
    Gik..US >= 1 by A30,FINSEQ_4:31;
    then Gik..US = 1 by A121,XXREAL_0:1;
    then Gik = US/.1 by A30,FINSEQ_5:41;
    hence contradiction by A15,A19,JORDAN1F:5;
  end;
A122: Cage(C,n) is_sequence_on Ga by JORDAN9:def 1;
  then
A123: ff is_sequence_on Ga by REVROT_1:34;
A124: right_cell(godo,1,Ga)\L~godo c= RightComp godo by A83,A88,JORDAN9:29;
A125: L~godo = L~(go^'pion1) \/ L~do by A87,TOPREAL8:35
    .= L~go \/ L~pion1 \/ L~do by A85,TOPREAL8:35;
  L~Cage(C,n) = L~US \/ L~LS by JORDAN1E:17;
  then
A126: L~US c= L~Cage(C,n) & L~LS c= L~Cage(C,n) by XBOOLE_1:7;
  then
A127: L~go c= L~Cage(C,n) & L~do c= L~Cage(C,n) by A42,A49,XBOOLE_1:1;
A128: W-min C in C by SPRECT_1:15;
A129: L~pion = LSeg(Gik,Gij) by SPPOL_2:21;
A130: now
    assume W-min C in L~godo;
    then
A131: W-min C in L~go \/ L~pion1 or W-min C in L~do by A125,XBOOLE_0:def 3;
    per cases by A131,XBOOLE_0:def 3;
    suppose W-min C in L~go;
      then C meets L~Cage(C,n) by A127,A128,XBOOLE_0:3;
      hence contradiction by JORDAN10:5;
    end;
    suppose W-min C in L~pion1;
      hence contradiction by A5,A74,A116,A129,XBOOLE_0:3;
    end;
    suppose W-min C in L~do;
      then C meets L~Cage(C,n) by A127,A128,XBOOLE_0:3;
      hence contradiction by JORDAN10:5;
    end;
  end;
  right_cell(Rotate(Cage(C,n),Wmin),1) =
  right_cell(ff,1,GoB ff) by A79,JORDAN1H:29
    .= right_cell(ff,1,GoB Cage(C,n)) by REVROT_1:28
    .= right_cell(ff,1,Ga) by JORDAN1H:52
    .= right_cell(ff-:Emax,1,Ga) by A119,A123,Th53
    .= right_cell(US,1,Ga) by JORDAN1E:def 1
    .= right_cell(R_Cut(US,Gik),1,Ga) by A30,A84,A120,Th52
    .= right_cell(go^'pion1,1,Ga) by A35,A86,Th51
    .= right_cell(godo,1,Ga) by A81,A88,Th51;
  then W-min C in right_cell(godo,1,Ga) by JORDAN1I:8;
  then
A132: W-min C in right_cell(godo,1,Ga)\L~godo by A130,XBOOLE_0:def 5;
A133: godo/.1 = (go^'pion1)/.1 by GRAPH_2:57
    .= Wmin by A55,GRAPH_2:57;
A134: len US >= 2 by A14,XXREAL_0:2;
A135: godo/.2 = (go^'pion1)/.2 by A80,GRAPH_2:61
    .= US/.2 by A29,A68,GRAPH_2:61
    .= (US^'LS)/.2 by A134,GRAPH_2:61
    .= Rotate(Cage(C,n),Wmin)/.2 by JORDAN1E:15;
A136: L~go \/ L~do is compact by COMPTS_1:19;
  Wmin in rng go by A55,FINSEQ_6:46;
  then Wmin in L~go \/ L~do by A57,XBOOLE_0:def 3;
  then
A137: W-min (L~go \/ L~do) = Wmin by A127,A136,Th21,XBOOLE_1:8;
A138: (W-min (L~go \/ L~do))`1 = W-bound (L~go \/ L~do) & Wmin`1 = Wbo
  by EUCLID:56;
  W-bound LSeg(Gik,Gij) = Gik`1 by A71,SPRECT_1:62;
  then
A139: W-bound L~pion1 = Gik`1 by A74,SPPOL_2:21;
  Gik`1 >= Wbo by A7,A126,PSCOMP_1:71;
  then Gik`1 > Wbo by A67,XXREAL_0:1;
  then W-min (L~go\/L~do\/L~pion1) = W-min (L~go \/ L~do)
  by A136,A137,A138,A139,Th33;
  then
A140: W-min L~godo = Wmin by A125,A137,XBOOLE_1:4;
A141: rng godo c= L~godo by A80,A82,SPPOL_2:18,XXREAL_0:2;
  2 in dom godo by A83,FINSEQ_3:27;
  then
A142: godo/.2 in rng godo by PARTFUN2:4;
  godo/.2 in W-most L~Cage(C,n) by A135,JORDAN1I:27;
  then (godo/.2)`1 = (W-min L~godo)`1 by A140,PSCOMP_1:88
    .= W-bound L~godo by EUCLID:56;
  then godo/.2 in W-most L~godo by A141,A142,SPRECT_2:16;
  then Rotate(godo,W-min L~godo)/.2 in W-most L~godo by A133,A140,FINSEQ_6:95;
  then reconsider godo as clockwise_oriented non constant standard
  special_circular_sequence by JORDAN1I:27;
  len US in dom US by FINSEQ_5:6;
  then
A143: US.len US = US/.len US by PARTFUN1:def 8
    .= Emax by JORDAN1F:7;
A144: E-max C in E-most C by PSCOMP_1:111;
A145: east_halfline E-max C misses L~go
  proof
    assume east_halfline E-max C meets L~go;
    then consider p be set such that
A146: p in east_halfline E-max C and
A147: p in L~go by XBOOLE_0:3;
    reconsider p as Point of TOP-REAL 2 by A146;
    p in L~US by A42,A147;
    then p in east_halfline E-max C /\ L~Cage(C,n) by A126,A146,XBOOLE_0:def 4;
    then
A148: p`1 = Ebo by A144,JORDAN1A:104;
    then
A149: p = Emax by A42,A147,Th46;
    then Emax = Gik by A7,A143,A147,Th43;
    then Gik`1 = Ga*(len Ga,k)`1 by A10,A13,A148,A149,JORDAN1A:92;
    hence contradiction by A1,A12,A26,JORDAN1G:7;
  end;
  now
    assume east_halfline E-max C meets L~godo;
    then
A150: east_halfline E-max C meets (L~go \/ L~pion1) or
    east_halfline E-max C meets L~do by A125,XBOOLE_1:70;
    per cases by A150,XBOOLE_1:70;
    suppose east_halfline E-max C meets L~go;
      hence contradiction by A145;
    end;
    suppose east_halfline E-max C meets L~pion1;
      then consider p be set such that
A151: p in east_halfline E-max C and
A152: p in L~pion1 by XBOOLE_0:3;
      reconsider p as Point of TOP-REAL 2 by A151;
A153: p`1 = Gik`1 by A71,A74,A129,A152,GOBOARD7:5;
      i+1 <= len Ga by A1,NAT_1:13;
      then i+1-1 <= len Ga-1 by XREAL_1:11;
      then
A154: i <= len Ga-'1 by XREAL_0:def 2;
      len Ga-'1 <= len Ga by NAT_D:35;
      then p`1 <= Ga*(len Ga-'1,1)`1 by A1,A10,A13,A17,A153,A154,JORDAN1A:39;
      then p`1 <= E-bound C by A17,JORDAN8:15;
      then
A155: p`1 <= (E-max C)`1 by EUCLID:56;
      p`1 >= (E-max C)`1 by A151,TOPREAL1:def 13;
      then
A156: p`1 = (E-max C)`1 by A155,XXREAL_0:1;
      p`2 = (E-max C)`2 by A151,TOPREAL1:def 13;
      then p = E-max C by A156,TOPREAL3:11;
      hence contradiction by A5,A74,A116,A129,A152,XBOOLE_0:3;
    end;
    suppose east_halfline E-max C meets L~do;
      then consider p be set such that
A157: p in east_halfline E-max C and
A158: p in L~do by XBOOLE_0:3;
      reconsider p as Point of TOP-REAL 2 by A157;
      p in L~LS by A49,A158;
      then p in east_halfline E-max C /\ L~Cage(C,n) by A126,A157,
XBOOLE_0:def 4;
      then
A159: p`1 = Ebo by A144,JORDAN1A:104;
A160: (E-max C)`2 = p`2 by A157,TOPREAL1:def 13;
      set RC = Rotate(Cage(C,n),Emax);
A161: E-max C in right_cell(RC,1) by JORDAN1I:9;
A162: 1+1 <= len LS by A20,XXREAL_0:2;
      LS = RC-:Wmin by JORDAN1G:26;
      then
A163: LSeg(LS,1) = LSeg(RC,1) by A162,SPPOL_2:9;
A164: L~RC = L~Cage(C,n) by REVROT_1:33;
A165: len RC = len Cage(C,n) by REVROT_1:14;
A166: GoB RC = GoB Cage(C,n) by REVROT_1:28
        .= Ga by JORDAN1H:52;
A167: Emax in rng Cage(C,n) by SPRECT_2:50;
A168: RC is_sequence_on Ga by A122,REVROT_1:34;
A169: RC/.1 = E-max L~RC by A164,A167,FINSEQ_6:98;
      then consider ii,jj be Element of NAT such that
A170: [ii,jj+1] in Indices Ga and
A171: [ii,jj] in Indices Ga and
A172: RC/.1 = Ga*(ii,jj+1) and
A173: RC/.(1+1) = Ga*(ii,jj) by A78,A165,A168,JORDAN1I:25;
      consider jj2 be Element of NAT such that
A174: 1 <= jj2 & jj2 <= width Ga and
A175: Emax = Ga*(len Ga,jj2) by JORDAN1D:29;
A176: len Ga >= 4 by JORDAN8:13;
      then len Ga >= 1 by XXREAL_0:2;
      then [len Ga,jj2] in Indices Ga by A174,MATRIX_1:37;
      then
A177: ii = len Ga by A164,A169,A170,A172,A175,GOBOARD1:21;
A178: 1 <= ii & ii <= len Ga & 1 <= jj+1 & jj+1 <= width Ga
      by A170,MATRIX_1:39;
A179: 1 <= ii & ii <= len Ga & 1 <= jj & jj <= width Ga by A171,MATRIX_1:39;
A180: ii+1 <> ii;
      jj+1+1 <> jj;
      then
A181: right_cell(RC,1) = cell(Ga,ii-'1,jj)
      by A78,A165,A166,A170,A171,A172,A173,A180,GOBOARD5:def 6;
A182: ii-'1+1 = ii by A178,XREAL_1:237;
      ii-1 >= 4-1 by A176,A177,XREAL_1:11;
      then
A183: ii-1 >= 1 by XXREAL_0:2;
      then
A184: 1 <= ii-'1 by XREAL_0:def 2;
A185: Ga*(ii-'1,jj)`1 <= (E-max C)`1 & (E-max C)`1 <= Ga*(ii-'1+1, jj)`1 &
      Ga*(ii-'1,jj)`2 <= (E-max C)`2 & (E-max C)`2 <= Ga*(ii-'1,jj+1)`2
      by A161,A178,A179,A181,A182,A183,JORDAN9:19;
A186: ii-'1 < len Ga by A178,A182,NAT_1:13;
      then
A187: Ga*(ii-'1,jj)`2 = Ga*(1,jj)`2 by A179,A184,GOBOARD5:2
        .= Ga*(ii,jj)`2 by A179,GOBOARD5:2;
A188: Ga*(ii-'1,jj+1)`2 = Ga*(1,jj+1)`2 by A178,A184,A186,GOBOARD5:2
        .= Ga*(ii,jj+1)`2 by A178,GOBOARD5:2;
      Ga*(len Ga,jj)`1 = Ebo & Ebo = Ga*(len Ga,jj+1)`1
      by A13,A178,A179,JORDAN1A:92;
      then p in LSeg(RC/.1,RC/.(1+1))
      by A159,A160,A172,A173,A177,A185,A187,A188,GOBOARD7:8;
      then
A189: p in LSeg(LS,1) by A78,A163,A165,TOPREAL1:def 5;
A190: p in LSeg(do,Index(p,do)) by A158,JORDAN3:42;
A191: do = mid(LS,Gij..LS,len LS) by A33,Th37;
A192: 1<=Gij..LS & Gij..LS<=len LS by A33,FINSEQ_4:31;
      Gij..LS <> len LS by A25,A33,FINSEQ_4:29;
      then
A193: Gij..LS < len LS by A192,XXREAL_0:1;
A194: 1<=Index(p,do) & Index(p,do) < len do by A158,JORDAN3:41;
A195: Index(Gij,LS)+1 = Gij..LS by A28,A33,Th56;
      consider t be Nat such that
A196: t in dom LS and
A197: LS.t = Gij by A33,FINSEQ_2:11;
A198: 1 <= t & t <= len LS by A196,FINSEQ_3:27;
      then 1 < t by A28,A197,XXREAL_0:1;
      then Index(Gij,LS)+1 = t by A197,A198,JORDAN3:45;
      then
A199: len L_Cut(LS,Gij) = len LS-Index(Gij,LS) by A6,A197,JORDAN3:61;
      set tt = Index(p,do)+(Gij..LS)-'1;
A200: 1<=Index(Gij,LS) & 0+Index(Gij,LS) < len LS by A6,JORDAN3:41;
      then
A201: len LS-Index(Gij,LS) > 0 by XREAL_1:22;
      Index(p,do) < len LS-'Index(Gij,LS) by A194,A199,XREAL_0:def 2;
      then Index(p,do)+1 <= len LS-'Index(Gij,LS) by NAT_1:13;
      then Index(p,do) <= len LS-'Index(Gij,LS)-1 by XREAL_1:21;
      then Index(p,do) <= len LS-Index(Gij,LS)-1 by A201,XREAL_0:def 2;
      then Index(p,do) <= len LS-Gij..LS by A195;
      then Index(p,do) <= len LS-'Gij..LS by XREAL_0:def 2;
      then Index(p,do) < len LS-'(Gij..LS)+1 by NAT_1:13;
      then
A202: LSeg(mid(LS,Gij..LS,len LS),Index(p,do)) =
      LSeg(LS,Index(p,do)+(Gij..LS)-'1) by A192,A193,A194,JORDAN4:31;
A203: 1+1 <= Gij..LS by A195,A200,XREAL_1:9;
      then Index(p,do)+Gij..LS >= 1+1+1 by A194,XREAL_1:9;
      then Index(p,do)+Gij..LS-1 >= 1+1+1-1 by XREAL_1:11;
      then
A204: tt >= 1+1 by XREAL_0:def 2;
A205: 2 in dom LS by A162,FINSEQ_3:27;
      now per cases by A204,XXREAL_0:1;
        suppose tt > 1+1;
          then LSeg(LS,1) misses LSeg(LS,tt) by TOPREAL1:def 9;
          hence contradiction by A189,A190,A191,A202,XBOOLE_0:3;
        end;
        suppose
A206:     tt = 1+1;
          then LSeg(LS,1) /\ LSeg(LS,tt) = {LS/.2} by A20,TOPREAL1:def 8;
          then p in {LS/.2} by A189,A190,A191,A202,XBOOLE_0:def 4;
          then
A207:     p = LS/.2 by TARSKI:def 1;
          then
A208:     p..LS = 2 by A205,FINSEQ_5:44;
          1+1 = Index(p,do)+(Gij..LS)-1 by A206,XREAL_0:def 2;
          then 1+1+1 = Index(p,do)+(Gij..LS);
          then
A209:     Gij..LS = 2 by A194,A203,JORDAN1E:10;
          p in rng LS by A205,A207,PARTFUN2:4;
          then p = Gij by A33,A208,A209,FINSEQ_5:10;
          then Gij`1 = Ebo by A207,JORDAN1G:40;
          then Gij`1 = Ga*(len Ga,j)`1 by A9,A13,JORDAN1A:92;
          hence contradiction by A1,A11,A60,JORDAN1G:7;
        end;
      end;
      hence contradiction;
    end;
  end;
  then east_halfline E-max C c= (L~godo)` by SUBSET_1:43;
  then consider W be Subset of TOP-REAL 2 such that
A210: W is_a_component_of (L~godo)` and
A211: east_halfline E-max C c= W by GOBOARD9:5;
  W is not Bounded by A211,JORDAN2C:16,129;
  then W is_outside_component_of L~godo by A210,JORDAN2C:def 4;
  then W c= UBD L~godo by JORDAN2C:27;
  then
A212: east_halfline E-max C c= UBD L~godo by A211,XBOOLE_1:1;
  E-max C in east_halfline E-max C by TOPREAL1:45;
  then E-max C in UBD L~godo by A212;
  then E-max C in LeftComp godo by GOBRD14:46;
  then LA meets L~godo by A115,A116,A124,A132,Th36;
  then
A213: LA meets (L~go \/ L~pion1) or LA meets L~do by A125,XBOOLE_1:70;
A214: LA c= C by JORDAN6:76;
  per cases by A213,XBOOLE_1:70;
  suppose LA meets L~go;
    then LA meets L~Cage(C,n) by A42,A126,XBOOLE_1:1,63;
    hence contradiction by A214,JORDAN10:5,XBOOLE_1:63;
  end;
  suppose LA meets L~pion1;
    hence contradiction by A5,A74,A129;
  end;
  suppose LA meets L~do;
    then LA meets L~Cage(C,n) by A49,A126,XBOOLE_1:1,63;
    hence contradiction by A214,JORDAN10:5,XBOOLE_1:63;
  end;
end;

theorem Th59:
  for C be Simple_closed_curve for i,j,k be Element of NAT st
  1 < i & i < len Gauge(C,n) & 1 <= j & j <= k & k <= width Gauge(C,n) &
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ L~Upper_Seq(C,n) =
  {Gauge(C,n)*(i,k)} &
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ L~Lower_Seq(C,n) =
  {Gauge(C,n)*(i,j)} holds
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Upper_Arc C
proof
  let C be Simple_closed_curve;
  let i,j,k be Element of NAT;
  set Ga = Gauge(C,n);
  set US = Upper_Seq(C,n);
  set LS = Lower_Seq(C,n);
  set UA = Upper_Arc C;
  set Wmin = W-min L~Cage(C,n);
  set Emax = E-max L~Cage(C,n);
  set Wbo = W-bound L~Cage(C,n);
  set Ebo = E-bound L~Cage(C,n);
  set Gij = Ga*(i,j);
  set Gik = Ga*(i,k);
  assume that
A1: 1 < i & i < len Ga and
A2: 1 <= j & j <= k & k <= width Ga and
A3: LSeg(Gij,Gik) /\ L~US = {Gik} and
A4: LSeg(Gij,Gik) /\ L~LS = {Gij} and
A5: LSeg(Gij,Gik) misses UA;
  Gij in {Gij} by TARSKI:def 1;
  then
A6: Gij in L~LS by A4,XBOOLE_0:def 4;
  Gik in {Gik} by TARSKI:def 1;
  then
A7: Gik in L~US by A3,XBOOLE_0:def 4;
  then
A8: j <> k by A1,A2,A6,Th57;
A9: 1 <= j & j <= width Ga by A2,XXREAL_0:2;
A10: 1 <= k & k <= width Ga by A2,XXREAL_0:2;
A11: [i,j] in Indices Ga by A1,A9,MATRIX_1:37;
A12: [i,k] in Indices Ga by A1,A10,MATRIX_1:37;
  set go = R_Cut(US,Gik);
  set do = L_Cut(LS,Gij);
A13: len Ga = width Ga by JORDAN8:def 1;
A14: len US >= 3 by JORDAN1E:19;
  then len US >= 1 by XXREAL_0:2;
  then 1 in dom US by FINSEQ_3:27;
  then
A15: US.1 = US/.1 by PARTFUN1:def 8
    .= Wmin by JORDAN1F:5;
A16: Wmin`1 = Wbo by EUCLID:56
    .= Ga*(1,k)`1 by A10,A13,JORDAN1A:94;
  len Ga >= 4 by JORDAN8:13;
  then
A17: len Ga >= 1 by XXREAL_0:2;
  then
A18: [1,k] in Indices Ga by A10,MATRIX_1:37;
  then
A19: Gik <> US.1 by A1,A12,A15,A16,JORDAN1G:7;
  then reconsider go as being_S-Seq FinSequence of TOP-REAL 2
  by A7,JORDAN3:70;
A20: len LS >= 1+2 by JORDAN1E:19;
  then len LS >= 1 by XXREAL_0:2;
  then
A21: 1 in dom LS & len LS in dom LS by FINSEQ_3:27;
  then
A22: LS.len LS = LS/.len LS by PARTFUN1:def 8
    .= Wmin by JORDAN1F:8;
A23: Wmin`1 = Wbo by EUCLID:56
    .= Ga*(1,k)`1 by A10,A13,JORDAN1A:94;
A24: [i,j] in Indices Ga by A1,A9,MATRIX_1:37;
  then
A25: Gij <> LS.len LS by A1,A18,A22,A23,JORDAN1G:7;
  then reconsider do as being_S-Seq FinSequence of TOP-REAL 2
  by A6,JORDAN3:69;
A26: [len Ga,k] in Indices Ga by A10,A17,MATRIX_1:37;
A27: LS.1 = LS/.1 by A21,PARTFUN1:def 8
    .= Emax by JORDAN1F:6;
  Emax`1 = Ebo by EUCLID:56
    .= Ga*(len Ga,k)`1 by A10,A13,JORDAN1A:92;
  then
A28: Gij <> LS.1 by A1,A24,A26,A27,JORDAN1G:7;
A29: len go >= 1+1 by TOPREAL1:def 10;
A30: Gik in rng US by A1,A7,A10,Th40,JORDAN1G:4;
  then
A31: go is_sequence_on Ga by Th38,JORDAN1G:4;
A32: len do >= 1+1 by TOPREAL1:def 10;
A33: Gij in rng LS by A1,A6,A9,Th40,JORDAN1G:5;
  then
A34: do is_sequence_on Ga by Th39,JORDAN1G:5;
  reconsider go as non constant s.c.c.
  (being_S-Seq FinSequence of TOP-REAL 2) by A29,A31,JGRAPH_1:16,JORDAN8:8;
  reconsider do as non constant s.c.c.
  (being_S-Seq FinSequence of TOP-REAL 2) by A32,A34,JGRAPH_1:16,JORDAN8:8;
A35: len go > 1 by A29,NAT_1:13;
  then
A36: len go in dom go by FINSEQ_3:27;
  then
A37: go/.len go = go.len go by PARTFUN1:def 8
    .= Gik by A7,JORDAN3:59;
  len do >= 1 by A32,XXREAL_0:2;
  then 1 in dom do by FINSEQ_3:27;
  then
A38: do/.1 = do.1 by PARTFUN1:def 8
    .= Gij by A6,JORDAN3:58;
  reconsider m = len go - 1 as Element of NAT by A36,FINSEQ_3:28;
A39: m+1 = len go;
  then
A40: len go-'1 = m by NAT_D:34;
A41: LSeg(go,m) c= L~go by TOPREAL3:26;
A42: L~go c= L~US by A7,JORDAN3:76;
  then LSeg(go,m) c= L~US by A41,XBOOLE_1:1;
  then
A43: LSeg(go,m) /\ LSeg(Gik,Gij) c= {Gik} by A3,XBOOLE_1:26;
  m >= 1 by A29,XREAL_1:21;
  then
A44: LSeg(go,m) = LSeg(go/.m,Gik) by A37,A39,TOPREAL1:def 5;
  {Gik} c= LSeg(go,m) /\ LSeg(Gik,Gij)
  proof
    let x be set;
    assume x in {Gik};
    then
A45: x = Gik by TARSKI:def 1;
A46: Gik in LSeg(go,m) by A44,RLTOPSP1:69;
    Gik in LSeg(Gik,Gij) by RLTOPSP1:69;
    hence thesis by A45,A46,XBOOLE_0:def 4;
  end;
  then
A47: LSeg(go,m) /\ LSeg(Gik,Gij) = {Gik} by A43,XBOOLE_0:def 10;
A48: LSeg(do,1) c= L~do by TOPREAL3:26;
A49: L~do c= L~LS by A6,JORDAN3:77;
  then LSeg(do,1) c= L~LS by A48,XBOOLE_1:1;
  then
A50: LSeg(do,1) /\ LSeg(Gik,Gij) c= {Gij} by A4,XBOOLE_1:26;
A51: LSeg(do,1) = LSeg(Gij,do/.(1+1)) by A32,A38,TOPREAL1:def 5;
  {Gij} c= LSeg(do,1) /\ LSeg(Gik,Gij)
  proof
    let x be set;
    assume x in {Gij};
    then
A52: x = Gij by TARSKI:def 1;
A53: Gij in LSeg(do,1) by A51,RLTOPSP1:69;
    Gij in LSeg(Gik,Gij) by RLTOPSP1:69;
    hence thesis by A52,A53,XBOOLE_0:def 4;
  end;
  then
A54: LSeg(Gik,Gij) /\ LSeg(do,1) = {Gij} by A50,XBOOLE_0:def 10;
A55: go/.1 = US/.1 by A7,SPRECT_3:39
    .= Wmin by JORDAN1F:5;
  then
A56: go/.1 = LS/.len LS by JORDAN1F:8
    .= do/.len do by A6,Th35;
A57: rng go c= L~go & rng do c= L~do by A29,A32,SPPOL_2:18;
A58: {go/.1} c= L~go /\ L~do
  proof
    let x be set;
    assume x in {go/.1};
    then x = go/.1 by TARSKI:def 1;
    then x in rng go & x in rng do by A56,FINSEQ_6:46,REVROT_1:3;
    hence thesis by A57,XBOOLE_0:def 4;
  end;
A59: LS.1 = LS/.1 by A21,PARTFUN1:def 8
    .= Emax by JORDAN1F:6;
A60: [len Ga,j] in Indices Ga by A9,A17,MATRIX_1:37;
  L~go /\ L~do c= {go/.1}
  proof
    let x be set;
    assume x in L~go /\ L~do;
    then
A61: x in L~go & x in L~do by XBOOLE_0:def 4;
    then x in L~US /\ L~LS by A42,A49,XBOOLE_0:def 4;
    then x in {Wmin,Emax} by JORDAN1E:20;
    then
A62: x = Wmin or x = Emax by TARSKI:def 2;
    now
      assume x = Emax;
      then
A63:  Emax = Gij by A6,A59,A61,JORDAN1E:11;
      Ga*(len Ga,j)`1 = Ebo by A9,A13,JORDAN1A:92;
      then Emax`1 <> Ebo by A1,A11,A60,A63,JORDAN1G:7;
      hence contradiction by EUCLID:56;
    end;
    hence thesis by A55,A62,TARSKI:def 1;
  end;
  then
A64: L~go /\ L~do = {go/.1} by A58,XBOOLE_0:def 10;
  set W2 = go/.2;
A65: 2 in dom go by A29,FINSEQ_3:27;
A66: Gik..US >= 1 by A30,FINSEQ_4:31;
A67: now
    assume Gik`1 = Wbo;
    then Ga*(1,k)`1 = Ga*(i,k)`1 by A10,A13,JORDAN1A:94;
    hence contradiction by A1,A12,A18,JORDAN1G:7;
  end;
  go = mid(US,1,Gik..US) by A30,JORDAN1G:57
    .= US|(Gik..US) by A66,JORDAN3:25;
  then
A68: W2 = US/.2 by A65,FINSEQ_4:85;
  set pion = <*Gik,Gij*>;
A69: now
    let n be Element of NAT;
    assume n in dom pion;
    then n in Seg 2 by FINSEQ_3:29;
    then n = 1 or n = 2 by FINSEQ_1:4,TARSKI:def 2;
    then pion/.n = Gik or pion/.n = Gij by FINSEQ_4:26;
    hence ex i,j be Element of NAT st [i,j] in Indices Ga & pion/.n = Ga*(i,j)
    by A11,A12;
  end;
A70: Gik <> Gij by A8,A11,A12,GOBOARD1:21;
A71: Gik`1 = Ga*(i,1)`1 by A1,A10,GOBOARD5:3
    .= Gij`1 by A1,A9,GOBOARD5:3;
  then LSeg(Gik,Gij) is vertical by SPPOL_1:37;
  then pion is being_S-Seq by A70,JORDAN1B:8;
  then consider pion1 be FinSequence of TOP-REAL 2 such that
A72: pion1 is_sequence_on Ga and
A73: pion1 is being_S-Seq and
A74: L~pion = L~pion1 and
A75: pion/.1 = pion1/.1 and
A76: pion/.len pion = pion1/.len pion1 and
A77: len pion <= len pion1 by A69,GOBOARD3:2;
  reconsider pion1 as being_S-Seq FinSequence of TOP-REAL 2 by A73;
  set godo = go^'pion1^'do;
A78: 1+1 <= len Cage(C,n) by GOBOARD7:36,XXREAL_0:2;
  then
A79: 1+1 <= len Rotate(Cage(C,n),Wmin) by REVROT_1:14;
  len (go^'pion1) >= len go by TOPREAL8:7;
  then
A80: len (go^'pion1) >= 1+1 by A29,XXREAL_0:2;
  then
A81: len (go^'pion1) > 1+0 by NAT_1:13;
A82: len godo >= len (go^'pion1) by TOPREAL8:7;
  then
A83: 1+1 <= len godo by A80,XXREAL_0:2;
A84: US is_sequence_on Ga by JORDAN1G:4;
A85: go/.len go = pion1/.1 by A37,A75,FINSEQ_4:26;
  then
A86: go^'pion1 is_sequence_on Ga by A31,A72,TOPREAL8:12;
A87: (go^'pion1)/.len (go^'pion1) = pion/.len pion by A76,GRAPH_2:58
    .= pion/.2 by FINSEQ_1:61
    .= do/.1 by A38,FINSEQ_4:26;
  then
A88: godo is_sequence_on Ga by A34,A86,TOPREAL8:12;
  LSeg(pion1,1) c= L~<*Gik,Gij*> by A74,TOPREAL3:26;
  then LSeg(pion1,1) c= LSeg(Gik,Gij) by SPPOL_2:21;
  then
A89: LSeg(go,len go-'1) /\ LSeg(pion1,1) c= {Gik} by A40,A47,XBOOLE_1:27;
A90: len pion1 >= 1+1 by A77,FINSEQ_1:61;
  {Gik} c= LSeg(go,m) /\ LSeg(pion1,1)
  proof
    let x be set;
    assume x in {Gik};
    then
A91: x = Gik by TARSKI:def 1;
A92: Gik in LSeg(go,m) by A44,RLTOPSP1:69;
    Gik in LSeg(pion1,1) by A37,A85,A90,TOPREAL1:27;
    hence thesis by A91,A92,XBOOLE_0:def 4;
  end;
  then LSeg(go,len go-'1) /\ LSeg(pion1,1) = { go/.len go }
  by A37,A40,A89,XBOOLE_0:def 10;
  then
A93: go^'pion1 is unfolded by A85,TOPREAL8:34;
  len pion1 >= 2+0 by A77,FINSEQ_1:61;
  then
A94: len pion1-2 >= 0 by XREAL_1:21;
  len (go^'pion1)+1-1 = len go+len pion1-1 by GRAPH_2:13;
  then len (go^'pion1)-1 = len go + (len pion1-2)
    .= len go + (len pion1-'2) by A94,XREAL_0:def 2;
  then
A95: len (go^'pion1)-'1 = len go + (len pion1-'2) by XREAL_0:def 2;
A96: len pion1-1 >= 1 by A90,XREAL_1:21;
  then
A97: len pion1-'1 = len pion1-1 by XREAL_0:def 2;
A98: len pion1-'2+1 = len pion1-2+1 by A94,XREAL_0:def 2
    .= len pion1-'1 by A96,XREAL_0:def 2;
  len pion1-1+1 <= len pion1;
  then
A99: len pion1-'1 < len pion1 by A97,NAT_1:13;
  LSeg(pion1,len pion1-'1) c= L~<*Gik,Gij*> by A74,TOPREAL3:26;
  then LSeg(pion1,len pion1-'1) c= LSeg(Gik,Gij) by SPPOL_2:21;
  then
A100: LSeg(pion1,len pion1-'1) /\ LSeg(do,1) c= {Gij} by A54,XBOOLE_1:27;
  {Gij} c= LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
  proof
    let x be set;
    assume x in {Gij};
    then
A101: x = Gij by TARSKI:def 1;
A102: Gij in LSeg(do,1) by A51,RLTOPSP1:69;
    pion1/.(len pion1-'1+1) = pion/.2 by A76,A97,FINSEQ_1:61
      .= Gij by FINSEQ_4:26;
    then Gij in LSeg(pion1,len pion1-'1) by A96,A97,TOPREAL1:27;
    hence thesis by A101,A102,XBOOLE_0:def 4;
  end;
  then LSeg(pion1,len pion1-'1) /\ LSeg(do,1) = {Gij} by A100,XBOOLE_0:def 10;
  then
A103: LSeg(go^'pion1,len go+(len pion1-'2)) /\ LSeg(do,1) =
  {(go^'pion1)/.len (go^'pion1)} by A38,A85,A87,A98,A99,TOPREAL8:31;
A104: (go^'pion1) is non trivial by A80,REALSET1:13;
A105: rng pion1 c= L~pion1 by A90,SPPOL_2:18;
A106: {pion1/.1} c= L~go /\ L~pion1
  proof
    let x be set;
    assume x in {pion1/.1};
    then x = pion1/.1 by TARSKI:def 1;
    then x in rng go & x in rng pion1 by A85,FINSEQ_6:46,REVROT_1:3;
    hence thesis by A57,A105,XBOOLE_0:def 4;
  end;
  L~go /\ L~pion1 c= {pion1/.1}
  proof
    let x be set;
    assume x in L~go /\ L~pion1;
    then x in L~go & x in L~pion1 by XBOOLE_0:def 4;
    then x in L~pion1 /\ L~US by A42,XBOOLE_0:def 4;
    hence thesis by A3,A37,A74,A85,SPPOL_2:21;
  end;
  then
A107: L~go /\ L~pion1 = {pion1/.1} by A106,XBOOLE_0:def 10;
  then
A108: (go^'pion1) is s.n.c. by A85,Th54;
  rng go /\ rng pion1 c= {pion1/.1} by A57,A105,A107,XBOOLE_1:27;
  then
A109: go^'pion1 is one-to-one by Th55;
A110: pion/.len pion = pion/.2 by FINSEQ_1:61
    .= do/.1 by A38,FINSEQ_4:26;
A111: {pion1/.len pion1} c= L~do /\ L~pion1
  proof
    let x be set;
    assume x in {pion1/.len pion1};
    then x = pion1/.len pion1 by TARSKI:def 1;
    then x in rng do & x in rng pion1 by A76,A110,FINSEQ_6:46,REVROT_1:3;
    hence thesis by A57,A105,XBOOLE_0:def 4;
  end;
  L~do /\ L~pion1 c= {pion1/.len pion1}
  proof
    let x be set;
    assume x in L~do /\ L~pion1;
    then x in L~do & x in L~pion1 by XBOOLE_0:def 4;
    then x in L~pion1 /\ L~LS by A49,XBOOLE_0:def 4;
    hence thesis by A4,A38,A74,A76,A110,SPPOL_2:21;
  end;
  then
A112: L~do /\ L~pion1 = {pion1/.len pion1} by A111,XBOOLE_0:def 10;
A113: L~(go^'pion1) /\ L~do = (L~go \/ L~pion1) /\ L~do by A85,TOPREAL8:35
    .= {go/.1} \/ {do/.1} by A64,A76,A110,A112,XBOOLE_1:23
    .= {(go^'pion1)/.1} \/ {do/.1} by GRAPH_2:57
    .= {(go^'pion1)/.1,do/.1} by ENUMSET1:41;
  do/.len do = (go^'pion1)/.1 by A56,GRAPH_2:57;
  then reconsider godo as non constant standard special_circular_sequence

by A83,A87,A88,A93,A95,A103,A104,A108,A109,A113,JORDAN8:7,8,TOPREAL8:11,33,34;
A114: UA is_an_arc_of W-min C,E-max C by JORDAN6:def 8;
  then
A115: UA is connected by JORDAN6:11;
A116: W-min C in UA & E-max C in UA by A114,TOPREAL1:4;
  set ff = Rotate(Cage(C,n),Wmin);
  Wmin in rng Cage(C,n) by SPRECT_2:47;
  then
A117: ff/.1 = Wmin by FINSEQ_6:98;
A118: L~ff = L~Cage(C,n) by REVROT_1:33;
  then (W-max L~ff)..ff > 1 by A117,SPRECT_5:23;
  then (N-min L~ff)..ff > 1 by A117,A118,SPRECT_5:24,XXREAL_0:2;
  then (N-max L~ff)..ff > 1 by A117,A118,SPRECT_5:25,XXREAL_0:2;
  then
A119: Emax..ff > 1 by A117,A118,SPRECT_5:26,XXREAL_0:2;
A120: now
    assume
A121: Gik..US <= 1;
    Gik..US >= 1 by A30,FINSEQ_4:31;
    then Gik..US = 1 by A121,XXREAL_0:1;
    then Gik = US/.1 by A30,FINSEQ_5:41;
    hence contradiction by A15,A19,JORDAN1F:5;
  end;
A122: Cage(C,n) is_sequence_on Ga by JORDAN9:def 1;
  then
A123: ff is_sequence_on Ga by REVROT_1:34;
A124: right_cell(godo,1,Ga)\L~godo c= RightComp godo by A83,A88,JORDAN9:29;
A125: L~godo = L~(go^'pion1) \/ L~do by A87,TOPREAL8:35
    .= L~go \/ L~pion1 \/ L~do by A85,TOPREAL8:35;
  L~Cage(C,n) = L~US \/ L~LS by JORDAN1E:17;
  then
A126: L~US c= L~Cage(C,n) & L~LS c= L~Cage(C,n) by XBOOLE_1:7;
  then
A127: L~go c= L~Cage(C,n) & L~do c= L~Cage(C,n) by A42,A49,XBOOLE_1:1;
A128: W-min C in C by SPRECT_1:15;
A129: L~pion = LSeg(Gik,Gij) by SPPOL_2:21;
A130: now
    assume W-min C in L~godo;
    then
A131: W-min C in L~go \/ L~pion1 or W-min C in L~do by A125,XBOOLE_0:def 3;
    per cases by A131,XBOOLE_0:def 3;
    suppose W-min C in L~go;
      then C meets L~Cage(C,n) by A127,A128,XBOOLE_0:3;
      hence contradiction by JORDAN10:5;
    end;
    suppose W-min C in L~pion1;
      hence contradiction by A5,A74,A116,A129,XBOOLE_0:3;
    end;
    suppose W-min C in L~do;
      then C meets L~Cage(C,n) by A127,A128,XBOOLE_0:3;
      hence contradiction by JORDAN10:5;
    end;
  end;
  right_cell(Rotate(Cage(C,n),Wmin),1) =
  right_cell(ff,1,GoB ff) by A79,JORDAN1H:29
    .= right_cell(ff,1,GoB Cage(C,n)) by REVROT_1:28
    .= right_cell(ff,1,Ga) by JORDAN1H:52
    .= right_cell(ff-:Emax,1,Ga) by A119,A123,Th53
    .= right_cell(US,1,Ga) by JORDAN1E:def 1
    .= right_cell(R_Cut(US,Gik),1,Ga) by A30,A84,A120,Th52
    .= right_cell(go^'pion1,1,Ga) by A35,A86,Th51
    .= right_cell(godo,1,Ga) by A81,A88,Th51;
  then W-min C in right_cell(godo,1,Ga) by JORDAN1I:8;
  then
A132: W-min C in right_cell(godo,1,Ga)\L~godo by A130,XBOOLE_0:def 5;
A133: godo/.1 = (go^'pion1)/.1 by GRAPH_2:57
    .= Wmin by A55,GRAPH_2:57;
A134: len US >= 2 by A14,XXREAL_0:2;
A135: godo/.2 = (go^'pion1)/.2 by A80,GRAPH_2:61
    .= US/.2 by A29,A68,GRAPH_2:61
    .= (US^'LS)/.2 by A134,GRAPH_2:61
    .= Rotate(Cage(C,n),Wmin)/.2 by JORDAN1E:15;
A136: L~go \/ L~do is compact by COMPTS_1:19;
  Wmin in rng go by A55,FINSEQ_6:46;
  then Wmin in L~go \/ L~do by A57,XBOOLE_0:def 3;
  then
A137: W-min (L~go \/ L~do) = Wmin by A127,A136,Th21,XBOOLE_1:8;
A138: (W-min (L~go \/ L~do))`1 = W-bound (L~go \/ L~do) & Wmin`1 = Wbo
  by EUCLID:56;
  W-bound LSeg(Gik,Gij) = Gik`1 by A71,SPRECT_1:62;
  then
A139: W-bound L~pion1 = Gik`1 by A74,SPPOL_2:21;
  Gik`1 >= Wbo by A7,A126,PSCOMP_1:71;
  then Gik`1 > Wbo by A67,XXREAL_0:1;
  then W-min (L~go\/L~do\/L~pion1) = W-min (L~go \/ L~do)
  by A136,A137,A138,A139,Th33;
  then
A140: W-min L~godo = Wmin by A125,A137,XBOOLE_1:4;
A141: rng godo c= L~godo by A80,A82,SPPOL_2:18,XXREAL_0:2;
  2 in dom godo by A83,FINSEQ_3:27;
  then
A142: godo/.2 in rng godo by PARTFUN2:4;
  godo/.2 in W-most L~Cage(C,n) by A135,JORDAN1I:27;
  then (godo/.2)`1 = (W-min L~godo)`1 by A140,PSCOMP_1:88
    .= W-bound L~godo by EUCLID:56;
  then godo/.2 in W-most L~godo by A141,A142,SPRECT_2:16;
  then Rotate(godo,W-min L~godo)/.2 in W-most L~godo by A133,A140,FINSEQ_6:95;
  then reconsider godo as clockwise_oriented non constant standard
  special_circular_sequence by JORDAN1I:27;
  len US in dom US by FINSEQ_5:6;
  then
A143: US.len US = US/.len US by PARTFUN1:def 8
    .= Emax by JORDAN1F:7;
A144: E-max C in E-most C by PSCOMP_1:111;
A145: east_halfline E-max C misses L~go
  proof
    assume east_halfline E-max C meets L~go;
    then consider p be set such that
A146: p in east_halfline E-max C and
A147: p in L~go by XBOOLE_0:3;
    reconsider p as Point of TOP-REAL 2 by A146;
    p in L~US by A42,A147;
    then p in east_halfline E-max C /\ L~Cage(C,n) by A126,A146,XBOOLE_0:def 4;
    then
A148: p`1 = Ebo by A144,JORDAN1A:104;
    then
A149: p = Emax by A42,A147,Th46;
    then Emax = Gik by A7,A143,A147,Th43;
    then Gik`1 = Ga*(len Ga,k)`1 by A10,A13,A148,A149,JORDAN1A:92;
    hence contradiction by A1,A12,A26,JORDAN1G:7;
  end;
  now
    assume east_halfline E-max C meets L~godo;
    then
A150: east_halfline E-max C meets (L~go \/ L~pion1) or
    east_halfline E-max C meets L~do by A125,XBOOLE_1:70;
    per cases by A150,XBOOLE_1:70;
    suppose east_halfline E-max C meets L~go;
      hence contradiction by A145;
    end;
    suppose east_halfline E-max C meets L~pion1;
      then consider p be set such that
A151: p in east_halfline E-max C and
A152: p in L~pion1 by XBOOLE_0:3;
      reconsider p as Point of TOP-REAL 2 by A151;
A153: p`1 = Gik`1 by A71,A74,A129,A152,GOBOARD7:5;
      i+1 <= len Ga by A1,NAT_1:13;
      then i+1-1 <= len Ga-1 by XREAL_1:11;
      then
A154: i <= len Ga-'1 by XREAL_0:def 2;
      len Ga-'1 <= len Ga by NAT_D:35;
      then p`1 <= Ga*(len Ga-'1,1)`1 by A1,A10,A13,A17,A153,A154,JORDAN1A:39;
      then p`1 <= E-bound C by A17,JORDAN8:15;
      then
A155: p`1 <= (E-max C)`1 by EUCLID:56;
      p`1 >= (E-max C)`1 by A151,TOPREAL1:def 13;
      then
A156: p`1 = (E-max C)`1 by A155,XXREAL_0:1;
      p`2 = (E-max C)`2 by A151,TOPREAL1:def 13;
      then p = E-max C by A156,TOPREAL3:11;
      hence contradiction by A5,A74,A116,A129,A152,XBOOLE_0:3;
    end;
    suppose east_halfline E-max C meets L~do;
      then consider p be set such that
A157: p in east_halfline E-max C and
A158: p in L~do by XBOOLE_0:3;
      reconsider p as Point of TOP-REAL 2 by A157;
      p in L~LS by A49,A158;
      then p in east_halfline E-max C /\ L~Cage(C,n) by A126,A157,
XBOOLE_0:def 4;
      then
A159: p`1 = Ebo by A144,JORDAN1A:104;
A160: (E-max C)`2 = p`2 by A157,TOPREAL1:def 13;
      set RC = Rotate(Cage(C,n),Emax);
A161: E-max C in right_cell(RC,1) by JORDAN1I:9;
A162: 1+1 <= len LS by A20,XXREAL_0:2;
      LS = RC-:Wmin by JORDAN1G:26;
      then
A163: LSeg(LS,1) = LSeg(RC,1) by A162,SPPOL_2:9;
A164: L~RC = L~Cage(C,n) by REVROT_1:33;
A165: len RC = len Cage(C,n) by REVROT_1:14;
A166: GoB RC = GoB Cage(C,n) by REVROT_1:28
        .= Ga by JORDAN1H:52;
A167: Emax in rng Cage(C,n) by SPRECT_2:50;
A168: RC is_sequence_on Ga by A122,REVROT_1:34;
A169: RC/.1 = E-max L~RC by A164,A167,FINSEQ_6:98;
      then consider ii,jj be Element of NAT such that
A170: [ii,jj+1] in Indices Ga and
A171: [ii,jj] in Indices Ga and
A172: RC/.1 = Ga*(ii,jj+1) and
A173: RC/.(1+1) = Ga*(ii,jj) by A78,A165,A168,JORDAN1I:25;
      consider jj2 be Element of NAT such that
A174: 1 <= jj2 & jj2 <= width Ga and
A175: Emax = Ga*(len Ga,jj2) by JORDAN1D:29;
A176: len Ga >= 4 by JORDAN8:13;
      then len Ga >= 1 by XXREAL_0:2;
      then [len Ga,jj2] in Indices Ga by A174,MATRIX_1:37;
      then
A177: ii = len Ga by A164,A169,A170,A172,A175,GOBOARD1:21;
A178: 1 <= ii & ii <= len Ga & 1 <= jj+1 & jj+1 <= width Ga
      by A170,MATRIX_1:39;
A179: 1 <= ii & ii <= len Ga & 1 <= jj & jj <= width Ga by A171,MATRIX_1:39;
A180: ii+1 <> ii;
      jj+1+1 <> jj;
      then
A181: right_cell(RC,1) = cell(Ga,ii-'1,jj)
      by A78,A165,A166,A170,A171,A172,A173,A180,GOBOARD5:def 6;
A182: ii-'1+1 = ii by A178,XREAL_1:237;
      ii-1 >= 4-1 by A176,A177,XREAL_1:11;
      then
A183: ii-1 >= 1 by XXREAL_0:2;
      then
A184: 1 <= ii-'1 by XREAL_0:def 2;
A185: Ga*(ii-'1,jj)`1 <= (E-max C)`1 & (E-max C)`1 <= Ga*(ii-'1+1, jj)`1 &
      Ga*(ii-'1,jj)`2 <= (E-max C)`2 & (E-max C)`2 <= Ga*(ii-'1,jj+1)`2
      by A161,A178,A179,A181,A182,A183,JORDAN9:19;
A186: ii-'1 < len Ga by A178,A182,NAT_1:13;
      then
A187: Ga*(ii-'1,jj)`2 = Ga*(1,jj)`2 by A179,A184,GOBOARD5:2
        .= Ga*(ii,jj)`2 by A179,GOBOARD5:2;
A188: Ga*(ii-'1,jj+1)`2 = Ga*(1,jj+1)`2 by A178,A184,A186,GOBOARD5:2
        .= Ga*(ii,jj+1)`2 by A178,GOBOARD5:2;
      Ga*(len Ga,jj)`1 = Ebo & Ebo = Ga*(len Ga,jj+1)`1
      by A13,A178,A179,JORDAN1A:92;
      then p in LSeg(RC/.1,RC/.(1+1))
      by A159,A160,A172,A173,A177,A185,A187,A188,GOBOARD7:8;
      then
A189: p in LSeg(LS,1) by A78,A163,A165,TOPREAL1:def 5;
A190: p in LSeg(do,Index(p,do)) by A158,JORDAN3:42;
A191: do = mid(LS,Gij..LS,len LS) by A33,Th37;
A192: 1<=Gij..LS & Gij..LS<=len LS by A33,FINSEQ_4:31;
      Gij..LS <> len LS by A25,A33,FINSEQ_4:29;
      then
A193: Gij..LS < len LS by A192,XXREAL_0:1;
A194: 1<=Index(p,do) & Index(p,do) < len do by A158,JORDAN3:41;
A195: Index(Gij,LS)+1 = Gij..LS by A28,A33,Th56;
      consider t be Nat such that
A196: t in dom LS and
A197: LS.t = Gij by A33,FINSEQ_2:11;
A198: 1 <= t & t <= len LS by A196,FINSEQ_3:27;
      then 1 < t by A28,A197,XXREAL_0:1;
      then Index(Gij,LS)+1 = t by A197,A198,JORDAN3:45;
      then
A199: len L_Cut(LS,Gij) = len LS-Index(Gij,LS) by A6,A197,JORDAN3:61;
      set tt = Index(p,do)+(Gij..LS)-'1;
A200: 1<=Index(Gij,LS) & 0+Index(Gij,LS) < len LS by A6,JORDAN3:41;
      then
A201: len LS-Index(Gij,LS) > 0 by XREAL_1:22;
      Index(p,do) < len LS-'Index(Gij,LS) by A194,A199,XREAL_0:def 2;
      then Index(p,do)+1 <= len LS-'Index(Gij,LS) by NAT_1:13;
      then Index(p,do) <= len LS-'Index(Gij,LS)-1 by XREAL_1:21;
      then Index(p,do) <= len LS-Index(Gij,LS)-1 by A201,XREAL_0:def 2;
      then Index(p,do) <= len LS-Gij..LS by A195;
      then Index(p,do) <= len LS-'Gij..LS by XREAL_0:def 2;
      then Index(p,do) < len LS-'(Gij..LS)+1 by NAT_1:13;
      then
A202: LSeg(mid(LS,Gij..LS,len LS),Index(p,do)) =
      LSeg(LS,Index(p,do)+(Gij..LS)-'1) by A192,A193,A194,JORDAN4:31;
A203: 1+1 <= Gij..LS by A195,A200,XREAL_1:9;
      then Index(p,do)+Gij..LS >= 1+1+1 by A194,XREAL_1:9;
      then Index(p,do)+Gij..LS-1 >= 1+1+1-1 by XREAL_1:11;
      then
A204: tt >= 1+1 by XREAL_0:def 2;
A205: 2 in dom LS by A162,FINSEQ_3:27;
      now per cases by A204,XXREAL_0:1;
        suppose tt > 1+1;
          then LSeg(LS,1) misses LSeg(LS,tt) by TOPREAL1:def 9;
          hence contradiction by A189,A190,A191,A202,XBOOLE_0:3;
        end;
        suppose
A206:     tt = 1+1;
          then LSeg(LS,1) /\ LSeg(LS,tt) = {LS/.2} by A20,TOPREAL1:def 8;
          then p in {LS/.2} by A189,A190,A191,A202,XBOOLE_0:def 4;
          then
A207:     p = LS/.2 by TARSKI:def 1;
          then
A208:     p..LS = 2 by A205,FINSEQ_5:44;
          1+1 = Index(p,do)+(Gij..LS)-1 by A206,XREAL_0:def 2;
          then 1+1+1 = Index(p,do)+(Gij..LS);
          then
A209:     Gij..LS = 2 by A194,A203,JORDAN1E:10;
          p in rng LS by A205,A207,PARTFUN2:4;
          then p = Gij by A33,A208,A209,FINSEQ_5:10;
          then Gij`1 = Ebo by A207,JORDAN1G:40;
          then Gij`1 = Ga*(len Ga,j)`1 by A9,A13,JORDAN1A:92;
          hence contradiction by A1,A11,A60,JORDAN1G:7;
        end;
      end;
      hence contradiction;
    end;
  end;
  then east_halfline E-max C c= (L~godo)` by SUBSET_1:43;
  then consider W be Subset of TOP-REAL 2 such that
A210: W is_a_component_of (L~godo)` and
A211: east_halfline E-max C c= W by GOBOARD9:5;
  W is not Bounded by A211,JORDAN2C:16,129;
  then W is_outside_component_of L~godo by A210,JORDAN2C:def 4;
  then W c= UBD L~godo by JORDAN2C:27;
  then
A212: east_halfline E-max C c= UBD L~godo by A211,XBOOLE_1:1;
  E-max C in east_halfline E-max C by TOPREAL1:45;
  then E-max C in UBD L~godo by A212;
  then E-max C in LeftComp godo by GOBRD14:46;
  then UA meets L~godo by A115,A116,A124,A132,Th36;
  then
A213: UA meets (L~go \/ L~pion1) or UA meets L~do by A125,XBOOLE_1:70;
A214: UA c= C by JORDAN6:76;
  per cases by A213,XBOOLE_1:70;
  suppose UA meets L~go;
    then UA meets L~Cage(C,n) by A42,A126,XBOOLE_1:1,63;
    hence contradiction by A214,JORDAN10:5,XBOOLE_1:63;
  end;
  suppose UA meets L~pion1;
    hence contradiction by A5,A74,A129;
  end;
  suppose UA meets L~do;
    then UA meets L~Cage(C,n) by A49,A126,XBOOLE_1:1,63;
    hence contradiction by A214,JORDAN10:5,XBOOLE_1:63;
  end;
end;

theorem Th60:
  for C be Simple_closed_curve for i,j,k be Element of NAT st
  1 < i & i < len Gauge(C,n) &
  1 <= j & j <= k & k <= width Gauge(C,n) & n > 0 &
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Upper_Arc L~Cage(C,n) =
  {Gauge(C,n)*(i,k)} &
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Lower_Arc L~Cage(C,n) =
  {Gauge(C,n)*(i,j)} holds
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Lower_Arc C
proof
  let C be Simple_closed_curve;
  let i,j,k be Element of NAT;
  assume that
A1: 1 < i & i < len Gauge(C,n) and
A2: 1 <= j & j <= k & k <= width Gauge(C,n) and
A3: n > 0 and
A4: LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Upper_Arc L~Cage(C,n) =
  {Gauge(C,n)*(i,k)} and
A5: LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Lower_Arc L~Cage(C,n) =
  {Gauge(C,n)*(i,j)};
A6: L~Upper_Seq(C,n) = Upper_Arc L~Cage(C,n) by A3,JORDAN1G:63;
  L~Lower_Seq(C,n) = Lower_Arc L~Cage(C,n) by A3,JORDAN1G:64;
  hence thesis
  by A1,A2,A4,A5,A6,Th58;
end;

theorem Th61:
  for C be Simple_closed_curve for i,j,k be Element of NAT st
  1 < i & i < len Gauge(C,n) &
  1 <= j & j <= k & k <= width Gauge(C,n) & n > 0 &
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Upper_Arc L~Cage(C,n) =
  {Gauge(C,n)*(i,k)} &
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Lower_Arc L~Cage(C,n) =
  {Gauge(C,n)*(i,j)} holds
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Upper_Arc C
proof
  let C be Simple_closed_curve;
  let i,j,k be Element of NAT;
  assume that
A1: 1 < i & i < len Gauge(C,n) and
A2: 1 <= j & j <= k & k <= width Gauge(C,n) and
A3: n > 0 and
A4: LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Upper_Arc L~Cage(C,n) =
  {Gauge(C,n)*(i,k)} and
A5: LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Lower_Arc L~Cage(C,n) =
  {Gauge(C,n)*(i,j)};
A6: L~Upper_Seq(C,n) = Upper_Arc L~Cage(C,n) by A3,JORDAN1G:63;
  L~Lower_Seq(C,n) = Lower_Arc L~Cage(C,n) by A3,JORDAN1G:64;
  hence thesis
  by A1,A2,A4,A5,A6,Th59;
end;

theorem
  for C be compact connected non vertical non horizontal Subset of TOP-REAL 2
  for j be Element of NAT holds
  Gauge(C,n+1)*(Center Gauge(C,n+1),j) in Upper_Arc L~Cage(C,n+1) &
  1 <= j & j <= width Gauge(C,n+1) implies
  LSeg(Gauge(C,1)*(Center Gauge(C,1),1),Gauge(C,n+1)*(Center Gauge(C,n+1),j))
  meets Lower_Arc L~Cage(C,n+1)
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
  let j be Element of NAT;
  assume that
A1: Gauge(C,n+1)*(Center Gauge(C,n+1),j) in Upper_Arc L~Cage(C,n+1) and
A2: 1 <= j & j <= width Gauge(C,n+1);
  set in1 = Center Gauge(C,n+1);
A3: n+1 >= 0+1 by NAT_1:11;
A4: 1 <= in1 by JORDAN1B:12;
A5: in1 <= len Gauge(C,n+1) by JORDAN1B:14;
A6: LSeg(Gauge(C,n+1)*(Center Gauge(C,n+1),1),
  Gauge(C,n+1)*(Center Gauge(C,n+1),j)) c=
  LSeg(Gauge(C,1)*(Center Gauge(C,1),1),
  Gauge(C,n+1)*(Center Gauge(C,n+1),j)) by A2,A3,JORDAN1A:66;
  Upper_Arc L~Cage(C,n+1) c= L~Cage(C,n+1) by JORDAN6:76;
  hence thesis by A1,A2,A4,A5,A6,JORDAN1G:65,XBOOLE_1:63;
end;

theorem
  for C be Simple_closed_curve for j,k be Element of NAT holds
  1 <= j & j <= k & k <= width Gauge(C,n+1) &
  LSeg(Gauge(C,n+1)*(Center Gauge(C,n+1),j),
  Gauge(C,n+1)*(Center Gauge(C,n+1),k)) /\ Upper_Arc L~Cage(C,n+1) =
  {Gauge(C,n+1)*(Center Gauge(C,n+1),k)} &
  LSeg(Gauge(C,n+1)*(Center Gauge(C,n+1),j),
  Gauge(C,n+1)*(Center Gauge(C,n+1),k)) /\ Lower_Arc L~Cage(C,n+1) =
  {Gauge(C,n+1)*(Center Gauge(C,n+1),j)} implies
  LSeg(Gauge(C,n+1)*(Center Gauge(C,n+1),j),
  Gauge(C,n+1)*(Center Gauge(C,n+1),k)) meets Lower_Arc C
proof
  let C be Simple_closed_curve;
  let j,k be Element of NAT;
  assume that
A1: 1 <= j & j <= k & k <= width Gauge(C,n+1) and
A2: LSeg(Gauge(C,n+1)*(Center Gauge(C,n+1),j),
  Gauge(C,n+1)*(Center Gauge(C,n+1),k)) /\ Upper_Arc L~Cage(C,n+1) =
  {Gauge(C,n+1)*(Center Gauge(C,n+1),k)} and
A3: LSeg(Gauge(C,n+1)*(Center Gauge(C,n+1),j),
  Gauge(C,n+1)*(Center Gauge(C,n+1),k)) /\ Lower_Arc L~Cage(C,n+1) =
  {Gauge(C,n+1)*(Center Gauge(C,n+1),j)};
A4: len Gauge(C,n+1) >= 4 by JORDAN8:13;
  then
A5: len Gauge(C,n+1) >= 3 by XXREAL_0:2;
  len Gauge(C,n+1) >= 2 by A4,XXREAL_0:2;
  then 1 < Center Gauge(C,n+1) by JORDAN1B:15;
  hence thesis
  by A1,A2,A3,A5,Th60,JORDAN1B:16;
end;

theorem
  for C be Simple_closed_curve for j,k be Element of NAT holds
  1 <= j & j <= k & k <= width Gauge(C,n+1) &
  LSeg(Gauge(C,n+1)*(Center Gauge(C,n+1),j),
  Gauge(C,n+1)*(Center Gauge(C,n+1),k)) /\ Upper_Arc L~Cage(C,n+1) =
  {Gauge(C,n+1)*(Center Gauge(C,n+1),k)} &
  LSeg(Gauge(C,n+1)*(Center Gauge(C,n+1),j),
  Gauge(C,n+1)*(Center Gauge(C,n+1),k)) /\ Lower_Arc L~Cage(C,n+1) =
  {Gauge(C,n+1)*(Center Gauge(C,n+1),j)} implies
  LSeg(Gauge(C,n+1)*(Center Gauge(C,n+1),j),
  Gauge(C,n+1)*(Center Gauge(C,n+1),k)) meets Upper_Arc C
proof
  let C be Simple_closed_curve;
  let j,k be Element of NAT;
  assume that
A1: 1 <= j & j <= k & k <= width Gauge(C,n+1) and
A2: LSeg(Gauge(C,n+1)*(Center Gauge(C,n+1),j),
  Gauge(C,n+1)*(Center Gauge(C,n+1),k)) /\ Upper_Arc L~Cage(C,n+1) =
  {Gauge(C,n+1)*(Center Gauge(C,n+1),k)} and
A3: LSeg(Gauge(C,n+1)*(Center Gauge(C,n+1),j),
  Gauge(C,n+1)*(Center Gauge(C,n+1),k)) /\ Lower_Arc L~Cage(C,n+1) =
  {Gauge(C,n+1)*(Center Gauge(C,n+1),j)};
A4: len Gauge(C,n+1) >= 4 by JORDAN8:13;
  then
A5: len Gauge(C,n+1) >= 3 by XXREAL_0:2;
  len Gauge(C,n+1) >= 2 by A4,XXREAL_0:2;
  then 1 < Center Gauge(C,n+1) by JORDAN1B:15;
  hence thesis
  by A1,A2,A3,A5,Th61,JORDAN1B:16;
end;

:: Moved from JORDAN, AG 20.01.2006

theorem
  for X, Y being non empty compact Subset of TOP-REAL 2 st
  X c= Y & (W-min Y in X or W-max Y in X) holds W-bound X = W-bound Y
proof
  let X, Y be non empty compact Subset of TOP-REAL 2;
  assume
A1: X c= Y & (W-min Y in X or W-max Y in X);
  (W-min X)`1 = W-bound X & (W-max X)`1 = W-bound X &
  (W-min Y)`1 = W-bound Y & (W-max Y)`1 = W-bound Y by EUCLID:56;
  hence thesis by A1,Th21,Th22;
end;

theorem
  for X, Y being non empty compact Subset of TOP-REAL 2 st
  X c= Y & (E-min Y in X or E-max Y in X) holds E-bound X = E-bound Y
proof
  let X, Y be non empty compact Subset of TOP-REAL 2;
  assume
A1: X c= Y & (E-min Y in X or E-max Y in X);
  (E-min X)`1 = E-bound X & (E-max X)`1 = E-bound X &
  (E-min Y)`1 = E-bound Y & (E-max Y)`1 = E-bound Y by EUCLID:56;
  hence thesis by A1,Th17,Th18;
end;

theorem
  for X, Y being non empty compact Subset of TOP-REAL 2 st
  X c= Y & (N-min Y in X or N-max Y in X) holds N-bound X = N-bound Y
proof
  let X, Y be non empty compact Subset of TOP-REAL 2;
  assume
A1: X c= Y & (N-min Y in X or N-max Y in X);
  (N-min X)`2 = N-bound X & (N-max X)`2 = N-bound X &
  (N-min Y)`2 = N-bound Y & (N-max Y)`2 = N-bound Y by EUCLID:56;
  hence thesis by A1,Th15,Th16;
end;

theorem
  for X, Y being non empty compact Subset of TOP-REAL 2 st
  X c= Y & (S-min Y in X or S-max Y in X) holds S-bound X = S-bound Y
proof
  let X, Y be non empty compact Subset of TOP-REAL 2;
  assume
A1: X c= Y & (S-min Y in X or S-max Y in X);
  (S-min X)`2 = S-bound X & (S-max X)`2 = S-bound X &
  (S-min Y)`2 = S-bound Y & (S-max Y)`2 = S-bound Y by EUCLID:56;
  hence thesis by A1,Th19,Th20;
end;

