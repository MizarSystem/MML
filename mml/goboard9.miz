:: Left and Right Component of the Complement of a Special Closed Curve
::  by Andrzej Trybulec
::
:: Received October 29, 1995
:: Copyright (c) 1995 Association of Mizar Users

environ

 vocabularies SEQM_3, GOBOARD5, PRE_TOPC, EUCLID, GOBOARD1, ARYTM_1, CONNSP_1,
      BOOLE, RELAT_1, SUBSET_1, RELAT_2, TOPREAL1, JORDAN1, FINSEQ_1, FINSEQ_5,
      FUNCT_1, MCART_1, GOBOARD2, CARD_1, MATRIX_1, ABSVALUE, FINSEQ_6, ARYTM,
      TREES_1, TOPS_1, ARYTM_3, GOBOARD9, FINSEQ_4, ORDINAL2;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, CARD_1, NUMBERS, COMPLEX1,
      REAL_1, NAT_1, NAT_D, BINARITH, FUNCT_1, PARTFUN1, FINSEQ_1, MATRIX_1,
      STRUCT_0, PRE_TOPC, TOPS_1, CONNSP_1, EUCLID, TOPREAL1, GOBOARD1,
      GOBOARD2, JORDAN1, FINSEQ_5, FINSEQ_6, GOBOARD5, XXREAL_0;
 constructors PARTFUN1, XXREAL_0, REAL_1, NAT_1, COMPLEX1, BINARITH, FINSEQ_5,
      TOPS_1, CONNSP_1, GOBOARD2, JORDAN1, GOBOARD5, SEQ_1, GOBOARD1, NAT_D;
 registrations XBOOLE_0, RELSET_1, NUMBERS, XXREAL_0, MEMBERED, FINSEQ_1,
      FINSEQ_6, STRUCT_0, PRE_TOPC, EUCLID, TOPREAL1, GOBOARD2, GOBOARD5,
      ORDINAL1, FINSET_1;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, FINSEQ_6, GOBOARD5, GOBOARD1, JORDAN1, XBOOLE_0, STRUCT_0;
 theorems CONNSP_1, GOBOARD5, TOPS_1, SPPOL_2, PRE_TOPC, GOBOARD1, TARSKI,
      TOPREAL1, GOBOARD2, FINSEQ_3, NAT_1, SUBSET_1, FINSEQ_6, GOBOARD6,
      GOBOARD7, GOBOARD8, FINSEQ_1, FINSEQ_5, JORDAN1, TSEP_1, INT_1, XBOOLE_0,
      XBOOLE_1, XREAL_1, COMPLEX1, XXREAL_0, PARTFUN1, MATRIX_1, NAT_D;
 schemes NAT_1;

begin

reserve f for non constant standard special_circular_sequence,
  i,j,k,i1,i2,j1,j2 for Element of NAT,
  r,s,r1,s1,r2,s2 for Real,
  a,b for natural number,
  p,q for Point of TOP-REAL 2,
  G for Go-board;

canceled 2;

theorem Th3:
  for GX being TopSpace, A1,A2,B being Subset of GX
  st A1 is_a_component_of B & A2 is_a_component_of B holds
  A1 = A2 or A1 misses A2
proof
  let GX be TopSpace, A1,A2,B be Subset of GX;
  assume A1 is_a_component_of B;
  then consider B1 being Subset of GX|B such that
A1: B1 = A1 and
A2: B1 is_a_component_of GX|B by CONNSP_1:def 6;
  assume A2 is_a_component_of B;
  then ex B2 being Subset of GX|B st B2 = A2 & B2 is_a_component_of GX|B
  by CONNSP_1:def 6;
  hence A1 = A2 or A1 misses A2 by A1,A2,CONNSP_1:37;
end;

theorem Th4:
  for GX being TopSpace, A,B being Subset of GX,
  AA being Subset of GX|B st A = AA holds GX|A = GX|B|AA
proof
  let GX be TopSpace, A,B be Subset of GX;
  let AA be Subset of GX|B;
  assume
A1: A = AA;
  the carrier of GX|A = [#](GX|A)
    .= A by PRE_TOPC:def 10;
  then reconsider GY = GX|A as strict SubSpace of GX|B by A1,TSEP_1:4;
  [#] GY = AA by A1,PRE_TOPC:def 10;
  hence GX|A = GX|B|AA by PRE_TOPC:def 10;
end;

theorem Th5:
  for GX being non empty TopSpace, A, B being non empty Subset of GX
  st A c= B & A is connected
  ex C being Subset of GX st C is_a_component_of B & A c= C
proof
  let GX be non empty TopSpace, A, B be non empty Subset of GX such that
A1: A c= B and
A2: A is connected;
  consider p being set such that
A3: p in A by XBOOLE_0:def 1;
A4: B = [#](GX|B) by PRE_TOPC:def 10
    .= the carrier of GX|B;
  then reconsider p as Point of GX|B by A1,A3;
  reconsider C = Component_of p as Subset of GX by PRE_TOPC:39;
  take C;
A5: Component_of p is_a_component_of GX|B by CONNSP_1:43;
  hence C is_a_component_of B by CONNSP_1:def 6;
  reconsider AA = A as Subset of GX|B by A1,A4;
  GX|A is connected by A2,CONNSP_1:def 3;
  then GX|B|AA is connected by Th4;
  then
A6: AA is connected by CONNSP_1:def 3;
  p in Component_of p by CONNSP_1:40;
  then AA /\ Component_of p <> {}(GX|B) by A3,XBOOLE_0:def 4;
  then AA meets Component_of p by XBOOLE_0:def 7;
  hence A c= C by A5,A6,CONNSP_1:38;
end;

theorem Th6:
  for GX being non empty TopSpace, A,B,C,D being Subset of GX
  st B is connected & C is_a_component_of D & A c= C & A meets B & B c= D
  holds B c= C
proof
  let GX be non empty TopSpace, A,B,C,D be Subset of GX such that
A1: B is connected and
A2: C is_a_component_of D and
A3: A c= C and
A4: A meets B and
A5: B c= D;
A6: A <> {} & B <> {} by A4,XBOOLE_1:65;
  reconsider A,B as non empty Subset of GX by A4,XBOOLE_1:65;
  reconsider C,D as non empty Subset of GX by A3,A5,A6,XBOOLE_1:3;
  consider CC being Subset of GX such that
A7: CC is_a_component_of D and
A8: B c= CC by A1,A5,Th5;
A9: A meets CC by A4,A8,XBOOLE_1:63;
  consider C1 being Subset of GX|D such that
A10: C1 = C and
A11: C1 is_a_component_of GX|D by A2,CONNSP_1:def 6;
  consider CC1 being Subset of GX|D such that
A12: CC1 = CC and
A13: CC1 is_a_component_of GX|D by A7,CONNSP_1:def 6;
  thus thesis by A3,A8,A9,A10,A11,A12,A13,CONNSP_1:37,XBOOLE_1:63;
end;

theorem Th7:
  LSeg(p,q) is convex
proof
  for w1,w2 be Point of TOP-REAL 2 st w1 in LSeg(p,q) & w2 in LSeg(p,q)
  holds LSeg(w1,w2) c= LSeg(p,q) by TOPREAL1:12;
  hence LSeg(p,q) is convex by JORDAN1:def 1;
end;

theorem Th8:
  LSeg(p,q) is connected by Th7,JORDAN1:9;

registration
  cluster convex non empty Subset of TOP-REAL 2;
  existence
  proof consider p being Point of TOP-REAL 2;
    take LSeg(p,p);
    thus thesis by Th7;
  end;
end;

theorem Th9:
  for P,Q being convex Subset of TOP-REAL 2 holds P /\ Q is convex
proof
  let P,Q be convex Subset of TOP-REAL 2;
  let p,q;
  assume
A1: p in P /\ Q & q in P /\ Q;
  then p in P & q in P by XBOOLE_0:def 4;
  then
A2: LSeg(p,q) c= P by JORDAN1:def 1;
  p in Q & q in Q by A1,XBOOLE_0:def 4;
  then LSeg(p,q) c= Q by JORDAN1:def 1;
  hence LSeg(p,q) c= P /\ Q by A2,XBOOLE_1:19;
end;

theorem Th10:
  for f being FinSequence of TOP-REAL 2 holds Rev X_axis f = X_axis Rev f
proof
  let f be FinSequence of TOP-REAL 2;
A1: len Rev X_axis f = len X_axis f by FINSEQ_5:def 3
    .= len f by GOBOARD1:def 3
    .= len Rev f by FINSEQ_5:def 3;
  len X_axis f = len f by GOBOARD1:def 3;
  then
A2: dom X_axis f = dom f by FINSEQ_3:31;
A3: len f = len Rev f by FINSEQ_5:def 3;
  now
    let k such that
A4: k in dom Rev X_axis f;
    set l = len f + 1 -' k;
    k <= len f & len f < len f + 1 by A1,A3,A4,FINSEQ_3:27,NAT_1:13;
    then
A5: l + k = len f + 1 by XREAL_1:237,XXREAL_0:2;
A6: Rev Rev X_axis f = X_axis f by FINSEQ_6:29;
    then
A7: l in dom X_axis f by A1,A3,A4,A5,FINSEQ_5:62;
    thus (Rev X_axis f).k = (Rev X_axis f)/.k by A4,PARTFUN1:def 8
      .= (X_axis f)/.l by A1,A3,A4,A5,A6,FINSEQ_5:69
      .= (X_axis f).l by A7,PARTFUN1:def 8
      .= (f/.l)`1 by A7,GOBOARD1:def 3
      .= ((Rev f)/.k)`1 by A1,A2,A3,A4,A5,A6,FINSEQ_5:62,69;
  end;
  hence Rev X_axis f = X_axis Rev f by A1,GOBOARD1:def 3;
end;

theorem Th11:
  for f being FinSequence of TOP-REAL 2 holds Rev Y_axis f = Y_axis Rev f
proof
  let f be FinSequence of TOP-REAL 2;
A1: len Rev Y_axis f = len Y_axis f by FINSEQ_5:def 3
    .= len f by GOBOARD1:def 4
    .= len Rev f by FINSEQ_5:def 3;
  len Y_axis f = len f by GOBOARD1:def 4;
  then
A2: dom Y_axis f = dom f by FINSEQ_3:31;
A3: len f = len Rev f by FINSEQ_5:def 3;
  now
    let k such that
A4: k in dom Rev Y_axis f;
    set l = len f + 1 -' k;
    k <= len f & len f < len f + 1 by A1,A3,A4,FINSEQ_3:27,NAT_1:13;
    then
A5: l + k = len f + 1 by XREAL_1:237,XXREAL_0:2;
A6: Rev Rev Y_axis f = Y_axis f by FINSEQ_6:29;
    then
A7: l in dom Y_axis f by A1,A3,A4,A5,FINSEQ_5:62;
    thus (Rev Y_axis f).k = (Rev Y_axis f)/.k by A4,PARTFUN1:def 8
      .= (Y_axis f)/.l by A1,A3,A4,A5,A6,FINSEQ_5:69
      .= (Y_axis f).l by A7,PARTFUN1:def 8
      .= (f/.l)`2 by A7,GOBOARD1:def 4
      .= ((Rev f)/.k)`2 by A1,A2,A3,A4,A5,A6,FINSEQ_5:62,69;
  end;
  hence Rev Y_axis f = Y_axis Rev f by A1,GOBOARD1:def 4;
end;

Lm1: for f,ff being non empty FinSequence of TOP-REAL 2 st ff = Rev f
holds GoB ff = GoB f
proof
  let f,ff be non empty FinSequence of TOP-REAL 2;
  assume
A1: ff = Rev f;
  then
A2: Rev X_axis f = X_axis ff by Th10;
A3: rng Incr X_axis ff = rng X_axis ff by GOBOARD2:def 2
    .= rng X_axis f by A2,FINSEQ_5:60;
  len Incr X_axis ff = card rng X_axis ff by GOBOARD2:def 2
    .= card rng X_axis f by A2,FINSEQ_5:60;
  then
A4: Incr X_axis f = Incr X_axis ff by A3,GOBOARD2:def 2;
A5: Rev Y_axis f = Y_axis ff by A1,Th11;
A6: rng Incr Y_axis ff = rng Y_axis ff by GOBOARD2:def 2
    .= rng Y_axis f by A5,FINSEQ_5:60;
  len Incr Y_axis ff = card rng Y_axis ff by GOBOARD2:def 2
    .= card rng Y_axis f by A5,FINSEQ_5:60;
  then Incr Y_axis f = Incr Y_axis ff by A6,GOBOARD2:def 2;
  hence GoB ff = GoB(Incr X_axis f,Incr Y_axis f) by A4,GOBOARD2:def 3
    .= GoB f by GOBOARD2:def 3;
end;

registration
  cluster non constant FinSequence;
  existence
  proof
    take <*1,2*>,1,2;
    1 in {1,2} & 2 in {1,2} by TARSKI:def 2;
    hence 1 in dom <*1,2*> & 2 in dom <*1,2*> by FINSEQ_1:4,FINSEQ_3:29;
    <*1,2*>.1 = 1 & <*1,2*>.2 = 2 by FINSEQ_1:61;
    hence thesis;
  end;
end;

registration
  let f be non constant FinSequence;
  cluster Rev f -> non constant;
  coherence
  proof
    consider i,j such that
A1: i in dom f & j in dom f and
A2: f.i <> f.j by GOBOARD1:def 2;
    i <= len f & j <= len f by A1,FINSEQ_3:27;
    then reconsider k1 = len f -i, l1 = len f -j as Element of NAT by INT_1:18;
    take k = k1+1, l = l1+1;
    k + i = len f +1;
    hence k in dom Rev f by A1,FINSEQ_5:62;
    then
A3: (Rev f).k = f.(len f -k +1) by FINSEQ_5:def 3
      .= f.(0 + i);
    l + j = len f +1;
    hence l in dom Rev f by A1,FINSEQ_5:62;
    then (Rev f).l = f.(len f -l +1) by FINSEQ_5:def 3
      .= f.(0 + j);
    hence thesis by A2,A3;
  end;
end;

definition
  let f be standard special_circular_sequence;
  redefine func Rev f -> standard special_circular_sequence;
  coherence
  proof
    reconsider ff = Rev f as non empty FinSequence of TOP-REAL 2;
A1: Rev ff = f by FINSEQ_6:29;
A2: GoB ff = GoB f by Lm1;
A3: f is_sequence_on GoB f by GOBOARD5:def 5;
A4: len f = len ff by FINSEQ_5:def 3;
A5: ff is standard
    proof
      hereby
        let k such that
A6:     k in dom ff;
        set l = len f + 1 -' k;
        k <= len f & len f < len f + 1 by A4,A6,FINSEQ_3:27,NAT_1:13;
        then
A7:     l + k = len f + 1 by XREAL_1:237,XXREAL_0:2;
        then l in dom f by A1,A4,A6,FINSEQ_5:62;
        then consider i,j such that
A8:     [i,j] in Indices GoB f and
A9:     f/.l = (GoB f)*(i,j) by A3,GOBOARD1:def 11;
        take i,j;
        thus [i,j] in Indices GoB ff by A8,Lm1;
        thus ff/.k = (GoB ff)*(i,j) by A1,A2,A4,A6,A7,A9,FINSEQ_5:69;
      end;
      let k such that
A10:  k in dom ff and
A11:  k+1 in dom ff;
      set l = len f + 1 -' (k + 1);
      k <= len f by A4,A10,FINSEQ_3:27;
      then k+1 <= len f +1 by XREAL_1:8;
      then
A12:  l + (k+1) = len f + 1 by XREAL_1:237;
      then
A13:  l in dom f by A1,A4,A11,FINSEQ_5:62;
A14:  l+1 + k = len f + 1 by A12;
      then
A15:  l+1 in dom f by A1,A4,A10,FINSEQ_5:62;
      let i1,j1,i2,j2 such that
A16:  [i1,j1] in Indices GoB ff and
A17:  [i2,j2] in Indices GoB ff and
A18:  ff/.k = (GoB ff)*(i1,j1) and
A19:  ff/.(k+1) = (GoB ff)*(i2,j2);
A20:  abs(i2-i1) = abs(-(i1-i2))
        .= abs(i1-i2) by COMPLEX1:138;
A21:  abs(j2-j1) = abs(-(j1-j2))
        .= abs(j1-j2) by COMPLEX1:138;
A22:  f/.l = (GoB f)*(i2,j2) by A1,A2,A4,A11,A12,A19,FINSEQ_5:69;
      f/.(l+1) = (GoB f)*(i1,j1) by A1,A2,A4,A10,A14,A18,FINSEQ_5:69;
      hence abs(i1-i2)+abs(j1-j2) = 1
      by A2,A3,A13,A15,A16,A17,A20,A21,A22,GOBOARD1:def 11;
    end;
A23: ff is circular
    proof
      thus ff/.1 = f/.len f by FINSEQ_5:68
        .= f/.1 by FINSEQ_6:def 1
        .= ff/.len ff by A4,FINSEQ_5:68;
    end;
    ff is s.c.c.
    proof
      let i,j such that
A24:  i+1 < j and
A25:  i > 1 & j < len ff or j+1 < len ff;
      set k = len f -' i, l = len f -' j;
A26:  j <= len f by A4,A25,NAT_1:13;
      then
A27:  l + j = len f by XREAL_1:237;
      i < j by A24,NAT_1:13;
      then
A28:  k + i = len f by A26,XREAL_1:237,XXREAL_0:2;
      then i+1+k = l+1+j by A27;
      then l+1+j < j+k by A24,XREAL_1:8;
      then
A29:  l+1 < k by XREAL_1:8;
A30:  j+1 <= len f by A4,A25,NAT_1:13;
      per cases by A30,NAT_1:14,XXREAL_0:1;
      suppose j+1 = len f;
        then k+1 < len f by A25,A28,FINSEQ_5:def 3,XREAL_1:8;
        then LSeg(f,k) misses LSeg(f,l) by A29,GOBOARD5:def 4;
        then
A31:    LSeg(f,k) /\ LSeg(f,l) = {} by XBOOLE_0:def 7;
        LSeg(f,k) = LSeg(ff,i) by A28,SPPOL_2:2;
        hence LSeg(ff,i) /\ LSeg(ff,j) = {} by A27,A31,SPPOL_2:2;
      end;
      suppose that
A32:    i >= 1 and
A33:    j+1 < len f;
A34:    l > 1 by A27,A33,XREAL_1:8;
        k+1 <= len f by A28,A32,XREAL_1:8;
        then k < len f by NAT_1:13;
        then LSeg(f,k) misses LSeg(f,l) by A29,A34,GOBOARD5:def 4;
        then
A35:    LSeg(f,k) /\ LSeg(f,l) = {} by XBOOLE_0:def 7;
        LSeg(f,k) = LSeg(ff,i) by A28,SPPOL_2:2;
        hence LSeg(ff,i) /\ LSeg(ff,j) = {} by A27,A35,SPPOL_2:2;
      end;
      suppose i = 0;
        then LSeg(ff,i) = {} by TOPREAL1:def 5;
        hence LSeg(ff,i) /\ LSeg(ff,j) = {};
      end;
    end;
    hence thesis by A5,A23,SPPOL_2:29,42;
  end;
end;

theorem Th12:
  i >= 1 & j >= 1 & i+j = len f implies left_cell(f,i) = right_cell(Rev f,j)
proof
  assume that
A1: i >= 1 and
A2: j >= 1 and
A3: i+j = len f;
A4: i+1 <= len f by A2,A3,XREAL_1:8;
  len f = len Rev f by FINSEQ_5:def 3;
  then
A5: j+1 <= len Rev f by A1,A3,XREAL_1:8;
A6: GoB Rev f = GoB f by Lm1;
  now
    let i1,j1,i2,j2 be Element of NAT such that
A7: [i1,j1] in Indices GoB f and
A8: [i2,j2] in Indices GoB f and
A9: f/.i = (GoB f)*(i1,j1) and
A10: f/.(i+1) = (GoB f)*(i2,j2);
    1 <= i+1 by NAT_1:11;
    then
A11: i+1 in dom f by A4,FINSEQ_3:27;
    i+1+j = len f + 1 by A3;
    then
A12: (Rev f)/.j = (GoB f)*(i2,j2) by A10,A11,FINSEQ_5:69;
    i <= len f by A4,NAT_1:13;
    then
A13: i in dom f by A1,FINSEQ_3:27;
    j+1+i = len f + 1 by A3;
    then
A14: (Rev f)/.(j+1) = (GoB f)*(i1,j1) by A9,A13,FINSEQ_5:69;
    1 <= i1 by A7,MATRIX_1:39;
    then
A15: i1-'1+1 = i1 by XREAL_1:237;
    1 <= j1 by A7,MATRIX_1:39;
    then
A16: j1-'1+1 = j1 by XREAL_1:237;
    reconsider i1'=i1, i2'=i2, j1'=j1, j2'=j2 as Element of REAL;
    f is_sequence_on GoB f by GOBOARD5:def 5;
    then abs(i1-i2)+abs(j1-j2) = 1 by A7,A8,A9,A10,A11,A13,GOBOARD1:def 11;
    then
A17: abs(i1'-i2')=1 & j1=j2 or abs(j1'-j2')=1 & i1=i2 by GOBOARD1:2;
    per cases by A17,GOBOARD1:1;
    case i1 = i2 & j1+1 = j2;
      hence right_cell(Rev f,j) = cell(GoB f,i1-'1,j1)
      by A2,A5,A6,A7,A8,A12,A14,A15,GOBOARD5:31;
    end;
    case i1+1 = i2 & j1 = j2;
      hence right_cell(Rev f,j) = cell(GoB f,i1,j1)
      by A2,A5,A6,A7,A8,A12,A14,A16,GOBOARD5:30;
    end;
    case i1 = i2+1 & j1 = j2;
      hence right_cell(Rev f,j) = cell(GoB f,i2,j2-'1)
      by A2,A5,A6,A7,A8,A12,A14,A16,GOBOARD5:29;
    end;
    case i1 = i2 & j1 = j2+1;
      hence right_cell(Rev f,j) = cell(GoB f,i1,j2)
      by A2,A5,A6,A7,A8,A12,A14,A15,GOBOARD5:28;
    end;
  end;
  hence left_cell(f,i) = right_cell(Rev f,j) by A1,A4,GOBOARD5:def 7;
end;

theorem Th13:
  i >= 1 & j >= 1 & i+j = len f implies left_cell(Rev f,i) = right_cell(f,j)
proof
  len Rev f = len f & Rev Rev f = f by FINSEQ_5:def 3,FINSEQ_6:29;
  hence thesis by Th12;
end;

theorem Th14:
  1 <= k & k+1 <= len f implies ex i,j st i <= len GoB f & j <= width GoB f &
  cell(GoB f,i,j) = left_cell(f,k)
proof
  assume that
A1: 1 <= k and
A2: k+1 <= len f;
A3: f is_sequence_on GoB f by GOBOARD5:def 5;
  k <= len f by A2,NAT_1:13;
  then
A4: k in dom f by A1,FINSEQ_3:27;
  then consider i1,j1 being Element of NAT such that
A5: [i1,j1] in Indices GoB f and
A6: f/.k = (GoB f)*(i1,j1) by A3,GOBOARD1:def 11;
  1 <= k+1 by NAT_1:11;
  then
A7: k+1 in dom f by A2,FINSEQ_3:27;
  then consider i2,j2 being Element of NAT such that
A8: [i2,j2] in Indices GoB f and
A9: f/.(k+1) = (GoB f)*(i2,j2) by A3,GOBOARD1:def 11;
  1 <= i1 by A5,MATRIX_1:39;
  then
A10: i1-'1+1 = i1 by XREAL_1:237;
  1 <= j1 by A5,MATRIX_1:39;
  then
A11: j1-'1+1 = j1 by XREAL_1:237;
  reconsider i1'=i1, i2'=i2, j1'=j1, j2'=j2 as Element of REAL;
  abs(i1-i2)+abs(j1-j2) = 1 by A3,A4,A5,A6,A7,A8,A9,GOBOARD1:def 11;
  then
A12: abs(i1'-i2')=1 & j1=j2 or abs(j1'-j2')=1 & i1=i2 by GOBOARD1:2;
A13: i1 <= len GoB f by A5,MATRIX_1:39;
A14: j1 <= width GoB f by A5,MATRIX_1:39;
  per cases by A12,GOBOARD1:1;
  suppose
A15: i1 = i2 & j1+1 = j2;
    take i1-'1,j1;
    i1-'1 <= i1 by NAT_D:35;
    hence i1-'1 <= len GoB f by A13,XXREAL_0:2;
    thus j1 <= width GoB f by A5,MATRIX_1:39;
    thus left_cell(f,k) = cell(GoB f,i1-'1,j1)
    by A1,A2,A5,A6,A8,A9,A10,A15,GOBOARD5:28;
  end;
  suppose
A16: i1+1 = i2 & j1 = j2;
    take i1,j1;
    thus i1 <= len GoB f by A5,MATRIX_1:39;
    thus j1 <= width GoB f by A5,MATRIX_1:39;
    thus left_cell(f,k) = cell(GoB f,i1,j1)
    by A1,A2,A5,A6,A8,A9,A11,A16,GOBOARD5:29;
  end;
  suppose
A17: i1 = i2+1 & j1 = j2;
    take i2,j1-'1;
    thus i2 <= len GoB f by A8,MATRIX_1:39;
    j1-'1 <= j1 by NAT_D:35;
    hence j1-'1 <= width GoB f by A14,XXREAL_0:2;
    thus left_cell(f,k) = cell(GoB f,i2,j1-'1)
    by A1,A2,A5,A6,A8,A9,A11,A17,GOBOARD5:30;
  end;
  suppose
A18: i1 = i2 & j1 = j2+1;
    take i1,j2;
    thus i1 <= len GoB f by A5,MATRIX_1:39;
    thus j2 <= width GoB f by A8,MATRIX_1:39;
    thus left_cell(f,k) = cell(GoB f,i1,j2)
    by A1,A2,A5,A6,A8,A9,A10,A18,GOBOARD5:31;
  end;
end;

theorem Th15:
  j <= width G implies Int h_strip(G,j) is convex
proof
  assume
A1: j <= width G;
  per cases by A1,NAT_1:14,XXREAL_0:1;
  suppose j = 0;
    then Int h_strip(G,j) = { |[r,s]| : s < G*(1,1)`2 } by GOBOARD6:18;
    hence Int h_strip(G,j) is convex by JORDAN1:22;
  end;
  suppose j = width G;
    then Int h_strip(G,j) = { |[r,s]| : G*(1,width G)`2 < s } by GOBOARD6:19;
    hence Int h_strip(G,j) is convex by JORDAN1:20;
  end;
  suppose 1 <= j & j < width G;
    then
A2: Int h_strip(G,j) = { |[r,s]| : G*(1,j)`2 < s & s < G*(1,j+1)`2 }
    by GOBOARD6:20;
A3: { |[r,s]| : G*(1,j)`2 < s} c= the carrier of TOP-REAL 2
    proof
      let x be set;
      assume x in { |[r,s]| : G*(1,j)`2 < s};
      then ex r,s st x = |[r,s]| & G*(1,j)`2 < s;
      hence thesis;
    end;
    { |[r,s]| : s < G*(1,j+1)`2 } c= the carrier of TOP-REAL 2
    proof
      let x be set;
      assume x in { |[r,s]| : s < G*(1,j+1)`2 };
      then ex r,s st x = |[r,s]| & s < G*(1,j+1)`2;
      hence thesis;
    end;
    then reconsider P = { |[r,s]| : G*(1,j)`2 < s},
    Q = { |[r,s]| : s < G*(1,j+1)`2 } as Subset of TOP-REAL 2 by A3;
A4: Int h_strip(G,j) = P /\ Q
    proof
      hereby
        let x be set;
        assume x in Int h_strip(G,j);
        then ex r,s st x = |[r,s]| & G*(1,j)`2 < s & s < G*(1,j+1)`2 by A2;
        then x in P & x in Q;
        hence x in P /\ Q by XBOOLE_0:def 4;
      end;
      let x be set;
      assume
A5:   x in P /\ Q;
      then x in P by XBOOLE_0:def 4;
      then consider r1,s1 such that
A6:   x = |[r1,s1]| and
A7:   G*(1,j)`2 < s1;
      x in Q by A5,XBOOLE_0:def 4;
      then consider r2,s2 such that
A8:   x = |[r2,s2]| and
A9:   s2 < G*(1,j+1)`2;
      r1 = r2 & s1 = s2 by A6,A8,SPPOL_2:1;
      hence x in Int h_strip(G,j) by A2,A6,A7,A9;
    end;
A10: P is convex by JORDAN1:20;
    Q is convex by JORDAN1:22;
    hence Int h_strip(G,j) is convex by A4,A10,Th9;
  end;
end;

theorem Th16:
  i <= len G implies Int v_strip(G,i) is convex
proof
  assume
A1: i <= len G;
  per cases by A1,NAT_1:14,XXREAL_0:1;
  suppose i = 0;
    then Int v_strip(G,i) = { |[r,s]| : r < G*(1,1)`1 } by GOBOARD6:15;
    hence Int v_strip(G,i) is convex by JORDAN1:18;
  end;
  suppose i = len G;
    then Int v_strip(G,i) = { |[r,s]| : G*(len G,1)`1 < r } by GOBOARD6:16;
    hence Int v_strip(G,i) is convex by JORDAN1:16;
  end;
  suppose 1 <= i & i < len G;
    then
A2: Int v_strip(G,i) = { |[r,s]| : G*(i,1)`1 < r & r < G*(i+1,1)`1 }
    by GOBOARD6:17;
A3: { |[r,s]| : G*(i,1)`1 < r} c= the carrier of TOP-REAL 2
    proof
      let x be set;
      assume x in { |[r,s]| : G*(i,1)`1 < r};
      then ex r,s st x = |[r,s]| & G*(i,1)`1 < r;
      hence thesis;
    end;
    { |[r,s]| : r < G*(i+1,1)`1 } c= the carrier of TOP-REAL 2
    proof
      let x be set;
      assume x in { |[r,s]| : r < G*(i+1,1)`1 };
      then ex r,s st x = |[r,s]| & r < G*(i+1,1)`1;
      hence thesis;
    end;
    then reconsider P = { |[r,s]| : G*(i,1)`1 < r},
    Q = { |[r,s]| : r < G*(i+1,1)`1 } as Subset of TOP-REAL 2 by A3;
A4: Int v_strip(G,i) = P /\ Q
    proof
      hereby
        let x be set;
        assume x in Int v_strip(G,i);
        then ex r,s st x = |[r,s]| & G*(i,1)`1 < r & r < G*(i+1,1)`1 by A2;
        then x in P & x in Q;
        hence x in P /\ Q by XBOOLE_0:def 4;
      end;
      let x be set;
      assume
A5:   x in P /\ Q;
      then x in P by XBOOLE_0:def 4;
      then consider r1,s1 such that
A6:   x = |[r1,s1]| and
A7:   G*(i,1)`1 < r1;
      x in Q by A5,XBOOLE_0:def 4;
      then consider r2,s2 such that
A8:   x = |[r2,s2]| and
A9:   r2 < G*(i+1,1)`1;
      r1 = r2 & s1 = s2 by A6,A8,SPPOL_2:1;
      hence x in Int v_strip(G,i) by A2,A6,A7,A9;
    end;
A10: P is convex by JORDAN1:16;
    Q is convex by JORDAN1:18;
    hence Int v_strip(G,i) is convex by A4,A10,Th9;
  end;
end;

theorem Th17:
  i <= len G & j <= width G implies Int cell(G,i,j) <> {}
proof
  assume
A1: i <= len G & j <= width G;
  then
A2: j = width G or j < width G by XXREAL_0:1;
A3: i = len G or i < len G by A1,XXREAL_0:1;
  consider p being Point of TOP-REAL 2;
  per cases by A2,A3,NAT_1:13,14;
  suppose 1 <= i & i+1 <= len G & 1 <= j & j+1 <= width G;
    then LSeg(1/2*(G*(i,j)+G*
    (i+1,j+1)),p) meets Int cell(G,i,j) by GOBOARD6:85;
    hence Int cell(G,i,j) <> {} by XBOOLE_1:65;
  end;
  suppose 1 <= i & i+1 <= len G & j = width G;
    then LSeg(p,1/2*(G*(i,j)+G*(i+1,j))+|[0,1]|) meets Int cell(G,i,j)
    by GOBOARD6:86;
    hence Int cell(G,i,j) <> {} by XBOOLE_1:65;
  end;
  suppose 1 <= i & i+1 <= len G & j = 0;
    then LSeg(1/2*(G*(i,j+1)+G*(i+1,j+1))- |[0,1]|,p) meets Int cell(G,i,j)
    by GOBOARD6:87;
    hence Int cell(G,i,j) <> {} by XBOOLE_1:65;
  end;
  suppose 1 <= j & j+1 <= width G & i = 0;
    then LSeg(1/2*(G*(i+1,j)+G*(i+1,j+1))- |[1,0]|,p) meets Int cell(G,i,j)
    by GOBOARD6:88;
    hence Int cell(G,i,j) <> {} by XBOOLE_1:65;
  end;
  suppose 1 <= j & j+1 <= width G & i = len G;
    then LSeg(p,1/2*(G*(i,j)+G*(i,j+1))+|[1,0]|) meets Int cell(G,i,j)
    by GOBOARD6:89;
    hence Int cell(G,i,j) <> {} by XBOOLE_1:65;
  end;
  suppose i = 0 & j = 0;
    then LSeg(p,G*(i+1,j+1)- |[1,1]|) meets Int cell(G,i,j) by GOBOARD6:90;
    hence Int cell(G,i,j) <> {} by XBOOLE_1:65;
  end;
  suppose i = len G & j = width G;
    then LSeg(p,G*(i,j)+|[1,1]|) meets Int cell(G,i,j) by GOBOARD6:91;
    hence Int cell(G,i,j) <> {} by XBOOLE_1:65;
  end;
  suppose i = 0 & j = width G;
    then LSeg(p,G*(i+1,j)+|[-1,1]|) meets Int cell(G,i,j) by GOBOARD6:92;
    hence Int cell(G,i,j) <> {} by XBOOLE_1:65;
  end;
  suppose i = len G & j = 0;
    then LSeg(p,G*(i,j+1)+|[1,-1]|) meets Int cell(G,i,j) by GOBOARD6:93;
    hence Int cell(G,i,j) <> {} by XBOOLE_1:65;
  end;
end;

theorem Th18:
  1 <= k & k+1 <= len f implies Int left_cell(f,k) <> {}
proof
  assume 1 <= k & k+1 <= len f;
  then ex i,j st i <= len GoB f & j <=
  width GoB f & cell(GoB f,i,j) = left_cell(f,k) by Th14;
  hence thesis by Th17;
end;

theorem Th19:
  1 <= k & k+1 <= len f implies Int right_cell(f,k) <> {}
proof
  assume that
A1: 1 <= k and
A2: k+1 <= len f;
A3: len f = len Rev f by FINSEQ_5:def 3;
  k <= len f by A2,NAT_1:13;
  then
A4: len f-'k+k = len f by XREAL_1:237;
  then
A5: len f-'k+1 <= len f by A1,XREAL_1:8;
A6: len f-'k >= 1 by A2,A4,XREAL_1:8;
  then right_cell(f,k) = left_cell(Rev f,len f-'k) by A1,A4,Th13;
  hence Int right_cell(f,k) <> {} by A3,A5,A6,Th18;
end;

theorem Th20:
  i <= len G & j <= width G implies Int cell(G,i,j) is convex
proof
  assume
A1: i <= len G & j <= width G;
  set P = Int cell(G,i,j);
A2: P = Int v_strip(G,i) /\ Int h_strip(G,j) by TOPS_1:46;
  Int v_strip(G,i) is convex & Int h_strip(G,j) is convex by A1,Th15,Th16;
  hence P is convex by A2,Th9;
end;

theorem Th21:
  i <= len G & j <= width G implies Int cell(G,i,j) is connected
  by Th20,JORDAN1:9;

theorem Th22:
  1 <= k & k+1 <= len f implies Int left_cell(f,k) is connected
proof
  assume 1 <= k & k+1 <= len f;
  then ex i,j st i <= len GoB f & j <=
  width GoB f & cell(GoB f,i,j) = left_cell(f,k) by Th14;
  hence thesis by Th21;
end;

theorem Th23:
  1 <= k & k+1 <= len f implies Int right_cell(f,k) is connected
proof
  assume that
A1: 1 <= k and
A2: k+1 <= len f;
A3: len f = len Rev f by FINSEQ_5:def 3;
  k <= len f by A2,NAT_1:13;
  then
A4: len f-'k+k = len f by XREAL_1:237;
  then
A5: len f-'k+1 <= len f by A1,XREAL_1:8;
A6: len f-'k >= 1 by A2,A4,XREAL_1:8;
  then right_cell(f,k) = left_cell(Rev f,len f-'k) by A1,A4,Th13;
  hence Int right_cell(f,k) is connected by A3,A5,A6,Th22;
end;

definition
  let f;
A1: 1+1 < len f by GOBOARD7:36,XXREAL_0:2;
  then
A2: Int left_cell(f,1) <> {} by Th18;
A3: Int right_cell(f,1) <> {} by A1,Th19;
  func LeftComp f -> Subset of TOP-REAL 2 means
  :Def1:
  it is_a_component_of (L~f)` & Int left_cell(f,1) c= it;
  existence
  proof
A4: Int left_cell(f,1) is connected by A1,Th22;
    Int left_cell(f,1) misses L~f by A1,GOBOARD8:37;
    then
A5: Int left_cell(f,1) c= (L~f)` by SUBSET_1:43;
    then (L~f)` <> {} by A1,Th18,XBOOLE_1:3;
    hence thesis by A2,A4,A5,Th5;
  end;
  uniqueness by A2,Th3,XBOOLE_1:67;
  func RightComp f -> Subset of TOP-REAL 2 means
  :Def2:
  it is_a_component_of (L~f)` & Int right_cell(f,1) c= it;
  existence
  proof
A6: Int right_cell(f,1) is connected by A1,Th23;
    Int right_cell(f,1) misses L~f by A1,GOBOARD8:38;
    then
A7: Int right_cell(f,1) c= (L~f)` by SUBSET_1:43;
    then (L~f)` <> {} by A1,Th19,XBOOLE_1:3;
    hence thesis by A3,A6,A7,Th5;
  end;
  uniqueness by A3,Th3,XBOOLE_1:67;
end;

theorem Th24:
  for k st 1 <= k & k+1 <= len f holds Int left_cell(f,k) c= LeftComp f
proof
  defpred P[Element of NAT] means
  1 <= $1 & $1+1 <= len f implies Int left_cell(f,$1) c= LeftComp f;
A1: P[0];
A2: now
    let k such that
A3: P[k];
    thus P[k+1]
    proof
      assume that
A4:   1 <= k+1 and
A5:   k+1+1 <= len f;
      per cases by NAT_1:14;
      suppose k = 0;
        hence Int left_cell(f,k+1) c= LeftComp f by Def1;
      end;
      suppose
A6:     k >= 1;
A7:     k+1 <= len f by A5,NAT_1:13;
A8:     k <= k+1 by NAT_1:11;
        then k <= len f by A7,XXREAL_0:2;
        then
A9:     k in dom f by A6,FINSEQ_3:27;
        then consider i0,j0 being Element of NAT such that
A10:    [i0,j0] in Indices GoB f and
A11:    f/.k = (GoB f)*(i0,j0) by GOBOARD2:25;
A12:    k+1 in dom f by A4,A7,FINSEQ_3:27;
        then consider i1,j1 being Element of NAT such that
A13:    [i1,j1] in Indices GoB f and
A14:    f/.(k+1) = (GoB f)*(i1,j1) by GOBOARD2:25;
        k+1+1 >= 1 by NAT_1:11;
        then
A15:    k+1+1 in dom f by A5,FINSEQ_3:27;
        then consider i2,j2 being Element of NAT such that
A16:    [i2,j2] in Indices GoB f and
A17:    f/.(k+1+1) = (GoB f)*(i2,j2) by GOBOARD2:25;
A18:    1 <= i0 & i0 <= len GoB f by A10,MATRIX_1:39;
A19:    1 <= i1 & i1 <= len GoB f by A13,MATRIX_1:39;
A20:    1 <= i2 & i2 <= len GoB f by A16,MATRIX_1:39;
A21:    1 <= j0 & j0 <= width GoB f by A10,MATRIX_1:39;
A22:    1 <= j1 & j1 <= width GoB f by A13,MATRIX_1:39;
A23:    1 <= j2 & j2 <= width GoB f by A16,MATRIX_1:39;
A24:    i0 = i1 or j0 = j1 by A9,A10,A11,A12,A13,A14,GOBOARD2:16;
A25:    i1 = i2 or j1 = j2 by A12,A13,A14,A15,A16,A17,GOBOARD2:16;
        reconsider i1'=i1, i0'=i0, i2'=i2, j1'=j1, j0'=j0, j2'=j2
        as Element of REAL;
A26:    f is_sequence_on GoB f by GOBOARD5:def 5;

then abs(i0-i1)+abs(j0-j1) = 1 by A9,A10,A11,A12,A13,A14,GOBOARD1:def 11;
        then
A27:    abs(i0'-i1')=1 & j0 = j1 or abs(j0'-j1')=1 & i0=i1 by GOBOARD1:2;
        then
A28:    i0 = i1 or i0 = i1+1 or i0+1 = i1 by GOBOARD1:1;

abs(i1-i2)+abs(j1-j2) = 1 by A12,A13,A14,A15,A16,A17,A26,GOBOARD1:def 11;
        then
A29:    abs(i1'-i2')=1 & j1 = j2 or abs(j1'-j2')=1 & i1=i2 by GOBOARD1:2;
        then
A30:    i1 = i2 or i1 = i2+1 or i1+1 = i2 by GOBOARD1:1;
A31:    j0 = j1 or j0 = j1+1 or j0+1 = j1 by A27,GOBOARD1:1;
A32:    j1 = j2 or j1 = j2+1 or j1+1 = j2 by A29,GOBOARD1:1;
A33:    now
          assume
A34:      i0 = i2 & j0 = j2;
A35:      now
            assume k <= 1;
            then
A36:        k = 1 by A6,XXREAL_0:1;
            assume k+1+1 >= len f;
            then k+1+1 = len f by A5,XXREAL_0:1;
            hence contradiction by A36,GOBOARD7:36;
          end;
A37:      k < k+1+1 by A8,NAT_1:13;
          per cases by A35;
          suppose k > 1;
            hence contradiction by A5,A11,A17,A34,A37,GOBOARD7:39;
          end;
          suppose k+1+1 < len f;
            hence contradiction by A6,A11,A17,A34,A37,GOBOARD7:38;
          end;
        end;
        i1 >= 1 by A13,MATRIX_1:39;
        then
A38:    i1 = i1 -' 1 + 1 by XREAL_1:237;
        j1 >= 1 by A13,MATRIX_1:39;
        then
A39:    j1 = j1 -' 1 + 1 by XREAL_1:237;
        then j1 -' 1 <= j1 by NAT_1:11;
        then
A40:    j1 -' 1 <= width GoB f by A22,XXREAL_0:2;
        len GoB f > 1 by GOBOARD7:34;
        then
A41:    len GoB f -'1 + 1 = len GoB f by XREAL_1:237;
        width GoB f >= 1 by GOBOARD7:35;
        then
A42:    width GoB f -'1 + 1 = width GoB f by XREAL_1:237;
A43:    k+1+1 = k+(1+1);
A44:    i0+1+1 = i0+(1+1);
A45:    i2+1+1 = i2+(1+1);
A46:    j0+1+1 = j0+(1+1);
A47:    j2+1+1 = j2+(1+1);
A48:    LeftComp f is_a_component_of (L~f)` by Def1;
A49:    0+1 = 1;

now per cases by A9,A11,A12,A14,A15,A17,A28,A30,A31,A32,A33,GOBOARD7:31;
          suppose that
A50:        i0 = i1+1 and
A51:        i1 = i2+1 and
A52:        j0 = 1;
A53:        left_cell(f,k) = cell(GoB f,i1,0)
            by A6,A7,A10,A11,A13,A14,A24,A49,A50,A52,GOBOARD5:30;
            0+1 = j2 by A24,A25,A50,A51,A52;
            then
A54:        left_cell(f,k+1) = cell(GoB f,i2,0)
            by A4,A5,A13,A14,A16,A17,A24,A50,A51,A52,GOBOARD5:30;
A55:        LSeg(1/2*((GoB f)*(i1,1)+(GoB f)*(i0,1))- |[0,1]|,
            1/2*((GoB f)*(i2,1)+(GoB f)*(i1,1))- |[0,1]|) is connected by Th7,
JORDAN1:9;
A56:        LSeg(1/2*((GoB f)*(i1,1)+(GoB f)*(i0,1))- |[0,1]|,
            1/2*((GoB f)*(i2,1)+(GoB f)*(i1,1))- |[0,1]|)
            meets Int cell(GoB f,i1,0) by A18,A19,A50,GOBOARD6:87;
            LSeg(1/2*((GoB f)*(i1,1)+(GoB f)*(i0,1))- |[0,1]|,
            1/2*((GoB f)*(i2,1)+(GoB f)*(i1,1))- |[0,1]|) misses L~f
            by A18,A20,A45,A50,A51,GOBOARD8:25;
            then LSeg(1/2*((GoB f)*(i1,1)+(GoB f)*(i0,1))- |[0,1]|,
            1/2*((GoB f)*(i2,1)+(GoB f)*(i1,1))- |[0,1]|) c= (L~f)`
            by SUBSET_1:43;
            then
A57:        LSeg(1/2*((GoB f)*(i1,1)+(GoB f)*(i0,1))- |[0,1]|,
            1/2*((GoB f)*(i2,1)+(GoB f)*(i1,1))- |[0,1]|) c= LeftComp f
            by A3,A5,A6,A48,A53,A55,A56,Th6,NAT_1:13;
A58:        LSeg(1/2*((GoB f)*(i1,1)+(GoB f)*(i0,1))- |[0,1]|,
            1/2*((GoB f)*(i2,1)+(GoB f)*(i1,1))- |[0,1]|) meets
            Int cell(GoB f,i2,0) by A19,A20,A51,GOBOARD6:87;
            Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
            then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;

hence Int left_cell(f,k+1) c= LeftComp f by A4,A5,A48,A54,A57,A58,Th6,Th22;
          end;
          suppose that
A59:        i0 = i1+1 and
A60:        i1 = i2+1 and
A61:        j0 <> 1;
A62:        left_cell(f,k) = cell(GoB f,i1,j1-'1)
            by A6,A7,A10,A11,A13,A14,A24,A39,A59,GOBOARD5:30;
            1 < j0 by A21,A61,XXREAL_0:1;
            then
A63:        1 <= j0-'1 by A24,A39,A59,NAT_1:13;
A64:        left_cell(f,k+1) = cell(GoB f,i2,j1-'1)
            by A4,A5,A13,A14,A16,A17,A25,A39,A60,GOBOARD5:30;
A65:        LSeg(1/2*((GoB f)*(i1,j0-'1)+(GoB f)*(i0,j0)),

1/2*((GoB f)*(i2,j0-'1)+(GoB f)*(i1,j0))) is connected by Th7,JORDAN1:9;
A66:        LSeg(1/2*((GoB f)*(i1,j0-'1)+(GoB f)*(i0,j0)),
            1/2*((GoB f)*(i2,j0-'1)+(GoB f)*(i1,j0)))
            meets Int cell(GoB f,i1,j1-'1)
            by A18,A19,A22,A24,A39,A59,A63,GOBOARD6:85;
            LSeg(1/2*((GoB f)*(i1,j0-'1)+(GoB f)*(i0,j0)),
            1/2*((GoB f)*(i2,j0-'1)+(GoB f)*(i1,j0))) misses L~f
            by A5,A6,A11,A14,A17,A18,A20,A21,A24,A25,A39,A43,A45,A59,A60,A63,
GOBOARD8:11;
            then LSeg(1/2*((GoB f)*(i1,j0-'1)+(GoB f)*(i0,j0)),
            1/2*((GoB f)*(i2,j0-'1)+(GoB f)*(i1,j0))) c= (L~f)` by SUBSET_1:43;
            then
A67:        LSeg(1/2*((GoB f)*(i1,j0-'1)+(GoB f)*(i0,j0)),
            1/2*((GoB f)*(i2,j0-'1)+(GoB f)*(i1,j0))) c= LeftComp f
            by A3,A5,A6,A48,A62,A65,A66,Th6,NAT_1:13;
A68:        LSeg(1/2*((GoB f)*(i1,j0-'1)+(GoB f)*(i0,j0)),
            1/2*((GoB f)*(i2,j0-'1)+(GoB f)*(i2+1,j0))) meets
            Int cell(GoB f,i2,j0-'1) by A19,A20,A21,A24,A39,A59,A60,A63,
GOBOARD6:85;
            Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
            then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
            hence Int left_cell(f,k+1) c= LeftComp f
            by A4,A5,A24,A48,A59,A60,A64,A67,A68,Th6,Th22;
          end;
          suppose that
A69:        i0 = i1+1 and
A70:        j1 = j2+1;
            left_cell(f,k) = cell(GoB f,i1,j2)
            by A6,A7,A10,A11,A13,A14,A24,A69,A70,GOBOARD5:30

.= left_cell(f,k+1) by A4,A5,A13,A14,A16,A17,A25,A38,A70,GOBOARD5:31;
            hence Int left_cell(f,k+1) c= LeftComp f by A3,A5,A6,NAT_1:13;
          end;
          suppose that
A71:        j0 = j1+1 and
A72:        i1 = i2+1 and
A73:        i0 = len GoB f and
A74:        j1 = 1;
A75:        left_cell(f,k) = cell(GoB f,i0,j1)
            by A6,A7,A10,A11,A13,A14,A24,A38,A71,GOBOARD5:31;
A76:        LSeg((GoB f)*(len GoB f,1)+|[1,-1]|,
            1/2*((GoB f)*(len GoB f,1)+(GoB f)*(len GoB f,2))+|[1,0]|)
            is connected by Th7,JORDAN1:9;
A77:        LSeg((GoB f)*(len GoB f,1)+|[1,-1]|,
            1/2*((GoB f)*(len GoB f,1)+(GoB f)*(len GoB f,2))+|[1,0]|)
            meets Int cell(GoB f,i0,j1) by A21,A71,A73,A74,GOBOARD6:89;
            LSeg((GoB f)*(len GoB f,1)+|[1,-1]|,
            1/2*((GoB f)*(len GoB f,1)+(GoB f)*(len GoB f,2))+|[1,0]|)
            misses L~f by GOBOARD8:35;
            then LSeg((GoB f)*(len GoB f,1)+|[1,-1]|,
            1/2*((GoB f)*(len GoB f,1)+(GoB f)*(len GoB f,2))+|[1,0]|)
            c= (L~f)` by SUBSET_1:43;
            then
A78:        LSeg((GoB f)*(len GoB f,1)+|[1,-1]|,
            1/2*((GoB f)*(len GoB f,1)+(GoB f)*(len GoB f,2))+|[1,0]|)
            c= LeftComp f by A3,A5,A6,A48,A75,A76,A77,Th6,NAT_1:13;
A79:        LSeg((GoB f)*(len GoB f,1)+|[1,-1]|,
            1/2*((GoB f)*(len GoB f,1)+(GoB f)*(len GoB f,2))+|[1,0]|) meets
            Int cell(GoB f,i1,0) by A24,A71,A73,GOBOARD6:93;
            Int cell(GoB f,i1,0) misses L~f by A19,A22,GOBOARD7:14;
            then Int cell(GoB f,i1,0) c= (L~f)` by SUBSET_1:43;
            then
A80:        Int cell(GoB f,i1,0) c= LeftComp f by A19,A22,A48,A78,A79,Th6,Th21;
A81:        left_cell(f,k+1) = cell(GoB f,i2,0)
            by A4,A5,A13,A14,A16,A17,A25,A39,A72,A74,GOBOARD5:30;
A82:        LSeg
            (1/2*((GoB f)*(len GoB f -' 1,1)+(GoB f)*(len GoB f,1))- |[0,1]|,
            (GoB f)*(len GoB f,1)+|[1,-1]|)
            meets Int cell(GoB f,i1,0) by A24,A71,A73,GOBOARD6:93;

LSeg(1/2*((GoB f)*(len GoB f -' 1,1)+(GoB f)*(len GoB f,1))- |[0,1]|,
            (GoB f)*(len GoB f,1)+|[1,-1]|) misses L~f by GOBOARD8:27;
            then LSeg(1/2*((GoB f)*(len GoB f -' 1,1)+(GoB f)*
            (len GoB f,1))- |[0,1]|,
            (GoB f)*(len GoB f,1)+|[1,-1]|) c= (L~f)` by SUBSET_1:43;
            then
A83:        LSeg(1/2*((GoB f)*(len GoB f -' 1,1)+(GoB f)*(len GoB f,1))- |[0
            ,1]|, (GoB f)*(len GoB f,1)+|[1,-1]|) c= LeftComp f
            by A48,A80,A82,Th6,Th8;
A84:        LSeg
            (1/2*((GoB f)*(len GoB f -' 1,1)+(GoB f)*(len GoB f,1))- |[0,1]|,
            (GoB f)*(len GoB f,1)+|[1,-1]|) meets
            Int cell(GoB f,i2,0) by A20,A24,A38,A71,A72,A73,GOBOARD6:87;
            Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
            then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
            hence Int left_cell(f,k+1) c= LeftComp f
            by A4,A5,A48,A81,A83,A84,Th6,Th22;
          end;
          suppose that
A85:        j0 = j1+1 and
A86:        i1 = i2+1 and
A87:        i0 <> len GoB f and
A88:        j1 = 1;
A89:        left_cell(f,k) = cell(GoB f,i0,j1)
            by A6,A7,A10,A11,A13,A14,A24,A38,A85,GOBOARD5:31;
            len GoB f > i0 by A18,A87,XXREAL_0:1;
            then
A90:        len GoB f >= i0+1 by NAT_1:13;
A91:        LSeg(1/2*((GoB f)*(i0,1)+(GoB f)*(i0+1,1))- |[0,1]|,

1/2*((GoB f)*(i1,1)+(GoB f)*(i1+1,2))) is connected by Th7,JORDAN1:9;
A92:        LSeg(1/2*((GoB f)*(i0,1)+(GoB f)*(i0+1,1))- |[0,1]|,
            1/2*((GoB f)*(i1,1)+(GoB f)*(i1+1,1+1)))
            meets Int cell(GoB f,i0,j1)
            by A19,A21,A24,A85,A88,A90,GOBOARD6:85;
            LSeg(1/2*((GoB f)*(i0,1)+(GoB f)*(i0+1,1))- |[0,1]|,
            1/2*((GoB f)*(i1,1)+(GoB f)*(i1+1,2))) misses L~f

by A5,A6,A11,A14,A17,A20,A24,A25,A43,A45,A85,A86,A88,A90,GOBOARD8:8;
            then LSeg(1/2*((GoB f)*(i0,1)+(GoB f)*(i0+1,1))- |[0,1]|,
            1/2*((GoB f)*(i1,1)+(GoB f)*(i1+1,2))) c= (L~f)` by SUBSET_1:43;
            then
A93:        LSeg(1/2*((GoB f)*(i0,1)+(GoB f)*(i0+1,1))- |[0,1 ]|,
            1/2*((GoB f)*(i1,1)+(GoB f)*(i1+1,2))) c= LeftComp f
            by A3,A5,A6,A48,A89,A91,A92,Th6,NAT_1:13;
A94:        LSeg(1/2*((GoB f)*(i0,1)+(GoB f)*(i0+1,1))- |[0,1]|,
            1/2*((GoB f)*(i1,1)+(GoB f)*(i1+1,2))) meets
            Int cell(GoB f,i1,0) by A19,A24,A85,A90,GOBOARD6:87;
            Int cell(GoB f,i1,0) misses L~f by A19,A22,GOBOARD7:14;
            then Int cell(GoB f,i1,0) c= (L~f)` by SUBSET_1:43;
            then
A95:        Int cell(GoB f,i1,0) c= LeftComp f by A19,A22,A48,A93,A94,Th6,Th21;
A96:        left_cell(f,k+1) = cell(GoB f,i2,0)
            by A4,A5,A13,A14,A16,A17,A25,A39,A86,A88,GOBOARD5:30;
A97:        LSeg(1/2*((GoB f)*(i2,1)+(GoB f)*(i2+1,1))- |[0,1]|,
            1/2*((GoB f)*(i2+1,1)+(GoB f)*(i2+2,1))- |[0,1]|)
            meets Int cell(GoB f,i1,0) by A19,A24,A85,A86,A90,GOBOARD6:87;
            LSeg(1/2*((GoB f)*(i2,1)+(GoB f)*(i2+1,1))- |[0,1]|,
            1/2*((GoB f)*(i2+1,1)+(GoB f)*(i2+2,1))- |[0,1]|) misses L~f
            by A20,A24,A85,A86,A90,GOBOARD8:25;
            then LSeg(1/2*((GoB f)*(i2,1)+(GoB f)*(i2+1,1))- |[0,1]|,
            1/2*((GoB f)*(i2+1,1)+(GoB f)*(i2+2,1))- |[0,1]|) c= (L~f)`
            by SUBSET_1:43;
            then
A98:        LSeg(1/2*((GoB f)*(i2,1)+(GoB f)*(i2+1,1))- |[0,1 ]|,
            1/2*((GoB f)*(i2+1,1)+(GoB f)*(i2+2,1))- |[0,1]|) c= LeftComp f
            by A48,A95,A97,Th6,Th8;
A99:        LSeg(1/2*((GoB f)*(i2,1)+(GoB f)*(i2+1,1))- |[0,1]|,
            1/2*((GoB f)*(i2+1,1)+(GoB f)*(i2+2,1))- |[0,1]|) meets
            Int cell(GoB f,i2,0) by A19,A20,A86,GOBOARD6:87;
            Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
            then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
            hence Int left_cell(f,k+1) c= LeftComp f
            by A4,A5,A48,A96,A98,A99,Th6,Th22;
          end;
          suppose that
A100:       j0 = j1+1 and
A101:       i1 = i2+1 and
A102:       i0 = len GoB f and
A103:       j1 <> 1;
A104:       left_cell(f,k) = cell(GoB f,i0,j1)
            by A6,A7,A10,A11,A13,A14,A24,A38,A100,GOBOARD5:31;
            1 < j1 by A22,A103,XXREAL_0:1;
            then
A105:       1 <= j1-'1 by A39,NAT_1:13;
A106:       j1+1 = j1-'1+(1+1) by A39;
A107:       LSeg
            (1/2*((GoB f)*(len GoB f,j1-'1)+(GoB f)*(len GoB f,j1))+|[1,0]|,
            1/2*((GoB f)*(len GoB f,j1)+(GoB f)*(len GoB f,j1+1))+|[1,0]|)
            is connected by Th7,JORDAN1:9;
A108:       LSeg
            (1/2*((GoB f)*(len GoB f,j1-'1)+(GoB f)*(len GoB f,j1))+|[1,0]|,
            1/2*((GoB f)*(len GoB f,j1)+(GoB f)*(len GoB f,j1+1))+|[1,0]|)
            meets Int cell(GoB f,i0,j1) by A21,A22,A100,A102,GOBOARD6:89;

LSeg(1/2*((GoB f)*(len GoB f,j1-'1)+(GoB f)*(len GoB f,j1))+|[1,0]|,
            1/2*((GoB f)*(len GoB f,j1)+(GoB f)*(len GoB f,j1+1))+|[1,0]|)
            misses L~f by A21,A39,A100,A105,A106,GOBOARD8:34;
            then LSeg(1/2*((GoB f)*(len GoB f,j1-'1)+(GoB f)*
            (len GoB f,j1))+|[1,0]|,
            1/2*((GoB f)*(len GoB f,j1)+(GoB f)*(len GoB f,j1+1))+|[1,0]|)
            c= (L~f)` by SUBSET_1:43;
            then
A109:       LSeg(1/2*((GoB f)*(len GoB f,j1-'1)+(GoB f)*(len GoB f,j1))+|[1
            ,0]|,
            1/2*((GoB f)*(len GoB f,j1)+(GoB f)*(len GoB f,j1+1))+|[1,0]|)
            c= LeftComp f by A3,A5,A6,A48,A104,A107,A108,Th6,NAT_1:13;
            j1 > 1 by A22,A103,XXREAL_0:1;
            then 1 <= j1-'1 by A39,NAT_1:13;
            then
A110:       LSeg
            (1/2*((GoB f)*(len GoB f,j1-'1)+(GoB f)*(len GoB f,j1))+|[1,0]|,
            1/2*((GoB f)*(len GoB f,j1)+(GoB f)*
            (len GoB f,j1+1))+|[1,0]|) meets
            Int cell(GoB f,i1,j1-'1) by A22,A24,A39,A100,A102,GOBOARD6:89;
            Int cell(GoB f,i1,j1-'1) misses L~f by A19,A40,GOBOARD7:14;
            then Int cell(GoB f,i1,j1-'1) c= (L~f)` by SUBSET_1:43;
            then
A111:       Int cell(GoB f,i1,j1-'1) c= LeftComp f by A19,A40,A48,A109,A110,Th6
,Th21;
A112:       left_cell(f,k+1) = cell(GoB f,i2,j1-'1)
            by A4,A5,A13,A14,A16,A17,A25,A39,A101,GOBOARD5:30;
A113:       LSeg(1/2*((GoB f)*(i1 -' 1,j1-'1)+(GoB f)*(i1,j1)),
            1/2*((GoB f)*(i1,j1-'1)+(GoB f)*(i1,j1))+|[1,0]|)
            meets Int cell(GoB f,i1,j1-'1) by A22,A24,A39,A100,A102,A105,
GOBOARD6:89;
A114:       i1-'1 = i2 by A101,NAT_D:34;
            then LSeg(1/2*((GoB f)*(i1 -' 1,j1-'1)+(GoB f)*(i1,j1)),
            1/2*((GoB f)*(i1,j1-'1)+(GoB f)*(i1,j1))+|[1,0]|) misses L~f
            by A5,A6,A11,A14,A17,A21,A24,A25,A39,A43,A100,A101,A102,A105,A106,
GOBOARD8:19;
            then LSeg(1/2*((GoB f)*(i1 -' 1,j1-'1)+(GoB f)*(i1,j1)),
            1/2*((GoB f)*(i1,j1-'1)+(GoB f)*(i1,j1))+|[1,0]|) c= (L~f)`
            by SUBSET_1:43;
            then
A115:       LSeg(1/2*((GoB f)*(i1 -' 1,j1-'1)+(GoB f)*(i1,j1)) ,
            1/2*((GoB f)*(i1,j1-'1)+(GoB f)*(i1,j1))+|[1,0]|) c= LeftComp f
            by A48,A111,A113,Th6,Th8;
A116:       LSeg(1/2*((GoB f)*(i1 -' 1,j1-'1)+(GoB f)*(i1,j1)),
            1/2*((GoB f)*(i1,j1-'1)+(GoB f)*(i1,j1))+|[1,0]|) meets
            Int cell(GoB f,i2,j1-'1) by A19,A20,A22,A39,A101,A105,A114,
GOBOARD6:85;
            Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
            then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
            hence Int left_cell(f,k+1) c= LeftComp f
            by A4,A5,A48,A112,A115,A116,Th6,Th22;
          end;
          suppose that
A117:       j0 = j1+1 and
A118:       i1 = i2+1 and
A119:       i0 <> len GoB f and
A120:       j1 <> 1;
A121:       left_cell(f,k) = cell(GoB f,i0,j1)
            by A6,A7,A10,A11,A13,A14,A24,A38,A117,GOBOARD5:31;
            1 < j1 by A22,A120,XXREAL_0:1;
            then
A122:       1 <= j1-'1 by A39,NAT_1:13;
            len GoB f > i0 by A18,A119,XXREAL_0:1;
            then
A123:       len GoB f >= i0+1 by NAT_1:13;
A124:       j1+1 = j1-'1+(1+1) by A39;
A125:       LSeg(1/2*((GoB f)*(i0,j1-'1)+(GoB f)*(i0+1,j1)),
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i1+1,j1+1))) is connected by Th7,
JORDAN1:9;
A126:       LSeg(1/2*((GoB f)*(i0,j1-'1)+(GoB f)*(i0+1,j1)),
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i1+1,j1+1)))

meets Int cell(GoB f,i0,j1) by A19,A21,A22,A24,A117,A123,GOBOARD6:85;
            LSeg(1/2*((GoB f)*(i0,j1-'1)+(GoB f)*(i0+1,j1)),
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i1+1,j1+1))) misses L~f

by A5,A6,A11,A14,A17,A20,A21,A24,A25,A39,A43,A45,A117,A118,A122,A123,A124,
GOBOARD8:5;
            then LSeg(1/2*((GoB f)*(i0,j1-'1)+(GoB f)*(i0+1,j1)),

1/2*((GoB f)*(i1,j1)+(GoB f)*(i1+1,j1+1))) c= (L~f)` by SUBSET_1:43;
            then
A127:       LSeg(1/2*((GoB f)*(i0,j1-'1)+(GoB f)*(i0+1,j1)),
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i1+1,j1+1))) c= LeftComp f
            by A3,A5,A6,A48,A121,A125,A126,Th6,NAT_1:13;
            j1 > 1 by A22,A120,XXREAL_0:1;
            then 1 <= j1-'1 by A39,NAT_1:13;
            then
A128:       LSeg(1/2*((GoB f)*(i1,j1-'1)+(GoB f)*(i1+1,j1)),
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i1+1,j1+1))) meets
            Int cell(GoB f,i1,j1-'1) by A19,A22,A24,A39,A117,A123,GOBOARD6:85;
            Int cell(GoB f,i1,j1-'1) misses L~f by A19,A40,GOBOARD7:14;
            then Int cell(GoB f,i1,j1-'1) c= (L~f)` by SUBSET_1:43;
            then
A129:       Int cell(GoB f,i1,j1-'1) c= LeftComp f by A19,A24,A40,A48,A117,A127
,A128,Th6,Th21;
A130:       left_cell(f,k+1) = cell(GoB f,i2,j1-'1)
            by A4,A5,A13,A14,A16,A17,A25,A39,A118,GOBOARD5:30;
A131:       LSeg(1/2*((GoB f)*(i1,j1-'1)+(GoB f)*(i1+1,j1)),
            1/2*((GoB f)*(i2,j1-'1)+(GoB f)*(i1,j1)))
            meets Int cell(GoB f,i1,j1-'1) by A19,A22,A24,A39,A117,A122,A123,
GOBOARD6:85;
            i1 < len GoB f by A19,A24,A117,A119,XXREAL_0:1;
            then i1+1 <= len GoB f by NAT_1:13;
            then LSeg(1/2*((GoB f)*(i1,j1-'1)+(GoB f)*(i1+1,j1)),
            1/2*((GoB f)*(i2,j1-'1)+(GoB f)*(i1,j1))) misses L~f

by A5,A6,A11,A14,A17,A20,A21,A24,A25,A39,A43,A45,A117,A118,A122,A124,
GOBOARD8:13;
            then LSeg(1/2*((GoB f)*(i1,j1-'1)+(GoB f)*(i1+1,j1)),
            1/2*((GoB f)*(i2,j1-'1)+(GoB f)*(i1,j1))) c= (L~f)` by SUBSET_1:43;
            then
A132:       LSeg(1/2*((GoB f)*(i1,j1-'1)+(GoB f)*(i1+1,j1)),
            1/2*((GoB f)*(i2,j1-'1)+(GoB f)*(i1,j1))) c= LeftComp f
            by A48,A129,A131,Th6,Th8;
A133:       LSeg(1/2*((GoB f)*(i1,j1-'1)+(GoB f)*(i1+1,j1)),
            1/2*((GoB f)*(i2,j1-'1)+(GoB f)*(i2+1,j1))) meets
            Int cell(GoB f,i2,j1-'1) by A19,A20,A22,A39,A118,A122,GOBOARD6:85;
            Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
            then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
            hence Int left_cell(f,k+1) c= LeftComp f
            by A4,A5,A48,A118,A130,A132,A133,Th6,Th22;
          end;
          suppose that
A134:       j0 = j1+1 and
A135:       j1 = j2+1 and
A136:       i0 = len GoB f;
A137:       left_cell(f,k) = cell(GoB f,len GoB f,j1)
            by A6,A7,A10,A11,A13,A14,A24,A41,A134,A136,GOBOARD5:31;
A138:       left_cell(f,k+1) = cell(GoB f,len GoB f,j2)
            by A4,A5,A13,A14,A16,A17,A24,A25,A41,A134,A135,A136,GOBOARD5:31;
A139:       LSeg(1/2*((GoB f)*(len GoB f,j2)+(GoB f)*(len GoB f,j2+1))+|[1,0]|,
            1/2*((GoB f)*(len GoB f,j2+1)+(GoB f)*(len GoB f,j2+2))+|[1,0]|)
            is connected by Th7,JORDAN1:9;
A140:       LSeg(1/2*((GoB f)*(len GoB f,j2)+(GoB f)*(len GoB f,j2+1))+|[1,0]|,
            1/2*((GoB f)*(len GoB f,j2+1)+(GoB f)*(len GoB f,j2+2))+|[1,0]|)
            meets Int cell(GoB f,len GoB f,j1)
            by A21,A22,A134,A135,GOBOARD6:89;
            LSeg(1/2*((GoB f)*(len GoB f,j2)+(GoB f)*(len GoB f,j2+1))+|[1,0]|
            , 1/2*((GoB f)*(len GoB f,j2+1)+(GoB f)*(len GoB f,j2+2))+|[1,0]|)
            misses L~f by A21,A23,A134,A135,GOBOARD8:34;

then LSeg(1/2*((GoB f)*(len GoB f,j2)+(GoB f)*(len GoB f,j2+1))+|[1,0]|,
            1/2*((GoB f)*(len GoB f,j2+1)+(GoB f)* (len GoB f,j2+2))+|[1,0]|)
            c= (L~f)` by SUBSET_1:43;
            then
A141:       LSeg(1/2*((GoB f)*(len GoB f,j2)+(GoB f)*(len GoB f,j2+1))+|[1,
            0]|,
            1/2*((GoB f)*(len GoB f,j2+1)+(GoB f)*(len GoB f,j2+2))+|[1,0]|)
            c= LeftComp f by A3,A5,A6,A48,A137,A139,A140,Th6,NAT_1:13;
A142:       LSeg(1/2*((GoB f)*(len GoB f,j2)+(GoB f)*(len GoB f,j2+1))+|[1,0]|,
            1/2*((GoB f)*(len GoB f,j2+1)+(GoB f)*(len GoB f,j2+2))+|[1,0]|)
            meets Int cell(GoB f,i1,j2) by A22,A23,A24,A134,A135,A136,
GOBOARD6:89;
            Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
            then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;

hence Int left_cell(f,k+1) c= LeftComp f by A4,A5,A24,A48,A134,A136,A138,A141
,A142,Th6,Th22;
          end;
          suppose that
A143:       j0 = j1+1 and
A144:       j1 = j2+1 and
A145:       i0 <> len GoB f;
A146:       left_cell(f,k) = cell(GoB f,i0,j1)
            by A6,A7,A10,A11,A13,A14,A24,A38,A143,GOBOARD5:31;
            len GoB f > i0 by A18,A145,XXREAL_0:1;
            then
A147:       len GoB f >= i0+1 by NAT_1:13;
A148:       left_cell(f,k+1) = cell(GoB f,i1,j2)
            by A4,A5,A13,A14,A16,A17,A25,A38,A144,GOBOARD5:31;
A149:       LSeg(1/2*((GoB f)*(i0,j2)+(GoB f)*(i0+1,j2+1)),
            1/2*((GoB f)*(i1,j2+1)+(GoB f)*(i1+1,j2+2))) is connected by Th7,
JORDAN1:9;
A150:       LSeg(1/2*((GoB f)*(i0,j2)+(GoB f)*(i0+1,j2+1)),
            1/2*((GoB f)*(i1,j2+1)+(GoB f)*(i1+1,j2+2)))
            meets Int cell(GoB f,i0,j1)
            by A19,A21,A22,A24,A143,A144,A147,GOBOARD6:85;
            LSeg(1/2*((GoB f)*(i0,j2)+(GoB f)*(i0+1,j2+1)),
            1/2*((GoB f)*(i1,j2+1)+(GoB f)*(i1+1,j2+2))) misses L~f

by A5,A6,A11,A14,A17,A18,A21,A23,A24,A25,A43,A143,A144,A147,GOBOARD8:4;
            then LSeg(1/2*((GoB f)*(i0,j2)+(GoB f)*(i0+1,j2+1)),
            1/2*((GoB f)*(i1,j2+1)+(GoB f)*
            (i1+1,j2+2))) c= (L~f)` by SUBSET_1:43;
            then
A151:       LSeg(1/2*((GoB f)*(i0,j2)+(GoB f)*(i0+1,j2+1)),
            1/2*((GoB f)*(i1,j2+1)+(GoB f)*(i1+1,j2+2))) c= LeftComp f
            by A3,A5,A6,A48,A146,A149,A150,Th6,NAT_1:13;
A152:       LSeg(1/2*((GoB f)*(i0,j2)+(GoB f)*(i0+1,j2+1)),
            1/2*((GoB f)*(i1,j2+1)+(GoB f)*(i1+1,j2+2))) meets

Int cell(GoB f,i1,j2) by A19,A22,A23,A24,A143,A144,A147,GOBOARD6:85;
            Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
            then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;

hence Int left_cell(f,k+1) c= LeftComp f by A4,A5,A48,A148,A151,A152,Th6,Th22;
          end;
          suppose that
A153:       i0+1 = i1 and
A154:       j1 = j2+1 and
A155:       i1 = len GoB f and
A156:       j0 = width GoB f;
A157:       left_cell(f,k) = cell(GoB f,i0,j1)
            by A6,A7,A10,A11,A13,A14,A24,A39,A153,GOBOARD5:29;
A158:       LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i1,j0))+|[0,1]|,
            (GoB f)*(i1,j0)+|[1,1]|) is connected by Th7,JORDAN1:9;
A159:       LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i1,j0))+|[0,1]|,
            (GoB f)*(i1,j0)+|[1,1]|)
            meets Int cell(GoB f,i0,j1) by A18,A19,A24,A153,A156,GOBOARD6:86;
            LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i1,j0))+|[0,1]|,
            (GoB f)*(i1,j0)+|[1,1]|)
            misses L~f by A41,A153,A155,A156,GOBOARD8:30;
            then LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i1,j0))+|[0,1]|,
            (GoB f)*(i1,j0)+|[1,1]|) c= (L~f)` by SUBSET_1:43;
            then
A160:       LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i1,j0))+|[0,1]| ,

(GoB f)*(i1,j0)+|[1,1]|) c= LeftComp f by A3,A5,A6,A48,A157,A158,A159,Th6,
NAT_1:13;
A161:       LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i1,j0))+|[0,1]|,
            (GoB f)*(i1,j0)+|[1,1]|) meets
            Int cell(GoB f,i1,j1) by A24,A153,A155,A156,GOBOARD6:91;
            Int cell(GoB f,i1,j1) misses L~f by A19,A22,GOBOARD7:14;
            then Int cell(GoB f,i1,j1) c= (L~f)` by SUBSET_1:43;
            then
A162:       Int cell(GoB f,i1,j1) c= LeftComp f by A19,A22,A48,A160,A161,Th6
,Th21;
A163:       left_cell(f,k+1) = cell(GoB f,i1,j2)
            by A4,A5,A13,A14,A16,A17,A25,A38,A154,GOBOARD5:31;
A164:       LSeg(1/2*((GoB f)*(i1,j2)+(GoB f)*(i1,j1))+|[1,0]|,
            (GoB f)*(i1,j1)+|[1,1]|) meets Int cell(GoB f,i1,j1)
            by A24,A153,A155,A156,GOBOARD6:91;
            LSeg(1/2*((GoB f)*(i1,j2)+(GoB f)*(i1,j1))+|[1,0]|, (GoB f)*

(i1,j1)+|[1,1]|) misses L~f by A24,A42,A153,A154,A155,A156,GOBOARD8:36;
            then LSeg(1/2*((GoB f)*(i1,j2)+(GoB f)*(i1,j1))+|[1,0]|,
            (GoB f)*(i1,j1)+|[1,1]|) c= (L~f)` by SUBSET_1:43;
            then
A165:       LSeg(1/2*((GoB f)*(i1,j2)+(GoB f)*(i1,j1))+|[1,0]| ,
            (GoB f)*(i1,j1)+|[1,1]|) c= LeftComp f by A48,A162,A164,Th6,Th8;
A166:       LSeg(1/2*((GoB f)*(i1,j2)+(GoB f)*(i1,j1))+|[1,0]|,
            (GoB f)*(i1,j1)+|[1,1]|) meets Int cell(GoB f,i1,j2)
            by A22,A23,A154,A155,GOBOARD6:89;
            Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
            then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;

hence Int left_cell(f,k+1) c= LeftComp f by A4,A5,A48,A163,A165,A166,Th6,Th22;
          end;
          suppose that
A167:       i0+1 = i1 and
A168:       j1 = j2+1 and
A169:       i1 <> len GoB f and
A170:       j0 = width GoB f;
A171:       left_cell(f,k) = cell(GoB f,i0,j1)
            by A6,A7,A10,A11,A13,A14,A24,A39,A167,GOBOARD5:29;
            len GoB f > i1 by A19,A169,XXREAL_0:1;
            then
A172:       i1+1 <= len GoB f by NAT_1:13;
A173:       LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0+1,j0))+|[0,1]|,
            1/2*((GoB f)*(i0+1,j0)+(GoB f)*
            (i0+2,j0))+|[0,1]|) is connected by Th7,JORDAN1:9;
A174:       LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0+1,j0))+|[0,1]|,
            1/2*((GoB f)*(i0+1,j0)+(GoB f)*(i0+2,j0))+|[0,1]|)
            meets Int cell(GoB f,i0,j1) by A18,A19,A24,A167,A170,GOBOARD6:86;
            LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0+1,j0))+|[0,1]|,
            1/2*((GoB f)*(i0+1,j0)+(GoB f)*(i0+2,j0))+|[0,1]|)
            misses L~f by A18,A167,A170,A172,GOBOARD8:28;
            then LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0+1,j0))+|[0,1]|,
            1/2*((GoB f)*(i0+1,j0)+(GoB f)*(i0+2,j0))+|[0,1]|)
            c= (L~f)` by SUBSET_1:43;
            then
A175:       LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0+1,j0))+|[0,1 ]|,
            1/2*((GoB f)*(i0+1,j0)+(GoB f)*(i0+2,j0))+|[0,1]|) c= LeftComp f
            by A3,A5,A6,A48,A171,A173,A174,Th6,NAT_1:13;
A176:       LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0+1,j0))+|[0,1]|,
            1/2*((GoB f)*(i0+1,j0)+(GoB f)*(i0+2,j0))+|[0,1]|) meets
            Int cell(GoB f,i1,j1) by A19,A24,A167,A170,A172,GOBOARD6:86;
            Int cell(GoB f,i1,j1) misses L~f by A19,A22,GOBOARD7:14;
            then Int cell(GoB f,i1,j1) c= (L~f)` by SUBSET_1:43;
            then
A177:       Int cell(GoB f,i1,j1) c= LeftComp f by A19,A22,A48,A175,A176,Th6
,Th21;
A178:       left_cell(f,k+1) = cell(GoB f,i1,j2)
            by A4,A5,A13,A14,A16,A17,A25,A38,A168,GOBOARD5:31;
A179:       LSeg(1/2*((GoB f)*(i0+1,j2)+(GoB f)*(i1+1,j0)),
            1/2*((GoB f)*(i0+1,j0)+(GoB f)*(i1+1,j0))+|[0,1]|)
            meets Int cell(GoB f,i1,j1) by A19,A24,A167,A170,A172,GOBOARD6:86;
            LSeg(1/2*((GoB f)*(i0+1,j2)+(GoB f)*(i1+1,j0)),
            1/2*((GoB f)*(i0+1,j0)+(GoB f)*(i1+1,j0))+|[0,1]|) misses L~f
            by A5,A6,A11,A14,A17,A18,A24,A25,A42,A43,A44,A167,A168,A170,A172,
GOBOARD8:10;
            then LSeg(1/2*((GoB f)*(i0+1,j2)+(GoB f)*(i1+1,j0)),
            1/2*((GoB f)*(i0+1,j0)+(GoB f)*(i1+1,j0))+|[0,1]|) c= (L~f)`
            by SUBSET_1:43;
            then
A180:       LSeg(1/2*((GoB f)*(i0+1,j2)+(GoB f)*(i1+1,j0)),
            1/2*((GoB f)*(i0+1,j0)+(GoB f)*(i1+1,j0))+|[0,1]|) c= LeftComp f
            by A48,A177,A179,Th6,Th8;
A181:       LSeg(1/2*((GoB f)*(i0+1,j2)+(GoB f)*(i1+1,j0)),
            1/2*((GoB f)*(i0+1,j0)+(GoB f)*(i1+1,j0))+|[0,1]|) meets
            Int cell(GoB f,i1,j2) by A19,A23,A24,A167,A168,A170,A172,
GOBOARD6:85;
            Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
            then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;

hence Int left_cell(f,k+1) c= LeftComp f by A4,A5,A48,A178,A180,A181,Th6,Th22;
          end;
          suppose that
A182:       i0+1 = i1 and
A183:       j1 = j2+1 and
A184:       i1 = len GoB f and
A185:       j0 <> width GoB f;
A186:       left_cell(f,k) = cell(GoB f,i0,j1)
            by A6,A7,A10,A11,A13,A14,A24,A39,A182,GOBOARD5:29;
            width GoB f > j0 by A21,A185,XXREAL_0:1;
            then
A187:       width GoB f >= j0+1 by NAT_1:13;
A188:       LSeg(1/2*((GoB f)*(i0,j1)+(GoB f)*(i1,j1+1)),
            1/2*((GoB f)*(i1,j1)+(GoB f)*
            (i1,j1+1))+|[1,0]|) is connected by Th7,JORDAN1:9;
A189:       LSeg(1/2*((GoB f)*(i0,j1)+(GoB f)*(i1,j1+1)),
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i1,j1+1))+|[1,0]|)

meets Int cell(GoB f,i0,j1) by A18,A19,A22,A24,A182,A187,GOBOARD6:85;
            LSeg(1/2*((GoB f)*(i0,j1)+(GoB f)*(i1,j1+1)),
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i1,j1+1))+|[1,0]|) misses L~f
            by A5,A6,A11,A14,A17,A23,A24,A25,A41,A43,A47,A182,A183,A184,A187,
GOBOARD8:20;
            then LSeg(1/2*((GoB f)*(i0,j1)+(GoB f)*(i1,j1+1)),
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i1,j1+1))+|[1,0]|)
            c= (L~f)` by SUBSET_1:43;
            then
A190:       LSeg(1/2*((GoB f)*(i0,j1)+(GoB f)*(i1,j1+1)),
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i1,j1+1))+|[1,0]|) c= LeftComp f
            by A3,A5,A6,A48,A186,A188,A189,Th6,NAT_1:13;
A191:       LSeg(1/2*((GoB f)*(i0,j1)+(GoB f)*(i1,j1+1)),
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i1,j1+1))+|[1,0]|) meets
            Int cell(GoB f,i1,j1) by A22,A24,A182,A184,A187,GOBOARD6:89;
            Int cell(GoB f,i1,j1) misses L~f by A19,A22,GOBOARD7:14;
            then Int cell(GoB f,i1,j1) c= (L~f)` by SUBSET_1:43;
            then
A192:       Int cell(GoB f,i1,j1) c= LeftComp f by A19,A22,A48,A190,A191,Th6
,Th21;
A193:       left_cell(f,k+1) = cell(GoB f,i1,j2)
            by A4,A5,A13,A14,A16,A17,A25,A38,A183,GOBOARD5:31;
A194:       LSeg(1/2*((GoB f)*(len GoB f,j2)+(GoB f)*(len GoB f,j1))+|[1,0]|,
            1/2*((GoB f)*(len GoB f,j1)+(GoB f)*(len GoB f,j1+1))+|[1,0]|)
            meets Int cell(GoB f,i1,j1) by A22,A24,A182,A184,A187,GOBOARD6:89;
            LSeg(1/2*((GoB f)*(len GoB f,j2)+(GoB f)*(len GoB f,j1))+|[1,0]|,
            1/2*((GoB f)*(len GoB f,j1)+(GoB f)*(len GoB f,j1+1))+|[1,0]|)
            misses L~f by A23,A24,A47,A182,A183,A187,GOBOARD8:34;

then LSeg(1/2*((GoB f)*(len GoB f,j2)+(GoB f)*(len GoB f,j1))+|[1,0]|,
            1/2*((GoB f)*(len GoB f,j1)+(GoB f)*(len GoB f,j1+1))+|[1,0]|)
            c= (L~f)` by SUBSET_1:43;
            then
A195:       LSeg(1/2*((GoB f)*(len GoB f,j2)+(GoB f)*(len GoB f,j1))+|[1,0 ]|,
            1/2*((GoB f)*(len GoB f,j1)+(GoB f)*(len GoB f,j1+1))+|[1,0]|)
            c= LeftComp f by A48,A192,A194,Th6,Th8;
A196:       LSeg(1/2*((GoB f)*(len GoB f,j2)+(GoB f)*(len GoB f,j1))+|[1,0]|,

1/2*((GoB f)*(len GoB f,j1)+(GoB f)*(len GoB f,j1+1))+|[1,0]|) meets
            Int cell(GoB f,i1,j2) by A22,A23,A183,A184,GOBOARD6:89;
            Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
            then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;

hence Int left_cell(f,k+1) c= LeftComp f by A4,A5,A48,A193,A195,A196,Th6,Th22;
          end;
          suppose that
A197:       i0+1 = i1 and
A198:       j1 = j2+1 and
A199:       i1 <> len GoB f and
A200:       j0 <> width GoB f;
A201:       left_cell(f,k) = cell(GoB f,i0,j1)
            by A6,A7,A10,A11,A13,A14,A24,A39,A197,GOBOARD5:29;
            len GoB f > i1 by A19,A199,XXREAL_0:1;
            then
A202:       i1+1 <= len GoB f by NAT_1:13;
            width GoB f > j0 by A21,A200,XXREAL_0:1;
            then
A203:       width GoB f >= j0+1 by NAT_1:13;
A204:       LSeg(1/2*((GoB f)*(i0,j2+1)+(GoB f)*(i0+1,j1+1)),
            1/2*((GoB f)*(i0+1,j2+1)+(GoB f)*(i1+1,j1+1))) is connected by Th7,
JORDAN1:9;
A205:       LSeg(1/2*((GoB f)*(i0,j2+1)+(GoB f)*(i0+1,j1+1)),
            1/2*((GoB f)*(i0+1,j2+1)+(GoB f)*(i1+1,j1+1)))
            meets Int cell(GoB f,i0,j1) by A18,A19,A22,A24,A197,A198,A203,
GOBOARD6:85;
            LSeg(1/2*((GoB f)*(i0,j2+1)+(GoB f)*(i0+1,j1+1)),
            1/2*((GoB f)*(i0+1,j2+1)+(GoB f)*(i1+1,j1+1))) misses L~f

by A5,A6,A11,A14,A17,A18,A23,A24,A25,A43,A44,A47,A197,A198,A202,A203,
GOBOARD8:16;
            then LSeg(1/2*((GoB f)*(i0,j2+1)+(GoB f)*(i0+1,j1+1)),
            1/2*((GoB f)*(i0+1,j2+1)+(GoB f)*(i1+1,j1+1)))
            c= (L~f)` by SUBSET_1:43;
            then
A206:       LSeg(1/2*((GoB f)*(i0,j2+1)+(GoB f)*(i0+1,j1+1)),
            1/2*((GoB f)*(i0+1,j2+1)+(GoB f)*(i1+1,j1+1))) c= LeftComp f
            by A3,A5,A6,A48,A201,A204,A205,Th6,NAT_1:13;
A207:       LSeg(1/2*((GoB f)*(i0,j2+1)+(GoB f)*(i0+1,j1+1)),
            1/2*((GoB f)*(i0+1,j2+1)+(GoB f)*(i1+1,j1+1))) meets
            Int cell(GoB f,i1,j1) by A19,A22,A24,A197,A198,A202,A203,
GOBOARD6:85;
            Int cell(GoB f,i1,j1) misses L~f by A19,A22,GOBOARD7:14;
            then Int cell(GoB f,i1,j1) c= (L~f)` by SUBSET_1:43;
            then
A208:       Int cell(GoB f,i1,j1) c= LeftComp f by A19,A22,A48,A206,A207,Th6
,Th21;
A209:       left_cell(f,k+1) = cell(GoB f,i1,j2)
            by A4,A5,A13,A14,A16,A17,A25,A38,A198,GOBOARD5:31;
A210:       LSeg(1/2*((GoB f)*(i0+1,j2)+(GoB f)*(i1+1,j2+1)),
            1/2*((GoB f)*(i0+1,j2+1)+(GoB f)*(i1+1,j1+1)))
            meets Int cell(GoB f,i1,j1)
            by A19,A22,A24,A197,A198,A202,A203,GOBOARD6:85;
            LSeg(1/2*((GoB f)*(i0+1,j2)+(GoB f)*(i1+1,j2+1)),
            1/2*((GoB f)*(i0+1,j2+1)+(GoB f)*(i1+1,j1+1))) misses L~f

by A5,A6,A11,A14,A17,A18,A23,A24,A25,A43,A44,A47,A197,A198,A202,A203,GOBOARD8:6
            ;
            then LSeg(1/2*((GoB f)*(i0+1,j2)+(GoB f)*(i1+1,j2+1)),
            1/2*((GoB f)*(i0+1,j2+1)+(GoB f)*(i1+1,j1+1))) c= (L~f)`
            by SUBSET_1:43;
            then
A211:       LSeg(1/2*((GoB f)*(i0+1,j2)+(GoB f)*(i1+1,j2+1)),
            1/2*((GoB f)*(i0+1,j2+1)+(GoB f)*(i1+1,j1+1))) c= LeftComp f
            by A48,A208,A210,Th6,Th8;
A212:       LSeg(1/2*((GoB f)*(i0+1,j2)+(GoB f)*(i1+1,j2+1)),
            1/2*((GoB f)*(i0+1,j2+1)+(GoB f)*(i1+1,j1+1))) meets
            Int cell(GoB f,i1,j2) by A19,A22,A23,A197,A198,A202,GOBOARD6:85;
            Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
            then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;

hence Int left_cell(f,k+1) c= LeftComp f by A4,A5,A48,A209,A211,A212,Th6,Th22;
          end;
          suppose that
A213:       j0+1 = j1 and
A214:       i1 = i2+1;
            left_cell(f,k) = cell(GoB f,i2,j0)
            by A6,A7,A10,A11,A13,A14,A24,A213,A214,GOBOARD5:28

.= left_cell(f,k+1) by A4,A5,A13,A14,A16,A17,A25,A213,A214,GOBOARD5:30;
            hence Int left_cell(f,k+1) c= LeftComp f by A3,A5,A6,NAT_1:13;
          end;
          suppose that
A215:       i0 = i1+1 and
A216:       j1+1 = j2 and
A217:       i1 = 1 and
A218:       j1 = 1;
A219:       left_cell(f,k) = cell(GoB f,i1,j1-'1)
            by A6,A7,A10,A11,A13,A14,A24,A39,A215,GOBOARD5:30;
A220:       LSeg((GoB f)*(i1,j1)- |[1,1]|,

1/2*((GoB f)*(i1,j1)+(GoB f)*(i0,j1))- |[0,1]|) is connected by Th7,JORDAN1:9;
            i1-'1 <= i1 by NAT_D:35;
            then
A221:       i1-'1 <= len GoB f by A19,XXREAL_0:2;
A222:       j1-'1 = 0 by A218,XREAL_1:234;
A223:       i1-'1 = 0 by A217,XREAL_1:234;
            j1-'1 <= j1 by NAT_D:35;
            then
A224:       j1-'1 <= width GoB f by A22,XXREAL_0:2;
A225:       LSeg((GoB f)*(i1,j1)- |[1,1]|,
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i0,j1))- |[0,1]|)

meets Int cell(GoB f,i1,j1-'1) by A18,A19,A215,A218,A222,GOBOARD6:87;
            LSeg((GoB f)*(i1,j1)- |[1,1]|,
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i0,j1))- |[0,1]|)
            misses L~f by A215,A217,A218,GOBOARD8:26;
            then LSeg((GoB f)*(i1,j1)- |[1,1]|,
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i0,j1))- |[0,1]|) c= (L~f)`
            by SUBSET_1:43;
            then
A226:       LSeg((GoB f)*(i1,j1)- |[1,1]|,
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i0,j1))- |[0,1]|) c= LeftComp f
            by A3,A5,A6,A48,A219,A220,A225,Th6,NAT_1:13;
A227:       LSeg((GoB f)*(i1,j1)- |[1,1]|,
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i0,j1))- |[0,1]|) meets
            Int cell(GoB f,i1-'1,j0-'1) by A24,A215,A217,A218,A222,GOBOARD6:90;
            Int cell(GoB f,i1-'1,j1-'1) misses L~f by A221,A224,GOBOARD7:14;
            then Int cell(GoB f,i1-'1,j1-'1) c= (L~f)` by SUBSET_1:43;
            then
A228:       Int cell(GoB f,i1-'1,j1-'1) c= LeftComp f
            by A24,A48,A215,A221,A224,A226,A227,Th6,Th21;
A229:       left_cell(f,k+1) = cell(GoB f,i1-'1,j1)
            by A4,A5,A13,A14,A16,A17,A25,A38,A216,GOBOARD5:28;
A230:       LSeg((GoB f)*(i1,j1)- |[1,1]|,
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i1,j2))- |[1,0]|)
            meets Int cell(GoB f,i1-'1,j1-'1) by A217,A218,A222,GOBOARD6:90;
            LSeg((GoB f)*(i1,j1)- |[1,1]|,
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i1,j2))- |[1,0]|)
            misses L~f by A216,A217,A218,GOBOARD8:32;
            then LSeg((GoB f)*(i1,j1)- |[1,1]|,
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i1,j2))- |[1,0]|) c= (L~f)`
            by SUBSET_1:43;
            then
A231:       LSeg((GoB f)*(i1,j1)- |[1,1]|,
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i1,j2))- |[1,0]|) c= LeftComp f
            by A48,A228,A230,Th6,Th8;
A232:       LSeg((GoB f)*(i1,j1)- |[1,1]|,
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i1,j2))- |[1,0]|) meets
            Int cell(GoB f,i1-'1,j1) by A22,A23,A216,A217,A223,GOBOARD6:88;
            Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
            then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;

hence Int left_cell(f,k+1) c= LeftComp f by A4,A5,A48,A229,A231,A232,Th6,Th22;
          end;
          suppose that
A233:       i0 = i1+1 and
A234:       j1+1 = j2 and
A235:       i1 <> 1 and
A236:       j1 = 1;
A237:       left_cell(f,k) = cell(GoB f,i1,j1-'1)
            by A6,A7,A10,A11,A13,A14,A24,A39,A233,GOBOARD5:30;
            1 < i1 by A19,A235,XXREAL_0:1;
            then
A238:       1 <= i1-'1 by A38,NAT_1:13;
A239:       i1-'1+(1+1) = i0 by A38,A233;
A240:       LSeg(1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,1))- |[0,1]|,
            1/2*((GoB f)*(i1,1)+(GoB f)*(i0,1))- |[0,1]|) is connected by Th7,
JORDAN1:9;
            i1-'1 <= i1 by NAT_D:35;
            then
A241:       i1-'1 <= len GoB f by A19,XXREAL_0:2;
A242:       j1-'1 = 0 by A236,XREAL_1:234;
            j1-'1 <= j1 by NAT_D:35;
            then
A243:       j1-'1 <= width GoB f by A22,XXREAL_0:2;
A244:       LSeg(1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,1))- |[0,1]|,
            1/2*((GoB f)*(i1,1)+(GoB f)*(i0,1))- |[0,1]|)
            meets Int cell(GoB f,i1,j1-'1) by A18,A19,A233,A242,GOBOARD6:87;
            LSeg(1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,1))- |[0,1]|,
            1/2*((GoB f)*(i1,1)+(GoB f)*(i0,1))- |[0,1]|) misses L~f
            by A18,A38,A238,A239,GOBOARD8:25;
            then LSeg(1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,1))- |[0,1]|,
            1/2*((GoB f)*(i1,1)+(GoB f)*
            (i0,1))- |[0,1]|) c= (L~f)` by SUBSET_1:43;
            then
A245:       LSeg(1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,1))- |[0,1 ]|,
            1/2*((GoB f)*(i1,1)+(GoB f)*(i0,1))- |[0,1]|) c= LeftComp f
            by A3,A5,A6,A48,A237,A240,A244,Th6,NAT_1:13;
A246:       LSeg(1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,1))- |[0,1]|,
            1/2*((GoB f)*(i1,1)+(GoB f)*(i0,1))- |[0,1]|) meets
            Int cell(GoB f,i1-'1,j0-'1) by A19,A24,A38,A233,A238,A242,
GOBOARD6:87;
            Int cell(GoB f,i1-'1,j1-'1) misses L~f by A241,A243,GOBOARD7:14;
            then Int cell(GoB f,i1-'1,j1-'1) c= (L~f)` by SUBSET_1:43;
            then
A247:       Int cell(GoB f,i1-'1,j1-'1) c= LeftComp f
            by A24,A48,A233,A241,A243,A245,A246,Th6,Th21;
A248:       left_cell(f,k+1) = cell(GoB f,i1-'1,j1)
            by A4,A5,A13,A14,A16,A17,A25,A38,A234,GOBOARD5:28;
A249:       LSeg(1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,1))- |[0,1]|,
            1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,2)))
            meets Int cell(GoB f,i1-'1,j1-'1) by A19,A38,A238,A242,GOBOARD6:87;
            LSeg(1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,1))- |[0,1]|,
            1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,2))) misses L~f
            by A5,A6,A11,A14,A17,A18,A24,A25,A38,A43,A234,A236,A238,A239,
GOBOARD8:7;
            then LSeg(1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,1))- |[0,1]|,
            1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,2))) c= (L~f)` by SUBSET_1:43;
            then
A250:       LSeg(1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,1))- |[0,1 ]|,
            1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,2))) c= LeftComp f
            by A48,A247,A249,Th6,Th8;
A251:       LSeg(1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,1))- |[0,1]|,
            1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,2))) meets
            Int cell(GoB f,i1-'1,j1) by A19,A23,A38,A234,A236,A238,GOBOARD6:85;
            Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
            then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;

hence Int left_cell(f,k+1) c= LeftComp f by A4,A5,A48,A248,A250,A251,Th6,Th22;
          end;
          suppose that
A252:       i0 = i1+1 and
A253:       j1+1 = j2 and
A254:       i1 = 1 and
A255:       j1 <> 1;
A256:       left_cell(f,k) = cell(GoB f,i1,j1-'1)
            by A6,A7,A10,A11,A13,A14,A24,A39,A252,GOBOARD5:30;
            1 < j0 by A21,A24,A252,A255,XXREAL_0:1;
            then
A257:       1 <= j0-'1 by A24,A39,A252,NAT_1:13;
A258:       j0-'1+(1+1) = j2 by A24,A39,A252,A253;
A259:       LSeg(1/2*((GoB f)*(1,j1-'1)+(GoB f)*(1,j1-'1+1))- |[1,0]|,
            1/2*((GoB f)*(1,j1-'1)+(GoB f)*(2,j1-'1+1))) is connected by Th7,
JORDAN1:9;
A260:       LSeg(1/2*((GoB f)*(1,j1-'1)+(GoB f)*(1,j1-'1+1))- |[1,0]|,
            1/2*((GoB f)*(1,j1-'1)+(GoB f)*(2,j1-'1+1)))
            meets Int cell(GoB f,i1,j1-'1)
            by A18,A22,A24,A39,A252,A254,A257,GOBOARD6:85;
            LSeg(1/2*((GoB f)*(1,j1-'1)+(GoB f)*(1,j1-'1+1))- |[1,0]|,
            1/2*((GoB f)*(1,j1-'1)+(GoB f)*(2,j1-'1+1))) misses L~f
            by A5,A6,A11,A14,A17,A23,A24,A25,A39,A43,A252,A253,A254,A257,A258,
GOBOARD8:17;
            then LSeg(1/2*((GoB f)*(1,j1-'1)+(GoB f)*(1,j1-'1+1))- |[1,0]|,
            1/2*((GoB f)*(1,j1-'1)+(GoB f)*(2,j1-'1+1))) c= (L~f)`
            by SUBSET_1:43;
            then
A261:       LSeg(1/2*((GoB f)*(1,j1-'1)+(GoB f)*(1,j1-'1+1))- |[1,0]|,
            1/2*((GoB f)*(1,j1-'1)+(GoB f)*(2,j1-'1+1))) c= LeftComp f
            by A3,A5,A6,A48,A256,A259,A260,Th6,NAT_1:13;
            i1-'1 <= i1 by NAT_D:35;
            then
A262:       i1-'1 <= len GoB f by A19,XXREAL_0:2;
A263:       i1-'1 = 0 by A254,XREAL_1:234;
            j1-'1 <= j1 by NAT_D:35;
            then
A264:       j1-'1 <= width GoB f by A22,XXREAL_0:2;
A265:       LSeg(1/2*((GoB f)*(1,j1-'1)+(GoB f)*(1,j1-'1+1))- |[1,0]|,
            1/2*((GoB f)*(1,j1-'1)+(GoB f)*(2,j1-'1+1))) meets
            Int cell(GoB f,i1-'1,j0-'1) by A22,A24,A39,A252,A257,A263,
GOBOARD6:88;
            Int cell(GoB f,i1-'1,j1-'1) misses L~f by A262,A264,GOBOARD7:14;
            then Int cell(GoB f,i1-'1,j1-'1) c= (L~f)` by SUBSET_1:43;
            then
A266:       Int cell(GoB f,i1-'1,j1-'1) c= LeftComp f
            by A24,A48,A252,A261,A262,A264,A265,Th6,Th21;
A267:       left_cell(f,k+1) = cell(GoB f,i1-'1,j1)
            by A4,A5,A13,A14,A16,A17,A25,A38,A253,GOBOARD5:28;
A268:       LSeg(1/2*((GoB f)*(1,j1-'1)+(GoB f)*(1,j1-'1+1))- |[1,0]|,
            1/2*((GoB f)*(1,j1-'1+1)+(GoB f)*(1,j1-'1+2))- |[1,0]|)
            meets Int cell(GoB f,i1-'1,j1-'1)
            by A22,A24,A39,A252,A257,A263,GOBOARD6:88;
            LSeg(1/2*((GoB f)*(1,j1-'1)+(GoB f)*(1,j1-'1+1))- |[1,0]|,
            1/2*((GoB f)*(1,j1-'1+1)+(GoB f)*(1,j1-'1+2))- |[1,0]|) misses L~f
            by A23,A24,A39,A252,A253,A257,GOBOARD8:31;
            then LSeg(1/2*((GoB f)*(1,j1-'1)+(GoB f)*(1,j1-'1+1))- |[1,0]|,
            1/2*((GoB f)*(1,j1-'1+1)+(GoB f)*(1,j1-'1+2))- |[1,0]|) c= (L~f)`
            by SUBSET_1:43;
            then
A269:       LSeg(1/2*((GoB f)*(1,j1-'1)+(GoB f)*(1,j1-'1+1))- |[1,0]|,

1/2*((GoB f)*(1,j1-'1+1)+(GoB f)*(1,j1-'1+2))- |[1,0]|) c= LeftComp f
            by A48,A266,A268,Th6,Th8;
A270:       LSeg(1/2*((GoB f)*(1,j1-'1)+(GoB f)*(1,j1-'1+1))- |[1,0]|,
            1/2*((GoB f)*(1,j1-'1+1)+(GoB f)*(1,j1-'1+2))- |[1,0]|) meets
            Int cell(GoB f,i1-'1,j1) by A22,A23,A39,A253,A263,GOBOARD6:88;
            Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
            then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;

hence Int left_cell(f,k+1) c= LeftComp f by A4,A5,A48,A267,A269,A270,Th6,Th22;
          end;
          suppose that
A271:       i0 = i1+1 and
A272:       j1+1 = j2 and
A273:       i1 <> 1 and
A274:       j1 <> 1;
A275:       left_cell(f,k) = cell(GoB f,i1,j1-'1)
            by A6,A7,A10,A11,A13,A14,A24,A39,A271,GOBOARD5:30;
            1 < j0 by A21,A24,A271,A274,XXREAL_0:1;
            then
A276:       1 <= j0-'1 by A24,A39,A271,NAT_1:13;
            1 < i1 by A19,A273,XXREAL_0:1;
            then
A277:       1 <= i1-'1 by A38,NAT_1:13;
A278:       i1-'1+(1+1) = i0 by A38,A271;
A279:       j0-'1+(1+1) = j2 by A24,A39,A271,A272;
A280:       LSeg(1/2*((GoB f)*(i1-'1,j0-'1)+(GoB f)*(i1,j1)),

1/2*((GoB f)*(i1,j0-'1)+(GoB f)*(i0,j1))) is connected by Th7,JORDAN1:9;
A281:       LSeg(1/2*((GoB f)*(i1-'1,j0-'1)+(GoB f)*(i1,j1)),
            1/2*((GoB f)*(i1,j0-'1)+(GoB f)*(i0,j1)))
            meets Int cell(GoB f,i1,j1-'1)
            by A18,A19,A22,A24,A39,A271,A276,GOBOARD6:85;
            LSeg(1/2*((GoB f)*(i1-'1,j0-'1)+(GoB f)*(i1,j1)),
            1/2*((GoB f)*(i1,j0-'1)+(GoB f)*(i0,j1))) misses L~f

by A5,A6,A11,A14,A17,A18,A23,A24,A25,A38,A39,A43,A272,A276,A277,A278,A279,
GOBOARD8:12;
            then LSeg(1/2*((GoB f)*(i1-'1,j0-'1)+(GoB f)*(i1,j1)),
            1/2*((GoB f)*(i1,j0-'1)+(GoB f)*(i0,j1))) c= (L~f)` by SUBSET_1:43;
            then
A282:       LSeg(1/2*((GoB f)*(i1-'1,j0-'1)+(GoB f)*(i1,j1)),
            1/2*((GoB f)*(i1,j0-'1)+(GoB f)*(i0,j1))) c= LeftComp f
            by A3,A5,A6,A48,A275,A280,A281,Th6,NAT_1:13;
            i1-'1 <= i1 by NAT_D:35;
            then
A283:       i1-'1 <= len GoB f by A19,XXREAL_0:2;
            j1-'1 <= j1 by NAT_D:35;
            then
A284:       j1-'1 <= width GoB f by A22,XXREAL_0:2;
A285:       LSeg(1/2*((GoB f)*(i1-'1,j0-'1)+(GoB f)*(i1,j1)),
            1/2*((GoB f)*(i1,j0-'1)+(GoB f)*(i0,j1))) meets
            Int cell(GoB f,i1-'1,j0-'1)
            by A19,A22,A24,A38,A39,A271,A276,A277,GOBOARD6:85;
            Int cell(GoB f,i1-'1,j1-'1) misses L~f by A283,A284,GOBOARD7:14;
            then Int cell(GoB f,i1-'1,j1-'1) c= (L~f)` by SUBSET_1:43;
            then
A286:       Int cell(GoB f,i1-'1,j1-'1) c= LeftComp f
            by A24,A48,A271,A282,A283,A284,A285,Th6,Th21;
A287:       left_cell(f,k+1) = cell(GoB f,i1-'1,j1)
            by A4,A5,A13,A14,A16,A17,A25,A38,A272,GOBOARD5:28;
A288:       LSeg(1/2*((GoB f)*(i1-'1,j1-'1)+(GoB f)*(i1,j1)),
            1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j2)))
            meets Int cell(GoB f,i1-'1,j1-'1)
            by A19,A22,A24,A38,A39,A271,A276,A277,GOBOARD6:85;
            LSeg(1/2*((GoB f)*(i1-'1,j1-'1)+(GoB f)*(i1,j1)),
            1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j2))) misses L~f

by A5,A6,A11,A14,A17,A18,A23,A24,A25,A38,A39,A43,A272,A276,A277,A278,A279,
GOBOARD8:2;
            then LSeg(1/2*((GoB f)*(i1-'1,j1-'1)+(GoB f)*(i1,j1)),
            1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j2))) c= (L~f)` by SUBSET_1:43;
            then
A289:       LSeg(1/2*((GoB f)*(i1-'1,j1-'1)+(GoB f)*(i1,j1)),
            1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j2))) c= LeftComp f
            by A48,A286,A288,Th6,Th8;
A290:       LSeg(1/2*((GoB f)*(i1-'1,j1-'1)+(GoB f)*(i1,j1)),
            1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j2))) meets
            Int cell(GoB f,i1-'1,j1) by A19,A22,A23,A38,A272,A277,GOBOARD6:85;
            Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
            then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;

hence Int left_cell(f,k+1) c= LeftComp f by A4,A5,A48,A287,A289,A290,Th6,Th22;
          end;
          suppose that
A291:       j0 = j1+1 and
A292:       i1+1 = i2;
            left_cell(f,k) = cell(GoB f,i0,j1)
            by A6,A7,A10,A11,A13,A14,A24,A38,A291,GOBOARD5:31
              .= left_cell(f,k+1)
            by A4,A5,A13,A14,A16,A17,A24,A25,A39,A291,A292,GOBOARD5:29;
            hence Int left_cell(f,k+1) c= LeftComp f by A3,A5,A6,NAT_1:13;
          end;
          suppose that
A293:       i0+1 = i1 and
A294:       i1+1 = i2 and
A295:       j0 = width GoB f;
A296:       left_cell(f,k) = cell(GoB f,i0,width GoB f)
            by A6,A7,A10,A11,A13,A14,A24,A42,A293,A295,GOBOARD5:29;
A297:       left_cell(f,k+1) = cell(GoB f,i1,width GoB f)
            by A4,A5,A13,A14,A16,A17,A24,A25,A42,A293,A294,A295,GOBOARD5:29;
A298:       LSeg
            (1/2*((GoB f)*(i0,width GoB f)+(GoB f)*(i0+1,width GoB f))+|[0,1]|,
            1/2*((GoB f)*(i1,width GoB f)+(GoB f)*(i1+1,width GoB f))+|[0,1]|)
            is connected by Th7,JORDAN1:9;
A299:       LSeg
            (1/2*((GoB f)*(i0,width GoB f)+(GoB f)*(i0+1,width GoB f))+|[0,1]|,
            1/2*((GoB f)*(i1,width GoB f)+(GoB f)*(i1+1,width GoB f))+|[0,1]|)
            meets Int cell(GoB f,i0,width GoB f) by A18,A19,A293,GOBOARD6:86;

LSeg(1/2*((GoB f)*(i0,width GoB f)+(GoB f)*(i0+1,width GoB f))+|[0,1 ]| ,
            1/2*((GoB f)*(i1,width GoB f)+(GoB f)*(i1+1,width GoB f))+|[0,1]|)
            misses L~f by A18,A20,A44,A293,A294,GOBOARD8:28;

then LSeg(1/2*((GoB f)*(i0,width GoB f)+(GoB f)*(i0+1,width GoB f))+|[0,1 ]|,
            1/2*((GoB f)*(i1,width GoB f)+(GoB f)*(i1+1,width GoB f))+|[0,1]|)
            c= (L~f)` by SUBSET_1:43;
            then
A300:       LSeg(1/2*((GoB f)*(i0,width GoB f)+(GoB f)*(i0+1, width GoB f))+
            |[0,1]|,
            1/2*((GoB f)*(i1,width GoB f)+(GoB f)*(i1+1,width GoB f))+|[0,1]|)
            c= LeftComp f by A3,A5,A6,A48,A296,A298,A299,Th6,NAT_1:13;
A301:       LSeg
            (1/2*((GoB f)*(i0,width GoB f)+(GoB f)*(i0+1,width GoB f))+|[0,1]|,
            1/2*((GoB f)*(i1,width GoB f)+(GoB f)*(i1+1,width GoB f))+|[0,1]|)
            meets Int cell(GoB f,i1,width GoB f) by A19,A20,A294,GOBOARD6:86;
            Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
            then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;

hence Int left_cell(f,k+1) c= LeftComp f by A4,A5,A48,A297,A300,A301,Th6,Th22;
          end;
          suppose that
A302:       i0+1 = i1 and
A303:       i1+1 = i2 and
A304:       j0 <> width GoB f;
A305:       left_cell(f,k) = cell(GoB f,i0,j1)
            by A6,A7,A10,A11,A13,A14,A24,A39,A302,GOBOARD5:29;
            width GoB f > j0 by A21,A304,XXREAL_0:1;
            then
A306:       width GoB f >= j0+1 by NAT_1:13;
A307:       left_cell(f,k+1) = cell(GoB f,i1,j1)
            by A4,A5,A13,A14,A16,A17,A25,A39,A303,GOBOARD5:29;
A308:       LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0+1,j0+1)),
            1/2*((GoB f)*(i1,j0)+(GoB f)*(i1+1,j0+1))) is connected by Th7,
JORDAN1:9;
A309:       LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0+1,j0+1)),
            1/2*((GoB f)*(i1,j0)+(GoB f)*(i1+1,j0+1)))
            meets Int cell(GoB f,i0,j1)
            by A18,A19,A22,A24,A302,A306,GOBOARD6:85;
            LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0+1,j0+1)),
            1/2*((GoB f)*(i1,j0)+(GoB f)*(i1+1,j0+1))) misses L~f
            by A5,A6,A11,A14,A17,A18,A20,A21,A24,A25,A43,A44,A302,A303,A306,
GOBOARD8:14;
            then LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0+1,j0+1)),
            1/2*((GoB f)*(i1,j0)+(GoB f)*
            (i1+1,j0+1))) c= (L~f)` by SUBSET_1:43;
            then
A310:       LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0+1,j0+1)),
            1/2*((GoB f)*(i1,j0)+(GoB f)*(i1+1,j0+1))) c= LeftComp f
            by A3,A5,A6,A48,A305,A308,A309,Th6,NAT_1:13;
A311:       LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0+1,j0+1)),
            1/2*((GoB f)*(i1,j0)+(GoB f)*(i1+1,j0+1))) meets

Int cell(GoB f,i1,j1) by A19,A20,A21,A24,A302,A303,A306,GOBOARD6:85;
            Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
            then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;

hence Int left_cell(f,k+1) c= LeftComp f by A4,A5,A48,A307,A310,A311,Th6,Th22;
          end;
          suppose that
A312:       i0+1 = i1 and
A313:       j1+1 = j2;
            left_cell(f,k) = cell(GoB f,i0,j1)
            by A6,A7,A10,A11,A13,A14,A24,A39,A312,GOBOARD5:29

.= left_cell(f,k+1) by A4,A5,A13,A14,A16,A17,A25,A312,A313,GOBOARD5:28;
            hence Int left_cell(f,k+1) c= LeftComp f by A3,A5,A6,NAT_1:13;
          end;
          suppose that
A314:       j0+1 = j1 and
A315:       i1+1 = i2 and
A316:       i0 = 1 and
A317:       j1 = width GoB f;
A318:       left_cell(f,k) = cell(GoB f,i1-'1,j0)
            by A6,A7,A10,A11,A13,A14,A24,A38,A314,GOBOARD5:28;
A319:       i0-'1 = 0 by A316,XREAL_1:234;
A320:       j1-'1 = j0 by A314,NAT_D:34;
A321:       LSeg(1/2*((GoB f)*(1,j0)+(GoB f)*(1,j1))- |[1,0]|,
            (GoB f)*(1,j1)+|[-1,1]|) is connected by Th7,JORDAN1:9;
A322:       LSeg(1/2*((GoB f)*(1,j0)+(GoB f)*(1,j1))- |[1,0]|,
            (GoB f)*(1,j1)+|[-1,1]|) meets Int cell(GoB f,i1-'1,j0)
            by A21,A22,A24,A314,A319,GOBOARD6:88;
            LSeg(1/2*((GoB f)*(1,j0)+(GoB f)*(1,j1))- |[1,0]|,
            (GoB f)*(1,j1)+|[-1,1]|) misses L~f by A317,A320,GOBOARD8:33;
            then LSeg(1/2*((GoB f)*(1,j0)+(GoB f)*(1,j1))- |[1,0]|,
            (GoB f)*(1,j1)+|[-1,1]|) c= (L~f)` by SUBSET_1:43;
            then
A323:       LSeg(1/2*((GoB f)*(1,j0)+(GoB f)*(1,j1))- |[1,0]|,
            (GoB f)*(1,j1)+|[-1,1]|) c= LeftComp f
            by A3,A5,A6,A48,A318,A321,A322,Th6,NAT_1:13;
            i1-'1 <= i1 by NAT_D:35;
            then
A324:       i1-'1 <= len GoB f by A19,XXREAL_0:2;
A325:       LSeg(1/2*((GoB f)*(1,j0)+(GoB f)*(1,j1))- |[1,0]|,
            (GoB f)*(1,j1)+|[-1,1]|) meets Int cell(GoB f,i0-'1,j1)
            by A317,A319,GOBOARD6:92;
            Int cell(GoB f,i1-'1,j1) misses L~f by A22,A324,GOBOARD7:14;
            then Int cell(GoB f,i1-'1,j1) c= (L~f)` by SUBSET_1:43;
            then
A326:       Int cell(GoB f,i1-'1,j1) c= LeftComp f by A22,A24,A48,A314,A323
,A324,A325,Th6,Th21;
A327:       left_cell(f,k+1) = cell(GoB f,i1,j1)
            by A4,A5,A13,A14,A16,A17,A25,A314,A315,GOBOARD5:29;
A328:       LSeg((GoB f)*(1,j1)+|[-1,1]|,
            1/2*((GoB f)*(1,j1)+(GoB f)*(2,j1))+|[0,1]|)
            meets Int cell(GoB f,i1-'1,j1) by A24,A314,A317,A319,GOBOARD6:92;
            LSeg((GoB f)*(1,j1)+|[-1,1]|,
            1/2*((GoB f)*(1,j1)+(GoB f)*(2,j1))+|[0,1]|) misses L~f
            by A317,GOBOARD8:29;
            then LSeg((GoB f)*(1,j1)+|[-1,1]|,
            1/2*((GoB f)*(1,j1)+(GoB f)*(2,j1))+|[0,1]|) c= (L~f)`
            by SUBSET_1:43;
            then
A329:       LSeg((GoB f)*(1,j1)+|[-1,1]|,
            1/2*((GoB f)*(1,j1)+(GoB f)*(2,j1))+|[0,1]|) c= LeftComp f
            by A48,A326,A328,Th6,Th8;
A330:       LSeg((GoB f)*(1,j1)+|[-1,1]|,
            1/2*((GoB f)*(1,j1)+(GoB f)*(2,j1))+|[0,1]|) meets
            Int cell(GoB f,i1,j1) by A20,A24,A314,A315,A316,A317,GOBOARD6:86;
            Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
            then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;

hence Int left_cell(f,k+1) c= LeftComp f by A4,A5,A48,A327,A329,A330,Th6,Th22;
          end;
          suppose that
A331:       j0+1 = j1 and
A332:       i1+1 = i2 and
A333:       i0 <> 1 and
A334:       j1 = width GoB f;
A335:       left_cell(f,k) = cell(GoB f,i1-'1,j0)
            by A6,A7,A10,A11,A13,A14,A24,A38,A331,GOBOARD5:28;
            1 < i0 by A18,A333,XXREAL_0:1;
            then
A336:       1 <= i0-'1 by A24,A38,A331,NAT_1:13;
A337:       j1-'1 = j0 by A331,NAT_D:34;
A338:       i1-'1+(1+1) = i2 by A38,A332;
A339:       LSeg(1/2*((GoB f)*(i1-'1,j0)+(GoB f)*(i1,j1)),
            1/2*((GoB f)*(i1-'1,j1)+(GoB f)*
            (i1,j1))+|[0,1]|) is connected by Th7,JORDAN1:9;
A340:       LSeg(1/2*((GoB f)*(i1-'1,j0)+(GoB f)*(i1,j1)),
            1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1))+|[0,1]|)
            meets Int cell(GoB f,i1-'1,j0)
            by A19,A21,A22,A24,A38,A331,A336,GOBOARD6:85;
            LSeg(1/2*((GoB f)*(i1-'1,j0)+(GoB f)*(i1,j1)),
            1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1))+|[0,1]|) misses L~f
            by A5,A6,A11,A14,A17,A20,A24,A25,A38,A43,A331,A334,A336,A337,A338,
GOBOARD8:9;
            then LSeg(1/2*((GoB f)*(i1-'1,j0)+(GoB f)*(i1,j1)),
            1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1))+|[0,1]|) c= (L~f)`
            by SUBSET_1:43;
            then
A341:       LSeg(1/2*((GoB f)*(i1-'1,j0)+(GoB f)*(i1,j1)),
            1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1))+|[0,1]|) c= LeftComp f
            by A3,A5,A6,A48,A335,A339,A340,Th6,NAT_1:13;
            i1-'1 <= i1 by NAT_D:35;
            then
A342:       i1-'1 <= len GoB f by A19,XXREAL_0:2;
A343:       LSeg(1/2*((GoB f)*(i1-'1,j0)+(GoB f)*(i1,j1)),
            1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1))+|[0,1]|) meets
            Int cell(GoB f,i0-'1,j1) by A18,A24,A38,A331,A334,A336,GOBOARD6:86;
            Int cell(GoB f,i1-'1,j1) misses L~f by A22,A342,GOBOARD7:14;
            then Int cell(GoB f,i1-'1,j1) c= (L~f)` by SUBSET_1:43;
            then
A344:       Int cell(GoB f,i1-'1,j1) c= LeftComp f by A22,A24,A48,A331,A341
,A342,A343,Th6,Th21;
A345:       left_cell(f,k+1) = cell(GoB f,i1,j1)
            by A4,A5,A13,A14,A16,A17,A25,A331,A332,GOBOARD5:29;
A346:       LSeg(1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1))+|[0,1]|,
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i2,j1))+|[0,1]|)
            meets Int cell(GoB f,i1-'1,j1) by A19,A24,A38,A331,A334,A336,
GOBOARD6:86;
            LSeg(1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1))+|[0,1]|,
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i2,j1))+|[0,1]|) misses L~f
            by A20,A24,A38,A331,A334,A336,A338,GOBOARD8:28;
            then LSeg(1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1))+|[0,1]|,
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i2,j1))+|[0,1]|) c= (L~f)`
            by SUBSET_1:43;
            then
A347:       LSeg(1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1))+|[0, 1]|,
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i2,j1))+|[0,1]|) c= LeftComp f
            by A48,A344,A346,Th6,Th8;
A348:       LSeg(1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1))+|[0,1]|,
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i2,j1))+|[0,1]|) meets
            Int cell(GoB f,i1,j1) by A19,A20,A332,A334,GOBOARD6:86;
            Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
            then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;

hence Int left_cell(f,k+1) c= LeftComp f by A4,A5,A48,A345,A347,A348,Th6,Th22;
          end;
          suppose that
A349:       j0+1 = j1 and
A350:       i1+1 = i2 and
A351:       i0 = 1 and
A352:       j1 <> width GoB f;
A353:       left_cell(f,k) = cell(GoB f,i1-'1,j0)
            by A6,A7,A10,A11,A13,A14,A24,A38,A349,GOBOARD5:28;
            width GoB f > j1 by A22,A352,XXREAL_0:1;
            then
A354:       width GoB f >= j1+1 by NAT_1:13;
A355:       i0-'1 = 0 by A351,XREAL_1:234;
A356:       LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0,j1))- |[1,0]|,
            1/2*((GoB f)*(i0,j1)+(GoB f)*
            (i0,j1+1))- |[1,0]|) is connected by Th7,JORDAN1:9;
A357:       LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0,j1))- |[1,0]|,
            1/2*((GoB f)*(i0,j1)+(GoB f)*(i0,j1+1))- |[1,0]|)
            meets Int cell(GoB f,i1-'1,j0)
            by A21,A22,A24,A349,A351,A355,GOBOARD6:88;
            LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0,j1))- |[1,0]|,
            1/2*((GoB f)*(i0,j1)+(GoB f)*(i0,j1+1))- |[1,0]|) misses L~f
            by A21,A46,A349,A351,A354,GOBOARD8:31;
            then LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0,j1))- |[1,0]|,
            1/2*((GoB f)*(i0,j1)+(GoB f)*(i0,j1+1))- |[1,0]|) c= (L~f)`
            by SUBSET_1:43;
            then
A358:       LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0,j1))- |[1,0 ]|,
            1/2*((GoB f)*(i0,j1)+(GoB f)*(i0,j1+1))- |[1,0]|) c= LeftComp f
            by A3,A5,A6,A48,A353,A356,A357,Th6,NAT_1:13;
            i1-'1 <= i1 by NAT_D:35;
            then
A359:       i1-'1 <= len GoB f by A19,XXREAL_0:2;
A360:       LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0,j1))- |[1,0]|,
            1/2*((GoB f)*(i0,j1)+(GoB f)*(i0,j1+1))- |[1,0]|) meets
            Int cell(GoB f,i0-'1,j1) by A22,A351,A354,A355,GOBOARD6:88;
            Int cell(GoB f,i1-'1,j1) misses L~f by A22,A359,GOBOARD7:14;
            then Int cell(GoB f,i1-'1,j1) c= (L~f)` by SUBSET_1:43;
            then
A361:       Int cell(GoB f,i1-'1,j1) c= LeftComp f by A22,A24,A48,A349,A358
,A359,A360,Th6,Th21;
A362:       left_cell(f,k+1) = cell(GoB f,i1,j1)
            by A4,A5,A13,A14,A16,A17,A25,A349,A350,GOBOARD5:29;
A363:       LSeg(1/2*((GoB f)*(i1,j0+1)+(GoB f)*(i1,j0+2))- |[1,0]|,
            1/2*((GoB f)*(i1,j0+1)+(GoB f)*(i2,j0+2)))
            meets Int cell(GoB f,i1-'1,j1)
            by A22,A24,A349,A351,A354,A355,GOBOARD6:88;
            LSeg(1/2*((GoB f)*(i1,j0+1)+(GoB f)*(i1,j0+2))- |[1,0]|,
            1/2*((GoB f)*(i1,j0+1)+(GoB f)*(i2,j0+2))) misses L~f

by A5,A6,A11,A14,A17,A21,A24,A25,A43,A349,A350,A351,A354,GOBOARD8:18;
            then LSeg(1/2*((GoB f)*(i1,j0+1)+(GoB f)*(i1,j0+2))- |[1,0]|,
            1/2*((GoB f)*(i1,j0+1)+(GoB f)*
            (i2,j0+2))) c= (L~f)` by SUBSET_1:43;
            then
A364:       LSeg(1/2*((GoB f)*(i1,j0+1)+(GoB f)*(i1,j0+2))- |[ 1,0]|,
            1/2*((GoB f)*(i1,j0+1)+(GoB f)*(i2,j0+2))) c= LeftComp f
            by A48,A361,A363,Th6,Th8;
A365:       LSeg(1/2*((GoB f)*(i1,j0+1)+(GoB f)*(i1,j0+2))- |[1,0]|,
            1/2*((GoB f)*(i1,j0+1)+(GoB f)*(i2,j0+2))) meets
            Int cell(GoB f,i1,j1) by A19,A20,A22,A349,A350,A354,GOBOARD6:85;
            Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
            then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;

hence Int left_cell(f,k+1) c= LeftComp f by A4,A5,A48,A362,A364,A365,Th6,Th22;
          end;
          suppose that
A366:       j0+1 = j1 and
A367:       i1+1 = i2 and
A368:       i0 <> 1 and
A369:       j1 <> width GoB f;
A370:       left_cell(f,k) = cell(GoB f,i1-'1,j0)
            by A6,A7,A10,A11,A13,A14,A24,A38,A366,GOBOARD5:28;
            1 < i0 by A18,A368,XXREAL_0:1;
            then
A371:       1 <= i0-'1 by A24,A38,A366,NAT_1:13;
            width GoB f > j1 by A22,A369,XXREAL_0:1;
            then
A372:       width GoB f >= j1+1 by NAT_1:13;
A373:       i1-'1+(1+1) = i2 by A38,A367;
A374:       LSeg(1/2*((GoB f)*(i0-'1,j0)+(GoB f)*(i0,j1)),
            1/2*((GoB f)*(i0-'1,j1)+(GoB f)*(i0,j1+1))) is connected by Th7,
JORDAN1:9;
A375:       LSeg(1/2*((GoB f)*(i0-'1,j0)+(GoB f)*(i0,j1)),
            1/2*((GoB f)*(i0-'1,j1)+(GoB f)*(i0,j1+1)))
            meets Int cell(GoB f,i1-'1,j0)
            by A19,A21,A22,A24,A38,A366,A371,GOBOARD6:85;
            LSeg(1/2*((GoB f)*(i0-'1,j0)+(GoB f)*(i0,j1)),
            1/2*((GoB f)*(i0-'1,j1)+(GoB f)*(i0,j1+1))) misses L~f

by A5,A6,A11,A14,A17,A20,A21,A24,A25,A38,A43,A46,A366,A371,A372,A373,GOBOARD8:3
            ;
            then LSeg(1/2*((GoB f)*(i0-'1,j0)+(GoB f)*(i0,j1)),
            1/2*((GoB f)*(i0-'1,j1)+(GoB f)*
            (i0,j1+1))) c= (L~f)` by SUBSET_1:43;
            then
A376:       LSeg(1/2*((GoB f)*(i0-'1,j0)+(GoB f)*(i0,j1)),
            1/2*((GoB f)*(i0-'1,j1)+(GoB f)*(i0,j1+1))) c= LeftComp f
            by A3,A5,A6,A48,A370,A374,A375,Th6,NAT_1:13;
            i1-'1 <= i1 by NAT_D:35;
            then
A377:       i1-'1 <= len GoB f by A19,XXREAL_0:2;
A378:       LSeg(1/2*((GoB f)*(i0-'1,j0)+(GoB f)*(i0,j1)),
            1/2*((GoB f)*(i0-'1,j1)+(GoB f)*(i0,j1+1))) meets
            Int cell(GoB f,i0-'1,j1) by A18,A22,A24,A38,A366,A371,A372,
GOBOARD6:85;
            Int cell(GoB f,i1-'1,j1) misses L~f by A22,A377,GOBOARD7:14;
            then Int cell(GoB f,i1-'1,j1) c= (L~f)` by SUBSET_1:43;
            then
A379:       Int cell(GoB f,i1-'1,j1) c= LeftComp f by A22,A24,A48,A366,A376
,A377,A378,Th6,Th21;
A380:       left_cell(f,k+1) = cell(GoB f,i1,j1)
            by A4,A5,A13,A14,A16,A17,A25,A366,A367,GOBOARD5:29;
A381:       LSeg(1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1+1)),
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i2,j1+1)))
            meets Int cell(GoB f,i1-'1,j1) by A19,A22,A24,A38,A366,A371,A372,
GOBOARD6:85;
            LSeg(1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1+1)),
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i2,j1+1))) misses L~f

by A5,A6,A11,A14,A17,A20,A21,A24,A25,A38,A43,A46,A366,A371,A372,A373,
GOBOARD8:15;
            then LSeg(1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1+1)),
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i2,j1+1))) c= (L~f)` by SUBSET_1:43;
            then
A382:       LSeg(1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1+1)),
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i2,j1+1))) c= LeftComp f
            by A48,A379,A381,Th6,Th8;
A383:       LSeg(1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1+1)),
            1/2*((GoB f)*(i1,j1)+(GoB f)*(i2,j1+1))) meets
            Int cell(GoB f,i1,j1) by A19,A20,A22,A367,A372,GOBOARD6:85;
            Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
            then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;

hence Int left_cell(f,k+1) c= LeftComp f by A4,A5,A48,A380,A382,A383,Th6,Th22;
          end;
          suppose that
A384:       j0+1 = j1 and
A385:       j1+1 = j2 and
A386:       i0 = 1;
A387:       left_cell(f,k) = cell(GoB f,0,j0)
            by A6,A7,A10,A11,A13,A14,A24,A49,A384,A386,GOBOARD5:28;
A388:       left_cell(f,k+1) = cell(GoB f,0,j1)
            by A4,A5,A13,A14,A16,A17,A24,A25,A49,A384,A385,A386,GOBOARD5:28;
A389:       LSeg(1/2*((GoB f)*(1,j0)+(GoB f)*(1,j1))- |[1,0]|,
            1/2*((GoB f)*(1,j1)+(GoB f)*(1,j2))- |[1,0]|) is connected by Th7,
JORDAN1:9;
A390:       LSeg(1/2*((GoB f)*(1,j0)+(GoB f)*(1,j1))- |[1,0]|,
            1/2*((GoB f)*(1,j1)+(GoB f)*(1,j2))- |[1,0]|)
            meets Int cell(GoB f,0,j0) by A21,A22,A384,GOBOARD6:88;
            LSeg(1/2*((GoB f)*(1,j0)+(GoB f)*(1,j1))- |[1,0]|,
            1/2*((GoB f)*(1,j1)+(GoB f)*(1,j2))- |[1,0]|) misses L~f
            by A21,A23,A46,A384,A385,GOBOARD8:31;
            then LSeg(1/2*((GoB f)*(1,j0)+(GoB f)*(1,j1))- |[1,0]|,
            1/2*((GoB f)*(1,j1)+(GoB f)*(1,j2))- |[1,0]|) c= (L~f)`
            by SUBSET_1:43;
            then
A391:       LSeg(1/2*((GoB f)*(1,j0)+(GoB f)*(1,j1))- |[1,0]|,
            1/2*((GoB f)*(1,j1)+(GoB f)*(1,j2))- |[1,0]|) c= LeftComp f
            by A3,A5,A6,A48,A387,A389,A390,Th6,NAT_1:13;
A392:       LSeg(1/2*((GoB f)*(1,j0)+(GoB f)*(1,j1))- |[1,0]|,
            1/2*((GoB f)*(1,j1)+(GoB f)*(1,j2))- |[1,0]|) meets
            Int cell(GoB f,0,j1) by A22,A23,A385,GOBOARD6:88;
            Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
            then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;

hence Int left_cell(f,k+1) c= LeftComp f by A4,A5,A48,A388,A391,A392,Th6,Th22;
          end;
          suppose that
A393:       j0+1 = j1 and
A394:       j1+1 = j2 and
A395:       i0 <> 1;
A396:       left_cell(f,k) = cell(GoB f,i1-'1,j0)
            by A6,A7,A10,A11,A13,A14,A24,A38,A393,GOBOARD5:28;
            1 < i0 by A18,A395,XXREAL_0:1;
            then
A397:       1 <= i0-'1 by A24,A38,A393,NAT_1:13;
A398:       left_cell(f,k+1) = cell(GoB f,i1-'1,j1)
            by A4,A5,A13,A14,A16,A17,A25,A38,A394,GOBOARD5:28;
A399:       LSeg(1/2*((GoB f)*(i0-'1,j0)+(GoB f)*(i0,j1)),

1/2*((GoB f)*(i0-'1,j1)+(GoB f)*(i0,j2))) is connected by Th7,JORDAN1:9;
A400:       LSeg(1/2*((GoB f)*(i0-'1,j0)+(GoB f)*(i0,j1)),
            1/2*((GoB f)*(i0-'1,j1)+(GoB f)*(i0,j2)))
            meets Int cell(GoB f,i1-'1,j0)
            by A19,A21,A22,A24,A38,A393,A397,GOBOARD6:85;
            LSeg(1/2*((GoB f)*(i0-'1,j0)+(GoB f)*(i0,j1)),
            1/2*((GoB f)*(i0-'1,j1)+(GoB f)*(i0,j2))) misses L~f

by A5,A6,A11,A14,A17,A18,A21,A23,A24,A25,A38,A43,A46,A393,A394,A397,GOBOARD8:1;
            then LSeg(1/2*((GoB f)*(i0-'1,j0)+(GoB f)*(i0,j1)),
            1/2*((GoB f)*(i0-'1,j1)+(GoB f)*(i0,j2))) c= (L~f)` by SUBSET_1:43;
            then
A401:       LSeg(1/2*((GoB f)*(i0-'1,j0)+(GoB f)*(i0,j1)),
            1/2*((GoB f)*(i0-'1,j1)+(GoB f)*(i0,j2))) c= LeftComp f
            by A3,A5,A6,A48,A396,A399,A400,Th6,NAT_1:13;
A402:       LSeg(1/2*((GoB f)*(i0-'1,j0)+(GoB f)*(i0,j1)),
            1/2*((GoB f)*(i0-'1,j1)+(GoB f)*(i0,j2))) meets
            Int cell(GoB f,i0-'1,j1) by A18,A22,A23,A24,A38,A393,A394,A397,
GOBOARD6:85;
            Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
            then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;

hence Int left_cell(f,k+1) c= LeftComp f by A4,A5,A24,A48,A393,A398,A401,A402
,Th6,Th22;
          end;
        end;
        hence Int left_cell(f,k+1) c= LeftComp f;
      end;
    end;
  end;
  thus for k holds P[k] from NAT_1:sch 1(A1,A2);
end;

theorem
  GoB Rev f = GoB f by Lm1;

theorem Th26:
  RightComp f = LeftComp Rev f
proof
  LeftComp Rev f is_a_component_of (L~Rev f)` by Def1;
  then
A1: LeftComp Rev f is_a_component_of (L~f)` by SPPOL_2:22;
A2: len f >= 4 by GOBOARD7:36;
A3: len f >= 1+1 by GOBOARD7:36,XXREAL_0:2;
A4: len f -' 1 + 1 = len f by A2,XREAL_1:237,XXREAL_0:2;
  then
A5: 1 <= len f -' 1 by A3,XREAL_1:8;
A6: len f -' 1 + 1 <= len Rev f by A4,FINSEQ_5:def 3;
  right_cell(f,1) = left_cell(Rev f,len f -' 1) by A4,A5,Th13;
  then Int right_cell(f,1) c= LeftComp Rev f by A5,A6,Th24;
  hence thesis by A1,Def2;
end;

theorem
  RightComp Rev f = LeftComp f
proof Rev Rev f = f by FINSEQ_6:29;
  hence thesis by Th26;
end;

theorem
  for k st 1 <= k & k+1 <= len f holds Int right_cell(f,k) c= RightComp f
proof
  let k such that
A1: 1 <= k & k+1 <= len f;
A2: len f = len Rev f by FINSEQ_5:def 3;
  k <= len f by A1,NAT_1:13;
  then
A3: len f-'k+k = len f by XREAL_1:237;
  then
A4: 1 <= len f-'k & len f-'k+1 <= len f by A1,XREAL_1:8;
  then
A5: right_cell(f,k) = left_cell(Rev f,len f-'k) by A1,A3,Th13;
  RightComp f = LeftComp Rev f by Th26;
  hence Int right_cell(f,k) c= RightComp f by A2,A4,A5,Th24;
end;

