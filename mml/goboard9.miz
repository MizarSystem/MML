:: Left and Right Component of the Complement of a Special Closed Curve
::  by Andrzej Trybulec
::
:: Received October 29, 1995
:: Copyright (c) 1995 Association of Mizar Users

environ

 vocabularies SEQM_3, GOBOARD5, PRE_TOPC, EUCLID, GOBOARD1, ARYTM_1, CONNSP_1,
      BOOLE, RELAT_1, SUBSET_1, RELAT_2, TOPREAL1, JORDAN1, FINSEQ_1, FINSEQ_5,
      FUNCT_1, MCART_1, GOBOARD2, CARD_1, MATRIX_1, ABSVALUE, FINSEQ_6, ARYTM,
      TREES_1, TOPS_1, ARYTM_3, GOBOARD9, FINSEQ_4, ORDINAL2;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, CARD_1, NUMBERS, XCMPLX_0,
      COMPLEX1,
      REAL_1, NAT_1, BINARITH, FUNCT_1, PARTFUN1, FINSEQ_1, FINSEQ_4, MATRIX_1,
      STRUCT_0, PRE_TOPC, TOPS_1, CONNSP_1, EUCLID, TOPREAL1, GOBOARD1,
      GOBOARD2, JORDAN1, FINSEQ_5, FINSEQ_6, GOBOARD5, XXREAL_0;
 constructors PARTFUN1, XXREAL_0, REAL_1, NAT_1, COMPLEX1, FINSEQ_4, BINARITH,
      FINSEQ_5, TOPS_1, CONNSP_1, GOBOARD2, JORDAN1, GOBOARD5;
 registrations XBOOLE_0, RELSET_1, NUMBERS, XXREAL_0, XREAL_0, MEMBERED,
      FINSEQ_1, FINSEQ_6, STRUCT_0, PRE_TOPC, EUCLID, TOPREAL1, GOBOARD2,
      GOBOARD5, ORDINAL1;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, FINSEQ_6, GOBOARD5, GOBOARD1, JORDAN1, XBOOLE_0;
 theorems CONNSP_1, GOBOARD5, TOPS_1, SPPOL_2, PRE_TOPC, GOBOARD1, TARSKI,
      TOPREAL1, GOBOARD2, FINSEQ_3, NAT_1, SUBSET_1, FINSEQ_6, GOBOARD6,
      GOBOARD7, GOBOARD8, BINARITH, FINSEQ_1, FINSEQ_5, JORDAN1, TSEP_1, INT_1,
      XBOOLE_0, XBOOLE_1, XREAL_1, COMPLEX1, XXREAL_0, XREAL_0, PARTFUN1;
 schemes NAT_1;

begin

reserve f for non constant standard special_circular_sequence,
        i,j,k,i1,i2,j1,j2 for Element of NAT,
        r,s,r1,s1,r2,s2 for Real,
        a,b for natural number,
        p,q for Point of TOP-REAL 2,
        G for Go-board;

canceled 2;

theorem Th3:
 for GX being TopSpace, A1,A2,B being Subset of GX
  st A1 is_a_component_of B & A2 is_a_component_of B holds
   A1 = A2 or A1 misses A2
proof let GX be TopSpace, A1,A2,B be Subset of GX;
 assume A1 is_a_component_of B;
 then consider B1 being Subset of GX|B such that
A1: B1 = A1 and
A2: B1 is_a_component_of GX|B by CONNSP_1:def 6;
 assume A2 is_a_component_of B;
 then ex B2 being Subset of GX|B st B2 = A2 & B2 is_a_component_of GX|B
  by CONNSP_1:def 6;
 hence A1 = A2 or A1 misses A2 by A1,A2,CONNSP_1:37;
end;

theorem Th4:
 for GX being TopSpace,
     A,B being Subset of GX,
     AA being Subset of GX|B st A = AA holds GX|A = GX|B|AA
proof
 let GX be TopSpace, A,B be Subset of GX;
 let AA be Subset of GX|B; assume
A1: A = AA;
      the carrier of GX|A = [#](GX|A) by PRE_TOPC:12
   .= A by PRE_TOPC:def 10;
  then reconsider GY = GX|A as strict SubSpace of GX|B by A1,TSEP_1:4;
      [#] GY = AA by A1,PRE_TOPC:def 10;
 hence GX|A = GX|B|AA by PRE_TOPC:def 10;
end;

theorem Th5:
 for GX being non empty TopSpace,
     A, B being non empty Subset of GX
  st A c= B & A is connected
 ex C being Subset of GX st C is_a_component_of B & A c= C
 proof
  let GX be non empty TopSpace, A, B be non empty Subset of GX such that
A1: A c= B and
A2: A is connected;
  consider p being set such that
A3: p in A by XBOOLE_0:def 1;
A4: B = [#](GX|B) by PRE_TOPC:def 10
   .= the carrier of GX|B by PRE_TOPC:12;
 then reconsider p as Point of GX|B by A1,A3;
 reconsider C = Component_of p as Subset of GX by PRE_TOPC:39;
 take C;
A5: Component_of p is_a_component_of GX|B by CONNSP_1:43;
  hence C is_a_component_of B by CONNSP_1:def 6;
   reconsider AA = A as Subset of GX|B by A1,A4;
       GX|A is connected by A2,CONNSP_1:def 3;
   then GX|B|AA is connected by Th4;
then A6: AA is connected by CONNSP_1:def 3;
       p in Component_of p by CONNSP_1:40;
   then AA /\ Component_of p <> {}(GX|B) by A3,XBOOLE_0:def 3;
   then AA meets Component_of p by XBOOLE_0:def 7;
  hence A c= C by A5,A6,CONNSP_1:38;
 end;

theorem Th6:
 for GX being non empty TopSpace, A,B,C,D being Subset of GX
  st B is connected & C is_a_component_of D
      & A c= C & A meets B & B c= D
  holds B c= C
 proof let GX be non empty TopSpace,
           A,B,C,D be Subset of GX such that
A1: B is connected and
A2: C is_a_component_of D and
A3: A c= C and
A4: A meets B and
A5: B c= D;
A6: A <> {} & B <> {} by A4,XBOOLE_1:65;
   reconsider A,B as non empty Subset of GX by A4,XBOOLE_1:65;
   reconsider C,D as non empty Subset of GX by A3,A5,A6,XBOOLE_1:3;
   consider CC being Subset of GX such that
A7: CC is_a_component_of D and
A8: B c= CC by A1,A5,Th5;
       A meets CC by A4,A8,XBOOLE_1:63;
then A9: C meets CC by A3,XBOOLE_1:63;
    consider C1 being Subset of GX|D such that
A10:  C1 = C and
A11:  C1 is_a_component_of GX|D by A2,CONNSP_1:def 6;
    consider CC1 being Subset of GX|D such that
A12:  CC1 = CC and
A13:  CC1 is_a_component_of GX|D by A7,CONNSP_1:def 6;
  thus thesis by A8,A9,A10,A11,A12,A13,CONNSP_1:37;
 end;

theorem Th7:
 LSeg(p,q) is convex
 proof
   for w1,w2 be Point of TOP-REAL 2 st w1 in LSeg(p,q) & w2 in LSeg(p,q)
    holds LSeg(w1,w2) c= LSeg(p,q) by TOPREAL1:12;
  hence LSeg(p,q) is convex by JORDAN1:def 1;
 end;

theorem Th8:
 LSeg(p,q) is connected
 proof
   LSeg(p,q) is convex by Th7;
  hence LSeg(p,q) is connected by JORDAN1:9;
 end;

registration
 cluster convex non empty Subset of TOP-REAL 2;
 existence
  proof consider p being Point of TOP-REAL 2;
   take LSeg(p,p);
   thus thesis by Th7;
  end;
end;

theorem Th9:
 for P,Q being convex Subset of TOP-REAL 2 holds P /\ Q is convex
  proof let P,Q be convex Subset of TOP-REAL 2;
   let p,q; assume
A1: p in P /\ Q & q in P /\ Q;
    then p in P & q in P by XBOOLE_0:def 3;
then A2:  LSeg(p,q) c= P by JORDAN1:def 1;
        p in Q & q in Q by A1,XBOOLE_0:def 3;
    then LSeg(p,q) c= Q by JORDAN1:def 1;
   hence LSeg(p,q) c= P /\ Q by A2,XBOOLE_1:19;
  end;

theorem Th10:
  for f being FinSequence of TOP-REAL 2 holds Rev X_axis f = X_axis Rev f
 proof let f be FinSequence of TOP-REAL 2;
A1: len Rev X_axis f = len X_axis f by FINSEQ_5:def 3
           .= len f by GOBOARD1:def 3
           .= len Rev f by FINSEQ_5:def 3;
     len X_axis f = len f by GOBOARD1:def 3;
then A2: dom X_axis f = dom f by FINSEQ_3:31;
A3: len f = len Rev f by FINSEQ_5:def 3;
       now let k such that
A4:  k in dom Rev X_axis f;
    set l = len f + 1 -' k;
        k <= len f & len f < len f + 1 by A1,A3,A4,FINSEQ_3:27,NAT_1:13;
    then k <= len f +1 by XXREAL_0:2;
then A5: l + k = len f + 1 by BINARITH:53;
A6:  Rev Rev X_axis f = X_axis f by FINSEQ_6:29;
    then A7: l in dom X_axis f by A1,A3,A4,A5,FINSEQ_5:62;
    thus (Rev X_axis f).k = (Rev X_axis f)/.k by A4,PARTFUN1:def 8
               .= (X_axis f)/.l by A1,A3,A4,A5,A6,FINSEQ_5:69
               .= (X_axis f).l by A7,PARTFUN1:def 8
               .= (f/.l)`1 by A7,GOBOARD1:def 3
               .= ((Rev f)/.k)`1 by A2,A5,A7,FINSEQ_5:69;
   end;
  hence Rev X_axis f = X_axis Rev f by A1,GOBOARD1:def 3;
 end;

theorem Th11:
  for f being FinSequence of TOP-REAL 2 holds Rev Y_axis f = Y_axis Rev f
 proof let f be FinSequence of TOP-REAL 2;
A1: len Rev Y_axis f = len Y_axis f by FINSEQ_5:def 3
           .= len f by GOBOARD1:def 4
           .= len Rev f by FINSEQ_5:def 3;
     len Y_axis f = len f by GOBOARD1:def 4;
then A2: dom Y_axis f = dom f by FINSEQ_3:31;
A3: len f = len Rev f by FINSEQ_5:def 3;
       now let k such that
A4:  k in dom Rev Y_axis f;
    set l = len f + 1 -' k;
        k <= len f & len f < len f + 1 by A1,A3,A4,FINSEQ_3:27,NAT_1:13;
    then k <= len f +1 by XXREAL_0:2;
then A5: l + k = len f + 1 by BINARITH:53;
A6:  Rev Rev Y_axis f = Y_axis f by FINSEQ_6:29;
    then A7: l in dom Y_axis f by A1,A3,A4,A5,FINSEQ_5:62;
    thus (Rev Y_axis f).k = (Rev Y_axis f)/.k by A4,PARTFUN1:def 8
               .= (Y_axis f)/.l by A1,A3,A4,A5,A6,FINSEQ_5:69
               .= (Y_axis f).l by A7,PARTFUN1:def 8
               .= (f/.l)`2 by A7,GOBOARD1:def 4
               .= ((Rev f)/.k)`2 by A2,A5,A7,FINSEQ_5:69;
   end;
  hence Rev Y_axis f = Y_axis Rev f by A1,GOBOARD1:def 4;
 end;

Lm1: for f,ff being non empty FinSequence of TOP-REAL 2 st ff = Rev f
 holds GoB ff = GoB f
 proof let f,ff be non empty FinSequence of TOP-REAL 2; assume
A1:  ff = Rev f;
then A2: Rev X_axis f = X_axis ff by Th10;
A3: rng Incr X_axis ff = rng X_axis ff by GOBOARD2:def 2
          .= rng X_axis f by A2,FINSEQ_5:60;
       len Incr X_axis ff = card rng X_axis ff by GOBOARD2:def 2
                .= card rng X_axis f by A2,FINSEQ_5:60;
then A4: Incr X_axis f = Incr X_axis ff by A3,GOBOARD2:def 2;
A5: Rev Y_axis f = Y_axis ff by A1,Th11;
A6: rng Incr Y_axis ff = rng Y_axis ff by GOBOARD2:def 2
               .= rng Y_axis f by A5,FINSEQ_5:60;
       len Incr Y_axis ff = card rng Y_axis ff by GOBOARD2:def 2
               .= card rng Y_axis f by A5,FINSEQ_5:60;
   then Incr Y_axis f = Incr Y_axis ff by A6,GOBOARD2:def 2;
  hence GoB ff = GoB(Incr X_axis f,Incr Y_axis f) by A4,GOBOARD2:def 3
       .= GoB f by GOBOARD2:def 3;
 end;

registration
 cluster non constant FinSequence;
 existence
  proof take <*1,2*>,1,2;
    1 in {1,2} & 2 in {1,2} by TARSKI:def 2;
   hence 1 in dom <*1,2*> & 2 in dom <*1,2*> by FINSEQ_1:4,FINSEQ_3:29;
    <*1,2*>.1 = 1 & <*1,2*>.2 = 2 by FINSEQ_1:61;
   hence thesis;
  end;
end;

registration let f be non constant FinSequence;
 cluster Rev f -> non constant;
 coherence
 proof
  consider i,j such that
A1: i in dom f & j in dom f and
A2: f.i <> f.j by GOBOARD1:def 2;
       i <= len f & j <= len f by A1,FINSEQ_3:27;
   then reconsider k1 = len f -i, l1 = len f -j as Element of NAT by INT_1:18;
  take k = k1+1, l = l1+1;
       k + i = len f +1;
  hence k in dom Rev f by A1,FINSEQ_5:62;
   then A3: (Rev f).k = f.(len f -k +1) by FINSEQ_5:def 3
           .= f.(0 + i);
       l + j = len f +1;
  hence l in dom Rev f by A1,FINSEQ_5:62;
   then (Rev f).l = f.(len f -l +1) by FINSEQ_5:def 3
           .= f.(0 + j);
  hence thesis by A2,A3;
 end;
end;

definition let f be standard special_circular_sequence;
 redefine func Rev f -> standard special_circular_sequence;
 coherence
  proof
    reconsider ff = Rev f as non empty FinSequence of TOP-REAL 2;
A1:  Rev ff = f by FINSEQ_6:29;
A2:  GoB ff = GoB f by Lm1;
A3:  f is_sequence_on GoB f by GOBOARD5:def 5;
A4:  len f = len ff by FINSEQ_5:def 3;
A5:  ff is standard
     proof
      hereby let k such that
A6:     k in dom ff;
       set l = len f + 1 -' k;
           k <= len f & len f < len f + 1 by A4,A6,FINSEQ_3:27,NAT_1:13;
       then k <= len f +1 by XXREAL_0:2;
then A7:    l + k = len f + 1 by BINARITH:53;
       then l in dom f by A1,A4,A6,FINSEQ_5:62;
       then consider i,j such that
A8:     [i,j] in Indices GoB f and
A9:     f/.l = (GoB f)*(i,j) by A3,GOBOARD1:def 11;
       take i,j;
       thus [i,j] in Indices GoB ff by A8,Lm1;
       thus ff/.k = (GoB ff)*(i,j) by A1,A2,A4,A6,A7,A9,FINSEQ_5:69;
      end;
      let k such that
A10:    k in dom ff and
A11:    k+1 in dom ff;
       set l = len f + 1 -' (k + 1);
           k <= len f by A4,A10,FINSEQ_3:27;
       then k+1 <= len f +1 by XREAL_1:8;
then A12:    l + (k+1) = len f + 1 by BINARITH:53;
       then A13:     l in dom f by A1,A4,A11,FINSEQ_5:62;
A14:    l+1 + k = len f + 1 by A12;
then A15:    l+1 in dom f by A1,A4,A10,FINSEQ_5:62;
      let i1,j1,i2,j2 such that
A16:    [i1,j1] in Indices GoB ff and
A17:    [i2,j2] in Indices GoB ff and
A18:    ff/.k = (GoB ff)*(i1,j1) and
A19:    ff/.(k+1) = (GoB ff)*(i2,j2);
A20:    abs(i2-i1) = abs(-(i1-i2))
           .= abs(i1-i2) by COMPLEX1:138;
A21:    abs(j2-j1) = abs(-(j1-j2))
           .= abs(j1-j2) by COMPLEX1:138;
A22:    f/.l = (GoB f)*(i2,j2) by A1,A2,A4,A11,A12,A19,FINSEQ_5:69;
           f/.(l+1) = (GoB f)*(i1,j1) by A1,A2,A4,A10,A14,A18,FINSEQ_5:69;
      hence abs(i1-i2)+abs(j1-j2) = 1
         by A2,A3,A13,A15,A16,A17,A20,A21,A22,GOBOARD1:def 11;
     end;
A23:  ff is circular
     proof
      thus ff/.1 = f/.len f by FINSEQ_5:68
         .= f/.1 by FINSEQ_6:def 1
         .= ff/.len ff by A4,FINSEQ_5:68;
     end;
      ff is s.c.c.
     proof let i,j such that
A24:   i+1 < j and
A25:   i > 1 & j < len ff or j+1 < len ff;
       set k = len f -' i, l = len f -' j;
A26:   j <= len f by A4,A25,NAT_1:13;
then A27:   l + j = len f by BINARITH:53;
        i < j by A24,NAT_1:13;
       then i <= len f by A26,XXREAL_0:2;
then A28:   k + i = len f by BINARITH:53;
       then i+1+k = l+1+j by A27;
       then l+1+j < j+k by A24,XREAL_1:8;
       then A29:     l+1 < k by XREAL_1:8;
A30:   j+1 <= len f by A4,A25,NAT_1:13;
       per cases by A30,NAT_1:14,XXREAL_0:1;
       suppose j+1 = len f;
        then k+1 < len f by A25,A28,FINSEQ_5:def 3,XREAL_1:8;
       then LSeg(f,k) misses LSeg(f,l) by A29,GOBOARD5:def 4;
then A31:     LSeg(f,k) /\ LSeg(f,l) = {} by XBOOLE_0:def 7;
        LSeg(f,k) = LSeg(ff,i) by A28,SPPOL_2:2;
      hence LSeg(ff,i) /\ LSeg(ff,j) = {} by A27,A31,SPPOL_2:2;
       end;
       suppose that
A32:   i >= 1 and
A33:   j+1 < len f;
A34:   l > 1 by A27,A33,XREAL_1:8;
        k+1 <= len f by A28,A32,XREAL_1:8;
       then k < len f by NAT_1:13;
       then LSeg(f,k) misses LSeg(f,l) by A29,A34,GOBOARD5:def 4;
then A35:    LSeg(f,k) /\ LSeg(f,l) = {} by XBOOLE_0:def 7;
       LSeg(f,k) = LSeg(ff,i) by A28,SPPOL_2:2;
      hence LSeg(ff,i) /\ LSeg(ff,j) = {} by A27,A35,SPPOL_2:2;
      end;
      suppose i = 0;
       then LSeg(ff,i) = {} by TOPREAL1:def 5;
      hence LSeg(ff,i) /\ LSeg(ff,j) = {};
     end;
     end;
   hence thesis by A5,A23,SPPOL_2:29,42;
  end;
end;

theorem Th12:
  i >= 1 & j >= 1 & i+j = len f implies left_cell(f,i) = right_cell(Rev f,j)
proof assume that
A1: i >= 1 and
A2: j >= 1 and
A3: i+j = len f;
A4: i+1 <= len f by A2,A3,XREAL_1:8;
     len f = len Rev f by FINSEQ_5:def 3;
then A5: j+1 <= len Rev f by A1,A3,XREAL_1:8;
A6: GoB Rev f = GoB f by Lm1;
     now let i1,j1,i2,j2 be Element of NAT such that
A7: [i1,j1] in Indices GoB f and
A8: [i2,j2] in Indices GoB f and
A9: f/.i = (GoB f)*(i1,j1) and
A10: f/.(i+1) = (GoB f)*(i2,j2);
        1 <= i+1 by NAT_1:11;
then A11: i+1 in dom f by A4,FINSEQ_3:27;
        i+1+j = len f + 1 by A3;
then A12: (Rev f)/.j = (GoB f)*(i2,j2) by A10,A11,FINSEQ_5:69;
        i <= len f by A4,NAT_1:13;
then A13: i in dom f by A1,FINSEQ_3:27;
        j+1+i = len f + 1 by A3;
then A14: (Rev f)/.(j+1) = (GoB f)*(i1,j1) by A9,A13,FINSEQ_5:69;
        1 <= i1 by A7,GOBOARD5:1;
then A15: i1-'1+1 = i1 by BINARITH:53;
        1 <= j1 by A7,GOBOARD5:1;
then A16: j1-'1+1 = j1 by BINARITH:53;
   reconsider i1'=i1, i2'=i2, j1'=j1, j2'=j2 as Element of REAL
      by XREAL_0:def 1;
       f is_sequence_on GoB f by GOBOARD5:def 5;
   then abs(i1-i2)+abs(j1-j2) = 1 by A7,A8,A9,A10,A11,A13,GOBOARD1:def 11;
   then A17: abs(i1'-i2')=1 & j1=j2 or abs(j1'-j2')=1 & i1=i2 by GOBOARD1:2;
   per cases by A17,GOBOARD1:1;
    case i1 = i2 & j1+1 = j2;
    hence right_cell(Rev f,j) = cell(GoB f,i1-'1,j1)
                        by A2,A5,A6,A7,A8,A12,A14,A15,GOBOARD5:31;
    end;
    case i1+1 = i2 & j1 = j2;
    hence right_cell(Rev f,j) = cell(GoB f,i1,j1)
                        by A2,A5,A6,A7,A8,A12,A14,A16,GOBOARD5:30;
    end;
    case i1 = i2+1 & j1 = j2;
    hence right_cell(Rev f,j) = cell(GoB f,i2,j2-'1)
                        by A2,A5,A6,A7,A8,A12,A14,A16,GOBOARD5:29;
    end;
    case i1 = i2 & j1 = j2+1;
    hence right_cell(Rev f,j) = cell(GoB f,i1,j2)
                        by A2,A5,A6,A7,A8,A12,A14,A15,GOBOARD5:28;
  end;
  end;
 hence left_cell(f,i) = right_cell(Rev f,j) by A1,A4,GOBOARD5:def 7;
end;

theorem Th13:
  i >= 1 & j >= 1 & i+j = len f implies left_cell(Rev f,i) = right_cell(f,j)
proof
   len Rev f = len f & Rev Rev f = f by FINSEQ_5:def 3,FINSEQ_6:29;
  hence thesis by Th12;
end;

theorem Th14:
  1 <= k & k+1 <= len f implies
  ex i,j st i <= len GoB f & j <= width GoB f &
    cell(GoB f,i,j) = left_cell(f,k)
proof assume that
A1: 1 <= k and
A2: k+1 <= len f;
A3: f is_sequence_on GoB f by GOBOARD5:def 5;
     k <= len f by A2,NAT_1:13;
then A4: k in dom f by A1,FINSEQ_3:27;
 then consider i1,j1 being Element of NAT such that
A5: [i1,j1] in Indices GoB f and
A6: f/.k = (GoB f)*(i1,j1) by A3,GOBOARD1:def 11;
       1 <= k+1 by NAT_1:11;
then A7: k+1 in dom f by A2,FINSEQ_3:27;
 then consider i2,j2 being Element of NAT such that
A8: [i2,j2] in Indices GoB f and
A9: f/.(k+1) = (GoB f)*(i2,j2) by A3,GOBOARD1:def 11;
        1 <= i1 by A5,GOBOARD5:1;
then A10: i1-'1+1 = i1 by BINARITH:53;
        1 <= j1 by A5,GOBOARD5:1;
then A11: j1-'1+1 = j1 by BINARITH:53;
   reconsider i1'=i1, i2'=i2, j1'=j1, j2'=j2 as Element of REAL
      by XREAL_0:def 1;
       abs(i1-i2)+abs(j1-j2) = 1 by A3,A4,A5,A6,A7,A8,A9,GOBOARD1:def 11;
   then A12: abs(i1'-i2')=1 & j1=j2 or abs(j1'-j2')=1 & i1=i2 by GOBOARD1:2;
A13: i1 <= len GoB f by A5,GOBOARD5:1;
A14: j1 <= width GoB f by A5,GOBOARD5:1;
  per cases by A12,GOBOARD1:1;
  suppose
A15:  i1 = i2 & j1+1 = j2;
   take i1-'1,j1;
        i1-'1 <= i1 by BINARITH:52;
   hence i1-'1 <= len GoB f by A13,XXREAL_0:2;
   thus j1 <= width GoB f by A5,GOBOARD5:1;
   thus left_cell(f,k) = cell(GoB f,i1-'1,j1)
                        by A1,A2,A5,A6,A8,A9,A10,A15,GOBOARD5:28;
  end;
  suppose
A16:  i1+1 = i2 & j1 = j2;
   take i1,j1;
   thus i1 <= len GoB f by A5,GOBOARD5:1;
   thus j1 <= width GoB f by A5,GOBOARD5:1;
   thus left_cell(f,k) = cell(GoB f,i1,j1)
                        by A1,A2,A5,A6,A8,A9,A11,A16,GOBOARD5:29;
  end;
  suppose
A17: i1 = i2+1 & j1 = j2;
   take i2,j1-'1;
   thus i2 <= len GoB f by A8,GOBOARD5:1;
        j1-'1 <= j1 by BINARITH:52;
   hence j1-'1 <= width GoB f by A14,XXREAL_0:2;
   thus left_cell(f,k) = cell(GoB f,i2,j1-'1)
                        by A1,A2,A5,A6,A8,A9,A11,A17,GOBOARD5:30;
  end;
  suppose
A18: i1 = i2 & j1 = j2+1;
   take i1,j2;
   thus i1 <= len GoB f by A5,GOBOARD5:1;
   thus j2 <= width GoB f by A8,GOBOARD5:1;
   thus left_cell(f,k) = cell(GoB f,i1,j2)
                        by A1,A2,A5,A6,A8,A9,A10,A18,GOBOARD5:31;
end;
end;

theorem Th15:
  j <= width G implies Int h_strip(G,j) is convex
proof assume
A1: j <= width G;
 per cases by A1,NAT_1:14,XXREAL_0:1;
 suppose j = 0;
  then Int h_strip(G,j) = { |[r,s]| : s < G*(1,1)`2 } by GOBOARD6:18;
 hence Int h_strip(G,j) is convex by JORDAN1:22;
 end;
 suppose j = width G;
  then Int h_strip(G,j) = { |[r,s]| : G*(1,width G)`2 < s } by GOBOARD6:19;
 hence Int h_strip(G,j) is convex by JORDAN1:20;
 end;
 suppose 1 <= j & j < width G;
  then A2: Int h_strip(G,j) = { |[r,s]| : G*(1,j)`2 < s & s < G*(1,j+1)`2 }
                     by GOBOARD6:20;
A3: { |[r,s]| : G*(1,j)`2 < s} c= the carrier of TOP-REAL 2
     proof let x be set;
      assume x in { |[r,s]| : G*(1,j)`2 < s};
       then ex r,s st x = |[r,s]| & G*(1,j)`2 < s;
      hence thesis;
     end;
       { |[r,s]| : s < G*(1,j+1)`2 } c= the carrier of TOP-REAL 2
     proof let x be set;
      assume x in { |[r,s]| : s < G*(1,j+1)`2 };
       then ex r,s st x = |[r,s]| & s < G*(1,j+1)`2;
      hence thesis;
     end;
   then reconsider P = { |[r,s]| : G*(1,j)`2 < s},
              Q = { |[r,s]| : s < G*(1,j+1)`2 }
              as Subset of TOP-REAL 2 by A3;
A4: Int h_strip(G,j) = P /\ Q
   proof
    hereby let x be set;
     assume x in Int h_strip(G,j);
      then ex r,s st x = |[r,s]| & G*(1,j)`2 < s & s < G*(1,j+1)`2 by A2;
      then x in P & x in Q;
     hence x in P /\ Q by XBOOLE_0:def 3;
    end;
    let x be set;
    assume
A5:   x in P /\ Q;
     then x in P by XBOOLE_0:def 3;
     then consider r1,s1 such that
A6:   x = |[r1,s1]| and
A7:   G*(1,j)`2 < s1;
         x in Q by A5,XBOOLE_0:def 3;
     then consider r2,s2 such that
A8:   x = |[r2,s2]| and
A9:   s2 < G*(1,j+1)`2;
         r1 = r2 & s1 = s2 by A6,A8,SPPOL_2:1;
    hence x in Int h_strip(G,j) by A2,A6,A7,A9;
   end;
A10: P is convex by JORDAN1:20;
       Q is convex by JORDAN1:22;
 hence Int h_strip(G,j) is convex by A4,A10,Th9;
end;
end;

theorem Th16:
  i <= len G implies Int v_strip(G,i) is convex
proof assume
A1: i <= len G;
 per cases by A1,NAT_1:14,XXREAL_0:1;
 suppose i = 0;
  then Int v_strip(G,i) = { |[r,s]| : r < G*(1,1)`1 } by GOBOARD6:15;
 hence Int v_strip(G,i) is convex by JORDAN1:18;
 end;
 suppose i = len G;
  then Int v_strip(G,i) = { |[r,s]| : G*(len G,1)`1 < r } by GOBOARD6:16;
 hence Int v_strip(G,i) is convex by JORDAN1:16;
 end;
 suppose 1 <= i & i < len G;
  then A2: Int v_strip(G,i) = { |[r,s]| : G*(i,1)`1 < r & r < G*(i+1,1)`1 }
                   by GOBOARD6:17;
A3: { |[r,s]| : G*(i,1)`1 < r} c= the carrier of TOP-REAL 2
     proof let x be set;
      assume x in { |[r,s]| : G*(i,1)`1 < r};
       then ex r,s st x = |[r,s]| & G*(i,1)`1 < r;
      hence thesis;
     end;
       { |[r,s]| : r < G*(i+1,1)`1 } c= the carrier of TOP-REAL 2
     proof let x be set;
      assume x in { |[r,s]| : r < G*(i+1,1)`1 };
       then ex r,s st x = |[r,s]| & r < G*(i+1,1)`1;
      hence thesis;
     end;
   then reconsider P = { |[r,s]| : G*(i,1)`1 < r},
              Q = { |[r,s]| : r < G*(i+1,1)`1 }
          as Subset of TOP-REAL 2 by A3;
A4: Int v_strip(G,i) = P /\ Q
   proof
    hereby let x be set;
     assume x in Int v_strip(G,i);
      then ex r,s st x = |[r,s]| & G*(i,1)`1 < r & r < G*(i+1,1)`1 by A2;
      then x in P & x in Q;
     hence x in P /\ Q by XBOOLE_0:def 3;
    end;
    let x be set;
    assume
A5:   x in P /\ Q;
     then x in P by XBOOLE_0:def 3;
     then consider r1,s1 such that
A6:   x = |[r1,s1]| and
A7:   G*(i,1)`1 < r1;
         x in Q by A5,XBOOLE_0:def 3;
     then consider r2,s2 such that
A8:   x = |[r2,s2]| and
A9:   r2 < G*(i+1,1)`1;
         r1 = r2 & s1 = s2 by A6,A8,SPPOL_2:1;
    hence x in Int v_strip(G,i) by A2,A6,A7,A9;
   end;
A10: P is convex by JORDAN1:16;
       Q is convex by JORDAN1:18;
 hence Int v_strip(G,i) is convex by A4,A10,Th9;
end;
end;

theorem Th17:
  i <= len G & j <= width G implies Int cell(G,i,j) <> {}
proof assume
A1: i <= len G & j <= width G;
   then A2: j = width G or j < width G by XXREAL_0:1;
   A3: i = len G or i < len G by A1,XXREAL_0:1;
  consider p being Point of TOP-REAL 2;
 per cases by A2,A3,NAT_1:13,14;
 suppose 1 <= i & i+1 <= len G & 1 <= j & j+1 <= width G;
  then LSeg(1/2*(G*(i,j)+G*
(i+1,j+1)),p) meets Int cell(G,i,j) by GOBOARD6:85;
 hence Int cell(G,i,j) <> {} by XBOOLE_1:65;
 end;
 suppose 1 <= i & i+1 <= len G & j = width G;
  then LSeg(p,1/2*(G*(i,j)+G*(i+1,j))+|[0,1]|) meets Int cell(G,i,j)
          by GOBOARD6:86;
 hence Int cell(G,i,j) <> {} by XBOOLE_1:65;
 end;
 suppose 1 <= i & i+1 <= len G & j = 0;
  then LSeg(1/2*(G*(i,j+1)+G*(i+1,j+1))- |[0,1]|,p) meets Int cell(G,i,j)
                by GOBOARD6:87;
 hence Int cell(G,i,j) <> {} by XBOOLE_1:65;
 end;
 suppose 1 <= j & j+1 <= width G & i = 0;
  then LSeg(1/2*(G*(i+1,j)+G*(i+1,j+1))- |[1,0]|,p) meets Int cell(G,i,j)
                by GOBOARD6:88;
 hence Int cell(G,i,j) <> {} by XBOOLE_1:65;
 end;
 suppose 1 <= j & j+1 <= width G & i = len G;
  then LSeg(p,1/2*(G*(i,j)+G*(i,j+1))+|[1,0]|) meets Int cell(G,i,j)
                by GOBOARD6:89;
 hence Int cell(G,i,j) <> {} by XBOOLE_1:65;
 end;
 suppose i = 0 & j = 0;
  then LSeg(p,G*(i+1,j+1)- |[1,1]|) meets Int cell(G,i,j) by GOBOARD6:90;
 hence Int cell(G,i,j) <> {} by XBOOLE_1:65;
 end;
 suppose i = len G & j = width G;
  then LSeg(p,G*(i,j)+|[1,1]|) meets Int cell(G,i,j) by GOBOARD6:91;
 hence Int cell(G,i,j) <> {} by XBOOLE_1:65;
 end;
 suppose i = 0 & j = width G;
  then LSeg(p,G*(i+1,j)+|[-1,1]|) meets Int cell(G,i,j) by GOBOARD6:92;
 hence Int cell(G,i,j) <> {} by XBOOLE_1:65;
 end;
 suppose i = len G & j = 0;
  then LSeg(p,G*(i,j+1)+|[1,-1]|) meets Int cell(G,i,j) by GOBOARD6:93;
 hence Int cell(G,i,j) <> {} by XBOOLE_1:65;
end;
end;

theorem Th18:
  1 <= k & k+1 <= len f implies Int left_cell(f,k) <> {}
proof assume 1 <= k & k+1 <= len f;
  then ex i,j st i <= len GoB f & j <=
 width GoB f & cell(GoB f,i,j) = left_cell(f,k)
                  by Th14;
 hence thesis by Th17;
end;

theorem Th19:
  1 <= k & k+1 <= len f implies Int right_cell(f,k) <> {}
proof assume that
A1: 1 <= k and
A2: k+1 <= len f;
A3: len f = len Rev f by FINSEQ_5:def 3;
       k <= len f by A2,NAT_1:13;
then A4: len f-'k+k = len f by BINARITH:53;
then A5:  len f-'k+1 <= len f by A1,XREAL_1:8;
A6: len f-'k >= 1 by A2,A4,XREAL_1:8;
  then right_cell(f,k) = left_cell(Rev f,len f-'k) by A1,A4,Th13;
 hence Int right_cell(f,k) <> {} by A3,A5,A6,Th18;
end;

theorem Th20:
 i <= len G & j <= width G implies Int cell(G,i,j) is convex
proof assume
A1: i <= len G & j <= width G;
  set P = Int cell(G,i,j);
A2: P = Int v_strip(G,i) /\ Int h_strip(G,j) by TOPS_1:46;
      Int v_strip(G,i) is convex & Int h_strip(G,j) is convex by A1,Th15,Th16;
 hence P is convex by A2,Th9;
end;

theorem Th21:
  i <= len G & j <= width G implies Int cell(G,i,j) is connected
proof assume
A1: i <= len G & j <= width G;
  then reconsider P = Int cell(G,i,j)
   as non empty Subset of TOP-REAL 2 by Th17;
      P is convex by A1,Th20;
 hence Int cell(G,i,j) is connected by JORDAN1:9;
end;

theorem Th22:
  1 <= k & k+1 <= len f implies Int left_cell(f,k) is connected
proof assume 1 <= k & k+1 <= len f;
  then ex i,j st i <= len GoB f & j <=
 width GoB f & cell(GoB f,i,j) = left_cell(f,k)
                  by Th14;
 hence thesis by Th21;
end;

theorem Th23:
  1 <= k & k+1 <= len f implies Int right_cell(f,k) is connected
proof assume that
A1: 1 <= k and
A2: k+1 <= len f;
A3: len f = len Rev f by FINSEQ_5:def 3;
       k <= len f by A2,NAT_1:13;
then A4: len f-'k+k = len f by BINARITH:53;
then A5:  len f-'k+1 <= len f by A1,XREAL_1:8;
A6: len f-'k >= 1 by A2,A4,XREAL_1:8;
  then right_cell(f,k) = left_cell(Rev f,len f-'k) by A1,A4,Th13;
 hence Int right_cell(f,k) is connected by A3,A5,A6,Th22;
end;

definition let f;
   4 < len f by GOBOARD7:36;
then A1:  1+1 < len f by XXREAL_0:2;
then A2: Int left_cell(f,1) <> {} by Th18;
A3: Int right_cell(f,1) <> {} by A1,Th19;
 func LeftComp f -> Subset of TOP-REAL 2 means
:Def1: it is_a_component_of (L~f)` & Int left_cell(f,1) c= it;
 existence
  proof
A4: Int left_cell(f,1) is connected by A1,Th22;
        Int left_cell(f,1) misses L~f by A1,GOBOARD8:37;
    then A5:   Int left_cell(f,1) c= (L~f)` by SUBSET_1:43;
    then (L~f)` <> {} by A2,XBOOLE_1:3;
   hence thesis by A2,A4,A5,Th5;
  end;
 uniqueness
  proof let P1,P2 be Subset of TOP-REAL 2 such that
A6: P1 is_a_component_of (L~f)` and
A7: Int left_cell(f,1) c= P1 and
A8: P2 is_a_component_of (L~f)` and
A9: Int left_cell(f,1) c= P2;
        P1 meets P2 by A2,A7,A9,XBOOLE_1:67;
   hence P1 = P2 by A6,A8,Th3;
  end;
 func RightComp f -> Subset of TOP-REAL 2 means
:Def2:  it is_a_component_of (L~f)` & Int right_cell(f,1) c= it;
 existence
  proof
A10: Int right_cell(f,1) is connected by A1,Th23;
        Int right_cell(f,1) misses L~f by A1,GOBOARD8:38;
    then A11:   Int right_cell(f,1) c= (L~f)` by SUBSET_1:43;
    then (L~f)` <> {} by A3,XBOOLE_1:3;
   hence thesis by A3,A10,A11,Th5;
  end;
 uniqueness
  proof let P1,P2 be Subset of TOP-REAL 2 such that
A12: P1 is_a_component_of (L~f)` and
A13: Int right_cell(f,1) c= P1 and
A14: P2 is_a_component_of (L~f)` and
A15: Int right_cell(f,1) c= P2;
        P1 meets P2 by A3,A13,A15,XBOOLE_1:67;
   hence P1 = P2 by A12,A14,Th3;
  end;
end;

theorem Th24:
 for k st 1 <= k & k+1 <= len f holds Int left_cell(f,k) c= LeftComp f
proof
  defpred P[Element of NAT] means
  1 <= $1 & $1+1 <= len f implies Int left_cell(f,$1) c= LeftComp f;
A1: P[0];
A2: now let k such that
A3: P[k];
    thus P[k+1]
    proof
    assume that
A4: 1 <= k+1 and
A5: k+1+1 <= len f;
    per cases by NAT_1:14;
    suppose k = 0;
    hence Int left_cell(f,k+1) c= LeftComp f by Def1;
    end;
    suppose
A6:   k >= 1;
A7:  k+1 <= len f by A5,NAT_1:13;
A8:  k <= k+1 by NAT_1:11;
  then k <= len f by A7,XXREAL_0:2;
then A9:  k in dom f by A6,FINSEQ_3:27;
     then consider i0,j0 being Element of NAT such that
A10:  [i0,j0] in Indices GoB f and
A11:  f/.k = (GoB f)*(i0,j0) by GOBOARD2:25;
A12:  k+1 in dom f by A4,A7,FINSEQ_3:27;
     then consider i1,j1 being Element of NAT such that
A13:  [i1,j1] in Indices GoB f and
A14:  f/.(k+1) = (GoB f)*(i1,j1) by GOBOARD2:25;
         k+1+1 >= 1 by NAT_1:11;
then A15:  k+1+1 in dom f by A5,FINSEQ_3:27;
     then consider i2,j2 being Element of NAT such that
A16:  [i2,j2] in Indices GoB f and
A17:  f/.(k+1+1) = (GoB f)*(i2,j2) by GOBOARD2:25;
A18: 1 <= i0 & i0 <= len GoB f by A10,GOBOARD5:1;
A19: 1 <= i1 & i1 <= len GoB f by A13,GOBOARD5:1;
A20: 1 <= i2 & i2 <= len GoB f by A16,GOBOARD5:1;
A21: 1 <= j0 & j0 <= width GoB f by A10,GOBOARD5:1;
A22: 1 <= j1 & j1 <= width GoB f by A13,GOBOARD5:1;
A23: 1 <= j2 & j2 <= width GoB f by A16,GOBOARD5:1;
A24: i0 = i1 or j0 = j1 by A9,A10,A11,A12,A13,A14,GOBOARD2:16;
A25: i1 = i2 or j1 = j2 by A12,A13,A14,A15,A16,A17,GOBOARD2:16;
   reconsider i1'=i1, i0'=i0, i2'=i2,  j1'=j1, j0'=j0, j2'=j2
          as Element of REAL by XREAL_0:def 1;
A26:  f is_sequence_on GoB f by GOBOARD5:def 5;
    then abs(i0-i1)+abs(j0-j1) = 1 by A9,A10,A11,A12,A13,A14,GOBOARD1:def 11;
    then A27: abs(i0'-i1')=1 & j0 = j1 or abs(j0'-j1')=1 & i0=i1 by GOBOARD1:2;
    then A28: i0 = i1 or i0 = i1+1 or i0+1 = i1 by GOBOARD1:1;
      abs(i1-i2)+abs(j1-j2) = 1 by A12,A13,A14,A15,A16,A17,A26,GOBOARD1:def 11;
    then A29: abs(i1'-i2')=1 & j1 = j2 or abs(j1'-j2')=1 & i1=i2 by GOBOARD1:2;
    then A30: i1 = i2 or i1 = i2+1 or i1+1 = i2 by GOBOARD1:1;
A31: j0 = j1 or j0 = j1+1 or j0+1 = j1 by A27,GOBOARD1:1;
A32: j1 = j2 or j1 = j2+1 or j1+1 = j2 by A29,GOBOARD1:1;
A33:
   now assume A34: i0 = i2 & j0 = j2;
A35:  now assume k <= 1;
then A36:    k = 1 by A6,XXREAL_0:1;
      assume k+1+1 >= len f;
       then k+1+1 = len f by A5,XXREAL_0:1;
      hence contradiction by A36,GOBOARD7:36;
     end;
A37: k < k+1+1 by A8,NAT_1:13;
    per cases by A35;
    suppose k > 1;
    hence contradiction by A5,A11,A17,A34,A37,GOBOARD7:39;
    end;
    suppose k+1+1 < len f;
    hence contradiction by A6,A11,A17,A34,A37,GOBOARD7:38;
   end;
   end;
       i1 >= 1 by A13,GOBOARD5:1;
then A38: i1 = i1 -' 1 + 1 by BINARITH:53;
       j1 >= 1 by A13,GOBOARD5:1;
then A39: j1 = j1 -' 1 + 1 by BINARITH:53;
     then j1 -' 1 <= j1 by NAT_1:11;
then A40: j1 -' 1 <= width GoB f by A22,XXREAL_0:2;
        len GoB f > 1 by GOBOARD7:34;
then A41: len GoB f -'1 + 1 = len GoB f by BINARITH:53;
        width GoB f >= 1 by GOBOARD7:35;
then A42: width GoB f -'1 + 1 = width GoB f by BINARITH:53;
A43: k+1+1 = k+(1+1);
A44: i0+1+1 = i0+(1+1);
A45: i2+1+1 = i2+(1+1);
A46: j0+1+1 = j0+(1+1);
A47: j2+1+1 = j2+(1+1);
A48:  LeftComp f is_a_component_of (L~f)` by Def1;
A49: Int left_cell(f,k+1) is connected by A4,A5,Th22;
A50: 0+1 = 1;
       now per cases by A9,A11,A12,A14,A15,A17,A28,A30,A31,A32,A33,GOBOARD7:31
;
    suppose that
A51: i0 = i1+1 and
A52: i1 = i2+1 and
A53: j0 = 1;
A54: left_cell(f,k) = cell(GoB f,i1,0)
                by A6,A7,A10,A11,A13,A14,A24,A50,A51,A53,GOBOARD5:30;
     0+1 = j2 by A24,A25,A51,A52,A53;
then A55: left_cell(f,k+1) = cell(GoB f,i2,0)
                by A4,A5,A13,A14,A16,A17,A24,A51,A52,A53,GOBOARD5:30;
A56: LSeg(1/2*((GoB f)*(i1,1)+(GoB f)*(i0,1))- |[0,1]|,
          1/2*((GoB f)*(i2,1)+(GoB f)*(i1,1))- |[0,1]|) is connected by Th8;
A57: LSeg(1/2*((GoB f)*(i1,1)+(GoB f)*(i0,1))- |[0,1]|,
          1/2*((GoB f)*(i2,1)+(GoB f)*(i1,1))- |[0,1]|)
           meets Int cell(GoB f,i1,0) by A18,A19,A51,GOBOARD6:87;
         LSeg(1/2*((GoB f)*(i1,1)+(GoB f)*(i0,1))- |[0,1]|,
          1/2*((GoB f)*(i2,1)+(GoB f)*(i1,1))- |[0,1]|) misses L~f
         by A18,A20,A45,A51,A52,GOBOARD8:25;
     then LSeg(1/2*((GoB f)*(i1,1)+(GoB f)*(i0,1))- |[0,1]|,
               1/2*((GoB f)*(i2,1)+(GoB f)*(i1,1))- |[0,1]|) c= (L~f)`
           by SUBSET_1:43;
     then A58: LSeg(1/2*((GoB f)*(i1,1)+(GoB f)*(i0,1))- |[0,1]|,
          1/2*((GoB f)*(i2,1)+(GoB f)*(i1,1))- |[0,1]|) c= LeftComp f
                             by A3,A5,A6,A48,A54,A56,A57,Th6,NAT_1:13;
A59: LSeg(1/2*((GoB f)*(i1,1)+(GoB f)*(i0,1))- |[0,1]|,
          1/2*((GoB f)*(i2,1)+(GoB f)*(i1,1))- |[0,1]|) meets
             Int cell(GoB f,i2,0) by A19,A20,A52,GOBOARD6:87;
         Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
     then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
    hence Int left_cell(f,k+1) c= LeftComp f by A48,A49,A55,A58,A59,Th6;
    end;
    suppose that
A60: i0 = i1+1 and
A61: i1 = i2+1 and
A62: j0 <> 1;
A63: left_cell(f,k) = cell(GoB f,i1,j1-'1)
                by A6,A7,A10,A11,A13,A14,A24,A39,A60,GOBOARD5:30;
        1 < j0 by A21,A62,XXREAL_0:1;
then A64: 1 <= j0-'1 by A24,A39,A60,NAT_1:13;
A65: left_cell(f,k+1) = cell(GoB f,i2,j1-'1)
                by A4,A5,A13,A14,A16,A17,A25,A39,A61,GOBOARD5:30;
A66: LSeg(1/2*((GoB f)*(i1,j0-'1)+(GoB f)*(i0,j0)),
          1/2*((GoB f)*(i2,j0-'1)+(GoB f)*(i1,j0))) is connected by Th8;
A67: LSeg(1/2*((GoB f)*(i1,j0-'1)+(GoB f)*(i0,j0)),
          1/2*((GoB f)*(i2,j0-'1)+(GoB f)*(i1,j0)))
           meets Int cell(GoB f,i1,j1-'1)
             by A18,A19,A22,A24,A39,A60,A64,GOBOARD6:85;
         LSeg(1/2*((GoB f)*(i1,j0-'1)+(GoB f)*(i0,j0)),
          1/2*((GoB f)*(i2,j0-'1)+(GoB f)*(i1,j0))) misses L~f
       by A5,A6,A11,A14,A17,A18,A20,A21,A24,A25,A39,A43,A45,A60,A61,A64,
GOBOARD8:11;
     then LSeg(1/2*((GoB f)*(i1,j0-'1)+(GoB f)*(i0,j0)),
          1/2*((GoB f)*(i2,j0-'1)+(GoB f)*(i1,j0))) c= (L~f)` by SUBSET_1:43;
     then A68: LSeg(1/2*((GoB f)*(i1,j0-'1)+(GoB f)*(i0,j0)),
          1/2*((GoB f)*(i2,j0-'1)+(GoB f)*(i1,j0))) c= LeftComp f
                             by A3,A5,A6,A48,A63,A66,A67,Th6,NAT_1:13;
A69: LSeg(1/2*((GoB f)*(i1,j0-'1)+(GoB f)*(i0,j0)),
          1/2*((GoB f)*(i2,j0-'1)+(GoB f)*(i2+1,j0))) meets
             Int cell(GoB f,i2,j0-'1) by A19,A20,A21,A24,A39,A60,A61,A64,
GOBOARD6:85;
         Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
     then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
    hence Int left_cell(f,k+1) c= LeftComp f
               by A24,A48,A49,A60,A61,A65,A68,A69,Th6;
    end;
    suppose that
A70: i0 = i1+1 and
A71: j1 = j2+1;
         left_cell(f,k) = cell(GoB f,i1,j2)
                by A6,A7,A10,A11,A13,A14,A24,A70,A71,GOBOARD5:30
        .= left_cell(f,k+1) by A4,A5,A13,A14,A16,A17,A25,A38,A71,GOBOARD5:31
;
    hence Int left_cell(f,k+1) c= LeftComp f by A3,A5,A6,NAT_1:13;
    end;
    suppose that
A72: j0 = j1+1 and
A73: i1 = i2+1 and
A74: i0 = len GoB f and
A75: j1 = 1;
A76: left_cell(f,k) = cell(GoB f,i0,j1)
                   by A6,A7,A10,A11,A13,A14,A24,A38,A72,GOBOARD5:31;
A77: LSeg((GoB f)*(len GoB f,1)+|[1,-1]|,
          1/2*((GoB f)*(len GoB f,1)+(GoB f)*(len GoB f,2))+|[1,0]|)
           is connected by Th8;
A78: LSeg((GoB f)*(len GoB f,1)+|[1,-1]|,
          1/2*((GoB f)*(len GoB f,1)+(GoB f)*(len GoB f,2))+|[1,0]|)
           meets Int cell(GoB f,i0,j1) by A21,A72,A74,A75,GOBOARD6:89;
         LSeg((GoB f)*(len GoB f,1)+|[1,-1]|,
          1/2*((GoB f)*(len GoB f,1)+(GoB f)*(len GoB f,2))+|[1,0]|)
          misses L~f by GOBOARD8:35;

     then LSeg((GoB f)*(len GoB f,1)+|[1,-1]|,
          1/2*((GoB f)*(len GoB f,1)+(GoB f)*(len GoB f,2))+|[1,0]|)
              c= (L~f)` by SUBSET_1:43;
     then A79: LSeg((GoB f)*(len GoB f,1)+|[1,-1]|,
          1/2*((GoB f)*(len GoB f,1)+(GoB f)*(len GoB f,2))+|[1,0]|)
            c= LeftComp f by A3,A5,A6,A48,A76,A77,A78,Th6,NAT_1:13;
A80: Int cell(GoB f,i1,0) is connected by A19,A22,Th21;
A81: LSeg((GoB f)*(len GoB f,1)+|[1,-1]|,
          1/2*((GoB f)*(len GoB f,1)+(GoB f)*(len GoB f,2))+|[1,0]|) meets
             Int cell(GoB f,i1,0) by A24,A72,A74,GOBOARD6:93;
         Int cell(GoB f,i1,0) misses L~f by A19,A22,GOBOARD7:14;
     then Int cell(GoB f,i1,0) c= (L~f)` by SUBSET_1:43;
     then A82:  Int cell(GoB f,i1,0) c= LeftComp f by A48,A79,A80,A81,Th6;
A83: left_cell(f,k+1) = cell(GoB f,i2,0)
                by A4,A5,A13,A14,A16,A17,A25,A39,A73,A75,GOBOARD5:30;
A84: LSeg(1/2*((GoB f)*(len GoB f -' 1,1)+(GoB f)*(len GoB f,1))- |[0,1]|,
         (GoB f)*(len GoB f,1)+|[1,-1]|) is connected by Th8;
A85: LSeg(1/2*((GoB f)*(len GoB f -' 1,1)+(GoB f)*(len GoB f,1))- |[0,1]|,
         (GoB f)*(len GoB f,1)+|[1,-1]|)
           meets Int cell(GoB f,i1,0) by A24,A72,A74,GOBOARD6:93;
        LSeg(1/2*((GoB f)*(len GoB f -' 1,1)+(GoB f)*(len GoB f,1))- |[0,1]|,
         (GoB f)*(len GoB f,1)+|[1,-1]|) misses L~f by GOBOARD8:27;
     then LSeg(1/2*((GoB f)*(len GoB f -' 1,1)+(GoB f)*
(len GoB f,1))- |[0,1]|,
         (GoB f)*(len GoB f,1)+|[1,-1]|) c= (L~f)` by SUBSET_1:43;
     then A86: LSeg(1/2*((GoB f)*(len GoB f -' 1,1)+(GoB f)*(len GoB f,1))- |[0
,1]|,
         (GoB f)*(len GoB f,1)+|[1,-1]|) c= LeftComp f
                             by A48,A82,A84,A85,Th6;
A87: LSeg(1/2*((GoB f)*(len GoB f -' 1,1)+(GoB f)*(len GoB f,1))- |[0,1]|,
         (GoB f)*(len GoB f,1)+|[1,-1]|) meets
             Int cell(GoB f,i2,0) by A20,A24,A38,A72,A73,A74,GOBOARD6:87;
         Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
     then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
    hence Int left_cell(f,k+1) c= LeftComp f
               by A48,A49,A83,A86,A87,Th6;
    end;
    suppose that
A88: j0 = j1+1 and
A89: i1 = i2+1 and
A90: i0 <> len GoB f and
A91: j1 = 1;
A92: left_cell(f,k) = cell(GoB f,i0,j1)
                   by A6,A7,A10,A11,A13,A14,A24,A38,A88,GOBOARD5:31;
        len GoB f > i0 by A18,A90,XXREAL_0:1;
then A93: len GoB f >= i0+1 by NAT_1:13;
A94: LSeg(1/2*((GoB f)*(i0,1)+(GoB f)*(i0+1,1))- |[0,1]|,
          1/2*((GoB f)*(i1,1)+(GoB f)*(i1+1,2))) is connected by Th8;
A95: LSeg(1/2*((GoB f)*(i0,1)+(GoB f)*(i0+1,1))- |[0,1]|,
          1/2*((GoB f)*(i1,1)+(GoB f)*(i1+1,1+1)))
           meets Int cell(GoB f,i0,j1)
             by A19,A21,A24,A88,A91,A93,GOBOARD6:85;
         LSeg(1/2*((GoB f)*(i0,1)+(GoB f)*(i0+1,1))- |[0,1]|,
          1/2*((GoB f)*(i1,1)+(GoB f)*(i1+1,2))) misses L~f
           by A5,A6,A11,A14,A17,A20,A24,A25,A43,A45,A88,A89,A91,A93,GOBOARD8:8
;
     then LSeg(1/2*((GoB f)*(i0,1)+(GoB f)*(i0+1,1))- |[0,1]|,
          1/2*((GoB f)*(i1,1)+(GoB f)*(i1+1,2))) c= (L~f)` by SUBSET_1:43;
     then A96: LSeg(1/2*((GoB f)*(i0,1)+(GoB f)*(i0+1,1))- |[0,1]|,
          1/2*((GoB f)*(i1,1)+(GoB f)*(i1+1,2))) c= LeftComp f
                             by A3,A5,A6,A48,A92,A94,A95,Th6,NAT_1:13;
A97: Int cell(GoB f,i1,0) is connected by A19,A22,Th21;
A98: LSeg(1/2*((GoB f)*(i0,1)+(GoB f)*(i0+1,1))- |[0,1]|,
          1/2*((GoB f)*(i1,1)+(GoB f)*(i1+1,2))) meets
             Int cell(GoB f,i1,0) by A19,A24,A88,A93,GOBOARD6:87;
         Int cell(GoB f,i1,0) misses L~f by A19,A22,GOBOARD7:14;
     then Int cell(GoB f,i1,0) c= (L~f)` by SUBSET_1:43;
     then A99:  Int cell(GoB f,i1,0) c= LeftComp f by A48,A96,A97,A98,Th6;
A100: left_cell(f,k+1) = cell(GoB f,i2,0)
                by A4,A5,A13,A14,A16,A17,A25,A39,A89,A91,GOBOARD5:30;
A101: LSeg(1/2*((GoB f)*(i2,1)+(GoB f)*(i2+1,1))- |[0,1]|,
   1/2*((GoB f)*(i2+1,1)+(GoB f)*(i2+2,1))- |[0,1]|) is connected by Th8;
A102: LSeg(1/2*((GoB f)*(i2,1)+(GoB f)*(i2+1,1))- |[0,1]|,
         1/2*((GoB f)*(i2+1,1)+(GoB f)*(i2+2,1))- |[0,1]|)
           meets Int cell(GoB f,i1,0) by A19,A24,A88,A89,A93,GOBOARD6:87
;
        LSeg(1/2*((GoB f)*(i2,1)+(GoB f)*(i2+1,1))- |[0,1]|,
         1/2*((GoB f)*(i2+1,1)+(GoB f)*(i2+2,1))- |[0,1]|) misses L~f
                          by A20,A24,A88,A89,A93,GOBOARD8:25;
     then LSeg(1/2*((GoB f)*(i2,1)+(GoB f)*(i2+1,1))- |[0,1]|,
               1/2*((GoB f)*(i2+1,1)+(GoB f)*(i2+2,1))- |[0,1]|) c= (L~f)`
          by SUBSET_1:43;
     then A103: LSeg(1/2*((GoB f)*(i2,1)+(GoB f)*(i2+1,1))- |[0,1]|,
         1/2*((GoB f)*(i2+1,1)+(GoB f)*(i2+2,1))- |[0,1]|) c= LeftComp f
                             by A48,A99,A101,A102,Th6;
A104: LSeg(1/2*((GoB f)*(i2,1)+(GoB f)*(i2+1,1))- |[0,1]|,
         1/2*((GoB f)*(i2+1,1)+(GoB f)*(i2+2,1))- |[0,1]|) meets
             Int cell(GoB f,i2,0) by A19,A20,A89,GOBOARD6:87;
         Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
     then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
    hence Int left_cell(f,k+1) c= LeftComp f
               by A48,A49,A100,A103,A104,Th6;
    end;
    suppose that
A105: j0 = j1+1 and
A106: i1 = i2+1 and
A107: i0 = len GoB f and
A108: j1 <> 1;
A109: left_cell(f,k) = cell(GoB f,i0,j1)
                   by A6,A7,A10,A11,A13,A14,A24,A38,A105,GOBOARD5:31;
        1 < j1 by A22,A108,XXREAL_0:1;
then A110: 1 <= j1-'1 by A39,NAT_1:13;
A111: j1+1 = j1-'1+(1+1) by A39;
A112: LSeg(1/2*((GoB f)*(len GoB f,j1-'1)+(GoB f)*(len GoB f,j1))+|[1,0]|,
         1/2*((GoB f)*(len GoB f,j1)+(GoB f)*(len GoB f,j1+1))+|[1,0]|)
             is connected by Th8;
A113: LSeg(1/2*((GoB f)*(len GoB f,j1-'1)+(GoB f)*(len GoB f,j1))+|[1,0]|,
          1/2*((GoB f)*(len GoB f,j1)+(GoB f)*(len GoB f,j1+1))+|[1,0]|)
           meets Int cell(GoB f,i0,j1) by A21,A22,A105,A107,GOBOARD6:89;
        LSeg(1/2*((GoB f)*(len GoB f,j1-'1)+(GoB f)*(len GoB f,j1))+|[1,0]|,
         1/2*((GoB f)*(len GoB f,j1)+(GoB f)*(len GoB f,j1+1))+|[1,0]|)
            misses L~f by A21,A39,A105,A110,A111,GOBOARD8:34;
     then LSeg(1/2*((GoB f)*(len GoB f,j1-'1)+(GoB f)*
(len GoB f,j1))+|[1,0]|,
               1/2*((GoB f)*(len GoB f,j1)+(GoB f)*(len GoB f,j1+1))+|[1,0]|)
             c= (L~f)` by SUBSET_1:43;
     then A114: LSeg(1/2*((GoB f)*(len GoB f,j1-'1)+(GoB f)*(len GoB f,j1))+|[1
,0]|,
          1/2*((GoB f)*(len GoB f,j1)+(GoB f)*(len GoB f,j1+1))+|[1,0]|)
              c= LeftComp f by A3,A5,A6,A48,A109,A112,A113,Th6,NAT_1:13;
A115: Int cell(GoB f,i1,j1-'1) is connected by A19,A40,Th21;
         j1 > 1 by A22,A108,XXREAL_0:1;
     then 1 <= j1-'1 by A39,NAT_1:13;
then A116: LSeg(1/2*((GoB f)*(len GoB f,j1-'1)+(GoB f)*(len GoB f,j1))+|[1,0]|,
          1/2*((GoB f)*(len GoB f,j1)+(GoB f)*
(len GoB f,j1+1))+|[1,0]|) meets
             Int cell(GoB f,i1,j1-'1) by A22,A24,A39,A105,A107,GOBOARD6:89
;
         Int cell(GoB f,i1,j1-'1) misses L~f by A19,A40,GOBOARD7:14;
     then Int cell(GoB f,i1,j1-'1) c= (L~f)` by SUBSET_1:43;
     then A117:  Int cell(GoB f,i1,j1-'1) c= LeftComp f by A48,A114,A115,A116,
Th6;
A118: left_cell(f,k+1) = cell(GoB f,i2,j1-'1)
                by A4,A5,A13,A14,A16,A17,A25,A39,A106,GOBOARD5:30;
A119: LSeg(1/2*((GoB f)*(i1 -' 1,j1-'1)+(GoB f)*(i1,j1)),
          1/2*((GoB f)*(i1,j1-'1)+(GoB f)*
(i1,j1))+|[1,0]|) is connected by Th8
;
A120: LSeg(1/2*((GoB f)*(i1 -' 1,j1-'1)+(GoB f)*(i1,j1)),
          1/2*((GoB f)*(i1,j1-'1)+(GoB f)*(i1,j1))+|[1,0]|)
           meets Int cell(GoB f,i1,j1-'1) by A22,A24,A39,A105,A107,A110,
GOBOARD6:89;
A121:  i1-'1 = i2 by A106,BINARITH:39;
     then LSeg(1/2*((GoB f)*(i1 -' 1,j1-'1)+(GoB f)*(i1,j1)),
          1/2*((GoB f)*(i1,j1-'1)+(GoB f)*(i1,j1))+|[1,0]|) misses L~f
         by A5,A6,A11,A14,A17,A21,A24,A25,A39,A43,A105,A106,A107,A110,A111,
GOBOARD8:19;
     then LSeg(1/2*((GoB f)*(i1 -' 1,j1-'1)+(GoB f)*(i1,j1)),
               1/2*((GoB f)*(i1,j1-'1)+(GoB f)*(i1,j1))+|[1,0]|) c= (L~f)`
           by SUBSET_1:43;
     then A122: LSeg(1/2*((GoB f)*(i1 -' 1,j1-'1)+(GoB f)*(i1,j1)),
          1/2*((GoB f)*(i1,j1-'1)+(GoB f)*(i1,j1))+|[1,0]|) c= LeftComp f
                             by A48,A117,A119,A120,Th6;
A123: LSeg(1/2*((GoB f)*(i1 -' 1,j1-'1)+(GoB f)*(i1,j1)),
          1/2*((GoB f)*(i1,j1-'1)+(GoB f)*(i1,j1))+|[1,0]|) meets
             Int cell(GoB f,i2,j1-'1) by A19,A20,A22,A39,A106,A110,A121,
GOBOARD6:85;
         Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
     then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
    hence Int left_cell(f,k+1) c= LeftComp f
               by A48,A49,A118,A122,A123,Th6;
    end;
    suppose that
A124: j0 = j1+1 and
A125: i1 = i2+1 and
A126: i0 <> len GoB f and
A127: j1 <> 1;
A128: left_cell(f,k) = cell(GoB f,i0,j1)
                   by A6,A7,A10,A11,A13,A14,A24,A38,A124,GOBOARD5:31;
        1 < j1 by A22,A127,XXREAL_0:1;
then A129: 1 <= j1-'1 by A39,NAT_1:13;
        len GoB f > i0 by A18,A126,XXREAL_0:1;
then A130: len GoB f >= i0+1 by NAT_1:13;
A131: j1+1 = j1-'1+(1+1) by A39;
A132: LSeg(1/2*((GoB f)*(i0,j1-'1)+(GoB f)*(i0+1,j1)),
          1/2*((GoB f)*(i1,j1)+(GoB f)*(i1+1,j1+1))) is connected by Th8;
A133: LSeg(1/2*((GoB f)*(i0,j1-'1)+(GoB f)*(i0+1,j1)),
          1/2*((GoB f)*(i1,j1)+(GoB f)*(i1+1,j1+1)))
           meets Int cell(GoB f,i0,j1) by A19,A21,A22,A24,A124,A130,GOBOARD6:85
;
         LSeg(1/2*((GoB f)*(i0,j1-'1)+(GoB f)*(i0+1,j1)),
          1/2*((GoB f)*(i1,j1)+(GoB f)*(i1+1,j1+1))) misses L~f
    by A5,A6,A11,A14,A17,A20,A21,A24,A25,A39,A43,A45,A124,A125,A129,A130,A131,
GOBOARD8:5;
     then LSeg(1/2*((GoB f)*(i0,j1-'1)+(GoB f)*(i0+1,j1)),
         1/2*((GoB f)*(i1,j1)+(GoB f)*(i1+1,j1+1))) c= (L~f)` by SUBSET_1:43;
     then A134: LSeg(1/2*((GoB f)*(i0,j1-'1)+(GoB f)*(i0+1,j1)),
          1/2*((GoB f)*(i1,j1)+(GoB f)*(i1+1,j1+1))) c= LeftComp f
                             by A3,A5,A6,A48,A128,A132,A133,Th6,NAT_1:13;
A135: Int cell(GoB f,i1,j1-'1) is connected by A19,A40,Th21;
         j1 > 1 by A22,A127,XXREAL_0:1;
     then 1 <= j1-'1 by A39,NAT_1:13;
then A136: LSeg(1/2*((GoB f)*(i1,j1-'1)+(GoB f)*(i1+1,j1)),
          1/2*((GoB f)*(i1,j1)+(GoB f)*(i1+1,j1+1))) meets
             Int cell(GoB f,i1,j1-'1) by A19,A22,A24,A39,A124,A130,GOBOARD6:85
;
         Int cell(GoB f,i1,j1-'1) misses L~f by A19,A40,GOBOARD7:14;
     then Int cell(GoB f,i1,j1-'1) c= (L~f)` by SUBSET_1:43;
     then A137:  Int cell(GoB f,i1,j1-'1) c= LeftComp f by A24,A48,A124,A134
,A135,A136,Th6;
A138: left_cell(f,k+1) = cell(GoB f,i2,j1-'1)
                by A4,A5,A13,A14,A16,A17,A25,A39,A125,GOBOARD5:30;
A139: LSeg(1/2*((GoB f)*(i1,j1-'1)+(GoB f)*(i1+1,j1)),
          1/2*((GoB f)*(i2,j1-'1)+(GoB f)*(i1,j1))) is connected by Th8;
A140: LSeg(1/2*((GoB f)*(i1,j1-'1)+(GoB f)*(i1+1,j1)),
          1/2*((GoB f)*(i2,j1-'1)+(GoB f)*(i1,j1)))
     meets Int cell(GoB f,i1,j1-'1) by A19,A22,A24,A39,A124,A129,A130,
GOBOARD6:85;
         i1 < len GoB f by A19,A24,A124,A126,XXREAL_0:1;
     then i1+1 <= len GoB f by NAT_1:13;
     then LSeg(1/2*((GoB f)*(i1,j1-'1)+(GoB f)*(i1+1,j1)),
               1/2*((GoB f)*(i2,j1-'1)+(GoB f)*(i1,j1))) misses L~f
       by A5,A6,A11,A14,A17,A20,A21,A24,A25,A39,A43,A45,A124,A125,A129,A131,
GOBOARD8:13;
     then LSeg(1/2*((GoB f)*(i1,j1-'1)+(GoB f)*(i1+1,j1)),
          1/2*((GoB f)*(i2,j1-'1)+(GoB f)*(i1,j1))) c= (L~f)` by SUBSET_1:43;
     then A141: LSeg(1/2*((GoB f)*(i1,j1-'1)+(GoB f)*(i1+1,j1)),
          1/2*((GoB f)*(i2,j1-'1)+(GoB f)*(i1,j1))) c= LeftComp f
                             by A48,A137,A139,A140,Th6;
A142: LSeg(1/2*((GoB f)*(i1,j1-'1)+(GoB f)*(i1+1,j1)),
          1/2*((GoB f)*(i2,j1-'1)+(GoB f)*(i2+1,j1))) meets
             Int cell(GoB f,i2,j1-'1) by A19,A20,A22,A39,A125,A129,GOBOARD6:85;
         Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
     then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
    hence Int left_cell(f,k+1) c= LeftComp f
               by A48,A49,A125,A138,A141,A142,Th6;
    end;
    suppose that
A143: j0 = j1+1 and
A144: j1 = j2+1 and
A145: i0 = len GoB f;
A146: left_cell(f,k) = cell(GoB f,len GoB f,j1)
                   by A6,A7,A10,A11,A13,A14,A24,A41,A143,A145,GOBOARD5:31
;
A147: left_cell(f,k+1) = cell(GoB f,len GoB f,j2)
                by A4,A5,A13,A14,A16,A17,A24,A25,A41,A143,A144,A145,GOBOARD5:31
;
A148: LSeg(1/2*((GoB f)*(len GoB f,j2)+(GoB f)*(len GoB f,j2+1))+|[1,0]|,
          1/2*((GoB f)*(len GoB f,j2+1)+(GoB f)*(len GoB f,j2+2))+|[1,0]|)
           is connected by Th8;
A149: LSeg(1/2*((GoB f)*(len GoB f,j2)+(GoB f)*(len GoB f,j2+1))+|[1,0]|,
          1/2*((GoB f)*(len GoB f,j2+1)+(GoB f)*(len GoB f,j2+2))+|[1,0]|)
           meets Int cell(GoB f,len GoB f,j1)
             by A21,A22,A143,A144,GOBOARD6:89;
         LSeg(1/2*((GoB f)*(len GoB f,j2)+(GoB f)*(len GoB f,j2+1))+|[1,0]|,
          1/2*((GoB f)*(len GoB f,j2+1)+(GoB f)*(len GoB f,j2+2))+|[1,0]|)
                  misses L~f by A21,A23,A143,A144,GOBOARD8:34;
     then LSeg(1/2*((GoB f)*(len GoB f,j2)+(GoB f)*(len GoB f,j2+1))+|[1,0]|,
               1/2*((GoB f)*(len GoB f,j2+1)+(GoB f)*
(len GoB f,j2+2))+|[1,0]|)
           c= (L~f)` by SUBSET_1:43;
     then A150: LSeg(1/2*((GoB f)*(len GoB f,j2)+(GoB f)*(len GoB f,j2+1))+|[1,
0]|,
          1/2*((GoB f)*(len GoB f,j2+1)+(GoB f)*(len GoB f,j2+2))+|[1,0]|)
           c= LeftComp f by A3,A5,A6,A48,A146,A148,A149,Th6,NAT_1:13;
A151: LSeg(1/2*((GoB f)*(len GoB f,j2)+(GoB f)*(len GoB f,j2+1))+|[1,0]|,
          1/2*((GoB f)*(len GoB f,j2+1)+(GoB f)*(len GoB f,j2+2))+|[1,0]|)
           meets Int cell(GoB f,i1,j2) by A22,A23,A24,A143,A144,A145,
GOBOARD6:89;
         Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
     then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
    hence Int left_cell(f,k+1) c= LeftComp f by A24,A48,A49,A143,A145,A147,A150
,A151,Th6;
    end;
    suppose that
A152: j0 = j1+1 and
A153: j1 = j2+1 and
A154: i0 <> len GoB f;
A155: left_cell(f,k) = cell(GoB f,i0,j1)
                   by A6,A7,A10,A11,A13,A14,A24,A38,A152,GOBOARD5:31;
        len GoB f > i0 by A18,A154,XXREAL_0:1;
then A156: len GoB f >= i0+1 by NAT_1:13;
A157: left_cell(f,k+1) = cell(GoB f,i1,j2)
                by A4,A5,A13,A14,A16,A17,A25,A38,A153,GOBOARD5:31;
A158: LSeg(1/2*((GoB f)*(i0,j2)+(GoB f)*(i0+1,j2+1)),
          1/2*((GoB f)*(i1,j2+1)+(GoB f)*(i1+1,j2+2))) is connected by Th8;
A159: LSeg(1/2*((GoB f)*(i0,j2)+(GoB f)*(i0+1,j2+1)),
          1/2*((GoB f)*(i1,j2+1)+(GoB f)*(i1+1,j2+2)))
           meets Int cell(GoB f,i0,j1)
             by A19,A21,A22,A24,A152,A153,A156,GOBOARD6:85;
         LSeg(1/2*((GoB f)*(i0,j2)+(GoB f)*(i0+1,j2+1)),
          1/2*((GoB f)*(i1,j2+1)+(GoB f)*(i1+1,j2+2))) misses L~f
       by A5,A6,A11,A14,A17,A18,A21,A23,A24,A25,A43,A152,A153,A156,GOBOARD8:4
;
     then LSeg(1/2*((GoB f)*(i0,j2)+(GoB f)*(i0+1,j2+1)),
         1/2*((GoB f)*(i1,j2+1)+(GoB f)*
(i1+1,j2+2))) c= (L~f)` by SUBSET_1:43;
     then A160: LSeg(1/2*((GoB f)*(i0,j2)+(GoB f)*(i0+1,j2+1)),
          1/2*((GoB f)*(i1,j2+1)+(GoB f)*(i1+1,j2+2))) c= LeftComp f
                             by A3,A5,A6,A48,A155,A158,A159,Th6,NAT_1:13;
A161: LSeg(1/2*((GoB f)*(i0,j2)+(GoB f)*(i0+1,j2+1)),
          1/2*((GoB f)*(i1,j2+1)+(GoB f)*(i1+1,j2+2))) meets
             Int cell(GoB f,i1,j2) by A19,A22,A23,A24,A152,A153,A156,
GOBOARD6:85;
         Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
     then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
    hence Int left_cell(f,k+1) c= LeftComp f by A48,A49,A157,A160,A161,Th6;
    end;
    suppose that
A162: i0+1 = i1 and
A163: j1 = j2+1 and
A164: i1 = len GoB f and
A165: j0 = width GoB f;
A166: left_cell(f,k) = cell(GoB f,i0,j1)
                   by A6,A7,A10,A11,A13,A14,A24,A39,A162,GOBOARD5:29;
A167: LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i1,j0))+|[0,1]|,
         (GoB f)*(i1,j0)+|[1,1]|) is connected by Th8;
A168: LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i1,j0))+|[0,1]|,
         (GoB f)*(i1,j0)+|[1,1]|)
           meets Int cell(GoB f,i0,j1) by A18,A19,A24,A162,A165,GOBOARD6:86
;
        LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i1,j0))+|[0,1]|,
         (GoB f)*(i1,j0)+|[1,1]|)
         misses L~f by A41,A162,A164,A165,GOBOARD8:30;
     then LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i1,j0))+|[0,1]|,
         (GoB f)*(i1,j0)+|[1,1]|) c= (L~f)` by SUBSET_1:43;
     then A169: LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i1,j0))+|[0,1]|,
         (GoB f)*(i1,j0)+|[1,1]|) c= LeftComp f by A3,A5,A6,A48,A166,A167,A168,
Th6,NAT_1:13;
A170: Int cell(GoB f,i1,j1) is connected by A19,A22,Th21;
A171: LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i1,j0))+|[0,1]|,
         (GoB f)*(i1,j0)+|[1,1]|) meets
             Int cell(GoB f,i1,j1) by A24,A162,A164,A165,GOBOARD6:91;
         Int cell(GoB f,i1,j1) misses L~f by A19,A22,GOBOARD7:14;
     then Int cell(GoB f,i1,j1) c= (L~f)` by SUBSET_1:43;
     then A172:  Int cell(GoB f,i1,j1) c= LeftComp f by A48,A169,A170,A171,Th6;
A173: left_cell(f,k+1) = cell(GoB f,i1,j2)
                by A4,A5,A13,A14,A16,A17,A25,A38,A163,GOBOARD5:31;
A174: LSeg(1/2*((GoB f)*(i1,j2)+(GoB f)*(i1,j1))+|[1,0]|,
         (GoB f)*(i1,j1)+|[1,1]|)
          is connected by Th8;
A175: LSeg(1/2*((GoB f)*(i1,j2)+(GoB f)*(i1,j1))+|[1,0]|,
         (GoB f)*(i1,j1)+|[1,1]|) meets Int cell(GoB f,i1,j1)
            by A24,A162,A164,A165,GOBOARD6:91;
        LSeg(1/2*((GoB f)*(i1,j2)+(GoB f)*(i1,j1))+|[1,0]|,
         (GoB f)*
(i1,j1)+|[1,1]|) misses L~f by A24,A42,A162,A163,A164,A165,GOBOARD8:36;
     then LSeg(1/2*((GoB f)*(i1,j2)+(GoB f)*(i1,j1))+|[1,0]|,
         (GoB f)*(i1,j1)+|[1,1]|) c= (L~f)`
           by SUBSET_1:43;
     then A176: LSeg(1/2*((GoB f)*(i1,j2)+(GoB f)*(i1,j1))+|[1,0]|,
         (GoB f)*(i1,j1)+|[1,1]|) c= LeftComp f
                             by A48,A172,A174,A175,Th6;
A177: LSeg(1/2*((GoB f)*(i1,j2)+(GoB f)*(i1,j1))+|[1,0]|,
         (GoB f)*(i1,j1)+|[1,1]|) meets Int cell(GoB f,i1,j2)
             by A22,A23,A163,A164,GOBOARD6:89;
         Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
     then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
    hence Int left_cell(f,k+1) c= LeftComp f by A48,A49,A173,A176,A177,Th6;
    end;
    suppose that
A178: i0+1 = i1 and
A179: j1 = j2+1 and
A180: i1 <> len GoB f and
A181: j0 = width GoB f;
A182: left_cell(f,k) = cell(GoB f,i0,j1)
                   by A6,A7,A10,A11,A13,A14,A24,A39,A178,GOBOARD5:29;
        len GoB f > i1 by A19,A180,XXREAL_0:1;
then A183:  i1+1 <= len GoB f by NAT_1:13;
A184: LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0+1,j0))+|[0,1]|,
         1/2*((GoB f)*(i0+1,j0)+(GoB f)*
(i0+2,j0))+|[0,1]|) is connected by Th8
;
A185: LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0+1,j0))+|[0,1]|,
         1/2*((GoB f)*(i0+1,j0)+(GoB f)*(i0+2,j0))+|[0,1]|)
           meets Int cell(GoB f,i0,j1) by A18,A19,A24,A178,A181,GOBOARD6:86
;
        LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0+1,j0))+|[0,1]|,
         1/2*((GoB f)*(i0+1,j0)+(GoB f)*(i0+2,j0))+|[0,1]|)
            misses L~f by A18,A178,A181,A183,GOBOARD8:28;
     then LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0+1,j0))+|[0,1]|,
         1/2*((GoB f)*(i0+1,j0)+(GoB f)*(i0+2,j0))+|[0,1]|)
           c= (L~f)` by SUBSET_1:43;
     then A186: LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0+1,j0))+|[0,1]|,
         1/2*((GoB f)*(i0+1,j0)+(GoB f)*(i0+2,j0))+|[0,1]|) c= LeftComp f
                             by A3,A5,A6,A48,A182,A184,A185,Th6,NAT_1:13;
A187: Int cell(GoB f,i1,j1) is connected by A19,A22,Th21;
A188: LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0+1,j0))+|[0,1]|,
         1/2*((GoB f)*(i0+1,j0)+(GoB f)*(i0+2,j0))+|[0,1]|) meets
             Int cell(GoB f,i1,j1) by A19,A24,A178,A181,A183,GOBOARD6:86
;
         Int cell(GoB f,i1,j1) misses L~f by A19,A22,GOBOARD7:14;
     then Int cell(GoB f,i1,j1) c= (L~f)` by SUBSET_1:43;
     then A189:  Int cell(GoB f,i1,j1) c= LeftComp f by A48,A186,A187,A188,Th6;
A190: left_cell(f,k+1) = cell(GoB f,i1,j2)
                by A4,A5,A13,A14,A16,A17,A25,A38,A179,GOBOARD5:31;
A191: LSeg(1/2*((GoB f)*(i0+1,j2)+(GoB f)*(i1+1,j0)),
          1/2*((GoB f)*(i0+1,j0)+(GoB f)*(i1+1,j0))+|[0,1]|)
          is connected by Th8;
A192: LSeg(1/2*((GoB f)*(i0+1,j2)+(GoB f)*(i1+1,j0)),
          1/2*((GoB f)*(i0+1,j0)+(GoB f)*(i1+1,j0))+|[0,1]|)
           meets Int cell(GoB f,i1,j1) by A19,A24,A178,A181,A183,GOBOARD6:86
;
         LSeg(1/2*((GoB f)*(i0+1,j2)+(GoB f)*(i1+1,j0)),
          1/2*((GoB f)*(i0+1,j0)+(GoB f)*(i1+1,j0))+|[0,1]|)
        misses L~f
         by A5,A6,A11,A14,A17,A18,A24,A25,A42,A43,A44,A178,A179,A181,A183,
GOBOARD8:10;
     then LSeg(1/2*((GoB f)*(i0+1,j2)+(GoB f)*(i1+1,j0)),
          1/2*((GoB f)*(i0+1,j0)+(GoB f)*(i1+1,j0))+|[0,1]|) c= (L~f)`
           by SUBSET_1:43;
     then A193: LSeg(1/2*((GoB f)*(i0+1,j2)+(GoB f)*(i1+1,j0)),
          1/2*((GoB f)*(i0+1,j0)+(GoB f)*(i1+1,j0))+|[0,1]|) c= LeftComp f
                             by A48,A189,A191,A192,Th6;
A194: LSeg(1/2*((GoB f)*(i0+1,j2)+(GoB f)*(i1+1,j0)),
          1/2*((GoB f)*(i0+1,j0)+(GoB f)*(i1+1,j0))+|[0,1]|) meets
             Int cell(GoB f,i1,j2) by A19,A23,A24,A178,A179,A181,A183,
GOBOARD6:85;
         Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
     then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
    hence Int left_cell(f,k+1) c= LeftComp f by A48,A49,A190,A193,A194,Th6;
    end;
    suppose that
A195: i0+1 = i1 and
A196: j1 = j2+1 and
A197: i1 = len GoB f and
A198: j0 <> width GoB f;
A199: left_cell(f,k) = cell(GoB f,i0,j1)
                   by A6,A7,A10,A11,A13,A14,A24,A39,A195,GOBOARD5:29;
        width GoB f > j0 by A21,A198,XXREAL_0:1;
then A200: width GoB f >= j0+1 by NAT_1:13;
A201: LSeg(1/2*((GoB f)*(i0,j1)+(GoB f)*(i1,j1+1)),
          1/2*((GoB f)*(i1,j1)+(GoB f)*
(i1,j1+1))+|[1,0]|) is connected by Th8;
A202: LSeg(1/2*((GoB f)*(i0,j1)+(GoB f)*(i1,j1+1)),
          1/2*((GoB f)*(i1,j1)+(GoB f)*(i1,j1+1))+|[1,0]|)
           meets Int cell(GoB f,i0,j1) by A18,A19,A22,A24,A195,A200,GOBOARD6:85
;
         LSeg(1/2*((GoB f)*(i0,j1)+(GoB f)*(i1,j1+1)),
          1/2*((GoB f)*(i1,j1)+(GoB f)*(i1,j1+1))+|[1,0]|) misses L~f
        by A5,A6,A11,A14,A17,A23,A24,A25,A41,A43,A47,A195,A196,A197,A200,
GOBOARD8:20;
     then LSeg(1/2*((GoB f)*(i0,j1)+(GoB f)*(i1,j1+1)),
          1/2*((GoB f)*(i1,j1)+(GoB f)*(i1,j1+1))+|[1,0]|)
           c= (L~f)` by SUBSET_1:43;
     then A203: LSeg(1/2*((GoB f)*(i0,j1)+(GoB f)*(i1,j1+1)),
          1/2*((GoB f)*(i1,j1)+(GoB f)*(i1,j1+1))+|[1,0]|) c= LeftComp f
                             by A3,A5,A6,A48,A199,A201,A202,Th6,NAT_1:13;
A204: Int cell(GoB f,i1,j1) is connected by A19,A22,Th21;
A205: LSeg(1/2*((GoB f)*(i0,j1)+(GoB f)*(i1,j1+1)),
          1/2*((GoB f)*(i1,j1)+(GoB f)*(i1,j1+1))+|[1,0]|) meets
             Int cell(GoB f,i1,j1) by A22,A24,A195,A197,A200,GOBOARD6:89
;
         Int cell(GoB f,i1,j1) misses L~f by A19,A22,GOBOARD7:14;
     then Int cell(GoB f,i1,j1) c= (L~f)` by SUBSET_1:43;
     then A206:  Int cell(GoB f,i1,j1) c= LeftComp f by A48,A203,A204,A205,Th6;
A207: left_cell(f,k+1) = cell(GoB f,i1,j2)
                by A4,A5,A13,A14,A16,A17,A25,A38,A196,GOBOARD5:31;
A208: LSeg(1/2*((GoB f)*(len GoB f,j2)+(GoB f)*(len GoB f,j1))+|[1,0]|,
          1/2*((GoB f)*(len GoB f,j1)+(GoB f)*(len GoB f,j1+1))+|[1,0]|)
                 is connected by Th8;
A209: LSeg(1/2*((GoB f)*(len GoB f,j2)+(GoB f)*(len GoB f,j1))+|[1,0]|,
          1/2*((GoB f)*(len GoB f,j1)+(GoB f)*(len GoB f,j1+1))+|[1,0]|)
           meets Int cell(GoB f,i1,j1) by A22,A24,A195,A197,A200,GOBOARD6:89
;
        LSeg(1/2*((GoB f)*(len GoB f,j2)+(GoB f)*(len GoB f,j1))+|[1,0]|,
         1/2*((GoB f)*(len GoB f,j1)+(GoB f)*(len GoB f,j1+1))+|[1,0]|)
            misses L~f by A23,A24,A47,A195,A196,A200,GOBOARD8:34;
     then LSeg(1/2*((GoB f)*(len GoB f,j2)+(GoB f)*(len GoB f,j1))+|[1,0]|,
         1/2*((GoB f)*(len GoB f,j1)+(GoB f)*(len GoB f,j1+1))+|[1,0]|)
            c= (L~f)` by SUBSET_1:43;
     then A210: LSeg(1/2*((GoB f)*(len GoB f,j2)+(GoB f)*(len GoB f,j1))+|[1,0
]|,
         1/2*((GoB f)*(len GoB f,j1)+(GoB f)*(len GoB f,j1+1))+|[1,0]|)
          c= LeftComp f by A48,A206,A208,A209,Th6;
A211: LSeg(1/2*((GoB f)*(len GoB f,j2)+(GoB f)*(len GoB f,j1))+|[1,0]|,
         1/2*((GoB f)*(len GoB f,j1)+(GoB f)*(len GoB f,j1+1))+|[1,0]|) meets
             Int cell(GoB f,i1,j2) by A22,A23,A196,A197,GOBOARD6:89;
         Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
     then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
    hence Int left_cell(f,k+1) c= LeftComp f by A48,A49,A207,A210,A211,Th6;
    end;
    suppose that
A212: i0+1 = i1 and
A213: j1 = j2+1 and
A214: i1 <> len GoB f and
A215: j0 <> width GoB f;
A216: left_cell(f,k) = cell(GoB f,i0,j1)
                   by A6,A7,A10,A11,A13,A14,A24,A39,A212,GOBOARD5:29;
        len GoB f > i1 by A19,A214,XXREAL_0:1;
then A217:  i1+1 <= len GoB f by NAT_1:13;
        width GoB f > j0 by A21,A215,XXREAL_0:1;
then A218: width GoB f >= j0+1 by NAT_1:13;
A219: LSeg(1/2*((GoB f)*(i0,j2+1)+(GoB f)*(i0+1,j1+1)),
          1/2*((GoB f)*(i0+1,j2+1)+(GoB f)*(i1+1,j1+1))) is connected by Th8;
A220: LSeg(1/2*((GoB f)*(i0,j2+1)+(GoB f)*(i0+1,j1+1)),
          1/2*((GoB f)*(i0+1,j2+1)+(GoB f)*(i1+1,j1+1)))
           meets Int cell(GoB f,i0,j1) by A18,A19,A22,A24,A212,A213,A218,
GOBOARD6:85;
         LSeg(1/2*((GoB f)*(i0,j2+1)+(GoB f)*(i0+1,j1+1)),
          1/2*((GoB f)*(i0+1,j2+1)+(GoB f)*(i1+1,j1+1))) misses L~f
     by A5,A6,A11,A14,A17,A18,A23,A24,A25,A43,A44,A47,A212,A213,A217,A218,
GOBOARD8:16;
     then LSeg(1/2*((GoB f)*(i0,j2+1)+(GoB f)*(i0+1,j1+1)),
          1/2*((GoB f)*(i0+1,j2+1)+(GoB f)*(i1+1,j1+1)))
           c= (L~f)` by SUBSET_1:43;
     then A221: LSeg(1/2*((GoB f)*(i0,j2+1)+(GoB f)*(i0+1,j1+1)),
          1/2*((GoB f)*(i0+1,j2+1)+(GoB f)*(i1+1,j1+1))) c= LeftComp f
                             by A3,A5,A6,A48,A216,A219,A220,Th6,NAT_1:13;
A222: Int cell(GoB f,i1,j1) is connected by A19,A22,Th21;
A223: LSeg(1/2*((GoB f)*(i0,j2+1)+(GoB f)*(i0+1,j1+1)),
          1/2*((GoB f)*(i0+1,j2+1)+(GoB f)*(i1+1,j1+1))) meets
             Int cell(GoB f,i1,j1) by A19,A22,A24,A212,A213,A217,A218,
GOBOARD6:85;
         Int cell(GoB f,i1,j1) misses L~f by A19,A22,GOBOARD7:14;
     then Int cell(GoB f,i1,j1) c= (L~f)` by SUBSET_1:43;
     then A224:  Int cell(GoB f,i1,j1) c= LeftComp f by A48,A221,A222,A223,Th6;
A225: left_cell(f,k+1) = cell(GoB f,i1,j2)
                by A4,A5,A13,A14,A16,A17,A25,A38,A213,GOBOARD5:31;
A226: LSeg(1/2*((GoB f)*(i0+1,j2)+(GoB f)*(i1+1,j2+1)),
          1/2*((GoB f)*(i0+1,j2+1)+(GoB f)*(i1+1,j1+1))) is connected by Th8;
A227: LSeg(1/2*((GoB f)*(i0+1,j2)+(GoB f)*(i1+1,j2+1)),
          1/2*((GoB f)*(i0+1,j2+1)+(GoB f)*(i1+1,j1+1)))
           meets Int cell(GoB f,i1,j1)
            by A19,A22,A24,A212,A213,A217,A218,GOBOARD6:85;
         LSeg(1/2*((GoB f)*(i0+1,j2)+(GoB f)*(i1+1,j2+1)),
          1/2*((GoB f)*(i0+1,j2+1)+(GoB f)*(i1+1,j1+1))) misses L~f
       by A5,A6,A11,A14,A17,A18,A23,A24,A25,A43,A44,A47,A212,A213,A217,A218,
GOBOARD8:6;
     then LSeg(1/2*((GoB f)*(i0+1,j2)+(GoB f)*(i1+1,j2+1)),
          1/2*((GoB f)*(i0+1,j2+1)+(GoB f)*(i1+1,j1+1))) c= (L~f)`
           by SUBSET_1:43;
     then A228: LSeg(1/2*((GoB f)*(i0+1,j2)+(GoB f)*(i1+1,j2+1)),
          1/2*((GoB f)*(i0+1,j2+1)+(GoB f)*(i1+1,j1+1))) c= LeftComp f
                             by A48,A224,A226,A227,Th6;
A229: LSeg(1/2*((GoB f)*(i0+1,j2)+(GoB f)*(i1+1,j2+1)),
          1/2*((GoB f)*(i0+1,j2+1)+(GoB f)*(i1+1,j1+1))) meets
             Int cell(GoB f,i1,j2) by A19,A22,A23,A212,A213,A217,GOBOARD6:85;
         Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
     then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
    hence Int left_cell(f,k+1) c= LeftComp f by A48,A49,A225,A228,A229,Th6;
    end;
    suppose that
A230: j0+1 = j1 and
A231: i1 = i2+1;
         left_cell(f,k) = cell(GoB f,i2,j0)
                by A6,A7,A10,A11,A13,A14,A24,A230,A231,GOBOARD5:28
        .= left_cell(f,k+1) by A4,A5,A13,A14,A16,A17,A25,A230,A231,GOBOARD5:30
;
    hence Int left_cell(f,k+1) c= LeftComp f by A3,A5,A6,NAT_1:13;
    end;
    suppose that
A232: i0 = i1+1 and
A233: j1+1 = j2 and
A234: i1 = 1 and
A235: j1 = 1;
A236: left_cell(f,k) = cell(GoB f,i1,j1-'1)
                by A6,A7,A10,A11,A13,A14,A24,A39,A232,GOBOARD5:30;
A237: LSeg((GoB f)*(i1,j1)- |[1,1]|,
        1/2*((GoB f)*(i1,j1)+(GoB f)*(i0,j1))- |[0,1]|) is connected by Th8;
         i1-'1 <= i1 by BINARITH:52;
then A238:  i1-'1 <= len GoB f by A19,XXREAL_0:2;
A239:  j1-'1 = 0 by A235,BINARITH:51;
A240:  i1-'1 = 0 by A234,BINARITH:51;
         j1-'1 <= j1 by BINARITH:52;
then A241:  j1-'1 <= width GoB f by A22,XXREAL_0:2;
A242: LSeg((GoB f)*(i1,j1)- |[1,1]|,
          1/2*((GoB f)*(i1,j1)+(GoB f)*(i0,j1))- |[0,1]|)
     meets Int cell(GoB f,i1,j1-'1) by A18,A19,A232,A235,A239,GOBOARD6:87;
         LSeg((GoB f)*(i1,j1)- |[1,1]|,
          1/2*((GoB f)*(i1,j1)+(GoB f)*(i0,j1))- |[0,1]|)
       misses L~f by A232,A234,A235,GOBOARD8:26;
     then LSeg((GoB f)*(i1,j1)- |[1,1]|,
          1/2*((GoB f)*(i1,j1)+(GoB f)*(i0,j1))- |[0,1]|) c= (L~f)`
           by SUBSET_1:43;
     then A243: LSeg((GoB f)*(i1,j1)- |[1,1]|,
          1/2*((GoB f)*(i1,j1)+(GoB f)*(i0,j1))- |[0,1]|) c= LeftComp f
                             by A3,A5,A6,A48,A236,A237,A242,Th6,NAT_1:13;
A244: Int cell(GoB f,i1-'1,j1-'1) is connected by A238,A241,Th21;
A245: LSeg((GoB f)*(i1,j1)- |[1,1]|,
          1/2*((GoB f)*(i1,j1)+(GoB f)*(i0,j1))- |[0,1]|) meets
             Int cell(GoB f,i1-'1,j0-'1) by A24,A232,A234,A235,A239,GOBOARD6:90
;
         Int cell(GoB f,i1-'1,j1-'1) misses L~f by A238,A241,GOBOARD7:14;
     then Int cell(GoB f,i1-'1,j1-'1) c= (L~f)` by SUBSET_1:43;
     then A246:   Int cell(GoB f,i1-'1,j1-'1) c= LeftComp f
                     by A24,A48,A232,A243,A244,A245,Th6;
A247: left_cell(f,k+1) = cell(GoB f,i1-'1,j1)
                by A4,A5,A13,A14,A16,A17,A25,A38,A233,GOBOARD5:28;
A248: LSeg((GoB f)*(i1,j1)- |[1,1]|,
           1/2*((GoB f)*(i1,j1)+(GoB f)*
(i1,j2))- |[1,0]|) is connected by Th8;
A249: LSeg((GoB f)*(i1,j1)- |[1,1]|,
           1/2*((GoB f)*(i1,j1)+(GoB f)*(i1,j2))- |[1,0]|)
           meets Int cell(GoB f,i1-'1,j1-'1) by A234,A235,A239,GOBOARD6:90;
          LSeg((GoB f)*(i1,j1)- |[1,1]|,
           1/2*((GoB f)*(i1,j1)+(GoB f)*(i1,j2))- |[1,0]|)
       misses L~f by A233,A234,A235,GOBOARD8:32;

     then LSeg((GoB f)*(i1,j1)- |[1,1]|,
           1/2*((GoB f)*(i1,j1)+(GoB f)*(i1,j2))- |[1,0]|) c= (L~f)`
            by SUBSET_1:43;
     then A250: LSeg((GoB f)*(i1,j1)- |[1,1]|,
           1/2*((GoB f)*(i1,j1)+(GoB f)*(i1,j2))- |[1,0]|) c= LeftComp f
                             by A48,A246,A248,A249,Th6;
A251: LSeg((GoB f)*(i1,j1)- |[1,1]|,
           1/2*((GoB f)*(i1,j1)+(GoB f)*(i1,j2))- |[1,0]|) meets
             Int cell(GoB f,i1-'1,j1) by A22,A23,A233,A234,A240,GOBOARD6:88;
         Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
     then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
    hence Int left_cell(f,k+1) c= LeftComp f by A48,A49,A247,A250,A251,Th6;
    end;
    suppose that
A252: i0 = i1+1 and
A253: j1+1 = j2 and
A254: i1 <> 1 and
A255: j1 = 1;
A256: left_cell(f,k) = cell(GoB f,i1,j1-'1)
                by A6,A7,A10,A11,A13,A14,A24,A39,A252,GOBOARD5:30;
        1 < i1 by A19,A254,XXREAL_0:1;
then A257: 1 <= i1-'1 by A38,NAT_1:13;
A258: i1-'1+(1+1) = i0 by A38,A252;
A259: LSeg(1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,1))- |[0,1]|,
         1/2*((GoB f)*(i1,1)+(GoB f)*(i0,1))- |[0,1]|) is connected by Th8;
         i1-'1 <= i1 by BINARITH:52;
then A260:  i1-'1 <= len GoB f by A19,XXREAL_0:2;
A261:  j1-'1 = 0 by A255,BINARITH:51;
         j1-'1 <= j1 by BINARITH:52;
then A262:  j1-'1 <= width GoB f by A22,XXREAL_0:2;
A263: LSeg(1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,1))- |[0,1]|,
         1/2*((GoB f)*(i1,1)+(GoB f)*(i0,1))- |[0,1]|)
           meets Int cell(GoB f,i1,j1-'1) by A18,A19,A252,A261,GOBOARD6:87;
        LSeg(1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,1))- |[0,1]|,
         1/2*((GoB f)*(i1,1)+(GoB f)*(i0,1))- |[0,1]|) misses L~f
          by A18,A38,A257,A258,GOBOARD8:25;
     then LSeg(1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,1))- |[0,1]|,
         1/2*((GoB f)*(i1,1)+(GoB f)*
(i0,1))- |[0,1]|) c= (L~f)` by SUBSET_1:43;
     then A264: LSeg(1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,1))- |[0,1]|,
         1/2*((GoB f)*(i1,1)+(GoB f)*(i0,1))- |[0,1]|) c= LeftComp f
                             by A3,A5,A6,A48,A256,A259,A263,Th6,NAT_1:13;
A265: Int cell(GoB f,i1-'1,j1-'1) is connected by A260,A262,Th21;
A266: LSeg(1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,1))- |[0,1]|,
         1/2*((GoB f)*(i1,1)+(GoB f)*(i0,1))- |[0,1]|) meets
             Int cell(GoB f,i1-'1,j0-'1) by A19,A24,A38,A252,A257,A261,
GOBOARD6:87;
         Int cell(GoB f,i1-'1,j1-'1) misses L~f by A260,A262,GOBOARD7:14;
     then Int cell(GoB f,i1-'1,j1-'1) c= (L~f)` by SUBSET_1:43;
     then A267:   Int cell(GoB f,i1-'1,j1-'1) c= LeftComp f
                     by A24,A48,A252,A264,A265,A266,Th6;
A268: left_cell(f,k+1) = cell(GoB f,i1-'1,j1)
                by A4,A5,A13,A14,A16,A17,A25,A38,A253,GOBOARD5:28;
A269: LSeg(1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,1))- |[0,1]|,
          1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,2))) is connected by Th8;
A270: LSeg(1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,1))- |[0,1]|,
          1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,2)))
           meets Int cell(GoB f,i1-'1,j1-'1) by A19,A38,A257,A261,GOBOARD6:87;
         LSeg(1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,1))- |[0,1]|,
          1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,2))) misses L~f
          by A5,A6,A11,A14,A17,A18,A24,A25,A38,A43,A253,A255,A257,A258,
GOBOARD8:7;
     then LSeg(1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,1))- |[0,1]|,
          1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,2))) c= (L~f)` by SUBSET_1:43;
     then A271: LSeg(1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,1))- |[0,1]|,
          1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,2))) c= LeftComp f
                             by A48,A267,A269,A270,Th6;
A272: LSeg(1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,1))- |[0,1]|,
          1/2*((GoB f)*(i1-'1,1)+(GoB f)*(i1,2))) meets
          Int cell(GoB f,i1-'1,j1) by A19,A23,A38,A253,A255,A257,GOBOARD6:85;
         Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
     then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
    hence Int left_cell(f,k+1) c= LeftComp f by A48,A49,A268,A271,A272,Th6;
    end;
    suppose that
A273: i0 = i1+1 and
A274: j1+1 = j2 and
A275: i1 = 1 and
A276: j1 <> 1;
A277: left_cell(f,k) = cell(GoB f,i1,j1-'1)
                by A6,A7,A10,A11,A13,A14,A24,A39,A273,GOBOARD5:30;
     1 < j0 by A21,A24,A273,A276,XXREAL_0:1;
then A278: 1 <= j0-'1 by A24,A39,A273,NAT_1:13;
A279: j0-'1+(1+1) = j2 by A24,A39,A273,A274;
A280: LSeg(1/2*((GoB f)*(1,j1-'1)+(GoB f)*(1,j1-'1+1))- |[1,0]|,
          1/2*((GoB f)*(1,j1-'1)+(GoB f)*(2,j1-'1+1))) is connected by Th8;
A281: LSeg(1/2*((GoB f)*(1,j1-'1)+(GoB f)*(1,j1-'1+1))- |[1,0]|,
          1/2*((GoB f)*(1,j1-'1)+(GoB f)*(2,j1-'1+1)))
           meets Int cell(GoB f,i1,j1-'1)
           by A18,A22,A24,A39,A273,A275,A278,GOBOARD6:85;
         LSeg(1/2*((GoB f)*(1,j1-'1)+(GoB f)*(1,j1-'1+1))- |[1,0]|,
          1/2*((GoB f)*(1,j1-'1)+(GoB f)*(2,j1-'1+1))) misses L~f
           by A5,A6,A11,A14,A17,A23,A24,A25,A39,A43,A273,A274,A275,A278,A279,
GOBOARD8:17;
     then LSeg(1/2*((GoB f)*(1,j1-'1)+(GoB f)*(1,j1-'1+1))- |[1,0]|,
               1/2*((GoB f)*(1,j1-'1)+(GoB f)*(2,j1-'1+1))) c= (L~f)`
           by SUBSET_1:43;
     then A282: LSeg(1/2*((GoB f)*(1,j1-'1)+(GoB f)*(1,j1-'1+1))- |[1,0]|,
          1/2*((GoB f)*(1,j1-'1)+(GoB f)*(2,j1-'1+1))) c= LeftComp f
                             by A3,A5,A6,A48,A277,A280,A281,Th6,NAT_1:13;
         i1-'1 <= i1 by BINARITH:52;
then A283:  i1-'1 <= len GoB f by A19,XXREAL_0:2;
A284:  i1-'1 = 0 by A275,BINARITH:51;
         j1-'1 <= j1 by BINARITH:52;
then A285:  j1-'1 <= width GoB f by A22,XXREAL_0:2;
then A286: Int cell(GoB f,i1-'1,j1-'1) is connected by A283,Th21;
A287: LSeg(1/2*((GoB f)*(1,j1-'1)+(GoB f)*(1,j1-'1+1))- |[1,0]|,
          1/2*((GoB f)*(1,j1-'1)+(GoB f)*(2,j1-'1+1))) meets
             Int cell(GoB f,i1-'1,j0-'1) by A22,A24,A39,A273,A278,A284,
GOBOARD6:88;
         Int cell(GoB f,i1-'1,j1-'1) misses L~f by A283,A285,GOBOARD7:14;
     then Int cell(GoB f,i1-'1,j1-'1) c= (L~f)` by SUBSET_1:43;
     then A288:   Int cell(GoB f,i1-'1,j1-'1) c= LeftComp f
                     by A24,A48,A273,A282,A286,A287,Th6;
A289: left_cell(f,k+1) = cell(GoB f,i1-'1,j1)
                by A4,A5,A13,A14,A16,A17,A25,A38,A274,GOBOARD5:28;
A290: LSeg(1/2*((GoB f)*(1,j1-'1)+(GoB f)*(1,j1-'1+1))- |[1,0]|,
         1/2*((GoB f)*(1,j1-'1+1)+(GoB f)*(1,j1-'1+2))- |[1,0]|)
          is connected by Th8;
A291: LSeg(1/2*((GoB f)*(1,j1-'1)+(GoB f)*(1,j1-'1+1))- |[1,0]|,
         1/2*((GoB f)*(1,j1-'1+1)+(GoB f)*(1,j1-'1+2))- |[1,0]|)
           meets Int cell(GoB f,i1-'1,j1-'1)
           by A22,A24,A39,A273,A278,A284,GOBOARD6:88;
        LSeg(1/2*((GoB f)*(1,j1-'1)+(GoB f)*(1,j1-'1+1))- |[1,0]|,
         1/2*((GoB f)*(1,j1-'1+1)+(GoB f)*(1,j1-'1+2))- |[1,0]|) misses L~f
         by A23,A24,A39,A273,A274,A278,GOBOARD8:31;
     then LSeg(1/2*((GoB f)*(1,j1-'1)+(GoB f)*(1,j1-'1+1))- |[1,0]|,
         1/2*((GoB f)*(1,j1-'1+1)+(GoB f)*(1,j1-'1+2))- |[1,0]|) c= (L~f)`
          by SUBSET_1:43;
     then A292: LSeg(1/2*((GoB f)*(1,j1-'1)+(GoB f)*(1,j1-'1+1))- |[1,0]|,
         1/2*((GoB f)*(1,j1-'1+1)+(GoB f)*(1,j1-'1+2))- |[1,0]|) c= LeftComp f
                             by A48,A288,A290,A291,Th6;
A293: LSeg(1/2*((GoB f)*(1,j1-'1)+(GoB f)*(1,j1-'1+1))- |[1,0]|,
         1/2*((GoB f)*(1,j1-'1+1)+(GoB f)*(1,j1-'1+2))- |[1,0]|) meets
             Int cell(GoB f,i1-'1,j1)
             by A22,A23,A39,A274,A284,GOBOARD6:88;
         Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
     then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
    hence Int left_cell(f,k+1) c= LeftComp f by A48,A49,A289,A292,A293,Th6;
    end;
    suppose that
A294: i0 = i1+1 and
A295: j1+1 = j2 and
A296: i1 <> 1 and
A297: j1 <> 1;
A298: left_cell(f,k) = cell(GoB f,i1,j1-'1)
                by A6,A7,A10,A11,A13,A14,A24,A39,A294,GOBOARD5:30;
        1 < j0 by A21,A24,A294,A297,XXREAL_0:1;
then A299: 1 <= j0-'1 by A24,A39,A294,NAT_1:13;
        1 < i1 by A19,A296,XXREAL_0:1;
then A300: 1 <= i1-'1 by A38,NAT_1:13;
A301: i1-'1+(1+1) = i0 by A38,A294;
A302: j0-'1+(1+1) = j2 by A24,A39,A294,A295;
A303: LSeg(1/2*((GoB f)*(i1-'1,j0-'1)+(GoB f)*(i1,j1)),
          1/2*((GoB f)*(i1,j0-'1)+(GoB f)*(i0,j1))) is connected by Th8;
A304: LSeg(1/2*((GoB f)*(i1-'1,j0-'1)+(GoB f)*(i1,j1)),
          1/2*((GoB f)*(i1,j0-'1)+(GoB f)*(i0,j1)))
           meets Int cell(GoB f,i1,j1-'1)
             by A18,A19,A22,A24,A39,A294,A299,GOBOARD6:85;
         LSeg(1/2*((GoB f)*(i1-'1,j0-'1)+(GoB f)*(i1,j1)),
          1/2*((GoB f)*(i1,j0-'1)+(GoB f)*(i0,j1))) misses L~f
    by A5,A6,A11,A14,A17,A18,A23,A24,A25,A38,A39,A43,A295,A299,A300,A301,A302,
GOBOARD8:12;
     then LSeg(1/2*((GoB f)*(i1-'1,j0-'1)+(GoB f)*(i1,j1)),
          1/2*((GoB f)*(i1,j0-'1)+(GoB f)*(i0,j1))) c= (L~f)` by SUBSET_1:43;
     then A305: LSeg(1/2*((GoB f)*(i1-'1,j0-'1)+(GoB f)*(i1,j1)),
          1/2*((GoB f)*(i1,j0-'1)+(GoB f)*(i0,j1))) c= LeftComp f
                             by A3,A5,A6,A48,A298,A303,A304,Th6,NAT_1:13;
         i1-'1 <= i1 by BINARITH:52;
then A306:  i1-'1 <= len GoB f by A19,XXREAL_0:2;
         j1-'1 <= j1 by BINARITH:52;
then A307:  j1-'1 <= width GoB f by A22,XXREAL_0:2;
then A308: Int cell(GoB f,i1-'1,j1-'1) is connected by A306,Th21;
A309: LSeg(1/2*((GoB f)*(i1-'1,j0-'1)+(GoB f)*(i1,j1)),
          1/2*((GoB f)*(i1,j0-'1)+(GoB f)*(i0,j1))) meets
             Int cell(GoB f,i1-'1,j0-'1)
             by A19,A22,A24,A38,A39,A294,A299,A300,GOBOARD6:85;
         Int cell(GoB f,i1-'1,j1-'1) misses L~f by A306,A307,GOBOARD7:14;
     then Int cell(GoB f,i1-'1,j1-'1) c= (L~f)` by SUBSET_1:43;
     then A310:   Int cell(GoB f,i1-'1,j1-'1) c= LeftComp f
                     by A24,A48,A294,A305,A308,A309,Th6;
A311: left_cell(f,k+1) = cell(GoB f,i1-'1,j1)
                by A4,A5,A13,A14,A16,A17,A25,A38,A295,GOBOARD5:28;
A312: LSeg(1/2*((GoB f)*(i1-'1,j1-'1)+(GoB f)*(i1,j1)),
          1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j2))) is connected by Th8;
A313: LSeg(1/2*((GoB f)*(i1-'1,j1-'1)+(GoB f)*(i1,j1)),
          1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j2)))
           meets Int cell(GoB f,i1-'1,j1-'1)
           by A19,A22,A24,A38,A39,A294,A299,A300,GOBOARD6:85;
         LSeg(1/2*((GoB f)*(i1-'1,j1-'1)+(GoB f)*(i1,j1)),
          1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j2))) misses L~f
      by A5,A6,A11,A14,A17,A18,A23,A24,A25,A38,A39,A43,A295,A299,A300,A301,A302
,GOBOARD8:2;
     then LSeg(1/2*((GoB f)*(i1-'1,j1-'1)+(GoB f)*(i1,j1)),
          1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j2))) c= (L~f)` by SUBSET_1:43;
     then A314: LSeg(1/2*((GoB f)*(i1-'1,j1-'1)+(GoB f)*(i1,j1)),
          1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j2))) c= LeftComp f
                             by A48,A310,A312,A313,Th6;
A315: LSeg(1/2*((GoB f)*(i1-'1,j1-'1)+(GoB f)*(i1,j1)),
          1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j2))) meets
             Int cell(GoB f,i1-'1,j1) by A19,A22,A23,A38,A295,A300,GOBOARD6:85;
         Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
     then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
    hence Int left_cell(f,k+1) c= LeftComp f by A48,A49,A311,A314,A315,Th6;
    end;
    suppose that
A316: j0 = j1+1 and
A317: i1+1 = i2;
         left_cell(f,k) = cell(GoB f,i0,j1)
                by A6,A7,A10,A11,A13,A14,A24,A38,A316,GOBOARD5:31
        .= left_cell(f,k+1)
                by A4,A5,A13,A14,A16,A17,A24,A25,A39,A316,A317,GOBOARD5:29
;
    hence Int left_cell(f,k+1) c= LeftComp f by A3,A5,A6,NAT_1:13;
   end;
   suppose that
A318: i0+1 = i1 and
A319: i1+1 = i2 and
A320: j0 = width GoB f;
A321: left_cell(f,k) = cell(GoB f,i0,width GoB f)
                   by A6,A7,A10,A11,A13,A14,A24,A42,A318,A320,GOBOARD5:29
;
A322: left_cell(f,k+1) = cell(GoB f,i1,width GoB f)
             by A4,A5,A13,A14,A16,A17,A24,A25,A42,A318,A319,A320,GOBOARD5:29
;
A323: LSeg(1/2*((GoB f)*(i0,width GoB f)+(GoB f)*(i0+1,width GoB f))+|[0,1]|,
          1/2*((GoB f)*(i1,width GoB f)+(GoB f)*(i1+1,width GoB f))+|[0,1]|)
           is connected by Th8;
A324: LSeg(1/2*((GoB f)*(i0,width GoB f)+(GoB f)*(i0+1,width GoB f))+|[0,1]|,
          1/2*((GoB f)*(i1,width GoB f)+(GoB f)*(i1+1,width GoB f))+|[0,1]|)
           meets Int cell(GoB f,i0,width GoB f) by A18,A19,A318,GOBOARD6:86;
         LSeg(1/2*((GoB f)*(i0,width GoB f)+(GoB f)*(i0+1,width GoB f))+|[0,1]|
,
          1/2*((GoB f)*(i1,width GoB f)+(GoB f)*(i1+1,width GoB f))+|[0,1]|)
           misses L~f by A18,A20,A44,A318,A319,GOBOARD8:28;
     then LSeg(1/2*((GoB f)*(i0,width GoB f)+(GoB f)*(i0+1,width GoB f))+|[0,1
]|,
           1/2*((GoB f)*(i1,width GoB f)+(GoB f)*(i1+1,width GoB f))+|[0,1]|)
           c= (L~f)` by SUBSET_1:43;
     then A325: LSeg(1/2*((GoB f)*(i0,width GoB f)+(GoB f)*(i0+1,width GoB f))+
|[0,1]|,
           1/2*((GoB f)*(i1,width GoB f)+(GoB f)*(i1+1,width GoB f))+|[0,1]|)
           c= LeftComp f by A3,A5,A6,A48,A321,A323,A324,Th6,NAT_1:13;
A326: LSeg(1/2*((GoB f)*(i0,width GoB f)+(GoB f)*(i0+1,width GoB f))+|[0,1]|,
           1/2*((GoB f)*(i1,width GoB f)+(GoB f)*(i1+1,width GoB f))+|[0,1]|)
         meets Int cell(GoB f,i1,width GoB f) by A19,A20,A319,GOBOARD6:86;
         Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
     then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
    hence Int left_cell(f,k+1) c= LeftComp f by A48,A49,A322,A325,A326,Th6;
    end;
    suppose that
A327: i0+1 = i1 and
A328: i1+1 = i2 and
A329: j0 <> width GoB f;
A330: left_cell(f,k) = cell(GoB f,i0,j1)
                   by A6,A7,A10,A11,A13,A14,A24,A39,A327,GOBOARD5:29;
        width GoB f > j0 by A21,A329,XXREAL_0:1;
then A331: width GoB f >= j0+1 by NAT_1:13;
A332: left_cell(f,k+1) = cell(GoB f,i1,j1)
                by A4,A5,A13,A14,A16,A17,A25,A39,A328,GOBOARD5:29;
A333: LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0+1,j0+1)),
          1/2*((GoB f)*(i1,j0)+(GoB f)*(i1+1,j0+1))) is connected by Th8;
A334: LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0+1,j0+1)),
          1/2*((GoB f)*(i1,j0)+(GoB f)*(i1+1,j0+1)))
           meets Int cell(GoB f,i0,j1)
             by A18,A19,A22,A24,A327,A331,GOBOARD6:85;
         LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0+1,j0+1)),
          1/2*((GoB f)*(i1,j0)+(GoB f)*(i1+1,j0+1))) misses L~f
       by A5,A6,A11,A14,A17,A18,A20,A21,A24,A25,A43,A44,A327,A328,A331,
GOBOARD8:14;
     then LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0+1,j0+1)),
          1/2*((GoB f)*(i1,j0)+(GoB f)*
(i1+1,j0+1))) c= (L~f)` by SUBSET_1:43;
     then A335: LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0+1,j0+1)),
          1/2*((GoB f)*(i1,j0)+(GoB f)*(i1+1,j0+1))) c= LeftComp f
                             by A3,A5,A6,A48,A330,A333,A334,Th6,NAT_1:13;
A336: LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0+1,j0+1)),
          1/2*((GoB f)*(i1,j0)+(GoB f)*(i1+1,j0+1))) meets
             Int cell(GoB f,i1,j1) by A19,A20,A21,A24,A327,A328,A331,
GOBOARD6:85;
         Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
     then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
    hence Int left_cell(f,k+1) c= LeftComp f by A48,A49,A332,A335,A336,Th6;
    end;
    suppose that
A337: i0+1 = i1 and
A338: j1+1 = j2;
         left_cell(f,k) = cell(GoB f,i0,j1)
                by A6,A7,A10,A11,A13,A14,A24,A39,A337,GOBOARD5:29
        .= left_cell(f,k+1) by A4,A5,A13,A14,A16,A17,A25,A337,A338,GOBOARD5:28
;
    hence Int left_cell(f,k+1) c= LeftComp f by A3,A5,A6,NAT_1:13;
    end;
    suppose that
A339: j0+1 = j1 and
A340: i1+1 = i2 and
A341: i0 = 1 and
A342: j1 = width GoB f;
A343: left_cell(f,k) = cell(GoB f,i1-'1,j0)
                by A6,A7,A10,A11,A13,A14,A24,A38,A339,GOBOARD5:28;
A344: i0-'1 = 0 by A341,BINARITH:51;
A345: j1-'1 = j0 by A339,BINARITH:39;
A346: LSeg(1/2*((GoB f)*(1,j0)+(GoB f)*(1,j1))- |[1,0]|,
         (GoB f)*(1,j1)+|[-1,1]|) is connected by Th8;
A347: LSeg(1/2*((GoB f)*(1,j0)+(GoB f)*(1,j1))- |[1,0]|,
         (GoB f)*(1,j1)+|[-1,1]|) meets Int cell(GoB f,i1-'1,j0)
         by A21,A22,A24,A339,A344,GOBOARD6:88;
        LSeg(1/2*((GoB f)*(1,j0)+(GoB f)*(1,j1))- |[1,0]|,
         (GoB f)*(1,j1)+|[-1,1]|) misses L~f by A342,A345,GOBOARD8:33;
     then LSeg(1/2*((GoB f)*(1,j0)+(GoB f)*(1,j1))- |[1,0]|,
         (GoB f)*(1,j1)+|[-1,1]|) c= (L~f)` by SUBSET_1:43;
     then A348: LSeg(1/2*((GoB f)*(1,j0)+(GoB f)*(1,j1))- |[1,0]|,
         (GoB f)*(1,j1)+|[-1,1]|) c= LeftComp f
                             by A3,A5,A6,A48,A343,A346,A347,Th6,NAT_1:13;
         i1-'1 <= i1 by BINARITH:52;
then A349:  i1-'1 <= len GoB f by A19,XXREAL_0:2;
     then A350: Int cell(GoB f,i1-'1,j1) is connected by A22,Th21;
A351: LSeg(1/2*((GoB f)*(1,j0)+(GoB f)*(1,j1))- |[1,0]|,
         (GoB f)*(1,j1)+|[-1,1]|) meets Int cell(GoB f,i0-'1,j1)
             by A342,A344,GOBOARD6:92;
         Int cell(GoB f,i1-'1,j1) misses L~f by A22,A349,GOBOARD7:14;
     then Int cell(GoB f,i1-'1,j1) c= (L~f)` by SUBSET_1:43;
     then A352:  Int cell(GoB f,i1-'1,j1) c= LeftComp f by A24,A48,A339,A348
,A350,A351,Th6;
A353: left_cell(f,k+1) = cell(GoB f,i1,j1)
                by A4,A5,A13,A14,A16,A17,A25,A339,A340,GOBOARD5:29;
A354: LSeg((GoB f)*(1,j1)+|[-1,1]|,
        1/2*((GoB f)*(1,j1)+(GoB f)*(2,j1))+|[0,1]|) is connected by Th8;
A355: LSeg((GoB f)*(1,j1)+|[-1,1]|,
        1/2*((GoB f)*(1,j1)+(GoB f)*(2,j1))+|[0,1]|)
           meets Int cell(GoB f,i1-'1,j1) by A24,A339,A342,A344,GOBOARD6:92
;
      LSeg((GoB f)*(1,j1)+|[-1,1]|,
        1/2*((GoB f)*(1,j1)+(GoB f)*(2,j1))+|[0,1]|) misses L~f
         by A342,GOBOARD8:29;
     then LSeg((GoB f)*(1,j1)+|[-1,1]|,
        1/2*((GoB f)*(1,j1)+(GoB f)*(2,j1))+|[0,1]|) c= (L~f)`
          by SUBSET_1:43;
     then A356: LSeg((GoB f)*(1,j1)+|[-1,1]|,
        1/2*((GoB f)*(1,j1)+(GoB f)*(2,j1))+|[0,1]|) c= LeftComp f
                             by A48,A352,A354,A355,Th6;
A357: LSeg((GoB f)*(1,j1)+|[-1,1]|,
        1/2*((GoB f)*(1,j1)+(GoB f)*(2,j1))+|[0,1]|) meets
             Int cell(GoB f,i1,j1) by A20,A24,A339,A340,A341,A342,GOBOARD6:86
;
         Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
     then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
    hence Int left_cell(f,k+1) c= LeftComp f by A48,A49,A353,A356,A357,Th6;
    end;
    suppose that
A358: j0+1 = j1 and
A359: i1+1 = i2 and
A360: i0 <> 1 and
A361: j1 = width GoB f;
A362: left_cell(f,k) = cell(GoB f,i1-'1,j0)
                by A6,A7,A10,A11,A13,A14,A24,A38,A358,GOBOARD5:28;
        1 < i0 by A18,A360,XXREAL_0:1;
then A363: 1 <= i0-'1 by A24,A38,A358,NAT_1:13;
A364: j1-'1 = j0 by A358,BINARITH:39;
A365: i1-'1+(1+1) = i2 by A38,A359;
A366: LSeg(1/2*((GoB f)*(i1-'1,j0)+(GoB f)*(i1,j1)),
          1/2*((GoB f)*(i1-'1,j1)+(GoB f)*
(i1,j1))+|[0,1]|) is connected by Th8;
A367: LSeg(1/2*((GoB f)*(i1-'1,j0)+(GoB f)*(i1,j1)),
          1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1))+|[0,1]|)
           meets Int cell(GoB f,i1-'1,j0)
           by A19,A21,A22,A24,A38,A358,A363,GOBOARD6:85;
         LSeg(1/2*((GoB f)*(i1-'1,j0)+(GoB f)*(i1,j1)),
          1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1))+|[0,1]|) misses L~f
        by A5,A6,A11,A14,A17,A20,A24,A25,A38,A43,A358,A361,A363,A364,A365,
GOBOARD8:9;
     then LSeg(1/2*((GoB f)*(i1-'1,j0)+(GoB f)*(i1,j1)),
          1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1))+|[0,1]|) c= (L~f)`
           by SUBSET_1:43;
     then A368: LSeg(1/2*((GoB f)*(i1-'1,j0)+(GoB f)*(i1,j1)),
          1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1))+|[0,1]|) c= LeftComp f
                             by A3,A5,A6,A48,A362,A366,A367,Th6,NAT_1:13;
         i1-'1 <= i1 by BINARITH:52;
then A369:  i1-'1 <= len GoB f by A19,XXREAL_0:2;
     then A370: Int cell(GoB f,i1-'1,j1) is connected by A22,Th21;
A371: LSeg(1/2*((GoB f)*(i1-'1,j0)+(GoB f)*(i1,j1)),
          1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1))+|[0,1]|) meets
             Int cell(GoB f,i0-'1,j1) by A18,A24,A38,A358,A361,A363,GOBOARD6:86
;
         Int cell(GoB f,i1-'1,j1) misses L~f by A22,A369,GOBOARD7:14;
     then Int cell(GoB f,i1-'1,j1) c= (L~f)` by SUBSET_1:43;
     then A372:  Int cell(GoB f,i1-'1,j1) c= LeftComp f by A24,A48,A358,A368
,A370,A371,Th6;
A373: left_cell(f,k+1) = cell(GoB f,i1,j1)
                by A4,A5,A13,A14,A16,A17,A25,A358,A359,GOBOARD5:29;
A374: LSeg(1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1))+|[0,1]|,
         1/2*((GoB f)*(i1,j1)+(GoB f)*(i2,j1))+|[0,1]|) is connected by Th8;
A375: LSeg(1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1))+|[0,1]|,
         1/2*((GoB f)*(i1,j1)+(GoB f)*(i2,j1))+|[0,1]|)
           meets Int cell(GoB f,i1-'1,j1) by A19,A24,A38,A358,A361,A363,
GOBOARD6:86;
        LSeg(1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1))+|[0,1]|,
         1/2*((GoB f)*(i1,j1)+(GoB f)*(i2,j1))+|[0,1]|) misses L~f
            by A20,A24,A38,A358,A361,A363,A365,GOBOARD8:28;
     then LSeg(1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1))+|[0,1]|,
         1/2*((GoB f)*(i1,j1)+(GoB f)*(i2,j1))+|[0,1]|) c= (L~f)`
          by SUBSET_1:43;
     then A376: LSeg(1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1))+|[0,1]|,
         1/2*((GoB f)*(i1,j1)+(GoB f)*(i2,j1))+|[0,1]|) c= LeftComp f
                             by A48,A372,A374,A375,Th6;
A377: LSeg(1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1))+|[0,1]|,
         1/2*((GoB f)*(i1,j1)+(GoB f)*(i2,j1))+|[0,1]|) meets
             Int cell(GoB f,i1,j1) by A19,A20,A359,A361,GOBOARD6:86;
         Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
     then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
    hence Int left_cell(f,k+1) c= LeftComp f by A48,A49,A373,A376,A377,Th6;
    end;
    suppose that
A378: j0+1 = j1 and
A379: i1+1 = i2 and
A380: i0 = 1 and
A381: j1 <> width GoB f;
A382: left_cell(f,k) = cell(GoB f,i1-'1,j0)
                by A6,A7,A10,A11,A13,A14,A24,A38,A378,GOBOARD5:28;
        width GoB f > j1 by A22,A381,XXREAL_0:1;
then A383: width GoB f >= j1+1 by NAT_1:13;
A384:  i0-'1 = 0 by A380,BINARITH:51;
A385: LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0,j1))- |[1,0]|,
          1/2*((GoB f)*(i0,j1)+(GoB f)*
(i0,j1+1))- |[1,0]|) is connected by Th8;
A386: LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0,j1))- |[1,0]|,
          1/2*((GoB f)*(i0,j1)+(GoB f)*(i0,j1+1))- |[1,0]|)
           meets Int cell(GoB f,i1-'1,j0)
            by A21,A22,A24,A378,A380,A384,GOBOARD6:88;
         LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0,j1))- |[1,0]|,
          1/2*((GoB f)*(i0,j1)+(GoB f)*(i0,j1+1))- |[1,0]|) misses L~f
         by A21,A46,A378,A380,A383,GOBOARD8:31;
     then LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0,j1))- |[1,0]|,
          1/2*((GoB f)*(i0,j1)+(GoB f)*(i0,j1+1))- |[1,0]|) c= (L~f)`
           by SUBSET_1:43;
     then A387: LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0,j1))- |[1,0]|,
          1/2*((GoB f)*(i0,j1)+(GoB f)*(i0,j1+1))- |[1,0]|) c= LeftComp f
                             by A3,A5,A6,A48,A382,A385,A386,Th6,NAT_1:13;
         i1-'1 <= i1 by BINARITH:52;
then A388:  i1-'1 <= len GoB f by A19,XXREAL_0:2;
     then A389: Int cell(GoB f,i1-'1,j1) is connected by A22,Th21;
A390: LSeg(1/2*((GoB f)*(i0,j0)+(GoB f)*(i0,j1))- |[1,0]|,
          1/2*((GoB f)*(i0,j1)+(GoB f)*(i0,j1+1))- |[1,0]|) meets
             Int cell(GoB f,i0-'1,j1) by A22,A380,A383,A384,GOBOARD6:88;
         Int cell(GoB f,i1-'1,j1) misses L~f by A22,A388,GOBOARD7:14;
     then Int cell(GoB f,i1-'1,j1) c= (L~f)` by SUBSET_1:43;
     then A391:  Int cell(GoB f,i1-'1,j1) c= LeftComp f by A24,A48,A378,A387
,A389,A390,Th6;
A392: left_cell(f,k+1) = cell(GoB f,i1,j1)
                by A4,A5,A13,A14,A16,A17,A25,A378,A379,GOBOARD5:29;
A393: LSeg(1/2*((GoB f)*(i1,j0+1)+(GoB f)*(i1,j0+2))- |[1,0]|,
          1/2*((GoB f)*(i1,j0+1)+(GoB f)*(i2,j0+2))) is connected by Th8;
A394: LSeg(1/2*((GoB f)*(i1,j0+1)+(GoB f)*(i1,j0+2))- |[1,0]|,
          1/2*((GoB f)*(i1,j0+1)+(GoB f)*(i2,j0+2)))
           meets Int cell(GoB f,i1-'1,j1)
           by A22,A24,A378,A380,A383,A384,GOBOARD6:88;
         LSeg(1/2*((GoB f)*(i1,j0+1)+(GoB f)*(i1,j0+2))- |[1,0]|,
          1/2*((GoB f)*(i1,j0+1)+(GoB f)*(i2,j0+2))) misses L~f
          by A5,A6,A11,A14,A17,A21,A24,A25,A43,A378,A379,A380,A383,GOBOARD8:18
;
     then LSeg(1/2*((GoB f)*(i1,j0+1)+(GoB f)*(i1,j0+2))- |[1,0]|,
          1/2*((GoB f)*(i1,j0+1)+(GoB f)*
(i2,j0+2))) c= (L~f)` by SUBSET_1:43;
     then A395: LSeg(1/2*((GoB f)*(i1,j0+1)+(GoB f)*(i1,j0+2))- |[1,0]|,
          1/2*((GoB f)*(i1,j0+1)+(GoB f)*(i2,j0+2))) c= LeftComp f
                             by A48,A391,A393,A394,Th6;
A396: LSeg(1/2*((GoB f)*(i1,j0+1)+(GoB f)*(i1,j0+2))- |[1,0]|,
          1/2*((GoB f)*(i1,j0+1)+(GoB f)*(i2,j0+2))) meets
             Int cell(GoB f,i1,j1) by A19,A20,A22,A378,A379,A383,GOBOARD6:85;
         Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
     then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
    hence Int left_cell(f,k+1) c= LeftComp f by A48,A49,A392,A395,A396,Th6;
    end;
    suppose that
A397: j0+1 = j1 and
A398: i1+1 = i2 and
A399: i0 <> 1 and
A400: j1 <> width GoB f;
A401: left_cell(f,k) = cell(GoB f,i1-'1,j0)
                by A6,A7,A10,A11,A13,A14,A24,A38,A397,GOBOARD5:28;
        1 < i0 by A18,A399,XXREAL_0:1;
then A402: 1 <= i0-'1 by A24,A38,A397,NAT_1:13;
        width GoB f > j1 by A22,A400,XXREAL_0:1;
then A403: width GoB f >= j1+1 by NAT_1:13;
A404: i1-'1+(1+1) = i2 by A38,A398;
A405: LSeg(1/2*((GoB f)*(i0-'1,j0)+(GoB f)*(i0,j1)),
          1/2*((GoB f)*(i0-'1,j1)+(GoB f)*(i0,j1+1))) is connected by Th8;
A406: LSeg(1/2*((GoB f)*(i0-'1,j0)+(GoB f)*(i0,j1)),
          1/2*((GoB f)*(i0-'1,j1)+(GoB f)*(i0,j1+1)))
           meets Int cell(GoB f,i1-'1,j0)
           by A19,A21,A22,A24,A38,A397,A402,GOBOARD6:85;
         LSeg(1/2*((GoB f)*(i0-'1,j0)+(GoB f)*(i0,j1)),
          1/2*((GoB f)*(i0-'1,j1)+(GoB f)*(i0,j1+1))) misses L~f
      by A5,A6,A11,A14,A17,A20,A21,A24,A25,A38,A43,A46,A397,A402,A403,A404,
GOBOARD8:3;
     then LSeg(1/2*((GoB f)*(i0-'1,j0)+(GoB f)*(i0,j1)),
          1/2*((GoB f)*(i0-'1,j1)+(GoB f)*
(i0,j1+1))) c= (L~f)` by SUBSET_1:43;
     then A407: LSeg(1/2*((GoB f)*(i0-'1,j0)+(GoB f)*(i0,j1)),
          1/2*((GoB f)*(i0-'1,j1)+(GoB f)*(i0,j1+1))) c= LeftComp f
                             by A3,A5,A6,A48,A401,A405,A406,Th6,NAT_1:13;
         i1-'1 <= i1 by BINARITH:52;
then A408:  i1-'1 <= len GoB f by A19,XXREAL_0:2;
     then A409: Int cell(GoB f,i1-'1,j1) is connected by A22,Th21;
A410: LSeg(1/2*((GoB f)*(i0-'1,j0)+(GoB f)*(i0,j1)),
          1/2*((GoB f)*(i0-'1,j1)+(GoB f)*(i0,j1+1))) meets
             Int cell(GoB f,i0-'1,j1) by A18,A22,A24,A38,A397,A402,A403,
GOBOARD6:85;
         Int cell(GoB f,i1-'1,j1) misses L~f by A22,A408,GOBOARD7:14;
     then Int cell(GoB f,i1-'1,j1) c= (L~f)` by SUBSET_1:43;
     then A411:  Int cell(GoB f,i1-'1,j1) c= LeftComp f by A24,A48,A397,A407
,A409,A410,Th6;
A412: left_cell(f,k+1) = cell(GoB f,i1,j1)
                by A4,A5,A13,A14,A16,A17,A25,A397,A398,GOBOARD5:29;
A413: LSeg(1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1+1)),
          1/2*((GoB f)*(i1,j1)+(GoB f)*(i2,j1+1))) is connected by Th8;
A414: LSeg(1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1+1)),
          1/2*((GoB f)*(i1,j1)+(GoB f)*(i2,j1+1)))
           meets Int cell(GoB f,i1-'1,j1) by A19,A22,A24,A38,A397,A402,A403,
GOBOARD6:85;
         LSeg(1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1+1)),
          1/2*((GoB f)*(i1,j1)+(GoB f)*(i2,j1+1))) misses L~f
      by A5,A6,A11,A14,A17,A20,A21,A24,A25,A38,A43,A46,A397,A402,A403,A404,
GOBOARD8:15;
     then LSeg(1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1+1)),
          1/2*((GoB f)*(i1,j1)+(GoB f)*(i2,j1+1))) c= (L~f)` by SUBSET_1:43;
     then A415: LSeg(1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1+1)),
          1/2*((GoB f)*(i1,j1)+(GoB f)*(i2,j1+1))) c= LeftComp f
                             by A48,A411,A413,A414,Th6;
A416: LSeg(1/2*((GoB f)*(i1-'1,j1)+(GoB f)*(i1,j1+1)),
          1/2*((GoB f)*(i1,j1)+(GoB f)*(i2,j1+1))) meets
             Int cell(GoB f,i1,j1) by A19,A20,A22,A398,A403,GOBOARD6:85;
         Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
     then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
    hence Int left_cell(f,k+1) c= LeftComp f by A48,A49,A412,A415,A416,Th6;
    end;
    suppose that
A417: j0+1 = j1 and
A418: j1+1 = j2 and
A419: i0 = 1;
A420: left_cell(f,k) = cell(GoB f,0,j0)
        by A6,A7,A10,A11,A13,A14,A24,A50,A417,A419,GOBOARD5:28;
A421: left_cell(f,k+1) = cell(GoB f,0,j1)
                by A4,A5,A13,A14,A16,A17,A24,A25,A50,A417,A418,A419,GOBOARD5:28
;
A422: LSeg(1/2*((GoB f)*(1,j0)+(GoB f)*(1,j1))- |[1,0]|,
          1/2*((GoB f)*(1,j1)+(GoB f)*(1,j2))- |[1,0]|) is connected by Th8;
A423: LSeg(1/2*((GoB f)*(1,j0)+(GoB f)*(1,j1))- |[1,0]|,
          1/2*((GoB f)*(1,j1)+(GoB f)*(1,j2))- |[1,0]|)
           meets Int cell(GoB f,0,j0) by A21,A22,A417,GOBOARD6:88;
         LSeg(1/2*((GoB f)*(1,j0)+(GoB f)*(1,j1))- |[1,0]|,
          1/2*((GoB f)*(1,j1)+(GoB f)*(1,j2))- |[1,0]|) misses L~f
         by A21,A23,A46,A417,A418,GOBOARD8:31;
     then LSeg(1/2*((GoB f)*(1,j0)+(GoB f)*(1,j1))- |[1,0]|,
               1/2*((GoB f)*(1,j1)+(GoB f)*(1,j2))- |[1,0]|) c= (L~f)`
           by SUBSET_1:43;
     then A424: LSeg(1/2*((GoB f)*(1,j0)+(GoB f)*(1,j1))- |[1,0]|,
          1/2*((GoB f)*(1,j1)+(GoB f)*(1,j2))- |[1,0]|) c= LeftComp f
                             by A3,A5,A6,A48,A420,A422,A423,Th6,NAT_1:13;
A425: LSeg(1/2*((GoB f)*(1,j0)+(GoB f)*(1,j1))- |[1,0]|,
          1/2*((GoB f)*(1,j1)+(GoB f)*(1,j2))- |[1,0]|) meets
             Int cell(GoB f,0,j1) by A22,A23,A418,GOBOARD6:88;
         Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
     then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
    hence Int left_cell(f,k+1) c= LeftComp f by A48,A49,A421,A424,A425,Th6;
    end;
    suppose that
A426: j0+1 = j1 and
A427: j1+1 = j2 and
A428: i0 <> 1;
A429: left_cell(f,k) = cell(GoB f,i1-'1,j0)
                by A6,A7,A10,A11,A13,A14,A24,A38,A426,GOBOARD5:28;
        1 < i0 by A18,A428,XXREAL_0:1;
then A430: 1 <= i0-'1 by A24,A38,A426,NAT_1:13;
A431: left_cell(f,k+1) = cell(GoB f,i1-'1,j1)
                by A4,A5,A13,A14,A16,A17,A25,A38,A427,GOBOARD5:28;
A432: LSeg(1/2*((GoB f)*(i0-'1,j0)+(GoB f)*(i0,j1)),
          1/2*((GoB f)*(i0-'1,j1)+(GoB f)*(i0,j2))) is connected by Th8;
A433: LSeg(1/2*((GoB f)*(i0-'1,j0)+(GoB f)*(i0,j1)),
          1/2*((GoB f)*(i0-'1,j1)+(GoB f)*(i0,j2)))
           meets Int cell(GoB f,i1-'1,j0)
             by A19,A21,A22,A24,A38,A426,A430,GOBOARD6:85;
         LSeg(1/2*((GoB f)*(i0-'1,j0)+(GoB f)*(i0,j1)),
          1/2*((GoB f)*(i0-'1,j1)+(GoB f)*(i0,j2))) misses L~f
       by A5,A6,A11,A14,A17,A18,A21,A23,A24,A25,A38,A43,A46,A426,A427,A430,
GOBOARD8:1;
     then LSeg(1/2*((GoB f)*(i0-'1,j0)+(GoB f)*(i0,j1)),
          1/2*((GoB f)*(i0-'1,j1)+(GoB f)*(i0,j2))) c= (L~f)` by SUBSET_1:43;
     then A434: LSeg(1/2*((GoB f)*(i0-'1,j0)+(GoB f)*(i0,j1)),
          1/2*((GoB f)*(i0-'1,j1)+(GoB f)*(i0,j2))) c= LeftComp f
                             by A3,A5,A6,A48,A429,A432,A433,Th6,NAT_1:13;
A435: LSeg(1/2*((GoB f)*(i0-'1,j0)+(GoB f)*(i0,j1)),
          1/2*((GoB f)*(i0-'1,j1)+(GoB f)*(i0,j2))) meets
             Int cell(GoB f,i0-'1,j1) by A18,A22,A23,A24,A38,A426,A427,A430,
GOBOARD6:85;
         Int left_cell(f,k+1) misses L~f by A4,A5,GOBOARD8:37;
     then Int left_cell(f,k+1) c= (L~f)` by SUBSET_1:43;
    hence Int left_cell(f,k+1) c= LeftComp f by A24,A48,A49,A426,A431,A434,A435
,Th6;
   end;
   end;
    hence Int left_cell(f,k+1) c= LeftComp f;
    end;
    end;
   end;
 thus for k holds P[k] from NAT_1:sch 1(A1,A2);
end;

theorem
     GoB Rev f = GoB f by Lm1;

theorem Th26:
 RightComp f = LeftComp Rev f
proof
      LeftComp Rev f is_a_component_of (L~Rev f)` by Def1;
then A1: LeftComp Rev f is_a_component_of (L~f)` by SPPOL_2:22;
   A2: len f >= 4 by GOBOARD7:36;
then A3: len f >= 1+1 by XXREAL_0:2;
       len f >= 1 by A2,XXREAL_0:2;
then A4: len f -' 1 + 1 = len f by BINARITH:53;
then A5: 1 <= len f -' 1 by A3,XREAL_1:8;
A6: len f -' 1 + 1 <= len Rev f by A4,FINSEQ_5:def 3;
      right_cell(f,1) = left_cell(Rev f,len f -' 1) by A4,A5,Th13;
  then Int right_cell(f,1) c= LeftComp Rev f by A5,A6,Th24;
 hence thesis by A1,Def2;
end;

theorem
     RightComp Rev f = LeftComp f
proof Rev Rev f = f by FINSEQ_6:29;
 hence thesis by Th26;
end;

theorem
     for k st 1 <= k & k+1 <= len f holds Int right_cell(f,k) c= RightComp f
proof let k such that
A1: 1 <= k & k+1 <= len f;
A2: len f = len Rev f by FINSEQ_5:def 3;
       k <= len f by A1,NAT_1:13;
   then A3:  len f-'k+k = len f by BINARITH:53;
then A4: 1 <= len f-'k & len f-'k+1 <= len f by A1,XREAL_1:8;
   then A5: right_cell(f,k) = left_cell(Rev f,len f-'k) by A1,A3,Th13;
      RightComp f = LeftComp Rev f by Th26;
 hence Int right_cell(f,k) c= RightComp f by A2,A4,A5,Th24;
end;
