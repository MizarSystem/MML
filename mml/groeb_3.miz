:: Construction of {G}r\"obner Bases: Avoiding S-Polynomials -- Buchberger's
:: First Criterium
::  by Christoph Schwarzweller
::
:: Received December 10, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies POLYNOM1, POLYNOM7, BOOLE, FUNCT_1, RELAT_1, VECTSP_1, RLVECT_1,
      ORDINAL1, LATTICES, ANPROJ_1, VECTSP_2, ARYTM_1, REALSET1, GROUP_1,
      CAT_3, BINOP_1, ARYTM_3, TERMORD, RELAT_2, DICKSON, FINSEQ_1, REWRITE1,
      TARSKI, FINSET_1, POLYRED, BAGORDER, ORDERS_1, WAYBEL_4, CARD_1, FUNCT_4,
      ALGSEQ_1, ALGSTR_1, GROEB_1, GROEB_2, FUNCOP_1, MCART_1, GROEB_3,
      CONNSP_3, FUNCT_7, QC_LANG1, NAT_1;
 notations TARSKI, SUBSET_1, RELAT_1, XBOOLE_0, RELAT_2, CARD_1, ORDINAL1,
      NUMBERS, XCMPLX_0, XXREAL_0, NAT_1, NAT_D, FUNCT_1, RELSET_1, FUNCT_2,
      FUNCT_4, PRE_CIRC, STRUCT_0, GROUP_1, ALGSTR_1, RLVECT_1, FINSET_1,
      MCART_1, REALSET2, FINSEQ_1, VECTSP_2, VECTSP_1, POLYNOM2, POLYNOM7,
      ORDERS_2, FUNCT_7, REWRITE1, BAGORDER, TERMORD, GROEB_1, POLYRED,
      GROEB_2, WAYBEL_4, POLYNOM1;
 constructors XXREAL_0, REAL_1, PRE_CIRC, REWRITE1, VECTSP_2, WAYBEL_4,
      POLYNOM2, BAGORDER, TERMORD, POLYRED, GROEB_1, GROEB_2, NAT_D;
 registrations STRUCT_0, FINSET_1, RELSET_1, VECTSP_1, ALGSTR_1, POLYNOM1,
      POLYNOM2, INT_1, BINOM, POLYNOM7, TERMORD, POLYRED, CARD_1, XREAL_0,
      POLYNOM4, XXREAL_0, SUBSET_1;
 requirements NUMERALS, REAL, SUBSET, BOOLE, ARITHM;
 definitions TARSKI;
 theorems TARSKI, VECTSP_1, POLYNOM1, REAL_1, RLVECT_1, NAT_1, INT_1, POLYNOM7,
      REWRITE1, XBOOLE_0, TERMORD, CARD_1, FUNCT_1, XBOOLE_1, POLYRED, FUNCT_2,
      RELAT_1, BAGORDER, ALGSTR_1, CARD_2, GROEB_1, FUNCT_4, FUNCOP_1,
      FINSET_1, VECTSP_2, RELSET_1, WAYBEL_4, MCART_1, GROEB_2, TRIANG_1,
      GROUP_1, FUNCT_7, POLYNOM2, FINSEQ_3, XREAL_1, XXREAL_0;
 schemes FUNCT_2, NAT_1, POLYNOM2;

begin :: Preliminaries

theorem Th1:
for X being set, b1,b2 being bag of X holds (b1 + b2) / b2 = b1
proof
let X be set, b1,b2 be bag of X;
b2 divides b1 + b2 by POLYNOM1:54;
then b2 + ((b1 + b2) / b2) = b1 + b2 by GROEB_2:def 1;
then (b2 + ((b1 + b2) / b2)) -' b2 = b1 by POLYNOM1:52;
hence thesis by POLYNOM1:52;
end;

theorem Th2:
for n being Ordinal,
    T being admissible TermOrder of n,
    b1,b2,b3 being bag of n
holds b1 <= b2,T implies b1 + b3 <= b2 + b3,T
proof
let n be Ordinal, T be admissible TermOrder of n, b1,b2,b3 be bag of n;
assume b1 <= b2,T;
then [b1,b2] in T by TERMORD:def 2;
then [b1+b3,b2+b3] in T by BAGORDER:def 7;
hence thesis by TERMORD:def 2;
end;

theorem Th3:
for n being Ordinal,
    T being TermOrder of n,
    b1,b2,b3 being bag of n
holds b1 <= b2,T & b2 < b3,T implies b1 < b3,T
proof
let n be Ordinal, T be TermOrder of n, b1,b2,b3 be bag of n;
assume A1: b1 <= b2,T & b2 < b3,T;
then A2: b2 <= b3,T & b2 <> b3 by TERMORD:def 3;
then A3: b1 <= b3,T by A1,TERMORD:8;
b1 <> b3 by A1,A2,TERMORD:7;
hence thesis by A3,TERMORD:def 3;
end;

theorem Th4:
for n being Ordinal,
    T being admissible TermOrder of n,
    b1,b2,b3 being bag of n
holds b1 < b2,T implies b1 + b3 < b2 + b3,T
proof
let n be Ordinal, T be admissible TermOrder of n, b1,b2,b3 be bag of n;
assume A1: b1 < b2,T;
then b1 <= b2,T & b1 <> b2 by TERMORD:def 3;
then [b1,b2] in T by TERMORD:def 2;
then [b1+b3,b2+b3] in T by BAGORDER:def 7;
then A2: b1+b3 <= b2+b3,T by TERMORD:def 2;
now assume A3: b1+b3 = b2+b3;
  b1 = (b1 + b3) -' b3 by POLYNOM1:52
    .= b2 by A3,POLYNOM1:52;
  hence contradiction by A1,TERMORD:def 3;
  end;
hence thesis by A2,TERMORD:def 3;
end;

theorem Th5:
for n being Ordinal, T being admissible TermOrder of n,
    b1,b2,b3,b4 being bag of n
holds (b1 < b2,T & b3 <= b4,T) implies b1 + b3 < b2 + b4,T
proof
let n be Ordinal, T be admissible TermOrder of n, b1,b2,b3,b4 be bag of n;
assume A1: b1 < b2,T & b3 <= b4,T;
then b1 <= b2,T & b1 <> b2 by TERMORD:def 3;
then [b1,b2] in T & [b3,b4] in T by A1,TERMORD:def 2;
then [b1+b3,b2+b3] in T & [b2+b3,b2+b4] in T by BAGORDER:def 7;
then A2: b1+b3 <= b2+b3,T & b2+b3 <= b2+b4,T by TERMORD:def 2;
then A3: b1+b3 <= b2+b4,T by TERMORD:8;
now assume A4: b1+b3 = b2+b4;
  b4 = b4 + b2 -' b2 & b3 = b3 + b2 -' b2 by POLYNOM1:52;
  then A5: b1+b4 = b2+b4 by A4,A2,TERMORD:7;
  b1 = b1 + b4 -' b4 & b2 = b2 + b4 -' b4 by POLYNOM1:52;
  hence contradiction by A1,A5,TERMORD:def 3;
  end;
hence thesis by A3,TERMORD:def 3;
end;

theorem Th6:
for n being Ordinal,
    T being admissible TermOrder of n,
    b1,b2,b3,b4 being bag of n
holds (b1 <= b2,T & b3 < b4,T) implies b1 + b3 < b2 + b4,T
proof
let n be Ordinal, T be admissible TermOrder of n, b1,b2,b3,b4 be bag of n;
assume A1: b1 <= b2,T & b3 < b4,T;
then b3 <= b4,T & b3 <> b4 by TERMORD:def 3;
then [b1,b2] in T & [b3,b4] in T by A1,TERMORD:def 2;
then [b1+b3,b2+b3] in T & [b2+b3,b2+b4] in T by BAGORDER:def 7;
then A2: b1+b3 <= b2+b3,T & b2+b3 <= b2+b4,T by TERMORD:def 2;
then A3: b1+b3 <= b2+b4,T by TERMORD:8;
now assume b1+b3 = b2+b4;
  then A4: b2+b4 = b2 +b3 by A2,TERMORD:7;
  b4 = b4 + b2 -' b2 & b3 = b3 + b2 -' b2 by POLYNOM1:52;
  hence contradiction by A1,A4,TERMORD:def 3;
  end;
hence thesis by A3,TERMORD:def 3;
end;

begin :: More on Polynomials

theorem Th7:
for n being Ordinal,
    L being add-associative right_complementable right_zeroed
            unital distributive domRing-like (non trivial doubleLoopStr),
    m1,m2 being non-zero Monomial of n,L
holds term(m1*'m2) = term(m1) + term(m2)
proof
let n be Ordinal,
    L be add-associative right_complementable right_zeroed
         unital distributive domRing-like (non trivial doubleLoopStr),
    m1,m2 be non-zero Monomial of n,L;
 consider T be connected TermOrder of n;
m1 <> 0_(n,L) & m2 <> 0_(n,L) by POLYNOM7:def 2;
then A1: HC(m1,T) <> 0.L & HC(m2,T) <> 0.L by TERMORD:17;
then reconsider a = coefficient(m1), b = coefficient(m2)
                   as non-zero Element of L by TERMORD:23;
coefficient(m1) <> 0.L & coefficient(m2) <> 0.L by A1,TERMORD:23;
then a * b <> 0.L by VECTSP_2:def 5;
then reconsider c = a * b as non-zero Element of L by RLVECT_1:def 13;
m1 = Monom(a,term(m1)) & m2 = Monom(b,term(m2)) by POLYNOM7:11;
then term(m1 *' m2) = term(Monom(c,term(m1)+term(m2))) by TERMORD:3;
hence thesis by POLYNOM7:10;
end;

theorem Th8:
for n being Ordinal,
    L being add-associative right_complementable right_zeroed
            unital distributive domRing-like (non trivial doubleLoopStr),
    p being Polynomial of n,L,
    m being non-zero Monomial of n,L,
    b being bag of n
holds b in Support(p) iff term(m) + b in Support(m*'p)
proof
let n be Ordinal,
    L be add-associative right_complementable right_zeroed
         unital distributive domRing-like (non trivial doubleLoopStr),
    p be Polynomial of n,L, m be non-zero Monomial of n,L, b be bag of n;
m <> 0_(n,L) by POLYNOM7:def 2;
then Support m <> {} by POLYNOM7:1;
then Support m = {term(m)} by POLYNOM7:7;
then term(m) in Support m by TARSKI:def 1;
then A1: m.term(m) <> 0.L by POLYNOM1:def 9;
A2: (m*'p).(term(m) + b) = m.term(m) * p.b by POLYRED:7;
A3: now assume b in Support p;
   then A4: p.b <> 0.L by POLYNOM1:def 9;
   A5: term(m) + b is Element of Bags n by POLYNOM1:def 14;
   (m*'p).(term(m) + b) <> 0.L by A1,A2,A4,VECTSP_2:def 5;
   hence term(m) + b in Support(m*'p) by A5,POLYNOM1:def 9;
end;
now assume term(m) + b in Support(m*'p);
  then m.term(m) * p.b <> 0.L by A2,POLYNOM1:def 9;
  then A6: p.b <> 0.L by VECTSP_1:36;
  b is Element of Bags n by POLYNOM1:def 14;
  hence b in Support p by A6,POLYNOM1:def 9;
  end;
hence thesis by A3;
end;

theorem Th9:
for n being Ordinal,
    L being add-associative right_complementable right_zeroed
            unital distributive domRing-like (non trivial doubleLoopStr),
    p being Polynomial of n,L,
    m being non-zero Monomial of n,L holds
Support(m*'p) = { term(m) + b where b is Element of Bags n : b in Support p }
proof
let n be Ordinal,
    L be add-associative right_complementable right_zeroed
         unital distributive domRing-like (non trivial doubleLoopStr),
    p be Polynomial of n,L, m be non-zero Monomial of n,L;
A1: Support(m*'p) c= {s + t where s,t is Element of Bags n :
                       s in Support m & t in Support p} by TERMORD:30;
m <> 0_(n,L) by POLYNOM7:def 2; then Support m <> {} by POLYNOM7:1;
then A2: Support m = {term(m)} by POLYNOM7:7;
A3: now let u be set;
   assume A4: u in Support(m*'p);
   then reconsider u' = u as Element of Bags n;
   u' in {s + t where s,t is Element of Bags n :
                       s in Support m & t in Support p} by A4,A1;
   then consider s,t being Element of Bags n such that
   A5: u' = s + t & s in Support m & t in Support p;
   u' = term(m) + t by A5,A2,TARSKI:def 1;
   hence u in {term(m)+b where b is Element of Bags n : b in Support p} by A5;
   end;
now let u be set;
  assume u in {term(m)+b where b is Element of Bags n : b in Support p};
  then consider t being Element of Bags n such that
  A6: u = term(m) + t & t in Support p;
  thus u in Support(m*'p) by Th8,A6;
  end;
hence thesis by A3,TARSKI:2;
end;

theorem Th10:
for n being Ordinal,
    L being add-associative right_complementable left_zeroed right_zeroed
            unital distributive domRing-like (non trivial doubleLoopStr),
    p being Polynomial of n,L,
    m being non-zero Monomial of n,L
holds card Support(p) = card Support(m*'p)
proof
let n be Ordinal,
    L be add-associative right_complementable left_zeroed right_zeroed
            unital distributive domRing-like (non trivial doubleLoopStr),
    p be Polynomial of n,L, m be non-zero Monomial of n,L;
A1: Support(m*'p) c= {s + t where s,t is Element of Bags n :
                       s in Support m & t in Support p} by TERMORD:30;
m <> 0_(n,L) by POLYNOM7:def 2; then Support m <> {} by POLYNOM7:1;
then A2: Support m = {term(m)} by POLYNOM7:7;
  defpred P[set,set] means $2 = term(m) + In($1,Bags n)@;
A3: for x being set st x in Support(p)
   ex y being set st y in Support(m*'p) & P[x,y]
   proof
   let x be set; assume A4:x in Support p; then
   reconsider x' = x as Element of Bags n;
A5: x' = In(x',Bags n) by FUNCT_7:def 1
     .= In(x',Bags n)@ by POLYNOM2:def 3;
   term(m) + x' in Support(m*'p) by A4,Th8;
   hence ex y being set st y in Support(m*'p) & P[x,y] by A5;
   end;
 consider T be admissible connected TermOrder of n;
consider f being Function of Support(p),Support(m*'p) such that
A6: for x being set st x in Support(p) holds P[x,f.x] from FUNCT_2:sch 1(A3);
A7: now assume A8: Support(m*'p) = {};
    now assume Support p <> {};
    then p <> 0_(n,L) by POLYNOM7:1;
    then reconsider p' = p as non-zero Polynomial of n,L by POLYNOM7:def 2;
    HT(m,T) + HT(p',T) in Support(m*'p') by TERMORD:29;
    hence contradiction by A8;
    end;
  hence Support(p) = {};
  end;
then A9: Support(p) c= dom f by FUNCT_2:def 1;
now let x1,x2 be set;
  assume A10: x1 in Support(p) & x2 in Support(p) & f.x1 = f.x2;
  then reconsider x1' = x1, x2' = x2 as Element of Bags n;
A11: x2' = In(x2',Bags n) by FUNCT_7:def 1
     .= In(x2',Bags n)@ by POLYNOM2:def 3;
  x1' = In(x1',Bags n) by FUNCT_7:def 1
     .= In(x1',Bags n)@ by POLYNOM2:def 3; then
  A12: term(m) + x1' = f.x2' by A10,A6 .= term(m) + x2' by A11,A10,A6;
  thus x1 = x2' + term(m) -' term(m) by A12,POLYNOM1:52
         .= x2 by POLYNOM1:52;
  end;
then f is one-to-one by A7,FUNCT_2:25;
then A13: Support(p),f.:Support(p) are_equipotent by A9,CARD_1:60;
A14: for u being set holds u in f.:Support(p) implies u in Support(m*'p);
now let u be set; assume A15: u in Support(m*'p);
  then reconsider u' = u as Element of Bags n;
  u' in {s + t where s,t is Element of Bags n :
                       s in Support m & t in Support p} by A15,A1;
  then consider s,t being Element of Bags n such that
  A16: u' = s + t & s in Support m & t in Support p;
A17: t = In(t,Bags n) by FUNCT_7:def 1
     .= In(t,Bags n)@ by POLYNOM2:def 3;
  u' = term(m) + t by A16,A2,TARSKI:def 1;
  then A18: u' = f.t by A16,A6,A17;
  t in dom f by A16,A7,FUNCT_2:def 1;
  hence u in f.:Support(p) by A18,FUNCT_1:def 12;
  end;
then f.:Support(p) = Support(m*'p) by A14,TARSKI:2;
hence thesis by A13,CARD_1:21;
end;

Lm1:for n being Ordinal, T being connected TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative left_unital right_unital distributive
            Field-like (non trivial doubleLoopStr),
    f be Polynomial of n,L, g be set, P be Subset of Polynom-Ring(n,L)
holds PolyRedRel(P,T) reduces f,g implies g is Polynomial of n,L
proof
let n be Ordinal, T be connected TermOrder of n,
    L be add-associative right_complementable right_zeroed
         commutative associative left_unital right_unital distributive
         Field-like (non trivial doubleLoopStr),
    f be Polynomial of n,L, g be set, P be Subset of Polynom-Ring(n,L);
set R = PolyRedRel(P,T); assume R reduces f,g;
then consider p being RedSequence of R such that
A1: p.1 = f & p.len p = g by REWRITE1:def 3;
len p > 0 by REWRITE1:def 2;
then A2: 1 <= len p by NAT_1:14;
then reconsider l = len p - 1 as Element of NAT by INT_1:18;
1 <= l + 1 & l + 1 <= len p by NAT_1:12;
then A3: l + 1 in dom p by FINSEQ_3:27;
set h = p.l; per cases;
suppose len p = 1; hence thesis by A1; end;
suppose len p <> 1;
  then 0 + 1 < l + 1 by A2,REAL_1:def 5;
  then 1 <= l & l <= l + 1 by NAT_1:13;
  then l in dom p by FINSEQ_3:27;
  then [h,g] in R by A1,A3,REWRITE1:def 2;
  then consider h',g' being set such that
  A4: [h,g] = [h',g'] &
      h' in (the carrier of Polynom-Ring(n,L)) \ {0_(n,L)} &
      g' in (the carrier of Polynom-Ring(n,L)) by RELSET_1:6;
    g = [h',g']`2 by A4,MCART_1:def 2 .= g' by MCART_1:def 2;
  hence g is Polynomial of n,L by A4,POLYNOM1:def 27;
end; end;

theorem Th11:
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_complementable right_zeroed
            (non trivial LoopStr)
holds Red(0_(n,L),T) = 0_(n,L)
proof
let n be Ordinal, T be connected TermOrder of n,
    L be add-associative right_complementable right_zeroed
         (non trivial LoopStr);
set e = 0_(n,L), h = HM(e,T);
HC(h,T) = HC(e,T) by TERMORD:27 .= e.(HT(e,T)) by TERMORD:def 7
    .= 0.L by POLYNOM1:81; then h = 0_(n,L) by TERMORD:17;
hence Red(e,T) = e - 0_(n,L) by TERMORD:def 9 .= 0_(n,L) by POLYRED:4;
end;

theorem Th12:
for n being Ordinal,
    L being Abelian add-associative right_zeroed right_complementable
            commutative unital distributive (non trivial doubleLoopStr),
    p,q being Polynomial of n,L
holds p - q = 0_(n,L) implies p = q
proof
let n be Ordinal,
    L be Abelian add-associative right_zeroed right_complementable
         commutative unital distributive (non trivial doubleLoopStr),
    p,q be Polynomial of n,L;
assume p - q = 0_(n,L);
hence q = q + (p - q) by POLYNOM1:82 .= q + (p + -q) by POLYNOM1:def 23
       .= (q + -q) + p by POLYNOM1:80 .= 0_(n,L) + p by POLYRED:3
       .= p by POLYRED:2;
end;

theorem Th13:
for X being set,
    L being add-associative right_zeroed right_complementable
            (non empty LoopStr)
holds -(0_(X,L)) = 0_(X,L)
proof
let X be set, L be add-associative right_zeroed right_complementable
              (non empty LoopStr);
set o = -(0_(X,L)), e = 0_(X,L);
A1: dom o = Bags X by FUNCT_2:def 1 .= dom e by FUNCT_2:def 1;
now let x be set;
  assume x in dom o; then reconsider b = x as bag of X by POLYNOM1:def 14;
  o.b = -(e.b) by POLYNOM1:def 22 .= -(0.L) by POLYNOM1:81
     .= 0.L by RLVECT_1:25 .= e.b by POLYNOM1:81;
  hence o.x = e.x;
  end;
hence thesis by A1,FUNCT_1:9;
end;

theorem Th14:
for X being set,
    L being add-associative right_zeroed right_complementable
            (non empty LoopStr),
    f being Series of X,L
holds 0_(X,L) - f = -f
proof
let X be set, L be add-associative right_zeroed right_complementable
         (non empty LoopStr), f be Series of X,L;
set p = 0_(X,L) - f;
A1: dom p = Bags X by FUNCT_2:def 1 .= dom(-f) by FUNCT_2:def 1;
now let x be set; assume x in dom p;
  then reconsider b = x as Element of Bags X;
  p.b = (0_(X,L) + -f).b by POLYNOM1:def 23
     .= 0_(X,L).b + (-f).b by POLYNOM1:def 21
     .= 0.L + (-f).b by POLYNOM1:81
     .= (-f).b by ALGSTR_1:def 5;
  hence p.x = (-f).x;
end;
hence thesis by A1,FUNCT_1:9;
end;

theorem Th15:
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_complementable right_zeroed
            (non trivial doubleLoopStr),
    p being Polynomial of n,L
holds p - Red(p,T) = HM(p,T)
proof
let n be Ordinal, T be connected TermOrder of n,
    L be add-associative right_complementable right_zeroed
         (non trivial doubleLoopStr),
    p be Polynomial of n,L;
thus p - Red(p,T)
   = (HM(p,T) + Red(p,T)) - Red(p,T) by TERMORD:38
  .= (HM(p,T) + Red(p,T)) + (-Red(p,T)) by POLYNOM1:def 23
  .= HM(p,T) + (Red(p,T) + (-Red(p,T))) by POLYNOM1:80
  .= HM(p,T) + 0_(n,L) by POLYRED:3
  .= HM(p,T) by POLYNOM1:82;
end;

registration
let n be Ordinal,
    L be add-associative right_complementable right_zeroed
         (non empty LoopStr),
    p be Polynomial of n,L;
cluster Support p -> finite;
coherence by POLYNOM1:def 10;
end;

definition
let n be Ordinal,
    L be right_zeroed add-associative right_complementable
         unital distributive (non trivial doubleLoopStr),
    p,q be Polynomial of n,L;
redefine func {p,q} -> Subset of Polynom-Ring(n,L);
coherence
 proof
  now let u be set; assume A1: u in {p,q};
   now per cases by A1,TARSKI:def 2;
   case u = p;
     hence u in the carrier of Polynom-Ring(n,L) by POLYNOM1:def 27;
     end;
   case u = q;
     hence u in the carrier of Polynom-Ring(n,L) by POLYNOM1:def 27;
     end;
   end;
   hence u in the carrier of Polynom-Ring(n,L);
   end;
 hence thesis by TARSKI:def 3;
 end;
end;

begin :: Restriction and Splitting of Polynomials

definition
let X be set,
    L be non empty ZeroStr,
    s be Series of X,L,
    Y be Subset of Bags X;
func s|Y -> Series of X,L equals
  s +* ((Support s \ Y) --> 0.L);
coherence
 proof
 set m = (Support s \ Y) --> 0.L; set r = s +* m;
 A1: dom s = Bags X by FUNCT_2:def 1;
 A2: dom m = Support s \ Y by FUNCOP_1:19;
 then A3: dom r = Bags X \/ (Support s \ Y)  by A1,FUNCT_4:def 1;
 now let u be set; assume u in Support s \ Y;
   then u in Support s by XBOOLE_0:def 4;
   hence u in Bags X;
   end;
 then Support s \ Y c= Bags X by TARSKI:def 3;
 then A4: dom r = Bags X by A3,XBOOLE_1:12;
 now let x be set; assume x in Bags X;
   then reconsider x' = x as Element of Bags X;
   now per cases;
   case A5: x' in dom m;
     then r.x' = m.x' by FUNCT_4:14
              .= 0.L by A5,A2,FUNCOP_1:13;
     hence r.x in the carrier of L;
     end;
   case not(x in dom m);
     then r.x' = s.x' by FUNCT_4:12;
     hence r.x in the carrier of L;
     end;
   end;
   hence r.x in the carrier of L;
 end;
 hence thesis by A4,FUNCT_2:5;
 end;
end;

Lm2:for X being set, L being non empty ZeroStr,
    s being Series of X,L, Y being Subset of Bags X
holds Support(s|Y) c= Support s
proof
let X be set, L be non empty ZeroStr,
    s be Series of X,L, Y be Subset of Bags X ;
set m = (Support s \ Y) --> 0.L; set r = s +* m;
A1: dom m = Support s \ Y by FUNCOP_1:19;
  let u be set; assume A2: u in Support(s|Y);
  then reconsider u' = u as Element of Bags X;
  A3: r.u' <> 0.L by A2,POLYNOM1:def 9;
  now per cases;
  case u' in dom m;
     hence u' in Support s by A1,XBOOLE_0:def 4;
    end;
  case not u' in dom m;
     hence s.u' <> 0.L by A3,FUNCT_4:12;
    end;
  end;
  hence u in Support s by POLYNOM1:def 9;
end;

registration
let n be Ordinal,
    L be non empty ZeroStr,
    p be Polynomial of n,L,
    Y be Subset of Bags n;
cluster p|Y -> finite-Support;
coherence
 proof
 A1: Support(p|Y) c= Support p by Lm2;
 Support p is finite by POLYNOM1:def 10;
 then Support(p|Y) is finite by A1,FINSET_1:13;
 hence thesis by POLYNOM1:def 10;
 end;
end;

theorem Th16:
for X being set,
    L being non empty ZeroStr,
    s being Series of X,L,
    Y being Subset of Bags X
holds Support s|Y = (Support s) /\ Y &
      for b being bag of X st b in Support s|Y holds (s|Y).b = s.b
proof
let X be set, L be non empty ZeroStr,
    s be Series of X,L, Y be Subset of Bags X;
set m = (Support s \ Y) --> 0.L; set r = s +* m;
A1: dom m = Support s \ Y by FUNCOP_1:19;
A2: now let u be set; assume u in Support s /\ Y;
   then A3: u in Support s & u in Y by XBOOLE_0:def 3;
   then reconsider u' = u as Element of Bags X;
   not(u in Support s \ Y) by A3,XBOOLE_0:def 4;
   then r.u' = s.u' by A1,FUNCT_4:12;
   then r.u' <> 0.L by A3,POLYNOM1:def 9;
   hence u in Support s|Y by POLYNOM1:def 9;
   end;
now let u be set; assume A4: u in Support(s|Y);
  then reconsider u' = u as Element of Bags X;
  A5: r.u' <> 0.L by A4,POLYNOM1:def 9;
  A6: dom m = Support s \ Y by FUNCOP_1:19;
  A7: now assume A8: u' in dom m;
     then r.u' = m.u' by FUNCT_4:14
              .= 0.L by A8,A6,FUNCOP_1:13;
     hence contradiction by A4,POLYNOM1:def 9;
     end;
  then s.u' <> 0.L by A5,FUNCT_4:12;
  then A9: u' in Support s by POLYNOM1:def 9;
  u' in Y by A6,A7,A9,XBOOLE_0:def 4;
  hence u in (Support s) /\ Y by A9,XBOOLE_0:def 3;
  end;
hence A10: Support s|Y = Support s /\ Y by A2,TARSKI:2;
now let b be bag of X; assume b in Support s|Y;
  then b in Support s & b in Y by A10,XBOOLE_0:def 3;
  then not(b in dom m) by A1,XBOOLE_0:def 4;
  hence (s|Y).b = s.b by FUNCT_4:12;
  end;
hence thesis;
end;

theorem
for X being set,
    L being non empty ZeroStr,
    s being Series of X,L,
    Y being Subset of Bags X
holds Support(s|Y) c= Support s by Lm2;

theorem
for X being set,
    L being non empty ZeroStr,
    s being Series of X,L
holds s|(Support s) = s & s|({} Bags X) = 0_(X,L)
proof
let X be set, L be non empty ZeroStr,
    s be Series of X,L; set r = s|(Support s);
A1: r = s +* ({} --> 0.L) by XBOOLE_1:37;
dom({} --> 0.L) = {} by FUNCOP_1:16;
hence r = s +* {} by A1,RELAT_1:64
       .= s by FUNCT_4:22;
set e = s|({} Bags X);
A2: dom((Support s) --> 0.L) = Support s by FUNCOP_1:19;
A3: dom e = Bags X by FUNCT_2:def 1 .= dom(0_(X,L)) by FUNCT_2:def 1;
now let u be set; assume u in dom e;
  then reconsider u' = u as Element of Bags X;
  now per cases;
  case A4: u' in Support s;
    then e.u' = ((Support s) --> 0.L).u' by A2,FUNCT_4:14
             .= 0.L by A4,FUNCOP_1:13;
    hence e.u' = 0_(X,L).u' by POLYNOM1:81;
    end;
  case A5: not(u' in Support s);
    then e.u' = s.u' by A2,FUNCT_4:12;
    then e.u' = 0.L by A5,POLYNOM1:def 9;
    hence e.u' = 0_(X,L).u' by POLYNOM1:81;
    end;
  end;
  hence e.u = 0_(X,L).u;
  end;
hence thesis by A3,FUNCT_1:9;
end;

definition
let n be Ordinal,
    T be connected TermOrder of n,
    L be add-associative right_zeroed right_complementable
         (non empty LoopStr),
    p be Polynomial of n,L,
    i be Element of NAT such that A1:i <= card(Support p);
func Upper_Support(p,T,i) -> finite Subset of Bags n means :Def2:
  it c= Support p & card it = i &
  for b,b' being bag of n
  st b in it & b' in Support p & b <= b',T holds b' in it;
existence
 proof
 defpred P[Element of NAT] means $1 > card(Support p) or
 ex M being finite Subset of Bags n
 st M c= Support p & card M = $1 &
    for b,b' being bag of n
    st b in M & b' in Support p & b <= b',T holds b' in M;
 A2: P[0]
     proof
     ex M being finite Subset of Bags n st M c= Support p & card M = 0 &
     for b,b' being bag of n
              st b in M & b' in Support p & b <= b',T holds b' in M
       proof
       set M = {} Bags n; take M;
       thus thesis by XBOOLE_1:2,CARD_1:47;
       end;
     hence thesis;
     end;
 A3: now let k be Element of NAT; assume A4: P[k];
     k + 1 > card(Support p) or
     ex M being finite Subset of Bags n
       st M c= Support p & card M = k + 1 &
          for b,b' being bag of n
          st b in M & b' in Support p & b <= b',T holds b' in M
       proof
       assume A5: not k + 1 > card(Support p);
       k <= k + 1 by NAT_1:11;
       then consider M1 being finite Subset of Bags n such that
       A6: M1 c= Support p & card M1 = k &
           for b,b' being bag of n
           st b in M1 & b' in Support p & b <= b',T holds b' in M1
         by A4,A5,XXREAL_0:2;
       A7: for u being set holds u in M1 implies u in Support p by A6;
       set G = Support p \ M1;
       now let u be set; assume u in G;
          then u in Support p by XBOOLE_0:def 4;
          hence u in Bags n;
          end;
       then reconsider G as Subset of Bags n by TARSKI:def 3;
       now assume G = {};
         then Support p c= M1 by XBOOLE_1:37;
         then for u being set holds u in Support p implies u in M1;
         then card Support p = k by A6,A7,TARSKI:2;
         hence contradiction by A5,NAT_1:16;
         end;
       then reconsider G as non empty finite Subset of Bags n;
       set R = RelStr(#Bags n,T#);
       consider x being Element of R such that
       A8: x in G & x is_maximal_wrt G, the InternalRel of R by BAGORDER:7;
       reconsider b = x as bag of n by POLYNOM1:def 14;
       A9: now let b' be bag of n; assume A10: b' in G;
         now per cases;
         case b' = b;
           hence b' <= b,T by TERMORD:6;
           end;
         case b' <> b;
           then not [b,b'] in T by A10,A8,WAYBEL_4:def 24;
           then not(b <= b',T) by TERMORD:def 2;
           then b' < b,T by TERMORD:5;
           hence b' <= b,T by TERMORD:def 3;
           end;
         end;
         hence b' <= b,T;
         end;
       set M = M1 \/ {b};
       now let u be set; assume u in {b};
         then u = b by TARSKI:def 1;
         hence u in Bags n;
         end;
       then {b} c= Bags n by TARSKI:def 3;
       then M c= Bags n \/ Bags n by XBOOLE_1:13;
       then reconsider M as finite Subset of Bags n;
       now let u be set; assume A11: u in M;
         now per cases by A11,XBOOLE_0:def 2;
         case u in M1;
           hence u in Support p by A6;
           end;
         case u in {b};
           then u in G by A8,TARSKI:def 1;
           hence u in Support p by XBOOLE_0:def 4;
           end;
         end;
         hence u in Support p;
         end;
       then A12: M c= Support p by TARSKI:def 3;
       not(b in M1) by A8,XBOOLE_0:def 4;
       then A13: card M = k + 1 by A6,CARD_2:54;
       now let b1,b2 being bag of n;
         assume A14: b1 in M & b2 in Support p & b1 <= b2,T;
         now per cases by A14,XBOOLE_0:def 2;
         case b1 in M1;
           then b2 in M1 by A14,A6;
           hence b2 in M by XBOOLE_0:def 2;
           end;
         case b1 in {b};
           then A15: b1 = b & b1 in G by A8,TARSKI:def 1;
           now per cases;
           case b2 = b1;
             hence b2 in M by A14;
             end;
           case b2 <> b1;
             then A16: b1 < b2,T by A14,TERMORD:def 3;
             now assume b2 in G;
               then b2 <= b1,T by A15,A9;
               hence contradiction by A16,TERMORD:5;
               end;
             then b2 in M1 by A14,XBOOLE_0:def 4;
             hence b2 in M by XBOOLE_0:def 2;
             end;
           end;
           hence b2 in M;
           end;
         end;
         hence b2 in M;
         end;
       hence ex M being finite Subset of Bags n
       st M c= Support p & card M = k + 1 &
          for b,b' being bag of n
          st b in M & b' in Support p & b <= b',T holds b' in M by A12,A13;
       end;
     hence P[k+1];
     end;
 A17: for k being Element of NAT holds P[k] from NAT_1:sch 1(A2,A3);
 thus thesis by A17,A1;
 end;
uniqueness
 proof
 let F1,F2 be finite Subset of Bags n;
 assume A18: F1 c= Support p & card F1 = i &
            for b,b' being bag of n
            st b in F1 & b' in Support p & b <= b',T holds b' in F1;
 assume A19: F2 c= Support p & card F2 = i &
            for b,b' being bag of n
            st b in F2 & b' in Support p & b <= b',T holds b' in F2;
 now let u be set; assume A20: u in F1;
   then reconsider u' = u as Element of Bags n;
   now assume A21: not(u' in F2);
     now let x be set; assume A22: x in F2;
       then reconsider x' = x as Element of Bags n;
       now per cases;
       case u' <= x',T;
         hence x' in F1 by A18,A19,A22,A20;
         end;
       case not(u' <= x',T);
         then x' < u',T by TERMORD:5;
         then x' <= u',T by TERMORD:def 3;
         hence x' in F1 by A21,A22,A19,A18,A20;
         end;
       end;
       hence x in F1;
       end;
     then F2 c= F1 by TARSKI:def 3;
     then F2 c< F1 by A20,A21,XBOOLE_0:def 8;
     hence contradiction by A18,A19,CARD_2:67;
     end;
   hence u in F2;
   end;
 then F1 c= F2 by TARSKI:def 3;
 hence thesis by A18,A19,TRIANG_1:3;
 end;
end;

definition
let n be Ordinal,
    T be connected TermOrder of n,
    L be add-associative right_zeroed right_complementable
         (non empty LoopStr),
    p be Polynomial of n,L,
    i be Element of NAT;
func Lower_Support(p,T,i) -> finite Subset of Bags n equals
  Support(p) \ Upper_Support(p,T,i);
coherence
 proof
 Support(p) \ Upper_Support(p,T,i) c= Support p by XBOOLE_1:36;
 hence thesis by XBOOLE_1:1;
 end;
end;

Lm3:for n being Ordinal, T being connected TermOrder of n,
    L being add-associative right_zeroed right_complementable
            (non empty LoopStr),
    p being Polynomial of n,L, i being Element of NAT st i <= card(Support p)
holds Lower_Support(p,T,i) c= Support p by XBOOLE_1:36;

theorem Th19:
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_zeroed right_complementable
            (non empty LoopStr),
    p being Polynomial of n,L,
    i being Element of NAT st i <= card(Support p)
holds Upper_Support(p,T,i) \/ Lower_Support(p,T,i) = Support p &
      Upper_Support(p,T,i) /\ Lower_Support(p,T,i) = {}
proof
let n be Ordinal, T be connected TermOrder of n,
    L be add-associative right_zeroed right_complementable
         (non empty LoopStr), p be Polynomial of n,L, i be Element of NAT;
assume i <= card(Support p);
then A1: Upper_Support(p,T,i) c= Support p by Def2;
thus Upper_Support(p,T,i) \/ Lower_Support(p,T,i)
   = Upper_Support(p,T,i) \/ Support p by XBOOLE_1:39
  .= Support p by A1,XBOOLE_1:12;
set M = Upper_Support(p,T,i) /\ (Support(p)\Upper_Support(p,T,i));
now assume A2: M <> {};
  consider x being Element of M;
  x in Upper_Support(p,T,i) &
      x in Support(p)\Upper_Support(p,T,i) by A2,XBOOLE_0:def 3;
  hence contradiction by XBOOLE_0:def 4;
  end;
hence thesis;
end;

theorem Th20:
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_zeroed right_complementable
            (non empty LoopStr),
    p being Polynomial of n,L,
    i being Element of NAT st i <= card(Support p)
for b,b' being bag of n
st b in Upper_Support(p,T,i) & b' in Lower_Support(p,T,i) holds b' < b,T
proof
let n be Ordinal, T be connected TermOrder of n,
    L be add-associative right_zeroed right_complementable
         (non empty LoopStr), p be Polynomial of n,L, i be Element of NAT;
assume A1: i <= card(Support p);
let b,b' being bag of n;
assume A2: b in Upper_Support(p,T,i) & b' in Lower_Support(p,T,i);
A3: Lower_Support(p,T,i) c= Support p by A1,Lm3;
now assume b <= b',T;
  then b' in Upper_Support(p,T,i) by A2,A3,A1,Def2; then
  b' in Upper_Support(p,T,i) /\ Lower_Support(p,T,i) by A2,XBOOLE_0:def 3;
  hence contradiction by A1,Th19;
  end;
hence thesis by TERMORD:5;
end;

theorem
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_zeroed right_complementable
            (non empty LoopStr),
    p being Polynomial of n,L
holds Upper_Support(p,T,0) = {} & Lower_Support(p,T,0) = Support p
proof
let n be Ordinal, T be connected TermOrder of n,
    L be add-associative right_zeroed right_complementable
          (non empty LoopStr), p be Polynomial of n,L;
set u = Upper_Support(p,T,0), l = Lower_Support(p,T,0);
0 <= card(Support p);
then card u = 0 by Def2;
then u,0 are_equipotent by CARD_1:def 5;
hence u = {} by CARD_1:46;
hence l = Support p;
end;

theorem Th22:
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_zeroed right_complementable
            (non empty LoopStr),
    p being Polynomial of n,L
holds Upper_Support(p,T,card(Support p)) = Support p &
      Lower_Support(p,T,card(Support p)) = {}
proof
let n be Ordinal, T be connected TermOrder of n,
    L be add-associative right_zeroed right_complementable
          (non empty LoopStr), p be Polynomial of n,L;
set u = Upper_Support(p,T,card(Support p)),
    l = Lower_Support(p,T,card(Support p));
u c= Support p & card u = card(Support p) by Def2;
hence u = Support p by TRIANG_1:3;
hence l = {} by XBOOLE_1:37;
end;

theorem Th23:
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_zeroed right_complementable
            (non trivial LoopStr),
    p being non-zero Polynomial of n,L,
    i being Element of NAT st 1 <= i & i <= card(Support p)
holds HT(p,T) in Upper_Support(p,T,i)
proof
let n be Ordinal, T be connected TermOrder of n,
    L be add-associative right_zeroed right_complementable
     (non trivial LoopStr), p be non-zero Polynomial of n,L,
     i be Element of NAT;
assume A1: 1 <= i & i <= card(Support p);
set u = Upper_Support(p,T,i);
A2: u c= Support p & card u = i &
   for b,b' being bag of n
   st b in u & b' in Support p & b <= b',T holds b' in u by A1,Def2;
consider x being Element of u;
A3: u <> {} by A1,Def2,CARD_1:78;
then A4: x in u;
then reconsider x' = x as Element of Bags n;
p <> 0_(n,L) by POLYNOM7:def 2;
then Support p <> {} by POLYNOM7:1;
then A5: HT(p,T) in Support p by TERMORD:def 6;
x' <= HT(p,T),T by A2,A4,TERMORD:def 6;
hence HT(p,T) in u by A1,A3,A5,Def2;
end;

theorem Th24:
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_zeroed right_complementable
            (non empty LoopStr),
    p being Polynomial of n,L,
    i being Element of NAT st i <= card(Support p)
holds Lower_Support(p,T,i) c= Support p &
      card Lower_Support(p,T,i) = card(Support p) - i &
      for b,b' being bag of n
      st b in Lower_Support(p,T,i) & b' in Support p & b' <= b,T
      holds b' in Lower_Support(p,T,i)
proof
let n be Ordinal, T be connected TermOrder of n,
    L be add-associative right_zeroed right_complementable
         (non empty LoopStr), p be Polynomial of n,L, i being Element of NAT;
assume A1: i <= card Support p;
set l = Lower_Support(p,T,i);
thus l c= Support p by A1,Lm3;
Upper_Support(p,T,i) c= Support p by A1,Def2;
hence card l = card(Support p)-card(Upper_Support(p,T,i)) by CARD_2:63
            .= card(Support p) - i by A1,Def2;
now let b,b' be bag of n;
  assume A2: b in Lower_Support(p,T,i) & b' in Support p & b' <= b,T;
  then A3: b' in Upper_Support(p,T,i) \/ Lower_Support(p,T,i) by A1,Th19;
  now assume not(b' in Lower_Support(p,T,i));
    then b' in Upper_Support(p,T,i) by A3,XBOOLE_0:def 2;
    then b < b',T by A2,A1,Th20;
    hence contradiction by A2,TERMORD:5;
    end;
  hence b' in Lower_Support(p,T,i);
  end;
hence thesis;
end;

definition
let n be Ordinal,
    T be connected TermOrder of n,
    L be add-associative right_zeroed right_complementable
         (non empty LoopStr),
    p be Polynomial of n,L,
    i be Element of NAT;
func Up(p,T,i) -> Polynomial of n,L equals
  p|(Upper_Support(p,T,i));
coherence;
func Low(p,T,i) -> Polynomial of n,L equals
  p|(Lower_Support(p,T,i));
coherence;
end;

Lm4:for n being Ordinal, T being connected TermOrder of n,
    L being add-associative right_zeroed right_complementable
            (non empty LoopStr), p being Polynomial of n,L,
    i being Element of NAT st i <= card(Support p)
holds Support(p|(Upper_Support(p,T,i))) = Upper_Support(p,T,i) &
      Support(p|(Lower_Support(p,T,i))) = Lower_Support(p,T,i)
proof
let n be Ordinal, T be connected TermOrder of n,
    L be add-associative right_zeroed right_complementable
         (non empty LoopStr), p be Polynomial of n,L, i be Element of NAT;
assume A1: i <= card(Support p);
set u = Upper_Support(p,T,i),
    pu = p|(Upper_Support(p,T,i));
A2: Support pu = (Support p) /\ u by Th16;
u c= Support p by A1,Def2;
hence Support pu = u by A2,XBOOLE_1:28;
set l = Lower_Support(p,T,i),
    pl = p|(Lower_Support(p,T,i));
A3: Support pl = (Support p) /\ l by Th16;
l c= Support p by A1,Lm3;
hence Support pl = l by A3,XBOOLE_1:28;
end;

theorem Th25:
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_zeroed right_complementable
            (non empty LoopStr),
    p being Polynomial of n,L,
    i being Element of NAT st i <= card(Support p)
holds Support Up(p,T,i) = Upper_Support(p,T,i) &
      Support Low(p,T,i) = Lower_Support(p,T,i) by Lm4;

theorem Th26:
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_zeroed right_complementable
            (non empty LoopStr),
    p being Polynomial of n,L,
    i being Element of NAT st i <= card(Support p)
holds Support(Up(p,T,i)) c= Support(p) & Support(Low(p,T,i)) c= Support(p)
proof
let n be Ordinal, T be connected TermOrder of n,
    L be add-associative right_zeroed right_complementable
         (non empty LoopStr), p be Polynomial of n,L, i be Element of NAT;
assume A1: i <= card(Support p);
Support(p|(Upper_Support(p,T,i))) = Upper_Support(p,T,i) &
     Support(p|(Lower_Support(p,T,i))) = Lower_Support(p,T,i) by A1,Lm4;
hence thesis by A1,Def2,Th24;
end;

theorem Th27:
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_complementable right_zeroed
            (non trivial LoopStr),
    p being Polynomial of n,L,
    i being Element of NAT st 1 <= i & i <= card(Support p)
holds Support(Low(p,T,i)) c= Support(Red(p,T))
proof
let n be Ordinal, T be connected TermOrder of n,
    L be add-associative right_complementable right_zeroed
         (non trivial LoopStr), p be Polynomial of n,L, i be Element of NAT;
assume A1: 1 <= i & i <= card(Support p);
now assume Support p = {};
  then card(Support p) = 0 by CARD_1:def 5;
  hence contradiction by A1;
  end;
then p <> 0_(n,L) by POLYNOM7:1;
then reconsider p as non-zero Polynomial of n,L by POLYNOM7:def 2;
set l = Low(p,T,i), sl = Lower_Support(p,T,i);
Support(Low(p,T,i)) c= Support(p) by A1,Th26; then
Support(Low(p,T,i)) \ {HT(p,T)} c= Support(p) \ {HT(p,T)}
       by XBOOLE_1:33; then
A2: Support(Low(p,T,i)) \ {HT(p,T)} c= Support(Red(p,T))
       by TERMORD:36;
A3: now assume A4: HT(p,T) in sl;
    HT(p,T) in Upper_Support(p,T,i) by A1,Th23;
    then HT(p,T) in Upper_Support(p,T,i) /\ sl by A4,XBOOLE_0:def 3;
    hence contradiction by A1,Th19;
    end;
now assume A5: {HT(p,T)} /\ sl <> {};
  consider u being Element of {HT(p,T)} /\ sl;
  u in {HT(p,T)} & u in sl by A5,XBOOLE_0:def 3;
  hence contradiction by A3,TARSKI:def 1;
  end;
then {HT(p,T)} misses sl by XBOOLE_0:def 7;
then sl \ {HT(p,T)} = sl by XBOOLE_1:83 .= Support(Low(p,T,i)) by A1,Th25;
hence thesis by A2,A1,Th25;
end;

theorem Th28:
for n be Ordinal,
    T be connected TermOrder of n,
    L be add-associative right_zeroed right_complementable
         (non empty LoopStr),
    p be Polynomial of n,L,
    i be Element of NAT st i <= card(Support p)
for b being bag of n st b in Support p
holds (b in Support Up(p,T,i) or b in Support Low(p,T,i)) &
      not(b in Support Up(p,T,i) /\ Support Low(p,T,i))
proof
let n be Ordinal, T be connected TermOrder of n,
    L be add-associative right_zeroed right_complementable
         (non empty LoopStr), p be Polynomial of n,L, i be Element of NAT;
assume A1: i <= card(Support p);
let b being bag of n;
assume A2: b in Support p;
Support p = Upper_Support(p,T,i) \/ Lower_Support(p,T,i) by A1,Th19
         .= Support Up(p,T,i) \/ Lower_Support(p,T,i) by A1,Th25
         .= Support Up(p,T,i) \/ Support Low(p,T,i) by A1,Th25;
hence b in Support Up(p,T,i) or b in Support Low(p,T,i) by A2,XBOOLE_0:def 2;
Support Up(p,T,i) = Upper_Support(p,T,i) &
   Support Low(p,T,i) = Lower_Support(p,T,i) by A1,Th25;
hence thesis by A1,Th19;
end;

theorem Th29:
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_zeroed right_complementable
            (non empty LoopStr),
    p being Polynomial of n,L,
    i being Element of NAT st i <= card(Support p)
for b,b' being bag of n
st b in Support Low(p,T,i) & b' in Support Up(p,T,i) holds b < b',T
proof
let n be Ordinal, T be connected TermOrder of n,
    L be add-associative right_zeroed right_complementable
         (non empty LoopStr), p be Polynomial of n,L, i be Element of NAT;
assume A1: i <= card(Support p);
let b,b' be bag of n ;
assume A2: b in Support Low(p,T,i) & b' in Support Up(p,T,i);
Support Up(p,T,i) = Upper_Support(p,T,i) &
Support Low(p,T,i) = Lower_Support(p,T,i) by A1,Th25;
hence thesis by A2,A1,Th20;
end;

theorem Th30:
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_zeroed right_complementable
            (non empty LoopStr),
    p being Polynomial of n,L,
    i being Element of NAT st 1 <= i & i <= card(Support p)
holds HT(p,T) in Support Up(p,T,i)
proof
let n be Ordinal, T be connected TermOrder of n,
    L be add-associative right_zeroed right_complementable
         (non empty LoopStr), p be Polynomial of n,L, i be Element of NAT;
assume A1: 1 <= i & i <= card(Support p);
set u = Up(p,T,i);
A2: Support u = Upper_Support(p,T,i) by A1,Lm4;
then A3: card (Support u) <> 0 by A1,Def2;
consider x being Element of Support u;
A4: Support u <> {} by A3,CARD_1:def 5;
then A5: x in Support u;
then reconsider x as Element of Bags n;
A6: Support u c= Support p by A1,A2,Def2;
now assume Support p = {};
  then card(Support p) = 0 by CARD_1:def 5;
  hence contradiction by A1;
  end;
then A7: HT(p,T) in Support p by TERMORD:def 6;
x <= HT(p,T),T by A5,A6,TERMORD:def 6;
hence thesis by A4,A7,A1,A2,Def2;
end;

theorem Th31:
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_zeroed right_complementable
            (non empty LoopStr),
    p being Polynomial of n,L,
    i being Element of NAT st i <= card(Support p)
for b being bag of n st b in Support Low(p,T,i)
holds Low(p,T,i).b = p.b & Up(p,T,i).b = 0.L
proof
let n be Ordinal, T be connected TermOrder of n,
    L be add-associative right_zeroed right_complementable
         (non empty LoopStr), p be Polynomial of n,L, i be Element of NAT;
assume A1: i <= card(Support p);
let b be bag of n;
assume A2: b in Support Low(p,T,i);
set l = Lower_Support(p,T,i);
A3: b in l by A2,A1,Th25;
Support Low(p,T,i) c= Support p by A1,Th26;
then b in Support p /\ l by A2,A3,XBOOLE_0:def 3;
then b in Support p|l by Th16;
hence Low(p,T,i).b = p.b by Th16;
l /\ Upper_Support(p,T,i) = {} by A1,Th19;
then not(b in Upper_Support(p,T,i)) by A3,XBOOLE_0:def 3;
then A4: not(b in Support(Up(p,T,i))) by A1,Th25;
b is Element of Bags n by POLYNOM1:def 14;
hence thesis by A4,POLYNOM1:def 9;
end;

theorem Th32:
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_zeroed right_complementable
            (non empty LoopStr),
    p being Polynomial of n,L,
    i being Element of NAT st i <= card(Support p)
for b being bag of n st b in Support Up(p,T,i)
holds Up(p,T,i).b = p.b & Low(p,T,i).b = 0.L
proof
let n be Ordinal, T be connected TermOrder of n,
    L be add-associative right_zeroed right_complementable
         (non empty LoopStr), p be Polynomial of n,L, i be Element of NAT;
assume A1: i <= card(Support p);
let b be bag of n;
assume A2: b in Support Up(p,T,i);
set u = Upper_Support(p,T,i);
A3: b in u by A2,A1,Th25;
Support Up(p,T,i) c= Support p by A1,Th26;
then b in Support p /\ u by A2,A3,XBOOLE_0:def 3;
then b in Support p|u by Th16;
hence Up(p,T,i).b = p.b by Th16;
u /\ Lower_Support(p,T,i) = {} by A1,Th19;
then not(b in Lower_Support(p,T,i)) by A3,XBOOLE_0:def 3;
then A4: not(b in Support(Low(p,T,i))) by A1,Th25;
b is Element of Bags n by POLYNOM1:def 14;
hence thesis by A4,POLYNOM1:def 9;
end;

theorem Th33:
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_zeroed right_complementable
            (non empty LoopStr),
    p being Polynomial of n,L,
    i being Element of NAT st i <= card(Support p)
holds Up(p,T,i) + Low(p,T,i) = p
proof
let n be Ordinal, T be connected TermOrder of n,
    L be add-associative right_zeroed right_complementable
         (non empty LoopStr), p be Polynomial of n,L, i be Element of NAT;
assume A1: i <= card(Support p);
set u = Up(p,T,i) + Low(p,T,i);
A2: now let x be set; assume A3: x in Support u;
    A4: Support u c= Support Up(p,T,i) \/ Support Low(p,T,i) by POLYNOM1:79;
    Support Up(p,T,i) c= Support p
                & Support Low(p,T,i) c= Support p by A1,Th26;
    then Support Up(p,T,i) \/ Support Low(p,T,i) c= Support p by XBOOLE_1:8;
    then Support u c= Support p by A4,XBOOLE_1:1;
    hence x in Support p by A3;
    end;
now let x be set; assume A5: x in Support p;
  then reconsider x' = x as Element of Bags n;
  A6: u.x' = Up(p,T,i).x' + Low(p,T,i).x' by POLYNOM1:def 21;
  A7: p.x' <> 0.L by A5,POLYNOM1:def 9;
  now per cases by Th28,A5,A1;
  case A8: x' in Support Up(p,T,i);
    hence u.x' = Up(p,T,i).x' + 0.L by A6,A1,Th32
              .= Up(p,T,i).x' by RLVECT_1:def 7
              .= p.x' by A1,A8,Th32;
    end;
  case A9: x' in Support Low(p,T,i);
    hence u.x' = 0.L + Low(p,T,i).x' by A6,A1,Th31
              .= Low(p,T,i).x' by ALGSTR_1:def 5
              .= p.x' by A1,A9,Th31;
    end;
  end;
  hence x in Support u by A7,POLYNOM1:def 9;
  end;
then A10: Support u = Support p by A2,TARSKI:2;
A11: dom p = Bags n by FUNCT_2:def 1 .= dom u by FUNCT_2:def 1;
now let x be set; assume x in dom p;
  then reconsider x' = x as Element of Bags n;
  A12: u.x' = Up(p,T,i).x' + Low(p,T,i).x' by POLYNOM1:def 21;
  now per cases;
  case A13: x' in Support p;
    now per cases by Th28,A13,A1;
    case A14: x' in Support Up(p,T,i);
      hence u.x' = Up(p,T,i).x' + 0.L by A12,A1,Th32
                .= Up(p,T,i).x' by RLVECT_1:def 7
                .= p.x' by A1,A14,Th32;
      end;
    case A15: x' in Support Low(p,T,i);
      hence u.x' = 0.L + Low(p,T,i).x' by A12,A1,Th31
                .= Low(p,T,i).x' by ALGSTR_1:def 5
                .= p.x' by A1,A15,Th31;
      end;
    end;
    hence p.x' = u.x';
    end;
  case A16: not x' in Support p;
    hence p.x' = 0.L by POLYNOM1:def 9
              .= u.x' by A16,A10,POLYNOM1:def 9;
    end;
  end;
  hence p.x = u.x;
  end;
hence thesis by A11,FUNCT_1:9;
end;

theorem Th34:
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_zeroed right_complementable
            (non empty LoopStr),
    p being Polynomial of n,L
holds Up(p,T,0) = 0_(n,L) & Low(p,T,0) = p
proof
let n be Ordinal, T be connected TermOrder of n,
    L be add-associative right_zeroed right_complementable
         (non empty LoopStr), p be Polynomial of n,L;
set u = Up(p,T,0), l = Low(p,T,0);
A1: 0 <= card(Support p);
Support u = Upper_Support(p,T,0) by A1,Lm4;
then card(Support u) = 0 by A1,Def2;
then Support u,0 are_equipotent by CARD_1:def 5;
then Support u = {} by CARD_1:46;
hence u = 0_(n,L) by POLYNOM7:1;
then 0_(n,L) + l = p by A1,Th33;
hence thesis by POLYRED:2;
end;

theorem Th35:
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_zeroed right_complementable
            Abelian (non empty doubleLoopStr),
    p being Polynomial of n,L
holds Up(p,T,card(Support p)) = p & Low(p,T,card(Support p)) = 0_(n,L)
proof
let n be Ordinal, T be connected TermOrder of n,
    L be add-associative right_zeroed right_complementable
         Abelian (non empty doubleLoopStr), p be Polynomial of n,L;
set u = Up(p,T,card(Support p)), l = Low(p,T,card(Support p));
Support u = (Support p) /\ Upper_Support(p,T,card(Support p)) by Th16;
then A1: Support u c= Support p by XBOOLE_1:17;
then A2: for x being set holds x in Support u implies x in Support p;
A3: card(Support u) = card(Upper_Support(p,T,card(Support p))) by Lm4
                   .= card(Support p) by Th22;
now let x be set; assume A4: x in Support p;
   now assume not x in Support u;
     then Support u c< Support p by A1,A4,XBOOLE_0:def 8;
     hence contradiction by A3,CARD_2:67;
     end;
   hence x in Support u;
   end;
then A5: Support u = Support p by A2,TARSKI:2;
A6: dom p = Bags n by FUNCT_2:def 1 .= dom u by FUNCT_2:def 1;
now let x be set; assume x in dom p;
  then reconsider x' = x as Element of Bags n;
  now per cases;
  case x in Support p;
    hence p.x' = u.x' by A5,Th16;
    end;
  case A7: not x in Support p;
    hence p.x' = 0.L by POLYNOM1:def 9
              .= u.x' by A7,A5,POLYNOM1:def 9;
    end;
  end;
  hence p.x = u.x;
  end;
hence A8: p = u by A6,FUNCT_1:9;
thus 0_(n,L) = p + -p by POLYRED:3
      .= (l + p) + -p by A8,Th33
      .= l + (p + -p) by POLYNOM1:80
      .= l + 0_(n,L) by POLYRED:3
      .= l by POLYRED:2;
end;

theorem Th36:
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_zeroed right_complementable
            Abelian (non trivial doubleLoopStr),
    p being non-zero Polynomial of n,L
holds Up(p,T,1) = HM(p,T) & Low(p,T,1) = Red(p,T)
proof
let n be Ordinal, T be connected TermOrder of n,
    L be add-associative right_zeroed right_complementable
 Abelian (non trivial doubleLoopStr), p be non-zero Polynomial of n,L;
set u = Up(p,T,1), l = Low(p,T,1);
A1: now assume card(Support p) < 1;
   then card(Support p) = 0 by NAT_1:14;
   then Support p,0 are_equipotent by CARD_1:def 5;
   then Support p = {} by CARD_1:46;
   then p = 0_(n,L) by POLYNOM7:1;
   hence contradiction by POLYNOM7:def 2;
   end;
Support u = Upper_Support(p,T,1) by A1,Lm4;
then card(Support u) = 1 by A1,Def2;
then consider x being set such that
A2: Support u = {x} by CARD_2:60;
HM(p,T) <> 0_(n,L) by POLYNOM7:def 2;
then A3: Support HM(p,T) <> {} by POLYNOM7:1;
HT(p,T) in {x} by A1,A2,Th30;
then A4: Support u = {HT(p,T)} by A2,TARSKI:def 1;
then A5: Support u = Support(HM(p,T)) by A3,TERMORD:21;
A6: dom HM(p,T) = Bags n by FUNCT_2:def 1 .= dom u by FUNCT_2:def 1;
now let x be set; assume x in dom HM(p,T);
  then reconsider x' = x as Element of Bags n;
  now per cases;
  case A7: x in Support HM(p,T);
    then x' = HT(p,T) by A4,A5,TARSKI:def 1;
    hence (HM(p,T)).x'= p.x' by TERMORD:18 .= u.x' by A5,A7,Th16;
    end;
  case A8: not x in Support HM(p,T);
    hence (HM(p,T)).x' = 0.L by POLYNOM1:def 9
                      .= u.x' by A8,A5,POLYNOM1:def 9;
    end;
  end;
  hence (HM(p,T)).x = u.x;
  end;
hence HM(p,T) = u by A6,FUNCT_1:9;
then A9: HM(p,T) + l = p by A1,Th33;
thus Red(p,T) = p - HM(p,T) by TERMORD:def 9
             .= (l + HM(p,T)) + -HM(p,T) by A9,POLYNOM1:def 23
             .= l + (HM(p,T) + -HM(p,T)) by POLYNOM1:80
             .= l + 0_(n,L) by POLYRED:3
             .= l by POLYRED:2;
end;

registration
let n be Ordinal,
    T be connected TermOrder of n,
    L be add-associative right_zeroed right_complementable
         (non trivial LoopStr),
    p be non-zero Polynomial of n,L;
cluster Up(p,T,0) -> monomial-like;
coherence
  proof
  Up(p,T,0) = 0_(n,L) by Th34;
  hence thesis;
  end;
end;

registration
let n be Ordinal,
    T be connected TermOrder of n,
    L be add-associative right_zeroed right_complementable
         Abelian (non trivial doubleLoopStr),
    p be non-zero Polynomial of n,L;
cluster Up(p,T,1) -> non-zero monomial-like;
coherence
  proof
  Up(p,T,1) = HM(p,T) by Th36;
  hence thesis;
  end;
cluster Low(p,T,card(Support p)) -> monomial-like;
coherence
  proof
  Low(p,T,card(Support p)) = 0_(n,L) by Th35;
  hence thesis;
  end;
end;

theorem Th37:
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_zeroed right_complementable
            (non trivial LoopStr),
    p being Polynomial of n,L,
    j being Element of NAT st j = card(Support p) - 1
holds Low(p,T,j) is non-zero Monomial of n,L
proof
let n be Ordinal, T be connected TermOrder of n,
    L be add-associative right_zeroed right_complementable
         (non trivial LoopStr), p be Polynomial of n,L, j be Element of NAT;
assume A1: j = card(Support p) - 1;
set l = Low(p,T,j);
A2: now assume j > card(Support p);
    then card(Support p) - 1 + 1 > card(Support p) + 1 by A1,XREAL_1:10;
    then card(Support p) + -card(Support p) >
             (card(Support p) + 1) + -card(Support p) by XREAL_1:10;
    then 0 > (card(Support p) + 1) + -card(Support p);
    hence contradiction;
    end;
then Support l = Lower_Support(p,T,j) by Th25;
then card(Support l) = card(Support p) - (card(Support p) - 1)
    by A2,A1,Th24;
then consider x being set such that
A3: Support l = {x} by CARD_2:60;
A4: l <> 0_(n,L) by A3,POLYNOM7:1;
x in Support l by A3,TARSKI:def 1;
then x is Element of Bags n;
hence thesis by A3,A4,POLYNOM7:6,def 2;
end;

theorem Th38:
for n being Ordinal,
    T being connected admissible TermOrder of n,
    L being add-associative right_zeroed right_complementable
            (non empty LoopStr),
    p being Polynomial of n,L,
    i being Element of NAT st i < card(Support p)
holds HT(Low(p,T,i+1),T) <= HT(Low(p,T,i),T), T
proof
let n be Ordinal, T be connected admissible TermOrder of n,
    L be add-associative right_zeroed right_complementable
         (non empty LoopStr), p be Polynomial of n,L, i be Element of NAT;
assume A1: i < card(Support p);
then A2: i + 1 <= card(Support p) by NAT_1:13;
set li = Low(p,T,i), li1 = Low(p,T,i+1);
A3: Support li c= Support(p) by A1,Th26;
A4: Support li = Lower_Support(p,T,i) by A1,Th25;
A5: Support li1 = Lower_Support(p,T,i+1) by A2,Th25;
A6: card Support li = card(Support p) - i by A1,A4,Th24;
A7: card Support li1 = card(Support p) - (i+1) by A2,A5,Th24;
now per cases;
case i = card(Support p) - 1;
  then card Support li1
     = card(Support p) - card(Support p) by A5,Th24
    .= 0;
  then Support li1,0 are_equipotent by CARD_1:def 5;
  then Support li1 = {} by CARD_1:46;
  then HT(li1,T) = EmptyBag n by TERMORD:def 6;
  hence thesis by TERMORD:9;
  end;
case A8:i <> card(Support p) - 1;
  card Support li1 = card(Support p) - (i+1) by A7; then
  card Support li1 = (card(Support p) - 1) - i; then
  card Support li1 <> i - i by A8; then
  card Lower_Support(p,T,i+1) <> 0 by A5; then
  Lower_Support(p,T,i+1) <> {} by CARD_1:def 5;
  then A9: HT(Low(p,T,i+1),T) in Lower_Support(p,T,i+1) by A5,TERMORD:def 6;
  now assume HT(Low(p,T,i),T) < HT(Low(p,T,i+1),T), T;
    then A10: HT(Low(p,T,i),T) <= HT(Low(p,T,i+1),T), T by TERMORD:def 3;
    now let u' be set; assume A11: u' in Support li;
      then reconsider u = u' as Element of Bags n;
      u <= HT(Low(p,T,i),T),T by A11,TERMORD:def 6;
      then u <= HT(Low(p,T,i+1),T),T by A10,TERMORD:8;
      hence u' in Support li1 by A9,A11,A3,A2,Th24,A5;
      end;
    then Support li c= Support li1 by TARSKI:def 3;
    then card(Support p) + -i <= card(Support p) + -(i+1) by A7,A6,CARD_1:80;
    then - i <= -(i + 1) by XREAL_1:8;
    then i + 1 <= i by XREAL_1:26;
    then (i + 1) - i <= i - i by XREAL_1:11;
    then 1 <= 0;
    hence contradiction;
    end;
  hence thesis by TERMORD:5;
  end;
end;
hence thesis;
end;

theorem Th39:
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_zeroed right_complementable
            (non empty LoopStr),
    p being Polynomial of n,L,
    i being Element of NAT st 0 < i & i < card(Support p)
holds HT(Low(p,T,i),T) < HT(p,T),T
proof
let n be Ordinal, T be connected TermOrder of n,
    L be add-associative right_zeroed right_complementable
         (non empty LoopStr), p be Polynomial of n,L, i be Element of NAT;
assume A1: 0 < i & i < card(Support p);
set l = Low(p,T,i);
now per cases;
case l = 0_(n,L);
then A2: card Support l = 0 by CARD_1:78,POLYNOM7:1;
Support l = Lower_Support(p,T,i) by A1,Th25;
then 0 + i = card(Support p) - i + i by A1,A2,Th24;
hence contradiction by A1;
end;
case l <> 0_(n,L);
then A3: Support l <> {} by POLYNOM7:1;
A4: Support(Low(p,T,i)) c= Support(p) by A1,Th26;
A5: Support Low(p,T,i) = Lower_Support(p,T,i) by A1,Th25;
A6: now assume A7: HT(p,T) in Support l;
     A8: for u being set holds u in Support l implies u in Support p by A4;
     now let u be set; assume A9: u in Support p;
        then reconsider x = u as Element of Bags n;
        x <= HT(p,T),T by A9,TERMORD:def 6;
        hence u in Support l by A5,A1,A7,A9,Th24;
        end;
     then card Support p = card Support l by A8,TARSKI:2
                        .= card(Support p) - i by A5,A1,Th24;
     then card Support p + i = card Support p - i + i;
     then i + (card Support p + -card Support p)
                = card Support p - card Support p;
     hence contradiction by A1;
     end;
A10: HT(l,T) in Support l by A3,TERMORD:def 6;
then HT(l,T) <= HT(p,T),T by A4,TERMORD:def 6;
hence thesis by A10,A6,TERMORD:def 3;
end;
end;
hence thesis;
end;

theorem Th40:
for n being Ordinal,
    T being connected admissible TermOrder of n,
    L being add-associative right_complementable right_zeroed
            unital distributive domRing-like (non trivial doubleLoopStr),
    p being Polynomial of n,L,
    m being non-zero Monomial of n,L,
    i being Element of NAT st i <= card(Support p)
for b being bag of n
holds term(m) + b in Support Low(m*'p,T,i) iff b in Support Low(p,T,i)
proof
let n be Ordinal, T be connected admissible TermOrder of n,
    L be add-associative right_complementable right_zeroed
         unital distributive domRing-like (non trivial doubleLoopStr),
    p be Polynomial of n,L, m be non-zero Monomial of n,L, i be Element of NAT;
assume A1: i <= card(Support p);
then A2: i <= card Support(m*'p) by Th10;
let b be bag of n; set l = Low(p,T,i), lm = Low(m*'p,T,i);
A3: Support l c= Support p by A1,Th26;
A4: Support Up(p,T,i) = Upper_Support(p,T,i) &
    Support Low(p,T,i) = Lower_Support(p,T,i) by A1,Th25;
A5: Support Up(m*'p,T,i) = Upper_Support(m*'p,T,i) &
    Support Low(m*'p,T,i) = Lower_Support(m*'p,T,i) by A2,Th25;
A6: Support Up(p,T,i) c= Support(p) by A1,Th26;
A7: card(Support Up(m*'p,T,i)) = i by A5,A2,Def2;
A8: card(Support Up(p,T,i)) = i by A4,A1,Def2;
A9: Support Low(m*'p,T,i) c= Support(m*'p) &
    Support Up(m*'p,T,i) c= Support(m*'p) by A2,Th26;
A10: Support Up(m*'p,T,i) = {} implies Support Up(p,T,i) = {}
    proof
    assume Support Up(m*'p,T,i) = {};
    then Support Up(p,T,i),{} are_equipotent by A7,A8,CARD_1:21;
    hence thesis by CARD_1:46;
    end;
A11: Support Up(p,T,i) = {} implies Support Up(m*'p,T,i) = {}
    proof
    assume Support Up(p,T,i) = {};
    then Support Up(m*'p,T,i),{} are_equipotent by A7,A8,CARD_1:21;
    hence thesis by CARD_1:46;
    end;
A12: now assume A13: b in Support Low(p,T,i);
   then A14: term(m) + b in Support(m*'p) by A3,Th8;
   A15: now assume b in Support Up(p,T,i);
        then b in Support(Up(p,T,i)) /\ Support(Low(p,T,i))
             by A13,XBOOLE_0:def 3;
        hence contradiction by A13,A3,A1,Th28;
        end;
   now assume A16: term(m) + b in Support Up(m*'p,T,i);
     A17: now let b' be bag of n; assume A18: b' in Support Up(p,T,i);
       then A19: term(m) + b' in Support(m*'p) by A6,Th8;
       b < b',T by A1,Th20,A13,A4,A18;
       then term(m) + b < term(m) + b',T by Th4;
       then term(m) + b <= term(m) + b',T by TERMORD:def 3;
       hence term(m) + b' in Support Up(m*'p,T,i) by A19,A16,A2,A5,Def2;
       end;
     defpred P[set,set] means $2 = term(m) + In($1,Bags n)@;
     A20: for x being set st x in Support Up(p,T,i)
          ex y being set st y in Support Up(m*'p,T,i) & P[x,y]
          proof
          let x be set;
          assume A21: x in Support Up(p,T,i);
          then reconsider x' = x as Element of Bags n;
          take term(m) + x';
          x' = In(x',Bags n) by FUNCT_7:def 1
            .= In(x',Bags n)@ by POLYNOM2:def 3;
          hence thesis by A21,A17;
          end;
     consider f being Function of Support Up(p,T,i),Support Up(m*'p,T,i)
          such that
     A22: for x being set st x in Support Up(p,T,i) holds P[x,f.x]
          from FUNCT_2:sch 1(A20);
     now let x1,x2 be set;
       assume A23: x1 in dom f & x2 in dom f & f.x1 = f.x2;
       then x1 in Support Up(p,T,i) & x2 in Support Up(p,T,i);
       then reconsider x = x1, y = x2 as Element of Bags n;
A24:     y = In(y,Bags n) by FUNCT_7:def 1
        .= In(y,Bags n)@ by POLYNOM2:def 3;
       x = In(x,Bags n) by FUNCT_7:def 1
        .= In(x,Bags n)@ by POLYNOM2:def 3; then
       A25: f.x = term(m) + x & f.y = term(m) + y by A24,A23,A22;
       thus x1 =term(m)+x-'term(m) by POLYNOM1:52 .= x2 by A25,A23,POLYNOM1:52;
       end;
     then A26: f is one-to-one by FUNCT_1:def 8;
     Support(Up(p,T,i)) c= dom f by A10,FUNCT_2:def 1; then
     Support(Up(p,T,i)),f.:Support(Up(p,T,i)) are_equipotent by A26,CARD_1:60;
     then card(f.:Support(Up(p,T,i)))
             = card Support(Up(p,T,i)) by CARD_1:21 .= i by A4,A1,Def2;
     then term(m) + b in f.:Support(Up(p,T,i)) by A16,A7,TRIANG_1:3;
     then consider bb being set such that
     A27: bb in dom f & bb in Support Up(p,T,i) & f.bb = term(m) + b
          by FUNCT_1:def 12;
     reconsider bb as Element of Bags n by A27;
     bb = In(bb,Bags n) by FUNCT_7:def 1
       .= In(bb,Bags n)@ by POLYNOM2:def 3; then
     A28: term(m) + bb = term(m) + b by A27,A22;
     bb = term(m) + bb -' term(m) by POLYNOM1:52 .= b by A28,POLYNOM1:52;
     hence contradiction by A15,A27;
     end;
   hence term(m) + b in Support Low(m*'p,T,i) by Th28,A2,A14;
   end;
now assume A29: term(m) + b in Support Low(m*'p,T,i);
   then A30: term(m) + b in Support(m*'p) by A9;
   A31: Support(m*'p) = { term(m) + u where u is Element of Bags n :
                                                   u in Support p } by Th9;
   term(m) + b in { term(m) + u where u is Element of Bags n :
                                             u in Support p } by A30,Th9;
   then consider u being Element of Bags n such that
   A32: term(m) + b = term(m) + u & u in Support p;
   A33: b = term(m) + b -' term(m) by POLYNOM1:52 .= u by A32,POLYNOM1:52;
   A34: now assume term(m) + b in Support Up(m*'p,T,i);
        then term(m) + b in Support(Up(m*'p,T,i)) /\ Support(Low(m*'p,T,i))
             by A29,XBOOLE_0:def 3;
        hence contradiction by A29,A9,A2,Th28;
        end;
   now assume A35: b in Support Up(p,T,i);
     A36: now let b' be bag of n;
       assume A37: term(m) + b' in Support Up(m*'p,T,i);
       then A38: b' in Support(p) by A9,Th8;
       A39: term(m) + b < term(m) + b',T by A2,Th20,A29,A5,A37;
       now assume b' <= b,T;
         then term(m) + b' <= term(m) + b,T by Th2;
         hence contradiction by A39,TERMORD:5;
         end;
       then b < b',T by TERMORD:5;
       then b <= b',T by TERMORD:def 3;
       hence b' in Support Up(p,T,i) by A38,A35,A1,A4,Def2;
       end;
     defpred P[set,set] means $1 = term(m) + (In($2, Bags n)@);
     A40: for x being set st x in Support Up(m*'p,T,i)
          ex y being set st y in Support Up(p,T,i) & P[x,y]
          proof
          let x be set;
          assume A41: x in Support Up(m*'p,T,i);
          then x in Support(m*'p) by A9;
          then consider x' being Element of Bags n such that
          A42: x = term(m) + x' & x' in Support p by A31;
          take x';
          x' = In(x',Bags n) by FUNCT_7:def 1
            .= In (x',Bags n)@ by POLYNOM2:def 3;
          hence thesis by A42,A41,A36;
          end;
     consider f being Function of Support Up(m*'p,T,i),Support Up(p,T,i)
          such that
     A43: for x being set st x in Support Up(m*'p,T,i) holds P[x,f.x]
          from FUNCT_2:sch 1(A40);
     now let x1,x2 be set;
       assume A44: x1 in dom f & x2 in dom f & f.x1 = f.x2;
       then f.x1 in rng f & f.x2 in rng f by FUNCT_1:12;
       then f.x1 in Support Up(p,T,i) & f.x2 in Support Up(p,T,i);
       then reconsider x1' = f.x1, x2' = f.x2 as Element of Bags n;
A45:     x1' = In(x1',Bags n) by FUNCT_7:def 1
        .= In(x1',Bags n)@ by POLYNOM2:def 3;
       x2' = In(x2',Bags n) by FUNCT_7:def 1
        .= In(x2',Bags n)@ by POLYNOM2:def 3;
       hence x1 = term(m) + x1' by A44,A43 .= x2 by A43,A44,A45;
       end;
     then A46: f is one-to-one by FUNCT_1:def 8;
     Support(Up(m*'p,T,i)) c= dom f by A11,FUNCT_2:def 1; then
     Support(Up(m*'p,T,i)),f.:Support(Up(m*'p,T,i)) are_equipotent
         by A46,CARD_1:60;
     then card(f.:Support(Up(m*'p,T,i)))
        = card Support(Up(m*'p,T,i)) by CARD_1:21 .= i by A5,A2,Def2;
     then b in f.:Support(Up(m*'p,T,i)) by A35,A8,TRIANG_1:3;
     then consider bb being set such that
     A47: bb in dom f & bb in Support Up(m*'p,T,i) & f.bb = b
          by FUNCT_1:def 12;
     f.bb in rng f by A47,FUNCT_1:12;
     then f.bb in Support Up(p,T,i);
     then reconsider bb' = f.bb as Element of Bags n;
     bb' = In(bb',Bags n) by FUNCT_7:def 1
        .= In(bb',Bags n)@ by POLYNOM2:def 3;
     hence contradiction by A34,A47,A43;
     end;
   hence b in Support Low(p,T,i) by Th28,A1,A33,A32;
   end;
hence term(m) + b in Support Low(m*'p,T,i) iff b in Support Low(p,T,i) by A12;
end;

theorem Th41:
for n being Ordinal,
    T being connected admissible TermOrder of n,
    L being add-associative right_zeroed right_complementable
            (non empty LoopStr),
    p being Polynomial of n,L,
    i being Element of NAT st i < card(Support p)
holds Support Low(p,T,i+1) c= Support Low(p,T,i)
proof
let n be Ordinal, T be connected admissible TermOrder of n,
    L be add-associative right_zeroed right_complementable
         (non empty LoopStr), p be Polynomial of n,L, i be Element of NAT;
assume A1: i < card(Support p);
then A2: i + 1 <= card(Support p) by NAT_1:13; then
A3: card Support p - i >= 1 by XREAL_1:21;
set l = Low(p,T,i), l1 = Low(p,T,i+1);
A4: Support(Low(p,T,i+1)) c= Support(p) by A2,Th26;
A5: Support Low(p,T,i) = Lower_Support(p,T,i) by A1,Th25;
A6: card Support l = card(Support p) - i by A1,A5,Th24;
A7: HT(Low(p,T,i+1),T) <= HT(Low(p,T,i),T), T by A1,Th38;
card Lower_Support(p,T,i) = card Support p - i by A5,A6; then
Lower_Support(p,T,i) <> {} by A3,CARD_1:78;
then A8: HT(Low(p,T,i),T) in Lower_Support(p,T,i) by A5,TERMORD:def 6;
now let u' be set; assume A9: u' in Support l1;
  then reconsider u = u' as Element of Bags n;
  u <= HT(Low(p,T,i+1),T),T by A9,TERMORD:def 6;
  then u <= HT(Low(p,T,i),T),T by A7,TERMORD:8;
  hence u' in Support l by A5,A9,A4,A1,A8,Th24;
  end;
hence thesis by TARSKI:def 3;
end;

theorem Th42:
for n being Ordinal,
    T being connected admissible TermOrder of n,
    L being add-associative right_zeroed right_complementable
            (non empty LoopStr),
    p being Polynomial of n,L,
    i being Element of NAT st i < card(Support p)
holds Support Low(p,T,i) \ Support Low(p,T,i+1) = {HT(Low(p,T,i),T)}
proof
let n be Ordinal, T be connected admissible TermOrder of n,
    L be add-associative right_zeroed right_complementable
         (non empty LoopStr), p be Polynomial of n,L, i be Element of NAT;
assume A1: i < card(Support p);
then A2: i + 1 <= card(Support p) by NAT_1:13;
set l = Low(p,T,i), l1 = Low(p,T,i+1);
A3: Support(Low(p,T,i)) c= Support(p) by A1,Th26;
A4: Support Low(p,T,i+1) = Lower_Support(p,T,i+1) by A2,Th25;
A5: Support Low(p,T,i) = Lower_Support(p,T,i) by A1,Th25;
A6: Support Low(p,T,i+1) = Lower_Support(p,T,i+1) by A2,Th25;
A7: card Support l = card(Support p) - i by A1,A5,Th24;
A8: card Support l1 = card(Support p) - (i+1) by A2,A6,Th24;
now assume Lower_Support(p,T,i) = {};
    then card(Support p) - i = 0 by A1,Th24,CARD_1:78;
    hence contradiction by A1;
    end;
then A9: HT(Low(p,T,i),T) in Support l by A5,TERMORD:def 6;
Support Low(p,T,i+1) c= Support Low(p,T,i) by A1,Th41;
then card(Support Low(p,T,i) \ Support Low(p,T,i+1))
   = (card(Support p) - i) - (card(Support p) - (i+1)) by A7,A8,CARD_2:63
  .= 1;
then consider x being set such that
A10: Support Low(p,T,i) \ Support Low(p,T,i+1) = {x} by CARD_2:60;
now assume A11: x <> HT(Low(p,T,i),T);
  A12: now assume not HT(Low(p,T,i),T) in Support l1;
      then HT(Low(p,T,i),T) in Support l \ Support l1 by A9,XBOOLE_0:def 4;
      hence contradiction by A10,TARSKI:def 1,A11;
      end;
  A13: now let u be set; assume A14: u in Support l;
      then reconsider u' = u as Element of Bags n;
      u' <= HT(Low(p,T,i),T),T by A14,TERMORD:def 6;
      hence u in Support l1 by A4,A14,A3,A2,A12,Th24;
      end;
  Support Low(p,T,i+1) c= Support Low(p,T,i) by A1,Th41;
  then for u being set holds u in Support l1 implies u in Support l;
  then card(Support p) + -i <= card(Support p) + -(i+1) by A7,A8,A13,TARSKI:2;
  then - i <= -(i + 1) by XREAL_1:8;
  then i + 1 <= i by XREAL_1:26;
  then (i + 1) - i <= i - i by XREAL_1:11;
  then 1 <= 0;
  hence contradiction;
  end;
hence thesis by A10;
end;

theorem Th43:
for n being Ordinal,
    T being connected admissible TermOrder of n,
    L being add-associative right_zeroed right_complementable
            (non trivial LoopStr),
    p being Polynomial of n,L,
    i being Element of NAT st i < card(Support p)
holds Low(p,T,i+1) = Red(Low(p,T,i),T)
proof
let n be Ordinal, T be connected admissible TermOrder of n,
    L be add-associative right_zeroed right_complementable
         (non trivial LoopStr), p be Polynomial of n,L, i be Element of NAT;
assume A1: i < card(Support p);
then A2: i + 1 <= card(Support p) by NAT_1:13;
set l = Low(p,T,i), l1 = Low(p,T,i+1), r = Red(l,T);
A3: dom l1 = Bags n by FUNCT_2:def 1 .= dom r by FUNCT_2:def 1;
A4: Support Low(p,T,i) \ Support Low(p,T,i+1) = {HT(Low(p,T,i),T)}
    by A1,Th42;
A5: Support(Low(p,T,i)) c= Support(p) by A1,Th26;
A6: Support Low(p,T,i) = Lower_Support(p,T,i) by A1,Th25;
A7: Support Low(p,T,i+1) = Lower_Support(p,T,i+1) by A2,Th25;
A8: Support Low(p,T,i+1) = Lower_Support(p,T,i+1) by A2,Th25;
A9: card Support l = card(Support p) - i by A1,A6,Th24;
A10: card Support l1 = card(Support p) - (i+1) by A2,A8,Th24;
now assume A11: {HT(l,T)} /\ Support l1 <> {};
  consider u being Element of {HT(l,T)} /\ Support l1;
  A12: u in {HT(l,T)} & u in Support l1 by A11,XBOOLE_0:def 3;
  then u = HT(l,T) by TARSKI:def 1;
  then A13: HT(l,T) <= HT(l1,T),T by A12,TERMORD:def 6;
  A14: HT(l1,T) in Support l1 by A12,TERMORD:def 6;
  now let u' be set; assume A15: u' in Support l;
    then reconsider u = u' as Element of Bags n;
    u <= HT(Low(p,T,i),T),T by A15,TERMORD:def 6;
    then u <= HT(Low(p,T,i+1),T),T by A13,TERMORD:8;
    hence u' in Support l1 by A7,A2,A15,A5,A14,Th24;
    end;
  then Support l c= Support l1 by TARSKI:def 3;
  then card(Support p) + -i <= card(Support p) + -(i+1) by A10,A9,CARD_1:80;
  then - i <= -(i + 1) by XREAL_1:8;
  then i + 1 <= i by XREAL_1:26;
  then (i + 1) - i <= i - i by XREAL_1:11;
  then 1 <= 0;
  hence contradiction;
  end;
then A16: Support l1 misses {HT(l,T)} by XBOOLE_0:def 7;
A17: Support(Low(p,T,i+1)) c= Support(Low(p,T,i)) by A1,Th41;
then Support Low(p,T,i) = Support l1 \/ {HT(l,T)} by A4,XBOOLE_1:45;
then A18: Support r = (Support l1 \/ {HT(l,T)}) \ {HT(l,T)} by TERMORD:36
                  .= Support l1 \ {HT(l,T)} by XBOOLE_1:40
                  .= Support l1 by A16,XBOOLE_1:83;
now let x be set; assume x in dom l1;
  then reconsider b = x as Element of Bags n;
  now per cases;
  case A19: b in Support l1;
    then not b in {HT(Low(p,T,i),T)} by A4,XBOOLE_0:def 4;
    then A20: b <> HT(l,T) by TARSKI:def 1;
    thus l1.b = p.b by A19,A2,Th31
             .= l.b by A19,A17,A1,Th31
             .= r.b by A19,A17,A20,TERMORD:40;
    end;
  case A21: not b in Support l1;
    hence l1.b = 0.L by POLYNOM1:def 9
              .= r.b by A21,A18,POLYNOM1:def 9;
    end;
  end;
  hence l1.x = r.x;
  end;
hence thesis by A3,FUNCT_1:9;
end;

theorem Th44:
for n being Ordinal,
    T being connected admissible TermOrder of n,
    L being add-associative right_complementable right_zeroed
            unital distributive domRing-like (non trivial doubleLoopStr),
    p being Polynomial of n,L,
    m being non-zero Monomial of n,L,
    i being Element of NAT st i <= card(Support p)
holds Low(m*'p,T,i) = m *' Low(p,T,i)
proof
let n be Ordinal, T be connected admissible TermOrder of n,
    L be add-associative right_complementable right_zeroed
         unital distributive domRing-like (non trivial doubleLoopStr),
    p be Polynomial of n,L, m be non-zero Monomial of n,L, i be Element of NAT;
assume A1: i <= card(Support p);
then A2: i <= card(Support(m*'p)) by Th10;
set l = Low(p,T,i), lm = Low(m*'p,T,i);
A3: dom(m*'l) = Bags n by FUNCT_2:def 1 .= dom lm by FUNCT_2:def 1;
A4: Support(m*'l) c= {s + t where s,t is Element of Bags n :
                      s in Support m & t in Support l} by TERMORD:30;
A5: Support(m*'p) c= {s + t where s,t is Element of Bags n :
                      s in Support m & t in Support p} by TERMORD:30;
A6: now let u be set; assume A7: u in Support m*'l;
     then reconsider u' = u as Element of Bags n;
     u in {s + t where s,t is Element of Bags n :
                         s in Support m & t in Support l} by A4,A7;
     then consider s,t being Element of Bags n such that
     A8: u' = s + t & s in Support m & t in Support l;
     A9: l.t <> 0.L by A8,POLYNOM1:def 9;
     m <> 0_(n,L) by POLYNOM7:def 2;
     then Support m <> {} by POLYNOM7:1;
     then A10: Support m = {term(m)} by POLYNOM7:7;
     then term(m) in Support m by TARSKI:def 1;
     then A11: m.term(m) <> 0.L by POLYNOM1:def 9;
     A12: term(m) = s by A8,A10,TARSKI:def 1;
     then (m*'p).u' = m.term(m) * p.t by A8,POLYRED:7
                   .= m.term(m) * l.t by A1,A8,Th31;
     then (m*'p).u' <> 0.L by A9,A11,VECTSP_2:def 5;
     then A13: u' in Support(m*'p) by POLYNOM1:def 9;
     now assume not(s+t in Support Low(m*'p,T,i));
        then A14: s+t in Support Up(m*'p,T,i) by A13,A8,A2,Th28;
        now let t' be bag of n; assume t' in Support Low(p,T,i);
          then s+t' in Support Low(m*'p,T,i) by A12,A1,Th40;
          then A15: s+t' < s+t,T by A14,A2,Th29;
          now assume t <= t',T;
            then s + t <= s + t',T by Th2;
            hence contradiction by A15,TERMORD:5;
            end;
          hence t' < t,T by TERMORD:5;
          end;
        then t < t,T by A8;
        hence contradiction by TERMORD:def 3;
        end;
     hence u in Support lm by A8;
     end;
now let u be set; assume A16: u in Support Low(m*'p,T,i);
  then reconsider u' = u as Element of Bags n;
  Support Low(m*'p,T,i) c= Support(m*'p) by A2,Th26;
  then u' in Support(m*'p) by A16;
  then u' in {s + t where s,t is Element of Bags n :
                      s in Support m & t in Support p} by A5;
  then consider s,t being Element of Bags n such that
  A17: u = s + t & s in Support m & t in Support p;
  m <> 0_(n,L) by POLYNOM7:def 2;
  then Support m <> {} by POLYNOM7:1;
  then A18: Support m = {term(m)} by POLYNOM7:7;
  then term(m) in Support m by TARSKI:def 1;
  then A19: m.term(m) <> 0.L by POLYNOM1:def 9;
  A20: term(m) = s by A17,A18,TARSKI:def 1;
  A21: p.t <> 0.L by A17,POLYNOM1:def 9;
  A22: t in Support l by A20,A17,A16,A1,Th40;
  (m*'l).(term(m)+t) = m.term(m) * l.t by POLYRED:7
                    .= m.term(m) * p.t by A22,A1,Th31;
  then (m*'l).u' <> 0.L by A17,A21,A20,A19,VECTSP_2:def 5;
  hence u in Support(m*'Low(p,T,i)) by POLYNOM1:def 9;
  end;
then A23: Support(m*'l) = Support lm by A6,TARSKI:2;
now let x be set; assume x in dom(m*'l);
  then reconsider b = x as Element of Bags n;
  now per cases;
  case A24: b in Support(m*'l);
    then A25: b in Support lm by A6;
    b in {s + t where s,t is Element of Bags n :
                                  s in Support m & t in Support l}
      by A24,A4;
    then consider s,t being Element of Bags n such that
    A26: b = s + t & s in Support m & t in Support l;
    Support m = {term(m)} by A26,POLYNOM7:7;
    then A27: term(m) = s by A26,TARSKI:def 1;
    hence (m*'l).b = m.term(m) * l.t by A26,POLYRED:7
            .= m.term(m) * p.t by A26,A1,Th31
            .= (m*'p).b by A27,A26,POLYRED:7
            .= lm.b by A25,A2,Th31;
    end;
  case A28: not b in Support(m*'l);
    hence (m*'l).b = 0.L by POLYNOM1:def 9
                  .= lm.b by A28,A23,POLYNOM1:def 9;
    end;
  end;
  hence (m*'l).x = lm.x;
  end;
hence thesis by A3,FUNCT_1:9;
end;

begin :: More on Polynomial Reduction

Lm5:for n being Ordinal, T being connected TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative left_unital right_unital distributive
            Field-like (non trivial doubleLoopStr),
    P being Subset of Polynom-Ring(n,L),
    R being RedSequence of PolyRedRel(P,T),
    i being Element of NAT st 1 <= i & i <= len R & len R > 1
holds R.i is Polynomial of n,L
proof
let n be Ordinal, T be connected TermOrder of n,
    L be add-associative right_complementable right_zeroed
         commutative associative left_unital right_unital distributive
         Field-like (non trivial doubleLoopStr),
    P be Subset of Polynom-Ring(n,L),
    R be RedSequence of PolyRedRel(P,T), i be Element of NAT;
assume A1: 1 <= i & i <= len R & 1 < len R;
then A2: i in dom R by FINSEQ_3:27;
now per cases;
case i <> len R;
  then i < len R by A1,REAL_1:def 5;
  then 1 <= i + 1 & i + 1 <= len R by NAT_1:11,13;
  then i + 1 in dom R by FINSEQ_3:27;
  then [R.i,R.(i+1)] in PolyRedRel(P,T) by A2,REWRITE1:def 2;
  then R.i in dom PolyRedRel(P,T) by RELAT_1:def 4;
  then R.i in the carrier of Polynom-Ring(n,L) by XBOOLE_0:def 4;
  hence R.i is Polynomial of n,L by POLYNOM1:def 27;
  end;
case A3: i = len R;
  A4: i - 1 is Element of NAT by A1,INT_1:18;
  1 + -1 < i + -1 by A3,A1,XREAL_1:10; then
  A5: 1 <= i - 1 by A4,NAT_1:14;
  i - 1 <= len R by A3,XREAL_1:148;
  then A6: i - 1 in dom R by A4,A5,FINSEQ_3:27;
  i = (i - 1) + 1;
  then [R.(i-1),R.i] in PolyRedRel(P,T) by A2,A6,REWRITE1:def 2;
  then R.i in rng PolyRedRel(P,T) by RELAT_1:def 5;
  hence R.i is Polynomial of n,L by POLYNOM1:def 27;
  end;
end;
hence thesis;
end;

theorem Th45:
for n being Ordinal,
    T being connected admissible TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative left_unital right_unital distributive
            Abelian Field-like (non trivial doubleLoopStr),
    f,g,p being Polynomial of n,L
st f reduces_to g,p,T holds -f reduces_to -g,p,T
proof
let n be Ordinal, T be connected admissible TermOrder of n,
    L be add-associative right_complementable right_zeroed
         commutative associative left_unital right_unital distributive
         Abelian Field-like (non trivial doubleLoopStr),
    f,g,p be Polynomial of n,L;
assume f reduces_to g,p,T;
then consider b being bag of n such that
A1: f reduces_to g,p,b,T by POLYRED:def 6;
A2: f <> 0_(n,L) & p <> 0_(n,L) &
    b in Support f &
    ex s being bag of n
    st s + HT(p,T) = b & g = f - (f.b/HC(p,T)) * (s *' p) by A1,POLYRED:def 5;
consider s being bag of n such that
A3: s + HT(p,T) = b & g = f - (f.b/HC(p,T)) * (s *' p) by A1,POLYRED:def 5;
A4: now assume -f = 0_(n,L);
    then f = -(0_(n,L)) .= 0_(n,L) by Th13;
    hence contradiction by A1,POLYRED:def 5;
    end;
g = f + (-((f.b/HC(p,T)) * (s *' p))) by A3,POLYNOM1:def 23;
then -g = -f + -(-((f.b/HC(p,T)) * (s *' p))) by POLYRED:1
       .= -f - -((f.b/HC(p,T)) * (s *' p)) by POLYNOM1:def 23
       .= -f - (-(f.b/HC(p,T))) * (s *' p) by POLYRED:9
       .= -f - (-(f.b*(HC(p,T))")) * (s *' p) by VECTSP_1:def 23
       .= -f - ((-f.b)*(HC(p,T))") * (s *' p) by VECTSP_1:41
       .= -f - ((-f.b)/(HC(p,T))) * (s *' p) by VECTSP_1:def 23
       .= -f - ((-f).b/HC(p,T)) * (s *' p) by POLYNOM1:def 22;
then -f <> 0_(n,L) & p <> 0_(n,L) &
     b in Support -f &
     ex s being bag of n
     st s+HT(p,T) = b & -g = -f-((-f).b/HC(p,T))*(s*'p) by A2,A3,A4,GROEB_1:5;
then -f reduces_to -g,p,b,T by POLYRED:def 5;
hence -f reduces_to -g,p,T by POLYRED:def 6;
end;

Lm6:for n being Ordinal, T being connected admissible TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative left_unital right_unital distributive
            Field-like (non trivial doubleLoopStr),
    p1,p2 being Polynomial of n,L st HT(p1,T),HT(p2,T) are_disjoint
for b1,b2 being bag of n st b1 in Support p1 & b2 in Support p2
holds not(b1 = HT(p1,T) & b2 = HT(p2,T))
implies not(HT(p1,T) + b2 = HT(p2,T) + b1)
proof
let n be Ordinal, T be connected admissible TermOrder of n,
    L be add-associative right_complementable right_zeroed
         commutative associative left_unital right_unital distributive
         Field-like (non trivial doubleLoopStr),
    p1,p2 be Polynomial of n,L;
assume A1: HT(p1,T),HT(p2,T) are_disjoint;
let b1,b2 be bag of n;
assume b1 in Support p1 & b2 in Support p2;
then b1 <= HT(p1,T),T & b2 <= HT(p2,T),T by TERMORD:def 6;
then A2: HT(p2,T) + b1 <= HT(p1,T) + HT(p2,T),T &
         HT(p1,T) + b2 <= HT(p1,T) + HT(p2,T),T by Th2;
assume A3: not(b1 = HT(p1,T) & b2 = HT(p2,T));
assume HT(p1,T) + b2 = HT(p2,T) + b1;
then A4: HT(p2,T) divides HT(p1,T) + b2 &
         HT(p1,T) divides HT(p2,T) + b1 by POLYNOM1:54;
now per cases by A3;
case A5: not(b1 = HT(p1,T));
  HT(p2,T) divides HT(p2,T) + b1 by POLYNOM1:54;
  then lcm(HT(p1,T),HT(p2,T)) divides HT(p2,T) + b1 by A4,GROEB_2:8;
  then HT(p1,T) + HT(p2,T) divides HT(p2,T) + b1 by A1,GROEB_2:9;
  then HT(p1,T) + HT(p2,T) <= HT(p2,T) + b1,T by TERMORD:10;
  then A6: HT(p1,T) + HT(p2,T) = HT(p2,T) + b1 by A2,TERMORD:7;
  HT(p1,T) = HT(p1,T) + HT(p2,T) -' HT(p2,T) by POLYNOM1:52;
  hence contradiction by A5,A6,POLYNOM1:52;
  end;
case A7: not(b2 = HT(p2,T));
  HT(p1,T) divides HT(p1,T) + b2 by POLYNOM1:54;
  then lcm(HT(p1,T),HT(p2,T)) divides HT(p1,T) + b2 by A4,GROEB_2:8;
  then HT(p1,T) + HT(p2,T) divides HT(p1,T) + b2 by A1,GROEB_2:9;
  then HT(p1,T) + HT(p2,T) <= HT(p1,T) + b2,T by TERMORD:10;
  then A8: HT(p1,T) + HT(p2,T) = HT(p1,T) + b2 by A2,TERMORD:7;
  HT(p2,T) = HT(p1,T) + HT(p2,T) -' HT(p1,T) by POLYNOM1:52;
  hence contradiction by A7,A8,POLYNOM1:52;
  end;
end;
hence thesis;
end;

theorem Th46:
for n being Ordinal,
    T being connected admissible TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative left_unital right_unital distributive
            Abelian Field-like (non trivial doubleLoopStr),
    f,f1,g,p being Polynomial of n,L
st f reduces_to f1,{p},T &
   for b1 being bag of n st b1 in Support g holds not(HT(p,T) divides b1)
holds f + g reduces_to f1 + g,{p},T
proof
let n be Ordinal, T be connected admissible TermOrder of n,
    L be add-associative right_complementable right_zeroed
         commutative associative left_unital right_unital distributive
         Abelian Field-like (non trivial doubleLoopStr),
    f,f1,g,p be Polynomial of n,L;
assume A1: f reduces_to f1,{p},T &
   for b1 being bag of n st b1 in Support g holds not(HT(p,T) divides b1);
then consider q being Polynomial of n,L such that
A2: q in {p} & f reduces_to f1,q,T by POLYRED:def 7;
A3: p = q & p in {p} by A2,TARSKI:def 1;
then consider br being bag of n such that
A4: f reduces_to f1,p,br,T by A2,POLYRED:def 6;
A5: f <> 0_(n,L) & p <> 0_(n,L) &
    br in Support f &
    ex s being bag of n
    st s + HT(p,T) = br & f1 = f -(f.br/HC(p,T))*(s*'p) by A4,POLYRED:def 5;
consider s being bag of n such that
A6: s + HT(p,T) = br & f1 = f -(f.br/HC(p,T))*(s*'p) by A4,POLYRED:def 5;
A7: now assume A8: br in Support g;
    HT(p,T) divides br by A6,TERMORD:1;
    hence contradiction by A8,A1;
    end;
A9: br is Element of Bags n by POLYNOM1:def 14;
A10: (f+g).br = f.br + g.br by POLYNOM1:def 21
         .= f.br + 0.L by A9,A7,POLYNOM1:def 9 .= f.br by RLVECT_1:def 7;
now per cases;
case f + g = 0_(n,L);
  then (f + g) - f = -f by Th14;
  then (f + g) + -f = -f by POLYNOM1:def 23;
  then (f + -f) + g = -f by POLYNOM1:80;
  then 0_(n,L) + g = -f by POLYRED:3;
  then g = -f by POLYRED:2;
  hence contradiction by A7,A5,GROEB_1:5;
  end;
case A11: f + g <> 0_(n,L);
  set g1 = (f+g) - ((f+g).br/HC(p,T))*(s*'p);
  (f+g).br <> 0.L & br is Element of Bags n by A10,A5,POLYNOM1:def 9;
  then br in Support(f+g) by POLYNOM1:def 9;
  then f+g reduces_to g1,p,br,T by A11,A5,A6,POLYRED:def 5;
  then A12: f+g reduces_to g1,p,T by POLYRED:def 6;
  g1 = (f+g) + (-((f.br/HC(p,T))*(s*'p))) by A10,POLYNOM1:def 23
    .= (f + -((f.br/HC(p,T))*(s*'p))) + g by POLYNOM1:80
    .= f1 + g by A6,POLYNOM1:def 23;
  hence thesis by POLYRED:def 7,A3,A12;
  end;
end;
hence thesis;
end;

theorem Th47:
for n being Ordinal,
    T being connected admissible TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative left_unital right_unital distributive
            Abelian Field-like (non trivial doubleLoopStr),
    f,g being non-zero Polynomial of n,L
holds f*'g reduces_to Red(f,T)*'g,{g},T
proof
let n be Ordinal, T be connected admissible TermOrder of n,
    L be add-associative right_complementable right_zeroed
         commutative associative left_unital right_unital distributive
         Abelian Field-like (non trivial doubleLoopStr),
    f,g be non-zero Polynomial of n,L;
set fg = f*'g, rf = Red(f,T);
set q = fg - (fg.HT(fg,T)/HC(g,T)) * (HT(f,T) *' g);
A1: fg <> 0_(n,L) & g <> 0_(n,L) by POLYNOM7:def 2;
then Support fg <> {} by POLYNOM7:1;
then A2: HT(fg,T) in Support fg by TERMORD:def 6;
A3: g in {g} by TARSKI:def 1;
HT(fg,T) = HT(f,T) + HT(g,T) by TERMORD:31;
then fg reduces_to q,g,HT(fg,T),T by A1,A2,POLYRED:def 5;
then A4: fg reduces_to q,g,T by POLYRED:def 6;
A5: HC(g,T) <> 0.L by A1,TERMORD:17;
reconsider r = -HM(f,T) as Polynomial of n,L;
q = fg - (HC(fg,T)/HC(g,T)) * (HT(f,T) *' g) by TERMORD:def 7
 .= fg - ((HC(f,T)*HC(g,T))/HC(g,T)) * (HT(f,T) *' g) by TERMORD:32
 .= fg - ((HC(f,T)*HC(g,T))*HC(g,T)") * (HT(f,T) *' g) by VECTSP_1:def 23
 .= fg - (HC(f,T)*(HC(g,T)*HC(g,T)")) * (HT(f,T) *' g) by GROUP_1:def 4
 .= fg - (HC(f,T)*1_L) * (HT(f,T) *' g) by A5,VECTSP_1:def 22
 .= fg - HC(f,T) * (HT(f,T) *' g) by GROUP_1:def 5
 .= fg - Monom(HC(f,T),HT(f,T)) *' g by POLYRED:22
 .= fg - HM(f,T) *' g by TERMORD:def 8
 .= fg + -(HM(f,T) *' g) by POLYNOM1:def 23
 .= fg + (r *' g) by POLYRED:6
 .= g *' (f + -HM(f,T)) by POLYNOM1:85
 .= (f - HM(f,T)) *' g by POLYNOM1:def 23
 .= Red(f,T) *' g by TERMORD:def 9;
hence thesis by A4,A3,POLYRED:def 7;
end;

theorem
for n being Ordinal,
    T being connected admissible TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative left_unital right_unital distributive
            Abelian Field-like (non trivial doubleLoopStr),
    f,g being non-zero Polynomial of n,L,
    p being Polynomial of n,L st p.(HT(f*'g,T)) = 0.L
holds f*'g+p reduces_to Red(f,T)*'g+p,{g},T
proof
let n be Ordinal, T be connected admissible TermOrder of n,
    L be add-associative right_complementable right_zeroed
         commutative associative left_unital right_unital distributive
         Abelian Field-like (non trivial doubleLoopStr),
    f,g be non-zero Polynomial of n,L, p be Polynomial of n,L;
assume A1: p.(HT(f*'g,T)) = 0.L;
set fg = f*'g+p, rf = Red(f,T)+p;
set q = fg - ((fg).HT(f*'g,T)/HC(g,T)) * (HT(f,T) *' g);
f*'g <> 0_(n,L) by POLYNOM7:def 2;
then Support(f*'g) <> {} by POLYNOM7:1;
then HT(f*'g,T) in Support(f*'g) by TERMORD:def 6;
then A2: (f*'g).HT(f*'g,T) <> 0.L by POLYNOM1:def 9;
fg.(HT(f*'g,T)) = (f*'g).HT(f*'g,T) + p.HT(f*'g,T) by POLYNOM1:def 21
               .= (f*'g).HT(f*'g,T) by A1,RLVECT_1:def 7;
then A3: HT(f*'g,T) in Support fg by A2,POLYNOM1:def 9;
then A4: fg <> 0_(n,L) & g <> 0_(n,L) by POLYNOM7:def 2,1;
A5: g in {g} by TARSKI:def 1;
HT(f*'g,T) = HT(f,T) + HT(g,T) by TERMORD:31;
then fg reduces_to q,g,HT(f*'g,T),T by A4,A3,POLYRED:def 5;
then A6: fg reduces_to q,g,T by POLYRED:def 6;
A7: HC(g,T) <> 0.L by A4,TERMORD:17;
reconsider r =  -HM(f,T) as Polynomial of n,L;
q = fg - (((f*'g).HT(f*'g,T)+0.L)/HC(g,T)) * (HT(f,T) *' g)
     by A1,POLYNOM1:def 21
 .= fg - ((f*'g).HT(f*'g,T)/HC(g,T)) * (HT(f,T) *' g) by RLVECT_1:def 7
 .= fg - (HC(f*'g,T)/HC(g,T)) * (HT(f,T) *' g) by TERMORD:def 7
 .= fg - ((HC(f,T)*HC(g,T))/HC(g,T)) * (HT(f,T) *' g) by TERMORD:32
 .= fg - ((HC(f,T)*HC(g,T))*HC(g,T)") * (HT(f,T) *' g) by VECTSP_1:def 23
 .= fg - (HC(f,T)*(HC(g,T)*HC(g,T)")) * (HT(f,T) *' g) by GROUP_1:def 4
 .= fg - (HC(f,T)*1_L) * (HT(f,T) *' g) by A7,VECTSP_1:def 22
 .= fg - HC(f,T) * (HT(f,T) *' g) by GROUP_1:def 5
 .= fg - Monom(HC(f,T),HT(f,T)) *' g by POLYRED:22
 .= fg - HM(f,T) *' g by TERMORD:def 8
 .= fg + -(HM(f,T) *' g) by POLYNOM1:def 23
 .= (f*'g+p) + (r *' g) by POLYRED:6
 .= (f*'g + r *' g) + p by POLYNOM1:80
 .= g *' (f + -HM(f,T)) + p by POLYNOM1:85
 .= (f - HM(f,T)) *' g + p by POLYNOM1:def 23
 .= Red(f,T) *' g + p by TERMORD:def 9;
  hence thesis by A6,A5,POLYRED:def 7;
end;

theorem Th49:
for n being Ordinal,
    T being connected admissible TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative left_unital right_unital distributive
            Abelian Field-like (non trivial doubleLoopStr),
    P be Subset of Polynom-Ring(n,L),
    f,g being Polynomial of n,L
st PolyRedRel(P,T) reduces f,g holds PolyRedRel(P,T) reduces -f,-g
proof
let n be Ordinal, T be connected admissible TermOrder of n,
    L be add-associative right_complementable right_zeroed
         commutative associative left_unital right_unital distributive
         Abelian Field-like (non trivial doubleLoopStr),
    P be Subset of Polynom-Ring(n,L), f,g be Polynomial of n,L;
assume PolyRedRel(P,T) reduces f,g;
then consider R being RedSequence of PolyRedRel(P,T) such that
A1: R.1 = f & R.len R = g by REWRITE1:def 3;
defpred P[Element of NAT] means
  for q being Polynomial of n,L st q = R.$1
  holds PolyRedRel(P,T) reduces -f,-q;
A2: P[1] by A1,REWRITE1:13;
A3: now let k be Element of NAT; assume A4: 1 <= k & k < len R;
    assume A5: P[k];
    1 < len R by A4,XXREAL_0:2;
    then reconsider p = R.k as Polynomial of n,L by A4,Lm5;
    A6: PolyRedRel(P,T) reduces -f,-p by A5;
    now let q be Polynomial of n,L; assume A7: q = R.(k+1);
      A8: k in dom R by A4,FINSEQ_3:27;
      1 <= k+1 & k+1 <= len R by A4,NAT_1:13;
      then k+1 in dom R by FINSEQ_3:27;
      then [R.k, R.(k+1)] in PolyRedRel(P,T) by A8,REWRITE1:def 2;
      then p reduces_to q,P,T by A7,POLYRED:def 13;
      then consider l being Polynomial of n,L such that
      A9: l in P & p reduces_to q,l,T by POLYRED:def 7;
      -p reduces_to -q,l,T by A9,Th45;
      then -p reduces_to -q,P,T by A9,POLYRED:def 7;
      then [-p,-q] in PolyRedRel(P,T) by POLYRED:def 13;
      then PolyRedRel(P,T) reduces -p,-q by REWRITE1:16;
      hence PolyRedRel(P,T) reduces -f,-q by A6,REWRITE1:17;
      end;
    hence P[k+1];
    end;
A10: for i being Element of NAT st 1<=i & i<=len R holds P[i]
from POLYNOM2:sch 4(A2,A3);
0 < len R by REWRITE1:def 2;
then 1 <= len R by NAT_1:14;
hence thesis by A1,A10;
end;

theorem
for n being Ordinal,
    T being connected admissible TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative left_unital right_unital distributive
            Abelian Field-like (non trivial doubleLoopStr),
    f,f1,g,p being Polynomial of n,L
st PolyRedRel({p},T) reduces f,f1 &
   for b1 being bag of n st b1 in Support g holds not(HT(p,T) divides b1)
holds PolyRedRel({p},T) reduces f+g,f1+g
proof
let n be Ordinal, T be connected admissible TermOrder of n,
    L be add-associative right_complementable right_zeroed
         commutative associative left_unital right_unital distributive
         Abelian Field-like (non trivial doubleLoopStr),
    f,f1,g,p be Polynomial of n,L;
assume A1: PolyRedRel({p},T) reduces f,f1 &
   for b1 being bag of n st b1 in Support g holds not(HT(p,T) divides b1);
then consider R being RedSequence of PolyRedRel({p},T) such that
A2: R.1 = f & R.len R = f1 by REWRITE1:def 3;
defpred P[Element of NAT] means
  for q being Polynomial of n,L st q = R.$1
  holds PolyRedRel({p},T) reduces f+g,q+g;
A3: P[1] by A2,REWRITE1:13;
A4: now let k be Element of NAT; assume A5: 1 <= k & k < len R;
    assume A6: P[k];
    1 < len R by A5,XXREAL_0:2;
    then reconsider h = R.k as Polynomial of n,L by A5,Lm5;
    A7: PolyRedRel({p},T) reduces f+g,h+g by A6;
    now let q be Polynomial of n,L; assume A8: q = R.(k+1);
      A9: k in dom R by A5,FINSEQ_3:27;
      1 <= k+1 & k+1 <= len R by A5,NAT_1:13;
      then k+1 in dom R by FINSEQ_3:27;
      then [R.k, R.(k+1)] in PolyRedRel({p},T) by A9,REWRITE1:def 2;
      then h reduces_to q,{p},T by A8,POLYRED:def 13;
      then h+g reduces_to q+g,{p},T by A1,Th46;
      then [h+g,q+g] in PolyRedRel({p},T) by POLYRED:def 13;
      then PolyRedRel({p},T) reduces h+g,q+g by REWRITE1:16;
      hence PolyRedRel({p},T) reduces f+g,q+g by A7,REWRITE1:17;
      end;
    hence P[k+1];
    end;
A10: for i being Element of NAT st 1<=i & i<=len R holds P[i]
from POLYNOM2:sch 4(A3,A4);
0 < len R by REWRITE1:def 2;
then 1 <= len R by NAT_1:14;
hence thesis by A2,A10;
end;

theorem Th51:
for n being Ordinal,
    T being connected admissible TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative left_unital right_unital distributive
            Abelian Field-like (non trivial doubleLoopStr),
    f,g being non-zero Polynomial of n,L
holds PolyRedRel({g},T) reduces f*'g,0_(n,L)
proof
let n be Ordinal, T be connected admissible TermOrder of n,
    L be add-associative right_complementable right_zeroed
         commutative associative left_unital right_unital distributive
         Abelian Field-like (non trivial doubleLoopStr),
    f,g be non-zero Polynomial of n,L;
defpred P[Element of NAT] means
  for f being Polynomial of n,L st card(Support f) = $1
  holds PolyRedRel({g},T) reduces f*'g,0_(n,L);
now let f be Polynomial of n,L; assume card(Support f) = 0;
  then Support f,0 are_equipotent by CARD_1:def 5;
  then Support f = {} by CARD_1:46;
  then f = 0_(n,L) by POLYNOM7:1;
  then f*'g = 0_(n,L) by POLYRED:5;
  hence PolyRedRel({g},T) reduces f*'g,0_(n,L) by REWRITE1:13;
  end;
then A1: P[0];
A2: now let k be Element of NAT; assume A3: P[k];
    now let f be Polynomial of n,L; assume A4: card(Support f) = k+1;
      now assume f = 0_(n,L);
        then Support f = {} by POLYNOM7:1;
        then Card(Support f) = 0 by CARD_1:def 5;
        hence contradiction by A4;
        end;
      then reconsider f1 = f as non-zero Polynomial of n,L by POLYNOM7:def 2;
      set rf = Red(f,T);
      f1 <> 0_(n,L) by POLYNOM7:def 2;
      then Support f <> {} by POLYNOM7:1;
      then HT(f,T) in Support f by TERMORD:def 6; then
      for u being set st u in {HT(f,T)} holds u in Support f by TARSKI:def 1;
      then A5: {HT(f,T)} c= Support f by TARSKI:def 3;
      Support rf = Support(f) \ {HT(f,T)} by TERMORD:36; then
      card(Support rf) = card(Support f) - card({HT(f,T)}) by A5,CARD_2:63
                      .= (k + 1) - 1 by CARD_1:50,A4
                      .= k + 0;
      then A6: PolyRedRel({g},T) reduces rf*'g,0_(n,L) by A3;
      f1*'g reduces_to rf*'g,{g},T by Th47;
      then [f1*'g,rf*'g] in PolyRedRel({g},T) by POLYRED:def 13;
      then PolyRedRel({g},T) reduces f*'g,rf*'g by REWRITE1:16;
      hence PolyRedRel({g},T) reduces f*'g,0_(n,L) by REWRITE1:17,A6;
      end;
    hence P[k+1];
    end;
A7: for k being Element of NAT holds P[k] from NAT_1:sch 1(A1,A2);
consider n being Element of NAT such that A8: card(Support f) = n;
thus thesis by A7,A8;
end;


begin :: The Criterium

theorem Th52:
for n being Ordinal,
    T being connected admissible TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative left_unital right_unital distributive
            Field-like (non trivial doubleLoopStr),
    p1,p2 being Polynomial of n,L st HT(p1,T),HT(p2,T) are_disjoint
for b1,b2 being bag of n st b1 in Support Red(p1,T) & b2 in Support Red(p2,T)
holds not(HT(p1,T) + b2 = HT(p2,T) + b1)
proof
let n be Ordinal, T be connected admissible TermOrder of n,
    L be add-associative right_complementable right_zeroed
         commutative associative left_unital right_unital distributive
         Field-like (non trivial doubleLoopStr),
    p1,p2 be Polynomial of n,L;
assume A1: HT(p1,T),HT(p2,T) are_disjoint;
let b1,b2 be bag of n;
assume A2: b1 in Support Red(p1,T) & b2 in Support Red(p2,T);
A3: Support(Red(p1,T)) c= Support(p1) &  Support(Red(p2,T)) c= Support(p2)
   by TERMORD:35;
now assume b1 = HT(p1,T);
  then Red(p1,T).b1 = 0.L by TERMORD:39;
  hence contradiction by A2,POLYNOM1:def 9;
  end;
hence thesis by A3,A2,A1,Lm6;
end;

theorem Th53:
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative left_unital right_unital distributive
            Abelian Field-like (non trivial doubleLoopStr),
    p1,p2 being Polynomial of n,L
st HT(p1,T),HT(p2,T) are_disjoint
holds S-Poly(p1,p2,T) = HM(p2,T) *' Red(p1,T) - HM(p1,T) *' Red(p2,T)
proof
let n be Ordinal, T be connected TermOrder of n,
    L be add-associative right_complementable right_zeroed
         commutative associative left_unital right_unital distributive
         Abelian Field-like (non trivial doubleLoopStr),
    p1,p2 be Polynomial of n,L;
assume HT(p1,T),HT(p2,T) are_disjoint;
then lcm(HT(p1,T),HT(p2,T)) = HT(p1,T) + HT(p2,T) by GROEB_2:9;
hence S-Poly(p1,p2,T)
  = HC(p2,T) * ((HT(p1,T) + HT(p2,T))/HT(p1,T)) *' p1 -
    HC(p1,T) * ((HT(p1,T) + HT(p2,T))/HT(p2,T)) *' p2 by GROEB_2:def 4
 .= HC(p2,T) * HT(p2,T) *' p1 -
    HC(p1,T) * ((HT(p1,T) + HT(p2,T))/HT(p2,T)) *' p2 by Th1
 .= HC(p2,T) * HT(p2,T) *' p1 -
    HC(p1,T) * HT(p1,T) *' p2 by Th1
 .= HC(p2,T) * HT(p2,T) *' (HM(p1,T) + Red(p1,T)) -
    HC(p1,T) * HT(p1,T) *' p2 by TERMORD:38
 .= HC(p2,T) * HT(p2,T) *' (HM(p1,T) + Red(p1,T)) -
    HC(p1,T) * HT(p1,T) *' (HM(p2,T) + Red(p2,T)) by TERMORD:38
 .= Monom(HC(p2,T),HT(p2,T)) *' (HM(p1,T) + Red(p1,T)) -
    HC(p1,T) * HT(p1,T) *' (HM(p2,T) + Red(p2,T)) by POLYRED:22
 .= Monom(HC(p2,T),HT(p2,T)) *' (HM(p1,T) + Red(p1,T)) -
    Monom(HC(p1,T),HT(p1,T)) *' (HM(p2,T) + Red(p2,T)) by POLYRED:22
 .= HM(p2,T) *' (HM(p1,T) + Red(p1,T)) -
    Monom(HC(p1,T),HT(p1,T)) *' (HM(p2,T) + Red(p2,T)) by TERMORD:def 8
 .= HM(p2,T) *' (HM(p1,T) + Red(p1,T)) -
    HM(p1,T) *' (HM(p2,T) + Red(p2,T)) by TERMORD:def 8
 .= (HM(p2,T) *' HM(p1,T) + HM(p2,T) *' Red(p1,T)) -
    HM(p1,T) *' (HM(p2,T) + Red(p2,T)) by POLYNOM1:85
 .= (HM(p2,T) *' HM(p1,T) + HM(p2,T) *' Red(p1,T)) -
    (HM(p1,T) *' HM(p2,T) + HM(p1,T) *' Red(p2,T)) by POLYNOM1:85
 .= (HM(p2,T) *' HM(p1,T) + HM(p2,T) *' Red(p1,T)) +
    -((HM(p1,T) *' HM(p2,T)) + (HM(p1,T) *' Red(p2,T))) by POLYNOM1:def 23
 .= (HM(p2,T) *' HM(p1,T) + HM(p2,T) *' Red(p1,T)) +
    (-(HM(p1,T) *' HM(p2,T)) + -(HM(p1,T) *' Red(p2,T))) by POLYRED:1
 .= HM(p2,T) *' Red(p1,T) + (HM(p2,T) *' HM(p1,T) +
    (-(HM(p1,T) *' HM(p2,T)) + -(HM(p1,T) *' Red(p2,T)))) by POLYNOM1:80
 .= HM(p2,T) *' Red(p1,T) + ((HM(p2,T) *' HM(p1,T) +
    -(HM(p1,T) *' HM(p2,T))) + -(HM(p1,T) *' Red(p2,T))) by POLYNOM1:80
 .= HM(p2,T) *' Red(p1,T) + (0_(n,L) + -(HM(p1,T) *' Red(p2,T))) by POLYRED:3
 .= HM(p2,T) *' Red(p1,T) + -(HM(p1,T) *' Red(p2,T)) by POLYRED:2
 .= HM(p2,T) *' Red(p1,T) - (HM(p1,T) *' Red(p2,T)) by POLYNOM1:def 23;
end;

theorem Th54:
for n being Ordinal,
    T being connected TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative left_unital right_unital distributive
            Abelian Field-like (non trivial doubleLoopStr),
    p1,p2 being Polynomial of n,L
st HT(p1,T),HT(p2,T) are_disjoint
holds S-Poly(p1,p2,T) = Red(p1,T) *' p2 - Red(p2,T) *' p1
proof
let n be Ordinal, T be connected TermOrder of n,
    L be add-associative right_complementable right_zeroed
         commutative associative left_unital right_unital distributive
         Abelian Field-like (non trivial doubleLoopStr),
    p1,p2 be Polynomial of n,L;
assume A1: HT(p1,T),HT(p2,T) are_disjoint;
reconsider r1 = -Red(p1,T),
           r2 = -Red(p2,T) as Polynomial of n,L;
r2 *' Red(p1,T) = -(Red(p2,T) *' Red(p1,T)) by POLYRED:6
               .= r1 *' Red(p2,T) by POLYRED:6;
then A2: (r2 *' Red(p1,T) + -(r1 *' Red(p2,T))) = 0_(n,L) by POLYRED:3;
thus S-Poly(p1,p2,T)
   = HM(p2,T) *' Red(p1,T) - (HM(p1,T) *' Red(p2,T)) by A1,Th53
  .= (p2 - Red(p2,T)) *' Red(p1,T) - (HM(p1,T) *' Red(p2,T)) by Th15
  .= (p2 - Red(p2,T)) *' Red(p1,T) - ((p1 - Red(p1,T)) *' Red(p2,T)) by Th15
  .= (p2 + -Red(p2,T)) *' Red(p1,T)
           - ((p1 - Red(p1,T)) *' Red(p2,T)) by POLYNOM1:def 23
  .= (p2 + -Red(p2,T)) *' Red(p1,T)
           - ((p1 + -Red(p1,T)) *' Red(p2,T)) by POLYNOM1:def 23
  .= (p2 *' Red(p1,T) + r2 *' Red(p1,T))
           - ((p1 + -Red(p1,T)) *' Red(p2,T)) by POLYNOM1:85
  .= (p2 *' Red(p1,T) + r2 *' Red(p1,T))
          - (p1 *' Red(p2,T) + r1 *' Red(p2,T)) by POLYNOM1:85
  .= (p2 *' Red(p1,T) + r2 *' Red(p1,T))
          + -(p1 *' Red(p2,T) + r1 *' Red(p2,T)) by POLYNOM1:def 23
  .= (p2 *' Red(p1,T) + r2 *' Red(p1,T))
          + (-(p1 *' Red(p2,T)) + -(r1 *' Red(p2,T))) by POLYRED:1
  .= p2 *' Red(p1,T) + (r2 *' Red(p1,T)
          + (-(r1 *' Red(p2,T)) + -(p1 *' Red(p2,T)))) by POLYNOM1:80
  .= p2 *' Red(p1,T) + (0_(n,L) + -(p1 *' Red(p2,T))) by A2,POLYNOM1:80
  .= p2 *' Red(p1,T) + -(p1 *' Red(p2,T)) by POLYRED:2
  .= Red(p1,T) *' p2 - Red(p2,T) *' p1 by POLYNOM1:def 23;
end;

theorem Th55:
for n being Ordinal,
    T being connected admissible TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative left_unital right_unital distributive
            Abelian Field-like (non trivial doubleLoopStr),
    p1,p2 being non-zero Polynomial of n,L
st HT(p1,T),HT(p2,T) are_disjoint &
   Red(p1,T) is non-zero & Red(p2,T) is non-zero
holds PolyRedRel({p1},T) reduces
           HM(p2,T)*'Red(p1,T) - HM(p1,T)*'Red(p2,T), p2 *' Red(p1,T)
proof
let n be Ordinal, T be connected admissible TermOrder of n,
    L be add-associative right_complementable right_zeroed
         commutative associative left_unital right_unital distributive
         Abelian Field-like (non trivial doubleLoopStr),
    p1,p2 be non-zero Polynomial of n,L;
assume A1: HT(p1,T),HT(p2,T) are_disjoint &
           Red(p1,T) is non-zero & Red(p2,T) is non-zero;
then reconsider red1 = Red(p1,T), red2 = Red(p2,T)
  as non-zero Polynomial of n,L;
set j = card(Support p2);
p2 <> 0_(n,L) by POLYNOM7:def 2;
then Support p2 <> {} by POLYNOM7:1;
then HT(p2,T) in Support p2 by TERMORD:def 6;
then for t being set holds
     t in {HT(p2,T)} implies t in Support p2 by TARSKI:def 1;
then A2: {HT(p2,T)} c= Support p2 by TARSKI:def 3;
A3: card(Support red2)
  = card(Support(p2) \ {HT(p2,T)}) by TERMORD:36
 .= card(Support(p2)) - card({HT(p2,T)}) by A2,CARD_2:63
 .= j - 1 by CARD_2:60;
now assume j = 0;
  then Support p2,0 are_equipotent by CARD_1:def 5;
  then Support p2 = {} by CARD_1:46;
  then p2 = 0_(n,L) by POLYNOM7:1;
  hence contradiction by POLYNOM7:def 2;
  end;
then A4: 1 <= j by NAT_1:14;
then 1 - 1 <= j - 1 by XREAL_1:11;
then reconsider j' = j - 1 as Element of NAT by INT_1:16;
defpred P[Element of NAT] means
  for m being Element of NAT st m <= card(Support p2) &
  card(Support Low(p2,T,m)) = $1
  holds PolyRedRel({p1},T) reduces
      HM(p2,T)*'Red(p1,T) - HM(p1,T)*'Red(p2,T),
      HM(p2,T)*'Red(p1,T)
          - HM(p1,T)*'(Red(p2,T)-Low(p2,T,m)) + Red(p1,T)*'Low(p2,T,m);
A5: P[0]
    proof
    let m be Element of NAT;
    assume m <= card(Support p2) & card(Support Low(p2,T,m)) = 0;
    then Support Low(p2,T,m),0 are_equipotent by CARD_1:def 5;
    then Support Low(p2,T,m) = {} by CARD_1:46;
    then Low(p2,T,m) = 0_(n,L) by POLYNOM7:1;
    then HM(p2,T)*'Red(p1,T)
         - HM(p1,T)*'(Red(p2,T)-Low(p2,T,m)) + Red(p1,T)*'Low(p2,T,m)
       = HM(p2,T)*'Red(p1,T)
         - HM(p1,T)*'Red(p2,T) + Red(p1,T)*'0_(n,L) by POLYRED:4
      .= HM(p2,T)*'Red(p1,T) - HM(p1,T)*'Red(p2,T) + 0_(n,L) by POLYRED:5
      .= HM(p2,T)*'Red(p1,T) - HM(p1,T)*'Red(p2,T) by POLYRED:2;
    hence thesis by REWRITE1:13;
    end;
A6: for k being Element of NAT st 0 <= k & k < j' holds P[k] implies P[k+1]
    proof
    let k be Element of NAT;
    assume A7: 0 <= k & k < j';
    now assume A8: P[k];
      now let m be Element of NAT;
      assume A9: m <= card(Support p2) & card(Support Low(p2,T,m)) = k+1;
      set m' = m + 1;
      now assume m = card(Support p2);
        then Low(p2,T,m) = 0_(n,L) by Th35;
        hence contradiction by A9,CARD_1:47,POLYNOM7:1;
        end;
      then A10: m < card(Support p2) by A9,REAL_1:def 5;
      then A11: m' <= card(Support p2) by NAT_1:13;
      Support Low(p2,T,m') c= Support Low(p2,T,m) by Th41,A10;
      then A12: card(Support Low(p2,T,m) \ Support Low(p2,T,m')) =
           card(Support Low(p2,T,m)) - card(Support Low(p2,T,m')) by CARD_2:63;
      k + 1 - card(Support Low(p2,T,m'))
         = card {HT(Low(p2,T,m),T)} by A12,Th42,A10,A9
        .= 1 by CARD_1:50;
      then A13: k + 1 = card(Support Low(p2,T,m')) + 1; then
      A14: m' <= card(Support p2) & card(Support Low(p2,T,m')) = k
           by A10,NAT_1:13;
      A15: 1 <= m' by NAT_1:14;
      set f = HM(p2,T)*'Red(p1,T)
              - HM(p1,T)*'(Red(p2,T)-Low(p2,T,m')) + Red(p1,T)*'Low(p2,T,m');
      red1 <> 0_(n,L) & HM(p2,T) <> 0_(n,L) by POLYNOM7:def 2;
      then Support red1 <> {} & Support HM(p2,T) <> {} by POLYNOM7:1;
      then HT(red1,T) in Support red1 & HT(HM(p2,T),T) in Support HM(p2,T)
           by TERMORD:def 6;
      then red1.(HT(red1,T)) <> 0.L &
           HM(p2,T).(HT(HM(p2,T),T)) <> 0.L by POLYNOM1:def 9;
      then HC(red1,T) <> 0.L & HC(HM(p2,T),T) <> 0.L by TERMORD:def 7;
      then A16: HC(HM(p2,T),T)*HC(red1,T) <> 0.L by VECTSP_2:def 5;
      A17: HT(HM(p2,T)*'red1,T) = HT(HM(p2,T),T) + HT(red1,T) by TERMORD:31;
      HC(HM(p2,T)*'red1,T) <> 0.L by A16,TERMORD:32;
      then A18: (HM(p2,T)*'red1).(HT(HM(p2,T),T) + HT(red1,T)) <> 0.L
                 by A17,TERMORD:def 7;
      A19: HT(HM(p2,T),T) + HT(red1,T) is Element of Bags n by POLYNOM1:def 14;
      now assume A20: HT(HM(p2,T),T) + HT(red1,T) in Support(red1*'Low(p2,T,m')
);
        Support(red1*'Low(p2,T,m')) c=
                  {s + t where s,t is Element of Bags n :
                           s in Support red1 & t in Support Low(p2,T,m')}
           by TERMORD:30;
        then HT(HM(p2,T),T) + HT(red1,T) in
                  {s + t where s,t is Element of Bags n :
                           s in Support red1 & t in Support Low(p2,T,m')} by
A20;
        then consider s,t being Element of Bags n such that
        A21: HT(HM(p2,T),T) + HT(red1,T) = s + t &
           s in Support red1 & t in Support Low(p2,T,m');
        A22: t < HT(p2,T),T
           proof
           now per cases;
           case m' = card(Support p2);
             then Low(p2,T,m') = 0_(n,L) by Th35;
             hence contradiction by A21,POLYNOM7:1;
             end;
           case m' <> card(Support p2);
             then m' < card(Support p2) by A11,REAL_1:def 5;
             then A23: HT(Low(p2,T,m'),T) < HT(p2,T),T by Th39;
             t <= HT(Low(p2,T,m'),T),T by A21,TERMORD:def 6;
             hence t < HT(p2,T),T by A23,Th3;
             end;
           end;
           hence thesis;
           end;
        s <= HT(red1,T),T by A21,TERMORD:def 6;
        then s + t < HT(p2,T) + HT(red1,T),T by Th5,A22;
        then s + t < HT(HM(p2,T),T) + HT(red1,T),T by TERMORD:26;
        hence contradiction by A21,TERMORD:def 3;
        end;
      then A24: (red1*'Low(p2,T,m')).(HT(HM(p2,T),T) + HT(red1,T)) = 0.L
                by A19,POLYNOM1:def 9;
      now assume A25: HT(HM(p2,T),T) + HT(red1,T) in
                    Support(HM(p1,T)*'(red2-Low(p2,T,m')));
        Support(HM(p1,T)*'(red2-Low(p2,T,m'))) c=
                  {s + t where s,t is Element of Bags n :
                     s in Support HM(p1,T) & t in Support(red2-Low(p2,T,m'))}
           by TERMORD:30;
        then HT(HM(p2,T),T) + HT(red1,T) in
              {s + t where s,t is Element of Bags n :
               s in Support HM(p1,T) & t in Support(red2-Low(p2,T,m'))} by A25;
        then consider s,t being Element of Bags n such that
        A26: HT(HM(p2,T),T) + HT(red1,T) = s + t &
           s in Support HM(p1,T) & t in Support(red2-Low(p2,T,m'));
        A27: HT(HM(p2,T),T) = HT(p2,T) by TERMORD:26;
        red1 <> 0_(n,L) by POLYNOM7:def 2;
        then Support red1 <> {} by POLYNOM7:1;
        then A28: HT(red1,T) in Support red1 by TERMORD:def 6;
        HM(p1,T) <> 0_(n,L) by POLYNOM7:def 2;
        then Support HM(p1,T) <> {} by POLYNOM7:1;
        then Support HM(p1,T) = {HT(p1,T)} by TERMORD:21;
        then A29: s = HT(p1,T) by A26,TARSKI:def 1;
        A30: red2 - Low(p2,T,m') = red2 + -Low(p2,T,m') by POLYNOM1:def 23;
        Support(-Low(p2,T,m')) = Support Low(p2,T,m') by GROEB_1:5;
        then A31: Support(red2 - Low(p2,T,m')) c=
                 Support red2 \/ Support Low(p2,T,m') by A30,POLYNOM1:79;
        A32: Support Low(p2,T,m') c= Support red2 by A11,A15,Th27;
        t in Support red2
          proof
          now per cases by A31,A26,XBOOLE_0:def 2;
          case t in Support red2;
            hence thesis;
            end;
          case t in Support Low(p2,T,m');
            hence thesis by A32;
            end;
          end;
          hence thesis;
          end;
        hence contradiction by A27,A28,A29,A26,A1,Th52;
        end;
      then (HM(p1,T)*'(red2-Low(p2,T,m'))).(HT(HM(p2,T),T) + HT(red1,T))
                = 0.L by A19,POLYNOM1:def 9;
      then A33: - (HM(p1,T)*'(red2-Low(p2,T,m'))).(HT(HM(p2,T),T) + HT(red1,T))
               = 0.L by RLVECT_1:25;
      A34: HT(HM(p2,T),T) + HT(red1,T) is Element of Bags n by POLYNOM1:def 14;
      f.(HT(HM(p2,T),T) + HT(red1,T))
        = ((HM(p2,T)*'red1+-HM(p1,T)*'(red2-Low(p2,T,m')))+red1*'Low(p2,T,m')).
          (HT(HM(p2,T),T) + HT(red1,T)) by POLYNOM1:def 23
       .= (HM(p2,T)*'red1 + -HM(p1,T)*'(red2-Low(p2,T,m'))).
                (HT(HM(p2,T),T) + HT(red1,T)) + 0.L by A24,POLYNOM1:def 21
       .= (HM(p2,T)*'red1 + -HM(p1,T)*'(red2-Low(p2,T,m'))).
                (HT(HM(p2,T),T) + HT(red1,T)) by RLVECT_1:def 7
       .= (HM(p2,T)*'red1).(HT(HM(p2,T),T) + HT(red1,T))
          + (-HM(p1,T)*'(red2-Low(p2,T,m'))).
                (HT(HM(p2,T),T) + HT(red1,T)) by POLYNOM1:def 21
       .= (HM(p2,T)*'red1).(HT(HM(p2,T),T) + HT(red1,T))
          + 0.L by A33,POLYNOM1:def 22
       .= (HM(p2,T)*'red1).(HT(HM(p2,T),T) + HT(red1,T)) by RLVECT_1:def 7;
      then HT(HM(p2,T),T) + HT(red1,T) in Support f by A18,A34,POLYNOM1:def 9;
      then A35: f <> 0_(n,L) by POLYNOM7:1;
      red2 <> 0_(n,L) by POLYNOM7:def 2;
      then A36: Support red2 <> {} by POLYNOM7:1;
      A37: Support Low(p2,T,m') = Lower_Support(p2,T,m') by Lm4,A11;
      now assume A38: HT(red2,T) in Support Low(p2,T,m');
         Support Low(p2,T,m') c= Support red2 by A15,A11,Th27;
         then A39: for t being set holds
         t in Support Low(p2,T,m') implies t in Support red2;
         now let t be set; assume A40: t in Support red2;
            then reconsider t' = t as bag of n by POLYNOM1:def 14;
           A41: Support red2 c= Support p2 by TERMORD:35;
           t' <= HT(red2,T),T by A40,TERMORD:def 6;
           hence t in Support Low(p2,T,m') by A38,A41,A37,A40,A11,Th24;
           end;
         then j' = k by  A3,A39,TARSKI:2,A13;
         hence contradiction by A7;
         end;
      then Low(p2,T,m') <> red2 by A36,TERMORD:def 6;
      then Red(p2,T) - Low(p2,T,m') <> 0_(n,L) by Th12;
      then reconsider z1 = Red(p2,T) - Low(p2,T,m')
                           as non-zero Polynomial of n,L by POLYNOM7:def 2;
      z1 = Red(p2,T) + -Low(p2,T,m') by POLYNOM1:def 23;
      then Support z1 c= Support Red(p2,T) \/ Support -Low(p2,T,m')
           by POLYNOM1:79;
      then A42: Support z1 c= Support Red(p2,T) \/ Support Low(p2,T,m')
               by GROEB_1:5;
      Support Low(p2,T,m') c= Support Red(p2,T) by A11,A15,Th27;
      then Support Red(p2,T) \/ Support Low(p2,T,m') c=
           Support Red(p2,T) \/ Support Red(p2,T)  by XBOOLE_1:9;
      then A43: Support z1 c= Support red2 by A42,XBOOLE_1:1;
      reconsider z = HM(p1,T) *' z1 as non-zero Polynomial of n,L;
      z <> 0_(n,L) by POLYNOM7:def 2;
      then Support z <> {} by POLYNOM7:1;
      then card(Support z) <> 0 by CARD_2:59;
      then 1 <= card(Support z) by NAT_1:14;
      then reconsider w = card(Support z) - 1 as Element of NAT by INT_1:18;
      reconsider lowzw = Low(z,T,w) as non-zero Monomial of n,L by Th37;
      A44: card(Support z) = card(Support z1) by Th10;
      w = card(Support z1) - 1 by Th10; then
      reconsider lowz1w = Low(z1,T,w) as non-zero Monomial of n,L by Th37;
      set b = term(lowzw);
      A45: b is Element of Bags n by POLYNOM1:def 14;
      set s = b / HT(p1,T);
      A46: s is Element of Bags n by POLYNOM1:def 14;
      set g = f - (f.b/HC(p1,T)) * (s *' p1);
      A47: p1 <> 0_(n,L) by POLYNOM7:def 2;
      lowzw <> 0_(n,L) by POLYNOM7:def 2;
      then Support lowzw <> {} by POLYNOM7:1;
      then Support lowzw = {b} by POLYNOM7:7;
      then A48: b in Support lowzw by TARSKI:def 1;
      card(Support z) = w + 1;
      then A49: w < card(Support z) by NAT_1:16;
      then A50: Support(lowzw) c= Support(z) by Th26;
      then A51: b in Support HM(p1,T) *' z1 by A48;
      A52: (HM(p1,T) *' z1).b <> 0.L by A48,A50,POLYNOM1:def 9;
      Support(HM(p1,T) *' z1) c= {t' + t where t',t is Element of Bags n :
                   t' in Support HM(p1,T) & t in Support z1} by TERMORD:30;
      then b in {t' + t where t',t is Element of Bags n :
                   t' in Support HM(p1,T) & t in Support z1} by A51;
      then consider t',t being Element of Bags n such that
      A53: b =  t' + t & t' in Support HM(p1,T) & t in Support z1;
      HM(p1,T) <> 0_(n,L) by POLYNOM7:def 2;
      then Support HM(p1,T) <> {} by POLYNOM7:1;
      then Support HM(p1,T) = {term(HM(p1,T))} by POLYNOM7:7
                               .= {HT(p1,T)} by TERMORD:22;
      then A54: t' = HT(p1,T) by A53,TARSKI:def 1;
      then A55: HT(p1,T) divides b by A53,POLYNOM1:54;
      then A56: s + HT(p1,T) = b by GROEB_2:def 1;
      A57: s = s +HT(p1,T)-'HT(p1,T) by POLYNOM1:52 .= t by A56,A53,A54,
POLYNOM1:52;
      then A58: s in Support Red(p2,T) by A53,A43;
      A59: Support Red(p2,T) c= Support p2 by TERMORD:35;
      s in (Support(p2) \ {HT(p2,T)}) by A58,TERMORD:36;
      then not(s in {HT(p2,T)}) by XBOOLE_0:def 4;
      then A60: s <> HT(p2,T) by TARSKI:def 1;
      then A61: Red(p2,T).s = p2.s by A58,A59,TERMORD:40;
      A62: now assume s in Support Low(p2,T,m');
           then A63: s in Lower_Support(p2,T,m') by A11,Th25;
           s in Support(p2|Lower_Support(p2,T,m')) by A14,A63,Lm4;
           then A64: p2.s = Low(p2,T,m').s by Th16;
           (Red(p2,T) - Low(p2,T,m')).s
              = (Red(p2,T) + -Low(p2,T,m')).s by POLYNOM1:def 23
             .= Red(p2,T).s + (-Low(p2,T,m')).s by POLYNOM1:def 21
             .= Red(p2,T).s + -(Low(p2,T,m').s) by POLYNOM1:def 22
             .= 0.L by A64,A61,RLVECT_1:16;
           hence contradiction by POLYNOM1:def 9,A53,A57;
           end;
      then A65: Low(p2,T,m').s = 0.L by A46,POLYNOM1:def 9;
      A66: now assume A67: b in Support(HM(p2,T)*'Red(p1,T));
          Support(HM(p2,T)*'Red(p1,T)) c= {u+v where u,v is Element of Bags n :
              u in Support HM(p2,T) & v in Support Red(p1,T)} by TERMORD:30;
          then b in {u + v where u,v is Element of Bags n :
              u in Support HM(p2,T) & v in Support Red(p1,T)} by A67;
          then consider t',t being Element of Bags n such that
          A68: b = t' + t & t' in Support HM(p2,T) & t in Support Red(p1,T);
          HM(p2,T) <> 0_(n,L) by POLYNOM7:def 2;
          then A69: Support HM(p2,T) <> {} by POLYNOM7:1;
          then HT(HM(p2,T),T) in Support HM(p2,T) by TERMORD:def 6;
          then A70: HT(p2,T) in Support HM(p2,T) by TERMORD:26;
          consider x being bag of n such that
          A71: Support HM(p2,T) = {x} by A69,POLYNOM7:6;
          Support HM(p2,T) = {HT(p2,T)} by A71,A70,TARSKI:def 1;
          then t' = HT(p2,T) by A68,TARSKI:def 1;
          hence contradiction by A68,A56,A57,A53,A43,A1,Th52;
          end;
      A72: now assume (Red(p1,T)*'Low(p2,T,m')).b <> 0.L;
        then A73: b in Support(Red(p1,T)*'Low(p2,T,m')) by A45,POLYNOM1:def 9;
        Support(Red(p1,T)*'Low(p2,T,m')) c=
           {u + v where u,v is Element of Bags n :
            u in Support Red(p1,T) & v in Support Low(p2,T,m')} by TERMORD:30;
        then b in {u + v where u,v is Element of Bags n :
                   u in Support Red(p1,T) & v in Support Low(p2,T,m')} by A73;
        then consider t',t being Element of Bags n such that
        A74: b =  t' + t & t' in Support Red(p1,T) & t in Support Low(p2,T,m');
        A75: s + HT(p1,T) = t' + t by A55,A74,GROEB_2:def 1;
        Support Red(p1,T) c= Support p1 by TERMORD:35;
        then A76: t' <= HT(p1,T),T by A74,TERMORD:def 6;
        Support(Red(p1,T)) = Support(p1) \ {HT(p1,T)} by TERMORD:36;
        then not(t' in {HT(p1,T)}) by A74,XBOOLE_0:def 4;
        then t' <> HT(p1,T) by TARSKI:def 1;
        then A77: t' < HT(p1,T),T by A76,TERMORD:def 3;
        now assume s < t,T;
          then A78: s <= t,T by TERMORD:def 3;
          t in Lower_Support(p2,T,m') by A11,Th25,A74;
          then s in Lower_Support(p2,T,m') by A11,A78,A58,A59,Th24;
          hence contradiction by A62,A11,Th25;
          end;
        then t <= s,T by TERMORD:5;
        then t + t' < s + HT(p1,T) ,T by A77,Th6;
        hence contradiction by A75,TERMORD:def 3;
        end;
      A79: f.b
        = ((HM(p2,T)*'red1+-HM(p1,T)*'(red2-Low(p2,T,m')))+red1*'Low(p2,T,m')).
          b by POLYNOM1:def 23
       .= (HM(p2,T)*'red1 + -HM(p1,T)*'(red2-Low(p2,T,m'))).b
                + 0.L by A72,POLYNOM1:def 21
       .= (HM(p2,T)*'red1+-HM(p1,T)*'(red2-Low(p2,T,m'))).b by RLVECT_1:def 7
       .= (HM(p2,T)*'red1).b
          + (-HM(p1,T)*'(red2-Low(p2,T,m'))).b by POLYNOM1:def 21
       .= 0.L + (-HM(p1,T)*'(red2-Low(p2,T,m'))).b by A66,A45,POLYNOM1:def 9
       .= (-HM(p1,T)*'(red2-Low(p2,T,m'))).b  by RLVECT_1:def 7
       .= -(HM(p1,T)*'(red2-Low(p2,T,m')).b) by POLYNOM1:def 22;
      now assume f.b = 0.L;
         then (HM(p1,T) *' z1).b = -0.L by A79,RLVECT_1:30;
         hence contradiction by A52,RLVECT_1:25;
         end;
      then b in Support f by A45,POLYNOM1:def 9;
      then f reduces_to g,p1,b,T by A35,A47,A56,POLYRED:def 5;
      then A80: f reduces_to g,p1,T by POLYRED:def 6;
      p1 in {p1} by TARSKI:def 1;
      then f reduces_to g,{p1},T by A80,POLYRED:def 7;
      then [f,g] in PolyRedRel({p1},T) by POLYRED:def 13;
      then A81: PolyRedRel({p1},T) reduces f,g by REWRITE1:16;
      p1 <> 0_(n,L) by POLYNOM7:def 2;
      then A82: HC(p1,T) <> 0.L by TERMORD:17;
      s + HT(p1,T) = term(HM(p1,T) *' lowz1w) by Th44,A56,A49,A44
                  .= term(HM(p1,T)) + term(lowz1w) by Th7
                  .= HT(p1,T) + term(lowz1w) by TERMORD:22;
      then A83: s = HT(p1,T) + term(lowz1w) -' HT(p1,T) by POLYNOM1:52
                 .= term(lowz1w) by POLYNOM1:52;
      w + 1 = card(Support z1) - 1 + 1 by Th10;
      then A84: w <= card(Support z1) by NAT_1:13;
      lowz1w <> 0_(n,L) by POLYNOM7:def 2;
      then Support lowz1w <> {} by POLYNOM7:1;
      then A85: Support lowz1w = {term(lowz1w)} by POLYNOM7:7;
      then A86: s in Support lowz1w by A83,TARSKI:def 1;
      A87: s in Lower_Support(z1,T,w) by A86,A84,Th25;
      A88: Monom(p2.s,s) = HM(Low(p2,T,m),T)
          proof
          A89: now let t be bag of n; assume A90: t in Support z1;
            now assume A91: t < s,T;
              then t <= s,T by TERMORD:def 3;
              then t in Lower_Support(z1,T,w) by A87,A90,A84,Th24;
              then t in Support lowz1w by A84,Th25;
              then t = term lowz1w by A85,TARSKI:def 1;
              hence contradiction by A83,A91,TERMORD:def 3;
              end;
            hence s <= t,T by TERMORD:5;
            end;
          set r = HT(Low(p2,T,m),T);
          Support Low(p2,T,m)\Support Low(p2,T,m') = {HT(Low(p2,T,m),T)}
                by A10,Th42; then
          A92: r in Support Low(p2,T,m)\Support Low(p2,T,m') by TARSKI:def 1;
          then A93: r in Support Low(p2,T,m) by XBOOLE_0:def 4;
          then A94: r in Lower_Support(p2,T,m) by Th25,A9;
          A95: Support Low(p2,T,m) c= Support p2 by Th26,A9;
          A96: not(r in Support Low(p2,T,m')) by A92,XBOOLE_0:def 4;
          now assume A97: r = HT(p2,T);
             A98: now let u be set; assume A99: u in Support p2;
                 then reconsider u' = u as Element of Bags n;
                 u' <= r,T by A97,A99,TERMORD:def 6;
                 then u' in Lower_Support(p2,T,m) by A94,A99,Th24,A9;
                 hence u in Support Low(p2,T,m) by Th25,A9;
                 end;
             for u being set holds u in Support Low(p2,T,m) implies
                u in Support p2 by A95;
             then A100: k + 1 = j by A9,A98,TARSKI:2;
             thus contradiction by A100,A7;
             end;
          then A101: not(r in {HT(p2,T)}) by TARSKI:def 1;
          Support(Red(p2,T)) = Support(p2) \ {HT(p2,T)} by TERMORD:36;
          then A102: r in Support red2 by A101,A93,A95,XBOOLE_0:def 4;
          (Red(p2,T) - Low(p2,T,m')).r
            = (Red(p2,T) + -Low(p2,T,m')).r by POLYNOM1:def 23
           .= Red(p2,T).r + (-Low(p2,T,m')).r by POLYNOM1:def 21
           .= Red(p2,T).r + -(Low(p2,T,m').r) by POLYNOM1:def 22
           .= Red(p2,T).r + -0.L by A96,POLYNOM1:def 9
           .= Red(p2,T).r + 0.L by RLVECT_1:25
           .= Red(p2,T).r by RLVECT_1:def 7;
          then z1.r <> 0.L by A102,POLYNOM1:def 9;
          then r in Support z1 by POLYNOM1:def 9;
          then A103: s <= r,T by A89;
          Support red2 c= Support p2 by TERMORD:35;
          then s in Lower_Support(p2,T,m) by A58,A9,A94,A103,Th24;
          then A104: s in Support(Low(p2,T,m)) by A9,Th25;
          then s in Support(Low(p2,T,m)) \ Support(Low(p2,T,m'))
             by A62,XBOOLE_0:def 4;
          then s in {HT(Low(p2,T,m),T)} by A10,Th42;
          then A105: s = HT(Low(p2,T,m),T) by TARSKI:def 1;
          then A106: (HM(Low(p2,T,m),T)).(HT(Low(p2,T,m),T))
                 = Low(p2,T,m).s by TERMORD:18
                .= p2.s by A9,A104,Th31;
          HC(Low(p2,T,m),T)
             = Low(p2,T,m).(HT(Low(p2,T,m),T)) by TERMORD:def 7
            .= p2.s by A106,TERMORD:18;
          hence thesis by A105,TERMORD:def 8;
          end;
      A107: Low(p2,T,m) = Monom(p2.s,s) + Red(Low(p2,T,m),T) by A88,TERMORD:38
                     .= Monom(p2.s,s) + Low(p2,T,m') by A10,Th43;
      A108: HT(p1,T) = HT(HM(p1,T),T) by TERMORD:26
                  .= term(HM(p1,T)) by TERMORD:23;
      f.b = -HC(p1,T) * p2.s
        proof
            (HM(p1,T)*'(red2-Low(p2,T,m'))).b
          = (HM(p1,T)*'(red2-Low(p2,T,m'))).(s+HT(p1,T)) by A55,GROEB_2:def 1
         .= (HM(p1,T)).HT(p1,T) * (red2-Low(p2,T,m')).s by A108,POLYRED:7
         .= p1.HT(p1,T) * (red2-Low(p2,T,m')).s by TERMORD:18
         .= HC(p1,T) * (red2-Low(p2,T,m')).s by TERMORD:def 7
         .= HC(p1,T) * (red2 + -Low(p2,T,m')).s by POLYNOM1:def 23
         .= HC(p1,T) * (red2.s + (-Low(p2,T,m')).s) by POLYNOM1:def 21
         .= HC(p1,T) * (p2.s + (-Low(p2,T,m')).s) by A60,A58,A59,TERMORD:40
         .= HC(p1,T) * (p2.s + -(Low(p2,T,m').s)) by POLYNOM1:def 22
         .= HC(p1,T) * (p2.s + 0.L) by A65,RLVECT_1:25
         .= HC(p1,T) * p2.s by RLVECT_1:def 7;
        hence f.b = -HC(p1,T) * p2.s by A79;
        end;
      then (f.b/HC(p1,T)) * (s *' p1)
         = ((HC(p1,T) * (-p2.s))/HC(p1,T)) * (s *' p1) by VECTSP_1:41
        .= ((HC(p1,T) * (-p2.s))*HC(p1,T)") * (s *' p1) by VECTSP_1:def 23
        .= ((-p2.s)*(HC(p1,T)*HC(p1,T)")) * (s *' p1) by GROUP_1:def 4
        .= ((-p2.s)*1_ L) * (s *' p1) by A82,VECTSP_1:def 22
        .= (-p2.s) * (s *' p1) by GROUP_1:def 5;
      then A109: g
         = f + -(-p2.s) * (s *' p1) by POLYNOM1:def 23
        .= f + (-(-p2.s)) * (s *' p1) by POLYRED:9
        .= f + p2.s * (s *' p1) by RLVECT_1:30
        .= f + Monom(p2.s,s) *' p1 by POLYRED:22
        .= f + Monom(p2.s,s) *' (HM(p1,T) + Red(p1,T)) by TERMORD:38
        .= f + (Monom(p2.s,s) *' HM(p1,T) +
               Monom(p2.s,s) *' Red(p1,T)) by POLYNOM1:85
        .= (HM(p2,T)*'Red(p1,T)
             + -HM(p1,T)*'(Red(p2,T)-Low(p2,T,m')) + Red(p1,T)*'Low(p2,T,m'))
            + (Monom(p2.s,s) *' HM(p1,T) +
               Monom(p2.s,s) *' Red(p1,T)) by POLYNOM1:def 23
        .= (((HM(p2,T)*'Red(p1,T)
             + -HM(p1,T)*'(Red(p2,T)-Low(p2,T,m'))) + Red(p1,T)*'Low(p2,T,m'))
            + Monom(p2.s,s) *' HM(p1,T)) +
               Monom(p2.s,s) *' Red(p1,T) by POLYNOM1:80
        .= (((HM(p2,T)*'Red(p1,T)
             + -HM(p1,T)*'(Red(p2,T)-Low(p2,T,m'))) + Monom(p2.s,s)*'HM(p1,T))
            + Red(p1,T)*'Low(p2,T,m')) +
               Monom(p2.s,s) *' Red(p1,T) by POLYNOM1:80
        .= (((HM(p2,T)*'Red(p1,T)
             + HM(p1,T)*'-(Red(p2,T)-Low(p2,T,m')))+Monom(p2.s,s) *' HM(p1,T))
            + Red(p1,T)*'Low(p2,T,m')) +
               Monom(p2.s,s) *' Red(p1,T) by POLYRED:6
        .= ((HM(p2,T)*'Red(p1,T)
             + (HM(p1,T)*'-(Red(p2,T)-Low(p2,T,m')) + Monom(p2.s,s)*'HM(p1,T)))
            + Red(p1,T)*'Low(p2,T,m')) +
               Monom(p2.s,s) *' Red(p1,T) by POLYNOM1:80
        .= ((HM(p2,T)*'Red(p1,T)
             + (HM(p1,T)*'(-(Red(p2,T)-Low(p2,T,m')) + Monom(p2.s,s))))
            + Red(p1,T)*'Low(p2,T,m')) +
               Monom(p2.s,s) *' Red(p1,T) by POLYNOM1:85
        .= (HM(p2,T)*'Red(p1,T)
             + (HM(p1,T)*'(-(Red(p2,T)-Low(p2,T,m')) + Monom(p2.s,s))))
            + (Red(p1,T)*'Low(p2,T,m') +
               Monom(p2.s,s) *' Red(p1,T)) by POLYNOM1:80
        .= (HM(p2,T)*'Red(p1,T)
             + (HM(p1,T)*'(-(Red(p2,T)-Low(p2,T,m')) + Monom(p2.s,s))))
            + Red(p1,T)*'Low(p2,T,m) by A107,POLYNOM1:85
        .= (HM(p2,T)*'Red(p1,T)
             + (HM(p1,T)*'(-(Red(p2,T)+ -Low(p2,T,m')) + Monom(p2.s,s))))
            + Red(p1,T)*'Low(p2,T,m) by POLYNOM1:def 23
        .= (HM(p2,T)*'Red(p1,T)
             + (HM(p1,T)*'((-Red(p2,T)+ --Low(p2,T,m')) + Monom(p2.s,s))))
            + Red(p1,T)*'Low(p2,T,m) by POLYRED:1
        .= (HM(p2,T)*'Red(p1,T)
             + (HM(p1,T)*'(-Red(p2,T) + --Low(p2,T,m)) ))
            + Red(p1,T)*'Low(p2,T,m) by A107,POLYNOM1:80
        .= (HM(p2,T)*'Red(p1,T)
             + (HM(p1,T)*'-(Red(p2,T) + -Low(p2,T,m)) ))
            + Red(p1,T)*'Low(p2,T,m) by POLYRED:1
        .= (HM(p2,T)*'Red(p1,T)
             + (HM(p1,T)*'-(Red(p2,T) -Low(p2,T,m)) ))
            + Red(p1,T)*'Low(p2,T,m) by POLYNOM1:def 23
        .= (HM(p2,T)*'Red(p1,T)
             + -(HM(p1,T)*'(Red(p2,T) -Low(p2,T,m)) ))
            + Red(p1,T)*'Low(p2,T,m) by POLYRED:6
        .= HM(p2,T)*'Red(p1,T)
           - HM(p1,T)*'(Red(p2,T)-Low(p2,T,m)) + Red(p1,T)*'Low(p2,T,m)
           by POLYNOM1:def 23;
      PolyRedRel({p1},T) reduces
      HM(p2,T)*'Red(p1,T) - HM(p1,T)*'Red(p2,T),f
        by A8,A14;
      hence PolyRedRel({p1},T) reduces
      HM(p2,T)*'Red(p1,T)-HM(p1,T)*'Red(p2,T),HM(p2,T)*'Red(p1,T)
           - HM(p1,T)*'(Red(p2,T)-Low(p2,T,m)) + Red(p1,T)*'Low(p2,T,m)
           by A81,REWRITE1:17,A109;
      end;
      hence P[k+1];
      end;
    hence thesis;
    end;
A110: for i being Element of NAT st 0 <= i & i <= j' holds P[i]
from POLYNOM2:sch 4(A5,A6)
;
A111: card(Support Low(p2,T,1)) = j' by A3,Th36;
A112: HM(p2,T)*'Red(p1,T)
          - HM(p1,T)*'(Red(p2,T)-Low(p2,T,1)) + Red(p1,T)*'Low(p2,T,1)
  = HM(p2,T)*'Red(p1,T)
         - HM(p1,T)*'(Red(p2,T)-Red(p2,T)) + Red(p1,T)*'Low(p2,T,1) by Th36
 .= HM(p2,T)*'Red(p1,T)
         - HM(p1,T)*'0_(n,L) + Red(p1,T)*'Low(p2,T,1) by POLYNOM1:83
 .= HM(p2,T)*'Red(p1,T)
         - 0_(n,L) + Red(p1,T)*'Low(p2,T,1) by POLYRED:5
 .= HM(p2,T)*'Red(p1,T) + Red(p1,T)*'Low(p2,T,1) by POLYRED:4
 .= HM(p2,T)*'Red(p1,T) + Red(p1,T)*'Red(p2,T) by Th36
 .= (HM(p2,T) + Red(p2,T)) *' Red(p1,T) by POLYNOM1:85
 .= p2 *' Red(p1,T) by TERMORD:38;
thus thesis by A110,A112,A111,A4;
end;

theorem Th56:
::: theorem 5.66, p. 222 (Buchberger's first criterium)
for n being Ordinal,
    T being connected admissible TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative left_unital right_unital distributive
            Abelian Field-like (non trivial doubleLoopStr),
    p1,p2 being Polynomial of n,L
st HT(p1,T),HT(p2,T) are_disjoint
holds PolyRedRel({p1,p2},T) reduces S-Poly(p1,p2,T),0_(n,L)
proof
let n be Ordinal, T be connected admissible TermOrder of n,
    L be add-associative right_complementable right_zeroed
         commutative associative left_unital right_unital distributive
         Abelian Field-like (non trivial doubleLoopStr),
    p1,p2 be Polynomial of n,L;
assume A1: HT(p1,T),HT(p2,T) are_disjoint;
then A2: S-Poly(p1,p2,T) = Red(p1,T) *' p2 - Red(p2,T) *' p1 by Th54;
now per cases;
case A3: p1 = 0_(n,L);
  then A4: Red(p2,T) *' p1 = 0_(n,L) by POLYNOM1:87;
  Red(p1,T) = 0_(n,L) by A3,Th11;
  then S-Poly(p1,p2,T) = 0_(n,L) - 0_(n,L) by POLYNOM1:87,A4,A2
                      .= 0_(n,L) by POLYRED:4;
  hence thesis by REWRITE1:13;
  end;
case p1 <> 0_(n,L);
  then reconsider p1a = p1 as non-zero Polynomial of n,L by POLYNOM7:def 2;
  now let u be set; assume u in {p1};
    then u = p1 by TARSKI:def 1;
    hence u in {p1,p2} by TARSKI:def 2;
    end;
  then {p1} c= {p1,p2} by TARSKI:def 3;
  then A5: PolyRedRel({p1},T) c= PolyRedRel({p1,p2},T) by GROEB_1:4;
  now let u be set; assume u in {p2};
    then u = p2 by TARSKI:def 1;
    hence u in {p1,p2} by TARSKI:def 2;
    end;
  then {p2} c= {p1,p2} by TARSKI:def 3;
  then A6: PolyRedRel({p2},T) c= PolyRedRel({p1,p2},T) by GROEB_1:4;
  now per cases;
  case A7: p2 = 0_(n,L);
    then A8: Red(p1,T) *' p2 = 0_(n,L) by POLYNOM1:87;
    Red(p2,T) = 0_(n,L) by A7,Th11;
    then S-Poly(p1,p2,T) = 0_(n,L) - 0_(n,L) by A8,A2,POLYNOM1:87
                        .= 0_(n,L) by POLYRED:4;
    hence thesis by REWRITE1:13;
    end;
  case p2 <> 0_(n,L);
    then reconsider p2a = p2 as non-zero Polynomial of n,L
      by POLYNOM7:def 2;
    now per cases;
    case Red(p1,T) = 0_(n,L);
      then A9: S-Poly(p1,p2,T) = 0_(n,L) - Red(p2,T) *' p1
               by A2,POLYNOM1:87;
      now per cases;
      case Red(p2,T) = 0_(n,L);
        then S-Poly(p1,p2,T) = 0_(n,L) - 0_(n,L) by A9,POLYNOM1:87
                            .= 0_(n,L) by POLYRED:4;
        hence thesis by REWRITE1:13;
        end;
      case Red(p2,T) <> 0_(n,L);
        then reconsider rp2 = Red(p2,T) as non-zero Polynomial of n,L
           by POLYNOM7:def 2;
        PolyRedRel({p1a},T) reduces rp2*'p1a,0_(n,L) by Th51;
        then PolyRedRel({p1a},T) reduces -(rp2*'p1a),-0_(n,L) by Th49;
        then PolyRedRel({p1a},T) reduces -(rp2*'p1a),0_(n,L) by Th13;
        then PolyRedRel({p1},T) reduces S-Poly(p1,p2,T),0_(n,L) by A9,Th14;
        hence thesis by A5,REWRITE1:23;
        end;
      end;
      hence thesis;
      end;
    case Red(p1,T) <> 0_(n,L);
      then reconsider rp1 = Red(p1,T) as non-zero Polynomial of n,L
         by POLYNOM7:def 2;
      now per cases;
      case Red(p2,T) = 0_(n,L);
        then Red(p2,T) *' p1 = 0_(n,L) by POLYNOM1:87;
        then A10: S-Poly(p1,p2,T) = Red(p1,T) *' p2 - 0_(n,L) by A1,Th54
                                .= Red(p1,T) *' p2 by POLYRED:4;
        PolyRedRel({p2a},T) reduces rp1*'p2a,0_(n,L) by Th51;
        hence thesis by A10,A6,REWRITE1:23;
        end;
      case Red(p2,T) <> 0_(n,L);
        then reconsider rp2 = Red(p2,T) as non-zero Polynomial of n,L
           by POLYNOM7:def 2;
         S-Poly(p1,p2,T) = HM(p2a,T)*'rp1 - HM(p1a,T)*'rp2
            by A1,Th53;
         then PolyRedRel({p1},T) reduces
                    S-Poly(p1,p2,T),p2 *' Red(p1,T) by A1,Th55;
         then A11: PolyRedRel({p1,p2},T) reduces
                    S-Poly(p1,p2,T),p2 *' Red(p1,T) by A5,REWRITE1:23;
         PolyRedRel({p2},T) reduces rp1*'p2a,0_(n,L) by Th51;
         then PolyRedRel({p1,p2},T) reduces rp1*'p2a,0_(n,L)
                by A6,REWRITE1:23;
         hence thesis by A11,REWRITE1:17;
         end;
      end;
      hence thesis;
      end;
    end;
    hence thesis;
    end;
  end;
  hence thesis;
  end;
end;
hence thesis;
end;

theorem
::: theorem 5.68 (i) ==> (ii), p. 223
for n being Element of NAT,
    T being connected admissible TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative left_unital right_unital distributive
            Abelian Field-like non degenerated (non empty doubleLoopStr),
    G being Subset of Polynom-Ring(n,L)
holds G is_Groebner_basis_wrt T
implies (for g1,g2 being Polynomial of n,L
               st g1 in G & g2 in G & not(HT(g1,T),HT(g2,T) are_disjoint)
         holds PolyRedRel(G,T) reduces S-Poly(g1,g2,T),0_(n,L))
proof
let n be Element of NAT, T be connected admissible TermOrder of n,
    L be add-associative right_complementable right_zeroed
         commutative associative left_unital right_unital distributive Abelian
         Field-like non degenerated (non empty doubleLoopStr),
    G be Subset of Polynom-Ring(n,L);
assume G is_Groebner_basis_wrt T;
then for g1,g2,h being Polynomial of n,L
           st g1 in G & g2 in G &
              h is_a_normal_form_of S-Poly(g1,g2,T),PolyRedRel(G,T)
       holds h = 0_(n,L) by GROEB_2:28;
hence thesis by GROEB_2:29;
end;

theorem
::: theorem 5.68 (ii) ==> (iii), p. 223
for n being Element of NAT,
    T being connected admissible TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative left_unital right_unital distributive
            Abelian Field-like non degenerated (non trivial doubleLoopStr),
    G being Subset of Polynom-Ring(n,L) st not(0_(n,L) in G)
holds (for g1,g2 being Polynomial of n,L
             st g1 in G & g2 in G & not(HT(g1,T),HT(g2,T) are_disjoint)
       holds PolyRedRel(G,T) reduces S-Poly(g1,g2,T),0_(n,L))
implies (for g1,g2,h being Polynomial of n,L
               st g1 in G & g2 in G & not(HT(g1,T),HT(g2,T) are_disjoint) &
                  h is_a_normal_form_of S-Poly(g1,g2,T),PolyRedRel(G,T)
         holds h = 0_(n,L))
proof
let n be Element of NAT, T be connected admissible TermOrder of n,
    L be add-associative right_complementable right_zeroed
         commutative associative left_unital right_unital distributive Abelian
         Field-like non degenerated (non trivial doubleLoopStr),
    G be Subset of Polynom-Ring(n,L);
assume A1: not(0_(n,L) in G);
assume A2: for g1,g2 being Polynomial of n,L
             st g1 in G & g2 in G & not(HT(g1,T),HT(g2,T) are_disjoint)
          holds PolyRedRel(G,T) reduces S-Poly(g1,g2,T),0_(n,L);
for g1,g2 being Polynomial of n,L st g1 in G & g2 in G
    holds PolyRedRel(G,T) reduces S-Poly(g1,g2,T),0_(n,L)
  proof
  let g1,g2 being Polynomial of n,L;
  assume A3: g1 in G & g2 in G;
  now per cases;
  case HT(g1,T),HT(g2,T) are_disjoint;
    then A4: PolyRedRel({g1,g2},T) reduces S-Poly(g1,g2,T),0_(n,L) by Th56;
    now let u be set; assume A5: u in {g1,g2};
      now per cases by A5,TARSKI:def 2;
      case u = g1; hence u in G by A3; end;
      case u = g2; hence u in G by A3; end;
      end;
      hence u in G;
      end;
    then {g1,g2} c= G by TARSKI:def 3;
    then PolyRedRel({g1,g2},T) c= PolyRedRel(G,T) by GROEB_1:4;
    hence PolyRedRel(G,T) reduces S-Poly(g1,g2,T),0_(n,L) by A4,REWRITE1:23;
    end;
  case not(HT(g1,T),HT(g2,T) are_disjoint);
    hence PolyRedRel(G,T) reduces S-Poly(g1,g2,T),0_(n,L) by A3,A2;
    end;
  end;
  hence thesis;
  end;
then G is_Groebner_basis_wrt T by A1,GROEB_2:30;
hence thesis by GROEB_2:28;
end;

theorem
::: theorem 5.68 (iii) ==> (i), p. 223
for n being Element of NAT,
    T being connected admissible TermOrder of n,
    L being add-associative right_complementable right_zeroed
            commutative associative left_unital right_unital distributive
            Abelian Field-like non degenerated (non empty doubleLoopStr),
    G being Subset of Polynom-Ring(n,L) st not(0_(n,L) in G)
holds (for g1,g2,h being Polynomial of n,L
             st g1 in G & g2 in G & not(HT(g1,T),HT(g2,T) are_disjoint) &
                h is_a_normal_form_of S-Poly(g1,g2,T),PolyRedRel(G,T)
       holds h = 0_(n,L))
implies G is_Groebner_basis_wrt T
proof
let n be Element of NAT, T be connected admissible TermOrder of n,
    L be add-associative right_complementable right_zeroed
         commutative associative left_unital right_unital distributive
         Abelian Field-like non degenerated (non empty doubleLoopStr),
    G be Subset of Polynom-Ring(n,L);
assume A1: not(0_(n,L) in G);
assume A2: for g1,g2,h being Polynomial of n,L
          st g1 in G & g2 in G & not(HT(g1,T),HT(g2,T) are_disjoint) &
             h is_a_normal_form_of S-Poly(g1,g2,T),PolyRedRel(G,T)
          holds h = 0_(n,L);
for g1,g2 being Polynomial of n,L st g1 in G & g2 in G
   holds PolyRedRel(G,T) reduces S-Poly(g1,g2,T),0_(n,L)
  proof
  let g1,g2 being Polynomial of n,L;
  assume A3: g1 in G & g2 in G;
  now per cases;
  case HT(g1,T),HT(g2,T) are_disjoint;
    then A4: PolyRedRel({g1,g2},T) reduces S-Poly(g1,g2,T),0_(n,L) by Th56;
    now let u be set; assume A5: u in {g1,g2};
      now per cases by A5,TARSKI:def 2;
      case u = g1; hence u in G by A3; end;
      case u = g2; hence u in G by A3; end;
      end;
      hence u in G;
      end;
    then {g1,g2} c= G by TARSKI:def 3;
    then PolyRedRel({g1,g2},T) c= PolyRedRel(G,T) by GROEB_1:4;
    hence PolyRedRel(G,T) reduces S-Poly(g1,g2,T),0_(n,L) by A4,REWRITE1:23;
    end;
  case A6: not(HT(g1,T),HT(g2,T) are_disjoint);
    S-Poly(g1,g2,T) has_a_normal_form_wrt PolyRedRel(G,T)
      proof
      now per cases;
      case not(S-Poly(g1,g2,T) in field PolyRedRel(G,T));
        hence thesis by REWRITE1:47;
        end;
      case S-Poly(g1,g2,T) in field PolyRedRel(G,T);
        hence thesis by REWRITE1:def 14;
        end;
      end;
      hence thesis;
      end;
    then consider h being set such that
    A7: h is_a_normal_form_of S-Poly(g1,g2,T),PolyRedRel(G,T)
       by REWRITE1:def 11;
    PolyRedRel(G,T) reduces S-Poly(g1,g2,T),h by A7,REWRITE1:def 6;
    then reconsider h as Polynomial of n,L by Lm1;
    h = 0_(n,L) by A7,A6,A3,A2;
    hence PolyRedRel(G,T) reduces S-Poly(g1,g2,T),0_(n,L) by A7,REWRITE1:def 6;
    end;
  end;
  hence thesis;
  end;
hence thesis by A1,GROEB_2:30;
end;

