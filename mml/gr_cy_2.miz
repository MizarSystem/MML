:: Isomorphisms of Cyclic Groups. Some Properties of Cyclic Groups
::  by Dariusz Surowik
::
:: Received June 5, 1992
:: Copyright (c) 1992 Association of Mizar Users

environ

 vocabularies REALSET1, GROUP_2, GRAPH_1, GROUP_6, INT_1, GR_CY_1, NAT_1,
      ARYTM_1, ARYTM_3, GROUP_1, FINSET_1, GROUP_4, RELAT_1, FINSEQ_1, FUNCT_1,
      QC_LANG1, WELLORD1, FILTER_0, GROUP_5, GROUP_3, ARYTM, ALGSTR_0,
      VECTSP_1, SGRAPH1;
 notations TARSKI, SUBSET_1, XCMPLX_0, RELAT_1, FUNCT_1, FUNCT_2, BINOP_1,
      NUMBERS, INT_1, INT_2, NAT_1, CARD_1, NAT_D, GROUP_2, DOMAIN_1, STRUCT_0,
      ALGSTR_0, GROUP_1, GROUP_3, GROUP_4, GROUP_5, GROUP_6, GR_CY_1, FINSEQ_1,
      XXREAL_0;
 constructors BINOP_1, DOMAIN_1, XXREAL_0, REAL_1, NAT_1, NAT_D, BINOP_2,
      GROUP_4, GROUP_5, GROUP_6, GR_CY_1;
 registrations XBOOLE_0, RELSET_1, XXREAL_0, XREAL_0, NAT_1, INT_1, FINSEQ_1,
      STRUCT_0, GROUP_1, GROUP_2, GR_CY_1, ORDINAL1, FINSET_1;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions FUNCT_1, CARD_1, TARSKI, ALGSTR_0;
 theorems FUNCT_2, GROUP_1, GROUP_2, GROUP_4, GROUP_5, FUNCT_1, GROUP_6,
      GR_CY_1, NAT_1, INT_1, INT_2, REAL_1, FINSEQ_1, TARSKI, RELSET_1,
      XBOOLE_0, XCMPLX_1, NEWTON, XREAL_1, ORDINAL1, NAT_D, STRUCT_0;
 schemes FUNCT_2, FINSEQ_1;

begin

reserve F,G for Group;
reserve G1 for Subgroup of G;
reserve Gc for cyclic Group;
reserve H for Subgroup of Gc;
reserve f for Homomorphism of G,Gc;
reserve a,b for Element of G;
reserve g for Element of Gc;
reserve a1 for Element of G1;
reserve k,m,n,p,s for Element of NAT;
reserve i0,i,i1,i2 for Integer;
reserve j,j1 for Element of INT.Group;
reserve x,y,t for set;

canceled 5;

theorem
  ord a>1 & a=b|^k implies k<>0
proof
  assume that
A1: ord a>1 and
A2: a=b|^k and
A3: k=0;
  a=1_G by A2,A3,GROUP_1:43;
  hence contradiction by A1,GROUP_1:84;
end;

:::::::::::::::::::: Some properties of Cyclic Groups ::::::::::::::::::::::

canceled;

theorem Th8:
  a in gr {a}
proof
  ex i st a=a|^i
  proof
    reconsider i=1 as Integer;
    take i;
    thus thesis by GROUP_1:44;
  end;
  hence thesis by GR_CY_1:25;
end;

theorem Th9:
  a=a1 implies gr {a} = gr {a1}
proof
  assume
A1: a= a1;
  reconsider Gr = gr {a1} as Subgroup of G by GROUP_2:65;
  for b holds b in gr {a} iff b in Gr
  proof
A2: for b holds b in gr {a} implies b in Gr
    proof
      let b;
      assume b in gr {a};
      then consider i such that
A3:   b= a|^i by GR_CY_1:25;
      b=a1|^i by A1,A3,GROUP_4:4;
      hence b in Gr by GR_CY_1:25;
    end;
    for b holds b in Gr implies b in gr {a}
    proof
      let b;
      assume
A4:   b in Gr;
      then b in G1 by GROUP_2:49;
      then reconsider b1=b as Element of G1 by STRUCT_0:def 5;
      consider i such that
A5:   b1= a1|^i by A4,GR_CY_1:25;
      b=a|^i by A1,A5,GROUP_4:4;
      hence b in gr {a} by GR_CY_1:25;
    end;
    hence thesis by A2;
  end;
  hence thesis by GROUP_2:69;
end;

theorem Th10:
  gr {a} is cyclic Group
proof
  ex a1 being Element of gr {a} st gr {a}=gr {a1}
  proof
    a in gr {a} by Th8;
    then reconsider a1=a as Element of gr {a} by STRUCT_0:def 5;
    take a1;
    thus thesis by Th9;
  end;
  hence thesis by GR_CY_1:def 9;
end;

theorem Th11:
  for G being strict Group,b being Element of G holds
  ( for a being Element of G holds ex i st a=b|^i ) iff G = gr {b}
proof
  let G be strict Group;
  let b be Element of G;
  thus ( for a being Element of G holds ex i st a=b|^i ) implies G = gr {b}
  proof
    assume
A1: for a being Element of G holds ex i st a=b|^i;
    for a being Element of G holds a in gr {b}
    proof
      let a be Element of G;
      ex i st a=b|^i by A1;
      hence thesis by GR_CY_1:25;
    end;
    hence thesis by GROUP_2:71;
  end;
  assume
A2: G= gr {b};
  let a be Element of G;
  a in gr {b} by A2,STRUCT_0:def 5;
  hence thesis by GR_CY_1:25;
end;

theorem Th12:
  for G being strict finite Group,b being Element of G holds
  (for a being Element of G holds ex p st a=b|^p) iff G = gr {b}
proof
  let G be strict finite Group,b be Element of G;
  thus (for a being Element of G holds ex p st a=b|^p) implies G = gr {b}
  proof
    assume
A1: for a being Element of G holds ex p st a=b|^p;
    for a being Element of G holds ex i st a=b|^i
    proof
      let a be Element of G;
      consider p such that
A2:   a=b|^p by A1;
      reconsider p1=p as Integer;
      take p1; thus thesis by A2;
    end;
    hence G=gr {b} by Th11;
  end;
  assume
A3: G= gr {b};
  let a be Element of G;
  consider i such that
A4: a= b|^i by A3,Th11;
  reconsider n1=ord G as Integer;
A5: n1 > 0 by GROUP_1:90;
  reconsider p=i mod n1 as Element of NAT by INT_1:16,NEWTON:78;
A6: a = b|^((i div n1) * n1 + (i mod n1)) by A4,A5,NEWTON:80
    .= b|^((i div n1)*n1) *(b|^(i mod n1)) by GROUP_1:63
    .= b|^(i div n1)|^ord G *(b|^(i mod n1)) by GROUP_1:67
    .= (1_G) *(b|^(i mod n1)) by GR_CY_1:29
    .= b|^(i mod n1) by GROUP_1:def 5;
  take p;
  thus thesis by A6;
end;

theorem Th13:
  for G being strict Group,a being Element of G holds
  G is finite & G = gr {a} implies for G1 being strict Subgroup of G holds
  ex p st G1 = gr {a|^p}
proof
  let G be strict Group,a be Element of G;
  assume that
A1: G is finite and
A2: G = gr {a};
A3: G is cyclic Group by A2,GR_CY_1:def 9;
  let G1 be strict Subgroup of G;
  G1 is cyclic Group by A1,A3,GR_CY_1:44;
  then consider b being Element of G1 such that
A4: G1 = gr {b} by GR_CY_1:def 9;
  reconsider b1 = b as Element of G by GROUP_2:51;
  consider p such that
A5: b1 = a|^p by A1,A2,Th12;
  take p;
  thus thesis by A4,A5,Th9;
end;

theorem Th14:
  for G being finite Group, a being Element of G holds
  G=gr {a} & ord G = n & n = p * s implies ord (a|^p) = s
proof
  let G be finite Group, a be Element of G;
  assume that
A1: G=gr {a} and
A2: ord G = n and
A3: n = p * s;
A4: a|^p is_not_of_order_0 by GR_CY_1:26;
A5: a|^p|^ s = 1_G
  proof
    thus a|^p|^s = a|^n by A3,GROUP_1:50
      .=1_G by A2,GR_CY_1:29;
  end;
A6: s <> 0 by A2,A3,GROUP_1:90;
A7: p <> 0 by A2,A3,GROUP_1:90;
  for k being Nat st a|^p|^ k = 1_G & k <> 0 holds s <= k
  proof
    let k be Nat;
    assume that
A8: a|^p|^k=1_G and
A9: k <> 0 and
A10: s > k;
A11: p*s > p*k by A7,A10,XREAL_1:70;
A12: ord a = n by A1,A2,GR_CY_1:27;
A13: p*k <> 0 by A7,A9,XCMPLX_1:6;
A14: a is_not_of_order_0 by GR_CY_1:26;
    a|^(p*k) = 1_G by A8,GROUP_1:50;
    hence contradiction by A3,A11,A12,A13,A14,GROUP_1:def 12;
  end;
  hence thesis by A4,A5,A6,GROUP_1:def 12;
end;

theorem Th15:
  s divides k implies a|^k in gr {a|^s}
proof
  assume s divides k;
  then consider p be Nat such that
A1: k=s*p by NAT_D:def 3;
  ex i st a|^k=a|^s|^i
  proof
    reconsider p'=p as Integer;
    take p';
    thus thesis by A1,GROUP_1:50;
  end;
  hence thesis by GR_CY_1:25;
end;

theorem Th16:
  for G being finite Group, a being Element of G holds
  ord gr {a|^s} = ord gr {a|^k} & a|^k in gr {a|^s} implies
  gr {a|^s} = gr {a|^k}
proof
  let G be finite Group, a be Element of G;
  assume
A1: ord gr {a|^s} = ord gr {a|^k};
  assume a|^k in gr {a|^s};
  then reconsider h=a|^k as Element of gr {a|^s} by STRUCT_0:def 5;
  ord gr {h} = ord gr {a|^s} by A1,Th9;
  hence gr {a|^s} = gr {h} by GROUP_2:85
    .= gr {a|^k} by Th9;
end;

theorem Th17:
  for G being finite Group, G1 being Subgroup of G, a being Element of G holds
  ord G = n & G=gr {a} & ord G1 = p & G1= gr{a|^k} implies n divides k*p
proof
  let G be finite Group, G1 be Subgroup of G, a be Element of G;
  assume that
A1: ord G = n and
A2: G=gr {a} and
A3: ord G1 = p and
A4: G1= gr{a|^k};
  set z=k*p;
  n>0 by A1,GROUP_1:90;
  then consider m,l being Nat such that
A5: z=(n*m)+l and
A6: l < n by NAT_1:17;
  l=0
  proof
    assume
A7: l<>0;
    a|^k in gr {a|^k} by Th8;
    then reconsider h=a|^k as Element of G1 by A4,STRUCT_0:def 5;
    a|^z = a|^k|^p by GROUP_1:50
      .= h|^p by GROUP_4:3
      .= 1_G1 by A3,GR_CY_1:29
      .= 1_G by GROUP_2:53;
    then
A8: 1_G = (a|^(n*m))*(a|^l) by A5,GROUP_1:48
      .= (a|^n|^m)*(a|^l) by GROUP_1:50
      .= ((1_G)|^m)*(a|^l) by A1,GR_CY_1:29
      .= (1_G)*(a|^l) by GROUP_1:42
      .= a|^l by GROUP_1:def 5;
A9: a is_not_of_order_0 by GR_CY_1:26;
    ord a = n by A1,A2,GR_CY_1:27;
    hence contradiction by A6,A7,A8,A9,GROUP_1:def 12;
  end;
  hence thesis by A5,NAT_D:def 3;
end;

theorem
  for G being strict finite Group, a be Element of G holds
  G = gr {a} & ord G = n implies ( G = gr {a|^k} iff k hcf n = 1 )
proof
  let G be strict finite Group, a be Element of G;
  assume that
A1: G = gr {a} and
A2: ord G = n;
A3: n > 0 by A2,GROUP_1:90;
A4: G=gr {a|^k} implies k hcf n = 1
  proof
    assume that
A5: G = gr {a|^k} and
A6: k hcf n <> 1;
    set d=k hcf n;
A7: d divides k by NAT_D:def 5;
A8: d divides n by NAT_D:def 5;
    consider l be Nat such that
A9: k=d*l by A7,NAT_D:def 3;
    consider p be Nat such that
A10: n=d*p by A8,NAT_D:def 3;
A11: p divides n by A10,NAT_D:def 3;
A12: (a|^k)|^p = a|^((l*d)*p) by A9,GROUP_1:50
      .=a|^(n*l) by A10
      .=a|^n|^l by GROUP_1:50
      .=(1_G)|^l by A2,GR_CY_1:29
      .= 1_G by GROUP_1:42;
A13: p <> 0 by A2,A10,GROUP_1:90;
A14: p<n
    proof
A15:  p <= n by A3,A11,NAT_D:7;
      p <> n
      proof
        assume p=n;
        then d*p = p*1 by A10;
        hence contradiction by A6,A13,XCMPLX_1:5;
      end;
      hence thesis by A15,REAL_1:def 5;
    end;
    a|^k is_not_of_order_0 by GR_CY_1:26;
    then ord (a|^k) <= p by A12,A13,GROUP_1:def 12;
    hence contradiction by A2,A5,A14,GR_CY_1:27;
  end;
  k hcf n = 1 implies G=gr {a|^k}
  proof
    assume k hcf n = 1;
    then consider i,i1 such that
A16: i*k+ i1*n=1 by A3,NEWTON:81;
    for b be Element of G holds b in gr {a|^k}
    proof
      let b be Element of G;
      b in G by STRUCT_0:def 5;
      then consider i0 such that
A17:  b=a|^i0 by A1,GR_CY_1:25;
A18:  i0=(k*i+n*i1)*i0 by A16
        .= k*i*i0+n*i1*i0;
      ex i2 st b=(a|^k)|^i2
      proof
A19:    b= (a|^(k*i*i0)) *a|^(n*(i1*i0)) by A17,A18,GROUP_1:63
          .= (a|^(k*i*i0))*a|^n|^(i1*i0) by GROUP_1:68
          .= (a|^(k*i*i0))*(1_G)|^(i1*i0) by A2,GR_CY_1:29
          .= (a|^(k*i*i0))*1_G by GROUP_1:61
          .= a|^(k*(i*i0)) by GROUP_1:def 5
          .= a|^k|^(i*i0) by GROUP_1:68;
        take i*i0;
        thus thesis by A19;
      end;
      hence thesis by GR_CY_1:25;
    end;
    hence thesis by GROUP_2:71;
  end;
  hence thesis by A4;
end;

theorem Th19:
  Gc = gr {g} & g in H implies the multMagma of Gc = the multMagma of H
proof
  assume that
A1: Gc=gr{g} and
A2: g in H;
  reconsider g'=g as Element of H by A2,STRUCT_0:def 5;
  gr {g'} is Subgroup of H;
  then gr {g} is Subgroup of H by Th9;
  hence thesis by A1,GROUP_2:64;
end;

theorem Th20:
  Gc = gr {g} implies ( Gc is finite iff ex i,i1 st i<>i1 & g|^i = g|^i1 )
proof
  assume
A1: Gc = gr {g};
A2: Gc is finite implies ex i,i1 st i<>i1 & g|^i = g|^i1
  proof
    assume Gc is finite;
    then reconsider Gc as finite cyclic Group;
    reconsider zero=0,i0=ord Gc as Integer;
A3: zero <> i0 by GROUP_1:90;
    g|^zero = 1_Gc by GROUP_1:43
      .= g|^i0 by GR_CY_1:29;
    hence thesis by A3;
  end;
  ( ex i,i1 st i<>i1 & g|^i = g|^i1) implies Gc is finite
  proof
    given i,i1 such that
A4: i<>i1 & g|^i = g|^i1;
    now per cases by A4,REAL_1:def 5;
      suppose i < i1;
        then i1 > i+0;
        then
A5:     i1-i > 0 by XREAL_1:22;
        set r = i1-i;
A6:     g|^r = 1_Gc
        proof
          (g|^i1)*g|^(-i) = (g|^i) *(g|^(i))" by A4,GROUP_1:70;
          then (g|^i1)*g|^(-i) = 1_Gc by GROUP_1:def 6;
          then g|^(i1+-i) = 1_Gc by GROUP_1:63;
          hence thesis;
        end;
A7:     for i2 ex n st g|^i2=g|^n & n >= 0 & n < i1-i
        proof
          let i2;
A8:       g|^i2 = g|^( (i2 div r) * r + (i2 mod r) ) by A5,NEWTON:80
            .=g|^(r* (i2 div r) ) *(g|^ (i2 mod r)) by GROUP_1:63
            .=(1_Gc)|^(i2 div r) *(g|^ (i2 mod r)) by A6,GROUP_1:67
            .=(1_Gc) *(g|^ (i2 mod r)) by GROUP_1:61
            .=g|^ (i2 mod r) by GROUP_1:def 5;
          reconsider m=i2 mod r as Element of NAT by A5,INT_1:16,NEWTON:78;
          take m;
          thus thesis by A5,A8,NEWTON:79;
        end;
        reconsider m=r as Element of NAT by A5,INT_1:16;
        ex F being FinSequence st rng F= the carrier of Gc
        proof
          deffunc F(Nat) = g|^$1;
          consider F being FinSequence such that
A9:       len F = m and
A10:      for p be Nat holds p in dom F implies F.p = F(p) from FINSEQ_1:sch 2;
A11:      dom F = Seg m by A9,FINSEQ_1:def 3;
A12:      for y st y in rng F holds ex n st y=g|^n
          proof
            let y;
            assume y in rng F;
            then consider x such that
A13:        x in dom F and
A14:        F.x=y by FUNCT_1:def 5;
            reconsider n=x as Element of NAT by A13;
            take n;
            thus thesis by A10,A13,A14;
          end;
A15:      rng F c= the carrier of Gc
          proof
            let y;
            assume y in rng F;
            then consider n such that
A16:        y= g|^n by A12;
            thus thesis by A16;
          end;
A17:      the carrier of Gc c= rng F
          proof
            let y;
            assume y in the carrier of Gc;
            then reconsider a=y as Element of Gc;
            a in Gc by STRUCT_0:def 5;
            then
A18:        ex i2 st a=g|^i2 by A1,GR_CY_1:25;
            ex n st n in dom F & F.n=a
            proof
              consider n such that
A19:          a=g|^n and n >= 0 and
A20:          n < i1-i by A7,A18;
              per cases;
              suppose n=0;
                then
A21:            a = g|^m by A6,A19,GROUP_1:43;
A22:            m >= 1
                proof m >= 0 +1 by A5,NAT_1:13;
                  hence m >= 1;
                end;
                then
A23:            m in Seg m by FINSEQ_1:3;
A24:            m in dom F by A11,A22,FINSEQ_1:3;
                F.m = a by A10,A21,A23,A11;
                hence thesis by A24;
              end;
              suppose
A25:            n >0;
A26:            n >= 1
                proof n >= 0 +1 by A25,NAT_1:13;
                  hence n >= 1;
                end;
                then
A27:            n in Seg m by A20,FINSEQ_1:3;
A28:            n in dom F by A11,A20,A26,FINSEQ_1:3;
                F.n = a by A10,A19,A27,A11;
                hence thesis by A28;
              end;
            end;
            hence thesis by FUNCT_1:def 5;
          end;
          take F;
          thus thesis by A15,A17,XBOOLE_0:def 10;
        end;
        hence thesis;
      end;
      suppose i1 < i;
        then i > i1+0;
        then
A29:    i-i1 > 0 by XREAL_1:22;
        set r = i-i1;
A30:    g|^r = 1_Gc
        proof
          (g|^i)*g|^(-i1) = (g|^i1) *(g|^(i1))" by A4,GROUP_1:70;
          then (g|^i)*g|^(-i1) = 1_Gc by GROUP_1:def 6;
          then g|^(i+-i1) = 1_Gc by GROUP_1:63;
          hence thesis;
        end;
A31:    for i2 ex n st g|^i2=g|^n & n >= 0 & n < i-i1
        proof
          let i2;
A32:      g|^i2 = g|^( (i2 div r) * r + (i2 mod r) ) by A29,NEWTON:80
            .=g|^(r* (i2 div r) ) *(g|^ (i2 mod r)) by GROUP_1:63
            .=(1_Gc)|^(i2 div r) *(g|^ (i2 mod r)) by A30,GROUP_1:67
            .=(1_Gc) *(g|^ (i2 mod r)) by GROUP_1:61
            .=g|^ (i2 mod r) by GROUP_1:def 5;
          reconsider m=i2 mod r as Element of NAT by A29,INT_1:16,NEWTON:78;
          take m;
          thus thesis by A29,A32,NEWTON:79;
        end;
        reconsider m=r as Element of NAT by A29,INT_1:16;
        ex F being FinSequence st rng F= the carrier of Gc
        proof
          deffunc F(Nat) = g|^$1;
          consider F being FinSequence such that
A33:      len F = m and
A34:      for p be Nat holds p in dom F implies F.p=F(p) from FINSEQ_1:sch 2;
A35:      dom F = Seg m by A33,FINSEQ_1:def 3;
A36:      for y st y in rng F holds ex n st y=g|^n
          proof
            let y;
            assume y in rng F;
            then consider x such that
A37:        x in dom F and
A38:        F.x=y by FUNCT_1:def 5;
            reconsider n=x as Element of NAT by A37;
            take n;
            thus thesis by A34,A37,A38;
          end;
A39:      rng F c= the carrier of Gc
          proof
            let y;
            assume y in rng F;
            then consider n such that
A40:        y= g|^n by A36;
            thus thesis by A40;
          end;
A41:      the carrier of Gc c= rng F
          proof
            let y;
            assume y in the carrier of Gc;
            then reconsider a=y as Element of Gc;
            a in Gc by STRUCT_0:def 5;
            then
A42:        ex i2 st a=g|^i2 by A1,GR_CY_1:25;
            ex n st n in dom F & F.n=a
            proof
              consider n such that
A43:          a=g|^n and n >= 0 and
A44:          n < i-i1 by A31,A42;
              per cases;
              suppose n=0;
                then
A45:            a = g|^m by A30,A43,GROUP_1:43;
A46:            m >= 1
                proof m >= 0 +1 by A29,NAT_1:13;
                  hence m >= 1;
                end;
                then
A47:            m in Seg m by FINSEQ_1:3;
A48:            m in dom F by A35,A46,FINSEQ_1:3;
                F.m = a by A34,A45,A47,A35;
                hence thesis by A48;
              end;
              suppose
A49:            n >0;
A50:            n >= 1
                proof n >= 0 +1 by A49,NAT_1:13;
                  hence n >= 1;
                end;
                then
A51:            n in Seg m by A44,FINSEQ_1:3;
A52:            n in dom F by A35,A44,A50,FINSEQ_1:3;
                F.n = a by A34,A43,A51,A35;
                hence thesis by A52;
              end;
            end;
            hence thesis by FUNCT_1:def 5;
          end;
          take F;
          thus thesis by A39,A41,XBOOLE_0:def 10;
        end;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
  hence thesis by A2;
end;

definition
  let n such that
A1: n>0;
  let h be Element of INT.Group(n);
  func @h -> Element of NAT equals
  :Def1:
  h;
  coherence
  proof
    INT.Group(n) = multMagma(#Segm(n),addint(n)#) by A1,GR_CY_1:def 6;
    hence thesis by TARSKI:def 3;
  end;
end;

:::::::::::::::::::: Isomorphisms of Cyclic Groups. :::::::::::::::::::::::::::

theorem Th21:
  for Gc being strict finite cyclic Group holds
  INT.Group(ord Gc),Gc are_isomorphic
proof
  let Gc be strict finite cyclic Group;
  set n = ord Gc;
A1: n>0 by GROUP_1:90;
  consider g being Element of Gc such that
A2: for h be Element of Gc holds ex p st h=g|^p by GR_CY_1:42;
A3: Gc = gr {g}
  proof
    for h be Element of Gc holds ex i st h=g|^i
    proof
      let h be Element of Gc;
      consider p such that
A4:   h=g|^p by A2;
      reconsider p1=p as Integer;
      take p1;
      thus thesis by A4;
    end;
    hence thesis by Th11;
  end;
  ex h being Homomorphism of INT.Group(n),Gc st h is bijective
  proof
    deffunc F(Element of INT.Group(n)) = g|^@$1;
    consider h being Function of the carrier of INT.Group(n),the carrier of Gc
    such that
A5: for p be Element of INT.Group(n) holds h.p=F(p) from FUNCT_2:sch 4;
A6: INT.Group(n) = multMagma(#Segm(n),addint(n)#) by A1,GR_CY_1:def 6;
A7: dom h = the carrier of INT.Group(n) by FUNCT_2:def 1;
A8: rng h = the carrier of Gc
    proof
A9:   rng h c= the carrier of Gc by RELSET_1:12;
      the carrier of Gc c= rng h
      proof
        let x;
        assume x in the carrier of Gc;
        then reconsider z=x as Element of Gc;
        consider p such that
A10:    z=g|^p by A2;
        ex t st t in dom h & x=h.(t)
        proof
          set t=p mod n;
A11:      t < n by A1,NAT_D:1;
A12:      z=g|^(n * (p div n) + (p mod n)) by A1,A10,NAT_D:2
            .=g|^(n * (p div n))*(g|^(p mod n)) by GROUP_1:48
            .=g |^ n |^ (p div n)*(g |^ (p mod n)) by GROUP_1:50
            .=(1_Gc) |^ (p div n)*(g |^ (p mod n)) by GR_CY_1:29
            .=(1_Gc)*(g |^ (p mod n)) by GROUP_1:42
            .=(g |^ (p mod n)) by GROUP_1:def 5;
          reconsider t'=t as Element of INT.Group(n) by A6,A11,GR_CY_1:10;
A13:      @t'=p mod n by A1,Def1;
          take t';
          thus thesis by A5,A7,A12,A13;
        end;
        hence thesis by FUNCT_1:def 5;
      end;
      hence thesis by A9,XBOOLE_0:def 10;
    end;
A14: h is one-to-one
    proof
      let x,y;
      assume that
A15:  x in dom h and
A16:  y in dom h and
A17:  h.x=h.y and
A18:  x<>y;
      reconsider x'=x as Element of INT.Group(n) by A15,FUNCT_2:def 1;
      reconsider y'=y as Element of INT.Group(n) by A16,FUNCT_2:def 1;
A19:  g|^@x'=h.y' by A5,A17
        .= g|^@y' by A5;
      ex p,m st p<>m & g|^p=g|^m & p < n & m < n
      proof
        set p=@x',m=@y';
A20:    p=x' by A1,Def1;
        then
A21:    p < n by A6,GR_CY_1:10;
A22:    m=y' by A1,Def1;
        then m < n by A6,GR_CY_1:10;
        hence thesis by A18,A19,A20,A21,A22;
      end;
      then consider p,m such that
A23:  p<>m and
A24:  g|^p=g|^m and
A25:  p < n and
A26:  m < n;
A27:  ex k st k<>0 & k < n & g|^k=1_Gc
      proof
        now
          per cases by REAL_1:def 5;
          case
A28:        p < m;
            reconsider m1=m,p1=p as Integer;
            reconsider r1 = (m1-p1) as Element of NAT by A28,INT_1:18;
            now per cases;
              suppose
A29:            r1 >0;
                set zero=0;
                g|^m1*(g|^(-p1)) = g|^(p1+-p1) by A24,GROUP_1:63;
                then
A30:            g|^(m1+-p1) = g|^zero by GROUP_1:63;
                r1 < n
                proof
                  per cases;
                  suppose p=0;
                    hence thesis by A26;
                  end;
                  suppose p>0;
                    reconsider n1=n as Integer;
                    m1+-p1 < n1+0 by A26,XREAL_1:10;
                    hence thesis;
                  end;
                end;
                hence thesis by A29,A30,GROUP_1:43;
              end;
              suppose r1 = 0;
                hence thesis by A23;
              end;
            end;
            hence thesis;
          end;
          case
A32:        m < p;
            reconsider m1=m,p1=p as Integer;
            reconsider r1 = (p1-m1) as Element of NAT by A32,INT_1:18;
            per cases;
            suppose
A33:          r1 >0;
              set zero=0;
              g|^p1*(g|^(-m1)) = g|^(m1+-m1) by A24,GROUP_1:63;
              then
A34:          g|^(p1+-m1) = g|^zero by GROUP_1:63;
              r1 < n
              proof
                per cases;
                suppose m=0;
                  hence thesis by A25;
                end;
                suppose m>0;
                  reconsider n1=n as Integer;
                  p1+-m1 < n1+0 by A25,XREAL_1:10;
                  hence thesis;
                end;
              end;
              hence thesis by A33,A34,GROUP_1:43;
            end;
            suppose r1 = 0;
              hence thesis by A23;
            end;
          end;
          case m=p;
            hence contradiction by A23;
          end;
        end;
        hence thesis;
      end;
A36:  g is_not_of_order_0 by GR_CY_1:26;
      ord g = n by A3,GR_CY_1:27;
      hence contradiction by A27,A36,GROUP_1:def 12;
    end;
    for j,j1 being Element of INT.Group(n) holds h.(j*j1)=h.(j)*h.(j1)
    proof
      let j,j1 be Element of INT.Group(n);
A37:  @j1=j1 by A1,Def1;
      reconsider j'=j,j1'=j1 as Element of Segm(n) by A6;
      @(j*j1)= (addint(n)).(j',j1') by Def1,A6
        .=(j'+j1') mod n by A1,GR_CY_1:def 5
        .= (@j+@j1) mod n by A1,A37,Def1
        .=(@j+@j1)- n*((@j+@j1) div n) by A1,NEWTON:77;
      then h.(j*j1) = g|^((@j+@j1) +-n *((@j+@j1) div n)) by A5
        .= g|^(@j+@j1) *g|^(-n *((@j+@j1) div n)) by GROUP_1:64
        .= g|^(@j+@j1)*(g|^(n *((@j+@j1) div n)))" by GROUP_1:71
        .= g|^(@j+@j1)*(g|^n|^((@j+@j1) div n))" by GROUP_1:50
        .= g|^(@j+@j1)*((1_Gc)|^((@j+@j1) div n))" by GR_CY_1:29
        .= g|^(@j+@j1)*(1_Gc)" by GROUP_1:42
        .= g|^(@j+@j1)*(1_Gc) by GROUP_1:16
        .= g|^(@j+@j1) by GROUP_1:def 5
        .= (g|^@j)*(g|^@j1) by GROUP_1:48
        .= h.(j)*(g|^@j1) by A5
        .= h.(j)*h.(j1) by A5;
      hence thesis;
    end;
    then reconsider h as Homomorphism of INT.Group(n),Gc by GROUP_6:def 7;
A38: h is onto by A8,FUNCT_2:def 3;
A39: h is one-to-one by A14;
    take h;
    thus thesis by A38,A39,FUNCT_2:def 4;
  end;
  hence thesis by GROUP_6:def 15;
end;

theorem
  for Gc being strict cyclic Group st
  Gc is infinite holds INT.Group,Gc are_isomorphic
proof
  let Gc be strict cyclic Group;
  assume
A1: Gc is infinite;
  consider g being Element of Gc such that
A2: for h be Element of Gc holds ex i st h=g|^i by GR_CY_1:41;
  ex h being Homomorphism of INT.Group,Gc st h is bijective
  proof
    deffunc F(Element of INT.Group) = g|^@'$1;
    consider h being Function of the carrier of INT.Group,the carrier of Gc
    such that
A3: for j1 be Element of INT.Group holds h.j1=F(j1) from FUNCT_2:sch 4;
A4: Gc=gr {g} by A2,Th11;
A5: dom h = the carrier of INT.Group by FUNCT_2:def 1;
A6: rng h = the carrier of Gc
    proof
A7:   rng h c= the carrier of Gc by RELSET_1:12;
      the carrier of Gc c= rng h
      proof
        let x;
        assume x in the carrier of Gc;
        then reconsider z=x as Element of Gc;
        consider i such that
A8:     z=g|^i by A2;
        reconsider i'=i as Element of INT.Group by GR_CY_1:def 4,INT_1:def 2;
        ex t st t in dom h & x=h.(t)
        proof
          i=@'i' by GR_CY_1:def 8;
          then x = h.(i') by A3,A8;
          hence thesis by A5;
        end;
        hence thesis by FUNCT_1:def 5;
      end;
      hence thesis by A7,XBOOLE_0:def 10;
    end;
A9: h is one-to-one
    proof
      let x,y;
      assume that
A10:  x in dom h and
A11:  y in dom h and
A12:  h.x=h.y and
A13:  x<>y;
      reconsider x'=x as Element of INT.Group by A10,FUNCT_2:def 1;
      reconsider y'=y as Element of INT.Group by A11,FUNCT_2:def 1;
A14:  g|^@'x'=h.y' by A3,A12
        .= g|^@'y' by A3;
      ex i,i1 st i<>i1 & g|^i=g|^i1
      proof
        set i=@'x';
        i=x' by GR_CY_1:def 8;
        then i <> @'y' by A13,GR_CY_1:def 8;
        hence thesis by A14;
      end;
      hence contradiction by A1,A4,Th20;
    end;
    for j,j1 holds h.(j*j1)=h.(j)*h.(j1)
    proof
      let j,j1;
A15:  @'j=j by GR_CY_1:def 8;
      @'j1=j1 by GR_CY_1:def 8;
      then @'(j*j1)= @'j+@'j1 by A15,GR_CY_1:def 8;
      then h.(j*j1) = g|^(@'j+@'j1) by A3
        .= (g|^@'j)*(g|^@'j1) by GROUP_1:63
        .= h.(j)*(g|^@'j1) by A3
        .= h.(j)*h.(j1) by A3;
      hence thesis;
    end;
    then reconsider h as Homomorphism of INT.Group,Gc by GROUP_6:def 7;
A16: h is onto by A6,FUNCT_2:def 3;
A17: h is one-to-one by A9;
    take h;
    thus thesis by A16,A17,FUNCT_2:def 4;
  end;
  hence thesis by GROUP_6:def 15;
end;

theorem Th23:
  for Gc, Hc being strict finite cyclic Group st
  ord Hc = ord Gc holds Hc,Gc are_isomorphic
proof
  let Gc, Hc be strict finite cyclic Group;
  assume
A1: ord Hc = ord Gc;
  set p = ord Hc;
A2: INT.Group(p),Hc are_isomorphic by Th21;
  INT.Group(p),Gc are_isomorphic by A1,Th21;
  hence thesis by A2,GROUP_6:78;
end;

theorem Th24:
  for F,G being strict finite Group st
  ord F = p & ord G = p & p is prime holds F,G are_isomorphic
proof
  let F,G be strict finite Group;
  assume that
A1: ord F = p and
A2: ord G = p and
A3: p is prime;
A4: F is cyclic Group by A1,A3,GR_CY_1:45;
  G is cyclic Group by A2,A3,GR_CY_1:45;
  hence thesis by A1,A2,A4,Th23;
end;

theorem
  for F,G being strict finite Group st ord F = 2 & ord G = 2 holds
  F,G are_isomorphic by Th24,INT_2:44;

theorem
  for G being strict finite Group holds ord G = 2 implies
  for H being strict Subgroup of G holds
  H = (1).G or H = G by GR_CY_1:32,INT_2:44;

theorem
  for G being strict finite Group st
  ord G = 2 holds G is cyclic Group by GR_CY_1:45,INT_2:44;

theorem
  for G being strict finite Group st G is cyclic & ord G = n
  holds (for p st p divides n holds
  (ex G1 being strict Subgroup of G st ord G1 = p &
  for G2 being strict Subgroup of G st ord G2 = p holds G2=G1))
proof
  let G be strict finite Group;
  assume that
A1: G is cyclic and
A2: ord G = n;
  let p such that
A3: p divides n;
  ex G1 being strict Subgroup of G st ord G1 = p &
  for G2 being strict Subgroup of G st ord G2=p holds G2=G1
  proof
    consider a being Element of G such that
A4: G= gr {a} by A1,GR_CY_1:def 9;
    consider s be Nat such that
A5: n=p*s by A3,NAT_D:def 3;
A6: s in NAT by ORDINAL1:def 13;
    then
A7: ord (a|^s) = p by A2,A4,A5,Th14;
    then
A8: ord gr {a|^s} = p by GR_CY_1:27;
A9: for G2 being strict Subgroup of G st ord G2 = p holds G2=gr {a|^s}
    proof
      let G2 be strict Subgroup of G such that
A10:  ord G2 = p;
      consider k such that
A11:  G2 = gr {a|^k} by A4,Th13;
      n divides k*p by A2,A4,A10,A11,Th17;
      then consider m be Nat such that
A12:  k*p=n*m by NAT_D:def 3;
      s divides k
      proof
        ex l be Nat st k=s*l
        proof
          reconsider p1=p as Integer;
A13:      p<>0 by A2,A5,GROUP_1:90;
          (1/p1)*p1 *k = (1/p1)*(p1*s*m) by A5,A12,XCMPLX_1:4;
          then 1*k = p1*(1/p1)*s*m by A13,XCMPLX_1:107;
          then
A14:      k = 1*s*m by A13,XCMPLX_1:107;
          take m;
          thus thesis by A14;
        end;
        hence thesis by NAT_D:def 3;
      end;
      hence thesis by A6,A8,A10,A11,Th15,Th16;
    end;
    take gr {a|^s};
    thus thesis by A7,A9,GR_CY_1:27;
  end;
  hence thesis;
end;

theorem
  Gc=gr{g} implies (for G,f holds g in Image f implies f is onto)
proof
  assume
A1: Gc=gr {g};
  let G,f;
  assume g in Image f;
  then Image f = Gc by A1,Th19;
  hence thesis by GROUP_6:67;
end;

theorem Th30:
  for Gc being strict finite cyclic Group st (ex k st ord Gc = 2*k) holds
  ex g1 being Element of Gc st ord g1 = 2 &
  for g2 being Element of Gc st ord g2 = 2 holds g1=g2
proof
  let Gc be strict finite cyclic Group;
  set n = ord Gc;
  given k such that
A1: n=2*k;
  consider g being Element of Gc such that
A2: Gc=gr{g} by GR_CY_1:def 9;
  take g|^k;
A3: g|^k|^2=g|^ord Gc by A1,GROUP_1:50
    .= 1_Gc by GR_CY_1:29;
A4: g|^k is_not_of_order_0 by GR_CY_1:26;
A5: ord g = n by A2,GR_CY_1:27;
A6: k<>0 & 2<>0 by A1,GROUP_1:90;
  then
A7: k>0;
  for p being Nat st g|^k|^ p = 1_Gc & p <> 0 holds 2 <= p
  proof
    let p be Nat;
    assume that
A8: g|^k|^p=1_Gc and
A9: p<>0 and
A10: 2>p;
A11: n>k*p by A1,A7,A10,XREAL_1:70;
A12: g is_not_of_order_0 by GR_CY_1:26;
A13: k*p<>0 by A6,A9,XCMPLX_1:6;
    1_Gc = g|^(k*p) by A8,GROUP_1:50;
    hence contradiction by A5,A11,A12,A13,GROUP_1:def 12;
  end;
  hence ord(g|^k) = 2 by A3,A4,GROUP_1:def 12;
  let g2 be Element of Gc;
  assume that
A14: ord g2=2 and
A15: g|^k<>g2;
  consider k1 being Element of NAT such that
A16: g2=g|^k1 by A2,Th12;
  now
    consider t,t1 being Nat such that
A17: k1=(k*t)+t1 and
A18: t1<k by A7,NAT_1:17;
    t1<>0
    proof
      assume t1=0;
      then
A19:  g|^k1=g|^(k*( 2*(t div 2)+(t mod 2) )) by A17,NAT_D:2
        .=g|^( k* 2*(t div 2)+ k*(t mod 2) )
        .=g|^( k* 2*(t div 2) )*(g|^( k*(t mod 2))) by GROUP_1:48
        .=g|^( k* 2)|^(t div 2) *(g|^ (k*(t mod 2))) by GROUP_1:50
        .=(1_Gc)|^(t div 2) *(g|^ (k*(t mod 2))) by A3,GROUP_1:50
        .=(1_Gc) *(g|^ (k*(t mod 2))) by GROUP_1:42
        .=(g|^(k*(t mod 2))) by GROUP_1:def 5;
      per cases by NAT_D:12;
      suppose t mod 2 = 0;
        then g|^k1 = 1_Gc by A19,GROUP_1:43;
        hence contradiction by A14,A16,GROUP_1:84;
      end;
      suppose t mod 2 = 1;
        hence contradiction by A15,A16,A19;
      end;
    end;
    then
A20: 2*t1<>0;
A21: 2*t1<n by A1,A18,XREAL_1:70;
A22: g|^(2*t1)=1_Gc
    proof
      thus 1_Gc=g|^k1|^2 by A14,A16,GROUP_1:82
        .=g|^(((k*t)+t1)*2) by A17,
      GROUP_1:50
        .=g|^(n*t+t1*2) by A1
        .=g|^(n*t)*(g|^(t1*2)) by GROUP_1:48
        .=g|^(ord g)|^t*(g|^(t1*2)) by A5,GROUP_1:50
        .=(1_Gc)|^t*(g|^(t1*2)) by GROUP_1:82
        .=1_Gc*(g|^(t1*2)) by GROUP_1:42
        .=g|^(2*t1) by GROUP_1:def 5;
    end;
    g is_not_of_order_0 by GR_CY_1:26;
    hence contradiction by A5,A20,A21,A22,GROUP_1:def 12;
  end;
  hence thesis;
end;

registration
  let G;
  cluster center G -> normal;
  coherence by GROUP_5:90;
end;

theorem
  for Gc being strict finite cyclic Group st ex k st ord Gc = 2*k holds
  ex H being Subgroup of Gc st ord H = 2 & H is cyclic Group
proof
  let Gc be strict finite cyclic Group;
  set n = ord Gc;
  assume
A1: ex k st n=2*k;
  consider g1 being Element of Gc such that
A2: ord g1 = 2 and for g2 being Element of Gc st ord g2=2 holds g1=g2
  by A1,Th30;
  take gr {g1};
  thus thesis by A2,Th10,GR_CY_1:27;
end;

theorem Th32:
  for G being strict Group holds for g being Homomorphism of G,F st
  G is cyclic holds Image g is cyclic
proof
  let G be strict Group;
  let g be Homomorphism of G,F;
  assume G is cyclic;
  then consider a being Element of G such that
A1: G=gr{a} by GR_CY_1:def 9;
  ex f1 being Element of Image g st Image g=gr{f1}
  proof
    g.a in Image g by GROUP_6:54;
    then reconsider f=g.a as Element of Image g by STRUCT_0:def 5;
A2: for d being Element of Image g holds ex i st d=f|^i
    proof
      let d be Element of Image g;
      d in Image g by STRUCT_0:def 5;
      then consider b being Element of G such that
A3:   d=g.(b) by GROUP_6:54;
      b in gr{a} by A1,STRUCT_0:def 5;
      then consider i such that
A4:   b=a|^i by GR_CY_1:25;
A5:   d=(g.a)|^i by A3,A4,GROUP_6:46
        .=f|^i by GROUP_4:4;
      take i;
      thus thesis by A5;
    end;
    take f;
    thus thesis by A2,Th11;
  end;
  hence thesis by GR_CY_1:def 9;
end;

theorem
  for G,F being strict Group st G,F are_isomorphic & G is cyclic holds
  F is cyclic
proof
  let G,F be strict Group;
  assume that
A1: G,F are_isomorphic and
A2: G is cyclic;
  consider h being Homomorphism of G,F such that
A3: h is bijective by A1,GROUP_6:def 15;
  h is one-to-one & h is onto by A3,FUNCT_2:def 4;
  then Image h = F by GROUP_6:67;
  hence thesis by A2,Th32;
end;

