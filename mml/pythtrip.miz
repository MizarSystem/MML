:: Pythagorean triples
::  by Freek Wiedijk
::
:: Received August 26, 2001
:: Copyright (c) 2001 Association of Mizar Users

environ

 vocabularies INT_1, ARYTM_3, FILTER_0, ARYTM, SQUARE_1, ORDINAL2, MATRIX_2,
      NAT_1, ARYTM_1, FINSET_1, FUNCT_1, RELAT_1, ORDINAL1, BOOLE, ABSVALUE,
      INT_2, TARSKI, PYTHTRIP, COMPLEX1;
 notations TARSKI, XBOOLE_0, ENUMSET1, SUBSET_1, FINSET_1, NUMBERS, XCMPLX_0,
      XXREAL_0, INT_1, INT_2, NAT_1, NAT_D, SQUARE_1, ABIAN, PEPIN, DOMAIN_1,
      RELAT_1, FUNCT_1, LIMFUNC1, ORDINAL1;
 constructors DOMAIN_1, REAL_1, NAT_1, NAT_D, INT_2, LIMFUNC1, ABIAN, PEPIN;
 registrations ORDINAL1, FINSET_1, XXREAL_0, XREAL_0, NAT_1, INT_1, NEWTON,
      ABIAN;
 requirements SUBSET, BOOLE, NUMERALS, REAL, ARITHM;
 definitions INT_2, SQUARE_1, RELAT_1;
 theorems SQUARE_1, NAT_1, INT_2, WSIERP_1, REAL_2, EULER_2, ABIAN, EULER_1,
      PEPIN, INT_1, ENUMSET1, FINSET_1, TARSKI, RELAT_1, CARD_1, FUNCT_1,
      ORDINAL1, ZFMISC_1, XBOOLE_0, XCMPLX_1, NEWTON, XREAL_1, COMPLEX1,
      XXREAL_0, ABSVALUE, PREPOWER, NAT_D;
 schemes NAT_1;

begin :: relative primeness

reserve a,b,c,k,k',m,n,n',mn,p,p' for Element of NAT;
reserve i,i' for Integer;

definition
 let m,n be Nat;
 redefine pred m,n are_relative_prime means :Def1:
  for k be Nat st k divides m & k divides n holds k = 1;
 compatibility
 proof
  hereby
   assume m,n are_relative_prime;
   then A1: m hcf n = 1 by INT_2:def 6;
   let k be Nat;
A2: k in NAT by ORDINAL1:def 13;
   assume k divides m & k divides n;
   then k divides 1 by A1,NAT_D:def 5;
   hence k = 1 by A2,WSIERP_1:20;
  end;
  assume
    for k be Nat st k divides m & k divides n holds k = 1;
  then 1 divides m & 1 divides n &
  for k be Nat st k divides m & k divides n holds k
  divides 1 by NAT_D:6;
  hence m hcf n = 1 by NAT_D:def 5;
 end;
end;

definition
 let m,n be Nat;
 redefine pred m,n are_relative_prime means :Def2:
  for p being prime Nat holds not (p divides m & p divides n);
 compatibility
 proof
  hereby
   assume
A1: m,n are_relative_prime;
   let p be prime Nat;
   assume p divides m & p divides n;
   then p = 1 by A1,Def1;
   hence contradiction by INT_2:def 5;
  end;
  assume
A2: for p being prime Nat holds not (p divides m & p divides n);
  let k be Nat;
  assume
A3: k divides m & k divides n;
  per cases by NAT_1:26;
  suppose k = 0;
   then m = 0 & n = 0 by A3,INT_2:3;
   then 2 divides m & 2 divides n by NAT_D:6;
   hence k = 1 by A2,INT_2:44;
  end;
  suppose k = 1;
   hence k = 1;
  end;
  suppose k > 1;
   then k >= 1+1 by NAT_1:13;
   then consider p such that
A4: p is prime & p divides k by INT_2:48;
   reconsider p' = p as prime Element of NAT by A4;
       p' divides m & p' divides n by A3,A4,NAT_D:4;
   hence k = 1 by A2;
 end;
 end;
end;

begin :: squares

definition
 let n be number;
 attr n is square means :Def3:
  ex m being Nat st n = m^2;
end;

registration
 cluster square -> natural number;
 coherence
 proof
  let n be number;
  assume n is square;
  then ex m being Nat st n = m^2 by Def3;
  hence n is natural;
 end;
end;

registration
 let n be Nat;
 cluster n^2 -> square;
 coherence by Def3;
end;

registration
 cluster even square Element of NAT;
 existence
 proof
  take 0;
      0 = 2*0;
  hence thesis by SQUARE_1:60;
 end;
end;

registration
 cluster odd square Element of NAT;
 existence
 proof
  take 1;
      1 = 2*0 + 1;
  hence thesis by SQUARE_1:59;
 end;
end;

registration
 cluster even square number;
 existence
 proof
  consider n being even square Element of NAT;
  take n;
  thus thesis;
 end;
end;

registration
 cluster odd square number;
 existence
 proof
  consider n being odd square Element of NAT;
  take n;
  thus thesis;
 end;
end;

registration
 let m,n be square number;
 cluster m*n -> square;
 coherence
 proof
  consider m' being Nat such that
A1: m = m'^2 by Def3;
  consider n' being Nat such that
A2: n = n'^2 by Def3;
     m*n = (m'*n')^2 by A1,A2;
  hence thesis;
 end;
end;

theorem Th1:
 m*n is square & m,n are_relative_prime implies m is square & n is square
proof
  defpred P[Nat] means for m,n st m*n = $1 & m*n is square &
   m,n are_relative_prime holds m is square & n is square;
A1: for mn st for k st k < mn holds P[k] holds P[mn]
proof
  let mn;
  assume
A2: for k st k < mn for m,n
   st m*n = k & m*n is square & m,n are_relative_prime
   holds m is square & n is square;
  let m,n;
  assume
A3: m*n = mn;
  assume m*n is square;
  then consider mn' being Nat such that
A4: mn = mn'^2 by A3,Def3;
  assume
A5: m,n are_relative_prime;
  then A6: m hcf n = 1 by INT_2:def 6;
  per cases by A3,NAT_1:26;
  suppose
A7: m*n = 0;
   hereby
    per cases by A7,XCMPLX_1:6;
    suppose m = 0;
     hence m is square & n is square
      by A6,NEWTON:65,SQUARE_1:59,60;
    end;
    suppose n = 0;
     hence m is square & n is square
      by A6,NEWTON:65,SQUARE_1:59,60;
   end;
   end;
  end;
  suppose m*n = 1;
   hence m is square & n is square by NAT_1:15,SQUARE_1:59;
  end;
  suppose
A8: mn > 1;
   then m <> 0 & n <> 0 by A3;
   then A9: m > 0 & n > 0;
       mn >= 1 + 1 by A8,NAT_1:13;
   then consider p' such that
A10: p' is prime & p' divides mn by INT_2:48;
   reconsider p = p' as prime Element of NAT by A10;
A11: p > 1 by INT_2:def 5;
   then p*p > p by REAL_2:144;
   then A12: p*p > 1 by A11,XXREAL_0:2;
A13: p <> 0 by INT_2:def 5;
    p divides mn' by A4,A10,NEWTON:98;
   then consider mn'' be Nat such that
A14: mn' = p*mn'' by NAT_D:def 3;
   hereby
    per cases by A3,A10,NEWTON:98;
    suppose
A15:  p divides m;
     then A16:  not p divides n by A5,Def2;
     consider m' be Nat such that
A17:  m = p*m' by A15,NAT_D:def 3;
         p*(m'*n) = p*(p*(mn''*mn'')) by A3,A4,A14,A17;
     then m'*n = p*(mn''*mn'') by A13,XCMPLX_1:5;
     then p divides m'*n by NAT_D:def 3;
     then p divides m' by A16,NEWTON:98;
     then consider m'' be Nat such that
A18:  m' = p*m'' by NAT_D:def 3;
      reconsider m'' as Element of NAT by ORDINAL1:def 13;
A19: m = (p*p)*m'' by A17,A18;
        A20: m'' <> 0 by A3,A8,A17,A18;
     then A21:  m'' > 0;
      1*m'' < m by A12,A19,A20,REAL_2:199;
     then A22:  m''*n < mn by A3,A9,A21,REAL_2:199;
         p*(p*(m''*n)) = p*(p*(mn''*mn'')) by A3,A4,A14,A17,A18;
     then p*(m''*n) = p*(mn''*mn'') by A13,XCMPLX_1:5;
     then A23:  m''*n = mn''^2 by A13,XCMPLX_1:5;
         m = (p*p)*m'' by A17,A18;
     then m'' divides m by NAT_D:def 3;
     then m'' hcf n = 1 by A6,WSIERP_1:21;
     then A24:  m'',n are_relative_prime by INT_2:def 6;
     then m'' is square by A2,A22,A23;
     then consider m''' being Nat such that
A25:  m'' = m'''^2 by Def3;
         m = (p*m''')^2 by A17,A18,A25;
     hence m is square & n is square by A2,A22,A23,A24;
    end;
    suppose
A26:  p divides n;
     then A27:  not p divides m by A5,Def2;
     consider n' be Nat such that
A28:  n = p*n' by A26,NAT_D:def 3;
         p*(m*n') = p*(p*(mn''*mn'')) by A3,A4,A14,A28;
     then m*n' = p*(mn''*mn'') by A13,XCMPLX_1:5;
     then p divides m*n' by NAT_D:def 3;
     then p divides n' by A27,NEWTON:98;
     then consider n'' be Nat such that
A29:  n' = p*n'' by NAT_D:def 3;
      reconsider n'' as Element of NAT by ORDINAL1:def 13;
A30: n = (p*p)*n'' by A28,A29;
        A31: n'' <> 0 by A3,A8,A28,A29;
     then A32:  n'' > 0;
      1*n'' < n by A12,A30,A31,REAL_2:199;
     then A33:  m*n'' < mn by A3,A9,A32,REAL_2:199;
         p*(p*(m*n'')) = p*(p*(mn''*mn'')) by A3,A4,A14,A28,A29;
     then p*(m*n'') = p*(mn''*mn'') by A13,XCMPLX_1:5;
     then A34:  m*n'' = mn''^2 by A13,XCMPLX_1:5;
         n = (p*p)*n'' by A28,A29;
     then n'' divides n by NAT_D:def 3;
     then m hcf n'' = 1 by A6,WSIERP_1:21;
     then A35:  m,n'' are_relative_prime by INT_2:def 6;
     then n'' is square by A2,A33,A34;
     then consider n''' being Nat such that
A36:  n'' = n'''^2 by Def3;
         n = (p*n''')^2 by A28,A29,A36;
     hence m is square & n is square by A2,A33,A34,A35;
   end;
   end;
 end;
 end;
   for mn holds P[mn] from NAT_1:sch 4(A1);
 hence thesis;
end;

registration
 let i be even Integer;
 cluster i^2 -> even;
 coherence;
end;

registration
 let i be odd Integer;
 cluster i^2 -> odd;
 coherence;
end;

theorem Th2:
 i is even iff i^2 is even
proof
 hereby
  assume i is even;
  then reconsider i' = i as even Integer;
      i'^2 is even;
  hence i^2 is even;
 end;
 assume
A1: i^2 is even;
 assume i is odd;
 then reconsider i' = i as odd Integer;
     i'^2 is odd;
 hence contradiction by A1;
end;

theorem Th3:
 i is even implies i^2 mod 4 = 0
proof
 assume i is even;
 then consider i' such that
A1: i = 2*i' by ABIAN:def 1;
 i^2 = 4*(i'^2) + 0 by A1;
 hence i^2 mod 4 = (0 qua Integer) mod 4 by EULER_1:13
  .= 0 by NAT_D:24;
end;

theorem Th4:
 i is odd implies i^2 mod 4 = 1
proof
 assume i is odd;
 then consider i' such that
A1: i = 2*i' + 1 by ABIAN:1;
 i^2 = 4*(i'^2 + i') + 1 by A1;
 hence i^2 mod 4 = (1 qua Integer) mod 4 by EULER_1:13
  .= 1 by NAT_D:24;
end;

registration
 let m,n be odd square number;
 cluster m + n -> non square;
 coherence
 proof
  consider m' being Nat such that
A1: m = m'^2 by Def3;
A2: m' is odd by A1,Th2;
  consider n' being Nat such that
A3: n = n'^2 by Def3;
A4: n' is odd by A3,Th2;
  reconsider m'' = m as Element of NAT by ORDINAL1:def 13;
  reconsider n'' = n as Element of NAT by ORDINAL1:def 13;
A5: (m'' + n'') mod 4 = ((m'' mod 4) + (n'' mod 4)) mod 4 by EULER_2:8
   .= (1 + (n'' mod 4)) mod 4 by A1,A2,Th4
   .= (1 + 1) mod 4 by A3,A4,Th4
   .= 2 by NAT_D:24;
  hereby
   assume m + n is square;
   then consider mn' being Nat such that
A6: m + n = mn'^2 by Def3;
       mn' is even by A6,Th2;
   hence contradiction by A5,A6,Th3;
  end;
 end;
end;

theorem Th5:
 m^2 = n^2 implies m = n
proof
 assume
A1: m^2 = n^2;
 per cases by A1,SQUARE_1:109;
 suppose m = n;
  hence thesis;
 end;
 suppose
A2: m = -n;
      m >= 0 & n >= 0;
  then m = -0 by A2;
  hence thesis by A2;
end;
end;

theorem Th6:
 m divides n iff m^2 divides n^2
proof
defpred P[Element of NAT] means for n holds $1 divides n iff $1^2 divides n^2;
A1: for m st for k st k < m holds P[k] holds P[m]
 proof
  let m;
  assume
A2: for k st k < m for n holds k divides n iff k^2 divides n^2;
  let n;
  hereby
   assume m divides n;
   then consider k' be Nat such that
A3: n = m*k' by NAT_D:def 3;
       n^2 = (m^2)*(k'^2) by A3;
   hence m^2 divides n^2 by NAT_D:def 3;
  end;
  assume
A4: m^2 divides n^2;
  per cases by NAT_1:26;
  suppose m = 0;
   then n^2 = 0 by A4,INT_2:3;
   then n = 0 by XCMPLX_1:6;
   hence thesis by NAT_D:6;
  end;
  suppose m = 1;
   hence thesis by NAT_D:6;
  end;
  suppose
A5: m > 1;
   then m >= 1 + 1 by NAT_1:13;
   then consider p' such that
A6: p' is prime & p' divides m by INT_2:48;
   reconsider p = p' as prime Element of NAT by A6;
   consider m' be Nat such that
A7: m = p*m' by A6,NAT_D:def 3;
    reconsider m' as Element of NAT by ORDINAL1:def 13;
     m^2 = (m*m')*p by A7;
   then p divides m^2 by NAT_D:def 3;
   then p divides n^2 by A4,NAT_D:4;
   then p divides n by NEWTON:98;
   then consider n' be Nat such that
A8: n = p*n' by NAT_D:def 3;
    reconsider n' as Element of NAT by ORDINAL1:def 13;
A9: p > 1 by INT_2:def 5;
   then A10: p*m' > 1*m' by A5,A7,REAL_2:199;
   consider k' be Nat such that
A11: n^2 = (m^2)*k' by A4,NAT_D:def 3;
A12: (p^2)*(n'^2) = (p^2)*((m'^2)*k') by A7,A8,A11;
    p^2 > 0 by A9,SQUARE_1:74;
   then n'^2 = (m'^2)*k' by A12,XCMPLX_1:5;
   then m'^2 divides n'^2 by NAT_D:def 3;
   then m' divides n' by A2,A7,A10;
   then consider k be Nat such that
A13: n' = m'*k by NAT_D:def 3;
       n = m*k by A7,A8,A13;
   hence thesis by NAT_D:def 3;
 end;
 end;
   for m holds P[m] from NAT_1:sch 4(A1);
 hence thesis;
end;

begin :: distributive law for hcf

theorem Th7:
 m divides n or k = 0 iff k*m divides k*n
proof
 hereby
  assume
A1: m divides n or k = 0;
  per cases by A1;
  suppose m divides n;
   then consider k' be Nat such that
A2: n = m*k' by NAT_D:def 3;
       k*n = (k*m)*k' by A2;
   hence k*m divides k*n by NAT_D:def 3;
  end;
  suppose k = 0;
   hence k*m divides k*n;
 end;
 end;
 assume
A3: k*m divides k*n;
     now
  assume
A4: k <> 0;
  consider k' be Nat such that
A5: k*n = k*m*k' by A3,NAT_D:def 3;
      k*n = k*(m*k') by A5;
  then n = m*k' by A4,XCMPLX_1:5;
  hence m divides n by NAT_D:def 3;
 end;
 hence thesis;
end;

theorem Th8:
 (k*m) hcf (k*n) = k*(m hcf n)
proof
 per cases;
 suppose
A1: k <> 0;
      k divides k*m & k divides k*n by NAT_D:9;
  then k divides (k*m) hcf (k*n) by NAT_D:def 5;
  then consider k' be Nat such that
A2: (k*m) hcf (k*n) = k*k' by NAT_D:def 3;
    reconsider k' as Element of NAT by ORDINAL1:def 13;
      now
       k*k' divides k*m by A2,NAT_D:def 5;
   hence k' divides m by A1,Th7;
       k*k' divides k*n by A2,NAT_D:def 5;
   hence k' divides n by A1,Th7;
   let p be Nat;
   reconsider p'=p as Element of NAT by ORDINAL1:def 13;
   assume p divides m & p divides n;
   then k*p' divides k*m & k*p' divides k*n by Th7;
   then k*p divides k*k' by A2,NAT_D:def 5;
   then p' divides k' by A1,Th7;
   hence p divides k';
  end;
  hence thesis by A2,NAT_D:def 5;
 end;
 suppose k = 0;
  hence (k*m) hcf (k*n) = k*(m hcf n);
end;
end;

begin :: unbounded sets are infinite

theorem Th9:
 for X being set st for m ex n st n >= m & n in X holds X is infinite
proof
A1: now
  let f be Function;
  defpred P[Element of NAT] means ex m st for n st n >= m holds not n in f.:$1;
A2: P[0]
  proof
   take 0;
   let n such that n >= 0;
   thus not n in f.:0 by RELAT_1:149;
  end;
A3: for k st P[k] holds P[k+1]
  proof
   let k;
   assume ex m st for n st n >= m holds not n in f.:k;
   then consider m such that
A4: for n st n >= m holds not n in f.:k;
       k + 1 = succ k by CARD_1:52
    .= k \/ { k } by ORDINAL1:def 1;
   then A5: f.:(k + 1) = f.:k \/ Im(f,k) by RELAT_1:153;
   per cases;
   suppose
A6:  k in dom f & f.k in NAT;
    then reconsider m' = f.k as Element of NAT;
A7: f.:(k + 1) = f.:k \/ { m' } by A5,A6,FUNCT_1:117;
    take max(m,m' + 1);
    let n;
    assume n >= max(m,m' + 1);
    then n >= m & n >= m' + 1 by SQUARE_1:50;
    then n >= m & n <> m' by NAT_1:13;
    then not n in f.:k & not n in { m' } by A4,TARSKI:def 1;
    hence not n in f.:(k + 1) by A7,XBOOLE_0:def 2;
   end;
   suppose
A8:  k in dom f & not f.k in NAT;
    set m' = f.k;
A9: f.:(k + 1) = f.:k \/ { m' } by A5,A8,FUNCT_1:117;
    take m;
    let n;
    assume n >= m;
    then n >= m & n <> m' by A8;
    then not n in f.:k & not n in { m' } by A4,TARSKI:def 1;
    hence not n in f.:(k + 1) by A9,XBOOLE_0:def 2;
   end;
   suppose
     not k in dom f;
then A10:   dom f misses { k } by ZFMISC_1:56;
A11: Im(f,k) = f.:(dom f /\ { k }) by RELAT_1:145
     .= f.:{} by A10,XBOOLE_0:def 7
     .= {} by RELAT_1:149;
    take m;
    let n;
    assume n >= m;
    hence not n in f.:(k + 1) by A4,A5,A11;
  end;
  end;
  thus for k holds P[k] from NAT_1:sch 1(A2,A3);
 end;
 let X be set;
     now
  assume X is finite;
  then consider f being Function such that
A12: rng f = X & dom f in omega by FINSET_1:def 1;
  reconsider k = dom f as Element of NAT by A12;
      f.:k = X by A12,RELAT_1:146;
  hence ex m st for n st n >= m holds not n in X by A1;
 end;
 hence thesis;
end;

begin :: Pythagorean triples

theorem Th10:
 a,b are_relative_prime implies a is odd or b is odd
proof
 assume
A1: a,b are_relative_prime;
 assume a is even;
 then A2: 2 divides a by PEPIN:22;
 assume b is even;
 then 2 divides b by PEPIN:22;
 hence contradiction by A1,A2,Def1;
end;

theorem Th11:
 a^2 + b^2 = c^2 & a,b are_relative_prime & a is odd implies
  ex m,n st m <= n & a = n^2 - m^2 & b = 2*m*n & c = n^2 + m^2
proof
 assume
A1: a^2 + b^2 = c^2;
 assume
A2: a,b are_relative_prime;
 assume a is odd;
 then reconsider a' = a as odd Element of NAT;
     b is even
 proof
  assume b is odd;
  then reconsider b' = b as odd Element of NAT;
      a'^2 + b'^2 = c^2 by A1;
  hence contradiction;
 end;
 then reconsider b' = b as even Element of NAT;
     a'^2 + b'^2 = c^2 by A1;
 then reconsider c' = c as odd Element of NAT by Th2;
 consider n' such that
A3: c' + a' = 2*n' by ABIAN:def 2;
 consider i such that
A4: c' - a' = 2*i by ABIAN:def 1;
  c^2 >= a^2 + 0 by A1,XREAL_1:8;
 then c >= a by SQUARE_1:78;
 then 2*i >= 2*0 by A4,XREAL_1:50;
 then i >= 0 by XREAL_1:70;
 then reconsider m' = i as Element of NAT by INT_1:16;
 consider k' such that
A5: b' = 2*k' by ABIAN:def 2;
A6: n' - m' = a by A3,A4;
A7: n' + m' = c by A3,A4;
A8: n',m' are_relative_prime
 proof
  let p be prime Nat;
  assume
A9: p divides n' & p divides m';
  reconsider p as prime Element of NAT by ORDINAL1:def 13;
   p divides -m' by A9,INT_2:14;
  then A10: p divides (n' + -m') by A9,WSIERP_1:9;
  then p divides a*a by A3,A4,NAT_D:9;
  then A11: p divides -(a*a) by INT_2:14;
      p divides c by A7,A9,NAT_D:8;
  then A12: p divides c*c by NAT_D:9;
      b*b = c*c + -(a*a) by A1;
  then p divides (b*b qua Integer) by A11,A12,WSIERP_1:9;
  then p divides b by NEWTON:98;
  hence contradiction by A2,A3,A4,A10,Def2;
 end;
A13: n'*m' = ((c + a)/2)*((c - a)/2) by A3,A4
  .= (b/2)^2 by A1
  .= k'^2 by A5;
 then A14: n' is square & m' is square by A8,Th1;
 then consider n be Nat such that
A15: n' = n^2 by Def3;
 consider m be Nat such that
A16: m' = m^2 by A14,Def3;
 reconsider m,n as Element of NAT by ORDINAL1:def 13;
 take m,n;
  n >= 0 & m^2 <= n^2 by A6,A15,A16,REAL_2:105;
 hence m <= n by SQUARE_1:78;
 thus a = n^2 - m^2 by A3,A4,A15,A16;
     b^2 = (2^2)*(n*m)^2 by A5,A13,A15,A16,SQUARE_1:68
  .= (2*m*n)^2;
 hence b = 2*m*n by Th5;
 thus c = n^2 + m^2 by A3,A4,A15,A16;
end;

theorem
  a = n^2 - m^2 & b = 2*m*n & c = n^2 + m^2 implies a^2 + b^2 = c^2;

definition
 mode Pythagorean_triple -> Subset of NAT means :Def4:
  ex a,b,c st a^2 + b^2 = c^2 & it = { a,b,c };
 existence
 proof
  take { 0,0,0 };
  take 0,0,0;
  thus 0^2 + 0^2 = 0^2;
  thus thesis;
 end;
end;

reserve X for Pythagorean_triple;

registration
 cluster -> finite Pythagorean_triple;
 coherence
 proof
  let X;
      ex a,b,c st a^2 + b^2 = c^2 & X = { a,b,c } by Def4;
  hence thesis;
 end;
end;

definition
 redefine mode Pythagorean_triple means :Def5:
  ex k,m,n st m <= n & it = { k*(n^2 - m^2), k*(2*m*n), k*(n^2 + m^2) };
 compatibility
 proof
  let X be Subset of NAT;
  hereby
   assume X is Pythagorean_triple;
   then consider a,b,c such that
A1: a^2 + b^2 = c^2 & X = { a,b,c } by Def4;
   set k = a hcf b;
A2: k divides a & k divides b by NAT_D:def 5;
   per cases;
   suppose k = 0;
    then A3:  a = 0 & b = 0 by A2,INT_2:3;
    thus ex k,m,n st m <= n & X = { k*(n^2 - m^2), k*(2*m*n), k*(n^2 + m^2) }
    proof
     take 0,0,0;
     thus thesis by A1,A3,XCMPLX_1:6;
    end;
   end;
   suppose
A4:  k <> 0;
    then A5: k*k <> 0 by XCMPLX_1:6;
    consider a' be Nat such that
A6:  a = k*a' by A2,NAT_D:def 3;
    consider b' be Nat such that
A7:  b = k*b' by A2,NAT_D:def 3;
     reconsider a',b' as Element of NAT by ORDINAL1:def 13;
 c^2 = (k^2)*(a'^2 + b'^2) by A1,A6,A7;
    then k^2 divides c^2 by NAT_D:def 3;
    then k divides c by Th6;
    then consider c' be Nat such that
A8: c = k*c' by NAT_D:def 3;
    reconsider c' as Element of NAT by ORDINAL1:def 13;
        k^2*(a'^2 + b'^2) = k^2*c'^2 by A1,A6,A7,A8;
    then A9: a'^2 + b'^2 = c'^2 by A5,XCMPLX_1:5;
        k*(a' hcf b') = k*1 by A6,A7,Th8;
    then a' hcf b' = 1 by A4,XCMPLX_1:5;
    then A10: a',b' are_relative_prime & b',a' are_relative_prime by
INT_2:def 6;
    thus ex k,m,n st m <= n & X = { k*(n^2 - m^2), k*(2*m*n), k*(n^2 + m^2) }
    proof
     per cases by A10,Th10;
     suppose a' is odd;
      then consider m,n such that
A11:   m <= n & a' = n^2 - m^2 & b' = 2*m*n & c' = n^2 + m^2 by A9,A10,Th11;
      take k,m,n;
      thus m <= n & X = { k*(n^2 - m^2), k*(2*m*n), k*(n^2 + m^2) }
       by A1,A6,A7,A8,A11;
     end;
     suppose b' is odd;
      then consider m,n such that
A12:   m <= n & b' = n^2 - m^2 & a' = 2*m*n & c' = n^2 + m^2 by A9,A10,Th11;
      take k,m,n;
      thus m <= n & X = { k*(n^2 - m^2), k*(2*m*n), k*(n^2 + m^2) }
       by A1,A6,A7,A8,A12,ENUMSET1:99;
    end;
    end;
  end;
  end;
  assume
       ex k,m,n st m <= n & X = { k*(n^2 - m^2), k*(2*m*n), k*(n^2 + m^2) };
  then consider k,m,n such that
A13: m <= n & X = { k*(n^2 - m^2), k*(2*m*n), k*(n^2 + m^2) };
   m^2 <= n^2 by A13,SQUARE_1:77;
  then n^2 - m^2 >= 0 by XREAL_1:50;
  then reconsider a' = n^2 - m^2 as Element of NAT by INT_1:16;
  set b' = 2*m*n;
  set c' = n^2 + m^2;
  set a = k*a';
  set b = k*b';
  set c = k*c';
      a^2 + b^2 = c^2;
  hence X is Pythagorean_triple by A13,Def4;
 end;
end;

definition
 let X;
 attr X is degenerate means :Def6:
  0 in X;
end;

theorem
     n > 2 implies ex X st X is non degenerate & n in X
proof
 assume
A1: n > 2;
 per cases;
 suppose n is even;
  then consider m such that
A2: n = 2*m by ABIAN:def 2;
      2*m > 2*1 by A1,A2;
  then A3: m > 1 by XREAL_1:66;
  then m^2 > 1^2 by SQUARE_1:78;
  then m^2 - 1^2 > 0 by XREAL_1:52;
  then reconsider a = 1*(m^2 - 1^2) as Element of NAT by INT_1:16;
  set b = 1*(2*1*m);
  set c = 1*(m^2 + 1^2);
  reconsider X = { a,b,c } as Pythagorean_triple by A3,Def5;
  take X;
A4: c = m^2 + 1;
 a <> 0 by A3,SQUARE_1:78;
  hence not 0 in X by A1,A2,A4,ENUMSET1:def 1;
  thus n in X by A2,ENUMSET1:def 1;
 end;
 suppose n is odd;
  then consider i such that
A5: n = 2*i + 1 by ABIAN:1;
     A6: 2*i >= 2*1 by A1,A5,INT_1:20;
  then i >= 1 by XREAL_1:70;
  then reconsider m = i as Element of NAT by INT_1:16;
  reconsider a = 1*((m + 1)^2 - m^2) as Element of NAT by A5;
  set b = 1*(2*m*(m + 1));
  set c = 1*((m + 1)^2 + m^2);
      m <= m + 1 by NAT_1:11;
  then reconsider X = { a,b,c } as Pythagorean_triple by Def5;
  take X;
A7: a = 2*m + 1;
A8: c = m^2 + 2*m + m^2 + 1;
   b <> 0 by A6,XCMPLX_1:6;
  hence not 0 in X by A7,A8,ENUMSET1:def 1;
  thus n in X by A5,ENUMSET1:def 1;
end;
end;

definition
 let X;
 attr X is simplified means :Def7:
  for k st for n st n in X holds k divides n holds k = 1;
end;

definition
 let X;
 redefine attr X is simplified means :Def8:
  ex m,n st m in X & n in X & m,n are_relative_prime;
 compatibility
 proof
  hereby
   assume
A1: X is simplified;
   consider a,b,c such that
A2: a^2 + b^2 = c^2 & X = { a,b,c } by Def4;
   take a,b;
   thus a in X by A2,ENUMSET1:def 1;
   thus b in X by A2,ENUMSET1:def 1;
    now
    let k be Nat;
    assume
A3:  k divides a & k divides b;
     reconsider k1=k as Element of NAT by ORDINAL1:def 13;
     k1^2 divides a^2 & k1^2 divides b^2 by A3,Th6;
    then k^2 divides c^2 by A2,NAT_D:8;
    then k1 divides c by Th6;
    then for n st n in X holds k1 divides n by A2,A3,ENUMSET1:def 1;
    hence k = 1 by A1,Def7;
   end;
   hence a,b are_relative_prime by Def1;
  end;
  assume ex m,n st m in X & n in X & m,n are_relative_prime;
  then consider m,n such that
A4: m in X & n in X & m,n are_relative_prime;
A5: m hcf n = 1 by A4,INT_2:def 6;
  let k;
  assume for n st n in X holds k divides n;
  then k divides m & k divides n by A4;
  then k divides 1 by A5,NAT_D:def 5;
  hence k = 1 by WSIERP_1:20;
 end;
end;

theorem Th14:
 n > 0 implies ex X st X is non degenerate & X is simplified & 4*n in X
proof
 assume
A1: n > 0;
 then n > 0 & n >= 0 + 1 by NAT_1:13;
 then A2: n + n > 0 + 1 by XREAL_1:10;
 then (2*n)^2 > 1^2 by SQUARE_1:78;
 then (2*n)^2 - 1^2 > 0 by XREAL_1:52;
 then reconsider a = 1*((2*n)^2 - 1^2) as Element of NAT by INT_1:16;
 set b = 1*(2*1*(2*n));
 set c = 1*((2*n)^2 + 1^2);
 reconsider X = { a,b,c } as Pythagorean_triple by A2,Def5;
 take X;
A3: c = (2*n)^2 + 1;
A4: a <> 0 by A2,SQUARE_1:78;
    b <> 0 by A1;
 hence not 0 in X by A3,A4,ENUMSET1:def 1;
A5: a in X by ENUMSET1:def 1;
A6: c in X by ENUMSET1:def 1;
A7: a - c = -2;
     a hcf c = abs(a) hcf c by ABSVALUE:def 1
  .= a gcd c by ABSVALUE:def 1
  .= c gcd -2 by A7,PREPOWER:111
  .= (2*(2*n^2) + 1) gcd 2 by COMPLEX1:138
  .= 1 gcd 2 by EULER_1:17
  .= 1 by WSIERP_1:13;
 then a,c are_relative_prime by INT_2:def 6;
 hence X is simplified by A5,A6,Def8;
 thus 4*n in X by ENUMSET1:def 1;
end;

registration
 cluster non degenerate simplified Pythagorean_triple;
 existence
 proof
  consider X such that
A1: X is non degenerate & X is simplified & 4*1 in X by Th14;
  take X;
  thus thesis by A1;
 end;
end;

theorem
     { 3,4,5 } is non degenerate simplified Pythagorean_triple
proof
     3^2 + 4^2 = 5^2;
 then reconsider X = { 3,4,5 } as Pythagorean_triple by Def4;
A1: not 0 in X by ENUMSET1:def 1;
     3 hcf 4 = abs(3) hcf 4 by ABSVALUE:def 1
  .= 3 gcd 4 by ABSVALUE:def 1
  .= 3 gcd (4 - 3) by PREPOWER:111
  .= 1 by WSIERP_1:13;
 then 3 in X & 4 in X & 3,4 are_relative_prime by ENUMSET1:def 1,INT_2:def 6;
 hence thesis by A1,Def6,Def8;
end;

theorem
     { X: X is non degenerate & X is simplified } is infinite
proof
 set T = { X: X is non degenerate & X is simplified };
     for m ex n st n >= m & n in union T
 proof
  let m;
  set m' = m + 1;
  consider X such that
A1: X is non degenerate & X is simplified & 4*m' in X by Th14;
  set n = 4*m';
  take n;
 n + 0 = 1*m' + 3*m';
  then A2: n >= m' + 0 by XREAL_1:8;
      m' >= m by NAT_1:11;
  hence n >= m by A2,XXREAL_0:2;
      n in X & X in T by A1;
  hence n in union T by TARSKI:def 4;
 end;
 then A3: union T is infinite by Th9;
     now
  let X be set;
  assume X in T;
  then ex X' being Pythagorean_triple
   st X = X' & X' is non degenerate & X' is simplified;
  hence X is finite;
 end;
 hence T is infinite by A3,FINSET_1:25;
end;

