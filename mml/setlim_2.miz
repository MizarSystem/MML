:: Some Equations Related to the Limit of Sequence of Subsets
::  by Bo Zhang , Hiroshi Yamazaki and Yatsuka Nakamura
::
:: Received May 24, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies RELAT_1, SEQ_2, FUNCT_1, ORDINAL2, SETLIM_1, BOOLE, SETLIM_2,
      SETFAM_1, TARSKI, SEQM_3, PROB_1;
 notations TARSKI, SUBSET_1, FUNCT_1, NUMBERS, NAT_1, SETFAM_1, KURATO_2,
      PROB_1, SETLIM_1, XXREAL_0;
 constructors SETFAM_1, XXREAL_0, NAT_1, KURATO_2, SETLIM_1;
 registrations SUBSET_1, XREAL_0, RELSET_1, ARYTM_3, ORDINAL1;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, XBOOLE_0;
 theorems FUNCT_2, XBOOLE_0, XBOOLE_1, KURATO_2, SETLIM_1, CARD_3, PROB_1;
 schemes KURATO_2;

begin

 reserve n,m,k for Element of NAT,
         x,X for set,
         A for Subset of X,
         A1,A2 for SetSequence of X;

theorem Th1:
  (inferior_setsequence(A1)).n = Intersection (A1 ^\n)
proof reconsider Y = {A1.k: n <= k} as Subset-Family of X by SETLIM_1:33;
  (inferior_setsequence(A1)).n = meet Y by SETLIM_1:def 2
                                .= meet rng (A1 ^\ n) by SETLIM_1:10;
  hence thesis by SETLIM_1:13;
end;

theorem Th2:
  (superior_setsequence(A1)).n = Union (A1 ^\n)
proof reconsider Y = {A1.k: n <= k} as Subset-Family of X by SETLIM_1:33;
  (superior_setsequence(A1)).n = union Y by SETLIM_1:def 3
                                .= union rng (A1 ^\ n) by SETLIM_1:10;
  hence thesis by CARD_3:def 4;
end;

definition let X;
           let A1,A2 be SetSequence of X;
  func A1 (/\) A2 -> SetSequence of X means :Def1:
    for n holds it.n = A1.n /\ A2.n;
  existence
  proof
    deffunc F(Element of NAT) = A1.$1 /\ A2.$1;
    consider f being SetSequence of X such that
A1: for n holds f.n = F(n) from KURATO_2:sch 1;
    thus thesis by A1;
  end;
  uniqueness
  proof
  let A3 be SetSequence of X,A4 be SetSequence of X such that
A2: for n holds A3.n = A1.n /\ A2.n and
A3: for n holds A4.n = A1.n /\ A2.n;
   now let n;
     thus A3.n = A1.n /\ A2.n by A2 .= A4.n by A3;
    end;
    hence A3 = A4 by FUNCT_2:113;
  end;
  commutativity;
  func A1 (\/) A2 -> SetSequence of X means :Def2:
  for n holds it.n = A1.n \/ A2.n;
  existence
  proof
    deffunc F(Element of NAT) = A1.$1 \/ A2.$1;
    consider f being SetSequence of X such that
A4: for n holds f.n = F(n) from KURATO_2:sch 1;
    thus thesis by A4;
  end;
  uniqueness
  proof
  let A3 be SetSequence of X,A4 be SetSequence of X such that
A5: for n holds A3.n = A1.n \/ A2.n and
A6: for n holds A4.n = A1.n \/ A2.n;
   now let n;
     thus A3.n = A1.n \/ A2.n by A5 .= A4.n by A6;
    end;
    hence A3 = A4 by FUNCT_2:113;
  end;
  commutativity;
  func A1 (\) A2 -> SetSequence of X means :Def3:
  for n holds it.n = A1.n \ A2.n;
  existence
  proof
    deffunc F(Element of NAT) = A1.$1 \ A2.$1;
    consider f being SetSequence of X such that
A7: for n holds f.n = F(n) from KURATO_2:sch 1;
    thus thesis by A7;
  end;
  uniqueness
  proof
  let A3 be SetSequence of X,A4 be SetSequence of X such that
A8: for n holds A3.n = A1.n \ A2.n and
A9: for n holds A4.n = A1.n \ A2.n;
   now let n;
     thus A3.n = A1.n \ A2.n by A8 .= A4.n by A9;
    end;
    hence A3 = A4 by FUNCT_2:113;
  end;
  func A1 (\+\) A2 -> SetSequence of X means :Def4:
  for n holds it.n = A1.n \+\ A2.n;
  existence
  proof
    deffunc F(Element of NAT) = A1.$1 \+\ A2.$1;
    consider f being SetSequence of X such that
A10: for n holds f.n = F(n) from KURATO_2:sch 1;
    thus thesis by A10;
  end;
  uniqueness
  proof
  let A3 be SetSequence of X,A4 be SetSequence of X such that
A11: for n holds A3.n = A1.n \+\ A2.n and
A12: for n holds A4.n = A1.n \+\ A2.n;
   now let n;
     thus A3.n = A1.n \+\ A2.n by A11 .= A4.n by A12;
    end;
    hence A3 = A4 by FUNCT_2:113;
  end;
  commutativity;
end;

theorem Th3:
  A1 (\+\) A2 = (A1 (\) A2) (\/) (A2 (\) A1)
proof
 now let n;
 thus (A1 (\+\) A2).n = A1.n \+\ A2.n by Def4
   .= (A1 (\) A2).n \/ (A2.n \ A1.n) by Def3
   .= (A1 (\) A2).n \/ (A2 (\) A1).n by Def3
   .= ((A1 (\) A2) (\/) (A2 (\) A1)).n by Def2;
 end;
 hence thesis by FUNCT_2:113;
end;

theorem Th4:
  (A1 (/\) A2) ^\k = (A1 ^\k) (/\) (A2 ^\k)
proof
 now let n;
 thus ((A1 (/\) A2) ^\k).n = (A1 (/\) A2).(n+k) by KURATO_2:def 2
   .= A1.(n+k) /\ A2.(n+k) by Def1
   .= (A1 ^\k).n /\ A2.(n+k) by KURATO_2:def 2
   .= (A1 ^\k).n /\ (A2 ^\k).n by KURATO_2:def 2
   .= ((A1 ^\k) (/\) (A2 ^\k)).n by Def1;
 end;
 hence thesis by FUNCT_2:113;
end;

theorem Th5:
  (A1 (\/) A2) ^\k = (A1 ^\k) (\/) (A2 ^\k)
proof
 now let n;
 thus ((A1 (\/) A2) ^\k).n = (A1 (\/) A2).(n+k) by KURATO_2:def 2
   .= A1.(n+k) \/ A2.(n+k) by Def2
   .= (A1 ^\k).n \/ A2.(n+k) by KURATO_2:def 2
   .= (A1 ^\k).n \/ (A2 ^\k).n by KURATO_2:def 2
   .= ((A1 ^\k) (\/) (A2 ^\k)).n by Def2;
 end;
hence thesis by FUNCT_2:113;
end;

theorem Th6:
  (A1 (\) A2) ^\k = (A1 ^\k) (\) (A2 ^\k)
proof
 now let n;
  thus ((A1 (\) A2) ^\k).n = (A1 (\) A2).(n+k) by KURATO_2:def 2
   .= A1.(n+k) \ A2.(n+k) by Def3
   .= (A1 ^\k).n \ A2.(n+k) by KURATO_2:def 2
   .= (A1 ^\k).n \ (A2 ^\k).n by KURATO_2:def 2
   .= ((A1 ^\k) (\) (A2 ^\k)).n by Def3;
 end;
 hence thesis by FUNCT_2:113;
end;

theorem Th7:
  (A1 (\+\) A2) ^\k = (A1 ^\k) (\+\) (A2 ^\k)
proof
 now let n;
  thus ((A1 (\+\) A2) ^\k).n = (A1 (\+\) A2).(n+k) by KURATO_2:def 2
   .= A1.(n+k) \+\ A2.(n+k) by Def4
   .= (A1 ^\k).n \+\ A2.(n+k) by KURATO_2:def 2
   .= (A1 ^\k).n \+\ (A2 ^\k).n by KURATO_2:def 2
   .= ((A1 ^\k) (\+\) (A2 ^\k)).n by Def4;
 end;
 hence thesis by FUNCT_2:113;
end;

theorem Th8:
  Union (A1 (/\) A2) c= Union A1 /\ Union A2
proof
   let x;assume x in Union (A1 (/\) A2);then
   consider n such that
A1: x in (A1 (/\) A2).n by PROB_1:25;
   x in A1.n /\ A2.n by A1,Def1;then
   x in A1.n & x in A2.n by XBOOLE_0:def 3;then
   x in Union A1 & x in Union A2 by PROB_1:25;
   hence thesis by XBOOLE_0:def 3;
end;

theorem Th9:
  Union (A1 (\/) A2) = Union A1 \/ Union A2
proof
  thus Union (A1 (\/) A2) c= Union A1 \/ Union A2
  proof let x;assume x in Union (A1 (\/) A2);then
    consider n1 being Element of NAT such that
A1: x in (A1 (\/) A2).n1 by PROB_1:25;
A2: x in A1.n1 \/ A2.n1 by A1,Def2;
    per cases by A2,XBOOLE_0:def 2;
     suppose x in A1.n1;then
      x in Union A1 by PROB_1:25;
      hence x in Union A1 \/ Union A2 by XBOOLE_0:def 2;
     end;
     suppose x in A2.n1;then
      x in Union A2 by PROB_1:25;
      hence x in Union A1 \/ Union A2 by XBOOLE_0:def 2;
     end;
   end;
   let x; assume
A3: x in Union A1 \/ Union A2;
  per cases by A3,XBOOLE_0:def 2;
   suppose x in Union A1;then
    consider n2 being Element of NAT such that
    A4: x in A1.n2 by PROB_1:25;
     x in A1.n2 \/ A2.n2 by A4,XBOOLE_0:def 2;then
     x in (A1 (\/) A2).n2 by Def2;
   hence x in Union (A1 (\/) A2) by PROB_1:25;
   end;
   suppose x in Union A2;then
    consider n3 being Element of NAT such that
A5: x in A2.n3 by PROB_1:25;
     x in A1.n3 \/ A2.n3 by A5,XBOOLE_0:def 2;then
     x in (A1 (\/) A2).n3 by Def2;
   hence x in Union (A1 (\/) A2) by PROB_1:25;
   end;
end;

theorem Th10:
  Union A1 \ Union A2 c= Union (A1 (\) A2)
proof
 let x;assume x in Union A1 \ Union A2;then
 A1: x in Union A1 & not x in Union A2 by XBOOLE_0:def 4;then
     consider n1 being Element of NAT such that
 A2: x in A1.n1 by PROB_1:25;
     not x in A2.n1 by A1,PROB_1:25;then
     x in A1.n1 \ A2.n1 by A2,XBOOLE_0:def 4;then
     x in (A1 (\) A2).n1 by Def3;
  hence x in Union (A1 (\) A2) by PROB_1:25;
end;

theorem Th11:
  Union A1 \+\ Union A2 c= Union (A1 (\+\) A2)
proof
A1:(Union A1 \ Union A2) c= Union (A1 (\) A2) by Th10;
   (Union A2 \ Union A1) c= Union (A2 (\) A1) by Th10;then
A2:(Union A1 \ Union A2) \/ (Union A2 \ Union A1)
    c= Union (A1 (\) A2) \/ Union (A2 (\) A1) by A1,XBOOLE_1:13;
   Union (A1 (\) A2) \/ Union (A2 (\) A1)
    = Union ((A1 (\) A2) (\/) (A2 (\) A1)) by Th9
   .= Union (A1 (\+\) A2) by Th3;
   hence thesis by A2;
end;

theorem Th12:
  Intersection (A1 (/\) A2) = Intersection A1 /\ Intersection A2
proof
 thus Intersection (A1 (/\) A2) c= Intersection A1 /\ Intersection A2
 proof let x;assume
 A1: x in Intersection (A1 (/\) A2);
 A2: now let k;
      x in (A1 (/\) A2).k by A1,PROB_1:29;then
      x in (A1.k /\ A2.k) by Def1;
     hence x in A1.k & x in A2.k by XBOOLE_0:def 3;
     end;then
 A3: x in Intersection A1 by PROB_1:29;
     x in Intersection A2 by A2,PROB_1:29;
  hence x in Intersection A1 /\ Intersection A2 by A3,XBOOLE_0:def 3;
 end;
 let x;assume x in Intersection A1 /\ Intersection A2;then
 A4: x in Intersection A1 & x in Intersection A2 by XBOOLE_0:def 3;
     now let k;
     A5: x in A1.k by A4,PROB_1:29;
          x in A2.k by A4,PROB_1:29;then
      x in A1.k /\ A2.k by A5,XBOOLE_0:def 3;
     hence x in (A1 (/\) A2).k by Def1;
   end;
  hence x in Intersection (A1 (/\) A2) by PROB_1:29;
end;

theorem Th13:
  Intersection A1 \/ Intersection A2 c= Intersection (A1 (\/) A2)
proof let x;assume
A1: x in Intersection A1 \/ Intersection A2;
  per cases by A1,XBOOLE_0:def 2;
   suppose A2: x in Intersection A1;
    now let k;
     x in A1.k by A2,PROB_1:29;then
     x in (A1.k \/ A2.k) by XBOOLE_0:def 2;
    hence x in (A1 (\/) A2).k by Def2;
    end;
   hence thesis by PROB_1:29;
   end;
   suppose A3: x in Intersection A2;
    now let k;
     x in A2.k by A3,PROB_1:29;then
     x in A1.k \/ A2.k by XBOOLE_0:def 2;
    hence x in (A1 (\/) A2).k by Def2;
    end;
   hence thesis by PROB_1:29;
  end;
end;

theorem Th14:
  Intersection (A1 (\) A2) c= Intersection A1 \ Intersection A2
proof let x;assume
A1: x in Intersection (A1 (\) A2);
A2: now let k;
      x in (A1 (\) A2).k by A1,PROB_1:29;then
      x in (A1.k \ A2.k) by Def3;
      hence x in A1.k & not x in A2.k by XBOOLE_0:def 4;
    end;then
A3: x in Intersection A1 by PROB_1:29;
    not x in A2.0 by A2;then
    not x in Intersection A2 by PROB_1:29;
   hence thesis by A3,XBOOLE_0:def 4;
end;

definition let X;
           let A1 be SetSequence of X, A be Subset of X;
  func A (/\) A1 -> SetSequence of X means :Def5:
  for n holds it.n = A /\ A1.n;
  existence
  proof
    deffunc F(Element of NAT) = A /\ A1.$1;
    consider f being SetSequence of X such that
A1: for n holds f.n = F(n) from KURATO_2:sch 1;
    thus thesis by A1;
  end;
  uniqueness
  proof
  let A3 be SetSequence of X,A4 be SetSequence of X such that
A2: for n holds A3.n = A /\ A1.n and
A3: for n holds A4.n = A /\ A1.n;
   now let n;
     thus A3.n = A /\ A1.n by A2 .= A4.n by A3;
    end;
    hence A3 = A4 by FUNCT_2:113;
  end;
  func A (\/) A1 -> SetSequence of X means :Def6:
  for n holds it.n = A \/ A1.n;
  existence
  proof
    deffunc F(Element of NAT) = A \/ A1.$1;
    consider f being SetSequence of X such that
A4: for n holds f.n = F(n) from KURATO_2:sch 1;
    thus thesis by A4;
  end;
  uniqueness
  proof
  let A3 be SetSequence of X,A4 be SetSequence of X such that
A5: for n holds A3.n = A \/ A1.n and
A6: for n holds A4.n = A \/ A1.n;
   now let n;
    thus A3.n = A \/ A1.n by A5 .= A4.n by A6;
   end;
   hence A3 = A4 by FUNCT_2:113;
  end;
  func A (\) A1 -> SetSequence of X means :Def7:
  for n holds it.n = A \ A1.n;
  existence
  proof
    deffunc F(Element of NAT) = A \ A1.$1;
    consider f being SetSequence of X such that
A7: for n holds f.n = F(n) from KURATO_2:sch 1;
   thus thesis by A7;
  end;
  uniqueness
  proof
  let A3 be SetSequence of X,A4 be SetSequence of X such that
A8: for n holds A3.n = A \ A1.n and
A9: for n holds A4.n = A \ A1.n;
   now let n;
     thus A3.n = A \ A1.n by A8 .= A4.n by A9;
    end;
    hence A3 = A4 by FUNCT_2:113;
  end;
  func A1 (\) A -> SetSequence of X means :Def8:
  for n holds it.n = A1.n \ A;
  existence
  proof
    deffunc F(Element of NAT) = A1.$1 \ A;
    consider f being SetSequence of X such that
A10: for n holds f.n = F(n) from KURATO_2:sch 1;
    thus thesis by A10;
  end;
  uniqueness
  proof
  let A3 be SetSequence of X,A4 be SetSequence of X such that
A11: for n holds A3.n = A1.n \ A and
A12: for n holds A4.n = A1.n \ A;
   now let n;
     thus A3.n = A1.n \ A by A11 .= A4.n by A12;
    end;
    hence A3 = A4 by FUNCT_2:113;
  end;
  func A (\+\) A1 -> SetSequence of X means :Def9:
  for n holds it.n = A \+\ A1.n;
  existence
  proof
    deffunc F(Element of NAT) = A \+\ A1.$1;
    consider f being SetSequence of X such that
A13: for n holds f.n = F(n) from KURATO_2:sch 1;
    thus thesis by A13;
  end;
  uniqueness
  proof
  let A3 be SetSequence of X,A4 be SetSequence of X such that
A14: for n holds A3.n = A \+\ A1.n and
A15: for n holds A4.n = A \+\ A1.n;
   now let n;
     thus A3.n = A \+\ A1.n by A14 .= A4.n by A15;
    end;
    hence A3 = A4 by FUNCT_2:113;
  end;
end;

theorem
  A (\+\) A1 = (A (\) A1) (\/) (A1 (\) A)
proof
  now let n;
    thus (A (\+\) A1).n = A \+\ A1.n by Def9
     .= (A (\) A1).n \/ (A1.n \ A) by Def7
     .= (A (\) A1).n \/ (A1 (\) A).n by Def8
     .= ((A (\) A1) (\/) (A1 (\) A)).n by Def2;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th16:
  (A (/\) A1) ^\k = A (/\) (A1 ^\k)
proof
 now let n;
 thus ((A (/\) A1) ^\k).n = (A (/\) A1).(n+k) by KURATO_2:def 2
   .= A /\ A1.(n+k) by Def5
   .= A /\ (A1 ^\k).n by KURATO_2:def 2
   .= (A (/\) (A1 ^\k)).n by Def5;
 end;
hence thesis by FUNCT_2:113;
end;

theorem Th17:
  (A (\/) A1) ^\k = A (\/) (A1 ^\k)
proof
 now let n;
 thus ((A (\/) A1) ^\k).n = (A (\/) A1).(n+k) by KURATO_2:def 2
   .= A \/ A1.(n+k) by Def6
   .= A \/ (A1 ^\k).n by KURATO_2:def 2
   .= (A (\/) (A1 ^\k)).n by Def6;
 end;
hence thesis by FUNCT_2:113;
end;

theorem Th18:
  (A (\) A1) ^\k = A (\) (A1 ^\k)
proof
 now let n;
 thus ((A (\) A1) ^\k).n = (A (\) A1).(n+k) by KURATO_2:def 2
   .= A \ A1.(n+k) by Def7
   .= A \ (A1 ^\k).n by KURATO_2:def 2
   .= (A (\) (A1 ^\k)).n by Def7;
 end;
hence thesis by FUNCT_2:113;
end;

theorem Th19:
  (A1 (\) A) ^\k = (A1 ^\k) (\) A
proof
 now let n;
 thus ((A1 (\) A) ^\k).n = (A1 (\) A).(n+k) by KURATO_2:def 2
   .= A1.(n+k) \ A by Def8
   .= (A1 ^\k).n \ A by KURATO_2:def 2
   .= ((A1 ^\k) (\) A).n by Def8;
 end;
 hence thesis by FUNCT_2:113;
end;

theorem Th20:
  (A (\+\) A1) ^\k = A (\+\) (A1 ^\k)
proof
 now let n;
 thus ((A (\+\) A1) ^\k).n = (A (\+\) A1).(n+k) by KURATO_2:def 2
   .= A \+\ A1.(n+k) by Def9
   .= A \+\ (A1 ^\k).n by KURATO_2:def 2
   .= (A (\+\) (A1 ^\k)).n by Def9;
 end;
 hence thesis by FUNCT_2:113;
end;

theorem Th21:
  A1 is non-increasing implies A (/\) A1 is non-increasing
proof assume
A1: A1 is non-increasing;
    for n,m st n <= m holds (A (/\) A1).m c= (A (/\) A1).n
    proof let n,m such that
    A2: n <= m;
        A1.m c= A1.n by A2,A1,PROB_1:def 6;then
        A /\ A1.m c= A /\ A1.n by XBOOLE_1:27;then
        (A (/\) A1).m c= A /\ A1.n by Def5;
     hence thesis by Def5;
    end;
 hence thesis by PROB_1:def 6;
end;

theorem Th22:
  A1 is non-decreasing implies A (/\) A1 is non-decreasing
proof assume
A1: A1 is non-decreasing;
    for n,m st n <= m holds (A (/\) A1).n c= (A (/\) A1).m
    proof let n,m such that
    A2: n <= m;
        A1.n c= A1.m by A2,A1,PROB_1:def 7;then
        A /\ A1.n c= A /\ A1.m by XBOOLE_1:27;then
        (A (/\) A1).n c= A /\ A1.m by Def5;
     hence thesis by Def5;
    end;
  hence thesis by PROB_1:def 7;
end;

theorem
  A1 is monotone implies A (/\) A1 is monotone
proof assume
A1: A1 is monotone;
 per cases by A1, SETLIM_1:def 1;
  suppose A1 is non-increasing;then
    A (/\) A1 is non-increasing by Th21;
   hence A (/\) A1 is monotone by SETLIM_1:def 1;
  end;
  suppose A1 is non-decreasing;then
    A (/\) A1 is non-decreasing by Th22;
   hence A (/\) A1 is monotone by SETLIM_1:def 1;
  end;
end;

theorem Th24:
  A1 is non-increasing implies A (\/) A1 is non-increasing
proof assume
A1: A1 is non-increasing;
    for n,m st n <= m holds (A (\/) A1).m c= (A (\/) A1).n
    proof let n,m such that
    A2: n <= m;
        A1.m c= A1.n by A2,A1,PROB_1:def 6;then
        A \/ A1.m c= A \/ A1.n by XBOOLE_1:9;then
        (A (\/) A1).m c= A \/ A1.n by Def6;
     hence thesis by Def6;
    end;
 hence thesis by PROB_1:def 6;
 end;

theorem Th25:
  A1 is non-decreasing implies A (\/) A1 is non-decreasing
proof assume
A1:A1 is non-decreasing;
   for n,m st n <= m holds (A (\/) A1).n c= (A (\/) A1).m
   proof let n,m such that
A2:  n <= m;
     A1.n c= A1.m by A2,A1,PROB_1:def 7;then
     A \/ A1.n c= A \/ A1.m by XBOOLE_1:9;then
     (A (\/) A1).n c= A \/ A1.m by Def6;
     hence thesis by Def6;
   end;
   hence thesis by PROB_1:def 7;
end;

theorem
  A1 is monotone implies A (\/) A1 is monotone
proof assume
A1: A1 is monotone;
 per cases by A1, SETLIM_1:def 1;
  suppose A1 is non-increasing;then
   A (\/) A1 is non-increasing by Th24;
   hence A (\/) A1 is monotone by SETLIM_1:def 1;
  end;
  suppose A1 is non-decreasing;then
   A (\/) A1 is non-decreasing by Th25;
   hence A (\/) A1 is monotone by SETLIM_1:def 1;
  end;
end;

theorem Th27:
  A1 is non-increasing implies A (\) A1 is non-decreasing
proof assume
A1: A1 is non-increasing;
    for n,m st n <= m holds (A (\) A1).n c= (A (\) A1).m
    proof let n,m such that
  A2: n <= m;
      A1.m c= A1.n by A2,A1,PROB_1:def 6;then
      A \ A1.n c= A \ A1.m by XBOOLE_1:34;then
      (A (\) A1).n c= A \ A1.m by Def7;
      hence thesis by Def7;
    end;
  hence thesis by PROB_1:def 7;
end;

theorem Th28:
  A1 is non-decreasing implies A (\) A1 is non-increasing
proof assume
A1: A1 is non-decreasing;
    for n,m st n <= m holds (A (\) A1).m c= (A (\) A1).n
    proof let n,m such that
  A2: n <= m;
      A1.n c= A1.m by A2,A1,PROB_1:def 7;then
      A \ A1.m c= A \ A1.n by XBOOLE_1:34;then
      (A (\) A1).m c= A \ A1.n by Def7;
      hence thesis by Def7;
    end;
   hence thesis by PROB_1:def 6;
end;

theorem
  A1 is monotone implies A (\) A1 is monotone
proof assume
A1: A1 is monotone;
  per cases by A1, SETLIM_1:def 1;
  suppose A1 is non-increasing;then
    A (\) A1 is non-decreasing by Th27;
    hence A (\) A1 is monotone by SETLIM_1:def 1;
  end;
  suppose A1 is non-decreasing;then
    A (\) A1 is non-increasing by Th28;
    hence A (\) A1 is monotone by SETLIM_1:def 1;
  end;
end;

theorem Th30:
  A1 is non-increasing implies A1 (\) A is non-increasing
proof assume
A1:A1 is non-increasing;
   for n,m st n <= m holds (A1 (\) A).m c= (A1 (\) A).n
   proof let n,m such that
A2:  n <= m;
     A1.m c= A1.n by A2,A1,PROB_1:def 6;then
     A1.m \ A c= A1.n \ A by XBOOLE_1:33;then
     (A1 (\) A).m c= A1.n \ A by Def8;
     hence thesis by Def8;
    end;
  hence thesis by PROB_1:def 6;
end;

theorem Th31:
  A1 is non-decreasing implies A1 (\) A is non-decreasing
proof assume
A1: A1 is non-decreasing;
    for n,m st n <= m holds (A1 (\) A).n c= (A1 (\) A).m
    proof let n,m such that
    A2: n <= m;
       A1.n c= A1.m by A2,A1,PROB_1:def 7;then
       A1.n \ A c= A1.m \ A by XBOOLE_1:33;then
       (A1 (\) A).n c= A1.m \ A by Def8;
     hence thesis by Def8;
    end;
  hence thesis by PROB_1:def 7;
end;

theorem
  A1 is monotone implies A1 (\) A is monotone
proof assume
A1: A1 is monotone;
 per cases by A1, SETLIM_1:def 1;
  suppose A1 is non-increasing;then
   A1 (\) A is non-increasing by Th30;
   hence A1 (\) A is monotone by SETLIM_1:def 1;
  end;
  suppose A1 is non-decreasing;then
   A1 (\) A is non-decreasing by Th31;
   hence A1 (\) A is monotone by SETLIM_1:def 1;
  end;
end;

theorem Th33:
  Intersection (A (/\) A1) = A /\ Intersection A1
proof
  thus Intersection (A (/\) A1) c= A /\ Intersection A1
  proof let x;assume
A1: x in Intersection (A (/\) A1);
    now let k;
      x in (A (/\) A1).k by A1,PROB_1:29;then
      x in (A /\ A1.k) by Def5;
      hence x in A & x in A1.k by XBOOLE_0:def 3;
    end;then
    x in A & x in Intersection A1 by PROB_1:29;
    hence x in A /\ Intersection A1 by XBOOLE_0:def 3;
  end;
  let x;assume x in A /\ Intersection A1;then
A2: x in A & x in Intersection A1 by XBOOLE_0:def 3;
  now let k;
    x in A & x in A1.k by A2,PROB_1:29;then
    x in A /\ A1.k by XBOOLE_0:def 3;
    hence x in (A (/\) A1).k by Def5;
  end;
  hence thesis by PROB_1:29;
end;

theorem Th34:
  Intersection (A (\/) A1) = A \/ Intersection A1
proof
 thus Intersection (A (\/) A1) c= A \/ Intersection A1
 proof let x;assume
A1: x in Intersection (A (\/) A1);
A2: now let k;
     x in (A (\/) A1).k by A1,PROB_1:29;then
     x in (A \/ A1.k) by Def6;
     hence x in A or x in A1.k by XBOOLE_0:def 2;
    end;
    per cases by A2;
    suppose x in A;
      hence x in A \/ Intersection A1 by XBOOLE_0:def 2;
    end;
    suppose x in A1.k;then
      x in Intersection A1 by PROB_1:29;
      hence x in A \/ Intersection A1 by XBOOLE_0:def 2;
    end;
 end;
 let x;assume
 A3: x in A \/ Intersection A1;
  per cases by A3,XBOOLE_0:def 2;
   suppose A4: x in A;
    now let k;
     x in (A \/ A1.k) by A4,XBOOLE_0:def 2;
    hence x in (A (\/) A1).k by Def6;
    end;
   hence x in Intersection (A (\/) A1) by PROB_1:29;
   end;
   suppose A5: x in Intersection A1;
    now let k;
     x in A1.k by A5,PROB_1:29;then
     x in A \/ A1.k by XBOOLE_0:def 2;
    hence x in (A (\/) A1).k by Def6;
    end;
   hence thesis by PROB_1:29;
  end;
end;

theorem Th35:
  Intersection (A (\) A1) c= A \ Intersection A1
proof
 let x;assume
 A1: x in Intersection (A (\) A1);
 A2: now let k;
     x in (A (\) A1).k by A1,PROB_1:29;then
     x in (A \ A1.k) by Def7;
     hence x in A & not x in A1.k by XBOOLE_0:def 4;
   end;then
   not x in A1.0;then
   x in A & not x in Intersection A1 by A2,PROB_1:29;
  hence thesis by XBOOLE_0:def 4;
end;

theorem Th36:
  Intersection (A1 (\) A) = Intersection A1 \ A
proof
 thus Intersection (A1 (\) A) c= Intersection A1 \ A
 proof let x;assume
 A1: x in Intersection (A1 (\) A);
 now let k;
      x in (A1 (\) A).k by A1,PROB_1:29;then
      x in (A1.k \ A) by Def8;
     hence x in A1.k & not x in A by XBOOLE_0:def 4;
     end;then
     x in Intersection A1 & not x in A by PROB_1:29;
  hence x in Intersection A1 \ A by XBOOLE_0:def 4;
 end;
 let x;assume x in Intersection A1 \ A;then
 A2: x in Intersection A1 & not x in A by XBOOLE_0:def 4;
   now let k;
     x in A1.k & not x in A by A2,PROB_1:29;then
     x in A1.k \ A by XBOOLE_0:def 4;
     hence x in (A1 (\) A).k by Def8;
   end;
   hence thesis by PROB_1:29;
end;

theorem Th37:
  Intersection (A (\+\) A1) c= A \+\ Intersection A1
proof let x;assume
A1: x in Intersection (A (\+\) A1);
A2: now let n; x in (A (\+\) A1).n by A1,PROB_1:29;then
     x in A \+\ A1.n by Def9;
     hence x in A & not x in A1.n or not x in A & x in A1.n by XBOOLE_0:1;
    end;
 assume not x in A \+\ Intersection A1;then
 A3: (not x in A & not x in Intersection A1) or
  (x in Intersection A1 & x in A) by XBOOLE_0:1;
  per cases by A3,PROB_1:29;
   suppose not x in A & not for n holds x in A1.n;
   hence contradiction by A2;
   end;
   suppose x in A & for n holds x in A1.n;then
   x in A & x in A1.0;
   hence contradiction by A2;
  end;
end;

theorem Th38:
  Union (A (/\) A1) = A /\ Union A1
proof
 thus Union (A (/\) A1) c= A /\ Union A1
 proof
   let x; assume x in Union (A (/\) A1);then
    consider n such that
    A1: x in (A (/\) A1).n by PROB_1:25;
    x in A /\ A1.n by A1,Def5;then
    x in A & x in A1.n by XBOOLE_0:def 3;then
    x in A & x in Union A1 by PROB_1:25;
   hence thesis by XBOOLE_0:def 3;
 end;
  let x;assume x in A /\ Union A1;then
A2: x in A & x in Union A1 by XBOOLE_0:def 3;then
    consider n such that
A3: x in A1.n by PROB_1:25;
    x in A /\ A1.n by A2,A3,XBOOLE_0:def 3;then
    x in (A (/\) A1).n by Def5;
    hence thesis by PROB_1:25;
end;

theorem Th39:
  Union (A (\/) A1) = A \/ Union A1
proof
 thus Union (A (\/) A1) c= A \/ Union A1
 proof let x;assume
 A1: x in Union (A (\/) A1);
 A2: x in A or ex k st x in A1.k
    proof consider k such that
  A3: x in (A (\/) A1).k by A1,PROB_1:25;
      x in (A \/ A1.k) by A3,Def6;then
      x in A or x in A1.k by XBOOLE_0:def 2;
      hence thesis;
    end;
    per cases by A2;
    suppose x in A;
      hence x in A \/ Union A1 by XBOOLE_0:def 2;
    end;
    suppose ex k st x in A1.k;then
      x in Union A1 by PROB_1:25;
      hence x in A \/ Union A1 by XBOOLE_0:def 2;
    end;
 end;
 let x;assume
 A4: x in A \/ Union A1;
  per cases by A4,XBOOLE_0:def 2;
   suppose A5: x in A;
     x in (A \/ A1.1) by A5,XBOOLE_0:def 2;then
     x in (A (\/) A1).1 by Def6;
    hence thesis by PROB_1:25;
   end;
   suppose A6: x in Union A1;
    consider k such that
    A7: x in A1.k by A6,PROB_1:25;
     x in A \/ A1.k by A7,XBOOLE_0:def 2;then
     x in (A (\/) A1).k by Def6;
    hence thesis by PROB_1:25;
  end;
end;

theorem Th40:
  A \ Union A1 c= Union (A (\) A1)
proof
 let x;assume x in A \ Union A1;then
 A1: x in A & not x in Union A1 by XBOOLE_0:def 4;
     now let k;
       x in A & not x in A1.k by A1,PROB_1:25;then
       x in A \ A1.k by XBOOLE_0:def 4;
     hence x in (A (\) A1).k by Def7;
     end;then
     x in (A (\) A1).1;
   hence x in Union (A (\) A1) by PROB_1:25;
end;

theorem Th41:
  Union (A1 (\) A) = Union A1 \ A
proof
 thus Union (A1 (\) A) c= Union A1 \ A
 proof
   let x;assume
A1: x in Union (A1 (\) A);
    ex k st x in A1.k & not x in A
    proof consider k such that
A2:  x in (A1 (\) A).k by A1,PROB_1:25;
     x in A1.k \ A by A2,Def8;then
     x in A1.k & not x in A by XBOOLE_0:def 4;
     hence thesis;
    end;then
    x in Union A1 & not x in A by PROB_1:25;
    hence x in Union A1 \ A by XBOOLE_0:def 4;
  end;
   let x;assume x in Union A1 \ A;then
A3: x in Union A1 & not x in A by XBOOLE_0:def 4;
    ex k st x in (A1 (\) A).k
    proof
      consider k such that
  A4: x in A1.k by A3,PROB_1:25;
      x in A1.k \ A by A3,A4,XBOOLE_0:def 4;then
      x in (A1 (\) A).k by Def8;
      hence thesis;
    end;
    hence x in Union (A1 (\) A) by PROB_1:25;
end;

theorem Th42:
  A \+\ Union A1 c= Union (A (\+\) A1)
proof let x;assume
A1: x in A \+\ Union A1;
     per cases by A1,XBOOLE_0:1;
     suppose x in A & not x in Union A1;then
       x in A & not x in A1.0 by PROB_1:25;then
       x in A \+\ A1.0 by XBOOLE_0:1;then
       x in (A (\+\) A1).0 by Def9;
       hence x in Union (A (\+\) A1) by PROB_1:25;
     end;
     suppose A2: (not x in A & x in Union A1);then
       consider n1 being Element of NAT such that
  A3: x in A1.n1 by PROB_1:25;
       x in A \+\ A1.n1 by A3,A2,XBOOLE_0:1;then
       x in (A (\+\) A1).n1 by Def9;
       hence x in Union (A (\+\) A1) by PROB_1:25;
     end;
end;

theorem
  (inferior_setsequence(A1 (/\) A2)).n
    = (inferior_setsequence A1).n /\ (inferior_setsequence A2).n
proof
 (inferior_setsequence(A1 (/\) A2)).n
    = Intersection ((A1 (/\) A2) ^\n) by Th1
   .= Intersection ((A1 ^\n) (/\) (A2 ^\n)) by Th4
   .= Intersection (A1 ^\n) /\ Intersection (A2 ^\n) by Th12
   .= (inferior_setsequence A1).n /\ Intersection (A2 ^\n) by Th1
   .= (inferior_setsequence A1).n /\ (inferior_setsequence A2).n by Th1;
 hence thesis;
end;

theorem
  (inferior_setsequence A1).n \/ (inferior_setsequence A2).n
    c= (inferior_setsequence(A1 (\/) A2)).n
proof
A1: (inferior_setsequence A1).n \/ (inferior_setsequence A2).n
 = Intersection (A1 ^\n) \/ (inferior_setsequence A2).n by Th1
.= Intersection (A1 ^\n) \/ Intersection (A2 ^\n) by Th1;
 Intersection ((A1 ^\n) (\/) (A2 ^\n))
 = Intersection ((A1 (\/) A2) ^\n) by Th5
.= (inferior_setsequence(A1 (\/) A2)).n by Th1;
hence thesis by A1,Th13;
end;

theorem
  (inferior_setsequence(A1 (\) A2)).n
    c= (inferior_setsequence A1).n \ (inferior_setsequence A2).n
proof
 (inferior_setsequence(A1 (\) A2)).n
    = Intersection ((A1 (\) A2) ^\n) by Th1
   .= Intersection ((A1 ^\n) (\) (A2 ^\n)) by Th6;then
 (inferior_setsequence(A1 (\) A2)).n
   c= Intersection (A1 ^\n) \ Intersection (A2 ^\n) by Th14;then
 (inferior_setsequence(A1 (\) A2)).n
   c= (inferior_setsequence A1).n \ Intersection (A2 ^\n) by Th1;
 hence thesis by Th1;
end;

theorem
  (superior_setsequence(A1 (/\) A2)).n
   c= (superior_setsequence A1).n /\ (superior_setsequence A2).n
proof
 (superior_setsequence(A1 (/\) A2)).n
    = Union ((A1 (/\) A2) ^\n) by Th2
   .= Union ((A1 ^\n) (/\) (A2 ^\n)) by Th4;then
 (superior_setsequence(A1 (/\) A2)).n
   c= Union (A1 ^\n) /\ Union (A2 ^\n) by Th8;then
 (superior_setsequence(A1 (/\) A2)).n
   c= (superior_setsequence A1).n /\ Union (A2 ^\n) by Th2;
 hence thesis by Th2;
end;

theorem
  (superior_setsequence(A1 (\/) A2)).n
   = (superior_setsequence A1).n \/ (superior_setsequence A2).n
proof
 (superior_setsequence(A1 (\/) A2)).n
    = Union ((A1 (\/) A2) ^\n) by Th2
   .= Union ((A1 ^\n) (\/) (A2 ^\n)) by Th5
   .= Union (A1 ^\n) \/ Union (A2 ^\n) by Th9
   .= (superior_setsequence A1).n \/ Union (A2 ^\n) by Th2;
 hence thesis by Th2;
end;

theorem
  (superior_setsequence A1).n \ (superior_setsequence A2).n
   c= (superior_setsequence(A1 (\) A2)).n
proof
 (superior_setsequence A1).n \ (superior_setsequence A2).n
  = Union (A1 ^\n) \ (superior_setsequence A2).n by Th2
 .= Union (A1 ^\n) \ Union (A2 ^\n) by Th2;then
 (superior_setsequence A1).n \ (superior_setsequence A2).n
    c= Union ((A1 ^\n) (\) (A2 ^\n)) by Th10;then
 (superior_setsequence A1).n \ (superior_setsequence A2).n
    c= Union ((A1 (\) A2) ^\n) by Th6;
 hence thesis by Th2;
end;

theorem
  (superior_setsequence A1).n \+\ (superior_setsequence A2).n
   c= (superior_setsequence(A1 (\+\) A2)).n
proof
 (superior_setsequence A1).n \+\ (superior_setsequence A2).n
  = Union (A1 ^\n) \+\ (superior_setsequence A2).n by Th2
 .= Union (A1 ^\n) \+\ Union (A2 ^\n) by Th2;then
 (superior_setsequence A1).n \+\ (superior_setsequence A2).n
    c= Union ((A1 ^\n) (\+\) (A2 ^\n)) by Th11;then
 (superior_setsequence A1).n \+\ (superior_setsequence A2).n
    c= Union ((A1 (\+\) A2) ^\n) by Th7;
 hence thesis by Th2;
end;

theorem Th50:
  (inferior_setsequence(A (/\) A1)).n =  A /\ (inferior_setsequence A1).n
proof
 (inferior_setsequence(A (/\) A1)).n
    = Intersection ((A (/\) A1) ^\n) by Th1
   .= Intersection (A (/\) (A1 ^\n)) by Th16
   .= A /\ Intersection (A1 ^\n) by Th33
   .= A /\ (inferior_setsequence A1).n by Th1;
 hence thesis;
end;

theorem Th51:
  (inferior_setsequence(A (\/) A1)).n =  A \/ (inferior_setsequence A1).n
proof
 (inferior_setsequence(A (\/) A1)).n
    = Intersection ((A (\/) A1) ^\n) by Th1
   .= Intersection (A (\/) (A1 ^\n)) by Th17
   .= A \/ Intersection (A1 ^\n) by Th34
   .= A \/ (inferior_setsequence A1).n by Th1;
 hence thesis;
end;

theorem
  (inferior_setsequence(A (\) A1)).n c=  A \ (inferior_setsequence A1).n
proof
 (inferior_setsequence(A (\) A1)).n
    = Intersection ((A (\) A1) ^\n) by Th1
   .= Intersection (A (\) (A1 ^\n)) by Th18;then
 (inferior_setsequence(A (\) A1)).n
   c= A \ Intersection (A1 ^\n) by Th35;
 hence thesis by Th1;
 end;

theorem Th53:
  (inferior_setsequence(A1 (\) A)).n = (inferior_setsequence A1).n \ A
proof
 (inferior_setsequence(A1 (\) A)).n
    = Intersection ((A1 (\) A) ^\n) by Th1
   .= Intersection ((A1 ^\n) (\) A) by Th19
   .= Intersection (A1 ^\n) \ A by Th36
   .= (inferior_setsequence A1).n \ A by Th1;
 hence thesis;
end;

theorem
  (inferior_setsequence(A (\+\) A1)).n c= A \+\ (inferior_setsequence A1).n
proof
(inferior_setsequence(A (\+\) A1)).n
    = Intersection ((A (\+\) A1) ^\n) by Th1
   .= Intersection (A (\+\) (A1 ^\n)) by Th20;then
 (inferior_setsequence(A (\+\) A1)).n
   c= A \+\ Intersection (A1 ^\n) by Th37;
 hence thesis by Th1;
end;

theorem Th55:
  (superior_setsequence(A (/\) A1)).n =  A /\ (superior_setsequence A1).n
proof
 (superior_setsequence(A (/\) A1)).n
    = Union ((A (/\) A1) ^\n) by Th2
   .= Union (A (/\) (A1 ^\n)) by Th16
   .= A /\ Union (A1 ^\n) by Th38
   .= A /\ (superior_setsequence A1).n by Th2;
 hence thesis;
end;

theorem Th56:
  (superior_setsequence(A (\/) A1)).n =  A \/ (superior_setsequence A1).n
proof
 (superior_setsequence(A (\/) A1)).n
    = Union ((A (\/) A1) ^\n) by Th2
   .= Union (A (\/) (A1 ^\n)) by Th17
   .= A \/ Union (A1 ^\n) by Th39
   .= A \/ (superior_setsequence A1).n by Th2;
 hence thesis;
end;

theorem
  A \ (superior_setsequence A1).n c= (superior_setsequence(A (\) A1)).n
proof
 A \ (superior_setsequence A1).n
   = A \ Union (A1 ^\n) by Th2;then
 A \ (superior_setsequence A1).n
   c= Union (A (\) (A1 ^\n)) by Th40;then
 A \ (superior_setsequence A1).n
   c= Union ((A (\) A1) ^\n) by Th18;
hence thesis by Th2;
end;

theorem Th58:
  (superior_setsequence(A1 (\) A)).n = (superior_setsequence A1).n \ A
proof
 reconsider X1 = (superior_setsequence(A1)) as SetSequence of X;
 reconsider X2 = (superior_setsequence(A1 (\) A)) as SetSequence of X;
A1: X2.n c= X1.n \ A
 proof let x;assume
 A2: x in X2.n;
     ex k st x in A1.(n+k) & not x in A
     proof consider k such that
     A3: x in (A1 (\) A).(n+k) by A2,SETLIM_1:25;
      x in A1.(n+k) \ A by A3,Def8;then
      x in A1.(n+k) & not x in A by XBOOLE_0:def 4;
     hence thesis;
     end;then
     x in X1.n & not x in A by SETLIM_1:25;
  hence x in X1.n \ A by XBOOLE_0:def 4;
 end;
 X1.n \ A c= X2.n
 proof let x;assume x in X1.n \ A;then
 A4: x in X1.n & not x in A by XBOOLE_0:def 4;
     ex k st x in (A1 (\) A).(n+k)
     proof
      consider k such that
     A5: x in A1.(n+k) by A4,SETLIM_1:25;
      x in A1.(n+k) \ A by A4,A5,XBOOLE_0:def 4;then
      x in (A1 (\) A).(n+k) by Def8;
     hence thesis;
     end;
  hence x in X2.n by SETLIM_1:25;
 end;
hence thesis by A1,XBOOLE_0:def 10;
end;

theorem
  A \+\ (superior_setsequence A1).n c= (superior_setsequence(A (\+\) A1)).n
proof
  A \+\ (superior_setsequence A1).n
   = A \+\ Union (A1 ^\n) by Th2;then
  A \+\ (superior_setsequence A1).n
   c= Union (A (\+\) (A1 ^\n)) by Th42;then
  A \+\ (superior_setsequence A1).n
   c= Union ((A (\+\) A1) ^\n) by Th20;
  hence thesis by Th2;
end;

theorem Th60:
  lim_inf (A1 (/\) A2) = lim_inf A1 /\ lim_inf A2
proof
  (A1 (/\) A2).n = A1.n /\ A2.n by Def1;
  hence thesis by KURATO_2:13;
end;

theorem Th61:
  lim_inf A1 \/ lim_inf A2 c= lim_inf (A1 (\/) A2)
proof
  (A1 (\/) A2).n = A1.n \/ A2.n by Def2;
  hence thesis by KURATO_2:15;
end;

theorem Th62:
  lim_inf (A1 (\) A2) c= lim_inf A1 \ lim_inf A2
proof
  let x; assume x in lim_inf (A1 (\) A2);then
    consider n such that
A1: for k holds x in (A1 (\) A2).(n+k) by KURATO_2:7;
A2: now let k;
     x in (A1 (\) A2).(n+k) by A1;then
     x in A1.(n+k) \ A2.(n+k) by Def3;
    hence x in A1.(n+k) & not x in A2.(n+k) by XBOOLE_0:def 4;
    end;then
A3:  x in lim_inf A1 by KURATO_2:7;
    not x in lim_inf A2
    proof assume x in lim_inf A2;then
        consider n1 being Element of NAT such that
    A4: for k holds x in A2.(n1+k) by KURATO_2:7;
        x in A2.(n1+n) by A4;
        hence contradiction by A2;
    end;
    hence x in lim_inf A1 \ lim_inf A2 by A3,XBOOLE_0:def 4;
end;

theorem Th63:
  A1 is convergent or A2 is convergent implies
lim_inf (A1 (\/) A2) = lim_inf A1 \/ lim_inf A2
proof assume
A1: A1 is convergent or A2 is convergent;
A2: for A1,A2 st A1 is convergent holds
 lim_inf (A1 (\/) A2) = lim_inf A1 \/ lim_inf A2
proof let A1,A2;assume
A3: A1 is convergent;
 thus lim_inf (A1 (\/) A2) c= lim_inf A1 \/ lim_inf A2
 proof let x;assume x in lim_inf (A1 (\/) A2);then
 consider n1 being Element of NAT such that
A4: for k holds x in (A1 (\/) A2).(n1+k) by KURATO_2:7;
A5:  now let n; x in (A1 (\/) A2).(n1+n) by A4;then
   x in A1.(n1+n) \/ A2.(n1+n) by Def2;
   hence x in A1.(n1+n) or x in A2.(n1+n) by XBOOLE_0:def 2;
  end;
 x in lim_inf A1 \/ lim_inf A2
 proof assume not(x in lim_inf A1 \/ lim_inf A2);then
  A6: not x in lim_inf A1 & not x in lim_inf A2 by XBOOLE_0:def 2;then
      not x in lim_sup A1 by A3,KURATO_2:def 7;then
     consider n2 being Element of NAT such that
 A7: for k holds not x in A1.(n2+k) by KURATO_2:8;
     consider k1 being Element of NAT such that
 A8: not x in A2.((n1+n2)+k1) by A6,KURATO_2:7;
     not x in A1.(n2+(n1+k1)) by A7;then
     not x in A1.(n1+(n2+k1)) & not x in A2.(n1+(n2+k1)) by A8;
  hence contradiction by A5;
  end;
 hence thesis;
 end;
 thus thesis by Th61;
 end;
 per cases by A1;
  suppose A1 is convergent;
   hence thesis by A2;
  end;
  suppose A2 is convergent;
   hence thesis by A2;
  end;
end;

theorem Th64:
  A2 is convergent implies
    lim_inf (A1 (\) A2) = lim_inf A1 \ lim_inf A2
proof assume
A1: A2 is convergent;
 thus lim_inf (A1 (\) A2) c= lim_inf A1 \ lim_inf A2 by Th62;
 thus lim_inf A1 \ lim_inf A2 c= lim_inf (A1 (\) A2)
 proof let x; assume x in lim_inf A1 \ lim_inf A2;then
 A2: x in lim_inf A1 & not x in lim_inf A2 by XBOOLE_0:def 4;then
 A3: not x in lim_sup A2 by A1,KURATO_2:def 7;
     consider n0 being Element of NAT such that
 A4: for k holds x in A1.(n0+k) by A2,KURATO_2:7;
     consider n1 being Element of NAT such that
 A5: for k holds not x in A2.(n1+k) by A3,KURATO_2:8;
     now let k;
     A6: x in A1.(n0+(n1+k)) by A4;
         not x in A2.(n1+(n0+k)) by A5;then
         x in A1.((n0+n1)+k) \ A2.((n0+n1)+k) by A6,XBOOLE_0:def 4;
      hence x in (A1 (\) A2).((n0+n1)+k) by Def3;
     end;
   hence x in lim_inf (A1 (\) A2) by KURATO_2:7;
 end;
end;

theorem Th65:
  A1 is convergent or A2 is convergent implies
    lim_inf (A1 (\+\) A2) c= lim_inf A1 \+\ lim_inf A2
proof assume
A1: A1 is convergent or A2 is convergent;
A2: for A1,A2 st A1 is convergent holds
 lim_inf (A1 (\+\) A2) c= lim_inf A1 \+\ lim_inf A2
 proof let A1,A2;assume
  A3: A1 is convergent;
  thus lim_inf (A1 (\+\) A2) c= lim_inf A1 \+\ lim_inf A2
   proof let x; assume x in lim_inf (A1 (\+\) A2);then
     consider n1 being Element of NAT such that
   A4: for k holds x in (A1 (\+\) A2).(n1+k) by KURATO_2:7;
   A5: now let k; x in (A1 (\+\) A2).(n1+k) by A4;then
       x in A1.(n1+k) \+\ A2.(n1+k) by Def4;
      hence (x in A1.(n1+k) & not x in A2.(n1+k)) or
       (not x in A1.(n1+k) & x in A2.(n1+k)) by XBOOLE_0:1;
     end;
     assume
     A6: not x in lim_inf A1 \+\ lim_inf A2;
      per cases by A6,XBOOLE_0:1;
       suppose A7: not x in lim_inf A1 & not x in lim_inf A2;then
          not x in lim_sup A1 by A3,KURATO_2:def 7;then
          consider n2 being Element of NAT such that
      A8: for k holds not x in A1.(n2+k) by KURATO_2:8;
          consider k1 being Element of NAT such that
      A9: not x in A2.((n1+n2)+k1) by A7,KURATO_2:7;
      A10: not x in A1.(n2+(n1+k1)) & not x in A2.((n1+n2)+k1) by A8,A9;
         (x in A1.(n1+(n2+k1)) & not x in A2.(n1+(n2+k1))) or
         (not x in A1.(n1+(n2+k1)) & x in A2.(n1+(n2+k1))) by A5;
        hence contradiction by A10;
       end;
       suppose A11: x in lim_inf A1 & x in lim_inf A2;then
           consider n2 being Element of NAT such that
       A12: for k holds x in A1.(n2+k) by KURATO_2:7;
           consider n3 being Element of NAT such that
       A13: for k holds x in A2.(n3+k) by A11,KURATO_2:7;
       A14: x in A1.(n2+(n1+n3)) & x in A2.(n3+(n1+n2)) by A12,A13;
         (x in A1.(n1+(n2+n3)) & not x in A2.(n1+(n2+n3))) or
         (not x in A1.(n1+(n2+n3)) & x in A2.(n1+(n2+n3))) by A5;
        hence contradiction by A14;
     end;
    end;
  end;
 per cases by A1;
  suppose A1 is convergent;
   hence thesis by A2;
  end;
  suppose A2 is convergent;
   hence thesis by A2;
  end;
end;

theorem Th66:
  A1 is convergent & A2 is convergent implies
   lim_inf (A1 (\+\) A2) = lim_inf A1 \+\ lim_inf A2
proof assume
A1: A1 is convergent & A2 is convergent;
 thus lim_inf (A1 (\+\) A2) c= lim_inf A1 \+\ lim_inf A2 by A1,Th65;
 thus lim_inf A1 \+\ lim_inf A2 c= lim_inf (A1 (\+\) A2)
  proof let x;assume
  A2: x in lim_inf A1 \+\ lim_inf A2;
      per cases by A2,XBOOLE_0:1;
       suppose A3: x in lim_inf A1 & not x in lim_inf A2;then
        consider n1 being Element of NAT such that
       A4: for k holds x in A1.(n1+k) by KURATO_2:7;
         not x in lim_sup A2 by A3,A1,KURATO_2:def 7;then
         consider n2 being Element of NAT such that
       A5: for k holds not x in A2.(n2+k) by KURATO_2:8;
         now let k;
          x in A1.(n1+(n2+k)) & not x in A2.(n2+(n1+k)) by A4,A5;then
          x in A1.(n1+n2+k) \+\ A2.(n1+n2+k) by XBOOLE_0:1;
          hence x in (A1 (\+\) A2). (n1+n2+k) by Def4;
         end;
        hence x in lim_inf (A1 (\+\) A2) by KURATO_2:7;
       end;
       suppose A6: not x in lim_inf A1 & x in lim_inf A2;then
         not x in lim_sup A1 by A1,KURATO_2:def 7;then
         consider n3 being Element of NAT such that
       A7: for k holds not x in A1.(n3+k) by KURATO_2:8;
         consider n4 being Element of NAT such that
       A8: for k holds x in A2.(n4+k) by A6,KURATO_2:7;
         now let k;
          not x in A1.(n3+(n4+k)) & x in A2.(n4+(n3+k)) by A7,A8;then
          x in A1.(n3+n4+k) \+\ A2.(n3+n4+k) by XBOOLE_0:1;
          hence x in (A1 (\+\) A2). (n3+n4+k) by Def4;
         end;
        hence x in lim_inf (A1 (\+\) A2) by KURATO_2:7;
       end;
  end;
end;

theorem Th67:
  lim_sup (A1 (/\) A2) c= lim_sup A1 /\ lim_sup A2
proof
 (A1 (/\) A2).n = A1.n /\ A2.n by Def1;
 hence thesis by KURATO_2:16;
end;

theorem Th68:
  lim_sup (A1 (\/) A2) = lim_sup A1 \/ lim_sup A2
proof
 (A1 (\/) A2).n = A1.n \/ A2.n by Def2;
 hence thesis by KURATO_2:14;
end;

theorem Th69:
  lim_sup A1 \ lim_sup A2 c= lim_sup (A1 (\) A2)
proof let x; assume x in lim_sup A1 \ lim_sup A2;then
A1: x in lim_sup A1 & not x in lim_sup A2 by XBOOLE_0:def 4;
    consider n1 being Element of NAT such that
A2: for k holds not x in A2.(n1+k) by A1,KURATO_2:8;
     assume not x in lim_sup (A1 (\) A2);then
         consider n2 being Element of NAT such that
     A3: for k holds not x in (A1 (\) A2).(n2+k) by KURATO_2:8;
     A4: now let k;
          not x in (A1 (\) A2).(n2+k) by A3;then
          not x in A1.(n2+k) \ A2.(n2+k) by Def3;
          hence not x in A1.(n2+k) or x in A2.(n2+k) by XBOOLE_0:def 4;
         end;
         consider k1 being Element of NAT such that
     A5: x in A1.(n1+n2+k1) by A1,KURATO_2:8;
         x in A1.(n1+n2+k1) & not x in A2.(n1+(n2+k1)) by A2,A5;then
         x in A1.(n2+(k1+n1)) & not x in A2.(n2+(k1+n1));
      hence contradiction by A4;
end;

theorem
  lim_sup A1 \+\ lim_sup A2 c= lim_sup (A1 (\+\) A2)
proof let x;assume
A1: x in lim_sup A1 \+\ lim_sup A2;
A2:  for A1,A2 st x in lim_sup A1 & not x in lim_sup A2 holds
      x in lim_sup (A1 (\+\) A2)
     proof let A1,A2; assume
      A3: x in lim_sup A1 & not x in lim_sup A2;
          consider n1 being Element of NAT such that
      A4: for k holds not x in A2.(n1+k) by A3,KURATO_2:8;
        now let n;
         consider k1 being Element of NAT such that
        A5: x in A1.((n+n1)+k1) by A3,KURATO_2:8;
            not x in A2.(n1+(n+k1)) by A4;then
            x in A1.(n+(n1+k1)) \+\ A2.(n+(n1+k1)) by A5,XBOOLE_0:1;then
            x in (A1 (\+\) A2).(n+(n1+k1)) by Def4;
         hence ex k st x in (A1 (\+\) A2).(n+k);
        end;
      hence thesis by KURATO_2:8;
     end;
 per cases by A1,XBOOLE_0:1;
  suppose x in lim_sup A1 & not x in lim_sup A2;
   hence x in lim_sup (A1 (\+\) A2) by A2;
  end;
  suppose not x in lim_sup A1 & x in lim_sup A2;
   hence x in lim_sup (A1 (\+\) A2) by A2;
  end;
end;

theorem Th71:
  A1 is convergent or A2 is convergent implies
    lim_sup (A1 (/\) A2) = lim_sup A1 /\ lim_sup A2
proof assume
A1: A1 is convergent or A2 is convergent;
A2: for A1,A2 st A1 is convergent holds
 lim_sup (A1 (/\) A2) = lim_sup A1 /\ lim_sup A2
 proof let A1,A2;assume
 A3: A1 is convergent;
  thus lim_sup (A1 (/\) A2) c= lim_sup A1 /\ lim_sup A2 by Th67;
  thus lim_sup A1 /\ lim_sup A2 c= lim_sup (A1 (/\) A2)
   proof let x;assume x in lim_sup A1 /\ lim_sup A2;then
   A4: x in lim_sup A1 & x in lim_sup A2 by XBOOLE_0:def 3;then
       x in lim_inf A1 by A3,KURATO_2:def 7;then
    consider n1 being Element of NAT such that
   A5: for k holds x in A1.(n1+k) by KURATO_2:7;
    now let n;consider k1 being Element of NAT such that
    A6: x in A2.((n+n1)+k1) by A4,KURATO_2:8;
        x in A1.(n1+(n+k1)) by A5;then
        x in A1.(n+(n1+k1)) /\ A2.(n+(n1+k1)) by A6,XBOOLE_0:def 3;then
        x in (A1 (/\) A2).(n+(n1+k1)) by Def1;
     hence ex k st x in (A1 (/\) A2).(n+k);
     end;
   hence x in lim_sup (A1 (/\) A2) by KURATO_2:8;
   end;
 end;
 per cases by A1;
  suppose A1 is convergent;
   hence thesis by A2;
  end;
  suppose A2 is convergent;
   hence thesis by A2;
  end;
end;

theorem Th72:
  A2 is convergent implies
   lim_sup (A1 (\) A2) = lim_sup A1 \ lim_sup A2
proof assume
A1: A2 is convergent;
  thus lim_sup (A1 (\) A2) c= lim_sup A1 \ lim_sup A2
  proof let x; assume
  A2: x in lim_sup (A1 (\) A2);
  A3: now let n; consider k1 being Element of NAT such that
   A4: x in (A1 (\) A2).(n+k1) by A2,KURATO_2:8;
       x in A1.(n+k1) \ A2.(n+k1) by A4,Def3;then
       x in A1.(n+k1) & not x in A2.(n+k1) by XBOOLE_0:def 4;
    hence ex k st x in A1.(n+k) & not x in A2.(n+k);
   end;
   assume not x in lim_sup A1 \ lim_sup A2;then
   A5: not (x in lim_sup A1 & not x in lim_sup A2) by XBOOLE_0:def 4;
     per cases by A5,A1,KURATO_2:def 7;
      suppose not x in lim_sup A1;then
          consider n1 being Element of NAT such that
      A6: for k holds not x in A1.(n1+k) by KURATO_2:8;
          consider k2 being Element of NAT such that
      A7: x in A1.(n1+k2) & not x in A2.(n1+k2) by A3;
       thus contradiction by A6,A7;
      end;
      suppose x in lim_inf A2;then
          consider n2 being Element of NAT such that
      A8: for k holds x in A2.(n2+k) by KURATO_2:7;
          consider k3 being Element of NAT such that
      A9: x in A1.(n2+k3) & not x in A2.(n2+k3) by A3;
       thus contradiction by A8,A9;
    end;
  end;
  thus thesis by Th69;
end;

theorem Th73:
  A1 is convergent & A2 is convergent implies
   lim_sup (A1 (\+\) A2) = lim_sup A1 \+\ lim_sup A2
proof assume
A1: A1 is convergent & A2 is convergent;
thus lim_sup (A1 (\+\) A2)
 = lim_sup ((A1 (\) A2) (\/) (A2 (\) A1)) by Th3
.= lim_sup (A1 (\) A2) \/ lim_sup (A2 (\) A1) by Th68
.= (lim_sup A1 \ lim_sup A2) \/ lim_sup (A2 (\) A1) by A1,Th72
.= lim_sup A1 \+\ lim_sup A2 by A1,Th72;
end;

theorem Th74:
  lim_inf (A (/\) A1) = A /\ lim_inf A1
proof
 reconsider X1 = (inferior_setsequence(A1)) as SetSequence of X;
 reconsider X2 = (inferior_setsequence(A (/\) A1)) as SetSequence of X;
 X2 = A (/\) X1
 proof
  now let n;
  thus X2.n = A /\ X1.n by Th50
       .= (A (/\) X1).n by Def5;
  end;
 hence thesis by FUNCT_2:113;
 end;then
 Union X2 = A /\ Union X1 by Th38;then
 lim_inf (A (/\) A1) = A /\ Union X1 by SETLIM_1:def 4;
 hence thesis by SETLIM_1:def 4;
end;

theorem Th75:
  lim_inf (A (\/) A1) = A \/ lim_inf A1
proof
 reconsider X1 = (inferior_setsequence(A1)) as SetSequence of X;
 reconsider X2 = (inferior_setsequence(A (\/) A1)) as SetSequence of X;
 X2 = A (\/) X1
 proof
  now let n;
  thus X2.n = A \/ X1.n by Th51
       .= (A (\/) X1).n by Def6;
  end;
 hence thesis by FUNCT_2:113;
 end;then
 Union X2 = A \/ Union X1 by Th39;then
 lim_inf (A (\/) A1) = A \/ Union X1 by SETLIM_1:def 4;
 hence thesis by SETLIM_1:def 4;
end;

theorem Th76:
  lim_inf (A (\) A1) c= A \ lim_inf A1
proof let x; assume x in lim_inf (A (\) A1);then
    consider n such that
A1: for k holds x in (A (\) A1).(n+k) by KURATO_2:7;
A2: now let k;
     x in (A (\) A1).(n+k) by A1;then
     x in A \ A1.(n+k) by Def7;
    hence x in A & not x in A1.(n+k) by XBOOLE_0:def 4;
    end;
    not x in lim_inf A1
    proof assume x in lim_inf A1;then
      consider n1 being Element of NAT such that
  A3: for k holds x in A1.(n1+k) by KURATO_2:7;
      x in A1.(n1+n) by A3;
      hence contradiction by A2;
    end;
   hence x in A \ lim_inf A1 by A2,XBOOLE_0:def 4;
end;

theorem Th77:
  lim_inf (A1 (\) A) = lim_inf A1 \ A
proof
 reconsider X1 = (inferior_setsequence(A1)) as SetSequence of X;
 reconsider X2 = (inferior_setsequence(A1 (\) A)) as SetSequence of X;
 X2 = X1 (\) A
 proof
  now let n;
  thus X2.n = X1.n \ A by Th53
       .= (X1 (\) A).n by Def8;
  end;
 hence thesis by FUNCT_2:113;
 end;then
 Union X2 = Union X1 \ A by Th41;then
 lim_inf (A1 (\) A) = Union X1 \ A by SETLIM_1:def 4;
 hence thesis by SETLIM_1:def 4;
end;

theorem Th78:
  lim_inf (A (\+\) A1) c= A \+\ lim_inf A1
proof let x; assume x in lim_inf (A (\+\) A1);then
    consider n1 being Element of NAT such that
A1: for k holds x in (A (\+\) A1).(n1+k) by KURATO_2:7;
A2: now let k;
     x in (A (\+\) A1).(n1+k) by A1;then
     x in A \+\ A1.(n1+k) by Def9;
    hence (x in A & not x in A1.(n1+k)) or (not x in A & x in A1.(n1+k))
     by XBOOLE_0:1;
    end;
 assume not x in A \+\ lim_inf A1;then
 A3: (not x in A & not x in lim_inf A1) or
  (x in lim_inf A1 & x in A) by XBOOLE_0:1;
  per cases by A3,KURATO_2:7;
   suppose A4:  not x in A & not ex n st for k holds  x in A1.(n+k);then
    consider k1 being Element of NAT such that
   A5: not x in A1.(n1+k1);
   thus contradiction by A4,A5,A2;
   end;
   suppose A6: x in A & ex n st for k holds x in A1.(n+k);then
     consider n2 being Element of NAT such that
   A7: for k holds x in A1.(n2+k);
     x in A1.(n2+n1) by A7;
   hence contradiction by A6,A2;
   end;
end;

theorem Th79:
  A1 is convergent implies
   lim_inf (A (\) A1) = A \ lim_inf A1
proof assume
A1: A1 is convergent;
 thus lim_inf (A (\) A1) c= A \ lim_inf A1 by Th76;
 thus A \ lim_inf A1 c= lim_inf (A (\) A1)
 proof let x;assume
    A2: x in A \ lim_inf A1;then
         x in A \ lim_sup A1 by A1,KURATO_2:def 7;then
         not x in lim_sup A1 by XBOOLE_0:def 4;then
         consider n1 being Element of NAT such that
    A3: for k holds not x in A1.(n1+k) by KURATO_2:8;
    assume
A4: not x in lim_inf (A (\) A1);
A5: for n holds not x in A or ex k st x in A1.(n+k)
    proof let n;
      consider k such that
  A6: not x in (A (\) A1).(n+k) by A4,KURATO_2:7;
      not x in A \ A1.(n+k) by A6,Def7;then
      not ( x in A & not x in A1.(n+k)) by XBOOLE_0:def 4;
      hence thesis;
    end;
    per cases by A5;
    suppose not x in A;
      hence contradiction by A2,XBOOLE_0:def 4;
    end;
    suppose ex k st x in A1.(n1+k);then
      consider k such that
  A7: x in A1.(n1+k);
      thus contradiction by A7,A3;
    end;
  end;
end;

theorem Th80:
  A1 is convergent implies
   lim_inf (A (\+\) A1) = A \+\ lim_inf A1
proof assume
A1: A1 is convergent;
  thus lim_inf (A (\+\) A1) c= A \+\ lim_inf A1 by Th78;
  thus A \+\ lim_inf A1 c= lim_inf (A (\+\) A1)
  proof let x; assume
  A2: x in A \+\ lim_inf A1;
   per cases by A2,XBOOLE_0:1;
     suppose A3: x in A & not x in lim_inf A1;then
         not x in lim_sup A1 by A1,KURATO_2:def 7;then
         consider n1 being Element of NAT such that
     A4: for k holds not x in A1.(n1+k) by KURATO_2:8;
        now let k; x in A & not x in A1.(n1+k) by A3,A4;then
         x in A \+\ A1.(n1+k) by XBOOLE_0:1;
        hence x in (A (\+\) A1).(n1+k) by Def9;
        end;
      hence x in lim_inf (A (\+\) A1) by KURATO_2:7;
     end;
     suppose A5: not x in A & x in lim_inf A1;then
       consider n2 being Element of NAT such that
     A6: for k holds x in A1.(n2+k) by KURATO_2:7;
       now let k; not x in A & x in A1.(n2+k) by A5,A6;then
         x in A \+\ A1.(n2+k) by XBOOLE_0:1;
        hence x in (A (\+\) A1).(n2+k) by Def9;
        end;
      hence x in lim_inf (A (\+\) A1) by KURATO_2:7;
     end;
  end;
end;

theorem Th81:
  lim_sup (A (/\) A1) = A /\ lim_sup A1
proof
 reconsider X1 = (superior_setsequence(A1)) as SetSequence of X;
 reconsider X2 = (superior_setsequence(A (/\) A1)) as SetSequence of X;
 X2 = A (/\) X1
 proof
  now let n;
  thus X2.n = A /\ X1.n by Th55
       .= (A (/\) X1).n by Def5;
  end;
 hence thesis by FUNCT_2:113;
 end;then
 Intersection X2 = A /\ Intersection X1 by Th33;then
 lim_sup (A (/\) A1) = A /\ Intersection X1 by SETLIM_1:def 5;
 hence thesis by SETLIM_1:def 5;
end;

theorem Th82:
  lim_sup (A (\/) A1) = A \/ lim_sup A1
proof
 reconsider X1 = (superior_setsequence(A1)) as SetSequence of X;
 reconsider X2 = (superior_setsequence(A (\/) A1)) as SetSequence of X;
 X2 = A (\/) X1
 proof
  now let n;
  thus X2.n = A \/ X1.n by Th56
       .= (A (\/) X1).n by Def6;
  end;
 hence thesis by FUNCT_2:113;
 end;then
 Intersection X2 = A \/ Intersection X1 by Th34;then
 lim_sup (A (\/) A1) = A \/ Intersection X1 by SETLIM_1:def 5;
 hence thesis by SETLIM_1:def 5;
end;

theorem Th83:
  A \ lim_sup A1 c= lim_sup (A (\) A1)
proof let x; assume
A1: x in A \ lim_sup A1;then
    not x in lim_sup A1 by XBOOLE_0:def 4;then
    consider n1 being Element of NAT such that
A2: for k holds not x in A1.(n1+k) by KURATO_2:8;
    assume not x in lim_sup (A (\) A1);then
    consider n such that
A3: for k holds not x in (A (\) A1).(n+k) by KURATO_2:8;
A4: now let k;
      not x in (A (\) A1).(n+k) by A3;then
      not x in A \ A1.(n+k) by Def7;
      hence not x in A or x in A1.(n+k) by XBOOLE_0:def 4;
    end;
    per cases by A4;
    suppose not x in A;
      hence contradiction by A1,XBOOLE_0:def 4;
    end;
    suppose A5: x in A1.(n+k);
      not x in A1.(n1+n) by A2;
      hence contradiction by A5;
    end;
end;

theorem Th84:
  lim_sup (A1 (\) A) = lim_sup A1 \ A
proof
  reconsider X1 = (superior_setsequence(A1)) as SetSequence of X;
  reconsider X2 = (superior_setsequence(A1 (\) A)) as SetSequence of X;
  X2 = X1 (\) A
  proof
    now let n;
      thus X2.n = X1.n \ A by Th58
       .= (X1 (\) A).n by Def8;
    end;
    hence thesis by FUNCT_2:113;
  end;then
  Intersection X2 = Intersection X1 \ A by Th36;then
  lim_sup (A1 (\) A) = Intersection X1 \ A by SETLIM_1:def 5;
  hence thesis by SETLIM_1:def 5;
end;

theorem Th85:
  A \+\ lim_sup A1 c= lim_sup (A (\+\) A1)
proof
  (A (\+\) A1).n = A \+\ A1.n by Def9;
  hence thesis by KURATO_2:20;
end;

theorem Th86:
  A1 is convergent implies
    lim_sup (A (\) A1) = A \ lim_sup A1
proof assume
A1: A1 is convergent;
    thus lim_sup (A (\) A1) c= A \ lim_sup A1
    proof
    let x;assume
A2: x in lim_sup (A (\) A1);
A3: for n ex k st x in A & not x in A1.(n+k)
     proof let n;consider k such that
     A4: x in (A (\) A1).(n+k) by A2,KURATO_2:8;
        x in A \ A1.(n+k) by A4,Def7;then
        x in A & not x in A1.(n+k) by XBOOLE_0:def 4;
      hence thesis;
     end;
A5:  x in A
     proof assume not x in A;then
     A6: for n holds not x in A;
      for n holds ex k st x in A
      proof let n; consider k such that
       A7: x in A & not x in A1.(n+k) by A3;
       thus thesis by A7;
      end;
      hence contradiction by A6;
     end;
     for n holds ex k st not x in A1.(n+k)
     proof let n; consider k such that
     A8: x in A & not x in A1.(n+k) by A3;
       thus thesis by A8;
     end;then
     not x in lim_inf A1 by KURATO_2:7;then
     not x in lim_sup A1 by A1,KURATO_2:def 7;
   hence x in A \ lim_sup A1 by A5,XBOOLE_0:def 4;
 end;
 thus thesis by Th83;
end;

theorem Th87:
  A1 is convergent implies
    lim_sup (A (\+\) A1) = A \+\ lim_sup A1
proof assume
A1: A1 is convergent;
  thus lim_sup (A (\+\) A1) c= A \+\ lim_sup A1
   proof let x; assume
   A2: x in lim_sup (A (\+\) A1);
   A3: now let n; consider k1 being Element of NAT such that
     A4: x in (A (\+\) A1).(n+k1) by A2,KURATO_2:8;
         x in A \+\ A1.(n+k1) by A4,Def9;then
         (x in A & not x in A1.(n+k1)) or
         (not x in A & x in A1.(n+k1)) by XBOOLE_0:1;
        hence ex k st (x in A & not x in A1.(n+k)) or
           (not x in A & x in A1.(n+k));
       end;
     assume
     A5: not x in A \+\ lim_sup A1;
       per cases by A5,XBOOLE_0:1;
        suppose A6: not x in A & not x in lim_sup A1;then
            consider n1 being Element of NAT such that
        A7: for k holds not x in A1.(n1+k) by KURATO_2:8;
            consider k1 being Element of NAT such that
        A8: (x in A & not x in A1.(n1+k1)) or
            (not x in A & x in A1.(n1+k1)) by A3;
         thus contradiction by A7,A8,A6;
        end;
        suppose A9: x in A & x in lim_sup A1;then
            x in lim_inf A1 by A1,KURATO_2:def 7;then
            consider n2 being Element of NAT such that
        A10: for k holds x in A1.(n2+k) by KURATO_2:7;
            consider k2 being Element of NAT such that
        A11: (x in A & not x in A1.(n2+k2)) or
            (not x in A & x in A1.(n2+k2)) by A3;
         thus contradiction by A10,A11,A9;
        end;
   end;
  thus thesis by Th85;
end;

theorem
  A1 is convergent & A2 is convergent implies
    (A1 (/\) A2) is convergent & lim (A1 (/\) A2) = lim A1 /\ lim A2
proof assume
A1: A1 is convergent & A2 is convergent;
A2: lim_inf (A1 (/\) A2) = lim_inf A1 /\ lim_inf A2 by Th60
                        .= lim A1 /\ lim_inf A2 by A1,SETLIM_1:def 6
                        .= lim A1 /\ lim A2 by A1,SETLIM_1:def 6;
    lim_sup (A1 (/\) A2) = lim_sup A1 /\ lim_sup A2 by A1,Th71
                        .= lim A1 /\ lim_sup A2 by A1,SETLIM_1:def 6
                        .= lim A1 /\ lim A2 by A1,SETLIM_1:def 6;then
    (A1 (/\) A2) is convergent by A2,KURATO_2:def 7;
 hence thesis by A2,SETLIM_1:def 6;
end;

theorem
  A1 is convergent & A2 is convergent implies
    (A1 (\/) A2) is convergent & lim (A1 (\/) A2) = lim A1 \/ lim A2
proof assume
A1: A1 is convergent & A2 is convergent;
A2: lim_inf (A1 (\/) A2) = lim_inf A1 \/ lim_inf A2 by A1,Th63
                        .= lim A1 \/ lim_inf A2 by A1,SETLIM_1:def 6
                        .= lim A1 \/ lim A2 by A1,SETLIM_1:def 6;
    lim_sup (A1 (\/) A2) = lim_sup A1 \/ lim_sup A2 by Th68
                        .= lim A1 \/ lim_sup A2 by A1,SETLIM_1:def 6
                        .= lim A1 \/ lim A2 by A1,SETLIM_1:def 6;then
    (A1 (\/) A2) is convergent by A2,KURATO_2:def 7;
 hence thesis by A2,SETLIM_1:def 6;
end;

theorem
  A1 is convergent & A2 is convergent implies
   (A1 (\) A2) is convergent & lim (A1 (\) A2) = lim A1 \ lim A2
proof assume
A1: A1 is convergent & A2 is convergent;
A2: lim_inf (A1 (\) A2) = lim_inf A1 \ lim_inf A2 by A1,Th64
                       .= lim A1 \ lim_inf A2 by A1,SETLIM_1:def 6
                       .= lim A1 \ lim A2 by A1,SETLIM_1:def 6;
    lim_sup (A1 (\) A2) = lim_sup A1 \ lim_sup A2 by A1,Th72
                       .= lim A1 \ lim_sup A2 by A1,SETLIM_1:def 6
                       .= lim A1 \ lim A2 by A1,SETLIM_1:def 6;then
    (A1 (\) A2) is convergent by A2,KURATO_2:def 7;
 hence thesis by A2,SETLIM_1:def 6;
end;

theorem
  A1 is convergent & A2 is convergent implies
   (A1 (\+\) A2) is convergent & lim (A1 (\+\) A2) = lim A1 \+\ lim A2
proof assume
A1: A1 is convergent & A2 is convergent;
A2: lim_inf (A1 (\+\) A2) = lim_inf A1 \+\ lim_inf A2 by A1,Th66
                         .= lim A1 \+\ lim_inf A2 by A1,SETLIM_1:def 6
                         .= lim A1 \+\ lim A2 by A1,SETLIM_1:def 6;
    lim_sup (A1 (\+\) A2) = lim_sup A1 \+\ lim_sup A2 by A1,Th73
                         .= lim A1 \+\ lim_sup A2 by A1,SETLIM_1:def 6
                         .= lim A1 \+\ lim A2 by A1,SETLIM_1:def 6;then
    (A1 (\+\) A2) is convergent by A2,KURATO_2:def 7;
 hence thesis by A2,SETLIM_1:def 6;
end;

theorem
  A1 is convergent implies (A (/\) A1) is convergent &
   lim (A (/\) A1) = A /\ lim A1
proof assume
A1: A1 is convergent;
A2: lim_inf (A (/\) A1) = A /\ lim_inf A1 by Th74
                       .= A /\ lim A1 by A1,SETLIM_1:def 6;
    lim_sup (A (/\) A1) = A /\ lim_sup A1 by Th81
                       .= lim_inf (A (/\) A1) by A2,A1,SETLIM_1:def 6;then
    (A (/\) A1) is convergent by KURATO_2:def 7;
  hence thesis by A2,SETLIM_1:def 6;
end;

theorem
  A1 is convergent implies (A (\/) A1) is convergent &
   lim (A (\/) A1) = A \/ lim A1
proof assume
A1: A1 is convergent;
A2: lim_inf (A (\/) A1) = A \/ lim_inf A1 by Th75
                       .= A \/ lim A1 by A1,SETLIM_1:def 6;
    lim_sup (A (\/) A1) = A \/ lim_sup A1 by Th82
                       .= lim_inf (A (\/) A1) by A2,A1,SETLIM_1:def 6;then
  (A (\/) A1) is convergent by KURATO_2:def 7;
  hence thesis by A2,SETLIM_1:def 6;
end;

theorem
  A1 is convergent implies (A (\) A1) is convergent &
   lim (A (\) A1) = A \ lim A1
proof assume
A1: A1 is convergent;
A2: lim_inf (A (\) A1) = A \ lim_inf A1 by A1,Th79
                      .= A \ lim A1 by A1,SETLIM_1:def 6;
    lim_sup (A (\) A1) = A \ lim_sup A1 by A1,Th86
                      .= lim_inf (A (\) A1) by A2,A1,SETLIM_1:def 6;then
  (A (\) A1) is convergent by KURATO_2:def 7;
  hence thesis by A2,SETLIM_1:def 6;
end;

theorem
  A1 is convergent implies (A1 (\) A) is convergent &
   lim (A1 (\) A) = lim A1 \ A
proof assume
A1: A1 is convergent;
A2: lim_inf (A1 (\) A) = lim_inf A1 \ A by Th77
                      .= lim A1 \ A by A1,SETLIM_1:def 6;
    lim_sup (A1 (\) A) = lim_sup A1 \ A by Th84
                      .= lim_inf (A1 (\) A) by A2,A1,SETLIM_1:def 6;then
  (A1 (\) A) is convergent by KURATO_2:def 7;
  hence thesis by A2,SETLIM_1:def 6;
end;

theorem
  A1 is convergent implies (A (\+\) A1) is convergent &
   lim (A (\+\) A1) = A \+\ lim A1
proof assume
A1: A1 is convergent;then
A2: lim_inf (A (\+\) A1) = A \+\ lim_inf A1 by Th80
                        .= A \+\ lim A1 by A1,SETLIM_1:def 6;
    lim_sup (A (\+\) A1) = A \+\ lim_sup A1 by A1,Th87
                        .= lim_inf (A (\+\) A1) by A2,A1,SETLIM_1:def 6;then
   (A (\+\) A1) is convergent by KURATO_2:def 7;
   hence thesis by A2,SETLIM_1:def 6;
end;
