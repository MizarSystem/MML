:: Some Equations Related to the Limit of Sequence of Subsets
::  by Bo Zhang , Hiroshi Yamazaki and Yatsuka Nakamura
:: 
:: Received May 24, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies RELAT_1, SEQ_2, FUNCT_1, ORDINAL2, SETLIM_1, BOOLE, SETLIM_2,
      SETFAM_1, TARSKI, SEQM_3, PROB_1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, RELAT_1, FUNCT_1, ORDINAL1,
      NUMBERS, XREAL_0, NAT_1, SETFAM_1, ORDINAL2, KURATO_2, PROB_1, SETLIM_1;
 constructors NAT_1, XBOOLE_0, SETFAM_1, FUNCT_1, PROB_1, KURATO_2, SEQM_3,
      SETLIM_1;
 registrations SUBSET_1, XREAL_0, RELSET_1, ARYTM_3, ZFMISC_1, PROB_1,
      XBOOLE_0, ORDINAL2, SETFAM_1, FUNCT_1;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, XBOOLE_0;
 theorems FUNCT_2, XBOOLE_0, XBOOLE_1, KURATO_2, SETLIM_1, CARD_3, PROB_1;
 schemes KURATO_2;

begin

 reserve n,m,k for Nat,
         x,y,z,X for set,
         A for Subset of X,
         A1,A2,A3 for SetSequence of X;

theorem Th1:
  (inferior_setsequence(A1)).n = Intersection (A1 ^\n)
proof reconsider Y = {A1.k: n <= k} as Subset of bool X by SETLIM_1:33;
  (inferior_setsequence(A1)).n = meet Y by SETLIM_1:def 2
                                .= meet rng (A1 ^\ n) by SETLIM_1:10;
  hence thesis by SETLIM_1:13;
end;

theorem Th2:
  (superior_setsequence(A1)).n = Union (A1 ^\n)
proof reconsider Y = {A1.k: n <= k} as Subset of bool X by SETLIM_1:33;
  (superior_setsequence(A1)).n = union Y by SETLIM_1:def 3
                                .= union rng (A1 ^\ n) by SETLIM_1:10;
  hence thesis by CARD_3:def 4;
end;

definition let X;
           let A1,A2 be SetSequence of X;
  func A1 (/\) A2 -> SetSequence of X means :def1:
    for n holds it.n = A1.n /\ A2.n;
  existence
  proof
    deffunc F(Nat) = A1.$1 /\ A2.$1;
    consider f being SetSequence of X such that
A1: for n holds f.n = F(n) from KURATO_2:sch 1;
    thus thesis by A1;
  end;
  uniqueness
  proof
  let A3 be SetSequence of X,A4 be SetSequence of X such that
B1: for n holds A3.n = A1.n /\ A2.n and
B2: for n holds A4.n = A1.n /\ A2.n;
   now let n;
     thus A3.n = A1.n /\ A2.n by B1 .= A4.n by B2;
    end;
    hence A3 = A4 by FUNCT_2:113;
  end;
  commutativity;
  func A1 (\/) A2 -> SetSequence of X means :def2:
  for n holds it.n = A1.n \/ A2.n;
  existence
  proof
    deffunc F(Nat) = A1.$1 \/ A2.$1;
    consider f being SetSequence of X such that
A1: for n holds f.n = F(n) from KURATO_2:sch 1;
    thus thesis by A1;
  end;
  uniqueness
  proof
  let A3 be SetSequence of X,A4 be SetSequence of X such that
B1: for n holds A3.n = A1.n \/ A2.n and
B2: for n holds A4.n = A1.n \/ A2.n;
   now let n;
     thus A3.n = A1.n \/ A2.n by B1 .= A4.n by B2;
    end;
    hence A3 = A4 by FUNCT_2:113;
  end;
  commutativity;
  func A1 (\) A2 -> SetSequence of X means :def3:
  for n holds it.n = A1.n \ A2.n;
  existence
  proof
    deffunc F(Nat) = A1.$1 \ A2.$1;
    consider f being SetSequence of X such that
A1: for n holds f.n = F(n) from KURATO_2:sch 1;
    thus thesis by A1;
  end;
  uniqueness
  proof
  let A3 be SetSequence of X,A4 be SetSequence of X such that
B1: for n holds A3.n = A1.n \ A2.n and
B2: for n holds A4.n = A1.n \ A2.n;
   now let n;
     thus A3.n = A1.n \ A2.n by B1 .= A4.n by B2;
    end;
    hence A3 = A4 by FUNCT_2:113;
  end;
  func A1 (\+\) A2 -> SetSequence of X means :def4:
  for n holds it.n = A1.n \+\ A2.n;
  existence
  proof
    deffunc F(Nat) = A1.$1 \+\ A2.$1;
    consider f being SetSequence of X such that
A1: for n holds f.n = F(n) from KURATO_2:sch 1;
    thus thesis by A1;
  end;
  uniqueness
  proof
  let A3 be SetSequence of X,A4 be SetSequence of X such that
B1: for n holds A3.n = A1.n \+\ A2.n and
B2: for n holds A4.n = A1.n \+\ A2.n;
   now let n;
     thus A3.n = A1.n \+\ A2.n by B1 .= A4.n by B2;
    end;
    hence A3 = A4 by FUNCT_2:113;
  end;
  commutativity;
end;

theorem Th3:
  A1 (\+\) A2 = (A1 (\) A2) (\/) (A2 (\) A1)
proof
 now let n;
 thus (A1 (\+\) A2).n = A1.n \+\ A2.n by def4
   .= (A1.n \ A2.n) \/ (A2.n \ A1.n) by XBOOLE_0:def 6
   .= (A1 (\) A2).n \/ (A2.n \ A1.n) by def3
   .= (A1 (\) A2).n \/ (A2 (\) A1).n by def3
   .= ((A1 (\) A2) (\/) (A2 (\) A1)).n by def2;
 end;
 hence thesis by FUNCT_2:113;
end;

theorem Th4:
  (A1 (/\) A2) ^\k = (A1 ^\k) (/\) (A2 ^\k)
proof
 now let n;
 thus ((A1 (/\) A2) ^\k).n = (A1 (/\) A2).(n+k) by KURATO_2:def 2
   .= A1.(n+k) /\ A2.(n+k) by def1
   .= (A1 ^\k).n /\ A2.(n+k) by KURATO_2:def 2
   .= (A1 ^\k).n /\ (A2 ^\k).n by KURATO_2:def 2
   .= ((A1 ^\k) (/\) (A2 ^\k)).n by def1;
 end;
 hence thesis by FUNCT_2:113;
end;

theorem Th5:
  (A1 (\/) A2) ^\k = (A1 ^\k) (\/) (A2 ^\k)
proof
 now let n;
 thus ((A1 (\/) A2) ^\k).n = (A1 (\/) A2).(n+k) by KURATO_2:def 2
   .= A1.(n+k) \/ A2.(n+k) by def2
   .= (A1 ^\k).n \/ A2.(n+k) by KURATO_2:def 2
   .= (A1 ^\k).n \/ (A2 ^\k).n by KURATO_2:def 2
   .= ((A1 ^\k) (\/) (A2 ^\k)).n by def2;
 end;
hence thesis by FUNCT_2:113;     
end;

theorem Th6:
  (A1 (\) A2) ^\k = (A1 ^\k) (\) (A2 ^\k)
proof
 now let n;
  thus ((A1 (\) A2) ^\k).n = (A1 (\) A2).(n+k) by KURATO_2:def 2
   .= A1.(n+k) \ A2.(n+k) by def3
   .= (A1 ^\k).n \ A2.(n+k) by KURATO_2:def 2
   .= (A1 ^\k).n \ (A2 ^\k).n by KURATO_2:def 2
   .= ((A1 ^\k) (\) (A2 ^\k)).n by def3;
 end;
 hence thesis by FUNCT_2:113;
end;

theorem Th7:
  (A1 (\+\) A2) ^\k = (A1 ^\k) (\+\) (A2 ^\k)
proof 
 now let n;
  thus ((A1 (\+\) A2) ^\k).n = (A1 (\+\) A2).(n+k) by KURATO_2:def 2
   .= A1.(n+k) \+\ A2.(n+k) by def4
   .= (A1 ^\k).n \+\ A2.(n+k) by KURATO_2:def 2
   .= (A1 ^\k).n \+\ (A2 ^\k).n by KURATO_2:def 2
   .= ((A1 ^\k) (\+\) (A2 ^\k)).n by def4;
 end;
 hence thesis by FUNCT_2:113;
end;

theorem Th8:
  Union (A1 (/\) A2) c= Union A1 /\ Union A2
proof
   let x;assume x in Union (A1 (/\) A2);then
   consider n such that
B1: x in (A1 (/\) A2).n by PROB_1:25;
   x in A1.n /\ A2.n by B1,def1;then
   x in A1.n & x in A2.n by XBOOLE_0:def 3;then
   x in Union A1 & x in Union A2 by PROB_1:25;
   hence thesis by XBOOLE_0:def 3;
end;
 
theorem Th9:
  Union (A1 (\/) A2) = Union A1 \/ Union A2
proof
  thus Union (A1 (\/) A2) c= Union A1 \/ Union A2
  proof let x;assume x in Union (A1 (\/) A2);then
    consider n1 being Nat such that
B2: x in (A1 (\/) A2).n1 by PROB_1:25;
B3: x in A1.n1 \/ A2.n1 by B2,def2;
    per cases by B3,XBOOLE_0:def 2;
     suppose x in A1.n1;then
      x in Union A1 by PROB_1:25;
      hence x in Union A1 \/ Union A2 by XBOOLE_0:def 2;
     end;
     suppose x in A2.n1;then
      x in Union A2 by PROB_1:25;
      hence x in Union A1 \/ Union A2 by XBOOLE_0:def 2;
     end;
   end;
   let x; assume
C1: x in Union A1 \/ Union A2;
  per cases by C1,XBOOLE_0:def 2;
   suppose x in Union A1;then
    consider n2 being Nat such that
    CD1: x in A1.n2 by PROB_1:25;
     x in A1.n2 \/ A2.n2 by CD1,XBOOLE_0:def 2;then
     x in (A1 (\/) A2).n2 by def2;
   hence x in Union (A1 (\/) A2) by PROB_1:25;
   end;
   suppose x in Union A2;then
    consider n3 being Nat such that
CE1: x in A2.n3 by PROB_1:25;
     x in A1.n3 \/ A2.n3 by CE1,XBOOLE_0:def 2;then
     x in (A1 (\/) A2).n3 by def2;
   hence x in Union (A1 (\/) A2) by PROB_1:25;
   end;
end;

theorem Th10:
  Union A1 \ Union A2 c= Union (A1 (\) A2)
proof
 let x;assume x in Union A1 \ Union A2;then
 B2: x in Union A1 & not x in Union A2 by XBOOLE_0:def 4;then
     consider n1 being Nat such that
 B3: x in A1.n1 by PROB_1:25;
     not x in A2.n1 by B2,PROB_1:25;then
     x in A1.n1 \ A2.n1 by B3,XBOOLE_0:def 4;then
     x in (A1 (\) A2).n1 by def3;
  hence x in Union (A1 (\) A2) by PROB_1:25;
end;

theorem Th11:
  Union A1 \+\ Union A2 c= Union (A1 (\+\) A2)
proof
A2:(Union A1 \ Union A2) c= Union (A1 (\) A2) by Th10;
   (Union A2 \ Union A1) c= Union (A2 (\) A1) by Th10;then
A3:(Union A1 \ Union A2) \/ (Union A2 \ Union A1)
    c= Union (A1 (\) A2) \/ Union (A2 (\) A1) by A2,XBOOLE_1:13;
   Union (A1 (\) A2) \/ Union (A2 (\) A1)
    = Union ((A1 (\) A2) (\/) (A2 (\) A1)) by Th9
   .= Union (A1 (\+\) A2) by Th3;
   hence thesis by XBOOLE_0:def 6,A3;
end;

theorem Th12:
  Intersection (A1 (/\) A2) = Intersection A1 /\ Intersection A2
proof
 thus Intersection (A1 (/\) A2) c= Intersection A1 /\ Intersection A2
 proof let x;assume
 B1: x in Intersection (A1 (/\) A2);
 B2: now let k;
      x in (A1 (/\) A2).k by B1,PROB_1:29;then
      x in (A1.k /\ A2.k) by def1;
     hence x in A1.k & x in A2.k by XBOOLE_0:def 3;
     end;then
 B3: x in Intersection A1 by PROB_1:29;
     x in Intersection A2 by B2,PROB_1:29;
  hence x in Intersection A1 /\ Intersection A2 by B3,XBOOLE_0:def 3;
 end;
 let x;assume x in Intersection A1 /\ Intersection A2;then
 C1: x in Intersection A1 & x in Intersection A2 by XBOOLE_0:def 3;
     now let k;
     C11: x in A1.k by C1,PROB_1:29;
          x in A2.k by C1,PROB_1:29;then
      x in A1.k /\ A2.k by C11,XBOOLE_0:def 3;
     hence x in (A1 (/\) A2).k by def1;
   end;
  hence x in Intersection (A1 (/\) A2) by PROB_1:29;
end;

theorem Th13:
  Intersection A1 \/ Intersection A2 c= Intersection (A1 (\/) A2)
proof let x;assume
A1: x in Intersection A1 \/ Intersection A2;
  per cases by A1,XBOOLE_0:def 2;
   suppose AA1: x in Intersection A1;
    now let k;
     x in A1.k by AA1,PROB_1:29;then
     x in (A1.k \/ A2.k) by XBOOLE_0:def 2;
    hence x in (A1 (\/) A2).k by def2;
    end;
   hence thesis by PROB_1:29;
   end;
   suppose CC2: x in Intersection A2;
    now let k;
     x in A2.k by CC2,PROB_1:29;then
     x in A1.k \/ A2.k by XBOOLE_0:def 2;
    hence x in (A1 (\/) A2).k by def2;
    end;
   hence thesis by PROB_1:29;
  end;
end;

theorem Th14:
  Intersection (A1 (\) A2) c= Intersection A1 \ Intersection A2
proof let x;assume
B1: x in Intersection (A1 (\) A2);
B2: now let k;
      x in (A1 (\) A2).k by B1,PROB_1:29;then
      x in (A1.k \ A2.k) by def3;
      hence x in A1.k & not x in A2.k by XBOOLE_0:def 4;
    end;then
B3: x in Intersection A1 by PROB_1:29;
    not x in A2.0 by B2;then
    not x in Intersection A2 by PROB_1:29;
   hence thesis by B3,XBOOLE_0:def 4;
end;

definition let X;
           let A1 be SetSequence of X, A be Subset of X;
  func A (/\) A1 -> SetSequence of X means :def5:
  for n holds it.n = A /\ A1.n;
  existence
  proof
    deffunc F(Nat) = A /\ A1.$1;
    consider f being SetSequence of X such that
A1: for n holds f.n = F(n) from KURATO_2:sch 1;
    thus thesis by A1;
  end;
  uniqueness
  proof
  let A3 be SetSequence of X,A4 be SetSequence of X such that
B1: for n holds A3.n = A /\ A1.n and
B2: for n holds A4.n = A /\ A1.n;
   now let n;
     thus A3.n = A /\ A1.n by B1 .= A4.n by B2;
    end;
    hence A3 = A4 by FUNCT_2:113;
  end;
  func A (\/) A1 -> SetSequence of X means :def6:
  for n holds it.n = A \/ A1.n;
  existence
  proof
    deffunc F(Nat) = A \/ A1.$1;
    consider f being SetSequence of X such that
A1: for n holds f.n = F(n) from KURATO_2:sch 1;
    thus thesis by A1;
  end;
  uniqueness
  proof
  let A3 be SetSequence of X,A4 be SetSequence of X such that
B1: for n holds A3.n = A \/ A1.n and
B2: for n holds A4.n = A \/ A1.n;
   now let n;
    thus A3.n = A \/ A1.n by B1 .= A4.n by B2;
   end;
   hence A3 = A4 by FUNCT_2:113;
  end;
  func A (\) A1 -> SetSequence of X means :def7:
  for n holds it.n = A \ A1.n;
  existence
  proof
    deffunc F(Nat) = A \ A1.$1;
    consider f being SetSequence of X such that
A1: for n holds f.n = F(n) from KURATO_2:sch 1;
   thus thesis by A1;
  end;
  uniqueness
  proof
  let A3 be SetSequence of X,A4 be SetSequence of X such that
B1: for n holds A3.n = A \ A1.n and
B2: for n holds A4.n = A \ A1.n;
   now let n;
     thus A3.n = A \ A1.n by B1 .= A4.n by B2;
    end;
    hence A3 = A4 by FUNCT_2:113;
  end;
  func A1 (\) A -> SetSequence of X means :def8:
  for n holds it.n = A1.n \ A;
  existence
  proof
    deffunc F(Nat) = A1.$1 \ A;
    consider f being SetSequence of X such that
A1: for n holds f.n = F(n) from KURATO_2:sch 1;
    thus thesis by A1;
  end;
  uniqueness
  proof
  let A3 be SetSequence of X,A4 be SetSequence of X such that
B1: for n holds A3.n = A1.n \ A and
B2: for n holds A4.n = A1.n \ A;
   now let n;
     thus A3.n = A1.n \ A by B1 .= A4.n by B2;
    end;
    hence A3 = A4 by FUNCT_2:113;
  end;
  func A (\+\) A1 -> SetSequence of X means :def9:
  for n holds it.n = A \+\ A1.n;
  existence
  proof
    deffunc F(Nat) = A \+\ A1.$1;
    consider f being SetSequence of X such that
A1: for n holds f.n = F(n) from KURATO_2:sch 1;
    thus thesis by A1;
  end;
  uniqueness
  proof
  let A3 be SetSequence of X,A4 be SetSequence of X such that
B1: for n holds A3.n = A \+\ A1.n and
B2: for n holds A4.n = A \+\ A1.n;
   now let n;
     thus A3.n = A \+\ A1.n by B1 .= A4.n by B2;
    end;
    hence A3 = A4 by FUNCT_2:113;
  end;
end;

theorem
  A (\+\) A1 = (A (\) A1) (\/) (A1 (\) A)
proof
  now let n;
    thus (A (\+\) A1).n = A \+\ A1.n by def9
     .= (A \ A1.n) \/ (A1.n \ A) by XBOOLE_0:def 6
     .= (A (\) A1).n \/ (A1.n \ A) by def7
     .= (A (\) A1).n \/ (A1 (\) A).n by def8
     .= ((A (\) A1) (\/) (A1 (\) A)).n by def2;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th16:
  (A (/\) A1) ^\k = A (/\) (A1 ^\k)
proof
 now let n;
 thus ((A (/\) A1) ^\k).n = (A (/\) A1).(n+k) by KURATO_2:def 2
   .= A /\ A1.(n+k) by def5
   .= A /\ (A1 ^\k).n by KURATO_2:def 2
   .= (A (/\) (A1 ^\k)).n by def5;
 end;
hence thesis by FUNCT_2:113;     
end;

theorem Th17:
  (A (\/) A1) ^\k = A (\/) (A1 ^\k)
proof
 now let n;
 thus ((A (\/) A1) ^\k).n = (A (\/) A1).(n+k) by KURATO_2:def 2
   .= A \/ A1.(n+k) by def6
   .= A \/ (A1 ^\k).n by KURATO_2:def 2
   .= (A (\/) (A1 ^\k)).n by def6;
 end;
hence thesis by FUNCT_2:113;
end;

theorem Th18:
  (A (\) A1) ^\k = A (\) (A1 ^\k)
proof
 now let n;
 thus ((A (\) A1) ^\k).n = (A (\) A1).(n+k) by KURATO_2:def 2
   .= A \ A1.(n+k) by def7
   .= A \ (A1 ^\k).n by KURATO_2:def 2
   .= (A (\) (A1 ^\k)).n by def7;
 end;
hence thesis by FUNCT_2:113;     
end;

theorem Th19:
  (A1 (\) A) ^\k = (A1 ^\k) (\) A
proof
 now let n;
 thus ((A1 (\) A) ^\k).n = (A1 (\) A).(n+k) by KURATO_2:def 2
   .= A1.(n+k) \ A by def8
   .= (A1 ^\k).n \ A by KURATO_2:def 2
   .= ((A1 ^\k) (\) A).n by def8;
 end;
 hence thesis by FUNCT_2:113;
end;

theorem Th20:
  (A (\+\) A1) ^\k = A (\+\) (A1 ^\k)
proof 
 now let n;
 thus ((A (\+\) A1) ^\k).n = (A (\+\) A1).(n+k) by KURATO_2:def 2
   .= A \+\ A1.(n+k) by def9
   .= A \+\ (A1 ^\k).n by KURATO_2:def 2
   .= (A (\+\) (A1 ^\k)).n by def9;
 end;
 hence thesis by FUNCT_2:113;
end;

theorem Th21:
  A1 is non-increasing implies A (/\) A1 is non-increasing
proof assume
A0: A1 is non-increasing;
    for n,m st n <= m holds (A (/\) A1).m c= (A (/\) A1).n
    proof let n,m such that
    B1: n <= m;
        A1.m c= A1.n by B1,A0,PROB_1:def 6;then
        A /\ A1.m c= A /\ A1.n by XBOOLE_1:27;then
        (A (/\) A1).m c= A /\ A1.n by def5;
     hence thesis by def5;
    end;
 hence thesis by PROB_1:def 6;
end;
 
theorem Th22:
  A1 is non-decreasing implies A (/\) A1 is non-decreasing
proof assume
A0: A1 is non-decreasing;
    for n,m st n <= m holds (A (/\) A1).n c= (A (/\) A1).m
    proof let n,m such that
    B1: n <= m;
        A1.n c= A1.m by B1,A0,PROB_1:def 7;then
        A /\ A1.n c= A /\ A1.m by XBOOLE_1:27;then
        (A (/\) A1).n c= A /\ A1.m by def5;
     hence thesis by def5;
    end;
  hence thesis by PROB_1:def 7;
end;

theorem
  A1 is monotone implies A (/\) A1 is monotone
proof assume 
A0: A1 is monotone;
 per cases by A0, SETLIM_1:def 1;
  suppose A1 is non-increasing;then
    A (/\) A1 is non-increasing by Th21;
   hence A (/\) A1 is monotone by SETLIM_1:def 1;
  end;
  suppose A1 is non-decreasing;then
    A (/\) A1 is non-decreasing by Th22;
   hence A (/\) A1 is monotone by SETLIM_1:def 1;
  end;
end;

theorem Th24:
  A1 is non-increasing implies A (\/) A1 is non-increasing
proof assume
A0: A1 is non-increasing;
    for n,m st n <= m holds (A (\/) A1).m c= (A (\/) A1).n
    proof let n,m such that
    B1: n <= m;
        A1.m c= A1.n by B1,A0,PROB_1:def 6;then
        A \/ A1.m c= A \/ A1.n by XBOOLE_1:9;then
        (A (\/) A1).m c= A \/ A1.n by def6;
     hence thesis by def6;  
    end;
 hence thesis by PROB_1:def 6;
 end;
 
theorem Th25:
  A1 is non-decreasing implies A (\/) A1 is non-decreasing
proof assume
A0:A1 is non-decreasing;
   for n,m st n <= m holds (A (\/) A1).n c= (A (\/) A1).m
   proof let n,m such that
B1:  n <= m;
     A1.n c= A1.m by B1,A0,PROB_1:def 7;then
     A \/ A1.n c= A \/ A1.m by XBOOLE_1:9;then
     (A (\/) A1).n c= A \/ A1.m by def6;
     hence thesis by def6;  
   end;
   hence thesis by PROB_1:def 7;
end;

theorem
  A1 is monotone implies A (\/) A1 is monotone
proof assume
A0: A1 is monotone;
 per cases by A0, SETLIM_1:def 1;
  suppose A1 is non-increasing;then
   A (\/) A1 is non-increasing by Th24;
   hence A (\/) A1 is monotone by SETLIM_1:def 1;
  end;
  suppose A1 is non-decreasing;then
   A (\/) A1 is non-decreasing by Th25;
   hence A (\/) A1 is monotone by SETLIM_1:def 1;
  end;
end;

theorem Th27:
  A1 is non-increasing implies A (\) A1 is non-decreasing
proof assume
A0: A1 is non-increasing;
    for n,m st n <= m holds (A (\) A1).n c= (A (\) A1).m
    proof let n,m such that
  B1: n <= m;
      A1.m c= A1.n by B1,A0,PROB_1:def 6;then
      A \ A1.n c= A \ A1.m by XBOOLE_1:34;then
      (A (\) A1).n c= A \ A1.m by def7;
      hence thesis by def7;
    end;
  hence thesis by PROB_1:def 7;
end;

theorem Th28:
  A1 is non-decreasing implies A (\) A1 is non-increasing
proof assume 
B0: A1 is non-decreasing;
    for n,m st n <= m holds (A (\) A1).m c= (A (\) A1).n
    proof let n,m such that
  B1: n <= m;
      A1.n c= A1.m by B1,B0,PROB_1:def 7;then
      A \ A1.m c= A \ A1.n by XBOOLE_1:34;then
      (A (\) A1).m c= A \ A1.n by def7;
      hence thesis by def7;
    end;
   hence thesis by PROB_1:def 6;
end;

theorem
  A1 is monotone implies A (\) A1 is monotone
proof assume
A0: A1 is monotone;
  per cases by A0, SETLIM_1:def 1;
  suppose A1 is non-increasing;then
    A (\) A1 is non-decreasing by Th27;
    hence A (\) A1 is monotone by SETLIM_1:def 1;
  end;
  suppose A1 is non-decreasing;then
    A (\) A1 is non-increasing by Th28;
    hence A (\) A1 is monotone by SETLIM_1:def 1;
  end;
end;

theorem Th30:
  A1 is non-increasing implies A1 (\) A is non-increasing
proof assume
A0:A1 is non-increasing;
   for n,m st n <= m holds (A1 (\) A).m c= (A1 (\) A).n
   proof let n,m such that
B1:  n <= m;
     A1.m c= A1.n by B1,A0,PROB_1:def 6;then
     A1.m \ A c= A1.n \ A by XBOOLE_1:33;then
     (A1 (\) A).m c= A1.n \ A by def8;
     hence thesis by def8;
    end;
  hence thesis by PROB_1:def 6;
end;
 
theorem Th31:
  A1 is non-decreasing implies A1 (\) A is non-decreasing
proof assume
B0: A1 is non-decreasing;
    for n,m st n <= m holds (A1 (\) A).n c= (A1 (\) A).m
    proof let n,m such that
    B1: n <= m;
       A1.n c= A1.m by B1,B0,PROB_1:def 7;then
       A1.n \ A c= A1.m \ A by XBOOLE_1:33;then
       (A1 (\) A).n c= A1.m \ A by def8;
     hence thesis by def8;
    end;
  hence thesis by PROB_1:def 7;
end;

theorem
  A1 is monotone implies A1 (\) A is monotone
proof assume 
A0: A1 is monotone;
 per cases by A0, SETLIM_1:def 1;
  suppose A1 is non-increasing;then
   A1 (\) A is non-increasing by Th30;
   hence A1 (\) A is monotone by SETLIM_1:def 1;
  end;
  suppose A1 is non-decreasing;then
   A1 (\) A is non-decreasing by Th31;
   hence A1 (\) A is monotone by SETLIM_1:def 1;
  end;
end;

theorem Th33:
  Intersection (A (/\) A1) = A /\ Intersection A1
proof
  thus Intersection (A (/\) A1) c= A /\ Intersection A1
  proof let x;assume
B1: x in Intersection (A (/\) A1);
    now let k;
      x in (A (/\) A1).k by B1,PROB_1:29;then
      x in (A /\ A1.k) by def5;
      hence x in A & x in A1.k by XBOOLE_0:def 3;
    end;then
    x in A & x in Intersection A1 by PROB_1:29;
    hence x in A /\ Intersection A1 by XBOOLE_0:def 3;
  end;
  let x;assume x in A /\ Intersection A1;then
C1: x in A & x in Intersection A1 by XBOOLE_0:def 3;
  now let k;
    x in A & x in A1.k by C1,PROB_1:29;then
    x in A /\ A1.k by XBOOLE_0:def 3;
    hence x in (A (/\) A1).k by def5;
  end;
  hence thesis by PROB_1:29;
end;

theorem Th34:
  Intersection (A (\/) A1) = A \/ Intersection A1
proof
 thus Intersection (A (\/) A1) c= A \/ Intersection A1
 proof let x;assume
B1: x in Intersection (A (\/) A1);
B2: now let k;
     x in (A (\/) A1).k by B1,PROB_1:29;then
     x in (A \/ A1.k) by def6;
     hence x in A or x in A1.k by XBOOLE_0:def 2;
    end;
    per cases by B2;
    suppose x in A;
      hence x in A \/ Intersection A1 by XBOOLE_0:def 2;
    end;
    suppose x in A1.k;then
      x in Intersection A1 by PROB_1:29;
      hence x in A \/ Intersection A1 by XBOOLE_0:def 2;
    end;
 end;
 let x;assume
 C1: x in A \/ Intersection A1;
  per cases by C1,XBOOLE_0:def 2;
   suppose CC1: x in A;
    now let k;
     x in (A \/ A1.k) by CC1,XBOOLE_0:def 2;
    hence x in (A (\/) A1).k by def6;
    end;
   hence x in Intersection (A (\/) A1) by PROB_1:29;
   end;
   suppose CC2: x in Intersection A1;
    now let k;
     x in A1.k by CC2,PROB_1:29;then
     x in A \/ A1.k by XBOOLE_0:def 2;
    hence x in (A (\/) A1).k by def6;
    end;
   hence thesis by PROB_1:29;
  end;
end;

theorem Th35:
  Intersection (A (\) A1) c= A \ Intersection A1
proof
 let x;assume
 B1: x in Intersection (A (\) A1);
 B2: now let k;
     x in (A (\) A1).k by B1,PROB_1:29;then
     x in (A \ A1.k) by def7;
     hence x in A & not x in A1.k by XBOOLE_0:def 4;
   end;then
   not x in A1.0;then
   x in A & not x in Intersection A1 by B2,PROB_1:29;
  hence thesis by XBOOLE_0:def 4;
end;

theorem Th36:
  Intersection (A1 (\) A) = Intersection A1 \ A
proof
 thus Intersection (A1 (\) A) c= Intersection A1 \ A
 proof let x;assume
 B1: x in Intersection (A1 (\) A);
 now let k;
      x in (A1 (\) A).k by B1,PROB_1:29;then
      x in (A1.k \ A) by def8;
     hence x in A1.k & not x in A by XBOOLE_0:def 4;
     end;then
     x in Intersection A1 & not x in A by PROB_1:29;
  hence x in Intersection A1 \ A by XBOOLE_0:def 4;
 end;
 let x;assume x in Intersection A1 \ A;then
 C1: x in Intersection A1 & not x in A by XBOOLE_0:def 4;
   now let k;
     x in A1.k & not x in A by C1,PROB_1:29;then
     x in A1.k \ A by XBOOLE_0:def 4;
     hence x in (A1 (\) A).k by def8;
   end;
   hence thesis by PROB_1:29;
end;

theorem Th37:
  Intersection (A (\+\) A1) c= A \+\ Intersection A1
proof let x;assume 
A1: x in Intersection (A (\+\) A1);
A2: now let n; x in (A (\+\) A1).n by A1,PROB_1:29;then
     x in A \+\ A1.n by def9;
     hence x in A & not x in A1.n or not x in A & x in A1.n by XBOOLE_0:1;
    end;
 assume not x in A \+\ Intersection A1;then
 B1: (not x in A & not x in Intersection A1) or
  (x in Intersection A1 & x in A) by XBOOLE_0:1;
  per cases by B1,PROB_1:29;
   suppose not x in A & not for n holds x in A1.n;
   hence contradiction by A2;
   end;
   suppose x in A & for n holds x in A1.n;then
   x in A & x in A1.0;
   hence contradiction by A2;
  end;
end;

theorem Th38:
  Union (A (/\) A1) = A /\ Union A1
proof
 thus Union (A (/\) A1) c= A /\ Union A1
 proof 
   let x; assume x in Union (A (/\) A1);then
    consider n such that
    B1: x in (A (/\) A1).n by PROB_1:25;
    x in A /\ A1.n by B1,def5;then
    x in A & x in A1.n by XBOOLE_0:def 3;then
    x in A & x in Union A1 by PROB_1:25;
   hence thesis by XBOOLE_0:def 3;
 end;
  let x;assume x in A /\ Union A1;then
C1: x in A & x in Union A1 by XBOOLE_0:def 3;then
    consider n such that
C2: x in A1.n by PROB_1:25;
    x in A /\ A1.n by C1,C2,XBOOLE_0:def 3;then
    x in (A (/\) A1).n by def5;
    hence thesis by PROB_1:25;
end;

theorem Th39:
  Union (A (\/) A1) = A \/ Union A1
proof
 thus Union (A (\/) A1) c= A \/ Union A1
 proof let x;assume
 B1: x in Union (A (\/) A1);
 B2: x in A or ex k st x in A1.k
    proof consider k such that
  C1: x in (A (\/) A1).k by B1,PROB_1:25;
      x in (A \/ A1.k) by C1,def6;then
      x in A or x in A1.k by XBOOLE_0:def 2;
      hence thesis;
    end;
    per cases by B2;
    suppose x in A;
      hence x in A \/ Union A1 by XBOOLE_0:def 2;
    end;
    suppose ex k st x in A1.k;then
      x in Union A1 by PROB_1:25;
      hence x in A \/ Union A1 by XBOOLE_0:def 2;
    end;
 end;
 let x;assume
 C1: x in A \/ Union A1;
  per cases by C1,XBOOLE_0:def 2;
   suppose CC1: x in A;
     x in (A \/ A1.1) by CC1,XBOOLE_0:def 2;then
     x in (A (\/) A1).1 by def6;
    hence thesis by PROB_1:25;
   end;
   suppose CC2: x in Union A1;
    consider k such that
    D1: x in A1.k by CC2,PROB_1:25;
     x in A \/ A1.k by D1,XBOOLE_0:def 2;then
     x in (A (\/) A1).k by def6;
    hence thesis by PROB_1:25;
  end;
end;

theorem Th40:
  A \ Union A1 c= Union (A (\) A1)
proof
 let x;assume x in A \ Union A1;then
 B2: x in A & not x in Union A1 by XBOOLE_0:def 4;
     now let k;
       x in A & not x in A1.k by B2,PROB_1:25;then
       x in A \ A1.k by XBOOLE_0:def 4;
     hence x in (A (\) A1).k by def7;
     end;then
     x in (A (\) A1).1;
   hence x in Union (A (\) A1) by PROB_1:25;
end;

theorem Th41:
  Union (A1 (\) A) = Union A1 \ A
proof
 thus Union (A1 (\) A) c= Union A1 \ A
 proof
   let x;assume
B1: x in Union (A1 (\) A);
    ex k st x in A1.k & not x in A
    proof consider k such that
C1:  x in (A1 (\) A).k by B1,PROB_1:25;
     x in A1.k \ A by C1,def8;then
     x in A1.k & not x in A by XBOOLE_0:def 4;
     hence thesis;
    end;then
    x in Union A1 & not x in A by PROB_1:25;
    hence x in Union A1 \ A by XBOOLE_0:def 4;
  end;
   let x;assume x in Union A1 \ A;then
C1: x in Union A1 & not x in A by XBOOLE_0:def 4;
    ex k st x in (A1 (\) A).k
    proof
      consider k such that
  D1: x in A1.k by C1,PROB_1:25;
      x in A1.k \ A by C1,D1,XBOOLE_0:def 4;then
      x in (A1 (\) A).k by def8;
      hence thesis;
    end;
    hence x in Union (A1 (\) A) by PROB_1:25;
end;

theorem Th42:
  A \+\ Union A1 c= Union (A (\+\) A1)
proof let x;assume
A1: x in A \+\ Union A1;
     per cases by A1,XBOOLE_0:1;
     suppose x in A & not x in Union A1;then
       x in A & not x in A1.0 by PROB_1:25;then
       x in A \+\ A1.0 by XBOOLE_0:1;then
       x in (A (\+\) A1).0 by def9;
       hence x in Union (A (\+\) A1) by PROB_1:25;
     end;
     suppose B2: (not x in A & x in Union A1);then
       consider n1 being Nat such that
  B21: x in A1.n1 by PROB_1:25;
       x in A \+\ A1.n1 by B21,B2,XBOOLE_0:1;then
       x in (A (\+\) A1).n1 by def9;
       hence x in Union (A (\+\) A1) by PROB_1:25;
     end;
end;

theorem
  (inferior_setsequence(A1 (/\) A2)).n
    = (inferior_setsequence A1).n /\ (inferior_setsequence A2).n
proof
 (inferior_setsequence(A1 (/\) A2)).n 
    = Intersection ((A1 (/\) A2) ^\n) by Th1
   .= Intersection ((A1 ^\n) (/\) (A2 ^\n)) by Th4
   .= Intersection (A1 ^\n) /\ Intersection (A2 ^\n) by Th12
   .= (inferior_setsequence A1).n /\ Intersection (A2 ^\n) by Th1
   .= (inferior_setsequence A1).n /\ (inferior_setsequence A2).n by Th1;
 hence thesis;
end;

theorem
  (inferior_setsequence A1).n \/ (inferior_setsequence A2).n
    c= (inferior_setsequence(A1 (\/) A2)).n
proof
A1: (inferior_setsequence A1).n \/ (inferior_setsequence A2).n
 = Intersection (A1 ^\n) \/ (inferior_setsequence A2).n by Th1
.= Intersection (A1 ^\n) \/ Intersection (A2 ^\n) by Th1;
 Intersection ((A1 ^\n) (\/) (A2 ^\n))
 = Intersection ((A1 (\/) A2) ^\n) by Th5
.= (inferior_setsequence(A1 (\/) A2)).n by Th1;
hence thesis by A1,Th13;
end;

theorem
  (inferior_setsequence(A1 (\) A2)).n
    c= (inferior_setsequence A1).n \ (inferior_setsequence A2).n
proof
 (inferior_setsequence(A1 (\) A2)).n 
    = Intersection ((A1 (\) A2) ^\n) by Th1
   .= Intersection ((A1 ^\n) (\) (A2 ^\n)) by Th6;then
 (inferior_setsequence(A1 (\) A2)).n 
   c= Intersection (A1 ^\n) \ Intersection (A2 ^\n) by Th14;then
 (inferior_setsequence(A1 (\) A2)).n 
   c= (inferior_setsequence A1).n \ Intersection (A2 ^\n) by Th1; 
 hence thesis by Th1;
end;

theorem
  (superior_setsequence(A1 (/\) A2)).n
   c= (superior_setsequence A1).n /\ (superior_setsequence A2).n
proof
 (superior_setsequence(A1 (/\) A2)).n
    = Union ((A1 (/\) A2) ^\n) by Th2
   .= Union ((A1 ^\n) (/\) (A2 ^\n)) by Th4;then
 (superior_setsequence(A1 (/\) A2)).n
   c= Union (A1 ^\n) /\ Union (A2 ^\n) by Th8;then
 (superior_setsequence(A1 (/\) A2)).n
   c= (superior_setsequence A1).n /\ Union (A2 ^\n) by Th2;  
 hence thesis by Th2;
end;

theorem
  (superior_setsequence(A1 (\/) A2)).n
   = (superior_setsequence A1).n \/ (superior_setsequence A2).n
proof
 (superior_setsequence(A1 (\/) A2)).n
    = Union ((A1 (\/) A2) ^\n) by Th2
   .= Union ((A1 ^\n) (\/) (A2 ^\n)) by Th5
   .= Union (A1 ^\n) \/ Union (A2 ^\n) by Th9
   .= (superior_setsequence A1).n \/ Union (A2 ^\n) by Th2;
 hence thesis by Th2;
end;

theorem
  (superior_setsequence A1).n \ (superior_setsequence A2).n
   c= (superior_setsequence(A1 (\) A2)).n
proof
 (superior_setsequence A1).n \ (superior_setsequence A2).n
  = Union (A1 ^\n) \ (superior_setsequence A2).n by Th2
 .= Union (A1 ^\n) \ Union (A2 ^\n) by Th2;then
 (superior_setsequence A1).n \ (superior_setsequence A2).n
    c= Union ((A1 ^\n) (\) (A2 ^\n)) by Th10;then
 (superior_setsequence A1).n \ (superior_setsequence A2).n
    c= Union ((A1 (\) A2) ^\n) by Th6;
 hence thesis by Th2;
end;

theorem
  (superior_setsequence A1).n \+\ (superior_setsequence A2).n
   c= (superior_setsequence(A1 (\+\) A2)).n
proof
 (superior_setsequence A1).n \+\ (superior_setsequence A2).n
  = Union (A1 ^\n) \+\ (superior_setsequence A2).n by Th2
 .= Union (A1 ^\n) \+\ Union (A2 ^\n) by Th2;then
 (superior_setsequence A1).n \+\ (superior_setsequence A2).n
    c= Union ((A1 ^\n) (\+\) (A2 ^\n)) by Th11;then
 (superior_setsequence A1).n \+\ (superior_setsequence A2).n
    c= Union ((A1 (\+\) A2) ^\n) by Th7;
 hence thesis by Th2;
end;

theorem Th49:
  (inferior_setsequence(A (/\) A1)).n =  A /\ (inferior_setsequence A1).n
proof
 (inferior_setsequence(A (/\) A1)).n
    = Intersection ((A (/\) A1) ^\n) by Th1
   .= Intersection (A (/\) (A1 ^\n)) by Th16
   .= A /\ Intersection (A1 ^\n) by Th33
   .= A /\ (inferior_setsequence A1).n by Th1;
 hence thesis;
end;

theorem Th50:
  (inferior_setsequence(A (\/) A1)).n =  A \/ (inferior_setsequence A1).n
proof
 (inferior_setsequence(A (\/) A1)).n
    = Intersection ((A (\/) A1) ^\n) by Th1
   .= Intersection (A (\/) (A1 ^\n)) by Th17
   .= A \/ Intersection (A1 ^\n) by Th34
   .= A \/ (inferior_setsequence A1).n by Th1;
 hence thesis;
end;

theorem
  (inferior_setsequence(A (\) A1)).n c=  A \ (inferior_setsequence A1).n
proof
 (inferior_setsequence(A (\) A1)).n
    = Intersection ((A (\) A1) ^\n) by Th1
   .= Intersection (A (\) (A1 ^\n)) by Th18;then
 (inferior_setsequence(A (\) A1)).n
   c= A \ Intersection (A1 ^\n) by Th35;
 hence thesis by Th1;
 end;

theorem Th52:
  (inferior_setsequence(A1 (\) A)).n = (inferior_setsequence A1).n \ A
proof
 (inferior_setsequence(A1 (\) A)).n
    = Intersection ((A1 (\) A) ^\n) by Th1
   .= Intersection ((A1 ^\n) (\) A) by Th19
   .= Intersection (A1 ^\n) \ A by Th36
   .= (inferior_setsequence A1).n \ A by Th1;
 hence thesis;
end;

theorem
  (inferior_setsequence(A (\+\) A1)).n c= A \+\ (inferior_setsequence A1).n
proof
(inferior_setsequence(A (\+\) A1)).n 
    = Intersection ((A (\+\) A1) ^\n) by Th1
   .= Intersection (A (\+\) (A1 ^\n)) by Th20;then
 (inferior_setsequence(A (\+\) A1)).n
   c= A \+\ Intersection (A1 ^\n) by Th37;
 hence thesis by Th1;
end;

theorem Th54:
  (superior_setsequence(A (/\) A1)).n =  A /\ (superior_setsequence A1).n
proof
 (superior_setsequence(A (/\) A1)).n
    = Union ((A (/\) A1) ^\n) by Th2
   .= Union (A (/\) (A1 ^\n)) by Th16
   .= A /\ Union (A1 ^\n) by Th38
   .= A /\ (superior_setsequence A1).n by Th2;
 hence thesis;
end;

theorem Th55:
  (superior_setsequence(A (\/) A1)).n =  A \/ (superior_setsequence A1).n
proof
 (superior_setsequence(A (\/) A1)).n 
    = Union ((A (\/) A1) ^\n) by Th2
   .= Union (A (\/) (A1 ^\n)) by Th17
   .= A \/ Union (A1 ^\n) by Th39
   .= A \/ (superior_setsequence A1).n by Th2;
 hence thesis;
end;

theorem
  A \ (superior_setsequence A1).n c= (superior_setsequence(A (\) A1)).n
proof
 A \ (superior_setsequence A1).n
   = A \ Union (A1 ^\n) by Th2;then
 A \ (superior_setsequence A1).n
   c= Union (A (\) (A1 ^\n)) by Th40;then
 A \ (superior_setsequence A1).n
   c= Union ((A (\) A1) ^\n) by Th18;
hence thesis by Th2;
end;

theorem Th57:
  (superior_setsequence(A1 (\) A)).n = (superior_setsequence A1).n \ A
proof
 reconsider X1 = (superior_setsequence(A1)) as SetSequence of X;
 reconsider X2 = (superior_setsequence(A1 (\) A)) as SetSequence of X;
A1: X2.n c= X1.n \ A
 proof let x;assume
 B1: x in X2.n;
     ex k st x in A1.(n+k) & not x in A
     proof consider k such that
     C1: x in (A1 (\) A).(n+k) by B1,SETLIM_1:25;
      x in A1.(n+k) \ A by C1,def8;then
      x in A1.(n+k) & not x in A by XBOOLE_0:def 4;
     hence thesis;
     end;then
     x in X1.n & not x in A by SETLIM_1:25;
  hence x in X1.n \ A by XBOOLE_0:def 4;
 end;
 X1.n \ A c= X2.n
 proof let x;assume x in X1.n \ A;then
 C1: x in X1.n & not x in A by XBOOLE_0:def 4;
     ex k st x in (A1 (\) A).(n+k)
     proof 
      consider k such that
     D1: x in A1.(n+k) by C1,SETLIM_1:25;
      x in A1.(n+k) \ A by C1,D1,XBOOLE_0:def 4;then
      x in (A1 (\) A).(n+k) by def8;
     hence thesis;
     end;
  hence x in X2.n by SETLIM_1:25;
 end;
hence thesis by A1,XBOOLE_0:def 10;
end;

theorem
  A \+\ (superior_setsequence A1).n c= (superior_setsequence(A (\+\) A1)).n
proof
  A \+\ (superior_setsequence A1).n
   = A \+\ Union (A1 ^\n) by Th2;then
  A \+\ (superior_setsequence A1).n
   c= Union (A (\+\) (A1 ^\n)) by Th42;then
  A \+\ (superior_setsequence A1).n
   c= Union ((A (\+\) A1) ^\n) by Th20;
  hence thesis by Th2;
end;

theorem Th59:
  lim_inf (A1 (/\) A2) = lim_inf A1 /\ lim_inf A2
proof
  (A1 (/\) A2).n = A1.n /\ A2.n by def1;
  hence thesis by KURATO_2:13;
end;

theorem Th590:
  lim_inf A1 \/ lim_inf A2 c= lim_inf (A1 (\/) A2)
proof
  (A1 (\/) A2).n = A1.n \/ A2.n by def2;
  hence thesis by KURATO_2:15;
end;

theorem Th60:
  lim_inf (A1 (\) A2) c= lim_inf A1 \ lim_inf A2
proof
  let x; assume x in lim_inf (A1 (\) A2);then
    consider n such that
A1: for k holds x in (A1 (\) A2).(n+k) by KURATO_2:7;
A2: now let k;
     x in (A1 (\) A2).(n+k) by A1;then
     x in A1.(n+k) \ A2.(n+k) by def3;
    hence x in A1.(n+k) & not x in A2.(n+k) by XBOOLE_0:def 4;
    end;then
A3:  x in lim_inf A1 by KURATO_2:7;
    not x in lim_inf A2 
    proof assume x in lim_inf A2;then
        consider n1 being Nat such that 
    B1: for k holds x in A2.(n1+k) by KURATO_2:7;
        x in A2.(n1+n) by B1;
        hence contradiction by A2;
    end;
    hence x in lim_inf A1 \ lim_inf A2 by A3,XBOOLE_0:def 4; 
end;

theorem Th61:
  A1 is convergent or A2 is convergent implies
lim_inf (A1 (\/) A2) = lim_inf A1 \/ lim_inf A2
proof assume
A0: A1 is convergent or A2 is convergent;
A1: for A1,A2 st A1 is convergent holds
 lim_inf (A1 (\/) A2) = lim_inf A1 \/ lim_inf A2
proof let A1,A2;assume
AB0: A1 is convergent;
 thus lim_inf (A1 (\/) A2) c= lim_inf A1 \/ lim_inf A2
 proof let x;assume x in lim_inf (A1 (\/) A2);then
 consider n1 being Nat such that 
B0: for k holds x in (A1 (\/) A2).(n1+k) by KURATO_2:7;
B1:  now let n; x in (A1 (\/) A2).(n1+n) by B0;then
   x in A1.(n1+n) \/ A2.(n1+n) by def2;
   hence x in A1.(n1+n) or x in A2.(n1+n) by XBOOLE_0:def 2;
  end;
 x in lim_inf A1 \/ lim_inf A2
 proof assume not(x in lim_inf A1 \/ lim_inf A2);then
  C1: not x in lim_inf A1 & not x in lim_inf A2 by XBOOLE_0:def 2;then
      not x in lim_sup A1 by AB0,KURATO_2:def 7;then
     consider n2 being Nat such that
 C2: for k holds not x in A1.(n2+k) by KURATO_2:8;
     consider k1 being Nat such that
 C3: not x in A2.((n1+n2)+k1) by C1,KURATO_2:7;
     not x in A1.(n2+(n1+k1)) by C2;then
     not x in A1.(n1+(n2+k1)) & not x in A2.(n1+(n2+k1)) by C3;
  hence contradiction by B1;
  end;
 hence thesis;
 end;
 thus thesis by Th590;
 end;
 per cases by A0;
  suppose A1 is convergent;
   hence thesis by A1;
  end;
  suppose A2 is convergent;
   hence thesis by A1;
  end;
end;

theorem Th62:
  A2 is convergent implies
    lim_inf (A1 (\) A2) = lim_inf A1 \ lim_inf A2
proof assume
A0: A2 is convergent;
 thus lim_inf (A1 (\) A2) c= lim_inf A1 \ lim_inf A2 by Th60;
 thus lim_inf A1 \ lim_inf A2 c= lim_inf (A1 (\) A2)
 proof let x; assume x in lim_inf A1 \ lim_inf A2;then
 B0: x in lim_inf A1 & not x in lim_inf A2 by XBOOLE_0:def 4;then
 B1: not x in lim_sup A2 by A0,KURATO_2:def 7;
     consider n0 being Nat such that
 B2: for k holds x in A1.(n0+k) by B0,KURATO_2:7;
     consider n1 being Nat such that
 B3: for k holds not x in A2.(n1+k) by B1,KURATO_2:8;
     now let k; 
     C1: x in A1.(n0+(n1+k)) by B2;
         not x in A2.(n1+(n0+k)) by B3;then
         x in A1.((n0+n1)+k) \ A2.((n0+n1)+k) by C1,XBOOLE_0:def 4;
      hence x in (A1 (\) A2).((n0+n1)+k) by def3;
     end;
   hence x in lim_inf (A1 (\) A2) by KURATO_2:7;
 end;
end;

theorem Th63:
  A1 is convergent or A2 is convergent implies
    lim_inf (A1 (\+\) A2) c= lim_inf A1 \+\ lim_inf A2
proof assume 
A0: A1 is convergent or A2 is convergent;
A1: for A1,A2 st A1 is convergent holds
 lim_inf (A1 (\+\) A2) c= lim_inf A1 \+\ lim_inf A2
 proof let A1,A2;assume
  B0: A1 is convergent;
  thus lim_inf (A1 (\+\) A2) c= lim_inf A1 \+\ lim_inf A2
   proof let x; assume x in lim_inf (A1 (\+\) A2);then
     consider n1 being Nat such that
   B1: for k holds x in (A1 (\+\) A2).(n1+k) by KURATO_2:7;
   B2: now let k; x in (A1 (\+\) A2).(n1+k) by B1;then
       x in A1.(n1+k) \+\ A2.(n1+k) by def4;
      hence (x in A1.(n1+k) & not x in A2.(n1+k)) or 
       (not x in A1.(n1+k) & x in A2.(n1+k)) by XBOOLE_0:1;
     end;
     assume
     C1: not x in lim_inf A1 \+\ lim_inf A2;
      per cases by C1,XBOOLE_0:1;
       suppose D00: not x in lim_inf A1 & not x in lim_inf A2;then
          not x in lim_sup A1 by B0,KURATO_2:def 7;then
          consider n2 being Nat such that
      D1: for k holds not x in A1.(n2+k) by KURATO_2:8;
          consider k1 being Nat such that
      D2: not x in A2.((n1+n2)+k1) by D00,KURATO_2:7;
      D3: not x in A1.(n2+(n1+k1)) & not x in A2.((n1+n2)+k1) by D1,D2;
         (x in A1.(n1+(n2+k1)) & not x in A2.(n1+(n2+k1))) or
         (not x in A1.(n1+(n2+k1)) & x in A2.(n1+(n2+k1))) by B2;
        hence contradiction by D3;
       end;
       suppose E00: x in lim_inf A1 & x in lim_inf A2;then
           consider n2 being Nat such that
       E1: for k holds x in A1.(n2+k) by KURATO_2:7;
           consider n3 being Nat such that
       E2: for k holds x in A2.(n3+k) by E00,KURATO_2:7;
       E3: x in A1.(n2+(n1+n3)) & x in A2.(n3+(n1+n2)) by E1,E2;
         (x in A1.(n1+(n2+n3)) & not x in A2.(n1+(n2+n3))) or
         (not x in A1.(n1+(n2+n3)) & x in A2.(n1+(n2+n3))) by B2;
        hence contradiction by E3;
     end;
    end;
  end;
 per cases by A0;
  suppose A1 is convergent;
   hence thesis by A1;
  end;
  suppose A2 is convergent;
   hence thesis by A1;
  end;
end;

theorem Th64:
  A1 is convergent & A2 is convergent implies
   lim_inf (A1 (\+\) A2) = lim_inf A1 \+\ lim_inf A2
proof assume 
A0: A1 is convergent & A2 is convergent;
 thus lim_inf (A1 (\+\) A2) c= lim_inf A1 \+\ lim_inf A2 by A0,Th63;
 thus lim_inf A1 \+\ lim_inf A2 c= lim_inf (A1 (\+\) A2)
  proof let x;assume 
  B1: x in lim_inf A1 \+\ lim_inf A2;
      per cases by B1,XBOOLE_0:1;
       suppose C00: x in lim_inf A1 & not x in lim_inf A2;then
        consider n1 being Nat such that
       C1: for k holds x in A1.(n1+k) by KURATO_2:7;
         not x in lim_sup A2 by C00,A0,KURATO_2:def 7;then
         consider n2 being Nat such that
       C2: for k holds not x in A2.(n2+k) by KURATO_2:8;
         now let k;
          x in A1.(n1+(n2+k)) & not x in A2.(n2+(n1+k)) by C1,C2;then
          x in A1.(n1+n2+k) \+\ A2.(n1+n2+k) by XBOOLE_0:1;
          hence x in (A1 (\+\) A2). (n1+n2+k) by def4;           
         end;
        hence x in lim_inf (A1 (\+\) A2) by KURATO_2:7;
       end;
       suppose C01: not x in lim_inf A1 & x in lim_inf A2;then
         not x in lim_sup A1 by A0,KURATO_2:def 7;then
         consider n3 being Nat such that
       D1: for k holds not x in A1.(n3+k) by KURATO_2:8;
         consider n4 being Nat such that
       D2: for k holds x in A2.(n4+k) by C01,KURATO_2:7;
         now let k;
          not x in A1.(n3+(n4+k)) & x in A2.(n4+(n3+k)) by D1,D2;then
          x in A1.(n3+n4+k) \+\ A2.(n3+n4+k) by XBOOLE_0:1;
          hence x in (A1 (\+\) A2). (n3+n4+k) by def4;
         end;
        hence x in lim_inf (A1 (\+\) A2) by KURATO_2:7;
       end;
  end;
end;

theorem Th65:
  lim_sup (A1 (/\) A2) c= lim_sup A1 /\ lim_sup A2
proof
 (A1 (/\) A2).n = A1.n /\ A2.n by def1;
 hence thesis by KURATO_2:16;
end;

theorem Th861:
  lim_sup (A1 (\/) A2) = lim_sup A1 \/ lim_sup A2
proof
 (A1 (\/) A2).n = A1.n \/ A2.n by def2;
 hence thesis by KURATO_2:14;
end;

theorem Th66:
  lim_sup A1 \ lim_sup A2 c= lim_sup (A1 (\) A2)
proof let x; assume x in lim_sup A1 \ lim_sup A2;then
A0: x in lim_sup A1 & not x in lim_sup A2 by XBOOLE_0:def 4;
    consider n1 being Nat such that
A2: for k holds not x in A2.(n1+k) by A0,KURATO_2:8;
     assume not x in lim_sup (A1 (\) A2);then
         consider n2 being Nat such that
     B2: for k holds not x in (A1 (\) A2).(n2+k) by KURATO_2:8;
     B3: now let k;
          not x in (A1 (\) A2).(n2+k) by B2;then
          not x in A1.(n2+k) \ A2.(n2+k) by def3;
          hence not x in A1.(n2+k) or x in A2.(n2+k) by XBOOLE_0:def 4;
         end;
         consider k1 being Nat such that
     B4: x in A1.(n1+n2+k1) by A0,KURATO_2:8;
         x in A1.(n1+n2+k1) & not x in A2.(n1+(n2+k1)) by A2,B4;then
         x in A1.(n2+(k1+n1)) & not x in A2.(n2+(k1+n1));
      hence contradiction by B3;
end;

theorem
  lim_sup A1 \+\ lim_sup A2 c= lim_sup (A1 (\+\) A2)
proof let x;assume
B1: x in lim_sup A1 \+\ lim_sup A2;
B2:  for A1,A2 st x in lim_sup A1 & not x in lim_sup A2 holds
      x in lim_sup (A1 (\+\) A2)
     proof let A1,A2; assume
      C1: x in lim_sup A1 & not x in lim_sup A2;
          consider n1 being Nat such that
      C3: for k holds not x in A2.(n1+k) by C1,KURATO_2:8;
        now let n;
         consider k1 being Nat such that
        D1: x in A1.((n+n1)+k1) by C1,KURATO_2:8;
            not x in A2.(n1+(n+k1)) by C3;then
            x in A1.(n+(n1+k1)) \+\ A2.(n+(n1+k1)) by D1,XBOOLE_0:1;then
            x in (A1 (\+\) A2).(n+(n1+k1)) by def4;  
         hence ex k st x in (A1 (\+\) A2).(n+k);
        end;
      hence thesis by KURATO_2:8;
     end;
 per cases by B1,XBOOLE_0:1;
  suppose x in lim_sup A1 & not x in lim_sup A2;
   hence x in lim_sup (A1 (\+\) A2) by B2;
  end;
  suppose not x in lim_sup A1 & x in lim_sup A2;
   hence x in lim_sup (A1 (\+\) A2) by B2;
  end;
end;

theorem Th68:
  A1 is convergent or A2 is convergent implies
    lim_sup (A1 (/\) A2) = lim_sup A1 /\ lim_sup A2
proof assume
A0: A1 is convergent or A2 is convergent;
A1: for A1,A2 st A1 is convergent holds
 lim_sup (A1 (/\) A2) = lim_sup A1 /\ lim_sup A2
 proof let A1,A2;assume
 AB0: A1 is convergent;
  thus lim_sup (A1 (/\) A2) c= lim_sup A1 /\ lim_sup A2 by Th65;
  thus lim_sup A1 /\ lim_sup A2 c= lim_sup (A1 (/\) A2)
   proof let x;assume x in lim_sup A1 /\ lim_sup A2;then
   B0: x in lim_sup A1 & x in lim_sup A2 by XBOOLE_0:def 3;then
       x in lim_inf A1 by AB0,KURATO_2:def 7;then
    consider n1 being Nat such that 
   B1: for k holds x in A1.(n1+k) by KURATO_2:7;
    now let n;consider k1 being Nat such that
    C1: x in A2.((n+n1)+k1) by B0,KURATO_2:8;
        x in A1.(n1+(n+k1)) by B1;then
        x in A1.(n+(n1+k1)) /\ A2.(n+(n1+k1)) by C1,XBOOLE_0:def 3;then
        x in (A1 (/\) A2).(n+(n1+k1)) by def1;
     hence ex k st x in (A1 (/\) A2).(n+k);
     end;
   hence x in lim_sup (A1 (/\) A2) by KURATO_2:8;
   end;
 end;
 per cases by A0;
  suppose A1 is convergent;
   hence thesis by A1;
  end;
  suppose A2 is convergent;
   hence thesis by A1;
  end;
end;

theorem Th69:
  A2 is convergent implies
   lim_sup (A1 (\) A2) = lim_sup A1 \ lim_sup A2
proof assume
A0: A2 is convergent;
  thus lim_sup (A1 (\) A2) c= lim_sup A1 \ lim_sup A2
  proof let x; assume 
  B1: x in lim_sup (A1 (\) A2);
  B2: now let n; consider k1 being Nat such that
   C1: x in (A1 (\) A2).(n+k1) by B1,KURATO_2:8;
       x in A1.(n+k1) \ A2.(n+k1) by C1,def3;then
       x in A1.(n+k1) & not x in A2.(n+k1) by XBOOLE_0:def 4;
    hence ex k st x in A1.(n+k) & not x in A2.(n+k);
   end;
   assume not x in lim_sup A1 \ lim_sup A2;then
   D1: not (x in lim_sup A1 & not x in lim_sup A2) by XBOOLE_0:def 4;
     per cases by D1,A0,KURATO_2:def 7;
      suppose not x in lim_sup A1;then
          consider n1 being Nat such that
      D2: for k holds not x in A1.(n1+k) by KURATO_2:8;
          consider k2 being Nat such that
      D3: x in A1.(n1+k2) & not x in A2.(n1+k2) by B2;
       thus contradiction by D2,D3;
      end;
      suppose x in lim_inf A2;then
          consider n2 being Nat such that
      D4: for k holds x in A2.(n2+k) by KURATO_2:7;
          consider k3 being Nat such that
      D5: x in A1.(n2+k3) & not x in A2.(n2+k3) by B2;
       thus contradiction by D4,D5;
    end;
  end;
  thus thesis by Th66;
end;

theorem Th70:
  A1 is convergent & A2 is convergent implies
   lim_sup (A1 (\+\) A2) = lim_sup A1 \+\ lim_sup A2
proof assume
A0: A1 is convergent & A2 is convergent;
thus lim_sup (A1 (\+\) A2)
 = lim_sup ((A1 (\) A2) (\/) (A2 (\) A1)) by Th3
.= lim_sup (A1 (\) A2) \/ lim_sup (A2 (\) A1) by Th861
.= (lim_sup A1 \ lim_sup A2) \/ lim_sup (A2 (\) A1) by A0,Th69
.= (lim_sup A1 \ lim_sup A2) \/ (lim_sup A2 \ lim_sup A1) by A0,Th69
.= lim_sup A1 \+\ lim_sup A2 by XBOOLE_0:def 6;
end;

theorem Th71:
  lim_inf (A (/\) A1) = A /\ lim_inf A1
proof
 reconsider X1 = (inferior_setsequence(A1)) as SetSequence of X;
 reconsider X2 = (inferior_setsequence(A (/\) A1)) as SetSequence of X;
 X2 = A (/\) X1
 proof
  now let n;
  thus X2.n = A /\ X1.n by Th49
       .= (A (/\) X1).n by def5;
  end; 
 hence thesis by FUNCT_2:113;
 end;then
 Union X2 = A /\ Union X1 by Th38;then
 lim_inf (A (/\) A1) = A /\ Union X1 by SETLIM_1:def 4;
 hence thesis by SETLIM_1:def 4;
end;

theorem Th72:
  lim_inf (A (\/) A1) = A \/ lim_inf A1
proof
 reconsider X1 = (inferior_setsequence(A1)) as SetSequence of X;
 reconsider X2 = (inferior_setsequence(A (\/) A1)) as SetSequence of X;
 X2 = A (\/) X1
 proof
  now let n;
  thus X2.n = A \/ X1.n by Th50
       .= (A (\/) X1).n by def6;
  end; 
 hence thesis by FUNCT_2:113;
 end;then
 Union X2 = A \/ Union X1 by Th39;then
 lim_inf (A (\/) A1) = A \/ Union X1 by SETLIM_1:def 4;
 hence thesis by SETLIM_1:def 4;
end;

theorem Th73:
  lim_inf (A (\) A1) c= A \ lim_inf A1
proof let x; assume x in lim_inf (A (\) A1);then
    consider n such that
A1: for k holds x in (A (\) A1).(n+k) by KURATO_2:7;
A2: now let k;
     x in (A (\) A1).(n+k) by A1;then
     x in A \ A1.(n+k) by def7;
    hence x in A & not x in A1.(n+k) by XBOOLE_0:def 4; 
    end;
    not x in lim_inf A1
    proof assume x in lim_inf A1;then
      consider n1 being Nat such that
  B1: for k holds x in A1.(n1+k) by KURATO_2:7;
      x in A1.(n1+n) by B1;
      hence contradiction by A2;
    end;
   hence x in A \ lim_inf A1 by A2,XBOOLE_0:def 4;
end;

theorem Th74:
  lim_inf (A1 (\) A) = lim_inf A1 \ A
proof
 reconsider X1 = (inferior_setsequence(A1)) as SetSequence of X;
 reconsider X2 = (inferior_setsequence(A1 (\) A)) as SetSequence of X;
 X2 = X1 (\) A
 proof
  now let n;
  thus X2.n = X1.n \ A by Th52
       .= (X1 (\) A).n by def8;
  end; 
 hence thesis by FUNCT_2:113;
 end;then
 Union X2 = Union X1 \ A by Th41;then
 lim_inf (A1 (\) A) = Union X1 \ A by SETLIM_1:def 4;
 hence thesis by SETLIM_1:def 4;
end;

theorem Th75:
  lim_inf (A (\+\) A1) c= A \+\ lim_inf A1
proof let x; assume x in lim_inf (A (\+\) A1);then
    consider n1 being Nat such that
A1: for k holds x in (A (\+\) A1).(n1+k) by KURATO_2:7;
A2: now let k;
     x in (A (\+\) A1).(n1+k) by A1;then
     x in A \+\ A1.(n1+k) by def9;
    hence (x in A & not x in A1.(n1+k)) or (not x in A & x in A1.(n1+k)) 
     by XBOOLE_0:1; 
    end;
 assume not x in A \+\ lim_inf A1;then
 B1: (not x in A & not x in lim_inf A1) or
  (x in lim_inf A1 & x in A) by XBOOLE_0:1;
  per cases by B1,KURATO_2:7;
   suppose BB1:  not x in A & not ex n st for k holds  x in A1.(n+k);then
    consider k1 being Nat such that
   BB2: not x in A1.(n1+k1);
   thus contradiction by BB1,BB2,A2;
   end;
   suppose BC1: x in A & ex n st for k holds x in A1.(n+k);then
     consider n2 being Nat such that
   BC2: for k holds x in A1.(n2+k);
     x in A1.(n2+n1) by BC2;
   hence contradiction by BC1,A2;
   end;
end;

theorem Th76:
  A1 is convergent implies
   lim_inf (A (\) A1) = A \ lim_inf A1
proof assume
A0: A1 is convergent;
 thus lim_inf (A (\) A1) c= A \ lim_inf A1 by Th73;
 thus A \ lim_inf A1 c= lim_inf (A (\) A1)
 proof let x;assume
    C21: x in A \ lim_inf A1;then
         x in A \ lim_sup A1 by A0,KURATO_2:def 7;then
         not x in lim_sup A1 by XBOOLE_0:def 4;then
         consider n1 being Nat such that
    C22: for k holds not x in A1.(n1+k) by KURATO_2:8;
    assume
D1: not x in lim_inf (A (\) A1);
D2: for n holds not x in A or ex k st x in A1.(n+k)
    proof let n;
      consider k such that
  E1: not x in (A (\) A1).(n+k) by D1,KURATO_2:7;
      not x in A \ A1.(n+k) by E1,def7;then
      not ( x in A & not x in A1.(n+k)) by XBOOLE_0:def 4;
      hence thesis;
    end;
    per cases by D2;
    suppose not x in A;
      hence contradiction by C21,XBOOLE_0:def 4;
    end;
    suppose ex k st x in A1.(n1+k);then
      consider k such that
  F1: x in A1.(n1+k);
      thus contradiction by F1,C22;
    end;
  end;
end;

theorem Th77:
  A1 is convergent implies
   lim_inf (A (\+\) A1) = A \+\ lim_inf A1
proof assume
A0: A1 is convergent;
  thus lim_inf (A (\+\) A1) c= A \+\ lim_inf A1 by Th75;
  thus A \+\ lim_inf A1 c= lim_inf (A (\+\) A1)
  proof let x; assume 
  B1: x in A \+\ lim_inf A1;
   per cases by B1,XBOOLE_0:1;
     suppose C1: x in A & not x in lim_inf A1;then
         not x in lim_sup A1 by A0,KURATO_2:def 7;then
         consider n1 being Nat such that
     C2: for k holds not x in A1.(n1+k) by KURATO_2:8;
        now let k; x in A & not x in A1.(n1+k) by C1,C2;then
         x in A \+\ A1.(n1+k) by XBOOLE_0:1;
        hence x in (A (\+\) A1).(n1+k) by def9;
        end;
      hence x in lim_inf (A (\+\) A1) by KURATO_2:7;
     end;
     suppose BB1: not x in A & x in lim_inf A1;then
       consider n2 being Nat such that
     D1: for k holds x in A1.(n2+k) by KURATO_2:7;
       now let k; not x in A & x in A1.(n2+k) by BB1,D1;then
         x in A \+\ A1.(n2+k) by XBOOLE_0:1;
        hence x in (A (\+\) A1).(n2+k) by def9;
        end;
      hence x in lim_inf (A (\+\) A1) by KURATO_2:7;
     end;
  end;
end;

theorem Th78:
  lim_sup (A (/\) A1) = A /\ lim_sup A1
proof
 reconsider X1 = (superior_setsequence(A1)) as SetSequence of X;
 reconsider X2 = (superior_setsequence(A (/\) A1)) as SetSequence of X;
 X2 = A (/\) X1
 proof
  now let n;
  thus X2.n = A /\ X1.n by Th54
       .= (A (/\) X1).n by def5;
  end; 
 hence thesis by FUNCT_2:113;
 end;then
 Intersection X2 = A /\ Intersection X1 by Th33;then
 lim_sup (A (/\) A1) = A /\ Intersection X1 by SETLIM_1:def 5;
 hence thesis by SETLIM_1:def 5;
end;

theorem Th79:
  lim_sup (A (\/) A1) = A \/ lim_sup A1
proof
 reconsider X1 = (superior_setsequence(A1)) as SetSequence of X;
 reconsider X2 = (superior_setsequence(A (\/) A1)) as SetSequence of X;
 X2 = A (\/) X1
 proof
  now let n;
  thus X2.n = A \/ X1.n by Th55
       .= (A (\/) X1).n by def6;
  end; 
 hence thesis by FUNCT_2:113;
 end;then
 Intersection X2 = A \/ Intersection X1 by Th34;then
 lim_sup (A (\/) A1) = A \/ Intersection X1 by SETLIM_1:def 5;
 hence thesis by SETLIM_1:def 5;
end;

theorem Th80:
  A \ lim_sup A1 c= lim_sup (A (\) A1)
proof let x; assume
A0: x in A \ lim_sup A1;then
    not x in lim_sup A1 by XBOOLE_0:def 4;then
    consider n1 being Nat such that
A1: for k holds not x in A1.(n1+k) by KURATO_2:8;   
    assume not x in lim_sup (A (\) A1);then
    consider n such that
B2: for k holds not x in (A (\) A1).(n+k) by KURATO_2:8;
B3: now let k;
      not x in (A (\) A1).(n+k) by B2;then
      not x in A \ A1.(n+k) by def7;
      hence not x in A or x in A1.(n+k) by XBOOLE_0:def 4;
    end;
    per cases by B3;
    suppose not x in A;
      hence contradiction by A0,XBOOLE_0:def 4;
    end;
    suppose C2: x in A1.(n+k);
      not x in A1.(n1+n) by A1;
      hence contradiction by C2;
    end;
end;

theorem Th81:
  lim_sup (A1 (\) A) = lim_sup A1 \ A
proof
  reconsider X1 = (superior_setsequence(A1)) as SetSequence of X;
  reconsider X2 = (superior_setsequence(A1 (\) A)) as SetSequence of X;
  X2 = X1 (\) A
  proof
    now let n;
      thus X2.n = X1.n \ A by Th57
       .= (X1 (\) A).n by def8;
    end;
    hence thesis by FUNCT_2:113;
  end;then
  Intersection X2 = Intersection X1 \ A by Th36;then
  lim_sup (A1 (\) A) = Intersection X1 \ A by SETLIM_1:def 5;
  hence thesis by SETLIM_1:def 5;
end;

theorem Th82:
  A \+\ lim_sup A1 c= lim_sup (A (\+\) A1)
proof
  (A (\+\) A1).n = A \+\ A1.n by def9;
  hence thesis by KURATO_2:20;
end;

theorem Th820:
  A1 is convergent implies
    lim_sup (A (\) A1) = A \ lim_sup A1
proof assume 
A0: A1 is convergent;
    thus lim_sup (A (\) A1) c= A \ lim_sup A1
    proof
    let x;assume 
A1: x in lim_sup (A (\) A1);
A2: for n ex k st x in A & not x in A1.(n+k)
     proof let n;consider k such that
     B1: x in (A (\) A1).(n+k) by A1,KURATO_2:8;
        x in A \ A1.(n+k) by B1,def7;then
        x in A & not x in A1.(n+k) by XBOOLE_0:def 4;
      hence thesis;
     end;
A3:  x in A
     proof assume not x in A;then
     C1: for n holds not x in A;
      for n holds ex k st x in A
      proof let n; consider k such that
       D1: x in A & not x in A1.(n+k) by A2;
       thus thesis by D1;
      end;
      hence contradiction by C1;
     end;
     for n holds ex k st not x in A1.(n+k)
     proof let n; consider k such that
     E1: x in A & not x in A1.(n+k) by A2;
       thus thesis by E1;
     end;then
     not x in lim_inf A1 by KURATO_2:7;then
     not x in lim_sup A1 by A0,KURATO_2:def 7;
   hence x in A \ lim_sup A1 by A3,XBOOLE_0:def 4;
 end;
 thus thesis by Th80;
end;

theorem Th83:
  A1 is convergent implies
    lim_sup (A (\+\) A1) = A \+\ lim_sup A1
proof assume
A0: A1 is convergent;
  thus lim_sup (A (\+\) A1) c= A \+\ lim_sup A1
   proof let x; assume 
   B1: x in lim_sup (A (\+\) A1);
   B2: now let n; consider k1 being Nat such that
     C1: x in (A (\+\) A1).(n+k1) by B1,KURATO_2:8;
         x in A \+\ A1.(n+k1) by C1,def9;then
         (x in A & not x in A1.(n+k1)) or 
         (not x in A & x in A1.(n+k1)) by XBOOLE_0:1;
        hence ex k st (x in A & not x in A1.(n+k)) or 
           (not x in A & x in A1.(n+k));
       end;
     assume
     D1: not x in A \+\ lim_sup A1;
       per cases by D1,XBOOLE_0:1;
        suppose DD0: not x in A & not x in lim_sup A1;then
            consider n1 being Nat such that
        E1: for k holds not x in A1.(n1+k) by KURATO_2:8;
            consider k1 being Nat such that
        E2: (x in A & not x in A1.(n1+k1)) or
            (not x in A & x in A1.(n1+k1)) by B2;
         thus contradiction by E1,E2,DD0;
        end;
        suppose DD1: x in A & x in lim_sup A1;then
            x in lim_inf A1 by A0,KURATO_2:def 7;then
            consider n2 being Nat such that 
        E3: for k holds x in A1.(n2+k) by KURATO_2:7;
            consider k2 being Nat such that
        E4: (x in A & not x in A1.(n2+k2)) or 
            (not x in A & x in A1.(n2+k2)) by B2;
         thus contradiction by E3,E4,DD1;
        end;
   end;
  thus thesis by Th82;
end;

theorem
  A1 is convergent & A2 is convergent implies
    (A1 (/\) A2) is convergent & lim (A1 (/\) A2) = lim A1 /\ lim A2
proof assume 
A0: A1 is convergent & A2 is convergent;
A1: lim_inf (A1 (/\) A2) = lim_inf A1 /\ lim_inf A2 by Th59
                        .= lim A1 /\ lim_inf A2 by A0,SETLIM_1:def 6
                        .= lim A1 /\ lim A2 by A0,SETLIM_1:def 6;
    lim_sup (A1 (/\) A2) = lim_sup A1 /\ lim_sup A2 by A0,Th68
                        .= lim A1 /\ lim_sup A2 by A0,SETLIM_1:def 6
                        .= lim A1 /\ lim A2 by A0,SETLIM_1:def 6;then
    (A1 (/\) A2) is convergent by A1,KURATO_2:def 7;
 hence thesis by A1,SETLIM_1:def 6;
end;

theorem
  A1 is convergent & A2 is convergent implies
    (A1 (\/) A2) is convergent & lim (A1 (\/) A2) = lim A1 \/ lim A2
proof assume
A0: A1 is convergent & A2 is convergent;
A1: lim_inf (A1 (\/) A2) = lim_inf A1 \/ lim_inf A2 by A0,Th61
                        .= lim A1 \/ lim_inf A2 by A0,SETLIM_1:def 6
                        .= lim A1 \/ lim A2 by A0,SETLIM_1:def 6;
    lim_sup (A1 (\/) A2) = lim_sup A1 \/ lim_sup A2 by Th861
                        .= lim A1 \/ lim_sup A2 by A0,SETLIM_1:def 6
                        .= lim A1 \/ lim A2 by A0,SETLIM_1:def 6;then
    (A1 (\/) A2) is convergent by A1,KURATO_2:def 7;
 hence thesis by A1,SETLIM_1:def 6;
end;

theorem
  A1 is convergent & A2 is convergent implies
   (A1 (\) A2) is convergent & lim (A1 (\) A2) = lim A1 \ lim A2
proof assume
A0: A1 is convergent & A2 is convergent;
A1: lim_inf (A1 (\) A2) = lim_inf A1 \ lim_inf A2 by A0,Th62
                       .= lim A1 \ lim_inf A2 by A0,SETLIM_1:def 6
                       .= lim A1 \ lim A2 by A0,SETLIM_1:def 6;
    lim_sup (A1 (\) A2) = lim_sup A1 \ lim_sup A2 by A0,Th69
                       .= lim A1 \ lim_sup A2 by A0,SETLIM_1:def 6
                       .= lim A1 \ lim A2 by A0,SETLIM_1:def 6;then
    (A1 (\) A2) is convergent by A1,KURATO_2:def 7;
 hence thesis by A1,SETLIM_1:def 6;
end;

theorem
  A1 is convergent & A2 is convergent implies
   (A1 (\+\) A2) is convergent & lim (A1 (\+\) A2) = lim A1 \+\ lim A2
proof assume
A0: A1 is convergent & A2 is convergent;
A2: lim_inf (A1 (\+\) A2) = lim_inf A1 \+\ lim_inf A2 by A0,Th64
                         .= lim A1 \+\ lim_inf A2 by A0,SETLIM_1:def 6
                         .= lim A1 \+\ lim A2 by A0,SETLIM_1:def 6;
    lim_sup (A1 (\+\) A2) = lim_sup A1 \+\ lim_sup A2 by A0,Th70
                         .= lim A1 \+\ lim_sup A2 by A0,SETLIM_1:def 6
                         .= lim A1 \+\ lim A2 by A0,SETLIM_1:def 6;then
    (A1 (\+\) A2) is convergent by A2,KURATO_2:def 7;
 hence thesis by A2,SETLIM_1:def 6;
end;

theorem
  A1 is convergent implies (A (/\) A1) is convergent &
   lim (A (/\) A1) = A /\ lim A1
proof assume 
A0: A1 is convergent;
A1: lim_inf (A (/\) A1) = A /\ lim_inf A1 by Th71
                       .= A /\ lim A1 by A0,SETLIM_1:def 6;
    lim_sup (A (/\) A1) = A /\ lim_sup A1 by Th78
                       .= lim_inf (A (/\) A1) by A1,A0,SETLIM_1:def 6;then
    (A (/\) A1) is convergent by KURATO_2:def 7;
  hence thesis by A1,SETLIM_1:def 6;
end;

theorem
  A1 is convergent implies (A (\/) A1) is convergent &
   lim (A (\/) A1) = A \/ lim A1
proof assume
A0: A1 is convergent;
A1: lim_inf (A (\/) A1) = A \/ lim_inf A1 by Th72
                       .= A \/ lim A1 by A0,SETLIM_1:def 6;
    lim_sup (A (\/) A1) = A \/ lim_sup A1 by Th79
                       .= lim_inf (A (\/) A1) by A1,A0,SETLIM_1:def 6;then
  (A (\/) A1) is convergent by KURATO_2:def 7;
  hence thesis by A1,SETLIM_1:def 6;
end;

theorem
  A1 is convergent implies (A (\) A1) is convergent &
   lim (A (\) A1) = A \ lim A1
proof assume
A1: A1 is convergent;
A2: lim_inf (A (\) A1) = A \ lim_inf A1 by A1,Th76
                      .= A \ lim A1 by A1,SETLIM_1:def 6;
    lim_sup (A (\) A1) = A \ lim_sup A1 by A1,Th820
                      .= lim_inf (A (\) A1) by A2,A1,SETLIM_1:def 6;then
  (A (\) A1) is convergent by KURATO_2:def 7;
  hence thesis by A2,SETLIM_1:def 6;
end;

theorem
  A1 is convergent implies (A1 (\) A) is convergent &
   lim (A1 (\) A) = lim A1 \ A
proof assume
A0: A1 is convergent;
A1: lim_inf (A1 (\) A) = lim_inf A1 \ A by Th74
                      .= lim A1 \ A by A0,SETLIM_1:def 6;
    lim_sup (A1 (\) A) = lim_sup A1 \ A by Th81
                      .= lim_inf (A1 (\) A) by A1,A0,SETLIM_1:def 6;then
  (A1 (\) A) is convergent by KURATO_2:def 7;
  hence thesis by A1,SETLIM_1:def 6;
end;

theorem
  A1 is convergent implies (A (\+\) A1) is convergent &
   lim (A (\+\) A1) = A \+\ lim A1
proof assume
A0: A1 is convergent;then
A2: lim_inf (A (\+\) A1) = A \+\ lim_inf A1 by Th77
                        .= A \+\ lim A1 by A0,SETLIM_1:def 6;
    lim_sup (A (\+\) A1) = A \+\ lim_sup A1 by A0,Th83
                        .= lim_inf (A (\+\) A1) by A2,A0,SETLIM_1:def 6;then
   (A (\+\) A1) is convergent by KURATO_2:def 7;
   hence thesis by A2,SETLIM_1:def 6;
end;
