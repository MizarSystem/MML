:: Semilattice Operations on Finite Subsets
::  by Andrzej Trybulec
::
:: Received September 18, 1989
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies TARSKI, XBOOLE_0, FUNCT_1, RELAT_1, SUBSET_1, FINSUB_1, BINOP_1,
      FINSET_1, FUNCOP_1, SETWISEO;
 notations TARSKI, XBOOLE_0, ENUMSET1, SUBSET_1, RELAT_1, FUNCT_1, FINSUB_1,
      RELSET_1, FINSET_1, PARTFUN1, FUNCT_2, BINOP_1, FUNCOP_1;
 constructors ENUMSET1, PARTFUN1, BINOP_1, FUNCOP_1, FINSET_1, FINSUB_1,
      RELSET_1;
 registrations XBOOLE_0, SUBSET_1, RELSET_1, FUNCT_2, FINSET_1, FINSUB_1;
 requirements BOOLE, SUBSET;
 definitions BINOP_1, TARSKI, RELAT_1;
 theorems FINSUB_1, ZFMISC_1, SUBSET_1, BINOP_1, TARSKI, FUNCT_1, FUNCT_2,
      FUNCOP_1, ENUMSET1, RELAT_1, XBOOLE_0, XBOOLE_1;
 schemes FINSET_1, FUNCT_2, BINOP_1, SUBSET_1;

begin :: Auxiliary Theorems

reserve x,y,z,X,Y for set;

canceled 2;

theorem Th3:
  {x} c= {x,y,z}
proof
  {x,y,z} = {x} \/ {y,z} by ENUMSET1:42;
  hence thesis by XBOOLE_1:7;
end;

theorem Th4:
  {x,y} c= {x,y,z}
proof
  {x,y,z} = {x,y} \/ {z} by ENUMSET1:43;
  hence thesis by XBOOLE_1:7;
end;

theorem Th5:
  X c= Y \/ {x} implies x in X or X c= Y
proof
  assume that
A1: X c= Y \/ {x} and
A2: not x in X;
A3: X misses {x} by A2,ZFMISC_1:56;
  X = X /\ (Y \/ {x}) by A1,XBOOLE_1:28
    .= X /\ Y \/ X /\ {x} by XBOOLE_1:23
    .= X /\ Y \/ {} by A3,XBOOLE_0:def 7
    .= X /\ Y;
  hence thesis by XBOOLE_1:17;
end;

theorem Th6:
  x in X \/ {y} iff x in X or x = y
proof
  x in X \/ {y} iff x in X or x in {y} by XBOOLE_0:def 3;
  hence thesis by TARSKI:def 1;
end;

canceled;

theorem Th8:
  X \/ {x} c= Y iff x in Y & X c= Y
proof
A1: X c= Y & {x} c= Y implies X \/ {x} c= Y by XBOOLE_1:8;
  X \/ {x} c= Y implies X c= Y & {x} c= Y by XBOOLE_1:11;
  hence thesis by A1,ZFMISC_1:37;
end;

canceled 2;

theorem Th11:
  for X,Y for f being Function holds f.:(Y \ f"X) = f.:Y \ X
proof
  let X,Y;
  let f be Function;
  now
    let x;
    thus x in f.:(Y \ f"X) implies x in f.:Y \ X
    proof
      assume x in f.:(Y \ f"X);
      then consider z such that
A1:   z in dom f and
A2:   z in Y \ f"X and
A3:   f.z = x by FUNCT_1:def 12;
      not z in f"X by A2,XBOOLE_0:def 5;
      then
A4:   not x in X by A1,A3,FUNCT_1:def 13;
      f.z in f.:Y by A1,A2,FUNCT_1:def 12;
      hence thesis by A3,A4,XBOOLE_0:def 5;
    end;
    assume
A5: x in f.:Y \ X;
    then consider z such that
A6: z in dom f and
A7: z in Y and
A8: f.z = x by FUNCT_1:def 12;
    not x in X by A5,XBOOLE_0:def 5;
    then not z in f"X by A8,FUNCT_1:def 13;
    then z in Y \ f"X by A7,XBOOLE_0:def 5;
    hence x in f.:(Y \ f"X) by A6,A8,FUNCT_1:def 12;
  end;
  hence thesis by TARSKI:2;
end;

reserve X,Y for non empty set,
  f for Function of X,Y;

theorem Th12:
  for x being Element of X holds x in f"{f.x}
proof
  let x be Element of X;
  f.x in {f.x} by TARSKI:def 1;
  hence thesis by FUNCT_2:46;
end;

theorem Th13:
  for x being Element of X holds Im(f,x) = {f.x}
proof
  let x be Element of X;
  for y holds y in f.:{x} iff y = f.x
  proof
    let y;
    thus y in f.:{x} implies y = f.x
    proof
      assume y in f.:{x};
      then ex z st z in dom f & z in {x} & f.z = y by FUNCT_1:def 12;
      hence thesis by TARSKI:def 1;
    end;
    x in {x} by TARSKI:def 1;
    hence thesis by FUNCT_2:43;
  end;
  hence thesis by TARSKI:def 1;
end;

:: Theorems related to Fin ...

theorem Th14:
  for B being Element of Fin X for x st x in B holds x is Element of X
proof
  let B be Element of Fin X;
A1: B c= X by FINSUB_1:def 5;
  let x;
  assume x in B;
  hence thesis by A1;
end;

theorem
  for A being (Element of Fin X), B being set for f being Function of X,
  Y st for x being Element of X holds x in A implies f.x in B holds f.:A c= B
proof
  let A be (Element of Fin X), B be set;
  let f be Function of X,Y such that
A1: for x being Element of X holds x in A implies f.x in B;
  let x be set;
  assume x in f.:A;
  then consider y being set such that
  y in dom f and
A2: y in A and
A3: x = f.y by FUNCT_1:def 12;
  reconsider y as Element of X by A2,Th14;
  x = f.y by A3;
  hence thesis by A1,A2;
end;

theorem Th16:
  for X being set for B being Element of Fin X for A being set st
  A c= B holds A is Element of Fin X
proof
  let X be set, B be Element of Fin X;
  let A be set such that
A1: A c= B;
  B c= X by FINSUB_1:def 5;
  then A c= X by A1,XBOOLE_1:1;
  hence thesis by A1,FINSUB_1:def 5;
end;

Lm1: for A being Element of Fin X holds f.: A is Element of Fin Y by FINSUB_1:
def 5;

canceled;

theorem Th18:
  for B being Element of Fin X st B <> {} ex x being Element of X st x in B
proof
  let B be Element of Fin X;
  consider x being Element of B;
  assume
A1: B <> {};
  then reconsider x as Element of X by Th14;
  take x;
  thus thesis by A1;
end;

theorem Th19:
  for A being Element of Fin X holds f.:A = {} implies A = {}
proof
  let A be Element of Fin X;
  assume
A1: f.:A = {};
  assume A <> {};
  then consider x being Element of X such that
A2: x in A by Th18;
  f.x in f.:A by A2,FUNCT_2:43;
  hence contradiction by A1;
end;

registration
  let X be set;
  cluster empty Element of Fin X;
  existence
  proof
    {} is Element of Fin X by FINSUB_1:18;
    hence thesis;
  end;
end;

definition
  let X be set;
  func {}.X -> empty Element of Fin X equals
  {};
  coherence by FINSUB_1:18;
end;

scheme
  FinSubFuncEx{ A()->non empty set, B()->(Element of Fin A()), P[set,set] } :
ex f being Function of A(), Fin A() st for b,a being Element of A() holds a in
  f.b iff a in B() & P[a,b] proof
  defpred X[set,Element of Fin A()] means for a being Element of A() holds a
  in $2 iff a in B() & P[a,$1];
A1: now
    reconsider B = B() as Subset of A() by FINSUB_1:def 5;
    let x be Element of A();
    defpred X[set] means $1 in B() & P[$1,x];
    consider y being Subset of A() such that
A2: for a being Element of A() holds a in y iff X[a] from SUBSET_1:sch
    3;
    for x being Element of A() holds x in y implies x in B by A2;
    then y c= B() by SUBSET_1:7;
    then reconsider y as Element of Fin A() by FINSUB_1:def 5;
    take y9 = y;
    thus X[x,y9] by A2;
  end;
  thus ex f being Function of A(), Fin A() st for x being Element of A() holds
  X[x,f.x] from FUNCT_2:sch 3(A1);
end;

:: theorems related to BinOp of ...

definition
  let X be non empty set, F be BinOp of X;
  attr F is having_a_unity means
  :Def2:
  ex x being Element of X st x is_a_unity_wrt F;
end;

canceled 2;

theorem Th22:
  for X being non empty set, F being BinOp of X holds F is
  having_a_unity iff the_unity_wrt F is_a_unity_wrt F
proof
  let X be non empty set, F be BinOp of X;
  thus F is having_a_unity implies the_unity_wrt F is_a_unity_wrt F
  proof
    assume F is having_a_unity;
    then ex x being Element of X st x is_a_unity_wrt F by Def2;
    hence thesis by BINOP_1:def 8;
  end;
  thus thesis by Def2;
end;

theorem Th23:
  for X being non empty set, F being BinOp of X st F is
having_a_unity for x being Element of X holds F.(the_unity_wrt F, x) = x & F.(x
  , the_unity_wrt F) = x
proof
  let X be non empty set, F be BinOp of X;
  assume F is having_a_unity;
  then
A1: the_unity_wrt F is_a_unity_wrt F by Th22;
  let x be Element of X;
  thus thesis by A1,BINOP_1:11;
end;

:: Main part

registration
  let X be non empty set;
  cluster non empty Element of Fin X;
  existence
  proof
    consider x being Element of X;
    {x} is Subset of X by SUBSET_1:63;
    then {x} is Element of Fin X by FINSUB_1:def 5;
    hence thesis;
  end;
end;

notation
  let X be non empty set, x be Element of X;
  synonym {.x.} for {x};
  let y be Element of X;
  synonym {.x,y.} for {x,y};
  let z be Element of X;
  synonym {.x,y,z.} for {x,y,z};
end;

definition
  let X be non empty set, x be Element of X;
  redefine func {.x.} -> Element of Fin X;
  coherence
  proof
    {x} is Subset of X by SUBSET_1:63;
    hence thesis by FINSUB_1:def 5;
  end;
  let y be Element of X;
  redefine func {.x,y.} -> Element of Fin X;
  coherence
  proof
    {x,y} is Subset of X by SUBSET_1:56;
    hence thesis by FINSUB_1:def 5;
  end;
  let z be Element of X;
  redefine func {.x,y,z.} -> Element of Fin X;
  coherence
  proof
    {x,y,z} is Subset of X by SUBSET_1:57;
    hence thesis by FINSUB_1:def 5;
  end;
end;

definition
  let X be set;
  let A,B be Element of Fin X;
  redefine func A \/ B -> Element of Fin X;
  coherence by FINSUB_1:10;
end;

definition
  let X be set;
  let A,B be Element of Fin X;
  redefine func A \ B -> Element of Fin X;
  coherence by FINSUB_1:10;
end;

scheme
  FinSubInd1{ X() -> non empty set, P[set] } : for B being Element of Fin X()
  holds P[B]
provided
A1: P[{}.X()] and
A2: for B9 being (Element of Fin X()), b being Element of X() holds P[B9
] & not b in B9 implies P[B9 \/ {b}]
proof
  defpred X[set] means ex B9 being Element of Fin X() st B9 = $1 & P[B9];
  let B be Element of Fin X();
A3: for x,A being set st x in B & A c= B & X[A] holds X[A \/ {x}]
  proof
    let x,A be set such that
A4: x in B and
A5: A c= B and
A6: ex B9 being Element of Fin X() st B9 = A & P[B9];
    reconsider x9 = x as Element of X() by A4,Th14;
    reconsider A9 = A as Element of Fin X() by A5,Th16;
    take A9 \/ {.x9.};
    thus A9 \/ {.x9.} = A \/ {x};
    thus P[A9 \/ {x9}] by A2,A6,ZFMISC_1:46;
  end;
A7: B is finite;
A8: X[{}] by A1;
  X[B] from FINSET_1:sch 2(A7,A8,A3);
  hence thesis;
end;

scheme
  FinSubInd2{ X() -> non empty set, P[Element of Fin X()] } : for B being non
  empty Element of Fin X() holds P[B]
provided
A1: for x being Element of X() holds P[{.x.}] and
A2: for B1,B2 being non empty Element of Fin X() st P[B1] & P[B2] holds
P[B1 \/ B2]
proof
  defpred X[set] means $1 <> {} implies ex B9 being Element of Fin X() st B9 =
  $1 & P[B9];
  let B be non empty Element of Fin X();
A3: for x,A being set st x in B & A c= B & X[A] holds X[A \/ {x}]
  proof
    let x,A be set such that
A4: x in B and
A5: A c= B and
A6: A <> {} implies ex B9 being Element of Fin X() st B9 = A & P[B9] and
    A \/ {x} <> {};
    reconsider x9 = x as Element of X() by A4,Th14;
    reconsider A9 = A as Element of Fin X() by A5,Th16;
    take A9 \/ {.x9.};
    thus A9 \/ {.x9.} =A \/ {x};
A7: P[{.x9.}] by A1;
    per cases;
    suppose
      A = {};
      hence thesis by A1;
    end;
    suppose
      A <> {};
      hence thesis by A2,A6,A7;
    end;
  end;
A8: X[{}];
A9: B is finite;
  X[B] from FINSET_1:sch 2(A9,A8,A3);
  hence thesis;
end;

scheme
  FinSubInd3{ X() -> non empty set, P[set] } : for B being Element of Fin X()
  holds P[B]
provided
A1: P[{}.X()] and
A2: for B9 being (Element of Fin X()), b being Element of X() holds P[B9
] implies P[B9 \/ {b}]
proof
A3: for B9 being (Element of Fin X()), b being Element of X() holds P[B9] &
  not b in B9 implies P[B9 \/ {b}] by A2;
A4: P[{}.X()] by A1;
  thus for B being Element of Fin X() holds P[B] from FinSubInd1(A4, A3);
end;

definition
  let X, Y be non empty set;
  let F be BinOp of Y;
  let B be Element of Fin X;
  let f be Function of X,Y;
  assume that
A1: B <> {} or F is having_a_unity and
A2: F is commutative and
A3: F is associative;
  func F $$ (B,f) -> Element of Y means
  :Def3:
  ex G being Function of Fin X, Y
st it = G.B & (for e being Element of Y st e is_a_unity_wrt F holds G.{} = e) &
(for x being Element of X holds G.{x} = f.x) & for B9 being Element of Fin X st
B9 c= B & B9 <> {} for x being Element of X st x in B \ B9 holds G.(B9 \/ {x})
  = F.(G.B9,f.x);
  existence
  proof
    defpred X[set] means ex G being Function of Fin X, Y st (for e being
Element of Y st e is_a_unity_wrt F holds G.{} = e) & (for x being Element of X
holds G.{x} = f.x) & for B9 being Element of Fin X st B9 c= $1 & B9 <> {} for x
    being Element of X st x in $1 \ B9 holds G.(B9 \/ {x}) = F.(G.B9, f.x);
A4: for B9 being Element of Fin X, b being Element of X holds X[B9] & not
    b in B9 implies X[B9 \/ {b}]
    proof
      let B be (Element of Fin X), x be Element of X;
      given G being Function of Fin X, Y such that
A5:   for e being Element of Y st e is_a_unity_wrt F holds G.{} = e and
A6:   for x being Element of X holds G.{x} = f.x and
A7:   for B9 being Element of Fin X st B9 c= B & B9 <> {} for x being
      Element of X st x in B \ B9 holds G.(B9 \/ {x}) = F.(G.B9, f.x);
      assume
A8:   not x in B;
      thus ex G being Function of Fin X, Y st (for e being Element of Y st e
is_a_unity_wrt F holds G.{} = e) & (for x being Element of X holds G.{x} = f.x)
      & for B9 being Element of Fin X st B9 c= B \/ {x} & B9 <> {} for x9 being
Element of X st x9 in (B \/ {x}) \ B9 holds G.(B9 \/ {x9}) = F.(G.B9, f.x9)
      proof
        defpred X[set,set] means (for C being Element of Fin X st C <> {} & C
c= B & $1 = C \/ {.x.} holds $2 = F.(G.C, f.x)) & ((not ex C being Element of
        Fin X st C <> {} & C c= B & $1 = C \/ {.x.}) implies $2 = G.$1);
A9:     now
          let B9 be Element of Fin X;
          thus ex y being Element of Y st X[B9,y]
          proof
A10:        now
              given C being Element of Fin X such that
A11:          C <> {} and
A12:          C c= B and
A13:          B9 = C \/ {x};
              take y = F.(G.C, f.x);
              for C being Element of Fin X st C <> {} & C c= B & B9 = C
              \/ {x} holds y = F.(G.C, f.x)
              proof
                not x in C by A8,A12;
                then
A14:            C c= B9 \ {x} by A13,XBOOLE_1:7,ZFMISC_1:40;
                B9 \ {x} = C \ {x} by A13,XBOOLE_1:40;
                then
A15:            B9 \ {x} = C by A14,XBOOLE_0:def 10;
                let C1 be Element of Fin X such that
                C1 <> {} and
A16:            C1 c= B and
A17:            B9 = C1 \/ {x};
                not x in C1 by A8,A16;
                then
A18:            C1 c= B9 \ {x} by A17,XBOOLE_1:7,ZFMISC_1:40;
                B9 \{x} = C1 \ {x} by A17,XBOOLE_1:40;
                hence thesis by A18,A15,XBOOLE_0:def 10;
              end;
              hence thesis by A11,A12,A13;
            end;
            now
              assume
A19:          not ex C being Element of Fin X st C <> {} & C c= B &
              B9 = C \/ {x};
              take y = G.B9;
              thus for C being Element of Fin X st C <> {} & C c= B & B9 = C
              \/ {x} holds y = F.(G.C, f.x) by A19;
              thus (not ex C being Element of Fin X st C <> {} & C c= B & B9 =
              C \/ {x}) implies y = G.B9;
            end;
            hence thesis by A10;
          end;
        end;
        consider H being Function of Fin X, Y such that
A20:    for B9 being Element of Fin X holds X[B9,H.B9] from FUNCT_2:
        sch 3 (A9);
        take J = H;
        now
          given C being Element of Fin X such that
A21:      C <> {} and
A22:      C c= B and
A23:      {x} = C \/ {x};
          C c= {x} by A23,XBOOLE_1:7;
          then C = {x} by A21,ZFMISC_1:39;
          then x in C by TARSKI:def 1;
          hence contradiction by A8,A22;
        end;
        then
A24:    J.{x} = G.{.x.} by A20;
        thus for e being Element of Y st e is_a_unity_wrt F holds J.{} = e
        proof
          reconsider E = {} as Element of Fin X by FINSUB_1:18;
          not ex C being Element of Fin X st C <> {} & C c= B & E = C \/ { x};
          then J.E = G.E by A20;
          hence thesis by A5;
        end;
        thus for x being Element of X holds J.{x} = f.x
        proof
          let x9 be Element of X;
          now
            given C being Element of Fin X such that
A25:        C <> {} and
A26:        C c= B and
A27:        {x9} = C \/ {x};
            C c= {x9} by A27,XBOOLE_1:7;
            then
A28:        C = {x9} by A25,ZFMISC_1:39;
            x = x9 by A27,XBOOLE_1:7,ZFMISC_1:24;
            then x in C by A28,TARSKI:def 1;
            hence contradiction by A8,A26;
          end;
          hence J.{x9} = G.{.x9.} by A20
            .= f.x9 by A6;
        end;
        let B9 be Element of Fin X such that
A29:    B9 c= B \/ {x} and
A30:    B9 <> {};
        let x9 be Element of X;
        assume
A31:    x9 in (B \/ {x}) \ B9;
        then
A32:    not x9 in B9 by XBOOLE_0:def 5;
        per cases;
        suppose
A33:      x in B9;
          then
A34:      x9 in B by A31,A32,Th6;
          then
A35:      {x9} c= B by ZFMISC_1:37;
          per cases;
          suppose
A36:        B9 = {x};
            hence J.(B9 \/ {.x9.}) = F.(G.{.x9.}, f.x) by A20,A35
              .= F.(f.x9, f.x) by A6
              .= F.(f.x, f.x9) by A2,BINOP_1:def 2
              .= F.(J.B9, f.x9) by A6,A24,A36;
          end;
          suppose
            B9 <> {x};
            then not B9 c= {x} by A30,ZFMISC_1:39;
            then
A37:        B9 \ {x} <> {} by XBOOLE_1:37;
            set C = (B9 \ {.x.}) \/ {.x9.};
            not x9 in B9 \ {x} by A31,XBOOLE_0:def 5;
            then
A38:        x9 in B \(B9 \ {x}) by A34,XBOOLE_0:def 5;
            B9 \ {x} c= B by A29,XBOOLE_1:43;
            then
A39:        C c= B by A34,Th8;
            B9 \/ {.x9.} = B9 \/ {x} \/ {x9} by A33,ZFMISC_1:46
              .= (B9 \ {x}) \/ {x} \/ {x9} by XBOOLE_1:39
              .= C \/ {.x.} by XBOOLE_1:4;
            hence J.(B9 \/ {.x9.}) = F.(G.C, f.x) by A20,A39
              .= F.(F.(G.(B9\{.x.}), f.x9), f.x) by A7,A29,A37,A38,XBOOLE_1
            :43
              .= F.(G.(B9\{.x.}),F.(f.x9, f.x)) by A3,BINOP_1:def 3
              .= F.(G.(B9\{x}),F.(f.x, f.x9)) by A2,BINOP_1:def 2
              .= F.(F.(G.(B9\{.x.}), f.x), f.x9) by A3,BINOP_1:def 3
              .= F.(J.((B9\{.x.})\/{.x.}), f.x9) by A20,A29,A37,XBOOLE_1:43
              .= F.(J.(B9 \/ {x}), f.x9) by XBOOLE_1:39
              .= F.(J.B9, f.x9) by A33,ZFMISC_1:46;
          end;
        end;
        suppose
A40:      not x in B9;
          then
A41:      B9 c= B by A29,Th5;
A42:      not ex C being Element of Fin X st C <> {} & C c= B & B9 = C
          \/ {x} by A40,Th6;
          per cases;
          suppose
A43:        x <> x9;
            then x9 in B by A31,Th6;
            then
A44:        x9 in B \ B9 by A32,XBOOLE_0:def 5;
            not x in B9 \/ {x9} by A40,A43,Th6;
            then not ex C being Element of Fin X st C <> {} & C c= B & B9 \/
            {x9} = C \/ {x} by Th6;
            hence J.(B9 \/ {.x9.}) = G.(B9 \/ {.x9.}) by A20
              .= F.(G.B9, f.x9) by A7,A30,A41,A44
              .= F.(J.B9, f.x9) by A20,A42;
          end;
          suppose
            x = x9;
            hence J.(B9 \/ {.x9.}) = F.(G.B9, f.x9) by A20,A29,A30,A40,
            Th5
              .= F.(J.B9, f.x9) by A20,A42;
          end;
        end;
      end;
    end;
A45: X[{}.X]
    proof
      consider e being Element of Y such that
A46:  (ex e being Element of Y st e is_a_unity_wrt F) implies e =
      the_unity_wrt F;
      defpred X[set,set] means (for x9 being Element of X st $1 = {x9} holds
$2 = f.x9) & (not (ex x9 being Element of X st $1 = {x9}) implies $2 = e);
A47:  now
        let x be Element of Fin X;
A48:    now
          given x9 being Element of X such that
A49:      x = {x9};
          take y = f.x9;
          thus for x9 being Element of X st x = {x9} holds y = f.x9 by A49,
          ZFMISC_1:6;
          thus not (ex x9 being Element of X st x = {x9}) implies y = e by A49;
        end;
        now
          assume
A50:      not ex x9 being Element of X st x = {x9};
          take y = e;
          thus (for x9 being Element of X st x = {x9} holds y = f.x9) & (not (
          ex x9 being Element of X st x = {x9}) implies y = e) by A50;
        end;
        hence ex y being Element of Y st X[x,y] by A48;
      end;
      consider G9 being Function of Fin X, Y such that
A51:  for B9 being Element of Fin X holds X[B9,G9.B9] from FUNCT_2:
      sch 3(A47 );
      take G = G9;
      thus for e being Element of Y st e is_a_unity_wrt F holds G.{} = e
      proof
        reconsider E = {} as Element of Fin X by FINSUB_1:18;
        let e9 be Element of Y such that
A52:    e9 is_a_unity_wrt F;
        not ex x9 being Element of X st E = {x9};
        hence G.{} = e by A51
          .= e9 by A46,A52,BINOP_1:def 8;
      end;
      thus for x being Element of X holds G.{.x.} = f.x by A51;
      thus for B9 being Element of Fin X st B9 c= {}.X & B9 <> {} for x being
      Element of X st x in {}.X \ B9 holds G.(B9 \/ {x}) = F.(G.B9, f.x);
    end;
    for B being Element of Fin X holds X[B] from FinSubInd1(A45,A4);
    then consider G being Function of Fin X, Y such that
A53: ( ( for e being Element of Y st e is_a_unity_wrt F holds G.{} =
e)& for x being Element of X holds G.{x} = f.x )& for B9 being Element of Fin X
st B9 c= B & B9 <> {} for x being Element of X st x in B \ B9 holds G.(B9 \/ {x
    }) = F.(G .B9, f.x);
    take G.B, G;
    thus thesis by A53;
  end;
  uniqueness
  proof
    let x,y be Element of Y;
    given G1 being Function of Fin X, Y such that
A54: x = G1.B and
A55: for e being Element of Y st e is_a_unity_wrt F holds G1.{} = e and
A56: for x being Element of X holds G1.{x} = f.x and
A57: for B9 being Element of Fin X st B9 c= B & B9 <> {} for x being
    Element of X st x in B \ B9 holds G1.(B9 \/ {x}) = F.(G1.B9,f.x);
    given G2 being Function of Fin X, Y such that
A58: y = G2.B and
A59: for e being Element of Y st e is_a_unity_wrt F holds G2.{} = e and
A60: for x being Element of X holds G2.{x} = f.x and
A61: for B9 being Element of Fin X st B9 c= B & B9 <> {} for x being
    Element of X st x in B \ B9 holds G2.(B9 \/ {x}) = F.(G2.B9,f.x);
    per cases;
    suppose
A62:  B = {};
      then
A63:  the_unity_wrt F is_a_unity_wrt F by A1,Th22;
      hence x = the_unity_wrt F by A54,A55,A62
        .= y by A58,A59,A62,A63;
    end;
    suppose
A64:  B <> {};
      defpred X[set] means $1 c= B & $1 <> {} implies G1.$1 = G2.$1;
A65:  for B9 being Element of Fin X, b being Element of X holds X[B9] &
      not b in B9 implies X[B9 \/ {b}]
      proof
        let B9 be (Element of Fin X), x be Element of X;
        assume
A66:    B9 c= B & B9 <> {} implies G1.B9 = G2.B9;
        assume
A67:    not x in B9;
        assume
A68:    B9 \/ {x} c= B;
        then
A69:    B9 c= B by Th8;
        assume B9 \/ {x} <> {};
        x in B by A68,Th8;
        then
A70:    x in B \ B9 by A67,XBOOLE_0:def 5;
        per cases;
        suppose
A71:      B9 = {};
          hence G1.(B9 \/ {x}) = f.x by A56
            .= G2.(B9 \/ {x}) by A60,A71;
        end;
        suppose
A72:      B9 <> {};
          hence G1.(B9 \/ {x}) = F.(G1.B9, f.x) by A57,A69,A70
            .= G2.(B9 \/ {x}) by A61,A66,A69,A70,A72;
        end;
      end;
A73:  X[{}.X];
      for B9 being Element of Fin X holds X[B9] from FinSubInd1(A73,
      A65 );
      hence thesis by A54,A58,A64;
    end;
  end;
end;

canceled;

theorem Th25:
  for X, Y being non empty set for F being BinOp of Y for B being
Element of Fin X for f being Function of X,Y st (B <> {} or F is having_a_unity
) & F is idempotent & F is commutative & F is associative for IT being Element
of Y holds IT = F $$ (B,f) iff ex G being Function of Fin X, Y st IT = G.B & (
  for e being Element of Y st e is_a_unity_wrt F holds G.{} = e) & (for x being
Element of X holds G.{x} = f.x) & for B9 being Element of Fin X st B9 c= B & B9
  <> {} for x being Element of X st x in B holds G.(B9 \/ {x}) = F.(G.B9,f.x)
proof
  let X, Y be non empty set;
  let F be BinOp of Y;
  let B be Element of Fin X;
  let f be Function of X,Y;
  assume that
A1: B <> {} or F is having_a_unity and
A2: F is idempotent and
A3: F is commutative and
A4: F is associative;
  let IT be Element of Y;
  thus IT = F $$ (B,f) implies ex G being Function of Fin X, Y st IT = G.B & (
  for e being Element of Y st e is_a_unity_wrt F holds G.{} = e) & (for x being
Element of X holds G.{x} = f.x) & for B9 being Element of Fin X st B9 c= B & B9
  <> {} for x being Element of X st x in B holds G.(B9 \/ {x}) = F.(G.B9,f.x)
  proof
    assume IT = F $$ (B,f);
    then consider G being Function of Fin X, Y such that
A5: IT = G.B & for e being Element of Y st e is_a_unity_wrt F holds G.
    {} = e and
A6: for x being Element of X holds G.{x} = f.x and
A7: for B9 being Element of Fin X st B9 c= B & B9 <> {} for x being
    Element of X st x in B \ B9 holds G.(B9 \/ {x}) = F.(G.B9,f.x) by A1,A3,A4,
    Def3;
    for B9 being Element of Fin X st B9 c= B & B9 <> {} for x being
    Element of X st x in B holds G.(B9 \/ {x}) = F.(G.B9,f.x)
    proof
      let B9 be Element of Fin X such that
A8:   B9 c= B and
A9:   B9 <> {};
      let x be Element of X such that
A10:  x in B;
      per cases;
      suppose
        x in B9;
        then
A11:    B9 \/ {x} = B9 by ZFMISC_1:46;
        then
A12:    {x} c= B9 by XBOOLE_1:7;
        not x in B9 \ {x} by ZFMISC_1:64;
        then
A13:    x in B \ (B9 \ {x}) by A10,XBOOLE_0:def 5;
        per cases;
        suppose
A14:      B9 = {x};
          hence G.(B9 \/ {x}) = f.x by A6
            .= F.(f.x, f.x) by A2,BINOP_1:def 4
            .= F.(G.B9,f.x) by A6,A14;
        end;
        suppose
          B9 <> {x};
          then not B9 c= {x} by A12,XBOOLE_0:def 10;
          then B9 \ {x} <> {} by XBOOLE_1:37;
          then
A15:      G.(B9 \ {.x.} \/ {.x.}) = F.(G.(B9 \ {.x.}), f.x) by A7,A8,
          A13,XBOOLE_1:1;
          thus G.(B9 \/ {x}) = G.(B9 \ {x} \/ {x}) by XBOOLE_1:39
            .= F.(G.(B9 \ {x}), F.(f.x,f.x)) by A2,A15,BINOP_1:def 4
            .= F.(F.(G.(B9 \ {.x.}),f.x), f.x) by A4,BINOP_1:def 3
            .= F.(G.B9,f.x) by A11,A15,XBOOLE_1:39;
        end;
      end;
      suppose
        not x in B9;
        then x in B \ B9 by A10,XBOOLE_0:def 5;
        hence thesis by A7,A8,A9;
      end;
    end;
    hence thesis by A5,A6;
  end;
  given G being Function of Fin X, Y such that
A16: ( IT = G.B & for e being Element of Y st e is_a_unity_wrt F holds G
  .{} = e ) & for x being Element of X holds G.{x} = f.x and
A17: for B9 being Element of Fin X st B9 c= B & B9 <> {} for x being
  Element of X st x in B holds G.(B9 \/ {x}) = F.(G.B9,f.x);
  for B9 being Element of Fin X st B9 c= B & B9 <> {} for x being Element
  of X st x in B \ B9 holds G.(B9 \/ {x}) = F.(G.B9,f.x)
  proof
    let B9 be Element of Fin X such that
A18: B9 c= B & B9 <> {};
    let x be Element of X;
    assume x in B \ B9;
    then x in B by XBOOLE_0:def 5;
    hence thesis by A17,A18;
  end;
  hence thesis by A1,A3,A4,A16,Def3;
end;

reserve X, Y for non empty set,
  F for (BinOp of Y),
  B for (Element of Fin X),
  f for Function of X,Y;

theorem Th26:
  F is commutative & F is associative implies for b being Element
  of X holds F $$ ({.b.},f) = f.b
proof
  assume
A1: F is commutative & F is associative;
  let b be Element of X;
  ex G being Function of Fin X, Y st F $$ ({.b.},f) = G.{b} & (for e being
Element of Y st e is_a_unity_wrt F holds G.{} = e) & ( for x being Element of X
holds G.{x} = f.x) & for B9 being Element of Fin X st B9 c= {b} & B9 <> {} for
x being Element of X st x in {b} \ B9 holds G.(B9 \/ {x}) = F.(G.B9,f.x) by A1,
  Def3;
  hence thesis;
end;

theorem Th27:
  F is idempotent & F is commutative & F is associative implies
  for a,b being Element of X holds F $$ ({.a,b.},f) = F.(f.a, f.b)
proof
  assume
A1: F is idempotent & F is commutative & F is associative;
  let a,b be Element of X;
  consider G being Function of Fin X, Y such that
A2: F $$ ({.a,b.},f) = G.{a,b} and
  for e being Element of Y st e is_a_unity_wrt F holds G.{} = e and
A3: for x being Element of X holds G.{x} = f.x and
A4: for B9 being Element of Fin X st B9 c= {a,b} & B9 <> {} for x being
  Element of X st x in {a,b} holds G.(B9 \/ {x}) = F.(G.B9,f.x) by A1,Th25;
A5: b in {a,b} by TARSKI:def 2;
  thus F $$ ({.a,b.},f) = G.({.a.} \/ {.b.}) by A2,ENUMSET1:41
    .= F.(G.{.a.}, f.b) by A4,A5,ZFMISC_1:12
    .= F.(f.a, f.b) by A3;
end;

theorem Th28:
  F is idempotent & F is commutative & F is associative implies
  for a,b,c being Element of X holds F $$ ({.a,b,c.},f) = F.(F.(f.a, f.b), f.c)
proof
  assume
A1: F is idempotent & F is commutative & F is associative;
  let a,b,c be Element of X;
  consider G being Function of Fin X, Y such that
A2: F $$ ({.a,b,c.},f) = G.{a,b,c} and
  for e being Element of Y st e is_a_unity_wrt F holds G.{} = e and
A3: for x being Element of X holds G.{x} = f.x and
A4: for B9 being Element of Fin X st B9 c= {a,b,c} & B9 <> {} for x
  being Element of X st x in {a,b,c} holds G.(B9 \/ {x}) = F.(G.B9,f.x) by A1,
  Th25;
A5: b in {a,b,c} by ENUMSET1:def 1;
A6: G.{a,b} = G.({a} \/ {b}) by ENUMSET1:41
    .= F.(G.{.a.}, f.b) by A4,A5,Th3
    .= F.(f.a, f.b) by A3;
A7: c in {a,b,c} by ENUMSET1:def 1;
  thus F $$ ({.a,b,c.},f) = G.({.a,b.} \/ {.c.}) by A2,ENUMSET1:43
    .= F.(F.(f.a, f.b), f.c) by A4,A6,A7,Th4;
end;
 
:: If B is non empty

theorem Th29:
  F is idempotent & F is commutative & F is associative & B <> {}
implies for x being Element of X holds F $$(B \/ {.x.}, f) = F.(F $$(B,f),f.x)
proof
  assume
A1: F is idempotent & F is commutative & F is associative;
  assume
A2: B <> {};
  then consider G9 being Function of Fin X, Y such that
A3: F $$ (B,f) = G9.B and
  for e being Element of Y st e is_a_unity_wrt F holds G9.{} = e and
A4: for x being Element of X holds G9.{x} = f.x and
A5: for B9 being Element of Fin X st B9 c= B & B9 <> {} for x being
  Element of X st x in B holds G9.(B9 \/ {x}) = F.(G9.B9,f.x) by A1,Th25;
  let x be Element of X;
  consider G being Function of Fin X, Y such that
A6: F $$ (B \/ {.x.},f) = G.(B \/ {.x.}) and
  for e being Element of Y st e is_a_unity_wrt F holds G.{} = e and
A7: for x being Element of X holds G.{x} = f.x and
A8: for B9 being Element of Fin X st B9 c= B \/ {x} & B9 <> {} for x9
being Element of X st x9 in B \/ {x} holds G.(B9 \/ {x9}) = F.(G.B9,f.x9) by A1
  ,Th25;
  defpred X[set] means $1 <> {} & $1 c= B implies G.$1 = G9.$1;
A9: for B9 being Element of Fin X, b being Element of X holds X[B9] implies
  X[B9 \/ {b}]
  proof
A10: B c= B \/ {x} by XBOOLE_1:7;
    let C be (Element of Fin X), y be Element of X such that
A11: C <> {} & C c= B implies G.C = G9.C;
    assume that
    C \/ {y} <> {} and
A12: C \/ {y} c= B;
A13: C c= B & y in B by A12,Th8;
    per cases;
    suppose
A14:  C = {};
      hence G.(C \/ {y}) = f.y by A7
        .= G9.(C \/ {y}) by A4,A14;
    end;
    suppose
A15:  C <> {};
      hence G.(C \/ {y}) = F.(G9.C, f.y) by A8,A11,A13,A10,XBOOLE_1:1
        .= G9.(C \/ {y}) by A5,A13,A15;
    end;
  end;
A16: X[{}.X];
A17: for C being Element of Fin X holds X[C] from FinSubInd3(A16,A9);
  x in B \/ {x} by Th6;
  hence F $$(B \/ {.x.}, f) = F.(G.B, f.x) by A2,A6,A8,XBOOLE_1:7
    .= F.(F $$(B,f),f.x) by A2,A3,A17;
end;

theorem Th30:
  F is idempotent & F is commutative & F is associative implies
for B1,B2 being Element of Fin X st B1 <> {} & B2 <> {} holds F $$ (B1 \/ B2,f)
  = F.(F $$ (B1,f),F $$ (B2,f))
proof
  assume that
A1: F is idempotent and
A2: F is commutative and
A3: F is associative;
  let B1,B2 be Element of Fin X;
  defpred X[Element of Fin X] means $1 <> {} implies F $$ (B1 \/ $1,f) = F.(F
  $$ (B1,f),F $$ ($1,f));
  assume
A4: B1 <> {};
A5: for B9 being Element of Fin X, b being Element of X holds X[B9] implies
  X[B9 \/ {.b.}]
  proof
    let B be (Element of Fin X), x be Element of X such that
A6: B <> {} implies F $$ (B1 \/ B,f) = F.(F $$ (B1,f),F $$ (B,f)) and
    B \/ {x} <> {};
    per cases;
    suppose
A7:   B = {};
      hence
      F $$ (B1 \/ (B \/ {.x.}),f) = F.(F $$ (B1,f), f.x) by A1,A2,A3,A4,
      Th29
        .= F.(F $$ (B1,f),F $$ (B \/ {.x.},f)) by A2,A3,A7,Th26;
    end;
    suppose
A8:   B <> {};
      thus F $$ (B1 \/ (B \/ {.x.}),f) = F $$ (B1 \/ B \/ {.x.},f) by XBOOLE_1
      :4
        .= F.(F.(F $$ (B1,f),F $$ (B,f)), f.x) by A1,A2,A3,A6,A8,Th29
        .= F.(F $$ (B1,f),F.(F $$ (B,f), f.x)) by A3,BINOP_1:def 3
        .= F.(F $$ (B1,f),F $$ (B \/ {.x.},f)) by A1,A2,A3,A8,Th29;
    end;
  end;
A9: X[{}.X];
  for B2 being Element of Fin X holds X[B2] from FinSubInd3(A9,A5);
  hence thesis;
end;

theorem
  F is commutative & F is associative & F is idempotent implies for x
  being Element of X st x in B holds F.(f.x,F$$(B,f)) = F$$(B,f)
proof
  assume that
A1: F is commutative & F is associative and
A2: F is idempotent;
  let x be Element of X;
  assume
A3: x in B;
  thus F.(f.x,F$$(B,f)) = F.(F$$({.x.},f), F$$(B,f)) by A1,Th26
    .= F$$({.x.} \/ B, f) by A1,A2,A3,Th30
    .= F$$(B,f) by A3,ZFMISC_1:46;
end;

theorem
  F is commutative & F is associative & F is idempotent implies for B,C
being Element of Fin X st B <> {} & B c= C holds F.(F$$(B,f),F$$(C,f)) = F$$(C,
  f)
proof
  assume
A1: F is commutative & F is associative & F is idempotent;
  let B,C be Element of Fin X such that
A2: B <> {} and
A3: B c= C;
  C <> {} by A2,A3;
  hence F.(F$$(B,f),F$$(C,f)) = F$$(B \/ C,f) by A1,A2,Th30
    .= F$$(C,f) by A3,XBOOLE_1:12;
end;

theorem Th33:
  B <> {} & F is commutative & F is associative & F is idempotent
implies for a being Element of Y st for b being Element of X st b in B holds f.
  b = a holds F$$(B,f) = a
proof
  assume that
A1: B <> {} and
A2: F is commutative & F is associative and
A3: F is idempotent;
  let a be Element of Y;
  defpred X[Element of Fin X] means (for b being Element of X st b in $1 holds
  f.b = a) implies F$$($1,f) = a;
A4: for B1,B2 being non empty Element of Fin X holds X[B1] & X[B2] implies X
  [B1 \/ B2]
  proof
    let B1,B2 be non empty Element of Fin X;
    assume that
A5: ( (for b being Element of X st b in B1 holds f.b = a) implies F$$
(B1,f) = a)&( (for b being Element of X st b in B2 holds f.b = a) implies F$$(
    B2,f) = a) and
A6: for b being Element of X st b in B1 \/ B2 holds f.b = a;
A7: now
      let b be Element of X;
      assume b in B2;
      then b in B1 \/ B2 by XBOOLE_0:def 3;
      hence f.b = a by A6;
    end;
    now
      let b be Element of X;
      assume b in B1;
      then b in B1 \/ B2 by XBOOLE_0:def 3;
      hence f.b = a by A6;
    end;
    hence F$$(B1 \/ B2,f) = F.(a,a) by A2,A3,A5,A7,Th30
      .= a by A3,BINOP_1:def 4;
  end;
A8: for x being Element of X holds X[{.x.}]
  proof
    let x be Element of X such that
A9: for b being Element of X st b in {x} holds f.b = a;
A10: x in { x } by TARSKI:def 1;
    thus F$$({.x.},f) = f.x by A2,Th26
      .= a by A9,A10;
  end;
  for B being non empty Element of Fin X holds X[B] from FinSubInd2(A8,
  A4);
  hence thesis by A1;
end;

theorem Th34:
  F is commutative & F is associative & F is idempotent implies
  for a being Element of Y st f.:B = {a} holds F$$(B,f) = a
proof
  assume
A1: F is commutative & F is associative & F is idempotent;
  let a be Element of Y;
  assume
A2: f.:B = {a};
A3: for b being Element of X st b in B holds f.b = a
  proof
    let b be Element of X;
    assume b in B;
    then f.b in {a} by A2,FUNCT_2:43;
    hence thesis by TARSKI:def 1;
  end;
  B <> {} by A2,RELAT_1:149;
  hence thesis by A1,A3,Th33;
end;

theorem Th35:
  F is commutative & F is associative & F is idempotent implies
for f,g being Function of X,Y for A,B being Element of Fin X st A <> {} & f.:A
  = g.:B holds F$$(A,f) = F$$(B,g)
proof
  assume that
A1: F is commutative and
A2: F is associative and
A3: F is idempotent;
  let f,g be Function of X,Y;
  defpred X[Element of Fin X] means $1 <> {} implies for B being Element of
  Fin X st f.:$1 = g.:B holds F$$($1,f) = F$$(B,g);
A4: for B9 being Element of Fin X, b being Element of X holds X[B9] implies
  X[B9 \/ {.b.}]
  proof
    let A be (Element of Fin X), x be Element of X such that
A5: A <> {} implies for B being Element of Fin X st f.:A = g.:B holds
    F$$(A,f) = F$$(B,g);
    assume A \/ {x} <> {};
    let B be Element of Fin X such that
A6: f.:(A \/ {x}) = g.:B;
    per cases;
    suppose
      f.x in f.:A;
      then consider x9 being Element of X such that
A7:   x9 in A and
A8:   f.x9 = f.x by FUNCT_2:116;
A9:   g.:B = f.:A \/ Im(f,x) by A6,RELAT_1:153
        .= f.:A \/ {f.x} by Th13
        .= f.:A by A7,A8,FUNCT_2:43,ZFMISC_1:46;
      thus F$$(A \/ {.x.},f) = F.(F$$(A,f), f.x) by A1,A2,A3,A7,Th29
        .= F.(F$$(A \/ {.x9.},f), f.x) by A7,ZFMISC_1:46
        .= F.(F.(F$$(A,f), f.x9), f.x) by A1,A2,A3,A7,Th29
        .= F.(F$$(A,f), F.(f.x9, f.x9)) by A2,A8,BINOP_1:def 3
        .= F.(F$$(A,f), f.x9) by A3,BINOP_1:def 4
        .= F$$(A \/ {.x9.},f) by A1,A2,A3,A7,Th29
        .= F$$(A,f) by A7,ZFMISC_1:46
        .= F$$(B,g) by A5,A7,A9;
    end;
    suppose
A10:  not f.x in f.:A;
      per cases;
      suppose
A11:    A = {};
        then
A12:    g.:B = Im(f,x) by A6
          .= {f.x} by Th13;
        thus F$$(A \/ {.x.},f) = f.x by A1,A2,A11,Th26
          .= F$$(B,g) by A1,A2,A3,A12,Th34;
      end;
      suppose
A13:    A <> {};
        reconsider B1 = B \ g"{f.x} as Element of Fin X by Th16;
A14:    now
          assume B1 = {};
          then B c= g"{f.x} by XBOOLE_1:37;
          then
A15:      g.:B c= g.:g"{f.x} by RELAT_1:156;
A16:      f.:A misses {f.x} by A10,ZFMISC_1:56;
          g.:g"{f.x} c= {f.x} by FUNCT_1:145;
          then f.:(A \/ {x}) c= {f.x} by A6,A15,XBOOLE_1:1;
          then f.:A \/ f.:{x} c= {f.x} by RELAT_1:153;
          then f.:A = f.:A /\ {f.x} by XBOOLE_1:11,28
            .= {} by A16,XBOOLE_0:def 7;
          hence contradiction by A13,Th19;
        end;
        reconsider B2 = B /\ g"{f.x} as Element of Fin X by Th16,XBOOLE_1:17;
A17:    B = B1 \/ B2 by XBOOLE_1:51;
A18:    for b being Element of X st b in B2 holds g.b = f.x
        proof
          let b be Element of X;
          assume b in B2;
          then b in g"{f.x} by XBOOLE_0:def 4;
          then g.b in {f.x} by FUNCT_1:def 13;
          hence thesis by TARSKI:def 1;
        end;
A19:    f.:A = f.:A \ {f.x} by A10,ZFMISC_1:65
          .= f.:A \ Im(f,x) by Th13
          .= (f.:A \/ f.:{x}) \ f.:{x} by XBOOLE_1:40
          .= f.:(A \/ {x}) \ Im(f,x) by RELAT_1:153
          .= g.:B \ {f.x} by A6,Th13
          .= g.:B1 by Th11;
        x in A \/ {x} by Th6;
        then consider x9 being Element of X such that
A20:    x9 in B and
A21:    g.x9 = f.x by A6,FUNCT_2:43,116;
        x9 in g"{f.x} by A21,Th12;
        then
A22:    B2 <> {} by A20,XBOOLE_0:def 4;
        thus F$$(A \/ {.x.},f) = F.(F$$(A,f), f.x) by A1,A2,A3,A13,Th29
          .= F.(F$$(B1,g), f.x) by A5,A13,A19
          .= F.(F$$(B1,g), F$$(B2,g)) by A1,A2,A3,A22,A18,Th33
          .= F$$(B,g) by A1,A2,A3,A17,A14,A22,Th30;
      end;
    end;
  end;
A23: X[{}.X];
A24: for A being Element of Fin X holds X[A] from FinSubInd3(A23,A4);
  let A,B be Element of Fin X;
  assume A <> {} & f.:A = g.:B;
  hence thesis by A24;
end;

theorem
  for F,G being BinOp of Y st F is idempotent & F is commutative & F is
  associative & G is_distributive_wrt F for B being Element of Fin X st B <> {}
for f being Function of X,Y for a being Element of Y holds G.(a,F$$(B,f)) = F$$
  (B,G[;](a,f))
proof
  let F,G be BinOp of Y such that
A1: F is idempotent and
A2: F is commutative & F is associative and
A3: G is_distributive_wrt F;
  let B be Element of Fin X such that
A4: B<> {};
  let f be Function of X,Y;
  let a be Element of Y;
  defpred X[Element of Fin X] means G.(a,F$$($1,f)) = F$$($1,G[;](a,f));
A5: for B1,B2 being non empty Element of Fin X holds X[B1] & X[B2] implies X
  [B1 \/ B2]
  proof
    let B1,B2 be non empty Element of Fin X;
    assume
A6: G.(a,F$$(B1,f)) = F$$(B1,G[;](a,f)) & G.(a,F$$(B2,f)) = F$$(B2,G [;](a,f));
    thus G.(a,F$$(B1 \/ B2,f)) = G.(a,F.(F$$(B1,f),F$$(B2,f))) by A1,A2,Th30
      .= F.(F$$(B1,G[;](a,f)), F$$(B2,G[;] (a,f))) by A3,A6,BINOP_1:23
      .= F$$(B1 \/ B2,G[;](a,f)) by A1,A2,Th30;
  end;
A7: for x being Element of X holds X[{.x.}]
  proof
    let x be Element of X;
    thus G.(a,F$$({.x.},f)) = G.(a,f.x) by A2,Th26
      .= (G[;](a,f)).x by FUNCOP_1:66
      .= F$$({.x.},G[;](a,f)) by A2,Th26;
  end;
  for B being non empty Element of Fin X holds X[B] from FinSubInd2(A7,
  A5);
  hence thesis by A4;
end;

theorem
  for F,G being BinOp of Y st F is idempotent & F is commutative & F is
  associative & G is_distributive_wrt F for B being Element of Fin X st B <> {}
for f being Function of X,Y for a being Element of Y holds G.(F$$(B,f),a) = F$$
  (B,G[:](f,a))
proof
  let F,G be BinOp of Y such that
A1: F is idempotent and
A2: F is commutative & F is associative and
A3: G is_distributive_wrt F;
  let B be Element of Fin X such that
A4: B<> {};
  let f be Function of X,Y;
  let a be Element of Y;
  defpred X[Element of Fin X] means G.(F$$($1,f),a) = F$$($1,G[:](f,a));
A5: for B1,B2 being non empty Element of Fin X holds X[B1] & X[B2] implies X
  [B1 \/ B2]
  proof
    let B1,B2 be non empty Element of Fin X;
    assume
A6: G.(F$$(B1,f),a) = F$$(B1,G[:](f,a)) & G.(F$$(B2,f),a) = F$$(B2,G [:](f,a));
    thus G.(F$$(B1 \/ B2,f),a) = G.(F.(F$$(B1,f),F$$(B2,f)),a) by A1,A2,Th30
      .= F.(F$$(B1,G[:](f,a)), F$$(B2,G[:] (f,a))) by A3,A6,BINOP_1:23
      .= F$$(B1 \/ B2,G[:](f,a)) by A1,A2,Th30;
  end;
A7: for x being Element of X holds X[{.x.}]
  proof
    let x be Element of X;
    thus G.(F$$({.x.},f),a) = G.(f.x,a) by A2,Th26
      .= (G[:](f,a)).x by FUNCOP_1:60
      .= F$$({.x.},G[:](f,a)) by A2,Th26;
  end;
  for B being non empty Element of Fin X holds X[B] from FinSubInd2(A7,
  A5);
  hence thesis by A4;
end;

definition
  let X, Y be non empty set;
  let f be Function of X,Y;
  let A be Element of Fin X;
  redefine func f.:A -> Element of Fin Y;
  coherence by Lm1;
end;

theorem Th38:
  for A, X, Y being non empty set for F being BinOp of A st F is
  idempotent & F is commutative & F is associative for B being Element of Fin X
st B <> {} for f being Function of X,Y holds for g being Function of Y,A holds
  F$$(f.:B,g) = F$$(B,g*f)
proof
  let A, X, Y be non empty set, F be BinOp of A such that
A1: F is idempotent and
A2: F is commutative & F is associative;
  let B be Element of Fin X such that
A3: B <> {};
  let f be Function of X,Y;
  let g be Function of Y,A;
  defpred X[Element of Fin X] means F$$(f.:$1,g) = F$$($1,g*f);
A4: dom f = X by FUNCT_2:def 1;
A5: for B1,B2 being non empty Element of Fin X holds X[B1] & X[B2] implies X
  [B1 \/ B2]
  proof
    let B1,B2 be non empty Element of Fin X;
    assume
A6: F$$(f.:B1,g) = F$$(B1,g*f) & F$$(f.:B2,g) = F$$(B2,g*f);
    B1 c= X by FINSUB_1:def 5;
    then X /\ B1 <> {} by XBOOLE_1:28;
    then X meets B1 by XBOOLE_0:def 7;
    then
A7: f.:B1 <> {} by A4,RELAT_1:151;
    B2 c= X by FINSUB_1:def 5;
    then X /\ B2 <> {} by XBOOLE_1:28;
    then X meets B2 by XBOOLE_0:def 7;
    then
A8: f.:B2 <> {} by A4,RELAT_1:151;
    thus F$$(f.:(B1 \/ B2),g) = F$$(f.:B1 \/ f.:B2, g) by RELAT_1:153
      .= F.(F$$(f.:B1,g), F$$(f.:B2,g)) by A1,A2,A7,A8,Th30
      .= F$$(B1 \/ B2,g*f) by A1,A2,A6,Th30;
  end;
A9: for x being Element of X holds X[{.x.}]
  proof
    let x be Element of X;
    f.:{.x.} = Im(f,x);
    hence F$$(f.:{.x.},g) = F$$({.f.x.},g) by A4,FUNCT_1:117
      .= g.(f.x) by A2,Th26
      .= (g*f).x by FUNCT_2:21
      .= F$$({.x.},g*f) by A2,Th26;
  end;
  for B being non empty Element of Fin X holds X[B] from FinSubInd2(A9,
  A5);
  hence thesis by A3;
end;

theorem Th39:
  F is commutative & F is associative & F is idempotent implies
  for Z being non empty set for G being BinOp of Z st G is commutative & G is
associative & G is idempotent for f being Function of X, Y for g being Function
  of Y,Z st for x,y being Element of Y holds g.(F.(x,y)) = G.(g.x,g.y) for B
  being Element of Fin X st B <> {} holds g.(F$$(B,f)) = G$$(B,g*f)
proof
  assume that
A1: F is commutative & F is associative and
A2: F is idempotent;
  let Z be non empty set, G be BinOp of Z such that
A3: G is commutative & G is associative and
A4: G is idempotent;
  let f be Function of X,Y;
  let g be Function of Y,Z such that
A5: for x,y being Element of Y holds g.(F.(x, y)) = G.(g.x, g.y);
  defpred X[Element of Fin X] means $1 <> {} implies g.(F$$($1,f)) = G$$($1,g*
  f);
A6: for B9 being Element of Fin X, b being Element of X holds X[B9] implies
  X[B9 \/ {.b.}]
  proof
    let B be (Element of Fin X), x be Element of X;
    assume that
A7: B <> {} implies g.(F$$(B,f)) = G$$(B,g*f) and
    B \/ {x} <> {};
    per cases;
    suppose
A8:   B = {};
      hence g.(F$$(B \/ {.x.},f)) = g.(f.x) by A1,Th26
        .= (g*f).x by FUNCT_2:21
        .= G$$(B \/ {.x.},g*f) by A3,A8,Th26;
    end;
    suppose
A9:   B <> {};
      hence g.(F$$(B \/ {.x.},f)) = g.(F.(F$$(B,f), f.x)) by A1,A2,Th29
        .= G.(g.(F$$(B,f)), g.(f.x)) by A5
        .= G.(G$$(B,g*f), (g*f).x) by A7,A9,FUNCT_2:21
        .= G$$(B \/ {.x.},g*f) by A3,A4,A9,Th29;
    end;
  end;
A10: X[{}.X];
  thus for B being Element of Fin X holds X[B] from FinSubInd3(A10,A6 );
end;
 
:: If F has a unity

theorem Th40:
  F is commutative & F is associative & F is having_a_unity
  implies for f holds F$$({}.X,f) = the_unity_wrt F
proof
  assume
A1: F is commutative & F is associative & F is having_a_unity;
  let f;
  the_unity_wrt F is_a_unity_wrt F & ex G being Function of Fin X, Y st F
$$ ( {}.X,f) = G.{}.X & (for e being Element of Y st e is_a_unity_wrt F holds G
.{} = e) & (for x being Element of X holds G.{x} = f.x) & for B9 being Element
  of Fin X st B9 c= {}.X & B9 <> {} for x being Element of X st x in {}.X \ B9
  holds G.( B9 \/ {x}) = F.(G.B9,f.x) by A1,Def3,Th22;
  hence thesis;
end;

theorem Th41:
  F is idempotent & F is commutative & F is associative & F is
having_a_unity implies for x being Element of X holds F $$(B \/ {.x.}, f) = F.(
  F $$(B,f),f.x)
proof
  assume that
A1: F is idempotent and
A2: F is commutative & F is associative;
  assume
A3: F is having_a_unity;
  let x be Element of X;
A4: {} = {}.X;
  now
    assume
A5: B = {};
    hence F $$(B \/ {.x.}, f) = f.x by A2,Th26
      .= F.(the_unity_wrt F, f.x) by A3,Th23
      .= F.(F $$(B,f),f.x) by A2,A3,A4,A5,Th40;
  end;
  hence thesis by A1,A2,Th29;
end;

theorem
  F is idempotent & F is commutative & F is associative & F is
having_a_unity implies for B1,B2 being Element of Fin X holds F $$ (B1 \/ B2,f)
  = F.(F $$ (B1,f),F $$ (B2,f))
proof
  assume that
A1: F is idempotent and
A2: F is commutative & F is associative and
A3: F is having_a_unity;
  let B1,B2 be Element of Fin X;
  now
A4: {} = {}.X;
    assume
A5: B1 = {} or B2 = {};
    per cases by A5;
    suppose
A6:   B2 = {};
      hence F $$ (B1 \/ B2,f) = F.(F$$(B1,f),the_unity_wrt F) by A3,Th23
        .= F.(F $$ (B1,f),F $$ (B2,f)) by A2,A3,A4,A6,Th40;
    end;
    suppose
A7:   B1 = {};
      hence F $$ (B1 \/ B2,f) = F.(the_unity_wrt F, F$$(B2,f)) by A3,Th23
        .= F.(F $$ (B1,f),F $$ (B2,f)) by A2,A3,A4,A7,Th40;
    end;
  end;
  hence thesis by A1,A2,Th30;
end;

theorem
  F is commutative & F is associative & F is idempotent & F is
  having_a_unity implies for f,g being Function of X,Y for A,B being Element of
  Fin X st f.:A = g.:B holds F$$(A,f) = F$$(B,g)
proof
  assume that
A1: F is commutative & F is associative and
A2: F is idempotent and
A3: F is having_a_unity;
  let f,g be Function of X,Y;
  let A,B be Element of Fin X such that
A4: f.:A = g.:B;
  now
    assume
A5: A = {};
    then A = {}.X;
    then
A6: F$$(A,f) = the_unity_wrt F by A1,A3,Th40;
    B = {}.X by A4,A5,Th19,RELAT_1:149;
    hence thesis by A1,A3,A6,Th40;
  end;
  hence thesis by A1,A2,A4,Th35;
end;

theorem Th44:
  for A, X, Y being non empty set for F being BinOp of A st F is
  idempotent & F is commutative & F is associative & F is having_a_unity for B
  being Element of Fin X for f being Function of X,Y holds for g being Function
  of Y,A holds F$$(f.:B,g) = F$$(B,g*f)
proof
  let A, X, Y be non empty set, F be BinOp of A such that
A1: F is idempotent and
A2: F is commutative & F is associative and
A3: F is having_a_unity;
  let B be Element of Fin X;
  let f be Function of X,Y;
  let g be Function of Y,A;
  now
    assume
A4: B = {};
    then f.:B = {}.Y by RELAT_1:149;
    then
A5: F$$(f.:B,g) = the_unity_wrt F by A2,A3,Th40;
    B = {}.X by A4;
    hence thesis by A2,A3,A5,Th40;
  end;
  hence thesis by A1,A2,Th38;
end;

theorem
  F is commutative & F is associative & F is idempotent & F is
having_a_unity implies for Z being non empty set for G being BinOp of Z st G is
  commutative & G is associative & G is idempotent & G is having_a_unity for f
  being Function of X, Y for g being Function of Y,Z st g.the_unity_wrt F =
  the_unity_wrt G & for x,y being Element of Y holds g.(F.(x,y)) = G.(g.x,g.y)
  for B being Element of Fin X holds g.(F$$(B,f)) = G$$(B,g*f)
proof
  assume that
A1: F is commutative & F is associative and
A2: F is idempotent and
A3: F is having_a_unity;
  let Z be non empty set;
  let G be BinOp of Z such that
A4: G is commutative & G is associative and
A5: G is idempotent and
A6: G is having_a_unity;
  let f be Function of X, Y;
  let g be Function of Y,Z such that
A7: g.the_unity_wrt F = the_unity_wrt G and
A8: for x,y being Element of Y holds g.(F.(x,y)) = G.(g.x,g.y);
  let B be Element of Fin X;
  per cases;
  suppose
    B = {};
    then
A9: B = {}.X;
    hence g.(F$$(B,f)) = g.the_unity_wrt F by A1,A3,Th40
      .= G$$(B,g*f) by A4,A6,A7,A9,Th40;
  end;
  suppose
    B <> {};
    hence thesis by A1,A2,A4,A5,A8,Th39;
  end;
end;

definition
  let A be set;
  func FinUnion A -> BinOp of Fin A means
  :Def4:
  for x,y being Element of Fin A holds it.(x,y) = (x \/ y);
  existence
  proof
    deffunc U(Element of Fin A,Element of Fin A) = $1 \/ $2;
    ex IT being BinOp of Fin A st for x,y being Element of Fin A holds IT.
    (x,y) = U(x,y) from BINOP_1:sch 4;
    hence thesis;
  end;
  uniqueness
  proof
    let IT, IT9 be BinOp of Fin A such that
A1: for x,y being Element of Fin A holds IT.(x,y) = (x \/ y) and
A2: for x,y being Element of Fin A holds IT9.(x,y) = (x \/ y);
    now
      let x,y be Element of Fin A;
      thus IT.(x,y) = (x \/ y) by A1
        .= IT9.(x,y) by A2;
    end;
    hence IT = IT9 by BINOP_1:2;
  end;
end;

reserve A for set,
  x,y,z for Element of Fin A;

canceled 3;

theorem Th49:
  FinUnion A is idempotent
proof
  let x;
  thus FinUnion A.(x,x) = x \/ x by Def4
    .= x;
end;

theorem Th50:
  FinUnion A is commutative
proof
  let x,y;
  thus FinUnion A.(x,y) = y \/ x by Def4
    .= FinUnion A.(y,x) by Def4;
end;

theorem Th51:
  FinUnion A is associative
proof
  let x,y,z;
  thus FinUnion A.(FinUnion A.(x,y), z) = FinUnion A.(x \/ y, z) by Def4
    .= x \/ y \/ z by Def4
    .= x \/ (y \/ z) by XBOOLE_1:4
    .= FinUnion A.(x, y \/ z) by Def4
    .= FinUnion A.(x, FinUnion A.(y,z)) by Def4;
end;

theorem Th52:
  {}.A is_a_unity_wrt FinUnion A
proof
  thus for x holds FinUnion A.({}.A, x) =x
  proof
    let x;
    thus FinUnion A.({}.A, x) = {} \/ x by Def4
      .= x;
  end;
  let x;
  thus FinUnion A.(x, {}.A) = x \/ {} by Def4
    .= x;
end;

theorem Th53:
  FinUnion A is having_a_unity
proof
  {}.A is_a_unity_wrt FinUnion A by Th52;
  hence thesis by Def2;
end;

theorem
  the_unity_wrt FinUnion A is_a_unity_wrt FinUnion A
proof
  FinUnion A is having_a_unity by Th53;
  hence thesis by Th22;
end;

theorem Th55:
  the_unity_wrt FinUnion A = {}
proof
  {}.A is_a_unity_wrt FinUnion A by Th52;
  hence thesis by BINOP_1:def 8;
end;

reserve X,Y for non empty set,
  A for set,
  f for (Function of X, Fin A),
  i,j,k for (Element of X);

definition
  let X be non empty set, A be set;
  let B be Element of Fin X;
  let f be Function of X, Fin A;
  func FinUnion(B,f) -> Element of Fin A equals
  FinUnion A $$(B,f);
  coherence;
end;

theorem
  FinUnion({.i.},f) = f.i
proof
  FinUnion A is commutative by Th50;
  hence thesis by Th26,Th51;
end;

theorem
  FinUnion({.i,j.},f) = f.i \/ f.j
proof
  FinUnion A is idempotent & FinUnion A is commutative by Th49,Th50;
  hence FinUnion({.i,j.},f) = FinUnion A.(f.i, f.j) by Th27,Th51
    .= f.i \/ f.j by Def4;
end;

theorem
  FinUnion({.i,j,k.},f) = f.i \/ f.j \/ f.k
proof
  FinUnion A is idempotent & FinUnion A is commutative by Th49,Th50;
  hence FinUnion({.i,j,k.},f) = FinUnion A.(FinUnion A.(f.i, f.j), f.k) by
  Th28,Th51
    .= FinUnion A.(f.i \/ f.j, f.k) by Def4
    .= f.i \/ f.j \/ f.k by Def4;
end;

theorem Th59:
  FinUnion({}.X,f) = {}
proof
  FinUnion A is commutative & FinUnion A is associative by Th50,Th51;
  hence FinUnion({}.X,f) = the_unity_wrt FinUnion A by Th40,Th53
    .= {} by Th55;
end;

theorem Th60:
  for B being Element of Fin X holds FinUnion(B \/ {.i.}, f) =
  FinUnion(B,f) \/ f.i
proof
  let B be Element of Fin X;
A1: FinUnion A is associative by Th51;
  FinUnion A is idempotent & FinUnion A is commutative by Th49,Th50;
  hence FinUnion(B \/ {.i.}, f) = FinUnion A.(FinUnion(B,f), f.i) by A1,Th41
  ,Th53
    .= FinUnion(B,f) \/ f.i by Def4;
end;

theorem Th61:
  for B being Element of Fin X holds FinUnion(B,f) = union (f.:B)
proof
  defpred X[Element of Fin X] means FinUnion($1,f) = union (f.:$1);
A1: for B being (Element of Fin X), i st X[B] holds X[B \/ {.i.}]
  proof
    let B be (Element of Fin X), i;
    assume FinUnion(B,f) = union (f.:B);
    hence FinUnion(B \/ {.i.}, f) = union(f.:B) \/ f.i by Th60
      .= union (f.:B) \/ union {f.i} by ZFMISC_1:31
      .= union (f.:B \/ {f.i}) by ZFMISC_1:96
      .= union (f.:B \/ Im(f,i)) by Th13
      .= union (f.:(B \/ {i})) by RELAT_1:153;
  end;
  FinUnion({}.X,f) = union {} by Th59,ZFMISC_1:2
    .= union (f.:{}.X) by RELAT_1:149;
  then
A2: X[{}.X];
  thus for B being Element of Fin X holds X[B] from FinSubInd3(A2,A1 );
end;

theorem
  for B1,B2 being Element of Fin X holds FinUnion(B1 \/ B2, f) =
  FinUnion(B1,f) \/ FinUnion(B2,f)
proof
  let B1,B2 be Element of Fin X;
  thus FinUnion(B1 \/ B2, f) = union(f.:(B1 \/ B2)) by Th61
    .= union(f.:B1 \/ f .:B2) by RELAT_1:153
    .= union(f.:B1) \/ union(f.:B2) by ZFMISC_1:96
    .= FinUnion(B1,f) \/ union(f.:B2) by Th61
    .= FinUnion(B1,f) \/ FinUnion(B2,f) by Th61;
end;

theorem
  for B being Element of Fin X for f being Function of X,Y holds for g
  being Function of Y,Fin A holds FinUnion(f.:B,g) = FinUnion(B,g*f)
proof
  let B be Element of Fin X;
  let f be Function of X,Y;
  let g be Function of Y,Fin A;
  thus FinUnion(f.:B,g) = union (g.:(f.:B)) by Th61
    .= union ((g*f).:B) by RELAT_1:159
    .= FinUnion(B,g*f) by Th61;
end;

theorem Th64:
  for A,X being non empty set, Y being set for G being BinOp of A
st G is commutative & G is associative & G is idempotent for B being Element of
Fin X st B <> {} for f being (Function of X,Fin Y), g being Function of Fin Y,A
  st for x,y being Element of Fin Y holds g.(x \/ y) = G.(g.x,g.y) holds g.(
  FinUnion(B,f)) = G$$(B,g*f)
proof
  let A,X be non empty set, Y be set, G be BinOp of A such that
A1: G is commutative & G is associative & G is idempotent;
  let B be Element of Fin X such that
A2: B <> {};
  let f be (Function of X,Fin Y), g be Function of Fin Y,A such that
A3: for x,y being Element of Fin Y holds g.(x \/ y) = G.(g.x,g.y);
A4: now
    let x,y be Element of Fin Y;
    thus g.(FinUnion Y.(x,y)) = g.(x \/ y) by Def4
      .= G.(g.x,g.y) by A3;
  end;
A5: FinUnion Y is idempotent by Th49;
  FinUnion Y is commutative & FinUnion Y is associative by Th50,Th51;
  hence thesis by A1,A5,A2,A4,Th39;
end;

theorem Th65:
  for Z being non empty set, Y being set for G being BinOp of Z st
G is commutative & G is associative & G is idempotent & G is having_a_unity for
  f being Function of X, Fin Y for g being Function of Fin Y,Z st g.{}.Y =
the_unity_wrt G & for x,y being Element of Fin Y holds g.(x \/ y) = G.(g.x,g.y)
  for B being Element of Fin X holds g.(FinUnion(B,f)) = G$$(B,g*f)
proof
  let Z be non empty set, Y be set;
  let G be BinOp of Z such that
A1: G is commutative & G is associative and
A2: G is idempotent and
A3: G is having_a_unity;
  let f be Function of X, Fin Y;
  let g be Function of Fin Y,Z such that
A4: g.{}.Y = the_unity_wrt G and
A5: for x,y being Element of Fin Y holds g.(x \/ y) = G.(g.x,g.y);
  let B be Element of Fin X;
A6: {} = {}.X;
A7: {} = {}.Fin Y;
  per cases;
  suppose
A8: B = {};
    then
A9: f.:B = {} by RELAT_1:149;
    thus g.(FinUnion(B,f)) = g.{}.Y by A6,A8,Th59
      .= G$$(f.:B,g) by A1,A3,A4,A7,A9,Th40
      .= G$$(B,g*f) by A1,A2,A3,Th44;
  end;
  suppose
    B <> {};
    hence thesis by A1,A2,A5,Th64;
  end;
end;

definition
  let A be set;
  func singleton A -> Function of A, Fin A means
  :Def6:
  for x being set st x in A holds it.x = {x};
  existence
  proof
    deffunc U(set) = {$1};
A1: now
      let x be set;
      assume
A2:   x in A;
      then reconsider A9 = A as non empty set;
      reconsider x9 = x as Element of A9 by A2;
      {.x9.} in Fin A9;
      hence U(x) in Fin A;
    end;
    thus ex f being Function of A, Fin A st for x being set st x in A holds f.
    x = U(x) from FUNCT_2:sch 2(A1);
  end;
  uniqueness
  proof
    let IT,IT9 be Function of A, Fin A such that
A3: for x being set st x in A holds IT.x = {x} and
A4: for x being set st x in A holds IT9.x = {x};
    now
      let x be set;
      assume
A5:   x in A;
      then IT.x = {x} by A3;
      hence IT.x = IT9.x by A4,A5;
    end;
    hence thesis by FUNCT_2:18;
  end;
end;

canceled;

theorem Th67:
  for A being non empty set for f being Function of A, Fin A holds
  f = singleton A iff for x being Element of A holds f.x = {x}
proof
  let A be non empty set;
  let f be Function of A, Fin A;
  thus f = singleton A implies for x being Element of A holds f.x = {x} by
  Def6;
  assume for x being Element of A holds f.x = {x};
  then for x be set holds x in A implies f.x = {x};
  hence thesis by Def6;
end;

theorem Th68:
  for x being set, y being Element of X holds x in singleton X.y iff x = y
proof
  let x be set, y be Element of X;
  singleton X.y = {y} by Th67;
  hence thesis by TARSKI:def 1;
end;

theorem
  for x,y,z being Element of X st x in singleton X.z & y in singleton X.
  z holds x = y
proof
  let x,y,z be Element of X;
  assume that
A1: x in singleton X.z and
A2: y in singleton X.z;
  x = z by A1,Th68;
  hence thesis by A2,Th68;
end;

Lm2: for D being non empty set, X, P being set for f being Function of X,D
holds f.:P c= D;

theorem Th70:
  for B being Element of Fin X, x being set holds x in FinUnion(B,
  f) iff ex i being Element of X st i in B & x in f.i
proof
  let B be Element of Fin X, x be set;
A1: now
    assume x in union (f.:B);
    then consider Z being set such that
A2: x in Z and
A3: Z in f .:B by TARSKI:def 4;
    f.:B is Subset of Fin A by Lm2;
    then reconsider Z as Element of Fin A by A3;
    consider i being Element of X such that
A4: i in B & Z = f.i by A3,FUNCT_2:116;
    take i9 = i;
    thus i9 in B & x in f.i9 by A2,A4;
  end;
  now
    given i being Element of X such that
A5: i in B and
A6: x in f.i;
    f.i in f.:B by A5,FUNCT_2:43;
    hence x in union (f.:B) by A6,TARSKI:def 4;
  end;
  hence thesis by A1,Th61;
end;

theorem
  for B being Element of Fin X holds FinUnion(B, singleton X) = B
proof
  let B be Element of Fin X;
  now
    let x be set;
    thus x in FinUnion(B, singleton X) implies x in B
    proof
      assume x in FinUnion(B, singleton X);
      then ex i being Element of X st i in B & x in singleton X.i by
      Th70;
      hence thesis by Th68;
    end;
    assume
A1: x in B;
    then reconsider x9 = x as Element of X by Th14;
    x in singleton X.x9 by Th68;
    hence x in FinUnion(B, singleton X) by A1,Th70;
  end;
  hence thesis by TARSKI:2;
end;

theorem
  for Y,Z being set for f being Function of X, Fin Y for g being
  Function of Fin Y, Fin Z st g.{}.Y = {}.Z & for x,y being Element of Fin Y
holds g.(x \/ y) = g.x \/ g.y for B being Element of Fin X holds g.(FinUnion(B,
  f)) = FinUnion(B,g*f)
proof
  let Y,Z be set;
  let f be Function of X, Fin Y;
  let g be Function of Fin Y, Fin Z;
  assume that
A1: g.{}.Y = {}.Z and
A2: for x,y being Element of Fin Y holds g.(x \/ y) = g.x \/ g.y;
A3: g.{}.Y = the_unity_wrt FinUnion Z by A1,Th55;
A4: now
    let x,y be Element of Fin Y;
    thus g.(x \/ y) = g.x \/ g.y by A2
      .= FinUnion Z.(g.x,g.y) by Def4;
  end;
  let B be Element of Fin X;
A5: FinUnion Z is idempotent by Th49;
  FinUnion Z is associative & FinUnion Z is commutative by Th50,Th51;
  hence thesis by A5,A3,A4,Th53,Th65;
end;
