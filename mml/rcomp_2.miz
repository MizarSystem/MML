:: Half Open Intervals in Real Numbers
::  by Yatsuka Nakamura
::
:: Received February 1, 2002
:: Copyright (c) 2002 Association of Mizar Users

environ

 vocabularies ARYTM, BOOLE, SQUARE_1, RCOMP_1, SUPINF_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XREAL_0, REAL_1,
      XXREAL_0, XXREAL_1, RCOMP_1;
 constructors MEMBERED, RCOMP_1;
 registrations NUMBERS, XREAL_0, MEMBERED;
 requirements SUBSET, BOOLE;
 definitions TARSKI, XXREAL_1;
 theorems TARSKI, XREAL_0, RCOMP_1, XBOOLE_0, XXREAL_0, NUMBERS, XXREAL_1;

begin

reserve s,g,h,r,p,p1,p2,q,q1,q2,x,y,z for real number;

definition
  let g be real number,s be ext-real number;
  redefine func [. g,s .[ -> Subset of REAL equals
  { r where r is Real : g<=r & r<s };
  coherence
  proof
    now
      let e be set;
      assume e in [. g,s .[;
      then
A1:   ex r being Element of ExtREAL st e = r & g <= r & r<s;
      g in REAL by XREAL_0:def 1;
      hence e in REAL by A1,XXREAL_0:46;
    end;
    hence thesis by TARSKI:def 3;
  end;
  compatibility
  proof
    let X be Subset of REAL;
    set Y = {r where r is Real: g<=r & r<s };
A2: [. g,s .[ c= Y
    proof
      let e be set;
      assume e in [. g,s .[;
      then consider r being Element of ExtREAL such that
A3:   e = r & g <= r & r<s;
      g in REAL by XREAL_0:def 1;
      then r in REAL by A3,XXREAL_0:46;
      hence e in Y by A3;
    end;
    Y c= [. g,s .[
    proof
      let e be set;
      assume e in Y;
      then consider r being Element of REAL such that
A4:   e = r & g <= r & r<s;
      r in REAL;
      hence e in [. g,s .[ by A4,NUMBERS:31;
    end;
    hence X = [. g,s .[ iff X = Y by A2,XBOOLE_0:def 10;
  end;
end;

definition
  let g be ext-real number,s be real number;
  redefine func ]. g,s .] -> Subset of REAL equals
  { r where r is Real : g<r & r<=s };
  coherence
  proof
    now
      let e be set;
      assume e in ]. g,s .];
      then
A1:   ex r being Element of ExtREAL st e = r & g < r & r<=s;
      s in REAL by XREAL_0:def 1;
      hence e in REAL by A1,XXREAL_0:47;
    end;
    hence thesis by TARSKI:def 3;
  end;
  compatibility
  proof
    let X be Subset of REAL;
    set Y = {r where r is Real: g<r & r<=s };
A2: ]. g,s .] c= Y
    proof
      let e be set;
      assume e in ]. g,s .];
      then consider r being Element of ExtREAL such that
A3:   e = r & g < r & r<=s;
      s in REAL by XREAL_0:def 1;
      then r in REAL by A3,XXREAL_0:47;
      hence e in Y by A3;
    end;
    Y c= ]. g,s .]
    proof
      let e be set;
      assume e in Y;
      then consider r being Element of REAL such that
A4:   e = r & g < r & r<=s;
      r in REAL;
      hence e in ]. g,s .] by A4,NUMBERS:31;
    end;
    hence X = ]. g,s .] iff X = Y by A2,XBOOLE_0:def 10;
  end;
end;

canceled 17;

theorem
  r in [.p,g.[ & s in [.p,g.[ implies [.r,s.] c= [.p,g.[
proof
  assume
A1: r in [.p,g.[ & s in [.p,g.[;
  then
A2: p<=r & r<g by XXREAL_1:3;
A3: p<=s & s<g by A1,XXREAL_1:3;
  let x be set;
  assume
A4: x in [.r,s.];
  then reconsider x2=x as Real;
  r<=x2 & x2<=s by A4,XXREAL_1:1;
  then p<=x2 & x2<g by A2,A3,XXREAL_0:2;
  hence x in [.p,g.[;
end;

theorem
  r in ].p,g.] & s in ].p,g.] implies [.r,s.] c= ].p,g.]
proof
  assume
A1: r in ].p,g.] & s in ].p,g.];
  then
A2: p<r & r<=g by XXREAL_1:2;
A3: p<s & s<=g by A1,XXREAL_1:2;
  let x be set;
  assume
A4: x in [.r,s.];
  then reconsider x2=x as Real;
  r<=x2 & x2<=s by A4,XXREAL_1:1;
  then p<x2 & x2<=g by A2,A3,XXREAL_0:2;
  hence x in ].p,g.];
end;
