:: Half Open Intervals in Real Numbers
::  by Yatsuka Nakamura
::
:: Received February 1, 2002
:: Copyright (c) 2002 Association of Mizar Users

environ

 vocabularies ARYTM, BOOLE, SQUARE_1, RCOMP_1, NAT_1, SUPINF_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XREAL_0, REAL_1,
      XXREAL_0, XXREAL_1, RCOMP_1;
 constructors MEMBERED, RCOMP_1;
 registrations NUMBERS, XREAL_0, MEMBERED;
 requirements SUBSET, BOOLE;
 definitions TARSKI, XXREAL_1;
 theorems TARSKI, XREAL_0, RCOMP_1, XBOOLE_0, XXREAL_0, NUMBERS, XXREAL_1;

begin

 reserve s,g,h,r,p,p1,p2,q,q1,q2,x,y,z for real number;

canceled;

theorem
  y < x & z < x iff max(y,z) < x by XXREAL_0:29,31;

definition let g be real number,s be ext-real number;
 redefine func [. g,s .[ -> Subset of REAL equals
  { r where r is Real : g<=r & r<s };
coherence
 proof
   now let e be set;
   assume e in [. g,s .[;
   then A1:  ex r being Element of ExtREAL st e = r & g <= r & r<s;
     g in REAL by XREAL_0:def 1;
   hence e in REAL by A1,XXREAL_0:46;
  end;
  hence thesis by TARSKI:def 3;
 end;
 compatibility
  proof let X be Subset of REAL;
    set Y = {r where r is Real: g<=r & r<s };
A2:  [. g,s .[ c= Y
     proof let e be set;
      assume e in [. g,s .[;
       then consider r being Element of ExtREAL such that
A3:      e = r & g <= r & r<s;
        g in REAL by XREAL_0:def 1;
       then r in REAL by A3,XXREAL_0:46;
      hence e in Y by A3;
     end;
     Y c= [. g,s .[
     proof let e be set;
      assume e in Y;
       then consider r being Element of REAL such that
A4:      e = r & g <= r & r<s;
        r in REAL;
      hence e in [. g,s .[ by A4,NUMBERS:31;
     end;
   hence X = [. g,s .[ iff X = Y by A2,XBOOLE_0:def 10;
  end;
end;

definition let g be ext-real number,s be real number;
 redefine func ]. g,s .] -> Subset of REAL equals
    { r where r is Real : g<r & r<=s };
coherence
 proof
   now let e be set;
   assume e in ]. g,s .];
   then A1:  ex r being Element of ExtREAL st e = r & g < r & r<=s;
     s in REAL by XREAL_0:def 1;
   hence e in REAL by A1,XXREAL_0:47;
  end;
  hence thesis by TARSKI:def 3;
 end;
 compatibility
  proof let X be Subset of REAL;
    set Y = {r where r is Real: g<r & r<=s };
A2:  ]. g,s .] c= Y
     proof let e be set;
      assume e in ]. g,s .];
       then consider r being Element of ExtREAL such that
A3:      e = r & g < r & r<=s;
        s in REAL by XREAL_0:def 1;
       then r in REAL by A3,XXREAL_0:47;
      hence e in Y by A3;
     end;
     Y c= ]. g,s .]
     proof let e be set;
      assume e in Y;
       then consider r being Element of REAL such that
A4:      e = r & g < r & r<=s;
        r in REAL;
      hence e in ]. g,s .] by A4,NUMBERS:31;
     end;
   hence X = ]. g,s .] iff X = Y by A2,XBOOLE_0:def 10;
  end;
end;

theorem
  r in [.p,q.[ iff p<=r & r<q by XXREAL_1:3;

theorem
  r in ].p,q.] iff p<r & r<=q by XXREAL_1:2;

theorem
   for g,s st g<s holds [.g,s.[ = ].g,s.[ \/ {g} by XXREAL_1:131;

theorem
   for g,s st g<s holds ].g,s.] = ].g,s.[ \/ {s} by XXREAL_1:132;

theorem
   [.g,g.[ = {} by XXREAL_1:18;

theorem
   ].g,g.] = {} by XXREAL_1:19;

theorem
   p <= g implies [.g,p.[ = {} by XXREAL_1:27;

theorem
   p <= g implies ].g,p.] = {} by XXREAL_1:26;

theorem
   g <= p & p<=h implies [.g,p.[ \/ [.p,h.[ = [.g,h.[ by XXREAL_1:168;

theorem
   g <= p & p<=h implies ].g,p.] \/ ].p,h.] = ].g,h.] by XXREAL_1:170;

theorem
   g<=p1 & g<=p2 & p1<=h & p2<=h implies
    [.g,h.] = [.g,p1.[ \/ [.p1,p2.] \/ ].p2,h.] by XXREAL_1:179;

theorem
   g<p1 & g<p2 & p1<h & p2<h implies
    ].g,h.[ = ].g,p1.] \/ ].p1,p2.[ \/ [.p2,h.[ by XXREAL_1:180;

theorem   [.q1,q2.[ /\ [.p1,p2.[ = [.max(q1,p1),min(q2,p2).[ by XXREAL_1:139;

theorem   ].q1,q2.] /\ ].p1,p2.] = ].max(q1,p1),min(q2,p2).] by XXREAL_1:141;

theorem   ].p,q.[ c= [.p,q.[ & ].p,q.[ c= ].p,q.]
  & [.p,q.[ c= [.p,q.] & ].p,q.] c= [.p,q.] by XXREAL_1:21,22,
23,24;

theorem
    r in [.p,g.[ & s in [.p,g.[ implies [.r,s.] c= [.p,g.[
proof assume A1: r in [.p,g.[ & s in [.p,g.[;
  then A2: p<=r & r<g by XXREAL_1:3;
  A3: p<=s & s<g by A1,XXREAL_1:3;
  let x be set;assume
    A4: x in [.r,s.];
    then reconsider x2=x as Real;
        r<=x2 & x2<=s by A4,RCOMP_1:48;
    then p<=x2 & x2<g by A2,A3,XXREAL_0:2;
   hence x in [.p,g.[;
end;

theorem
    r in ].p,g.] & s in ].p,g.] implies [.r,s.] c= ].p,g.]
proof assume A1: r in ].p,g.] & s in ].p,g.];
  then A2: p<r & r<=g by XXREAL_1:2;
  A3: p<s & s<=g by A1,XXREAL_1:2;
  let x be set;assume
    A4: x in [.r,s.];
    then reconsider x2=x as Real;
     r<=x2 & x2<=s by A4,RCOMP_1:48;
    then p<x2 & x2<=g by A2,A3,XXREAL_0:2;
   hence x in ].p,g.];
end;

theorem   p<=q & q<=r implies [.p,q.] \/ ].q,r.] = [.p,r.] by XXREAL_1:167;

theorem   p<=q & q<=r implies [.p,q.[ \/ [.q,r.] = [.p,r.] by XXREAL_1:166;

theorem   [.q1,q2.[ meets [.p1,p2.[ implies q2>=p1 by XXREAL_1:96;

theorem   ].q1,q2.] meets ].p1,p2.] implies q2>=p1 by XXREAL_1:92;

theorem   [.q1,q2.[ meets [.p1,p2.[ implies
    [.q1,q2.[ \/ [.p1,p2.[ = [.min(q1,p1),max(q2,p2).[ by XXREAL_1:162;

theorem   ].q1,q2.] meets ].p1,p2.] implies
    ].q1,q2.] \/ ].p1,p2.] = ].min(q1,p1),max(q2,p2).] by XXREAL_1:164;

theorem   [.p1,p2.[ meets [.q1,q2.[ implies
  [.p1,p2.[ \ [.q1,q2.[ = [.p1,q1.[ \/ [.q2,p2.[ by XXREAL_1:198;

theorem   ].p1,p2.] meets ].q1,q2.] implies
  ].p1,p2.] \ ].q1,q2.] = ].p1,q1.] \/ ].q2,p2.] by XXREAL_1:199;
