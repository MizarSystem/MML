:: Transformations in Affine Spaces
::  by Henryk Oryszczyszyn and Krzysztof Pra\.zmowski
::
:: Received May 31, 1990
:: Copyright (c) 1990 Association of Mizar Users
 
environ

 vocabularies XBOOLE_0, SUBSET_1, FUNCT_2, RELAT_1, FUNCT_1, TARSKI, ZFMISC_1,
      RELAT_2, ANALOAF, STRUCT_0, DIRAF, PARSP_1, INCSP_1, AFF_1, TRANSGEO;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, RELAT_2, FUNCT_1, RELSET_1,
      STRUCT_0, ANALOAF, DIRAF, PARTFUN1, FUNCT_2, AFF_1;
 constructors RELAT_2, PARTFUN1, DOMAIN_1, AFF_1, RELSET_1;
 registrations XBOOLE_0, FUNCT_1, RELSET_1, PARTFUN1, FUNCT_2, STRUCT_0,
      ANALOAF, DIRAF;
 requirements SUBSET, BOOLE;
 definitions RELAT_2;
 theorems DOMAIN_1, FUNCT_1, FUNCT_2, ANALOAF, DIRAF, AFF_1, PASCH, TARSKI,
      ZFMISC_1, RELAT_1, XBOOLE_0, STRUCT_0;
 schemes FUNCT_2;

begin

reserve A for non empty set,
  a,b,x,y,z,t for Element of A,
  f,g,h for Permutation of A;

definition
  let A be set, f,g be Permutation of A;
  redefine func g*f -> Permutation of A;
  coherence
  proof
    thus g*f is Permutation of A;
  end;
end;

canceled;

theorem Th2:
  ex x st f.x=y
proof
  rng f = A by FUNCT_2:def 3;
  then ex x being set st x in dom f & f.x = y by FUNCT_1:def 5;
  hence thesis;
end;

canceled;

theorem Th4:
  f.x=y iff (f").y=x
proof
A1: now
    x in A;
    then
A2: x in dom f by FUNCT_2:def 1;
    assume f.x=y;
    hence x=f".y by A2,FUNCT_1:56;
  end;
  rng f = A by FUNCT_2:def 3;
  hence thesis by A1,FUNCT_1:57;
end;

definition
  let A,f,g;
  func f\g -> Permutation of A equals
  (g*f)*g";
  coherence;
end;

scheme
  EXPermutation{A() -> non empty set,P[set,set]}: ex f being Permutation of A(
  ) st for x,y being Element of A() holds f.x=y iff P[x,y]
provided
A1: for x being Element of A() ex y being Element of A() st P[x,y] and
A2: for y being Element of A() ex x being Element of A() st P[x,y] and
A3: for x,y,x9 being Element of A() st P[x,y] & P[x9,y] holds x=x9 and
A4: for x,y,y9 being Element of A() st P[x,y] & P[x,y9] holds y=y9
proof
A5: for x being Element of A() ex y being Element of A() st P[x,y] by A1;
  consider f being Function of A(), A() such that
A6: for x being Element of A() holds P[x,f.x] from FUNCT_2:sch 3(A5);
  now
    let y be set;
    assume
A7: y in A();
    then consider x being Element of A() such that
A8: P[x,y] by A2;
    P[x,f.x] by A6;
    then f.x = y by A4,A7,A8;
    hence y in rng f by FUNCT_2:6;
  end;
  then A() c= rng f by TARSKI:def 3;
  then
A9: rng f = A() by XBOOLE_0:def 10;
  now
    let x1,x2 be set;
    assume that
A10: x1 in A() and
A11: x2 in A();
A12: f.x1 is Element of A() by A10,FUNCT_2:7;
    ( P[x1,f.x1])& P[x2,f.x2] by A6,A10,A11;
    hence f.x1 = f.x2 implies x1 = x2 by A3,A10,A11,A12;
  end;
  then f is one-to-one by FUNCT_2:25;
  then reconsider f as Permutation of A() by A9,FUNCT_2:83;
  take f;
  let x,y be Element of A();
  thus f.x=y implies P[x,y] by A6;
  assume
A13: P[x,y];
  P[x,f.x] by A6;
  hence thesis by A4,A13;
end;

canceled 4;

theorem
  f.(f".x) = x & f".(f.x) = x by Th4;

canceled;

theorem
  f*(id A) = (id A)*f
proof
  f*(id A)=f by FUNCT_2:23;
  hence thesis by FUNCT_2:23;
end;

Lm1: f*g=f*h implies g=h
proof
  assume f*g=f*h;
  then (f"*f)*g=f"*(f*h) by RELAT_1:55;
  then (f"*f)*g=(f"*f)*h by RELAT_1:55;
  then (id A)*g=(f"*f)*h by FUNCT_2:88;
  then (id A)*g=(id A)*h by FUNCT_2:88;
  then g=(id A)*h by FUNCT_2:23;
  hence thesis by FUNCT_2:23;
end;

Lm2: g*f=h*f implies g=h
proof
  assume g*f=h*f;
  then g*(f*f")=(h*f)*f" by RELAT_1:55;
  then g*(f*f")=h*(f*f") by RELAT_1:55;
  then g*(id A)=h*(f*f") by FUNCT_2:88;
  then g*(id A)=h*(id A) by FUNCT_2:88;
  then g=h*(id A) by FUNCT_2:23;
  hence thesis by FUNCT_2:23;
end;

canceled;

theorem
  g*f=h*f or f*g=f*h implies g=h by Lm1,Lm2;

canceled 2;

theorem
  (f*g)\h = (f\h)*(g\h)
proof
  thus (f*g)\h = (h*(f*((id A)*g)))*h" by FUNCT_2:23
    .= (h*(f*((h"*h)*g)))*h" by FUNCT_2:88
    .= (h*(f*(h"*(h*g))))*h" by RELAT_1:55
    .= (h*((f*h")*(h*g)))*h" by RELAT_1:55
    .= ((h*(f*h"))*(h*g))*h" by RELAT_1:55
    .= (h*(f*h"))*((h*g)*h") by RELAT_1:55
    .= (f\h)*(g\h) by RELAT_1:55;
end;

theorem
  (f")\g = (f\g)"
proof
  thus (f\g)" = (g")"*(g*f)" by FUNCT_1:66
    .= (g")"*(f"*g") by FUNCT_1:66
    .= g*(f"*g") by FUNCT_1:65
    .= f"\g by RELAT_1:55;
end;

theorem
  f\(g*h) = (f\h)\g
proof
  thus f\(g*h) = ((g*h)*f)*(h"*g") by FUNCT_1:66
    .= (((g*h)*f)*h")*g" by RELAT_1:55
    .= ((g*(h*f))*h")*g" by RELAT_1:55
    .= (f\h)\g by RELAT_1:55;
end;

theorem
  (id A)\f = id A
proof
  thus (id A)\f = f*f" by FUNCT_2:23
    .= id A by FUNCT_2:88;
end;

theorem
  f\(id A) = f
proof
  thus f\(id A) = f*(id A)" by FUNCT_2:23
    .= f*(id A) by FUNCT_1:67
    .= f by FUNCT_2:23;
end;

theorem
  f.a=a implies (f\g).(g.a)=g.a
proof
  assume
A1: f.a=a;
  g".(g.a) = (g"*g).a by FUNCT_2:21
    .= (id A).a by FUNCT_2:88
    .= a by FUNCT_1:35;
  hence (f\g).(g.a) = (g*f).a by FUNCT_2:21
    .= g.a by A1,FUNCT_2:21;
end;

reserve R for Relation of [:A,A:];

definition
  let A,f,R;
  pred f is_FormalIz_of R means
  :Def2:
  for x,y holds [[x,y],[f.x,f.y]] in R;
end;

canceled;

theorem Th23:
  R is_reflexive_in [:A,A:] implies id A is_FormalIz_of R
proof
  assume
A1: for x being set st x in [:A,A:] holds [x,x] in R;
  let x,y;
A2: [x,y] in [:A,A:] by ZFMISC_1:def 2;
  (id A).x = x & (id A).y = y by FUNCT_1:35;
  hence thesis by A1,A2;
end;

theorem Th24:
  R is_symmetric_in [:A,A:] & f is_FormalIz_of R implies f" is_FormalIz_of R
proof
  assume
A1: for x,y being set st x in [:A,A:] & y in [:A,A:] & [x,y] in R holds
  [y,x] in R;
  assume
A2: for x,y holds [[x,y],[f.x,f.y]] in R;
  let x,y;
A3: [[f".x,f".y],[f.(f".x),f.(f".y)]] in R by A2;
A4: [f".x,f".y] in [:A,A:] & [f.(f".x),f.(f".y)] in [:A,A:] by ZFMISC_1:def
  2;
  f.(f".x) = x & f.(f".y) =y by Th4;
  hence thesis by A1,A3,A4;
end;

theorem
  R is_transitive_in [:A,A:] & f is_FormalIz_of R & g is_FormalIz_of R
  implies (f*g) is_FormalIz_of R
proof
  assume that
A1: for x,y,z being set st x in [:A,A:] & y in [:A,A:] & z in [:A,A:] &
  [x,y] in R & [y,z] in R holds [x,z] in R and
A2: for x,y holds [[x,y],[f.x,f.y]] in R and
A3: for x,y holds [[x,y],[g.x,g.y]] in R;
  let x,y;
  f.(g.x) = (f*g).x & f.(g.y) = (f*g).y by FUNCT_2:21;
  then
A4: [[g.x,g.y],[(f*g).x,(f*g).y]] in R by A2;
A5: [(f*g).x,(f*g).y] in [:A,A:] by ZFMISC_1:def 2;
A6: [x,y] in [:A,A:] & [g.x,g.y] in [:A,A:] by ZFMISC_1:def 2;
  [[x,y],[g.x,g.y]] in R by A3;
  hence thesis by A1,A4,A6,A5;
end;

theorem Th26:
  (for a,b,x,y,z,t st [[x,y],[a,b]] in R & [[a,b],[z,t]] in R & a
  <>b holds [[x,y],[z,t]] in R) & (for x,y,z holds [[x,x],[y,z]] in R) & f
  is_FormalIz_of R & g is_FormalIz_of R implies f*g is_FormalIz_of R
proof
  assume that
A1: for a,b,x,y,z,t st [[x,y],[a,b]] in R & [[a,b],[z,t]] in R & a<>b
  holds [[x,y],[z,t]] in R and
A2: for x,y,z holds [[x,x],[y,z]] in R and
A3: for x,y holds [[x,y],[f.x,f.y]] in R and
A4: for x,y holds [[x,y],[g.x,g.y]] in R;
  let x,y;
  f.(g.x) = (f*g).x & f.(g.y) = (f*g).y by FUNCT_2:21;
  then
A5: [[g.x,g.y],[(f*g).x,(f*g).y]] in R by A3;
A6: now
    assume g.x = g.y;
    then x = y by FUNCT_2:85;
    hence thesis by A2;
  end;
  [[x,y],[g.x,g.y]] in R by A4;
  hence thesis by A1,A5,A6;
end;

definition
  let A;
  let f;
  let R;
  pred f is_automorphism_of R means
  :Def3:
  for x,y,z,t holds ([[x,y],[z,t]] in R iff [[f.x,f.y],[f.z,f.t]] in R);
end;

canceled;

theorem Th28:
  id A is_automorphism_of R
proof
  let x,y,z,t;
A1: (id A).z = z by FUNCT_1:35;
A2: (id A).x = x & (id A).y = y by FUNCT_1:35;
  hence
  [[x,y],[z,t]] in R implies [[(id A).x,(id A).y],[(id A).z,(id A).t]] in
  R by A1,FUNCT_1:35;
  assume [[(id A).x,(id A).y],[(id A).z,(id A).t]] in R;
  hence thesis by A2,A1,FUNCT_1:35;
end;

theorem Th29:
  f is_automorphism_of R implies f" is_automorphism_of R
proof
  assume
A1: for x,y,z,t holds ([[x,y],[z,t]] in R iff [[f.x,f.y],[f.z,f.t]] in R );
  let x,y,z,t;
A2: f.(f".z) = z & f.(f".t) = t by Th4;
  f.(f".x) = x & f.(f".y) = y by Th4;
  hence thesis by A1,A2;
end;

theorem Th30:
  f is_automorphism_of R & g is_automorphism_of R implies g*f
  is_automorphism_of R
proof
  assume that
A1: for x,y,z,t holds ([[x,y],[z,t]] in R iff [[f.x,f.y],[f.z,f.t]] in R ) and
A2: for x,y,z,t holds ([[x,y],[z,t]] in R iff [[g.x,g.y],[g.z,g.t]] in R );
  let x,y,z,t;
A3: g.(f.x) = (g*f).x & g.(f.y) = (g*f).y by FUNCT_2:21;
A4: g.(f.z) = (g*f).z & g.(f.t) = (g*f).t by FUNCT_2:21;
  [[x,y],[z,t]] in R iff [[f.x,f.y],[f.z,f.t]] in R by A1;
  hence thesis by A2,A3,A4;
end;

theorem
  R is_symmetric_in [:A,A:] & R is_transitive_in [:A,A:] & f
  is_FormalIz_of R implies f is_automorphism_of R
proof
  assume that
A1: for x,y being set st x in [:A,A:] & y in [:A,A:] & [x,y] in R holds
  [y,x] in R and
A2: for x,y,z being set st x in [:A,A:] & y in [:A,A:] & z in [:A,A:] &
  [x,y] in R & [y,z] in R holds [x,z] in R and
A3: for x,y holds [[x,y],[f.x,f.y]] in R;
  let x,y,z,t;
A4: [z,t] in [:A,A:] by ZFMISC_1:def 2;
A5: [f.z,f.t] in [:A,A:] by ZFMISC_1:def 2;
A6: [f.x,f.y] in [:A,A:] by ZFMISC_1:def 2;
A7: [x,y] in [:A,A:] by ZFMISC_1:def 2;
A8: now
    [[z,t],[f.z,f.t]] in R by A3;
    then
A9: [[f.z,f.t],[z,t]] in R by A1,A4,A5;
    assume
A10: [[f.x,f.y],[f.z,f.t]] in R;
    [[x,y],[f.x,f.y]] in R by A3;
    then [[x,y],[f.z,f.t]] in R by A2,A7,A6,A5,A10;
    hence [[x,y],[z,t]] in R by A2,A7,A4,A5,A9;
  end;
  now
    [[x,y],[f.x,f.y]] in R by A3;
    then
A11: [[f.x,f.y],[x,y]] in R by A1,A7,A6;
A12: [[z,t],[f.z,f.t]] in R by A3;
    assume [[x,y],[z,t]] in R;
    then [[f.x,f.y],[z,t]] in R by A2,A7,A4,A6,A11;
    hence [[f.x,f.y],[f.z,f.t]] in R by A2,A4,A6,A5,A12;
  end;
  hence thesis by A8;
end;

theorem
  (for a,b,x,y,z,t st [[x,y],[a,b]] in R & [[a,b],[z,t]] in R & a<>b
  holds [[x,y],[z,t]] in R) & (for x,y,z holds [[x,x],[y,z]] in R) & R
  is_symmetric_in [:A,A:] & f is_FormalIz_of R implies f is_automorphism_of R
proof
  assume that
A1: for a,b,x,y,z,t st [[x,y],[a,b]] in R & [[a,b],[z,t]] in R & a<>b
  holds [[x,y],[z,t]] in R and
A2: for x,y,z holds [[x,x],[y,z]] in R and
A3: for x,y being set st x in [:A,A:] & y in [:A,A:] & [x,y] in R holds
  [y,x] in R and
A4: for x,y holds [[x,y],[f.x,f.y]] in R;
A5: for x,y,z holds [[y,z],[x,x]] in R
  proof
    let x,y,z;
A6: [y,z] in [:A,A:] & [x,x] in [:A,A:] by ZFMISC_1:def 2;
    [[x,x],[y,z]] in R by A2;
    hence thesis by A3,A6;
  end;
  let x,y,z,t;
A7: [[x,y],[f.x,f.y]] in R by A4;
A8: [[z,t],[f.z,f.t]] in R by A4;
  [z,t] in [:A,A:] & [f.z,f.t] in [:A,A:] by ZFMISC_1:def 2;
  then
A9: [[f.z,f.t],[z,t]] in R by A3,A8;
A10: now
    assume
A11: [[f.x,f.y],[f.z,f.t]] in R;
A12: now
      assume that
A13:  f.x<>f.y and
A14:  f.z<>f.t;
      [[x,y],[f.z,f.t]] in R by A1,A7,A11,A13;
      hence [[x,y],[z,t]] in R by A1,A9,A14;
    end;
A15: now
      assume f.z = f.t;
      then z=t by FUNCT_2:85;
      hence [[x,y],[z,t]] in R by A5;
    end;
    now
      assume f.x = f.y;
      then x=y by FUNCT_2:85;
      hence [[x,y],[z,t]] in R by A2;
    end;
    hence [[x,y],[z,t]] in R by A15,A12;
  end;
  [x,y] in [:A,A:] & [f.x,f.y] in [:A,A:] by ZFMISC_1:def 2;
  then
A16: [[f.x,f.y],[x,y]] in R by A3,A7;
  now
    assume
A17: [[x,y],[z,t]] in R;
    now
      assume that
A18:  x<>y and
A19:  z<>t;
      [[f.x,f.y],[z,t]] in R by A1,A16,A17,A18;
      hence [[f.x,f.y],[f.z,f.t]] in R by A1,A8,A19;
    end;
    hence [[f.x,f.y],[f.z,f.t]] in R by A2,A5;
  end;
  hence thesis by A10;
end;

theorem
  f is_FormalIz_of R & g is_automorphism_of R implies f\g is_FormalIz_of R
proof
  assume that
A1: for x,y holds [[x,y],[f.x,f.y]] in R and
A2: for x,y,z,t holds ([[x,y],[z,t]] in R iff [[g.x,g.y],[g.z,g.t]] in R );
  let x,y;
A3: [[g".x,g".y],[f.(g".x),f.(g".y)]] in R by A1;
  g.(g".x) = x & g.(g".y) = y by Th4;
  then [[x,y],[g.(f.(g".x)),g.(f.(g".y))]] in R by A2,A3;
  then [[x,y],[(g*f).(g".x),g.(f.(g".y))]] in R by FUNCT_2:21;
  then [[x,y],[(g*f).(g".x),(g*f).(g".y)]] in R by FUNCT_2:21;
  then [[x,y],[((g*f)*g").x,(g*f).(g".y)]] in R by FUNCT_2:21;
  hence thesis by FUNCT_2:21;
end;

reserve AS for non empty AffinStruct;

definition
  let AS;
  let f be Permutation of the carrier of AS;
  pred f is_DIL_of AS means
  :Def4:
  f is_FormalIz_of the CONGR of AS;
end;

reserve a,b,x,y for Element of AS;

canceled;

theorem Th35:
  for f being Permutation of the carrier of AS holds (f is_DIL_of
  AS iff for a,b holds a,b // f.a,f.b )
proof
  let f be Permutation of the carrier of AS;
A1: now
    assume
A2: for a,b holds a,b // f.a,f.b;
    for x,y holds [[x,y],[f.x,f.y]] in the CONGR of AS
    proof
      let x,y;
      x,y // f.x,f.y by A2;
      hence thesis by ANALOAF:def 2;
    end;
    then f is_FormalIz_of the CONGR of AS by Def2;
    hence f is_DIL_of AS by Def4;
  end;
  now
    assume
A3: f is_DIL_of AS;
    let a,b;
    f is_FormalIz_of the CONGR of AS by A3,Def4;
    then [[a,b],[f.a,f.b]] in the CONGR of AS by Def2;
    hence a,b // f.a,f.b by ANALOAF:def 2;
  end;
  hence thesis by A1;
end;

definition
  let IT be non empty AffinStruct;
  attr IT is CongrSpace-like means
  :Def5:
  (for x,y,z,t,a,b being Element of IT
st x,y // a,b & a,b // z,t & a<>b holds x,y // z,t) & (for x,y,z being Element
of IT holds x,x // y,z) & (for x,y,z,t being Element of IT st x,y // z,t holds
  z,t // x,y) & for x,y being Element of IT holds x,y // x,y;
end;

registration
  cluster strict CongrSpace-like (non empty AffinStruct);
  existence
  proof
    consider OAS being strict OAffinSpace;
A1: ( for x,y,z,t being Element of OAS st x,y // z,t holds z,t // x,y)&
    for x,y being Element of OAS holds x,y // x,y by DIRAF:4,5;
    ( for x,y,z,t,a,b being Element of OAS st x,y // a,b & a,b // z,t & a
<>b holds x,y // z,t)& for x,y,z being Element of OAS holds x,x // y,z by DIRAF
    :6,7;
    then OAS is CongrSpace-like by A1,Def5;
    hence thesis;
  end;
end;

definition
  mode CongrSpace is CongrSpace-like (non empty AffinStruct);
end;

reserve CS for CongrSpace;

Lm3: the CONGR of CS is_reflexive_in [:the carrier of CS,the carrier of CS:]
proof
  let x be set;
  assume x in [:the carrier of CS,the carrier of CS:];
  then consider x1,x2 being Element of CS such that
A1: x=[x1,x2] by DOMAIN_1:9;
  x1,x2 // x1,x2 by Def5;
  hence thesis by A1,ANALOAF:def 2;
end;

Lm4: the CONGR of CS is_symmetric_in [:the carrier of CS,the carrier of CS:]
proof
  let x,y be set;
  assume that
A1: x in [:the carrier of CS,the carrier of CS:] and
A2: y in [:the carrier of CS,the carrier of CS:] and
A3: [x,y] in the CONGR of CS;
  consider x1,x2 being Element of CS such that
A4: x=[x1,x2] by A1,DOMAIN_1:9;
  consider y1,y2 being Element of CS such that
A5: y=[y1,y2] by A2,DOMAIN_1:9;
  x1,x2 // y1,y2 by A3,A4,A5,ANALOAF:def 2;
  then y1,y2 // x1,x2 by Def5;
  hence thesis by A4,A5,ANALOAF:def 2;
end;

canceled;

theorem Th37:
  id the carrier of CS is_DIL_of CS
proof
  id the carrier of CS is_FormalIz_of the CONGR of CS by Lm3,Th23;
  hence thesis by Def4;
end;

theorem Th38:
  for f being Permutation of the carrier of CS st f is_DIL_of CS
  holds f" is_DIL_of CS
proof
  let f be Permutation of the carrier of CS;
  assume f is_DIL_of CS;
  then f is_FormalIz_of the CONGR of CS by Def4;
  then f" is_FormalIz_of the CONGR of CS by Lm4,Th24;
  hence thesis by Def4;
end;

theorem Th39:
  for f,g being Permutation of the carrier of CS st f is_DIL_of CS
  & g is_DIL_of CS holds f*g is_DIL_of CS
proof
  let f,g be Permutation of the carrier of CS;
A1: now
    let a,b,x,y,z,t be Element of CS;
    assume that
A2: [[x,y],[a,b]] in the CONGR of CS & [[a,b],[z,t]] in the CONGR of CS and
A3: a<>b;
    x,y // a,b & a,b // z,t by A2,ANALOAF:def 2;
    then x,y // z,t by A3,Def5;
    hence [[x,y],[z,t]] in the CONGR of CS by ANALOAF:def 2;
  end;
A4: now
    let x,y,z be Element of CS;
    x,x // y,z by Def5;
    hence [[x,x],[y,z]] in the CONGR of CS by ANALOAF:def 2;
  end;
  assume f is_DIL_of CS & g is_DIL_of CS;
  then f is_FormalIz_of the CONGR of CS & g is_FormalIz_of the CONGR of CS by
  Def4;
  then f*g is_FormalIz_of the CONGR of CS by A1,A4,Th26;
  hence thesis by Def4;
end;

reserve OAS for OAffinSpace;
reserve a,b,c,d,p,q,r,x,y,z,t,u for Element of OAS;

theorem Th40:
  OAS is CongrSpace-like
proof
  thus for x,y,z,t,a,b being Element of OAS st x,y // a,b & a,b // z,t & a<>b
  holds x,y // z,t by DIRAF:6;
  thus for x,y,z being Element of OAS holds x,x // y,z by DIRAF:7;
  thus for x,y,z,t being Element of OAS st x,y // z,t holds z,t // x,y by
  DIRAF:5;
  thus thesis by DIRAF:4;
end;

reserve f,g for Permutation of the carrier of OAS;

definition
  let OAS;
  let f be Permutation of the carrier of OAS;
  attr f is positive_dilatation means
  :Def6:
  f is_DIL_of OAS;
end;

canceled;

theorem Th42:
  for f being Permutation of the carrier of OAS holds (f is
  positive_dilatation iff for a,b holds a,b // f.a,f.b )
proof
  let f be Permutation of the carrier of OAS;
A1: now
    assume for a,b holds a,b // f.a,f.b;
    then f is_DIL_of OAS by Th35;
    hence f is positive_dilatation by Def6;
  end;
  now
    assume f is positive_dilatation;
    then f is_DIL_of OAS by Def6;
    hence for a,b holds a,b // f.a,f.b by Th35;
  end;
  hence thesis by A1;
end;

definition
  let OAS;
  let f be Permutation of the carrier of OAS;
  attr f is negative_dilatation means
  :Def7:
  for a,b holds a,b // f.b,f.a;
end;

canceled;

theorem Th44:
  id the carrier of OAS is positive_dilatation
proof
  OAS is CongrSpace by Th40;
  then id the carrier of OAS is_DIL_of OAS by Th37;
  hence thesis by Def6;
end;

theorem
  for f being Permutation of the carrier of OAS st f is
  positive_dilatation holds f" is positive_dilatation
proof
  let f be Permutation of the carrier of OAS;
  assume f is positive_dilatation;
  then
A1: f is_DIL_of OAS by Def6;
  OAS is CongrSpace by Th40;
  then f" is_DIL_of OAS by A1,Th38;
  hence thesis by Def6;
end;

theorem
  for f,g being Permutation of the carrier of OAS st f is
positive_dilatation & g is positive_dilatation holds f*g is positive_dilatation
proof
  let f,g be Permutation of the carrier of OAS;
  assume f is positive_dilatation & g is positive_dilatation;
  then
A1: f is_DIL_of OAS & g is_DIL_of OAS by Def6;
  OAS is CongrSpace by Th40;
  then f*g is_DIL_of OAS by A1,Th39;
  hence thesis by Def6;
end;

theorem
  not ex f st f is negative_dilatation & f is positive_dilatation
proof
  given f such that
A1: f is negative_dilatation & f is positive_dilatation;
  consider a,b such that
A2: a<>b by STRUCT_0:def 10;
  a,b // f.a,f.b & a,b // f.b,f.a by A1,Def7,Th42;
  then f.a,f.b // f.b,f.a by A2,ANALOAF:def 5;
  then f.a = f.b by ANALOAF:def 5;
  hence contradiction by A2,FUNCT_2:85;
end;

theorem
  f is negative_dilatation implies f" is negative_dilatation
proof
  assume
A1: f is negative_dilatation;
  let x,y;
  set x9=f".x, y9=f".y;
  f.x9=x & f.y9=y by Th4;
  then y9,x9 // x,y by A1,Def7;
  hence thesis by DIRAF:5;
end;

theorem
  f is positive_dilatation & g is negative_dilatation implies f*g is
  negative_dilatation & g*f is negative_dilatation
proof
  assume
A1: f is positive_dilatation & g is negative_dilatation;
  thus x,y // (f*g).y,(f*g).x
  proof
    set x9=g.x;
    set y9=g.y;
A2: (f*g).x= f.x9 & (f*g).y=f.y9 by FUNCT_2:21;
A3: now
      assume x9=y9;
      then x=y by FUNCT_2:85;
      hence thesis by DIRAF:7;
    end;
    x,y // y9,x9 & y9,x9 // f.y9,f.x9 by A1,Def7,Th42;
    hence thesis by A2,A3,DIRAF:6;
  end;
  thus x,y // (g*f).y,(g*f).x
  proof
    set x9=f.x;
    set y9=f.y;
A4: (g*f).x= g.x9 & (g*f).y=g.y9 by FUNCT_2:21;
A5: now
      assume x9=y9;
      then x=y by FUNCT_2:85;
      hence thesis by DIRAF:7;
    end;
    x,y // x9,y9 & x9,y9 // g.y9,g.x9 by A1,Def7,Th42;
    hence thesis by A4,A5,DIRAF:6;
  end;
end;

definition
  let OAS;
  let f be Permutation of the carrier of OAS;
  attr f is dilatation means
  :Def8:
  f is_FormalIz_of lambda(the CONGR of OAS);
end;

canceled;

theorem Th51:
  for f being Permutation of the carrier of OAS holds (f is
  dilatation iff for a,b holds a,b '||' f.a,f.b )
proof
  let f be Permutation of the carrier of OAS;
A1: now
    assume
A2: for a,b holds a,b '||' f.a,f.b;
    now
      let a,b;
      a,b '||' f.a,f.b by A2;
      hence [[a,b],[f.a,f.b]] in lambda (the CONGR of OAS) by DIRAF:23;
    end;
    then f is_FormalIz_of lambda(the CONGR of OAS) by Def2;
    hence f is dilatation by Def8;
  end;
  now
    assume
A3: f is dilatation;
    let a,b;
    f is_FormalIz_of lambda(the CONGR of OAS) by A3,Def8;
    then [[a,b],[f.a,f.b]] in lambda(the CONGR of OAS) by Def2;
    hence a,b '||' f.a,f.b by DIRAF:23;
  end;
  hence thesis by A1;
end;

theorem
  f is positive_dilatation or f is negative_dilatation implies f is dilatation
proof
  assume
A1: f is positive_dilatation or f is negative_dilatation;
  now
    let x,y;
    x,y // f.x,f.y or x,y // f.y,f.x by A1,Def7,Th42;
    hence x,y '||' f.x,f.y by DIRAF:def 4;
  end;
  hence thesis by Th51;
end;

theorem
  for f being Permutation of the carrier of OAS st f is dilatation ex f9
being Permutation of the carrier of Lambda(OAS) st f=f9 & f9 is_DIL_of Lambda(
  OAS)
proof
  let f be Permutation of the carrier of OAS;
A1: Lambda(OAS) = AffinStruct(#the carrier of OAS, lambda(the CONGR of OAS)
  #) by DIRAF:def 2;
  then reconsider f9=f as Permutation of the carrier of Lambda(OAS);
  assume f is dilatation;
  then
A2: f is_FormalIz_of lambda(the CONGR of OAS) by Def8;
  take f9;
  thus thesis by A2,A1,Def4;
end;

theorem
  for f being Permutation of the carrier of Lambda(OAS) st f is_DIL_of
  Lambda(OAS) ex f9 being Permutation of the carrier of OAS st f=f9 & f9 is
  dilatation
proof
  let f be Permutation of the carrier of Lambda(OAS);
A1: Lambda(OAS) = AffinStruct(#the carrier of OAS, lambda(the CONGR of OAS)
  #) by DIRAF:def 2;
  then reconsider f9=f as Permutation of the carrier of OAS;
  assume f is_DIL_of Lambda(OAS);
  then
A2: f is_FormalIz_of the CONGR of Lambda(OAS) by Def4;
  take f9;
  thus thesis by A2,A1,Def8;
end;

theorem
  id the carrier of OAS is dilatation
proof
  now
    let x,y;
    (id the carrier of OAS).x = x & (id the carrier of OAS).y = y by
    FUNCT_1:35;
    hence x,y '||' (id the carrier of OAS).x,(id the carrier of OAS).y by
    DIRAF:24;
  end;
  hence thesis by Th51;
end;

theorem Th56:
  f is dilatation implies f" is dilatation
proof
  assume
A1: f is dilatation;
  now
    let x,y;
    set x9=f".x;
    set y9=f".y;
    f.x9=x & f.y9=y by Th4;
    then x9,y9 '||' x,y by A1,Th51;
    hence x,y '||' f".x,f".y by DIRAF:27;
  end;
  hence thesis by Th51;
end;

theorem Th57:
  f is dilatation & g is dilatation implies f*g is dilatation
proof
  assume
A1: f is dilatation & g is dilatation;
  now
    let x,y;
    set x9=g.x;
    set y9=g.y;
A2: (f*g).x= f.x9 & (f*g).y=f.y9 by FUNCT_2:21;
A3: now
      assume x9=y9;
      then x=y by FUNCT_2:85;
      hence x,y '||' (f*g).x,(f*g).y by DIRAF:25;
    end;
    x,y '||' x9,y9 & x9,y9 '||' f.x9,f.y9 by A1,Th51;
    hence x,y '||' (f*g).x,(f*g).y by A2,A3,DIRAF:28;
  end;
  hence thesis by Th51;
end;

theorem Th58:
  f is dilatation implies for a,b,c,d holds a,b '||' c,d iff f.a,f
  .b '||' f.c,f.d
proof
  assume
A1: f is dilatation;
  let a,b,c,d;
A2: c,d '||' f.c,f.d by A1,Th51;
A3: a,b '||' f.a,f.b by A1,Th51;
A4: now
A5: now
A6:   now
        assume f.c = f.d;
        then c = d by FUNCT_2:85;
        hence a,b '||' c,d by DIRAF:25;
      end;
      assume a,b '||' f.c,f.d;
      hence a,b '||' c,d by A2,A6,DIRAF:28;
    end;
A7: now
      assume f.a=f.b;
      then a=b by FUNCT_2:85;
      hence a,b '||' c,d by DIRAF:25;
    end;
    assume f.a,f.b '||' f.c,f.d;
    hence a,b '||' c,d by A3,A7,A5,DIRAF:28;
  end;
  now
A8: now
A9:   c = d implies f.a,f.b '||' f.c,f.d by DIRAF:25;
      assume f.a,f.b '||' c,d;
      hence f.a,f.b '||' f.c,f.d by A2,A9,DIRAF:28;
    end;
    assume a,b '||' c,d;
    then f.a,f.b '||' c,d or a=b by A3,DIRAF:28;
    hence f.a,f.b '||' f.c,f.d by A8,DIRAF:25;
  end;
  hence thesis by A4;
end;

theorem Th59:
  f is dilatation implies for a,b,c holds LIN a,b,c iff LIN f.a,f. b,f.c
proof
  assume
A1: f is dilatation;
  let a,b,c;
  a,b '||' a,c iff f.a,f.b '||' f.a,f.c by A1,Th58;
  hence thesis by DIRAF:def 5;
end;

theorem Th60:
  f is dilatation & LIN x,f.x,y implies LIN x,f.x,f.y
proof
  assume
A1: f is dilatation;
  assume
A2: LIN x,f.x,y;
  now
    assume
A3: x<>y;
    x,f.x '||' x,y & x,y '||' f.x,f.y by A1,A2,Th51,DIRAF:def 5;
    then x,f.x '||' f.x,f.y by A3,DIRAF:28;
    then f.x,x '||' f.x,f.y by DIRAF:27;
    then LIN f.x,x,f.y by DIRAF:def 5;
    hence thesis by DIRAF:36;
  end;
  hence thesis by DIRAF:37;
end;

theorem Th61:
  a,b '||' c,d implies (a,c '||' b,d or ex x st LIN a,c,x & LIN b, d,x )
proof
  assume
A1: a,b '||' c,d;
  assume
A2: not a,c '||' b,d;
A3: now
    consider z such that
A4: a,b '||' c,z and
A5: a,c '||' b,z by DIRAF:31;
    assume
A6: a<>b;
A7: now
      c,d '||' c,z by A1,A6,A4,DIRAF:28;
      then LIN c,d,z by DIRAF:def 5;
      then LIN d,c,z by DIRAF:36;
      then d,c '||' d,z by DIRAF:def 5;
      then z,d '||' d,c by DIRAF:27;
      then consider t such that
A8:   b,d '||' d,t and
A9:   b,z '||' c,t by A2,A5,DIRAF:32;
      assume b<>z;
      then a,c '||' c,t by A5,A9,DIRAF:28;
      then c,a '||' c,t by DIRAF:27;
      then LIN c,a,t by DIRAF:def 5;
      then
A10:  LIN a,c,t by DIRAF:36;
      d,b '||' d,t by A8,DIRAF:27;
      then LIN d,b,t by DIRAF:def 5;
      then LIN b,d,t by DIRAF:36;
      hence thesis by A10;
    end;
    now
      assume b=z;
      then b,a '||' b,c by A4,DIRAF:27;
      then LIN b,a,c by DIRAF:def 5;
      then
A11:  LIN a,c,b by DIRAF:36;
      LIN b,d,b by DIRAF:37;
      hence thesis by A11;
    end;
    hence thesis by A7;
  end;
  now
    assume a=b;
    then LIN a,c,a & LIN b,d,a by DIRAF:37;
    hence thesis;
  end;
  hence thesis by A3;
end;

theorem Th62:
  f is dilatation implies ((f=id the carrier of OAS or for x holds
  f.x<>x) iff for x,y holds x,f.x '||' y,f.y )
proof
  assume
A1: f is dilatation;
A2: now
    assume
A3: for x,y holds x,f.x '||' y,f.y;
    assume f<>(id the carrier of OAS);
    then consider y such that
A4: f.y<>(id the carrier of OAS).y by FUNCT_2:113;
    given x such that
A5: f.x=x;
    x<>y by A5,A4,FUNCT_1:35;
    then consider z such that
A6: not LIN x,y,z by DIRAF:43;
    x,z '||' f.x,f.z by A1,Th51;
    then LIN x,z,f.z by A5,DIRAF:def 5;
    then
A7: LIN z,f.z,x by DIRAF:36;
    LIN z,f.z,z by DIRAF:37;
    then
A8: z,f.z '||' x,z by A7,DIRAF:40;
A9: f.y<>y by A4,FUNCT_1:35;
    x,y '||' f.x,f.y by A1,Th51;
    then
A10: LIN x,y,f.y by A5,DIRAF:def 5;
    then LIN y,x,f.y by DIRAF:36;
    then
A11: y,x '||' y,f.y by DIRAF:def 5;
A12: LIN y,f.y,x by A10,DIRAF:36;
A13: now
      assume z=f.z;
      then z,y '||' z,f.y by A1,Th51;
      then LIN z,y,f.y by DIRAF:def 5;
      then LIN y,f.y,y & LIN y,f.y,z by DIRAF:36,37;
      hence contradiction by A9,A12,A6,DIRAF:38;
    end;
    y,f.y '||' z,f.z by A3;
    then y,f.y '||' x,z by A13,A8,DIRAF:28;
    then y,x '||' x,z by A9,A11,DIRAF:28;
    then x,y '||' x,z by DIRAF:27;
    hence contradiction by A6,DIRAF:def 5;
  end;
  now
    assume
A14: f=(id the carrier of OAS) or for z holds f.z<>z;
    let x,y;
A15: x,y '||' f.x,f.y by A1,Th51;
A16: now
      assume
A17:  for z holds f.z<>z;
      assume
A18:  not x,f.x '||' y,f.y;
      then consider z such that
A19:  LIN x,f.x,z and
A20:  LIN y,f.y,z by A15,Th61;
      set t=f.z;
      LIN x,f.x,t by A1,A19,Th60;
      then
A21:  x,f.x '||' z,t by A19,DIRAF:40;
      LIN y,f.y,t by A1,A20,Th60;
      then
A22:  y,f.y '||' z,t by A20,DIRAF:40;
      z<>t by A17;
      hence contradiction by A18,A21,A22,DIRAF:28;
    end;
    now
      assume f=(id the carrier of OAS);
      then f.y=y by FUNCT_1:35;
      hence x,f.x '||' y,f.y by DIRAF:25;
    end;
    hence x,f.x '||' y,f.y by A14,A16;
  end;
  hence thesis by A2;
end;

theorem Th63:
  f is dilatation & f.a=a & f.b=b & not LIN a,b,x implies f.x=x
proof
  assume that
A1: f is dilatation and
A2: f.a=a and
A3: f.b=b and
A4: not LIN a,b,x;
  a,x '||' a,f.x by A1,A2,Th51;
  then LIN a,x,f.x by DIRAF:def 5;
  then
A5: LIN x,f.x,a by DIRAF:36;
  b,x '||' b,f.x by A1,A3,Th51;
  then LIN b,x,f.x by DIRAF:def 5;
  then
A6: LIN x,f.x,x & LIN x,f.x,b by DIRAF:36,37;
  assume f.x<>x;
  hence contradiction by A4,A5,A6,DIRAF:38;
end;

theorem Th64:
  f is dilatation & f.a=a & f.b=b & a<>b implies f=(id the carrier of OAS)
proof
  assume that
A1: f is dilatation and
A2: f.a=a and
A3: f.b=b and
A4: a<>b;
  now
    let x;
A5: now
      assume
A6:   LIN a,b,x;
      now
        consider z such that
A7:     not LIN a,b,z by A4,DIRAF:43;
        assume
A8:     x<>a;
A9:     not LIN a,z,x
        proof
          assume LIN a,z,x;
          then
A10:      LIN a,x,z by DIRAF:36;
          LIN a,x,a & LIN a,x,b by A6,DIRAF:36,37;
          hence contradiction by A8,A7,A10,DIRAF:38;
        end;
        f.z=z by A1,A2,A3,A7,Th63;
        hence f.x=x by A1,A2,A9,Th63;
      end;
      hence f.x=x by A2;
    end;
    not LIN a,b,x implies f.x=x by A1,A2,A3,Th63;
    hence f.x=(id the carrier of OAS).x by A5,FUNCT_1:35;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem
  f is dilatation & g is dilatation & f.a=g.a & f.b=g.b implies a=b or f =g
proof
  assume that
A1: f is dilatation and
A2: g is dilatation and
A3: f.a=g.a and
A4: f.b=g.b;
A5: (g"*f).b=g".(g.b) by A4,FUNCT_2:21
    .=b by Th4;
A6: g" is dilatation by A2,Th56;
  assume
A7: a<>b;
  (g"*f).a=g".(g.a) by A3,FUNCT_2:21
    .=a by Th4;
  then
A8: g"*f=(id the carrier of OAS) by A1,A7,A5,A6,Th57,Th64;
  now
    let x;
    g".(f.x)=(g"*f).x by FUNCT_2:21;
    then g".(f.x)=x by A8,FUNCT_1:35;
    hence g.x=f.x by Th4;
  end;
  hence thesis by FUNCT_2:113;
end;

definition
  let OAS;
  let f be Permutation of the carrier of OAS;
  attr f is translation means
  :Def9:
  f is dilatation & (f = id the carrier of OAS or for a holds a<>f.a);
end;

canceled;

theorem Th67:
  f is dilatation implies (f is translation iff for x,y holds x,f.
  x '||' y,f.y )
proof
  assume
A1: f is dilatation;
  then
  (f=id the carrier of OAS or for x holds f.x<>x) iff for x,y holds x,f.x
  '||' y,f.y by Th62;
  hence thesis by A1,Def9;
end;

canceled;

theorem Th69:
  f is translation & g is translation & f.a=g.a & not LIN a,f.a,x
  implies f.x=g.x
proof
  assume that
A1: f is translation and
A2: g is translation and
A3: f.a=g.a and
A4: not LIN a,f.a,x;
  set b=f.a, y=f.x, z=g.x;
  g is dilatation by A2,Def9;
  then
A5: a,x '||' b,z & a,b '||' x,z by A2,A3,Th51,Th67;
A6: f is dilatation by A1,Def9;
  then
A7: a,x '||' b,y by Th51;
  a,b '||' x,y by A1,A6,Th67;
  hence thesis by A4,A7,A5,PASCH:12;
end;

theorem Th70:
  f is translation & g is translation & f.a=g.a implies f=g
proof
  assume that
A1: f is translation and
A2: g is translation and
A3: f.a=g.a;
  set b=f.a;
A4: f is dilatation by A1,Def9;
A5: now
    assume
A6: a<>b;
    for x holds f.x=g.x
    proof
      let x;
      now
        assume
A7:     LIN a,b,x;
        now
A8:       f<>(id the carrier of OAS) by A6,FUNCT_1:35;
          then
A9:       f.x<>x by A1,Def9;
          assume x<>a;
          consider p such that
A10:      not LIN a,b,p by A6,DIRAF:43;
A11:      f.p<>p by A1,A8,Def9;
A12:      not LIN p,f.p,x
          proof
A13:        LIN a,b,f.x by A4,A7,Th60;
            LIN a,b,a by DIRAF:37;
            then
A14:        LIN x,f.x,a by A6,A7,A13,DIRAF:38;
A15:        LIN p,f.p,p by DIRAF:37;
            LIN a,b,b by DIRAF:37;
            then
A16:        LIN x,f.x,b by A6,A7,A13,DIRAF:38;
            assume
A17:        LIN p,f.p,x;
            then LIN p,f.p,f.x by A4,Th60;
            then LIN x,f.x,p by A11,A17,A15,DIRAF:38;
            hence contradiction by A10,A9,A14,A16,DIRAF:38;
          end;
          f.p=g.p by A1,A2,A3,A10,Th69;
          hence thesis by A1,A2,A12,Th69;
        end;
        hence thesis by A3;
      end;
      hence thesis by A1,A2,A3,Th69;
    end;
    hence thesis by FUNCT_2:113;
  end;
  now
    assume
A18: b=a;
    then f=id the carrier of OAS by A1,Def9;
    hence thesis by A2,A3,A18,Def9;
  end;
  hence thesis by A5;
end;

theorem Th71:
  f is translation implies f" is translation
proof
A1: now
    assume
A2: for x holds f.x<>x;
    given y such that
A3: f".y=y;
    f.y=y by A3,Th4;
    hence contradiction by A2;
  end;
  assume
A4: f is translation;
  then f is dilatation by Def9;
  then
A5: f" is dilatation by Th56;
  f=(id the carrier of OAS) implies f"=(id the carrier of OAS) by FUNCT_1:
  67;
  hence thesis by A4,A5,A1,Def9;
end;

theorem
  f is translation & g is translation implies (f*g) is translation
proof
  assume that
A1: f is translation and
A2: g is translation;
  f is dilatation & g is dilatation by A1,A2,Def9;
  then
A3: (f*g) is dilatation by Th57;
  now
    assume
A4: (f*g)<>(id the carrier of OAS);
    for x holds (f*g).x<>x
    proof
      given x such that
A5:   (f*g).x=x;
      f.(g.x)=x by A5,FUNCT_2:21;
      then
A6:   g.x=f".x by Th4;
      f" is translation by A1,Th71;
      then f*g=f*f" by A2,A6,Th70;
      hence contradiction by A4,FUNCT_2:88;
    end;
    hence thesis by A3,Def9;
  end;
  hence thesis by A3,Def9;
end;

Lm5: f is translation & not LIN a,f.a,b implies a,b // f.a,f.b & a,f.a // b,f.
b
proof
  assume that
A1: f is translation and
A2: not LIN a,f.a,b;
A3: f is dilatation by A1,Def9;
  then
A4: a,b '||' f.a,f.b by Th51;
  a,f.a '||' b,f.b by A1,A3,Th67;
  hence thesis by A2,A4,PASCH:21;
end;

Lm6: f is translation & a<>f.a & LIN a,f.a,b implies a,f.a // b,f.b
proof
  assume that
A1: f is translation and
A2: a<>f.a and
A3: LIN a,f.a,b;
  consider p such that
A4: not LIN a,f.a,p by A2,DIRAF:43;
A5: f is dilatation by A1,Def9;
A6: f<>(id the carrier of OAS) by A2,FUNCT_1:35;
  then
A7: p<>f.p by A1,Def9;
A8: b<>f.b by A1,A6,Def9;
  not LIN p,f.p,b
  proof
A9: LIN p,f.p,p by DIRAF:37;
    assume
A10: LIN p,f.p,b;
    then LIN p,f.p,f.b by A5,Th60;
    then
A11: LIN b,f.b,p by A7,A10,A9,DIRAF:38;
    LIN a,f.a,f.b & LIN a,f.a,a by A3,A5,Th60,DIRAF:37;
    then
A12: LIN b,f.b,a by A2,A3,DIRAF:38;
    then LIN b,f.b,f.a by A5,Th60;
    hence contradiction by A4,A8,A12,A11,DIRAF:38;
  end;
  then
A13: p,f.p // b,f.b by A1,Lm5;
  not LIN p,f.p,a
  proof
    assume
A14: LIN p,f.p,a;
    then LIN p,f.p,p & LIN p,f.p,f.a by A5,Th60,DIRAF:37;
    hence contradiction by A4,A7,A14,DIRAF:38;
  end;
  then p,f.p // a,f.a by A1,Lm5;
  hence thesis by A7,A13,ANALOAF:def 5;
end;

Lm7: f is translation & Mid a,f.a,b & a<>f.a implies a,b // f.a,f.b
proof
  assume that
A1: f is translation and
A2: Mid a,f.a,b and
A3: a<>f.a;
A4: a,f.a // b,f.b by A1,A2,A3,Lm6,DIRAF:34;
  now
    Mid b,f.a,a by A2,DIRAF:13;
    then b,f.a // f.a,a by DIRAF:def 3;
    then b,f.a // b,a by ANALOAF:def 5;
    then
A5: a,b // f.a,b by DIRAF:5;
    a,f.a // f.a,b by A2,DIRAF:def 3;
    then f.a,b // b,f.b by A3,A4,ANALOAF:def 5;
    then
A6: f.a,b // f.a,f.b by ANALOAF:def 5;
    assume b<>f.a;
    hence thesis by A5,A6,DIRAF:6;
  end;
  hence thesis by A1,A2,A3,Lm6,DIRAF:34;
end;

Lm8: f is translation & a<>f.a & b<>f.a & Mid a,b,f.a implies a,b // f.a,f.b
proof
  assume that
A1: f is translation and
A2: a<>f.a and
A3: b<>f.a and
A4: Mid a,b,f.a;
A5: a,b // b,f.a by A4,DIRAF:def 3;
A6: f is dilatation by A1,Def9;
A7: LIN a,b,f.a by A4,DIRAF:34;
  then
A8: LIN a,f.a,b by DIRAF:36;
A9: f<>(id the carrier of OAS) by A2,FUNCT_1:35;
  now
    assume
A10: a<>b;
A11: now
      LIN f.a,a,b by A7,DIRAF:36;
      then f.a,a '||' f.a,b by DIRAF:def 5;
      then
A12:  a,f.a '||' b,f.a by DIRAF:27;
      consider q such that
A13:  not LIN a,f.a,q by A2,DIRAF:43;
      consider x such that
A14:  q,b // b,x and
A15:  q,a // f.a,x by A5,A10,ANALOAF:def 5;
A16:  a,q // x,f.a by A15,DIRAF:5;
A17:  Mid q,b,x by A14,DIRAF:def 3;
      then
A18:  LIN x,b,q by DIRAF:13,34;
A19:  x<>f.a
      proof
        assume x=f.a;
        then Mid q,b,f.a by A14,DIRAF:def 3;
        then LIN q,b,f.a by DIRAF:34;
        then
A20:    LIN f.a,b,q by DIRAF:36;
A21:    LIN a,b,a by DIRAF:37;
        LIN f.a,b,a & LIN f.a,b,b by A7,DIRAF:36,37;
        then LIN a,b,q by A3,A20,DIRAF:38;
        hence contradiction by A7,A10,A13,A21,DIRAF:38;
      end;
A22:  not LIN x,f.a,b
      proof
        assume LIN x,f.a,b;
        then LIN f.a,x,b by DIRAF:36;
        then
A23:    f.a,x '||' f.a,b by DIRAF:def 5;
        LIN f.a,b,a by A7,DIRAF:36;
        then
A24:    f.a,b '||' f.a,a by DIRAF:def 5;
        q,a '||' f.a,x by A15,DIRAF:def 4;
        then f.a,b '||' q,a by A19,A23,DIRAF:28;
        then f.a,a '||' q,a by A3,A24,DIRAF:28;
        then a,f.a '||' a,q by DIRAF:27;
        hence contradiction by A13,DIRAF:def 5;
      end;
      a,f.a // q,f.q by A1,A13,Lm5;
      then a,f.a '||' q,f.q by DIRAF:def 4;
      then b,f.a '||' q,f.q by A2,A12,DIRAF:28;
      then
A25:  f.a,b '||' q,f.q by DIRAF:27;
A26:  LIN a,f.a,f.b by A6,A8,Th60;
      a,q // f.a,f.q & a<>q by A1,A13,Lm5,DIRAF:37;
      then f.a,f.q // x,f.a by A16,ANALOAF:def 5;
      then f.a,f.q '||' f.a,x by DIRAF:def 4;
      then LIN f.a,f.q,x by DIRAF:def 5;
      then
A27:  LIN x,f.a,f.q by DIRAF:36;
A28:  b<>f.b by A1,A9,Def9;
      LIN a,f.a,f.a by DIRAF:37;
      then
A29:  LIN b,f.b,f.a by A2,A8,A26,DIRAF:38;
      LIN a,f.a,a by DIRAF:37;
      then LIN b,f.b,a by A2,A8,A26,DIRAF:38;
      then
A30:  not LIN b,f.b,q by A13,A29,A28,DIRAF:38;
A31:  not LIN b,f.b,f.q
      proof
        b,f.b '||' q,f.q by A1,A6,Th67;
        then
A32:    b,f.b '||' f.q,q by DIRAF:27;
        assume LIN b,f.b,f.q;
        hence contradiction by A28,A30,A32,DIRAF:39;
      end;
      then
A33:  f.b<>f.q by DIRAF:37;
      a,b // a,f.a & a,f.a // b,f.b by A1,A2,A8,A5,Lm6,ANALOAF:def 5;
      then
A34:  a,b // b,f.b by A2,DIRAF:6;
      assume a,b // f.b,f.a;
      then b,f.b // f.b,f.a by A10,A34,ANALOAF:def 5;
      then Mid b,f.b,f.a by DIRAF:def 3;
      then
A35:  Mid f.a,f.b,b by DIRAF:13;
A36:  LIN x,b,b by DIRAF:37;
      Mid x,b,q by A17,DIRAF:13;
      then Mid x,f.a,f.q by A22,A27,A25,PASCH:15;
      then consider y such that
A37:  Mid x,y,b and
A38:  Mid y,f.b,f.q by A35,A22,PASCH:35;
      LIN x,y,b by A37,DIRAF:34;
      then
A39:  LIN x,b,y by DIRAF:36;
A40:  x<>b by A22,DIRAF:37;
      then LIN b,q,y by A18,A39,A36,DIRAF:38;
      then
A41:  b,q '||' b,y by DIRAF:def 5;
A42:  LIN y,f.b,f.q by A38,DIRAF:34;
      then LIN f.b,f.q,y by DIRAF:36;
      then
A43:  f.b,f.q '||' f.b,y by DIRAF:def 5;
      b,q '||' f.b,f.q by A6,Th51;
      then b,q '||' f.b,y by A33,A43,DIRAF:28;
      then f.b,y '||' b,y by A33,A41,DIRAF:28;
      then y,b '||' y,f.b by DIRAF:27;
      then
A44:  LIN y,b,f.b by DIRAF:def 5;
A45:  LIN y,b,b by DIRAF:37;
      LIN y,b,q by A40,A18,A39,A36,DIRAF:38;
      hence contradiction by A42,A30,A31,A44,A45,DIRAF:38;
    end;
    a,b '||' f.a,f.b by A6,Th51;
    hence thesis by A11,DIRAF:def 4;
  end;
  hence thesis by DIRAF:7;
end;

Lm9: f is translation & a<>f.a & LIN a,f.a,b implies a,b // f.a,f.b
proof
  assume that
A1: f is translation and
A2: a<>f.a and
A3: LIN a,f.a,b;
  f<>(id the carrier of OAS) by A2,FUNCT_1:35;
  then
A4: b<>f.b by A1,Def9;
A5: f is dilatation by A1,Def9;
  now
    assume
A6: a<>b;
A7: now
      assume
A8:   Mid f.a,a,b;
A9:   now
        assume Mid f.b,b,a;
        then Mid a,b,f.b by DIRAF:13;
        then a,b // b,f.b by DIRAF:def 3;
        then
A10:    a,b // a,f.b by ANALOAF:def 5;
        Mid b,a,f.a by A8,DIRAF:13;
        then b,a // a,f.a by DIRAF:def 3;
        then
A11:    a,b // f.a,a by ANALOAF:def 5;
        then f.a,a // a,f.b by A6,A10,ANALOAF:def 5;
        then f.a,a // f.a,f.b by ANALOAF:def 5;
        hence thesis by A10,A11,DIRAF:6;
      end;
A12:  now
A13:    now
          assume
A14:      a=f.b;
          a,f.a // b,f.b by A1,A2,A3,Lm6;
          hence thesis by A14,DIRAF:5;
        end;
        assume Mid b,a,f.b;
        then b,a // f.b,f.a or a=f.b by A1,A4,Lm8;
        hence thesis by A13,DIRAF:5;
      end;
A15:  now
        assume Mid b,f.b,a;
        then b,a // f.b,f.a by A1,A4,Lm7;
        hence thesis by DIRAF:5;
      end;
      LIN a,f.a,f.b & LIN a,f.a,a by A3,A5,Th60,DIRAF:37;
      hence thesis by A2,A3,A15,A12,A9,DIRAF:35,38;
    end;
A16: now
      assume
A17:  Mid a,b,f.a;
      b=f.a implies thesis by A1,A2,A3,Lm6;
      hence thesis by A1,A2,A17,Lm8;
    end;
    Mid a,f.a,b implies thesis by A1,A2,Lm7;
    hence thesis by A3,A7,A16,DIRAF:35;
  end;
  hence thesis by DIRAF:7;
end;

theorem Th73:
  f is translation implies f is positive_dilatation
proof
  assume
A1: f is translation;
A2: now
    assume
A3: for x holds f.x<>x;
    for a,b holds a,b // f.a,f.b
    proof
      let a,b;
A4:   a<>f.a by A3;
      not LIN a,f.a,b implies a,b // f.a,f.b by A1,Lm5;
      hence thesis by A1,A4,Lm9;
    end;
    hence thesis by Th42;
  end;
  f=id the carrier of OAS implies thesis by Th44;
  hence thesis by A1,A2,Def9;
end;

theorem Th74:
  f is dilatation & f.p=p & Mid q,p,f.q & not LIN p,q,x implies Mid x,p,f.x
proof
  assume that
A1: f is dilatation and
A2: f.p=p and
A3: Mid q,p,f.q & not LIN p,q,x;
  q,x '||' f.q,f.x by A1,Th51;
  then
A4: q,x '||' f.x,f.q by DIRAF:27;
  p,x '||' p,f.x by A1,A2,Th51;
  then LIN p,x,f.x by DIRAF:def 5;
  hence thesis by A3,A4,PASCH:13;
end;

theorem Th75:
  f is dilatation & f.p=p & Mid q,p,f.q & q<>p implies Mid x,p,f.x
proof
  assume that
A1: f is dilatation & f.p=p and
A2: Mid q,p,f.q and
A3: q<>p;
  now
    consider r such that
A4: not LIN p,q,r by A3,DIRAF:43;
    assume
A5: LIN p,q,x;
A6: x=p or not LIN p,r,x
    proof
A7:   LIN p,x,q & LIN p,x,p by A5,DIRAF:36,37;
      assume that
A8:   x<>p and
A9:   LIN p,r,x;
      LIN p,x,r by A9,DIRAF:36;
      hence contradiction by A4,A8,A7,DIRAF:38;
    end;
    Mid r,p,f.r by A1,A2,A4,Th74;
    hence thesis by A1,A6,Th74,DIRAF:14;
  end;
  hence thesis by A1,A2,Th74;
end;

theorem Th76:
  f is dilatation & f.p=p & q<>p & Mid q,p,f.q & not LIN p,x,y
  implies x,y // f.y,f.x
proof
  assume
A1: f is dilatation;
  assume
A2: f.p=p & q<>p & Mid q,p,f.q;
  then Mid x,p,f.x by A1,Th75;
  then
A3: x,p // p,f.x by DIRAF:def 3;
  Mid y,p,f.y by A1,A2,Th75;
  then
A4: y,p // p,f.y by DIRAF:def 3;
  x,y '||' f.x,f.y by A1,Th51;
  hence thesis by A3,A4,PASCH:19;
end;

theorem Th77:
  f is dilatation & f.p=p & q<>p & Mid q,p,f.q & LIN p,x,y implies
  x,y // f.y,f.x
proof
  assume that
A1: f is dilatation and
A2: f.p=p and
A3: q<>p & Mid q,p,f.q and
A4: LIN p,x,y;
A5: Mid y,p,f.y by A1,A2,A3,Th75;
A6: now
    assume
A7: x=p;
    then y,x // x,f.y by A5,DIRAF:def 3;
    hence thesis by A2,A7,DIRAF:5;
  end;
A8: now
    assume that
A9: x<>p and
    y<>p and
A10: x<>y;
    consider u such that
A11: not LIN p,x,u by A9,DIRAF:43;
    consider r such that
A12: x,y '||' u,r and
A13: x,u '||' y,r by DIRAF:31;
A14: not LIN x,y,u
    proof
      assume
A15:  LIN x,y,u;
      LIN x,y,p & LIN x,y,x by A4,DIRAF:36,37;
      hence contradiction by A10,A11,A15,DIRAF:38;
    end;
    then
A16: x,y // u,r by A12,A13,PASCH:21;
A17: not LIN p,u,r
    proof
A18:  now
        assume u=r;
        then u,x '||' u,y by A13,DIRAF:27;
        then LIN u,x,y by DIRAF:def 5;
        hence contradiction by A14,DIRAF:36;
      end;
      LIN x,y,p by A4,DIRAF:36;
      then x,y '||' x,p by DIRAF:def 5;
      then x,y '||' p,x by DIRAF:27;
      then
A19:  u,r '||' p,x by A10,A12,DIRAF:28;
A20:  LIN u,r,u by DIRAF:37;
      assume LIN p,u,r;
      then
A21:  LIN u,r,p by DIRAF:36;
      p,x '||' p,y by A4,DIRAF:def 5;
      then u,r '||' p,y by A9,A19,DIRAF:28;
      then
A22:  LIN u,r,y by A18,A21,DIRAF:39;
      LIN u,r,x by A18,A21,A19,DIRAF:39;
      hence contradiction by A14,A18,A22,A20,DIRAF:38;
    end;
    then
A23: u<>r by DIRAF:37;
    set u9=f.u, r9=f.r, x9=f.x, y9=f.y;
A24: not LIN x9,y9,u9 by A1,A14,Th59;
    x9,y9 '||' u9,r9 & x9,u9 '||' y9,r9 by A1,A12,A13,Th58;
    then x9,y9 // u9,r9 by A24,PASCH:21;
    then
A25: r9,u9 // y9,x9 by DIRAF:5;
    u,r // f.r,f.u by A1,A2,A3,A17,Th76;
    then x,y // r9,u9 by A16,A23,DIRAF:6;
    hence thesis by A25,A23,DIRAF:6,FUNCT_2:85;
  end;
  Mid x,p,f.x by A1,A2,A3,Th75;
  hence thesis by A2,A6,A8,DIRAF:7,def 3;
end;

theorem Th78:
  f is dilatation & f.p=p & q<>p & Mid q,p,f.q implies f is negative_dilatation
proof
  assume
A1: f is dilatation & f.p=p & q<>p & Mid q,p,f.q;
  x,y // f.y,f.x
  proof
    not LIN p,x,y implies x,y // f.y,f.x by A1,Th76;
    hence thesis by A1,Th77;
  end;
  hence thesis by Def7;
end;

theorem Th79:
  f is dilatation & f.p=p & (for x holds p,x // p,f.x) implies for
  y,z holds y,z // f.y,f.z
proof
  assume that
A1: f is dilatation and
A2: f.p=p and
A3: p,x // p,f.x;
A4: not LIN p,y,z implies y,z // f.y,f.z
  proof
    assume
A5: not LIN p,y,z;
A6: p,y // p,f.y & p,z // p,f.z by A3;
    y,z '||' f.y,f.z by A1,Th51;
    hence thesis by A5,A6,PASCH:20;
  end;
  let y,z;
  LIN p,y,z implies y,z // f.y,f.z
  proof
    assume
A7: LIN p,y,z;
A8: now
      assume that
A9:   p<>y and
A10:  y<>z and
      z<>p;
      consider q such that
A11:  not LIN p,y,q by A9,DIRAF:43;
A12:  not LIN y,z,q
      proof
        assume
A13:    LIN y,z,q;
        LIN y,z,p & LIN y,z,y by A7,DIRAF:36,37;
        hence contradiction by A10,A11,A13,DIRAF:38;
      end;
      then
A14:  not LIN f.y,f.z,f.q by A1,Th59;
      consider r such that
A15:  y,z '||' q,r and
A16:  y,q '||' z,r by DIRAF:31;
      f.y,f.z '||' f.q,f.r & f.y,f.q '||' f.z,f.r by A1,A15,A16,Th58;
      then f.y,f.z // f.q,f.r by A14,PASCH:21;
      then
A17:  f.q,f.r // f.y,f.z by DIRAF:5;
A18:  q<>r
      proof
        assume q=r;
        then q,y '||' q,z by A16,DIRAF:27;
        then LIN q,y,z by DIRAF:def 5;
        hence contradiction by A12,DIRAF:36;
      end;
      not LIN p,q,r
      proof
A19:    LIN q,r,q by DIRAF:37;
        assume LIN p,q,r;
        then
A20:    LIN q,r,p by DIRAF:36;
        LIN y,p,z by A7,DIRAF:36;
        then y,p '||' y,z by DIRAF:def 5;
        then y,z '||' p,y by DIRAF:27;
        then q,r '||' p,y by A10,A15,DIRAF:28;
        then LIN q,r,y by A18,A20,DIRAF:39;
        hence contradiction by A11,A18,A20,A19,DIRAF:38;
      end;
      then
A21:  q,r // f.q,f.r by A4;
      y,z // q,r by A15,A16,A12,PASCH:21;
      then y,z // f.q,f.r by A18,A21,DIRAF:6;
      hence thesis by A18,A17,DIRAF:6,FUNCT_2:85;
    end;
    now
      assume p=z;
      then z,y // f.z,f.y by A2,A3;
      hence thesis by DIRAF:5;
    end;
    hence thesis by A2,A3,A8,DIRAF:7;
  end;
  hence thesis by A4;
end;

theorem
  f is dilatation implies f is positive_dilatation or f is negative_dilatation
proof
  assume
A1: f is dilatation;
A2: now
    given p such that
A3: f.p=p;
A4: now
      given q such that
A5:   not p,q // p,f.q;
      p,q '||' p,f.q by A1,A3,Th51;
      then
A6:   p,q // f.q,p by A5,DIRAF:def 4;
      then q,p // p,f.q by DIRAF:5;
      then
A7:   Mid q,p,f.q by DIRAF:def 3;
      p<>q by A5,A6,DIRAF:5;
      hence f is negative_dilatation by A1,A3,A7,Th78;
    end;
    now
      assume for x holds p,x // p,f.x;
      then for x,y holds x,y // f.x,f.y by A1,A3,Th79;
      hence f is positive_dilatation by Th42;
    end;
    hence thesis by A4;
  end;
  now
    assume for x holds f.x<>x;
    then f is translation by A1,Def9;
    hence f is positive_dilatation by Th73;
  end;
  hence thesis by A2;
end;

reserve AFS for AffinSpace;
reserve a,b,c,d,d1,d2,p,x,y,z,t for Element of AFS;

canceled;

theorem Th82:
  AFS is CongrSpace-like
proof
  thus for x,y,z,t,a,b st x,y // a,b & a,b // z,t & a<>b holds x,y // z,t by
  AFF_1:14;
  thus for x,y,z holds x,x // y,z by AFF_1:12;
  thus for x,y,z,t st x,y // z,t holds z,t // x,y by AFF_1:13;
  thus thesis by AFF_1:11;
end;

theorem
  Lambda(OAS) is CongrSpace
proof
  Lambda(OAS) is AffinSpace by DIRAF:48;
  hence thesis by Th82;
end;

reserve f,g for Permutation of the carrier of AFS;

definition
  let AFS;
  let f;
  attr f is dilatation means
  :Def10:
  f is_DIL_of AFS;
end;

canceled;

theorem Th85:
  f is dilatation iff for a,b holds a,b // f.a,f.b
proof
  thus f is dilatation implies for a,b holds a,b // f.a,f.b
  proof
    assume
A1: f is dilatation;
    let a,b;
    f is_DIL_of AFS by A1,Def10;
    hence thesis by Th35;
  end;
  assume for a,b holds a,b // f.a,f.b;
  then f is_DIL_of AFS by Th35;
  hence thesis by Def10;
end;

theorem Th86:
  id the carrier of AFS is dilatation
proof
  now
    let x,y;
    (id the carrier of AFS).x = x & (id the carrier of AFS).y = y by
    FUNCT_1:35;
    hence x,y // (id the carrier of AFS).x,(id the carrier of AFS).y by
    AFF_1:11;
  end;
  hence thesis by Th85;
end;

theorem Th87:
  f is dilatation implies f" is dilatation
proof
  assume
A1: f is dilatation;
  now
    let x,y;
    set x9=f".x;
    set y9=f".y;
    f.x9=x & f.y9=y by Th4;
    then x9,y9 // x,y by A1,Th85;
    hence x,y // f".x,f".y by AFF_1:13;
  end;
  hence thesis by Th85;
end;

theorem Th88:
  f is dilatation & g is dilatation implies (f*g) is dilatation
proof
  assume
A1: f is dilatation & g is dilatation;
  now
    let x,y;
    set x9=g.x;
    set y9=g.y;
A2: (f*g).x= f.x9 & (f*g).y=f.y9 by FUNCT_2:21;
A3: now
      assume x9=y9;
      then x=y by FUNCT_2:85;
      hence x,y // (f*g).x,(f*g).y by AFF_1:12;
    end;
    x,y // x9,y9 & x9,y9 // f.x9,f.y9 by A1,Th85;
    hence x,y // (f*g).x,(f*g).y by A2,A3,AFF_1:14;
  end;
  hence thesis by Th85;
end;

theorem Th89:
  f is dilatation implies for a,b,c,d holds a,b // c,d iff f.a,f.b // f.c,f.d
proof
  assume
A1: f is dilatation;
  let a,b,c,d;
A2: c,d // f.c,f.d by A1,Th85;
A3: a,b // f.a,f.b by A1,Th85;
A4: now
A5: now
A6:   now
        assume f.c = f.d;
        then c = d by FUNCT_2:85;
        hence a,b // c,d by AFF_1:12;
      end;
      assume a,b // f.c,f.d;
      hence a,b // c,d by A2,A6,AFF_1:14;
    end;
A7: now
      assume f.a=f.b;
      then a=b by FUNCT_2:85;
      hence a,b // c,d by AFF_1:12;
    end;
    assume f.a,f.b // f.c,f.d;
    hence a,b // c,d by A3,A7,A5,AFF_1:14;
  end;
  now
A8: now
A9:   c = d implies f.a,f.b // f.c,f.d by AFF_1:12;
      assume f.a,f.b // c,d;
      hence f.a,f.b // f.c,f.d by A2,A9,AFF_1:14;
    end;
    assume a,b // c,d;
    then f.a,f.b // c,d or a=b by A3,AFF_1:14;
    hence f.a,f.b // f.c,f.d by A8,AFF_1:12;
  end;
  hence thesis by A4;
end;

theorem
  f is dilatation implies for a,b,c holds LIN a,b,c iff LIN f.a,f.b,f.c
proof
  assume
A1: f is dilatation;
  let a,b,c;
  a,b // a,c iff f.a,f.b // f.a,f.c by A1,Th89;
  hence thesis by AFF_1:def 1;
end;

theorem Th91:
  f is dilatation & LIN x,f.x,y implies LIN x,f.x,f.y
proof
  assume
A1: f is dilatation;
  assume
A2: LIN x,f.x,y;
  now
    assume
A3: x<>y;
    x,f.x // x,y & x,y // f.x,f.y by A1,A2,Th85,AFF_1:def 1;
    then x,f.x // f.x,f.y by A3,AFF_1:14;
    then f.x,x // f.x,f.y by AFF_1:13;
    then LIN f.x,x,f.y by AFF_1:def 1;
    hence thesis by AFF_1:15;
  end;
  hence thesis by AFF_1:16;
end;

theorem Th92:
  a,b // c,d implies (a,c // b,d or ex x st LIN a,c,x & LIN b,d,x )
proof
  assume
A1: a,b // c,d;
  assume
A2: not a,c // b,d;
A3: now
    consider z such that
A4: a,b // c,z and
A5: a,c // b,z by DIRAF:47;
    assume
A6: a<>b;
A7: now
      c,d // c,z by A1,A6,A4,AFF_1:14;
      then LIN c,d,z by AFF_1:def 1;
      then LIN d,c,z by AFF_1:15;
      then d,c // d,z by AFF_1:def 1;
      then z,d // d,c by AFF_1:13;
      then consider t such that
A8:   b,d // d,t and
A9:   b,z // c,t by A2,A5,DIRAF:47;
      assume b<>z;
      then a,c // c,t by A5,A9,AFF_1:14;
      then c,a // c,t by AFF_1:13;
      then LIN c,a,t by AFF_1:def 1;
      then
A10:  LIN a,c,t by AFF_1:15;
      d,b // d,t by A8,AFF_1:13;
      then LIN d,b,t by AFF_1:def 1;
      then LIN b,d,t by AFF_1:15;
      hence thesis by A10;
    end;
    now
      assume b=z;
      then b,a // b,c by A4,AFF_1:13;
      then LIN b,a,c by AFF_1:def 1;
      then
A11:  LIN a,c,b by AFF_1:15;
      LIN b,d,b by AFF_1:16;
      hence thesis by A11;
    end;
    hence thesis by A7;
  end;
  now
    assume a=b;
    then LIN a,c,a & LIN b,d,a by AFF_1:16;
    hence thesis;
  end;
  hence thesis by A3;
end;

theorem Th93:
  f is dilatation implies ((f=id the carrier of AFS or for x holds
  f.x<>x) iff for x,y holds x,f.x // y,f.y )
proof
  assume
A1: f is dilatation;
A2: now
    assume
A3: for x,y holds x,f.x // y,f.y;
    assume f<>(id the carrier of AFS);
    then consider y such that
A4: f.y<>(id the carrier of AFS).y by FUNCT_2:113;
    given x such that
A5: f.x=x;
    x<>y by A5,A4,FUNCT_1:35;
    then consider z such that
A6: not LIN x,y,z by AFF_1:22;
    x,z // f.x,f.z by A1,Th85;
    then LIN x,z,f.z by A5,AFF_1:def 1;
    then
A7: LIN z,f.z,x by AFF_1:15;
    LIN z,f.z,z by AFF_1:16;
    then
A8: z,f.z // x,z by A7,AFF_1:19;
A9: f.y<>y by A4,FUNCT_1:35;
    x,y // f.x,f.y by A1,Th85;
    then
A10: LIN x,y,f.y by A5,AFF_1:def 1;
    then LIN y,x,f.y by AFF_1:15;
    then
A11: y,x // y,f.y by AFF_1:def 1;
A12: LIN y,f.y,x by A10,AFF_1:15;
A13: now
      assume z=f.z;
      then z,y //z,f.y by A1,Th85;
      then LIN z,y,f.y by AFF_1:def 1;
      then LIN y,f.y,y & LIN y,f.y,z by AFF_1:15,16;
      hence contradiction by A9,A12,A6,AFF_1:17;
    end;
    y,f.y // z,f.z by A3;
    then y,f.y // x,z by A13,A8,AFF_1:14;
    then y,x // x,z by A9,A11,AFF_1:14;
    then x,y // x,z by AFF_1:13;
    hence contradiction by A6,AFF_1:def 1;
  end;
  now
    assume
A14: f=id the carrier of AFS or for z holds f.z<>z;
    let x,y;
A15: x,y // f.x,f.y by A1,Th85;
A16: now
      assume
A17:  for z holds f.z<>z;
      assume
A18:  not x,f.x // y,f.y;
      then consider z such that
A19:  LIN x,f.x,z and
A20:  LIN y,f.y,z by A15,Th92;
      set t=f.z;
      LIN x,f.x,t by A1,A19,Th91;
      then
A21:  x,f.x // z,t by A19,AFF_1:19;
      LIN y,f.y,t by A1,A20,Th91;
      then
A22:  y,f.y // z,t by A20,AFF_1:19;
      z<>t by A17;
      hence contradiction by A18,A21,A22,AFF_1:14;
    end;
    now
      assume f=(id the carrier of AFS);
      then f.y=y by FUNCT_1:35;
      hence x,f.x // y,f.y by AFF_1:12;
    end;
    hence x,f.x // y,f.y by A14,A16;
  end;
  hence thesis by A2;
end;

theorem Th94:
  f is dilatation & f.a=a & f.b=b & not LIN a,b,x implies f.x=x
proof
  assume that
A1: f is dilatation and
A2: f.a=a and
A3: f.b=b and
A4: not LIN a,b,x;
  a,x // a,f.x by A1,A2,Th85;
  then LIN a,x,f.x by AFF_1:def 1;
  then
A5: LIN x,f.x,a by AFF_1:15;
  b,x // b,f.x by A1,A3,Th85;
  then LIN b,x,f.x by AFF_1:def 1;
  then
A6: LIN x,f.x,x & LIN x,f.x,b by AFF_1:15,16;
  assume f.x<>x;
  hence contradiction by A4,A5,A6,AFF_1:17;
end;

theorem Th95:
  f is dilatation & f.a=a & f.b=b & a<>b implies f=id the carrier of AFS
proof
  assume that
A1: f is dilatation and
A2: f.a=a and
A3: f.b=b and
A4: a<>b;
  now
    let x;
A5: now
      assume
A6:   LIN a,b,x;
      now
        consider z such that
A7:     not LIN a,b,z by A4,AFF_1:22;
        assume
A8:     x<>a;
A9:     not LIN a,z,x
        proof
          assume LIN a,z,x;
          then
A10:      LIN a,x,z by AFF_1:15;
          LIN a,x,a & LIN a,x,b by A6,AFF_1:15,16;
          hence contradiction by A8,A7,A10,AFF_1:17;
        end;
        f.z=z by A1,A2,A3,A7,Th94;
        hence f.x=x by A1,A2,A9,Th94;
      end;
      hence f.x=x by A2;
    end;
    not LIN a,b,x implies f.x=x by A1,A2,A3,Th94;
    hence f.x=(id the carrier of AFS).x by A5,FUNCT_1:35;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem
  f is dilatation & g is dilatation & f.a=g.a & f.b=g.b implies a=b or f =g
proof
  assume that
A1: f is dilatation and
A2: g is dilatation and
A3: f.a=g.a and
A4: f.b=g.b;
A5: (g"*f).b=g".(g.b) by A4,FUNCT_2:21
    .=b by Th4;
A6: g" is dilatation by A2,Th87;
  assume
A7: a<>b;
  (g"*f).a=g".(g.a) by A3,FUNCT_2:21
    .=a by Th4;
  then
A8: g"*f=(id the carrier of AFS) by A1,A7,A5,A6,Th88,Th95;
  now
    let x;
    g".(f.x)=(g"*f).x by FUNCT_2:21;
    then g".(f.x)=x by A8,FUNCT_1:35;
    hence g.x=f.x by Th4;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th97:
  not LIN a,b,c & a,b // c,d1 & a,b // c,d2 & a,c // b,d1 & a,c //
  b,d2 implies d1=d2
proof
  assume that
A1: not LIN a,b,c and
A2: a,b // c,d1 and
A3: a,b // c,d2 and
A4: a,c // b,d1 and
A5: a,c // b,d2;
  assume
A6: d1<>d2;
  a<>c by A1,AFF_1:16;
  then b,d1 // b,d2 by A4,A5,AFF_1:14;
  then LIN b,d1,d2 by AFF_1:def 1;
  then
A7: LIN d1,d2,b by AFF_1:15;
A8: now
    assume c = d1;
    then c,a // c,b by A4,AFF_1:13;
    then LIN c,a,b by AFF_1:def 1;
    hence contradiction by A1,AFF_1:15;
  end;
A9: LIN d1,d2,d1 by AFF_1:16;
  a<>b by A1,AFF_1:16;
  then c,d1 // c,d2 by A2,A3,AFF_1:14;
  then
A10: LIN c,d1,d2 by AFF_1:def 1;
  then
A11: LIN d1,d2,c by AFF_1:15;
  LIN d1,d2,c by A10,AFF_1:15;
  then d1,d2 // c,d1 by A9,AFF_1:19;
  then d1,d2 // a,b or c = d1 by A2,AFF_1:14;
  then d1,d2 // b,a by A8,AFF_1:13;
  then LIN d1,d2,a by A6,A7,AFF_1:18;
  hence contradiction by A1,A6,A11,A7,AFF_1:17;
end;

definition
  let AFS;
  let f;
  attr f is translation means
  :Def11:
  f is dilatation & (f = id the carrier of AFS or for a holds a<>f.a );
end;

canceled;

theorem
  id the carrier of AFS is translation
proof
  id the carrier of AFS is dilatation by Th86;
  hence thesis by Def11;
end;

theorem Th100:
  f is dilatation implies (f is translation iff for x,y holds x,f .x // y,f.y )
proof
  assume
A1: f is dilatation;
  then
  (f=(id the carrier of AFS) or for x holds f.x<>x) iff for x,y holds x,f.
  x // y,f.y by Th93;
  hence thesis by A1,Def11;
end;

canceled;

theorem Th102:
  f is translation & g is translation & f.a=g.a & not LIN a,f.a,x
  implies f.x=g.x
proof
  assume that
A1: f is translation and
A2: g is translation and
A3: f.a=g.a and
A4: not LIN a,f.a,x;
  set b=f.a, y=f.x, z=g.x;
  g is dilatation by A2,Def11;
  then
A5: a,x // b,z & a,b // x,z by A2,A3,Th85,Th100;
A6: f is dilatation by A1,Def11;
  then
A7: a,x // b,y by Th85;
  a,b // x,y by A1,A6,Th100;
  hence thesis by A4,A7,A5,Th97;
end;

theorem Th103:
  f is translation & g is translation & f.a=g.a implies f=g
proof
  assume that
A1: f is translation and
A2: g is translation and
A3: f.a=g.a;
  set b=f.a;
A4: f is dilatation by A1,Def11;
A5: now
    assume
A6: a<>b;
    for x holds f.x=g.x
    proof
      let x;
      now
        assume
A7:     LIN a,b,x;
        now
A8:       f<>(id the carrier of AFS) by A6,FUNCT_1:35;
          then
A9:       f.x<>x by A1,Def11;
          assume x<>a;
          consider p such that
A10:      not LIN a,b,p by A6,AFF_1:22;
A11:      f.p<>p by A1,A8,Def11;
A12:      not LIN p,f.p,x
          proof
A13:        LIN a,b,f.x by A4,A7,Th91;
            LIN a,b,a by AFF_1:16;
            then
A14:        LIN x,f.x,a by A6,A7,A13,AFF_1:17;
A15:        LIN p,f.p,p by AFF_1:16;
            LIN a,b,b by AFF_1:16;
            then
A16:        LIN x,f.x,b by A6,A7,A13,AFF_1:17;
            assume
A17:        LIN p,f.p,x;
            then LIN p,f.p,f.x by A4,Th91;
            then LIN x,f.x,p by A11,A17,A15,AFF_1:17;
            hence contradiction by A10,A9,A14,A16,AFF_1:17;
          end;
          f.p=g.p by A1,A2,A3,A10,Th102;
          hence thesis by A1,A2,A12,Th102;
        end;
        hence thesis by A3;
      end;
      hence thesis by A1,A2,A3,Th102;
    end;
    hence thesis by FUNCT_2:113;
  end;
  now
    assume
A18: b=a;
    then f=id the carrier of AFS by A1,Def11;
    hence thesis by A2,A3,A18,Def11;
  end;
  hence thesis by A5;
end;

theorem Th104:
  f is translation implies f" is translation
proof
A1: now
    assume
A2: for x holds f.x<>x;
    given y such that
A3: f".y=y;
    f.y=y by A3,Th4;
    hence contradiction by A2;
  end;
  assume
A4: f is translation;
  then f is dilatation by Def11;
  then
A5: f" is dilatation by Th87;
  f=(id the carrier of AFS) implies f"=(id the carrier of AFS) by FUNCT_1:
  67;
  hence thesis by A4,A5,A1,Def11;
end;

theorem
  f is translation & g is translation implies (f*g) is translation
proof
  assume that
A1: f is translation and
A2: g is translation;
  f is dilatation & g is dilatation by A1,A2,Def11;
  then
A3: (f*g) is dilatation by Th88;
  now
    assume
A4: (f*g)<>(id the carrier of AFS);
    for x holds (f*g).x<>x
    proof
      given x such that
A5:   (f*g).x=x;
      f.(g.x)=x by A5,FUNCT_2:21;
      then
A6:   g.x=f".x by Th4;
      f" is translation by A1,Th104;
      then (f*g) = f*f" by A2,A6,Th103;
      hence contradiction by A4,FUNCT_2:88;
    end;
    hence thesis by A3,Def11;
  end;
  hence thesis by A3,Def11;
end;

definition
  let AFS;
  let f;
  attr f is collineation means
  f is_automorphism_of the CONGR of AFS;
end;

canceled;

theorem Th107:
  f is collineation iff for x,y,z,t holds (x,y // z,t iff f.x,f.y // f.z,f.t)
proof
  thus f is collineation implies for x,y,z,t holds x,y // z,t iff f.x,f.y // f
  .z,f.t
  proof
    assume
A1: f is_automorphism_of the CONGR of AFS;
    let x,y,z,t;
    thus x,y // z,t implies f.x,f.y // f.z,f.t
    proof
      assume x,y // z,t;
      then [[x,y],[z,t]] in the CONGR of AFS by ANALOAF:def 2;
      then [[f.x,f.y],[f.z,f.t]] in the CONGR of AFS by A1,Def3;
      hence thesis by ANALOAF:def 2;
    end;
    assume f.x,f.y // f.z,f.t;
    then [[f.x,f.y],[f.z,f.t]] in the CONGR of AFS by ANALOAF:def 2;
    then [[x,y],[z,t]] in the CONGR of AFS by A1,Def3;
    hence thesis by ANALOAF:def 2;
  end;
  assume
A2: for x,y,z,t holds (x,y // z,t iff f.x,f.y // f.z,f.t);
  let x,y,z,t;
  x,y // z,t iff f.x,f.y // f.z,f.t by A2;
  hence thesis by ANALOAF:def 2;
end;

theorem Th108:
  f is collineation implies (LIN x,y,z iff LIN f.x,f.y,f.z)
proof
  assume f is collineation;
  then x,y // x,z iff f.x,f.y // f.x,f.z by Th107;
  hence thesis by AFF_1:def 1;
end;

theorem
  f is collineation & g is collineation implies f" is collineation & f*g
  is collineation & id the carrier of AFS is collineation
proof
  assume f is_automorphism_of the CONGR of AFS & g is_automorphism_of the
  CONGR of AFS;
  hence f" is_automorphism_of the CONGR of AFS & f*g is_automorphism_of the
  CONGR of AFS by Th29,Th30;
  thus id the carrier of AFS is_automorphism_of the CONGR of AFS by Th28;
end;

reserve A,C,K for Subset of AFS;

theorem Th110:
  a in A implies f.a in f.:A
proof
A1: dom f = the carrier of AFS by FUNCT_2:67;
  assume a in A;
  hence thesis by A1,FUNCT_1:def 12;
end;

theorem Th111:
  x in f.:A iff ex y st y in A & f.y=x
proof
  thus x in f.:A implies ex y st y in A & f.y=x
  proof
    assume x in f.:A;
    then ex y being set st y in dom f & y in A & x=f.y by FUNCT_1:def 12;
    hence thesis;
  end;
  given y such that
A1: y in A & f.y=x;
  dom f = the carrier of AFS by FUNCT_2:67;
  hence thesis by A1,FUNCT_1:def 12;
end;

theorem Th112:
  f.:A=f.:C implies A=C
proof
  assume
A1: f.:A=f.:C;
  now
    let b be set;
    assume
A2: b in C;
    then reconsider b9=b as Element of AFS;
    set y=f.b9;
    y in f.:C by A2,Th110;
    then ex a st a in A & f.a=y by A1,Th111;
    hence b in A by FUNCT_2:85;
  end;
  then
A3: C c= A by TARSKI:def 3;
  now
    let a be set;
    assume
A4: a in A;
    then reconsider a9=a as Element of AFS;
    set x=f.a9;
    x in f.:A by A4,Th110;
    then ex b st b in C & f.b=x by A1,Th111;
    hence a in C by FUNCT_2:85;
  end;
  then A c= C by TARSKI:def 3;
  hence thesis by A3,XBOOLE_0:def 10;
end;

theorem Th113:
  f is collineation implies f.:(Line(a,b))=Line(f.a,f.b)
proof
  assume
A1: f is collineation;
  now
    let x be set;
    assume
A2: x in Line(f.a,f.b);
    then reconsider x9=x as Element of AFS;
    consider y such that
A3: f.y=x9 by Th2;
    LIN f.a,f.b,f.y by A2,A3,AFF_1:def 2;
    then LIN a,b,y by A1,Th108;
    then y in Line(a,b) by AFF_1:def 2;
    hence x in f.:(Line(a,b)) by A3,Th111;
  end;
  then
A4: Line(f.a,f.b) c= f.:(Line(a,b)) by TARSKI:def 3;
  now
    let x be set;
    assume
A5: x in f.:(Line(a,b));
    then reconsider x9=x as Element of AFS;
    consider y such that
A6: y in Line(a,b) and
A7: f.y=x9 by A5,Th111;
    LIN a,b,y by A6,AFF_1:def 2;
    then LIN f.a,f.b,x9 by A1,A7,Th108;
    hence x in Line(f.a,f.b) by AFF_1:def 2;
  end;
  then f.:(Line(a,b)) c= Line(f.a,f.b) by TARSKI:def 3;
  hence thesis by A4,XBOOLE_0:def 10;
end;

theorem
  f is collineation & K is being_line implies f.:K is being_line
proof
  assume that
A1: f is collineation and
A2: K is being_line;
  consider a,b such that
A3: a<>b and
A4: K=Line(a,b) by A2,AFF_1:def 3;
  set q=f.b;
  set p=f.a;
A5: p<>q by A3,FUNCT_2:85;
  f.:K=Line(p,q) by A1,A4,Th113;
  hence thesis by A5,AFF_1:def 3;
end;

theorem Th115:
  f is collineation & A // C implies f.:A // f.:C
proof
  assume that
A1: f is collineation and
A2: A // C;
  consider a,b,c,d such that
A3: a<>b & c <>d and
A4: a,b // c,d and
A5: A=Line(a,b) & C=Line(c,d) by A2,AFF_1:51;
A6: f.a,f.b // f.c,f.d by A1,A4,Th107;
A7: f.a<>f.b & f.c <>f.d by A3,FUNCT_2:85;
  f.:A=Line(f.a,f.b) & f.:C=Line(f.c,f.d) by A1,A5,Th113;
  hence thesis by A7,A6,AFF_1:51;
end;

reserve AFP for AffinPlane,
  A,C,D,K for Subset of AFP,
  a,b,c,d,p,x,y for Element of AFP,
  f for Permutation of the carrier of AFP;

theorem
  (for A st A is being_line holds f.:A is being_line) implies f is collineation
proof
  assume
A1: for A st A is being_line holds f.:A is being_line;
A2: a<>b implies f.:(Line(a,b))=Line(f.a,f.b)
  proof
    set A=Line(a,b);
    set x=f.a;
    set y=f.b;
    set K=Line(x,y);
    set M=f.:A;
    assume
A3: a<>b;
    then x<>y by FUNCT_2:85;
    then
A4: K is being_line by AFF_1:def 3;
    A is being_line by A3,AFF_1:def 3;
    then
A5: M is being_line by A1;
    a in A by AFF_1:26;
    then
A6: x in M by Th110;
    b in A by AFF_1:26;
    then
A7: y in M by Th110;
    x in K & y in K by AFF_1:26;
    hence thesis by A3,A4,A5,A6,A7,AFF_1:30,FUNCT_2:85;
  end;
A8: f.:A is being_line implies A is being_line
  proof
    set K=f.:A;
    assume f.:A is being_line;
    then consider x,y such that
A9: x<>y and
A10: K=Line(x,y) by AFF_1:def 3;
    y in K by A10,AFF_1:26;
    then consider b such that
    b in A and
A11: f.b=y by Th111;
    x in K by A10,AFF_1:26;
    then consider a such that
    a in A and
A12: f.a=x by Th111;
    set C=Line(a,b);
    C is being_line & f.:C=K by A2,A9,A10,A12,A11,AFF_1:def 3;
    hence thesis by Th112;
  end;
A13: f.:A // f.:C implies A // C
  proof
    set K=f.:A;
    set M=f.:C;
    assume
A14: f.:A // f.:C;
    then M is being_line by AFF_1:50;
    then
A15: C is being_line by A8;
A16: K is being_line by A14,AFF_1:50;
    then
A17: A is being_line by A8;
    now
      assume
A18:  A<>C;
      assume not A // C;
      then consider p such that
A19:  p in A & p in C by A17,A15,AFF_1:72;
      set x=f.p;
      x in K & x in M by A19,Th110;
      hence contradiction by A14,A18,Th112,AFF_1:59;
    end;
    hence thesis by A8,A16,A17,AFF_1:55;
  end;
A20: f.a,f.b // f.c,f.d implies a,b // c,d
  proof
    set x=f.a;
    set y=f.b;
    set z=f.c;
    set t=f.d;
    assume
A21: f.a,f.b // f.c,f.d;
    now
      set C=Line(c,d);
      set A=Line(a,b);
      set K=f.:A;
      set M=f.:C;
A22:  c in C & d in C by AFF_1:26;
      assume
A23:  a<>b & c <>d;
      then
A24:  x<>y & z<>t by FUNCT_2:85;
      K=Line(x,y) & M=Line(z,t) by A2,A23;
      then
A25:  K // M by A21,A24,AFF_1:51;
      a in A & b in A by AFF_1:26;
      hence thesis by A13,A22,A25,AFF_1:53;
    end;
    hence thesis by AFF_1:12;
  end;
A26: A // C implies f.:A // f.:C
  proof
    assume
A27: A // C;
    then C is being_line by AFF_1:50;
    then
A28: f.:C is being_line by A1;
A29: A is being_line by A27,AFF_1:50;
    then
A30: f.:A is being_line by A1; then
A:  f.:A // f.:A by AFF_1:55;   
    A<>C implies f.:A // f.:C
    proof
      assume
A31:  A<>C;
      assume not f.:A // f.:C;
      then consider x such that
A32:  x in f.:A and
A33:  x in f.:C by A30,A28,AFF_1:72;
      consider b such that
A34:  b in C and
A35:  x=f.b by A33,Th111;
      consider a such that
A36:  a in A and
A37:  x=f.a by A32,Th111;
      a=b by A37,A35,FUNCT_2:85;
      hence contradiction by A27,A31,A36,A34,AFF_1:59;
    end;
    hence thesis by A,A1,A29,AFF_1:55;
  end;
  a,b // c,d implies f.a,f.b // f.c,f.d
  proof
    assume
A38: a,b // c,d;
    now
      set C=Line(c,d);
      set A=Line(a,b);
      set K=f.:A;
      set M=f.:C;
      a in A by AFF_1:26;
      then
A39:  f.a in K by Th110;
      b in A by AFF_1:26;
      then
A40:  f.b in K by Th110;
      d in C by AFF_1:26;
      then
A41:  f.d in M by Th110;
      c in C by AFF_1:26;
      then
A42:  f.c in M by Th110;
      assume a<>b & c <>d;
      then A // C by A38,AFF_1:51;
      hence thesis by A26,A39,A40,A42,A41,AFF_1:53;
    end;
    hence thesis by AFF_1:12;
  end;
  hence thesis by A20,Th107;
end;

theorem
  f is collineation & K is being_line & (for x st x in K holds f.x=x) &
  not p in K & f.p=p implies f=id the carrier of AFP
proof
  assume that
A1: f is collineation and
A2: K is being_line and
A3: for x st x in K holds f.x=x and
A4: not p in K and
A5: f.p=p;
A6: for x holds f.x=x
  proof
    let x;
    now
      assume not x in K;
      consider a,b such that
A7:   a in K and
A8:   b in K and
A9:   a<>b by A2,AFF_1:31;
      set A=Line(p,a);
A10:  p in A by AFF_1:26;
      f.:A=Line(f.p,f.a) by A1,Th113;
      then
A11:  f.:A=A by A3,A5,A7;
      A is being_line by A4,A7,AFF_1:def 3;
      then consider C such that
A12:  x in C and
A13:  A // C by AFF_1:63;
A14:  C is being_line by A13,AFF_1:50;
      f.:A // f.:C by A1,A13,Th115;
      then
A15:  f.:C // C by A13,A11,AFF_1:58;
A16:  a in A by AFF_1:26;
      not C // K
      proof
        assume C // K;
        then A // K by A13,AFF_1:58;
        hence contradiction by A4,A7,A10,A16,AFF_1:59;
      end;
      then consider c such that
A17:  c in C and
A18:  c in K by A2,A14,AFF_1:72;
      f.c = c by A3,A18;
      then c in f.:C by A17,Th110;
      then
A19:  f.:C=C by A17,A15,AFF_1:59;
      set M=Line(p,b);
A20:  b in M by AFF_1:26;
      f.:M=Line(f.p,f.b) by A1,Th113;
      then
A21:  f.:M=M by A3,A5,A8;
      M is being_line by A4,A8,AFF_1:def 3;
      then consider D such that
A22:  x in D and
A23:  M // D by AFF_1:63;
A24:  D is being_line by A23,AFF_1:50;
      f.:M // f.:D by A1,A23,Th115;
      then
A25:  f.:D // D by A23,A21,AFF_1:58;
A26:  p in M by AFF_1:26;
      not D // K
      proof
        assume D // K;
        then M // K by A23,AFF_1:58;
        hence contradiction by A4,A8,A26,A20,AFF_1:59;
      end;
      then consider d such that
A27:  d in D and
A28:  d in K by A2,A24,AFF_1:72;
      f.d=d by A3,A28;
      then d in f.:D by A27,Th110;
      then
A29:  f.:D=D by A27,A25,AFF_1:59;
A30:  A is being_line by A13,AFF_1:50;
      x=f.x
      proof
        assume
A31:    x<>f.x;
        f.x in C & f.x in D by A12,A22,A19,A29,Th110;
        then C=D by A12,A22,A14,A24,A31,AFF_1:30;
        then A // M by A13,A23,AFF_1:58;
        then A=M by A10,A26,AFF_1:59;
        hence contradiction by A2,A4,A7,A8,A9,A30,A10,A16,A20,AFF_1:30;
      end;
      hence thesis;
    end;
    hence thesis by A3;
  end;
  for x holds f.x=(id the carrier of AFP).x
  proof
    let x;
    f.x=x by A6;
    hence thesis by FUNCT_1:35;
  end;
  hence thesis by FUNCT_2:113;
end;
