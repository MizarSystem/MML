:: Limit of Sequence of Subsets
::  by Bo Zhang , Hiroshi Yamazaki and Yatsuka Nakamura
::
:: Received March 15, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies RELAT_1, SEQ_2, FUNCT_1, ORDINAL2, BOOLE, SUBSET_1, PROB_2,
      SETLIM_1, SETFAM_1, TARSKI, SEQM_3, PROB_1, YELLOW_6;
 notations NUMBERS, XCMPLX_0, XXREAL_0,
      NAT_1, FUNCT_1, TARSKI, XBOOLE_0, YELLOW_6, ZFMISC_1,
      SUBSET_1, RELSET_1, SETFAM_1, PROB_1, FUNCT_2, KURATO_2;
 constructors SETFAM_1, XXREAL_0, NAT_1, PROB_1, YELLOW_6, KURATO_2;
 registrations SUBSET_1, XREAL_0, RELSET_1, ARYTM_3, XBOOLE_0, ORDINAL1,
      NAT_1;
 requirements NUMERALS, BOOLE, SUBSET;
 definitions TARSKI;
 theorems FUNCT_1, FUNCT_2, SUBSET_1, PROB_1, NAT_1, TARSKI, XBOOLE_0,
      XBOOLE_1, XCMPLX_1, RELAT_1, SETFAM_1, ZFMISC_1, CARD_3, KURATO_2,
      YELLOW_6, PROB_2, XREAL_1, ORDINAL1;
 schemes FUNCT_2, NAT_1;

begin

 reserve n,m,k,k1,k2,i,j for Element of NAT;
 reserve x,y,z,X,Y,Z for set;
 reserve A for Subset of X;
 reserve A1,A2,A3 for SetSequence of X;
 reserve Si for SigmaField of X;
 reserve S,S1,S2,S3 for SetSequence of Si;

Lm1:
i <= j implies i = j or i + 1 <= j
proof
 assume i<=j;
 then i + 1 <= j + 1 by XREAL_1:8;
 then i + 1 <= j or i + 1 = j + 1 by NAT_1:26;
 hence thesis by XCMPLX_1:2;
end;

theorem Th1:
  (X <> {} & for x st x in X holds x = y) implies union X = y
proof assume that
A1: X <> {} and
A2: for x st x in X holds x = y;
    for x st x in X holds x c= y by A2;then
A3: union X c= y by ZFMISC_1:94;
    y c= union X
    proof
     consider x such that
     A4: x in X by A1,XBOOLE_0:def 1;
     x = y by A2,A4;
     hence thesis by A4,ZFMISC_1:92;
    end;
 hence thesis by A3,XBOOLE_0:def 10;
end;

theorem Th2:
  (X <> {} & for x st x in X holds x = y) implies meet X = y
proof assume that
A1: X <> {} and
A2: for x st x in X holds x = y;
    for x st x in X holds y c= x by A2;then
A3: y c= meet X by A1,SETFAM_1:6;
    meet X c= union X & union X c= y by A1,A2,Th1,SETFAM_1:3;then
    meet X c= y by XBOOLE_1:1;
 hence thesis by A3,XBOOLE_0:def 10;
end;

theorem Th3:
  for f be Function of NAT,Y for n holds {f.k: n <= k} <> {}
proof
  let f be Function of NAT,Y;
  let n;
  f.n in {f.k: n <= k};
  hence thesis;
end;

theorem Th4:
  for f be Function of NAT,Y holds f.(n + m) in {f.k : n <= k}
proof
  n <= n + m by NAT_1:29;
  hence thesis;
end;

theorem Th5:
  for f being Function of NAT,Y holds
    {f.k1: n <= k1} = {f.k2 : n+1 <=k2} \/ {f.n}
proof
    let f be Function of NAT,Y;
    set Z1 = {f.k1:n <= k1};
    set Z2 = {f.k2:(n+1) <= k2};
A1: {f.k1: n <= k1} c= {f.k2 : n+1 <= k2} \/ {f.n}
   proof
    let x;
    assume x in Z1;
    then consider z such that
     A2: x=z & z in Z1;
    consider k such that
     A3: z = f.k & n <= k by A2;
     z = f.k & n+1 <= k or z = f.k & n = k by A3,Lm1;
     then z in Z2 or z in {f.n} by TARSKI:def 1;
    hence x in (Z2 \/ {f.n}) by A2, XBOOLE_0:def 2;
   end;
   Z2 \/ {f.n} c= Z1
    proof
     let x; assume
         A4:x in Z2 \/ {f.n};
         now per cases by A4,XBOOLE_0:def 2;
          case x in Z2;
           then consider z such that
           A5: x = z & z in Z2;
           consider k11 being Element of NAT such that
           A6: z=f.k11 & n+1 <= k11 by A5;
           n <= k11 by A6,NAT_1:38;
          hence x in Z1 by A5,A6;
          end;
          case x in {f.n};
           then x = f.n by TARSKI:def 1;
           hence x in Z1;
          end;
         end;
        hence thesis;
    end;
 hence thesis by A1,XBOOLE_0:def 10;
end;

theorem Th6:
  for f be Function of NAT,Y holds
    (for k1 holds x in f.(n+k1)) iff
    (for Z st Z in {f.k2 : n <= k2} holds x in Z)
proof
 let f be Function of NAT,Y;
 set Z = {f.k2 : n <= k2};
A1: now assume
     A2: for k1 holds x in f.(n+k1);
     now let Z1 be set;
      assume Z1 in Z;
      then consider k1 being Element of NAT such that
      A3: Z1=f.k1 & n <= k1;
      ex m be Nat st k1 = n + m by A3,NAT_1:28;
      then consider k2 being Nat such that
      A4: Z1=f.(n + k2) by A3;
      k2 in NAT by ORDINAL1:def 13;
      hence x in Z1 by A2,A4;
     end;
     hence for Z1 being set st Z1 in {f.k2 : n <= k2} holds x in Z1;
    end;
   now assume
     A5: for Z1 being set st Z1 in {f.k2 : n <= k2} holds x in Z1;
     now let k;
      f.(n + k) in Z by Th4;
      hence x in f.(n + k) by A5;
     end;
     hence for k1 holds x in f.(n+k1);
    end;
 hence thesis by A1;
 end;

theorem Th7:
  for Y being non empty set for f being Function of NAT,Y holds
    x in rng f iff ex n st x = f.n
proof
let Y be non empty set;
let f be Function of NAT,Y;
A1: dom f = NAT by FUNCT_2:def 1;
  thus x in rng f implies ex n st x = f.n
   proof assume x in rng f;
    then consider y such that
    A2: y in dom f & x = f.y by FUNCT_1:def 5;
    reconsider m=y as Element of NAT by A2;
    take m;
   thus thesis by A2;
   end;
  given n such that
  A3: x = f.n;
 thus thesis by A1,A3,FUNCT_1:def 5;
end;

theorem Th8:
  for X, Y being non empty set for f being Function of X,Y holds
    rng f <> {} & f <> {}
 proof
  let X, Y be non empty set;
  let f be Function of X,Y;
  dom f <> {} by FUNCT_2:def 1;then
  consider x1 being set such that
  A1: x1 in dom f by XBOOLE_0:def 1;
  consider y1 being set such that
  A2:[x1,y1] in f by A1, RELAT_1:def 4;
  thus thesis by A2,RELAT_1:def 5;
 end;

theorem Th9:
  for Y be non empty set for f being Function of NAT,Y holds
    rng f = {f.k : 0 <= k}
proof
  let Y be non empty set;
  let f be Function of NAT,Y;
A1: dom f = NAT by FUNCT_2:def 1;
 set Z = {f.k : 0 <= k};
A2: Z c= rng f
   proof let x;
    assume x in Z;
    then consider n1 being Element of NAT such that
     A3: x=f.n1 & 0 <= n1;
    thus x in rng f by A3,FUNCT_2:6;
   end;
  rng f c= Z
   proof let y;
    assume y in rng f;
    then consider n be set such that
    A4: n in NAT & y = f.n by A1,FUNCT_1:def 5;
    reconsider n as Element of NAT by A4;
    0 <= n by NAT_1:18;
    hence y in Z by A4;
   end;
 hence rng f = Z by A2,XBOOLE_0:def 10;
end;

theorem Th10:
  for Y being non empty set for f being Function of NAT,Y holds
    rng (f ^\ k) = {f.n: k <= n}
  proof
  let Y be non empty set;
  let f be Function of NAT,Y;
   reconsider f1 = f ^\ k as Function of NAT,Y;
   rng f1 = {f.m: k <= m}
    proof
    A1: dom f1 = NAT by FUNCT_2:def 1;
    set Z = {f.m : k <= m};
    A2: Z c= rng f1
     proof let x;
      assume x in Z;
      then consider k1 being Element of NAT such that
      A3: x=f.k1 & k <= k1;
      consider k2 being Nat such that
      A4: k1 = k + k2 by A3,NAT_1:28;
AA:   k2 in NAT by ORDINAL1:def 13; then
      x = f1.k2 by A3,A4,KURATO_2:def 2;
      hence x in rng f1 by FUNCT_2:6,AA;
     end;
   rng f1 c= Z
    proof let y;
    assume y in rng f1;
    then consider m1 be set such that
    A5: m1 in NAT & y = f1.m1 by A1,FUNCT_1:def 5;
    reconsider m1 as Element of NAT by A5;
    y = f.(k+m1) by A5,KURATO_2:def 2;
    hence y in Z by Th4;
   end;
  hence thesis by A2,XBOOLE_0:def 10;
 end;
 hence thesis;
 end;

canceled;

theorem Th12:
  x in meet rng A1 iff for n holds x in A1.n
 proof
 A1: rng A1 <> {} by Th8;
 A2: dom A1 = NAT by FUNCT_2:def 1;
A3: now let x; assume
      A4: x in meet rng A1;
       now let k;
        A1.k in rng A1 by FUNCT_2:6;
        hence x in A1.k by A4,SETFAM_1:def 1;
       end;
      hence for n holds x in A1.n;
     end;
   now let x;
      assume A5: for n holds x in A1.n;
       now let Y;
        assume Y in rng A1;
        then consider n be set such that
        A6: n in NAT & Y = A1.n by A2,FUNCT_1:def 5;
        reconsider n as Element of NAT by A6;
        thus x in Y by A5,A6;
       end;
     hence x in meet rng A1 by A1,SETFAM_1:def 1;
    end;
   hence thesis by A3;
 end;

theorem Th13:
  Intersection A1 = meet rng A1
 proof
  A1: Intersection A1 c= meet rng A1
  proof let x;
   assume x in Intersection A1;
   then for n holds x in A1.n by PROB_1:29;
   hence x in meet rng A1 by Th12;
  end;
  now let x;
   assume x in meet rng A1;
     then for n holds x in A1.n by Th12;
     hence x in Intersection A1 by PROB_1:29;
   end;
  then meet rng A1 c= Intersection A1 by TARSKI:def 3;
 hence thesis by A1,XBOOLE_0:def 10;
end;

theorem
  Intersection A1 c= Union A1
proof
 meet rng A1 c= union rng A1 by SETFAM_1:3;
 then Intersection A1 c= union rng A1 by Th13;
 hence thesis by CARD_3:def 4;
end;

theorem Th15:
  (for n holds A1.n = A) implies Union A1 = A
proof assume
 A1: for n holds A1.n = A;
 A2:  rng A1 <> {} by Th8;
 now let x; assume
  x in rng A1;then
  consider n such that
  A3: x = A1.n by Th7;
  thus x = A by A3,A1;
 end;
 then union rng A1 = A by A2,Th1;
 hence thesis by CARD_3:def 4;
end;

theorem Th16:
  (for n holds A1.n = A) implies Intersection A1 = A
proof assume
 A1: for n holds A1.n = A;
 A2: rng A1 <> {} by Th8;
 now let x; assume
  x in rng A1;then
  consider n such that
  A3: x = A1.n by Th7;
  thus x = A by A3,A1;
 end;
 then meet rng A1 = A by A2,Th2;
 hence thesis by Th13;
end;

theorem
  A1 is constant implies Union A1 = Intersection A1
proof assume A1 is constant;then
   consider A being Subset of X such that
A1: for n holds A1.n = A by KURATO_2:def 8;
    Union A1 = A & Intersection A1 = A by A1,Th15,Th16;
 hence thesis;
end;

Lm2:
  A1 is constant & the_value_of A1 = A implies
    for n holds A1.n = A & Union A1 = A & Intersection A1 = A
proof
A1: A1 <> {} by Th8;assume that
A2: A1 is constant and
A3: the_value_of A1 = A;
consider x being set such that
A4: x in dom A1 & A = A1.x by A1,A2,A3,YELLOW_6:def 1;
A5: dom A1 = NAT by FUNCT_2:def 1;
reconsider x as Element of NAT by A4;
  for n holds A1.n = A by A4,A5,A2,FUNCT_1:def 16;
hence thesis by Th15,Th16;
end;

theorem Th18:
  A1 is constant & the_value_of A1 = A implies
    for n holds union {A1.k : n <= k} = A
proof assume
 A1: A1 is constant & the_value_of A1 = A;
 let n;
 set Y = {A1.k : n <= k};
 A2: Y <> {} by Th3;
 now let x; assume
  x in Y;then
  consider k such that
  A3: x = A1.k & n <= k;
  thus x = A by A1,A3,Lm2;
 end;
 hence thesis by A2,Th1;
end;

theorem Th19:
  A1 is constant & the_value_of A1 = A implies
    for n holds meet {A1.k : n <= k} = A
proof assume
 A1: A1 is constant & the_value_of A1 = A;
 let n;
 set Y = {A1.k : n <= k};
 A2: Y <> {} by Th3;
 now let x; assume
  x in Y;then
  consider k such that
  A3: x = A1.k & n <= k;
  thus x = A by A3,A1,Lm2;
 end;
 hence thesis by A2,Th2;
end;

theorem Th20:
for X being set, A1 being SetSequence of X holds
 ex f being Function of NAT,bool X st
  for n,Y st Y = {A1.k: n <= k} holds f.n = meet Y
 proof
  let X be set, A1 be SetSequence of X;
  defpred P[Element of NAT, Subset of X] means
    for Y st Y = {A1.k: $1 <= k} holds $2 = meet Y;
  A1: for n being Element of NAT ex y being Subset of X st P[n,y]
    proof
     let n be Element of NAT;
     set Y = {A1.k: n <= k};
     set y = meet Y;
     y is Subset of X
     proof
 A2:  now assume Y<>{};
      then consider Z1 being set such that
       A3: Z1 in Y by XBOOLE_0:def 1;
       consider k1 being Element of NAT such that
       A4: Z1 = A1.k1 & n <= k1 by A3;
       now let z;assume z in y;then
        z in Z1 by A3,SETFAM_1:def 1;
        hence z in X by A4;
       end;
      hence y is Subset of X by TARSKI:def 3;
      end;
    now assume Y = {};
       then y = {} by SETFAM_1:def 1;
       hence y is Subset of X by XBOOLE_1:2;
       end;
      hence thesis by A2;
     end;
   then reconsider y as Subset of X;
   for Y st Y = {A1.k: n <= k} holds y = meet Y;
   hence thesis;
  end;
  ex f being Function of NAT,bool X st
   for n being Element of NAT holds P[n, f.n] from FUNCT_2:sch 3(A1);
  hence thesis;
 end;

theorem Th21:
  for X being set,A1 being SetSequence of X holds
    ex f being Function of NAT,bool X st
      for n,Y st Y = {A1.k: n <= k} holds f.n = union Y
 proof
  let X be set, A1 be SetSequence of X;
  defpred P[Element of NAT, Subset of X] means
    for Y st Y = {A1.k: $1 <= k} holds $2 = union Y;
A1: for n being Element of NAT ex y being Subset of X st P[n,y]
    proof
     let n be Element of NAT;
     set Y = {A1.k: n <= k};
     set y = union Y;
     y is Subset of X
     proof
      now let z;
       assume z in y;
       then ex Z st z in Z & Z in Y by TARSKI:def 4;
       then consider Z1 be set such that
       A2: Z1 in Y & z in Z1;
       consider k1 being Element of NAT such that
       A3: Z1 = A1.k1 & n <= k1 by A2;
       thus z in X by A2,A3;
      end;
      hence thesis by TARSKI:def 3;
     end;
    then reconsider y as Subset of X;
    for Y st Y = {A1.k: n <= k} holds y = union Y;
   hence thesis;
  end;
  ex f being Function of NAT,bool X st
   for n being Element of NAT holds P[n, f.n] from FUNCT_2:sch 3(A1);
  hence thesis;
 end;

definition let X,A1;
  attr A1 is monotone means
:Def1:  A1 is non-decreasing or A1 is non-increasing;
end;

definition
 let X be set, A1 be SetSequence of X;
func inferior_setsequence A1 -> SetSequence of X means
:Def2: for n,Y st Y = {A1.k : n <= k} holds it.n = meet Y;
 existence by Th20;
 uniqueness
 proof let BSeq,CSeq be SetSequence of X such that
  A1: for n,Y st Y = {A1.k : n <= k} holds BSeq.n = meet Y and
  A2: for m,Y st Y = {A1.k : m <= k} holds CSeq.m = meet Y;
  A3: for y st y in NAT holds BSeq.y = CSeq.y
  proof
   let y;
   assume y in NAT;
   then reconsider y as Element of NAT;
   set Y = {A1.k: y <= k};
   BSeq.y = meet Y by A1;
  hence thesis by A2;
  end;
  NAT = dom BSeq & NAT = dom CSeq by FUNCT_2:def 1;
 hence thesis by A3,FUNCT_1:9;
 end;
end;

definition
 let X be set, A1 be SetSequence of X;
func superior_setsequence A1 -> SetSequence of X means
:Def3: for n,Y st Y = {A1.k : n <= k} holds it.n = union Y;
 existence by Th21;
 uniqueness
 proof let B,C be SetSequence of X such that
 A1: for n,Y st Y = {A1.k : n <= k} holds B.n = union Y and
 A2: for m,Y st Y = {A1.k : m <= k} holds C.m = union Y;
 A3: for y st y in NAT holds B.y = C.y
  proof
   let y;
   assume y in NAT;
   then reconsider y as Element of NAT;
   set Y = {A1.k: y <= k};
   B.y = union Y by A1;
   hence thesis by A2;
  end;
 NAT = dom B & NAT = dom C by FUNCT_2:def 1;
 hence thesis by A3,FUNCT_1:9;
 end;
end;

theorem Th22:
  (inferior_setsequence A1).0 = Intersection A1
proof
  (inferior_setsequence A1).0 = meet {A1.k : 0 <= k} by Def2
                       .= meet rng A1 by Th9
                       .= Intersection A1 by Th13;
 hence thesis;
end;

theorem Th23:
  (superior_setsequence A1).0 = Union A1
proof
  (superior_setsequence A1).0 = union {A1.k : 0 <= k} by Def3
                      .= union rng A1 by Th9
                      .= Union A1 by CARD_3:def 4;
 hence thesis;
end;

theorem Th24:
  x in (inferior_setsequence A1).n iff for k holds x in A1.(n+k)
proof
 A1: (inferior_setsequence A1).n = meet {A1.k : n <= k} by Def2;
 set Y = {A1.k : n <= k};
 A2: Y <> {} by Th3;
A3: now assume
     A4: x in (inferior_setsequence A1).n;
       now let k;
        A1.(n + k) in Y by Th4;
        hence x in A1.(n + k) by A1,A4,SETFAM_1:def 1;
       end;
      hence for k holds x in A1.(n+k);
     end;
  now assume
     for k holds x in A1.(n+k);
     then for Z st Z in Y holds x in Z by Th6;
     hence x in (inferior_setsequence A1).n by A1,A2,SETFAM_1:def 1;
    end;
  hence thesis by A3;
end;

theorem Th25:
  x in (superior_setsequence A1).n iff ex k st x in A1.(n + k)
proof
 A1: (superior_setsequence A1).n = union {A1.k : n <= k} by Def3;
 set Y = {A1.k : n <= k};
 thus x in (superior_setsequence A1).n implies ex k st x in A1.(n + k)
 proof
   assume x in (superior_setsequence A1).n;
   then consider Y1 being set such that
   A2: x in Y1 & Y1 in Y by A1,TARSKI:def 4;
   consider k11 being Element of NAT such that
   A3: Y1=A1.k11 & n <= k11 by A2;
   ex k be Nat st k11 = n + k by A3,NAT_1:28;
   then consider k22 being Nat such that
   A4: Y1=A1.(n + k22) by A3;
   k22 in NAT by ORDINAL1:def 13;
   hence thesis by A2,A4;
  end;
  now assume ex k st x in A1.(n + k);
   then consider k1 being Element of NAT such that
   A5: x in A1.(n + k1);
   A1.(n + k1) in Y by Th4;
   hence x in (superior_setsequence A1).n by A1,A5,TARSKI:def 4;
  end;
 hence thesis;
end;

theorem Th26:
  (inferior_setsequence A1).n = (inferior_setsequence A1).(n+1) /\ A1.n
proof
 now let n;
 A1: (inferior_setsequence A1).n = meet {A1.k1 : n <= k1} by Def2;
 A2: (inferior_setsequence A1).(n+1) = meet {A1.k2 : n+1 <= k2} by Def2;
 A3:{A1.k1 : n <= k1} <> {} by Th3;
 A4:{A1.k2 : n+1 <= k2} <> {} by Th3;
 {A1.k1: n <= k1} = {A1.k2 : n+1 <=k2} \/ {A1.n} by Th5;
 then {A1.k2 : n+1 <=k2} c= {A1.k1: n <= k1} by XBOOLE_1:7;then
 A5: (inferior_setsequence A1).(n)
  c= (inferior_setsequence A1).(n+1) by A1,A2,A4,SETFAM_1:7;
A6: now let x;assume
      A7: x in (inferior_setsequence A1).n;
      A1.n in {A1.k1 : n <= k1};
      hence x in (inferior_setsequence A1).(n+1) & x in A1.n
        by A1,A7,A5,SETFAM_1:def 1;
     end;
A8: now let x;
      assume A9: x in (inferior_setsequence A1).(n+1) & x in A1.n;
      for Z st Z in {A1.k2 : n <= k2} holds x in Z
      proof let Z;
        assume Z in {A1.k1 : n <= k1};
        then consider Z1 being set such that
        A10: Z=Z1 & Z1 in {A1.k1 : n <= k1};
        consider k11 being Element of NAT such that
        A11: Z1=A1.k11 & n <= k11 by A10;
          now per cases by A11,Lm1;
           case Z1=A1.k11 & n = k11;
            hence x in Z1 by A9;
           end;
           case Z1=A1.k11 & n+1 <= k11;
            then Z1 in {A1.k2 : n+1 <= k2};
            hence x in Z1 by A2,A9,SETFAM_1:def 1;
           end;
          end;
         hence x in Z by A10;
      end;
     then x in meet {A1.k2 : n <= k2} by A3,SETFAM_1:def 1;
     hence x in (inferior_setsequence A1).n by Def2;
    end;
  thus (inferior_setsequence A1).n
    = (inferior_setsequence A1).(n+1) /\ A1.n
      by A6,A8,XBOOLE_0:def 3;
 end;
hence thesis;
end;

theorem Th27:
  (superior_setsequence A1).n = (superior_setsequence A1).(n+1) \/ A1.n
proof
 now let n;
 {A1.k1: n <= k1} = {A1.k2 : n+1 <=k2} \/ {A1.n} by Th5;then
 {A1.k2 : n+1 <=k2} c= {A1.k1: n <= k1} by XBOOLE_1:7;then
 union {A1.k2 : n+1 <= k2} c= union {A1.k1 : n <= k1} by ZFMISC_1:95;then
 (superior_setsequence A1).(n+1)
   c= union {A1.k1 : n <= k1} by Def3;then
A1:(superior_setsequence A1).(n+1)
   c= (superior_setsequence A1).n by Def3;
A2: now let x;
      assume x in (superior_setsequence A1).n;
      then x in union {A1.k1 : n <= k1} by Def3;
      then consider Y1 being set such that
      A3: x in Y1 & Y1 in {A1.k1 : n <= k1} by TARSKI:def 4;
      consider k11 being Element of NAT such that
      A4: Y1=A1.k11 & n <= k11 by A3;
        now per cases by A4,Lm1;
          case Y1=A1.k11 & n = k11;
           hence x in A1.n by A3;
          end;
          case Y1=A1.k11 & n+1 <= k11;
            then Y1 in {A1.k2 : n+1 <= k2};
           hence x in union {A1.k2 : n+1 <= k2} by A3,TARSKI:def 4;
          end;
          end;
       hence x in A1.n or x in (superior_setsequence A1).(n+1) by Def3;
    end;
A5: now let x;
      assume A6: x in (superior_setsequence A1).(n+1) or x in A1.n;
      thus x in (superior_setsequence A1).n
      proof
       now per cases by A6;
          case x in (superior_setsequence A1).(n+1);
           hence x in (superior_setsequence A1).n by A1;
          end;
          case A7: x in A1.n;
           A1.n in {A1.k1 : n <= k1};
           then x in union {A1.k1 : n <= k1} by A7,TARSKI:def 4;
          hence x in (superior_setsequence A1).n by Def3;
          end;
          end;
       hence thesis;
      end;
    end;
  x in (superior_setsequence A1).n iff
  x in A1.n or x in (superior_setsequence A1).(n+1) by A2,A5;
 hence (superior_setsequence A1).n = (superior_setsequence A1).(n+1) \/ A1.n
      by XBOOLE_0:def 2;
 end;
hence thesis;
end;

theorem Th28:
  inferior_setsequence A1 is non-decreasing
proof
  now let n be Element of NAT;
    (inferior_setsequence A1).n
    = (inferior_setsequence A1).(n+1) /\ A1.n by Th26;
   hence (inferior_setsequence A1).n
    c= (inferior_setsequence A1).(n+1) by XBOOLE_1:17;
  end;
 hence thesis by PROB_2:15;
end;

theorem Th29:
  superior_setsequence A1 is non-increasing
proof
  now let n be Element of NAT;
  (superior_setsequence A1).n
    = (superior_setsequence A1).(n+1) \/ A1.n by Th27;
   hence (superior_setsequence A1).(n+1)
     c= (superior_setsequence A1).n by XBOOLE_1:7;
  end;
 hence thesis by PROB_2:14;
end;

theorem
  inferior_setsequence A1 is monotone &
    superior_setsequence A1 is monotone
proof
 thus inferior_setsequence A1 is monotone
  proof
   inferior_setsequence A1 is non-decreasing by Th28;
   hence thesis by Def1;
  end;
  superior_setsequence A1 is non-increasing by Th29;
 hence thesis by Def1;
end;

registration
  let X be set, A be SetSequence of X;
  cluster inferior_setsequence A -> non-decreasing;
  coherence by Th28;
end;

registration
  let X be set, A be SetSequence of X;
  cluster superior_setsequence A -> non-increasing;
  coherence by Th29;
end;

theorem
  Intersection A1 c= (inferior_setsequence A1).n
proof
  A1: 0 <= n by NAT_1:18;
  (inferior_setsequence A1).0 c= (inferior_setsequence A1).n
    by A1,PROB_1:def 7;
  hence thesis by Th22;
end;

theorem
  (superior_setsequence A1).n c= Union A1
proof
 A1: 0 <= n by NAT_1:18;
 (superior_setsequence A1).n c= (superior_setsequence A1).0
   by A1,PROB_1:def 6;
 hence thesis by Th23;
end;

theorem Th33:
  for A1,n holds {A1.k: n <= k} is Subset-Family of X
proof
 let A1,n;
 set Y1 = {A1.k: n <= k};
 Y1 c= bool X
 proof
 let x;
  assume x in Y1; then
  consider k such that
  A1: x = A1.k & n <= k;
  thus x in bool X by A1;
 end;
hence thesis;
end;

canceled;

theorem
  Union A1 = (Intersection Complement A1)`
proof
  (Intersection Complement A1)`
  = (Union Complement Complement A1)`` by PROB_1:def 5
 .= Union A1;
 hence thesis;
end;

theorem Th36:
  (inferior_setsequence A1).n = ((superior_setsequence Complement A1).n)`
proof
  set Y1 = {A1.k1: n <= k1};
  set Y2 = {(Complement A1).k2 : n <= k2};
  set Y3 = {(A1.k)`: n <= k};
 A1: Y1 <> {} & Y2 <> {} by Th3;
 A2: Y2 = Y3
  proof
   A3: Y2 c= Y3
    proof let x;
     assume x in Y2;then
     consider k such that
     A4: x = (Complement A1).k & n <= k;
     x = (A1.k)` & n <= k by A4,PROB_1:def 4;
     hence thesis;
    end;
  Y3 c= Y2
    proof let y;
     assume y in Y3; then
     consider k such that
     A5: y = (A1.k)` & n <= k;
     y = (Complement A1).k & n <= k by A5,PROB_1:def 4;
     hence thesis;
    end;
   hence thesis by A3,XBOOLE_0:def 10;
  end;
 reconsider Y1 as Subset-Family of X by Th33;
 Y3 = COMPLEMENT Y1
 proof
  A6: Y3 c= COMPLEMENT Y1
   proof let x;
     assume x in Y3; then
     consider k such that
     A7: x = (A1.k)` & n <= k;
     reconsider z = A1.k as Subset of X;
     (z`)` in Y1 by A7;
     hence thesis by A7,SETFAM_1:def 8;
    end;
 COMPLEMENT Y1 c= Y3
   proof let y; assume
     A8: y in COMPLEMENT Y1;then
     reconsider y as Subset of X;
     y` in Y1 by A8,SETFAM_1:def 8;then
     consider k such that
     A9: y` = A1.k & n <= k;
     y = (A1.k)` & n <= k by A9;
     hence thesis;
    end;
   hence thesis by A6,XBOOLE_0:def 10;
 end;
 then (superior_setsequence(Complement A1)).n
            = union COMPLEMENT Y1 by A2,Def3
           .= [#] X \ meet Y1 by A1,SETFAM_1:48
           .= X \ meet Y1 by SUBSET_1:def 4
           .= (meet Y1)` by SUBSET_1:def 5;
 hence thesis by Def2;
end;

theorem
  (superior_setsequence A1).n = ((inferior_setsequence Complement A1).n)`
proof
  reconsider Y = (inferior_setsequence(Complement A1)).n as Subset of X;
  Y = ((superior_setsequence(Complement Complement A1)).n)` by Th36
   .= ((superior_setsequence A1).n)`;
  hence thesis;
end;

theorem Th38:
Complement (inferior_setsequence A1) = (superior_setsequence Complement A1)
proof
 reconsider A2 = inferior_setsequence A1 as SetSequence of X;
 reconsider A3 = superior_setsequence Complement A1 as SetSequence of X;
 now let n;
  (A2.n)` = ((A3.n)`)` by Th36;
  hence (Complement A2).n = A3.n by PROB_1:def 4;
 end;
 hence thesis by FUNCT_2:113;
 end;

theorem
Complement superior_setsequence A1 = inferior_setsequence Complement A1
proof
 reconsider A2 = inferior_setsequence Complement A1 as SetSequence of X;
 Complement A2 = superior_setsequence Complement Complement A1 by Th38
              .= superior_setsequence A1;
 hence thesis;
end;

theorem
  (for n holds A3.n = A1.n \/ A2.n) implies for n holds
    (inferior_setsequence(A1)).n \/ (inferior_setsequence(A2)).n
      c= (inferior_setsequence(A3)).n
proof assume
A1: for n holds A3.n = A1.n \/ A2.n;
  reconsider X1 = (inferior_setsequence(A1)) as SetSequence of X;
  reconsider X2 = (inferior_setsequence(A2)) as SetSequence of X;
  reconsider X3 = (inferior_setsequence(A3)) as SetSequence of X;
  let n;
  X1.n \/ X2.n c= X3.n
  proof
   let x; assume
    A2: x in (X1.n \/ X2.n);
    A3: now per cases by A2,XBOOLE_0:def 2;
     case A4: x in X1.n;
       now let k;
        A5: x in A1.(n+k) by A4,Th24;
        A3.(n+k) = A1.(n+k) \/ A2.(n+k) by A1; hence
        x in A3.(n+k) by A5,XBOOLE_0:def 2;
       end;hence
      x in X3.n by Th24;
     end;
     case A6: x in X2.n;
      now let k;
        A7: x in A2.(n+k) by A6,Th24;
        A3.(n+k) = A1.(n+k) \/ A2.(n+k) by A1; hence
        x in A3.(n+k) by A7,XBOOLE_0:def 2;
       end;hence x in X3.n by Th24;
     end;
    end;
   thus x in X3.n by A3;
end;
 hence thesis;
end;

theorem
  (for n holds A3.n = A1.n /\ A2.n) implies for n holds
    (inferior_setsequence A3).n
      = (inferior_setsequence A1).n /\ (inferior_setsequence A2).n
proof assume
A1: for n holds A3.n = A1.n /\ A2.n;
  reconsider X1 = inferior_setsequence A1 as SetSequence of X;
  reconsider X2 = inferior_setsequence A2 as SetSequence of X;
  reconsider X3 = inferior_setsequence A3 as SetSequence of X;
  let n;
A2: X3.n c= X1.n /\ X2.n
  proof
   let x; assume
    A3: x in X3.n;
     now let k;
      x in A3.(n+k) by A3,Th24;then
      x in (A1.(n+k) /\ A2.(n+k)) by A1;hence
      x in A1.(n+k) & x in A2.(n+k) by XBOOLE_0:def 3;
     end;
    then x in X1.n & x in X2.n by Th24;
    hence x in X1.n /\ X2.n by XBOOLE_0:def 3;
  end;
 X1.n /\ X2.n c= X3.n
  proof
   let x;assume x in (X1.n /\ X2.n);then
   A4: x in X1.n & x in X2.n by XBOOLE_0:def 3;
    now let k;
     x in A1.(n+k) & x in A2.(n+k) by A4,Th24; then
     x in A1.(n+k) /\ A2.(n+k) by XBOOLE_0:def 3;
     hence x in A3.(n+k) by A1;
    end;
   hence x in X3.n by Th24;
 end;
 hence thesis by A2,XBOOLE_0:def 10;
end;

theorem
  (for n holds A3.n = A1.n  \/ A2.n) implies for n holds
    (superior_setsequence(A3)).n
     = (superior_setsequence(A1)).n \/ (superior_setsequence(A2)).n
 proof assume
A1: for n holds A3.n = A1.n \/ A2.n;
  reconsider X1 = (superior_setsequence(A1)) as SetSequence of X;
  reconsider X2 = (superior_setsequence(A2)) as SetSequence of X;
  reconsider X3 = (superior_setsequence(A3)) as SetSequence of X;
  let n;
A2: X3.n c= X1.n \/ X2.n
  proof
   let x;
    assume x in X3.n;then
    consider k such that
    A3: x in A3.(n+k) by Th25;
    A4: x in (A1.(n+k) \/ A2.(n+k)) by A1,A3;
     x in X1.n or x in X2.n
    proof
      now per cases by A4,XBOOLE_0:def 2;
      case x in A1.(n+k);
       hence x in X1.n by Th25;
      end;
      case x in A2.(n+k);
       hence x in X2.n by Th25;
      end;
      end;
     hence thesis;
    end;
   hence x in X1.n \/ X2.n by XBOOLE_0:def 2;
  end;
 X1.n \/ X2.n c= X3.n
  proof
   let x; assume
    A5: x in (X1.n \/ X2.n);
    A6: now per cases by A5,XBOOLE_0:def 2;
     case x in X1.n;then
      consider k such that
       A7: x in A1.(n+k) by Th25;
       A3.(n+k) = A1.(n+k) \/ A2.(n+k) by A1; then
        x in A3.(n+k) by A7,XBOOLE_0:def 2;
      hence x in X3.n by Th25;
     end;
     case x in X2.n;then
      consider k such that
       A8: x in A2.(n+k) by Th25;
       A3.(n+k) = A1.(n+k) \/ A2.(n+k) by A1; then
        x in A3.(n+k) by A8,XBOOLE_0:def 2;
      hence x in X3.n by Th25;
     end;
     end;
    thus x in X3.n by A6;
  end;
hence thesis by A2,XBOOLE_0:def 10;
end;

theorem
  (for n holds A3.n = A1.n /\ A2.n) implies for n holds
    (superior_setsequence A3).n
      c= (superior_setsequence A1).n /\ (superior_setsequence A2).n
 proof assume
A1: for n holds A3.n = A1.n /\ A2.n;
  reconsider X1 = superior_setsequence A1 as SetSequence of X;
  reconsider X2 = superior_setsequence A2 as SetSequence of X;
  reconsider X3 = superior_setsequence A3 as SetSequence of X;
  let n;
  X3.n c= X1.n /\ X2.n
  proof
   let x;
    assume x in X3.n;then
    consider k such that
    A2: x in A3.(n+k) by Th25;
    A3.(n+k) = A1.(n+k) /\ A2.(n+k) by A1;then
    x in A1.(n+k) & x in A2.(n+k) by A2,XBOOLE_0:def 3;then
    x in X1.n & x in X2.n by Th25;
    hence x in X1.n /\ X2.n by XBOOLE_0:def 3;
   end;
 hence thesis;
 end;

theorem Th44:
  A1 is constant & the_value_of A1 = A implies
    for n holds (inferior_setsequence A1).n = A
proof assume
 A1: A1 is constant & the_value_of A1 = A;
 let n;
 (inferior_setsequence(A1)).n = meet {A1.k : n <= k} by Def2;
 hence thesis by A1,Th19;
end;

theorem Th45:
  A1 is constant & the_value_of A1 = A implies
    for n holds (superior_setsequence A1).n = A
proof assume
 A1: A1 is constant & the_value_of A1 = A;
 let n;
 (superior_setsequence(A1)).n = union {A1.k : n <= k} by Def3;
 hence thesis by A1,Th18;
end;

theorem Th46:
  A1 is non-decreasing implies A1.n c= (superior_setsequence(A1)).(n+1)
proof
  assume A1 is non-decreasing;then
  A1: A1.n c= A1.(n+1) by PROB_2:15;
  A1.n c= union {A1.k : n+1 <= k}
  proof let x; assume
   A2: x in A1.n;
   A1.(n+1) in {A1.k : n+1 <= k};
   hence x in union {A1.k : n+1 <= k} by A2,A1,TARSKI:def 4;
  end;
 hence thesis by Def3;
end;

theorem Th47:
  A1 is non-decreasing implies
    (superior_setsequence A1).n = (superior_setsequence A1).(n+1)
proof
  assume A1 is non-decreasing;
  then A1.n c= (superior_setsequence A1).(n+1) by Th46;
  then (superior_setsequence A1).(n+1) \/ A1.n
    = (superior_setsequence A1).(n+1) by XBOOLE_1:12;
 hence thesis by Th27;
end;

theorem Th48:
  A1 is non-decreasing implies (superior_setsequence A1).n = Union A1
proof
 assume A1: A1 is non-decreasing;
 defpred P[Element of NAT] means (superior_setsequence A1).$1 = Union A1;
 for n being Element of NAT holds (superior_setsequence A1).n = Union A1
  proof
   A2: P[0] by Th23;
   A3: for k st P[k] holds P[k+1] by A1,Th47;
   thus for k holds P[k] from NAT_1:sch 1(A2,A3);
  end;
 hence thesis;
end;

theorem Th49:
A1 is non-decreasing implies Intersection superior_setsequence A1 = Union A1
proof
 assume A1: A1 is non-decreasing;
  Intersection superior_setsequence A1 = Union A1
   proof
    now let x;assume
     A2: x in Intersection superior_setsequence A1;
     now let n;
      (superior_setsequence A1).n = Union A1 by A1,Th48;
      hence x in Union A1 by A2,PROB_1:29;
     end;
    hence x in Union A1;
    end;
    then A3: Intersection superior_setsequence(A1)
     c= Union A1 by TARSKI:def 3;
    now let y;
     assume A4: y in Union A1;
     for n holds y in (superior_setsequence(A1)).n by A4,A1,Th48;
     hence y in Intersection superior_setsequence(A1) by PROB_1:29;
    end;
    then Union A1 c= Intersection superior_setsequence(A1) by TARSKI:def 3;
    hence thesis by A3,XBOOLE_0:def 10;
   end;
 hence thesis;
end;

theorem Th50:
  A1 is non-decreasing implies A1.n c= (inferior_setsequence A1).(n+1)
proof
  A1: (inferior_setsequence A1).(n+1) = meet {A1.k : n+1 <= k} by Def2;
  set Y = {A1.k : n+1 <= k};
  A2: Y <> {} by Th3;
  assume A3: A1 is non-decreasing;
  let x;
   assume A4: x in A1.n;
    now let y;
     assume y in Y;
     then consider k1 being Element of NAT such that
      A5: y=A1.k1 & n+1 <= k1;
     n <= k1 by A5,NAT_1:38;
     then A1.n c= A1.k1 by A3,PROB_1:def 7;
     hence x in y by A4,A5;
    end;
   hence thesis by A1,A2,SETFAM_1:def 1;
end;

theorem Th51:
  A1 is non-decreasing implies (inferior_setsequence A1).n = A1.n
proof
  assume A1 is non-decreasing;
  then A1.n c= (inferior_setsequence A1).(n+1) by Th50;
  then (inferior_setsequence A1).(n+1) /\ A1.n = A1.n by XBOOLE_1:28;
 hence thesis by Th26;
end;

theorem Th52:
  A1 is non-decreasing implies inferior_setsequence A1 = A1
proof
 assume A1 is non-decreasing;
 then (inferior_setsequence A1).n = A1.n by Th51;
 hence inferior_setsequence A1 = A1 by FUNCT_2:113;
end;

theorem Th53:
  A1 is non-increasing implies (superior_setsequence A1).(n+1) c= A1.n
proof
  assume A1: A1 is non-increasing;
   let x;
    assume x in (superior_setsequence(A1)).(n+1);
    then consider k such that
    A2: x in A1.(n +1+k) by Th25;
    n+1 <= n+1+k by NAT_1:29;
    then n <= n+1+k by NAT_1:38;
    then A1.(n +1+k) c= A1.n by A1,PROB_1:def 6;
   hence x in A1.n by A2;
end;

theorem Th54:
  A1 is non-increasing implies (superior_setsequence(A1)).n = A1.n
proof
  assume A1 is non-increasing;
  then (superior_setsequence(A1)).(n+1) c= A1.n by Th53;
  then (superior_setsequence(A1)).(n+1) \/ A1.n = A1.n by XBOOLE_1:12;
 hence thesis by Th27;
end;

theorem Th55:
  A1 is non-increasing implies superior_setsequence(A1) = A1
proof
 assume A1 is non-increasing;
 then (superior_setsequence(A1)).n = A1.n by Th54;
 hence superior_setsequence(A1) = A1 by FUNCT_2:113;
end;

theorem Th56:
  A1 is non-increasing implies (inferior_setsequence(A1)).(n+1) c= A1.n
proof
  A1: (inferior_setsequence(A1)).(n+1) = meet {A1.k : n+1 <= k} by Def2;
  set Y = {A1.k : n+1 <= k};
  A2:A1.(n+1) in Y;
  assume A1 is non-increasing;
  then A3: A1.(n+1) c= A1.n by PROB_2:14;
   now let x;
    assume x in meet Y;
    then x in A1.(n+1) by A2,SETFAM_1:def 1;
    hence x in A1.n by A3;
   end;
 hence thesis by A1,TARSKI:def 3;
end;

theorem Th57:
  A1 is non-increasing implies
   (inferior_setsequence(A1)).n = (inferior_setsequence(A1)).(n+1)
proof
  assume A1 is non-increasing;
  then (inferior_setsequence(A1)).(n+1) c= A1.n by Th56;then
   (inferior_setsequence(A1)).(n+1) /\ A1.n
    = (inferior_setsequence(A1)).(n+1) by XBOOLE_1:28;
 hence thesis by Th26;
end;

theorem Th58:
A1 is non-increasing implies (inferior_setsequence(A1)).n = Intersection A1
proof
 assume A1: A1 is non-increasing;
 defpred P[Element of NAT] means
  (inferior_setsequence(A1)).$1 = Intersection A1;
 for n being Element of NAT
  holds (inferior_setsequence(A1)).n = Intersection A1
  proof
   A2: P[0] by Th22;
   A3: for k st P[k] holds P[k+1] by A1,Th57;
   thus for k holds P[k] from NAT_1:sch 1(A2,A3);
  end;
 hence thesis;
end;

theorem Th59:
A1 is non-increasing implies Union inferior_setsequence(A1) = Intersection A1
proof
 assume A1: A1 is non-increasing;
  Union inferior_setsequence(A1) = Intersection A1
  proof
   now let x;
    assume x in Union inferior_setsequence(A1);
    then consider k such that
    A2: x in (inferior_setsequence(A1)).k by PROB_1:25;
   thus x in Intersection A1 by A1,A2,Th58;
   end;
   then A3: Union inferior_setsequence(A1) c= Intersection A1 by TARSKI:def 3;
   now let y;
    assume y in Intersection A1;
    then y in (inferior_setsequence(A1)).0 by Th22;
    hence y in Union inferior_setsequence(A1) by PROB_1:25;
   end;
   then Intersection A1 c= Union inferior_setsequence(A1) by TARSKI:def 3;
   hence thesis by A3,XBOOLE_0:def 10;
  end;
 hence thesis;
end;

definition
  let X be set, A1 be SetSequence of X;
  redefine func lim_inf A1 equals
 Union inferior_setsequence A1;
  compatibility
  proof
    let F be Subset of X;
    hereby assume A1: F = lim_inf A1;
      for x being set holds
        x in F iff x in Union inferior_setsequence A1
      proof
        let x be set;
        hereby assume x in F; then
          consider n being Element of NAT such that
A2:       for k being Element of NAT holds x in A1.(n+k) by A1,KURATO_2:7;
          x in (inferior_setsequence A1).n by A2,Th24;
          hence x in Union inferior_setsequence A1 by PROB_1:25;
        end;
        assume x in Union inferior_setsequence A1; then
        consider n being Element of NAT such that
A3:     x in (inferior_setsequence A1).n by PROB_1:25;
        for k being Element of NAT holds x in A1.(n + k) by Th24,A3;
        hence thesis by A1,KURATO_2:7;
      end;
      hence F = Union inferior_setsequence A1 by TARSKI:2;
    end;
    assume A4: F = Union inferior_setsequence A1;
    for x being set holds x in F iff x in lim_inf A1
    proof
      let x be set;
      hereby assume x in F; then
        consider n being Element of NAT such that
A5:     x in (inferior_setsequence A1).n by A4,PROB_1:25;
        for k being Element of NAT holds x in A1.(n + k) by Th24,A5;
        hence x in lim_inf A1 by KURATO_2:7;
      end;
      assume x in lim_inf A1; then
      consider n being Element of NAT such that
A6:   for k being Element of NAT holds x in A1.(n+k) by KURATO_2:7;
      x in (inferior_setsequence A1).n by A6,Th24;
      hence x in F by A4,PROB_1:25;
    end;
    hence F = lim_inf A1 by TARSKI:2;
  end;
end;

definition
  let X be set, A1 be SetSequence of X;
  redefine func lim_sup A1 equals
 Intersection superior_setsequence A1;
  compatibility
  proof
    let F be Subset of X;
    hereby assume A1: F = lim_sup A1;
      for x being set holds
        x in F iff x in Intersection superior_setsequence A1
      proof
        let x be set;
        hereby assume x in F; then
A2:      for n holds ex k st x in A1.(n+k) by A1,KURATO_2:8;
          for m holds x in (superior_setsequence A1).m
          proof
            let m;
            ex k st x in A1.(m+k) by A2;
            hence thesis by Th25;
          end;
          hence x in Intersection superior_setsequence A1 by PROB_1:29;
        end;
        assume A3: x in Intersection superior_setsequence A1;
        now let m;
          x in (superior_setsequence A1).m by A3,PROB_1:29;
          hence ex k st x in A1.(m + k) by Th25;
        end;
        hence thesis by A1,KURATO_2:8;
      end;
      hence F = Intersection superior_setsequence A1 by TARSKI:2;
    end;
    assume A4: F = Intersection superior_setsequence A1;
    for x being set holds x in F iff x in lim_sup A1
    proof
      let x be set;
      hereby assume A5: x in F;
        now let m;
          x in (superior_setsequence A1).m by A5,A4,PROB_1:29;
          hence ex k st x in A1.(m + k) by Th25;
        end;
        hence x in lim_sup A1 by KURATO_2:8;
      end;
      assume x in lim_sup A1; then
A6:  for n holds ex k st x in A1.(n+k) by KURATO_2:8;
      for m holds x in (superior_setsequence A1).m
      proof
        let m;
        ex k st x in A1.(m+k) by A6;
        hence thesis by Th25;
      end;
      hence x in F by A4,PROB_1:29;
    end;
    hence F = lim_sup A1 by TARSKI:2;
  end;
end;

definition
  let X be set, A1 be SetSequence of X;
  assume A1: A1 is convergent;
  func lim A1 -> Subset of X means
:Def6: it = lim_sup A1 & it = lim_inf A1;
 existence
 proof
  lim_sup A1 = lim_inf A1 by A1,KURATO_2:def 7;
  hence thesis;
 end;
 uniqueness;
end;

theorem
  Intersection A1 c= lim_inf A1
proof
  let x;
  assume x in Intersection A1;
  then for k being Element of NAT holds x in A1.(0+k) by PROB_1:29;
  hence thesis by KURATO_2:7;
end;

theorem
  lim_inf A1 = lim inferior_setsequence(A1)
proof
  set A2 = inferior_setsequence(A1);
  inferior_setsequence(A2) = A2 by Th52;then
A1: lim_inf A2 = Union A2;
  Intersection superior_setsequence(A2) = Union A2 by Th49;then
A2: lim_sup A2 = Union A2;then
  A2 is convergent by A1,KURATO_2:def 7;then
  lim A2 = Union A2 by A2,Def6;
  hence thesis;
end;

theorem
  lim_sup A1 = lim superior_setsequence(A1)
proof
  set A2 = superior_setsequence(A1);
  Union inferior_setsequence(A2) = Intersection A2 by Th59;then
A1: lim_inf A2 = Intersection A2;
  superior_setsequence(A2) = A2 by Th55;then
A2: lim_sup A2 = Intersection A2;then
  A2 is convergent by A1,KURATO_2:def 7;then
  lim A2 = Intersection A2 by A2,Def6;
  hence thesis;
end;

theorem
  lim_sup A1 = (lim_inf Complement A1)`
proof
  lim_inf Complement A1 = (lim_sup Complement Complement A1)` by KURATO_2:12
                      .= (lim_sup A1)`;
  hence thesis;
end;

theorem Th64:
  A1 is constant & the_value_of A1 = A implies
    A1 is convergent & lim A1 = A & lim_inf A1 = A & lim_sup A1 = A
proof assume
 A1: A1 is constant & the_value_of A1 = A;then
 A2: for n holds (inferior_setsequence(A1)).n = A by Th44;
 A3: for n holds (superior_setsequence(A1)).n = A by A1,Th45;
 A4: lim_inf A1 = Union (inferior_setsequence(A1))
           .= A by A2,Th15;
     lim_sup A1 = Intersection (superior_setsequence(A1))
           .= A by A3,Th16;then
 lim_inf A1 = lim_sup A1 & A1 is convergent by A4,KURATO_2:def 7;
hence thesis by A4,Def6;
end;

theorem Th65:
  A1 is non-decreasing implies lim_sup A1 = Union A1 by Th49;

theorem Th66:
  A1 is non-decreasing implies lim_inf A1 = Union A1 by Th52;

theorem Th67:
  A1 is non-increasing implies lim_sup A1 = Intersection A1 by Th55;

theorem Th68:
  A1 is non-increasing implies lim_inf A1 = Intersection A1 by Th59;

theorem Th69:
  A1 is non-decreasing implies A1 is convergent & lim A1 = Union A1
proof
  assume A1 is non-decreasing;
  then A1: lim_sup A1 = Union A1 & lim_inf A1 = Union A1 by Th65,Th66;
  hence A1 is convergent by KURATO_2:def 7;
  hence lim A1 = Union A1 by A1,Def6;
end;

theorem Th70:
  A1 is non-increasing implies A1 is convergent & lim A1 = Intersection A1
proof
  assume A1 is non-increasing;
   then A1: lim_sup A1 = Intersection A1 & lim_inf A1 = Intersection A1
     by Th67,Th68;
   hence A1 is convergent by KURATO_2:def 7;
   hence lim A1 = Intersection A1 by A1,Def6;
end;

theorem
  A1 is monotone implies A1 is convergent
proof assume
  A1: A1 is monotone;
  per cases by A1,Def1;
   suppose A1 is non-increasing;
   hence A1 is convergent by Th70;
   end;
   suppose A1 is non-decreasing;
   hence A1 is convergent by Th69;
  end;
end;

definition
 let X be set,
     Si be SigmaField of X,
     S be SetSequence of Si;
 redefine attr S is constant means
  ex A being Element of Si st for n holds S.n = A;
 compatibility
  proof
A1:dom S = NAT by FUNCT_2:def 1;
  hereby assume
A2:   S is constant;
    consider n0 being Element of NAT;
    reconsider B = S.n0 as Element of Si by PROB_1:def 9;
    take B;
    let n be Element of NAT;
    thus S.n = B by A1,A2,FUNCT_1:def 16;
   end;
   given B being Element of Si such that
A3:  for n holds S.n=B;
   for n1, n2 being set st n1 in dom S
    & n2 in dom S holds S.n1 = S.n2
   proof let n1,n2 be set; assume
A4:  n1 in dom S & n2 in dom S;
   then S.n1 = B by A3
            .= S.n2 by A3,A4;
   hence thesis;
  end;
  hence thesis by FUNCT_1:def 16;
 end;
end;

definition
 let X be set,
     Si be SigmaField of X,
     S be SetSequence of Si;
 func @inferior_setsequence S -> SetSequence of Si equals
    inferior_setsequence S;
 coherence
  proof
   inferior_setsequence S is SetSequence of Si
    proof
     now let n;
      reconsider DSeq = S ^\ n as SetSequence of X;
      for m being Element of NAT holds DSeq.m in Si
       proof
         let m;
          DSeq.m = S.(m+n) by KURATO_2:def 2;
          hence thesis by PROB_1:def 9;
       end;
      then A1: Intersection DSeq in Si by PROB_1:def 8;
      Intersection DSeq = meet rng DSeq by Th13;
      then Intersection DSeq =meet {S.k: n <= k} by Th10;
      hence (inferior_setsequence S).n in Si by A1,Def2;
     end;
  hence thesis by PROB_1:def 9;
  end;
 hence thesis;
 end;
end;

definition
  let X be set,
      Si be SigmaField of X,
      S be SetSequence of Si;
  func @superior_setsequence S -> SetSequence of Si equals
 superior_setsequence S;
  coherence
  proof
    superior_setsequence S is SetSequence of Si
    proof
      now let n;
      reconsider DSeq = S ^\ n as SetSequence of X;
      for m being Element of NAT holds DSeq.m in Si
      proof
        let m;
        DSeq.m = S.(m+n) by KURATO_2:def 2;
        hence thesis by PROB_1:def 9;
      end;
      then DSeq is SetSequence of Si by PROB_1:def 9;
      then A1: Union DSeq in Si by PROB_1:46;
      Union DSeq = union rng DSeq by CARD_3:def 4;
      then Union DSeq =union {S.k: n <= k} by Th10;
      hence (superior_setsequence(S)).n in Si by A1,Def3;
    end;
    hence thesis by PROB_1:def 9;
  end;
  hence thesis;
end;
end;

theorem
  @superior_setsequence S is non-increasing;

theorem
  @inferior_setsequence(S) is non-decreasing;

registration
  let X be set,
      Si be SigmaField of X,
      S be SetSequence of Si;
  cluster @superior_setsequence(S) -> non-increasing;
  coherence;
end;

registration
  let X be set,
      Si be SigmaField of X,
      S be SetSequence of Si;
  cluster @inferior_setsequence(S) -> non-decreasing;
  coherence;
end;

definition
  let X be set,
      Si be SigmaField of X,
      S be SetSequence of Si;
  func lim_inf S -> Element of Si equals
  Union @inferior_setsequence(S);
  coherence by PROB_1:46;
end;

definition
 let X be set,
     Si be SigmaField of X,
     S be SetSequence of Si;
 func lim_sup S -> Element of Si equals
  Intersection @superior_setsequence(S);
 coherence
 proof
   for n holds (@superior_setsequence(S)).n in Si by PROB_1:def 9;
   hence thesis by PROB_1:32;
 end;
end;

definition
  let X be set,
     Si be SigmaField of X,
     S be SetSequence of Si;
  attr S is convergent means
:Def12: lim_sup S = lim_inf S;
end;

definition
  let X be set,
      Si be SigmaField of X,
      S be SetSequence of Si;
  assume A1: S is convergent;
  func lim S -> Element of Si means
:Def13: it = lim_sup S & it = lim_inf S;
  existence
  proof
    lim_sup S = lim_inf S by A1,Def12;
    hence thesis;
  end;
  uniqueness;
end;

theorem Th74:
  x in lim_sup S iff for n holds ex k st x in S.(n+k)
proof
A1: x in Intersection superior_setsequence A1 iff
       for n holds ex k st x in A1.(n+k)
   proof
     lim_sup A1 = Intersection superior_setsequence A1;
     hence thesis by KURATO_2:8;
   end;
   thus thesis by A1;
end;

theorem Th75:
  x in lim_inf S iff ex n st for k holds x in S.(n+k)
proof
A1: x in Union inferior_setsequence(A1) iff
       ex n st for k holds x in A1.(n+k)
  proof
    lim_inf A1 = Union inferior_setsequence(A1);
    hence thesis by KURATO_2:7;
  end;
  thus thesis by A1;
end;

theorem
  Intersection S c= lim_inf S
proof
 let x;
 assume x in Intersection S;
 then for k being Element of NAT holds x in S.(0+k) by PROB_1:29;
 hence thesis by Th75;
end;

theorem
  lim_sup S c= Union S
proof
  let x;
  assume x in lim_sup S;
  then consider k being Element of NAT such that
A1: x in S.(0+k) by Th74;
  thus thesis by A1,PROB_1:25;
end;

theorem
  lim_inf S c= lim_sup S
proof
 A1: Union inferior_setsequence A1
  c= Intersection superior_setsequence A1
  proof
   A2: lim_inf A1 = Union inferior_setsequence(A1);
   lim_sup A1 = Intersection superior_setsequence(A1);
  hence thesis by A2,KURATO_2:9;
  end;
   thus thesis by A1;
end;

definition
  let X be set,
      Si be SigmaField of X,
      S be SetSequence of Si;
  func @Complement S -> SetSequence of Si equals
 Complement S;
  coherence
proof
 now let n;
  S.n in Si by PROB_1:def 9;then
  A1: S.n is Event of Si by PROB_1:def 10;
  (Complement S).n = (S.n)` by PROB_1:def 4;
  then (Complement S).n is Event of Si by A1,PROB_1:50;
   hence (Complement S).n in Si by PROB_1:def 10;
   end; hence thesis by PROB_1:def 9;
  end;
end;

theorem Th79:
  lim_inf S = (lim_sup @Complement S)`
proof
 Union inferior_setsequence(A1)
   = (Intersection superior_setsequence(Complement A1))`
   proof
    A1: lim_inf A1 = Union inferior_setsequence(A1);
    (lim_sup Complement A1)`
      = (Intersection superior_setsequence(Complement A1))`;
    hence thesis by A1,KURATO_2:12;
  end;
 hence thesis;
end;

theorem
  lim_sup S = (lim_inf @Complement S)`
proof
  lim_inf @Complement S = (lim_sup @Complement @Complement S)` by Th79
                       .= (lim_sup S)`;
  hence thesis;
end;

theorem
  (for n holds S3.n = S1.n \/ S2.n) implies
    lim_inf S1 \/ lim_inf S2 c= lim_inf S3
proof assume
A1: (for n holds S3.n = S1.n \/ S2.n);
 (for n holds A3.n = A1.n \/ A2.n) implies
 Union inferior_setsequence(A1) \/ Union inferior_setsequence(A2)
 c= Union inferior_setsequence(A3)
 proof assume
  A2: (for n holds A3.n = A1.n \/ A2.n);
  A3: lim_inf A1 = Union inferior_setsequence(A1);
  A4: lim_inf A2 = Union inferior_setsequence(A2);
  lim_inf A3 = Union inferior_setsequence(A3);
  hence thesis by A2,A3,A4,KURATO_2:15;
 end;
hence thesis by A1;
end;

theorem
  (for n holds S3.n = S1.n /\ S2.n) implies
    lim_inf S3 = lim_inf S1 /\ lim_inf S2
proof assume
A1: (for n holds S3.n = S1.n /\ S2.n);
 (for n holds A3.n = A1.n /\ A2.n) implies
 Union inferior_setsequence(A1) /\ Union inferior_setsequence(A2)
 = Union inferior_setsequence(A3)
 proof assume
  A2: (for n holds A3.n = A1.n /\ A2.n);
  A3: lim_inf A1 = Union inferior_setsequence(A1);
  A4: lim_inf A2 = Union inferior_setsequence(A2);
 lim_inf A3 = Union inferior_setsequence(A3);
  hence thesis by A2,A3,A4,KURATO_2:13;
 end;
hence thesis by A1;
end;

theorem
  (for n holds S3.n = S1.n \/ S2.n) implies
    lim_sup S3 = lim_sup S1 \/ lim_sup S2
proof assume
A1: (for n holds S3.n = S1.n \/ S2.n);
 (for n holds A3.n = A1.n \/ A2.n) implies
 Intersection superior_setsequence(A1) \/ Intersection superior_setsequence(A2)
 = Intersection superior_setsequence(A3)
 proof assume
  A2: (for n holds A3.n = A1.n \/ A2.n);
  A3: lim_sup A1 = Intersection superior_setsequence(A1);
  A4: lim_sup A2 = Intersection superior_setsequence(A2);
 lim_sup A3 = Intersection superior_setsequence(A3);
  hence thesis by A2,A3,A4,KURATO_2:14;
 end;
hence thesis by A1;
end;

theorem
  (for n holds S3.n = S1.n /\ S2.n) implies
    lim_sup S3 c= lim_sup S1 /\ lim_sup S2
proof assume
A1: (for n holds S3.n = S1.n /\ S2.n);
 (for n holds A3.n = A1.n /\ A2.n) implies
 Intersection superior_setsequence(A3) c=
 Intersection superior_setsequence(A1) /\ Intersection superior_setsequence(A2)
 proof assume
  A2: (for n holds A3.n = A1.n /\ A2.n);
  A3: lim_sup A1 = Intersection superior_setsequence(A1);
  A4: lim_sup A2 = Intersection superior_setsequence(A2);
 lim_sup A3 = Intersection superior_setsequence(A3);
  hence thesis by A2,A3,A4,KURATO_2:16;
 end;
hence thesis by A1;
end;

theorem
  S is constant & the_value_of S = A implies
    S is convergent & lim S = A &
    lim_inf S = A & lim_sup S = A
proof assume
A1: S is constant & the_value_of S = A;
  A1 is constant & the_value_of A1 = A implies
  Union inferior_setsequence(A1) = A &
   Intersection superior_setsequence(A1) = A
  proof assume
   A2: A1 is constant & the_value_of A1 = A;
   A3: lim_inf A1 = Union inferior_setsequence(A1);
   lim_sup A1 = Intersection superior_setsequence(A1);
   hence thesis by A2,A3,Th64;
  end;then
A4: lim_inf S = A & lim_sup S = A by A1;then
  S is convergent by Def12;
 hence thesis by A4,Def13;
end;

theorem Th86:
  S is non-decreasing implies lim_sup S = Union S
proof
A1: A1 is non-decreasing implies Intersection superior_setsequence(A1)
   = Union A1
 proof
   lim_sup A1 = Intersection superior_setsequence(A1);
  hence thesis by Th65;
 end;
 thus thesis by A1;
end;

theorem Th87:
  S is non-decreasing implies lim_inf S = Union S
proof
A1: A1 is non-decreasing implies Union inferior_setsequence(A1) = Union A1
 proof
   lim_inf A1 = Union inferior_setsequence(A1);
  hence thesis by Th66;
 end;
 thus thesis by A1;
end;

theorem Th88:
  S is non-decreasing implies S is convergent & lim S = Union S
proof
  assume S is non-decreasing;then
  A1: lim_sup S = Union S & lim_inf S = Union S by Th86,Th87;
  hence S is convergent by Def12;
  hence lim S = Union S by A1,Def13;
end;

theorem Th89:
  S is non-increasing implies lim_sup S = Intersection S
proof
A1: A1 is non-increasing implies Intersection superior_setsequence(A1)
 = Intersection A1
 proof
   lim_sup A1 = Intersection superior_setsequence(A1);
  hence thesis by Th67;
 end;
 thus thesis by A1;
end;

theorem Th90:
  S is non-increasing implies lim_inf S = Intersection S
proof
A1: A1 is non-increasing implies Union inferior_setsequence(A1)
  = Intersection A1
 proof
   lim_inf A1 = Union inferior_setsequence(A1);
  hence thesis by Th68;
 end;
 thus thesis by A1;
end;

theorem Th91:
  S is non-increasing implies S is convergent &
    lim S = Intersection S
proof
  assume S is non-increasing;then
  A1: lim_sup S = Intersection S & lim_inf S = Intersection S
       by Th89,Th90;
  hence S is convergent by Def12;
  hence lim S = Intersection S by A1,Def13;
end;

theorem
  S is monotone implies S is convergent
proof
  assume A1: S is monotone;
  per cases by A1,Def1;
   suppose S is non-increasing;
   hence S is convergent by Th91;
   end;
   suppose S is non-decreasing;
   hence S is convergent by Th88;
  end;
end;
