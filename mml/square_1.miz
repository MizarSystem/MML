:: Some Properties of Real Numbers.
:: Operations: min, max, square, and square root
::  by Andrzej Trybulec and Czes{\l}aw Byli\'nski
::
:: Received November 16, 1989
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies ARYTM, ARYTM_3, RELAT_1, ARYTM_1, SQUARE_1, XCMPLX_0, COMPLEX1;
 notations TARSKI, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0, XXREAL_0;
 constructors NUMBERS, XCMPLX_0, XXREAL_0, XREAL_0;
 registrations NUMBERS, XCMPLX_0, XXREAL_0, XREAL_0, REAL_1, ORDINAL1,
      XBOOLE_0;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, REAL_1;
 theorems AXIOMS, REAL_1, XREAL_0, XCMPLX_0, XCMPLX_1, XREAL_1, XXREAL_0;

begin

 reserve a,b,c,x,y,z for real number;

canceled;

theorem
   1 < x implies 1/x < 1
 proof
   assume
A1: 1<x;
   then 0<x" by XREAL_1:124;
   then 1*x"<x*x" by A1,XREAL_1:70;
   then 1"*(1*x")<1"*1 by A1,XCMPLX_0:def 7;
   hence 1/x < 1 by XCMPLX_0:def 9;
 end;

canceled 22;

theorem
   0 <= x*y implies 0 <= x & 0 <= y or x <= 0 & y <= 0 by XREAL_1:134;

scheme RealContinuity { P[set], Q[set] } :
 ex z st for x,y st P[x] & Q[y] holds x <= z & z <= y
provided
A1: for x,y st P[x] & Q[y] holds x <= y
proof
 set X = { z where z is Element of REAL : P[z] };
 set Y = { z where z is Element of REAL : Q[z] };
A2: X c= REAL
   proof let x be set; assume x in X;
    then ex z being Element of REAL st z = x & P[z];
    hence thesis;
   end;
      Y c= REAL
   proof let x be set; assume x in Y;
   then ex z being Element of REAL st z = x & Q[z];
    hence thesis;
   end;
 then reconsider X, Y as Subset of REAL by A2;
      for x,y being real number st x in X & y in Y holds x <= y
   proof let x,y be real number;
    assume x in X & y in Y;
    then (ex z being Element of REAL st z = x & P[z]) &
    ex z being Element of REAL st z = y & Q[z];
    hence x <= y by A1;
   end;
  then consider z being real number such that
A3: for x,y being real number st x in X & y in Y holds x <= z & z <= y
  by AXIOMS:26;
 take z; let x,y;
 assume
A4: P[x] & Q[y];
      x is Element of REAL & y is Element of REAL by XREAL_0:def 1;
 then x in X & y in Y by A4;
 hence x <= z & z <= y by A3;
end;

definition let x,y be Element of REAL;
 redefine func min(x,y) -> Element of REAL;
 coherence by XREAL_0:def 1;
 redefine func max(x,y) -> Element of REAL;
 coherence by XREAL_0:def 1;
end;

canceled 13;

theorem
   x <= y & x <= z iff x <= min(y,z) by XXREAL_0:20,22;

canceled 10;

theorem
    y <= x & z <= x iff max(y,z) <= x by XXREAL_0:28,30;

canceled 2;

theorem
    min(x,y) + max(x,y) = x + y
 proof
   per cases;
   suppose x <= y;
     then min(x,y) = x & max (x,y) = y by XXREAL_0:def 8,def 9;
     hence min(x,y) + max(x,y) = x + y;
   end;
   suppose x > y;
     then min(x,y) = y & max (x,y) = x by XXREAL_0:def 8,def 9;
     hence min(x,y) + max(x,y) = x + y;
   end;
 end;

definition let x be complex number;
 canceled 2;

  func x^2 equals
     x * x;
  correctness;
end;

registration let x be complex number;
  cluster x^2 -> complex;
  coherence;
end;

registration let x;
  cluster x^2 -> real;
  coherence;
end;

definition let x be Element of COMPLEX;
  redefine func x^2 -> Element of COMPLEX;
  coherence by XCMPLX_0:def 2;
end;

definition let x be Element of REAL;
  redefine func x^2 -> Element of REAL;
  coherence by XREAL_0:def 1;
end;

canceled 5;

theorem
Th59: 1^2 = 1;

theorem
Th60: 0^2 = 0;

theorem
   for a being complex number holds a^2 = (-a)^2;

canceled;

theorem
   for a, b being complex number holds
  (a + b)^2 = a^2 + 2*a*b + b^2;

theorem
   for a, b being complex number holds
  (a - b)^2 = a^2 - 2*a*b + b^2;

theorem
   for a being complex number holds
  (a + 1)^2 = a^2 + 2*a + 1;

theorem
    for a being complex number holds
  (a - 1)^2 = a^2 - 2*a + 1;

theorem
   for a, b being complex number holds
  (a - b)*(a + b) = a^2 - b^2;

theorem
   for a, b being complex number holds
  (a*b)^2 = a^2*b^2;

canceled;

theorem Th70:
  for a, b being complex number holds
  a^2 - b^2 <> 0 implies 1/(a+b) = (a-b)/(a^2-b^2)
 proof
  let a, b be complex number;
  assume a^2-b^2 <> 0;
  then (a+b)*(a-b) <> 0;
  then (a+b) <> 0 & (a-b) <> 0;
  hence 1/(a+b) = (1*(a-b))/((a+b)*(a-b)) by XCMPLX_1:92
               .= (a-b)/(a^2-b^2);
 end;

theorem Th71:
  for a, b being complex number holds
  a^2-b^2 <> 0 implies 1/(a-b) = (a+b)/(a^2-b^2)
 proof
  let a, b be complex number;
  assume a^2-b^2 <> 0;
  then (a+b)*(a-b) <> 0;
  then (a+b) <> 0 & (a-b) <> 0;
  hence 1/(a-b) = (1*(a+b))/((a-b)*(a+b)) by XCMPLX_1:92
               .= (a+b)/(a^2-b^2);
 end;

canceled 2;

theorem
    0 <> a implies 0 < a^2
 proof assume 0 <> a; hence 0 <= a^2 & a^2 <> 0 by XCMPLX_1:6,XREAL_1:65; end;

theorem Th75:
  0 < a & a < 1 implies a^2 < a
 proof assume 0 < a & a < 1;
   then a*a < a*1 by XREAL_1:70;
  hence a^2 < a;
 end;

theorem Th76:
  1 < a implies a < a^2
 proof assume 1 < a;
   then a*1 < a*a by XREAL_1:70;
   hence a < a^2;
 end;

Lm1:
  0 < a implies ex x st 0 < x & x^2 < a
 proof assume
A1: 0 < a;
     per cases;
    suppose
A2:   1 <= a;
     reconsider x = 2" as real number;
     take x;
     thus 0 < x;
     thus x^2 < a by A2,XXREAL_0:2;
    end;
    suppose
A3:   a < 1;
     take x = a;
     thus 0 < x by A1;
     thus x^2 < a by A1,A3,Th75;
   end;
 end;

theorem Th77:
  0 <= x & x <= y implies x^2 <= y^2
 proof assume 0 <= x & x <= y;
   then x*x <= x*y & x*y <= y*y by XREAL_1:66;
   hence x^2 <= y^2 by XXREAL_0:2;
 end;

theorem Th78:
  0 <= x & x < y implies x^2 < y^2
 proof assume 0 <= x & x < y;
   then x*x <= x*y & x*y < y*y by XREAL_1:66,70;
  hence x^2 < y^2 by XXREAL_0:2;
 end;

Lm2:
  0 <= x & 0 <= y & x^2 = y^2 implies x = y
proof assume
A1: 0 <= x & 0 <= y;
  assume x^2 = y ^2;
  then x <= y & y <= x by A1,Th78;
  hence x = y by XXREAL_0:1;
end;

definition let a;
 assume A1: 0 <= a;
  func sqrt a -> real number means
:Def4: 0 <= it & it^2 = a;
 existence
  proof
A2: a <= a + 1 by XREAL_1:31;
A3: (a + 1)^2 = a^2 + a + (a + 1);
     0 <= a^2 by XREAL_1:65;
    then 0 + 0 <= a^2 + a by A1,XREAL_1:9;
    then A4:  0 + a <= a^2 + a + (a + 1) by A2,XREAL_1:9;
   defpred X[real number] means $1 <= 0 or $1^2 <= a;
   defpred Y[real number] means 0 <= $1 & a <= $1^2;
A5: now let x,y such that
A6:  X[x] and
A7:  Y[y];
       per cases;
      suppose x <= 0; hence x <= y by A7;
      end;
      suppose not x <= 0;
       then x^2 <= y^2 by A6,A7,XXREAL_0:2;
      hence x <= y by A7,Th78;
     end;
   end;
   consider z such that
A8:  for x,y st X[x] & Y[y] holds x <= z & z <= y from RealContinuity(A5);
   take z;
   thus 0 <= z by A1,A2,A3,A4,A8;
   assume
A9:  z^2 <> a;
        now per cases by A9,XXREAL_0:1;
     suppose A10: z <= 0;
then z = 0 by A1,A2,A3,A4,A8;
       then consider c such that
A11:     0 < c and
A12:     c^2 < a by A1,A9,Lm1;
      thus contradiction by A1,A2,A3,A4,A8,A10,A11,A12;
     end;
     suppose
A13:   z^2 < a & not z <= 0;
      set b = a - z^2;
A14:   0/2 = 0;
A15:  0 < b by A13,XREAL_1:52;
      then 0 < b/2 by A14,REAL_1:73;
      then consider c such that
A16:    0 < c and
A17:    c^2 < b/2 by Lm1;
A18:    2*z <> 0 by A13;
A19:    0 <= 2*z by A13,XREAL_1:129;
       set eps = min(c, b/(4*z));
A20:    0/(4*z) = 0;
           0 < 4*z by A13,XREAL_1:131;
       then 0 < b/(4*z) by A15,A20,REAL_1:73;
then A21:    0 < eps by A16,XXREAL_0:15;
           eps <= b/(2*2*z) by XXREAL_0:17;
       then eps*(2*z) <= b/(2*(2*z))*(2*z) by A19,XREAL_1:66;
       then eps*(2*z) <= b/2/(2*z)*(2*z) by XCMPLX_1:79;
       then A22:    2*z*eps <= b/2 by A18,XCMPLX_1:88;
        eps <= c by XXREAL_0:17;
       then eps^2 <= c^2 by A21,Th77;
       then A23:    eps^2 <= b/2 by A17,XXREAL_0:2;
A24:    a = z^2 + b;
A25:    (z + eps)^2 = z^2 + (2*z*eps + eps^2);
           2*z*eps + eps^2 <= b/2 + b/2 by A22,A23,XREAL_1:9;
then A26:    (z + eps)^2 <= a by A24,A25,XREAL_1:8;
        z < z + eps by A21,XREAL_1:31;
      hence contradiction by A1,A2,A3,A4,A8,A26;
     end;
     suppose
A27:   a < z^2 & not z <= 0;
      set b = z^2 - a;
A28:   0 < 2*z by A27,XREAL_1:131;
A29:   2*z <> 0 by A27;
A30:   0 < b by A27,XREAL_1:52;
      set eps = min(b/(2*z),z);
A31:   0/(2*z) = 0;
          0 < 2*z by A27,XREAL_1:131;
      then 0 < b/(2*z) by A30,A31,REAL_1:73;
then A32:   0 < eps by A27,XXREAL_0:15;
A33:    a = z^2 - b;
A34:    (z - eps)^2 = z^2 - (2*z*eps - eps^2);
           eps <= b/(2*z) by XXREAL_0:17;
       then eps*(2*z) <= b/(2*z)*(2*z) by A28,XREAL_1:66;
       then A35:    2*z*eps <= b by A29,XCMPLX_1:88;
        0 <= eps^2 by XREAL_1:65;
       then 2*z*eps - eps^2 <= 2*z*eps - 0 by REAL_1:92;
       then 2*z*eps - eps^2 <= b by A35,XXREAL_0:2;
then A36:    a <= (z - eps)^2 by A33,A34,REAL_1:92;
        eps <= z by XXREAL_0:17;
then A37:   0 <= z - eps by XREAL_1:50;
        z - eps < z by A32,XREAL_1:46;
      hence contradiction by A8,A36,A37;
    end;
    end;
   hence contradiction;
  end;
 uniqueness by Lm2;
end;

definition let a be Element of REAL;
  redefine func sqrt a -> Element of REAL;
  coherence by XREAL_0:def 1;
end;

canceled 3;

theorem
   sqrt 0 = 0 by Def4,Th60;

theorem Th83:
  sqrt 1 = 1 by Def4,Th59;

Lm3:  0 <= a implies sqrt a^2 = a
 proof 0 <= a^2 by XREAL_1:65; hence thesis by Def4; end;

Lm4:  0 <= x & x < y implies sqrt x < sqrt y
 proof assume that
A1: 0 <= x and
A2: x < y;
A3: 0 <= sqrt x & 0 <= sqrt y by A1,A2,Def4;
       (sqrt x)^2 = x & (sqrt y)^2 = y by A1,A2,Def4;
  hence sqrt x < sqrt y by A2,A3,Th77;
 end;

theorem
   1 < sqrt 2 by Lm4,Th83;
Lm5: 2^2 = 2*2;

theorem Th85:
  sqrt 4 = 2 by Def4,Lm5;

theorem
   sqrt 2 < 2 by Lm4,Th85;

canceled 2;

theorem
  0 <= a implies sqrt a^2 = a by Lm3;

theorem
   a <= 0 implies sqrt a^2 = -a
 proof assume a <= 0;
  then -0 <= -a & a^2 = (-a)^2 by XREAL_1:26;
  hence sqrt a^2 = -a by Lm3;
 end;

canceled;

theorem
   0 <= a & sqrt a = 0 implies a = 0 by Def4,Th60;

theorem Th93:
  0 < a implies 0 < sqrt a
 proof assume 0 < a;
  then sqrt a <> 0 & 0 <= sqrt a by Def4,Th60;
  hence thesis;
 end;

theorem Th94:
  0 <= x & x <= y implies sqrt x <= sqrt y
  proof
     per cases;
     suppose x = y; hence thesis;
     end;
     suppose
A1:   x <> y;
      assume
A2:   0 <= x;
      assume x <= y; then x < y by A1,REAL_1:def 5;
      hence sqrt x <= sqrt y by A2,Lm4;
    end;
  end;

theorem
   0 <= x & x < y implies sqrt x < sqrt y by Lm4;

theorem Th96:
  0 <= x & 0 <= y & sqrt x = sqrt y implies x = y
 proof assume that
A1: 0 <= x & 0 <= y and
A2: sqrt x = sqrt y;
  assume x <> y; then x < y or y < x by XXREAL_0:1;
  hence contradiction by A1,A2,Lm4;
 end;

theorem Th97:
  0 <= a & 0 <= b implies sqrt (a*b) = sqrt a * sqrt b
 proof assume
A1:  0 <= a & 0 <= b;
then A2: 0 <= a*b by XREAL_1:129;
then A3:  (sqrt(a*b))^2 = a*b by Def4 .= (sqrt a)^2*b by A1,Def4
                 .= (sqrt a)^2*(sqrt b)^2 by A1,Def4
         .= (sqrt a*sqrt b)^2;
       0 <= sqrt a & 0 <= sqrt b by A1,Def4;
then A4: 0 <= sqrt(a*b) & 0 <= sqrt a*sqrt b by A2,Def4,XREAL_1:129;
  thus sqrt(a*b) = sqrt(sqrt a*sqrt b)^2 by A2,A3,Def4
     .= sqrt a*sqrt b by A4,Lm3;
 end;

canceled;

theorem Th99:
  0 <= a & 0 <= b implies sqrt (a/b) = sqrt a/sqrt b
 proof assume
A1: 0 <= a & 0 <= b;
then A2: 0 <= sqrt a & 0 <= sqrt b by Def4;
      (sqrt a/sqrt b)^2 = (sqrt a)^2/(sqrt b)^2 &
   (sqrt a)^2 = a & (sqrt b)^2 = b by A1,Def4,XCMPLX_1:77;
  then 0 <= a/b & 0 <= sqrt a/sqrt b & (sqrt a/sqrt b)^2 = a/b by A1,A2,
XREAL_1:138;
  hence thesis by Def4;
 end;

canceled;

theorem
   0 < a implies sqrt (1/a) = 1/sqrt a by Th83,Th99;

theorem
   0 < a implies sqrt a/a = 1/sqrt a
proof assume A1: 0 < a;
  then sqrt a <> 0 by Def4,Th60;
  hence sqrt a/a = (sqrt a)^2/(a*sqrt a) by XCMPLX_1:92
            .= (1*a)/(sqrt a*a) by A1,Def4
            .= 1/sqrt a by A1,XCMPLX_1:92;
 end;

theorem
    0 < a implies a / sqrt a = sqrt a
 proof assume A1: 0 < a;
   then sqrt a <> 0 by Def4,Th60;
   hence a /sqrt a = (a*sqrt a) /(sqrt a)^2 by XCMPLX_1:92
              .= (sqrt a*a) /(1*a) by A1,Def4
              .= sqrt a/1 by A1,XCMPLX_1:92
              .= sqrt a;
 end;

theorem
   0 <= a & 0 <= b implies (sqrt a - sqrt b)*(sqrt a + sqrt b) = a - b
 proof assume
A1: 0 <= a & 0 <= b;
  thus (sqrt a - sqrt b)*(sqrt a + sqrt b) = (sqrt a)^2 - (sqrt b)^2
                          .= a - (sqrt b)^2 by A1,Def4
                          .= a - b by A1,Def4;
 end;

Lm6:
  0 <= a & 0 <= b & a <> b implies (sqrt a)^2-(sqrt b)^2 <> 0
 proof assume 0 <= a & 0 <= b & a <> b;
  then sqrt a <> sqrt b & 0 <= sqrt a & 0 <= sqrt b by Def4,Th96;
  hence (sqrt a)^2-(sqrt b)^2 <> 0 by Lm2;
 end;

theorem
   0 <= a & 0 <= b & a <>b implies 1/(sqrt a+sqrt b) = (sqrt a - sqrt b)/(a-b)
 proof assume that
A1: 0 <= a and
A2: 0 <= b and
A3: a <>b;
      (sqrt a)^2-(sqrt b)^2 <> 0 by A1,A2,A3,Lm6;
  hence 1/(sqrt a+sqrt b) = (sqrt a - sqrt b)/((sqrt a)^2-(sqrt b)^2) by Th70
                 .= (sqrt a - sqrt b)/(a-(sqrt b)^2) by A1,Def4
                 .= (sqrt a - sqrt b)/(a-b) by A2,Def4;
 end;

theorem
    0 <= b & b < a implies 1/(sqrt a+sqrt b) = (sqrt a - sqrt b)/(a-b)
 proof assume A1: 0 <= b & b < a;
  then (sqrt a)^2-(sqrt b)^2 <> 0 by Lm6;
  hence 1/(sqrt a+sqrt b) = (sqrt a - sqrt b)/((sqrt a)^2-(sqrt b)^2) by Th70
                 .= (sqrt a - sqrt b)/(a-(sqrt b)^2) by A1,Def4
                 .= (sqrt a - sqrt b)/(a-b) by A1,Def4;
 end;

theorem
   0 <= a & 0 <= b implies
    1/(sqrt a-sqrt b) = (sqrt a + sqrt b)/(a-b)
 proof assume
A1: 0 <= a & 0 <= b;
  per cases;
  suppose a <> b;
  then (sqrt a)^2-(sqrt b)^2 <> 0 by A1,Lm6;
  hence 1/(sqrt a-sqrt b) = (sqrt a + sqrt b)/((sqrt a)^2-(sqrt b)^2) by Th71
                 .= (sqrt a + sqrt b)/(a-(sqrt b)^2) by A1,Def4
                 .= (sqrt a + sqrt b)/(a-b) by A1,Def4;
  end;
  suppose
 a = b;
  then 1/(sqrt a-sqrt b) = 0 & (sqrt a + sqrt b)/(a-b) = 0 by XCMPLX_1:49;
  hence thesis;
  end;
 end;

theorem
   0 <= b & b < a implies 1/(sqrt a-sqrt b) = (sqrt a + sqrt b)/(a-b)
 proof assume A1: 0 <= b & b < a;
  then (sqrt a)^2-(sqrt b)^2 <> 0 by Lm6;
  hence 1/(sqrt a-sqrt b) = (sqrt a + sqrt b)/((sqrt a)^2-(sqrt b)^2) by Th71
                 .= (sqrt a + sqrt b)/(a-(sqrt b)^2) by A1,Def4
                 .= (sqrt a + sqrt b)/(a-b) by A1,Def4;
 end;

theorem
   for x,y being complex number st x^2=y^2 holds x=y or x=-y
 proof
   let x,y be complex number;
   assume x^2=y^2;
   then (x-y)*(x+y)=0;
   then x-y=0 or x+y=0 by XCMPLX_1:6;
  hence x=y or x=-y;
 end;

theorem
   for x being complex number st x^2=1 holds x=1 or x=-1
proof
  let x be complex number;
  assume x^2=1;
  then (x-1)*(x+1)=0;
  then x-1=0 or x+1=0 by XCMPLX_1:6;
  hence x=1 or x=-1;
end;

theorem
   0<=x & x<=1 implies x^2<=x
  proof assume A1:0<=x & x<=1;
   per cases by A1;
   suppose 0=x; hence x^2<=x;
   end;
   suppose A2:0<x;
   per cases by A1,REAL_1:def 5;
   suppose x=1; hence x^2<=x;
     end;
   suppose x<1; hence x^2<=x by A2,Th75;
     end;
 end;
 end;

theorem
   x^2-1<=0 implies -1<=x & x<=1
 proof assume x^2-1<=0;
  then 1>=0 & (x-1)*(x+1)<=0;
  hence thesis by XREAL_1:95;
 end;

theorem
   x < y & x < z iff x < min(y,z) by XXREAL_0:21,23;

begin :: Addenda

:: from JGRAPH_5, 2006.08.13, A.T.

theorem Th114:
 a <= 0 & x < a implies x^2 > a^2
proof assume A1: a<=0 & x<a;
  then --a<=0;
  then A2: -a>=0 by XREAL_1:60;
       -x>-a by A1,XREAL_1:26;
  then (-x)^2>(-a)^2 by A2,Th78;
 hence thesis;
end;

:: from JGRAPH_2, 2006.12.29, AK

theorem
   -1 >= a implies -a <= a^2
proof assume -1 >= a;
  then --1<=-a by XREAL_1:26;
  then -a<= (-a)^2 by XREAL_1:153;
 hence -a <= a^2;
end;

theorem
   -1 > a implies -a < a^2
proof assume -1 > a;
  then --1< -a by XREAL_1:26;
  then -a< (-a)^2 by Th76;
 hence -a < a^2;
end;

theorem
   b^2 <= a^2 & a >= 0 implies -a <= b & b <= a
proof
assume A1:b^2<= a^2 & a>=0;
       now assume A2:-a>b or b>a;
         now per cases by A2;
    case -a>b; then --a<-b by XREAL_1:26;
      then a^2<(-b)^2 by A1,Th78;
     hence contradiction by A1;
    end;
    case b>a;
     hence contradiction by A1,Th78;
    end;
    end;
   hence contradiction;
  end;
hence -a<=b & b<=a;
end;

theorem
   b^2 < a^2 & a >= 0 implies -a < b & b < a
proof
assume A1:b^2< a^2 & a>=0;
       now assume A2:-a>=b or b>=a;
         now per cases by A2;
    case -a>=b; then --a<= -b by XREAL_1:26;
      then a^2<=(-b)^2 by A1,Th77;
     hence contradiction by A1;
    end;
    case b>=a;
     hence contradiction by A1,Th77;
    end;
    end;
   hence contradiction;
  end;
hence -a<b & b<a;
end;

theorem Th119:
  -a <= b & b <= a implies b^2 <= a^2
proof
assume A1:
  -a<=b & b<=a;
  per cases;
  suppose b>=0;
   hence b^2<= a^2 by A1,Th77;
  end;
  suppose b<0; then A2: -b>0 by XREAL_1:60;
         --a>=-b by A1,XREAL_1:26;
    then (-b)^2<= a^2 by A2,Th77;
   hence b^2<= a^2;
end;
end;

theorem
   -a < b & b < a implies b^2 < a^2
proof assume A1: -a<b & b<a;
  per cases;
  suppose b>=0;
   hence b^2< a^2 by A1,Th78;
  end;
  suppose b<0; then A2: -b>0 by XREAL_1:60;
         --a>-b by A1,XREAL_1:26;
    then (-b)^2< a^2 by A2,Th78;
   hence b^2< a^2;
end;
end;

:: from JGRAPH_4, 2006.12.29, AK

theorem
   a^2 <= 1 implies -1 <= a & a <= 1
proof assume a^2<=1;
  then a^2-1^2<=1^2-1^2 by XREAL_1:11;
  then (a-1)*(a+1)<=0;
 hence -1<=a & a<=1 by XREAL_1:95;
end;

theorem
   a^2 < 1 implies -1 < a & a < 1
proof assume a^2<1;
  then a^2-1^2<1^2-1^2 by XREAL_1:11;
  then (a-1)*(a+1)<0;
 hence -1<a & a<1 by XREAL_1:96;
end;

:: from JGRAPH_6, 2006.12.29, AK

theorem Th123:
  -1 <= a & a <= 1 & -1 <= b & b <= 1 implies a^2*b^2 <= 1
proof
 assume A1: -1<=a & a<=1 & -1<=b & b<=1;
  then A2: a^2<=1 by Th59,Th119;
  A3: b^2<=1 by A1,Th59,Th119;
        0<=b^2 by XREAL_1:65;
  then a^2*b^2 <= 1*b^2 by A2,XREAL_1:66;
 hence a^2*b^2<=1 by A3,XXREAL_0:2;
end;

theorem Th124:
  a >= 0 & b >= 0 implies a*sqrt(b) = sqrt(a^2*b)
proof
 assume A1: a>=0 & b>=0;
  then A2: sqrt(a^2)=a by Lm3;
        a^2>=0 by XREAL_1:65;
 hence a*sqrt(b)=sqrt(a^2*b) by A1,A2,Th97;
end;

Lm7: -1<=a & a<=1 & -1<=b & b<=1 implies (1+a^2)*b^2<=1+b^2
proof
  assume -1<=a & a<=1 & -1<=b & b<=1;
  then a^2*b^2<=1 by Th123;
  then 1*b^2+a^2*b^2<=1+b^2 by XREAL_1:9;
 hence (1+a^2)*b^2<=1+b^2;
end;

theorem Th125:
  -1 <= a & a <= 1 & -1 <= b & b <= 1 implies
  (-b)*sqrt(1+a^2) <= sqrt(1+b^2) & -sqrt(1+b^2) <= b*sqrt(1+a^2)
proof
  assume A1: -1<=a & a<=1 & -1<=b & b<=1;
        a^2>=0 by XREAL_1:65;
  then A2: 1+a^2>=1+0 by XREAL_1:9;
  then A3: sqrt(1+a^2)>=0 by Def4;
        b^2>=0 by XREAL_1:65;
  then 1+b^2>=1+0 by XREAL_1:9;
  then A4: sqrt(1+b^2)>=0 by Def4;
  A5: now per cases;
  case b>=0;
    then -b<=0 by XREAL_1:60;
   hence (-b)*sqrt(1+a^2)<=sqrt(1+b^2) by A3,A4,XREAL_1:133;
  end;
  case b<0; then -b>0 by XREAL_1:60;
    then A6: (-b)*sqrt(1+a^2)=sqrt((-b)^2*(1+a^2)) by A2,Th124;
         A7: (1+a^2)*b^2<=1+b^2 by A1,Lm7;
          (-b)^2>=0 by XREAL_1:65;
    then (-b)^2*(1+a^2)>=0 by A2,XREAL_1:129;
   hence (-b)*sqrt(1+a^2)<=sqrt(1+b^2) by A6,A7,Th94;
  end;
  end;
  then -((-b)*sqrt(1+a^2)) >= - sqrt(1+b^2) by XREAL_1:26;
 hence (-b)*sqrt(1+a^2)<=sqrt(1+b^2) & -sqrt(1+b^2)<= b*sqrt(1+a^2) by A5;
end;

theorem
   -1 <= a & a <= 1 & -1 <= b & b <= 1
  implies b*sqrt(1+a^2) <= sqrt(1+b^2)
proof
  assume A1: -1<=a & a<=1 & -1<=b & b<=1;
   then A2: --1>=-b by XREAL_1:26;
         -1<=-b by A1,XREAL_1:26;
   then (--b)*sqrt(1+a^2)<=sqrt(1+(-b)^2) by A1,A2,Th125;
 hence b*sqrt(1+a^2)<=sqrt(1+b^2);
end;

Lm8: b<=0 & a<=b implies a*sqrt(1+b^2)<= b*sqrt(1+a^2)
proof
 assume A1: b<=0 & a<=b;
        a^2>=0 by XREAL_1:65;
  then A2: 1+a^2>=1+0 by XREAL_1:9;
        b^2>=0 by XREAL_1:65;
  then A3: 1+b^2>=1+0 by XREAL_1:9;
        --b<=0 by A1;
  then -b>=0 by XREAL_1:60;
  then A4: (-b)*sqrt(1+a^2)=sqrt((-b)^2*(1+a^2)) by A2,Th124;
        --a<=0 by A1;
  then -a>=0 by XREAL_1:60;
  then A5: (-a)*sqrt(1+b^2)=sqrt((-a)^2*(1+b^2)) by A3,Th124;
        a<b or a=b by A1,REAL_1:def 5;
  then b^2<a^2 or a=b by A1,Th114;
  then A6: b^2*1+b^2*a^2<=a^2*1+a^2*b^2 by XREAL_1:9;
        (b)^2>=0 by XREAL_1:65;
  then (b)^2*(1+a^2)>=0 by A2,XREAL_1:129;
  then -(a*sqrt(1+b^2))>= -(b*sqrt(1+a^2)) by A4,A5,A6,Th94;
   hence (a)*sqrt(1+b^2)<=b*sqrt(1+a^2) by XREAL_1:26;
end;

Lm9: for a,b being real number st a<=0 & a<=b
holds a*sqrt(1+b^2)<= b*sqrt(1+a^2)
proof let a,b be real number;
 assume A1: a<=0 & a<=b;
       now per cases;
 case b<=0;
  hence b*sqrt(1+a^2)>= a*sqrt(1+b^2) by A1,Lm8;
 end;
 case A2: b>0;
      A3: (b)^2 >=0 by XREAL_1:65;
            1+(b)^2 >(b)^2 by XREAL_1:31;
      then sqrt(1+(b)^2)>0 by A3,Th93;
      then A4: a*sqrt(1+b^2)<=0 by A1,XREAL_1:133;
      A5: (a)^2 >=0 by XREAL_1:65;
            1+(a)^2 >(a)^2 by XREAL_1:31;
      then sqrt(1+(a)^2)>0 by A5,Th93;
  hence b*sqrt(1+a^2)>= a*sqrt(1+b^2) by A2,A4,XREAL_1:129;
 end;
 end;
 hence thesis;
end;

Lm10: for a,b being real number st a>=0 & a>=b
holds a*sqrt(1+b^2)>= b*sqrt(1+a^2)
proof let a,b be real number;
 assume A1: a>=0 & a>=b;
  then A2: -a<=0 by XREAL_1:60;
        -a <= -b by A1,XREAL_1:26;
  then (-a)*sqrt(1+(-b)^2)<= (-b)*sqrt(1+(-a)^2) by A2,Lm9;
  then -(a*sqrt(1+(b)^2))<= -(b*sqrt(1+(a)^2));
  hence a*sqrt(1+b^2)>= b*sqrt(1+a^2) by XREAL_1:26;
end;

theorem
   a >= b implies a*sqrt(1+b^2) >= b*sqrt(1+a^2)
proof
 assume A1: a>=b;
  per cases;
  suppose a>=0;
   hence thesis by A1,Lm10;
  end;
  suppose a<0;
   hence thesis by A1,Lm8;
end;
end;

