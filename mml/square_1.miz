:: Some Properties of Real Numbers.
:: Operations: min, max, square, and square root
::  by Andrzej Trybulec and Czes{\l}aw Byli\'nski
::
:: Received November 16, 1989
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies ARYTM, ARYTM_3, RELAT_1, ARYTM_1, SQUARE_1, XCMPLX_0, COMPLEX1;
 notations TARSKI, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0;
 constructors REAL_1, XCMPLX_0, XREAL_0, XBOOLE_0;
 registrations XREAL_0, REAL_1, NUMBERS, ARYTM_3, ZFMISC_1, XBOOLE_0, XCMPLX_0;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, REAL_1, XREAL_0;
 theorems AXIOMS, REAL_1, XREAL_0, XCMPLX_0, XCMPLX_1, XREAL_1;

begin

registration
  cluster -> complex Element of COMPLEX;
  coherence by XCMPLX_0:def 2;
end;

 reserve a,b,c,x,y,z for real number;

canceled;

theorem
  1 < x implies 1/x < 1
 proof
   assume
A1: 1<x;
then A2: 0<x;
   then 0<x" by XREAL_1:124;
   then 1*x"<x*x" by A1,XREAL_1:70;
   then 1"*(1*x")<1"*1 by A2,XCMPLX_0:def 7;
   hence 1/x < 1 by XCMPLX_0:def 9;
 end;

 Lm1:
  x < y implies 0 < y - x by XREAL_1:52;

 Lm2:
  x <= y implies 0 <= y - x by XREAL_1:50;

 Lm3:
  0 <= x & 0 <= y implies 0 <= x*y by XREAL_1:129;

 Lm4:
  0 < x & 0 < y implies 0 < x*y by XREAL_1:131;

 Lm5:
  0 < x & y < 0 implies x*y < 0 by XREAL_1:134;

canceled 22;

theorem
   0 <= x*y implies 0 <= x & 0 <= y or x <= 0 & y <= 0 by Lm5;

 Lm6:
  0 <= a & 0 <= b implies 0 <= a/b by XREAL_1:138;

 Lm7:
  0 < x implies y - x < y by XREAL_1:46;

scheme RealContinuity { P[set], Q[set] } :
 ex z st for x,y st P[x] & Q[y] holds x <= z & z <= y
provided
A1: for x,y st P[x] & Q[y] holds x <= y
proof
 set X = { z where z is Element of REAL : P[z] };
 set Y = { z where z is Element of REAL : Q[z] };
A2: X c= REAL
   proof let x be set; assume x in X;
    then ex z being Element of REAL st z = x & P[z];
    hence thesis;
   end;
     Y c= REAL
   proof let x be set; assume x in Y;
   then ex z being Element of REAL st z = x & Q[z];
    hence thesis;
   end;
 then reconsider X, Y as Subset of REAL by A2;
     for x,y being real number st x in X & y in Y holds x <= y
   proof let x,y be real number;
    assume x in X & y in Y;
    then (ex z being Element of REAL st z = x & P[z]) &
    ex z being Element of REAL st z = y & Q[z];
    hence x <= y by A1;
   end;
  then consider z being real number such that
A3: for x,y being real number st x in X & y in Y holds x <= z & z <= y
  by AXIOMS:26;
 take z; let x,y;
 assume
A4: P[x] & Q[y];
     x is Element of REAL & y is Element of REAL by XREAL_0:def 1;
 then x in X & y in Y by A4;
 hence x <= z & z <= y by A3;
end;

definition let x,y;
 func min(x,y) -> real number equals
:Def1: x if x <= y otherwise y;
 correctness;
 commutativity by XREAL_1:1;
 idempotence;
 func max(x,y) -> real number equals
:Def2: x if y <= x otherwise y;
 correctness;
 commutativity by XREAL_1:1;
 idempotence;
end;

definition let x,y be Element of REAL;
 redefine func min(x,y) -> Element of REAL;
 coherence by XREAL_0:def 1;
 redefine func max(x,y) -> Element of REAL;
 coherence by XREAL_0:def 1;
end;

canceled 9;

theorem Th35:
  min(x,y) <= x
 proof x <= y or not x <= y;
  hence min(x,y) <= x by Def1;
 end;

canceled 2;

theorem Th38:
  min(x,y) = x or min(x,y) = y
 proof assume min(x,y) <> x; then not x <= y by Def1;
  hence min(x,y) = y by Def1; end;

theorem Th39:
  x <= y & x <= z iff x <= min(y,z)
 proof
  thus x <= y & x <= z implies x <= min(y,z) by Th38;
      min(y,z) <= y & min(y,z) <= z by Th35;
  hence thesis by XREAL_1:2;
 end;

theorem
   min(x,min(y,z)) = min(min(x,y),z)
 proof
  per cases by XREAL_1:2;
   suppose x <= y & x <= z;
    then min(x,y) = x & min(x,z) = x & (min(y,z) = y or min(y,z) = z) by Def1,
Th38;
    hence thesis;
   end;
   suppose y <= x & y <= z; then min(x,y) = y & min(y,z) = y by Def1;
    hence thesis;
   end;
   suppose z <= y & z <= x;
    then min(y,z) = z & min(x,z) = z & (min(x,y) = x or min(x,y) = y) by Def1,
Th38;
    hence thesis;
 end;
 end;

canceled 5;

theorem Th46:
  x <= max(x,y)
 proof y <= x or not y <= x;
  hence x <= max(x,y) by Def2;
 end;

canceled 2;

theorem Th49:
  max(x,y) = x or max(x,y) = y
 proof assume max(x,y) <> x; then not y <= x by Def2;
  hence max(x,y) = y by Def2; end;

theorem
   y <= x & z <= x iff max(y,z) <= x
 proof
  thus y <= x & z <= x implies max(y,z) <= x by Th49;
   y <= max(y,z) & z <= max(y,z) by Th46;
  hence max(y,z) <= x implies y <= x & z <= x by XREAL_1:2;
 end;

theorem
   max(x,max(y,z)) = max(max(x,y),z)
 proof
  per cases by XREAL_1:2;
  suppose x <= y & x <= z;
    then max(x,y) = y & max(x,z) = z & (max(y,z) = z or max(y,z) = y) by Def2,
Th49;
    hence thesis;
  end;
  suppose y <= x & y <= z; then max(x,y) = x & max(y,z) = z by Def2;
    hence thesis;
  end;
  suppose z <= y & z <= x;
    then max(y,z) = y & max(x,z) = x & (max(x,y) = y or max(x,y) = x) by Def2,
Th49;
    hence thesis;
 end;
 end;

canceled;

theorem
   min(x,y) + max(x,y) = x + y
 proof
   per cases;
   suppose x <= y; then
     min(x,y) = x & max (x,y) = y by Def1,Def2;
     hence min(x,y) + max(x,y) = x + y;
   end;
   suppose x > y; then
     min(x,y) = y & max (x,y) = x by Def1,Def2;
     hence min(x,y) + max(x,y) = x + y;
   end;
 end;

theorem Th54:
  max(x,min(x,y)) = x
 proof
  per cases;
    suppose x <= y;
     hence max(x,min(x,y)) = max (x,x) by Def1 .= x;
    end;
    suppose
A1:   y <= x;
     hence max(x,min(x,y)) = max (x,y) by Def1 .= x by A1,Def2;
 end;
 end;

theorem Th55:
  min(x,max(x,y)) = x
 proof
  per cases;
    suppose
A1:   x <= y;
     hence min(x,max(x,y)) = min(x,y) by Def2 .= x by A1,Def1;
    end;
    suppose y <= x;
     hence min(x,max(x,y)) = min(x,x) by Def2 .= x;
 end;
 end;

theorem
   min(x,max(y,z)) = max(min(x,y),min(x,z))
 proof
   per cases by XREAL_1:2;
   suppose
A1:   x <= y & x <= z;
        y <= max(y,z) by Th46;
     then x <= max(y,z) by A1,XREAL_1:2;
    hence min(x,max(y,z)) = x by Def1
               .= max(min(x,y),x) by Th54
               .= max(min(x,y),min(x,z)) by A1,Def1;
   end;
   suppose
A2:   y <= x & y <= z;
then A3:   y <= min(x,z) by Th39;
    thus min(x,max(y,z)) = min(x,z) by A2,Def2
               .= max(y,min(x,z)) by A3,Def2
               .= max(min(x,y),min(x,z)) by A2,Def1;
   end;
   suppose
A4:   z <= y & z <= x;
then A5:   z <= min(x,y) by Th39;
    thus min(x,max(y,z)) = min(x,y) by A4,Def2
               .= max(min(x,y),z) by A5,Def2
               .= max(min(x,y),min(x,z)) by A4,Def1;
 end;
 end;

theorem
   max(x,min(y,z)) = min(max(x,y),max(x,z))
 proof
  per cases by XREAL_1:2;
  suppose
A1:   x <= y & x <= z;
     then x <= min(y,z) by Th39;
    hence max(x,min(y,z)) = min(y,z) by Def2
               .= min(max(x,y),z) by A1,Def2
               .= min(max(x,y),max(x,z)) by A1,Def2;
  end;
  suppose
A2:   y <= x & y <= z;
    hence max(x,min(y,z)) = max(x,y) by Def1
               .= x by A2,Def2
               .= min(x,max(x,z)) by Th55
               .= min(max(x,y),max(x,z)) by A2,Def2;
  end;
  suppose
A3:   z <= y & z <= x;
    hence max(x,min(y,z)) = max(x,z) by Def1
               .= x by A3,Def2
               .= min(max(x,y),x) by Th55
               .= min(max(x,y),max(x,z)) by A3,Def2;
 end;
 end;

definition let x be complex number;
  func x^2 equals
 x * x;
  correctness;
end;

registration let x be complex number;
  cluster x^2 -> complex;
 correctness;
end;

registration let x;
  cluster x^2 -> real;
 correctness;
end;

definition let x be Element of COMPLEX;
  redefine func x^2 -> Element of COMPLEX;
  correctness by XCMPLX_0:def 2;
end;

definition let x be Element of REAL;
  redefine func x^2 -> Element of REAL;
  correctness by XREAL_0:def 1;
end;

canceled;

theorem
Th59: 1^2 = 1;

theorem
Th60: 0^2 = 0;

theorem
  for a being complex number holds a^2 = (-a)^2;

canceled;

theorem
  for a, b being complex number holds
  (a + b)^2 = a^2 + 2*a*b + b^2;

theorem
  for a, b being complex number holds
  (a - b)^2 = a^2 - 2*a*b + b^2;

theorem
  for a being complex number holds
  (a + 1)^2 = a^2 + 2*a + 1;

theorem
   for a being complex number holds
  (a - 1)^2 = a^2 - 2*a + 1;

theorem
  for a, b being complex number holds
  (a - b)*(a + b) = a^2 - b^2;

theorem
  for a, b being complex number holds
  (a*b)^2 = a^2*b^2;

theorem Th69:
  for a, b being complex number holds
  (a/b)^2 = a^2/b^2 by XCMPLX_1:77;

theorem Th70:
  for a, b being complex number holds
  a^2 - b^2 <> 0 implies 1/(a+b) = (a-b)/(a^2-b^2)
 proof
  let a, b be complex number;
  assume a^2-b^2 <> 0;
  then (a+b)*(a-b) <> 0;
  then (a+b) <> 0 & (a-b) <> 0;
  hence 1/(a+b) = (1*(a-b))/((a+b)*(a-b)) by XCMPLX_1:92
               .= (a-b)/(a^2-b^2);
 end;

theorem Th71:
  for a, b being complex number holds
  a^2-b^2 <> 0 implies 1/(a-b) = (a+b)/(a^2-b^2)
 proof
  let a, b be complex number;
  assume a^2-b^2 <> 0;
  then (a+b)*(a-b) <> 0;
  then (a+b) <> 0 & (a-b) <> 0;
  hence 1/(a-b) = (1*(a+b))/((a-b)*(a+b)) by XCMPLX_1:92
               .= (a+b)/(a^2-b^2);
 end;

theorem Th72:
  0 <= a^2 by XREAL_1:65;

theorem Th73:
  for a being complex number st a^2 = 0 holds a = 0 by XCMPLX_1:6;

theorem
   0 <> a implies 0 < a^2
 proof assume 0 <> a; hence 0 <= a^2 & a^2 <> 0 by Th72,Th73; end;

theorem Th75:
  0 < a & a < 1 implies a^2 < a
 proof assume 0 < a & a < 1;
   then a*a < a*1 by XREAL_1:70;
  hence a^2 < a;
 end;

theorem
   1 < a implies a < a^2
 proof assume
A1:    1 < a;
   a*1 < a*a by A1,XREAL_1:70;
  hence a < a^2;
 end;

Lm8:
  0 < a implies ex x st 0 < x & x^2 < a
 proof assume
A1: 0 < a;
     per cases;
    suppose
A2:   1 <= a;
     reconsider x = 2" as real number;
     take x;
     thus 0 < x;
     thus x^2 < a by A2,XREAL_1:2;
    end;
    suppose
A3:   a < 1;
     take x = a;
     thus 0 < x by A1;
     thus x^2 < a by A1,A3,Th75;
   end;
 end;

theorem Th77:
  0 <= x & x <= y implies x^2 <= y^2
 proof assume
  0 <= x & x <= y;
   then x*x <= x*y & x*y <= y*y by XREAL_1:66;
   then x*x <= y*y by XREAL_1:2;
   then x^2 <= y*y;
  hence x^2 <= y^2;
 end;

theorem Th78:
  0 <= x & x < y implies x^2 < y^2
 proof assume 0 <= x & x < y;
   then x*x <= x*y & x*y < y*y by XREAL_1:66,XREAL_1:70;
   then x*x < y*y by XREAL_1:2;
   then x^2 < y*y;
  hence x^2 < y^2;
 end;

Lm9:
  0 <= x & 0 <= y & x^2 = y^2 implies x = y
proof assume
A1: 0 <= x & 0 <= y;
  assume x^2 = y ^2;
  then x <= y & y <= x by A1,Th78;
  hence x = y by XREAL_1:1;
end;

definition let a;
 assume A1: 0 <= a;
  func sqrt a -> real number means
:Def4: 0 <= it & it^2 = a;
 existence
  proof
A2: a <= a + 1 by XREAL_1:31;
A3: (a + 1)^2 = a^2 + 2*a + 1
            .= a^2 + a + (a + 1);
       0 <= a^2 by Th72;
    then 0 + 0 <= a^2 + a by A1,XREAL_1:9;
    then A4:  0 + a <= a^2 + a + (a + 1) by A2,XREAL_1:9;
   defpred X[real number] means $1 <= 0 or $1^2 <= a;
   defpred Y[real number] means 0 <= $1 & a <= $1^2;
A5: now let x,y such that
A6:  X[x] and
A7:  Y[y];
       per cases;
      suppose x <= 0; hence x <= y by A7;
      end;
      suppose not x <= 0;
       then x^2 <= y^2 by A6,A7,XREAL_1:2;
      hence x <= y by A7,Th78;
     end;
   end;
   consider z such that
A8:  for x,y st X[x] & Y[y] holds x <= z & z <= y from RealContinuity(A5);
   take z;
   thus 0 <= z by A1,A2,A3,A4,A8;
   assume
A9:  z^2 <> a;
       now per cases by A9,XREAL_1:1;
     suppose A10: z <= 0;
then z = 0 by A1,A2,A3,A4,A8; then z^2 = 0*0 .= 0;
       then consider c such that
A11:     0 < c and
A12:     c^2 < a by A1,A9,Lm8;
      thus contradiction by A1,A2,A3,A4,A8,A10,A11,A12;
     end;
     suppose
A13:   z^2 < a & not z <= 0;
      set b = a - z^2;
A14:   0/2 = 0;
A15:  0 < b by A13,Lm1;
      then 0 < b/2 by A14,REAL_1:73;
      then consider c such that
A16:    0 < c and
A17:    c^2 < b/2 by Lm8;
       A18:    2*z <> 0 by A13;
       A19:    0 <= 2*z by A13,Lm3;
       set eps = min(c, b/(4*z));
A20:    0/(4*z) = 0;
          0 < 4*z by A13,Lm4;
       then 0 < b/(4*z) by A15,A20,REAL_1:73;
then A21:    0 < eps by A16,Th38;
          eps <= b/(2*2*z) by Th35;
       then eps*(2*z) <= b/(2*(2*z))*(2*z) by A19,XREAL_1:66;
       then eps*(2*z) <= b/2/(2*z)*(2*z) by XCMPLX_1:79;
       then A22:    2*z*eps <= b/2 by A18,XCMPLX_1:88;
          eps <= c by Th35;
       then eps^2 <= c^2 by A21,Th77;
       then A23:    eps^2 <= b/2 by A17,XREAL_1:2;
A24:    a = z^2 + b;
A25:    (z + eps)^2 = z^2 + 2*z*eps + eps^2
        .= z^2 + (2*z*eps + eps^2);
          2*z*eps + eps^2 <= b/2 + b/2 by A22,A23,XREAL_1:9;
then A26:    (z + eps)^2 <= a by A24,A25,XREAL_1:8;
       z < z + eps by A21,XREAL_1:31;
      hence contradiction by A1,A2,A3,A4,A8,A26;
     end;
     suppose
A27:   a < z^2 & not z <= 0;
      set b = z^2 - a;
A28:   0 < 2*z by A27,Lm4;
A29:   2*z <> 0 by A27;
A30:   0 < b by A27,Lm1;
      set eps = min(b/(2*z),z);
A31:   0/(2*z) = 0;
         0 < 2*z by A27,Lm4;
      then 0 < b/(2*z) by A30,A31,REAL_1:73;
then A32:   0 < eps by A27,Th38;
A33:    a = z^2 - b;
A34:    (z - eps)^2 = z^2 - 2*z*eps + eps^2
        .= z^2 - (2*z*eps - eps^2);
          eps <= b/(2*z) by Th35;
       then eps*(2*z) <= b/(2*z)*(2*z) by A28,XREAL_1:66;
       then A35:    2*z*eps <= b by A29,XCMPLX_1:88;
          0 <= eps^2 by Th72;
       then 2*z*eps - eps^2 <= 2*z*eps - 0 by REAL_1:92;
       then 2*z*eps - eps^2 <= b by A35,XREAL_1:2;
then A36:    a <= (z - eps)^2 by A33,A34,REAL_1:92;
          eps <= z by Th35;
then A37:   0 <= z - eps by Lm2;
       z - eps < z by A32,Lm7;
      hence contradiction by A8,A36,A37;
    end;
    end;
   hence contradiction;
  end;
 uniqueness by Lm9;
end;

definition let a be Element of REAL;
  redefine func sqrt a -> Element of REAL;
  coherence by XREAL_0:def 1;
end;

canceled 3;

theorem
   sqrt 0 = 0 by Def4,Th60;

theorem Th83:
  sqrt 1 = 1 by Def4,Th59;

Lm10:  0 <= a implies sqrt a^2 = a
 proof 0 <= a^2 by Th72; hence thesis by Def4; end;

Lm11:  0 <= x & x < y implies sqrt x < sqrt y
 proof assume that
A1: 0 <= x and
A2: x < y;
A3: 0 <= y by A1,A2;
then A4: 0 <= sqrt x & 0 <= sqrt y by A1,Def4;
      (sqrt x)^2 = x & (sqrt y)^2 = y by A1,A3,Def4;
  hence sqrt x < sqrt y by A2,A4,Th77;
 end;

theorem
   1 < sqrt 2 by Lm11,Th83;
Lm12: 2^2 = 2*2;

theorem Th85:
  sqrt 4 = 2 by Def4,Lm12;

theorem
  sqrt 2 < 2 by Lm11,Th85;

canceled 2;

theorem  0 <= a implies sqrt a^2 = a by Lm10;

theorem
  a <= 0 implies sqrt a^2 = -a
 proof assume a <= 0;
  then -0 <= -a & a^2 = (-a)^2 by XREAL_1:26;
  hence sqrt a^2 = -a by Lm10;
 end;

canceled;

theorem
   0 <= a & sqrt a = 0 implies a = 0 by Def4,Th60;

theorem
   0 < a implies 0 < sqrt a
 proof assume 0 < a;
  then sqrt a <> 0 & 0 <= sqrt a by Def4,Th60;
  hence thesis;
 end;

theorem
  0 <= x & x <= y implies sqrt x <= sqrt y
  proof
     per cases;
     suppose x = y; hence thesis;
     end;
     suppose
A1:     x <> y;
      assume
A2:     0 <= x;
      assume x <= y; then x < y by A1,REAL_1:def 5;
      hence sqrt x <= sqrt y by A2,Lm11;
    end;
  end;

theorem
   0 <= x & x < y implies sqrt x < sqrt y by Lm11;

theorem Th96:
  0 <= x & 0 <= y & sqrt x = sqrt y implies x = y
 proof assume that
A1: 0 <= x & 0 <= y and
A2: sqrt x = sqrt y;
  assume x <> y; then x < y or y < x by XREAL_1:1;
  hence contradiction by A1,A2,Lm11;
 end;

theorem
  0 <= a & 0 <= b implies sqrt (a*b) = sqrt a * sqrt b
 proof assume
A1:  0 <= a & 0 <= b;
then A2: 0 <= a*b by Lm3;
then A3:  (sqrt(a*b))^2 = a*b by Def4 .= (sqrt a)^2*b by A1,Def4
                 .= (sqrt a)^2*(sqrt b)^2 by A1,Def4
         .= (sqrt a*sqrt b)^2;
      0 <= sqrt a & 0 <= sqrt b by A1,Def4;
then A4: 0 <= sqrt(a*b) & 0 <= sqrt a*sqrt b by A2,Def4,Lm3;
  thus sqrt(a*b) = sqrt(sqrt a*sqrt b)^2 by A2,A3,Def4
     .= sqrt a*sqrt b by A4,Lm10;
 end;

canceled;

theorem Th99:
  0 <= a & 0 <= b implies sqrt (a/b) = sqrt a/sqrt b
 proof assume
A1: 0 <= a & 0 <= b;
then A2: 0 <= sqrt a & 0 <= sqrt b by Def4;
     (sqrt a/sqrt b)^2 = (sqrt a)^2/(sqrt b)^2 &
   (sqrt a)^2 = a & (sqrt b)^2 = b by A1,Def4,Th69;
  then 0 <= a/b & 0 <= sqrt a/sqrt b & (sqrt a/sqrt b)^2 = a/b by A1,A2,Lm6;
  hence thesis by Def4;
 end;

canceled;

theorem
  0 < a implies sqrt (1/a) = 1/sqrt a by Th83,Th99;

theorem
  0 < a implies sqrt a/a = 1/sqrt a
proof assume A1: 0 < a;
  then sqrt a <> 0 by Def4,Th60;
  hence sqrt a/a = (sqrt a*sqrt a)/(a*sqrt a) by XCMPLX_1:92
            .= (sqrt a)^2/(a*sqrt a)
            .= (1*a)/(sqrt a*a) by A1,Def4
            .= 1/sqrt a by A1,XCMPLX_1:92;
 end;

theorem
   0 < a implies a / sqrt a = sqrt a
 proof assume A1: 0 < a;
   then sqrt a <> 0 by Def4,Th60;
   hence a /sqrt a = (a*sqrt a) /(sqrt a*sqrt a) by XCMPLX_1:92
              .= (a*sqrt a) /(sqrt a)^2
              .= (sqrt a*a) /(1*a) by A1,Def4
              .= sqrt a/1 by A1,XCMPLX_1:92
              .= sqrt a;
 end;

theorem
   0 <= a & 0 <= b implies (sqrt a - sqrt b)*(sqrt a + sqrt b) = a - b
 proof assume
A1: 0 <= a & 0 <= b;
  thus (sqrt a - sqrt b)*(sqrt a + sqrt b) = (sqrt a)^2 - (sqrt b)^2
                          .= a - (sqrt b)^2 by A1,Def4
                          .= a - b by A1,Def4;
 end;

Lm13:
  0 <= a & 0 <= b & a <> b implies (sqrt a)^2-(sqrt b)^2 <> 0
 proof assume 0 <= a & 0 <= b & a <> b;
  then sqrt a <> sqrt b & 0 <= sqrt a & 0 <= sqrt b by Def4,Th96;
  then (sqrt a)^2 <>(sqrt b)^2 by Lm9;
  hence (sqrt a)^2-(sqrt b)^2 <> 0;
 end;

theorem
   0 <= a & 0 <= b & a <>b implies 1/(sqrt a+sqrt b) = (sqrt a - sqrt b)/(a-b)
 proof assume that
A1:   0 <= a and
A2: 0 <= b and
A3: a <>b;
     (sqrt a)^2-(sqrt b)^2 <> 0 by A1,A2,A3,Lm13;
  hence 1/(sqrt a+sqrt b) = (sqrt a - sqrt b)/((sqrt a)^2-(sqrt b)^2) by Th70
                 .= (sqrt a - sqrt b)/(a-(sqrt b)^2) by A1,Def4
                 .= (sqrt a - sqrt b)/(a-b) by A2,Def4;
 end;

theorem
   0 <= b & b < a implies 1/(sqrt a+sqrt b) = (sqrt a - sqrt b)/(a-b)
 proof assume A1: 0 <= b & b < a;
then A2: 0 < a;
  (sqrt a)^2-(sqrt b)^2 <> 0 by A1,Lm13;
  hence 1/(sqrt a+sqrt b) = (sqrt a - sqrt b)/((sqrt a)^2-(sqrt b)^2) by Th70
                 .= (sqrt a - sqrt b)/(a-(sqrt b)^2) by A2,Def4
                 .= (sqrt a - sqrt b)/(a-b) by A1,Def4;
 end;

theorem
   0 <= a & 0 <= b implies
  1/(sqrt a-sqrt b) = (sqrt a + sqrt b)/(a-b)
 proof assume
A1:   0 <= a & 0 <= b;
  per cases;
  suppose a <> b;
  then (sqrt a)^2-(sqrt b)^2 <> 0 by A1,Lm13;
  hence 1/(sqrt a-sqrt b) = (sqrt a + sqrt b)/((sqrt a)^2-(sqrt b)^2) by Th71
                 .= (sqrt a + sqrt b)/(a-(sqrt b)^2) by A1,Def4
                 .= (sqrt a + sqrt b)/(a-b) by A1,Def4;
  end;
  suppose
A2: a = b; then
  sqrt a - sqrt b = 0 by XCMPLX_1:14;
  then 1/(sqrt a-sqrt b) = 0 & (sqrt a + sqrt b)/(a-b) = 0 by A2,XCMPLX_1:49;
  hence thesis;
  end;
 end;

theorem
   0 <= b & b < a implies 1/(sqrt a-sqrt b) = (sqrt a + sqrt b)/(a-b)
 proof assume A1: 0 <= b & b < a;
then A2: 0 < a;
  (sqrt a)^2-(sqrt b)^2 <> 0 by A1,Lm13;
  hence 1/(sqrt a-sqrt b) = (sqrt a + sqrt b)/((sqrt a)^2-(sqrt b)^2) by Th71
                 .= (sqrt a + sqrt b)/(a-(sqrt b)^2) by A2,Def4
                 .= (sqrt a + sqrt b)/(a-b) by A1,Def4;
 end;
