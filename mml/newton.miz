:: Factorial and Newton coeffitients
::  by Rafa{\l} Kwiatek
::
:: Received July 27, 1990
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies ORDINAL2, ARYTM, FINSEQ_1, ARYTM_3, ARYTM_1, RELAT_1, FUNCT_1,
      BOOLE, FINSEQ_2, GROUP_1, QC_LANG1, RLVECT_1, NEWTON, FINSEQ_4, CARD_3,
      NAT_1, INT_1, ABSVALUE, FILTER_0, NAT_LAT, FINSET_1, CARD_1, TARSKI,
      SQUARE_1, XCMPLX_0, SEQ_1, COMPLEX1;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0,
      CARD_1, REAL_1, FUNCT_1, SEQ_1, FINSET_1, FINSEQ_1, FINSEQ_2, FINSEQ_4,
      INT_1, INT_2, NAT_1, NAT_D, RVSUM_1, SQUARE_1, XXREAL_0;
 constructors PARTFUN1, WELLORD2, REAL_1, SQUARE_1, NAT_1, NAT_D, BINOP_2,
      INT_2, FINSEQ_4, FINSOP_1, RVSUM_1;
 registrations XBOOLE_0, ORDINAL1, RELSET_1, FINSET_1, NUMBERS, XCMPLX_0,
      XXREAL_0, XREAL_0, NAT_1, INT_1, MEMBERED, FINSEQ_1, SEQ_1, FINSEQ_2,
      RVSUM_1;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, XBOOLE_0, FINSEQ_1, SQUARE_1, FINSEQ_2, RVSUM_1;
 theorems TARSKI, REAL_1, NAT_1, RVSUM_1, FINSEQ_1, FINSEQ_2, FINSEQ_3,
      FINSEQ_4, FUNCT_1, INT_1, XREAL_0, XBOOLE_0, XCMPLX_1, REAL_2, INT_2,
      FINSET_1, CARD_2, CARD_1, XBOOLE_1, XREAL_1, XCMPLX_0, XXREAL_0, RELAT_1,
      FINSOP_1, ORDINAL1, SEQ_1, NAT_D;
 schemes NAT_1, FINSEQ_1, SUBSET_1;

begin

 reserve i,k,n,m,l for Element of NAT;
 reserve s,t,r for Nat;
 reserve a,b for real number;
 reserve F,G,H for FinSequence of REAL;

canceled 2;

theorem
Th3: for F,G being FinSequence st
     len F = len G & (for i st i in dom F holds F.i = G.i) holds F = G
  proof
   let F,G be FinSequence;
   assume A1: len F = len G & for i st i in dom F holds F.i = G.i;
   A2: dom F = Seg len F by FINSEQ_1:def 3;
       dom G = Seg len F by A1,FINSEQ_1:def 3;
   hence thesis by A1,A2,FINSEQ_1:17;
  end;

canceled;

theorem
Th5: for n st n>=1 holds Seg n = {1} \/ {k: 1<k & k<n} \/ {n}
  proof
   let n;
   assume A1: n>=1;
   A2: Seg n c= {1} \/ {k: 1<k & k<n} \/ {n}
    proof let d be set;
     assume A3: d in Seg n;
     per cases by A1,REAL_1:def 5;
      suppose A4: n>1;
      reconsider l = d as Element of NAT by A3;
       1 <= l & l <= n by A3,FINSEQ_1:3;
      then 1 = l or (1<l & l<n) or l = n or 1 = n by REAL_1:def 5;
      then d in {1} or d in {k: 1<k & k<n} or d in {n} by A4,TARSKI:def 1;
      then d in {1} \/ {k: 1<k & k<n} or d in {n} by XBOOLE_0:def 2;
      hence thesis by XBOOLE_0:def 2;
      end;
      suppose n = 1;
      hence thesis by A3,FINSEQ_1:4,XBOOLE_0:def 2;
      end;
    end;
     {1} \/ {k: 1<k & k<n} \/ {n} c= Seg n
    proof
       for d be set holds d in {1} \/ {k: 1<k & k<n} \/ {n} implies d in Seg n
      proof
  A5: for d be set holds d in {1} implies d in Seg n
      proof
       let d be set; assume d in {1};
       then d = 1 by TARSKI:def 1;
       hence d in Seg n by A1;
      end;
     A6: for d be set holds d in {k: 1<k & k<n} implies d in Seg n
      proof
       let d be set;
       assume d in {k: 1<k & k<n};
        then consider k such that A7: d = k & 1<k & k<n;
        thus d in Seg n by A7;
      end;
     A8: for d be set holds d in {n} implies d in Seg n
      proof
       let d be set; assume d in {n};
       then d = n by TARSKI:def 1;
       hence d in Seg n by A1;
      end;
       let d be set;
       assume d in ({1} \/ {k: 1<k & k<n} \/ {n});
       then d in ({1} \/ {k: 1<k & k<n}) or d in {n} by XBOOLE_0:def 2;
       then d in {1} or d in {k: 1<k & k<n} or d in {n} by XBOOLE_0:def 2;
       hence thesis by A5,A6,A8;
      end;
     hence thesis by TARSKI:def 3;
    end;
   hence thesis by A2,XBOOLE_0:def 10;
  end;

theorem
Th6: for F holds len (a*F) = len F
 proof
  let F;
  reconsider G = F as Element of (len F)-tuples_on REAL by FINSEQ_2:110;
   len (a*G) = len G by FINSEQ_2:109;
  hence thesis;
 end;

theorem
Th7: n in dom G iff n in dom (a*G)
 proof
    len (a*G) = len G by Th6; hence thesis by FINSEQ_3:31;
 end;

::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
::    x |^ n  Function                                                      ::
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

definition let i be natural number; let x be real number;
  redefine func i |-> x -> FinSequence of REAL;
  coherence
  proof
   let z be set;
   assume z in rng (i |-> x);
   then consider y be set such that A1: y in dom (i |-> x) and
    A2: z = (i |-> x).y by FUNCT_1:def 5;
   reconsider y as Element of NAT by A1;
   thus z in REAL by A2;
  end;
end;

registration let x be complex number, n be natural number;
  cluster n |-> x -> complex-yielding;
  coherence
  proof
     x in COMPLEX by XCMPLX_0:def 2;
    then n |-> x is FinSequence of COMPLEX by FINSEQ_2:77;
    hence thesis;
  end;
end;

definition let x be complex number, n be natural number;
  func x |^ n equals
     Product (n |-> x);
  coherence;
end;

registration let x be real number, n be natural number;
  cluster x |^ n -> real;
  coherence;
end;

definition let x be Real, n be natural number;
  redefine func x |^ n -> Real;
  coherence by XREAL_0:def 1;
end;

reserve z for complex number;

canceled;

theorem Th9:
 z|^0 = 1 by FINSEQ_2:72,RVSUM_1:124;

registration let z be complex number, n be natural number;
 cluster z|^n -> complex;
 coherence;
end;

theorem
Th10: z|^1 = z
 proof
  reconsider z as Element of COMPLEX by XCMPLX_0:def 2;
      z|^1 = Product(<*z*>) by FINSEQ_2:73
    .= z by RVSUM_1:125;
  hence thesis;
 end;

theorem
Th11: z|^(s+1) = z|^s*z
 proof
   defpred P[natural number] means z|^($1+1) = z|^$1*z;
  A1: P[0] proof
     reconsider z as Element of COMPLEX by XCMPLX_0:def 2;
       z|^(0+1) = Product(<*z*>)
       by FINSEQ_2:73 .= 1*z by RVSUM_1:125;
      hence thesis by FINSEQ_2:72,RVSUM_1:124;
   end;
  A2: for s st P[s] holds P[s+1] proof let s;
    assume z|^(s+1) = z|^s*z;
     reconsider z as Element of COMPLEX by XCMPLX_0:def 2;
      s+1 is Element of NAT by ORDINAL1:def 13;
          then z|^(s+1+1) = Product(((s+1) |-> z) ^ <*z*>)
        by FINSEQ_2:74 .= Product((s+1) |-> z)*z by RVSUM_1:126;
       hence thesis;
   end;
    for s holds P[s] from NAT_1:sch 2(A1,A2);
  hence thesis;
 end;

registration let x, n be natural number;
  cluster x|^n -> natural;
coherence
 proof
   defpred P[natural number] means x|^$1 is natural;
A1: P[0] by FINSEQ_2:72,RVSUM_1:124;
A2: for a being natural number st P[a] holds P[a+1]
  proof
    let a be natural number;
    assume P[a];
    then reconsider b = x|^a as natural number;
      x|^(a+1) = b*x by Th11;
    hence thesis;
  end;
     for a being natural number holds P[a]
     from NAT_1:sch 2(A1,A2);
   hence thesis;
 end;
end;

reserve x,y for complex number;

theorem
   (x*y)|^s = x|^s * y|^s
 proof
  reconsider x, y as Element of COMPLEX by XCMPLX_0:def 2;
  reconsider s as Element of NAT by ORDINAL1:def 13;
   x|^s*y|^s = Product(s |-> (x*y)) by RVSUM_1:143;
  hence thesis;
 end;

theorem
   x|^(s+t) = x|^s * x|^t
 proof
  reconsider x as Element of COMPLEX by XCMPLX_0:def 2;
  reconsider s,t as Element of NAT by ORDINAL1:def 13;
   x|^s*x|^t = Product((s+t) |-> x) by RVSUM_1:141;
  hence thesis;
 end;

theorem
   (x|^s)|^t = x|^(s*t)
 proof
  reconsider x as Element of COMPLEX by XCMPLX_0:def 2;
  reconsider s,t as Element of NAT by ORDINAL1:def 13;
   x|^(s*t) = Product(t |-> x|^s) by RVSUM_1:142;
 hence thesis;
 end;

theorem
Th15: 1|^s = 1
 proof
   defpred P[natural number] means 1|^$1 = 1;
  A1: P[0] by FINSEQ_2:72,RVSUM_1:124;
  A2: now
    let s;
    assume P[s];
    then 1|^(s+1) = 1 * 1 by Th11;
    hence P[s+1];
   end;
     for s holds P[s] from NAT_1:sch 2(A1,A2);
   hence thesis;
 end;

theorem Th16:
  s >= 1 implies 0|^s = 0
 proof
  A1: s is Element of NAT by ORDINAL1:def 13;
  defpred P[Element of NAT] means 0|^$1 = 0;
  A2: P[1] by Th10;
  A3: now let n; assume n>=1 & P[n];   0|^(n+1) = 0|^n * 0 by Th11 .= 0;
       hence P[n+1];
      end;
      for n st n >=1 holds P[n] from NAT_1:sch 8(A2,A3);
  hence thesis by A1;
 end;

::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
::     n!  Function                                                         ::
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

definition let n be natural number;
 redefine func idseq n -> FinSequence of REAL;
 coherence
  proof
   let x be set;
   assume x in rng idseq n;
   then consider y be set such that
A1: y in dom idseq n and
A2: x = idseq n.y by FUNCT_1:def 5;
A3: y in Seg n by A1,RELAT_1:71;
   reconsider y as Element of NAT by A1;
    (idseq n).y = y by A3,FUNCT_1:35;
   hence x in REAL by A2;
  end;
end;

definition let n be natural number;
  func n! -> Real equals
     Product idseq n;
  coherence;
end;

registration let n be natural number;
  cluster n! -> real;
  coherence;
end;

definition let n be natural number;
  redefine func n! -> Real;
  coherence;
end;

canceled;

theorem Th18:
  0! = 1 by FINSEQ_2:58,RVSUM_1:124;

theorem
  1! = 1 by FINSEQ_2:59,RVSUM_1:125;

theorem
   2! = 2
 proof
   2! = 1*2 by FINSEQ_2:61,RVSUM_1:129;
  hence thesis;
 end;

theorem Th21:
  for s holds (s+1)! = (s!) * (s+1)
proof
  let s;
  reconsider s as Element of NAT by ORDINAL1:def 13;
  set r1 = s+1;
   (s+1)! = (s!) * (s+1)
  proof
     idseq (s+1) = idseq s ^ <* r1 *> by FINSEQ_2:60;
    hence thesis by RVSUM_1:126;
  end;
  hence thesis;
end;

theorem Th22:
  for s holds s! is Element of NAT
 proof
   defpred P[natural number] means $1! is Element of NAT;
  A1: P[0] by FINSEQ_2:58,RVSUM_1:124;
  A2: now let s; assume P[s]; then reconsider k=s! as Element of NAT;
         (s+1)! = (s+1) * k by Th21;
    hence P[s+1] by ORDINAL1:def 13;
   end;
  thus for s holds P[s] from NAT_1:sch 2(A1,A2);
 end;

theorem
Th23: for s holds s!>0
 proof
   defpred P[natural number] means $1!>0;
  A1: P[0] by FINSEQ_2:58,RVSUM_1:124;
  A2: now let s; assume P[s];
    then (s+1) * (s!)>(s+1) * 0 by XREAL_1:70;
    hence P[s+1] by Th21;
   end;
  thus for s holds P[s] from NAT_1:sch 2(A1,A2);
 end;

canceled;

theorem
Th25: for s,t holds (s!) * (t!)<>0
 proof
  let s,t;
   s!<>0 & t!<>0 by Th23;
  hence thesis by XCMPLX_1:6;
 end;

::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
::     n choose k  Function                                                 ::
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

definition let k,n be natural number;
  func n choose k means :Def3:
    for l be natural number st l = n-k holds
     it = (n!)/((k!) * (l!)) if n >= k
  otherwise it = 0;
 consistency;
 existence
  proof
   thus n >= k implies ex r1 be set st
    for l being natural number st l = n-k holds r1 = (n!)/((k!) * (l!))
    proof
     assume n >= k;
     then reconsider m = n-k as Element of NAT by INT_1:18;
     take (n!)/((k!)*(m!));
     thus thesis;
    end;
   thus thesis;
  end;
  uniqueness
  proof let r1,r2 be set;
   thus n>=k &
    (for l being natural number st l = n-k holds r1 = (n!)/((k!) * (l!))) &
    (for l being natural number st l = n-k holds r2 = (n!)/((k!) * (l!)))
     implies r1 = r2
    proof
     assume A1: n>=k;
     assume A2: (for l being natural number st l = n-k holds
     r1 = (n!)/((k!) * (l!))) &
     (for l being natural number st l = n-k holds r2 = (n!)/((k!) * (l!)));
     reconsider m = n-k as Element of NAT by A1,INT_1:18;
      r1 = (n!)/((k!) * (m!)) & r2 = (n!)/((k!) * (m!)) by A2;
     hence thesis;
    end;
   thus thesis;
  end;
end;

registration let k,n be natural number;
  cluster n choose k -> real;
  coherence
  proof
    per cases;
    suppose
A1: n >= k;
    then reconsider l = n-k as Element of NAT by INT_1:18;
     n choose k = (n!)/((k!) * (l!)) by A1,Def3;
    hence thesis;
    end;
    suppose n < k; hence thesis by Def3;
  end;
  end;
end;

definition let k,n be natural number;
  redefine func n choose k -> Real;
  coherence by XREAL_0:def 1;
end;

canceled;

theorem
Th27: 0 choose 0 = 1
 proof
    0-0 = 0;
   then 0 choose 0 = 1/(1 * 1) by Def3,Th18 .= 1;
   hence thesis;
 end;

canceled;

theorem
Th29: s choose 0 = 1
 proof
  reconsider m = s-0 as Element of NAT by ORDINAL1:def 13;
  A1: s!<>0 by Th23;
      m = s;
  then (s choose 0) = (s!)/(1 * (s!)) by Def3,Th18
   .= 1 by A1,XCMPLX_1:60;
  hence thesis;
 end;

theorem
Th30: for s,t st s>=t holds
       (for r st r = s-t holds s choose t = s choose r)
 proof
  let s,t;
  assume A1: s>=t;
  let r;
  assume A2: r = s-t;
  A3:s>=r
   proof
     t-s>=0-s by XREAL_1:11;
    then --s>=-(t-s) by XREAL_1:26;
    hence thesis by A2;
   end;
    t = s-r by A2;
  then s choose r = (s!)/((r!) * (t!)) by A3,Def3;
  hence thesis by A1,A2,Def3;
 end;

theorem
Th31: for s holds s choose s = 1
 proof
  let s;
  reconsider m = s-s as Element of NAT by INT_1:18;
   m = 0;
  then s choose s = s choose 0 by Th30;
  hence thesis by Th29;
 end;

theorem Th32:
  (t+1) choose (s+1) = (t choose (s+1)) + (t choose s)
 proof
  per cases by REAL_1:def 5;
  suppose A1: s<t;
  thus ((t+1) choose (s+1)) = (t choose (s+1)) + (t choose s)
   proof
    reconsider m1 = t-s as Element of NAT by A1,INT_1:18;
    A2: s+1<=t by A1,NAT_1:13;
    then reconsider m2 = t-(s+1) as Element of NAT by INT_1:18;
    A3: (t choose (s+1)) = (t!)/(((s+1)!) * (m2!)) by A2,Def3;
    A4: ((s+1)!) * (m2!)<>0 & ((s!) * (m1!))<>0 by Th25;
    A5: s!<>0 by Th23;
    A6: m2!<>0 by Th23;
    A7: (t choose (s+1)) + (t choose s) =
     (t!)/(((s+1)!) * (m2!)) + (t!)/((s!) * (m1!)) by A1,A3,Def3 .=
     ((t!)*((s!)*(m1!))+(t!)*(((s+1)!)*(m2!)))/((((s+1)!)*(m2!))*((s!)*(m1!)))
      by A4,XCMPLX_1:117 .=
     (t!*((s!)*(m1!))+(t!)*(s!*(s+1)*(m2!)))/((((s+1)!)*(m2!))*((s!)*(m1!)))
      by Th21 .=
     (s!*((t!)*((m1!)+(s+1)*(m2!))))/(s!*((m1!)*((s+1)!*(m2!))))
 .=(t!*(((m2+1)!)+(s+1)*(m2!)))/(m1!*((s+1)!*(m2!))) by A5,XCMPLX_1:92
 .=(t!*((m2!)*(m2+1)+(m2!)*(s+1)))/(m1!*((s+1)!*(m2!))) by Th21 .=
(m2!*((t!)*((m2+1)+(s+1))))/(m2!*(((s+1)!)*(m1!))) .=
     ((t!)*(t-(s-(s+1))))/(((s+1)!)*(m1!)) by A6,XCMPLX_1:92 .=
     ((t+1)!)/(((s+1)!)*(m1!)) by Th21;
    A8: s+1<=t+1 by A1,XREAL_1:8;
     then reconsider m = (t+1)-(s+1) as Element of NAT by INT_1:18;
      m = m1;
     hence thesis by A7,A8,Def3;
   end;
   end;
    suppose A9:s=t;
      then s<t+1 by NAT_1:13;
      then (t+1) choose (s+1)=1 & t choose s =1 & t choose (s+1)=0
      by A9,Th31,Def3;
      hence thesis;
    end;
    suppose A10:s>t;
      then s+1>t+0 & s+1 >t+1 by XREAL_1:10;
      then (t+1) choose (s+1)=0 & t choose s =0 & t choose (s+1)=0
        by A10,Def3;
      hence thesis;
    end;
 end;

theorem Th33:
  for s st s >= 1 holds s choose 1 = s
proof
    let s;
A1: s is Element of NAT by ORDINAL1:def 13;
    defpred P[Element of NAT] means $1 choose 1 = $1;
A2: P[1] by Th31;
A3: now let n; assume A4: n>=1 & P[n];
       (n+1) choose 1 = (n+1) choose (0+1)
                    .= n + n choose 0 by A4,Th32 .= n+1 by Th29;
      hence P[n+1];
    end;
     for n st n >= 1 holds P[n] from NAT_1:sch 8(A2,A3);
    hence thesis by A1;
end;

theorem
   for s,t st s>=1 & t = s-1 holds s choose t = s
proof
  let s,t;
  assume A1: s>=1 & t = s-1;
  then s choose t = s choose 1 by Th30;
  hence thesis by A1,Th33;
end;

theorem Th35:
  s choose r is Element of NAT
 proof
   defpred P[natural number] means for r holds ($1 choose r) is Element of NAT;
A1: P[0] proof let r;
     r = 0 or r > 0;
    hence 0 choose r is Element of NAT by Def3,Th27;
   end;
A2: for s st P[s] holds P[s+1] proof let s;
    assume A3: P[s];
A4: for r st r <> 0 holds ((s+1) choose r) is Element of NAT
    proof
      let r;
      assume A5: r <> 0;
       ((s+1) choose r) is Element of NAT
      proof
        consider t being Nat such that A6: r = t+1 by A5,NAT_1:6;
        reconsider t as Element of NAT by ORDINAL1:def 13;
        reconsider m1 = (s choose t), m2 = (s choose (t+1)) as Element of NAT
        by A3;
         m1 + m2 =(s choose t) + (s choose (t+1));
        hence thesis by A6,Th32;
      end;
      hence thesis;
    end;
    let r;
     r = 0 or r<>0;
    hence thesis by A4,Th29;
  end;
   for s holds P[s] from NAT_1:sch 2(A1,A2);
  hence thesis;
 end;

theorem
   for m,F st m <> 0 & len F = m & (for i,l st i in dom F & l = n+i-1
    holds F.i = l choose n) holds Sum F = (n+m) choose (n+1)
 proof
   defpred P[Element of NAT] means
   for F st $1 <> 0 & len F = $1 & (for i,l st i in dom F & l =
   n+i-1 holds F.i = l choose n) holds Sum F = (n+$1) choose (n+1);
A1: P[0];
A2: for m st P[m] holds P[m+1] proof let m;
   assume
A3: P[m];
   let F;
   assume
A4: m+1 <> 0 & len F = m+1 & (for i,l st i in dom F &
    l = n+i-1 holds F.i = l choose n);
    then consider G be FinSequence of REAL,x being Real such that
A5: F = G^<*x*> by FINSEQ_2:22;
A6: m+1 = len G +1 by A4,A5,FINSEQ_2:19;
   per cases;
    suppose A7: m = 0;
     then A8: dom F = Seg 1 by A4,FINSEQ_1:def 3;
      then A9: 1 in dom F;
       n = n+1-1;
      then F.1 = n choose n by A4,A9;
     hence Sum F = Sum<*n choose n*> by A8,FINSEQ_1:def 8
        .= n choose n by FINSOP_1:12
        .= 1 by Th31
        .= (n+(m+1)) choose (n+1) by A7,Th31;
    end;
    suppose A10:m <> 0;
A11: for i,l st i in dom G & l = n+i-1 holds G.i = l choose n
    proof
     let i,l;
     assume A12: i in dom G & l = n+i-1;
     then A13: G.i = F.i by A5,FINSEQ_1:def 7;
      dom G c= dom F by A5,FINSEQ_1:39;
     hence G.i = l choose n by A4,A12,A13;
    end;
     dom F = Seg (m+1) by A4,FINSEQ_1:def 3;
   then A14: m+1 in dom F by FINSEQ_1:6;
    n+m = n +(m+1)-1;
   then A15: (n+m) choose n = (G^<*x*>).(len G +1) by A4,A5,A6,A14 .= x by
FINSEQ_1:59;
   thus Sum F = Sum G + x by A5,RVSUM_1:104
    .= (n+m) choose (n+1) + (n+m) choose n
      by A3,A6,A10,A11,A15 .= (n+m+1) choose (n+1) by Th32
    .= (n+(m+1)) choose (n+1);
   end;
  end;
  thus for m holds P[m] from NAT_1:sch 1(A1,A2);
 end;

registration
  let k,n be natural number;
  cluster n choose k -> natural;
  coherence by Th35;
end;

definition
  let k,n be natural number;
  redefine func n choose k -> Element of NAT;
  coherence by ORDINAL1:def 13;
end;

definition let a,b be real number;
           let n be natural number;
  func (a,b) In_Power n -> FinSequence of REAL means
 :Def4:  len it = n+1 &
   (for i,l,m being natural number st i in dom it & m = i - 1 & l = n-m holds
   it.i = (n choose m)* a|^l * b|^m);
existence
 proof
  reconsider n as Element of NAT by ORDINAL1:def 13;
  defpred P[Element of NAT,set] means
   for l,m be natural number st m = $1 - 1 & l = n-m holds
    $2 = (n choose m)* a|^l * b|^m;
 A1: for k for y1,y2 being set st k in Seg(n+1) & P[k,y1] & P[k,y2] holds y1=y2
  proof
   let k;
   let y1,y2 be set;
   assume A2: k in Seg (n+1) & P[k,y1] & P[k,y2];
   then A3: k >= 1 & k <= n+1 by FINSEQ_1:3;
   then reconsider m1 = k-1 as Element of NAT by INT_1:18;
    k-1<=n+1-1 by A3,XREAL_1:11;
   then reconsider l1 = n-m1 as Element of NAT by INT_1:18;
    y1 = (n choose m1)* a|^l1 * b|^m1 by A2;
   hence thesis by A2;
  end;
 A4: for k st k in Seg(n+1) ex x being set st P[k,x]
  proof
   let k;
   assume k in Seg (n+1);
   then A5: k >= 1 & k <= n+1 by FINSEQ_1:3;
   then reconsider m1 = k-1 as Element of NAT by INT_1:18;
    k-1<=n+1-1 by A5,XREAL_1:11;
   then reconsider l1 = n-m1 as Element of NAT by INT_1:18;
   take z = (n choose m1)*a|^l1*b|^m1;
   thus thesis;
  end;
  consider p being FinSequence such that
A6: dom p = Seg(n+1) and
A7: for k st k in Seg(n+1) holds P[k,p.k] from FINSEQ_1:sch 1(A1,A4);
      rng p c= REAL
     proof
      let x be set;
      assume x in rng p;
      then consider y be set such that A8: y in dom p & x = p.y by
FUNCT_1:def 5;
      reconsider y as Element of NAT by A8;
      A9: y >= 1 & y <= n+1 by A6,A8,FINSEQ_1:3;
      then reconsider m1 = y-1 as Element of NAT by INT_1:18;
       y-1<=n+1-1 by A9,XREAL_1:11;
      then reconsider l1 = n-m1 as Element of NAT by INT_1:18;
       p.y = (n choose m1) *a|^l1*b|^m1 by A6,A7,A8;
      then reconsider x as Real by A8,XREAL_0:def 1;
       x in REAL;
     hence thesis;
     end;
    then reconsider p as FinSequence of REAL by FINSEQ_1:def 4;
    take p;
    thus thesis by A6,A7,FINSEQ_1:def 3;
  end;
uniqueness
 proof
  let G,H;
  assume A10: len G = n+1 &
   (for i,l,m be natural number st i in dom G & m = i - 1 & l = n-m holds
   G.i = (n choose m)* a|^l * b|^m);
   assume A11: len H = n+1 &
   (for i,l,m be natural number st i in dom H & m = i - 1 & l = n-m holds
   H.i = (n choose m)* a|^l * b|^m);
    for i st i in dom G holds G.i = H.i
   proof
     let i;
     assume A12: i in dom G;
     then A13: i in Seg (n+1) by A10,FINSEQ_1:def 3;
     then A14:i in dom H by A11,FINSEQ_1:def 3;
A15: i>=1 & i<=n+1 by A13,FINSEQ_1:3;
     then reconsider m = i-1 as Element of NAT by INT_1:18;
      i-1<=n+1-1 by A15,XREAL_1:11;
     then reconsider l = n-m as Element of NAT by INT_1:18;
      H.i = (n choose m)* a|^l *b|^m by A11,A14;
     hence thesis by A10,A12;
   end;
  hence G = H by A10,A11,Th3;
 end;
end;

canceled;

theorem Th38:
  (a,b) In_Power 0 = <*1*>
 proof
   set F = (a,b) In_Power 0;
A1: len F = 0+1 by Def4 .= 1;
   then A2: dom F = {1} by FINSEQ_1:4,def 3;
   then 1 in dom F by TARSKI:def 1;
   then consider i be set such that A3: i in dom F;
A4: 1 in dom (a,b) In_Power 0 by A2,TARSKI:def 1;
   reconsider i as Element of NAT by A3;
A5: i = 1 by A2,A3,TARSKI:def 1;
   then reconsider m1 = i-1 as Element of NAT by INT_1:18;
   reconsider l1 = 0-m1 as Element of NAT by A5;
    F.1 = (0 choose 0)*a|^l1*b|^m1 by A4,A5,Def4 .=
   1*a|^0*b|^0 by A5,Th31 .= 1*1*b|^0 by FINSEQ_2:72,RVSUM_1:124 .= 1 by
FINSEQ_2:72,RVSUM_1:124;
   hence thesis by A1,FINSEQ_1:57;
 end;

theorem Th39:
  ((a,b) In_Power s).1 = a|^s
  proof
    reconsider m1 = 1-1 as Element of NAT by INT_1:18;
    reconsider l1 = s-m1 as natural number;
     len ((a,b) In_Power s) = s+1 by Def4;
    then A1: dom ((a,b) In_Power s) = Seg (s+1) by FINSEQ_1:def 3;
     s+1>=0+1 by XREAL_1:8;
    then 1 in dom ((a,b) In_Power s) by A1;
    then ((a,b) In_Power s).1 = (s choose 0)*a|^l1*b|^m1 by Def4
      .= 1*a|^l1*b|^m1 by Th29
      .= a|^s by FINSEQ_2:72,RVSUM_1:124;
    hence thesis;
 end;

theorem Th40:
  ((a,b) In_Power s).(s+1) = b|^s
 proof
   s+1>=1 by NAT_1:12;
  then reconsider m1 = s+1-1 as Element of NAT by INT_1:18;
  reconsider l1 = s-m1 as Element of NAT by INT_1:18;
   len ((a,b) In_Power s) = s+1 by Def4;
  then dom ((a,b) In_Power s) = Seg (s+1) by FINSEQ_1:def 3;
  then s+1 in dom ((a,b) In_Power s) by FINSEQ_1:6;
  then ((a,b) In_Power s).(s+1) = (s choose s)*a|^l1*b|^m1 by Def4
    .= 1*a|^l1*b|^m1 by Th31  .= 1*b|^m1 by FINSEQ_2:72,RVSUM_1:124  .= b|^s;
  hence thesis;
 end;

theorem
Th41: for s holds (a+b)|^s = Sum((a,b) In_Power s)
 proof
   defpred P[Element of NAT] means (a+b)|^$1 = Sum((a,b) In_Power $1);
     (a+b)|^0 = 1 by FINSEQ_2:72,RVSUM_1:124 .= Sum<*1 qua Real*> by FINSOP_1:
12
           .= Sum((a,b) In_Power 0) by Th38;
then A1: P[0];
  A2: for n st P[n] holds P[n+1]
  proof let n;
    assume
A3:   P[n];
    reconsider a, b as Real by XREAL_0:def 1;
    A4: (a+b)|^(n+1) = (a+b)*Sum((a,b) In_Power n) by A3,Th11
     .= a*Sum((a,b) In_Power n) + b*Sum((a,b) In_Power n)
     .= Sum(a*((a,b) In_Power n)) + b*Sum((a,b) In_Power n) by RVSUM_1:117
     .= Sum(a*((a,b) In_Power n)) + Sum(b*((a,b) In_Power n)) by RVSUM_1:117;
    reconsider G1 = ((a*((a,b) In_Power n))^<*0 qua Real*>)
      as FinSequence of REAL;
    set G2 = (<*0 qua Real*>^(b*((a,b) In_Power n)));
        len G1 = len (a*((a,b) In_Power n)) + len <*0*> by FINSEQ_1:35
          .= len (a*((a,b) In_Power n)) +1 by FINSEQ_1:57
          .= len ((a,b) In_Power n) +1 by Th6
          .= n+1+1 by Def4;
    then reconsider F1 = G1 as Element of (n+1+1)-tuples_on REAL by
FINSEQ_2:110;
    A5: len F1 = n+1+1 by FINSEQ_2:109;
        len G2 = len <*0*> + len (b*((a,b) In_Power n)) by FINSEQ_1:35
          .= 1+ len (b*((a,b) In_Power n)) by FINSEQ_1:57
          .= 1+ len ((a,b) In_Power n) by Th6
          .= n+1+1 by Def4;
    then reconsider F2 = G2 as Element of (n+1+1)-tuples_on REAL by
FINSEQ_2:110;
    A6: len F2 = n+1+1 by FINSEQ_2:109;
    A7: Sum F1 = Sum(a*((a,b) In_Power n)) +0 by RVSUM_1:104
       .= Sum(a*((a,b) In_Power n));
         Sum F2 = 0+ Sum(b*((a,b) In_Power n)) by RVSUM_1:106
       .= Sum(b*((a,b) In_Power n));
then A8:    Sum(G1+G2) = Sum(a*((a,b) In_Power n))+ Sum
(b*((a,b) In_Power n)) by A7,RVSUM_1:119;
    set F = F1 + F2;
    A9: len F = n+1+1 by FINSEQ_2:109;
    A10: for i st i in dom ((a,b) In_Power (n+1))
         holds F.i = ((a,b) In_Power (n+1)).i
     proof
      let i;
      assume A11: i in dom ((a,b) In_Power (n+1));
A12:      len ((a,b) In_Power (n+1)) = n+1+1 by Def4;
      then A13: dom ((a,b) In_Power (n+1)) = Seg (n+1+1) by FINSEQ_1:def 3;
      A14: i in Seg (n+1+1) by A11,A12,FINSEQ_1:def 3;
      then i>=1 & n+1+1>=i by FINSEQ_1:3;
      then reconsider j = i-1 as Element of NAT by INT_1:18;
A15:   i in dom F by A9,A14,FINSEQ_1:def 3;
A16:  i in dom F1 by A5,A14,FINSEQ_1:def 3;
A17:  i in dom F2 by A6,A14,FINSEQ_1:def 3;
      A18: i = j+1;
      A19: n+1+1>=1 by NAT_1:12;
      set x = ((a,b) In_Power n)/.j;
      set r = ((a,b) In_Power n)/.i;
      set r1 = F1/.i;
      set r2 = F2/.i;
      A20: i in {1} implies F.i = ((a,b) In_Power (n+1)).i
       proof
        assume i in {1};
        then A21: i = 1 by TARSKI:def 1;
         n+1>=0+1 by XREAL_1:8;
        then 1 in Seg (n+1);
        then 1 in Seg len (((a,b) In_Power n)) by Def4;
        then A22: 1 in dom ((a,b) In_Power n) by FINSEQ_1:def 3;
        then A23: 1 in dom (a*((a,b) In_Power n)) by Th7;
         A24:r = ((a,b) In_Power n).i by A21,A22,FINSEQ_4:def 4;
        A25: r = ((a,b) In_Power n).1 by A21,A22,FINSEQ_4:def 4;
        A26: r = a|^n by A21,A24,Th39;
        A27: r1 = F1.i by A16,FINSEQ_4:def 4;
        A28: r1 = ((a*((a,b) In_Power n))^<*0*>).1 by A16,A21,FINSEQ_4:def 4
         .= (a*((a,b) In_Power n)).1 by A23,FINSEQ_1:def 7
         .= a *a|^n by A25,A26,RVSUM_1:66 .= a|^(n+1) by Th11;
        A29: r2 = F2.i by A17,FINSEQ_4:def 4;
             r2 = (<*0*>^(b*((a,b) In_Power n))).1 by A17,A21,FINSEQ_4:def 4
         .= 0 by FINSEQ_1:58;
        then F.i = r1+0 by A27,A29,RVSUM_1:27
         .= ((a,b) In_Power (n+1)).i by A21,A28,Th39;
        hence thesis;
       end;
      A30: i in {k: k>1 & k<n+1+1} implies F.i = ((a,b) In_Power (n+1)).i
       proof
        assume i in {k: 1<k & k<n+1+1};
         then A31:ex k st k=i & 1<k & k<n+1+1;
        then A32: 1<=i & i<=n+1 by NAT_1:13;
        then i in Seg (n+1);
        then i in Seg len ((a,b) In_Power n) by Def4;
        then A33: i in dom ((a,b) In_Power n) by FINSEQ_1:def 3;
        then A34: i in dom (a*((a,b) In_Power n)) by Th7;
        reconsider m1 = i-1 as Element of NAT by A31,INT_1:18;
            i-1<=n+1-1 by A32,XREAL_1:11;
        then reconsider l1 = n-m1 as Element of NAT by INT_1:18;
A35:        1<=j & j+1<=n+1+1 by A18,A31,NAT_1:13;
        A36: 1<=j & j<=n+1 by A18,A31,NAT_1:13,XREAL_1:8;
        then j in Seg (n+1);
        then j in Seg len ((a,b) In_Power n) by Def4;
        then A37: j in dom ((a,b) In_Power n) by FINSEQ_1:def 3;
        then A38: j in dom (b*((a,b) In_Power n)) by Th7;
        reconsider m2 = j-1 as Element of NAT by A35,INT_1:18;
        A39: l1+1 = n+1-(m2+1);
            j-1<=n+1-1 by A36,XREAL_1:11;
        then reconsider l2 = n-m2 as Element of NAT by INT_1:18;
        A40: x = ((a,b) In_Power n).j by A37,FINSEQ_4:def 4;
        A41: r = ((a,b) In_Power n).i by A33,FINSEQ_4:def 4;
        A42: r1 = ((a*((a,b) In_Power n))^<*0*>).i by A16,FINSEQ_4:def 4;
        A43: r2 = (<*0*>^(b*((a,b) In_Power n))).i by A17,FINSEQ_4:def 4;
        A44: r1 = (a*((a,b) In_Power n)).i by A34,A42,FINSEQ_1:def 7
         .= a*r by A41,RVSUM_1:66;
             r2 = (<*0*>^(b*((a,b) In_Power n))).(len <*0*> +j) by A18,A43,
FINSEQ_1:57
         .= (b*((a,b) In_Power n)).j by A38,FINSEQ_1:def 7
         .= b*x by A40,RVSUM_1:66;
        then F.i = a*r + b*x by A15,A42,A43,A44,SEQ_1:def 3
         .= a*(a|^l1*(n choose m1)*b|^m1) + b*x by A33,A41,Def4
         .= a*a|^l1*((n choose m1)*b|^m1) + b*x
         .= a|^(l1+1)*((n choose m1)*b|^m1) + b*x by Th11
         .= a|^(l1+1)*((n choose m1)*b|^m1) + b*(b|^m2*((n choose m2)*a|^l2))
 by A37,A40,Def4
         .= a|^(l1+1)*((n choose m1)*b|^m1) + b*b|^m2*((n choose m2)*a|^l2)
         .=
a|^(l1+1)*((n choose (m2+1))*b|^(m2+1)) + b|^(m2+1)*((n choose m2)*a|^l2)
           by Th11
         .= ((n choose (m2+1)) + (n choose m2))*a|^(l1+1)*b|^(m2+1)
         .= ((n+1) choose (m2+1))*a|^(l1+1)*b|^(m2+1) by Th32
         .= ((a,b) In_Power (n+1)).i by A11,A39,Def4;
        hence thesis;
       end;
      A45: i in {n+1+1} implies F.i = ((a,b) In_Power (n+1)).i
       proof
        assume A46: i in {n+1+1};
        then A47: i = n+1+1 by TARSKI:def 1;
        A48: j = n+1+1-1 by A46,TARSKI:def 1 .= n+1;
        A49: n+1 = len ((a,b) In_Power n) by Def4
         .= len (a*((a,b) In_Power n)) by Th6;
            n+1 in Seg (n+1) by FINSEQ_1:6;
        then j in Seg len (((a,b) In_Power n)) by A48,Def4;
        then A50: j in dom ((a,b) In_Power n) by FINSEQ_1:def 3;
        then A51: j in dom (b*((a,b) In_Power n)) by Th7;
        A52: x = ((a,b) In_Power n).j by A50,FINSEQ_4:def 4;
        A53: x = ((a,b) In_Power n).(n+1) by A48,A50,FINSEQ_4:def 4
               .= b|^n by Th40;
        A54: r1 = F1.i by A16,FINSEQ_4:def 4;
        A55: r1 =
((a*((a,b) In_Power n))^<*0*>).(len (a*((a,b) In_Power n))+1)
           by A16,A47,A49,FINSEQ_4:def 4
         .= 0 by FINSEQ_1:59;
        A56: r2 = F2.i by A17,FINSEQ_4:def 4;
        A57: r2 = (<*0*>^(b*((a,b) In_Power n))).(1+n+1) by A17,A47,
FINSEQ_4:def 4
         .= (<*0*>^(b*((a,b) In_Power n))).(len <*0*> +j) by A48,FINSEQ_1:56
         .= (b*((a,b) In_Power n)).j by A51,FINSEQ_1:def 7
         .= b*b|^n by A52,A53,RVSUM_1:66 .= b|^(n+1) by Th11;
            F.i = 0+r2 by A54,A55,A56,RVSUM_1:27
         .= ((a,b) In_Power (n+1)).i by A47,A57,Th40;
        hence thesis;
       end;
          now assume i in {1} \/ {k: 1<k & k<n+1+1} \/ {n+1+1};
      then i in {1} \/ {k: 1<k & k<n+1+1} or i in {n+1+1} by XBOOLE_0:def 2;
      hence F.i = ((a,b) In_Power (n+1)).i by A20,A30,A45,XBOOLE_0:def 2;
      end;
      hence thesis by A11,A13,A19,Th5;
     end;
        len ((a,b) In_Power (n+1)) = len F by A9,Def4;
    hence thesis by A4,A8,A10,Th3;
   end;
A58:  for n holds P[n] from NAT_1:sch 1(A1,A2);
  let s;
   s is Element of NAT by ORDINAL1:def 13;
  hence thesis by A58;
 end;

definition let n be natural number;
  func Newton_Coeff n -> FinSequence of REAL means :Def5:
    len it = n+1 &
    (for i,k be natural number st i in dom it & k = i-1 holds
      it.i = n choose k);
existence
 proof
  reconsider n as Element of NAT by ORDINAL1:def 13;
 defpred P[natural number,set] means
 for s st s = $1-1 holds $2 = n choose s;
 A1: for l for r1,r2 being set
      st l in Seg (n+1) & P[l,r1] & P[l,r2] holds r1 = r2
   proof
    let l;
    let r1,r2 be set;
    assume A2: l in Seg (n+1) & P[l,r1] & P[l,r2];
    then l>=1 & l<=n+1 by FINSEQ_1:3;
    then reconsider k = l-1 as Element of NAT by INT_1:18;
     r1 = n choose k by A2;
    hence thesis by A2;
   end;
 A3: for l st l in Seg (n+1) ex x being set st P[l,x]
   proof
    let l;
    assume l in Seg (n+1);
    then l>=1 & l<=n+1 by FINSEQ_1:3;
    then reconsider k = l-1 as Element of NAT by INT_1:18;
    set x = n choose k;
    take x;
    thus thesis;
   end;
  consider F being FinSequence such that A4:
   dom F = Seg (n+1) & for l st l in Seg (n+1) holds
   P[l,F.l] from FINSEQ_1:sch 1(A1,A3);
      rng F c= REAL
   proof
      let x be set;
      assume x in rng F;
      then consider y be set such that A5: y in dom F & x = F.y by
FUNCT_1:def 5;
      reconsider y as Element of NAT by A5;
       y >= 1 & y <= n+1 by A4,A5,FINSEQ_1:3;
      then reconsider m1 = y-1 as Element of NAT by INT_1:18;
       F.y = n choose m1 by A4,A5;
      then reconsider x as Real by A5;
       x in REAL;
     hence thesis;
     end;
    then reconsider F as FinSequence of REAL by FINSEQ_1:def 4;
    take F;
    thus thesis by A4,FINSEQ_1:def 3;
 end;
uniqueness
 proof
  let G,H;
  assume A6: len G = n+1 &
   (for i,m be natural number st i in dom G & m = i - 1 holds
   G.i = n choose m);
  assume A7: len H = n+1 &
   (for i,m be natural number st i in dom H & m = i - 1 holds
   H.i = n choose m);
     for i st i in dom G holds G.i = H.i
    proof
     let i;
     assume A8: i in dom G;
     then A9: i in Seg (n+1) by A6,FINSEQ_1:def 3;
     then A10:i in dom H by A7,FINSEQ_1:def 3;
      i>=1 & i<=n+1 by A9,FINSEQ_1:3;
     then reconsider m = i-1 as Element of NAT by INT_1:18;
      H.i = n choose m by A7,A10;
     hence thesis by A6,A8;
    end;
  hence G = H by A6,A7,Th3;
 end;
end;

canceled;

theorem Th43:
  for s holds Newton_Coeff s = (1,1) In_Power s
 proof
  let s;
  A1: len (Newton_Coeff s) = s+1 by Def5 .= len ((1,1) In_Power s) by Def4;
    for i st i in dom (Newton_Coeff s) holds
   (Newton_Coeff s).i = ((1,1) In_Power s).i
  proof
   let i;
   assume A2: i in dom (Newton_Coeff s);
A3: dom (Newton_Coeff s) = Seg len (Newton_Coeff s) by FINSEQ_1:def 3
    .= Seg (s+1) by Def5;
   then A4: dom (Newton_Coeff s) = Seg len ((1,1) In_Power s) by Def4
     .= dom ((1,1) In_Power s) by FINSEQ_1:def 3;
A5: i>=1 & i<=(s+1) by A2,A3,FINSEQ_1:3;
   then reconsider m1 = i-1 as Element of NAT by INT_1:18;
       s+1-1>=i-1 by A5,XREAL_1:11;
   then reconsider l1 = s-m1 as Element of NAT by INT_1:18;
       ((1,1) In_Power s).i = (s choose m1)*1|^l1*1|^m1 by A2,A4,Def4
    .= (s choose m1)*1*1|^m1 by Th15 .= (s choose m1)*1 by Th15 .=
 (Newton_Coeff s).i by A2,Def5;
   hence thesis;
  end;
 hence thesis by A1,Th3;
 end;

theorem
   for s holds 2|^s = Sum(Newton_Coeff s)
  proof
    let s;
     2|^s = (1+1)|^s .= Sum((1,1) In_Power s) by Th41 .= Sum(Newton_Coeff s)
      by Th43;
    hence thesis;
  end;

begin :: Addenda

:: from NAT_LAT

theorem Th45:
  l >= 1 implies k*l>=k
  proof
    assume A1: l>=1;
     for k holds k*l>=k
    proof
      defpred P[Element of NAT] means $1*l>=$1;
   A2:P[0];
   A3:for k st P[k] holds P[k+1]
   proof let k;
     assume A4:k*l>=k;
      (k + 1)*l = k * l + 1*l;
     then A5:(k+1)*l>=k+l by A4,XREAL_1:9;
      k+l>=k+1 by A1,XREAL_1:9;
     hence (k+1)*l>=k+1 by A5,XXREAL_0:2;
    end;
   thus for k holds P[k] from NAT_1:sch 1(A2,A3);
  end;
 hence thesis;
end;

theorem Th46:
  l >= 1 & n >= k*l implies n>=k
proof
  assume that A1: l>=1 and A2: n>=k*l;
   k*l>=k by A1,Th45;
  hence thesis by A2,XXREAL_0:2;
end;

definition let n;
  redefine func n! -> Element of NAT;
  coherence by Th22;
end;

theorem Th47:
  l <> 0 implies l divides l!
proof
  assume l<>0;
  then consider n being Nat such that A1: l = n+1 by NAT_1:6;
  reconsider n as Element of NAT by ORDINAL1:def 13;
   (n+1) divides (n+1)!
  proof
     (n+1)! = (n+1) * (n!) by Th21;
    hence thesis by NAT_D:def 3;
  end;
  hence thesis by A1;
end;

theorem
   n <> 0 implies (n+1)/n > 1
  proof
   assume A1: n<>0;
   then A2: n>0;
A3:(n+1)/n=n/n+1/n by XCMPLX_1:63
         .=1+1/n by A1,XCMPLX_1:60;
    1/n>0 by A2,REAL_2:127;
   hence thesis by A3,XREAL_1:31;
  end;

theorem Th49:
  k/(k+1) < 1
  proof
    defpred P[Element of NAT] means $1/($1+1)<1;
A1: P[0];
A2: P[m] implies P[m+1] proof
    assume m/(m+1)<1;
    set r=m+1;
     (m+1) < ((m+1)+1) by XREAL_1:31;
    hence thesis by REAL_2:142;
   end;
    for m holds P[m] from NAT_1:sch 1(A1,A2);
  hence thesis;
 end;

theorem Th50:
  for l being Element of NAT holds l! >= l
  proof
    defpred P[Element of NAT] means $1! >= $1;
A1: P[0];
A2: for l st P[l] holds P[l+1] proof let l;
    assume A3:l!>=l;
A4: l!*(l+1)=(l+1)! by Th21;
     l=0 & (l+1)!>=(l+1) or l>=1 & (l+1)!>=(l+1)
    proof
      per cases by NAT_1:14;
       case l=0;
       hence thesis by FINSEQ_2:59,RVSUM_1:125;
       end;
       case A5: l>=1;   (l+1)!>=(l+1)*l by A3,A4,XREAL_1:66;
       hence (l+1)!>=(l+1) by A5,Th46;
     end;
    end;
    hence (l+1)!>=(l+1);
   end;
   thus for l being Element of NAT holds P[l] from NAT_1:sch 1(A1,A2);
  end;

 reserve s,j,r for Element of NAT;

theorem Th51:
  for m,n st m<>1 holds m divides n implies not m divides (n+1)
   proof let m,n;
    assume that A1:m<>1 and A2:m divides n and A3: m divides (n+1);
    consider t being Nat such that A4: n = m * t by A2,NAT_D:def 3;
    consider s being Nat such that A5: n+1 = m * s by A3,NAT_D:def 3;
A6:  1=m*s-m*t by A4,A5;
A7:  s<>0 by A5;
      t <= s
     proof
      assume A8: t > s;
       (n+1)*t=m*s*t by A5 .= n*s by A4;
then A9:       t=n*s/(n+1) by XCMPLX_1:90
           .=s*(n/(n+1)) by XCMPLX_1:75;
A10:  s>0 by A7;
       n/(n+1)<1 by Th49;
      hence contradiction by A8,A9,A10,REAL_2:145;
     end;
    then reconsider r =s-t as Element of NAT by INT_1:18;
     1=m*r by A6;
    hence contradiction by A1,NAT_1:15;
end;

theorem
   j divides l & j divides l+1 iff j=1 by Th51,NAT_D:6;

theorem Th53:
  for k,j st j<>0 holds j divides (j+k)!
  proof
    defpred P[Element of NAT] means for j st j<>0 holds j divides (j+$1)!;
A1: P[0] by Th47;
A2: for k st P[k] holds P[k+1]  proof let k;
    assume A3: for j st j<>0 holds j divides (j+k)!;
    let j;
    assume j<>0;
     then j divides (j+k)! by A3;
      then j divides ((j+k)!)*((j+k)+1) by NAT_D:9;
     hence thesis by Th21;
    end;
   thus for k holds P[k] from NAT_1:sch 1(A1,A2);
  end;

theorem Th54:
  j<=l & j<>0 implies j divides l!
  proof
    assume that A1: j<=l and A2: j<>0;
    consider k being Nat such that A3: l=j+k by A1,NAT_1:10;
    reconsider k as Element of NAT by ORDINAL1:def 13;
     l=j+k by A3;
    hence thesis by A2,Th53;
  end;

theorem Th55:
  for l,j st j<>1 & j<>0 holds j divides (l!+1) implies j>l
  proof let l,j;
    assume that A1: j<>1 and A2: j<>0 and A3: j divides (l!+1);
     not j divides l! by A1,A3,Th51;
    hence thesis by A2,Th54;
  end;

:: The fundamental properties of lcm, hcf

theorem
   m lcm (n lcm k) = (m lcm n) lcm k
 proof
  set M = n lcm k;
  set K = m lcm M;
  set N = m lcm n;
  set L = N lcm k;
   K = L
  proof
  A1: n divides M & k divides M & for l st n divides l & k divides
 l holds M divides l by NAT_D:def 4;
  A2: m divides K & M divides K & for i st m divides i & M divides
 i holds K divides i by NAT_D:def 4;
  A3: m divides N & n divides N & for s st m divides s & n divides
 s holds N divides s by NAT_D:def 4;
  A4: N divides L & k divides L & for j st N divides j & k divides
 j holds L divides j by NAT_D:def 4;
  A5: K divides L
   proof
   A6: m divides L by A3,A4,NAT_D:4;
    n divides L by A3,A4,NAT_D:4;
   then M divides L by A4,NAT_D:def 4;
   hence thesis by A6,NAT_D:def 4;
   end;
    L divides K
   proof
   A7: k divides K by A1,A2,NAT_D:4;
    n divides K by A1,A2,NAT_D:4;
   then N divides K by A2,NAT_D:def 4;
   hence thesis by A7,NAT_D:def 4;
   end;
  hence thesis by A5,NAT_D:5;
  end;
 hence thesis;
end;

theorem Th57:
  m divides n iff m lcm n = n
proof
thus m divides n implies m lcm n = n
proof
 set M = m lcm n;
 assume A1: m divides n;
  A2: m divides M & n divides M & for s st m divides s & n divides
 s holds M divides s by NAT_D:def 4;
    M = n
   proof
     M divides n by A1,NAT_D:def 4;
    hence thesis by A2,NAT_D:5;
   end;
 hence thesis;
end;
thus m lcm n = n implies m divides n by NAT_D:def 4;
end;

theorem
   n divides m & k divides m iff n lcm k divides m
 proof
    n lcm k divides m implies (n divides m & k divides m)
   proof
    set M = n lcm k;
    assume A1: n lcm k divides m;
     n divides M & k divides M & for l st n divides l &
    k divides l holds M divides l by NAT_D:def 4;
    hence thesis by A1,NAT_D:4;
   end;
   hence thesis by NAT_D:def 4;
  end;

theorem
   m lcm 1 = m
proof
set M = m lcm 1;
A1: m divides M & 1 divides M & for l st m divides l & 1 divides
 l holds M divides l by NAT_D:def 4;
     M divides m
 proof
    1 divides m by NAT_D:6;
 hence thesis by NAT_D:def 4; end;
hence thesis by A1,NAT_D:5; end;

theorem Th60:
  m lcm n divides m*n
proof
   set s=m*n;
A1:m divides s by NAT_D:9;
    n divides s by NAT_D:9;
   hence thesis by A1,NAT_D:def 4;
end;

theorem
   m hcf (n hcf k) = (m hcf n) hcf k
 proof
  set M = n hcf k;
  set K = m hcf M;
  set N = m hcf n;
  set L = N hcf k;
    K = L
  proof
  A1: M divides n & M divides k & for l st l divides n & l divides
 k holds l divides M by NAT_D:def 5;
  A2: K divides m & K divides M & for i st i divides m & i divides
 M holds i divides K by NAT_D:def 5;
  A3: N divides m & N divides n & for s st s divides m & s divides
 n holds s divides N by NAT_D:def 5;
  A4: L divides N & L divides k & for j st j divides N & j divides
 k holds j divides L by NAT_D:def 5;
  A5: K divides L
   proof
   A6: K divides k by A1,A2,NAT_D:4;
   A7: K divides m by NAT_D:def 5;
        K divides n by A1,A2,NAT_D:4;
   then K divides N by A7,NAT_D:def 5;
   hence thesis by A6,NAT_D:def 5;
   end;
       L divides K
   proof
   A8: L divides n by A3,A4,NAT_D:4;
   A9: L divides m by A3,A4,NAT_D:4;
     L divides M by A4,A8,NAT_D:def 5;
   hence thesis by A9,NAT_D:def 5;
   end;
  hence thesis by A5,NAT_D:5;
  end;
 hence thesis;
 end;

theorem Th62:
  n divides m implies n hcf m = n
proof
 set N = n hcf m;
 assume A1: n divides m;
 A2: N divides n & N divides m & for k st k divides n & k divides
 m holds k divides N by NAT_D:def 5;
  N = n
 proof
   n divides N by A1,NAT_D:def 5;
  hence thesis by A2,NAT_D:5;
 end;
 hence thesis;
end;

theorem
   m divides n & m divides k iff m divides n hcf k
 proof
    m divides n hcf k implies (m divides n & m divides k)
   proof
    set M = n hcf k;
    assume A1: m divides n hcf k;
     M divides n & M divides k & for l st l divides n & l divides
 k holds l divides M by NAT_D:def 5;
    hence thesis by A1,NAT_D:4;
   end;
   hence thesis by NAT_D:def 5;
  end;

theorem
   m hcf 1 = 1
proof set M = m hcf 1;
A1: M divides m & M divides 1 & for l st l divides m & l divides
  1 holds l divides M by NAT_D:def 5;
   1 divides M by NAT_D:6;
  hence thesis by A1,NAT_D:5;
end;

theorem
   m hcf 0 = m
proof set M = m hcf 0;
A1: m divides M
 proof
   m divides 0 by NAT_D:6;
  hence thesis by NAT_D:def 5;end;
    M divides m by NAT_D:def 5;
hence thesis by A1,NAT_D:5;end;

theorem Th66:
  (m hcf n) lcm n = n
proof
  set M = m hcf n;
   M divides m & M divides n & for k st k divides m & k divides
  n holds k divides M by NAT_D:def 5;
  hence thesis by Th57;
end;

theorem Th67:
  m hcf (m lcm n) = m
proof
  set M = m lcm n;
   m divides M & n divides M & for k st m divides k & n divides
  k holds M divides k by NAT_D:def 4;
  hence m hcf (m lcm n) = m by Th62;
end;

theorem
   m hcf (m lcm n) = (n hcf m) lcm m
proof
   m hcf (m lcm n) = m by Th67;
  hence thesis by Th66;
end;

theorem
   m divides n implies m hcf k divides n hcf k
proof
  set M = m hcf k;
A1: M divides m & M divides k & for i st i divides m & i divides
  k holds i divides M by NAT_D:def 5;
  assume m divides n;
  then M divides n by A1,NAT_D:4;
  hence thesis by A1,NAT_D:def 5;
end;

theorem
   m divides n implies k hcf m divides k hcf n
proof
 set M = k hcf m;
 A1: M divides k & M divides m & for i st i divides m & i divides
 k holds i divides M by NAT_D:def 5;
 assume m divides n;
  then M divides n by A1,NAT_D:4;
 hence thesis by A1,NAT_D:def 5;
end;

theorem
   n > 0 implies n hcf m > 0
proof
  assume that A1: n>0 and A2: n hcf m <= 0;
A3: (n hcf m) divides n by NAT_D:def 5;
   n hcf m = 0 or n hcf m < 0 by A2;
  then ex r being Nat st n = 0*r by A3,NAT_D:def 3;
  hence contradiction by A1;
end;

canceled;

theorem
   m > 0 & n > 0 implies m lcm n > 0
proof
   assume that A1: m>0 and A2: n>0 and A3: m lcm n <= 0;
A4:(m lcm n) divides m*n by Th60;
    m lcm n = 0 or m lcm n < 0 by A3;
   then ex r being Nat st m*n = 0*r by A4,NAT_D:def 3;
   hence contradiction by A1,A2,REAL_2:122;
end;

theorem
   (n hcf m) lcm (n hcf k) divides n hcf (m lcm k)
proof
  set M = m lcm k;
  set N = n hcf k;
  set P = n hcf m;
  set L = P lcm N;
A1: m divides M & k divides M & for l st m divides l & k divides
   l holds M divides l by NAT_D:def 4;
A2: N divides n & N divides k & for s st s divides n & s divides
   k holds s divides N by NAT_D:def 5;
A3: P divides n & P divides m & for j st j divides n & j divides
   m holds j divides P by NAT_D:def 5;
    L divides (n hcf M)
   proof
A4:  L divides n by A2,A3,NAT_D:def 4;
A5:  P divides M by A1,A3,NAT_D:4;
      N divides M by A1,A2,NAT_D:4;
     then L divides M by A5,NAT_D:def 4;
     hence thesis by A4,NAT_D:def 5;
   end;
   hence thesis;
end;

theorem
   m divides l implies m lcm (n hcf l) divides (m lcm n) hcf l
proof
  assume A1: m divides l;
  set M = m lcm n;
  set K = M hcf l;
  set N = n hcf l;
A2: m divides M & n divides M & for j st m divides j & n divides
  j holds M divides j by NAT_D:def 4;
A3: N divides n & N divides l & for s st s divides n & s divides
  l holds s divides N by NAT_D:def 5;
   (m lcm N) divides K
  proof
A4: m divides K by A1,A2,NAT_D:def 5;
     N divides M by A2,A3,NAT_D:4;
    then N divides K by A3,NAT_D:def 5;
    hence thesis by A4,NAT_D:def 4;
  end;
  hence thesis;
end;

theorem
   n hcf m divides n lcm m
proof
  set K = n hcf m;
  set N = n lcm m;
A1: K divides n & K divides m & for i st i divides n & i divides
  m holds i divides K by NAT_D:def 5;
   n divides N & m divides N & for r st n divides r & m divides
  r holds N divides r by NAT_D:def 4;
  hence thesis by A1,NAT_D:4;
end;

:: from GR_CY_2

 reserve k,m,n,p for Element of NAT;
 reserve i0,i,i1,i2,i3,i4 for Integer;

theorem
   for n,m st 0 < m holds n mod m= n - m * (n div m)
  proof
   let n,m such that A1: m >0;
   reconsider z1=m * (n div m),z2=(n mod m) as Integer;
    n - z1 = z1 + z2 -z1 by A1,NAT_D:2;
   hence n -m * (n div m) = n mod m;
  end;

theorem
   i2 >= 0 implies i1 mod i2 >= 0 by INT_1:84;

theorem
   i2 > 0 implies i1 mod i2 < i2 by INT_1:85;

theorem
   i2 <> 0 implies i1 = (i1 div i2) * i2 + (i1 mod i2) by INT_1:86;

theorem
   for m,n st m>0 or n>0 holds ex i,i1 st i*m + i1*n = m hcf n
  proof
    let m,n;
    assume A1:m>0 or n>0;
    defpred P[Element of NAT] means $1>0 & ex i,i1 st i*m+i1*n=$1;
A2: ex p st P[p]
    proof
      per cases by A1;
      suppose A3:m>0;
         ex i,i1 st i*m+i1*n=m
        proof
          set i1=0;
           1*m + i1*n=m;
          hence thesis;
        end;
        hence thesis by A3;
      end;
      suppose A4:n>0;
         ex i,i1 st i*m+i1*n=n
        proof
          set i=0;
           i*m + 1*n=n;
          hence thesis;
        end;
        hence thesis by A4;
      end;
    end;
     ex p st P[p] & for n st P[n] holds p <= n from NAT_1:sch 5(A2);
    then consider p such that A5:p>0 and A6:ex i,i0 st i*m+i0*n=p and
A7: for k st (k>0 & ex i1,i2 st i1*m+i2*n=k) holds p <= k;
    consider i,i0 such that A8:i*m+i0*n=p by A6;
A9: for k st ex i1,i2 st i1*m+i2*n=k holds p divides k
    proof
      let k;
      given i1,i2 such that A10:i1*m+i2*n=k;
      consider l,s being Nat
      such that A11:k=p*l+s and A12:s<p by A5,NAT_1:17;
      reconsider l,s as Element of NAT by ORDINAL1:def 13;
       s=0
      proof
        assume s<>0; then A13:s>0;
         ex i3,i4 st i3*m+i4*n=s
        proof
           s = k - p*l by A11;
          then s=(i1-i*l)*m+(i2-i0*l)*n by A8,A10;
          hence thesis;
        end;
        hence contradiction by A7,A12,A13;
      end;
      hence thesis by A11,NAT_D:def 3;
    end;
     p= m hcf n
    proof
A14: ex i,i1 st i*m+i1*n=m
     proof
       1*m + 0*n=m;
      hence thesis;
     end;
A15: ex i,i1 st i*m+i1*n=n
     proof
        0*m + 1*n=n;
       hence thesis;
     end;
A16: p divides m by A9,A14;
A17: p divides n by A9,A15;
    for s be Nat st s divides m & s divides n holds s divides p
   proof
     let s be Nat;
     assume that A18:s divides m and A19:s divides n;
     consider l being Nat such that A20:m = s*l by A18,NAT_D:def 3;
     consider r being Nat such that A21:n = s*r by A19,NAT_D:def 3;
     reconsider p'=p,s'=s as Integer;
      ex i4 st p'=s'*i4
     proof
       take i*l+i0*r;
       thus thesis by A8,A20,A21;
     end;
      hence thesis by INT_1:def 9;
     end;
   hence thesis by A16,A17,NAT_D:def 5;
  end;
  hence thesis by A6;
 end;

:: from NAT_LAT

 reserve n,j,k,m,l for Element of NAT;

definition
  func SetPrimes -> Subset of NAT means
:Def6: for n being Nat holds n in it iff n is prime;
  existence
  proof
    defpred P[Nat] means $1 is prime;
    consider X being Subset of NAT such that
A1: for n being Element of NAT holds n in X iff P[n] from SUBSET_1:sch 3;
    take X;
    let n be Nat;
    thus n in X implies n is prime by A1;
A2: n in NAT by ORDINAL1:def 13;
    assume n is prime;
    hence thesis by A1,A2;
  end;
  uniqueness proof
  let A, B be Subset of NAT such that
A3: for n being Nat holds n in A iff n is prime and
A4: for n being Nat holds n in B iff n is prime;
    thus A c= B
    proof
      let x be set;
      assume
A5:   x in A;
      then reconsider x as Element of NAT;
       x is prime by A3,A5;
      hence thesis by A4;
    end;
    let x be set;
    assume
A6: x in B;
    then reconsider x as Element of NAT;
     x is prime by A4,A6;
    hence thesis by A3;
  end;
end;

registration
  cluster prime Element of NAT;
  existence by INT_2:44;
  cluster prime Nat;
  existence by INT_2:44;
end;

definition
  mode Prime is prime Element of NAT;
end;

 reserve
         x for set;

definition let p be Nat;
  func SetPrimenumber p -> Subset of NAT means :Def7:
    for q being Nat holds q in it iff q < p & q is prime;
  existence
  proof defpred P[Nat] means $1 < p & $1 is prime;
    consider X being Subset of NAT such that
A1: for q being Element of NAT holds q in X iff P[q] from SUBSET_1:sch 3;
    take X;
    let q be Nat;
    thus q in X implies q < p & q is prime by A1;
A2: q in NAT by ORDINAL1:def 13;
    assume q < p & q is prime;
    hence thesis by A1,A2;
  end;
  uniqueness proof
  let A, B be Subset of NAT such that
A3: for q being Nat holds q in A iff q < p & q is prime and
A4: for q being Nat holds q in B iff q < p & q is prime;
    thus A c= B
    proof
      let x be set;
      assume
A5:   x in A;
      then reconsider x as Element of NAT;
       x < p & x is prime by A3,A5;
      hence thesis by A4;
    end;
    let x be set;
    assume
A6: x in B;
    then reconsider x as Element of NAT;
     x < p & x is prime by A4,A6;
    hence thesis by A3;
  end;
end;

theorem Th82:
  for p being Nat holds SetPrimenumber p c= SetPrimes
proof
  let p be Nat;
   for x holds x in SetPrimenumber p implies x in SetPrimes
  proof let x;
   assume A1: x in SetPrimenumber p;
   then reconsider x' = x as Element of NAT;
    x' < p & x' is prime by A1,Def7;
   hence thesis by Def6;
 end;
 hence thesis by TARSKI:def 3;
end;

theorem
   for p,q being Nat st p <= q holds
    SetPrimenumber p c= SetPrimenumber q
proof let p,q be Nat;
  assume A1: p <= q;
   for x holds x in SetPrimenumber p implies x in SetPrimenumber q
  proof let x;
   assume A2: x in SetPrimenumber p;
   then reconsider x' = x as Element of NAT;
    x' < p & x' is prime by A2,Def7;
   then x' < q & x' is prime by A1,XXREAL_0:2;
   hence thesis by Def7;
  end;
  hence thesis by TARSKI:def 3;
end;

theorem Th84:
  for p being Nat holds SetPrimenumber p c= Seg p
proof let p be Nat; let x be set;
  assume A1: x in SetPrimenumber p;
  then reconsider q = x as Element of NAT;
  A2: q < p & q is prime by A1,Def7;
  then 1 <= q by INT_2:def 5;
  hence thesis by A2;
end;

theorem Th85:
  for p being Nat holds SetPrimenumber p is finite
proof
  let p be Nat;
   SetPrimenumber p c= Seg p by Th84;
  hence thesis by FINSET_1:13;
end;

registration
  let n be Nat;
  cluster SetPrimenumber n -> finite;
  coherence by Th85;
end;

 reserve p for Prime;

theorem Th86:
  for l being Nat ex p st p is prime & p>l
  proof let l be Nat;
   reconsider l as Element of NAT by ORDINAL1:def 13;
   reconsider two = 2 as Prime by INT_2:44;
    l=0 & (ex p st p is prime & p>l) or (l=1 & (ex p st p is prime & p>l)) or
   (2<=l & (ex p st p is prime & p>l))
   proof
    per cases by NAT_1:27;
     case A1: l=0; take two; thus thesis by A1;
     end;
     case A2: l=1; take two; thus thesis by A2;
     end;
     case A3: 2<=l;
       l<=l! by Th50;
      then 2<=l! by A3,XXREAL_0:2;
      then (2+0) <= (l!+1) by XREAL_1:9;
      then consider j such that
      A4: j is prime and A5: j divides (l!+1) by INT_2:48;
      A6: j<>1 by A4,INT_2:def 5;
A7:   j<>0 by A4,INT_2:def 5;
      reconsider j'=j as Prime by A4;
      take j';
      thus thesis by A5,A6,A7,Th55;
    end;
   end;
   hence thesis;
  end;

theorem
   SetPrimes <> {} by Def6,INT_2:44;

theorem Th88:
  SetPrimenumber 2 = {}
  proof
    assume SetPrimenumber 2<>{};
    then consider x being set such that
A1: x in SetPrimenumber 2 by XBOOLE_0:def 1;
    reconsider x as Nat by A1,ORDINAL1:def 13;
     x<2 & x is prime by A1,Def7;
    then 0 is prime or 1 is prime by NAT_1:27;
    hence contradiction by INT_2:def 5;
  end;

registration
  cluster SetPrimenumber 2 -> empty;
  coherence by Th88;
end;

theorem
   SetPrimenumber n c= NAT;

theorem
   SetPrimenumber m c= Seg m
  proof
    let x be set;
    assume
A1: x in SetPrimenumber m;
    then reconsider x as Element of NAT;
A2: x < m & x is prime by A1,Def7;
    then 1 <= x by INT_2:def 5;
    hence thesis by A2;
  end;

theorem
   SetPrimenumber n is finite;

theorem
   k>=m implies not k in SetPrimenumber m by Def7;

theorem
   for f being Nat holds SetPrimenumber f \/ {f} is finite;

theorem Th94:
  for f being Prime, g being Nat st f < g holds
  SetPrimenumber f \/ {f} c= SetPrimenumber g
  proof
    let f be Prime,g be Nat;
    assume A1: f<g;
    let x be set;
    assume A2: x in SetPrimenumber f\/{f};
    then x in SetPrimenumber f or x in {f} by XBOOLE_0:def 2;
    then reconsider x as Element of NAT by TARSKI:def 1;
    per cases by A2,XBOOLE_0:def 2;
    suppose x in SetPrimenumber f;
      then x < f & x is prime by Def7;
      then x < g & x is prime by A1,XXREAL_0:2;
      hence thesis by Def7;
    end;
    suppose x in {f};
      then x = f by TARSKI:def 1;
      hence thesis by A1,Def7;
    end;
  end;

theorem
   k>=m implies not k in SetPrimenumber m by Def7;

Lm1:
SetPrimenumber n = {k:k<n & k is prime}
proof
  thus SetPrimenumber n c= {k:k<n & k is prime}
  proof
    let x be set;
    assume
A1: x in SetPrimenumber n;
    then reconsider x as Element of NAT;
     x < n & x is prime by A1,Def7;
    hence thesis;
  end;
  let x be set;
  assume x in {k:k<n & k is prime};
  then ex k st x=k & k<n & k is prime;
  hence thesis by Def7;
end;

definition let n be Nat;
  func primenumber n -> Prime means :Def8:
  n = card SetPrimenumber it;
existence
proof
A1: n in NAT by ORDINAL1:def 13;
  defpred P1[Nat] means
  ex m st $1=card SetPrimenumber m & m is Prime;
    SetPrimenumber 2 = {k:k<2 & k is prime} by Lm1;
   then A2: P1[0] by CARD_1:78,INT_2:44;
A3: for n st P1[n] holds P1[n+1]
    proof let n;
     given m such that
A4:  n=card SetPrimenumber m & m is Prime;
     defpred P[Nat] means $1 is prime & $1>m;
     A5: ex k st P[k]
     proof
        ex p st p is prime & p>m by Th86;
       then consider k being Nat such that
A6:    P[k];
        k in NAT by ORDINAL1:def 13;
       hence thesis by A6;
     end;
     consider k such that
A7:  P[k] & for l st P[l] holds k <= l from NAT_1:sch 5(A5);
     take k;
A8:  SetPrimenumber k=SetPrimenumber m\/{m}
     proof
       thus SetPrimenumber k c= SetPrimenumber m\/{m}
       proof let s be set;
         assume
A9:      s in SetPrimenumber k;
         then reconsider s as Element of NAT;
A10:      s<k & s is prime by A9,Def7;
then A11:      s<=m by A7;
         per cases by A11,REAL_1:def 5;
         suppose s=m;
           then s in {m} by TARSKI:def 1;
           hence thesis by XBOOLE_0:def 2;
         end;
         suppose s<m;
           then A12: s in SetPrimenumber m by A10,Def7;
            SetPrimenumber m c= SetPrimenumber m\/{m} by XBOOLE_1:7;
           hence thesis by A12;
         end;
       end;
       thus SetPrimenumber m\/{m}c=SetPrimenumber k by A4,A7,Th94;
     end;
      not m in SetPrimenumber m by Def7;
     hence n + 1 = card SetPrimenumber k by A4,A8,CARD_2:54;
     thus k is Prime by A7;
   end;
    for n holds P1[n] from NAT_1:sch 1(A2,A3);
   then ex m st n=card SetPrimenumber m & m is Prime by A1;
   hence thesis;
end;
uniqueness
 proof let f,g be Prime;
   assume that
A13: n = card SetPrimenumber f and
A14: n = card SetPrimenumber g;
  assume
A15: f<>g;
   f<g & n+1 <= n or g<f & n+1 <= n
  proof
   per cases by A15,XXREAL_0:1;
    case f<g;
    then A16: SetPrimenumber f\/{f}c=SetPrimenumber g by Th94;
A17: not f in SetPrimenumber f by Def7;
      card (SetPrimenumber f\/{f}) <= card SetPrimenumber g by A16,CARD_1:80;
     hence thesis by A13,A14,A17,CARD_2:54;
    end;
    case g<f;
    then A18: SetPrimenumber g\/{g}c=SetPrimenumber f by Th94;
A19: not g in SetPrimenumber g by Def7;
     card (SetPrimenumber g\/{g}) <= card SetPrimenumber f by A18,CARD_1:80;
    hence thesis by A13,A14,A19,CARD_2:54;
   end;
   end;
  hence contradiction by NAT_1:13;
 end;
end;

theorem
   SetPrimenumber n = {k: k<n & k is prime} by Lm1;

theorem Th97: :: Euclid
  SetPrimes is infinite
proof
  assume A1: SetPrimes is finite;
  then reconsider SP = SetPrimes as finite set;
  consider n such that A2: SetPrimes,Seg n are_equipotent by A1,FINSEQ_1:77;
  set p=primenumber (n+1);
   Card SetPrimes = Card Seg n by A2,CARD_1:21;
  then Card SetPrimes = Card n by FINSEQ_1:76;
  then A3: card SP = n by CARD_1:def 11;
A4: SetPrimenumber p c= SP by Th82;
  set SPp = SetPrimenumber p;
A5: card SPp <= card SP by A4,CARD_1:80;
   n+1 = card SetPrimenumber p by Def8;
  hence contradiction by A3,A5,NAT_1:13;
end;

registration
  cluster SetPrimes -> non empty infinite;
  coherence by Th97;
end;

 reserve m,i for Nat;

theorem :: divisibility
   for i st i is prime holds
    for m,n being Nat holds i divides m * n implies i divides m or i divides n
proof
  let i;
  assume A1: i is prime;
   given m,n being Nat such that
A2: not (i divides m * n implies (i divides m or i divides n));
   defpred P[Nat] means $1 is prime &
   ex m,n being Nat st $1 divides m * n & not $1 divides m & not $1 divides n;
A3: ex i be Element of NAT st P[i]
    proof
      consider i being Nat such that
A4:   P[i] by A1,A2;
      reconsider i as Element of NAT by ORDINAL1:def 13;
      take i;
      thus thesis by A4;
    end;
   consider p' being Element of NAT such that
A5: P[p'] and
A6: for p1 being Element of NAT st P[p1] holds p' <= p1 from NAT_1:sch 5(A3);
    defpred Q[Nat] means
    ex n being Nat st p' divides $1 * n & not p' divides $1 & not p' divides n;
A7: ex i be Element of NAT st Q[i]
    proof
      consider i being Nat such that
A8:   Q[i] by A5;
      reconsider i as Element of NAT by ORDINAL1:def 13;
      take i;
      thus thesis by A8;
    end;
    consider m be Element of NAT such that
A9: Q[m] and
A10: for m1 being Element of NAT st Q[m1] holds m <= m1 from NAT_1:sch 5(A7);
     m <> 0 by A9;
    then A11: m > 0;
    consider n being Nat such that
A12: p' divides m * n & (not p' divides m) & (not p' divides n) by A9;
A13: m < p'
     proof
       assume A14: m >= p';
       set m1 = m mod p';
A15:   p' > 0 by A5,INT_2:def 5;
       then m1 < p' by NAT_D:1;
       then A16: m1 < m by A14,XXREAL_0:2;
A17:   p' divides p' * (m div p') by NAT_D:def 3;
A18:   m = p'* (m div p') + m1 by A15,NAT_D:2;
A19:   p' divides m1 * n
       proof
A20:     p' divides p' * ((m div p') * n) by NAT_D:def 3;
          m = p'* (m div p') + m1 by A15,NAT_D:2;
         then m * n = p' * (m div p') * n + m1 * n;
         hence thesis by A12,A20,NAT_D:10;
       end;
        not p' divides m1 by A12,A17,A18,NAT_D:8;
       hence contradiction by A10,A12,A16,A19;
     end;
A21: m < 2
     proof
      assume m >= 2;
      then consider p1 being Element of NAT such that
A22:  p1 is prime & p1 divides m by INT_2:48;
A23:  p1 > 1 by A22,INT_2:def 5;
A24:  p1 <> 0 by A22,INT_2:def 5;
       p1 <= m by A11,A22,NAT_D:7;
then A25:  not p' <= p1 by A13,XXREAL_0:2;
A26:  not p1 divides p'
      proof
        assume p1 divides p';
        then p1 = 1 or p1 = p' by A5,INT_2:def 5;
        hence contradiction by A11,A13,A22,INT_2:def 5,NAT_D:7;
      end;
      consider k being Nat such that
A27:  m * n = p' * k by A12,NAT_D:def 3;
       p1 divides p' * k by A22,A27,NAT_D:9;
      then p1 divides k by A6,A22,A25,A26;
      then consider k1 being Nat such that
      A28: k = p1 * k1 by NAT_D:def 3;
      consider m1 being Nat such that
      A29: m = p1 * m1 by A22,NAT_D:def 3;
A30:   m1 in NAT by ORDINAL1:def 13;
A31:  m1 > 0
      proof
         m1 <> 0 by A9,A29;
        hence thesis;
      end;
 A32: m1 < m
      proof
         m1 divides m by A29,NAT_D:def 3;
        then A33: m1 <= m by A11,NAT_D:7;
         m1 <> m
        proof
          assume m1 = m;
          then m = 1 * m1;
          hence contradiction by A23,A29,A31,XREAL_1:70;
        end;
        hence thesis by A33,REAL_1:def 5;
      end;
 A34: p' divides m1 * n
      proof
         (p' * k1) * p1 = p1 *(m1 * n) by A27,A28,A29;
        then m1 * n = p' * k1 by A24,XCMPLX_1:5;
        hence thesis by NAT_D:def 3;
      end;
       not p' divides m1 by A12,A29,NAT_D:9;
      hence contradiction by A30,A10,A12,A32,A34;
   end;
    m >= 2
   proof
     assume m < 2;
     then A35: m <= 1 + 1 & m <> 2;
      m >= 0 + 1 by A11,NAT_1:13;
     then m = 1 by A35,NAT_1:9;
     hence contradiction by A12;
   end;
   hence thesis by A21;
 end;

canceled;

theorem
   for x being real number holds x |^ 2 = x * x & x^2 = x |^ 2
  proof
    let x be real number;
    thus x * x = (x |^ 1) * x by Th10 .= x |^ (1+1) by Th11 .= x |^ 2;
    hence x|^2 = x^2;
  end;

:: from SCMFSA9A, 2005.11.16, A.T

theorem
  for m,n being natural number holds
 m qua Integer div n = m div n &
 m qua Integer mod n = m mod n;

:: from HEINE, 2006.01.07, A.T

 reserve k for Nat,
         x for real number;

theorem
  x > 0 implies x|^k > 0
 proof
  defpred P[Nat] means for x st x > 0 holds x|^$1 > 0;
  A1: P[0] by FINSEQ_2:72,RVSUM_1:124;
  A2: for k holds P[k] implies P[k+1]
    proof
     let k such that A3: for x st x > 0 holds x|^k > 0;
     let x; assume x > 0;
     then x > 0 & x|^k > 0 & x|^(k+1) = x * x|^k by A3,Th11;
     hence thesis by REAL_2:122;
    end;
     for k holds P[k] from NAT_1:sch 2(A1,A2);
  hence thesis;
 end;

:: missing, 2006.07.17, A.T.

theorem
  n > 0 implies  0 |^ n = 0
 proof
  assume n>0;
   then n>=1+0 by NAT_1:13;
  hence thesis by Th16;
 end;

:: from CARD_4 and WSIERP_1, 2007.02.07, AK

definition let m,n be Element of NAT;
  redefine func m|^n -> Element of NAT;
  coherence by ORDINAL1:def 13;
end;

:: from HEINE, 2007.07.27, A.T.

  defpred P[Element of NAT] means 2|^$1 >= $1 + 1;
  Lm1: P[0] by Th9;
  Lm2: for n st P[n] holds P[n+1]
      proof
       let n; assume 2|^n >= n+1;
        then 2|^n - n >= 1 & 2 >= 0 by XREAL_1:21;
        then A1: 2*(2|^n - n) >= 2*1 by XREAL_1:66;
              2|^(n+1) - (n + 1 + 1) = 2*2|^n - (2*n - n + 2) by Th11
             .= 2*(2|^n - n) + (n - 2);
         then 2|^(n+1) - (n + 1 + 1) >= 2 + (n - 2) by A1,XREAL_1:8;
         then 2|^(n+1) - (n + 1 + 1) >= n & n >= 0;
         then 2|^(n+1) >= 0 + (n + 1 + 1) by XREAL_1:21;
        hence 2|^(n+1) >= n + 1 + 1;
      end;

theorem Th104:
  2|^n >= n + 1
   proof for n holds P[n] from NAT_1:sch 1(Lm1,Lm2); hence thesis; end;

theorem
  2|^n > n
 proof
  2|^n >= n + 1 & n+1 > n by Th104,NAT_1:13;
  hence thesis by XXREAL_0:2;
 end;



