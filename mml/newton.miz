:: Factorial and Newton coeffitients
::  by Rafa{\l} Kwiatek
::
:: Received July 27, 1990
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies ORDINAL2, ARYTM, FINSEQ_1, ARYTM_3, ARYTM_1, RELAT_1, FUNCT_1,
      BOOLE, FINSEQ_2, GROUP_1, QC_LANG1, RLVECT_1, NEWTON, FINSEQ_4, CARD_3,
      NAT_1, INT_1, ABSVALUE, FILTER_0, NAT_LAT, FINSET_1, CARD_1, TARSKI;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, ORDINAL2, NUMBERS, XCMPLX_0,
      XREAL_0, CARD_1, REAL_1, FUNCT_1, FINSET_1, FINSEQ_1, FINSEQ_2, FINSEQ_4,
      INT_1, INT_2, NAT_1, RVSUM_1;
 constructors FINSEQOP, INT_1, REAL_1, NAT_1, RVSUM_1, FINSEQ_4, SEQ_1,
      MEMBERED, XBOOLE_0, BINOP_2, ABSVALUE, INT_2, WELLORD2;
 registrations RELSET_1, FINSEQ_2, INT_1, XREAL_0, NAT_1, MEMBERED, ORDINAL2,
      FINSEQ_1, FINSET_1;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, FINSEQ_1;
 theorems TARSKI, AXIOMS, REAL_1, NAT_1, RVSUM_1, FINSEQ_1, FINSEQ_2, FINSEQ_3,
      FINSEQ_4, FUNCT_1, INT_1, XREAL_0, ORDINAL2, XBOOLE_0, XCMPLX_1, REAL_2,
      SQUARE_1, ABSVALUE, INT_2, FINSET_1, CQC_THE1, CARD_2, CARD_1, XBOOLE_1;
 schemes NAT_1, FINSEQ_1, INT_2, SETWISEO, SUBSET_1;

begin

  reserve i,k,n,m,l for Nat;
  reserve s,t,r for natural number;
  reserve a,b,x,y for real number;
  reserve F,G,H for FinSequence of REAL;

canceled 2;

theorem
Th3: for F,G being FinSequence st
     len F = len G & (for i st i in dom F holds F.i = G.i) holds F = G
  proof
   let F,G be FinSequence;
   assume A1: len F = len G & for i st i in dom F holds F.i = G.i;
   A2: dom F = Seg len F by FINSEQ_1:def 3;
      dom G = Seg len F by A1,FINSEQ_1:def 3;
   hence thesis by A1,A2,FINSEQ_1:17;
  end;

canceled;

theorem
Th5: for n st n>=1 holds Seg n = {1} \/ {k: 1<k & k<n} \/ {n}
  proof
   let n;
   assume A1: n>=1;
   A2: Seg n c= {1} \/ {k: 1<k & k<n} \/ {n}
    proof let d be set;
     assume A3: d in Seg n;
         now per cases by A1,REAL_1:def 5;
      suppose A4: n>1;
      reconsider l = d as Nat by A3;
         1 <= l & l <= n by A3,FINSEQ_1:3;
      then 1 = l or (1<l & l<n) or l = n or 1 = n by REAL_1:def 5;
      then d in {1} or d in {k: 1<k & k<n} or d in {n} by A4,TARSKI:def 1;
      then d in {1} \/ {k: 1<k & k<n} or d in {n} by XBOOLE_0:def 2;
      hence thesis by XBOOLE_0:def 2;
      end;
      suppose n = 1;
      hence thesis by A3,FINSEQ_1:4,XBOOLE_0:def 2;
      end;
      end;
     hence d in {1} \/ {k: 1<k & k<n} \/ {n};
    end;
      {1} \/ {k: 1<k & k<n} \/ {n} c= Seg n
    proof
        for d be set holds d in {1} \/ {k: 1<k & k<n} \/ {n} implies d in Seg n
      proof
     A5: for d be set holds d in {1} implies d in Seg n
      proof
       let d be set; assume d in {1};
       then d = 1 by TARSKI:def 1;
       hence d in Seg n by A1,FINSEQ_1:3;
      end;
     A6: for d be set holds d in {k: 1<k & k<n} implies d in Seg n
      proof
       let d be set;
       assume d in {k: 1<k & k<n};
        then consider k such that A7: d = k & 1<k & k<n;
        thus d in Seg n by A7,FINSEQ_1:3;
      end;
     A8: for d be set holds d in {n} implies d in Seg n
      proof
       let d be set; assume d in {n};
       then A9: d = n by TARSKI:def 1;
          n<>0 by A1;
       hence d in Seg n by A9,FINSEQ_1:5;
      end;
       let d be set;
       assume d in ({1} \/ {k: 1<k & k<n} \/ {n});
       then d in ({1} \/ {k: 1<k & k<n}) or d in {n} by XBOOLE_0:def 2;
       then d in {1} or d in {k: 1<k & k<n} or d in {n} by XBOOLE_0:def 2;
       hence thesis by A5,A6,A8;
      end;
     hence thesis by TARSKI:def 3;
    end;
   hence thesis by A2,XBOOLE_0:def 10;
  end;

theorem
Th6: for F holds len (a*F) = len F
 proof
  let F;
  reconsider G = F as Element of (len F)-tuples_on REAL by FINSEQ_2:110;
     len (a*G) = len G by FINSEQ_2:109;
  hence thesis;
 end;

theorem
Th7: n in dom G iff n in dom (a*G)
 proof
      len (a*G) = len G by Th6; hence thesis by FINSEQ_3:31;
 end;

::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
::    x |^ n  Function                                                      ::
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

 definition
 let i be natural number; let x be real number;
 redefine func i |-> x -> FinSequence of REAL;
 coherence
  proof
   let z be set;
   assume z in rng (i |-> x);
   then consider y be set such that A1: y in dom (i |-> x) and
    A2: z = (i |-> x).y by FUNCT_1:def 5;
   A3: y in Seg i by A1,FINSEQ_2:68;
   reconsider y as Nat by A1;
   reconsider r1 = (i |-> x).y as real number by A3,FINSEQ_2:70;
      r1 = z by A2;
   hence z in REAL by XREAL_0:def 1;
  end;
end;

definition
  let x be real number; let n be natural number;
  func x|^n equals :Def1:  Product(n |-> x);
  coherence;
end;

registration
  let x be real number; let n be natural number;
  cluster x|^n -> real;
coherence
 proof
    x|^n = Product(n |-> x) by Def1;
   hence thesis;
 end;
end;

definition
  let x be Real; let n be natural number;
  redefine func x|^n -> Real;
  coherence by XREAL_0:def 1;
end;

canceled;

theorem
Th9: x|^0 = 1
 proof
  0 |-> x = <*> REAL by FINSEQ_2:72;
  hence thesis by Def1,RVSUM_1:124;
 end;

theorem
Th10: x|^1 = x
 proof
  reconsider x as Real by XREAL_0:def 1;
     x|^1 = Product(1 |-> x) by Def1 .= Product(<*x*>) by FINSEQ_2:73
    .= x by RVSUM_1:125;
  hence thesis;
 end;

theorem
Th11: x|^(s+1) = x|^s*x
 proof
   defpred P[natural number] means x|^($1+1) = x|^$1*x;
  A1: P[0] proof
     reconsider x as Real by XREAL_0:def 1;
         x|^(0+1) = Product(1 |-> x) by Def1 .= Product(<*x*>)
       by FINSEQ_2:73 .= 1*x by RVSUM_1:125;
      hence thesis by Th9;
   end;
  A2: for s st P[s] holds P[s+1] proof let s;
    assume x|^(s+1) = x|^s*x;
     reconsider x as Real by XREAL_0:def 1;
     A3: s+1 is Nat by ORDINAL2:def 21;
          x|^(s+1+1) = Product((s+1+1) |-> x) by Def1
         .= Product(((s+1) |-> x) ^ <*x*>)
        by A3,FINSEQ_2:74 .= Product((s+1) |-> x)*x by RVSUM_1:126;
       hence thesis by Def1;
   end;
   for s holds P[s] from NAT_1:sch 2(A1,A2);
  hence thesis;
 end;

registration
  let x, n be natural number;
  cluster x|^n -> natural;
coherence
 proof
   defpred P[natural number] means x|^$1 is natural;
A1: P[0] by Th9;
A2: for a being natural number st P[a] holds P[a+1]
  proof
    let a be natural number;
    assume P[a];
    then reconsider b = x|^a as natural number;
     x|^(a+1) = b*x by Th11;
    hence thesis;
  end;
    for a being natural number holds P[a]
     from NAT_1:sch 2(A1,A2);
   hence thesis;
 end;
end;

theorem
  (x*y)|^s = x|^s * y|^s
 proof
  reconsider x, y as Real by XREAL_0:def 1;
A1: s is Nat by ORDINAL2:def 21;
     x|^s*y|^s = Product(s |-> x)*y|^s by Def1
    .= (Product(s |-> x))*(Product(s |-> y))
   by Def1 .= Product(s |-> (x*y)) by A1,RVSUM_1:136;
  hence thesis by Def1;
 end;

theorem
    x|^(s+t) = x|^s * x|^t
 proof
  reconsider x as Real by XREAL_0:def 1;
  reconsider s,t as Nat by ORDINAL2:def 21;
     x|^s*x|^t = (Product(s |->x))*x|^t by Def1
   .= (Product(s |-> x))*(Product(t |-> x))
   by Def1 .= Product((s+t) |-> x) by RVSUM_1:134;
  hence thesis by Def1;
 end;

theorem
    (x|^s)|^t = x|^(s*t)
 proof
  reconsider x as Real by XREAL_0:def 1;
  reconsider s,t as Nat by ORDINAL2:def 21;
     x|^(s*t) = Product((s*t) |-> x) by Def1 .= Product(t |-> Product(s |-> x))
    by RVSUM_1:135
  .= Product(t |-> x|^s) by Def1;
 hence thesis by Def1;
 end;

theorem
Th15: 1|^s = 1
 proof
   defpred P[natural number] means 1|^$1 = 1;
  A1: P[0] by Th9;
  A2: now
    let s;
    assume P[s];
    then 1|^(s+1) = 1 * 1 by Th11;
    hence P[s+1];
   end;
    for s holds P[s] from NAT_1:sch 2(A1,A2);
   hence thesis;
 end;

theorem
   s >= 1 implies 0|^s = 0
 proof
  A1: s is Nat by ORDINAL2:def 21;
  defpred P[Nat] means 0|^$1 = 0;
  A2: P[1] by Th10;
  A3: now let n; assume n>=1 & P[n];  0|^(n+1) = 0|^n * 0 by Th11 .= 0;
       hence P[n+1];
      end;
     for n st n >=1 holds P[n] from INT_2:sch 1(A2,A3);
  hence thesis by A1;
 end;

::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
::     n!  Function                                                         ::
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

definition let n be natural number;
 redefine func idseq n -> FinSequence of REAL;
 coherence
  proof
   let x be set;
   assume x in rng idseq n;
   then consider y be set such that
A1: y in dom idseq n and
A2: x = idseq n.y by FUNCT_1:def 5;
A3: n is Nat by ORDINAL2:def 21;
   A4: y in Seg n by A1,FINSEQ_2:54;
   reconsider y as Nat by A1;
      (idseq n).y = y by A3,A4,FINSEQ_2:56;
   hence x in REAL by A2;
  end;
end;

definition
  let n be natural number;
  func n! equals :Def2:  Product(idseq n);
  coherence;
end;

registration
  let n be natural number;
  cluster n! -> real;
  coherence
  proof
       n! = Product(idseq n) by Def2;
    hence thesis;
  end;
end;

definition
  let n be natural number;
  redefine func n! -> Real;
  coherence by XREAL_0:def 1;
end;

canceled;

theorem
Th18: 0! = 1 by Def2,FINSEQ_2:58,RVSUM_1:124;

theorem Th19:
    1! = 1
 proof
     1! = Product<*1 qua Real*> by Def2,FINSEQ_2:59 .= 1 by RVSUM_1:125;
  hence thesis;
 end;

theorem
    2! = 2
 proof
     2! = Product<*1 qua Real,2*> by Def2,FINSEQ_2:61 .= 1*2 by RVSUM_1:129;
  hence thesis;
 end;

theorem
Th21: for s holds (s+1)! = (s!) * (s+1)
 proof
  let s;
  reconsider s as Nat by ORDINAL2:def 21;
  reconsider r1 = s+1 as Real;
       (s+1)! = (s!) * (s+1)
    proof
         idseq (s+1) = idseq s ^ <* r1 *> by FINSEQ_2:60;
     then Product(idseq (s+1)) = Product(idseq s) * r1 by RVSUM_1:126
                        .= (s!) * r1 by Def2;
     hence thesis by Def2;
    end;
    hence thesis;
 end;

theorem Th22:
    for s holds s! is Nat
 proof
   defpred P[natural number] means $1! is Nat;
  A1: P[0] by Def2,FINSEQ_2:58,RVSUM_1:124;
  A2: now let s; assume P[s]; then reconsider k=s! as Nat;
        (s+1)! = (s+1) * k by Th21;
    hence P[s+1] by ORDINAL2:def 21;
   end;
  thus for s holds P[s] from NAT_1:sch 2(A1,A2);
 end;

theorem
Th23: for s holds s!>0
 proof
   defpred P[natural number] means $1!>0;
  A1: P[0] by Th18;
  A2: now let s;  A3: s+1>0 by NAT_1:18; assume P[s];
    then (s+1) * (s!)>(s+1) * 0 by A3,REAL_1:70;
    hence P[s+1] by Th21;
   end;
  thus for s holds P[s] from NAT_1:sch 2(A1,A2);
 end;

canceled;

theorem
Th25: for s,t holds (s!) * (t!)<>0
 proof
  let s,t;
     s!<>0 & t!<>0 by Th23;
  hence thesis by XCMPLX_1:6;
 end;

::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
::     n choose k  Function                                                 ::
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

definition
 let k,n be natural number;
 func n choose k means :Def3:
    for l be natural number st l = n-k holds
     it = (n!)/((k!) * (l!)) if n >= k
  otherwise it = 0;
 consistency;
 existence
  proof
   thus n >= k implies ex r1 be set st
    for l being natural number st l = n-k holds r1 = (n!)/((k!) * (l!))
    proof
     assume n >= k;
     then reconsider m = n-k as Nat by INT_1:18;
     take (n!)/((k!)*(m!));
     thus thesis;
    end;
   thus thesis;
  end;
 uniqueness
  proof let r1,r2 be set;
   thus n>=k &
    (for l being natural number st l = n-k holds r1 = (n!)/((k!) * (l!))) &
    (for l being natural number st l = n-k holds r2 = (n!)/((k!) * (l!)))
     implies r1 = r2
    proof
     assume A1: n>=k;
     assume A2: (for l being natural number st l = n-k holds
      r1 = (n!)/((k!) * (l!)))
      & (for l being natural number st l = n-k holds
       r2 = (n!)/((k!) * (l!)));

     reconsider m = n-k as Nat by A1,INT_1:18;
        r1 = (n!)/((k!) * (m!)) & r2 = (n!)/((k!) * (m!)) by A2;
     hence thesis;
    end;
   thus thesis;
  end;
end;

registration
 let k,n be natural number;
 cluster n choose k -> real;
coherence
  proof
    per cases;
    suppose
A1:   n >= k;
    then reconsider l = n-k as Nat by INT_1:18;
       n choose k = (n!)/((k!) * (l!)) by A1,Def3;
    hence thesis;
    end;
    suppose n < k; hence thesis by Def3;
  end;
  end;
end;

definition
  let k,n be natural number;
  redefine func n choose k -> Real;
  coherence by XREAL_0:def 1;
end;

canceled;

theorem
Th27: 0 choose 0 = 1
 proof
   0-0 = 0;
   then 0 choose 0 = 1/(1 * 1) by Def3,Th18 .= 1;
   hence thesis;
 end;

canceled;

theorem
Th29: s choose 0 = 1
 proof
  A1: s >= 0 by NAT_1:18;
  reconsider m = s-0 as Nat by ORDINAL2:def 21;
  A2: s!<>0 by Th23;
     m = s;
  then (s choose 0) = (s!)/(1 * (s!)) by A1,Def3,Th18
   .= 1 by A2,XCMPLX_1:60;
  hence thesis;
 end;

theorem
Th30: for s,t st s>=t holds
       (for r st r = s-t holds (s choose t) = (s choose r))
 proof
  let s,t;
  assume A1: s>=t;
  let r;
  assume A2: r = s-t;
  A3:s>=r
   proof
    t>=0 by NAT_1:18;
    then t-s>=0-s by REAL_1:49;
    then -(-s)>=-(t-s) by REAL_1:50;
    hence thesis by A2;
   end;
   t = s-r by A2;
  then s choose r = (s!)/((r!) * (t!)) by A3,Def3;
  hence thesis by A1,A2,Def3;
 end;

theorem
Th31: for s holds s choose s = 1
 proof
  let s;
  reconsider m = s-s as Nat by INT_1:18;
     m = 0;
  then s choose s = s choose 0 by Th30;
  hence thesis by Th29;
 end;

theorem Th32:
  (t+1) choose (s+1) = (t choose (s+1)) + (t choose s)
 proof
  per cases by REAL_1:def 5;
  suppose A1: s<t;
  thus ((t+1) choose (s+1)) = (t choose (s+1)) + (t choose s)
   proof
    reconsider m1 = t-s as Nat by A1,INT_1:18;
    A2: s+1<=t by A1,NAT_1:38;
    then reconsider m2 = t-(s+1) as Nat by INT_1:18;
    A3: (t choose (s+1)) = (t!)/(((s+1)!) * (m2!)) by A2,Def3;
    A4: ((s+1)!) * (m2!)<>0 & ((s!) * (m1!))<>0 by Th25;
    A5: s!<>0 by Th23;
    A6: m2!<>0 by Th23;
    A7: (t choose (s+1)) + (t choose s) =
     (t!)/(((s+1)!) * (m2!)) + (t!)/((s!) * (m1!)) by A1,A3,Def3 .=
     ((t!)*((s!)*(m1!))+(t!)*(((s+1)!)*(m2!)))/((((s+1)!)*(m2!))*((s!)*(m1!)))
      by A4,XCMPLX_1:117 .=
     (t!*((s!)*(m1!))+(t!)*(s!*(s+1)*(m2!)))/((((s+1)!)*(m2!))*((s!)*(m1!)))
      by Th21 .=
     (s!*((t!)*((m1!)+(s+1)*(m2!))))/(s!*((m1!)*((s+1)!*(m2!))))
 .=(t!*(((m2+1)!)+(s+1)*(m2!)))/(m1!*((s+1)!*(m2!))) by A5,XCMPLX_1:92
 .=(t!*((m2!)*(m2+1)+(m2!)*(s+1)))/(m1!*((s+1)!*(m2!))) by Th21 .=
(m2!*((t!)*((m2+1)+(s+1))))/(m2!*(((s+1)!)*(m1!))) .=
     ((t!)*(t-(s-(s+1))))/(((s+1)!)*(m1!)) by A6,XCMPLX_1:92 .=
     ((t+1)!)/(((s+1)!)*(m1!)) by Th21;
    A8: s+1<=t+1 by A1,AXIOMS:24;
     then reconsider m = (t+1)-(s+1) as Nat by INT_1:18;
     m = m1;
     hence thesis by A7,A8,Def3;
   end;
   end;
    suppose A1:s=t;
      then s<t+1 by NAT_1:38;
      then (t+1) choose (s+1)=1 & t choose s =1 & t choose (s+1)=0
      by A1,Th31,Def3;
      hence thesis;
    end;
    suppose A1:s>t;
      then s+1>t+0 & s+1 >t+1 by REAL_1:67;
      then (t+1) choose (s+1)=0 & t choose s =0 & t choose (s+1)=0
        by A1,Def3;
      hence thesis;
    end;
 end;

theorem
Th33: for s st s >= 1 holds s choose 1 = s
 proof
  let s;
  A1: s is Nat by ORDINAL2:def 21;
      defpred P[Nat] means $1 choose 1 = $1;
  A2: P[1] by Th31;
  A3: now let n; assume A4: n>=1 & P[n];
       (n+1) choose 1 = (n+1) choose (0+1)
                    .= n + n choose 0 by A4,Th32 .= n+1 by Th29;
    hence P[n+1];
   end;
   for n st n >= 1 holds P[n] from INT_2:sch 1(A2,A3);
  hence thesis by A1;
 end;

theorem
  for s,t st s>=1 & t = s-1 holds s choose t = s
 proof
  let s,t;
  assume A1: s>=1 & t = s-1;
   then s choose t = s choose 1 by Th30;
  hence thesis by A1,Th33;
 end;

theorem
  s choose r is Nat
 proof
   defpred P[natural number] means for r holds ($1 choose r) is Nat;
  A1: P[0] proof let r;
    r = 0 or r > 0 by NAT_1:19;
    hence (0 choose r) is Nat by Def3,Th27;
   end;
  A2: for s st P[s] holds P[s+1] proof let s;
    assume A3: P[s];
A4:    for r st r <> 0 holds ((s+1) choose r) is Nat
     proof
      let r;
      assume A5: r <> 0;
       A7: ((s+1) choose r) is Nat
        proof
         consider t being Nat such that A9: r = t+1 by A5,NAT_1:22;
         reconsider m1 = (s choose t), m2 = (s choose (t+1)) as Nat by A3;
         m1 + m2 =(s choose t) + (s choose (t+1));
         hence thesis by A9,Th32;
        end;
       hence thesis;
      end;
    let r;
    r = 0 or r<>0;
    hence thesis by A4,Th29;
   end;
  for s holds P[s] from NAT_1:sch 2(A1,A2);
  hence thesis;
 end;

theorem
    for m,F st m <> 0 & len F = m & (for i,l st i in dom F & l = n+i-1
      holds F.i = l choose n) holds Sum F = (n+m) choose (n+1)
 proof
   defpred P[Nat] means
   for F st $1 <> 0 & len F = $1 & (for i,l st i in dom F & l =
      n+i-1 holds F.i = l choose n) holds Sum F = (n+$1) choose (n+1);
  A1:  P[0];
  A2:  for m st P[m] holds P[m+1] proof let m;
   assume
      A3:  P[m];
   let F;
   assume
      A4:  m+1 <> 0 & len F = m+1 & (for i,l st i in dom F &
      l = n+i-1 holds F.i = l choose n);
   then consider G be FinSequence of REAL,
    x being Real such that A5: F = G^<*x*> by FINSEQ_2:22;
A6:   m+1 = len G +1 by A4,A5,FINSEQ_2:19;
   then A7: len G = m by XCMPLX_1:2;
      now per cases;
    suppose A8: m = 0;
     then A9: dom F = Seg 1 by A4,FINSEQ_1:def 3;
      then A10: 1 in dom F by FINSEQ_1:5;
         n = n+1-1;
      then F.1 = n choose n by A4,A10;
     hence Sum F = Sum<*n choose n*> by A9,FINSEQ_1:def 8
        .= n choose n by RVSUM_1:103
        .= 1 by Th31
        .= (n+(m+1)) choose (n+1) by A8,Th31;
    end;
    suppose A11:m <> 0;
A12:    for i,l st i in dom G & l = n+i-1 holds G.i = l choose n
    proof
     let i,l;
     assume A13: i in dom G & l = n+i-1;
     then A14: G.i = F.i by A5,FINSEQ_1:def 7;
        dom G c= dom F by A5,FINSEQ_1:39;
     hence G.i = l choose n by A4,A13,A14;
    end;
      dom F = Seg (m+1) by A4,FINSEQ_1:def 3;
   then A16: m+1 in dom F by FINSEQ_1:6;
       n+m = n +(m+1)-1;
   then A17: (n+m) choose n = (G^<*x*>).(len G +1) by A4,A5,A6,A16 .= x by
FINSEQ_1:59;
   thus Sum F = Sum G + x by A5,RVSUM_1:104
   .= (n+m) choose (n+1) + (n+m) choose n
    by A3,A7,A11,A12,A17 .= (n+m+1) choose (n+1) by Th32
 .= (n+(m+1)) choose (n+1);
   end;
   end;
  hence thesis;
  end;
  thus for m holds P[m] from NAT_1:sch 1(A1,A2);
 end;

definition
let a,b be real number;
let n be natural number;
func (a,b) In_Power n -> FinSequence of REAL means
 :Def4:  len it = n+1 &
   (for i,l,m being natural number st i in dom it & m = i - 1 & l = n-m holds
   it.i = (n choose m)* a|^l * b|^m);
existence
 proof
  reconsider n as Nat by ORDINAL2:def 21;
  defpred P[Nat,set] means
   for l,m be natural number st m = $1 - 1 & l = n-m holds
    $2 = (n choose m)* a|^l * b|^m;
 A1: for k for y1,y2 being set st k in Seg(n+1) & P[k,y1] & P[k,y2] holds y1=y2
  proof
   let k;
   let y1,y2 be set;
   assume A2: k in Seg (n+1) & P[k,y1] & P[k,y2];
   then A3: k >= 1 & k <= n+1 by FINSEQ_1:3;
   then reconsider m1 = k-1 as Nat by INT_1:18;
      k-1<=n+1-1 by A3,REAL_1:49;
   then reconsider l1 = n-m1 as Nat by INT_1:18;
       y1 = (n choose m1)* a|^l1 * b|^m1 by A2;
   hence thesis by A2;
  end;
 A4: for k st k in Seg(n+1) ex x being set st P[k,x]
  proof
   let k;
   assume k in Seg (n+1);
   then A5: k >= 1 & k <= n+1 by FINSEQ_1:3;
   then reconsider m1 = k-1 as Nat by INT_1:18;
      k-1<=n+1-1 by A5,REAL_1:49;
   then reconsider l1 = n-m1 as Nat by INT_1:18;
   reconsider z = (n choose m1)*a|^l1*b|^m1 as set;
   take z;
   thus thesis;
  end;
  consider p being FinSequence such that
   A6: dom p = Seg(n+1) and
   A7: for k st k in Seg(n+1) holds P[k,p.k] from FINSEQ_1:sch 1(A1,A4);
       rng p c= REAL
     proof
      let x be set;
      assume x in rng p;
      then consider y be set such that A8: y in dom p & x = p.y by
FUNCT_1:def 5;
      reconsider y as Nat by A8;
      A9: y >= 1 & y <= n+1 by A6,A8,FINSEQ_1:3;
      then reconsider m1 = y-1 as Nat by INT_1:18;
         y-1<=n+1-1 by A9,REAL_1:49;
      then reconsider l1 = n-m1 as Nat by INT_1:18;
         p.y = (n choose m1) *a|^l1*b|^m1 by A6,A7,A8;
      then reconsider x as Real by A8,XREAL_0:def 1;
         x in REAL;
     hence thesis;
     end;
    then reconsider p as FinSequence of REAL by FINSEQ_1:def 4;
    take p;
    thus thesis by A6,A7,FINSEQ_1:def 3;
  end;
uniqueness
 proof
  let G,H;
  assume A10: len G = n+1 &
   (for i,l,m be natural number st i in dom G & m = i - 1 & l = n-m holds
   G.i = (n choose m)* a|^l * b|^m);
  assume A11: len H = n+1 &
   (for i,l,m be natural number st i in dom H & m = i - 1 & l = n-m holds
   H.i = (n choose m)* a|^l * b|^m);

     for i st i in dom G holds G.i = H.i
    proof
     let i;
     assume A12: i in dom G;
     then A13: i in Seg (n+1) by A10,FINSEQ_1:def 3;
     then A14:i in dom H by A11,FINSEQ_1:def 3;
     A15: i>=1 & i<=n+1 by A13,FINSEQ_1:3;
     then reconsider m = i-1 as Nat by INT_1:18;
        i-1<=n+1-1 by A15,REAL_1:49;
     then reconsider l = n-m as Nat by INT_1:18;
        H.i = (n choose m)* a|^l *b|^m by A11,A14;
     hence thesis by A10,A12;
    end;
  hence G = H by A10,A11,Th3;
 end;
end;

canceled;

theorem
Th38: (a,b) In_Power 0 = <*1*>
 proof
  set F = (a,b) In_Power 0;
  A1: len F = 0+1 by Def4 .= 1;
  then A2: dom F = {1} by FINSEQ_1:4,def 3;
  then 1 in dom F by TARSKI:def 1;
  then consider i be set such that A3: i in dom F;
  A4: 1 in dom (a,b) In_Power 0 by A2,TARSKI:def 1;
  reconsider i as Nat by A3;
  A5: i = 1 by A2,A3,TARSKI:def 1;
  then reconsider m1 = i-1 as Nat by INT_1:18;
   reconsider l1 = 0-m1 as Nat by A5;
     F.1 = (0 choose 0)*a|^l1*b|^m1
   by A4,A5,Def4 .=
   1*a|^0*b|^0 by A5,Th31 .= 1*1*b|^0 by Th9 .= 1 by Th9;
  hence thesis by A1,FINSEQ_1:57;
 end;

theorem
Th39: ((a,b) In_Power s).1 = a|^s
 proof
  reconsider m1 = 1-1 as Nat by INT_1:18;
  A1: s>=0 by NAT_1:18;
  reconsider l1 = s-m1 as natural number;
     len ((a,b) In_Power s) = s+1 by Def4;
  then A2: dom ((a,b) In_Power s) = Seg (s+1) by FINSEQ_1:def 3;
     s+1>=0+1 by A1,AXIOMS:24;
  then 1 in dom ((a,b) In_Power s) by A2,FINSEQ_1:3;
  then ((a,b) In_Power s).1 = (s choose 0)*a|^l1*b|^m1 by Def4
 .= 1*a|^l1*b|^m1 by Th29
   .= a|^s by Th9;
  hence thesis;
 end;

theorem
Th40: ((a,b) In_Power s).(s+1) = b|^s
 proof
     s+1>=1 by NAT_1:37;
  then reconsider m1 = s+1-1 as Nat by INT_1:18;
  reconsider l1 = s-m1 as Nat by INT_1:18;
     len ((a,b) In_Power s) = s+1 by Def4;
  then dom ((a,b) In_Power s) = Seg (s+1) by FINSEQ_1:def 3;
  then s+1 in dom ((a,b) In_Power s) by FINSEQ_1:6;
  then ((a,b) In_Power s).(s+1) = (s choose s)*a|^l1*b|^m1 by Def4 .= 1*a|^l1*
b|^m1 by Th31
   .= 1*b|^m1 by Th9
   .= b|^s;
  hence thesis;
 end;

theorem
Th41: for s holds (a+b)|^s = Sum((a,b) In_Power s)
 proof
   defpred P[Nat] means (a+b)|^$1 = Sum((a,b) In_Power $1);
    (a+b)|^0 = 1 by Th9 .= Sum<*1 qua Real*> by RVSUM_1:103
           .= Sum((a,b) In_Power 0) by Th38;
then A1: P[0];
  A2: for n st P[n] holds P[n+1]   proof let n;
    assume
A3:   P[n];
    reconsider a, b as Real by XREAL_0:def 1;
    A4: (a+b)|^(n+1) = (a+b)*Sum((a,b) In_Power n) by A3,Th11
     .= a*Sum((a,b) In_Power n) + b*Sum((a,b) In_Power n)
     .= Sum(a*((a,b) In_Power n)) + b*Sum((a,b) In_Power n) by RVSUM_1:117
     .= Sum(a*((a,b) In_Power n)) + Sum(b*((a,b) In_Power n)) by RVSUM_1:117;
    set G1 = ((a*((a,b) In_Power n))^<*0 qua Real*>);
    set G2 = (<*0 qua Real*>^(b*((a,b) In_Power n)));
       len G1 = len (a*((a,b) In_Power n)) + len <*0*> by FINSEQ_1:35
          .= len (a*((a,b) In_Power n)) +1 by FINSEQ_1:57
          .= len ((a,b) In_Power n) +1 by Th6
          .= n+1+1 by Def4;
    then reconsider F1 = G1 as Element of (n+1+1)-tuples_on REAL by
FINSEQ_2:110;
    A5: len F1 = n+1+1 by FINSEQ_2:109;
       len G2 = len <*0*> + len (b*((a,b) In_Power n)) by FINSEQ_1:35
          .= 1+ len (b*((a,b) In_Power n)) by FINSEQ_1:57
          .= 1+ len ((a,b) In_Power n) by Th6
          .= n+1+1 by Def4;
    then reconsider F2 = G2 as Element of (n+1+1)-tuples_on REAL by
FINSEQ_2:110;
    A6: len F2 = n+1+1 by FINSEQ_2:109;
    A7: Sum F1 = Sum(a*((a,b) In_Power n)) +0 by RVSUM_1:104
       .= Sum(a*((a,b) In_Power n));
        Sum F2 = 0+ Sum(b*((a,b) In_Power n)) by RVSUM_1:106
       .= Sum(b*((a,b) In_Power n));
then A8:    Sum(G1+G2) = Sum(a*((a,b) In_Power n))+ Sum
(b*((a,b) In_Power n)) by A7,RVSUM_1:119;
    set F = F1 + F2;
    A9: len F = n+1+1 by FINSEQ_2:109;
    A10: for i st i in dom ((a,b) In_Power (n+1))
         holds F.i = ((a,b) In_Power (n+1)).i
     proof
      let i;
      assume A11: i in dom ((a,b) In_Power (n+1));
A12:      len ((a,b) In_Power (n+1)) = n+1+1 by Def4;
      then A13: dom ((a,b) In_Power (n+1)) = Seg (n+1+1) by FINSEQ_1:def 3;
      A14: i in Seg (n+1+1) by A11,A12,FINSEQ_1:def 3;
      then i>=1 & n+1+1>=i by FINSEQ_1:3;
      then reconsider j = i-1 as Nat by INT_1:18;
A15:   i in dom F by A9,A14,FINSEQ_1:def 3;
A16:  i in dom F1 by A5,A14,FINSEQ_1:def 3;
A17:  i in dom F2 by A6,A14,FINSEQ_1:def 3;
      A18: i = j+1;
      A19: n+1+1>=1 by NAT_1:37;
      set x = ((a,b) In_Power n)/.j;
      set r = ((a,b) In_Power n)/.i;
      set r1 = F1/.i;
      set r2 = F2/.i;
      A20: i in {1} implies F.i = ((a,b) In_Power (n+1)).i
       proof
        assume i in {1};
        then A21: i = 1 by TARSKI:def 1;
           n>=0 by NAT_1:18;
        then n+1>=0+1 by AXIOMS:24;
        then 1 in Seg (n+1) by FINSEQ_1:3;
        then 1 in Seg len (((a,b) In_Power n)) by Def4;
        then A22: 1 in dom ((a,b) In_Power n) by FINSEQ_1:def 3;
        then A23: 1 in dom (a*((a,b) In_Power n)) by Th7;
         A24:r = ((a,b) In_Power n).i by A21,A22,FINSEQ_4:def 4;
        A25: r = ((a,b) In_Power n).1 by A21,A22,FINSEQ_4:def 4;
        A26: r = a|^n by A21,A24,Th39;
        A27: r1 = F1.i by A16,FINSEQ_4:def 4;
        A28: r1 = ((a*((a,b) In_Power n))^<*0*>).1 by A16,A21,FINSEQ_4:def 4
         .= (a*((a,b) In_Power n)).1 by A23,FINSEQ_1:def 7
         .= a *a|^n by A25,A26,RVSUM_1:66 .= a|^(n+1) by Th11;
        A29: r2 = F2.i by A17,FINSEQ_4:def 4;
            r2 = (<*0*>^(b*((a,b) In_Power n))).1 by A17,A21,FINSEQ_4:def 4
         .= 0 by FINSEQ_1:58;
        then F.i = r1+0 by A27,A29,RVSUM_1:27
         .= ((a,b) In_Power (n+1)).i by A21,A28,Th39;
        hence thesis;
       end;
      A30: i in {k: k>1 & k<n+1+1} implies F.i = ((a,b) In_Power (n+1)).i
       proof
        assume i in {k: 1<k & k<n+1+1};
         then A31:ex k st k=i & 1<k & k<n+1+1;
        then A32: 1<=i & i<=n+1 by NAT_1:38;
        then i in Seg (n+1) by FINSEQ_1:3;
        then i in Seg len ((a,b) In_Power n) by Def4;
        then A33: i in dom ((a,b) In_Power n) by FINSEQ_1:def 3;
        then A34: i in dom (a*((a,b) In_Power n)) by Th7;
        reconsider m1 = i-1 as Nat by A31,INT_1:18;
           i-1<=n+1-1 by A32,REAL_1:49;
        then reconsider l1 = n-m1 as Nat by INT_1:18;
A35:        1<=j & j+1<=n+1+1 by A18,A31,NAT_1:38;
        A36: 1<=j & j<=n+1 by A18,A31,NAT_1:38,REAL_1:53;
        then j in Seg (n+1) by FINSEQ_1:3;
        then j in Seg len ((a,b) In_Power n) by Def4;
        then A37: j in dom ((a,b) In_Power n) by FINSEQ_1:def 3;
        then A38: j in dom (b*((a,b) In_Power n)) by Th7;
        reconsider m2 = j-1 as Nat by A35,INT_1:18;
        A39: l1+1 = n+1-(m2+1);
           j-1<=n+1-1 by A36,REAL_1:49;
        then reconsider l2 = n-m2 as Nat by INT_1:18;
        A41: x = ((a,b) In_Power n).j by A37,FINSEQ_4:def 4;

        A42: r = ((a,b) In_Power n).i by A33,FINSEQ_4:def 4;

        A43: r1 = ((a*((a,b) In_Power n))^<*0*>).i by A16,FINSEQ_4:def 4;
        A44: r2 = (<*0*>^(b*((a,b) In_Power n))).i by A17,FINSEQ_4:def 4;
        A45: r1 = (a*((a,b) In_Power n)).i by A34,A43,FINSEQ_1:def 7
         .= a*r by A42,RVSUM_1:66;
            r2 = (<*0*>^(b*((a,b) In_Power n))).(len <*0*> +j) by A18,A44,
FINSEQ_1:57
         .= (b*((a,b) In_Power n)).j by A38,FINSEQ_1:def 7
         .= b*x by A41,RVSUM_1:66;
        then F.i = a*r + b*x by A15,A43,A44,A45,RVSUM_1:26
         .= a*(a|^l1*(n choose m1)*b|^m1) + b*x by A33,A42,Def4
         .= a*a|^l1*((n choose m1)*b|^m1) + b*x
         .= a|^(l1+1)*((n choose m1)*b|^m1) + b*x by Th11
         .= a|^(l1+1)*((n choose m1)*b|^m1) + b*(b|^m2*((n choose m2)*a|^l2))
 by A37,A41,Def4
         .= a|^(l1+1)*((n choose m1)*b|^m1) + b*b|^m2*((n choose m2)*a|^l2)
         .=
a|^(l1+1)*((n choose (m2+1))*b|^(m2+1)) + b|^(m2+1)*((n choose m2)*a|^l2)
           by Th11
         .= ((a|^(l1+1)*(n choose (m2+1))) + ((n choose m2)*a|^l2))*b|^(m2+1)
         .= (((n choose (m2+1))*a|^(l1+1)) + ((n choose m2)*a|^(l1+1)))*b|^(m2
+1)
         .= ((n choose (m2+1)) + (n choose m2))*a|^(l1+1)*b|^(m2+1)
         .= ((n+1) choose (m2+1))*a|^(l1+1)*b|^(m2+1) by Th32
         .= ((a,b) In_Power (n+1)).i by A11,A39,Def4;
        hence thesis;
       end;
      A46: i in {n+1+1} implies F.i = ((a,b) In_Power (n+1)).i
       proof
        assume A47: i in {n+1+1};
        then A48: i = n+1+1 by TARSKI:def 1;
        A49: j = n+1+1-1 by A47,TARSKI:def 1 .= n+1;
        A50: n+1 = len ((a,b) In_Power n) by Def4
         .= len (a*((a,b) In_Power n)) by Th6;
           n+1 in Seg (n+1) by FINSEQ_1:6;
        then j in Seg len (((a,b) In_Power n)) by A49,Def4;
        then A51: j in dom ((a,b) In_Power n) by FINSEQ_1:def 3;
        then A52: j in dom (b*((a,b) In_Power n)) by Th7;
        A53: x = ((a,b) In_Power n).j by A51,FINSEQ_4:def 4;
        A54: x = ((a,b) In_Power n).(n+1) by A49,A51,FINSEQ_4:def 4
               .= b|^n by Th40;
        A55: r1 = F1.i by A16,FINSEQ_4:def 4;
        A56: r1 =
((a*((a,b) In_Power n))^<*0*>).(len (a*((a,b) In_Power n))+1)
           by A16,A48,A50,FINSEQ_4:def 4
         .= 0 by FINSEQ_1:59;
        A57: r2 = F2.i by A17,FINSEQ_4:def 4;
        A58: r2 = (<*0*>^(b*((a,b) In_Power n))).(1+n+1) by A17,A48,
FINSEQ_4:def 4
         .= (<*0*>^(b*((a,b) In_Power n))).(len <*0*> +j) by A49,FINSEQ_1:56
         .= (b*((a,b) In_Power n)).j by A52,FINSEQ_1:def 7
         .= b*b|^n by A53,A54,RVSUM_1:66 .= b|^(n+1) by Th11;
           F.i = 0+r2 by A55,A56,A57,RVSUM_1:27
         .= ((a,b) In_Power (n+1)).i by A48,A58,Th40;
        hence thesis;
       end;
         now assume i in {1} \/ {k: 1<k & k<n+1+1} \/ {n+1+1};
      then i in {1} \/ {k: 1<k & k<n+1+1} or i in {n+1+1} by XBOOLE_0:def 2;
      hence F.i = ((a,b) In_Power (n+1)).i by A20,A30,A46,XBOOLE_0:def 2;
      end;
      hence thesis by A11,A13,A19,Th5;
     end;
       len ((a,b) In_Power (n+1)) = len F by A9,Def4;
    hence thesis by A4,A8,A10,Th3;
   end;
A59:  for n holds P[n] from NAT_1:sch 1(A1,A2);
  let s;
     s is Nat by ORDINAL2:def 21;
  hence thesis by A59;
 end;

definition let n be natural number;
func Newton_Coeff n -> FinSequence of REAL means :Def5: len it = n+1 &
 (for i,k be natural number st i in dom it & k = i-1 holds it.i = n choose k);
existence
 proof
  reconsider n as Nat by ORDINAL2:def 21;
 defpred P[natural number,set] means
 for s st s = $1-1 holds $2 = n choose s;

 A1: for l for r1,r2 being set
      st l in Seg (n+1) & P[l,r1] & P[l,r2] holds r1 = r2
   proof
    let l;
    let r1,r2 be set;
    assume A2: l in Seg (n+1) & P[l,r1] & P[l,r2];
    then l>=1 & l<=n+1 by FINSEQ_1:3;
    then reconsider k = l-1 as Nat by INT_1:18;
        r1 = n choose k by A2;
    hence thesis by A2;
   end;
 A3: for l st l in Seg (n+1) ex x being set st P[l,x]
   proof
    let l;
    assume l in Seg (n+1);
    then l>=1 & l<=n+1 by FINSEQ_1:3;
    then reconsider k = l-1 as Nat by INT_1:18;
    reconsider x = n choose k as set;
    take x;
    thus thesis;
   end;
  consider F being FinSequence such that A4:
   dom F = Seg (n+1) & for l st l in Seg (n+1) holds
   P[l,F.l] from FINSEQ_1:sch 1(A1,A3);
     rng F c= REAL
   proof
      let x be set;
      assume x in rng F;
      then consider y be set such that A5: y in dom F & x = F.y by
FUNCT_1:def 5;
      reconsider y as Nat by A5;
         y >= 1 & y <= n+1 by A4,A5,FINSEQ_1:3;
      then reconsider m1 = y-1 as Nat by INT_1:18;
         F.y = n choose m1 by A4,A5;
      then reconsider x as Real by A5;
         x in REAL;
     hence thesis;
     end;
    then reconsider F as FinSequence of REAL by FINSEQ_1:def 4;
    take F;
    thus thesis by A4,FINSEQ_1:def 3;
 end;
uniqueness
 proof
  let G,H;
  assume A6: len G = n+1 &
   (for i,m be natural number st i in dom G & m = i - 1 holds
   G.i = n choose m);
  assume A7: len H = n+1 &
   (for i,m be natural number st i in dom H & m = i - 1 holds
   H.i = n choose m);

     for i st i in dom G holds G.i = H.i
    proof
     let i;
     assume A8: i in dom G;
     then A9: i in Seg (n+1) by A6,FINSEQ_1:def 3;
     then A10:i in dom H by A7,FINSEQ_1:def 3;
         i>=1 & i<=n+1 by A9,FINSEQ_1:3;
     then reconsider m = i-1 as Nat by INT_1:18;
        H.i = n choose m by A7,A10;
     hence thesis by A6,A8;
    end;
  hence G = H by A6,A7,Th3;
 end;
end;

canceled;

theorem
Th43: for s holds Newton_Coeff s = (1,1) In_Power s
 proof
  let s;
  A1: len (Newton_Coeff s) = s+1 by Def5 .= len ((1,1) In_Power s) by Def4;
     for i st i in dom (Newton_Coeff s) holds
   (Newton_Coeff s).i = ((1,1) In_Power s).i
  proof
   let i;
   assume A2: i in dom (Newton_Coeff s);
   A3: dom (Newton_Coeff s) = Seg len (Newton_Coeff s) by FINSEQ_1:def 3
    .= Seg (s+1) by Def5;
   then A4: dom (Newton_Coeff s) = Seg len ((1,1) In_Power s) by Def4
     .= dom ((1,1) In_Power s) by FINSEQ_1:def 3;

   A5: i>=1 & i<=(s+1) by A2,A3,FINSEQ_1:3;
   then reconsider m1 = i-1 as Nat by INT_1:18;
      s+1-1>=i-1 by A5,REAL_1:49;
   then reconsider l1 = s-m1 as Nat by INT_1:18;
      ((1,1) In_Power s).i = (s choose m1)*1|^l1*1|^m1 by A2,A4,Def4
    .= (s choose m1)*1*1|^m1 by Th15 .= (s choose m1)*1 by Th15 .=
 (Newton_Coeff s).i by A2,Def5;
   hence thesis;
  end;
 hence thesis by A1,Th3;
 end;

theorem
    for s holds 2|^s = Sum(Newton_Coeff s)
 proof
  let s;
     2|^s = (1+1)|^s .= Sum((1,1) In_Power s) by Th41 .= Sum(Newton_Coeff s)
   by Th43;
  hence thesis;
 end;

begin :: Addenda

:: from NAT_LAT

theorem Th2:
  l >= 1 implies k*l>=k
proof
assume A1: l>=1;
    for k holds k*l>=k
  proof
   defpred _P[Nat] means $1*l>=$1;
   A2:_P[0];
   A3:for k st _P[k] holds _P[k+1]
    proof let k;
     assume A4:k*l>=k;
        (k + 1)*l = k * l + 1*l;
     then A5:(k+1)*l>=k+l by A4,REAL_1:55;
        k+l>=k+1 by A1,REAL_1:55;
     hence (k+1)*l>=k+1 by A5,AXIOMS:22;
    end;
   thus for k holds _P[k] from NAT_1:sch 1(A2,A3);
  end;
 hence thesis;
end;

theorem Th3:
 l >= 1 & n >= k*l implies n>=k
proof
 assume that A1: l>=1 and A2: n>=k*l;
    k*l>=k by A1,Th2;
 hence thesis by A2,AXIOMS:22;
end;

definition let n;
 redefine func n! -> Nat;
 coherence by Th22;
end;

theorem Th5:
 l <> 0 implies l divides l!
proof
 assume l<>0;
 then consider n such that A1: l = n+1 by NAT_1:22;
    (n+1) divides (n+1)!
  proof
     (n+1)! = (n+1) * (n!) by Th21;
  hence thesis by NAT_1:def 3;
  end;
 hence thesis by A1;
end;

theorem
    n <> 0 implies (n+1)/n > 1
  proof
   assume A1: n<>0;
   then A2: n>0 by NAT_1:19;
   A3: (n+1)/n=n/n+1/n by XCMPLX_1:63
         .=1+1/n by A1,XCMPLX_1:60;
       1/n>0 by A2,REAL_2:127;
   hence thesis by A3,REAL_1:69;
  end;

theorem Tw9:
  k/(k+1) < 1
  proof
    defpred _P[Nat] means $1/($1+1)<1;
A1: _P[0];
A2: _P[m] implies _P[m+1] proof
     assume m/(m+1)<1;
     set r=m+1;
     A3: 0 < r by NAT_1:19;
        (m+1) < ((m+1)+1) by REAL_1:69;
    hence thesis by A3,REAL_2:142;
   end;
     for m holds _P[m] from NAT_1:sch 1(A1,A2);
  hence thesis;
 end;

theorem Th10:
  for l being Nat holds l! >= l
  proof
    defpred _P[Nat] means $1! >= $1;
  A1: _P[0] by Th18;
  A2:for l st _P[l] holds _P[l+1] proof let l;
    assume A3:l!>=l;
A4:    l!*(l+1)=(l+1)! by Th21;
    A5:     l+1>=0 by NAT_1:18;
       l=0 & (l+1)!>=(l+1) or l>=1 & (l+1)!>=(l+1)
     proof
      per cases by NAT_1:39;
       case l=0;
       hence thesis by Th19;
       end;
       case A6: l>=1;  (l+1)!>=(l+1)*l by A3,A4,A5,AXIOMS:25;
       hence (l+1)!>=(l+1) by A6,Th3;
     end;
     end;
    hence (l+1)!>=(l+1);
   end;
   thus for l being Nat holds _P[l] from NAT_1:sch 1(A1,A2);
  end;

reserve t,s,j,r for Nat;

theorem Th12:
  for m,n st m<>1 holds m divides n implies not m divides (n+1)
   proof let m,n;
    assume that A1:m<>1 and A2:m divides n and A3: m divides (n+1);
    consider t such that A4: n = m * t by A2,NAT_1:def 3;
    consider s such that A5: n+1 = m * s by A3,NAT_1:def 3;
A6:    1=m*s-m*t by A4,A5,XCMPLX_1:26;
    A7: s<>0 by A5;
        t <= s
     proof
      assume A8: t > s;
        (n+1)*t=n*s by A4,A5;
then A9:       t=n*s/(n+1) by XCMPLX_1:90
           .=s*(n/(n+1)) by XCMPLX_1:75;
       A10: s>0 by A7,NAT_1:19;
          n/(n+1)<1 by Tw9;
      hence contradiction by A8,A9,A10,REAL_2:145;
     end;
    then reconsider r =s-t as Nat by INT_1:18;
        1=m*r by A6;
      hence contradiction by A1,NAT_1:40;
end;

theorem
     j divides l & j divides l+1 iff j=1 by Th12,NAT_1:53;

theorem Th15:
 for k,j st j<>0 holds j divides (j+k)!
  proof
    defpred _P[Nat] means for j st j<>0 holds j divides (j+$1)!;
A1: _P[0] by Th5;
A2: for k st _P[k] holds _P[k+1]  proof let k;
    assume A3: for j st j<>0 holds j divides (j+k)!;
    let j;
    assume j<>0;
     then j divides (j+k)! by A3;
      then j divides ((j+k)!)*((j+k)+1) by NAT_1:56;
     hence thesis by Th21;
    end;
   thus for k holds _P[k] from NAT_1:sch 1(A1,A2);
  end;

theorem Th16:
  j<=l & j<>0 implies j divides l!
     proof
      assume that A1: j<=l and A2: j<>0;
          ex k st l=j+k by A1,NAT_1:28;
      hence thesis by A2,Th15;
     end;

theorem Th17:
  for l,j st j<>1 & j<>0 holds j divides (l!+1) implies j>l
    proof let l,j;
     assume that A1: j<>1 and A2: j<>0 and A3: j divides (l!+1);
        not j divides l! by A1,A3,Th12;
     hence thesis by A2,Th16;
    end;

:: The fundamental properties of lcm, hcf

theorem
  m lcm (n lcm k) = (m lcm n) lcm k
 proof
  set M = n lcm k;
  set K = m lcm M;
  set N = m lcm n;
  set L = N lcm k;
      K = L
  proof
  A1: n divides M & k divides M & for l st n divides l & k divides
 l holds M divides l by NAT_1:def 4;
  A2: m divides K & M divides K & for i st m divides i & M divides
 i holds K divides i by NAT_1:def 4;
  A3: m divides N & n divides N & for s st m divides s & n divides
 s holds N divides s by NAT_1:def 4;
  A4: N divides L & k divides L & for j st N divides j & k divides
 j holds L divides j by NAT_1:def 4;
  A5: K divides L
   proof
   A6: m divides L by A3,A4,NAT_1:51;
       n divides L by A3,A4,NAT_1:51;
   then M divides L by A4,NAT_1:def 4;
   hence thesis by A6,NAT_1:def 4;
   end;
      L divides K
   proof
   A7: k divides K by A1,A2,NAT_1:51;
       n divides K by A1,A2,NAT_1:51;
   then N divides K by A2,NAT_1:def 4;
   hence thesis by A7,NAT_1:def 4;
   end;
  hence thesis by A5,NAT_1:52;
  end;
 hence thesis;
end;

theorem Th20:
   m divides n iff m lcm n = n
proof
thus m divides n implies m lcm n = n
proof
 set M = m lcm n;
 assume A1: m divides n;
  A2: m divides M & n divides M & for s st m divides s & n divides
 s holds M divides s by NAT_1:def 4;
      M = n
   proof
       M divides n by A1,NAT_1:def 4;
    hence thesis by A2,NAT_1:52;
   end;
 hence thesis;
end;
thus m lcm n = n implies m divides n by NAT_1:def 4;
end;

theorem
    n divides m & k divides m iff n lcm k divides m
 proof
     n lcm k divides m implies (n divides m & k divides m)
   proof
    set M = n lcm k;
    assume A1: n lcm k divides m;
        n divides M & k divides M & for l st n divides l & k divides
 l holds M divides l by NAT_1:def 4;
    hence thesis by A1,NAT_1:51;
   end;
   hence thesis by NAT_1:def 4;
  end;

theorem
     m lcm 1 = m
proof
set M = m lcm 1;
A1: m divides M & 1 divides M & for l st m divides l & 1 divides
 l holds M divides l by NAT_1:def 4;
    M divides m
 proof
     1 divides m by NAT_1:53;
 hence thesis by NAT_1:def 4; end;
hence thesis by A1,NAT_1:52; end;

theorem Th27:
 m lcm n divides m*n
 proof
  set s=m*n;
  A1: m divides s by NAT_1:56;
       n divides s by NAT_1:56;
  hence thesis by A1,NAT_1:def 4; end;

theorem
  m hcf (n hcf k) = (m hcf n) hcf k
 proof
  set M = n hcf k;
  set K = m hcf M;
  set N = m hcf n;
  set L = N hcf k;
      K = L
  proof
  A1: M divides n & M divides k & for l st l divides n & l divides
 k holds l divides M by NAT_1:def 5;
  A2: K divides m & K divides M & for i st i divides m & i divides
 M holds i divides K by NAT_1:def 5;
  A3: N divides m & N divides n & for s st s divides m & s divides
 n holds s divides N by NAT_1:def 5;
  A4: L divides N & L divides k & for j st j divides N & j divides
 k holds j divides L by NAT_1:def 5;
  A5: K divides L
   proof
   A6: K divides k by A1,A2,NAT_1:51;
   A7: K divides m by NAT_1:def 5;
       K divides n by A1,A2,NAT_1:51;
   then K divides N by A7,NAT_1:def 5;
   hence thesis by A6,NAT_1:def 5;
   end;
      L divides K
   proof
   A8: L divides n by A3,A4,NAT_1:51;
   A9: L divides m by A3,A4,NAT_1:51;
       L divides M by A4,A8,NAT_1:def 5;
   hence thesis by A9,NAT_1:def 5;
   end;
  hence thesis by A5,NAT_1:52;
  end;
 hence thesis;
 end;

theorem Th30:
  n divides m implies n hcf m = n
proof
 set N = n hcf m;
 assume A1: n divides m;
 A2: N divides n & N divides m & for k st k divides n & k divides
 m holds k divides N by NAT_1:def 5;
    N = n
 proof
      n divides N by A1,NAT_1:def 5;
  hence thesis by A2,NAT_1:52;
 end;
 hence thesis;
end;

theorem
     m divides n & m divides k iff m divides n hcf k
 proof
     m divides n hcf k implies (m divides n & m divides k)
   proof
    set M = n hcf k;
    assume A1: m divides n hcf k;
        M divides n & M divides k & for l st l divides n & l divides
 k holds l divides M by NAT_1:def 5;
    hence thesis by A1,NAT_1:51;
   end;
   hence thesis by NAT_1:def 5;
  end;

theorem
  m hcf 1 = 1
proof set M = m hcf 1;
A1: M divides m & M divides 1 & for l st l divides m & l divides
 1 holds l divides M by NAT_1:def 5;
    1 divides M by NAT_1:53;
hence thesis by A1,NAT_1:52;end;

theorem
     m hcf 0 = m
proof set M = m hcf 0;
A1: m divides M
 proof
    m divides 0 by NAT_1:53;
hence thesis by NAT_1:def 5;end;
      M divides m by NAT_1:def 5;
hence thesis by A1,NAT_1:52;end;

theorem Th37:
  (m hcf n) lcm n = n
proof
 set M = m hcf n;
     M divides m & M divides n & for k st k divides m & k divides
 n holds k divides M by NAT_1:def 5;
 hence thesis by Th20;
end;

theorem Th38:
  m hcf (m lcm n) = m
proof
 set M = m lcm n;
     m divides M & n divides M & for k st m divides k & n divides
 k holds M divides k by NAT_1:def 4;
 hence m hcf (m lcm n) = m by Th30;
end;

theorem
     m hcf (m lcm n) = (n hcf m) lcm m
proof
    m hcf (m lcm n) = m by Th38;
 hence thesis by Th37;
 end;

theorem
     m divides n implies m hcf k divides n hcf k
proof
 set M = m hcf k;
 A1: M divides m & M divides k & for i st i divides m & i divides
 k holds i divides M by NAT_1:def 5;
 assume m divides n;
  then M divides n by A1,NAT_1:51;
 hence thesis by A1,NAT_1:def 5;
end;

theorem
     m divides n implies k hcf m divides k hcf n
proof
 set M = k hcf m;
 A1: M divides k & M divides m & for i st i divides m & i divides
 k holds i divides M by NAT_1:def 5;
 assume m divides n;
  then M divides n by A1,NAT_1:51;
 hence thesis by A1,NAT_1:def 5;
end;

theorem
     m > 0 implies 0 hcf m > 0
proof
 assume A1: m>0;
 A2: m divides 0 by NAT_1:53;
    for n st n divides 0 & n divides m holds n divides m;
 hence 0 hcf m > 0 by A1,A2,NAT_1:def 5;
end;

theorem 
  n > 0 implies n hcf m > 0
proof
 assume that A1: n>0 and A2: n hcf m <= 0;
 A3: (n hcf m) divides n by NAT_1:def 5;
    n hcf m = 0 or n hcf m < 0 by A2;
  then ex r st n = 0*r by A3,NAT_1:18,def 3;
 hence contradiction by A1;
end;

theorem
  m > 0 & n > 0 implies m lcm n > 0
proof
 assume that A1: m>0 and A2: n>0 and A3: m lcm n <= 0;
 A4: (m lcm n) divides m*n by Th27;
    m lcm n = 0 or m lcm n < 0 by A3;
  then ex r st m*n = 0*r by A4,NAT_1:18,def 3;
 hence contradiction by A1,A2,REAL_2:122;
end;

theorem
     (n hcf m) lcm (n hcf k) divides n hcf (m lcm k)
proof
  set M = m lcm k;
  set N = n hcf k;
  set P = n hcf m;
  set L = P lcm N;
  A1: m divides M & k divides M & for l st m divides l & k divides
 l holds M divides l by NAT_1:def 4;
  A2: N divides n & N divides k & for s st s divides n & s divides
 k holds s divides N by NAT_1:def 5;
  A3: P divides n & P divides m & for j st j divides n & j divides
 m holds j divides P by NAT_1:def 5;
       L divides (n hcf M)
    proof
     A4: L divides n by A2,A3,NAT_1:def 4;
     A5: P divides M by A1,A3,NAT_1:51;
         N divides M by A1,A2,NAT_1:51;
      then L divides M by A5,NAT_1:def 4;
     hence thesis by A4,NAT_1:def 5;
    end;
hence thesis;
end;

theorem
     m divides l implies m lcm (n hcf l) divides (m lcm n) hcf l
proof
  assume A1: m divides l;
  set M = m lcm n;
  set K = M hcf l;
  set N = n hcf l;
  A2: m divides M & n divides M & for j st m divides j & n divides
 j holds M divides j by NAT_1:def 4;
  A3: N divides n & N divides l & for s st s divides n & s divides
 l holds s divides N by NAT_1:def 5;
      (m lcm N) divides K
    proof
     A4: m divides K by A1,A2,NAT_1:def 5;
         N divides M by A2,A3,NAT_1:51;
      then N divides K by A3,NAT_1:def 5;
     hence thesis by A4,NAT_1:def 4;
    end;
hence thesis;
end;

theorem
      n hcf m divides n lcm m
proof
  set K = n hcf m;
  set N = n lcm m;
  A1: K divides n & K divides m & for i st i divides n & i divides
 m holds i divides K by NAT_1:def 5;
      n divides N & m divides N & for r st n divides r & m divides
 r holds N divides r by NAT_1:def 4;
hence thesis by A1,NAT_1:51;
end;

:: from GR_CY_2

 reserve k,l,m,n,p,s,r for Nat;
 reserve i0,i,i1,i2,i3,i4 for Integer;

theorem
 for n,m st 0 < m holds n mod m= n - m * (n div m)
  proof
   let n,m such that A1: m >0;
   reconsider z1=m * (n div m),z2=(n mod m) as Integer;
      n - z1 = z1 + z2 -z1 by A1,NAT_1:47;
   hence n -m * (n div m) = n mod m;
  end;

theorem
 i2 >= 0 implies i1 mod i2 >= 0
  proof
   assume A1:i2 >= 0;
   per cases by A1;
   suppose A2:i2 > 0;
      [\ i1/i2 /] <= i1/i2 by INT_1:def 4;
   then i1 div i2 <= i1/i2 by INT_1:def 7;
   then (i1 div i2)*i2 <= (i1/i2)*i2 by A2,AXIOMS:25;
   then (i1 div i2)*i2 <= i1 by A2,XCMPLX_1:88;
   then i1 - (i1 div i2)*i2 >= 0 by SQUARE_1:12;
   hence thesis by A2,INT_1:def 8;
   end;
   suppose i2 = 0;
   hence thesis by INT_1:def 8;
 end;
 end;

theorem
 i2 > 0 implies i1 mod i2 < i2
proof
 assume A1: i2 > 0;
    i1/i2 -1 < [\ i1/i2 /] by INT_1:def 4;
 then i1/i2 -1 < i1 div i2 by INT_1:def 7;
 then (i1/i2 -1)*i2 < (i1 div i2)*i2 by A1,REAL_1:70;
 then i1/i2*i2 -1*i2 < (i1 div i2)*i2;
 then i1 -i2 < (i1 div i2)*i2-0 by A1,XCMPLX_1:88;
 then i1 -(i1 div i2)*i2 < i2-0 by REAL_2:170;
 hence thesis by A1,INT_1:def 8;
end;

theorem
 i2 <> 0 implies i1 = (i1 div i2) * i2 + (i1 mod i2)
proof
 assume i2 <> 0;
 then (i1 div i2) * i2 +(i1 mod i2) =
 (i1 div i2 )*i2 + (i1 - ( i1 div i2 )*i2 ) by INT_1:def 8
 .= i1;
 hence thesis;
end;

theorem
 for m,n st m>0 or n>0 holds ex i,i1 st i*m + i1*n = m hcf n
 proof
  let m,n;
  assume A1:m>0 or n>0;
  defpred P[Nat] means $1>0 & ex i,i1 st i*m+i1*n=$1;
  A2: ex p st P[p]
   proof
       now per cases by A1;
       suppose A3:m>0;
               ex i,i1 st i*m+i1*n=m
               proof
                 set i1=0;
                    1*m + i1*n=m;
                 hence thesis;
               end;
        hence thesis by A3;
       end;
       suppose A4:n>0;
               ex i,i1 st i*m+i1*n=n
               proof
                 set i=0;
                  i*m + 1*n=n;
                 hence thesis;
               end;
       hence thesis by A4;
  end;
  end;
  hence thesis;
 end;
    ex p st P[p] & for n st P[n] holds p <= n from NAT_1:sch 5(A2);
   then consider p such that A5:p>0 and A6:ex i,i0 st i*m+i0*n=p and A7: (for k
st
   (k>0 & ex i1,i2 st i1*m+i2*n=k) holds p <= k);
   consider i,i0 such that A8:i*m+i0*n=p by A6;
   A9:for k st ex i1,i2 st i1*m+i2*n=k holds p divides k
    proof
    let k;
    given i1,i2 such that A10:i1*m+i2*n=k;
    consider l,s such that A11:k=p*l+s and A12:s<p by A5,NAT_1:42;
     s=0
    proof
     assume s<>0; then A13:s>0 by NAT_1:19;
        ex i3,i4 st i3*m+i4*n=s
     proof
         s=(i1*m+i2*n)-(i*m*l+i0*n*l) by A8,A10,A11
         .=(i1-i*l)*m+(i2-i0*l)*n;
         hence thesis;
       end;
    hence contradiction by A7,A12,A13;
    end;
    hence thesis by A11,NAT_1:def 3;
  end;
    p= m hcf n
 proof
  A14: ex i,i1 st i*m+i1*n=m
    proof
     set i1=0;
      1*m + i1*n=m;
     hence thesis;
    end;
 A15: ex i,i1 st i*m+i1*n=n
   proof
    set i=0;
     i*m + 1*n=n;
    hence thesis;
   end;
  A16:p divides m by A9,A14;
  A17:p divides n by A9,A15;
       for s st s divides m & s divides n holds s divides p
     proof
      let s;
      assume that A18:s divides m and A19:s divides n;
      consider l such that A20:m = s*l by A18,NAT_1:def 3;
      consider r such that A21:n = s*r by A19,NAT_1:def 3;
      reconsider p'=p,s'=s as Integer;
       ex i4 st p'=s'*i4
     proof
     take i*l+i0*r;
       thus thesis by A8,A20,A21;
     end;
        then A22:s' divides p' by INT_1:def 9;
       A23: for z being Nat holds abs z=z
          proof
          let z be Nat;
             z>=0 by NAT_1:18;
          hence thesis by ABSVALUE:def 1;
          end;
        then A24:abs s=s;
              abs p=p by A23;
      hence thesis by A22,A24,INT_2:21;
     end;
   hence thesis by A16,A17,NAT_1:def 5;
 end;
 hence thesis by A6;
 end;

:: from NAT_LAT

 reserve n,j,i,k,k1,k2,k3,k4,m,l,s for Nat;

definition
  func SetPrimes -> Subset of NAT means
:Def17: for n being Nat holds n in it iff n is prime;
existence
 proof
  defpred _P[Nat] means $1 is prime;
  consider X being Subset of NAT such that
A1: for n being Nat holds n in X iff _P[n] from SETWISEO:sch 1;
  take X;
  let n be Nat;
  thus n in X implies n is prime by A1;
  assume n is prime;
  hence thesis by A1;
  end;
uniqueness proof
    defpred P[Nat] means $1 is prime;
thus for X1,X2 being Subset of NAT st
   (for y being Element of NAT holds y in X1 iff P[y]) &
   (for y being Element of NAT holds y in X2 iff P[y]) holds
  X1 = X2 from SUBSET_1:sch 2;
  end;
end;

registration
  cluster prime Nat;
existence by INT_2:44;
end;

definition
  mode Prime is prime Nat;
end;

reserve p,f for Prime;
reserve x for set;

definition let p;
  func SetPrimenumber p -> Subset of NAT means
:Def19: for q being Nat holds q in it iff (q < p & q is prime);
existence
 proof defpred P[Nat] means ($1 < p & $1 is prime);
  consider X being Subset of NAT such that
A1: for q being Nat holds q in X iff P[q] from SETWISEO:sch 1;
  take X;
  let q be Nat;
  thus q in X implies q < p & q is prime by A1;
  assume q < p & q is prime;
  hence thesis by A1;
  end;
uniqueness proof
    defpred P[Nat] means ($1 < p & $1 is prime);
thus for X1,X2 being Subset of NAT st
   (for y being Element of NAT holds y in X1 iff P[y]) &
   (for y being Element of NAT holds y in X2 iff P[y]) holds
  X1 = X2 from SUBSET_1:sch 2;
  end;
end;

theorem Th77:
 SetPrimenumber p c= SetPrimes
proof
    for x holds x in SetPrimenumber p implies x in SetPrimes
  proof let x;
   assume A1: x in SetPrimenumber p;
   then reconsider x' = x as Nat;
      x' < p & x' is prime by A1,Def19;
   hence thesis by Def17;
 end;
 hence thesis by TARSKI:def 3;
end;

theorem
    for q being Prime st p < q holds
       SetPrimenumber p c= SetPrimenumber q
proof let q be Prime;
 assume A1: p < q;
    for x holds x in SetPrimenumber p implies x in SetPrimenumber q
  proof let x;
   assume A2: x in SetPrimenumber p;
   then reconsider x' = x as Nat;
      x' < p & x' is prime by A2,Def19;
   then x' < q & x' is prime by A1,AXIOMS:22;
   hence thesis by Def19;
  end;
 hence thesis by TARSKI:def 3;
end;

theorem Th79:
 SetPrimenumber p c= Seg p
proof let x be set;
   assume A1: x in SetPrimenumber p;
   then reconsider q = x as Nat;
   A2: q < p & q is prime by A1,Def19;
   then 1 <= q by INT_2:def 5;
   hence thesis by A2,FINSEQ_1:3;
end;

theorem
    SetPrimenumber p is finite
proof
    SetPrimenumber p c= Seg p by Th79;
 hence thesis by FINSET_1:13;
end;

Lm14:
k=0 or k=1 or 2<=k
 proof
  assume k<>0 & k<>1;
  then 1<k by CQC_THE1:2;
  then 1+1<=k by NAT_1:38;
 hence thesis;
 end;

reserve p for Prime;

theorem Th81:
   for l holds ex p st p is prime & p>l
  proof let l;
   reconsider two = 2 as Prime by INT_2:44;
      l=0 & (ex p st p is prime & p>l) or (l=1 & (ex p st p is prime & p>l)) or
   (2<=l & (ex p st p is prime & p>l))
   proof
    per cases by Lm14;
     case A1: l=0; take two; thus thesis by A1;
     end;
     case A2: l=1; take two; thus thesis by A2;
     end;
     case A3: 2<=l;
          l<=l! by Th10;
      then 2<=l! by A3,AXIOMS:22;
       then (2+0) <= (l!+1) by REAL_1:55;
      then consider j such that
      A4: j is prime and A5: j divides (l!+1) by INT_2:48;
      A6: j<>1 by A4,INT_2:def 5;
A7:      j<>0 by A4,INT_2:def 5;
      reconsider j'=j as Prime by A4;
      take j';
    thus thesis by A5,A6,A7,Th17;
   end;
   end;
   hence thesis;
  end;

theorem
    SetPrimes <> {} by Def17,INT_2:44;

theorem Th85:
    {k:k<2 & k is prime}={}
    proof
      consider x being Element of {k:k<2 & k is prime};
      assume {k:k<2 & k is prime}<>{};
       then x in {k:k<2 & k is prime};
       then ex n st x=n & n<2 & n is prime;
      then 0 is prime or 1 is prime by Lm14;
     hence contradiction by INT_2:def 5;
    end;

theorem
    for p holds {k:k<p & k is prime} c= NAT
proof let p;
 let x be set;
 assume x in {k:k<p & k is prime};
 then ex k st x = k & k<p & k is prime;
 hence x in NAT;
end;

theorem Th87:
 for m holds {k:k<m & k is prime} c= Seg m
proof let m;
    for x holds x in {k:k<m & k is prime} implies x in Seg m
  proof let x be set;
   assume x in {k:k<m & k is prime};
   then consider k such that A1: x = k & k<m & k is prime;
      1 <= k by A1,INT_2:def 5;
   hence thesis by A1,FINSEQ_1:3;
  end;
 hence thesis by TARSKI:def 3;
end;

theorem Th88:
 for m holds {k:k<m & k is prime} is finite
proof let m;
     {k:k<m & k is prime} c= Seg m by Th87;
 hence thesis by FINSET_1:13;
end;

theorem Th89:
 for f being Prime holds not f in {k: k<f & k is prime}
  proof let f;
   assume f in {k: k<f & k is prime};
    then ex k st f=k & k<f & k is prime;
   hence contradiction;
  end;

theorem
    for f holds {k: k<f & k is prime}\/{f} is finite
proof let f;
    {k:k<f & k is prime} is finite by Th88;
 hence thesis by FINSET_1:14;
end;

theorem Th91:
 for f,g being Prime st f<g holds
   {k1: k1<f & k1 is prime}\/{f} c={k2: k2<g & k2 is prime}
proof let f,g be Prime;
 assume A1: f<g;
    for x holds
       x in {k1:k1<f & k1 is prime}\/{f} implies x in {k2: k2<g & k2 is prime}
  proof let x be set;
   assume A2: x in {k1:k1<f & k1 is prime}\/{f};
      x in {k1:k1<f & k1 is prime} & x in {k2:k2<g & k2 is prime} or
   x in {f} & x in {k3:k3<g & k3 is prime}
    proof
     per cases by A2,XBOOLE_0:def 2;
     case x in {k1:k1<f & k1 is prime};
      then consider k such that A3: x = k & k<f & k is prime;
          k < g by A1,A3,AXIOMS:22;
      hence thesis by A3;
     end;
     case x in {f};
       then x = f by TARSKI:def 1;
      hence thesis by A1;
    end;
    end;
   hence thesis;
  end;
 hence thesis by TARSKI:def 3;
end;

theorem
    for k st k>m holds not k in {k1:k1<m & k1 is prime}
       proof let k;
        assume that A1: k>m and A2: k in {k1:k1<m & k1 is prime};
            ex k1 st k=k1 & k1<m & k1 is prime by A2;
        hence contradiction by A1;
       end;

definition let n;
  func primenumber n -> Prime means
:Def20:
 ex B being finite set st B = {k:k<it & k is prime} & n = card B;
existence
proof
  defpred _P1[Nat] means ex m st
   ex B being finite set st B = {k:k<m & k is prime} & $1=card B & m is Prime;
  A1: _P1[0] by Th85,CARD_1:78,INT_2:44;
 A2: for n st _P1[n] holds _P1[n+1]
    proof let n;
     given m being Nat, B being finite set such that
      A3:  B = {k1:k1<m & k1 is prime} & n=card B & m is Prime;
      defpred _P[Nat] means $1 is prime & $1>m;
         ex p st p is prime & p>m by Th81;
     then A4: ex k st _P[k];
     consider k such that A5: _P[k] & for l st _P[l] holds k <= l from
NAT_1:sch 5(A4);
     take k;
     A6: {k2: k2<k & k2 is prime}={k1: k1<m & k1 is prime}\/{m}
      proof
      A7: {k2: k2<k & k2 is prime}c={k1: k1<m & k1 is prime}\/{m}
       proof
           for x holds x in {k2: k2<k & k2 is prime} implies
            x in {k1: k1<m & k1 is prime}\/{m}
         proof let x;
          assume x in {k2: k2<k & k2 is prime};
          then consider s such that A8: x = s & s<k & s is prime;
A9:            s<=m by A5,A8;
              s=m & x in {k1:k1<m & k1 is prime}\/{m} or
              s<m & x in {k3:k3<m & k3 is prime}\/{m}
          proof
           per cases by A9,REAL_1:def 5;
            case s=m;
            then s in {m} by TARSKI:def 1;
            hence thesis by A8,XBOOLE_0:def 2;
            end;
            case s<m;
            then A10: x in {k2: k2<m & k2 is prime} by A8;
      {k1: k1<m & k1 is prime}c={k3: k3<m & k3 is prime}\/{m} by XBOOLE_1:7;
            hence thesis by A10;
           end;
           end;
          hence thesis;
         end;
        hence thesis by TARSKI:def 3;
       end;
         {k1: k1<m & k1 is prime}\/{m}c={k2: k2<k & k2 is prime} by A3,A5,Th91;
      hence thesis by A7,XBOOLE_0:def 10;
      end;
     reconsider C = {k1:k1<k & k1 is prime} as finite set by Th88;
    take C;
     reconsider x=m as set;
    thus C = {k1:k1<k & k1 is prime};
        not x in {k1:k1<m & k1 is prime} by A3,Th89;
    hence n + 1 = card C by A3,A6,CARD_2:54;
    thus k is Prime by A5;
    end;
     for n holds _P1[n] from NAT_1:sch 1(A1,A2);
  then ex m st ex B being finite set st
   B = {k1:k1<m & k1 is prime} & n=card B & m is Prime;
 hence thesis;
end;
uniqueness
 proof let f,g be Prime;
  given B being finite set such that
A11: B = {k:k<f & k is prime} & n = card B;
  given B being finite set such that
A12: B = {k:k<g & k is prime} & n = card B;
  assume
A13: f<>g;
     f<g & n+1 <= n or g<f & n+1 <= n
  proof
   per cases by A13,AXIOMS:21;
    case f<g;
    then A14: {k1: k1<f & k1 is prime}\/{f}c={k2: k2<g & k2 is prime} by Th91;
    A15: {k2: k2<g & k2 is prime} is finite by Th88;
    A16: not f in {k1:k1<f & k1 is prime} by Th89;
      reconsider x=f as set;
      reconsider B1 = {k4: k4<f & k4 is prime}\/{x},
                 B2 = {k3: k3<g & k3 is prime} as finite set
                                     by A14,A15,FINSET_1:13;
        card B1 <= card B2 by A14,CARD_1:80;
    hence thesis by A11,A12,A16,CARD_2:54;
    end;
    case g<f;
    then A17: {k2: k2<g & k2 is prime}\/{g}c={k1: k1<f & k1 is prime} by Th91;
    A18: {k1: k1<f & k1 is prime} is finite by Th88;
    A19: not g in {k2:k2<g & k2 is prime} by Th89;
      reconsider x=g as set;
      reconsider B3 = {k4: k4<g & k4 is prime}\/{x},
                 B4 = {k3: k3<f & k3 is prime} as finite set
                                     by A17,A18,FINSET_1:13;
        card B3 <= card B4 by A17,CARD_1:80;
    hence thesis by A11,A12,A19,CARD_2:54;
   end;
   end;
  hence contradiction by NAT_1:38;
 end;
end;

theorem Th93:
  SetPrimenumber p = {k:k<p & k is prime}
proof
    for x holds x in SetPrimenumber p iff x in {k:k<p & k is prime}
  proof
   A1: for x holds x in SetPrimenumber p implies x in {k:k<p & k is prime}
   proof let x;
    assume A2: x in SetPrimenumber p;
    then reconsider x' = x as Nat;
       x' < p & x' is prime by A2,Def19;
    hence thesis;
   end;
      for x holds x in {k:k<p & k is prime} implies x in SetPrimenumber p
   proof let x;
    assume x in {k:k<p & k is prime};
    then ex n st n=x & n < p & n is prime;
    hence thesis by Def19;
   end;
   hence thesis by A1;
  end;
 hence thesis by TARSKI:2;
end;

theorem ::Euklidesa::
    SetPrimes is infinite
proof
 assume A1: SetPrimes is finite;
 then reconsider SP = SetPrimes as finite set;
 consider n such that A2: SetPrimes,Seg n are_equipotent by A1,FINSEQ_1:77;
 set p=primenumber (n+1);
     Card SetPrimes = Card Seg n by A2,CARD_1:21;
  then Card SetPrimes = Card n by FINSEQ_1:76;
 then A3: card SP = n by CARD_1:def 11;
A4: SetPrimenumber p c= SP by Th77;
  then reconsider SPp = SetPrimenumber p as finite set by FINSET_1:13;
 A5: card SPp <= card SP by A4,CARD_1:80;
  consider B being finite set such that
A6: B = {k:k< p & k is prime} & n + 1 = card B by Def20;
    n+1 <= n by A3,A5,A6,Th93;
 hence contradiction by NAT_1:38;
end;

reserve m,m1,p',p1,i for Nat;

Lm15: n is prime implies n > 0 by INT_2:def 5;

theorem :: divisibility
           for i st i is prime holds
   for m,n holds i divides m * n implies i divides m or i divides n
proof
  let i;
  assume A1: i is prime;
   given m,n such that
   A2: not ( i divides m * n implies (i divides m or i divides n) );
   defpred _P[Nat] means
   $1 is prime &
   ex m,n st $1 divides m * n & (not $1 divides m) & (not $1 divides n);
   A3: ex i st _P[i] by A1,A2;
   consider p' such that
     A4: _P[p'] and
     A5: for p1 st _P[p1] holds p' <= p1 from NAT_1:sch 5(A3);
    defpred _Q[Nat] means
    ex n st p' divides $1 * n & (not p' divides $1) & (not p' divides n);
A6: ex m st _Q[m] by A4;
   consider m such that
     A7: _Q[m] and
     A8: for m1 st _Q[m1] holds m <= m1 from NAT_1:sch 5(A6);
      m <> 0 by A7;
   then A9: m > 0 by NAT_1:19;
   consider n such that
   A10: p' divides m * n & (not p' divides m) & (not p' divides n) by A7;

   A11: m < p'
       proof
        assume A12: m >= p';
        set m1 = m mod p';
        A13: p' > 0 by A4,Lm15;
        then m1 < p' by NAT_1:46;
        then A14: m1 < m by A12,AXIOMS:22;
           A15: p' divides p' * (m div p') by NAT_1:def 3;
           A16: m = p'* (m div p') + m1 by A13,NAT_1:47;
        A17: p' divides m1 * n
          proof
           A18: p' divides p' * ((m div p') * n) by NAT_1:def 3;
               m = p'* (m div p') + m1 by A13,NAT_1:47;

           then m * n = p' * (m div p') * n + m1 * n;
           hence thesis by A10,A18,NAT_1:57;
          end;
            not p' divides m1 by A10,A15,A16,NAT_1:55;
        hence contradiction by A8,A10,A14,A17;
       end;

   A19: m < 2
   proof
      assume m >= 2;
      then consider p1 such that
             A20: p1 is prime & p1 divides m by INT_2:48;
           A21:p1 > 1 by A20,INT_2:def 5;
           A22:p1 <> 0 by A20,INT_2:def 5;
          p1 <= m by A9,A20,NAT_1:54;
then A23:      not p' <= p1 by A11,AXIOMS:22;
      A24: not p1 divides p'
          proof
           assume p1 divides p';
            then p1 = 1 or p1 = p' by A4,INT_2:def 5;
           hence contradiction by A9,A11,A20,INT_2:def 5,NAT_1:54;
          end;
      consider k such that
      A25: m * n = p' * k by A10,NAT_1:def 3;
          p1 divides p' * k by A20,A25,NAT_1:56;
      then p1 divides k by A5,A20,A23,A24;
      then consider k1 being Nat such that A26: k = p1 * k1 by NAT_1:def 3;
      consider m1 such that A27: m = p1 * m1 by A20,NAT_1:def 3;
      A28: m1 > 0
      proof
         m1 <> 0 by A7,A27;
       hence thesis by NAT_1:18;
      end;
      A29: m1 < m
           proof
                m1 divides m by A27,NAT_1:def 3;
             then A30: m1 <= m by A9,NAT_1:54;
                m1 <> m
               proof
                assume m1 = m;
                then m = 1 * m1;
                hence contradiction by A21,A27,A28,REAL_1:70;
               end;
             hence thesis by A30,REAL_1:def 5;
           end;
      A31: p' divides m1 * n
        proof
             m * n = p1 *( m1 * n) by A27;
             then p1 *( m1 * n) = p1 *(p' * k1) by A25,A26;
             then m1 * n = p' * k1 by A22,XCMPLX_1:5;
             hence thesis by NAT_1:def 3;
        end;
          not p' divides m1 by A10,A27,NAT_1:56;
      hence contradiction by A8,A10,A29,A31;
   end;

       m >= 2
   proof
     assume m < 2;
     then A32: m <= 1 + 1 & m <> 2;
        m >= 0 + 1 by A9,NAT_1:38;
     then m = 1 by A32,NAT_1:27;
     hence contradiction by A10;
   end;
   hence thesis by A19;
   end;

:: from URYSOHN1, the proof from CARD_4

theorem
   m|^n is Nat
    proof
      defpred P[Nat] means m|^$1 is Nat;
A1:    P[0] by Th9;
A2:    P[k] implies P[k+1]
       proof assume m|^k is Nat;
        then reconsider x = m|^k as Nat;
            m|^(k+1) = x*m by Th11;
        hence thesis;
       end;
         P[k] from NAT_1:sch 1(A1,A2);
     hence thesis;
    end;

