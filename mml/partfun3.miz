:: On the Real Valued Functions
::  by Artur Korni{\l}owicz
::
:: Received December 10, 2004
:: Copyright (c) 2004 Association of Mizar Users
 
environ

 vocabularies NUMBERS, XREAL_0, ORDINAL1, ARYTM_3, XXREAL_0, CARD_1, RELAT_1,
      SQUARE_1, FUNCT_1, TARSKI, XBOOLE_0, FUNCOP_1, ARYTM_1, SUBSET_1,
      VALUED_0, ORDINAL4, PARTFUN1, VALUED_1, XCMPLX_0, PRALG_1, COMPLEX1,
      MEMBERED, PRE_TOPC, ORDINAL2, PSCOMP_1, STRUCT_0, TOPMETR, REAL_1,
      RCOMP_1, PARTFUN3;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, COMPLEX1, MEMBERED, SQUARE_1,
      RELAT_1, FUNCT_1, RELSET_1, PARTFUN1, FUNCT_2, MEASURE6, PSCOMP_1,
      VALUED_0, VALUED_1, SEQ_1, RFUNCT_1, NUMBERS, XCMPLX_0, XXREAL_0,
      XREAL_0, REAL_1, FUNCOP_1, RCOMP_1, STRUCT_0, PRE_TOPC, TOPALG_2;
 constructors REAL_1, SQUARE_1, COMPLEX1, RCOMP_1, RFUNCT_1, MEASURE6,
      FUNCOP_1, TOPALG_2, SEQ_1, PSCOMP_1;
 registrations XBOOLE_0, FUNCT_1, RELSET_1, FUNCOP_1, NUMBERS, XCMPLX_0,
      XXREAL_0, XREAL_0, MEMBERED, STRUCT_0, PRE_TOPC, TOPMETR, MEASURE6,
      VALUED_0, VALUED_1, FUNCT_2, PSCOMP_1;
 requirements NUMERALS, BOOLE, SUBSET, REAL, ARITHM;
 definitions MEASURE6, RELAT_1, VALUED_1, VALUED_0, STRUCT_0, PSCOMP_1;
 theorems TARSKI, FUNCT_2, RELAT_1, FUNCT_1, ZFMISC_1, JGRAPH_2, XBOOLE_1,
      XBOOLE_0, FUNCOP_1, TOPMETR, RFUNCT_1, SQUARE_1, XCMPLX_0, JGRAPH_3,
      TOPREAL6, JGRAPH_4, XREAL_1, COMPLEX1, VALUED_0, VALUED_1, RELSET_1;
 schemes FUNCT_1;

begin

registration
  let r be real number;
  cluster r/r -> non negative;
  coherence
  proof
    r <= 0 or 0 <= r;
    hence thesis;
  end;
end;

registration
  let r be real number;
  cluster r*r -> non negative;
  coherence by XREAL_1:65;
  cluster r*(r") -> non negative;
  coherence
  proof
    r*(r") = r/r by XCMPLX_0:def 9;
    hence thesis;
  end;
end;

registration
  let r be non negative (real number);
  cluster sqrt r -> non negative;
  coherence by SQUARE_1:def 4;
end;

registration
  let r be positive (real number);
  cluster sqrt r -> positive;
  coherence by SQUARE_1:93;
end;

theorem
  for f being Function, A being set st f is one-to-one & A c= dom (f")
  holds f.:(f".:A) = A
proof
  let f be Function, A be set;
  assume that
A1: f is one-to-one and
A2: A c= dom (f");
  f"".:(f".:A)=A by A1,A2,FUNCT_1:177;
  hence thesis by A1,FUNCT_1:65;
end;

registration
  let f be non-empty Function;
  cluster f"{0} -> empty;
  coherence
  proof
    assume not thesis;
    then consider x being set such that
A1: x in f"{0} by XBOOLE_0:def 1;
    x in dom f by A1,FUNCT_1:def 13;
    then
A2: f.x in rng f by FUNCT_1:def 5;
    f.x in {0} by A1,FUNCT_1:def 13;
    then f.x = 0 by TARSKI:def 1;
    hence thesis by A2,RELAT_1:def 9;
  end;
end;

definition
  let R be Relation;
  attr R is positive-yielding means
  :Def1:
  for r being real number st r in rng R holds 0 < r;
  attr R is negative-yielding means
  :Def2:
  for r being real number st r in rng R holds 0 > r;
  attr R is nonpositive-yielding means
  :Def3:
  for r being real number st r in rng R holds 0 >= r;
  attr R is nonnegative-yielding means
  :Def4:
  for r being real number st r in rng R holds 0 <= r;
end;

registration
  let X be set, r be positive (real number);
  cluster X --> r -> positive-yielding;
  coherence
  proof
    let x be real number;
    assume x in rng (X --> r);
    hence thesis by TARSKI:def 1;
  end;
end;

registration
  let X be set, r be negative (real number);
  cluster X --> r -> negative-yielding;
  coherence
  proof
    let x be real number;
    assume x in rng (X --> r);
    hence thesis by TARSKI:def 1;
  end;
end;

registration
  let X be set, r be non positive (real number);
  cluster X --> r -> nonpositive-yielding;
  coherence
  proof
    let x be real number;
    assume x in rng (X --> r);
    hence thesis by TARSKI:def 1;
  end;
end;

registration
  let X be set, r be non negative (real number);
  cluster X --> r -> nonnegative-yielding;
  coherence
  proof
    let x be real number;
    assume x in rng (X --> r);
    hence thesis by TARSKI:def 1;
  end;
end;

registration
  let X be non empty set;
  cluster X --> 0 -> non non-empty;
  coherence
  proof
    rng (X --> 0) = {0} by FUNCOP_1:14;
    hence {} in rng (X --> 0) by TARSKI:def 1;
  end;
end;

registration
  cluster positive-yielding -> nonnegative-yielding non-empty Relation;
  coherence
  proof
    let R be Relation such that
A1: for r being real number st r in rng R holds 0 < r;
    thus R is nonnegative-yielding
    proof
      let r be real number;
      thus thesis by A1;
    end;
    assume not thesis;
    then 0 in rng R by RELAT_1:def 9;
    hence thesis by A1;
  end;
  cluster negative-yielding -> nonpositive-yielding non-empty Relation;
  coherence
  proof
    let R be Relation such that
A2: for r being real number st r in rng R holds 0 > r;
    thus R is nonpositive-yielding
    proof
      let r be real number;
      thus thesis by A2;
    end;
    assume not thesis;
    then 0 in rng R by RELAT_1:def 9;
    hence thesis by A2;
  end;
end;

registration
  let X be set;
  cluster negative-yielding Function of X,REAL;
  existence
  proof
    take X --> -1;
    thus thesis;
  end;
  cluster positive-yielding Function of X,REAL;
  existence
  proof
    take X --> (1 qua Element of REAL);
    thus thesis;
  end;
end;

registration
  cluster non-empty real-valued Function;
  existence
  proof
    consider f being non-empty Function of 0,REAL;
    take f;
    thus thesis;
  end;
end;

theorem Th2:
  for f being non-empty real-valued Function holds dom(f^) = dom f
proof
  let f be non-empty real-valued Function;
  thus dom (f^) = dom f \ f"{0} by RFUNCT_1:def 8
    .= dom f;
end;

theorem Th3:
  for X being non empty set, f being PartFunc of X,REAL, g being
  non-empty PartFunc of X,REAL holds dom(f/g) = dom f /\ dom g
proof
  let X be non empty set, f be PartFunc of X,REAL, g be non-empty PartFunc of
  X,REAL;
  thus dom (f/g) = dom f /\ (dom g \ g"{0}) by RFUNCT_1:def 4
    .= dom f /\ dom g;
end;

registration
  let X be set;
  let f, g be nonpositive-yielding PartFunc of X,REAL;
  cluster f+g -> nonpositive-yielding;
  coherence
  proof
    let r be real number;
    set R = f+g;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
A3: dom R = dom f /\ dom g by VALUED_1:def 1;
    then x in dom g by A1,XBOOLE_0:def 4;
    then
A4: g.x in rng g by FUNCT_1:def 5;
    x in dom f by A1,A3,XBOOLE_0:def 4;
    then f.x in rng f by FUNCT_1:def 5;
    then reconsider a = f.x, b = g.x as non positive (real number) by A4,Def3;
    a+b is non positive;
    hence thesis by A1,A2,VALUED_1:def 1;
  end;
end;

registration
  let X be set;
  let f, g be nonnegative-yielding PartFunc of X,REAL;
  cluster f+g -> nonnegative-yielding;
  coherence
  proof
    let r be real number;
    set R = f+g;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
A3: dom R = dom f /\ dom g by VALUED_1:def 1;
    then x in dom g by A1,XBOOLE_0:def 4;
    then
A4: g.x in rng g by FUNCT_1:def 5;
    x in dom f by A1,A3,XBOOLE_0:def 4;
    then f.x in rng f by FUNCT_1:def 5;
    then reconsider a = f.x, b = g.x as non negative (real number) by A4,Def4;
    a+b is non negative;
    hence thesis by A1,A2,VALUED_1:def 1;
  end;
end;

registration
  let X be set;
  let f be positive-yielding PartFunc of X,REAL;
  let g be nonnegative-yielding PartFunc of X,REAL;
  cluster f+g -> positive-yielding;
  coherence
  proof
    let r be real number;
    set R = f+g;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
A3: dom R = dom f /\ dom g by VALUED_1:def 1;
    then x in dom f by A1,XBOOLE_0:def 4;
    then f.x in rng f by FUNCT_1:def 5;
    then reconsider a = f.x as positive (real number) by Def1;
    x in dom g by A1,A3,XBOOLE_0:def 4;
    then g.x in rng g by FUNCT_1:def 5;
    then reconsider b = g.x as non negative (real number) by Def4;
    a+b is positive;
    hence thesis by A1,A2,VALUED_1:def 1;
  end;
end;

registration
  let X be set;
  let f be nonnegative-yielding PartFunc of X,REAL;
  let g be positive-yielding PartFunc of X,REAL;
  cluster f+g -> positive-yielding;
  coherence;
end;

registration
  let X be set;
  let f be nonpositive-yielding PartFunc of X,REAL;
  let g be negative-yielding PartFunc of X,REAL;
  cluster f+g -> negative-yielding;
  coherence
  proof
    let r be real number;
    set R = f+g;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
A3: dom R = dom f /\ dom g by VALUED_1:def 1;
    then x in dom f by A1,XBOOLE_0:def 4;
    then f.x in rng f by FUNCT_1:def 5;
    then reconsider a = f.x as non positive (real number) by Def3;
    x in dom g by A1,A3,XBOOLE_0:def 4;
    then g.x in rng g by FUNCT_1:def 5;
    then reconsider b = g.x as negative (real number) by Def2;
    a+b is negative;
    hence thesis by A1,A2,VALUED_1:def 1;
  end;
end;

registration
  let X be set;
  let f be negative-yielding PartFunc of X,REAL;
  let g be nonpositive-yielding PartFunc of X,REAL;
  cluster f+g -> negative-yielding;
  coherence;
end;

registration
  let X be set;
  let f be nonnegative-yielding PartFunc of X,REAL;
  let g be nonpositive-yielding PartFunc of X,REAL;
  cluster f-g -> nonnegative-yielding;
  coherence
  proof
    let r be real number;
    set R = f-g;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
A3: dom R = dom f /\ dom g by VALUED_1:12;
    then x in dom f by A1,XBOOLE_0:def 4;
    then f.x in rng f by FUNCT_1:def 5;
    then reconsider a = f.x as non negative (real number) by Def4;
    x in dom g by A1,A3,XBOOLE_0:def 4;
    then g.x in rng g by FUNCT_1:def 5;
    then reconsider b = g.x as non positive (real number) by Def3;
    a-b is non negative;
    hence thesis by A1,A2,VALUED_1:13;
  end;
end;

registration
  let X be set;
  let f be nonpositive-yielding PartFunc of X,REAL;
  let g be nonnegative-yielding PartFunc of X,REAL;
  cluster f-g -> nonpositive-yielding;
  coherence
  proof
    let r be real number;
    set R = f-g;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
A3: dom R = dom f /\ dom g by VALUED_1:12;
    then x in dom f by A1,XBOOLE_0:def 4;
    then f.x in rng f by FUNCT_1:def 5;
    then reconsider a = f.x as non positive (real number) by Def3;
    x in dom g by A1,A3,XBOOLE_0:def 4;
    then g.x in rng g by FUNCT_1:def 5;
    then reconsider b = g.x as non negative (real number) by Def4;
    a-b is non positive;
    hence thesis by A1,A2,VALUED_1:13;
  end;
end;

registration
  let X be set;
  let f be positive-yielding PartFunc of X,REAL;
  let g be nonpositive-yielding PartFunc of X,REAL;
  cluster f-g -> positive-yielding;
  coherence
  proof
    let r be real number;
    set R = f-g;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
A3: dom R = dom f /\ dom g by VALUED_1:12;
    then x in dom f by A1,XBOOLE_0:def 4;
    then f.x in rng f by FUNCT_1:def 5;
    then reconsider a = f.x as positive (real number) by Def1;
    x in dom g by A1,A3,XBOOLE_0:def 4;
    then g.x in rng g by FUNCT_1:def 5;
    then reconsider b = g.x as non positive (real number) by Def3;
    a-b is positive;
    hence thesis by A1,A2,VALUED_1:13;
  end;
end;

registration
  let X be set;
  let f be nonpositive-yielding PartFunc of X,REAL;
  let g be positive-yielding PartFunc of X,REAL;
  cluster f-g -> negative-yielding;
  coherence
  proof
    let r be real number;
    set R = f-g;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
A3: dom R = dom f /\ dom g by VALUED_1:12;
    then x in dom f by A1,XBOOLE_0:def 4;
    then f.x in rng f by FUNCT_1:def 5;
    then reconsider a = f.x as non positive (real number) by Def3;
    x in dom g by A1,A3,XBOOLE_0:def 4;
    then g.x in rng g by FUNCT_1:def 5;
    then reconsider b = g.x as positive (real number) by Def1;
    a-b is negative;
    hence thesis by A1,A2,VALUED_1:13;
  end;
end;

registration
  let X be set;
  let f be negative-yielding PartFunc of X,REAL;
  let g be nonnegative-yielding PartFunc of X,REAL;
  cluster f-g -> negative-yielding;
  coherence
  proof
    let r be real number;
    set R = f-g;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
A3: dom R = dom f /\ dom g by VALUED_1:12;
    then x in dom f by A1,XBOOLE_0:def 4;
    then f.x in rng f by FUNCT_1:def 5;
    then reconsider a = f.x as negative (real number) by Def2;
    x in dom g by A1,A3,XBOOLE_0:def 4;
    then g.x in rng g by FUNCT_1:def 5;
    then reconsider b = g.x as non negative (real number) by Def4;
    a-b is negative;
    hence thesis by A1,A2,VALUED_1:13;
  end;
end;

registration
  let X be set;
  let f be nonnegative-yielding PartFunc of X,REAL;
  let g be negative-yielding PartFunc of X,REAL;
  cluster f-g -> positive-yielding;
  coherence
  proof
    let r be real number;
    set R = f-g;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
A3: dom R = dom f /\ dom g by VALUED_1:12;
    then x in dom f by A1,XBOOLE_0:def 4;
    then f.x in rng f by FUNCT_1:def 5;
    then reconsider a = f.x as non negative (real number) by Def4;
    x in dom g by A1,A3,XBOOLE_0:def 4;
    then g.x in rng g by FUNCT_1:def 5;
    then reconsider b = g.x as negative (real number) by Def2;
    a-b is positive;
    hence thesis by A1,A2,VALUED_1:13;
  end;
end;

registration
  let X be set;
  let f, g be nonpositive-yielding PartFunc of X,REAL;
  cluster f(#)g -> nonnegative-yielding;
  coherence
  proof
    let r be real number;
    set R = f(#)g;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
A3: dom R = dom f /\ dom g by VALUED_1:def 4;
    then x in dom g by A1,XBOOLE_0:def 4;
    then
A4: g.x in rng g by FUNCT_1:def 5;
    x in dom f by A1,A3,XBOOLE_0:def 4;
    then f.x in rng f by FUNCT_1:def 5;
    then reconsider a = f.x, b = g.x as non positive (real number) by A4,Def3;
    a*b is non negative;
    hence thesis by A2,VALUED_1:5;
  end;
end;

registration
  let X be set;
  let f, g be nonnegative-yielding PartFunc of X,REAL;
  cluster f(#)g -> nonnegative-yielding;
  coherence
  proof
    let r be real number;
    set R = f(#)g;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
A3: dom R = dom f /\ dom g by VALUED_1:def 4;
    then x in dom g by A1,XBOOLE_0:def 4;
    then
A4: g.x in rng g by FUNCT_1:def 5;
    x in dom f by A1,A3,XBOOLE_0:def 4;
    then f.x in rng f by FUNCT_1:def 5;
    then reconsider a = f.x, b = g.x as non negative (real number) by A4,Def4;
    a*b is non negative;
    hence thesis by A2,VALUED_1:5;
  end;
end;

registration
  let X be set;
  let f be nonpositive-yielding PartFunc of X,REAL;
  let g be nonnegative-yielding PartFunc of X,REAL;
  cluster f(#)g -> nonpositive-yielding;
  coherence
  proof
    let r be real number;
    set R = f(#)g;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
A3: dom R = dom f /\ dom g by VALUED_1:def 4;
    then x in dom f by A1,XBOOLE_0:def 4;
    then f.x in rng f by FUNCT_1:def 5;
    then reconsider a = f.x as non positive (real number) by Def3;
    x in dom g by A1,A3,XBOOLE_0:def 4;
    then g.x in rng g by FUNCT_1:def 5;
    then reconsider b = g.x as non negative (real number) by Def4;
    a*b is non positive;
    hence thesis by A2,VALUED_1:5;
  end;
end;

registration
  let X be set;
  let f be nonnegative-yielding PartFunc of X,REAL;
  let g be nonpositive-yielding PartFunc of X,REAL;
  cluster f(#)g -> nonpositive-yielding;
  coherence;
end;

registration
  let X be set;
  let f be positive-yielding PartFunc of X,REAL;
  let g be negative-yielding PartFunc of X,REAL;
  cluster f(#)g -> negative-yielding;
  coherence
  proof
    let r be real number;
    set R = f(#)g;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
A3: dom R = dom f /\ dom g by VALUED_1:def 4;
    then x in dom f by A1,XBOOLE_0:def 4;
    then f.x in rng f by FUNCT_1:def 5;
    then reconsider a = f.x as positive (real number) by Def1;
    x in dom g by A1,A3,XBOOLE_0:def 4;
    then g.x in rng g by FUNCT_1:def 5;
    then reconsider b = g.x as negative (real number) by Def2;
    a*b is negative;
    hence thesis by A2,VALUED_1:5;
  end;
end;

registration
  let X be set;
  let f be negative-yielding PartFunc of X,REAL;
  let g be positive-yielding PartFunc of X,REAL;
  cluster f(#)g -> negative-yielding;
  coherence;
end;

registration
  let X be set;
  let f, g be positive-yielding PartFunc of X,REAL;
  cluster f(#)g -> positive-yielding;
  coherence
  proof
    let r be real number;
    set R = f(#)g;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
A3: dom R = dom f /\ dom g by VALUED_1:def 4;
    then x in dom g by A1,XBOOLE_0:def 4;
    then
A4: g.x in rng g by FUNCT_1:def 5;
    x in dom f by A1,A3,XBOOLE_0:def 4;
    then f.x in rng f by FUNCT_1:def 5;
    then reconsider a = f.x, b = g.x as positive (real number) by A4,Def1;
    a*b is positive;
    hence thesis by A2,VALUED_1:5;
  end;
end;

registration
  let X be set;
  let f, g be negative-yielding PartFunc of X,REAL;
  cluster f(#)g -> positive-yielding;
  coherence
  proof
    let r be real number;
    set R = f(#)g;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
A3: dom R = dom f /\ dom g by VALUED_1:def 4;
    then x in dom g by A1,XBOOLE_0:def 4;
    then
A4: g.x in rng g by FUNCT_1:def 5;
    x in dom f by A1,A3,XBOOLE_0:def 4;
    then f.x in rng f by FUNCT_1:def 5;
    then reconsider a = f.x, b = g.x as negative (real number) by A4,Def2;
    a*b is positive;
    hence thesis by A2,VALUED_1:5;
  end;
end;

registration
  let X be set;
  let f, g be non-empty PartFunc of X,REAL;
  cluster f(#)g -> non-empty;
  coherence
  proof
    set R = f(#)g;
    assume not thesis;
    then 0 in rng R by RELAT_1:def 9;
    then consider x being set such that
A1: x in dom R and
A2: R.x = 0 by FUNCT_1:def 5;
A3: dom R = dom f /\ dom g by VALUED_1:def 4;
    then x in dom g by A1,XBOOLE_0:def 4;
    then
A4: g.x in rng g by FUNCT_1:def 5;
    x in dom f by A1,A3,XBOOLE_0:def 4;
    then f.x in rng f by FUNCT_1:def 5;
    then reconsider a = f.x, b = g.x as non zero (real number) by
    A4,RELAT_1:def
    9;
    a*b is non zero;
    hence thesis by A2,VALUED_1:5;
  end;
end;

registration
  let X be set;
  let f be PartFunc of X,REAL;
  cluster f(#)f -> nonnegative-yielding;
  coherence
  proof
    let r be real number;
    set R = f(#)f;
    assume r in rng R;
    then consider x being set such that
    x in dom R and
A1: R.x = r by FUNCT_1:def 5;
    f.x * f.x is non negative;
    hence thesis by A1,VALUED_1:5;
  end;
end;

registration
  let X be set;
  let r be non positive (real number);
  let f be nonpositive-yielding PartFunc of X,REAL;
  cluster r(#)f -> nonnegative-yielding;
  coherence
  proof
    let z be real number;
    set R = r(#)f;
    assume z in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = z by FUNCT_1:def 5;
    dom R = dom f by VALUED_1:def 5;
    then f.x in rng f by A1,FUNCT_1:def 5;
    then reconsider a = f.x as non positive (real number) by Def3;
    r*a is non negative;
    hence thesis by A1,A2,VALUED_1:def 5;
  end;
end;

registration
  let X be set;
  let r be non negative (real number);
  let f be nonnegative-yielding PartFunc of X,REAL;
  cluster r(#)f -> nonnegative-yielding;
  coherence
  proof
    let z be real number;
    set R = r(#)f;
    assume z in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = z by FUNCT_1:def 5;
    dom R = dom f by VALUED_1:def 5;
    then f.x in rng f by A1,FUNCT_1:def 5;
    then reconsider a = f.x as non negative (real number) by Def4;
    r*a is non negative;
    hence thesis by A1,A2,VALUED_1:def 5;
  end;
end;

registration
  let X be set;
  let r be non positive (real number);
  let f be nonnegative-yielding PartFunc of X,REAL;
  cluster r(#)f -> nonpositive-yielding;
  coherence
  proof
    let z be real number;
    set R = r(#)f;
    assume z in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = z by FUNCT_1:def 5;
    dom R = dom f by VALUED_1:def 5;
    then f.x in rng f by A1,FUNCT_1:def 5;
    then reconsider a = f.x as non negative (real number) by Def4;
    r*a is non positive;
    hence thesis by A1,A2,VALUED_1:def 5;
  end;
end;

registration
  let X be set;
  let r be non negative (real number);
  let f be nonpositive-yielding PartFunc of X,REAL;
  cluster r(#)f -> nonpositive-yielding;
  coherence
  proof
    let z be real number;
    set R = r(#)f;
    assume z in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = z by FUNCT_1:def 5;
    dom R = dom f by VALUED_1:def 5;
    then f.x in rng f by A1,FUNCT_1:def 5;
    then reconsider a = f.x as non positive (real number) by Def3;
    r*a is non positive;
    hence thesis by A1,A2,VALUED_1:def 5;
  end;
end;

registration
  let X be set;
  let r be positive (real number);
  let f be negative-yielding PartFunc of X,REAL;
  cluster r(#)f -> negative-yielding;
  coherence
  proof
    let z be real number;
    set R = r(#)f;
    assume z in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = z by FUNCT_1:def 5;
    dom R = dom f by VALUED_1:def 5;
    then f.x in rng f by A1,FUNCT_1:def 5;
    then reconsider a = f.x as negative (real number) by Def2;
    r*a is negative;
    hence thesis by A1,A2,VALUED_1:def 5;
  end;
end;

registration
  let X be set;
  let r be negative (real number);
  let f be positive-yielding PartFunc of X,REAL;
  cluster r(#)f -> negative-yielding;
  coherence
  proof
    let z be real number;
    set R = r(#)f;
    assume z in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = z by FUNCT_1:def 5;
    dom R = dom f by VALUED_1:def 5;
    then f.x in rng f by A1,FUNCT_1:def 5;
    then reconsider a = f.x as positive (real number) by Def1;
    r*a is negative;
    hence thesis by A1,A2,VALUED_1:def 5;
  end;
end;

registration
  let X be set;
  let r be positive (real number);
  let f be positive-yielding PartFunc of X,REAL;
  cluster r(#)f -> positive-yielding;
  coherence
  proof
    let z be real number;
    set R = r(#)f;
    assume z in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = z by FUNCT_1:def 5;
    dom R = dom f by VALUED_1:def 5;
    then f.x in rng f by A1,FUNCT_1:def 5;
    then reconsider a = f.x as positive (real number) by Def1;
    r*a is positive;
    hence thesis by A1,A2,VALUED_1:def 5;
  end;
end;

registration
  let X be set;
  let r be negative (real number);
  let f be negative-yielding PartFunc of X,REAL;
  cluster r(#)f -> positive-yielding;
  coherence
  proof
    let z be real number;
    set R = r(#)f;
    assume z in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = z by FUNCT_1:def 5;
    dom R = dom f by VALUED_1:def 5;
    then f.x in rng f by A1,FUNCT_1:def 5;
    then reconsider a = f.x as negative (real number) by Def2;
    r*a is positive;
    hence thesis by A1,A2,VALUED_1:def 5;
  end;
end;

registration
  let X be set;
  let r be non zero (real number);
  let f be non-empty PartFunc of X,REAL;
  cluster r(#)f -> non-empty;
  coherence
  proof
    set R = r(#)f;
    assume not thesis;
    then 0 in rng R by RELAT_1:def 9;
    then consider x being set such that
A1: x in dom R and
A2: R.x = 0 by FUNCT_1:def 5;
    dom R = dom f by VALUED_1:def 5;
    then f.x in rng f by A1,FUNCT_1:def 5;
    then reconsider a = f.x as non zero (real number) by RELAT_1:def 9;
    r*a is non zero;
    hence thesis by A1,A2,VALUED_1:def 5;
  end;
end;

registration
  let X be non empty set;
  let f, g be nonpositive-yielding PartFunc of X,REAL;
  cluster f/g -> nonnegative-yielding;
  coherence
  proof
    let r be real number;
    set R = f/g;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
A3: dom R = dom f /\ (dom g \ g"{0}) by RFUNCT_1:def 4;
    then x in dom g \ g"{0} by A1,XBOOLE_0:def 4;
    then x in dom g by XBOOLE_0:def 5;
    then
A4: g.x in rng g by FUNCT_1:def 5;
    x in dom f by A1,A3,XBOOLE_0:def 4;
    then f.x in rng f by FUNCT_1:def 5;
    then reconsider a = f.x, b = g.x as non positive (real number) by A4,Def3;
    a*b" is non negative;
    hence thesis by A1,A2,RFUNCT_1:def 4;
  end;
end;

registration
  let X be non empty set;
  let f, g be nonnegative-yielding PartFunc of X,REAL;
  cluster f/g -> nonnegative-yielding;
  coherence
  proof
    let r be real number;
    set R = f/g;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
A3: dom R = dom f /\ (dom g \ g"{0}) by RFUNCT_1:def 4;
    then x in dom g \ g"{0} by A1,XBOOLE_0:def 4;
    then x in dom g by XBOOLE_0:def 5;
    then
A4: g.x in rng g by FUNCT_1:def 5;
    x in dom f by A1,A3,XBOOLE_0:def 4;
    then f.x in rng f by FUNCT_1:def 5;
    then reconsider a = f.x, b = g.x as non negative (real number) by A4,Def4;
    a*b" is non negative;
    hence thesis by A1,A2,RFUNCT_1:def 4;
  end;
end;

registration
  let X be non empty set;
  let f be nonpositive-yielding PartFunc of X,REAL;
  let g be nonnegative-yielding PartFunc of X,REAL;
  cluster f/g -> nonpositive-yielding;
  coherence
  proof
    let r be real number;
    set R = f/g;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
A3: dom R = dom f /\ (dom g \ g"{0}) by RFUNCT_1:def 4;
    then x in dom f by A1,XBOOLE_0:def 4;
    then f.x in rng f by FUNCT_1:def 5;
    then reconsider a = f.x as non positive (real number) by Def3;
    x in dom g \ g"{0} by A1,A3,XBOOLE_0:def 4;
    then x in dom g by XBOOLE_0:def 5;
    then g.x in rng g by FUNCT_1:def 5;
    then reconsider b = g.x as non negative (real number) by Def4;
    a*b" is non positive;
    hence thesis by A1,A2,RFUNCT_1:def 4;
  end;
end;

registration
  let X be non empty set;
  let f be nonnegative-yielding PartFunc of X,REAL;
  let g be nonpositive-yielding PartFunc of X,REAL;
  cluster f/g -> nonpositive-yielding;
  coherence
  proof
    let r be real number;
    set R = f/g;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
A3: dom R = dom f /\ (dom g \ g"{0}) by RFUNCT_1:def 4;
    then x in dom f by A1,XBOOLE_0:def 4;
    then f.x in rng f by FUNCT_1:def 5;
    then reconsider a = f.x as non negative (real number) by Def4;
    x in dom g \ g"{0} by A1,A3,XBOOLE_0:def 4;
    then x in dom g by XBOOLE_0:def 5;
    then g.x in rng g by FUNCT_1:def 5;
    then reconsider b = g.x as non positive (real number) by Def3;
    a*b" is non positive;
    hence thesis by A1,A2,RFUNCT_1:def 4;
  end;
end;

registration
  let X be non empty set;
  let f be positive-yielding PartFunc of X,REAL;
  let g be negative-yielding PartFunc of X,REAL;
  cluster f/g -> negative-yielding;
  coherence
  proof
    let r be real number;
    set R = f/g;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
A3: dom R = dom f /\ (dom g \ g"{0}) by RFUNCT_1:def 4;
    then x in dom f by A1,XBOOLE_0:def 4;
    then f.x in rng f by FUNCT_1:def 5;
    then reconsider a = f.x as positive (real number) by Def1;
    x in dom g \ g"{0} by A1,A3,XBOOLE_0:def 4;
    then g.x in rng g by FUNCT_1:def 5;
    then reconsider b = g.x as negative (real number) by Def2;
    a*b" is negative;
    hence thesis by A1,A2,RFUNCT_1:def 4;
  end;
end;

registration
  let X be non empty set;
  let f be negative-yielding PartFunc of X,REAL;
  let g be positive-yielding PartFunc of X,REAL;
  cluster f/g -> negative-yielding;
  coherence
  proof
    let r be real number;
    set R = f/g;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
A3: dom R = dom f /\ (dom g \ g"{0}) by RFUNCT_1:def 4;
    then x in dom f by A1,XBOOLE_0:def 4;
    then f.x in rng f by FUNCT_1:def 5;
    then reconsider a = f.x as negative (real number) by Def2;
    x in dom g \ g"{0} by A1,A3,XBOOLE_0:def 4;
    then g.x in rng g by FUNCT_1:def 5;
    then reconsider b = g.x as positive (real number) by Def1;
    a*b" is negative;
    hence thesis by A1,A2,RFUNCT_1:def 4;
  end;
end;

registration
  let X be non empty set;
  let f, g be positive-yielding PartFunc of X,REAL;
  cluster f/g -> positive-yielding;
  coherence
  proof
    let r be real number;
    set R = f/g;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
A3: dom R = dom f /\ (dom g \ g"{0}) by RFUNCT_1:def 4;
    then x in dom g \ g"{0} by A1,XBOOLE_0:def 4;
    then
A4: g.x in rng g by FUNCT_1:def 5;
    x in dom f by A1,A3,XBOOLE_0:def 4;
    then f.x in rng f by FUNCT_1:def 5;
    then reconsider a = f.x, b = g.x as positive (real number) by A4,Def1;
    a*b" is positive;
    hence thesis by A1,A2,RFUNCT_1:def 4;
  end;
end;

registration
  let X be non empty set;
  let f, g be negative-yielding PartFunc of X,REAL;
  cluster f/g -> positive-yielding;
  coherence
  proof
    let r be real number;
    set R = f/g;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
A3: dom R = dom f /\ (dom g \ g"{0}) by RFUNCT_1:def 4;
    then x in dom g \ g"{0} by A1,XBOOLE_0:def 4;
    then
A4: g.x in rng g by FUNCT_1:def 5;
    x in dom f by A1,A3,XBOOLE_0:def 4;
    then f.x in rng f by FUNCT_1:def 5;
    then reconsider a = f.x, b = g.x as negative (real number) by A4,Def2;
    a*b" is positive;
    hence thesis by A1,A2,RFUNCT_1:def 4;
  end;
end;

registration
  let X be non empty set;
  let f be PartFunc of X,REAL;
  cluster f/f -> nonnegative-yielding;
  coherence
  proof
    let r be real number;
    set R = f/f;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R & R.x = r by FUNCT_1:def 5;
    f.x * (f.x)" is non negative;
    hence thesis by A1,RFUNCT_1:def 4;
  end;
end;

registration
  let X be non empty set;
  let f, g be non-empty PartFunc of X,REAL;
  cluster f/g -> non-empty;
  coherence
  proof
    set R = f/g;
    assume not thesis;
    then 0 in rng R by RELAT_1:def 9;
    then consider x being set such that
A1: x in dom R and
A2: R.x = 0 by FUNCT_1:def 5;
A3: dom R = dom f /\ (dom g \ g"{0}) by RFUNCT_1:def 4;
    then x in dom g \ g"{0} by A1,XBOOLE_0:def 4;
    then
A4: g.x in rng g by FUNCT_1:def 5;
    x in dom f by A1,A3,XBOOLE_0:def 4;
    then f.x in rng f by FUNCT_1:def 5;
    then reconsider a = f.x, b = g.x as non zero (real number) by
    A4,RELAT_1:def
    9;
    a*b" is non zero;
    hence thesis by A1,A2,RFUNCT_1:def 4;
  end;
end;

registration
  let X be set;
  let f be nonpositive-yielding Function of X,REAL;
  cluster Inv f -> nonpositive-yielding;
  coherence
  proof
    let r be real number;
    set R = Inv f;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
    dom R = X by FUNCT_2:def 1
      .= dom f by FUNCT_2:def 1;
    then f.x in rng f by A1,FUNCT_1:def 5;
    then reconsider a = f.x as non positive (real number) by Def3;
    a" is non positive;
    hence thesis by A2,VALUED_1:10;
  end;
end;

registration
  let X be set;
  let f be nonnegative-yielding Function of X,REAL;
  cluster Inv f -> nonnegative-yielding;
  coherence
  proof
    let r be real number;
    set R = Inv f;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
    dom R = X by FUNCT_2:def 1
      .= dom f by FUNCT_2:def 1;
    then f.x in rng f by A1,FUNCT_1:def 5;
    then reconsider a = f.x as non negative (real number) by Def4;
    a" is non negative;
    hence thesis by A2,VALUED_1:10;
  end;
end;

registration
  let X be set;
  let f be positive-yielding Function of X,REAL;
  cluster Inv f -> positive-yielding;
  coherence
  proof
    let r be real number;
    set R = Inv f;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
    dom R = X by FUNCT_2:def 1
      .= dom f by FUNCT_2:def 1;
    then f.x in rng f by A1,FUNCT_1:def 5;
    then reconsider a = f.x as positive (real number) by Def1;
    a" is positive;
    hence thesis by A2,VALUED_1:10;
  end;
end;

registration
  let X be set;
  let f be negative-yielding Function of X,REAL;
  cluster Inv f -> negative-yielding;
  coherence
  proof
    let r be real number;
    set R = Inv f;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
    dom R = X by FUNCT_2:def 1
      .= dom f by FUNCT_2:def 1;
    then f.x in rng f by A1,FUNCT_1:def 5;
    then reconsider a = f.x as negative (real number) by Def2;
    a" is negative;
    hence thesis by A2,VALUED_1:10;
  end;
end;

registration
  let X be set;
  let f be non-empty Function of X,REAL;
  cluster Inv f -> non-empty;
  coherence
  proof
    set R = Inv f;
    assume not thesis;
    then 0 in rng R by RELAT_1:def 9;
    then consider x being set such that
A1: x in dom R and
A2: R.x = 0 by FUNCT_1:def 5;
    dom R = X by FUNCT_2:def 1
      .= dom f by FUNCT_2:def 1;
    then reconsider a = f.x as non zero (real number) by A1;
    a" is non zero;
    hence thesis by A2,VALUED_1:10;
  end;
end;

registration
  let X be set;
  let f be non-empty Function of X,REAL;
  cluster -f -> non-empty;
  coherence;
end;

registration
  let X be set;
  let f be nonpositive-yielding Function of X,REAL;
  cluster -f -> nonnegative-yielding;
  coherence;
end;

registration
  let X be set;
  let f be nonnegative-yielding Function of X,REAL;
  cluster -f -> nonpositive-yielding;
  coherence;
end;

registration
  let X be set;
  let f be positive-yielding Function of X,REAL;
  cluster -f -> negative-yielding;
  coherence;
end;

registration
  let X be set;
  let f be negative-yielding Function of X,REAL;
  cluster -f -> positive-yielding;
  coherence;
end;

registration
  let X be set;
  let f be Function of X,REAL;
  cluster abs f -> nonnegative-yielding;
  coherence
  proof
    let r be real number;
    set R = abs f;
    assume r in rng R;
    then consider x being set such that
    x in dom R and
A1: R.x = r by FUNCT_1:def 5;
    abs (f.x) is non negative by COMPLEX1:132;
    hence thesis by A1,VALUED_1:18;
  end;
end;

registration
  let X be set;
  let f be non-empty Function of X,REAL;
  cluster abs f -> positive-yielding;
  coherence
  proof
    let r be real number;
    set R = abs f;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
    dom R = dom f by VALUED_1:def 11;
    then reconsider a = f.x as non zero (real number) by A1;
    abs a is positive by COMPLEX1:133;
    hence thesis by A2,VALUED_1:18;
  end;
end;

registration
  let X be non empty set;
  let f be nonpositive-yielding Function of X,REAL;
  cluster f^ -> nonpositive-yielding;
  coherence
  proof
    let r be real number;
    set R = f^;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
    dom R = dom f \ f"{0} by RFUNCT_1:def 8;
    then x in dom f by A1,XBOOLE_0:def 5;
    then f.x in rng f by FUNCT_1:def 5;
    then reconsider a = f.x as non positive (real number) by Def3;
    a" is non positive;
    hence thesis by A1,A2,RFUNCT_1:def 8;
  end;
end;

registration
  let X be non empty set;
  let f be nonnegative-yielding Function of X,REAL;
  cluster f^ -> nonnegative-yielding;
  coherence
  proof
    let r be real number;
    set R = f^;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
    dom R = dom f \ f"{0} by RFUNCT_1:def 8;
    then x in dom f by A1,XBOOLE_0:def 5;
    then f.x in rng f by FUNCT_1:def 5;
    then reconsider a = f.x as non negative (real number) by Def4;
    a" is non negative;
    hence thesis by A1,A2,RFUNCT_1:def 8;
  end;
end;

registration
  let X be non empty set;
  let f be positive-yielding Function of X,REAL;
  cluster f^ -> positive-yielding;
  coherence
  proof
    let r be real number;
    set R = f^;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
    dom R = dom f by Th2;
    then f.x in rng f by A1,FUNCT_1:def 5;
    then reconsider a = f.x as positive (real number) by Def1;
    a" is positive;
    hence thesis by A1,A2,RFUNCT_1:def 8;
  end;
end;

registration
  let X be non empty set;
  let f be negative-yielding Function of X,REAL;
  cluster f^ -> negative-yielding;
  coherence
  proof
    let r be real number;
    set R = f^;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
    dom R = dom f by Th2;
    then f.x in rng f by A1,FUNCT_1:def 5;
    then reconsider a = f.x as negative (real number) by Def2;
    a" is negative;
    hence thesis by A1,A2,RFUNCT_1:def 8;
  end;
end;

registration
  let X be non empty set;
  let f be non-empty Function of X,REAL;
  cluster f^ -> non-empty;
  coherence
  proof
    set R = f^;
    assume not thesis;
    then 0 in rng R by RELAT_1:def 9;
    then consider x being set such that
A1: x in dom R and
A2: R.x = 0 by FUNCT_1:def 5;
    dom R = dom f by Th2;
    then reconsider a = f.x as non zero (real number) by A1;
    a" is non zero;
    hence thesis by A1,A2,RFUNCT_1:def 8;
  end;
end;

definition
  let f be real-valued Function;
  func sqrt f -> Function means
  :Def5:
  dom it = dom f & for x being set st x in dom it holds it.x = sqrt(f.x);
  existence
  proof
    deffunc F(set) = sqrt (f.$1);
    ex h being Function st dom h = dom f & for x being set st x in dom f
    holds h.x = F(x) from FUNCT_1:sch 3;
    hence thesis;
  end;
  uniqueness
  proof
    let h, g be Function such that
A1: dom h = dom f and
A2: for c being set st c in dom h holds h.c = sqrt (f.c) and
A3: dom g = dom f and
A4: for c being set st c in dom g holds g.c = sqrt (f.c);
    now
      let x be set;
      assume
A5:   x in dom h;
      hence h.x = sqrt (f.x) by A2
        .= g.x by A1,A3,A4,A5;
    end;
    hence thesis by A1,A3,FUNCT_1:9;
  end;
end;

registration
  let f be real-valued Function;
  cluster sqrt f -> real-valued;
  coherence
  proof
    let x be set;
    set F = sqrt f;
    assume x in dom F;
    then F.x = sqrt(f.x) by Def5;
    hence thesis;
  end;
end;

definition
  let C be set, D be real-membered set, f be PartFunc of C,D;
  redefine func sqrt f -> PartFunc of C,REAL;
  coherence
  proof
    set F = sqrt f;
    dom F = dom f & rng F c= REAL by Def5,VALUED_0:def 3;
    hence thesis by RELSET_1:11;
  end;
end;

registration
  let X be set;
  let f be nonnegative-yielding Function of X,REAL;
  cluster sqrt f -> nonnegative-yielding;
  coherence
  proof
    let r be real number;
    set R = sqrt f;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
    dom R = dom f by Def5;
    then f.x in rng f by A1,FUNCT_1:def 5;
    then reconsider a = f.x as non negative (real number) by Def4;
    sqrt a is non negative;
    hence thesis by A1,A2,Def5;
  end;
end;

registration
  let X be set;
  let f be positive-yielding Function of X,REAL;
  cluster sqrt f -> positive-yielding;
  coherence
  proof
    let r be real number;
    set R = sqrt f;
    assume r in rng R;
    then consider x being set such that
A1: x in dom R and
A2: R.x = r by FUNCT_1:def 5;
    dom R = dom f by Def5;
    then f.x in rng f by A1,FUNCT_1:def 5;
    then reconsider a = f.x as positive (real number) by Def1;
    sqrt a is positive;
    hence thesis by A1,A2,Def5;
  end;
end;

definition
  let X be set, f be Function of X, REAL;
  redefine func sqrt f -> Function of X,REAL;
  coherence
  proof
    dom sqrt f = dom f by Def5
      .= X by FUNCT_2:def 1;
    hence thesis by FUNCT_2:def 1;
  end;
end;

definition
  let X be set, f be non-empty Function of X, REAL;
  redefine func f^ -> Function of X,REAL;
  coherence
  proof
    dom (f^) = dom f by Th2
      .= X by FUNCT_2:def 1;
    hence thesis by FUNCT_2:def 1;
  end;
end;

definition
  let X be non empty set, f be Function of X, REAL, g be non-empty Function of
  X, REAL;
  redefine func f/g -> Function of X,REAL;
  coherence
  proof
    dom (f/g) = dom f /\ dom g by Th3
      .= X /\ dom g by FUNCT_2:def 1
      .= X /\ X by FUNCT_2:def 1;
    hence thesis by FUNCT_2:def 1;
  end;
end;

reserve T for non empty TopSpace,
  f, g for continuous RealMap of T,
  r for real number;

definition
  let T, f, g;
  set c = the carrier of T;
  redefine func f+g -> continuous RealMap of T;
  coherence
  proof
    reconsider F = f, G = g as continuous Function of T,R^1 by TOPMETR:24,
    TOPREAL6:83;
    consider H being Function of T,R^1 such that
A1: for p being Point of T, r1,r2 being real number st F.p = r1 & G.p
    = r2 holds H.p = r1+r2 and
A2: H is continuous by JGRAPH_2:29;
    reconsider h = H as RealMap of T by TOPMETR:24;
A3: dom h = c /\ c by FUNCT_2:def 1
      .= c /\ dom g by FUNCT_2:def 1
      .= dom f /\ dom g by FUNCT_2:def 1;
    for c being set st c in dom h holds h.c = f.c + g.c by A1;
    then h = f+g by A3,VALUED_1:def 1;
    hence thesis by A2,TOPREAL6:83;
  end;
  redefine func f-g -> continuous RealMap of T;
  coherence
  proof
    reconsider F = f, G = g as continuous Function of T,R^1 by TOPMETR:24,
    TOPREAL6:83;
    consider H being Function of T,R^1 such that
A4: for p being Point of T, r1,r2 being real number st F.p = r1 & G.p
    = r2 holds H.p = r1-r2 and
A5: H is continuous by JGRAPH_2:31;
    reconsider h = H as RealMap of T by TOPMETR:24;
A6: dom h = c /\ c by FUNCT_2:def 1
      .= c /\ dom g by FUNCT_2:def 1
      .= dom f /\ dom g by FUNCT_2:def 1;
    dom (f-g) = dom f /\ dom g & for c being set st c in dom h holds h.c =
    f.c - g.c by A4,VALUED_1:12;
    then h = f-g by A6,VALUED_1:14;
    hence thesis by A5,TOPREAL6:83;
  end;
  redefine func f(#)g -> continuous RealMap of T;
  coherence
  proof
    reconsider F = f, G = g as continuous Function of T,R^1 by TOPMETR:24,
    TOPREAL6:83;
    consider H being Function of T,R^1 such that
A7: for p being Point of T, r1,r2 being real number st F.p = r1 & G.p
    = r2 holds H.p = r1*r2 and
A8: H is continuous by JGRAPH_2:35;
    reconsider h = H as RealMap of T by TOPMETR:24;
A9: dom h = c /\ c by FUNCT_2:def 1
      .= c /\ dom g by FUNCT_2:def 1
      .= dom f /\ dom g by FUNCT_2:def 1;
    for c being set st c in dom h holds h.c = f.c * g.c by A7;
    then h = f(#)g by A9,VALUED_1:def 4;
    hence thesis by A8,TOPREAL6:83;
  end;
end;

definition
  let T, f;
  redefine func -f -> continuous RealMap of T;
  coherence
  proof
    reconsider F = f as continuous Function of T,R^1 by TOPMETR:24,TOPREAL6:83;
    set c = the carrier of T;
    consider H being Function of T,R^1 such that
A1: for p being Point of T, r1 being real number st F.p = r1 holds H.p
    = -r1 and
A2: H is continuous by JGRAPH_4:13;
    reconsider h = H as RealMap of T by TOPMETR:24;
A3: dom h = c by FUNCT_2:def 1
      .= dom f by FUNCT_2:def 1;
    for c being set st c in dom h holds h.c = -f.c by A1;
    then h = -f by A3,VALUED_1:9;
    hence thesis by A2,TOPREAL6:83;
  end;
end;

definition
  let T, f;
  redefine func abs f -> continuous RealMap of T;
  coherence
  proof
    reconsider F = f as continuous Function of T,R^1 by TOPMETR:24,TOPREAL6:83;
    set c = the carrier of T;
    consider H being Function of T,R^1 such that
A1: for p being Point of T, r1 being real number st F.p = r1 holds H.p
    = abs r1 and
A2: H is continuous by JGRAPH_4:12;
    reconsider h = H as RealMap of T by TOPMETR:24;
A3: dom h = c by FUNCT_2:def 1
      .= dom f by FUNCT_2:def 1;
    for c being set st c in dom h holds h.c = abs(f.c) by A1;
    then h = abs f by A3,VALUED_1:def 11;
    hence thesis by A2,TOPREAL6:83;
  end;
end;

Lm1: now
  let T;
  let a be Real;
  set c = the carrier of T;
  set f = c --> a;
  thus f is continuous
  proof
    let Y be Subset of REAL;
A1: dom f = c by FUNCT_2:def 1;
    assume Y is closed;
A2: rng f = {a} by FUNCOP_1:14;
    per cases;
    suppose
      a in Y;
      then
A3:   rng f c= Y by A2,ZFMISC_1:37;
      f"Y = f"(rng f /\ Y) by RELAT_1:168
        .= f"rng f by A3,XBOOLE_1:28
        .= c by A1,RELAT_1:169
        .= [#]T;
      hence thesis;
    end;
    suppose
      not a in Y;
      then
A4:   rng f misses Y by A2,ZFMISC_1:56;
      f"Y = f"(rng f /\ Y) by RELAT_1:168
        .= f"{} by A4,XBOOLE_0:def 7
        .= {}T by RELAT_1:171;
      hence thesis;
    end;
  end;
end;

registration
  let T;
  cluster positive-yielding continuous RealMap of T;
  existence
  proof
    take f = (the carrier of T) --> (1 qua Real);
    thus f is positive-yielding;
    thus thesis by Lm1;
  end;
  cluster negative-yielding continuous RealMap of T;
  existence
  proof
    take f = (the carrier of T) --> -1;
    thus f is negative-yielding;
    thus thesis by Lm1;
  end;
end;

definition
  let T;
  let f be nonnegative-yielding continuous RealMap of T;
  redefine func sqrt f -> continuous RealMap of T;
  coherence
  proof
    reconsider F = f as continuous Function of T,R^1 by TOPMETR:24,TOPREAL6:83;
    set c = the carrier of T;
    for q being Point of T ex r being real number st f.q = r & r >= 0
    proof
      let q be Point of T;
      take f.q;
      thus f.q = f.q;
      dom f = c by FUNCT_2:def 1;
      then f.q in rng f by FUNCT_1:def 5;
      hence thesis by Def4;
    end;
    then consider H being Function of T,R^1 such that
A1: for p being Point of T, r1 being real number st F.p = r1 holds H.p
    = sqrt r1 and
A2: H is continuous by JGRAPH_3:15;
    reconsider h = H as RealMap of T by TOPMETR:24;
A3: dom h = c by FUNCT_2:def 1
      .= dom f by FUNCT_2:def 1;
    for c being set st c in dom h holds h.c = sqrt(f.c) by A1;
    then h = sqrt f by A3,Def5;
    hence thesis by A2,TOPREAL6:83;
  end;
end;

definition
  let T, f, r;
  redefine func r(#)f -> continuous RealMap of T;
  coherence
  proof
    reconsider F = f as continuous Function of T,R^1 by TOPMETR:24,TOPREAL6:83;
    set c = the carrier of T;
    consider H being Function of T,R^1 such that
A1: for p being Point of T, r1 being real number st F.p = r1 holds H.p
    = r*r1 and
A2: H is continuous by JGRAPH_2:33;
    reconsider h = H as RealMap of T by TOPMETR:24;
A3: dom h = c by FUNCT_2:def 1
      .= dom f by FUNCT_2:def 1;
    for c being set st c in dom h holds h.c = r * f.c by A1;
    then h = r(#)f by A3,VALUED_1:def 5;
    hence thesis by A2,TOPREAL6:83;
  end;
end;

definition
  let T;
  let f be non-empty continuous RealMap of T;
  redefine func f^ -> continuous RealMap of T;
  coherence
  proof
    reconsider F = f as continuous Function of T,R^1 by TOPMETR:24,TOPREAL6:83;
    set c = the carrier of T;
    for q being Point of T holds f.q <> 0
    proof
      let q be Point of T;
      dom f = c by FUNCT_2:def 1;
      hence thesis;
    end;
    then consider H being Function of T,R^1 such that
A1: for p being Point of T, r1 being real number st F.p = r1 holds H.p
    = 1/r1 and
A2: H is continuous by JGRAPH_2:36;
    reconsider h = H as RealMap of T by TOPMETR:24;
A3: now
      let a be set;
      assume a in dom h;
      hence h.a = 1/f.a by A1
        .= 1 * (f.a)" by XCMPLX_0:def 9
        .= (f.a)";
    end;
    dom h = c by FUNCT_2:def 1
      .= dom f \ f"{0} by FUNCT_2:def 1;
    then h = f^ by A3,RFUNCT_1:def 8;
    hence thesis by A2,TOPREAL6:83;
  end;
end;

definition
  let T, f;
  let g be non-empty continuous RealMap of T;
  redefine func f/g -> continuous RealMap of T;
  coherence
  proof
    reconsider F = f, G = g as continuous Function of T,R^1 by TOPMETR:24,
    TOPREAL6:83;
    set c = the carrier of T;
    for q being Point of T holds g.q <> 0
    proof
      let q be Point of T;
      dom g = c by FUNCT_2:def 1;
      hence thesis;
    end;
    then consider H being Function of T,R^1 such that
A1: for p being Point of T, r1,r2 being real number st F.p = r1 & G.p
    = r2 holds H.p = r1/r2 and
A2: H is continuous by JGRAPH_2:37;
    reconsider h = H as RealMap of T by TOPMETR:24;
A3: now
      let c be set;
      assume c in dom h;
      hence h.c = f.c / g.c by A1
        .= f.c * (g.c)" by XCMPLX_0:def 9;
    end;
    dom h = c /\ c by FUNCT_2:def 1
      .= c /\ dom g by FUNCT_2:def 1
      .= dom f /\ (dom g \ g"{0}) by FUNCT_2:def 1;
    then h = f/g by A3,RFUNCT_1:def 4;
    hence thesis by A2,TOPREAL6:83;
  end;
end;
