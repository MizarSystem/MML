:: On the Real Valued Functions
::  by Artur Korni{\l}owicz
:: 
:: Received December 10, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies FUNCT_1, PRE_TOPC, ORDINAL2, RELAT_1, ARYTM, PARTFUN3, SEQ_1,
      ARYTM_1, ZF_LANG, PSCOMP_1, ARYTM_3, ZF_REFLE, BOOLE, SUBSET_1, ASYMPT_0,
      FUNCOP_1, TOPMETR, ABSVALUE, PARTFUN1, SQUARE_1, FUNCT_2, PRALG_1,
      XREAL_0, FINSEQ_1, MEMBERED;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, COMPLEX1, MEMBERED, SQUARE_1,
      RELAT_1, RELSET_1, FUNCT_1, PARTFUN1, FUNCT_2, PSCOMP_1, SEQ_1, RFUNCT_1,
      NUMBERS, XCMPLX_0, XREAL_0, REAL_1, FUNCOP_1, RCOMP_1, STRUCT_0,
      PRE_TOPC, BORSUK_1, TOPALG_2;
 constructors REAL_1, SQUARE_1, PSCOMP_1, RCOMP_1, COMPLEX1, TOPREALA,
      RFUNCT_1;
 registrations STRUCT_0, RELSET_1, ARYTM_3, XREAL_0, REAL_1, XCMPLX_0, FUNCT_1,
      BORSUK_2, TOPS_1, TOPMETR, RELAT_1, PSCOMP_1, FUNCOP_1, SEQ_1, MEMBERED;
 requirements NUMERALS, BOOLE, SUBSET, REAL, ARITHM;
 definitions TARSKI, PSCOMP_1, RELAT_1, SEQ_1;
 theorems JGRAPH_6, TARSKI, SUBSET_1, FUNCT_2, PRE_TOPC, RELAT_1, FUNCT_1,
      ZFMISC_1, JGRAPH_2, XBOOLE_1, XBOOLE_0, FUNCOP_1, TOPMETR, PARTFUN1,
      SEQ_1, RFUNCT_1, SQUARE_1, REAL_1, ABSVALUE, XCMPLX_0, REAL_2, JGRAPH_3,
      TOPREAL6, PSCOMP_1, JGRAPH_4;
 schemes SEQ_1, FUNCT_1;

begin

registration
  let r be real number;
  cluster r/r -> non negative;
coherence
proof
  r <= 0 or 0 <= r;
  hence thesis by REAL_2:125;
end;
end;

registration
  let r be real number;
  cluster r*r -> non negative;
coherence by REAL_1:93;
  cluster r*(r") -> non negative;
coherence
proof
  r*(r") = r/r by XCMPLX_0:def 9;
  hence thesis;
end;
end;

registration
  let r be non negative (real number);
  cluster sqrt r -> non negative;
coherence by SQUARE_1:def 4;
end;

registration
  let r be positive (real number);
  cluster sqrt r -> positive;
coherence by SQUARE_1:93;
end;

theorem
  for f being Function, A being set st f is one-to-one & A c= dom (f")
   holds f.:(f".:A) = A
proof
  let f be Function, A be set;
  assume that
A1: f is one-to-one and
A2: A c= dom (f");
  f" is one-to-one by A1,FUNCT_1:62;
  then f"".:(f".:A)=A by A2,JGRAPH_6:22;
  hence thesis by A1,FUNCT_1:65;
end;

registration
  let f be non-empty Function;
  cluster f"{0} -> empty;
coherence
proof
  assume not thesis;
  then consider x being set such that
a1:  x in f"{0} by XBOOLE_0:def 1;
  f.x in {0} by a1,FUNCT_1:def 13;
  then
a2: f.x = 0 by TARSKI:def 1;
  x in dom f by a1,FUNCT_1:def 13;
  then f.x in rng f by FUNCT_1:def 5;
  hence thesis by a2,RELAT_1:def 9;
end;
end;

definition
  let R be Relation;
  attr R is positive-yielding means :dposy:
  for r being real number st r in rng R holds 0 < r;
  attr R is negative-yielding means :dnegy:
  for r being real number st r in rng R holds 0 > r;
  attr R is nonpositive-yielding means :dnonpy:
  for r being real number st r in rng R holds 0 >= r;
  attr R is nonnegative-yielding means :dnonny:
  for r being real number st r in rng R holds 0 <= r;
end;

registration
  let X be set, r be positive (real number);
  cluster X --> r -> positive-yielding;
coherence
proof
  let x be real number;
  assume
a1: x in rng (X --> r);
  then X is non empty by FUNCOP_1:16;
  then rng (X --> r) = {r} by FUNCOP_1:14;
  hence thesis by a1,TARSKI:def 1;
end;
end;

registration
  let X be set, r be negative (real number);
  cluster X --> r -> negative-yielding;
coherence
proof
  let x be real number;
  assume
a1: x in rng (X --> r);
  then X is non empty by FUNCOP_1:16;
  then rng (X --> r) = {r} by FUNCOP_1:14;
  hence thesis by a1,TARSKI:def 1;
end;
end;

registration
  let X be set, r be non positive (real number);
  cluster X --> r -> nonpositive-yielding;
coherence
proof
  let x be real number;
  assume
a1: x in rng (X --> r);
  then X is non empty by FUNCOP_1:16;
  then rng (X --> r) = {r} by FUNCOP_1:14;
  hence thesis by a1,TARSKI:def 1;
end;
end;

registration
  let X be set, r be non negative (real number);
  cluster X --> r -> nonnegative-yielding;
coherence
proof
  let x be real number;
  assume
a1: x in rng (X --> r);
  then X is non empty by FUNCOP_1:16;
  then rng (X --> r) = {r} by FUNCOP_1:14;
  hence thesis by a1,TARSKI:def 1;
end;
end;

registration
  let X be non empty set;
  cluster X --> 0 -> non non-empty;
coherence
proof
  rng (X --> 0) = {0} by FUNCOP_1:14;
  hence {} in rng (X --> 0) by TARSKI:def 1;
end;
end;

registration
  cluster positive-yielding -> nonnegative-yielding non-empty Relation;
coherence
proof
  let R be Relation such that
a1: for r being real number st r in rng R holds 0 < r;
  thus R is nonnegative-yielding
  proof
    let r be real number;
    thus thesis by a1;
  end;
  assume not thesis;
  then 0 in rng R by RELAT_1:def 9;
  hence thesis by a1;
end;
  cluster negative-yielding -> nonpositive-yielding non-empty Relation;
coherence
proof
  let R be Relation such that
a1: for r being real number st r in rng R holds 0 > r;
  thus R is nonpositive-yielding
  proof
    let r be real number;
    thus thesis by a1;
  end;
  assume not thesis;
  then 0 in rng R by RELAT_1:def 9;
  hence thesis by a1;
end;
end;

registration
  let X be set;
  cluster negative-yielding Function of X,REAL;
existence
proof
  take X --> -1;
  thus thesis;
end;
  cluster positive-yielding Function of X,REAL;
existence
proof
  take X --> (1 qua Element of REAL);
  thus thesis;
end;
end;

registration
  cluster non-empty real-yielding Function;
  existence
  proof
    consider f being non-empty Function of 0,REAL;
    take f;
    thus thesis;
  end;
end;

theorem Tdd:
  for f being non-empty real-yielding Function holds
  dom(f^) = dom f
proof
  let f be non-empty real-yielding Function;
  thus dom (f^) = dom f \ f"{0} by RFUNCT_1:def 8
  .= dom f;
end;

theorem Tdd2:
  for X being non empty set,
      f being PartFunc of X,REAL, g being non-empty PartFunc of X,REAL holds
  dom(f/g) = dom f /\ dom g
proof
  let X be non empty set,
      f be PartFunc of X,REAL,
      g be non-empty PartFunc of X,REAL;
  thus dom (f/g) = dom f /\ (dom g \ g"{0}) by RFUNCT_1:def 4
  .= dom f /\ dom g;
end;

registration
  let X be set;
  let f, g be nonpositive-yielding PartFunc of X,REAL;
  cluster f+g -> nonpositive-yielding;
coherence
proof
  set R = f+g;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f /\ dom g by SEQ_1:def 3;
  then x in dom f & x in dom g by a2,XBOOLE_0:def 3;
  then f.x in rng f & g.x in rng g by FUNCT_1:def 5;
  then reconsider a = f.x, b = g.x as non positive (real number) by dnonpy;
  a+b is non positive;
  hence thesis by a3,a2,SEQ_1:def 3;
end;
end;

registration
  let X be set;
  let f, g be nonnegative-yielding PartFunc of X,REAL;
  cluster f+g -> nonnegative-yielding;
coherence
proof
  set R = f+g;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f /\ dom g by SEQ_1:def 3;
  then x in dom f & x in dom g by a2,XBOOLE_0:def 3;
  then f.x in rng f & g.x in rng g by FUNCT_1:def 5;
  then reconsider a = f.x, b = g.x as non negative (real number) by dnonny;
  a+b is non negative;
  hence thesis by a3,a2,SEQ_1:def 3;
end;
end;

registration
  let X be set;
  let f be positive-yielding PartFunc of X,REAL;
  let g be nonnegative-yielding PartFunc of X,REAL;
  cluster f+g -> positive-yielding;
coherence
proof
  set R = f+g;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f /\ dom g by SEQ_1:def 3;
  then x in dom f & x in dom g by a2,XBOOLE_0:def 3;
  then
a4: f.x in rng f & g.x in rng g by FUNCT_1:def 5;
  then reconsider a = f.x as positive (real number) by dposy;
  reconsider b = g.x as non negative (real number) by a4,dnonny;
  a+b is positive;
  hence thesis by a3,a2,SEQ_1:def 3;
end;
end;

registration
  let X be set;
  let f be nonnegative-yielding PartFunc of X,REAL;
  let g be positive-yielding PartFunc of X,REAL;
  cluster f+g -> positive-yielding;
coherence
proof
  f+g = g+f;
  hence thesis;
end;
end;

registration
  let X be set;
  let f be nonpositive-yielding PartFunc of X,REAL;
  let g be negative-yielding PartFunc of X,REAL;
  cluster f+g -> negative-yielding;
coherence
proof
  set R = f+g;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f /\ dom g by SEQ_1:def 3;
  then x in dom f & x in dom g by a2,XBOOLE_0:def 3;
  then
a4: f.x in rng f & g.x in rng g by FUNCT_1:def 5;
  then reconsider a = f.x as non positive (real number) by dnonpy;
  reconsider b = g.x as negative (real number) by a4,dnegy;
  a+b is negative;
  hence thesis by a3,a2,SEQ_1:def 3;
end;
end;

registration
  let X be set;
  let f be negative-yielding PartFunc of X,REAL;
  let g be nonpositive-yielding PartFunc of X,REAL;
  cluster f+g -> negative-yielding;
coherence
proof
  f+g = g+f;
  hence thesis;
end;
end;

registration
  let X be set;
  let f be nonnegative-yielding PartFunc of X,REAL;
  let g be nonpositive-yielding PartFunc of X,REAL;
  cluster f-g -> nonnegative-yielding;
coherence
proof
  set R = f-g;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f /\ dom g by SEQ_1:def 4;
  then x in dom f & x in dom g by a2,XBOOLE_0:def 3;
  then
a4: f.x in rng f & g.x in rng g by FUNCT_1:def 5;
  then reconsider a = f.x as non negative (real number) by dnonny;
  reconsider b = g.x as non positive (real number) by a4,dnonpy;
  a-b is non negative;
  hence thesis by a3,a2,SEQ_1:def 4;
end;
end;

registration
  let X be set;
  let f be nonpositive-yielding PartFunc of X,REAL;
  let g be nonnegative-yielding PartFunc of X,REAL;
  cluster f-g -> nonpositive-yielding;
coherence
proof
  set R = f-g;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f /\ dom g by SEQ_1:def 4;
  then x in dom f & x in dom g by a2,XBOOLE_0:def 3;
  then
a4: f.x in rng f & g.x in rng g by FUNCT_1:def 5;
  then reconsider a = f.x as non positive (real number) by dnonpy;
  reconsider b = g.x as non negative (real number) by a4,dnonny;
  a-b is non positive;
  hence thesis by a3,a2,SEQ_1:def 4;
end;
end;

registration
  let X be set;
  let f be positive-yielding PartFunc of X,REAL;
  let g be nonpositive-yielding PartFunc of X,REAL;
  cluster f-g -> positive-yielding;
coherence
proof
  set R = f-g;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f /\ dom g by SEQ_1:def 4;
  then x in dom f & x in dom g by a2,XBOOLE_0:def 3;
  then
a4: f.x in rng f & g.x in rng g by FUNCT_1:def 5;
  then reconsider a = f.x as positive (real number) by dposy;
  reconsider b = g.x as non positive (real number) by a4,dnonpy;
  a-b is positive;
  hence thesis by a3,a2,SEQ_1:def 4;
end;
end;

registration
  let X be set;
  let f be nonpositive-yielding PartFunc of X,REAL;
  let g be positive-yielding PartFunc of X,REAL;
  cluster f-g -> negative-yielding;
coherence
proof
  set R = f-g;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f /\ dom g by SEQ_1:def 4;
  then x in dom f & x in dom g by a2,XBOOLE_0:def 3;
  then
a4: f.x in rng f & g.x in rng g by FUNCT_1:def 5;
  then reconsider a = f.x as non positive (real number) by dnonpy;
  reconsider b = g.x as positive (real number) by a4,dposy;
  a-b is negative;
  hence thesis by a3,a2,SEQ_1:def 4;
end;
end;

registration
  let X be set;
  let f be negative-yielding PartFunc of X,REAL;
  let g be nonnegative-yielding PartFunc of X,REAL;
  cluster f-g -> negative-yielding;
coherence
proof
  set R = f-g;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f /\ dom g by SEQ_1:def 4;
  then x in dom f & x in dom g by a2,XBOOLE_0:def 3;
  then
a4: f.x in rng f & g.x in rng g by FUNCT_1:def 5;
  then reconsider a = f.x as negative (real number) by dnegy;
  reconsider b = g.x as non negative (real number) by a4,dnonny;
  a-b is negative;
  hence thesis by a3,a2,SEQ_1:def 4;
end;
end;

registration
  let X be set;
  let f be nonnegative-yielding PartFunc of X,REAL;
  let g be negative-yielding PartFunc of X,REAL;
  cluster f-g -> positive-yielding;
coherence
proof
  set R = f-g;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f /\ dom g by SEQ_1:def 4;
  then x in dom f & x in dom g by a2,XBOOLE_0:def 3;
  then
a4: f.x in rng f & g.x in rng g by FUNCT_1:def 5;
  then reconsider a = f.x as non negative (real number) by dnonny;
  reconsider b = g.x as negative (real number) by a4,dnegy;
  a-b is positive;
  hence thesis by a3,a2,SEQ_1:def 4;
end;
end;

registration
  let X be set;
  let f, g be nonpositive-yielding PartFunc of X,REAL;
  cluster f(#)g -> nonnegative-yielding;
coherence
proof
  set R = f(#)g;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f /\ dom g by SEQ_1:def 5;
  then x in dom f & x in dom g by a2,XBOOLE_0:def 3;
  then f.x in rng f & g.x in rng g by FUNCT_1:def 5;
  then reconsider a = f.x, b = g.x as non positive (real number) by dnonpy;
  a*b is non negative;
  hence thesis by a3,a2,SEQ_1:def 5;
end;
end;

registration
  let X be set;
  let f, g be nonnegative-yielding PartFunc of X,REAL;
  cluster f(#)g -> nonnegative-yielding;
coherence
proof
  set R = f(#)g;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f /\ dom g by SEQ_1:def 5;
  then x in dom f & x in dom g by a2,XBOOLE_0:def 3;
  then f.x in rng f & g.x in rng g by FUNCT_1:def 5;
  then reconsider a = f.x, b = g.x as non negative (real number) by dnonny;
  a*b is non negative;
  hence thesis by a3,a2,SEQ_1:def 5;
end;
end;

registration
  let X be set;
  let f be nonpositive-yielding PartFunc of X,REAL;
  let g be nonnegative-yielding PartFunc of X,REAL;
  cluster f(#)g -> nonpositive-yielding;
coherence
proof
  set R = f(#)g;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f /\ dom g by SEQ_1:def 5;
  then x in dom f & x in dom g by a2,XBOOLE_0:def 3;
  then
a4: f.x in rng f & g.x in rng g by FUNCT_1:def 5;
  then reconsider a = f.x as non positive (real number) by dnonpy;
  reconsider b = g.x as non negative (real number) by a4,dnonny;
  a*b is non positive;
  hence thesis by a3,a2,SEQ_1:def 5;
end;
end;

registration
  let X be set;
  let f be nonnegative-yielding PartFunc of X,REAL;
  let g be nonpositive-yielding PartFunc of X,REAL;
  cluster f(#)g -> nonpositive-yielding;
coherence
proof
  f(#)g = g(#)f;
  hence thesis;
end;
end;

registration
  let X be set;
  let f be positive-yielding PartFunc of X,REAL;
  let g be negative-yielding PartFunc of X,REAL;
  cluster f(#)g -> negative-yielding;
coherence
proof
  set R = f(#)g;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f /\ dom g by SEQ_1:def 5;
  then x in dom f & x in dom g by a2,XBOOLE_0:def 3;
  then
a4: f.x in rng f & g.x in rng g by FUNCT_1:def 5;
  then reconsider a = f.x as positive (real number) by dposy;
  reconsider b = g.x as negative (real number) by a4,dnegy;
  a*b is negative;
  hence thesis by a3,a2,SEQ_1:def 5;
end;
end;

registration
  let X be set;
  let f be negative-yielding PartFunc of X,REAL;
  let g be positive-yielding PartFunc of X,REAL;
  cluster f(#)g -> negative-yielding;
coherence
proof
  set R = f(#)g;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f /\ dom g by SEQ_1:def 5;
  then x in dom f & x in dom g by a2,XBOOLE_0:def 3;
  then
a4: f.x in rng f & g.x in rng g by FUNCT_1:def 5;
  then reconsider a = f.x as negative (real number) by dnegy;
  reconsider b = g.x as positive (real number) by a4,dposy;
  a*b is negative;
  hence thesis by a3,a2,SEQ_1:def 5;
end;
end;

registration
  let X be set;
  let f, g be positive-yielding PartFunc of X,REAL;
  cluster f(#)g -> positive-yielding;
coherence
proof
  set R = f(#)g;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f /\ dom g by SEQ_1:def 5;
  then x in dom f & x in dom g by a2,XBOOLE_0:def 3;
  then f.x in rng f & g.x in rng g by FUNCT_1:def 5;
  then reconsider a = f.x, b = g.x as positive (real number) by dposy;
  a*b is positive;
  hence thesis by a3,a2,SEQ_1:def 5;
end;
end;

registration
  let X be set;
  let f, g be negative-yielding PartFunc of X,REAL;
  cluster f(#)g -> positive-yielding;
coherence
proof
  set R = f(#)g;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f /\ dom g by SEQ_1:def 5;
  then x in dom f & x in dom g by a2,XBOOLE_0:def 3;
  then f.x in rng f & g.x in rng g by FUNCT_1:def 5;
  then reconsider a = f.x, b = g.x as negative (real number) by dnegy;
  a*b is positive;
  hence thesis by a3,a2,SEQ_1:def 5;
end;
end;

registration
  let X be set;
  let f, g be non-empty PartFunc of X,REAL;
  cluster f(#)g -> non-empty;
coherence
proof
  set R = f(#)g;
  assume not thesis;
  then 0 in rng R by RELAT_1:def 9;
  then consider x being set such that
a2: x in dom R and
a3: R.x = 0 by FUNCT_1:def 5;
  dom R = dom f /\ dom g by SEQ_1:def 5;
  then x in dom f & x in dom g by a2,XBOOLE_0:def 3;
  then f.x in rng f & g.x in rng g by FUNCT_1:def 5;
  then reconsider a = f.x, b = g.x as non zero (real number) by RELAT_1:def 9;
  a*b is non zero;
  hence thesis by a3,a2,SEQ_1:def 5;
end;
end;

registration
  let X be set;
  let f be PartFunc of X,REAL;
  cluster f(#)f -> nonnegative-yielding;
coherence
proof
  set R = f(#)f;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a1: x in dom R & R.x = r by FUNCT_1:def 5;
  f.x * f.x is non negative;
  hence thesis by a1,SEQ_1:def 5;
end;
end;

registration
  let X be set;
  let r be non positive (real number);
  let f be nonpositive-yielding PartFunc of X,REAL;
  cluster r(#)f -> nonnegative-yielding;
coherence
proof
  set R = r(#)f;
  let z be real number;
  assume z in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = z by FUNCT_1:def 5;
  dom R = dom f by SEQ_1:def 6;
  then f.x in rng f by a2,FUNCT_1:def 5;
  then reconsider a = f.x as non positive (real number) by dnonpy;
  r*a is non negative;
  hence thesis by a3,a2,SEQ_1:def 6;
end;
end;

registration
  let X be set;
  let r be non negative (real number);
  let f be nonnegative-yielding PartFunc of X,REAL;
  cluster r(#)f -> nonnegative-yielding;
coherence
proof
  set R = r(#)f;
  let z be real number;
  assume z in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = z by FUNCT_1:def 5;
  dom R = dom f by SEQ_1:def 6;
  then f.x in rng f by a2,FUNCT_1:def 5;
  then reconsider a = f.x as non negative (real number) by dnonny;
  r*a is non negative;
  hence thesis by a3,a2,SEQ_1:def 6;
end;
end;

registration
  let X be set;
  let r be non positive (real number);
  let f be nonnegative-yielding PartFunc of X,REAL;
  cluster r(#)f -> nonpositive-yielding;
coherence
proof
  set R = r(#)f;
  let z be real number;
  assume z in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = z by FUNCT_1:def 5;
  dom R = dom f by SEQ_1:def 6;
  then f.x in rng f by a2,FUNCT_1:def 5;
  then reconsider a = f.x as non negative (real number) by dnonny;
  r*a is non positive;
  hence thesis by a3,a2,SEQ_1:def 6;
end;
end;

registration
  let X be set;
  let r be non negative (real number);
  let f be nonpositive-yielding PartFunc of X,REAL;
  cluster r(#)f -> nonpositive-yielding;
coherence
proof
  set R = r(#)f;
  let z be real number;
  assume z in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = z by FUNCT_1:def 5;
  dom R = dom f by SEQ_1:def 6;
  then f.x in rng f by a2,FUNCT_1:def 5;
  then reconsider a = f.x as non positive (real number) by dnonpy;
  r*a is non positive;
  hence thesis by a3,a2,SEQ_1:def 6;
end;
end;

registration
  let X be set;
  let r be positive (real number);
  let f be negative-yielding PartFunc of X,REAL;
  cluster r(#)f -> negative-yielding;
coherence
proof
  set R = r(#)f;
  let z be real number;
  assume z in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = z by FUNCT_1:def 5;
  dom R = dom f by SEQ_1:def 6;
  then f.x in rng f by a2,FUNCT_1:def 5;
  then reconsider a = f.x as negative (real number) by dnegy;
  r*a is negative;
  hence thesis by a3,a2,SEQ_1:def 6;
end;
end;

registration
  let X be set;
  let r be negative (real number);
  let f be positive-yielding PartFunc of X,REAL;
  cluster r(#)f -> negative-yielding;
coherence
proof
  set R = r(#)f;
  let z be real number;
  assume z in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = z by FUNCT_1:def 5;
  dom R = dom f by SEQ_1:def 6;
  then f.x in rng f by a2,FUNCT_1:def 5;
  then reconsider a = f.x as positive (real number) by dposy;
  r*a is negative;
  hence thesis by a3,a2,SEQ_1:def 6;
end;
end;

registration
  let X be set;
  let r be positive (real number);
  let f be positive-yielding PartFunc of X,REAL;
  cluster r(#)f -> positive-yielding;
coherence
proof
  set R = r(#)f;
  let z be real number;
  assume z in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = z by FUNCT_1:def 5;
  dom R = dom f by SEQ_1:def 6;
  then f.x in rng f by a2,FUNCT_1:def 5;
  then reconsider a = f.x as positive (real number) by dposy;
  r*a is positive;
  hence thesis by a3,a2,SEQ_1:def 6;
end;
end;

registration
  let X be set;
  let r be negative (real number);
  let f be negative-yielding PartFunc of X,REAL;
  cluster r(#)f -> positive-yielding;
coherence
proof
  set R = r(#)f;
  let z be real number;
  assume z in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = z by FUNCT_1:def 5;
  dom R = dom f by SEQ_1:def 6;
  then f.x in rng f by a2,FUNCT_1:def 5;
  then reconsider a = f.x as negative (real number) by dnegy;
  r*a is positive;
  hence thesis by a3,a2,SEQ_1:def 6;
end;
end;

registration
  let X be set;
  let r be non zero (real number);
  let f be non-empty PartFunc of X,REAL;
  cluster r(#)f -> non-empty;
coherence
proof
  set R = r(#)f;
  assume not thesis;
  then 0 in rng R by RELAT_1:def 9;
  then consider x being set such that
a2: x in dom R and
a3: R.x = 0 by FUNCT_1:def 5;
  dom R = dom f by SEQ_1:def 6;
  then f.x in rng f by a2,FUNCT_1:def 5;
  then reconsider a = f.x as non zero (real number) by RELAT_1:def 9;
  r*a is non zero;
  hence thesis by a3,a2,SEQ_1:def 6;
end;
end;

registration
  let X be non empty set;
  let f, g be nonpositive-yielding PartFunc of X,REAL;
  cluster f/g -> nonnegative-yielding;
coherence
proof
  set R = f/g;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f /\ (dom g \ g"{0}) by RFUNCT_1:def 4;
  then x in dom f & x in dom g \ g"{0} by a2,XBOOLE_0:def 3;
  then x in dom f & x in dom g by XBOOLE_0:def 4;
  then f.x in rng f & g.x in rng g by FUNCT_1:def 5;
  then reconsider a = f.x, b = g.x as non positive (real number) by dnonpy;
  a*b" is non negative;
  hence thesis by a3,a2,RFUNCT_1:def 4;
end;
end;

registration
  let X be non empty set;
  let f, g be nonnegative-yielding PartFunc of X,REAL;
  cluster f/g -> nonnegative-yielding;
coherence
proof
  set R = f/g;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f /\ (dom g \ g"{0}) by RFUNCT_1:def 4;
  then x in dom f & x in dom g \ g"{0} by a2,XBOOLE_0:def 3;
  then x in dom f & x in dom g by XBOOLE_0:def 4;
  then f.x in rng f & g.x in rng g by FUNCT_1:def 5;
  then reconsider a = f.x, b = g.x as non negative (real number) by dnonny;
  a*b" is non negative;
  hence thesis by a3,a2,RFUNCT_1:def 4;
end;
end;

registration
  let X be non empty set;
  let f be nonpositive-yielding PartFunc of X,REAL;
  let g be nonnegative-yielding PartFunc of X,REAL;
  cluster f/g -> nonpositive-yielding;
coherence
proof
  set R = f/g;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f /\ (dom g \ g"{0}) by RFUNCT_1:def 4;
  then x in dom f & x in dom g \ g"{0} by a2,XBOOLE_0:def 3;
  then x in dom f & x in dom g by XBOOLE_0:def 4;
  then
a4: f.x in rng f & g.x in rng g by FUNCT_1:def 5;
  then reconsider a = f.x as non positive (real number) by dnonpy;
  reconsider b = g.x as non negative (real number) by a4,dnonny;
  a*b" is non positive;
  hence thesis by a3,a2,RFUNCT_1:def 4;
end;
end;

registration
  let X be non empty set;
  let f be nonnegative-yielding PartFunc of X,REAL;
  let g be nonpositive-yielding PartFunc of X,REAL;
  cluster f/g -> nonpositive-yielding;
coherence
proof
  set R = f/g;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f /\ (dom g \ g"{0}) by RFUNCT_1:def 4;
  then x in dom f & x in dom g \ g"{0} by a2,XBOOLE_0:def 3;
  then x in dom f & x in dom g by XBOOLE_0:def 4;
  then
a4: f.x in rng f & g.x in rng g by FUNCT_1:def 5;
  then reconsider a = f.x as non negative (real number) by dnonny;
  reconsider b = g.x as non positive (real number) by a4,dnonpy;
  a*b" is non positive;
  hence thesis by a3,a2,RFUNCT_1:def 4;
end;
end;

registration
  let X be non empty set;
  let f be positive-yielding PartFunc of X,REAL;
  let g be negative-yielding PartFunc of X,REAL;
  cluster f/g -> negative-yielding;
coherence
proof
  set R = f/g;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f /\ (dom g \ g"{0}) by RFUNCT_1:def 4;
  then x in dom f & x in dom g \ g"{0} by a2,XBOOLE_0:def 3;
  then
a4: f.x in rng f & g.x in rng g by FUNCT_1:def 5;
  then reconsider a = f.x as positive (real number) by dposy;
  reconsider b = g.x as negative (real number) by a4,dnegy;
  a*b" is negative;
  hence thesis by a3,a2,RFUNCT_1:def 4;
end;
end;

registration
  let X be non empty set;
  let f be negative-yielding PartFunc of X,REAL;
  let g be positive-yielding PartFunc of X,REAL;
  cluster f/g -> negative-yielding;
coherence
proof
  set R = f/g;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f /\ (dom g \ g"{0}) by RFUNCT_1:def 4;
  then x in dom f & x in dom g \ g"{0} by a2,XBOOLE_0:def 3;
  then
a4: f.x in rng f & g.x in rng g by FUNCT_1:def 5;
  then reconsider a = f.x as negative (real number) by dnegy;
  reconsider b = g.x as positive (real number) by a4,dposy;
  a*b" is negative;
  hence thesis by a3,a2,RFUNCT_1:def 4;
end;
end;

registration
  let X be non empty set;
  let f, g be positive-yielding PartFunc of X,REAL;
  cluster f/g -> positive-yielding;
coherence
proof
  set R = f/g;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f /\ (dom g \ g"{0}) by RFUNCT_1:def 4;
  then x in dom f & x in dom g \ g"{0} by a2,XBOOLE_0:def 3;
  then f.x in rng f & g.x in rng g by FUNCT_1:def 5;
  then reconsider a = f.x, b = g.x as positive (real number) by dposy;
  a*b" is positive;
  hence thesis by a3,a2,RFUNCT_1:def 4;
end;
end;

registration
  let X be non empty set;
  let f, g be negative-yielding PartFunc of X,REAL;
  cluster f/g -> positive-yielding;
coherence
proof
  set R = f/g;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f /\ (dom g \ g"{0}) by RFUNCT_1:def 4;
  then x in dom f & x in dom g \ g"{0} by a2,XBOOLE_0:def 3;
  then f.x in rng f & g.x in rng g by FUNCT_1:def 5;
  then reconsider a = f.x, b = g.x as negative (real number) by dnegy;
  a*b" is positive;
  hence thesis by a3,a2,RFUNCT_1:def 4;
end;
end;

registration
  let X be non empty set;
  let f be PartFunc of X,REAL;
  cluster f/f -> nonnegative-yielding;
coherence
proof
  set R = f/f;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a1: x in dom R & R.x = r by FUNCT_1:def 5;
  f.x * (f.x)" is non negative;
  hence thesis by a1,RFUNCT_1:def 4;
end;
end;

registration
  let X be non empty set;
  let f, g be non-empty PartFunc of X,REAL;
  cluster f/g -> non-empty;
coherence
proof
  set R = f/g;
  assume not thesis;
  then 0 in rng R by RELAT_1:def 9;
  then consider x being set such that
a2: x in dom R and
a3: R.x = 0 by FUNCT_1:def 5;
  dom R = dom f /\ (dom g \ g"{0}) by RFUNCT_1:def 4;
  then x in dom f & x in dom g \ g"{0} by a2,XBOOLE_0:def 3;
  then f.x in rng f & g.x in rng g by FUNCT_1:def 5;
  then reconsider a = f.x, b = g.x as non zero (real number) by RELAT_1:def 9;
  a*b" is non zero;
  hence thesis by a3,a2,RFUNCT_1:def 4;
end;
end;

registration
  let X be set;
  let f be nonpositive-yielding Function of X,REAL;
  cluster Inv f -> nonpositive-yielding;
coherence
proof
  set R = Inv f;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = X by FUNCT_2:def 1
       .= dom f by FUNCT_2:def 1;
  then f.x in rng f by a2,FUNCT_1:def 5;
  then reconsider a = f.x as non positive (real number) by dnonpy;
  1/a is non positive;
  hence thesis by a3,a2,PSCOMP_1:def 18;
end;
end;

registration
  let X be set;
  let f be nonnegative-yielding Function of X,REAL;
  cluster Inv f -> nonnegative-yielding;
coherence
proof
  set R = Inv f;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = X by FUNCT_2:def 1
       .= dom f by FUNCT_2:def 1;
  then f.x in rng f by a2,FUNCT_1:def 5;
  then reconsider a = f.x as non negative (real number) by dnonny;
  1/a is non negative;
  hence thesis by a3,a2,PSCOMP_1:def 18;
end;
end;

registration
  let X be set;
  let f be positive-yielding Function of X,REAL;
  cluster Inv f -> positive-yielding;
coherence
proof
  set R = Inv f;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = X by FUNCT_2:def 1
       .= dom f by FUNCT_2:def 1;
  then f.x in rng f by a2,FUNCT_1:def 5;
  then reconsider a = f.x as positive (real number) by dposy;
  1/a is positive;
  hence thesis by a3,a2,PSCOMP_1:def 18;
end;
end;

registration
  let X be set;
  let f be negative-yielding Function of X,REAL;
  cluster Inv f -> negative-yielding;
coherence
proof
  set R = Inv f;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = X by FUNCT_2:def 1
       .= dom f by FUNCT_2:def 1;
  then f.x in rng f by a2,FUNCT_1:def 5;
  then reconsider a = f.x as negative (real number) by dnegy;
  1/a is negative;
  hence thesis by a3,a2,PSCOMP_1:def 18;
end;
end;

registration
  let X be set;
  let f be non-empty Function of X,REAL;
  cluster Inv f -> non-empty;
coherence
proof
  set R = Inv f;
  assume not thesis;
  then 0 in rng R by RELAT_1:def 9;
  then consider x being set such that
a2: x in dom R and
a3: R.x = 0 by FUNCT_1:def 5;
  dom R = X by FUNCT_2:def 1
       .= dom f by FUNCT_2:def 1;
  then f.x in rng f by a2,FUNCT_1:def 5;
  then reconsider a = f.x as non zero (real number) by RELAT_1:def 9;
  1/a is non zero;
  hence thesis by a3,a2,PSCOMP_1:def 18;
end;
end;

registration
  let X be set;
  let f be non-empty Function of X,REAL;
  cluster -f -> non-empty;
coherence
proof
  set R = -f;
  assume not thesis;
  then 0 in rng R by RELAT_1:def 9;
  then consider x being set such that
a2: x in dom R and
a3: R.x = 0 by FUNCT_1:def 5;
  dom R = dom f by SEQ_1:def 7;
  then f.x in rng f by a2,FUNCT_1:def 5;
  then reconsider a = f.x as non zero (real number) by RELAT_1:def 9;
  -a is non zero;
  hence thesis by a3,a2,SEQ_1:def 7;
end;
end;

registration
  let X be set;
  let f be nonpositive-yielding Function of X,REAL;
  cluster -f -> nonnegative-yielding;
coherence
proof
  set R = -f;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f by SEQ_1:def 7;
  then f.x in rng f by a2,FUNCT_1:def 5;
  then reconsider a = f.x as non positive (real number) by dnonpy;
  -a is non negative;
  hence thesis by a3,a2,SEQ_1:def 7;
end;
end;

registration
  let X be set;
  let f be nonnegative-yielding Function of X,REAL;
  cluster -f -> nonpositive-yielding;
coherence
proof
  set R = -f;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f by SEQ_1:def 7;
  then f.x in rng f by a2,FUNCT_1:def 5;
  then reconsider a = f.x as non negative (real number) by dnonny;
  -a is non positive;
  hence thesis by a3,a2,SEQ_1:def 7;
end;
end;

registration
  let X be set;
  let f be positive-yielding Function of X,REAL;
  cluster -f -> negative-yielding;
coherence
proof
  set R = -f;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f by SEQ_1:def 7;
  then f.x in rng f by a2,FUNCT_1:def 5;
  then reconsider a = f.x as positive (real number) by dposy;
  -a is negative;
  hence thesis by a3,a2,SEQ_1:def 7;
end;
end;

registration
  let X be set;
  let f be negative-yielding Function of X,REAL;
  cluster -f -> positive-yielding;
coherence
proof
  set R = -f;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f by SEQ_1:def 7;
  then f.x in rng f by a2,FUNCT_1:def 5;
  then reconsider a = f.x as negative (real number) by dnegy;
  -a is positive;
  hence thesis by a3,a2,SEQ_1:def 7;
end;
end;

registration
  let X be set;
  let f be Function of X,REAL;
  cluster abs f -> nonnegative-yielding;
coherence
proof
  set R = abs f;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  abs (f.x) is non negative by ABSVALUE:5;
  hence thesis by a3,a2,SEQ_1:def 10;
end;
end;

registration
  let X be set;
  let f be non-empty Function of X,REAL;
  cluster abs f -> positive-yielding;
coherence
proof
  set R = abs f;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f by SEQ_1:def 10;
  then f.x in rng f by a2,FUNCT_1:def 5;
  then reconsider a = f.x as non zero (real number) by RELAT_1:def 9;
  abs a is positive by ABSVALUE:6;
  hence thesis by a3,a2,SEQ_1:def 10;
end;
end;

registration
  let X be non empty set;
  let f be nonpositive-yielding Function of X,REAL;
  cluster f^ -> nonpositive-yielding;
coherence
proof
  set R = f^;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f \ f"{0} by RFUNCT_1:def 8;
  then x in dom f by a2,XBOOLE_0:def 4;
  then f.x in rng f by FUNCT_1:def 5;
  then reconsider a = f.x as non positive (real number) by dnonpy;
  a" is non positive;
  hence thesis by a3,a2,RFUNCT_1:def 8;
end;
end;

registration
  let X be non empty set;
  let f be nonnegative-yielding Function of X,REAL;
  cluster f^ -> nonnegative-yielding;
coherence
proof
  set R = f^;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f \ f"{0} by RFUNCT_1:def 8;
  then x in dom f by a2,XBOOLE_0:def 4;
  then f.x in rng f by FUNCT_1:def 5;
  then reconsider a = f.x as non negative (real number) by dnonny;
  a" is non negative;
  hence thesis by a3,a2,RFUNCT_1:def 8;
end;
end;

registration
  let X be non empty set;
  let f be positive-yielding Function of X,REAL;
  cluster f^ -> positive-yielding;
coherence
proof
  set R = f^;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f by Tdd;
  then f.x in rng f by a2,FUNCT_1:def 5;
  then reconsider a = f.x as positive (real number) by dposy;
  a" is positive;
  hence thesis by a3,a2,RFUNCT_1:def 8;
end;
end;

registration
  let X be non empty set;
  let f be negative-yielding Function of X,REAL;
  cluster f^ -> negative-yielding;
coherence
proof
  set R = f^;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f by Tdd;
  then f.x in rng f by a2,FUNCT_1:def 5;
  then reconsider a = f.x as negative (real number) by dnegy;
  a" is negative;
  hence thesis by a3,a2,RFUNCT_1:def 8;
end;
end;

registration
  let X be non empty set;
  let f be non-empty Function of X,REAL;
  cluster f^ -> non-empty;
coherence
proof
  set R = f^;
  assume not thesis;
  then 0 in rng R by RELAT_1:def 9;
  then consider x being set such that
a2: x in dom R and
a3: R.x = 0 by FUNCT_1:def 5;
  dom R = dom f by Tdd;
  then f.x in rng f by a2,FUNCT_1:def 5;
  then reconsider a = f.x as non zero (real number) by RELAT_1:def 9;
  a" is non zero;
  hence thesis by a3,a2,RFUNCT_1:def 8;
end;
end;

definition
  let f be real-yielding Function;
  func sqrt f -> Function means :dsqrt:
  dom it = dom f &
  for x being set st x in dom it holds it.x = sqrt(f.x);
existence
  proof
    deffunc F(set) = sqrt (f.$1);
    ex h being Function st dom h = dom f &
    for x being set st x in dom f holds h.x = F(x) from FUNCT_1:sch 3;
    hence thesis;
  end;
  uniqueness
  proof
  let h, g be Function such that
a1: dom h = dom f and
a2: for c being set st c in dom h holds h.c = sqrt (f.c) and
a3: dom g = dom f and
a4: for c being set st c in dom g holds g.c = sqrt (f.c);
    now let x be set;
      assume
a5:   x in dom h;
      hence h.x = sqrt (f.x) by a2 .= g.x by a1,a3,a4,a5;
    end;
    hence h = g by a1,a3,FUNCT_1:9;
  end;
end;

registration
  let f be real-yielding Function;
  cluster sqrt f -> real-yielding;
  coherence
  proof
    set F = sqrt f;
    let y be set;
    assume y in rng F;
    then consider x being set such that
a1: x in dom F and
a2: F.x = y by FUNCT_1:def 5;
    F.x = sqrt(f.x) by a1,dsqrt;
    hence thesis by a2;
  end;
end;

definition
  let C be set, D be real-membered set, f be PartFunc of C,D;
  redefine func sqrt f -> PartFunc of C,REAL;
  coherence
  proof
    set F = sqrt f;
a2: dom F = dom f by dsqrt;
    rng F c= REAL by SEQ_1:def 1;
    then F is PartFunc of dom F, REAL by PARTFUN1:25;
    hence thesis by a2,PARTFUN1:28;
  end;
end;

registration
  let X be set;
  let f be nonnegative-yielding Function of X,REAL;
  cluster sqrt f -> nonnegative-yielding;
coherence
proof
  set R = sqrt f;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f by dsqrt;
  then f.x in rng f by a2,FUNCT_1:def 5;
  then reconsider a = f.x as non negative (real number) by dnonny;
  sqrt a is non negative;
  hence thesis by a3,a2,dsqrt;
end;
end;

registration
  let X be set;
  let f be positive-yielding Function of X,REAL;
  cluster sqrt f -> positive-yielding;
coherence
proof
  set R = sqrt f;
  let r be real number;
  assume r in rng R;
  then consider x being set such that
a2: x in dom R and
a3: R.x = r by FUNCT_1:def 5;
  dom R = dom f by dsqrt;
  then f.x in rng f by a2,FUNCT_1:def 5;
  then reconsider a = f.x as positive (real number) by dposy;
  sqrt a is positive;
  hence thesis by a3,a2,dsqrt;
end;
end;

definition
  let X be set,
  f, g be Function of X, REAL;
  redefine func f+g -> Function of X,REAL;
coherence
proof
  dom (f+g) = dom f /\ dom g by SEQ_1:def 3
  .= X /\ dom g by FUNCT_2:def 1
  .= X /\ X by FUNCT_2:def 1;
  hence thesis by FUNCT_2:def 1;
end;
  redefine func f-g -> Function of X,REAL;
coherence
proof
  dom (f-g) = dom f /\ dom g by SEQ_1:def 4
  .= X /\ dom g by FUNCT_2:def 1
  .= X /\ X by FUNCT_2:def 1;
  hence thesis by FUNCT_2:def 1;
end;
  redefine func f(#)g -> Function of X,REAL;
coherence
proof
  dom (f(#)g) = dom f /\ dom g by SEQ_1:def 5
  .= X /\ dom g by FUNCT_2:def 1
  .= X /\ X by FUNCT_2:def 1;
  hence thesis by FUNCT_2:def 1;
end;
end;
  
definition
  let X be set,
      f be Function of X, REAL;
  redefine func -f -> Function of X,REAL;
coherence
proof
  dom -f = dom f by SEQ_1:def 7
  .= X by FUNCT_2:def 1;
  hence thesis by FUNCT_2:def 1;
end;
  redefine func abs f -> Function of X,REAL;
coherence
proof
  dom abs f = dom f by SEQ_1:def 10
  .= X by FUNCT_2:def 1;
  hence thesis by FUNCT_2:def 1;
end;
  redefine func sqrt f -> Function of X,REAL;
coherence
proof
  dom sqrt f = dom f by dsqrt
  .= X by FUNCT_2:def 1;
  hence thesis by FUNCT_2:def 1;
end;
end;

definition
  let X be set,
      f be Function of X, REAL,
      r be real number;
  redefine func  r(#)f -> Function of X,REAL;
coherence
proof
  dom (r(#)f) = dom f by SEQ_1:def 6
  .= X by FUNCT_2:def 1;
  hence thesis by FUNCT_2:def 1;
end;
end;

definition
  let X be set, f be non-empty Function of X, REAL;
  redefine func f^ -> Function of X,REAL;
coherence
proof
  dom (f^) = dom f by Tdd
  .= X by FUNCT_2:def 1;
  hence thesis by FUNCT_2:def 1;
end;
end;

definition
  let X be non empty set,
      f be Function of X, REAL,
      g be non-empty Function of X, REAL;
  redefine func f/g -> Function of X,REAL;
coherence
proof
  dom (f/g) = dom f /\ dom g by Tdd2
  .= X /\ dom g by FUNCT_2:def 1
  .= X /\ X by FUNCT_2:def 1;
  hence thesis by FUNCT_2:def 1;
end;
end;

reserve
  T for non empty TopSpace,
  f, g for continuous RealMap of T,
  r for real number;

definition
  let T, f, g;
  set c = the carrier of T;
  redefine func f+g -> continuous RealMap of T;
coherence
proof
  reconsider F = f, G = g as continuous map of T,R^1 by TOPMETR:24,TOPREAL6:83;
  consider H being map of T,R^1 such that
a1: for p being Point of T, r1,r2 being real number st
     F.p = r1 & G.p = r2 holds H.p = r1+r2 and
a2: H is continuous by JGRAPH_2:29;
  reconsider h = H as RealMap of T by TOPMETR:24;
a3: dom h = c /\ c by FUNCT_2:def 1
   .= c /\ dom g by FUNCT_2:def 1
   .= dom f /\ dom g by FUNCT_2:def 1;
  for c being set st c in dom h holds h.c = f.c + g.c by a1;
  then h = f+g by a3,SEQ_1:def 3;
  hence thesis by a2,TOPREAL6:83;
end;
  redefine func f-g -> continuous RealMap of T;
coherence
proof
  reconsider F = f, G = g as continuous map of T,R^1 by TOPMETR:24,TOPREAL6:83;
  consider H being map of T,R^1 such that
a1: for p being Point of T, r1,r2 being real number st
     F.p = r1 & G.p = r2 holds H.p = r1-r2 and
a2: H is continuous by JGRAPH_2:31;
  reconsider h = H as RealMap of T by TOPMETR:24;
a3: dom h = c /\ c by FUNCT_2:def 1
   .= c /\ dom g by FUNCT_2:def 1
   .= dom f /\ dom g by FUNCT_2:def 1;
  for c being set st c in dom h holds h.c = f.c - g.c by a1;
  then h = f-g by a3,SEQ_1:def 4;
  hence thesis by a2,TOPREAL6:83;
end;
  redefine func f(#)g -> continuous RealMap of T;
coherence
proof
  reconsider F = f, G = g as continuous map of T,R^1 by TOPMETR:24,TOPREAL6:83;
  consider H being map of T,R^1 such that
a1: for p being Point of T, r1,r2 being real number st
     F.p = r1 & G.p = r2 holds H.p = r1*r2 and
a2: H is continuous by JGRAPH_2:35;
  reconsider h = H as RealMap of T by TOPMETR:24;
a3: dom h = c /\ c by FUNCT_2:def 1
   .= c /\ dom g by FUNCT_2:def 1
   .= dom f /\ dom g by FUNCT_2:def 1;
  for c being set st c in dom h holds h.c = f.c * g.c by a1;
  then h = f(#)g by a3,SEQ_1:def 5;
  hence thesis by a2,TOPREAL6:83;
end;
end;

definition
  let T, f;
  redefine func -f -> continuous RealMap of T;
coherence
proof
  set c = the carrier of T;
  reconsider F = f as continuous map of T,R^1 by TOPMETR:24,TOPREAL6:83;
  consider H being map of T,R^1 such that
a1: for p being Point of T, r1 being real number st
     F.p = r1 holds H.p = -r1 and
a2: H is continuous by JGRAPH_4:13;
  reconsider h = H as RealMap of T by TOPMETR:24;
a3: dom h = c by FUNCT_2:def 1
   .= dom f by FUNCT_2:def 1;
  for c being set st c in dom h holds h.c = -f.c by a1;
  then h = -f by a3,SEQ_1:def 7;
  hence thesis by a2,TOPREAL6:83;
end;
end;

definition
  let T, f;
  redefine func abs f -> continuous RealMap of T;
coherence
proof
  set c = the carrier of T;
  reconsider F = f as continuous map of T,R^1 by TOPMETR:24,TOPREAL6:83;
  consider H being map of T,R^1 such that
a1: for p being Point of T, r1 being real number st
     F.p = r1 holds H.p = abs r1 and
a2: H is continuous by JGRAPH_4:12;
  reconsider h = H as RealMap of T by TOPMETR:24;
a3: dom h = c by FUNCT_2:def 1
   .= dom f by FUNCT_2:def 1;
  for c being set st c in dom h holds h.c = abs(f.c) by a1;
  then h = abs f by a3,SEQ_1:def 10;
  hence thesis by a2,TOPREAL6:83;
end;
end;

Lm10:
now
  let T;
  let a be Real;
  set c = the carrier of T;
  set f = c --> a;
  thus f is continuous
  proof
A1: dom f = c by FUNCT_2:def 1;
A2: rng f = {a} by FUNCOP_1:14;
  let Y be Subset of REAL; assume Y is closed;
  per cases;
  suppose a in Y;
  then
A3: rng f c= Y by A2,ZFMISC_1:37;
  f"Y = f"(rng f /\ Y) by RELAT_1:168
     .= f"rng f by A3,XBOOLE_1:28
     .= c by A1,RELAT_1:169
     .= [#]T by PRE_TOPC:12;
  hence f"Y is closed;
  end;
  suppose not a in Y;
  then
A4: rng f misses Y by A2,ZFMISC_1:56;
  f"Y = f"(rng f /\ Y) by RELAT_1:168
     .= f"{} by A4,XBOOLE_0:def 7
     .= {}T by RELAT_1:171;
  hence f"Y is closed;
  end;
end;
end;

registration
  let T;
  cluster positive-yielding continuous RealMap of T;
existence
proof
  take f = (the carrier of T) --> (1 qua Real);
  thus f is positive-yielding;
  thus thesis by Lm10;
end;
  cluster negative-yielding continuous RealMap of T;
existence
proof
  take f = (the carrier of T) --> -1;
  thus f is negative-yielding;
  thus thesis by Lm10;
end;
end;

definition
  let T; let f be nonnegative-yielding continuous RealMap of T;
  redefine func sqrt f -> continuous RealMap of T;
coherence
proof
  set c = the carrier of T;
  reconsider F = f as continuous map of T,R^1 by TOPMETR:24,TOPREAL6:83;
  for q being Point of T ex r being real number st f.q = r & r >= 0
  proof
    let q be Point of T;
    take f.q;
    thus f.q = f.q;
    dom f = c by FUNCT_2:def 1;
    then f.q in rng f by FUNCT_1:def 5;
    hence f.q >= 0 by dnonny;
  end;
  then consider H being map of T,R^1 such that
a1: for p being Point of T, r1 being real number st
     F.p = r1 holds H.p = sqrt r1 and
a2: H is continuous by JGRAPH_3:15;
  reconsider h = H as RealMap of T by TOPMETR:24;
a3: dom h = c by FUNCT_2:def 1
   .= dom f by FUNCT_2:def 1;
  for c being set st c in dom h holds h.c = sqrt(f.c) by a1;
  then h = sqrt f by a3,dsqrt;
  hence thesis by a2,TOPREAL6:83;
end;
end;

definition
  let T, f, r;
  redefine func r(#)f -> continuous RealMap of T;
coherence
proof
  set c = the carrier of T;
  reconsider F = f as continuous map of T,R^1 by TOPMETR:24,TOPREAL6:83;
  consider H being map of T,R^1 such that
a1: for p being Point of T, r1 being real number st
     F.p = r1 holds H.p = r*r1 and
a2: H is continuous by JGRAPH_2:33;
  reconsider h = H as RealMap of T by TOPMETR:24;
a3: dom h = c by FUNCT_2:def 1
   .= dom f by FUNCT_2:def 1;
  for c being set st c in dom h holds h.c = r * f.c by a1;
  then h = r(#)f by a3,SEQ_1:def 6;
  hence thesis by a2,TOPREAL6:83;
end;
end;

definition
  let T; let f be non-empty continuous RealMap of T;
  redefine func f^ -> continuous RealMap of T;
coherence
proof
  set c = the carrier of T;
  reconsider F = f as continuous map of T,R^1 by TOPMETR:24,TOPREAL6:83;
  for q being Point of T holds f.q <> 0
  proof
    let q be Point of T;
    dom f = c by FUNCT_2:def 1;
    then f.q in rng f by FUNCT_1:def 5;
    hence f.q <> 0 by RELAT_1:def 9;
  end;
  then consider H being map of T,R^1 such that
a1: for p being Point of T, r1 being real number st
     F.p = r1 holds H.p = 1/r1 and
a2: H is continuous by JGRAPH_2:36;
  reconsider h = H as RealMap of T by TOPMETR:24;
a3: dom h = c by FUNCT_2:def 1
   .= dom f \ f"{0} by FUNCT_2:def 1;
  now
    let a be set;
    assume a in dom h;
    hence h.a = 1/f.a by a1
    .= 1 * (f.a)" by XCMPLX_0:def 9
    .= (f.a)";
  end;
  then h = f^ by a3,RFUNCT_1:def 8;
  hence thesis by a2,TOPREAL6:83;
end;
end;

definition
  let T, f; let g be non-empty continuous RealMap of T;
  redefine func f/g -> continuous RealMap of T;
coherence
proof
  set c = the carrier of T;
  reconsider F = f, G = g as continuous map of T,R^1 by TOPMETR:24,TOPREAL6:83;
  for q being Point of T holds g.q <> 0
  proof
    let q be Point of T;
    dom g = c by FUNCT_2:def 1;
    then g.q in rng g by FUNCT_1:def 5;
    hence g.q <> 0 by RELAT_1:def 9;
  end;
  then consider H being map of T,R^1 such that
a1: for p being Point of T, r1,r2 being real number st
     F.p = r1 & G.p = r2 holds H.p = r1/r2 and
a2: H is continuous by JGRAPH_2:37;
  reconsider h = H as RealMap of T by TOPMETR:24;
a3: dom h = c /\ c by FUNCT_2:def 1
   .= c /\ dom g by FUNCT_2:def 1
   .= dom f /\ (dom g \ g"{0}) by FUNCT_2:def 1;
  now
    let c be set;
    assume c in dom h;
    hence h.c = f.c / g.c by a1
    .= f.c * (g.c)" by  XCMPLX_0:def 9;
  end;
  then h = f/g by a3,RFUNCT_1:def 4;
  hence thesis by a2,TOPREAL6:83;
end;
end;
