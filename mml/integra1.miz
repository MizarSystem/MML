:: The Definition of Riemann Definite Integral and some Related Lemmas
::  by Noboru Endou and Artur Korni{\l}owicz
::
:: Received March 13, 1999
:: Copyright (c) 1999 Association of Mizar Users

environ

 vocabularies FINSEQ_1, RCOMP_1, COMPTS_1, SEQ_2, LATTICES, ORDINAL2, ARYTM,
      ARYTM_1, RELAT_1, FUNCT_1, MEASURE5, PARTFUN1, RLVECT_1, SUBSET_1, BOOLE,
      RFUNCT_1, FUNCT_3, PARTFUN2, FINSEQ_4, FINSEQ_2, SQUARE_1, RFINSEQ,
      JORDAN3, SEQ_4, CARD_1, INTEGRA1, REALSET1, FUNCOP_1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, ORDINAL1, CARD_1, NUMBERS,
      XREAL_0, REAL_1, NAT_1, FUNCT_1, REALSET1, RELSET_1, VALUED_1, SEQ_1,
      SEQ_4, GOBOARD1, RCOMP_1, PSCOMP_1, FINSEQ_2, PARTFUN2, RFUNCT_1,
      PRE_CIRC, RVSUM_1, COMPLSP1, RFINSEQ, BINARITH, JORDAN3, PARTFUN1,
      FINSEQ_1, FUNCT_2, XXREAL_0;
 constructors PARTFUN1, REAL_1, NAT_1, RCOMP_1, FINSOP_1, PARTFUN2, RFUNCT_1,
      REALSET1, RFINSEQ, BINARITH, FINSEQ_5, COMPLSP1, GOBOARD1, PSCOMP_1,
      JORDAN3, SEQ_1, BINOP_2;
 registrations XBOOLE_0, SUBSET_1, RELAT_1, ORDINAL1, FUNCT_2, NUMBERS,
      XXREAL_0, XREAL_0, NAT_1, MEMBERED, FINSEQ_1, FINSEQ_2, GOBOARD1,
      PSCOMP_1, VALUED_0, VALUED_1, FINSET_1;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions SEQ_4, XBOOLE_0, RVSUM_1, SUBSET_1;
 theorems TARSKI, RCOMP_1, REAL_1, FINSEQ_1, FINSEQ_2, SEQ_4, FUNCT_1, NAT_1,
      NAT_2, SUBSET_1, GOBOARD1, PARTFUN1, RFUNCT_1, PSCOMP_1, RVSUM_1,
      PARTFUN2, JORDAN3, BINARITH, ZFMISC_1, COMPLSP1, FUNCT_3, FINSEQ_4,
      GOBOARD2, RFINSEQ, JORDAN7, CARD_1, RELAT_1, FUNCT_2, XBOOLE_0, XBOOLE_1,
      XREAL_0, FUNCOP_1, RELSET_1, XREAL_1, XXREAL_0, FINSOP_1, ORDINAL1,
      XXREAL_1, VALUED_1, FINSEQ_3, CARD_2;
 schemes FINSEQ_2, SEQ_1, NAT_1, XBOOLE_0;

begin :: Definition of closed interval and its properties

reserve a,a1,b,b1,x,y,z for Real,
  F,G,H for FinSequence of REAL,
  i,j,k,n,m for Element of NAT,
  I for Subset of REAL,
  X for non empty set,
  x1,R,s for set;

definition
  let IT be Subset of REAL;
  attr IT is closed-interval means
  :Def1:
  ex a,b being Real st a <= b & IT=[.a,b.];
end;

registration
  cluster closed-interval Subset of REAL;
  existence
  proof
    take [. 0,1 .];
    thus thesis by Def1;
  end;
end;

reserve A for closed-interval Subset of REAL;

theorem Th1:
  A is compact
proof
  consider a,b being Real such that
A1: a <= b & A=[.a,b.] by Def1;
  thus thesis by A1,RCOMP_1:24;
end;

theorem Th2:
  A is non empty
proof
  consider a,b being Real such that
A1: a <= b & A=[.a,b.] by Def1;
  [.a,b.]={x:a<=x & x<=b} by RCOMP_1:def 1;
  then a in A by A1;
  hence thesis;
end;

registration
  cluster closed-interval -> non empty compact Subset of REAL;
  coherence by Th1,Th2;
end;

theorem Th3:
  A is bounded_below & A is bounded_above
proof
  A is bounded by RCOMP_1:28;
  hence thesis by SEQ_4:def 3;
end;

registration
  cluster closed-interval -> bounded Subset of REAL;
  coherence
  proof
    let A be Subset of REAL;
    assume A is closed-interval;
    hence A is bounded_below bounded_above by Th3;
  end;
end;

registration
  cluster closed-interval Subset of REAL;
  existence
  proof
    consider I being closed-interval Subset of REAL;
    take I;
    thus thesis;
  end;
end;

reserve A for closed-interval Subset of REAL;

theorem Th4:
  ex a,b st a<=b & a=inf A & b=sup A
proof
  consider a,b being Real such that
A1: a <= b & A=[.a,b.] by Def1;
A2: a=inf A & b=sup A
  proof
    a in A by A1,RCOMP_1:15;
    then
A3: (ex x be real number st x in A) & A is bounded_below by Th3;
A4: for x be real number st x in A holds a <= x
    proof
      let x be real number;
      assume x in A;
      then x in {y:a <= y & y <= b} by A1,RCOMP_1:def 1;
      then ex y st x=y & a <= y & y <= b;
      hence thesis;
    end;
    for y be real number st 0<y ex x be real number st x in A & x<a + y
    proof
      let y be real number;
      assume
A5:   0<y;
      take a;
      thus thesis by A1,A5,RCOMP_1:15,XREAL_1:31;
    end;
    hence a=inf A by A3,A4,SEQ_4:def 5;
    b in A by A1,RCOMP_1:15;
    then
A6: (ex x be real number st x in A) & A is bounded_above by Th3;
A7: for x be real number st x in A holds x <= b
    proof
      let x be real number;
      assume x in A;
      then x in {y:a <= y & y <= b} by A1,RCOMP_1:def 1;
      then ex y st x=y & a <= y & y <= b;
      hence thesis;
    end;
    for y be real number st 0<y ex x be real number st x in A & b-y<x
    proof
      let y be real number;
      assume
A8:   0<y;
      take b;
      b < b+y by A8,XREAL_1:31;
      then b-y < b+y-y by XREAL_1:11;
      hence thesis by A1,RCOMP_1:15;
    end;
    hence thesis by A6,A7,SEQ_4:def 4;
  end;
  take a,b;
  thus a<=b & a=inf A & b=sup A by A1,A2;
end;

theorem Th5:
  A = [. inf A, sup A .]
proof
  consider a,b being Real such that
A1: a <= b & A=[.a,b.] by Def1;
  a=inf A & b=sup A
  proof
    a in A by A1,RCOMP_1:15;
    then
A2: (ex x be real number st x in A) & A is bounded_below by Th3;
A3: for x be real number st x in A holds a <= x
    proof
      let x be real number;
      assume
A4:   x in A;
      A={y:a <= y & y <= b} by A1,RCOMP_1:def 1;
      then ex y st x=y & a <= y & y <= b by A4;
      hence thesis;
    end;
    for y be real number st 0<y ex x be real number st x in A & x<a + y
    proof
      let y be real number;
      assume
A5:   0<y;
      take a;
      thus thesis by A1,A5,RCOMP_1:15,XREAL_1:31;
    end;
    hence a=inf A by A2,A3,SEQ_4:def 5;
    b in A by A1,RCOMP_1:15;
    then
A6: (ex x be real number st x in A) & A is bounded_above by Th3;
A7: for x be real number st x in A holds x <= b
    proof
      let x be real number;
      assume
A8:   x in A;
      A={y:a <= y & y <= b} by A1,RCOMP_1:def 1;
      then ex y st x=y & a <= y & y <= b by A8;
      hence thesis;
    end;
    for y be real number st 0<y ex x be real number st x in A & b-y<x
    proof
      let y be real number;
      assume
A9:   0<y;
      take b;
      b < b+y by A9,XREAL_1:31;
      then b-y < b+y-y by XREAL_1:11;
      hence thesis by A1,RCOMP_1:15;
    end;
    hence thesis by A6,A7,SEQ_4:def 4;
  end;
  hence thesis by A1;
end;

theorem Th6:
  for a1,a2,b1,b2 being real number holds
  A=[.a1,b1.] & A=[.a2,b2.] implies a1=a2 & b1=b2
proof
  let a1,a2,b1,b2 be real number;
  assume that
A1: A=[.a1,b1.] and
A2: A=[.a2,b2.];
  a1 <= b1 by A1,XXREAL_1:29;
  hence thesis by A1,A2,XXREAL_1:66;
end;

begin :: Definition of division of closed interval and its properties

definition
  let A be non empty compact Subset of REAL;
  mode DivisionPoint of A -> non empty increasing FinSequence of REAL means
    :Def2:
    rng it c= A & it.(len it) = sup A;
  existence
  proof
A1: sup A in A by RCOMP_1:32;
    consider a being Element of REAL such that
A2: a = sup A;
    reconsider p = Seg 1 --> a as Function of Seg 1, REAL by FUNCOP_1:57;
A3: dom p = Seg 1 by FUNCOP_1:19;
    then reconsider p as FinSequence by FINSEQ_1:def 2;
    rng p c= REAL by RELSET_1:12;
    then reconsider p as FinSequence of REAL by FINSEQ_1:def 4;
A4: len p = 1 by A3,FINSEQ_1:def 3;
    for n being Nat st n in Seg 1 holds p.n in A by A1,A2,FUNCOP_1:13;
    then p is FinSequence of A by A3,FINSEQ_2:14;
    then
A5: rng p c= A by FINSEQ_1:def 4;
A6: 1 in Seg 1 by FINSEQ_1:4,TARSKI:def 1;
    then
A7: p.(len p)=sup A by A2,A4,FUNCOP_1:13;
    p is non empty increasing FinSequence of REAL
    proof
      dom p = Seg 1 & p.1 = a by A6,FUNCOP_1:13,19;
      then
A8:   p=<*a*> by FINSEQ_1:def 8;
      for n,m st n in dom p & m in dom p & n<m holds p.n < p.m
      proof
        let n,m;
        assume n in dom p & m in dom p;
        then n = 1 & m = 1 by A3,FINSEQ_1:4,TARSKI:def 1;
        hence thesis;
      end;
      hence thesis by A8,GOBOARD1:def 1;
    end;
    hence thesis by A5,A7;
  end;
end;

definition
  let A be non empty compact Subset of REAL;
  func divs A means
  :Def3:
  x1 in it iff x1 is DivisionPoint of A;
  existence
  proof
    defpred P[set] means $1 is DivisionPoint of A;
    consider R such that
A1: x1 in R iff x1 in bool [:NAT,REAL:] & P[x1] from XBOOLE_0:sch 1;
    take R;
    let x1;
    thus x1 in R implies x1 is DivisionPoint of A by A1;
    assume x1 is DivisionPoint of A;
    then reconsider p = x1 as DivisionPoint of A;
    p c= [:NAT,REAL:];
    hence x1 in R by A1;
  end;
  uniqueness
  proof
    let D1,D2 be set such that
A2: x1 in D1 iff x1 is DivisionPoint of A and
A3: x1 in D2 iff x1 is DivisionPoint of A;
    now
      let x1;
      thus x1 in D1 implies x1 in D2
      proof
        assume x1 in D1;
        then x1 is DivisionPoint of A by A2;
        hence x1 in D2 by A3;
      end;
      assume x1 in D2;
      then x1 is DivisionPoint of A by A3;
      hence x1 in D1 by A2;
    end;
    hence thesis by TARSKI:2;
  end;
end;

registration
  let A be non empty compact Subset of REAL;
  cluster divs A -> non empty;
  coherence
  proof
    consider f being DivisionPoint of A;
    f in divs A by Def3;
    hence thesis;
  end;
end;

definition
  let A be non empty compact Subset of REAL;
  mode Division of A -> non empty set means
    :Def4:
    x1 in it iff x1 is DivisionPoint of A;
  existence
  proof
    take divs A;
    thus thesis by Def3;
  end;
end;

registration
  let A be non empty compact Subset of REAL;
  cluster non empty Division of A;
  existence
  proof
    consider R being Division of A;
    R is non empty;
    hence thesis;
  end;
end;

definition
  let A be non empty compact Subset of REAL, S be non empty Division of A;
  redefine mode Element of S -> DivisionPoint of A;
  coherence by Def4;
end;

reserve r for Real;
reserve S for non empty Division of A;
reserve D for Element of S;

canceled;

theorem Th8:
  i in dom D implies D.i in A
proof
  assume i in dom D;
  then
A1: D.i in rng D by FUNCT_1:def 5;
  rng D c= A by Def2;
  hence thesis by A1;
end;

theorem Th9:
  i in dom D & i<>1 implies i-1 in dom D & D.(i-1) in A & i-1 in NAT
proof
  assume that
A1: i in dom D and
A2: i<>1;
  consider j be Nat such that
A3: dom D = Seg j by FINSEQ_1:def 2;
A4: 1<=i & i<=j by A1,A3,FINSEQ_1:3;
  i<>0 & i<>1 by A1,A2,A3,FINSEQ_1:3;
  then i is non trivial by NAT_2:def 1;
  then
A5: i >= 2 by NAT_2:31;
  then
A6: 1+1-1 <= i-1 by XREAL_1:11;
A7: i-1<=j-0 by A4,REAL_1:92;
  consider l being Nat such that
A8: i=2+l by A5,NAT_1:10;
  reconsider l as Element of NAT by ORDINAL1:def 13;
A9: l+1=i-(2-1) by A8;
  then i-1 in dom D by A3,A6,A7,FINSEQ_1:3;
  then
A10: D.(i-1) in rng D by FUNCT_1:def 5;
  rng D c= A by Def2;
  hence thesis by A3,A6,A7,A9,A10,FINSEQ_1:3;
end;

definition
  let A be closed-interval Subset of REAL;
  let S be non empty Division of A;
  let D be Element of S;
  let i be Nat;
  assume
A1: i in dom D;
  func divset(D,i) -> closed-interval Subset of REAL means
  :Def5:
  inf it = inf A & sup it = D.i if i=1
  otherwise inf it = D.(i-1) & sup it = D.i;
  existence
  proof
    hereby
      assume i=1;
      thus ex IT being closed-interval Subset of REAL st
      inf IT = inf A & sup IT = D.i
      proof
A2:     D.i in A by A1,Th8;
        consider I such that
A3:     I=[.inf A,D.i .];
        inf A <= D.i
        proof
          A is bounded_below by Th3;
          hence thesis by A2,SEQ_4:def 5;
        end;
        then
A4:     I is closed-interval Subset of REAL by A3,Def1;
        inf I = inf A & sup I = D.i
        proof
          I = [.inf I,sup I.] by A4,Th5;
          hence thesis by A3,A4,Th6;
        end;
        hence thesis by A4;
      end;
    end;
    assume that
A5: i<>1;
    thus ex IT being closed-interval Subset of REAL st
    inf IT = D.(i-1) & sup IT = D.i
    proof
A6:   i-1 in dom D & D.(i-1) in A by A1,A5,Th9;
A7:   i+-1< i+(1+-1) by XREAL_1:8;
      consider a1,b1 such that
A8:   a1=D.(i-1) & b1=D.i;
      reconsider j=i-1 as Element of NAT by A1,A5,Th9;
A9:   D.j < D.i by A1,A6,A7,GOBOARD1:def 1;
      consider I such that
A10:  I=[.a1,b1.];
A11:  I is closed-interval Subset of REAL by A8,A9,A10,Def1;
      then I=[.inf I,sup I.] by Th5;
      then inf I=D.(i-1) & sup I=D.i by A8,A10,A11,Th6;
      hence thesis by A11;
    end;
  end;
  uniqueness
  proof
    let A1,A2 be closed-interval Subset of REAL;
    hereby
      assume that i=1 and
A12:  inf A1 =inf A & sup A1 = D.i and
A13:  inf A2 =inf A & sup A2 = D.i;
      consider b such that
A14:  b=D.i;
      A1=[. inf A, b .] by A12,A14,Th5;
      hence A1=A2 by A13,A14,Th5;
    end;
    assume that i<>1 and
A15: inf A1 = D.(i-1) & sup A1 = D.i and
A16: inf A2 = D.(i-1) & sup A2 = D.i;
    consider a,b such that
A17: a=D.(i-1) and
A18: b=D.i;
    A1=[.a,b.] by A15,A17,A18,Th5;
    hence thesis by A16,A17,A18,Th5;
  end;
  correctness;
end;

theorem Th10:
  i in dom D implies divset(D,i) c= A
proof
  assume
A1: i in dom D;
  now per cases;
    suppose
A2:   i=1;
      consider b such that
A3:   b=D.i;
A4:   inf divset(D,i) = inf A by A1,A2,Def5;
      sup divset(D,i) = b by A1,A2,A3,Def5;
      then
A5:   divset(D,i)=[. inf A,b .] by A4,Th5;
A6:   inf A in [.inf A,sup A.]
      proof
        inf A in A by RCOMP_1:32;
        hence thesis by Th5;
      end;
      b in [.inf A,sup A.]
      proof
        b in A by A1,A3,Th8;
        hence thesis by Th5;
      end;
      then [. inf A,b .] c= [.inf A,sup A.] by A6,RCOMP_1:16;
      hence divset(D,i) c= A by A5,Th5;
    end;
    suppose
A7:   i<>1;
      then
A8:   D.(i-1) in A by A1,Th9;
      consider a such that
A9:   a=D.(i-1);
A10:  inf divset(D,i) = a by A1,A7,A9,Def5;
      consider b such that
A11:  b=D.i;
      sup divset(D,i) = b by A1,A7,A11,Def5;
      then
A12:  divset(D,i)=[.a,b.] by A10,Th5;
A13:  a in [.inf A,sup A.] by A8,A9,Th5;
      b in [.inf A,sup A.]
      proof
        b in A by A1,A11,Th8;
        hence thesis by Th5;
      end;
      then [.a,b.] c= [.inf A,sup A.] by A13,RCOMP_1:16;
      hence divset(D,i) c= A by A12,Th5;
    end;
  end;
  hence thesis;
end;

definition
  let A be Subset of REAL;
  func vol(A) -> Real equals

  sup A - inf A;
  correctness;
end;

theorem
  for A be bounded non empty Subset of REAL holds 0 <= vol(A)
  by SEQ_4:24,XREAL_1:50;

begin :: Definitions of integrability and related topics

definition
  let A be closed-interval Subset of REAL;
  let f be PartFunc of A,REAL;
  let S be non empty Division of A;
  let D be Element of S;
  func upper_volume(f,D) -> FinSequence of REAL means
  :Def7:
  len it = len D & for i be Nat st i in dom D holds
  it.i=(sup rng (f|divset(D,i)))*vol divset(D,i);
  existence
  proof
    deffunc F(Nat)=(sup (rng (f|divset(D,$1))))*vol(divset(D,$1));
    consider IT being FinSequence of REAL such that
A1:  len IT = len D & for i be Nat st i in dom IT holds
    IT.i=F(i) from FINSEQ_2:sch 1;
   take IT;
   thus len IT = len D by A1;
   let i be Nat;
   assume i in dom D;
    then i in dom IT by A1,FINSEQ_3:31;
   hence thesis by A1;
  end;
  uniqueness
  proof
    let s1,s2 be FinSequence of REAL such that
A2: len s1 = len D & for i be Nat st i in dom D holds
    s1.i=(sup (rng (f|divset(D,i))))*vol(divset(D,i)) and
A3: len s2 = len D & for i be Nat st i in dom D holds
    s2.i=(sup (rng (f|divset(D,i))))*vol(divset(D,i));
Y: dom s1 = dom D & dom s2 = dom D by A2,A3,FINSEQ_3:31;
    for i being Nat st i in dom s1 holds s1.i=s2.i
    proof
      let i be Nat;
      assume
A4:   i in dom s1;
      then s1.i=(sup (rng (f|divset(D,i))))*vol(divset(D,i)) by A2,Y;
      hence thesis by A3,A4,Y;
    end;
    hence s1=s2 by A2,A3,FINSEQ_2:10;
  end;
  func lower_volume(f,D) -> FinSequence of REAL means
  :Def8:
  len it = len D & for i be Nat st i in dom D holds
  it.i=(inf rng (f|divset(D,i)))*vol divset(D,i);
  existence
  proof
    deffunc F(Nat)=(inf (rng (f|divset(D,$1))))*vol(divset(D,$1));
    consider IT being FinSequence of REAL such that
A5:  len IT = len D & for i be Nat st i in dom IT holds
    IT.i=F(i) from FINSEQ_2:sch 1;
   take IT;
   thus len IT = len D by A5;
   let i be Nat;
   assume i in dom D;
    then i in dom IT by A5,FINSEQ_3:31;
   hence thesis by A5;
  end;
  uniqueness
  proof
    let s1,s2 be FinSequence of REAL such that
A6: len s1 = len D & for i be Nat st i in dom D holds
    s1.i=(inf (rng (f|divset(D,i))))*vol(divset(D,i)) and
A7: len s2 = len D & for i be Nat st i in dom D holds
    s2.i=(inf (rng (f|divset(D,i))))*vol(divset(D,i));
Y:  dom s1 = dom D & dom s2 = dom D by A6,A7,FINSEQ_3:31;
    for i being Nat st i in dom s1 holds s1.i=s2.i
    proof
      let i be Nat;
      assume
A8:   i in dom s1;
      then s1.i=(inf (rng (f|divset(D,i))))*vol(divset(D,i)) by A6,Y;
      hence thesis by A7,A8,Y;
    end;
    hence s1=s2 by A6,A7,FINSEQ_2:10;
  end;
end;

definition
  let A be closed-interval Subset of REAL;
  let f be PartFunc of A,REAL;
  let S be non empty Division of A;
  let D be Element of S;
  func upper_sum(f,D) -> Real equals

  Sum(upper_volume(f,D));
  correctness;
  func lower_sum(f,D) -> Real equals

  Sum(lower_volume(f,D));
  correctness;
end;

definition
  let A be closed-interval Subset of REAL;
  redefine func divs A -> Division of A;
  coherence
  proof
    let x1;
    thus x1 in divs A iff x1 is DivisionPoint of A by Def3;
  end;
end;

definition
  let A be closed-interval Subset of REAL;
  let f be PartFunc of A,REAL;
  func upper_sum_set(f) -> PartFunc of divs A,REAL means
  :Def11:
  dom it = divs A &
  for D be Element of divs A st D in dom it holds it.D=upper_sum(f,D);
  existence
  proof
    set S = divs A;
    defpred P[set] means $1 in S;
    deffunc F(Element of S)=upper_sum(f,$1);
    consider g being PartFunc of S,REAL such that
A1: (for D be Element of S holds D in dom g iff P[D]) and
A2: (for D be Element of S st D in dom g holds g.D=F(D)) from SEQ_1:sch 3;
    take g;
    [#]S = S;
    hence dom g = S by A1,SUBSET_1:8;
    now
      let D be Element of S;
      assume D in S;
      D in dom g by A1;
      hence g.D = upper_sum(f,D) by A2;
    end;
    hence thesis;
  end;
  uniqueness
  proof
    set S = divs A;
    let g1,g2 be PartFunc of S,REAL such that
A3: (dom g1=S & for D be Element of S st D in dom g1 holds
    g1.D = upper_sum(f,D)) and
A4: (dom g2=S & for D be Element of S st D in dom g2 holds
    g2.D = upper_sum(f,D));
    now
      let D be Element of S;
      assume D in dom g1;
      g1.D = upper_sum(f,D) by A3;
      hence g1.D=g2.D by A4;
    end;
    hence thesis by A3,A4,PARTFUN1:34;
  end;
  func lower_sum_set(f) -> PartFunc of divs A,REAL means
  :Def12:
  dom it = divs A &
  for D be Element of divs A st D in dom it holds it.D=lower_sum(f,D);
  existence
  proof
    set S = divs A;
    defpred P[set] means $1 in S;
    deffunc F(Element of S)=lower_sum(f,$1);
    consider g being PartFunc of S,REAL such that
A5: (for D be Element of S holds D in dom g iff P[D]) and
A6: (for D be Element of S st D in dom g holds g.D=F(D)) from SEQ_1:sch 3;
    take g;
    [#]S = S;
    hence dom g = S by A5,SUBSET_1:8;
    now
      let D be Element of S;
      assume D in S;
      D in dom g by A5;
      hence g.D = lower_sum(f,D) by A6;
    end;
    hence thesis;
  end;
  uniqueness
  proof
    set S = divs A;
    let g1,g2 be PartFunc of S,REAL such that
A7: (dom g1=S & for D be Element of S st D in dom g1 holds
    g1.D = lower_sum(f,D)) and
A8: (dom g2=S & for D be Element of S st D in dom g2 holds
    g2.D = lower_sum(f,D));
    now
      let D be Element of S;
      assume D in dom g1;
      g1.D = lower_sum(f,D) by A7;
      hence g1.D=g2.D by A8;
    end;
    hence thesis by A7,A8,PARTFUN1:34;
  end;
end;

definition
  let A be closed-interval Subset of REAL;
  let f be PartFunc of A,REAL;
  pred f is_upper_integrable_on A means
  :Def13:
  rng upper_sum_set(f) is bounded_below;
  pred f is_lower_integrable_on A means
  :Def14:
  rng lower_sum_set(f) is bounded_above;
end;

definition
  let A be closed-interval Subset of REAL;
  let f be PartFunc of A,REAL;
  func upper_integral(f) -> Real equals

  inf rng upper_sum_set(f);
  correctness;
end;

definition
  let A be closed-interval Subset of REAL;
  let f be PartFunc of A,REAL;
  func lower_integral(f) -> Real equals

  sup rng lower_sum_set(f);
  correctness;
end;

definition
  let A be closed-interval Subset of REAL;
  let f be PartFunc of A,REAL;
  pred f is_integrable_on A means
  :Def17:
  f is_upper_integrable_on A & f is_lower_integrable_on A &
  upper_integral(f) = lower_integral(f);
end;

definition
  let A be closed-interval Subset of REAL;
  let f be PartFunc of A,REAL;
  func integral(f) -> Real equals

  upper_integral(f);
  correctness;
end;

begin :: Real function's properties

theorem Th12:
  for f,g be PartFunc of X,REAL holds rng(f+g) c= rng f + rng g
proof
  let f,g be PartFunc of X,REAL;
  for y st y in rng(f+g) holds y in rng f + rng g
  proof
    let y;
    assume y in rng(f+g);
    then consider x1 such that
A1: x1 in dom(f+g) & y=(f+g).x1 by FUNCT_1:def 5;
    dom(f+g)=dom f /\ dom g by VALUED_1:def 1;
    then x1 in dom f & x1 in dom g by A1,XBOOLE_0:def 3;
    then
A2: f.x1 in rng f & g.x1 in rng g by FUNCT_1:def 5;
A3: rng f + rng g = { x+z : x in rng f & z in rng g } by COMPLSP1:def 21;
    (f+g).x1=f.x1+g.x1 by A1,VALUED_1:def 1;
    hence thesis by A1,A2,A3;
  end;
  hence thesis by SUBSET_1:7;
end;

theorem Th13:
  for f be PartFunc of X,REAL holds
  f is_bounded_below_on X implies rng f is bounded_below
proof
  let f be PartFunc of X,REAL;
  assume that
A1: f is_bounded_below_on X;
  consider a be real number such that
A2: for x1 being set st x1 in X /\ dom f holds a <= f.x1
  by A1,RFUNCT_1:def 10;
A3: X /\ dom f = dom f by XBOOLE_1:28;
  for y be real number st y in rng f holds a <= y
  proof
    let y be real number;
    assume y in rng f;
    then consider s such that
A4: s in dom f & y = f.s by FUNCT_1:def 5;
    thus thesis by A2,A3,A4;
  end;
  hence thesis by SEQ_4:def 2;
end;

theorem
  for f be PartFunc of X,REAL
  st rng f is bounded_below holds f is_bounded_below_on X
proof
  let f be PartFunc of X,REAL;
  assume that
A1: rng f is bounded_below;
  consider a be real number such that
A2: for y be real number st y in rng f holds a <= y by A1,SEQ_4:def 2;
  for x1 being set st x1 in X /\ dom f holds a <= f.x1
  proof
    let x1 be set;
    assume
A3: x1 in X /\ dom f;
    X /\ dom f = dom f by XBOOLE_1:28;
    then f.x1 in rng f by A3,FUNCT_1:def 5;
    hence thesis by A2;
  end;
  hence thesis by RFUNCT_1:def 10;
end;

theorem Th15:
  for f be PartFunc of X,REAL
  st f is_bounded_above_on X holds rng f is bounded_above
proof
  let f be PartFunc of X,REAL;
  assume that
A1: f is_bounded_above_on X;
  consider a be real number such that
A2: for x1 being set st x1 in X /\ dom f holds f.x1 <= a by A1,RFUNCT_1:def 9;
A3: X /\ dom f = dom f by XBOOLE_1:28;
  for y be real number st y in rng f holds y <= a
  proof
    let y be real number;
    assume y in rng f;
    then consider s such that
A4: s in dom f & y = f.s by FUNCT_1:def 5;
    thus thesis by A2,A3,A4;
  end;
  hence thesis by SEQ_4:def 1;
end;

theorem
  for f be PartFunc of X,REAL
  st rng f is bounded_above holds f is_bounded_above_on X
proof
  let f be PartFunc of X,REAL;
  assume that
A1: rng f is bounded_above;
  consider a be real number such that
A2: for y be real number st y in rng f holds y <= a by A1,SEQ_4:def 1;
  for x1 being set st x1 in X /\ dom f holds f.x1 <= a
  proof
    let x1 be set;
    assume
A3: x1 in X /\ dom f;
    X /\ dom f = dom f by XBOOLE_1:28;
    then f.x1 in rng f by A3,FUNCT_1:def 5;
    hence thesis by A2;
  end;
  hence thesis by RFUNCT_1:def 9;
end;

theorem
  for f be PartFunc of X,REAL holds f is_bounded_on X implies rng f is bounded
proof
  let f be PartFunc of X,REAL;
  assume
A1: f is_bounded_on X;
  then
A2: f is_bounded_below_on X by RFUNCT_1:def 11;
A3: f is_bounded_above_on X by A1,RFUNCT_1:def 11;
A4: rng f is bounded_below by A2,Th13;
  rng f is bounded_above by A3,Th15;
  hence thesis by A4,SEQ_4:def 3;
end;

begin :: Characteristic function's properties

theorem Th18:
  for A be non empty set holds chi(A,A) is_constant_on A
proof
  let A be non empty set;
  for x being Element of A st x in A /\ dom chi(A,A) holds chi(A,A)/.x=1
  proof
    let x be Element of A;
    assume x in A /\ dom chi(A,A);
    then
A1: x in A & x in dom chi(A,A) by XBOOLE_0:def 3;
    chi(A,A).x=1 by FUNCT_3:def 3;
    hence thesis by A1,PARTFUN1:def 8;
  end;
  hence thesis by PARTFUN2:def 3;
end;

theorem Th19:
  for A be non empty Subset of X holds rng chi(A,A) = {1}
proof
  let A be non empty Subset of X;
A1: A = A /\ dom chi(A,A)
  proof
    dom chi(A,A) = A by FUNCT_3:def 3;
    hence thesis;
  end;
A2: chi(A,A) = chi(A,A)|A
  proof
A3: A=dom chi(A,A) & A = dom (chi(A,A)|A) by A1,FUNCT_3:def 3,RELAT_1:90;
    for x being Element of A st x in A holds chi(A,A).x = chi(A,A)|A.x
    proof
      let x be Element of A;
      assume x in A;
A4:   chi(A,A)/.x = (chi(A,A)|A)/.x by A3,PARTFUN2:32;
      chi(A,A)/.x = chi(A,A).x by A3,PARTFUN1:def 8;
      hence thesis by A3,A4,PARTFUN1:def 8;
    end;
    hence thesis by A3,PARTFUN1:34;
  end;
A5: A meets dom chi(A,A) by A1,XBOOLE_0:def 7;
  chi(A,A) is_constant_on A by Th18;
  then consider y being Element of REAL such that
A6: rng (chi(A,A)|A)={y} by A5,PARTFUN2:56;
  y=1
  proof
A7: dom chi(A,A) = A by FUNCT_3:def 3;
    ex x being Element of X st x in dom chi(A,A) & chi(A,A).x=1
    proof
      consider x being Element of X such that
A8:   x in dom chi(A,A) by A7,SUBSET_1:10;
      take x;
      thus thesis by A8,FUNCT_3:def 3;
    end;
    then 1 in rng chi(A,A) by FUNCT_1:def 5;
    hence thesis by A2,A6,TARSKI:def 1;
  end;
  hence thesis by A2,A6;
end;

theorem Th20:
  for A be non empty Subset of X, B be set holds
  B meets dom chi(A,A) implies rng (chi(A,A)|B) = {1}
proof
  let A be non empty Subset of X;
  let B be set;
  assume
A1: B /\ dom (chi(A,A)) <> {};
  rng (chi(A,A)|B) c= rng (chi(A,A)) by RELAT_1:99;
  then
A2: rng (chi(A,A)|B) c= {1} by Th19;
  rng (chi(A,A)|B) <> {}
  proof
    dom (chi(A,A)|B) = B /\ dom (chi(A,A)) by RELAT_1:90;
    hence thesis by A1,RELAT_1:65;
  end;
  hence thesis by A2,ZFMISC_1:39;
end;

theorem Th21:
  i in dom D implies vol(divset(D,i))=lower_volume(chi(A,A),D).i
proof
  assume
A1: i in dom D;
  then
A2: lower_volume(chi(A,A),D).i=
  (inf (rng (chi(A,A)|divset(D,i))))*vol(divset(D,i)) by Def8;
A3: rng chi(A,A) = {1} by Th19;
  then
A4: inf rng (chi(A,A)) = 1 by SEQ_4:22;
  divset(D,i) /\ dom (chi(A,A)) <> {}
  proof
A5: dom (chi(A,A)) = A by FUNCT_3:def 3;
    divset(D,i) c= A by Th10,A1;
    then
A6: divset(D,i) c= divset(D,i) /\ dom (chi(A,A)) by A5,XBOOLE_1:19;
    divset(D,i) /\ dom (chi(A,A)) c= divset(D,i) by XBOOLE_1:17;
    hence thesis by A6,XBOOLE_0:def 10;
  end;
  then divset(D,i) meets dom (chi(A,A)) by XBOOLE_0:def 7;
  then rng (chi(A,A)|divset(D,i)) = {1} by Th20;
  hence thesis by A2,A3,A4;
end;

theorem Th22:
  i in dom D implies vol(divset(D,i))=upper_volume(chi(A,A),D).i
proof
  assume
A1: i in dom D;
  then
A2: upper_volume(chi(A,A),D).i=
  (sup (rng (chi(A,A)|divset(D,i))))*vol(divset(D,i)) by Def7;
A3: rng chi(A,A) = {1} by Th19;
  then
A4: sup rng (chi(A,A)) = 1 by SEQ_4:22;
  divset(D,i) /\ dom (chi(A,A)) <> {}
  proof
A5: dom (chi(A,A)) = A by FUNCT_3:def 3;
    divset(D,i) c= A by Th10,A1;
    then
A6: divset(D,i) c= divset(D,i) /\ dom (chi(A,A)) by A5,XBOOLE_1:19;
    divset(D,i) /\ dom (chi(A,A)) c= divset(D,i) by XBOOLE_1:17;
    hence thesis by A6,XBOOLE_0:def 10;
  end;
  then divset(D,i) meets dom (chi(A,A)) by XBOOLE_0:def 7;
  then rng (chi(A,A)|divset(D,i)) = {1} by Th20;
  hence thesis by A2,A3,A4;
end;

theorem
  len F = len G & len F = len H &
  (for k st k in dom F holds H.k = F/.k + G/.k) implies
  Sum(H) = Sum(F) + Sum(G)
proof
  assume that
A1: len F = len G & len F = len H and
A2: for k st k in dom F holds H.k = F/.k + G/.k;
A3: for k st k in dom F holds H.k = F.k + G.k
  proof
    let k;
    assume
A4: k in dom F;
    then
A5: F/.k = F.k by PARTFUN1:def 8;
    k in Seg(len G) by A1,A4,FINSEQ_1:def 3;
    then k in dom G by FINSEQ_1:def 3;
    then G/.k = G.k by PARTFUN1:def 8;
    hence thesis by A2,A4,A5;
  end;
A6: F is Element of (len F)-tuples_on REAL by FINSEQ_2:110;
A7: G is Element of (len F)-tuples_on REAL by A1,FINSEQ_2:110;
  then F+G is Element of (len F)-tuples_on REAL by A6,FINSEQ_2:140;
  then
A8: len H = len (F+G) by A1,FINSEQ_2:109;
then
X: dom H = Seg len(F+G) by FINSEQ_1:def 3;
  for k being Nat st k in dom H holds H.k = (F+G).k
  proof
    let k be Nat;
    assume
A9: k in dom H;
    then k in dom F by A1,A8,FINSEQ_1:def 3,X;
    then
A10: H.k=F.k+G.k by A3;
    k in dom(F+G) by A9,FINSEQ_1:def 3,X;
    hence thesis by A10,VALUED_1:def 1;
  end;
  then Sum H=Sum(F+G) by A8,FINSEQ_2:10
    .=Sum F+Sum G by A6,A7,RVSUM_1:119;
  hence thesis;
end;

theorem Th24:
  len F = len G & len F = len H &
  (for k st k in dom F holds H.k = F/.k - G/.k) implies
  Sum(H) = Sum(F) - Sum(G)
proof
  assume that
A1: len F = len G & len F = len H and
A2: for k st k in dom F holds H.k = F/.k - G/.k;
A3: for k st k in dom F holds H.k = F.k - G.k
  proof
    let k;
    assume
A4: k in dom F;
    then
A5: F/.k = F.k by PARTFUN1:def 8;
    k in Seg(len G) by A1,A4,FINSEQ_1:def 3;
    then k in dom G by FINSEQ_1:def 3;
    then G/.k = G.k by PARTFUN1:def 8;
    hence thesis by A2,A4,A5;
  end;
A6: F is Element of (len F)-tuples_on REAL by FINSEQ_2:110;
A7: G is Element of (len F)-tuples_on REAL by A1,FINSEQ_2:110;
  then
A8: F-G is Element of (len F)-tuples_on REAL by A6,FINSEQ_2:140;
  then
A9: len H = len (F-G) by A1,FINSEQ_2:109;
then
X: dom H = Seg len (F-G) by FINSEQ_1:def 3;
  for k being Nat st k in dom H holds H.k = (F-G).k
  proof
    let k be Nat;
    assume
A10: k in dom H;
    then k in Seg(len F) by A8,FINSEQ_2:109,X;
    then k in dom F by FINSEQ_1:def 3;
    then
A11: H.k=F.k-G.k by A3;
    k in dom(F-G) by A10,FINSEQ_1:def 3,X;
    hence thesis by A11,VALUED_1:13;
  end;
  then Sum H=Sum(F-G) by A9,FINSEQ_2:10
    .=Sum F-Sum G by A6,A7,RVSUM_1:120;
  hence thesis;
end;

theorem Th25:
  for A be closed-interval Subset of REAL, S being non empty Division of A,
  D being Element of S holds Sum(lower_volume(chi(A,A),D))=vol(A)
proof
  let A be closed-interval Subset of REAL;
  let S be non empty Division of A;
  let D be Element of S;
  deffunc F(Nat)=vol(divset(D,$1));
  consider p being FinSequence of REAL such that
A1: len p = len D & for i be Nat st i in dom p holds
  p.i = F(i) from FINSEQ_2:sch 1;
A2: dom p = Seg len D by A1,FINSEQ_1:def 3;
A3: for i st i in Seg(len D) holds p.i = sup divset(D,i) - inf divset(D,i)
  proof
    let i;
    assume
A4: i in Seg(len D);
    vol(divset(D,i))=sup divset(D,i)-inf divset(D,i);
    hence thesis by A1,A4,A2;
  end;
  len D - 1 in NAT
  proof
    len D <> 0 by CARD_2:59;
    then len D >= 1 by NAT_1:14;
    then consider j being Nat such that
A5: len D = 1 + j by NAT_1:10;
    thus thesis by A5,ORDINAL1:def 13;
  end;
  then reconsider k = len D - 1 as Element of NAT;
  deffunc F(Nat)=sup divset(D,$1);
  consider s1 being FinSequence of REAL such that
A6: len s1 = k & for i be Nat st i in dom s1 holds
  s1.i = F(i) from FINSEQ_2:sch 1;
  deffunc G(Nat)=inf divset(D,$1+1);
  consider s2 being FinSequence of REAL such that
A8: len s2 = k & for i be Nat st i in dom s2 holds
  s2.i = G(i) from FINSEQ_2:sch 1;
A9: dom s2 = Seg k by A8,FINSEQ_1:def 3;
A10: for i st i in Seg k holds sup divset(D,i)=inf divset(D,i+1)
  proof
    let i;
    assume
A11: i in Seg k;
    k + 1 = len D;
    then k <= len D by NAT_1:11;
    then Seg k c= Seg len D by FINSEQ_1:7;
    then i in Seg (len D) by A11;
    then
A12: i in dom D by FINSEQ_1:def 3;
A13: i+1 in dom D
    proof
      1 <= i & i <= k by A11,FINSEQ_1:3;
      then 1+0 <= i+1 & i+1 <= k+1 by XREAL_1:9;
      then i+1 in Seg(len D) by FINSEQ_1:3;
      hence thesis by FINSEQ_1:def 3;
    end;
A14: i+1-1=i;
    now per cases;
      suppose
A15:    i=1;
        then sup divset(D,i) = D.i by A12,Def5;
        hence sup divset(D,i) = inf divset(D,i+1) by A13,A14,A15,Def5;
      end;
      suppose
A16:    i<>1;
        i >= 1 by A11,FINSEQ_1:3;
        then i+1>=1+1 by XREAL_1:8;
        then
A17:    i+1 <> 1;
        sup divset(D,i) = D.i by A12,A16,Def5;
        hence thesis by A13,A14,A17,Def5;
      end;
    end;
    hence thesis;
  end;
A18: s1=s2
  proof
X: dom s1 = Seg k by A6,FINSEQ_1:def 3;
    for i being Nat st i in dom s1 holds s1.i = s2.i
    proof
      let i be Nat;
      assume
A19:  i in dom s1;
      then s1.i=sup divset(D,i) by A6;
      then s1.i=inf divset(D,i+1) by A10,A19,X;
      hence thesis by A8,A19,A9,X;
    end;
    hence thesis by A6,A8,FINSEQ_2:10;
  end;
A20: len (s1^<*sup A*>) = len (<*inf A*>^s2) & len (s1^<*sup A*>) = len p &
  (for i st i in dom (s1^<*sup A*>) holds
  p.i=(s1^<*sup A*>)/.i - (<*inf A*>^s2)/.i)
  proof
    dom(<*sup A*>)=Seg 1 by FINSEQ_1:def 8;
    then
A21: len(<*sup A*>)=1 by FINSEQ_1:def 3;
    dom(<*inf A*>)=Seg 1 by FINSEQ_1:def 8;
    then
A22: len(<*inf A*>)=1 by FINSEQ_1:def 3;
A23: len(s1^<*sup A*>)=k+1 by A6,A21,FINSEQ_1:35;
    hence
A24: len (s1^<*sup A*>) = len (<*inf A*>^s2) by A8,A22,FINSEQ_1:35;
    thus len (s1^<*sup A*>) = len p by A1,A23;
    let i;
    assume
A25: i in dom (s1^<*sup A*>);
A26: i in dom (<*inf A*>^s2)
    proof
      i in Seg(len (s1^<*sup A*>)) by A25,FINSEQ_1:def 3;
      hence thesis by A24,FINSEQ_1:def 3;
    end;
A27: len D = 1 or len D >= 2
    proof
      len D <> 0 by CARD_2:59;
      then len D = 1 or len D is non trivial by NAT_2:def 1;
      hence thesis by NAT_2:31;
    end;
A28: (s1^<*sup A*>)/.i=(s1^<*sup A*>).i by A25,PARTFUN1:def 8;
A29: (<*inf A*>^s2)/.i=(<*inf A*>^s2).i by A26,PARTFUN1:def 8;
    now per cases by A27;
      suppose
A30:    len D = 1;
        then
A31:    i in Seg 1 by A23,A25,FINSEQ_1:def 3;
        then
A32:    i = 1 by FINSEQ_1:4,TARSKI:def 1;
A33:    i in Seg len D & i in dom D by A30,A31,FINSEQ_1:def 3;
        s1={} by A6,A30,CARD_2:59;
        then s1^<*sup A*> = <*sup A*> by FINSEQ_1:47;
        then
A34:    (s1^<*sup A*>)/.i=sup A by A28,A32,FINSEQ_1:def 8;
        s2={} by A8,A30,CARD_2:59;
        then <*inf A*>^s2 = <*inf A*> by FINSEQ_1:47;
        then
A35:    (<*inf A*>^s2)/.i=inf A by A29,A32,FINSEQ_1:def 8;
        D.i=sup A by A30,A32,Def2;
        then
A36:    sup divset(D,i)=sup A by A32,A33,Def5;
        p.i=sup divset(D,i)-inf divset(D,i) by A3,A30,A31;
        hence thesis by A32,A33,A34,A35,A36,Def5;
      end;
      suppose
A37:    len D >= 2;
A38:    k>=1
        proof
          1 = 2 - 1;
          hence thesis by A37,XREAL_1:11;
        end;
        now per cases;
          suppose
A39:        i=1;
A40:        i in Seg len D & i in dom D
            proof
A41:          Seg 2 c= Seg len D by A37,FINSEQ_1:7;
              i in Seg 2 by A39,FINSEQ_1:4,TARSKI:def 2;
              hence i in Seg(len D) by A41;
              hence thesis by FINSEQ_1:def 3;
            end;
A42:        i in Seg 1 & i in Seg k & i in dom s1
            proof
A43:          Seg 1 c= Seg k by A38,FINSEQ_1:7;
              thus i in Seg 1 by A39,FINSEQ_1:4,TARSKI:def 1;
              hence i in Seg k by A43;
              hence thesis by A6,FINSEQ_1:def 3;
            end;
            then (s1^<*sup A*>).i=s1.i by FINSEQ_1:def 7;
            then
A44:        (s1^<*sup A*>).i=sup divset(D,i) by A6,A42;
A45:        (<*inf A*>^s2).i = inf A
            proof
              i in dom <*inf A*> by A42,FINSEQ_1:def 8;
              then (<*inf A*>^s2).i=<*inf A*>.i by FINSEQ_1:def 7;
              hence thesis by A39,FINSEQ_1:def 8;
            end;
            p.i=sup divset(D,i)-inf divset(D,i) by A3,A40;
            hence thesis by A28,A29,A39,A40,A44,A45,Def5;
          end;
          suppose
A46:        i=len D;
A47:        i in Seg(len D) & i in dom D
            proof
              thus i in Seg(len D) by A37,A46,FINSEQ_1:5;
              hence thesis by FINSEQ_1:def 3;
            end;
A48:        i-len s1 = 1 & i-len s1 in dom <*sup A*>
            & i=i-len s1 + len s1 & i<>1
            proof
              thus i-len s1 = 1 by A6,A46;
              i-len s1 in Seg 1 by A6,A46,FINSEQ_1:4,TARSKI:def 1;
              hence i-len s1 in dom <*sup A*> by FINSEQ_1:def 8;
              thus i = i - len s1 + len s1;
              thus thesis by A37,A46;
            end;
            then (s1^<*sup A*>).i=<*sup A*>.(i-len s1) by FINSEQ_1:def 7;
            then
A49:        (s1^<*sup A*>)/.i=sup A by A28,A48,FINSEQ_1:def 8;
A50:        i-len <*inf A*>=i-1 & len <*inf A*> + (i - len <*inf A*>) = i &
            i-1 in Seg k & i-len <*inf A*> in dom s2 & i-1+1=i
            proof thus
A51:          i-len <*inf A*> = i-1 by FINSEQ_1:57;
              thus len <*inf A*>+(i-len <*inf A*>)=i;
              i-1<>0 & i-1=k by A37,A46;
              hence i-1 in Seg k by FINSEQ_1:5;
              hence i-len <*inf A*> in dom s2 by A8,A51,FINSEQ_1:def 3;
              thus thesis;
            end;
A52:        (<*inf A*>^s2).i = D.(i-1)
            proof
A53:          i-1 is Nat by A50,ORDINAL1:def 13;
              then (<*inf A*>^s2).i = s2.(i-len <*inf A*>)
              by A50,FINSEQ_1:def 7;
              then (<*inf A*>^s2).i = inf divset(D,i) by A8,A50,A53;
              hence thesis by A47,A48,Def5;
            end;
            p.i=sup divset(D,i)-inf divset(D,i) by A3,A47;
            then p.i=sup divset(D,i)-D.(i-1) by A47,A48,Def5;
            then p.i=D.i-D.(i-1) by A47,A48,Def5;
            hence thesis by A29,A46,A49,A52,Def2;
          end;
          suppose
A54:        i<>1 & i<>len D;
A55:        i in Seg(len D) & i in dom D
            proof
A56:          len s1+len <*sup A*> = k+1 by A6,FINSEQ_1:56;
              hence i in Seg(len D) by A25,FINSEQ_1:def 7;
              dom (s1^<*sup A*>) = Seg(len D) by A56,FINSEQ_1:def 7;
              hence thesis by A25,FINSEQ_1:def 3;
            end;
A57:        i in dom s1 & i in Seg k & i-1 in Seg k & i-1+1=i
            & i-len<*inf A*> in dom s2
            proof
A58:          1 <= i & i <= len D by A55,FINSEQ_1:3;
              then
A59:          1 <= i & i < k + 1 by A54,REAL_1:def 5;
              then
A60:          1 <= i & i <= k by NAT_1:13;
              then i in Seg(len s1) by A6,FINSEQ_1:3;
              hence i in dom s1 by FINSEQ_1:def 3;
              thus i in Seg k by A60,FINSEQ_1:3;
              consider j being Nat such that
A61:          i = 1 + j by A58,NAT_1:10;
              i <> 0 & i <> 1 by A54,A55,FINSEQ_1:3;
              then i is non trivial by NAT_2:def 1;
              then i >= 2 & i <= k by A59,NAT_1:13,NAT_2:31;
              then i >= 1+1 & i-1 <= k-1 by XREAL_1:11;
              then i-1 >= 1 & i-1+0 <= k-1+1 by XREAL_1:9,21;
              hence i-1 in Seg k by A61,FINSEQ_1:3;
              then
A62:          i-len<*inf A*> in Seg(len s2) by A8,FINSEQ_1: 56;
              thus i-1+1=i;
              thus thesis by A62,FINSEQ_1:def 3;
            end;
A63:        (s1^<*sup A*>).i=D.i
            proof
              (s1^<*sup A*>).i = s1.i by A57,FINSEQ_1:def 7;
              then (s1^<*sup A*>).i = sup divset(D,i) by A6,A57;
              hence thesis by A54,A55,Def5;
            end;
A64:        (<*inf A*>^s2).i = D.(i-1)
            proof
A65:          i-len<*inf A*> in Seg(len s2) by A57,FINSEQ_1:def 3;
A66:          i-1 is Nat by A57,ORDINAL1:def 13;
              i-len<*inf A*> is Element of NAT by A57;
              then 1 <= i-len<*inf A*> & i-len<*inf A*> <= len s2 by A65,
              FINSEQ_1:3;

then len<*inf A*>+1 <= i & i <= len<*inf A*>+len s2 by XREAL_1:21, 22;
              then (<*inf A*>^s2).i = s2.(i-len<*inf A*>) by FINSEQ_1:36;
              then (<*inf A*>^s2).i=s2.(i-1) by FINSEQ_1:56;
              then (<*inf A*>^s2).i= inf divset(D,i) by A8,A57,A66,A9;
              hence thesis by A54,A55,Def5;
            end;
            p.i=sup divset(D,i)-inf divset(D,i) by A3,A55;
            then p.i=sup divset(D,i)-D.(i-1) by A54,A55,Def5;
            hence thesis by A28,A29,A54,A55,A63,A64,Def5;
          end;
        end;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
A67: Sum p=Sum s1 + sup A - inf A - Sum s2
  proof
    Sum p = Sum(s1^<*sup A*>)-Sum(<*inf A*>^s2) by A20,Th24;
    then Sum p=Sum s1 + sup A -Sum(<*inf A*>^s2) by RVSUM_1:104;
    then Sum p=Sum s1 + sup A - (inf A + Sum s2) by RVSUM_1:106;
    hence thesis;
  end;
  Sum(lower_volume(chi(A,A),D)) = Sum p
  proof
    lower_volume(chi(A,A),D)=p
    proof
A68:  len lower_volume(chi(A,A),D) = len D & len p = len D by A1,Def8;
    then
X: dom lower_volume(chi(A,A),D) = Seg len D by FINSEQ_1:def 3;
for i being Nat st i in dom lower_volume(chi(A,A),D)
 holds lower_volume(chi(A,A),D).i = p.i
      proof
        let i be Nat;
        assume
A69:    i in dom lower_volume(chi(A,A),D);
        then i in dom D by A68,FINSEQ_3:31;
        then lower_volume(chi(A,A),D).i=vol(divset(D,i)) by Th21;
        hence thesis by A1,A69,A2,X;
      end;
      hence thesis by A68,FINSEQ_2:10;
    end;
    hence thesis;
  end;
  hence thesis by A18,A67;
end;

theorem Th26:
  for A be closed-interval Subset of REAL, S being non empty Division of A,
  D being Element of S holds Sum(upper_volume(chi(A,A),D))=vol(A)
proof
  let A be closed-interval Subset of REAL;
  let S be non empty Division of A;
  let D be Element of S;
  lower_volume(chi(A,A),D)=upper_volume(chi(A,A),D)
  proof
A1: len (lower_volume(chi(A,A),D)) = len D by Def8
      .= len (upper_volume(chi(A,A),D)) by Def7;
    for i be Nat st 1 <= i & i <= len(lower_volume(chi(A,A),D)) holds
    lower_volume(chi(A,A),D).i=upper_volume(chi(A,A),D).i
    proof
      let i be Nat;
      assume 1 <= i & i <= len(lower_volume(chi(A,A),D));
      then 1 <= i & i <= len D by Def8;
      then
A2:   i in dom D by FINSEQ_3:27;
      then lower_volume(chi(A,A),D).i=vol(divset(D,i)) by Th21
        .=upper_volume(chi(A,A),D).i by A2,Th22;
      hence thesis;
    end;
    hence thesis by A1,FINSEQ_1:18;
  end;
  hence thesis by Th25;
end;

begin :: Some properties of Darboux sum

definition
  let A be closed-interval Subset of REAL;
  let f be PartFunc of A,REAL;
  let S be non empty Division of A;
  let D be Element of S;
  redefine func upper_volume(f,D) -> non empty FinSequence of REAL;
  coherence
  proof
    len upper_volume(f,D) = len D by Def7;
    then len upper_volume(f,D) <> 0 by CARD_2:59;
    hence upper_volume(f,D) is non empty FinSequence of REAL by CARD_1:47;
  end;
end;

definition
  let A be closed-interval Subset of REAL;
  let f be PartFunc of A,REAL;
  let S be non empty Division of A;
  let D be Element of S;
  redefine func lower_volume(f,D) -> non empty FinSequence of REAL;
  coherence
  proof
    len lower_volume(f,D) = len D by Def8;
    then len lower_volume(f,D) <> 0 by CARD_2:59;
    hence lower_volume(f,D) is non empty FinSequence of REAL by CARD_1:47;
  end;
end;

theorem Th27:
  for A being closed-interval Subset of REAL, f being Function of A,REAL,
  S being non empty Division of A, D being Element of S holds
  (f is_bounded_below_on A implies (inf rng f)*vol(A) <= lower_sum(f,D))
proof
  let A be closed-interval Subset of REAL;
  let f be Function of A,REAL;
  let S be non empty Division of A;
  let D be Element of S;
  assume
A1: f is_bounded_below_on A;
A2: for i st i in dom D holds (inf rng f)*vol(divset(D,i))
  <= (inf rng (f|divset(D,i)))*vol(divset(D,i))
  proof
    let i;
    assume
A3: i in dom D;
A4: rng(f|divset(D,i)) c= rng f by RELAT_1:99;
A5: rng(f|divset(D,i)) is non empty Subset of REAL
    proof
A6:   dom f = A by FUNCT_2:def 1;
      dom (f|divset(D,i)) = divset(D,i) by A6,Th10,RELAT_1:91,A3;
      hence thesis by RELAT_1:65;
    end;
A7: rng f is bounded_below by A1,Th13;
    0 <= vol(divset(D,i)) by SEQ_4:24,XREAL_1:50;
    hence thesis by A4,A5,A7,SEQ_4:64,XREAL_1:66;
  end;
A8: for i st i in dom D holds (inf rng f)*(lower_volume(chi(A,A),D).i)
  <= (inf rng (f|divset(D,i)))*vol(divset(D,i))
  proof
    let i;
    assume
A9: i in dom D;
    then (inf rng f)*vol(divset(D,i))
    <= (inf rng (f|divset(D,i)))*vol(divset(D,i)) by A2;
    hence thesis by A9,Th21;
  end;
  Sum((inf rng f)*lower_volume(chi(A,A),D)) <=Sum(lower_volume(f,D))
  proof
    deffunc F(set)=(inf rng f)*(lower_volume(chi(A,A),D).$1);
    consider p being FinSequence of REAL such that
A10: len p = len D & for i be Nat st i in dom p holds
    p.i=F(i) from FINSEQ_2:sch 1;
A11: dom p = Seg len D by A10,FINSEQ_1:def 3;
    deffunc G(Nat)=(inf rng (f|divset(D,$1)))*vol(divset(D,$1));
    consider q being FinSequence of REAL such that
A12: len q = len D & for i be Nat st i in dom q holds
    q.i=G(i) from FINSEQ_2:sch 1;
A13: dom q = dom D by A12,FINSEQ_3:31;
A14: q=lower_volume(f,D) by A12,Def8,A13;
    len (lower_volume(chi(A,A),D))
    = len ((inf rng f)*lower_volume(chi(A,A),D)) by FINSEQ_2:37;
    then
A15: len ((inf rng f)*lower_volume(chi(A,A),D))=len D by Def8;
    for i be Nat st 1 <= i & i <= len p holds
    p.i=((inf rng f)*lower_volume(chi(A,A),D)).i
    proof
      let i be Nat;
      assume 1 <= i & i <= len p;
      then i in Seg(len D) by A10,FINSEQ_1:3;
      then p.i=(inf rng f)*(lower_volume(chi(A,A),D)).i by A10,A11;
      hence thesis by RVSUM_1:66;
    end;
    then
A16: p=(inf rng f)*lower_volume(chi(A,A),D) by A10,A15,FINSEQ_1:18;
    reconsider p as Element of (len D)-tuples_on REAL by A10,FINSEQ_2:110;
    reconsider q as Element of (len D)-tuples_on REAL by A12,FINSEQ_2:110;
    now
      let i be Nat;
      assume
A17:  i in Seg (len D);
      then
A18:  p.i=(inf rng f)*(lower_volume(chi(A,A),D).i) by A10,A11;
X:    i in dom D by A17,FINSEQ_1:def 3;
      then q.i=(inf rng (f|divset(D,i)))*vol(divset(D,i)) by A12,A13;
      hence p.i <= q.i by A8,A18,X;
    end;
    hence thesis by A14,A16,RVSUM_1:112;
  end;
  then (inf rng f)*Sum(lower_volume(chi(A,A),D))<=Sum(lower_volume(f,D))
  by RVSUM_1:117;
  hence thesis by Th25;
end;

theorem
  for A being closed-interval Subset of REAL, f being Function of A,REAL,
  S being non empty Division of A, D being Element of S,
  i being Element of NAT holds
  f is_bounded_above_on A & i in dom D implies
  (sup rng f)*vol(divset(D,i)) >= (sup rng (f|divset(D,i)))*vol(divset(D,i))
proof
  let A be closed-interval Subset of REAL;
  let f be Function of A,REAL;
  let S be non empty Division of A;
  let D be Element of S;
  let i be Element of NAT;
  assume
A1: f is_bounded_above_on A;
  assume
A2: i in dom D;
A3: rng(f|divset(D,i)) c= rng f by RELAT_1:99;
A4: rng(f|divset(D,i)) is non empty Subset of REAL
  proof
A5: dom f = A by FUNCT_2:def 1;
    dom (f|divset(D,i)) = divset(D,i) by A5,Th10,RELAT_1:91,A2;
    hence thesis by RELAT_1:65;
  end;
A6: rng f is bounded_above by A1,Th15;
  0 <= vol(divset(D,i)) by SEQ_4:24,XREAL_1:50;
  hence thesis by A3,A4,A6,SEQ_4:65,XREAL_1:66;
end;

theorem Th29:
  for A being closed-interval Subset of REAL, f being Function of A,REAL,
  S being non empty Division of A, D being Element of S holds
  (f is_bounded_above_on A implies upper_sum(f,D) <= (sup rng f)*vol(A))
proof
  let A be closed-interval Subset of REAL;
  let f be Function of A,REAL;
  let S be non empty Division of A;
  let D be Element of S;
  assume
A1: f is_bounded_above_on A;
A2: for i st i in Seg(len D) holds (sup rng f)*vol(divset(D,i))
  >= (sup rng (f|divset(D,i)))*vol(divset(D,i))
  proof
    let i;
    assume
A3: i in Seg(len D);
A4: rng(f|divset(D,i)) c= rng f by RELAT_1:99;
A5: rng(f|divset(D,i)) is non empty Subset of REAL
    proof
A6:   dom f = A by FUNCT_2:def 1;
      i in dom D by A3,FINSEQ_1:def 3;
      then dom (f|divset(D,i)) = divset(D,i) by A6,Th10,RELAT_1:91;
      hence thesis by RELAT_1:65;
    end;
A7: rng f is bounded_above by A1,Th15;
    0 <= vol(divset(D,i)) by SEQ_4:24,XREAL_1:50;
    hence thesis by A4,A5,A7,SEQ_4:65,XREAL_1:66;
  end;
A8: for i st i in Seg(len D) holds (sup rng f)*(upper_volume(chi(A,A),D).i)
  >= (sup rng (f|divset(D,i)))*vol(divset(D,i))
  proof
    let i;
    assume
A9: i in Seg(len D);
    then
X:     (sup rng f)*vol(divset(D,i))
    >= (sup rng (f|divset(D,i)))*vol(divset(D,i)) by A2;
     i in dom D by A9,FINSEQ_1:def 3;
    hence thesis by Th22,X;
  end;
  Sum((sup rng f)*upper_volume(chi(A,A),D)) >=Sum(upper_volume(f,D))
  proof
    deffunc F(set)=(sup rng f)*(upper_volume(chi(A,A),D).$1);
    consider p being FinSequence of REAL such that
A10: len p = len D & for i be Nat st i in dom p holds
    p.i=F(i) from FINSEQ_2:sch 1;
A11: dom p = Seg len D by A10,FINSEQ_1:def 3;
    deffunc G(Nat)=(sup rng (f|divset(D,$1)))*vol(divset(D,$1));
    consider q being FinSequence of REAL such that
A12: len q = len D & for i be Nat st i in dom q holds
    q.i=G(i) from FINSEQ_2:sch 1;
A13: dom q = dom D by A12,FINSEQ_3:31;
A14: q=upper_volume(f,D) by A12,Def7,A13;
    len (upper_volume(chi(A,A),D))
    = len ((sup rng f)*upper_volume(chi(A,A),D)) by FINSEQ_2:37;
    then
A15: len ((sup rng f)*upper_volume(chi(A,A),D))=len D by Def7;
    for i be Nat st 1 <= i & i <= len p holds
    p.i=((sup rng f)*upper_volume(chi(A,A),D)).i
    proof
      let i be Nat;
      assume 1 <= i & i <= len p;
      then i in Seg(len D) by A10,FINSEQ_1:3;
      then p.i=(sup rng f)*(upper_volume(chi(A,A),D)).i by A10,A11;
      hence thesis by RVSUM_1:66;
    end;
    then
A16: p=(sup rng f)*upper_volume(chi(A,A),D) by A10,A15,FINSEQ_1:18;
    reconsider p as Element of (len D)-tuples_on REAL by A10,FINSEQ_2:110;
    reconsider q as Element of (len D)-tuples_on REAL by A12,FINSEQ_2:110;
    now
      let i be Nat;
      assume
A17:  i in Seg (len D);
      then i in dom D by FINSEQ_1:def 3;
      then
A18:  q.i=(sup rng (f|divset(D,i)))*vol(divset(D,i)) by A12,A13;
      p.i=(sup rng f)*(upper_volume(chi(A,A),D).i) by A10,A17,A11;
      hence q.i <= p.i by A8,A17,A18;
    end;
    hence thesis by A14,A16,RVSUM_1:112;
  end;
  then (sup rng f)*Sum(upper_volume(chi(A,A),D))>=Sum(upper_volume(f,D))
  by RVSUM_1:117;
  hence thesis by Th26;
end;

theorem Th30:
  for A being closed-interval Subset of REAL, f being Function of A,REAL,
  S being non empty Division of A, D being Element of S holds
  f is_bounded_on A implies lower_sum(f,D) <= upper_sum(f,D)
proof
  let A be closed-interval Subset of REAL;
  let f be Function of A,REAL;
  let S be non empty Division of A;
  let D be Element of S;
  assume f is_bounded_on A;
  then f is_bounded_below_on A & f is_bounded_above_on A by RFUNCT_1:def 11;
  then
A1: rng f is bounded_below & rng f is bounded_above by Th13,Th15;
  deffunc F(Nat)=(inf rng (f|divset(D,$1)))*vol(divset(D,$1));
  consider p being FinSequence of REAL such that
A2: len p = len D & for i be Nat st i in dom p holds
  p.i=F(i) from FINSEQ_2:sch 1;
A3: dom p = dom D by A2,FINSEQ_3:31;
  deffunc G(Nat)=(sup rng (f|divset(D,$1)))*vol(divset(D,$1));
  consider q being FinSequence of REAL such that
A4: len q = len D & for i be Nat st i in dom q holds
  q.i=G(i) from FINSEQ_2:sch 1;
A5: dom q = dom D by A4,FINSEQ_3:31;
A6: q=upper_volume(f,D) by A4,Def7,A5;
  reconsider p as Element of (len D)-tuples_on REAL by A2,FINSEQ_2:110;
  reconsider q as Element of (len D)-tuples_on REAL by A4,FINSEQ_2:110;
  for i be Nat st i in Seg(len D) holds p.i <= q.i
  proof
    let i be Nat;
    assume
A7: i in Seg(len D);
    then
B7: i in dom D by FINSEQ_1:def 3;
A8: rng (f|divset(D,i)) is bounded_below by A1,RCOMP_1:3,RELAT_1:99;
    rng (f|divset(D,i)) is bounded_above by A1,RCOMP_1:4,RELAT_1:99;
    then
A9: rng (f|divset(D,i)) is bounded by A8,SEQ_4:def 3;
A10: rng(f|divset(D,i)) is non empty Subset of REAL
    proof
A11:   dom f = A by FUNCT_2:def 1;
      i in dom D by A7,FINSEQ_1:def 3;
      then dom (f|divset(D,i)) = divset(D,i) by A11,Th10,RELAT_1:91;
      hence thesis by RELAT_1:65;
    end;
    0 <= vol(divset(D,i)) by SEQ_4:24,XREAL_1:50;
    then inf (rng(f|divset(D,i)))*vol(divset(D,i))
    <= sup (rng(f|divset(D,i)))*vol(divset(D,i)) by A9,A10,SEQ_4:24,XREAL_1:66;
    then p.i <= sup (rng(f|divset(D,i)))*vol(divset(D,i)) by A2,A3,B7;
    hence thesis by A4,A5,B7;
  end;
  then Sum p <= Sum q by RVSUM_1:112;
  hence thesis by A2,A6,Def8,A3;
end;

definition
  let A be closed-interval Subset of REAL;
  let D be Element of divs A;
  func delta(D) -> Real equals

  max rng upper_volume(chi(A,A),D);
  correctness by XREAL_0:def 1;
end;

definition
  let A be closed-interval Subset of REAL;
  let S be non empty Division of A;
  let D1,D2 be Element of S;
  pred D1 <= D2 means
  :Def20:
  len D1 <= len D2 & rng D1 c= rng D2;
end;

notation
  let A be closed-interval Subset of REAL;
  let S be non empty Division of A;
  let D1,D2 be Element of S;
  synonym D2 >= D1 for D1 <= D2;
end;

theorem Th31:
  for A be closed-interval Subset of REAL, S be non empty Division of A,
  D1,D2 be Element of S holds len D1 = 1 implies D1 <= D2
proof
  let A be closed-interval Subset of REAL;
  let S be non empty Division of A;
  let D1,D2 be Element of S;
  assume
A1: len D1 = 1;
  len D2 <> 0 by CARD_2:59;
  then
A2: len D2 in Seg(len D2) by FINSEQ_1:5;
  hence len D1 <= len D2 by A1,FINSEQ_1:3;
  D1.1 = sup A by A1,Def2;
  then D1=<*sup A*> by A1,FINSEQ_1:57;
  then
A3: rng D1 = {sup A} by FINSEQ_1:55;
A4: len D2 in dom D2 by A2,FINSEQ_1:def 3;
  D2.(len D2) = sup A by Def2;
  then sup A in rng D2 by A4,FUNCT_1:def 5;
  then rng D1 is Subset of rng D2 by A3,SUBSET_1:63;
  hence thesis;
end;

theorem Th32:
  for A be closed-interval Subset of REAL, f be Function of A,REAL,
  S be non empty Division of A, D1,D2 be Element of S
  holds f is_bounded_above_on A & len D1 = 1
  implies upper_sum(f,D1) >= upper_sum(f,D2)
proof
  let A be closed-interval Subset of REAL;
  let f be Function of A,REAL;
  let S be non empty Division of A;
  let D1,D2 be Element of S;
  assume that
A1: f is_bounded_above_on A and
A2: len D1=1;
    1 in Seg(len D1) by A2,FINSEQ_1:5;
  then
A4: 1 in dom D1 by FINSEQ_1:def 3;
A5: divset(D1,1)=A
  proof
A6: inf divset(D1,1)=inf A by A4,Def5;
A7: sup divset(D1,1)=D1.1 by A4,Def5
      .=sup A by A2,Def2;
    divset(D1,1)=[.inf divset(D1,1),sup divset(D1,1).] by Th5;
    hence thesis by A6,A7,Th5;
  end;
A8: dom f = A by FUNCT_2:def 1;
A9: len upper_volume(f,D1) = 1 by A2,Def7;
  upper_volume(f,D1).1=(sup (rng(f|divset(D1,1))))*vol(divset(D1,1))
  by Def7,A4;
  then upper_sum(f,D1)=Sum
  <*(sup (rng (f|divset(D1,1))))*vol(divset(D1,1))*> by A9,FINSEQ_1:57
    .=(sup (rng(f|A)))*vol(A) by A5,FINSOP_1:12
    .=(sup (rng f))*vol(A) by A8,RELAT_1:97;
  hence upper_sum(f,D1)>=upper_sum(f,D2) by A1,Th29;
end;

theorem Th33:
  for A be closed-interval Subset of REAL, f be Function of A,REAL,
  S be non empty Division of A, D1,D2 be Element of S
  holds f is_bounded_below_on A & len D1 = 1
  implies lower_sum(f,D1) <= lower_sum(f,D2)
proof
  let A be closed-interval Subset of REAL;
  let f be Function of A,REAL;
  let S be non empty Division of A;
  let D1,D2 be Element of S;
  assume that
A1: f is_bounded_below_on A and
A2: len D1=1;
    1 in Seg(len D1) by A2,FINSEQ_1:5;
  then
A4: 1 in dom D1 by FINSEQ_1:def 3;
A5: divset(D1,1)=A
  proof
A6: inf divset(D1,1)=inf A by A4,Def5;
    sup divset(D1,1)=D1.1 by A4,Def5
      .=sup A by A2,Def2;
    then divset(D1,1)=[.inf A,sup A.] by A6,Th5;
    hence thesis by Th5;
  end;
A7: dom f = A by FUNCT_2:def 1;
A8: len lower_volume(f,D1) = 1 by A2,Def8;
  lower_volume(f,D1).1=(inf (rng(f|divset(D1,1))))*vol(divset(D1,1))
  by Def8,A4;
  then lower_sum(f,D1)=Sum
  <*(inf (rng (f|divset(D1,1))))*vol(divset(D1,1))*> by A8,FINSEQ_1:57
    .=(inf (rng(f|A)))*vol(A) by A5,FINSOP_1:12
    .=(inf (rng f))*vol(A) by A7,RELAT_1:97;
  hence lower_sum(f,D1)<=lower_sum(f,D2) by A1,Th27;
end;

theorem
  for A be closed-interval Subset of REAL, S be non empty Division of A,
  D be Element of S st i in dom D holds
  ex A1,A2 be closed-interval Subset of REAL
  st A1=[.inf A,D.i .] & A2=[. D.i,sup A.] & A=A1 \/ A2
proof
  let A be closed-interval Subset of REAL;
  let S be non empty Division of A;
  let D be Element of S;
  assume
A1: i in dom D;
A2: rng D c= A by Def2;
  D.i in rng D by A1,FUNCT_1:def 5;
  then D.i in A by A2;
  then D.i in [.inf A,sup A.] by Th5;
  then D.i in {a: inf A <= a & a <= sup A} by RCOMP_1:def 1;
  then
A3: ex a st a=D.i & inf A <= a & a <= sup A;
  then reconsider A1 =[.inf A,D.i .] as closed-interval Subset of REAL by Def1;
  take A1;
  reconsider A2 = [. D.i,sup A.] as closed-interval Subset of REAL by A3,Def1;
  take A2;
  A1 \/ A2 = [.inf A,sup A.] by A3,RCOMP_1:50
    .= A by Th5;
  hence thesis;
end;

theorem Th35:
  for A be closed-interval Subset of REAL, S be non empty Division of A,
  D1,D2 be Element of S st i in dom D1
  holds D1 <= D2 implies ex j st j in dom D2 & D1.i=D2.j
proof
  let A be closed-interval Subset of REAL;
  let S be non empty Division of A;
  let D1,D2 be Element of S;
  assume
A1: i in dom D1;
  assume
A2: D1 <= D2;
A3: D1.i in rng D1 by A1,FUNCT_1:def 5;
  rng D1 c= rng D2 by A2,Def20;
  then consider x1 such that
A4: x1 in dom D2 & D1.i=D2.x1 by A3,FUNCT_1:def 5;
  reconsider x1 as Element of NAT by A4;
  take x1;
  thus thesis by A4;
end;

definition
  let A be closed-interval Subset of REAL;
  let S be non empty Division of A;
  let D1,D2 be Element of S;
  let i be Nat;
  assume
A1: D1 <= D2;
  func indx(D2,D1,i) -> Element of NAT means
  :Def21:
  it in dom D2 & D1.i=D2.it if i in dom D1 otherwise it = 0;
  existence by A1,Th35;
  uniqueness
  proof
    let m,n be Element of NAT;
    hereby
      assume that i in dom D1 and
A2:   m in dom D2 & D1.i=D2.m and
A3:   n in dom D2 & D1.i=D2.n;
      assume
A4:   m <> n;
      now per cases by A4,REAL_1:def 5;
        suppose m < n;
          hence contradiction by A2,A3,GOBOARD1:def 1;
        end;
        suppose n < m;
          hence contradiction by A2,A3,GOBOARD1:def 1;
        end;
      end;
      hence contradiction;
    end;
    assume that not(i in dom D1) and
A5: m=0 and
A6: n=0;
    thus m=n by A5,A6;
  end;
  correctness;
end;

theorem Th36:
  for p be increasing FinSequence of REAL, n be Element of NAT
  holds n <= len p implies p/^n is increasing FinSequence of REAL
proof
  let p be increasing FinSequence of REAL;
  let n be Element of NAT;
  assume
A1: n <= len p;
  for i,j st i in dom (p/^n) & j in
  dom (p/^n) & i< j holds (p/^n).i < (p/^n).j
  proof
    let i,j;
    assume that
A2: i in dom(p/^n) and
A3: j in dom(p/^n) and
A4: i<j;
A5: (p/^n).i=p.(i+n) by A1,A2,RFINSEQ:def 2;
A6: (p/^n).j=p.(j+n) by A1,A3,RFINSEQ:def 2;
    i in Seg(len (p/^n)) by A2,FINSEQ_1:def 3;
    then 1 <= i & i <= len (p/^n) by FINSEQ_1:3;
    then
A7: 1+n <= i+n & i <= len p - n by A1,RFINSEQ:def 2,XREAL_1:8;
    1 <= 1+n by NAT_1:11;
    then 1 <= i+n & i+n <= len p by A7,XREAL_1:21,XXREAL_0:2;
    then i+n in Seg(len p) by FINSEQ_1:3;
    then
A8: i+n in dom p by FINSEQ_1:def 3;
    j in Seg(len (p/^n)) by A3,FINSEQ_1:def 3;
    then 1 <= j & j <= len (p/^n) by FINSEQ_1:3;
    then 1 <= j & j <= len p - n by A1,RFINSEQ:def 2;
    then
A9: 1+n <= j+n & j+n <= len p by XREAL_1:8,21;
    1 <= 1+n by NAT_1:11;
    then 1 <= j+n & j+n <= len p by A9,XXREAL_0:2;
    then j+n in Seg(len p) by FINSEQ_1:3;
    then
A10: j+n in dom p by FINSEQ_1:def 3;
    i+n<j+n by A4,XREAL_1:8;
    hence thesis by A5,A6,A8,A10,GOBOARD1:def 1;
  end;
  hence thesis by GOBOARD1:def 1;
end;

theorem Th37:
  for p be increasing FinSequence of REAL,
  i,j be Element of NAT holds j in dom p & i <= j implies
  mid(p,i,j) is increasing FinSequence of REAL
proof
  let p be increasing FinSequence of REAL;
  let i,j be Element of NAT;
  assume that
A1: j in dom p and
A2: i <= j;
A3: mid(p,i,j)=(p/^(i-'1))|(j-'i+1) by A2,JORDAN3:def 1;
  j in Seg(len p) by A1,FINSEQ_1:def 3;
  then j <= len p by FINSEQ_1:3;
  then i <= len p by A2,XXREAL_0:2;
  then p/^(i-'1) is increasing FinSequence of REAL by Th36,BINARITH:62;
  then (p/^(i-'1))|Seg(j-'i+1) is increasing FinSequence of REAL
  by FINSEQ_1:23,GOBOARD2:20;
  hence thesis by A3,FINSEQ_1:def 15;
end;

theorem Th38:
  for A be closed-interval Subset of REAL,
  S be non empty Division of A, D be Element of S, i,j be Element of NAT holds
  i in dom D & j in dom D & i<=j implies ex B be closed-interval Subset of REAL
  st inf B=mid(D,i,j).1 & sup B=mid(D,i,j).(len mid(D,i,j))
  & len mid(D,i,j)=j-i+1 & mid(D,i,j) is DivisionPoint of B
proof
  let A be closed-interval Subset of REAL;
  let S be non empty Division of A;
  let D be Element of S;
  let i,j be Element of NAT;
  assume that
A1: i in dom D and
A2: j in dom D and
A3: i<=j;
  i-'1 <= len D
  proof
    j in Seg(len D) by A2,FINSEQ_1:def 3;
    then j <= len D by FINSEQ_1:3;
    then i <= len D by A3,XXREAL_0:2;
    hence thesis by BINARITH:62;
  end;
  then
A4: len (D/^(i-'1)) = len D - (i-'1) by RFINSEQ:def 2;
A5: j-'i+1 <= len (D/^(i-'1))
  proof
    j in Seg(len D) by A2,FINSEQ_1:def 3;
    then j <= len D by FINSEQ_1:3;
    then
A6: j-(i-'1) <= len D-(i-'1) by XREAL_1:11;
    i in Seg(len D) by A1,FINSEQ_1:def 3;
    then 1 <= i by FINSEQ_1:3;
    then j-(i-'1)=j-(i-1) by BINARITH:50;
    then j-(i-'1)=j-i+1;
    hence thesis by A3,A4,A6,BINARITH:50;
  end;
  reconsider k=j-'i+1 as Element of NAT;
  reconsider D1=D/^(i-'1) as FinSequence of REAL;
  reconsider D2=D1|Seg k as FinSequence of REAL by FINSEQ_1:23;
A7: mid(D,i,j) = D2
  proof
    mid(D,i,j) = (D/^(i-'1))|(j-'i+1) by A3,JORDAN3:def 1
      .=D1|Seg k by FINSEQ_1:def 15;
    hence thesis;
  end;
  then
A8: len mid(D,i,j) = j-'i+1 by A5,FINSEQ_1:21;
  0<len mid(D,i,j) by A5,A7,FINSEQ_1:21;
  then reconsider M=mid(D,i,j) as non empty increasing FinSequence of REAL
  by A2,A3,Th37,CARD_1:47;
A9: 1 <= len M
  proof
    j-'i+1 >= 0 + 1 by XREAL_1:8;
    hence thesis by A5,A7,FINSEQ_1:21;
  end;
A10: 1 in dom M & len M in dom M
  proof
    1 in Seg(len M) by A9,FINSEQ_1:3;
    hence 1 in dom M by FINSEQ_1:def 3;
    len M in Seg(len M) by A9,FINSEQ_1:3;
    hence thesis by FINSEQ_1:def 3;
  end;
  then M.1 <= M.(len M) by A9,GOBOARD2:18;
  then reconsider B=[. M.1,M.(len M) .] as closed-interval Subset of REAL
  by Def1;
A11: rng M c= B
  proof
    for x st x in rng M holds x in B
    proof
      let x;
      assume x in rng M;
      then consider i such that
A12:  i in dom M & x=M.i by PARTFUN1:26;
      i in Seg(len M) by A12,FINSEQ_1:def 3;
      then 1 <= i & i <= len M by FINSEQ_1:3;
      then M.1 <= M.i & M.i <= M.(len M) by A10,A12,GOBOARD2:18;
      then M.i in {a: M.1 <= a & a <= M.(len M)};
      hence thesis by A12,RCOMP_1:def 1;
    end;
    hence thesis by SUBSET_1:7;
  end;
A13: len mid(D,i,j) = j-i+1 by A3,A8,BINARITH:50;
A14: B=[.inf B,sup B.] by Th5;
  then
A15: inf B = M.1 by Th6;
A16: M.(len M)=sup B by A14,Th6;
  then M is DivisionPoint of B by A11,Def2;
  hence thesis by A13,A15,A16;
end;

theorem Th39:
  for A,B be closed-interval Subset of REAL,
  S be non empty Division of A, S1 be non empty Division of B,
  D be Element of S, i,j be Element of NAT
  holds i in dom D & j in dom D & i<=j & D.i>=inf B & D.j=sup B
  implies mid(D,i,j) is Element of S1
proof
  let A,B be closed-interval Subset of REAL;
  let S be non empty Division of A;
  let S1 be non empty Division of B;
  let D be Element of S;
  let i,j be Element of NAT;
  assume that
A1: i in dom D and
A2: j in dom D and
A3: i<=j and
A4: D.i>=inf B and
A5: D.j=sup B;
  consider A1 be closed-interval Subset of REAL such that
A6: inf A1=mid(D,i,j).1 and
A7: sup A1=mid(D,i,j).(len mid(D,i,j)) and
A8: len mid(D,i,j)=j-i+1 and
A9: mid(D,i,j) is DivisionPoint of A1 by A1,A2,A3,Th38;
  i in Seg(len D) by A1,FINSEQ_1:def 3;
  then
A10: 1 <= i by FINSEQ_1:3;
  j in Seg(len D) by A2,FINSEQ_1:def 3;
  then
A11: j<=len D by FINSEQ_1:3;
  0<=j-i by A3,XREAL_1:50;
  then
A12: 0+1<=j-i+1 by XREAL_1:8;
A13: 1+i-1 = i;
A14: j-i+1+i-1=j;
A15: A1 c= B
  proof
    for x st x in A1 holds x in B
    proof
      let x;
      assume x in A1;
      then x in [.inf A1,sup A1.] by Th5;
      then x in {a: inf A1 <= a & a <= sup A1} by RCOMP_1:def 1;
      then ex a st x=a & inf A1 <= a & a <= sup A1;
      then D.i <= x & x <= D.j by A3,A6,A7,A8,A10,A11,A12,A13,A14,JORDAN3:31;
      then inf B <= x & x <= sup B by A4,A5,XXREAL_0:2;
      then x in {a: inf B <= a & a <= sup B};
      then x in [.inf B,sup B.] by RCOMP_1:def 1;
      hence thesis by Th5;
    end;
    hence thesis by SUBSET_1:7;
  end;
  rng mid(D,i,j) c= A1 by A9,Def2;
  then
A16: rng mid(D,i,j) c= B by A15,XBOOLE_1:1;
  mid(D,i,j).(len mid(D,i,j))=D.j by A3,A8,A10,A11,A12,A14,JORDAN3:31;
  then mid(D,i,j) is DivisionPoint of B by A5,A9,A16,Def2;
  hence thesis by Def4;
end;

definition
  let p be FinSequence of REAL;
  func PartSums(p) -> FinSequence of REAL means
  :Def22:
  len it = len p & for i be Nat st i in dom p holds it.i=Sum(p|i);
  existence
  proof
    deffunc F(Nat)=Sum(p|$1);
    consider IT being FinSequence of REAL such that
A1:  len IT = len p & for i be Nat st i in dom IT holds
    IT.i=F(i) from FINSEQ_2:sch 1;
   take IT;
   thus len IT = len p by A1;
   let i be Nat;
   assume i in dom p;
    then i in dom IT by A1,FINSEQ_3:31;
   hence thesis by A1;
  end;
  uniqueness
  proof
    let p1,p2 be FinSequence of REAL such that
A2: len p1=len p & for i be Nat st i in dom p holds p1.i=Sum(p|i) and
A3: len p2=len p & for i be Nat st i in dom p holds p2.i=Sum(p|i);
    for i be Nat st 1 <= i & i <= len p1 holds p1.i=p2.i
    proof
      let i be Nat;
      assume 1 <= i & i <= len p1;
      then
A4:   i in dom p by A2,FINSEQ_3:27;
      then p1.i=Sum(p|i) by A2;
      hence thesis by A3,A4;
    end;
    hence thesis by A2,A3,FINSEQ_1:18;
  end;
end;

theorem Th40:
  for A be closed-interval Subset of REAL,
  f be Function of A,REAL, S be non empty Division of A,
  D1,D2 be Element of S st D1 <= D2 & f is_bounded_above_on A
  holds for i be non empty Element of NAT holds (i in dom D1 implies
  Sum(upper_volume(f,D1)|i) >= Sum(upper_volume(f,D2)|indx(D2,D1,i)))
proof
  let A be closed-interval Subset of REAL;
  let f be Function of A,REAL;
  let S be non empty Division of A;
  let D1,D2 be Element of S;
  assume that
A1: D1 <= D2 and
A2: f is_bounded_above_on A;
  for i be non empty Nat holds i in dom D1 implies
  Sum(upper_volume(f,D1)|i) >= Sum(upper_volume(f,D2)|indx(D2,D1,i))
  proof
    defpred P[Nat] means $1 in dom D1 implies
    Sum(upper_volume(f,D1)|$1) >= Sum(upper_volume(f,D2)|indx(D2,D1,$1));
A3: P[1]
    proof
      assume
A4:   1 in dom D1;
      then
      1 in Seg(len D1) by FINSEQ_1:def 3;
      then
A6:   1 <= len D1 by FINSEQ_1:3;
      then
A7:   len mid(D1,1,1)=1-'1+1 by JORDAN3:27;
      then
A8:   len mid(D1,1,1)=1 by BINARITH:53;
A9:   mid(D1,1,1)=D1|1
      proof
A10:    len mid(D1,1,1)=len (D1|1) by A6,A8,FINSEQ_1:80;

for k be Nat st 1 <= k & k <= len mid(D1,1,1) holds mid(D1,1,1).k=(D1|1).k
        proof
          let k be Nat;
          assume
A11:      1 <= k & k <= len mid(D1,1,1);
          then
A12:      k = 1 by A8,XXREAL_0:1;
          then
A13:      mid(D1,1,1).k = D1.(1+1-1) by A6,JORDAN3:27;
          k in Seg(len(D1|1)) by A10,A11,FINSEQ_1:3;
          then k in dom (D1|1) by FINSEQ_1:def 3;
          then k in dom (D1|Seg 1) by FINSEQ_1:def 15;
          then (D1|Seg 1).k = D1.k by FUNCT_1:68;
          hence thesis by A12,A13,FINSEQ_1:def 15;
        end;
        hence thesis by A10,FINSEQ_1:18;
      end;
      indx(D2,D1,1) in dom D2 by A1,A4,Def21;
      then indx(D2,D1,1) in Seg(len D2) by FINSEQ_1:def 3;
      then
A14:  1 <= indx(D2,D1,1) & indx(D2,D1,1) <= len D2 by FINSEQ_1:3;
      then
A15:  1 <= len D2 by XXREAL_0:2;
      then len mid(D2,1,indx(D2,D1,1))=indx(D2,D1,1)-'1+1 by A14,JORDAN3:27;
      then
A16:  len mid(D2,1,indx(D2,D1,1))=indx(D2,D1,1)-1+1 by A14, BINARITH :50;
      then
A17:  len mid(D2,1,indx(D2,D1,1))=len (D2|indx(D2,D1,1)) by A14, FINSEQ_1:80;
A18:  for k be Nat st 1 <= k & k <= len mid(D2,1,indx(D2,D1,1)) holds
      mid(D2,1,indx(D2,D1,1)).k=(D2|indx(D2,D1,1)).k
      proof
        let k be Nat;
        assume
A19:    1 <= k & k <= len mid(D2,1,indx(D2,D1,1));
        k in NAT by ORDINAL1:def 13;
        then mid(D2,1,indx(D2,D1,1)).k=D2.(k+1-'1) by A14,A15,A19,JORDAN3:27;
        then
A20:    mid(D2,1,indx(D2,D1,1)).k=D2.(k+1-1) by BINARITH:50,NAT_1: 11;
        k in Seg len (D2|indx(D2,D1,1)) by A17,A19,FINSEQ_1:3;
        then k in dom (D2|indx(D2,D1,1)) by FINSEQ_1:def 3;
        then k in dom (D2|Seg indx(D2,D1,1)) by FINSEQ_1:def 15;
        then (D2|Seg indx(D2,D1,1)).k=D2.k by FUNCT_1:68;
        hence thesis by A20,FINSEQ_1:def 15;
      end;
      then
A21:  mid(D2,1,indx(D2,D1,1))=D2|indx(D2,D1,1) by A17,FINSEQ_1:18;
      set DD1=mid(D1,1,1);
      set B=divset(D1,1);
      consider S1 being non empty Division of B;
A22:  D1.1 = sup B by A4,Def5;
      then D1.1 >= inf B by SEQ_4:24;
      then reconsider DD1 as Element of S1 by A4,A22,Th39;
A23:  indx(D2,D1,1) in dom D2 by A1,A4,Def21;
      then indx(D2,D1,1) in Seg(len D2) by FINSEQ_1:def 3;
      then
A24:  1 <= indx(D2,D1,1) & indx(D2,D1,1) <= len D2 by FINSEQ_1:3;
      then 1 <= len D2 by XXREAL_0:2;
      then 1 in Seg(len D2) by FINSEQ_1:3;
      then
A25:  1 in dom D2 by FINSEQ_1:def 3;
A26:  D2.indx(D2,D1,1) = sup B by A1,A4,A22,Def21;
A27:  rng D2 c= A by Def2;
      D2.1 in rng D2 by A25,FUNCT_1:def 5;
      then D2.1 in A by A27;
      then D2.1 in [.inf A,sup A.] by Th5;
      then D2.1 in {a: inf A <= a & a <= sup A} by RCOMP_1:def 1;
      then ex a st D2.1=a & inf A <= a & a <= sup A;
      then D2.1 >= inf B by A4,Def5;
      then reconsider DD2=mid(D2,1,indx(D2,D1,1)) as Element of S1
      by A23,A24,A25,A26,Th39;
      reconsider g=f|divset(D1,1) as PartFunc of divset(D1,1),REAL
      by PARTFUN1:43;
A28:  dom g = dom f /\ divset(D1,1) by FUNCT_1:68;
      then dom g = A /\ divset(D1,1) by FUNCT_2:def 1;
      then dom g = divset(D1,1) by A4,Th10,XBOOLE_1:28;
      then g is total by PARTFUN1:def 4;
      then
A29:  g is Function of divset(D1,1), REAL by FUNCT_2:131;
      g is_bounded_above_on divset(D1,1)
      proof
        consider a be real number such that
A30:    for x being set st x in A /\ dom f
        holds f.x <= a by A2,RFUNCT_1:def 9;
        for x being set st x in divset(D1,1) /\ dom g holds g.x <= a
        proof
          let x be set;
          assume x in divset(D1,1) /\ dom g;
          then
A31:      x in dom g by XBOOLE_0:def 3;
A32:      A /\ dom f = dom f by XBOOLE_1:28;
A33:      dom g c= dom f by RELAT_1:89;
          then x in A /\ dom f by A31,A32;
          then reconsider x as Element of A;
          f.x <= a by A30,A31,A32,A33;
          hence thesis by A31,FUNCT_1:68;
        end;
        hence thesis by RFUNCT_1:def 9;
      end;
      then
A34:  upper_sum(g,DD1) >= upper_sum(g,DD2) by A7,A29,Th32,BINARITH: 53;
A35:  upper_volume(g,DD1) = upper_volume(f,D1)|1
      proof
A36:    len upper_volume(g,DD1)=len DD1 by Def7
          .= 1 by A7,BINARITH:53;
        1 <= len upper_volume(f,D1) by A6,Def7;
        then
A37:    len upper_volume(g,DD1) = len (upper_volume(f,D1)|1)
        by A36,FINSEQ_1:80;
        for i be Nat st 1 <= i & i <= len upper_volume(g,DD1) holds
        upper_volume(g,DD1).i=(upper_volume(f,D1)|1).i
        proof
          let i be Nat;
          assume
A38:      1 <= i & i <= len upper_volume(g,DD1);
A39:      len DD1 = 1 by A7,BINARITH:53;
A40:      1 in Seg 1 by FINSEQ_1:5;
          then
A41:      1 in dom DD1 by A39,FINSEQ_1:def 3;
A42:      1 in dom(D1|Seg 1)
          proof
            dom(D1|Seg 1) = dom D1 /\ Seg 1 by FUNCT_1:68;
            hence thesis by A4,A40,XBOOLE_0:def 3;
          end;
A43:      upper_volume(g,DD1).i = upper_volume(g,DD1).1 by A36,A38,XXREAL_0:1
            .= (sup (rng (g|divset(DD1,1))))*vol(divset(DD1,1))
          by Def7,A41;
A44:      divset(DD1,1)=[.inf divset(DD1,1),sup divset(DD1,1).] by Th5
            .=[.inf B,sup divset(DD1,1).] by A41,Def5
            .=[.inf B,DD1.1 .] by A41,Def5
            .=[.inf A,(D1|1).1 .] by A4,A9,Def5
            .=[.inf A,(D1|Seg 1).1 .] by FINSEQ_1:def 15
            .=[.inf A,D1.1 .] by A42,FUNCT_1:68;
A45:      divset(D1,1)=[.inf divset(D1,1),sup divset(D1,1).] by Th5
            .=[.inf A,sup divset(D1,1).] by A4,Def5
            .=[.inf A,D1.1 .] by A4,Def5;
          dom(upper_volume(f,D1))=Seg(len upper_volume(f,D1))
          by FINSEQ_1:def 3
            .=Seg(len D1) by Def7;
          then dom(upper_volume(f,D1)|Seg 1)
          =Seg(len D1) /\ Seg 1 by FUNCT_1:68
            .=Seg 1 by A6,FINSEQ_1:9;
          then
A46:      1 in dom (upper_volume(f,D1)|Seg 1) by FINSEQ_1:5;
          (upper_volume(f,D1)|1).i=(upper_volume(f,D1)|Seg 1).i
          by FINSEQ_1:def 15
            .=(upper_volume(f,D1)|Seg 1).1 by A36,A38,XXREAL_0:1
            .=upper_volume(f,D1).1 by A46,FUNCT_1:68
            .=(sup (rng(f|divset(D1,1))))*vol(divset(D1,1)) by Def7,A4;
          hence thesis by A28,A43,A44,A45,RELAT_1:97;
        end;
        hence thesis by A37,FINSEQ_1:18;
      end;
      upper_volume(g,DD2) = upper_volume(f,D2)|indx(D2,D1,1)
      proof
A47:    len upper_volume(g,DD2)= indx(D2,D1,1) by A16,Def7;
        indx(D2,D1,1) <= len upper_volume(f,D2) by A14,Def7;
        then
A48:    len upper_volume(g,DD2)=len(upper_volume(f,D2)|indx(D2,D1, 1))
        by A47,FINSEQ_1:80;
        for i be Nat st 1 <= i & i <= len upper_volume(g,DD2) holds
        upper_volume(g,DD2).i = (upper_volume(f,D2)|indx(D2,D1,1)).i
        proof
          let i be Nat;
          assume
A49:      1 <= i & i <= len upper_volume(g,DD2);
A50:      1 <= i & i <= len DD2 & i in Seg(len DD2) & i in dom DD2
          proof
            thus 1 <= i & i <= len DD2 by A49,Def7;
            hence i in Seg(len DD2) by FINSEQ_1:3;
            hence thesis by FINSEQ_1:def 3;
          end;
          divset(DD2,i)=divset(D2,i)
          proof
A51:        i in dom DD2 & i in dom D2
            proof
              thus i in dom DD2 by A50;
              Seg indx(D2,D1,1) c= Seg(len D2) by A14,FINSEQ_1:7;
              then i in Seg(len D2) by A16,A50;
              hence thesis by FINSEQ_1:def 3;
            end;
            now per cases;
              suppose
A52:            i=1;
                then
A53:            1 in dom (D2|Seg indx(D2,D1,1)) by A21,A51,FINSEQ_1:def 15;
                then 1 in dom D2 /\ Seg indx(D2,D1,1) by RELAT_1:90;
                then
A54:            1 in dom D2 by XBOOLE_0:def 3;
A55:            divset
                (DD2,i)=[.inf divset(DD2,1),sup divset(DD2,1).] by A52,Th5
                  .=[.inf B,sup divset(DD2,1).] by A51,A52,Def5
                  .=[.inf B,DD2.1 .] by A51,A52,Def5
                  .=[.inf B,(D2|indx(D2,D1,1)).1 .] by A18,A50,A52
                  .=[.inf B,(D2|Seg indx(D2,D1,1)).1 .] by FINSEQ_1:def 15
                  .=[.inf B,D2.1 .] by A53,FUNCT_1:68
                  .=[.inf A,D2.1 .] by A4,Def5;
                divset(D2,i)=[.inf divset(D2,1),sup divset(D2,1).] by A52, Th5
                  .=[.inf A,sup divset(D2,1).] by A54,Def5
                  .=[.inf A,D2.1 .] by A54,Def5;
                hence thesis by A55;
              end;
              suppose
A56:            i<>1;
A57:            DD2.(i-1)=D2.(i-1)
                proof
A58:              i-1 in dom(D2|Seg indx(D2,D1,1))
                  proof
                    consider j being Nat such that
A59:                i = 1 + j by A50,NAT_1:10;
                    i is non trivial by A50,A56,NAT_2:def 1;
                    then
A60:                i>=1+1&i-1<=indx(D2,D1,1)-0 by A16,A50,NAT_2:31
                    ,REAL_1:92;
                    then i-1 >= 1 & i-1 <= indx(D2,D1,1) by XREAL_1:21;
                    then
A61:                i-1 in Seg indx(D2,D1,1) by A59, FINSEQ_1:3;
                    1 <= i-1 & i-1 <= len D2 by A24,A60,XREAL_1:21
                    ,XXREAL_0:2;
                    then i-1 in Seg(len D2) by A59,FINSEQ_1:3;
                    then i-1 in dom D2 by FINSEQ_1:def 3;
                    then i-1 in dom D2 /\ Seg indx(D2,D1,1) by A61,
                    XBOOLE_0:def 3;
                    hence thesis by FUNCT_1:68;
                  end;
                  DD2.(i-1)=(D2|indx(D2,D1,1)).(i-1) by A17,A18,FINSEQ_1:18
                    .=(D2|Seg indx(D2,D1,1)).(i-1) by FINSEQ_1:def 15;
                  hence thesis by A58,FUNCT_1:68;
                end;
A62:            DD2.i=D2.i
                proof
A63:              i in dom(D2|Seg indx(D2,D1,1))
                  proof
                    1 <= i & i <= len D2 by A16,A24,A50,XXREAL_0:2;
                    then i in Seg(len D2) by FINSEQ_1:3;
                    then i in dom D2 by FINSEQ_1:def 3;
                    then i in dom D2 /\ Seg indx(D2,D1,1) by A16,A50,
                    XBOOLE_0:def 3;
                    hence thesis by FUNCT_1:68;
                  end;
                  DD2.i=(D2|indx(D2,D1,1)).i by A17,A18,FINSEQ_1:18
                    .=(D2|Seg indx(D2,D1,1)).i by FINSEQ_1:def 15;
                  hence thesis by A63,FUNCT_1:68;
                end;
A64:            divset(DD2,i)=[.inf divset(DD2,i),sup divset(DD2,i).] by Th5
                  .=[. DD2.(i-1),sup divset(DD2,i).] by A51,A56,Def5
                  .=[. D2.(i-1),D2.i .] by A51,A56,A57,A62,Def5;
                divset(D2,i)=[.inf divset(D2,i),sup divset(D2,i).] by Th5
                  .=[. D2.(i-1),sup divset(D2,i).] by A51,A56,Def5
                  .=[. D2.(i-1),D2.i .] by A51,A56,Def5;
                hence thesis by A64;
              end;
            end;
            hence thesis;
          end;
          then
A65:      upper_volume(g,DD2).i
          =(sup (rng (g|divset(D2,i))))*vol(divset(D2,i)) by A50,Def7;
A66:      divset(D2,i) c= divset(D1,1)
          proof
A67:        divset(D2,i)=[.inf divset(D2,i),sup divset(D2,i).] by Th5;
A68:        divset(D1,1)=[.inf divset(D1,1),sup divset(D1,1).] by Th5;
            inf divset(D2,i) in [.inf divset(D1,1),sup divset(D1,1).] &
            sup divset(D2,i) in [.inf divset(D1,1),sup divset(D1,1).]
            proof
A69:          i in dom DD2 & i in dom D2
              proof
                thus i in dom DD2 by A50;
                Seg indx(D2,D1,1) c= Seg(len D2) by A14,FINSEQ_1:7;
                then i in Seg(len D2) by A16,A50;
                hence thesis by FINSEQ_1:def 3;
              end;
              now per cases;
                suppose
A70:              i=1;
                  then 1 in dom (D2|Seg indx(D2,D1,1))
                  by A21,A69,FINSEQ_1:def 15;
                  then 1 in dom D2 /\ Seg indx(D2,D1,1) by RELAT_1:90;
                  then
A71:              1 in dom D2 by XBOOLE_0:def 3;
                  then inf divset(D2,i)=inf A by A70,Def5;
                  then
A72:              inf divset(D2,i)=inf divset(D1,1) by A4,Def5;
                  inf divset(D1,1) <= sup divset(D1,1)
                  proof
                    ex a,b st a<=b & a=inf divset(D1,1) & b=sup divset(D1,1 )
                    by Th4;
                    hence thesis;
                  end;
                  hence inf divset(D2,i)in[.inf divset(D1,1),sup divset(D1,1).]
                  by A72,RCOMP_1:15;
A73:              sup divset(D2,i)=D2.1 by A70,A71,Def5;
                  D2.1 <= D2.indx(D2,D1,1) by A23,A24,A71,GOBOARD2:18;
                  then sup divset(D2,i) <= D1.1 by A1,A4,A73,Def21;
                  then
A74:              sup divset(D2,i) <= sup divset(D1,1) by A4,Def5;
                  inf divset(D2,i) <= sup divset(D2,i)
                  proof
                    ex a,b st a<=b & a=inf divset(D2,i) & b=sup divset(D2,i )
                    by Th4;
                    hence thesis;
                  end;
                  then consider a being Real such that
A75:              a = sup divset(D2,i) & inf divset(D1,1) <= a &
                  a <= sup divset(D1,1) by A72,A74;
                  sup divset(D2,i)in{r: inf divset(D1,1)<=r & r<=
                  sup divset(D1,1)} by A75;
                  hence sup divset(D2,i)in[.inf divset(D1,1),sup divset(D1,1).]
                  by RCOMP_1:def 1;
                end;
                suppose
A76:              i<>1;
                  consider j being Nat such that
A77:              i = 1 + j by A50,NAT_1:10;
                  i is non trivial by A50,A76,NAT_2:def 1;
                  then
A78:              i >= 1+1 & i-1 <= indx(D2,D1,1)-0
                  by A16,A50,NAT_2:31,REAL_1:92;
                  then 1 <= i-1 & i-1 <= len D2 by A24,XREAL_1:21,XXREAL_0:2;
                  then i-1 in Seg(len D2) by A77,FINSEQ_1:3;
                  then
A79:              i-1 in dom D2 by FINSEQ_1:def 3;
A80:              A <> {} & A is bounded_below by Th3;
A81:              inf divset(D2,i)=D2.(i-1) by A69,A76,Def5;
A82:              sup divset(D2,i)=D2.i by A69,A76,Def5;
A83:              inf divset(D1,1)=inf A by A4,Def5;
A84:              sup divset(D1,1)=D1.1 by A4,Def5;
A85:              rng D2 c= A by Def2;
                  D2.(i-1) in rng D2 by A79,FUNCT_1:def 5;
                  then
A86:              inf divset(D2,i) >= inf divset(D1,1)
                  by A80,A81,A83,A85,SEQ_4:def 5;
                  D2.(i-1)<= D2.indx(D2,D1,1) by A23,A78,A79,GOBOARD2:18;
                  then inf divset(D2,i) <= sup divset(D1,1)
                  by A1,A4,A81,A84,Def21;
                  then consider a being Real such that
A87:              a = inf divset(D2,i) & inf divset(D1,1) <= a &
                  a <= sup divset(D1,1) by A86;
                  inf divset(D2,i)in{r: inf divset(D1,1)<=r & r<=
                  sup divset(D1,1)} by A87;
                  hence inf divset(D2,i)in[.inf divset(D1,1),sup divset(D1,1).]
                  by RCOMP_1:def 1;
                  D2.i in rng D2 by A69,FUNCT_1:def 5;
                  then
A88:              sup divset(D2,i) >= inf divset(D1,1)
                  by A80,A82,A83,A85,SEQ_4:def 5;
                  D2.i <= D2.indx(D2,D1,1) by A16,A23,A50,A69,GOBOARD2:18;

then sup divset(D2,i) <= sup divset(D1,1) by A1,A4,A82,A84,Def21;
                  then consider a being Real such that
A89:              a = sup divset(D2,i) & inf divset(D1,1) <= a &
                  a <= sup divset(D1,1) by A88;
                  sup divset(D2,i)in{r: inf divset(D1,1)<=r & r<=
                  sup divset(D1,1)} by A89;
                  hence sup divset(D2,i)in[.inf divset(D1,1),sup divset(D1,1).]
                  by RCOMP_1:def 1;
                end;
              end;
              hence thesis;
            end;
            hence thesis by A67,A68,RCOMP_1:16;
          end;
A90:      i in dom D2 & i in dom (upper_volume(f,D2)|Seg indx(D2,D1,1))
          proof
A91:        Seg indx(D2,D1,1) c= Seg(len D2) by A14,FINSEQ_1:7;
            then i in Seg(len D2) by A16,A50;
            hence i in dom D2 by FINSEQ_1:def 3;
            dom (upper_volume(f,D2)|Seg indx(D2,D1,1))
            =dom upper_volume(f,D2) /\ Seg indx(D2,D1,1) by FUNCT_1:68
              .=Seg(len upper_volume(f,D2)) /\
            Seg indx(D2,D1,1) by FINSEQ_1:def 3
              .=Seg(len D2) /\ Seg indx(D2,D1,1) by Def7
              .=Seg indx(D2,D1,1) by A91,XBOOLE_1:28;
            hence thesis by A16,A50;
          end;
          (upper_volume(f,D2)|indx(D2,D1,1)).i
          =(upper_volume(f,D2)|Seg indx(D2,D1,1)).i by FINSEQ_1:def 15
            .=upper_volume(f,D2).i by A90,FUNCT_1:68
            .=(sup (rng (f|divset(D2,i))))*vol(divset(D2,i)) by A90,Def7;
          hence thesis by A65,A66,FUNCT_1:82;
        end;
        hence thesis by A48,FINSEQ_1:18;
      end;
      hence thesis by A34,A35;
    end;
A92: for k being non empty Nat st P[k] holds P[k+1]
    proof
      let k be non empty Nat;
      assume
A93:  k in dom D1 implies
      Sum(upper_volume(f,D1)|k) >= Sum(upper_volume(f,D2)|indx(D2,D1,k));
      assume
A94:  k+1 in dom D1;
      then
      k+1 in Seg(len D1) by FINSEQ_1:def 3;
      then
A96:  1 <= k+1 & k+1 <= len D1 by FINSEQ_1:3;
      now per cases;
        suppose 1=k+1;
          hence Sum(upper_volume(f,D1)|(k+1))
          >=Sum(upper_volume(f,D2)|indx(D2,D1,k+1)) by A3,A94;
        end;
        suppose
A97:      1<>k+1;
          set n=k+1;
          n is non trivial by A97,NAT_2:def 1;
          then n >= 2 by NAT_2:31;
          then
A98:      k >= (1+1)-1 by XREAL_1:22;
A99:      k <= k+1 by NAT_1:11;
          then
A100:     1 <= k & k <= len D1 by A96,A98,XXREAL_0:2;
          then
A101:     k in Seg(len D1) by FINSEQ_1:3;
          then
A102:     k in dom D1 by FINSEQ_1:def 3;
A103:     1 <= k+1 & k+1 <= len upper_volume(f,D1) by A96,Def7;
A104:     len upper_volume(f,D2) = len D2 by Def7;
A105:     indx(D2,D1,k) in dom D2 by A1,A102,Def21;
A106:     indx(D2,D1,k+1) in dom D2 by A1,A94,Def21;
          then
A107:     indx(D2,D1,k+1) in Seg(len D2) by FINSEQ_1:def 3;
          then
A108:     1 <= indx(D2,D1,k+1) & indx(D2,D1,k+1) <= len D2 by FINSEQ_1:3;
A109:     1 <= indx(D2,D1,k+1) & indx(D2,D1,k+1) <= len upper_volume(f,D2)
          by A104,A107,FINSEQ_1:3;
A110:     indx(D2,D1,k) < indx(D2,D1,k+1)
          proof
            assume indx(D2,D1,k) >= indx(D2,D1,k+1);
            then
A111:       D2.indx(D2,D1,k) >= D2.indx(D2,D1,k+1) by A105, A106, GOBOARD2:18;
            k < k+1 by NAT_1:13;
            then
A112:       D1.k < D1.(k+1) by A94,A102,GOBOARD1:def 1;
            D1.k=D2.indx(D2,D1,k) by A1,A102,Def21;
            hence contradiction by A1,A94,A111,A112,Def21;
          end;
A113:     indx(D2,D1,k+1) >= indx(D2,D1,k)
          proof
            assume indx(D2,D1,k+1) < indx(D2,D1,k);
            then
A114:       D2.indx(D2,D1,k+1) < D2.indx(D2,D1,k) by A105,A106,
            GOBOARD1:def 1;
            D1.(k+1) = D2.indx(D2,D1,k+1) by A1,A94,Def21;
            then D1.(k+1) < D1.k by A1,A102,A114,Def21;
            hence contradiction by A94,A102,GOBOARD2:18,NAT_1:11;
          end;
          then consider ID being Nat such that
A115:     indx(D2,D1,k+1) = indx(D2,D1,k) + ID by NAT_1:10;
          reconsider ID as Element of NAT by ORDINAL1:def 13;
A116:     ID = indx(D2,D1,k+1) - indx(D2,D1,k) by A115;
A117:     1 <= k & k <= len upper_volume(f,D1) by A100,Def7;
          indx(D2,D1,k) in dom D2 by A1,A102,Def21;
          then indx(D2,D1,k)in
          Seg(len upper_volume(f,D2)) by A104,FINSEQ_1:def 3;
          then
A118:     1<=indx(D2,D1,k) & indx(D2,D1,k)<=len upper_volume(f, D2)
          by FINSEQ_1:3;
          set K1D1=upper_volume(f,D1)|(k+1);
          set KD1 =upper_volume(f,D1)|k;
          set K1D2=upper_volume(f,D2)|indx(D2,D1,k+1);
          set KD2 =upper_volume(f,D2)|indx(D2,D1,k);
          set IDK1=indx(D2,D1,k+1);
          set IDK =indx(D2,D1,k);
A119:     len K1D1=k+1 by A103,FINSEQ_1:80;
          then consider p1,q1 being FinSequence of REAL such that
A120:     len p1=k & len q1=1 & K1D1=p1^q1 by FINSEQ_2:26;
          len K1D2=IDK + (IDK1-IDK) by A109,FINSEQ_1:80;
          then consider p2,q2 being FinSequence of REAL such that
A121:     len p2=IDK & len q2=IDK1-IDK & K1D2=p2^q2 by A115,FINSEQ_2:26;
A122:     p1=KD1
          proof
A123:       len p1 = len KD1 by A117,A120,FINSEQ_1:80;
            for i be Nat st 1 <= i & i <= len p1 holds p1.i=KD1.i
            proof
              let i be Nat;
              assume 1 <= i & i <= len p1;
              then
A124:         i in Seg(len p1) by FINSEQ_1:3;
              then
A125:         i in dom p1 by FINSEQ_1:def 3;
A126:         i in dom KD1 by A123,A124,FINSEQ_1:def 3;
              then
A127:         i in dom (upper_volume(f,D1)|Seg k) by FINSEQ_1:def 15;
A128:         KD1.i = (upper_volume(f,D1)|Seg k).i by FINSEQ_1:def 15
                .= upper_volume(f,D1).i by A127,FUNCT_1:68;
A129:         dom KD1=Seg(len KD1) by FINSEQ_1:def 3
                .= Seg k by A117,FINSEQ_1:80;
              k <= k+1 by NAT_1:11;
              then
A130:         Seg k c= Seg(k+1) by FINSEQ_1:7;
              dom K1D1 =Seg(len K1D1) by FINSEQ_1:def 3
                .= Seg(k+1)
              by A103,FINSEQ_1:80;
              then i in dom K1D1 by A126,A129,A130;
              then
A131:         i in dom (upper_volume(f,D1)|Seg(k+1)) by FINSEQ_1:def 15;
              K1D1.i = (upper_volume(f,D1)|Seg (k+1)).i by FINSEQ_1:def 15
                .= upper_volume(f,D1).i by A131,FUNCT_1:68;
              hence thesis by A120,A125,A128,FINSEQ_1:def 7;
            end;
            hence thesis by A123,FINSEQ_1:18;
          end;
A132:     p2=KD2
          proof
A133:       len p2 = len KD2 by A118,A121,FINSEQ_1:80;
            for i be Nat st 1 <= i & i <= len p2 holds p2.i=KD2.i
            proof
              let i be Nat;
              assume 1 <= i & i <= len p2;
              then
A134:         i in Seg(len p2) by FINSEQ_1:3;
              then
A135:         i in dom p2 by FINSEQ_1:def 3;
A136:         i in dom KD2 by A133,A134,FINSEQ_1:def 3;
              then
A137:         i in
              dom (upper_volume(f,D2)|Seg indx(D2,D1,k)) by FINSEQ_1:def 15;
A138:         KD2.i=(upper_volume(f,D2)|Seg indx(D2,D1,k)).i by FINSEQ_1:def 15
                .= upper_volume(f,D2).i by A137,FUNCT_1:68;
A139:         dom KD2 =Seg(len KD2) by FINSEQ_1:def 3
                .= Seg indx(D2,D1,k) by A118,FINSEQ_1:80;
A140:         Seg indx(D2,D1,k) c= Seg indx(D2,D1,k+1) by A113,FINSEQ_1:7;
              dom K1D2 = Seg(len K1D2) by FINSEQ_1:def 3
                .= Seg indx(D2,D1,k+1) by A109,FINSEQ_1:80;
              then i in dom K1D2 by A136,A139,A140;
              then
A141:         i in dom (upper_volume(f,D2)|Seg indx(D2,D1,k+ 1))
              by FINSEQ_1:def 15;
              K1D2.i=(upper_volume(f,D2)|Seg indx(D2,D1,k+1)).i
              by FINSEQ_1:def 15
                .=upper_volume(f,D2).i by A141,FUNCT_1:68;
              hence thesis by A121,A135,A138,FINSEQ_1:def 7;
            end;
            hence thesis by A133,FINSEQ_1:18;
          end;
A142:     Sum q1 >= Sum q2
          proof
            set DD1 = divset(D1,k+1);
            set MD1 = mid(D1,k+1,k+1);
            set MD2 = mid(D2,indx(D2,D1,k)+1,indx(D2,D1,k+1));
            set g = f|DD1;
            consider S1 being non empty Division of DD1;
A143:       k+1 in dom D1 & D1.(k+1) >= inf DD1 & D1.(k+1) = sup DD1
            proof
              (k+1)-1=k;
              then inf DD1=D1.k by A94,A97,Def5;
              hence thesis by A94,A97,A99,A102,Def5,GOBOARD2:18;
            end;
            then reconsider MD1 as Element of S1 by Th39;
A144:       indx(D2,D1,k)+1 <= indx(D2,D1,k+1) by A110,NAT_1:13;
            then
A145:       indx(D2,D1,k)+1 <= len D2 by A108,XXREAL_0:2;
A146:       1 <= indx(D2,D1,k)+1 by NAT_1:11;
            then indx(D2,D1,k)+1 in Seg(len D2) by A145,FINSEQ_1:3;
            then
A147:       indx(D2,D1,k)+1 in dom D2 by FINSEQ_1:def 3;
A148:       D2.indx(D2,D1,k+1)=D1.(k+1) by A1,A94,Def21;
            (k+1)-1=k;
            then
A149:       inf DD1=D1.k by A94,A97,Def5;

D2.(indx(D2,D1,k)+1)>=D2.indx(D2,D1,k)by A105,A147,GOBOARD2:18,NAT_1 :11;
            then
A150:       D2.(indx(D2,D1,k)+1) >= inf DD1 by A1,A102,A149, Def21;
            D2.indx(D2,D1,k+1) = sup DD1 by A94,A97,A148,Def5;
            then reconsider MD2 as Element of S1 by A106,A144,A147,A150,Th39;
            reconsider g as PartFunc of DD1,REAL by PARTFUN1:43;
A151:       g is total & g is_bounded_above_on DD1
            proof
              dom g = dom f /\ divset(D1,k+1) by FUNCT_1:68;
              then dom g = A /\ divset(D1,k+1) by FUNCT_2:def 1;
              then dom g = divset(D1,k+1) by A143,Th10,XBOOLE_1:28;
              hence g is total by PARTFUN1:def 4;
              g is_bounded_above_on divset(D1,k+1)
              proof
                consider a be real number such that
A152:           for x being set st x in A /\ dom f
                holds f.x <= a by A2,RFUNCT_1:def 9;
                for x being set st x in divset(D1,k+1) /\ dom g holds g.x <= a
                proof
                  let x be set;
                  assume x in divset(D1,k+1) /\ dom g;
                  then
A153:             x in dom g by XBOOLE_0:def 3;
A154:             A /\ dom f = dom f by XBOOLE_1:28;
A155:             dom g c= dom f by RELAT_1:89;
                  then x in A /\ dom f by A153,A154;
                  then reconsider x as Element of A;
                  f.x <= a by A152,A153,A154,A155;
                  hence thesis by A153,FUNCT_1:68;
                end;
                hence thesis by RFUNCT_1:def 9;
              end;
              hence thesis;
            end;
            then
A156:       g is Function of divset(D1,k+1),REAL by FUNCT_2: 131;
A157:       q1=upper_volume(g,MD1)
            proof
              len MD1 = (k+1) -'(k+1) + 1 by A96,JORDAN3:27;
              then
A158:         len MD1 = ((k+1)-(k+1)) +1 by BINARITH:50;
              then
A159:         len q1 = len(upper_volume(g,MD1)) by A120,Def7;

Y:            dom q1 = dom MD1 by A120,A158,FINSEQ_3:31;
for n be Nat st 1 <= n & n <= len q1 holds q1.n=upper_volume(g,MD1).n
              proof
                let n be Nat;
                assume
A160:           1 <= n & n <= len q1;
                then
A161:           n = 1 by A120,XXREAL_0:1;
                n in Seg(len q1) by A160,FINSEQ_1:3;
                then
A163:           n in dom q1 by FINSEQ_1:def 3;
                k+1 in Seg(len K1D1) by A119,FINSEQ_1:6;
                then k+1 in dom K1D1 by FINSEQ_1:def 3;
                then
A164:           k+1 in dom (upper_volume(f,D1)|Seg(k+1)) by FINSEQ_1:def 15;

K1D1.(k+1)=(upper_volume(f,D1)|Seg(k+1)).(k+1) by FINSEQ_1:def 15
                  .=upper_volume(f,D1).(k+1) by A164,FUNCT_1:68;
                then
A165:           q1.n = upper_volume(f,D1).(k+1) by A120, A161,A163,
                FINSEQ_1:def 7

.=(sup(rng(f|divset(D1,k+1))))*vol(divset(D1,k+1)) by Def7,A94;
A166:           divset(MD1,1)=[.inf divset(MD1,1),sup divset(MD1,1).] by Th5;
A167:           1 in dom MD1
                proof
                  1 in Seg(len MD1) by A158,FINSEQ_1:5;
                  hence thesis by FINSEQ_1:def 3;
                end;
A168:           MD1.1 =D1.(k+1) by A96,JORDAN3:27;
A169:           divset(MD1,1)=[.inf DD1,sup divset(MD1,1).] by A166,A167,Def5
                  .=[.inf DD1,D1.(k+1).] by A167,A168,Def5;
                (k+1)-1=k;
                then
A170:           inf DD1 = D1.k by A94,A97,Def5;
                sup DD1 = D1.(k+1) by A94,A97,Def5;
                then divset(D1,k+1)=[. D1.k,D1.(k+1).] by A170,Th5;
                then upper_volume(g,MD1).n
                =(sup(rng(g|divset(D1,k+1))))*vol(divset(D1,k+1))
                by A161,A169,A170,Def7,A163,Y;
                hence thesis by A165,FUNCT_1:82;
              end;
              hence thesis by A159,FINSEQ_1:18;
            end;
A171:       q2=upper_volume(g,MD2)
            proof
A172:         indx(D2,D1,k+1)-'(indx(D2,D1,k)+1)+1
              =indx(D2,D1,k+1)-(indx(D2,D1,k)+1)+1 by A144,BINARITH:50
                .=indx(D2,D1,k+1)-indx(D2,D1,k);
A173:         len(upper_volume(g,MD2))=
              len mid(D2,indx(D2,D1,k)+1,indx(D2,D1,k+1)) by Def7
                .=indx(D2,D1,k+1)-indx(D2,D1,k) by A108,A144,A145,A146,A172,
              JORDAN3:27;

for n be Nat st 1 <= n & n <= len q2 holds q2.n=upper_volume(g,MD2).n
              proof
                let n be Nat;
                assume
A174:           1 <= n & n <= len q2;
                then
A175:           n in Seg(len q2) by FINSEQ_1:3;
                then
A176:           n in dom q2 by FINSEQ_1:def 3;
A177:           dom K1D2 = Seg(len K1D2) by FINSEQ_1:def 3
                  .= Seg indx(D2,D1,k+1) by A109,FINSEQ_1:80;
A178:           indx(D2,D1,k)+n in dom K1D2 by A121,A176,FINSEQ_1:41;
                then
A179:           indx(D2,D1,k)+n in dom (upper_volume(f,D2) |Seg indx(D2,D1,k
                +1)) by FINSEQ_1:def 15;
A180:           dom K1D2 c= Seg(len D2) by A108,A177,FINSEQ_1:7;
                then
B180:           dom K1D2 c= dom D2 by FINSEQ_1:def 3;
                indx(D2,D1,k)+n in Seg len D2 by A178,A180;
                then
A181:           indx(D2,D1,k)+n in dom D2 by FINSEQ_1:def 3;
A182:           1 <=indx(D2,D1,k)+n & indx(D2,D1,k)+n <= indx(D2,D1,k+1)
                by A177,A178,FINSEQ_1:3;
                then
A183:           n <= ID by A116,XREAL_1:21;
                then
A184:           n in Seg ID by A174,FINSEQ_1:3;
A185:           len mid(D2,indx(D2,D1,k)+1,indx(D2,D1,k+1))
                =ID by A108,A115,A144,A145,A146,A172,JORDAN3:27;
                then
                n in Seg(len mid(D2,indx(D2,D1,k)+1,indx( D2, D1,k+1)))
                by A174,A183,FINSEQ_1:3;
                then
B186:           n in dom mid(D2,indx(D2,D1,k)+1,indx( D2, D1,k+1))
 by FINSEQ_1:def 3;
A187:           n in dom MD2 by A184,A185,FINSEQ_1:def 3;
A188:           q2.n=K1D2.(indx(D2,D1,k)+n) by A121,A176,FINSEQ_1:def 7

.=(upper_volume(f,D2)|Seg indx(D2,D1,k+1)).(indx(D2,D1,k)+n) by FINSEQ_1:def 15
                  .=upper_volume(f,D2).(indx(D2,D1,k)+n) by A179,FUNCT_1:68

.=(sup(rng(f|divset(D2,indx(D2,D1,k)+n))))*vol(divset(D2,indx(D2,D1,k)+n))
                by A178,Def7,B180;
A189:           divset(MD2,n)=divset(D2,indx(D2,D1,k)+n) &
                divset(D2,indx(D2,D1,k)+n) c= divset(D1,k+1)
                proof
                  now per cases;
                    suppose
A190:                 n=1;
A191:                 (k+1)-1=k;
A192:                 1<=indx(D2,D1,k)+1 & indx(D2,D1,k)+1<=len D2
                      by A178,A180,A190,FINSEQ_1:3;
A193:                 indx(D2,D1,k)+1 <> 1 by A118,NAT_1:13;
A194:                 inf divset(MD2,1)=inf divset(D1,k+1) by A187,A190,Def5
                        .= D1.k by A94,A97,A191,Def5;
A195:                 sup divset(MD2,1)=

mid(D2,indx(D2,D1,k)+1,indx(D2,D1,k+1)).1 by A187,A190,Def5
                        .= D2.(1+indx(D2,D1,k)) by A108,A192,JORDAN3:27;
                      then
A196:                 divset(MD2,n)=[.D1.k,D2.(indx( D2,D1,k)+1).] by A190
                      ,A194,Th5;
A197:                 divset(D2,indx(D2,D1,k)+n)=

[.inf divset(D2,indx(D2,D1,k)+1),sup divset(D2,indx(D2,D1,k)+1).] by A190,Th5

.=[.D2.(indx(D2,D1,k)+1-1),sup divset(D2,indx(D2,D1,k)+1).] by A147,A193,Def5

.=[.D2.indx(D2,D1,k),D2.(indx(D2,D1,k)+1).] by A147,A193,Def5
                        .=[.D1.k,D2.(indx(D2,D1,k)+1).] by A1,A102,Def21;

hence divset(MD2,n)=divset(D2,indx(D2,D1,k)+n) by A190,A194,A195,Th5;
                      thus thesis by A187,A196,A197,Th10;
                    end;
                    suppose
A198:                 n<>1;
A199:                 indx(D2,D1,k)+1 <= indx(D2,D1,k+1) by A110,NAT_1:13;
                      consider n1 being Nat such that
A200:                 n=1+n1 by A174,NAT_1:10;
                      reconsider n1 as Element of NAT by ORDINAL1:def 13;
A201:                 n1+(indx(D2,D1,k)+1)-'1=indx(D2,D1,k)+n-1
                      by A182,A200,BINARITH:50;
A202:                 1<=n-1 & n-1<=len MD2
                      proof
                        n is non trivial by A174,A198,NAT_2:def 1;
                        then n >= 1+1 by NAT_2:31;
                        hence n-1>=1 by XREAL_1:21;
                        n<=n+1 by NAT_1:12;
                        then n-1 <= n by XREAL_1:22;
                        hence thesis by A183,A185,XXREAL_0:2;
                      end;
A203:                 n

+(indx(D2,D1,k)+1)-'1=n+indx(D2,D1,k)+1-1 by BINARITH:50, NAT_1:11
                        .=indx(D2,D1,k)+n;
A204:                 indx(D2,D1,k)+n <> 1
                      proof
                        assume indx(D2,D1,k)+n = 1;
                        then indx(D2,D1,k)=1-n;
                        then n+1 <= 1 by A118,XREAL_1:21;
                        then n <= 1-1 by XREAL_1:21;
                        hence contradiction by A174,NAT_1:3;
                      end;
A205:                 inf divset(MD2,n)=MD2.(n-1) by A187,A198,Def5

.=D2.(indx(D2,D1,k)+n-1) by A108,A145,A146,A199,A200,A201,A202, JORDAN3:27;
A206:                 sup divset(MD2,n)=MD2.n by A187,A198,Def5

.=D2.(indx(D2,D1,k)+n) by A108,A145,A146,A174,A175,A183,A185,A199,A203,
                      JORDAN3:27;
                      then
A207:                 divset(MD2,n)=

[. D2.(indx(D2,D1,k)+n-1),D2.(indx(D2,D1,k)+n) .] by A205,Th5;
A208:                 divset(D2,indx(D2,D1,k)+n)

=[.inf divset(D2,indx(D2,D1,k)+n),sup divset(D2,indx(D2,D1,k)+n).] by Th5

.=[.D2.(indx(D2,D1,k)+n-1),sup divset(D2,indx(D2,D1,k)+n).] by A181,A204,Def5

.=[. D2.(indx(D2,D1,k)+n-1),D2.(indx(D2,D1,k)+n) .] by A181,A204,Def5;

hence divset(MD2,n)=divset(D2,indx(D2,D1,k)+n) by A205,A206, Th5;
                      thus thesis by A187,A207,A208,Th10;
                    end;
                  end;
                  hence thesis;
                end;
                then g|divset(MD2,n)
                =f|divset(D2,indx(D2,D1,k)+n) by FUNCT_1:82;
                hence thesis by A188,A189,Def7,B186;
              end;
              hence thesis by A121,A173,FINSEQ_1:18;
            end;
            len MD1 = 1 & MD1 <= MD2
            proof
              len MD1 = (k+1) -'(k+1) + 1 by A96,JORDAN3:27;
              then
A209:         len MD1 = (k+1) - (k+1) + 1 by BINARITH:50;
              hence len MD1 = 1;
              thus thesis by A209,Th31;
            end;
            then upper_sum(g,MD1) >= upper_sum(g,MD2) by A151,A156,Th32;
            hence thesis by A157,A171;
          end;
A210:     Sum K1D1=Sum p1+Sum q1 by A120,RVSUM_1:105;
A211:     Sum K1D2=Sum p2+Sum q2 by A121,RVSUM_1:105;
          Sum q1 = Sum K1D1 - Sum p1 by A210;
          then Sum K1D1 >= Sum q2 + Sum p1 by A142,XREAL_1:21;
          then Sum K1D1 - Sum q2 >= Sum p1 by XREAL_1:21;
          then Sum K1D1 - Sum q2 >= Sum
          p2 by A93,A101,A122,A132,FINSEQ_1:def 3,XXREAL_0:2;
          hence thesis by A211,XREAL_1:21;
        end;
      end;
      hence thesis;
    end;
    thus for k being non empty Nat holds P[k] from NAT_1:sch 10(A3,A92);
  end;
  hence thesis;
end;

theorem Th41:
  for A be closed-interval Subset of REAL,
  f be Function of A,REAL, S be non empty Division of A,
  D1,D2 be Element of S st D1 <= D2 & f is_bounded_below_on A
  holds for i be non empty Element of NAT holds (i in dom D1 implies
  Sum(lower_volume(f,D1)|i) <= Sum(lower_volume(f,D2)|indx(D2,D1,i)))
proof
  let A be closed-interval Subset of REAL;
  let f be Function of A,REAL;
  let S be non empty Division of A;
  let D1,D2 be Element of S;
  assume that
A1: D1 <= D2 and
A2: f is_bounded_below_on A;
  for i be non empty Nat holds i in dom D1 implies
  Sum(lower_volume(f,D1)|i) <= Sum(lower_volume(f,D2)|indx(D2,D1,i))
  proof
    defpred P[Nat] means $1 in dom D1 implies
    Sum(lower_volume(f,D1)|$1) <= Sum(lower_volume(f,D2)|indx(D2,D1,$1));
A3: P[1]
    proof
      assume
A4:   1 in dom D1;
      then
      1 in Seg(len D1) by FINSEQ_1:def 3;
      then
A6:   1 <= len D1 by FINSEQ_1:3;
      then
A7:   len mid(D1,1,1)=1-'1+1 by JORDAN3:27;
      then
A8:   len mid(D1,1,1)=1 by BINARITH:53;
A9:   mid(D1,1,1)=D1|1
      proof
A10:    len mid(D1,1,1)=len (D1|1) by A6,A8,FINSEQ_1:80;

for k be Nat st 1 <= k & k <= len mid(D1,1,1) holds mid(D1,1,1).k=(D1|1).k
        proof
          let k be Nat;
          assume
A11:      1 <= k & k <= len mid(D1,1,1);
          then
A12:      k = 1 by A8,XXREAL_0:1;
          then
A13:      mid(D1,1,1).k = D1.(1+1-1) by A6,JORDAN3:27;
          k in Seg(len(D1|1)) by A10,A11,FINSEQ_1:3;
          then k in dom (D1|1) by FINSEQ_1:def 3;
          then k in dom (D1|Seg 1) by FINSEQ_1:def 15;
          then (D1|Seg 1).k = D1.k by FUNCT_1:68;
          hence thesis by A12,A13,FINSEQ_1:def 15;
        end;
        hence thesis by A10,FINSEQ_1:18;
      end;
      indx(D2,D1,1) in dom D2 by A1,A4,Def21;
      then indx(D2,D1,1) in Seg(len D2) by FINSEQ_1:def 3;
      then
A14:  1 <= indx(D2,D1,1) & indx(D2,D1,1) <= len D2 by FINSEQ_1:3;
      then
A15:  1 <= len D2 by XXREAL_0:2;
      then len mid(D2,1,indx(D2,D1,1))=indx(D2,D1,1)-'1+1 by A14,JORDAN3:27;
      then
A16:  len mid(D2,1,indx(D2,D1,1))=indx(D2,D1,1)-1+1 by A14, BINARITH :50;
      then
A17:  len mid(D2,1,indx(D2,D1,1))=len(D2|indx(D2,D1,1)) by A14, FINSEQ_1:80;
A18:  for k be Nat st 1 <= k & k <= len mid(D2,1,indx(D2,D1,1)) holds
      mid(D2,1,indx(D2,D1,1)).k=(D2|indx(D2,D1,1)).k
      proof
        let k be Nat;
        assume
A19:    1 <= k & k <= len mid(D2,1,indx(D2,D1,1));
        then
A20:    k in Seg len (D2|indx(D2,D1,1)) by A17,FINSEQ_1:3;
        then mid(D2,1,indx(D2,D1,1)).k=D2.(k+1-'1) by A14,A15,A19,JORDAN3:27;
        then
A21:    mid(D2,1,indx(D2,D1,1)).k=D2.(k+1-1) by BINARITH:50,NAT_1: 11;
        k in dom (D2|indx(D2,D1,1)) by A20,FINSEQ_1:def 3;
        then k in dom (D2|Seg indx(D2,D1,1)) by FINSEQ_1:def 15;
        then (D2|Seg indx(D2,D1,1)).k=D2.k by FUNCT_1:68;
        hence thesis by A21,FINSEQ_1:def 15;
      end;
      then
A22:  mid(D2,1,indx(D2,D1,1))=D2|indx(D2,D1,1) by A17,FINSEQ_1:18;
      set DD1=mid(D1,1,1);
      set DD2=mid(D2,1,indx(D2,D1,1));
      set B=divset(D1,1);
      consider S1 being non empty Division of B;
A23:  D1.1 = sup B by A4,Def5;
      inf B <= sup B by SEQ_4:24;
      then reconsider DD1 as Element of S1 by A4,A23,Th39;
A24:  indx(D2,D1,1) in dom D2 by A1,A4,Def21;
      then indx(D2,D1,1) in Seg(len D2) by FINSEQ_1:def 3;
      then
A25:  1 <= indx(D2,D1,1) & indx(D2,D1,1) <= len D2 by FINSEQ_1:3;
      then 1 <= len D2 by XXREAL_0:2;
      then 1 in Seg(len D2) by FINSEQ_1:3;
      then
A26:  1 in dom D2 by FINSEQ_1:def 3;
A27:  D2.indx(D2,D1,1) = sup B by A1,A4,A23,Def21;
A28:  rng D2 c= A by Def2;
      D2.1 in rng D2 by A26,FUNCT_1:def 5;
      then D2.1 in A by A28;
      then D2.1 in [.inf A,sup A.] by Th5;
      then D2.1 in {a: inf A <= a & a <= sup A} by RCOMP_1:def 1;
      then ex a st D2.1=a & inf A <= a & a <= sup A;
      then D2.1 >= inf B by A4,Def5;
      then reconsider DD2 as Element of S1 by A24,A25,A26,A27,Th39;
      set g=f|divset(D1,1);
      reconsider g as PartFunc of divset(D1,1),REAL by PARTFUN1:43;
A29:  dom g = dom f /\ divset(D1,1) by FUNCT_1:68;
      then dom g = A /\ divset(D1,1) by FUNCT_2:def 1;
      then dom g = divset(D1,1) by A4,Th10,XBOOLE_1:28;
      then g is total by PARTFUN1:def 4;
      then
A30:  g is Function of divset(D1,1),REAL by FUNCT_2:131;
      g is_bounded_below_on divset(D1,1)
      proof
        consider a be real number such that
A31:    for x being set st x in A /\ dom f
        holds a <= f.x by A2,RFUNCT_1:def 10;
        for x being set st x in divset(D1,1) /\ dom g holds a <= g.x
        proof
          let x be set;
          assume x in divset(D1,1) /\ dom g;
          then
A32:      x in dom g by XBOOLE_0:def 3;
A33:      A /\ dom f = dom f by XBOOLE_1:28;
A34:      dom g c= dom f by RELAT_1:89;
          then reconsider x as Element of A by A32,A33,XBOOLE_0:def 3;
          a <= f.x by A31,A32,A33,A34;
          hence thesis by A32,FUNCT_1:68;
        end;
        hence thesis by RFUNCT_1:def 10;
      end;
      then
A35:  lower_sum(g,DD1) <= lower_sum(g,DD2) by A7,A30,Th33,BINARITH: 53;
A36:  lower_volume(g,DD1) = lower_volume(f,D1)|1
      proof
A37:    len lower_volume(g,DD1)=len DD1 by Def8
          .= 1 by A7,BINARITH:53;
        1 <= len lower_volume(f,D1) by A6,Def8;
        then
A38:    len lower_volume(g,DD1) = len (lower_volume(f,D1)|1)
        by A37,FINSEQ_1:80;
        for i be Nat st 1 <= i & i <= len lower_volume(g,DD1) holds
        lower_volume(g,DD1).i=(lower_volume(f,D1)|1).i
        proof
          let i be Nat;
          assume
A39:      1 <= i & i <= len lower_volume(g,DD1);
A40:      len DD1 = 1 by A7,BINARITH:53;
A41:      1 in Seg 1 by FINSEQ_1:5;
A43:      1 in dom DD1 by A40,A41,FINSEQ_1:def 3;
A44:      1 in dom(D1|Seg 1)
          proof
            dom(D1|Seg 1) = dom D1 /\ Seg 1 by FUNCT_1:68;
            hence thesis by A4,A41,XBOOLE_0:def 3;
          end;
A45:      lower_volume(g,DD1).i = lower_volume(g,DD1).1 by A37,A39,XXREAL_0:1
            .= (inf (rng (g|divset(DD1,1))))*vol(divset(DD1,1)) by A43,Def8;
A46:      divset(DD1,1)=[.inf divset(DD1,1),sup divset(DD1,1).] by Th5
            .=[.inf B,sup divset(DD1,1).] by A43,Def5
            .=[.inf B,DD1.1 .] by A43,Def5
            .=[.inf A,(D1|1).1 .] by A4,A9,Def5
            .=[.inf A,(D1|Seg 1).1 .] by FINSEQ_1:def 15
            .=[.inf A,D1.1 .] by A44,FUNCT_1:68;
A47:      divset(D1,1)=[.inf divset(D1,1),sup divset(D1,1).] by Th5
            .=[.inf A,sup divset(D1,1).] by A4,Def5
            .=[.inf A,D1.1 .] by A4,Def5;
          dom(lower_volume(f,D1))=Seg(len lower_volume(f,D1))
          by FINSEQ_1:def 3
            .=Seg(len D1) by Def8;
          then dom(lower_volume(f,D1)|Seg 1)
          =Seg(len D1) /\ Seg 1 by FUNCT_1:68
            .=Seg 1 by A6,FINSEQ_1:9;
          then
A48:      1 in dom (lower_volume(f,D1)|Seg 1) by FINSEQ_1:5;
          (lower_volume(f,D1)|1).i=(lower_volume(f,D1)|Seg 1).i
          by FINSEQ_1:def 15
            .=(lower_volume(f,D1)|Seg 1).1 by A37,A39,XXREAL_0:1
            .=lower_volume(f,D1).1 by A48,FUNCT_1:68
            .=(inf (rng(f|divset(D1,1))))*vol(divset(D1,1)) by Def8,A4;
          hence thesis by A29,A45,A46,A47,RELAT_1:97;
        end;
        hence thesis by A38,FINSEQ_1:18;
      end;
      lower_volume(g,DD2) = lower_volume(f,D2)|indx(D2,D1,1)
      proof
A49:    len lower_volume(g,DD2)= indx(D2,D1,1) by A16,Def8;
        indx(D2,D1,1) <= len lower_volume(f,D2) by A14,Def8;
        then
A50:    len lower_volume(g,DD2)=len(lower_volume(f,D2)|indx(D2,D1, 1))
        by A49,FINSEQ_1:80;
        for i be Nat st 1 <= i & i <= len lower_volume(g,DD2) holds
        lower_volume(g,DD2).i = (lower_volume(f,D2)|indx(D2,D1,1)).i
        proof
          let i be Nat;
          assume
A51:      1 <= i & i <= len lower_volume(g,DD2);
A52:      (1 <= i & i <= len DD2) & i in Seg(len DD2) & i in dom DD2
          proof
            thus 1 <= i & i <= len DD2 by A51,Def8;
            hence i in Seg(len DD2) by FINSEQ_1:3;
            hence thesis by FINSEQ_1:def 3;
          end;
          divset(DD2,i)=divset(D2,i)
          proof
A53:        i in dom DD2 & i in dom D2
            proof
              thus i in dom DD2 by A52;
              Seg indx(D2,D1,1) c= Seg(len D2) by A14,FINSEQ_1:7;
              then i in Seg(len D2) by A16,A52;
              hence thesis by FINSEQ_1:def 3;
            end;
            now per cases;
              suppose
A54:            i=1;
                then
A55:            1 in dom (D2|Seg indx(D2,D1,1)) by A22,A53,FINSEQ_1:def 15;
                then 1 in dom D2 /\ Seg indx(D2,D1,1) by RELAT_1:90;
                then
A56:            1 in dom D2 by XBOOLE_0:def 3;
A57:            divset
                (DD2,i)=[.inf divset(DD2,1),sup divset(DD2,1).] by A54,Th5
                  .=[.inf B,sup divset(DD2,1).] by A53,A54,Def5
                  .=[.inf B,DD2.1 .] by A53,A54,Def5
                  .=[.inf B,(D2|indx(D2,D1,1)).1 .] by A18,A52,A54
                  .=[.inf B,(D2|Seg indx(D2,D1,1)).1 .] by FINSEQ_1:def 15
                  .=[.inf B,D2.1 .] by A55,FUNCT_1:68
                  .=[.inf A,D2.1 .] by A4,Def5;
                divset(D2,i)=[.inf divset(D2,1),sup divset(D2,1).] by A54, Th5
                  .=[.inf A,sup divset(D2,1).] by A56,Def5
                  .=[.inf A,D2.1 .] by A56,Def5;
                hence thesis by A57;
              end;
              suppose
A58:            i<>1;
A59:            DD2.(i-1)=D2.(i-1)
                proof
A60:              i-1 in dom(D2|Seg indx(D2,D1,1))
                  proof
                    consider j being Nat such that
A61:                i = 1 + j by A52,NAT_1:10;
                    i is non trivial by A52,A58,NAT_2:def 1;
                    then
A62:                i >= 1+1 & i-1 <= indx(D2,D1,1)-0
                    by A16,A52,NAT_2:31,REAL_1:92;
                    then i-1 >= 1 & i-1 <= indx(D2,D1,1) by XREAL_1:21;
                    then
A63:                i-1 in Seg indx(D2,D1,1) by A61, FINSEQ_1:3;
                    1 <= i-1 & i-1 <= len D2 by A25,A62,XREAL_1:21
                    ,XXREAL_0:2;
                    then i-1 in Seg(len D2) by A61,FINSEQ_1:3;
                    then i-1 in dom D2 by FINSEQ_1:def 3;
                    then i-1 in dom D2 /\ Seg indx(D2,D1,1) by A63,
                    XBOOLE_0:def 3;
                    hence thesis by FUNCT_1:68;
                  end;
                  DD2.(i-1)=(D2|indx(D2,D1,1)).(i-1) by A17,A18,FINSEQ_1:18
                    .=(D2|Seg indx(D2,D1,1)).(i-1) by FINSEQ_1:def 15;
                  hence thesis by A60,FUNCT_1:68;
                end;
A64:            DD2.i=D2.i
                proof
A65:              i in dom(D2|Seg indx(D2,D1,1))
                  proof
                    1 <= i & i <= len D2 by A16,A25,A52,XXREAL_0:2;
                    then i in Seg(len D2) by FINSEQ_1:3;
                    then i in dom D2 by FINSEQ_1:def 3;
                    then i in dom D2 /\ Seg indx(D2,D1,1) by A16,A52,
                    XBOOLE_0:def 3;
                    hence thesis by FUNCT_1:68;
                  end;
                  DD2.i=(D2|indx(D2,D1,1)).i by A17,A18,FINSEQ_1:18
                    .=(D2|Seg indx(D2,D1,1)).i by FINSEQ_1:def 15;
                  hence thesis by A65,FUNCT_1:68;
                end;
A66:            divset(DD2,i)=[.inf divset(DD2,i),sup divset(DD2,i).] by Th5
                  .=[. DD2.(i-1),sup divset(DD2,i).] by A53,A58,Def5
                  .=[. D2.(i-1),D2.i .] by A53,A58,A59,A64,Def5;
                divset(D2,i)=[.inf divset(D2,i),sup divset(D2,i).] by Th5
                  .=[. D2.(i-1),sup divset(D2,i).] by A53,A58,Def5
                  .=[. D2.(i-1),D2.i .] by A53,A58,Def5;
                hence thesis by A66;
              end;
            end;
            hence thesis;
          end;
          then
A67:      lower_volume(g,DD2).i
          =(inf (rng (g|divset(D2,i))))*vol(divset(D2,i)) by A52,Def8;
A68:      divset(D2,i) c= divset(D1,1)
          proof
A69:        divset(D2,i)=[.inf divset(D2,i),sup divset(D2,i).] by Th5;
A70:        divset(D1,1)=[.inf divset(D1,1),sup divset(D1,1).] by Th5;
            inf divset(D2,i) in [.inf divset(D1,1),sup divset(D1,1).] &
            sup divset(D2,i) in [.inf divset(D1,1),sup divset(D1,1).]
            proof
A71:          i in dom DD2 & i in dom D2
              proof
                thus i in dom DD2 by A52;
                Seg indx(D2,D1,1) c= Seg(len D2) by A14,FINSEQ_1:7;
                then i in Seg(len D2) by A16,A52;
                hence thesis by FINSEQ_1:def 3;
              end;
              now per cases;
                suppose
A72:              i=1;
                  then 1 in dom (D2|Seg indx(D2,D1,1))
                  by A22,A71,FINSEQ_1:def 15;
                  then 1 in dom D2 /\ Seg indx(D2,D1,1) by RELAT_1:90;
                  then
A73:              1 in dom D2 by XBOOLE_0:def 3;
                  then inf divset(D2,i)=inf A by A72,Def5;
                  then
A74:              inf divset(D2,i)=inf divset(D1,1) by A4,Def5;
                  inf divset(D1,1) <= sup divset(D1,1)
                  proof
                    ex a,b st a<=b & a=inf divset(D1,1) & b=sup divset(D1,1 )
                    by Th4;
                    hence thesis;
                  end;
                  hence inf divset(D2,i)in[.inf divset(D1,1),sup divset(D1,1).]
                  by A74,RCOMP_1:15;
A75:              sup divset(D2,i)=D2.1 by A72,A73,Def5;
                  D2.1 <= D2.indx(D2,D1,1) by A24,A25,A73,GOBOARD2:18;
                  then D2.1 <= D1.1 by A1,A4,Def21;
                  then
A76:              sup divset(D2,i) <= sup divset(D1,1) by A4,A75,Def5;
                  inf divset(D2,i) <= sup divset(D2,i)
                  proof
                    ex a,b st a<=b & a=inf divset(D2,i) & b=sup divset(D2,i )
                    by Th4;
                    hence thesis;
                  end;
                  then consider a being Real such that
A77:              a = sup divset(D2,i) & inf divset(D1,1) <= a &
                  a <= sup divset(D1,1) by A74,A76;
                  sup divset(D2,i)in{r: inf divset(D1,1)<=r & r<=
                  sup divset(D1,1)} by A77;
                  hence sup divset(D2,i)in[.inf divset(D1,1),sup divset(D1,1).]
                  by RCOMP_1:def 1;
                end;
                suppose
A78:              i<>1;
                  consider j being Nat such that
A79:              i = 1 + j by A52,NAT_1:10;
                  i is non trivial by A52,A78,NAT_2:def 1;
                  then
A80:              i >= 1+1 & i-1 <= indx(D2,D1,1)-0
                  by A16,A52,NAT_2:31,REAL_1:92;
                  then 1 <= i-1 & i-1 <= len D2 by A25,XREAL_1:21,XXREAL_0:2;
                  then i-1 in Seg(len D2) by A79,FINSEQ_1:3;
                  then
A81:              i-1 in dom D2 by FINSEQ_1:def 3;
A82:              A <> {} & A is bounded_below by Th3;
A83:              inf divset(D2,i)=D2.(i-1) by A71,A78,Def5;
A84:              sup divset(D2,i)=D2.i by A71,A78,Def5;
A85:              inf divset(D1,1)=inf A by A4,Def5;
A86:              sup divset(D1,1)=D1.1 by A4,Def5;
A87:              rng D2 c= A by Def2;
                  D2.(i-1) in rng D2 by A81,FUNCT_1:def 5;
                  then
A88:              inf divset(D2,i) >= inf divset(D1,1)
                  by A82,A83,A85,A87,SEQ_4:def 5;
                  D2.(i-1)<= D2.indx(D2,D1,1) by A24,A80,A81,GOBOARD2:18;
                  then inf divset(D2,i) <= sup divset(D1,1) by A1,A4,A83,A86,
                  Def21;
                  then consider a being Real such that
A89:              a = inf divset(D2,i) & inf divset(D1,1) <= a &
                  a <= sup divset(D1,1) by A88;
                  inf divset(D2,i)in{r: inf divset(D1,1)<=r & r<=
                  sup divset(D1,1)} by A89;
                  hence inf divset(D2,i)in[.inf divset(D1,1),sup divset(D1,1).]
                  by RCOMP_1:def 1;
                  D2.i in rng D2 by A71,FUNCT_1:def 5;
                  then
A90:              sup divset(D2,i) >= inf divset(D1,1)
                  by A82,A84,A85,A87,SEQ_4:def 5;
                  D2.i <= D2.indx(D2,D1,1) by A16,A24,A52,A71,GOBOARD2:18;

then sup divset(D2,i) <= sup divset(D1,1) by A1,A4,A84,A86,Def21;
                  then consider a being Real such that
A91:              a = sup divset(D2,i) & inf divset(D1,1) <= a &
                  a <= sup divset(D1,1) by A90;
                  sup divset(D2,i)in{r: inf divset(D1,1)<=r & r<=
                  sup divset(D1,1)} by A91;
                  hence sup divset(D2,i)in[.inf divset(D1,1),sup divset(D1,1).]
                  by RCOMP_1:def 1;
                end;
              end;
              hence thesis;
            end;
            hence thesis by A69,A70,RCOMP_1:16;
          end;
A92:      i in dom D2 & i in dom (lower_volume(f,D2)|Seg indx(D2,D1,1))
          proof
A93:        Seg indx(D2,D1,1) c= Seg(len D2) by A14,FINSEQ_1:7;
            then i in Seg(len D2) by A16,A52;
            hence i in dom D2 by FINSEQ_1:def 3;
            dom (lower_volume(f,D2)|Seg indx(D2,D1,1))
            =dom lower_volume(f,D2) /\ Seg indx(D2,D1,1) by FUNCT_1:68
              .=Seg(len lower_volume(f,D2)) /\
            Seg indx(D2,D1,1) by FINSEQ_1:def 3
              .=Seg(len D2) /\ Seg indx(D2,D1,1) by Def8
              .=Seg indx(D2,D1,1) by A93,XBOOLE_1:28;
            hence thesis by A16,A52;
          end;
          (lower_volume(f,D2)|indx(D2,D1,1)).i
          =(lower_volume(f,D2)|Seg indx(D2,D1,1)).i by FINSEQ_1:def 15
            .=lower_volume(f,D2).i by A92,FUNCT_1:68
            .=(inf (rng (f|divset(D2,i))))*vol(divset(D2,i)) by A92,Def8;
          hence thesis by A67,A68,FUNCT_1:82;
        end;
        hence thesis by A50,FINSEQ_1:18;
      end;
      hence thesis by A35,A36;
    end;
A94: for k being non empty Nat st P[k] holds P[k+1]
    proof
      let k be non empty Nat;
      assume
A95:  k in dom D1 implies
      Sum(lower_volume(f,D1)|k) <= Sum(lower_volume(f,D2)|indx(D2,D1,k));
      assume
A96:  k+1 in dom D1;
      then
      k+1 in Seg(len D1) by FINSEQ_1:def 3;
      then
A98:  1 <= k+1 & k+1 <= len D1 by FINSEQ_1:3;
      now per cases;
        suppose 1=k+1;
          hence Sum(lower_volume(f,D1)|(k+1))
          <=Sum(lower_volume(f,D2)|indx(D2,D1,k+1)) by A3,A96;
        end;
        suppose
A99:      1<>k+1;
          set n=k+1;
          n is non trivial by A99,NAT_2:def 1;
          then n >= 2 by NAT_2:31;
          then
A100:     k >= (1+1)-1 by XREAL_1:22;
A101:     k <= k+1 by NAT_1:11;
          then
A102:     1 <= k & k <= len D1 by A98,A100,XXREAL_0:2;
          then
A103:     k in Seg(len D1) by FINSEQ_1:3;
          then
A104:     k in dom D1 by FINSEQ_1:def 3;
A105:     1 <= k+1 & k+1 <= len lower_volume(f,D1) by A98,Def8;
A106:     len lower_volume(f,D2) = len D2 by Def8;
A107:     indx(D2,D1,k) in dom D2 by A1,A104,Def21;
A108:     indx(D2,D1,k+1) in dom D2 by A1,A96,Def21;
          then
A109:     indx(D2,D1,k+1) in Seg(len D2) by FINSEQ_1:def 3;
          then
A110:     1 <= indx(D2,D1,k+1) & indx(D2,D1,k+1) <= len D2 by FINSEQ_1:3;
A111:     1 <= indx(D2,D1,k+1) & indx(D2,D1,k+1) <= len lower_volume(f,D2)
          by A106,A109,FINSEQ_1:3;
A112:     indx(D2,D1,k) < indx(D2,D1,k+1)
          proof
            assume indx(D2,D1,k) >= indx(D2,D1,k+1);
            then
A113:       D2.indx(D2,D1,k) >= D2.indx(D2,D1,k+1) by A107, A108, GOBOARD2:18;
            k < k+1 by NAT_1:13;
            then
A114:       D1.k < D1.(k+1) by A96,A104,GOBOARD1:def 1;
            D1.k=D2.indx(D2,D1,k) by A1,A104,Def21;
            hence contradiction by A1,A96,A113,A114,Def21;
          end;
A115:     indx(D2,D1,k+1) >= indx(D2,D1,k)
          proof
            assume indx(D2,D1,k+1) < indx(D2,D1,k);
            then
A116:       D2.indx(D2,D1,k+1) < D2.indx(D2,D1,k) by A107,A108,
            GOBOARD1:def 1;
            D1.(k+1) = D2.indx(D2,D1,k+1) by A1,A96,Def21;
            then D1.(k+1) < D1.k by A1,A104,A116,Def21;
            hence contradiction by A96,A104,GOBOARD2:18,NAT_1:11;
          end;
          then consider ID being Nat such that
A117:     indx(D2,D1,k+1) = indx(D2,D1,k) + ID by NAT_1:10;
          reconsider ID as Element of NAT by ORDINAL1:def 13;
A118:     1 <= k & k <= len lower_volume(f,D1) by A102,Def8;
          indx(D2,D1,k) in dom D2 by A1,A104,Def21;
          then indx(D2,D1,k) in Seg(len D2) by FINSEQ_1:def 3;
          then
A119:     1<=indx(D2,D1,k) & indx(D2,D1,k)<=len lower_volume(f, D2)
          by A106,FINSEQ_1:3;
          set K1D1=lower_volume(f,D1)|(k+1);
          set KD1 =lower_volume(f,D1)|k;
          set K1D2=lower_volume(f,D2)|indx(D2,D1,k+1);
          set KD2 =lower_volume(f,D2)|indx(D2,D1,k);
          set IDK1=indx(D2,D1,k+1);
          set IDK =indx(D2,D1,k);
A120:     len K1D1=k+1 by A105,FINSEQ_1:80;
          then consider p1,q1 being FinSequence of REAL such that
A121:     len p1=k & len q1=1 & K1D1=p1^q1 by FINSEQ_2:26;
          len K1D2=IDK + (IDK1-IDK) by A111,FINSEQ_1:80;
          then consider p2,q2 being FinSequence of REAL such that
A122:     len p2=IDK & len q2=IDK1-IDK & K1D2=p2^q2 by A117,FINSEQ_2:26;
A123:     p1=KD1
          proof
A124:       len p1 = len KD1 by A118,A121,FINSEQ_1:80;
            for i be Nat st 1 <= i & i <= len p1 holds p1.i=KD1.i
            proof
              let i be Nat;
              assume 1 <= i & i <= len p1;
              then
A125:         i in Seg(len p1) by FINSEQ_1:3;
              then
A126:         i in dom p1 by FINSEQ_1:def 3;
A127:         i in dom KD1 by A124,A125,FINSEQ_1:def 3;
              then
A128:         i in dom (lower_volume(f,D1)|Seg k) by FINSEQ_1:def 15;
A129:         KD1.i = (lower_volume(f,D1)|Seg k).i by FINSEQ_1:def 15
                .= lower_volume(f,D1).i by A128,FUNCT_1:68;
A130:         dom KD1=Seg(len KD1) by FINSEQ_1:def 3
                .= Seg k
              by A118,FINSEQ_1:80;
              k <= k+1 by NAT_1:11;
              then
A131:         Seg k c= Seg(k+1) by FINSEQ_1:7;
              dom K1D1 =Seg(len K1D1) by FINSEQ_1:def 3
                .= Seg(k+1)
              by A105,FINSEQ_1:80;
              then i in dom K1D1 by A127,A130,A131;
              then
A132:         i in dom (lower_volume(f,D1)|Seg(k+1)) by FINSEQ_1:def 15;
              K1D1.i = (lower_volume(f,D1)|Seg (k+1)).i by FINSEQ_1:def 15
                .= lower_volume(f,D1).i by A132,FUNCT_1:68;
              hence thesis by A121,A126,A129,FINSEQ_1:def 7;
            end;
            hence thesis by A124,FINSEQ_1:18;
          end;
A133:     p2=KD2
          proof
A134:       len p2 = len KD2 by A119,A122,FINSEQ_1:80;
            for i be Nat st 1 <= i & i <= len p2 holds p2.i=KD2.i
            proof
              let i be Nat;
              assume 1 <= i & i <= len p2;
              then
A135:         i in Seg(len p2) by FINSEQ_1:3;
              then
A136:         i in dom p2 by FINSEQ_1:def 3;
A137:         i in dom KD2 by A134,A135,FINSEQ_1:def 3;
              then
A138:         i in
              dom (lower_volume(f,D2)|Seg indx(D2,D1,k)) by FINSEQ_1:def 15;
A139:         KD2.i=(lower_volume(f,D2)|Seg indx(D2,D1,k)).i by FINSEQ_1:def 15
                .= lower_volume(f,D2).i by A138,FUNCT_1:68;
A140:         dom KD2 =Seg(len KD2) by FINSEQ_1:def 3
                .= Seg indx(D2,D1,k) by A119,FINSEQ_1:80;
A141:         Seg indx(D2,D1,k) c= Seg indx(D2,D1,k+1) by A115,FINSEQ_1:7;
              dom K1D2 = Seg(len K1D2) by FINSEQ_1:def 3
                .= Seg indx(D2,D1,k+1) by A111,FINSEQ_1:80;
              then i in dom K1D2 by A137,A140,A141;
              then
A142:         i in dom (lower_volume(f,D2)|Seg indx(D2,D1,k+ 1))
              by FINSEQ_1:def 15;
              K1D2.i=(lower_volume(f,D2)|Seg indx(D2,D1,k+1)).i
              by FINSEQ_1:def 15
                .=lower_volume(f,D2).i by A142,FUNCT_1:68;
              hence thesis by A122,A136,A139,FINSEQ_1:def 7;
            end;
            hence thesis by A134,FINSEQ_1:18;
          end;
A143:     Sum q1 <= Sum q2
          proof
            set DD1 = divset(D1,k+1);
            set MD1 = mid(D1,k+1,k+1);
            set MD2 = mid(D2,indx(D2,D1,k)+1,indx(D2,D1,k+1));
            set g = f|DD1;
            consider S1 being non empty Division of DD1;
A144:       k+1 in dom D1 & D1.(k+1) >= inf DD1 & D1.(k+1) = sup DD1
            proof
              (k+1)-1=k;
              then inf DD1=D1.k by A96,A99,Def5;
              hence thesis by A96,A99,A101,A104,Def5,GOBOARD2:18;
            end;
            then reconsider MD1 as Element of S1 by Th39;
A145:       indx(D2,D1,k)+1 <= indx(D2,D1,k+1) by A112,NAT_1:13;
            then
A146:       indx(D2,D1,k)+1 <= len D2 by A110,XXREAL_0:2;
A147:       1 <= indx(D2,D1,k)+1 by NAT_1:11;
            then indx(D2,D1,k)+1 in Seg(len D2) by A146,FINSEQ_1:3;
            then
A148:       indx(D2,D1,k)+1 in dom D2 by FINSEQ_1:def 3;
A149:       D2.indx(D2,D1,k+1)=D1.(k+1) by A1,A96,Def21;
A150:       inf divset(D1,k+1)=D1.((k+1)-1) & sup divset(D1,k+1)=D1.(k+1)
            by A96,A99,Def5;
            (k+1)-1=k;
            then
A151:       inf DD1=D1.k by A96,A99,Def5;
            D2.(indx(D2,D1,k)+1)>=D2.indx(D2,D1,k)by A107,A148,GOBOARD2:18,
            NAT_1:11;
            then D2.(indx(D2,D1,k)+1) >= inf DD1 by A1,A104,A151,Def21;
            then reconsider MD2 as Element of S1 by A108,A145,A148,A149,A150,
            Th39;
            reconsider g as PartFunc of DD1,REAL by PARTFUN1:43;
A152:       g is total & g is_bounded_below_on DD1
            proof
              dom g = dom f /\ divset(D1,k+1) by FUNCT_1:68;
              then dom g = A /\ divset(D1,k+1) by FUNCT_2:def 1;
              then dom g = divset(D1,k+1) by A144,Th10,XBOOLE_1:28;
              hence g is total by PARTFUN1:def 4;
              g is_bounded_below_on divset(D1,k+1)
              proof
                consider a be real number such that
A153:           for x being set st x in A /\ dom f
                holds a <= f.x by A2,RFUNCT_1:def 10;
                for x being set st x in divset(D1,k+1) /\ dom g holds a <= g.x
                proof
                  let x be set;
                  assume x in divset(D1,k+1) /\ dom g;
                  then
A154:             x in dom g by XBOOLE_0:def 3;
A155:             A /\ dom f = dom f by XBOOLE_1:28;
A156:             dom g c= dom f by RELAT_1:89;

then reconsider x as Element of A by A154,A155,XBOOLE_0:def 3;
                  a <= f.x by A153,A154,A155,A156;
                  hence thesis by A154,FUNCT_1:68;
                end;
                hence thesis by RFUNCT_1:def 10;
              end;
              hence thesis;
            end;
            then
A157:       g is Function of divset(D1,k+1),REAL by FUNCT_2: 131;
A158:       q1=lower_volume(g,MD1)
            proof
              len MD1 = (k+1) -'(k+1) + 1 by A98,JORDAN3:27;
              then
A159:         len MD1 = (k+1) - (k+1) + 1 by BINARITH:50;
              then
A160:         len q1 = len(lower_volume(g,MD1)) by A121,Def8;

for n be Nat st 1 <= n & n <= len q1 holds q1.n=lower_volume(g,MD1).n
              proof
                let n be Nat;
                assume
A161:           1 <= n & n <= len q1;
                then
A162:           n = 1 by A121,XXREAL_0:1;
U:              n in Seg(len q1) by A161,FINSEQ_1:3;
                then
A164:           n in dom q1 by FINSEQ_1:def 3;
Y:              n in dom MD1 by A121,A159,FINSEQ_1:def 3,U;
                k+1 in Seg(len K1D1) by A120,FINSEQ_1:6;
                then k+1 in dom K1D1 by FINSEQ_1:def 3;
                then
A165:           k+1 in dom (lower_volume(f,D1)|Seg(k+1)) by FINSEQ_1:def 15;
                K1D1.(k+1)=(lower_volume(f,D1)|Seg(k+1)).(k+1)
                by FINSEQ_1:def 15
                  .=lower_volume(f,D1).(k+1) by A165,FUNCT_1:68;
                then
A166:           q1.n = lower_volume(f,D1).(k+1) by A121, A162,A164,
                FINSEQ_1:def 7

.=(inf(rng(f|divset(D1,k+1))))*vol(divset(D1,k+1)) by Def8,A96;
                1 in dom MD1
                proof
                  1 in Seg 1 by FINSEQ_1:5;
                  hence thesis by A159,FINSEQ_1:def 3;
                end;
                then
A167:           inf divset(MD1,1) = inf DD1 & sup divset( MD1,1) = MD1.1
                by Def5;
                MD1.1 =D1.(k+1) by A98,JORDAN3:27;
                then
A168:           divset(MD1,1)=[.inf DD1,D1.(k+1).] by A167,Th5;
                (k+1)-1=k;
                then
A169:           inf DD1 = D1.k by A96,A99,Def5;
                sup DD1 = D1.(k+1) by A96,A99,Def5;
                then divset(D1,k+1)=[. D1.k,D1.(k+1).] by A169,Th5;
                then lower_volume(g,MD1).n
                =(inf(rng(g|divset(D1,k+1))))*vol(divset(D1,k+1))
                by A162,A168,A169,Def8,Y;
                hence thesis by A166,FUNCT_1:82;
              end;
              hence thesis by A160,FINSEQ_1:18;
            end;
A170:       q2=lower_volume(g,MD2)
            proof
A171:         indx(D2,D1,k+1)-'(indx(D2,D1,k)+1)+1
              =indx(D2,D1,k+1)-(indx(D2,D1,k)+1)+1 by A145,BINARITH:50
                .=indx(D2,D1,k+1)-indx(D2,D1,k);
A172:         len(lower_volume(g,MD2))=
              len mid(D2,indx(D2,D1,k)+1,indx(D2,D1,k+1)) by Def8
                .=indx(D2,D1,k+1)-indx(D2,D1,k) by A110,A145,A146,A147,A171,
              JORDAN3:27;

for n be Nat st 1 <= n & n <= len q2 holds q2.n=lower_volume(g,MD2).n
              proof
                let n be Nat;
                assume
A173:           1 <= n & n <= len q2;
                then
A174:           n in Seg(len q2) by FINSEQ_1:3;
                then
A175:           n in dom q2 by FINSEQ_1:def 3;
A176:           dom K1D2 = Seg(len K1D2) by FINSEQ_1:def 3
                  .= Seg indx(D2,D1,k+1) by A111,FINSEQ_1:80;
A177:           indx(D2,D1,k)+n in dom K1D2 by A122,A175,FINSEQ_1:41;
                then
A178:           indx(D2,D1,k)+n in dom (lower_volume(f,D2) |Seg indx(D2,D1,k
                +1)) by FINSEQ_1:def 15;
A179:           Seg indx(D2,D1,k+1) c= Seg(len D2) by A110,FINSEQ_1:7;
                then indx(D2,D1,k)+n in Seg(len D2) by A176,A177;
                then
A180:           indx(D2,D1,k)+n in dom D2 by FINSEQ_1:def 3;
A181:           1 <=indx(D2,D1,k)+n & indx(D2,D1,k)+n <= indx(D2,D1,k+1)
                by A176,A177,FINSEQ_1:3;
                then
A182:           n <= indx(D2,D1,k+1)-indx(D2,D1,k) by XREAL_1:21;
A183:           len mid(D2,indx(D2,D1,k)+1,indx(D2,D1,k+1))
                =ID by A110,A117,A145,A146,A147,A171,JORDAN3:27;
                then
                n in Seg(len mid(D2,indx(D2,D1,k)+1,indx( D2,D1,k+1)))
                by A117,A173,A182,FINSEQ_1:3;
                then
A185:           n in dom MD2 by FINSEQ_1:def 3;
A186:           q2.n=K1D2.(indx(D2,D1,k)+n) by A122,A175,FINSEQ_1:def 7

.=(lower_volume(f,D2)|Seg indx(D2,D1,k+1)).(indx(D2,D1,k)+n) by FINSEQ_1:def 15
                  .=lower_volume(f,D2).(indx(D2,D1,k)+n) by A178,FUNCT_1:68

.=(inf(rng(f|divset(D2,indx(D2,D1,k)+n))))*vol(divset(D2,indx(D2,D1,k)+n))
                by Def8,A180;
A187:           divset(MD2,n)=divset(D2,indx(D2,D1,k)+n) &
                divset(D2,indx(D2,D1,k)+n) c= divset(D1,k+1)
                proof
                  now per cases;
                    suppose
A188:                 n=1;
A189:                 (k+1)-1=k;
A190:                 1<=indx(D2,D1,k)+1&indx(D2,D1,k)+1<=len D2
                      by A176,A177,A179,A188,FINSEQ_1:3;
A191:                 indx(D2,D1,k)+1 <> 1 by A119,NAT_1:13;
A192:                 inf divset(MD2,1)=inf divset(D1,k+1) by A185,A188,Def5
                        .= D1.k by A96,A99,A189,Def5;
A193:                 sup divset(MD2,1)=

mid(D2,indx(D2,D1,k)+1,indx(D2,D1,k+1)).1 by A185,A188,Def5
                        .= D2.(1+indx(D2,D1,k)) by A110,A190,JORDAN3:27;
                      then
A194:                 divset(MD2,n)=[.D1.k,D2.(indx( D2,D1,k)+1).] by A188
                      ,A192,Th5;
A195:                 divset(D2,indx(D2,D1,k)+n)=

[.inf divset(D2,indx(D2,D1,k)+1),sup divset(D2,indx(D2,D1,k)+1).] by A188,Th5

.=[.D2.(indx(D2,D1,k)+1-1),sup divset(D2,indx(D2,D1,k)+1).] by A148,A191,Def5

.=[.D2.indx(D2,D1,k),D2.(indx(D2,D1,k)+1).] by A148,A191,Def5
                        .=[.D1.k,D2.(indx(D2,D1,k)+1).] by A1,A104,Def21;

hence divset(MD2,n)=divset(D2,indx(D2,D1,k)+n) by A188,A192,A193,Th5;
                      thus thesis by A185,A194,A195,Th10;
                    end;
                    suppose
A196:                 n<>1;
A197:                 indx(D2,D1,k)+1 <= indx(D2,D1,k+1) by A112,NAT_1:13;
                      consider n1 being Nat such that
A198:                 n=1+n1 by A173,NAT_1:10;
                      reconsider n1 as Element of NAT by ORDINAL1:def 13;
A199:                 n1+(indx(D2,D1,k)+1)-'1=indx(D2,D1,k)+n-1
                      by A181,A198,BINARITH:50;
A200:                 1<=n-1 & n-1<=len MD2
                      proof
                        n is non trivial by A173,A196,NAT_2:def 1;
                        then n >= 1+1 by NAT_2:31;
                        hence n-1>=1 by XREAL_1:21;
                        n<=n+1 by NAT_1:12;
                        then n-1 <= n by XREAL_1:22;
                        hence thesis by A117,A182,A183,XXREAL_0:2;
                      end;
A201:                 n+(indx(D2,D1,k)+1)-'1=indx(D2,D1,k)+n
                      proof

n+(indx(D2,D1,k)+1)-'1=n+indx(D2,D1,k)+1-1 by BINARITH:50 ,NAT_1:11
                          .=indx(D2,D1,k)+n;
                        hence thesis;
                      end;
A202:                 indx(D2,D1,k)+n <> 1
                      proof
                        assume indx(D2,D1,k)+n = 1;
                        then indx(D2,D1,k)=1-n;
                        then n+1 <= 1 by A119,XREAL_1:21;
                        then n <= 1-1 by XREAL_1:21;
                        hence contradiction by A173,NAT_1:3;
                      end;
A203:                 inf divset(MD2,n)=MD2.(n-1) by A185,A196,Def5

.=D2.(indx(D2,D1,k)+n-1) by A110,A146,A147,A197,A198,A199,A200, JORDAN3:27;
A204:                 sup divset(MD2,n)=MD2.n by A185,A196,Def5

.=D2.(indx(D2,D1,k)+n) by A110,A117,A146,A147,A173,A174,A182,A183,
                      A197,A201,JORDAN3:27;
                      then
A205:                 divset(MD2,n)=

[. D2.(indx(D2,D1,k)+n-1),D2.(indx(D2,D1,k)+n) .] by A203,Th5;
A206:                 divset(D2,indx(D2,D1,k)+n)

=[.inf divset(D2,indx(D2,D1,k)+n),sup divset(D2,indx(D2,D1,k)+n).] by Th5

.=[.D2.(indx(D2,D1,k)+n-1),sup divset(D2,indx(D2,D1,k)+n).] by A180,A202,Def5

.=[. D2.(indx(D2,D1,k)+n-1),D2.(indx(D2,D1,k)+n) .] by A180,A202,Def5;

hence divset(MD2,n)=divset(D2,indx(D2,D1,k)+n) by A203,A204, Th5;
                      thus thesis by A185,A205,A206,Th10;
                    end;
                  end;
                  hence thesis;
                end;
                then g|divset(MD2,n)
                =f|divset(D2,indx(D2,D1,k)+n) by FUNCT_1:82;
                hence q2.n=lower_volume(g,MD2).n by A186,A187,Def8,A185;
              end;
              hence thesis by A122,A172,FINSEQ_1:18;
            end;
            len MD1 = 1 & MD1 <= MD2
            proof
              len MD1 = (k+1) -'(k+1) + 1 by A98,JORDAN3:27;
              then len MD1 = (k+1) - (k+1) + 1 by BINARITH:50;
              hence thesis by Th31;
            end;
            then lower_sum(g,MD1) <= lower_sum(g,MD2) by A152,A157,Th33;
            hence thesis by A158,A170;
          end;
A207:     Sum K1D1=Sum p1+Sum q1 by A121,RVSUM_1:105;
A208:     Sum K1D2=Sum p2+Sum q2 by A122,RVSUM_1:105;
          Sum q1 = Sum K1D1 - Sum p1 by A207;
          then Sum K1D1 <= Sum q2 + Sum p1 by A143,XREAL_1:22;
          then Sum K1D1 - Sum q2 <= Sum p1 by XREAL_1:22;
          then Sum K1D1 - Sum q2 <= Sum
          p2 by A95,A103,A123,A133,FINSEQ_1:def 3,XXREAL_0:2;
          hence thesis by A208,XREAL_1:22;
        end;
      end;
      hence thesis;
    end;
    thus for n being non empty Nat holds P[n] from NAT_1:sch 10(A3,A94);
  end;
  hence thesis;
end;

theorem Th42:
  for A be closed-interval Subset of REAL, f be Function of A,REAL,
  S be non empty Division of A, D1,D2 be Element of S, i be Element of NAT
  holds D1 <= D2 & i in dom D1 & f is_bounded_above_on A implies
  (PartSums(upper_volume(f,D1))).i >=
  (PartSums(upper_volume(f,D2))).indx(D2,D1,i)
proof
  let A be closed-interval Subset of REAL;
  let f be Function of A,REAL;
  let S be non empty Division of A;
  let D1,D2 be Element of S;
  let i be Element of NAT;
  assume that
A1: D1 <= D2 and
A2: i in dom D1 and
A3: f is_bounded_above_on A;
A4: i is non empty Element of NAT
  proof
    i in Seg(len D1) by A2,FINSEQ_1:def 3;
    hence thesis by FINSEQ_1:3;
  end;
    indx(D2,D1,i) in Seg(len upper_volume(f,D2))
  proof
A6: len upper_volume(f,D2)=len D2 by Def7;
    indx(D2,D1,i) in dom D2 by A1,A2,Def21;
    hence thesis by A6,FINSEQ_1:def 3;
  end;
  then
B5: indx(D2,D1,i) in dom upper_volume(f,D2) by FINSEQ_1:def 3;
  i in Seg(len D1) by A2,FINSEQ_1:def 3;
  then i in Seg(len upper_volume(f,D1)) by Def7;
  then
     i in dom upper_volume(f,D1) by FINSEQ_1:def 3;
  then (PartSums(upper_volume(f,D1))).i=Sum(upper_volume(f,D1)|i) by Def22;
  then (PartSums(upper_volume(f,D1))).i >=
  Sum(upper_volume(f,D2)|indx(D2,D1,i)) by A1,A2,A3,A4,Th40;
  hence thesis by Def22,B5;
end;

theorem Th43:
  for A be closed-interval Subset of REAL, f be Function of A,REAL,
  S be non empty Division of A, D1,D2 be Element of S, i be Element of NAT
  holds D1 <= D2 & i in dom D1 & f is_bounded_below_on A implies
  (PartSums(lower_volume(f,D1))).i <=
  (PartSums(lower_volume(f,D2))).indx(D2,D1,i)
proof
  let A be closed-interval Subset of REAL;
  let f be Function of A,REAL;
  let S be non empty Division of A;
  let D1,D2 be Element of S;
  let i be Element of NAT;
  assume that
A1: D1 <= D2 and
A2: i in dom D1 and
A3: f is_bounded_below_on A;
A4: i is non empty Element of NAT
  proof
    i in Seg(len D1) by A2,FINSEQ_1:def 3;
    hence thesis by FINSEQ_1:3;
  end;
    indx(D2,D1,i) in Seg(len lower_volume(f,D2))
  proof
A6: len lower_volume(f,D2)=len D2 by Def8;
    indx(D2,D1,i) in dom D2 by A1,A2,Def21;
    hence thesis by A6,FINSEQ_1:def 3;
  end;
  then
B5: indx(D2,D1,i) in dom lower_volume(f,D2) by FINSEQ_1:def 3;
  i in Seg(len D1) by A2,FINSEQ_1:def 3;
  then i in Seg(len lower_volume(f,D1)) by Def8;
  then
     i in dom lower_volume(f,D1) by FINSEQ_1:def 3;
  then (PartSums(lower_volume(f,D1))).i=Sum(lower_volume(f,D1)|i) by Def22;
  then (PartSums(lower_volume(f,D1))).i <=
  Sum(lower_volume(f,D2)|indx(D2,D1,i)) by A1,A2,A3,A4,Th41;
  hence thesis by Def22,B5;
end;

theorem Th44:
  for A be closed-interval Subset of REAL,
  f be PartFunc of A,REAL, S be non empty Division of A, D be Element of S
  holds (PartSums(upper_volume(f,D))).(len D) = upper_sum(f,D)
proof
  let A be closed-interval Subset of REAL;
  let f be PartFunc of A,REAL;
  let S be non empty Division of A;
  let D be Element of S;
A1: len D <> 0 by CARD_2:59;
  len upper_volume(f,D) = len D by Def7;
  then len D in Seg(len upper_volume(f,D)) by A1,FINSEQ_1:5;
  then len D in dom upper_volume(f,D) by FINSEQ_1:def 3;
  then
A2: (PartSums(upper_volume(f,D))).(len D) = Sum(upper_volume(f,D)|(len D ))
  by Def22;
  upper_volume(f,D)|(Seg len D) = upper_volume(f,D)
  proof
    dom upper_volume(f,D)=Seg len upper_volume(f,D) by FINSEQ_1:def 3;
    then dom upper_volume(f,D)=Seg len D by Def7;
    hence thesis by RELAT_1:97;
  end;
  hence thesis by A2,FINSEQ_1:def 15;
end;

theorem Th45:
  for A be closed-interval Subset of REAL,
  f be PartFunc of A,REAL, S be non empty Division of A, D be Element of S
  holds (PartSums(lower_volume(f,D))).(len D) = lower_sum(f,D)
proof
  let A be closed-interval Subset of REAL;
  let f be PartFunc of A,REAL;
  let S be non empty Division of A;
  let D be Element of S;
A1: len D <> 0 by CARD_2:59;
  len lower_volume(f,D) = len D by Def8;
  then len D in Seg(len lower_volume(f,D)) by A1,FINSEQ_1:5;
  then len D in dom lower_volume(f,D) by FINSEQ_1:def 3;
  then
A2: (PartSums(lower_volume(f,D))).(len D) = Sum(lower_volume(f,D)|(len D ))
  by Def22;
  lower_volume(f,D)|(Seg len D) = lower_volume(f,D)
  proof
    dom lower_volume(f,D)=Seg len lower_volume(f,D) by FINSEQ_1:def 3;
    then dom lower_volume(f,D)=Seg len D by Def8;
    hence thesis by RELAT_1:97;
  end;
  hence thesis by A2,FINSEQ_1:def 15;
end;

theorem Th46:
  for A be closed-interval Subset of REAL,
  S be non empty Division of A, D1,D2 be Element of S
  holds D1 <= D2 implies indx(D2,D1,len D1) = len D2
proof
  let A be closed-interval Subset of REAL;
  let S be non empty Division of A;
  let D1,D2 be Element of S;
  assume
A1: D1 <= D2;
  assume
A2: indx(D2,D1,len D1) <> len D2;
A3: len D1 in dom D1
  proof
    len D1 <> 0 by CARD_2:59;
    then len D1 in Seg(len D1) by FINSEQ_1:5;
    hence thesis by FINSEQ_1:def 3;
  end;
  then D1.(len D1)=D2.indx(D2,D1,len D1) by A1,Def21;
  then
A4: D2.indx(D2,D1,len D1)=upper_bound A by Def2;
A5: len D2 in dom D2
  proof
    len D2 <> 0 by CARD_2:59;
    then len D2 in Seg(len D2) by FINSEQ_1:5;
    hence thesis by FINSEQ_1:def 3;
  end;
A6: indx(D2,D1,len D1) in dom D2 by A1,A3,Def21;
  indx(D2,D1,len D1) < len D2
  proof
    indx(D2,D1,len D1) in Seg(len D2) by A6,FINSEQ_1:def 3;
    then indx(D2,D1,len D1) <= len D2 by FINSEQ_1:3;
    hence thesis by A2,REAL_1:def 5;
  end;
  then D2.indx(D2,D1,len D1) < D2.(len D2) by A5,A6,GOBOARD1:def 1;
  hence contradiction by A4,Def2;
end;

theorem Th47:
  for A be closed-interval Subset of REAL, f be Function of A,REAL,
  S be non empty Division of A, D1,D2 be Element of S
  holds D1 <= D2 & f is_bounded_above_on A implies
  upper_sum(f,D2) <= upper_sum(f,D1)
proof
  let A be closed-interval Subset of REAL;
  let f be Function of A,REAL;
  let S be non empty Division of A;
  let D1,D2 be Element of S;
  assume that
A1: D1 <= D2 and
A2: f is_bounded_above_on A;
  len D1 in dom D1
  proof
    len D1 <> 0 by CARD_2:59;
    then len D1 in Seg(len D1) by FINSEQ_1:5;
    hence thesis by FINSEQ_1:def 3;
  end;
  then (PartSums(upper_volume(f,D1))).(len D1) >=
  (PartSums(upper_volume(f,D2))).indx(D2,D1,len D1) by A1,A2,Th42;
  then upper_sum(f,D1) >= (PartSums(upper_volume(f,D2))).indx(D2,D1,len D1)
  by Th44;
  then upper_sum(f,D1) >= (PartSums(upper_volume(f,D2))).(len D2) by A1,Th46;
  hence thesis by Th44;
end;

theorem Th48:
  for A be closed-interval Subset of REAL, f be Function of A,REAL,
  S be non empty Division of A, D1,D2 be Element of S
  holds D1 <= D2 & f is_bounded_below_on A implies
  lower_sum(f,D2) >= lower_sum(f,D1)
proof
  let A be closed-interval Subset of REAL;
  let f be Function of A,REAL;
  let S be non empty Division of A;
  let D1,D2 be Element of S;
  assume that
A1: D1 <= D2 and
A2: f is_bounded_below_on A;
  len D1 in dom D1
  proof
    len D1 <> 0 by CARD_2:59;
    then len D1 in Seg(len D1) by FINSEQ_1:5;
    hence thesis by FINSEQ_1:def 3;
  end;
  then (PartSums(lower_volume(f,D1))).(len D1) <=
  (PartSums(lower_volume(f,D2))).indx(D2,D1,len D1) by A1,A2,Th43;
  then lower_sum(f,D1) <= (PartSums(lower_volume(f,D2))).indx(D2,D1,len D1)
  by Th45;
  then lower_sum(f,D1) <= (PartSums(lower_volume(f,D2))).(len D2) by A1,Th46;
  hence thesis by Th45;
end;

theorem Th49:
  for A be closed-interval Subset of REAL,
  S be non empty Division of A, D1,D2 be Element of S
  holds ex D be Element of S st D1 <= D & D2 <= D
proof
  let A be closed-interval Subset of REAL;
  let S be non empty Division of A;
  let D1,D2 be Element of S;
  consider D3 being FinSequence of REAL such that
A1: rng D3 = rng(D1^D2) & len D3 = card rng(D1^D2) &
  D3 is increasing by GOBOARD2:21;
  reconsider D3 as non empty increasing FinSequence of REAL by A1,RELAT_1:60;
A2: rng D1 c= A by Def2;
  rng D2 c= A by Def2;
  then rng D1 \/ rng D2 c= A by A2,XBOOLE_1:8;
  then
A3: rng D3 c= A by A1,FINSEQ_1:44;
  D3.(len D3) = sup A
  proof
    assume
A4: D3.(len D3) <> sup A;
    now per cases by A4,REAL_1:def 5;
      suppose
A5:     D3.(len D3) > sup A;
        len D3 <> 0 by CARD_2:59;
        then len D3 in Seg(len D3) by FINSEQ_1:5;
        then len D3 in dom D3 by FINSEQ_1:def 3;
        then D3.(len D3) in rng D3 by FUNCT_1:def 5;
        then D3.(len D3) in A by A3;
        then D3.(len D3) in [.inf A,sup A.] by Th5;
        then D3.(len D3) in {r:inf A <= r & r <= sup A} by RCOMP_1:def 1;
        then consider a such that
A6:     a=D3.(len D3) & inf A <= a & a <= sup A;
        thus contradiction by A5,A6;
      end;
      suppose
A7:     D3.(len D3) < sup A;
        len D3 <> 0 by CARD_2:59;
        then len D3 in Seg(len D3) by FINSEQ_1:5;
        then
A8:     len D3 in dom D3 by FINSEQ_1:def 3;
        len D1 <> 0 by CARD_2:59;
        then len D1 in Seg(len D1) by FINSEQ_1:5;
        then
A9:     len D1 in dom D1 by FINSEQ_1:def 3;
A10:    rng D1 c= rng (D1^D2) by FINSEQ_1:42;
        D1.(len D1) = sup A by Def2;
        then sup A in rng D1 by A9,FUNCT_1:def 5;
        then consider k being Nat such that
A11:    k in dom D3 & D3.k = sup A by A1,A10,FINSEQ_2:11;
        k in Seg(len D3) by A11,FINSEQ_1:def 3;
        then k <= len D3 by FINSEQ_1:3;
        hence contradiction by A7,A8,A11,GOBOARD2:18;
      end;
    end;
    hence thesis;
  end;
  then D3 is DivisionPoint of A by A3,Def2;
  then reconsider D3 as Element of S by Def4;
  D1 is one-to-one by JORDAN7:17;
  then
A12: len D1 = card(rng D1) by FINSEQ_4:77;
  D2 is one-to-one by JORDAN7:17;
  then
A13: len D2 = card(rng D2) by FINSEQ_4:77;
A14: rng D1 c= rng (D1^D2) by FINSEQ_1:42;
A15: len D1 <= len D3 by A1,A12,FINSEQ_1:42,NAT_1:44;
A16: rng D2 c= rng (D1^D2) by FINSEQ_1:43;
A17: len D2 <= len D3 by A1,A13,FINSEQ_1:43,NAT_1:44;
  take D3;
  thus thesis by A1,A14,A15,A16,A17,Def20;
end;

theorem Th50:
  for A be closed-interval Subset of REAL, f be Function of A,REAL,
  S be non empty Division of A, D1,D2 be Element of S st f is_bounded_on A
  holds lower_sum(f,D1) <= upper_sum(f,D2)
proof
  let A be closed-interval Subset of REAL;
  let f be Function of A,REAL;
  let S be non empty Division of A;
  let D1,D2 be Element of S;
  assume that
A1: f is_bounded_on A;
  consider D be Element of S such that
A2: D1 <= D & D2 <= D by Th49;
A3: f is_bounded_below_on A & f is_bounded_above_on A by A1,RFUNCT_1:def 11;
  then
A4: lower_sum(f,D1) <= lower_sum(f,D) by A2,Th48;
A5: lower_sum(f,D) <= upper_sum(f,D) by A1,Th30;
  upper_sum(f,D) <= upper_sum(f,D2) by A2,A3,Th47;
  then lower_sum(f,D) <= upper_sum(f,D2) by A5,XXREAL_0:2;
  hence thesis by A4,XXREAL_0:2;
end;

begin :: Additivity of integral

theorem Th51:
  for A be closed-interval Subset of REAL, f be Function of A,REAL st
  f is_bounded_on A holds upper_integral(f) >= lower_integral(f)
proof
  let A be closed-interval Subset of REAL;
  let f be Function of A,REAL;
  assume
A1: f is_bounded_on A;
  dom upper_sum_set(f) is non empty by Def11;
  then
A2: rng upper_sum_set(f) is non empty by RELAT_1:65;
  dom lower_sum_set(f) is non empty by Def12;
  then
A3: rng lower_sum_set(f) is non empty by RELAT_1:65;
  for b be real number st
  b in rng upper_sum_set(f) holds lower_integral(f) <= b
  proof
    let b be real number;
    assume b in rng upper_sum_set(f);
    then consider D1 being Element of divs A such that
A4: D1 in dom upper_sum_set(f) & b=(upper_sum_set(f)).D1 by PARTFUN1:26;
A5: b=upper_sum(f,D1) by A4,Def11;
    for a being real number st
    a in rng lower_sum_set(f) holds a <= upper_sum(f,D1)
    proof
      let a be real number;
      assume a in rng lower_sum_set(f);
      then consider D2 being Element of divs A such that
A6:   D2 in dom lower_sum_set(f) & a=(lower_sum_set(f)).D2 by PARTFUN1:26;
      a=lower_sum(f,D2) by A6,Def12;
      hence thesis by A1,Th50;
    end;
    hence thesis by A3,A5,SEQ_4:62;
  end;
  hence thesis by A2,SEQ_4:60;
end;

theorem Th52:
  for X,Y be Subset of REAL holds (-X)+(-Y)=-(X+Y)
proof
  let X,Y be Subset of REAL;
A1: (-X)+(-Y)={ x + y : x in -X & y in -Y} by COMPLSP1:def 21;
A2: -(X+Y)={ -z : z in X+Y} by PSCOMP_1:def 7;
A3: -X={ -z : z in X} by PSCOMP_1:def 7;
A4: -Y={ -z : z in Y} by PSCOMP_1:def 7;
A5: X+Y={x + y : x in X & y in Y} by COMPLSP1:def 21;
  for z st z in (-X)+(-Y) holds z in -(X+Y)
  proof
    let z;
    assume z in -(X)+(-Y);
    then consider x,y such that
A6: z=x+y & x in -X & y in -Y by A1;
    consider a such that
A7: x=-a & a in X by A3,A6;
    consider b such that
A8: y=-b & b in Y by A4,A6;
A9: z=-(a+b) by A6,A7,A8;
    a+b in X+Y by A5,A7,A8;
    hence thesis by A2,A9;
  end;
  then
A10: (-X)+(-Y) c= -(X+Y) by SUBSET_1:7;
  for z st z in -(X+Y) holds z in (-X)+(-Y)
  proof
    let z;
    assume z in -(X+Y);
    then consider x such that
A11: z=-x & x in X+Y by A2;
    consider a,b such that
A12: x=a+b & a in X & b in Y by A5,A11;
A13: z=-a+-b by A11,A12;
    -a in -X & -b in -Y by A3,A4,A12;
    hence thesis by A1,A13;
  end;
  then -(X+Y) c= (-X)+(-Y) by SUBSET_1:7;
  hence thesis by A10,XBOOLE_0:def 10;
end;

theorem Th53:
  for X,Y being Subset of REAL holds X is bounded_above & Y is bounded_above
  implies X+Y is bounded_above
proof
  let X,Y be Subset of REAL;
  assume that
A1: X is bounded_above and
A2: Y is bounded_above;
A3: (-X) is bounded_below by A1,PSCOMP_1:15;
  (-Y) is bounded_below by A2,PSCOMP_1:15;
  then (-X)+(-Y) is bounded_below by A3,COMPLSP1:95;
  then -(X+Y) is bounded_below by Th52;
  hence thesis by PSCOMP_1:15;
end;

theorem Th54:
  for X,Y be non empty Subset of REAL
  st X is bounded_above & Y is bounded_above holds sup(X+Y) = sup X + sup Y
proof
  let X,Y be non empty Subset of REAL;
  assume that
A1: X is bounded_above and
A2: Y is bounded_above;
A3: (-X)+(-Y)=-(X+Y) by Th52;
  then
A4: -(X+Y) is non empty by COMPLSP1:94;
A5: (-X) is bounded_below by A1,PSCOMP_1:15;
A6: (-Y) is bounded_below by A2,PSCOMP_1:15;
  then
A7: -(X+Y) is bounded_below by A3,A5,COMPLSP1:95;
  inf(-X+-Y) = inf(-X)+inf(-Y) by A5,A6,COMPLSP1:96;
  then inf(-X+-Y) = -sup(--X)+inf(-Y) by A5,PSCOMP_1:17
    .= -sup X + -sup(--Y) by A6,PSCOMP_1:17
    .= -(sup X + sup Y);
  then -sup(--(X+Y))=-(sup X + sup Y) by A3,A4,A7,PSCOMP_1:17;
  hence thesis;
end;

theorem Th55:
  for A be closed-interval Subset of REAL, f,g be Function of A,REAL,
  S be non empty Division of A,
  D be Element of S st i in dom D & f is_bounded_above_on A &
  g is_bounded_above_on A
  holds upper_volume(f+g,D).i <= upper_volume(f,D).i + upper_volume(g,D).i
proof
  let A be closed-interval Subset of REAL;
  let f,g be Function of A,REAL;
  let S be non empty Division of A;
  let D be Element of S;
  assume
A1: i in dom D;
  assume that
A2: f is_bounded_above_on A;
  assume that
A3: g is_bounded_above_on A;
A4: rng(f|divset(D,i)) is bounded_above & rng(f|divset(D,i)) is non empty
  proof
    rng(f|divset(D,i)) c= rng f by RELAT_1:99;
    hence rng(f|divset(D,i)) is bounded_above by A2,Th15,RCOMP_1:4;
A5: dom f = A by FUNCT_2:def 1;
    dom (f|divset(D,i)) = divset(D,i) by A5,Th10,RELAT_1:91,A1;
    hence thesis by RELAT_1:65;
  end;
A6: rng(g|divset(D,i)) is bounded_above & rng(g|divset(D,i)) is non empty
  proof
    rng(g|divset(D,i)) c= rng g by RELAT_1:99;
    hence rng(g|divset(D,i)) is bounded_above by A3,Th15,RCOMP_1:4;
A7: dom g = A by FUNCT_2:def 1;
    dom (g|divset(D,i)) = divset(D,i) by A7,Th10,RELAT_1:91,A1;
    hence thesis by RELAT_1:65;
  end;
  then
A8: sup(rng(f|divset(D,i))+rng(g|divset(D,i)))
  =sup rng(f|divset(D,i)) + sup rng(g|divset(D,i)) by A4,Th54;
A9: rng(f|divset(D,i))+rng(g|divset(D,i)) is bounded_above by A4,A6,Th53;
A10: rng((f+g)|divset(D,i)) is non empty
  proof
A11: dom(f+g) = A /\ A by FUNCT_2:def 1;
    dom((f+g)|divset(D,i)) = divset(D,i) by A11,Th10,RELAT_1:91,A1;
    hence thesis by RELAT_1:65;
  end;
A12: 0 <= vol(divset(D,i)) by SEQ_4:24,XREAL_1:50;
  (f+g)|divset(D,i)=f|divset(D,i) + g|divset(D,i) by RFUNCT_1:60;
  then rng((f+g)|divset(D,i))c=rng(f|divset(D,i))+rng(g|divset(D,i)) by Th12;
  then sup rng((f+g)|divset(D,i))*vol(divset(D,i))
  <= (sup rng(f|divset(D,i)) + sup rng(g|divset(D,i)))*vol(divset(D,i))
  by A8,A9,A10,A12,SEQ_4:65,XREAL_1:66;
  then upper_volume(f+g,D).i <= sup rng(f|divset(D,i))*vol(divset(D,i))+
  sup rng(g|divset(D,i))*vol(divset(D,i)) by A1,Def7;
  then upper_volume(f+g,D).i <= upper_volume(f,D).i+
  sup rng(g|divset(D,i))*vol(divset(D,i)) by A1,Def7;
  hence thesis by A1,Def7;
end;

theorem Th56:
  for A be closed-interval Subset of REAL, f,g be Function of A,REAL,
  S be non empty Division of A,
  D be Element of S st i in dom D & f is_bounded_below_on A
  & g is_bounded_below_on A
  holds lower_volume(f,D).i + lower_volume(g,D).i <= lower_volume(f+g,D).i
proof
  let A be closed-interval Subset of REAL;
  let f,g be Function of A,REAL;
  let S be non empty Division of A;
  let D be Element of S;
  assume
A1: i in dom D;
  assume that
A2: f is_bounded_below_on A;
  assume that
A3: g is_bounded_below_on A;
A4: rng(f|divset(D,i)) is bounded_below & rng(f|divset(D,i)) is non empty
  proof
    rng(f|divset(D,i)) c= rng f by RELAT_1:99;
    hence rng(f|divset(D,i)) is bounded_below by A2,Th13,RCOMP_1:3;
A5: dom f = A by FUNCT_2:def 1;
    dom (f|divset(D,i)) = divset(D,i) by A5,Th10,RELAT_1:91,A1;
    hence thesis by RELAT_1:65;
  end;
A6: rng(g|divset(D,i)) is bounded_below & rng(g|divset(D,i)) is non empty
  proof
    rng(g|divset(D,i)) c= rng g by RELAT_1:99;
    hence rng(g|divset(D,i)) is bounded_below by A3,Th13,RCOMP_1:3;
A7: dom g = A by FUNCT_2:def 1;
    dom (g|divset(D,i)) = divset(D,i) by A7,Th10,RELAT_1:91,A1;
    hence thesis by RELAT_1:65;
  end;
  then
A8: inf(rng(f|divset(D,i))+rng(g|divset(D,i)))
  =inf rng(f|divset(D,i)) + inf rng(g|divset(D,i)) by A4,COMPLSP1:96;
A9: rng
  (f|divset(D,i))+rng(g|divset(D,i)) is bounded_below by A4,A6,COMPLSP1:95;
A10: rng((f+g)|divset(D,i)) is non empty
  proof
A11: dom(f+g) = A /\ A by FUNCT_2:def 1;
    dom((f+g)|divset(D,i)) = divset(D,i) by A11,Th10,RELAT_1:91,A1;
    hence thesis by RELAT_1:65;
  end;
A12: 0 <= vol(divset(D,i)) by SEQ_4:24,XREAL_1:50;
  (f+g)|divset(D,i)=f|divset(D,i) + g|divset(D,i) by RFUNCT_1:60;
  then rng((f+g)|divset(D,i))c=rng(f|divset(D,i))+rng(g|divset(D,i)) by Th12;
  then inf rng((f+g)|divset(D,i))*vol(divset(D,i))
  >= (inf rng(f|divset(D,i)) + inf rng(g|divset(D,i)))*vol(divset(D,i))
  by A8,A9,A10,A12,SEQ_4:64,XREAL_1:66;
  then lower_volume(f+g,D).i >= inf rng(f|divset(D,i))*vol(divset(D,i))+
  inf rng(g|divset(D,i))*vol(divset(D,i)) by A1,Def8;
  then lower_volume(f+g,D).i >= lower_volume(f,D).i+
  inf rng(g|divset(D,i))*vol(divset(D,i)) by A1,Def8;
  hence thesis by A1,Def8;
end;

theorem Th57:
  for A be closed-interval Subset of REAL, f,g be Function of A,REAL,
  S be non empty Division of A, D be Element of S st f is_bounded_above_on A
  & g is_bounded_above_on A
  holds upper_sum(f+g,D) <= upper_sum(f,D) + upper_sum(g,D)
proof
  let A be closed-interval Subset of REAL;
  let f,g be Function of A,REAL;
  let S be non empty Division of A;
  let D be Element of S;
  assume
A1: f is_bounded_above_on A & g is_bounded_above_on A;
  set F=upper_volume(f,D);
  set G=upper_volume(g,D);
  set H=upper_volume(f+g,D);
A2: len F = len D by Def7;
A3: len G = len D by Def7;
A4: len H = len D by Def7;
A5: F is Element of (len D)-tuples_on REAL by A2,FINSEQ_2:110;
A6: G is Element of (len D)-tuples_on REAL by A3,FINSEQ_2:110;
A7: H is Element of (len D)-tuples_on REAL by A4,FINSEQ_2:110;
A8: F+G is Element of (len D)-tuples_on REAL by A5,A6,FINSEQ_2:140;
  for j be Nat st j in Seg(len D) holds H.j <= (F+G).j
  proof
    let j be Nat;
    assume
    j in Seg(len D);
    then j in dom D by FINSEQ_1:def 3;
    then upper_volume(f+g,D).j <= upper_volume(f,D).j + upper_volume(g,D).j
    by A1,Th55;
    hence thesis by A5,A6,RVSUM_1:27;
  end;
  then Sum H <= Sum(F+G) by A7,A8,RVSUM_1:112;
  hence thesis by A5,A6,RVSUM_1:119;
end;

theorem Th58:
  for A be closed-interval Subset of REAL, f,g be Function of A,REAL,
  S be non empty Division of A, D be Element of S st f is_bounded_below_on A
  & g is_bounded_below_on A
  holds lower_sum(f,D) + lower_sum(g,D) <= lower_sum(f+g,D)
proof
  let A be closed-interval Subset of REAL;
  let f,g be Function of A,REAL;
  let S be non empty Division of A;
  let D be Element of S;
  assume
A1: f is_bounded_below_on A & g is_bounded_below_on A;
  set F=lower_volume(f,D);
  set G=lower_volume(g,D);
  set H=lower_volume(f+g,D);
A2: len F = len D by Def8;
A3: len G = len D by Def8;
A4: len H = len D by Def8;
A5: F is Element of (len D)-tuples_on REAL by A2,FINSEQ_2:110;
A6: G is Element of (len D)-tuples_on REAL by A3,FINSEQ_2:110;
A7: H is Element of (len D)-tuples_on REAL by A4,FINSEQ_2:110;
A8: F+G is Element of (len D)-tuples_on REAL by A5,A6,FINSEQ_2:140;
  for j be Nat st j in Seg(len D) holds (F+G).j <= H.j
  proof
    let j be Nat;
    assume
    j in Seg(len D);
    then j in dom D by FINSEQ_1:def 3;
    then lower_volume(f,D).j + lower_volume(g,D).j <= lower_volume(f+g,D).j
    by A1,Th56;
    hence thesis by A5,A6,RVSUM_1:27;
  end;
  then Sum(F+G) <= Sum H by A7,A8,RVSUM_1:112;
  hence thesis by A5,A6,RVSUM_1:119;
end;

theorem
  for A be closed-interval Subset of REAL, f,g be Function of A,REAL st
  f is_bounded_on A & g is_bounded_on A &
  f is_integrable_on A & g is_integrable_on A
  holds f+g is_integrable_on A & integral(f+g)=integral(f)+integral(g)
proof
  let A be closed-interval Subset of REAL;
  let f,g be Function of A,REAL;
  assume that
A1: f is_bounded_on A and
A2: g is_bounded_on A and
A3: f is_integrable_on A and
A4: g is_integrable_on A;
A5: f is_bounded_above_on A & f is_bounded_below_on A by A1,RFUNCT_1:def 11;
A6: g is_bounded_above_on A & g is_bounded_below_on A by A2,RFUNCT_1:def 11;
A7: f+g is_bounded_on (A /\ A) by A1,A2,RFUNCT_1:100;
A8: dom upper_sum_set(f+g) = divs A by Def11;
A9: dom lower_sum_set(f+g) = divs A by Def12;
  for D be set st D in divs A /\ dom(upper_sum_set(f+g))
  holds (inf rng f)*vol(A) + (inf rng g)*vol(A) <= (upper_sum_set(f+g)).D
  proof
    let D be set;
    assume
A10: D in divs A /\ dom(upper_sum_set(f+g));
    then reconsider D as Element of divs A;
    D in dom(upper_sum_set(f+g)) by A10,XBOOLE_0:def 3;
    then (upper_sum_set(f+g)).D = upper_sum((f+g),D) by Def11;
    then
A11: lower_sum((f+g),D) <= (upper_sum_set(f+g)).D by A7,Th30;
    lower_sum(f,D)+lower_sum(g,D) <= lower_sum((f+g),D) by A5,A6,Th58;
    then
A12: lower_sum(f,D)+lower_sum(g,D) <= (upper_sum_set(f+g)).D by A11,
    XXREAL_0:2;
    (inf rng f)*vol(A)+lower_sum(g,D) <= lower_sum(f,D)+lower_sum(g,D)
    proof
      (inf rng f)*vol(A) <= lower_sum(f,D) by A5,Th27;
      hence thesis by XREAL_1:8;
    end;
    then
A13: (inf rng f)*vol(A)+lower_sum(g,D) <= (upper_sum_set(f+g)).D
    by A12,XXREAL_0:2;
    (inf rng f)*vol(A)+(inf rng g)*vol(A)<=(inf rng f)*vol(A)+lower_sum(g,D)
    proof
      (inf rng g)*vol(A) <= lower_sum(g,D) by A6,Th27;
      hence thesis by XREAL_1:8;
    end;
    hence thesis by A13,XXREAL_0:2;
  end;
  then upper_sum_set(f+g) is_bounded_below_on divs A by RFUNCT_1:def 10;
  then
A14: rng upper_sum_set(f+g) is bounded_below by Th13;
  then
A15: f+g is_upper_integrable_on A by Def13;
A16: rng lower_sum_set(f+g) is bounded_above
  proof
    lower_sum_set(f+g) is_bounded_above_on divs A
    proof
      for D be set st D in divs A /\ dom(lower_sum_set(f+g))
      holds (lower_sum_set(f+g)).D <= (sup rng f)*vol(A) + (sup rng g)*vol(A)
      proof
        let D be set;
        assume
A17:    D in divs A /\ dom(lower_sum_set(f+g));
        then reconsider D as Element of divs A;
        D in dom(lower_sum_set(f+g)) by A17,XBOOLE_0:def 3;
        then (lower_sum_set(f+g)).D = lower_sum((f+g),D) by Def12;
        then
A18:    (lower_sum_set(f+g)).D <= upper_sum((f+g),D) by A7,Th30;
        upper_sum((f+g),D) <= upper_sum(f,D)+upper_sum(g,D) by A5,A6,Th57;
        then
A19:    (lower_sum_set(f+g)).D <= upper_sum(f,D)+upper_sum(g,D)
        by A18,XXREAL_0:2;
        upper_sum(f,D)+upper_sum(g,D) <= (sup rng f)*vol(A)+upper_sum(g,D )
        proof
          upper_sum(f,D) <= (sup rng f)*vol(A) by A5,Th29;
          hence thesis by XREAL_1:8;
        end;
        then
A20:    (lower_sum_set(f+g)).D <= (sup rng f)*vol(A)+upper_sum(g,D )
        by A19,XXREAL_0:2;
        (sup rng f)*vol(A)+upper_sum(g,D)<=
        (sup rng f)*vol(A)+(sup rng g)*vol(A)
        proof
          upper_sum(g,D) <= (sup rng g)*vol(A) by A6,Th29;
          hence thesis by XREAL_1:8;
        end;
        hence thesis by A20,XXREAL_0:2;
      end;
      hence thesis by RFUNCT_1:def 9;
    end;
    hence thesis by Th15;
  end;
  then
A21: f+g is_lower_integrable_on A by Def14;
  upper_integral(f+g) = lower_integral(f+g) &
  upper_integral(f+g) = integral(f) + integral(g)
  proof
    dom upper_sum_set f <> {} by Def11;
    then
A22: rng upper_sum_set f is non empty Subset of REAL by RELAT_1:65;
    dom lower_sum_set f <> {} by Def12;
    then
A23: rng lower_sum_set f is non empty Subset of REAL by RELAT_1:65;
    dom upper_sum_set g <> {} by Def11;
    then
A24: rng upper_sum_set g is non empty Subset of REAL by RELAT_1:65;
    dom lower_sum_set g <> {} by Def12;
    then
A25: rng lower_sum_set g is non empty Subset of REAL by RELAT_1:65;
A26: for D be Element of divs A st D in divs A /\ dom upper_sum_set(f+g) holds
    (upper_sum_set f).D + (upper_sum_set g).D >= upper_integral(f+g)
    proof
      let D be Element of divs A;
      assume D in divs A /\ dom upper_sum_set(f+g);
      then
A27:  D in dom(upper_sum_set(f+g)) by XBOOLE_0:def 3;
      then (upper_sum_set(f+g)).D in rng upper_sum_set(f+g) by FUNCT_1:def 5;
      then
A28:  (upper_sum_set(f+g)).D >= upper_integral(f+g) by A14,SEQ_4:def 5;
A29:  D in dom(upper_sum_set(f)) & D in dom(upper_sum_set(g))
      proof
A30:    D in divs A;
        hence D in dom(upper_sum_set(f)) by Def11;
        thus thesis by A30,Def11;
      end;
      upper_sum(f,D)+upper_sum(g,D) >= upper_sum((f+g),D) by A5,A6,Th57;
      then (upper_sum_set f).D+upper_sum(g,D) >= upper_sum((f+g),D) by A29,
      Def11;
      then (upper_sum_set f).D+(upper_sum_set g).D >= upper_sum((f+g),D)
      by A29,Def11;
      then (upper_sum_set f).D+(upper_sum_set g).D>=(upper_sum_set(f+g)).D
      by A27,Def11;
      hence thesis by A28,XXREAL_0:2;
    end;
A31: for D be Element of divs A st D in divs A /\ dom lower_sum_set(f+g) holds
    (lower_sum_set f).D + (lower_sum_set g).D <= lower_integral(f+g)
    proof
      let D be Element of divs A;
      assume D in divs A /\ dom lower_sum_set(f+g);
      then
A32:  D in dom(lower_sum_set(f+g)) by XBOOLE_0:def 3;
      then (lower_sum_set(f+g)).D in rng lower_sum_set(f+g) by FUNCT_1:def 5;
      then
A33:  (lower_sum_set(f+g)).D <= lower_integral(f+g) by A16,SEQ_4:def 4;
A34:  D in dom(lower_sum_set(f)) & D in dom(lower_sum_set(g))
      proof
A35:    D in divs A;
        hence D in dom(lower_sum_set(f)) by Def12;
        thus thesis by A35,Def12;
      end;
      lower_sum(f,D)+lower_sum(g,D) <= lower_sum((f+g),D) by A5,A6,Th58;
      then (lower_sum_set f).D+lower_sum(g,D) <= lower_sum((f+g),D) by A34,
      Def12;
      then (lower_sum_set f).D+(lower_sum_set g).D <= lower_sum((f+g),D)
      by A34,Def12;
      then (lower_sum_set f).D+(lower_sum_set g).D<=(lower_sum_set(f+g)).D
      by A32,Def12;
      hence thesis by A33,XXREAL_0:2;
    end;
    for a1 be real number st a1 in rng upper_sum_set(f) holds
    a1 >= upper_integral(f+g) - upper_integral(g)
    proof
      let a1 be real number;
      assume a1 in rng upper_sum_set(f);
      then consider D1 be Element of divs A such that
A36:  D1 in dom upper_sum_set(f) & a1=(upper_sum_set(f)).D1 by PARTFUN1:26;
A37:  a1=upper_sum(f,D1) by A36,Def11;
      for a2 being real number st a2 in rng upper_sum_set(g) holds
      a2 >= upper_integral(f+g) - a1
      proof
        let a2 be real number;
        assume a2 in rng upper_sum_set(g);
        then consider D2 be Element of divs A such that
A38:    D2 in dom upper_sum_set(g) & a2=(upper_sum_set(g)).D2 by PARTFUN1:26;
A39:    a2=upper_sum(g,D2) by A38,Def11;
        consider D be Element of divs A such that
A40:    D1 <= D & D2 <= D by Th49;
A41:    (upper_sum_set f).D + (upper_sum_set g).D >=
        upper_integral(f+g) by A8,A26;
A42:    D in dom upper_sum_set(f) & D in dom upper_sum_set(g)
        proof
A43:      D in divs A;
          hence D in dom(upper_sum_set(f)) by Def11;
          thus thesis by A43,Def11;
        end;
        then (upper_sum_set(f)).D = upper_sum(f,D) by Def11;
        then
A44:    (upper_sum_set(f)).D <= upper_sum(f,D1) by A5,A40,Th47;
        (upper_sum_set(g)).D = upper_sum(g,D) by A42,Def11;
        then (upper_sum_set(g)).D <= upper_sum(g,D2) by A6,A40,Th47;
        then (upper_sum_set f).D + (upper_sum_set g).D <=
        upper_sum(f,D1) + upper_sum(g,D2) by A44,XREAL_1:9;
        then upper_sum(f,D1)+upper_sum(g,D2) >= upper_integral(f+g)
        by A41,XXREAL_0:2;
        hence thesis by A37,A39,XREAL_1:22;
      end;
      then inf rng upper_sum_set(g) >= upper_integral(f+g) - a1 by A24,
      SEQ_4:60;
      then a1+inf rng upper_sum_set(g) >= upper_integral(f+g) by XREAL_1:22;
      hence thesis by XREAL_1:22;
    end;
    then
A45: upper_integral(f) >= upper_integral(f+g) - upper_integral(g) by
    A22,SEQ_4:60;
    for a1 be real number st a1 in rng lower_sum_set(f) holds
    a1 <= lower_integral(f+g) - lower_integral(g)
    proof
      let a1 be real number;
      assume a1 in rng lower_sum_set(f);
      then consider D1 be Element of divs A such that
A46:  D1 in dom lower_sum_set(f) & a1=(lower_sum_set(f)).D1 by PARTFUN1:26;
A47:  a1=lower_sum(f,D1) by A46,Def12;
      for a2 be real number st a2 in rng lower_sum_set(g) holds
      a2 <= lower_integral(f+g) - a1
      proof
        let a2 be real number;
        assume a2 in rng lower_sum_set(g);
        then consider D2 be Element of divs A such that
A48:    D2 in dom lower_sum_set(g) & a2=(lower_sum_set(g)).D2 by PARTFUN1:26;
A49:    a2=lower_sum(g,D2) by A48,Def12;
        consider D be Element of divs A such that
A50:    D1 <= D & D2 <= D by Th49;
A51:    (lower_sum_set f).D + (lower_sum_set g).D <= lower_integral(f+g)
        by A9,A31;
A52:    D in dom lower_sum_set(f) & D in dom lower_sum_set(g)
        proof
A53:      D in divs A;
          hence D in dom(lower_sum_set(f)) by Def12;
          thus thesis by A53,Def12;
        end;
        then (lower_sum_set(f)).D = lower_sum(f,D) by Def12;
        then
A54:    (lower_sum_set(f)).D >= lower_sum(f,D1) by A5,A50,Th48;
        (lower_sum_set(g)).D = lower_sum(g,D) by A52,Def12;
        then (lower_sum_set(g)).D >= lower_sum(g,D2) by A6,A50,Th48;
        then (lower_sum_set f).D + (lower_sum_set g).D >=
        lower_sum(f,D1) + lower_sum(g,D2) by A54,XREAL_1:9;
        then lower_sum(f,D1)+lower_sum(g,D2) <= lower_integral(f+g)
        by A51,XXREAL_0:2;
        hence thesis by A47,A49,XREAL_1:21;
      end;
      then sup rng lower_sum_set(g)<=
      lower_integral(f+g) - a1 by A25,SEQ_4:62;
      then a1+sup rng lower_sum_set(g) <= lower_integral(f+g) by XREAL_1:21;
      hence thesis by XREAL_1:21;
    end;
    then sup rng lower_sum_set(f)<=lower_integral(f+g)-lower_integral(g)
    by A23,SEQ_4:62;
    then
A55: lower_integral(f) + lower_integral(g) <= lower_integral(f+g) by
    XREAL_1:21;
A56: upper_integral(f+g)>=lower_integral(f+g) by A7,Th51;
A57: lower_integral(f)+lower_integral(g)
    =upper_integral(f) + lower_integral(g) by A3,Def17
      .=integral(f) + integral(g) by A4,Def17;
    then
A58: integral(f)+integral(g) <= upper_integral(f+g) by A55,A56, XXREAL_0:2;
    integral(f)+upper_integral(g)>=upper_integral(f+g) by A45,XREAL_1:22;
    then upper_integral(f+g)=integral(f) + integral(g) by A58,XXREAL_0:1;
    hence thesis by A55,A56,A57,XXREAL_0:1;
  end;
  hence thesis by A15,A21,Def17;
end;

