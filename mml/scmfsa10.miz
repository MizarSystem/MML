:: On the Instructions of { \bf SCM+FSA }
::  by Artur Korni{\l}owicz
::
:: Received May 8, 2001
:: Copyright (c) 2001-2011 Association of Mizar Users
::           (Stowarzyszenie Uzytkownikow Mizara, Bialystok, Poland).
:: This code can be distributed under the GNU General Public Licence
:: version 3.0 or later, or the Creative Commons Attribution-ShareAlike
:: License version 3.0 or later, subject to the binding interpretation
:: detailed in file COPYING.interpretation.
:: See COPYING.GPL and COPYING.CC-BY-SA for the full text of these
:: licenses, or see http://www.gnu.org/licenses/gpl.html and
:: http://creativecommons.org/licenses/by-sa/3.0/.

environ

 vocabularies NUMBERS, SCMFSA_2, AMI_3, AMI_1, FSM_1, ORDINAL1, INT_1,
      FUNCOP_1, SUBSET_1, SCMFSA_1, CAT_1, XBOOLE_0, GRAPHSP, FINSEQ_1,
      RELAT_1, CARD_1, AMI_2, AMISTD_2, CARD_3, FUNCT_1, AMISTD_1, CIRCUIT2,
      FUNCT_4, SETFAM_1, ZFMISC_1, XXREAL_0, TARSKI, ARYTM_3, GOBOARD5,
      FRECHET, ARYTM_1, COMPLEX1, PARTFUN1, FINSEQ_2, UNIALG_1, CARD_5,
      RECDEF_2, NAT_1, COMPOS_1, PBOOLE;
 notations TARSKI, XBOOLE_0, ENUMSET1, SUBSET_1, MCART_1, SETFAM_1, RELAT_1,
      ZFMISC_1, FUNCT_1, PARTFUN1, PBOOLE, FUNCT_2, ORDINAL1, XCMPLX_0, INT_1,
      FUNCOP_1, FINSEQ_1, FINSEQ_2, FUNCT_4, FUNCT_7, COMPLEX1, CARD_1, CARD_3,
      XXREAL_0, NAT_1, RECDEF_2, VALUED_0, VALUED_1, NUMBERS, STRUCT_0,
      MEMSTR_0, COMPOS_1, EXTPRO_1, AMISTD_1, AMISTD_2, AMI_2, AMI_3, SCMFSA_1,
      SCMFSA_3, SCMFSA_2;
 constructors XXREAL_0, REAL_1, NAT_D, FINSEQ_4, ZFMISC_1, AMI_3, SCMFSA_3,
      AMISTD_2, SEQ_1, RELSET_1, RECDEF_2, VALUED_1, VALUED_0, AMISTD_1,
      SCMFSA_1, PRE_POLY, PBOOLE, ENUMSET1, DOMAIN_1, FUNCT_7, MEMSTR_0;
 registrations XBOOLE_0, SETFAM_1, RELAT_1, FUNCT_1, ORDINAL1, FUNCOP_1,
      NUMBERS, XREAL_0, NAT_1, INT_1, FINSEQ_1, SCMFSA_2, AMISTD_2, RELSET_1,
      CARD_3, FUNCT_2, ZFMISC_1, AMI_3, FUNCT_4, VALUED_0, VALUED_1, COMPOS_1,
      EXTPRO_1, PBOOLE, FUNCT_7, PRE_POLY, MEMSTR_0, CARD_1;
 requirements NUMERALS, BOOLE, SUBSET, REAL, ARITHM;
 definitions TARSKI, FUNCT_1, SCMFSA_2, AMISTD_1, AMISTD_2, XBOOLE_0, FUNCOP_1,
      SCMFSA_1, COMPOS_1, EXTPRO_1, AMI_3, CARD_3, NAT_1, MCART_1, FUNCT_7,
      MEMSTR_0;
 theorems TARSKI, NAT_1, AMI_3, FUNCT_4, FUNCT_1, FUNCOP_1, SETFAM_1, AMISTD_1,
      MCART_1, FINSEQ_1, FUNCT_7, CARD_3, SCMFSA_2, INT_1, BVFUNC14, ABSVALUE,
      FINSEQ_2, XBOOLE_0, XBOOLE_1, NAT_D, RELAT_1, MEMSTR_0, PARTFUN1, PBOOLE,
      RECDEF_2, AMI_6, VALUED_1, COMPOS_1, EXTPRO_1;

begin

reserve a, b, d1, d2, d3, d4 for Int-Location,
  A, B for Data-Location,
  f, f1, f2, f3 for FinSeq-Location,
  il, i1, i2 for Element of NAT,
  L for Element of NAT,
  I for Instruction of SCM+FSA,
  s,s1,s2 for State of SCM+FSA,
  T for InsType of SCM+FSA,
  k for natural number;

definition
  let la, lb be Int-Location, a, b be Integer;
  redefine func (la,lb) --> (a,b) -> PartState of SCM+FSA;
  coherence
  proof
A1: ObjectKind lb = INT by SCMFSA_2:11;
 b is Element of INT by INT_1:def 2;
     then reconsider b as Element of ObjectKind lb by A1;
A2: ObjectKind la = INT by SCMFSA_2:11;
    a is Element of INT by INT_1:def 2;
     then reconsider a as Element of ObjectKind la by A2;
      (la,lb) --> (a,b) is PartState of SCM+FSA;
    hence thesis;
  end;
end;

theorem Th7:
  for o being Object of SCM+FSA st o in Data-Locations SCM+FSA
   holds o is Int-Location or o is FinSeq-Location
proof
  let o be Object of SCM+FSA;
  assume o in Data-Locations SCM+FSA;
   then o in SCM+FSA-Data-Loc or o in SCM+FSA-Data*-Loc
    by SCMFSA_2:100,XBOOLE_0:def 3;
  hence thesis by SCMFSA_2:def 4,def 5;
end;

theorem Th9:
  a := b = [1,{},<*a,b*>]
proof
  ex A,B st a = A & b = B & a := b = A:=B by SCMFSA_2:def 8;
  hence thesis;
end;

theorem Th10:
  AddTo(a,b) = [2,{},<*a,b*>]
proof
  ex A,B st a = A & b = B & AddTo(a,b) = AddTo(A,B) by SCMFSA_2:def 9;
  hence thesis;
end;

theorem Th11:
  SubFrom(a,b) = [3,{},<* a,b *>]
proof
  ex A,B st a = A & b = B & SubFrom(a,b) = SubFrom(A,B) by SCMFSA_2:def 10;
  hence thesis;
end;

theorem Th12:
  MultBy(a,b) = [4,{},<* a,b *>]
proof
  ex A,B st a = A & b = B & MultBy(a,b) = MultBy(A,B) by SCMFSA_2:def 11;
  hence thesis;
end;

theorem Th13:
  Divide(a,b) = [5,{},<* a,b *>]
proof
  ex A,B st a = A & b = B & Divide(a,b) = Divide(A,B) by SCMFSA_2:def 12;
  hence thesis;
end;

theorem Th15:
  a=0_goto il = [7, <* il*>,<*a *>]
proof
  ex A st A = a & A=0_goto il = a=0_goto il by SCMFSA_2:def 14;
  hence thesis;
end;

theorem Th16:
  a>0_goto il = [8, <* il*>,<*a *>]
proof
  ex A st A = a & A>0_goto il = a>0_goto il by SCMFSA_2:def 15;
  hence thesis;
end;

reserve J,K for Element of Segm 13,
  b,b1,c,c1 for Element of SCM+FSA-Data-Loc,
  f,f1 for Element of SCM+FSA-Data*-Loc;

Lm1: T = 0 or T = 1 or T = 2 or T = 3 or T = 4 or T = 5 or T = 6 or T = 7 or T
= 8 or T = 9 or T = 10 or T = 11 or T = 12

proof
  consider y being set such that
A1: [T,y] in proj1 the Instructions of SCM+FSA by RELAT_1:def 4;
  consider x being set such that
A2: [[T,y],x] in the Instructions of SCM+FSA by A1,RELAT_1:def 4;
   reconsider I = [T,y,x] as Instruction of SCM+FSA by A2;
A3: I in SCM-Instr \/ { [J,{},<*c,f,b*>] : J in {9,10} }
 or I in { [K,{},<*c1,f1*>] : K in {11,12} }
  by XBOOLE_0:def 3;

  per cases by A3,XBOOLE_0:def 3;
  suppose
    I in SCM-Instr;
    then reconsider I as Instruction of SCM;
    T = InsCode I by RECDEF_2:def 1;
    hence thesis by AMI_6:1;
  end;
  suppose
    I in { [J,{},<*c,f,b*>] : J in {9,10} };
    then ex J,c,b,f st I = [J,{},<*c,f,b*>] & J in {9,10};
    then T in {9,10} by MCART_1:25;
    hence thesis by TARSKI:def 2;
  end;
  suppose
    I in { [K,{},<*c1,f1*>] : K in {11,12} };
    then ex K,c1,f1 st I = [K,{},<*c1,f1*>] & K in {11,12};
    then T in {11,12} by MCART_1:25;
    hence thesis by TARSKI:def 2;
  end;
end;

theorem Th17:
  JumpPart halt SCM+FSA = {};

reserve a, b, d1, d2, d3, d4 for Int-Location,
  A, B for Data-Location,
  f, f1,
  f2, f3 for FinSeq-Location;

theorem Th18:
  JumpPart (a:=b) = {}
proof
  thus JumpPart (a:=b) = [ 1,{}, <*a,b*>]`2_3 by Th9
    .= {} by RECDEF_2:def 2;
end;

theorem Th19:
  JumpPart AddTo(a,b) = {}
proof
  thus JumpPart AddTo(a,b) = [ 2,{}, <*a,b*>]`2_3 by Th10
    .= {} by RECDEF_2:def 2;
end;

theorem Th20:
  JumpPart SubFrom(a,b) = {}
proof
  thus JumpPart SubFrom(a,b) = [ 3,{}, <*a,b*>]`2_3 by Th11
    .= {} by RECDEF_2:def 2;
end;

theorem Th21:
  JumpPart MultBy(a,b) = {}
proof
  thus JumpPart MultBy(a,b) = [ 4,{}, <*a,b*>]`2_3 by Th12
    .= {} by RECDEF_2:def 2;
end;

theorem Th22:
  JumpPart Divide(a,b) = {}
proof
  thus JumpPart Divide(a,b) = [ 5,{}, <*a,b*>]`2_3 by Th13
    .= {} by RECDEF_2:def 2;
end;

theorem Th24:
  JumpPart (a=0_goto i1) = <*i1*>
proof
  thus JumpPart (a=0_goto i1) = [ 7,<*i1*>,<*a*>]`2_3 by Th15
    .= <*i1*> by RECDEF_2:def 2;
end;

theorem Th25:
  JumpPart (a>0_goto i1) = <*i1*>
proof
  thus JumpPart (a>0_goto i1) = [ 8,<*i1*>,<*a*>]`2_3 by Th16
    .= <*i1*> by RECDEF_2:def 2;
end;

theorem Th30:
  T = 0 implies JumpParts T = {0}
proof
  assume
A1: T = 0;
  hereby
    let a be set;
    assume a in JumpParts T;
    then consider I such that
A2: a = JumpPart I and
A3: InsCode I = T;
    I = halt SCM+FSA by A1,A3,SCMFSA_2:95;
    hence a in {0} by A2,Th17,TARSKI:def 1;
  end;
  let a be set;
  assume a in {0};
  then a = 0 by TARSKI:def 1;
  hence thesis by A1,Th17,SCMFSA_2:97;
end;

theorem Th31:
  T = 1 implies JumpParts T = {{}}
proof
  assume
A1: T = 1;
  hereby
    let x be set;
    assume x in JumpParts T;
     then consider I being Instruction of SCM+FSA such that
A2:   x = JumpPart I and
A3:   InsCode I = T;
     consider a,b such that
A4:   I = a:=b by A1,A3,SCMFSA_2:30;
     x = {} by A2,Th18,A4;
    hence x in {{}} by TARSKI:def 1;
  end;
  set a = the Int-Location;
  let x be set;
  assume x in {{}};
   then x = {} by TARSKI:def 1;
   then
A5:  x = JumpPart(a:= a) by Th18;
    InsCode(a:= a) = 1 by SCMFSA_2:18;
  hence thesis by A5,A1;
end;

theorem Th32:
  T = 2 implies JumpParts T = {{}}
proof
  assume
A1: T = 2;
  hereby
    let x be set;
    assume x in JumpParts T;
     then consider I being Instruction of SCM+FSA such that
A2:   x = JumpPart I and
A3:   InsCode I = T;
     consider a,b such that
A4:   I = AddTo(a,b) by A1,A3,SCMFSA_2:31;
     x = {} by A2,Th19,A4;
    hence x in {{}} by TARSKI:def 1;
  end;
  set a = the Int-Location;
  let x be set;
  assume x in {{}};
   then x = {} by TARSKI:def 1;
   then
A5:  x = JumpPart AddTo(a,a) by Th19;
    InsCode AddTo(a,a) = 2 by SCMFSA_2:19;
  hence thesis by A5,A1;
end;

theorem Th33:
  T = 3 implies JumpParts T = {{}}
proof
  assume
A1: T = 3;
  hereby
    let x be set;
    assume x in JumpParts T;
     then consider I being Instruction of SCM+FSA such that
A2:   x = JumpPart I and
A3:  InsCode I = T;
     consider a,b such that
A4:   I = SubFrom(a,b) by A1,A3,SCMFSA_2:32;
     x = {} by A2,Th20,A4;
    hence x in {{}} by TARSKI:def 1;
  end;
  set a = the Int-Location;
  let x be set;
  assume x in {{}};
   then x = {} by TARSKI:def 1;
   then
A5:  x = JumpPart SubFrom(a,a) by Th20;
    InsCode SubFrom(a,a) = 3 by SCMFSA_2:20;
  hence thesis by A5,A1;
end;

theorem Th34:
  T = 4 implies JumpParts T = {{}}
proof
  assume
A1: T = 4;
  hereby
    let x be set;
    assume x in JumpParts T;
     then consider I being Instruction of SCM+FSA such that
A2:   x = JumpPart I and
A3:   InsCode I = T;
     consider a,b such that
A4:   I = MultBy(a,b) by A1,A3,SCMFSA_2:33;
     x = {} by A2,Th21,A4;
    hence x in {{}} by TARSKI:def 1;
  end;
  set a = the Int-Location;
  let x be set;
  assume x in {{}};
   then x = {} by TARSKI:def 1;
   then
A5:  x = JumpPart MultBy(a,a) by Th21;
    InsCode MultBy(a,a) = 4 by SCMFSA_2:21;
  hence thesis by A5,A1;
end;

theorem Th35:
  T = 5 implies JumpParts T = {{}}
proof
  assume
A1: T = 5;
  hereby
    let x be set;
    assume x in JumpParts T;
     then consider I being Instruction of SCM+FSA such that
A2:   x = JumpPart I and
A3:   InsCode I = T;
     consider a,b such that
A4:   I = Divide(a,b) by A1,A3,SCMFSA_2:34;
     x = {} by A2,Th22,A4;
    hence x in {{}} by TARSKI:def 1;
  end;
  set a = the Int-Location;
  let x be set;
  assume x in {{}};
   then x = {} by TARSKI:def 1;
   then
A5:  x = JumpPart Divide(a,a) by Th22;
    InsCode Divide(a,a) = 5 by SCMFSA_2:22;
  hence thesis by A5,A1;
end;

theorem Th36:
  T = 6 implies dom product" JumpParts T = {1}
proof
  set i1 = the Element of NAT;
  assume
A1: T = 6;
A2: JumpPart goto i1 = <*i1*> by RECDEF_2:def 2;
  hereby
    let x be set;
    InsCode goto i1 = 6 by SCMFSA_2:23;
    then
A3: JumpPart goto i1 in JumpParts T by A1;
    assume x in dom product" JumpParts T;
    then x in dom JumpPart goto i1 by A3,CARD_3:def 12;
    hence x in {1} by A2,FINSEQ_1:2,def 8;
  end;
  let x be set;
  assume
A4: x in {1};
  for f being Function st f in JumpParts T holds x in dom f
  proof
    let f be Function;
    assume f in JumpParts T;
    then consider I being Instruction of SCM+FSA such that
A5: f = JumpPart I and
A6: InsCode I = T;
    consider i1 such that
A7: I = goto i1 by A1,A6,SCMFSA_2:35;
     f = <*i1*> by A5,A7,RECDEF_2:def 2;
    hence thesis by A4,FINSEQ_1:2,def 8;
  end;
  hence thesis by CARD_3:def 12;
end;

theorem Th37:
  T = 7 implies dom product" JumpParts T = {1}
proof
  set i1 = the Element of NAT,a = the Int-Location;
  assume
A1: T = 7;
A2: JumpPart (a =0_goto i1) = <*i1*> by Th24;
  hereby
    let x be set;
    InsCode (a =0_goto i1) = 7 by SCMFSA_2:24;
    then
A3: JumpPart (a =0_goto i1) in JumpParts T by A1;
    assume x in dom product" JumpParts T;
    then x in dom JumpPart (a =0_goto i1) by A3,CARD_3:def 12;
    hence x in {1} by A2,FINSEQ_1:2,38;
  end;
  let x be set;
  assume
A4: x in {1};
  for f being Function st f in JumpParts T holds x in dom f
  proof
    let f be Function;
    assume f in JumpParts T;
    then consider I being Instruction of SCM+FSA such that
A5: f = JumpPart I and
A6: InsCode I = T;
    consider i1, a such that
A7: I = a =0_goto i1 by A1,A6,SCMFSA_2:36;
    f = <*i1*> by A5,A7,Th24;
    hence thesis by A4,FINSEQ_1:2,38;
  end;
  hence thesis by CARD_3:def 12;
end;

theorem Th38:
  T = 8 implies dom product" JumpParts T = {1}
proof
  set i1 = the Element of NAT,a = the Int-Location;
  assume
A1: T = 8;
A2: JumpPart (a >0_goto i1) = <*i1*> by Th25;
  hereby
    let x be set;
    InsCode (a >0_goto i1) = 8 by SCMFSA_2:25;
    then
A3: JumpPart (a >0_goto i1) in JumpParts T by A1;
    assume x in dom product" JumpParts T;
    then x in dom JumpPart (a >0_goto i1) by A3,CARD_3:def 12;
    hence x in {1} by A2,FINSEQ_1:2,38;
  end;
  let x be set;
  assume
A4: x in {1};
  for f being Function st f in JumpParts T holds x in dom f
  proof
    let f be Function;
    assume f in JumpParts T;
    then consider I being Instruction of SCM+FSA such that
A5: f = JumpPart I and
A6: InsCode I = T;
    consider i1, a such that
A7: I = a >0_goto i1 by A1,A6,SCMFSA_2:37;
    f = <*i1*> by A5,A7,Th25;
    hence thesis by A4,FINSEQ_1:2,38;
  end;
  hence thesis by CARD_3:def 12;
end;

theorem Th39:
  T = 9 implies JumpParts T = {{}}
proof
  assume
A1: T = 9;
  hereby
    let x be set;
    assume x in JumpParts T;
     then consider I being Instruction of SCM+FSA such that
A2:   x = JumpPart I and
A3:   InsCode I = T;
     consider a,b,f such that
A4:   I = b:=(f,a) by A1,A3,SCMFSA_2:38;
     x = {} by A2,A4,RECDEF_2:def 2;
    hence x in {{}} by TARSKI:def 1;
  end;
  set a = the Int-Location, f = the FinSeq-Location;
  let x be set;
  assume x in {{}};
   then x = {} by TARSKI:def 1;
   then
A5:  x = JumpPart(a:=(f,a)) by RECDEF_2:def 2;
    InsCode(a:=(f,a)) = 9 by SCMFSA_2:26;
  hence thesis by A5,A1;
end;

theorem Th40:
  T = 10 implies JumpParts T = {{}}
proof
  assume
A1: T = 10;
  hereby
    let x be set;
    assume x in JumpParts T;
     then consider I being Instruction of SCM+FSA such that
A2:   x = JumpPart I and
A3:   InsCode I = T;
     consider a,b,f such that
A4:   I = (f,a):=b by A1,A3,SCMFSA_2:39;
     x = {} by A2,A4,RECDEF_2:def 2;
    hence x in {{}} by TARSKI:def 1;
  end;
  set a = the Int-Location, f = the FinSeq-Location;
  let x be set;
  assume x in {{}};
   then x = {} by TARSKI:def 1;
   then
A5:  x = JumpPart((f,a):=a) by RECDEF_2:def 2;
    InsCode((f,a):=a) = 10 by SCMFSA_2:27;
  hence thesis by A5,A1;
end;

theorem Th41:
  T = 11 implies JumpParts T = {{}}
proof
  assume
A1: T = 11;
  hereby
    let x be set;
    assume x in JumpParts T;
     then consider I being Instruction of SCM+FSA such that
A2:   x = JumpPart I and
A3:  InsCode I = T;
     consider a,f such that
A4:   I = a:=len f by A1,A3,SCMFSA_2:40;
     x = {} by A2,A4,RECDEF_2:def 2;
    hence x in {{}} by TARSKI:def 1;
  end;
  set a = the Int-Location, f = the FinSeq-Location;
  let x be set;
  assume x in {{}};
   then x = {} by TARSKI:def 1;
   then
A5:  x = JumpPart(a:=len f) by RECDEF_2:def 2;
    InsCode(a:=len f) = 11 by SCMFSA_2:28;
  hence thesis by A5,A1;
end;

theorem Th42:
  T = 12 implies JumpParts T = {{}}
proof
  assume
A1: T = 12;
  hereby
    let x be set;
    assume x in JumpParts T;
     then consider I being Instruction of SCM+FSA such that
A2:   x = JumpPart I and
A3:   InsCode I = T;
     consider a,f such that
A4:   I = f:=<0,...,0>a by A1,A3,SCMFSA_2:41;
     x = {} by A2,A4,RECDEF_2:def 2;
    hence x in {{}} by TARSKI:def 1;
  end;
  set a = the Int-Location, f = the FinSeq-Location;
  let x be set;
  assume x in {{}};
   then x = {} by TARSKI:def 1;
   then
A5:  x = JumpPart(f:=<0,...,0>a) by RECDEF_2:def 2;
    InsCode(f:=<0,...,0>a) = 12 by SCMFSA_2:29;
  hence thesis by A5,A1;
end;

theorem Th53:
  (product" JumpParts InsCode goto i1).1 = NAT
proof
  dom product" JumpParts InsCode goto i1 = {1} by Th36,SCMFSA_2:23;
  then
A1: 1 in dom product" JumpParts InsCode goto i1 by TARSKI:def 1;
  hereby
    let x be set;
    assume x in (product" JumpParts InsCode goto i1).1;
    then x in pi(JumpParts InsCode goto i1,1) by A1,CARD_3:76;
    then consider g being Function such that
A2: g in JumpParts InsCode goto i1 and
A3: x = g.1 by CARD_3:def 6;
    consider I being Instruction of SCM+FSA such that
A4: g = JumpPart I and
A5: InsCode I = InsCode goto i1 by A2;
    consider i2 such that
A6: I = goto i2 by A5,SCMFSA_2:23,35;
    g = <*i2*> by A4,A6,RECDEF_2:def 2;
    then x = i2 by A3,FINSEQ_1:def 8;
    hence x in NAT;
  end;
  let x be set;
  assume x in NAT;
  then reconsider x as Element of NAT;
A7: <*x*>.1 = x by FINSEQ_1:def 8;
  InsCode goto i1 = 6 by SCMFSA_2:23;
  then
A8: InsCode goto i1 = InsCode goto x by SCMFSA_2:23;
  JumpPart goto x = <*x*> by RECDEF_2:def 2;
  then <*x*> in JumpParts InsCode goto i1 by A8;
  then x in pi(JumpParts InsCode goto i1,1) by A7,CARD_3:def 6;
  hence thesis by A1,CARD_3:76;
end;

theorem Th54:
  (product" JumpParts InsCode (a =0_goto i1)).1 = NAT
proof
  dom product" JumpParts InsCode (a =0_goto i1) = {1} by Th37,SCMFSA_2:24;
  then
A1: 1 in dom product" JumpParts InsCode (a =0_goto i1) by TARSKI:def 1;
  hereby
    let x be set;
    assume x in (product" JumpParts InsCode (a =0_goto i1)).1;
    then x in pi(JumpParts InsCode (a =0_goto i1),1) by A1,CARD_3:76;
    then consider g being Function such that
A2: g in JumpParts InsCode (a =0_goto i1) and
A3: x = g.1 by CARD_3:def 6;
    consider I being Instruction of SCM+FSA such that
A4: g = JumpPart I and
A5: InsCode I = InsCode (a =0_goto i1) by A2;
    consider i2, b such that
A6: I = b =0_goto i2 by A5,SCMFSA_2:24,36;
    g = <*i2*> by A4,A6,Th24;
    then x = i2 by A3,FINSEQ_1:40;
    hence x in NAT;
  end;
  let x be set;
  assume x in NAT;
  then reconsider x as Element of NAT;
A7: <*x*>.1 = x by FINSEQ_1:40;
  InsCode (a =0_goto i1) = 7 by SCMFSA_2:24;
  then
A8: InsCode (a =0_goto i1) = InsCode (a =0_goto x) by SCMFSA_2:24;
  JumpPart (a =0_goto x) = <*x*> by Th24;
  then <*x*> in JumpParts InsCode (a =0_goto i1) by A8;
  then x in pi(JumpParts InsCode (a =0_goto i1),1) by A7,CARD_3:def 6;
  hence thesis by A1,CARD_3:76;
end;

theorem Th56:
  (product" JumpParts InsCode (a >0_goto i1)).1 = NAT
proof
  dom product" JumpParts InsCode (a >0_goto i1) = {1} by Th38,SCMFSA_2:25;
  then
A1: 1 in dom product" JumpParts InsCode (a >0_goto i1) by TARSKI:def 1;
  hereby
    let x be set;
    assume x in (product" JumpParts InsCode (a >0_goto i1)).1;
    then x in pi(JumpParts InsCode (a >0_goto i1),1) by A1,CARD_3:76;
    then consider g being Function such that
A2: g in JumpParts InsCode (a >0_goto i1) and
A3: x = g.1 by CARD_3:def 6;
    consider I being Instruction of SCM+FSA such that
A4: g = JumpPart I and
A5: InsCode I = InsCode (a >0_goto i1) by A2;
    consider i2, b such that
A6: I = b >0_goto i2 by A5,SCMFSA_2:25,37;
    g = <*i2*> by A4,A6,Th25;
    then x = i2 by A3,FINSEQ_1:40;
    hence x in NAT;
  end;
  let x be set;
  assume x in NAT;
  then reconsider x as Element of NAT;
A7: <*x*>.1 = x by FINSEQ_1:40;
  InsCode (a >0_goto i1) = 8 by SCMFSA_2:25;
  then
A8: InsCode (a >0_goto i1) = InsCode (a >0_goto x) by SCMFSA_2:25;
  JumpPart (a >0_goto x) = <*x*> by Th25;
  then <*x*> in JumpParts InsCode (a >0_goto i1) by A8;
  then x in pi(JumpParts InsCode (a >0_goto i1),1) by A7,CARD_3:def 6;
  hence thesis by A1,CARD_3:76;
end;

Lm3: for i being Instruction of SCM+FSA holds (for l being
Element of NAT holds NIC(i,l)={succ l}) implies JUMP i is
empty

proof
  reconsider p=0, q=1 as Element of NAT;
  let i be Instruction of SCM+FSA;
  assume
A1: for l being Element of NAT holds NIC(i,l)={succ l};

  set X = { NIC(i,f) where f is Element of NAT: not
  contradiction };

  reconsider p, q as Element of NAT;
  assume not thesis;
  then consider x being set such that
A2: x in meet X by XBOOLE_0:def 1;
  NIC(i,p) = {succ p} by A1;
  then {succ p} in X;
  then x in {succ p} by A2,SETFAM_1:def 1;
  then
A3: x = succ p by TARSKI:def 1;
  NIC(i,q) = {succ q} by A1;
  then {succ q} in X;
  then x in {succ q} by A2,SETFAM_1:def 1;
  hence contradiction by A3,TARSKI:def 1;
end;

registration
  cluster JUMP halt SCM+FSA -> empty;
  coherence;
end;

registration
  let a, b;
  cluster a:=b -> sequential;
  coherence
  proof
    let s be State of SCM+FSA;
    thus thesis by SCMFSA_2:63;
  end;
  cluster AddTo(a,b) -> sequential;
  coherence
  proof
    let s be State of SCM+FSA;
    thus thesis by SCMFSA_2:64;
  end;
  cluster SubFrom(a,b) -> sequential;
  coherence
  proof
    let s be State of SCM+FSA;
    thus thesis by SCMFSA_2:65;
  end;
  cluster MultBy(a,b) -> sequential;
  coherence
  proof
    let s be State of SCM+FSA;
    thus thesis by SCMFSA_2:66;
  end;
  cluster Divide(a,b) -> sequential;
  coherence
  proof
    let s be State of SCM+FSA;
    thus thesis by SCMFSA_2:67;
  end;
end;

registration
  let a, b;
  cluster JUMP (a := b) -> empty;
  coherence
  proof
    for l being Element of NAT holds NIC(a:=b,l)={succ l}
    by AMISTD_1:12;
    hence thesis by Lm3;
  end;
  cluster JUMP AddTo(a, b) -> empty;
  coherence
  proof
    for l being Element of NAT holds NIC(AddTo(a,b),l)={
    succ l } by AMISTD_1:12;
    hence thesis by Lm3;
  end;
  cluster JUMP SubFrom(a, b) -> empty;
  coherence
  proof
    for l being Element of NAT holds NIC(SubFrom(a,b),l)=
    {succ l} by AMISTD_1:12;
    hence thesis by Lm3;
  end;
  cluster JUMP MultBy(a,b) -> empty;
  coherence
  proof
    for l being Element of NAT holds NIC(MultBy(a,b),l)={
    succ l } by AMISTD_1:12;
    hence thesis by Lm3;
  end;
  cluster JUMP Divide(a,b) -> empty;
  coherence
  proof
    for l being Element of NAT holds NIC(Divide(a,b),l)={
    succ l } by AMISTD_1:12;
    hence thesis by Lm3;
  end;
end;

theorem Th74:
  NIC(goto i1, il) = {i1}
proof
  now
    let x be set;
A1: now
      reconsider il1 = il as Element of ObjectKind IC SCM+FSA by MEMSTR_0:def 3
;
      reconsider n = il1 as Element of NAT;
      set I = goto i1;
      set t = the State of SCM+FSA,
      Q = the Instruction-Sequence of SCM+FSA;
      assume
A2:   x = i1;
      reconsider u = t+*(IC SCM+FSA,il1)
       as Element of product the Object-Kind of SCM+FSA by CARD_3:107;
      reconsider P = Q +* (il,I)
       as Instruction-Sequence of SCM+FSA;
    IC SCM+FSA in dom t by MEMSTR_0:2;
    then
A3: IC u = n by FUNCT_7:31;
A4:   P/.il = P.il by PBOOLE:143;
    il in NAT;
    then il in dom Q by PARTFUN1:def 2;
    then
B4: P.n = I by FUNCT_7:31;
    then IC Following(P,u) = i1 by A3,A4,SCMFSA_2:69;
      hence x in {IC Exec(goto i1,s)
       where s is Element of product the Object-Kind of SCM+FSA
       : IC s = il} by A2,A3,B4,A4;
    end;
    now
      assume x in {IC Exec(goto i1,s)
       where s is Element of product the Object-Kind of SCM+FSA
       : IC s = il};
      then ex s being Element of product the Object-Kind of SCM+FSA
       st x = IC Exec(goto i1,s) & IC s = il;
      hence x = i1 by SCMFSA_2:69;
    end;
    hence
    x in {i1} iff x in {IC Exec(goto i1,s)
       where s is Element of product the Object-Kind of SCM+FSA
     : IC s = il} by A1,TARSKI:def 1;
  end;
  hence thesis by TARSKI:1;
end;

theorem Th75:
  JUMP goto i1 = {i1}
proof
  set X = { NIC(goto i1, il) : not contradiction };
  now
    let x be set;
    hereby
      set il1 =  1;
A1:   NIC(goto i1, il1) in X;
      assume x in meet X;
      then x in NIC(goto i1, il1) by A1,SETFAM_1:def 1;
      hence x in {i1} by Th74;
    end;
    assume x in {i1};
    then
A2: x = i1 by TARSKI:def 1;
A3: now
      let Y be set;
      assume Y in X;
      then consider il being Element of NAT such that
A4:   Y = NIC(goto i1, il);
      NIC(goto i1, il) = {i1} by Th74;
      hence i1 in Y by A4,TARSKI:def 1;
    end;
    NIC(goto i1, i1) in X;
    hence x in meet X by A2,A3,SETFAM_1:def 1;
  end;
  hence thesis by TARSKI:1;
end;

registration
  let i1;
  cluster JUMP goto i1 -> 1-element;
  coherence
  proof
    JUMP goto i1 = {i1} by Th75;
    hence thesis;
  end;
end;

theorem Th76:
  NIC(a=0_goto i1, il) = {i1, succ il}
proof
  set t = the State of SCM+FSA,
  Q = the Instruction-Sequence of SCM+FSA;
  hereby
    let x be set;
    assume x in NIC(a=0_goto i1, il);
    then consider s being Element of product the Object-Kind of SCM+FSA
    such that
A1: x = IC Exec(a=0_goto i1,s) and
A2: IC s = il;
    per cases;
    suppose
      s.a = 0;
      then x = i1 by A1,SCMFSA_2:70;
      hence x in {i1, succ il} by TARSKI:def 2;
    end;
    suppose
      s.a <> 0;
      then x = succ il by A1,A2,SCMFSA_2:70;
      hence x in {i1, succ il} by TARSKI:def 2;
    end;
  end;
  let x be set;
    set I = a=0_goto i1;
A3: IC SCM+FSA <> a by SCMFSA_2:56;
  reconsider il1 = il as Element of ObjectKind IC SCM+FSA by MEMSTR_0:def 3;
      reconsider u = t+*(IC SCM+FSA,il1)
       as Element of product the Object-Kind of SCM+FSA by CARD_3:107;
      reconsider P = Q +* (il,I)
       as Instruction-Sequence of SCM+FSA;
    reconsider n = il as Element of NAT;
  assume
A5: x in {i1, succ il};
  per cases by A5,TARSKI:def 2;
  suppose
A6: x = i1;
    reconsider v = u+*(a .--> 0)
     as Element of product the Object-Kind of SCM+FSA by CARD_3:107;
xx: IC SCM+FSA in dom t by MEMSTR_0:2;
A7: dom (a .--> 0) = {a} by FUNCOP_1:13;
    then not IC SCM+FSA in dom (a .--> 0) by A3,TARSKI:def 1;
    then
A8: IC v = IC u by FUNCT_4:11
      .= n by xx,FUNCT_7:31;
B10:   P/.il = P.il by PBOOLE:143;
    il in NAT;
    then il in dom Q by PARTFUN1:def 2;
    then
A10: P.il = I by FUNCT_7:31;
    a in dom (a .--> 0) by A7,TARSKI:def 1;
    then v.a = (a .--> 0).a by FUNCT_4:13
      .= 0 by FUNCOP_1:72;
    then IC Following(P,v) = i1 by A8,A10,B10,SCMFSA_2:70;
    hence thesis by A6,A8,A10,B10;
  end;
  suppose
A11: x = succ il;
    reconsider v = u+*(a .--> 1)
     as Element of product the Object-Kind of SCM+FSA by CARD_3:107;
xx: IC SCM+FSA in dom t by MEMSTR_0:2;
A12: dom (a .--> 1) = {a} by FUNCOP_1:13;
    then not IC SCM+FSA in dom (a .--> 1) by A3,TARSKI:def 1;
    then
A13: IC v = IC u by FUNCT_4:11
      .= n by xx,FUNCT_7:31;
B15:   P/.il = P.il by PBOOLE:143;
    il in NAT;
    then il in dom Q by PARTFUN1:def 2;
    then
A15: P.il = I by FUNCT_7:31;
    a in dom (a .--> 1) by A12,TARSKI:def 1;
    then v.a = (a .--> 1).a by FUNCT_4:13
      .= 1 by FUNCOP_1:72;
    then IC Following(P,v) = succ il by A13,A15,B15,SCMFSA_2:70;
    hence thesis by A11,A13,A15,B15;
  end;
end;

theorem Th77:
  JUMP (a=0_goto i1) = {i1}
proof
  set X = { NIC(a=0_goto i1, il) : not contradiction };
  now
    let x be set;
A1: now
      let Y be set;
      assume Y in X;
      then consider il being Element of NAT such that
A2:   Y = NIC(a=0_goto i1, il);
      NIC(a=0_goto i1, il) = {i1, succ il} by Th76;
      hence i1 in Y by A2,TARSKI:def 2;
    end;
    hereby
      set il1 =  1, il2 =  2;
      assume
A3:   x in meet X;
A4:   NIC(a=0_goto i1, il2) = {i1, succ il2} by Th76;
      NIC(a=0_goto i1, il2) in X;
      then x in NIC(a=0_goto i1, il2) by A3,SETFAM_1:def 1;
      then
A5:   x = i1 or x = succ il2 by A4,TARSKI:def 2;
A6:   NIC(a=0_goto i1, il1) = {i1, succ il1} by Th76;
      NIC(a=0_goto i1, il1) in X;
      then x in NIC(a=0_goto i1, il1) by A3,SETFAM_1:def 1;
      then x = i1 or x = succ il1 by A6,TARSKI:def 2;
      hence x in {i1} by A5,TARSKI:def 1;
    end;
    assume x in {i1};
    then
A7: x = i1 by TARSKI:def 1;
    NIC(a=0_goto i1, i1) in X;
    hence x in meet X by A7,A1,SETFAM_1:def 1;
  end;
  hence thesis by TARSKI:1;
end;

registration
  let a, i1;
  cluster JUMP (a =0_goto i1) -> 1-element;
  coherence
  proof
    JUMP (a =0_goto i1) = {i1} by Th77;
    hence thesis;
  end;
end;

theorem Th78:
  NIC(a>0_goto i1, il) = {i1, succ il}
proof
  set t = the State of SCM+FSA,
  Q = the Instruction-Sequence of SCM+FSA;
  hereby
    let x be set;
    assume x in NIC(a>0_goto i1, il);
    then consider s being Element of product the Object-Kind of SCM+FSA
    such that
A1: x = IC Exec(a>0_goto i1,s) and
A2: IC s = il;
    per cases;
    suppose
      s.a > 0;
      then x = i1 by A1,SCMFSA_2:71;
      hence x in {i1, succ il} by TARSKI:def 2;
    end;
    suppose
      s.a <= 0;
      then x = succ il by A1,A2,SCMFSA_2:71;
      hence x in {i1, succ il} by TARSKI:def 2;
    end;
  end;
  let x be set;
   set I = a>0_goto i1;
A3: IC SCM+FSA <> a by SCMFSA_2:56;
  reconsider il1 = il as Element of ObjectKind IC SCM+FSA by MEMSTR_0:def 3;
  reconsider n = il as Element of NAT;
      reconsider u = t+*(IC SCM+FSA,il1)
       as Element of product the Object-Kind of SCM+FSA by CARD_3:107;
      reconsider P = Q +* (il,I) as Instruction-Sequence of SCM+FSA;
  assume
A5: x in {i1, succ il};
  per cases by A5,TARSKI:def 2;
  suppose
A6: x = i1;
    reconsider  v = u+*(a .--> 1)
       as Element of product the Object-Kind of SCM+FSA by CARD_3:107;
xx: IC SCM+FSA in dom t by MEMSTR_0:2;
A7: dom (a .--> 1) = {a} by FUNCOP_1:13;
    then not IC SCM+FSA in dom (a .--> 1) by A3,TARSKI:def 1;
    then
A8: IC v = IC u by FUNCT_4:11
      .= n by xx,FUNCT_7:31;
B10:   P/.il = P.il by PBOOLE:143;
    il in NAT;
    then il in dom Q by PARTFUN1:def 2;
    then
A10: P.il = I by FUNCT_7:31;
    a in dom (a .--> 1) by A7,TARSKI:def 1;
    then v.a = (a .--> 1).a by FUNCT_4:13
      .= 1 by FUNCOP_1:72;
    then IC Following(P,v) = i1 by A8,A10,B10,SCMFSA_2:71;
    hence thesis by A6,A8,A10,B10;
  end;
  suppose
A11: x = succ il;
    reconsider v = u+*(a .--> 0)
      as Element of product the Object-Kind of SCM+FSA by CARD_3:107;
xx: IC SCM+FSA in dom t by MEMSTR_0:2;
A12: dom (a .--> 0) = {a} by FUNCOP_1:13;
    then not IC SCM+FSA in dom (a .--> 0) by A3,TARSKI:def 1;
    then
A13: IC v = IC u by FUNCT_4:11
      .= n by xx,FUNCT_7:31;
B15:   P/.il = P.il by PBOOLE:143;
    il in NAT;
    then il in dom Q by PARTFUN1:def 2;
    then
A15: P.il = I by FUNCT_7:31;
    a in dom (a .--> 0) by A12,TARSKI:def 1;
    then v.a = (a .--> 0).a by FUNCT_4:13
      .= 0 by FUNCOP_1:72;
    then IC Following(P,v) = succ il by A13,A15,B15,SCMFSA_2:71;
    hence thesis by A11,A13,A15,B15;
  end;
end;

theorem Th79:
  JUMP (a>0_goto i1) = {i1}
proof
  set X = { NIC(a>0_goto i1, il) : not contradiction };
  now
    let x be set;
A1: now
      let Y be set;
      assume Y in X;
      then consider il being Element of NAT such that
A2:   Y = NIC(a>0_goto i1, il);
      NIC(a>0_goto i1, il) = {i1, succ il} by Th78;
      hence i1 in Y by A2,TARSKI:def 2;
    end;
    hereby
      set il1 =  1, il2 =  2;
      assume
A3:   x in meet X;
A4:   NIC(a>0_goto i1, il2) = {i1, succ il2} by Th78;
      NIC(a>0_goto i1, il2) in X;
      then x in NIC(a>0_goto i1, il2) by A3,SETFAM_1:def 1;
      then
A5:   x = i1 or x = succ il2 by A4,TARSKI:def 2;
A6:   NIC(a>0_goto i1, il1) = {i1, succ il1} by Th78;
      NIC(a>0_goto i1, il1) in X;
      then x in NIC(a>0_goto i1, il1) by A3,SETFAM_1:def 1;
      then x = i1 or x = succ il1 by A6,TARSKI:def 2;
      hence x in {i1} by A5,TARSKI:def 1;
    end;
    assume x in {i1};
    then
A7: x = i1 by TARSKI:def 1;
    NIC(a>0_goto i1, i1) in X;
    hence x in meet X by A7,A1,SETFAM_1:def 1;
  end;
  hence thesis by TARSKI:1;
end;

registration
  let a, i1;
  cluster JUMP (a >0_goto i1) -> 1-element;
  coherence
  proof
    JUMP (a >0_goto i1) = {i1} by Th79;
    hence thesis;
  end;
end;

registration
  let a, b, f;
  cluster a:=(f,b) -> sequential;
  coherence
  proof
    let s be State of SCM+FSA;
    thus thesis by SCMFSA_2:72;
  end;
end;

registration
  let a, b, f;
  cluster JUMP (a:=(f,b)) -> empty;
  coherence
  proof
    for l being Element of NAT holds NIC(a:=(f,b),l)={
    succ l} by AMISTD_1:12;
    hence thesis by Lm3;
  end;
end;

registration
  let a, b, f;
  cluster (f,b):=a -> sequential;
  coherence
  proof
    let s be State of SCM+FSA;
    thus thesis by SCMFSA_2:73;
  end;
end;

registration
  let a, b, f;
  cluster JUMP ((f,b):=a) -> empty;
  coherence
  proof
    for l being Element of NAT holds NIC((f,b):=a,l)={
    succ l} by AMISTD_1:12;
    hence thesis by Lm3;
  end;
end;

registration
  let a, f;
  cluster a:=len f -> sequential;
  coherence
  proof
    let s be State of SCM+FSA;
    thus thesis by SCMFSA_2:74;
  end;
end;

registration
  let a, f;
  cluster JUMP (a:=len f) -> empty;
  coherence
  proof
    for l being Element of NAT holds NIC(a:=len f,l)={
    succ l} by AMISTD_1:12;
    hence thesis by Lm3;
  end;
end;

registration
  let a, f;
  cluster f:=<0,...,0>a -> sequential;
  coherence
  proof
    let s be State of SCM+FSA;
    thus thesis by SCMFSA_2:75;
  end;
end;

registration
  let a, f;
  cluster JUMP (f:=<0,...,0>a) -> empty;
  coherence
  proof
    for l being Element of NAT holds NIC(f:=<0,...,0>a,l)
    ={succ l} by AMISTD_1:12;
    hence thesis by Lm3;
  end;
end;

theorem Th84:
  SUCC(il,SCM+FSA) = {il, succ il}
proof
  set X = { NIC(I, il) \ JUMP I where I is Element of the Instructions of
  SCM+FSA: not contradiction };
  set N = {il, succ il};
  now
    let x be set;
    hereby
      assume x in union X;
      then consider Y being set such that
A1:   x in Y and
A2:   Y in X by TARSKI:def 4;
      consider i being Element of the Instructions of SCM+FSA such that
A3:   Y = NIC(i, il) \ JUMP i by A2;
      per cases by SCMFSA_2:93;
      suppose
        i = [0,{},{}];
        then x in {il} \ JUMP halt SCM+FSA
        by A1,A3,AMISTD_1:2,AMI_3:26,SCMFSA_2:96;
        then x = il by TARSKI:def 1;
        hence x in N by TARSKI:def 2;
      end;
      suppose
        ex a,b st i = a:=b;
        then consider a, b such that
A4:     i = a:=b;
        x in {succ il} \ JUMP (a:=b) by A1,A3,A4,AMISTD_1:12;
        then x = succ il by TARSKI:def 1;
        hence x in N by TARSKI:def 2;
      end;
      suppose
        ex a,b st i = AddTo(a,b);
        then consider a, b such that
A5:     i = AddTo(a,b);
        x in {succ il} \ JUMP AddTo(a,b) by A1,A3,A5,AMISTD_1:12;
        then x = succ il by TARSKI:def 1;
        hence x in N by TARSKI:def 2;
      end;
      suppose
        ex a,b st i = SubFrom(a,b);
        then consider a, b such that
A6:     i = SubFrom(a,b);
        x in {succ il} \ JUMP SubFrom(a,b) by A1,A3,A6,AMISTD_1:12;
        then x = succ il by TARSKI:def 1;
        hence x in N by TARSKI:def 2;
      end;
      suppose
        ex a,b st i = MultBy(a,b);
        then consider a, b such that
A7:     i = MultBy(a,b);
        x in {succ il} \ JUMP MultBy(a,b) by A1,A3,A7,AMISTD_1:12;
        then x = succ il by TARSKI:def 1;
        hence x in N by TARSKI:def 2;
      end;
      suppose
        ex a,b st i = Divide(a,b);
        then consider a, b such that
A8:     i = Divide(a,b);
        x in {succ il} \ JUMP Divide(a,b) by A1,A3,A8,AMISTD_1:12;
        then x = succ il by TARSKI:def 1;
        hence x in N by TARSKI:def 2;
      end;
      suppose
        ex i1 st i = goto i1;
        then consider i1 such that
A9:     i = goto i1;
        x in {i1} \ JUMP i by A1,A3,A9,Th74;
        then x in {i1} \ {i1} by A9,Th75;
        hence x in N by XBOOLE_1:37;
      end;
      suppose
        ex i1,a st i = a=0_goto i1;
        then consider i1, a such that
A10:    i = a=0_goto i1;
A11:    NIC(i, il) = {i1, succ il} by A10,Th76;
        x in NIC(i, il) by A1,A3,XBOOLE_0:def 5;
        then
A12:    x = i1 or x = succ il by A11,TARSKI:def 2;
        x in NIC(i, il) \ {i1} by A1,A3,A10,Th77;
        then not x in {i1} by XBOOLE_0:def 5;
        hence x in N by A12,TARSKI:def 1,def 2;
      end;
      suppose
        ex i1,a st i = a>0_goto i1;
        then consider i1, a such that
A13:    i = a>0_goto i1;
A14:    NIC(i, il) = {i1, succ il} by A13,Th78;
        x in NIC(i, il) by A1,A3,XBOOLE_0:def 5;
        then
A15:    x = i1 or x = succ il by A14,TARSKI:def 2;
        x in NIC(i, il) \ {i1} by A1,A3,A13,Th79;
        then not x in {i1} by XBOOLE_0:def 5;
        hence x in N by A15,TARSKI:def 1,def 2;
      end;
      suppose
        ex a,b,f st i = b:=(f,a);
        then consider a, b, f such that
A16:    i = b:=(f,a);
        x in {succ il} \ JUMP (b:=(f,a)) by A1,A3,A16,AMISTD_1:12;
        then x = succ il by TARSKI:def 1;
        hence x in N by TARSKI:def 2;
      end;
      suppose
        ex a,b,f st i = (f,a):=b;
        then consider a, b, f such that
A17:    i = (f,a):=b;
        x in {succ il} \ JUMP ((f,a):=b) by A1,A3,A17,AMISTD_1:12;
        then x = succ il by TARSKI:def 1;
        hence x in N by TARSKI:def 2;
      end;
      suppose
        ex a,f st i = a:=len f;
        then consider a, f such that
A18:    i = a:=len f;
        x in {succ il} \ JUMP (a:=len f) by A1,A3,A18,AMISTD_1:12;
        then x = succ il by TARSKI:def 1;
        hence x in N by TARSKI:def 2;
      end;
      suppose
        ex a,f st i = f:=<0,...,0>a;
        then consider a, f such that
A19:    i = f:=<0,...,0>a;
        x in {succ il} \ JUMP (f:=<0,...,0>a) by A1,A3,A19,AMISTD_1:12;
        then x = succ il by TARSKI:def 1;
        hence x in N by TARSKI:def 2;
      end;
    end;
    assume
A20: x in {il, succ il};
    per cases by A20,TARSKI:def 2;
    suppose
A21:  x = il;
      set i = halt SCM+FSA;
      NIC(i, il) \ JUMP i = {il} by AMISTD_1:2;
      then
A22:  {il} in X;
      x in {il} by A21,TARSKI:def 1;
      hence x in union X by A22,TARSKI:def 4;
    end;
    suppose
A23:  x = succ il;
      set a = the Int-Location;
      set i = AddTo(a,a);
      NIC(i, il) \ JUMP i = {succ il} by AMISTD_1:12;
      then
A24:  {succ il} in X;
      x in {succ il} by A23,TARSKI:def 1;
      hence x in union X by A24,TARSKI:def 4;
    end;
  end;
  hence thesis by TARSKI:1;
end;

theorem Th85:
  for k being Element of NAT holds k+1 in SUCC(k,SCM+FSA) &
   for j being Element of NAT st j in SUCC(k,SCM+FSA) holds k <= j
proof
  let k be Element of NAT;
A1: SUCC(k,SCM+FSA) = {k, succ k} by Th84;
  hence k+1 in SUCC(k,SCM+FSA) by TARSKI:def 2;
  let j be Element of NAT;
  assume
A2: j in SUCC(k,SCM+FSA);
  per cases by A1,A2,TARSKI:def 2;
  suppose
    j = k;
    hence thesis;
  end;
  suppose
    j = succ k;
    hence thesis by NAT_1:11;
  end;
end;

registration
  cluster SCM+FSA -> standard;
  coherence by Th85,AMISTD_1:3;
end;

registration
  cluster InsCode halt SCM+FSA -> jump-only for InsType of SCM+FSA;
  coherence
  proof
    now
      let s be State of SCM+FSA, o be Object of SCM+FSA, I be Instruction of
      SCM+FSA;
      assume that
A1:   InsCode I = InsCode halt SCM+FSA and
      o in Data-Locations SCM+FSA;
      I = halt SCM+FSA by A1,SCMFSA_2:95,97;
      hence Exec(I, s).o = s.o by EXTPRO_1:def 3;
    end;
    hence thesis by AMISTD_1:def 1;
  end;
end;

registration
  cluster halt SCM+FSA -> jump-only;
  coherence
  proof
    thus InsCode halt SCM+FSA is jump-only;
  end;
end;

registration
  let i1;
  cluster InsCode goto i1 -> jump-only for InsType of SCM+FSA;
  coherence
  proof
    set S = SCM+FSA;
    now
      let s be State of S, o be Object of S, I be Instruction of S;
      assume that
A1:   InsCode I = InsCode goto i1 and
A2:   o in Data-Locations S;
A3:   ex i2 st I = goto i2 by A1,SCMFSA_2:23,35;
      per cases by A2,Th7;
      suppose
        o is Int-Location;
        hence Exec(I, s).o = s.o by A3,SCMFSA_2:69;
      end;
      suppose
        o is FinSeq-Location;
        hence Exec(I, s).o = s.o by A3,SCMFSA_2:69;
      end;
    end;
    hence thesis by AMISTD_1:def 1;
  end;
end;

registration
  let i1;
  cluster goto i1 -> jump-only non sequential non ins-loc-free;
  coherence
  proof
    thus InsCode goto i1 is jump-only;
    JUMP goto i1 <> {};
    hence goto i1 is non sequential by AMISTD_1:13;
    dom JumpPart goto i1 = dom <*i1*> by RECDEF_2:def 2
      .= {1} by FINSEQ_1:2,def 8;
   hence JumpPart goto i1 is not empty;
  end;
end;

registration
  let a, i1;
  cluster InsCode (a =0_goto i1) -> jump-only for InsType of SCM+FSA;
  coherence
  proof
    set S = SCM+FSA;
    now
      let s be State of S, o be Object of S, I be Instruction of S;
      assume that
A1:   InsCode I = InsCode (a =0_goto i1) and
A2:   o in Data-Locations S;
A3:   ex i2, b st I = b =0_goto i2 by A1,SCMFSA_2:24,36;
      per cases by A2,Th7;
      suppose
        o is Int-Location;
        hence Exec(I, s).o = s.o by A3,SCMFSA_2:70;
      end;
      suppose
        o is FinSeq-Location;
        hence Exec(I, s).o = s.o by A3,SCMFSA_2:70;
      end;
    end;
    hence thesis by AMISTD_1:def 1;
  end;
  cluster InsCode (a >0_goto i1) -> jump-only for InsType of SCM+FSA;
  coherence
  proof
    set S = SCM+FSA;
    now
      let s be State of S, o be Object of S, I be Instruction of S;
      assume that
A4:   InsCode I = InsCode (a >0_goto i1) and
A5:   o in Data-Locations S;
A6:   ex i2, b st I = b >0_goto i2 by A4,SCMFSA_2:25,37;
      per cases by A5,Th7;
      suppose
        o is Int-Location;
        hence Exec(I, s).o = s.o by A6,SCMFSA_2:71;
      end;
      suppose
        o is FinSeq-Location;
        hence Exec(I, s).o = s.o by A6,SCMFSA_2:71;
      end;
    end;
    hence thesis by AMISTD_1:def 1;
  end;
end;

registration
  let a, i1;
  cluster a =0_goto i1 -> jump-only non sequential non ins-loc-free;
  coherence
  proof
    thus InsCode (a =0_goto i1) is jump-only;
    JUMP (a =0_goto i1) <> {};
    hence a =0_goto i1 is non sequential by AMISTD_1:13;
    dom JumpPart (a =0_goto i1) = dom <*i1*> by Th24
      .= {1} by FINSEQ_1:2,38;
   hence JumpPart(a =0_goto i1) is not empty;
  end;
  cluster a >0_goto i1 -> jump-only non sequential non ins-loc-free;
  coherence
  proof
    thus InsCode (a >0_goto i1) is jump-only;
    JUMP (a >0_goto i1) <> {};
    hence a >0_goto i1 is non sequential by AMISTD_1:13;
    dom JumpPart (a >0_goto i1) = dom <*i1*> by Th25
      .= {1} by FINSEQ_1:2,38;
   hence JumpPart(a >0_goto i1) is not empty;
  end;
end;

Lm4: intloc 0 <> intloc 1 by AMI_3:10;

registration
  let a, b;
  cluster InsCode (a:=b) -> non jump-only for InsType of SCM+FSA;
  coherence
  proof
    set w = the State of SCM+FSA;
    set t = w+*((intloc 0, intloc 1)-->(0,1));
A1: InsCode (a:=b) = 1 by SCMFSA_2:18
      .= InsCode (intloc 0:=intloc 1) by SCMFSA_2:18;
A2: dom ((intloc 0, intloc 1)-->(0,1)) = {intloc 0, intloc 1} by FUNCT_4:62;
    then
A3: intloc 1 in dom ((intloc 0, intloc 1)-->(0,1)) by TARSKI:def 2;
    intloc 0 in dom ((intloc 0, intloc 1)-->(0,1)) by A2,TARSKI:def 2;
    then
A4: t.intloc 0 = (intloc 0, intloc 1)-->(0,1).intloc 0 by FUNCT_4:13
      .= 0 by AMI_3:10,FUNCT_4:63;
    intloc 0 in Int-Locations by SCMFSA_2:def 4;
    then
A5: intloc 0 in Data-Locations SCM+FSA by SCMFSA_2:100,XBOOLE_0:def 3;
    Exec((intloc 0:=intloc 1), t).intloc 0 = t.intloc 1 by SCMFSA_2:63
      .= (intloc 0, intloc 1)-->(0,1).intloc 1 by A3,FUNCT_4:13
      .= 1 by FUNCT_4:63;
    hence thesis by A1,A4,A5,AMISTD_1:def 1;
  end;
  cluster InsCode AddTo(a,b) -> non jump-only for InsType of SCM+FSA;
  coherence
  proof
    set w = the State of SCM+FSA;
    set t = w+*((intloc 0, intloc 1)-->(0,1));
A6: InsCode AddTo(a,b) = 2 by SCMFSA_2:19
      .= InsCode AddTo(intloc 0, intloc 1) by SCMFSA_2:19;
A7: dom ((intloc 0, intloc 1)-->(0,1)) = {intloc 0, intloc 1} by FUNCT_4:62;
    then intloc 0 in dom ((intloc 0, intloc 1)-->(0,1)) by TARSKI:def 2;
    then
A8: t.intloc 0 = (intloc 0, intloc 1)-->(0,1).intloc 0 by FUNCT_4:13
      .= 0 by AMI_3:10,FUNCT_4:63;
    intloc 0 in Int-Locations by SCMFSA_2:def 4;
    then
A9: intloc 0 in Data-Locations SCM+FSA by SCMFSA_2:100,XBOOLE_0:def 3;
    intloc 1 in dom ((intloc 0, intloc 1)-->(0,1)) by A7,TARSKI:def 2;
    then t.intloc 1 = (intloc 0, intloc 1)-->(0,1).intloc 1 by FUNCT_4:13
      .= 1 by FUNCT_4:63;
    then Exec(AddTo(intloc 0, intloc 1), t).intloc 0
     = (0 qua Nat)+1 by A8,SCMFSA_2:64;
    hence thesis by A6,A8,A9,AMISTD_1:def 1;
  end;
  cluster InsCode SubFrom(a,b) -> non jump-only for InsType of SCM+FSA;
  coherence
  proof
    set w = the State of SCM+FSA;
    set t = w+*((intloc 0, intloc 1)-->(0,1));
A10: InsCode SubFrom(a,b) = 3 by SCMFSA_2:20
      .= InsCode SubFrom(intloc 0, intloc 1) by SCMFSA_2:20;
A11: dom ((intloc 0, intloc 1)-->(0,1)) = {intloc 0, intloc 1} by FUNCT_4:62;
    then intloc 0 in dom ((intloc 0, intloc 1)-->(0,1)) by TARSKI:def 2;
    then
A12: t.intloc 0 = (intloc 0, intloc 1)-->(0,1).intloc 0 by FUNCT_4:13
      .= 0 by AMI_3:10,FUNCT_4:63;
    intloc 0 in Int-Locations by SCMFSA_2:def 4;
    then
A13: intloc 0 in Data-Locations SCM+FSA by SCMFSA_2:100,XBOOLE_0:def 3;
    intloc 1 in dom ((intloc 0, intloc 1)-->(0,1)) by A11,TARSKI:def 2;
    then
A14: t.intloc 1 = (intloc 0, intloc 1)-->(0,1).intloc 1 by FUNCT_4:13
      .= 1 by FUNCT_4:63;
    Exec(SubFrom(intloc 0, intloc 1), t).intloc 0 = t.intloc 0 - t.intloc
    1 by SCMFSA_2:65
      .= -1 by A12,A14;
    hence thesis by A10,A12,A13,AMISTD_1:def 1;
  end;
  cluster InsCode MultBy(a,b) -> non jump-only for InsType of SCM+FSA;
  coherence
  proof
    set w = the State of SCM+FSA;
    set t = w+*((intloc 0, intloc 1)-->(1,0));
A15: InsCode MultBy(a,b) = 4 by SCMFSA_2:21
      .= InsCode MultBy(intloc 0, intloc 1) by SCMFSA_2:21;
A16: dom ((intloc 0, intloc 1)-->(1,0)) = {intloc 0, intloc 1} by FUNCT_4:62;
    then intloc 0 in dom ((intloc 0, intloc 1)-->(1,0)) by TARSKI:def 2;
    then
A17: t.intloc 0 = (intloc 0, intloc 1)-->(1,0).intloc 0 by FUNCT_4:13
      .= 1 by AMI_3:10,FUNCT_4:63;
    intloc 0 in Int-Locations by SCMFSA_2:def 4;
    then
A18: intloc 0 in Data-Locations SCM+FSA by SCMFSA_2:100,XBOOLE_0:def 3;
    intloc 1 in dom ((intloc 0, intloc 1)-->(1,0)) by A16,TARSKI:def 2;
    then
A19: t.intloc 1 = (intloc 0, intloc 1)-->(1,0).intloc 1 by FUNCT_4:13
      .= 0 by FUNCT_4:63;
    Exec(MultBy(intloc 0, intloc 1), t).intloc 0 = t.intloc 0 * t.intloc
    1 by SCMFSA_2:66
      .= 0 by A19;
    hence thesis by A15,A17,A18,AMISTD_1:def 1;
  end;
  cluster InsCode Divide(a,b) -> non jump-only for InsType of SCM+FSA;
  coherence
  proof
    set w = the State of SCM+FSA;
    set t = w+*((intloc 0, intloc 1)-->(7,3));
A20: InsCode Divide(a,b) = 5 by SCMFSA_2:22
      .= InsCode Divide(intloc 0, intloc 1) by SCMFSA_2:22;
A21: dom ((intloc 0, intloc 1)-->(7,3)) = {intloc 0, intloc 1} by FUNCT_4:62;
    then intloc 0 in dom ((intloc 0, intloc 1)-->(7,3)) by TARSKI:def 2;
    then
A22: t.intloc 0 = (intloc 0, intloc 1)-->(7,3).intloc 0 by FUNCT_4:13
      .= 7 by AMI_3:10,FUNCT_4:63;
A23: 7 = 2 * 3 + 1;
    intloc 0 in Int-Locations by SCMFSA_2:def 4;
    then
A24: intloc 0 in Data-Locations SCM+FSA by SCMFSA_2:100,XBOOLE_0:def 3;
    intloc 1 in dom ((intloc 0, intloc 1)-->(7,3)) by A21,TARSKI:def 2;
    then t.intloc 1 = (intloc 0, intloc 1)-->(7,3).intloc 1 by FUNCT_4:13
      .= 3 by FUNCT_4:63;
    then Exec(Divide(intloc 0, intloc 1), t).intloc 0 = 7 div (3 qua Element
    of NAT) by A22,Lm4,SCMFSA_2:67
      .= 2 by A23,NAT_D:def 1;
    hence thesis by A20,A22,A24,AMISTD_1:def 1;
  end;
end;

Lm5: fsloc 0 <> intloc 0 by SCMFSA_2:99;

Lm6: fsloc 0 <> intloc 1 by SCMFSA_2:99;

registration
  let a, b, f;
  cluster InsCode (b:=(f,a)) -> non jump-only for InsType of SCM+FSA;
  coherence
  proof
    ObjectKind intloc 1 = INT by SCMFSA_2:11;
    then reconsider E = 1 as Element of ObjectKind intloc 1 by INT_1:def 1;
    ObjectKind intloc 0 = INT by SCMFSA_2:11;
    then reconsider D = 1 as Element of ObjectKind intloc 0 by INT_1:def 1;
    reconsider DWA = 2 as Element of INT by INT_1:def 1;
    set w = the State of SCM+FSA;
    <*DWA*> in INT* by FINSEQ_1:def 11;
    then reconsider F = <*2*> as Element of ObjectKind fsloc 0 by SCMFSA_2:12;
    reconsider t = w+*(fsloc 0 .--> F)+*(intloc 0 .--> D)+*(intloc 1 .--> E)
         as State of SCM+FSA;
A1: t.intloc 0 = D by AMI_3:10,BVFUNC14:12;
A2: t.fsloc 0 = F by Lm5,Lm6,FUNCT_7:114;
    then dom (t.fsloc 0) = {1} by FINSEQ_1:2,def 8;
    then
A3: 1 in dom (t.fsloc 0) by TARSKI:def 1;
    consider k being Element of NAT such that
A4: k = abs( t.intloc 1 ) and
A5: Exec((intloc 0):=(fsloc 0, intloc 1), t).intloc 0 = (t.fsloc 0)
    /. k by SCMFSA_2:72;
    intloc 0 in Int-Locations by SCMFSA_2:def 4;
    then
A6: intloc 0 in Data-Locations SCM+FSA by SCMFSA_2:100,XBOOLE_0:def 3;
    t.intloc 1 = E by FUNCT_7:94;
    then k = 1 by A4,ABSVALUE:def 1;
    then
A7: Exec(intloc 0:=(fsloc 0, intloc 1), t).intloc 0 = (t.fsloc 0).1 by A5,A3,
PARTFUN1:def 6
      .= 2 by A2,FINSEQ_1:def 8;
    InsCode (b:=(f,a)) = 9 by SCMFSA_2:26
      .= InsCode ((intloc 0):=(fsloc 0, intloc 1)) by SCMFSA_2:26;
    hence thesis by A1,A7,A6,AMISTD_1:def 1;
  end;
  cluster InsCode ((f,a):=b) -> non jump-only for InsType of SCM+FSA;
  coherence
  proof
    ObjectKind intloc 0 = INT by SCMFSA_2:11;
    then reconsider D = 1 as Element of ObjectKind intloc 0 by INT_1:def 1;
    reconsider DWA = 2 as Element of INT by INT_1:def 1;
    set w = the State of SCM+FSA;
A8: InsCode ((f,a):=b) = 10 by SCMFSA_2:27
      .= InsCode ((fsloc 0, intloc 1):=(intloc 0)) by SCMFSA_2:27;
    ObjectKind intloc 1 = INT by SCMFSA_2:11;
    then reconsider E = 1 as Element of ObjectKind intloc 1 by INT_1:def 1;
    <*DWA*> in INT* by FINSEQ_1:def 11;
    then reconsider F = <*2*> as Element of ObjectKind fsloc 0 by SCMFSA_2:12;
    reconsider t = w+*(fsloc 0 .--> F)+*(intloc 0 .--> D)+*(intloc 1 .--> E)
       as State of SCM+FSA;
    consider k being Element of NAT such that
A9: k = abs( t.intloc 1 ) and
A10: Exec((fsloc 0, intloc 1):=(intloc 0), t).fsloc 0 = (t.fsloc 0) +*
    (k,t.intloc 0) by SCMFSA_2:73;
    t.intloc 1 = E by FUNCT_7:94;
    then
A11: k = 1 by A9,ABSVALUE:def 1;
    fsloc 0 in FinSeq-Locations by SCMFSA_2:def 5;
    then
A12: fsloc 0 in Data-Locations SCM+FSA by SCMFSA_2:100,XBOOLE_0:def 3;
A13: F <> <*D*> by FINSEQ_1:76;
A14: t.fsloc 0 = F by Lm5,Lm6,FUNCT_7:114;
    t.intloc 0 = D by AMI_3:10,BVFUNC14:12;
    then Exec((fsloc 0, intloc 1):=intloc 0, t).fsloc 0 = <*D*> by A14,A10,A11,
FUNCT_7:95;
    hence thesis by A8,A14,A13,A12,AMISTD_1:def 1;
  end;
end;

registration
  let a, b, f;
  cluster b:=(f,a) -> non jump-only;
  coherence
  proof
    thus InsCode (b:=(f,a)) is not jump-only;
  end;
  cluster (f,a):=b -> non jump-only;
  coherence
  proof
    thus InsCode ((f,a):=b) is not jump-only;
  end;
end;

registration
  let a, f;
  cluster InsCode (a:=len f) -> non jump-only for InsType of SCM+FSA;
  coherence
  proof
    ObjectKind intloc 0 = INT by SCMFSA_2:11;
    then reconsider D = 3 as Element of ObjectKind intloc 0 by INT_1:def 1;
    reconsider DWA = 2 as Element of INT by INT_1:def 1;
    set w = the State of SCM+FSA;
A1: InsCode (a:=len f) = 11 by SCMFSA_2:28
      .= InsCode (intloc 0:=len fsloc 0) by SCMFSA_2:28;
    <*DWA*> in INT* by FINSEQ_1:def 11;
    then reconsider F = <*2*> as Element of ObjectKind fsloc 0 by SCMFSA_2:12;
    reconsider t = w+*(fsloc 0 .--> F)+*(intloc 0 .--> D) as State of SCM+FSA;
A2: t.fsloc 0 = F by BVFUNC14:12,SCMFSA_2:99;
    intloc 0 in Int-Locations by SCMFSA_2:def 4;
    then
A3: intloc 0 in Data-Locations SCM+FSA by SCMFSA_2:100,XBOOLE_0:def 3;
A4: t.intloc 0 = D by FUNCT_7:94;
    Exec(intloc 0 :=len fsloc 0, t).intloc 0 = len (t.fsloc 0) by SCMFSA_2:74
      .= 1 by A2,FINSEQ_1:39;
    hence thesis by A1,A4,A3,AMISTD_1:def 1;
  end;
  cluster InsCode (f:=<0,...,0>a) -> non jump-only for InsType of SCM+FSA;
  coherence
  proof
    ObjectKind intloc 0 = INT by SCMFSA_2:11;
    then reconsider D = 1 as Element of ObjectKind intloc 0 by INT_1:def 1;
    reconsider DWA = 2 as Element of INT by INT_1:def 1;
    set w = the State of SCM+FSA;
    <*DWA*> in INT* by FINSEQ_1:def 11;
    then reconsider F = <*2*> as Element of ObjectKind fsloc 0 by SCMFSA_2:12;
    reconsider t = w+*(fsloc 0 .--> F)+*(intloc 0 .--> D) as State of SCM+FSA;
A5: t.fsloc 0 = F by BVFUNC14:12,SCMFSA_2:99;
A6: F <> <*0*> by FINSEQ_1:76;
    consider k being Element of NAT such that
A7: k = abs( t.intloc 0 ) and
A8: Exec(fsloc 0:=<0,...,0>intloc 0, t).fsloc 0 = k |-> 0 by SCMFSA_2:75;
    fsloc 0 in FinSeq-Locations by SCMFSA_2:def 5;
    then
A9: fsloc 0 in Data-Locations SCM+FSA by SCMFSA_2:100,XBOOLE_0:def 3;
    t.intloc 0 = D by FUNCT_7:94;
    then k = 1 by A7,ABSVALUE:def 1;
    then
A10: Exec(fsloc 0:=<0,...,0>intloc 0, t).fsloc 0 = <*0*> by A8,FINSEQ_2:59;
    InsCode (f:=<0,...,0>a) = 12 by SCMFSA_2:29
      .= InsCode (fsloc 0:=<0,...,0>intloc 0) by SCMFSA_2:29;
    hence thesis by A5,A6,A10,A9,AMISTD_1:def 1;
  end;
end;

registration
  let a, f;
  cluster a:=len f -> non jump-only;
  coherence
  proof
    thus InsCode (a:=len f) is not jump-only;
  end;
  cluster f:=<0,...,0>a -> non jump-only;
  coherence
  proof
    thus InsCode (f:=<0,...,0>a) is not jump-only;
  end;
end;

registration
  cluster SCM+FSA -> homogeneous with_explicit_jumps;
  coherence
  proof
    thus SCM+FSA is homogeneous
    proof
      let I, J be Instruction of SCM+FSA such that
A1:   InsCode I = InsCode J;
A2:   J = [0,{},{}] or (ex a,b st J = a:=b) or (ex a,b st J = AddTo(a,b)) or
(ex a,b st J = SubFrom(a,b)) or (ex a,b st J = MultBy(a,b)) or (ex a,b st J =
Divide(a,b)) or (ex i1 st J = goto i1) or (ex i1,a st J = a=0_goto i1) or (ex
i1,a st J = a>0_goto i1) or (ex b,a,f st J = a:=(f,b)) or (ex a,b,f st J = (f,a
):=b) or (ex a,f st J = a:=len f) or (ex a,f st J = f:=<0,...,0>a)
    by SCMFSA_2:93;
      per cases by SCMFSA_2:93;
      suppose I = [0,{},{}];
        hence thesis by A1,A2,AMI_3:26,SCMFSA_2:23,24,25,96,97;
      end;
      suppose
        ex a,b st I = a:=b;
        then consider a, b such that
A3:     I = a:=b;
A4:     InsCode I = 1 by A3,SCMFSA_2:18;
        now
          per cases by SCMFSA_2:93;
          suppose
            J = [0,{},{}];
            hence thesis by A1,A3,AMI_3:26,SCMFSA_2:96;
          end;
          suppose
            ex a,b st J = a:=b;
            then consider d1, d2 such that
A5:         J = d1:=d2;
            thus dom JumpPart I = dom {} by A3,Th18
              .= dom JumpPart J by A5,Th18;
          end;
          suppose
            (ex a,b st J = AddTo(a,b)) or (ex a,b st J = SubFrom(a,b)
) or (ex a,b st J = MultBy(a,b)) or (ex a,b st J = Divide(a,b)) or (ex i1 st J
= goto i1) or (ex i1,a st J = a=0_goto i1) or (ex i1,a st J = a>0_goto i1) or (
ex b,a,f st J = a:=(f,b)) or (ex a,b,f st J = (f,a):=b) or (ex a,f st J = a
            :=len f) or (ex a,f st J = f:=<0,...,0>a);
            hence thesis
             by A1,A4,SCMFSA_2:19,20,21,22,23,24,25,26,27,28,29;
          end;
        end;
        hence thesis;
      end;
      suppose
        ex a,b st I = AddTo(a,b);
        then consider a, b such that
A6:     I = AddTo(a,b);
A7:     InsCode I = 2 by A6,SCMFSA_2:19;
          per cases by SCMFSA_2:93;
          suppose
            J = [0,{},{}];
            hence thesis by A1,A6,AMI_3:26,SCMFSA_2:96;
          end;
          suppose
            ex a,b st J = AddTo(a,b);
            then consider d1, d2 such that
A8:         J = AddTo(d1,d2);
            thus dom JumpPart I = dom {} by A6,Th19
              .= dom JumpPart J by A8,Th19;
          end;
          suppose
            (ex a,b st J = a:=b) or (ex a,b st J = SubFrom(a,b)) or (
ex a,b st J = MultBy(a,b)) or (ex a,b st J = Divide(a,b)) or (ex i1 st J = goto
i1) or (ex i1,a st J = a=0_goto i1) or (ex i1,a st J = a>0_goto i1) or (ex b,a,
f st J = a:=(f,b)) or (ex a,b,f st J = (f,a):=b) or (ex a,f st J = a:=len f) or
            (ex a,f st J = f:=<0,...,0>a);
            hence thesis
             by A1,A7,SCMFSA_2:18,20,21,22,23,24,25,26,27,28,29;
          end;
      end;
      suppose
        ex a,b st I = SubFrom(a,b);
        then consider a, b such that
A9:     I = SubFrom(a,b);
A10:    InsCode I = 3 by A9,SCMFSA_2:20;
          per cases by SCMFSA_2:93;
          suppose
            J = [0,{},{}];
            hence thesis by A1,A9,AMI_3:26,SCMFSA_2:96;
          end;
          suppose
            ex a,b st J = SubFrom(a,b);
            then consider d1, d2 such that
A11:        J = SubFrom(d1,d2);
            thus dom JumpPart I = dom {} by A9,Th20
              .= dom JumpPart J by A11,Th20;
          end;
          suppose
            (ex a,b st J = a:=b) or (ex a,b st J = AddTo(a,b)) or (ex
a,b st J = MultBy(a,b)) or (ex a,b st J = Divide(a,b)) or (ex i1 st J = goto i1
) or (ex i1,a st J = a=0_goto i1) or (ex i1,a st J = a>0_goto i1) or (ex b,a,f
st J = a:=(f,b)) or (ex a,b,f st J = (f,a):=b) or (ex a,f st J = a:=len f) or
            (ex a,f st J = f:=<0,...,0>a);
            hence thesis
             by A1,A10,SCMFSA_2:18,19,21,22,23,24,25,26,27,28,29;
          end;
      end;
      suppose
        ex a,b st I = MultBy(a,b);
        then consider a, b such that
A12:    I = MultBy(a,b);
A13:    InsCode I = 4 by A12,SCMFSA_2:21;
          per cases by SCMFSA_2:93;
          suppose
            J = [0,{},{}];
            hence thesis by A1,A12,AMI_3:26,SCMFSA_2:96;
          end;
          suppose
            ex a,b st J = MultBy(a,b);
            then consider d1, d2 such that
A14:        J = MultBy(d1,d2);
            thus dom JumpPart I = dom {} by A12,Th21
              .= dom JumpPart J by A14,Th21;
          end;
          suppose
            (ex a,b st J = a:=b) or (ex a,b st J = AddTo(a,b)) or (ex
a,b st J = SubFrom(a,b)) or (ex a,b st J = Divide(a,b)) or (ex i1 st J = goto
i1) or (ex i1,a st J = a=0_goto i1) or (ex i1,a st J = a>0_goto i1) or (ex b,a,
f st J = a:=(f,b)) or (ex a,b,f st J = (f,a):=b) or (ex a,f st J = a:=len f) or
            (ex a,f st J = f:=<0,...,0>a);
            hence thesis
             by A1,A13,SCMFSA_2:18,19,20,22,23,24,25,26,27,28,29;
          end;
      end;
      suppose
        ex a,b st I = Divide(a,b);
        then consider a, b such that
A15:    I = Divide(a,b);
A16:    InsCode I = 5 by A15,SCMFSA_2:22;
          per cases by SCMFSA_2:93;
          suppose
            J = [0,{},{}];
            hence thesis by A1,A15,AMI_3:26,SCMFSA_2:96;
          end;
          suppose
            ex a,b st J = Divide(a,b);
            then consider d1, d2 such that
A17:        J = Divide(d1,d2);
            thus dom JumpPart I = dom {} by A15,Th22
              .= dom JumpPart J by A17,Th22;
          end;
          suppose
            (ex a,b st J = a:=b) or (ex a,b st J = AddTo(a,b)) or (ex
a,b st J = SubFrom(a,b)) or (ex a,b st J = MultBy(a,b)) or (ex i1 st J = goto
i1) or (ex i1,a st J = a=0_goto i1) or (ex i1,a st J = a>0_goto i1) or (ex b,a,
f st J = a:=(f,b)) or (ex a,b,f st J = (f,a):=b) or (ex a,f st J = a:=len f) or
            (ex a,f st J = f:=<0,...,0>a);
            hence thesis
             by A1,A16,SCMFSA_2:18,19,20,21,23,24,25,26,27,28,29;
          end;
      end;
      suppose
        ex i1 st I = goto i1;
        then consider i1 such that
A18:    I = goto i1;
A19:    InsCode I = 6 by A18,SCMFSA_2:23;
          per cases by SCMFSA_2:93;
          suppose
            J = [0,{},{}];
            hence thesis by A1,A18,AMI_3:26,SCMFSA_2:23,96,97;
          end;
          suppose
            ex i2 st J = goto i2;
            then consider i2 such that
A20:        J = goto i2;
            thus dom JumpPart I = dom <*i1*> by A18,RECDEF_2:def 2
              .= Seg 1 by FINSEQ_1:def 8
              .= dom <*i2*> by FINSEQ_1:def 8
              .= dom JumpPart J by A20,RECDEF_2:def 2;
          end;
          suppose
            (ex a,b st J = a:=b) or (ex a,b st J = AddTo(a,b)) or (ex
a,b st J = SubFrom(a,b)) or (ex a,b st J = MultBy(a,b)) or (ex a,b st J =
Divide(a,b)) or (ex i1,a st J = a=0_goto i1) or (ex i1,a st J = a>0_goto i1) or
(ex b,a,f st J = a:=(f,b)) or (ex a,b,f st J = (f,a):=b) or (ex a,f st J = a
            :=len f) or (ex a,f st J = f:=<0,...,0>a);
            hence thesis
             by A1,A19,SCMFSA_2:18,19,20,21,22,24,25,26,27,28,29;
          end;
      end;
      suppose
        ex i1,a st I = a=0_goto i1;
        then consider a, i1 such that
A21:    I = a=0_goto i1;
A22:    InsCode I = 7 by A21,SCMFSA_2:24;
          per cases by SCMFSA_2:93;
          suppose
            J = [0,{},{}];
            hence thesis by A1,A21,AMI_3:26,SCMFSA_2:24,96,97;
          end;
          suppose
            ex i2,d1 st J = d1 =0_goto i2;
            then consider d1, i2 such that
A23:        J = d1 =0_goto i2;
            thus dom JumpPart I = dom <*i1*> by A21,Th24
              .= Seg 1 by FINSEQ_1:38
              .= dom <*i2*> by FINSEQ_1:38
              .= dom JumpPart J by A23,Th24;
          end;
          suppose
            (ex a,b st J = a:=b) or (ex a,b st J = AddTo(a,b)) or (ex
a,b st J = SubFrom(a,b)) or (ex a,b st J = MultBy(a,b)) or (ex a,b st J =
Divide(a,b)) or (ex i1 st J = goto i1) or (ex i1,a st J = a>0_goto i1) or (ex b
,a,f st J = a:=(f,b)) or (ex a,b,f st J = (f,a):=b) or (ex a,f st J = a:=len f)
            or (ex a,f st J = f:=<0,...,0>a);
            hence thesis
             by A1,A22,SCMFSA_2:18,19,20,21,22,23,25,26,27,28,29;
          end;
      end;
      suppose
        ex i1,a st I = a>0_goto i1;
        then consider a, i1 such that
A24:    I = a>0_goto i1;
A25:    InsCode I = 8 by A24,SCMFSA_2:25;
          per cases by SCMFSA_2:93;
          suppose
            J = [0,{},{}];
            hence thesis by A1,A24,AMI_3:26,SCMFSA_2:25,96,97;
          end;
          suppose
            ex i2,d1 st J = d1 >0_goto i2;
            then consider d1, i2 such that
A26:        J = d1 >0_goto i2;
            thus dom JumpPart I = dom <*i1*> by A24,Th25
              .= Seg 1 by FINSEQ_1:38
              .= dom <*i2*> by FINSEQ_1:38
              .= dom JumpPart J by A26,Th25;
          end;
          suppose
            (ex a,b st J = a:=b) or (ex a,b st J = AddTo(a,b)) or (ex
a,b st J = SubFrom(a,b)) or (ex a,b st J = MultBy(a,b)) or (ex a,b st J =
Divide(a,b)) or (ex i1 st J = goto i1) or (ex i1,a st J = a=0_goto i1) or (ex b
,a,f st J = a:=(f,b)) or (ex a,b,f st J = (f,a):=b) or (ex a,f st J = a:=len f)
            or (ex a,f st J = f:=<0,...,0>a);
            hence thesis
             by A1,A25,SCMFSA_2:18,19,20,21,22,23,24,26,27,28,29;
          end;
      end;
      suppose
        ex a,b,f st I = b:=(f,a);
        then consider a, b, f such that
A27:    I = b:=(f,a);
A28:    InsCode I = 9 by A27,SCMFSA_2:26;
          per cases by SCMFSA_2:93;
          suppose
            J = [0,{},{}];
            hence thesis by A1,A27,AMI_3:26,SCMFSA_2:96;
          end;
          suppose
            ex a,b,f st J = b:=(f,a);
            then consider d1, d2, f1 such that
A29:        J = d2:=(f1,d1);
            thus dom JumpPart I = dom {} by A27,RECDEF_2:def 2
              .= dom JumpPart J by A29,RECDEF_2:def 2;
          end;
          suppose
            (ex a,b st J = a:=b) or (ex a,b st J = AddTo(a,b)) or (ex
a,b st J = SubFrom(a,b)) or (ex a,b st J = MultBy(a,b)) or (ex a,b st J =
Divide(a,b)) or (ex i1 st J = goto i1) or (ex i1,a st J = a=0_goto i1) or (ex
i1,a st J = a>0_goto i1) or (ex a,b,f st J = (f,a):=b) or (ex a,f st J = a:=len
            f) or (ex a,f st J = f:=<0,...,0>a);
            hence thesis
             by A1,A28,SCMFSA_2:18,19,20,21,22,23,24,25,27,28,29;
          end;
      end;
      suppose
        ex a,b,f st I = (f,a):=b;
        then consider a, b, f such that
A30:    I = (f,a):=b;
A31:    InsCode I = 10 by A30,SCMFSA_2:27;
          per cases by SCMFSA_2:93;
          suppose
            J = [0,{},{}];
            hence thesis by A1,A30,AMI_3:26,SCMFSA_2:96;
          end;
          suppose
            ex a,b,f st J = (f,a):=b;
            then consider d1, d2, f1 such that
A32:        J = (f1,d1):=d2;
            thus dom JumpPart I = dom {} by A30,RECDEF_2:def 2
              .= dom JumpPart J by A32,RECDEF_2:def 2;
          end;
          suppose
            (ex a,b st J = a:=b) or (ex a,b st J = AddTo(a,b)) or (ex
a,b st J = SubFrom(a,b)) or (ex a,b st J = MultBy(a,b)) or (ex a,b st J =
Divide(a,b)) or (ex i1 st J = goto i1) or (ex i1,a st J = a=0_goto i1) or (ex
i1,a st J = a>0_goto i1) or (ex a,b,f st J = b:=(f,a)) or (ex a,f st J = a:=len
            f) or (ex a,f st J = f:=<0,...,0>a);
            hence thesis
             by A1,A31,SCMFSA_2:18,19,20,21,22,23,24,25,26,28,29;
          end;
      end;
      suppose
        ex a,f st I = a:=len f;
        then consider a, f such that
A33:    I = a:=len f;
A34:    InsCode I = 11 by A33,SCMFSA_2:28;
          per cases by SCMFSA_2:93;
          suppose
            J = [0,{},{}];
            hence thesis by A1,A33,AMI_3:26,SCMFSA_2:96;
          end;
          suppose
            ex a,f st J = a:=len f;
            then consider d1, f1 such that
A35:        J = d1:=len f1;
            thus dom JumpPart I = dom {} by A33,RECDEF_2:def 2
              .= dom JumpPart J by A35,RECDEF_2:def 2;
          end;
          suppose
            (ex a,b st J = a:=b) or (ex a,b st J = AddTo(a,b)) or (ex
a,b st J = SubFrom(a,b)) or (ex a,b st J = MultBy(a,b)) or (ex a,b st J =
Divide(a,b)) or (ex i1 st J = goto i1) or (ex i1,a st J = a=0_goto i1) or (ex
i1,a st J = a>0_goto i1) or (ex a,b,f st J = b:=(f,a)) or (ex a,b,f st J = (f,a
            ):=b) or (ex a,f st J = f:=<0,...,0>a);
            hence thesis
             by A1,A34,SCMFSA_2:18,19,20,21,22,23,24,25,26,27,29;
          end;
      end;
      suppose
        ex a,f st I = f:=<0,...,0>a;
        then consider a, f such that
A36:    I = f:=<0,...,0>a;
A37:    InsCode I = 12 by A36,SCMFSA_2:29;
          per cases by SCMFSA_2:93;
          suppose
            J = [0,{},{}];
            hence thesis by A1,A36,AMI_3:26,SCMFSA_2:96;
          end;
          suppose
            ex a,f st J = f:=<0,...,0>a;
            then consider d1, f1 such that
A38:        J = f1:=<0,...,0>d1;
            thus dom JumpPart I = dom {} by A36,RECDEF_2:def 2
              .= dom JumpPart J by A38,RECDEF_2:def 2;
          end;
          suppose
            (ex a,b st J = a:=b) or (ex a,b st J = AddTo(a,b)) or (ex
a,b st J = SubFrom(a,b)) or (ex a,b st J = MultBy(a,b)) or (ex a,b st J =
Divide(a,b)) or (ex i1 st J = goto i1) or (ex i1,a st J = a=0_goto i1) or (ex
i1,a st J = a>0_goto i1) or (ex a,b,f st J = b:=(f,a)) or (ex a,b,f st J = (f,a
            ):=b) or (ex a,f st J = a:=len f);
            hence thesis
             by A1,A37,SCMFSA_2:18,19,20,21,22,23,24,25,26,27,28;
          end;
      end;
    end;
    thus SCM+FSA is with_explicit_jumps
    proof
      let I be Instruction of SCM+FSA;
     thus JUMP I c= rng JumpPart I
     proof
      let f be set such that
A39:  f in JUMP I;
      per cases by SCMFSA_2:93;
      suppose
        I = [0,{},{}];
        hence thesis by A39,AMI_3:26,SCMFSA_2:96;
      end;
      suppose
        ex a,b st I = a:=b;
        hence thesis by A39;
      end;
      suppose
        ex a,b st I = AddTo(a,b);
        hence thesis by A39;
      end;
      suppose
        ex a,b st I = SubFrom(a,b);
        hence thesis by A39;
      end;
      suppose
        ex a,b st I = MultBy(a,b);
        hence thesis by A39;
      end;
      suppose
        ex a,b st I = Divide(a,b);
        hence thesis by A39;
      end;
      suppose
A40:    ex i1 st I = goto i1;
        consider i1 such that
A41:    I = goto i1 by A40;
A42:    JumpPart goto i1 = <*i1*> by RECDEF_2:def 2;
      rng<*i1*> = {i1} by FINSEQ_1:39;
        hence thesis by A39,A41,A42,Th75;
      end;
      suppose
A44:    ex i1,a st I = a=0_goto i1;
        consider a, i1 such that
A45:    I = a=0_goto i1 by A44;
A46:    JumpPart (a=0_goto i1) = <*i1*> by Th24;
      rng<*i1*> = {i1} by FINSEQ_1:39;
        hence thesis by A39,A45,A46,Th77;
      end;
      suppose
A48:    ex i1,a st I = a>0_goto i1;
        consider a, i1 such that
A49:    I = a>0_goto i1 by A48;
A50:    JumpPart (a>0_goto i1) = <*i1*> by Th25;
      rng<*i1*> = {i1} by FINSEQ_1:39;
        hence thesis by A39,A49,A50,Th79;
      end;
      suppose
        ex a,b,f st I = b:=(f,a);
        hence thesis by A39;
      end;
      suppose
        ex a,b,f st I = (f,a):=b;
        hence thesis by A39;
      end;
      suppose
        ex a,f st I = a:=len f;
        hence thesis by A39;
      end;
      suppose
        ex a,f st I = f:=<0,...,0>a;
        hence thesis by A39;
      end;
    end;
    let f being set;
    assume f in rng JumpPart I;
    then consider k being set such that
A52: k in dom JumpPart I and
A53: f = (JumpPart I).k by FUNCT_1:def 3;
    per cases by SCMFSA_2:93;
    suppose
      I = [0,{},{}];
      then dom JumpPart I = dom {} by RECDEF_2:def 2;
      hence thesis by A52;
    end;
    suppose
      ex a,b st I = a:=b;
      then consider a, b such that
A54:  I = a:=b;
      k in dom {} by A52,A54,Th18;
      hence thesis;
    end;
    suppose
      ex a,b st I = AddTo(a,b);
      then consider a, b such that
A55:  I = AddTo(a,b);
      k in dom {} by A52,A55,Th19;
      hence thesis;
    end;
    suppose
      ex a,b st I = SubFrom(a,b);
      then consider a, b such that
A56:  I = SubFrom(a,b);
      k in dom {} by A52,A56,Th20;
      hence thesis;
    end;
    suppose
      ex a,b st I = MultBy(a,b);
      then consider a, b such that
A57:  I = MultBy(a,b);
      k in dom {} by A52,A57,Th21;
      hence thesis;
    end;
    suppose
      ex a,b st I = Divide(a,b);
      then consider a, b such that
A58:  I = Divide(a,b);
      k in dom {} by A52,A58,Th22;
      hence thesis;
    end;
    suppose
      ex i1 st I = goto i1;
      then consider i1 such that
A59:  I = goto i1;
A60:  JumpPart I = <*i1*> by A59,RECDEF_2:def 2;
      then k = 1 by A52,FINSEQ_1:90;
      then
A61:  f = i1 by A60,A53,FINSEQ_1:def 8;
      JUMP I = {i1} by A59,Th75;
      hence thesis by A61,TARSKI:def 1;
    end;
    suppose
      ex i1,a st I = a=0_goto i1;
      then consider a, i1 such that
A62:  I = a=0_goto i1;
A63:  JumpPart I = <*i1*> by A62,Th24;
      then k = 1 by A52,FINSEQ_1:90;
      then
A64:  f = i1 by A63,A53,FINSEQ_1:def 8;
      JUMP I = {i1} by A62,Th77;
      hence thesis by A64,TARSKI:def 1;
    end;
    suppose
      ex i1,a st I = a>0_goto i1;
      then consider a, i1 such that
A65:  I = a>0_goto i1;
A66:  JumpPart I = <*i1*> by A65,Th25;
      then k = 1 by A52,FINSEQ_1:90;
      then
A67:  f = i1 by A66,A53,FINSEQ_1:def 8;
      JUMP I = {i1} by A65,Th79;
      hence thesis by A67,TARSKI:def 1;
    end;
    suppose
      ex a,b,f st I = b:=(f,a);
      then consider a, b, f such that
A68:  I = b:=(f,a);
      k in dom {} by A52,A68,RECDEF_2:def 2;
      hence thesis;
    end;
    suppose
      ex a,b,f st I = (f,a):=b;
      then consider a, b, f such that
A69:  I = (f,a):=b;
      k in dom {} by A52,A69,RECDEF_2:def 2;
      hence thesis;
    end;
    suppose
      ex a,f st I = a:=len f;
      then consider a, f such that
A70:  I = a:=len f;
      k in dom {} by A52,A70,RECDEF_2:def 2;
      hence thesis;
    end;
    suppose
      ex a,f st I = f:=<0,...,0>a;
      then consider a, f such that
A71:  I = f:=<0,...,0>a;
      k in dom {} by A52,A71,RECDEF_2:def 2;
      hence thesis;
    end;
  end;
  end;
end;

registration
  cluster SCM+FSA -> regular J/A-independent;
  coherence
  proof
   thus SCM+FSA is regular
   proof
    let I be Instruction of SCM+FSA;
     set T = InsCode I;
A1:   JumpPart I in JumpParts T;
    per cases by Lm1;
    suppose T = 0;
      then JumpParts T = {{}} by Th30;
      then JumpPart I = {} by A1,TARSKI:def 1;
     hence thesis;
    end;
    suppose T = 1;
      then JumpParts T = {{}} by Th31;
      then JumpPart I = {} by A1,TARSKI:def 1;
     hence thesis;
    end;
    suppose T = 2;
      then JumpParts T = {{}} by Th32;
      then JumpPart I = {} by A1,TARSKI:def 1;
     hence thesis;
    end;
    suppose T = 3;
      then JumpParts T = {{}} by Th33;
      then JumpPart I = {} by A1,TARSKI:def 1;
     hence thesis;
    end;
    suppose T = 4;
      then JumpParts T = {{}} by Th34;
      then JumpPart I = {} by A1,TARSKI:def 1;
     hence thesis;
    end;
    suppose T = 5;
      then JumpParts T = {{}} by Th35;
      then JumpPart I = {} by A1,TARSKI:def 1;
     hence thesis;
    end;
    suppose
A2: T = 6;
     then consider i2 such that
A3:    I = goto i2 by SCMFSA_2:35;
     let k be set;
     assume
A4:      k in dom JumpPart I;
      for f being Function st f in JumpParts T holds k in dom f
       proof let f be Function;
        assume f in JumpParts T;
         then consider J being Instruction of SCM+FSA such that
A5:       f = JumpPart J and
A6:       InsCode J = T;
        thus k in dom f by A4,A5,A6,COMPOS_1:def 12;
       end;
      then k in dom product" JumpParts T by CARD_3:def 12;
      then k in {1} by A2,Th36;
      then k = 1 by TARSKI:def 1;
     hence (product" JumpParts InsCode I).k = NAT by A3,Th53;
    end;
    suppose
A7: T = 7;
     then consider i1,a such that
A8:    I = a=0_goto i1 by SCMFSA_2:36;
     let k be set;
     assume
A9:      k in dom JumpPart I;
      for f being Function st f in JumpParts T holds k in dom f
       proof let f be Function;
        assume f in JumpParts T;
         then consider J being Instruction of SCM+FSA such that
A10:       f = JumpPart J and
A11:       InsCode J = T;
        thus k in dom f by A9,A10,A11,COMPOS_1:def 12;
       end;
      then k in dom product" JumpParts T by CARD_3:def 12;
      then k in {1} by A7,Th37;
      then k = 1 by TARSKI:def 1;
     hence (product" JumpParts InsCode I).k = NAT by A8,Th54;
    end;
    suppose
A12: T = 8;
     then consider i1,a such that
A13:    I = a>0_goto i1 by SCMFSA_2:37;
     let k be set;
     assume
A14:      k in dom JumpPart I;
      for f being Function st f in JumpParts T holds k in dom f
       proof let f be Function;
        assume f in JumpParts T;
         then consider J being Instruction of SCM+FSA such that
A15:       f = JumpPart J and
A16:       InsCode J = T;
        thus k in dom f by A14,A15,A16,COMPOS_1:def 12;
       end;
      then k in dom product" JumpParts T by CARD_3:def 12;
      then k in {1} by A12,Th38;
      then k = 1 by TARSKI:def 1;
     hence (product" JumpParts InsCode I).k = NAT by A13,Th56;
    end;
    suppose T = 9;
      then JumpParts T = {{}} by Th39;
      then JumpPart I = {} by A1,TARSKI:def 1;
     hence thesis;
    end;
    suppose T = 10;
      then JumpParts T = {{}} by Th40;
      then JumpPart I = {} by A1,TARSKI:def 1;
     hence thesis;
    end;
    suppose T = 11;
      then JumpParts T = {{}} by Th41;
      then JumpPart I = {} by A1,TARSKI:def 1;
     hence thesis;
    end;
    suppose T = 12;
      then JumpParts T = {{}} by Th42;
      then JumpPart I = {} by A1,TARSKI:def 1;
     hence thesis;
    end;
   end;
   let T be InsType of SCM+FSA,
       f1,f2 be Function, p be set such that
A17: f1 in JumpParts T and
A18: f2 in product product" JumpParts T and
A19: [T,f1,p] in the Instructions of SCM+FSA;
    per cases by Lm1;
    suppose T = 0 or T = 1 or T = 2 or T = 3 or T = 4 or T = 5
       or T = 9 or T = 10 or T = 11 or T = 12;
     then
A20:  JumpParts T = {0} by Th30,Th31,Th32,Th33,Th34,Th35,Th39,Th40,Th41,Th42;
     then f1 = 0 by A17,TARSKI:def 1 .= f2 by A18,A20,CARD_3:10,106
,TARSKI:def 1;
    hence [T,f2,p] in the Instructions of SCM+FSA by A19;
    end;
    suppose
A21:    T = 6;
    reconsider J = [T,f1,p] as Instruction of SCM+FSA by A19;
A22: InsCode J = 6 by A21,RECDEF_2:def 1;
    then consider i1 being Element of NAT such that
A23:  J = goto i1 by SCMFSA_2:35;
A24:   p = {} by A23,MCART_1:25;
A25: dom f2 = dom product" JumpParts T  by A18,CARD_3:9;
A26: dom f2 = {1} by A21,A25,Th36;
    then 1 in dom f2 by TARSKI:def 1;
    then f2.1 in (product" JumpParts T).1 by A25,A18,CARD_3:9;
    then reconsider l = f2.1 as Element of NAT by A23,A21,A22,Th53;
    set I = [T,f2,{}];
    I = goto l by A21,A26,FINSEQ_1:2,def 8;
    then  reconsider I as Instruction of SCM+FSA;
    f2 = JumpPart I by RECDEF_2:def 2;
    hence [T,f2,p] in the Instructions of SCM+FSA by A24;
    end;
    suppose
A27:    T = 7;
    reconsider J = [T,f1,p] as Instruction of SCM+FSA by A19;
A28: InsCode J = T by RECDEF_2:def 1;
    then consider i1,a such that
A29:  J = a=0_goto i1 by A27,SCMFSA_2:36;
    consider A being Data-Location such that
A30:  a = A & a=0_goto i1 = A=0_goto i1 by SCMFSA_2:def 14;
A31:   p = <*a*> by A29,A30,MCART_1:25;
A32: dom f2 = dom product" JumpParts T  by A18,CARD_3:9;
A33: dom f2 = {1} by A27,A32,Th37;
    then 1 in dom f2 by TARSKI:def 1;
    then f2.1 in (product" JumpParts T).1 by A32,A18,CARD_3:9;
    then reconsider l = f2.1 as Element of NAT by A29,A28,Th54;
    set I = [T,f2,p];
    f2 = <*l*> by A33,FINSEQ_1:2,def 8;
    then I = a=0_goto l by A27,A31,Th15;
    then reconsider I as Instruction of SCM+FSA;
A34: InsCode I = T by RECDEF_2:def 1;
    consider i2,b such that
A35: I = b=0_goto i2 by A27,A34,SCMFSA_2:36;
    thus [T,f2,p] in the Instructions of SCM+FSA by A35;
    end;
    suppose
A36:    T = 8;
    reconsider J = [T,f1,p] as Instruction of SCM+FSA by A19;
A37: InsCode J = T by RECDEF_2:def 1;
    then consider i1,a such that
A38:  J = a>0_goto i1 by A36,SCMFSA_2:37;
    consider A being Data-Location such that
A39:  a = A & a>0_goto i1 = A>0_goto i1 by SCMFSA_2:def 15;
A40:   p = <*a*> by A38,A39,MCART_1:25;
A41: dom f2 = dom product" JumpParts T  by A18,CARD_3:9;
A42: dom f2 = {1} by A36,A41,Th38;
    then 1 in dom f2 by TARSKI:def 1;
    then f2.1 in (product" JumpParts T).1 by A41,A18,CARD_3:9;
    then reconsider l = f2.1 as Element of NAT by A38,A37,Th56;
    set I = [T,f2,p];
    f2 = <*l*> by A42,FINSEQ_1:2,def 8;
    then I = a>0_goto l by A36,A40,Th16;
    then reconsider I as Instruction of SCM+FSA;
A43: InsCode I = T by RECDEF_2:def 1;
    consider i2,b such that
A44: I = b>0_goto i2 by A36,A43,SCMFSA_2:37;
    thus [T,f2,p] in the Instructions of SCM+FSA by A44;
    end;
  end;
end;

theorem Th88:
  IncAddr(goto i1,k) = goto (i1+ k)
proof
A1: InsCode IncAddr(goto i1,k) = InsCode goto i1 by COMPOS_1:def 17
    .= 6 by SCMFSA_2:23
    .= InsCode goto (i1+k) by SCMFSA_2:23;
A2: AddressPart IncAddr(goto i1,k) = AddressPart goto i1 by COMPOS_1:def 17
    .= {} by RECDEF_2:def 3
    .= AddressPart goto (i1+k) by RECDEF_2:def 3;
A3: JumpPart IncAddr(goto i1,k) = k + JumpPart goto i1
 by COMPOS_1:def 17;
    then
A4: dom JumpPart IncAddr(goto i1,k) = dom JumpPart goto i1
   by VALUED_1:def 2;
A5: for x being set st x in dom JumpPart goto i1 holds (JumpPart
  IncAddr(goto i1,k)).x = (JumpPart goto (i1+k)).x
  proof
    let x be set;
    assume
A6: x in dom JumpPart goto i1;
    then x in dom <*i1*> by RECDEF_2:def 2;
    then
A7: x = 1 by FINSEQ_1:90;
    set f = (JumpPart goto i1).x;
A8: (JumpPart IncAddr(goto i1,k)).x = k+f by A4,A3,A6,VALUED_1:def 2;
    f = <*i1*>.x by RECDEF_2:def 2
      .= i1 by A7,FINSEQ_1:def 8;
    hence
    (JumpPart IncAddr(goto i1,k)).x =
     <*(i1+k)*>.
    x by A7,A8,FINSEQ_1:def 8
      .= (JumpPart goto (i1+k)).x by RECDEF_2:def 2;
  end;
  dom JumpPart goto (i1+k)
   = dom <*(i1+k)*> by RECDEF_2:def 2
    .= Seg 1 by FINSEQ_1:def 8
    .= dom <*i1*> by FINSEQ_1:def 8
    .= dom JumpPart goto i1 by RECDEF_2:def 2;
   then JumpPart IncAddr(goto i1,k) = JumpPart goto(i1+k) by A4,A5,FUNCT_1:2;
  hence thesis by A1,A2,COMPOS_1:1;
end;

theorem Th89:
  IncAddr(a=0_goto i1,k) = a=0_goto (i1+k)
proof
A1: InsCode IncAddr(a=0_goto i1,k) = InsCode (a=0_goto i1) by COMPOS_1:def 17
    .= 7 by SCMFSA_2:24
    .= InsCode (a=0_goto (i1+k)) by SCMFSA_2:24;
A2: a=0_goto i1 = [7, <* i1*>,<*a *>] by Th15;
A3: a=0_goto(i1+k) = [7, <* i1+k*>,<*a *>] by Th15;
A4: AddressPart IncAddr(a=0_goto i1,k)
     = AddressPart (a=0_goto i1) by COMPOS_1:def 17
    .= <*a*> by A2,RECDEF_2:def 3
    .= AddressPart (a=0_goto (i1+k)) by A3,RECDEF_2:def 3;
A5: JumpPart IncAddr(a=0_goto i1,k) = k + JumpPart (a=0_goto i1)
  by COMPOS_1:def 17;
  then
A6: dom JumpPart IncAddr(a=0_goto i1,k) = dom JumpPart (a=0_goto i1)
  by VALUED_1:def 2;
A7: for x being set st x in dom JumpPart (a=0_goto i1) holds (JumpPart
IncAddr(a=0_goto i1,k)).x = (JumpPart (a=0_goto (i1+k))).x
  proof
    let x be set;
    assume
A8: x in dom JumpPart (a=0_goto i1);
    then
    x in dom <*i1*> by Th24;
     then
A9:   x = 1 by FINSEQ_1:90;
      set f = (JumpPart (a=0_goto i1)).x;
A10:   (JumpPart IncAddr(a=0_goto i1,k)).x = k+f by A6,A5,A8,VALUED_1:def 2;
      f = <*i1*>.x by Th24
        .= i1 by A9,FINSEQ_1:40;
      hence
      (JumpPart IncAddr(a=0_goto i1,k)).x =
       <*(i1+k)*>.x by A9,A10,FINSEQ_1:40
        .= (JumpPart (a=0_goto (i1+k))).x
         by Th24;
  end;
  dom JumpPart (a=0_goto (i1+k))
   = dom <*(i1+k)*> by Th24
    .= Seg 1 by FINSEQ_1:38
    .= dom <*i1*> by FINSEQ_1:38
    .= dom JumpPart (a=0_goto i1) by Th24;
   then JumpPart IncAddr(a=0_goto i1,k) = JumpPart (a=0_goto (i1+k))
     by A6,A7,FUNCT_1:2;
  hence thesis by A1,A4,COMPOS_1:1;
end;

theorem Th90:
  IncAddr(a>0_goto i1,k) = a>0_goto (i1+k)
proof
A1: InsCode IncAddr(a>0_goto i1,k) = InsCode (a>0_goto i1) by COMPOS_1:def 17
    .= 8 by SCMFSA_2:25
    .= InsCode (a>0_goto (i1+k)) by SCMFSA_2:25;
A2: a>0_goto i1 = [8, <* i1*>,<*a *>] by Th16;
A3: a>0_goto(i1+k) = [8, <* i1+k*>,<*a *>] by Th16;
A4: AddressPart IncAddr(a>0_goto i1,k)
     = AddressPart (a>0_goto i1) by COMPOS_1:def 17
    .= <*a*> by A2,RECDEF_2:def 3
    .= AddressPart (a>0_goto (i1+k)) by A3,RECDEF_2:def 3;
A5:  JumpPart IncAddr(a>0_goto i1,k) = k + JumpPart (a>0_goto i1)
  by COMPOS_1:def 17;
  then
A6: dom JumpPart IncAddr(a>0_goto i1,k) = dom JumpPart (a>0_goto i1)
  by VALUED_1:def 2;
A7: for x being set st x in dom JumpPart (a>0_goto i1) holds (JumpPart
IncAddr(a>0_goto i1,k)).x = (JumpPart (a>0_goto (i1+k))).x
  proof
    let x be set;
    assume
A8: x in dom JumpPart (a>0_goto i1);
    then x in dom <*i1*> by Th25;
     then
A9:   x = 1 by FINSEQ_1:90;
    set f = (JumpPart (a>0_goto i1)).1;
A10:   (JumpPart IncAddr(a>0_goto i1,k)).1 = k+f by A9,A6,A5,A8,VALUED_1:def 2;
      f = <*i1*>.x by Th25,A9
        .= i1 by A9,FINSEQ_1:40;
      hence
      (JumpPart IncAddr(a>0_goto i1,k)).x
       = <*(i1+k)*>.x by A9,A10,FINSEQ_1:40
        .= (JumpPart (a>0_goto (i1+k))).x by Th25;
  end;
  dom JumpPart (a>0_goto (i1+k)) =
   dom <*(i1+k)*> by Th25
    .= Seg 1 by FINSEQ_1:38
    .= dom <*i1*> by FINSEQ_1:38
    .= dom JumpPart (a>0_goto i1) by Th25;
   then JumpPart IncAddr(a>0_goto i1,k) = JumpPart (a>0_goto (i1+k))
     by A6,A7,FUNCT_1:2;
  hence thesis by A1,A4,COMPOS_1:1;
end;

registration
  cluster SCM+FSA -> IC-relocable;
  coherence
  proof
   let I be Instruction of SCM+FSA;
      per cases by SCMFSA_2:93;
      suppose
        I = [0,{},{}];
        hence thesis by AMI_3:26,SCMFSA_2:96;
      end;
      suppose
        ex a,b st I = a:=b;
        hence thesis;
      end;
      suppose
        ex a,b st I = AddTo(a,b);
        hence thesis;
      end;
      suppose
        ex a,b st I = SubFrom(a,b);
        hence thesis;
      end;
      suppose
        ex a,b st I = MultBy(a,b);
        hence thesis;
      end;
      suppose
        ex a,b st I = Divide(a,b);
        hence thesis;
      end;
      suppose
A1:     ex i1 st I = goto i1;
        let j,k be Nat, s1 be State of SCM+FSA;
        set s2 = IncIC(s1,k);
        consider i1 such that
A2:     I = goto i1 by A1;
        thus IC Exec(IncAddr(I,j),s1) + k
         = IC Exec(goto(j+i1),s1) + k by A2,Th88
        .= j+i1+k by SCMFSA_2:69
        .= IC Exec(goto(j+k+i1),s2) by SCMFSA_2:69
        .= IC Exec(IncAddr(I,j+k), s2) by A2,Th88;
      end;
      suppose
        ex i1,a st I = a=0_goto i1;
        then consider a, i1 such that
A3:     I = a=0_goto i1;
        let j,k be natural number, s1 be State of SCM+FSA;
        set s2 = IncIC(s1,k);
        a <> IC SCM+FSA & dom (IC SCM+FSA .--> (IC s1 + k)) = {IC SCM+FSA}
        by FUNCOP_1:13,SCMFSA_2:56;
        then not a in dom (IC SCM+FSA .--> (IC s1 + k)) by TARSKI:def 1;
        then
A4:     s1.a = s2.a by FUNCT_4:11;
          per cases;
          suppose
A5:         s1.a = 0;
            thus IC Exec(IncAddr(I,j),s1) + k
         = IC Exec(a=0_goto(j+i1),s1) + k by A3,Th89
        .= j+i1+k by A5,SCMFSA_2:70
        .= IC Exec(a=0_goto(j+k+i1),s2) by A4,A5,SCMFSA_2:70
            .= IC Exec(IncAddr(I,j+k), s2) by A3,Th89;
          end;
          suppose
A6:         s1.a <> 0;
A7:         IncAddr(I,j) = a=0_goto(i1+j) by A3,Th89;
A8:         IncAddr(I,j+k) = a=0_goto(i1+(j+k)) by A3,Th89;
            dom (IC SCM+FSA .--> (IC s1 + k)) = {IC SCM+FSA} by FUNCOP_1:13;
            then IC SCM+FSA in dom (IC SCM+FSA .--> (IC s1 + k))
                 by TARSKI:def 1;
            then
A9:         IC s2 = (IC SCM+FSA .--> (IC s1 + k)).IC SCM+FSA by FUNCT_4:13
              .= (IC s1 + k) by FUNCOP_1:72;
            thus IC Exec(IncAddr(I,j),s1) + k
              = succ IC s1 + k by A7,A6,SCMFSA_2:70
             .= succ IC s2 by A9
             .= IC Exec(IncAddr(I,j+k), s2) by A8,A6,A4,SCMFSA_2:70;
          end;
      end;
      suppose
        ex i1,a st I = a>0_goto i1;
        then consider i1,a such that
A10:    I = a>0_goto i1;
        let j,k be natural number, s1 be State of SCM+FSA;
        set s2 = IncIC(s1,k);
        a <> IC SCM+FSA & dom (IC SCM+FSA .--> (IC s1 + k)) = {IC SCM+FSA}
        by FUNCOP_1:13,SCMFSA_2:56;
        then not a in dom (IC SCM+FSA .--> (IC s1 + k)) by TARSKI:def 1;
        then
A11:    s1.a = s2.a by FUNCT_4:11;
          per cases;
          suppose
A12:        s1.a > 0;
            thus IC Exec(IncAddr(I,j),s1) + k
         = IC Exec(a>0_goto(j+i1),s1) + k by A10,Th90
        .= j+i1+k by A12,SCMFSA_2:71
        .= IC Exec(a>0_goto(j+k+i1),s2) by A11,A12,SCMFSA_2:71
            .= IC Exec(IncAddr(I,j+k), s2) by A10,Th90;
          end;
          suppose
A13:         s1.a <= 0;
A14:         IncAddr(I,j) = a>0_goto(i1+j) by A10,Th90;
A15:         IncAddr(I,j+k) = a>0_goto(i1+(j+k)) by A10,Th90;
            dom (IC SCM+FSA .--> (IC s1 + k)) = {IC SCM+FSA} by FUNCOP_1:13;
            then IC SCM+FSA in dom (IC SCM+FSA .--> (IC s1 + k))
             by TARSKI:def 1;
            then
A16:         IC s2 = (IC SCM+FSA .--> (IC s1 + k)).IC SCM+FSA by FUNCT_4:13
              .= (IC s1 + k) by FUNCOP_1:72;
            thus IC Exec(IncAddr(I,j),s1) + k
              = succ IC s1 + k by A14,A13,SCMFSA_2:71
             .= succ IC s2 by A16
             .= IC Exec(IncAddr(I,j+k), s2) by A15,A13,A11,SCMFSA_2:71;
          end;
      end;
      suppose
        ex a,b,f st I = b:=(f,a);
        hence thesis;
      end;
      suppose
        ex a,b,f st I = (f,a):=b;
        hence thesis;
      end;
      suppose
        ex a,f st I = a:=len f;
        hence thesis;
      end;
      suppose
        ex a,f st I = f:=<0,...,0>a;
        hence thesis;
      end;
  end;
end;

