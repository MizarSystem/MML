:: Arrow's Impossibility Theorem
::  by Freek Wiedijk
::
:: Received August 13, 2007
:: Copyright (c) 2007 Association of Mizar Users

environ

 vocabularies NUMBERS, XBOOLE_0, SUBSET_1, FUNCT_1, FINSET_1, CARD_1, XXREAL_0,
      TARSKI, ARYTM_1, RELAT_1, ZFMISC_1, RELAT_2, ORDERS_1, WELLORD1, FUNCT_2,
      NAT_1, FINSEQ_1, ARYTM_3, ARROW;
 notations ORDERS_1, RELAT_1, RELAT_2, RELSET_1, XBOOLE_0, SUBSET_1, TARSKI,
      FINSET_1, FUNCT_1, FUNCT_2, CARD_1, XXREAL_0, ZFMISC_1, NAT_1, NUMBERS,
      FINSEQ_1, XCMPLX_0, WELLORD1;
 constructors XXREAL_0, FUNCT_2, REAL_1, FINSEQ_1, INT_1, RELAT_2, PARTFUN1,
      WELLORD1, RELSET_1;
 registrations RELSET_1, FINSET_1, NAT_1, INT_1, XREAL_0, XBOOLE_0, ORDINAL1;
 requirements BOOLE, SUBSET, NUMERALS, ARITHM, REAL;
 definitions RELAT_1, FUNCT_2, WELLORD1;
 theorems RELSET_1, ZFMISC_1, TARSKI, FUNCT_2, FINSEQ_4, FINSEQ_1, FINSEQ_3,
      NAT_1, ORDINAL1, XREAL_1, FUNCT_1, XXREAL_0, PARTFUN1, INT_1, CARD_2,
      CARD_1, XBOOLE_0, SUBSET_1, RELAT_2, RELAT_1, ORDERS_1, XBOOLE_1,
      WELLORD2, WELLORD1;
 schemes XBOOLE_0, FUNCT_2, NAT_1, RELSET_1;

begin :: Preliminaries

definition
  let A,B9 be non empty set;
  let B be non empty Subset of B9;
  let f be Function of A,B;
  let x be Element of A;
  redefine func f.x -> Element of B;
  coherence
  proof
    thus f.x is Element of B;
  end;
end;

theorem Th1:
  for A being finite set st card A >= 2 holds for a being Element of A holds
  ex b being Element of A st b <> a
proof
  let A9 be finite set;
  assume
A1: card A9 >= 2;
  then reconsider A = A9 as finite non empty set by CARD_1:47;
  let a be Element of A9;
 {a} c= A by ZFMISC_1:37;
then  card (A \ {a}) = card A - card {a} by CARD_2:63
    .= card A - 1 by CARD_1:50;
then  card (A \ {a}) <> 0 by A1;
  then consider b being set such that
A5: b in A \ {a} by CARD_1:47,XBOOLE_0:def 1;
  reconsider b as Element of A9 by A5;
  take b;
 not b in {a} by A5,XBOOLE_0:def 5;
  hence thesis by TARSKI:def 1;
end;

theorem Th2:
  for A being finite set st card A >= 3 holds for a,b being Element of A holds
  ex c being Element of A st c <> a & c <> b
proof
  let A9 be finite set;
  assume
A1: card A9 >= 3;
  then reconsider A = A9 as finite non empty set by CARD_1:47;
  let a,b be Element of A9;
 {a,b} c= A by ZFMISC_1:38;
then A3: card (A \ {a,b}) = card A - card {a,b} by CARD_2:63;
 card {a,b} <= 2 by CARD_2:69;
then  card (A \ {a,b}) >= 3 - 2 by A1,A3,XREAL_1:15;
then  card (A \ {a,b}) <> 0;
  then consider c being set such that
A7: c in A \ {a,b} by CARD_1:47,XBOOLE_0:def 1;
  reconsider c as Element of A9 by A7;
  take c;
 not c in {a,b} by A7,XBOOLE_0:def 5;
  hence thesis by TARSKI:def 2;
end;

begin :: Linear preorders and linear orders

reserve A for non empty set;
reserve a,b,c,x,y,z for Element of A;

definition
  let A;
  defpred P[set] means $1 is Relation of A &
  (for a,b holds [a,b] in $1 or [b,a] in $1) &
  (for a,b,c st [a,b] in $1 & [b,c] in $1 holds [a,c] in $1);
  defpred Q[set] means for R being set holds R in $1 iff P[R];
  func LinPreorders A means
  :Def1:
  for R being set holds R in it iff
  R is Relation of A & (for a,b holds [a,b] in R or [b,a] in R) &
  for a,b,c st [a,b] in R & [b,c] in R holds [a,c] in R;
  existence
  proof
    consider it0 being set such that
A1: for R being set holds R in it0 iff R in bool [:A,A:] & P[R]
    from XBOOLE_0:sch 1;
    take it0;
    let R be set;
    thus R in it0 implies P[R] by A1;
    assume
A2: P[R];
 [:A,A:] in bool [:A,A:] by ZFMISC_1:def 1;
    hence thesis by A1,A2;
  end;
  uniqueness
  proof
    let it1,it2 be set;
    assume that
A4: Q[it1] and
A5: Q[it2];
 now
      let R be set;
   R in it1 iff P[R] by A4;
      hence R in it1 iff R in it2 by A5;
    end;
    hence thesis by TARSKI:2;
  end;
end;

registration
  let A;
  cluster LinPreorders A -> non empty;
  coherence
  proof
 [:A,A:] c= [:A,A:];
    then reconsider R = [:A,A:] as Relation of A;
     (
 for a,b holds [a,b] in R or [b,a] in R)& for a,b,c st [a,b] in R & [b,c] in
    R holds [a,c] in R by ZFMISC_1:106;
    hence thesis by Def1;
  end;
end;

definition
  let A;
  defpred P[set] means for a,b st [a,b] in $1 & [b,a] in $1 holds a = b;
  defpred Q[set] means
  for R being Element of LinPreorders A holds R in $1 iff P[R];
  func LinOrders A -> Subset of LinPreorders A means
  :Def2:
  for R being Element of LinPreorders A holds R in it iff
  for a,b st [a,b] in R & [b,a] in R holds a = b;
  existence
  proof
    consider it0 being set such that
A1: for R being set holds R in it0 iff R in LinPreorders A & P[R]
    from XBOOLE_0:sch 1;
 for R being set st R in it0 holds R in LinPreorders A by A1;
    then reconsider it0 as Subset of LinPreorders A by TARSKI:def 3;
    take it0;
    let R be Element of LinPreorders A;
    thus R in it0 implies P[R] by A1;
    assume P[R];
    hence thesis by A1;
  end;
  uniqueness
  proof
    let it1,it2 be Subset of LinPreorders A;
    assume that
A4: Q[it1] and
A5: Q[it2];
 now
      let R be Element of LinPreorders A;
   R in it1 iff P[R] by A4;
      hence R in it1 iff R in it2 by A5;
    end;
    hence thesis by SUBSET_1:8;
  end;
end;

registration
  let A be set;
  cluster connected Order of A;
  existence
  proof
    consider R9 being Relation such that
A1: R9 well_orders A by WELLORD2:26;
    set R = R9 |_2 A;
A2: R is well-ordering by A1,WELLORD2:25;
    reconsider R as Relation of A by XBOOLE_1:17;
 now
      let a be set;
      assume
A4:   a in A;
   R9 is_reflexive_in A by A1,WELLORD1:def 5;
then A6:   [a,a] in R9 by A4,RELAT_2:def 1;
   [a,a] in [:A,A:] by A4,ZFMISC_1:def 2;
then    [a,a] in R by A6,XBOOLE_0:def 4;
      hence a in dom R by RELAT_1:def 4;
    end;
then  A c= dom R by TARSKI:def 3;
then  dom R = A by XBOOLE_0:def 10;
    then reconsider R as Order of A by A2,PARTFUN1:def 4,WELLORD1:def 4;
    take R;
    thus thesis by A2,WELLORD1:def 4;
  end;
end;

definition
  let A;
  redefine func LinOrders A means
  :Def3:
  for R being set holds R in it iff R is connected Order of A;
  compatibility
  proof
A1: now
      let R be set;
      assume
A2:   R in LinOrders A;
      then reconsider R9 = R as Relation of A by Def1;
   now
        let a be set;
        assume a in A;
then      [a,a] in R by A2,Def1;
        hence a in dom R9 by RELAT_1:def 4;
      end;
then    A c= dom R9 by TARSKI:def 3;
then A7:   dom R9 = A by XBOOLE_0:def 10;
   now
        let a be set;
        assume a in A;
then     [a,a] in R by A2,Def1;
        hence a in rng R9 by RELAT_1:def 5;
      end;
then   A c= rng R9 by TARSKI:def 3;
then A12:  field R9 = A \/ A by A7,XBOOLE_0:def 10;
  for a,b being set st a in A & b in A & a <> b
      holds [a,b] in R or [b,a] in R by A2,Def1;
then A14:  R9 is_connected_in A by RELAT_2:def 6;
  for a being set st a in A holds [a,a] in R by A2,Def1;
then A16:  R9 is_reflexive_in A by RELAT_2:def 1;
  for
 a,b being set st a in A & b in A & [a,b] in R & [b,a] in R holds a = b
      by A2,Def2;
then A18:  R9 is_antisymmetric_in A by RELAT_2:def 4;
        for
 a,b,c being set st a in A & b in A & c in A & [a,b] in R & [b,c] in
      R holds [a,c] in R by A2,Def1;
then   R9 is_transitive_in A by RELAT_2:def 8;
      hence R is connected Order of A
      by A7,A12,A14,A16,A18,PARTFUN1:def 4,RELAT_2:def 9,def 12,def 14
      ,def 16;
    end;
A21: now
      let R be set;
      assume
A22:  R is connected Order of A;
      then reconsider R9 = R as connected Order of A;
A23:  now
        let a,b;
    dom R9 = A by PARTFUN1:def 4;
then     A c= dom R9 \/ rng R9 by XBOOLE_1:7;
then A26:    field R9 = A by XBOOLE_0:def 10;
        A27:    R9
 is_connected_in field R9 & R9 is_reflexive_in field R9 by RELAT_2:def 9
        ,def 14;
    a = b or a <> b;
        hence [a,b] in R or [b,a] in R by A26,A27,RELAT_2:def 1,def 6;
      end;
  for a,b,c st [a,b] in R & [b,c] in R holds [a,c] in R by A22,
      ORDERS_1:14;
then A30:  R in LinPreorders A by A22,A23,Def1;
  for a,b st [a,b] in R & [b,a] in R holds a = b by A22,ORDERS_1:13;
      hence R in LinOrders A by A30,Def2;
    end;
    let it0 be Subset of LinPreorders A;
    thus it0 = LinOrders A implies
    for R being set holds R in it0 iff R is connected Order of A by A1,A21;
    assume
A32: for R being set holds R in it0 iff R is connected Order of A;
 now
      let R be set;
  R in it0 iff R is connected Order of A by A32;
      hence R in it0 iff R in LinOrders A by A1,A21;
    end;
    hence thesis by TARSKI:2;
  end;
end;

registration
  let A;
  cluster LinOrders A -> non empty;
  coherence
  proof
    consider R being connected Order of A;
 R in LinOrders A by Def3;
    hence thesis;
  end;
end;

registration let A;
 cluster -> Relation-like Element of LinPreorders A;
 coherence by Def1;
 cluster -> Relation-like Element of LinOrders A;
 coherence;
end;

reserve o,o9 for Element of LinPreorders A;
reserve o99 for Element of LinOrders A;

definition
  let o be Relation, a,b be set;
  pred a <=_o, b means
  :Def4:
  [a,b] in o;
end;

notation
  let o be Relation, a,b be set;
  synonym b >=_o, a for a <=_o, b;
  antonym b <_o, a for a <=_o, b;
  antonym a >_o, b for a <=_o, b;
end;

theorem Th3:
  a <=_o, a
proof
  thus [a,a] in o by Def1;
end;

theorem Th4:
  a <=_o, b or b <=_o, a
proof
 [a,b] in o or [b,a] in o by Def1;
  hence thesis by Def4;
end;

theorem Th5:
  (a <=_o, b or a <_o, b) & (b <=_ o, c or b <_o, c) implies a <=_o, c
proof
  assume a <=_o, b or a <_o, b;
then  a <=_o, b by Th4;
then A3: [a,b] in o by Def4;
  assume b <=_o, c or b <_o, c;
then  b <=_o, c by Th4;
then  [b,c] in o by Def4;
  hence [a,c] in o by A3,Def1;
end;

theorem Th6:
  a <=_o99, b & b <=_o99, a implies a = b
proof
 [a,b] in o99 & [b,a] in o99 implies a = b by Def2;
  hence thesis by Def4;
end;

theorem Th7:
  a <> b & b <> c & a <> c implies ex o st a <_o, b & b <_o, c
proof
  assume that
A1: a <> b & b <> c and
A2: a <> c;
  defpred P[set,set] means ($1 = a or $2 <> a) & ($1 <> c or $2 = c);
  consider R being Relation of A such that
A3: for x,y holds [x,y] in R iff P[x,y] from RELSET_1:sch 2;
A4: now
    let x,y;
 P[x,y] or P[y,x] by A2;
    hence [x,y] in R or [y,x] in R by A3;
  end;
 now
    let x,y,z;
    assume [x,y] in R & [y,z] in R;
then  ( P[x,y])& P[y,z] by A3;
    hence [x,z] in R by A3;
  end;
  then reconsider o = R as Element of LinPreorders A by A4,Def1;
  take o;
 ( not [b,a] in R)& not [c,b] in R by A1,A3;
  hence thesis by Def4;
end;

theorem Th8:
  ex o st for a st a <> b holds b <_o, a
proof
  defpred P[set,set] means $1 = b or $2 <> b;
  consider R being Relation of A such that
A1: for x,y holds [x,y] in R iff P[x,y] from RELSET_1:sch 2;
A2: now
    let x,y;
 P[x,y] or P[y,x];
    hence [x,y] in R or [y,x] in R by A1;
  end;
 now
    let x,y,z;
    assume that
A5: [x,y] in R and
A6: [y,z] in R;
 P[y,z] by A1,A6;
    hence [x,z] in R by A1,A5;
  end;
  then reconsider o = R as Element of LinPreorders A by A2,Def1;
  take o;
  let a;
  assume a <> b;
then  not [a,b] in R by A1;
  hence thesis by Def4;
end;

theorem Th9:
  ex o st for a st a <> b holds a <_o, b
proof
  defpred P[set,set] means $1 <> b or $2 = b;
  consider R being Relation of A such that
A1: for x,y holds [x,y] in R iff P[x,y] from RELSET_1:sch 2;
A2: now
    let x,y;
 P[x,y] or P[y,x];
    hence [x,y] in R or [y,x] in R by A1;
  end;
 now
    let x,y,z;
    assume that
A5: [x,y] in R and
A6: [y,z] in R;
 P[x,y] by A1,A5;
    hence [x,z] in R by A1,A6;
  end;
  then reconsider o = R as Element of LinPreorders A by A2,Def1;
  take o;
  let a;
  assume a <> b;
then  not [b,a] in R by A1;
  hence thesis by Def4;
end;

theorem Th10:
  a <> b & a <> c implies ex o st a <_o, b & a <_o, c &
  (b <_o, c iff b <_o9, c) & (c <_o, b iff c <_o9, b)
proof
  assume
A1: a <> b & a <> c;
  defpred P[Element of A,Element of A] means
  $1 = a or ($1 <=_o9, $2 & $2 <> a);
  consider R being Relation of A such that
A2: for x,y holds [x,y] in R iff P[x,y] from RELSET_1:sch 2;
A3: now
    let x,y;
 P[x,y] or P[y,x] by Th4;
    hence [x,y] in R or [y,x] in R by A2;
  end;
 now
    let x,y,z;
    assume [x,y] in R & [y,z] in R;
then  ( P[x,y])& P[y,z] by A2;
then  P[x,z] by Th5;
    hence [x,z] in R by A2;
  end;
  then reconsider o = R as Element of LinPreorders A by A3,Def1;
  take o;
A9: ( not [b,a] in R)& not [c,a] in R by A1,A2;
A10: not [c,b] in R iff b <_o9, c by A1,A2;
 not [b,c] in R iff c <_o9, b by A1,A2;
  hence thesis by A9,A10,Def4;
end;

theorem Th11:
  a <> b & a <> c implies ex o st b <_o, a & c <_o, a &
  (b <_o, c iff b <_o9, c) & (c <_o, b iff c <_o9, b)
proof
  assume
A1: a <> b & a <> c;
  defpred P[Element of A,Element of A] means
  ($1 <> a & $1 <=_o9, $2) or $2 = a;
  consider R being Relation of A such that
A2: for x,y holds [x,y] in R iff P[x,y] from RELSET_1:sch 2;
A3: now
    let x,y;
 P[x,y] or P[y,x] by Th4;
    hence [x,y] in R or [y,x] in R by A2;
  end;
 now
    let x,y,z;
    assume [x,y] in R & [y,z] in R;
then  ( P[x,y])& P[y,z] by A2;
then  P[x,z] by Th5;
    hence [x,z] in R by A2;
  end;
  then reconsider o = R as Element of LinPreorders A by A3,Def1;
  take o;
A9: ( not [a,b] in R)& not [a,c] in R by A1,A2;
A10: not [c,b] in R iff b <_o9, c by A1,A2;
 not [b,c] in R iff c <_o9, b by A1,A2;
  hence thesis by A9,A10,Def4;
end;

theorem
  for o,o9 being Element of LinOrders A holds
  (a <_o, b iff a <_o9, b) & (b <_o, a iff b <_o9, a) iff
  (a <_o, b iff a <_o9, b)
proof
  let o,o9 be Element of LinOrders A;
  thus (a <_o, b iff a <_o9, b) & (b <_o, a iff b <_o9, a) implies
  (a <_o, b iff a <_o9, b);
  assume
A1: a <_o, b iff a <_o9, b;
  hence a <_o, b iff a <_o9, b;
  hereby
    assume
A2: b <_o, a;
then  a <> b by Th4;
    hence b <_o9, a by A1,A2,Th4,Th6;
  end;
  assume
A4: b <_o9, a;
then  a <> b by Th4;
  hence thesis by A1,A4,Th4,Th6;
end;

theorem Th13:
  for o being Element of LinOrders A, o9 being Element of LinPreorders A holds
  (for a,b st a <_o, b holds a <_o9, b) iff
  for a,b holds a <_o, b iff a <_o9, b
proof
  let o be Element of LinOrders A, o9 be Element of LinPreorders A;
  hereby
    assume
A1: for a,b st a <_o, b holds a <_o9, b;
    let a,b;
    per cases by Th6;
    suppose
   a <_o, b;
      hence a <_o, b iff a <_o9, b by A1;
    end;
    suppose
   a = b;
      hence a <_o, b iff a <_o9, b by Th3;
    end;
    suppose
A4:   b <_o, a;
then    b <_o9, a by A1;
      hence a <_o, b iff a <_o9, b by A4,Th4;
    end;
  end;
  thus thesis;
end;

begin :: Arrow's theorem
:: version with weak orders, the one from the paper

reserve A,N for finite non empty set;
reserve a,b,c,d,a9,c9 for Element of A;
reserve i,n,nb,nc for Element of N;
reserve o,oI,oII for Element of LinPreorders A;
reserve p,p9,pI,pII,pI9,pII9 for Element of Funcs(N,LinPreorders A);
reserve f for Function of Funcs(N,LinPreorders A),LinPreorders A;
reserve k,k0 for Nat;

theorem Th14:
  (for p,a,b st for i holds a <_p.i, b holds a <_f.p, b) & (for p,p9,a,b st
  for i holds (a <_p.i, b iff a <_p9.i, b) & (b <_p.i, a iff b <_p9.i, a)
  holds a <_f.p, b iff a <_f.p9, b) & card A >= 3 implies
  ex n st for p,a,b st a <_p.n, b holds a <_f.p, b
proof
  assume that
A1: for p,a,b st for i holds a <_p.i, b holds a <_f.p, b and
A2: for p,p9,a,b st
  for i holds (a <_p.i, b iff a <_p9.i, b) & (b <_p.i, a iff b <_p9.i, a)
  holds a <_f.p, b iff a <_f.p9, b and
A3: card A >= 3;
  defpred extreme[Element of LinPreorders A,Element of A] means
  (for a st a <> $2 holds $2 <_$1, a) or (for a st a <> $2 holds a <_$1, $2);
A4: for p,b st for i holds extreme[p.i,b] holds extreme[f.p,b]
  proof
    assume not thesis;
    then consider p,b such that
A6: ex a st a <> b & a <=_f.p, b and
A7: ex c st c <> b & b <=_f.p, c and
A8: for i holds extreme[p.i,b];
    consider a9 such that
A9: a9 <> b & a9 <=_f.p, b by A6;
    consider c9 such that
A10: b <> c9 & b <=_f.p, c9 by A7;
 ex a,c st a <> b & b <> c & a <> c & a <=_f.p, b & b <=_f.p, c
    proof
      per cases;
      suppose
A12:    a9 <> c9;
        take a9,c9;
        thus thesis by A9,A10,A12;
      end;
      suppose
A13:    a9 = c9;
        consider d such that
A14:    d <> b & d <> a9 by A3,Th2;
        per cases by Th4;
        suppose
A15:      d <=_f.p, b;
          take d,c9;
          thus thesis by A10,A13,A14,A15;
        end;
        suppose
A16:      b <=_f.p, d;
          take a9,d;
          thus thesis by A9,A14,A16;
        end;
      end;
    end;
    then consider a,c such that
A17: a <> b & b <> c and
A18: a <> c and
A19: a <=_f.p, b & b <=_f.p, c;
    defpred P[Element of N,Element of LinPreorders A] means
    (a <_p.$1, b iff a <_$2, b) & (b <_p.$1, a iff b <_$2, a) &
    (b <_p.$1, c iff b <_$2, c) & (c <_p.$1, b iff c <_$2, b) & c <_$2, a;
A20: for i holds ex o st P[i,o]
    proof
      let i;
      per cases by A8;
      suppose
    for c st c <> b holds b <_p.i, c;
then A22:    b <_p.i, a & b <_p.i, c by A17;
        consider o such that
A23:    b <_o, c & c <_o, a by A17,A18,Th7;
        take o;
        thus thesis by A22,A23,Th4,Th5;
      end;
      suppose
    for a st a <> b holds a <_p.i, b;
then A25:    a <_p.i, b & c <_p.i, b by A17;
        consider o such that
A26:    c <_o, a & a <_o, b by A17,A18,Th7;
        take o;
        thus thesis by A25,A26,Th4,Th5;
      end;
    end;
    consider p9 being Function of N,LinPreorders A such that
A27: for i holds P[i,p9.i] from FUNCT_2:sch 3(A20);
    reconsider p9 as Element of Funcs(N,LinPreorders A) by FUNCT_2:11;
 a <=_f.p9, b & b <=_f.p9, c by A2,A19,A27;
then  a <=_f.p9, c by Th5;
    hence contradiction by A1,A27;
  end;
A30: for b holds ex nb,pI,pII st (for i st i <> nb holds pI.i = pII.i) &
  (for i holds extreme[pI.i,b]) & (for i holds extreme[pII.i,b]) &
  (for a st a <> b holds b <_pI.nb, a) &
  (for a st a <> b holds a <_pII.nb, b) &
  (for a st a <> b holds b <_f.pI, a) & for a st a <> b holds a <_f.pII, b
  proof
    consider t being FinSequence such that
A31: rng t = N and
A32: t is one-to-one by FINSEQ_4:73;
    reconsider t as FinSequence of N by A31,FINSEQ_1:def 4;
    let b;
    consider oI such that
A33: for a st a <> b holds b <_oI, a by Th8;
    consider oII such that
A34: for a st a <> b holds a <_oII, b by Th9;
A35: for k0 holds ex p st (for k st k in dom t & k < k0 holds p.(t.k) = oII) &
    for k st k in dom t & k >= k0 holds p.(t.k) = oI
    proof
      let k0;
      defpred P[Element of N,Element of LinPreorders A] means
      ex k st k in dom t & $1 = t.k &
      (k < k0 implies $2 = oII) & (k >= k0 implies $2 = oI);
A36:  for i holds ex o st P[i,o]
      proof
        let i;
        consider k being set such that
A37:    k in dom t and
A38:    i = t.k by A31,FUNCT_1:def 5;
        reconsider k as Nat by A37;
        per cases;
        suppose
A39:      k < k0;
          take oII;
          thus thesis by A37,A38,A39;
        end;
        suppose
A40:      k >= k0;
          take oI;
          thus thesis by A37,A38,A40;
        end;
      end;
      consider p being Function of N,LinPreorders A such that
A41:  for i holds P[i,p.i] from FUNCT_2:sch 3(A36);
      reconsider p as Element of Funcs(N,LinPreorders A) by FUNCT_2:11;
      take p;
      thus for k st k in dom t & k < k0 holds p.(t.k) = oII
      proof
        let k;
        assume that
A42:    k in dom t and
A43:    k < k0;
        reconsider i = t.k as Element of N by A42,PARTFUN1:27;
    P[i,p.i] by A41;
        hence thesis by A32,A42,A43,FUNCT_1:def 8;
      end;
      let k;
      assume that
A45:  k in dom t and
A46:  k >= k0;
      reconsider i = t.k as Element of N by A45,PARTFUN1:27;
  P[i,p.i] by A41;
      hence thesis by A32,A45,A46,FUNCT_1:def 8;
    end;
    defpred Q[Nat] means for p st
    (for k st k in dom t & k < $1 holds p.(t.k) = oII) &
    (for k st k in dom t & k >= $1 holds p.(t.k) = oI)
    holds for a st a <> b holds a <_f.p, b;
    reconsider kII9 = len t + 1 as Element of NAT by ORDINAL1:def 13;
A48: Q[kII9]
    proof
      let p;
      assume that
A49:  for k st k in dom t & k < kII9 holds p.(t.k) = oII
      and for k st k in dom t & k >= kII9 holds p.(t.k) = oI;
      let a;
      assume
A50:  a <> b;
  for i holds a <_p.i, b
      proof
        let i;
        consider k being set such that
A52:    k in dom t and
A53:    i = t.k by A31,FUNCT_1:def 5;
        reconsider k as Nat by A52;
    k <= len t by A52,FINSEQ_3:27;
then     k + 0 < kII9 by XREAL_1:10;
then     p.i = oII by A49,A52,A53;
        hence thesis by A34,A50;
      end;
      hence thesis by A1;
    end;
then A57: ex kII9 being Nat st Q[kII9];
    consider kII being Nat such that
A58: Q[kII] & for k0 being Nat st Q[k0] holds k0 >= kII from NAT_1:sch 5(A57
    );
    consider pII such that
A59: for k st k in dom t & k < kII holds pII.(t.k) = oII and
A60: for k st k in dom t & k >= kII holds pII.(t.k) = oI by A35;
A61: kII > 1
    proof
      assume
A62:  kII <= 1;
      consider a such that
A63:  a <> b by A3,Th1,XXREAL_0:2;
A64:  for i holds b <_pII.i, a
      proof
        let i;
        consider k being set such that
A65:    k in dom t and
A66:    i = t.k by A31,FUNCT_1:def 5;
        reconsider k as Nat by A65;
    k >= 1 by A65,FINSEQ_3:27;
then     pII.i = oI by A60,A62,A65,A66,XXREAL_0:2;
        hence thesis by A33,A63;
      end;
A69:  a <_f.pII, b by A58,A59,A60,A63;
  b <_f.pII, a by A1,A64;
      hence contradiction by A69,Th4;
    end;
    then reconsider kI = kII - 1 as Nat by NAT_1:20;
    reconsider kI as Element of NAT by ORDINAL1:def 13;
A71: kII = kI + 1;
 kI > 1 - 1 by A61,XREAL_1:11;
then A73: kI >= 0 + 1 by INT_1:20;
 kII <= kII9 by A48,A58;
then  kI <= kII9 - 1 by XREAL_1:11;
then A76: kI in dom t by A73,FINSEQ_3:27;
    then reconsider nb = t.kI as Element of N by PARTFUN1:27;
A77: kI + 0 < kII by A71,XREAL_1:10;
    then consider pI such that
A78: for k st k in dom t & k < kI holds pI.(t.k) = oII and
A79: for k st k in dom t & k >= kI holds pI.(t.k) = oI and
A80: not(for a st a <> b holds a <_f.pI, b) by A58;
    take nb,pI,pII;
    thus for i st i <> nb holds pI.i = pII.i
    proof
      let i;
      assume
A81:  i <> nb;
      consider k being set such that
A82:  k in dom t and
A83:  i = t.k by A31,FUNCT_1:def 5;
      reconsider k as Nat by A82;
      per cases by A81,A83,XXREAL_0:1;
      suppose
    k < kI;
then     k + 0 < kII & pI.i = oII by A71,A78,A82,A83,XREAL_1:10;
        hence thesis by A59,A82,A83;
      end;
      suppose
    k > kI;
then     k >= kII & pI.i = oI by A71,A79,A82,A83,INT_1:20;
        hence thesis by A60,A82,A83;
      end;
    end;
    thus
A88: for i holds extreme[pI.i,b]
    proof
      let i;
  ex k being set st k in dom t & i = t.k by A31,FUNCT_1:def 5;
then   pI.i = oII or pI.i = oI by A78,A79;
      hence thesis by A33,A34;
    end;
    thus for i holds extreme[pII.i,b]
    proof
      let i;
  ex k being set st k in dom t & i = t.k by A31,FUNCT_1:def 5;
then   pII.i = oII or pII.i = oI by A59,A60;
      hence thesis by A33,A34;
    end;
 pI.nb = oI by A76,A79;
    hence for a st a <> b holds b <_pI.nb, a by A33;
 pII.nb = oII by A59,A76,A77;
    hence for a st a <> b holds a <_pII.nb, b by A34;
    thus for a st a <> b holds b <_f.pI, a by A4,A80,A88;
    thus thesis by A58,A59,A60;
  end;
A95: for b holds ex nb,pI,pII st (for i st i <> nb holds pI.i = pII.i) &
  (for i holds extreme[pI.i,b]) &
  (for a st a <> b holds b <_f.pI, a) & (for a st a <> b holds a <_f.pII, b) &
  for p,a,c st a <> b & c <> b & a <_p.nb, c holds a <_f.p, c
  proof
    let b;
    consider nb,pI,pII such that
A96: for i st i <> nb holds pI.i = pII.i and
A97: for i holds extreme[pI.i,b] and
A98: for i holds extreme[pII.i,b] and
A99: for a st a <> b holds b <_pI.nb, a and
A100: for a st a <> b holds a <_pII.nb, b and
    A101: (
 for a st a <> b holds b <_f.pI, a)& for a st a <> b holds a <_f.pII, b
    by A30;
    take nb,pI,pII;
    thus (for i st i <> nb holds pI.i = pII.i) &
    (for i holds extreme[pI.i,b]) &
    (for a st a <> b holds b <_f.pI, a) & for a st a <> b holds a <_f.pII, b
    by A96,A97,A101;
    let p,a,c;
    assume that
A102: a <> b and
A103: c <> b and
A104: a <_p.nb, c;
A105: a <> c by A104,Th3;
    defpred P[Element of N,Element of LinPreorders A] means
    (a <_p.$1, c iff a <_$2, c) & (c <_p.$1, a iff c <_$2, a) &
    ($1 = nb implies a <_$2, b & b <_$2, c) & ($1 <> nb implies
    ((for d st d <> b holds b <_pII.$1, d) implies b <_$2, a & b <_$2, c) &
    ((for d st d <> b holds d <_pII.$1, b) implies a <_$2, b & c <_$2, b));
A106: for i holds ex o st P[i,o]
    proof
      let i;
      per cases;
      suppose
A107:   i = nb;
        consider o such that
A108:   a <_o, b & b <_o, c by A102,A103,A105,Th7;
        take o;
        thus thesis by A104,A107,A108,Th4,Th5;
      end;
      suppose
A109:   i <> nb;
        per cases by A98;
        suppose
     for d st d <> b holds b <_pII.i, d;
then      b <_pII.i, a by A102;
then A112:     not a <_pII.i, b by Th4;
          consider o such that
          A113:     b
 <_o, a & b <_o, c & ( a <_o, c iff a <_p.i, c)&( c <_o, a iff c <_p.i,
          a)
          by A102,A103,Th10;
          take o;
          thus thesis by A102,A109,A112,A113;
        end;
        suppose
     for d st d <> b holds d <_pII.i, b;
then      a <_pII.i, b by A102;
then A116:     not b <_pII.i, a by Th4;
          consider o such that
          A117:     a
 <_o, b & c <_o, b & ( a <_o, c iff a <_p.i, c)&( c <_o, a iff c <_p.i,
          a)
          by A102,A103,Th11;
          take o;
          thus thesis by A102,A109,A116,A117;
        end;
      end;
    end;
    consider pIII being Function of N,LinPreorders A such that
A118: for i holds P[i,pIII.i] from FUNCT_2:sch 3(A106);
    reconsider pIII as Element of Funcs(N,LinPreorders A) by FUNCT_2:11;
 for i holds (a <_pII.i, b iff a <_pIII.i, b) &
    (b <_pII.i, a iff b <_pIII.i, a)
    proof
      let i;
      per cases;
      suppose
   i = nb;
then    a <_pII.i, b & a <_pIII.i, b by A100,A102,A118;
        hence thesis by Th4;
      end;
      suppose
A122:   i <> nb;
        per cases by A98;
        suppose
     for d st d <> b holds b <_pII.i, d;
then      b <_pII.i, a & b <_pIII.i, a by A102,A118,A122;
          hence thesis by Th4;
        end;
        suppose
     for d st d <> b holds d <_pII.i, b;
then      a <_pII.i, b & a <_pIII.i, b by A102,A118,A122;
          hence thesis by Th4;
        end;
      end;
    end;
then A127: a <_f.pII, b iff a <_f.pIII, b by A2;
     for
 i holds (b <_pI.i, c iff b <_pIII.i, c) & (c <_pI.i, b iff c <_pIII.i, b )
    proof
      let i;
      per cases;
      suppose
   i = nb;
then    b <_pI.i, c & b <_pIII.i, c by A99,A103,A118;
        hence thesis by Th4;
      end;
      suppose
A131:   i <> nb;
        per cases by A98;
        suppose
A132:     for d st d <> b holds b <_pII.i, d;
then      b <_pII.i, c by A103;
then A134:     b <_pI.i, c by A96,A131;
     b <_pIII.i, c by A118,A131,A132;
          hence thesis by A134,Th4;
        end;
        suppose
A136:     for d st d <> b holds d <_pII.i, b;
then      c <_pII.i, b by A103;
then A138:     c <_pI.i, b by A96,A131;
     c <_pIII.i, b by A118,A131,A136;
          hence thesis by A138,Th4;
        end;
      end;
    end;
then  b <_f.pI, c iff b <_f.pIII, c by A2;
then  a <_f.pIII, c by A101,A102,A127,Th5;
    hence thesis by A2,A118;
  end;
  consider b;
  consider nb,pI,pII such that
A142: for i st i <> nb holds pI.i = pII.i and
A143: for i holds extreme[pI.i,b] and
  A144: (
 for a st a <> b holds b <_f.pI, a)& for a st a <> b holds a <_f.pII, b and
A145: for p,a,c st a <> b & c <> b & a <_p.nb, c holds a <_f.p, c by A95;
  take nb;
  let p,a,a9;
  assume
A146: a <_p.nb, a9;
then A147: a <> a9 by Th4;
  per cases;
  suppose
 a <> b & a9 <> b;
    hence thesis by A145,A146;
  end;
  suppose
A149: a = b or a9 = b;
    consider c such that
A150: c <> a & c <> a9 by A3,Th2;
    consider nc,pI9,pII9 such that
    for i st i <> nc holds pI9.i = pII9.i and
    for i holds extreme[pI9.i,c] and
    for a st a <> c holds c <_f.pI9, a and
    for a st a <> c holds a <_f.pII9, c and
A151: for p,a,a9 st a <> c & a9 <> c & a <_p.nc, a9 holds a <_f.p, a9 by A95;
 nc = nb
    proof
      per cases by A149;
      suppose
A153:   a = b;
        assume
A154:   nc <> nb;
   b <_pI.nc, a9 or a9 <_pI.nc, b by A143,A147,A153;
then    b <_pII.nc, a9 & a9 <_f.pII, b or
        a9 <_pI.nc, b & b <_f.pI, a9 by A142,A144,A147,A153,A154;
then    b <_pII.nc, a9 & a9 <=_f.pII, b or
        a9 <_pI.nc, b & b <=_f.pI, a9 by Th4;
        hence contradiction by A150,A151,A153;
      end;
      suppose
A158:   a9 = b;
        assume
A159:   nc <> nb;
   b <_pI.nc, a or a <_pI.nc, b by A143,A147,A158;
then    b <_pII.nc, a & a <_f.pII, b or
        a <_pI.nc, b & b <_f.pI, a by A142,A144,A147,A158,A159;
then    b <_pII.nc, a & a <=_f.pII, b or
        a <_pI.nc, b & b <=_f.pI, a by Th4;
        hence contradiction by A150,A151,A158;
      end;
    end;
    hence thesis by A146,A150,A151;
  end;
end;
 
:: and then a stronger version

reserve o,o1 for Element of LinOrders A;
reserve o9 for Element of LinPreorders A;
reserve p,p9 for Element of Funcs(N,LinOrders A);
reserve q,q9 for Element of Funcs(N,LinPreorders A);
reserve f for Function of Funcs(N,LinOrders A),LinPreorders A;

theorem
  (for p,a,b st for i holds a <_p.i, b holds a <_f.p, b) & (for p,p9,a,b st
  for i holds a <_p.i, b iff a <_p9.i, b holds a <_f.p, b iff a <_f.p9, b) &
  card A >= 3 implies ex n st for p,a,b holds a <_p.n, b iff a <_f.p, b
proof
  assume that
A1: for p,a,b st for i holds a <_p.i, b holds a <_f.p, b and
A2: for p,p9,a,b st for i holds a <_p.i, b iff a <_p9.i, b
  holds a <_f.p, b iff a <_f.p9, b and
A3: card A >= 3;
  consider o;
  defpred O[Element of LinPreorders A,Element of A,Element of A] means
  $2 <=_$1, $3 & ($2 <_$1, $3 or $2 <=_o, $3);
  defpred P[Element of LinPreorders A,Element of LinOrders A] means
  for a,b holds O[$1,a,b] iff a <=_$2, b;
A4: for o9 ex o1 st P[o9,o1]
  proof
    let o9;
    defpred Q[Element of A,Element of A] means O[o9,$1,$2];
    consider o1 being Relation of A such that
A5: for a,b holds [a,b] in o1 iff Q[a,b] from RELSET_1:sch 2;
A6: now
      let a,b;
   Q[a,b] or Q[b,a] by Th4;
      hence [a,b] in o1 or [b,a] in o1 by A5;
    end;
 now
      let a,b,c;
   Q[a,b] & Q[b,c] implies Q[a,c] by Th5;
      hence [a,b] in o1 & [b,c] in o1 implies [a,c] in o1 by A5;
    end;
    then reconsider o1 as Element of LinPreorders A by A6,Def1;
 now
      let a,b;
  Q[a,b] & Q[b,a] implies a = b by Th6;
      hence [a,b] in o1 & [b,a] in o1 implies a = b by A5;
    end;
    then reconsider o1 as Element of LinOrders A by Def2;
    take o1;
    let a,b;
 [a,b] in o1 iff Q[a,b] by A5;
    hence thesis by Def4;
  end;
  defpred R[Element of Funcs(N,LinPreorders A),Element of Funcs(N,LinOrders A)]
  means for i holds P[$1.i,$2.i];
A13: for q,p,p9 st R[q,p] & R[q,p9] holds p = p9
  proof
    let q,p,p9;
    assume that
A14: R[q,p] and
A15: R[q,p9];
    let i;
    reconsider pi = p.i as Relation of A by Def1;
    reconsider pi9 = p9.i as Relation of A by Def1;
 now
      let a,b;
A17:  O[q.i,a,b] iff a <=_p.i, b by A14;
  O[q.i,a,b] iff a <=_p9.i, b by A15;
      hence [a,b] in p.i iff [a,b] in p9.i by A17,Def4;
    end;
then  pi = pi9 by RELSET_1:def 3;
    hence p.i = p9.i;
  end;
A20: for q ex p st R[q,p]
  proof
    let q;
    defpred S[Element of N,Element of LinOrders A] means P[q.$1,$2];
A21: for i ex o1 st S[i,o1] by A4;
    consider p being Function of N,LinOrders A such that
A22: for i holds S[i,p.i qua Element of LinOrders A] from FUNCT_2:sch 3(A21);
    reconsider p as Element of Funcs(N,LinOrders A) by FUNCT_2:11;
    take p;
    thus thesis by A22;
  end;
  defpred T[Element of Funcs(N,LinPreorders A),Element of LinPreorders A] means
  ex p st R[$1,p] & f.p = $2;
A23: for q ex o9 st T[q,o9]
  proof
    let q;
    consider p such that
A24: R[q,p] by A20;
    take f.p;
    thus thesis by A24;
  end;
consider f9 being Function of Funcs(N,LinPreorders A),LinPreorders A such that
A25: for q holds T[q,f9.q] from FUNCT_2:sch 3(A23);
A26: for q,a,b st for i holds a <_q.i, b holds a <_f9.q, b
  proof
    let q,a,b;
    assume
A27: for i holds a <_q.i, b;
    consider p such that
A28: R[q,p] and
A29: f.p = f9.q by A25;
 now
      let i;
  not O[q.i,b,a] by A27;
      hence a <_p.i, b by A28;
    end;
    hence thesis by A1,A29;
  end;
 now
    let q,q9,a,b;
    assume
A33: for i holds (a <_q.i, b iff a <_q9.i, b) & (b <_q.i, a iff b <_q9.i, a);
    consider p such that
A34: R[q,p] and
A35: f.p = f9.q by A25;
    consider p9 such that
A36: R[q9,p9] and
A37: f.p9 = f9.q9 by A25;
 for i holds a <_p.i, b iff a <_p9.i, b
    proof
      let i;
  O[q.i,b,a] iff O[q9.i,b,a] by A33;
      hence thesis by A34,A36;
    end;
    hence a <_f9.q, b iff a <_f9.q9, b by A2,A35,A37;
  end;
  then consider n such that
A40: for q,a,b st a <_q.n, b holds a <_f9.q, b by A3,A26,Th14;
  take n;
  let p;
 now
 rng p c= LinOrders A by RELAT_1:def 19;
then  dom p = N & rng p c= LinPreorders A by FUNCT_2:def 1,XBOOLE_1:1;
    then
reconsider q = p as Element of Funcs(N,LinPreorders A) by FUNCT_2:def 2
    ;
A44: R[q,p]
    proof
      let i;
      let a,b;
  a <_p.i, b or a = b or a >_p.i, b by Th6;
      hence thesis by Th4;
    end;
A46: ex p9 st ( R[q,p9])& f.p9 = f9.q by A25;
    let a,b;
    assume a <_p.n, b;
then  a <_f9.q, b by A40;
    hence a <_f.p, b by A13,A44,A46;
  end;
  hence thesis by Th13;
end;
