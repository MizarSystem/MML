:: Arrow's Impossibility Theorem
::  by Freek Wiedijk
::
:: Received August 13, 2007
:: Copyright (c) 2007 Association of Mizar Users

environ

 vocabularies NUMBERS, XBOOLE_0, SUBSET_1, FUNCT_1, FINSET_1, CARD_1, XXREAL_0,
      TARSKI, ARYTM_1, RELAT_1, ZFMISC_1, RELAT_2, ORDERS_1, WELLORD1, FUNCT_2,
      NAT_1, FINSEQ_1, ARYTM_3, ARROW;
 notations ORDERS_1, RELAT_1, RELAT_2, RELSET_1, XBOOLE_0, SUBSET_1, TARSKI,
      FINSET_1, FUNCT_1, FUNCT_2, CARD_1, XXREAL_0, ZFMISC_1, NAT_1, NUMBERS,
      FINSEQ_1, XCMPLX_0, WELLORD1;
 constructors XXREAL_0, FUNCT_2, REAL_1, FINSEQ_1, INT_1, RELAT_2, PARTFUN1,
      WELLORD1, RELSET_1;
 registrations RELSET_1, FINSET_1, NAT_1, INT_1, XREAL_0, XBOOLE_0, ORDINAL1;
 requirements BOOLE, SUBSET, NUMERALS, ARITHM, REAL;
 definitions RELAT_1, FUNCT_2, WELLORD1;
 theorems RELSET_1, ZFMISC_1, TARSKI, FUNCT_2, FINSEQ_4, FINSEQ_1, FINSEQ_3,
      NAT_1, ORDINAL1, XREAL_1, FUNCT_1, XXREAL_0, PARTFUN1, INT_1, CARD_2,
      CARD_1, XBOOLE_0, SUBSET_1, RELAT_2, RELAT_1, ORDERS_1, XBOOLE_1,
      WELLORD2, WELLORD1;
 schemes XBOOLE_0, FUNCT_2, NAT_1, RELSET_1;

begin :: Preliminaries

definition
  let A,B' be non empty set;
  let B be non empty Subset of B';
  let f be Function of A,B;
  let x be Element of A;
  redefine func f.x -> Element of B;
  coherence
  proof
    thus f.x is Element of B;
  end;
end;

theorem Th1:
  for A being finite set st card A >= 2 holds for a being Element of A holds
  ex b being Element of A st b <> a
proof
  let A' be finite set;
  assume
A1: card A' >= 2;
  reconsider A = A' as finite non empty set by A1,CARD_1:47;
  let a be Element of A';
A2: {a} c= A by ZFMISC_1:37;
A3: card (A \ {a}) = card A - card {a} by A2,CARD_2:63
    .= card A - 1 by CARD_1:50;
A4: card (A \ {a}) <> 0 by A1,A3;
  consider b being set such that
A5: b in A \ {a} by A4,CARD_1:47,XBOOLE_0:def 1;
  reconsider b as Element of A' by A5;
  take b;
A6: not b in {a} by A5,XBOOLE_0:def 5;
  thus thesis by A6,TARSKI:def 1;
end;

theorem Th2:
  for A being finite set st card A >= 3 holds for a,b being Element of A holds
  ex c being Element of A st c <> a & c <> b
proof
  let A' be finite set;
  assume
A1: card A' >= 3;
  reconsider A = A' as finite non empty set by A1,CARD_1:47;
  let a,b be Element of A';
A2: {a,b} c= A by ZFMISC_1:38;
A3: card (A \ {a,b}) = card A - card {a,b} by A2,CARD_2:63;
A4: card {a,b} <= 2 by CARD_2:69;
A5: card (A \ {a,b}) >= 3 - 2 by A1,A3,A4,XREAL_1:15;
A6: card (A \ {a,b}) <> 0 by A5;
  consider c being set such that
A7: c in A \ {a,b} by A6,CARD_1:47,XBOOLE_0:def 1;
  reconsider c as Element of A' by A7;
  take c;
A8: not c in {a,b} by A7,XBOOLE_0:def 5;
  thus thesis by A8,TARSKI:def 2;
end;

begin :: Linear preorders and linear orders

reserve A for non empty set;
reserve a,b,c,x,y,z for Element of A;

definition
  let A;
  defpred P[set] means $1 is Relation of A &
  (for a,b holds [a,b] in $1 or [b,a] in $1) &
  (for a,b,c st [a,b] in $1 & [b,c] in $1 holds [a,c] in $1);
  defpred Q[set] means for R being set holds R in $1 iff P[R];
  func LinPreorders A means
  :Def1:
  for R being set holds R in it iff
  R is Relation of A & (for a,b holds [a,b] in R or [b,a] in R) &
  for a,b,c st [a,b] in R & [b,c] in R holds [a,c] in R;
  existence
  proof
    consider it0 being set such that
A1: for R being set holds R in it0 iff R in bool [:A,A:] & P[R]
    from XBOOLE_0:sch 1;
    take it0;
    let R be set;
    thus R in it0 implies P[R] by A1;
    assume
A2: P[R];
A3: [:A,A:] in bool [:A,A:] by ZFMISC_1:def 1;
    thus thesis by A1,A2,A3;
  end;
  uniqueness
  proof
    let it1,it2 be set;
    assume that
A4: Q[it1] and
A5: Q[it2];
A6: now
      let R be set;
A7:   R in it1 iff P[R] by A4;
      thus R in it1 iff R in it2 by A5,A7;
    end;
    thus thesis by A6,TARSKI:2;
  end;
end;

registration
  let A;
  cluster LinPreorders A -> non empty;
  coherence
  proof
A1: [:A,A:] c= [:A,A:];
    reconsider R = [:A,A:] as Relation of A by A1;
    A2: (
 for a,b holds [a,b] in R or [b,a] in R)& for a,b,c st [a,b] in R & [b,c] in
    R holds [a,c] in R by ZFMISC_1:106;
    thus thesis by A2,Def1;
  end;
end;

definition
  let A;
  defpred P[set] means for a,b st [a,b] in $1 & [b,a] in $1 holds a = b;
  defpred Q[set] means
  for R being Element of LinPreorders A holds R in $1 iff P[R];
  func LinOrders A -> Subset of LinPreorders A means
  :Def2:
  for R being Element of LinPreorders A holds R in it iff
  for a,b st [a,b] in R & [b,a] in R holds a = b;
  existence
  proof
    consider it0 being set such that
A1: for R being set holds R in it0 iff R in LinPreorders A & P[R]
    from XBOOLE_0:sch 1;
A2: for R being set st R in it0 holds R in LinPreorders A by A1;
    reconsider it0 as Subset of LinPreorders A by A2,TARSKI:def 3;
    take it0;
    let R be Element of LinPreorders A;
    thus R in it0 implies P[R] by A1;
    assume
A3: P[R];
    thus thesis by A1,A3;
  end;
  uniqueness
  proof
    let it1,it2 be Subset of LinPreorders A;
    assume that
A4: Q[it1] and
A5: Q[it2];
A6: now
      let R be Element of LinPreorders A;
A7:   R in it1 iff P[R] by A4;
      thus R in it1 iff R in it2 by A5,A7;
    end;
    thus thesis by A6,SUBSET_1:8;
  end;
end;

registration
  let A be set;
  cluster connected Order of A;
  existence
  proof
    consider R' being Relation such that
A1: R' well_orders A by WELLORD2:26;
    set R = R' |_2 A;
A2: R is well-ordering by A1,WELLORD2:25;
    reconsider R as Relation of A by XBOOLE_1:17;
A3: now
      let a be set;
      assume
A4:   a in A;
A5:   R' is_reflexive_in A by A1,WELLORD1:def 5;
A6:   [a,a] in R' by A4,A5,RELAT_2:def 1;
A7:   [a,a] in [:A,A:] by A4,ZFMISC_1:def 2;
A8:   [a,a] in R by A6,A7,XBOOLE_0:def 4;
      thus a in dom R by A8,RELAT_1:def 4;
    end;
A9: A c= dom R by A3,TARSKI:def 3;
A10: dom R = A by A9,XBOOLE_0:def 10;
    reconsider R as Order of A by A2,A10,PARTFUN1:def 4,WELLORD1:def 4;
    take R;
    thus thesis by A2,WELLORD1:def 4;
  end;
end;

definition
  let A;
  redefine func LinOrders A means
  :Def3:
  for R being set holds R in it iff R is connected Order of A;
  compatibility
  proof
A1: now
      let R be set;
      assume
A2:   R in LinOrders A;
      reconsider R' = R as Relation of A by A2,Def1;
A3:   now
        let a be set;
        assume
A4:     a in A;
A5:     [a,a] in R by A2,A4,Def1;
        thus a in dom R' by A5,RELAT_1:def 4;
      end;
A6:   A c= dom R' by A3,TARSKI:def 3;
A7:   dom R' = A by A6,XBOOLE_0:def 10;
A8:   now
        let a be set;
        assume
A9:     a in A;
A10:    [a,a] in R by A2,A9,Def1;
        thus a in rng R' by A10,RELAT_1:def 5;
      end;
A11:  A c= rng R' by A8,TARSKI:def 3;
A12:  field R' = A \/ A by A7,A11,XBOOLE_0:def 10;
A13:  for a,b being set st a in A & b in A & a <> b
      holds [a,b] in R or [b,a] in R by A2,Def1;
A14:  R' is_connected_in A by A13,RELAT_2:def 6;
A15:  for a being set st a in A holds [a,a] in R by A2,Def1;
A16:  R' is_reflexive_in A by A15,RELAT_2:def 1;
A17:  for
 a,b being set st a in A & b in A & [a,b] in R & [b,a] in R holds a = b
      by A2,Def2;
A18:  R' is_antisymmetric_in A by A17,RELAT_2:def 4;
      A19:  for
 a,b,c being set st a in A & b in A & c in A & [a,b] in R & [b,c] in
      R holds [a,c] in R by A2,Def1;
A20:  R' is_transitive_in A by A19,RELAT_2:def 8;
      thus R is connected Order of A
      by A7,A12,A14,A16,A18,A20,PARTFUN1:def 4,RELAT_2:def 9,def 12,def 14
      ,def 16;
    end;
A21: now
      let R be set;
      assume
A22:  R is connected Order of A;
      reconsider R' = R as connected Order of A by A22;
A23:  now
        let a,b;
A24:    dom R' = A by PARTFUN1:def 4;
A25:    A c= dom R' \/ rng R' by A24,XBOOLE_1:7;
A26:    field R' = A by A25,XBOOLE_0:def 10;
        A27:    R'
 is_connected_in field R' & R' is_reflexive_in field R' by RELAT_2:def 9
        ,def 14;
A28:    a = b or a <> b;
        thus [a,b] in R or [b,a] in R by A26,A27,A28,RELAT_2:def 1,def 6;
      end;
A29:  for a,b,c st [a,b] in R & [b,c] in R holds [a,c] in R by A22,
      ORDERS_1:14;
A30:  R in LinPreorders A by A22,A23,A29,Def1;
A31:  for a,b st [a,b] in R & [b,a] in R holds a = b by A22,ORDERS_1:13;
      thus R in LinOrders A by A30,A31,Def2;
    end;
    let it0 be Subset of LinPreorders A;
    thus it0 = LinOrders A implies
    for R being set holds R in it0 iff R is connected Order of A by A1,A21;
    assume
A32: for R being set holds R in it0 iff R is connected Order of A;
A33: now
      let R be set;
A34:  R in it0 iff R is connected Order of A by A32;
      thus R in it0 iff R in LinOrders A by A1,A21,A34;
    end;
    thus thesis by A33,TARSKI:2;
  end;
end;

registration
  let A;
  cluster LinOrders A -> non empty;
  coherence
  proof
    consider R being connected Order of A;
A1: R in LinOrders A by Def3;
    thus thesis by A1;
  end;
end;

registration let A;
 cluster -> Relation-like Element of LinPreorders A;
 coherence by Def1;
 cluster -> Relation-like Element of LinOrders A;
 coherence;
end;

reserve o,o' for Element of LinPreorders A;
reserve o'' for Element of LinOrders A;

definition
  let o be Relation, a,b be set;
  pred a <=_o, b means
  :Def4:
  [a,b] in o;
end;

notation
  let o be Relation, a,b be set;
  synonym b >=_o, a for a <=_o, b;
  antonym b <_o, a for a <=_o, b;
  antonym a >_o, b for a <=_o, b;
end;

theorem Th3:
  a <=_o, a
proof
  thus [a,a] in o by Def1;
end;

theorem Th4:
  a <=_o, b or b <=_o, a
proof
A1: [a,b] in o or [b,a] in o by Def1;
  thus thesis by A1,Def4;
end;

theorem Th5:
  (a <=_o, b or a <_o, b) & (b <=_ o, c or b <_o, c) implies a <=_o, c
proof
  assume
A1: a <=_o, b or a <_o, b;
A2: a <=_o, b by A1,Th4;
A3: [a,b] in o by A2,Def4;
  assume
A4: b <=_o, c or b <_o, c;
A5: b <=_o, c by A4,Th4;
A6: [b,c] in o by A5,Def4;
  thus [a,c] in o by A3,A6,Def1;
end;

theorem Th6:
  a <=_o'', b & b <=_o'', a implies a = b
proof
A1: [a,b] in o'' & [b,a] in o'' implies a = b by Def2;
  thus thesis by A1,Def4;
end;

theorem Th7:
  a <> b & b <> c & a <> c implies ex o st a <_o, b & b <_o, c
proof
  assume that
A1: a <> b & b <> c and
A2: a <> c;
  defpred P[set,set] means ($1 = a or $2 <> a) & ($1 <> c or $2 = c);
  consider R being Relation of A such that
A3: for x,y holds [x,y] in R iff P[x,y] from RELSET_1:sch 2;
A4: now
    let x,y;
A5: P[x,y] or P[y,x] by A2;
    thus [x,y] in R or [y,x] in R by A3,A5;
  end;
A6: now
    let x,y,z;
    assume
A7: [x,y] in R & [y,z] in R;
A8: ( P[x,y])& P[y,z] by A3,A7;
    thus [x,z] in R by A3,A8;
  end;
  reconsider o = R as Element of LinPreorders A by A4,A6,Def1;
  take o;
A9: ( not [b,a] in R)& not [c,b] in R by A1,A3;
  thus thesis by A9,Def4;
end;

theorem Th8:
  ex o st for a st a <> b holds b <_o, a
proof
  defpred P[set,set] means $1 = b or $2 <> b;
  consider R being Relation of A such that
A1: for x,y holds [x,y] in R iff P[x,y] from RELSET_1:sch 2;
A2: now
    let x,y;
A3: P[x,y] or P[y,x];
    thus [x,y] in R or [y,x] in R by A1,A3;
  end;
A4: now
    let x,y,z;
    assume that
A5: [x,y] in R and
A6: [y,z] in R;
A7: P[y,z] by A1,A6;
    thus [x,z] in R by A1,A5,A7;
  end;
  reconsider o = R as Element of LinPreorders A by A2,A4,Def1;
  take o;
  let a;
  assume
A8: a <> b;
A9: not [a,b] in R by A1,A8;
  thus thesis by A9,Def4;
end;

theorem Th9:
  ex o st for a st a <> b holds a <_o, b
proof
  defpred P[set,set] means $1 <> b or $2 = b;
  consider R being Relation of A such that
A1: for x,y holds [x,y] in R iff P[x,y] from RELSET_1:sch 2;
A2: now
    let x,y;
A3: P[x,y] or P[y,x];
    thus [x,y] in R or [y,x] in R by A1,A3;
  end;
A4: now
    let x,y,z;
    assume that
A5: [x,y] in R and
A6: [y,z] in R;
A7: P[x,y] by A1,A5;
    thus [x,z] in R by A1,A6,A7;
  end;
  reconsider o = R as Element of LinPreorders A by A2,A4,Def1;
  take o;
  let a;
  assume
A8: a <> b;
A9: not [b,a] in R by A1,A8;
  thus thesis by A9,Def4;
end;

theorem Th10:
  a <> b & a <> c implies ex o st a <_o, b & a <_o, c &
  (b <_o, c iff b <_o', c) & (c <_o, b iff c <_o', b)
proof
  assume
A1: a <> b & a <> c;
  defpred P[Element of A,Element of A] means
  $1 = a or ($1 <=_o', $2 & $2 <> a);
  consider R being Relation of A such that
A2: for x,y holds [x,y] in R iff P[x,y] from RELSET_1:sch 2;
A3: now
    let x,y;
A4: P[x,y] or P[y,x] by Th4;
    thus [x,y] in R or [y,x] in R by A2,A4;
  end;
A5: now
    let x,y,z;
    assume
A6: [x,y] in R & [y,z] in R;
A7: ( P[x,y])& P[y,z] by A2,A6;
A8: P[x,z] by A7,Th5;
    thus [x,z] in R by A2,A8;
  end;
  reconsider o = R as Element of LinPreorders A by A3,A5,Def1;
  take o;
A9: ( not [b,a] in R)& not [c,a] in R by A1,A2;
A10: not [c,b] in R iff b <_o', c by A1,A2;
A11: not [b,c] in R iff c <_o', b by A1,A2;
  thus thesis by A9,A10,A11,Def4;
end;

theorem Th11:
  a <> b & a <> c implies ex o st b <_o, a & c <_o, a &
  (b <_o, c iff b <_o', c) & (c <_o, b iff c <_o', b)
proof
  assume
A1: a <> b & a <> c;
  defpred P[Element of A,Element of A] means
  ($1 <> a & $1 <=_o', $2) or $2 = a;
  consider R being Relation of A such that
A2: for x,y holds [x,y] in R iff P[x,y] from RELSET_1:sch 2;
A3: now
    let x,y;
A4: P[x,y] or P[y,x] by Th4;
    thus [x,y] in R or [y,x] in R by A2,A4;
  end;
A5: now
    let x,y,z;
    assume
A6: [x,y] in R & [y,z] in R;
A7: ( P[x,y])& P[y,z] by A2,A6;
A8: P[x,z] by A7,Th5;
    thus [x,z] in R by A2,A8;
  end;
  reconsider o = R as Element of LinPreorders A by A3,A5,Def1;
  take o;
A9: ( not [a,b] in R)& not [a,c] in R by A1,A2;
A10: not [c,b] in R iff b <_o', c by A1,A2;
A11: not [b,c] in R iff c <_o', b by A1,A2;
  thus thesis by A9,A10,A11,Def4;
end;

theorem
  for o,o' being Element of LinOrders A holds
  (a <_o, b iff a <_o', b) & (b <_o, a iff b <_o', a) iff
  (a <_o, b iff a <_o', b)
proof
  let o,o' be Element of LinOrders A;
  thus (a <_o, b iff a <_o', b) & (b <_o, a iff b <_o', a) implies
  (a <_o, b iff a <_o', b);
  assume
A1: a <_o, b iff a <_o', b;
  thus a <_o, b iff a <_o', b by A1;
  hereby
    assume
A2: b <_o, a;
A3: a <> b by A2,Th4;
    thus b <_o', a by A1,A2,A3,Th4,Th6;
  end;
  assume
A4: b <_o', a;
A5: a <> b by A4,Th4;
  thus thesis by A1,A4,A5,Th4,Th6;
end;

theorem Th13:
  for o being Element of LinOrders A, o' being Element of LinPreorders A holds
  (for a,b st a <_o, b holds a <_o', b) iff
  for a,b holds a <_o, b iff a <_o', b
proof
  let o be Element of LinOrders A, o' be Element of LinPreorders A;
  hereby
    assume
A1: for a,b st a <_o, b holds a <_o', b;
    let a,b;
    per cases by Th6;
    suppose
A2:   a <_o, b;
      thus a <_o, b iff a <_o', b by A1,A2;
    end;
    suppose
A3:   a = b;
      thus a <_o, b iff a <_o', b by A3,Th3;
    end;
    suppose
A4:   b <_o, a;
A5:   b <_o', a by A1,A4;
      thus a <_o, b iff a <_o', b by A4,A5,Th4;
    end;
  end;
  thus thesis;
end;

begin :: Arrow's theorem

:: version with weak orders, the one from the paper

reserve A,N for finite non empty set;
reserve a,b,c,d,a',b',c' for Element of A;
reserve i,n,nb,nc for Element of N;
reserve o,oI,oII for Element of LinPreorders A;
reserve p,p',pI,pII,pI',pII' for Element of Funcs(N,LinPreorders A);
reserve f for Function of Funcs(N,LinPreorders A),LinPreorders A;
reserve k,k',k0,kI,kII,kII' for Nat;

theorem Th14:
  (for p,a,b st for i holds a <_p.i, b holds a <_f.p, b) & (for p,p',a,b st
  for i holds (a <_p.i, b iff a <_p'.i, b) & (b <_p.i, a iff b <_p'.i, a)
  holds a <_f.p, b iff a <_f.p', b) & card A >= 3 implies
  ex n st for p,a,b st a <_p.n, b holds a <_f.p, b
proof
  assume that
A1: for p,a,b st for i holds a <_p.i, b holds a <_f.p, b and
A2: for p,p',a,b st
  for i holds (a <_p.i, b iff a <_p'.i, b) & (b <_p.i, a iff b <_p'.i, a)
  holds a <_f.p, b iff a <_f.p', b and
A3: card A >= 3;
  defpred extreme[Element of LinPreorders A,Element of A] means
  (for a st a <> $2 holds $2 <_$1, a) or (for a st a <> $2 holds a <_$1, $2);
A4: for p,b st for i holds extreme[p.i,b] holds extreme[f.p,b]
  proof
    assume
A5: not thesis;
    consider p,b such that
A6: ex a st a <> b & a <=_f.p, b and
A7: ex c st c <> b & b <=_f.p, c and
A8: for i holds extreme[p.i,b] by A5;
    consider a' such that
A9: a' <> b & a' <=_f.p, b by A6;
    consider c' such that
A10: b <> c' & b <=_f.p, c' by A7;
A11: ex a,c st a <> b & b <> c & a <> c & a <=_f.p, b & b <=_f.p, c
    proof
      per cases;
      suppose
A12:    a' <> c';
        take a',c';
        thus thesis by A9,A10,A12;
      end;
      suppose
A13:    a' = c';
        consider d such that
A14:    d <> b & d <> a' by A3,Th2;
        per cases by Th4;
        suppose
A15:      d <=_f.p, b;
          take d,c';
          thus thesis by A10,A13,A14,A15;
        end;
        suppose
A16:      b <=_f.p, d;
          take a',d;
          thus thesis by A9,A14,A16;
        end;
      end;
    end;
    consider a,c such that
A17: a <> b & b <> c and
A18: a <> c and
A19: a <=_f.p, b & b <=_f.p, c by A11;
    defpred P[Element of N,Element of LinPreorders A] means
    (a <_p.$1, b iff a <_$2, b) & (b <_p.$1, a iff b <_$2, a) &
    (b <_p.$1, c iff b <_$2, c) & (c <_p.$1, b iff c <_$2, b) & c <_$2, a;
A20: for i holds ex o st P[i,o]
    proof
      let i;
      per cases by A8;
      suppose
A21:    for c st c <> b holds b <_p.i, c;
A22:    b <_p.i, a & b <_p.i, c by A17,A21;
        consider o such that
A23:    b <_o, c & c <_o, a by A17,A18,Th7;
        take o;
        thus thesis by A22,A23,Th4,Th5;
      end;
      suppose
A24:    for a st a <> b holds a <_p.i, b;
A25:    a <_p.i, b & c <_p.i, b by A17,A24;
        consider o such that
A26:    c <_o, a & a <_o, b by A17,A18,Th7;
        take o;
        thus thesis by A25,A26,Th4,Th5;
      end;
    end;
    consider p' being Function of N,LinPreorders A such that
A27: for i holds P[i,p'.i] from FUNCT_2:sch 3(A20);
    reconsider p' as Element of Funcs(N,LinPreorders A) by FUNCT_2:11;
A28: a <=_f.p', b & b <=_f.p', c by A2,A19,A27;
A29: a <=_f.p', c by A28,Th5;
    thus contradiction by A1,A27,A29;
  end;
A30: for b holds ex nb,pI,pII st (for i st i <> nb holds pI.i = pII.i) &
  (for i holds extreme[pI.i,b]) & (for i holds extreme[pII.i,b]) &
  (for a st a <> b holds b <_pI.nb, a) &
  (for a st a <> b holds a <_pII.nb, b) &
  (for a st a <> b holds b <_f.pI, a) & for a st a <> b holds a <_f.pII, b
  proof
    consider t being FinSequence such that
A31: rng t = N and
A32: t is one-to-one by FINSEQ_4:73;
    reconsider t as FinSequence of N by A31,FINSEQ_1:def 4;
    let b;
    consider oI such that
A33: for a st a <> b holds b <_oI, a by Th8;
    consider oII such that
A34: for a st a <> b holds a <_oII, b by Th9;
A35: for k0 holds ex p st (for k st k in dom t & k < k0 holds p.(t.k) = oII) &
    for k st k in dom t & k >= k0 holds p.(t.k) = oI
    proof
      let k0;
      defpred P[Element of N,Element of LinPreorders A] means
      ex k st k in dom t & $1 = t.k &
      (k < k0 implies $2 = oII) & (k >= k0 implies $2 = oI);
A36:  for i holds ex o st P[i,o]
      proof
        let i;
        consider k being set such that
A37:    k in dom t and
A38:    i = t.k by A31,FUNCT_1:def 5;
        reconsider k as Nat by A37;
        per cases;
        suppose
A39:      k < k0;
          take oII;
          thus thesis by A37,A38,A39;
        end;
        suppose
A40:      k >= k0;
          take oI;
          thus thesis by A37,A38,A40;
        end;
      end;
      consider p being Function of N,LinPreorders A such that
A41:  for i holds P[i,p.i] from FUNCT_2:sch 3(A36);
      reconsider p as Element of Funcs(N,LinPreorders A) by FUNCT_2:11;
      take p;
      thus for k st k in dom t & k < k0 holds p.(t.k) = oII
      proof
        let k;
        assume that
A42:    k in dom t and
A43:    k < k0;
        reconsider i = t.k as Element of N by A42,PARTFUN1:27;
A44:    P[i,p.i] by A41;
        thus thesis by A32,A42,A43,A44,FUNCT_1:def 8;
      end;
      let k;
      assume that
A45:  k in dom t and
A46:  k >= k0;
      reconsider i = t.k as Element of N by A45,PARTFUN1:27;
A47:  P[i,p.i] by A41;
      thus thesis by A32,A45,A46,A47,FUNCT_1:def 8;
    end;
    defpred Q[Nat] means for p st
    (for k st k in dom t & k < $1 holds p.(t.k) = oII) &
    (for k st k in dom t & k >= $1 holds p.(t.k) = oI)
    holds for a st a <> b holds a <_f.p, b;
    reconsider kII' = len t + 1 as Element of NAT by ORDINAL1:def 13;
A48: Q[kII']
    proof
      let p;
      assume that
A49:  for k st k in dom t & k < kII' holds p.(t.k) = oII
      and for k st k in dom t & k >= kII' holds p.(t.k) = oI;
      let a;
      assume
A50:  a <> b;
A51:  for i holds a <_p.i, b
      proof
        let i;
        consider k being set such that
A52:    k in dom t and
A53:    i = t.k by A31,FUNCT_1:def 5;
        reconsider k as Nat by A52;
A54:    k <= len t by A52,FINSEQ_3:27;
A55:    k + 0 < kII' by A54,XREAL_1:10;
A56:    p.i = oII by A49,A52,A53,A55;
        thus thesis by A34,A50,A56;
      end;
      thus thesis by A1,A51;
    end;
A57: ex kII' being Nat st Q[kII'] by A48;
    consider kII being Nat such that
A58: Q[kII] & for k0 being Nat st Q[k0] holds k0 >= kII from NAT_1:sch 5(A57
    );
    consider pII such that
A59: for k st k in dom t & k < kII holds pII.(t.k) = oII and
A60: for k st k in dom t & k >= kII holds pII.(t.k) = oI by A35;
A61: kII > 1
    proof
      assume
A62:  kII <= 1;
      consider a such that
A63:  a <> b by A3,Th1,XXREAL_0:2;
A64:  for i holds b <_pII.i, a
      proof
        let i;
        consider k being set such that
A65:    k in dom t and
A66:    i = t.k by A31,FUNCT_1:def 5;
        reconsider k as Nat by A65;
A67:    k >= 1 by A65,FINSEQ_3:27;
A68:    pII.i = oI by A60,A62,A65,A66,A67,XXREAL_0:2;
        thus thesis by A33,A63,A68;
      end;
A69:  a <_f.pII, b by A58,A59,A60,A63;
A70:  b <_f.pII, a by A1,A64;
      thus contradiction by A69,A70,Th4;
    end;
    reconsider kI = kII - 1 as Nat by A61,NAT_1:20;
    reconsider kI as Element of NAT by ORDINAL1:def 13;
A71: kII = kI + 1;
A72: kI > 1 - 1 by A61,XREAL_1:11;
A73: kI >= 0 + 1 by A72,INT_1:20;
A74: kII <= kII' by A48,A58;
A75: kI <= kII' - 1 by A74,XREAL_1:11;
A76: kI in dom t by A73,A75,FINSEQ_3:27;
    reconsider nb = t.kI as Element of N by A76,PARTFUN1:27;
A77: kI + 0 < kII by A71,XREAL_1:10;
    consider pI such that
A78: for k st k in dom t & k < kI holds pI.(t.k) = oII and
A79: for k st k in dom t & k >= kI holds pI.(t.k) = oI and
A80: not(for a st a <> b holds a <_f.pI, b) by A58,A77;
    take nb,pI,pII;
    thus for i st i <> nb holds pI.i = pII.i
    proof
      let i;
      assume
A81:  i <> nb;
      consider k being set such that
A82:  k in dom t and
A83:  i = t.k by A31,FUNCT_1:def 5;
      reconsider k as Nat by A82;
      per cases by A81,A83,XXREAL_0:1;
      suppose
A84:    k < kI;
A85:    k + 0 < kII & pI.i = oII by A71,A78,A82,A83,A84,XREAL_1:10;
        thus thesis by A59,A82,A83,A85;
      end;
      suppose
A86:    k > kI;
A87:    k >= kII & pI.i = oI by A71,A79,A82,A83,A86,INT_1:20;
        thus thesis by A60,A82,A83,A87;
      end;
    end;
    thus
A88: for i holds extreme[pI.i,b]
    proof
      let i;
A89:  ex k being set st k in dom t & i = t.k by A31,FUNCT_1:def 5;
A90:  pI.i = oII or pI.i = oI by A78,A79,A89;
      thus thesis by A33,A34,A90;
    end;
    thus for i holds extreme[pII.i,b]
    proof
      let i;
A91:  ex k being set st k in dom t & i = t.k by A31,FUNCT_1:def 5;
A92:  pII.i = oII or pII.i = oI by A59,A60,A91;
      thus thesis by A33,A34,A92;
    end;
A93: pI.nb = oI by A76,A79;
    thus for a st a <> b holds b <_pI.nb, a by A33,A93;
A94: pII.nb = oII by A59,A76,A77;
    thus for a st a <> b holds a <_pII.nb, b by A34,A94;
    thus for a st a <> b holds b <_f.pI, a by A4,A80,A88;
    thus thesis by A58,A59,A60;
  end;
A95: for b holds ex nb,pI,pII st (for i st i <> nb holds pI.i = pII.i) &
  (for i holds extreme[pI.i,b]) &
  (for a st a <> b holds b <_f.pI, a) & (for a st a <> b holds a <_f.pII, b) &
  for p,a,c st a <> b & c <> b & a <_p.nb, c holds a <_f.p, c
  proof
    let b;
    consider nb,pI,pII such that
A96: for i st i <> nb holds pI.i = pII.i and
A97: for i holds extreme[pI.i,b] and
A98: for i holds extreme[pII.i,b] and
A99: for a st a <> b holds b <_pI.nb, a and
A100: for a st a <> b holds a <_pII.nb, b and
    A101: (
 for a st a <> b holds b <_f.pI, a)& for a st a <> b holds a <_f.pII, b
    by A30;
    take nb,pI,pII;
    thus (for i st i <> nb holds pI.i = pII.i) &
    (for i holds extreme[pI.i,b]) &
    (for a st a <> b holds b <_f.pI, a) & for a st a <> b holds a <_f.pII, b
    by A96,A97,A101;
    let p,a,c;
    assume that
A102: a <> b and
A103: c <> b and
A104: a <_p.nb, c;
A105: a <> c by A104,Th3;
    defpred P[Element of N,Element of LinPreorders A] means
    (a <_p.$1, c iff a <_$2, c) & (c <_p.$1, a iff c <_$2, a) &
    ($1 = nb implies a <_$2, b & b <_$2, c) & ($1 <> nb implies
    ((for d st d <> b holds b <_pII.$1, d) implies b <_$2, a & b <_$2, c) &
    ((for d st d <> b holds d <_pII.$1, b) implies a <_$2, b & c <_$2, b));
A106: for i holds ex o st P[i,o]
    proof
      let i;
      per cases;
      suppose
A107:   i = nb;
        consider o such that
A108:   a <_o, b & b <_o, c by A102,A103,A105,Th7;
        take o;
        thus thesis by A104,A107,A108,Th4,Th5;
      end;
      suppose
A109:   i <> nb;
        per cases by A98;
        suppose
A110:     for d st d <> b holds b <_pII.i, d;
A111:     b <_pII.i, a by A102,A110;
A112:     not a <_pII.i, b by A111,Th4;
          consider o such that
          A113:     b
 <_o, a & b <_o, c & ( a <_o, c iff a <_p.i, c)&( c <_o, a iff c <_p.i,
          a)
          by A102,A103,Th10;
          take o;
          thus thesis by A102,A109,A112,A113;
        end;
        suppose
A114:     for d st d <> b holds d <_pII.i, b;
A115:     a <_pII.i, b by A102,A114;
A116:     not b <_pII.i, a by A115,Th4;
          consider o such that
          A117:     a
 <_o, b & c <_o, b & ( a <_o, c iff a <_p.i, c)&( c <_o, a iff c <_p.i,
          a)
          by A102,A103,Th11;
          take o;
          thus thesis by A102,A109,A116,A117;
        end;
      end;
    end;
    consider pIII being Function of N,LinPreorders A such that
A118: for i holds P[i,pIII.i] from FUNCT_2:sch 3(A106);
    reconsider pIII as Element of Funcs(N,LinPreorders A) by FUNCT_2:11;
A119: for i holds (a <_pII.i, b iff a <_pIII.i, b) &
    (b <_pII.i, a iff b <_pIII.i, a)
    proof
      let i;
      per cases;
      suppose
A120:   i = nb;
A121:   a <_pII.i, b & a <_pIII.i, b by A100,A102,A118,A120;
        thus thesis by A121,Th4;
      end;
      suppose
A122:   i <> nb;
        per cases by A98;
        suppose
A123:     for d st d <> b holds b <_pII.i, d;
A124:     b <_pII.i, a & b <_pIII.i, a by A102,A118,A122,A123;
          thus thesis by A124,Th4;
        end;
        suppose
A125:     for d st d <> b holds d <_pII.i, b;
A126:     a <_pII.i, b & a <_pIII.i, b by A102,A118,A122,A125;
          thus thesis by A126,Th4;
        end;
      end;
    end;
A127: a <_f.pII, b iff a <_f.pIII, b by A2,A119;
    A128: for
 i holds (b <_pI.i, c iff b <_pIII.i, c) & (c <_pI.i, b iff c <_pIII.i, b )
    proof
      let i;
      per cases;
      suppose
A129:   i = nb;
A130:   b <_pI.i, c & b <_pIII.i, c by A99,A103,A118,A129;
        thus thesis by A130,Th4;
      end;
      suppose
A131:   i <> nb;
        per cases by A98;
        suppose
A132:     for d st d <> b holds b <_pII.i, d;
A133:     b <_pII.i, c by A103,A132;
A134:     b <_pI.i, c by A96,A131,A133;
A135:     b <_pIII.i, c by A118,A131,A132;
          thus thesis by A134,A135,Th4;
        end;
        suppose
A136:     for d st d <> b holds d <_pII.i, b;
A137:     c <_pII.i, b by A103,A136;
A138:     c <_pI.i, b by A96,A131,A137;
A139:     c <_pIII.i, b by A118,A131,A136;
          thus thesis by A138,A139,Th4;
        end;
      end;
    end;
A140: b <_f.pI, c iff b <_f.pIII, c by A2,A128;
A141: a <_f.pIII, c by A101,A102,A127,A140,Th5;
    thus thesis by A2,A118,A141;
  end;
  consider b;
  consider nb,pI,pII such that
A142: for i st i <> nb holds pI.i = pII.i and
A143: for i holds extreme[pI.i,b] and
  A144: (
 for a st a <> b holds b <_f.pI, a)& for a st a <> b holds a <_f.pII, b and
A145: for p,a,c st a <> b & c <> b & a <_p.nb, c holds a <_f.p, c by A95;
  take nb;
  let p,a,a';
  assume
A146: a <_p.nb, a';
A147: a <> a' by A146,Th4;
  per cases;
  suppose
A148: a <> b & a' <> b;
    thus thesis by A145,A146,A148;
  end;
  suppose
A149: a = b or a' = b;
    consider c such that
A150: c <> a & c <> a' by A3,Th2;
    consider nc,pI',pII' such that
    for i st i <> nc holds pI'.i = pII'.i and
    for i holds extreme[pI'.i,c] and
    for a st a <> c holds c <_f.pI', a and
    for a st a <> c holds a <_f.pII', c and
A151: for p,a,a' st a <> c & a' <> c & a <_p.nc, a' holds a <_f.p, a' by A95;
A152: nc = nb
    proof
      per cases by A149;
      suppose
A153:   a = b;
        assume
A154:   nc <> nb;
A155:   b <_pI.nc, a' or a' <_pI.nc, b by A143,A147,A153;
A156:   b <_pII.nc, a' & a' <_f.pII, b or
        a' <_pI.nc, b & b <_f.pI, a' by A142,A144,A147,A153,A154,A155;
A157:   b <_pII.nc, a' & a' <=_f.pII, b or
        a' <_pI.nc, b & b <=_f.pI, a' by A156,Th4;
        thus contradiction by A150,A151,A153,A157;
      end;
      suppose
A158:   a' = b;
        assume
A159:   nc <> nb;
A160:   b <_pI.nc, a or a <_pI.nc, b by A143,A147,A158;
A161:   b <_pII.nc, a & a <_f.pII, b or
        a <_pI.nc, b & b <_f.pI, a by A142,A144,A147,A158,A159,A160;
A162:   b <_pII.nc, a & a <=_f.pII, b or
        a <_pI.nc, b & b <=_f.pI, a by A161,Th4;
        thus contradiction by A150,A151,A158,A162;
      end;
    end;
    thus thesis by A146,A150,A151,A152;
  end;
end;

:: and then a stronger version

reserve o,o1 for Element of LinOrders A;
reserve o' for Element of LinPreorders A;
reserve p,p' for Element of Funcs(N,LinOrders A);
reserve q,q' for Element of Funcs(N,LinPreorders A);
reserve f for Function of Funcs(N,LinOrders A),LinPreorders A;

theorem
  (for p,a,b st for i holds a <_p.i, b holds a <_f.p, b) & (for p,p',a,b st
  for i holds a <_p.i, b iff a <_p'.i, b holds a <_f.p, b iff a <_f.p', b) &
  card A >= 3 implies ex n st for p,a,b holds a <_p.n, b iff a <_f.p, b
proof
  assume that
A1: for p,a,b st for i holds a <_p.i, b holds a <_f.p, b and
A2: for p,p',a,b st for i holds a <_p.i, b iff a <_p'.i, b
  holds a <_f.p, b iff a <_f.p', b and
A3: card A >= 3;
  consider o;
  defpred O[Element of LinPreorders A,Element of A,Element of A] means
  $2 <=_$1, $3 & ($2 <_$1, $3 or $2 <=_o, $3);
  defpred P[Element of LinPreorders A,Element of LinOrders A] means
  for a,b holds O[$1,a,b] iff a <=_$2, b;
A4: for o' ex o1 st P[o',o1]
  proof
    let o';
    defpred Q[Element of A,Element of A] means O[o',$1,$2];
    consider o1 being Relation of A such that
A5: for a,b holds [a,b] in o1 iff Q[a,b] from RELSET_1:sch 2;
A6: now
      let a,b;
A7:   Q[a,b] or Q[b,a] by Th4;
      thus [a,b] in o1 or [b,a] in o1 by A5,A7;
    end;
A8: now
      let a,b,c;
A9:   Q[a,b] & Q[b,c] implies Q[a,c] by Th5;
      thus [a,b] in o1 & [b,c] in o1 implies [a,c] in o1 by A5,A9;
    end;
    reconsider o1 as Element of LinPreorders A by A6,A8,Def1;
A10: now
      let a,b;
A11:  Q[a,b] & Q[b,a] implies a = b by Th6;
      thus [a,b] in o1 & [b,a] in o1 implies a = b by A5,A11;
    end;
    reconsider o1 as Element of LinOrders A by A10,Def2;
    take o1;
    let a,b;
A12: [a,b] in o1 iff Q[a,b] by A5;
    thus thesis by A12,Def4;
  end;
  defpred R[Element of Funcs(N,LinPreorders A),Element of Funcs(N,LinOrders A)]
  means for i holds P[$1.i,$2.i];
A13: for q,p,p' st R[q,p] & R[q,p'] holds p = p'
  proof
    let q,p,p';
    assume that
A14: R[q,p] and
A15: R[q,p'];
    let i;
    reconsider pi = p.i as Relation of A by Def1;
    reconsider pi' = p'.i as Relation of A by Def1;
A16: now
      let a,b;
A17:  O[q.i,a,b] iff a <=_p.i, b by A14;
A18:  O[q.i,a,b] iff a <=_p'.i, b by A15;
      thus [a,b] in p.i iff [a,b] in p'.i by A17,A18,Def4;
    end;
A19: pi = pi' by A16,RELSET_1:def 3;
    thus p.i = p'.i by A19;
  end;
A20: for q ex p st R[q,p]
  proof
    let q;
    defpred S[Element of N,Element of LinOrders A] means P[q.$1,$2];
A21: for i ex o1 st S[i,o1] by A4;
    consider p being Function of N,LinOrders A such that
A22: for i holds S[i,p.i qua Element of LinOrders A] from FUNCT_2:sch 3(A21);
    reconsider p as Element of Funcs(N,LinOrders A) by FUNCT_2:11;
    take p;
    thus thesis by A22;
  end;
  defpred T[Element of Funcs(N,LinPreorders A),Element of LinPreorders A] means
  ex p st R[$1,p] & f.p = $2;
A23: for q ex o' st T[q,o']
  proof
    let q;
    consider p such that
A24: R[q,p] by A20;
    take f.p;
    thus thesis by A24;
  end;
consider f' being Function of Funcs(N,LinPreorders A),LinPreorders A such that
A25: for q holds T[q,f'.q] from FUNCT_2:sch 3(A23);
A26: for q,a,b st for i holds a <_q.i, b holds a <_f'.q, b
  proof
    let q,a,b;
    assume
A27: for i holds a <_q.i, b;
    consider p such that
A28: R[q,p] and
A29: f.p = f'.q by A25;
A30: now
      let i;
A31:  not O[q.i,b,a] by A27;
      thus a <_p.i, b by A28,A31;
    end;
    thus thesis by A1,A29,A30;
  end;
A32: now
    let q,q',a,b;
    assume
A33: for i holds (a <_q.i, b iff a <_q'.i, b) & (b <_q.i, a iff b <_q'.i, a);
    consider p such that
A34: R[q,p] and
A35: f.p = f'.q by A25;
    consider p' such that
A36: R[q',p'] and
A37: f.p' = f'.q' by A25;
A38: for i holds a <_p.i, b iff a <_p'.i, b
    proof
      let i;
A39:  O[q.i,b,a] iff O[q'.i,b,a] by A33;
      thus thesis by A34,A36,A39;
    end;
    thus a <_f'.q, b iff a <_f'.q', b by A2,A35,A37,A38;
  end;
  consider n such that
A40: for q,a,b st a <_q.n, b holds a <_f'.q, b by A3,A26,A32,Th14;
  take n;
  let p;
A41: now
A42: rng p c= LinOrders A by RELAT_1:def 19;
A43: dom p = N & rng p c= LinPreorders A by A42,FUNCT_2:def 1,XBOOLE_1:1;
    reconsider q = p as Element of Funcs(N,LinPreorders A) by A43,FUNCT_2:def 2
    ;
A44: R[q,p]
    proof
      let i;
      let a,b;
A45:  a <_p.i, b or a = b or a >_p.i, b by Th6;
      thus thesis by A45,Th4;
    end;
A46: ex p' st ( R[q,p'])& f.p' = f'.q by A25;
    let a,b;
    assume
A47: a <_p.n, b;
A48: a <_f'.q, b by A40,A47;
    thus a <_f.p, b by A13,A44,A46,A48;
  end;
  thus thesis by A41,Th13;
end;

