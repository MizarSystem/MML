:: The Differentiable Functions on Normed Linear Spaces
::  by Hiroshi Imura , Morishige Kimura and Yasunari Shidama
::
:: Received May 24, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies PRE_TOPC, FCONT_1, SUBSET_1, RLVECT_1, FUNCT_1, ARYTM, ARYTM_1,
      ARYTM_3, RELAT_1, FUNCOP_1, ABSVALUE, LATTICES, SQUARE_1, ORDINAL2,
      SEQ_1, SEQ_2, SEQM_3, RCOMP_1, BOOLE, REALSET1, FINSEQ_4, PARTFUN1,
      PARTFUN2, NORMSP_1, RSSPACE, LOPBAN_1, LOPBAN_2, FDIFF_1, FRECHET2;
 notations TARSKI, XBOOLE_0, SUBSET_1, FUNCT_1, RELSET_1, PARTFUN1, FUNCT_2,
      PRE_TOPC, STRUCT_0, FUNCOP_1, BINOP_1, PARTFUN2, XCMPLX_0, XREAL_0,
      COMPLEX1, ORDINAL1, NUMBERS, REAL_1, NAT_1, FINSEQ_4, RLVECT_1, SEQ_1,
      SEQ_2, SEQM_3, REALSET2, SQUARE_1, VFUNCT_1, NORMSP_1, RSSPACE,
      BHSP_3, LOPBAN_1,
      LOPBAN_2, LOPBAN_3, NFCONT_1, FDIFF_1;
 constructors REAL_1, BINARITH, SQUARE_1, MEMBERED, DOMAIN_1, SEQ_2, PREPOWER,
      PARTFUN1, PARTFUN2, REALSET1, FINSEQ_4, VFUNCT_1, RSSPACE3, LOPBAN_3,
      NFCONT_1, FDIFF_1, COMPLEX1, REALSET2, NAT_1, BHSP_3;
 registrations RELSET_1, STRUCT_0, ORDINAL2, XREAL_0, MEMBERED, FUNCT_1, SEQ_1,
      SEQM_3, LOPBAN_1, LOPBAN_2, FDIFF_1;
 requirements SUBSET, REAL, BOOLE, NUMERALS, ARITHM;
 definitions TARSKI, XBOOLE_0, LOPBAN_3, NFCONT_1, NORMSP_1, BHSP_3;
 theorems AXIOMS, TARSKI, ABSVALUE, XBOOLE_0, XBOOLE_1, SUBSET_1, REAL_1,
      REAL_2, BINOP_1, RELSET_1, RLVECT_1, XCMPLX_0, XCMPLX_1, ZFMISC_1,
      XREAL_0, FUNCOP_1, SEQ_1, SEQ_2, NAT_1, SEQM_3, SQUARE_1, RELAT_1,
      FUNCT_1, FINSEQ_4, VFUNCT_1, FUNCT_2, SEQ_4, NORMSP_1, LOPBAN_1,
      LOPBAN_2, LOPBAN_3, ANPROJ_1, PARTFUN1, PARTFUN2, NFCONT_1, FDIFF_1,
      BHSP_3;
 schemes NAT_1, SEQ_1, RECDEF_1, FUNCT_2;

begin :: Differentiable Function on Normed Linear Spaces

 reserve n,k for Nat;
 reserve x,y,X,X1,Z,Z1 for set;
 reserve s,g,r,p for Real;
 reserve S,T for RealNormSpace;
 reserve f,f1,f2 for PartFunc of S,T;
 reserve rseq,rseq1,rseq2 for Real_Sequence;
 reserve s1,s2,q1,seq,seq1,seq2 for sequence of S;
 reserve x0,x1,x2 for Point of S;
 reserve Y for Subset of S;

::::: Normed Linear Spaces varsions of RCOMP_1:37 -

::: RCOMP_1:37 --> NFCONT_1:4

theorem Th1:
 for x0 be Point of S
    for N1,N2 being Neighbourhood of x0
 ex N being Neighbourhood of x0 st N c= N1 & N c= N2
 proof
 let x0 be Point of S;
  let N1,N2 be Neighbourhood of x0;
  consider g1 be Real such that A1: 0<g1
  & {y where y is Point of S: ||.y-x0 .|| < g1} c= N1 by NFCONT_1:def 3;
  consider g2 be Real such that A2: 0<g2
  & {y where y is Point of S: ||.y-x0 .|| < g2} c= N2 by NFCONT_1:def 3;
  set g = min(g1,g2);
  A3: 0<g by A1,A2,SQUARE_1:38;
  A4: g<=g2 & g<=g1 by SQUARE_1:35;
  A5:  {y where y is Point of S: ||.y-x0 .|| < g}
     c=  {y where y is Point of S: ||.y-x0 .|| < g1}
   proof
   let z be set such that
     A6: z in  {y where y is Point of S: ||.y-x0 .|| < g};
     consider y be Point of S such that
     A7: z=y &  ||.y-x0 .|| < g by A6;
      ||.y-x0 .|| < g1 by A4,A7,AXIOMS:22;
     hence
     z in {y1 where y1 is Point of S: ||.y1-x0 .|| < g1} by A7;
   end;
A8:  {y where y is Point of S: ||.y-x0 .|| < g}
     c= {y where y is Point of S: ||.y-x0 .|| < g2} proof
   let z be set such that
     A9: z in {y where y is Point of S: ||.y-x0 .|| < g};
     consider y be Point of S such that
     A10: z=y & ||.y-x0 .|| < g by A9;
      ||.y-x0 .|| < g2 by A4,A10,AXIOMS:22;
     hence z in {y1 where y1 is Point of S: ||.y1-x0 .|| < g2} by A10;
   end;
  take N={y where y is Point of S: ||.y-x0 .|| < g};
  thus thesis by A1,A2,A3,A5,A8,NFCONT_1:3,XBOOLE_1:1;
 end;

theorem Th2:
for X being Subset of S st X is open
for r be Point of S st r in X
ex N being Neighbourhood of r st N c= X
proof
let X be Subset of S such that
 A1: X is open;
 let r be Point of S;
 assume that
      A2: r in X and
      A3: for N be Neighbourhood of r holds not N c= X;
  A4: now
       let g be Real such that A5: 0<g;
       set N={y where y is Point of S:||.y-r.|| < g};
       N is  Neighbourhood of r by A5,NFCONT_1:3;
       then not N c= X by A3;
       then consider x be set such that
        A6: x in N & not x in X by TARSKI:def 3;
        consider s be Point of S such that
A7:     x=s & ||.s-r.|| <g by A6;
        take s;
        thus s in N by A7;
        s in (the carrier of S) \ X by A6,A7,XBOOLE_0:def 4;
        hence s in X` by SUBSET_1:def 5;
    end;
   defpred P[Nat,Point of S] means
              $2 in {y where y is Point of S:||.y-r.|| < 1/($1+1)}
           & $2 in X`;
A8: for n ex s be Point of S st P[n,s]
  proof
   let n;
   set N= {y where y is Point of S:||.y-r.|| < 1/(n+1)};
    0 <= n by NAT_1:18; then
    0+0 < 1+ n by REAL_1:67;
   then 0 < 1 * (n + 1)" by REAL_1:72;
   then 0 < 1/(n + 1) by XCMPLX_0:def 9;
   hence thesis by A4;
  end;
consider s1 be sequence of S such that
 A9:  for n being Nat holds P[n,s1.n] from FUNCT_2:sch 3(A8);
A10: now
       let p be Real; assume 0<p;
 then A11: 0<p" by REAL_1:72;
      consider n such that A12: p"<n by SEQ_4:10;
       p" + 0 < n + 1 by A12,REAL_1:67;
     then 1/(n+1) < 1/p" by A11,SEQ_2:10;
 then A13: 1/(n+1) < p by XCMPLX_1:218;
     take n;
     let m be Nat; assume n<=m;
 then A14: n + 1 <= m + 1 by AXIOMS:24;
   0 <= n by NAT_1:18; then
   0+0 < 1+ n by REAL_1:67;
 then A15: 1/(m+1) <= 1/(n+1) by A14,SEQ_4:1;
        s1.m in {y where y is Point of S:||.y-r.|| < 1/(m+1)} by A9; then
                consider y be Point of S such that
                A16: s1.m=y & ||.y-r.|| < 1/(m+1);
||.s1.m - r.|| <  1/(n+1) by A15,A16,AXIOMS:22;
    hence ||.s1.m - r.|| <p by A13,AXIOMS:22;
    end;
then A17: s1 is convergent by NORMSP_1:def 9;
then A18: lim s1 = r by A10,NORMSP_1:def 11;
A19: rng s1 c= X`
    proof
     let x; assume x in rng s1;
     then consider y be set such that
A20: y in dom s1 and
A21: s1.y=x by FUNCT_1:def 5;
     reconsider y as Nat by A20;
     s1.y in X` by A9;
     hence x in X` by A21;
    end;
    X` is closed by A1,NFCONT_1:def 6;
   then r in X` by A17,A18,A19,NFCONT_1:def 5;
   then r in (the carrier of S) \ X by SUBSET_1:def 5;
   hence contradiction by A2,XBOOLE_0:def 4;
end;

theorem
for X being Subset of S st X is open
for r be Point of S st r in X holds
ex g st 0<g & {y where y is Point of S:||.y-r.|| < g} c= X
proof
let X be Subset of S such that A1: X is open;
let r be Point of S;
 assume r in X;
 then consider N be Neighbourhood of r such that A2: N c= X
 by A1,Th2;
 consider g such that A3: 0<g
 & {y where y is Point of S:||.y-r.|| < g}  c= N by NFCONT_1:def 3;
 take g;
 thus thesis by A2,A3,XBOOLE_1:1;
end;

theorem Th4:
for X being Subset of S holds
((for r be Point of S
  st r in X holds ex N be Neighbourhood of r st N c= X)
   implies X is open )
 proof
 let X be Subset of S;
 assume that
A1: for r be Point of S st r in X
  holds ex N be Neighbourhood of r st N c= X and
A2: not X is open;
     not X` is closed by A2,NFCONT_1:def 6;
 then consider s1 be sequence of S such that
A3: rng s1 c= X` & s1 is convergent & not lim s1 in X` by NFCONT_1:def 5;
    lim s1 in X by A3,SUBSET_1:50;
  then consider N be Neighbourhood of (lim s1) such that A4: N c= X by A1;
  consider g be Real such that A5: 0<g and
A6: {y where y is Point of S:||.y-(lim s1).|| < g} c= N by NFCONT_1:def 3;
  consider n such that
A7: for m be Nat st n<=m holds ||.(s1.m) - (lim s1).||<g
  by A3,A5,NORMSP_1:def 11;
    ||.s1.n - (lim s1).|| <g by A7; then
   s1.n in {y where y is Point of S:||.y-(lim s1).|| < g}; then
A8:   s1.n in N by A6;
 n in NAT;
 then n in dom s1 by FUNCT_2:def 1;
 then s1.n in rng s1 by FUNCT_1:def 5;
  hence contradiction by A3,A4,A8,SUBSET_1:54;
 end;

theorem
for X being Subset of S holds
( (for r be Point of S st r in X holds ex N
be Neighbourhood of r st N c= X) iff X is open)
by Th2,Th4;

::::: Normed Linear Spaces versions of SEQ_1: -

definition let S be ZeroStr;
  let f be sequence of S;
  attr f is being_not_0 means :Def1:
  rng f c= (the carrier of S) \ {0.S};
end;

notation let S be ZeroStr;
  let f be sequence of S;
  synonym f is_not_0 for f is being_not_0;
end;

theorem Th6:
  seq is being_not_0 iff for x st x in NAT holds seq.x<>0.S
  proof
  thus seq is being_not_0 implies for x st x in NAT holds seq.x<>0.S
  proof
   assume seq is being_not_0; then
A1: rng seq c= (the carrier of S) \{0.S} by Def1;
   let x;
   assume x in NAT;
   then x in dom seq by FUNCT_2:def 1;
   then seq.x in rng seq by FUNCT_1:def 5;
   then not seq.x in {0.S} by A1,XBOOLE_0:def 4;
   hence seq.x<>0.S by TARSKI:def 1;
  end;
  assume
A2: for x st x in NAT holds seq.x<>0.S;
     now let y;
    assume
    A3: y in rng seq;
    then consider x such that
A4: x in dom seq and
A5: seq.x=y by FUNCT_1:def 5;
     y<>0.S by A2,A4,A5; then
    not y in {0.S} by TARSKI:def 1;
    hence y in (the carrier of S) \{0.S} by A3,XBOOLE_0:def 4;
   end;
  then rng seq c=(the carrier of S) \{0.S} by TARSKI:def 3;
  hence thesis by Def1;
 end;

theorem Th7:
  seq is being_not_0 iff for n holds seq.n<>0.S
  proof
thus seq is being_not_0 implies for n holds seq.n<>0.S by Th6;
  assume for n holds seq.n<>0.S;
   then for x holds x in NAT implies seq.x<>0.S;
  hence thesis by Th6;
 end;

definition let RNS be RealLinearSpace;
          let S be sequence of RNS;
          let a be Real_Sequence;
          func a (#) S -> sequence of RNS means :Def2:
  for n holds it.n = a.n * S.n;
existence proof
  deffunc F(Nat) = a.$1 * S.$1;
  thus ex S being sequence of RNS st for n
  holds S.n = F(n) from FUNCT_2:sch 4;
end;
uniqueness
proof
     let S1, S2 be sequence of RNS;
     assume that
A1:  ( for n holds S1.n = a.n * S.n ) and
A2:  ( for n holds S2.n = a.n * S.n );
       for n holds S1.n = S2.n
     proof
          let n;
         S1.n = a.n * S.n by A1;
          hence thesis by A2;
     end;
    hence thesis by FUNCT_2:113;
end;
end;

definition let RNS be RealLinearSpace;
           let z be Point of RNS;
           let a be Real_Sequence;
           func a * z -> sequence of RNS means :Def3:
for n holds it.n = a.n * z;
existence proof
  deffunc F(Nat) = a.$1 * z;
  thus ex S being sequence of RNS st for n
  holds S.n = F(n) from FUNCT_2:sch 4;
end;
uniqueness
proof
     let S1, S2 be sequence of RNS;
     assume that
A1:  ( for n holds S1.n = a.n * z ) and
A2:  ( for n holds S2.n = a.n * z );
       for n holds S1.n = S2.n
     proof
       let n;
       S1.n = a.n * z by A1;
       hence thesis by A2;
     end;
    hence thesis by FUNCT_2:113;
end;
end;

theorem
  for rseq1,rseq2 be Real_Sequence holds
  (rseq1+rseq2)(#)seq=rseq1(#)seq+rseq2(#)seq
  proof
  let rseq1,rseq2 be Real_Sequence;
  now let n;
    thus ((rseq1+rseq2)(#)seq).n
      =(rseq1+rseq2).n*seq.n by Def2
     .=(rseq1.n+rseq2.n)*seq.n by SEQ_1:11
     .=rseq1.n*seq.n+rseq2.n*seq.n by RLVECT_1:def 9
     .=(rseq1(#)seq).n+rseq2.n*seq.n by Def2
     .=(rseq1(#)seq).n+(rseq2(#)seq).n by Def2
     .=((rseq1(#)seq)+(rseq2(#)seq)).n by NORMSP_1:def 5;
   end;
  hence thesis by FUNCT_2:113;
 end;

theorem Th9:
  for rseq be Real_Sequence
  for seq1, seq2 be sequence of S holds
  rseq(#)(seq1+seq2)=rseq(#)seq1+rseq(#)seq2
  proof
  let rseq be Real_Sequence;
  let seq1, seq2 be sequence of S;
  now let n;
    thus (rseq(#)(seq1+seq2)).n
      =rseq.n*(seq1+seq2).n by Def2
     .=rseq.n*(seq1.n+seq2.n ) by NORMSP_1:def 5
     .=rseq.n*seq1.n+rseq.n*seq2.n by RLVECT_1:def 9
     .=(rseq(#)seq1).n+rseq.n*seq2.n by Def2
     .=(rseq(#)seq1).n+(rseq(#)seq2).n by Def2
     .=((rseq(#)seq1)+(rseq(#)seq2)).n by NORMSP_1:def 5;
   end;
  hence thesis by FUNCT_2:113;
 end;

theorem Th10:
  for rseq be Real_Sequence holds
   r*(rseq(#)seq) =rseq(#)(r*seq)
  proof
  let rseq be Real_Sequence;
  now let n;
    thus (r*(rseq(#)seq)).n
     =r*(rseq(#)seq).n by NORMSP_1:def 8
     .=r*(rseq.n*seq.n) by Def2
     .=(r*rseq.n)*seq.n by RLVECT_1:def 9
     .=rseq.n*(r*seq.n) by RLVECT_1:def 9
     .=rseq.n*(r*seq).n by NORMSP_1:def 8
     .=(rseq(#)(r*seq)).n by Def2;
   end;
  hence thesis by FUNCT_2:113;
 end;

theorem
  for rseq1,rseq2 be Real_Sequence holds
  (rseq1-rseq2)(#)seq=rseq1(#)seq-rseq2(#)seq
  proof
  let rseq1,rseq2 be Real_Sequence;
  now let n;
    thus ((rseq1-rseq2)(#)seq).n
      =(rseq1-rseq2).n*seq.n by Def2
      .=(rseq1+-rseq2).n*seq.n by SEQ_1:15
      .=(rseq1.n+(-rseq2).n)*seq.n by SEQ_1:11
     .=(rseq1.n+-rseq2.n)*seq.n by SEQ_1:14
     .=(rseq1.n-rseq2.n)*seq.n
     .=rseq1.n*seq.n-rseq2.n*seq.n by RLVECT_1:49
     .=(rseq1(#)seq).n-rseq2.n*seq.n by Def2
     .=(rseq1(#)seq).n-(rseq2(#)seq).n by Def2
     .=((rseq1(#)seq)-(rseq2(#)seq)).n by NORMSP_1:def 6;
   end;
  hence thesis by FUNCT_2:113;
 end;

theorem Th12:
  for rseq be Real_Sequence
  for seq1, seq2 be sequence of S
  holds
  rseq(#)(seq1-seq2)=rseq(#)seq1-rseq(#)seq2
  proof
  let rseq be Real_Sequence;
  let seq1, seq2 be sequence of S;
  now let n;
    thus (rseq(#)(seq1-seq2)).n
      =rseq.n*(seq1-seq2).n by Def2
     .=rseq.n*(seq1.n-seq2.n ) by NORMSP_1:def 6
     .=rseq.n*seq1.n-rseq.n*seq2.n by RLVECT_1:48
     .=(rseq(#)seq1).n-rseq.n*seq2.n by Def2
     .=(rseq(#)seq1).n-(rseq(#)seq2).n by Def2
     .=((rseq(#)seq1)-(rseq(#)seq2)).n by NORMSP_1:def 6;
   end;
  hence thesis by FUNCT_2:113;
 end;

theorem Th13:
  rseq is convergent & seq is convergent implies
                   rseq (#) seq is convergent
   proof
    assume that
 A1: rseq is convergent and
 A2: seq is convergent;
    consider g1 be real number such that
 A3: for p be real number st 0<p ex n be Nat st for m be Nat
 st n<=m holds abs(rseq.m-g1)<p
    by A1,SEQ_2:def 6;
    reconsider g1 as Real by XREAL_0:def 1;
    consider g2 be Point of S such that
 A4: for p be Real st 0<p ex n be Nat st for m be Nat
 st n<=m holds ||.seq.m-g2.||<p by A2,NORMSP_1:def 9;
    take g=g1*g2;
      rseq is bounded by A1,SEQ_2:27;
    then consider r be real number such that
 A5: 0<r and
 A6: for n be Nat holds abs(rseq.n)<r by SEQ_2:15;
  reconsider r as Real by XREAL_0:def 1;
 A7: 0<=||.g2.|| by NORMSP_1:8;
 then A8:   0+0<||.g2.||+r by A5,REAL_1:67;
    let p; assume 0<p;
 then A9: 0<p/(||.g2.||+r) by A8,SEQ_2:6;
    then consider n1 be Nat such that
 A10: for m be Nat st n1<=m holds abs(rseq.m-g1)<p/(||.g2.||+r) by A3;
    consider n2 be Nat such that
 A11: for m be Nat st n2<=m holds ||.seq.m-g2.||<p/(||.g2.||+r) by A4,A9;
    take n=n1+n2;
    let m be Nat such that
 A12:  n<=m;
 A13: 0<=abs(rseq.m) by ABSVALUE:5;
 A14: 0<=||.seq.m-g2.|| by NORMSP_1:8;
      n2<=n by NAT_1:37;
    then n2<=m by A12,AXIOMS:22;
 then A15: ||.seq.m-g2.||<p/(||.g2.||+r) by A11;
      n1<=n1+n2 by NAT_1:37;
    then n1<=m by A12,AXIOMS:22;
 then A16: abs(rseq.m-g1)<=p/(||.g2.||+r) by A10;
      ||.((rseq(#)seq).m)-g.||
       =||.rseq.m*seq.m-g1*g2.|| by Def2
      .=||.rseq.m*seq.m-0.S-g1*g2.|| by RLVECT_1:26
      .=||.rseq.m*seq.m-(rseq.m*g2-rseq.m*g2)-g1*g2.|| by RLVECT_1:28
      .=||.(rseq.m*seq.m-rseq.m*g2+rseq.m*g2)-g1*g2.|| by RLVECT_1:43
      .=||.rseq.m*(seq.m-g2)+rseq.m*g2-g1*g2.|| by RLVECT_1:48
      .=||.rseq.m*(seq.m-g2)+(rseq.m*g2-g1*g2).|| by RLVECT_1:42
      .=||.rseq.m*(seq.m-g2)+(rseq.m-g1)*g2.|| by RLVECT_1:49;
 then A17: ||.((rseq(#)seq).m)-g.||<=
      ||.rseq.m*(seq.m-g2).||+||.(rseq.m-g1)*g2.|| by NORMSP_1:def 2;
      abs(rseq.m)<r by A6;
    then abs(rseq.m)*||.seq.m-g2.||<r*(p/(||.g2.||+r)) by A13,A14,A15,SEQ_2:7;
 then A18: ||.rseq.m*(seq.m-g2).||<r*(p/(||.g2.||+r)) by NORMSP_1:def 2;
      ||.(rseq.m-g1)*g2.||=||.g2.||*abs(rseq.m-g1) by NORMSP_1:def 2;
 then A19:  ||.(rseq.m-g1)*g2.||<=||.g2.||*(p/(||.g2.||+r))
 by A7,A16,AXIOMS:25;
      r*(p/(||.g2.||+r))+||.g2.||*(p/(||.g2.||+r))
      =(p/(||.g2.||+r))*(||.g2.||+r)
     .=p by A8,XCMPLX_1:88;
    then ||.rseq.m*(seq.m-g2).||+||.(rseq.m-g1)*g2.||<p by A18,A19,REAL_1:67;
    hence ||.((rseq(#)seq).m)-g.||<p by A17,AXIOMS:22;
  end;

theorem Th14:
  rseq is convergent & seq is convergent implies
                 lim (rseq (#) seq) = (lim rseq) * (lim seq)
   proof
    assume that
 A1: rseq is convergent and
 A2: seq is convergent;
    reconsider g1 = lim rseq as Real;
 set g2= lim seq;
 set g=g1*g2;
      rseq is bounded by A1,SEQ_2:27;
    then consider r be real number such that
 A3: 0<r and
 A4: for n be Nat holds abs(rseq.n)<r by SEQ_2:15;
  reconsider r as Real by XREAL_0:def 1;
 A5: 0<=||.g2.|| by NORMSP_1:8;
 then A6:   0+0<||.g2.||+r by A3,REAL_1:67;
 A7:  (rseq (#) seq) is convergent by A1,A2,Th13;
 for p be Real st 0<p
   ex n be Nat st for m be Nat st n <=m  holds
       ||.((rseq(#)seq).m)-g.||<p
   proof
    let p be Real; assume 0<p;
 then A8: 0<p/(||.g2.||+r) by A6,SEQ_2:6;
    then consider n1 be Nat such that
 A9: for m be Nat st n1<=m holds abs(rseq.m-g1)<p/(||.g2.||+r)
  by A1,SEQ_2:def 7;
    consider n2 be Nat such that
 A10: for m be Nat st n2<=m holds ||.seq.m-g2.||<p/(||.g2.||+r)
  by A2,A8,NORMSP_1:def 11;
    take n=n1+n2;
    let m be Nat such that
 A11:  n<=m;
 A12: 0<=abs(rseq.m) by ABSVALUE:5;
 A13: 0<=||.seq.m-g2.|| by NORMSP_1:8;
      n2<=n by NAT_1:37;
    then n2<=m by A11,AXIOMS:22;
 then A14: ||.seq.m-g2.||<p/(||.g2.||+r) by A10;
      n1<=n1+n2 by NAT_1:37;
    then n1<=m by A11,AXIOMS:22;
 then A15: abs(rseq.m-g1)<=p/(||.g2.||+r) by A9;
      ||.((rseq(#)seq).m)-g.||
       =||.rseq.m*seq.m-g1*g2.||
     by Def2
      .=||.rseq.m*seq.m-0.S-g1*g2.||
       by RLVECT_1:26
      .=||.rseq.m*seq.m-(rseq.m*g2-rseq.m*g2)-g1*g2.|| by RLVECT_1:28
      .=||.(rseq.m*seq.m-rseq.m*g2+rseq.m*g2)-g1*g2.|| by RLVECT_1:43
      .=||.rseq.m*(seq.m-g2)+rseq.m*g2-g1*g2.|| by RLVECT_1:48
      .=||.rseq.m*(seq.m-g2)+(rseq.m*g2-g1*g2).|| by RLVECT_1:42
      .=||.rseq.m*(seq.m-g2)+(rseq.m-g1)*g2.|| by RLVECT_1:49;
 then A16: ||.((rseq(#)seq).m)-g.||<=
      ||.rseq.m*(seq.m-g2).||+||.(rseq.m-g1)*g2.|| by NORMSP_1:def 2;
      abs(rseq.m)<r by A4;
    then abs(rseq.m)*||.seq.m-g2.||<r*(p/(||.g2.||+r)) by A12,A13,A14,SEQ_2:7;
 then A17: ||.rseq.m*(seq.m-g2).||<r*(p/(||.g2.||+r)) by NORMSP_1:def 2;
      ||.(rseq.m-g1)*g2.||=||.g2.||*abs(rseq.m-g1) by NORMSP_1:def 2;
 then A18:  ||.(rseq.m-g1)*g2.||<=||.g2.||*(p/(||.g2.||+r))
 by A5,A15,AXIOMS:25;
      r*(p/(||.g2.||+r))+||.g2.||*(p/(||.g2.||+r))
      =(p/(||.g2.||+r))*(||.g2.||+r)
     .=p by A6,XCMPLX_1:88;
    then ||.rseq.m*(seq.m-g2).||+||.(rseq.m-g1)*g2.||<p by A17,A18,REAL_1:67;
    hence ||.((rseq(#)seq).m)-g.||<p by A16,AXIOMS:22;
 end;
    hence thesis by A7,NORMSP_1:def 11;
 end;

theorem Th15:
 (seq+seq1) ^\k=(seq ^\k) +(seq1 ^\k)
  proof
     now let n;
     thus ((seq+seq1) ^\k).n=(seq+seq1).(n+k) by LOPBAN_3:def 6
      .=seq.(n+k) + seq1.(n+k) by NORMSP_1:def 5
      .=(seq ^\k).n +seq1.(n+k) by LOPBAN_3:def 6
      .=(seq ^\k).n +(seq1 ^\k).n by LOPBAN_3:def 6
      .=((seq ^\k) +(seq1 ^\k)).n by NORMSP_1:def 5;
    end;
   hence thesis by FUNCT_2:113;
  end;

theorem Th16:
 (seq-seq1) ^\k=(seq ^\k) -(seq1 ^\k)
  proof
     now let n;
     thus ((seq-seq1) ^\k).n=(seq-seq1).(n+k) by LOPBAN_3:def 6
      .=seq.(n+k) - seq1.(n+k) by NORMSP_1:def 6
      .=(seq ^\k).n -seq1.(n+k) by LOPBAN_3:def 6
      .=(seq ^\k).n -(seq1 ^\k).n by LOPBAN_3:def 6
      .=((seq ^\k) -(seq1 ^\k)).n by NORMSP_1:def 6;
    end;
   hence thesis by FUNCT_2:113;
  end;

theorem Th17:
  seq is_not_0 implies seq ^\k is_not_0
   proof
    assume A1:seq is_not_0;
       now let n;
        (seq ^\k).n=seq.(n+k) by LOPBAN_3:def 6;
      hence (seq ^\k).n<>0.S by A1,Th7;
     end;
    hence thesis by Th7;
   end;

theorem Th18:
  seq ^\k is subsequence of seq
  proof deffunc U(Nat) = $1+k;
   consider seq1 be Real_Sequence such that
A1:for n holds seq1.n=U(n) from SEQ_1:sch 1;
      now let n;
     A2:seq1.n=n+k & seq1.(n+1)=n+1+k by A1;
       n+k<n+k+1 by NAT_1:38;
     hence seq1.n<seq1.(n+1) by A2;
    end;
   then A3:seq1 is increasing by SEQM_3:def 11;
      now let n;
       n+k is Nat;
     hence seq1.n is Nat by A1;
    end;
   then reconsider seq1 as increasing Seq_of_Nat by A3,SEQM_3:29;
   take Nseq=seq1;
      now let n;
     thus (seq*Nseq).n=seq.(Nseq.n) by LOPBAN_3:7
       .=seq.(n+k) by A1
       .=(seq ^\k).n by LOPBAN_3:def 6;
    end;
   hence thesis by FUNCT_2:113;
  end;

theorem Th19:
   seq is constant & seq1 is subsequence of seq implies
       seq1 is constant
 proof
  assume that A1:seq is constant and
              A2:seq1 is subsequence of seq;
  consider Nseq be increasing Seq_of_Nat such that
A3:seq1=seq*Nseq by A2,BHSP_3:def 4;
    consider r be Point of S such that
       A4: for n be Nat holds seq.n=r by A1,LOPBAN_3:def 5;
     now let n;
      thus seq1.n=seq.(Nseq.n) by A3,LOPBAN_3:7
              .=r by A4;
   end;
  hence thesis by LOPBAN_3:def 5;
 end;

theorem Th20:
   seq is constant & seq1 is subsequence of
   seq implies seq=seq1
  proof
    assume that
A1: seq is constant and
A2: seq1 is subsequence of seq;
    consider Nseq be increasing Seq_of_Nat such that
A3: seq1=seq*Nseq by A2,BHSP_3:def 4;
 consider r be Point of S such that
    A4: for n be Nat holds seq.n=r by A1,LOPBAN_3:def 5;
          now let n;
      thus seq1.n=seq.(Nseq.n) by A3,LOPBAN_3:7
                      .=r by A4
                      .=seq.n by A4;
     end;
    hence thesis by FUNCT_2:113;
  end;

definition let S;
  let IT be sequence of S;
  attr IT is convergent_to_0 means:Def4:
      IT is_not_0 & IT is convergent & lim IT = 0.S;
end;

theorem Th21:
for X be RealNormSpace
  for seq be sequence of X holds
  seq is constant implies
      ( seq is convergent
     & for k be Nat holds lim seq = seq.k )
  proof
  let X be RealNormSpace;
  let seq be sequence of X;
   assume
A1:  seq is constant;
   hence
   A2: seq is convergent by LOPBAN_3:17;
   consider r be Point of X such that
   A3:for n be Nat holds seq.n=r by A1,LOPBAN_3:def 5;
   now let k be Nat;
     thus lim seq = seq.k
      proof
        now let p be Real such that A4:0<p;
           take n=0;
           let m be Nat such that n<=m;
           ||.(seq.m)-(seq.k).||=||.r-(seq.k).|| by A3
           .=||.r-r.|| by A3
           .=||.0.X.|| by RLVECT_1:28
           .=0 by NORMSP_1:5;
          hence ||.(seq.m)-(seq.k).||<p by A4;
       end;
    hence thesis by A2,NORMSP_1:def 11;
    end;
end;
hence thesis;
end;

theorem Th22:
  for r be Real st ( 0<r & (for n holds seq.n=(1/(n+r))*x0))
   holds seq is convergent
  proof
  let r be Real;
   assume that A1:0<r and
               A2:for n holds seq.n=(1/(n+r))*x0;
   take g=0.S;
   let p be Real;
   assume A3: 0<p;
   ex pp be Real st pp > 0 & pp*||.x0.|| < p
   proof
       take pp=p/(||.x0.||+1);
      A4: 0 <= ||.x0.|| by NORMSP_1:8;
      A5: ||.x0.||+1 > 0+0 by A4,REAL_1:67;
      then
      A6:  0  <  p/(  ||.x0.||+1 ) by A3,SEQ_2:6;
      A7: ||.x0.||+0 < ||.x0.||+1 by REAL_1:67;
      0 <=  ||.x0.|| by NORMSP_1:8;
      then
      pp* ||.x0.|| < pp*(||.x0.|| + 1) by A6,A7,SEQ_4:6;
      hence   pp > 0 & pp* ||.x0.|| < p by A3,A5,SEQ_2:6,XCMPLX_1:88;
  end;
  then consider  pp be Real such that
     A8:   pp > 0  & pp*||.x0.|| < p;

  A9:0<pp" by A8,REAL_1:72;
   consider k1 be Nat such that A10:pp"<k1 by SEQ_4:10;
   take n=k1;
   let m be Nat such that A11:n<=m;
     pp"+0<k1+r by A1,A10,REAL_1:67;
   then 1/(k1+r)<1/pp" by A9,SEQ_2:10;
   then A12: 1/(k1+r)<1*pp"" by XCMPLX_0:def 9;
   0<n by A9,A10;
   then A13: 0+0<n+r by A1,REAL_1:67;
     n+r<=m+r by A11,AXIOMS:24;
   then 1/(m+r)<=1/(n+r) by A13,SEQ_4:1;
   then A14:1/(m+r)<pp by A12,AXIOMS:22;
     0<=m by NAT_1:18;
   then 0+0<m+r by A1,REAL_1:67;
   then A15: 0/(m+r)<1/(m+r) by REAL_1:73;
   A16: ||.seq.m-g.|| = ||.(1/(m+r))*x0  - 0.S.|| by A2
                   .= ||.(1/(m+r))*x0.|| by RLVECT_1:26
                   .= abs(1/(m+r))*||.x0.|| by NORMSP_1:def 2
                   .= (1/(m+r))*||.x0.|| by A15,ABSVALUE:def 1;
    0 <= ||.x0.|| by NORMSP_1:8;
    then
  (1/(m+r))*||.x0.|| <= pp* ||.x0.||
     by A14,AXIOMS:25;
   hence ||.seq.m-g.||<p by A8,A16,AXIOMS:22;
  end;

theorem Th23:
  for r be Real st
   (  0<r & (for n holds seq.n=(1/(n+r))*x0 ))
     holds lim seq=0.S
  proof
   let r be Real;
   assume that A1:0<r and
               A2:for n holds seq.n=1/(n+r)*x0;
   A3:seq is convergent by A1,A2,Th22;
      now let p;
      assume A4: 0<p;
   ex pp be Real st pp > 0 & pp*||.x0.|| < p
    proof
        take pp=p/(||.x0.||+1);
       A5: 0 <= ||.x0.|| by NORMSP_1:8;
       A6: ||.x0.||+1 > 0+0 by A5,REAL_1:67; then
       A7:  0 < p/(||.x0.||+1) by A4,SEQ_2:6;
       A8: ||.x0.||+0 < ||.x0.||+1 by REAL_1:67;
       0 <= ||.x0.|| by NORMSP_1:8; then
       pp* ||.x0.|| < pp*(||.x0.|| + 1) by A7,A8,SEQ_4:6;
       hence pp > 0 & pp* ||.x0.|| < p by A4,A6,SEQ_2:6,XCMPLX_1:88;
   end;
   then consider pp be Real such that
      A9: pp > 0 & pp*||.x0.|| < p;
     A10:0<pp" by A9,REAL_1:72;
     consider k1 be Nat such that
      A11:pp"<k1 by SEQ_4:10;
     take n=k1;
      let m be Nat such that A12:n<=m;
         pp"+0<k1+r by A1,A11,REAL_1:67;
       then 1/(k1+r)<1/pp" by A10,SEQ_2:10;
then A13:    1/(k1+r)<1*pp"" by XCMPLX_0:def 9;
      0<n by A10,A11;
then A14:       0+0<n+r by A1,REAL_1:67;
         n+r<=m+r by A12,AXIOMS:24;
       then 1/(m+r)<=1/(n+r) by A14,SEQ_4:1;
       then A15:1/(m+r)<pp by A13,AXIOMS:22;
         0<=m by NAT_1:18;
       then 0+0<m+r by A1,REAL_1:67;
       then A16:0/(m+r)<1/(m+r) by REAL_1:73;
   A17: ||.seq.m-0.S.|| = ||.(1/(m+r))*x0  - 0.S.|| by A2
                   .= ||.(1/(m+r))*x0.|| by RLVECT_1:26
                   .= abs(1/(m+r))*||.x0.|| by NORMSP_1:def 2
                   .= (1/(m+r))*||.x0.|| by A16,ABSVALUE:def 1;
    0 <=  ||.x0.|| by NORMSP_1:8;
    then (1/(m+r))*||.x0.|| <= pp* ||.x0.||
     by A15,AXIOMS:25;
   hence ||.seq.m-0.S.||<p by A9,A17,AXIOMS:22;
      end;
   hence thesis by A3,NORMSP_1:def 11;
  end;

theorem
  for a be convergent_to_0 Real_Sequence
  for z be Point of S st z <> 0.S
  holds a*z is convergent_to_0
proof
  let a be convergent_to_0 Real_Sequence;
  let z be Point of S such that
A1: z <> 0.S;
   thus a*z is_not_0
     proof
       now let n be Nat;
       assume (a*z).n = 0.S;
       then
       A2: a.n*z = 0.S by Def3;
       a is_not_0 by FDIFF_1:def 1; then
        a.n <> 0 & z <> 0.S by A1,SEQ_1:7;
       hence contradiction by A2,RLVECT_1:24;
       end;
     hence thesis by Th7;
     end;
 A3:  now let p;
   assume A4: 0<p;
        ex pp be Real st pp > 0 & pp*||.z.|| < p
        proof
          take pp=p/(||.z.||+1);
          A5: 0 <= ||.z.|| by NORMSP_1:8;
          A6: ||.z.||+1 > 0+0 by A5,REAL_1:67; then
          A7:  0 < p/(||.z.||+1) by A4,SEQ_2:6;
          A8: ||.z.||+0 < ||.z.||+1 by REAL_1:67;
          0 <=  ||.z.|| by NORMSP_1:8; then
           pp* ||.z.|| < pp*(||.z.|| + 1) by A7,A8,SEQ_4:6;
           hence pp > 0 & pp* ||.z.|| < p by A4,A6,SEQ_2:6,XCMPLX_1:88;
         end;
   then consider pp be Real such that
A9:   pp > 0 & pp*||.z.|| < p;
   a is convergent & lim a =0 by FDIFF_1:def 1;
   then
   consider n be Nat such that
      A10:  for m be Nat st n <= m  holds  abs(a.m- 0) < pp
    by A9,SEQ_2:def 7;
   take n;
      let m be Nat such that A11:n<=m;
      A12: abs(a.m-0) < pp by A10,A11;
      A13: ||.(a*z).m-0.S.|| = ||.a.m*z  - 0.S.|| by Def3
                     .= ||.a.m*z.|| by RLVECT_1:26
                     .= abs(a.m)*||.z.|| by NORMSP_1:def 2;
      0 <=  ||.z.|| by NORMSP_1:8;
      then abs(a.m)*||.z.|| <= pp* ||.z.|| by A12,AXIOMS:25;
      hence ||.(a*z).m-0.S.||<p by A9,A13,AXIOMS:22;
  end;
thus a*z is convergent by A3,NORMSP_1:def 9;
hence lim (a*z) = 0.S by A3,NORMSP_1:def 11;
end;

theorem
(for r be Point of S holds r in Y iff r in the carrier of S)
iff Y=the carrier of S
proof
 thus (for r be Point of S holds r in Y iff r in the carrier of S)
 implies Y=the carrier of S
 proof
  assume for r be Point of S holds r in Y iff r in the carrier of S;
  then for y holds y in Y iff y in the carrier of S;
  hence thesis by TARSKI:2;
 end;
 assume A1: Y=the carrier of S; let r be Point of S;
 thus r in Y implies r in the carrier of S;
 thus thesis by A1;
end;

reserve S,T for non trivial RealNormSpace;
reserve f,f1,f2 for PartFunc of S,T;
reserve s1,s2,q1,seq,seq1,seq2 for sequence of S;
reserve x0,x1,x2 for Point of S;

registration
  let S be non trivial RealNormSpace;
  cluster convergent_to_0 sequence of S;
existence
proof
 consider x0 be Point of S such that
 A1: x0 <> 0.S by ANPROJ_1:def 8;
 deffunc F(Nat) = (1/($1+1))*x0;
 consider s1 be sequence of S such that
A2: for n holds s1.n=F(n) from FUNCT_2:sch 4;
 take s1;
   now let n;
      (n+1) <> 0 by NAT_1:21; then
      (n+1)" <> 0 by XCMPLX_1:203; then
      A3: 1/(n+1) <> 0 by XCMPLX_1:217;
      thus s1.n <> 0.S
      proof
      assume  s1.n = 0.S;
       then
        (1/(n+1))*x0 = 0.S by A2;
       hence contradiction by A1,A3,RLVECT_1:24;
      end;
 end;
 hence s1 is_not_0 by Th7;
 thus thesis by A2,Th22,Th23;
end;
end;

registration let S;
  cluster constant sequence of S;
  existence
proof
 reconsider s1 = NAT --> 0.S as sequence of S by FUNCOP_1:57;
 take s1;
 thus s1 is constant;
end;
end;

::::Normed Linear Spaces versions of FDIFF_1:::::::::::::::::::::::::

reserve h for convergent_to_0 sequence of S;
reserve c for constant sequence of S;

definition let S,T;
  let IT be PartFunc of S,T;
  attr IT is REST-like means:Def5:
  IT is total &
  for h holds (||.h.||")(#)(IT*h) is convergent &
    lim ((||.h.||")(#)(IT*h)) = 0.T;
end;

registration let S,T;
  cluster REST-like PartFunc of S,T;
existence
 proof
  reconsider f = (the carrier of S) --> 0.T as PartFunc of S,T by FUNCOP_1:57;
A2: for r be Point of S holds f.r=0.T by FUNCOP_1:13;
  take f;
A1: dom f = the carrier of S by FUNCOP_1:19;
  hence f is total by PARTFUN1:def 4;
    now let h;
  A3: now let n;
   A4: f/.(h.n) =f.(h.n) by A1,FINSEQ_4:def 4
             .=0.T by A2;
   A5: rng h c= dom f by A1; thus
     ((||.h.||")(#)(f*h)).n = (||.h.||").n*((f*h).n) by Def2
    .= (||.h.||").n*(f/.(h.n)) by A5,NFCONT_1:8
    .= 0.T by A4,RLVECT_1:23;
  end;
  then A6: (||.h.||")(#)(f*h) is constant by LOPBAN_3:def 5;
    ((||.h.||")(#)(f*h)).0 = 0.T by A3;
  hence (||.h.||")(#)(f*h) is convergent
  & lim ((||.h.||")(#)(f*h)) = 0.T
 by A6,Th21;
 end; hence thesis;
end;
end;

definition let S,T;
  mode REST of S,T is REST-like PartFunc of S,T;
end;

theorem
  for R be PartFunc of S,T st R is total holds
      R is REST-like iff
        (for r be Real st r > 0  ex d be Real st
          d > 0 & (for z be Point of S st z <> 0.S & ||.z.|| < d
                       holds ( ||.z.||"* ||. R/.z .||) < r ) )
proof
let R be PartFunc of S,T such that
  A1: R is total;
A2: now assume
   A3: R is REST-like;
      assume
        A4: not (for r be Real st r > 0 ex d be Real st
          d > 0 & (for z be Point of S st z <> 0.S & ||.z.|| < d
                       holds ( ||.z.||"* ||. R/.z .||) < r ));
       consider r be Real such that
        A5:    r > 0 and
        A6: for d be Real st d > 0 holds
             ex z be Point of S st
              z <> 0.S &   ||.z.|| < d  &
              not ( ( ||.z.||"* ||. R/.z .||) < r ) by A4;
 defpred P[Nat,Point of S] means
       $2 <> 0.S &  ||.$2.|| < (1/($1+1))  &
       not ( ( ||.$2.||"* ||. R/.$2 .||) < r );
A7:  for n be Nat ex z be Point of S st P[n,z]
  proof
  let n be Nat;
    0 <= n by NAT_1:18; then
    0+0 < 1+ n by REAL_1:67;
    then 0 < 1 * (n + 1)" by REAL_1:72;
    then 0 < 1/(n + 1) by XCMPLX_0:def 9;
    hence ex z be Point of S st
          z <> 0.S &   ||.z.|| < (1/(n+1))  &
        not ( ( ||.z.||"* ||. R/.z .||) < r ) by A6;
    end;
consider s be sequence of S such that
A8:  for n being Nat holds P[n,s.n] from FUNCT_2:sch 3(A7);
A9: now
       let p be Real; assume 0<p;
 then A10: 0<p" by REAL_1:72;
      consider n such that A11: p"<n by SEQ_4:10;
       p" + 0 < n + 1 by A11,REAL_1:67;
     then 1/(n+1) < 1/p" by A10,SEQ_2:10;
     then A12: 1/(n+1) < p by XCMPLX_1:218;
     take n;
     let m be Nat; assume n<=m;
     then A13: n + 1 <= m + 1 by AXIOMS:24;
     0  <=  n by NAT_1:18; then
      0+0 < 1+ n by REAL_1:67;
    then A14: 1/(m+1) <= 1/(n+1) by A13,SEQ_4:1;
     ||.s.m.|| < (1/(m+1)) by A8; then
      ||.s.m-0.S.|| < (1/(m+1)) by RLVECT_1:26;
    then    ||.s.m - 0.S.|| <  1/(n+1) by A14,AXIOMS:22;
    hence  ||.s.m - 0.S.|| <p by A12,AXIOMS:22;
    end;
then A15: s is convergent by NORMSP_1:def 9;
then A16: lim s = 0.S by A9,NORMSP_1:def 11;
s is_not_0 by A8,Th7;
then  reconsider s as   convergent_to_0 sequence of S by A15,A16,Def4;
A17:  (||.s.||")(#)(R*s) is convergent  & lim ((||.s.||")(#)(R*s)) = 0.T
by A3,Def5;
consider n be Nat such that
A18:   for m be Nat st n <=m
          holds ||. ((||.s.||")(#)(R*s)).m- 0.T.|| < r
     by A5,A17,NORMSP_1:def 11;
A19:  ||. ((||.s.||")(#)(R*s)).n- 0.T.|| < r by A18;
  dom R = the carrier of S by A1,PARTFUN1:def 4; then
A20: rng s c= dom R;
A21: ||. ((||.s.||")(#)(R*s)).n- 0.T.||
       = ||. ((||.s.||")(#)(R*s)).n .|| by RLVECT_1:26
      .= ||.(||.s.||".n)*((R*s).n).|| by Def2
      .= ||.(||.s.||.n)"*((R*s).n).|| by SEQ_1:def 8
      .= ||.(||.s.n.||)"*((R*s).n).|| by NORMSP_1:def 10
      .= ||.(||.s.n.||)"*(R/.(s.n)).|| by A20,NFCONT_1:8;
  s.n <> 0.S by A8; then
    ||.s.n.|| <> 0 by NORMSP_1:def 2; then
      ||.s.n.|| > 0 by NORMSP_1:8;then
A22:  0 < ( ||.s.n.||)" by REAL_1:72;
   ||.(||.s.n.||)"*(R/.(s.n)).||
   = abs((||.s.n.||)") *   ||.(R/.(s.n)).|| by NORMSP_1:def 2
   .=(||.s.n.||)" *   ||.(R/.(s.n)).|| by A22,ABSVALUE:def 1;
hence (for r be Real st r > 0  ex d be Real st
          d > 0 & (for z be Point of S st z <> 0.S & ||.z.|| < d
                       holds ( ||.z.||"* ||. R/.z .||) < r )
       ) by A8,A19,A21;
end;
 now assume
   A23: for r be Real st r > 0  ex d be Real st
          d > 0 & (for z be Point of S st z <> 0.S & ||.z.|| < d
                       holds ( ||.z.||"* ||. R/.z .||) < r );
    now let s be convergent_to_0 sequence of S;
    A24: s is_not_0 by Def4;
    A25: s is convergent & lim s = 0.S by Def4;
    A26: now let r be Real such that
        A27: r > 0;
        consider d be Real such that
        A28: d > 0 and
        A29:  (for z be Point of S st z <> 0.S & ||.z.|| < d
                       holds ( ||.z.||"* ||. R/.z .||) < r ) by A23,A27;
        consider n be Nat such that
        A30:   for m be Nat st n <=m  holds
                 ||.s.m-0.S.|| < d by A25,A28,NORMSP_1:def 11;
        take n;
        thus  for m be Nat st n <=m holds
           ||. ((||.s.||")(#)(R*s)).m- 0.T.|| < r
        proof
          let m be Nat such that
          A31: n <=m;
                 ||.s.m-0.S .|| < d by A30,A31; then
             A32:  ||.s.m.|| < d by RLVECT_1:26;
          A33:s.m <> 0.S by A24,Th7;
            s.m <> 0.S by A24,Th7; then
             ||.s.m.|| <> 0 by NORMSP_1:def 2; then
             ||.s.m.|| > 0 by NORMSP_1:8; then
           A34:  0 < ( ||.s.m.||)" by REAL_1:72;
           dom R = the carrier of S by A1,PARTFUN1:def 4; then
           A35: rng s c= dom R;
          (||.s.m.||)" * ||.(R/.(s.m)).||
          =abs((||.s.m.||)") * ||.(R/.(s.m)).|| by A34,ABSVALUE:def 1
          .= ||.(||.s.m.||)"*(R/.(s.m)).|| by NORMSP_1:def 2
          .= ||.(||.s.m.||)"*((R*s).m).|| by A35,NFCONT_1:8
          .= ||.(||.s.||.m)"*((R*s).m).|| by NORMSP_1:def 10
          .= ||.(||.s.||".m)*((R*s).m).|| by SEQ_1:def 8
          .= ||. ((||.s.||")(#)(R*s)).m .|| by Def2
          .= ||. ((||.s.||")(#)(R*s)).m- 0.T.|| by RLVECT_1:26;
          hence ||. ((||.s.||")(#)(R*s)).m- 0.T.||   < r by A29,A32,A33;
        end;
        end;
    hence (||.s.||")(#)(R*s) is convergent by NORMSP_1:def 9;
    hence lim ((||.s.||")(#)(R*s)) = 0.T by A26,NORMSP_1:def 11;
    end;
    hence R is REST-like by A1,Def5;
end;
hence thesis by A2;
end;

theorem Th27:
  for R be REST of S,T
    for s be convergent_to_0 sequence of S
  holds (R*s) is convergent & lim (R*s) = 0.T
proof
let R be REST of S,T;
let s be convergent_to_0 sequence of S;
A1: s is_not_0 by Def4;
A2: s is convergent & lim s = 0.S by Def4;
now let n be Nat;
     s.n <> 0.S by A1,Th7; then
A3: ||.s.n.|| <> 0 by NORMSP_1:def 2; thus
    (||.s.||(#) ( (||.s.||") (#) (R*s) )).n
     =||.s.||.n*((||.s.||")(#)(R*s)).n by Def2
    .=||.s.||.n*((||.s.||").n*((R*s).n)) by Def2
    .= ||.s.n.||*((||.s.||").n*((R*s).n)) by NORMSP_1:def 10
    .= ||.s.n.||*((||.s.||.n)"*((R*s).n)) by SEQ_1:def 8
    .= ||.s.n.||*((||.s.n.||)"*((R*s).n)) by NORMSP_1:def 10
    .= ||.s.n.||*(||.s.n.||)"*((R*s).n) by RLVECT_1:def 9
    .= 1*(R*s).n by A3,XCMPLX_0:def 7
    .=(R*s).n by RLVECT_1:def 9;
 end;then
 A4:  ||.s.||(#) ( (||.s.||") (#) (R*s) )=  (R*s) by FUNCT_2:113;
 A5:  (||.s.||") (#) (R*s) is convergent
 & lim  ((||.s.||") (#) (R*s) ) = 0.T by Def5;
   ||.s.|| is convergent & lim ( ||.s.|| ) = ||.0.S.||
 by A2,LOPBAN_1:47; then
 A6:  ||.s.|| is convergent & lim ( ||.s.|| ) =0 by NORMSP_1:5;
  (R*s) is convergent & lim (R*s) = 0* 0.T
  by A4,A5,A6,Th13,Th14;
  hence thesis by RLVECT_1:23;
end;

reserve R,R1,R2 for REST of S,T;
reserve L,L1,L2 for Point of R_NormSpace_of_BoundedLinearOperators(S,T);

theorem Th28:
rng (seq^\n) c= rng seq
proof
 let x; assume x in rng (seq^\n); then consider y such that
A1: y in dom (seq^\n) & (seq^\n).y = x by FUNCT_1:def 5;
 reconsider y as Nat by A1;
A2: (seq^\n).y = seq.(y+n) by LOPBAN_3:def 6;
   y+n in NAT; then y+n in dom seq by FUNCT_2:def 1;
 hence x in rng seq by A1,A2,FUNCT_1:def 5;
end;

theorem Th29:
for h be PartFunc of S,T for seq be sequence of S holds
rng seq c= dom h implies (h*seq)^\n=h*(seq^\n)
proof
let h be PartFunc of S,T;
let seq be sequence of S;
assume A1: rng seq c= dom h;
rng (seq^\n) c= rng seq by Th28;
then A2: rng (seq^\n) c= dom h by A1,XBOOLE_1:1;
   now let m be Nat;
  thus ((h*seq)^\n).m = (h*seq).(m+n) by LOPBAN_3:def 6
   .= h/.(seq.(m+n)) by A1,NFCONT_1:8
   .= h/.((seq^\n).m) by LOPBAN_3:def 6
   .= (h*(seq^\n)).m by A2,NFCONT_1:8;
 end; hence thesis by FUNCT_2:113;
end;

theorem Th30:
for h1,h2 be PartFunc of S,T for seq be sequence of S holds
  h1 is total & h2 is total implies
  (h1+h2)*seq = h1*seq + h2*seq &
  (h1-h2)*seq = h1*seq - h2*seq
proof
let h1,h2 be PartFunc of S,T;
let seq be sequence of S;
assume h1 is total & h2 is total;
then h1+h2 is total by VFUNCT_1:38;
 then dom (h1+h2) = the carrier of S by PARTFUN1:def 4;
 then dom h1 /\ dom h2 = the carrier of S by VFUNCT_1:def 1;
 then A1: rng seq c= dom h1 /\ dom h2;
 hence (h1+h2)*seq = h1*seq + h2*seq by NFCONT_1:19;
 thus (h1-h2)*seq = h1*seq - h2*seq by A1,NFCONT_1:19;
end;

theorem Th31:
 for h be PartFunc of S,T for seq be sequence of S
 for r be Real holds
   h is total implies (r(#)h)*seq = r*(h*seq)
proof
let h be PartFunc of S,T;
let seq be sequence of S;
let r be Real;
assume h is total;
then dom h = the carrier of S by PARTFUN1:def 4;
 then rng seq c= dom h;
 hence thesis by NFCONT_1:20;
end;

theorem Th32:
  f is_continuous_in x0 iff x0 in dom f & for s1 be sequence of S
st rng s1 c= dom f &
s1 is convergent & lim s1=x0 & (for n holds s1.n<>x0) holds
f*s1 is convergent & f/.x0=lim(f*s1)
proof
thus f is_continuous_in x0 implies x0 in dom f
& for s1 be sequence of S st rng s1 c= dom f &
 s1 is convergent & lim s1=x0 & (for n holds s1.n<>x0) holds
 f*s1 is convergent & f/.x0=lim(f*s1) by NFCONT_1:def 9;
assume A1: x0 in dom f & for s1 st rng s1 c=dom f &
s1 is convergent & lim s1=x0
 & (for n holds s1.n<>x0) holds f*s1 is convergent & f/.x0=lim(f*s1);
hence x0 in dom f;
let s2 be sequence of S such that A2: rng s2 c=dom f
& s2 is convergent & lim s2=x0;
  now per cases;
suppose ex n st for m be Nat st n<=m holds s2.m=x0;
 then consider N be Nat such that A3: for m be Nat st N<=m holds s2.m=x0;
 A4: for n holds (s2^\N).n=x0
 proof let n;
    N<=n+N by NAT_1:37; then s2.(n+N)=x0 by A3;
  hence (s2^\N).n=x0 by LOPBAN_3:def 6;
 end;
   s2^\N is subsequence of s2 by LOPBAN_3:13;
 then rng (s2^\N) c= rng s2 by NFCONT_1:7;
 then A5: rng (s2^\N) c= dom f by A2,XBOOLE_1:1;
 A6: now let p be Real such that A7: p>0;
  take n=0; let m be Nat such that n<=m;
    ||.(f*(s2^\N)).m-f/.x0.||
   =||.f/.((s2^\N).m)-f/.x0.|| by A5,NFCONT_1:8
  .=||.f/.x0-f/.x0.|| by A4
  .=||.0.T.|| by RLVECT_1:28
  .=0 by NORMSP_1:5;
  hence ||.(f*(s2^\N)).m-f/.x0.||< p by A7;
 end;
 then A8: f*(s2^\N) is convergent by NORMSP_1:def 9;
 f*(s2^\N)=(f*s2)^\N by A2,Th29;
 then f*s2 is convergent & f/.x0=lim((f*s2)^\N)
by A6,A8,LOPBAN_3:15,NORMSP_1:def 11;
 hence thesis by LOPBAN_3:14;
end;
suppose A9: for n ex m be Nat st n<=m & s2.m<>x0;
 then consider m1 be Nat such that A10: 0<=m1 & s2.m1<>x0;
 defpred P[Nat] means s2.$1<>x0;
 A11: ex m be Nat st P[m] by A10;
 consider M be Element of NAT such that
 A12: P[M] & for n st P[n] holds M<=n from
 NAT_1:sch 5(A11);
 A13: now let n; consider m be Nat such that A14: n+1<=m & s2.m<>x0 by A9;
  take m; thus n<m & s2.m<>x0 by A14,NAT_1:38;
 end;
 defpred P[Nat,set,set] means for n,m be Nat st $2=n & $3=m holds
 n<m & s2.m<>x0 & for k st n<k & s2.k<>x0 holds m<=k;
 A15: for n for x be Element of NAT ex y be Element of NAT st P[n,x,y]
 proof let n; let x be Element of NAT;
  defpred P[Nat] means x<$1 & s2.$1<>x0;
  A16: ex m be Nat st P[m] by A13;
  consider l be Nat such that A17: P[l] &
   for k st P[k] holds l<=k from NAT_1:sch 5(A16);
  take l; thus thesis by A17;
 end;
 reconsider M' = M as Element of NAT;
 consider F be Function of NAT,NAT such that
 A18: F.0=M' & for n be Element of NAT holds P[n,F.n,F.(n+1)]
   from RECDEF_1:sch 2(A15);
 A19: dom F=NAT & rng F c= NAT by FUNCT_2:def 1;
   rng F c= REAL by XBOOLE_1:1;
 then reconsider F as Real_Sequence by A19,FUNCT_2:def 1,RELSET_1:11;
 A20: now let n; F.n in rng F by A19,FUNCT_1:def 5;hence F.n is Nat by A19;
 end;
   now let n; F.n is Nat & F.(n+1) is Nat by A20;
  hence F.n<F.(n+1) by A18;
 end;
 then reconsider F as increasing Seq_of_Nat by A19,SEQM_3:def 8,def 11;
A21: for n st s2.n<>x0 ex m be Nat st F.m=n
 proof
  defpred P[Nat] means s2.$1<>x0 & for m be Nat holds F.m<>$1;
  assume
A22: ex n st P[n];
  consider M1 be Nat such that A23: P[M1] &
  for n st P[n] holds M1<=n from NAT_1:sch 5(A22);
  defpred P[Nat] means $1<M1 & s2.$1<>x0 & ex m be Nat st F.m=$1;
  A24: ex n st P[n]
  proof take M;
   A25: M<=M1 by A12,A23;
      M <> M1 by A18,A23;
   hence M<M1 by A25,REAL_1:def 5;
   thus s2.M<>x0 by A12;
   take 0; thus thesis by A18;
  end;
  A26: for n st P[n] holds n<=M1;
  consider MX be Nat such that A27: P[MX] &
  for n st P[n] holds n<=MX from NAT_1:sch 6(A26,A24);
  A28: for k st MX<k & k<M1 holds s2.k=x0
  proof given k such that A29: MX<k & k<M1 & s2.k<>x0;
     now per cases;
   suppose ex m be Nat st F.m=k; hence contradiction by A27,A29;
   end;
   suppose for m be Nat holds F.m<>k; hence contradiction by A23,A29;
   end;
   end; hence contradiction;
  end;
  consider m be Nat such that A30: F.m=MX by A27;
  A31: MX<F.(m+1) & s2.(F.(m+1))<>x0 &
      for k st MX<k & s2.k<>x0 holds F.(m+1)<=k by A18,A30;
  A32: F.(m+1)<=M1 by A18,A23,A27,A30;
    now assume F.(m+1)<>M1; then F.(m+1)<M1 by A32,REAL_1:def 5;
hence contradiction by A28,A31;
  end;hence contradiction by A23;
 end;
A33: for n holds (s2*F).n<>x0
 proof
  defpred P[Nat] means (s2*F).$1<>x0;
  A34: P[0] by A12,A18,LOPBAN_3:7;
  A35: for k st P[k] holds P[k+1]
  proof let k such that (s2*F).k<>x0;
     P[k,F.k,F.(k+1)] by A18; then s2.(F.(k+1))<>x0;
   hence (s2*F).(k+1)<>x0 by LOPBAN_3:7;
  end;
  thus for n holds P[n] from NAT_1:sch 1(A34,A35);
 end;
 A36: s2*F is subsequence of s2 by BHSP_3:def 4;
then A37: s2*F is convergent by A2,LOPBAN_3:11;
   rng (s2*F) c= rng s2 by A36,NFCONT_1:7;
then A38: rng (s2*F) c= dom f by A2,XBOOLE_1:1;
   lim (s2*F)=x0 by A2,A36,LOPBAN_3:12;
 then A39: f*(s2*F) is convergent
 & f/.x0=lim(f*(s2*F)) by A1,A33,A37,A38;
A40: now let p be Real; assume A41: 0<p;
  then consider n such that
  A42: for m be Nat st n<=m holds ||.(f*(s2*F)).m-f/.x0.||<p
by A39,NORMSP_1:def 11;
  take k=F.n; let m be Nat such that A43: k<=m;
    now per cases;
  suppose A44: s2.m=x0;
     ||.(f*s2).m-f/.x0.||
     =||.f/.(s2.m)-f/.x0.|| by A2,NFCONT_1:8
   .=||.0.T.|| by A44,RLVECT_1:28
   .=0 by NORMSP_1:5;
   hence ||.(f*s2).m-f/.x0.||<p by A41;
  end;
  suppose s2.m<>x0; then consider l be Nat such that A45: m=F.l by A21;
     n<=l by A43,A45,SEQM_3:7;
   then ||.(f*(s2*F)).l-f/.x0.||<p by A42;
   then ||.f/.((s2*F).l)-f/.x0.||<p by A38,NFCONT_1:8;
   then ||.f/.(s2.m)-f/.x0.||<p by A45,LOPBAN_3:7;
   hence ||.(f*s2).m-f/.x0.||<p by A2,NFCONT_1:8;
  end;
  end; hence ||.(f*s2).m-f/.x0.||<p;
 end;
 hence f*s2 is convergent by NORMSP_1:def 9;
 hence f/.x0=lim(f*s2) by A40,NORMSP_1:def 11;
 end;
 end; hence thesis;
end;

theorem Th33:
for R1,R2 holds R1+R2 is REST of S,T & R1-R2 is REST of S,T
 proof let R1,R2;
A1: R1 is total & R2 is total by Def5;
then A2: R1+R2 is total by VFUNCT_1:38;
now let h;
A3: (||.h.||")(#)(R1*h) is convergent
     & lim ((||.h.||")(#)(R1*h)) = 0.T by Def5;
A4: (||.h.||")(#)(R2*h) is convergent
& lim ((||.h.||")(#)(R2*h)) = 0.T by Def5;
A5: (||.h.||")(#)((R1+R2)*h) =
(||.h.||")(#)((R1*h)+(R2*h)) by A1,Th30
  .= ((||.h.||")(#)(R1*h))+((||.h.||")(#)(R2*h)) by Th9;
   hence (||.h.||")(#)((R1+R2)*h) is convergent
 by A3,A4,NORMSP_1:34;
   thus lim ((||.h.||")(#)((R1+R2)*h)) = 0.T+0.T
 by A3,A4,A5,NORMSP_1:42
    .= 0.T by RLVECT_1:10;
  end; hence R1+R2 is REST of S,T by A2,Def5;
A6: R1-R2 is total by A1,VFUNCT_1:38;
    now let h;
A7: (||.h.||")(#)(R1*h) is convergent
& lim ((||.h.||")(#)(R1*h)) = 0.T by Def5;
A8: (||.h.||")(#)(R2*h) is convergent
& lim ((||.h.||")(#)(R2*h)) = 0.T by Def5;
A9: (||.h.||")(#)((R1-R2)*h)
      = (||.h.||")(#)((R1*h)-(R2*h)) by A1,Th30
     .= ((||.h.||")(#)(R1*h))-((||.h.||")(#)(R2*h)) by Th12;
   hence (||.h.||")(#)((R1-R2)*h) is convergent by A7,A8,NORMSP_1:35;
   thus lim ((||.h.||")(#)((R1-R2)*h))
   = 0.T-0.T by A7,A8,A9,NORMSP_1:43
     .= 0.T by RLVECT_1:26;
  end; hence R1-R2 is REST of S,T by A6,Def5;
 end;

theorem Th34:
for r,R holds r(#)R is REST of S,T
 proof let r,R; A1: R is total by Def5;
then A2: r(#)R is total by VFUNCT_1:40;
now let h;
A3: (||.h.||")(#)(R*h) is convergent
& lim ((||.h.||")(#)(R*h)) = 0.T by Def5;
A4: (||.h.||")(#)((r(#)R)*h)
= (||.h.||")(#)(r*(R*h)) by A1,Th31
     .= r*((||.h.||")(#)(R*h)) by Th10;
hence (||.h.||")(#)((r(#)R)*h) is convergent by A3,NORMSP_1:37;
thus lim ((||.h.||")(#)((r(#)R)*h)) = r*0.T by A3,A4,NORMSP_1:45
.= 0.T by RLVECT_1:23;
   end;
   hence thesis by A2,Def5;
 end;

definition
let S,T;
let f be PartFunc of S,T;
let x0 be Point of S;
pred f is_differentiable_in x0 means:Def6:
ex N being Neighbourhood of x0 st N c= dom f &
ex L,R st for x be Point of S st x in N holds
f/.x - f/.x0 = L.(x-x0) + R/.(x-x0);
end;

definition let S,T;
           let f be PartFunc of S,T;
           let x0 be Point of S;
assume A1: f is_differentiable_in x0;
func diff(f,x0) -> Point of R_NormSpace_of_BoundedLinearOperators(S,T)
means:Def7:
ex N being Neighbourhood of x0 st N c= dom f &
ex R st for x be Point of S st x in N
holds f/.x-f/.x0 = it.(x-x0) + R/.(x-x0);
existence
proof
 consider N being Neighbourhood of x0 such that A2: N c= dom f &
 ex L,R st for x be Point of S
 st x in N holds f/.x - f/.x0
 = L.(x-x0) + R/.(x-x0) by A1,Def6;
 consider L,R such that A3:
 for x be Point of S st x in N holds
   f/.x - f/.x0 = L.(x-x0) + R/.(x-x0) by A2;
 take L;
 thus thesis by A2,A3;
end;
uniqueness
proof
   let LB,LB1 be Point of R_NormSpace_of_BoundedLinearOperators(S,T);
A4:R_NormSpace_of_BoundedLinearOperators(S,T)
     = NORMSTR (# BoundedLinearOperators(S,T),
        Zero_(BoundedLinearOperators(S,T),
        R_VectorSpace_of_LinearOperators(S,T)),
         Add_(BoundedLinearOperators(S,T),
         R_VectorSpace_of_LinearOperators(S,T)),
          Mult_(BoundedLinearOperators(S,T),
          R_VectorSpace_of_LinearOperators(S,T)),
          BoundedLinearOperatorsNorm(S,T) #) by LOPBAN_1:def 15;
   assume that
A5: ex N being Neighbourhood of x0 st N c= dom f &
    ex R st for x be Point of S
     st x in N holds f/.x-f/.x0 = LB.(x-x0)+R/.(x-x0) and
A6: ex N being Neighbourhood of x0 st N c= dom f &
    ex R st for x be Point of S
     st x in N holds f/.x-f/.x0 = LB1.(x-x0) + R/.(x-x0);
   consider N being Neighbourhood of x0 such that
A7: N c= dom f & ex R st for x be Point of S
     st x in N holds f/.x-f/.x0 = LB.(x-x0)+R/.(x-x0) by A5;
   consider R such that
A8: for x be Point of S
     st x in N holds f/.x-f/.x0 = LB.(x-x0) + R/.(x-x0) by A7;
   consider N1 being Neighbourhood of x0 such that
A9: N1 c= dom f & ex R st for x be Point of S
     st x in N1 holds f/.x-f/.x0=LB1.(x-x0)+R/.(x-x0) by A6;
   consider R1 such that
A10:for x be Point of S
     st x in N1 holds f/.x-f/.x0 = LB1.(x-x0) + R1/.(x-x0) by A9;
   reconsider L = LB as Element of BoundedLinearOperators(S,T) by A4;
   reconsider L1=LB1 as Element of BoundedLinearOperators(S,T) by A4;
A11:for z be Point of S holds modetrans(L,S,T).z = modetrans(L1,S,T).z
   proof
     let z be Point of S;
     now per cases;
       case A12: z= 0.S;
         thus modetrans(L,S,T).z
             =modetrans(L,S,T).(0*z) by A12,RLVECT_1:23
            .=0*modetrans(L,S,T).z by LOPBAN_1:def 6
            .=0.T by RLVECT_1:23
            .=0*modetrans(L1,S,T).z by RLVECT_1:23
            .=modetrans(L1,S,T).(0*z) by LOPBAN_1:def 6
            .=modetrans(L1,S,T).z by A12,RLVECT_1:23;
       end;
       case A13: z<> 0.S;
A14:     now let x be Point of S;
           assume A15: x in N & x in N1; then
           f/.x-f/.x0 = L.(x-x0) + R/.(x-x0) by A8;
           hence L.(x-x0) + R/.(x-x0) = L1.(x-x0) + R1/.(x-x0) by A10,A15;
         end;
         consider N0 be Neighbourhood of x0 such that
A16:      N0 c= N & N0 c= N1 by Th1;
         consider g be Real such that
A17:      0<g & {y where y is Point of S: ||.y - x0  .|| < g} c= N0
          by NFCONT_1:def 3;
         consider n0 be Nat such that
A18:      ||.z.|| /g < n0 by SEQ_4:10;
         0 <= n0 by NAT_1:18; then
A19:      0< n0+1 by NAT_1:38;
         n0 <= n0+1 by NAT_1:29; then
         ||.z.|| /g < (n0+1) by A18,AXIOMS:22; then
         ( ||.z.|| /g)*g < (n0+1)*g by A17,REAL_1:70; then
         ||.z.|| < (n0+1)*g by A17,XCMPLX_1:88; then
A20:      ||.z.|| /(n0+1) < g by A19,REAL_2:178;
         set n1=n0+1;
         ex r be Real_Sequence st
           (for n be Nat holds (r.n=1 /(n+n1) & r.n > 0 & r.n*z+x0  in N0) )
            & r is convergent
            & lim r =0
         proof
           deffunc F(Nat) = 1 /($1+n1);
           consider r be Real_Sequence such that
A21:        for n holds r.n = F(n) from SEQ_1:sch 1;
           take r;
           thus for n be Nat holds
             (r.n=1 /(n+n1) & r.n > 0 & r.n*z+x0  in N0)
           proof
             let n be Nat;
             thus r.n=1 /(n+n1) by A21;
             0 <= n by NAT_1:18; then
             0+0 <n1+n by A19,REAL_1:67; then
             0 < 1 * (n + n1)" by REAL_1:72; then
A22:          0 < 1/(n + n1) by XCMPLX_0:def 9;
             hence r.n >0 by A21;
A23:          n1 <= n+n1 by NAT_1:37;
             0 <= ||.z.|| by NORMSP_1:8; then
A24:          ||.z.|| /(n+n1) <= ||.z.|| /n1 by A19,A23,REAL_2:201;
             ||.r.n*z+x0-x0.|| = ||.r.n*z+(x0-x0).|| by RLVECT_1:42
                              .= ||.r.n*z+0.S.|| by RLVECT_1:28
                              .= ||.r.n*z.|| by RLVECT_1:10
                              .= ||.(1 /(n+n1))*z.|| by A21
                              .= abs(1 /(n+n1))* ||.z.|| by NORMSP_1:def 2
                              .= ( 1 /(n+n1) )* ||.z.||
                                  by A22,ABSVALUE:def 1
                              .= ||.z.|| /(n+n1) by XCMPLX_1:100; then
             ||.r.n*z+x0-x0.|| < g by A20,A24,AXIOMS:22; then
             (r.n*z+x0) in {y where y is Point of S: ||.y - x0  .|| < g};
             hence (r.n*z+x0) in N0 by A17;
           end;
           thus r is convergent by A19,A21,SEQ_4:43;
           thus lim r = 0 by A19,A21,SEQ_4:44;
         end; then
         consider r be Real_Sequence such that
A25:      (for n be Nat holds (r.n=1 /(n+n1) & r.n > 0 & r.n*z+x0  in N0))
           & r is convergent & lim r =0;
         deffunc F(Nat) = r.$1*z;
         consider s be sequence of S such that
A26:      for n holds s.n = F(n) from FUNCT_2:sch 4;
         s is_not_0 & s is convergent & lim s = 0.S
         proof
           now let n be Nat;
             assume s.n = 0.S; then
             r.n*z = 0.S by A26; then
             r.n =0 or z=0.S by RLVECT_1:24;
             hence contradiction by A13,A25;
           end;
           hence s is_not_0 by Th7;
           now let n be Nat;
             thus s.n = r.n*z by A26
                     .=(1 /(n+n1))*z by A25;
           end;
           hence s is convergent & lim s=0.S by A19,Th22,Th23;
         end; then
         reconsider s as convergent_to_0 sequence of S by Def4;
A27:     now let n;
           set x=(r.n)*z+x0;
A28:       x-x0 = (r.n)*z+(x0 -x0) by RLVECT_1:42
               .= (r.n)*z+0.S by RLVECT_1:28
               .=(r.n)*z by RLVECT_1:10;
           x in N0 by A25; then
A29:       L.((r.n)*z) + R/.((r.n)*z) = L1.((r.n)*z) + R1/.((r.n)*z)
           by A14,A16,A28;
A30:       r.n <> 0 by A25;
A31:       (r.n)"*(L.((r.n)*z))+(r.n)"*(R/.((r.n)*z))
             =(r.n)"*(L1.((r.n)*z)+R1/.((r.n)*z)) by A29,RLVECT_1:def 9;
A32:       (r.n)"*(L.((r.n)*z)) =(r.n)"*(modetrans(L,S,T).((r.n)*z))
            by LOPBAN_1:def 12
           .=(r.n)"*((r.n)*modetrans(L,S,T).z) by LOPBAN_1:def 6
           .= (r.n)"*((r.n)*L.z ) by LOPBAN_1:def 12
           .= ((r.n)"*(r.n))*L.z by RLVECT_1:def 9
           .= ((r.n)/(r.n))*L.z by XCMPLX_0:def 9
           .= 1*L.z by A30,XCMPLX_1:60
           .= L.z by RLVECT_1:def 9;
           (r.n)"*(L1.((r.n)*z))
            =(r.n)"*(modetrans(L1,S,T).((r.n)*z)) by LOPBAN_1:def 12
           .=(r.n)"*((r.n)*modetrans(L1,S,T).z) by LOPBAN_1:def 6
           .= (r.n)"*((r.n)*L1.z ) by LOPBAN_1:def 12
           .= ((r.n)"*(r.n))*L1.z by RLVECT_1:def 9
           .= ((r.n)/(r.n))*L1.z by XCMPLX_0:def 9
           .= 1*L1.z by A30,XCMPLX_1:60
           .= L1.z by RLVECT_1:def 9; then
A33:       L.z + (r.n)" *(R/.((r.n)*z)) = L1.z + (r.n)"*(R1/.((r.n)*z))
            by A31,A32,RLVECT_1:def 9;
           R is total by Def5; then
           dom R = the carrier of S by PARTFUN1:def 4; then
A34:       rng s c= dom R; R1 is total by Def5; then
           dom R1 = the carrier of S by PARTFUN1:def 4; then
A35:       rng s c= dom R1;
A36:        r.n > 0 by A25;
           s is_not_0 by Def4; then
           s.n <> 0.S by Th7; then
A37:        ||.s.n.|| <> 0 by NORMSP_1:def 2;
           ||.s.n.|| = ||.(r.n)*z.|| by A26
           .= abs(r.n) * ||.z.|| by NORMSP_1:def 2
           .=r.n * ||.z.|| by A36,ABSVALUE:def 1; then
           (r.n)"* ||.s.n.|| = (r.n)"* r.n * ||.z.||
                      .= ((r.n)/(r.n))* ||.z.|| by XCMPLX_0:def 9
                      .= 1* ||.z.|| by A30,XCMPLX_1:60
                      .= ||.z.||; then
           ||.z.|| *( ||.s.n.|| )" = (r.n)"* ||.s.n.|| *( ||.s.n.|| )"
             .= (r.n)"* (||.s.n.|| *( ||.s.n.|| )")
             .= (r.n)"*  (||.s.n.|| /( ||.s.n.|| )) by XCMPLX_0:def 9
             .= (r.n)"*1 by A37,XCMPLX_1:60
             .= (r.n)"; then
A38:        ||.z.|| *( ||.s.||.n )" = (r.n)" by NORMSP_1:def 10;
A39:       (r.n)"*(R/.((r.n)*z))
            = (r.n)"*(R/.(s.n)) by A26
           .= ( ||.z.|| *||.s.||".n)*(R/.(s.n)) by A38,SEQ_1:def 8
           .= ||.z.|| *(||.s.||".n *(R/.(s.n)) ) by RLVECT_1:def 9
           .= ||.z.|| *((||.s.||".n)*((R*s).n)) by A34,NFCONT_1:8
           .= ||.z.|| *((||.s.||")(#)(R*s)).n by Def2;
           (r.n)"*(R1/.((r.n)*z))
            = (r.n)"*(R1/.(s.n)) by A26
           .= ( ||.z.|| *||.s.||".n)*(R1/.(s.n)) by A38,SEQ_1:def 8
           .= ||.z.|| *(||.s.||".n *(R1/.(s.n)) ) by RLVECT_1:def 9
           .= ||.z.|| *((||.s.||".n)*((R1*s).n)) by A35,NFCONT_1:8
           .= ||.z.|| *((||.s.||")(#)(R1*s)).n by Def2; then
           L.z + ( ||.z.|| *((||.s.||")(#)(R*s)).n
              -( ||.z.|| *((||.s.||")(#)(R*s)).n))
            = L1.z +  ||.z.|| *((||.s.||")(#)(R1*s)).n
              -(||.z.|| *((||.s.||")(#)(R*s)).n) by A33,A39,RLVECT_1:42; then
           L.z + 0.T
  = L1.z + ||.z.|| *((||.s.||")(#)(R1*s)).n
  -(||.z.|| *((||.s.||")(#)(R*s)).n) by RLVECT_1:28; then
  L.z
  = L1.z + ||.z.|| *((||.s.||")(#)(R1*s)).n
  -(||.z.|| *((||.s.||")(#)(R*s)).n) by RLVECT_1:def 7; then
  L.z - L1.z
  = (- L1.z)+ (L1.z + ||.z.|| *((||.s.||")(#)(R1*s)).n
  -(||.z.|| *((||.s.||")(#)(R*s)).n) ) by RLVECT_1:def 11; then
  L.z - L1.z
  = (- L1.z)+ (L1.z + ( ||.z.|| *((||.s.||")(#)(R1*s)).n
  -(||.z.|| *((||.s.||")(#)(R*s)).n) )) by RLVECT_1:42; then
  L.z - L1.z
  = (- L1.z)+ L1.z + (||.z.|| *((||.s.||")(#)(R1*s)).n
  -(||.z.|| *((||.s.||")(#)(R*s)).n) ) by RLVECT_1:def 6;then
   L.z - L1.z = 0.T+ ( ||.z.|| *((||.s.||")(#)(R1*s)).n
  -(||.z.|| *((||.s.||")(#)(R*s)).n) ) by RLVECT_1:16;then
   L.z - L1.z = (||.z.|| *((||.s.||")(#)(R1*s)).n
  -(||.z.|| *((||.s.||")(#)(R*s)).n) ) by RLVECT_1:10;then
    L.z - L1.z = ||.z.|| *((||.s.||"(#)(R1*s)).n-(||.s.||"(#)(R*s)).n)
 by RLVECT_1:48;then
     L.z - L1.z = ||.z.|| *((||.s.||"(#)(R1*s))-(||.s.||"(#)(R*s))).n
   by NORMSP_1:def 6;
 hence L.z - L1.z = ( ||.z.|| *((||.s.||"(#)(R1*s))-(||.s.||"(#)(R*s)))).n
   by NORMSP_1:def 8;
end;
 then A40: ||.z.|| *((||.s.||"(#)(R1*s))-(||.s.||"(#)(R*s)))
 is constant by LOPBAN_3:def 5;
 ( ||.z.|| *((||.s.||"(#)(R1*s))-(||.s.||"(#)(R*s)))).1
  =L.z - L1.z by A27; then
 A41: lim ( ||.z.|| *((||.s.||"(#)(R1*s))-(||.s.||"(#)(R*s))))
 = L.z - L1.z by A40,Th21;
 A42: (||.s.||")(#)(R*s) is convergent
 & lim ((||.s.||")(#)(R*s)) = 0.T by Def5;
 A43: (||.s.||")(#)(R1*s) is convergent
 & lim ((||.s.||")(#)(R1*s)) = 0.T by Def5;
 A44: ((||.s.||"(#)(R1*s))-(||.s.||"(#)(R*s))) is convergent
      by A42,A43,NORMSP_1:35;
 A45: lim ((||.s.||"(#)(R1*s))-(||.s.||"(#)(R*s)))
  = 0.T - 0.T by A42,A43,NORMSP_1:43
 .= 0.T by RLVECT_1:26;
 A46: lim ( ||.z.|| *((||.s.||"(#)(R1*s))-(||.s.||"(#)(R*s))))
        = ||.z.|| * lim ((||.s.||"(#)(R1*s))-(||.s.||"(#)(R*s)))
          by A44,NORMSP_1:45
        .=0.T by A45,RLVECT_1:23;
    thus modetrans(L,S,T).z
        =L.z by LOPBAN_1:def 12
        .=L1.z by A41,A46,RLVECT_1:35
        .= modetrans(L1,S,T).z by LOPBAN_1:def 12;
    end;
  end;
  hence thesis;
end;
thus LB=modetrans(L,S,T) by LOPBAN_1:def 12
 .=modetrans(L1,S,T) by A11,FUNCT_2:113
 .=LB1 by LOPBAN_1:def 12;
thus thesis;
end;
end;

definition let X;
           let S,T;
           let f be PartFunc of S,T;
 pred f is_differentiable_on X means:Def8:
 X c=dom f & for x be Point of S
 st x in X holds f|X is_differentiable_in x;
end;

theorem Th35:
for f be PartFunc of S,T holds
 (f is_differentiable_on X implies X is Subset of the carrier of S)
proof
let f be PartFunc of S,T;
assume f is_differentiable_on X; then X c=dom f by Def8;
 hence thesis by XBOOLE_1:1;
end;

theorem Th36:
for f be PartFunc of S,T
for Z be Subset of S st Z is open holds
( f is_differentiable_on Z iff
Z c=dom f & for x be Point of S
st x in Z holds f is_differentiable_in x )
proof
let f be PartFunc of S,T;
let Z be Subset of S such that
  A1: Z is open;
thus f is_differentiable_on Z implies Z c=dom f &
   for x be Point of S st x in Z holds f is_differentiable_in x
 proof assume A2: f is_differentiable_on Z;
  hence
  A3: Z c=dom f by Def8;
  let x0 be Point of S; assume A4: x0 in Z;
  then f|Z is_differentiable_in x0 by A2,Def8;
  then consider N being Neighbourhood of x0 such that A5: N c= dom(f|Z) &
  ex L,R st for x be Point of S st x in N
  holds (f|Z)/.x-(f|Z)/.x0=L.(x-x0)+R/.(x-x0) by Def6;
  take N;
  A6: dom(f|Z)=dom f/\Z by RELAT_1:90;
  then dom(f|Z) c=dom f by XBOOLE_1:17;
  hence N c= dom f by A5,XBOOLE_1:1;
  consider L,R such that
  A7: for x be Point of S st x in N
    holds (f|Z)/.x - (f|Z)/.x0 = L.(x-x0) + R/.(x-x0) by A5;
  take L,R;
  let x be Point of S; assume A8: x in N;
  then (f|Z)/.x-(f|Z)/.x0=L.(x-x0)+R/.(x-x0) by A7;
   then f/.x-(f|Z)/.x0=L.(x-x0)+R/.(x-x0)
 by A5,A6,A8,PARTFUN2:34;
  hence thesis by A3,A4,PARTFUN2:35;
 end;
 assume A9: Z c=dom f & for x be Point of S st x in Z
 holds f is_differentiable_in x;
 hence Z c=dom f;
 let x0 be Point of S;
 assume A10: x0 in Z; then f is_differentiable_in x0 by A9;
 then consider N being Neighbourhood of x0 such that A11: N c= dom f &
 ex L,R st for x be Point of S
 st x in N holds f/.x-f/.x0=L.(x-x0)+R/.(x-x0) by Def6;
 consider N1 being Neighbourhood of x0
 such that A12: N1 c= Z by A1,A10,Th2;
 consider N2 being Neighbourhood of x0 such that
 A13: N2 c= N1 & N2 c= N by Th1;
 take N2;
 A14: N2 c= dom f by A11,A13,XBOOLE_1:1; N2 c= Z by A12,A13,XBOOLE_1:1;
 then
 A15: N2 c= dom f/\Z by A14,XBOOLE_1:19;
 hence N2 c= dom(f|Z) by RELAT_1:90;
 consider L,R such that
 A16: for x be Point of S
 st x in N holds f/.x-f/.x0=L.(x-x0)+R/.(x-x0) by A11;
 take L,R;
 let x be Point of S; assume A17: x in N2;
  then f/.x-f/.x0=L.(x-x0)+R/.(x-x0) by A13,A16;
then A18: (f|Z)/.x-f/.x0=L.(x-x0)+R/.(x-x0) by A15,A17,PARTFUN2:34;
   x0 in N2 by NFCONT_1:4;
   hence thesis by A15,A18,PARTFUN2:34;
end;

theorem
for f be PartFunc of S,T
for Y be Subset of S holds
  f is_differentiable_on Y implies Y is open
proof
let f be PartFunc of S,T;
let Y be Subset of S;
assume A1: f is_differentiable_on Y;
   now let x0 be Point of S;
   assume x0 in Y; then f|Y is_differentiable_in x0 by A1,Def8;
  then consider N being Neighbourhood of x0 such that A2: N c= dom(f|Y) &
  ex L,R st for x be Point of S st x in N
  holds (f|Y)/.x-(f|Y)/.x0=L.(x-x0)+R/.(x-x0) by Def6;
  take N; dom(f|Y)=dom f/\Y by RELAT_1:90;
  then dom(f|Y) c=Y by XBOOLE_1:17;
  hence N c= Y by A2,XBOOLE_1:1;
 end; hence thesis by Th4;
end;

definition let S,T;
let f be PartFunc of S,T;
let X be set;
 assume A1:f is_differentiable_on X;
 func f`|X -> PartFunc of S, R_NormSpace_of_BoundedLinearOperators(S,T)
 means:Def9:
 dom it = X & for x be Point of S
 st x in X holds it/.x = diff(f,x);
existence
proof
 defpred P[Point of S] means $1 in X;
 deffunc F(Point of S) = diff(f,$1);
 consider F being PartFunc of  S,R_NormSpace_of_BoundedLinearOperators(S,T)
 such that
 A2: (for x be Point of S holds x in dom F iff P[x]) &
 for x be Point of S st x in dom F
 holds F.x = F(x) from SEQ_1:sch 3;
 take F;
   for y st y in dom F holds y in X by A2;
 then A3: dom F c= X by TARSKI:def 3;
   now let y such that
 A4: y in X; X is Subset of S by A1,Th35;
   hence y in dom F by A2,A4;
 end; then X c= dom F by TARSKI:def 3; hence
   dom F = X by A3,XBOOLE_0:def 10;
   now let x be Point of S;
  assume x in X; then A5: x in dom F by A2;
  then F.x = diff(f,x) by A2;
  hence F/.x = diff(f,x) by A5,FINSEQ_4:def 4;
 end; hence thesis;
end;
uniqueness
proof let F,G
be PartFunc of S,R_NormSpace_of_BoundedLinearOperators(S,T);
 assume that A6: dom F = X
 & for x be Point of S st x in X holds F/.x = diff(f,x) and
 A7: dom G = X
 & for x be Point of S st x in X holds G/.x = diff(f,x);
   now let x be Point of S; assume A8: x in dom F;
  then F/.x = diff(f,x) by A6; hence F/.x=G/.x by A6,A7,A8;
 end; hence thesis by A6,A7,PARTFUN2:3;
 end;
end;

theorem
  for f be PartFunc of S,T
  for Z be Subset of S st Z is open & Z c= dom f
   & ex r be Point of T st rng f = {r} holds
   f is_differentiable_on Z
   & for x be Point of S st x in Z holds (f`|Z)/.x
   = 0.R_NormSpace_of_BoundedLinearOperators(S,T)
proof
let f be PartFunc of S,T;
let Z be Subset of S
such that A1: Z is open & Z c= dom f;
 given r be Point of T such that A2: rng f = {r};
   A3: now let x0 be Point of S; assume A4: x0 in dom f;
    then f.x0 in {r} by A2,FUNCT_1:def 5;
    then f/.x0 in {r} by A4,FINSEQ_4:def 4;
    hence f/.x0 = r by TARSKI:def 1;
   end;
 set L = 0.R_NormSpace_of_BoundedLinearOperators(S,T);
 R_NormSpace_of_BoundedLinearOperators(S,T)
 = NORMSTR (# BoundedLinearOperators(S,T),
         Zero_(BoundedLinearOperators(S,T),
         R_VectorSpace_of_LinearOperators(S,T)),
          Add_(BoundedLinearOperators(S,T),
          R_VectorSpace_of_LinearOperators(S,T)),
           Mult_(BoundedLinearOperators(S,T),
           R_VectorSpace_of_LinearOperators(S,T)),
           BoundedLinearOperatorsNorm(S,T) #) by LOPBAN_1:def 15; then
 reconsider L as Element of BoundedLinearOperators(S,T);
A5:  ((the carrier of S) --> 0.T) = L by LOPBAN_1:37;
  reconsider R = (the carrier of S) --> 0.T as PartFunc of S,T by FUNCOP_1:57;
A6: for r be Point of S holds R.r=0.T by FUNCOP_1:13;
  A7: dom R = the carrier of S by FUNCOP_1:19;
  then A8: R is total by PARTFUN1:def 4;
    now let h;
   A9: now let n;
   A10: R/.(h.n) =R.(h.n) by A7,FINSEQ_4:def 4
                   .=0.T by A6;
    A11: rng h c= dom R by A7;
    thus ((||.h.||")(#)(R*h)).n
      = (||.h.||".n)*((R*h).n) by Def2
     .= (||.h.||".n)*(R/.(h.n)) by A11,NFCONT_1:8
     .= 0.T by A10,RLVECT_1:23;
   end;
   then A12: (||.h.||")(#)(R*h) is constant by LOPBAN_3:def 5;
   hence (||.h.||")(#)(R*h) is convergent by Th21;
     ((||.h.||")(#)(R*h)).0 = 0.T by A9;
     hence lim ((||.h.||")(#)(R*h)) = 0.T by A12,Th21;
  end;
  then reconsider R as REST of S,T by A8,Def5;
  A13: now let x0 be Point of S; assume A14: x0 in Z;
   then consider N being Neighbourhood of x0 such that A15: N c= Z
 by A1,Th2;
   A16: N c= dom f by A1,A15,XBOOLE_1:1;
     for x be Point of S st x in N
     holds f/.x - f/.x0 = L.(x-x0) + R/.(x-x0)
   proof let x be Point of S;
       A17: R/.(x-x0) =R.(x-x0) by A7,FINSEQ_4:def 4
                       .=0.T by A6;
    assume x in N;
     hence f/.x - f/.x0 = r - f/.x0 by A3,A16
     .= r - r by A1,A3,A14
     .=0.T  by RLVECT_1:28
     .= 0.T + 0.T by RLVECT_1:10
     .= L.(x-x0)+R/.(x-x0) by A5,A17,FUNCOP_1:13;
   end; hence f is_differentiable_in x0 by A16,Def6;
  end; hence A18:f is_differentiable_on Z by A1,Th36;
  let x0 be Point of S; assume A19: x0 in Z;
   then A20: f is_differentiable_in x0 by A13;
   then ex N being Neighbourhood of x0 st N c= dom f &
   ex L,R st for x be Point of S st x in N
   holds f/.x-f/.x0=L.(x-x0)+R/.(x-x0) by Def6;
   then consider N being Neighbourhood of x0 such that A21: N c= dom f;
   A22: for x be Point of S st x in N
   holds f/.x - f/.x0 = L.(x-x0) + R/.(x-x0)
   proof let x be Point of S;
       A23: R/.(x-x0) =R.(x-x0) by A7,FINSEQ_4:def 4
                       .=0.T by A6;
 assume x in N; hence f/.x - f/.x0 = r - f/.x0 by A3,A21
     .=r - r by A1,A3,A19
     .=0.T  by RLVECT_1:28
     .= 0.T + 0.T by RLVECT_1:10
     .=L.(x-x0) + R/.(x-x0) by A5,A23,FUNCOP_1:13;
   end;
  thus (f`|Z)/.x0 = diff(f,x0) by A18,A19,Def9
   .= 0.R_NormSpace_of_BoundedLinearOperators(S,T) by A20,A21,A22,Def7;
end;

registration let S;
             let h,n;
  cluster h^\n -> convergent_to_0;
coherence
 proof
    h^\n is convergent_to_0
  proof
   A1: h is_not_0 & h is convergent & lim h = 0.S by Def4;
   hence h^\n is_not_0 by Th17;
   thus thesis by A1,LOPBAN_3:14;
  end;
  hence thesis;
 end;
end;

registration let S;
             let c,n;
  cluster c^\n -> constant;
coherence
 proof
    c^\n is constant
  proof c^\n is subsequence of c by LOPBAN_3:13;
   hence c^\n is constant by Th19;
  end;
  hence thesis;
 end;
end;

theorem Th39:
for x0 be Point of S for N being Neighbourhood of x0 st
f is_differentiable_in x0 & N c= dom f
holds
for h be convergent_to_0 sequence of S
for c st rng c = {x0} & rng (h+c) c= N holds
(f*(h+c) - f*c) is convergent
& lim (f*(h+c) - f*c) = 0.T
proof
 let x0 be Point of S;
 let N be Neighbourhood of x0;
 assume A1: f is_differentiable_in x0 & N c= dom f;
 let h be convergent_to_0 sequence of S;
 let c such that
A2: rng c = {x0} & rng (h+c) c= N;
 consider N1 be Neighbourhood of x0 such that
 A3: N1 c= dom f &
    ex L,R st for x be Point of S st x in N1
    holds f/.x - f/.x0 = L.(x-x0) + R/.(x-x0) by A1,Def6;
 consider L,R such that
 A4: for x be Point of S st x in N1
   holds f/.x - f/.x0 = L.(x-x0) + R/.(x-x0) by A3;
 consider N2 be Neighbourhood of x0 such that A5:
 N2 c= N & N2 c= N1 by Th1;
 consider g be Real such that
 A6: 0<g & {y where y is Point of S: ||.y-x0.|| < g} c= N2 by NFCONT_1:def 3;
 A7: x0 in N2 by NFCONT_1:4;
 ex n st rng (c^\n) c= N2 & rng ((h+c)^\n) c= N2
   proof
   A8: c is convergent by Th21;
    c.0 in rng c by NFCONT_1:6;
    then
     c.0=x0 by A2,TARSKI:def 1;
     then
    A9: lim c = x0 by Th21;
   A10: h is convergent & lim h = 0.S by Def4;
   then A11: lim (h + c) = 0.S  + x0 by A8,A9,NORMSP_1:42
       .= x0 by RLVECT_1:10;
    h + c is convergent by A8,A10,NORMSP_1:34;
     then consider n such that
    A12: for m being Nat st n<=m holds
     ||.(h+c).m-x0.||<g by A6,A11,NORMSP_1:def 11;
       c^\n is subsequence of c by Th18;
    then A13: rng (c^\n) = {x0} by A2,Th20; take n;
    thus rng (c^\n) c= N2
     proof let y; assume y in rng (c^\n);
       hence y in N2 by A7,A13,TARSKI:def 1;
     end;
     let y be set; assume y in rng ((h+c)^\n);
    then consider m be Nat such that A14: y = ((h+c)^\n).m by NFCONT_1:6;
      0 <= m by NAT_1:18; then n + 0 <= n+m by REAL_1:55;
    then ||.(h+c).(n+m)-x0.||<g by A12;
    then
    A15: ||.((h+c)^\n).m - x0 .|| < g by LOPBAN_3:def 6;
    reconsider y1=y as Point of S by A14;
    y1 in  {z where z is Point of S: ||.z-x0.|| < g} by A14,A15;
    hence y in N2 by A6;
    end;
 then consider n such that
 A16: rng (c^\n) c= N2 & rng ((h+c)^\n) c= N2;
 A17: rng (c^\n) c= dom f
   proof let y; c^\n is subsequence of c by Th18;
  then A18: rng (c^\n) = rng c by Th20;
    assume y in rng (c^\n); then y = x0 by A2,A18,TARSKI:def 1;
    then y in N by NFCONT_1:4; hence thesis by A1;
   end;
A19:rng ((h+c)^\n) c= dom f
   proof let y; assume y in rng ((h+c)^\n);
    then y in N2 by A16; then y in N by A5; hence
      y in dom f by A1;
   end;
A20: rng c c= dom f
   proof let y; assume y in rng c;
    then y = x0 by A2,TARSKI:def 1; then y in N by NFCONT_1:4;
    hence thesis by A1;
   end;
A21: rng (h+c) c= dom f
   proof let y; assume y in rng (h+c);
    then y in N by A2; hence y in dom f by A1;
   end;
A22: for k holds f/.(((h+c)^\n).k) - f/.((c^\n).k)
= L.((h^\n).k) + R/.((h^\n).k)
  proof
   let k; ((h+c)^\n).k in rng ((h+c)^\n) by NFCONT_1:6;
   then A23: ((h+c)^\n).k in N2 by A16;
A24: ((h+c)^\n).k - (c^\n).k
     = (h+c).(k+n)  - (c^\n).k by LOPBAN_3:def 6
    .=h.(k+n)+ c.(k+n) - (c^\n).k by NORMSP_1:def 5
    .=(h^\n).k+ c.(k+n) - (c^\n).k by LOPBAN_3:def 6
    .=(h^\n).k+ (c^\n).k- (c^\n).k by LOPBAN_3:def 6
    .= (h^\n).k + ((c^\n).k - (c^\n).k) by RLVECT_1:42
    .= (h^\n).k + 0.S by RLVECT_1:28
    .= (h^\n).k by RLVECT_1:10;
 A25: (c^\n).k in rng (c^\n) by NFCONT_1:6;
     c^\n is subsequence of c by Th18;
   then rng (c^\n) = rng c by Th20;
   then (c^\n).k = x0 by A2,A25,TARSKI:def 1;
   hence thesis by A4,A5,A23,A24;
  end;
  R_NormSpace_of_BoundedLinearOperators(S,T) =
  NORMSTR (# BoundedLinearOperators(S,T),
          Zero_(BoundedLinearOperators(S,T),
          R_VectorSpace_of_LinearOperators(S,T)),
           Add_(BoundedLinearOperators(S,T),
           R_VectorSpace_of_LinearOperators(S,T)),
            Mult_(BoundedLinearOperators(S,T),
            R_VectorSpace_of_LinearOperators(S,T)),
            BoundedLinearOperatorsNorm(S,T) #)
          by LOPBAN_1:def 15; then
 reconsider L as Element of BoundedLinearOperators(S,T);
A26:dom modetrans(L,S,T) = the carrier of S by FUNCT_2:def 1; then
A27:rng (h^\n) c= dom modetrans(L,S,T);
 R is total by Def5;
then dom R = the carrier of S by PARTFUN1:def 4;
then
A28: rng (h^\n) c= dom R;
reconsider LP=modetrans(L,S,T) as PartFunc of S,T;
A29: f*((h+c)^\n) - f*(c^\n) = LP*(h^\n) + R*(h^\n)
  proof
     now let k;
     thus (f*((h+c)^\n)-f*(c^\n)).k = (f*((h+c)^\n)).k-(f*(c^\n)).k
         by NORMSP_1:def 6
     .= f/.(((h+c)^\n).k) - (f*(c^\n)).k by A19,NFCONT_1:8
     .= f/.(((h+c)^\n).k) - f/.((c^\n).k) by A17,NFCONT_1:8
     .= L.((h^\n).k) + R/.((h^\n).k) by A22
     .= modetrans(L,S,T).((h^\n).k) + R/.((h^\n).k) by LOPBAN_1:def 12
     .= LP/.((h^\n).k) + R/.((h^\n).k) by A26,FINSEQ_4:def 4
     .= (LP*(h^\n)).k + R/.((h^\n).k) by A27,NFCONT_1:8
     .= (LP*(h^\n)).k + (R*(h^\n)).k by A28,NFCONT_1:8
     .= (LP*(h^\n) + R*(h^\n)).k by NORMSP_1:def 5;
    end; hence thesis by FUNCT_2:113;
  end;
A30:  R*(h^\n) is convergent & lim (R*(h^\n)) = 0.T by Th27;
A31: dom modetrans(L,S,T) = the carrier of S by FUNCT_2:def 1; then
A32:  LP/.(0.S)
          =modetrans(L,S,T).(0.S) by FINSEQ_4:def 4
          .= modetrans(L,S,T).(0*0.S) by RLVECT_1:23
          .=0*modetrans(L,S,T).0.S by LOPBAN_1:def 6
          .=0.T by RLVECT_1:23;
LP is_Lipschitzian_on the carrier of S
proof
set LL=modetrans(L,S,T);
thus the carrier of S c=dom LP by FUNCT_2:def 1;
consider K being Real such that
A33:   0 <= K &
      for x being VECTOR of S holds
         ||. LL.x .|| <= K * ||. x .|| by LOPBAN_1:def 9;
 A34: 0 + K < 1 + K by REAL_1:67;
 A35: 0 + 0 < 1 + K by A33,REAL_1:67;
take K+1;
now let x1,x2 be Point of S
 such that x1 in the carrier of S
               & x2 in the carrier of S;
  A36: ||.LL.(x1-x2) .|| <= K * ||.x1-x2.|| by A33;
  0 <= ||.x1-x2.|| by NORMSP_1:8;
  then
A37:  K * ||.x1-x2.|| <= (K+1 ) *  ||.x1-x2.|| by A34,AXIOMS:25;
 ||.LP/.x1-LP/.x2 .||
 = ||.LL.x1-LP/.x2 .|| by A31,FINSEQ_4:def 4
 .= ||.LL.x1-LL.x2 .|| by A31,FINSEQ_4:def 4
 .= ||.LL.x1+-LL.x2 .|| by RLVECT_1:def 11
 .= ||.LL.x1+(-1)*LL.x2 .|| by RLVECT_1:29
 .= ||.LL.x1+LL.((-1)*x2) .|| by LOPBAN_1:def 6
 .= ||.LL.(x1+(-1)*x2) .|| by LOPBAN_1:def 5
 .= ||.LL.(x1+-x2) .|| by RLVECT_1:29
 .= ||.LL.(x1-x2) .|| by RLVECT_1:def 11;
 hence ||.LP/.x1-LP/.x2 .|| <= (K+1)  * ||.x1-x2.|| by A36,A37,AXIOMS:22;
 end;
 hence thesis by A35;
end;
then
A38: LP is_continuous_on the carrier of S
by NFCONT_1:52;
 h^\n is convergent & rng (h^\n) c= the carrier of S
& lim (h^\n) = 0.S by Def4;
then
LP*(h^\n) is convergent & lim (LP*(h^\n)) = 0.T by A32,A38,NFCONT_1:25;
then
 (LP*(h^\n) + R*(h^\n)) is convergent &
   lim (LP*(h^\n) + R*(h^\n)) = 0.T+0.T
 by A30,NORMSP_1:34,42;then
A39: (LP*(h^\n) + R*(h^\n)) is convergent &
   lim (LP*(h^\n) + R*(h^\n)) = 0.T by RLVECT_1:10;
f*((h+c)^\n) - f*(c^\n)
     =(f*(h+c))^\n - f*(c^\n) by A21,Th29
    .=(f*(h+c))^\n - (f*c) ^\n by A20,Th29
    .= (f*(h+c) - f*c)  ^\n by Th16;
 hence thesis by A29,A39,LOPBAN_3:15,16;
end;

theorem Th40:
for f1,f2,x0 st f1 is_differentiable_in x0 & f2 is_differentiable_in x0
holds f1+f2 is_differentiable_in x0 & diff(f1+f2,x0)=diff(f1,x0)+diff(f2,x0)
proof
let f1,f2,x0;
 assume that A1: f1 is_differentiable_in x0 and
             A2: f2 is_differentiable_in x0;
 consider N1 be Neighbourhood of x0 such that A3: N1 c= dom f1 &
 ex L,R st for x be Point of S st x in N1 holds
 f1/.x - f1/.x0 = L.(x-x0) + R/.(x-x0) by A1,Def6;
 consider L1,R1 such that A4:
 for x be Point of S st x in N1
 holds f1/.x - f1/.x0 = L1.(x-x0) + R1/.(x-x0) by A3;
 consider N2 be Neighbourhood of x0 such that A5: N2 c= dom f2 &
 ex L,R st for x be Point of S st x in N2 holds
 f2/.x - f2/.x0 = L.(x-x0) + R/.(x-x0) by A2,Def6;
 consider L2,R2 such that A6:
 for x be Point of S st x in N2
 holds f2/.x - f2/.x0 = L2.(x-x0) + R2/.(x-x0) by A5;
 consider N be Neighbourhood of x0 such that A7:
 N c= N1 & N c= N2 by Th1;
 set  L=L1+L2;
 reconsider R=R1+R2 as REST of S,T by Th33;
 A8: R1 is total & R2 is total by Def5;
 A9: N c= dom f1 by A3,A7,XBOOLE_1:1; N c= dom f2 by A5,A7,XBOOLE_1:1;
 then N /\ N c= dom f1 /\ dom f2 by A9,XBOOLE_1:27;
 then A10: N c= dom (f1+f2) by VFUNCT_1:def 1;
 A11: now let x be Point of S; assume A12: x in N; A13: x0 in N by NFCONT_1:4;
  thus (f1+f2)/.x - (f1+f2)/.x0
  = (f1/.x+f2/.x) - (f1+f2)/.x0 by A10,A12,VFUNCT_1:def 1
   .=f1/.x+f2/.x - (f1/.x0+f2/.x0) by A10,A13,VFUNCT_1:def 1
   .=f1/.x+f2/.x - f1/.x0 - f2/.x0 by RLVECT_1:41
   .=f2/.x+f1/.x +-f1/.x0 - f2/.x0 by RLVECT_1:def 11
   .=(f1/.x +-f1/.x0) + f2/.x - f2/.x0 by RLVECT_1:def 6
   .=(f1/.x +-f1/.x0) + (f2/.x - f2/.x0) by RLVECT_1:42
   .=(f1/.x - f1/.x0) + (f2/.x - f2/.x0) by RLVECT_1:def 11
   .=L1.(x-x0)+R1/.(x-x0) + (f2/.x - f2/.x0) by A4,A7,A12
   .=L1.(x-x0)+R1/.(x-x0) + (L2.(x-x0) + R2/.(x-x0)) by A6,A7,A12
   .=R1/.(x-x0)+L1.(x-x0) + L2.(x-x0) + R2/.(x-x0) by RLVECT_1:def 6
   .=(L1.(x-x0)+L2.(x-x0)) + R1/.(x-x0) + R2/.(x-x0) by RLVECT_1:def 6
   .=(L1.(x-x0)+L2.(x-x0)) + (R1/.(x-x0) + R2/.(x-x0)) by RLVECT_1:def 6
   .=L.(x-x0)+(R1/.(x-x0) + R2/.(x-x0)) by LOPBAN_1:41
   .=L.(x-x0)+R/.(x-x0) by A8,VFUNCT_1:43;
 end;
 hence f1+f2 is_differentiable_in x0 by A10,Def6;
 hence diff(f1+f2,x0)=L by A10,A11,Def7
  .=diff(f1,x0) + L2 by A1,A3,A4,Def7
  .=diff(f1,x0) + diff(f2,x0) by A2,A5,A6,Def7;
end;

theorem Th41:
for f1,f2,x0 st f1 is_differentiable_in x0 & f2 is_differentiable_in x0
holds f1-f2 is_differentiable_in x0 & diff(f1-f2,x0)=diff(f1,x0)-diff(f2,x0)
proof
let f1,f2,x0;
 assume that A1: f1 is_differentiable_in x0 and
             A2: f2 is_differentiable_in x0;
 consider N1 be Neighbourhood of x0 such that A3: N1 c= dom f1 &
 ex L,R st for x be Point of S st x in N1 holds
 f1/.x - f1/.x0 = L.(x-x0) + R/.(x-x0) by A1,Def6;
 consider L1,R1 such that A4:
 for x be Point of S st x in N1
 holds f1/.x - f1/.x0 = L1.(x-x0) + R1/.(x-x0) by A3;
 consider N2 be Neighbourhood of x0 such that A5: N2 c= dom f2 &
 ex L,R st for x be Point of S st x in N2 holds
 f2/.x - f2/.x0 = L.(x-x0) + R/.(x-x0) by A2,Def6;
 consider L2,R2 such that A6:
 for x be Point of S st x in N2
 holds f2/.x - f2/.x0 = L2.(x-x0) + R2/.(x-x0) by A5;
 consider N be Neighbourhood of x0 such that A7:
 N c= N1 & N c= N2 by Th1;
 set L=L1-L2;
 reconsider R=R1-R2 as REST of S,T by Th33;
 A8: R1 is total & R2 is total by Def5;
 A9: N c= dom f1 by A3,A7,XBOOLE_1:1; N c= dom f2 by A5,A7,XBOOLE_1:1;
 then N /\ N c= dom f1 /\ dom f2 by A9,XBOOLE_1:27;
 then A10: N c= dom (f1-f2) by VFUNCT_1:def 2;
 A11: now let x be Point of S; assume A12: x in N;
  A13: x0 in N by NFCONT_1:4; thus
    (f1-f2)/.x - (f1-f2)/.x0
    = (f1/.x-f2/.x) - (f1-f2)/.x0 by A10,A12,VFUNCT_1:def 2
   .=f1/.x - f2/.x - (f1/.x0-f2/.x0) by A10,A13,VFUNCT_1:def 2
   .=f1/.x - f2/.x - f1/.x0 + f2/.x0 by RLVECT_1:43
   .=f1/.x - (f1/.x0 + f2/.x) + f2/.x0 by RLVECT_1:41
   .=f1/.x - f1/.x0 - f2/.x + f2/.x0 by RLVECT_1:41
   .=f1/.x - f1/.x0 - (f2/.x - f2/.x0) by RLVECT_1:43
   .=L1.(x-x0) + R1/.(x-x0) - (f2/.x - f2/.x0) by A4,A7,A12
   .=L1.(x-x0) + R1/.(x-x0) - (L2.(x-x0) + R2/.(x-x0)) by A6,A7,A12
   .=L1.(x-x0) + R1/.(x-x0) - L2.(x-x0) - R2/.(x-x0) by RLVECT_1:41
   .=L1.(x-x0) + (R1/.(x-x0) - L2.(x-x0)) - R2/.(x-x0) by RLVECT_1:42
   .=L1.(x-x0) + (R1/.(x-x0) +- L2.(x-x0)) - R2/.(x-x0) by RLVECT_1:def 11
   .=L1.(x-x0) +- (L2.(x-x0) - R1/.(x-x0)) - R2/.(x-x0) by RLVECT_1:47
   .=L1.(x-x0) - (L2.(x-x0) - R1/.(x-x0)) - R2/.(x-x0) by RLVECT_1:def 11
   .=L1.(x-x0) - L2.(x-x0) + R1/.(x-x0) - R2/.(x-x0) by RLVECT_1:43
   .=L1.(x-x0) - L2.(x-x0) + (R1/.(x-x0) - R2/.(x-x0)) by RLVECT_1:42
   .=L.(x-x0) + (R1/.(x-x0) - R2/.(x-x0)) by LOPBAN_1:46
   .=L.(x-x0) + R/.(x-x0) by A8,VFUNCT_1:43;
 end;
 hence f1-f2 is_differentiable_in x0 by A10,Def6;
 hence diff(f1-f2,x0)=L by A10,A11,Def7
  .=diff(f1,x0) - L2 by A1,A3,A4,Def7
  .=diff(f1,x0) - diff(f2,x0) by A2,A5,A6,Def7;
end;

theorem Th42:
for r,f,x0 st f is_differentiable_in x0 holds
r(#)f is_differentiable_in x0 & diff((r(#)f),x0) = r*diff(f,x0)
proof
 let r,f,x0;
 assume A1: f is_differentiable_in x0;
 then consider N1 be Neighbourhood of x0 such that A2: N1 c= dom f &
 ex L,R st for x be Point of S st x in N1
    holds f/.x - f/.x0 = L.(x-x0) + R/.(x-x0) by Def6;
 consider L1,R1 such that A3:
 for x be Point of S st x in N1
   holds f/.x - f/.x0 = L1.(x-x0) + R1/.(x-x0) by A2;
 set L = r*L1;
 reconsider R = r(#)R1  as REST of S,T by Th34;
 A4: R1 is total by Def5;
 A5: N1 c= dom(r(#)f) by A2,VFUNCT_1:def 4;
 A6: now let x be Point of S;
      assume A7: x in N1;
  A8: x0 in N1 by NFCONT_1:4; thus
    (r(#)f)/.x - (r(#)f)/.x0
    = r*(f/.x) - (r(#)f)/.x0 by A5,A7,VFUNCT_1:def 4
   .= r*f/.x - r*f/.x0 by A5,A8,VFUNCT_1:def 4
   .= r*(f/.x - f/.x0) by RLVECT_1:48
   .= r*(L1.(x-x0) + R1/.(x-x0)) by A3,A7
   .= r*L1.(x-x0) + r*R1/.(x-x0) by RLVECT_1:def 9
   .= L.(x-x0) + r*R1/.(x-x0) by LOPBAN_1:42
   .= L.(x-x0) + R/.(x-x0) by A4,VFUNCT_1:45;
 end;
 hence r(#)f is_differentiable_in x0 by A5,Def6;
 hence diff((r(#)f),x0) = L by A5,A6,Def7
  .= r*diff(f,x0) by A1,A2,A3,Def7;
end;

theorem
  for f be PartFunc of S,S
  for Z be Subset of S st Z is open & Z c= dom f & f|Z = id Z holds
f is_differentiable_on Z
& for x be Point of S st x in Z
holds (f`|Z)/.x = id  (the carrier of S)
proof
let f be PartFunc of S,S;
let Z be Subset of S such that
A1: Z is open;
 assume that A2: Z c= dom f and A3: f|Z = id Z;
 A4: R_NormSpace_of_BoundedLinearOperators(S,S) =
  NORMSTR (# BoundedLinearOperators(S,S),
         Zero_(BoundedLinearOperators(S,S),
         R_VectorSpace_of_LinearOperators(S,S)),
          Add_(BoundedLinearOperators(S,S),
          R_VectorSpace_of_LinearOperators(S,S)),
           Mult_(BoundedLinearOperators(S,S),
           R_VectorSpace_of_LinearOperators(S,S)),
           BoundedLinearOperatorsNorm(S,S) #) by LOPBAN_1:def 15;
 set L= id (the carrier of S);
 L is bounded LinearOperator of S,S by LOPBAN_2:3;
 then
 reconsider L as Point
   of R_NormSpace_of_BoundedLinearOperators(S,S)
 by A4,LOPBAN_1:def 10;
 reconsider R = (the carrier of S) --> 0.S as PartFunc of S,S by FUNCOP_1:57;
A5: for r be Point of S holds R.r=0.S by FUNCOP_1:13;
 A6: dom R = (the carrier of S) by FUNCOP_1:19;
 then A7: R is total by PARTFUN1:def 4;
   now let h;
  A8: now let n;
     A9: R/.(h.n) =R.(h.n) by A6,FINSEQ_4:def 4
                 .=0.S by A5;
  A10: rng h c= dom R by A6;
  thus ((||.h.||")(#)(R*h)).n = (||.h.||".n)*((R*h).n) by Def2
   .= (||.h.||".n)*(R/.(h.n)) by A10,NFCONT_1:8
   .= 0.S by A9,RLVECT_1:23;
  end;
  then A11: (||.h.||")(#)(R*h) is constant by LOPBAN_3:def 5;
  hence (||.h.||")(#)(R*h) is convergent by Th21;
  ((||.h.||")(#)(R*h)).0 = 0.S by A8;
  hence lim ((||.h.||")(#)(R*h)) = 0.S by A11,Th21;
 end;
 then reconsider R as REST of S,S by A7,Def5;
 A12: now let x be Point of S;
 assume A13: x in Z; then f|Z.x = x by A3,FUNCT_1:35;
     then f.x = x by A13,FUNCT_1:72;
     hence f/.x =x by A2,A13,FINSEQ_4:def 4;
    end;
 A14: now let x0 be Point of S;
  assume A15: x0 in Z;
  then consider N being Neighbourhood of x0
  such that A16: N c= Z by A1,Th2;
  A17: N c= dom f by A2,A16,XBOOLE_1:1;
    for x be Point of S st x in N
     holds f/.x - f/.x0 = L.(x-x0) + R/.(x-x0)
  proof let x be Point of S;
   assume
   A18: x in N;
        A19: R/.(x-x0) =R.(x-x0) by A6,FINSEQ_4:def 4
                 .=0.S by A5;
    thus f/.x - f/.x0 = x - f/.x0 by A12,A16,A18
    .= x - x0 by A12,A15
    .= L.(x-x0) by FUNCT_1:34
    .= L.(x-x0) + R/.(x-x0) by A19,RLVECT_1:10;
  end;
  hence f is_differentiable_in x0 by A17,Def6;
 end;
 hence A20:f is_differentiable_on Z by A1,A2,Th36;
 let x0 be Point of S; assume A21: x0 in Z;
  then A22: f is_differentiable_in x0 by A14;
  then ex N being Neighbourhood of x0 st N c= dom f &
  ex L be Point of
  R_NormSpace_of_BoundedLinearOperators(S,S) ,
  R be REST of S,S st for x be Point of S
  st x in N holds
  f/.x - f/.x0 = L.(x-x0) + R/.(x-x0) by Def6;
  then consider N being Neighbourhood of x0 such that A23: N c= dom f;
  consider N1 being Neighbourhood of x0 such that
  A24: N1 c= Z by A1,A21,Th2;
  consider N2 being Neighbourhood of x0 such that A25: N2 c= N1 & N2 c= N
  by Th1;
  A26: N2 c= dom f by A23,A25,XBOOLE_1:1;
  A27: for x be Point of S st x in N2
  holds f/.x - f/.x0 = L.(x-x0) + R/.(x-x0)
  proof let x be Point of S;
      A28: R/.(x-x0) =R.(x-x0) by A6,FINSEQ_4:def 4
                   .=0.S by A5;
   assume x in N2; then x in N1 by A25;
   hence f/.x - f/.x0 = x - f/.x0 by A12,A24
    .= x - x0 by A12,A21
    .= L.(x-x0) by FUNCT_1:34
    .= L.(x-x0) + R/.(x-x0) by A28,RLVECT_1:10;
  end;
 thus (f`|Z)/.x0 = diff(f,x0) by A20,A21,Def9
  .= id the carrier of S by A22,A26,A27,Def7;
end;

theorem
  for Z be Subset of S st Z is open
  for f1,f2 st Z c= dom (f1+f2) & f1 is_differentiable_on Z &
f2 is_differentiable_on Z holds f1+f2 is_differentiable_on Z &
for x be Point of S st x in Z
holds ((f1+f2)`|Z)/.x = diff(f1,x) + diff(f2,x)
proof
let Z be Subset of S such that A1: Z is open;
let f1,f2;
 assume that A2: Z c= dom (f1+f2) and
             A3: f1 is_differentiable_on Z and
             A4: f2 is_differentiable_on Z;
   now let x0 be Point of S; assume A5: x0 in Z;
  then A6: f1 is_differentiable_in x0 by A1,A3,Th36;
    f2 is_differentiable_in x0 by A1,A4,A5,Th36;
  hence f1+f2 is_differentiable_in x0 by A6,Th40;
 end;
 hence A7:f1+f2 is_differentiable_on Z by A1,A2,Th36;
  now let x be Point of S; assume A8: x in Z;
  then A9: f1 is_differentiable_in x by A1,A3,Th36;
  A10: f2 is_differentiable_in x by A1,A4,A8,Th36;
  thus ((f1+f2)`|Z)/.x = diff((f1+f2),x) by A7,A8,Def9
  .= diff(f1,x) + diff(f2,x) by A9,A10,Th40;
 end; hence thesis;
end;

theorem
for Z be Subset of S st Z is open
for f1,f2 st Z c= dom (f1-f2) & f1 is_differentiable_on Z &
f2 is_differentiable_on Z holds f1-f2 is_differentiable_on Z &
for x be Point of S st x in Z
holds ((f1-f2)`|Z)/.x = diff(f1,x) - diff(f2,x)
proof
let Z be Subset of S such that A1: Z is open;
let f1,f2;
 assume that A2: Z c= dom (f1-f2) and
             A3: f1 is_differentiable_on Z and
             A4: f2 is_differentiable_on Z;
   now let x0 be Point of S; assume A5: x0 in Z;
  then A6: f1 is_differentiable_in x0 by A1,A3,Th36;
    f2 is_differentiable_in x0 by A1,A4,A5,Th36;
  hence f1-f2 is_differentiable_in x0 by A6,Th41;
 end;
 hence A7:f1-f2 is_differentiable_on Z by A1,A2,Th36;
   now let x be Point of S; assume A8: x in Z;
  then A9: f1 is_differentiable_in x by A1,A3,Th36;
  A10: f2 is_differentiable_in x by A1,A4,A8,Th36;
  thus ((f1-f2)`|Z)/.x = diff((f1-f2),x) by A7,A8,Def9
   .= diff(f1,x) - diff(f2,x) by A9,A10,Th41;
 end; hence thesis;
end;

theorem
for Z be Subset of S st Z is open
  for r,f st Z c= dom (r(#)f) & f is_differentiable_on Z holds
r(#)f is_differentiable_on Z & for x be Point of S
st x in Z holds
((r(#)f)`|Z)/.x =r*diff(f,x)
proof
let Z be Subset of S such that A1: Z is open;
let r,f;
 assume that A2: Z c= dom (r(#)f) and
             A3: f is_differentiable_on Z;
   now let x0 be Point of S; assume x0 in Z;
  then f is_differentiable_in x0 by A1,A3,Th36;
  hence r(#)f is_differentiable_in x0 by Th42;
 end;
 hence A4:r(#)f is_differentiable_on Z by A1,A2,Th36;
   now let x be Point of S; assume A5: x in Z;
  then A6: f is_differentiable_in x by A1,A3,Th36;
  thus ((r(#)f)`|Z)/.x = diff((r(#)f),x) by A4,A5,Def9
  .= r*diff(f,x) by A6,Th42;
 end; hence thesis;
end;

theorem
for Z be Subset of S st Z is open holds
(Z c= dom f & f is_constant_on Z implies
f is_differentiable_on Z & for x be Point of S
st x in Z holds (f`|Z)/.x =
0.R_NormSpace_of_BoundedLinearOperators(S,T))
proof
let Z be Subset of S such that A1: Z is open;
assume A2: Z c= dom f & f is_constant_on Z;
 then consider r be Point of T such that A3:
 for x be Point of S st x in Z/\dom f
 holds f.x=r by PARTFUN2:76;
A4: now let x be Point of S;
assume A5: x in Z/\dom f;
Z/\ dom f c= dom f by XBOOLE_1:17;
hence f/.x=f.x by A5,FINSEQ_4:def 4
           .=r by A3,A5;
end;
set L=0.R_NormSpace_of_BoundedLinearOperators(S,T);
 R_NormSpace_of_BoundedLinearOperators(S,T) =
 NORMSTR (# BoundedLinearOperators(S,T),
         Zero_(BoundedLinearOperators(S,T),
         R_VectorSpace_of_LinearOperators(S,T)),
          Add_(BoundedLinearOperators(S,T),
          R_VectorSpace_of_LinearOperators(S,T)),
           Mult_(BoundedLinearOperators(S,T),
           R_VectorSpace_of_LinearOperators(S,T)),
           BoundedLinearOperatorsNorm(S,T) #) by LOPBAN_1:def 15;
then reconsider L as Element of BoundedLinearOperators(S,T);
A6:  ((the carrier of S) --> 0.T) = L by LOPBAN_1:37;
 reconsider R = (the carrier of S) --> 0.T as PartFunc of S,T by FUNCOP_1:57;
A7: for r be Point of S holds R.r=0.T by FUNCOP_1:13;
 A8: dom R = the carrier of S by FUNCOP_1:19;
 then A9: R is total by PARTFUN1:def 4;
    now let h;
  A10: now let n;
      A11: R/.(h.n) =R.(h.n) by A8,FINSEQ_4:def 4
                       .=0.T by A7;
 A12: rng h c= dom R by A8;
    thus ((||.h.||")(#)(R*h)).n = (||.h.||".n)*((R*h).n) by Def2
     .=(||.h.||".n)*(R/.(h.n)) by A12,NFCONT_1:8
     .=0.T by A11,RLVECT_1:23;
  end;
  then A13: (||.h.||")(#)(R*h) is constant by LOPBAN_3:def 5;
  hence (||.h.||")(#)(R*h) is convergent by Th21;
    ((||.h.||")(#)(R*h)).0 = 0.T by A10;
    hence lim ((||.h.||")(#)(R*h)) = 0.T by A13,Th21;
 end;
 then reconsider R as REST of S,T by A9,Def5;
 A14: now let x0 be Point of S;
  assume A15: x0 in Z;
  then A16: x0 in Z/\dom f by A2,XBOOLE_0:def 3;
  consider N being Neighbourhood of x0 such that
  A17: N c= Z by A1,A15,Th2;
  A18: N c= dom f by A2,A17,XBOOLE_1:1;
    for x be Point of S st x in N
      holds f/.x-f/.x0=L.(x-x0)+R/.(x-x0)
  proof let x be Point of S;
       A19: R/.(x-x0) =R.(x-x0) by A8,FINSEQ_4:def 4
                       .=0.T by A7;
   assume x in N; then x in Z/\dom f by A17,A18,XBOOLE_0:def 3;
   hence f/.x-f/.x0=r-f/.x0 by A4
   .=r - r by A4,A16
     .=0.T  by RLVECT_1:28
     .= 0.T + 0.T by RLVECT_1:10
   .=L.(x-x0)+R/.(x-x0) by A6,A19,FUNCOP_1:13;
  end; hence f is_differentiable_in x0 by A18,Def6;
 end; hence A20:f is_differentiable_on Z by A1,A2,Th36;
 let x0 be Point of S;
 assume A21: x0 in Z;
 then A22: x0 in Z/\dom f by A2,XBOOLE_0:def 3;
 A23: f is_differentiable_in x0 by A14,A21;
 consider N being Neighbourhood of x0 such that
 A24: N c= Z by A1,A21,Th2;
 A25: N c= dom f by A2,A24,XBOOLE_1:1;
 A26: for x be Point of S st x in N holds f/.x-f/.x0
 =L.(x-x0)+R/.(x-x0)
  proof let x be Point of S;
   A27: R/.(x-x0) =R.(x-x0) by A8,FINSEQ_4:def 4
                         .=0.T by A7;
   assume x in N; then x in Z/\dom f by A24,A25,XBOOLE_0:def 3;
   hence f/.x - f/.x0 = r - f/.x0 by A4
   .=r - r by A4,A22
     .=0.T  by RLVECT_1:28
     .= 0.T + 0.T by RLVECT_1:10
   .=L.(x-x0) + R/.(x-x0) by A6,A27,FUNCOP_1:13;
  end;
 thus (f`|Z)/.x0 = diff(f,x0) by A20,A21,Def9
 .=0.R_NormSpace_of_BoundedLinearOperators(S,T) by A23,A25,A26,Def7;
end;

theorem
 for f be PartFunc of S,S
 for r be Real for p be Point of S
 for Z be Subset of S st Z is open holds
 ( Z c= dom f & (for x be Point of S
 st x in Z holds f/.x = r*x + p) implies
  f is_differentiable_on Z & for x be Point of S st x in Z
  holds (f`|Z)/.x = r * FuncUnit(S) )
proof
 let f be PartFunc of S,S;
 let r be Real;
 let p be Point of S;
 let Z be Subset of S such that A1: Z is open;
 assume
A2: Z c= dom f
& for x be Point of S st x in Z holds f/.x = r*x + p;
 A3:R_NormSpace_of_BoundedLinearOperators(S,S) =
   NORMSTR (# BoundedLinearOperators(S,S),
          Zero_(BoundedLinearOperators(S,S),
          R_VectorSpace_of_LinearOperators(S,S)),
           Add_(BoundedLinearOperators(S,S),
           R_VectorSpace_of_LinearOperators(S,S)),
            Mult_(BoundedLinearOperators(S,S),
            R_VectorSpace_of_LinearOperators(S,S)),
           BoundedLinearOperatorsNorm(S,S) #) by LOPBAN_1:def 15;
reconsider II=FuncUnit(S) as
      Point of R_NormSpace_of_BoundedLinearOperators(S,S) by A3;
 set L = r * II;
 set V = R_NormSpace_of_BoundedLinearOperators(S,S);
 reconsider L as
 Point of R_NormSpace_of_BoundedLinearOperators(S,S);
 A4:  L=(the Mult of V).([r,II]) by RLVECT_1:def 4
         .=Mult_(BoundedLinearOperators(S,S),
            R_VectorSpace_of_LinearOperators(S,S)).(r,FuncUnit(S))
          by A3,BINOP_1:def 1
         .=r*FuncUnit(S) by LOPBAN_2:def 3;
 reconsider R = (the carrier of S) --> 0.S as PartFunc of S,S by FUNCOP_1:57;
A5: for r be Point of S holds R.r=0.S by FUNCOP_1:13;
 A6: dom R = the carrier of S by FUNCOP_1:19;
 then A7: R is total by PARTFUN1:def 4;
   now let h;
  A8: now let n;
       A9: R/.(h.n) =R.(h.n) by A6,FINSEQ_4:def 4
                         .=0.S by A5;
  A10: rng h c= dom R by A6;
    thus ((||.h.||")(#)(R*h)).n = (||.h.||".n)*((R*h).n) by Def2
     .=(||.h.||".n)*(R/.(h.n)) by A10,NFCONT_1:8
     .=0.S by A9,RLVECT_1:23;
  end;
  then A11: (||.h.||")(#)(R*h) is constant by LOPBAN_3:def 5;
  hence (||.h.||")(#)(R*h) is convergent by Th21;
    ((||.h.||")(#)(R*h)).0 = 0.S by A8;
    hence lim ((||.h.||")(#)(R*h)) = 0.S by A11,Th21;
 end;
 then reconsider R as REST of S,S by A7,Def5;
 A12: now let x0 be Point of S;
 assume A13: x0 in Z;
  then consider N being Neighbourhood of x0 such that
  A14: N c= Z by A1,Th2;
  A15: N c= dom f by A2,A14,XBOOLE_1:1;
    for x be Point of S st x in N
    holds f/.x-f/.x0=L.(x-x0)+R/.(x-x0)
  proof let x be Point of S;
   assume
A16: x in N;
A17: R/.(x-x0) =R.(x-x0) by A6,FINSEQ_4:def 4
                      .=0.S by A5;
     (x-x0)=(id the carrier of S).(x-x0) by FUNCT_1:34; then
A18: r* (x-x0)=r* ((FuncUnit(S)).(x-x0)) by LOPBAN_2:def 5
                 .=L.(x-x0) by LOPBAN_1:42;
   thus f/.x-f/.x0=r*x+p-f/.x0 by A2,A14,A16
   .=r*x+p - (r*x0+p) by A2,A13
   .=r*x+(p-(r*x0+p)) by RLVECT_1:42
   .=r*x+(p-r*x0-p) by RLVECT_1:41
   .=r*x+(p+-r*x0-p) by RLVECT_1:def 11
   .=r*x+(-r*x0+(p-p)) by RLVECT_1:42
   .=r*x+(-r*x0+0.S) by RLVECT_1:28
   .=r*x+-r*x0 by RLVECT_1:10
   .=r*x-r*x0 by RLVECT_1:def 11
   .=r*(x-x0) by RLVECT_1:48
   .=L.(x-x0)+R/.(x-x0) by A17,A18,RLVECT_1:10;
  end; hence f is_differentiable_in x0 by A15,Def6;
 end; hence A19:f is_differentiable_on Z by A1,A2,Th36;
 let x0 be Point of S;
 assume A20: x0 in Z;
 then A21: f is_differentiable_in x0 by A12;
 consider N being Neighbourhood of x0
 such that A22: N c= Z by A1,A20,Th2;
 A23: N c= dom f by A2,A22,XBOOLE_1:1;
 A24: for x be Point of S st x in N holds f/.x-f/.x0=L.(x-x0)+R/.(x-x0)
  proof let x be Point of S;
   assume A25:x in N;
      A26: R/.(x-x0) =R.(x-x0) by A6,FINSEQ_4:def 4
                        .=0.S by A5;
    (x-x0)=(id the carrier of S).(x-x0) by FUNCT_1:34; then
        A27:   r* (x-x0)=r* ((FuncUnit(S)).(x-x0)) by LOPBAN_2:def 5
                    .=L.(x-x0) by LOPBAN_1:42;
    thus f/.x - f/.x0 = r*x+p - f/.x0 by A2,A22,A25
   .=r*x+p-(r*x0+p) by A2,A20
   .=r*x+(p-(r*x0+p)) by RLVECT_1:42
   .=r*x+(p-r*x0-p) by RLVECT_1:41
   .=r*x+(p+-r*x0-p) by RLVECT_1:def 11
   .=r*x+(-r*x0+(p-p)) by RLVECT_1:42
   .=r*x+(-r*x0+0.S) by RLVECT_1:28
   .=r*x+-r*x0 by RLVECT_1:10
   .=r*x-r*x0 by RLVECT_1:def 11
   .=r*(x-x0) by RLVECT_1:48
   .=L.(x-x0)+R/.(x-x0) by A26,A27,RLVECT_1:10;
  end;
 thus (f`|Z)/.x0 = diff(f,x0) by A19,A20,Def9
 .=r * FuncUnit(S) by A4,A21,A23,A24,Def7;
end;

theorem Th49:
for x0 be Point of S holds
 f is_differentiable_in x0 implies f is_continuous_in x0
proof
 let x0 be Point of S;
 assume A1: f is_differentiable_in x0;
 then consider N being Neighbourhood of x0 such that A2: N c= dom f &
 ex L,R st for x be Point of S st x in N
 holds f/.x - f/.x0 = L.(x-x0) + R/.(x-x0) by Def6;
 A3: x0 in N by NFCONT_1:4;
   now let s1 be sequence of S such that
  A4: rng s1 c= dom f & s1 is convergent & lim s1 = x0 & for n holds s1.n<>x0;
  consider g be Real such that
  A5: 0<g & {y where y is Point of S:||.y-x0.|| < g } c= N by NFCONT_1:def 3;
  reconsider s2 = NAT --> x0 as sequence of S by FUNCOP_1:57;
   A6: for n holds s2.n=x0 by FUNCOP_1:13;
  deffunc G(Nat) = s1.$1-s2.$1;
  consider s3 be sequence of S such that
  A7: for n holds s3.n=G(n) from FUNCT_2:sch 4;
  A9: s2 is convergent by Th21;
  A10: lim s2 = s2.0 by Th21
    .=x0 by A6;
  A11: s3=s1-s2 by A7,NORMSP_1:def 6;
  then A12: s3 is convergent by A4,A9,NORMSP_1:35;
  A13: lim s3 =x0-x0 by A4,A9,A10,A11,NORMSP_1:43
    .=0.S by RLVECT_1:28;
  A14: now given n such that A15: s3.n=0.S; s1.n-s2.n=0.S by A7,A15;
   then s1.n-x0=0.S by A6; then s1.n=x0 by RLVECT_1:35;
   hence contradiction by A4;
  end;
  consider l be Nat such that
  A16: for m be Nat st l<=m holds ||.s1.m-x0.||<g
 by A4,A5,NORMSP_1:def 11;
  A17: s3^\l is convergent & lim(s3^\l)=0.S by A12,A13,LOPBAN_3:14;
  now given n such that
A18: (s3^\l).n=0.S; s3.(n+l)=0.S by A18,LOPBAN_3:def 6;
   hence contradiction by A14;
  end;
   then s3^\l is_not_0 by Th7;
  then reconsider h=s3^\l as convergent_to_0
  sequence of S by A17,Def4;
  s2^\l is subsequence of s2 by Th18;
  then reconsider c =s2^\l as constant sequence of S by Th19;
  A20: rng c = {x0}
  proof
  thus rng c c= {x0}
   proof
   let y be set; assume y in rng c; then consider n such that
   A21: y=(s2^\l).n by NFCONT_1:6;
     y=s2.(n+l) by A21,LOPBAN_3:def 6; then y=x0 by A6;
     hence y in {x0} by TARSKI:def 1;
  end;
   let y; assume y in {x0}; then A22:y=x0 by TARSKI:def 1;
     c.0=s2.(0+l) by LOPBAN_3:def 6
   .= y by A6,A22;
   hence y in rng c by NFCONT_1:6;
  end;
   rng (h+c) c= N
  proof let y be set; assume
  A23: y in rng(h+c);
  then consider n such that
   A24: y=(h+c).n by NFCONT_1:6;
   A25: (h+c).n=((s1-s2+s2)^\l).n by A11,Th15
    .=(s1-s2+s2).(n+l) by LOPBAN_3:def 6
    .=(s1-s2).(n+l)+s2.(n+l) by NORMSP_1:def 5
    .=s1.(n+l)-s2.(n+l)+s2.(n+l) by NORMSP_1:def 6
    .=s1.(n+l)-(s2.(n+l)-s2.(n+l)) by RLVECT_1:43
    .=s1.(n+l)-0.S by RLVECT_1:28
    .=s1.(l+n) by RLVECT_1:26;
      l<=l+n by NAT_1:37; then
    A26: ||.(h+c).n-x0.||<g by A16,A25;
    reconsider y1=y as Point of S by A23;
    y1 in {z where z is Point of S:||.z-x0.|| < g } by A24,A26;
    hence y in N by A5;
  end;
 then A27:(f*(h+c) - f*c) is convergent
 & lim (f*(h+c) - f*c) = 0.T by A1,A2,A20,Th39;
 A28: now let p be Real such that A29: 0<p;
  take n=0; let m be Nat such that n<=m;
    x0 in N by NFCONT_1:4;
  then rng c c= dom f by A2,A20,ZFMISC_1:37;
   then ||.(f*c).m-f/.x0.||
   =||.f/.(c.m)-f/.x0.|| by NFCONT_1:8
  .=||.f/.(s2.(m+l))-f/.x0.|| by LOPBAN_3:def 6
  .=||.f/.x0-f/.x0.|| by A6
  .=||.0.T.|| by RLVECT_1:28
  .=0 by NORMSP_1:5;
  hence ||.(f*c).m-f/.x0.||<p by A29;
 end;
 then A30: f*c is convergent by NORMSP_1:def 9;
 then A31: lim(f*c)=f/.x0 by A28,NORMSP_1:def 11;
 A32: f*(h+c)-f*c+f*c is convergent by A27,A30,NORMSP_1:34;
 A33: lim(f*(h+c)-f*c+f*c)=0.T +f/.x0
 by A27,A30,A31,NORMSP_1:42
    .=f/.x0 by RLVECT_1:10;
   now let n;
 thus (f*(h+c)-f*c+f*c).n
 =(f*(h+c)-f*c).n+(f*c).n by NORMSP_1:def 5
  .=(f*(h+c)).n-(f*c).n+(f*c).n by NORMSP_1:def 6
  .=(f*(h+c)).n-((f*c).n-(f*c).n) by RLVECT_1:43
  .=(f*(h+c)).n-0.T by RLVECT_1:28
  .=(f*(h+c)).n by RLVECT_1:26;
 end;
 then A34: f*(h+c)-f*c+(f*c)=f*(h+c) by FUNCT_2:113;
   now let n;
  thus (h+c).n=((s1-s2+s2)^\l).n by A11,Th15
    .=(s1-s2+s2).(n+l) by LOPBAN_3:def 6
    .=(s1-s2).(n+l)+s2.(n+l) by NORMSP_1:def 5
    .=s1.(n+l)-s2.(n+l)+s2.(n+l) by NORMSP_1:def 6
    .=s1.(n+l)-(s2.(n+l)-s2.(n+l)) by RLVECT_1:43
     .=s1.(n+l)-0.S by RLVECT_1:28
     .=s1.(l+n) by RLVECT_1:26
    .=(s1^\l).n by LOPBAN_3:def 6;
 end;
 then A35: f*(h+c)-f*c+(f*c)=f*(s1^\l) by A34,FUNCT_2:113
 .=(f*s1)^\l by A4,Th29;
 hence f*s1 is convergent by A32,LOPBAN_3:15;
 thus f/.x0=lim(f*s1) by A32,A33,A35,LOPBAN_3:16;
end;
hence thesis by A2,A3,Th32;
end;

theorem
  f is_differentiable_on X implies f is_continuous_on X
proof assume A1: f is_differentiable_on X;
 hence X c= dom f by Def8;
 let x be Point of S;
 assume x in X;
 then f|X is_differentiable_in x by A1,Def8;
 hence f|X is_continuous_in x by Th49;
end;

theorem
for Z be Subset of S st Z is open holds
(  f is_differentiable_on X & Z c= X implies f is_differentiable_on Z )
proof
let Z be Subset of S such that
  A1: Z is open;
assume A2: f is_differentiable_on X & Z c= X;
 then X c= dom f by Def8;
 hence A3: Z c= dom f by A2,XBOOLE_1:1;
 let x0; assume A4: x0 in Z;
 then f|X is_differentiable_in x0 by A2,Def8;
 then consider N being Neighbourhood of x0 such that A5: N c= dom(f|X) &
 ex L,R st for x be Point of S st
 x in N holds (f|X)/.x-(f|X)/.x0=L.(x-x0)+R/.(x-x0) by Def6;
 consider N1 being Neighbourhood of x0 such that
A6: N1 c= Z by A1,A4,Th2;
 consider N2 being Neighbourhood of x0 such that
 A7: N2 c= N & N2 c= N1 by Th1;
 take N2;
   dom(f|X)=dom f/\X by RELAT_1:90; then dom(f|X) c=dom f by XBOOLE_1:17;
 then N c= dom f by A5,XBOOLE_1:1;
 then A8: N2 c=dom f by A7,XBOOLE_1:1;
   N2 c= Z by A6,A7,XBOOLE_1:1; then N2 c=dom f/\Z by A8,XBOOLE_1:19;
 hence A9: N2 c=dom(f|Z) by RELAT_1:90;
 consider L,R such that
 A10: for x be Point of S st x in N
 holds (f|X)/.x-(f|X)/.x0=L.(x-x0)+R/.(x-x0) by A5;
 take L,R;
 let x be Point of S; assume A11: x in N2; then A12: x in N by A7;
   (f|X)/.x-(f|X)/.x0=L.(x-x0)+R/.(x-x0) by A7,A10,A11;
 then (f|X)/.x-f/.x0=L.(x-x0)+R/.(x-x0) by A2,A3,A4,PARTFUN2:35;
 then f/.x-f/.x0=L.(x-x0)+R/.(x-x0) by A5,A12,PARTFUN2:32;
 then f/.x-(f|Z)/.x0=L.(x-x0)+R/.(x-x0) by A3,A4,PARTFUN2:35;
 hence thesis by A9,A11,PARTFUN2:32;
end;

theorem
  f is_differentiable_in x0 implies
  (ex N being Neighbourhood of x0 st
   N c= dom f &
   (ex R st R/.0.S=0.T
    &  R is_continuous_in 0.S
    &  for x be Point of S st x in N
        holds f/.x-f/.x0 = diff(f,x0).(x-x0) + R/.(x-x0) ))
proof assume f is_differentiable_in x0;
 then consider N being Neighbourhood of x0 such that
   A1:  N c= dom f and
   A2:  ex R st for x be Point of S
 st x in N holds f/.x - f/.x0 = diff(f,x0).(x-x0) + R/.(x-x0) by Def7;
 take N;
 ex R st R/.0.S=0.T
    &  R is_continuous_in 0.S
    &  for x be Point of S st x in N
        holds f/.x-f/.x0 = diff(f,x0).(x-x0) + R/.(x-x0)
  proof
  consider R such that
   A3: for x be Point of S st x in N
   holds f/.x-f/.x0 = diff(f,x0).(x-x0) + R/.(x-x0) by A2;
   take R;
  R_NormSpace_of_BoundedLinearOperators(S,T) =
  NORMSTR (# BoundedLinearOperators(S,T),
          Zero_(BoundedLinearOperators(S,T),
          R_VectorSpace_of_LinearOperators(S,T)),
           Add_(BoundedLinearOperators(S,T),
           R_VectorSpace_of_LinearOperators(S,T)),
            Mult_(BoundedLinearOperators(S,T),
            R_VectorSpace_of_LinearOperators(S,T)),
            BoundedLinearOperatorsNorm(S,T) #) by LOPBAN_1:def 15;
 then reconsider L =diff(f,x0)
 as Element of BoundedLinearOperators(S,T);
 A4:  L.(0.S) =modetrans(L,S,T).(0.S) by LOPBAN_1:def 12
          .= modetrans(L,S,T).(0*0.S) by RLVECT_1:23
          .=0*modetrans(L,S,T).0.S by LOPBAN_1:def 6
          .=0.T by RLVECT_1:23;
   x0 in N by NFCONT_1:4;
 then f/.x0 - f/.x0 = L.(x0-x0) + R/.(x0-x0) by A3;
 then 0.T = L.(x0-x0) + R/.(x0-x0) by RLVECT_1:28;
 then 0.T = L.0.S + R/.(x0-x0) by RLVECT_1:28;
 then 0.T = L.0.S + R/.0.S by RLVECT_1:28;
 hence
 A5: R/.0.S=0.T by A4,RLVECT_1:10;
 A6: R is total &
 for h holds (||.h.||")(#)(R*h) is convergent
 & lim ((||.h.||")(#)(R*h)) = 0.T by Def5;
  A7: now let s1 be sequence of S;
 assume A8: rng s1 c= dom R
 & s1 is convergent & lim s1 = 0.S &
  (for n holds s1.n<>0.S );
  then s1 is_not_0 by Th7;
  then s1 is convergent_to_0 sequence of S by A8,Def4;
    hence R*s1 is convergent & lim (R*s1)=R/.(0.S) by A5,Th27;
 end;
 dom R=the carrier of S by A6,PARTFUN1:def 4;
 hence thesis by A3,A7,Th32;
end;
hence thesis by A1;
end;
