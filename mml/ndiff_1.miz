:: The Differentiable Functions on Normed Linear Spaces
::  by Hiroshi Imura , Morishige Kimura and Yasunari Shidama
::
:: Received May 24, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies NUMBERS, SUBSET_1, REAL_1, NORMSP_1, SEQ_1, NAT_1, PRE_TOPC,
      RCOMP_1, TARSKI, CARD_1, ARYTM_3, ARYTM_1, XXREAL_0, RELAT_1, FUNCT_1,
      SEQ_2, ORDINAL2, STRUCT_0, VALUED_0, SUPINF_2, RLVECT_1, VALUED_1,
      XREAL_0, ORDINAL1, COMPLEX1, XXREAL_2, FDIFF_1, ZFMISC_1, PARTFUN1,
      FUNCOP_1, FUNCT_2, LOPBAN_1, XBOOLE_0, FCONT_1, RSSPACE, NFCONT_1,
      CFCONT_1, LOPBAN_2;
 notations TARSKI, SUBSET_1, FUNCT_1, RELSET_1, PARTFUN1, FUNCT_2, PRE_TOPC,
      XXREAL_0, XREAL_0, COMPLEX1, ORDINAL1, NUMBERS, REAL_1, NAT_1, RLVECT_1,
      STRUCT_0, FUNCOP_1, VALUED_0, VALUED_1, SEQ_1, SEQ_2, SQUARE_1, VFUNCT_1,
      NORMSP_0, NORMSP_1, RSSPACE, LOPBAN_1, LOPBAN_2, NFCONT_1, FDIFF_1,
      RECDEF_1;
 constructors PARTFUN1, REAL_1, SQUARE_1, NAT_1, COMPLEX1, SEQ_2, PARTFUN2,
      FDIFF_1, BHSP_3, RSSPACE, VFUNCT_1, LOPBAN_3, NFCONT_1, SEQ_1, VALUED_1,
      RECDEF_1, NORMSP_1, RELSET_1, BINOP_2, RVSUM_1;
 registrations FUNCT_1, ORDINAL1, RELSET_1, NUMBERS, XXREAL_0, XREAL_0,
      MEMBERED, FDIFF_1, STRUCT_0, LOPBAN_1, LOPBAN_2, VALUED_0, VALUED_1,
      FUNCT_2, FUNCOP_1;
 requirements SUBSET, REAL, BOOLE, NUMERALS, ARITHM;
 definitions TARSKI, XBOOLE_0, NFCONT_1, NORMSP_1, BHSP_3, RLVECT_1, SUBSET_1,
      VALUED_1, STRUCT_0;
 theorems TARSKI, ABSVALUE, XBOOLE_0, XBOOLE_1, SUBSET_1, RELSET_1, RLVECT_1,
      XCMPLX_0, XCMPLX_1, ZFMISC_1, XREAL_0, FUNCOP_1, SEQ_1, SEQ_2, NAT_1,
      SEQM_3, RELAT_1, FUNCT_1, VFUNCT_1, FUNCT_2, SEQ_4, NORMSP_1, LOPBAN_1,
      LOPBAN_2, LOPBAN_3, PARTFUN1, PARTFUN2, NFCONT_1, FDIFF_1, XREAL_1,
      COMPLEX1, XXREAL_0, ORDINAL1, VALUED_1, STRUCT_0, VALUED_0, GRCAT_1,
      NORMSP_0;
 schemes NAT_1, SEQ_1, RECDEF_1, FUNCT_2;

begin :: Differentiable Function on Normed Linear Spaces

reserve n,k for Element of NAT;
reserve x,y,X for set;
reserve g,r,p for Real;
reserve S for RealNormSpace;
reserve rseq for Real_Sequence;
reserve seq,seq1 for sequence of S;
reserve x0 for Point of S;
reserve Y for Subset of S;

:: Normed Linear Spaces varsions of RCOMP_1:37 -
:: RCOMP_1:37 --> NFCONT_1:4

theorem Th1:
  for x0 be Point of S for N1,N2 being Neighbourhood of x0 ex N
  being Neighbourhood of x0 st N c= N1 & N c= N2
proof
  let x0 be Point of S;
  let N1,N2 be Neighbourhood of x0;
  consider g1 be Real such that
A1: 0<g1 and
A2: {y where y is Point of S: ||.y-x0 .|| < g1} c= N1 by NFCONT_1:def 3;
  consider g2 be Real such that
A3: 0<g2 and
A4: {y where y is Point of S: ||.y-x0 .|| < g2} c= N2 by NFCONT_1:def 3;
  set g = min(g1,g2);
  take {y where y is Point of S: ||.y-x0 .|| < g};
A5: g<=g2 by XXREAL_0:17;
A6: {y where y is Point of S: ||.y-x0 .|| < g} c= {y where y is Point of S:
  ||.y-x0 .|| < g2}
  proof
    let z be set;
    assume z in {y where y is Point of S: ||.y-x0 .|| < g};
    then consider y be Point of S such that
A7: z=y and
A8: ||.y-x0 .|| < g;
    ||.y-x0 .|| < g2 by A5,A8,XXREAL_0:2;
    hence thesis by A7;
  end;
A9: g<=g1 by XXREAL_0:17;
A10: {y where y is Point of S: ||.y-x0 .|| < g} c= {y where y is Point of S:
  ||.y-x0 .|| < g1}
  proof
    let z be set;
    assume z in {y where y is Point of S: ||.y-x0 .|| < g};
    then consider y be Point of S such that
A11: z=y and
A12: ||.y-x0 .|| < g;
    ||.y-x0 .|| < g1 by A9,A12,XXREAL_0:2;
    hence thesis by A11;
  end;
  0<g by A1,A3,XXREAL_0:15;
  hence thesis by A2,A4,A10,A6,NFCONT_1:3,XBOOLE_1:1;
end;

theorem Th2:
  for X being Subset of S st X is open for r be Point of S st r in
  X ex N being Neighbourhood of r st N c= X
proof
  let X be Subset of S;
  assume X is open;
  then
A1: X` is closed by NFCONT_1:def 6;
  let r be Point of S;
  assume that
A2: r in X and
A3: for N be Neighbourhood of r holds not N c= X;
  defpred P[Element of NAT,Point of S] means $2 in {y where y is Point of S:
  ||.y-r.|| < 1/($1+1)} & $2 in X`;
A4: now
    let g be Real such that
A5: 0<g;
    set N={y where y is Point of S:||.y-r.|| < g};
    N is Neighbourhood of r by A5,NFCONT_1:3;
    then not N c= X by A3;
    then consider x be set such that
A6: x in N and
A7: not x in X by TARSKI:def 3;
    consider s be Point of S such that
A8: x=s and
A9: ||.s-r.|| <g by A6;
    take s;
    thus s in N by A9;
    thus s in X` by A7,A8,XBOOLE_0:def 5;
  end;
A10: for n ex s be Point of S st P[n,s]
  proof
    let n;
    0 <= n by NAT_1:2;
    then 0 < 1 * (n + 1)";
    then 0 < 1/(n + 1) by XCMPLX_0:def 9;
    hence thesis by A4;
  end;
  consider s1 be sequence of S such that
A11: for n being Element of NAT holds P[n,s1.n] from FUNCT_2:sch 3(A10);
A12: rng s1 c= X`
  proof
    let x;
    assume x in rng s1;
    then consider y be set such that
A13: y in dom s1 and
A14: s1.y=x by FUNCT_1:def 5;
    reconsider y as Element of NAT by A13;
    s1.y in X` by A11;
    hence thesis by A14;
  end;
A15: now
    let p be Real;
    assume
A16: 0<p;
    consider n such that
A17: p"<n by SEQ_4:10;
    p" + 0 < n + 1 by A17,XREAL_1:10;
    then 1/(n+1) < 1/p" by A16,XREAL_1:78;
    then
A18: 1/(n+1) < p by XCMPLX_1:218;
    take n;
    let m be Element of NAT;
    assume n<=m;
    then
A19: n + 1 <= m + 1 by XREAL_1:8;
    s1.m in {y where y is Point of S:||.y-r.|| < 1/(m+1)} by A11;
    then
A20: ex y be Point of S st s1.m=y & ||.y-r.|| < 1/(m+1);
    0 <= n by NAT_1:2;
    then 1/(m+1) <= 1/(n+1) by A19,XREAL_1:120;
    then ||.s1.m - r.|| < 1/(n+1) by A20,XXREAL_0:2;
    hence ||.s1.m - r.|| <p by A18,XXREAL_0:2;
  end;
  then
A21: s1 is convergent by NORMSP_1:def 9;
  then lim s1 = r by A15,NORMSP_1:def 11;
  then r in X` by A21,A12,A1,NFCONT_1:def 5;
  hence contradiction by A2,XBOOLE_0:def 5;
end;

theorem
  for X being Subset of S st X is open for r be Point of S st r in X
  holds ex g st 0<g & {y where y is Point of S:||.y-r.|| < g} c= X
proof
  let X be Subset of S such that
A1: X is open;
  let r be Point of S;
  assume r in X;
  then consider N be Neighbourhood of r such that
A2: N c= X by A1,Th2;
  consider g such that
A3: 0<g & {y where y is Point of S:||.y-r.|| < g} c= N by NFCONT_1:def 3;
  take g;
  thus thesis by A2,A3,XBOOLE_1:1;
end;

theorem Th4:
  for X being Subset of S holds ((for r be Point of S st r in X
  holds ex N be Neighbourhood of r st N c= X) implies X is open )
proof
  let X be Subset of S;
  assume that
A1: for r be Point of S st r in X holds ex N be Neighbourhood of r st N
  c= X and
A2: not X is open;
  not X` is closed by A2,NFCONT_1:def 6;
  then consider s1 be sequence of S such that
A3: rng s1 c= X` and
A4: s1 is convergent and
A5: not lim s1 in X` by NFCONT_1:def 5;
  consider N be Neighbourhood of (lim s1) such that
A6: N c= X by A1,A5,SUBSET_1:50;
  consider g be Real such that
A7: 0<g and
A8: {y where y is Point of S:||.y-(lim s1).|| < g} c= N by NFCONT_1:def 3;
  consider n such that
A9: for m be Element of NAT st n<=m holds ||.(s1.m) - (lim s1).||<g by A4,A7,
NORMSP_1:def 11;
  n in NAT;
  then n in dom s1 by FUNCT_2:def 1;
  then
A10: s1.n in rng s1 by FUNCT_1:def 5;
  ||.s1.n - (lim s1).|| <g by A9;
  then s1.n in {y where y is Point of S:||.y-(lim s1).|| < g};
  then s1.n in N by A8;
  hence contradiction by A3,A6,A10,XBOOLE_0:def 5;
end;

theorem
  for X being Subset of S holds ( (for r be Point of S st r in X holds
  ex N be Neighbourhood of r st N c= X) iff X is open) by Th2,Th4;

:: Normed Linear Spaces versions of SEQ_1: -

definition
  let X be set, S be ZeroStr;
  let f be Function of X,S;
  redefine attr f is non-zero means
  :Def1:
  rng f c= NonZero S;
  compatibility
  proof
    thus f is non-zero implies rng f c= NonZero S
    proof
      assume f is non-zero;
      then not 0.S in rng f by STRUCT_0:def 16;
      hence thesis by ZFMISC_1:40;
    end;
    assume
A1: rng f c= NonZero S;
    assume 0.S in rng f;
    then not 0.S in {0.S} by A1,XBOOLE_0:def 5;
    hence contradiction by TARSKI:def 1;
  end;
end;

theorem Th6:
  seq is non-zero iff for x st x in NAT holds seq.x<>0.S
proof
  thus seq is non-zero implies for x st x in NAT holds seq.x<>0.S
  proof
    assume seq is non-zero;
    then
A1: rng seq c= NonZero S by Def1;
    let x;
    assume x in NAT;
    then x in dom seq by FUNCT_2:def 1;
    then seq.x in rng seq by FUNCT_1:def 5;
    then not seq.x in {0.S} by A1,XBOOLE_0:def 5;
    hence thesis by TARSKI:def 1;
  end;
  assume
A2: for x st x in NAT holds seq.x<>0.S;
  now
    let y;
    assume
A3: y in rng seq;
    then ex x st x in dom seq & seq.x=y by FUNCT_1:def 5;
    then y<>0.S by A2;
    then not y in {0.S} by TARSKI:def 1;
    hence y in NonZero S by A3,XBOOLE_0:def 5;
  end;
  then rng seq c= NonZero S by TARSKI:def 3;
  hence thesis by Def1;
end;

theorem Th7:
  seq is non-zero iff for n holds seq.n<>0.S
proof
  thus seq is non-zero implies for n holds seq.n<>0.S by Th6;
  assume for n holds seq.n<>0.S;
  then for x holds x in NAT implies seq.x<>0.S;
  hence thesis by Th6;
end;

definition
  let RNS be RealLinearSpace;
  let S be sequence of RNS;
  let a be Real_Sequence;
  func a (#) S -> sequence of RNS means
  :Def2:
  for n holds it.n = a.n * S.n;
  existence
  proof
    deffunc F(Element of NAT) = a.$1 * S.$1;
    thus ex S being sequence of RNS st for n holds S.n = F(n) from FUNCT_2:sch
    4;
  end;
  uniqueness
  proof
    let S1, S2 be sequence of RNS;
    assume that
A1: for n holds S1.n = a.n * S.n and
A2: for n holds S2.n = a.n * S.n;
    for n holds S1.n = S2.n
    proof
      let n;
      S1.n = a.n * S.n by A1;
      hence thesis by A2;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

definition
  let RNS be RealLinearSpace;
  let z be Point of RNS;
  let a be Real_Sequence;
  func a * z -> sequence of RNS means
  :Def3:
  for n holds it.n = a.n * z;
  existence
  proof
    deffunc F(Element of NAT) = a.$1 * z;
    thus ex S being sequence of RNS st for n holds S.n = F(n) from FUNCT_2:sch
    4;
  end;
  uniqueness
  proof
    let S1, S2 be sequence of RNS;
    assume that
A1: for n holds S1.n = a.n * z and
A2: for n holds S2.n = a.n * z;
    for n holds S1.n = S2.n
    proof
      let n;
      S1.n = a.n * z by A1;
      hence thesis by A2;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

theorem
  for rseq1,rseq2 be Real_Sequence holds (rseq1+rseq2)(#)seq=rseq1(#)seq
  +rseq2(#)seq
proof
  let rseq1,rseq2 be Real_Sequence;
  now
    let n;
    thus ((rseq1+rseq2)(#)seq).n =(rseq1+rseq2).n*seq.n by Def2
      .=(rseq1.n+rseq2.n)*seq.n by SEQ_1:11
      .=rseq1.n*seq.n+rseq2.n*seq.n by RLVECT_1:def 9
      .=(rseq1(#)seq).n+rseq2.n*seq.n by Def2
      .=(rseq1(#)seq).n+(rseq2(#)seq).n by Def2
      .=((rseq1(#)seq)+(rseq2(#)seq)).n by NORMSP_1:def 5;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th9:
  for rseq be Real_Sequence for seq1, seq2 be sequence of S holds
  rseq(#)(seq1+seq2)=rseq(#)seq1+rseq(#)seq2
proof
  let rseq be Real_Sequence;
  let seq1, seq2 be sequence of S;
  now
    let n;
    thus (rseq(#)(seq1+seq2)).n =rseq.n*(seq1+seq2).n by Def2
      .=rseq.n*(seq1.n+seq2.n ) by NORMSP_1:def 5
      .=rseq.n*seq1.n+rseq.n*seq2.n by RLVECT_1:def 8
      .=(rseq(#)seq1).n+rseq.n*seq2.n by Def2
      .=(rseq(#)seq1).n+(rseq(#)seq2).n by Def2
      .=((rseq(#)seq1)+(rseq(#)seq2)).n by NORMSP_1:def 5;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th10:
  for rseq be Real_Sequence holds r*(rseq(#)seq) =rseq(#)(r*seq)
proof
  let rseq be Real_Sequence;
  now
    let n;
    thus (r*(rseq(#)seq)).n =r*(rseq(#)seq).n by NORMSP_1:def 8
      .=r*(rseq.n*seq.n) by Def2
      .=(r*rseq.n)*seq.n by RLVECT_1:def 10
      .=rseq.n*(r*seq.n) by RLVECT_1:def 10
      .=rseq.n*(r*seq).n by NORMSP_1:def 8
      .=(rseq(#)(r*seq)).n by Def2;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem
  for rseq1,rseq2 be Real_Sequence holds (rseq1-rseq2)(#)seq=rseq1(#)seq
  -rseq2(#)seq
proof
  let rseq1,rseq2 be Real_Sequence;
  now
    let n;
    thus ((rseq1-rseq2)(#)seq).n =(rseq1+-rseq2).n*seq.n by Def2
      .=(rseq1.n+(-rseq2).n)*seq.n by SEQ_1:11
      .=(rseq1.n+-rseq2.n)*seq.n by SEQ_1:14
      .=(rseq1.n-rseq2.n)*seq.n
      .=rseq1.n*seq.n-rseq2.n*seq.n by RLVECT_1:49
      .=(rseq1(#)seq).n-rseq2.n*seq.n by Def2
      .=(rseq1(#)seq).n-(rseq2(#)seq).n by Def2
      .=((rseq1(#)seq)-(rseq2(#)seq)).n by NORMSP_1:def 6;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th12:
  for rseq be Real_Sequence for seq1, seq2 be sequence of S holds
  rseq(#)(seq1-seq2)=rseq(#)seq1-rseq(#)seq2
proof
  let rseq be Real_Sequence;
  let seq1, seq2 be sequence of S;
  now
    let n;
    thus (rseq(#)(seq1-seq2)).n =rseq.n*(seq1-seq2).n by Def2
      .=rseq.n*(seq1.n-seq2.n ) by NORMSP_1:def 6
      .=rseq.n*seq1.n-rseq.n*seq2.n by RLVECT_1:48
      .=(rseq(#)seq1).n-rseq.n*seq2.n by Def2
      .=(rseq(#)seq1).n-(rseq(#)seq2).n by Def2
      .=((rseq(#)seq1)-(rseq(#)seq2)).n by NORMSP_1:def 6;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th13:
  rseq is convergent & seq is convergent implies rseq (#) seq is convergent
proof
  assume that
A1: rseq is convergent and
A2: seq is convergent;
  consider g1 be real number such that
A3: for p be real number st 0<p ex n be Element of NAT st for m be
  Element of NAT st n<=m holds abs(rseq.m-g1)<p by A1,SEQ_2:def 6;
  consider g2 be Point of S such that
A4: for p be Real st 0<p ex n be Element of NAT st for m be Element of
  NAT st n<=m holds ||.seq.m-g2.||<p by A2,NORMSP_1:def 9;
  reconsider g1 as Real by XREAL_0:def 1;
  take g=g1*g2;
  let p;
  rseq is bounded by A1,SEQ_2:27;
  then consider r be real number such that
A5: 0<r and
A6: for n be Element of NAT holds abs(rseq.n)<r by SEQ_2:15;
  reconsider r as Real by XREAL_0:def 1;
A7: 0+0<||.g2.||+r by A5,NORMSP_1:8,XREAL_1:10;
  assume
A8: 0<p;
  then consider n1 be Element of NAT such that
A9: for m be Element of NAT st n1<=m holds abs(rseq.m-g1)<p/(||.g2.||+r
  ) by A3,A7,XREAL_1:141;
  consider n2 be Element of NAT such that
A10: for m be Element of NAT st n2<=m holds ||.seq.m-g2.||<p/(||.g2.||+r
  ) by A4,A7,A8,XREAL_1:141;
  take n=n1+n2;
  let m be Element of NAT such that
A11: n<=m;
  n1<=n1+n2 by NAT_1:12;
  then n1<=m by A11,XXREAL_0:2;
  then
A12: abs(rseq.m-g1)<=p/(||.g2.||+r) by A9;
  0<=||.g2.|| & ||.(rseq.m-g1)*g2.||=||.g2.||*abs(rseq.m-g1) by NORMSP_1:8,def
2;
  then
A13: ||.(rseq.m-g1)*g2.||<=||.g2.||*(p/(||.g2.||+r)) by A12,XREAL_1:66;
  ||.((rseq(#)seq).m)-g.|| =||.rseq.m*seq.m-g1*g2.|| by Def2
    .=||.rseq.m*seq.m-0.S-g1*g2.|| by RLVECT_1:26
    .=||.rseq.m*seq.m-(rseq.m*g2-rseq.m*g2)-g1*g2.|| by RLVECT_1:28
    .=||.(rseq.m*seq.m-rseq.m*g2+rseq.m*g2)-g1*g2.|| by RLVECT_1:43
    .=||.rseq.m*(seq.m-g2)+rseq.m*g2-g1*g2.|| by RLVECT_1:48
    .=||.rseq.m*(seq.m-g2)+(rseq.m*g2-g1*g2).|| by RLVECT_1:def 6
    .=||.rseq.m*(seq.m-g2)+(rseq.m-g1)*g2.|| by RLVECT_1:49;
  then
A14: ||.((rseq(#)seq).m)-g.||<= ||.rseq.m*(seq.m-g2).||+||.(rseq.m-g1)*g2.||
  by NORMSP_1:def 2;
  n2<=n by NAT_1:12;
  then n2<=m by A11,XXREAL_0:2;
  then
A15: ||.seq.m-g2.||<p/(||.g2.||+r) by A10;
A16: 0<=abs(rseq.m) & 0<=||.seq.m-g2.|| by COMPLEX1:132,NORMSP_1:8;
  abs(rseq.m)<r by A6;
  then abs(rseq.m)*||.seq.m-g2.||<r*(p/(||.g2.||+r)) by A16,A15,XREAL_1:98;
  then
A17: ||.rseq.m*(seq.m-g2).||<r*(p/(||.g2.||+r)) by NORMSP_1:def 2;
  r*(p/(||.g2.||+r))+||.g2.||*(p/(||.g2.||+r)) =(p/(||.g2.||+r))*(||.g2 .||+r)
    .=p by A7,XCMPLX_1:88;
  then ||.rseq.m*(seq.m-g2).||+||.(rseq.m-g1)*g2.||<p by A17,A13,XREAL_1:10;
  hence thesis by A14,XXREAL_0:2;
end;

theorem Th14:
  rseq is convergent & seq is convergent implies lim (rseq (#) seq
  ) = (lim rseq) * (lim seq)
proof
  assume that
A1: rseq is convergent and
A2: seq is convergent;
  set g2= lim seq;
  reconsider g1 = lim rseq as Real;
  set g=g1*g2;
  rseq is bounded by A1,SEQ_2:27;
  then consider r be real number such that
A3: 0<r and
A4: for n be Element of NAT holds abs(rseq.n)<r by SEQ_2:15;
  reconsider r as Real by XREAL_0:def 1;
A5: 0+0<||.g2.||+r by A3,NORMSP_1:8,XREAL_1:10;
A6: 0<=||.g2.|| by NORMSP_1:8;
A7: for p be Real st 0<p ex n be Element of NAT st for m be Element of NAT
  st n <=m holds ||.((rseq(#)seq).m)-g.||<p
  proof
    let p be Real;
    assume 0<p;
    then
A8: 0<p/(||.g2.||+r) by A5,XREAL_1:141;
    then consider n1 be Element of NAT such that
A9: for m be Element of NAT st n1<=m holds abs(rseq.m-g1)<p/(||.g2.||
    +r) by A1,SEQ_2:def 7;
    consider n2 be Element of NAT such that
A10: for m be Element of NAT st n2<=m holds ||.seq.m-g2.||<p/(||.g2.||
    +r) by A2,A8,NORMSP_1:def 11;
    take n=n1+n2;
    let m be Element of NAT such that
A11: n<=m;
    n1<=n1+n2 by NAT_1:12;
    then n1<=m by A11,XXREAL_0:2;
    then
A12: abs(rseq.m-g1)<=p/(||.g2.||+r) by A9;
    ||.(rseq.m-g1)*g2.||=||.g2.||*abs(rseq.m-g1) by NORMSP_1:def 2;
    then
A13: ||.(rseq.m-g1)*g2.||<=||.g2.||*(p/(||.g2.||+r)) by A6,A12,XREAL_1:66;
A14: 0<=abs(rseq.m) & 0<=||.seq.m-g2.|| by COMPLEX1:132,NORMSP_1:8;
    n2<=n by NAT_1:12;
    then n2<=m by A11,XXREAL_0:2;
    then
A15: ||.seq.m-g2.||<p/(||.g2.||+r) by A10;
    ||.((rseq(#)seq).m)-g.|| =||.rseq.m*seq.m-g1*g2.|| by Def2
      .=||.rseq.m*seq.m-0.S-g1*g2.|| by RLVECT_1:26
      .=||.rseq.m*seq.m-(rseq.m*g2-rseq.m*g2)-g1*g2.|| by RLVECT_1:28
      .=||.(rseq.m*seq.m-rseq.m*g2+rseq.m*g2)-g1*g2.|| by RLVECT_1:43
      .=||.rseq.m*(seq.m-g2)+rseq.m*g2-g1*g2.|| by RLVECT_1:48
      .=||.rseq.m*(seq.m-g2)+(rseq.m*g2-g1*g2).|| by RLVECT_1:def 6
      .=||.rseq.m*(seq.m-g2)+(rseq.m-g1)*g2.|| by RLVECT_1:49;
    then
A16: ||.((rseq(#)seq).m)-g.||<= ||.rseq.m*(seq.m-g2).||+||.(rseq.m-g1)*g2
    .|| by NORMSP_1:def 2;
    abs(rseq.m)<r by A4;
    then abs(rseq.m)*||.seq.m-g2.||<r*(p/(||.g2.||+r)) by A14,A15,XREAL_1:98;
    then
A17: ||.rseq.m*(seq.m-g2).||<r*(p/(||.g2.||+r)) by NORMSP_1:def 2;
    r*(p/(||.g2.||+r))+||.g2.||*(p/(||.g2.||+r)) =(p/(||.g2.||+r))*(||.g2
    .||+r)
      .=p by A5,XCMPLX_1:88;
    then ||.rseq.m*(seq.m-g2).||+||.(rseq.m-g1)*g2.||<p by A17,A13,XREAL_1:10;
    hence thesis by A16,XXREAL_0:2;
  end;
  (rseq (#) seq) is convergent by A1,A2,Th13;
  hence thesis by A7,NORMSP_1:def 11;
end;

theorem Th15:
  (seq+seq1) ^\k=(seq ^\k) +(seq1 ^\k)
proof
  now
    let n;
    thus ((seq+seq1) ^\k).n=(seq+seq1).(n+k) by NAT_1:def 3
      .=seq.(n+k) + seq1.(n+k) by NORMSP_1:def 5
      .=(seq ^\k).n +seq1.(n+k) by NAT_1:def 3
      .=(seq ^\k).n +(seq1 ^\k).n by NAT_1:def 3
      .=((seq ^\k) +(seq1 ^\k)).n by NORMSP_1:def 5;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th16:
  (seq-seq1) ^\k=(seq ^\k) -(seq1 ^\k)
proof
  now
    let n;
    thus ((seq-seq1) ^\k).n=(seq-seq1).(n+k) by NAT_1:def 3
      .=seq.(n+k) - seq1.(n+k) by NORMSP_1:def 6
      .=(seq ^\k).n -seq1.(n+k) by NAT_1:def 3
      .=(seq ^\k).n -(seq1 ^\k).n by NAT_1:def 3
      .=((seq ^\k) -(seq1 ^\k)).n by NORMSP_1:def 6;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th17:
  seq is non-zero implies seq ^\k is non-zero
proof
  assume
A1: seq is non-zero;
  now
    let n;
    (seq ^\k).n=seq.(n+k) by NAT_1:def 3;
    hence (seq ^\k).n<>0.S by A1,Th7;
  end;
  hence thesis by Th7;
end;

canceled 3;

definition
  let S;
  let IT be sequence of S;
  attr IT is convergent_to_0 means
  :Def4:
  IT is non-zero & IT is convergent & lim IT = 0.S;
end;

theorem Th21:
  for X be RealNormSpace for seq be sequence of X holds seq is
constant implies ( seq is convergent & for k be Element of NAT holds lim seq =
  seq.k )
proof
  let X be RealNormSpace;
  let seq be sequence of X;
  assume
A1: seq is constant;
  then consider r be Point of X such that
A2: for n be Nat holds seq.n=r by VALUED_0:def 18;
  thus
A3: seq is convergent by A1,LOPBAN_3:17;
  now
    let k be Element of NAT;
    now
      let p be Real such that
A4:   0<p;
      take n=0;
      let m be Element of NAT such that
      n<=m;
      ||.(seq.m)-(seq.k).||=||.r-(seq.k).|| by A2
        .=||.r-r.|| by A2
        .=||.0.X.|| by RLVECT_1:28
        .=0 by NORMSP_1:5;
      hence ||.(seq.m)-(seq.k).||<p by A4;
    end;
    hence lim seq = seq.k by A3,NORMSP_1:def 11;
  end;
  hence thesis;
end;

theorem Th22:
  for r be Real st 0<r & (for n holds seq.n=(1/(n+r))*x0) holds
  seq is convergent
proof
  let r be Real;
  assume that
A1: 0<r and
A2: for n holds seq.n=(1/(n+r))*x0;
  take g=0.S;
  let p be Real;
  assume
A3: 0<p;
  ex pp be Real st pp > 0 & pp*||.x0.|| < p
  proof
    take pp=p/(||.x0.||+1);
A4: ||.x0.||+0 < ||.x0.||+1 & 0 <= ||.x0.|| by NORMSP_1:8,XREAL_1:10;
A5: ||.x0.||+1 > 0+0 by NORMSP_1:8,XREAL_1:10;
    then 0 < p/( ||.x0.||+1 ) by A3,XREAL_1:141;
    then pp* ||.x0.|| < pp*(||.x0.|| + 1) by A4,XREAL_1:99;
    hence thesis by A3,A5,XCMPLX_1:88;
  end;
  then consider pp be Real such that
A6: pp > 0 and
A7: pp*||.x0.|| < p;
  consider k1 be Element of NAT such that
A8: pp"<k1 by SEQ_4:10;
  pp"+0<k1+r by A1,A8,XREAL_1:10;
  then 1/(k1+r)<1/pp" by A6,XREAL_1:78;
  then
A9: 1/(k1+r)<1*pp"" by XCMPLX_0:def 9;
  take n=k1;
  let m be Element of NAT;
  assume n<=m;
  then
A10: n+r<=m+r by XREAL_1:8;
A11: 0 <= ||.x0.|| by NORMSP_1:8;
  0<pp" by A6;
  then 1/(m+r)<=1/(n+r) by A1,A8,A10,XREAL_1:120;
  then 1/(m+r)<pp by A9,XXREAL_0:2;
  then
A12: (1/(m+r))*||.x0.|| <= pp* ||.x0.|| by A11,XREAL_1:66;
A13: 0<=m by NAT_1:2;
  ||.seq.m-g.|| = ||.(1/(m+r))*x0 - 0.S.|| by A2
    .= ||.(1/(m+r))*x0.|| by RLVECT_1:26
    .= abs(1/(m+r))*||.x0.|| by NORMSP_1:def 2
    .= (1/(m+r))*||.x0.|| by A1,A13,ABSVALUE:def 1;
  hence thesis by A7,A12,XXREAL_0:2;
end;

theorem Th23:
  for r be Real st 0<r & (for n holds seq.n=(1/(n+r))*x0 ) holds lim seq=0.S
proof
  let r be Real;
  assume that
A1: 0<r and
A2: for n holds seq.n=1/(n+r)*x0;
A3: now
    let p;
A4: 0 <= ||.x0.|| by NORMSP_1:8;
    assume
A5: 0<p;
    ex pp be Real st pp > 0 & pp*||.x0.|| < p
    proof
      take pp=p/(||.x0.||+1);
A6:   ||.x0.||+0 < ||.x0.||+1 & 0 <= ||.x0.|| by NORMSP_1:8,XREAL_1:10;
A7:   ||.x0.||+1 > 0+0 by NORMSP_1:8,XREAL_1:10;
      then 0 < p/(||.x0.||+1) by A5,XREAL_1:141;
      then pp* ||.x0.|| < pp*(||.x0.|| + 1) by A6,XREAL_1:99;
      hence thesis by A5,A7,XCMPLX_1:88;
    end;
    then consider pp be Real such that
A8: pp > 0 and
A9: pp*||.x0.|| < p;
    consider k1 be Element of NAT such that
A10: pp"<k1 by SEQ_4:10;
    pp"+0<k1+r by A1,A10,XREAL_1:10;
    then 1/(k1+r)<1/pp" by A8,XREAL_1:78;
    then
A11: 1/(k1+r)<1*pp"" by XCMPLX_0:def 9;
    take n=k1;
    let m be Element of NAT;
    assume n<=m;
    then
A12: n+r<=m+r by XREAL_1:8;
    0<pp" by A8;
    then 1/(m+r)<=1/(n+r) by A1,A10,A12,XREAL_1:120;
    then 1/(m+r)<pp by A11,XXREAL_0:2;
    then
A13: (1/(m+r))*||.x0.|| <= pp* ||.x0.|| by A4,XREAL_1:66;
A14: 0<=m by NAT_1:2;
    ||.seq.m-0.S.|| = ||.(1/(m+r))*x0 - 0.S.|| by A2
      .= ||.(1/(m+r))*x0.|| by RLVECT_1:26
      .= abs(1/(m+r))*||.x0.|| by NORMSP_1:def 2
      .= (1/(m+r))*||.x0.|| by A1,A14,ABSVALUE:def 1;
    hence ||.seq.m-0.S.||<p by A9,A13,XXREAL_0:2;
  end;
  seq is convergent by A1,A2,Th22;
  hence thesis by A3,NORMSP_1:def 11;
end;

theorem
  for a be convergent_to_0 Real_Sequence for z be Point of S st z <> 0.S
  holds a*z is convergent_to_0
proof
  let a be convergent_to_0 Real_Sequence;
  let z be Point of S such that
A1: z <> 0.S;
  now
    let n be Element of NAT;
    a is non-zero by FDIFF_1:def 1;
    then
A2: a.n <> 0 by SEQ_1:7;
    assume (a*z).n = 0.S;
    then a.n*z = 0.S by Def3;
    hence contradiction by A1,A2,RLVECT_1:24;
  end;
  hence a*z is non-zero by Th7;
A3: now
    let p;
    assume
A4: 0<p;
    ex pp be Real st pp > 0 & pp*||.z.|| < p
    proof
      take pp=p/(||.z.||+1);
A5:   ||.z.||+0 < ||.z.||+1 & 0 <= ||.z.|| by NORMSP_1:8,XREAL_1:10;
A6:   ||.z.||+1 > 0+0 by NORMSP_1:8,XREAL_1:10;
      then 0 < p/(||.z.||+1) by A4,XREAL_1:141;
      then pp* ||.z.|| < pp*(||.z.|| + 1) by A5,XREAL_1:99;
      hence thesis by A4,A6,XCMPLX_1:88;
    end;
    then consider pp be Real such that
A7: pp > 0 and
A8: pp*||.z.|| < p;
    a is convergent & lim a =0 by FDIFF_1:def 1;
    then consider n be Element of NAT such that
A9: for m be Element of NAT st n <= m holds abs(a.m- 0) < pp by A7,SEQ_2:def 7;
    take n;
    let m be Element of NAT;
    assume n<=m;
    then
A10: abs(a.m-0) < pp by A9;
A11: ||.(a*z).m-0.S.|| = ||.a.m*z - 0.S.|| by Def3
      .= ||.a.m*z.|| by RLVECT_1:26
      .= abs(a.m)*||.z.|| by NORMSP_1:def 2;
    0 <= ||.z.|| by NORMSP_1:8;
    then abs(a.m)*||.z.|| <= pp* ||.z.|| by A10,XREAL_1:66;
    hence ||.(a*z).m-0.S.||<p by A8,A11,XXREAL_0:2;
  end;
  hence a*z is convergent by NORMSP_1:def 9;
  hence thesis by A3,NORMSP_1:def 11;
end;

theorem
  (for r be Point of S holds r in Y iff r in the carrier of S) iff Y=the
  carrier of S
proof
  thus (for r be Point of S holds r in Y iff r in the carrier of S) implies Y=
  the carrier of S
  proof
    assume for r be Point of S holds r in Y iff r in the carrier of S;
    then for y holds y in Y iff y in the carrier of S;
    hence thesis by TARSKI:2;
  end;
  assume
A1: Y=the carrier of S;
  let r be Point of S;
  thus r in Y implies r in the carrier of S;
  thus thesis by A1;
end;

reserve S,T for non trivial RealNormSpace;
reserve f,f1,f2 for PartFunc of S,T;
reserve s1 for sequence of S;
reserve x0 for Point of S;

registration
  let S be non trivial RealNormSpace;
  cluster convergent_to_0 sequence of S;
  existence
  proof
    consider x0 be Point of S such that
A1: x0 <> 0.S by STRUCT_0:def 19;
    deffunc F(Element of NAT) = (1/($1+1))*x0;
    consider s1 be sequence of S such that
A2: for n holds s1.n=F(n) from FUNCT_2:sch 4;
    take s1;
    now
      let n;
      (n+1)" <> 0 by NAT_1:5,XCMPLX_1:203;
      then
A3:   1/(n+1) <> 0 by XCMPLX_1:217;
      thus s1.n <> 0.S
      proof
        assume s1.n = 0.S;
        then (1/(n+1))*x0 = 0.S by A2;
        hence contradiction by A1,A3,RLVECT_1:24;
      end;
    end;
    hence s1 is non-zero by Th7;
    thus thesis by A2,Th22,Th23;
  end;
end;

registration
  let S;
  cluster constant sequence of S;
  existence
  proof
    reconsider s1 = NAT --> 0.S as sequence of S;
    take s1;
    thus thesis;
  end;
end;
 
:: Normed Linear Spaces versions of FDIFF_1

reserve h for convergent_to_0 sequence of S;
reserve c for constant sequence of S;

definition
  let S,T;
  let IT be PartFunc of S,T;
  attr IT is REST-like means
  :Def5:
  IT is total & for h holds (||.h.||")(#)(IT
  /*h) is convergent & lim ((||.h.||")(#)(IT/*h)) = 0.T;
end;

registration
  let S,T;
  cluster REST-like PartFunc of S,T;
  existence
  proof
    reconsider f = (the carrier of S) --> 0.T as PartFunc of S,T;
    take f;
    thus f is total;
A1: dom f = the carrier of S by FUNCOP_1:19;
    now
      let h;
      now
        let n be Nat;
A2:     f/.(h.n) =f.(h.n) by A1,PARTFUN1:def 8
          .=0.T by FUNCOP_1:13;
A3:     rng h c= dom f by A1;
A4:     n in NAT by ORDINAL1:def 13;
        hence ((||.h.||")(#)(f/*h)).n = (||.h.||").n*((f/*h).n) by Def2
          .= (||.h.||").n*(f/.(h.n)) by A3,A4,FUNCT_2:186
          .= 0.T by A2,RLVECT_1:23;
      end;
      then (||.h.||")(#)(f/*h) is constant & ((||.h.||")(#)(f/*h)).0 = 0.T by
VALUED_0:def 18;
      hence
      (||.h.||")(#)(f/*h) is convergent & lim ((||.h.||")(#)(f/*h)) = 0.T
      by Th21;
    end;
    hence thesis;
  end;
end;

definition
  let S,T;
  mode REST of S,T is REST-like PartFunc of S,T;
end;

theorem
  for R be PartFunc of S,T st R is total holds R is REST-like iff for r
be Real st r > 0 ex d be Real st d > 0 & for z be Point of S st z <> 0.S & ||.z
  .|| < d holds ( ||.z.||"* ||. R/.z .||) < r
proof
  let R be PartFunc of S,T such that
A1: R is total;
A2: now
    assume
A3: R is REST-like;
    assume not (for r be Real st r > 0 ex d be Real st d > 0 & for z be Point
    of S st z <> 0.S & ||.z.|| < d holds ( ||.z.||"* ||. R/.z .||) < r );
    then consider r be Real such that
A4: r > 0 and
A5: for d be Real st d > 0 holds ex z be Point of S st z <> 0.S & ||.z
    .|| < d & not ( ||.z.||"* ||. R/.z .||) < r;
    defpred P[Element of NAT,Point of S] means $2 <> 0.S & ||.$2.|| < (1/($1+1
    )) & not ( ( ||.$2.||"* ||. R/.$2 .||) < r );
A6: for n be Element of NAT ex z be Point of S st P[n,z]
    proof
      let n be Element of NAT;
      0 <= n by NAT_1:2;
      then 0 < 1 * (n + 1)";
      then 0 < 1/(n + 1) by XCMPLX_0:def 9;
      hence thesis by A5;
    end;
    consider s be sequence of S such that
A7: for n being Element of NAT holds P[n,s.n] from FUNCT_2:sch 3(A6);
A8: now
      let p be Real;
      assume
A9:   0<p;
      consider n such that
A10:  p"<n by SEQ_4:10;
      p" + 0 < n + 1 by A10,XREAL_1:10;
      then 1/(n+1) < 1/p" by A9,XREAL_1:78;
      then
A11:  1/(n+1) < p by XCMPLX_1:218;
      take n;
      let m be Element of NAT;
      assume n<=m;
      then
A12:  n + 1 <= m + 1 by XREAL_1:8;
      ||.s.m.|| < (1/(m+1)) by A7;
      then
A13:  ||.s.m-0.S.|| < (1/(m+1)) by RLVECT_1:26;
      0 <= n by NAT_1:2;
      then 1/(m+1) <= 1/(n+1) by A12,XREAL_1:120;
      then ||.s.m - 0.S.|| < 1/(n+1) by A13,XXREAL_0:2;
      hence ||.s.m - 0.S.|| <p by A11,XXREAL_0:2;
    end;
    then
A14: s is convergent by NORMSP_1:def 9;
    then
A15: lim s = 0.S by A8,NORMSP_1:def 11;
    s is non-zero by A7,Th7;
    then reconsider s as convergent_to_0 sequence of S by A14,A15,Def4;
    (||.s.||")(#)(R/*s) is convergent & lim ((||.s.||")(#)(R/*s)) = 0.T
    by A3,Def5;
    then consider n be Element of NAT such that
A16: for m be Element of NAT st n <=m holds ||. ((||.s.||")(#)(R/*s)).
    m- 0.T.|| < r by A4,NORMSP_1:def 11;
A17: ||. ((||.s.||")(#)(R/*s)).n- 0.T.|| < r by A16;
    s.n <> 0.S by A7;
    then ||.s.n.|| <> 0 by NORMSP_0:def 5;
    then
A18: ||.s.n.|| > 0 by NORMSP_1:8;
A19: ||.(||.s.n.||)"*(R/.(s.n)).|| = abs((||.s.n.||)") * ||.(R/.(s.n)).||
    by NORMSP_1:def 2
      .=(||.s.n.||)" * ||.(R/.(s.n)).|| by A18,ABSVALUE:def 1;
    dom R = the carrier of S by A1,PARTFUN1:def 4;
    then
A20: rng s c= dom R;
    ||. ((||.s.||")(#)(R/*s)).n- 0.T.|| = ||. ((||.s.||")(#)(R/*s)).n .||
    by RLVECT_1:26
      .= ||.(||.s.||".n)*((R/*s).n).|| by Def2
      .= ||.(||.s.||.n)"*((R/*s).n).|| by VALUED_1:10
      .= ||.(||.s.n.||)"*((R/*s).n).|| by NORMSP_0:def 4
      .= ||.(||.s.n.||)"*(R/.(s.n)).|| by A20,FUNCT_2:186;
    hence
    for r be Real st r > 0 ex d be Real st d > 0 & for z be Point of S st
    z <> 0.S & ||.z.|| < d holds ( ||.z.||"* ||. R/.z .||) < r by A7,A17,A19;
  end;
  now
    assume
A21: for r be Real st r > 0 ex d be Real st d > 0 & for z be Point of
    S st z <> 0.S & ||.z.|| < d holds ( ||.z.||"* ||. R/.z .||) < r;
    now
      let s be convergent_to_0 sequence of S;
A22:  s is non-zero by Def4;
A23:  s is convergent & lim s = 0.S by Def4;
A24:  now
        let r be Real;
        assume r > 0;
        then consider d be Real such that
A25:    d > 0 and
A26:    for z be Point of S st z <> 0.S & ||.z.|| < d holds ( ||.z.||
        "* ||. R/.z .||) < r by A21;
        consider n be Element of NAT such that
A27:    for m be Element of NAT st n <=m holds ||.s.m-0.S.|| < d by A23,A25,
NORMSP_1:def 11;
        take n;
        thus for m be Element of NAT st n <=m holds ||. ((||.s.||")(#)(R/*s)).
        m- 0.T.|| < r
        proof
          dom R = the carrier of S by A1,PARTFUN1:def 4;
          then
A28:      rng s c= dom R;
          let m be Element of NAT;
          assume n <=m;
          then ||.s.m-0.S .|| < d by A27;
          then
A29:      ||.s.m.|| < d by RLVECT_1:26;
A30:      s.m <> 0.S by A22,Th7;
          s.m <> 0.S by A22,Th7;
          then ||.s.m.|| <> 0 by NORMSP_0:def 5;
          then ||.s.m.|| > 0 by NORMSP_1:8;
          then
          (||.s.m.||)" * ||.(R/.(s.m)).|| =abs((||.s.m.||)") * ||.(R/.(s.
          m)).|| by ABSVALUE:def 1
            .= ||.(||.s.m.||)"*(R/.(s.m)).|| by NORMSP_1:def 2
            .= ||.(||.s.m.||)"*((R/*s).m).|| by A28,FUNCT_2:186
            .= ||.(||.s.||.m)"*((R/*s).m).|| by NORMSP_0:def 4
            .= ||.(||.s.||".m)*((R/*s).m).|| by VALUED_1:10
            .= ||. ((||.s.||")(#)(R/*s)).m .|| by Def2
            .= ||. ((||.s.||")(#)(R/*s)).m- 0.T.|| by RLVECT_1:26;
          hence thesis by A26,A29,A30;
        end;
      end;
      hence (||.s.||")(#)(R/*s) is convergent by NORMSP_1:def 9;
      hence lim ((||.s.||")(#)(R/*s)) = 0.T by A24,NORMSP_1:def 11;
    end;
    hence R is REST-like by A1,Def5;
  end;
  hence thesis by A2;
end;

theorem Th27:
  for R be REST of S,T for s be convergent_to_0 sequence of S
  holds (R/*s) is convergent & lim (R/*s) = 0.T
proof
  let R be REST of S,T;
  let s be convergent_to_0 sequence of S;
A1: (||.s.||") (#) (R/*s) is convergent by Def5;
A2: s is non-zero by Def4;
  now
    let n be Element of NAT;
    s.n <> 0.S by A2,Th7;
    then
A3: ||.s.n.|| <> 0 by NORMSP_0:def 5;
    thus (||.s.||(#) ( (||.s.||") (#) (R/*s) )).n =||.s.||.n*((||.s.||")(#)(R
    /*s)).n by Def2
      .=||.s.||.n*((||.s.||").n*((R/*s).n)) by Def2
      .= ||.s.n.||*((||.s.||").n*((R/*s).n)) by NORMSP_0:def 4
      .= ||.s.n.||*((||.s.||.n)"*((R/*s).n)) by VALUED_1:10
      .= ||.s.n.||*((||.s.n.||)"*((R/*s).n)) by NORMSP_0:def 4
      .= ||.s.n.||*(||.s.n.||)"*((R/*s).n) by RLVECT_1:def 10
      .= 1*(R/*s).n by A3,XCMPLX_0:def 7
      .=(R/*s).n by RLVECT_1:def 11;
  end;
  then
A4: ||.s.||(#) ( (||.s.||") (#) (R/*s) )= (R/*s) by FUNCT_2:113;
A5: s is convergent by Def4;
  then
A6: ||.s.|| is convergent by LOPBAN_1:47;
  lim s = 0.S by Def4;
  then lim ( ||.s.|| ) = ||.0.S.|| by A5,LOPBAN_1:47;
  then
A7: lim ( ||.s.|| ) =0 by NORMSP_1:5;
  lim ((||.s.||") (#) (R/*s) ) = 0.T by Def5;
  then lim (R/*s) = 0* 0.T by A5,A4,A1,A7,Th14,LOPBAN_1:47;
  hence thesis by A4,A1,A6,Th13,RLVECT_1:23;
end;

reserve R,R1,R2 for REST of S,T;
reserve L,L1,L2 for Point of R_NormSpace_of_BoundedLinearOperators(S,T);

canceled 2;

theorem Th30:
  for h1,h2 be PartFunc of S,T for seq be sequence of S holds h1
is total & h2 is total implies (h1+h2)/*seq = h1/*seq + h2/*seq & (h1-h2)/*seq
  = h1/*seq - h2/*seq
proof
  let h1,h2 be PartFunc of S,T;
  let seq be sequence of S;
  assume h1 is total & h2 is total;
  then h1+h2 is total by VFUNCT_1:38;
  then dom (h1+h2) = the carrier of S by PARTFUN1:def 4;
  then dom h1 /\ dom h2 = the carrier of S by VFUNCT_1:def 1;
  then
A1: rng seq c= dom h1 /\ dom h2;
  hence (h1+h2)/*seq = h1/*seq + h2/*seq by NFCONT_1:19;
  thus thesis by A1,NFCONT_1:19;
end;

theorem Th31:
  for h be PartFunc of S,T for seq be sequence of S for r be Real
  holds h is total implies (r(#)h)/*seq = r*(h/*seq)
proof
  let h be PartFunc of S,T;
  let seq be sequence of S;
  let r be Real;
  assume h is total;
  then dom h = the carrier of S by PARTFUN1:def 4;
  then rng seq c= dom h;
  hence thesis by NFCONT_1:20;
end;

theorem Th32:
  f is_continuous_in x0 iff x0 in dom f & for s1 be sequence of S
  st rng s1 c= dom f & s1 is convergent & lim s1=x0 & (for n holds s1.n<>x0)
  holds f/*s1 is convergent & f/.x0=lim(f/*s1)
proof
  thus f is_continuous_in x0 implies x0 in dom f & for s1 be sequence of S st
rng s1 c= dom f & s1 is convergent & lim s1=x0 & (for n holds s1.n<>x0) holds f
  /*s1 is convergent & f/.x0=lim(f/*s1) by NFCONT_1:def 9;
  assume that
A1: x0 in dom f and
A2: for s1 st rng s1 c=dom f & s1 is convergent & lim s1=x0 & (for n
  holds s1.n<>x0) holds f/*s1 is convergent & f/.x0=lim(f/*s1);
  thus x0 in dom f by A1;
  let s2 be sequence of S such that
A3: rng s2 c=dom f and
A4: s2 is convergent & lim s2=x0;
  now
    per cases;
    suppose
      ex n st for m be Element of NAT st n<=m holds s2.m=x0;
      then consider N be Element of NAT such that
A5:   for m be Element of NAT st N<=m holds s2.m=x0;
A6:   for n holds (s2^\N).n=x0
      proof
        let n;
        s2.(n+N)=x0 by A5,NAT_1:12;
        hence thesis by NAT_1:def 3;
      end;
A7:   rng (s2^\N) c= rng s2 by VALUED_0:21;
A8:   now
        let p be Real such that
A9:     p>0;
        take n=0;
        let m be Element of NAT such that
        n<=m;
        ||.(f/*(s2^\N)).m-f/.x0.|| =||.f/.((s2^\N).m)-f/.x0.|| by A3,A7,
FUNCT_2:186,XBOOLE_1:1
          .=||.f/.x0-f/.x0.|| by A6
          .=||.0.T.|| by RLVECT_1:28
          .=0 by NORMSP_1:5;
        hence ||.(f/*(s2^\N)).m-f/.x0.||< p by A9;
      end;
      then
A10:  f/*(s2^\N) is convergent by NORMSP_1:def 9;
A11:  f/*(s2^\N)=(f/*s2)^\N by A3,VALUED_0:27;
      then
A12:  f/*s2 is convergent by A10,LOPBAN_3:15;
      f/.x0=lim((f/*s2)^\N) by A8,A10,A11,NORMSP_1:def 11;
      hence thesis by A12,LOPBAN_3:14;
    end;
    suppose
A13:  for n ex m be Element of NAT st n<=m & s2.m<>x0;
      defpred P[Element of NAT,set,set] means for n,m be Element of NAT st $2=
      n & $3=m holds n<m & s2.m<>x0 & for k st n<k & s2.k<>x0 holds m<=k;
      defpred P[Nat] means s2.$1<>x0;
      ex m1 be Element of NAT st 0<=m1 & s2.m1<>x0 by A13;
      then
A14:  ex m be Nat st P[m];
      consider M be Nat such that
A15:  P[M] & for n be Nat st P[n] holds M<=n from NAT_1:sch 5(A14);
      reconsider M9 = M as Element of NAT by ORDINAL1:def 13;
A16:  now
        let n;
        consider m be Element of NAT such that
A17:    n+1<=m & s2.m<>x0 by A13;
        take m;
        thus n<m & s2.m<>x0 by A17,NAT_1:13;
      end;
A18:  for n for x be Element of NAT ex y be Element of NAT st P[n,x,y]
      proof
        let n;
        let x be Element of NAT;
        defpred P[Nat] means x<$1 & s2.$1<>x0;
        ex m be Element of NAT st P[m] by A16;
        then
A19:    ex m be Nat st P[m];
        consider l be Nat such that
A20:    P[l] & for k be Nat st P[k] holds l<=k from NAT_1:sch 5(A19);
        reconsider l as Element of NAT by ORDINAL1:def 13;
        take l;
        thus thesis by A20;
      end;
      consider F be Function of NAT,NAT such that
A21:  F.0=M9 & for n be Element of NAT holds P[n,F.n,F.(n+1)] from
      RECDEF_1:sch 2(A18);
A22:  rng F c= REAL by XBOOLE_1:1;
A23:  rng F c= NAT;
A24:  dom F=NAT by FUNCT_2:def 1;
      then reconsider F as Real_Sequence by A22,RELSET_1:11;
A25:  now
        let n;
        F.n in rng F by A24,FUNCT_1:def 5;
        hence F.n is Element of NAT by A23;
      end;
      now
        let n;
        F.n is Element of NAT & F.(n+1) is Element of NAT by A25;
        hence F.n<F.(n+1) by A21;
      end;
      then reconsider F as increasing sequence of NAT by SEQM_3:def 11;
A26:  s2*F is convergent & lim (s2*F)=x0 by A4,LOPBAN_3:11,12;
A27:  for n st s2.n<>x0 ex m be Element of NAT st F.m=n
      proof
        defpred P[Nat] means s2.$1<>x0 & for m be Element of NAT holds F.m<>$1;
        assume ex n st P[n];
        then
A28:    ex n be Nat st P[n];
        consider M1 be Nat such that
A29:    P[M1] & for n be Nat st P[n] holds M1<=n from NAT_1:sch 5(A28
        );
        defpred P[Nat] means $1<M1 & s2.$1<>x0 & ex m be Element of NAT st F.m
        =$1;
A30:    ex n be Nat st P[n]
        proof
          take M;
          M<=M1 & M <> M1 by A15,A21,A29;
          hence M<M1 by XXREAL_0:1;
          thus s2.M<>x0 by A15;
          take 0;
          thus thesis by A21;
        end;
A31:    for n be Nat st P[n] holds n<=M1;
        consider MX be Nat such that
A32:    P[MX] & for n be Nat st P[n] holds n<=MX from NAT_1:sch 6(A31
        ,A30);
A33:    for k st MX<k & k<M1 holds s2.k=x0
        proof
          given k such that
A34:      MX<k and
A35:      k<M1 & s2.k<>x0;
          now
            per cases;
            suppose
              ex m be Element of NAT st F.m=k;
              hence contradiction by A32,A34,A35;
            end;
            suppose
              for m be Element of NAT holds F.m<>k;
              hence contradiction by A29,A35;
            end;
          end;
          hence contradiction;
        end;
        consider m be Element of NAT such that
A36:    F.m=MX by A32;
A37:    MX<F.(m+1) & s2.(F.(m+1))<>x0 by A21,A36;
        M1 in NAT by ORDINAL1:def 13;
        then
A38:    F.(m+1)<=M1 by A21,A29,A32,A36;
        now
          assume F.(m+1)<>M1;
          then F.(m+1)<M1 by A38,XXREAL_0:1;
          hence contradiction by A33,A37;
        end;
        hence contradiction by A29;
      end;
A39:  for n holds (s2*F).n<>x0
      proof
        defpred P[Element of NAT] means (s2*F).$1<>x0;
A40:    for k st P[k] holds P[k+1]
        proof
          let k such that
          (s2*F).k<>x0;
          P[k,F.k,F.(k+1)] by A21;
          then s2.(F.(k+1))<>x0;
          hence thesis by FUNCT_2:21;
        end;
A41:    P[0] by A15,A21,FUNCT_2:21;
        thus for n holds P[n] from NAT_1:sch 1(A41,A40);
      end;
A42:  rng (s2*F) c= rng s2 by VALUED_0:21;
      then rng (s2*F) c= dom f by A3,XBOOLE_1:1;
      then
A43:  f/*(s2*F) is convergent & f/.x0=lim(f/*(s2*F)) by A2,A39,A26;
A44:  now
        let p be Real;
        assume
A45:    0<p;
        then consider n such that
A46:    for m be Element of NAT st n<=m holds ||.(f/*(s2*F)).m-f/.x0
        .||<p by A43,NORMSP_1:def 11;
        take k=F.n;
        let m be Element of NAT such that
A47:    k<=m;
        now
          per cases;
          suppose
A48:        s2.m=x0;
            ||.(f/*s2).m-f/.x0.|| =||.f/.(s2.m)-f/.x0.|| by A3,FUNCT_2:186
              .=||.0.T.|| by A48,RLVECT_1:28
              .=0 by NORMSP_1:5;
            hence ||.(f/*s2).m-f/.x0.||<p by A45;
          end;
          suppose
            s2.m<>x0;
            then consider l be Element of NAT such that
A49:        m=F.l by A27;
            n<=l by A47,A49,SEQM_3:7;
            then ||.(f/*(s2*F)).l-f/.x0.||<p by A46;
            then ||.f/.((s2*F).l)-f/.x0.||<p by A3,A42,FUNCT_2:186,XBOOLE_1:1;
            then ||.f/.(s2.m)-f/.x0.||<p by A49,FUNCT_2:21;
            hence ||.(f/*s2).m-f/.x0.||<p by A3,FUNCT_2:186;
          end;
        end;
        hence ||.(f/*s2).m-f/.x0.||<p;
      end;
      hence f/*s2 is convergent by NORMSP_1:def 9;
      hence f/.x0=lim(f/*s2) by A44,NORMSP_1:def 11;
    end;
  end;
  hence thesis;
end;

theorem Th33:
  for R1,R2 holds R1+R2 is REST of S,T & R1-R2 is REST of S,T
proof
  let R1,R2;
A1: R1 is total & R2 is total by Def5;
A2: now
    let h;
A3: (||.h.||")(#)(R1/*h) is convergent & (||.h.||")(#)(R2/*h) is
    convergent by Def5;
A4: (||.h.||")(#)((R1+R2)/*h) = (||.h.||")(#)((R1/*h)+(R2/*h)) by A1,Th30
      .= ((||.h.||")(#)(R1/*h))+((||.h.||")(#)(R2/*h)) by Th9;
    hence (||.h.||")(#)((R1+R2)/*h) is convergent by A3,NORMSP_1:34;
    lim ((||.h.||")(#)(R1/*h)) = 0.T & lim ((||.h.||")(#)(R2/*h)) = 0.T by Def5
;
    hence lim ((||.h.||")(#)((R1+R2)/*h)) = 0.T+0.T by A3,A4,NORMSP_1:42
      .= 0.T by RLVECT_1:10;
  end;
A5: now
    let h;
A6: (||.h.||")(#)(R1/*h) is convergent & (||.h.||")(#)(R2/*h) is
    convergent by Def5;
A7: (||.h.||")(#)((R1-R2)/*h) = (||.h.||")(#)((R1/*h)-(R2/*h)) by A1,Th30
      .= ((||.h.||")(#)(R1/*h))-((||.h.||")(#)(R2/*h)) by Th12;
    hence (||.h.||")(#)((R1-R2)/*h) is convergent by A6,NORMSP_1:35;
    lim ((||.h.||")(#)(R1/*h)) = 0.T & lim ((||.h.||")(#)(R2/*h)) = 0.T
    by Def5;
    hence lim ((||.h.||")(#)((R1-R2)/*h)) = 0.T-0.T by A6,A7,NORMSP_1:43
      .= 0.T by RLVECT_1:26;
  end;
  R1+R2 is total by A1,VFUNCT_1:38;
  hence R1+R2 is REST of S,T by A2,Def5;
  R1-R2 is total by A1,VFUNCT_1:38;
  hence thesis by A5,Def5;
end;

theorem Th34:
  for r,R holds r(#)R is REST of S,T
proof
  let r,R;
A1: R is total by Def5;
A2: now
    let h;
A3: (||.h.||")(#)(R/*h) is convergent by Def5;
A4: (||.h.||")(#)((r(#)R)/*h) = (||.h.||")(#)(r*(R/*h)) by A1,Th31
      .= r*((||.h.||")(#)(R/*h)) by Th10;
    hence (||.h.||")(#)((r(#)R)/*h) is convergent by A3,NORMSP_1:37;
    lim ((||.h.||")(#)(R/*h)) = 0.T by Def5;
    hence lim ((||.h.||")(#)((r(#)R)/*h)) = r*0.T by A3,A4,NORMSP_1:45
      .= 0.T by RLVECT_1:23;
  end;
  r(#)R is total by A1,VFUNCT_1:40;
  hence thesis by A2,Def5;
end;

definition
  let S,T;
  let f be PartFunc of S,T;
  let x0 be Point of S;
  pred f is_differentiable_in x0 means
  :Def6:
  ex N being Neighbourhood of x0
st N c= dom f & ex L,R st for x be Point of S st x in N holds f/.x - f/.x0 = L.
  (x-x0) + R/.(x-x0);
end;

definition
  let S,T;
  let f be PartFunc of S,T;
  let x0 be Point of S;
  assume
A1: f is_differentiable_in x0;
  func diff(f,x0) -> Point of R_NormSpace_of_BoundedLinearOperators(S,T) means
  :Def7:
  ex N being Neighbourhood of x0 st N c= dom f & ex R st for x be Point of
  S st x in N holds f/.x-f/.x0 = it.(x-x0) + R/.(x-x0);
  existence
  proof
    consider N being Neighbourhood of x0 such that
A2: N c= dom f and
A3: ex L,R st for x be Point of S st x in N holds f/.x - f/.x0 = L.(x-
    x0 ) + R/.(x-x0) by A1,Def6;
    consider L,R such that
A4: for x be Point of S st x in N holds f/.x - f/.x0 = L.(x-x0) + R/.(
    x-x0) by A3;
    take L;
    thus thesis by A2,A4;
  end;
  uniqueness
  proof
    let LB,LB1 be Point of R_NormSpace_of_BoundedLinearOperators(S,T);
A5: R_NormSpace_of_BoundedLinearOperators(S,T) = NORMSTR (#
      BoundedLinearOperators(S,T), Zero_(BoundedLinearOperators(S,T),
      R_VectorSpace_of_LinearOperators(S,T)), Add_(BoundedLinearOperators(S,T),
R_VectorSpace_of_LinearOperators(S,T)), Mult_(BoundedLinearOperators(S,T),
R_VectorSpace_of_LinearOperators(S,T)), BoundedLinearOperatorsNorm(S,T) #) by
LOPBAN_1:def 15;
    then reconsider L = LB as Element of BoundedLinearOperators(S,T);
    reconsider L1=LB1 as Element of BoundedLinearOperators(S,T) by A5;
    assume that
A6: ex N being Neighbourhood of x0 st N c= dom f & ex R st for x be
    Point of S st x in N holds f/.x-f/.x0 = LB.(x-x0)+R/.(x-x0) and
A7: ex N being Neighbourhood of x0 st N c= dom f & ex R st for x be
    Point of S st x in N holds f/.x-f/.x0 = LB1.(x-x0) + R/.(x-x0);
    consider N being Neighbourhood of x0 such that
    N c= dom f and
A8: ex R st for x be Point of S st x in N holds f/.x-f/.x0 = LB.(x-x0)
    +R /.(x-x0) by A6;
    consider R such that
A9: for x be Point of S st x in N holds f/.x-f/.x0 = LB.(x-x0) + R/.(x
    -x0) by A8;
    consider N1 being Neighbourhood of x0 such that
    N1 c= dom f and
A10: ex R st for x be Point of S st x in N1 holds f/.x-f/.x0=LB1.(x-x0
    )+ R/.(x-x0) by A7;
    consider R1 such that
A11: for x be Point of S st x in N1 holds f/.x-f/.x0 = LB1.(x-x0) + R1
    /.(x-x0) by A10;
A12: for z be Point of S holds modetrans(L,S,T).z = modetrans(L1,S,T).z
    proof
      let z be Point of S;
      now
        per cases;
        case
A13:      z= 0.S;
          hence modetrans(L,S,T).z =modetrans(L,S,T).(0*z) by RLVECT_1:23
            .=0*modetrans(L,S,T).z by LOPBAN_1:def 6
            .=0.T by RLVECT_1:23
            .=0*modetrans(L1,S,T).z by RLVECT_1:23
            .=modetrans(L1,S,T).(0*z) by LOPBAN_1:def 6
            .=modetrans(L1,S,T).z by A13,RLVECT_1:23;
        end;
        case
A14:      z<> 0.S;
          consider N0 be Neighbourhood of x0 such that
A15:      N0 c= N & N0 c= N1 by Th1;
          consider g be Real such that
A16:      0<g and
A17:      {y where y is Point of S: ||.y - x0 .|| < g} c= N0 by NFCONT_1:def 3;
          consider n0 be Element of NAT such that
A18:      ||.z.|| /g < n0 by SEQ_4:10;
          set n1=n0+1;
A19:      0 <= n0 by NAT_1:2;
          n0 <= n0+1 by NAT_1:11;
          then ||.z.|| /g < (n0+1) by A18,XXREAL_0:2;
          then ( ||.z.|| /g)*g < (n0+1)*g by A16,XREAL_1:70;
          then ||.z.|| < (n0+1)*g by A16,XCMPLX_1:88;
          then
A20:      ||.z.|| /(n0+1) < g by A19,XREAL_1:85;
          ex r be Real_Sequence st (for n be Element of NAT holds (r.n=1
          /(n+n1) & r.n > 0 & r.n*z+x0 in N0) ) & r is convergent & lim r =0
          proof
            deffunc F(Element of NAT) = 1 /($1+n1);
            consider r be Real_Sequence such that
A21:        for n holds r.n = F(n) from SEQ_1:sch 1;
            take r;
            thus for n be Element of NAT holds r.n=1 /(n+n1) & r.n > 0 & r.n*z
            +x0 in N0
            proof
              let n be Element of NAT;
              thus r.n=1 /(n+n1) by A21;
              n1 <= n+n1 & 0 <= ||.z.|| by NAT_1:12,NORMSP_1:8;
              then
A22:          ||.z.|| /(n+n1) <= ||.z.|| /n1 by A19,XREAL_1:120;
              0 <= n by NAT_1:2;
              then 0 < 1 * (n + n1)" by A19;
              then
A23:          0 < 1/(n + n1) by XCMPLX_0:def 9;
              hence r.n >0 by A21;
              ||.r.n*z+x0-x0.|| = ||.r.n*z+(x0-x0).|| by RLVECT_1:def 6
                .= ||.r.n*z+0.S.|| by RLVECT_1:28
                .= ||.r.n*z.|| by RLVECT_1:10
                .= ||.(1 /(n+n1))*z.|| by A21
                .= abs(1 /(n+n1))* ||.z.|| by NORMSP_1:def 2
                .= ( 1 /(n+n1) )* ||.z.|| by A23,ABSVALUE:def 1
                .= ||.z.|| /(n+n1) by XCMPLX_1:100;
              then ||.r.n*z+x0-x0.|| < g by A20,A22,XXREAL_0:2;
              then (r.n*z+x0) in {y where y is Point of S: ||.y - x0 .|| < g};
              hence thesis by A17;
            end;
            thus r is convergent by A21,NAT_1:2,SEQ_4:43;
            thus thesis by A21,NAT_1:2,SEQ_4:44;
          end;
          then consider r be Real_Sequence such that
A24:      for n be Element of NAT holds r.n=1 /(n+n1) & r.n > 0 & r.n
          *z+x0 in N0 and
          r is convergent and
          lim r =0;
          deffunc F(Element of NAT) = r.$1*z;
          consider s be sequence of S such that
A25:      for n holds s.n = F(n) from FUNCT_2:sch 4;
          now
            let n be Element of NAT;
            assume s.n = 0.S;
            then r.n*z = 0.S by A25;
            then r.n =0 or z=0.S by RLVECT_1:24;
            hence contradiction by A14,A24;
          end;
          then
A26:      s is non-zero by Th7;
          now
            let n be Element of NAT;
            thus s.n = r.n*z by A25
              .=(1 /(n+n1))*z by A24;
          end;
          then s is convergent & lim s = 0.S by A19,Th22,Th23;
          then reconsider s as convergent_to_0 sequence of S by A26,Def4;
A27:      now
            let x be Point of S;
            assume that
A28:        x in N and
A29:        x in N1;
            f/.x-f/.x0 = L.(x-x0) + R/.(x-x0) by A9,A28;
            hence L.(x-x0) + R/.(x-x0) = L1.(x-x0) + R1/.(x-x0) by A11,A29;
          end;
          now
            R1 is total by Def5;
            then dom R1 = the carrier of S by PARTFUN1:def 4;
            then
A30:        rng s c= dom R1;
            R is total by Def5;
            then dom R = the carrier of S by PARTFUN1:def 4;
            then
A31:        rng s c= dom R;
            let n be Nat;
            set x=(r.n)*z+x0;
A32:        x-x0 = (r.n)*z+(x0 -x0) by RLVECT_1:def 6
              .= (r.n)*z+0.S by RLVECT_1:28
              .=(r.n)*z by RLVECT_1:10;
A33:        n in NAT by ORDINAL1:def 13;
            then
A34:        r.n <> 0 by A24;
            s is non-zero by Def4;
            then s.n <> 0.S by A33,Th7;
            then
A35:        ||.s.n.|| <> 0 by NORMSP_0:def 5;
A36:        r.n > 0 by A24,A33;
            ||.s.n.|| = ||.(r.n)*z.|| by A25,A33
              .= abs(r.n) * ||.z.|| by NORMSP_1:def 2
              .=r.n * ||.z.|| by A36,ABSVALUE:def 1;
            then (r.n)"* ||.s.n.|| = (r.n)"* r.n * ||.z.||
              .= ((r.n)/(r.n))* ||.z.|| by XCMPLX_0:def 9
              .= 1* ||.z.|| by A34,XCMPLX_1:60
              .= ||.z.||;
            then
            ||.z.|| *( ||.s.n.|| )" = (r.n)"* (||.s.n.|| *( ||.s.n.|| )")
              .= (r.n)"* (||.s.n.|| /( ||.s.n.|| )) by XCMPLX_0:def 9
              .= (r.n)"*1 by A35,XCMPLX_1:60
              .= (r.n)";
            then
A37:        ||.z.|| *( ||.s.||.n )" = (r.n)" by A33,NORMSP_0:def 4;
            x in N0 by A24,A33;
            then
            L.((r.n)*z) + R/.((r.n)*z) = L1.((r.n)*z) + R1/.(( r.n)*z) by A27
,A15,A32;
            then
A38:        (r.n)"*(L.((r.n)*z))+(r.n)"*(R/.((r.n)*z)) =(r.n)"*(L1.((r.n)
            *z)+R1/.((r.n)*z)) by RLVECT_1:def 8;
A39:        (r.n)"*(L1.((r.n)*z)) =(r.n)"*(modetrans(L1,S,T).((r.n)*z))
            by LOPBAN_1:def 12
              .=(r.n)"*((r.n)*modetrans(L1,S,T).z) by LOPBAN_1:def 6
              .= (r.n)"*((r.n)*L1.z ) by LOPBAN_1:def 12
              .= ((r.n)"*(r.n))*L1.z by RLVECT_1:def 10
              .= ((r.n)/(r.n))*L1.z by XCMPLX_0:def 9
              .= 1*L1.z by A34,XCMPLX_1:60
              .= L1.z by RLVECT_1:def 11;
            (r.n)"*(L.((r.n)*z)) =(r.n)"*(modetrans(L,S,T).((r.n)*z)) by
LOPBAN_1:def 12
              .=(r.n)"*((r.n)*modetrans(L,S,T).z) by LOPBAN_1:def 6
              .= (r.n)"*((r.n)*L.z ) by LOPBAN_1:def 12
              .= ((r.n)"*(r.n))*L.z by RLVECT_1:def 10
              .= ((r.n)/(r.n))*L.z by XCMPLX_0:def 9
              .= 1*L.z by A34,XCMPLX_1:60
              .= L.z by RLVECT_1:def 11;
            then
A40:        L.z + (r.n)" *(R/.((r.n)*z)) = L1.z + (r.n)"*(R1/. ((r.n)*z))
            by A38,A39,RLVECT_1:def 8;
A41:        (r.n)"*(R1/.((r.n)*z)) = (r.n)"*(R1/.(s.n)) by A25,A33
              .= ( ||.z.|| *||.s.||".n)*(R1/.(s.n)) by A37,VALUED_1:10
              .= ||.z.|| *(||.s.||".n *(R1/.(s.n)) ) by RLVECT_1:def 10
              .= ||.z.|| *((||.s.||".n)*((R1/*s).n)) by A33,A30,FUNCT_2:186
              .= ||.z.|| *((||.s.||")(#)(R1/*s)).n by A33,Def2;
            (r.n)"*(R/.((r.n)*z)) = (r.n)"*(R/.(s.n)) by A25,A33
              .= ( ||.z.|| *||.s.||".n)*(R/.(s.n)) by A37,VALUED_1:10
              .= ||.z.|| *(||.s.||".n *(R/.(s.n)) ) by RLVECT_1:def 10
              .= ||.z.|| *((||.s.||".n)*((R/*s).n)) by A33,A31,FUNCT_2:186
              .= ||.z.|| *((||.s.||")(#)(R/*s)).n by A33,Def2;
            then L.z + ( ||.z.|| *((||.s.||")(#)(R/*s)).n -( ||.z.|| *((||.s
.||")(#)(R/*s)).n)) = L1.z + ||.z.|| *((||.s.||")(#)(R1/*s)).n -(||.z.|| *((||.
            s.||")(#)(R/*s)).n) by A40,A41,RLVECT_1:def 6;
            then L.z + 0.T = L1.z + ||.z.|| *((||.s.||")(#)(R1/*s)).n -(||.z
            .|| *((||.s.||")(#)(R/*s)).n) by RLVECT_1:28;
            then L.z = L1.z + ||.z.|| *((||.s.||")(#)(R1/*s)).n -(||.z.|| *((
            ||.s.||")(#)(R/*s)).n) by RLVECT_1:def 7;
            then
            L.z - L1.z = (- L1.z)+ (L1.z + ( ||.z.|| *((||.s.||")(#)(R1/*
            s)).n -(||.z.|| *((||.s.||")(#)(R/*s)).n) )) by RLVECT_1:def 6;
            then
            L.z - L1.z = (- L1.z)+ L1.z + (||.z.|| *((||.s.||")(#)(R1/*s)
            ).n -(||.z.|| *((||.s.||")(#)(R/*s)).n) ) by RLVECT_1:def 6;
            then L.z - L1.z = 0.T+ ( ||.z.|| *((||.s.||")(#)(R1/*s)).n -(||.z
            .|| *((||.s.||")(#)(R/*s)).n) ) by RLVECT_1:16;
            then
            L.z - L1.z = (||.z.|| *((||.s.||")(#)(R1/*s)).n -(||.z.|| *((
            ||.s.||")(#)(R/*s)).n) ) by RLVECT_1:10;
            then L.z - L1.z = ||.z.|| *((||.s.||"(#)(R1/*s)).n-(||.s.||"(#)(R
            /*s)).n) by RLVECT_1:48;
            then
            L.z - L1.z = ||.z.|| *((||.s.||"(#)(R1/*s))-(||.s.||"(#)(R/*s
            ))).n by A33,NORMSP_1:def 6;
            hence L.z - L1.z = ( ||.z.|| *((||.s.||"(#)(R1/*s))-(||.s.||"(#)(R
            /*s)))).n by A33,NORMSP_1:def 8;
          end;
          then
          ||.z.|| *((||.s.||"(#)(R1/*s))-(||.s.||"(#)(R/*s))) is constant
& ( ||.z.|| *((||.s.||"(#)(R1/*s))-(||.s.||"(#)(R/*s)))).1 =L.z - L1.z by
VALUED_0:def 18;
          then
A42:      lim ( ||.z.|| *((||.s.||"(#)(R1/*s))-(||.s.||"(#)(R/*s)) )) = L
          .z - L1.z by Th21;
A43:      (||.s.||")(#)(R/*s) is convergent & (||.s.||")(#)(R1/*s) is
          convergent by Def5;
          lim ((||.s.||")(#)(R/*s)) = 0.T & lim ((||.s.||")(#)(R1/*s)) =
          0.T by Def5;
          then
A44:      lim ((||.s.||"(#)(R1/*s))-(||.s.||"(#)(R/*s))) = 0.T - 0.T by A43,
NORMSP_1:43
            .= 0.T by RLVECT_1:26;
A45:      lim ( ||.z.|| *((||.s.||"(#)(R1/*s))-(||.s.||"(#)(R/*s)))) =
||.z.|| * lim ((||.s.||"(#)(R1/*s))-(||.s.||"(#)(R/*s))) by A43,NORMSP_1:35,45
            .=0.T by A44,RLVECT_1:23;
          thus modetrans(L,S,T).z =L.z by LOPBAN_1:def 12
            .=L1.z by A42,A45,RLVECT_1:35
            .= modetrans(L1,S,T).z by LOPBAN_1:def 12;
        end;
      end;
      hence thesis;
    end;
    thus LB=modetrans(L,S,T) by LOPBAN_1:def 12
      .=modetrans(L1,S,T) by A12,FUNCT_2:113
      .=LB1 by LOPBAN_1:def 12;
  end;
end;

definition
  let X;
  let S,T;
  let f be PartFunc of S,T;
  pred f is_differentiable_on X means
  :Def8:
  X c=dom f & for x be Point of S st x in X holds f|X is_differentiable_in x;
end;

theorem Th35:
  for f be PartFunc of S,T holds (f is_differentiable_on X implies
  X is Subset of S)
proof
  let f be PartFunc of S,T;
  assume f is_differentiable_on X;
  then X c=dom f by Def8;
  hence thesis by XBOOLE_1:1;
end;

theorem Th36:
  for f be PartFunc of S,T for Z be Subset of S st Z is open holds
( f is_differentiable_on Z iff Z c=dom f & for x be Point of S st x in Z holds
  f is_differentiable_in x )
proof
  let f be PartFunc of S,T;
  let Z be Subset of S such that
A1: Z is open;
  thus f is_differentiable_on Z implies Z c=dom f & for x be Point of S st x
  in Z holds f is_differentiable_in x
  proof
    assume
A2: f is_differentiable_on Z;
    hence
A3: Z c=dom f by Def8;
    let x0 be Point of S;
    assume
A4: x0 in Z;
    then f|Z is_differentiable_in x0 by A2,Def8;
    then consider N being Neighbourhood of x0 such that
A5: N c= dom(f|Z) and
A6: ex L,R st for x be Point of S st x in N holds (f|Z)/.x-(f|Z)/.x0=L
    .( x-x0)+R/.(x-x0) by Def6;
    consider L,R such that
A7: for x be Point of S st x in N holds (f|Z)/.x - (f|Z)/.x0 = L.(x-
    x0) + R/.(x-x0) by A6;
    take N;
A8: dom(f|Z)=dom f/\Z by RELAT_1:90;
    then dom(f|Z) c=dom f by XBOOLE_1:17;
    hence N c= dom f by A5,XBOOLE_1:1;
    take L,R;
    let x be Point of S;
    assume
A9: x in N;
    then (f|Z)/.x-(f|Z)/.x0=L.(x-x0)+R/.(x-x0) by A7;
    then f/.x-(f|Z)/.x0=L.(x-x0)+R/.(x-x0) by A5,A8,A9,PARTFUN2:34;
    hence thesis by A3,A4,PARTFUN2:35;
  end;
  assume that
A10: Z c=dom f and
A11: for x be Point of S st x in Z holds f is_differentiable_in x;
  thus Z c=dom f by A10;
  let x0 be Point of S;
  assume
A12: x0 in Z;
  then consider N1 being Neighbourhood of x0 such that
A13: N1 c= Z by A1,Th2;
  f is_differentiable_in x0 by A11,A12;
  then consider N being Neighbourhood of x0 such that
A14: N c= dom f and
A15: ex L,R st for x be Point of S st x in N holds f/.x-f/.x0=L.(x-x0)+R
  /.(x-x0) by Def6;
  consider N2 being Neighbourhood of x0 such that
A16: N2 c= N1 and
A17: N2 c= N by Th1;
A18: N2 c= Z by A13,A16,XBOOLE_1:1;
  take N2;
  N2 c= dom f by A14,A17,XBOOLE_1:1;
  then
A19: N2 c= dom f/\Z by A18,XBOOLE_1:19;
  hence N2 c= dom(f|Z) by RELAT_1:90;
A20: x0 in N2 by NFCONT_1:4;
  consider L,R such that
A21: for x be Point of S st x in N holds f/.x-f/.x0=L.(x-x0)+R/.(x-x0) by A15;
  take L,R;
  let x be Point of S;
  assume
A22: x in N2;
  then f/.x-f/.x0=L.(x-x0)+R/.(x-x0) by A17,A21;
  then (f|Z)/.x-f/.x0=L.(x-x0)+R/.(x-x0) by A19,A22,PARTFUN2:34;
  hence thesis by A19,A20,PARTFUN2:34;
end;

theorem
  for f be PartFunc of S,T for Y be Subset of S holds f
  is_differentiable_on Y implies Y is open
proof
  let f be PartFunc of S,T;
  let Y be Subset of S;
  assume
A1: f is_differentiable_on Y;
  now
    let x0 be Point of S;
    assume x0 in Y;
    then f|Y is_differentiable_in x0 by A1,Def8;
    then consider N being Neighbourhood of x0 such that
A2: N c= dom(f|Y) and
    ex L,R st for x be Point of S st x in N holds (f|Y)/.x-(f|Y)/.x0=L.( x
    -x0)+R/.(x-x0) by Def6;
    take N;
    dom(f|Y)=dom f/\Y by RELAT_1:90;
    then dom(f|Y) c=Y by XBOOLE_1:17;
    hence N c= Y by A2,XBOOLE_1:1;
  end;
  hence thesis by Th4;
end;

definition
  let S,T;
  let f be PartFunc of S,T;
  let X be set;
  assume
A1: f is_differentiable_on X;
  func f`|X -> PartFunc of S, R_NormSpace_of_BoundedLinearOperators(S,T) means
  :Def9:
  dom it = X & for x be Point of S st x in X holds it/.x = diff(f,x);
  existence
  proof
    deffunc F(Point of S) = diff(f,$1);
    defpred P[Point of S] means $1 in X;
    consider F being PartFunc of S,R_NormSpace_of_BoundedLinearOperators(S,T)
    such that
A2: (for x be Point of S holds x in dom F iff P[x]) & for x be Point
    of S st x in dom F holds F.x = F(x) from SEQ_1:sch 3;
    take F;
    now
A3:   X is Subset of S by A1,Th35;
      let y;
      assume y in X;
      hence y in dom F by A2,A3;
    end;
    then
A4: X c= dom F by TARSKI:def 3;
    for y st y in dom F holds y in X by A2;
    then dom F c= X by TARSKI:def 3;
    hence dom F = X by A4,XBOOLE_0:def 10;
    now
      let x be Point of S;
      assume x in X;
      then
A5:   x in dom F by A2;
      then F.x = diff(f,x) by A2;
      hence F/.x = diff(f,x) by A5,PARTFUN1:def 8;
    end;
    hence thesis;
  end;
  uniqueness
  proof
    let F,G be PartFunc of S,R_NormSpace_of_BoundedLinearOperators(S,T);
    assume that
A6: dom F = X and
A7: for x be Point of S st x in X holds F/.x = diff(f,x) and
A8: dom G = X and
A9: for x be Point of S st x in X holds G/.x = diff(f,x);
    now
      let x be Point of S;
      assume
A10:  x in dom F;
      then F/.x = diff(f,x) by A6,A7;
      hence F/.x=G/.x by A6,A9,A10;
    end;
    hence thesis by A6,A8,PARTFUN2:3;
  end;
end;

theorem
  for f be PartFunc of S,T for Z be Subset of S st Z is open & Z c= dom
f & ex r be Point of T st rng f = {r} holds f is_differentiable_on Z & for x be
Point of S st x in Z holds (f`|Z)/.x = 0.R_NormSpace_of_BoundedLinearOperators(
  S,T)
proof
  let f be PartFunc of S,T;
  let Z be Subset of S such that
A1: Z is open and
A2: Z c= dom f;
  reconsider R = (the carrier of S) --> 0.T as PartFunc of S,T;
  set L = 0.R_NormSpace_of_BoundedLinearOperators(S,T);
  given r be Point of T such that
A3: rng f = {r};
  R_NormSpace_of_BoundedLinearOperators(S,T) = NORMSTR (#
    BoundedLinearOperators(S,T), Zero_(BoundedLinearOperators(S,T),
    R_VectorSpace_of_LinearOperators(S,T)), Add_(BoundedLinearOperators(S,T),
    R_VectorSpace_of_LinearOperators(S,T)), Mult_(BoundedLinearOperators(S,T),
R_VectorSpace_of_LinearOperators(S,T)), BoundedLinearOperatorsNorm(S,T) #) by
LOPBAN_1:def 15;
  then reconsider L as Element of BoundedLinearOperators(S,T);
A4: dom R = the carrier of S by FUNCOP_1:19;
A5: now
    let h;
A6: now
      let n be Nat;
A7:   R/.(h.n) =R.(h.n) by A4,PARTFUN1:def 8
        .=0.T by FUNCOP_1:13;
A8:   rng h c= dom R by A4;
A9:   n in NAT by ORDINAL1:def 13;
      hence ((||.h.||")(#)(R/*h)).n = (||.h.||".n)*((R/*h).n) by Def2
        .= (||.h.||".n)*(R/.(h.n)) by A9,A8,FUNCT_2:186
        .= 0.T by A7,RLVECT_1:23;
    end;
    then
A10: (||.h.||")(#)(R/*h) is constant by VALUED_0:def 18;
    hence (||.h.||")(#)(R/*h) is convergent by Th21;
    ((||.h.||")(#)(R/*h)).0 = 0.T by A6;
    hence lim ((||.h.||")(#)(R/*h)) = 0.T by A10,Th21;
  end;
A11: now
    let x0 be Point of S;
    assume
A12: x0 in dom f;
    then f.x0 in {r} by A3,FUNCT_1:def 5;
    then f/.x0 in {r} by A12,PARTFUN1:def 8;
    hence f/.x0 = r by TARSKI:def 1;
  end;
  reconsider R as REST of S,T by A5,Def5;
A13: ((the carrier of S) --> 0.T) = L by LOPBAN_1:37;
A14: now
    let x0 be Point of S;
    assume
A15: x0 in Z;
    then consider N being Neighbourhood of x0 such that
A16: N c= Z by A1,Th2;
A17: N c= dom f by A2,A16,XBOOLE_1:1;
    for x be Point of S st x in N holds f/.x - f/.x0 = L.(x-x0) + R/.(x- x0)
    proof
      let x be Point of S;
A18:  R/.(x-x0) =R.(x-x0) by A4,PARTFUN1:def 8
        .=0.T by FUNCOP_1:13;
      assume x in N;
      hence f/.x - f/.x0 = r - f/.x0 by A11,A17
        .= r - r by A2,A11,A15
        .=0.T by RLVECT_1:28
        .= 0.T + 0.T by RLVECT_1:10
        .= L.(x-x0)+R/.(x-x0) by A13,A18,FUNCOP_1:13;
    end;
    hence f is_differentiable_in x0 by A17,Def6;
  end;
  hence
A19: f is_differentiable_on Z by A1,A2,Th36;
  let x0 be Point of S;
  assume
A20: x0 in Z;
  then
A21: f is_differentiable_in x0 by A14;
  then
  ex N being Neighbourhood of x0 st N c= dom f & ex L,R st for x be Point
  of S st x in N holds f/.x-f/.x0=L.(x-x0)+R/.(x-x0) by Def6;
  then consider N being Neighbourhood of x0 such that
A22: N c= dom f;
A23: for x be Point of S st x in N holds f/.x - f/.x0 = L.(x-x0) + R/.(x-x0)
  proof
    let x be Point of S;
A24: R/.(x-x0) =R.(x-x0) by A4,PARTFUN1:def 8
      .=0.T by FUNCOP_1:13;
    assume x in N;
    hence f/.x - f/.x0 = r - f/.x0 by A11,A22
      .=r - r by A2,A11,A20
      .=0.T by RLVECT_1:28
      .= 0.T + 0.T by RLVECT_1:10
      .=L.(x-x0) + R/.(x-x0) by A13,A24,FUNCOP_1:13;
  end;
  thus (f`|Z)/.x0 = diff(f,x0) by A19,A20,Def9
    .= 0.R_NormSpace_of_BoundedLinearOperators(S,T) by A21,A22,A23,Def7;
end;

registration
  let S;
  let h,n;
  cluster h^\n -> convergent_to_0;
  coherence
  proof
    h is non-zero by Def4;
    then
A1: h^\n is non-zero by Th17;
A2: h is convergent by Def4;
    lim h = 0.S by Def4;
    then
A3: lim (h^\n) = 0.S by A2,LOPBAN_3:14;
    h^\n is convergent by A2,LOPBAN_3:14;
    hence thesis by A1,A3,Def4;
  end;
end;

theorem Th39:
  for x0 be Point of S for N being Neighbourhood of x0 st f
is_differentiable_in x0 & N c= dom f holds for h be convergent_to_0 sequence of
S for c st rng c = {x0} & rng (h+c) c= N holds (f/*(h+c) - f/*c) is convergent
  & lim (f/*(h+c) - f/*c) = 0.T
proof
  let x0 be Point of S;
  let N be Neighbourhood of x0;
  assume that
A1: f is_differentiable_in x0 and
A2: N c= dom f;
  let h be convergent_to_0 sequence of S;
  let c such that
A3: rng c = {x0} and
A4: rng (h+c) c= N;
  consider N1 be Neighbourhood of x0 such that
  N1 c= dom f and
A5: ex L,R st for x be Point of S st x in N1 holds f/.x - f/.x0 = L.(x-
  x0) + R/.(x-x0) by A1,Def6;
  consider N2 be Neighbourhood of x0 such that
A6: N2 c= N and
A7: N2 c= N1 by Th1;
  consider L,R such that
A8: for x be Point of S st x in N1 holds f/.x - f/.x0 = L.(x-x0) + R/.(x
  -x0) by A5;
  consider g be Real such that
A9: 0<g and
A10: {y where y is Point of S: ||.y-x0.|| < g} c= N2 by NFCONT_1:def 3;
A11: x0 in N2 by NFCONT_1:4;
  ex n st rng (c^\n) c= N2 & rng ((h+c)^\n) c= N2
  proof
    c.0 in rng c by NFCONT_1:6;
    then c.0=x0 by A3,TARSKI:def 1;
    then
A12: lim c = x0 by Th21;
A13: c is convergent & h is convergent by Def4,Th21;
    then
A14: h + c is convergent by NORMSP_1:34;
    lim h = 0.S by Def4;
    then lim (h + c) = 0.S + x0 by A12,A13,NORMSP_1:42
      .= x0 by RLVECT_1:10;
    then consider n such that
A15: for m being Element of NAT st n<=m holds ||.(h+c).m-x0.||<g by A9,A14,
NORMSP_1:def 11;
    take n;
A16: rng (c^\n) = {x0} by A3,VALUED_0:26;
    thus rng (c^\n) c= N2
    proof
      let y;
      assume y in rng (c^\n);
      hence thesis by A11,A16,TARSKI:def 1;
    end;
    let y be set;
    assume y in rng ((h+c)^\n);
    then consider m be Element of NAT such that
A17: y = ((h+c)^\n).m by NFCONT_1:6;
    reconsider y1=y as Point of S by A17;
    0 <= m by NAT_1:2;
    then n + 0 <= n+m by XREAL_1:9;
    then ||.(h+c).(n+m)-x0.||<g by A15;
    then ||.((h+c)^\n).m - x0 .|| < g by NAT_1:def 3;
    then y1 in {z where z is Point of S: ||.z-x0.|| < g} by A17;
    hence thesis by A10;
  end;
  then consider n such that
  rng (c^\n) c= N2 and
A18: rng ((h+c)^\n) c= N2;
A19: lim (h^\n) = 0.S by Def4;
A20: for k holds f/.(((h+c)^\n).k) - f/.((c^\n).k) = L.((h^\n).k) + R/.((h^\
  n).k)
  proof
    let k;
    ((h+c)^\n).k in rng ((h+c)^\n) by NFCONT_1:6;
    then
A21: ((h+c)^\n).k in N2 by A18;
    (c^\n).k in rng (c^\n) & rng (c^\n) = rng c by NFCONT_1:6,VALUED_0:26;
    then
A22: (c^\n).k = x0 by A3,TARSKI:def 1;
    ((h+c)^\n).k - (c^\n).k = (h+c).(k+n) - (c^\n).k by NAT_1:def 3
      .=h.(k+n)+ c.(k+n) - (c^\n).k by NORMSP_1:def 5
      .=(h^\n).k+ c.(k+n) - (c^\n).k by NAT_1:def 3
      .=(h^\n).k+ (c^\n).k- (c^\n).k by NAT_1:def 3
      .= (h^\n).k + ((c^\n).k - (c^\n).k) by RLVECT_1:def 6
      .= (h^\n).k + 0.S by RLVECT_1:28
      .= (h^\n).k by RLVECT_1:10;
    hence thesis by A8,A7,A21,A22;
  end;
  R_NormSpace_of_BoundedLinearOperators(S,T) = NORMSTR (#
    BoundedLinearOperators(S,T), Zero_(BoundedLinearOperators(S,T),
    R_VectorSpace_of_LinearOperators(S,T)), Add_(BoundedLinearOperators(S,T),
    R_VectorSpace_of_LinearOperators(S,T)), Mult_(BoundedLinearOperators(S,T),
R_VectorSpace_of_LinearOperators(S,T)), BoundedLinearOperatorsNorm(S,T) #) by
LOPBAN_1:def 15;
  then reconsider L as Element of BoundedLinearOperators(S,T);
  reconsider LP=modetrans(L,S,T) as PartFunc of S,T;
A23: lim (R/*(h^\n)) = 0.T by Th27;
A24: rng ((h+c)^\n) c= dom f
  proof
    let y;
    assume y in rng ((h+c)^\n);
    then y in N2 by A18;
    then y in N by A6;
    hence thesis by A2;
  end;
  R is total by Def5;
  then dom R = the carrier of S by PARTFUN1:def 4;
  then
A25: rng (h^\n) c= dom R;
A26: rng (c^\n) c= dom f
  proof
    let y;
    assume
A27: y in rng (c^\n);
    rng (c^\n) = rng c by VALUED_0:26;
    then y = x0 by A3,A27,TARSKI:def 1;
    then y in N by NFCONT_1:4;
    hence thesis by A2;
  end;
A28: dom modetrans(L,S,T) = the carrier of S by FUNCT_2:def 1;
  then
A29: rng (h^\n) c= dom modetrans(L,S,T);
  now
    let k;
    thus (f/*((h+c)^\n)-f/*(c^\n)).k = (f/*((h+c)^\n)).k-(f/*(c^\n)).k by
NORMSP_1:def 6
      .= f/.(((h+c)^\n).k) - (f/*(c^\n)).k by A24,FUNCT_2:186
      .= f/.(((h+c)^\n).k) - f/.((c^\n).k) by A26,FUNCT_2:186
      .= L.((h^\n).k) + R/.((h^\n).k) by A20
      .= modetrans(L,S,T).((h^\n).k) + R/.((h^\n).k) by LOPBAN_1:def 12
      .= LP/.((h^\n).k) + R/.((h^\n).k) by A28,PARTFUN1:def 8
      .= (LP/*(h^\n)).k + R/.((h^\n).k) by A29,FUNCT_2:186
      .= (LP/*(h^\n)).k + (R/*(h^\n)).k by A25,FUNCT_2:186
      .= (LP/*(h^\n) + R/*(h^\n)).k by NORMSP_1:def 5;
  end;
  then
A30: f/*((h+c)^\n) - f/*(c^\n) = LP/*(h^\n) + R/*(h^\n) by FUNCT_2:113;
A31: dom modetrans(L,S,T) = the carrier of S by FUNCT_2:def 1;
  LP is_Lipschitzian_on the carrier of S
  proof
    thus the carrier of S c=dom LP by FUNCT_2:def 1;
    set LL=modetrans(L,S,T);
    consider K being Real such that
A32: 0 <= K and
A33: for x being VECTOR of S holds ||. LL.x .|| <= K * ||. x .|| by
LOPBAN_1:def 9;
    take K+1;
A34: 0 + K < 1 + K by XREAL_1:10;
    now
      let x1,x2 be Point of S such that
      x1 in the carrier of S and
      x2 in the carrier of S;
A35:  ||.LL.(x1-x2) .|| <= K * ||.x1-x2.|| by A33;
      0 <= ||.x1-x2.|| by NORMSP_1:8;
      then
A36:  K * ||.x1-x2.|| <= (K+1 ) * ||.x1-x2.|| by A34,XREAL_1:66;
      ||.LP/.x1-LP/.x2 .|| = ||.LL.x1-LP/.x2 .|| by A31,PARTFUN1:def 8
        .= ||.LL.x1+-LL.x2 .|| by A31,PARTFUN1:def 8
        .= ||.LL.x1+(-1)*LL.x2 .|| by RLVECT_1:29
        .= ||.LL.x1+LL.((-1)*x2) .|| by LOPBAN_1:def 6
        .= ||.LL.(x1+(-1)*x2) .|| by GRCAT_1:def 13
        .= ||.LL.(x1-x2) .|| by RLVECT_1:29;
      hence ||.LP/.x1-LP/.x2 .|| <= (K+1) * ||.x1-x2.|| by A35,A36,XXREAL_0:2;
    end;
    hence thesis by A32;
  end;
  then
A37: LP is_continuous_on the carrier of S by NFCONT_1:52;
A38: rng c c= dom f
  proof
    let y;
    assume y in rng c;
    then y = x0 by A3,TARSKI:def 1;
    then y in N by NFCONT_1:4;
    hence thesis by A2;
  end;
A39: h^\n is convergent & rng (h^\n) c= the carrier of S by Def4;
  then
A40: LP/*(h^\n) is convergent by A37,A19,NFCONT_1:25;
A41: R/*(h^\n) is convergent by Th27;
  then
A42: (LP/*(h^\n) + R/*(h^\n)) is convergent by A40,NORMSP_1:34;
  LP/.(0.S) =modetrans(L,S,T).(0.S) by A31,PARTFUN1:def 8
    .= modetrans(L,S,T).(0*0.S) by RLVECT_1:23
    .=0*modetrans(L,S,T).0.S by LOPBAN_1:def 6
    .=0.T by RLVECT_1:23;
  then lim (LP/*(h^\n)) = 0.T by A37,A39,A19,NFCONT_1:25;
  then lim (LP/*(h^\n) + R/*(h^\n)) = 0.T+0.T by A41,A23,A40,NORMSP_1:42;
  then
A43: lim (LP/*(h^\n) + R/*(h^\n)) = 0.T by RLVECT_1:10;
  rng (h+c) c= dom f
  proof
    let y;
    assume y in rng (h+c);
    then y in N by A4;
    hence thesis by A2;
  end;
  then f/*((h+c)^\n) - f/*(c^\n) =(f/*(h+c))^\n - f/*(c^\n) by VALUED_0:27
    .=(f/*(h+c))^\n - (f/*c) ^\n by A38,VALUED_0:27
    .= (f/*(h+c) - f/*c) ^\n by Th16;
  hence thesis by A30,A42,A43,LOPBAN_3:15,16;
end;

theorem Th40:
  for f1,f2,x0 st f1 is_differentiable_in x0 & f2
  is_differentiable_in x0 holds f1+f2 is_differentiable_in x0 & diff(f1+f2,x0)=
  diff(f1,x0)+diff(f2,x0)
proof
  let f1,f2,x0;
  assume that
A1: f1 is_differentiable_in x0 and
A2: f2 is_differentiable_in x0;
  consider N1 be Neighbourhood of x0 such that
A3: N1 c= dom f1 and
A4: ex L,R st for x be Point of S st x in N1 holds f1/.x - f1/.x0 = L.(x
  -x0) + R/.(x-x0) by A1,Def6;
  consider L1,R1 such that
A5: for x be Point of S st x in N1 holds f1/.x - f1/.x0 = L1.(x-x0) + R1
  /.(x-x0) by A4;
  consider N2 be Neighbourhood of x0 such that
A6: N2 c= dom f2 and
A7: ex L,R st for x be Point of S st x in N2 holds f2/.x - f2/.x0 = L.(x
  -x0) + R/.(x-x0) by A2,Def6;
  consider L2,R2 such that
A8: for x be Point of S st x in N2 holds f2/.x - f2/.x0 = L2.(x-x0) + R2
  /.(x-x0) by A7;
  reconsider R=R1+R2 as REST of S,T by Th33;
  set L=L1+L2;
  consider N be Neighbourhood of x0 such that
A9: N c= N1 and
A10: N c= N2 by Th1;
A11: N c= dom f2 by A6,A10,XBOOLE_1:1;
  N c= dom f1 by A3,A9,XBOOLE_1:1;
  then N /\ N c= dom f1 /\ dom f2 by A11,XBOOLE_1:27;
  then
A12: N c= dom (f1+f2) by VFUNCT_1:def 1;
A13: R1 is total & R2 is total by Def5;
A14: now
    let x be Point of S;
A15: x0 in N by NFCONT_1:4;
    assume
A16: x in N;
    hence (f1+f2)/.x - (f1+f2)/.x0 = (f1/.x+f2/.x) - (f1+f2)/.x0 by A12,
VFUNCT_1:def 1
      .=f1/.x+f2/.x - (f1/.x0+f2/.x0) by A12,A15,VFUNCT_1:def 1
      .=f1/.x+f2/.x - f1/.x0 - f2/.x0 by RLVECT_1:41
      .=(f1/.x +-f1/.x0) + f2/.x - f2/.x0 by RLVECT_1:def 6
      .=(f1/.x - f1/.x0) + (f2/.x - f2/.x0) by RLVECT_1:def 6
      .=L1.(x-x0)+R1/.(x-x0) + (f2/.x - f2/.x0) by A5,A9,A16
      .=L1.(x-x0)+R1/.(x-x0) + (L2.(x-x0) + R2/.(x-x0)) by A8,A10,A16
      .=R1/.(x-x0)+L1.(x-x0) + L2.(x-x0) + R2/.(x-x0) by RLVECT_1:def 6
      .=(L1.(x-x0)+L2.(x-x0)) + R1/.(x-x0) + R2/.(x-x0) by RLVECT_1:def 6
      .=(L1.(x-x0)+L2.(x-x0)) + (R1/.(x-x0) + R2/.(x-x0)) by RLVECT_1:def 6
      .=L.(x-x0)+(R1/.(x-x0) + R2/.(x-x0)) by LOPBAN_1:41
      .=L.(x-x0)+R/.(x-x0) by A13,VFUNCT_1:43;
  end;
  hence f1+f2 is_differentiable_in x0 by A12,Def6;
  hence diff(f1+f2,x0)=L by A12,A14,Def7
    .=diff(f1,x0) + L2 by A1,A3,A5,Def7
    .=diff(f1,x0) + diff(f2,x0) by A2,A6,A8,Def7;
end;

theorem Th41:
  for f1,f2,x0 st f1 is_differentiable_in x0 & f2
  is_differentiable_in x0 holds f1-f2 is_differentiable_in x0 & diff(f1-f2,x0)=
  diff(f1,x0)-diff(f2,x0)
proof
  let f1,f2,x0;
  assume that
A1: f1 is_differentiable_in x0 and
A2: f2 is_differentiable_in x0;
  consider N1 be Neighbourhood of x0 such that
A3: N1 c= dom f1 and
A4: ex L,R st for x be Point of S st x in N1 holds f1/.x - f1/.x0 = L.(x
  -x0) + R/.(x-x0) by A1,Def6;
  consider L1,R1 such that
A5: for x be Point of S st x in N1 holds f1/.x - f1/.x0 = L1.(x-x0) + R1
  /.(x-x0) by A4;
  consider N2 be Neighbourhood of x0 such that
A6: N2 c= dom f2 and
A7: ex L,R st for x be Point of S st x in N2 holds f2/.x - f2/.x0 = L.(x
  -x0) + R/.(x-x0) by A2,Def6;
  consider L2,R2 such that
A8: for x be Point of S st x in N2 holds f2/.x - f2/.x0 = L2.(x-x0) + R2
  /.(x-x0) by A7;
  reconsider R=R1-R2 as REST of S,T by Th33;
  set L=L1-L2;
  consider N be Neighbourhood of x0 such that
A9: N c= N1 and
A10: N c= N2 by Th1;
A11: N c= dom f2 by A6,A10,XBOOLE_1:1;
  N c= dom f1 by A3,A9,XBOOLE_1:1;
  then N /\ N c= dom f1 /\ dom f2 by A11,XBOOLE_1:27;
  then
A12: N c= dom (f1-f2) by VFUNCT_1:def 2;
A13: R1 is total & R2 is total by Def5;
A14: now
    let x be Point of S;
A15: x0 in N by NFCONT_1:4;
    assume
A16: x in N;
    hence (f1-f2)/.x - (f1-f2)/.x0 = (f1/.x-f2/.x) - (f1-f2)/.x0 by A12,
VFUNCT_1:def 2
      .=f1/.x - f2/.x - (f1/.x0-f2/.x0) by A12,A15,VFUNCT_1:def 2
      .=f1/.x - f2/.x - f1/.x0 + f2/.x0 by RLVECT_1:43
      .=f1/.x - (f1/.x0 + f2/.x) + f2/.x0 by RLVECT_1:41
      .=f1/.x - f1/.x0 - f2/.x + f2/.x0 by RLVECT_1:41
      .=f1/.x - f1/.x0 - (f2/.x - f2/.x0) by RLVECT_1:43
      .=L1.(x-x0) + R1/.(x-x0) - (f2/.x - f2/.x0) by A5,A9,A16
      .=L1.(x-x0) + R1/.(x-x0) - (L2.(x-x0) + R2/.(x-x0)) by A8,A10,A16
      .=L1.(x-x0) + R1/.(x-x0) - L2.(x-x0) - R2/.(x-x0) by RLVECT_1:41
      .=L1.(x-x0) + (R1/.(x-x0) +- L2.(x-x0)) - R2/.(x-x0) by RLVECT_1:def 6
      .=L1.(x-x0) - (L2.(x-x0) - R1/.(x-x0)) - R2/.(x-x0) by RLVECT_1:47
      .=L1.(x-x0) - L2.(x-x0) + R1/.(x-x0) - R2/.(x-x0) by RLVECT_1:43
      .=L1.(x-x0) - L2.(x-x0) + (R1/.(x-x0) - R2/.(x-x0)) by RLVECT_1:def 6
      .=L.(x-x0) + (R1/.(x-x0) - R2/.(x-x0)) by LOPBAN_1:46
      .=L.(x-x0) + R/.(x-x0) by A13,VFUNCT_1:43;
  end;
  hence f1-f2 is_differentiable_in x0 by A12,Def6;
  hence diff(f1-f2,x0)=L by A12,A14,Def7
    .=diff(f1,x0) - L2 by A1,A3,A5,Def7
    .=diff(f1,x0) - diff(f2,x0) by A2,A6,A8,Def7;
end;

theorem Th42:
  for r,f,x0 st f is_differentiable_in x0 holds r(#)f
  is_differentiable_in x0 & diff((r(#)f),x0) = r*diff(f,x0)
proof
  let r,f,x0;
  assume
A1: f is_differentiable_in x0;
  then consider N1 be Neighbourhood of x0 such that
A2: N1 c= dom f and
A3: ex L,R st for x be Point of S st x in N1 holds f/.x - f/.x0 = L.(x-
  x0) + R/.(x-x0) by Def6;
  consider L1,R1 such that
A4: for x be Point of S st x in N1 holds f/.x - f/.x0 = L1.(x-x0) + R1/.
  (x-x0) by A3;
  reconsider R = r(#)R1 as REST of S,T by Th34;
  set L = r*L1;
A5: N1 c= dom(r(#)f) by A2,VFUNCT_1:def 4;
A6: R1 is total by Def5;
A7: now
    let x be Point of S;
A8: x0 in N1 by NFCONT_1:4;
    assume
A9: x in N1;
    hence (r(#)f)/.x - (r(#)f)/.x0 = r*(f/.x) - (r(#)f)/.x0 by A5,
VFUNCT_1:def 4
      .= r*f/.x - r*f/.x0 by A5,A8,VFUNCT_1:def 4
      .= r*(f/.x - f/.x0) by RLVECT_1:48
      .= r*(L1.(x-x0) + R1/.(x-x0)) by A4,A9
      .= r*L1.(x-x0) + r*R1/.(x-x0) by RLVECT_1:def 8
      .= L.(x-x0) + r*R1/.(x-x0) by LOPBAN_1:42
      .= L.(x-x0) + R/.(x-x0) by A6,VFUNCT_1:45;
  end;
  hence r(#)f is_differentiable_in x0 by A5,Def6;
  hence diff((r(#)f),x0) = L by A5,A7,Def7
    .= r*diff(f,x0) by A1,A2,A4,Def7;
end;

theorem
  for f be PartFunc of S,S for Z be Subset of S st Z is open & Z c= dom
  f & f|Z = id Z holds f is_differentiable_on Z & for x be Point of S st x in Z
  holds (f`|Z)/.x = id (the carrier of S)
proof
  set L= id (the carrier of S);
  R_NormSpace_of_BoundedLinearOperators(S,S) = NORMSTR (#
    BoundedLinearOperators(S,S), Zero_(BoundedLinearOperators(S,S),
    R_VectorSpace_of_LinearOperators(S,S)), Add_(BoundedLinearOperators(S,S),
    R_VectorSpace_of_LinearOperators(S,S)), Mult_(BoundedLinearOperators(S,S),
R_VectorSpace_of_LinearOperators(S,S)), BoundedLinearOperatorsNorm(S,S) #) & L
  is bounded LinearOperator of S,S by LOPBAN_1:def 15,LOPBAN_2:3;
  then reconsider L as Point of R_NormSpace_of_BoundedLinearOperators(S,S) by
LOPBAN_1:def 10;
  let f be PartFunc of S,S;
  let Z be Subset of S such that
A1: Z is open;
  reconsider R = (the carrier of S) --> 0.S as PartFunc of S,S;
A2: dom R = (the carrier of S) by FUNCOP_1:19;
  now
    let h;
A3: now
      let n be Nat;
A4:   R/.(h.n) =R.(h.n) by A2,PARTFUN1:def 8
        .=0.S by FUNCOP_1:13;
A5:   rng h c= dom R by A2;
A6:   n in NAT by ORDINAL1:def 13;
      hence ((||.h.||")(#)(R/*h)).n = (||.h.||".n)*((R/*h).n) by Def2
        .= (||.h.||".n)*(R/.(h.n)) by A6,A5,FUNCT_2:186
        .= 0.S by A4,RLVECT_1:23;
    end;
    then
A7: (||.h.||")(#)(R/*h) is constant by VALUED_0:def 18;
    hence (||.h.||")(#)(R/*h) is convergent by Th21;
    ((||.h.||")(#)(R/*h)).0 = 0.S by A3;
    hence lim ((||.h.||")(#)(R/*h)) = 0.S by A7,Th21;
  end;
  then reconsider R as REST of S,S by Def5;
  assume that
A8: Z c= dom f and
A9: f|Z = id Z;
A10: now
    let x be Point of S;
    assume
A11: x in Z;
    then f|Z.x = x by A9,FUNCT_1:35;
    then f.x = x by A11,FUNCT_1:72;
    hence f/.x =x by A8,A11,PARTFUN1:def 8;
  end;
A12: now
    let x0 be Point of S;
    assume
A13: x0 in Z;
    then consider N being Neighbourhood of x0 such that
A14: N c= Z by A1,Th2;
A15: for x be Point of S st x in N holds f/.x - f/.x0 = L.(x-x0) + R/.(x- x0)
    proof
      let x be Point of S;
A16:  R/.(x-x0) =R.(x-x0) by A2,PARTFUN1:def 8
        .=0.S by FUNCOP_1:13;
      assume x in N;
      hence f/.x - f/.x0 = x - f/.x0 by A10,A14
        .= x - x0 by A10,A13
        .= L.(x-x0) by FUNCT_1:34
        .= L.(x-x0) + R/.(x-x0) by A16,RLVECT_1:10;
    end;
    N c= dom f by A8,A14,XBOOLE_1:1;
    hence f is_differentiable_in x0 by A15,Def6;
  end;
  hence
A17: f is_differentiable_on Z by A1,A8,Th36;
  let x0 be Point of S;
  assume
A18: x0 in Z;
  then consider N1 being Neighbourhood of x0 such that
A19: N1 c= Z by A1,Th2;
A20: f is_differentiable_in x0 by A12,A18;
  then ex N being Neighbourhood of x0 st N c= dom f & ex L be Point of
R_NormSpace_of_BoundedLinearOperators(S,S) , R be REST of S,S st for x be Point
  of S st x in N holds f/.x - f/.x0 = L.(x-x0) + R/.(x-x0) by Def6;
  then consider N being Neighbourhood of x0 such that
A21: N c= dom f;
  consider N2 being Neighbourhood of x0 such that
A22: N2 c= N1 and
A23: N2 c= N by Th1;
A24: N2 c= dom f by A21,A23,XBOOLE_1:1;
A25: for x be Point of S st x in N2 holds f/.x - f/.x0 = L.(x-x0) + R/.(x-x0 )
  proof
    let x be Point of S;
A26: R/.(x-x0) =R.(x-x0) by A2,PARTFUN1:def 8
      .=0.S by FUNCOP_1:13;
    assume x in N2;
    then x in N1 by A22;
    hence f/.x - f/.x0 = x - f/.x0 by A10,A19
      .= x - x0 by A10,A18
      .= L.(x-x0) by FUNCT_1:34
      .= L.(x-x0) + R/.(x-x0) by A26,RLVECT_1:10;
  end;
  thus (f`|Z)/.x0 = diff(f,x0) by A17,A18,Def9
    .= id the carrier of S by A20,A24,A25,Def7;
end;

theorem
  for Z be Subset of S st Z is open for f1,f2 st Z c= dom (f1+f2) & f1
  is_differentiable_on Z & f2 is_differentiable_on Z holds f1+f2
is_differentiable_on Z & for x be Point of S st x in Z holds ((f1+f2)`|Z)/.x =
  diff(f1,x) + diff(f2,x)
proof
  let Z be Subset of S such that
A1: Z is open;
  let f1,f2;
  assume that
A2: Z c= dom (f1+f2) and
A3: f1 is_differentiable_on Z & f2 is_differentiable_on Z;
  now
    let x0 be Point of S;
    assume x0 in Z;
    then f1 is_differentiable_in x0 & f2 is_differentiable_in x0 by A1,A3,Th36;
    hence f1+f2 is_differentiable_in x0 by Th40;
  end;
  hence
A4: f1+f2 is_differentiable_on Z by A1,A2,Th36;
  now
    let x be Point of S;
    assume
A5: x in Z;
    then
A6: f1 is_differentiable_in x & f2 is_differentiable_in x by A1,A3,Th36;
    thus ((f1+f2)`|Z)/.x = diff((f1+f2),x) by A4,A5,Def9
      .= diff(f1,x) + diff(f2,x) by A6,Th40;
  end;
  hence thesis;
end;

theorem
  for Z be Subset of S st Z is open for f1,f2 st Z c= dom (f1-f2) & f1
  is_differentiable_on Z & f2 is_differentiable_on Z holds f1-f2
is_differentiable_on Z & for x be Point of S st x in Z holds ((f1-f2)`|Z)/.x =
  diff(f1,x) - diff(f2,x)
proof
  let Z be Subset of S such that
A1: Z is open;
  let f1,f2;
  assume that
A2: Z c= dom (f1-f2) and
A3: f1 is_differentiable_on Z & f2 is_differentiable_on Z;
  now
    let x0 be Point of S;
    assume x0 in Z;
    then f1 is_differentiable_in x0 & f2 is_differentiable_in x0 by A1,A3,Th36;
    hence f1-f2 is_differentiable_in x0 by Th41;
  end;
  hence
A4: f1-f2 is_differentiable_on Z by A1,A2,Th36;
  now
    let x be Point of S;
    assume
A5: x in Z;
    then
A6: f1 is_differentiable_in x & f2 is_differentiable_in x by A1,A3,Th36;
    thus ((f1-f2)`|Z)/.x = diff((f1-f2),x) by A4,A5,Def9
      .= diff(f1,x) - diff(f2,x) by A6,Th41;
  end;
  hence thesis;
end;

theorem
  for Z be Subset of S st Z is open for r,f st Z c= dom (r(#)f) & f
is_differentiable_on Z holds r(#)f is_differentiable_on Z & for x be Point of S
  st x in Z holds ((r(#)f)`|Z)/.x =r*diff(f,x)
proof
  let Z be Subset of S such that
A1: Z is open;
  let r,f;
  assume that
A2: Z c= dom (r(#)f) and
A3: f is_differentiable_on Z;
  now
    let x0 be Point of S;
    assume x0 in Z;
    then f is_differentiable_in x0 by A1,A3,Th36;
    hence r(#)f is_differentiable_in x0 by Th42;
  end;
  hence
A4: r(#)f is_differentiable_on Z by A1,A2,Th36;
  now
    let x be Point of S;
    assume
A5: x in Z;
    then
A6: f is_differentiable_in x by A1,A3,Th36;
    thus ((r(#)f)`|Z)/.x = diff((r(#)f),x) by A4,A5,Def9
      .= r*diff(f,x) by A6,Th42;
  end;
  hence thesis;
end;

theorem
  for Z be Subset of S st Z is open holds (Z c= dom f & f|Z is constant
implies f is_differentiable_on Z & for x be Point of S st x in Z holds (f`|Z)/.
  x = 0.R_NormSpace_of_BoundedLinearOperators(S,T))
proof
  let Z be Subset of S such that
A1: Z is open;
  reconsider R = (the carrier of S) --> 0.T as PartFunc of S,T;
  set L=0.R_NormSpace_of_BoundedLinearOperators(S,T);
  assume that
A2: Z c= dom f and
A3: f|Z is constant;
  R_NormSpace_of_BoundedLinearOperators(S,T) = NORMSTR (#
    BoundedLinearOperators(S,T), Zero_(BoundedLinearOperators(S,T),
    R_VectorSpace_of_LinearOperators(S,T)), Add_(BoundedLinearOperators(S,T),
    R_VectorSpace_of_LinearOperators(S,T)), Mult_(BoundedLinearOperators(S,T),
R_VectorSpace_of_LinearOperators(S,T)), BoundedLinearOperatorsNorm(S,T) #) by
LOPBAN_1:def 15;
  then reconsider L as Element of BoundedLinearOperators(S,T);
A4: dom R = the carrier of S by FUNCOP_1:19;
  now
    let h;
A5: now
      let n be Nat;
A6:   R/.(h.n) =R.(h.n) by A4,PARTFUN1:def 8
        .=0.T by FUNCOP_1:13;
A7:   rng h c= dom R by A4;
A8:   n in NAT by ORDINAL1:def 13;
      hence ((||.h.||")(#)(R/*h)).n = (||.h.||".n)*((R/*h).n) by Def2
        .=(||.h.||".n)*(R/.(h.n)) by A8,A7,FUNCT_2:186
        .=0.T by A6,RLVECT_1:23;
    end;
    then
A9: (||.h.||")(#)(R/*h) is constant by VALUED_0:def 18;
    hence (||.h.||")(#)(R/*h) is convergent by Th21;
    ((||.h.||")(#)(R/*h)).0 = 0.T by A5;
    hence lim ((||.h.||")(#)(R/*h)) = 0.T by A9,Th21;
  end;
  then reconsider R as REST of S,T by Def5;
  consider r be Point of T such that
A10: for x be Point of S st x in Z/\dom f holds f.x=r by A3,PARTFUN2:76;
A11: now
    let x be Point of S;
    assume
A12: x in Z/\dom f;
    Z/\ dom f c= dom f by XBOOLE_1:17;
    hence f/.x=f.x by A12,PARTFUN1:def 8
      .=r by A10,A12;
  end;
A13: ((the carrier of S) --> 0.T) = L by LOPBAN_1:37;
A14: now
    let x0 be Point of S;
    assume
A15: x0 in Z;
    then consider N being Neighbourhood of x0 such that
A16: N c= Z by A1,Th2;
A17: N c= dom f by A2,A16,XBOOLE_1:1;
A18: x0 in Z/\dom f by A2,A15,XBOOLE_0:def 4;
    for x be Point of S st x in N holds f/.x-f/.x0=L.(x-x0)+R/.(x-x0)
    proof
      let x be Point of S;
A19:  R/.(x-x0) =R.(x-x0) by A4,PARTFUN1:def 8
        .=0.T by FUNCOP_1:13;
      assume x in N;
      then x in Z/\dom f by A16,A17,XBOOLE_0:def 4;
      hence f/.x-f/.x0=r-f/.x0 by A11
        .=r - r by A11,A18
        .=0.T by RLVECT_1:28
        .= 0.T + 0.T by RLVECT_1:10
        .=L.(x-x0)+R/.(x-x0) by A13,A19,FUNCOP_1:13;
    end;
    hence f is_differentiable_in x0 by A17,Def6;
  end;
  hence
A20: f is_differentiable_on Z by A1,A2,Th36;
  let x0 be Point of S;
  assume
A21: x0 in Z;
  then consider N being Neighbourhood of x0 such that
A22: N c= Z by A1,Th2;
A23: N c= dom f by A2,A22,XBOOLE_1:1;
A24: x0 in Z/\dom f by A2,A21,XBOOLE_0:def 4;
A25: for x be Point of S st x in N holds f/.x-f/.x0 =L.(x-x0)+R/.(x-x0)
  proof
    let x be Point of S;
A26: R/.(x-x0) =R.(x-x0) by A4,PARTFUN1:def 8
      .=0.T by FUNCOP_1:13;
    assume x in N;
    then x in Z/\dom f by A22,A23,XBOOLE_0:def 4;
    hence f/.x - f/.x0 = r - f/.x0 by A11
      .=r - r by A11,A24
      .=0.T by RLVECT_1:28
      .= 0.T + 0.T by RLVECT_1:10
      .=L.(x-x0) + R/.(x-x0) by A13,A26,FUNCOP_1:13;
  end;
A27: f is_differentiable_in x0 by A14,A21;
  thus (f`|Z)/.x0 = diff(f,x0) by A20,A21,Def9
    .=0.R_NormSpace_of_BoundedLinearOperators(S,T) by A27,A23,A25,Def7;
end;

theorem
  for f be PartFunc of S,S for r be Real for p be Point of S for Z be
  Subset of S st Z is open holds ( Z c= dom f & (for x be Point of S st x in Z
holds f/.x = r*x + p) implies f is_differentiable_on Z & for x be Point of S st
  x in Z holds (f`|Z)/.x = r * FuncUnit(S) )
proof
  let f be PartFunc of S,S;
  let r be Real;
  let p be Point of S;
  let Z be Subset of S such that
A1: Z is open;
A2: R_NormSpace_of_BoundedLinearOperators(S,S) = NORMSTR (#
    BoundedLinearOperators(S,S), Zero_(BoundedLinearOperators(S,S),
    R_VectorSpace_of_LinearOperators(S,S)), Add_(BoundedLinearOperators(S,S),
    R_VectorSpace_of_LinearOperators(S,S)), Mult_(BoundedLinearOperators(S,S),
R_VectorSpace_of_LinearOperators(S,S)), BoundedLinearOperatorsNorm(S,S) #) by
LOPBAN_1:def 15;
  then reconsider
  II=FuncUnit(S) as Point of R_NormSpace_of_BoundedLinearOperators(
  S,S);
  set L = r * II;
  reconsider L as Point of R_NormSpace_of_BoundedLinearOperators(S,S);
  reconsider R = (the carrier of S) --> 0.S as PartFunc of S,S;
  assume that
A3: Z c= dom f and
A4: for x be Point of S st x in Z holds f/.x = r*x + p;
A5: L=r*FuncUnit(S) by A2,LOPBAN_2:def 3;
A6: dom R = the carrier of S by FUNCOP_1:19;
  now
    let h;
A7: now
      let n be Nat;
A8:   R/.(h.n) =R.(h.n) by A6,PARTFUN1:def 8
        .=0.S by FUNCOP_1:13;
A9:   rng h c= dom R by A6;
A10:  n in NAT by ORDINAL1:def 13;
      hence ((||.h.||")(#)(R/*h)).n = (||.h.||".n)*((R/*h).n) by Def2
        .=(||.h.||".n)*(R/.(h.n)) by A10,A9,FUNCT_2:186
        .=0.S by A8,RLVECT_1:23;
    end;
    then
A11: (||.h.||")(#)(R/*h) is constant by VALUED_0:def 18;
    hence (||.h.||")(#)(R/*h) is convergent by Th21;
    ((||.h.||")(#)(R/*h)).0 = 0.S by A7;
    hence lim ((||.h.||")(#)(R/*h)) = 0.S by A11,Th21;
  end;
  then reconsider R as REST of S,S by Def5;
A12: now
    let x0 be Point of S;
    assume
A13: x0 in Z;
    then consider N being Neighbourhood of x0 such that
A14: N c= Z by A1,Th2;
A15: for x be Point of S st x in N holds f/.x-f/.x0=L.(x-x0)+R/.(x-x0)
    proof
      let x be Point of S;
A16:  R/.(x-x0) =R.(x-x0) by A6,PARTFUN1:def 8
        .=0.S by FUNCOP_1:13;
      (x-x0)=(id the carrier of S).(x-x0) by FUNCT_1:34;
      then
A17:  r* (x-x0)=r* ((FuncUnit(S)).(x-x0)) by LOPBAN_2:def 5
        .=L.(x-x0) by LOPBAN_1:42;
      assume x in N;
      hence f/.x-f/.x0=r*x+p-f/.x0 by A4,A14
        .=r*x+p - (r*x0+p) by A4,A13
        .=r*x+(p-(r*x0+p)) by RLVECT_1:def 6
        .=r*x+(p-r*x0-p) by RLVECT_1:41
        .=r*x+(p+-r*x0-p)
        .=r*x+(-r*x0+(p-p)) by RLVECT_1:def 6
        .=r*x+(-r*x0+0.S) by RLVECT_1:28
        .=r*x-r*x0 by RLVECT_1:10
        .=r*(x-x0) by RLVECT_1:48
        .=L.(x-x0)+R/.(x-x0) by A16,A17,RLVECT_1:10;
    end;
    N c= dom f by A3,A14,XBOOLE_1:1;
    hence f is_differentiable_in x0 by A15,Def6;
  end;
  hence
A18: f is_differentiable_on Z by A1,A3,Th36;
  let x0 be Point of S;
  assume
A19: x0 in Z;
  then consider N being Neighbourhood of x0 such that
A20: N c= Z by A1,Th2;
A21: for x be Point of S st x in N holds f/.x-f/.x0=L.(x-x0)+R/.(x-x0)
  proof
    let x be Point of S;
A22: R/.(x-x0) =R.(x-x0) by A6,PARTFUN1:def 8
      .=0.S by FUNCOP_1:13;
    (x-x0)=(id the carrier of S).(x-x0) by FUNCT_1:34;
    then
A23: r* (x-x0)=r* ((FuncUnit(S)).(x-x0)) by LOPBAN_2:def 5
      .=L.(x-x0) by LOPBAN_1:42;
    assume x in N;
    hence f/.x - f/.x0 = r*x+p - f/.x0 by A4,A20
      .=r*x+p-(r*x0+p) by A4,A19
      .=r*x+(p-(r*x0+p)) by RLVECT_1:def 6
      .=r*x+(p-r*x0-p) by RLVECT_1:41
      .=r*x+(p+-r*x0-p)
      .=r*x+(-r*x0+(p-p)) by RLVECT_1:def 6
      .=r*x+(-r*x0+0.S) by RLVECT_1:28
      .=r*x-r*x0 by RLVECT_1:10
      .=r*(x-x0) by RLVECT_1:48
      .=L.(x-x0)+R/.(x-x0) by A22,A23,RLVECT_1:10;
  end;
A24: N c= dom f by A3,A20,XBOOLE_1:1;
A25: f is_differentiable_in x0 by A12,A19;
  thus (f`|Z)/.x0 = diff(f,x0) by A18,A19,Def9
    .=r * FuncUnit(S) by A5,A25,A24,A21,Def7;
end;

theorem Th49:
  for x0 be Point of S holds f is_differentiable_in x0 implies f
  is_continuous_in x0
proof
  let x0 be Point of S;
  assume
A1: f is_differentiable_in x0;
  then consider N being Neighbourhood of x0 such that
A2: N c= dom f and
  ex L,R st for x be Point of S st x in N holds f/.x - f/.x0 = L.(x-x0 ) +
  R/.(x-x0) by Def6;
A3: now
    consider g be Real such that
A4: 0<g and
A5: {y where y is Point of S:||.y-x0.|| < g } c= N by NFCONT_1:def 3;
    reconsider s2 = NAT --> x0 as sequence of S;
    let s1 be sequence of S such that
A6: rng s1 c= dom f and
A7: s1 is convergent and
A8: lim s1 = x0 and
A9: for n holds s1.n<>x0;
    consider l be Element of NAT such that
A10: for m be Element of NAT st l<=m holds ||.s1.m-x0.||<g by A7,A8,A4,
NORMSP_1:def 11;
    deffunc G(Element of NAT) = s1.$1-s2.$1;
    consider s3 be sequence of S such that
A11: for n holds s3.n=G(n) from FUNCT_2:sch 4;
A12: now
      given n such that
A13:  s3.n=0.S;
      s1.n-s2.n=0.S by A11,A13;
      then s1.n-x0=0.S by FUNCOP_1:13;
      hence contradiction by A9,RLVECT_1:35;
    end;
    now
      given n such that
A14:  (s3^\l).n=0.S;
      s3.(n+l)=0.S by A14,NAT_1:def 3;
      hence contradiction by A12;
    end;
    then
A15: s3^\l is non-zero by Th7;
    reconsider c =s2^\l as constant sequence of S;
A16: s3=s1-s2 by A11,NORMSP_1:def 6;
A17: s2 is convergent by Th21;
    then
A18: s3 is convergent by A7,A16,NORMSP_1:35;
    then
A19: s3^\l is convergent by LOPBAN_3:14;
    lim s2 = s2.0 by Th21
      .=x0 by FUNCOP_1:13;
    then lim s3 =x0-x0 by A7,A8,A17,A16,NORMSP_1:43
      .=0.S by RLVECT_1:28;
    then lim(s3^\l)=0.S by A18,LOPBAN_3:14;
    then reconsider h=s3^\l as convergent_to_0 sequence of S by A19,A15,Def4;
    now
      let n;
      thus (f/*(h+c)-f/*c+f/*c).n =(f/*(h+c)-f/*c).n+(f/*c).n by NORMSP_1:def 5
        .=(f/*(h+c)).n-(f/*c).n+(f/*c).n by NORMSP_1:def 6
        .=(f/*(h+c)).n-((f/*c).n-(f/*c).n) by RLVECT_1:43
        .=(f/*(h+c)).n-0.T by RLVECT_1:28
        .=(f/*(h+c)).n by RLVECT_1:26;
    end;
    then
A20: f/*(h+c)-f/*c+(f/*c)=f/*(h+c) by FUNCT_2:113;
    now
      let n;
      thus (h+c).n=((s1-s2+s2)^\l).n by A16,Th15
        .=(s1-s2+s2).(n+l) by NAT_1:def 3
        .=(s1-s2).(n+l)+s2.(n+l) by NORMSP_1:def 5
        .=s1.(n+l)-s2.(n+l)+s2.(n+l) by NORMSP_1:def 6
        .=s1.(n+l)-(s2.(n+l)-s2.(n+l)) by RLVECT_1:43
        .=s1.(n+l)-0.S by RLVECT_1:28
        .=s1.(l+n) by RLVECT_1:26
        .=(s1^\l).n by NAT_1:def 3;
    end;
    then
A21: f/*(h+c)-f/*c+(f/*c)=f/*(s1^\l) by A20,FUNCT_2:113
      .=(f/*s1)^\l by A6,VALUED_0:27;
A22: rng c = {x0}
    proof
      thus rng c c= {x0}
      proof
        let y be set;
        assume y in rng c;
        then consider n such that
A23:    y=(s2^\l).n by NFCONT_1:6;
        y=s2.(n+l) by A23,NAT_1:def 3;
        then y=x0 by FUNCOP_1:13;
        hence thesis by TARSKI:def 1;
      end;
      let y;
      assume y in {x0};
      then
A24:  y=x0 by TARSKI:def 1;
      c.0=s2.(0+l) by NAT_1:def 3
        .= y by A24,FUNCOP_1:13;
      hence thesis by NFCONT_1:6;
    end;
A25: now
      let p be Real such that
A26:  0<p;
      take n=0;
      let m be Element of NAT such that
      n<=m;
      x0 in N by NFCONT_1:4;
      then rng c c= dom f by A2,A22,ZFMISC_1:37;
      then ||.(f/*c).m-f/.x0.|| =||.f/.(c.m)-f/.x0.|| by FUNCT_2:186
        .=||.f/.(s2.(m+l))-f/.x0.|| by NAT_1:def 3
        .=||.f/.x0-f/.x0.|| by FUNCOP_1:13
        .=||.0.T.|| by RLVECT_1:28
        .=0 by NORMSP_1:5;
      hence ||.(f/*c).m-f/.x0.||<p by A26;
    end;
    then
A27: f/*c is convergent by NORMSP_1:def 9;
A28: rng (h+c) c= N
    proof
      let y be set;
      assume
A29:  y in rng(h+c);
      then consider n such that
A30:  y=(h+c).n by NFCONT_1:6;
      reconsider y1=y as Point of S by A29;
      (h+c).n=((s1-s2+s2)^\l).n by A16,Th15
        .=(s1-s2+s2).(n+l) by NAT_1:def 3
        .=(s1-s2).(n+l)+s2.(n+l) by NORMSP_1:def 5
        .=s1.(n+l)-s2.(n+l)+s2.(n+l) by NORMSP_1:def 6
        .=s1.(n+l)-(s2.(n+l)-s2.(n+l)) by RLVECT_1:43
        .=s1.(n+l)-0.S by RLVECT_1:28
        .=s1.(l+n) by RLVECT_1:26;
      then ||.(h+c).n-x0.||<g by A10,NAT_1:12;
      then y1 in {z where z is Point of S:||.z-x0.|| < g } by A30;
      hence thesis by A5;
    end;
    then
A31: (f/*(h+c) - f/*c) is convergent by A1,A2,A22,Th39;
    then f/*(h+c)-f/*c+f/*c is convergent by A27,NORMSP_1:34;
    hence f/*s1 is convergent by A21,LOPBAN_3:15;
A32: lim (f/*(h+c) - f/*c) = 0.T by A1,A2,A22,A28,Th39;
    lim(f/*c)=f/.x0 by A25,A27,NORMSP_1:def 11;
    then lim(f/*(h+c)-f/*c+f/*c)=0.T +f/.x0 by A31,A32,A27,NORMSP_1:42
      .=f/.x0 by RLVECT_1:10;
    hence f/.x0=lim(f/*s1) by A31,A27,A21,LOPBAN_3:16,NORMSP_1:34;
  end;
  x0 in N by NFCONT_1:4;
  hence thesis by A2,A3,Th32;
end;

theorem
  f is_differentiable_on X implies f is_continuous_on X
proof
  assume
A1: f is_differentiable_on X;
  hence X c= dom f by Def8;
  let x be Point of S;
  assume x in X;
  then f|X is_differentiable_in x by A1,Def8;
  hence thesis by Th49;
end;

theorem
  for Z be Subset of S st Z is open holds ( f is_differentiable_on X & Z
  c= X implies f is_differentiable_on Z )
proof
  let Z be Subset of S such that
A1: Z is open;
  assume that
A2: f is_differentiable_on X and
A3: Z c= X;
  X c= dom f by A2,Def8;
  hence
A4: Z c= dom f by A3,XBOOLE_1:1;
  let x0;
  assume
A5: x0 in Z;
  then consider N1 being Neighbourhood of x0 such that
A6: N1 c= Z by A1,Th2;
  f|X is_differentiable_in x0 by A2,A3,A5,Def8;
  then consider N being Neighbourhood of x0 such that
A7: N c= dom(f|X) and
A8: ex L,R st for x be Point of S st x in N holds (f|X)/.x-(f|X)/.x0=L.(
  x-x0)+R/.(x-x0) by Def6;
  consider N2 being Neighbourhood of x0 such that
A9: N2 c= N and
A10: N2 c= N1 by Th1;
A11: N2 c= Z by A6,A10,XBOOLE_1:1;
  take N2;
  dom(f|X)=dom f/\X by RELAT_1:90;
  then dom(f|X) c=dom f by XBOOLE_1:17;
  then N c= dom f by A7,XBOOLE_1:1;
  then N2 c=dom f by A9,XBOOLE_1:1;
  then N2 c=dom f/\Z by A11,XBOOLE_1:19;
  hence
A12: N2 c=dom(f|Z) by RELAT_1:90;
  consider L,R such that
A13: for x be Point of S st x in N holds (f|X)/.x-(f|X)/.x0=L.(x-x0)+R/.
  (x-x0) by A8;
  take L,R;
  let x be Point of S;
  assume
A14: x in N2;
  then (f|X)/.x-(f|X)/.x0=L.(x-x0)+R/.(x-x0) by A9,A13;
  then
A15: (f|X)/.x-f/.x0=L.(x-x0)+R/.(x-x0) by A3,A4,A5,PARTFUN2:35;
  x in N by A9,A14;
  then f/.x-f/.x0=L.(x-x0)+R/.(x-x0) by A7,A15,PARTFUN2:32;
  then f/.x-(f|Z)/.x0=L.(x-x0)+R/.(x-x0) by A4,A5,PARTFUN2:35;
  hence thesis by A12,A14,PARTFUN2:32;
end;

theorem
  f is_differentiable_in x0 implies ex N being Neighbourhood of x0 st N
c= dom f & ex R st R/.0.S=0.T & R is_continuous_in 0.S & for x be Point of S st
  x in N holds f/.x-f/.x0 = diff(f,x0).(x-x0) + R/.(x-x0)
proof
  assume f is_differentiable_in x0;
  then consider N being Neighbourhood of x0 such that
A1: N c= dom f and
A2: ex R st for x be Point of S st x in N holds f/.x - f/.x0 = diff(f,x0
  ).(x-x0) + R/.(x-x0) by Def7;
  take N;
  ex R st R/.0.S=0.T & R is_continuous_in 0.S & for x be Point of S st x
  in N holds f/.x-f/.x0 = diff(f,x0).(x-x0) + R/.(x-x0)
  proof
    R_NormSpace_of_BoundedLinearOperators(S,T) = NORMSTR (#
      BoundedLinearOperators(S,T), Zero_(BoundedLinearOperators(S,T),
      R_VectorSpace_of_LinearOperators(S,T)), Add_(BoundedLinearOperators(S,T),
R_VectorSpace_of_LinearOperators(S,T)), Mult_(BoundedLinearOperators(S,T),
R_VectorSpace_of_LinearOperators(S,T)), BoundedLinearOperatorsNorm(S,T) #) by
LOPBAN_1:def 15;
    then reconsider L =diff(f,x0) as Element of BoundedLinearOperators(S,T);
    consider R such that
A3: for x be Point of S st x in N holds f/.x-f/.x0 = diff(f,x0).(x-x0)
    + R/.(x-x0) by A2;
    take R;
    f/.x0 - f/.x0 = L.(x0-x0) + R/.(x0-x0) by A3,NFCONT_1:4;
    then 0.T = L.(x0-x0) + R/.(x0-x0) by RLVECT_1:28;
    then 0.T = L.0.S + R/.(x0-x0) by RLVECT_1:28;
    then
A4: 0.T = L.0.S + R/.0.S by RLVECT_1:28;
    L.(0.S) =modetrans(L,S,T).(0.S) by LOPBAN_1:def 12
      .= modetrans(L,S,T).(0*0.S) by RLVECT_1:23
      .=0*modetrans(L,S,T).0.S by LOPBAN_1:def 6
      .=0.T by RLVECT_1:23;
    hence
A5: R/.0.S=0.T by A4,RLVECT_1:10;
A6: now
      let s1 be sequence of S;
      assume that
      rng s1 c= dom R and
A7:   s1 is convergent & lim s1 = 0.S and
A8:   for n holds s1.n<>0.S;
      s1 is non-zero by A8,Th7;
      then s1 is convergent_to_0 sequence of S by A7,Def4;
      hence R/*s1 is convergent & lim (R/*s1)=R/.(0.S) by A5,Th27;
    end;
    R is total by Def5;
    then dom R=the carrier of S by PARTFUN1:def 4;
    hence thesis by A3,A6,Th32;
  end;
  hence thesis by A1;
end;
