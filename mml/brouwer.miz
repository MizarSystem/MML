:: Brouwer Fixed Point Theorem for Disks on the Plane
::  by Artur Korni{\l}owicz and Yasunari Shidama
::
:: Received February 22, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies FUNCT_1, PRE_TOPC, EUCLID, ORDINAL2, RELAT_1, ARYTM, BROUWER,
      SEQ_1, COMPLEX1, ARYTM_1, ZF_LANG, PSCOMP_1, MCART_1, ARYTM_3, TERMORD,
      PROB_1, BOOLE, SUBSET_1, ABIAN, KNASTER, BORSUK_1, TOPALG_1, ALGSTR_1,
      SEQM_3, BORSUK_2, EQREL_1, WAYBEL20, BORSUK_6, ASYMPT_0, FINSET_1,
      FUNCOP_1, JORDAN1, TOPS_2, REALSET1, METRIC_1, RCOMP_1, TOPREALB,
      TOPMETR, ABSVALUE, PARTFUN1, SQUARE_1, SETFAM_1, TARSKI, TOPREALA,
      FUNCT_3, POLYEQ_1, PARTFUN3, RLVECT_1, EUCLID_2, RVSUM_1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, ORDINAL1, EQREL_1, COMPLEX1,
      SQUARE_1, FUNCT_1, RELSET_1, PARTFUN1, FUNCT_2, BINOP_1, FUNCT_3,
      PSCOMP_1, FINSET_1, FINSEQ_1, NUMBERS, XCMPLX_0, XREAL_0, REAL_1,
      RVSUM_1, 
      RCOMP_1, STRUCT_0, REALSET2, PRE_TOPC, GRCAT_1, BORSUK_1, TOPS_2, EUCLID,
      TOPRNS_1, JORDAN1, QUIN_1, POLYEQ_1, BORSUK_2, TOPALG_1, TOPREAL9,
      TOPALG_2, TOPREALA, TOPREALB, PARTFUN3, EUCLID_2, ABIAN,
      XXREAL_0;
 constructors SQUARE_1, COMPLEX1, QUIN_1, POLYEQ_1, ABIAN, COMPTS_1, REALSET2,
      GRCAT_1, TOPRNS_1, JORDAN1, MONOID_0, JORDAN2C, EUCLID_2, TOPALG_1,
      TOPREAL9, TOPREALA, TOPREALB, PARTFUN3, BINOP_2, NAT_D, JGRAPH_1;
 registrations STRUCT_0, RELSET_1, XREAL_0, REAL_1, BORSUK_1, SEQ_1, PRE_TOPC,
      FUNCT_1, TOPALG_1, BORSUK_2, JORDAN5A, TEX_2, QUIN_1, SQUARE_1, TOPS_1,
      TOPREAL9, NAT_1, TOPALG_2, TOPREALB, TOPMETR, RCOMP_1, RELAT_1, PSCOMP_1,
      TEX_1, REVROT_1, PARTFUN3, SUBSET_1, TOPALG_5, XXREAL_0, FUNCT_2,
      GROUP_1, EUCLID, MEMBERED, RVSUM_1;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM, REAL;
 definitions TARSKI, XBOOLE_0, PRE_TOPC, BORSUK_1, BORSUK_2, TOPALG_2,
      PSCOMP_1, ABIAN, SQUARE_1, BINOP_1, GRCAT_1, STRUCT_0;
 theorems TOPRNS_1, XREAL_0, TARSKI, FUNCT_2, TSEP_1, RFUNCT_1, PRE_TOPC,
      EUCLID, RELAT_1, FUNCT_1, ABIAN, ZFMISC_1, JGRAPH_2, XBOOLE_1, XBOOLE_0,
      TOPS_2, BORSUK_1, TOPALG_1, EQREL_1, BORSUK_2, FUNCOP_1, TOPMETR,
      JORDAN2C, SEQ_1, XCMPLX_0, SQUARE_1, REAL_1, XCMPLX_1, FUNCT_3, COMPLEX1,
      REAL_2, JGRAPH_1, QUIN_1, POLYEQ_1, TOPREAL3, TOPREAL6, TOPS_1, TOPREAL9,
      TOPALG_2, TOPREALB, BORSUK_5, RCOMP_1, PSCOMP_1, TOPREALA, YELLOW12,
      TOPGRP_1, YELLOW_8, PARTFUN3, RVSUM_1, EUCLID_2, XREAL_1, SEQ_2,
      XXREAL_0;
 schemes FUNCT_2, PSCOMP_1;

begin

set T2 = TOP-REAL 2;

reserve
   n for Element of NAT,
   a, r for real number,
   x for Point of TOP-REAL n;

definition
  let S, T be non empty TopSpace;
  func DiffElems(S,T) -> Subset of [:S,T:] equals
  {[s,t] where s is Point of S, t is Point of T: s <> t};
coherence
proof
  set A = {[s,t] where s is Point of S, t is Point of T: s <> t};
  A c= the carrier of [:S,T:]
  proof
    let a be set;
    assume a in A;
    then ex s being Point of S, t being Point of T st a = [s,t] & s <> t;
    hence thesis;
  end;
  hence thesis;
end;
end;

theorem
  for S, T being non empty TopSpace,
      x being set holds
   x in DiffElems(S,T) iff
   ex s being Point of S, t being Point of T st x = [s,t] & s <> t;

registration
  let S be non trivial non empty TopSpace;
  let T be non empty TopSpace;
  cluster DiffElems(S,T) -> non empty;
coherence
proof
  consider s1, s2 being Element of S such that
A1: s1 <> s2 by YELLOW_8:def 1;
  consider t1 being Element of T;
  per cases;
  suppose s1 = t1;
  then [s2,t1] in DiffElems(S,T) by A1;
  hence thesis;
  end;
  suppose s1 <> t1;
  then [s1,t1] in DiffElems(S,T);
  hence thesis;
  end;
end;
end;

registration
  let S be non empty TopSpace;
  let T be non trivial non empty TopSpace;
  cluster DiffElems(S,T) -> non empty;
coherence
proof
  consider s1 being Element of S;
  consider t1, t2 being Element of T such that
A1: t1 <> t2 by YELLOW_8:def 1;
  per cases;
  suppose s1 = t1;
  then [s1,t2] in DiffElems(S,T) by A1;
  hence thesis;
  end;
  suppose s1 <> t1;
  then [s1,t1] in DiffElems(S,T);
  hence thesis;
  end;
end;
end;

theorem Th2:
  cl_Ball(x,0) = {x}
  proof
    thus cl_Ball(x,0) c= {x}
    proof
      let a be set;
      assume
A1:   a in cl_Ball(x,0);
      then reconsider a as Point of TOP-REAL n;
      |. a-x .| <= 0 by A1,TOPREAL9:8;
      then |. a-x .| = 0;
      then a = x by TOPRNS_1:29;
      hence thesis by TARSKI:def 1;
    end;
    let a be set;
    assume a in {x};
    then
A2: a = x by TARSKI:def 1;
    |. x-x .| = 0 by TOPRNS_1:29;
    hence thesis by A2,TOPREAL9:8;
  end;

definition
  let n be Element of NAT, x be Point of TOP-REAL n, r be real number;
  func Tdisk(x,r) -> SubSpace of TOP-REAL n equals
  (TOP-REAL n) | cl_Ball(x,r);
coherence;
end;

registration
  let n be Element of NAT, x be Point of TOP-REAL n;
  let r be non negative (real number);
  cluster Tdisk(x,r) -> non empty;
coherence;
end;

theorem Th3:
  the carrier of Tdisk(x,r) = cl_Ball(x,r)
proof
  [#]Tdisk(x,r) = cl_Ball(x,r) by PRE_TOPC:def 10;
  hence the carrier of Tdisk(x,r) = cl_Ball(x,r);
end;

registration
  let n be Element of NAT, x be Point of TOP-REAL n, r be real number;
  cluster Tdisk(x,r) -> convex;
coherence
proof
  thus [#]Tdisk(x,r) is convex Subset of TOP-REAL n by Th3;
end;
end;

reserve
   n for Element of NAT,
   r for non negative (real number),
   s, t, x for Point of TOP-REAL n;

theorem Th4:
  s <> t & s is Point of Tdisk(x,r) & s is not Point of Tcircle(x,r) implies
  ex e being Point of Tcircle(x,r) st {e} = halfline(s,t) /\ Sphere(x,r)
proof
  assume that
A1: s <> t and
A2: s is Point of Tdisk(x,r) and
A3: s is not Point of Tcircle(x,r);
A4: the carrier of Tcircle(x,r) = Sphere(x,r) by TOPREALB:9;
A5: the carrier of Tdisk(x,r) = cl_Ball(x,r) by Th3;
A6: |. s-x .| <> r by A3,A4,TOPREAL9:9;
  reconsider S = s, T = t, X = x as Element of REAL n by EUCLID:25;
  set a = (-(2*|(t-s,s-x)|) + sqrt delta
  (Sum sqr (T-S), 2 * |(t-s,s-x)|, Sum sqr (S-X) - r^2)) / (2 * Sum sqr (T-S));
  |. s-x .| <= r by A2,A5,TOPREAL9:8;
  then |. s-x .| < r by A6,REAL_1:def 5;
  then s in Ball(x,r) by TOPREAL9:7;
  then consider e1 being Point of TOP-REAL n such that
A7: {e1} = halfline(s,t) /\ Sphere(x,r) and
    e1 = (1-a)*s + a*t by A1,TOPREAL9:37;
e1 in {e1} by TARSKI:def 1;
  then e1 in Sphere(x,r) by A7,XBOOLE_0:def 3;
  hence thesis by A4,A7;
end;

theorem Th5:
  s <> t & s in the carrier of Tcircle(x,r) & t is Point of Tdisk(x,r) implies
   ex e being Point of Tcircle(x,r) st e <> s &
     {s,e} = halfline(s,t) /\ Sphere(x,r)
proof
  assume that
A1: s <> t and
A2: s in the carrier of Tcircle(x,r) and
A3: t is Point of Tdisk(x,r);
A4: the carrier of Tcircle(x,r) = Sphere(x,r) by TOPREALB:9;
  reconsider S = 1/2*s + 1/2*t, T = t, X = x as Element of REAL n by EUCLID:25;
  set a = (-(2*|(t-(1/2*s + 1/2*t),1/2*s + 1/2*t-x)|) + sqrt delta
      (Sum sqr (T-S), 2 * |(t-(1/2*s + 1/2*t),1/2*s + 1/2*t-x)|,
      Sum sqr (S-X) - r^2)) / (2 * Sum sqr (T-S));
  the carrier of Tdisk(x,r) = cl_Ball(x,r) by Th3;
  then consider e1 being Point of TOP-REAL n such that
A5:  e1 <> s and
A6:  {s,e1} = halfline(s,t) /\ Sphere(x,r) and
     (t in Sphere(x,r) implies e1 = t) &
     (not t in Sphere(x,r) &
      a = (-(2*|(t-(1/2*s + 1/2*t),1/2*s + 1/2*t-x)|) + sqrt delta
      (Sum sqr (T-S), 2 * |(t-(1/2*s + 1/2*t),1/2*s + 1/2*t-x)|,
      Sum sqr (S-X) - r^2)) / (2 * Sum sqr (T-S))
      implies e1 = (1-a)*(1/2*s + 1/2*t) + a*t) by A3,A1,A4,A2,TOPREAL9:38;
  e1 in {s,e1} & s in {s,e1} by TARSKI:def 2;
  then e1 in Sphere(x,r) & s in Sphere(x,r) by A6,XBOOLE_0:def 3;
  hence thesis by A4,A5,A6;
end;

definition
  let n be non empty Element of NAT, o be Point of TOP-REAL n,
      s, t be Point of TOP-REAL n,
      r be non negative (real number);
  assume that
A1: s is Point of Tdisk(o,r) and
A2: t is Point of Tdisk(o,r) and
A3: s <> t;
  func HC(s,t,o,r) -> Point of TOP-REAL n means :Def3:
  it in halfline(s,t) /\ Sphere(o,r) & it <> s;
existence
proof
  per cases;
  suppose
A4: s is Point of Tcircle(o,r);
  consider e being Point of Tcircle(o,r) such that
A5: e <> s &
    {s,e} = halfline(s,t) /\ Sphere(o,r) by A2,A3,A4,Th5;
  reconsider e as Point of TOP-REAL n by TOPMETR:2;
  take e;
  thus thesis by A5,TARSKI:def 2;
  end;
  suppose
A6: s is not Point of Tcircle(o,r);
  consider e1 being Point of Tcircle(o,r) such that
A7: {e1} = halfline(s,t) /\ Sphere(o,r) by A6,A3,A1,Th4;
  reconsider e1 as Point of TOP-REAL n by TOPMETR:2;
  take e1;
  thus thesis by A7,A6,TARSKI:def 1;
  end;
end;
uniqueness
proof
  let m, u be Point of TOP-REAL n such that
A8:  m in halfline(s,t) /\ Sphere(o,r) & m <> s and
A9:  u in halfline(s,t) /\ Sphere(o,r) & u <> s;
  per cases;
  suppose
A10: s is Point of Tcircle(o,r);
  consider f1 being Point of Tcircle(o,r) such that
    f1 <> s and
A11: {s,f1} = halfline(s,t) /\ Sphere(o,r) by A2,A3,A10,Th5;
  per cases by A8,A9,A11,TARSKI:def 2;
  suppose m = f1 & u = f1;
  hence m = u;
  end;
  suppose m = f1 & u = s;
  hence m = u by A9;
  end;
  suppose m = s & u = f1;
  hence m = u by A8;
  end;
  suppose m = s & u = s;
  hence m = u;
  end;
  end;
  suppose
A12: s is not Point of Tcircle(o,r);
  consider e being Point of Tcircle(o,r) such that
A13: {e} = halfline(s,t) /\ Sphere(o,r) by A12,A1,A3,Th4;
  m = e by A8,A13,TARSKI:def 1;
  hence m = u by A9,A13,TARSKI:def 1;
  end;
end;
end;

reserve
   n for non empty Element of NAT,
   s, t, o for Point of TOP-REAL n;

theorem Th6:
  s is Point of Tdisk(o,r) & t is Point of Tdisk(o,r) & s <> t implies
  HC(s,t,o,r) is Point of Tcircle(o,r)
proof
  assume
A1: s is Point of Tdisk(o,r) & t is Point of Tdisk(o,r) & s <> t;
A2: the carrier of Tcircle(o,r) = Sphere(o,r) by TOPREALB:9;
  HC(s,t,o,r) in halfline(s,t) /\ Sphere(o,r) by A1,Def3;
  hence thesis by A2,XBOOLE_0:def 3;
end;

theorem
  for S, T, O being Element of REAL n st S = s & T = t & O = o holds
  s is Point of Tdisk(o,r) & t is Point of Tdisk(o,r) & s <> t &
  a = (-|(t-s,s-o)| + sqrt(|(t-s,s-o)|^2-(Sum sqr (T-S))*(Sum sqr (S-O)-r^2)))
      / Sum sqr (T-S)
  implies HC(s,t,o,r) = (1-a)*s+a*t
proof
  let S, T, O be Element of REAL n such that
A1: S = s & T = t & O = o and
A2: s is Point of Tdisk(o,r) and
A3: t is Point of Tdisk(o,r) and
A4: s <> t;
  set H = HC(s,t,o,r);
  set M = |(t-s,s-o)|;
  set A = Sum sqr (T-S);
  set B = 2*M;
  set C = Sum sqr (S-O) - r^2;
  set l1 = (- B - sqrt delta(A,B,C)) / (2 * A);
  set l2 = (- B + sqrt delta(A,B,C)) / (2 * A);
  assume
A5: a = (-M+sqrt(M^2-A*C)) / A;
A6: H in halfline(s,t) /\ Sphere(o,r) by A2,A3,A4,Def3;
  then H in halfline(s,t) by XBOOLE_0:def 3;
  then consider l being real number such that
A7: H = (1-l)*s + l*t and
A8: 0 <= l by TOPREAL9:26;
A9: H = 1 * s -l*s + l*t by A7,EUCLID:54
     .= s -l*s + l*t by EUCLID:33
     .= s+l*t-l*s by JORDAN2C:9
     .= s+(l*t-l*s) by EUCLID:49
     .= s+l*(t-s) by EUCLID:53;
A10: |. T-S .| <> 0 by A1,A4,EUCLID:19;
A11:now
    assume A <= 0;
    then A = 0 by RVSUM_1:116;
    hence contradiction by A10,EUCLID:def 5,SQUARE_1:82;
  end;
A12: delta(A,B,C) = B^2 - 4*A*C by QUIN_1:def 1
    .= 4*(M^2-A*C);
  the carrier of Tdisk(o,r) = cl_Ball(o,r) by Th3;
  then
A13: |. s-o .| <= r by A2,TOPREAL9:8;
A14: |. S-O .| = sqrt Sum sqr (S-O) by EUCLID:def 5;
A17: |. T-S .| = sqrt Sum sqr (T-S) by EUCLID:def 5;
  Sum sqr (T-S) >= 0 by RVSUM_1:116;
  then
A18: |. T-S .|^2 = Sum sqr (T-S) by A17,SQUARE_1:def 4;
  Sum sqr (S-O) >= 0 by RVSUM_1:116;
  then
A19: |. S-O .|^2 = Sum sqr (S-O) by A14,SQUARE_1:def 4;
A21: (sqrt Sum sqr (S-O))^2 <= r^2 by A1,A13,A14,SQUARE_1:77;
  then
A22: C <= 0 by A14,A19,REAL_2:106;
A23: delta(A,B,C) = B^2 - 4*A*C by QUIN_1:def 1;
  4*A > 0 by A11,XREAL_1:131;
  then
A24: 4*A*C <= 0 by A22,XREAL_1:133;
A25: B^2 - 4*A*C >= 0 by A24,REAL_2:174;
  l is Real by XREAL_0:def 1;
  then
A26: |. l*(t-s) .|^2 = (abs(l)*|. t-s .|)^2 by TOPRNS_1:8
   .= (abs(l))^2 * |. t-s .|^2
   .= l^2 * |. t-s .|^2 by COMPLEX1:161;
  H in Sphere(o,r) by A6,XBOOLE_0:def 3;
  then r = |. (1-l)*s+l*t - o .| by A7,TOPREAL9:9
    .= |. 1 * s - l*s + l*t - o .| by EUCLID:54
    .= |. s - l*s + l*t - o .| by EUCLID:33
    .= |. s - (l*s - l*t) - o .| by EUCLID:51
    .= |. s-o - (l*s - l*t) .| by TOPREAL9:1
    .= |. s-o - l*(s-t) .| by EUCLID:53
    .= |. s-o +- l*(s-t) .| by EUCLID:45
    .= |. s-o + l*(-(s-t)) .| by EUCLID:44
    .= |. s-o + l*(t-s) .| by EUCLID:48;
  then r^2 = |. s-o .|^2 + 2*|(l*(t-s),s-o)| + |. l*(t-s) .|^2 by EUCLID_2:67
    .= |. s-o .|^2 + 2*(l*|(t-s,s-o)|) + |. l*(t-s) .|^2 by EUCLID_2:41;
  then A*l^2+B*l+C = 0 by A1,A18,A19,A26;
  then
A28: Polynom(A,B,C,l) = 0 by POLYEQ_1:def 2;
  A*C <= 0 by A11,A22,XREAL_1:133;
  then
A29: -0 <= -A*C by XREAL_1:26;
  0+0 <= M^2+-A*C by A29,XREAL_1:9;
  then
A30: l2 = (-B+sqrt(4)*sqrt(M^2-A*C)) / (2*A) by A12,SQUARE_1:97
    .= (2*(-(M-sqrt(M^2-A*C)))) / (2*A) by SQUARE_1:85
    .= a by A5,XCMPLX_1:92;
A31: l = l1 or l = l2 by A11,A23,A28,A25,POLYEQ_1:5;
   now
    let x be real number;
    thus Polynom(A,B,C,x) = A*x^2+B*x+C by POLYEQ_1:def 2
      .= A*(x-l1)*(x-l2) by A11,A23,A25,QUIN_1:16
      .= A*((x-l1)*(x-l2))
      .= Quard(A,l1,l2,x) by POLYEQ_1:def 3;
  end;
  then
A32: C/A = l1*l2 by A11,POLYEQ_1:11;
A33: 2*A > 0 by A11,XREAL_1:131;
A34: now
    set D = sqrt delta(A,B,C);
    assume l1 > l2;
    then -D - B > D - B by A33,REAL_1:73;
    then -D > D by XREAL_1:11;
    then
A35: -D+D > D+D by XREAL_1:8;
    0 <= D by A23,A25,SQUARE_1:def 4;
    then 0+0 <= D+D by XREAL_1:9;
    hence contradiction by A35;
  end;
  per cases by A21,A14,A19,REAL_2:106;
  suppose C < 0;
  then C/A < 0 by A11,REAL_2:128;
  hence H = (1-a)*s+a*t by A34,A31,A30,A8,A7,A32,XREAL_1:135;
  end;
  suppose l1 = l2;
  hence H = (1-a)*s+a*t by A31,A30,A7;
  end;
  suppose
A36:  C = 0;
  now
    assume
A37:   l = l1;
A38: now
      assume l = 0;
      then H = s+0.REAL n by A9,EUCLID:33
      .= s by EUCLID:31;
      hence contradiction by A2,A3,A4,Def3;
    end;
    per cases;
    suppose
A39:   0 < B;
    then
A40: (-2)*B < 0 by XREAL_1:134;
    l1 = (-B-B) / (2*A) by A39,A36,A23,SQUARE_1:89;
    hence contradiction by A37,A40,A8,A33,REAL_2:128;
    end;
    suppose
A41:   B <= 0;
    l1 = (-B--B) / (2*A) by A41,A36,A23,SQUARE_1:90
    .= 0;
    hence contradiction by A37,A38;
    end;
  end;
  hence H = (1-a)*s+a*t by A31,A30,A7;
  end;
end;

theorem Th8:
  for r1, r2, s1, s2 being real number,
      s, t, o being Point of TOP-REAL 2 holds
  s is Point of Tdisk(o,r) & t is Point of Tdisk(o,r) & s <> t &
  r1 = t`1-s`1 & r2 = t`2-s`2 & s1 = s`1-o`1 & s2 = s`2-o`2 &
  a = (-(s1*r1+s2*r2)+sqrt((s1*r1+s2*r2)^2-(r1^2+r2^2)*(s1^2+s2^2-r^2)))
       / (r1^2+r2^2) implies
  HC(s,t,o,r) = |[ s`1+a*r1, s`2+a*r2 ]|
proof
  let r1, r2, s1, s2 be real number,
      s, t, o be Point of TOP-REAL 2 such that
A1: s is Point of Tdisk(o,r) and
A2: t is Point of Tdisk(o,r) and
A3: s <> t and
A4: r1 = t`1-s`1 and
A5: r2 = t`2-s`2 and
A6: s1 = s`1-o`1 and
A7: s2 = s`2-o`2 and
A8: a = (-(s1*r1+s2*r2)+sqrt((s1*r1+s2*r2)^2-(r1^2+r2^2)*(s1^2+s2^2-r^2)))
       / (r1^2+r2^2);
  set H = HC(s,t,o,r);
  set M = s1*r1+s2*r2;
  set A = r1^2+r2^2;
  set B = 2*M;
  set C = s1^2+s2^2-r^2;
  set l1 = (- B - sqrt delta(A,B,C)) / (2 * A);
  set l2 = (- B + sqrt delta(A,B,C)) / (2 * A);
A9: H in halfline(s,t) /\ Sphere(o,r) by A1,A2,A3,Def3;
  then H in halfline(s,t) by XBOOLE_0:def 3;
  then consider l being real number such that
A10: H = (1-l)*s + l*t and
A11: 0 <= l by TOPREAL9:26;
A12: H = 1 * s -l*s + l*t by A10,EUCLID:54
     .= s -l*s + l*t by EUCLID:33
     .= s+l*t-l*s by JORDAN2C:9
     .= s+(l*t-l*s) by EUCLID:49
     .= s+l*(t-s) by EUCLID:53;
A13: H`1 = s`1+(l*(t-s))`1 by A12,TOPREAL3:7
       .= s`1+l*(t-s)`1 by TOPREAL3:9
       .= s`1+l*(t`1-s`1) by TOPREAL3:8;
A14: H`2 = s`2+(l*(t-s))`2 by A12,TOPREAL3:7
     .= s`2+l*(t-s)`2 by TOPREAL3:9
     .= s`2+l*(t`2-s`2) by TOPREAL3:8;
  r1 <> 0 or r2 <> 0 by A4,A5,A3,TOPREAL3:11;
  then (0 < r1^2 or 0 < r2^2) & 0 <= r1^2 & 0 <= r2^2 by SQUARE_1:74;
  then
A15: 0+0 < A by XREAL_1:10;
A16: delta(A,B,C) = B^2 - 4*A*C by QUIN_1:def 1
    .= 4*(M^2-A*C);
A17: delta(A,B,C) = B^2 - 4*A*C by QUIN_1:def 1;
  H in Sphere(o,r) by A9,XBOOLE_0:def 3;
  then |. H-o .| = r by TOPREAL9:9;
  then r^2 = ((H-o)`1)^2 + ((H-o)`2)^2 by JGRAPH_1:46
    .= (H`1-o`1)^2 + ((H-o)`2)^2 by TOPREAL3:8
    .= (H`1-o`1)^2 + (H`2-o`2)^2 by TOPREAL3:8
    .= ((1-l)*s`1+l*t`1-o`1)^2 + (H`2-o`2)^2 by A10,TOPREAL9:41
    .= ((1-l)*s`1+l*t`1-o`1)^2 + ((1-l)*s`2+l*t`2-o`2)^2 by A10,TOPREAL9:42
    .= l^2*(r1^2+r2^2)+l*2*M+s1^2+s2^2 by A4,A5,A6,A7;
  then A*l^2+B*l+C = 0;
  then
A18: Polynom(A,B,C,l) = 0 by POLYEQ_1:def 2;
  the carrier of Tdisk(o,r) = cl_Ball(o,r) by Th3;
  then |. s-o .| <= r by A1,TOPREAL9:8;
  then
A20: |. s-o .|^2 <= r^2 by SQUARE_1:77;
  |. s-o .|^2 = ((s-o)`1)^2+((s-o)`2)^2 by JGRAPH_1:46
    .= s1^2+((s-o)`2)^2 by A6,TOPREAL3:8
    .= s1^2+s2^2 by A7,TOPREAL3:8;
  then
A21: C <= r^2-r^2 by A20,XREAL_1:11;
  then A*C <= 0 by A15,XREAL_1:133;
  then
A22: -0 <= -A*C by XREAL_1:26;
  0+0 <= M^2+-A*C by A22,XREAL_1:9;
  then
A23: l2 = (-B+sqrt(4)*sqrt(M^2-A*C)) / (2*A) by A16,SQUARE_1:97
    .= (2*(-(M-sqrt(M^2-A*C)))) / (2*A) by SQUARE_1:85
    .= a by A8,XCMPLX_1:92;
  4*A > 0 by A15,XREAL_1:131;
  then
A24: 4*A*C <= 0 by A21,XREAL_1:133;
A25: B^2 - 4*A*C >= 0 by A24,REAL_2:174;
  then
A26: l = l1 or l = l2 by A15,A17,A18,POLYEQ_1:5;
  for x being real number holds Polynom(A,B,C,x) = Quard(A,l1,l2,x)
  proof
    let x be real number;
    thus Polynom(A,B,C,x) = A*x^2+B*x+C by POLYEQ_1:def 2
      .= A*(x-l1)*(x-l2) by A15,A17,A25,QUIN_1:16
      .= A*((x-l1)*(x-l2))
      .= Quard(A,l1,l2,x) by POLYEQ_1:def 3;
  end;
  then
A27: C/A = l1*l2 by A15,POLYEQ_1:11;
A28: 2*A > 0 by A15,XREAL_1:131;
A29: now
    set D = sqrt delta(A,B,C);
    assume l1 > l2;
    then -D - B > D - B by A28,REAL_1:73;
    then -D > D by XREAL_1:11;
    then
A30: -D+D > D+D by XREAL_1:8;
    0 <= D by A17,A25,SQUARE_1:def 4;
    then 0+0 <= D+D by XREAL_1:9;
    hence contradiction by A30;
  end;
  per cases by A21;
  suppose C < 0;
  then C/A < 0 by A15,REAL_2:128;
  hence H = |[ s`1+a*r1, s`2+a*r2 ]|
    by A29,A26,A23,A11,A27,A13,A14,A4,A5,EUCLID:57,XREAL_1:135;
  end;
  suppose l1 = l2;
  hence H = |[ s`1+a*r1, s`2+a*r2 ]| by A13,A26,A23,A14,A4,A5,EUCLID:57;
  end;
  suppose
A31:  C = 0;
  now
    assume
A32:   l = l1;
A33: now
      assume l = 0;
      then H = s+0.REAL 2 by A12,EUCLID:33
      .= s by EUCLID:31;
      hence contradiction by A1,A2,A3,Def3;
    end;
    per cases;
    suppose
A34:   0 < B;
    then
A35: (-2)*B < 0 by XREAL_1:134;
    l1 = (-B-B) / (2*A) by A34,A31,A17,SQUARE_1:89;
    hence contradiction by A32,A35,A11,A28,REAL_2:128;
    end;
    suppose
A36:   B <= 0;
    l1 = (-B--B) / (2*A) by A36,A31,A17,SQUARE_1:90
    .= 0;
    hence contradiction by A32,A33;
    end;
  end;
  hence H = |[ s`1+a*r1, s`2+a*r2 ]| by A13,A14,A4,A5,A26,A23,EUCLID:57;
  end;
end;

definition
  let n be non empty Element of NAT, o be Point of TOP-REAL n,
      r be non negative (real number);
  let x be Point of Tdisk(o,r);
  let f be Function of Tdisk(o,r), Tdisk(o,r) such that
A1:   not x is_a_fixpoint_of f;
  func HC(x,f) -> Point of Tcircle(o,r) means :Def4:
  ex y, z being Point of TOP-REAL n st y = x & z = f.x & it = HC(z,y,o,r);
existence
proof
  reconsider y = x, z = f.x as Point of TOP-REAL n by TOPMETR:2;
  x <> f.x by A1,ABIAN:def 4;
  then reconsider a = HC(z,y,o,r) as Point of Tcircle(o,r) by Th6;
  take a;
  thus thesis;
end;
uniqueness;
end;

theorem Th9:
  for x being Point of Tdisk(o,r),
      f being Function of Tdisk(o,r), Tdisk(o,r) st not x is_a_fixpoint_of f &
   x is Point of Tcircle(o,r) holds HC(x,f) = x
proof
  let x be Point of Tdisk(o,r);
  let f be Function of Tdisk(o,r), Tdisk(o,r) such that
A1:  not x is_a_fixpoint_of f and
A2:  x is Point of Tcircle(o,r);
A3: x <> f.x by A1,ABIAN:def 4;
  consider y, z being Point of TOP-REAL n such that
A4:  y = x & z = f.x & HC(x,f) = HC(z,y,o,r) by A1,Def4;
A5: x in halfline(z,y) by A4,TOPREAL9:28;
  the carrier of Tcircle(o,r) = Sphere(o,r) by TOPREALB:9;
  then x in halfline(z,y) /\ Sphere(o,r) by A2,A5,XBOOLE_0:def 3;
  hence HC(x,f) = x by A4,A3,Def3;
end;

theorem Th10:
  for r being positive (real number),
      o being Point of TOP-REAL 2,
      Y being non empty SubSpace of Tdisk(o,r) st Y = Tcircle(o,r)
  holds not Y is_a_retract_of Tdisk(o,r)
proof
  let r be positive (real number),
      o be Point of TOP-REAL 2,
      Y be non empty SubSpace of Tdisk(o,r) such that
A1: Y = Tcircle(o,r);
  set X = Tdisk(o,r);
  given R being continuous Function of X,Y such that
A2: R is_a_retraction;
A3: the carrier of Tcircle(o,r) = Sphere(o,r) by TOPREALB:9;
A4: Sphere(o,r) c= cl_Ball(o,r) by TOPREAL9:17;
  consider y0 being Point of Y;
  y0 in the carrier of Y;
  then reconsider x0 = y0 as Point of X by A1,A3,A4,Th3;
  consider C being constant Loop of x0;
A5: C = I[01] --> x0 by BORSUK_2:6
   .= (the carrier of I[01]) --> y0;
  then reconsider D = C as Function of I[01], Y;
A6: D = I[01] --> y0 by A5;
  reconsider a0 = 0, a1 = 1 as Point of I[01] by BORSUK_1:def 17,def 18;
A7: y0,y0 are_connected;
  D.a0 = y0 & D.a1 = y0 by A5,FUNCOP_1:13;
  then reconsider D as constant Loop of y0 by A6,A7,BORSUK_2:def 2;
  reconsider z0 = y0 as Point of Tcircle(o,r) by A1;
A8: the carrier of pi_1(Tcircle(o,r),z0) is infinite;
  the carrier of pi_1(Y,y0) = { Class(EqRel(Y,y0),D) }
  proof
    set E = EqRel(Y,y0);
    hereby
      let x be set;
      assume x in the carrier of pi_1(Y,y0);
      then consider f0 being Loop of y0 such that
A9:     x = Class(E,f0) by TOPALG_1:48;
      reconsider g0 = f0 as Loop of x0 by TOPALG_2:1;
      g0,C are_homotopic by TOPALG_2:2;
      then consider f being Function of [:I[01],I[01]:], X such that
A10:    f is continuous and
A11:    for s being Point of I[01] holds f.(s,0) = g0.s & f.(s,1) = C.s &
       for t being Point of I[01] holds f.(0,t) = x0 & f.(1,t) = x0
         by BORSUK_2:def 7;
      f0,D are_homotopic
      proof
        take F = R*f;
        thus F is continuous by A10,TOPS_2:58;
        let s be Point of I[01];
        thus F.(s,0) = F. [s,a0]
          .= R.(f.(s,0)) by FUNCT_2:21
          .= R.(g0.s) by A11
          .= f0.s by A2,BORSUK_1:def 19;
        thus F.(s,1) = F. [s,a1]
          .= R.(f.(s,1)) by FUNCT_2:21
          .= R.(C.s) by A11
          .= D.s by A2,BORSUK_1:def 19;
        thus F.(0,s) = F. [a0,s]
          .= R.(f.(0,s)) by FUNCT_2:21
          .= R.x0 by A11
          .= y0 by A2,BORSUK_1:def 19;
        thus F.(1,s) = F. [a1,s]
          .= R.(f.(1,s)) by FUNCT_2:21
          .= R.x0 by A11
          .= y0 by A2,BORSUK_1:def 19;
      end;
      then x = Class(E,D) by A9,TOPALG_1:47;
      hence x in { Class(E,D) } by TARSKI:def 1;
    end;
    let x be set;
    assume x in { Class(E,D) };
    then
A12:   x = Class(E,D) by TARSKI:def 1;
    D in Loops y0 by TOPALG_1:def 1;
    then x in Class E by A12,EQREL_1:def 5;
    hence thesis by TOPALG_1:def 3;
  end;
  hence contradiction by A1,A8;
end;

definition
  let n be non empty Element of NAT; let r be non negative (real number);
  let o be Point of TOP-REAL n;
  let f be Function of Tdisk(o,r), Tdisk(o,r);
  func BR-map(f) -> Function of Tdisk(o,r), Tcircle(o,r) means :Def5:
  for x being Point of Tdisk(o,r) holds it.x = HC(x,f);
existence
proof
  defpred P[Point of Tdisk(o,r),set] means $2 = HC($1,f);
A1: for x being Point of Tdisk(o,r)
     ex m being Point of Tcircle(o,r) st P[x,m];
  ex h being Function of Tdisk(o,r), Tcircle(o,r) st
   for x being Point of Tdisk(o,r) holds P[x,h.x] from FUNCT_2:sch 3(A1);
  hence thesis;
end;
uniqueness
proof
  let f1, f2 be Function of Tdisk(o,r), Tcircle(o,r) such that
A2:for x being Point of Tdisk(o,r) holds f1.x = HC(x,f) and
A3:for x being Point of Tdisk(o,r) holds f2.x = HC(x,f);
  for x being Point of Tdisk(o,r) holds f1.x = f2.x
  proof
    let x be Point of Tdisk(o,r);
    thus f1.x = HC(x,f) by A2
      .= f2.x by A3;
  end;
  hence thesis by FUNCT_2:113;
end;
end;

theorem Th11:
  for o being Point of TOP-REAL n,
      x being Point of Tdisk(o,r),
      f being Function of Tdisk(o,r), Tdisk(o,r) st not x is_a_fixpoint_of f &
   x is Point of Tcircle(o,r) holds (BR-map(f)).x = x
proof
  let o be Point of TOP-REAL n;
  let x be Point of Tdisk(o,r);
  let f be Function of Tdisk(o,r), Tdisk(o,r) such that
A1:  not x is_a_fixpoint_of f and
A2:  x is Point of Tcircle(o,r);
  thus (BR-map(f)).x = HC(x,f) by Def5
    .= x by A1,A2,Th9;
end;

theorem
  for f being continuous Function of Tdisk(o,r), Tdisk(o,r) holds
  f has_no_fixpoint implies (BR-map(f)) | Sphere(o,r) = id Tcircle(o,r)
proof
  let f be continuous Function of Tdisk(o,r), Tdisk(o,r) such that
A1: f has_no_fixpoint;
  set g = BR-map(f);
  set C = Sphere(o,r);
  set D = cl_Ball(o,r);
A2: dom g = the carrier of Tdisk(o,r) by FUNCT_2:def 1;
A3: the carrier of Tdisk(o,r) = D by Th3;
  C c= D by TOPREAL9:17;
  then
A4: dom (g|C) = C by A2,A3,RELAT_1:91;
A5: dom id Tcircle(o,r) = the carrier of Tcircle(o,r) by RELAT_1:71;
A6: the carrier of Tcircle(o,r) = C by TOPREALB:9;
  for x being set st x in dom (g|C) holds (g|C).x = (id Tcircle(o,r)).x
  proof
    let x be set such that
A7:  x in dom (g|C);
    reconsider y = x as Point of Tdisk(o,r) by A7;
A8: not x is_a_fixpoint_of f by A1,ABIAN:def 5;
    thus (g|C).x = g.x by A4,A7,FUNCT_1:72
      .= y by A4,A7,A8,A6,Th11
      .= (id Tcircle(o,r)).x by A4,A7,A6,FUNCT_1:35;
  end;
  hence thesis by A4,A6,A5,FUNCT_1:9;
end;

Lm1:
now
  let T be non empty TopSpace;
  let a be Real;
  set c = the carrier of T;
  set f = c --> a;
  thus f is continuous
  proof
A1: dom f = c by FUNCT_2:def 1;
A2: rng f = {a} by FUNCOP_1:14;
  let Y be Subset of REAL; assume Y is closed;
  per cases;
  suppose a in Y;
  then
A3: rng f c= Y by A2,ZFMISC_1:37;
  f"Y = f"(rng f /\ Y) by RELAT_1:168
     .= f"rng f by A3,XBOOLE_1:28
     .= [#]T by A1,RELAT_1:169;
  hence f"Y is closed;
  end;
  suppose not a in Y;
  then
A4: rng f misses Y by A2,ZFMISC_1:56;
  f"Y = f"(rng f /\ Y) by RELAT_1:168
     .= f"{} by A4,XBOOLE_0:def 7
     .= {}T by RELAT_1:171;
  hence f"Y is closed;
  end;
end;
end;

theorem Th13:
  for r being positive (real number),
      o being Point of TOP-REAL 2,
      f being continuous Function of Tdisk(o,r), Tdisk(o,r) holds
   f has_no_fixpoint implies BR-map(f) is continuous
proof
  let r be positive (real number),
      o be Point of TOP-REAL 2;
  set D2 = Tdisk(o,r);
  set S1 = Tcircle(o,r);
  set OK = DiffElems(T2,T2) /\ the carrier of [:D2,D2:];
  consider s being Point of S1;
A1: s in the carrier of S1;
A2: the carrier of S1 = Sphere(o,r) by TOPREALB:9;
A3: the carrier of D2 = cl_Ball(o,r) by Th3;
  Sphere(o,r) c= cl_Ball(o,r) by TOPREAL9:17;
  then
A4: s is Point of D2 by A2,A1,Th3;
A5: s is Point of T2 by TOPMETR:2;
A6: |. o-o .| = |. 0.REAL 2 .| by EUCLID:46
  .= 0 by TOPRNS_1:24;
  then o is Point of D2 by A3,TOPREAL9:8;
  then
  [o,s] in [:the carrier of D2,the carrier of D2:] by A4,ZFMISC_1:106;
  then
A7: [o,s] in the carrier of [:D2,D2:] by BORSUK_1:def 5;
  now
    assume
A8:   o = s;
A9: Ball(o,r) misses Sphere(o,r) by TOPREAL9:19;
    o in Ball(o,r) by A6,TOPREAL9:7;
    hence contradiction by A8,A2,A9,XBOOLE_0:3;
  end;
  then [o,s] in DiffElems(T2,T2) by A5;
  then reconsider OK as non empty Subset of [:T2,T2:] by A7,XBOOLE_0:def 3;
  set TD = [:T2,T2:] | OK;
  let f be continuous Function of D2,D2 such that
A10: f has_no_fixpoint;
  set g = BR-map(f);
  defpred xo[set,set] means ex x1, x2 being Point of T2 st
    $1 = [x1,x2] & $2 = x2`1 - o`1;
A11: for x being set st x in the carrier of [:T2,T2:]
     ex r being Real st xo[x,r]
  proof
    let x be set;
    assume x in the carrier of [:T2,T2:];
    then consider x1, x2 being Point of T2 such that
A12:   x = [x1,x2] by BORSUK_1:50;
    take x2`1 - o`1, x1, x2;
    thus thesis by A12;
  end;
  consider xo being RealMap of [:T2,T2:] such that
A13: for x being Point of [:T2,T2:] holds xo[x,xo.x] from PSCOMP_1:sch 1(A11);
  defpred yo[set,set] means ex x1, x2 being Point of T2 st
    $1 = [x1,x2] & $2 = x2`2 - o`2;
A14: for x being set st x in the carrier of [:T2,T2:]
     ex r being Real st yo[x,r]
  proof
    let x be set;
    assume x in the carrier of [:T2,T2:];
    then consider x1, x2 being Point of T2 such that
A15:   x = [x1,x2] by BORSUK_1:50;
    take x2`2 - o`2, x1, x2;
    thus thesis by A15;
  end;
  consider yo being RealMap of [:T2,T2:] such that
A16: for x being Point of [:T2,T2:] holds yo[x,yo.x] from PSCOMP_1:sch 1(A14);
  defpred dx[set,set] means ex x1, x2 being Point of T2 st
    $1 = [x1,x2] & $2 = x1`1 - x2`1;
A17: for x being set st x in the carrier of [:T2,T2:]
     ex r being Real st dx[x,r]
  proof
    let x be set;
    assume x in the carrier of [:T2,T2:];
    then consider x1, x2 being Point of T2 such that
A18:   x = [x1,x2] by BORSUK_1:50;
    take x1`1 - x2`1, x1, x2;
    thus thesis by A18;
  end;
  consider dx being RealMap of [:T2,T2:] such that
A19: for x being Point of [:T2,T2:] holds dx[x,dx.x] from PSCOMP_1:sch 1(A17);
  defpred dy[set,set] means ex y1, y2 being Point of T2 st
    $1 = [y1,y2] & $2 = y1`2 - y2`2;
A20: for x being set st x in the carrier of [:T2,T2:] ex r being Real st dy[x,r
]
  proof
    let x be set;
    assume x in the carrier of [:T2,T2:];
    then consider x1, x2 being Point of T2 such that
A21:   x = [x1,x2] by BORSUK_1:50;
    take x1`2 - x2`2, x1, x2;
    thus thesis by A21;
  end;
  consider dy being RealMap of [:T2,T2:] such that
A22: for y being Point of [:T2,T2:] holds dy[y,dy.y] from PSCOMP_1:sch 1(A20);
  defpred fx2[set,set] means ex x1, x2 being Point of T2 st
    $1 = [x1,x2] & $2 = x2`1;
A23: for x being set st x in the carrier of [:T2,T2:]
      ex r being Real st fx2[x,r]
  proof
    let x be set;
    assume x in the carrier of [:T2,T2:];
    then consider x1, x2 being Point of T2 such that
A24:   x = [x1,x2] by BORSUK_1:50;
    take x2`1, x1, x2;
    thus thesis by A24;
  end;
  consider fx2 being RealMap of [:T2,T2:] such that
A25: for x being Point of [:T2,T2:] holds fx2[x,fx2.x] from PSCOMP_1:sch 1(A23)
;
  defpred fy2[set,set] means ex x1, x2 being Point of T2 st
    $1 = [x1,x2] & $2 = x2`2;
A26: for x being set st x in the carrier of [:T2,T2:]
     ex r being Real st fy2[x,r]
  proof
    let x be set;
    assume x in the carrier of [:T2,T2:];
    then consider x1, x2 being Point of T2 such that
A27:   x = [x1,x2] by BORSUK_1:50;
    take x2`2, x1, x2;
    thus thesis by A27;
  end;
  consider fy2 being RealMap of [:T2,T2:] such that
A28: for x being Point of [:T2,T2:] holds fy2[x,fy2.x] from PSCOMP_1:sch 1(A26)
;
  reconsider Dx = dx, Dy = dy, fX2 = fx2, fY2 = fy2, Xo = xo, Yo = yo
    as Function of [:T2,T2:],R^1 by TOPMETR:24;
  for p being Point of [:T2,T2:], V being Subset of R^1
   st Xo.p in V & V is open holds
    ex W being Subset of [:T2,T2:] st p in W & W is open & Xo.:W c= V
  proof
    let p be Point of [:T2,T2:],
        V be Subset of R^1 such that
A29:   Xo.p in V and
A30:   V is open;
    consider p1, p2 being Point of T2 such that
A31:   p = [p1,p2] and
A32:   Xo.p = p2`1 - o`1 by A13;
    set r = p2`1 - o`1;
    reconsider V1 = V as open Subset of REAL by A30,TOPMETR:24,BORSUK_5:62;
    consider g being real number such that
A33:   0 < g and
A34:   ].r-g,r+g.[ c= V1 by A29,A32,RCOMP_1:40;
    reconsider g as Element of REAL by XREAL_0:def 1;
    set W2 = {|[x,y]| where x, y is Element of REAL: p2`1-g < x & x < p2`1+g};
    W2 c= the carrier of T2
    proof
      let a be set;
      assume a in W2;
      then ex x, y being Element of REAL st
        a = |[x,y]| & p2`1-g < x & x < p2`1+g;
      hence thesis;
    end;
    then reconsider W2 as Subset of T2;
    take [:[#]T2,W2:];
A35: p2 = |[p2`1,p2`2]| by EUCLID:57;
    p2`1-g < p2`1-0 & p2`1+0 < p2`1+g by A33,XREAL_1:8,REAL_1:92;
    then
    p2 in W2 by A35;
    hence p in [:[#]T2,W2:] by A31,ZFMISC_1:def 2;
    W2 is open by PSCOMP_1:66;
    hence [:[#]T2,W2:] is open by BORSUK_1:46;
    let b be set;
    assume b in Xo.:[:[#]T2,W2:];
    then consider a being Point of [:T2,T2:] such that
A36:  a in [:[#]T2,W2:] and
A37:  Xo.a = b by FUNCT_2:116;
    consider a1, a2 being Point of T2 such that
A38:  a = [a1,a2] and
A39:  xo.a = a2`1 - o`1 by A13;
    a2 in W2 by A36,A38,ZFMISC_1:106;
    then consider x2, y2 being Element of REAL such that
A40:  a2 = |[x2,y2]| and
A41:  p2`1-g < x2 & x2 < p2`1+g;
    a2`1 = x2 by A40,EUCLID:56;
    then p2`1 - g - o`1 < a2`1 - o`1 & a2`1 - o`1 < p2`1 + g - o`1
      by A41,XREAL_1:11;
    then a2`1 - o`1 in ].r-g,r+g.[ by RCOMP_1:47;
    hence b in V by A34,A37,A39;
  end;
  then Xo is continuous by JGRAPH_2:20;
  then reconsider xo as continuous RealMap of [:T2,T2:] by TOPREAL6:83;
  for p being Point of [:T2,T2:], V being Subset of R^1
   st Yo.p in V & V is open holds
    ex W being Subset of [:T2,T2:] st p in W & W is open & Yo.:W c= V
  proof
    let p be Point of [:T2,T2:],
        V be Subset of R^1 such that
A42:   Yo.p in V and
A43:   V is open;
    consider p1, p2 being Point of T2 such that
A44:   p = [p1,p2] and
A45:   Yo.p = p2`2 - o`2 by A16;
    set r = p2`2 - o`2;
    reconsider V1 = V as open Subset of REAL by A43,TOPMETR:24,BORSUK_5:62;
    consider g being real number such that
A46:   0 < g and
A47:   ].r-g,r+g.[ c= V1 by A42,A45,RCOMP_1:40;
    reconsider g as Element of REAL by XREAL_0:def 1;
    set W2 = {|[x,y]| where x, y is Element of REAL: p2`2-g < y & y < p2`2+g};
    W2 c= the carrier of T2
    proof
      let a be set;
      assume a in W2;
      then ex x, y being Element of REAL st
        a = |[x,y]| & p2`2-g < y & y < p2`2+g;
      hence thesis;
    end;
    then reconsider W2 as Subset of T2;
    take [:[#]T2,W2:];
A48: p2 = |[p2`1,p2`2]| by EUCLID:57;
    p2`2-g < p2`2-0 & p2`2+0 < p2`2+g by A46,XREAL_1:8,REAL_1:92;
    then
    p2 in W2 by A48;
    hence p in [:[#]T2,W2:] by A44,ZFMISC_1:def 2;
    W2 is open by PSCOMP_1:68;
    hence [:[#]T2,W2:] is open by BORSUK_1:46;
    let b be set;
    assume b in Yo.:[:[#]T2,W2:];
    then consider a being Point of [:T2,T2:] such that
A49:  a in [:[#]T2,W2:] and
A50:  Yo.a = b by FUNCT_2:116;
    consider a1, a2 being Point of T2 such that
A51:  a = [a1,a2] and
A52:  yo.a = a2`2 - o`2 by A16;
    a2 in W2 by A49,A51,ZFMISC_1:106;
    then consider x2, y2 being Element of REAL such that
A53:  a2 = |[x2,y2]| and
A54:  p2`2-g < y2 & y2 < p2`2+g;
    a2`2 = y2 by A53,EUCLID:56;
    then p2`2 - g - o`2 < a2`2 - o`2 & a2`2 - o`2 < p2`2 + g - o`2
      by A54,XREAL_1:11;
    then a2`2 - o`2 in ].r-g,r+g.[ by RCOMP_1:47;
    hence b in V by A47,A50,A52;
  end;
  then Yo is continuous by JGRAPH_2:20;
  then reconsider yo as continuous RealMap of [:T2,T2:] by TOPREAL6:83;
  for p being Point of [:T2,T2:], V being Subset of R^1
   st Dx.p in V & V is open holds
    ex W being Subset of [:T2,T2:] st p in W & W is open & Dx.:W c= V
  proof
    let p be Point of [:T2,T2:],
        V be Subset of R^1 such that
A55:   Dx.p in V and
A56:   V is open;
    consider p1, p2 being Point of T2 such that
A57:   p = [p1,p2] and
A58:   dx.p = p1`1 - p2`1 by A19;
    set r = p1`1 - p2`1;
    reconsider V1 = V as open Subset of REAL by A56,TOPMETR:24,BORSUK_5:62;
    consider g being real number such that
A59:   0 < g and
A60:   ].r-g,r+g.[ c= V1 by A55,A58,RCOMP_1:40;
    reconsider g as Element of REAL by XREAL_0:def 1;
    set W1 = {|[x,y]| where x, y is Element of REAL:
      p1`1-g/2 < x & x < p1`1+g/2};
    set W2 = {|[x,y]| where x, y is Element of REAL:
      p2`1-g/2 < x & x < p2`1+g/2};
    W1 c= the carrier of T2
    proof
      let a be set;
      assume a in W1;
      then ex x, y being Element of REAL st
        a = |[x,y]| & p1`1-g/2 < x & x < p1`1+g/2;
      hence thesis;
    end;
    then reconsider W1 as Subset of T2;
    W2 c= the carrier of T2
    proof
      let a be set;
      assume a in W2;
      then ex x, y being Element of REAL st
        a = |[x,y]| & p2`1-g/2 < x & x < p2`1+g/2;
      hence thesis;
    end;
    then reconsider W2 as Subset of T2;
    take [:W1,W2:];
A61: p1 = |[p1`1,p1`2]| by EUCLID:57;
A62: 0/2 < g/2 by A59,REAL_1:73;
    then p1`1-g/2 < p1`1-0 & p1`1+0 < p1`1+g/2 by XREAL_1:8,REAL_1:92;
    then
A63: p1 in W1 by A61;
A64: p2 = |[p2`1,p2`2]| by EUCLID:57;
    p2`1-g/2 < p2`1-0 & p2`1+0 < p2`1+g/2 by A62,XREAL_1:8,REAL_1:92;
    then
    p2 in W2 by A64;
    hence p in [:W1,W2:] by A63,A57,ZFMISC_1:def 2;
    W1 is open & W2 is open by PSCOMP_1:66;
    hence [:W1,W2:] is open by BORSUK_1:46;
    let b be set;
    assume b in Dx.:[:W1,W2:];
    then consider a being Point of [:T2,T2:] such that
A65:  a in [:W1,W2:] and
A66:  Dx.a = b by FUNCT_2:116;
    consider a1, a2 being Point of T2 such that
A67:  a = [a1,a2] and
A68:  dx.a = a1`1 - a2`1 by A19;
    a1 in W1 by A65,A67,ZFMISC_1:106;
    then consider x1, y1 being Element of REAL such that
A69:  a1 = |[x1,y1]| and
A70:  p1`1-g/2 < x1 and
A71:  x1 < p1`1+g/2;
A72: a1`1 = x1 by A69,EUCLID:56;
    p1`1-g/2+g/2 < x1+g/2 & p1`1-x1 > p1`1-(p1`1+g/2) by A70,A71,XREAL_1:8,
REAL_1:92;
    then p1`1-x1 < x1+g/2-x1 & p1`1-x1 > -g/2 by XREAL_1:11;
    then
A73: abs(p1`1-x1) < g/2 by SEQ_2:9;
    a2 in W2 by A65,A67,ZFMISC_1:106;
    then consider x2, y2 being Element of REAL such that
A74:  a2 = |[x2,y2]| and
A75:  p2`1-g/2 < x2 and
A76:  x2 < p2`1+g/2;
A77: a2`1 = x2 by A74,EUCLID:56;
    p2`1-g/2+g/2 < x2+g/2 & p2`1-x2 > p2`1-(p2`1+g/2) by A75,A76,XREAL_1:8,
REAL_1:92;
    then p2`1-x2 < x2+g/2-x2 & p2`1-x2 > -g/2 by XREAL_1:11;
    then
    abs(p2`1-x2) < g/2 by SEQ_2:9;
    then
A78: abs(p1`1-x1)+abs(p2`1-x2) < g/2+g/2 by A73,XREAL_1:10;
    abs(p1`1-x1-(p2`1-x2)) <= abs(p1`1-x1)+abs(p2`1-x2) by COMPLEX1:143;
    then abs-(p1`1-x1-(p2`1-x2)) <= abs(p1`1-x1)+abs(p2`1-x2) by COMPLEX1:138;
    then abs(x1-x2-r) < g by A78,XXREAL_0:2;
    then a1`1 - a2`1 in ].r-g,r+g.[ by A72,A77,RCOMP_1:8;
    hence b in V by A60,A66,A68;
  end;
  then Dx is continuous by JGRAPH_2:20;
  then reconsider dx as continuous RealMap of [:T2,T2:] by TOPREAL6:83;
  for p being Point of [:T2,T2:], V being Subset of R^1
   st Dy.p in V & V is open holds
    ex W being Subset of [:T2,T2:] st p in W & W is open & Dy.:W c= V
  proof
    let p be Point of [:T2,T2:],
        V be Subset of R^1 such that
A79:   Dy.p in V and
A80:   V is open;
    consider p1, p2 being Point of T2 such that
A81:   p = [p1,p2] and
A82:   dy.p = p1`2 - p2`2 by A22;
    set r = p1`2 - p2`2;
    reconsider V1 = V as open Subset of REAL by A80,TOPMETR:24,BORSUK_5:62;
    consider g being real number such that
A83:   0 < g and
A84:   ].r-g,r+g.[ c= V1 by A79,A82,RCOMP_1:40;
    reconsider g as Element of REAL by XREAL_0:def 1;
    set W1 = {|[x,y]| where x, y is Element of REAL:
      p1`2-g/2 < y & y < p1`2+g/2};
    set W2 = {|[x,y]| where x, y is Element of REAL:
      p2`2-g/2 < y & y < p2`2+g/2};
    W1 c= the carrier of T2
    proof
      let a be set;
      assume a in W1;
      then ex x, y being Element of REAL st
        a = |[x,y]| & p1`2-g/2 < y & y < p1`2+g/2;
      hence thesis;
    end;
    then reconsider W1 as Subset of T2;
    W2 c= the carrier of T2
    proof
      let a be set;
      assume a in W2;
      then ex x, y being Element of REAL st
        a = |[x,y]| & p2`2-g/2 < y & y < p2`2+g/2;
      hence thesis;
    end;
    then reconsider W2 as Subset of T2;
    take [:W1,W2:];
A85: p1 = |[p1`1,p1`2]| by EUCLID:57;
A86: 0/2 < g/2 by A83,REAL_1:73;
    then p1`2-g/2 < p1`2-0 & p1`2+0 < p1`2+g/2 by XREAL_1:8,REAL_1:92;
    then
A87: p1 in W1 by A85;
A88: p2 = |[p2`1,p2`2]| by EUCLID:57;
    p2`2-g/2 < p2`2-0 & p2`2+0 < p2`2+g/2 by A86,XREAL_1:8,REAL_1:92;
    then
    p2 in W2 by A88;
    hence p in [:W1,W2:] by A87,A81,ZFMISC_1:def 2;
    W1 is open & W2 is open by PSCOMP_1:68;
    hence [:W1,W2:] is open by BORSUK_1:46;
    let b be set;
    assume b in Dy.:[:W1,W2:];
    then consider a being Point of [:T2,T2:] such that
A89:  a in [:W1,W2:] and
A90:  Dy.a = b by FUNCT_2:116;
    consider a1, a2 being Point of T2 such that
A91:  a = [a1,a2] and
A92:  dy.a = a1`2 - a2`2 by A22;
    a1 in W1 by A89,A91,ZFMISC_1:106;
    then consider x1, y1 being Element of REAL such that
A93:  a1 = |[x1,y1]| and
A94:  p1`2-g/2 < y1 and
A95:  y1 < p1`2+g/2;
A96: a1`2 = y1 by A93,EUCLID:56;
    p1`2-g/2+g/2 < y1+g/2 & p1`2-y1 > p1`2-(p1`2+g/2) by A94,A95,XREAL_1:8,
REAL_1:92;
    then p1`2-y1 < y1+g/2-y1 & p1`2-y1 > -g/2 by XREAL_1:11;
    then
A97: abs(p1`2-y1) < g/2 by SEQ_2:9;
    a2 in W2 by A89,A91,ZFMISC_1:106;
    then consider x2, y2 being Element of REAL such that
A98:  a2 = |[x2,y2]| and
A99:  p2`2-g/2 < y2 and
A100:  y2 < p2`2+g/2;
A101: a2`2 = y2 by A98,EUCLID:56;
    p2`2-g/2+g/2 < y2+g/2 & p2`2-y2 > p2`2-(p2`2+g/2) by A99,A100,XREAL_1:8,
REAL_1:92;
    then p2`2-y2 < y2+g/2-y2 & p2`2-y2 > -g/2 by XREAL_1:11;
    then
    abs(p2`2-y2) < g/2 by SEQ_2:9;
    then
A102: abs(p1`2-y1)+abs(p2`2-y2) < g/2+g/2 by A97,XREAL_1:10;
    abs(p1`2-y1-(p2`2-y2)) <= abs(p1`2-y1)+abs(p2`2-y2) by COMPLEX1:143;
    then abs-(p1`2-y1-(p2`2-y2)) <= abs(p1`2-y1)+abs(p2`2-y2) by COMPLEX1:138;
    then abs(y1-y2-r) < g by A102,XXREAL_0:2;
    then a1`2 - a2`2 in ].r-g,r+g.[ by A96,A101,RCOMP_1:8;
    hence b in V by A84,A90,A92;
  end;
  then Dy is continuous by JGRAPH_2:20;
  then reconsider dy as continuous RealMap of [:T2,T2:] by TOPREAL6:83;
  for p being Point of [:T2,T2:], V being Subset of R^1
   st fX2.p in V & V is open holds
    ex W being Subset of [:T2,T2:] st p in W & W is open & fX2.:W c= V
  proof
    let p be Point of [:T2,T2:],
        V be Subset of R^1 such that
A103:   fX2.p in V and
A104:   V is open;
    consider p1, p2 being Point of T2 such that
A105:   p = [p1,p2] and
A106:   fX2.p = p2`1 by A25;
    reconsider V1 = V as open Subset of REAL by A104,TOPMETR:24,BORSUK_5:62;
    consider g being real number such that
A107:   0 < g and
A108:   ].p2`1-g,p2`1+g.[ c= V1 by A103,A106,RCOMP_1:40;
    reconsider g as Element of REAL by XREAL_0:def 1;
    set W1 = {|[x,y]| where x, y is Element of REAL: p2`1-g < x & x < p2`1+g};
    W1 c= the carrier of T2
    proof
      let a be set;
      assume a in W1;
      then ex x, y being Element of REAL st
        a = |[x,y]| & p2`1-g < x & x < p2`1+g;
      hence thesis;
    end;
    then reconsider W1 as Subset of T2;
    take [:[#]T2,W1:];
A109: p2 = |[p2`1,p2`2]| by EUCLID:57;
    p2`1-g < p2`1-0 & p2`1+0 < p2`1+g by A107,XREAL_1:8,REAL_1:92;
    then p2 in W1 by A109;
    hence p in [:[#]T2,W1:] by A105,ZFMISC_1:def 2;
    W1 is open by PSCOMP_1:66;
    hence [:[#]T2,W1:] is open by BORSUK_1:46;
    let b be set;
    assume b in fX2.:[:[#]T2,W1:];
    then consider a being Point of [:T2,T2:] such that
A110:  a in [:[#]T2,W1:] and
A111:  fX2.a = b by FUNCT_2:116;
    consider a1, a2 being Point of T2 such that
A112:  a = [a1,a2] and
A113:  fX2.a = a2`1 by A25;
    a2 in W1 by A110,A112,ZFMISC_1:106;
    then consider x1, y1 being Element of REAL such that
A114:  a2 = |[x1,y1]| and
A115:  p2`1-g < x1 and
A116:  x1 < p2`1+g;
A117: a2`1 = x1 by A114,EUCLID:56;
    p2`1-g+g < x1+g & p2`1-x1 > p2`1-(p2`1+g) by A115,A116,XREAL_1:8,REAL_1:92;
    then p2`1-x1 < x1+g-x1 & p2`1-x1 > -g by XREAL_1:11;
    then abs(p2`1-x1) < g by SEQ_2:9;
    then abs-(p2`1-x1) < g by COMPLEX1:138;
    then abs(x1-p2`1) < g;
    then a2`1 in ].p2`1-g,p2`1+g.[ by A117,RCOMP_1:8;
    hence b in V by A108,A111,A113;
  end;
  then fX2 is continuous by JGRAPH_2:20;
  then reconsider fx2 as continuous RealMap of [:T2,T2:] by TOPREAL6:83;
  for p being Point of [:T2,T2:], V being Subset of R^1
   st fY2.p in V & V is open holds
    ex W being Subset of [:T2,T2:] st p in W & W is open & fY2.:W c= V
  proof
    let p be Point of [:T2,T2:],
        V be Subset of R^1 such that
A118:   fY2.p in V and
A119:   V is open;
    consider p1, p2 being Point of T2 such that
A120:   p = [p1,p2] and
A121:   fY2.p = p2`2 by A28;
    reconsider V1 = V as open Subset of REAL by A119,TOPMETR:24,BORSUK_5:62;
    consider g being real number such that
A122:   0 < g and
A123:   ].p2`2-g,p2`2+g.[ c= V1 by A118,A121,RCOMP_1:40;
    reconsider g as Element of REAL by XREAL_0:def 1;
    set W1 = {|[x,y]| where x, y is Element of REAL: p2`2-g < y & y < p2`2+g};
    W1 c= the carrier of T2
    proof
      let a be set;
      assume a in W1;
      then ex x, y being Element of REAL st
        a = |[x,y]| & p2`2-g < y & y < p2`2+g;
      hence thesis;
    end;
    then reconsider W1 as Subset of T2;
    take [:[#]T2,W1:];
A124: p2 = |[p2`1,p2`2]| by EUCLID:57;
    p2`2-g < p2`2-0 & p2`2+0 < p2`2+g by A122,XREAL_1:8,REAL_1:92;
    then p2 in W1 by A124;
    hence p in [:[#]T2,W1:] by A120,ZFMISC_1:def 2;
    W1 is open by PSCOMP_1:68;
    hence [:[#]T2,W1:] is open by BORSUK_1:46;
    let b be set;
    assume b in fY2.:[:[#]T2,W1:];
    then consider a being Point of [:T2,T2:] such that
A125:  a in [:[#]T2,W1:] and
A126:  fY2.a = b by FUNCT_2:116;
    consider a1, a2 being Point of T2 such that
A127:  a = [a1,a2] and
A128:  fY2.a = a2`2 by A28;
    a2 in W1 by A125,A127,ZFMISC_1:106;
    then consider x1, y1 being Element of REAL such that
A129:  a2 = |[x1,y1]| and
A130:  p2`2-g < y1 and
A131:  y1 < p2`2+g;
A132: a2`2 = y1 by A129,EUCLID:56;
    p2`2-g+g < y1+g & p2`2-y1 > p2`2-(p2`2+g) by A130,A131,XREAL_1:8,REAL_1:92;
    then p2`2-y1 < y1+g-y1 & p2`2-y1 > -g by XREAL_1:11;
    then abs(p2`2-y1) < g by SEQ_2:9;
    then abs-(p2`2-y1) < g by COMPLEX1:138;
    then abs(y1-p2`2) < g;
    then a2`2 in ].p2`2-g,p2`2+g.[ by A132,RCOMP_1:8;
    hence b in V by A123,A126,A128;
  end;
  then fY2 is continuous by JGRAPH_2:20;
  then reconsider fy2 as continuous RealMap of [:T2,T2:] by TOPREAL6:83;
  reconsider rr = r^2 as Real by XREAL_0:def 1;
  set f1 = (the carrier of [:T2,T2:]) --> rr;
  reconsider f1 as continuous RealMap of [:T2,T2:] by Lm1;
  set Zf1 = f1 || OK;
  set Zfx2 = fx2 || OK;
  set Zfy2 = fy2 || OK;
  set Zdx = dx || OK;
  set Zdy = dy || OK;
  set Zxo = xo || OK;
  set Zyo = yo || OK;
  set xx = Zxo(#)Zdx;
  set yy = Zyo(#)Zdy;
  set m = Zdx(#)Zdx + Zdy(#)Zdy;
A133: the carrier of TD = OK by PRE_TOPC:29;
A134: dom m = the carrier of TD by FUNCT_2:def 1;
A135: dom (Zdx(#)Zdx) = the carrier of TD by FUNCT_2:def 1;
A136: dom (Zdy(#)Zdy) = the carrier of TD by FUNCT_2:def 1;
A137: for y, z being Point of D2 st y <> z holds [y,z] in OK
  proof
    let y, z be Point of D2;
    assume
A138:   y <> z;
    y is Point of T2 & z is Point of T2 by TOPMETR:2;
    then [y,z] in DiffElems(T2,T2) by A138;
    hence [y,z] in OK by XBOOLE_0:def 3;
  end;
A139: for y, z being Point of D2 st y <> z holds Zdx. [y,z] = dx. [y,z]
  proof
    let y, z be Point of D2;
    assume y <> z;
    then
A140: [y,z] in OK by A137;
    thus Zdx. [y,z] = dx. [y,z] by A140,FUNCT_1:72;
  end;
A141: for y, z being Point of D2 st y <> z holds Zdy. [y,z] = dy. [y,z]
  proof
    let y, z be Point of D2;
    assume y <> z;
    then
A142: [y,z] in OK by A137;
    thus Zdy. [y,z] = dy. [y,z] by A142,FUNCT_1:72;
  end;
A143: for y, z being Point of D2 st y <> z holds Zfx2. [y,z] = fx2. [y,z]
  proof
    let y, z be Point of D2;
    assume y <> z;
    then
A144: [y,z] in OK by A137;
    thus Zfx2. [y,z] = fx2. [y,z] by A144,FUNCT_1:72;
  end;
A145: for y, z being Point of D2 st y <> z holds Zfy2. [y,z] = fy2. [y,z]
  proof
    let y, z be Point of D2;
    assume y <> z;
    then
A146: [y,z] in OK by A137;
    thus Zfy2. [y,z] = fy2. [y,z] by A146,FUNCT_1:72;
  end;
A147: for y, z being Point of D2 st y <> z holds Zf1. [y,z] = f1. [y,z]
  proof
    let y, z be Point of D2;
    assume y <> z;
    then
A148: [y,z] in OK by A137;
    thus Zf1. [y,z] = f1. [y,z] by A148,FUNCT_1:72;
  end;
A149: for y, z being Point of D2 st y <> z holds Zxo. [y,z] = xo. [y,z]
  proof
    let y, z be Point of D2;
    assume y <> z;
    then
A150: [y,z] in OK by A137;
    thus Zxo. [y,z] = xo. [y,z] by A150,FUNCT_1:72;
  end;
A151: for y, z being Point of D2 st y <> z holds Zyo. [y,z] = yo. [y,z]
  proof
    let y, z be Point of D2;
    assume y <> z;
    then
A152: [y,z] in OK by A137;
    thus Zyo. [y,z] = yo. [y,z] by A152,FUNCT_1:72;
  end;
  now
    let b be real number;
    assume b in rng m;
    then consider a being set such that
A153:   a in dom m and
A154:   m.a = b by FUNCT_1:def 5;
    a in DiffElems(T2,T2) by A153,A133,XBOOLE_0:def 3;
    then consider y, z being Point of T2 such that
A155:   a = [y,z] and
A156:   y <> z;
    a in the carrier of [:D2,D2:] by A153,A133,XBOOLE_0:def 3;
    then consider a1, a2 being Point of D2 such that
A157:   a = [a1,a2] by BORSUK_1:50;
A158: a1 = y & a2 = z by A157,A155,ZFMISC_1:33;
    consider x3, x4 being Point of T2 such that
A159:  [y,z] = [x3,x4] and
A160:  dx. [y,z] = x3`1 - x4`1 by A19;
A161: y = x3 & z = x4 by A159,ZFMISC_1:33;
    consider x5, x6 being Point of T2 such that
A162:  [y,z] = [x5,x6] and
A163:  dy. [y,z] = x5`2 - x6`2 by A22;
A164: y = x5 & z = x6 by A162,ZFMISC_1:33;
    set r1 = y`1-z`1,
        r2 = y`2-z`2;
A165: Zdx. [y,z] = dx. [y,z] by A156,A139,A158;
A166: Zdy. [y,z] = dy. [y,z] by A156,A141,A158;
A167: m. [y,z] = (Zdx(#)Zdx). [y,z] + (Zdy(#)Zdy). [y,z] by A153,A155,SEQ_1:def
3
    .= Zdx. [y,z] * Zdx. [y,z] + (Zdy(#)Zdy). [y,z] by A153,A155,A135,SEQ_1:def
5
    .= r1^2+r2^2 by A136,A153,A155,A160,A161,A163,A164,A165,A166,SEQ_1:def 5;
A168: 0+0 <= r1^2+r2^2 by XREAL_1:9;
    now
      assume r1^2+r2^2 = 0;
      then r1 = 0 & r2 = 0 by COMPLEX1:2;
      hence contradiction by A156,TOPREAL3:11;
    end;
    hence 0 < b by  A154,A155,A167,A168;
  end;
  then reconsider m as positive-yielding continuous RealMap of TD
    by PARTFUN3:def 1;
  set p1 = (xx+yy)(#)(xx+yy);
  set p2 = Zxo(#)Zxo + Zyo(#)Zyo - Zf1;
A169: dom p2 = the carrier of TD by FUNCT_2:def 1;
A170: dom (Zxo(#)Zxo + Zyo(#)Zyo) = the carrier of TD by FUNCT_2:def 1;
A171: dom (Zxo(#)Zxo) = the carrier of TD by FUNCT_2:def 1;
A172: dom (Zyo(#)Zyo) = the carrier of TD by FUNCT_2:def 1;
  now
    let b be real number;
    assume b in rng p2;
    then consider a being set such that
A173:   a in dom p2 and
A174:   p2.a = b by FUNCT_1:def 5;
    a in DiffElems(T2,T2) by A173,A133,XBOOLE_0:def 3;
    then consider y, z being Point of T2 such that
A175:   a = [y,z] and
A176:   y <> z;
    set r3 = z`1-o`1, r4 = z`2-o`2;
    a in the carrier of [:D2,D2:] by A173,A133,XBOOLE_0:def 3;
    then consider a1, a2 being Point of D2 such that
A177:   a = [a1,a2] by BORSUK_1:50;
A178: a1 = y & a2 = z by A177,A175,ZFMISC_1:33;
    then
A179: Zf1. [y,z] = f1. [y,z] by A176,A147;
A180: Zxo. [y,z] = xo. [y,z] by A176,A178,A149;
A181: Zyo. [y,z] = yo. [y,z] by A176,A178,A151;
    consider x9, x10 being Point of T2 such that
A182:  [y,z] = [x9,x10] and
A183:  xo. [y,z] = x10`1 - o`1 by A13;
A184: y = x9 & z = x10 by A182,ZFMISC_1:33;
    consider x11, x12 being Point of T2 such that
A185:  [y,z] = [x11,x12] and
A186:  yo. [y,z] = x12`2 - o`2 by A16;
A187: y = x11 & z = x12 by A185,ZFMISC_1:33;
A188: p2. [y,z] = (Zxo(#)Zxo + Zyo(#)Zyo). [y,z] - Zf1. [y,z]
    by A175,A173,SEQ_1:def 4
    .= (Zxo(#)Zxo + Zyo(#)Zyo). [y,z] - r^2 by A179,FUNCOP_1:13
    .= (Zxo(#)Zxo). [y,z] + (Zyo(#)Zyo). [y,z] - r^2
    by A175,A173,A170,SEQ_1:def 3
    .= Zxo. [y,z] * Zxo. [y,z] + (Zyo(#)Zyo). [y,z] - r^2
    by A175,A173,A171,SEQ_1:def 5
    .= r3^2+r4^2-r^2 by  A172,A173,A175,A180,A181,A183,A184,SEQ_1:def 5,
A186,A187;
    the carrier of D2 = cl_Ball(o,r) by Th3;
    then |. z-o .| <= r by A178,TOPREAL9:8;
    then
A190: |. z-o .|^2 <= r^2 by SQUARE_1:77;
    |. z-o .|^2 = ((z-o)`1)^2+((z-o)`2)^2 by JGRAPH_1:46
      .= r3^2+((z-o)`2)^2 by TOPREAL3:8
      .= r3^2+r4^2 by TOPREAL3:8;
    then r3^2+r4^2-r^2 <= r^2-r^2 by A190,XREAL_1:11;
    hence 0 >= b by A174,A175,A188;
  end;
  then reconsider p2 as nonpositive-yielding continuous RealMap of TD
    by PARTFUN3:def 3;
  set pp = p1 - m(#)p2;
  set k = (-(xx+yy) + sqrt(pp)) / m;
  set x3 = Zfx2 + k(#)Zdx;
  set y3 = Zfy2 + k(#)Zdy;
  reconsider X3 = x3, Y3 = y3 as Function of TD,R^1 by TOPMETR:24;
  set F = <:X3,Y3:>;
  set R = R2Homeomorphism;
A191: for x being Point of D2 holds g.x = (R*F). [x,f.x]
  proof
    let x be Point of D2;
A192: not x is_a_fixpoint_of f by A10,ABIAN:def 5;
    then consider y, z being Point of T2 such that
A193:   y = x and
A194:   z = f.x and
A195:   HC(x,f) = HC(z,y,o,r) by Def4;
A196: x <> f.x by A192,ABIAN:def 4;
    then [x,f.x] in DiffElems (T2,T2) by A193,A194;
    then
A197: [y,z] in OK by A193,A194,XBOOLE_0:def 3;
    set r1 = y`1-z`1, r2 = y`2-z`2,
        r3 = z`1-o`1, r4 = z`2-o`2;
    set l = (-(r3*r1+r4*r2)+sqrt((r3*r1+r4*r2)^2-(r1^2+r2^2)*(r3^2+r4^2-r^2)))
        / (r1^2+r2^2);
    consider x1, x2 being Point of T2 such that
A198:  [y,z] = [x1,x2] and
A199:  fx2. [y,z] = x2`1 by A25;
A200: z = x2 by A198,ZFMISC_1:33;
    consider x7, x8 being Point of T2 such that
A201:  [y,z] = [x7,x8] and
A202:  fy2. [y,z] = x8`2 by A28;
A203: z = x8 by A201,ZFMISC_1:33;
    consider x3, x4 being Point of T2 such that
A204:  [y,z] = [x3,x4] and
A205:  dx. [y,z] = x3`1 - x4`1 by A19;
A206: y = x3 & z = x4 by A204,ZFMISC_1:33;
    consider x5, x6 being Point of T2 such that
A207:  [y,z] = [x5,x6] and
A208:  dy. [y,z] = x5`2 - x6`2 by A22;
A209: y = x5 & z = x6 by A207,ZFMISC_1:33;
    consider x9, x10 being Point of T2 such that
A210:  [y,z] = [x9,x10] and
A211:  xo. [y,z] = x10`1 - o`1 by A13;
A212: y = x9 & z = x10 by A210,ZFMISC_1:33;
    consider x11, x12 being Point of T2 such that
A213:  [y,z] = [x11,x12] and
A214:  yo. [y,z] = x12`2 - o`2 by A16;
A215: y = x11 & z = x12 by A213,ZFMISC_1:33;
A216: dom X3 = the carrier of TD by FUNCT_2:def 1;
A217: dom Y3 = the carrier of TD by FUNCT_2:def 1;
A218: dom (k(#)Zdx) = the carrier of TD by FUNCT_2:def 1;
A219: dom (k(#)Zdy) = the carrier of TD by FUNCT_2:def 1;
A220: dom (Zdx(#)Zdx) = the carrier of TD by FUNCT_2:def 1;
A221: dom (Zdy(#)Zdy) = the carrier of TD by FUNCT_2:def 1;
A222: dom (-(xx+yy) + sqrt(pp)) = the carrier of TD by FUNCT_2:def 1;
A223: dom -(xx+yy) = the carrier of TD by FUNCT_2:def 1;
A224: dom (xx+yy) = the carrier of TD by FUNCT_2:def 1;
A225: dom xx = the carrier of TD by FUNCT_2:def 1;
A226: dom yy = the carrier of TD by FUNCT_2:def 1;
A227: dom pp = the carrier of TD by FUNCT_2:def 1;
A228: dom p1 = the carrier of TD by FUNCT_2:def 1;
A229: dom (m(#)p2) = the carrier of TD by FUNCT_2:def 1;
A230: Zdx. [y,z] = dx. [y,z] by A193,A194,A196,A139;
A231: Zdy. [y,z] = dy. [y,z] by A193,A194,A196,A141;
A232: Zf1. [y,z] = f1. [y,z] by A193,A194,A196,A147;
A233: Zxo. [y,z] = xo. [y,z] by A193,A194,A196,A149;
A234: Zyo. [y,z] = yo. [y,z] by A193,A194,A196,A151;
A235: m. [y,z] = (Zdx(#)Zdx). [y,z] + (Zdy(#)Zdy). [y,z]
    by A197,A133,A134,SEQ_1:def 3
    .= Zdx. [y,z] * Zdx. [y,z] + (Zdy(#)Zdy). [y,z]
    by A197,A133,A220,SEQ_1:def 5
    .= r1^2+r2^2 by  A133,A197,A205,A206,A221,A230,SEQ_1:def 5,A231,A208,A209;
A236: xx. [y,z] = Zxo. [y,z] * Zdx. [y,z] by A197,A133,A225,SEQ_1:def 5;
A237: yy. [y,z] = Zyo. [y,z] * Zdy. [y,z] by A197,A133,A226,SEQ_1:def 5;
A238: (xx+yy). [y,z] = xx. [y,z] + yy. [y,z] by A197,A133,A224,SEQ_1:def 3;
A239: p1. [y,z] = (r3*r1+r4*r2)^2
    by  A133,A197,A228,SEQ_1:def 5,
A230,A205,A206,A233,A211,A212,A238,A236,A237,A231,A208,A209,A234,A214,
A215;
A240: p2. [y,z] = (Zxo(#)Zxo + Zyo(#)Zyo). [y,z] - Zf1. [y,z]
    by A197,A133,A169,SEQ_1:def 4
    .= (Zxo(#)Zxo + Zyo(#)Zyo). [y,z] - r^2 by A232,FUNCOP_1:13
    .= (Zxo(#)Zxo). [y,z] + (Zyo(#)Zyo). [y,z] - r^2
    by A197,A133,A170,SEQ_1:def 3
    .= Zxo. [y,z] * Zxo. [y,z] + (Zyo(#)Zyo). [y,z] - r^2
    by A197,A133,A171,SEQ_1:def 5
    .= r3^2+r4^2-r^2 by  A133,A172,A197,A211,A212,A233,A234,SEQ_1:def 5,
A214,A215;
   dom sqrt pp = the carrier of TD by FUNCT_2:def 1;
   then
A241: sqrt(pp). [y,z] = sqrt(pp. [y,z]) by A197,A133,PARTFUN3:def 5
    .= sqrt(p1. [y,z] - (m(#)p2). [y,z]) by A197,A133,A227,SEQ_1:def 4
    .= sqrt((r3*r1+r4*r2)^2-(r1^2+r2^2)*(r3^2+r4^2-r^2))
    by A197,A235,A239,A240,A133,A229,SEQ_1:def 5;
   dom k = the carrier of TD by FUNCT_2:def 1;
   then
A242: k. [y,z] = (-(xx+yy) + sqrt(pp)). [y,z] * (m. [y,z])" by
   A197,A133,RFUNCT_1:def 4
  .= (-(xx+yy) + sqrt(pp)). [y,z] / m. [y,z] by XCMPLX_0:def 9
    .= ((-(xx+yy)). [y,z] + sqrt(pp). [y,z]) / (r1^2+r2^2)
    by A197,A133,A222,A235,SEQ_1:def 3
    .= l by A197,A133,A230,A205,A206,A233,A211,A212,A236,A231,A208,A209,A234,
A214,A215,A237,
    A238,A241,A223,SEQ_1:def 7;
A243: X3. [y,z] = Zfx2. [y,z] + (k(#)Zdx). [y,z] by A197,A133,A216,SEQ_1:def 3
    .= z`1 + (k(#)Zdx). [y,z] by A193,A194,A196,A143,A199,A200
    .= z`1+l*r1 by A197,A230,A205,A206,A242,A133,A218,SEQ_1:def 5;
A244: Y3. [y,z] = Zfy2. [y,z] + (k(#)Zdy). [y,z] by A197,A133,A217,SEQ_1:def 3
    .= z`2 + (k(#)Zdy). [y,z] by A193,A194,A196,A145,A202,A203
    .= z`2+l*r2 by A197,A231,A208,A209,A242,A133,A219,SEQ_1:def 5;
    thus g.x = HC(x,f) by Def5
    .= |[ z`1+l*r1, z`2+l*r2 ]| by A193,A194,A195,A196,Th8
    .= R. [X3. [y,z], Y3. [y,z]] by A243,A244,TOPREALA:def 2
    .= R.(F. [y,z]) by A197,A216,A217,A133,FUNCT_3:69
    .= (R*F). [x,f.x] by A197,A193,A194,A133,FUNCT_2:21;
  end;
  X3 is continuous & Y3 is continuous by TOPREAL6:83;
  then reconsider F as continuous Function of TD,[:R^1,R^1:] by YELLOW12:41;
  for p being Point of D2, V being Subset of S1
   st g.p in V & V is open holds
    ex W being Subset of D2 st p in W & W is open & g.:W c= V
  proof
    let p be Point of D2,
        V be Subset of S1 such that
A245:   g.p in V and
A246:   V is open;
    reconsider p1 = p, fp = f.p as Point of T2 by TOPMETR:2;
    not p is_a_fixpoint_of f by A10,ABIAN:def 5;
    then p <> f.p by ABIAN:def 4;
    then [p1,fp] in DiffElems (T2,T2);
    then
A247: [p,f.p] in OK by XBOOLE_0:def 3;
    consider V1 being Subset of T2 such that
A248:   V1 is open and
A249:   V1 /\ [#]S1 = V by A246,TOPS_2:32;
A250: g.p = (R*F). [p,f.p] by A191;
    R" is_homeomorphism by TOPS_2:70,TOPREALA:56;
    then
A251: R" .:V1 is open by A248,TOPGRP_1:25;
A252: dom F = the carrier of [:T2,T2:] | OK by FUNCT_2:def 1;
A253: dom R = the carrier of [:R^1,R^1:] by FUNCT_2:def 1;
    then
A254: rng F c= dom R;
    then
A255: dom (R*F) = dom F by RELAT_1:46;
A256: rng R = [#]T2 & R is one-to-one by TOPS_2:def 5,TOPREALA:56;
A257: R"*(R*F) = R"*R*F by RELAT_1:55
            .= id dom R*F by A256,TOPS_2:65;
    dom id dom R = dom R by RELAT_1:71;
    then
A258: dom (id dom R*F) = dom F by A254,RELAT_1:46;
    for x being set st x in dom F holds (id dom R*F).x = F.x
    proof
      let x be set such that
A259:     x in dom F;
A260:   F.x in rng F by A259,FUNCT_1:def 5;
      thus (id dom R*F).x = id dom R.(F.x) by A259,FUNCT_1:23
        .= F.x by A253,A260,FUNCT_1:35;
    end;
    then
A261: id dom R*F = F by A258,FUNCT_1:9;
    (R*F). [p1,fp] in V1 by A245,A249,A250,XBOOLE_0:def 3;
    then R" .((R*F). [p1,fp]) in R" .:V1 by FUNCT_2:43;
    then (R"*(R*F)). [p1,fp] in R" .:V1 by A247,A255,A252,A133,FUNCT_1:23;
    then consider W being Subset of TD such that
A262:  [p1,fp] in W and
A263:  W is open and
A264:  F.:W c= R" .:V1 by A247,A251,A257,A261,A133,JGRAPH_2:20;
    consider WW being Subset of [:T2,T2:] such that
A265:   WW is open and
A266:   WW /\ [#]TD = W by A263,TOPS_2:32;
    consider SF being Subset-Family of [:T2,T2:] such that
A267:  WW = union SF and
A268:  for e being set st e in SF
        ex X1 being Subset of T2, Y1 being Subset of T2 st
          e = [:X1,Y1:] & X1 is open & Y1 is open by A265,BORSUK_1:45;
    [p1,fp] in WW by A266,A262,XBOOLE_0:def 3;
    then consider Z being set such that
A269:  [p1,fp] in Z and
A270:  Z in SF by A267,TARSKI:def 4;
    consider X1, Y1 being Subset of T2 such that
A271:  Z = [:X1,Y1:] and
A272:  X1 is open and
A273:  Y1 is open by A268,A270;
    set ZZ = Z /\ [#]TD;
    reconsider XX = X1 /\ [#]D2, YY = Y1 /\ [#]D2 as open Subset of D2
      by A272,A273,TOPS_2:32;
    fp in Y1 by A269,A271,ZFMISC_1:106;
    then fp in YY by XBOOLE_0:def 3;
    then consider M being Subset of D2 such that
A274: p in M and
A275: M is open and
A276: f.:M c= YY by JGRAPH_2:20;
    take W1 = XX /\ M;
    p in X1 by A269,A271,ZFMISC_1:106;
    then p in XX by XBOOLE_0:def 3;
    hence p in W1 by A274,XBOOLE_0:def 3;
    thus W1 is open by A275,TOPS_1:38;
    let b be set;
    assume b in g.:W1;
    then consider a being Point of D2 such that
A277:  a in W1 and
A278:  b = g.a by FUNCT_2:116;
    reconsider a1 = a, fa = f.a as Point of T2 by TOPMETR:2;
    a in XX by A277,XBOOLE_0:def 3;
    then
A279: a in X1 by XBOOLE_0:def 3;
    not a is_a_fixpoint_of f by A10,ABIAN:def 5;
    then a <> f.a by ABIAN:def 4;
    then [a1,fa] in DiffElems (T2,T2);
    then
A280: [a,f.a] in OK by XBOOLE_0:def 3;
    a in M by A277,XBOOLE_0:def 3;
    then fa in f.:M by FUNCT_2:43;
    then f.a in Y1 by A276,XBOOLE_0:def 3;
    then [a,fa] in Z by A271,A279,ZFMISC_1:def 2;
    then [a,fa] in ZZ by A280,A133,XBOOLE_0:def 3;
    then
A281: F. [a1,fa] in F.:ZZ by FUNCT_2:43;
A282: R qua Function" = R" by A256,TOPS_2:def 4;
A283: dom(R") = [#]T2 by A256,TOPS_2:62;
    Z c= WW by A267,A270,ZFMISC_1:92;
    then ZZ c= WW /\ [#]TD by XBOOLE_1:27;
    then F.:ZZ c= F.:W by A266,RELAT_1:156;
    then F. [a1,fa] in F.:W by A281;
    then R.(F. [a1,fa]) in R.:(R" .:V1) by A264,FUNCT_2:43;
    then (R*F). [a1,fa] in R.:(R" .:V1) by A280,A133,FUNCT_2:21;
    then (R*F). [a1,fa] in V1 by A256,A283,A282,PARTFUN3:1;
    then g.a in V1 by A191;
    hence b in V by A249,A278,XBOOLE_0:def 3;
  end;
  hence g is continuous by JGRAPH_2:20;
end;

:: Brouwer Fixed Point Theorem
theorem Th14:
  for r being non negative (real number),
      o being Point of TOP-REAL 2,
      f being continuous Function of Tdisk(o,r), Tdisk(o,r) holds
  f has_a_fixpoint
proof
  let r be non negative (real number),
      o be Point of TOP-REAL 2,
      f be continuous Function of Tdisk(o,r), Tdisk(o,r);
A1: the carrier of Tcircle(o,r) = Sphere(o,r) by TOPREALB:9;
A2: the carrier of Tdisk(o,r) = cl_Ball(o,r) by Th3;
    per cases;
    suppose
A3:   r is positive;
      assume
A4:   f has_no_fixpoint;
      reconsider r as positive (real number) by A3;
      Sphere(o,r) c= cl_Ball(o,r) by TOPREAL9:17;
      then reconsider Y = Tcircle(o,r) as
      non empty SubSpace of Tdisk(o,r) by A1,A2,TSEP_1:4;
A5:   not Y is_a_retract_of Tdisk(o,r) by Th10;
      reconsider g = BR-map(f) as Function of Tdisk(o,r),Y;
A6:   g is continuous by A4,Th13;
      g is_a_retraction
      proof
        let W be Point of Tdisk(o,r);
        assume
A7:     W in the carrier of Y;
        not W is_a_fixpoint_of f by A4,ABIAN:def 5;
        hence g.W = W by A7,Th11;
      end;
      hence contradiction by A6,A5,BORSUK_1:def 20;
    end;
    suppose r is non positive;
      then reconsider r as non negative non positive (real number);
A8:   cl_Ball(o,r) = {o} by Th2;
      take o;
      dom f = the carrier of Tdisk(o,r) by FUNCT_2:def 1;
      hence o in dom f by A8,A2,TARSKI:def 1;
      then f.o in rng f by FUNCT_1:def 5;
      hence o = f.o by A2,A8,TARSKI:def 1;
    end;
  end;

theorem
  for r being non negative (real number),
      o being Point of TOP-REAL 2,
      f being continuous Function of Tdisk(o,r), Tdisk(o,r)
   ex x being Point of Tdisk(o,r) st f.x = x
proof
  let r be non negative (real number),
      o be Point of TOP-REAL 2,
      f be continuous Function of Tdisk(o,r), Tdisk(o,r);
  f has_a_fixpoint by Th14;
  then consider x being set such that
A1: x is_a_fixpoint_of f by ABIAN:def 5;
  take x;
  x in dom f by A1,ABIAN:def 3;
  hence x is Point of Tdisk(o,r);
  thus f.x = x by A1,ABIAN:def 3;
end;
