:: J\'onsson Theorem about Representation of Modular Lattices
::  by Mariusz {\L}api\'nski
::
:: Received June 29, 2000
:: Copyright (c) 2000 Association of Mizar Users

environ

 vocabularies RELAT_1, EQREL_1, LATTICES, FINSEQ_1, LATTICE5, WAYBEL_0,
      REALSET1, GROUP_6, FUNCT_1, FINSET_1, MATRIX_2, CAT_1, FILTER_2, BOOLE,
      YELLOW_0, ORDINAL2, ORDINAL1, WELLORD1, SEQM_3, LATTICE3, WAYBEL_1,
      MCART_1, RELAT_2, TARSKI, ZFMISC_1, HAHNBAN, PARTFUN1, PRALG_1, FUNCT_6,
      SGRAPH1, LATTICE8, ORDERS_1, ARYTM;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, RELAT_1, FUNCT_1, RELSET_1,
      FUNCT_2, FUNCT_6, NUMBERS, NAT_1, ORDINAL1, ORDINAL2, MCART_1, DOMAIN_1,
      PARTFUN1, FUNCOP_1, REALSET1, STRUCT_0, ORDERS_2, EQREL_1, FINSEQ_1,
      LATTICE3, BINOP_1, YELLOW_0, YELLOW_2, LATTICE5, YELLOW11, WAYBEL_1,
      WAYBEL_0, ABIAN, XXREAL_0;
 constructors ORDINAL2, NAT_1, FUNCT_6, RFUNCT_3, ABIAN, REALSET2, ORDERS_3,
      WAYBEL_1, LATTICE5, YELLOW11;
 registrations XBOOLE_0, SUBSET_1, ORDINAL1, RELSET_1, PARTFUN1, FUNCOP_1,
      XREAL_0, INT_1, CARD_1, FINSEQ_1, EQREL_1, REALSET1, FINSEQ_6, ABIAN,
      STRUCT_0, LATTICE3, YELLOW_0, YELLOW_2, WAYBEL_1, WAYBEL10, LATTICE5,
      YELLOW11, WAYBEL21, LATTICE7;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, LATTICE3, FUNCOP_1, WAYBEL_0, YELLOW_0, YELLOW11,
      LATTICE5, XBOOLE_0, BINOP_1, YELLOW_2, RELSET_1;
 theorems EQREL_1, RELAT_1, RELAT_2, FINSEQ_1, FINSEQ_2, ORDERS_2, TARSKI,
      ORDINAL1, ORDINAL3, HAHNBAN, PARTFUN1, GRFUNC_1, FUNCT_6, MCART_1, NAT_1,
      BINOP_1, ZFMISC_1, FUNCT_1, FUNCT_2, LATTICE3, YELLOW_0, YELLOW_3,
      YELLOW_5, WAYBEL_0, WAYBEL_1, WAYBEL_6, RELSET_1, LATTICE5, YELLOW11,
      WAYBEL13, ORDERS_3, TEX_1, SYSREL, XBOOLE_0, XBOOLE_1, REALSET1,
      ORDERS_1, STRUCT_0, CARD_1;
 schemes LATTICE5, ORDINAL2, NAT_1, FINSEQ_1, BINOP_1, CLASSES1;

begin :: Preliminaries

definition
  let L be RelStr;
  canceled;
  attr L is finitely_typed means
  :Def2:
  ex A being non empty set
  st (for e being set st e in the carrier of L
  holds e is Equivalence_Relation of A) & ex o being Element of NAT
  st for e1,e2 being Equivalence_Relation of A, x,y being set
  st e1 in the carrier of L & e2 in the carrier of L & [x,y] in e1 "\/" e2
  holds ex F being non empty FinSequence of A
  st len F = o & x,y are_joint_by F,e1,e2;
end;

definition
  let L be lower-bounded LATTICE;
  let n be Element of NAT;
  pred L has_a_representation_of_type<= n means
  :Def3:
  ex A being non trivial set, f be Homomorphism of L,EqRelLATT A
  st f is one-to-one & Image f is finitely_typed &
  (ex e being Equivalence_Relation of A
  st e in the carrier of Image f & e <> id A) & type_of Image f <= n;
end;

registration
  cluster lower-bounded distributive finite LATTICE;
  existence
  proof
    consider L being distributive finite LATTICE;
    take L;
    thus thesis;
  end;
end;

Lm1: 1 is odd
proof
  2*0+1 = 1;
  hence thesis;
end;

Lm2: 2 is even
proof
  2*1 = 2;
  hence thesis;
end;

registration
  let A be non trivial set;

cluster non trivial finitely_typed full (non empty Sublattice of EqRelLATT A);
  existence
  proof
    consider a being Element of A;
    consider b being Element of A\{a};
A1: b in A & a <> b by ZFMISC_1:64;
    reconsider e1 = nabla A, e2 = id A as Element of EqRelLATT A
    by LATTICE5:4;
    set Y = subrelstr {e1,e2};
A2: the carrier of Y = {e1,e2} by YELLOW_0:def 15;
    e1 = [:A,A:] by EQREL_1:def 1;
    then
A3: e2 <= e1 by LATTICE5:6;
A4: Y is meet-inheriting
    proof
      thus for x,y being Element of EqRelLATT A st
      x in the carrier of Y & y in the carrier of Y &
      ex_inf_of {x,y},EqRelLATT A holds inf {x,y} in the carrier of Y
      proof
        let x,y be Element of EqRelLATT A;
        assume
A5:     x in the carrier of Y & y in the carrier of Y &
        ex_inf_of {x,y},EqRelLATT A;
        per cases by A2,A5,TARSKI:def 2;
        suppose x = e1 & y = e1;
          then inf {x,y} = e1"/\"e1 by YELLOW_0:40
            .= e1 by YELLOW_5:2;
          hence thesis by A2,TARSKI:def 2;
        end;
        suppose x = e1 & y = e2;
          then inf {x,y} = e1"/\"e2 by YELLOW_0:40
            .= e2 by A3,YELLOW_5:10;
          hence thesis by A2,TARSKI:def 2;
        end;
        suppose x = e2 & y = e1;
          then inf {x,y} = e2"/\"e1 by YELLOW_0:40
            .= e2 by A3,YELLOW_5:10;
          hence thesis by A2,TARSKI:def 2;
        end;
        suppose x = e2 & y = e2;
          then inf {x,y} = e2"/\"e2 by YELLOW_0:40
            .= e2 by YELLOW_5:2;
          hence thesis by A2,TARSKI:def 2;
        end;
      end;
      thus thesis;
    end;
A6: Y is join-inheriting
    proof
      thus for x,y being Element of EqRelLATT A st
      x in the carrier of Y & y in the carrier of Y &
      ex_sup_of {x,y},EqRelLATT A holds sup {x,y} in the carrier of Y
      proof
        let x,y be Element of EqRelLATT A;
        assume
A7:     x in the carrier of Y & y in the carrier of Y &
        ex_sup_of {x,y},EqRelLATT A;
        per cases by A2,A7,TARSKI:def 2;
        suppose x = e1 & y = e1;
          then sup {x,y} = e1"\/"e1 by YELLOW_0:41
            .= e1 by YELLOW_5:1;
          hence thesis by A2,TARSKI:def 2;
        end;
        suppose x = e1 & y = e2;
          then sup {x,y} = e1"\/"e2 by YELLOW_0:41
            .= e1 by A3,YELLOW_5:8;
          hence thesis by A2,TARSKI:def 2;
        end;
        suppose x = e2 & y = e1;
          then sup {x,y} = e2"\/"e1 by YELLOW_0:41
            .= e1 by A3,YELLOW_5:8;
          hence thesis by A2,TARSKI:def 2;
        end;
        suppose x = e2 & y = e2;
          then sup {x,y} = e2"\/"e2 by YELLOW_0:41
            .= e2 by YELLOW_5:1;
          hence thesis by A2,TARSKI:def 2;
        end;
      end;
      thus thesis;
    end;
A8: Y is non trivial
    proof
      assume Y is trivial;
      then consider s being Element of Y such that
A9:   the carrier of Y = {s} by TEX_1:def 1;
      nabla A = id A by A2,A9,ZFMISC_1:9;
      then [:A,A:] = id A by EQREL_1:def 1;
      then [a,b] in id A by ZFMISC_1:def 2;
      hence contradiction by A1,RELAT_1:def 10;
    end;
A10: Y is finitely_typed
    proof
      take A;
      thus for e being set st e in the carrier of Y
      holds e is Equivalence_Relation of A by A2,TARSKI:def 2;
      take o = 3;
      thus for eq1,eq2 being Equivalence_Relation of A, x1,y1 being set
      st eq1 in the carrier of Y & eq2 in the carrier of Y &
      [x1,y1] in eq1 "\/" eq2 holds ex F being non empty FinSequence of A st
      len F = o & x1,y1 are_joint_by F,eq1,eq2
      proof
        let eq1,eq2 be Equivalence_Relation of A;
        let x1,y1 be set;
        assume
A11:    eq1 in the carrier of Y & eq2 in the carrier of Y &
        [x1,y1] in eq1 "\/" eq2;
        eq1 = e2 or eq1 <> e2;
        then consider z being set such that
A12:    eq1 = e2 & z = x1 or eq1 <> e2 & z = y1;
        consider x2,y2 being set such that
A13:    [x1,y1] = [x2,y2] & x2 in A & y2 in A by A11,RELSET_1:6;
        x1 in A & y1 in A by A13,ZFMISC_1:33;
        then reconsider F = <*x1,z,y1*> as non empty FinSequence of A
        by A12,FINSEQ_2:16;
        take F;
        per cases by A12;
        suppose
A14:      eq1 = e2 & z = x1;
          then
A15:      eq1 = e2 & len F = 3 & F.1 = x1 & F.2 = x1 & F.3 = y1 by FINSEQ_1:62;
          for i being Element of NAT st 1 <= i & i < len F holds
          (i is odd implies [F.i,F.(i+1)] in eq1) &
          (i is even implies [F.i,F.(i+1)] in eq2)
          proof
            let i be Element of NAT;
            assume
A16:        1 <= i & i < len F;
            then i < 2+1 by FINSEQ_1:62;
            then i <= 2 by NAT_1:13;
            then
A17:        i = 0 or i = 1 or i = 2 by NAT_1:27;
            per cases by A2,A11,A14,A16,A17,Lm1,Lm2,TARSKI:def 2;
            suppose
A18:          i = 1 & i is odd & eq1 = e2 & eq2 = e1;
              consider x2,y2 being set such that
A19:          [x1,y1] = [x2,y2] & x2 in A & y2 in A by A11,RELSET_1:6;
              x1 in A by A19,ZFMISC_1:33;
              hence thesis by A15,A18,EQREL_1:11;
            end;
            suppose
A20:          i = 1 & i is odd & eq1 = e2 & eq2 = e2;
              consider x2,y2 being set such that
A21:          [x1,y1] = [x2,y2] & x2 in A & y2 in A by A11,RELSET_1:6;
              x1 in A by A21,ZFMISC_1:33;
              hence thesis by A15,A20,EQREL_1:11;
            end;
            suppose
A22:          i = 2 & i is even & eq1 = e2 & eq2 = e1;
              then eq1 "\/" eq2 = e2 "\/" e1 by LATTICE5:10
                .= eq2 by A3,A22,YELLOW_5:8;
              hence thesis by A11,A15,A22;
            end;
            suppose i = 2 & i is even & eq1 = e2 & eq2 = e2;
              hence thesis by A11,A15;
            end;
          end;
          hence len F = o & x1,y1 are_joint_by F,eq1,eq2 by A15,LATTICE5:def 3;
        end;
        suppose
A23:      eq1 <> e2 & z = y1;
          then
A24:      eq1 <> e2 & len F = 3 & F.1 = x1 & F.2 = y1 & F.3 = y1 by FINSEQ_1:62
          ;
          for i being Element of NAT st 1 <= i & i < len F holds
          (i is odd implies [F.i,F.(i+1)] in eq1) &
          (i is even implies [F.i,F.(i+1)] in eq2)
          proof
            let i be Element of NAT;
            assume
A25:        1 <= i & i < len F;
            then i < 2+1 by FINSEQ_1:62;
            then i <= 2 by NAT_1:13;
            then
A26:        i = 0 or i = 1 or i = 2 by NAT_1:27;
            per cases by A2,A11,A23,A25,A26,Lm1,Lm2,TARSKI:def 2;
            suppose i = 1 & i is odd & eq1 = e1 & eq2 = e1;
              hence (i is odd implies [F.i,F.(i+1)] in eq1) &
              (i is even implies [F.i,F.(i+1)] in eq2) by A11,A24;
            end;
            suppose
A27:          i = 1 & i is odd & eq1 = e1 & eq2 = e2;
              then eq1 "\/" eq2 = e1 "\/" e2 by LATTICE5:10
                .= eq1 by A3,A27,YELLOW_5:8;
              hence (i is odd implies [F.i,F.(i+1)] in eq1) &
              (i is even implies [F.i,F.(i+1)] in eq2) by A11,A24,A27;
            end;
            suppose
A28:          i = 2 & i is even & eq1 = e1 & eq2 = e1;
              consider x2,y2 being set such that
A29:          [x1,y1] = [x2,y2] & x2 in A & y2 in A by A11,RELSET_1:6;
              y1 in A by A29,ZFMISC_1:33;
              hence (i is odd implies [F.i,F.(i+1)] in eq1) &
              (i is even implies [F.i,F.(i+1)] in eq2) by A24,A28,EQREL_1:11;
            end;
            suppose
A30:          i = 2 & i is even & eq1 = e1 & eq2 = e2;
              consider x2,y2 being set such that
A31:          [x1,y1] = [x2,y2] & x2 in A & y2 in A by A11,RELSET_1:6;
              y1 in A by A31,ZFMISC_1:33;
              hence (i is odd implies [F.i,F.(i+1)] in eq1) &
              (i is even implies [F.i,F.(i+1)] in eq2) by A24,A30,EQREL_1:11;
            end;
          end;
          hence len F = o & x1,y1 are_joint_by F,eq1,eq2 by A24,LATTICE5:def 3;
        end;
      end;
    end;
    reconsider Y as full (non empty Sublattice of EqRelLATT A) by A4,A6;
    take Y; thus thesis by A8,A10;
  end;
end;

theorem Th1:
  for A be non empty set for L be lower-bounded LATTICE
  for d be distance_function of A,L holds succ {} c= DistEsti(d)
proof
  let A be non empty set;
  let L be lower-bounded LATTICE;
  let d be distance_function of A,L;
  succ {} c= DistEsti(d) or DistEsti(d) in succ {} by ORDINAL1:26;
  then succ {} c= DistEsti(d) or DistEsti(d) c= {} by ORDINAL1:34;
  hence succ {} c= DistEsti(d) by LATTICE5:23,XBOOLE_1:3;
end;

theorem
  for L being trivial Semilattice holds L is modular
proof
  let L be trivial Semilattice;
  let a,b,c be Element of L such that a <= c;
  thus a"\/"(b"/\"c) = (a"\/"b)"/\"c by STRUCT_0:def 10;
end;

theorem
  for A being non empty set for L being non empty Sublattice of EqRelLATT A
  holds L is trivial or ex e being Equivalence_Relation of A
  st e in the carrier of L & e <> id A
proof
  let A be non empty set;
  let L be non empty Sublattice of EqRelLATT A;
  now
    assume
A1: not ex e being Equivalence_Relation of A st
    e in the carrier of L & e <> id A;
    thus L is trivial
    proof
      consider x be set such that
A2:   x in the carrier of L by XBOOLE_0:def 1;
      the carrier of L c= the carrier of EqRelLATT A by YELLOW_0:def 13;
      then reconsider e=x as Equivalence_Relation of A by A2,LATTICE5:4;
      the carrier of L = {x}
      proof
        thus the carrier of L c= {x}
        proof
          let a be set;
          assume
A3:       a in the carrier of L;
          the carrier of L c= the carrier of EqRelLATT A by YELLOW_0:def 13;
          then reconsider B=a as Equivalence_Relation of A by A3,LATTICE5:4;
          B = id A by A1,A3
            .= e by A1,A2;
          hence a in {x} by TARSKI:def 1;
        end;
        let A be set;
        assume A in {x};
        hence A in the carrier of L by A2,TARSKI:def 1;
      end;
      hence thesis;
    end;
  end;
  hence thesis;
end;

theorem Th4:
  for L1,L2 be lower-bounded LATTICE
  for f being Function of L1,L2 st f is infs-preserving sups-preserving
  holds f is meet-preserving join-preserving
proof
  let L1,L2 be lower-bounded LATTICE;
  let f be Function of L1,L2;
  assume
A1: f is infs-preserving sups-preserving;
  thus f is meet-preserving
  proof
    let x,y be Element of L1;
    thus f preserves_inf_of {x,y} by A1,WAYBEL_0:def 32;
  end;
  thus f is join-preserving
  proof
    let x,y be Element of L1;
    thus f preserves_sup_of {x,y} by A1,WAYBEL_0:def 33;
  end;
end;

theorem Th5:
  for L1,L2 be lower-bounded LATTICE
  st L1,L2 are_isomorphic & L1 is modular holds L2 is modular
proof
  let L1,L2 be lower-bounded LATTICE;
  assume
A1: L1,L2 are_isomorphic & L1 is modular;
  then consider f be Function of L1,L2 such that
A2: f is isomorphic by WAYBEL_1:def 8;
  f is infs-preserving sups-preserving by A2,WAYBEL13:20;
  then
A3: f is meet-preserving join-preserving by Th4;
  let a,b,c be Element of L2;
  assume
A4: a <= c;
  set A = f".a;
  set B = f".b;
  set C = f".c;
A5: f is one-to-one & rng f = the carrier of L2 by A2,WAYBEL_0:66;
  then
A6: f is one-to-one & a in rng f & b in rng f & c in rng f &
  a = f.A & b = f.B & c = f.C by FUNCT_1:57;
  A in dom f & B in dom f & C in dom f by A5,FUNCT_1:54;
  then reconsider A,B,C as Element of L1;
  A <= C by A2,A4,A6,WAYBEL_0:66;
  then
A7: A"\/"(B"/\"C) = (A"\/"B)"/\"C by A1,YELLOW11:def 3;
  thus a"\/"(b"/\"c) = f.A "\/" f.(B"/\"C) by A3,A6,WAYBEL_6:1
    .= f.((A"\/"B)"/\"C) by A3,A7,WAYBEL_6:2
    .= (f.(A"\/"B)"/\"f.C) by A3,WAYBEL_6:1
    .= (a"\/"b)"/\"c by A3,A6,WAYBEL_6:2;
end;

theorem Th6:
  for S being lower-bounded non empty Poset for T being non empty Poset
  for f being monotone Function of S,T holds Image f is lower-bounded
proof
  let S be lower-bounded non empty Poset;
  let T be non empty Poset;
  let f be monotone Function of S,T;
  thus Image f is lower-bounded
  proof
    consider x being Element of S such that
A1: x is_<=_than the carrier of S by YELLOW_0:def 4;
    dom f = the carrier of S by FUNCT_2:def 1;
    then x in dom f & f.x in rng f by FUNCT_1:def 5;
    then reconsider fx = f.x as Element of Image f by YELLOW_0:def 15;
    take fx;
    let b be Element of Image f;
    assume
A2: b in the carrier of Image f;
    b in the carrier of subrelstr (rng f);
    then b in rng f by YELLOW_0:def 15;
    then consider c be set such that
A3: c in dom f & f.c = b by FUNCT_1:def 5;
    reconsider c as Element of S by A3;
    the carrier of Image f c= the carrier of T by YELLOW_0:def 13;
    then reconsider b1 = b as Element of T by A2;
    x <= c by A1,LATTICE3:def 8;
    then f.x <= b1 by A3,ORDERS_3:def 5;
    hence fx <= b by YELLOW_0:61;
  end;
end;

theorem Th7:
  for L being lower-bounded LATTICE for x,y being Element of L
  for A being non empty set
  for f be Homomorphism of L,EqRelLATT A st f is one-to-one
  holds (corestr f).x <= (corestr f).y implies x <= y
proof
  let L be lower-bounded LATTICE;
  let x,y be Element of L;
  let A be non empty set;
  let f be Homomorphism of L,EqRelLATT A;
  assume that
A1: f is one-to-one and
A2: (corestr f).x <= (corestr f).y;
  now
    assume
A3: not x <= y;
A4: corestr f = f & corestr f is one-to-one by A1,WAYBEL_1:32;
A5: Image f is Semilattice & L is Semilattice;
A6: Image f is strict full Sublattice of EqRelLATT A;
A7: for x,y being Element of L holds
    (corestr f).(x "/\" y) = (corestr f).x "/\" (corestr f).y
    proof
      let x,y be Element of L;
      thus (corestr f).(x "/\" y) = f.x "/\" f.y by A4,WAYBEL_6:1
        .= (corestr f).x "/\" (corestr f).y by A4,A6,YELLOW_0:70;
    end;
    (corestr f).y "/\" (corestr f).x = (corestr f).x by A2,A5,YELLOW_5:10;
    then (corestr f).x = (corestr f).(x "/\" y) by A7;
    then x = x "/\" y by A4,WAYBEL_1:def 1;
    hence contradiction by A3,YELLOW_0:25;
  end;
  hence thesis;
end;

begin :: J\'onsson theorem

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
::  =>  ::   L has_a_representation_of_type<= 2 implies L is modular   ::
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

theorem Th8:
  for A being non trivial set
  for L being finitely_typed full (non empty Sublattice of EqRelLATT A)
  for e being Equivalence_Relation of A st e in the carrier of L & e <> id A
  holds type_of L <= 2 implies L is modular
proof
  let A be non trivial set;
  let L be finitely_typed full (non empty Sublattice of EqRelLATT A);
  let e be Equivalence_Relation of A;
  assume that
A1: e in the carrier of L and
A2: e <> id A;
  assume
A3: type_of L <= 2;
  let a,b,c be Element of L;
  assume
A4: a <= c;
A5: the carrier of L c= the carrier of EqRelLATT A by YELLOW_0:def 13;
A6: a in the carrier of L;
  then reconsider a' = a as Equivalence_Relation of A by A5,LATTICE5:4;
A7: b in the carrier of L;
  then reconsider b' = b as Equivalence_Relation of A by A5,LATTICE5:4;
A8: c in the carrier of L;
  then reconsider c' = c as Equivalence_Relation of A by A5,LATTICE5:4;
  reconsider a'' = a' as Element of EqRelLATT A by A5,A6;
  reconsider b'' = b' as Element of EqRelLATT A by A5,A7;
  reconsider c'' = c' as Element of EqRelLATT A by A5,A8;
  a'' <= c'' by A4,YELLOW_0:60;
  then
A9: a' c= c' by LATTICE5:6;
A10: b' /\ c' = b''"/\"c'' & b''"/\"c'' = b"/\"c by LATTICE5:8,YELLOW_0:70;
A11: a' "\/" b' = a''"\/"b'' & a''"\/"b'' = a"\/"b by LATTICE5:10,YELLOW_0:71;
A12: a'"\/"(b' /\ c') = a''"\/"(b''"/\"c'') by A10,LATTICE5:10;
A13: a''"\/"(b''"/\"c'') <= (a''"\/"b'')"/\"c'' by A4,YELLOW11:8,YELLOW_0:60;
  (a''"\/"b'')"/\"c'' = (a''"\/"b'') /\ c' by LATTICE5:8
    .= (a'"\/"b') /\ c' by LATTICE5:10;
  then
A14: a'"\/"(b' /\ c') c= (a'"\/"b') /\ c' by A12,A13,LATTICE5:6;
  consider AA being non empty set such that
A15: for e being set st e in the carrier of L
  holds e is Equivalence_Relation of AA and
A16: ex i being Element of NAT st
  for e1,e2 being Equivalence_Relation of AA, x,y being set st
  e1 in the carrier of L & e2 in the carrier of L & [x,y] in e1 "\/" e2 holds
  ex F being non empty FinSequence of AA st
  len F = i & x,y are_joint_by F,e1,e2 by Def2;
  e is Equivalence_Relation of AA by A1,A15;
  then
A17: field e = A & field e = AA by EQREL_1:16;
A18: (a'"\/"b') /\ c' c= a'"\/"(b' /\ c')
  proof
    let x,y be Element of A;
    assume
A19: [x,y] in (a' "\/" b') /\ c';
    then
A20: [x,y] in a' "\/" b' & [x,y] in c' by XBOOLE_0:def 3;
    per cases by A3,NAT_1:27;
    suppose type_of L = 2;
      then consider F being non empty FinSequence of A such that
A21:  len F = 2+2 & x,y are_joint_by F,a',b' by A1,A2,A16,A17,A20,
      LATTICE5:def 4;
      set z1 = F.2;
      set z2 = F.3;
A22:  F.1 = x & F.4 = y & for i being Element of NAT st 1 <= i & i < 4 holds
      (i is odd implies [F.i,F.(i+1)] in a') &
      (i is even implies [F.i,F.(i+1)] in b') by A21,LATTICE5:def 3;
      consider j being Element of NAT such that
A23:  j = 0;
      2*j+1 = 1 by A23;
      then
A24:  1 is odd & [F.1,F.(1+1)] in a' by A21,LATTICE5:def 3;
      consider k being Element of NAT such that
A25:  k = 1;
      2*k = 2 by A25;
      then
A26:  2 is even & [F.2,F.(2+1)] in b' by A21,LATTICE5:def 3;
      consider l being Element of NAT such that
A27:  l = 1;
      2*l+1 = 3 by A27;
      then
A28:  3 is odd & [F.3,F.(3+1)] in a' by A21,LATTICE5:def 3;
      then [x,z1] in a' & [z2,y] in a' & [z1,z2] in b' & [y,x] in c' &
      [x,z1] in c' & [z2,y] in c' & [x,y] in c' by A9,A20,A22,A24,A26,
      EQREL_1:12;
      then [z2,x] in c' by EQREL_1:13;
      then [z2,z1] in c' by A9,A22,A24,EQREL_1:13;
      then [z1,z2] in c' by EQREL_1:12;
      then
A29:  [z1,z2] in b' /\ c' by A26,XBOOLE_0:def 3;
      reconsider BC = b' /\ c' as Element of EqRelLATT A by A10;
A30:  a'' <= a'' "\/" BC by YELLOW_0:22;
A31:  a' "\/" (b' /\ c') = a''"\/" BC by LATTICE5:10;
      then
A32:  a' c= a' "\/" (b' /\ c') by A30,LATTICE5:6;
      BC <= BC "\/" a'' by YELLOW_0:22;
      then b' /\ c' c= a' "\/" (b' /\ c') by A31,LATTICE5:6;
      then [x,z2] in a' "\/" (b' /\ c') by A22,A24,A29,A32,EQREL_1:13;
      hence thesis by A22,A28,A32,EQREL_1:13;
    end;
    suppose type_of L = 1;
      then consider F being non empty FinSequence of A such that
A33:  len F = 1+2 & x,y are_joint_by F,a',b' by A1,A2,A16,A17,A20,
      LATTICE5:def 4;
      set z1 = F.2;
A34:  F.1 = x & F.3 = y & for i being Element of NAT st 1 <= i & i < 3 holds
      (i is odd implies [F.i,F.(i+1)] in a') &
      (i is even implies [F.i,F.(i+1)] in b') by A33,LATTICE5:def 3;
      consider j being Element of NAT such that
A35:  j = 0;
      2*j+1 = 1 by A35;
      then
A36:  1 is odd & [F.1,F.(1+1)] in a' by A33,LATTICE5:def 3;
      consider k being Element of NAT such that
A37:  k = 1;
  2*k = 2 by A37;
      then
A39:  2 is even & [F.2,F.(2+1)] in b' by A33,LATTICE5:def 3;
      [x,z1] in a' & [x,z1] in c' & [x,y] in c' & [z1,y] in b' & [z1,x] in c'
      by A9,A19,A33,A34,A36,EQREL_1:12,XBOOLE_0:def 3;
      then [z1,y] in c' by EQREL_1:13;
      then
A40:  [z1,y] in b' /\ c' by A34,A39,XBOOLE_0:def 3;
      reconsider BC = b' /\ c' as Element of EqRelLATT A by A10;
A41:  a'' <= a'' "\/" BC by YELLOW_0:22;
A42:  a' "\/" (b' /\ c') = a''"\/" BC by LATTICE5:10;
      then
A43:  a' c= a' "\/" (b' /\ c') by A41,LATTICE5:6;
      BC <= BC "\/" a'' by YELLOW_0:22;
      then b' /\ c' c= a' "\/" (b' /\ c') by A42,LATTICE5:6;
      hence thesis by A34,A36,A40,A43,EQREL_1:13;
    end;
    suppose type_of L = 0;
      then consider F being non empty FinSequence of A such that
A44:  len F = 0+2 & x,y are_joint_by F,a',b' by A1,A2,A16,A17,A20,
      LATTICE5:def 4;
A45:  F.1 = x & F.2 = y & for i being Element of NAT st 1 <= i & i < 2 holds
      (i is odd implies [F.i,F.(i+1)] in a') &
      (i is even implies [F.i,F.(i+1)] in b') by A44,LATTICE5:def 3;
      consider j being Element of NAT such that
A46:  j = 0;
      2*j+1 = 1 by A46;
      then
A47:  1 is odd & [F.1,F.(1+1)] in a' by A44,LATTICE5:def 3;
      reconsider BC = b' /\ c' as Element of EqRelLATT A by A10;
A48:  a'' <= a'' "\/" BC by YELLOW_0:22;
      a' "\/" (b' /\ c') = a''"\/" BC by LATTICE5:10;
      then a' c= a' "\/" (b' /\ c') by A48,LATTICE5:6;
      hence thesis by A45,A47;
    end;
  end;
A49: a'"\/"(b' /\ c') = a''"\/"(b''"/\"c'') by A10,LATTICE5:10
    .= a"\/"(b"/\"c) by A10,YELLOW_0:71;
  (a"\/"b)"/\"c = (a''"\/"b'')"/\"c'' by A11,YELLOW_0:70
    .= (a''"\/"b'') /\ c' by LATTICE5:8
    .= (a'"\/"b') /\ c' by LATTICE5:10;
  hence thesis by A14,A18,A49,XBOOLE_0:def 10;
end;

theorem Th9:
  for L be lower-bounded LATTICE
  holds L has_a_representation_of_type<= 2 implies L is modular
proof
  let L be lower-bounded LATTICE;
  assume L has_a_representation_of_type<= 2;
  then consider A being non trivial set,
  f being Homomorphism of L,EqRelLATT A such that
A1: f is one-to-one & Image f is finitely_typed &
  (ex e being Equivalence_Relation of A st
  e in the carrier of Image f & e <> id A) & type_of Image f <= 2 by Def3;
A2: Image f is modular by A1,Th8;
A3: Image f is lower-bounded LATTICE &
  corestr f is one-to-one by A1,Th6,WAYBEL_1:32;
A4: rng (corestr f) = the carrier of Image f by FUNCT_2:def 3;
A5: for x,y being Element of L holds
  x <= y implies (corestr f).x <= (corestr f).y by WAYBEL_1:def 2;
  for x,y being Element of L holds
  (corestr f).x <= (corestr f).y implies x <= y by A1,Th7;
  then corestr f is isomorphic by A3,A4,A5,WAYBEL_0:66;
  then L, Image f are_isomorphic by WAYBEL_1:def 8;
  hence thesis by A2,A3,Th5,WAYBEL_1:7;
end;

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
::  <=  ::   L is modular implies L has_a_representation_of_type<= 2   ::
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

definition
  let A be set;
  func new_set2 A equals

  A \/ {{A}, {{A}}};
  correctness;
end;

registration
  let A be set;
  cluster new_set2 A -> non empty;
  coherence;
end;

definition
  let A be non empty set;
  let L be lower-bounded LATTICE;
  let d be BiFunction of A,L;
  let q be Element of [:A,A,the carrier of L,the carrier of L:];
  func new_bi_fun2(d,q) -> BiFunction of new_set2 A,L means
  :Def5:
  (for u,v being Element of A holds it.(u,v) = d.(u,v)) &
  it.({A},{A}) = Bottom L & it.({{A}},{{A}}) = Bottom L &
  it.({A},{{A}}) = (d.(q`1,q`2)"\/"q`3)"/\"q`4 &
  it.({{A}},{A}) = (d.(q`1,q`2)"\/"q`3)"/\"q`4 & for u being Element of A
  holds (it.(u,{A}) = d.(u,q`1)"\/"q`3 & it.({A},u) = d.(u,q`1)"\/"q`3 &
  it.(u,{{A}}) = d.(u,q`2)"\/"q`3 & it.({{A}},u) = d.(u,q`2)"\/"q`3);
  existence
  proof
    set x = q`1, y = q`2;
    reconsider a = q`3, b = q`4 as Element of L;
A1: {A} in new_set2 A & {{A}} in new_set2 A
    proof
      {A} in {{A}, {{A}} } by TARSKI:def 2;
      hence {A} in new_set2 A by XBOOLE_0:def 2;
      {{A}} in {{A}, {{A}} } by TARSKI:def 2;
      hence {{A}} in new_set2 A by XBOOLE_0:def 2;
    end;
    defpred X[Element of new_set2 A,Element of new_set2 A,set] means
    ($1 in A & $2 in A implies $3 = d.($1,$2)) &
    ($1 = {A} & $2 = {{A}} or $2 = {A} & $1 = {{A}}
    implies $3 = (d.(x,y)"\/"a)"/\"b) &
    (($1 = {A} or $1 = {{A}}) & $2 = $1 implies $3 = Bottom L) &
    ($1 in A & $2 = {A} implies
    ex p' being Element of A st p' = $1 & $3 = d.(p',x)"\/"a) &
    ($1 in A & $2 = {{A}} implies
    ex p' being Element of A st p' = $1 & $3 = d.(p',y)"\/"a) &
    ($2 in A & $1 = {A} implies ex q' being Element of A st
    q' = $2 & $3 = d.(q',x)"\/"a) &
    ($2 in A & $1 = {{A}} implies ex q' being Element of A st
    q' = $2 & $3 = d.(q',y)"\/"a);
A2: for p,q being Element of new_set2 A ex r being Element of L st X[p,q,r]
    proof
      let p,q be Element of new_set2 A;
A3:   (p in A or p in {{A},{{A}}}) &
      (q in A or q in {{A},{{A}}}) by XBOOLE_0:def 2;
A4:   {A} <> {{A}}
      proof
        assume {A} = {{A}};
        then {A} in {A} by TARSKI:def 1;
        hence contradiction;
      end;
A5:   not {A} in A by TARSKI:def 1;
A6:   not {{A}} in A
      proof
        assume
A7:     {{A}} in A;
        A in {A} & {A} in {{A}} by TARSKI:def 1;
        hence contradiction by A7,ORDINAL1:3;
      end;
A8:   (p = {A} or p = {{A}}) & p = q iff
      p = {A} & q = {A} or p = {{A}} & q = {{A}};
      per cases by A3,A8,TARSKI:def 2;
      suppose p in A & q in A;
        then reconsider p'=p,q'=q as Element of A;
        take d.(p',q');
        thus thesis by A5,A6;
      end;
      suppose
A9:     p = {A} & q = {{A}} or q = {A} & p = {{A}};
        take (d.(x,y)"\/"a)"/\"b;
        thus thesis by A4,A6,A9,TARSKI:def 1;
      end;
      suppose
A10:    (p = {A} or p = {{A}}) & q = p;
        take Bottom L;
        thus thesis by A4,A6,A10,TARSKI:def 1;
      end;
      suppose
A11:    p in A & q = {A};
        then reconsider p' = p as Element of A;
        take d.(p',x)"\/"a;
        thus thesis by A4,A6,A11,TARSKI:def 1;
      end;
      suppose
A12:    p in A & q = {{A}};
        then reconsider p' = p as Element of A;
        take d.(p',y)"\/"a;
        thus thesis by A4,A6,A12,TARSKI:def 1;
      end;
      suppose
A13:    q in A & p = {A};
        then reconsider q' = q as Element of A;
        take d.(q',x)"\/"a;
        thus thesis by A4,A6,A13,TARSKI:def 1;
      end;
      suppose
A14:    q in A & p = {{A}};
        then reconsider q' = q as Element of A;
        take d.(q',y)"\/"a;
        thus thesis by A4,A6,A14,TARSKI:def 1;
      end;
    end;
    consider f being Function of [:new_set2 A,new_set2 A:],the carrier of L
    such that
A15: for p,q being Element of new_set2 A holds X[p,q,f.(p,q)]
    from BINOP_1:sch 3(A2);
    reconsider f as BiFunction of new_set2 A,L;
    take f;
A16: for u,v being Element of A holds f.(u,v) = d.(u,v)
    proof
      let u,v be Element of A;
      reconsider u' = u, v' = v as Element of new_set2 A by XBOOLE_0:def 2;
      thus f.(u,v) = f.(u',v')
        .= d.(u,v) by A15;
    end;
A17: for u being Element of A
    holds (f.(u,{A}) = d.(u,x)"\/"a & f.(u,{{A}}) = d.(u,y)"\/"a)
    proof
      let u be Element of A;
      reconsider u' = u as Element of new_set2 A by XBOOLE_0:def 2;
      consider u1 being Element of A such that
A18:  u1 = u' & f.(u',{A}) = d.(u1,x)"\/"a by A1,A15;
      thus f.(u,{A}) = d.(u,x)"\/"a by A18;
      consider u2 being Element of A such that
A19:  u2 = u' & f.(u',{{A}}) = d.(u2,y)"\/"a by A1,A15;
      thus f.(u,{{A}}) = d.(u,y)"\/"a by A19;
    end;
    for u being Element of A holds (f.({A},u) = d.(u,x)"\/"a &
    f.({{A}},u) = d.(u,y)"\/"a)
    proof
      let u be Element of A;
      reconsider u' = u as Element of new_set2 A by XBOOLE_0:def 2;
      consider u1 being Element of A such that
A20:  u1 = u' & f.({A},u') = d.(u1,x)"\/"a by A1,A15;
      thus f.({A},u) = d.(u,x)"\/"a by A20;
      consider u2 being Element of A such that
A21:  u2 = u' & f.({{A}},u') = d.(u2,y)"\/"a by A1,A15;
      thus f.({{A}},u) = d.(u,y)"\/"a by A21;
    end;
    hence thesis by A1,A15,A16,A17;
  end;
  uniqueness
  proof
    set x = q`1, y = q`2, a = q`3;
    let f1,f2 be BiFunction of new_set2 A,L such that
A22: (for u,v being Element of A holds f1.(u,v) = d.(u,v)) &
    f1.({A},{A}) = Bottom L & f1.({{A}},{{A}}) = Bottom L &
    f1.({A},{{A}}) = (d.(q`1,q`2)"\/"q`3)"/\"q`4 &
    f1.({{A}},{A}) = (d.(q`1,q`2)"\/"q`3)"/\"q`4 &
    for u being Element of A holds (f1.(u,{A}) = d.(u,q`1)"\/"q`3 &
    f1.({A},u) = d.(u,q`1)"\/"q`3 & f1.(u,{{A}}) = d.(u,q`2)"\/"q`3 &
    f1.({{A}},u) = d.(u,q`2)"\/"q`3) and
A23: (for u,v being Element of A holds f2.(u,v) = d.(u,v)) &
    f2.({A},{A}) = Bottom L & f2.({{A}},{{A}}) = Bottom L &
    f2.({A},{{A}}) = (d.(q`1,q`2)"\/"q`3)"/\"q`4 &
    f2.({{A}},{A}) = (d.(q`1,q`2)"\/"q`3)"/\"q`4 &
    for u being Element of A holds (f2.(u,{A}) = d.(u,q`1)"\/"q`3 &
    f2.({A},u) = d.(u,q`1)"\/"q`3 & f2.(u,{{A}}) = d.(u,q`2)"\/"q`3 &
    f2.({{A}},u) = d.(u,q`2)"\/"q`3);
    now
      let p,q be Element of new_set2 A;
A24:  (p in A or p in {{A},{{A}}}) &
      (q in A or q in {{A},{{A}}}) by XBOOLE_0:def 2;
      per cases by A24,TARSKI:def 2;
      suppose
A25:    p in A & q in A;
        hence f1.(p,q) = d.(p,q) by A22
          .= f2.(p,q) by A23,A25;
      end;
      suppose
A26:    p in A & q = {A};
        then reconsider p' = p as Element of A;
        thus f1.(p,q) = d.(p',x)"\/"a by A22,A26
          .= f2.(p,q) by A23,A26;
      end;
      suppose
A27:    p in A & q = {{A}};
        then reconsider p' = p as Element of A;
        thus f1.(p,q) = d.(p',y)"\/"a by A22,A27
          .= f2.(p,q) by A23,A27;
      end;
      suppose
A28:    p = {A} & q in A;
        then reconsider q' = q as Element of A;
        thus f1.(p,q) = d.(q',x)"\/"a by A22,A28
          .= f2.(p,q) by A23,A28;
      end;
      suppose p = {A} & q = {A};
        hence f1.(p,q) = f2.(p,q) by A22,A23;
      end;
      suppose p = {A} & q = {{A}};
        hence f1.(p,q) = f2.(p,q) by A22,A23;
      end;
      suppose
A29:    p = {{A}} & q in A;
        then reconsider q' = q as Element of A;
        thus f1.(p,q) = d.(q',y)"\/"a by A22,A29
          .= f2.(p,q) by A23,A29;
      end;
      suppose p = {{A}} & q = {A};
        hence f1.(p,q) = f2.(p,q) by A22,A23;
      end;
      suppose p = {{A}} & q = {{A}};
        hence f1.(p,q) = f2.(p,q) by A22,A23;
      end;
    end;
    hence f1 = f2 by BINOP_1:2;
  end;
end;

theorem Th10:
  for A being non empty set for L being lower-bounded LATTICE
  for d being BiFunction of A,L st d is zeroed
  for q being Element of [:A,A,the carrier of L,the carrier of L:]
  holds new_bi_fun2(d,q) is zeroed
proof
  let A be non empty set;
  let L be lower-bounded LATTICE;
  let d be BiFunction of A,L;
  assume
A1: d is zeroed;
  let q be Element of [:A,A,the carrier of L,the carrier of L:];
  set f = new_bi_fun2(d,q);
  for u being Element of new_set2 A holds f.(u,u) = Bottom L
  proof
    let u be Element of new_set2 A;
A2: u in A or u in {{A},{{A}}} by XBOOLE_0:def 2;
    per cases by A2,TARSKI:def 2;
    suppose u in A;
      then reconsider u' = u as Element of A;
      thus f.(u,u) = d.(u',u') by Def5
        .= Bottom L by A1,LATTICE5:def 7;
    end;
    suppose u = {A} or u = {{A}};
      hence f.(u,u) = Bottom L by Def5;
    end;
  end;
  hence thesis by LATTICE5:def 7;
end;

theorem Th11:
  for A being non empty set for L being lower-bounded LATTICE
  for d being BiFunction of A,L st d is symmetric
  for q being Element of [:A,A,the carrier of L,the carrier of L:]
  holds new_bi_fun2(d,q) is symmetric
proof
  let A be non empty set;
  let L be lower-bounded LATTICE;
  let d be BiFunction of A,L;
  assume
A1: d is symmetric;
  let q be Element of [:A,A,the carrier of L,the carrier of L:];
  set f = new_bi_fun2(d,q), x = q`1, y = q`2, a = q`3, b = q`4;
  let p,q be Element of new_set2 A;
A2: (p in A or p in {{A},{{A}}}) & (q in A or q in {{A},{{A}}}) by
  XBOOLE_0:def 2;
  per cases by A2,TARSKI:def 2;
  suppose p in A & q in A;
    then reconsider p' = p, q' = q as Element of A;
    thus f.(p,q) = d.(p',q') by Def5
      .= d.(q',p') by A1,LATTICE5:def 6
      .= f.(q,p) by Def5;
  end;
  suppose
A3: p in A & q = {A};
    then reconsider p' = p as Element of A;
    thus f.(p,q) = d.(p',x)"\/"a by A3,Def5
      .= f.(q,p) by A3,Def5;
  end;
  suppose
A4: p in A & q = {{A}};
    then reconsider p' = p as Element of A;
    thus f.(p,q) = d.(p',y)"\/"a by A4,Def5
      .= f.(q,p) by A4,Def5;
  end;
  suppose
A5: p = {A} & q in A;
    then reconsider q' = q as Element of A;
    thus f.(p,q) = d.(q',x)"\/"a by A5,Def5
      .= f.(q,p) by A5,Def5;
  end;
  suppose p = {A} & q = {A};
    hence f.(p,q) = f.(q,p);
  end;
  suppose
A6: p = {A} & q = {{A}};
    hence f.(p,q) = (d.(x,y)"\/"a)"/\"b by Def5
      .= f.(q,p) by A6,Def5;
  end;
  suppose
A7: p = {{A}} & q in A;
    then reconsider q' = q as Element of A;
    thus f.(p,q) = d.(q',y)"\/"a by A7,Def5
      .= f.(q,p) by A7,Def5;
  end;
  suppose
A8: p = {{A}} & q = {A};
    hence f.(p,q) = (d.(x,y)"\/"a)"/\"b by Def5
      .= f.(q,p) by A8,Def5;
  end;
  suppose p = {{A}} & q = {{A}};
    hence f.(p,q) = f.(q,p);
  end;
end;

theorem Th12:
  for A being non empty set for L being lower-bounded LATTICE st L is modular
  for d being BiFunction of A,L st d is symmetric & d is u.t.i.
  for q being Element of [:A,A,the carrier of L,the carrier of L:]
  st d.(q`1,q`2) <= q`3"\/"q`4 holds new_bi_fun2(d,q) is u.t.i.
proof
  let A be non empty set;
  let L be lower-bounded LATTICE;
  assume
A1: L is modular;
  let d be BiFunction of A,L;
  assume that
A2: d is symmetric and
A3: d is u.t.i.;
  let q be Element of [:A,A,the carrier of L,the carrier of L:];
  set x = q`1, y = q`2, a = q`3, b = q`4, f = new_bi_fun2(d,q);
  assume
A4: d.(q`1,q`2) <= q`3"\/"q`4;
  reconsider B = {{A}, {{A}}} as non empty set;
  reconsider a,b as Element of L;
A5: for p,q,u being Element of new_set2 A st p in A & q in A & u in A
  holds f.(p,u) <= f.(p,q) "\/" f.(q,u)
  proof
    let p,q,u be Element of new_set2 A;
    assume p in A & q in A & u in A;
    then reconsider p' = p, q' = q, u' = u as Element of A;
A6: f.(p,u) = d.(p',u') by Def5;
A7: f.(p,q) = d.(p',q') by Def5;
    f.(q,u) = d.(q',u') by Def5;
    hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A3,A6,A7,LATTICE5:def 8;
  end;
A8: for p,q,u being Element of new_set2 A st p in A & q in A & u in B
  holds f.(p,u) <= f.(p,q) "\/" f.(q,u)
  proof
    let p,q,u be Element of new_set2 A;
    assume
A9: p in A & q in A & u in B;
    per cases by A9,TARSKI:def 2;
    suppose
A10:  p in A & q in A & u = {A};
      then reconsider p' = p, q' = q as Element of A;
A11:  f.(p,u) = d.(p',x)"\/"a by A10,Def5;
A12:  f.(q,u) = d.(q',x)"\/"a by A10,Def5;
A13:  f.(p,q) = d.(p',q') by Def5;
      d.(p',x) <= d.(p',q') "\/" d.(q',x) by A3,LATTICE5:def 8;
      then d.(p',x)"\/"a <= (d.(p',q') "\/" d.(q',x))"\/"a by WAYBEL_1:3;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A11,A12,A13,LATTICE3:14;
    end;
    suppose
A14:  p in A & q in A & u = {{A}};
      then reconsider p' = p, q' = q as Element of A;
A15:  f.(p,u) = d.(p',y)"\/"a by A14,Def5;
A16:  f.(q,u) = d.(q',y)"\/"a by A14,Def5;
A17:  f.(p,q) = d.(p',q') by Def5;
      d.(p',y) <= d.(p',q') "\/" d.(q',y) by A3,LATTICE5:def 8;
      then d.(p',y)"\/"a <= (d.(p',q') "\/" d.(q',y))"\/"a by WAYBEL_1:3;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A15,A16,A17,LATTICE3:14;
    end;
  end;
A18: for p,q,u being Element of new_set2 A st p in A & q in B & u in A
  holds f.(p,u) <= f.(p,q) "\/" f.(q,u)
  proof
    let p,q,u be Element of new_set2 A;
    assume
A19: p in A & q in B & u in A;
    per cases by A19,TARSKI:def 2;
    suppose
A20:  p in A & u in A & q = {A};
      then reconsider p' = p, u' = u as Element of A;
A21:  f.(p,q) = d.(p',x)"\/"a by A20,Def5;
A22:  f.(q,u) = d.(u',x)"\/"a by A20,Def5;
A23:  d.(p',x) "\/" d.(u',x) <= (d.(p',x) "\/" d.(u',x))"\/"a by YELLOW_0:22;
A24:  (d.(p',x)"\/"d.(u',x))"\/"a = d.(p',x)"\/"(d.(u',x)"\/"a) by LATTICE3:14
        .= d.(p',x)"\/"(d.(u',x)"\/"(a"\/"a)) by YELLOW_5:1
        .= d.(p',x)"\/"((d.(u',x)"\/"a)"\/"a) by LATTICE3:14
        .= (d.(p',x)"\/"a) "\/" (d.(u',x)"\/"a) by LATTICE3:14;
      d.(p',u') <= d.(p',x) "\/" d.(x,u') by A3,LATTICE5:def 8;
      then d.(p',u') <= d.(p',x) "\/" d.(u',x) by A2,LATTICE5:def 6;
      then d.(p',u') <= (d.(p',x)"\/"a) "\/" (d.(u',x)"\/"a) by A23,A24,
      ORDERS_2:26;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A21,A22,Def5;
    end;
    suppose
A25:  p in A & u in A & q = {{A}};
      then reconsider p' = p, u' = u as Element of A;
A26:  f.(p,q) = d.(p',y)"\/"a by A25,Def5;
A27:  f.(q,u) = d.(u',y)"\/"a by A25,Def5;
A28:  d.(p',y) "\/" d.(u',y) <= (d.(p',y) "\/" d.(u',y))"\/"a by YELLOW_0:22;
A29:  (d.(p',y)"\/"d.(u',y))"\/"a = d.(p',y)"\/"(d.(u',y)"\/"a) by LATTICE3:14
        .= d.(p',y)"\/"(d.(u',y)"\/"(a"\/"a)) by YELLOW_5:1
        .= d.(p',y)"\/"((d.(u',y)"\/"a)"\/"a) by LATTICE3:14
        .= (d.(p',y)"\/"a)"\/"(d.(u',y)"\/"a) by LATTICE3:14;
      d.(p',u') <= d.(p',y) "\/" d.(y,u') by A3,LATTICE5:def 8;
      then d.(p',u') <= d.(p',y) "\/" d.(u',y) by A2,LATTICE5:def 6;
      then d.(p',u') <= (d.(p',y)"\/"a) "\/" (d.(u',y)"\/"a) by A28,A29,
      ORDERS_2:26;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A26,A27,Def5;
    end;
  end;
A30: for p,q,u being Element of new_set2 A st p in A & q in B & u in B
  holds f.(p,u) <= f.(p,q) "\/" f.(q,u)
  proof
    let p,q,u be Element of new_set2 A;
    assume
A31: p in A & q in B & u in B;
    per cases by A31,TARSKI:def 2;
    suppose
A32:  p in A & q = {A} & u = {A};
      then f.(p,q) "\/" f.(q,u) = Bottom L"\/"f.(p,q) by Def5
        .= f.(p,q) by WAYBEL_1:4;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A32;
    end;
    suppose
A33:  p in A & q = {A} & u = {{A}};
      then reconsider p' = p as Element of A;
A34:  f.(p,q) = d.(p',x)"\/"a by A33,Def5;
A35:  f.(q,u) = (d.(x,y)"\/"a)"/\"b by A33,Def5;
      a <= a "\/" d.(x,y) by YELLOW_0:22;
      then
A36:  a"\/"((d.(x,y)"\/"a)"/\"b) = (a"\/"b)"/\"(a"\/"
      d.(x,y)) by A1,YELLOW11:def 3;
A37:  d.(x,y)"\/"a = (d.(x,y)"\/"a)"/\"(d.(x,y)"\/"a) by YELLOW_5:2;
      a <= a;
      then d.(x,y)"\/"a <= (a"\/"b)"\/"a by A4,YELLOW_3:3;
      then (d.(x,y)"\/"a)"/\"(d.(x,y)"\/"a)
      <= (d.(x,y)"\/"a)"/\"((a"\/"b)"\/"a) by YELLOW_5:6;
      then
A38:  d.(p',x)"\/"((d.(x,y)"\/"a)"/\"(d.(x,y)"\/"a))
      <= d.(p',x)"\/"((d.(x,y)"\/"a)"/\"((a"\/"b)"\/" a)) by WAYBEL_1:3;
      f.(p,q) "\/" f.(q,u) = d.(p',x)"\/"((a"\/"b)"/\"(a"\/"
      d.(x,y))) by A34,A35,A36,LATTICE3:14
        .= d.(p',x)"\/"((d.(x,y)"\/"a)"/\"((a"\/"a)"\/"b)) by YELLOW_5:1
        .= d.(p',x)"\/"((d.(x,y)"\/"a)"/\"((a"\/"b)"\/" a)) by LATTICE3:14;
      then
A39:  (d.(p',x)"\/"d.(x,y))"\/"a <= f.(p,q) "\/" f.(q,u) by A37,A38,LATTICE3:14
      ;
      d.(p',y) <= d.(p',x)"\/"d.(x,y) by A3,LATTICE5:def 8;
      then d.(p',y)"\/"a <= (d.(p',x)"\/"d.(x,y))"\/"a by YELLOW_5:7;
      then d.(p',y)"\/"a <= f.(p,q) "\/" f.(q,u) by A39,ORDERS_2:26;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A33,Def5;
    end;
    suppose
A40:  p in A & q = {{A}} & u = {A};
      then reconsider p' = p as Element of A;
A41:  f.(p,q) = d.(p',y)"\/"a by A40,Def5;
A42:  f.(q,u) = (d.(x,y)"\/"a)"/\"b by A40,Def5;
      a <= a "\/" d.(x,y) by YELLOW_0:22;
      then
A43:  a"\/"((d.(x,y)"\/"a)"/\"b) = (a"\/"b)"/\"(a"\/"
      d.(x,y)) by A1,YELLOW11:def 3;
A44:  d.(x,y)"\/"a = (d.(x,y)"\/"a)"/\"(d.(x,y)"\/"a) by YELLOW_5:2;
      a <= a;
      then d.(x,y)"\/"a <= (a"\/"b)"\/"a by A4,YELLOW_3:3;
      then (d.(x,y)"\/"a)"/\"(d.(x,y)"\/"a)
      <= (d.(x,y)"\/"a)"/\"((a"\/"b)"\/"a) by YELLOW_5:6;
      then
A45:  d.(p',y)"\/"((d.(x,y)"\/"a)"/\"(d.(x,y)"\/"a))
      <= d.(p',y)"\/"((d.(x,y)"\/"a)"/\"((a"\/"b)"\/" a)) by WAYBEL_1:3;
      f.(p,q) "\/" f.(q,u) = d.(p',y)"\/"((a"\/"b)"/\"(a"\/"
      d.(x,y))) by A41,A42,A43,LATTICE3:14
        .= d.(p',y)"\/"((d.(x,y)"\/"a)"/\"((a"\/"a)"\/"b)) by YELLOW_5:1
        .= d.(p',y)"\/"((d.(x,y)"\/"a)"/\"((a"\/"b)"\/" a)) by LATTICE3:14;
      then
A46:  (d.(p',y)"\/"d.(x,y))"\/"a <= f.(p,q) "\/" f.(q,u) by A44,A45,LATTICE3:14
      ;
      d.(y,x) = d.(x,y) by A2,LATTICE5:def 6;
      then d.(p',x) <= d.(p',y)"\/"d.(x,y) by A3,LATTICE5:def 8;
      then d.(p',x)"\/"a <= (d.(p',y)"\/"d.(x,y))"\/"a by YELLOW_5:7;
      then d.(p',x)"\/"a <= f.(p,q) "\/" f.(q,u) by A46,ORDERS_2:26;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A40,Def5;
    end;
    suppose
A47:  p in A & q = {{A}} & u = {{A}};
      then f.(p,q) "\/" f.(q,u) = Bottom L"\/"f.(p,q) by Def5
        .= f.(p,q) by WAYBEL_1:4;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A47;
    end;
  end;
A48: for p,q,u being Element of new_set2 A st p in B & q in A & u in A
  holds f.(p,u) <= f.(p,q) "\/" f.(q,u)
  proof
    let p,q,u be Element of new_set2 A;
    assume
A49: p in B & q in A & u in A;
    then reconsider q' = q, u' = u as Element of A;
    per cases by A49,TARSKI:def 2;
    suppose
A50:  p = {A} & q in A & u in A;
      then
A51:  f.(p,q) = d.(q',x)"\/"a by Def5;
A52:  f.(q,u) = d.(q',u') by Def5;
      d.(u',x) <= d.(u',q') "\/" d.(q',x) by A3,LATTICE5:def 8;
      then d.(u',x) <= d.(q',u') "\/" d.(q',x) by A2,LATTICE5:def 6;
      then d.(u',x)"\/"a <= (d.(q',x)"\/"d.(q',u'))"\/"a by WAYBEL_1:3;
      then d.(u',x)"\/"a <= (d.(q',x)"\/"a)"\/"d.(q',u') by LATTICE3:14;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A50,A51,A52,Def5;
    end;
    suppose
A53:  p = {{A}} & q in A & u in A;
      then
A54:  f.(p,q) = d.(q',y)"\/"a by Def5;
A55:  f.(q,u) = d.(q',u') by Def5;
      d.(u',y) <= d.(u',q') "\/" d.(q',y) by A3,LATTICE5:def 8;
      then d.(u',y) <= d.(q',u') "\/" d.(q',y) by A2,LATTICE5:def 6;
      then d.(u',y)"\/"a <= (d.(q',y)"\/"d.(q',u'))"\/"a by WAYBEL_1:3;
      then d.(u',y)"\/"a <= (d.(q',y)"\/"a)"\/"d.(q',u') by LATTICE3:14;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A53,A54,A55,Def5;
    end;
  end;
A56: for p,q,u being Element of new_set2 A st p in B & q in A & u in B
  holds f.(p,u) <= f.(p,q) "\/" f.(q,u)
  proof
    let p,q,u be Element of new_set2 A;
    assume
A57: p in B & q in A & u in B;
    per cases by A57,TARSKI:def 2;
    suppose q in A & p = {A} & u = {A};
      then f.(p,u) = Bottom L by Def5;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by YELLOW_0:44;
    end;
    suppose
A58:  q in A & p = {A} & u = {{A}};
      then reconsider q' = q as Element of A;
A59:  f.(p,u) = (d.(x,y)"\/"a)"/\"b by A58,Def5;
      f.(p,q) = d.(q',x)"\/"a by A58,Def5;
      then
A60:  f.(p,q) "\/" f.(q,u) = d.(q',x)"\/"a"\/"(d.(q',y)"\/"a) by A58,Def5
        .= d.(q',x)"\/"(d.(q',y)"\/"a)"\/"a by LATTICE3:14
        .= d.(q',x)"\/"d.(q',y)"\/"a"\/"a by LATTICE3:14
        .= d.(q',x)"\/"d.(q',y)"\/"(a"\/"a) by LATTICE3:14
        .= d.(q',x)"\/"d.(q',y)"\/"a by YELLOW_5:1;
      d.(q',x) = d.(x,q') by A2,LATTICE5:def 6;
      then d.(x,y) <= d.(q',x)"\/"d.(q',y) by A3,LATTICE5:def 8;
      then
A61:  d.(x,y)"\/"a <= f.(p,q) "\/" f.(q,u) by A60,YELLOW_5:7;
      (d.(x,y)"\/"a)"/\"b <= d.(x,y)"\/"a by YELLOW_0:23;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A59,A61,ORDERS_2:26;
    end;
    suppose
A62:  q in A & p = {{A}} & u = {A};
      then reconsider q' = q as Element of A;
A63:  f.(p,u) = (d.(x,y)"\/"a)"/\"b by A62,Def5;
      f.(p,q) = d.(q',y)"\/"a by A62,Def5;
      then
A64:  f.(p,q) "\/" f.(q,u) = d.(q',x)"\/"a"\/"(d.(q',y)"\/"a) by A62,Def5
        .= d.(q',x)"\/"(d.(q',y)"\/"a)"\/"a by LATTICE3:14
        .= d.(q',x)"\/"d.(q',y)"\/"a"\/"a by LATTICE3:14
        .= d.(q',x)"\/"d.(q',y)"\/"(a"\/"a) by LATTICE3:14
        .= d.(q',x)"\/"d.(q',y)"\/"a by YELLOW_5:1;
      d.(q',x) = d.(x,q') by A2,LATTICE5:def 6;
      then d.(x,y) <= d.(q',x)"\/"d.(q',y) by A3,LATTICE5:def 8;
      then
A65:  d.(x,y)"\/"a <= f.(p,q) "\/" f.(q,u) by A64,YELLOW_5:7;
      (d.(x,y)"\/"a)"/\"b <= d.(x,y)"\/"a by YELLOW_0:23;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A63,A65,ORDERS_2:26;
    end;
    suppose q in A & p = {{A}} & u = {{A}};
      then f.(p,u) = Bottom L by Def5;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by YELLOW_0:44;
    end;
  end;
A66: for p,q,u being Element of new_set2 A st p in B & q in B & u in A
  holds f.(p,u) <= f.(p,q) "\/" f.(q,u)
  proof
    let p,q,u be Element of new_set2 A;
    assume
A67: p in B & q in B & u in A;
    per cases by A67,TARSKI:def 2;
    suppose
A68:  u in A & q = {A} & p = {A};
      then f.(p,q)"\/"f.(q,u) = Bottom L"\/"f.(q,u) by Def5
        .= f.(p,u) by A68,WAYBEL_1:4;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u);
    end;
    suppose
A69:  u in A & q = {A} & p = {{A}};
      then reconsider u' = u as Element of A;
A70:  f.(p,q) = (d.(x,y)"\/"a)"/\"b by A69,Def5;
A71:  f.(q,u) = d.(u',x)"\/"a by A69,Def5;
      a <= a "\/" d.(x,y) by YELLOW_0:22;
      then
A72:  a"\/"((d.(x,y)"\/"a)"/\"b) = (a"\/"b)"/\"(a"\/"
      d.(x,y)) by A1,YELLOW11:def 3;
A73:  d.(x,y)"\/"a = (d.(x,y)"\/"a)"/\"(d.(x,y)"\/"a) by YELLOW_5:2;
      a <= a;
      then d.(x,y)"\/"a <= (a"\/"b)"\/"a by A4,YELLOW_3:3;
      then (d.(x,y)"\/"a)"/\"(d.(x,y)"\/"a)
      <= (d.(x,y)"\/"a)"/\"((a"\/"b)"\/"a) by YELLOW_5:6;
      then
A74:  d.(u',x)"\/"((d.(x,y)"\/"a)"/\"(d.(x,y)"\/"a))
      <= d.(u',x)"\/"((d.(x,y)"\/"a)"/\"((a"\/"b)"\/" a)) by WAYBEL_1:3;
      f.(p,q) "\/" f.(q,u) = d.(u',x)"\/"((a"\/"b)"/\"(a"\/"
      d.(x,y))) by A70,A71,A72,LATTICE3:14
        .= d.(u',x)"\/"((d.(x,y)"\/"a)"/\"((a"\/"a)"\/"b)) by YELLOW_5:1
        .= d.(u',x)"\/"((d.(x,y)"\/"a)"/\"((a"\/"b)"\/" a)) by LATTICE3:14;
      then
A75:  (d.(u',x)"\/"d.(x,y))"\/"a <= f.(p,q) "\/" f.(q,u) by A73,A74,LATTICE3:14
      ;
      d.(u',y) <= d.(u',x)"\/"d.(x,y) by A3,LATTICE5:def 8;
      then d.(u',y)"\/"a <= (d.(u',x)"\/"d.(x,y))"\/"a by YELLOW_5:7;
      then d.(u',y)"\/"a <= f.(p,q) "\/" f.(q,u) by A75,ORDERS_2:26;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A69,Def5;
    end;
    suppose
A76:  u in A & q = {{A}} & p = {A};
      then reconsider u' = u as Element of A;
A77:  f.(p,q) = (d.(x,y)"\/"a)"/\"b by A76,Def5;
A78:  f.(q,u) = d.(u',y)"\/"a by A76,Def5;
      a <= a "\/" d.(x,y) by YELLOW_0:22;
      then
A79:  a"\/"((d.(x,y)"\/"a)"/\"b) = (a"\/"b)"/\"(a"\/"
      d.(x,y)) by A1,YELLOW11:def 3;
A80:  d.(x,y)"\/"a = (d.(x,y)"\/"a)"/\"(d.(x,y)"\/"a) by YELLOW_5:2;
      a <= a;
      then d.(x,y)"\/"a <= (a"\/"b)"\/"a by A4,YELLOW_3:3;
      then (d.(x,y)"\/"a)"/\"(d.(x,y)"\/"a)
      <= (d.(x,y)"\/"a)"/\"((a"\/"b)"\/"a) by YELLOW_5:6;
      then
A81:  d.(u',y)"\/"((d.(x,y)"\/"a)"/\"(d.(x,y)"\/"a))
      <= d.(u',y)"\/"((d.(x,y)"\/"a)"/\"((a"\/"b)"\/" a)) by WAYBEL_1:3;
      f.(p,q) "\/" f.(q,u) = d.(u',y)"\/"((a"\/"b)"/\"(a"\/"
      d.(x,y))) by A77,A78,A79,LATTICE3:14
        .= d.(u',y)"\/"((d.(x,y)"\/"a)"/\"((a"\/"a)"\/"b)) by YELLOW_5:1
        .= d.(u',y)"\/"((d.(x,y)"\/"a)"/\"((a"\/"b)"\/" a)) by LATTICE3:14;
      then
A82:  (d.(u',y)"\/"d.(x,y))"\/"a <= f.(p,q) "\/" f.(q,u) by A80,A81,LATTICE3:14
      ;
      d.(y,x) = d.(x,y) by A2,LATTICE5:def 6;
      then d.(u',x) <= d.(u',y)"\/"d.(x,y) by A3,LATTICE5:def 8;
      then d.(u',x)"\/"a <= (d.(u',y)"\/"d.(x,y))"\/"a by YELLOW_5:7;
      then d.(u',x)"\/"a <= f.(p,q) "\/" f.(q,u) by A82,ORDERS_2:26;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A76,Def5;
    end;
    suppose
A83:  u in A & q = {{A}} & p = {{A}};
      then f.(p,q)"\/"f.(q,u) = Bottom L"\/"f.(q,u) by Def5
        .= f.(p,u) by A83,WAYBEL_1:4;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u);
    end;
  end;
A84: for p,q,u being Element of new_set2 A st p in B & q in B & u in B
  holds f.(p,u) <= f.(p,q) "\/" f.(q,u)
  proof
    let p,q,u be Element of new_set2 A;
    assume
A85: p in B & q in B & u in B;
    per cases by A85,TARSKI:def 2;
    suppose
A86:  p = {A} & q = {A} & u = {A};
      Bottom L <= f.(p,q) "\/" f.(q,u) by YELLOW_0:44;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A86,Def5;
    end;
    suppose
A87:  p = {A} & q = {A} & u = {{A}};
      then f.(p,q) "\/" f.(q,u) = Bottom L "\/" f.(p,u) by Def5
        .= Bottom L "\/" ((d.(x,y)"\/"a)"/\"b) by A87,Def5
        .= ((d.(x,y)"\/"a)"/\"b) by WAYBEL_1:4;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A87,Def5;
    end;
    suppose
A88:  p = {A} & q = {{A}} & u = {A};
      Bottom L <= f.(p,q) "\/" f.(q,u) by YELLOW_0:44;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A88,Def5;
    end;
    suppose
A89:  p = {A} & q = {{A}} & u = {{A}};
      then f.(p,q) "\/" f.(q,u) = ((d.(x,y)"\/"a)"/\"b)"\/"f.(q,u) by Def5
        .= Bottom L"\/"((d.(x,y)"\/"a)"/\"b) by A89,Def5
        .= ((d.(x,y)"\/"a)"/\"b) by WAYBEL_1:4;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A89,Def5;
    end;
    suppose
A90:  p = {{A}} & q = {A} & u = {A};
      then f.(p,q) "\/" f.(q,u) = ((d.(x,y)"\/"a)"/\"b)"\/" f.(q,u) by Def5
        .= Bottom L"\/"((d.(x,y)"\/"a)"/\"b) by A90,Def5
        .= ((d.(x,y)"\/"a)"/\"b) by WAYBEL_1:4
        .= f.(p,q) by A90,Def5;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A90;
    end;
    suppose
A91:  p = {{A}} & q = {A} & u = {{A}};
      Bottom L <= f.(p,q) "\/" f.(q,u) by YELLOW_0:44;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A91,Def5;
    end;
    suppose
A92:  p = {{A}} & q = {{A}} & u = {A};
      then f.(p,q) "\/" f.(q,u) = Bottom L"\/" f.(p,u) by Def5
        .= Bottom L"\/"((d.(x,y)"\/"a)"/\"b) by A92,Def5
        .= ((d.(x,y)"\/"a)"/\"b) by WAYBEL_1:4;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A92,Def5;
    end;
    suppose
A93:  p = {{A}} & q = {{A}} & u = {{A}};
      Bottom L <= f.(p,q) "\/" f.(q,u) by YELLOW_0:44;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A93,Def5;
    end;
  end;
  for p,q,u being Element of new_set2 A holds f.(p,u) <= f.(p,q) "\/" f. ( q,
  u )
  proof
    let p,q,u be Element of new_set2 A;
    per cases by XBOOLE_0:def 2;
    suppose p in A & q in A & u in A;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A5;
    end;
    suppose p in A & q in A & u in B;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A8;
    end;
    suppose p in A & q in B & u in A;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A18;
    end;
    suppose p in A & q in B & u in B;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A30;
    end;
    suppose p in B & q in A & u in A;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A48;
    end;
    suppose p in B & q in A & u in B;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A56;
    end;
    suppose p in B & q in B & u in A;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A66;
    end;
    suppose p in B & q in B & u in B;
      hence f.(p,u) <= f.(p,q) "\/" f.(q,u) by A84;
    end;
  end;
  hence f is u.t.i. by LATTICE5:def 8;
end;

canceled;

theorem Th14:
  for A being non empty set for L being lower-bounded LATTICE
  for d be BiFunction of A,L
  for q be Element of [:A,A,the carrier of L,the carrier of L:]
  holds d c= new_bi_fun2(d,q)
proof
  let A be non empty set;
  let L be lower-bounded LATTICE;
  let d be BiFunction of A,L;
  let q be Element of [:A,A,the carrier of L,the carrier of L:];
  set g = new_bi_fun2(d,q);
A1: dom d = [:A,A:] & dom g = [:new_set2 A,new_set2 A:] by FUNCT_2:def 1;
  A c= new_set2 A by XBOOLE_1:7;
  then
A2: dom d c= dom g by A1,ZFMISC_1:119;
  for z being set st z in dom d holds d.z = g.z
  proof
    let z be set;
    assume
A3: z in dom d;
    then consider x,y being set such that
A4: [x,y] = z by RELAT_1:def 1;
    reconsider x' = x, y' = y as Element of A by A3,A4,ZFMISC_1:106;
    d.[x,y] = d.(x',y')
      .= g.(x',y') by Def5
      .= g.[x,y];
    hence d.z = g.z by A4;
  end;
  hence d c= new_bi_fun2(d,q) by A2,GRFUNC_1:8;
end;

reserve x for set,
  C for Ordinal,
  L0 for T-Sequence;

definition
  let A be non empty set;
  let O be Ordinal;
  func ConsecutiveSet2(A,O) means
  :Def6:
  ex L0 being T-Sequence st it = last L0 & dom L0 = succ O & L0.{} = A &
  (for C being Ordinal st succ C in succ O holds L0.succ C = new_set2(L0.C)) &
  for C being Ordinal st C in succ O & C <> {} & C is_limit_ordinal
  holds L0.C = union rng (L0|C);
  correctness
  proof
    deffunc C(Ordinal,set) = new_set2 $2;
    deffunc D(set,T-Sequence) = union rng $2;
    thus (ex x,L0 st x = last L0 & dom L0 = succ O & L0.{} = A &
    (for C st succ C in succ O holds L0.succ C = C(C,L0.C)) &
    for C st C in succ O & C <> {} & C is_limit_ordinal
    holds L0.C = D(C,L0|C) ) & for x1,x2 being set st
    (ex L0 st x1 = last L0 & dom L0 = succ O & L0.{} = A &
    (for C st succ C in succ O holds L0.succ C = C(C,L0.C)) &
    for C st C in succ O & C <> {} & C is_limit_ordinal
    holds L0.C = D(C,L0|C) ) &
    (ex L0 st x2 = last L0 & dom L0 = succ O & L0.{} = A &
    (for C st succ C in succ O holds L0.succ C = C(C,L0.C)) &
    for C st C in succ O & C <> {} & C is_limit_ordinal
    holds L0.C = D(C,L0|C) ) holds x1 = x2 from ORDINAL2:sch 7;
  end;
end;

theorem Th15:
  for A being non empty set holds ConsecutiveSet2(A,{}) = A
proof
  let A be non empty set;
  deffunc C(Ordinal,set) = new_set2 $2;
  deffunc D(set,T-Sequence) = union rng $2;
  deffunc F(Ordinal) = ConsecutiveSet2(A,$1);
A1: for O being Ordinal, It being set holds It = F(O) iff
  ex L0 being T-Sequence st It = last L0 & dom L0 = succ O & L0.{} = A &
  (for C being Ordinal st succ C in succ O holds L0.succ C = C(C,L0.C)) &
  for C being Ordinal st C in succ O & C <> {} & C is_limit_ordinal
  holds L0.C = D(C,L0|C) by Def6;
  thus F({}) = A from ORDINAL2:sch 8(A1);
end;

theorem Th16:
  for A being non empty set for O being Ordinal
  holds ConsecutiveSet2(A,succ O) = new_set2 ConsecutiveSet2(A,O)
proof
  let A be non empty set;
  let O be Ordinal;
  deffunc C(Ordinal,set) = new_set2 $2;
  deffunc D(set,T-Sequence) = union rng $2;
  deffunc F(Ordinal) = ConsecutiveSet2(A,$1);
A1: for O being Ordinal, It being set holds It = F(O) iff
  ex L0 being T-Sequence st It = last L0 & dom L0 = succ O & L0.{} = A &
  (for C being Ordinal st succ C in succ O holds L0.succ C = C(C,L0.C)) &
  for C being Ordinal st C in succ O & C <> {} & C is_limit_ordinal
  holds L0.C = D(C,L0|C) by Def6;
  for O being Ordinal holds F(succ O) = C(O,F(O)) from ORDINAL2:sch 9(A1);
  hence thesis;
end;

theorem Th17:
  for A being non empty set for O being Ordinal for T being T-Sequence
  holds O <> {} & O is_limit_ordinal & dom T = O & (for O1 being Ordinal
  st O1 in O holds T.O1 = ConsecutiveSet2(A,O1))
  implies ConsecutiveSet2(A,O) = union rng T
proof
  let A be non empty set;
  let O be Ordinal;
  let T be T-Sequence;
  deffunc C(Ordinal,set) = new_set2 $2;
  deffunc D(set,T-Sequence) = union rng $2;
  deffunc F(Ordinal) = ConsecutiveSet2(A,$1);
  assume that
A1: O <> {} & O is_limit_ordinal and
A2: dom T = O and
A3: for O1 being Ordinal st O1 in O holds T.O1 = F(O1);
A4: for O being Ordinal, It being set holds It = F(O) iff
  ex L0 being T-Sequence st It = last L0 & dom L0 = succ O & L0.{} = A &
  (for C being Ordinal st succ C in succ O holds L0.succ C = C(C,L0.C)) &
  for C being Ordinal st C in succ O & C <> {} & C is_limit_ordinal
  holds L0.C = D(C,L0|C) by Def6;
  thus F(O) = D(O,T) from ORDINAL2:sch 10(A4,A1,A2,A3);
end;

reserve O1,O2 for Ordinal;

registration
  let A be non empty set;
  let O be Ordinal;
  cluster ConsecutiveSet2(A,O) -> non empty;
  coherence
  proof
    defpred X[Ordinal] means ConsecutiveSet2(A,$1) is non empty;
A1: X[{}] by Th15;
A2: for O1 being Ordinal st X[O1] holds X[succ O1]
    proof
      let O1 be Ordinal;
      assume ConsecutiveSet2(A,O1) is non empty;
      ConsecutiveSet2(A,succ O1) = new_set2 ConsecutiveSet2(A,O1) by Th16;
      hence ConsecutiveSet2(A,succ O1) is non empty;
    end;
A3: for O1 st O1 <> {} & O1 is_limit_ordinal & for O2 st O2 in O1 holds X[O2]
    holds X[O1]
    proof
      let O1 be Ordinal;
      assume
A4:   O1 <> {} & O1 is_limit_ordinal & for O2 being Ordinal st O2 in O1
      holds ConsecutiveSet2(A,O2) is non empty;
      deffunc U(Ordinal) = ConsecutiveSet2(A,$1);
      consider Ls being T-Sequence such that
A5:   dom Ls = O1 & for O2 being Ordinal st O2 in O1 holds
      Ls.O2 = U(O2) from ORDINAL2:sch 2;
A6:   ConsecutiveSet2(A,O1) = union rng Ls by A4,A5,Th17;
A7:   {} in O1 by A4,ORDINAL3:10;
      then Ls.{} = ConsecutiveSet2(A,{}) by A5
        .= A by Th15;
      then A in rng Ls by A5,A7,FUNCT_1:def 5;
      then
A8:   A c= ConsecutiveSet2(A,O1) by A6,ZFMISC_1:92;
      consider x being set;
      thus ConsecutiveSet2(A,O1) is non empty by A8;
    end;
    for O being Ordinal holds X[O] from ORDINAL2:sch 1(A1,A2,A3);
    hence thesis;
  end;
end;

theorem Th18:
  for A being non empty set
  for O being Ordinal holds A c= ConsecutiveSet2(A,O)
proof
  let A be non empty set;
  let O be Ordinal;
  defpred X[Ordinal] means A c= ConsecutiveSet2(A,$1);
A1: X[{}] by Th15;
A2: for O1 being Ordinal st X[O1] holds X[succ O1]
  proof
    let O1 be Ordinal;
    assume
A3: A c= ConsecutiveSet2(A,O1);
    ConsecutiveSet2(A,succ O1) = new_set2 ConsecutiveSet2(A,O1) by Th16;
    then ConsecutiveSet2(A,O1) c= ConsecutiveSet2(A,succ O1) by XBOOLE_1:7;
    hence A c= ConsecutiveSet2(A,succ O1) by A3,XBOOLE_1:1;
  end;
A4: for O1 st O1 <> {} & O1 is_limit_ordinal & for O2 st O2 in O1 holds X[O2]
  holds X[O1]
  proof
    let O2 be Ordinal;
    assume
A5: O2 <> {} & O2 is_limit_ordinal &
    for O1 be Ordinal st O1 in O2 holds A c= ConsecutiveSet2(A,O1);
    deffunc U(Ordinal) = ConsecutiveSet2(A,$1);
    consider Ls being T-Sequence such that
A6: dom Ls = O2 & for O1 being Ordinal st O1 in O2 holds
    Ls.O1 = U(O1) from ORDINAL2:sch 2;
A7: ConsecutiveSet2(A,O2) = union rng Ls by A5,A6,Th17;
A8: {} in O2 by A5,ORDINAL3:10;
    then Ls.{} = ConsecutiveSet2(A,{}) by A6
      .= A by Th15;
    then A in rng Ls by A6,A8,FUNCT_1:def 5;
    hence A c= ConsecutiveSet2(A,O2) by A7,ZFMISC_1:92;
  end;
  for O being Ordinal holds X[O] from ORDINAL2:sch 1(A1,A2,A4);
  hence thesis;
end;

definition
  let A be non empty set;
  let L be lower-bounded LATTICE;
  let d be BiFunction of A,L;
  let q be QuadrSeq of d;
  let O be Ordinal;
  assume
A1: O in dom q;
  func Quadr2(q,O) -> Element of [:ConsecutiveSet2(A,O),ConsecutiveSet2(A,O),
  the carrier of L,the carrier of L:] equals
  :Def7:
  q.O;
  correctness
  proof
    q.O in rng q by A1,FUNCT_1:def 5;
    then q.O in {[x,y,a,b] where x is Element of A, y is Element of A,
    a is Element of L, b is Element of L: d.(x,y) <= a"\/"b}
    by LATTICE5:def 14;
    then consider x,y be Element of A, a,b be Element of L such that
A2: q.O = [x,y,a,b] & d.(x,y) <= a"\/"b;
A3: x in A & y in A;
    A c= ConsecutiveSet2(A,O) by Th18;
    then reconsider x,y as Element of ConsecutiveSet2(A,O) by A3;
    reconsider a,b as Element of L;
    reconsider z = [x,y,a,b] as Element of
    [:ConsecutiveSet2(A,O),ConsecutiveSet2(A,O),
    the carrier of L,the carrier of L:];
    z = q.O by A2;
    hence thesis;
  end;
end;

definition
  let A be non empty set;
  let L be lower-bounded LATTICE;
  let d be BiFunction of A,L;
  let q be QuadrSeq of d;
  let O be Ordinal;
  func ConsecutiveDelta2(q,O) means
  :Def8:
  ex L0 being T-Sequence st it = last L0 & dom L0 = succ O & L0.{} = d &
  (for C being Ordinal st succ C in succ O holds
  L0.succ C = new_bi_fun2(BiFun(L0.C,ConsecutiveSet2(A,C),L),Quadr2(q,C))) &
  for C being Ordinal st C in succ O & C <> {} &
  C is_limit_ordinal holds L0.C = union rng(L0|C);
  correctness
  proof
    deffunc C(Ordinal,set) =
    new_bi_fun2(BiFun($2,ConsecutiveSet2(A,$1),L),Quadr2(q,$1));
    deffunc D(set,T-Sequence) = union rng $2;
    thus (ex x,L0 st x = last L0 & dom L0 = succ O & L0.{} = d &
    (for C st succ C in succ O holds L0.succ C = C(C,L0.C)) &
    for C st C in succ O & C <> {} & C is_limit_ordinal
    holds L0.C = D(C,L0|C)) & for x1,x2 being set st
    (ex L0 st x1 = last L0 & dom L0 = succ O & L0.{} = d &
    (for C st succ C in succ O holds L0.succ C = C(C,L0.C)) &
    for C st C in succ O & C <> {} & C is_limit_ordinal
    holds L0.C = D(C,L0|C) ) &
    (ex L0 st x2 = last L0 & dom L0 = succ O & L0.{} = d &
    (for C st succ C in succ O holds L0.succ C = C(C,L0.C)) &
    for C st C in succ O & C <> {} & C is_limit_ordinal
    holds L0.C = D(C,L0|C) ) holds x1 = x2 from ORDINAL2:sch 7;
  end;
end;

theorem Th19:
  for A being non empty set for L be lower-bounded LATTICE
  for d being BiFunction of A,L
  for q being QuadrSeq of d holds ConsecutiveDelta2(q,{}) = d
proof
  let A be non empty set;
  let L be lower-bounded LATTICE;
  let d be BiFunction of A,L;
  let q be QuadrSeq of d;
  deffunc C(Ordinal,set) =
  new_bi_fun2(BiFun($2,ConsecutiveSet2(A,$1),L),Quadr2(q,$1));
  deffunc D(set,T-Sequence) = union rng $2;
  deffunc F(Ordinal) = ConsecutiveDelta2(q,$1);
A1: for O being Ordinal, It being set holds It = F(O) iff
  ex L0 being T-Sequence st It = last L0 & dom L0 = succ O & L0.{} = d &
  (for C being Ordinal st succ C in succ O holds L0.succ C = C(C,L0.C)) &
  for C being Ordinal st C in succ O & C <> {} & C is_limit_ordinal
  holds L0.C = D(C,L0|C) by Def8;
  thus F({}) = d from ORDINAL2:sch 8(A1);
end;

theorem Th20:
  for A being non empty set for L be lower-bounded LATTICE
  for d be BiFunction of A,L for q being QuadrSeq of d
  for O being Ordinal holds
  ConsecutiveDelta2(q,succ O) = new_bi_fun2(BiFun(ConsecutiveDelta2(q,O),
  ConsecutiveSet2(A,O),L),Quadr2(q,O))
proof
  let A be non empty set;
  let L be lower-bounded LATTICE;
  let d be BiFunction of A,L;
  let q be QuadrSeq of d;
  let O be Ordinal;
  deffunc C(Ordinal,set) =
  new_bi_fun2(BiFun($2,ConsecutiveSet2(A,$1),L),Quadr2(q,$1));
  deffunc D(set,T-Sequence) = union rng $2;
  deffunc F(Ordinal) = ConsecutiveDelta2(q,$1);
A1: for O being Ordinal, It being set holds It = F(O) iff
  ex L0 being T-Sequence st It = last L0 & dom L0 = succ O & L0.{} = d &
  (for C being Ordinal st succ C in succ O holds L0.succ C = C(C,L0.C)) &
  for C being Ordinal st C in succ O & C <> {} & C is_limit_ordinal
  holds L0.C = D(C,L0|C) by Def8;
  for O being Ordinal holds F(succ O) = C(O,F(O)) from ORDINAL2:sch 9(A1);
  hence thesis;
end;

theorem Th21:
  for A being non empty set for L be lower-bounded LATTICE
  for d be BiFunction of A,L for q being QuadrSeq of d for T being T-Sequence
  for O being Ordinal holds O <> {} & O is_limit_ordinal & dom T = O &
  (for O1 being Ordinal st O1 in O holds T.O1 = ConsecutiveDelta2(q,O1))
  implies ConsecutiveDelta2(q,O) = union rng T
proof
  let A be non empty set;
  let L be lower-bounded LATTICE;
  let d be BiFunction of A,L;
  let q be QuadrSeq of d;
  let T be T-Sequence;
  let O be Ordinal;
  deffunc C(Ordinal,set) =
  new_bi_fun2(BiFun($2,ConsecutiveSet2(A,$1),L),Quadr2(q,$1));
  deffunc D(set,T-Sequence) = union rng $2;
  deffunc F(Ordinal) = ConsecutiveDelta2(q,$1);
  assume that
A1: O <> {} & O is_limit_ordinal and
A2: dom T = O and
A3: for O1 being Ordinal st O1 in O holds T.O1 = F(O1);
A4: for O being Ordinal, It being set holds It = F(O) iff
  ex L0 being T-Sequence st It = last L0 & dom L0 = succ O & L0.{} = d &
  (for C being Ordinal st succ C in succ O holds L0.succ C = C(C,L0.C) ) &
  for C being Ordinal st C in succ O & C <> {} & C is_limit_ordinal
  holds L0.C = D(C,L0|C) by Def8;
  thus F(O) = D(O,T) from ORDINAL2:sch 10(A4,A1,A2,A3);
end;

theorem Th22:
  for A being non empty set for O,O1,O2 being Ordinal
  holds O1 c= O2 implies ConsecutiveSet2(A,O1) c= ConsecutiveSet2(A,O2)
proof
  let A be non empty set;
  let O,O1,02 be Ordinal;
  defpred X[Ordinal] means O1 c= $1 implies
  ConsecutiveSet2(A,O1) c= ConsecutiveSet2(A,$1);
A1: X[{}];
A2: for O1 being Ordinal st X[O1] holds X[succ O1]
  proof
    let O2 be Ordinal;
    assume
A3: O1 c= O2 implies ConsecutiveSet2(A,O1) c= ConsecutiveSet2(A,O2);
    assume
A4: O1 c= succ O2;
    per cases;
    suppose O1 = succ O2;
      hence ConsecutiveSet2(A,O1) c= ConsecutiveSet2(A,succ O2);
    end;
    suppose O1 <> succ O2;
      then O1 c< succ O2 by A4,XBOOLE_0:def 8;
      then
A5:   O1 in succ O2 by ORDINAL1:21;
      ConsecutiveSet2(A,O2) c= new_set2 ConsecutiveSet2(A,O2) by XBOOLE_1:7;
      then ConsecutiveSet2(A,O1) c=
      new_set2 ConsecutiveSet2(A,O2) by A3,A5,ORDINAL1:34,XBOOLE_1:1;
      hence ConsecutiveSet2(A,O1) c= ConsecutiveSet2(A,succ O2) by Th16;
    end;
  end;
A6: for O1 st O1 <> {} & O1 is_limit_ordinal & for O2 st O2 in O1 holds X[O2]
  holds X[O1]
  proof
    let O2 be Ordinal;
    assume
A7: O2 <> {} & O2 is_limit_ordinal &
    for O3 being Ordinal st O3 in O2 holds O1 c= O3 implies
    ConsecutiveSet2(A,O1) c= ConsecutiveSet2(A,O3);
    assume
A8: O1 c= O2;
    deffunc U(Ordinal) = ConsecutiveSet2(A,$1);
    consider L being T-Sequence such that
A9: dom L = O2 & for O3 being Ordinal st O3 in O2 holds
    L.O3 = U(O3) from ORDINAL2:sch 2;
A10: ConsecutiveSet2(A,O2) = union rng L by A7,A9,Th17;
    per cases;
    suppose O1 = O2;
      hence ConsecutiveSet2(A,O1) c= ConsecutiveSet2(A,O2);
    end;
    suppose O1 <> O2;
      then
A11:  O1 c< O2 by A8,XBOOLE_0:def 8;
      then
A12:  O1 in O2 by ORDINAL1:21;
A13:  L.O1 = ConsecutiveSet2(A,O1) by A9,A11,ORDINAL1:21;
      L.O1 in rng L by A9,A12,FUNCT_1:def 5;
      hence ConsecutiveSet2(A,O1) c= ConsecutiveSet2(A,O2)
      by A10,A13,ZFMISC_1:92;
    end;
  end;
  for O being Ordinal holds X[O] from ORDINAL2:sch 1(A1,A2,A6);
  hence thesis;
end;

theorem Th23:
  for A being non empty set for L be lower-bounded LATTICE
  for d be BiFunction of A,L for q being QuadrSeq of d for O being Ordinal
  holds ConsecutiveDelta2(q,O) is BiFunction of ConsecutiveSet2(A,O),L
proof
  let A be non empty set;
  let L be lower-bounded LATTICE;
  let d be BiFunction of A,L;
  let q be QuadrSeq of d;
  let O be Ordinal;
  defpred X[Ordinal] means
  ConsecutiveDelta2(q,$1) is BiFunction of ConsecutiveSet2(A,$1),L;
A1: X[{}]
  proof
    ConsecutiveDelta2(q,{}) = d & ConsecutiveSet2(A,{}) = A by Th15,Th19;
    hence ConsecutiveDelta2(q,{}) is BiFunction of ConsecutiveSet2(A,{}),L;
  end;
A2: for O1 being Ordinal st X[O1] holds X[succ O1]
  proof
    let O1 be Ordinal;
    assume ConsecutiveDelta2(q,O1) is BiFunction of ConsecutiveSet2(A,O1),L;
    then reconsider CD = ConsecutiveDelta2(q,O1) as
    BiFunction of ConsecutiveSet2(A,O1),L;
A3: ConsecutiveSet2(A,succ O1) = new_set2 ConsecutiveSet2(A,O1) by Th16;
    ConsecutiveDelta2(q,succ O1) = new_bi_fun2(BiFun(ConsecutiveDelta2( q, O1
    ) , ConsecutiveSet2(A,O1),L),Quadr2(q,O1)) by Th20
      .= new_bi_fun2(CD,Quadr2(q,O1)) by LATTICE5:def 16;
    hence ConsecutiveDelta2(q,succ O1) is
    BiFunction of ConsecutiveSet2(A,succ O1),L by A3;
  end;
A4: for O1 st O1 <> {} & O1 is_limit_ordinal & for O2 st O2 in O1 holds X[O2]
  holds X[O1]
  proof
    let O1 be Ordinal;
    assume
A5: O1 <> {} & O1 is_limit_ordinal & for O2 be Ordinal st O2 in O1 holds
    ConsecutiveDelta2(q,O2) is BiFunction of ConsecutiveSet2(A,O2),L;
    deffunc U(Ordinal) = ConsecutiveDelta2(q,$1);
    consider Ls being T-Sequence such that
A6: dom Ls = O1 & for O2 being Ordinal st O2 in O1 holds
    Ls.O2 = U(O2) from ORDINAL2:sch 2;
A7: ConsecutiveDelta2(q,O1) = union rng Ls by A5,A6,Th21;
    deffunc U(Ordinal) = ConsecutiveSet2(A,$1);
    consider Ts being T-Sequence such that
A8: dom Ts = O1 & for O2 being Ordinal st O2 in O1 holds
    Ts.O2 = U(O2) from ORDINAL2:sch 2;
    set CS = ConsecutiveSet2(A,O1), Y = the carrier of L,
    X = [:ConsecutiveSet2(A,O1),ConsecutiveSet2(A,O1):], f = union rng Ls;
A9: for O,O2 being Ordinal st O c= O2 & O2 in dom Ls holds Ls.O c= Ls.O2
    proof
      let O be Ordinal;
      defpred X[Ordinal] means O c= $1 & $1 in dom Ls implies Ls.O c= Ls.$1;
A10:  X[{}];
A11:  for O1 being Ordinal st X[O1] holds X[succ O1]
      proof
        let O2 be Ordinal;
        assume
A12:    O c= O2 & O2 in dom Ls implies Ls.O c= Ls.O2;
        assume that
A13:    O c= succ O2 and
A14:    succ O2 in dom Ls;
        per cases;
        suppose O = succ O2;
          hence Ls.O c= Ls.succ O2;
        end;
        suppose O <> succ O2;
          then O c< succ O2 by A13,XBOOLE_0:def 8;
          then
A15:      O in succ O2 by ORDINAL1:21;
A16:      O2 in succ O2 by ORDINAL1:10;
          then O2 in dom Ls by A14,ORDINAL1:19;
          then reconsider Def8 = ConsecutiveDelta2(q,O2) as
          BiFunction of ConsecutiveSet2(A,O2),L by A5,A6;
          Ls.succ O2 = ConsecutiveDelta2(q,succ O2) by A6,A14
            .= new_bi_fun2(BiFun(ConsecutiveDelta2(q,O2),
          ConsecutiveSet2(A,O2),L),Quadr2(q,O2)) by Th20
            .= new_bi_fun2(Def8,Quadr2(q,O2)) by LATTICE5:def 16;
          then ConsecutiveDelta2(q,O2) c= Ls.succ O2 by Th14;
          then Ls.O2 c= Ls.succ O2 by A6,A14,A16,ORDINAL1:19;

hence Ls.O c= Ls.succ O2 by A12,A14,A15,A16,ORDINAL1:19,34,XBOOLE_1:1;
        end;
      end;
A17:  for O1 st O1 <> {} & O1 is_limit_ordinal & for O2 st O2 in O1
      holds X[O2] holds X[O1]
      proof
        let O2 be Ordinal;
        assume that
A18:    O2 <> {} & O2 is_limit_ordinal and
        for O3 be Ordinal st O3 in O2 holds O c= O3 & O3 in dom Ls
        implies Ls.O c= Ls.O3;
        assume that
A19:    O c= O2 and
A20:    O2 in dom Ls;
        deffunc U(Ordinal) = ConsecutiveDelta2(q,$1);
        consider Lt being T-Sequence such that
A21:    dom Lt = O2 & for O3 being Ordinal st O3 in O2 holds
        Lt.O3 = U(O3) from ORDINAL2:sch 2;
A22:    Ls.O2 = ConsecutiveDelta2(q,O2) by A6,A20
          .= union rng Lt by A18,A21,Th21;
        per cases;
        suppose O = O2;
          hence Ls.O c= Ls.O2;
        end;
        suppose O <> O2;
          then
A23:      O c< O2 by A19,XBOOLE_0:def 8;
          then
A24:      O in O2 by ORDINAL1:21;
          then Ls.O = ConsecutiveDelta2(q,O) by A6,A20,ORDINAL1:19
            .= Lt.O by A21,A23,ORDINAL1:21;
          then Ls.O in rng Lt by A21,A24,FUNCT_1:def 5;
          hence Ls.O c= Ls.O2 by A22,ZFMISC_1:92;
        end;
      end;
      thus for O being Ordinal holds X[O] from ORDINAL2:sch 1(A10,A11,A17);
    end;
    for x,y being set st x in rng Ls & y in rng Ls holds x,y are_c=-comparable
    proof
      let x,y be set;
      assume
A25:  x in rng Ls & y in rng Ls;
      then consider o1 being set such that
A26:  o1 in dom Ls & Ls.o1 = x by FUNCT_1:def 5;
      consider o2 being set such that
A27:  o2 in dom Ls & Ls.o2 = y by A25,FUNCT_1:def 5;
      reconsider o1' = o1, o2' = o2 as Ordinal by A26,A27;
      o1' c= o2' or o2' c= o1';
      then x c= y or y c= x by A9,A26,A27;
      hence thesis by XBOOLE_0:def 9;
    end;
    then
A28: rng Ls is c=-linear by ORDINAL1:def 9;
    rng Ls c= PFuncs(X,Y)
    proof
      let z be set;
      assume z in rng Ls;
      then consider o being set such that
A29:  o in dom Ls & z = Ls.o by FUNCT_1:def 5;
      reconsider o as Ordinal by A29;
      Ls.o = ConsecutiveDelta2(q,o) by A6,A29;
      then reconsider h = Ls.o as
      BiFunction of ConsecutiveSet2(A,o),L by A5,A6,A29;
A30:  dom h = [:ConsecutiveSet2(A,o),ConsecutiveSet2(A,o):] by FUNCT_2:def 1;
A31:  rng h c= Y;
      o c= O1 by A6,A29,ORDINAL1:def 2;
      then ConsecutiveSet2(A,o) c= ConsecutiveSet2(A,O1) by Th22;
      then dom h c= X by A30,ZFMISC_1:119;
      hence z in PFuncs(X,Y) by A29,A31,PARTFUN1:def 5;
    end;
    then f in PFuncs(X,Y) by A28,HAHNBAN:13;
    then consider g being Function such that
A32: f = g & dom g c= X & rng g c= Y by PARTFUN1:def 5;
    reconsider f as Function by A32;
    Ls is Function-yielding
    proof
      let x be set;
      assume
A33:  x in dom Ls;
      then reconsider o = x as Ordinal;
      Ls.o = ConsecutiveDelta2(q,o) by A6,A33;
      hence Ls.x is Function by A5,A6,A33;
    end;
    then reconsider LsF = Ls as Function-yielding Function;
A34: dom f = union rng doms LsF by LATTICE5:1;
    reconsider o1 = O1 as non empty Ordinal by A5;
    set YY = { [:ConsecutiveSet2(A,O2),ConsecutiveSet2(A,O2):]
    where O2 is Element of o1 : not contradiction };
A35: rng doms Ls = YY
    proof
      thus rng doms Ls c= YY
      proof
        let Z be set;
        assume Z in rng doms Ls;
        then consider o being set such that
A36:    o in dom doms Ls & Z = (doms Ls).o by FUNCT_1:def 5;
A37:    o in dom LsF by A36,FUNCT_6:89;
        then reconsider o' = o as Element of o1 by A6;
        Ls.o' = ConsecutiveDelta2(q,o') by A6;
        then reconsider ls = Ls.o' as
        BiFunction of ConsecutiveSet2(A,o'),L by A5;
        Z = dom ls by A36,A37,FUNCT_6:31
          .= [:ConsecutiveSet2(A,o'),ConsecutiveSet2(A,o'):] by FUNCT_2:def 1;
        hence Z in YY;
      end;
      let Z be set;
      assume Z in YY;
      then consider o being Element of o1 such that
A38:  Z = [:ConsecutiveSet2(A,o),ConsecutiveSet2(A,o):];
      o in dom LsF by A6;
      then
A39:  o in dom doms LsF by FUNCT_6:89;
      Ls.o = ConsecutiveDelta2(q,o) by A6;
      then reconsider ls = Ls.o as BiFunction of ConsecutiveSet2(A,o),L by A5;
      Z = dom ls by A38,FUNCT_2:def 1
        .= (doms Ls).o by A6,FUNCT_6:31;
      hence Z in rng doms Ls by A39,FUNCT_1:def 5;
    end;
    {} in O1 by A5,ORDINAL3:10;
    then reconsider RTs = rng Ts as non empty set by A8,FUNCT_1:12;
    for x,y being set st x in RTs & y in RTs holds x,y are_c=-comparable
    proof
      let x,y be set;
      assume
A40:  x in RTs & y in RTs;
      then consider o1 being set such that
A41:  o1 in dom Ts & Ts.o1 = x by FUNCT_1:def 5;
      consider o2 being set such that
A42:  o2 in dom Ts & Ts.o2 = y by A40,FUNCT_1:def 5;
      reconsider o1' = o1, o2' = o2 as Ordinal by A41,A42;
A43:  Ts.o1' = ConsecutiveSet2(A,o1') by A8,A41;
A44:  Ts.o2' = ConsecutiveSet2(A,o2') by A8,A42;
      o1' c= o2' or o2' c= o1';
      then x c= y or y c= x by A41,A42,A43,A44,Th22;
      hence thesis by XBOOLE_0:def 9;
    end;
    then
A45: RTs is c=-linear by ORDINAL1:def 9;
A46: YY = { [:a,a:] where a is Element of RTs : a in RTs }
    proof
      set XX = { [:a,a:] where a is Element of RTs : a in RTs };
      thus YY c= XX
      proof
        let Z be set;
        assume Z in YY;
        then consider o being Element of o1 such that
A47:    Z = [:ConsecutiveSet2(A,o),ConsecutiveSet2(A,o):];
        Ts.o = ConsecutiveSet2(A,o) by A8;
        then reconsider CoS = ConsecutiveSet2(A,o)
        as Element of RTs by A8,FUNCT_1:def 5;
        Z = [:CoS,CoS:] by A47;
        hence Z in XX;
      end;
      let Z be set;
      assume Z in XX;
      then consider a being Element of RTs such that
A48:  Z = [:a,a:] & a in RTs;
      consider o being set such that
A49:  o in dom Ts & a = Ts.o by FUNCT_1:def 5;
      reconsider o' = o as Ordinal by A49;
A50:  a = ConsecutiveSet2(A,o') by A8,A49;
      consider O being Element of o1 such that
A51:  O = o' by A8,A49;
      thus Z in YY by A48,A50,A51;
    end;
    X = [:union rng Ts, ConsecutiveSet2(A,O1):] by A5,A8,Th17
      .= [:union RTs, union RTs :] by A5,A8,Th17
      .= dom f by A34,A35,A45,A46,LATTICE5:3;
    hence ConsecutiveDelta2(q,O1) is BiFunction of CS,L by A7,A32,FUNCT_2:def 1
    ,RELSET_1:11;
  end;
  for O being Ordinal holds X[O] from ORDINAL2:sch 1(A1,A2,A4);
  hence thesis;
end;

definition
  let A be non empty set;
  let L be lower-bounded LATTICE;
  let d be BiFunction of A,L;
  let q be QuadrSeq of d;
  let O be Ordinal;
  redefine func ConsecutiveDelta2(q,O) -> BiFunction of
  ConsecutiveSet2(A,O),L;
  coherence by Th23;
end;

theorem Th24:
  for A being non empty set for L be lower-bounded LATTICE
  for d be BiFunction of A,L for q being QuadrSeq of d
  for O being Ordinal holds d c= ConsecutiveDelta2(q,O)
proof
  let A be non empty set;
  let L be lower-bounded LATTICE;
  let d be BiFunction of A,L;
  let q be QuadrSeq of d;
  let O be Ordinal;
  defpred X[Ordinal] means d c= ConsecutiveDelta2(q,$1);
A1: X[{}] by Th19;
A2: for O1 being Ordinal st X[O1] holds X[succ O1]
  proof
    let O1 be Ordinal;
    assume
A3: d c= ConsecutiveDelta2(q,O1);
    ConsecutiveDelta2(q,succ O1) = new_bi_fun2(BiFun(ConsecutiveDelta2( q, O1
    ) , ConsecutiveSet2(A,O1),L),Quadr2(q,O1)) by Th20
      .= new_bi_fun2(ConsecutiveDelta2(q,O1),Quadr2(q,O1)) by LATTICE5:def 16;
    then ConsecutiveDelta2(q,O1) c= ConsecutiveDelta2(q,succ O1) by Th14;
    hence d c= ConsecutiveDelta2(q,succ O1) by A3,XBOOLE_1:1;
  end;
A4: for O1 st O1 <> {} & O1 is_limit_ordinal & for O2 st O2 in O1 holds X[O2]
  holds X[O1]
  proof
    let O2 be Ordinal;
    assume
A5: O2 <> {} & O2 is_limit_ordinal &
    for O1 be Ordinal st O1 in O2 holds d c= ConsecutiveDelta2(q,O1);
    deffunc U(Ordinal) = ConsecutiveDelta2(q,$1);
    consider Ls being T-Sequence such that
A6: dom Ls = O2 & for O1 being Ordinal st O1 in O2 holds
    Ls.O1 = U(O1) from ORDINAL2:sch 2;
A7: ConsecutiveDelta2(q,O2) = union rng Ls by A5,A6,Th21;
A8: {} in O2 by A5,ORDINAL3:10;
    then Ls.{} = ConsecutiveDelta2(q,{}) by A6
      .= d by Th19;
    then d in rng Ls by A6,A8,FUNCT_1:def 5;
    hence d c= ConsecutiveDelta2(q,O2) by A7,ZFMISC_1:92;
  end;
  for O being Ordinal holds X[O] from ORDINAL2:sch 1(A1,A2,A4);
  hence thesis;
end;

theorem Th25:
  for A being non empty set for L be lower-bounded LATTICE
  for d being BiFunction of A,L for O1,O2 being Ordinal
  for q being QuadrSeq of d st O1 c= O2
  holds ConsecutiveDelta2(q,O1) c= ConsecutiveDelta2(q,O2)
proof
  let A be non empty set;
  let L be lower-bounded LATTICE;
  let d be BiFunction of A,L;
  let O1,O2 be Ordinal;
  let q be QuadrSeq of d;
  defpred X[Ordinal] means
  O1 c= $1 implies ConsecutiveDelta2(q,O1) c= ConsecutiveDelta2(q,$1);
A1: X[{}];
A2: for O1 being Ordinal st X[O1] holds X[succ O1]
  proof
    let O2 be Ordinal;
    assume
A3: O1 c= O2 implies ConsecutiveDelta2(q,O1) c= ConsecutiveDelta2(q,O2);
    assume
A4: O1 c= succ O2;
    per cases;
    suppose O1 = succ O2;
      hence ConsecutiveDelta2(q,O1) c= ConsecutiveDelta2(q,succ O2);
    end;
    suppose O1 <> succ O2;
      then O1 c< succ O2 by A4,XBOOLE_0:def 8;
      then
A5:   O1 in succ O2 by ORDINAL1:21;
      ConsecutiveDelta2(q,succ O2)
      = new_bi_fun2(BiFun(ConsecutiveDelta2(q,O2),
      ConsecutiveSet2(A,O2),L),Quadr2(q,O2)) by Th20
        .= new_bi_fun2(ConsecutiveDelta2(q,O2),Quadr2(q,O2))
      by LATTICE5:def 16;
      then ConsecutiveDelta2(q,O2) c= ConsecutiveDelta2(q,succ O2) by Th14;
      hence ConsecutiveDelta2(q,O1) c=
      ConsecutiveDelta2(q,succ O2) by A3,A5,ORDINAL1:34,XBOOLE_1:1;
    end;
  end;
A6: for O1 st O1 <> {} & O1 is_limit_ordinal & for O2 st O2 in O1 holds X[O2]
  holds X[O1]
  proof
    let O2 be Ordinal;
    assume
A7: O2 <> {} & O2 is_limit_ordinal &
    for O3 be Ordinal st O3 in O2 holds O1 c= O3 implies
    ConsecutiveDelta2(q,O1) c= ConsecutiveDelta2(q,O3);
    assume
A8: O1 c= O2;
    deffunc U(Ordinal) = ConsecutiveDelta2(q,$1);
    consider L being T-Sequence such that
A9: dom L = O2 & for O3 being Ordinal st O3 in O2 holds
    L.O3 = U(O3) from ORDINAL2:sch 2;
A10: ConsecutiveDelta2(q,O2) = union rng L by A7,A9,Th21;
    per cases;
    suppose O1 = O2;
      hence ConsecutiveDelta2(q,O1) c= ConsecutiveDelta2(q,O2);
    end;
    suppose O1 <> O2;
      then
A11:  O1 c< O2 by A8,XBOOLE_0:def 8;
      then
A12:  O1 in O2 by ORDINAL1:21;
A13:  L.O1 = ConsecutiveDelta2(q,O1) by A9,A11,ORDINAL1:21;
      L.O1 in rng L by A9,A12,FUNCT_1:def 5;
      hence ConsecutiveDelta2(q,O1) c=
      ConsecutiveDelta2(q,O2) by A10,A13,ZFMISC_1:92;
    end;
  end;
  for O being Ordinal holds X[O] from ORDINAL2:sch 1(A1,A2,A6);
  hence thesis;
end;

theorem Th26:
  for A being non empty set for L be lower-bounded LATTICE
  for d be BiFunction of A,L st d is zeroed for q being QuadrSeq of d
  for O being Ordinal holds ConsecutiveDelta2(q,O) is zeroed
proof
  let A be non empty set;
  let L be lower-bounded LATTICE;
  let d be BiFunction of A,L;
  assume
A1: d is zeroed;
  let q be QuadrSeq of d;
  let O be Ordinal;
  defpred X[Ordinal] means ConsecutiveDelta2(q,$1) is zeroed;
A2: X[{}]
  proof
    let z be Element of ConsecutiveSet2(A,{});
    reconsider z' = z as Element of A by Th15;
    thus ConsecutiveDelta2(q,{}).(z,z) = d.(z',z') by Th19
      .= Bottom L by A1,LATTICE5:def 7;
  end;
A3: for O1 being Ordinal st X[O1] holds X[succ O1]
  proof
    let O1 be Ordinal;
    assume ConsecutiveDelta2(q,O1) is zeroed;
    then
A4: new_bi_fun2(ConsecutiveDelta2(q,O1),Quadr2(q,O1)) is zeroed by Th10;
    let z be Element of ConsecutiveSet2(A,succ O1);
    reconsider z' = z as Element of new_set2 ConsecutiveSet2(A,O1) by Th16;
    ConsecutiveDelta2(q,succ O1) = new_bi_fun2(BiFun(ConsecutiveDelta2(q, O1
    ) , ConsecutiveSet2(A,O1),L),Quadr2(q,O1)) by Th20
      .= new_bi_fun2(ConsecutiveDelta2(q,O1),Quadr2(q,O1)) by LATTICE5:def 16;
    hence ConsecutiveDelta2(q,succ O1).(z,z) =
    new_bi_fun2(ConsecutiveDelta2(q,O1),Quadr2(q,O1)).(z',z')
      .= Bottom L by A4,LATTICE5:def 7;
  end;
A5: for O1 st O1 <> {} & O1 is_limit_ordinal & for O2 st O2 in O1 holds X[O2]
  holds X[O1]
  proof
    let O2 be Ordinal;
    assume
A6: O2 <> {} & O2 is_limit_ordinal &
    for O1 be Ordinal st O1 in O2 holds ConsecutiveDelta2(q,O1) is zeroed;
    deffunc U(Ordinal) = ConsecutiveDelta2(q,$1);
    consider Ls being T-Sequence such that
A7: dom Ls = O2 & for O1 being Ordinal st O1 in O2 holds
    Ls.O1 = U(O1) from ORDINAL2:sch 2;
A8: ConsecutiveDelta2(q,O2) = union rng Ls by A6,A7,Th21;
    deffunc U(Ordinal) = ConsecutiveSet2(A,$1);
    consider Ts being T-Sequence such that
A9: dom Ts = O2 & for O1 being Ordinal st O1 in O2 holds
    Ts.O1 = U(O1) from ORDINAL2:sch 2;
A10: ConsecutiveSet2(A,O2) = union rng Ts by A6,A9,Th17;
    set CS = ConsecutiveSet2(A,O2);
    reconsider f = union rng Ls as BiFunction of CS,L by A8;
    f is zeroed
    proof
      let x be Element of CS;
      consider y being set such that
A11:  x in y & y in rng Ts by A10,TARSKI:def 4;
      consider o being set such that
A12:  o in dom Ts & y = Ts.o by A11,FUNCT_1:def 5;
      reconsider o as Ordinal by A12;
A13:  x in ConsecutiveSet2(A,o) by A9,A11,A12;
A14:  Ls.o = ConsecutiveDelta2(q,o) by A7,A9,A12;
      then reconsider h = Ls.o as BiFunction of ConsecutiveSet2(A,o),L;
A15:  h is zeroed
      proof
        let z be Element of ConsecutiveSet2(A,o);
A16:    ConsecutiveDelta2(q,o) is zeroed by A6,A9,A12;
        thus h.(z,z) = ConsecutiveDelta2(q,o).(z,z) by A7,A9,A12
          .= Bottom L by A16,LATTICE5:def 7;
      end;
      ConsecutiveDelta2(q,o) in rng Ls by A7,A9,A12,A14,FUNCT_1:def 5;
      then
A17:  h c= f by A14,ZFMISC_1:92;
      reconsider x' = x as Element of ConsecutiveSet2(A,o) by A9,A11,A12;
      dom h = [:ConsecutiveSet2(A,o),ConsecutiveSet2(A,o):] by FUNCT_2:def 1;
      then [x,x] in dom h by A13,ZFMISC_1:106;
      hence f.(x,x) = h.(x',x') by A17,GRFUNC_1:8
        .= Bottom L by A15,LATTICE5:def 7;
    end;
    hence ConsecutiveDelta2(q,O2) is zeroed by A6,A7,Th21;
  end;
  for O being Ordinal holds X[O] from ORDINAL2:sch 1(A2,A3,A5);
  hence thesis;
end;

theorem Th27:
  for A being non empty set for L be lower-bounded LATTICE
  for d be BiFunction of A,L st d is symmetric for q being QuadrSeq of d
  for O being Ordinal holds ConsecutiveDelta2(q,O) is symmetric
proof
  let A be non empty set;
  let L be lower-bounded LATTICE;
  let d be BiFunction of A,L;
  assume
A1: d is symmetric;
  let q be QuadrSeq of d;
  let O be Ordinal;
  defpred X[Ordinal] means ConsecutiveDelta2(q,$1) is symmetric;
A2: X[{}]
  proof
    let x,y be Element of ConsecutiveSet2(A,{});
    reconsider x' = x, y' = y as Element of A by Th15;
    thus ConsecutiveDelta2(q,{}).(x,y) = d.(x',y') by Th19
      .= d.(y',x') by A1,LATTICE5:def 6
      .= ConsecutiveDelta2(q,{}).(y,x) by Th19;
  end;
A3: for O1 being Ordinal st X[O1] holds X[succ O1]
  proof
    let O1 be Ordinal;
    assume ConsecutiveDelta2(q,O1) is symmetric;
    then
A4: new_bi_fun2(ConsecutiveDelta2(q,O1),Quadr2(q,O1)) is symmetric by Th11;
    let x,y be Element of ConsecutiveSet2(A,succ O1);
    reconsider x'=x, y'=y as Element of new_set2 ConsecutiveSet2(A,O1)
    by Th16;
A5: ConsecutiveDelta2(q,succ O1) = new_bi_fun2(BiFun(ConsecutiveDelta2(q,O1),
    ConsecutiveSet2(A,O1),L),Quadr2(q,O1)) by Th20
      .= new_bi_fun2(ConsecutiveDelta2(q,O1),Quadr2(q,O1)) by LATTICE5:def 16;
    hence ConsecutiveDelta2(q,succ O1).(x,y) =
    new_bi_fun2(ConsecutiveDelta2(q,O1),Quadr2(q,O1)).(y',x')
    by A4,LATTICE5:def 6
      .= ConsecutiveDelta2(q,succ O1).(y,x) by A5;
  end;
A6: for O1 st O1 <> {} & O1 is_limit_ordinal & for O2 st O2 in O1 holds X[O2]
  holds X[O1]
  proof
    let O2 be Ordinal;
    assume
A7: O2 <> {} & O2 is_limit_ordinal & for O1 being Ordinal st O1 in O2
    holds ConsecutiveDelta2(q,O1) is symmetric;
    deffunc U(Ordinal) = ConsecutiveDelta2(q,$1);
    consider Ls being T-Sequence such that
A8: dom Ls = O2 & for O1 being Ordinal st O1 in O2 holds
    Ls.O1 = U(O1) from ORDINAL2:sch 2;
A9: ConsecutiveDelta2(q,O2) = union rng Ls by A7,A8,Th21;
    deffunc U(Ordinal) = ConsecutiveSet2(A,$1);
    consider Ts being T-Sequence such that
A10: dom Ts = O2 & for O1 being Ordinal st O1 in O2 holds
    Ts.O1 = U(O1) from ORDINAL2:sch 2;
A11: ConsecutiveSet2(A,O2) = union rng Ts by A7,A10,Th17;
    set CS = ConsecutiveSet2(A,O2);
    reconsider f = union rng Ls as BiFunction of CS,L by A9;
    f is symmetric
    proof
      let x,y be Element of CS;
      consider x1 being set such that
A12:  x in x1 & x1 in rng Ts by A11,TARSKI:def 4;
      consider o1 being set such that
A13:  o1 in dom Ts & x1 = Ts.o1 by A12,FUNCT_1:def 5;
      consider y1 being set such that
A14:  y in y1 & y1 in rng Ts by A11,TARSKI:def 4;
      consider o2 being set such that
A15:  o2 in dom Ts & y1 = Ts.o2 by A14,FUNCT_1:def 5;
      reconsider o1,o2 as Ordinal by A13,A15;
A16:  x in ConsecutiveSet2(A,o1) by A10,A12,A13;
A17:  y in ConsecutiveSet2(A,o2) by A10,A14,A15;
A18:  Ls.o1 = ConsecutiveDelta2(q,o1) by A8,A10,A13;
      then reconsider h1 = Ls.o1 as BiFunction of ConsecutiveSet2(A,o1),L;
A19:  h1 is symmetric
      proof
        let x,y be Element of ConsecutiveSet2(A,o1);
A20:    ConsecutiveDelta2(q,o1) is symmetric by A7,A10,A13;
        thus h1.(x,y) = ConsecutiveDelta2(q,o1).(x,y) by A8,A10,A13
          .=
        ConsecutiveDelta2(q,o1).(y,x) by A20,LATTICE5:def 6
          .= h1.(y,x) by A8,A10,A13;
      end;
A21:  dom h1 = [:ConsecutiveSet2(A,o1),ConsecutiveSet2(A,o1):]
      by FUNCT_2:def 1;
A22:  Ls.o2 = ConsecutiveDelta2(q,o2) by A8,A10,A15;
      then reconsider h2 = Ls.o2 as BiFunction of ConsecutiveSet2(A,o2),L;
A23:  h2 is symmetric
      proof
        let x,y be Element of ConsecutiveSet2(A,o2);
A24:    ConsecutiveDelta2(q,o2) is symmetric by A7,A10,A15;
        thus h2.(x,y) = ConsecutiveDelta2(q,o2).(x,y) by A8,A10,A15
          .= ConsecutiveDelta2(q,o2).(y,x) by A24,LATTICE5:def 6
          .= h2.(y,x) by A8,A10,A15;
      end;
A25:  dom h2 = [:ConsecutiveSet2(A,o2),ConsecutiveSet2(A,o2):]
      by FUNCT_2:def 1;
      per cases;
      suppose o1 c= o2;
        then
A26:    ConsecutiveSet2(A,o1) c= ConsecutiveSet2(A,o2) by Th22;
        then reconsider x'=x, y'=y as Element of ConsecutiveSet2(A,o2)
        by A10,A14,A15,A16;
        ConsecutiveDelta2(q,o2) in rng Ls by A8,A10,A15,A22,FUNCT_1:def 5;
        then
A27:    h2 c= f by A22,ZFMISC_1:92;
A28:    [x,y] in dom h2 & [y,x] in dom h2 by A16,A17,A25,A26,ZFMISC_1:106;
        hence f.(x,y) = h2.(x',y') by A27,GRFUNC_1:8
          .= h2.(y',x') by A23,LATTICE5:def 6
          .= f.(y,x) by A27,A28,GRFUNC_1:8;
      end;
      suppose o2 c= o1;
        then
A29:    ConsecutiveSet2(A,o2) c= ConsecutiveSet2(A,o1) by Th22;
        then reconsider x'=x, y'=y as Element of ConsecutiveSet2(A,o1)
        by A10,A12,A13,A17;
        ConsecutiveDelta2(q,o1) in rng Ls by A8,A10,A13,A18,FUNCT_1:def 5;
        then
A30:    h1 c= f by A18,ZFMISC_1:92;
A31:    [x,y] in dom h1 & [y,x] in dom h1 by A16,A17,A21,A29,ZFMISC_1:106;
        hence f.(x,y) = h1.(x',y') by A30,GRFUNC_1:8
          .= h1.(y',x') by A19,LATTICE5:def 6
          .= f.(y,x) by A30,A31,GRFUNC_1:8;
      end;
    end;
    hence ConsecutiveDelta2(q,O2) is symmetric by A7,A8,Th21;
  end;
  for O being Ordinal holds X[O] from ORDINAL2:sch 1(A2,A3,A6);
  hence thesis;
end;

theorem Th28:
  for A being non empty set for L being lower-bounded LATTICE st L is modular
  for d be BiFunction of A,L st d is symmetric u.t.i. for O being Ordinal
  for q being QuadrSeq of d st O c= DistEsti(d)
  holds ConsecutiveDelta2(q,O) is u.t.i.
proof
  let A be non empty set;
  let L be lower-bounded LATTICE;
  assume
A1: L is modular;
  let d be BiFunction of A,L;
  assume that
A2: d is symmetric and
A3: d is u.t.i.;
  let O be Ordinal;
  let q be QuadrSeq of d;
  defpred X[Ordinal] means
  $1 c= DistEsti(d) implies ConsecutiveDelta2(q,$1) is u.t.i.;
A4: X[{}]
  proof
    assume {} c= DistEsti(d);
    let x,y,z be Element of ConsecutiveSet2(A,{});
    reconsider x' = x, y' = y, z' = z as Element of A by Th15;
A5: ConsecutiveDelta2(q,{}) = d by Th19;
    d.(x',z') <= d.(x',y') "\/" d.(y',z') by A3,LATTICE5:def 8;
    hence ConsecutiveDelta2(q,{}).(x,z) <=
    ConsecutiveDelta2(q,{}).(x,y) "\/" ConsecutiveDelta2(q,{}).(y,z) by A5;
  end;
A6: for O1 being Ordinal st X[O1] holds X[succ O1]
  proof
    let O1 be Ordinal;
    assume that
A7: O1 c= DistEsti(d) implies ConsecutiveDelta2(q,O1) is u.t.i. and
A8: succ O1 c= DistEsti(d);
A9: O1 in DistEsti(d) by A8,ORDINAL1:33;
A10: ConsecutiveDelta2(q,O1) is symmetric by A2,Th27;
    let x,y,z be Element of ConsecutiveSet2(A,succ O1);
    set f = new_bi_fun2(ConsecutiveDelta2(q,O1),Quadr2(q,O1));
    reconsider x' = x, y' = y, z' = z as
    Element of new_set2 ConsecutiveSet2(A,O1) by Th16;
A11: O1 in dom q by A9,LATTICE5:28;
    then q.O1 in rng q by FUNCT_1:def 5;
    then q.O1 in {[u,v,a',b'] where u is Element of A, v is Element of A,
    a' is Element of L, b' is Element of L: d.(u,v) <=
    a'"\/"b'} by LATTICE5:def 14;
    then consider u,v be Element of A, a',b' be Element of L such that
A12: q.O1 = [u,v,a',b'] & d.(u,v) <= a'"\/"b';
    set X = Quadr2(q,O1)`1, Y = Quadr2(q,O1)`2;
    reconsider a = Quadr2(q,O1)`3, b = Quadr2(q,O1)`4 as Element of L;
A13: Quadr2(q,O1) = [u,v,a',b'] by A11,A12,Def7;
    then
A14: u = X by MCART_1:def 8;
A15: v = Y by A13,MCART_1:def 9;
A16: a' = a by A13,MCART_1:def 10;
A17: b' = b by A13,MCART_1:def 11;
A18: dom d = [:A,A:] by FUNCT_2:def 1;
A19: d c= ConsecutiveDelta2(q,O1) by Th24;
    d.(u,v) = d.[u,v]
      .= ConsecutiveDelta2(q,O1).(X,Y) by A14,A15,A18,A19,GRFUNC_1:8;
    then
A20: new_bi_fun2(ConsecutiveDelta2(q,O1),Quadr2(q,O1)) is u.t.i.
    by A1,A7,A9,A10,A12,A16,A17,Th12,ORDINAL1:def 2;
A21: ConsecutiveDelta2(q,succ O1) = new_bi_fun2(BiFun(ConsecutiveDelta2(q,O1),
    ConsecutiveSet2(A,O1),L),Quadr2(q,O1)) by Th20
      .= new_bi_fun2(ConsecutiveDelta2(q,O1),Quadr2(q,O1)) by LATTICE5:def 16;
    f.(x',z') <= f.(x',y') "\/" f.(y',z') by A20,LATTICE5:def 8;
    hence ConsecutiveDelta2(q,succ O1).(x,z) <=
    ConsecutiveDelta2(q,succ O1).(x,y) "\/" ConsecutiveDelta2(q,succ O1).(y,z)
    by A21;
  end;
A22: for O1 st O1 <> {} & O1 is_limit_ordinal & for O2 st O2 in O1 holds X[O2]
  holds X[O1]
  proof
    let O2 be Ordinal;
    assume that
A23: O2 <> {} & O2 is_limit_ordinal & for O1 be Ordinal st O1 in O2 holds
    (O1 c= DistEsti(d) implies ConsecutiveDelta2(q,O1) is u.t.i.) and
A24: O2 c= DistEsti(d);
    deffunc U(Ordinal) = ConsecutiveDelta2(q,$1);
    consider Ls being T-Sequence such that
A25: dom Ls = O2 & for O1 being Ordinal st O1 in O2 holds
    Ls.O1 = U(O1) from ORDINAL2:sch 2;
A26: ConsecutiveDelta2(q,O2) = union rng Ls by A23,A25,Th21;
    deffunc U(Ordinal) = ConsecutiveSet2(A,$1);
    consider Ts being T-Sequence such that
A27: dom Ts = O2 & for O1 being Ordinal st O1 in O2 holds
    Ts.O1 = U(O1) from ORDINAL2:sch 2;
A28: ConsecutiveSet2(A,O2) = union rng Ts by A23,A27,Th17;
    set CS = ConsecutiveSet2(A,O2);
    reconsider f = union rng Ls as BiFunction of CS,L by A26;
    f is u.t.i.
    proof
      let x,y,z be Element of CS;
      consider X being set such that
A29:  x in X & X in rng Ts by A28,TARSKI:def 4;
      consider o1 being set such that
A30:  o1 in dom Ts & X = Ts.o1 by A29,FUNCT_1:def 5;
      consider Y being set such that
A31:  y in Y & Y in rng Ts by A28,TARSKI:def 4;
      consider o2 being set such that
A32:  o2 in dom Ts & Y = Ts.o2 by A31,FUNCT_1:def 5;
      consider Z being set such that
A33:  z in Z & Z in rng Ts by A28,TARSKI:def 4;
      consider o3 being set such that
A34:  o3 in dom Ts & Z = Ts.o3 by A33,FUNCT_1:def 5;
      reconsider o1,o2,o3 as Ordinal by A30,A32,A34;
A35:  x in ConsecutiveSet2(A,o1) by A27,A29,A30;
A36:  y in ConsecutiveSet2(A,o2) by A27,A31,A32;
A37:  z in ConsecutiveSet2(A,o3) by A27,A33,A34;
A38:  Ls.o1 = ConsecutiveDelta2(q,o1) by A25,A27,A30;
      then reconsider h1 = Ls.o1 as BiFunction of ConsecutiveSet2(A,o1),L;
A39:  h1 is u.t.i.
      proof
        let x,y,z be Element of ConsecutiveSet2(A,o1);
A40:    ConsecutiveDelta2(q,o1) = h1 by A25,A27,A30;
        o1 c= DistEsti(d) by A24,A27,A30,ORDINAL1:def 2;
        then ConsecutiveDelta2(q,o1) is u.t.i. by A23,A27,A30;
        hence h1.(x,z) <= h1.(x,y) "\/" h1.(y,z) by A40,LATTICE5:def 8;
      end;
A41:  dom h1 = [:ConsecutiveSet2(A,o1),ConsecutiveSet2(A,o1):]
      by FUNCT_2:def 1;
A42:  Ls.o2 = ConsecutiveDelta2(q,o2) by A25,A27,A32;
      then reconsider h2 = Ls.o2 as BiFunction of ConsecutiveSet2(A,o2),L;
A43:  h2 is u.t.i.
      proof
        let x,y,z be Element of ConsecutiveSet2(A,o2);
A44:    ConsecutiveDelta2(q,o2) = h2 by A25,A27,A32;
        o2 c= DistEsti(d) by A24,A27,A32,ORDINAL1:def 2;
        then ConsecutiveDelta2(q,o2) is u.t.i. by A23,A27,A32;
        hence h2.(x,z) <= h2.(x,y) "\/" h2.(y,z) by A44,LATTICE5:def 8;
      end;
A45:  dom h2 = [:ConsecutiveSet2(A,o2),ConsecutiveSet2(A,o2):]
      by FUNCT_2:def 1;
A46:  Ls.o3 = ConsecutiveDelta2(q,o3) by A25,A27,A34;
      then reconsider h3 = Ls.o3 as BiFunction of ConsecutiveSet2(A,o3),L;
A47:  h3 is u.t.i.
      proof
        let x,y,z be Element of ConsecutiveSet2(A,o3);
A48:    ConsecutiveDelta2(q,o3) = h3 by A25,A27,A34;
        o3 c= DistEsti(d) by A24,A27,A34,ORDINAL1:def 2;
        then ConsecutiveDelta2(q,o3) is u.t.i. by A23,A27,A34;
        hence h3.(x,z) <= h3.(x,y) "\/" h3.(y,z) by A48,LATTICE5:def 8;
      end;
A49:  dom h3 = [:ConsecutiveSet2(A,o3),ConsecutiveSet2(A,o3):]
      by FUNCT_2:def 1;
      per cases;
      suppose
A50:    o1 c= o3;
        then
A51:    ConsecutiveSet2(A,o1) c= ConsecutiveSet2(A,o3) by Th22;
        thus f.(x,y) "\/" f.(y,z) >= f.(x,z)
        proof
          per cases;
          suppose o2 c= o3;
            then
A52:        ConsecutiveSet2(A,o2) c= ConsecutiveSet2(A,o3) by Th22;
            then reconsider y' = y as Element of ConsecutiveSet2(A,o3) by A36;
            reconsider x' = x as Element of ConsecutiveSet2(A,o3) by A35,A51;

reconsider z' = z as Element of ConsecutiveSet2(A,o3) by A27,A33,A34;
            ConsecutiveDelta2(q,o3) in rng Ls by A25,A27,A34,A46,FUNCT_1:def 5;
            then
A53:        h3 c= f by A46,ZFMISC_1:92;
A54:        [x,y] in dom h3 by A35,A36,A49,A51,A52,ZFMISC_1:106;
A55:        [y,z] in dom h3 by A36,A37,A49,A52,ZFMISC_1:106;
A56:        [x,z] in dom h3 by A35,A37,A49,A51,ZFMISC_1:106;
A57:        f.(x,y) = h3.(x',y') by A53,A54,GRFUNC_1:8;
A58:        f.(y,z) = h3.(y',z') by A53,A55,GRFUNC_1:8;
            f.(x,z) = h3.(x',z') by A53,A56,GRFUNC_1:8;

hence f.(x,y) "\/" f.(y,z) >= f.(x,z) by A47,A57,A58,LATTICE5:def 8;
          end;
          suppose o3 c= o2;
            then
A59:        ConsecutiveSet2(A,o3) c= ConsecutiveSet2(A,o2) by Th22;
            then reconsider z' = z as Element of ConsecutiveSet2(A,o2) by A37;
            ConsecutiveSet2(A,o1) c= ConsecutiveSet2(A,o3) by A50,Th22;
            then
A60:        ConsecutiveSet2(A,o1) c= ConsecutiveSet2(A,o2) by A59,XBOOLE_1:1;
            then reconsider x' = x as Element of ConsecutiveSet2(A,o2) by A35;

reconsider y' = y as Element of ConsecutiveSet2(A,o2) by A27,A31,A32;
            ConsecutiveDelta2(q,o2) in rng Ls by A25,A27,A32,A42,FUNCT_1:def 5;
            then
A61:        h2 c= f by A42,ZFMISC_1:92;
A62:        [x,y] in dom h2 by A35,A36,A45,A60,ZFMISC_1:106;
A63:        [y,z] in dom h2 by A36,A37,A45,A59,ZFMISC_1:106;
A64:        [x,z] in dom h2 by A35,A37,A45,A59,A60,ZFMISC_1:106;
A65:        f.(x,y) = h2.(x',y') by A61,A62,GRFUNC_1:8;
A66:        f.(y,z) = h2.(y',z') by A61,A63,GRFUNC_1:8;
            f.(x,z) = h2.(x',z') by A61,A64,GRFUNC_1:8;

hence f.(x,y) "\/" f.(y,z) >= f.(x,z) by A43,A65,A66,LATTICE5:def 8;
          end;
        end;
      end;
      suppose
A67:    o3 c= o1;
        then
A68:    ConsecutiveSet2(A,o3) c= ConsecutiveSet2(A,o1) by Th22;
        thus f.(x,y) "\/" f.(y,z) >= f.(x,z)
        proof
          per cases;
          suppose o1 c= o2;
            then
A69:        ConsecutiveSet2(A,o1) c= ConsecutiveSet2(A,o2) by Th22;
            then reconsider x' = x as Element of ConsecutiveSet2(A,o2) by A35;
            ConsecutiveSet2(A,o3) c= ConsecutiveSet2(A,o1) by A67,Th22;
            then
A70:        ConsecutiveSet2(A,o3) c= ConsecutiveSet2(A,o2) by A69,XBOOLE_1:1;
            then reconsider z' = z as Element of ConsecutiveSet2(A,o2) by A37;

reconsider y' = y as Element of ConsecutiveSet2(A,o2) by A27,A31,A32;
            ConsecutiveDelta2(q,o2) in rng Ls by A25,A27,A32,A42,FUNCT_1:def 5;
            then
A71:        h2 c= f by A42,ZFMISC_1:92;
A72:        [x,y] in dom h2 by A35,A36,A45,A69,ZFMISC_1:106;
A73:        [y,z] in dom h2 by A36,A37,A45,A70,ZFMISC_1:106;
A74:        [x,z] in dom h2 by A35,A37,A45,A69,A70,ZFMISC_1:106;
A75:        f.(x,y) = h2.(x',y') by A71,A72,GRFUNC_1:8;
A76:        f.(y,z) = h2.(y',z') by A71,A73,GRFUNC_1:8;
            f.(x,z) = h2.(x',z') by A71,A74,GRFUNC_1:8;

hence f.(x,y) "\/" f.(y,z) >= f.(x,z) by A43,A75,A76,LATTICE5:def 8;
          end;
          suppose o2 c= o1;
            then
A77:        ConsecutiveSet2(A,o2) c= ConsecutiveSet2(A,o1) by Th22;
            then reconsider y' = y as Element of ConsecutiveSet2(A,o1) by A36;
            reconsider z' = z as Element of ConsecutiveSet2(A,o1) by A37,A68;

reconsider x' = x as Element of ConsecutiveSet2(A,o1) by A27,A29,A30;
            ConsecutiveDelta2(q,o1) in rng Ls by A25,A27,A30,A38,FUNCT_1:def 5;
            then
A78:        h1 c= f by A38,ZFMISC_1:92;
A79:        [x,y] in dom h1 by A35,A36,A41,A77,ZFMISC_1:106;
A80:        [y,z] in dom h1 by A36,A37,A41,A68,A77,ZFMISC_1:106;
A81:        [x,z] in dom h1 by A35,A37,A41,A68,ZFMISC_1:106;
A82:        f.(x,y) = h1.(x',y') by A78,A79,GRFUNC_1:8;
A83:        f.(y,z) = h1.(y',z') by A78,A80,GRFUNC_1:8;
            f.(x,z) = h1.(x',z') by A78,A81,GRFUNC_1:8;

hence f.(x,y) "\/" f.(y,z) >= f.(x,z) by A39,A82,A83,LATTICE5:def 8;
          end;
        end;
      end;
    end;
    hence ConsecutiveDelta2(q,O2) is u.t.i. by A23,A25,Th21;
  end;
  for O being Ordinal holds X[O] from ORDINAL2:sch 1(A4,A6,A22);
  hence thesis;
end;

theorem
  for A being non empty set for L being lower-bounded modular LATTICE
  for d being distance_function of A,L for O being Ordinal
  for q being QuadrSeq of d st O c= DistEsti(d)
  holds ConsecutiveDelta2(q,O) is distance_function of ConsecutiveSet2(A,O),L
  by Th26,Th27,Th28;

definition
  let A be non empty set;
  let L be lower-bounded LATTICE;
  let d be BiFunction of A,L;
  func NextSet2(d) equals

  ConsecutiveSet2(A,DistEsti(d));
  correctness;
end;

registration
  let A be non empty set;
  let L be lower-bounded LATTICE;
  let d be BiFunction of A,L;
  cluster NextSet2(d) -> non empty;
  coherence;
end;

definition
  let A be non empty set;
  let L be lower-bounded LATTICE;
  let d be BiFunction of A,L;
  let q be QuadrSeq of d;
  func NextDelta2(q) equals

  ConsecutiveDelta2(q,DistEsti(d));
  correctness;
end;

definition
  let A be non empty set;
  let L be lower-bounded modular LATTICE;
  let d be distance_function of A,L;
  let q be QuadrSeq of d;
  redefine func NextDelta2(q) -> distance_function of NextSet2(d),L;
  coherence by Th26,Th27,Th28;
end;

definition
  let A be non empty set;
  let L be lower-bounded LATTICE;
  let d be distance_function of A,L;
  let Aq be non empty set;
  let dq be distance_function of Aq,L;
  pred Aq, dq is_extension2_of A,d means
  :Def11:
  ex q being QuadrSeq of d st Aq = NextSet2(d) & dq = NextDelta2(q);
end;

theorem Th30:
  for A being non empty set for L be lower-bounded LATTICE
  for d be distance_function of A,L for Aq be non empty set
  for dq be distance_function of Aq,L st Aq, dq is_extension2_of A,d
  for x,y being Element of A, a,b being Element of L st d.(x,y) <= a "\/" b
  ex z1,z2 being Element of Aq
  st dq.(x,z1) = a & dq.(z1,z2) = (d.(x,y) "\/" a) "/\" b & dq.(z2,y) = a
proof
  let A be non empty set;
  let L be lower-bounded LATTICE;
  let d be distance_function of A,L;
  let Aq be non empty set;
  let dq be distance_function of Aq,L;
  assume Aq, dq is_extension2_of A,d;
  then consider q being QuadrSeq of d such that
A1: Aq = NextSet2(d) and
A2: dq = NextDelta2(q) by Def11;
  let x,y be Element of A;
  let a,b be Element of L;
  assume
A3: d.(x,y) <= a "\/" b;
  rng q = {[x',y',a',b'] where x' is Element of A, y' is Element of A,
  a' is Element of L, b' is Element of L: d.(x',y')
  <= a'"\/"b'} by LATTICE5:def 14;
  then [x,y,a,b] in rng q by A3;
  then consider o being set such that
A4: o in dom q & q.o = [x,y,a,b] by FUNCT_1:def 5;
  reconsider o as Ordinal by A4;
A5: q.o = Quadr2(q,o) by A4,Def7;
  then
A6: x = Quadr2(q,o)`1 by A4,MCART_1:78;
A7: y = Quadr2(q,o)`2 by A4,A5,MCART_1:78;
A8: a = Quadr2(q,o)`3 by A4,A5,MCART_1:78;
A9: b = Quadr2(q,o)`4 by A4,A5,MCART_1:78;
  reconsider B = ConsecutiveSet2(A,o) as non empty set;
  reconsider Q = Quadr2(q,o)
  as Element of [:B,B,the carrier of L,the carrier of L:];
  reconsider cd = ConsecutiveDelta2(q,o) as BiFunction of B,L;
  x in A & y in A & A c= B by Th18;
  then reconsider xo = x, yo = y as Element of B;
  xo in B & yo in B & B c= new_set2 B by XBOOLE_1:7;
  then reconsider x1 = xo, y1 = yo as Element of new_set2 B;
A10: ConsecutiveSet2(A,succ o) = new_set2 B by Th16;
  o in DistEsti(d) by A4,LATTICE5:28;
  then
A11: succ o c= DistEsti(d) by ORDINAL1:33;
  then
A12: ConsecutiveDelta2(q,succ o) c= ConsecutiveDelta2(q,DistEsti(d)) by Th25;
A13: ConsecutiveDelta2(q,succ o) = new_bi_fun2(BiFun(ConsecutiveDelta2(q,o),
  ConsecutiveSet2(A,o),L),Quadr2(q,o)) by Th20
    .= new_bi_fun2(cd,Q) by LATTICE5:def 16;
  {B} in {{B}, {{B}} } by TARSKI:def 2;
  then
A14: {B} in B \/ {{B}, {{B}} } by XBOOLE_0:def 2;
A15: {{B}} in {{B}, {{B}} } by TARSKI:def 2;
  then
A16: {{B}} in B \/ {{B}, {{B}} } by XBOOLE_0:def 2;
A17: {{B}} in new_set2 B by A15,XBOOLE_0:def 2;
A18: cd is zeroed by Th26;
A19: dom new_bi_fun2(cd,Q) = [:new_set2 B,new_set2 B:] by FUNCT_2:def 1;
  then
A20: [x1,{B}] in dom new_bi_fun2(cd,Q) by A14,ZFMISC_1:106;
A21: [{B},{{B}}] in dom new_bi_fun2(cd,Q) by A14,A17,A19,ZFMISC_1:106;
A22: [{{B}},y1] in dom new_bi_fun2(cd,Q) by A16,A19,ZFMISC_1:106;
A23: d c= cd by Th24;
  dom d = [:A,A:] by FUNCT_2:def 1;
  then [xo,yo] in dom d by ZFMISC_1:106;
  then
A24: cd.(xo,yo) = d.(x,y) by A23,GRFUNC_1:8;
  new_set2 B c= ConsecutiveSet2(A,DistEsti(d)) by A10,A11,Th22;
  then reconsider z1={B},z2={{B}} as Element of Aq by A1,A14,A17;
  take z1,z2;
  thus dq.(x,z1) = new_bi_fun2(cd,Q).(x1,{B}) by A2,A12,A13,A20,GRFUNC_1:8
    .= cd.(xo,xo)"\/"a by A6,A8,Def5
    .= Bottom L"\/"a by A18,LATTICE5:def 7
    .= a by WAYBEL_1:4;
  thus dq.(z1,z2) = new_bi_fun2(cd,Q).({B},{{B}}) by A2,A12,A13,A21,GRFUNC_1:8
    .= (d.(x,y)"\/"a)"/\"b by A6,A7,A8,A9,A24,Def5;
  thus dq.(z2,y) = new_bi_fun2(cd,Q).({{B}},y1) by A2,A12,A13,A22,GRFUNC_1:8
    .= cd.(yo,yo)"\/"a by A7,A8,Def5
    .= Bottom L"\/"a by A18,LATTICE5:def 7
    .= a by WAYBEL_1:4;
end;

definition
  let A be non empty set;
  let L be lower-bounded modular LATTICE;
  let d be distance_function of A,L;
  mode ExtensionSeq2 of A,d -> Function means
    :Def12:
    dom it = NAT & it.0 = [A,d] & for n being Element of NAT holds
    ex A' being non empty set, d' being distance_function of A',L,
    Aq being non empty set, dq being distance_function of Aq,L
    st Aq, dq is_extension2_of A',d' & it.n = [A',d'] & it.(n+1) = [Aq,dq];
  existence
  proof
    defpred P[set,set,set] means
    (ex A' being non empty set, d' being distance_function of A',L,
    Aq being non empty set, dq being distance_function of Aq,L st
    Aq, dq is_extension2_of A',d' & $2 = [A',d'] & $3 = [Aq,dq]) or $3= 0 &
    not ex A' being non empty set, d' being distance_function of A',L,
    Aq being non empty set, dq being distance_function of Aq,L st
    Aq, dq is_extension2_of A',d' & $2 = [A',d'];
A1: for n being Element of NAT for x being set ex y being set st P[n,x,y]
    proof
      let n be Element of NAT;
      let x be set;
      per cases;
      suppose ex A' being non empty set, d' being distance_function of A',L,
        Aq being non empty set, dq being distance_function of Aq,L st
        Aq, dq is_extension2_of A',d' & x = [A',d'];

then consider A' being non empty set, d' being distance_function of A',L,
        Aq being non empty set, dq being distance_function of Aq,L such that
A2:     Aq, dq is_extension2_of A',d' and
A3:     x = [A',d'];
        take [Aq,dq];
        thus thesis by A2,A3;
      end;
      suppose
A4:     not ex A' being non empty set, d' being distance_function of A',L,
        Aq being non empty set, dq being distance_function of Aq,L st
        Aq, dq is_extension2_of A',d' & x = [A',d'];
        take 0;
        thus thesis by A4;
      end;
    end;
    consider f being Function such that
A5: dom f = NAT and
A6: f.0 = [A,d] and
A7: for n being Element of NAT holds P[n,f.n,f.(n+1)] from LATTICE5:sch 1(A1);
    take f;
    thus dom f = NAT by A5;
    thus f.0 = [A,d] by A6;
    defpred X[Element of NAT] means
    ex A' being non empty set, d' being distance_function of A',L,
    Aq being non empty set, dq being distance_function of Aq,L st
    Aq, dq is_extension2_of A',d' & f.$1 = [A',d'] & f.($1+1) = [Aq,dq];
    ex A' being non empty set, d' being distance_function of A',L,
    Aq being non empty set, dq being distance_function of Aq,L st
    Aq, dq is_extension2_of A',d' & f.0 = [A',d']
    proof
      take A,d;
      consider q being QuadrSeq of d;
      set Aq = NextSet2(d);
      consider dq being distance_function of Aq,L such that
A8:   dq = NextDelta2(q);
      take Aq,dq;
      thus Aq, dq is_extension2_of A,d by A8,Def11;
      thus f.0 = [A,d] by A6;
    end;
    then
A9: X[0] by A7;
A10: for k being Element of NAT st X[k] holds X[k+1]
    proof
      let k be Element of NAT;
      given A' being non empty set, d' being distance_function of A',L,
      Aq being non empty set, dq being distance_function of Aq,L such that
      Aq, dq is_extension2_of A',d' and f.k = [A',d'] and
A11:  f.(k+1) = [Aq,dq];
      ex A1 being non empty set, d1 being distance_function of A1,L,
      AQ being non empty set, DQ being distance_function of AQ,L st
      AQ, DQ is_extension2_of A1,d1 & f.(k+1) = [A1,d1]
      proof
        take Aq,dq;
        set AQ = NextSet2(dq);
        consider Q being QuadrSeq of dq;
        set DQ = NextDelta2(Q);
        take AQ,DQ;
        thus AQ, DQ is_extension2_of Aq,dq by Def11;
        thus f.(k+1) = [Aq,dq] by A11;
      end;
      hence thesis by A7;
    end;
    thus for k being Element of NAT holds X[k] from NAT_1:sch 1(A9,A10);
  end;
end;

theorem Th31:
  for A being non empty set for L be lower-bounded modular LATTICE
  for d be distance_function of A,L for S being ExtensionSeq2 of A,d
  for k,l being Element of NAT st k <= l holds (S.k)`1 c= (S.l)`1
proof
  let A be non empty set;
  let L be lower-bounded modular LATTICE;
  let d be distance_function of A,L;
  let S be ExtensionSeq2 of A,d;
  let k be Element of NAT;
  defpred X[Element of NAT] means k <= $1 implies (S.k)`1 c= (S.$1)`1;
A1: X[0] by NAT_1:3;
A2: for i being Element of NAT st X[i] holds X[i+1]
  proof
    let i be Element of NAT;
    assume that
A3: k <= i implies (S.k)`1 c= (S.i)`1 and
A4: k <= i+1;
    per cases by A4,NAT_1:8;
    suppose k = i+1;
      hence (S.k)`1 c= (S.(i+1))`1;
    end;
    suppose
A5:   k <= i;
      consider A' being non empty set, d' being distance_function of A',L,
      Aq being non empty set, dq being distance_function of Aq,L such that
A6:   Aq, dq is_extension2_of A',d' and
A7:   S.i = [A',d'] & S.(i+1) = [Aq,dq] by Def12;
      consider q being QuadrSeq of d' such that
A8:   Aq = NextSet2(d') and dq = NextDelta2(q) by A6,Def11;
A9:   (S.i)`1 = A' by A7,MCART_1:def 1;
A10:  (S.(i+1))`1 = ConsecutiveSet2(A',DistEsti(d')) by A7,A8,MCART_1:def 1;
      (S.i)`1 c= ConsecutiveSet2(A',DistEsti(d')) by A9,Th18;
      hence (S.k)`1 c= (S.(i+1))`1 by A3,A5,A10,XBOOLE_1:1;
    end;
  end;
  thus for l being Element of NAT holds X[l] from NAT_1:sch 1(A1,A2);
end;

theorem Th32:
  for A being non empty set for L be lower-bounded modular LATTICE
  for d be distance_function of A,L for S being ExtensionSeq2 of A,d
  for k,l being Element of NAT st k <= l holds (S.k)`2 c= (S.l)`2
proof
  let A be non empty set;
  let L be lower-bounded modular LATTICE;
  let d be distance_function of A,L;
  let S be ExtensionSeq2 of A,d;
  let k be Element of NAT;
  defpred X[Element of NAT] means k <= $1 implies (S.k)`2 c= (S.$1)`2;
A1: X[0] by NAT_1:3;
A2: for i being Element of NAT st X[i] holds X[i+1]
  proof
    let i be Element of NAT;
    assume that
A3: k <= i implies (S.k)`2 c= (S.i)`2 and
A4: k <= i+1;
    per cases by A4,NAT_1:8;
    suppose k = i+1;
      hence (S.k)`2 c= (S.(i+1))`2;
    end;
    suppose
A5:   k <= i;
      consider A' being non empty set, d' being distance_function of A',L,
      Aq being non empty set, dq being distance_function of Aq,L such that
A6:   Aq, dq is_extension2_of A',d' and
A7:   S.i = [A',d'] & S.(i+1) = [Aq,dq] by Def12;
      consider q being QuadrSeq of d' such that Aq = NextSet2(d') and
A8:   dq = NextDelta2(q) by A6,Def11;
A9:   (S.i)`2 = d' by A7,MCART_1:def 2;
A10:  (S.(i+1))`2 = ConsecutiveDelta2(q,DistEsti(d')) by A7,A8,MCART_1:def 2;
      (S.i)`2 c= ConsecutiveDelta2(q,DistEsti(d')) by A9,Th24;
      hence (S.k)`2 c= (S.(i+1))`2 by A3,A5,A10,XBOOLE_1:1;
    end;
  end;
  thus for l being Element of NAT holds X[l] from NAT_1:sch 1(A1,A2);
end;

theorem Th33:
  for L be lower-bounded modular LATTICE
  for S being ExtensionSeq2 of the carrier of L, BasicDF(L)
  for FS being non empty set
  st FS = union { (S.i)`1 where i is Element of NAT: not contradiction}
  holds union { (S.i)`2 where i is Element of NAT: not contradiction}
  is distance_function of FS,L
proof
  let L be lower-bounded modular LATTICE;
  let S be ExtensionSeq2 of the carrier of L, BasicDF(L);
  let FS be non empty set;
  assume
A1: FS = union { (S.i)`1 where i is Element of NAT: not contradiction};
  set FD = union { (S.i)`2 where i is Element of NAT: not contradiction};
  set A = the carrier of L;
  (S.0)`1 in { (S.i)`1 where i is Element of NAT: not contradiction};
  then reconsider X = { (S.i)`1 where i is Element of NAT: not contradiction}
  as non empty set;
  reconsider FS as non empty set;
A2: { (S.i)`2 where i is Element of NAT: not contradiction} is c=-linear
  proof
    now
      let x,y be set;
      assume that
A3:   x in { (S.i)`2 where i is Element of NAT: not contradiction} and
A4:   y in { (S.i)`2 where i is Element of NAT: not contradiction};
      consider k being Element of NAT such that
A5:   x = (S.k)`2 by A3;
      consider l being Element of NAT such that
A6:   y = (S.l)`2 by A4;
      k <= l or l <= k;
      then x c= y or y c= x by A5,A6,Th32;
      hence x,y are_c=-comparable by XBOOLE_0:def 9;
    end;
    hence thesis by ORDINAL1:def 9;
  end;
  { (S.i)`2 where i is Element of NAT: not contradiction}
  c= PFuncs([:FS,FS:],A)
  proof
    let z be set;
    assume z in { (S.i)`2 where i is Element of NAT: not contradiction};
    then consider j being Element of NAT such that
A7: z = (S.j)`2;
    consider A' being non empty set, d' being distance_function of A',L,
    Aq being non empty set, dq being distance_function of Aq,L such that
    Aq, dq is_extension2_of A',d' and
A8: S.j = [A',d'] & S.(j+1) = [Aq,dq] by Def12;
A9: z = d' by A7,A8,MCART_1:def 2;
A10: rng d' c= A;
A11: dom d' = [:A',A':] by FUNCT_2:def 1;
    A' = (S.j)`1 by A8,MCART_1:def 1;
    then A' in { (S.i)`1 where i is Element of NAT: not contradiction};
    then A' c= FS by A1,ZFMISC_1:92;
    then dom d' c= [:FS,FS:] by A11,ZFMISC_1:119;
    hence z in PFuncs([:FS,FS:],A) by A9,A10,PARTFUN1:def 5;
  end;
  then FD in PFuncs([:FS,FS:],A) by A2,HAHNBAN:13;
  then consider g being Function such that
A12: FD = g and dom g c= [:FS,FS:] and
A13: rng g c= A by PARTFUN1:def 5;
  reconsider FD as Function by A12;
A14: X is c=-linear
  proof
    now
      let x,y be set;
      assume that
A15:  x in X and
A16:  y in X;
      consider k being Element of NAT such that
A17:  x = (S.k)`1 by A15;
      consider l being Element of NAT such that
A18:  y = (S.l)`1 by A16;
      k <= l or l <= k;
      then x c= y or y c= x by A17,A18,Th31;
      hence x,y are_c=-comparable by XBOOLE_0:def 9;
    end;
    hence thesis by ORDINAL1:def 9;
  end;
  defpred X[set,set] means $2 = (S.$1)`2;
A20: for x being set st x in NAT ex y being set st X[x,y];
  consider F being Function such that
A21: dom F = NAT and
A22: for x being set st x in NAT holds X[x,F.x] from CLASSES1:sch 1(A20);
A23: rng F = { (S.i)`2 where i is Element of NAT: not contradiction}
  proof
    thus rng F c= { (S.i)`2 where i is Element of NAT: not contradiction}
    proof
      let x be set;
      assume x in rng F;
      then consider j being set such that
A24:  j in dom F & F.j = x by FUNCT_1:def 5;
      reconsider j as Element of NAT by A21,A24;
      x = (S.j)`2 by A22,A24;
      hence x in { (S.i)`2 where i is Element of NAT: not contradiction};
    end;
    let x be set;
    assume x in { (S.i)`2 where i is Element of NAT: not contradiction};
    then consider j being Element of NAT such that
A25: x = (S.j)`2;
    x = F.j by A22,A25;
    hence x in rng F by A21,FUNCT_1:def 5;
  end;
  F is Function-yielding
  proof
    let x be set;
    assume x in dom F;
    then reconsider j = x as Element of NAT by A21;
    consider A1 being non empty set, d1 being distance_function of A1,L,
    Aq1 being non empty set, dq1 being distance_function of Aq1,L such that
    Aq1, dq1 is_extension2_of A1,d1 and
A26: S.j = [A1,d1] & S.(j+1) = [Aq1,dq1] by Def12;
    (S.j)`2 = d1 by A26,MCART_1:def 2;
    hence F.x is Function by A22;
  end;
  then reconsider F as Function-yielding Function;
  set LL = { [:I,I:] where I is Element of X : I in X },
  PP = { [:(S.i)`1,(S.i)`1:] where i is Element of NAT: not contradiction };
A27: rng doms F = PP
  proof
    thus rng doms F c= PP
    proof
      let x be set;
      assume x in rng doms F;
      then consider j being set such that
A28:  j in dom doms F & x = (doms F).j by FUNCT_1:def 5;
A29:  j in dom F by A28,FUNCT_6:89;
      reconsider j as Element of NAT by A21,A28,FUNCT_6:89;
      consider A1 being non empty set, d1 being distance_function of A1,L,
      Aq1 being non empty set, dq1 being distance_function of Aq1,L such that
      Aq1, dq1 is_extension2_of A1,d1 and
A30:  S.j = [A1,d1] & S.(j+1) = [Aq1,dq1] by Def12;
A31:  (S.j)`2 = d1 by A30,MCART_1:def 2;
A32:  (S.j)`1 = A1 by A30,MCART_1:def 1;
      x = dom (F.j) by A28,A29,FUNCT_6:31
        .= dom d1 by A22,A31
        .= [:(S.j)`1,(S.j)`1:] by A32,FUNCT_2:def 1;
      hence x in PP;
    end;
    let x be set;
    assume x in PP;
    then consider j being Element of NAT such that
A33: x = [:(S.j)`1,(S.j)`1:];
    consider A1 being non empty set, d1 being distance_function of A1,L,
    Aq1 being non empty set, dq1 being distance_function of Aq1,L such that
    Aq1, dq1 is_extension2_of A1,d1 and
A34: S.j = [A1,d1] & S.(j+1) = [Aq1,dq1] by Def12;
A35: (S.j)`2 = d1 by A34,MCART_1:def 2;
A36: (S.j)`1 = A1 by A34,MCART_1:def 1;
    j in NAT;
    then
A37: j in dom doms F by A21,FUNCT_6:89;
    x = dom d1 by A33,A36,FUNCT_2:def 1
      .= dom (F.j) by A22,A35
      .= (doms F).j by A21,FUNCT_6:31;
    hence x in rng doms F by A37,FUNCT_1:def 5;
  end;
  LL = PP
  proof
    thus LL c= PP
    proof
      let x be set;
      assume x in LL;
      then consider J being Element of X such that
A38:  x = [:J,J:] and
A39:  J in X;
      consider j being Element of NAT such that
A40:  J = (S.j)`1 by A39;
      thus x in PP by A38,A40;
    end;
    let x be set;
    assume x in PP;
    then consider j being Element of NAT such that
A41: x = [:(S.j)`1,(S.j)`1:];
    (S.j)`1 in X;
    hence x in LL by A41;
  end;
  then [:FS,FS:] = union rng doms F by A1,A14,A27,LATTICE5:3
    .= dom FD by A23,LATTICE5:1;

then reconsider FD as BiFunction of FS,L by A12,A13,FUNCT_2:def 1,RELSET_1:11;
A42: FD is zeroed
  proof
    let x be Element of FS;
    consider y being set such that
A43: x in y & y in X by A1,TARSKI:def 4;
    consider j being Element of NAT such that
A44: y = (S.j)`1 by A43;
    consider A1 being non empty set, d1 being distance_function of A1,L,
    Aq1 being non empty set, dq1 being distance_function of Aq1,L
    such that Aq1, dq1 is_extension2_of A1,d1 and
A45: S.j = [A1,d1] & S.(j+1) = [Aq1,dq1] by Def12;
A46: (S.j)`2 = d1 by A45,MCART_1:def 2;
    reconsider x' = x as Element of A1 by A43,A44,A45,MCART_1:def 1;
    d1 in { (S.i)`2 where i is Element of NAT: not contradiction} by A46;
    then
A47: d1 c= FD by ZFMISC_1:92;
    dom d1 = [:A1,A1:] by FUNCT_2:def 1;
    hence FD.(x,x) = d1.[x',x'] by A47,GRFUNC_1:8
      .= d1.(x',x')
      .= Bottom L by LATTICE5:def 7;
  end;
A48: FD is symmetric
  proof
    let x,y be Element of FS;
    consider x1 being set such that
A49: x in x1 & x1 in X by A1,TARSKI:def 4;
    consider k being Element of NAT such that
A50: x1 = (S.k)`1 by A49;
    consider A1 being non empty set, d1 being distance_function of A1,L,
    Aq1 being non empty set, dq1 being distance_function of Aq1,L such that
    Aq1, dq1 is_extension2_of A1,d1 and
A51: S.k = [A1,d1] & S.(k+1) = [Aq1,dq1] by Def12;
A52: (S.k)`2 = d1 by A51,MCART_1:def 2;
A53: (S.k)`1 = A1 by A51,MCART_1:def 1;
    d1 in { (S.i)`2 where i is Element of NAT: not contradiction} by A52;
    then
A54: d1 c= FD by ZFMISC_1:92;
A55: x in A1 by A49,A50,A51,MCART_1:def 1;
    consider y1 being set such that
A56: y in y1 & y1 in X by A1,TARSKI:def 4;
    consider l being Element of NAT such that
A57: y1 = (S.l)`1 by A56;
    consider A2 being non empty set, d2 being distance_function of A2,L,
    Aq2 being non empty set, dq2 being distance_function of Aq2,L such that
    Aq2, dq2 is_extension2_of A2,d2 and
A58: S.l = [A2,d2] & S.(l+1) = [Aq2,dq2] by Def12;
A59: (S.l)`2 = d2 by A58,MCART_1:def 2;
A60: (S.l)`1 = A2 by A58,MCART_1:def 1;
    d2 in { (S.i)`2 where i is Element of NAT: not contradiction} by A59;
    then
A61: d2 c= FD by ZFMISC_1:92;
A62: y in A2 by A56,A57,A58,MCART_1:def 1;
    per cases;
    suppose k <= l;
      then A1 c= A2 by A53,A60,Th31;
      then reconsider x'=x,y'=y as Element of A2 by A55,A56,A57,A58,
      MCART_1:def 1;
A63:  dom d2 = [:A2,A2:] by FUNCT_2:def 1;
      hence FD.(x,y) = d2.[x',y'] by A61,GRFUNC_1:8
        .= d2.(x',y')
        .= d2.(y',x') by LATTICE5:def 6
        .= FD.[y',x'] by A61,A63,GRFUNC_1:8
        .= FD.(y,x);
    end;
    suppose l <= k;
      then A2 c= A1 by A53,A60,Th31;
      then reconsider x'=x,y'=y as Element of A1 by A49,A50,A51,A62,
      MCART_1:def 1;
A64:  dom d1 = [:A1,A1:] by FUNCT_2:def 1;
      hence FD.(x,y) = d1.[x',y'] by A54,GRFUNC_1:8
        .= d1.(x',y')
        .= d1.(y',x') by LATTICE5:def 6
        .= FD.[y',x'] by A54,A64,GRFUNC_1:8
        .= FD.(y,x);
    end;
  end;
  FD is u.t.i.
  proof
    let x,y,z be Element of FS;
    consider x1 being set such that
A65: x in x1 & x1 in X by A1,TARSKI:def 4;
    consider k being Element of NAT such that
A66: x1 = (S.k)`1 by A65;
    consider A1 being non empty set, d1 being distance_function of A1,L,
    Aq1 being non empty set, dq1 being distance_function of Aq1,L such that
    Aq1, dq1 is_extension2_of A1,d1 and
A67: S.k = [A1,d1] & S.(k+1) = [Aq1,dq1] by Def12;
A68: (S.k)`2 = d1 by A67,MCART_1:def 2;
A69: (S.k)`1 = A1 by A67,MCART_1:def 1;
    d1 in { (S.i)`2 where i is Element of NAT: not contradiction} by A68;
    then
A70: d1 c= FD by ZFMISC_1:92;
A71: x in A1 by A65,A66,A67,MCART_1:def 1;
A72: dom d1 = [:A1,A1:] by FUNCT_2:def 1;
    consider y1 being set such that
A73: y in y1 & y1 in X by A1,TARSKI:def 4;
    consider l being Element of NAT such that
A74: y1 = (S.l)`1 by A73;
    consider A2 being non empty set, d2 being distance_function of A2,L,
    Aq2 being non empty set, dq2 being distance_function of Aq2,L such that
    Aq2, dq2 is_extension2_of A2,d2 and
A75: S.l = [A2,d2] & S.(l+1) = [Aq2,dq2] by Def12;
A76: (S.l)`2 = d2 by A75,MCART_1:def 2;
A77: (S.l)`1 = A2 by A75,MCART_1:def 1;
    d2 in { (S.i)`2 where i is Element of NAT: not contradiction} by A76;
    then
A78: d2 c= FD by ZFMISC_1:92;
A79: y in A2 by A73,A74,A75,MCART_1:def 1;
A80: dom d2 = [:A2,A2:] by FUNCT_2:def 1;
    consider z1 being set such that
A81: z in z1 & z1 in X by A1,TARSKI:def 4;
    consider n being Element of NAT such that
A82: z1 = (S.n)`1 by A81;
    consider A3 being non empty set, d3 being distance_function of A3,L,
    Aq3 being non empty set, dq3 being distance_function of Aq3,L such that
    Aq3, dq3 is_extension2_of A3,d3 and
A83: S.n = [A3,d3] & S.(n+1) = [Aq3,dq3] by Def12;
A84: (S.n)`2 = d3 by A83,MCART_1:def 2;
A85: (S.n)`1 = A3 by A83,MCART_1:def 1;
    d3 in { (S.i)`2 where i is Element of NAT: not contradiction} by A84;
    then
A86: d3 c= FD by ZFMISC_1:92;
A87: z in A3 by A81,A82,A83,MCART_1:def 1;
A88: dom d3 = [:A3,A3:] by FUNCT_2:def 1;
    per cases;
    suppose k <= l;
      then
A89:  A1 c= A2 by A69,A77,Th31;
      thus FD.(x,y) "\/" FD.(y,z) >= FD.(x,z)
      proof
        per cases;
        suppose l <= n;
          then
A90:      A2 c= A3 by A77,A85,Th31;
          then A1 c= A3 by A89,XBOOLE_1:1;
          then reconsider x' = x, y' = y, z' = z as Element of A3
          by A71,A79,A81,A82,A83,A90,MCART_1:def 1;
A91:      FD.(x,y) = d3.[x',y'] by A86,A88,GRFUNC_1:8
            .= d3.(x',y');
A92:      FD.(y,z) = d3.[y',z'] by A86,A88,GRFUNC_1:8
            .= d3.(y',z');
          FD.(x,z) = d3.[x',z'] by A86,A88,GRFUNC_1:8
            .= d3.(x',z');
          hence FD.(x,y) "\/" FD.(y,z) >= FD.(x,z) by A91,A92,LATTICE5:def 8;
        end;
        suppose n <= l;
          then A3 c= A2 by A77,A85,Th31;
          then reconsider x' = x, y' = y, z' = z as Element of A2
          by A71,A73,A74,A75,A87,A89,MCART_1:def 1;
A93:      FD.(x,y) = d2.[x',y'] by A78,A80,GRFUNC_1:8
            .= d2.(x',y');
A94:      FD.(y,z) = d2.[y',z'] by A78,A80,GRFUNC_1:8
            .= d2.(y',z');
          FD.(x,z) = d2.[x',z'] by A78,A80,GRFUNC_1:8
            .= d2.(x',z');
          hence FD.(x,y) "\/" FD.(y,z) >= FD.(x,z) by A93,A94,LATTICE5:def 8;
        end;
      end;
    end;
    suppose l <= k;
      then
A95:  A2 c= A1 by A69,A77,Th31;
      thus FD.(x,y) "\/" FD.(y,z) >= FD.(x,z)
      proof
        per cases;
        suppose k <= n;
          then
A96:      A1 c= A3 by A69,A85,Th31;
          then A2 c= A3 by A95,XBOOLE_1:1;
          then reconsider x' = x, y' = y, z' = z as Element of A3
          by A71,A79,A81,A82,A83,A96,MCART_1:def 1;
A97:      FD.(x,y) = d3.[x',y'] by A86,A88,GRFUNC_1:8
            .= d3.(x',y');
A98:      FD.(y,z) = d3.[y',z'] by A86,A88,GRFUNC_1:8
            .= d3.(y',z');
          FD.(x,z) = d3.[x',z'] by A86,A88,GRFUNC_1:8
            .= d3.(x',z');
          hence FD.(x,y) "\/" FD.(y,z) >= FD.(x,z) by A97,A98,LATTICE5:def 8;
        end;
        suppose n <= k;
          then A3 c= A1 by A69,A85,Th31;
          then reconsider x' = x, y' = y, z' = z as Element of A1
          by A65,A66,A67,A79,A87,A95,MCART_1:def 1;
A99:      FD.(x,y) = d1.[x',y'] by A70,A72,GRFUNC_1:8
            .= d1.(x',y');
A100:     FD.(y,z) = d1.[y',z'] by A70,A72,GRFUNC_1:8
            .= d1.(y',z');
          FD.(x,z) = d1.[x',z'] by A70,A72,GRFUNC_1:8
            .= d1.(x',z');
          hence FD.(x,y) "\/" FD.(y,z) >= FD.(x,z) by A99,A100,LATTICE5:def 8;
        end;
      end;
    end;
  end;
  hence thesis by A42,A48;
end;

theorem Th34:
  for L be lower-bounded modular LATTICE
  for S being ExtensionSeq2 of the carrier of L, BasicDF(L)
  for FS being non empty set for FD being distance_function of FS,L
  for x,y being Element of FS for a,b being Element of L
  st FS = union { (S.i)`1 where i is Element of NAT: not contradiction} &
  FD = union { (S.i)`2 where i is Element of NAT: not contradiction} &
  FD.(x,y) <= a"\/"b ex z1,z2 being Element of FS st FD.(x,z1) = a &
  FD.(z1,z2) = (FD.(x,y)"\/"a)"/\"b & FD.(z2,y) = a
proof
  let L be lower-bounded modular LATTICE;
  let S be ExtensionSeq2 of the carrier of L, BasicDF(L);
  let FS be non empty set, FD be distance_function of FS,L;
  let x,y be Element of FS;
  let a,b be Element of L;
  assume that
A1: FS = union { (S.i)`1 where i is Element of NAT: not contradiction} &
  FD = union { (S.i)`2 where i is Element of NAT: not contradiction} and
A2: FD.(x,y) <= a"\/"b;
  (S.0)`1 in { (S.i)`1 where i is Element of NAT: not contradiction};
  then reconsider X = { (S.i)`1 where i is Element of NAT: not contradiction}
  as non empty set;
  consider x1 being set such that
A3: x in x1 & x1 in X by A1,TARSKI:def 4;
  consider k being Element of NAT such that
A4: x1 = (S.k)`1 by A3;
  consider A1 being non empty set, d1 being distance_function of A1,L,
  Aq1 being non empty set, dq1 being distance_function of Aq1,L such that
A5: Aq1, dq1 is_extension2_of A1,d1 and
A6: S.k = [A1,d1] & S.(k+1) = [Aq1,dq1] by Def12;
A7: (S.k)`2 = d1 by A6,MCART_1:def 2;
A8: (S.k)`1 = A1 by A6,MCART_1:def 1;
A9: (S.(k+1))`1 = Aq1 by A6,MCART_1:def 1;
A10: (S.(k+1))`2 = dq1 by A6,MCART_1:def 2;
  d1 in { (S.i)`2 where i is Element of NAT: not contradiction} by A7;
  then
A11: d1 c= FD by A1,ZFMISC_1:92;
  dq1 in { (S.i)`2 where i is Element of NAT: not contradiction} by A10;
  then
A12: dq1 c= FD by A1,ZFMISC_1:92;
  Aq1 in { (S.i)`1 where i is Element of NAT: not contradiction} by A9;
  then
A13: Aq1 c= FS by A1,ZFMISC_1:92;
A14: x in A1 by A3,A4,A6,MCART_1:def 1;
  consider y1 being set such that
A15: y in y1 & y1 in X by A1,TARSKI:def 4;
  consider l being Element of NAT such that
A16: y1 = (S.l)`1 by A15;
  consider A2 being non empty set, d2 being distance_function of A2,L,
  Aq2 being non empty set, dq2 being distance_function of Aq2,L such that
A17: Aq2, dq2 is_extension2_of A2,d2 and
A18: S.l = [A2,d2] & S.(l+1) = [Aq2,dq2] by Def12;
A19: (S.l)`2 = d2 by A18,MCART_1:def 2;
A20: (S.l)`1 = A2 by A18,MCART_1:def 1;
A21: (S.(l+1))`1 = Aq2 by A18,MCART_1:def 1;
A22: (S.(l+1))`2 = dq2 by A18,MCART_1:def 2;
  d2 in { (S.i)`2 where i is Element of NAT: not contradiction} by A19;
  then
A23: d2 c= FD by A1,ZFMISC_1:92;
  dq2 in { (S.i)`2 where i is Element of NAT: not contradiction} by A22;
  then
A24: dq2 c= FD by A1,ZFMISC_1:92;
  Aq2 in { (S.i)`1 where i is Element of NAT: not contradiction} by A21;
  then
A25: Aq2 c= FS by A1,ZFMISC_1:92;
A26: y in A2 by A15,A16,A18,MCART_1:def 1;
  per cases;
  suppose k <= l;
    then A1 c= A2 by A8,A20,Th31;

then reconsider x'=x,y'=y as Element of A2 by A14,A15,A16,A18,MCART_1:def 1;
A27: x' in A2 & y' in A2;
    dom d2 = [:A2,A2:] by FUNCT_2:def 1;
    then
A28: FD.(x,y) = d2.[x',y'] by A23,GRFUNC_1:8
      .= d2.(x',y');
    then consider z1,z2 being Element of Aq2 such that
A29: dq2.(x,z1) = a and
A30: dq2.(z1,z2) = (d2.(x',y')"\/"a)"/\"b and
A31: dq2.(z2,y) = a by A2,A17,Th30;
    z1 in Aq2 & z2 in Aq2;
    then reconsider z1' = z1, z2' = z2 as Element of FS by A25;
    A2 c= Aq2 by A20,A21,Th31,NAT_1:11;
    then reconsider x'' = x',y'' = y' as Element of Aq2 by A27;
A32: dom dq2 = [:Aq2,Aq2:] by FUNCT_2:def 1;
    take z1',z2';
    thus FD.(x,z1') = dq2.[x'',z1] by A24,A32,GRFUNC_1:8
      .= a by A29;
    thus FD.(z1',z2') = dq2.[z1,z2] by A24,A32,GRFUNC_1:8
      .= (FD.(x,y)"\/"a)"/\"b by A28,A30;
    thus FD.(z2',y) = dq2.[z2,y''] by A24,A32,GRFUNC_1:8
      .= a by A31;
  end;
  suppose l <= k;
    then A2 c= A1 by A8,A20,Th31;
    then reconsider x'=x,y'=y as Element of A1 by A3,A4,A6,A26,MCART_1:def 1;
A33: x' in A1 & y' in A1;
    dom d1 = [:A1,A1:] by FUNCT_2:def 1;
    then
A34: FD.(x,y) = d1.[x',y'] by A11,GRFUNC_1:8
      .= d1.(x',y');
    then consider z1,z2 being Element of Aq1 such that
A35: dq1.(x,z1) = a and
A36: dq1.(z1,z2) = (d1.(x',y')"\/"a)"/\"b and
A37: dq1.(z2,y) = a by A2,A5,Th30;
    z1 in Aq1 & z2 in Aq1;
    then reconsider z1' = z1, z2' = z2 as Element of FS by A13;
    A1 c= Aq1 by A8,A9,Th31,NAT_1:11;
    then reconsider x'' = x',y'' = y' as Element of Aq1 by A33;
A38: dom dq1 = [:Aq1,Aq1:] by FUNCT_2:def 1;
    take z1',z2';
    thus FD.(x,z1') = dq1.[x'',z1] by A12,A38,GRFUNC_1:8
      .= a by A35;
    thus FD.(z1',z2') = dq1.[z1,z2] by A12,A38,GRFUNC_1:8
      .= (FD.(x,y)"\/"a)"/\"b by A34,A36;
    thus FD.(z2',y) = dq1.[z2,y''] by A12,A38,GRFUNC_1:8
      .= a by A37;
  end;
end;

Lm3: for m being Element of NAT st m in Seg 4
holds (m = 1 or m = 2 or m = 3 or m = 4)
proof
  let m be Element of NAT;
  assume
A1: m in Seg 4;
  then 1 <= m & m <= 4 by FINSEQ_1:3;
  then m = 0 or m = 1 or m = 2 or m = 3 or m = 4 by NAT_1:29;
  hence m = 1 or m = 2 or m = 3 or m = 4 by A1,FINSEQ_1:3;
end;

Lm4: for j being Element of NAT st 1 <= j & j < 4 holds j = 1 or j = 2 or j = 3
proof
  let j be Element of NAT;
  assume
A1: 1 <= j & j < 4;
  then j < 3+1;
  then j <= 3 by NAT_1:13;
  then j = 0 or j = 1 or j = 2 or j = 3 by NAT_1:28;
  hence j = 1 or j = 2 or j = 3 by A1;
end;

theorem Th35:
  for L be lower-bounded modular LATTICE
  for S being ExtensionSeq2 of the carrier of L, BasicDF(L)
  for FS being non empty set for FD being distance_function of FS,L
  for f being Homomorphism of L,EqRelLATT FS
  for e1,e2 being Equivalence_Relation of FS
  for x,y being set st f = alpha FD &
  FS = union { (S.i)`1 where i is Element of NAT: not contradiction} &
  FD = union { (S.i)`2 where i is Element of NAT: not contradiction} &
  e1 in the carrier of Image f & e2 in the carrier of Image f &
  [x,y] in e1 "\/" e2 ex F being non empty FinSequence of FS
  st len F = 2+2 & x,y are_joint_by F,e1,e2
proof
  let L be lower-bounded modular LATTICE;
  let S be ExtensionSeq2 of the carrier of L, BasicDF(L);
  let FS be non empty set;
  let FD be distance_function of FS,L;
  let f be Homomorphism of L,EqRelLATT FS;
  let e1,e2 be Equivalence_Relation of FS;
  let x,y be set;
  assume that
A1: f = alpha FD and
A2: FS = union { (S.i)`1 where i is Element of NAT: not contradiction} &
  FD = union { (S.i)`2 where i is Element of NAT: not contradiction} and
A3: e1 in the carrier of Image f & e2 in the carrier of Image f and
A4: [x,y] in e1 "\/" e2;
A5: the carrier of Image f = rng f by YELLOW_0:def 15;
  then consider a being set such that
A6: a in dom f and
A7: e1 = f.a by A3,FUNCT_1:def 5;
  consider b being set such that
A8: b in dom f and
A9: e2 = f.b by A3,A5,FUNCT_1:def 5;
  reconsider a,b as Element of L by A6,A8;
  field (e1 "\/" e2) = FS by ORDERS_1:97;
  then reconsider u = x, v = y as Element of FS by A4,RELAT_1:30;
  reconsider a,b as Element of L;
  consider e1' being Equivalence_Relation of FS such that
A10: e1' = f.a and
A11: for u,v being Element of FS holds [u,v] in e1' iff FD.(u,v) <= a
  by A1,LATTICE5:def 9;
  consider e2' being Equivalence_Relation of FS such that
A12: e2' = f.b and
A13: for u,v being Element of FS holds [u,v] in e2' iff FD.(u,v) <= b
  by A1,LATTICE5:def 9;
  consider e being Equivalence_Relation of FS such that
A14: e = f.(a"\/"b) and
A15: for u,v being Element of FS holds [u,v] in e iff
  FD.(u,v) <= a"\/"b by A1,LATTICE5:def 9;
  e = f.a"\/"f.b by A14,WAYBEL_6:2
    .= e1 "\/" e2 by A7,A9,LATTICE5:10;
  then
A16: FD.(u,v) <= a"\/"b by A4,A15;
  then consider z1,z2 being Element of FS such that
A17: FD.(u,z1) = a and
A18: FD.(z1,z2) = (FD.(u,v)"\/"a)"/\"b and
A19: FD.(z2,v) = a by A2,Th34;
  defpred P[set,set] means
  ($1 = 1 implies $2 = u) & ($1 = 2 implies $2 = z1) &
  ($1 = 3 implies $2 = z2) & ($1 = 4 implies $2 = v);
A21: for m being Nat st m in Seg 4 ex w being set st P[m,w]
  proof
    let m be Nat;
    assume
A22: m in Seg 4;
    per cases by A22,Lm3;
    suppose
A23:  m = 1;
      take w = x; thus P[m,w] by A23;
    end;
    suppose
A24:  m = 2;
      take w = z1; thus P[m,w] by A24;
    end;
    suppose
A25:  m = 3;
      take w = z2; thus P[m,w] by A25;
    end;
    suppose
A26:  m = 4;
      take w = y; thus P[m,w] by A26;
    end;
  end;
  ex p being FinSequence st dom p = Seg 4
  & for k being Nat st k in Seg 4 holds P[k,p.k] from FINSEQ_1:sch 1(A21);
  then consider h being FinSequence such that
A27: dom h = Seg 4 and
A28: for m being Nat st m in Seg 4 holds
  (m = 1 implies h.m = u) & (m = 2 implies h.m = z1) &
  (m = 3 implies h.m = z2) & (m = 4 implies h.m = v);
  Seg 4 = { n where n is Element of NAT: 1 <= n & n <= 4 } by FINSEQ_1:def 1;
  then
A29: 1 in Seg 4 & 2 in Seg 4 & 3 in Seg 4 & 4 in Seg 4;
A30: len h = 4 by A27,FINSEQ_1:def 3;
  rng h c= FS
  proof
    let w be set;
    assume w in rng h;
    then consider j be set such that
A31: j in dom h & w = h.j by FUNCT_1:def 5;
    per cases by A27,A31,Lm3;
    suppose j = 1;
      then h.j = u by A28,A29;
      hence w in FS by A31;
    end;
    suppose j = 2;
      then h.j = z1 by A28,A29;
      hence w in FS by A31;
    end;
    suppose j = 3;
      then h.j = z2 by A28,A29;
      hence w in FS by A31;
    end;
    suppose j = 4;
      then h.j = v by A28,A29;
      hence w in FS by A31;
    end;
  end;
  then reconsider h as FinSequence of FS by FINSEQ_1:def 4;
  len h <> 0 by A27,FINSEQ_1:def 3;
  then reconsider h as non empty FinSequence of FS;
  take h;
  thus len h = 2+2 by A27,FINSEQ_1:def 3;
A32: h.1 = x by A28,A29;
A33: h.(len h) = h.4 by A27,FINSEQ_1:def 3
    .= y by A28,A29;
  for j being Element of NAT st 1 <= j & j < len h holds
  (j is odd implies [h.j,h.(j+1)] in e1) &
  (j is even implies [h.j,h.(j+1)] in e2)
  proof
    let j be Element of NAT;
    assume
A34: 1 <= j & j < len h;
    per cases by A30,A34,Lm4;
    suppose
A35:  j = 1;
      [u,z1] in e1' by A11,A17;
      then [h.1,z1] in e1' by A28,A29;
      hence (j is odd implies [h.j,h.(j+1)] in e1) &
      (j is even implies [h.j,h.(j+1)] in e2) by A7,A10,A28,A29,A35,Lm1;
    end;
    suppose
A36:  j = 2;
      consider i being Element of NAT such that
A37:  i = 1;
A38:  2*i = j by A36,A37;
      FD.(u,v)"\/"a <= a"\/"b"\/"a by A16,WAYBEL_1:3;
      then FD.(u,v)"\/"a <= b"\/"(a"\/"a) by LATTICE3:14;
      then FD.(u,v)"\/"a <= b"\/"a by YELLOW_5:1;
      then (FD.(u,v)"\/"a)"/\"b <= b"/\"(b"\/"a) by WAYBEL_1:2;
      then (FD.(u,v)"\/"a)"/\"b <= b by LATTICE3:18;
      then [z1,z2] in e2' by A13,A18;
      then [h.2,z2] in e2' by A28,A29;
      hence (j is odd implies [h.j,h.(j+1)] in e1) &
      (j is even implies [h.j,h.(j+1)] in e2) by A9,A12,A28,A29,A36,A38;
    end;
    suppose
A39:  j = 3;
      consider i being Element of NAT such that
A40:  i = 1;
A41:  2*i+1 = j by A39,A40;
      [z2,v] in e1' by A11,A19;
      then [h.3,v] in e1' by A28,A29;
      hence (j is odd implies [h.j,h.(j+1)] in e1) &
      (j is even implies [h.j,h.(j+1)] in e2) by A7,A10,A28,A29,A39,A41;
    end;
  end;
  hence thesis by A32,A33,LATTICE5:def 3;
end;

theorem Th36:
  for L be lower-bounded modular LATTICE
  holds L has_a_representation_of_type<= 2
proof
  let L be lower-bounded modular LATTICE;
  set A = the carrier of L, D = BasicDF(L);
A1: D is onto by LATTICE5:43;
  consider S being ExtensionSeq2 of A,D;
A2: S.0 = [A,D] by Def12;
  then
A3: (S.0)`1 = A by MCART_1:def 1;
  set FS = union { (S.i)`1 where i is Element of NAT: not contradiction};
  (S.0)`1 in { (S.i)`1 where i is Element of NAT: not contradiction};
  then
A4: A c= FS by A3,ZFMISC_1:92;
  consider xx being set;
  reconsider FS as non empty set by A4;
  reconsider FD
  = union { (S.i)`2 where i is Element of NAT: not contradiction}
  as distance_function of FS,L by Th33;
A6: FD is onto
  proof
A7: rng D = A by A1,FUNCT_2:def 3;
    for w being set st w in A ex z being set st z in [:FS,FS:] & w = FD . z
    proof
      let w be set;
      assume w in A;
      then consider z being set such that
A8:   z in [:A,A:] and
A9:   D.z = w by A7,FUNCT_2:17;
      take z;
A10:  S.0 = [A,D] by Def12;
      then
A11:  A = (S.0)`1 by MCART_1:def 1;
      (S.0)`1 in { (S.i)`1 where i is Element of NAT: not contradiction};
      then A c= FS by A11,ZFMISC_1:92;
      then [:A,A:] c= [:FS,FS:] by ZFMISC_1:119;
      hence z in [:FS,FS:] by A8;
A12:  z in dom D by A8,FUNCT_2:def 1;
A13:  D = (S.0)`2 by A10,MCART_1:def 2;
      (S.0)`2 in { (S.i)`2 where i is Element of NAT: not contradiction};
      then D c= FD by A13,ZFMISC_1:92;
      hence w = FD.z by A9,A12,GRFUNC_1:8;
    end;
    then rng FD = A by FUNCT_2:16;
    hence FD is onto by FUNCT_2:def 3;
  end;
  alpha FD is join-preserving
  proof
    let a,b be Element of L;
    set f = alpha FD;
A14: ex_sup_of f.:{a,b},EqRelLATT FS by YELLOW_0:17;
A15: dom f = the carrier of L by FUNCT_2:def 1;
    consider e1 being Equivalence_Relation of FS such that
A16: e1 = f.a and
A17: for x,y being Element of FS holds [x,y] in e1 iff FD.(x,y) <= a
    by LATTICE5:def 9;
    consider e2 being Equivalence_Relation of FS such that
A18: e2 = f.b and
A19: for x,y being Element of FS holds [x,y] in e2 iff FD.(x,y) <= b
    by LATTICE5:def 9;
    consider e3 being Equivalence_Relation of FS such that
A20: e3 = f.(a "\/" b) and
A21: for x,y being Element of FS holds [x,y] in e3 iff FD.(x,y) <= a"\/"b
    by LATTICE5:def 9;
A22: field e1 = FS by ORDERS_1:97;
A23: field e2 = FS by ORDERS_1:97;
A24: field e3 = FS by ORDERS_1:97;
A25: e1 "\/" e2 c= e3
    proof
      now
        let x,y be set;
        assume
A26:    [x,y] in e1;
        then reconsider x' = x, y' = y as Element of FS by A22,RELAT_1:30;
A27:    FD.(x',y') <= a by A17,A26;
        a <= a "\/" b by YELLOW_0:22;
        then FD.(x',y') <= a "\/" b by A27,ORDERS_2:26;
        hence [x,y] in e3 by A21;
      end;
      then
A28:  e1 c= e3 by RELAT_1:def 3;
      now
        let x,y be set;
        assume
A29:    [x,y] in e2;
        then reconsider x' = x, y' = y as Element of FS by A23,RELAT_1:30;
A30:    FD.(x',y') <= b by A19,A29;
        b <= b "\/" a by YELLOW_0:22;
        then FD.(x',y') <= b "\/" a by A30,ORDERS_2:26;
        hence [x,y] in e3 by A21;
      end;
      then e2 c= e3 by RELAT_1:def 3;
      then e1 \/ e2 c= e3 by A28,XBOOLE_1:8;
      hence thesis by EQREL_1:def 3;
    end;
A31: e3 c= e1 "\/" e2
    proof
      for u,v being set holds [u,v] in e3 implies [u,v] in e1 "\/" e2
      proof
        let u,v be set;
        assume
A32:    [u,v] in e3;
        then reconsider x = u, y = v as Element of FS by A24,RELAT_1:30;
A33:    u in FS by A24,A32,RELAT_1:30;
A34:    FD.(x,y) <= a"\/"b by A21,A32;
        then consider z1,z2 being Element of FS such that
A35:    FD.(x,z1) = a and
A36:    FD.(z1,z2) = (FD.(x,y)"\/"a)"/\"b and
A37:    FD.(z2,y) = a by Th34;
        defpred P[set,set] means
        ($1 = 1 implies $2 = x) & ($1 = 2 implies $2 = z1) &
        ($1 = 3 implies $2 = z2) & ($1 = 4 implies $2 = y);
A39:    for m being Nat st m in Seg 4 ex w being set st P[m,w]
        proof
          let m be Nat;
          assume
A40:      m in Seg 4;
          per cases by A40,Lm3;
          suppose
A41:        m = 1;
            take w = x;
            thus P[m,w] by A41;
          end;
          suppose
A42:        m = 2;
            take w = z1;
            thus P[m,w] by A42;
          end;
          suppose
A43:        m = 3;
            take w = z2;
            thus P[m,w] by A43;
          end;
          suppose
A44:        m = 4;
            take w = y;
            thus P[m,w] by A44;
          end;
        end;
        ex p being FinSequence st dom p = Seg 4 &
        for k being Nat st k in Seg 4 holds P[k,p.k]
        from FINSEQ_1:sch 1(A39);
        then consider h being FinSequence such that
A45:    dom h = Seg 4 and
A46:    for m being Nat st m in Seg 4 holds
        (m = 1 implies h.m = x) & (m = 2 implies h.m = z1) &
        (m = 3 implies h.m = z2) & (m = 4 implies h.m = y);
        Seg 4 = { n where n is Element of NAT: 1 <= n & n <= 4 }
        by FINSEQ_1:def 1;
        then
A47:    1 in Seg 4 & 2 in Seg 4 & 3 in Seg 4 & 4 in Seg 4;
A48:    len h = 4 by A45,FINSEQ_1:def 3;
A49:    u = h.1 by A46,A47;
A50:    v = h.4 by A46,A47
          .= h.(len h) by A45,FINSEQ_1:def 3;
        for j being Element of NAT st 1 <= j & j < len h
        holds [h.j,h.(j+1)] in e1 \/ e2
        proof
          let j be Element of NAT;
          assume
A51:      1 <= j & j < len h;
          per cases by A48,A51,Lm4;
          suppose
A52:        j = 1;
            [x,z1] in e1 by A17,A35;
            then [h.1,z1] in e1 by A46,A47;
            then [h.1,h.2] in e1 by A46,A47;
            hence [h.j,h.(j+1)] in e1 \/ e2 by A52,XBOOLE_0:def 2;
          end;
          suppose
A53:        j = 2;
            FD.(x,y)"\/"a <= a"\/"b"\/"a by A34,WAYBEL_1:3;
            then FD.(x,y)"\/"a <= b"\/"(a"\/"a) by LATTICE3:14;
            then FD.(x,y)"\/"a <= b"\/"a by YELLOW_5:1;
            then (FD.(x,y)"\/"a)"/\"b <= b"/\"(b"\/"a) by WAYBEL_1:2;
            then (FD.(x,y)"\/"a)"/\"b <= b by LATTICE3:18;
            then [z1,z2] in e2 by A19,A36;
            then [h.2,z2] in e2 by A46,A47;
            then [h.2,h.3] in e2 by A46,A47;
            hence [h.j,h.(j+1)] in e1 \/ e2 by A53,XBOOLE_0:def 2;
          end;
          suppose
A54:        j = 3;
            [z2,y] in e1 by A17,A37;
            then [h.3,y] in e1 by A46,A47;
            then [h.3,h.4] in e1 by A46,A47;
            hence [h.j,h.(j+1)] in e1 \/ e2 by A54,XBOOLE_0:def 2;
          end;
        end;
        hence [u,v] in e1 "\/" e2 by A33,A48,A49,A50,EQREL_1:36;
      end;
      hence thesis by RELAT_1:def 3;
    end;
    sup (f.:{a,b}) = sup {f.a,f.b} by A15,FUNCT_1:118
      .= f.a "\/" f.b by YELLOW_0:41
      .= e1 "\/" e2 by A16,A18,LATTICE5:10
      .= f.(a "\/" b) by A20,A25,A31,XBOOLE_0:def 10
      .= f.sup {a,b} by YELLOW_0:41;
    hence f preserves_sup_of {a,b} by A14,WAYBEL_0:def 31;
  end;

then reconsider f = alpha FD as Homomorphism of L,EqRelLATT FS by LATTICE5:16;
A55: dom f = the carrier of L by FUNCT_2:def 1;
A56: ex e being Equivalence_Relation of FS st
  e in the carrier of Image f & e <> id FS
  proof
    consider A' being non empty set, d' being distance_function of A',L,
    Aq' being non empty set, dq' being distance_function of Aq',L such that
A57: Aq', dq' is_extension2_of A',d' and
A58: S.0 = [A',d'] & S.(0+1) = [Aq',dq'] by Def12;
    A' = A & d' = D by A2,A58,ZFMISC_1:33;
    then consider q being QuadrSeq of D such that
A59: Aq' = NextSet2(D) and
A60: dq' = NextDelta2(q) by A57,Def11;
A61: (S.1)`2 = NextDelta2(q) by A58,A60,MCART_1:def 2;
    (S.1)`2 in { (S.i)`2 where i is Element of NAT: not contradiction};
    then
A62: NextDelta2(q) c= FD by A61,ZFMISC_1:92;
A63: (S.1)`1 = NextSet2(D) by A58,A59,MCART_1:def 1;
    (S.1)`1 in { (S.i)`1 where i is Element of NAT: not contradiction};
    then
A64: NextSet2(D) c= FS by A63,ZFMISC_1:92;
    succ {} c= DistEsti(D) by Th1;
    then {} in DistEsti(D) by ORDINAL1:33;
    then
A65: {} in dom q by LATTICE5:28;
    then q.{} in rng q by FUNCT_1:def 5;
    then q.{} in {[u,v,a',b'] where u is Element of A, v is Element of A,
    a' is Element of L, b' is Element of L: D.(u,v) <= a'"\/"b'}
    by LATTICE5:def 14;
    then consider u,v be Element of A, a,b be Element of L such that
A66: q.{} = [u,v,a,b] & D.(u,v) <= a"\/"b;
    ConsecutiveSet2(A,{}) = A by Th15;
    then reconsider Q = Quadr2(q,{}) as
    Element of [:A,A,the carrier of L,the carrier of L:];
A67: Quadr2(q,{}) = [u,v,a,b] by A65,A66,Def7;
    consider e being Equivalence_Relation of FS such that
A68: e = f.b and
A69: for x,y being Element of FS holds [x,y] in e iff FD.(x,y) <= b
    by LATTICE5:def 9;
    take e;
    e in rng f by A55,A68,FUNCT_1:def 5;
    hence e in the carrier of Image f by YELLOW_0:def 15;
    new_set2 A = new_set2 ConsecutiveSet2(A,{}) by Th15
      .= ConsecutiveSet2(A,succ {}) by Th16;
    then
A70: new_set2 A c= NextSet2(D) by Th1,Th22;
A71: ConsecutiveSet2(A,{}) = A & ConsecutiveDelta2(q,{}) = D by Th15,Th19;
    ConsecutiveDelta2(q,succ {}) = new_bi_fun2(BiFun(ConsecutiveDelta2(q,{}),
    ConsecutiveSet2(A,{}),L),Quadr2(q,{})) by Th20
      .= new_bi_fun2(D,Q) by A71,LATTICE5:def 16;
    then new_bi_fun2(D,Q) c= NextDelta2(q) by Th1,Th25;
    then
A72: new_bi_fun2(D,Q) c= FD by A62,XBOOLE_1:1;
A73: new_set2 A c= FS by A64,A70,XBOOLE_1:1;
A74: dom new_bi_fun2(D,Q) = [:new_set2 A,new_set2 A:] by FUNCT_2:def 1;
A75: {A} in {{A}, {{A}} } by TARSKI:def 2;
    then
A76: {A} in A \/ {{A}, {{A}} } by XBOOLE_0:def 2;
A77: {A} in new_set2 A by A75,XBOOLE_0:def 2;
A78: {{A}} in {{A}, {{A}} } by TARSKI:def 2;
    then
A79: {{A}} in A \/ {{A}, {{A}} } by XBOOLE_0:def 2;
A80: {{A}} in new_set2 A by A78,XBOOLE_0:def 2;
A81: [{A},{{A}}] in dom new_bi_fun2(D,Q) by A74,A77,A79,ZFMISC_1:106;
A82: b = Q`4 by A67,MCART_1:def 11;
    reconsider W = {A}, V = {{A}} as Element of FS by A73,A76,A80;
    FD.(W,V) = new_bi_fun2(D,Q).({A},{{A}}) by A72,A81,GRFUNC_1:8
      .= (D.(Q`1,Q`2)"\/"Q`3)"/\"Q`4 by Def5;
    then FD.(W,V) <= b by A82,YELLOW_0:23;
    then
A83: [{A},{{A}}] in e by A69;
    {A} <> {{A}}
    proof
      assume {A} = {{A}};
      then {A} in {A} by TARSKI:def 1;
      hence contradiction;
    end;
    hence e <> id FS by A83,RELAT_1:def 10;
  end;
  FS is non trivial set
  proof
    now
      assume that
A84:  FS is trivial;
      consider x being set such that
A85:  FS = {x} by A84,REALSET1:def 4;
      consider e being Equivalence_Relation of FS such that
A86:  e in the carrier of Image f & e <> id FS by A56;
      field e = {x} by A85,EQREL_1:16;
      then
A87:  id FS c= e by A85,RELAT_2:17;
A88:  [:{x},{x}:] = {[x,x]} by ZFMISC_1:35;
      id({x}) = {[x,x]} by SYSREL:30;
      hence contradiction by A85,A86,A87,A88,XBOOLE_0:def 10;
    end;
    hence thesis;
  end;
  then reconsider FS as non trivial set;
  take FS;
  reconsider f as Homomorphism of L,EqRelLATT FS;
  take f;
  thus f is one-to-one by A6,LATTICE5:17;
  reconsider FD as distance_function of FS,L;
  thus Image f is finitely_typed
  proof
    take FS;
    thus for e being set st e in the carrier of Image f
    holds e is Equivalence_Relation of FS
    proof
      let e be set;
      assume e in the carrier of Image f;
      then e in rng f by YELLOW_0:def 15;
      then consider x being set such that
A89:  x in dom f and
A90:  e = f.x by FUNCT_1:def 5;
      reconsider x as Element of L by A89;
      consider E being Equivalence_Relation of FS such that
A91:  E = f.x and
      for u,v being Element of FS holds [u,v] in E iff FD.(u,v) <= x
      by LATTICE5:def 9;
      thus thesis by A90,A91;
    end;
    take 2+2;
    thus thesis by Th35;
  end;
  thus ex e being Equivalence_Relation of FS st
  e in the carrier of Image f & e <> id FS
  proof
    consider A' being non empty set, d' being distance_function of A',L,
    Aq' being non empty set, dq' being distance_function of Aq',L such that
A92: Aq', dq' is_extension2_of A',d' and
A93: S.0 = [A',d'] & S.(0+1) = [Aq',dq'] by Def12;
    A' = A & d' = D by A2,A93,ZFMISC_1:33;
    then consider q being QuadrSeq of D such that
A94: Aq' = NextSet2(D) and
A95: dq' = NextDelta2(q) by A92,Def11;
A96: (S.1)`2 = NextDelta2(q) by A93,A95,MCART_1:def 2;
    (S.1)`2 in { (S.i)`2 where i is Element of NAT: not contradiction};
    then
A97: NextDelta2(q) c= FD by A96,ZFMISC_1:92;
A98: (S.1)`1 = NextSet2(D) by A93,A94,MCART_1:def 1;
    (S.1)`1 in { (S.i)`1 where i is Element of NAT: not contradiction};
    then
A99: NextSet2(D) c= FS by A98,ZFMISC_1:92;
    succ {} c= DistEsti(D) by Th1;
    then {} in DistEsti(D) by ORDINAL1:33;
    then
A100: {} in dom q by LATTICE5:28;
    then q.{} in rng q by FUNCT_1:def 5;
    then q.{} in {[u,v,a',b'] where u is Element of A, v is Element of A,
    a' is Element of L, b' is Element of L: D.(u,v) <= a'"\/"b'}
    by LATTICE5:def 14;
    then consider u,v be Element of A, a,b be Element of L such that
A101: q.{} = [u,v,a,b] & D.(u,v) <= a"\/"b;
    ConsecutiveSet2(A,{}) = A by Th15;
    then reconsider Q = Quadr2(q,{}) as
    Element of [:A,A,the carrier of L,the carrier of L:];
A102: Quadr2(q,{}) = [u,v,a,b] by A100,A101,Def7;
    consider e being Equivalence_Relation of FS such that
A103: e = f.b and
A104: for x,y being Element of FS holds [x,y] in e iff FD.(x,y) <= b
    by LATTICE5:def 9;
    take e;
    e in rng f by A55,A103,FUNCT_1:def 5;
    hence e in the carrier of Image f by YELLOW_0:def 15;
    new_set2 A = new_set2 ConsecutiveSet2(A,{}) by Th15
      .= ConsecutiveSet2(A,succ {}) by Th16;
    then
A105: new_set2 A c= NextSet2(D) by Th1,Th22;
A106: ConsecutiveSet2(A,{}) = A & ConsecutiveDelta2(q,{}) = D by Th15,Th19;
    ConsecutiveDelta2(q,succ {}) = new_bi_fun2(BiFun(ConsecutiveDelta2(q,{}),
    ConsecutiveSet2(A,{}),L),Quadr2(q,{})) by Th20
      .= new_bi_fun2(D,Q) by A106,LATTICE5:def 16;
    then new_bi_fun2(D,Q) c= NextDelta2(q) by Th1,Th25;
    then
A107: new_bi_fun2(D,Q) c= FD by A97,XBOOLE_1:1;
A108: new_set2 A c= FS by A99,A105,XBOOLE_1:1;
A109: dom new_bi_fun2(D,Q) = [:new_set2 A,new_set2 A:] by FUNCT_2:def 1;
A110: {A} in {{A}, {{A}} } by TARSKI:def 2;
    then
A111: {A} in A \/ {{A}, {{A}} } by XBOOLE_0:def 2;
A112: {A} in new_set2 A by A110,XBOOLE_0:def 2;
A113: {{A}} in {{A}, {{A}} } by TARSKI:def 2;
    then
A114: {{A}} in A \/ {{A}, {{A}} } by XBOOLE_0:def 2;
A115: {{A}} in new_set2 A by A113,XBOOLE_0:def 2;
A116: [{A},{{A}}] in dom new_bi_fun2(D,Q) by A109,A112,A114,ZFMISC_1:106;
A117: b = Q`4 by A102,MCART_1:def 11;
    reconsider W = {A}, V = {{A}} as Element of FS by A108,A111,A115;
    FD.(W,V) = new_bi_fun2(D,Q).({A},{{A}}) by A107,A116,GRFUNC_1:8
      .= (D.(Q`1,Q`2)"\/"Q`3)"/\"Q`4 by Def5;
    then FD.(W,V) <= b by A117,YELLOW_0:23;
    then
A118: [{A},{{A}}] in e by A104;
    {A} <> {{A}}
    proof
      assume {A} = {{A}};
      then {A} in {A} by TARSKI:def 1;
      hence contradiction;
    end;
    hence e <> id FS by A118,RELAT_1:def 10;
  end;
  for e1,e2 being Equivalence_Relation of FS for x,y being set st
  e1 in the carrier of Image f & e2 in the carrier of Image f &
  [x,y] in e1 "\/" e2 ex F being non empty FinSequence of FS st
  len F = 2+2 & x,y are_joint_by F,e1,e2 by Th35;
  hence type_of Image f <= 2 by A56,LATTICE5:15;
end;

:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
::  <=>  ::   L has_a_representation_of_type<= 2 iff L is modular      ::
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

theorem
  for L be lower-bounded LATTICE
  holds L has_a_representation_of_type<= 2 iff L is modular by Th9,Th36;

