:: Processes in {P}etri nets
::  by Grzegorz Bancerek , Mitsuru Aoki , Akio Matsumoto and Yasunari Shidama
::
:: Received December 20, 2002
:: Copyright (c) 2002 Association of Mizar Users

environ

 vocabularies NUMBERS, SUBSET_1, XXREAL_0, CARD_1, FINSEQ_1, RELAT_1, TARSKI,
      XBOOLE_0, NAT_1, ARYTM_3, FUNCT_1, FINSET_1, FUNCOP_1, ARYTM_1, PETRI,
      NET_1, MCART_1, FUNCT_2, FUNCT_7, PARTFUN1, ORDINAL4, VALUED_1, ORDINAL1,
      INT_1, PNPROC_1;
 notations TARSKI, XBOOLE_0, DOMAIN_1, RELAT_1, FUNCT_1, SUBSET_1, FINSET_1,
      PARTFUN1, CARD_1, FINSEQ_1, FUNCT_2, FUNCOP_1, NUMBERS, ORDINAL1,
      XCMPLX_0, NAT_1, NAT_D, MCART_1, FINSEQ_2, FUNCT_7, INT_1, XXREAL_0,
      PRE_POLY, VALUED_1;
 constructors WELLORD2, DOMAIN_1, XXREAL_0, REAL_1, FUNCT_7, NAT_D, RELSET_1,
      PRE_POLY;
 registrations XBOOLE_0, RELAT_1, FUNCT_1, ORDINAL1, XREAL_0, INT_1, FINSEQ_1,
      FUNCT_7, GRAPH_2, FINSET_1, VALUED_0, VALUED_1, FUNCT_2, MEMBERED,
      CARD_1;
 requirements BOOLE, SUBSET, REAL, NUMERALS, ARITHM;
 definitions TARSKI, XBOOLE_0, FUNCOP_1, RELAT_1;
 theorems TARSKI, NAT_1, FUNCT_1, FUNCT_2, RELSET_1, FUNCOP_1, XBOOLE_1,
      CARD_1, RELAT_1, MCART_1, INT_1, FINSEQ_1, FUNCT_7, XBOOLE_0, TREES_2,
      FINSEQ_3, ENUMSET1, ZFMISC_1, GRFUNC_1, PRE_CIRC, CARD_2, FINSEQ_2,
      FINSET_1, GRAPH_2, WELLORD2, FINSEQ_6, XREAL_1, XXREAL_0, ORDINAL1,
      PARTFUN1, VALUED_1;
 schemes NAT_1, FUNCT_1, FINSEQ_1, CLASSES1;

begin :: Preliminaries

reserve i,j,k,l for Element of NAT,
  x,x1,x2,y1,y2 for set;

theorem Th1:
  i > 0 implies {[i,x]} is FinSubsequence
proof
  assume
A1: i > 0;
A2: dom {[i,x]} = {i} by RELAT_1:23;
  {i} c= Seg i
  proof
    let x;
    assume x in {i};
    then x = i by TARSKI:def 1;
    hence thesis by A1,FINSEQ_1:5;
  end;
  hence thesis by A2,FINSEQ_1:def 12;
end;

Lm1: for p being FinSubsequence st Seq p = {} holds p = {}
proof
  let p be FinSubsequence such that
A1: Seq p = {};
A2: Seq p = p * Sgm(dom p) by FINSEQ_1:def 14;
  rng Sgm dom p = dom p by FINSEQ_1:71;
  then dom {} = dom Sgm dom p by A1,A2,RELAT_1:46;
  then Sgm dom p = {};
  then dom p = rng {} by FINSEQ_1:71;
  hence thesis;
end;

theorem
  for q being FinSubsequence holds q = {} iff Seq q = {}
proof
  let q be FinSubsequence;
  thus q = {} implies Seq q = {}
  proof
    assume
A1: q = {};
    Seq q = q*Sgm dom q by FINSEQ_1:def 14;
    hence Seq q = q*{} by A1
      .= {};
  end;
  thus thesis by Lm1;
end;

theorem Th3:
  for q being FinSubsequence st q = {[i,x]} holds Seq q = <*x*>
proof
  let q be FinSubsequence;
  assume
A1: q = {[i,x]};
  then [i,x] in q by TARSKI:def 1;
  then
A2: i in dom q by RELAT_1:20;
  ex k be Nat st ( dom q c= Seg k) by FINSEQ_1:def 12;
  then i >= 0 qua Nat+1 by A2,FINSEQ_1:3;
  then
A3: i > 0 by NAT_1:13;
  then reconsider p = {[i,x]} as FinSubsequence by Th1;
A4: Seq q = q* Sgm(dom q) by FINSEQ_1:def 14;
  dom p = {i} by RELAT_1:23;
  then Seq p = {[i,x]}*<*i*> by A1,A3,A4,FINSEQ_3:50
    .= <*{[i,x]}.i*> by A1,A2,FINSEQ_2:38
    .= <*x*> by GRFUNC_1:16;
  hence thesis by A1;
end;

registration
  cluster -> finite FinSubsequence;
  coherence
  proof
    let q be FinSubsequence;
    ex n being Nat st dom q c= Seg n by FINSEQ_1:def 12;
    hence thesis by FINSET_1:29;
  end;
end;

theorem Th4:
  for q being FinSubsequence st Seq q = <*x*> ex i st q = {[i,x]}
proof
  let q be FinSubsequence;
  assume Seq q = <*x*>;
  then
A1: Seq q = {[1,x]} by FINSEQ_1:def 5;
  then
A2: dom Seq q = {1} by RELAT_1:23;
A3: rng Seq q = {x} by A1,RELAT_1:23;
A4: Seq q = q* Sgm(dom q) by FINSEQ_1:def 14;
A5: rng Sgm(dom q) = dom q by FINSEQ_1:71;
  then
A6: {1} = dom Sgm(dom q) by A2,A4,RELAT_1:46;
A7: rng Seq q = rng q by A4,A5,RELAT_1:47;
  consider n being Nat such that
A8: dom q c= Seg n by FINSEQ_1:def 12;
  Seg card dom q = {1} by A6,A8,FINSEQ_3:45;
  then card dom q = card {1} by FINSEQ_1:78;
  then consider y be set such that
A9: dom q = {y} by CARD_1:49;
  y in dom q by A9,TARSKI:def 1;
  then y in Seg n by A8;
  then reconsider y as Element of NAT;
  q = {[y,x]} by A3,A7,A9,PRE_CIRC:2;
  hence thesis;
end;

theorem Th5:
  {[x1,y1], [x2,y2]} is FinSequence implies x1 = 1 & x2 = 1 & y1 = y2 or
  x1 = 1 & x2 = 2 or x1 = 2 & x2 = 1
proof
  assume {[x1,y1], [x2,y2]} is FinSequence;
  then reconsider p = {[x1,y1], [x2,y2]} as FinSequence;
A1: dom p = {x1,x2} by RELAT_1:24;
  then
A2: x1 in dom p by TARSKI:def 2;
A3: x2 in dom p by A1,TARSKI:def 2;
A4: [x1,y1] in p by TARSKI:def 2;
A5: [x2,y2] in p by TARSKI:def 2;
A6: p.x1 = y1 by A4,FUNCT_1:8;
A7: p.x2 = y2 by A5,FUNCT_1:8;
A8: dom p = Seg len p by FINSEQ_1:def 3;
A9: len p <= 1+1 by CARD_2:69;
  len p > 0 by NAT_1:3;
  then
A10: len p >= 0 qua Nat+1 by NAT_1:13;
A11: now
    assume len p = 1;
    hence x1 = 1 & x2 = 1 by A2,A3,A8,FINSEQ_1:4,TARSKI:def 1;
    hence y1 = y2 by A5,A6,FUNCT_1:8;
  end;
  now
    assume
A12: len p = 2;
A13: x1 = x2 implies p = {[x1,y1]} by A6,A7,ENUMSET1:69;
    x1 = 1 & x2 = 2 or x1 = 2 & x2 = 1 or x1 = 1 & x2 = 1 or x1 = 2 & x2 = 2
    by A2,A3,A8,A12,FINSEQ_1:4,TARSKI:def 2;
    hence x1 = 1 & x2 = 2 or x1 = 2 & x2 = 1 by A12,A13,CARD_1:50;
  end;
  hence thesis by A9,A10,A11,NAT_1:9;
end;

theorem Th6:
  <*x1,x2*> = {[1,x1], [2,x2]}
proof
  reconsider f={[1,x1], [2,x2]} as Function by GRFUNC_1:19;
A1: dom f = {1,2} by RELAT_1:24;
  then
A2: dom <*x1,x2*> = dom f by FINSEQ_1:4,FINSEQ_3:29;
  now
    let x;
    assume
A3: x in {1,2};
    per cases by A3,TARSKI:def 2;
    suppose
A4:   x = 1;
      then
A5:   <*x1,x2*>.x = x1 by FINSEQ_1:61;
      [x,x1] in f by A4,TARSKI:def 2;
      hence f.x = <*x1,x2*>.x by A5,FUNCT_1:8;
    end;
    suppose
A6:   x = 2;
      then
A7:   <*x1,x2*>.x = x2 by FINSEQ_1:61;
      [x,x2] in f by A6,TARSKI:def 2;
      hence f.x = <*x1,x2*>.x by A7,FUNCT_1:8;
    end;
  end;
  hence thesis by A1,A2,FUNCT_1:9;
end;

Lm2: for j,k,l st
1 <= j & j <= l or l+1 <= j & j <= l+k holds 1 <= j & j <= l+k
proof
  let j,k,l;
  assume that
A1: 1 <= j & j <= l or l+1 <= j & j <= l+k;
  per cases by A1;
  suppose
A2: 1 <= j & j <= l;
    0 <= k by NAT_1:2;
    then l+(0 qua Nat) <= l+k by XREAL_1:9;
    hence thesis by A2,XXREAL_0:2;
  end;
  suppose
A3: l+1 <= j & j <= l+k;
    0 <= l by NAT_1:2;
    then 0 qua Nat+1 <= l+1 by XREAL_1:9;
    hence thesis by A3,XXREAL_0:2;
  end;
end;

theorem Th7:
  for p being FinSubsequence holds card p = len Seq p
proof
  let p be FinSubsequence;
A1: ex k being Nat st ( dom p c= Seg k) by FINSEQ_1:def 12;
A2: Seq p = p*(Sgm dom p) by FINSEQ_1:def 14;
A3: rng Sgm dom p = dom p by FINSEQ_1:71;
  then
A4: dom Seq p = dom Sgm dom p by A2,RELAT_1:46;
  Sgm dom p is one-to-one by A1,FINSEQ_3:99;
  then rng Sgm dom p, dom Sgm dom p are_equipotent by WELLORD2:def 4;
  then
A5: card dom p = card dom Sgm dom p by A3,CARD_1:21;
  card dom p = card p by CARD_1:104;
  hence thesis by A4,A5,CARD_1:104;
end;

Lm3: for X, Y being set holds
(for x st x in X holds not x in Y) iff X misses Y
proof
  let X, Y be set;
  thus (for x st x in X holds not x in Y) implies X misses Y
  proof
    assume
A1: for x st x in X holds not x in Y;
    now
      given x1 such that
A2:   x1 in X /\ Y;
A3:   x1 in X by A2,XBOOLE_0:def 4;
      x1 in Y by A2,XBOOLE_0:def 4;
      hence contradiction by A1,A3;
    end;
    then X /\ Y = {} by XBOOLE_0:def 1;
    hence thesis by XBOOLE_0:def 7;
  end;
  assume
A4: X misses Y;
  now
    let x;
    assume
A5: x in X;
    now
      assume x in Y;
      then x in X /\ Y by A5,XBOOLE_0:def 4;
      hence contradiction by A4,XBOOLE_0:def 7;
    end;
    hence not x in Y;
  end;
  hence thesis;
end;

canceled;

Th8:for P,R being Relation st dom P misses dom R holds P misses R
      by RELAT_1:214;

theorem Th9: ::: RELAT_1
  for X,Y being set, P,R being Relation st X misses Y holds P|X misses R|Y
proof
  let X,Y be set, P,R be Relation;
  assume X misses Y;
  then
A1: X /\ Y = {} by XBOOLE_0:def 7;
A2: dom (P|X) = dom P /\ X by RELAT_1:90;
  dom (R|Y) = dom R /\ Y by RELAT_1:90;
  then dom (P|X) /\ dom (R|Y) = dom P /\ (X /\ (dom R /\ Y)) by A2,
  XBOOLE_1:16
    .= dom P /\ (X /\ Y /\ dom R) by XBOOLE_1:16
    .= {} by A1;
  then dom (P|X) misses dom (R|Y) by XBOOLE_0:def 7;
  hence thesis by Th8;
end;

Lm4: for q being FinSubsequence holds dom Seq q = dom Sgm dom q
proof
  let q be FinSubsequence;
A1: Seq q = q* Sgm dom q by FINSEQ_1:def 14;
  rng Sgm dom q = dom q by FINSEQ_1:71;
  hence thesis by A1,RELAT_1:46;
end;

Lm5: for q being FinSubsequence holds rng q = rng Seq q
proof
  let q be FinSubsequence;
A1: rng Seq q = rng (q*Sgm dom q) by FINSEQ_1:def 14;
  dom q = rng Sgm dom q by FINSEQ_1:71;
  hence thesis by A1,RELAT_1:47;
end;

theorem Th10:
  for f,g,h being Function st
  f c= h & g c= h & f misses g holds dom f misses dom g
proof
  let f,g,h be Function such that
A1: f c= h and
A2: g c= h and
A3: f misses g;
  for x st x in dom f holds not x in dom g
  proof
    let x;
    assume x in dom f;
    then
A4: [x,f.x] in f by FUNCT_1:def 4;
    now
      assume x in dom g;
      then
A5:   [x,g.x] in g by FUNCT_1:def 4;
      then f.x = g.x by A1,A2,A4,FUNCT_1:def 1;
      hence contradiction by A3,A4,A5,Lm3;
    end;
    hence thesis;
  end;
  hence thesis by Lm3;
end;

theorem :: RELAT_1
  for Y being set, R being Relation holds Y|R c= R|(R"Y)
proof
  let Y be set, R be Relation;
  let a,b be set;
  assume
A1: [a,b] in Y|R;
  then
A2: b in Y by RELAT_1:def 12;
A3: [a,b] in R by A1,RELAT_1:def 12;
  then a in R"Y by A2,RELAT_1:def 14;
  hence thesis by A3,RELAT_1:def 11;
end;

theorem Th12:
  for Y being set, f being Function holds Y|f = f|(f"Y)
proof
  let Y be set, f be Function;
  let x,y be set;
  thus [x,y] in Y|f implies [x,y] in f|(f"Y)
  proof
    assume
A1: [x,y] in Y|f;
    then
A2: y in Y by RELAT_1:def 12;
A3: [x,y] in f by A1,RELAT_1:def 12;
    then
A4: x in dom f by FUNCT_1:8;
    f.x in Y by A2,A3,FUNCT_1:8;
    then x in f"Y by A4,FUNCT_1:def 13;
    hence thesis by A3,RELAT_1:def 11;
  end;
  assume
A5: [x,y] in f|(f"Y);
  then
A6: x in f"Y by RELAT_1:def 11;
A7: [x,y] in f by A5,RELAT_1:def 11;
  f.x in Y by A6,FUNCT_1:def 13;
  then y in Y by A7,FUNCT_1:8;
  hence thesis by A7,RELAT_1:def 12;
end;

begin :: Markings on Petri nets

definition
  let P be set;
  mode marking of P is Function of P,NAT;
end;

reserve P,p,x,y,x1,x2 for set,
  m1,m2,m3,m4,m for marking of P,
  i,j,j1,j2,k,k1,k2,l,l1 for Element of NAT;

notation
  let P be set;
  let m be marking of P;
  let p be set;
  synonym m multitude_of p for m.p;
end;

scheme MarkingLambda { P() -> set, F(set) -> Element of NAT }:
  ex m being Function of P(),NAT st
    for p st p in P() holds m.p = F(p)
proof
  consider m being Function such that
A1: dom m = P() and
A2: for p st p in P() holds m.p = F(p) from FUNCT_1:sch 3;
  rng m c= NAT
  proof
    let y;
    assume y in rng m;
    then consider x such that
A3: x in dom m and
A4: y = m.x by FUNCT_1:def 5;
    y = F(x) by A1,A2,A3,A4;
    hence thesis;
  end;
  then reconsider m as marking of P() by A1,FUNCT_2:130,RELSET_1:11;
  take m;
  thus thesis by A2;
end;

definition
  let P,m1,m2;
  canceled;
  redefine pred m1 = m2 means
  for p st p in P holds m1 multitude_of p = m2 multitude_of p;
  compatibility
  proof
    thus m1 = m2 implies
    for p st p in P holds m1 multitude_of p = m2 multitude_of p;
A1: dom m1 = P by FUNCT_2:def 1;
    dom m2 = P by FUNCT_2:def 1;
    hence (for p st p in P holds m1 multitude_of p = m2 multitude_of p)
    implies m1 = m2 by A1,FUNCT_1:9;
  end;
end;

definition
  let P;
  func {$} P -> marking of P equals
  P --> 0;
  coherence;
end;

definition
  let P be set;
  let m1, m2 be marking of P;
  pred m1 c= m2 means
  :Def4:
  for p being set st p in P holds
  m1 multitude_of p <= m2 multitude_of p;
  reflexivity;
end;

Lm6: p in P implies ({$}P).p = 0 by FUNCOP_1:13;

theorem Th13:
  {$}P c= m
proof
  let p;
  assume p in P;
  then ({$}P).p = 0 by Lm6;
  hence thesis by NAT_1:2;
end;

theorem Th14:
  m1 c= m2 & m2 c= m3 implies m1 c= m3
proof
  assume that
A1: m1 c= m2 and
A2: m2 c= m3;
  let p;
  assume
A3: p in P;
  then
A4: m1.p <= m2.p by A1,Def4;
  m2.p <= m3.p by A2,A3,Def4;
  hence thesis by A4,XXREAL_0:2;
end;

definition
  let P be set;
  let m1, m2 be marking of P;
  redefine func m1 + m2 -> marking of P means
  :Def5:
  for p being set st p in P holds
  it multitude_of p = (m1 multitude_of p)+(m2 multitude_of p);
  coherence
  proof
    dom (m1+m2) = P by FUNCT_2:def 1;
    hence thesis;
  end;
  compatibility
  proof
    let m be marking of P;
    thus m = m1+m2 implies for p being set st p in P holds
    m multitude_of p = (m1 multitude_of p)+(m2 multitude_of p) by VALUED_1:1;
    assume
A1: for p being set st p in P holds
    m multitude_of p = (m1 multitude_of p)+(m2 multitude_of p);
A2: dom m = P by FUNCT_2:def 1;
A3: dom (m1+m2) = dom m1 /\ dom m2 by VALUED_1:def 1
      .= P /\ dom m2 by FUNCT_2:def 1
      .= P /\ P by FUNCT_2:def 1;
    now
      let x be set;
      assume
A4:   x in dom m;
      hence m.x = m1.x+m2.x by A1,A2
        .= (m1+m2).x by A2,A3,A4,VALUED_1:def 1;
    end;
    hence thesis by A2,A3,FUNCT_1:9;
  end;
end;

theorem
  m + {$}P = m
proof
  now
    let p;
    assume p in P;
    then ({$}P).p = 0 by Lm6;
    hence m.p = m.p + ({$}P).p;
  end;
  hence thesis by Def5;
end;

definition
  let P be set;
  let m1, m2 be marking of P such that
A1: m2 c= m1;
  func m1 - m2 -> marking of P means
  :Def6:
  for p being set st p in P holds
  it multitude_of p = (m1 multitude_of p)-(m2 multitude_of p);
  existence
  proof
    deffunc F(set) = (m1 multitude_of $1)-'(m2 multitude_of $1);
    consider m being marking of P such that
A2: for p st p in P holds m multitude_of p = F(p) from MarkingLambda;
    take m;
    let p;
    assume
A3: p in P;
    then
A4: (m1 multitude_of p) >= (m2 multitude_of p) by A1,Def4;
    thus m multitude_of p = (m1 multitude_of p)-'(m2 multitude_of p) by A2,A3
      .= (m1 multitude_of p)-(m2 multitude_of p) by A4,XREAL_1:235;
  end;
  uniqueness
  proof
    let M1, M2 be marking of P such that
A5: for p being set st p in P holds
    M1 multitude_of p = (m1 multitude_of p) - (m2 multitude_of p) and
A6: for p st p in P holds
    M2 multitude_of p = (m1 multitude_of p) - (m2 multitude_of p);
    let p;
    assume
A7: p in P;
    hence
    M1 multitude_of p = (m1 multitude_of p) - (m2 multitude_of p) by A5
      .= M2 multitude_of p by A6,A7;
  end;
end;

theorem Th16:
  m1 c= m1 + m2
proof
  let p;
  assume p in P;
  then (m1 + m2) multitude_of p = (m1 multitude_of p)+(m2 multitude_of p)
  by Def5;
  hence thesis by NAT_1:11;
end;

theorem
  m - {$}P = m
proof
A1: now
    let p;
    assume p in P;
    then ({$}P).p = 0 by Lm6;
    hence m.p = m.p - ({$}P).p;
  end;
  {$}P c= m by Th13;
  hence thesis by A1,Def6;
end;

theorem
  m1 c= m2 & m2 c= m3 implies m3 - m2 c= m3 - m1
proof
  assume that
A1: m1 c= m2 and
A2: m2 c= m3;
A3: m1 c= m3 by A1,A2,Th14;
  let p;
  assume
A4: p in P;
  then
A5: m1.p <= m2.p by A1,Def4;
A6: (m3-m1).p = m3.p - m1.p by A3,A4,Def6;
  (m3-m2).p = m3.p - m2.p by A2,A4,Def6;
  hence thesis by A5,A6,XREAL_1:12;
end;

theorem Th19:
  (m1 + m2) - m2 = m1
proof
  let p;
  assume
A1: p in P;
  m2 c= (m1 + m2) by Th16;
  hence ((m1 + m2) - m2).p = (m1 + m2) .p - m2.p by A1,Def6
    .= m1.p + m2.p - m2.p by A1,Def5
    .= m1.p;
end;

theorem Th20:
  m c= m1 & m1 c= m2 implies m1 - m c= m2 - m
proof
  assume
A1: m c= m1;
  assume
A2: m1 c= m2;
  let p;
  assume
A3: p in P;
A4: m c= m2 by A1,A2,Th14;
  m1.p <= m2.p by A2,A3,Def4;
  then (m1.p - m.p) <= (m2.p - m.p) by XREAL_1:11;
  then (m1 - m).p <= m2.p - m.p by A1,A3,Def6;
  hence thesis by A3,A4,Def6;
end;

theorem Th21:
  m1 c= m2 implies m2 + m3 -m1 = m2 - m1 + m3
proof
  assume
A1: m1 c= m2;
  let p;
  assume
A2: p in P;
  m2 c= m2 + m3 by Th16;
  then
A3: m1 c= m2 + m3 by A1,Th14;
  (m2 - m1 + m3).p = (m2 - m1).p + m3.p by A2,Def5
    .= m3.p + (m2.p - m1.p) by A1,A2,Def6
    .= m3.p + m2.p - m1.p
    .= (m3 + m2).p - m1.p by A2,Def5
    .= (m2 + m3 - m1).p by A2,A3,Def6;
  hence thesis;
end;

theorem
  m1 c= m2 & m2 c= m1 implies m1 = m2
proof
  assume
A1: m1 c= m2;
  assume
A2: m2 c= m1;
  let p;
  assume
A3: p in P;
  then
A4: m1.p <= m2.p by A1,Def4;
  m2.p <= m1.p by A2,A3,Def4;
  hence thesis by A4,XXREAL_0:1;
end;

theorem Th23:
  (m1 + m2) + m3 = m1 + (m2 + m3)
proof
  let p;
  assume
A1: p in P;
  then
A2: ((m1 + m2) + m3).p = (m1 + m2).p + m3.p by Def5
    .= m1.p + m2.p + m3.p by A1,Def5;
  (m1 + (m2 + m3)).p = m1.p + (m2 + m3).p by A1,Def5
    .= m1.p + (m2.p + m3.p) by A1,Def5
    .= m1.p + m2.p + m3.p;
  hence thesis by A2;
end;

theorem
  m1 c= m2 & m3 c= m4 implies m1 + m3 c= m2 + m4
proof
  assume
A1: m1 c= m2;
  assume
A2: m3 c= m4;
  let p;
  assume
A3: p in P;
  then
A4: m1.p <= m2.p by A1,Def4;
  m3.p <= m4.p by A2,A3,Def4;
  then
A5: m1.p + m3.p <= m2.p + m4.p by A4,XREAL_1:9;
  (m1 + m3).p = m1.p + m3.p by A3,Def5;
  hence thesis by A3,A5,Def5;
end;

theorem
  m1 c= m2 implies m2 - m1 c= m2
proof
  assume
A1: m1 c= m2;
  let p;
  assume p in P;
  then
A2: (m2 - m1).p = m2.p - m1.p by A1,Def6;
A3: m1 multitude_of p >= 0 by NAT_1:2;
  m2.p - 0 = m2.p;
  hence thesis by A2,A3,XREAL_1:15;
end;

theorem Th26:
  m1 c= m2 & m3 c= m4 & m4 c= m1 implies m1 - m4 c= m2 - m3
proof
  assume
A1: m1 c= m2;
  assume
A2: m3 c= m4;
  assume
A3: m4 c= m1;
  then m4 c= m2 by A1,Th14;
  then
A4: m3 c= m2 by A2,Th14;
  let p;
  assume
A5: p in P;
  then
A6: m1.p <= m2.p by A1,Def4;
A7: m3.p <= m4.p by A2,A5,Def4;
A8: (m2 - m3).p = m2.p - m3.p by A4,A5,Def6;
  (m1 - m4).p = m1.p - m4.p by A3,A5,Def6;
  hence thesis by A6,A7,A8,XREAL_1:15;
end;

theorem Th27:
  m1 c= m2 implies m2 = (m2 - m1) + m1
proof
  assume
A1: m1 c= m2;
  let p;
  assume
A2: p in P;
  then ((m2 - m1) + m1).p = (m2 - m1).p + m1.p by Def5
    .= m2.p - m1.p + m1.p by A1,A2,Def6
    .= m2.p;
  hence thesis;
end;

theorem Th28:
  (m1 + m2) - m1 = m2
proof
A1: m1 c= m1 + m2 by Th16;
  let p;
  assume
A2: p in P;
  then ((m1 + m2) - m1).p = (m1 + m2).p - m1.p by A1,Def6
    .= m1.p + m2.p - m1.p by A2,Def5
    .= m2.p;
  hence thesis;
end;

theorem Th29:
  m2 + m3 c= m1 implies (m1 - m2) - m3 = m1 - (m2 + m3)
proof
  assume m2 + m3 c= m1;
  then (m1 - m2) - m3 = ((m1 - (m2 + m3)) + (m2 + m3)) - m2 - m3 by Th27
    .= (((m1 - (m2 + m3) + m3) + m2) - m2) - m3 by Th23
    .= ((m1 - (m2 + m3)) + m3) - m3 by Th28
    .= m1 - (m2 + m3) by Th28;
  hence thesis;
end;

theorem
  m3 c= m2 & m2 c= m1 implies m1 - (m2 - m3) = (m1 - m2) + m3
proof
  assume
A1: m3 c= m2;
  assume
A2: m2 c= m1;
A3: m2 - m3 c= m3 + (m2 - m3) by Th16;
A4: m2 = m3 + (m2 - m3) by A1,Th27;
  then (m3 + (m2 - m3)) - (m2 - m3) c= m1 - (m2 - m3) by A2,A3,Th26;
  then
A5: m3 c= m1 - (m2 - m3) by Th28;
  thus (m1 - m2) + m3 = ((m1 - (m2 - m3)) - m3) + m3 by A2,A4,Th29
    .= m1 - (m2 - m3) by A5,Th27;
end;

begin :: Transitions and firing

definition
  let P;
  mode transition of P means
    :Def7:
    ex m1,m2 st it=[m1,m2];
  existence
  proof
    consider m1,m2;
    take [m1, m2];
    thus thesis;
  end;
end;

reserve t,t1,t2 for transition of P;

notation
  let P,t;
  synonym Pre t for t`1;
  synonym Post t for t`2;
end;

definition
  let P,t;
  redefine func Pre t -> marking of P;
  coherence
  proof
    ex m1,m2 st t = [m1,m2] by Def7;
    hence thesis by MCART_1:7;
  end;
  redefine func Post t -> marking of P;
  coherence
  proof
    ex m1, m2 st t = [m1, m2] by Def7;
    hence thesis by MCART_1:7;
  end;
end;

definition
  let P, m, t;
  func fire(t,m) -> marking of P equals
  :Def8:
  (m - Pre t) + Post t if Pre t c= m otherwise m;
  coherence;
  consistency;
end;

canceled 2;

theorem
  (Pre t1) + (Pre t2) c= m implies
  fire(t2, fire(t1,m)) = (m - (Pre t1) - Pre t2) + (Post t1) + (Post t2)
proof
  assume
A1: (Pre t1) + (Pre t2) c= m;
A2: (Pre t1) c= (Pre t1) + (Pre t2) by Th16;
  then
A3: (Pre t1) c= m by A1,Th14;
  then
A4: fire(t1, m) = m - (Pre t1) + (Post t1) by Def8;
A5: Pre t2 = (Pre t2) + (Pre t1) - (Pre t1) by Th19;
A6: (Pre t1) + (Pre t2) - (Pre t1) c= m - (Pre t1) by A1,A2,Th20;
  m - (Pre t1) c= m - (Pre t1) + (Post t1) by Th16;
  then (Pre t2) c= fire(t1, m) by A4,A5,A6,Th14;
  hence fire(t2, fire(t1, m)) = fire(t1, m)- (Pre t2) + (Post t2) by Def8
    .= (m - (Pre t1) + (Post t1)) - (Pre t2) + (Post t2) by A3,Def8
    .= (m - (Pre t1) - (Pre t2)) + (Post t1) + (Post t2) by A1,A2,A5,Th20,Th21;
end;

definition
  let P, t;
  func fire t -> Function means
  : Def9:
  dom it = Funcs(P, NAT) &
  for m being marking of P holds it.m = fire(t,m);
  existence
  proof
    defpred P[set,set] means ex m st m = $1 & $2 = fire(t, m);
A1: for x st x in Funcs(P, NAT) ex y st P[x,y]
    proof
      let x;
      assume x in Funcs(P, NAT);
      then reconsider m = x as marking of P by FUNCT_2:121;
      take fire(t, m);
      thus thesis;
    end;
    consider f being Function such that
A2: dom f = Funcs(P, NAT) and
A3: for x st x in Funcs(P, NAT) holds P[x,f.x] from CLASSES1:sch 1(A1);
    take f;
    thus dom f = Funcs(P, NAT) by A2;
    let m;
    m in Funcs(P, NAT) by FUNCT_2:11;
    then P[m,f.m] by A3;
    hence thesis;
  end;
  uniqueness
  proof
    let f1,f2 be Function such that
A4: dom f1 = Funcs(P, NAT) and
A5: for m being marking of P holds f1.m = fire(t,m) and
A6: dom f2 = Funcs(P, NAT) and
A7: for m being marking of P holds f2.m = fire(t,m);
    now
      let x be set;
      assume x in dom f1;
      then reconsider m = x as marking of P by A4,FUNCT_2:121;
      thus f1.x = fire(t,m) by A5
        .= f2.x by A7;
    end;
    hence thesis by A4,A6,FUNCT_1:9;
  end;
end;

theorem Th34:
  rng fire t c= Funcs(P, NAT)
proof
  let y;
  assume y in rng fire t;
  then consider x such that
A1: x in dom fire t and
A2: y = (fire t).x by FUNCT_1:def 5;
  dom fire t = Funcs(P, NAT) by Def9;
  then reconsider m=x as marking of P by A1,FUNCT_2:121;
  y = fire(t,m) by A2,Def9;
  hence thesis by FUNCT_2:11;
end;

theorem
  fire(t2, fire(t1,m)) = ((fire t2)*(fire t1)).m
proof
  dom fire t1 = Funcs(P, NAT) by Def9;
  then
A1: m in dom(fire t1) by FUNCT_2:11;
  thus fire(t2, fire(t1,m)) = (fire t2).fire(t1,m) by Def9
    .= (fire t2).((fire t1).m) by Def9
    .= ((fire t2)*(fire t1)).m by A1,FUNCT_1:23;
end;

definition
  let P;
  mode Petri_net of P -> non empty set means
    :Def10:
    for x being set st x in it holds x is transition of P;
  existence
  proof
    consider t;
    take {t};
    thus thesis by TARSKI:def 1;
  end;
end;

reserve N for Petri_net of P;

definition
  let P, N;
  redefine mode Element of N -> transition of P;
  coherence by Def10;
end;

reserve e, e1,e2 for Element of N;

begin :: Firing sequences of transitions

definition
  let P, N;
  mode firing-sequence of N is Element of N*;
end;

reserve C,C1,C2,C3,fs,fs1,fs2 for firing-sequence of N;

definition
  let P, N, C;
  func fire C -> Function means
  :Def11:
  ex F being Function-yielding FinSequence st it = compose(F, Funcs(P, NAT)) &
  len F = len C & for i being Element of NAT st i in dom C
  holds F.i = fire (C /. i qua Element of N);
  existence
  proof
    deffunc G(set) = fire (C/.$1 qua Element of N);
    consider F being FinSequence such that
A1: len F = len C and
A2: for k being Nat st k in dom F holds F.k = G(k) from FINSEQ_1:sch 2;
A3: dom F = Seg len F by FINSEQ_1:def 3;
A4: dom C = Seg len C by FINSEQ_1:def 3;
    F is Function-yielding
    proof
      let x be set;
      assume
A5:   x in dom F;
      then reconsider i = x as Element of NAT;
      F.x = fire (C/.i qua Element of N) by A2,A5;
      hence thesis;
    end;
    then reconsider F as Function-yielding FinSequence;
    take f = compose(F, Funcs(P, NAT)), F;
    thus f = compose(F, Funcs(P, NAT));
    thus thesis by A1,A2,A3,A4;
  end;
  uniqueness
  proof
    let f1,f2 be Function;
    given F1 being Function-yielding FinSequence such that
A6: f1 = compose(F1, Funcs(P, NAT)) and
A7: len F1 = len C and
A8: for i being Element of NAT st i in dom C
    holds F1.i = fire (C/.i qua Element of N);
    given F2 being Function-yielding FinSequence such that
A9: f2 = compose(F2, Funcs(P, NAT)) and
A10: len F2 = len C and
A11: for i being Element of NAT st i in dom C
    holds F2.i = fire (C/.i qua Element of N);
A12: dom F1 = Seg len F1 by FINSEQ_1:def 3;
A13: dom F2 = Seg len F2 by FINSEQ_1:def 3;
A14: dom C = Seg len C by FINSEQ_1:def 3;
    now
      let i be Nat;
      assume
A15:  i in dom C;
      then F1.i = fire (C/.i qua Element of N) by A8;
      hence F1.i = F2.i by A11,A15;
    end;
    hence thesis by A6,A7,A9,A10,A12,A13,A14,FINSEQ_1:17;
  end;
end;
 
:: Firing of empty firing-sequence <*>N

theorem
  fire(<*>N) = id Funcs(P, NAT)
proof
  consider F being Function-yielding FinSequence such that
A1: fire(<*>N) = compose(F, Funcs(P, NAT)) and
A2: len F = len <*>N and
  for i being Element of NAT st i in dom <*>N holds
  F.i = fire ((<*>N)/.i qua Element of N) by Def11;
  F = {} by A2;
  hence thesis by A1,FUNCT_7:41;
end;
 
:: Firing of firing-sequence with one translation <*e*>

theorem Th37:
  fire <*e*> = fire e
proof
  consider F being Function-yielding FinSequence such that
A1: fire <*e*> = compose(F, Funcs(P, NAT)) and
A2: len F = len <*e*> and
A3: for i being Element of NAT st i in dom <*e*> holds
  F.i = fire (<*e*>/.i qua Element of N) by Def11;
A4: len <*e*> = 1 by FINSEQ_1:57;
A5: <*e*>.1 = e by FINSEQ_1:57;
  dom <*e*> = {1} by FINSEQ_1:4,def 8;
  then
A6: 1 in dom <*e*> by TARSKI:def 1;
  then
A7: <*e*>/.1 = <*e*>.1 by PARTFUN1:def 8;
  F.1 = fire (<*e*>/.1 qua Element of N) by A3,A6;
  then
A8: F = <*fire e*> by A2,A4,A5,A7,FINSEQ_1:57;
  dom fire e c= Funcs(P,NAT) by Def9;
  hence thesis by A1,A8,FUNCT_7:48;
end;

theorem Th38:
  (fire e)*id Funcs(P, NAT) = fire e
proof
A1: compose(<*fire e*>, Funcs(P, NAT)) = (fire e)*id Funcs(P, NAT)
  by FUNCT_7:47;
  dom fire e c= Funcs(P, NAT) by Def9;
  hence thesis by A1,FUNCT_7:48;
end;
 
:: Firing of firing-sequence with two translations <*e1,e2*>

theorem
  fire <*e1,e2*> = (fire e2)*(fire e1)
proof
  consider F being Function-yielding FinSequence such that
A1: fire <*e1,e2*> = compose(F, Funcs(P, NAT)) and
A2: len F = len <*e1,e2*> and
A3: for i being Element of NAT st i in dom <*e1,e2*> holds
  F.i = fire (<*e1,e2*>/.i qua Element of N) by Def11;
A4: len <*e1,e2*> = 2 by FINSEQ_1:61;
A5: <*e1,e2*>.1 = e1 by FINSEQ_1:61;
A6: <*e1,e2*>.2 = e2 by FINSEQ_1:61;
A7: dom <*e1,e2*> = {1,2} by A4,FINSEQ_1:4,def 3;
A8: 1 in {1,2} by TARSKI:def 2;
A9: 2 in {1,2} by TARSKI:def 2;
A10: <*e1,e2*>/.1 = <*e1,e2*>.1 by A7,A8,PARTFUN1:def 8;
A11: <*e1,e2*>/.2 = <*e1,e2*>.2 by A7,A9,PARTFUN1:def 8;
A12: F.1 = fire e1 by A3,A5,A7,A8,A10;
  F.2 = fire e2 by A3,A6,A7,A9,A11;
  then
A13: F = <*fire e1,fire e2*> by A2,A4,A12,FINSEQ_1:61;
  (fire e1)*id Funcs(P, NAT) = fire e1 by Th38;
  then (fire e2)*(fire e1)*id Funcs(P, NAT) = (fire e2)*(fire e1)
  by RELAT_1:55;
  hence thesis by A1,A13,FUNCT_7:53;
end;

theorem Th40:
  dom fire C = Funcs(P, NAT) & rng fire C c= Funcs(P, NAT)
proof
  defpred P[Element of NAT] means for F being Function-yielding FinSequence st
  len F = $1 & for i st i in dom F ex t st F.i = fire t
  holds dom compose(F, Funcs(P, NAT)) = Funcs(P, NAT) &
  rng compose(F, Funcs(P, NAT)) c= Funcs(P, NAT);
A1: P[0]
  proof
    let F be Function-yielding FinSequence;
    assume len F = 0;
    then F = {};
    then compose(F, Funcs(P, NAT)) = id Funcs(P, NAT) by FUNCT_7:41;
    hence thesis by RELAT_1:71;
  end;
A2: for k st P[k] holds P[k + 1]
  proof
    let k such that
A3: for G being Function-yielding FinSequence st len G = k &
    for i st i in dom G ex t st G.i = fire t
    holds dom compose(G, Funcs(P, NAT)) = Funcs(P, NAT) &
    rng compose(G, Funcs(P, NAT)) c= Funcs(P, NAT);
    let F be Function-yielding FinSequence such that
A4: len F = k+1 and
A5: for i st i in dom F ex t st F.i = fire t;
    consider G being FinSequence, x being set such that
A6: F = G^<*x*> and
A7: len G = k by A4,TREES_2:4;
    reconsider G as Function-yielding FinSequence by A6,FUNCT_7:25;
    0 <= k by NAT_1:2;
    then 0 qua Nat+1 <= k+1 by XREAL_1:9;
    then k+1 in dom F by A4,FINSEQ_3:27;
    then consider t such that
A8: F.(k+1) = fire t by A5;
    x = F.(k+1) by A6,A7,FINSEQ_1:59;
    then
A9: compose(F, Funcs(P, NAT)) = (fire t)*compose(G, Funcs(P, NAT))
    by A6,A8,FUNCT_7:43;
A10: dom fire t = Funcs(P, NAT) by Def9;
A11: rng fire t c= Funcs(P, NAT) by Th34;
A12: for i st i in dom G ex t st G.i = fire t
    proof
      let i;
A13:  dom G c= dom F by A6,FINSEQ_1:39;
      assume
A14:  i in dom G;
      then G.i = F.i by A6,FINSEQ_1:def 7;
      hence thesis by A5,A13,A14;
    end;
    then
A15: dom compose(G, Funcs(P, NAT)) = Funcs(P, NAT) by A3,A7;
A16: rng compose(G, Funcs(P, NAT)) c= Funcs(P, NAT) by A3,A7,A12;
    rng compose(F, Funcs(P, NAT)) c= rng fire t by A9,RELAT_1:45;
    hence thesis by A9,A10,A11,A15,A16,RELAT_1:46,XBOOLE_1:1;
  end;
A17: P[k] from NAT_1:sch 1(A1,A2);
  consider F being Function-yielding FinSequence such that
A18: fire C = compose(F, Funcs(P, NAT)) and
A19: len F = len C and
A20: for i being Element of NAT st i in dom C holds
  F.i = fire (C/.i qua Element of N) by Def11;
  for i st i in dom F ex t st F.i = fire t
  proof
    let i;
    assume
A21: i in dom F;
A22: dom F = Seg len F by FINSEQ_1:def 3;
A23: dom C = Seg len C by FINSEQ_1:def 3;
    reconsider t = C/.i as Element of N;
    take t;
    thus thesis by A19,A20,A21,A22,A23;
  end;
  hence thesis by A17,A18,A19;
end;
 
:: Firing of compound firing-sequence

theorem Th41:
  fire (C1^C2) = (fire C2)*(fire C1)
proof
  consider F being Function-yielding FinSequence such that
A1: fire (C1^C2) = compose(F, Funcs(P, NAT)) and
A2: len F = len (C1^C2) and
A3: for i being Element of NAT st i in dom (C1^C2) holds
  F.i = fire ((C1^C2)/.i qua Element of N) by Def11;
  consider F1 being Function-yielding FinSequence such that
A4: fire C1 = compose(F1, Funcs(P, NAT)) and
A5: len F1 = len C1 and
A6: for i being Element of NAT st i in dom C1 holds
  F1.i = fire (C1/.i qua Element of N) by Def11;
  consider F2 being Function-yielding FinSequence such that
A7: fire C2 = compose(F2, Funcs(P, NAT)) and
A8: len F2 = len C2 and
A9: for i being Element of NAT st i in dom C2 holds
  F2.i = fire (C2/.i qua Element of N) by Def11;
A10: rng fire C1 c= Funcs(P, NAT) by Th40;
  len F = len C1 + len C2 by A2,FINSEQ_1:35;
  then
A11: dom F = Seg (len F1 + len F2) by A5,A8,FINSEQ_1:def 3;
A12: for k be Nat st k in dom F1 holds F.k = F1.k
  proof
    let k be Nat;
    assume
A13: k in dom F1;
A14: dom F1 = Seg len F1 by FINSEQ_1:def 3;
A15: dom F1 = Seg len C1 by A5,FINSEQ_1:def 3;
A16: dom F1 = dom C1 by A5,A14,FINSEQ_1:def 3;
A17: k in dom C1 by A13,A15,FINSEQ_1:def 3;
A18: dom C1 c= dom (C1^C2) by FINSEQ_1:39;
    then
A19: F.k = fire ((C1^C2)/.k qua Element of N) by A3,A17;
A20: (C1^C2)/.k = (C1^C2).k by A17,A18,PARTFUN1:def 8;
A21: (C1^C2).k = C1.k by A13,A16,FINSEQ_1:def 7;
    C1.k = C1/.k by A13,A16,PARTFUN1:def 8;
    hence thesis by A6,A13,A16,A19,A20,A21;
  end;
  for k be Nat st k in dom F2 holds F.(len F1 + k) = F2.k
  proof
    let k be Nat;
    assume
A22: k in dom F2;
    dom F2 = Seg len F2 by FINSEQ_1:def 3;
    then
A23: dom F2 = dom C2 by A8,FINSEQ_1:def 3;
    then
A24: len F1 + k in dom (C1^C2) by A5,A22,FINSEQ_1:41;
A25: F.(len F1 + k) = fire ((C1^C2)/.(len F1 + k) qua Element of N)
    by A3,A5,A22,A23,FINSEQ_1:41;
A26: (C1^C2)/.(len F1 + k) = (C1^C2).(len F1 +k) by A24,PARTFUN1:def 8;
A27: (C1^C2).(len F1 + k) = C2.k by A5,A22,A23,FINSEQ_1:def 7;
    C2.k = C2/.k by A22,A23,PARTFUN1:def 8;
    hence thesis by A9,A22,A23,A25,A26,A27;
  end;
  then F = F1^F2 by A11,A12,FINSEQ_1:def 7;
  hence thesis by A1,A4,A7,A10,FUNCT_7:50;
end;

theorem
  fire (C^<*e*>) = (fire e)*(fire C)
proof
  fire (C^<*e*>) = (fire <*e*>)*(fire C) by Th41;
  hence thesis by Th37;
end;

definition
  let P, N, C, m;
  func fire(C, m) -> marking of P equals
  (fire C).m;
  coherence
  proof
A1: dom fire C = Funcs(P, NAT) by Th40;
A2: rng fire C c= Funcs(P, NAT) by Th40;
    m in dom fire C by A1,FUNCT_2:11;
    then [m,(fire C).m] in fire C by FUNCT_1:def 4;
    then (fire C).m in rng fire C by RELAT_1:def 5;
    hence thesis by A2,FUNCT_2:121;
  end;
end;

begin :: Sequential composition

definition
  let P, N;
  mode process of N is Subset of N*;
end;

reserve R, R1, R2, R3, P1, P2 for process of N;

registration :: wyrzucic !!!!!!!!!!!!!!!!
  cluster FinSequence-like -> FinSubsequence-like Function;
  coherence;
end;

definition
  let P, N, R1, R2;
  func R1 before R2 -> process of N equals
  {C1^C2: C1 in R1 & C2 in R2};
  coherence
  proof
    set X = {C1^C2: C1 in R1 & C2 in R2};
    X c= N*
    proof
      let x;
      assume x in X;
      then ex C1,C2 st x = C1^C2 & C1 in R1 & C2 in R2;
      hence thesis;
    end;
    hence thesis;
  end;
end;

registration
  let P, N;
  let R1, R2 be non empty process of N;
  cluster R1 before R2 -> non empty;
  coherence
  proof
    consider fs1 being set such that
A1: fs1 in R1 by XBOOLE_0:def 1;
    consider fs2 being set such that
A2: fs2 in R2 by XBOOLE_0:def 1;
    reconsider fs1,fs2 as firing-sequence of N by A1,A2;
    fs1^fs2 in R1 before R2 by A1,A2;
    hence thesis;
  end;
end;

theorem Th43:
  (R1 \/ R2) before R = (R1 before R) \/ (R2 before R)
proof
  thus (R1 \/ R2) before R c= (R1 before R) \/ (R2 before R)
  proof
    let x;
    assume x in (R1 \/ R2) before R;
    then consider fs1, fs such that
A1: x = fs1^fs and
A2: fs1 in R1 \/ R2 and
A3: fs in R;
    fs1 in R1 or fs1 in R2 by A2,XBOOLE_0:def 3;
    then x in {a1^a where a1,a is firing-sequence of N: a1 in R1 & a in R} or
    x in {b2^b where b2,b is firing-sequence of N: b2 in R2 & b in R} by A1,A3;
    hence thesis by XBOOLE_0:def 3;
  end;
  let x be set;
  assume
A4: x in (R1 before R) \/ (R2 before R);
  per cases by A4,XBOOLE_0:def 3;
  suppose x in R1 before R;
    then consider fs1, fs such that
A5: x = fs1^fs and
A6: fs1 in R1 and
A7: fs in R;
    fs1 in R1 \/ R2 by A6,XBOOLE_0:def 3;
    hence thesis by A5,A7;
  end;
  suppose x in R2 before R;
    then consider fs2, fs such that
A8: x = fs2^fs and
A9: fs2 in R2 and
A10: fs in R;
    fs2 in R1 \/ R2 by A9,XBOOLE_0:def 3;
    hence thesis by A8,A10;
  end;
end;

theorem Th44:
  R before (R1 \/ R2) = (R before R1) \/ (R before R2)
proof
  thus R before (R1 \/ R2) c= (R before R1) \/ (R before R2)
  proof
    let x;
    assume x in R before (R1 \/ R2);
    then consider fs, fs1 such that
A1: x = fs^fs1 and
A2: fs in R and
A3: fs1 in R1 \/ R2;
    fs1 in R1 or fs1 in R2 by A3,XBOOLE_0:def 3;
    then x in {a^a1 where a,a1 is firing-sequence of N: a in R & a1 in R1} or
    x in {b^b2 where b,b2 is firing-sequence of N: b in R & b2 in R2} by A1,A2;
    hence thesis by XBOOLE_0:def 3;
  end;
  let x be set;
  assume
A4: x in (R before R1) \/ (R before R2);
  per cases by A4,XBOOLE_0:def 3;
  suppose x in R before R1;
    then consider fs, fs1 such that
A5: x = fs^fs1 and
A6: fs in R and
A7: fs1 in R1;
    fs1 in R1 \/ R2 by A7,XBOOLE_0:def 3;
    hence thesis by A5,A6;
  end;
  suppose x in R before R2;
    then consider fs, fs2 such that
A8: x = fs^fs2 and
A9: fs in R and
A10: fs2 in R2;
    fs2 in R1 \/ R2 by A10,XBOOLE_0:def 3;
    hence thesis by A8,A9;
  end;
end;

theorem Th45:
  {C1} before {C2} = {C1^C2}
proof
  thus {C1} before {C2} c= {C1^C2}
  proof
    let x;
    assume x in {C1} before {C2};
    then consider fs1, fs2 such that
A1: x = fs1^fs2 and
A2: fs1 in {C1} and
A3: fs2 in {C2};
A4: fs1 = C1 by A2,TARSKI:def 1;
    fs2 = C2 by A3,TARSKI:def 1;
    hence thesis by A1,A4,TARSKI:def 1;
  end;
  let x;
  assume x in {C1^C2};
  then
A5: x = C1^C2 by TARSKI:def 1;
A6: C1 in {C1} by TARSKI:def 1;
  C2 in {C2} by TARSKI:def 1;
  hence thesis by A5,A6;
end;

theorem
  {C1,C2} before {C} = {C1^C, C2^C}
proof
  thus
  {C1,C2} before {C} = ({C1} \/ {C2}) before {C} by ENUMSET1:41
    .= ({C1} before {C}) \/ ({C2} before {C}) by Th43
    .= {C1^C} \/ ({C2} before {C}) by Th45
    .= {C1^C} \/ {C2^C} by Th45
    .= {C1^C, C2^C} by ENUMSET1:41;
end;

theorem
  {C} before {C1,C2} = {C^C1, C^C2}
proof
  thus
  {C} before {C1,C2} = {C} before ({C1} \/ {C2}) by ENUMSET1:41
    .= ({C} before {C1}) \/ ({C} before {C2}) by Th44
    .= {C^C1} \/ ({C} before {C2}) by Th45
    .= {C^C1} \/ {C^C2} by Th45
    .= {C^C1, C^C2} by ENUMSET1:41;
end;

begin :: Concurrent composition

definition
  let P, N, R1, R2;
  func R1 concur R2 -> process of N equals
  {C: ex q1,q2 being FinSubsequence st C = q1 \/ q2 & q1 misses q2 &
  Seq q1 in R1 & Seq q2 in R2};
  coherence
  proof
    set X = {C: ex q1,q2 being FinSubsequence st C = q1 \/ q2 &
    q1 misses q2 & Seq q1 in R1 & Seq q2 in R2};
    X c= N*
    proof
      let x;
      assume x in X;
      then ex C st x = C & ex q1,q2 being FinSubsequence st C = q1 \/ q2 &
      q1 misses q2 & Seq q1 in R1 & Seq q2 in R2;
      hence thesis;
    end;
    hence thesis;
  end;
  commutativity
  proof
    let R,R1,R2;
    assume
A1: R = {C1: ex q1,q2 being FinSubsequence st C1 = q1 \/ q2 &
    q1 misses q2 & Seq q1 in R1 & Seq q2 in R2};
    thus R c= {C2: ex q1,q2 being FinSubsequence st C2 = q1 \/ q2 &
    q1 misses q2 & Seq q1 in R2 & Seq q2 in R1}
    proof
      let x;
      assume x in R;
      then
A2:   ex C1 st ( x = C1)&( ex q1,q2 being FinSubsequence st C1 =
      q1 \/ q2 & q1 misses q2 & Seq q1 in R1 & Seq q2 in R2) by A1;
      thus thesis by A2;
    end;
    let x;
    assume x in {C2: ex q1,q2 being FinSubsequence st C2 = q1 \/ q2 &
    q1 misses q2 & Seq q1 in R2 & Seq q2 in R1};
    then ex C2 st ( x = C2)&( ex q1,q2 being FinSubsequence st C2 =
    q1 \/ q2 & q1 misses q2 & Seq q1 in R2 & Seq q2 in R1);
    hence thesis by A1;
  end;
end;

theorem Th48:
  (R1 \/ R2) concur R = (R1 concur R) \/ (R2 concur R)
proof
  thus (R1 \/ R2) concur R c= (R1 concur R) \/ (R2 concur R)
  proof
    let x;
    assume x in (R1 \/ R2) concur R;
    then consider C such that
A1: x = C and
A2: ex q1,q2 being FinSubsequence st C = q1 \/ q2 & q1 misses q2 & Seq
    q1 in R1 \/ R2 & Seq q2 in R;
    consider q1,q2 being FinSubsequence such that
A3: C = q1 \/ q2 and
A4: q1 misses q2 and
A5: Seq q1 in R1 \/ R2 and
A6: Seq q2 in R by A2;
    Seq q1 in R1 or Seq q1 in R2 by A5,XBOOLE_0:def 3;
    then x in {C1: ex q1,q2 being FinSubsequence st C1 = q1 \/ q2 & q1
    misses q2 & Seq q1 in R1 & Seq q2 in R} or
    x in {C2: ex q1,q2 being FinSubsequence st C2 = q1 \/ q2 & q1 misses q2
    & Seq q1 in R2 & Seq q2 in R} by A1,A3,A4,A6;
    hence thesis by XBOOLE_0:def 3;
  end;
  let x be set;
  assume
A7: x in (R1 concur R) \/ (R2 concur R);
  per cases by A7,XBOOLE_0:def 3;
  suppose x in R1 concur R;
    then consider C such that
A8: x = C and
A9: ex q1,q2 being FinSubsequence st C = q1 \/ q2 & q1 misses q2 & Seq
    q1 in R1 & Seq q2 in R;
    consider q1,q2 being FinSubsequence such that
A10: C = q1 \/ q2 and
A11: q1 misses q2 and
A12: Seq q1 in R1 and
A13: Seq q2 in R by A9;
    Seq q1 in R1 \/ R2 by A12,XBOOLE_0:def 3;
    hence thesis by A8,A10,A11,A13;
  end;
  suppose x in R2 concur R;
    then consider C such that
A14: x = C and
A15: ex q1,q2 being FinSubsequence st C = q1 \/ q2 & q1 misses q2 & Seq
    q1 in R2 & Seq q2 in R;
    consider q1,q2 being FinSubsequence such that
A16: C = q1 \/ q2 and
A17: q1 misses q2 and
A18: Seq q1 in R2 and
A19: Seq q2 in R by A15;
    Seq q1 in R1 \/ R2 by A18,XBOOLE_0:def 3;
    hence thesis by A14,A16,A17,A19;
  end;
end;

theorem Th49:
  {<*e1*>} concur {<*e2*>} = {<*e1,e2*>, <*e2,e1*>}
proof
  set C1 = <*e1*>, C2 = <*e2*>;
  set R1 = {C1}, R2 = {C2};
  thus {C1} concur {C2} c= {<*e1,e2*>, <*e2,e1*>}
  proof
    let x;
    assume x in {C1} concur {C2};
    then consider C such that
A1: x = C and
A2: ex q1,q2 being FinSubsequence st C = q1 \/ q2 & q1 misses q2 & Seq
    q1 in R1 & Seq q2 in R2;
    consider q1,q2 being FinSubsequence such that
A3: C = q1 \/ q2 and
A4: q1 misses q2 and
A5: Seq q1 in R1 and
A6: Seq q2 in R2 by A2;
A7: Seq q1 = C1 by A5,TARSKI:def 1;
A8: Seq q2 = C2 by A6,TARSKI:def 1;
    consider i such that
A9: q1 = {[i,e1]} by A7,Th4;
    consider j such that
A10: q2 = {[j,e2]} by A8,Th4;
A11: [i,e1] in q1 by A9,TARSKI:def 1;
A12: [j,e2] in q2 by A10,TARSKI:def 1;
A13: C = {[i,e1], [j,e2]} by A3,A9,A10,ENUMSET1:41;
    then i = 1 & j = 1 & e1 = e2 or i = 1 & j = 2 or i = 2 & j = 1 by Th5;
    then C = <*e1,e2*> or C = <*e2,e1*> by A4,A11,A12,A13,Th6,XBOOLE_0:3;
    hence thesis by A1,TARSKI:def 2;
  end;
  let x;
  assume
A14: x in {<*e1,e2*>, <*e2,e1*>};
  per cases by A14,TARSKI:def 2;
  suppose
A15: x = <*e1,e2*>;
    then
A16: x = {[1,e1], [2,e2]} by Th6
      .= {[1,e1]} \/ {[2,e2]} by ENUMSET1:41;
    reconsider q1 = {[1,e1]}, q2 = {[2,e2]} as FinSubsequence by Th1;
    [1,e1] <> [2,e2] by ZFMISC_1:33;
    then not [1,e1] in q2 by TARSKI:def 1;
    then
A17: q1 misses q2 by ZFMISC_1:56;
A18: Seq q1 = <*e1*> by Th3;
A19: Seq q2 = <*e2*> by Th3;
A20: <*e1*> in R1 by TARSKI:def 1;
    <*e2*> in R2 by TARSKI:def 1;
    hence thesis by A15,A16,A17,A18,A19,A20;
  end;
  suppose
A21: x = <*e2,e1*>;
    then
A22: x = {[1,e2], [2,e1]} by Th6
      .= {[1,e2]} \/ {[2,e1]} by ENUMSET1:41;
    reconsider q1 = {[2,e1]}, q2 = {[1,e2]} as FinSubsequence by Th1;
    [1,e2] <> [2,e1] by ZFMISC_1:33;
    then not [2,e1] in q2 by TARSKI:def 1;
    then
A23: q1 misses q2 by ZFMISC_1:56;
A24: Seq q1 = <*e1*> by Th3;
A25: Seq q2 = <*e2*> by Th3;
A26: <*e1*> in R1 by TARSKI:def 1;
    <*e2*> in R2 by TARSKI:def 1;
    hence thesis by A21,A22,A23,A24,A25,A26;
  end;
end;

theorem
  {<*e1*>,<*e2*>} concur {<*e*>} = {<*e1,e*>, <*e2,e*>, <*e,e1*>, <*e,e2*>}
proof
  {<*e1*>,<*e2*>} = {<*e1*>} \/ {<*e2*>} by ENUMSET1:41;
  then
A1: {<*e1*>,<*e2*>} concur {<*e*>}
  = {<*e1*>} concur {<*e*>} \/ {<*e2*>} concur {<*e*>} by Th48;
A2: {<*e1*>} concur {<*e*>} = {<*e1,e*>,<*e,e1*>} by Th49;
  {<*e2*>} concur {<*e*>} = {<*e2,e*>,<*e,e2*>} by Th49;
  hence {<*e1*>,<*e2*>} concur {<*e*>}
  = {<*e1,e*>, <*e,e1*>, <*e2,e*>, <*e,e2*>} by A1,A2,ENUMSET1:45
    .= {<*e1,e*>, <*e2,e*>, <*e,e1*>, <*e,e2*>} by ENUMSET1:104;
end;

theorem
  (R1 before R2) before R3 = R1 before (R2 before R3)
proof
  thus (R1 before R2) before R3 c= R1 before (R2 before R3)
  proof
    let x;
    assume x in (R1 before R2) before R3;
    then consider C1,C2 such that
A1: x = C1^C2 and
A2: C1 in (R1 before R2) and
A3: C2 in R3;
    consider fs1,fs2 such that
A4: C1 = fs1^fs2 and
A5: fs1 in R1 and
A6: fs2 in R2 by A2;
A7: x = fs1^(fs2^C2) by A1,A4,FINSEQ_1:45;
    consider C3 such that
A8: C3 = fs2^C2 and
A9: fs2 in R2 and
A10: C2 in R3 by A3,A6;
    C3 in R2 before R3 by A8,A9,A10;
    hence thesis by A5,A7,A8;
  end;
  let x;
  assume x in R1 before (R2 before R3);
  then consider C1,C2 such that
A11: x = C1^C2 and
A12: C1 in R1 and
A13: C2 in R2 before R3;
  consider fs1,fs2 such that
A14: C2 = fs1^fs2 and
A15: fs1 in R2 and
A16: fs2 in R3 by A13;
A17: x = (C1^fs1)^fs2 by A11,A14,FINSEQ_1:45;
  consider C such that
A18: C = C1^fs1 and
A19: C1 in R1 and
A20: fs1 in R2 by A12,A15;
  C in R1 before R2 by A18,A19,A20;
  hence thesis by A16,A17,A18;
end;

definition
  let p be FinSubsequence;
  let i be Element of NAT;
  set X = {i+k where k is Element of NAT: k in dom p};
  func i Shift p -> FinSubsequence means
  :Def15:
  dom it = {i+k where k is Element of NAT: k in dom p} &
  for j being Nat st j in dom p holds it.(i+j) = p.j;
  existence
  proof
    consider K being Nat such that
A1: dom p c= Seg K by FINSEQ_1:def 12;
    defpred P[set,set] means ex k st $1 = i+k & $2 = p.k;
A2: for x st x in X ex y st P[x,y]
    proof
      let x;
      assume x in X;
      then consider k such that
A3:   x = i+k and
A4:   k in dom p;
      [k,p.k] in p by A4,FUNCT_1:def 4;
      hence thesis by A3;
    end;
    consider f being Function such that
A5: dom f = X and
A6: for x st x in X holds P[x,f.x] from CLASSES1:sch 1(A2);
    X c= Seg (i+K)
    proof
      let x;
      assume x in X;
      then consider k such that
A7:   x = i+k and
A8:   k in dom p;
A9:   i+k >= k by NAT_1:11;
A10:  k >= 1 by A1,A8,FINSEQ_1:3;
A11:  k <= K by A1,A8,FINSEQ_1:3;
A12:  i+k >= 1 by A9,A10,XXREAL_0:2;
      i+k <= i+K by A11,XREAL_1:8;
      hence thesis by A7,A12,FINSEQ_1:3;
    end;
    then reconsider f as FinSubsequence by A5,FINSEQ_1:def 12;
    take f;
    thus dom f = X by A5;
    let j be Nat;
A13: j in NAT by ORDINAL1:def 13;
    assume j in dom p;
    then i+j in X by A13;
    then ex k st i+j = i+k & f.(i+j) = p.k by A6;
    hence thesis;
  end;
  uniqueness
  proof
    let f1,f2 be FinSubsequence such that
A14: dom f1 = X and
A15: for j being Nat st j in dom p holds f1.(i+j) = p.j and
A16: dom f2 = X and
A17: for j being Nat st j in dom p holds f2.(i+j) = p.j;
    now
      let x be set;
      assume x in X;
      then consider k such that
A18:  x = i+k and
A19:  k in dom p;
      thus f1.x = p.k by A15,A18,A19
        .= f2.x by A17,A18,A19;
    end;
    hence thesis by A14,A16,FUNCT_1:9;
  end;
end;

reserve q,q1,q2,q3,q4 for FinSubsequence;

theorem
  0 Shift q = q
proof
  set X = {0 qua Nat+k where k is Element of NAT: k in dom q};
A1: dom (0 Shift q) = X by Def15;
A2: X = dom q
  proof
    thus X c= dom q
    proof
      let x;
      assume x in X;
      then ex k st ( x = 0 qua Nat+k)&( k in dom q);
      hence thesis;
    end;
    let x;
    assume
A3: x in dom q;
    consider l being Nat such that
A4: dom q c= Seg l by FINSEQ_1:def 12;
    x in Seg l by A3,A4;
    then reconsider k = x as Element of NAT;
    x = 0 qua Nat+k;
    hence thesis by A3;
  end;
  now
    let x;
    assume x in X;
    then ex k st ( x = 0 qua Nat+k)&( k in dom q);
    hence (0 Shift q).x = q.x by Def15;
  end;
  hence thesis by A1,A2,FUNCT_1:9;
end;

theorem
  (i+j) Shift q = i Shift (j Shift q)
proof
  set Sj = j Shift q;
  set Sij = (i+j) Shift q;
  set Si = i Shift Sj;
  set Xj = {j+k where k is Element of NAT: k in dom q};
  set Xi = {i+k where k is Element of NAT: k in dom Sj};
  set Xij = {(i+j)+k where k is Element of NAT: k in dom q};
A1: dom Sj = Xj by Def15;
A2: dom Si = Xi by Def15;
A3: dom Sij = Xij by Def15;
A4: Xi = Xij
  proof
    thus Xi c= Xij
    proof
      let x;
      assume x in Xi;
      then consider k such that
A5:   x = i+k and
A6:   k in dom Sj;
      dom (j Shift q) = {j+K where K is Element of NAT: K in dom q} by
      Def15;
      then consider K being Element of NAT such that
A7:   k = j+K and
A8:   K in dom q by A6;
      x = i+j+K by A5,A7;
      hence thesis by A8;
    end;
    let x;
    assume x in Xij;
    then consider k such that
A9: x = (i+j)+k and
A10: k in dom q;
    reconsider K = j+k as Element of NAT;
A11: dom Sj = {j+l where l is Element of NAT: l in dom q} by Def15;
A12: x = i+K by A9;
    K in dom Sj by A10,A11;
    hence thesis by A12;
  end;
  now
    let x;
    assume x in Xij;
    then consider k such that
A13: x = (i+j)+k and
A14: k in dom q;
A15: x = i+(j+k) by A13;
A16: j+k in dom Sj by A1,A14;
    thus Sij.x = q.k by A13,A14,Def15
      .= Sj.(j+k) by A14,Def15
      .= Si.x by A15,A16,Def15;
  end;
  hence thesis by A2,A3,A4,FUNCT_1:9;
end;

theorem Th54:
  for p being FinSequence
  holds
  dom (i Shift p) = {j1: i+1 <= j1 & j1 <= i+(len p)}
proof
  let p be FinSequence;
A1: dom p = Seg len p by FINSEQ_1:def 3
    .= {k: 1 <= k & k <= len p} by FINSEQ_1:def 1;
  set X = {j1: i+1 <= j1 & j1 <= i+(len p)};
A2: dom (i Shift p) = {i+k1: k1 in dom p} by Def15;
  thus dom (i Shift p) c= X
  proof
    let x;
    assume x in dom (i Shift p);
    then consider k1 such that
A3: x = i+k1 and
A4: k1 in dom p by A2;
    consider k such that
A5: k1 = k and
A6: 1 <= k and
A7: k <= len p by A1,A4;
A8: i+1 <= i+k by A6,XREAL_1:9;
    i+k <= i+(len p) by A7,XREAL_1:9;
    hence thesis by A3,A5,A8;
  end;
  let x;
  assume x in X;
  then consider j1 such that
A9: x = j1 and
A10: i+1 <= j1 and
A11: j1 <= i+(len p);
  i+(0 qua Nat) <= i+1 by XREAL_1:9;
  then consider k2 being Nat such that
A12: j1 = i+k2 by A10,NAT_1:10,XXREAL_0:2;
A13: k2 in NAT by ORDINAL1:def 13;
A14: 1 <= k2 by A10,A12,XREAL_1:8;
  k2 <= len p by A11,A12,XREAL_1:8;
  then k2 in dom p by A1,A13,A14;
  hence thesis by A2,A9,A12;
end;

theorem
  for q being FinSubsequence holds q = {} iff i Shift q = {}
proof
  let q be FinSubsequence;
A1: dom (i Shift q) = {i+k: k in dom q} by Def15;
  thus q = {} implies (i Shift q) = {}
  proof
    assume
A2: q = {};
    now
      let x,y;
      now
        assume x in dom (i Shift q);
        then ex k st ( x = i+k)&( k in dom q) by A1;
        hence contradiction by A2;
      end;
      hence not [x,y] in (i Shift q) by FUNCT_1:8;
    end;
    hence thesis by RELAT_1:56;
  end;
  assume
A3: (i Shift q) = {};
  now
    assume q <> {};
    then consider x such that
A4: x in dom q by XBOOLE_0:def 1;
A5: x in rng Sgm dom q by A4,FINSEQ_1:71;
    rng Sgm dom q c= NAT by FINSEQ_1:def 4;
    then reconsider x as Element of NAT by A5;
    consider k such that
A6: k = i+x and
A7: x in dom q by A4;
    k in dom (i Shift q) by A1,A6,A7;
    hence contradiction by A3;
  end;
  hence thesis;
end;

theorem Th56:
  for q being FinSubsequence ex ss being FinSubsequence st
  dom ss = dom q & rng ss = dom (i Shift q) &
  (for k st k in dom q holds ss.k = i+k) & ss is one-to-one
proof
  let q be FinSubsequence;
  consider n being Nat such that
A1: dom q c= Seg n by FINSEQ_1:def 12;
  defpred P[set,set] means ex k st k = $1 & $2 = i+k;
A2: for x st x in dom q ex y st P[x,y]
  proof
    let x;
    assume x in dom q;
    then x in Seg n by A1;
    then reconsider x as Element of NAT;
    take i+x;
    thus thesis;
  end;
  consider f being Function such that
A3: dom f = dom q and
A4: for x st x in dom q holds P[x, f.x] from CLASSES1:sch 1(A2);
  reconsider ss = f as FinSubsequence by A1,A3,FINSEQ_1:def 12;
A5: dom (i Shift q) = {i+k: k in dom q} by Def15;
A6: rng ss = dom (i Shift q)
  proof
    thus rng ss c= dom (i Shift q)
    proof
      let y;
      assume y in rng ss;
      then consider x such that
A7:   x in dom ss and
A8:   y = ss.x by FUNCT_1:def 5;
      ex k1 st ( k1 = x)&( ss.x = i+k1) by A3,A4,A7;
      hence thesis by A3,A5,A7,A8;
    end;
    let y;
    assume y in dom (i Shift q);
    then consider k2 such that
A9: y = i+k2 and
A10: k2 in dom q by A5;
    ex k3 being Element of NAT st ( k3 = k2)&( ss.k2 = i+k3)
    by A4,A10;
    hence thesis by A3,A9,A10,FUNCT_1:def 5;
  end;
A11: for k st k in dom q holds ss.k = i+k
  proof
    let k;
    assume k in dom q;
    then ex k1 st ( k1 = k)&( ss.k = i+k1) by A4;
    hence thesis;
  end;
  for x1,x2 st x1 in dom ss & x2 in dom ss & ss.x1 = ss.x2 holds x1 = x2
  proof
    let x1,x2;
    assume that
A12: x1 in dom ss and
A13: x2 in dom ss and
A14: ss.x1 = ss.x2;
A15: ex k1 st ( k1 = x1)&( ss.x1 = i+k1) by A3,A4,A12;
    ex k2 st ( k2 = x2)&( ss.x2 = i+k2) by A3,A4,A13;
    hence thesis by A14,A15;
  end;
  then ss is one-to-one by FUNCT_1:def 8;
  hence thesis by A3,A6,A11;
end;

Lm7: for p being FinSequence holds ex fs being FinSequence st
dom fs = dom p & rng fs = dom (i Shift p) &
(for k st k in dom p holds fs.k = i+k) & fs is one-to-one
proof
  let p be FinSequence;
  consider ss being FinSubsequence such that
A1: dom ss = dom p and
A2: rng ss = dom (i Shift p) and
A3: for k st k in dom p holds ss.k = i+k and
A4: ss is one-to-one by Th56;
  dom ss = Seg len p by A1,FINSEQ_1:def 3;
  then reconsider fs = ss as FinSequence by FINSEQ_1:def 2;
  dom fs = dom p by A1;
  hence thesis by A2,A3,A4;
end;

theorem Th57:
  for q being FinSubsequence holds card q = card (i Shift q)
proof
  let q be FinSubsequence;
  ex ss being FinSubsequence st ( dom ss = dom q)&( rng ss =
  dom (i Shift q))&( for k st k in dom q holds ss.k = i+k)&( ss is one-to-one)
  by Th56;
  then
A1: dom q, dom (i Shift q) are_equipotent by WELLORD2:def 4;
A2: card dom q = card q by CARD_1:104;
  card dom (i Shift q) = card (i Shift q) by CARD_1:104;
  hence thesis by A1,A2,CARD_1:21;
end;

theorem Th58:
  for p being FinSequence holds dom p = dom Seq (i Shift p)
proof
  let p be FinSequence;
A1: rng Sgm dom (i Shift p) = dom (i Shift p) by FINSEQ_1:71;
A2: Seq (i Shift p) = (i Shift p)*(Sgm dom (i Shift p)) by FINSEQ_1:def 14;
A3: dom p = Seg len p by FINSEQ_1:def 3;
A4: dom Sgm dom (i Shift p) = Seg len Sgm dom (i Shift p) by FINSEQ_1:def 3;
  ex k be Nat st ( dom (i Shift p) c= Seg k) by FINSEQ_1:def 12;
  then
A5: len Sgm dom (i Shift p) = card dom (i Shift p) by FINSEQ_3:44;
  card dom (i Shift p) = card (i Shift p) by CARD_1:104;
  then card dom (i Shift p) = len p by Th57;
  hence thesis by A1,A2,A3,A4,A5,RELAT_1:46;
end;

theorem Th59:
  for p being FinSequence st k in dom p holds (Sgm dom (i Shift p)).k = i + k
proof
  let p be FinSequence;
  assume
A1: k in dom p;
  consider fs being FinSequence such that
A2: dom fs = dom p and
A3: rng fs = dom (i Shift p) and
A4: for j st j in dom p holds fs.j = i+j
  and fs is one-to-one by Lm7;
A5: ex l be Nat st ( dom (i Shift p) c= Seg l) by FINSEQ_1:def 12;
  rng fs = rng Sgm dom (i Shift p) by A3,FINSEQ_1:71;
  then reconsider fs as FinSequence of NAT by FINSEQ_1:def 4;
  for n,m,k1,k2 being natural number st 1 <= n & n < m & m <= len fs &
  k1 = fs.n & k2 = fs.m holds k1 < k2
  proof
    let n,m,k1,k2 be natural number;
    assume that
A6: 1 <= n and
A7: n < m and
A8: m <= len fs and
A9: k1 = fs.n and
A10: k2 = fs.m;
    reconsider n,m as Element of NAT by ORDINAL1:def 13;
A11: dom fs = Seg len fs by FINSEQ_1:def 3
      .= {n1 where n1 is Element of NAT: 1 <= n1 & n1 <= len fs}
    by FINSEQ_1:def 1;
    n+1 <= m by A7,INT_1:20;
    then n+1 <= len fs by A8,XXREAL_0:2;
    then
A12: n <= (len fs) - 1 by XREAL_1:21;
    (len fs) + (0 qua Nat) <= (len fs) + 1 by XREAL_1:9;
    then (len fs) - 1 <= len fs by XREAL_1:22;
    then n <= len fs by A12,XXREAL_0:2;
    then
A13: n in dom p by A2,A6,A11;
    1 <= m by A6,A7,XXREAL_0:2;
    then
A14: m in dom p by A2,A8,A11;
A15: k1 = i+n by A4,A9,A13;
    k2 = i+m by A4,A10,A14;
    hence thesis by A7,A15,XREAL_1:10;
  end;
  then fs = Sgm dom (i Shift p) by A3,A5,FINSEQ_1:def 13;
  hence thesis by A1,A4;
end;

theorem Th60:
  for p being FinSequence st k in dom p holds (Seq (i Shift p)).k = p.k
proof
  let p be FinSequence;
  assume
A1: k in dom p;
  then
A2: k in dom Seq (i Shift p) by Th58;
A3: Seq (i Shift p) = (i Shift p)*(Sgm dom (i Shift p)) by FINSEQ_1:def 14;
A4: (Seq (i Shift p)).k = ((i Shift p)*(Sgm dom (i Shift p))).k by
  FINSEQ_1:def 14;
  ((i Shift p)*(Sgm dom (i Shift p))).k
  = (i Shift p).((Sgm dom (i Shift p)).k) by A2,A3,FUNCT_1:22
    .= (i Shift p).(i+k) by A1,Th59;
  hence thesis by A1,A4,Def15;
end;

theorem Th61:
  for p being FinSequence holds Seq (i Shift p) = p
proof
  let p be FinSequence;
A1: dom Seq (i Shift p) = dom p by Th58;
  x in dom p implies (Seq (i Shift p)).x = p.x by Th60;
  hence thesis by A1,FUNCT_1:9;
end;

reserve p1,p2 for FinSequence;

theorem Th62:
  dom (p1 \/ ((len p1) Shift p2)) = Seg (len p1 + len p2)
proof
A6: Seg (len p1 + len p2) = {j: 1 <= j & j <= len p1 + len p2}
    by FINSEQ_1:def 1;
A7: dom (p1 \/ ((len p1) Shift p2)) = dom p1 \/ dom ((len p1) Shift p2)
    by RELAT_1:13;
A8: dom p1 = Seg len p1 by FINSEQ_1:def 3
      .= {k: 1 <= k & k <= len p1} by FINSEQ_1:def 1;
A9: dom ((len p1) Shift p2) = {k1: len p1 + 1 <= k1 & k1 <= len p1 + len p2}
    by Th54;
    thus dom (p1 \/ ((len p1) Shift p2)) c= Seg (len p1 + len p2)
    proof
      let x;
      assume x in dom (p1 \/ ((len p1) Shift p2));
      then x in dom p1 or x in dom ((len p1) Shift p2) by A7,
      XBOOLE_0:def 3;
      then
A10:  ex k3 being Element of NAT st ( x = k3 & 1 <= k3 & k3 <=
      len p1 or x = k3 & len p1 + 1 <= k3 & k3 <= len p1 + len p2) by A8,A9;
      then reconsider x as Element of NAT;
A11:  1 <= x by A10,Lm2;
      x <= len p1 + len p2 by A10,Lm2;
      hence thesis by A6,A11;
    end;
    let x;
    assume x in Seg (len p1 + len p2);
    then consider j such that
A12: x = j and
A13: 1 <= j and
A14: j <= len p1 + len p2 by A6;
    reconsider i0 = len p1 as Integer;
    1 <= j & j <= i0 or i0 + 1 <= j & j <= i0 + len p2 by A13,A14,INT_1:20;
    then x in dom p1 or x in dom ((len p1) Shift p2) by A8,A9,A12;
    hence thesis by A7,XBOOLE_0:def 3;
end;

theorem Th63:
  for p1 being FinSequence, p2 being FinSubsequence st len p1 <= i holds
  dom p1 misses dom (i Shift p2)
proof
  let p1 be FinSequence, p2 be FinSubsequence;
  assume
A1: len p1 <= i;
A2: dom p1 = Seg len p1 by FINSEQ_1:def 3
    .= {k: 1 <= k & k <= len p1} by FINSEQ_1:def 1;
A3: dom (i Shift p2) = {i+k: k in dom p2} by Def15;
  not ex x st x in dom p1 /\ dom (i Shift p2)
  proof
    given x such that
A4: x in dom p1 /\ dom (i Shift p2);
A5: x in dom p1 by A4,XBOOLE_0:def 4;
A6: x in dom (i Shift p2) by A4,XBOOLE_0:def 4;
A7: ex k1 st ( x = k1)&( 1 <= k1)&( k1 <= len p1) by A2,A5;
    consider k2 such that
A8: x = i+k2 and
A9: k2 in dom p2 by A3,A6;
    consider n being Nat such that
A10: dom p2 c= Seg n by FINSEQ_1:def 12;
A11: k2 in Seg n by A9,A10;
    Seg n = {m where m is Element of NAT: 1 <= m & m <= n} by FINSEQ_1:def 1;
    then ex m being Element of NAT st ( k2 = m)&( 1 <= m)&( m <= n)
    by A11;
    then
A12: 1-1 < k2-0 by XREAL_1:17;
    reconsider x as Element of NAT by A4;
    len p1 + k2 <= i+k2 by A1,XREAL_1:9;
    then (len p1 + k2) - k2 < x - 0 by A8,A12,XREAL_1:17;
    hence contradiction by A7;
  end;
  hence dom p1 /\ dom (i Shift p2) = {} by XBOOLE_0:def 1;
end;

theorem Th64:
  for p1,p2 being FinSequence holds p1^p2 = p1 \/ ((len p1) Shift p2)
proof
  let p1,p2;
A1: dom (p1 \/ ((len p1) Shift p2)) = Seg (len p1 + len p2) by Th62;
  dom p1 misses dom ((len p1) Shift p2) by Th63;
  then reconsider p = p1 \/ ((len p1) Shift p2) as FinSequence by A1,
  FINSEQ_1:def 2,GRFUNC_1:31;
A2: dom p = Seg (len p1 + len p2) by Th62;
A3: for k be Nat st k in dom p1 holds p.k = p1.k
  proof
    let k be Nat;
    assume k in dom p1;
    then [k,p1.k] in p1 by FUNCT_1:def 4;
    then [k,p1.k] in p by XBOOLE_0:def 3;
    hence thesis by FUNCT_1:8;
  end;
  for k be Nat st k in dom p2 holds p.(len p1 + k) = p2.k
  proof
    let k be Nat;
    assume
A4: k in dom p2;
    dom ((len p1) Shift p2) = {len p1 + j: j in dom p2} by Def15;
    then len p1 + k in dom ((len p1) Shift p2) by A4;
    then [len p1 + k, ((len p1) Shift p2).(len p1 + k)] in
    ((len p1) Shift p2) by FUNCT_1:def 4;
    then [len p1 + k, ((len p1) Shift p2).(len p1 + k)] in p
    by XBOOLE_0:def 3;
    then p.(len p1 + k) = ((len p1) Shift p2).(len p1 + k) by FUNCT_1:8;
    hence thesis by A4,Def15;
  end;
  hence thesis by A2,A3,FINSEQ_1:def 7;
end;

theorem Th65:
  for p1 being FinSequence, p2 being FinSubsequence st i >= len p1
  holds p1 misses i Shift p2
proof
  let p1 be FinSequence, p2 be FinSubsequence;
  assume i >= len p1;
  then dom p1 misses dom (i Shift p2) by Th63;
  hence thesis by Th8;
end;

theorem
  (R1 concur R2) concur R3 = R1 concur (R2 concur R3)
proof
  thus (R1 concur R2) concur R3 c= R1 concur (R2 concur R3)
  proof
    let x;
    assume x in (R1 concur R2) concur R3;
    then consider C1 such that
A1: x = C1 and
A2: ex q1,q2 st C1 = q1 \/ q2 & q1 misses q2 & Seq q1 in R1 concur R2 &
    Seq q2 in R3;
    consider q1,q2 such that
A3: C1 = q1 \/ q2 and
A4: q1 misses q2 and
A5: Seq q1 in R1 concur R2 and
A6: Seq q2 in R3 by A2;
    consider C2 such that
A7: Seq q1 = C2 and
A8: ex q3,q4 st C2 = q3 \/ q4 & q3 misses q4 & Seq q3 in R1 & Seq q4 in
    R2
    by A5;
    consider q3,q4 such that
A9: C2 = q3 \/ q4 and
A10: q3 misses q4 and
A11: Seq q3 in R1 and
A12: Seq q4 in R2 by A8;
    consider n1 being Nat such that
A13: dom q1 c= Seg n1 by FINSEQ_1:def 12;
A14: q3 c= Seq q1 by A7,A9,XBOOLE_1:7;
A15: q4 c= Seq q1 by A7,A9,XBOOLE_1:7;
    Sgm dom q1 is one-to-one by A13,FINSEQ_3:99;
    then
A16: (Sgm dom q1).:dom q3 misses (Sgm dom q1).:dom q4 by A10,A14,A15,
    Th10,FUNCT_1:125;
A17: rng ((Sgm dom q1)|dom q3) = (Sgm dom q1).:dom q3 by RELAT_1:148;
A18: rng ((Sgm dom q1)|dom q4) = (Sgm dom q1).:dom q4 by RELAT_1:148;
    then
A19: q1|rng((Sgm dom q1)|dom q3) misses q1|rng((Sgm dom q1)|dom q4) by A16
    ,A17,Th9;
A20: dom Sgm dom q1 = dom (q3 \/ q4) by A7,A9,Lm4
      .= dom q3 \/ dom q4 by RELAT_1:13;
A21: q1|rng((Sgm dom q1)|dom q3) \/ q1|rng((Sgm dom q1)|dom q4)
    = q1|(rng((Sgm dom q1)|dom q3) \/ rng((Sgm dom q1)|dom q4)) by RELAT_1:107
      .= q1|((Sgm dom q1).:(dom q3 \/ dom q4)) by A17,A18,RELAT_1:153
      .= q1|rng((Sgm dom q1)|(dom Sgm dom q1)) by A20,RELAT_1:148
      .= q1|rng Sgm dom q1 by RELAT_1:98
      .= q1|dom q1 by FINSEQ_1:71
      .= q1 by RELAT_1:98;
A32: q1 c= C1 by A3,XBOOLE_1:7;
A33: q1|rng((Sgm dom q1)|dom q3) c= q1 by A21,XBOOLE_1:7;
A34: q1|rng((Sgm dom q1)|dom q4) c= q1 by A21,XBOOLE_1:7;
    rng C1 = rng q1 \/ rng q2 by A3,RELAT_1:26;
    then
A35: rng q1 c= rng C1 by XBOOLE_1:7;
A36: rng q1 = rng Seq q1 by Lm5;
A37: rng Seq q1 c= rng C1 by A35,Lm5;
A38: rng Seq q1 = rng q3 \/ rng q4 by A7,A9,RELAT_1:26;
    then
A39: rng q3 c= rng Seq q1 by XBOOLE_1:7;
    rng q3 = rng Seq q3 by Lm5;
    then
A40: rng Seq q3 c= rng C1 by A37,A39,XBOOLE_1:1;
A41: rng q4 c= rng Seq q1 by A38,XBOOLE_1:7;
    rng q4 = rng Seq q4 by Lm5;
    then
A42: rng Seq q4 c= rng C1 by A37,A41,XBOOLE_1:1;
    reconsider q5 = q1|rng((Sgm dom q1)|dom q3),
    q6 = q1|rng((Sgm dom q1)|dom q4) as FinSubsequence;
    reconsider fs = C1 as FinSequence of rng C1 by FINSEQ_1:def 4;
    reconsider fs1 = Seq q1 as FinSequence of rng C1 by A35,A36,FINSEQ_1:def 4;
    reconsider fs2 = Seq q3 as FinSequence of rng C1 by A40,FINSEQ_1:def 4;
    reconsider fs3 = Seq q4 as FinSequence of rng C1 by A42,FINSEQ_1:def 4;
    reconsider fss = q1, fss1 = q5, fss2 = q6 as Subset of fs
    by A32,A33,A34,XBOOLE_1:1;
    reconsider fss3 = q3, fss4 = q4 as Subset of fs1 by A7,A9,XBOOLE_1:7;
A43: Seq fss3 = fs2;
    fss1 = fss|rng((Sgm dom fss)|dom fss3);
    then
A44: Seq q3 = Seq q5 by A43,GRAPH_2:30;
A45: Seq fss4 = fs3;
A46: fss2 = fss|rng((Sgm dom fss)|dom fss4);
    q1 /\ q2 = {} by A4,XBOOLE_0:def 7;
    then
A47: q5 /\ q2 \/ q6 /\ q2 = {} by A21,XBOOLE_1:23;
    then
A48: q5 /\ q2 = {};
    q6 /\ q2 = {} by A47;
    then
A49: q6 misses q2 by XBOOLE_0:def 7;
A50: q1 c= C1 by A3,XBOOLE_1:7;
A51: q2 c= C1 by A3,XBOOLE_1:7;
    q6 c= q1 by A21,XBOOLE_1:7;
    then q6 c= C1 by A50,XBOOLE_1:1;
    then
A52: dom q6 misses dom q2 by A49,A51,Th10;
    then reconsider q7 = q6 \/ q2 as Function by GRFUNC_1:31;
A53: dom C1 = dom q1 \/ dom q2 by A3,RELAT_1:13
      .= (dom q5 \/ dom q6) \/ dom q2 by A21,RELAT_1:13
      .= dom q5 \/ (dom q6 \/ dom q2) by XBOOLE_1:4
      .= dom q5 \/ dom q7 by RELAT_1:13;
    then
A54: dom q7 c= dom C1 by XBOOLE_1:7;
A55: dom C1 = Seg len C1 by FINSEQ_1:def 3;
    then reconsider q7 as FinSubsequence by A54,FINSEQ_1:def 12;
A56: C1 = q5 \/ q7 by A3,A21,XBOOLE_1:4;
A57: q5 /\ q7 = q5 /\ q6 \/ q5 /\ q2 by XBOOLE_1:23;
    q5 /\ q6 = {} by A19,XBOOLE_0:def 7;
    then
A58: q5 misses q7 by A48,A57,XBOOLE_0:def 7;
A59: rng Seq q7 = rng q7 by Lm5;
    rng C1 = rng q7 \/ rng q5 by A56,RELAT_1:26;
    then
A60: rng Seq q7 c= rng C1 by A59,XBOOLE_1:7;
    rng C1 c= N by FINSEQ_1:def 4;
    then rng Seq q7 c= N by A60,XBOOLE_1:1;
    then Seq q7 is FinSequence of N by FINSEQ_1:def 4;
    then reconsider C3 = Seq q7 as firing-sequence of N by FINSEQ_1:def 11;
A61: dom Seq q7 = Seg len Seq q7 by FINSEQ_1:def 3;
A62: dom (q6*Sgm dom q7) c= dom Sgm dom q7 by RELAT_1:44;
A63: dom (q2*Sgm dom q7) c= dom Sgm dom q7 by RELAT_1:44;
A64: dom (q6*Sgm dom q7) c= dom Seq q7 by A62,Lm4;
    dom (q2*Sgm dom q7) c= dom Seq q7 by A63,Lm4;
    then reconsider q8 = q6*Sgm dom q7, q9 = q2*Sgm dom q7 as FinSubsequence
    by A61,A64,FINSEQ_1:def 12;
A65: C3 = q7*Sgm dom q7 by FINSEQ_1:def 14
      .= q8 \/ q9 by RELAT_1:51;
A66: dom q8 = (Sgm dom q7)"dom q6 by RELAT_1:182;
A67: dom q9 = (Sgm dom q7)"dom q2 by RELAT_1:182;
    dom q2 /\ dom q6 = {} by A52,XBOOLE_0:def 7;
    then (Sgm dom q7)"(dom q6 /\ dom q2) = {} by RELAT_1:171;
    then (Sgm dom q7)"dom q6 /\ (Sgm dom q7)"dom q2 = {} by FUNCT_1:137;
    then
A68: (Sgm dom q7)"dom q6 misses (Sgm dom q7)"dom q2 by
    XBOOLE_0:def 7;
A69: dom q6 c= dom q6 \/ dom q2 by XBOOLE_1:7;
A70: dom q2 c= dom q6 \/ dom q2 by XBOOLE_1:7;
A71: dom q6 c= dom q7 by A69,RELAT_1:13;
A72: dom q2 c= dom q7 by A70,RELAT_1:13;
A73: dom q6 c= rng Sgm dom q7 by A71,FINSEQ_1:71;
A74: dom q2 c= rng Sgm dom q7 by A72,FINSEQ_1:71;
A75: dom q8 = (Sgm dom q7)"dom q6 by RELAT_1:182;
A76: dom q9 = (Sgm dom q7)"dom q2 by RELAT_1:182;
A77: rng ((Sgm dom q7)|dom q8) = rng ((dom q6)|(Sgm dom q7)) by A75,Th12
      .= dom q6 by A73,RELAT_1:120;
A78: dom q8 c= dom Sgm dom q7 by RELAT_1:44;
A79: dom q9 c= dom Sgm dom q7 by RELAT_1:44;
A80: (Sgm dom q7)*(Sgm dom q8) = Sgm rng ((Sgm dom q7)|dom q8)
    by A53,A55,A78,GRAPH_2:3,XBOOLE_1:7;
A81: Seq q8 = (q6*Sgm dom q7)*(Sgm dom q8) by FINSEQ_1:def 14
      .= q6*((Sgm dom q7)*(Sgm dom q8)) by RELAT_1:55
      .= Seq q6 by A77,A80,FINSEQ_1:def 14;
A82: rng ((Sgm dom q7)|dom q9) = rng ((dom q2)|(Sgm dom q7)) by A76,Th12
      .= dom q2 by A74,RELAT_1:120;
A83: (Sgm dom q7)*(Sgm dom q9) = Sgm rng ((Sgm dom q7)|dom q9)
    by A53,A55,A79,GRAPH_2:3,XBOOLE_1:7;
A84: Seq q9 = (q2*Sgm dom q7)*(Sgm dom q9) by FINSEQ_1:def 14
      .= q2*((Sgm dom q7)*(Sgm dom q9)) by RELAT_1:55
      .= Seq q2 by A82,A83,FINSEQ_1:def 14;
    Seq q8 in R2 by A12,A45,A46,A81,GRAPH_2:30;
then ex ss1,ss2 being FinSubsequence st C3 = ss1 \/ ss2 & ss1 misses ss2 & Seq
    ss1 in R2 & Seq ss2 in R3 by A6,A65,A66,A67,A68,A84,Th8;
    then Seq q7 in R2 concur R3;
    hence thesis by A1,A11,A44,A56,A58;
  end;
  let x;
  assume x in R1 concur (R2 concur R3);
  then consider C1 such that
A85: x = C1 and
A86: ex q1,q2 st C1 = q1 \/ q2 & q1 misses q2 & Seq q1 in R1 & Seq q2 in
  R2 concur R3;
  consider q1,q2 such that
A87: C1 = q1 \/ q2 and
A88: q1 misses q2 and
A89: Seq q1 in R1 and
A90: Seq q2 in R2 concur R3 by A86;
  consider C2 such that
A91: Seq q2 = C2 and
A92: ex q3,q4 st C2 = q3 \/ q4 & q3 misses q4 & Seq q3 in R2 & Seq q4 in
  R3
  by A90;
  consider q3,q4 such that
A93: C2 = q3 \/ q4 and
A94: q3 misses q4 and
A95: Seq q3 in R2 and
A96: Seq q4 in R3 by A92;
  consider n1 being Nat such that
A97: dom q2 c= Seg n1 by FINSEQ_1:def 12;
A98: q3 c= Seq q2 by A91,A93,XBOOLE_1:7;
A99: q4 c= Seq q2 by A91,A93,XBOOLE_1:7;
  Sgm dom q2 is one-to-one by A97,FINSEQ_3:99;
  then
A100: (Sgm dom q2).:dom q3 misses (Sgm dom q2).:dom q4 by A94,A98,A99,
  Th10,FUNCT_1:125;
A101: rng ((Sgm dom q2)|dom q3) = (Sgm dom q2).:dom q3 by RELAT_1:148;
A102: rng ((Sgm dom q2)|dom q4) = (Sgm dom q2).:dom q4 by RELAT_1:148;
  then
A103: q2|rng((Sgm dom q2)|dom q3) misses q2|rng((Sgm dom q2)|dom q4)
  by A100,A101,Th9;
A104: dom Sgm dom q2 = dom Seq q2 by Lm4
    .= dom q3 \/ dom q4 by A91,A93,RELAT_1:13;
A105: q2|rng((Sgm dom q2)|dom q3) \/ q2|rng((Sgm dom q2)|dom q4)
  = q2|(rng((Sgm dom q2)|dom q3) \/ rng ((Sgm dom q2)|dom q4)) by RELAT_1:107
    .= q2|((Sgm dom q2).:(dom q3 \/ dom q4)) by A101,A102,RELAT_1:153
    .= q2|rng((Sgm dom q2)|(dom Sgm dom q2)) by A104,RELAT_1:148
    .= q2|rng Sgm dom q2 by RELAT_1:98
    .= q2|dom q2 by FINSEQ_1:71
    .= q2 by RELAT_1:98;
A116: q2 c= C1 by A87,XBOOLE_1:7;
A117: q2|rng((Sgm dom q2)|dom q3) c= q2 by A105,XBOOLE_1:7;
A118: q2|rng((Sgm dom q2)|dom q4) c= q2 by A105,XBOOLE_1:7;
  rng C1 = rng q1 \/ rng q2 by A87,RELAT_1:26;
  then
A119: rng q2 c= rng C1 by XBOOLE_1:7;
A120: rng q2 = rng Seq q2 by Lm5;
A121: rng Seq q2 c= rng C1 by A119,Lm5;
A122: rng Seq q2 = rng q3 \/ rng q4 by A91,A93,RELAT_1:26;
  then
A123: rng q3 c= rng Seq q2 by XBOOLE_1:7;
  rng q3 = rng Seq q3 by Lm5;
  then
A124: rng Seq q3 c= rng C1 by A121,A123,XBOOLE_1:1;
A125: rng q4 c= rng Seq q2 by A122,XBOOLE_1:7;
  rng q4 = rng Seq q4 by Lm5;
  then
A126: rng Seq q4 c= rng C1 by A121,A125,XBOOLE_1:1;
  reconsider q5 = q2|rng((Sgm dom q2)|dom q3),
  q6 = q2|rng((Sgm dom q2)|dom q4) as FinSubsequence;
  reconsider fs = C1 as FinSequence of rng C1 by FINSEQ_1:def 4;
  reconsider fs1 = Seq q2 as FinSequence of rng C1 by A119,A120,FINSEQ_1:def 4;
  reconsider fs2 = Seq q3 as FinSequence of rng C1 by A124,FINSEQ_1:def 4;
  reconsider fs3 = Seq q4 as FinSequence of rng C1 by A126,FINSEQ_1:def 4;
  reconsider fss = q2, fss1 = q5, fss2 = q6 as Subset of fs by A116,A117,A118,
  XBOOLE_1:1;
  reconsider fss3 = q3, fss4 = q4 as Subset of fs1 by A91,A93,XBOOLE_1:7;
A127: Seq fss3 = fs2;
A128: fss1 = fss|rng((Sgm dom fss)|dom fss3 );
A129: Seq fss4 = fs3;
  fss2 = fss|rng((Sgm dom fss)|dom fss4 );
  then
A130: Seq q4 = Seq q6 by A129,GRAPH_2:30;
  q1 /\ q2 = {} by A88,XBOOLE_0:def 7;
  then
A131: q1 /\ q5 \/ q1 /\ q6 = {} by A105,XBOOLE_1:23;
  then
A132: q1 /\ q5 = {};
A133: q1 /\ q6 = {} by A131;
A134: q1 misses q5 by A132,XBOOLE_0:def 7;
A135: q1 c= C1 by A87,XBOOLE_1:7;
A136: q2 c= C1 by A87,XBOOLE_1:7;
  q5 c= q2 by A105,XBOOLE_1:7;
  then q5 c= C1 by A136,XBOOLE_1:1;
  then
A137: dom q1 misses dom q5 by A134,A135,Th10;
  then reconsider q7 = q1 \/ q5 as Function by GRFUNC_1:31;
A138: dom C1 = dom q1 \/ dom q2 by A87,RELAT_1:13
    .= dom q1 \/ (dom q5 \/ dom q6) by A105,RELAT_1:13
    .= (dom q1 \/ dom q5) \/ dom q6 by XBOOLE_1:4
    .= dom q7 \/ dom q6 by RELAT_1:13;
  then
A139: dom q7 c= dom C1 by XBOOLE_1:7;
A140: dom C1 = Seg len C1 by FINSEQ_1:def 3;
  then reconsider q7 as FinSubsequence by A139,FINSEQ_1:def 12;
A141: C1 = q7 \/ q6 by A87,A105,XBOOLE_1:4;
A142: q7 /\ q6 = q1 /\ q6 \/ q5 /\ q6 by XBOOLE_1:23;
  q5 /\ q6 = {} by A103,XBOOLE_0:def 7;
  then
A143: q7 misses q6 by A133,A142,XBOOLE_0:def 7;
A144: rng Seq q7 = rng q7 by Lm5;
  rng C1 = rng q7 \/ rng q6 by A141,RELAT_1:26;
  then
A145: rng Seq q7 c= rng C1 by A144,XBOOLE_1:7;
  rng C1 c= N by FINSEQ_1:def 4;
  then rng Seq q7 c= N by A145,XBOOLE_1:1;
  then Seq q7 is FinSequence of N by FINSEQ_1:def 4;
  then reconsider C3 = Seq q7 as firing-sequence of N by FINSEQ_1:def 11;
A146: dom Seq q7 = Seg len Seq q7 by FINSEQ_1:def 3;
A147: dom (q1*Sgm dom q7) c= dom Sgm dom q7 by RELAT_1:44;
A148: dom (q5*Sgm dom q7) c= dom Sgm dom q7 by RELAT_1:44;
A149: dom (q1*Sgm dom q7) c= dom Seq q7 by A147,Lm4;
  dom (q5*Sgm dom q7) c= dom Seq q7 by A148,Lm4;
  then reconsider q8 = q1*Sgm dom q7, q9 = q5*Sgm dom q7 as FinSubsequence
  by A146,A149,FINSEQ_1:def 12;
A150: C3 = q7*Sgm dom q7 by FINSEQ_1:def 14
    .= q8 \/ q9 by RELAT_1:51;
A151: dom q8 = (Sgm dom q7)"dom q1 by RELAT_1:182;
A152: dom q9 = (Sgm dom q7)"dom q5 by RELAT_1:182;
  dom q1 /\ dom q5 = {} by A137,XBOOLE_0:def 7;
  then (Sgm dom q7)"(dom q1 /\ dom q5) = {} by RELAT_1:171;
  then (Sgm dom q7)"dom q1 /\ (Sgm dom q7)"dom q5 = {} by FUNCT_1:137;
  then
A153: (Sgm dom q7)"dom q1 misses (Sgm dom q7)"dom q5 by XBOOLE_0:def 7;
A154: dom q1 c= dom q1 \/ dom q5 by XBOOLE_1:7;
A155: dom q5 c= dom q1 \/ dom q5 by XBOOLE_1:7;
A156: dom q1 c= dom q7 by A154,RELAT_1:13;
A157: dom q5 c= dom q7 by A155,RELAT_1:13;
A158: dom q1 c= rng Sgm dom q7 by A156,FINSEQ_1:71;
A159: dom q5 c= rng Sgm dom q7 by A157,FINSEQ_1:71;
A160: dom q8 = (Sgm dom q7)"dom q1 by RELAT_1:182;
A161: dom q9 = (Sgm dom q7)"dom q5 by RELAT_1:182;
A162: rng ((Sgm dom q7)|dom q8) = rng ((dom q1)|(Sgm dom q7)) by A160,Th12
    .= dom q1 by A158,RELAT_1:120;
A163: dom q8 c= dom Sgm dom q7 by RELAT_1:44;
A164: dom q9 c= dom Sgm dom q7 by RELAT_1:44;
A165: (Sgm dom q7)*(Sgm dom q8) = Sgm rng ((Sgm dom q7)|dom q8)
  by A138,A140,A163,GRAPH_2:3,XBOOLE_1:7;
A166: Seq q8 = (q1*Sgm dom q7)*(Sgm dom q8) by FINSEQ_1:def 14
    .= q1*((Sgm dom q7)*(Sgm dom q8)) by RELAT_1:55
    .= Seq q1 by A162,A165,FINSEQ_1:def 14;
A167: rng ((Sgm dom q7)|dom q9) = rng ((dom q5)|(Sgm dom q7)) by A161,Th12
    .= dom q5 by A159,RELAT_1:120;
A168: (Sgm dom q7)*(Sgm dom q9) = Sgm rng ((Sgm dom q7)|dom q9)
  by A138,A140,A164,GRAPH_2:3,XBOOLE_1:7;
  Seq q9 = (q5*Sgm dom q7)*(Sgm dom q9) by FINSEQ_1:def 14
    .= q5*((Sgm dom q7)*(Sgm dom q9)) by RELAT_1:55
    .= Seq q5 by A167,A168,FINSEQ_1:def 14;
  then Seq q9 in R2 by A95,A127,A128,GRAPH_2:30;
then ex ss1,ss2 being FinSubsequence st C3 = ss1 \/ ss2 & ss1 misses ss2 & Seq
  ss1 in R1 & Seq ss2 in R2 by A89,A150,A151,A152,A153,A166,Th8;
  then Seq q7 in R1 concur R2;
  hence thesis by A85,A96,A130,A141,A143;
end;

theorem
  R1 before R2 c= R1 concur R2
proof
  let x;
  assume
A1: x in R1 before R2;
  then reconsider C = x as firing-sequence of N;
  consider C1,C2 such that
A2: x = C1^C2 and
A3: C1 in R1 and
A4: C2 in R2 by A1;
  set q1 = C1, q2 = (len C1) Shift C2;
  reconsider q1 as FinSequence;
A5: C = q1 \/ q2 by A2,Th64;
A6: q1 misses q2 by Th65;
A7: Seq q1 in R1 by A3,FINSEQ_3:125;
  Seq q2 in R2 by A4,Th61;
  hence thesis by A5,A6,A7;
end;

theorem
  R1 c= P1 & R2 c= P2 implies R1 before R2 c= P1 before P2
proof
  assume that
A1: R1 c= P1 and
A2: R2 c= P2;
  let x;
  assume x in R1 before R2;
  then ex C1,C2 st ( x = C1^C2)&( C1 in R1)&( C2 in R2);
  hence thesis by A1,A2;
end;

theorem
  R1 c= P1 & R2 c= P2 implies R1 concur R2 c= P1 concur P2
proof
  assume that
A1: R1 c= P1 and
A2: R2 c= P2;
  let x;
  assume x in R1 concur R2;
  then ex C st
  x = C & ex q1,q2 being FinSubsequence st C = q1 \/ q2 & q1 misses q2 &
  Seq q1 in R1 & Seq q2 in R2;
  hence thesis by A1,A2;
end;

Lm8: for p1,p2 being FinSequence, q1,q2 being FinSubsequence st
q1 c= p1 & q2 c= p2 holds dom (q1 \/ len p1 Shift q2) c= dom (p1^p2)
proof
  let p1,p2 be FinSequence, q1,q2 be FinSubsequence;
  assume that
A1: q1 c= p1 and
A2: q2 c= p2;
A3: dom q1 c= dom p1 by A1,GRFUNC_1:8;
A4: dom q2 c= dom p2 by A2,GRFUNC_1:8;
  let x;
  assume x in dom (q1 \/ len p1 Shift q2);
  then
A5: x in (dom q1 \/ dom (len p1 Shift q2)) by RELAT_1:13;
A6: dom (p1^p2) = Seg (len p1 + len p2) by FINSEQ_1:def 7;
A7: now
    assume
A8: x in dom q1;
A9: dom p1 = Seg len p1 by FINSEQ_1:def 3;
    len p1 <= len p1 + len p2 by INT_1:19;
    then Seg len p1 c= Seg (len p1 + len p2) by FINSEQ_1:7;
    hence thesis by A3,A6,A8,A9,TARSKI:def 3;
  end;
  now
    assume
A10: x in dom (len p1 Shift q2);
A11: dom (len p1 Shift q2) c= dom (len p1 Shift p2)
    proof
      let x;
      assume
A12:  x in dom (len p1 Shift q2);
A13:  dom (len p1 Shift p2) = {len p1 + k: k in dom p2} by Def15;
      dom (len p1 Shift q2) = {len p1 + k: k in dom q2} by Def15;
      then ex k st ( x = len p1 + k)&( k in dom q2) by A12;
      hence thesis by A4,A13;
    end;
    dom (p1^p2) = dom (p1 \/ len p1 Shift p2) by Th64
      .= dom p1 \/ dom (len p1 Shift p2) by RELAT_1:13;
    then dom (len p1 Shift p2) c= dom (p1^p2) by XBOOLE_1:7;
    then dom (len p1 Shift q2) c= dom (p1^p2) by A11,XBOOLE_1:1;
    hence thesis by A10;
  end;
  hence thesis by A5,A7,XBOOLE_0:def 3;
end;

Lm9: for p1 being FinSequence, q1,q2 being FinSubsequence st
q1 c= p1 holds q1 misses ((len p1) Shift q2) by Th65,XBOOLE_1:63;

Lm10: for p,q being FinSubsequence st q c= p holds
dom (i Shift q) c= dom (i Shift p)
proof
  let p,q be FinSubsequence;
  assume
A1: q c= p;
A2: dom (i Shift q) = {i+k: k in dom q} by Def15;
A3: dom (i Shift p) = {i+k: k in dom p} by Def15;
A4: dom q c= dom p by A1,GRFUNC_1:8;
  let x;
  assume x in dom (i Shift q);
  then ex k1 st ( x = i+k1)&( k1 in dom q) by A2;
  hence thesis by A3,A4;
end;

theorem Th70:
  for p,q being FinSubsequence st q c= p holds i Shift q c= i Shift p
proof
  let p,q be FinSubsequence;
  assume
A1: q c= p;
A2: dom (i Shift q) = {i+k: k in dom q} by Def15;
A3: dom (i Shift p) = {i+k: k in dom p} by Def15;
  let x,y;
  assume
A4: [x,y] in i Shift q;
  then
A5: x in dom (i Shift q) by FUNCT_1:8;
A6: y = (i Shift q).x by A4,FUNCT_1:8;
  consider k such that
A7: x = i+k and
A8: k in dom q by A2,A5;
A9: dom q c= dom p by A1,GRFUNC_1:8;
  then
A10: x in dom (i Shift p) by A3,A7,A8;
  y = q.k by A6,A7,A8,Def15
    .= p.k by A1,A8,GRFUNC_1:8
    .= (i Shift p).x by A7,A8,A9,Def15;
  hence thesis by A10,FUNCT_1:8;
end;

theorem Th71:
  for p1,p2 being FinSequence holds len p1 Shift p2 c= p1^p2
proof
  let p1,p2 be FinSequence;
A1: dom (len p1 Shift p2) = {len p1 + k: k in dom p2} by Def15;
A2: dom (len p1 Shift p2) = {k: len p1 + 1 <= k & k <= len p1 + len p2}
    by Th54;
A3: dom (p1^p2) = Seg (len p1 + len p2) by FINSEQ_1:def 7
      .= {k: 1 <= k & k <= len p1 + len p2} by FINSEQ_1:def 1;
    let x,y;
    assume
A4: [x,y] in len p1 Shift p2;
    then
A5: x in dom (len p1 Shift p2) by FUNCT_1:8;
A6: y = (len p1 Shift p2).x by A4,FUNCT_1:8;
    consider k such that
A7: x = k and
A8: len p1 + 1 <= k and
A9: k <= len p1 + len p2 by A2,A5;
    1 <= len p1 + 1 by INT_1:19;
    then 1 <= k by A8,XXREAL_0:2;
    then
A10: x in dom (p1^p2) by A3,A7,A9;
    consider j such that
A11: x = len p1 + j and
A12: j in dom p2 by A1,A5;
    y = p2.j by A6,A11,A12,Def15
      .= (p1^p2).x by A11,A12,FINSEQ_1:def 7;
    hence thesis by A10,FUNCT_1:8;
end;

theorem Th72:
  dom q1 misses dom q2 implies dom (i Shift q1) misses dom (i Shift q2)
proof
  assume
A1: dom q1 misses dom q2;
A2: dom (i Shift q1) = {i+k: k in dom q1} by Def15;
A3: dom (i Shift q2) = {i+k: k in dom q2} by Def15;
  now
    given x such that
A4: x in dom (i Shift q1) /\ dom (i Shift q2);
A5: x in dom (i Shift q1) by A4,XBOOLE_0:def 4;
A6: x in dom (i Shift q2) by A4,XBOOLE_0:def 4;
A7: ex k1 st ( x = i+k1)&( k1 in dom q1) by A2,A5;
    consider k2 such that
A8: x = i+k2 and
A9: k2 in dom q2 by A3,A6;
    k2 in dom q1 /\ dom q2 by A7,A8,A9,XBOOLE_0:def 4;
    hence contradiction by A1,XBOOLE_0:def 7;
  end;
  hence dom (i Shift q1) /\ dom (i Shift q2) = {} by XBOOLE_0:def 1;
end;

theorem Th73:
  for q,q1,q2 being FinSubsequence st q = q1 \/ q2 & q1 misses q2 holds
  (i Shift q1) \/ (i Shift q2) = i Shift q
proof
  let q,q1,q2 be FinSubsequence such that
A1: q = q1 \/ q2 and
A2: q1 misses q2;
A3: dom (i Shift q) = {i+k: k in dom q} by Def15;
A4: dom (i Shift q1) = {i+k: k in dom q1} by Def15;
A5: dom (i Shift q2) = {i+k: k in dom q2} by Def15;
A6: q1 c= q by A1,XBOOLE_1:7;
A7: q2 c= q by A1,XBOOLE_1:7;
A8: (i Shift q1) c= (i Shift q) by A1,Th70,XBOOLE_1:7;
A9: (i Shift q2) c= (i Shift q) by A1,Th70,XBOOLE_1:7;
  dom q1 misses dom q2 by A2,A6,A7,Th10;
  then dom (i Shift q1) misses dom (i Shift q2) by Th72;
  then reconsider q3 = (i Shift q1) \/ (i Shift q2) as Function by GRFUNC_1:31;
  let x,y;
  thus [x,y] in (i Shift q1) \/ (i Shift q2) implies [x,y] in i Shift q
  proof
    assume
A10: [x,y] in (i Shift q1) \/ (i Shift q2);
    then x in dom q3 by FUNCT_1:8;
    then
A11: x in dom (i Shift q1) \/ dom (i Shift q2) by RELAT_1:13;
A12: now
      assume
A13:  x in dom (i Shift q1);
A14:  dom (i Shift q1) c= dom (i Shift q) by A8,GRFUNC_1:8;
      (i Shift q1).x = (i Shift q).x by A8,A13,GRFUNC_1:8;
      then [x,(i Shift q).x] in (i Shift q1) by A13,FUNCT_1:def 4;
      then [x,(i Shift q).x] in q3 by XBOOLE_0:def 3;
      hence x in dom (i Shift q) & y = (i Shift q).x by A10,A13,A14,
      FUNCT_1:def 1;
    end;
    now
      assume
A15:  x in dom (i Shift q2);
A16:  dom (i Shift q2) c= dom (i Shift q) by A9,GRFUNC_1:8;
      (i Shift q2).x = (i Shift q).x by A9,A15,GRFUNC_1:8;
      then [x,(i Shift q).x] in (i Shift q2) by A15,FUNCT_1:def 4;
      then [x,(i Shift q).x] in q3 by XBOOLE_0:def 3;
      hence x in dom (i Shift q) & y = (i Shift q).x by A10,A15,A16,
      FUNCT_1:def 1;
    end;
    hence thesis by A11,A12,FUNCT_1:8,XBOOLE_0:def 3;
  end;
  assume
A17: [x,y] in (i Shift q);
  then
A18: x in dom (i Shift q) by FUNCT_1:8;
A19: y = (i Shift q).x by A17,FUNCT_1:8;
  consider k such that
A20: x = i+k and
A21: k in dom q by A3,A18;
  dom q = dom q1 \/ dom q2 by A1,RELAT_1:13;
  then
A22: k in dom q1 or k in dom q2 by A21,XBOOLE_0:def 3;
  then x in dom (i Shift q1) or x in dom (i Shift q2) by A4,A5,A20;
  then x in dom (i Shift q1) \/ dom (i Shift q2) by XBOOLE_0:def 3;
  then
A23: x in dom q3 by RELAT_1:13;
A24: now
    assume
A25: x in dom (i Shift q1);
    then
A26: ex k1 st ( x = i+k1)&( k1 in dom q1) by A4;
    thus y = q.k by A19,A20,A21,Def15
      .= q1.k by A1,A20,A26,GRFUNC_1:35
      .= (i Shift q1).x by A20,A26,Def15
      .= q3.x by A25,GRFUNC_1:35;
  end;
  now
    assume
A27: x in dom (i Shift q2);
    then
A28: ex k2 st ( x = i+k2)&( k2 in dom q2) by A5;
    thus y = q.k by A19,A20,A21,Def15
      .= q2.k by A1,A20,A28,GRFUNC_1:35
      .= (i Shift q2).x by A20,A28,Def15
      .= q3.x by A27,GRFUNC_1:35;
  end;
  hence thesis by A4,A5,A20,A22,A23,A24,FUNCT_1:8;
end;

theorem Th74:
  for q being FinSubsequence holds dom Seq q = dom Seq (i Shift q)
proof
  let q be FinSubsequence;
A1: card q = card (i Shift q) by Th57;
A2: len Seq q = card q by Th7;
A3: len Seq (i Shift q) = card (i Shift q) by Th7;
  thus dom Seq q = Seg len Seq q by FINSEQ_1:def 3
    .= dom Seq (i Shift q) by A1,A2,A3,FINSEQ_1:def 3;
end;

theorem Th75:
  for q being FinSubsequence st k in dom Seq q
  ex j st j = (Sgm dom q).k & (Sgm dom (i Shift q)).k = i + j
proof
  let q be FinSubsequence such that
A1: k in dom Seq q;
  consider ss being FinSubsequence such that
A2: dom ss = dom q and
A3: rng ss = dom (i Shift q) and
A4: for l st l in dom q holds ss.l = i+l
  and ss is one-to-one by Th56;
A5: rng Seq ss = dom (i Shift q) by A3,Lm5;
A6: rng Sgm dom q = dom q by FINSEQ_1:71;
A7: dom Seq q = dom (q*Sgm dom q) by FINSEQ_1:def 14
    .= dom Sgm dom q by A6,RELAT_1:46;
A8: for l1 st l1 in dom Seq q
  ex j1 st j1 = (Sgm dom q).l1 & (Seq ss).l1 = i+j1
  proof
    let l1 such that
A9: l1 in dom Seq q;
A10: (Sgm dom q).l1 in rng Sgm dom q by A7,A9,FUNCT_1:def 5;
    then
A11: (Sgm dom q).l1 in dom q by FINSEQ_1:71;
    rng Sgm dom q c= NAT by FINSEQ_1:def 4;
    then reconsider j1 = (Sgm dom q).l1 as Element of NAT by A10;
    (Seq ss).l1 = (ss*Sgm dom ss).l1 by FINSEQ_1:def 14
      .= ss.j1 by A2,A7,A9,FUNCT_1:23
      .= i+j1 by A4,A11;
    hence thesis;
  end;
A12: rng ss = rng Sgm dom (i Shift q) by A3,FINSEQ_1:71;
  rng Sgm dom (i Shift q) c= NAT by FINSEQ_1:def 4;
  then rng Seq ss c= NAT by A12,Lm5;
  then reconsider fs = Seq ss as FinSequence of NAT by FINSEQ_1:def 4;
A13: ex l2 be Nat st ( dom (i Shift q) c= Seg l2) by FINSEQ_1:def 12;
A14: dom Seq ss = dom (ss*Sgm dom ss) by FINSEQ_1:def 14
    .= dom Sgm dom q by A2,A6,RELAT_1:46;
  for n,m,k1,k2 being natural number st 1 <= n & n < m & m <= len fs &
  k1 = fs.n & k2 = fs.m holds k1 < k2
  proof
    let n,m,k1,k2 be natural number;
    assume that
A15: 1 <= n and
A16: n < m and
A17: m <= len fs and
A18: k1 = fs.n and
A19: k2 = fs.m;
    reconsider n,m as Element of NAT by ORDINAL1:def 13;
A20: dom fs = Seg len fs by FINSEQ_1:def 3
      .= {l3 where l3 is Element of NAT: 1 <= l3 & l3 <= len fs}
    by FINSEQ_1:def 1;
    n+1 <= m by A16,INT_1:20;
    then n+1 <= len fs by A17,XXREAL_0:2;
    then
A21: n <= (len fs) - 1 by XREAL_1:21;
    (len fs) + (0 qua Nat) <= (len fs) + 1 by XREAL_1:9;
    then (len fs) - 1 <= len fs by XREAL_1:22;
    then n <= len fs by A21,XXREAL_0:2;
    then
A22: n in dom Seq q by A7,A14,A15,A20;
    1 <= m by A15,A16,XXREAL_0:2;
    then
A23: m in dom Seq q by A7,A14,A17,A20;
    consider j2 being Element of NAT such that
A24: j2 = (Sgm dom q).n and
A25: fs.n = i+j2 by A8,A22;
    consider j3 being Element of NAT such that
A26: j3 = (Sgm dom q).m and
A27: fs.m = i+j3 by A8,A23;
A28: ex l3 being Nat st ( dom q c= Seg l3) by FINSEQ_1:def 12;
    dom Seq ss = Seg len fs by FINSEQ_1:def 3;
    then len fs = len Sgm dom q by A14,FINSEQ_1:def 3;
    then j2 < j3 by A15,A16,A17,A24,A26,A28,FINSEQ_1:def 13;
    hence thesis by A18,A19,A25,A27,XREAL_1:10;
  end;
  then fs = Sgm dom (i Shift q) by A5,A13,FINSEQ_1:def 13;
  hence thesis by A1,A8;
end;

theorem Th76:
  for q being FinSubsequence st
  k in dom Seq q holds (Seq (i Shift q)).k = (Seq q).k
proof
  let q be FinSubsequence;
  assume
A1: k in dom Seq q;
  then consider j such that
A2: j = (Sgm dom q).k and
A3: (Sgm dom (i Shift q)).k = i+j by Th75;
A4: rng Sgm dom q = dom q by FINSEQ_1:71;
A5: dom Seq q = dom Seq (i Shift q) by Th74
    .= dom ((i Shift q)*(Sgm dom (i Shift q))) by FINSEQ_1:def 14;
A6: dom Seq q = dom (q*Sgm dom q) by FINSEQ_1:def 14
    .= dom Sgm dom q by A4,RELAT_1:46;
  then j in rng Sgm dom q by A1,A2,FUNCT_1:def 5;
  then
A7: j in dom q by FINSEQ_1:71;
  (Seq (i Shift q)).k = ((i Shift q)*(Sgm dom (i Shift q))).k
  by FINSEQ_1:def 14
    .= (i Shift q).((Sgm dom (i Shift q)).k)
  by A1,A5,FUNCT_1:22
    .= q.j by A3,A7,Def15
    .= (q*Sgm dom q).k by A1,A2,A6,FUNCT_1:23
    .= (Seq q).k by FINSEQ_1:def 14;
  hence thesis;
end;

theorem
  for q being FinSubsequence holds Seq q = Seq (i Shift q)
proof
  let q be FinSubsequence;
A1: dom Seq q = dom Seq (i Shift q) by Th74;
  x in dom Seq q implies (Seq (i Shift q)).x = (Seq q).x by Th76;
  hence thesis by A1,FUNCT_1:9;
end;

theorem Th78:
  for q being FinSubsequence st
  dom q c= Seg k holds dom (i Shift q) c= Seg (i+k)
proof
  let q be FinSubsequence;
  assume
A1: dom q c= Seg k;
A2: dom (i Shift q) = {i+j: j in dom q} by Def15;
A3: Seg k = {j: 1 <= j & j <= k} by FINSEQ_1:def 1;
A4: Seg (i+k) = {j: 1 <= j & j <= i+k} by FINSEQ_1:def 1;
  let x;
  assume x in dom (i Shift q);
  then consider j1 such that
A5: x = i+j1 and
A6: j1 in dom q by A2;
  j1 in Seg k by A1,A6;
  then
A7: ex j2 st ( j1 = j2)&( 1 <= j2)&( j2 <= k) by A3;
  0 <= i by NAT_1:2;
  then
A8: 0 qua Nat+1 <= i+j1 by A7,XREAL_1:9;
  i+j1 <= i+k by A7,XREAL_1:9;
  hence thesis by A4,A5,A8;
end;

theorem Th79:
  for p being FinSequence, q1,q2 being FinSubsequence st q1 c= p
  ex ss being FinSubsequence st ss = q1 \/ (len p Shift q2)
proof
  let p be FinSequence, q1,q2 be FinSubsequence;
  assume q1 c= p;
  then
A1: dom q1 c= dom p by GRFUNC_1:8;
  dom p misses dom (len p Shift q2) by Th63;
  then reconsider ss = q1 \/ (len p Shift q2) as Function by A1,GRFUNC_1:31
  ,XBOOLE_1:63;
A2: dom p = Seg len p by FINSEQ_1:def 3;
  consider k be Nat such that
A3: dom q2 c= Seg k by FINSEQ_1:def 12;
  k in NAT by ORDINAL1:def 13;
  then
A4: dom (len p Shift q2) c= Seg (len p + k) by A3,Th78;
  0 <= k by NAT_1:2;
  then len p + (0 qua Nat) <= len p + k by XREAL_1:9;
  then Seg len p c= Seg (len p + k) by FINSEQ_1:7;
  then dom q1 c= Seg (len p + k) by A1,A2,XBOOLE_1:1;
  then dom q1 \/ dom (len p Shift q2) c= Seg (len p + k) by A4,XBOOLE_1:8;
  then dom (q1 \/ (len p Shift q2)) c= Seg (len p + k) by RELAT_1:13;
  then reconsider ss as FinSubsequence by FINSEQ_1:def 12;
  take ss;
  thus thesis;
end;

Lm11: for p1,p2 being FinSequence, q1,q2 being FinSubsequence st
q1 c= p1 & q2 c= p2 holds Sgm (dom q1 \/ dom (len p1 Shift q2))
= (Sgm dom q1)^(Sgm dom (len p1 Shift q2))
proof
  let p1,p2 be FinSequence, q1,q2 be FinSubsequence;
  assume that
A1: q1 c= p1 and
A2: q2 c= p2;
A3: ex k1 be Nat st ( dom q1 c= Seg k1) by FINSEQ_1:def 12;
A4: ex k2 be Nat st ( dom (len p1 Shift q2) c= Seg k2) by FINSEQ_1:def 12;
  for m,n being Element of NAT
  st m in dom q1 & n in dom (len p1 Shift q2) holds m < n
  proof
    let m,n be Element of NAT such that
A5: m in dom q1 and
A6: n in dom (len p1 Shift q2);
A7: dom p1 = Seg len p1 by FINSEQ_1:def 3
      .= {k: 1 <= k & k <= len p1} by FINSEQ_1:def 1;
A8: dom (len p1 Shift p2) = {k: len p1 + 1 <= k & k <= len p1 + len p2}
    by Th54;
A9: dom q1 c= dom p1 by A1,GRFUNC_1:8;
A10: dom (len p1 Shift q2) c= dom (len p1 Shift p2) by A2,Lm10;
A11: m in dom p1 by A5,A9;
A12: n in dom (len p1 Shift p2) by A6,A10;
    consider k3 being Element of NAT such that
A13: k3 = m
    and 1 <= k3 and
A14: k3 <= len p1 by A7,A11;
    consider k4 being Element of NAT such that
A15: k4 = n and
A16: len p1 + 1 <= k4
    and k4 <= len p1 + len p2 by A8,A12;
    len p1 < len p1 + 1 by XREAL_1:31;
    then k3 <= len p1 + 1 by A14,XXREAL_0:2;
    then
A17: k3 <= k4 by A16,XXREAL_0:2;
    dom p1 misses dom (len p1 Shift p2) by Th63;
    then k3 <> k4 by A5,A6,A9,A10,A13,A15,Lm3;
    hence thesis by A13,A15,A17,XXREAL_0:1;
  end;
  hence thesis by A3,A4,FINSEQ_3:48;
end;

theorem Th80:
for p1,p2 being FinSequence, q1,q2 being FinSubsequence st q1 c= p1 & q2 c= p2
  ex ss being FinSubsequence st ss = q1 \/ (len p1 Shift q2) &
  dom Seq ss = Seg (len Seq q1 + len Seq q2)
proof
  let p1,p2 be FinSequence, q1,q2 be FinSubsequence;
  assume that
A1: q1 c= p1 and
A2: q2 c= p2;
    consider ss being FinSubsequence such that
A6: ss = q1 \/ (len p1 Shift q2) by A1,Th79;
A7: ex k1 be Nat st ( dom q1 c= Seg k1) by FINSEQ_1:def 12;
A8: ex k2 be Nat st ( dom (len p1 Shift q2) c= Seg k2) by FINSEQ_1:def 12;
A9: rng Sgm dom ss = dom ss by FINSEQ_1:71;
    dom Seq ss = dom (ss*Sgm dom ss) by FINSEQ_1:def 14;
    then
A10: dom Seq ss = dom Sgm dom ss by A9,RELAT_1:46
      .= dom Sgm (dom q1 \/ dom (len p1 Shift q2)) by A6,RELAT_1:13
      .= dom ((Sgm dom q1)^(Sgm dom (len p1 Shift q2))) by A1,A2,Lm11
      .= Seg (len Sgm dom q1 + len Sgm dom (len p1 Shift q2))
    by FINSEQ_1:def 7;
A11: len Sgm dom (len p1 Shift q2) = card dom (len p1 Shift q2) by A8,
    FINSEQ_3:44;
A12: len Sgm dom q1 = card dom q1 by A7,FINSEQ_3:44;
    len Sgm dom (len p1 Shift q2) = card (len p1 Shift q2) by A11,CARD_1:104;
    then
A13: len Sgm dom (len p1 Shift q2) = card q2 by Th57;
A14: len Sgm dom q1 = card q1 by A12,CARD_1:104;
A15: len Sgm dom (len p1 Shift q2) = len Seq q2 by A13,Th7;
    len Sgm dom q1 = len Seq q1 by A14,Th7;
    hence thesis by A6,A10,A15;
end;

theorem Th81:
for p1,p2 being FinSequence, q1,q2 being FinSubsequence st q1 c= p1 & q2 c= p2
  ex ss being FinSubsequence st ss = q1 \/ (len p1 Shift q2) &
  dom Seq ss = Seg (len Seq q1 + len Seq q2) &
  Seq ss = Seq q1 \/ (len Seq q1 Shift Seq q2)
proof
  let p1,p2 be FinSequence, q1,q2 be FinSubsequence;
  assume that
A1: q1 c= p1 and
A2: q2 c= p2;
  consider ss being FinSubsequence such that
A3: ss = q1 \/ (len p1 Shift q2) and
A4: dom Seq ss = Seg (len Seq q1 + len Seq q2) by A1,A2,Th80;
A12: Seg (len Seq q1 + len Seq q2)
    = {k: 1 <= k & k <= len Seq q1 + len Seq q2} by FINSEQ_1:def 1;
A13: dom Seq q1 = Seg len Seq q1 by FINSEQ_1:def 3
      .= {k: 1 <= k & k <= len Seq q1} by FINSEQ_1:def 1;
A14: dom (len Seq q1 Shift Seq q2)
    = {k: len Seq q1 + 1 <= k & k <= len Seq q1 + len Seq q2} by Th54;
A15: Seg (len Seq q1 + len Seq q2)
    = dom Seq q1 \/ dom (len Seq q1 Shift Seq q2)
    proof
      thus Seg (len Seq q1 + len Seq q2)
      c= dom Seq q1 \/ dom (len Seq q1 Shift Seq q2)
      proof
        let x;
        assume x in Seg (len Seq q1 + len Seq q2);
        then consider k1 such that
A16:    x = k1 and
A17:    1 <= k1 and
A18:    k1 <= len Seq q1 + len Seq q2 by A12;
A19:    1 <= k1 & k1 <= len Seq q1 implies k1 in dom Seq q1 by A13;
        len Seq q1 + 1 <= k1 & k1 <= len Seq q1 + len Seq q2 implies
        k1 in dom (len Seq q1 Shift Seq q2) by A14;
        hence thesis by A16,A17,A18,A19,INT_1:20,XBOOLE_0:def 3;
      end;
      let x;
      assume
A20:  x in dom Seq q1 \/ dom (len Seq q1 Shift Seq q2);
A21:  0 <= len Seq q1 by NAT_1:2;
A22:  0 <= len Seq q2 by NAT_1:2;
A23:  now
        assume x in dom Seq q1;
        then consider k1 such that
A24:    x = k1 and
A25:    1 <= k1 and
A26:    k1 <= len Seq q1 by A13;
        len Seq q1 + (0 qua Nat) <= len Seq q1 + len Seq q2 by A22,
        XREAL_1:9;
        then k1 <= len Seq q1 + len Seq q2 by A26,XXREAL_0:2;
        hence thesis by A12,A24,A25;
      end;
      now
        assume x in dom (len Seq q1 Shift Seq q2);
        then consider k2 such that
A27:    x = k2 and
A28:    len Seq q1 + 1 <= k2 and
A29:    k2 <= len Seq q1 + len Seq q2 by A14;
        0 qua Nat+1 <= len Seq q1 + 1 by A21,XREAL_1:9;
        then 1 <= k2 by A28,XXREAL_0:2;
        hence thesis by A12,A27,A29;
      end;
      hence thesis by A20,A23,XBOOLE_0:def 3;
    end;
A30: dom Seq q1 \/ dom (len Seq q1 Shift Seq q2)
    = dom (Seq q1 \/ (len Seq q1 Shift Seq q2)) by RELAT_1:13;
    dom Seq q1 misses dom (len Seq q1 Shift Seq q2) by Th63;
    then reconsider ss1 = Seq q1 \/ (len Seq q1 Shift Seq q2) as Function
    by GRFUNC_1:31;
    for x st x in dom Seq ss holds (Seq ss).x = ss1.x
    proof
      let x;
      assume
A31:  x in dom Seq ss;
      then
A32:  x in dom (ss*Sgm dom ss) by FINSEQ_1:def 14;
A33:  (Seq ss).x = (ss*Sgm dom ss).x by FINSEQ_1:def 14
        .= ss.((Sgm dom ss).x) by A32,FUNCT_1:22
        .= ss.((Sgm (dom q1 \/ dom (len p1 Shift q2))).x) by A3,RELAT_1:13
        .= ss.(((Sgm dom q1)^(Sgm dom (len p1 Shift q2))).x) by A1,A2,Lm11;
A34:  now
        assume
A35:    x in dom Seq q1;
        then
A36:    x in dom Sgm dom q1 by Lm4;
        then (Sgm dom q1).x in rng Sgm dom q1 by FUNCT_1:def 5;
        then
A37:    (Sgm dom q1).x in dom q1 by FINSEQ_1:71;
        (Seq ss).x = ss.((Sgm dom q1).x) by A33,A36,FINSEQ_1:def 7
          .= q1.((Sgm dom q1).x) by A3,A37,GRFUNC_1:35
          .= (q1*Sgm dom q1).x by A36,FUNCT_1:23
          .= (Seq q1).x by FINSEQ_1:def 14;
        hence thesis by A35,GRFUNC_1:35;
      end;
      now
        assume
A38:    x in dom (len Seq q1 Shift Seq q2);
        dom (len Seq q1 Shift Seq q2) = {len Seq q1 + j: j in dom Seq q2 }
        by Def15;
        then consider j such that
A39:    x = len Seq q1 + j and
A40:    j in dom Seq q2 by A38;
A41:    ss1.x = (len Seq q1 Shift Seq q2).x by A38,GRFUNC_1:35
          .= (Seq q2).j by A39,A40,Def15;
A42:    ex l1 be Nat st ( dom q1 c= Seg l1) by FINSEQ_1:def 12;
A43:    ex l2 be Nat st ( dom (len p1 Shift q2) c= Seg l2) by FINSEQ_1:def 12;
        card dom q1 = len Sgm dom q1 by A42,FINSEQ_3:44;
        then
A44:    card q1 = len Sgm dom q1 by CARD_1:104;
A45:    len Seq q1 = card q1 by Th7;
A46:    dom Seq q2 = Seg len Seq q2 by FINSEQ_1:def 3;
        card q2 = len Seq q2 by Th7;
        then card (len p1 Shift q2) = len Seq q2 by Th57;
        then
A47:    card dom (len p1 Shift q2) = len Seq q2 by CARD_1:104;
A48:    card dom (len p1 Shift q2) = len Sgm dom (len p1 Shift q2)
        by A43,FINSEQ_3:44;
A49:    len Sgm dom (len p1 Shift q2) = len Seq q2 by A43,A47,FINSEQ_3:44;
A50:    dom Seq q2 = dom Sgm dom (len p1 Shift q2) by A46,A47,A48,
        FINSEQ_1:def 3;
A51:    j in dom Sgm dom (len p1 Shift q2) by A40,A46,A49,FINSEQ_1:def 3;
        (Sgm dom (len p1 Shift q2)).j in rng Sgm dom (len p1 Shift q2)
        by A40,A50,FUNCT_1:def 5;
        then
A52:    (Sgm dom (len p1 Shift q2)).j in dom (len p1 Shift q2) by
        FINSEQ_1:71;
        (Seq ss).x
        = ss.((Sgm dom (len p1 Shift q2)).j) by A33,A39,A44,A45,A51,
        FINSEQ_1:def 7
          .= (len p1 Shift q2).((Sgm dom (len p1 Shift q2)).j)
        by A3,A52,GRFUNC_1:35
          .= ((len p1 Shift q2)*(Sgm dom (len p1 Shift q2))).j
        by A40,A50,FUNCT_1:23
          .= (Seq (len p1 Shift q2)).j by FINSEQ_1:def 14
          .= (Seq q2).j by A40,Th76;
        hence thesis by A41;
      end;
      hence thesis by A4,A15,A31,A34,XBOOLE_0:def 3;
    end;
    then Seq ss = ss1 by A4,A15,A30,FUNCT_1:9;
    hence thesis by A3,A4;
end;

theorem Th82:
for p1,p2 being FinSequence, q1,q2 being FinSubsequence st q1 c= p1 & q2 c= p2
  ex ss being FinSubsequence st
  ss = q1 \/ (len p1 Shift q2) & (Seq q1)^(Seq q2) = Seq ss
proof
  let p1,p2 be FinSequence, q1,q2 be FinSubsequence;
  assume that
A1: q1 c= p1 and
A2: q2 c= p2;
  consider ss being FinSubsequence such that
A3: ss = q1 \/ (len p1 Shift q2) and
A4: dom Seq ss = Seg (len Seq q1 + len Seq q2) and
A5: Seq ss = Seq q1 \/ (len Seq q1 Shift Seq q2) by A1,A2,Th81;
A6: for j1 be Nat st j1 in dom Seq q1 holds (Seq ss).j1 = (Seq q1).j1
  by A5,GRFUNC_1:35;
  for j2 be Nat st j2 in dom Seq q2 holds (Seq ss).(len Seq q1 + j2) =
  (Seq q2).j2
  proof
    let j2 be Nat;
    assume
A7: j2 in dom Seq q2;
    dom (len Seq q1 Shift Seq q2) = {len Seq q1 + k: k in dom Seq q2}
    by Def15;
    then len Seq q1 + j2 in dom (len Seq q1 Shift Seq q2) by A7;
    hence (Seq ss).(len Seq q1 + j2)
    = (len Seq q1 Shift Seq q2).(len Seq q1 + j2) by A5,GRFUNC_1:35
      .= (Seq q2).j2 by A7,Def15;
  end;
  then Seq ss = (Seq q1)^(Seq q2) by A4,A6,FINSEQ_1:def 7;
  hence thesis by A3;
end;

theorem
  (R1 concur R2) before (P1 concur P2) c= (R1 before P1) concur (R2 before P2)
proof
  let x;
  assume x in (R1 concur R2) before (P1 concur P2);
  then consider C1,C2 such that
A1: x = C1^C2 and
A2: C1 in R1 concur R2 and
A3: C2 in P1 concur P2;
  consider C3 such that
A4: C1 = C3 and
A5: ex q1,q2 st C3 = q1 \/ q2 & q1 misses q2 & Seq q1 in R1 & Seq q2 in
  R2
  by A2;
  consider q1,q2 such that
A6: C3 = q1 \/ q2 and
A7: q1 misses q2 and
A8: Seq q1 in R1 and
A9: Seq q2 in R2 by A5;
  consider C4 being firing-sequence of N such that
A10: C2 = C4 and
A11: ex q3,q4 st C4 = q3 \/ q4 & q3 misses q4 & Seq q3 in P1 & Seq q4 in
  P2
  by A3;
  consider q3,q4 such that
A12: C4 = q3 \/ q4 and
A13: q3 misses q4 and
A14: Seq q3 in P1 and
A15: Seq q4 in P2 by A11;
  reconsider C = C1^C2 as firing-sequence of N;
  reconsider q5 = len C1 Shift q3, q6 = len C1 Shift q4 as FinSubsequence;
A16: q1 c= C1 by A4,A6,XBOOLE_1:7;
A17: q2 c= C1 by A4,A6,XBOOLE_1:7;
A18: q3 c= C2 by A10,A12,XBOOLE_1:7;
A19: q4 c= C2 by A10,A12,XBOOLE_1:7;
A20: C1 c= C1^C2 by FINSEQ_6:12;
  then
A21: q1 c= C1^C2 by A16,XBOOLE_1:1;
A22: q2 c= C1^C2 by A17,A20,XBOOLE_1:1;
  reconsider ss = C2 as FinSubsequence;
A23: q5 c= len C1 Shift ss by A10,A12,Th70,XBOOLE_1:7;
A24: q6 c= len C1 Shift ss by A10,A12,Th70,XBOOLE_1:7;
A25: len C1 Shift C2 c= C1^C2 by Th71;
  then
A26: q5 c= C1^C2 by A23,XBOOLE_1:1;
A27: q6 c= C1^C2 by A24,A25,XBOOLE_1:1;
A28: dom q1 misses dom q5 by A16,A21,A26,Lm9,Th10;
  dom q2 misses dom q6 by A17,A22,A27,Lm9,Th10;
  then reconsider ss1 = q1 \/ q5, ss2 = q2 \/ q6 as Function by A28,
  GRFUNC_1:31;
A29: dom C = Seg len C by FINSEQ_1:def 3;
A30: dom ss1 c= dom C by A16,A18,Lm8;
  dom ss2 c= dom C by A17,A19,Lm8;
  then reconsider ss1, ss2 as FinSubsequence by A29,A30,FINSEQ_1:def 12;
A31: ss1 \/ ss2 = q1 \/ (len C1 Shift q3) \/ q2 \/ (len C1 Shift q4)
  by XBOOLE_1:4
    .= (q1 \/ q2) \/ (len C1 Shift q3) \/ (len C1 Shift q4)
  by XBOOLE_1:4
    .= C1 \/ ((len C1 Shift q3) \/ (len C1 Shift q4))
  by A4,A6,XBOOLE_1:4
    .= C1 \/ (len C1 Shift C2) by A10,A12,A13,Th73
    .= C by Th64;
A32: ss1 /\ q2 = q1 /\ q2 \/ (len C1 Shift q3) /\ q2 by XBOOLE_1:23;
  ss1 /\ (len C1 Shift q4) = q1 /\ (len C1 Shift q4) \/ (len C1 Shift q3)
  /\ (len C1 Shift q4) by XBOOLE_1:23;
  then
A33: ss1 /\ ss2 = (q1 /\ q2 \/ (len C1 Shift q3) /\ q2) \/ (q1 /\
  (len C1 Shift q4) \/ (len C1 Shift q3) /\ (len C1 Shift q4)) by A32,
  XBOOLE_1:23;
A34: q1 /\ q2 = {} by A7,XBOOLE_0:def 7;
A35: (len C1 Shift q3) misses q2 by A4,A6,Lm9,XBOOLE_1:7;
A36: q1 misses (len C1 Shift q4) by A4,A6,Lm9,XBOOLE_1:7;
A37: (len C1 Shift q3) /\ q2 = {} by A35,XBOOLE_0:def 7;
A38: q1 /\ (len C1 Shift q4) = {} by A36,XBOOLE_0:def 7;
  dom q3 misses dom q4 by A13,A18,A19,Th10;
  then dom (len C1 Shift q3) misses dom (len C1 Shift q4) by Th72;
  then (len C1 Shift q3) misses (len C1 Shift q4) by Th8;
  then ss1 /\ ss2 = {} by A33,A34,A37,A38,XBOOLE_0:def 7;
  then
A39: ss1 misses ss2 by XBOOLE_0:def 7;
A40: ex ss3 being FinSubsequence st ( ss3 = ss1)&( (Seq q1)^(Seq
  q3) = Seq ss3) by A16,A18,Th82;
A41: ex ss4 being FinSubsequence st ( ss4 = ss2)&( (Seq q2)^(Seq
  q4) = Seq ss4) by A17,A19,Th82;
A42: Seq ss1 in R1 before P1 by A8,A14,A40;
  Seq ss2 in R2 before P2 by A9,A15,A41;
  hence thesis by A1,A31,A39,A42;
end;

registration
  let P, N;
  let R1, R2 be non empty process of N;
  cluster R1 concur R2 -> non empty;
  coherence
  proof
    consider fs1 being set such that
A1: fs1 in R1 by XBOOLE_0:def 1;
    consider fs2 being set such that
A2: fs2 in R2 by XBOOLE_0:def 1;
    reconsider fs1, fs2 as firing-sequence of N by A1,A2;
    reconsider C = fs1^fs2 as firing-sequence of N;
A3: C = fs1 \/ (len fs1 Shift fs2) by Th64;
A4: fs1 misses (len fs1 Shift fs2) by Th65;
A5: fs1 = Seq fs1 by FINSEQ_3:125;
    Seq (len fs1 Shift fs2) in R2 by A2,Th61;
    then C in R1 concur R2 by A1,A3,A4,A5;
    hence thesis;
  end;
end;

begin  :: Neutral process

definition
  let P;
  let N be Petri_net of P;
  func NeutralProcess(N) -> non empty process of N equals
  {<*>N};
  coherence;
end;

definition
  let P;
  let N be Petri_net of P;
  let t be Element of N;
  func ElementaryProcess(t) -> non empty process of N equals
  {<*t*>};
  coherence;
end;

theorem
  NeutralProcess(N) before R = R
proof
  thus NeutralProcess(N) before R c= R
  proof
    let x;
    assume x in (NeutralProcess(N) before R);
    then consider C1,C such that
A1: x = C1^C and
A2: C1 in NeutralProcess(N) and
A3: C in R;
    C1 = <*>N by A2,TARSKI:def 1;
    hence thesis by A1,A3,FINSEQ_1:47;
  end;
  let x;
  assume
A4: x in R;
  then reconsider C = x as Element of N*;
A5: <*>N in NeutralProcess(N) by TARSKI:def 1;
  x = (<*>N)^C by FINSEQ_1:47;
  hence thesis by A4,A5;
end;

theorem
  R before NeutralProcess(N) = R
proof
  thus R before NeutralProcess N c= R
  proof
    let x;
    assume x in R before NeutralProcess N;
    then consider C1,C such that
A1: x = C1^C and
A2: C1 in R and
A3: C in NeutralProcess(N);
    C = <*>N by A3,TARSKI:def 1;
    hence thesis by A1,A2,FINSEQ_1:47;
  end;
  let x;
  assume
A4: x in R;
  then reconsider C = x as Element of N*;
A5: <*>N in NeutralProcess(N) by TARSKI:def 1;
  x = C^(<*>N) by FINSEQ_1:47;
  hence thesis by A4,A5;
end;

theorem
  NeutralProcess(N) concur R = R
proof
  thus NeutralProcess(N) concur R c= R
  proof
    let x;
    assume x in NeutralProcess(N) concur R;
    then consider C such that
A1: x = C and
A2: ex q1,q2 being FinSubsequence st C = q1 \/ q2 & q1 misses q2 & Seq
    q1 in {<*>N} & Seq q2 in R;
    consider q1,q2 being FinSubsequence such that
A3: C = q1 \/ q2
    and q1 misses q2 and
A4: Seq q1 in {<*>N} and
A5: Seq q2 in R by A2;
    Seq q1 = {} by A4,TARSKI:def 1;
    then q1 = {} by Lm1;
    hence thesis by A1,A3,A5,FINSEQ_3:125;
  end;
  let x;
  assume
A6: x in R;
  then reconsider C = x as firing-sequence of N;
  reconsider q1 = <*>N, q2 = C as FinSubsequence;
A7: q1 /\ q2 = {};
A8: Seq q2 = C by FINSEQ_3:125;
A9: {} \/ q2 = C;
A10: Seq q1 = q1 by FINSEQ_3:125;
A11: q1 in {<*>N} by TARSKI:def 1;
  q1 misses q2 by A7,XBOOLE_0:def 7;
  hence thesis by A6,A8,A9,A10,A11;
end;
