:: Tuples and Cartesian Products for n=6
::  by Michal Muzalewski and Wojciech Skaba
::
:: Received October 15, 1990
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies BOOLE, TARSKI, MCART_1, MCART_2, MCART_3, MCART_4;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, MCART_1, MCART_2;
 constructors TARSKI, MCART_1, MCART_2;
 registrations XBOOLE_0;
 requirements SUBSET, BOOLE;
 theorems TARSKI, ZFMISC_1, MCART_1, MCART_2, XBOOLE_0, XBOOLE_1;
 schemes XBOOLE_0;

begin

reserve v,z,x,x1,x2,x3,x4,x5,x6,x7,y,y1,y2,y3,y4,y5,y6,y7,
  X,X1,X2,X3,X4,X5,X6,X7,Y,Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8,Y9,YA,YB,
  Z,Z1,Z2,Z3,Z4,Z5,Z6,Z7,Z8,Z9,ZA,ZB for set;

reserve xx1 for Element of X1;
reserve xx2 for Element of X2;
reserve xx3 for Element of X3;
reserve xx4 for Element of X4;
reserve xx5 for Element of X5;
reserve xx6 for Element of X6;
reserve xx7 for Element of X7;

theorem
  X <> {} implies ex Y st Y in X & for Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8
  st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7 in
  Y8 & Y8 in Y holds Y1 misses X
proof
  defpred P1[set] means ex Y1,Y2,Y3,Y4,Y5,Y6,Y7
  st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7
  & Y7 in $1 & Y1 meets X;
  consider Z1 such that
A1: for Y holds Y in Z1 iff Y in union X & P1[Y] from XBOOLE_0:sch 1;
  defpred P2[set] means ex Y1,Y2,Y3,Y4,Y5,Y6
  st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6
  & Y6 in $1 & Y1 meets X;
  consider Z2 such that
A2: for Y holds Y in Z2 iff Y in union union X & P2[Y] from XBOOLE_0:sch 1;
  defpred P3[set] means ex Y1,Y2,Y3,Y4,Y5
  st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in $1 & Y1 meets X;
  consider Z3 such that
A3: for Y holds Y in Z3 iff Y in union union union X & P3[Y] from
  XBOOLE_0:sch 1;
  defpred P4[set] means
  ex Y1,Y2,Y3,Y4 st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in $1 & Y1 meets X;
  consider Z4 such that
A4: for Y holds Y in Z4 iff
  Y in union union union union X & P4[Y] from XBOOLE_0:sch 1;
  defpred P5[set] means
  ex Y1,Y2,Y3 st Y1 in Y2 & Y2 in Y3 & Y3 in $1 & Y1 meets X;
  consider Z5 such that
A5: for Y holds Y in Z5 iff
  Y in union union union union union X & P5[Y] from XBOOLE_0:sch 1;
  defpred P6[set] means ex Y1,Y2 st Y1 in Y2 & Y2 in $1 & Y1 meets X;
  consider Z6 such that
A6: for Y holds Y in Z6 iff
  Y in union union union union union union X & P6[Y] from XBOOLE_0:sch 1;
  defpred P7[set] means ex Y1 st Y1 in $1 & Y1 meets X;
  consider Z7 such that
A7: for Y holds Y in Z7 iff
  Y in union union union union union union union X & P7[Y]
  from XBOOLE_0:sch 1;
  defpred P8[set] means $1 meets X;
  consider Z8 such that
A8: for Y holds Y in Z8 iff
  Y in union union union union union union union union X
  & P8[Y] from XBOOLE_0:sch 1;
  set V = X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8;
A9: V = X \/ (Z1 \/ Z2) \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3) \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4) \/ Z5 \/ Z6 \/ Z7 \/ Z8 by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5) \/ Z6 \/ Z7 \/ Z8 by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6) \/ Z7 \/ Z8 by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7) \/ Z8 by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8) by XBOOLE_1:4;
  assume X <> {};
  then V <> {} by A9,XBOOLE_1:15;
  then consider Y such that
A10: Y in V and
A11: Y misses V by MCART_1:1;
  assume
A12: not thesis;
  now
    assume
A13: Y in X;
    then consider Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8 such that
A14: Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7 in
    Y8 & Y8 in Y & not Y1 misses X by A12;
    Y8 in union X & Y1 meets X by A13,A14,TARSKI:def 4;
    then Y8 in Z1 by A1,A14;
    then Y8 in X \/ Z1 by XBOOLE_0:def 2;
    then Y8 in X \/ Z1 \/ Z2 by XBOOLE_0:def 2;
    then Y8 in X \/ Z1 \/ Z2 \/ Z3 by XBOOLE_0:def 2;
    then Y8 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 by XBOOLE_0:def 2;
    then Y8 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_0:def 2;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by A14,XBOOLE_0:3;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_1:70;
    hence contradiction by A11,XBOOLE_1:70;
  end;
  then Y in Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
  Z8 by A9,A10,XBOOLE_0:def 2;
  then Y in Z1 \/ (Z2 \/ Z3) \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 by XBOOLE_1:4;
  then Y in Z1 \/ (Z2 \/ Z3 \/ Z4) \/ Z5 \/ Z6 \/ Z7 \/ Z8 by XBOOLE_1:4;
  then Y in Z1 \/ (Z2 \/ Z3 \/ Z4 \/ Z5) \/ Z6 \/ Z7 \/ Z8 by XBOOLE_1:4;
  then Y in Z1 \/ (Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6) \/ Z7 \/ Z8 by XBOOLE_1:4;
  then Y in Z1 \/ (Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7) \/ Z8 by XBOOLE_1:4;
  then
A15: Y in Z1 \/ (Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8) by XBOOLE_1:4;
  now
    assume
A16: Y in Z1;
    then consider Y1,Y2,Y3,Y4,Y5,Y6,Y7 such that
A17: Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7 in
    Y & Y1 meets X by A1;
    Y in union X by A1,A16;
    then Y7 in union union X by A17,TARSKI:def 4;
    then Y7 in Z2 by A2,A17;
    then Y7 in X \/ Z1 \/ Z2 by XBOOLE_0:def 2;
    then Y meets X \/ Z1 \/ Z2 by A17,XBOOLE_0:3;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_1:70;
    hence contradiction by A11,XBOOLE_1:70;
  end;
  then Y in Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 by A15,XBOOLE_0:def 2;
  then Y in Z2 \/ (Z3 \/ Z4) \/ Z5 \/ Z6 \/ Z7 \/ Z8 by XBOOLE_1:4;
  then Y in Z2 \/ (Z3 \/ Z4 \/ Z5) \/ Z6 \/ Z7 \/ Z8 by XBOOLE_1:4;
  then Y in Z2 \/ (Z3 \/ Z4 \/ Z5 \/ Z6) \/ Z7 \/ Z8 by XBOOLE_1:4;
  then Y in Z2 \/ (Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7) \/ Z8 by XBOOLE_1:4;
  then
A18: Y in Z2 \/ (Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8) by XBOOLE_1:4;
  now
    assume
A19: Y in Z2;
    then consider Y1,Y2,Y3,Y4,Y5,Y6 such that
A20: Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y
    & Y1 meets X by A2;
    Y in union union X by A2,A19;
    then Y6 in union union union X by A20,TARSKI:def 4;
    then Y6 in Z3 by A3,A20;
    then Y6 in X \/ Z1 \/ Z2 \/ Z3 by XBOOLE_0:def 2;
    then Y6 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 by XBOOLE_0:def 2;
    then Y6 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_0:def 2;
    then Y6 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_0:def 2;
    then Y6 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
    then Y6 in V by XBOOLE_0:def 2;
    hence contradiction by A11,A20,XBOOLE_0:3;
  end;
  then Y in Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 by A18,XBOOLE_0:def 2;
  then Y in Z3 \/ (Z4 \/ Z5) \/ Z6 \/ Z7 \/ Z8 by XBOOLE_1:4;
  then Y in Z3 \/ (Z4 \/ Z5 \/ Z6) \/ Z7 \/ Z8 by XBOOLE_1:4;
  then Y in Z3 \/ (Z4 \/ Z5 \/ Z6 \/ Z7) \/ Z8 by XBOOLE_1:4;
  then
A21: Y in Z3 \/ (Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8) by XBOOLE_1:4;
  now
    assume
A22: Y in Z3;
    then consider Y1,Y2,Y3,Y4,Y5 such that
A23: Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y & Y1 meets X by A3;
    Y in union union union X by A3,A22;
    then Y5 in union union union union X by A23,TARSKI:def 4;
    then Y5 in Z4 by A4,A23;
    then Y5 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 by XBOOLE_0:def 2;
    then Y5 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_0:def 2;
    then Y5 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_0:def 2;
    then Y5 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
    then Y5 in V by XBOOLE_0:def 2;
    hence contradiction by A11,A23,XBOOLE_0:3;
  end;
  then Y in Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 by A21,XBOOLE_0:def 2;
  then Y in Z4 \/ (Z5 \/ Z6) \/ Z7 \/ Z8 by XBOOLE_1:4;
  then Y in Z4 \/ (Z5 \/ Z6 \/ Z7) \/ Z8 by XBOOLE_1:4;
  then
A24: Y in Z4 \/ (Z5 \/ Z6 \/ Z7 \/ Z8) by XBOOLE_1:4;
  now
    assume
A25: Y in Z4;
    then consider Y1,Y2,Y3,Y4 such that
A26: Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y & Y1 meets X by A4;
    Y in union union union union X by A4,A25;
    then Y4 in union union union union union X by A26,TARSKI:def 4;
    then Y4 in Z5 by A5,A26;
    then Y4 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_0:def 2;
    then Y4 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_0:def 2;
    then Y4 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
    then Y4 in V by XBOOLE_0:def 2;
    hence contradiction by A11,A26,XBOOLE_0:3;
  end;
  then Y in Z5 \/ Z6 \/ Z7 \/ Z8 by A24,XBOOLE_0:def 2;
  then Y in Z5 \/ (Z6 \/ Z7) \/ Z8 by XBOOLE_1:4;
  then
A27: Y in Z5 \/ (Z6 \/ Z7 \/ Z8) by XBOOLE_1:4;
  now
    assume
A28: Y in Z5;
    then consider Y1,Y2,Y3 such that
A29: Y1 in Y2 & Y2 in Y3 & Y3 in Y & Y1 meets X by A5;
    Y in union union union union union X by A5,A28;
    then Y3 in union union union union union union X by A29,TARSKI:def 4;
    then Y3 in Z6 by A6,A29;
    then Y3 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_0:def 2;
    then Y3 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
    then Y3 in V by XBOOLE_0:def 2;
    hence contradiction by A11,A29,XBOOLE_0:3;
  end;
  then Y in Z6 \/ Z7 \/ Z8 by A27,XBOOLE_0:def 2;
  then
A30: Y in Z6 \/ (Z7 \/ Z8) by XBOOLE_1:4;
  now
    assume
A31: Y in Z6;
    then consider Y1,Y2 such that
A32: Y1 in Y2 & Y2 in Y & Y1 meets X by A6;
    Y in union union union union union union X by A6,A31;
    then Y2 in
    union union union union union union union X by A32,TARSKI:def 4;
    then Y2 in Z7 by A7,A32;
    then Y2 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
    then Y2 in V by XBOOLE_0:def 2;
    hence contradiction by A11,A32,XBOOLE_0:3;
  end;
  then
A33: Y in Z7 \/ Z8 by A30,XBOOLE_0:def 2;
  now
    assume
A34: Y in Z7;
    then consider Y1 such that
A35: Y1 in Y & Y1 meets X by A7;
    Y in union union union union union union union X by A7,A34;
    then Y1 in union union union union union union union union X
    by A35,TARSKI:def 4;
    then Y1 in Z8 by A8,A35;
    then Y1 in V by XBOOLE_0:def 2;
    hence contradiction by A11,A35,XBOOLE_0:3;
  end;
  then Y in Z8 by A33,XBOOLE_0:def 2;
  then Y meets X by A8;
  hence contradiction by A9,A11,XBOOLE_1:70;
end;

theorem Th2:
  X <> {} implies ex Y st Y in X & for Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8,Y9
  st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7 in
  Y8 & Y8 in Y9 & Y9 in Y holds Y1 misses X
proof
  defpred P1[set] means ex Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8
  st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7
  & Y7 in Y8 & Y8 in $1 & Y1 meets X;
  consider Z1 such that
A1: for Y holds Y in Z1 iff Y in union X & P1[Y] from XBOOLE_0:sch 1;
  defpred P2[set] means ex Y1,Y2,Y3,Y4,Y5,Y6,Y7
  st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7
  & Y7 in $1 & Y1 meets X;
  consider Z2 such that
A2: for Y holds Y in Z2 iff Y in union union X & P2[Y] from XBOOLE_0:sch 1;
  defpred P3[set] means ex Y1,Y2,Y3,Y4,Y5,Y6
  st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in $1
  & Y1 meets X;
  consider Z3 such that
A3: for Y holds Y in Z3 iff
  Y in union union union X & P3[Y] from XBOOLE_0:sch 1;
  defpred P4[set] means ex Y1,Y2,Y3,Y4,Y5
  st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in $1 & Y1 meets X;
  consider Z4 such that
A4: for Y holds Y in Z4 iff
  Y in union union union union X & P4[Y] from XBOOLE_0:sch 1;
  defpred P5[set] means ex Y1,Y2,Y3,Y4
  st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in $1 & Y1 meets X;
  consider Z5 such that
A5: for Y holds Y in Z5 iff
  Y in union union union union union X & P5[Y] from XBOOLE_0:sch 1;
  defpred P6[set] means
  ex Y1,Y2,Y3 st Y1 in Y2 & Y2 in Y3 & Y3 in $1 & Y1 meets X;
  consider Z6 such that
A6: for Y holds Y in Z6 iff
  Y in union union union union union union X & P6[Y] from XBOOLE_0:sch 1;
  defpred P7[set] means ex Y1,Y2 st Y1 in Y2 & Y2 in $1 & Y1 meets X;
  consider Z7 such that
A7: for Y holds Y in Z7 iff
  Y in union union union union union union union X & P7[Y]
  from XBOOLE_0:sch 1;
  defpred P8[set] means ex Y1 st Y1 in $1 & Y1 meets X;
  consider Z8 such that
A8: for Y holds Y in Z8 iff
  Y in union union union union union union union union X & P8[Y]
  from XBOOLE_0:sch 1;
  defpred P9[set] means $1 meets X;
  consider Z9 such that
A9: for Y holds Y in Z9 iff
  Y in union union union union union union union union union X
  & P9[Y] from XBOOLE_0:sch 1;
  set V = X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9;
A10: V = X \/ (Z1 \/ Z2) \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 by
  XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3) \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4) \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5) \/ Z6 \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6) \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7) \/ Z8 \/ Z9 by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8) \/ Z9 by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9) by XBOOLE_1:4;
  assume X <> {};
  then V <> {} by A10,XBOOLE_1:15;
  then consider Y such that
A11: Y in V and
A12: Y misses V by MCART_1:1;
  assume
A13: not thesis;
  now
    assume
A14: Y in X;
    then consider Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8,Y9 such that
A15: Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7 in
    Y8 & Y8 in Y9 & Y9 in Y & not Y1 misses X by A13;
    Y9 in union X & Y1 meets X by A14,A15,TARSKI:def 4;
    then Y9 in Z1 by A1,A15;
    then Y9 in X \/ Z1 by XBOOLE_0:def 2;
    then Y9 in X \/ Z1 \/ Z2 by XBOOLE_0:def 2;
    then Y9 in X \/ Z1 \/ Z2 \/ Z3 by XBOOLE_0:def 2;
    then Y9 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 by XBOOLE_0:def 2;
    then Y9 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_0:def 2;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by A15,XBOOLE_0:3;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
    Z8 by XBOOLE_1:70;
    hence contradiction by A12,XBOOLE_1:70;
  end;
  then Y in Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/
  Z9 by A10,A11,XBOOLE_0:def 2;
  then Y in Z1 \/ (Z2 \/ Z3) \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:
  4;
  then Y in Z1 \/ (Z2 \/ Z3 \/ Z4) \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:
  4;
  then Y in Z1 \/ (Z2 \/ Z3 \/ Z4 \/ Z5) \/ Z6 \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:
  4;
  then Y in Z1 \/ (Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6) \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:
  4;
  then Y in Z1 \/ (Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7) \/ Z8 \/ Z9 by XBOOLE_1:
  4;
  then Y in Z1 \/ (Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8) \/
  Z9 by XBOOLE_1:4;
  then
A16: Y in Z1 \/ (Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9) by XBOOLE_1:4;
  now
    assume
A17: Y in Z1;
    then consider Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8 such that
A18: Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7 in
    Y8 & Y8 in Y & Y1 meets X by A1;
    Y in union X by A1,A17;
    then Y8 in union union X by A18,TARSKI:def 4;
    then Y8 in Z2 by A2,A18;
    then Y8 in X \/ Z1 \/ Z2 by XBOOLE_0:def 2;
    then Y meets X \/ Z1 \/ Z2 by A18,XBOOLE_0:3;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
    Z8 by XBOOLE_1:70;
    hence contradiction by A12,XBOOLE_1:70;
  end;
  then Y in Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 by A16,XBOOLE_0:def
  2;
  then Y in Z2 \/ (Z3 \/ Z4) \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:4;
  then Y in Z2 \/ (Z3 \/ Z4 \/ Z5) \/ Z6 \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:4;
  then Y in Z2 \/ (Z3 \/ Z4 \/ Z5 \/ Z6) \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:4;
  then Y in Z2 \/ (Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7) \/ Z8 \/ Z9 by XBOOLE_1:4;
  then Y in Z2 \/ (Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8) \/ Z9 by XBOOLE_1:4;
  then
A19: Y in Z2 \/ (Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9) by XBOOLE_1:4;
  now
    assume
A20: Y in Z2;
    then consider Y1,Y2,Y3,Y4,Y5,Y6,Y7 such that
A21: Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7 in Y
    & Y1 meets X by A2;
    Y in union union X by A2,A20;
    then Y7 in union union union X by A21,TARSKI:def 4;
    then Y7 in Z3 by A3,A21;
    then Y7 in X \/ Z1 \/ Z2 \/ Z3 by XBOOLE_0:def 2;
    then Y7 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 by XBOOLE_0:def 2;
    then Y7 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_0:def 2;
    then Y7 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_0:def 2;
    then Y7 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
    then Y7 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
    Z8 by XBOOLE_0:def 2;
    then Y7 in V by XBOOLE_0:def 2;
    hence contradiction by A12,A21,XBOOLE_0:3;
  end;
  then Y in Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 by A19,XBOOLE_0:def 2;
  then Y in Z3 \/ (Z4 \/ Z5) \/ Z6 \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:4;
  then Y in Z3 \/ (Z4 \/ Z5 \/ Z6) \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:4;
  then Y in Z3 \/ (Z4 \/ Z5 \/ Z6 \/ Z7) \/ Z8 \/ Z9 by XBOOLE_1:4;
  then Y in Z3 \/ (Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8) \/ Z9 by XBOOLE_1:4;
  then
A22: Y in Z3 \/ (Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9) by XBOOLE_1:4;
  now
    assume
A23: Y in Z3;
    then consider Y1,Y2,Y3,Y4,Y5,Y6 such that
A24: Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y
    & Y1 meets X by A3;
    Y in union union union X by A3,A23;
    then Y6 in union union union union X by A24,TARSKI:def 4;
    then Y6 in Z4 by A4,A24;
    then Y6 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 by XBOOLE_0:def 2;
    then Y6 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_0:def 2;
    then Y6 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_0:def 2;
    then Y6 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
    then Y6 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
    Z8 by XBOOLE_0:def 2;
    then Y6 in V by XBOOLE_0:def 2;
    hence contradiction by A12,A24,XBOOLE_0:3;
  end;
  then Y in Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 by A22,XBOOLE_0:def 2;
  then Y in Z4 \/ (Z5 \/ Z6) \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:4;
  then Y in Z4 \/ (Z5 \/ Z6 \/ Z7) \/ Z8 \/ Z9 by XBOOLE_1:4;
  then Y in Z4 \/ (Z5 \/ Z6 \/ Z7 \/ Z8) \/ Z9 by XBOOLE_1:4;
  then
A25: Y in Z4 \/ (Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9) by XBOOLE_1:4;
  now
    assume
A26: Y in Z4;
    then consider Y1,Y2,Y3,Y4,Y5 such that
A27: Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y & Y1 meets X by A4;
    Y in union union union union X by A4,A26;
    then Y5 in union union union union union X by A27,TARSKI:def 4;
    then Y5 in Z5 by A5,A27;
    then Y5 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_0:def 2;
    then Y5 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_0:def 2;
    then Y5 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
    then Y5 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
    Z8 by XBOOLE_0:def 2;
    then Y5 in V by XBOOLE_0:def 2;
    hence contradiction by A12,A27,XBOOLE_0:3;
  end;
  then Y in Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 by A25,XBOOLE_0:def 2;
  then Y in Z5 \/ (Z6 \/ Z7) \/ Z8 \/ Z9 by XBOOLE_1:4;
  then Y in Z5 \/ (Z6 \/ Z7 \/ Z8) \/ Z9 by XBOOLE_1:4;
  then
A28: Y in Z5 \/ (Z6 \/ Z7 \/ Z8 \/ Z9) by XBOOLE_1:4;
  now
    assume
A29: Y in Z5;
    then consider Y1,Y2,Y3,Y4 such that
A30: Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y & Y1 meets X by A5;
    Y in union union union union union X by A5,A29;
    then Y4 in union union union union union union X by A30,TARSKI:def 4;
    then Y4 in Z6 by A6,A30;
    then Y4 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_0:def 2;
    then Y4 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
    then Y4 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
    Z8 by XBOOLE_0:def 2;
    then Y4 in V by XBOOLE_0:def 2;
    hence contradiction by A12,A30,XBOOLE_0:3;
  end;
  then Y in Z6 \/ Z7 \/ Z8 \/ Z9 by A28,XBOOLE_0:def 2;
  then Y in Z6 \/ (Z7 \/ Z8) \/ Z9 by XBOOLE_1:4;
  then
A31: Y in Z6 \/ (Z7 \/ Z8 \/ Z9) by XBOOLE_1:4;
  now
    assume
A32: Y in Z6;
    then consider Y1,Y2,Y3 such that
A33: Y1 in Y2 & Y2 in Y3 & Y3 in Y & Y1 meets X by A6;
    Y in union union union union union union X by A6,A32;
    then Y3 in
    union union union union union union union X by A33,TARSKI:def 4;
    then Y3 in Z7 by A7,A33;
    then Y3 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
    then Y3 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
    Z8 by XBOOLE_0:def 2;
    then Y3 in V by XBOOLE_0:def 2;
    hence contradiction by A12,A33,XBOOLE_0:3;
  end;
  then Y in Z7 \/ Z8 \/ Z9 by A31,XBOOLE_0:def 2;
  then
A34: Y in Z7 \/ (Z8 \/ Z9) by XBOOLE_1:4;
  now
    assume
A35: Y in Z7;
    then consider Y1,Y2 such that
A36: Y1 in Y2 & Y2 in Y & Y1 meets X by A7;
    Y in union union union union union union union X by A7,A35;
    then Y2 in union union union union union union union union X
    by A36,TARSKI:def 4;
    then Y2 in Z8 by A8,A36;
    then Y2 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
    Z8 by XBOOLE_0:def 2;
    then Y2 in V by XBOOLE_0:def 2;
    hence contradiction by A12,A36,XBOOLE_0:3;
  end;
  then
A37: Y in Z8 \/ Z9 by A34,XBOOLE_0:def 2;
  now
    assume
A38: Y in Z8;
    then consider Y1 such that
A39: Y1 in Y & Y1 meets X by A8;
    Y in union union union union union union union union X by A8,A38;
    then Y1 in union union union union union union union union union X
    by A39,TARSKI:def 4;
    then Y1 in Z9 by A9,A39;
    then Y1 in V by XBOOLE_0:def 2;
    hence contradiction by A12,A39,XBOOLE_0:3;
  end;
  then Y in Z9 by A37,XBOOLE_0:def 2;
  then Y meets X by A9;
  hence contradiction by A10,A12,XBOOLE_1:70;
end;

::
::   Tuples for n=6
::

definition
  let x1,x2,x3,x4,x5,x6;
  func [x1,x2,x3,x4,x5,x6] equals

  [[x1,x2,x3,x4,x5],x6];
  correctness;
end;

theorem Th3:
  [x1,x2,x3,x4,x5,x6] = [[[[[x1,x2],x3],x4],x5],x6]
proof
  thus [x1,x2,x3,x4,x5,x6] = [[[x1,x2,x3,x4],x5],x6] by MCART_2:def 1
    .= [[[[x1,x2,x3],x4],x5],x6] by MCART_1:def 4
    .= [[[[[x1,x2],x3],x4],x5],x6] by MCART_1:def 3;
end;

theorem
  [x1,x2,x3,x4,x5,x6] = [[x1,x2,x3,x4],x5,x6]
proof
  thus [x1,x2,x3,x4,x5,x6] = [[[[[x1,x2],x3],x4],x5],x6] by Th3
    .= [[[[x1,x2],x3],x4],x5,x6] by MCART_1:def 3
    .= [[x1,x2,x3,x4],x5,x6] by MCART_1:31;
end;

theorem
  [x1,x2,x3,x4,x5,x6] = [[x1,x2,x3],x4,x5,x6]
proof
  thus [x1,x2,x3,x4,x5,x6] = [[[[[x1,x2],x3],x4],x5],x6] by Th3
    .= [[[x1,x2],x3],x4,x5,x6] by MCART_1:31
    .= [[x1,x2,x3],x4,x5,x6] by MCART_1:def 3;
end;

theorem Th6:
  [x1,x2,x3,x4,x5,x6] = [[x1,x2],x3,x4,x5,x6]
proof
  thus [x1,x2,x3,x4,x5,x6] = [[[[[x1,x2],x3],x4],x5],x6] by Th3
    .= [[[x1,x2],x3],x4,x5,x6] by MCART_1:31
    .= [[x1,x2],x3,x4,x5,x6] by MCART_2:6;
end;

theorem Th7:
  [x1,x2,x3,x4,x5,x6] = [y1,y2,y3,y4,y5,y6]
  implies x1 = y1 & x2 = y2 & x3 = y3 & x4 = y4 & x5 = y5 & x6 = y6
proof
  assume [x1,x2,x3,x4,x5,x6] = [y1,y2,y3,y4,y5,y6];
  then [x1,x2,x3,x4,x5] = [y1,y2,y3,y4,y5] & x6 = y6 by ZFMISC_1:33;
  hence thesis by MCART_2:7;
end;

theorem Th8:
  X <> {} implies ex v st v in X &
  not ex x1,x2,x3,x4,x5,x6 st (x1 in X or x2 in X) & v = [x1,x2,x3,x4,x5,x6]
proof
  assume X <> {};
  then consider Y such that
A1: Y in X and
A2: for Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8,Y9
  st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7
  in Y8 & Y8 in Y9 & Y9 in Y holds Y1 misses X by Th2;
  take v = Y;
  thus v in X by A1;
  given x1,x2,x3,x4,x5,x6 such that
A3: x1 in X or x2 in X and
A4: v = [x1,x2,x3,x4,x5,x6];
  set Y1 = { x1, x2 }, Y2 = { Y1, {x1} }, Y3 = { Y2, x3 }, Y4 = { Y3, {Y2} },
  Y5 = { Y4, x4 }, Y6 = { Y5, {Y4} }, Y7 = { Y6, x5 }, Y8 = { Y7, {Y6} },
  Y9 = { Y8, x6 };
  x1 in Y1 & x2 in Y1 by TARSKI:def 2;
  then
A5: not Y1 misses X by A3,XBOOLE_0:3;
  Y = [[[[[x1,x2],x3],x4],x5],x6] by A4,Th3
    .= [[[[ Y2,x3],x4],x5],x6 ] by TARSKI:def 5
    .= [[[ Y4,x4],x5],x6 ] by TARSKI:def 5
    .= [[ Y6,x5 ],x6] by TARSKI:def 5
    .= [ Y8,x6 ] by TARSKI:def 5
    .= { Y9, { Y8 } } by TARSKI:def 5;
  then Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7 in
  Y8 & Y8 in Y9 & Y9 in Y by TARSKI:def 2;
  hence contradiction by A2,A5;
end;

::
::   Cartesian products of six sets
::

definition
  let X1,X2,X3,X4,X5,X6;
  func [:X1,X2,X3,X4,X5,X6:] -> set equals

  [:[: X1,X2,X3,X4,X5 :],X6 :];
  coherence;
end;

theorem Th9:
  [:X1,X2,X3,X4,X5,X6:] = [:[:[:[:[:X1,X2:],X3:],X4:],X5:],X6:]
proof
  thus [:X1,X2,X3,X4,X5,X6:] = [:[:[:X1,X2,X3,X4:],X5:],X6:] by MCART_2:def 2
    .= [:[:[:[:X1,X2,X3:],X4:],X5:],X6:] by ZFMISC_1:def 4
    .= [:[:[:[:[:X1,X2:],X3:],X4:],X5:],X6:] by ZFMISC_1:def 3;
end;

theorem
  [:X1,X2,X3,X4,X5,X6:] = [:[:X1,X2,X3,X4:],X5,X6:]
proof
  thus [:X1,X2,X3,X4,X5,X6:] = [:[:[:[:[:X1,X2:],X3:],X4:],X5:],X6:] by Th9
    .= [:[:[:[:X1,X2:],X3:],X4:],X5,X6:] by ZFMISC_1:def 3
    .= [:[:X1,X2,X3,X4:],X5,X6:] by MCART_1:53;
end;

theorem
  [:X1,X2,X3,X4,X5,X6:] = [:[:X1,X2,X3:],X4,X5,X6:]
proof
  thus [:X1,X2,X3,X4,X5,X6:] = [:[:[:[:[:X1,X2:],X3:],X4:],X5:],X6:] by Th9
    .= [:[:[:X1,X2:],X3:],X4,X5,X6:] by MCART_1:53
    .= [:[:X1,X2,X3:],X4,X5,X6:] by ZFMISC_1:def 3;
end;

theorem Th12:
  [:X1,X2,X3,X4,X5,X6:] = [:[:X1,X2:],X3,X4,X5,X6:]
proof
  thus [:X1,X2,X3,X4,X5,X6:] = [:[:[:[:[:X1,X2:],X3:],X4:],X5:],X6:] by Th9
    .= [:[:[:X1,X2:],X3:],X4,X5,X6:] by MCART_1:53
    .= [:[:X1,X2:],X3,X4,X5,X6:] by MCART_2:12;
end;

theorem Th13:
  X1 <> {} & X2 <> {} & X3 <> {} & X4 <> {} & X5 <> {} & X6 <> {}
  iff [:X1,X2,X3,X4,X5,X6:] <> {}
proof
  X1 <> {} & X2 <> {} & X3 <> {} & X4 <> {} & X5 <> {}
  iff [:X1,X2,X3,X4,X5:] <> {} by MCART_2:13;
  hence thesis by ZFMISC_1:113;
end;

theorem Th14:
  X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} implies
  ( [:X1,X2,X3,X4,X5,X6:] = [:Y1,Y2,Y3,Y4,Y5,Y6:]
  implies X1=Y1 & X2=Y2 & X3=Y3 & X4=Y4 & X5=Y5 & X6=Y6 )
proof
  assume
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{};
  then
A2: [:X1,X2,X3,X4,X5:] <> {} by MCART_2:13;
  assume
A3: X6<>{};
  assume [:X1,X2,X3,X4,X5,X6:] = [:Y1,Y2,Y3,Y4,Y5,Y6:];
  then [:X1,X2,X3,X4,X5:] = [:Y1,Y2,Y3,Y4,Y5:] & X6 = Y6 by A2,A3,ZFMISC_1:134;
  hence thesis by A1,MCART_2:14;
end;

theorem
  [:X1,X2,X3,X4,X5,X6:]<>{} & [:X1,X2,X3,X4,X5,X6:] = [:Y1,Y2,Y3,Y4,Y5,Y6:]
  implies X1=Y1 & X2=Y2 & X3=Y3 & X4=Y4 & X5=Y5 & X6=Y6
proof
  assume [:X1,X2,X3,X4,X5,X6:]<>{};
  then X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} by Th13;
  hence thesis by Th14;
end;

theorem
  [:X,X,X,X,X,X:] = [:Y,Y,Y,Y,Y,Y:] implies X = Y
proof
  assume [:X,X,X,X,X,X:] = [:Y,Y,Y,Y,Y,Y:];
  then X<>{} or Y<>{} implies thesis by Th14;
  hence X = Y;
end;

theorem Th17:
  X1 <> {} & X2 <> {} & X3 <> {} & X4 <> {} & X5 <> {} & X6 <> {} implies
  for x being Element of [:X1,X2,X3,X4,X5,X6:]
  ex xx1,xx2,xx3,xx4,xx5,xx6 st x = [xx1,xx2,xx3,xx4,xx5,xx6]
proof
  assume
A1: X1 <> {} & X2 <> {} & X3 <> {} & X4 <> {} & X5 <> {} & X6 <> {};
  then
A2: [:X1,X2,X3,X4,X5:] <> {} by MCART_2:13;
  let x be Element of [:X1,X2,X3,X4,X5,X6:];
  reconsider x'=x as Element of [:[:X1,X2,X3,X4,X5:],X6:];
  consider x12345 being (Element of [:X1,X2,X3,X4,X5:]), xx6 such that
A3: x' = [x12345,xx6] by A1,A2,MCART_2:36;
  consider xx1,xx2,xx3,xx4,xx5 such that
A4: x12345 = [xx1,xx2,xx3,xx4,xx5] by A1,MCART_2:17;
  take xx1,xx2,xx3,xx4,xx5,xx6;
  thus x = [xx1,xx2,xx3,xx4,xx5,xx6] by A3,A4;
end;

definition
  let X1,X2,X3,X4,X5,X6;
  assume
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{};
  let x be Element of [:X1,X2,X3,X4,X5,X6:];
  func x`1 -> Element of X1 means
  :Def3:
  x = [x1,x2,x3,x4,x5,x6] implies it = x1;
  existence
  proof
    consider xx1,xx2,xx3,xx4,xx5,xx6 such that
A2: x = [xx1,xx2,xx3,xx4,xx5,xx6] by A1,Th17;
    take xx1;
    thus thesis by A2,Th7;
  end;
  uniqueness
  proof
    consider xx1,xx2,xx3,xx4,xx5,xx6 such that
A3: x = [xx1,xx2,xx3,xx4,xx5,xx6] by A1,Th17;
    let y,z be Element of X1;
    assume x = [x1,x2,x3,x4,x5,x6] implies y = x1;
    then
A4: y = xx1 by A3;
    assume x = [x1,x2,x3,x4,x5,x6] implies z = x1;
    hence thesis by A3,A4;
  end;
  func x`2 -> Element of X2 means
  :Def4:
  x = [x1,x2,x3,x4,x5,x6] implies it = x2;
  existence
  proof
    consider xx1,xx2,xx3,xx4,xx5,xx6 such that
A5: x = [xx1,xx2,xx3,xx4,xx5,xx6] by A1,Th17;
    take xx2; thus thesis by A5,Th7;
  end;
  uniqueness
  proof
    consider xx1,xx2,xx3,xx4,xx5,xx6 such that
A6: x = [xx1,xx2,xx3,xx4,xx5,xx6] by A1,Th17;
    let y,z be Element of X2;
    assume x = [x1,x2,x3,x4,x5,x6] implies y = x2;
    then
A7: y = xx2 by A6;
    assume x = [x1,x2,x3,x4,x5,x6] implies z = x2;
    hence thesis by A6,A7;
  end;
  func x`3 -> Element of X3 means
  :Def5:
  x = [x1,x2,x3,x4,x5,x6] implies it = x3;
  existence
  proof
    consider xx1,xx2,xx3,xx4,xx5,xx6 such that
A8: x = [xx1,xx2,xx3,xx4,xx5,xx6] by A1,Th17;
    take xx3; thus thesis by A8,Th7;
  end;
  uniqueness
  proof
    consider xx1,xx2,xx3,xx4,xx5,xx6 such that
A9: x = [xx1,xx2,xx3,xx4,xx5,xx6] by A1,Th17;
    let y,z be Element of X3;
    assume x = [x1,x2,x3,x4,x5,x6] implies y = x3;
    then
A10: y = xx3 by A9;
    assume x = [x1,x2,x3,x4,x5,x6] implies z = x3;
    hence thesis by A9,A10;
  end;
  func x`4 -> Element of X4 means
  :Def6:
  x = [x1,x2,x3,x4,x5,x6] implies it = x4;
  existence
  proof
    consider xx1,xx2,xx3,xx4,xx5,xx6 such that
A11: x = [xx1,xx2,xx3,xx4,xx5,xx6] by A1,Th17;
    take xx4; thus thesis by A11,Th7;
  end;
  uniqueness
  proof
    consider xx1,xx2,xx3,xx4,xx5,xx6 such that
A12: x = [xx1,xx2,xx3,xx4,xx5,xx6] by A1,Th17;
    let y,z be Element of X4;
    assume x = [x1,x2,x3,x4,x5,x6] implies y = x4;
    then
A13: y = xx4 by A12;
    assume x = [x1,x2,x3,x4,x5,x6] implies z = x4;
    hence thesis by A12,A13;
  end;
  func x`5 -> Element of X5 means
  :Def7:
  x = [x1,x2,x3,x4,x5,x6] implies it = x5;
  existence
  proof
    consider xx1,xx2,xx3,xx4,xx5,xx6 such that
A14: x = [xx1,xx2,xx3,xx4,xx5,xx6] by A1,Th17;
    take xx5; thus thesis by A14,Th7;
  end;
  uniqueness
  proof
    consider xx1,xx2,xx3,xx4,xx5,xx6 such that
A15: x = [xx1,xx2,xx3,xx4,xx5,xx6] by A1,Th17;
    let y,z be Element of X5;
    assume x = [x1,x2,x3,x4,x5,x6] implies y = x5;
    then
A16: y = xx5 by A15;
    assume x = [x1,x2,x3,x4,x5,x6] implies z = x5;
    hence thesis by A15,A16;
  end;
  func x`6 -> Element of X6 means
  :Def8:
  x = [x1,x2,x3,x4,x5,x6] implies it = x6;
  existence
  proof
    consider xx1,xx2,xx3,xx4,xx5,xx6 such that
A17: x = [xx1,xx2,xx3,xx4,xx5,xx6] by A1,Th17;
    take xx6; thus thesis by A17,Th7;
  end;
  uniqueness
  proof
    consider xx1,xx2,xx3,xx4,xx5,xx6 such that
A18: x = [xx1,xx2,xx3,xx4,xx5,xx6] by A1,Th17;
    let y,z be Element of X6;
    assume x = [x1,x2,x3,x4,x5,x6] implies y = x6;
    then
A19: y = xx6 by A18;
    assume x = [x1,x2,x3,x4,x5,x6] implies z = x6;
    hence thesis by A18,A19;
  end;
end;

theorem
  X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} implies
  for x being Element of [:X1,X2,X3,X4,X5,X6:]
  for x1,x2,x3,x4,x5,x6 st x = [x1,x2,x3,x4,x5,x6] holds
  x`1 = x1 & x`2 = x2 & x`3 = x3 & x`4 = x4 & x`5 = x5 & x`6 = x6
  by Def3,Def4,Def5,Def6,Def7,Def8;

theorem Th19:
  X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} implies
  for x being Element of [:X1,X2,X3,X4,X5,X6:]
  holds x = [x`1,x`2,x`3,x`4,x`5,x`6]
proof
  assume
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{};
  let x be Element of [:X1,X2,X3,X4,X5,X6:];
  consider xx1,xx2,xx3,xx4,xx5,xx6 such that
A2: x = [xx1,xx2,xx3,xx4,xx5,xx6] by A1,Th17;
  thus x = [x`1,xx2,xx3,xx4,xx5,xx6] by A1,A2,Def3
    .= [x`1,x`2,xx3,xx4,xx5,xx6] by A1,A2,Def4
    .= [x`1,x`2,x`3,xx4,xx5,xx6] by A1,A2,Def5
    .= [x`1,x`2,x`3,x`4,xx5,xx6] by A1,A2,Def6
    .= [x`1,x`2,x`3,x`4,x`5,xx6] by A1,A2,Def7
    .= [x`1,x`2,x`3,x`4,x`5,x`6] by A1,A2,Def8;
end;

theorem Th20:
  X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} implies
  for x being Element of [:X1,X2,X3,X4,X5,X6:] holds
  x`1 = (x qua set)`1`1`1`1`1 & x`2 = (x qua set)`1`1`1`1`2 &
  x`3 = (x qua set)`1`1`1`2 & x`4 = (x qua set)`1`1`2 &
  x`5 = (x qua set)`1`2 & x`6 = (x qua set)`2
proof
  assume
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{};
  let x be Element of [:X1,X2,X3,X4,X5,X6:];
  thus x`1 = [ x`1, x`2]`1 by MCART_1:7
    .= [[x`1, x`2],x`3]`1`1 by MCART_1:7
    .= [ x`1, x`2 ,x`3]`1`1 by MCART_1:def 3
    .= [[x`1, x`2 ,x`3],x`4]`1`1`1 by MCART_1:7
    .= [ x`1, x`2 ,x`3 ,x`4]`1`1`1 by MCART_1:def 4
    .= [[x`1, x`2 ,x`3 ,x`4], x`5]`1`1`1`1 by MCART_1:7
    .= [ x`1, x`2 ,x`3 ,x`4 , x`5]`1`1`1`1 by MCART_2:def 1
    .= [ x`1, x`2 ,x`3 ,x`4 ,x`5, x`6]`1`1`1`1`1 by MCART_1:7
    .= (x qua set)`1`1`1`1`1 by A1,Th19;
  thus x`2 = [ x`1, x`2]`2 by MCART_1:7
    .= [[x`1, x`2],x`3]`1`2 by MCART_1:7
    .= [ x`1, x`2, x`3]`1`2 by MCART_1:def 3
    .= [[x`1, x`2, x`3],x`4]`1`1`2 by MCART_1:7
    .= [ x`1, x`2, x`3, x`4]`1`1`2 by MCART_1:def 4
    .= [[x`1, x`2, x`3, x`4 ], x`5]`1`1`1`2 by MCART_1:7
    .= [ x`1, x`2, x`3, x`4 , x`5]`1`1`1`2 by MCART_2:def 1
    .= [ x`1, x`2, x`3, x`4, x`5, x`6]`1`1`1`1`2 by MCART_1:7
    .= (x qua set)`1`1`1`1`2 by A1,Th19;
  thus x`3 = [[x`1, x`2],x`3]`2 by MCART_1:7
    .= [ x`1, x`2, x`3]`2 by MCART_1:def 3
    .= [[x`1, x`2, x`3],x`4]`1`2 by MCART_1:7
    .= [ x`1, x`2, x`3, x`4]`1`2 by MCART_1:def 4
    .= [[x`1, x`2, x`3, x`4],x`5]`1`1`2 by MCART_1:7
    .= [ x`1, x`2, x`3, x`4 ,x`5]`1`1`2 by MCART_2:def 1
    .= [ x`1, x`2, x`3, x`4, x`5, x`6]`1`1`1`2 by MCART_1:7
    .= (x qua set)`1`1`1`2 by A1,Th19;
  thus x`4 = [[x`1,x`2,x`3],x`4]`2 by MCART_1:7
    .= [ x`1,x`2,x`3, x`4]`2 by MCART_1:def 4
    .= [[x`1,x`2,x`3, x`4],x`5]`1`2 by MCART_1:7
    .= [ x`1,x`2,x`3, x`4 ,x`5]`1`2 by MCART_2:def 1
    .= [ x`1,x`2,x`3, x`4, x`5, x`6]`1`1`2 by MCART_1:7
    .= (x qua set)`1`1`2 by A1,Th19;
  thus x`5 = [[x`1,x`2,x`3,x`4],x`5]`2 by MCART_1:7
    .= [ x`1,x`2,x`3,x`4 ,x`5]`2 by MCART_2:def 1
    .= [ x`1,x`2,x`3,x`4,x`5, x`6]`1`2 by MCART_1:7
    .= (x qua set)`1`2 by A1,Th19;
  thus x`6 = [ x`1,x`2,x`3,x`4,x`5, x`6]`2 by MCART_1:7
    .= (x qua set)`2 by A1,Th19;
end;

theorem Th21:
  X1 c= [:X1,X2,X3,X4,X5,X6:] or X1 c= [:X2,X3,X4,X5,X6,X1:]
  or X1 c= [:X3,X4,X5,X6,X1,X2:] or X1 c= [:X4,X5,X6,X1,X2,X3:]
  or X1 c= [:X5,X6,X1,X2,X3,X4:] or X1 c= [:X6,X1,X2,X3,X4,X5:]
  implies X1 = {}
proof
  assume that
A1: X1 c= [:X1,X2,X3,X4,X5,X6:] or X1 c= [:X2,X3,X4,X5,X6,X1:]
  or X1 c= [:X3,X4,X5,X6,X1,X2:] or X1 c= [:X4,X5,X6,X1,X2,X3:]
  or X1 c= [:X5,X6,X1,X2,X3,X4:] or X1 c= [:X6,X1,X2,X3,X4,X5:] and
A2: X1 <> {};
  [:X1,X2,X3,X4,X5,X6:]<>{} or [:X2,X3,X4,X5,X6,X1:]<>{}
  or [:X3,X4,X5,X6,X1,X2:]<>{} or [:X4,X5,X6,X1,X2,X3:]<>{}
  or [:X5,X6,X1,X2,X3,X4:]<>{}
  or [:X6,X1,X2,X3,X4,X5:]<>{} by A1,A2,XBOOLE_1:3;
  then
A3: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} by Th13;
  now per cases by A1;
    suppose
A4:   X1 c= [:X1,X2,X3,X4,X5,X6:];
      consider v such that
A5:   v in X1 and
A6:   for x1,x2,x3,x4,x5,x6
      st x1 in X1 or x2 in X1 holds v <> [x1,x2,x3,x4,x5,x6] by A2,Th8;
      reconsider v as Element of [:X1,X2,X3,X4,X5,X6:] by A4,A5;
      v = [v`1,v`2,v`3,v`4,v`5,v`6] & v`1 in X1 by A3,Th19;
      hence contradiction by A6;
    end;
    suppose X1 c= [:X2,X3,X4,X5,X6,X1:];
      then X1 c= [:[:X2,X3:],X4,X5,X6,X1:] by Th12;
      hence thesis by A2,MCART_2:21;
    end;
    suppose X1 c= [:X3,X4,X5,X6,X1,X2:];
      then X1 c= [:[:X3,X4:],X5,X6,X1,X2:] by Th12;
      hence thesis by A2,MCART_2:21;
    end;
    suppose X1 c= [:X4,X5,X6,X1,X2,X3:];
      then X1 c= [:[:X4,X5:],X6,X1,X2,X3:] by Th12;
      hence thesis by A2,MCART_2:21;
    end;
    suppose X1 c= [:X5,X6,X1,X2,X3,X4:];
      then X1 c= [:[:X5,X6:],X1,X2,X3,X4:] by Th12;
      hence thesis by A2,MCART_2:21;
    end;
    suppose
A7:   X1 c= [:X6,X1,X2,X3,X4,X5:];
      consider v such that
A8:   v in X1 and
A9:   for x1,x2,x3,x4,x5,x6
      st x1 in X1 or x2 in X1 holds v <> [x1,x2,x3,x4,x5,x6] by A2,Th8;
      reconsider v as Element of [:X6,X1,X2,X3,X4,X5:] by A7,A8;
      v = [v`1,v`2,v`3,v`4,v`5,v`6] & v`2 in X1 by A3,Th19;
      hence thesis by A9;
    end;
  end;
  hence contradiction;
end;

theorem
  [:X1,X2,X3,X4,X5,X6:] meets [:Y1,Y2,Y3,Y4,Y5,Y6:] implies
  X1 meets Y1 & X2 meets Y2 & X3 meets Y3 & X4 meets Y4 & X5 meets Y5
  & X6 meets Y6
proof
A1: [:[:[:[:[:X1,X2:],X3:],X4:],X5:],X6:] = [:X1,X2,X3,X4,X5,X6:]
  & [:[:[:[:[:Y1,Y2:],Y3:],Y4:],Y5:],Y6:] = [:Y1,Y2,Y3,Y4,Y5,Y6:] by Th9;
  assume [:X1,X2,X3,X4,X5,X6:] meets [:Y1,Y2,Y3,Y4,Y5,Y6:];
  then [:[:[:[:X1,X2:],X3:],X4:],X5:] meets [:[:[:[:Y1,Y2:],Y3:],Y4:],Y5:]
  & X6 meets Y6 by A1,ZFMISC_1:127;
  then [:[:[:X1,X2:],X3:],X4:] meets [:[:[:Y1,Y2:],Y3:],Y4:]
  & X5 meets Y5 & X6 meets Y6 by ZFMISC_1:127;
  then [:[:X1,X2:],X3:] meets [:[:Y1,Y2:],Y3:]
  & X4 meets Y4 & X5 meets Y5 & X6 meets Y6 by ZFMISC_1:127;
  then [:X1,X2:] meets [:Y1,Y2:] & X3 meets Y3
  & X4 meets Y4 & X5 meets Y5 & X6 meets Y6 by ZFMISC_1:127;
  hence thesis by ZFMISC_1:127;
end;

theorem Th23:
  [:{x1},{x2},{x3},{x4},{x5},{x6}:] = { [x1,x2,x3,x4,x5,x6] }
proof thus [:{x1},{x2},{x3},{x4},{x5},{x6}:]
  = [:[:{x1},{x2}:],{x3},{x4},{x5},{x6}:] by Th12
    .= [:{[x1,x2]}, {x3},{x4},{x5},{x6}:] by ZFMISC_1:35
    .= { [[x1,x2], x3, x4, x5, x6]} by MCART_2:23
    .= { [x1,x2,x3,x4,x5,x6] } by Th6;
end;

reserve A1 for Subset of X1,
  A2 for Subset of X2,
  A3 for Subset of X3,
  A4 for Subset of X4,
  A5 for Subset of X5,
  A6 for Subset of X6,
  A7 for Subset of X7;

:: 6 - Tuples

reserve x for Element of [:X1,X2,X3,X4,X5,X6:];

theorem
  X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} implies
  for x1,x2,x3,x4,x5,x6 st x = [x1,x2,x3,x4,x5,x6]
  holds x`1 = x1 & x`2 = x2 & x`3 = x3 & x`4 = x4 & x`5 = x5
  & x`6 = x6 by Def3,Def4,Def5,Def6,Def7,Def8;

theorem
  X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} &
  (for xx1,xx2,xx3,xx4,xx5,xx6
  st x = [xx1,xx2,xx3,xx4,xx5,xx6] holds y1 = xx1) implies y1 =x`1
proof
  assume that
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} and
A2: for xx1,xx2,xx3,xx4,xx5,xx6
  st x = [xx1,xx2,xx3,xx4,xx5,xx6] holds y1 = xx1;
  x = [x`1,x`2,x`3,x`4,x`5,x`6] by A1,Th19;
  hence thesis by A2;
end;

theorem
  X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} &
  (for xx1,xx2,xx3,xx4,xx5,xx6
  st x = [xx1,xx2,xx3,xx4,xx5,xx6] holds y2 = xx2) implies y2 =x`2
proof
  assume that
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} and
A2: for xx1,xx2,xx3,xx4,xx5,xx6
  st x = [xx1,xx2,xx3,xx4,xx5,xx6] holds y2 = xx2;
  x = [x`1,x`2,x`3,x`4,x`5,x`6] by A1,Th19;
  hence thesis by A2;
end;

theorem
  X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} &
  (for xx1,xx2,xx3,xx4,xx5,xx6
  st x = [xx1,xx2,xx3,xx4,xx5,xx6] holds y3 = xx3) implies y3 =x`3
proof
  assume that
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} and
A2: for xx1,xx2,xx3,xx4,xx5,xx6 st
  x = [xx1,xx2,xx3,xx4,xx5,xx6] holds y3 = xx3;
  x = [x`1,x`2,x`3,x`4,x`5,x`6] by A1,Th19;
  hence thesis by A2;
end;

theorem
  X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} &
  (for xx1,xx2,xx3,xx4,xx5,xx6
  st x = [xx1,xx2,xx3,xx4,xx5,xx6] holds y4 = xx4) implies y4 =x`4
proof
  assume that
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} and
A2: for xx1,xx2,xx3,xx4,xx5,xx6
  st x = [xx1,xx2,xx3,xx4,xx5,xx6] holds y4 = xx4;
  x = [x`1,x`2,x`3,x`4,x`5,x`6] by A1,Th19;
  hence thesis by A2;
end;

theorem
  X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} &
  (for xx1,xx2,xx3,xx4,xx5,xx6
  st x = [xx1,xx2,xx3,xx4,xx5,xx6] holds y5 = xx5) implies y5 =x`5
proof
  assume that
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} and
A2: for xx1,xx2,xx3,xx4,xx5,xx6
  st x = [xx1,xx2,xx3,xx4,xx5,xx6] holds y5 = xx5;
  x = [x`1,x`2,x`3,x`4,x`5,x`6] by A1,Th19;
  hence thesis by A2;
end;

theorem
  X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} &
  (for xx1,xx2,xx3,xx4,xx5,xx6
  st x = [xx1,xx2,xx3,xx4,xx5,xx6] holds y6 = xx6) implies y6 =x`6
proof
  assume that
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} and
A2: for xx1,xx2,xx3,xx4,xx5,xx6
  st x = [xx1,xx2,xx3,xx4,xx5,xx6] holds y6 = xx6;
  x = [x`1,x`2,x`3,x`4,x`5,x`6] by A1,Th19;
  hence thesis by A2;
end;

theorem Th31:
  z in [: X1,X2,X3,X4,X5,X6 :] implies ex x1,x2,x3,x4,x5,x6
  st x1 in X1 & x2 in X2 & x3 in X3 & x4 in X4 & x5 in X5 & x6 in X6
  & z = [x1,x2,x3,x4,x5,x6]
proof
  assume z in [: X1,X2,X3,X4,X5,X6 :];
  then consider x12345, x6 being set such that
A1: x12345 in [:X1,X2,X3,X4,X5:] and
A2: x6 in X6 and
A3: z = [x12345,x6] by ZFMISC_1:def 2;
  consider x1, x2, x3, x4, x5 such that
A4: x1 in X1 & x2 in X2 & x3 in X3 & x4 in X4 & x5 in X5
  & x12345 = [x1,x2,x3,x4,x5] by A1,MCART_2:30;
  z = [x1,x2,x3,x4,x5,x6] by A3,A4;
  hence thesis by A2,A4;
end;

theorem Th32:
  [x1,x2,x3,x4,x5,x6] in [: X1,X2,X3,X4,X5,X6 :]
  iff x1 in X1 & x2 in X2 & x3 in X3 & x4 in X4 & x5 in X5 & x6 in X6
proof
A1: [:X1,X2,X3,X4,X5,X6:] = [:[:X1,X2:],X3,X4,X5,X6:] by Th12;
A2: [x1,x2,x3,x4,x5,x6] = [[x1,x2],x3,x4,x5,x6] by Th6;
  [x1,x2] in [:X1,X2:] iff x1 in X1 & x2 in X2 by ZFMISC_1:106;
  hence thesis by A1,A2,MCART_2:31;
end;

theorem
  (for z holds z in Z iff ex x1,x2,x3,x4,x5,x6
  st x1 in X1 & x2 in X2 & x3 in X3 & x4 in X4 & x5 in X5 & x6 in X6
  & z = [x1,x2,x3,x4,x5,x6]) implies Z = [: X1,X2,X3,X4,X5,X6 :]
proof
  assume
A1: for z holds z in Z iff ex x1,x2,x3,x4,x5,x6
  st x1 in X1 & x2 in X2 & x3 in X3 & x4 in X4 & x5 in X5 & x6 in X6
  & z = [x1,x2,x3,x4,x5,x6];
  now
    let z;
    thus z in Z implies z in [:[:X1,X2,X3,X4,X5:],X6:]
    proof
      assume z in Z;
      then consider x1,x2,x3,x4,x5,x6 such that
A2:   x1 in X1 & x2 in X2 & x3 in X3 & x4 in X4 & x5 in X5 & x6 in X6
      & z = [x1,x2,x3,x4,x5,x6] by A1;
      [x1,x2,x3,x4,x5] in [:X1,X2,X3,X4,X5:] & x6 in X6 by A2,MCART_2:31;
      hence z in [:[:X1,X2,X3,X4,X5:],X6:] by A2,ZFMISC_1:def 2;
    end;
    assume z in [:[:X1,X2,X3,X4,X5:],X6:];
    then consider x12345,x6 being set such that
A3: x12345 in [:X1,X2,X3,X4,X5:] and
A4: x6 in X6 and
A5: z = [x12345,x6] by ZFMISC_1:def 2;
    consider x1,x2,x3,x4,x5 such that
A6: x1 in X1 & x2 in X2 & x3 in X3 & x4 in X4 & x5 in X5
    & x12345 = [x1,x2,x3,x4,x5] by A3,MCART_2:30;
    z = [x1,x2,x3,x4,x5,x6] by A5,A6;
    hence z in Z by A1,A4,A6;
  end;
  hence Z = [: X1,X2,X3,X4,X5,X6 :] by TARSKI:2;
end;

theorem Th34:
  X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{}
  & Y1<>{} & Y2<>{} & Y3<>{} & Y4<>{} & Y5<>{} & Y6<>{} implies
  for x being (Element of [:X1,X2,X3,X4,X5,X6:]),
  y being Element of [:Y1,Y2,Y3,Y4,Y5,Y6:] holds x = y
  implies x`1 = y`1 & x`2 = y`2 & x`3 = y`3 & x`4 = y`4 & x`5 = y`5
  & x`6 = y`6
proof
  assume that
A1: X1 <> {} & X2 <> {} & X3 <> {} & X4 <> {} & X5 <> {} & X6 <> {} and
A2: Y1 <> {} & Y2 <> {} & Y3 <> {} & Y4 <> {} & Y5 <> {} & Y6 <> {};
  let x be Element of [:X1,X2,X3,X4,X5,X6:];
  let y be Element of [:Y1,Y2,Y3,Y4,Y5,Y6:];
  assume
A3: x = y;
  thus x`1 = (x qua set)`1`1`1`1`1 by A1,Th20
    .= y`1 by A2,A3,Th20;
  thus x`2 = (x qua set)`1`1`1`1`2 by A1,Th20
    .= y`2 by A2,A3,Th20;
  thus x`3 = (x qua set)`1`1`1`2 by A1,Th20
    .= y`3 by A2,A3,Th20;
  thus x`4 = (x qua set)`1`1`2 by A1,Th20
    .= y`4 by A2,A3,Th20;
  thus x`5 = (x qua set)`1`2 by A1,Th20
    .= y`5 by A2,A3,Th20;
  thus x`6 = (x qua set)`2 by A1,Th20
    .= y`6 by A2,A3,Th20;
end;

theorem
  for x being Element of [:X1,X2,X3,X4,X5,X6:] st x in [:A1,A2,A3,A4,A5,A6:]
  holds x`1 in A1 & x`2 in A2 & x`3 in A3 & x`4 in A4 & x`5 in A5 & x`6 in A6
proof
  let x be Element of [:X1,X2,X3,X4,X5,X6:];
  assume
A1: x in [:A1,A2,A3,A4,A5,A6:];
  then
A2: A1 <> {} & A2 <> {} & A3 <> {} & A4 <> {} & A5 <> {} & A6 <> {} by Th13;
  reconsider y = x as Element of [:A1,A2,A3,A4,A5,A6:] by A1;
  y`1 in A1 & y`2 in A2 & y`3 in A3 & y`4 in A4 & y`5 in A5 & y`6 in A6 by A2;
  hence x`1 in A1 & x`2 in A2 & x`3 in A3 & x`4 in A4 & x`5 in A5
  & x`6 in A6 by Th34;
end;

theorem Th36:
  X1 c= Y1 & X2 c= Y2 & X3 c= Y3 & X4 c= Y4 & X5 c= Y5 & X6 c= Y6
  implies [:X1,X2,X3,X4,X5,X6:] c= [:Y1,Y2,Y3,Y4,Y5,Y6:]
proof
  assume X1 c= Y1 & X2 c= Y2 & X3 c= Y3 & X4 c= Y4 & X5 c= Y5;
  then
A1: [:X1,X2,X3,X4,X5:] c= [:Y1,Y2,Y3,Y4,Y5:] by MCART_2:35;
  assume X6 c= Y6;
  hence thesis by A1,ZFMISC_1:119;
end;

theorem
  [:A1,A2,A3,A4,A5,A6:] is Subset of [:X1,X2,X3,X4,X5,X6:] by Th36;

begin :: Original MCART_4

theorem
  X <> {} implies ex Y st Y in X & for Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8,Y9,YA
  st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7 in
  Y8 & Y8 in Y9 & Y9 in YA & YA in Y holds Y1 misses X
proof
  defpred P1[set] means ex Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8,Y9
  st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7
  & Y7 in Y8 & Y8 in Y9 & Y9 in $1 & Y1 meets X;
  consider Z1 such that
A1: for Y holds Y in Z1 iff Y in union X & P1[Y] from XBOOLE_0:sch 1;
  defpred P2[set] means ex Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8
  st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7
  & Y7 in Y8 & Y8 in $1 & Y1 meets X;
  consider Z2 such that
A2: for Y holds Y in Z2 iff Y in union union X & P2[Y] from XBOOLE_0:sch 1;
  defpred P3[set] means ex Y1,Y2,Y3,Y4,Y5,Y6,Y7
  st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7
  & Y7 in $1 & Y1 meets X;
  consider Z3 such that
A3: for Y holds Y in Z3 iff Y in union union union X & P3[Y] from
  XBOOLE_0:sch 1;
  defpred P4[set] means ex Y1,Y2,Y3,Y4,Y5,Y6
  st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 &
  Y6 in $1 & Y1 meets X;
  consider Z4 such that
A4: for Y holds Y in Z4 iff
  Y in union union union union X & P4[Y] from XBOOLE_0:sch 1;
  defpred P5[set] means ex Y1,Y2,Y3,Y4,Y5
  st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in $1 & Y1 meets X;
  consider Z5 such that
A5: for Y holds Y in Z5 iff
  Y in union union union union union X & P5[Y] from XBOOLE_0:sch 1;
  defpred P5[set] means
  ex Y1,Y2,Y3,Y4 st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in $1 & Y1 meets X;
  consider Z6 such that
A6: for Y holds Y in Z6 iff
  Y in union union union union union union X & P5[Y] from XBOOLE_0:sch 1;
  defpred P6[set] means
  ex Y1,Y2,Y3 st Y1 in Y2 & Y2 in Y3 & Y3 in $1 & Y1 meets X;
  consider Z7 such that
A7: for Y holds Y in Z7 iff
  Y in union union union union union union union X & P6[Y]
  from XBOOLE_0:sch 1;
  defpred P7[set] means ex Y1,Y2 st Y1 in Y2 & Y2 in $1 & Y1 meets X;
  consider Z8 such that
A8: for Y holds Y in Z8 iff
  Y in union union union union union union union union X & P7[Y]
  from XBOOLE_0:sch 1;
  defpred P8[set] means ex Y1 st Y1 in $1 & Y1 meets X;
  consider Z9 such that
A9: for Y holds Y in Z9 iff
  Y in union union union union union union union union union X & P8[Y]
  from XBOOLE_0:sch 1;
  defpred P9[set] means $1 meets X;
  consider ZA such that
A10: for Y holds Y in ZA iff
  Y in union union union union union union union union union union X
  & P9[Y] from XBOOLE_0:sch 1;
  set V = X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/ ZA;
A11: V = X \/ (Z1 \/ Z2) \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/
  ZA by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3) \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/
  ZA by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4) \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/
  ZA by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5) \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/
  ZA by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6) \/ Z7 \/ Z8 \/ Z9 \/
  ZA by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7) \/ Z8 \/ Z9 \/
  ZA by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8) \/ Z9 \/
  ZA by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9) \/
  ZA by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/
  ZA) by XBOOLE_1:4;
  assume X <> {};
  then V <> {} by A11,XBOOLE_1:15;
  then consider Y such that
A12: Y in V and
A13: Y misses V by MCART_1:1;
  assume
A14: not thesis;
  now
    assume
A15: Y in X;
    then consider Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8,Y9,YA such that
A16: Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7 in
    Y8 & Y8 in Y9 & Y9 in YA & YA in Y & not Y1 misses X by A14;
    YA in union X & Y1 meets X by A15,A16,TARSKI:def 4;
    then YA in Z1 by A1,A16;
    then YA in X \/ Z1 by XBOOLE_0:def 2;
    then YA in X \/ Z1 \/ Z2 by XBOOLE_0:def 2;
    then YA in X \/ Z1 \/ Z2 \/ Z3 by XBOOLE_0:def 2;
    then YA in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 by XBOOLE_0:def 2;
    then YA in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_0:def 2;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by A16,XBOOLE_0:3;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
    Z8 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/
    Z9 by XBOOLE_1:70;
    hence contradiction by A13,XBOOLE_1:70;
  end;
  then Y in Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/
  ZA by A11,A12,XBOOLE_0:def 2;
  then Y in Z1 \/ (Z2 \/ Z3) \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/
  ZA by XBOOLE_1:4;
  then Y in Z1 \/ (Z2 \/ Z3 \/ Z4) \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/
  ZA by XBOOLE_1:4;
  then Y in Z1 \/ (Z2 \/ Z3 \/ Z4 \/ Z5) \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/
  ZA by XBOOLE_1:4;
  then Y in Z1 \/ (Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6) \/ Z7 \/ Z8 \/ Z9 \/
  ZA by XBOOLE_1:4;
  then Y in Z1 \/ (Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7) \/ Z8 \/ Z9 \/
  ZA by XBOOLE_1:4;
  then Y in Z1 \/ (Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8) \/ Z9 \/
  ZA by XBOOLE_1:4;
  then Y in Z1 \/ (Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9) \/
  ZA by XBOOLE_1:4;
  then
A17: Y in Z1 \/ (Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/
  ZA) by XBOOLE_1:4;
  now
    assume
A18: Y in Z1;
    then consider Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8,Y9 such that
A19: Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7 in
    Y8 & Y8 in Y9 & Y9 in Y & Y1 meets X by A1;
    Y in union X by A1,A18;
    then Y9 in union union X by A19,TARSKI:def 4;
    then Y9 in Z2 by A2,A19;
    then Y9 in X \/ Z1 \/ Z2 by XBOOLE_0:def 2;
    then Y meets X \/ Z1 \/ Z2 by A19,XBOOLE_0:3;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
    Z8 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/
    Z9 by XBOOLE_1:70;
    hence contradiction by A13,XBOOLE_1:70;
  end;
  then Y in Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/
  ZA by A17,XBOOLE_0:def 2;
  then Y in Z2 \/ (Z3 \/ Z4) \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/ ZA by XBOOLE_1:
  4;
  then Y in Z2 \/ (Z3 \/ Z4 \/ Z5) \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/ ZA by XBOOLE_1:
  4;
  then Y in Z2 \/ (Z3 \/ Z4 \/ Z5 \/ Z6) \/ Z7 \/ Z8 \/ Z9 \/ ZA by XBOOLE_1:
  4;
  then Y in Z2 \/ (Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7) \/ Z8 \/ Z9 \/ ZA by XBOOLE_1:
  4;
  then Y in Z2 \/ (Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8) \/ Z9 \/ ZA by XBOOLE_1:
  4;
  then Y in Z2 \/ (Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9) \/
  ZA by XBOOLE_1:4;
  then
A20: Y in Z2 \/ (Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/ ZA) by XBOOLE_1:4;
  now
    assume
A21: Y in Z2;
    then consider Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8 such that
A22: Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7 in
    Y8 & Y8 in Y & Y1 meets X by A2;
    Y in union union X by A2,A21;
    then Y8 in union union union X by A22,TARSKI:def 4;
    then Y8 in Z3 by A3,A22;
    then Y8 in X \/ Z1 \/ Z2 \/ Z3 by XBOOLE_0:def 2;
    then Y8 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 by XBOOLE_0:def 2;
    then Y8 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_0:def 2;
    then Y8 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_0:def 2;
    then Y8 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
    then Y8 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
    Z8 by XBOOLE_0:def 2;
    then Y8 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/
    Z9 by XBOOLE_0:def 2;
    then Y8 in V by XBOOLE_0:def 2;
    hence contradiction by A13,A22,XBOOLE_0:3;
  end;
  then Y in Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/ ZA by A20,XBOOLE_0:def
  2;
  then Y in Z3 \/ (Z4 \/ Z5) \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/ ZA by XBOOLE_1:4;
  then Y in Z3 \/ (Z4 \/ Z5 \/ Z6) \/ Z7 \/ Z8 \/ Z9 \/ ZA by XBOOLE_1:4;
  then Y in Z3 \/ (Z4 \/ Z5 \/ Z6 \/ Z7) \/ Z8 \/ Z9 \/ ZA by XBOOLE_1:4;
  then Y in Z3 \/ (Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8) \/ Z9 \/ ZA by XBOOLE_1:4;
  then Y in Z3 \/ (Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9) \/ ZA by XBOOLE_1:4;
  then
A23: Y in Z3 \/ (Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/ ZA) by XBOOLE_1:4;
  now
    assume
A24: Y in Z3;
    then consider Y1,Y2,Y3,Y4,Y5,Y6,Y7 such that
A25: Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7 in Y
    & Y1 meets X by A3;
    Y in union union union X by A3,A24;
    then Y7 in union union union union X by A25,TARSKI:def 4;
    then Y7 in Z4 by A4,A25;
    then Y7 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 by XBOOLE_0:def 2;
    then Y7 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_0:def 2;
    then Y7 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_0:def 2;
    then Y7 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
    then Y7 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
    Z8 by XBOOLE_0:def 2;
    then Y7 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/
    Z9 by XBOOLE_0:def 2;
    then Y7 in V by XBOOLE_0:def 2;
    hence contradiction by A13,A25,XBOOLE_0:3;
  end;
  then Y in Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/ ZA by A23,XBOOLE_0:def 2;
  then Y in Z4 \/ (Z5 \/ Z6) \/ Z7 \/ Z8 \/ Z9 \/ ZA by XBOOLE_1:4;
  then Y in Z4 \/ (Z5 \/ Z6 \/ Z7) \/ Z8 \/ Z9 \/ ZA by XBOOLE_1:4;
  then Y in Z4 \/ (Z5 \/ Z6 \/ Z7 \/ Z8) \/ Z9 \/ ZA by XBOOLE_1:4;
  then Y in Z4 \/ (Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9) \/ ZA by XBOOLE_1:4;
  then
A26: Y in Z4 \/ (Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/ ZA) by XBOOLE_1:4;
  now
    assume
A27: Y in Z4;
    then consider Y1,Y2,Y3,Y4,Y5,Y6 such that
A28: Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y
    & Y1 meets X by A4;
    Y in union union union union X by A4,A27;
    then Y6 in union union union union union X by A28,TARSKI:def 4;
    then Y6 in Z5 by A5,A28;
    then Y6 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_0:def 2;
    then Y6 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_0:def 2;
    then Y6 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
    then Y6 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
    Z8 by XBOOLE_0:def 2;
    then Y6 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/
    Z9 by XBOOLE_0:def 2;
    then Y6 in V by XBOOLE_0:def 2;
    hence contradiction by A13,A28,XBOOLE_0:3;
  end;
  then Y in Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/ ZA by A26,XBOOLE_0:def 2;
  then Y in Z5 \/ (Z6 \/ Z7) \/ Z8 \/ Z9 \/ ZA by XBOOLE_1:4;
  then Y in Z5 \/ (Z6 \/ Z7 \/ Z8) \/ Z9 \/ ZA by XBOOLE_1:4;
  then Y in Z5 \/ (Z6 \/ Z7 \/ Z8 \/ Z9) \/ ZA by XBOOLE_1:4;
  then
A29: Y in Z5 \/ (Z6 \/ Z7 \/ Z8 \/ Z9 \/ ZA) by XBOOLE_1:4;
  now
    assume
A30: Y in Z5;
    then consider Y1,Y2,Y3,Y4,Y5 such that
A31: Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y & Y1 meets X by A5;
    Y in union union union union union X by A5,A30;
    then Y5 in union union union union union union X by A31,TARSKI:def 4;
    then Y5 in Z6 by A6,A31;
    then Y5 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_0:def 2;
    then Y5 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
    then Y5 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
    Z8 by XBOOLE_0:def 2;
    then Y5 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/
    Z9 by XBOOLE_0:def 2;
    then Y5 in V by XBOOLE_0:def 2;
    hence contradiction by A13,A31,XBOOLE_0:3;
  end;
  then Y in Z6 \/ Z7 \/ Z8 \/ Z9 \/ ZA by A29,XBOOLE_0:def 2;
  then Y in Z6 \/ (Z7 \/ Z8) \/ Z9 \/ ZA by XBOOLE_1:4;
  then Y in Z6 \/ (Z7 \/ Z8 \/ Z9) \/ ZA by XBOOLE_1:4;
  then
A32: Y in Z6 \/ (Z7 \/ Z8 \/ Z9 \/ ZA) by XBOOLE_1:4;
  now
    assume
A33: Y in Z6;
    then consider Y1,Y2,Y3,Y4 such that
A34: Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y & Y1 meets X by A6;
    Y in union union union union union union X by A6,A33;
    then Y4 in
    union union union union union union union X by A34,TARSKI:def 4;
    then Y4 in Z7 by A7,A34;
    then Y4 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
    then Y4 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
    Z8 by XBOOLE_0:def 2;
    then Y4 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/
    Z9 by XBOOLE_0:def 2;
    then Y4 in V by XBOOLE_0:def 2;
    hence contradiction by A13,A34,XBOOLE_0:3;
  end;
  then Y in Z7 \/ Z8 \/ Z9 \/ ZA by A32,XBOOLE_0:def 2;
  then Y in Z7 \/ (Z8 \/ Z9) \/ ZA by XBOOLE_1:4;
  then
A35: Y in Z7 \/ (Z8 \/ Z9 \/ ZA) by XBOOLE_1:4;
  now
    assume
A36: Y in Z7;
    then consider Y1,Y2,Y3 such that
A37: Y1 in Y2 & Y2 in Y3 & Y3 in Y & Y1 meets X by A7;
    Y in union union union union union union union X by A7,A36;
    then Y3 in union union union union union union union union X
    by A37,TARSKI:def 4;
    then Y3 in Z8 by A8,A37;
    then Y3 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
    Z8 by XBOOLE_0:def 2;
    then Y3 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/
    Z9 by XBOOLE_0:def 2;
    then Y3 in V by XBOOLE_0:def 2;
    hence contradiction by A13,A37,XBOOLE_0:3;
  end;
  then Y in Z8 \/ Z9 \/ ZA by A35,XBOOLE_0:def 2;
  then
A38: Y in Z8 \/ (Z9 \/ ZA) by XBOOLE_1:4;
  now
    assume
A39: Y in Z8;
    then consider Y1,Y2 such that
A40: Y1 in Y2 & Y2 in Y & Y1 meets X by A8;
    Y in union union union union union union union union X by A8,A39;
    then Y2 in union union union union union union union union union X
    by A40,TARSKI:def 4;
    then Y2 in Z9 by A9,A40;
    then Y2 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/
    Z9 by XBOOLE_0:def 2;
    then Y2 in V by XBOOLE_0:def 2;
    hence contradiction by A13,A40,XBOOLE_0:3;
  end;
  then
A41: Y in Z9 \/ ZA by A38,XBOOLE_0:def 2;
  now
    assume
A42: Y in Z9;
    then consider Y1 such that
A43: Y1 in Y & Y1 meets X by A9;
    Y in union union union union union union union union union X by A9, A42;
    then Y1 in union union union union union union union union union union X
    by A43,TARSKI:def 4;
    then Y1 in ZA by A10,A43;
    then Y1 in V by XBOOLE_0:def 2;
    hence contradiction by A13,A43,XBOOLE_0:3;
  end;
  then Y in ZA by A41,XBOOLE_0:def 2;
  then Y meets X by A10;
  hence contradiction by A11,A13,XBOOLE_1:70;
end;

theorem Th39:
  X <> {} implies ex Y st Y in X & for Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8,Y9,YA,YB
  st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7 in
  Y8 & Y8 in Y9 & Y9 in YA & YA in YB & YB in Y holds Y1 misses X
proof
  defpred P1[set] means ex Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8,Y9,YA
  st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7
  & Y7 in Y8 & Y8 in Y9 & Y9 in YA & YA in $1 & Y1 meets X;
  consider Z1 such that
A1: for Y holds Y in Z1 iff Y in union X & P1[Y] from XBOOLE_0:sch 1;
  defpred P2[set] means ex Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8,Y9
  st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7
  & Y7 in Y8 & Y8 in Y9 & Y9 in $1 & Y1 meets X;
  consider Z2 such that
A2: for Y holds Y in Z2 iff Y in union union X & P2[Y] from XBOOLE_0:sch 1;
  defpred P3[set] means ex Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8
  st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7
  & Y7 in Y8 & Y8 in $1 & Y1 meets X;
  consider Z3 such that
A3: for Y holds Y in Z3 iff
  Y in union union union X & P3[Y] from XBOOLE_0:sch 1;
  defpred P4[set] means ex Y1,Y2,Y3,Y4,Y5,Y6,Y7
  st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7
  & Y7 in $1 & Y1 meets X;
  consider Z4 such that
A4: for Y holds Y in Z4 iff
  Y in union union union union X & P4[Y] from XBOOLE_0:sch 1;
  defpred P5[set] means ex Y1,Y2,Y3,Y4,Y5,Y6
  st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in $1 &
  Y1 meets X;
  consider Z5 such that
A5: for Y holds Y in Z5 iff
  Y in union union union union union X & P5[Y] from XBOOLE_0:sch 1;
  defpred P6[set] means ex Y1,Y2,Y3,Y4,Y5
  st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in $1 & Y1 meets X;
  consider Z6 such that
A6: for Y holds Y in Z6 iff
  Y in union union union union union union X & P6[Y] from XBOOLE_0:sch 1;
  defpred P7[set] means
  ex Y1,Y2,Y3,Y4 st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in $1 & Y1 meets X;
  consider Z7 such that
A7: for Y holds Y in Z7 iff
  Y in union union union union union union union X & P7[Y]
  from XBOOLE_0:sch 1;
  defpred P8[set] means
  ex Y1,Y2,Y3 st Y1 in Y2 & Y2 in Y3 & Y3 in $1 & Y1 meets X;
  consider Z8 such that
A8: for Y holds Y in Z8 iff
  Y in union union union union union union union union X & P8[Y]
  from XBOOLE_0:sch 1;
  defpred P9[set] means ex Y1,Y2 st Y1 in Y2 & Y2 in $1 & Y1 meets X;
  consider Z9 such that
A9: for Y holds Y in Z9 iff
  Y in union union union union union union union union union X & P9[Y]
  from XBOOLE_0:sch 1;
  defpred P10[set] means ex Y1 st Y1 in $1 & Y1 meets X;
  consider ZA such that
A10: for Y holds Y in ZA iff
  Y in union union union union union union union union union union X &
  P10[Y] from XBOOLE_0:sch 1;
  defpred P11[set] means $1 meets X;
  consider ZB such that
A11: for Y holds Y in ZB iff
  Y in union union union union union union union union union union
  (union X) & P11[Y] from XBOOLE_0:sch 1;
  set V = (X \/ Z1) \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/ ZA \/
  ZB;
A12: V = X \/ (Z1 \/ Z2) \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/ ZA \/
  ZB by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3) \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/ ZA \/
  ZB by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4) \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/ ZA \/
  ZB by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5) \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/ ZA \/
  ZB by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6) \/ Z7 \/ Z8 \/ Z9 \/ ZA \/
  ZB by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7) \/ Z8 \/ Z9 \/ ZA \/
  ZB by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8) \/ Z9 \/ ZA \/
  ZB by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9) \/ ZA \/
  ZB by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/ ZA) \/
  ZB by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/ ZA \/
  ZB) by XBOOLE_1:4;
  assume X <> {};
  then V <> {} by A12,XBOOLE_1:15;
  then consider Y such that
A13: Y in V and
A14: Y misses V by MCART_1:1;
  assume
A15: not thesis;
  Y in (X \/ Z1) \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/ ZA
  or Y in ZB by A13,XBOOLE_0:def 2;
  then Y in (X \/ Z1) \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/
  Z9 or Y in ZA or Y in ZB by XBOOLE_0:def 2;
  then Y in (X \/ Z1) \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
  Z8 or Y in Z9 or Y in ZA or Y in ZB by XBOOLE_0:def 2;
  then Y in (X \/ Z1) \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/
  Z7 or Y in Z8 or Y in Z9 or Y in ZA or Y in ZB by XBOOLE_0:def 2;
  then Y in (X \/ Z1) \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/
  Z6 or Y in Z7 or Y in Z8 or Y in Z9 or Y in ZA or Y in ZB by XBOOLE_0:def 2;
  then Y in (X \/ Z1) \/ Z2 \/ Z3 \/ Z4 \/ Z5 or Y in Z6 or Y in Z7 or Y in Z8
  or Y in Z9 or Y in ZA or Y in ZB by XBOOLE_0:def 2;
  then Y in (X \/ Z1) \/ Z2 \/ Z3 \/
  Z4 or Y in Z5 or Y in Z6 or Y in Z7 or Y in Z8
  or Y in Z9 or Y in ZA or Y in ZB by XBOOLE_0:def 2;
  then Y in (X \/ Z1) \/ Z2 \/ Z3 or Y in Z4 or Y in Z5 or Y in Z6 or Y in Z7
  or Y in Z8 or Y in Z9 or Y in ZA or Y in ZB by XBOOLE_0:def 2;
  then Y in (X \/ Z1) \/
  Z2 or Y in Z3 or Y in Z4 or Y in Z5 or Y in Z6 or Y in Z7
  or Y in Z8 or Y in Z9 or Y in ZA or Y in ZB by XBOOLE_0:def 2;
  then
A16: Y in X \/
  Z1 or Y in Z2 or Y in Z3 or Y in Z4 or Y in Z5 or Y in Z6 or Y in Z7
  or Y in Z8 or Y in Z9 or Y in ZA or Y in ZB by XBOOLE_0:def 2;
  per cases by A16,XBOOLE_0:def 2;
  suppose
A17: Y in X;
    then consider Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8,Y9,YA,YB such that
A18: Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7 in
    Y8 & Y8 in Y9 & Y9 in YA & YA in YB & YB in Y & not Y1 misses X by A15;
    YB in union X & Y1 meets X by A17,A18,TARSKI:def 4;
    then YB in Z1 by A1,A18;
    then YB in X \/ Z1 by XBOOLE_0:def 2;
    then YB in X \/ Z1 \/ Z2 by XBOOLE_0:def 2;
    then YB in X \/ Z1 \/ Z2 \/ Z3 by XBOOLE_0:def 2;
    then YB in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 by XBOOLE_0:def 2;
    then YB in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_0:def 2;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by A18,XBOOLE_0:3;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
    Z8 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/
    Z9 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/ ZA
    by XBOOLE_1:70;
    hence contradiction by A14,XBOOLE_1:70;
  end;
  suppose
A19: Y in Z1;
    then consider Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8,Y9,YA such that
A20: Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7 in
    Y8 & Y8 in Y9 & Y9 in YA & YA in Y & Y1 meets X by A1;
    Y in union X by A1,A19;
    then YA in union union X by A20,TARSKI:def 4;
    then YA in Z2 by A2,A20;
    then YA in X \/ Z1 \/ Z2 by XBOOLE_0:def 2;
    then Y meets X \/ Z1 \/ Z2 by A20,XBOOLE_0:3;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
    Z8 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/
    Z9 by XBOOLE_1:70;
    then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/ ZA
    by XBOOLE_1:70;
    hence contradiction by A14,XBOOLE_1:70;
  end;
  suppose
A21: Y in Z2;
    then consider Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8,Y9 such that
A22: Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7 in
    Y8 & Y8 in Y9 & Y9 in Y & Y1 meets X by A2;
    Y in union union X by A2,A21;
    then Y9 in union union union X by A22,TARSKI:def 4;
    then Y9 in Z3 by A3,A22;
    then Y9 in X \/ Z1 \/ Z2 \/ Z3 by XBOOLE_0:def 2;
    then Y9 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 by XBOOLE_0:def 2;
    then Y9 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_0:def 2;
    then Y9 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_0:def 2;
    then Y9 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
    then Y9 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
    Z8 by XBOOLE_0:def 2;
    then Y9 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/
    Z9 by XBOOLE_0:def 2;
    then Y9 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/
    ZA by XBOOLE_0:def 2;
    then Y9 in V by XBOOLE_0:def 2;
    hence contradiction by A14,A22,XBOOLE_0:3;
  end;
  suppose
A23: Y in Z3;
    then consider Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8 such that
A24: Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7 in
    Y8 & Y8 in Y & Y1 meets X by A3;
    Y in union union union X by A3,A23;
    then Y8 in union union union union X by A24,TARSKI:def 4;
    then Y8 in Z4 by A4,A24;
    then Y8 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 by XBOOLE_0:def 2;
    then Y8 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_0:def 2;
    then Y8 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_0:def 2;
    then Y8 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
    then Y8 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
    Z8 by XBOOLE_0:def 2;
    then Y8 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/
    Z9 by XBOOLE_0:def 2;
    then Y8 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/
    ZA by XBOOLE_0:def 2;
    then Y8 in V by XBOOLE_0:def 2;
    hence contradiction by A14,A24,XBOOLE_0:3;
  end;
  suppose
A25: Y in Z4;
    then consider Y1,Y2,Y3,Y4,Y5,Y6,Y7 such that
A26: Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7 in Y
    & Y1 meets X by A4;
    Y in union union union union X by A4,A25;
    then Y7 in union union union union union X by A26,TARSKI:def 4;
    then Y7 in Z5 by A5,A26;
    then Y7 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_0:def 2;
    then Y7 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_0:def 2;
    then Y7 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
    then Y7 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
    Z8 by XBOOLE_0:def 2;
    then Y7 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/
    Z9 by XBOOLE_0:def 2;
    then Y7 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/
    ZA by XBOOLE_0:def 2;
    then Y7 in V by XBOOLE_0:def 2;
    hence contradiction by A14,A26,XBOOLE_0:3;
  end;
  suppose
A27: Y in Z5;
    then consider Y1,Y2,Y3,Y4,Y5,Y6 such that
A28: Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y
    & Y1 meets X by A5;
    Y in union union union union union X by A5,A27;
    then Y6 in union union union union union union X by A28,TARSKI:def 4;
    then Y6 in Z6 by A6,A28;
    then Y6 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_0:def 2;
    then Y6 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
    then Y6 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
    Z8 by XBOOLE_0:def 2;
    then Y6 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/
    Z9 by XBOOLE_0:def 2;
    then Y6 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/
    ZA by XBOOLE_0:def 2;
    then Y6 in V by XBOOLE_0:def 2;
    hence contradiction by A14,A28,XBOOLE_0:3;
  end;
  suppose
A29: Y in Z6;
    then consider Y1,Y2,Y3,Y4,Y5 such that
A30: Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y & Y1 meets X by A6;
    Y in union union union union union union X by A6,A29;
    then Y5 in
    union union union union union union union X by A30,TARSKI:def 4;
    then Y5 in Z7 by A7,A30;
    then Y5 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
    then Y5 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
    Z8 by XBOOLE_0:def 2;
    then Y5 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/
    Z9 by XBOOLE_0:def 2;
    then Y5 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/
    ZA by XBOOLE_0:def 2;
    then Y5 in V by XBOOLE_0:def 2;
    hence contradiction by A14,A30,XBOOLE_0:3;
  end;
  suppose
A31: Y in Z7;
    then consider Y1,Y2,Y3,Y4 such that
A32: Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y & Y1 meets X by A7;
    Y in union union union union union union union X by A7,A31;
    then Y4 in union union union union union union union union X
    by A32,TARSKI:def 4;
    then Y4 in Z8 by A8,A32;
    then Y4 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
    Z8 by XBOOLE_0:def 2;
    then Y4 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/
    Z9 by XBOOLE_0:def 2;
    then Y4 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/
    ZA by XBOOLE_0:def 2;
    then Y4 in V by XBOOLE_0:def 2;
    hence contradiction by A14,A32,XBOOLE_0:3;
  end;
  suppose
A33: Y in Z8;
    then consider Y1,Y2,Y3 such that
A34: Y1 in Y2 & Y2 in Y3 & Y3 in Y & Y1 meets X by A8;
    Y in union union union union union union union union X by A8,A33;
    then Y3 in union union union union union union union union union X
    by A34,TARSKI:def 4;
    then Y3 in Z9 by A9,A34;
    then Y3 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/
    Z9 by XBOOLE_0:def 2;
    then Y3 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/
    ZA by XBOOLE_0:def 2;
    then Y3 in V by XBOOLE_0:def 2;
    hence contradiction by A14,A34,XBOOLE_0:3;
  end;
  suppose
A35: Y in Z9;
    then consider Y1,Y2 such that
A36: Y1 in Y2 & Y2 in Y & Y1 meets X by A9;
    Y in union union union union union union union union union X by A9, A35;
    then Y2 in union union union union union union union union union union X
    by A36,TARSKI:def 4;
    then Y2 in ZA by A10,A36;
    then Y2 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 \/
    ZA by XBOOLE_0:def 2;
    then Y2 in V by XBOOLE_0:def 2;
    hence contradiction by A14,A36,XBOOLE_0:3;
  end;
  suppose
A37: Y in ZA;
    then consider Y1 such that
A38: Y1 in Y & Y1 meets X by A10;
    Y in union union union union union union union union union union X
    by A10,A37;
    then Y1 in union union union union union union union union union union
    (union X) by A38,TARSKI:def 4;
    then Y1 in ZB by A11,A38;
    then Y1 in V by XBOOLE_0:def 2;
    hence contradiction by A14,A38,XBOOLE_0:3;
  end;
  suppose Y in ZB;
    then Y meets X by A11;
    hence contradiction by A12,A14,XBOOLE_1:70;
  end;
end;

::
::   Tuples for n=7
::

definition
  let x1,x2,x3,x4,x5,x6,x7;
  func [x1,x2,x3,x4,x5,x6,x7] equals

  [[x1,x2,x3,x4,x5,x6],x7];
  correctness;
end;

theorem Th40:
  [x1,x2,x3,x4,x5,x6,x7] = [[[[[[x1,x2],x3],x4],x5],x6],x7]
proof
  thus [x1,x2,x3,x4,x5,x6,x7] = [[[[x1,x2,x3,x4],x5],x6],x7] by MCART_2:def 1
    .= [[[[[x1,x2,x3],x4],x5],x6],x7] by MCART_1:def 4
    .= [[[[[[x1,x2],x3],x4],x5],x6],x7] by MCART_1:def 3;
end;

theorem
  [x1,x2,x3,x4,x5,x6,x7] = [[x1,x2,x3,x4,x5],x6,x7] by MCART_1:def 3;

theorem
  [x1,x2,x3,x4,x5,x6,x7] = [[x1,x2,x3,x4],x5,x6,x7]
proof
  thus [x1,x2,x3,x4,x5,x6,x7] = [[[[[[x1,x2],x3],x4],x5],x6],x7] by Th40
    .= [[[[x1,x2],x3],x4],x5,x6,x7] by MCART_1:31
    .= [[x1,x2,x3,x4],x5,x6,x7] by MCART_1:31;
end;

theorem
  [x1,x2,x3,x4,x5,x6,x7] = [[x1,x2,x3],x4,x5,x6,x7]
proof
  thus [x1,x2,x3,x4,x5,x6,x7] = [[[[[[x1,x2],x3],x4],x5],x6],x7] by Th40
    .= [[[x1,x2],x3],x4,x5,x6,x7] by MCART_2:3
    .= [[x1,x2,x3],x4,x5,x6,x7] by MCART_1:def 3;
end;

theorem
  [x1,x2,x3,x4,x5,x6,x7] = [[x1,x2],x3,x4,x5,x6,x7] by Th6;

theorem Th45:
  [x1,x2,x3,x4,x5,x6,x7] = [y1,y2,y3,y4,y5,y6,y7]
  implies x1 = y1 & x2 = y2 & x3 = y3 & x4 = y4 & x5 = y5 & x6 = y6 & x7 = y7
proof
  assume [x1,x2,x3,x4,x5,x6,x7] = [y1,y2,y3,y4,y5,y6,y7];
  then [x1,x2,x3,x4,x5,x6] = [y1,y2,y3,y4,y5,y6] & x7 = y7 by ZFMISC_1:33;
  hence thesis by Th7;
end;

theorem Th46:
  X <> {} implies ex x being set st x in X & not ex x1,x2,x3,x4,x5,x6,x7
  st (x1 in X or x2 in X) & x = [x1,x2,x3,x4,x5,x6,x7]
proof
  assume X <> {};
  then consider Y such that
A1: Y in X and
A2: for Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8,Y9,YA,YB
  st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7
  in Y8 & Y8 in Y9 & Y9 in YA & YA in YB & YB in Y holds Y1 misses X by Th39;
  take x = Y;
  thus x in X by A1;
  given x1,x2,x3,x4,x5,x6,x7 such that
A3: x1 in X or x2 in X and
A4: x = [x1,x2,x3,x4,x5,x6,x7];
  set Y1 = { x1, x2 }, Y2 = { Y1, {x1} }, Y3 = { Y2, x3 }, Y4 = { Y3, {Y2} },
  Y5 = { Y4, x4 }, Y6 = { Y5, {Y4} }, Y7 = { Y6, x5 }, Y8 = { Y7, {Y6} },
  Y9 = { Y8, x6 }, YA = { Y9, {Y8} }, YB = { YA, x7 };
  x1 in Y1 & x2 in Y1 by TARSKI:def 2;
  then
A5: not Y1 misses X by A3,XBOOLE_0:3;
  Y = [[[[[[x1,x2],x3],x4],x5],x6],x7] by A4,Th40
    .= [[[[[ Y2,x3],x4],x5],x6],x7 ] by TARSKI:def 5
    .= [[[[ Y4,x4],x5],x6],x7 ] by TARSKI:def 5
    .= [[[ Y6,x5 ],x6],x7 ] by TARSKI:def 5
    .= [[ Y8,x6],x7 ] by TARSKI:def 5
    .= [ YA,x7 ] by TARSKI:def 5
    .= { YB, { YA } } by TARSKI:def 5;
  then Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7
  in Y8 & Y8 in Y9 & Y9 in YA & YA in YB & YB in Y by TARSKI:def 2;
  hence contradiction by A2,A5;
end;

::
::   Cartesian products of seven sets
::

definition
  let X1,X2,X3,X4,X5,X6,X7;
  func [:X1,X2,X3,X4,X5,X6,X7:] -> set equals

  [:[: X1,X2,X3,X4,X5,X6 :],X7 :];
  correctness;
end;

theorem Th47:
  [:X1,X2,X3,X4,X5,X6,X7:] = [:[:[:[:[:[:X1,X2:],X3:],X4:],X5:],X6:],X7:]
proof
  thus [:X1,X2,X3,X4,X5,X6,X7:]
  = [:[:[:[:X1,X2,X3,X4:],X5:],X6:],X7:] by MCART_2:def 2
    .= [:[:[:[:[:X1,X2,X3:],X4:],X5:],X6:],X7:] by ZFMISC_1:def 4
    .= [:[:[:[:[:[:X1,X2:],X3:],X4:],X5:],X6:],X7:] by ZFMISC_1:def 3;
end;

theorem
  [:X1,X2,X3,X4,X5,X6,X7:] = [:[:X1,X2,X3,X4,X5:],X6,X7:] by ZFMISC_1:def 3;

theorem
  [:X1,X2,X3,X4,X5,X6,X7:] = [:[:X1,X2,X3,X4:],X5,X6,X7:]
proof
  thus [:X1,X2,X3,X4,X5,X6,X7:]
  = [:[:[:[:[:[:X1,X2:],X3:],X4:],X5:],X6:],X7:] by Th47
    .= [:[:[:[:X1,X2:],X3:],X4:],X5,X6,X7:] by MCART_1:53
    .= [:[:X1,X2,X3,X4:],X5,X6,X7:] by MCART_1:53;
end;

theorem
  [:X1,X2,X3,X4,X5,X6,X7:] = [:[:X1,X2,X3:],X4,X5,X6,X7:]
proof
  thus [:X1,X2,X3,X4,X5,X6,X7:]
  = [:[:[:[:[:[:X1,X2:],X3:],X4:],X5:],X6:],X7:] by Th47
    .= [:[:[:X1,X2:],X3:],X4,X5,X6,X7:] by MCART_2:9
    .= [:[:X1,X2,X3:],X4,X5,X6,X7:] by ZFMISC_1:def 3;
end;

theorem
  [:X1,X2,X3,X4,X5,X6,X7:] = [:[:X1,X2:],X3,X4,X5,X6,X7:] by Th12;

theorem Th52:
  X1 <> {} & X2 <> {} & X3 <> {} & X4 <> {} & X5 <> {} & X6 <> {} & X7 <> {}
  iff [:X1,X2,X3,X4,X5,X6,X7:] <> {}
proof
  X1 <> {} & X2 <> {} & X3 <> {} & X4 <> {} & X5 <> {} & X6 <> {}
  iff [:X1,X2,X3,X4,X5,X6:] <> {} by Th13;
  hence thesis by ZFMISC_1:113;
end;

theorem Th53:
  X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} & X7<> {} implies
  ( [:X1,X2,X3,X4,X5,X6,X7:] = [:Y1,Y2,Y3,Y4,Y5,Y6,Y7:]
  implies X1=Y1 & X2=Y2 & X3=Y3 & X4=Y4 & X5=Y5 & X6=Y6 & X7=Y7 )
proof
  assume
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{};
  then
A2: [:X1,X2,X3,X4,X5,X6:] <> {} by Th13;
  assume
A3: X7<>{};
  assume [:X1,X2,X3,X4,X5,X6,X7:] = [:Y1,Y2,Y3,Y4,Y5,Y6,Y7:];
  then [:X1,X2,X3,X4,X5,X6:] = [:Y1,Y2,Y3,Y4,Y5,Y6:]
  & X7 = Y7 by A2,A3,ZFMISC_1:134;
  hence thesis by A1,Th14;
end;

theorem
  [:X1,X2,X3,X4,X5,X6,X7:]<>{}
  & [:X1,X2,X3,X4,X5,X6,X7:] = [:Y1,Y2,Y3,Y4,Y5,Y6,Y7:]
  implies X1=Y1 & X2=Y2 & X3=Y3 & X4=Y4 & X5=Y5 & X6=Y6 & X7=Y7
proof
  assume [:X1,X2,X3,X4,X5,X6,X7:]<>{};
  then X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} & X7<>{} by Th52;
  hence thesis by Th53;
end;

theorem
  [:X,X,X,X,X,X,X:] = [:Y,Y,Y,Y,Y,Y,Y:] implies X = Y
proof
  assume [:X,X,X,X,X,X,X:] = [:Y,Y,Y,Y,Y,Y,Y:];
  then X<>{} or Y<>{} implies thesis by Th53;
  hence X = Y;
end;

theorem Th56:
  X1 <> {} & X2 <> {} & X3 <> {} & X4 <> {} & X5 <> {} & X6 <> {} & X7 <> {}
  implies for x being Element of [:X1,X2,X3,X4,X5,X6,X7:]
  ex xx1,xx2,xx3,xx4,xx5,xx6,xx7 st x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7]
proof
  assume
A1: X1 <> {} & X2 <> {} & X3 <> {} & X4 <> {} & X5 <> {} & X6 <> {} & X7 <> {};
  then
A2: [:X1,X2,X3,X4,X5,X6:] <> {} by Th13;
  let x be Element of [:X1,X2,X3,X4,X5,X6,X7:];
  reconsider x'=x as Element of [:[:X1,X2,X3,X4,X5,X6:],X7:];
  consider x123456 being (Element of [:X1,X2,X3,X4,X5,X6:]), xx7 such that
A3: x' = [x123456,xx7] by A1,A2,MCART_2:36;
  consider xx1,xx2,xx3,xx4,xx5,xx6 such that
A4: x123456 = [xx1,xx2,xx3,xx4,xx5,xx6] by A1,Th17;
  take xx1,xx2,xx3,xx4,xx5,xx6,xx7;
  thus x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] by A3,A4;
end;

definition
  let X1,X2,X3,X4,X5,X6,X7;
  assume
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} & X7<>{};
  let x be Element of [:X1,X2,X3,X4,X5,X6,X7:];
  func x`1 -> Element of X1 means
  :Def11:
  x = [x1,x2,x3,x4,x5,x6,x7] implies it = x1;
  existence
  proof
    consider xx1,xx2,xx3,xx4,xx5,xx6,xx7 such that
A2: x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] by A1,Th56;
    take xx1;
    thus thesis by A2,Th45;
  end;
  uniqueness
  proof
    consider xx1,xx2,xx3,xx4,xx5,xx6,xx7 such that
A3: x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] by A1,Th56;
    let y,z be Element of X1;
    assume x = [x1,x2,x3,x4,x5,x6,x7] implies y = x1;
    then
A4: y = xx1 by A3;
    assume x = [x1,x2,x3,x4,x5,x6,x7] implies z = x1;
    hence thesis by A3,A4;
  end;
  func x`2 -> Element of X2 means
  :Def12:
  x = [x1,x2,x3,x4,x5,x6,x7] implies it = x2;
  existence
  proof
    consider xx1,xx2,xx3,xx4,xx5,xx6,xx7 such that
A5: x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] by A1,Th56;
    take xx2; thus thesis by A5,Th45;
  end;
  uniqueness
  proof
    consider xx1,xx2,xx3,xx4,xx5,xx6,xx7 such that
A6: x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] by A1,Th56;
    let y,z be Element of X2;
    assume x = [x1,x2,x3,x4,x5,x6,x7] implies y = x2;
    then
A7: y = xx2 by A6;
    assume x = [x1,x2,x3,x4,x5,x6,x7] implies z = x2;
    hence thesis by A6,A7;
  end;
  func x`3 -> Element of X3 means
  :Def13:
  x = [x1,x2,x3,x4,x5,x6,x7] implies it = x3;
  existence
  proof
    consider xx1,xx2,xx3,xx4,xx5,xx6,xx7 such that
A8: x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] by A1,Th56;
    take xx3; thus thesis by A8,Th45;
  end;
  uniqueness
  proof
    consider xx1,xx2,xx3,xx4,xx5,xx6,xx7 such that
A9: x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] by A1,Th56;
    let y,z be Element of X3;
    assume x = [x1,x2,x3,x4,x5,x6,x7] implies y = x3;
    then
A10: y = xx3 by A9;
    assume x = [x1,x2,x3,x4,x5,x6,x7] implies z = x3;
    hence thesis by A9,A10;
  end;
  func x`4 -> Element of X4 means
  :Def14:
  x = [x1,x2,x3,x4,x5,x6,x7] implies it = x4;
  existence
  proof
    consider xx1,xx2,xx3,xx4,xx5,xx6,xx7 such that
A11: x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] by A1,Th56;
    take xx4; thus thesis by A11,Th45;
  end;
  uniqueness
  proof
    consider xx1,xx2,xx3,xx4,xx5,xx6,xx7 such that
A12: x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] by A1,Th56;
    let y,z be Element of X4;
    assume x = [x1,x2,x3,x4,x5,x6,x7] implies y = x4;
    then
A13: y = xx4 by A12;
    assume x = [x1,x2,x3,x4,x5,x6,x7] implies z = x4;
    hence thesis by A12,A13;
  end;
  func x`5 -> Element of X5 means
  :Def15:
  x = [x1,x2,x3,x4,x5,x6,x7] implies it = x5;
  existence
  proof
    consider xx1,xx2,xx3,xx4,xx5,xx6,xx7 such that
A14: x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] by A1,Th56;
    take xx5; thus thesis by A14,Th45;
  end;
  uniqueness
  proof
    consider xx1,xx2,xx3,xx4,xx5,xx6,xx7 such that
A15: x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] by A1,Th56;
    let y,z be Element of X5;
    assume x = [x1,x2,x3,x4,x5,x6,x7] implies y = x5;
    then
A16: y = xx5 by A15;
    assume x = [x1,x2,x3,x4,x5,x6,x7] implies z = x5;
    hence thesis by A15,A16;
  end;
  func x`6 -> Element of X6 means
  :Def16:
  x = [x1,x2,x3,x4,x5,x6,x7] implies it = x6;
  existence
  proof
    consider xx1,xx2,xx3,xx4,xx5,xx6,xx7 such that
A17: x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] by A1,Th56;
    take xx6; thus thesis by A17,Th45;
  end;
  uniqueness
  proof
    consider xx1,xx2,xx3,xx4,xx5,xx6,xx7 such that
A18: x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] by A1,Th56;
    let y,z be Element of X6;
    assume x = [x1,x2,x3,x4,x5,x6,x7] implies y = x6;
    then
A19: y = xx6 by A18;
    assume x = [x1,x2,x3,x4,x5,x6,x7] implies z = x6;
    hence thesis by A18,A19;
  end;
  func x`7 -> Element of X7 means
  :Def17:
  x = [x1,x2,x3,x4,x5,x6,x7] implies it = x7;
  existence
  proof
    consider xx1,xx2,xx3,xx4,xx5,xx6,xx7 such that
A20: x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] by A1,Th56;
    take xx7; thus thesis by A20,Th45;
  end;
  uniqueness
  proof
    consider xx1,xx2,xx3,xx4,xx5,xx6,xx7 such that
A21: x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] by A1,Th56;
    let y,z be Element of X7;
    assume x = [x1,x2,x3,x4,x5,x6,x7] implies y = x7;
    then
A22: y = xx7 by A21;
    assume x = [x1,x2,x3,x4,x5,x6,x7] implies z = x7;
    hence thesis by A21,A22;
  end;
end;

theorem
  X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} & X7<>{} implies
  for x being Element of [:X1,X2,X3,X4,X5,X6,X7:]
  for x1,x2,x3,x4,x5,x6,x7 st x = [x1,x2,x3,x4,x5,x6,x7] holds
  x`1 = x1 & x`2 = x2 & x`3 = x3 & x`4 = x4 & x`5 = x5 & x`6 = x6
  & x`7 = x7 by Def11,Def12,Def13,Def14,Def15,Def16,Def17;

theorem Th58:
  X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} & X7<>{} implies
  for x being Element of [:X1,X2,X3,X4,X5,X6,X7:]
  holds x = [x`1,x`2,x`3,x`4,x`5,x`6,x`7]
proof
  assume
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} & X7<>{};
  let x be Element of [:X1,X2,X3,X4,X5,X6,X7:];
  consider xx1,xx2,xx3,xx4,xx5,xx6,xx7 such that
A2: x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] by A1,Th56;
  thus x = [x`1,xx2,xx3,xx4,xx5,xx6,xx7] by A1,A2,Def11
    .= [x`1,x`2,xx3,xx4,xx5,xx6,xx7] by A1,A2,Def12
    .= [x`1,x`2,x`3,xx4,xx5,xx6,xx7] by A1,A2,Def13
    .= [x`1,x`2,x`3,x`4,xx5,xx6,xx7] by A1,A2,Def14
    .= [x`1,x`2,x`3,x`4,x`5,xx6,xx7] by A1,A2,Def15
    .= [x`1,x`2,x`3,x`4,x`5,x`6,xx7] by A1,A2,Def16
    .= [x`1,x`2,x`3,x`4,x`5,x`6,x`7] by A1,A2,Def17;
end;

theorem Th59:
  X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} & X7<>{} implies
  for x being Element of [:X1,X2,X3,X4,X5,X6,X7:] holds
  x`1 = (x qua set)`1`1`1`1`1`1 & x`2 = (x qua set)`1`1`1`1`1`2 &
  x`3 = (x qua set)`1`1`1`1`2 & x`4 = (x qua set)`1`1`1`2 &
  x`5 = (x qua set)`1`1`2 & x`6 = (x qua set)`1`2 & x`7 = (x qua set)`2
proof
  assume
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} & X7<>{};
  let x be Element of [:X1,X2,X3,X4,X5,X6,X7:];
  thus x`1 = [ x`1, x`2]`1 by MCART_1:7
    .= [[x`1, x`2],x`3]`1`1 by MCART_1:7
    .= [ x`1, x`2 ,x`3]`1`1 by MCART_1:def 3
    .= [[x`1, x`2 ,x`3],x`4]`1`1`1 by MCART_1:7
    .= [ x`1, x`2 ,x`3 ,x`4]`1`1`1 by MCART_1:def 4
    .= [[x`1, x`2 ,x`3 ,x`4], x`5]`1`1`1`1 by MCART_1:7
    .= [ x`1, x`2 ,x`3 ,x`4 , x`5]`1`1`1`1 by MCART_2:def 1
    .= [[x`1, x`2 ,x`3 ,x`4, x`5],x`6]`1`1`1`1`1 by MCART_1:7
    .= [ x`1, x`2 ,x`3 ,x`4 ,x`5, x`6, x`7]`1`1`1`1`1`1 by MCART_1:7
    .= (x qua set)`1`1`1`1`1`1 by A1,Th58;
  thus x`2 = [ x`1, x`2]`2 by MCART_1:7
    .= [[x`1, x`2],x`3]`1`2 by MCART_1:7
    .= [ x`1, x`2, x`3]`1`2 by MCART_1:def 3
    .= [[x`1, x`2, x`3],x`4]`1`1`2 by MCART_1:7
    .= [ x`1, x`2, x`3, x`4]`1`1`2 by MCART_1:def 4
    .= [[x`1, x`2, x`3, x`4 ], x`5]`1`1`1`2 by MCART_1:7
    .= [ x`1, x`2, x`3, x`4 , x`5]`1`1`1`2 by MCART_2:def 1
    .= [[x`1, x`2, x`3, x`4, x`5],x`6]`1`1`1`1`2 by MCART_1:7
    .= [ x`1, x`2, x`3, x`4, x`5, x`6, x`7]`1`1`1`1`1`2 by MCART_1:7
    .= (x qua set)`1`1`1`1`1`2 by A1,Th58;
  thus x`3 = [[x`1, x`2],x`3]`2 by MCART_1:7
    .= [ x`1, x`2, x`3]`2 by MCART_1:def 3
    .= [[x`1, x`2, x`3],x`4]`1`2 by MCART_1:7
    .= [ x`1, x`2, x`3, x`4]`1`2 by MCART_1:def 4
    .= [[x`1, x`2, x`3, x`4],x`5]`1`1`2 by MCART_1:7
    .= [ x`1, x`2, x`3, x`4 ,x`5]`1`1`2 by MCART_2:def 1
    .= [[x`1, x`2, x`3, x`4, x`5],x`6]`1`1`1`2 by MCART_1:7
    .= [ x`1, x`2, x`3, x`4, x`5, x`6, x`7]`1`1`1`1`2 by MCART_1:7
    .= (x qua set)`1`1`1`1`2 by A1,Th58;
  thus x`4 = [[x`1,x`2,x`3],x`4]`2 by MCART_1:7
    .= [ x`1,x`2,x`3, x`4]`2 by MCART_1:def 4
    .= [[x`1,x`2,x`3, x`4],x`5]`1`2 by MCART_1:7
    .= [ x`1,x`2,x`3, x`4 ,x`5]`1`2 by MCART_2:def 1
    .= [[x`1,x`2,x`3, x`4, x`5],x`6]`1`1`2 by MCART_1:7
    .= [ x`1,x`2,x`3, x`4, x`5, x`6, x`7]`1`1`1`2 by MCART_1:7
    .= (x qua set)`1`1`1`2 by A1,Th58;
  thus x`5 = [[x`1,x`2,x`3,x`4],x`5]`2 by MCART_1:7
    .= [ x`1,x`2,x`3,x`4 ,x`5]`2 by MCART_2:def 1
    .= [[x`1,x`2,x`3,x`4,x`5],x`6]`1`2 by MCART_1:7
    .= [ x`1,x`2,x`3,x`4,x`5, x`6,x`7]`1`1`2 by MCART_1:7
    .= (x qua set)`1`1`2 by A1,Th58;
  thus x`6 = [[x`1,x`2,x`3,x`4,x`5],x`6]`2 by MCART_1:7
    .= [ x`1,x`2,x`3,x`4,x`5,x`6, x`7]`1`2 by MCART_1:7
    .= (x qua set)`1`2 by A1,Th58;
  thus x`7 = [ x`1,x`2,x`3,x`4,x`5,x`6, x`7]`2 by MCART_1:7
    .= (x qua set)`2 by A1,Th58;
end;

theorem
  X1 c= [:X1,X2,X3,X4,X5,X6,X7:] or X1 c= [:X2,X3,X4,X5,X6,X7,X1:]
  or X1 c= [:X3,X4,X5,X6,X7,X1,X2:] or X1 c= [:X4,X5,X6,X7,X1,X2,X3:]
  or X1 c= [:X5,X6,X7,X1,X2,X3,X4:] or X1 c= [:X6,X7,X1,X2,X3,X4,X5:]
  or X1 c= [:X7,X1,X2,X3,X4,X5,X6:] implies X1 = {}
proof
  assume that
A1: X1 c= [:X1,X2,X3,X4,X5,X6,X7:] or X1 c= [:X2,X3,X4,X5,X6,X7,X1:]
  or X1 c= [:X3,X4,X5,X6,X7,X1,X2:] or X1 c= [:X4,X5,X6,X7,X1,X2,X3:]
  or X1 c= [:X5,X6,X7,X1,X2,X3,X4:] or X1 c= [:X6,X7,X1,X2,X3,X4,X5:]
  or X1 c= [:X7,X1,X2,X3,X4,X5,X6:] and
A2: X1 <> {};
  [:X1,X2,X3,X4,X5,X6,X7:]<>{} or [:X2,X3,X4,X5,X6,X7,X1:]<>{}
  or [:X3,X4,X5,X6,X7,X1,X2:]<>{} or [:X4,X5,X6,X7,X1,X2,X3:]<>{}
  or [:X5,X6,X7,X1,X2,X3,X4:]<>{} or [:X6,X7,X1,X2,X3,X4,X5:]<>{}
  or [:X7,X1,X2,X3,X4,X5,X6:]<>{} by A1,A2,XBOOLE_1:3;
  then
A3: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} & X7<>{} by Th52;
  now per cases by A1;
    suppose
A4:   X1 c= [:X1,X2,X3,X4,X5,X6,X7:];
      consider y such that
A5:   y in X1 and
A6:   for x1,x2,x3,x4,x5,x6,x7
      st x1 in X1 or x2 in X1 holds y <> [x1,x2,x3,x4,x5,x6,x7] by A2,Th46;
      reconsider y as Element of [:X1,X2,X3,X4,X5,X6,X7:] by A4,A5;
      y = [y`1,y`2,y`3,y`4,y`5,y`6,y`7] & y`1 in X1 by A3,Th58;
      hence contradiction by A6;
    end;
    suppose X1 c= [:X2,X3,X4,X5,X6,X7,X1:];
      then X1 c= [:[:X2,X3:],X4,X5,X6,X7,X1:] by Th12;
      hence thesis by A2,Th21;
    end;
    suppose X1 c= [:X3,X4,X5,X6,X7,X1,X2:];
      then X1 c= [:[:X3,X4:],X5,X6,X7,X1,X2:] by Th12;
      hence thesis by A2,Th21;
    end;
    suppose X1 c= [:X4,X5,X6,X7,X1,X2,X3:];
      then X1 c= [:[:X4,X5:],X6,X7,X1,X2,X3:] by Th12;
      hence thesis by A2,Th21;
    end;
    suppose X1 c= [:X5,X6,X7,X1,X2,X3,X4:];
      then X1 c= [:[:X5,X6:],X7,X1,X2,X3,X4:] by Th12;
      hence thesis by A2,Th21;
    end;
    suppose X1 c= [:X6,X7,X1,X2,X3,X4,X5:];
      then X1 c= [:[:X6,X7:],X1,X2,X3,X4,X5:] by Th12;
      hence thesis by A2,Th21;
    end;
    suppose
A7:   X1 c= [:X7,X1,X2,X3,X4,X5,X6:];
      consider y such that
A8:   y in X1 and
A9:   for x1,x2,x3,x4,x5,x6,x7
      st x1 in X1 or x2 in X1 holds y <> [x1,x2,x3,x4,x5,x6,x7] by A2,Th46;
      reconsider y as Element of [:X7,X1,X2,X3,X4,X5,X6:] by A7,A8;
      y = [y`1,y`2,y`3,y`4,y`5,y`6,y`7] & y`2 in X1 by A3,Th58;
      hence thesis by A9;
    end;
  end;
  hence contradiction;
end;

theorem
  [:X1,X2,X3,X4,X5,X6,X7:] meets [:Y1,Y2,Y3,Y4,Y5,Y6,Y7:] implies
  X1 meets Y1 & X2 meets Y2 & X3 meets Y3 & X4 meets Y4 & X5 meets Y5
  & X6 meets Y6 & X7 meets Y7
proof
A1: [:[:[:[:[:[:X1,X2:],X3:],X4:],X5:],X6:],X7:] = [:X1,X2,X3,X4,X5,X6,X7:]
  & [:[:[:[:[:[:Y1,Y2:],Y3:],Y4:],Y5:],Y6:],Y7:]
  = [:Y1,Y2,Y3,Y4,Y5,Y6,Y7:] by Th47;
  assume [:X1,X2,X3,X4,X5,X6,X7:] meets [:Y1,Y2,Y3,Y4,Y5,Y6,Y7:];
  then
A2: [:[:[:[:[:X1,X2:],X3:],X4:],X5:],X6:] meets
  [:[:[:[:[:Y1,Y2:],Y3:],Y4:],Y5:],Y6:] & X7 meets Y7 by A1,ZFMISC_1:127;
  then [:[:[:[:X1,X2:],X3:],X4:],X5:] meets
  [:[:[:[:Y1,Y2:],Y3:],Y4:],Y5:] & X6 meets Y6 by ZFMISC_1:127;
  then
A3: [:[:[:X1,X2:],X3:],X4:] meets [:[:[:Y1,Y2:],Y3:],Y4:]
  & X5 meets Y5 by ZFMISC_1:127;
  then [:[:X1,X2:],X3:] meets [:[:Y1,Y2:],Y3:] & X4 meets Y4 by ZFMISC_1:127;
  then [:X1,X2:] meets [:Y1,Y2:] & X3 meets Y3 by ZFMISC_1:127;
  hence thesis by A2,A3,ZFMISC_1:127;
end;

theorem
  [:{x1},{x2},{x3},{x4},{x5},{x6},{x7}:] = { [x1,x2,x3,x4,x5,x6,x7] }
proof thus [:{x1},{x2},{x3},{x4},{x5},{x6},{x7}:]
  = [:[:{x1},{x2}:],{x3},{x4},{x5},{x6},{x7}:] by Th12
    .= [:{[x1,x2]}, {x3},{x4},{x5},{x6},{x7}:] by ZFMISC_1:35
    .= { [[x1,x2], x3, x4, x5, x6, x7]} by Th23
    .= { [x1,x2,x3,x4,x5,x6,x7] } by Th6;
end;

reserve A1 for Subset of X1,
  A2 for Subset of X2,
  A3 for Subset of X3,
  A4 for Subset of X4,
  A5 for Subset of X5,
  A6 for Subset of X6,
  A7 for Subset of X7;

:: 7 - Tuples

reserve x for Element of [:X1,X2,X3,X4,X5,X6,X7:];

theorem
  X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} & X7<>{} implies
  for x1,x2,x3,x4,x5,x6,x7 st x = [x1,x2,x3,x4,x5,x6,x7]
  holds x`1 = x1 & x`2 = x2 & x`3 = x3 & x`4 = x4 & x`5 = x5
  & x`6 = x6 & x`7 = x7 by Def11,Def12,Def13,Def14,Def15,Def16,Def17;

theorem
  X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} & X7<>{} &
  (for xx1,xx2,xx3,xx4,xx5,xx6,xx7
  st x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] holds y1 = xx1) implies y1 =x`1
proof
  assume that
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} & X7<>{} and
A2: for xx1,xx2,xx3,xx4,xx5,xx6,xx7
  st x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] holds y1 = xx1;
  x = [x`1,x`2,x`3,x`4,x`5,x`6,x`7] by A1,Th58;
  hence thesis by A2;
end;

theorem
  X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} & X7<>{} &
  (for xx1,xx2,xx3,xx4,xx5,xx6,xx7
  st x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] holds y2 = xx2) implies y2 =x`2
proof
  assume that
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} & X7<>{} and
A2: for xx1,xx2,xx3,xx4,xx5,xx6,xx7
  st x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] holds y2 = xx2;
  x = [x`1,x`2,x`3,x`4,x`5,x`6,x`7] by A1,Th58;
  hence thesis by A2;
end;

theorem
  X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} & X7<>{} &
  (for xx1,xx2,xx3,xx4,xx5,xx6,xx7
  st x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] holds y3 = xx3) implies y3 =x`3
proof
  assume that
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} & X7<>{} and
A2: for xx1,xx2,xx3,xx4,xx5,xx6,xx7 st
  x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] holds y3 = xx3;
  x = [x`1,x`2,x`3,x`4,x`5,x`6,x`7] by A1,Th58;
  hence thesis by A2;
end;

theorem
  X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} & X7<>{} &
  (for xx1,xx2,xx3,xx4,xx5,xx6,xx7
  st x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] holds y4 = xx4) implies y4 =x`4
proof
  assume that
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} & X7<>{} and
A2: for xx1,xx2,xx3,xx4,xx5,xx6,xx7
  st x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] holds y4 = xx4;
  x = [x`1,x`2,x`3,x`4,x`5,x`6,x`7] by A1,Th58;
  hence thesis by A2;
end;

theorem
  X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} & X7<>{} &
  (for xx1,xx2,xx3,xx4,xx5,xx6,xx7
  st x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] holds y5 = xx5) implies y5 =x`5
proof
  assume that
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} & X7<>{} and
A2: for xx1,xx2,xx3,xx4,xx5,xx6,xx7
  st x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] holds y5 = xx5;
  x = [x`1,x`2,x`3,x`4,x`5,x`6,x`7] by A1,Th58;
  hence thesis by A2;
end;

theorem
  X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} & X7<>{} &
  (for xx1,xx2,xx3,xx4,xx5,xx6,xx7
  st x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] holds y6 = xx6) implies y6 =x`6
proof
  assume that
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} & X7<>{} and
A2: for xx1,xx2,xx3,xx4,xx5,xx6,xx7
  st x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] holds y6 = xx6;
  x = [x`1,x`2,x`3,x`4,x`5,x`6,x`7] by A1,Th58;
  hence thesis by A2;
end;

theorem
  X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} & X7<>{} &
  (for xx1,xx2,xx3,xx4,xx5,xx6,xx7
  st x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] holds y7 = xx7) implies y7 =x`7
proof
  assume that
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} & X7<>{} and
A2: for xx1,xx2,xx3,xx4,xx5,xx6,xx7
  st x = [xx1,xx2,xx3,xx4,xx5,xx6,xx7] holds y7 = xx7;
  x = [x`1,x`2,x`3,x`4,x`5,x`6,x`7] by A1,Th58;
  hence thesis by A2;
end;

theorem
  y in [: X1,X2,X3,X4,X5,X6,X7 :] implies ex x1,x2,x3,x4,x5,x6,x7
  st x1 in X1 & x2 in X2 & x3 in X3 & x4 in X4 & x5 in X5
  & x6 in X6 & x7 in X7 & y = [x1,x2,x3,x4,x5,x6,x7]
proof
  assume y in [: X1,X2,X3,X4,X5,X6,X7 :];
  then consider x123456, x7 being set such that
A1: x123456 in [:X1,X2,X3,X4,X5,X6:] and
A2: x7 in X7 and
A3: y = [x123456,x7] by ZFMISC_1:def 2;
  consider x1, x2, x3, x4, x5, x6 such that
A4: x1 in X1 & x2 in X2 & x3 in X3 & x4 in X4 & x5 in X5 & x6 in X6
  & x123456 = [x1,x2,x3,x4,x5,x6] by A1,Th31;
  y = [x1,x2,x3,x4,x5,x6,x7] by A3,A4;
  hence thesis by A2,A4;
end;

theorem
  [x1,x2,x3,x4,x5,x6,x7] in [: X1,X2,X3,X4,X5,X6,X7 :]
  iff x1 in X1 & x2 in X2 & x3 in X3 & x4 in X4 & x5 in X5
  & x6 in X6 & x7 in X7
proof
A1: [:X1,X2,X3,X4,X5,X6,X7:] = [:[:X1,X2:],X3,X4,X5,X6,X7:] by Th12;
A2: [x1,x2,x3,x4,x5,x6,x7] = [[x1,x2],x3,x4,x5,x6,x7] by Th6;
  [x1,x2] in [:X1,X2:] iff x1 in X1 & x2 in X2 by ZFMISC_1:106;
  hence thesis by A1,A2,Th32;
end;

theorem
  (for y holds y in Z iff ex x1,x2,x3,x4,x5,x6,x7
  st x1 in X1 & x2 in X2 & x3 in X3 & x4 in X4 & x5 in X5
  & x6 in X6 & x7 in X7 & y = [x1,x2,x3,x4,x5,x6,x7])
  implies Z = [: X1,X2,X3,X4,X5,X6,X7 :]
proof
  assume
A1: for y holds y in Z iff ex x1,x2,x3,x4,x5,x6,x7
  st x1 in X1 & x2 in X2 & x3 in X3 & x4 in X4 & x5 in X5
  & x6 in X6 & x7 in X7 & y = [x1,x2,x3,x4,x5,x6,x7];
  now
    let y;
    thus y in Z implies y in [:[:X1,X2,X3,X4,X5,X6:],X7:]
    proof
      assume y in Z;
      then consider x1,x2,x3,x4,x5,x6,x7 such that
A2:   x1 in X1 & x2 in X2 & x3 in X3 & x4 in X4 & x5 in X5
      & x6 in X6 & x7 in X7 & y = [x1,x2,x3,x4,x5,x6,x7] by A1;
      [x1,x2,x3,x4,x5,x6] in [:X1,X2,X3,X4,X5,X6:] & x7 in X7 by A2,Th32;
      hence y in [:[:X1,X2,X3,X4,X5,X6:],X7:] by A2,ZFMISC_1:def 2;
    end;
    assume y in [:[:X1,X2,X3,X4,X5,X6:],X7:];
    then consider x123456,x7 being set such that
A3: x123456 in [:X1,X2,X3,X4,X5,X6:] and
A4: x7 in X7 and
A5: y = [x123456,x7] by ZFMISC_1:def 2;
    consider x1,x2,x3,x4,x5,x6 such that
A6: x1 in X1 & x2 in X2 & x3 in X3 & x4 in X4 & x5 in X5 & x6 in X6
    & x123456 = [x1,x2,x3,x4,x5,x6] by A3,Th31;
    y = [x1,x2,x3,x4,x5,x6,x7] by A5,A6;
    hence y in Z by A1,A4,A6;
  end;
  hence Z = [: X1,X2,X3,X4,X5,X6,X7 :] by TARSKI:2;
end;

theorem Th74:
  X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} & X7<>{}
  & Y1<>{} & Y2<>{} & Y3<>{} & Y4<>{} & Y5<>{} & Y6<>{} & Y7<>{} implies
  for x being (Element of [:X1,X2,X3,X4,X5,X6,X7:]),
  y being Element of [:Y1,Y2,Y3,Y4,Y5,Y6,Y7:] holds x = y
  implies x`1 = y`1 & x`2 = y`2 & x`3 = y`3 & x`4 = y`4 & x`5 = y`5
  & x`6 = y`6 & x`7 = y`7
proof
  assume that
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} & X7<>{} and
A2: Y1<>{} & Y2<>{} & Y3<>{} & Y4<>{} & Y5<>{} & Y6<>{} & Y7<>{};
  let x be Element of [:X1,X2,X3,X4,X5,X6,X7:];
  let y be Element of [:Y1,Y2,Y3,Y4,Y5,Y6,Y7:];
  assume
A3: x = y;
  thus x`1 = (x qua set)`1`1`1`1`1`1 by A1,Th59
    .= y`1 by A2,A3,Th59;
  thus x`2 = (x qua set)`1`1`1`1`1`2 by A1,Th59
    .= y`2 by A2,A3,Th59;
  thus x`3 = (x qua set)`1`1`1`1`2 by A1,Th59
    .= y`3 by A2,A3,Th59;
  thus x`4 = (x qua set)`1`1`1`2 by A1,Th59
    .= y`4 by A2,A3,Th59;
  thus x`5 = (x qua set)`1`1`2 by A1,Th59
    .= y`5 by A2,A3,Th59;
  thus x`6 = (x qua set)`1`2 by A1,Th59
    .= y`6 by A2,A3,Th59;
  thus x`7 = (x qua set)`2 by A1,Th59
    .= y`7 by A2,A3,Th59;
end;

theorem
  for x being Element of [:X1,X2,X3,X4,X5,X6,X7:]
  st x in [:A1,A2,A3,A4,A5,A6,A7:]
  holds x`1 in A1 & x`2 in A2 & x`3 in A3 & x`4 in A4 & x`5 in A5
  & x`6 in A6 & x`7 in A7
proof
  let x be Element of [:X1,X2,X3,X4,X5,X6,X7:];
  assume
A1: x in [:A1,A2,A3,A4,A5,A6,A7:];
  then
A2: A1<>{} & A2<>{} & A3<>{} & A4<>{} & A5<>{} & A6<>{} & A7<>{} by Th52;
  reconsider y = x as Element of [:A1,A2,A3,A4,A5,A6,A7:] by A1;
  y`1 in A1 & y`2 in A2 & y`3 in A3 & y`4 in A4 & y`5 in A5
  & y`6 in A6 & y`7 in A7 by A2;
  hence x`1 in A1 & x`2 in A2 & x`3 in A3 & x`4 in A4 & x`5 in A5
  & x`6 in A6 & x`7 in A7 by Th74;
end;

theorem Th76:
  X1 c= Y1 & X2 c= Y2 & X3 c= Y3 & X4 c= Y4 & X5 c= Y5 & X6 c= Y6 & X7 c= Y7
  implies [:X1,X2,X3,X4,X5,X6,X7:] c= [:Y1,Y2,Y3,Y4,Y5,Y6,Y7:]
proof
  assume X1 c= Y1 & X2 c= Y2 & X3 c= Y3 & X4 c= Y4 & X5 c= Y5 & X6 c= Y6;
  then
A1: [:X1,X2,X3,X4,X5,X6:] c= [:Y1,Y2,Y3,Y4,Y5,Y6:] by Th36;
  assume X7 c= Y7;
  hence thesis by A1,ZFMISC_1:119;
end;

theorem
  [:A1,A2,A3,A4,A5,A6,A7:] is Subset of [:X1,X2,X3,X4,X5,X6,X7:] by Th76;

