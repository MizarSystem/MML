:: Tuples and Cartesian Products for n=6
::  by Michal Muzalewski and Wojciech Skaba
::
:: Received October 15, 1990
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabulary BOOLE, TARSKI, MCART_1, MCART_2, MCART_3;
 notation TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, MCART_1, MCART_2;
 constructors TARSKI, MCART_1, MCART_2, MEMBERED, XBOOLE_0;
 clusters MEMBERED, ZFMISC_1, XBOOLE_0;
 requirements SUBSET, BOOLE;
 theorems TARSKI, ZFMISC_1, MCART_1, MCART_2, XBOOLE_0, XBOOLE_1;
 schemes XBOOLE_0;

begin

  reserve v,z,x1,x2,x3,x4,x5,x6,y1,y2,y3,y4,y5,y6,
          X,X1,X2,X3,X4,X5,X6,Y,Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8,Y9,
          Z,Z1,Z2,Z3,Z4,Z5,Z6,Z7,Z8,Z9 for set;
  reserve xx1 for Element of X1;
  reserve xx2 for Element of X2;
  reserve xx3 for Element of X3;
  reserve xx4 for Element of X4;
  reserve xx5 for Element of X5;

theorem
   X <> {} implies
  ex Y st Y in X &
   for Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8
    st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7 in
 Y8
       & Y8 in Y
        holds Y1 misses X
 proof
   defpred P1[set] means
     ex Y1,Y2,Y3,Y4,Y5,Y6,Y7
         st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7
           & Y7 in $1 & Y1 meets X;
  consider Z1 such that
A1: for Y holds Y in Z1 iff Y in union X & P1[Y] from Separation;
   defpred P2[set] means
     ex Y1,Y2,Y3,Y4,Y5,Y6
         st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6
           & Y6 in $1 & Y1 meets X;
  consider Z2 such that
A2:  for Y holds Y in Z2 iff
         Y in union union X & P2[Y] from Separation;
   defpred P3[set] means
     ex Y1,Y2,Y3,Y4,Y5
         st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in $1 & Y1 meets X;
  consider Z3 such that
A3:  for Y holds Y in Z3 iff Y in union union union X & P3[Y] from Separation;
   defpred P4[set] means
     ex Y1,Y2,Y3,Y4 st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in $1 & Y1 meets X;
  consider Z4 such that
A4:  for Y holds Y in Z4 iff
         Y in union union union union X & P4[Y] from Separation;
  defpred P5[set] means
     ex Y1,Y2,Y3 st Y1 in Y2 & Y2 in Y3 & Y3 in $1 & Y1 meets X;
  consider Z5 such that
A5:  for Y holds Y in Z5 iff
         Y in union union union union union X & P5[Y] from Separation;
  defpred P6[set] means
     ex Y1,Y2 st Y1 in Y2 & Y2 in $1 & Y1 meets X;
  consider Z6 such that
A6:  for Y holds Y in Z6 iff
         Y in union union union union union union X & P6[Y] from Separation;
  defpred P7[set] means
     ex Y1 st Y1 in $1 & Y1 meets X;
  consider Z7 such that
A7:  for Y holds Y in Z7 iff
         Y in union union union union union union union X & P7[Y]
          from Separation;
  defpred P8[set] means $1 meets X;
  consider Z8 such that
A8:  for Y holds Y in Z8 iff
         Y in union union union union union union union union X
                                             & P8[Y]  from Separation;
   set V = X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8;
A9: V = X \/ (Z1 \/ Z2) \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3) \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4) \/ Z5 \/ Z6 \/ Z7 \/ Z8 by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5) \/ Z6 \/ Z7 \/ Z8 by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6) \/ Z7 \/ Z8 by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7) \/ Z8 by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8) by XBOOLE_1:4;

  assume X <> {};
  then V <> {} by A9,XBOOLE_1:15;
  then consider Y such that
A10:  Y in V and
A11:  Y misses V by MCART_1:1;
  assume
A12: not thesis;
     now assume
A13:  Y in X;
     then consider Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8 such that
A14:   Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7 in
 Y8
     & Y8 in Y & not Y1 misses X by A12;

        Y8 in union X & Y1 meets X by A13,A14,TARSKI:def 4;
     then Y8 in Z1 by A1,A14;
     then Y8 in X \/ Z1 by XBOOLE_0:def 2;
     then Y8 in X \/ Z1 \/ Z2 by XBOOLE_0:def 2;
     then Y8 in X \/ Z1 \/ Z2 \/ Z3 by XBOOLE_0:def 2;
     then Y8 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 by XBOOLE_0:def 2;
     then Y8 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_0:def 2;
     then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by A14,XBOOLE_0:3;
     then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_1:70;
     then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_1:70;
    hence contradiction by A11,XBOOLE_1:70;
   end;
   then Y in Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
 Z8 by A9,A10,XBOOLE_0:def 2;
   then Y in Z1 \/ (Z2 \/ Z3) \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 by XBOOLE_1:4;
   then Y in Z1 \/ (Z2 \/ Z3 \/ Z4) \/ Z5 \/ Z6 \/ Z7 \/ Z8 by XBOOLE_1:4;
   then Y in Z1 \/ (Z2 \/ Z3 \/ Z4 \/ Z5) \/ Z6 \/ Z7 \/ Z8 by XBOOLE_1:4;
   then Y in Z1 \/ (Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6) \/ Z7 \/ Z8 by XBOOLE_1:4;
   then Y in Z1 \/ (Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7) \/ Z8 by XBOOLE_1:4;
then A15: Y in Z1 \/ (Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8) by XBOOLE_1:4;
     now assume
A16:  Y in Z1;
     then consider Y1,Y2,Y3,Y4,Y5,Y6,Y7 such that
A17:    Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7 in
 Y
                                                       & Y1 meets X by A1;
       Y in union X by A1,A16;
     then Y7 in union union X by A17,TARSKI:def 4;
     then Y7 in Z2 by A2,A17;
     then Y7 in X \/ Z1 \/ Z2 by XBOOLE_0:def 2;
     then Y meets X \/ Z1 \/ Z2 by A17,XBOOLE_0:3;
     then Y meets X \/ Z1 \/ Z2 \/ Z3 by XBOOLE_1:70;
     then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 by XBOOLE_1:70;
     then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_1:70;
     then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_1:70;
     then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_1:70;
    hence contradiction by A11,XBOOLE_1:70;
   end;
   then Y in Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 by A15,XBOOLE_0:def 2;
   then Y in Z2 \/ (Z3 \/ Z4) \/ Z5 \/ Z6 \/ Z7 \/ Z8 by XBOOLE_1:4;
   then Y in Z2 \/ (Z3 \/ Z4 \/ Z5) \/ Z6 \/ Z7 \/ Z8 by XBOOLE_1:4;
   then Y in Z2 \/ (Z3 \/ Z4 \/ Z5 \/ Z6) \/ Z7 \/ Z8 by XBOOLE_1:4;
   then Y in Z2 \/ (Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7) \/ Z8 by XBOOLE_1:4;
then A18: Y in Z2 \/ (Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8) by XBOOLE_1:4;
     now assume
A19:  Y in Z2;
     then consider Y1,Y2,Y3,Y4,Y5,Y6 such that
A20:    Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y
      & Y1 meets X by A2;
       Y in union union X by A2,A19;
     then Y6 in union union union X by A20,TARSKI:def 4;
     then Y6 in Z3 by A3,A20;
     then Y6 in X \/ Z1 \/ Z2 \/ Z3 by XBOOLE_0:def 2;
     then Y6 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 by XBOOLE_0:def 2;
     then Y6 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_0:def 2;
     then Y6 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_0:def 2;
     then Y6 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
     then Y6 in V by XBOOLE_0:def 2;
    hence contradiction by A11,A20,XBOOLE_0:3;
   end;
   then Y in Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 by A18,XBOOLE_0:def 2;
   then Y in Z3 \/ (Z4 \/ Z5) \/ Z6 \/ Z7 \/ Z8 by XBOOLE_1:4;
   then Y in Z3 \/ (Z4 \/ Z5 \/ Z6) \/ Z7 \/ Z8 by XBOOLE_1:4;
   then Y in Z3 \/ (Z4 \/ Z5 \/ Z6 \/ Z7) \/ Z8 by XBOOLE_1:4;
then A21: Y in Z3 \/ (Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8) by XBOOLE_1:4;
     now assume
A22:  Y in Z3;
     then consider Y1,Y2,Y3,Y4,Y5 such that
A23:    Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y & Y1 meets X by A3;
       Y in union union union X by A3,A22;
     then Y5 in union union union union X by A23,TARSKI:def 4;
     then Y5 in Z4 by A4,A23;
     then Y5 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 by XBOOLE_0:def 2;
     then Y5 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_0:def 2;
     then Y5 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_0:def 2;
     then Y5 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
     then Y5 in V by XBOOLE_0:def 2;
    hence contradiction by A11,A23,XBOOLE_0:3;
   end;
   then Y in Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 by A21,XBOOLE_0:def 2;
   then Y in Z4 \/ (Z5 \/ Z6) \/ Z7 \/ Z8 by XBOOLE_1:4;
   then Y in Z4 \/ (Z5 \/ Z6 \/ Z7) \/ Z8 by XBOOLE_1:4;
then A24: Y in Z4 \/ (Z5 \/ Z6 \/ Z7 \/ Z8) by XBOOLE_1:4;
     now assume
A25:  Y in Z4;
     then consider Y1,Y2,Y3,Y4 such that
A26:    Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y & Y1 meets X by A4;
       Y in union union union union X by A4,A25;
     then Y4 in union union union union union X by A26,TARSKI:def 4;
     then Y4 in Z5 by A5,A26;
     then Y4 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_0:def 2;
     then Y4 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_0:def 2;
     then Y4 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
     then Y4 in V by XBOOLE_0:def 2;
    hence contradiction by A11,A26,XBOOLE_0:3;
   end;
   then Y in Z5 \/ Z6 \/ Z7 \/ Z8 by A24,XBOOLE_0:def 2;
   then Y in Z5 \/ (Z6 \/ Z7) \/ Z8 by XBOOLE_1:4;
then A27: Y in Z5 \/ (Z6 \/ Z7 \/ Z8) by XBOOLE_1:4;
     now assume
A28:  Y in Z5;
     then consider Y1,Y2,Y3 such that
A29:    Y1 in Y2 & Y2 in Y3 & Y3 in Y & Y1 meets X by A5;
       Y in union union union union union X by A5,A28;
     then Y3 in union union union union union union X by A29,TARSKI:def 4;
     then Y3 in Z6 by A6,A29;
     then Y3 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_0:def 2;
     then Y3 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
     then Y3 in V by XBOOLE_0:def 2;
    hence contradiction by A11,A29,XBOOLE_0:3;
   end;
   then Y in Z6 \/ Z7 \/ Z8 by A27,XBOOLE_0:def 2;
then A30: Y in Z6 \/ (Z7 \/ Z8) by XBOOLE_1:4;
     now assume
A31:  Y in Z6;
     then consider Y1,Y2 such that
A32:    Y1 in Y2 & Y2 in Y & Y1 meets X by A6;
       Y in union union union union union union X by A6,A31;
     then Y2 in
 union union union union union union union X by A32,TARSKI:def 4;
     then Y2 in Z7 by A7,A32;
     then Y2 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
     then Y2 in V by XBOOLE_0:def 2;
    hence contradiction by A11,A32,XBOOLE_0:3;
   end;
   then A33: Y in Z7 \/ Z8 by A30,XBOOLE_0:def 2;
     now assume
A34:  Y in Z7;
     then consider Y1 such that
A35:    Y1 in Y & Y1 meets X by A7;
       Y in union union union union union union union X by A7,A34;
     then Y1 in union union union union union union union union X
               by A35,TARSKI:def 4;
     then Y1 in Z8 by A8,A35;
     then Y1 in V by XBOOLE_0:def 2;
    hence contradiction by A11,A35,XBOOLE_0:3;
   end;
   then Y in Z8 by A33,XBOOLE_0:def 2;
   then Y meets X by A8;
  hence contradiction by A9,A11,XBOOLE_1:70;
 end;

theorem
Th2:
 X <> {} implies
  ex Y st Y in X &
   for Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8,Y9
    st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7 in
 Y8
       & Y8 in Y9 & Y9 in Y
        holds Y1 misses X
 proof
   defpred P1[set] means
     ex Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8
         st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7
           & Y7 in Y8 & Y8 in $1 & Y1 meets X;
  consider Z1 such that
A1: for Y holds Y in Z1 iff
        Y in union X & P1[Y] from Separation;
  defpred P2[set] means
     ex Y1,Y2,Y3,Y4,Y5,Y6,Y7
         st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7
           & Y7 in $1 & Y1 meets X;
  consider Z2 such that
A2:  for Y holds Y in Z2 iff
         Y in union union X & P2[Y] from Separation;
   defpred P3[set] means
     ex Y1,Y2,Y3,Y4,Y5,Y6
         st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in $1
           & Y1 meets X;
  consider Z3 such that
A3:  for Y holds Y in Z3 iff
         Y in union union union X & P3[Y] from Separation;
  defpred P4[set] means
     ex Y1,Y2,Y3,Y4,Y5
         st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in $1
           & Y1 meets X;
  consider Z4 such that
A4:  for Y holds Y in Z4 iff
         Y in union union union union X & P4[Y] from Separation;
  defpred P5[set] means
     ex Y1,Y2,Y3,Y4
         st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in $1 & Y1 meets X;
  consider Z5 such that
A5:  for Y holds Y in Z5 iff
         Y in union union union union union X & P5[Y] from Separation;
  defpred P6[set] means
     ex Y1,Y2,Y3 st Y1 in Y2 & Y2 in Y3 & Y3 in $1 & Y1 meets X;
  consider Z6 such that
A6:  for Y holds Y in Z6 iff
         Y in union union union union union union X & P6[Y] from Separation;
  defpred P7[set] means ex Y1,Y2 st Y1 in Y2 & Y2 in $1 & Y1 meets X;
  consider Z7 such that
A7:  for Y holds Y in Z7 iff
         Y in union union union union union union union X & P7[Y]
          from Separation;
  defpred P8[set] means ex Y1 st Y1 in $1 & Y1 meets X;
  consider Z8 such that
A8:  for Y holds Y in Z8 iff
         Y in union union union union union union union union X & P8[Y]
          from Separation;
  defpred P9[set] means $1 meets X;
  consider Z9 such that
A9:  for Y holds Y in Z9 iff
         Y in union union union union union union union union union X
                                             & P9[Y] from Separation;

   set V = X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9;

A10: V = X \/ (Z1 \/ Z2) \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:
4
    .= X \/ (Z1 \/ Z2 \/ Z3) \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4) \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5) \/ Z6 \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6) \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7) \/ Z8 \/ Z9 by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8) \/ Z9 by XBOOLE_1:4
    .= X \/ (Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9) by XBOOLE_1:4;

  assume X <> {};
  then V <> {} by A10,XBOOLE_1:15;
  then consider Y such that
A11:  Y in V and
A12:  Y misses V by MCART_1:1;
  assume
A13: not thesis;
     now assume
A14:  Y in X;
     then consider Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8,Y9 such that
A15:   Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7 in
 Y8
     & Y8 in Y9 & Y9 in Y & not Y1 misses X by A13;

       Y9 in union X & Y1 meets X by A14,A15,TARSKI:def 4;
     then Y9 in Z1 by A1,A15;
     then Y9 in X \/ Z1 by XBOOLE_0:def 2;
     then Y9 in X \/ Z1 \/ Z2 by XBOOLE_0:def 2;
     then Y9 in X \/ Z1 \/ Z2 \/ Z3 by XBOOLE_0:def 2;
     then Y9 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 by XBOOLE_0:def 2;
     then Y9 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_0:def 2;
     then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by A15,XBOOLE_0:3;
     then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_1:70;
     then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_1:70;
     then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
 Z8 by XBOOLE_1:70;
    hence contradiction by A12,XBOOLE_1:70;
   end;
   then Y in Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/
 Z9 by A10,A11,XBOOLE_0:def 2;
   then Y in Z1 \/ (Z2 \/ Z3) \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:4
;
   then Y in Z1 \/ (Z2 \/ Z3 \/ Z4) \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:4
;
   then Y in Z1 \/ (Z2 \/ Z3 \/ Z4 \/ Z5) \/ Z6 \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:4
;
   then Y in Z1 \/ (Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6) \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:4
;
   then Y in Z1 \/ (Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7) \/ Z8 \/ Z9 by XBOOLE_1:4
;
   then Y in Z1 \/ (Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8) \/
 Z9 by XBOOLE_1:4;
then A16: Y in Z1 \/ (Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9) by XBOOLE_1
:4;
     now assume
A17:  Y in Z1;
     then consider Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8 such that
A18:    Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7 in
 Y8
      & Y8 in Y & Y1 meets X by A1;
       Y in union X by A1,A17;
     then Y8 in union union X by A18,TARSKI:def 4;
     then Y8 in Z2 by A2,A18;
     then Y8 in X \/ Z1 \/ Z2 by XBOOLE_0:def 2;
     then Y meets X \/ Z1 \/ Z2 by A18,XBOOLE_0:3;
     then Y meets X \/ Z1 \/ Z2 \/ Z3 by XBOOLE_1:70;
     then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 by XBOOLE_1:70;
     then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_1:70;
     then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_1:70;
     then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_1:70;
     then Y meets X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
 Z8 by XBOOLE_1:70;
    hence contradiction by A12,XBOOLE_1:70;
   end;
   then Y in Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 by A16,XBOOLE_0:def 2
;
   then Y in Z2 \/ (Z3 \/ Z4) \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:4;
   then Y in Z2 \/ (Z3 \/ Z4 \/ Z5) \/ Z6 \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:4;
   then Y in Z2 \/ (Z3 \/ Z4 \/ Z5 \/ Z6) \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:4;
   then Y in Z2 \/ (Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7) \/ Z8 \/ Z9 by XBOOLE_1:4;
   then Y in Z2 \/ (Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8) \/ Z9 by XBOOLE_1:4;
then A19: Y in Z2 \/ (Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9) by XBOOLE_1:4;
     now assume
A20:  Y in Z2;
     then consider Y1,Y2,Y3,Y4,Y5,Y6,Y7 such that
A21:    Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7 in
 Y
      & Y1 meets X by A2;
       Y in union union X by A2,A20;
     then Y7 in union union union X by A21,TARSKI:def 4;
     then Y7 in Z3 by A3,A21;
     then Y7 in X \/ Z1 \/ Z2 \/ Z3 by XBOOLE_0:def 2;
     then Y7 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 by XBOOLE_0:def 2;
     then Y7 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_0:def 2;
     then Y7 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_0:def 2;
     then Y7 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
     then Y7 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
 Z8 by XBOOLE_0:def 2;
     then Y7 in V by XBOOLE_0:def 2;
    hence contradiction by A12,A21,XBOOLE_0:3;
   end;
   then Y in Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 by A19,XBOOLE_0:def 2;
   then Y in Z3 \/ (Z4 \/ Z5) \/ Z6 \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:4;
   then Y in Z3 \/ (Z4 \/ Z5 \/ Z6) \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:4;
   then Y in Z3 \/ (Z4 \/ Z5 \/ Z6 \/ Z7) \/ Z8 \/ Z9 by XBOOLE_1:4;
   then Y in Z3 \/ (Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8) \/ Z9 by XBOOLE_1:4;
then A22: Y in Z3 \/ (Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9) by XBOOLE_1:4;
     now assume
A23:  Y in Z3;
     then consider Y1,Y2,Y3,Y4,Y5,Y6 such that
A24:    Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y
      & Y1 meets X by A3;
       Y in union union union X by A3,A23;
     then Y6 in union union union union X by A24,TARSKI:def 4;
     then Y6 in Z4 by A4,A24;
     then Y6 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 by XBOOLE_0:def 2;
     then Y6 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_0:def 2;
     then Y6 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_0:def 2;
     then Y6 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
     then Y6 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
 Z8 by XBOOLE_0:def 2;
     then Y6 in V by XBOOLE_0:def 2;
    hence contradiction by A12,A24,XBOOLE_0:3;
   end;
   then Y in Z4 \/ Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 by A22,XBOOLE_0:def 2;
   then Y in Z4 \/ (Z5 \/ Z6) \/ Z7 \/ Z8 \/ Z9 by XBOOLE_1:4;
   then Y in Z4 \/ (Z5 \/ Z6 \/ Z7) \/ Z8 \/ Z9 by XBOOLE_1:4;
   then Y in Z4 \/ (Z5 \/ Z6 \/ Z7 \/ Z8) \/ Z9 by XBOOLE_1:4;
then A25: Y in Z4 \/ (Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9) by XBOOLE_1:4;
     now assume
A26:  Y in Z4;
     then consider Y1,Y2,Y3,Y4,Y5 such that
A27:    Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y & Y1 meets X by A4;
       Y in union union union union X by A4,A26;
     then Y5 in union union union union union X by A27,TARSKI:def 4;
     then Y5 in Z5 by A5,A27;
     then Y5 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 by XBOOLE_0:def 2;
     then Y5 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_0:def 2;
     then Y5 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
     then Y5 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
 Z8 by XBOOLE_0:def 2;
     then Y5 in V by XBOOLE_0:def 2;
    hence contradiction by A12,A27,XBOOLE_0:3;
   end;
   then Y in Z5 \/ Z6 \/ Z7 \/ Z8 \/ Z9 by A25,XBOOLE_0:def 2;
   then Y in Z5 \/ (Z6 \/ Z7) \/ Z8 \/ Z9 by XBOOLE_1:4;
   then Y in Z5 \/ (Z6 \/ Z7 \/ Z8) \/ Z9 by XBOOLE_1:4;
then A28: Y in Z5 \/ (Z6 \/ Z7 \/ Z8 \/ Z9) by XBOOLE_1:4;
     now assume
A29:  Y in Z5;
     then consider Y1,Y2,Y3,Y4 such that
A30:    Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y & Y1 meets X by A5;
       Y in union union union union union X by A5,A29;
     then Y4 in union union union union union union X by A30,TARSKI:def 4;
     then Y4 in Z6 by A6,A30;
     then Y4 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 by XBOOLE_0:def 2;
     then Y4 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
     then Y4 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
 Z8 by XBOOLE_0:def 2;
     then Y4 in V by XBOOLE_0:def 2;
    hence contradiction by A12,A30,XBOOLE_0:3;
   end;
   then Y in Z6 \/ Z7 \/ Z8 \/ Z9 by A28,XBOOLE_0:def 2;
   then Y in Z6 \/ (Z7 \/ Z8) \/ Z9 by XBOOLE_1:4;
then A31: Y in Z6 \/ (Z7 \/ Z8 \/ Z9) by XBOOLE_1:4;
     now assume
A32:  Y in Z6;
     then consider Y1,Y2,Y3 such that
A33:    Y1 in Y2 & Y2 in Y3 & Y3 in Y & Y1 meets X by A6;
       Y in union union union union union union X by A6,A32;
     then Y3 in
 union union union union union union union X by A33,TARSKI:def 4;
     then Y3 in Z7 by A7,A33;
     then Y3 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 by XBOOLE_0:def 2;
     then Y3 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
 Z8 by XBOOLE_0:def 2;
     then Y3 in V by XBOOLE_0:def 2;
    hence contradiction by A12,A33,XBOOLE_0:3;
   end;
   then Y in Z7 \/ Z8 \/ Z9 by A31,XBOOLE_0:def 2;
then A34: Y in Z7 \/ (Z8 \/ Z9) by XBOOLE_1:4;
     now assume
A35:  Y in Z7;
     then consider Y1,Y2 such that
A36:    Y1 in Y2 & Y2 in Y & Y1 meets X by A7;
       Y in union union union union union union union X by A7,A35;
     then Y2 in union union union union union union union union X
                                                   by A36,TARSKI:def 4;
     then Y2 in Z8 by A8,A36;
     then Y2 in X \/ Z1 \/ Z2 \/ Z3 \/ Z4 \/ Z5 \/ Z6 \/ Z7 \/
 Z8 by XBOOLE_0:def 2;
     then Y2 in V by XBOOLE_0:def 2;
    hence contradiction by A12,A36,XBOOLE_0:3;
   end;
   then A37: Y in Z8 \/ Z9 by A34,XBOOLE_0:def 2;
     now assume
A38:  Y in Z8;
     then consider Y1 such that
A39:    Y1 in Y & Y1 meets X by A8;
       Y in union union union union union union union union X by A8,A38;
     then Y1 in union union union union union union union union union X
               by A39,TARSKI:def 4;
     then Y1 in Z9 by A9,A39;
     then Y1 in V by XBOOLE_0:def 2;
    hence contradiction by A12,A39,XBOOLE_0:3;
   end;
   then Y in Z9 by A37,XBOOLE_0:def 2;
   then Y meets X by A9;
  hence contradiction by A10,A12,XBOOLE_1:70;
 end;


::
::   Tuples for n=6
::


definition
  let x1,x2,x3,x4,x5,x6;

  func [x1,x2,x3,x4,x5,x6] equals
  :Def1:  [[x1,x2,x3,x4,x5],x6];
  correctness;
end;


theorem
Th3: [x1,x2,x3,x4,x5,x6] = [[[[[x1,x2],x3],x4],x5],x6]
 proof
  thus [x1,x2,x3,x4,x5,x6] = [[x1,x2,x3,x4,x5],x6] by Def1
                          .= [[[x1,x2,x3,x4],x5],x6] by MCART_2:def 1
                          .= [[[[x1,x2,x3],x4],x5],x6] by MCART_1:def 4
                          .= [[[[[x1,x2],x3],x4],x5],x6] by MCART_1:def 3;
 end;


canceled;

theorem
   [x1,x2,x3,x4,x5,x6] = [[x1,x2,x3,x4],x5,x6]
  proof
    thus [x1,x2,x3,x4,x5,x6] = [[[[[x1,x2],x3],x4],x5],x6] by Th3
                            .= [[[[x1,x2],x3],x4],x5,x6] by MCART_1:def 3
                            .= [[x1,x2,x3,x4],x5,x6] by MCART_1:31;
  end;


theorem
   [x1,x2,x3,x4,x5,x6] = [[x1,x2,x3],x4,x5,x6]
  proof
    thus [x1,x2,x3,x4,x5,x6] = [[[[[x1,x2],x3],x4],x5],x6] by Th3
                            .= [[[x1,x2],x3],x4,x5,x6] by MCART_1:31
                            .= [[x1,x2,x3],x4,x5,x6] by MCART_1:def 3;
 end;


theorem
Th7: [x1,x2,x3,x4,x5,x6] = [[x1,x2],x3,x4,x5,x6]
  proof
    thus [x1,x2,x3,x4,x5,x6] = [[[[[x1,x2],x3],x4],x5],x6] by Th3
                            .= [[[x1,x2],x3],x4,x5,x6] by MCART_1:31
                            .= [[x1,x2],x3,x4,x5,x6] by MCART_2:6;
 end;


theorem
Th8:
  [x1,x2,x3,x4,x5,x6] = [y1,y2,y3,y4,y5,y6]
  implies x1 = y1 & x2 = y2 & x3 = y3 & x4 = y4 & x5 = y5 & x6 = y6
     proof
       assume [x1,x2,x3,x4,x5,x6] = [y1,y2,y3,y4,y5,y6];

       then [[x1,x2,x3,x4,x5],x6] = [y1,y2,y3,y4,y5,y6] by Def1
                                 .= [[y1,y2,y3,y4,y5],y6] by Def1;
       then [x1,x2,x3,x4,x5] = [y1,y2,y3,y4,y5]
          & x6 = y6 by ZFMISC_1:33;
       hence thesis by MCART_2:7;
 end;


theorem
Th9:
 X <> {} implies
  ex v st v in X &
   not ex x1,x2,x3,x4,x5,x6 st (x1 in X or x2 in X) & v = [x1,x2,x3,x4,x5,x6]
 proof
   assume X <> {};

   then consider Y such that
A1:  Y in X and
A2:  for Y1,Y2,Y3,Y4,Y5,Y6,Y7,Y8,Y9
      st Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7
in Y8
        & Y8 in Y9 & Y9 in Y
               holds Y1 misses X by Th2;

   take v = Y;
   thus v in X by A1;
   given x1,x2,x3,x4,x5,x6 such that
A3:  x1 in X or x2 in X and
A4:  v = [x1,x2,x3,x4,x5,x6];

   set Y1 = { x1, x2 },
       Y2 = { Y1, {x1} },
       Y3 = { Y2, x3 },
       Y4 = { Y3, {Y2} },
       Y5 = { Y4, x4 },
       Y6 = { Y5, {Y4} },
       Y7 = { Y6, x5 },
       Y8 = { Y7, {Y6} },
       Y9 = { Y8, x6 };

     x1 in Y1 & x2 in Y1 by TARSKI:def 2;
   then A5: not Y1 misses X by A3,XBOOLE_0:3;

      Y = [[[[[x1,x2],x3],x4],x5],x6] by A4,Th3
     .= [[[[ Y2,x3],x4],x5],x6 ] by TARSKI:def 5
     .= [[[ Y4,x4],x5],x6 ] by TARSKI:def 5
     .= [[ Y6,x5 ],x6] by TARSKI:def 5
     .= [ Y8,x6 ] by TARSKI:def 5
     .= { Y9, { Y8 } } by TARSKI:def 5;

   then Y1 in Y2 & Y2 in Y3 & Y3 in Y4 & Y4 in Y5 & Y5 in Y6 & Y6 in Y7 & Y7
in
 Y8
       & Y8 in Y9 & Y9 in Y
                                                               by TARSKI:def 2;
  hence contradiction by A2,A5;
 end;


::
::   Cartesian products of six sets
::


definition
  let X1,X2,X3,X4,X5,X6;

  func [:X1,X2,X3,X4,X5,X6:] -> set equals
  :Def2:  [:[: X1,X2,X3,X4,X5 :],X6 :];

  coherence;
end;


theorem
Th10:
 [:X1,X2,X3,X4,X5,X6:] = [:[:[:[:[:X1,X2:],X3:],X4:],X5:],X6:]
  proof
   thus [:X1,X2,X3,X4,X5,X6:]
      = [:[:X1,X2,X3,X4,X5:],X6:] by Def2
     .= [:[:[:X1,X2,X3,X4:],X5:],X6:] by MCART_2:def 2
     .= [:[:[:[:X1,X2,X3:],X4:],X5:],X6:] by ZFMISC_1:def 4
     .= [:[:[:[:[:X1,X2:],X3:],X4:],X5:],X6:] by ZFMISC_1:def 3;
  end;


canceled;

theorem
   [:X1,X2,X3,X4,X5,X6:] = [:[:X1,X2,X3,X4:],X5,X6:]
  proof
    thus [:X1,X2,X3,X4,X5,X6:]
       = [:[:[:[:[:X1,X2:],X3:],X4:],X5:],X6:] by Th10
      .= [:[:[:[:X1,X2:],X3:],X4:],X5,X6:] by ZFMISC_1:def 3
      .= [:[:X1,X2,X3,X4:],X5,X6:] by MCART_1:53;
  end;


theorem
   [:X1,X2,X3,X4,X5,X6:] = [:[:X1,X2,X3:],X4,X5,X6:]
  proof
    thus [:X1,X2,X3,X4,X5,X6:]
       = [:[:[:[:[:X1,X2:],X3:],X4:],X5:],X6:] by Th10
      .= [:[:[:X1,X2:],X3:],X4,X5,X6:] by MCART_1:53
      .= [:[:X1,X2,X3:],X4,X5,X6:] by ZFMISC_1:def 3;
 end;


theorem
Th14: [:X1,X2,X3,X4,X5,X6:] = [:[:X1,X2:],X3,X4,X5,X6:]
  proof
    thus [:X1,X2,X3,X4,X5,X6:]
       = [:[:[:[:[:X1,X2:],X3:],X4:],X5:],X6:] by Th10
      .= [:[:[:X1,X2:],X3:],X4,X5,X6:] by MCART_1:53
      .= [:[:X1,X2:],X3,X4,X5,X6:] by MCART_2:12;
 end;


theorem
 Th15: X1 <> {} & X2 <> {} & X3 <> {} & X4 <> {} & X5 <> {} & X6 <> {}
   iff [:X1,X2,X3,X4,X5,X6:] <> {}
 proof

A1: [:[:X1,X2,X3,X4,X5:],X6:] = [:X1,X2,X3,X4,X5,X6:] by Def2;

     X1 <> {} & X2 <> {} & X3 <> {} & X4 <> {} & X5 <> {}
   iff [:X1,X2,X3,X4,X5:] <> {} by MCART_2:13;
   hence thesis by A1,ZFMISC_1:113;
 end;


theorem
Th16:
 X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} implies
  ( [:X1,X2,X3,X4,X5,X6:] = [:Y1,Y2,Y3,Y4,Y5,Y6:]
    implies X1=Y1 & X2=Y2 & X3=Y3 & X4=Y4 & X5=Y5 & X6=Y6 )
proof

A1: [:X1,X2,X3,X4,X5,X6:] = [:[:X1,X2,X3,X4,X5:],X6:] by Def2;
 assume
A2: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{};
 then A3: [:X1,X2,X3,X4,X5:] <> {} by MCART_2:13;
 assume
A4: X6<>{};

  assume [:X1,X2,X3,X4,X5,X6:] = [:Y1,Y2,Y3,Y4,Y5,Y6:];

  then [:[:X1,X2,X3,X4,X5:],X6:] = [:[:Y1,Y2,Y3,Y4,Y5:],Y6:] by A1,Def2;
  then [:X1,X2,X3,X4,X5:] = [:Y1,Y2,Y3,Y4,Y5:] & X6 = Y6 by A3,A4,ZFMISC_1:134
;
  hence thesis by A2,MCART_2:14;
end;


theorem
   [:X1,X2,X3,X4,X5,X6:]<>{} & [:X1,X2,X3,X4,X5,X6:] = [:Y1,Y2,Y3,Y4,Y5,Y6:]
   implies X1=Y1 & X2=Y2 & X3=Y3 & X4=Y4 & X5=Y5 & X6=Y6
proof
  assume [:X1,X2,X3,X4,X5,X6:]<>{};
  then X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} by Th15;
 hence thesis by Th16;
end;


theorem
    [:X,X,X,X,X,X:] = [:Y,Y,Y,Y,Y,Y:] implies X = Y
proof
 assume [:X,X,X,X,X,X:] = [:Y,Y,Y,Y,Y,Y:];

  then X<>{} or Y<>{} implies thesis by Th16;
  hence X = Y;
end;


 reserve xx6 for Element of X6;


theorem
Th19:
 X1 <> {} & X2 <> {} & X3 <> {} & X4 <> {} & X5 <> {} & X6 <> {} implies
 for x being Element of [:X1,X2,X3,X4,X5,X6:]
   ex xx1,xx2,xx3,xx4,xx5,xx6 st x = [xx1,xx2,xx3,xx4,xx5,xx6]
 proof
   assume
A1:  X1 <> {} & X2 <> {} & X3 <> {} & X4 <> {} & X5 <> {} & X6 <> {};
then A2:  [:X1,X2,X3,X4,X5:] <> {} by MCART_2:13;

  let x be Element of [:X1,X2,X3,X4,X5,X6:];
   reconsider x'=x as Element of [:[:X1,X2,X3,X4,X5:],X6:] by Def2;
   consider x12345 being (Element of [:X1,X2,X3,X4,X5:]), xx6 such that
A3:   x' = [x12345,xx6] by A1,A2,MCART_2:36;
   consider xx1,xx2,xx3,xx4,xx5 such that
A4:   x12345 = [xx1,xx2,xx3,xx4,xx5] by A1,MCART_2:17;

  take xx1,xx2,xx3,xx4,xx5,xx6;
  thus x = [xx1,xx2,xx3,xx4,xx5,xx6] by A3,A4,Def1;
 end;


definition
  let X1,X2,X3,X4,X5,X6;

  assume
         A1:  X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{};

 let x be Element of [:X1,X2,X3,X4,X5,X6:];

  func x`1 -> Element of X1 means
:Def3: x = [x1,x2,x3,x4,x5,x6] implies it = x1;
  existence
   proof
    consider xx1,xx2,xx3,xx4,xx5,xx6 such that
A2:   x = [xx1,xx2,xx3,xx4,xx5,xx6] by A1,Th19;
    take xx1;
    thus thesis by A2,Th8;
   end;
  uniqueness
   proof
    consider xx1,xx2,xx3,xx4,xx5,xx6 such that
A3:   x = [xx1,xx2,xx3,xx4,xx5,xx6] by A1,Th19;
    let y,z be Element of X1;
    assume x = [x1,x2,x3,x4,x5,x6] implies y = x1;
then A4:   y = xx1 by A3;
    assume x = [x1,x2,x3,x4,x5,x6] implies z = x1;
    hence thesis by A3,A4;
   end;

  func x`2 -> Element of X2 means
:Def4: x = [x1,x2,x3,x4,x5,x6] implies it = x2;
  existence
   proof
    consider xx1,xx2,xx3,xx4,xx5,xx6 such that
A5:   x = [xx1,xx2,xx3,xx4,xx5,xx6] by A1,Th19;
    take xx2; thus thesis by A5,Th8;
   end;
  uniqueness
   proof
    consider xx1,xx2,xx3,xx4,xx5,xx6 such that
A6:   x = [xx1,xx2,xx3,xx4,xx5,xx6] by A1,Th19;
    let y,z be Element of X2;
    assume x = [x1,x2,x3,x4,x5,x6] implies y = x2;
then A7:   y = xx2 by A6;
    assume x = [x1,x2,x3,x4,x5,x6] implies z = x2;
    hence thesis by A6,A7;
   end;

  func x`3 -> Element of X3 means
:Def5: x = [x1,x2,x3,x4,x5,x6] implies it = x3;
  existence
   proof
    consider xx1,xx2,xx3,xx4,xx5,xx6 such that
A8:   x = [xx1,xx2,xx3,xx4,xx5,xx6] by A1,Th19;
    take xx3; thus thesis by A8,Th8;
   end;
  uniqueness
   proof
    consider xx1,xx2,xx3,xx4,xx5,xx6 such that
A9:   x = [xx1,xx2,xx3,xx4,xx5,xx6] by A1,Th19;
    let y,z be Element of X3;
    assume x = [x1,x2,x3,x4,x5,x6] implies y = x3;
then A10:   y = xx3 by A9;
    assume x = [x1,x2,x3,x4,x5,x6] implies z = x3;
    hence thesis by A9,A10;
   end;

  func x`4 -> Element of X4 means
:Def6: x = [x1,x2,x3,x4,x5,x6] implies it = x4;
  existence
   proof
    consider xx1,xx2,xx3,xx4,xx5,xx6 such that
A11:   x = [xx1,xx2,xx3,xx4,xx5,xx6] by A1,Th19;
    take xx4; thus thesis by A11,Th8;
   end;
  uniqueness
   proof
    consider xx1,xx2,xx3,xx4,xx5,xx6 such that
A12:   x = [xx1,xx2,xx3,xx4,xx5,xx6] by A1,Th19;
    let y,z be Element of X4;
    assume x = [x1,x2,x3,x4,x5,x6] implies y = x4;
then A13:   y = xx4 by A12;
    assume x = [x1,x2,x3,x4,x5,x6] implies z = x4;
    hence thesis by A12,A13;
   end;

  func x`5 -> Element of X5 means
:Def7: x = [x1,x2,x3,x4,x5,x6] implies it = x5;
  existence
   proof
    consider xx1,xx2,xx3,xx4,xx5,xx6 such that
A14:   x = [xx1,xx2,xx3,xx4,xx5,xx6] by A1,Th19;
    take xx5; thus thesis by A14,Th8;
   end;
  uniqueness
   proof
    consider xx1,xx2,xx3,xx4,xx5,xx6 such that
A15:   x = [xx1,xx2,xx3,xx4,xx5,xx6] by A1,Th19;
    let y,z be Element of X5;
    assume x = [x1,x2,x3,x4,x5,x6] implies y = x5;
then A16:   y = xx5 by A15;
    assume x = [x1,x2,x3,x4,x5,x6] implies z = x5;
    hence thesis by A15,A16;
   end;

  func x`6 -> Element of X6 means
:Def8: x = [x1,x2,x3,x4,x5,x6] implies it = x6;
  existence
   proof
    consider xx1,xx2,xx3,xx4,xx5,xx6 such that
A17:   x = [xx1,xx2,xx3,xx4,xx5,xx6] by A1,Th19;
    take xx6; thus thesis by A17,Th8;
   end;
  uniqueness
   proof
    consider xx1,xx2,xx3,xx4,xx5,xx6 such that
A18:   x = [xx1,xx2,xx3,xx4,xx5,xx6] by A1,Th19;
    let y,z be Element of X6;
    assume x = [x1,x2,x3,x4,x5,x6] implies y = x6;
then A19:   y = xx6 by A18;
    assume x = [x1,x2,x3,x4,x5,x6] implies z = x6;
    hence thesis by A18,A19;
   end;

 end;


theorem
  X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} implies
 for x being Element of [:X1,X2,X3,X4,X5,X6:]
  for x1,x2,x3,x4,x5,x6 st x = [x1,x2,x3,x4,x5,x6] holds
   x`1 = x1 & x`2 = x2 & x`3 = x3 & x`4 = x4 & x`5 = x5 & x`6 = x6
                                           by Def3,Def4,Def5,Def6,Def7,Def8;


theorem Th21:
 X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} implies
 for x being Element of [:X1,X2,X3,X4,X5,X6:]
  holds x = [x`1,x`2,x`3,x`4,x`5,x`6]
 proof
   assume
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{};

  let x be Element of [:X1,X2,X3,X4,X5,X6:];
  consider xx1,xx2,xx3,xx4,xx5,xx6 such that

A2:  x = [xx1,xx2,xx3,xx4,xx5,xx6] by A1,Th19;

  thus x = [x`1,xx2,xx3,xx4,xx5,xx6] by A1,A2,Def3
        .= [x`1,x`2,xx3,xx4,xx5,xx6] by A1,A2,Def4
        .= [x`1,x`2,x`3,xx4,xx5,xx6] by A1,A2,Def5
        .= [x`1,x`2,x`3,x`4,xx5,xx6] by A1,A2,Def6
        .= [x`1,x`2,x`3,x`4,x`5,xx6] by A1,A2,Def7
        .= [x`1,x`2,x`3,x`4,x`5,x`6] by A1,A2,Def8;
 end;


theorem
Th22:
 X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} implies
 for x being Element of [:X1,X2,X3,X4,X5,X6:] holds
   x`1 = (x qua set)`1`1`1`1`1 &
   x`2 = (x qua set)`1`1`1`1`2 &
   x`3 = (x qua set)`1`1`1`2 &
   x`4 = (x qua set)`1`1`2 &
   x`5 = (x qua set)`1`2 &
   x`6 = (x qua set)`2
 proof
   assume
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{};

  let x be Element of [:X1,X2,X3,X4,X5,X6:];

  thus x`1
  = [ x`1, x`2]`1 by MCART_1:7
 .= [[x`1, x`2],x`3]`1`1 by MCART_1:7
 .= [ x`1, x`2 ,x`3]`1`1 by MCART_1:def 3
 .= [[x`1, x`2 ,x`3],x`4]`1`1`1 by MCART_1:7
 .= [ x`1, x`2 ,x`3 ,x`4]`1`1`1 by MCART_1:def 4
 .= [[x`1, x`2 ,x`3 ,x`4], x`5]`1`1`1`1 by MCART_1:7
 .= [ x`1, x`2 ,x`3 ,x`4 , x`5]`1`1`1`1 by MCART_2:def 1
 .= [[x`1, x`2 ,x`3 ,x`4, x`5],x`6]`1`1`1`1`1 by MCART_1:7
 .= [ x`1, x`2 ,x`3 ,x`4 ,x`5, x`6]`1`1`1`1`1 by Def1
 .= (x qua set)`1`1`1`1`1 by A1,Th21;

  thus x`2
  = [ x`1, x`2]`2 by MCART_1:7
 .= [[x`1, x`2],x`3]`1`2 by MCART_1:7
 .= [ x`1, x`2, x`3]`1`2 by MCART_1:def 3
 .= [[x`1, x`2, x`3],x`4]`1`1`2 by MCART_1:7
 .= [ x`1, x`2, x`3, x`4]`1`1`2 by MCART_1:def 4
 .= [[x`1, x`2, x`3, x`4 ], x`5]`1`1`1`2 by MCART_1:7
 .= [ x`1, x`2, x`3, x`4 , x`5]`1`1`1`2 by MCART_2:def 1
 .= [[x`1, x`2, x`3, x`4, x`5],x`6]`1`1`1`1`2 by MCART_1:7
 .= [ x`1, x`2, x`3, x`4, x`5, x`6]`1`1`1`1`2 by Def1
 .= (x qua set)`1`1`1`1`2 by A1,Th21;

  thus x`3
  = [[x`1, x`2],x`3]`2 by MCART_1:7
 .= [ x`1, x`2, x`3]`2 by MCART_1:def 3
 .= [[x`1, x`2, x`3],x`4]`1`2 by MCART_1:7
 .= [ x`1, x`2, x`3, x`4]`1`2 by MCART_1:def 4
 .= [[x`1, x`2, x`3, x`4],x`5]`1`1`2 by MCART_1:7
 .= [ x`1, x`2, x`3, x`4 ,x`5]`1`1`2 by MCART_2:def 1
 .= [[x`1, x`2, x`3, x`4, x`5],x`6]`1`1`1`2 by MCART_1:7
 .= [ x`1, x`2, x`3, x`4, x`5, x`6]`1`1`1`2 by Def1
 .= (x qua set)`1`1`1`2 by A1,Th21;

  thus x`4
  = [[x`1,x`2,x`3],x`4]`2 by MCART_1:7
 .= [ x`1,x`2,x`3, x`4]`2 by MCART_1:def 4
 .= [[x`1,x`2,x`3, x`4],x`5]`1`2 by MCART_1:7
 .= [ x`1,x`2,x`3, x`4 ,x`5]`1`2 by MCART_2:def 1
 .= [[x`1,x`2,x`3, x`4, x`5],x`6]`1`1`2 by MCART_1:7
 .= [ x`1,x`2,x`3, x`4, x`5, x`6]`1`1`2 by Def1
 .= (x qua set)`1`1`2 by A1,Th21;

  thus x`5
  = [[x`1,x`2,x`3,x`4],x`5]`2 by MCART_1:7
 .= [ x`1,x`2,x`3,x`4 ,x`5]`2 by MCART_2:def 1
 .= [[x`1,x`2,x`3,x`4,x`5],x`6]`1`2 by MCART_1:7
 .= [ x`1,x`2,x`3,x`4,x`5, x`6]`1`2 by Def1
 .= (x qua set)`1`2 by A1,Th21;

  thus x`6
  = [[x`1,x`2,x`3,x`4,x`5],x`6]`2 by MCART_1:7
 .= [ x`1,x`2,x`3,x`4,x`5, x`6]`2 by Def1
 .= (x qua set)`2 by A1,Th21;

 end;


theorem
      X1 c= [:X1,X2,X3,X4,X5,X6:]
 or X1 c= [:X2,X3,X4,X5,X6,X1:]
 or X1 c= [:X3,X4,X5,X6,X1,X2:]
 or X1 c= [:X4,X5,X6,X1,X2,X3:]
 or X1 c= [:X5,X6,X1,X2,X3,X4:]
 or X1 c= [:X6,X1,X2,X3,X4,X5:]
 implies X1 = {}
proof
  assume that
A1:  X1 c= [:X1,X2,X3,X4,X5,X6:]
 or X1 c= [:X2,X3,X4,X5,X6,X1:]
 or X1 c= [:X3,X4,X5,X6,X1,X2:]
 or X1 c= [:X4,X5,X6,X1,X2,X3:]
 or X1 c= [:X5,X6,X1,X2,X3,X4:]
 or X1 c= [:X6,X1,X2,X3,X4,X5:]
   and
A2: X1 <> {};
         [:X1,X2,X3,X4,X5,X6:]<>{}
    or [:X2,X3,X4,X5,X6,X1:]<>{}
    or [:X3,X4,X5,X6,X1,X2:]<>{}
    or [:X4,X5,X6,X1,X2,X3:]<>{}
    or [:X5,X6,X1,X2,X3,X4:]<>{}
    or [:X6,X1,X2,X3,X4,X5:]<>{}
                             by A1,A2,XBOOLE_1:3;

then A3: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} by Th15;
    now per cases by A1;
   suppose
A4:  X1 c= [:X1,X2,X3,X4,X5,X6:];
     consider v such that
A5:   v in X1 and
A6:   for x1,x2,x3,x4,x5,x6
       st x1 in X1 or x2 in X1 holds v <> [x1,x2,x3,x4,x5,x6]
                                                        by A2,Th9;
      reconsider v as Element of [:X1,X2,X3,X4,X5,X6:] by A4,A5;
        v = [v`1,v`2,v`3,v`4,v`5,v`6] & v`1 in X1 by A3,Th21;
    hence contradiction by A6;

   suppose X1 c= [:X2,X3,X4,X5,X6,X1:];
    then X1 c= [:[:X2,X3:],X4,X5,X6,X1:] by Th14;
    hence thesis by A2,MCART_2:21;

   suppose X1 c= [:X3,X4,X5,X6,X1,X2:];
    then X1 c= [:[:X3,X4:],X5,X6,X1,X2:] by Th14;
    hence thesis by A2,MCART_2:21;

   suppose X1 c= [:X4,X5,X6,X1,X2,X3:];
    then X1 c= [:[:X4,X5:],X6,X1,X2,X3:] by Th14;
    hence thesis by A2,MCART_2:21;

   suppose X1 c= [:X5,X6,X1,X2,X3,X4:];
    then X1 c= [:[:X5,X6:],X1,X2,X3,X4:] by Th14;
    hence thesis by A2,MCART_2:21;

   suppose
A7:  X1 c= [:X6,X1,X2,X3,X4,X5:];
     consider v such that
A8:   v in X1 and
A9:   for x1,x2,x3,x4,x5,x6
      st x1 in X1 or x2 in X1 holds v <> [x1,x2,x3,x4,x5,x6]
                                                    by A2,Th9;
      reconsider v as Element of [:X6,X1,X2,X3,X4,X5:] by A7,A8;
        v = [v`1,v`2,v`3,v`4,v`5,v`6] & v`2 in X1 by A3,Th21;
    hence thesis by A9;
  end;
 hence contradiction;
end;


theorem
   [:X1,X2,X3,X4,X5,X6:] meets [:Y1,Y2,Y3,Y4,Y5,Y6:] implies
   X1 meets Y1 & X2 meets Y2 & X3 meets Y3 & X4 meets Y4 & X5 meets Y5
 & X6 meets Y6
proof
A1: [:[:[:[:[:X1,X2:],X3:],X4:],X5:],X6:] = [:X1,X2,X3,X4,X5,X6:]
 & [:[:[:[:[:Y1,Y2:],Y3:],Y4:],Y5:],Y6:] = [:Y1,Y2,Y3,Y4,Y5,Y6:] by Th10;

 assume [:X1,X2,X3,X4,X5,X6:] meets [:Y1,Y2,Y3,Y4,Y5,Y6:];
  then [:[:[:[:X1,X2:],X3:],X4:],X5:] meets [:[:[:[:Y1,Y2:],Y3:],Y4:],Y5:]
                                     & X6 meets Y6 by A1,ZFMISC_1:127;
  then [:[:[:X1,X2:],X3:],X4:] meets [:[:[:Y1,Y2:],Y3:],Y4:]
                          & X5 meets Y5 & X6 meets Y6 by ZFMISC_1:127;
  then [:[:X1,X2:],X3:] meets [:[:Y1,Y2:],Y3:]
           & X4 meets Y4 & X5 meets Y5 & X6 meets Y6 by ZFMISC_1:127;
  then [:X1,X2:] meets [:Y1,Y2:] & X3 meets Y3
           & X4 meets Y4 & X5 meets Y5 & X6 meets Y6 by ZFMISC_1:127;
 hence thesis by ZFMISC_1:127;
end;

theorem [:{x1},{x2},{x3},{x4},{x5},{x6}:] = { [x1,x2,x3,x4,x5,x6] }
 proof thus
      [:{x1},{x2},{x3},{x4},{x5},{x6}:]
  = [:[:{x1},{x2}:],{x3},{x4},{x5},{x6}:] by Th14
 .= [:{[x1,x2]}, {x3},{x4},{x5},{x6}:] by ZFMISC_1:35
 .= { [[x1,x2], x3, x4, x5, x6]} by MCART_2:23
 .= { [x1,x2,x3,x4,x5,x6] } by Th7;
 end;

reserve A1 for Subset of X1,
        A2 for Subset of X2,
        A3 for Subset of X3,
        A4 for Subset of X4,
        A5 for Subset of X5,
        A6 for Subset of X6;

:: 6 - Tuples

reserve x for Element of [:X1,X2,X3,X4,X5,X6:];

theorem
   X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} implies
  for x1,x2,x3,x4,x5,x6 st x = [x1,x2,x3,x4,x5,x6]
   holds x`1 = x1 & x`2 = x2 & x`3 = x3 & x`4 = x4 & x`5 = x5
       & x`6 = x6 by Def3,Def4,Def5,Def6,Def7,Def8;

theorem
   X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} &
     (for xx1,xx2,xx3,xx4,xx5,xx6
  st x = [xx1,xx2,xx3,xx4,xx5,xx6] holds y1 = xx1)
   implies y1 =x`1
 proof
   assume that
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} and
A2:     for xx1,xx2,xx3,xx4,xx5,xx6
    st x = [xx1,xx2,xx3,xx4,xx5,xx6] holds y1 = xx1;
     x = [x`1,x`2,x`3,x`4,x`5,x`6] by A1,Th21;
  hence thesis by A2;
 end;

theorem
   X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} &
     (for xx1,xx2,xx3,xx4,xx5,xx6
  st x = [xx1,xx2,xx3,xx4,xx5,xx6] holds y2 = xx2)
   implies y2 =x`2
 proof
   assume that
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} and
A2:    for xx1,xx2,xx3,xx4,xx5,xx6
   st x = [xx1,xx2,xx3,xx4,xx5,xx6] holds y2 = xx2;
     x = [x`1,x`2,x`3,x`4,x`5,x`6] by A1,Th21;
  hence thesis by A2;
 end;

theorem
   X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} &
     (for xx1,xx2,xx3,xx4,xx5,xx6
  st x = [xx1,xx2,xx3,xx4,xx5,xx6] holds y3 = xx3)
   implies y3 =x`3
 proof
   assume that
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} and
A2:  for xx1,xx2,xx3,xx4,xx5,xx6 st
    x = [xx1,xx2,xx3,xx4,xx5,xx6] holds y3 = xx3;
     x = [x`1,x`2,x`3,x`4,x`5,x`6] by A1,Th21;
  hence thesis by A2;
 end;

theorem
   X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} &
     (for xx1,xx2,xx3,xx4,xx5,xx6
  st x = [xx1,xx2,xx3,xx4,xx5,xx6] holds y4 = xx4)
   implies y4 =x`4
 proof
   assume that
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} and
A2:    for xx1,xx2,xx3,xx4,xx5,xx6
   st x = [xx1,xx2,xx3,xx4,xx5,xx6] holds y4 = xx4;
     x = [x`1,x`2,x`3,x`4,x`5,x`6] by A1,Th21;
  hence thesis by A2;
 end;

theorem
   X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} &
     (for xx1,xx2,xx3,xx4,xx5,xx6
  st x = [xx1,xx2,xx3,xx4,xx5,xx6] holds y5 = xx5)
   implies y5 =x`5
 proof
   assume that
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} and
A2:    for xx1,xx2,xx3,xx4,xx5,xx6
   st x = [xx1,xx2,xx3,xx4,xx5,xx6] holds y5 = xx5;
     x = [x`1,x`2,x`3,x`4,x`5,x`6] by A1,Th21;
  hence thesis by A2;
 end;

theorem
   X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} &
     (for xx1,xx2,xx3,xx4,xx5,xx6
  st x = [xx1,xx2,xx3,xx4,xx5,xx6] holds y6 = xx6)
   implies y6 =x`6
 proof
   assume that
A1: X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{} and
A2:    for xx1,xx2,xx3,xx4,xx5,xx6
   st x = [xx1,xx2,xx3,xx4,xx5,xx6] holds y6 = xx6;
     x = [x`1,x`2,x`3,x`4,x`5,x`6] by A1,Th21;
  hence thesis by A2;
 end;

theorem
   z in [: X1,X2,X3,X4,X5,X6 :] implies
  ex x1,x2,x3,x4,x5,x6
  st x1 in X1 & x2 in X2 & x3 in X3 & x4 in X4 & x5 in X5
   & x6 in X6
   & z = [x1,x2,x3,x4,x5,x6]
   proof
     assume z in [: X1,X2,X3,X4,X5,X6 :];
     then z in [:[:X1,X2,X3,X4,X5:],X6:] by Def2;
     then consider x12345, x6 being set such that
A1:   x12345 in [:X1,X2,X3,X4,X5:] and
A2:   x6 in X6 and
A3:   z = [x12345,x6] by ZFMISC_1:def 2;
     consider x1, x2, x3, x4, x5 such that
A4:   x1 in X1 & x2 in X2 & x3 in X3 & x4 in X4 & x5 in X5
    & x12345 = [x1,x2,x3,x4,x5] by A1,MCART_2:30;
       z = [x1,x2,x3,x4,x5,x6] by A3,A4,Def1;
    hence thesis by A2,A4;
   end;

theorem
       [x1,x2,x3,x4,x5,x6] in [: X1,X2,X3,X4,X5,X6 :]
 iff x1 in X1 & x2 in X2 & x3 in X3 & x4 in X4 & x5 in X5
   & x6 in X6
   proof
A1: [:X1,X2,X3,X4,X5,X6:] = [:[:X1,X2:],X3,X4,X5,X6:] by Th14;
A2: [x1,x2,x3,x4,x5,x6] = [[x1,x2],x3,x4,x5,x6] by Th7;
       [x1,x2] in [:X1,X2:] iff x1 in X1 & x2 in X2 by ZFMISC_1:106;
    hence thesis by A1,A2,MCART_2:31;
   end;

theorem
   (for z holds z in Z iff
  ex x1,x2,x3,x4,x5,x6
  st x1 in X1 & x2 in X2 & x3 in X3 & x4 in X4 & x5 in X5
   & x6 in X6
   & z = [x1,x2,x3,x4,x5,x6])
  implies Z = [: X1,X2,X3,X4,X5,X6 :]
  proof
   assume
A1:   for z holds z in Z iff
       ex x1,x2,x3,x4,x5,x6
       st x1 in X1 & x2 in X2 & x3 in X3 & x4 in X4 & x5 in X5
        & x6 in X6
        & z = [x1,x2,x3,x4,x5,x6];
      now let z;
     thus z in Z implies z in [:[:X1,X2,X3,X4,X5:],X6:]
      proof
        assume z in Z; then consider x1,x2,x3,x4,x5,x6 such that
A2:      x1 in X1 & x2 in X2 & x3 in X3 & x4 in X4 & x5 in X5
      & x6 in X6
      & z = [x1,x2,x3,x4,x5,x6] by A1;
A3:      z = [[x1,x2,x3,x4,x5],x6] by A2,Def1;
          [x1,x2,x3,x4,x5] in [:X1,X2,X3,X4,X5:] & x6 in X6 by A2,MCART_2:31;
       hence z in [:[:X1,X2,X3,X4,X5:],X6:] by A3,ZFMISC_1:def 2;
      end;
     assume z in [:[:X1,X2,X3,X4,X5:],X6:];
      then consider x12345,x6 being set such that
A4:     x12345 in [:X1,X2,X3,X4,X5:] and
A5:     x6 in X6 and
A6:     z = [x12345,x6] by ZFMISC_1:def 2;
      consider x1,x2,x3,x4,x5 such that
A7:   x1 in X1 & x2 in X2 & x3 in X3 & x4 in X4 & x5 in X5
    & x12345 = [x1,x2,x3,x4,x5] by A4,MCART_2:30;
         z = [x1,x2,x3,x4,x5,x6] by A6,A7,Def1;
     hence z in Z by A1,A5,A7;
    end;
    then Z = [:[:X1,X2,X3,X4,X5:],X6:] by TARSKI:2;
   hence Z = [: X1,X2,X3,X4,X5,X6 :] by Def2;
  end;

theorem Th36:
   X1<>{} & X2<>{} & X3<>{} & X4<>{} & X5<>{} & X6<>{}
 & Y1<>{} & Y2<>{} & Y3<>{} & Y4<>{} & Y5<>{} & Y6<>{} implies
  for x being (Element of [:X1,X2,X3,X4,X5,X6:]),
      y being Element of [:Y1,Y2,Y3,Y4,Y5,Y6:]
  holds x = y
  implies x`1 = y`1 & x`2 = y`2 & x`3 = y`3 & x`4 = y`4 & x`5 = y`5
        & x`6 = y`6
 proof
   assume that
A1: X1 <> {} & X2 <> {} & X3 <> {} & X4 <> {} & X5 <> {} & X6 <> {} and
A2: Y1 <> {} & Y2 <> {} & Y3 <> {} & Y4 <> {} & Y5 <> {} & Y6 <> {};
  let x be Element of [:X1,X2,X3,X4,X5,X6:];
  let y be Element of [:Y1,Y2,Y3,Y4,Y5,Y6:];
  assume
A3: x = y;
  thus x`1 = (x qua set)`1`1`1`1`1 by A1,Th22 .= y`1 by A2,A3,Th22;
  thus x`2 = (x qua set)`1`1`1`1`2 by A1,Th22 .= y`2 by A2,A3,Th22;
  thus x`3 = (x qua set)`1`1`1`2 by A1,Th22 .= y`3 by A2,A3,Th22;
  thus x`4 = (x qua set)`1`1`2 by A1,Th22 .= y`4 by A2,A3,Th22;
  thus x`5 = (x qua set)`1`2 by A1,Th22 .= y`5 by A2,A3,Th22;
  thus x`6 = (x qua set)`2 by A1,Th22 .= y`6 by A2,A3,Th22;
 end;

theorem
   for x being Element of [:X1,X2,X3,X4,X5,X6:]
      st x in [:A1,A2,A3,A4,A5,A6:]
   holds x`1 in A1 & x`2 in A2 & x`3 in A3 & x`4 in A4 & x`5 in A5
       & x`6 in A6
 proof
   let x be Element of [:X1,X2,X3,X4,X5,X6:];
  assume
A1: x in [:A1,A2,A3,A4,A5,A6:];
   then A2: A1 <> {} & A2 <> {} & A3 <> {} & A4 <> {} & A5 <> {} & A6 <> {} by
Th15;
   reconsider y = x as Element of [:A1,A2,A3,A4,A5,A6:] by A1;
     y`1 in A1 & y`2 in A2 & y`3 in A3 & y`4 in A4 & y`5 in A5
 & y`6 in A6 by A2;
  hence x`1 in A1 & x`2 in A2 & x`3 in A3 & x`4 in A4 & x`5 in A5
      & x`6 in A6 by Th36;
 end;

theorem Th38:
 X1 c= Y1 & X2 c= Y2 & X3 c= Y3 & X4 c= Y4 & X5 c= Y5 & X6 c= Y6
  implies [:X1,X2,X3,X4,X5,X6:] c= [:Y1,Y2,Y3,Y4,Y5,Y6:]
 proof
    assume X1 c= Y1 & X2 c= Y2 & X3 c= Y3 & X4 c= Y4 & X5 c= Y5;
    then A1: [:X1,X2,X3,X4,X5:] c= [:Y1,Y2,Y3,Y4,Y5:] by MCART_2:35;
A2: [:X1,X2,X3,X4,X5,X6:] = [:[:X1,X2,X3,X4,X5:],X6:] &
    [:Y1,Y2,Y3,Y4,Y5,Y6:] = [:[:Y1,Y2,Y3,Y4,Y5:],Y6:] by Def2;
  assume X6 c= Y6;
  hence thesis by A1,A2,ZFMISC_1:119;
 end;

theorem
    [:A1,A2,A3,A4,A5,A6:] is Subset of [:X1,X2,X3,X4,X5,X6:] by Th38;


