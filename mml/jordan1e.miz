:: Upper and Lower Sequence of a Cage
::  by Robert Milewski
::
:: Received August 8, 2001
:: Copyright (c) 2001 Association of Mizar Users

environ

 vocabularies EUCLID, COMPTS_1, PSCOMP_1, FINSEQ_1, SPRECT_2, RELAT_1,
      FINSEQ_5, FINSEQ_4, ARYTM_1, PRE_TOPC, TOPREAL1, JORDAN3, BOOLE, GROUP_2,
      FUNCT_1, SPPOL_1, FINSEQ_6, JORDAN9, GRAPH_2, TOPREAL2, CARD_1, RELAT_2,
      MCART_1, GOBOARD1, JORDAN8, MATRIX_1, TREES_1, JORDAN1E, ARYTM;
 notations TARSKI, XBOOLE_0, ENUMSET1, SUBSET_1, CARD_1, NUMBERS, XCMPLX_0,
      XXREAL_0, NAT_1, NAT_D, FUNCT_1, PARTFUN1, FINSEQ_1, FINSEQ_4, FINSEQ_5,
      MATRIX_1, FINSEQ_6, STRUCT_0, GRAPH_2, PRE_TOPC, TOPREAL2, BINARITH,
      CONNSP_1, COMPTS_1, EUCLID, PSCOMP_1, SPRECT_2, GOBOARD1, TOPREAL1,
      SPPOL_1, JORDAN3, JORDAN8, JORDAN9;
 constructors PARTFUN1, XXREAL_0, NAT_1, FINSEQ_4, NEWTON, BINARITH, FINSEQ_5,
      CONNSP_1, REALSET2, TOPREAL2, PSCOMP_1, GRAPH_2, JORDAN3, JORDAN8,
      GOBRD13, JORDAN9, NAT_D;
 registrations XBOOLE_0, RELSET_1, XXREAL_0, XREAL_0, MEMBERED, FINSEQ_1,
      FINSEQ_6, STRUCT_0, TOPREAL2, SPPOL_2, SPRECT_1, SPRECT_2, REVROT_1,
      TOPREAL6, JORDAN8, JORDAN1A, CARD_1, FUNCT_1;
 requirements NUMERALS, BOOLE, SUBSET, REAL, ARITHM;
 definitions TARSKI, TOPREAL1, SPRECT_2, XBOOLE_0, PSCOMP_1;
 theorems JORDAN8, PSCOMP_1, JORDAN1A, NAT_1, GOBOARD5, FINSEQ_1, FINSEQ_2,
      SPRECT_2, FINSEQ_4, FINSEQ_5, FINSEQ_6, GOBOARD7, SPPOL_2, REVROT_1,
      TOPREAL1, SPRECT_3, JORDAN5B, JORDAN3, JORDAN9, GOBOARD1, TARSKI,
      FINSEQ_3, FUNCT_1, TOPREAL5, JORDAN10, GRAPH_2, CARD_1, CARD_2, PRE_CIRC,
      ENUMSET1, JORDAN1B, SPRECT_5, XBOOLE_0, XBOOLE_1, XREAL_1, XXREAL_0,
      EUCLID, ORDINAL1, PARTFUN1, MATRIX_1, XREAL_0;

begin  :: Preliminaries

reserve n for Element of NAT;

canceled 4;

theorem Th5:
  for f,g be FinSequence of TOP-REAL 2 st f is_in_the_area_of g
  for p be Element of TOP-REAL 2 st p in rng f holds f-:p is_in_the_area_of g
proof
  let f,g be FinSequence of TOP-REAL 2;
  assume
A1: f is_in_the_area_of g;
  let p be Element of TOP-REAL 2;
  assume
A2: p in rng f;
  let n be Element of NAT;
  assume
A3: n in dom(f-:p);
A4: p..f <= len f by A2,FINSEQ_4:31;
A5: len(f-:p) = p..f by A2,FINSEQ_5:45;
  then n in Seg(p..f) by A3,FINSEQ_1:def 3;
  then
A6: (f-:p)/.n = f/.n by A2,FINSEQ_5:46;
  n in Seg len(f-:p) by A3,FINSEQ_1:def 3;
  then 1 <= n & n <= p..f by A5,FINSEQ_1:3;
  then 1 <= n & n <= len f by A4,XXREAL_0:2;
  then n in dom f by FINSEQ_3:27;
  hence thesis by A1,A6,SPRECT_2:def 1;
end;

theorem Th6:
  for f,g be FinSequence of TOP-REAL 2 st f is_in_the_area_of g
  for p be Element of TOP-REAL 2 st p in rng f holds f:-p is_in_the_area_of g
proof
  let f,g be FinSequence of TOP-REAL 2;
  assume
A1: f is_in_the_area_of g;
  let p be Element of TOP-REAL 2;
  assume
A2: p in rng f;
  let n be Element of NAT;
  assume
A3: n in dom(f:-p);
A4: len(f:-p) = len f - p..f + 1 by A2,FINSEQ_5:53;
  n in Seg len(f:-p) by A3,FINSEQ_1:def 3;
  then
A5: 0+1 <= n & n <= len f - p..f + 1 by A4,FINSEQ_1:3;
  1 <= p..f by A2,FINSEQ_4:31;
  then
A7: 0+1 <= n-'1+p..f by XREAL_1:9;
A8: n-1 >= 0 by A5,XREAL_1:21;
  n - 1 <= len f - p..f by A5,XREAL_1:22;
  then n - 1 + p..f <= len f by XREAL_1:21;
  then n-'1+p..f <= len f by A8,XREAL_0:def 2;
  then
A9: n-'1+p..f in dom f by A7,FINSEQ_3:27;
  n-'1+1 = n by A5,XREAL_1:237;
  then (f:-p)/.n = f/.(n-'1+p..f) by A2,A3,FINSEQ_5:55;
  hence thesis by A1,A9,SPRECT_2:def 1;
end;

theorem
  for f be FinSequence of TOP-REAL 2
  for p be Point of TOP-REAL 2 st p in L~f holds L_Cut (f,p) <> {}
proof
  let f be FinSequence of TOP-REAL 2;
  let p be Point of TOP-REAL 2;
  assume that
A1: p in L~f and
A2: L_Cut (f,p) = {};
  <*p*>^mid(f,Index(p,f)+1,len f) is non empty &
  (p<>f.(Index(p,f)+1) or p=f.(Index(p,f)+1));
  then
A3: L_Cut(f,p) = mid(f,Index(p,f)+1,len f) & p=f.(Index(p,f)+1)
  by A2,JORDAN3:def 4;
  len f <> 0 by A1,TOPREAL1:28;
  then f <> {};
  then len f in dom f by FINSEQ_5:6;
  then not Index(p,f)+1 in dom f by A2,A3,SPRECT_2:11;
  then Index(p,f)+1 < 1 or Index(p,f)+1 > len f by FINSEQ_3:27;
  then Index(p,f) >= len f by NAT_1:11,13;
  hence contradiction by A1,JORDAN3:41;
end;

theorem
  for f be FinSequence of TOP-REAL 2
  for p be Point of TOP-REAL 2 st p in L~f & len R_Cut(f,p) >= 2 holds
  f.1 in L~R_Cut(f,p)
proof
  let f be FinSequence of TOP-REAL 2;
  let p be Point of TOP-REAL 2;
  assume
A1: p in L~f;
  then len f <> 0 by TOPREAL1:28;
  then len f > 0 by NAT_1:3;
  then 0+1 <= len f by NAT_1:13;
  then
A2: R_Cut(f,p).1 = f.1 by A1,JORDAN1B:4;
  assume 2 <= len R_Cut(f,p);
  hence f.1 in L~R_Cut(f,p) by A2,JORDAN3:34;
end;

theorem Th9:
  for f be FinSequence of TOP-REAL 2 st f is being_S-Seq
  for p be Point of TOP-REAL 2
  st p in L~f holds not f.1 in L~mid(f,Index(p,f)+1,len f)
proof
  let f be FinSequence of TOP-REAL 2 such that
A1: f is being_S-Seq;
  let p be Point of TOP-REAL 2 such that
A2: p in L~f and
A3: f.1 in L~mid(f,Index(p,f)+1,len f);
  len f <> 0 by A2,TOPREAL1:28;
  then
A4: f <> {};
  Index(p,f) < len f by A2,JORDAN3:41;
  then
A5: Index(p,f)+1 <= len f by NAT_1:13;
  1 in dom f by A4,FINSEQ_5:6;
  then f/.1 in L~mid(f,Index(p,f)+1,len f) by A3,PARTFUN1:def 8;
  then Index(p,f)+1 = 0+1 by A1,A5,JORDAN5B:16,NAT_1:11;
  hence contradiction by A2,JORDAN3:41;
end;

theorem Th10:
  for i,j,m,n be Element of NAT holds
  i+j = m+n & i <= m & j <= n implies i = m
proof
  let i,j,m,n be Element of NAT;
  assume that
A1: i+j = m+n and
A2: i <= m and
A3: j <= n;
  consider k be Nat such that
A4: m = i + k by A2,NAT_1:10;
  consider l be Nat such that
A5: n = j + l by A3,NAT_1:10;
  j + (l+k) = j+0 by A1,A4,A5;
  then k = 0 by NAT_1:7;
  hence i = m by A4;
end;

theorem
  for f be FinSequence of TOP-REAL 2 st f is being_S-Seq
  for p be Point of TOP-REAL 2 st p in L~f & f.1 in L~L_Cut(f,p) holds f.1 = p
proof
  let f be FinSequence of TOP-REAL 2;
  assume
A1: f is being_S-Seq;
  then
A2: f is one-to-one unfolded s.n.c. & len f >= 2 by TOPREAL1:def 10;
  then
A3: f <> 0;
  let p be Point of TOP-REAL 2 such that
A4: p in L~f and
A5: f.1 in L~L_Cut(f,p) and
A6: f.1 <> p;
  set g = mid(f,Index(p,f)+1,len f);
A7: len f in dom f by A3,FINSEQ_5:6;
  1 in dom f by A3,FINSEQ_5:6;
  then
A8: f/.1 = f.1 by PARTFUN1:def 8;
A9: not f.1 in L~g by A1,A4,Th9;
  then p<>f.(Index(p,f)+1) by A5,JORDAN3:def 4;
  then
A10: L_Cut(f,p) = <*p*>^g by JORDAN3:def 4;
  per cases;
  suppose g is empty;
    then L_Cut(f,p) = <*p*> by A10,FINSEQ_1:47;
    then len L_Cut(f,p) = 1 by FINSEQ_1:56;
    hence contradiction by A5,TOPREAL1:28;
  end;
  suppose g is non empty;
    then L~L_Cut(f,p) = LSeg(p,g/.1) \/ L~g by A10,SPPOL_2:20;
    then
A11: f.1 in LSeg(p,g/.1) by A5,A9,XBOOLE_0:def 3;
    consider i being Element of NAT such that
A12: 1 <= i & i+1 <= len(<*p*>^g) and
A13: f/.1 in LSeg(<*p*>^g,i) by A5,A8,A10,SPPOL_2:13;
A14: 1 <= Index(p,f) by A4,JORDAN3:41;
    LSeg(<*p*>^g,i) c= LSeg(f,Index(p,f)+i-'1) by A4,A12,JORDAN3:49;
    then
A15: Index(p,f)+i-'1 = 1 by A2,A13,JORDAN5B:33;
    1+1 <= Index(p,f)+i by A12,A14,XREAL_1:9;
    then
A16: Index(p,f)+i = 1+1 by A15,XREAL_1:237,XXREAL_0:2;
    then Index(p,f) = 1 by A12,A14,Th10;
    then
A17: p in LSeg(f,1) by A4,JORDAN3:42;
A18: i = 1 by A12,A14,A16,Th10;
A19: 1+1 <= len f by A1,TOPREAL1:def 10;
    then
A20: p in LSeg(f/.1,f/.(1+1)) by A17,TOPREAL1:def 5;
    2 in dom f by A19,FINSEQ_3:27;
    then g/.1 = f/.(1+1) by A7,A16,A18,SPRECT_2:12;
    hence contradiction by A6,A8,A11,A20,SPRECT_3:16;
  end;
end;

begin  :: About Upper and Lower Sequence of a Cage

definition
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  let n be Nat;
  func Upper_Seq(C,n) -> FinSequence of TOP-REAL 2 equals
  Rotate(Cage(C,n),W-min L~Cage(C,n))-:E-max L~Cage(C,n);
  coherence;
end;

theorem Th12:
  for C be compact non vertical non horizontal Subset of TOP-REAL 2
  for n be Nat holds len Upper_Seq(C,n) =
  (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n))
proof
  let C be compact non vertical non horizontal non empty Subset of TOP-REAL 2;
  let n be Nat;
  E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
  then E-max L~Cage(C,n) in rng Rotate(Cage(C,n),W-min L~Cage(C,n))
  by FINSEQ_6:96,SPRECT_2:47;
  hence len Upper_Seq(C,n) =
  (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n)) by FINSEQ_5:45;
end;

definition
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  let n be Nat;
  func Lower_Seq(C,n) -> FinSequence of TOP-REAL 2 equals
  Rotate(Cage(C,n),W-min L~Cage(C,n)):-E-max L~Cage(C,n);
  coherence;
end;

theorem Th13:
  for C be compact non vertical non horizontal Subset of TOP-REAL 2
  for n be Nat holds len Lower_Seq(C,n) =
  len Rotate(Cage(C,n),W-min L~Cage(C,n))-
  (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n))+1
proof
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  let n be Nat;
  E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
  then E-max L~Cage(C,n) in rng Rotate(Cage(C,n),W-min L~Cage(C,n))
  by FINSEQ_6:96,SPRECT_2:47;
  hence len Lower_Seq(C,n) = len Rotate(Cage(C,n),W-min L~Cage(C,n))-
  (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n))+1 by FINSEQ_5:53;
end;

registration
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  let n be Nat;
  cluster Upper_Seq(C,n) -> non empty;
  coherence
  proof
    E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
    then
A1: E-max L~Cage(C,n) in rng Rotate(Cage(C,n),W-min L~Cage(C,n))
    by FINSEQ_6:96,SPRECT_2:47;
    len Upper_Seq(C,n) = (E-max L~Cage(C,n))..Rotate(Cage(C,n),
    W-min L~Cage(C,n)) by Th12;
    then len Upper_Seq(C,n) >= 0+1 by A1,FINSEQ_4:31;
    hence thesis;
  end;
  cluster Lower_Seq(C,n) -> non empty;
  coherence
  proof
    E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
    then
A2: E-max L~Cage(C,n) in rng Rotate(Cage(C,n),W-min L~Cage(C,n))
    by FINSEQ_6:96,SPRECT_2:47;
    len Lower_Seq(C,n) = len Rotate(Cage(C,n),W-min L~Cage(C,n))-
    (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n))+1 by Th13;
    then len Rotate(Cage(C,n),W-min L~Cage(C,n))-len Lower_Seq(C,n)+1 <=
    len Rotate(Cage(C,n),W-min L~Cage(C,n)) by A2,FINSEQ_4:31;
    then len Rotate(Cage(C,n),W-min L~Cage(C,n))-len Lower_Seq(C,n) <=
    len Rotate(Cage(C,n),W-min L~Cage(C,n))-1 by XREAL_1:21;
    then len Lower_Seq(C,n) >= 0+1 by XREAL_1:12;
    hence thesis;
  end;
end;

registration
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  let n be Element of NAT;
  cluster Upper_Seq(C,n) -> one-to-one special unfolded s.n.c.;
  coherence
  proof
    set f = Rotate(Cage(C,n),W-min L~Cage(C,n));
A1: W-min L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:47;
    E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
    then
A2: E-max L~Cage(C,n) in rng f by FINSEQ_6:96,SPRECT_2:47;
A3: len Upper_Seq(C,n) = (E-max L~Cage(C,n))..f by Th12;
    now
      let i1,j1 be set;
      assume that
A4:   i1 in dom Upper_Seq(C,n) and
A5:   j1 in dom Upper_Seq(C,n) and
A6:   Upper_Seq(C,n).i1 = Upper_Seq(C,n).j1 and
A7:   i1 <> j1;
A8:   L~Cage(C,n) = L~f by REVROT_1:33;
      reconsider i2 = i1, j2 = j1 as Element of NAT by A4,A5;
A9:   i2 in Seg((E-max L~Cage(C,n))..f) & j2 in Seg((E-max L~Cage(C,n))..f)
      by A3,A4,A5,FINSEQ_1:def 3;
A10:  Upper_Seq(C,n).i1 = Upper_Seq(C,n)/.i2 by A4,PARTFUN1:def 8
        .= f/.i2 by A2,A9,FINSEQ_5:46;
A11:  Upper_Seq(C,n).j1 = Upper_Seq(C,n)/.j2 by A5,PARTFUN1:def 8
        .= f/.j2 by A2,A9,FINSEQ_5:46;
A12:  1 <= i2 & 1 <= j2 by A9,FINSEQ_1:3;
      j2 <= (E-max L~Cage(C,n))..f & i2 <= (E-max L~Cage(C,n))..f
      by A9,FINSEQ_1:3;
      then
A13:  j2 < len f & i2 < len f by A8,SPRECT_5:17,XXREAL_0:2;
      per cases by A7,XXREAL_0:1;
      suppose i2 < j2;
        hence contradiction by A6,A10,A11,A12,A13,GOBOARD7:38;
      end;
      suppose j2 < i2;
        hence contradiction by A6,A10,A11,A12,A13,GOBOARD7:38;
      end;
    end;
    hence Upper_Seq(C,n) is one-to-one by FUNCT_1:def 8;
    thus Upper_Seq(C,n) is special;
    thus Upper_Seq(C,n) is unfolded;
    let i,j be Nat;
    assume
A14: i+1 < j;
    len f > 0 by NAT_1:3;
    then 0+1 <= len f by NAT_1:13;
    then
A15: len f in dom f by FINSEQ_3:27;
A16: i in NAT & j in NAT by ORDINAL1:def 13;
    now per cases;
      suppose
A17:    j+1 <= len Upper_Seq(C,n);
        then
A18:    LSeg(Upper_Seq(C,n),j) = LSeg(f,j) by SPPOL_2:9;
        j < len Upper_Seq(C,n) by A17,NAT_1:13;
        then
A19:    LSeg(Upper_Seq(C,n),i) = LSeg(f,i) by A14,SPPOL_2:9,XXREAL_0:2;
A20:    j+1 <= (E-max L~Cage(C,n))..f by A2,A17,FINSEQ_5:45;
A21:    (E-max L~Cage(C,n))..f <= len f by A2,FINSEQ_4:31;
        now per cases by A20,XXREAL_0:1;
          suppose j+1 < (E-max L~Cage(C,n))..f;
            hence j+1 < len f by A21,XXREAL_0:2;
          end;
          suppose
A22:        j+1 = (E-max L~Cage(C,n))..f;
            assume j+1 >= len f;
            then (E-max L~Cage(C,n))..f = len f by A21,A22,XXREAL_0:1;
            then E-max L~Cage(C,n) = f.(len f) by A2,FINSEQ_4:29
              .= f/.(len f) by A15,PARTFUN1:def 8
              .= f/.1 by FINSEQ_6:def 1
              .= W-min L~Cage(C,n) by A1,FINSEQ_6:98;
            hence contradiction by TOPREAL5:25;
          end;
        end;
        hence LSeg(Upper_Seq(C,n),i) misses LSeg(Upper_Seq(C,n),j)
        by A14,A16,A18,A19,GOBOARD5:def 4;
      end;
      suppose j+1 > len Upper_Seq(C,n);
        then LSeg(Upper_Seq(C,n),j) = {} by TOPREAL1:def 5;
        then LSeg(Upper_Seq(C,n),i) /\ LSeg(Upper_Seq(C,n),j) = {};
        hence LSeg(Upper_Seq(C,n),i) misses LSeg(Upper_Seq(C,n),j)
        by XBOOLE_0:def 7;
      end;
    end;
    hence thesis;
  end;
  cluster Lower_Seq(C,n) -> one-to-one special unfolded s.n.c.;
  coherence
  proof
    E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
    then
A23: E-max L~Cage(C,n) in rng Rotate(Cage(C,n),W-min L~Cage(C,n))
    by FINSEQ_6:96,SPRECT_2:47;
    set f = Rotate(Cage(C,n),W-min L~Cage(C,n));
A24: W-min L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:47;
    E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
    then
A25: E-max L~Cage(C,n) in rng f by FINSEQ_6:96,SPRECT_2:47;
A26: len Lower_Seq(C,n) = len f-(E-max L~Cage(C,n))..f+1 by Th13;
A27: L~Cage(C,n) = L~f by REVROT_1:33;
    then
A28: f/.1 = W-min L~f by A24,FINSEQ_6:98;
    then (W-max L~Cage(C,n))..f <= (N-min L~Cage(C,n))..f by A27,SPRECT_5:24;
    then (N-min L~Cage(C,n))..f > 1 by A27,A28,SPRECT_5:23,XXREAL_0:2;
    then (N-max L~Cage(C,n))..f > 1 by A27,A28,SPRECT_5:25,XXREAL_0:2;
    then
A29: (E-max L~Cage(C,n))..f > 1 by A27,A28,SPRECT_5:26,XXREAL_0:2;
    now
      let i1,j1 be set;
      assume that
A30:  i1 in dom Lower_Seq(C,n) and
A31:  j1 in dom Lower_Seq(C,n) and
A32:  Lower_Seq(C,n).i1 = Lower_Seq(C,n).j1 and
A33:  i1 <> j1;
      reconsider i2 = i1, j2 = j1 as Element of NAT by A30,A31;
A34:  i2 in Seg len Lower_Seq(C,n) & j2 in Seg len Lower_Seq(C,n)
      by A30,A31,FINSEQ_1:def 3;
      then i2 >= 1 & j2 >= 1 by FINSEQ_1:3;
      then
A35:  i2 = i2-'1+1 & j2 = j2-'1+1 by XREAL_1:237;
A36:  Lower_Seq(C,n).i1 = Lower_Seq(C,n)/.i2 by A30,PARTFUN1:def 8
        .= f/.(i2-'1+(E-max L~Cage(C,n))..f) by A25,A30,A35,FINSEQ_5:55;
A37:  Lower_Seq(C,n).j1 = Lower_Seq(C,n)/.j2 by A31,PARTFUN1:def 8
        .= f/.(j2-'1+(E-max L~Cage(C,n))..f) by A25,A31,A35,FINSEQ_5:55;
      0+1 <= i2 & 0+1 <= j2 by A34,FINSEQ_1:3;
      then
A38:  i2-1 >= 0 & j2-1 >= 0 by XREAL_1:21;
      j2 <= len f-(E-max L~Cage(C,n))..f+1 &
      i2 <= len f-(E-max L~Cage(C,n))..f+1 by A26,A34,FINSEQ_1:3;
      then j2-1 <= len f-(E-max L~Cage(C,n))..f &
      i2-1 <= len f-(E-max L~Cage(C,n))..f by XREAL_1:22;
      then j2-1+(E-max L~Cage(C,n))..f <= len f &
      i2-1+(E-max L~Cage(C,n))..f <= len f by XREAL_1:21;
      then
A39:  j2-'1+(E-max L~Cage(C,n))..f <= len f &
      i2-'1+(E-max L~Cage(C,n))..f <= len f by A38,XREAL_0:def 2;
      i2-'1+(E-max L~Cage(C,n))..f >= (E-max L~Cage(C,n))..f &
      j2-'1+(E-max L~Cage(C,n))..f >= (E-max L~Cage(C,n))..f by NAT_1:11;
      then
A40:  1 < i2-'1+(E-max L~Cage(C,n))..f &
      1 < j2-'1+(E-max L~Cage(C,n))..f by A29,XXREAL_0:2;
      per cases by A33,XXREAL_0:1;
      suppose i2 < j2;
        then i2-1 < j2-1 by XREAL_1:11;
        then i2-1 < j2-'1 by XREAL_0:def 2;
        then i2-'1 < j2-'1 by A38,XREAL_0:def 2;
        then i2-'1+(E-max L~Cage(C,n))..f < j2-'1+(E-max L~Cage(C,n))..f
        by XREAL_1:8;
        hence contradiction by A32,A36,A37,A39,A40,GOBOARD7:39;
      end;
      suppose j2 < i2;
        then j2-1 < i2-1 by XREAL_1:11;
        then j2-1 < i2-'1 by XREAL_0:def 2;
        then j2-'1 < i2-'1 by A38,XREAL_0:def 2;
        then j2-'1+(E-max L~Cage(C,n))..f < i2-'1+(E-max L~Cage(C,n))..f
        by XREAL_1:8;
        hence contradiction by A32,A36,A37,A39,A40,GOBOARD7:39;
      end;
    end;
    hence Lower_Seq(C,n) is one-to-one by FUNCT_1:def 8;
    thus Lower_Seq(C,n) is special by A23,SPPOL_2:41;
    thus Lower_Seq(C,n) is unfolded by A23,SPPOL_2:28;
    let i,j be Nat;
    assume
A41: i+1 < j;
A42: i+1 >= 1 by NAT_1:11;
    j = j-'1+1 by A41,A42,XREAL_1:237,XXREAL_0:2;
    then
A44: LSeg(Lower_Seq(C,n),j) = LSeg(f,j-'1+(E-max L~Cage(C,n))..f)
    by A23,SPPOL_2:10;
    now per cases by NAT_1:2;
      suppose i > 0;
        then
A45:    i >= 0+1 by NAT_1:13;
        then i = i-'1+1 by XREAL_1:237;
        then
A46:    LSeg(Lower_Seq(C,n),i) = LSeg(f,i-'1+(E-max L~Cage(C,n)).. f)
        by A25,SPPOL_2:10;
A48:    i-1 >= 0 by A45,XREAL_1:21;
        i+1-1 < j-1 by A41,XREAL_1:11;
        then i-1+1 < j-'1 by XREAL_0:def 2;
        then i-'1+1 < j-'1 by A48,XREAL_0:def 2;
        then i-'1+1+(E-max L~Cage(C,n))..f < j-'1+(E-max L~Cage(C,n))..f
        by XREAL_1:8;
        then
A49:    i-'1+(E-max L~Cage(C,n))..f+1 < j-'1+(E-max L~Cage(C,n)).. f;
A50:    i-'1+(E-max L~Cage(C,n))..f > 0+1 by A29,XREAL_1:10;
        now per cases;
          suppose j-'1+(E-max L~Cage(C,n))..f+1 <= len f;
            then j-'1+(E-max L~Cage(C,n))..f < len f by NAT_1:13;
            then LSeg(Lower_Seq(C,n),i) misses LSeg(Lower_Seq(C,n),j)
            by A44,A46,A49,A50,GOBOARD5:def 4;
            hence LSeg(Lower_Seq(C,n),i) /\ LSeg(Lower_Seq(C,n),j) = {}
            by XBOOLE_0:def 7;
          end;
          suppose j-'1+(E-max L~Cage(C,n))..f+1 > len f;
            then LSeg(Lower_Seq(C,n),j) = {} by A44,TOPREAL1:def 5;
            hence LSeg(Lower_Seq(C,n),i) /\ LSeg(Lower_Seq(C,n),j) = {};
          end;
        end;
        hence LSeg(Lower_Seq(C,n),i) /\ LSeg(Lower_Seq(C,n),j) = {};
      end;
      suppose i = 0;
        then LSeg(Lower_Seq(C,n),i) = {} by TOPREAL1:def 5;
        hence LSeg(Lower_Seq(C,n),i) /\ LSeg(Lower_Seq(C,n),j) = {};
      end;
    end;
    hence thesis by XBOOLE_0:def 7;
  end;
end;

theorem Th14:
  for C be compact non vertical non horizontal Subset of TOP-REAL 2
  for n be Element of NAT holds
  len Upper_Seq(C,n) + len Lower_Seq(C,n) = len Cage(C,n)+1
proof
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  let n be Element of NAT;
  thus len Upper_Seq(C,n) + len Lower_Seq(C,n) =
  (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n))+
  len Lower_Seq(C,n) by Th12
    .= (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n))+
  (len Rotate(Cage(C,n),W-min L~Cage(C,n))-
  (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n))+1) by Th13
    .= (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n))+
  len Rotate(Cage(C,n),W-min L~Cage(C,n))-
  (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n))+1
    .= len Cage(C,n)+1 by REVROT_1:14;
end;

theorem Th15:
  for C be compact non vertical non horizontal Subset of TOP-REAL 2
  for n be Element of NAT holds
  Rotate(Cage(C,n),W-min L~Cage(C,n)) = Upper_Seq(C,n) ^' Lower_Seq(C,n)
proof
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  let n be Element of NAT;
A1: len (Upper_Seq(C,n) ^' Lower_Seq(C,n)) +1 =
  len Upper_Seq(C,n) + len Lower_Seq(C,n) by GRAPH_2:13
    .= len Cage(C,n)+1 by Th14
    .= len Rotate(Cage(C,n),W-min L~Cage(C,n))+1 by REVROT_1:14;
X: dom Rotate(Cage(C,n),W-min L~Cage(C,n))
       = Seg len Rotate(Cage(C,n),W-min L~Cage(C,n)) by FINSEQ_1:def 3;

  now
    let i be Nat;
    assume
A2: i in dom Rotate(Cage(C,n),W-min L~Cage(C,n));
    then
A3: 1 <= i & i <= len Rotate(Cage(C,n),W-min L~Cage(C,n)) by X,FINSEQ_1:3;
    E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
    then
A4: E-max L~Cage(C,n) in rng Rotate(Cage(C,n),W-min L~Cage(C,n))
    by FINSEQ_6:96,SPRECT_2:47;
A5: i in dom Rotate(Cage(C,n),W-min L~Cage(C,n)) by A2;
    per cases;
    suppose
A6:   i <= len Upper_Seq(C,n);
      then i <= (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n))
      by Th12;
      then
A7:   i in Seg ((E-max L~Cage(C,n))..Rotate(Cage(C,n),
      W-min L~Cage(C,n))) by A3,FINSEQ_1:3;
      len (Rotate(Cage(C,n),W-min L~Cage(C,n))-:E-max L~Cage(C,n)) =
      (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n))
      by A4,FINSEQ_5:45;
      then
A8:   i in dom (Rotate(Cage(C,n),W-min L~Cage(C,n))-:
      E-max L~Cage(C,n)) by A7,FINSEQ_1:def 3;
      i in NAT by ORDINAL1:def 13;
      hence (Upper_Seq(C,n) ^' Lower_Seq(C,n)).i =

(Rotate(Cage(C,n),W-min L~Cage(C,n))-:E-max L~Cage(C,n)).i by A3,A6,GRAPH_2:14
        .= (Rotate(Cage(C,n),W-min L~Cage(C,n))-:E-max L~Cage(C,n))/.i
      by A8,PARTFUN1:def 8
        .= Rotate(Cage(C,n),W-min L~Cage(C,n))/.i by A4,A7,FINSEQ_5:46
        .= Rotate(Cage(C,n),W-min L~Cage(C,n)).i by A5,PARTFUN1:def 8;
    end;
    suppose i > len Upper_Seq(C,n);
      then i >= len Upper_Seq(C,n)+1 by NAT_1:13;
      then consider j be Nat such that
A9:   i = len Upper_Seq(C,n)+1+j by NAT_1:10;
      reconsider j as Element of NAT by ORDINAL1:def 13;
A10:  i = len Upper_Seq(C,n)+(j+1) by A9;
      i < len (Upper_Seq(C,n) ^' Lower_Seq(C,n))+1 by A1,A3,NAT_1:13;
      then i < len Lower_Seq(C,n) + len Upper_Seq(C,n) by GRAPH_2:13;
      then
A11:  i-len Upper_Seq(C,n) < len Lower_Seq(C,n) by XREAL_1:21;
A12:  i = (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n))+(j+1)
      by A10,Th12;
A13:  j+1+1 >= 1 by NAT_1:11;
A14:  len (Rotate(Cage(C,n),W-min L~Cage(C,n)):-E-max L~Cage(C,n)) =
      len Rotate(Cage(C,n),W-min L~Cage(C,n))-
      (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n))+1
      by A4,FINSEQ_5:53;
      j+1+(E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n)) <=
      len Rotate(Cage(C,n),W-min L~Cage(C,n)) by A3,A10,Th12;
      then j+1 <= len Rotate(Cage(C,n),W-min L~Cage(C,n))-
      (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n)) by XREAL_1:21;
      then j+1+1 <= len (Rotate(Cage(C,n),W-min L~Cage(C,n)):-
      (E-max L~Cage(C,n))) by A14,XREAL_1:9;
      then
A15:  j+1+1 in dom (Rotate(Cage(C,n),W-min L~Cage(C,n)):-
      (E-max L~Cage(C,n))) by A13,FINSEQ_3:27;
      thus (Upper_Seq(C,n) ^' Lower_Seq(C,n)).i =
      (Rotate(Cage(C,n),W-min L~Cage(C,n)):-(E-max L~Cage(C,n))).
      (j+1+1) by A10,A11,GRAPH_2:15,NAT_1:11
        .= (Rotate(Cage(C,n),W-min L~Cage(C,n)):-(E-max L~Cage(C,n)))/.
      (j+1+1) by A15,PARTFUN1:def 8
        .= Rotate(Cage(C,n),W-min L~Cage(C,n))/.
      (j+1+(E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n)))
      by A4,A15,FINSEQ_5:55
        .= Rotate(Cage(C,n),W-min L~Cage(C,n)).i by A5,A12,PARTFUN1:def 8;
    end;
  end;
  hence Rotate(Cage(C,n),W-min L~Cage(C,n)) =
  Upper_Seq(C,n) ^' Lower_Seq(C,n) by A1,FINSEQ_2:10;
end;

theorem
  for C be compact non vertical non horizontal Subset of TOP-REAL 2
  for n be Element of NAT holds
  L~Cage(C,n) = L~(Upper_Seq(C,n) ^' Lower_Seq(C,n))
proof
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  let n be Element of NAT;
  Rotate(Cage(C,n),W-min L~Cage(C,n)) = Upper_Seq(C,n) ^' Lower_Seq(C,n)
  by Th15;
  hence L~Cage(C,n) = L~(Upper_Seq(C,n) ^' Lower_Seq(C,n)) by REVROT_1:33;
end;

theorem
  for C be compact non vertical non horizontal non empty Subset of TOP-REAL 2
  for n be Element of NAT holds
  L~Cage(C,n) = L~Upper_Seq(C,n) \/ L~Lower_Seq(C,n)
proof
  let C be compact non vertical non horizontal non empty Subset of TOP-REAL 2;
  let n be Element of NAT;
  E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
  then E-max L~Cage(C,n) in rng Rotate(Cage(C,n),W-min L~Cage(C,n))
  by FINSEQ_6:96,SPRECT_2:47;
  then L~Rotate(Cage(C,n),W-min L~Cage(C,n)) =
  L~Upper_Seq(C,n) \/ L~Lower_Seq(C,n) by SPPOL_2:25;
  hence thesis by REVROT_1:33;
end;

theorem Th18:
  for P be Simple_closed_curve holds W-min(P) <> E-min(P)
proof
  let P be Simple_closed_curve;
  now
    assume W-min(P) = E-min(P);
    then W-bound P= E-bound P by SPPOL_2:1;
    hence contradiction by TOPREAL5:23;
  end;
  hence W-min(P) <> E-min(P);
end;

theorem Th19:
  for C be compact non vertical non horizontal Subset of TOP-REAL 2
  for n be Element of NAT holds
  len Upper_Seq(C,n) >= 3 & len Lower_Seq(C,n) >= 3
proof
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  let n be Element of NAT;
  set pWi = W-min L~Cage(C,n);
  set pWa = W-max L~Cage(C,n);
  set pEi = E-min L~Cage(C,n);
  set pEa = E-max L~Cage(C,n);
A1: pWi <> pWa by SPRECT_2:62;
A2: pWa <> pEa by JORDAN1B:6;
  pWi <> pEa by TOPREAL5:25;
  then
A3: card {pWi,pWa,pEa} = 3 by A1,A2,CARD_2:77;
A4: W-min L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:47;
  E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
  then
A5: E-max L~Cage(C,n) in rng Rotate(Cage(C,n),W-min L~Cage(C,n))
  by FINSEQ_6:96,SPRECT_2:47;
  (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n)) <=
  (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n));
  then
A6: pEa in rng Upper_Seq(C,n) by A5,FINSEQ_5:49;
A7: Upper_Seq(C,n)/.1 = Rotate(Cage(C,n),W-min L~Cage(C,n))/.1
  by A5,FINSEQ_5:47;
  then
A8: Upper_Seq(C,n)/.1 = W-min L~Cage(C,n) by A4,FINSEQ_6:98;
  then
A9: pWi in rng Upper_Seq(C,n) by FINSEQ_6:46;
  pWa in rng Cage(C,n) by SPRECT_2:48;
  then
A10: pWa in rng Rotate(Cage(C,n),W-min L~Cage(C,n)) by FINSEQ_6:96,SPRECT_2:47;
  set f=Rotate(Cage(C,n),W-min L~Cage(C,n));
A11: L~Cage(C,n) = L~f by REVROT_1:33;
  then (W-max L~f)..f <= (N-min L~f)..f by A4,A7,FINSEQ_6:98,SPRECT_5:24;
  then
A12: (W-max L~f)..f < (N-max L~f)..f by A7,A8,A11,SPRECT_5:25,XXREAL_0:2;
  (N-max L~f)..f <= (E-max L~f)..f by A4,A7,A11,FINSEQ_6:98,SPRECT_5:26;
  then
A13: pWa in rng Upper_Seq(C,n) by A5,A10,A11,A12,FINSEQ_5:49,XXREAL_0:2;
  {pWi,pWa,pEa} c= rng Upper_Seq(C,n)
  proof
    let x be set;
    assume x in {pWi,pWa,pEa};
    hence x in rng Upper_Seq(C,n) by A6,A9,A13,ENUMSET1:def 1;
  end;
  then
A14: card {pWi,pWa,pEa} c= card rng Upper_Seq(C,n) by CARD_1:27;
  card rng Upper_Seq(C,n) c= card dom Upper_Seq(C,n) by CARD_2:80;
  then card rng Upper_Seq(C,n) c= len Upper_Seq(C,n) by PRE_CIRC:21;
  then 3 c= len Upper_Seq(C,n) by A3,A14,XBOOLE_1:1;
  hence len Upper_Seq(C,n) >= 3 by NAT_1:40;
A15: pWi <> pEi by Th18;
A16: pEi <> pEa by SPRECT_2:58;
  pWi <> pEa by TOPREAL5:25;
  then
A17: card {pWi,pEi,pEa} = 3 by A15,A16,CARD_2:77;
A18: pEa in rng Lower_Seq(C,n) by FINSEQ_6:66;
  Lower_Seq(C,n)/.(len Lower_Seq(C,n)) = Rotate(Cage(C,n),W-min L~Cage(C,n))/.
  (len Rotate(Cage(C,n),W-min L~Cage(C,n))) by A5,FINSEQ_5:57
    .= Rotate(Cage(C,n),W-min L~Cage(C,n))/.1 by FINSEQ_6:def 1
    .= W-min L~Cage(C,n) by A4,FINSEQ_6:98;
  then
A19: pWi in rng Lower_Seq(C,n) by REVROT_1:3;
  pEi in rng Cage(C,n) by SPRECT_2:49;
  then
A20: pEi in rng Rotate(Cage(C,n),W-min L~Cage(C,n)) by FINSEQ_6:96,SPRECT_2:47;
  pEi..Rotate(Cage(C,n),W-min L~Cage(C,n)) >

pEa..Rotate(Cage(C,n),W-min L~Cage(C,n)) by A4,A7,A11,FINSEQ_6:98,SPRECT_5:27;
  then
A21: pEi in rng Lower_Seq(C,n) by A5,A20,FINSEQ_6:67;
  {pWi,pEi,pEa} c= rng Lower_Seq(C,n)
  proof
    let x be set;
    assume x in {pWi,pEi,pEa};
    hence x in rng Lower_Seq(C,n) by A18,A19,A21,ENUMSET1:def 1;
  end;
  then
A22: card {pWi,pEi,pEa} c= card rng Lower_Seq(C,n) by CARD_1:27;
  card rng Lower_Seq(C,n) c= card dom Lower_Seq(C,n) by CARD_2:80;
  then card rng Lower_Seq(C,n) c= len Lower_Seq(C,n) by PRE_CIRC:21;
  then 3 c= len Lower_Seq(C,n) by A17,A22,XBOOLE_1:1;
  hence len Lower_Seq(C,n) >= 3 by NAT_1:40;
end;

registration
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  let n be Element of NAT;
  cluster Upper_Seq(C,n) -> being_S-Seq;
  coherence
  proof
    len Upper_Seq(C,n) >= 3 by Th19;
    then len Upper_Seq(C,n) >= 2 by XXREAL_0:2;
    hence thesis by TOPREAL1:def 10;
  end;
  cluster Lower_Seq(C,n) -> being_S-Seq;
  coherence
  proof
    len Lower_Seq(C,n) >= 3 by Th19;
    then len Lower_Seq(C,n) >= 2 by XXREAL_0:2;
    hence thesis by TOPREAL1:def 10;
  end;
end;

theorem
  for C be compact non vertical non horizontal Subset of TOP-REAL 2
  for n be Element of NAT holds L~Upper_Seq(C,n) /\ L~Lower_Seq(C,n) =
  {W-min L~Cage(C,n),E-max L~Cage(C,n)}
proof
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  let n be Element of NAT;
A1: W-min L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:47;
  E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
  then
A2: E-max L~Cage(C,n) in rng Rotate(Cage(C,n),W-min L~Cage(C,n))
  by FINSEQ_6:96,SPRECT_2:47;
A3: (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n)) <=
  (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n));
  len Lower_Seq(C,n) >= 3 by Th19;
  then
A4: rng Lower_Seq(C,n) c= L~Lower_Seq(C,n) by SPPOL_2:18,XXREAL_0:2;
A5: E-max L~Cage(C,n) in rng Lower_Seq(C,n) by FINSEQ_6:66;
A6: len Upper_Seq(C,n) >= 3 by Th19;
  then
A7: rng Upper_Seq(C,n) c= L~Upper_Seq(C,n) by SPPOL_2:18,XXREAL_0:2;
A8: E-max L~Cage(C,n) in rng Upper_Seq(C,n) by A2,A3,FINSEQ_5:49;
  Upper_Seq(C,n)/.1 = Rotate(Cage(C,n),W-min L~Cage(C,n))/.1 by A2,FINSEQ_5:47
    .= W-min L~Cage(C,n) by A1,FINSEQ_6:98;
  then
A9: W-min L~Cage(C,n) in rng Upper_Seq(C,n) by FINSEQ_6:46;
  Lower_Seq(C,n)/.(len Lower_Seq(C,n)) = Rotate(Cage(C,n),W-min L~Cage(C,n))/.
  (len Rotate(Cage(C,n),W-min L~Cage(C,n))) by A2,FINSEQ_5:57
    .= Rotate(Cage(C,n),W-min L~Cage(C,n))/.1 by FINSEQ_6:def 1
    .= W-min L~Cage(C,n) by A1,FINSEQ_6:98;
  then
A10: W-min L~Cage(C,n) in rng Lower_Seq(C,n) by REVROT_1:3;
  thus L~Upper_Seq(C,n) /\ L~Lower_Seq(C,n) c=
  {W-min L~Cage(C,n),E-max L~Cage(C,n)}
  proof
    let x be set;
    assume
A11: x in L~Upper_Seq(C,n) /\ L~Lower_Seq(C,n);
    then
A12: x in L~Upper_Seq(C,n) & x in L~Lower_Seq(C,n) by XBOOLE_0:def 4;
    reconsider x1=x as Point of TOP-REAL 2 by A11;
    consider i1 be Element of NAT such that
A13: 1 <= i1 & i1+1 <= len Upper_Seq(C,n) and
A14: x1 in LSeg(Upper_Seq(C,n),i1) by A12,SPPOL_2:13;
    consider i2 be Element of NAT such that
A15: 1 <= i2 & i2+1 <= len Lower_Seq(C,n) and
A16: x1 in LSeg(Lower_Seq(C,n),i2) by A12,SPPOL_2:13;
    set f=Rotate(Cage(C,n),W-min L~Cage(C,n));
    set pE=E-max L~Cage(C,n);
    set pW=W-min L~Cage(C,n);
A17: LSeg(Upper_Seq(C,n),i1) = LSeg(f,i1) by A13,SPPOL_2:9;
    set i3=i2-'1;
A18: i3+1 = i2 by A15,XREAL_1:237;
    then
A19: LSeg(Lower_Seq(C,n),i2) = LSeg(f,i3+pE..f) by A2,SPPOL_2:10;
A20: len Upper_Seq(C,n) = pE..f by Th12;
A21: len Lower_Seq(C,n) = len f-pE..f+1 by Th13;
A22: i2-1 >= 1-1 by A15,XREAL_1:11;
    i2 < len f-pE..f+1 by A15,A21,NAT_1:13;
    then i2-1 < len f-pE..f by XREAL_1:21;
    then i2-1+pE..f < len f by XREAL_1:22;
    then
A23: i3+pE..f < len f by A22,XREAL_0:def 2;
A25: pE..f >= 1 by A2,FINSEQ_4:31;
    i3+1 < len f-pE..f+1 by A15,A18,A21,NAT_1:13;
    then i3 < len f-pE..f by XREAL_1:9;
    then
A26: i3+pE..f < len f by XREAL_1:22;
A27: i1+1 < pE..f+1 by A13,A20,NAT_1:13;
    1+pE..f <= i3+1+pE..f by A15,A18,XREAL_1:9;
    then i1+1 < i3+pE..f+1 by A27,XXREAL_0:2;
    then
A28: i1+1 <= i3+pE..f by NAT_1:13;
A29: i3+pE..f+1 <= len f by A26,NAT_1:13;
A30: i3+pE..f >= 0 & i1 >= 0 by NAT_1:2;
A31: pE..f-'1+1 = pE..f by A25,XREAL_1:237;
    assume
A32: not x in {W-min L~Cage(C,n),E-max L~Cage(C,n)};
    now per cases by A13,A28,XXREAL_0:1;
      suppose i1+1 < i3+pE..f & i1 > 1;
        then LSeg(f,i1) misses LSeg(f,i3+pE..f) by A26,GOBOARD5:def 4;
        then LSeg(f,i1) /\ LSeg(f,i3+pE..f) = {} by XBOOLE_0:def 7;
        hence contradiction by A14,A16,A17,A19,XBOOLE_0:def 4;
      end;
      suppose
A33:    i1 = 1;
        i3+pE..f >= 0+3 by A6,A20,XREAL_1:9;
        then
A34:    i1+1 < i3+pE..f by A33,XXREAL_0:2;
        now per cases by A29,XXREAL_0:1;
          suppose i3+pE..f+1 < len f;
            then LSeg(f,i1) misses LSeg(f,i3+pE..f) by A34,GOBOARD5:def 4;
            then LSeg(f,i1) /\ LSeg(f,i3+pE..f) = {} by XBOOLE_0:def 7;
            hence contradiction by A14,A16,A17,A19,XBOOLE_0:def 4;
          end;
          suppose i3+pE..f+1 = len f;
            then i3+pE..f = len f-1;
            then i3+pE..f = len f-'1 by A30,XREAL_0:def 2;
            then LSeg(f,i1) /\ LSeg(f,i3+pE..f) = {f/.1} by A33,GOBOARD7:36
,REVROT_1:30;
            then x1 in {f/.1} by A14,A16,A17,A19,XBOOLE_0:def 4;
            then x1 = f/.1 by TARSKI:def 1
              .= pW by A1,FINSEQ_6:98;
            hence contradiction by A32,TARSKI:def 2;
          end;
        end;
        hence contradiction;
      end;
      suppose
A35:    i1+1 = i3+pE..f;
        0+pE..f <= i3+pE..f by XREAL_1:9;
        then
A36:    pE..f = i1+1 by A13,A20,A35,XXREAL_0:1;
        i3+pE..f >= pE..f by NAT_1:11;
        then pE..f < len f by A23,XXREAL_0:2;
        then pE..f+1 <= len f by NAT_1:13;
        then pE..f-'1 + (1+1) <= len f by A31;
        then LSeg(f,i1) /\ LSeg(f,i3+(pE..f)) = {f/.(pE..f)}
        by A13,A31,A35,A36,TOPREAL1:def 8;
        then x1 in {f/.(pE..f)} by A14,A16,A17,A19,XBOOLE_0:def 4;
        then x1 = f/.(pE..f) by TARSKI:def 1
          .= pE by A2,FINSEQ_5:41;
        hence contradiction by A32,TARSKI:def 2;
      end;
    end;
    hence contradiction;
  end;
  thus {W-min L~Cage(C,n),E-max L~Cage(C,n)} c=
  L~Upper_Seq(C,n) /\ L~Lower_Seq(C,n)
  proof
    let x be set;
    assume
A37: x in {W-min L~Cage(C,n),E-max L~Cage(C,n)};
    per cases by A37,TARSKI:def 2;
    suppose x = W-min L~Cage(C,n);
      hence x in L~Upper_Seq(C,n) /\ L~Lower_Seq(C,n)
      by A4,A7,A9,A10,XBOOLE_0:def 4;
    end;
    suppose x = E-max L~Cage(C,n);
      hence x in L~Upper_Seq(C,n) /\ L~Lower_Seq(C,n)
      by A4,A5,A7,A8,XBOOLE_0:def 4;
    end;
  end;
end;

theorem
  for C be compact non vertical non horizontal Subset of TOP-REAL 2 holds
  Upper_Seq(C,n) is_in_the_area_of Cage(C,n)
proof
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
  then E-max L~Cage(C,n) in rng Rotate(Cage(C,n),W-min L~Cage(C,n))
  by FINSEQ_6:96,SPRECT_2:47;
  hence Upper_Seq(C,n) is_in_the_area_of Cage(C,n) by Th5,JORDAN1B:11;
end;

theorem
  for C be compact non vertical non horizontal Subset of TOP-REAL 2 holds
  Lower_Seq(C,n) is_in_the_area_of Cage(C,n)
proof
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
  then E-max L~Cage(C,n) in rng Rotate(Cage(C,n),W-min L~Cage(C,n))
  by FINSEQ_6:96,SPRECT_2:47;
  hence thesis by Th6,JORDAN1B:11;
end;

theorem
  for C be compact connected non vertical non horizontal Subset of TOP-REAL 2
  holds (Cage(C,n)/.2)`2 = N-bound L~Cage(C,n)
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
  Cage(C,n)/.1 = N-min L~Cage(C,n) by JORDAN9:34;
  then Cage(C,n)/.2 in N-most L~Cage(C,n) by SPRECT_2:34;
  then (Cage(C,n)/.2)`2 = (N-min L~Cage(C,n))`2 by PSCOMP_1:98;
  hence thesis by EUCLID:56;
end;

theorem
  for C be compact connected non vertical non horizontal Subset of TOP-REAL 2
  for k be Element of NAT st 1 <= k & k+1 <= len Cage(C,n) &
  Cage(C,n)/.k = E-max L~Cage(C,n) holds
  (Cage(C,n)/.(k+1))`1 = E-bound L~Cage(C,n)
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
A1: Cage(C,n) is_sequence_on Gauge(C,n) by JORDAN9:def 1;
  let k be Element of NAT;
  assume that
A2: 1 <= k and
A3: k+1 <= len Cage(C,n) and
A4: Cage(C,n)/.k = E-max L~Cage(C,n);
A5: Cage(C,n)/.1 = N-min L~Cage(C,n) by JORDAN9:34;
  then 1 < (N-max L~Cage(C,n))..Cage(C,n) by SPRECT_2:73;
  then
A6: (E-max L~Cage(C,n))..Cage(C,n) > 1 by A5,SPRECT_2:74,XXREAL_0:2;
A7: k < len Cage(C,n) by A3,NAT_1:13;
  then
A8: k in dom Cage(C,n) by A2,FINSEQ_3:27;
  then (E-max L~Cage(C,n))..Cage(C,n) <= k by A4,FINSEQ_5:42;
  then
A9: k > 1 by A6,XXREAL_0:2;
  then consider i1,j1,i2,j2 be Element of NAT such that
A10: [i1,j1] in Indices Gauge(C,n) and
A11: Cage(C,n)/.k = Gauge(C,n)*(i1,j1) and
A12: [i2,j2] in Indices Gauge(C,n) and
A13: Cage(C,n)/.(k+1) = Gauge(C,n)*(i2,j2) and
A14: i1 = i2 & j1+1 = j2 or i1+1 = i2 & j1 = j2 or
  i1 = i2+1 & j1 = j2 or i1 = i2 & j1 = j2+1 by A1,A3,JORDAN8:6;
A15: 1 <= i1 & i1 <= len Gauge(C,n) & 1 <= j1 & j1 <= width Gauge(C,n)
  by A10,MATRIX_1:39;
A16: 1 <= i2 & i2 <= len Gauge(C,n) & 1 <= j2 & j2 <= width Gauge(C,n)
  by A12,MATRIX_1:39;
A17: len Gauge(C,n) = width Gauge(C,n) by JORDAN8:def 1;
  Gauge(C,n)*(i1,j1)`1 = E-bound L~Cage(C,n) by A4,A11,EUCLID:56
    .= Gauge(C,n)*(len Gauge(C,n),j1)`1 by A15,A17,JORDAN1A:92;
  then
A18: i1 <= len Gauge(C,n) & i1 >= len Gauge(C,n) by A15,GOBOARD5:4;
  then
A19: i1 = len Gauge(C,n) by XXREAL_0:1;
  reconsider k'=k-1 as Element of NAT by A8,FINSEQ_3:28;
  k >= 1+1 by A9,NAT_1:13;
  then
A20: k' >= 1+1-1 by XREAL_1:11;
A21: k'+1 < len Cage(C,n) by A3,NAT_1:13;
  consider i3,j3,i4,j4 be Element of NAT such that
A22: [i3,j3] in Indices Gauge(C,n) and
A23: Cage(C,n)/.k' = Gauge(C,n)*(i3,j3) and
A24: [i4,j4] in Indices Gauge(C,n) and
A25: Cage(C,n)/.(k'+1) = Gauge(C,n)*(i4,j4) and
A26: i3 = i4 & j3+1 = j4 or i3+1 = i4 & j3 = j4 or
  i3 = i4+1 & j3 = j4 or i3 = i4 & j3 = j4+1 by A1,A7,A20,JORDAN8:6;
A27: k'+1 = k;
A28: i1 = i4 & j1 = j4 by A10,A11,A24,A25,GOBOARD1:21;
A29: 1 <= i3 & i3 <= len Gauge(C,n) & 1 <= j3 & j3 <= width Gauge(C,n)
  by A22,MATRIX_1:39;
A30: j3 = j4
  proof
    assume
A31: j3 <> j4;
    per cases by A26,A31;
    suppose i3 = i4 & j3+1 = j4;
      hence contradiction by A7,A18,A20,A22,A23,A24,A25,A28,JORDAN10:1;
    end;
    suppose
A32:  i3 = i4 & j3 = j4+1;
      k' < len Cage(C,n) by A21,NAT_1:13;
      then Gauge(C,n)*(i3,j3) in E-most L~Cage(C,n)
      by A19,A20,A23,A28,A29,A32,JORDAN1A:82;
      then
A33:  (Gauge(C,n)*(i4,j4+1))`2 <= (Gauge(C,n)*(i4,j4))`2
      by A4,A25,A32,PSCOMP_1:108;
      j4 < j4+1 by NAT_1:13;
      hence contradiction by A15,A28,A29,A32,A33,GOBOARD5:5;
    end;
  end;
  i1 = i2
  proof
    assume
A34: i1 <> i2;
    per cases by A14,A34;
    suppose i1+1 = i2 & j1 = j2;
      hence contradiction by A16,A18,NAT_1:13;
    end;
    suppose
A35:  i1 = i2+1 & j1 = j2;
      k'+(1+1) <= len Cage(C,n) by A3;
      then
A36:  LSeg(Cage(C,n),k') /\ LSeg(Cage(C,n),k) = {Cage(C,n)/.k}
      by A20,A27,TOPREAL1:def 8;
      Cage(C,n)/.k' in LSeg(Cage(C,n),k') &
      Cage(C,n)/.(k+1) in LSeg(Cage(C,n),k) by A2,A3,A7,A20,A27,TOPREAL1:27;
      then Cage(C,n)/.(k+1) in {Cage(C,n)/.k} by A13,A18,A23,A26,A28,A29,A30
,A35,A36,NAT_1:13,XBOOLE_0:def 4;
      then Cage(C,n)/.(k+1) = Cage(C,n)/.k by TARSKI:def 1;
      hence contradiction by A10,A11,A12,A13,A34,GOBOARD1:21;
    end;
  end;
  then Gauge(C,n)*(i1,j1)`1 = Gauge(C,n)*(i2,1)`1 by A15,GOBOARD5:3
    .= Gauge(C,n)*(i2,j2)`1 by A16,GOBOARD5:3;
  hence thesis by A4,A11,A13,EUCLID:56;
end;

theorem
  for C be compact connected non vertical non horizontal Subset of TOP-REAL 2
  for k be Element of NAT st 1 <= k & k+1 <= len Cage(C,n) &
  Cage(C,n)/.k = S-max L~Cage(C,n) holds
  (Cage(C,n)/.(k+1))`2 = S-bound L~Cage(C,n)
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
A1: Cage(C,n) is_sequence_on Gauge(C,n) by JORDAN9:def 1;
  let k be Element of NAT;
  assume that
A2: 1 <= k and
A3: k+1 <= len Cage(C,n) and
A4: Cage(C,n)/.k = S-max L~Cage(C,n);
A5: Cage(C,n)/.1 = N-min L~Cage(C,n) by JORDAN9:34;
  then 1 < (N-max L~Cage(C,n))..Cage(C,n) by SPRECT_2:73;
  then 1 < (E-max L~Cage(C,n))..Cage(C,n) by A5,SPRECT_2:74,XXREAL_0:2;
  then 1 < (E-min L~Cage(C,n))..Cage(C,n) by A5,SPRECT_2:75,XXREAL_0:2;
  then
A6: (S-max L~Cage(C,n))..Cage(C,n) > 1 by A5,SPRECT_2:76,XXREAL_0:2;
A7: k < len Cage(C,n) by A3,NAT_1:13;
  then
A8: k in dom Cage(C,n) by A2,FINSEQ_3:27;
  then (S-max L~Cage(C,n))..Cage(C,n) <= k by A4,FINSEQ_5:42;
  then
A9: k > 1 by A6,XXREAL_0:2;
  then consider i1,j1,i2,j2 be Element of NAT such that
A10: [i1,j1] in Indices Gauge(C,n) and
A11: Cage(C,n)/.k = Gauge(C,n)*(i1,j1) and
A12: [i2,j2] in Indices Gauge(C,n) and
A13: Cage(C,n)/.(k+1) = Gauge(C,n)*(i2,j2) and
A14: i1 = i2 & j1+1 = j2 or i1+1 = i2 & j1 = j2 or
  i1 = i2+1 & j1 = j2 or i1 = i2 & j1 = j2+1 by A1,A3,JORDAN8:6;
A15: 1 <= i1 & i1 <= len Gauge(C,n) & 1 <= j1 & j1 <= width Gauge(C,n)
  by A10,MATRIX_1:39;
A16: 1 <= i2 & i2 <= len Gauge(C,n) & 1 <= j2 & j2 <= width Gauge(C,n)
  by A12,MATRIX_1:39;
  Gauge(C,n)*(i1,j1)`2 = S-bound L~Cage(C,n) by A4,A11,EUCLID:56
    .= Gauge(C,n)*(i1,1)`2 by A15,JORDAN1A:93;
  then
A17: j1 <= 1 & j1 >= 1 by A15,GOBOARD5:5;
  then
A18: j1 = 1 by XXREAL_0:1;
  reconsider k'=k-1 as Element of NAT by A8,FINSEQ_3:28;
  k >= 1+1 by A9,NAT_1:13;
  then
A19: k' >= 1+1-1 by XREAL_1:11;
A20: k'+1 < len Cage(C,n) by A3,NAT_1:13;
  consider i3,j3,i4,j4 be Element of NAT such that
A21: [i3,j3] in Indices Gauge(C,n) and
A22: Cage(C,n)/.k' = Gauge(C,n)*(i3,j3) and
A23: [i4,j4] in Indices Gauge(C,n) and
A24: Cage(C,n)/.(k'+1) = Gauge(C,n)*(i4,j4) and
A25: i3 = i4 & j3+1 = j4 or i3+1 = i4 & j3 = j4 or
  i3 = i4+1 & j3 = j4 or i3 = i4 & j3 = j4+1 by A1,A7,A19,JORDAN8:6;
A26: k'+1 = k;
A27: i1 = i4 & j1 = j4 by A10,A11,A23,A24,GOBOARD1:21;
A28: 1 <= i3 & i3 <= len Gauge(C,n) & 1 <= j3 & j3 <= width Gauge(C,n)
  by A21,MATRIX_1:39;
A29: i3 = i4
  proof
    assume
A30: i3 <> i4;
    per cases by A25,A30;
    suppose j3 = j4 & i3+1 = i4;
      hence contradiction by A7,A17,A19,A21,A22,A23,A24,A27,JORDAN10:3;
    end;
    suppose
A31:  j3 = j4 & i3 = i4+1;
      k' < len Cage(C,n) by A20,NAT_1:13;
      then Gauge(C,n)*(i3,j3) in S-most L~Cage(C,n)
      by A18,A19,A22,A27,A28,A31,JORDAN1A:81;
      then
A32:  (Gauge(C,n)*(i4+1,j4))`1 <= (Gauge(C,n)*(i4,j4))`1
      by A4,A24,A31,PSCOMP_1:118;
      i4 < i4+1 by NAT_1:13;
      hence contradiction by A15,A27,A28,A31,A32,GOBOARD5:4;
    end;
  end;
  j1 = j2
  proof
    assume
A33: j1 <> j2;
    per cases by A14,A33;
    suppose j1 = j2+1 & i1 = i2;
      hence contradiction by A16,A17,NAT_1:13;
    end;
    suppose
A34:  j1+1 = j2 & i1 = i2;
      k'+(1+1) <= len Cage(C,n) by A3;
      then
A35:  LSeg(Cage(C,n),k') /\ LSeg(Cage(C,n),k) = {Cage(C,n)/.k}
      by A19,A26,TOPREAL1:def 8;
      Cage(C,n)/.k' in LSeg(Cage(C,n),k') &
      Cage(C,n)/.(k+1) in LSeg(Cage(C,n),k) by A2,A3,A7,A19,A26,TOPREAL1:27;
      then Cage(C,n)/.(k+1) in {Cage(C,n)/.k}
      by A13,A17,A22,A25,A27,A28,A29,A34,A35,NAT_1:13,XBOOLE_0:def 4;
      then Cage(C,n)/.(k+1) = Cage(C,n)/.k by TARSKI:def 1;
      hence contradiction by A10,A11,A12,A13,A33,GOBOARD1:21;
    end;
  end;
  then Gauge(C,n)*(i1,j1)`2 = Gauge(C,n)*(1,j2)`2 by A15,GOBOARD5:2
    .= Gauge(C,n)*(i2,j2)`2 by A16,GOBOARD5:2;
  hence thesis by A4,A11,A13,EUCLID:56;
end;

theorem
  for C be compact connected non vertical non horizontal Subset of TOP-REAL 2
  for k be Element of NAT st 1 <= k & k+1 <= len Cage(C,n) &
  Cage(C,n)/.k = W-min L~Cage(C,n) holds
  (Cage(C,n)/.(k+1))`1 = W-bound L~Cage(C,n)
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
A1: Cage(C,n) is_sequence_on Gauge(C,n) by JORDAN9:def 1;
  let k be Element of NAT;
  assume that
A2: 1 <= k and
A3: k+1 <= len Cage(C,n) and
A4: Cage(C,n)/.k = W-min L~Cage(C,n);
A5: Cage(C,n)/.1 = N-min L~Cage(C,n) by JORDAN9:34;
  then 1 < (N-max L~Cage(C,n))..Cage(C,n) by SPRECT_2:73;
  then 1 < (E-max L~Cage(C,n))..Cage(C,n) by A5,SPRECT_2:74,XXREAL_0:2;
  then 1 < (E-min L~Cage(C,n))..Cage(C,n) by A5,SPRECT_2:75,XXREAL_0:2;
  then 1 < (S-max L~Cage(C,n))..Cage(C,n) by A5,SPRECT_2:76,XXREAL_0:2;
  then 1 < (S-min L~Cage(C,n))..Cage(C,n) by A5,SPRECT_2:77,XXREAL_0:2;
  then
A6: (W-min L~Cage(C,n))..Cage(C,n) > 1 by A5,SPRECT_2:78,XXREAL_0:2;
A7: k < len Cage(C,n) by A3,NAT_1:13;
  then
A8: k in dom Cage(C,n) by A2,FINSEQ_3:27;
  then (W-min L~Cage(C,n))..Cage(C,n) <= k by A4,FINSEQ_5:42;
  then
A9: k > 1 by A6,XXREAL_0:2;
  then consider i1,j1,i2,j2 be Element of NAT such that
A10: [i1,j1] in Indices Gauge(C,n) and
A11: Cage(C,n)/.k = Gauge(C,n)*(i1,j1) and
A12: [i2,j2] in Indices Gauge(C,n) and
A13: Cage(C,n)/.(k+1) = Gauge(C,n)*(i2,j2) and
A14: i1 = i2 & j1+1 = j2 or i1+1 = i2 & j1 = j2 or
  i1 = i2+1 & j1 = j2 or i1 = i2 & j1 = j2+1 by A1,A3,JORDAN8:6;
A15: 1 <= i1 & i1 <= len Gauge(C,n) & 1 <= j1 & j1 <= width Gauge(C,n)
  by A10,MATRIX_1:39;
A16: 1 <= i2 & i2 <= len Gauge(C,n) & 1 <= j2 & j2 <= width Gauge(C,n)
  by A12,MATRIX_1:39;
A17: len Gauge(C,n) = width Gauge(C,n) by JORDAN8:def 1;
  Gauge(C,n)*(i1,j1)`1 = W-bound L~Cage(C,n) by A4,A11,EUCLID:56
    .= Gauge(C,n)*(1,j1)`1 by A15,A17,JORDAN1A:94;
  then
A18: i1 <= 1 & i1 >= 1 by A15,GOBOARD5:4;
  then
A19: i1 = 1 by XXREAL_0:1;
  reconsider k'=k-1 as Element of NAT by A8,FINSEQ_3:28;
  k >= 1+1 by A9,NAT_1:13;
  then
A20: k' >= 1+1-1 by XREAL_1:11;
A21: k'+1 < len Cage(C,n) by A3,NAT_1:13;
  consider i3,j3,i4,j4 be Element of NAT such that
A22: [i3,j3] in Indices Gauge(C,n) and
A23: Cage(C,n)/.k' = Gauge(C,n)*(i3,j3) and
A24: [i4,j4] in Indices Gauge(C,n) and
A25: Cage(C,n)/.(k'+1) = Gauge(C,n)*(i4,j4) and
A26: i3 = i4 & j3+1 = j4 or i3+1 = i4 & j3 = j4 or
  i3 = i4+1 & j3 = j4 or i3 = i4 & j3 = j4+1 by A1,A7,A20,JORDAN8:6;
A27: k'+1 = k;
A28: i1 = i4 & j1 = j4 by A10,A11,A24,A25,GOBOARD1:21;
A29: 1 <= i3 & i3 <= len Gauge(C,n) & 1 <= j3 & j3 <= width Gauge(C,n)
  by A22,MATRIX_1:39;
A30: j3 = j4
  proof
    assume
A31: j3 <> j4;
    per cases by A26,A31;
    suppose i3 = i4 & j3 = j4+1;
      hence contradiction by A7,A18,A20,A22,A23,A24,A25,A28,JORDAN10:2;
    end;
    suppose
A32:  i3 = i4 & j3+1 = j4;
      k' < len Cage(C,n) by A21,NAT_1:13;
      then Gauge(C,n)*(i3,j3) in W-most L~Cage(C,n)
      by A19,A20,A23,A28,A29,A32,JORDAN1A:80;
      then
A33:  (Gauge(C,n)*(i3,j3+1))`2 <= (Gauge(C,n)*(i3,j3))`2
      by A4,A25,A32,PSCOMP_1:88;
      j3 < j3+1 by NAT_1:13;
      hence contradiction by A15,A28,A29,A32,A33,GOBOARD5:5;
    end;
  end;
  i1 = i2
  proof
    assume
A34: i1 <> i2;
    per cases by A14,A34;
    suppose i1 = i2+1 & j1 = j2;
      hence contradiction by A16,A18,NAT_1:13;
    end;
    suppose
A35:  i1+1 = i2 & j1 = j2;
      k'+(1+1) <= len Cage(C,n) by A3;
      then
A36:  LSeg(Cage(C,n),k') /\ LSeg(Cage(C,n),k) = {Cage(C,n)/.k}
      by A20,A27,TOPREAL1:def 8;
      Cage(C,n)/.k' in LSeg(Cage(C,n),k') &
      Cage(C,n)/.(k+1) in LSeg(Cage(C,n),k) by A2,A3,A7,A20,A27,TOPREAL1:27;
      then Cage(C,n)/.(k+1) in {Cage(C,n)/.k}
      by A13,A18,A23,A26,A28,A29,A30,A35,A36,NAT_1:13,XBOOLE_0:def 4;
      then Cage(C,n)/.(k+1) = Cage(C,n)/.k by TARSKI:def 1;
      hence contradiction by A10,A11,A12,A13,A34,GOBOARD1:21;
    end;
  end;
  then Gauge(C,n)*(i1,j1)`1 = Gauge(C,n)*(i2,1)`1 by A15,GOBOARD5:3
    .= Gauge(C,n)*(i2,j2)`1 by A16,GOBOARD5:3;
  hence thesis by A4,A11,A13,EUCLID:56;
end;

