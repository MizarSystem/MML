:: Upper and Lower Sequence of a Cage
::  by Robert Milewski
::
:: Received August 8, 2001
:: Copyright (c) 2001 Association of Mizar Users
 
environ

 vocabularies NUMBERS, SUBSET_1, FINSEQ_1, EUCLID, SPRECT_2, RELAT_1, FINSEQ_5,
      FINSEQ_4, XXREAL_0, PARTFUN1, CARD_1, ARYTM_3, ARYTM_1, PRE_TOPC,
      TOPREAL1, JORDAN3, XBOOLE_0, FUNCT_1, GROUP_2, ORDINAL4, NAT_1, RLTOPSP1,
      TARSKI, RCOMP_1, SPPOL_1, FINSEQ_6, JORDAN9, PSCOMP_1, GRAPH_2, TOPREAL2,
      RELAT_2, MCART_1, GOBOARD1, JORDAN8, TREES_1, MATRIX_1, JORDAN1E;
 notations TARSKI, XBOOLE_0, ENUMSET1, SUBSET_1, CARD_1, NUMBERS, XCMPLX_0,
      XXREAL_0, NAT_1, NAT_D, FUNCT_1, PARTFUN1, FINSEQ_1, FINSEQ_4, FINSEQ_5,
      MATRIX_1, FINSEQ_6, STRUCT_0, GRAPH_2, PRE_TOPC, TOPREAL2, CONNSP_1,
      COMPTS_1, RLTOPSP1, EUCLID, PSCOMP_1, SPRECT_2, GOBOARD1, TOPREAL1,
      SPPOL_1, JORDAN3, JORDAN8, JORDAN9;
 constructors PARTFUN1, XXREAL_0, NAT_1, FINSEQ_4, NEWTON, BINARITH, FINSEQ_5,
      CONNSP_1, REALSET2, TOPREAL2, PSCOMP_1, GRAPH_2, JORDAN3, JORDAN8,
      GOBRD13, JORDAN9, NAT_D, RELSET_1, REAL_1;
 registrations XBOOLE_0, RELSET_1, XXREAL_0, XREAL_0, MEMBERED, FINSEQ_1,
      FINSEQ_6, STRUCT_0, TOPREAL2, SPPOL_2, SPRECT_2, REVROT_1, TOPREAL6,
      JORDAN8, JORDAN1A, CARD_1, FUNCT_1, EUCLID;
 requirements NUMERALS, BOOLE, SUBSET, REAL, ARITHM;
 definitions TARSKI, TOPREAL1, SPRECT_2, XBOOLE_0, PSCOMP_1;
 theorems JORDAN8, PSCOMP_1, JORDAN1A, NAT_1, GOBOARD5, FINSEQ_1, FINSEQ_2,
      SPRECT_2, FINSEQ_4, FINSEQ_5, FINSEQ_6, GOBOARD7, SPPOL_2, REVROT_1,
      TOPREAL1, SPRECT_3, JORDAN5B, JORDAN3, JORDAN9, GOBOARD1, TARSKI,
      FINSEQ_3, FUNCT_1, TOPREAL5, JORDAN10, GRAPH_2, CARD_1, CARD_2, ENUMSET1,
      JORDAN1B, SPRECT_5, XBOOLE_0, XBOOLE_1, XREAL_1, XXREAL_0, EUCLID,
      ORDINAL1, PARTFUN1, MATRIX_1, XREAL_0;

begin  :: Preliminaries

reserve n for Element of NAT;

canceled 4;

theorem Th5:
  for f,g be FinSequence of TOP-REAL 2 st f is_in_the_area_of g for
  p be Element of TOP-REAL 2 st p in rng f holds f-:p is_in_the_area_of g
proof
  let f,g be FinSequence of TOP-REAL 2;
  assume
A1: f is_in_the_area_of g;
  let p be Element of TOP-REAL 2;
  assume
A2: p in rng f;
  then
A3: p..f <= len f by FINSEQ_4:31;
  let n be Element of NAT;
  assume
A4: n in dom(f-:p);
A5: len(f-:p) = p..f by A2,FINSEQ_5:45;
  then n in Seg(p..f) by A4,FINSEQ_1:def 3;
  then
A6: (f-:p)/.n = f/.n by A2,FINSEQ_5:46;
A7: n in Seg len(f-:p) by A4,FINSEQ_1:def 3;
  then n <= p..f by A5,FINSEQ_1:3;
  then
A8: n <= len f by A3,XXREAL_0:2;
  1 <= n by A7,FINSEQ_1:3;
  then n in dom f by A8,FINSEQ_3:27;
  hence thesis by A1,A6,SPRECT_2:def 1;
end;

theorem Th6:
  for f,g be FinSequence of TOP-REAL 2 st f is_in_the_area_of g for
  p be Element of TOP-REAL 2 st p in rng f holds f:-p is_in_the_area_of g
proof
  let f,g be FinSequence of TOP-REAL 2;
  assume
A1: f is_in_the_area_of g;
  let p be Element of TOP-REAL 2;
  assume
A2: p in rng f;
  let n be Element of NAT;
  1 <= p..f by A2,FINSEQ_4:31;
  then
A3: 0+1 <= n-'1+p..f by XREAL_1:9;
  assume
A4: n in dom(f:-p);
  then
A5: n in Seg len(f:-p) by FINSEQ_1:def 3;
  then
A6: 0+1 <= n by FINSEQ_1:3;
  then n-'1+1 = n by XREAL_1:237;
  then
A7: (f:-p)/.n = f/.(n-'1+p..f) by A2,A4,FINSEQ_5:55;
  len(f:-p) = len f - p..f + 1 by A2,FINSEQ_5:53;
  then n <= len f - p..f + 1 by A5,FINSEQ_1:3;
  then n - 1 <= len f - p..f by XREAL_1:22;
  then
A8: n - 1 + p..f <= len f by XREAL_1:21;
  n-1 >= 0 by A6,XREAL_1:21;
  then n-'1+p..f <= len f by A8,XREAL_0:def 2;
  then n-'1+p..f in dom f by A3,FINSEQ_3:27;
  hence thesis by A1,A7,SPRECT_2:def 1;
end;

theorem
  for f be FinSequence of TOP-REAL 2 for p be Point of TOP-REAL 2 st p
  in L~f holds L_Cut (f,p) <> {}
proof
  let f be FinSequence of TOP-REAL 2;
  let p be Point of TOP-REAL 2;
  assume that
A1: p in L~f and
A2: L_Cut (f,p) = {};
  len f <> 0 by A1,TOPREAL1:28;
  then f <> {};
  then
A3: len f in dom f by FINSEQ_5:6;
A4: p<>f.(Index(p,f)+1) or p=f.(Index(p,f)+1);
  <*p*>^mid(f,Index(p,f)+1,len f) is non empty;
  then L_Cut(f,p) = mid(f,Index(p,f)+1,len f) by A2,A4,JORDAN3:def 4;
  then not Index(p,f)+1 in dom f by A2,A3,SPRECT_2:11;
  then Index(p,f)+1 < 1 or Index(p,f)+1 > len f by FINSEQ_3:27;
  then Index(p,f) >= len f by NAT_1:11,13;
  hence contradiction by A1,JORDAN3:41;
end;

theorem
  for f be FinSequence of TOP-REAL 2 for p be Point of TOP-REAL 2 st p
  in L~f & len R_Cut(f,p) >= 2 holds f.1 in L~R_Cut(f,p)
proof
  let f be FinSequence of TOP-REAL 2;
  let p be Point of TOP-REAL 2;
  assume
A1: p in L~f;
  then len f <> 0 by TOPREAL1:28;
  then len f > 0 by NAT_1:3;
  then 0+1 <= len f by NAT_1:13;
  then
A2: R_Cut(f,p).1 = f.1 by A1,JORDAN1B:4;
  assume 2 <= len R_Cut(f,p);
  hence thesis by A2,JORDAN3:34;
end;

theorem Th9:
  for f be FinSequence of TOP-REAL 2 st f is being_S-Seq for p be
  Point of TOP-REAL 2 st p in L~f holds not f.1 in L~mid(f,Index(p,f)+1,len f)
proof
  let f be FinSequence of TOP-REAL 2 such that
A1: f is being_S-Seq;
  let p be Point of TOP-REAL 2 such that
A2: p in L~f and
A3: f.1 in L~mid(f,Index(p,f)+1,len f);
  len f <> 0 by A2,TOPREAL1:28;
  then f <> {};
  then 1 in dom f by FINSEQ_5:6;
  then
A4: f/.1 in L~mid(f,Index(p,f)+1,len f) by A3,PARTFUN1:def 8;
  Index(p,f) < len f by A2,JORDAN3:41;
  then Index(p,f)+1 <= len f by NAT_1:13;
  then Index(p,f)+1 = 0+1 by A1,A4,JORDAN5B:16,NAT_1:11;
  hence contradiction by A2,JORDAN3:41;
end;

theorem Th10:
  for i,j,m,n be Element of NAT holds i+j = m+n & i <= m & j <= n implies i = m
proof
  let i,j,m,n be Element of NAT;
  assume that
A1: i+j = m+n and
A2: i <= m and
A3: j <= n;
  consider k be Nat such that
A4: m = i + k by A2,NAT_1:10;
  consider l be Nat such that
A5: n = j + l by A3,NAT_1:10;
  j + (l+k) = j+0 by A1,A4,A5;
  then k = 0 by NAT_1:7;
  hence thesis by A4;
end;

theorem
  for f be FinSequence of TOP-REAL 2 st f is being_S-Seq for p be Point
  of TOP-REAL 2 st p in L~f & f.1 in L~L_Cut(f,p) holds f.1 = p
proof
  let f be FinSequence of TOP-REAL 2;
  assume
A1: f is being_S-Seq;
  then
A2: len f >= 2 by TOPREAL1:def 10;
A3: len f in dom f by A1,FINSEQ_5:6;
  1 in dom f by A1,FINSEQ_5:6;
  then
A4: f/.1 = f.1 by PARTFUN1:def 8;
  let p be Point of TOP-REAL 2 such that
A5: p in L~f and
A6: f.1 in L~L_Cut(f,p) and
A7: f.1 <> p;
  set g = mid(f,Index(p,f)+1,len f);
A8: not f.1 in L~g by A1,A5,Th9;
  then p<>f.(Index(p,f)+1) by A6,JORDAN3:def 4;
  then
A9: L_Cut(f,p) = <*p*>^g by JORDAN3:def 4;
  per cases;
  suppose
    g is empty;
    then L_Cut(f,p) = <*p*> by A9,FINSEQ_1:47;
    then len L_Cut(f,p) = 1 by FINSEQ_1:56;
    hence contradiction by A6,TOPREAL1:28;
  end;
  suppose
    g is non empty;
    then L~L_Cut(f,p) = LSeg(p,g/.1) \/ L~g by A9,SPPOL_2:20;
    then
A10: f.1 in LSeg(p,g/.1) by A6,A8,XBOOLE_0:def 3;
A11: 1+1 <= len f by A1,TOPREAL1:def 10;
    then
A12: 2 in dom f by FINSEQ_3:27;
    consider i being Element of NAT such that
A13: 1 <= i and
A14: i+1 <= len(<*p*>^g) and
A15: f/.1 in LSeg(<*p*>^g,i) by A6,A4,A9,SPPOL_2:13;
    LSeg(<*p*>^g,i) c= LSeg(f,Index(p,f)+i-'1) by A5,A13,A14,JORDAN3:49;
    then
A16: Index(p,f)+i-'1 = 1 by A1,A2,A15,JORDAN5B:33;
A17: 1 <= Index(p,f) by A5,JORDAN3:41;
    then 1+1 <= Index(p,f)+i by A13,XREAL_1:9;
    then
A18: Index(p,f)+i = 1+1 by A16,XREAL_1:237,XXREAL_0:2;
    then Index(p,f) = 1 by A13,A17,Th10;
    then p in LSeg(f,1) by A5,JORDAN3:42;
    then
A19: p in LSeg(f/.1,f/.(1+1)) by A11,TOPREAL1:def 5;
    i = 1 by A13,A17,A18,Th10;
    then g/.1 = f/.(1+1) by A3,A18,A12,SPRECT_2:12;
    hence contradiction by A7,A4,A10,A19,SPRECT_3:16;
  end;
end;

begin  :: About Upper and Lower Sequence of a Cage

definition
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  let n be Nat;
  func Upper_Seq(C,n) -> FinSequence of TOP-REAL 2 equals
  Rotate(Cage(C,n),
  W-min L~Cage(C,n))-:E-max L~Cage(C,n);
  coherence;
end;

theorem Th12:
  for C be compact non vertical non horizontal Subset of TOP-REAL
2 for n be Nat holds len Upper_Seq(C,n) = (E-max L~Cage(C,n))..Rotate(Cage(C,n)
  ,W-min L~Cage(C,n))
proof
  let C be compact non vertical non horizontal non empty Subset of TOP-REAL 2;
  let n be Nat;
  E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
  then E-max L~Cage(C,n) in rng Rotate(Cage(C,n),W-min L~Cage(C,n)) by
FINSEQ_6:96,SPRECT_2:47;
  hence thesis by FINSEQ_5:45;
end;

definition
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  let n be Nat;
  func Lower_Seq(C,n) -> FinSequence of TOP-REAL 2 equals
  Rotate(Cage(C,n),
  W-min L~Cage(C,n)):-E-max L~Cage(C,n);
  coherence;
end;

theorem Th13:
  for C be compact non vertical non horizontal Subset of TOP-REAL
2 for n be Nat holds len Lower_Seq(C,n) = len Rotate(Cage(C,n),W-min L~Cage(C,n
  ))- (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n))+1
proof
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  let n be Nat;
  E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
  then E-max L~Cage(C,n) in rng Rotate(Cage(C,n),W-min L~Cage(C,n)) by
FINSEQ_6:96,SPRECT_2:47;
  hence thesis by FINSEQ_5:53;
end;

registration
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  let n be Nat;
  cluster Upper_Seq(C,n) -> non empty;
  coherence
  proof
    E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
    then
A1: E-max L~Cage(C,n) in rng Rotate(Cage(C,n),W-min L~Cage(C,n)) by FINSEQ_6:96
,SPRECT_2:47;
    len Upper_Seq(C,n) = (E-max L~Cage(C,n))..Rotate(Cage(C,n), W-min L~
    Cage(C,n)) by Th12;
    hence thesis by A1,FINSEQ_4:31;
  end;
  cluster Lower_Seq(C,n) -> non empty;
  coherence
  proof
    E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
    then
A2: E-max L~Cage(C,n) in rng Rotate(Cage(C,n),W-min L~Cage(C,n)) by FINSEQ_6:96
,SPRECT_2:47;
    len Lower_Seq(C,n) = len Rotate(Cage(C,n),W-min L~Cage(C,n))- (E-max
    L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n))+1 by Th13;
    then len Rotate(Cage(C,n),W-min L~Cage(C,n))-len Lower_Seq(C,n)+1 <= len
    Rotate(Cage(C,n),W-min L~Cage(C,n)) by A2,FINSEQ_4:31;
    then len Rotate(Cage(C,n),W-min L~Cage(C,n))-len Lower_Seq(C,n) <= len
    Rotate(Cage(C,n),W-min L~Cage(C,n))-1 by XREAL_1:21;
    hence thesis by XREAL_1:12;
  end;
end;

registration
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  let n be Element of NAT;
  cluster Upper_Seq(C,n) -> one-to-one special unfolded s.n.c.;
  coherence
  proof
    set f = Rotate(Cage(C,n),W-min L~Cage(C,n));
A1: W-min L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:47;
    E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
    then
A2: E-max L~Cage(C,n) in rng f by FINSEQ_6:96,SPRECT_2:47;
A3: len Upper_Seq(C,n) = (E-max L~Cage(C,n))..f by Th12;
    now
      let i1,j1 be set;
      assume that
A4:   i1 in dom Upper_Seq(C,n) and
A5:   j1 in dom Upper_Seq(C,n) and
A6:   Upper_Seq(C,n).i1 = Upper_Seq(C,n).j1 and
A7:   i1 <> j1;
      reconsider i2 = i1, j2 = j1 as Element of NAT by A4,A5;
A8:   i2 in Seg((E-max L~Cage(C,n))..f) by A3,A4,FINSEQ_1:def 3;
      then
A9:   1 <= i2 by FINSEQ_1:3;
A10:  L~Cage(C,n) = L~f by REVROT_1:33;
A11:  j2 in Seg((E-max L~Cage(C,n))..f) by A3,A5,FINSEQ_1:def 3;
      then
A12:  1 <= j2 by FINSEQ_1:3;
      j2 <= (E-max L~Cage(C,n))..f by A11,FINSEQ_1:3;
      then
A13:  j2 < len f by A10,SPRECT_5:17,XXREAL_0:2;
A14:  Upper_Seq(C,n).j1 = Upper_Seq(C,n)/.j2 by A5,PARTFUN1:def 8
        .= f/.j2 by A2,A11,FINSEQ_5:46;
      i2 <= (E-max L~Cage(C,n))..f by A8,FINSEQ_1:3;
      then
A15:  i2 < len f by A10,SPRECT_5:17,XXREAL_0:2;
A16:  Upper_Seq(C,n).i1 = Upper_Seq(C,n)/.i2 by A4,PARTFUN1:def 8
        .= f/.i2 by A2,A8,FINSEQ_5:46;
      per cases by A7,XXREAL_0:1;
      suppose
        i2 < j2;
        hence contradiction by A6,A16,A14,A9,A13,GOBOARD7:38;
      end;
      suppose
        j2 < i2;
        hence contradiction by A6,A16,A14,A12,A15,GOBOARD7:38;
      end;
    end;
    hence Upper_Seq(C,n) is one-to-one by FUNCT_1:def 8;
    thus Upper_Seq(C,n) is special;
    thus Upper_Seq(C,n) is unfolded;
    let i,j be Nat;
    assume
A17: i+1 < j;
    len f > 0 by NAT_1:3;
    then 0+1 <= len f by NAT_1:13;
    then
A18: len f in dom f by FINSEQ_3:27;
A19: i in NAT & j in NAT by ORDINAL1:def 13;
    now
      per cases;
      suppose
A20:    j+1 <= len Upper_Seq(C,n);
A21:    (E-max L~Cage(C,n))..f <= len f by A2,FINSEQ_4:31;
A22:    j+1 <= (E-max L~Cage(C,n))..f by A2,A20,FINSEQ_5:45;
A23:    now
          per cases by A22,XXREAL_0:1;
          suppose
            j+1 < (E-max L~Cage(C,n))..f;
            hence j+1 < len f by A21,XXREAL_0:2;
          end;
          suppose
A24:        j+1 = (E-max L~Cage(C,n))..f;
            assume j+1 >= len f;
            then (E-max L~Cage(C,n))..f = len f by A21,A24,XXREAL_0:1;
            then E-max L~Cage(C,n) = f.(len f) by A2,FINSEQ_4:29
              .= f/.(len f) by A18,PARTFUN1:def 8
              .= f/.1 by FINSEQ_6:def 1
              .= W-min L~Cage(C,n) by A1,FINSEQ_6:98;
            hence contradiction by TOPREAL5:25;
          end;
        end;
        j < len Upper_Seq(C,n) by A20,NAT_1:13;
        then
A25:    LSeg(Upper_Seq(C,n),i) = LSeg(f,i) by A17,SPPOL_2:9,XXREAL_0:2;
        LSeg(Upper_Seq(C,n),j) = LSeg(f,j) by A20,SPPOL_2:9;
        hence thesis by A17,A19,A25,A23,GOBOARD5:def 4;
      end;
      suppose
        j+1 > len Upper_Seq(C,n);
        then LSeg(Upper_Seq(C,n),j) = {} by TOPREAL1:def 5;
        then LSeg(Upper_Seq(C,n),i) /\ LSeg(Upper_Seq(C,n),j) = {};
        hence thesis by XBOOLE_0:def 7;
      end;
    end;
    hence thesis;
  end;
  cluster Lower_Seq(C,n) -> one-to-one special unfolded s.n.c.;
  coherence
  proof
    set f = Rotate(Cage(C,n),W-min L~Cage(C,n));
A26: L~Cage(C,n) = L~f by REVROT_1:33;
    W-min L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:47;
    then
A27: f/.1 = W-min L~f by A26,FINSEQ_6:98;
    then (W-max L~Cage(C,n))..f <= (N-min L~Cage(C,n))..f by A26,SPRECT_5:24;
    then (N-min L~Cage(C,n))..f > 1 by A26,A27,SPRECT_5:23,XXREAL_0:2;
    then (N-max L~Cage(C,n))..f > 1 by A26,A27,SPRECT_5:25,XXREAL_0:2;
    then
A28: (E-max L~Cage(C,n))..f > 1 by A26,A27,SPRECT_5:26,XXREAL_0:2;
    E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
    then
A29: E-max L~Cage(C,n) in rng f by FINSEQ_6:96,SPRECT_2:47;
A30: len Lower_Seq(C,n) = len f-(E-max L~Cage(C,n))..f+1 by Th13;
    now
      let i1,j1 be set;
      assume that
A31:  i1 in dom Lower_Seq(C,n) and
A32:  j1 in dom Lower_Seq(C,n) and
A33:  Lower_Seq(C,n).i1 = Lower_Seq(C,n).j1 and
A34:  i1 <> j1;
      reconsider i2 = i1, j2 = j1 as Element of NAT by A31,A32;
A35:  i2 in Seg len Lower_Seq(C,n) by A31,FINSEQ_1:def 3;
      then i2 >= 1 by FINSEQ_1:3;
      then
A36:  i2 = i2-'1+1 by XREAL_1:237;
A37:  Lower_Seq(C,n).i1 = Lower_Seq(C,n)/.i2 by A31,PARTFUN1:def 8
        .= f/.(i2-'1+(E-max L~Cage(C,n))..f) by A29,A31,A36,FINSEQ_5:55;
A38:  j2 in Seg len Lower_Seq(C,n) by A32,FINSEQ_1:def 3;
      then j2 >= 1 by FINSEQ_1:3;
      then
A39:  j2 = j2-'1+1 by XREAL_1:237;
A40:  Lower_Seq(C,n).j1 = Lower_Seq(C,n)/.j2 by A32,PARTFUN1:def 8
        .= f/.(j2-'1+(E-max L~Cage(C,n))..f) by A29,A32,A39,FINSEQ_5:55;
      0+1 <= i2 by A35,FINSEQ_1:3;
      then
A41:  i2-1 >= 0 by XREAL_1:21;
      i2 <= len f-(E-max L~Cage(C,n))..f+1 by A30,A35,FINSEQ_1:3;
      then i2-1 <= len f-(E-max L~Cage(C,n))..f by XREAL_1:22;
      then i2-1+(E-max L~Cage(C,n))..f <= len f by XREAL_1:21;
      then
A42:  i2-'1+(E-max L~Cage(C,n))..f <= len f by A41,XREAL_0:def 2;
      0+1 <= j2 by A38,FINSEQ_1:3;
      then
A43:  j2-1 >= 0 by XREAL_1:21;
      j2 <= len f-(E-max L~Cage(C,n))..f+1 by A30,A38,FINSEQ_1:3;
      then j2-1 <= len f-(E-max L~Cage(C,n))..f by XREAL_1:22;
      then j2-1+(E-max L~Cage(C,n))..f <= len f by XREAL_1:21;
      then
A44:  j2-'1+(E-max L~Cage(C,n))..f <= len f by A43,XREAL_0:def 2;
      j2-'1+(E-max L~Cage(C,n))..f >= (E-max L~Cage(C,n))..f by NAT_1:11;
      then
A45:  1 < j2-'1+(E-max L~Cage(C,n))..f by A28,XXREAL_0:2;
      i2-'1+(E-max L~Cage(C,n))..f >= (E-max L~Cage(C,n))..f by NAT_1:11;
      then
A46:  1 < i2-'1+(E-max L~Cage(C,n))..f by A28,XXREAL_0:2;
      per cases by A34,XXREAL_0:1;
      suppose
        i2 < j2;
        then i2-1 < j2-1 by XREAL_1:11;
        then i2-1 < j2-'1 by XREAL_0:def 2;
        then i2-'1 < j2-'1 by A41,XREAL_0:def 2;
        then i2-'1+(E-max L~Cage(C,n))..f < j2-'1+(E-max L~Cage(C,n))..f by
XREAL_1:8;
        hence contradiction by A33,A37,A40,A44,A46,GOBOARD7:39;
      end;
      suppose
        j2 < i2;
        then j2-1 < i2-1 by XREAL_1:11;
        then j2-1 < i2-'1 by XREAL_0:def 2;
        then j2-'1 < i2-'1 by A43,XREAL_0:def 2;
        then j2-'1+(E-max L~Cage(C,n))..f < i2-'1+(E-max L~Cage(C,n))..f by
XREAL_1:8;
        hence contradiction by A33,A37,A40,A42,A45,GOBOARD7:39;
      end;
    end;
    hence Lower_Seq(C,n) is one-to-one by FUNCT_1:def 8;
    E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
    then
A47: E-max L~Cage(C,n) in rng Rotate(Cage(C,n),W-min L~Cage(C,n)) by
FINSEQ_6:96,SPRECT_2:47;
    hence Lower_Seq(C,n) is special by SPPOL_2:41;
    thus Lower_Seq(C,n) is unfolded by A47,SPPOL_2:28;
    let i,j be Nat;
    assume
A48: i+1 < j;
    i+1 >= 1 by NAT_1:11;
    then j = j-'1+1 by A48,XREAL_1:237,XXREAL_0:2;
    then
A49: LSeg(Lower_Seq(C,n),j) = LSeg(f,j-'1+(E-max L~Cage(C,n))..f) by A47,
SPPOL_2:10;
    now
      per cases by NAT_1:2;
      suppose
        i > 0;
        then
A50:    i >= 0+1 by NAT_1:13;
        then i = i-'1+1 by XREAL_1:237;
        then
A51:    LSeg(Lower_Seq(C,n),i) = LSeg(f,i-'1+(E-max L~Cage(C,n)).. f) by A29,
SPPOL_2:10;
        i+1-1 < j-1 by A48,XREAL_1:11;
        then
A52:    i-1+1 < j-'1 by XREAL_0:def 2;
        i-1 >= 0 by A50,XREAL_1:21;
        then i-'1+1 < j-'1 by A52,XREAL_0:def 2;
        then i-'1+1+(E-max L~Cage(C,n))..f < j-'1+(E-max L~Cage(C,n))..f by
XREAL_1:8;
        then
A53:    i-'1+(E-max L~Cage(C,n))..f+1 < j-'1+(E-max L~Cage(C,n)).. f;
A54:    i-'1+(E-max L~Cage(C,n))..f > 0+1 by A28,XREAL_1:10;
        now
          per cases;
          suppose
            j-'1+(E-max L~Cage(C,n))..f+1 <= len f;
            then j-'1+(E-max L~Cage(C,n))..f < len f by NAT_1:13;
            then LSeg(Lower_Seq(C,n),i) misses LSeg(Lower_Seq(C,n),j) by A49
,A51,A53,A54,GOBOARD5:def 4;
            hence LSeg(Lower_Seq(C,n),i) /\ LSeg(Lower_Seq(C,n),j) = {} by
XBOOLE_0:def 7;
          end;
          suppose
            j-'1+(E-max L~Cage(C,n))..f+1 > len f;
            then LSeg(Lower_Seq(C,n),j) = {} by A49,TOPREAL1:def 5;
            hence LSeg(Lower_Seq(C,n),i) /\ LSeg(Lower_Seq(C,n),j) = {};
          end;
        end;
        hence LSeg(Lower_Seq(C,n),i) /\ LSeg(Lower_Seq(C,n),j) = {};
      end;
      suppose
        i = 0;
        then LSeg(Lower_Seq(C,n),i) = {} by TOPREAL1:def 5;
        hence LSeg(Lower_Seq(C,n),i) /\ LSeg(Lower_Seq(C,n),j) = {};
      end;
    end;
    hence thesis by XBOOLE_0:def 7;
  end;
end;

theorem Th14:
  for C be compact non vertical non horizontal Subset of TOP-REAL
  2 for n be Element of NAT holds len Upper_Seq(C,n) + len Lower_Seq(C,n) = len
  Cage(C,n)+1
proof
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  let n be Element of NAT;
  thus len Upper_Seq(C,n) + len Lower_Seq(C,n) = (E-max L~Cage(C,n))..Rotate(
  Cage(C,n),W-min L~Cage(C,n))+ len Lower_Seq(C,n) by Th12
    .= (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n))+ (len Rotate
  (Cage(C,n),W-min L~Cage(C,n))- (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~
  Cage(C,n))+1) by Th13
    .= (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n))+ len Rotate(
  Cage(C,n),W-min L~Cage(C,n))- (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~
  Cage(C,n))+1
    .= len Cage(C,n)+1 by REVROT_1:14;
end;

theorem Th15:
  for C be compact non vertical non horizontal Subset of TOP-REAL
2 for n be Element of NAT holds Rotate(Cage(C,n),W-min L~Cage(C,n)) = Upper_Seq
  (C,n) ^' Lower_Seq(C,n)
proof
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  let n be Element of NAT;
A1: dom Rotate(Cage(C,n),W-min L~Cage(C,n)) = Seg len Rotate(Cage(C,n),W-min
  L~Cage(C,n)) by FINSEQ_1:def 3;
A2: len (Upper_Seq(C,n) ^' Lower_Seq(C,n)) +1 = len Upper_Seq(C,n) + len
  Lower_Seq(C,n) by GRAPH_2:13
    .= len Cage(C,n)+1 by Th14
    .= len Rotate(Cage(C,n),W-min L~Cage(C,n))+1 by REVROT_1:14;
  now
    let i be Nat;
    E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
    then
A3: E-max L~Cage(C,n) in rng Rotate(Cage(C,n),W-min L~Cage(C,n)) by FINSEQ_6:96
,SPRECT_2:47;
    assume
A4: i in dom Rotate(Cage(C,n),W-min L~Cage(C,n));
    then
A5: 1 <= i by A1,FINSEQ_1:3;
A6: i <= len Rotate(Cage(C,n),W-min L~Cage(C,n)) by A1,A4,FINSEQ_1:3;
    per cases;
    suppose
A7:   i <= len Upper_Seq(C,n);
      then
      i <= (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n)) by Th12;
      then
A8:   i in Seg ((E-max L~Cage(C,n))..Rotate(Cage(C,n), W-min L~Cage(C,n))
      ) by A5,FINSEQ_1:3;
      len (Rotate(Cage(C,n),W-min L~Cage(C,n))-:E-max L~Cage(C,n)) = (
E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n)) by A3,FINSEQ_5:45;
      then
A9:   i in dom (Rotate(Cage(C,n),W-min L~Cage(C,n))-: E-max L~Cage(C,n))
      by A8,FINSEQ_1:def 3;
      i in NAT by ORDINAL1:def 13;
      hence (Upper_Seq(C,n) ^' Lower_Seq(C,n)).i = (Rotate(Cage(C,n),W-min L~
      Cage(C,n))-:E-max L~Cage(C,n)).i by A5,A7,GRAPH_2:14
        .= (Rotate(Cage(C,n),W-min L~Cage(C,n))-:E-max L~Cage(C,n))/.i by A9,
PARTFUN1:def 8
        .= Rotate(Cage(C,n),W-min L~Cage(C,n))/.i by A3,A8,FINSEQ_5:46
        .= Rotate(Cage(C,n),W-min L~Cage(C,n)).i by A4,PARTFUN1:def 8;
    end;
    suppose
      i > len Upper_Seq(C,n);
      then i >= len Upper_Seq(C,n)+1 by NAT_1:13;
      then consider j be Nat such that
A10:  i = len Upper_Seq(C,n)+1+j by NAT_1:10;
      reconsider j as Element of NAT by ORDINAL1:def 13;
A11:  i = len Upper_Seq(C,n)+(j+1) by A10;
      then
A12:  i = (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n))+(j+1)
      by Th12;
A13:  len (Rotate(Cage(C,n),W-min L~Cage(C,n)):-E-max L~Cage(C,n)) = len
Rotate(Cage(C,n),W-min L~Cage(C,n))- (E-max L~Cage(C,n))..Rotate(Cage(C,n),
      W-min L~Cage(C,n))+1 by A3,FINSEQ_5:53;
      j+1+(E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n)) <= len
      Rotate(Cage(C,n),W-min L~Cage(C,n)) by A6,A11,Th12;
      then
      j+1 <= len Rotate(Cage(C,n),W-min L~Cage(C,n))- (E-max L~Cage(C,n))
      ..Rotate(Cage(C,n),W-min L~Cage(C,n)) by XREAL_1:21;
      then j+1+1 >= 1 & j+1+1 <= len (Rotate(Cage(C,n),W-min L~Cage(C,n)):- (
      E-max L~ Cage(C,n))) by A13,NAT_1:11,XREAL_1:9;
      then
A14:  j+1+1 in dom (Rotate(Cage(C,n),W-min L~Cage(C,n)):- (E-max L~Cage(C
      ,n))) by FINSEQ_3:27;
      i < len (Upper_Seq(C,n) ^' Lower_Seq(C,n))+1 by A2,A6,NAT_1:13;
      then i < len Lower_Seq(C,n) + len Upper_Seq(C,n) by GRAPH_2:13;
      then i-len Upper_Seq(C,n) < len Lower_Seq(C,n) by XREAL_1:21;
      hence (Upper_Seq(C,n) ^' Lower_Seq(C,n)).i = (Rotate(Cage(C,n),W-min L~
      Cage(C,n)):-(E-max L~Cage(C,n))). (j+1+1) by A11,GRAPH_2:15,NAT_1:11
        .= (Rotate(Cage(C,n),W-min L~Cage(C,n)):-(E-max L~Cage(C,n)))/. (j+1
      +1) by A14,PARTFUN1:def 8
        .= Rotate(Cage(C,n),W-min L~Cage(C,n))/. (j+1+(E-max L~Cage(C,n))..
      Rotate(Cage(C,n),W-min L~Cage(C,n))) by A3,A14,FINSEQ_5:55
        .= Rotate(Cage(C,n),W-min L~Cage(C,n)).i by A4,A12,PARTFUN1:def 8;
    end;
  end;
  hence thesis by A2,FINSEQ_2:10;
end;

theorem
  for C be compact non vertical non horizontal Subset of TOP-REAL 2 for
  n be Element of NAT holds L~Cage(C,n) = L~(Upper_Seq(C,n) ^' Lower_Seq(C,n))
proof
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  let n be Element of NAT;
  Rotate(Cage(C,n),W-min L~Cage(C,n)) = Upper_Seq(C,n) ^' Lower_Seq(C,n)
  by Th15;
  hence thesis by REVROT_1:33;
end;

theorem
  for C be compact non vertical non horizontal non empty Subset of
  TOP-REAL 2 for n be Element of NAT holds L~Cage(C,n) = L~Upper_Seq(C,n) \/ L~
  Lower_Seq(C,n)
proof
  let C be compact non vertical non horizontal non empty Subset of TOP-REAL 2;
  let n be Element of NAT;
  E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
  then E-max L~Cage(C,n) in rng Rotate(Cage(C,n),W-min L~Cage(C,n)) by
FINSEQ_6:96,SPRECT_2:47;
  then
  L~Rotate(Cage(C,n),W-min L~Cage(C,n)) = L~Upper_Seq(C,n) \/ L~Lower_Seq(
  C,n) by SPPOL_2:25;
  hence thesis by REVROT_1:33;
end;

theorem Th18:
  for P be Simple_closed_curve holds W-min(P) <> E-min(P)
proof
  let P be Simple_closed_curve;
  now
    assume W-min(P) = E-min(P);
    then W-bound P= E-bound P by SPPOL_2:1;
    hence contradiction by TOPREAL5:23;
  end;
  hence thesis;
end;

theorem Th19:
  for C be compact non vertical non horizontal Subset of TOP-REAL
2 for n be Element of NAT holds len Upper_Seq(C,n) >= 3 & len Lower_Seq(C,n) >=
  3
proof
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  let n be Element of NAT;
  set pWi = W-min L~Cage(C,n);
  set pWa = W-max L~Cage(C,n);
  set pEi = E-min L~Cage(C,n);
  set pEa = E-max L~Cage(C,n);
A1: pWi <> pEa by TOPREAL5:25;
  set f=Rotate(Cage(C,n),W-min L~Cage(C,n));
A2: W-min L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:47;
  E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
  then
A3: E-max L~Cage(C,n) in rng Rotate(Cage(C,n),W-min L~Cage(C,n)) by FINSEQ_6:96
,SPRECT_2:47;
  then
  Lower_Seq(C,n)/.(len Lower_Seq(C,n)) = Rotate(Cage(C,n),W-min L~Cage(C,
  n))/. (len Rotate(Cage(C,n),W-min L~Cage(C,n))) by FINSEQ_5:57
    .= Rotate(Cage(C,n),W-min L~Cage(C,n))/.1 by FINSEQ_6:def 1
    .= W-min L~Cage(C,n) by A2,FINSEQ_6:98;
  then
A4: pEa in rng Lower_Seq(C,n) & pWi in rng Lower_Seq(C,n) by FINSEQ_6:66
,REVROT_1:3;
  (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n)) <= (E-max L~
  Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n));
  then
A5: pEa in rng Upper_Seq(C,n) by A3,FINSEQ_5:49;
  pWa in rng Cage(C,n) by SPRECT_2:48;
  then
A6: pWa in rng Rotate(Cage(C,n),W-min L~Cage(C,n)) by FINSEQ_6:96,SPRECT_2:47;
A7: Upper_Seq(C,n)/.1 = Rotate(Cage(C,n),W-min L~Cage(C,n))/.1 by A3,
FINSEQ_5:47;
  then
A8: Upper_Seq(C,n)/.1 = W-min L~Cage(C,n) by A2,FINSEQ_6:98;
  then
A9: pWi in rng Upper_Seq(C,n) by FINSEQ_6:46;
A10: L~Cage(C,n) = L~f by REVROT_1:33;
  then (W-max L~f)..f <= (N-min L~f)..f by A2,A7,FINSEQ_6:98,SPRECT_5:24;
  then
A11: (W-max L~f)..f < (N-max L~f)..f by A7,A8,A10,SPRECT_5:25,XXREAL_0:2;
  (N-max L~f)..f <= (E-max L~f)..f by A2,A7,A10,FINSEQ_6:98,SPRECT_5:26;
  then
A12: pWa in rng Upper_Seq(C,n) by A3,A6,A10,A11,FINSEQ_5:49,XXREAL_0:2;
  {pWi,pWa,pEa} c= rng Upper_Seq(C,n)
  proof
    let x be set;
    assume x in {pWi,pWa,pEa};
    hence thesis by A5,A9,A12,ENUMSET1:def 1;
  end;
  then
A13: card {pWi,pWa,pEa} c= card rng Upper_Seq(C,n) by CARD_1:27;
  card rng Upper_Seq(C,n) c= card dom Upper_Seq(C,n) by CARD_2:80;
  then
A14: card rng Upper_Seq(C,n) c= len Upper_Seq(C,n) by CARD_1:104;
  pWi <> pWa & pWa <> pEa by JORDAN1B:6,SPRECT_2:62;
  then card {pWi,pWa,pEa} = 3 by A1,CARD_2:77;
  then 3 c= len Upper_Seq(C,n) by A13,A14,XBOOLE_1:1;
  hence len Upper_Seq(C,n) >= 3 by NAT_1:40;
A15: pWi <> pEa by TOPREAL5:25;
  pEi in rng Cage(C,n) by SPRECT_2:49;
  then
A16: pEi in rng Rotate(Cage(C,n),W-min L~Cage(C,n)) by FINSEQ_6:96,SPRECT_2:47;
  pEi..Rotate(Cage(C,n),W-min L~Cage(C,n)) > pEa..Rotate(Cage(C,n),W-min
  L~Cage(C,n)) by A2,A7,A10,FINSEQ_6:98,SPRECT_5:27;
  then
A17: pEi in rng Lower_Seq(C,n) by A3,A16,FINSEQ_6:67;
  {pWi,pEi,pEa} c= rng Lower_Seq(C,n)
  proof
    let x be set;
    assume x in {pWi,pEi,pEa};
    hence thesis by A4,A17,ENUMSET1:def 1;
  end;
  then
A18: card {pWi,pEi,pEa} c= card rng Lower_Seq(C,n) by CARD_1:27;
  card rng Lower_Seq(C,n) c= card dom Lower_Seq(C,n) by CARD_2:80;
  then
A19: card rng Lower_Seq(C,n) c= len Lower_Seq(C,n) by CARD_1:104;
  pWi <> pEi & pEi <> pEa by Th18,SPRECT_2:58;
  then card {pWi,pEi,pEa} = 3 by A15,CARD_2:77;
  then 3 c= len Lower_Seq(C,n) by A18,A19,XBOOLE_1:1;
  hence thesis by NAT_1:40;
end;

registration
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  let n be Element of NAT;
  cluster Upper_Seq(C,n) -> being_S-Seq;
  coherence
  proof
    len Upper_Seq(C,n) >= 3 by Th19;
    then len Upper_Seq(C,n) >= 2 by XXREAL_0:2;
    hence thesis by TOPREAL1:def 10;
  end;
  cluster Lower_Seq(C,n) -> being_S-Seq;
  coherence
  proof
    len Lower_Seq(C,n) >= 3 by Th19;
    then len Lower_Seq(C,n) >= 2 by XXREAL_0:2;
    hence thesis by TOPREAL1:def 10;
  end;
end;

theorem
  for C be compact non vertical non horizontal Subset of TOP-REAL 2 for
n be Element of NAT holds L~Upper_Seq(C,n) /\ L~Lower_Seq(C,n) = {W-min L~Cage(
  C,n),E-max L~Cage(C,n)}
proof
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  let n be Element of NAT;
  E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
  then
A1: E-max L~Cage(C,n) in rng Rotate(Cage(C,n),W-min L~Cage(C,n)) by FINSEQ_6:96
,SPRECT_2:47;
A2: len Upper_Seq(C,n) >= 3 by Th19;
  then
A3: rng Upper_Seq(C,n) c= L~Upper_Seq(C,n) by SPPOL_2:18,XXREAL_0:2;
A4: W-min L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:47;
  thus L~Upper_Seq(C,n) /\ L~Lower_Seq(C,n) c= {W-min L~Cage(C,n),E-max L~Cage
  (C,n)}
  proof
    set pW=W-min L~Cage(C,n);
    set pE=E-max L~Cage(C,n);
    set f=Rotate(Cage(C,n),W-min L~Cage(C,n));
    let x be set;
    assume
A5: x in L~Upper_Seq(C,n) /\ L~Lower_Seq(C,n);
    then reconsider x1=x as Point of TOP-REAL 2;
    assume
A6: not x in {W-min L~Cage(C,n),E-max L~Cage(C,n)};
    x in L~Upper_Seq(C,n) by A5,XBOOLE_0:def 4;
    then consider i1 be Element of NAT such that
A7: 1 <= i1 and
A8: i1+1 <= len Upper_Seq(C,n) and
A9: x1 in LSeg(Upper_Seq(C,n),i1) by SPPOL_2:13;
A10: LSeg(Upper_Seq(C,n),i1) = LSeg(f,i1) by A8,SPPOL_2:9;
    x in L~Lower_Seq(C,n) by A5,XBOOLE_0:def 4;
    then consider i2 be Element of NAT such that
A11: 1 <= i2 and
A12: i2+1 <= len Lower_Seq(C,n) and
A13: x1 in LSeg(Lower_Seq(C,n),i2) by SPPOL_2:13;
    set i3=i2-'1;
A14: i3+1 = i2 by A11,XREAL_1:237;
    then
A15: 1+pE..f <= i3+1+pE..f by A11,XREAL_1:9;
A16: len Lower_Seq(C,n) = len f-pE..f+1 by Th13;
    then i2 < len f-pE..f+1 by A12,NAT_1:13;
    then i2-1 < len f-pE..f by XREAL_1:21;
    then
A17: i2-1+pE..f < len f by XREAL_1:22;
    i2-1 >= 1-1 by A11,XREAL_1:11;
    then
A18: i3+pE..f < len f by A17,XREAL_0:def 2;
A19: i3+pE..f >= 0 by NAT_1:2;
A20: LSeg(Lower_Seq(C,n),i2) = LSeg(f,i3+pE..f) by A1,A14,SPPOL_2:10;
A21: len Upper_Seq(C,n) = pE..f by Th12;
    then i1+1 < pE..f+1 by A8,NAT_1:13;
    then i1+1 < i3+pE..f+1 by A15,XXREAL_0:2;
    then
A22: i1+1 <= i3+pE..f by NAT_1:13;
A23: pE..f-'1+1 = pE..f by A1,FINSEQ_4:31,XREAL_1:237;
    i3+1 < len f-pE..f+1 by A12,A14,A16,NAT_1:13;
    then i3 < len f-pE..f by XREAL_1:9;
    then
A24: i3+pE..f < len f by XREAL_1:22;
    then
A25: i3+pE..f+1 <= len f by NAT_1:13;
    now
      per cases by A7,A22,XXREAL_0:1;
      suppose
        i1+1 < i3+pE..f & i1 > 1;
        then LSeg(f,i1) misses LSeg(f,i3+pE..f) by A24,GOBOARD5:def 4;
        then LSeg(f,i1) /\ LSeg(f,i3+pE..f) = {} by XBOOLE_0:def 7;
        hence contradiction by A9,A13,A10,A20,XBOOLE_0:def 4;
      end;
      suppose
A26:    i1 = 1;
        i3+pE..f >= 0+3 by A2,A21,XREAL_1:9;
        then
A27:    i1+1 < i3+pE..f by A26,XXREAL_0:2;
        now
          per cases by A25,XXREAL_0:1;
          suppose
            i3+pE..f+1 < len f;
            then LSeg(f,i1) misses LSeg(f,i3+pE..f) by A27,GOBOARD5:def 4;
            then LSeg(f,i1) /\ LSeg(f,i3+pE..f) = {} by XBOOLE_0:def 7;
            hence contradiction by A9,A13,A10,A20,XBOOLE_0:def 4;
          end;
          suppose
            i3+pE..f+1 = len f;
            then i3+pE..f = len f-1;
            then i3+pE..f = len f-'1 by A19,XREAL_0:def 2;
            then LSeg(f,i1) /\ LSeg(f,i3+pE..f) = {f/.1} by A26,GOBOARD7:36
,REVROT_1:30;
            then x1 in {f/.1} by A9,A13,A10,A20,XBOOLE_0:def 4;
            then x1 = f/.1 by TARSKI:def 1
              .= pW by A4,FINSEQ_6:98;
            hence contradiction by A6,TARSKI:def 2;
          end;
        end;
        hence contradiction;
      end;
      suppose
A28:    i1+1 = i3+pE..f;
        i3+pE..f >= pE..f by NAT_1:11;
        then pE..f < len f by A18,XXREAL_0:2;
        then pE..f+1 <= len f by NAT_1:13;
        then
A29:    pE..f-'1 + (1+1) <= len f by A23;
        0+pE..f <= i3+pE..f by XREAL_1:9;
        then pE..f = i1+1 by A8,A21,A28,XXREAL_0:1;
        then LSeg(f,i1) /\ LSeg(f,i3+(pE..f)) = {f/.(pE..f)} by A7,A23,A28,A29,
TOPREAL1:def 8;
        then x1 in {f/.(pE..f)} by A9,A13,A10,A20,XBOOLE_0:def 4;
        then x1 = f/.(pE..f) by TARSKI:def 1
          .= pE by A1,FINSEQ_5:41;
        hence contradiction by A6,TARSKI:def 2;
      end;
    end;
    hence contradiction;
  end;
  Lower_Seq(C,n)/.(len Lower_Seq(C,n)) = Rotate(Cage(C,n),W-min L~Cage(C,
  n))/. (len Rotate(Cage(C,n),W-min L~Cage(C,n))) by A1,FINSEQ_5:57
    .= Rotate(Cage(C,n),W-min L~Cage(C,n))/.1 by FINSEQ_6:def 1
    .= W-min L~Cage(C,n) by A4,FINSEQ_6:98;
  then
A30: W-min L~Cage(C,n) in rng Lower_Seq(C,n) by REVROT_1:3;
  (E-max L~Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n)) <= (E-max L~
  Cage(C,n))..Rotate(Cage(C,n),W-min L~Cage(C,n));
  then
A31: E-max L~Cage(C,n) in rng Lower_Seq(C,n) & E-max L~Cage(C,n) in rng
  Upper_Seq (C,n) by A1,FINSEQ_5:49,FINSEQ_6:66;
  len Lower_Seq(C,n) >= 3 by Th19;
  then
A32: rng Lower_Seq(C,n) c= L~Lower_Seq(C,n) by SPPOL_2:18,XXREAL_0:2;
  Upper_Seq(C,n)/.1 = Rotate(Cage(C,n),W-min L~Cage(C,n))/.1 by A1,FINSEQ_5:47
    .= W-min L~Cage(C,n) by A4,FINSEQ_6:98;
  then
A33: W-min L~Cage(C,n) in rng Upper_Seq(C,n) by FINSEQ_6:46;
  thus {W-min L~Cage(C,n),E-max L~Cage(C,n)} c= L~Upper_Seq(C,n) /\ L~
  Lower_Seq(C,n)
  proof
    let x be set;
    assume
A34: x in {W-min L~Cage(C,n),E-max L~Cage(C,n)};
    per cases by A34,TARSKI:def 2;
    suppose
      x = W-min L~Cage(C,n);
      hence thesis by A32,A3,A33,A30,XBOOLE_0:def 4;
    end;
    suppose
      x = E-max L~Cage(C,n);
      hence thesis by A32,A3,A31,XBOOLE_0:def 4;
    end;
  end;
end;

theorem
  for C be compact non vertical non horizontal Subset of TOP-REAL 2
  holds Upper_Seq(C,n) is_in_the_area_of Cage(C,n)
proof
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
  then E-max L~Cage(C,n) in rng Rotate(Cage(C,n),W-min L~Cage(C,n)) by
FINSEQ_6:96,SPRECT_2:47;
  hence thesis by Th5,JORDAN1B:11;
end;

theorem
  for C be compact non vertical non horizontal Subset of TOP-REAL 2
  holds Lower_Seq(C,n) is_in_the_area_of Cage(C,n)
proof
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
  then E-max L~Cage(C,n) in rng Rotate(Cage(C,n),W-min L~Cage(C,n)) by
FINSEQ_6:96,SPRECT_2:47;
  hence thesis by Th6,JORDAN1B:11;
end;

theorem
  for C be compact connected non vertical non horizontal Subset of
  TOP-REAL 2 holds (Cage(C,n)/.2)`2 = N-bound L~Cage(C,n)
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
  Cage(C,n)/.1 = N-min L~Cage(C,n) by JORDAN9:34;
  then Cage(C,n)/.2 in N-most L~Cage(C,n) by SPRECT_2:34;
  then (Cage(C,n)/.2)`2 = (N-min L~Cage(C,n))`2 by PSCOMP_1:98;
  hence thesis by EUCLID:56;
end;

theorem
  for C be compact connected non vertical non horizontal Subset of
TOP-REAL 2 for k be Element of NAT st 1 <= k & k+1 <= len Cage(C,n) & Cage(C,n)
  /.k = E-max L~Cage(C,n) holds (Cage(C,n)/.(k+1))`1 = E-bound L~Cage(C,n)
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
A1: Cage(C,n) is_sequence_on Gauge(C,n) by JORDAN9:def 1;
A2: len Gauge(C,n) = width Gauge(C,n) by JORDAN8:def 1;
A3: Cage(C,n)/.1 = N-min L~Cage(C,n) by JORDAN9:34;
  then 1 < (N-max L~Cage(C,n))..Cage(C,n) by SPRECT_2:73;
  then
A4: (E-max L~Cage(C,n))..Cage(C,n) > 1 by A3,SPRECT_2:74,XXREAL_0:2;
  let k be Element of NAT;
  assume that
A5: 1 <= k and
A6: k+1 <= len Cage(C,n) and
A7: Cage(C,n)/.k = E-max L~Cage(C,n);
A8: k < len Cage(C,n) by A6,NAT_1:13;
  then
A9: k in dom Cage(C,n) by A5,FINSEQ_3:27;
  then reconsider k9=k-1 as Element of NAT by FINSEQ_3:28;
  (E-max L~Cage(C,n))..Cage(C,n) <= k by A7,A9,FINSEQ_5:42;
  then
A10: k > 1 by A4,XXREAL_0:2;
  then consider i1,j1,i2,j2 be Element of NAT such that
A11: [i1,j1] in Indices Gauge(C,n) and
A12: Cage(C,n)/.k = Gauge(C,n)*(i1,j1) and
A13: [i2,j2] in Indices Gauge(C,n) and
A14: Cage(C,n)/.(k+1) = Gauge(C,n)*(i2,j2) and
A15: i1 = i2 & j1+1 = j2 or i1+1 = i2 & j1 = j2 or i1 = i2+1 & j1 = j2
  or i1 = i2 & j1 = j2+1 by A1,A6,JORDAN8:6;
A16: 1 <= i1 by A11,MATRIX_1:39;
A17: k9+1 < len Cage(C,n) by A6,NAT_1:13;
A18: 1 <= j1 by A11,MATRIX_1:39;
A19: j2 <= width Gauge(C,n) by A13,MATRIX_1:39;
A20: i2 <= len Gauge(C,n) by A13,MATRIX_1:39;
A21: j1 <= width Gauge(C,n) by A11,MATRIX_1:39;
  Gauge(C,n)*(i1,j1)`1 = E-bound L~Cage(C,n) by A7,A12,EUCLID:56
    .= Gauge(C,n)*(len Gauge(C,n),j1)`1 by A18,A21,A2,JORDAN1A:92;
  then
A22: i1 >= len Gauge(C,n) by A16,A18,A21,GOBOARD5:4;
  k >= 1+1 by A10,NAT_1:13;
  then
A23: k9 >= 1+1-1 by XREAL_1:11;
  then consider i3,j3,i4,j4 be Element of NAT such that
A24: [i3,j3] in Indices Gauge(C,n) and
A25: Cage(C,n)/.k9 = Gauge(C,n)*(i3,j3) and
A26: [i4,j4] in Indices Gauge(C,n) and
A27: Cage(C,n)/.(k9+1) = Gauge(C,n)*(i4,j4) and
A28: i3 = i4 & j3+1 = j4 or i3+1 = i4 & j3 = j4 or i3 = i4+1 & j3 = j4
  or i3 = i4 & j3 = j4+1 by A1,A8,JORDAN8:6;
A29: i1 = i4 by A11,A12,A26,A27,GOBOARD1:21;
A30: j1 = j4 by A11,A12,A26,A27,GOBOARD1:21;
A31: 1 <= j3 by A24,MATRIX_1:39;
A32: j3 <= width Gauge(C,n) by A24,MATRIX_1:39;
A33: i1 <= len Gauge(C,n) by A11,MATRIX_1:39;
  then
A34: i1 = len Gauge(C,n) by A22,XXREAL_0:1;
A35: j3 = j4
  proof
    assume
A36: j3 <> j4;
    per cases by A28,A36;
    suppose
      i3 = i4 & j3+1 = j4;
      hence contradiction by A8,A22,A23,A24,A25,A26,A27,A29,JORDAN10:1;
    end;
    suppose
A37:  i3 = i4 & j3 = j4+1;
      k9 < len Cage(C,n) by A17,NAT_1:13;
      then
      Gauge(C,n)*(i3,j3) in E-most L~Cage(C,n) by A34,A23,A25,A29,A31,A32,A37,
JORDAN1A:82;
      then
A38:  (Gauge(C,n)*(i4,j4+1))`2 <= (Gauge(C,n)*(i4,j4))`2 by A7,A27,A37,
PSCOMP_1:108;
      j4 < j4+1 by NAT_1:13;
      hence contradiction by A16,A33,A18,A29,A30,A32,A37,A38,GOBOARD5:5;
    end;
  end;
A39: 1 <= i2 & 1 <= j2 by A13,MATRIX_1:39;
A40: k9+1 = k;
A41: i3 <= len Gauge(C,n) by A24,MATRIX_1:39;
  i1 = i2
  proof
    assume
A42: i1 <> i2;
    per cases by A15,A42;
    suppose
      i1+1 = i2 & j1 = j2;
      hence contradiction by A20,A22,NAT_1:13;
    end;
    suppose
A43:  i1 = i2+1 & j1 = j2;
      k9+(1+1) <= len Cage(C,n) by A6;
      then
A44:  LSeg(Cage(C,n),k9) /\ LSeg(Cage(C,n),k) = {Cage(C,n)/.k} by A23,A40,
TOPREAL1:def 8;
      Cage(C,n)/.k9 in LSeg(Cage(C,n),k9) & Cage(C,n)/.(k+1) in LSeg(Cage
      (C,n),k) by A5,A6,A8,A23,A40,TOPREAL1:27;
      then
      Cage(C,n)/.(k+1) in {Cage(C,n)/.k} by A14,A22,A25,A28,A29,A30,A41,A35,A43
,A44,NAT_1:13,XBOOLE_0:def 4;
      then Cage(C,n)/.(k+1) = Cage(C,n)/.k by TARSKI:def 1;
      hence contradiction by A11,A12,A13,A14,A42,GOBOARD1:21;
    end;
  end;
  then Gauge(C,n)*(i1,j1)`1 = Gauge(C,n)*(i2,1)`1 by A16,A33,A18,A21,GOBOARD5:3
    .= Gauge(C,n)*(i2,j2)`1 by A20,A39,A19,GOBOARD5:3;
  hence thesis by A7,A12,A14,EUCLID:56;
end;

theorem
  for C be compact connected non vertical non horizontal Subset of
TOP-REAL 2 for k be Element of NAT st 1 <= k & k+1 <= len Cage(C,n) & Cage(C,n)
  /.k = S-max L~Cage(C,n) holds (Cage(C,n)/.(k+1))`2 = S-bound L~Cage(C,n)
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
A1: Cage(C,n) is_sequence_on Gauge(C,n) by JORDAN9:def 1;
A2: Cage(C,n)/.1 = N-min L~Cage(C,n) by JORDAN9:34;
  then 1 < (N-max L~Cage(C,n))..Cage(C,n) by SPRECT_2:73;
  then 1 < (E-max L~Cage(C,n))..Cage(C,n) by A2,SPRECT_2:74,XXREAL_0:2;
  then 1 < (E-min L~Cage(C,n))..Cage(C,n) by A2,SPRECT_2:75,XXREAL_0:2;
  then
A3: (S-max L~Cage(C,n))..Cage(C,n) > 1 by A2,SPRECT_2:76,XXREAL_0:2;
  let k be Element of NAT;
  assume that
A4: 1 <= k and
A5: k+1 <= len Cage(C,n) and
A6: Cage(C,n)/.k = S-max L~Cage(C,n);
A7: k < len Cage(C,n) by A5,NAT_1:13;
  then
A8: k in dom Cage(C,n) by A4,FINSEQ_3:27;
  then reconsider k9=k-1 as Element of NAT by FINSEQ_3:28;
  (S-max L~Cage(C,n))..Cage(C,n) <= k by A6,A8,FINSEQ_5:42;
  then
A9: k > 1 by A3,XXREAL_0:2;
  then consider i1,j1,i2,j2 be Element of NAT such that
A10: [i1,j1] in Indices Gauge(C,n) and
A11: Cage(C,n)/.k = Gauge(C,n)*(i1,j1) and
A12: [i2,j2] in Indices Gauge(C,n) and
A13: Cage(C,n)/.(k+1) = Gauge(C,n)*(i2,j2) and
A14: i1 = i2 & j1+1 = j2 or i1+1 = i2 & j1 = j2 or i1 = i2+1 & j1 = j2
  or i1 = i2 & j1 = j2+1 by A1,A5,JORDAN8:6;
A15: 1 <= i1 by A10,MATRIX_1:39;
A16: j2 <= width Gauge(C,n) by A12,MATRIX_1:39;
A17: 1 <= j2 by A12,MATRIX_1:39;
A18: j1 <= width Gauge(C,n) by A10,MATRIX_1:39;
A19: k9+1 < len Cage(C,n) by A5,NAT_1:13;
A20: i1 <= len Gauge(C,n) by A10,MATRIX_1:39;
  Gauge(C,n)*(i1,j1)`2 = S-bound L~Cage(C,n) by A6,A11,EUCLID:56
    .= Gauge(C,n)*(i1,1)`2 by A15,A20,JORDAN1A:93;
  then
A21: j1 <= 1 by A15,A20,A18,GOBOARD5:5;
  k >= 1+1 by A9,NAT_1:13;
  then
A22: k9 >= 1+1-1 by XREAL_1:11;
  then consider i3,j3,i4,j4 be Element of NAT such that
A23: [i3,j3] in Indices Gauge(C,n) and
A24: Cage(C,n)/.k9 = Gauge(C,n)*(i3,j3) and
A25: [i4,j4] in Indices Gauge(C,n) and
A26: Cage(C,n)/.(k9+1) = Gauge(C,n)*(i4,j4) and
A27: i3 = i4 & j3+1 = j4 or i3+1 = i4 & j3 = j4 or i3 = i4+1 & j3 = j4
  or i3 = i4 & j3 = j4+1 by A1,A7,JORDAN8:6;
A28: i1 = i4 by A10,A11,A25,A26,GOBOARD1:21;
A29: j1 = j4 by A10,A11,A25,A26,GOBOARD1:21;
A30: 1 <= i3 by A23,MATRIX_1:39;
A31: i3 <= len Gauge(C,n) by A23,MATRIX_1:39;
A32: 1 <= j1 by A10,MATRIX_1:39;
  then
A33: j1 = 1 by A21,XXREAL_0:1;
A34: i3 = i4
  proof
    assume
A35: i3 <> i4;
    per cases by A27,A35;
    suppose
      j3 = j4 & i3+1 = i4;
      hence contradiction by A7,A21,A22,A23,A24,A25,A26,A29,JORDAN10:3;
    end;
    suppose
A36:  j3 = j4 & i3 = i4+1;
      k9 < len Cage(C,n) by A19,NAT_1:13;
      then
      Gauge(C,n)*(i3,j3) in S-most L~Cage(C,n) by A33,A22,A24,A29,A30,A31,A36,
JORDAN1A:81;
      then
A37:  (Gauge(C,n)*(i4+1,j4))`1 <= (Gauge(C,n)*(i4,j4))`1 by A6,A26,A36,
PSCOMP_1:118;
      i4 < i4+1 by NAT_1:13;
      hence contradiction by A15,A32,A18,A28,A29,A31,A36,A37,GOBOARD5:4;
    end;
  end;
A38: 1 <= i2 & i2 <= len Gauge(C,n) by A12,MATRIX_1:39;
A39: k9+1 = k;
A40: 1 <= j3 by A23,MATRIX_1:39;
  j1 = j2
  proof
    assume
A41: j1 <> j2;
    per cases by A14,A41;
    suppose
      j1 = j2+1 & i1 = i2;
      hence contradiction by A17,A21,NAT_1:13;
    end;
    suppose
A42:  j1+1 = j2 & i1 = i2;
      k9+(1+1) <= len Cage(C,n) by A5;
      then
A43:  LSeg(Cage(C,n),k9) /\ LSeg(Cage(C,n),k) = {Cage(C,n)/.k} by A22,A39,
TOPREAL1:def 8;
      Cage(C,n)/.k9 in LSeg(Cage(C,n),k9) & Cage(C,n)/.(k+1) in LSeg(Cage
      (C,n),k) by A4,A5,A7,A22,A39,TOPREAL1:27;
      then Cage(C,n)/.(k+1) in {Cage(C,n)/.k} by A13,A21,A24,A27,A28,A29,A40
,A34,A42,A43,NAT_1:13,XBOOLE_0:def 4;
      then Cage(C,n)/.(k+1) = Cage(C,n)/.k by TARSKI:def 1;
      hence contradiction by A10,A11,A12,A13,A41,GOBOARD1:21;
    end;
  end;
  then Gauge(C,n)*(i1,j1)`2 = Gauge(C,n)*(1,j2)`2 by A15,A20,A32,A18,GOBOARD5:2
    .= Gauge(C,n)*(i2,j2)`2 by A38,A17,A16,GOBOARD5:2;
  hence thesis by A6,A11,A13,EUCLID:56;
end;

theorem
  for C be compact connected non vertical non horizontal Subset of
TOP-REAL 2 for k be Element of NAT st 1 <= k & k+1 <= len Cage(C,n) & Cage(C,n)
  /.k = W-min L~Cage(C,n) holds (Cage(C,n)/.(k+1))`1 = W-bound L~Cage(C,n)
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
A1: Cage(C,n) is_sequence_on Gauge(C,n) by JORDAN9:def 1;
A2: Cage(C,n)/.1 = N-min L~Cage(C,n) by JORDAN9:34;
  then 1 < (N-max L~Cage(C,n))..Cage(C,n) by SPRECT_2:73;
  then 1 < (E-max L~Cage(C,n))..Cage(C,n) by A2,SPRECT_2:74,XXREAL_0:2;
  then 1 < (E-min L~Cage(C,n))..Cage(C,n) by A2,SPRECT_2:75,XXREAL_0:2;
  then 1 < (S-max L~Cage(C,n))..Cage(C,n) by A2,SPRECT_2:76,XXREAL_0:2;
  then 1 < (S-min L~Cage(C,n))..Cage(C,n) by A2,SPRECT_2:77,XXREAL_0:2;
  then
A3: (W-min L~Cage(C,n))..Cage(C,n) > 1 by A2,SPRECT_2:78,XXREAL_0:2;
  let k be Element of NAT;
  assume that
A4: 1 <= k and
A5: k+1 <= len Cage(C,n) and
A6: Cage(C,n)/.k = W-min L~Cage(C,n);
A7: k < len Cage(C,n) by A5,NAT_1:13;
  then
A8: k in dom Cage(C,n) by A4,FINSEQ_3:27;
  then reconsider k9=k-1 as Element of NAT by FINSEQ_3:28;
  (W-min L~Cage(C,n))..Cage(C,n) <= k by A6,A8,FINSEQ_5:42;
  then
A9: k > 1 by A3,XXREAL_0:2;
  then consider i1,j1,i2,j2 be Element of NAT such that
A10: [i1,j1] in Indices Gauge(C,n) and
A11: Cage(C,n)/.k = Gauge(C,n)*(i1,j1) and
A12: [i2,j2] in Indices Gauge(C,n) and
A13: Cage(C,n)/.(k+1) = Gauge(C,n)*(i2,j2) and
A14: i1 = i2 & j1+1 = j2 or i1+1 = i2 & j1 = j2 or i1 = i2+1 & j1 = j2
  or i1 = i2 & j1 = j2+1 by A1,A5,JORDAN8:6;
A15: i1 <= len Gauge(C,n) by A10,MATRIX_1:39;
A16: j2 <= width Gauge(C,n) by A12,MATRIX_1:39;
A17: 1 <= i2 by A12,MATRIX_1:39;
A18: j1 <= width Gauge(C,n) by A10,MATRIX_1:39;
A19: len Gauge(C,n) = width Gauge(C,n) by JORDAN8:def 1;
A20: i2 <= len Gauge(C,n) & 1 <= j2 by A12,MATRIX_1:39;
A21: k9+1 = k;
A22: k9+1 < len Cage(C,n) by A5,NAT_1:13;
A23: 1 <= j1 by A10,MATRIX_1:39;
  Gauge(C,n)*(i1,j1)`1 = W-bound L~Cage(C,n) by A6,A11,EUCLID:56
    .= Gauge(C,n)*(1,j1)`1 by A23,A18,A19,JORDAN1A:94;
  then
A24: i1 <= 1 by A15,A23,A18,GOBOARD5:4;
  k >= 1+1 by A9,NAT_1:13;
  then
A25: k9 >= 1+1-1 by XREAL_1:11;
  then consider i3,j3,i4,j4 be Element of NAT such that
A26: [i3,j3] in Indices Gauge(C,n) and
A27: Cage(C,n)/.k9 = Gauge(C,n)*(i3,j3) and
A28: [i4,j4] in Indices Gauge(C,n) and
A29: Cage(C,n)/.(k9+1) = Gauge(C,n)*(i4,j4) and
A30: i3 = i4 & j3+1 = j4 or i3+1 = i4 & j3 = j4 or i3 = i4+1 & j3 = j4
  or i3 = i4 & j3 = j4+1 by A1,A7,JORDAN8:6;
A31: i1 = i4 by A10,A11,A28,A29,GOBOARD1:21;
A32: j1 = j4 by A10,A11,A28,A29,GOBOARD1:21;
A33: j3 <= width Gauge(C,n) by A26,MATRIX_1:39;
A34: 1 <= j3 by A26,MATRIX_1:39;
A35: 1 <= i1 by A10,MATRIX_1:39;
  then
A36: i1 = 1 by A24,XXREAL_0:1;
A37: j3 = j4
  proof
    assume
A38: j3 <> j4;
    per cases by A30,A38;
    suppose
      i3 = i4 & j3 = j4+1;
      hence contradiction by A7,A24,A25,A26,A27,A28,A29,A31,JORDAN10:2;
    end;
    suppose
A39:  i3 = i4 & j3+1 = j4;
      k9 < len Cage(C,n) by A22,NAT_1:13;
      then
      Gauge(C,n)*(i3,j3) in W-most L~Cage(C,n) by A36,A25,A27,A31,A34,A33,A39,
JORDAN1A:80;
      then
A40:  (Gauge(C,n)*(i3,j3+1))`2 <= (Gauge(C,n)*(i3,j3))`2 by A6,A29,A39,
PSCOMP_1:88;
      j3 < j3+1 by NAT_1:13;
      hence contradiction by A35,A15,A18,A31,A32,A34,A39,A40,GOBOARD5:5;
    end;
  end;
A41: 1 <= i3 by A26,MATRIX_1:39;
  i1 = i2
  proof
    assume
A42: i1 <> i2;
    per cases by A14,A42;
    suppose
      i1 = i2+1 & j1 = j2;
      hence contradiction by A17,A24,NAT_1:13;
    end;
    suppose
A43:  i1+1 = i2 & j1 = j2;
      k9+(1+1) <= len Cage(C,n) by A5;
      then
A44:  LSeg(Cage(C,n),k9) /\ LSeg(Cage(C,n),k) = {Cage(C,n)/.k} by A25,A21,
TOPREAL1:def 8;
      Cage(C,n)/.k9 in LSeg(Cage(C,n),k9) & Cage(C,n)/.(k+1) in LSeg(Cage
      (C,n),k) by A4,A5,A7,A25,A21,TOPREAL1:27;
      then Cage(C,n)/.(k+1) in {Cage(C,n)/.k} by A13,A24,A27,A30,A31,A32,A41
,A37,A43,A44,NAT_1:13,XBOOLE_0:def 4;
      then Cage(C,n)/.(k+1) = Cage(C,n)/.k by TARSKI:def 1;
      hence contradiction by A10,A11,A12,A13,A42,GOBOARD1:21;
    end;
  end;
  then Gauge(C,n)*(i1,j1)`1 = Gauge(C,n)*(i2,1)`1 by A35,A15,A23,A18,GOBOARD5:3
    .= Gauge(C,n)*(i2,j2)`1 by A17,A20,A16,GOBOARD5:3;
  hence thesis by A6,A11,A13,EUCLID:56;
end;
