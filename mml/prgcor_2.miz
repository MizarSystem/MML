:: Logical Correctness of Vector Calculation Programs
::  by Takaya Nishiyama , Hirofumi Fukura and Yatsuka Nakamura
::
:: Received July 13, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies FUNCT_1, BOOLE, ORDINAL1, FINSEQ_1, FINSET_1, RELAT_1, ORDINAL2,
      CARD_1, ALGSEQ_1, ARYTM_1, FINSEQ_7, AFINSQ_1, ARYTM, INT_1, FINSEQ_4,
      RVSUM_1, EUCLID_2, FUNCOP_1, RLVECT_1, FINSEQ_2, CQC_LANG, PRGCOR_2;
 notations TARSKI, XBOOLE_0, SUBSET_1, NUMBERS, XCMPLX_0, XREAL_0, RELAT_1,
      FUNCT_1, ORDINAL1, ORDINAL2, REAL_1, NAT_1, FINSET_1, CARD_1, FINSEQ_1,
      CQC_LANG, AFINSQ_1, INT_1, FINSEQ_4, SEQ_1, RVSUM_1, BINARITH, EUCLID_2,
      FUNCOP_1, FINSEQ_2;
 constructors XCMPLX_0, REAL_1, NAT_1, WELLORD2, CQC_LANG, FUNCT_7, ORDINAL4,
      XREAL_0, MEMBERED, AFINSQ_1, FINSEQ_1, FUNCT_1, FUNCT_2, AMI_2, INT_1,
      FINSEQ_4, SEQ_1, RVSUM_1, BINARITH, EUCLID_2, FUNCOP_1, RLVECT_1,
      FINSEQ_2, BINOP_2;
 registrations RELSET_1, SUBSET_1, ORDINAL1, NAT_1, MEMBERED, ORDINAL2,
      FINSEQ_1, AFINSQ_1, INT_1, FUNCOP_1;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI;
 theorems TARSKI, AXIOMS, FUNCT_1, REAL_1, NAT_1, ZFMISC_1, RELAT_1, ORDINAL1,
      CARD_1, FINSEQ_1, ORDINAL2, CARD_2, CQC_LANG, ORDINAL3, AFINSQ_1,
      RVSUM_1, FINSEQ_4, EUCLID_2, SQUARE_1, BINARITH, FUNCOP_1, FINSEQ_2,
      JORDAN8;
 schemes NAT_1, FINSEQ_1, AFINSQ_1;

begin

 reserve k,m,n,i for Nat,
         D,x,y,z,y1,y2 for set;

Lm1: for x,n st x in n holds x is Nat :: NECKLACE:4
proof let x,n;assume A1: x in n;then
  reconsider nx=x as Ordinal by ORDINAL1:23;
  nx=x;
 hence x is Nat by A1,ORDINAL1:19;
end;

theorem Th1: for n,m holds n in m iff n < m
proof let n,m;
  A1: Card n <` Card m iff n < m by CARD_1:73;
  Card n=n by CARD_1:66;
 hence n in m iff n < m by A1,CARD_1:66;
end;

Lm2: for p being XFinSequence,D being set holds rng p c=D iff
  p is XFinSequence of D by ORDINAL1:def 8;

registration
  let D be non empty set;
  cluster non empty XFinSequence of D;
existence
 proof
  consider d being Element of D;
  A1: {[0,d]} is Relation by RELAT_1:4;
  for x,y1,y2 being set st [x,y1] in {[0,d]} &
   [x,y2] in {[0,d]} holds y1 = y2
   proof let x,y1,y2 be set;
     assume A2: [x,y1] in {[0,d]} &
       [x,y2] in {[0,d]};then
       [x,y1]=[0,d] by TARSKI:def 1;then
       A3: y1=d by ZFMISC_1:33;
       [x,y2]=[0,d] by A2,TARSKI:def 1;
    hence y1 = y2 by A3,ZFMISC_1:33;
   end;then
  reconsider f={[0,d]} as Function by A1,FUNCT_1:def 1;
  A4: dom f=one by ORDINAL3:18,RELAT_1:23;then
  reconsider p= {[0,d]} as T-Sequence by ORDINAL1:def 7;
  A5: one is Nat by CARD_2:20;
  A6: rng p={d} by RELAT_1:23;
  {d} c= D
  proof let x be set;assume x in {d};then
    x=d by TARSKI:def 1;
   hence x in D;
  end;then
  p is finite T-Sequence of D by A4,A5,A6,AFINSQ_1:7,ORDINAL1:def 8;
   hence thesis;
 end;
end;

theorem Th2: for D being non empty set, f being non empty XFinSequence of D
holds len f >0
proof let D be non empty set,
  f be non empty XFinSequence of D;
  len f <>0 by AFINSQ_1:18;
 hence len f >0 by NAT_1:19;
end;

definition let D be set,q be FinSequence of D;
func FS2XFS q -> XFinSequence of D means
:Def1: len it=len q & for i st i<len q holds
  q.(i+1)=it.i;
existence
proof
 deffunc F(Nat) =q.($1 +1);
 ex p being XFinSequence st len p = len q &
 for k st k in len q holds p.k=F(k) from AFINSQ_1:sch 2;then
 consider p being XFinSequence such that
 A1: len p = len q &
 for k st k in len q holds p.k=F(k);
 A2: for i st i<len q holds
  q.(i+1)=p.i
  proof let i;
   assume i<len q;then
    i in len q by Th1;
   hence q.(i+1)=p.i by A1;
  end;
  rng p c= D
  proof let y be set;assume y in rng p;then
    consider x being set such that
    A3: x in dom p & y=p.x by FUNCT_1:def 5;
    A4: x in len p by A3,AFINSQ_1:def 1;
    reconsider nx=x as Nat by A3;
    A5: p.nx= q.(nx +1) by A1,A4;
    nx<len q by A1,A4,Th1;then
    A6: nx+1<=len q by NAT_1:38;
    nx+1>0 by NAT_1:19;then
    0+1<=nx+1 by NAT_1:38;then
    nx+1 in Seg len q by A6,FINSEQ_1:3;then
    nx+1 in dom q by FINSEQ_1:def 3;then
    A7: q.(nx+1) in rng q by FUNCT_1:def 5;
    rng q c= D by FINSEQ_1:def 4;
   hence y in D by A3,A5,A7;
  end;then
  p is XFinSequence of D by Lm2;
 hence thesis by A1,A2;
end;
uniqueness
 proof
  thus for p1,p2 being XFinSequence of D st
  (len p1=len q & for i st i<len q holds
  q.(i+1)=p1.i)&
  (len p2=len q & for i st i<len q holds
  q.(i+1)=p2.i) holds p1=p2
  proof let p1,p2 be XFinSequence of D;
   assume A8: (len p1=len q & for i st i<len q holds
      q.(i+1)=p1.i)&
      (len p2=len q & for i st i<len q holds
      q.(i+1)=p2.i);
      for i st i<len p1 holds p1.i=p2.i
      proof let i;
       assume A9: i<len p1;then
        q.(i+1)=p1.i by A8;
       hence p1.i=p2.i by A8,A9;
      end;
    hence p1=p2 by A8,AFINSQ_1:11;
  end;
 end;
end;

definition let D be set,q be XFinSequence of D;
func XFS2FS q -> FinSequence of D means
 len it=len q & for i st  1<=i & i<=len q holds
  q.(i-'1)=it.i;
existence
proof
 deffunc F(Nat) =q.($1-'1);
 ex p being FinSequence st len p = len q &
 for k st k in Seg len q holds p.k=F(k) from FINSEQ_1:sch 2;then
 consider p being FinSequence such that
 A1: len p = len q &
 for k st k in Seg len q holds p.k=F(k);
 A2: for i st 1<=i & i<=len q holds
  q.(i-'1)=p.i
  proof let i;
   assume 1<=i & i<=len q;then
    i in Seg len q by FINSEQ_1:3;
   hence q.(i-'1)=p.i by A1;
  end;
  rng p c= D
  proof let y be set;assume y in rng p;then
    consider x being set such that
    A3: x in dom p & y=p.x by FUNCT_1:def 5;
    A4: x in Seg len p by A3,FINSEQ_1:def 3;
    reconsider nx=x as Nat by A3;
    A5: nx in Seg len q by A1,A4;
    A6: p.nx= q.(nx -'1) by A1,A4;
    A7: 1<=nx & nx<=len q by A5,FINSEQ_1:3;then
    nx-1>=0 by SQUARE_1:12;then
    A8: nx-1=nx-'1 by BINARITH:def 3;
    nx-'1<nx-'1+1 by NAT_1:38;then
    nx-'1<len q by A7,A8,AXIOMS:22;then
    nx-'1 in len q by Th1;then
    nx-'1 in dom q by AFINSQ_1:def 1;then
    A9: q.(nx-'1) in rng q by FUNCT_1:def 5;
    rng q c= D by Lm2;
   hence y in D by A3,A6,A9;
  end;then
  p is FinSequence of D by FINSEQ_1:def 4;
 hence thesis by A1,A2;
end;
uniqueness
 proof
  thus for p1,p2 being FinSequence of D st
  (len p1=len q & for i st 1<=i & i<=len q holds
  q.(i-'1)=p1.i)&
  (len p2=len q & for i st 1<=i & i<=len q holds
  q.(i-'1)=p2.i) holds p1=p2
  proof let p1,p2 be FinSequence of D;
   assume A10: (len p1=len q & for i st 1<=i & i<=len q holds
      q.(i-'1)=p1.i)&
      (len p2=len q & for i st 1<=i & i<=len q holds
      q.(i-'1)=p2.i);
      for i st 1<=i & i<=len p1 holds p1.i=p2.i
      proof let i;
       assume A11: 1<=i & i<=len p1;then
        q.(i-'1)=p1.i by A10;
       hence p1.i=p2.i by A10,A11;
      end;
    hence p1=p2 by A10,FINSEQ_1:18;
  end;
 end;
end;

theorem Th3: for k being Nat,a being set holds
  k --> a is XFinSequence
 proof
  let k be Nat,a be set;
 reconsider p = (k --> a) as Function;
   dom p = k by FUNCOP_1:19;
  hence (k --> a) is XFinSequence by AFINSQ_1:7;
 end;

theorem Th4: for D being set, n being Nat, r being set
 st r in D
 holds (n-->r) is XFinSequence of D & for q2 being XFinSequence
 st q2=(n-->r) holds len q2=n
proof let D be set, n be Nat, r be set;
  assume A1: r in D;
  reconsider p=(n-->r) as XFinSequence by Th3;
  now per cases;
  case n<>0;then
  A2: rng p={r} by FUNCOP_1:14;
  rng p c= D
  proof let x be set;assume x in rng p;
   hence x in D by A1,A2,TARSKI:def 1;
  end;
  hence (n-->r) is XFinSequence of D by Lm2;
  end;
  case n=0;then
   dom (n-->r)={} & rng (n-->r)={} by FUNCOP_1:16;then
   len p=0 by AFINSQ_1:def 1;then
   (n-->r)={} by AFINSQ_1:18;
  hence (n-->r) is XFinSequence of D by AFINSQ_1:19;
  end;
  end;
  hence (n-->r) is XFinSequence of D;
 thus for q2 being XFinSequence st q2=(n-->r) holds len q2=n
  proof let q2 be XFinSequence;
   assume q2=(n-->r);then
    dom q2=n by FUNCOP_1:19;
   hence len q2=n by AFINSQ_1:def 1;
  end;
end;

definition let D be non empty set,q be FinSequence of D,
n be Nat;
assume A1: n>len q & NAT c= D;
func FS2XFS*(q,n) -> non empty XFinSequence of D
means :Def3: len q = it.0 & len it=n &
(for i st 1<=i & i<= len q  holds it.i=q.i)&
(for j being Nat st len q<j & j<n holds it.j=0);
existence
proof
  0 in NAT;then
  A2: 0 in D by A1;
  reconsider r=0 as Element of D by A2;
  reconsider q5= ((n-'len q-'1)-->r) as XFinSequence of D
                  by Th4;
  A3: len q5=(n-'len q-'1) by Th4;
  A4: n-len q>0 by A1,SQUARE_1:11;then
  A5: n-'len q=n-len q by BINARITH:def 3;then
  n-'len q>=0+1 by A4,NAT_1:38;then
  n-'len q -1>=0 by SQUARE_1:12;then
  A6: n-'len q -'1=n-(len q+1) by A5,BINARITH:def 3;
  len q in NAT;then
  reconsider x=len q as Element of D by A1;
  { [0,x] } <> {};then
  <%x%> <>{} by AFINSQ_1:def 2;then
  <%x%> ^ (FS2XFS q) <>{} by AFINSQ_1:33;then
  A7: <%x%> ^ (FS2XFS q)^q5 <>{} by AFINSQ_1:33;
  reconsider p0=<%x%> ^ (FS2XFS q)^q5 as non empty XFinSequence of D
                         by A7;
  A8: len p0=len (<%x%> ^ (FS2XFS q)) + len q5 by AFINSQ_1:20
       .=len <%x%> + len (FS2XFS q) + len q5 by AFINSQ_1:20
       .= 1 + len (FS2XFS q) + len q5 by AFINSQ_1:37
       .=1 + len q + len q5 by Def1
       .=1+len q+(n-'len q-'1) by Th4
       .=(n-(len q+1))+(len q+1) by A6
       .=n;
  0 in 1 by Th1;then
  A9: 0 in dom (<%x%>) by AFINSQ_1:def 5;
  dom <%x%>=1 by AFINSQ_1:def 5;then
  A10: len <%x%>=1 by AFINSQ_1:def 1;
  0<1+ len (FS2XFS q) by NAT_1:19;then
  0 in len <%x%> + len (FS2XFS q) by A10,Th1;then
  0 in len (<%x%> ^ (FS2XFS q)) by AFINSQ_1:20;then
  0 in dom (<%x%> ^ (FS2XFS q)) by AFINSQ_1:def 1;then
  p0.0=(<%x%> ^ (FS2XFS q)).0 by AFINSQ_1:def 4
      .=(<%x%>).0 by A9,AFINSQ_1:def 4 .=x by AFINSQ_1:def 5;then
  A11: len q = p0.0;
  A12: for i st 1<=i & i<= len q  holds p0.i=q.i
  proof let i;
   assume A13: 1<=i & i<= len q;then
    i-1>=0 by SQUARE_1:12;then
    A14: i-'1=i-1 by BINARITH:def 3;
    i<i+1 by NAT_1:38;then
    i-1<i+1-1 by REAL_1:54;then
    A15: i-'1 <len q by A13,A14,AXIOMS:22;then
    i-'1 in len q by Th1;then
    i-'1 in len (FS2XFS q) by Def1;then
    A16: i-'1 in dom (FS2XFS q) by AFINSQ_1:def 1;
    i<1+len q by A13,NAT_1:38;then
    i< (len (<%x%>)+len (FS2XFS q)) by A10,Def1;then
    i in (len (<%x%>)+len (FS2XFS q)) by Th1;then
    i in len (<%x%> ^ (FS2XFS q)) by AFINSQ_1:20;then
    i in dom (<%x%> ^ (FS2XFS q)) by AFINSQ_1:def 1;then
    p0.i=(<%x%>^ (FS2XFS q)).i by AFINSQ_1:def 4
        .=(<%x%>^(FS2XFS q)).(1+(i-'1)) by A14
        .=(FS2XFS q).(i-'1) by A10,A16,AFINSQ_1:def 4
        .=q.(i-'1+1) by A15,Def1
        .=q.i by A14;
   hence p0.i=q.i;
  end;
  for j being Nat st len q<j & j<n holds p0.j=0
  proof let j be Nat;
   assume A17: len q<j & j<n;
    A18: len (<%x%> ^ (FS2XFS q))
         =len (<%x%>) + len (FS2XFS q) by AFINSQ_1:20
        .=1+len q by A10,Def1;
    1+len q<=j by A17,NAT_1:38;then
    j-(1+len q)>=0 by SQUARE_1:12;then
    A19: j-'(1+len q)=j-(1+len q) by BINARITH:def 3;
    len q<n by A17,AXIOMS:22;then
    A20: n-len q>0 by SQUARE_1:11;then
    A21: n-'len q=n-len q by BINARITH:def 3;then
    n-len q>=0+1 by A20,NAT_1:38;then
    n-'len q-1>=0 by A21,SQUARE_1:12;then
    A22: n-'len q-'1=n-'len q-1 by BINARITH:def 3
               .=n-(len q+1) by A21;
    j-(len q+1)< n-(len q+1) by A17,REAL_1:54;then
    A23: j-'len (<%x%> ^ (FS2XFS q)) in (n-'len q-'1)
                           by A18,A19,A22,Th1;then
    A24: j-'len (<%x%> ^ (FS2XFS q)) in dom q5 by A3,AFINSQ_1:def 1;
    j=len (<%x%> ^ (FS2XFS q))+(j-'len (<%x%> ^ (FS2XFS q)))
                     by A18,A19;then
    p0.j=q5.(j-'len (<%x%> ^ (FS2XFS q))) by A24,AFINSQ_1:def 4
        .=0 by A23,FUNCOP_1:13;
   hence p0.j=0;
  end;
 hence ex p being non empty XFinSequence of D st
 len q = p.0 & len p=n &
(for i st 1<=i & i<= len q holds p.i=q.i)&
(for j being Nat st len q<j & j<n holds p.j=0) by A8,A11,A12;
end;
uniqueness
proof let p1,p2 be non empty XFinSequence of D;
 assume
  A25: ( len q = (p1.0) & len p1=n &
   (for i st 1<=i & i<= len q  holds p1.i=q.i)&
   (for j being Nat st len q<j & j<n holds p1.j=0))&
  ( len q = (p2.0) & len p2=n &
   (for i st 1<=i & i<= len q  holds p2.i=q.i)&
   (for j being Nat st len q<j & j<n holds p2.j=0));
   for i st i<n holds p1.i=p2.i
   proof let i;assume A26: i<n;
   len q in NAT;then
   reconsider x=len q as Element of D by A1;
     A27: x=p1.0 by A25;
     A28: x=p2.0 by A25;
     i<0+1 or 1<=i & i<=len q or len q<i & i<n by A26;then
     A29: i<=0 or 1<=i & i<=len q or len q<i & i<n by NAT_1:38;
     A30: i=0 or 1<=i & i<=len q  or len q<i & i<n by A29,NAT_1:18;
     now per cases by A30;
     case i=0;
      hence p1.i=p2.i by A27,A28;
     end;
     case A31: 1<=i & i<=len q;then
       p1.i=q.i by A25;
      hence p1.i=p2.i by A25,A31;
     end;
     case A32: len q<i & i<n;then
       p1.i=0 by A25;
      hence p1.i=p2.i by A25,A32;
     end;
     end;
    hence p1.i=p2.i;
   end;
hence p1=p2 by A25,AFINSQ_1:11;
end;
end;

definition let D be non empty set,p be non empty XFinSequence of D;
assume A1: NAT c= D & p.0 is Nat & p.0 in len p;
func XFS2FS*(p) -> FinSequence of D
means :Def4: for m st m = p.0 holds len it =m &
for i st 1<=i & i<= m holds it.i=p.i;
existence
proof
 reconsider m0=p.0 as Nat by A1;
 deffunc F(set)= p.$1;
 ex q being FinSequence st len q = m0 & for k st k in Seg m0 holds q.k=F(k)
                from FINSEQ_1:sch 2;then
 consider q being FinSequence such that
 A2: len q = m0 & for k st k in Seg m0 holds q.k=F(k);
 rng q c= D
 proof let y be set;assume y in rng q;then
   consider x being set such that
   A3: x in dom q & y=q.x by FUNCT_1:def 5;
   A4: x in Seg len q by A3,FINSEQ_1:def 3;
   reconsider k0=x as Nat by A3;
   A5: k0 in Seg m0 by A2,A4;
   A6: q.k0=p.k0 & y=p.k0 by A2,A3,A4;
   A7: 1<=k0 & k0<=m0 by A5,FINSEQ_1:3;
  A8: rng p c= D by ORDINAL1:def 8;
  m0 < len p by A1,Th1;then
  k0 < len p by A7,AXIOMS:22;then
  k0 in len p by Th1;then
  k0 in dom p by AFINSQ_1:def 1;then
  y in rng p by A6,FUNCT_1:def 5;
  hence y in D by A8;
 end;then
 reconsider q0=q as FinSequence of D by FINSEQ_1:def 4;
 for m st m = (p.0) holds len q0 =m &
  for i st 1<=i & i<= m holds q0.i=p.i
  proof let m;assume m = (p.0);then
     A9: m=m0;
     for i st 1<=i & i<= m0  holds q0.i=p.i
     proof let i;assume 1<=i & i<= m0;then
       i in Seg m0 by FINSEQ_1:3;
      hence q0.i=p.i by A2;
     end;
   hence len q0 =m &
     for i st 1<=i & i<= m  holds q0.i=p.i by A2,A9;
  end;
 hence thesis;
end;
uniqueness
proof let g1,g2 be FinSequence of D;
 assume A10:
 (for m st m = p.0 holds len g1 =m &
    for i st 1<=i & i<= m holds g1.i=p.i)&
 (for m st m = p.0 holds len g2 =m &
    for i st 1<=i & i<= m holds g2.i=p.i);
  reconsider m2=(p.0) as Nat by A1;
  A11: len g1=m2 by A10;
  A12: len g2=m2 by A10;
  A13: for i st 1<=i & i<=m2 holds g1.i=p.i by A10;
  A14: for i st 1<=i & i<=m2 holds g2.i=p.i by A10;
  for i st 1<=i & i<=len g1 holds g1.i=g2.i
  proof let i;assume A15: 1<=i & i<=len g1;then
    g1.i=p.i by A11,A13;
   hence g1.i=g2.i by A11,A14,A15;
  end;
 hence g1=g2 by A11,A12,FINSEQ_1:18;
end;
end;

theorem Th5: for D being non empty set,p being non empty XFinSequence of D
 st NAT c= D & p.0=0 & 0<len p holds XFS2FS*(p)={}
proof let D be non empty set,p be non empty XFinSequence of D;
 assume A1: NAT c= D & (p.0)=0 & 0<len p;then
   A2: 0 in len p by Th1;
   set q= XFS2FS*(p);
  len q=0 by A1,A2,Def4;then
  q=<*>(D) by FINSEQ_1:32;
 hence XFS2FS*p={};
end;

definition let D be non empty set,p be XFinSequence of D,
 q be FinSequence of D;
pred p is_an_xrep_of q means
:Def5: NAT c= D & (p.0)=len q & len q < len p
& for i st 1<=i & i<=len q holds p.i=q.i;
end;

theorem  for D being non empty set,p being non empty XFinSequence of D st
NAT c= D & (p.0) is Nat & (p.0) in len p holds
p is_an_xrep_of (XFS2FS*(p))
proof let D be non empty set,p be non empty XFinSequence of D;
 assume
  A1: NAT c= D & (p.0) is Nat & (p.0) in len p;then
  A2: for m st m = (p.0) holds len (XFS2FS*(p)) =m &
    for i st 1<=i & i<= m  holds (XFS2FS*(p)).i=p.i by Def4;
  reconsider m0=(p.0) as Nat by A1;
  A3: len (XFS2FS*(p)) =m0 &
    for i st 1<=i & i<= m0  holds (XFS2FS*(p)).i=p.i by A2;
  A4: m0<len p by A1,Th1;
  set q=XFS2FS*(p);
 thus p is_an_xrep_of (XFS2FS*(p)) by A1,A3,A4,Def5;
end;

definition let x,y,a,b,c be set;
 func IFLGT(x,y,a,b,c) -> set equals :Def6:
 a if x in y,
 b if x = y otherwise c;
correctness;
end;

theorem Th7: for D being non empty set,q being FinSequence of D,
 n being Nat st NAT c= D & n>len q
holds ex p being XFinSequence of D st len p=n & p is_an_xrep_of q
proof let D be non empty set,q be FinSequence of D,
  n be Nat;
 assume A1: NAT c= D & n>len q;
  len q in NAT;then
  consider d2 being set such that
  A2: d2 in D & d2=len q by A1;
  reconsider d=d2 as Element of D by A2;
  deffunc F(set)= IFEQ($1,0,d,IFLGT($1,len q,q.$1,q.$1,d));
  ex p being XFinSequence st len p = n & for k st k in n
                  holds p.k=F(k) from AFINSQ_1:sch 2;then
  consider p being XFinSequence such that
   A3: len p = n & for k st k in n holds p.k=F(k);
  rng p c= D
  proof let y be set;assume y in rng p;then
    consider x being set such that
    A4: x in dom p & y=p.x by FUNCT_1:def 5;
    A5: x in n by A3,A4,AFINSQ_1:def 1;then
    reconsider nx=x as Ordinal by ORDINAL1:23;
    reconsider kx=nx as Nat by A5,ORDINAL1:19;
    A6: p.kx=F(kx) by A3,A5;
    now per cases;
    case x=0;then F(x)=d by CQC_LANG:def 1;
     hence p.x in D by A6;
    end;
    case A7: x<>0;then
     A8: F(x)=IFLGT(x,len q,q.x,q.x,d) by CQC_LANG:def 1;
     now per cases;
     case A9: x in len q;then
       A10: F(x)=q.x by A8,Def6;
       kx>0 by A7,NAT_1:19;then
       A11: 0+1<=kx by NAT_1:38;
       kx<len q by A9,Th1;then
       kx in Seg len q by A11,FINSEQ_1:3;then
       x in dom q by FINSEQ_1:def 3;then
       A12: q.x in rng q by FUNCT_1:def 5;
       rng q c= D by FINSEQ_1:def 4;
      hence p.x in D by A6,A10,A12;
     end;
     case A13: x=len q;then
       A14: F(x)=q.x by A8,Def6;
       kx>0 by A7,NAT_1:19;then
       A15: 0+1<=kx by NAT_1:38;
       kx in Seg len q by A13,A15,FINSEQ_1:3;then
       x in dom q by FINSEQ_1:def 3;then
       A16: q.x in rng q by FUNCT_1:def 5;
       rng q c= D by FINSEQ_1:def 4;
      hence p.x in D by A6,A14,A16;
     end;
     case not (x in len q or x=len q);then
       F(x)=d by A8,Def6;
      hence p.x in D by A6;
     end;
     end;
     hence p.x in D;
    end;
    end;
   hence y in D by A4;
  end;then
  reconsider p as XFinSequence of D by Lm2;
  reconsider p2=Replace(p,0,d) as XFinSequence of D;
  n>0 by A1,NAT_1:18;then
  A17: p2.0=d by A3,AFINSQ_1:48;
  A18: len p2=len p by AFINSQ_1:48;
  A19: (p2.0)=d by A17 .=len q by A2;
  for i st 1<=i & i<=len q holds p2.i=q.i
  proof let i;assume A20: 1<=i & i<=len q;then
    i<n by A1,AXIOMS:22;then
    A21: i in n by Th1;
    i<len q or i=len q by A20,REAL_1:def 5;then
    A22: i in len q or i=len q by Th1;
    A23: i<>0 by A20;
   hence p2.i=p.i by AFINSQ_1:48.=F(i) by A3,A21
   .=IFLGT(i,len q,q.i,q.i,d) by A23,CQC_LANG:def 1
   .=q.i by A22,Def6;
  end;then
  p2 is_an_xrep_of q by A1,A3,A18,A19,Def5;
 hence ex p being XFinSequence of D st len p=n & p is_an_xrep_of q
                                  by A3,A18;
end;

definition let b be XFinSequence of REAL,n be Nat;
 redefine func b.n -> Real;
coherence
proof
 now per cases;
 case n in dom b;then
  A1: b.n in rng b by FUNCT_1:def 5;
  rng b c= REAL by Lm2;
 hence b.n is Real by A1;
 end;
 case not n in dom b;then
  b.n = 0 by FUNCT_1:def 4;
 hence b.n is Real;
 end;
 end;
 hence b.n is Real;
end;
end;

:: Non-overwriting program calculating inner product of vectors

::#define V_SIZE 1024
:: float inner_prd_prg(float a[V_SIZE],float b[V_SIZE]){
::    int n,i; float s[V_SIZE];
::        s[0]=0;
::        n=(int)(b[0]);
::        if (n != 0)
::         { for(i=0;i<n;i++)s[i+1]=s[i]+a[i+1]*b[i+1];
::         }
::     return s[n];
::   }

definition let a,b be XFinSequence of REAL;
assume A1:b.0 is Nat & 0<= b.0 & b.0 < len a;
func inner_prd_prg(a,b) -> Real means :Def7:
 ex s being XFinSequence of REAL, n being Integer st
 len s=len a & s.0=0 & n= b.0
& (n<>0 implies
 for i being Nat st i<n
 holds s.(i+1)=s.i + (a.(i+1))*(b.(i+1))
 )
& it=s.n;
existence
proof
 reconsider nn2=(b.0) as Nat by A1;
 reconsider nn=nn2 as Integer;
 A2: 0<len a by A1;
 set n2=len a;
 defpred P[natural number] means ex q being XFinSequence of REAL st
  (len q=$1+1 &
  q.0=0 & (for k st k<$1 & k<nn2 holds q.(k+1)=q.k+(a.(k+1))*(b.(k+1))));
  reconsider q0=<%0 qua Element of REAL %> as XFinSequence of REAL;
  dom q0=1 & q0.0=0 by AFINSQ_1:def 5;then
  A3: len q0=1 & q0.0=0 by AFINSQ_1:def 1;
  for k st k< 0 & k<nn2 holds q0.(k+1)=q0.k+(a.(k+1))*(b.(k+1))
                          by NAT_1:18;then
  A4: P[0] by A3;
  A5: for k be natural number st P[k] holds P[k + 1]
  proof let k be natural number;
   assume P[k];then
    consider q2 being XFinSequence of REAL such that
     A6: (len q2= k+1 &
     q2.0=0 & (for k2 being Nat st k2<k & k2<nn2 holds q2.(k2+1)
     =q2.k2+(a.(k2+1))*(b.(k2+1))));
     reconsider k0=k as Nat by ORDINAL2:def 21;
     reconsider q3=q2^<% q2.k0+(a.(k0+1))*(b.(k0+1)) %>
                  as XFinSequence of REAL;
     A7: len q3=len q2+len (<% q2.k0+(a.(k0+1))*(b.(k0+1)) %>)
                            by AFINSQ_1:20
          .=k+1+1 by A6,AFINSQ_1:37;
     0 < k+1 by NAT_1:19;then
     0 in len q2 by A6,Th1;then
     0 in dom q2 by AFINSQ_1:def 1;then
     A8: q3.0=q2.0 by AFINSQ_1:def 4 .=0 by A6;
     for k2 being Nat st k2<k+1 & k2<nn2 holds q3.(k2+1)
      =q3.k2+(a.(k2+1))*(b.(k2+1))
      proof let k2 be Nat;
       assume A9: k2<k+1 & k2<nn2;then k2<=k by NAT_1:38;then
        A10: k2<k or k2=k by REAL_1:def 5;
        now per cases by A10;
        case A11: k2<k;then
           k2+1<k+1 by REAL_1:53;then
           k2+1 in len q2 by A6,Th1;then
           k2+1 in dom q2 by AFINSQ_1:def 1;then
           A12: q3.(k2+1)=q2.(k2+1) by AFINSQ_1:def 4;
           k2< len q2 by A6,A11,NAT_1:38;then
           k2 in len q2 by Th1;then
           k2 in dom q2 by AFINSQ_1:def 1;then
           q3.(k2)=q2.(k2) by AFINSQ_1:def 4;
          hence q3.(k2+1) =q3.k2+(a.(k2+1))*(b.(k2+1)) by A6,A9,A11,A12;
        end;
        case A13: k2=k;
           A14: (<% q2.k0+(a.(k0+1))*(b.(k0+1)) %>).0
               = q2.k0+(a.(k0+1))*(b.(k0+1)) by AFINSQ_1:38;
           k2<k2+1 by NAT_1:38;then
           k2 in len q2 by A6,A13,Th1;then
           k2 in dom q2 by AFINSQ_1:def 1;then
           A15: q3.(k2)=q2.(k2) by AFINSQ_1:def 4;
           0 in 1 by Th1;then
           0 in dom (<% q2.k0+(a.(k0+1))*(b.(k0+1)) %>)
                                   by AFINSQ_1:def 5;then
           q3.(k2+1+0) =q2.(k0)+(a.(k0+1))*(b.(k0+1))
                           by A6,A13,A14,AFINSQ_1:def 4;
          hence q3.(k2+1) =q3.k2+(a.(k2+1))*(b.(k2+1)) by A13,A15;
        end;
        end;
       hence q3.(k2+1) =q3.k2+(a.(k2+1))*(b.(k2+1));
      end;
   hence P[k + 1] by A7,A8;
  end;
 for k being natural number holds P[k] from NAT_1:sch 2(A4,A5);then
 consider q being XFinSequence of REAL such that
 A16: (len q=n2-'1+1 &
  q.0=0 & (for k3 being Nat st k3<n2-'1 & k3<nn2 holds
  q.(k3+1)=q.k3+(a.(k3+1))*(b.(k3+1))));
 n2>=0+1 by A2,NAT_1:38;then
 n2-1>=0 by SQUARE_1:12;then
 A17: n2-'1=n2-1 by BINARITH:def 3;then
 A18: len q=len a & q.0=0 by A16;
 nn<>0 implies
 for i being Nat st i<nn
 holds q.(i+1)=q.i + (a.(i+1))*(b.(i+1))
 proof  assume nn<>0;
  thus for i being Nat st i<nn
  holds q.(i+1)=q.i + (a.(i+1))*(b.(i+1))
   proof let i be Nat;
    assume A19: i<nn;then A20: i<nn2;
     nn+1<=n2 by A1,NAT_1:38;then
     nn+1-1<=n2-1 by REAL_1:49;then
     i<n2-'1 by A17,A19,AXIOMS:22;
    hence q.(i+1)=q.i + (a.(i+1))*(b.(i+1)) by A16,A20;
   end;
 end;then
  A21: len q=len a & q.0=0 & nn=b.0 & (nn<>0 implies
 for i being Nat st i<nn
 holds q.(i+1)=q.i + (a.(i+1))*(b.(i+1)))
& q.nn=q.nn by A18;
 reconsider ss2=q.nn2 as Real;
 ex s being XFinSequence of REAL, n being Integer st
 len s=len a
&  s.0=0
& n=b.0
& (n<>0 implies
 for i being Nat st i<n
 holds s.(i+1)=s.i + (a.(i+1))*(b.(i+1)) )
& ss2=s.n by A21;
 hence ex inn being Real st
 (ex s being XFinSequence of REAL, n being Integer st
 len s=len a & s.0=0 & n= b.0 & (n<>0 implies
 for i being Nat st i<n
 holds s.(i+1)=s.i + (a.(i+1))*(b.(i+1)) ) & inn=s.n);
end;
uniqueness
 proof
  thus for w1,w2 being Real st
 (ex s being XFinSequence of REAL, n1 being Integer st
 len s=len a
& s.0=0
& n1=b.0
& (n1<>0 implies
 for i being Nat st i<n1
 holds s.(i+1)=s.i + (a.(i+1))*(b.(i+1)) )
& w1=s.n1) &
( ex s being XFinSequence of REAL, n2 being Integer st
 len s=len a & s.0=0 & n2=b.0 & (n2<>0 implies
 for i being Nat st i<n2
 holds s.(i+1)=s.i + (a.(i+1))*(b.(i+1)) )
& w2=s.n2) holds w1=w2
  proof let w1,w2 be Real;
   assume A22: (ex s being XFinSequence of REAL, n1 being Integer st
     len s=len a & s.0=0 & n1=b.0 & (n1<>0 implies
    for i being Nat st i<n1
    holds s.(i+1)=s.i + (a.(i+1))*(b.(i+1))
    )
   & w1=s.n1)
   &
   ( ex s being XFinSequence of REAL, n2 being Integer st
    len s=len a & s.0=0 & n2=b.0 & (n2<>0 implies
    for i being Nat st i<n2
    holds s.(i+1)=s.i + (a.(i+1))*(b.(i+1)))
   & w2=s.n2);then
   consider s1 being XFinSequence of REAL, n1 being Integer such that
   A23: len s1=len a & s1.0=0
    & n1=b.0
    & (n1<>0 implies
    for i being Nat st i<n1
    holds s1.(i+1)=s1.i + (a.(i+1))*(b.(i+1)))
   & w1=s1.n1;
   consider s2 being XFinSequence of REAL, n2 being Integer such that
   A24: len s2 =len a & s2.0=0
    & n2=b.0
    & (n2<>0 implies
    for i being Nat st i<n2
    holds s2.(i+1)=s2.i + (a.(i+1))*(b.(i+1))
    )
   & w2=s2.n2 by A22;
   A25: n1=n2 by A23,A24;
   reconsider n=n1 as Nat by A1,A23;
   defpred P2[Nat] means
    $1<=n implies  s1.$1=s2.$1;
    A26: P2[0] by A23,A24;
    A27: for k being Nat st P2[k] holds P2[k+1]
    proof let k be Nat;assume
      P2[k];then
      A28: k<=n implies  s1.(k)=s2.(k);
      now assume A29: k+1<=n;
        A30: k<n by A29,NAT_1:38;
        now per cases;
        case A31: n<>0;then
          s1.(k+1)=s1.k + (a.(k+1))*(b.(k+1)) by A23,A30;
         hence s1.(k+1)=s2.(k+1) by A24,A25,A28,A30,A31;
        end;
        case n=0;
         hence s1.(k+1)=s2.(k+1) by A30,NAT_1:18;
        end;
        end;
       hence s1.(k+1)=s2.(k+1);
      end;
     hence P2[k+1];
    end;
    A32: for k being Nat holds P2[k] from NAT_1:sch 1(A26,A27);
    reconsider n11=n as Nat;
    s1.n11=s2.n11 by A32.=s2.n2 by A25;
   hence w1=w2 by A23,A24;
  end;
 end;
end;

theorem Th8: for a being FinSequence of REAL,
s being XFinSequence of REAL st
 len s> len a & s.0=0 &
(for i st i<len a holds s.(i+1)=s.i+a.(i+1))
 holds  Sum a =s.(len a)
proof let a be FinSequence of REAL,
 s be XFinSequence of REAL;
 assume A1: len s> len a & s.0=0 &
  (for i st i<len a holds s.(i+1)=s.i+a.(i+1));
  defpred P[Nat] means
   $1<=len a implies  Sum (a|$1)=s.($1);
  A2: P[0] by A1,RVSUM_1:102;
  A3: for k st P[k] holds P[k+1]
  proof let k;assume P[k];then
    A4: k<=len a implies Sum (a|k)=s.(k);
    reconsider m=k+1 as Nat;
    k+1<=len a implies Sum (a|m)=s.(k+1)
    proof assume A5: k+1<=len a;
       A6: 1<=k+1 by NAT_1:29;
       A7: k<len a by A5,NAT_1:38;
       A8: a|m = (a|k)^<*a/.m*> by A5,JORDAN8:2;
       reconsider q0=a|k as FinSequence of REAL;
       reconsider rx=a/.m as Element of REAL;
       reconsider x0=<*a/.m*> as FinSequence of REAL;
       m in Seg m by A6,FINSEQ_1:3;then
       m in Seg (len (a|m)) by A5,FINSEQ_1:80;then
       A9: m in dom (a|m) by FINSEQ_1:def 3;
       A10: len (a|m) =len q0 + len (<*rx*>) by A8,FINSEQ_1:35
                 .=len q0 +1 by FINSEQ_1:57;
       A11: len (a|m)=m by A5,FINSEQ_1:80;
       m in Seg len a by A5,A6,FINSEQ_1:3;then
       A12: m in dom a by FINSEQ_1:def 3;
       rx=(a|m).m by A8,A10,A11,FINSEQ_1:59.=(a|m)/.m by A9,FINSEQ_4:def 4
       .=a/.m by A9,FINSEQ_4:85 .=a.m by A12,FINSEQ_4:def 4;then
       A13: rx=a.(k+1);
       len (a|m)=len q0 + len x0 by A8,FINSEQ_1:35
       .=len q0 +1 by FINSEQ_1:56;then
       len q0 +1=k+1 by A5,FINSEQ_1:80;then
       len q0+1-1=k;then
       A14: len q0=k;then
       A15: Sum q0=s.(len q0) by A4,A7;
       A16: Sum x0=rx by RVSUM_1:103;
       Sum (a|m)=(Sum q0)+Sum x0 by A8,RVSUM_1:105 .=(Sum q0)+rx by A16
            .=s.(k)+rx by A14,A15;
      hence thesis by A1,A7,A13;
    end;
   hence P[k+1];
  end;
  for k holds P[k] from NAT_1:sch 1(A2,A3);then
  P[len a];
 hence Sum a =s.(len a) by FINSEQ_1:79;
end;

theorem for a being FinSequence of REAL holds
ex s being XFinSequence of REAL
st len s=len a +1 & s.0=0 &
(for i st i<len a holds s.(i+1)=s.i+a.(i+1))
& Sum a =s.(len a)
proof let a be FinSequence of REAL;
  deffunc F(Nat) = Sum (a|$1);
  ex p being XFinSequence st len p = len a +1 &
  for k st k in len a+1 holds p.k=F(k) from AFINSQ_1:sch 2;then
  consider p being XFinSequence such that
  A1: len p = len a +1 &
  for k st k in len a+1 holds p.k=F(k);
  rng p c= REAL
  proof let y be set;assume y in rng p;then
    consider x being set such that
    A2: x in dom p & y=p.x by FUNCT_1:def 5;
    A3: x in len p by A2,AFINSQ_1:def 1;then
    reconsider nx=x as Nat by Lm1;
    p.nx=F(nx) by A1,A3 .=Sum(a|nx);
   hence y in REAL by A2;
  end;then
  reconsider p as XFinSequence of REAL by Lm2;
  0<len a +1 by NAT_1:19;then
  0 in len a +1 by Th1;then
  A4: p.0=F(0) by A1 .=Sum (<*>REAL) .=0 by RVSUM_1:102;
  A5: for i st i<len a holds p.(i+1)=p.i+a.(i+1)
  proof let i;
   assume A6: i<len a;
    reconsider ii=i+1 as Nat;
    A7: i+1<len a+1 by A6,REAL_1:53;then
    i+1 in len a +1 by Th1;then
    A8: p.(i+1)=Sum (a|ii) by A1;
    i<len a+1 by A7,NAT_1:38;then
    i in len a+1 by Th1;then
    A9: p.i=Sum (a|i) by A1;
    A10: i+1<=len a by A6,NAT_1:38;
    1<=1+i by NAT_1:29;then
    i+1 in Seg len a by A10,FINSEQ_1:3;then
    A11: i+1 in dom a by FINSEQ_1:def 3;
    a|ii=(a|i)^<*a/.ii*> by A10,JORDAN8:2;then
    Sum(a|ii)=Sum(a|i)+Sum(<*a/.ii*>) by RVSUM_1:105
             .=p.i +a/.ii by A9,RVSUM_1:103
             .=p.i +a.ii by A11,FINSEQ_4:def 4;
   hence p.(i+1)=p.i+a.(i+1) by A8;
  end;
  len a<len a+1 by NAT_1:38;then
  Sum a=p.(len a) by A1,A4,A5,Th8;
 hence ex s being XFinSequence of REAL
  st len s=len a +1 & s.0=0 &
  (for i st i<len a holds s.(i+1)=s.i+a.(i+1))
  & Sum a =s.(len a) by A1,A4,A5;
end;

theorem for a,b being FinSequence of REAL, n being Nat st len a=len b
& n>len a
holds
 |(a,b)| = inner_prd_prg(FS2XFS*(a,n),FS2XFS*(b,n))
proof let a,b be FinSequence of REAL, n2 be Nat;
 assume A1: len a=len b & n2>len a;
  set pa= FS2XFS*(a,n2);
  set pb= FS2XFS*(b,n2);
  set ri= inner_prd_prg(FS2XFS*(a,n2),FS2XFS*(b,n2));
  A2: len a = (pa.0) & len pa=n2 &
  (for i st 1<=i & i<= len a  holds pa.i=a.i)&
  (for j being Nat st len a<j & j<n2 holds pa.j=0) by A1,Def3;
  A3: len b = (pb.0) & len pb=n2 &
  (for i st 1<=i & i<= len b holds pb.i=b.i)&
  (for j being Nat st len b<j & j<n2 holds pb.j=0) by A1,Def3;
  A4: pb.0 is Nat by A1,Def3;
  0<=pb.0 & pb.0 < len pa by A1,A2,A3,NAT_1:18;then
  consider s being XFinSequence of REAL, n being Integer such that
   A5: len s=len pa
   & s.0=0
   & n=pb.0
   & (n<>0 implies
   for i being Nat st i<n
    holds s.(i+1)=s.i + (pa.(i+1))*(pb.(i+1))
   )
   & ri=s.n by A4,Def7;
   reconsider ra=a as Element of (len a)-tuples_on REAL by FINSEQ_2:110;
   reconsider rb=b as Element of (len a)-tuples_on REAL by A1,FINSEQ_2:110;
   A6: len mlt(ra,rb)=len a by FINSEQ_2:109;
   A7: dom mlt(ra,rb) =Seg len mlt(ra,rb) by FINSEQ_1:def 3
                 .=Seg len a by A6;
   A8: for i st i in dom(mlt(a,b)) holds
   (mlt(a,b)).i=(a.i)*(b.i) by RVSUM_1:86;
   for i st i<len mlt(a,b) holds s.(i+1)=s.i+(mlt(a,b)).(i+1)
   proof let i;
     assume
     A9: i<len mlt(a,b);
     A10: 1<=i+1 by NAT_1:29;
     A11: i+1<=len a by A6,A9,NAT_1:38;then
     A12: pa.(i+1)=a.(i+1) by A2,A10;
     A13: pb.(i+1)=b.(i+1) by A1,A3,A10,A11;
     A14: i+1 in dom(mlt(a,b)) by A7,A10,A11,FINSEQ_1:3;
     A15: i<n by A1,A3,A5,A6,A9;
     now per cases;
     case A16: n<>0;
     s.(i+1)=s.i + (pa.(i+1))*(pb.(i+1)) by A5,A15,A16
           .=s.i+(a.(i+1))*(b.(i+1)) by A12,A13
           .=s.i+(mlt(a,b)).(i+1) by A8,A14;
      hence s.(i+1)=s.i+(mlt(a,b)).(i+1);
     end;
     case n=0;
      hence contradiction by A15,NAT_1:18;
     end;
     end;
    hence s.(i+1)=s.i+(mlt(a,b)).(i+1);
   end;then
   Sum mlt(a,b)=s.(len mlt(a,b)) by A1,A2,A5,A6,Th8
               .=s.(len a) by A6
               .=s.n by A1,A3,A5;
 hence |(a,b)| = inner_prd_prg(FS2XFS*(a,n2),FS2XFS*(b,n2))
                       by A5,EUCLID_2:def 1;
end;

:: Vector Calculation Program: Scalar Product of Vector
::
:: The following C program is correct if it is used under some limited
::conditions, which are shown in the theorem following the definition
::after this program.
:: But it still remains a possibility of overflow.
:: The following program does not take an explicit function form.
::It means the value of the function does not have a sense.
::The result of the calculation is
::given in a variable c. Precisely speaking, the result is not unique,
::because of the difference of initial value of c.
::  For a model of such a program, we propose the logical predicate form
::(we call such a model, Logical-Model of a program) in the following
::definition.
::
::#define V_SIZE 1024
::void scalar_prd_prg(float c[V_SIZE], float a, float b[V_SIZE])
::{       int n,i;
::        c[0]=b[0];
::        n=(int)(b[0]);
::        if (n != 0)
::         { for(i=1;i<=n;i++)c[i]=a*b[i];
::         }
::        return;
::}
::The following definition is Logical-Model of the above program.
definition let b,c be XFinSequence of REAL,a be Real,m be Integer;
pred m scalar_prd_prg c,a,b means
:Def8: len c =m & len b =m &
 ex n being Integer st
  c.0=b.0 & n=b.0 & (n<>0 implies
 for i being Nat st 1<=i & i<=n holds c.i=a*(b.i));
end;

theorem for b being non empty XFinSequence of REAL,
a being Real,m being Nat
st b.0 is Nat & len b=m & 0<=b.0 & b.0 < m holds
(ex c being XFinSequence of REAL st
 m scalar_prd_prg c,a,b)&
 for c being non empty XFinSequence of REAL st m scalar_prd_prg c,a,b
 holds (XFS2FS*(c))=a*(XFS2FS*(b))
proof let b be non empty XFinSequence of REAL,a be Real,m be Nat;
 assume A1: b.0 is Nat & len b=m & 0<=(b.0) & (b.0) < m;then
  A2: (b.0) is Nat;
  A3: (b.0) in m by A1,Th1;
  A4: 0 < len b by Th2;
  reconsider k= (b.0) as Nat by A1;
  A5: k<m by A1;
  A6: len XFS2FS*(b) =k &
    for i st 1<=i & i<= k  holds (XFS2FS*(b)).i=b.i by A1,A3,Def4;
  reconsider c2= a*(XFS2FS*(b)) as FinSequence of REAL;
  A7: len (XFS2FS*(b)) = (b.0) by A1,A3,Def4;
  dom(a*(XFS2FS*(b))) = dom (XFS2FS*(b))
                            by RVSUM_1:65;then
  Seg len (a*(XFS2FS*(b)))= dom (XFS2FS*(b))
                            by FINSEQ_1:def 3;then
  A8: len c2=len (XFS2FS*(b)) by FINSEQ_1:def 3 .=k by A7;
   consider p being XFinSequence of REAL such that
  A9: len p=m & p is_an_xrep_of c2 by A5,A8,Th7;
 A10: (p.0)=len c2 & len c2 < len p &
   for i st 1<=i & i<=len c2 holds p.i=c2.i by A9,Def5;
 set p2=Replace(p,0,b.0);
 A11: len p=len p2 by AFINSQ_1:48;
 ex n being Integer st
 p2.0=b.0 & n=(b.0)
 &
 (n<>0 implies
 for i being Nat st 1<=i & i<=n
 holds p2.i=a*(b.i)
 )
 proof
 reconsider n=(b.0) as Integer by A2;
 A12: (k<>0 implies
 for i being Nat st 1<=i & i<=k
 holds p2.i=a*(b.i)
 )
 proof
  now assume k<>0;
   thus for i being Nat st 1<=i & i<=k
   holds p2.i=a*(b.i)
   proof let i be Nat;
    assume A13: 1<=i & i<=k;then
      A14: i<>0;
      A15: p.i=c2.i by A8,A10,A13;
      (XFS2FS*(b)).i= b.i by A6,A13;then
      (a*(XFS2FS*(b))).i = a*(b.i) by RVSUM_1:66;
    hence p2.i=a*(b.i) by A14,A15,AFINSQ_1:48;
   end;
  end;
  hence thesis;
 end;
 p2.0=b.0 by A1,A9,AFINSQ_1:48;
 hence thesis by A12;
 end;then
 m scalar_prd_prg p2,a,b by A1,A9,A11,Def8;
 hence (ex c being XFinSequence of REAL st
  m scalar_prd_prg c,a,b);
 thus for c being non empty XFinSequence of REAL st
 m scalar_prd_prg c,a,b
 holds (XFS2FS*(c))=a*(XFS2FS*(b))
 proof let c be non empty XFinSequence of REAL;
   assume m scalar_prd_prg c,a,b;then
   A16: len c =m & len b =m &
   ex n being Integer st
   c.0=b.0 & n=(b.0)
  &
   (n<>0 implies
   for i being Nat st 1<=i & i<=n
   holds c.i=a*(b.i)) by Def8;then
   consider n being Integer such that
   A17: c.0=b.0 & n=(b.0)
    &
    (n<>0 implies
    for i being Nat st 1<=i & i<=n
     holds c.i=a*(b.i));
    A18: len (XFS2FS*(c)) = (c.0) by A1,A3,A16,Def4;
    now per cases;
    case A19: n=0;then
      A20: (XFS2FS*(b))=<*>REAL by A4,A17,Th5;
      (XFS2FS*(c))=<*>REAL by A4,A16,A17,A19,Th5;
     hence (XFS2FS*(c))=a*(XFS2FS*(b))
                                by A20,RVSUM_1:68;
    end;
    case n<>0;
      A21: for i being Nat st 1<=i & i<=n
       holds c.i=a*(b.i) by A17;
       set p3=(XFS2FS*(c));
       A22: for k3 being Nat st 1 <=k3 & k3 <= len p3 holds p3.k3=c2.k3
       proof let k3 be Nat;
        assume A23: 1 <=k3 & k3 <= len p3;
         A24: n=k by A17;
         (c.0) in len c by A5,A16,Th1;then
         A25: len p3=n & for i st 1<=i & i<= n  holds p3.i=c.i
                        by A2,A17,Def4;
         A26: 1 <=k3 & k3 <= n by A23,A25;
         A27: p3.k3=c.k3 by A23,A25
                 .=a*(b.k3) by A21,A26;
          b.k3 = (XFS2FS*(b)).k3 by A6,A24,A26;
        hence p3.k3=c2.k3 by A27,RVSUM_1:66;
       end;
       len p3= k by A17,A18
            .=len c2 by A8;
     hence (XFS2FS*(c))=a*(XFS2FS*(b))
                           by A22,FINSEQ_1:18;
    end;
    end;
  hence (XFS2FS*(c))=a*(XFS2FS*(b));
 end;
end;

:: Vector Calculation Program: Minus Vector
::#define V_SIZE 1024
::void vector_minus_prg(float c[V_SIZE], float b[V_SIZE])
::{       int n,i;
::        c[0]=b[0];
::        n=(int)(b[0]);
::        if (n != 0)
::         { for(i=1;i<=n;i++)c[i]= -b[i];
::         }
::        return;
::}
::The following definition is Logical-Model of the above program.
definition let b,c be XFinSequence of REAL,m be Integer;
pred m vector_minus_prg c,b means
:Def9: len c =m & len b =m &
 ex n being Integer st c.0=b.0 & n= b.0 & (n<>0 implies
 for i being Nat st 1<=i & i<=n
 holds c.i= -(b.i));
end;

theorem for b being non empty XFinSequence of REAL,m being Nat
st b.0 is Nat & len b=m & 0<=(b.0) & (b.0) < m holds
(ex c being XFinSequence of REAL st
 m vector_minus_prg c,b)&
 for c being non empty XFinSequence of REAL st m vector_minus_prg c,b
 holds (XFS2FS*(c))= -(XFS2FS*(b))
proof let b be non empty XFinSequence of REAL,m be Nat;
 assume A1: b.0 is Nat & len b=m & 0<=(b.0) & (b.0) < m;then
  A2: (b.0) is Nat;
  A3: (b.0) in m by A1,Th1;
  A4: 0 < len b by Th2;
  reconsider k= (b.0) as Nat by A1;
  A5: k<m by A1;
  A6: len XFS2FS*(b) =k &
    for i st 1<=i & i<= k  holds (XFS2FS*(b)).i=b.i by A1,A3,Def4;
  reconsider c2=  -(XFS2FS*(b)) as FinSequence of REAL;
  A7: len (XFS2FS*(b)) = (b.0) by A1,A3,Def4;
  dom( -(XFS2FS*(b))) = dom (XFS2FS*(b))
                            by RVSUM_1:34;then
  Seg len ( -(XFS2FS*(b)))= dom (XFS2FS*(b))
                            by FINSEQ_1:def 3;then
  A8: len c2=len (XFS2FS*(b)) by FINSEQ_1:def 3 .=k by A7;
   consider p being XFinSequence of REAL such that
  A9: len p=m & p is_an_xrep_of c2 by A5,A8,Th7;
 A10: (p.0)=len c2 & len c2 < len p &
   for i st 1<=i & i<=len c2 holds p.i=c2.i by A9,Def5;
 set p2=Replace(p,0,b.0);
 A11: len p=len p2 by AFINSQ_1:48;
 ex n being Integer st p2.0=b.0 & n=(b.0) &
 (n<>0 implies for i being Nat st 1<=i & i<=n
 holds p2.i= -(b.i))
 proof
 reconsider n=(b.0) as Integer by A2;
 A12: (k<>0 implies
 for i being Nat st 1<=i & i<=k
 holds p2.i= -(b.i))
 proof
  now assume k<>0;
   thus for i being Nat st 1<=i & i<=k
   holds p2.i= -(b.i)
   proof let i be Nat;
    assume A13: 1<=i & i<=k;then
      A14: i<>0;
      A15: p.i=c2.i by A8,A10,A13;
      (XFS2FS*(b)).i= b.i by A6,A13;then
      ( -(XFS2FS*(b))).i =  -(b.i) by RVSUM_1:35;
    hence p2.i= -(b.i) by A14,A15,AFINSQ_1:48;
   end;
  end;
  hence thesis;
 end;
 p2.0=b.0 by A1,A9,AFINSQ_1:48;
 hence thesis by A12;
 end;then
 m vector_minus_prg p2,b by A1,A9,A11,Def9;
 hence (ex c being XFinSequence of REAL st
  m vector_minus_prg c,b);
 thus for c being non empty XFinSequence of REAL st
 m vector_minus_prg c,b
 holds (XFS2FS*(c))= -(XFS2FS*(b))
 proof let c be non empty XFinSequence of REAL;
   assume m vector_minus_prg c,b;then
   A16: len c =m & len b =m &
   ex n being Integer st
   c.0=b.0 & n=(b.0)
  &
   (n<>0 implies
   for i being Nat st 1<=i & i<=n
   holds c.i= -(b.i)) by Def9;then
   consider n being Integer such that
   A17: c.0=b.0 & n=(b.0) &
    (n<>0 implies
    for i being Nat st 1<=i & i<=n
     holds c.i= -(b.i));
    A18: n=(c.0) by A17;
    A19: len (XFS2FS*(c)) = (c.0) by A2,A3,A16,Def4;
    now per cases;
    case A20: n=0;then
      (XFS2FS*(b))=<*>REAL by A4,A17,Th5;
     hence (XFS2FS*(c))= -(XFS2FS*(b))
                                by A4,A16,A17,A20,Th5,RVSUM_1:37;
    end;
    case n<>0;
      A21: for i being Nat st 1<=i & i<=n
       holds c.i= -(b.i) by A17;
       set p3=(XFS2FS*(c));
       A22: for k3 being Nat st 1 <=k3 & k3 <= len p3 holds p3.k3=c2.k3
       proof let k3 be Nat;
        assume A23: 1 <=k3 & k3 <= len p3;
         A24: n=k by A17;
         A25: (c.0) is Nat by A2,A17;
         (c.0) in len c by A5,A16,Th1;then
         A26: len p3=n & for i st 1<=i & i<= n  holds p3.i=c.i
                      by A18,A25,Def4;
         A27: 1 <=k3 & k3 <= n by A23,A26;
         A28: p3.k3=c.k3 by A23,A26
                 .= -(b.k3) by A21,A27;
          b.k3 = (XFS2FS*b).k3 by A6,A24,A27;
        hence p3.k3=c2.k3 by A28,RVSUM_1:35;
       end;
       len p3= k by A17,A19
            .=len c2 by A8;
     hence (XFS2FS*(c))= -(XFS2FS*(b))
                           by A22,FINSEQ_1:18;
    end;
    end;
  hence (XFS2FS*(c))= -(XFS2FS*(b));
 end;
end;

:: Vector Calculation Program: Sum of Vectors
::
:: The following program is the same type of scalar_prd_prg, but gives a result
:: a+b in a variable c.
::
::#define V_SIZE 1024
::void vector_add_prg(float c[V_SIZE], float a[V_SIZE], float b[V_SIZE])
::{       int n,i;
::        c[0]=b[0];
::        n=(int)(b[0]);
::        if (n != 0)
::         { for(i=1;i<=n;i++)c[i]=a[i]+b[i];
::         }
::        return;
::}
definition let a,b,c be XFinSequence of REAL,m be Integer;
pred m vector_add_prg c,a,b means
:Def10: len c =m & len a=m & len b =m &
 ex n being Integer st
  c.0=b.0
& n=(b.0)
& (n<>0 implies
 for i being Nat st 1<=i & i<=n
 holds c.i=(a.i)+(b.i)
 );
end;

theorem for a,b being non empty XFinSequence of REAL,
m being Nat
st b.0 is Nat & len a=m & len b=m & (a.0)=(b.0) &
0<=(b.0) & (b.0) < m  holds
(ex c being XFinSequence of REAL st
 m vector_add_prg c,a,b)&
 for c being non empty XFinSequence of REAL st m vector_add_prg c,a,b
 holds (XFS2FS*(c))=(XFS2FS*(a))+(XFS2FS*(b))
proof let a,b be non empty XFinSequence of REAL,m be Nat;
 assume A1: b.0 is Nat & len a=m & len b=m & (a.0)=(b.0)
 & 0<=(b.0) & (b.0) < m ;then
  A2: (b.0) is Nat;
  A3: (b.0) in m by A1,Th1;
  A4: 0 < len b by Th2;
  reconsider k= (b.0) as Nat by A2;
  A5: k<m by A1;
  A6: len XFS2FS*(b) =k &
    for i st 1<=i & i<= k  holds (XFS2FS*(b)).i=b.i by A1,A3,Def4;
  A7: len XFS2FS*(a) =k &
    for i st 1<=i & i<= k  holds (XFS2FS*(a)).i=a.i by A1,A3,Def4;
  reconsider Fb=(XFS2FS*(b)) as FinSequence of REAL;
  reconsider Fa=(XFS2FS*(a)) as FinSequence of REAL;
  reconsider c2= Fa + Fb as FinSequence of REAL;
  A8: len (XFS2FS*(b)) = (b.0) by A1,A3,Def4;
  A9: len (XFS2FS*(a)) = (b.0) by A1,A3,Def4;then
  A10: len c2=len (XFS2FS*(a)) by A8,EUCLID_2:6;then
  dom c2= Seg len (XFS2FS*(b)) by A8,A9,FINSEQ_1:def 3;then
  dom((XFS2FS*(a))+(XFS2FS*(b))) = dom (XFS2FS*(b))
                            by FINSEQ_1:def 3;then
  Seg len (c2) = dom (XFS2FS*(b)) by FINSEQ_1:def 3;then
  A11: len c2=len (XFS2FS*(b)) by FINSEQ_1:def 3 .=k by A8;
   consider p being XFinSequence of REAL such that
  A12: len p=m & p is_an_xrep_of c2 by A5,A11,Th7;
 A13: (p.0)=len c2 & len c2 < len p &
   for i st 1<=i & i<=len c2 holds p.i=c2.i by A12,Def5;
 set p2=Replace(p,0,b.0);
 A14: len p=len p2 by AFINSQ_1:48;
 ex n being Integer st
 p2.0=b.0 & n=(b.0) &
 (n<>0 implies
 for i being Nat st 1<=i & i<=n
 holds p2.i=(a.i)+(b.i) )
 proof
 reconsider n=(b.0) as Integer by A2;
 A15: (k<>0 implies
 for i being Nat st 1<=i & i<=k
 holds p2.i=(a.i)+(b.i) )
 proof
  now assume k<>0;
   thus for i being Nat st 1<=i & i<=k
   holds p2.i=(a.i)+(b.i)
   proof let i be Nat;
    assume A16: 1<=i & i<=k;
      A17: i<>0 by A16;
      A18: p.i=c2.i by A11,A13,A16;
      i in Seg len c2 by A9,A10,A16,FINSEQ_1:3;then
      A19: i in dom c2 by FINSEQ_1:def 3;
      A20: (XFS2FS*(a)).i= a.i by A7,A16;
      A21: (XFS2FS*(b)).i= b.i by A6,A16;
      ((XFS2FS*(a))+(XFS2FS*(b))).i
      = (XFS2FS*(a)).i +(XFS2FS*(b)).i by A19,RVSUM_1:26
     .= (a.i)+(b.i) by A20,A21;
    hence p2.i=(a.i)+(b.i) by A17,A18,AFINSQ_1:48;
   end;
  end;
  hence thesis;
 end;
 p2.0=b.0 by A1,A12,AFINSQ_1:48;
 hence thesis by A15;
 end;then
 m vector_add_prg p2,a,b by A1,A12,A14,Def10;
 hence (ex c being XFinSequence of REAL st
  m vector_add_prg c,a,b);
 thus for c being non empty XFinSequence of REAL st
 m vector_add_prg c,a,b
 holds (XFS2FS*(c))=(XFS2FS*(a))+(XFS2FS*(b))
 proof let c be non empty XFinSequence of REAL;
   assume m vector_add_prg c,a,b;then
   A22: len c =m & len b =m &
   ex n being Integer st
   c.0=b.0 & n=(b.0)
  &
   (n<>0 implies
   for i being Nat st 1<=i & i<=n
   holds c.i=(a.i)+(b.i)) by Def10;then
   consider n being Integer such that
   A23: c.0=b.0 & n=(b.0)
    &
    (n<>0 implies
    for i being Nat st 1<=i & i<=n
     holds c.i=(a.i)+(b.i));
    A24: len (XFS2FS*(c)) = (c.0) by A2,A3,A22,Def4;
    now per cases;
    case A25: n=0;then
      A26: (XFS2FS*(b))=<*>REAL by A4,A23,Th5;
      (XFS2FS*(c))=<*>REAL by A4,A22,A23,A25,Th5;
     hence (XFS2FS*(c))=(XFS2FS*(a))+(XFS2FS*(b)) by A26,RVSUM_1:28;
    end;
    case n<>0;
      A27: for i being Nat st 1<=i & i<=n
       holds c.i=(a.i)+(b.i) by A23;
       set p3=(XFS2FS*(c));
       A28: for k3 being Nat st 1 <=k3 & k3 <= len p3 holds p3.k3=c2.k3
       proof let k3 be Nat;
        assume A29: 1 <=k3 & k3 <= len p3;
         A30: n=k by A23;
         (c.0) in len c by A5,A22,Th1;then
         A31: len p3=n & for i st 1<=i & i<= n  holds p3.i=c.i
                        by A2,A23,Def4;
         A32: 1 <=k3 & k3 <= n by A29,A31;
         A33: p3.k3=c.k3 by A29,A31 .=(a.k3)+(b.k3) by A27,A32;
         k3 in Seg len c2 by A11,A30,A32,FINSEQ_1:3;then
         A34: k3 in dom c2 by FINSEQ_1:def 3;
         A35: a.k3 = (XFS2FS*(a)).k3 by A7,A30,A32;
          b.k3 = (XFS2FS*(b)).k3 by A6,A30,A32;
        hence p3.k3=c2.k3 by A33,A34,A35,RVSUM_1:26;
       end;
       len p3= k by A23,A24
            .=len c2 by A11;
     hence (XFS2FS*(c))=(XFS2FS*(a))+(XFS2FS*(b))
                           by A28,FINSEQ_1:18;
    end;
    end;
  hence (XFS2FS*(c))=(XFS2FS*(a))+(XFS2FS*(b));
 end;
end;

:: Vector Calculation Program: Subtraction of Vectors
::
:: The following program is the same type of scalar_prd_prg, but gives a result
:: a-b in a variable c.
::
::#define V_SIZE 1024
::void vector_sub_prg(float c[V_SIZE], float a[V_SIZE], float b[V_SIZE])
::{       int n,i;
::        c[0]=b[0];
::        n=(int)(b[0]);
::        if (n != 0)
::         { for(i=1;i<=n;i++)c[i]=a[i]-b[i];
::         }
::        return;
::}
definition let a,b,c be XFinSequence of REAL,m be Integer;
pred m vector_sub_prg c,a,b means
:Def11: len c =m & len a=m & len b =m &
 ex n being Integer st
  c.0=b.0
& n=(b.0)
& (n<>0 implies
 for i being Nat st 1<=i & i<=n
 holds c.i=(a.i)-(b.i)
 );
end;

theorem for a,b being non empty XFinSequence of REAL,
m being Nat
st b.0 is Nat & len a=m & len b=m & (a.0)=(b.0) &
0<=(b.0) & (b.0) < m  holds
(ex c being XFinSequence of REAL st
 m vector_sub_prg c,a,b)&
 for c being non empty XFinSequence of REAL st m vector_sub_prg c,a,b
 holds (XFS2FS*(c))=(XFS2FS*(a))-(XFS2FS*(b))
proof let a,b be non empty XFinSequence of REAL,m be Nat;
 assume A1: b.0 is Nat & len a=m & len b=m & (a.0)=(b.0)
 & 0<=(b.0) & (b.0) < m;then
  A2: (b.0) is Nat;
  A3: (b.0) in m by A1,Th1;
  A4: 0 < len b by Th2;
  reconsider k= (b.0) as Nat by A2;
  A5: k<m by A1;
  A6: len XFS2FS*(b) =k &
    for i st 1<=i & i<= k  holds (XFS2FS*(b)).i=b.i by A1,A3,Def4;
  A7: len XFS2FS*(a) =k &
    for i st 1<=i & i<= k  holds (XFS2FS*(a)).i=a.i by A1,A3,Def4;
  reconsider Fb=(XFS2FS*(b)) as FinSequence of REAL;
  reconsider Fa=(XFS2FS*(a)) as FinSequence of REAL;
  reconsider c2= Fa - Fb as FinSequence of REAL;
  A8: len (XFS2FS*(b)) = (b.0) by A1,A3,Def4;
  A9: len (XFS2FS*(a)) = (b.0) by A1,A3,Def4;then
  A10: len c2=len (XFS2FS*(a)) by A8,EUCLID_2:7;then
  dom c2= Seg len (XFS2FS*(b)) by A8,A9,FINSEQ_1:def 3;then
  dom((XFS2FS*(a))-(XFS2FS*(b)))
      = dom (XFS2FS*(b)) by FINSEQ_1:def 3;then
  Seg len (c2) = dom (XFS2FS*(b)) by FINSEQ_1:def 3;then
  A11: len c2=len (XFS2FS*(b)) by FINSEQ_1:def 3 .=k by A8;
   consider p being XFinSequence of REAL such that
  A12: len p=m & p is_an_xrep_of c2 by A5,A11,Th7;
 A13: (p.0)=len c2 & len c2 < len p &
   for i st 1<=i & i<=len c2 holds p.i=c2.i by A12,Def5;
 set p2=Replace(p,0,b.0);
 A14: len p=len p2 by AFINSQ_1:48;
 ex n being Integer st
 p2.0=b.0 & n=(b.0)
 &
 (n<>0 implies
 for i being Nat st 1<=i & i<=n
 holds p2.i=(a.i)-(b.i)
 )
 proof
 reconsider n=(b.0) as Integer by A2;
 A15: (k<>0 implies
 for i being Nat st 1<=i & i<=k
 holds p2.i=(a.i)-(b.i)
 )
 proof
  now assume k<>0;
   thus for i being Nat st 1<=i & i<=k
   holds p2.i=(a.i)-(b.i)
   proof let i be Nat;
    assume A16: 1<=i & i<=k;
      A17: i<>0 by A16;
      A18: p.i=c2.i by A11,A13,A16;
      i in Seg len c2 by A9,A10,A16,FINSEQ_1:3;then
      A19: i in dom c2 by FINSEQ_1:def 3;
      A20: (XFS2FS*(a)).i= a.i by A7,A16;
      A21: (XFS2FS*(b)).i= b.i by A6,A16;
      ((XFS2FS*(a))-(XFS2FS*(b))).i
      = (XFS2FS*(a)).i -(XFS2FS*(b)).i by A19,RVSUM_1:47
     .= (a.i)-(b.i) by A20,A21;
    hence p2.i=(a.i)-(b.i) by A17,A18,AFINSQ_1:48;
   end;
  end;
  hence thesis;
 end;
 p2.0=b.0 by A1,A12,AFINSQ_1:48;
 hence thesis by A15;
 end;then
 m vector_sub_prg p2,a,b by A1,A12,A14,Def11;
 hence (ex c being XFinSequence of REAL st
  m vector_sub_prg c,a,b);
 thus for c being non empty XFinSequence of REAL st
 m vector_sub_prg c,a,b
 holds (XFS2FS*(c))=(XFS2FS*(a))-(XFS2FS*(b))
 proof let c be non empty XFinSequence of REAL;
   assume m vector_sub_prg c,a,b;then
   A22: len c =m & len b =m &
   ex n being Integer st
   c.0=b.0 & n=(b.0)
  &
   (n<>0 implies
   for i being Nat st 1<=i & i<=n
   holds c.i=(a.i)-(b.i)) by Def11;then
   consider n being Integer such that
   A23: c.0=b.0 & n=(b.0)
    &
    (n<>0 implies
    for i being Nat st 1<=i & i<=n
     holds c.i=(a.i)-(b.i));
    A24: len (XFS2FS*(c)) = (c.0) by A2,A3,A22,Def4;
    now per cases;
    case A25: n=0;then
      A26: (XFS2FS*(b))=<*>REAL by A4,A23,Th5;
      (XFS2FS*(c))=<*>REAL by A4,A22,A23,A25,Th5;
     hence (XFS2FS*(c))=(XFS2FS*(a))-(XFS2FS*(b))
                                by A26,RVSUM_1:49;
    end;
    case n<>0;
      A27: for i being Nat st 1<=i & i<=n
       holds c.i=(a.i)-(b.i) by A23;
       set p3=(XFS2FS*(c));
       A28: for k3 being Nat st 1 <=k3 & k3 <= len p3 holds p3.k3=c2.k3
       proof let k3 be Nat;
        assume A29: 1 <=k3 & k3 <= len p3;
         A30: n=k by A23;
         (c.0) in len c by A5,A22,Th1;then
         A31: len p3=n & for i st 1<=i & i<= n  holds p3.i=c.i
                 by A2,A23,Def4;
         A32: 1 <=k3 & k3 <= n by A29,A31;
         A33: p3.k3=c.k3 by A29,A31 .=(a.k3)-(b.k3) by A27,A32;
         k3 in Seg len c2 by A11,A30,A32,FINSEQ_1:3;then
         A34: k3 in dom c2 by FINSEQ_1:def 3;
         A35: a.k3 = (XFS2FS*(a)).k3 by A7,A30,A32;
          b.k3 = (XFS2FS*(b)).k3 by A6,A30,A32;
        hence p3.k3=c2.k3 by A33,A34,A35,RVSUM_1:47;
       end;
       len p3= k by A23,A24
            .=len c2 by A11;
     hence (XFS2FS*(c))=(XFS2FS*(a))-(XFS2FS*(b))
                           by A28,FINSEQ_1:18;
    end;
    end;
  hence (XFS2FS*(c))=(XFS2FS*(a))-(XFS2FS*(b));
 end;
end;

