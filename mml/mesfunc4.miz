:: Linearity of {L}ebesgue Integral of Simple Valued Function
::  by Noboru Endou and Yasunari Shidama
::
:: Received September 14, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies PARTFUN1, SUPINF_1, MEASURE1, RELAT_1, FUNCT_1, COMPLEX1, BOOLE,
      ARYTM_1, TARSKI, ARYTM_3, NAT_1, RLVECT_1, GROUP_1, FINSEQ_1, INTEGRA1,
      MESFUNC2, MESFUNC3, ARYTM, ORDINAL2;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0,
      REAL_1, BINARITH, ORDINAL2, RELAT_1, FUNCT_1, PARTFUN1, FUNCT_2,
      FINSEQ_1, NAT_1, RAT_1, SUPINF_1, SUPINF_2, MEASURE1, EXTREAL1, MESFUNC1,
      MESFUNC2, MESFUNC3, CONVFUN1;
 constructors REAL_1, NAT_1, BINARITH, FINSEQOP, FINSOP_1, EXTREAL1, SUPINF_2,
      PARTFUN1, RAT_1, MESFUNC1, MESFUNC3, MEASURE6, MESFUNC2, CONVFUN1,
      MEASURE3;
 registrations SUPINF_1, XREAL_0, RELSET_1, NAT_1, FINSEQ_1, ORDINAL2,
      MEASURE1;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;
 definitions MESFUNC3, TARSKI, XBOOLE_0;
 theorems MEASURE1, TARSKI, SUPINF_1, FUNCT_1, NAT_1, REAL_1, SUPINF_2,
      RELSET_1, EXTREAL1, EXTREAL2, MESFUNC1, ZFMISC_1, FINSEQ_1, FINSEQ_2,
      FINSEQ_5, PROB_2, XBOOLE_0, XBOOLE_1, XCMPLX_1, RELAT_1, MESFUNC2,
      CONVFUN1, MESFUNC3, BINARITH, XREAL_1, NAT_2, JORDAN3, FINSEQ_3;
 schemes FINSEQ_1, FINSEQ_2, NAT_1;

begin  :: Linearity of Lebesgue Integral of Simple Valued Function

theorem LM000:
for F,G,H be FinSequence of ExtREAL
 st (for i be Nat st i in dom F holds 0. <=' F.i)
  & (for i be Nat st i in dom G holds 0. <=' G.i)
   & dom F = dom G & H = F + G
 holds
  Sum(H)=Sum(F)+Sum(G)
proof
   let F,G,H be FinSequence of ExtREAL;
   assume that
A1:for i be Nat st i in dom F holds 0. <=' F.i and
A2:for i be Nat st i in dom G holds 0. <=' G.i and
A3:dom F = dom G and
A4:H = F + G;
   consider f be Function of NAT,ExtREAL such that
A5: Sum(F) = f.(len F) & f.0 = 0. &
    for i be Nat st i < len F holds f.(i+1) = f.i + F.(i+1) by CONVFUN1:def 5;
   consider g be Function of NAT,ExtREAL such that
A6: Sum(G) = g.(len G) & g.0 = 0. &
    for i be Nat st i < len G holds g.(i+1) = g.i + G.(i+1) by CONVFUN1:def 5;
   consider h be Function of NAT,ExtREAL such that
A7: Sum(H) = h.(len H) & h.0 = 0. &
    for i be Nat st i < len H holds h.(i+1) = h.i + H.(i+1) by CONVFUN1:def 5;
   for y be set st y in rng F holds not y in {-infty}
   proof
    let y be set;
    assume y in rng F; then
    consider x be set such that
A8:  x in dom F & y = F.x by FUNCT_1:def 5;
    reconsider x as Nat by A8;
    0. <=' F.x by A1,A8;
    hence thesis by A8,TARSKI:def 1,SUPINF_2:19;
   end; then
   rng F misses {-infty} by XBOOLE_0:3; then
A9:F"{-infty} = {} by RELAT_1:173;
   for y be set st y in rng G holds not y in {-infty}
   proof
    let y be set;
    assume y in rng G; then
    consider x be set such that
A10: x in dom G & y = G.x by FUNCT_1:def 5;
    reconsider x as Nat by A10;
    0. <=' G.x by A2,A10;
    hence thesis by A10,TARSKI:def 1,SUPINF_2:19;
   end; then
   rng G misses {-infty} by XBOOLE_0:3; then
A11:G"{-infty} = {} by RELAT_1:173;
A12:dom H
     = (dom F /\ dom G)\((F"{-infty}/\G"{+infty})\/(F"{+infty}/\G"{-infty}))
       by A4,MESFUNC1:def 3
    .= dom F /\ dom G by A11,A9
    .= dom F by A3; then
A13:len H = len F by FINSEQ_3:31;
A14:len H = len G by A12,A3,FINSEQ_3:31;
    defpred P[Nat] means $1 <= len H implies h.$1 = f.$1 + g.$1;
A15:P[0] by A5,A6,A7,SUPINF_2:18;
A16:for k being Nat st P[k] holds P[k + 1]
   proof
    let k be Nat;
    assume A17:P[k];
    assume A18:k+1 <= len H;
    1 <= k+1 by NAT_1:29; then
B1: k+1 in dom H by A18,FINSEQ_3:27;
A19:k < len H by A18,NAT_1:38; then
A20:f.(k+1) = f.k + F.(k+1) & g.(k+1) = g.k + G.(k+1) & h.(k+1) = h.k + H.(k+1)
     by A5,A6,A7,A13,A14;
A21:h.(k+1) = (f.k + g.k) + H.(k+1) by A17,A19,A7
           .= (f.k + g.k) + (F.(k+1) + G.(k+1)) by B1,A4,MESFUNC1:def 3;
A22:f.k <> -infty & g.k <> -infty & F.(k+1) <> -infty & G.(k+1) <> -infty
    proof
     defpred Pf[Nat] means
     $1 <= len H implies f.$1 <> -infty;
f1:  Pf[0] by A5,SUPINF_2:19;
f2:  for m be Nat st Pf[m] holds Pf[m+1]
     proof
       let m be Nat;
       assume f3:Pf[m];
       assume f4:m+1 <= len H; then
       m < len H by NAT_1:38; then
f6:    f.(m+1) = f.m + F.(m+1) by A5,A13;
       1 <= m+1 by NAT_1:29; then
       m+1 in dom H by f4,FINSEQ_3:27; then
       -infty <' 0. & 0. <=' F.(m+1) by A1,A12,SUPINF_2:19;
       hence thesis by f3,f4,f6,NAT_1:38,SUPINF_2:9;
     end;
     for i be Nat holds Pf[i] from NAT_1:sch 1(f1,f2);
     hence f.k <> -infty by A19;
     defpred Pg[Nat] means
     $1 <= len H implies g.$1 <> -infty;
g1:  Pg[0] by A6,SUPINF_2:19;
g2:  for m be Nat st Pg[m] holds Pg[m+1]
     proof
       let m be Nat;
       assume g3:Pg[m];
       assume g4:m+1 <= len H; then
       m < len H by NAT_1:38; then
g6:    g.(m+1) = g.m + G.(m+1) by A6,A14;
       1 <= m+1 by NAT_1:29; then
       m+1 in dom H by g4,FINSEQ_3:27; then
       -infty <' 0. & 0. <=' G.(m+1) by A2,A3,A12,SUPINF_2:19;
       hence thesis by g3,g4,g6,NAT_1:38,SUPINF_2:9;
     end;
     for i be Nat holds Pg[i] from NAT_1:sch 1(g1,g2);
     hence g.k <> -infty by A19;
     thus F.(k+1) <> -infty by A1,B1,A12,SUPINF_2:19;
     thus G.(k+1) <> -infty by A2,A3,B1,A12,SUPINF_2:19;
    end; then
A23:f.k + g.k <> -infty & F.(k+1) + G.(k+1) <> -infty &
    f.k + F.(k+1) <> -infty by SUPINF_2:9; then
    h.(k+1) = ((f.k + g.k) + F.(k+1)) + G.(k+1) by A21,A22,EXTREAL1:8
           .= (f.k + (F.(k+1) + g.k)) + G.(k+1) by A22,EXTREAL1:8
           .= (f.k + F.(k+1) + g.k) + G.(k+1) by A22,EXTREAL1:8
           .= (f.k + F.(k+1)) + (g.k + G.(k+1)) by A22,A23,EXTREAL1:8
           .= f.(k+1) + g.(k+1) by A20;
    hence thesis;
   end;
A17:for i be Nat holds P[i] from NAT_1:sch 1(A15,A16);
   Sum H = h.(len H) by A7
        .= f.(len H) + g.(len H) by A17
        .= f.(len F) + g.(len G) by A13,A14;
   hence thesis by A5,A6;
end;

theorem LM004:
for X be non empty set,
    S be sigma_Field_Subset of X,
    M be sigma_Measure of S
 holds
   for n be Nat,
       f be PartFunc of X,ExtREAL,
       F be Finite_Sep_Sequence of S,
       a,x be FinSequence of ExtREAL
    st f is_simple_func_in S
     & dom f <> {}
     & (for x be set st x in dom f holds 0. <=' f.x)
     & F,a are_Re-presentation_of f
     & dom x = dom F
     & (for i be Nat st i in dom x holds x.i=a.i*(M*F).i)
     & len F = n
   holds
    integral(X,S,M,f)=Sum(x)
proof
   let X be non empty set;
   let S be sigma_Field_Subset of X;
   let M be sigma_Measure of S;
   defpred P[Nat] means
   for f be PartFunc of X,ExtREAL, F be Finite_Sep_Sequence of S,
       a,x be FinSequence of ExtREAL st
        f is_simple_func_in S
      & dom f <> {}
      & (for x be set st x in dom f holds 0. <=' f.x)
      & F,a are_Re-presentation_of f
      & dom x = dom F
      & (for i be Nat st i in dom x holds x.i=a.i*(M*F).i)
      & len F = $1
      holds integral(X,S,M,f)=Sum(x);
P1:P[0]
   proof
    let f be PartFunc of X,ExtREAL;
    let F be Finite_Sep_Sequence of S;
    let a,x be FinSequence of ExtREAL;
    assume that
     f is_simple_func_in S and
B2:  dom f <> {} and
     (for x be set st x in dom f holds 0. <=' f.x) and
B4:  F,a are_Re-presentation_of f and
     dom x = dom F and
     (for i be Nat st i in dom x holds x.i = a.i*(M*F).i) and
B7:  len F = 0;
    dom F = Seg len F by FINSEQ_1:def 3
          .= {} by B7,FINSEQ_1:4; then
    union rng F = {} by ZFMISC_1:2,RELAT_1:65;
    hence thesis by B2,B4,MESFUNC3:def 1;
   end;
P2:for n be Nat st P[n] holds P[n+1]
   proof
    let n be Nat;
    assume
AS:  P[n];
     let f be PartFunc of X,ExtREAL, F be Finite_Sep_Sequence of S,
        a,x be FinSequence of ExtREAL such that
B1:   f is_simple_func_in S and
B2:   dom f <> {} and
B3:   (for x be set st x in dom f holds 0. <=' f.x) and
B4:   F,a are_Re-presentation_of f and
B5:   dom x = dom F and
B6:   (for i be Nat st i in dom x holds x.i=a.i*(M*F).i) and
B7:   len F = n+1;
     set F1=F|(Seg n);
     set a1=a|(Seg n);
     set x1=x|(Seg n);
     set f1=f|(union rng F1);
     reconsider F1 as Finite_Sep_Sequence of S by MESFUNC2:36;
     reconsider a1 as FinSequence of ExtREAL by FINSEQ_1:23;
     reconsider x1 as FinSequence of ExtREAL by FINSEQ_1:23;
B8:  f is_finite by B1,MESFUNC2:def 5;
     for x be Element of X st x in dom f1 holds |. f1.x .| <' +infty
     proof
      let x be Element of X;
      assume D1:x in dom f1; then
      x in (dom f)/\(union rng F1) by FUNCT_1:68; then
D2:   x in dom f by XBOOLE_0:def 3;
      f1.x = f.x by D1,FUNCT_1:70;
      hence thesis by D2,B8,MESFUNC2:def 1;
     end; then
B9:  f1 is_finite by MESFUNC2:def 1;
B10: dom f1 = union rng F1
     proof
C1:   dom f1 = (dom f) /\ (union rng F1) by FUNCT_1:68
            .= (union rng F) /\ (union rng F1) by B4,MESFUNC3:def 1;
      rng F1 c= rng F by FUNCT_1:76; then
      union rng F1 c= union rng F by ZFMISC_1:95;
      hence thesis by C1,XBOOLE_1:28;
     end;
C1:  f1 is_simple_func_in S
     proof
      ex F1' be Finite_Sep_Sequence of S st
       (dom f1 = union rng F1' &
        for n be Nat, x,y be Element of X st
         n in dom F1' & x in F1'.n & y in F1'.n holds f1.x = f1.y)
      proof
       take F1' = F1;
       for n be Nat, x,y be Element of X st
        n in dom F1 & x in F1.n & y in F1.n holds f1.x = f1.y
       proof
        let n be Nat;
        let x,y be Element of X;
        assume
C2:     n in dom F1 & x in F1.n & y in F1.n; then
        F1.n in rng F1 by FUNCT_1:12; then
        F1.n c= dom f1 by B10,ZFMISC_1:92; then
C3:     f1.x = f.x & f1.y = f.y by C2,FUNCT_1:70;
C4:     F1.n = F.n by C2,FUNCT_1:70;
        dom F1 c= dom F by FUNCT_1:76; then
        f.x = a.n & f.y = a.n by C2,C4,B4,MESFUNC3:def 1;
        hence thesis by C3;
       end;
       hence thesis by B10;
      end;
      hence thesis by B9,MESFUNC2:def 5;
     end;
C3:  for x be set st x in dom f1 holds 0. <=' f1.x
     proof
      let x be set;
      assume
C31:  x in dom f1;
      dom f1 c= dom f by FUNCT_1:76; then
      x in dom f & f1.x = f.x by C31,FUNCT_1:70;
      hence thesis by B3;
     end;
C32: dom F1 = dom F /\ Seg n by FUNCT_1:68
           .= dom a /\ Seg n by B4,MESFUNC3:def 1
           .= dom a1 by FUNCT_1:68;
     for n be Nat st n in dom F1
      for x be set st x in F1.n holds f1.x = a1.n
     proof
      let n be Nat;
      assume C33:n in dom F1;
      dom F1 c= dom F & dom a1 c= dom a by FUNCT_1:76; then
C35:  n in dom F & F1.n = F.n & n in dom a & a1.n = a.n by C33,C32,FUNCT_1:70;
      let x be set;
      assume C36:x in F1.n;
      F1.n in rng F1 by C33,FUNCT_1:12; then
      F1.n c= dom f1 by B10,ZFMISC_1:92; then
      f1.x = f.x by C36,FUNCT_1:70;
      hence thesis by C35,C36,B4,MESFUNC3:def 1;
     end; then
C4:  F1,a1 are_Re-presentation_of f1 by B10,C32,MESFUNC3:def 1;
C5:  dom x1 = dom x /\ Seg n by FUNCT_1:68
           .= dom F /\ Seg n by B5
           .= dom F1 by FUNCT_1:68;
     now per cases;
      suppose
K1a:   dom f1 = {};
K2:    for m be Nat st m in dom F1 holds F1.m = {}
       proof
        let m be Nat;
        assume m in dom F1; then
        F1.m in rng F1 by FUNCT_1:12; then
        F1.m c= {} by K1a,B10,ZFMISC_1:92;
        hence thesis by XBOOLE_1:3;
       end;
K3:    for m be Nat st m in dom F1 holds x.m = 0.
       proof
        let m be Nat;
        assume
K31:    m in dom F1;
        m in dom F /\ (Seg n) by K31,FUNCT_1:68; then
K32:    m in dom x by B5,XBOOLE_0:def 3;
        F1.m = {} by K2,K31; then
K33:    F.m = {} by K31,FUNCT_1:70;
        reconsider EMPTY = {} as Element of S by MEASURE1:45;
        M.EMPTY = 0. by MEASURE1:def 11; then
K34:    (M*F).m = 0. by K33,K32,B5,FUNCT_1:23;
        x.m = a.m*(M*F).m by K32,B6;
        hence thesis by K34,EXTREAL1:22;
       end;
       consider sumx be Function of NAT,ExtREAL such that
K4:     Sum x = sumx.(len x) & sumx.0 = 0. &
        for m be Nat st m < len x holds sumx.(m+1) = sumx.m + x.(m+1)
         by CONVFUN1:def 5;
K9a:   Seg len x = dom x by FINSEQ_1:def 3
                .= dom F by B5
                .= Seg len F by FINSEQ_1:def 3
                .= Seg(n+1) by B7; then
K9:    len x = n+1 by FINSEQ_1:8;
       defpred PSumx[Nat] means $1 < len x implies sumx.$1 = 0.;
K5:    PSumx[0] by K4;
K6:    for m be Nat st PSumx[m] holds PSumx[m+1]
       proof
        let m be Nat;
        assume
K61:    PSumx[m];
        assume
K62:    m+1 < len x;
K63a:   m <= m+1 by NAT_1:29; then
K63:    m < len x by K62,XREAL_1:2;
K65:    1 <= m+1 & m+1 + 1 <= len x by K62,NAT_1:29,38; then
        m+1 in Seg len x by K62,FINSEQ_1:3; then
K66:    m+1 in dom F by B5,FINSEQ_1:def 3;
        m+1 <= n by K9,K62,NAT_1:38; then
        m+1 in Seg n by K65,FINSEQ_1:3; then
        m+1 in dom F /\ Seg n by K66,XBOOLE_0:def 3; then
K67:    m+1 in dom F1 by FUNCT_1:68;
        sumx.(m+1) = sumx.m + x.(m+1) by K63,K4
                  .= 0. + x.(m+1) by K63a,K62,XREAL_1:2,K61
                  .= x.(m+1) by SUPINF_2:18;
        hence thesis by K67,K3;
       end;
K7:    for m be Nat holds PSumx[m] from NAT_1:sch 1(K5,K6);
K8:    n < len x by K9,NAT_1:38;
SUM1:  Sum x = sumx.(len x) by K4
            .= sumx.(n+1) by K9a,FINSEQ_1:8
            .= sumx.n + x.(n+1) by K8,K4
            .= 0. + x.(n+1) by K8,K7
            .= x.(n+1) by SUPINF_2:18;
       1 <= n+1 & n+1 <= len F by B7,NAT_1:29; then
N1:    n+1 in dom F by FINSEQ_3:27;
U1:    union rng F = (union rng F1) \/ F.(n+1)
       proof
        thus union rng F c= (union rng F1) \/ F.(n+1)
        proof
         let v be set;
         assume v in union rng F; then
         consider A be set such that
U2:      v in A & A in rng F by TARSKI:def 4;
         consider i be set such that
U3:      i in dom F & A = F.i by U2,FUNCT_1:def 5;
         reconsider i as Nat by U3;
         i in Seg len F by U3,FINSEQ_1:def 3; then
U4:      1 <= i & i <= n+1 by B7,FINSEQ_1:3;
         per cases;
          suppose i = n+1;
           hence thesis by U2,U3,XBOOLE_0:def 2;
          end;
          suppose i <> n+1; then
           i < n+1 by U4,REAL_1:def 5; then
           i <= n by NAT_1:38; then
           i in Seg n by U4,FINSEQ_1:3; then
           i in dom F /\ Seg n by U3,XBOOLE_0:def 3; then
           i in dom F1 by FUNCT_1:68; then
           F1.i = A & F1.i in rng F1 by U3,FUNCT_1:12,70; then
           v in union rng F1 by U2,TARSKI:def 4;
           hence thesis by XBOOLE_0:def 2;
          end;
        end;
         let v be set;
         assume
U6:      v in (union rng F1) \/ F.(n+1);
         per cases by U6,XBOOLE_0:def 2;
          suppose v in union rng F1; then
           consider A be set such that
U7:        v in A & A in rng F1 by TARSKI:def 4;
           consider i be set such that
U8:        i in dom F1 & A = F1.i by U7,FUNCT_1:def 5;
           i in dom F /\ Seg n by U8,FUNCT_1:68; then
U9:        i in dom F by XBOOLE_0:def 3;
           F.i = A by U8,FUNCT_1:70; then
           A in rng F by U9,FUNCT_1:12;
           hence v in union rng F by U7,TARSKI:def 4;
          end;
          suppose
U10:       v in F.(n+1);
           F.(n+1) in rng F by N1,FUNCT_1:12;
           hence v in union rng F by U10,TARSKI:def 4;
          end;
       end;
       now per cases;
        case
NONZERO: a.(n+1) <> 0.;
         defpred PG[Nat,set] means
         ($1 = 1 implies $2 = union rng F1) &
         ($1 = 2 implies $2 = F.(n+1));
PG:      for k be Nat st k in Seg 2
          ex x being Element of S st PG[k,x]
         proof
          let k be Nat;
          assume
PG1:      k in Seg 2;
          per cases by PG1,FINSEQ_1:4,TARSKI:def 2;
           suppose
PG2:        k = 1;
            set x = union rng F1;
            reconsider x as Element of S by MESFUNC2:34;
            take x;
            thus (k = 1 implies x = union rng F1) &
             (k = 2 implies x = F.(n+1)) by PG2;
           end;
           suppose
PG3:        k = 2;
            set x = F.(n+1);
            F.(n+1) in rng F & rng F c= S by N1,FUNCT_1:12,FINSEQ_1:def 4; then
            reconsider x as Element of S;
            take x;
            thus (k = 1 implies x = union rng F1) &
             (k = 2 implies x = F.(n+1)) by PG3;
           end;
         end;
         consider G be FinSequence of S such that
G1:      dom G = Seg 2 &
         for k be Nat st k in Seg 2 holds PG[k,G.k] from FINSEQ_1:sch 5(PG);
G2a:     Seg len G = dom G by FINSEQ_1:def 3
                  .= Seg 2 by G1; then
G2:      len G = 2 by FINSEQ_1:8;
         1 in Seg 2 by FINSEQ_1:4,TARSKI:def 2;then
G3:      G.1 = union rng F1 by G1;
         2 in Seg 2 by FINSEQ_1:4,TARSKI:def 2;then
G4:      G.2 = F.(n+1) by G1;
RNGG1:   G = <* union rng F1, F.(n+1) *> by G2,G3,G4,FINSEQ_1:61;
         defpred Pb[Nat,set] means
         ($1 = 1 implies $2 = 0.) &
         ($1 = 2 implies $2 = a.(n+1));
Pb:      for k be Nat st k in Seg 2
          ex x being Element of ExtREAL st Pb[k,x]
         proof
          let k be Nat;
          assume
Pb1:      k in Seg 2;
          per cases by Pb1,FINSEQ_1:4,TARSKI:def 2;
           suppose
Pb2:        k = 1;
            set x = 0.;
            reconsider x as Element of ExtREAL;
            take x;
            thus (k = 1 implies x = 0.) &
             (k = 2 implies x = a.(n+1)) by Pb2;
           end;
           suppose
Pb3:        k = 2;
            set x = a.(n+1);
            reconsider x as Element of ExtREAL;
            take x;
            thus (k = 1 implies x = 0.) &
             (k = 2 implies x = a.(n+1)) by Pb3;
           end;
         end;
         consider b be FinSequence of ExtREAL such that
b1:      dom b = Seg 2 &
         for k be Nat st k in Seg 2 holds Pb[k,b.k] from FINSEQ_1:sch 5(Pb);
b2a:     Seg len b = dom b by FINSEQ_1:def 3
                  .= Seg 2 by b1;
         1 in Seg 2 by FINSEQ_1:4,TARSKI:def 2;then
b3:      b.1 = 0. by b1;
         2 in Seg 2 by FINSEQ_1:4,TARSKI:def 2; then
b4:      b.2 = a.(n+1) by b1;
         deffunc Fy(Nat) = b.$1*(M*G).$1;
         consider y be FinSequence of ExtREAL such that
y1:      len y = 2 &
         for m be Nat st m in Seg 2 holds y.m = Fy(m) from FINSEQ_2:sch 1;
         1 in Seg 2 by FINSEQ_1:4,TARSKI:def 2; then
         y.1 = b.1*(M*G).1 by y1;then
y2:      y.1 = 0. by b3,EXTREAL1:22;
y3:      y.2 = x.(n+1)
         proof
y4:       2 in Seg 2 by FINSEQ_1:4,TARSKI:def 2; then
y5:       (M*G).2 = M.(G.2) by G1,FUNCT_1:23
                 .= M.(F.(n+1)) by G4
                 .= (M*F).(n+1) by N1,FUNCT_1:23;
          y.2 = b.2*(M*G).2 by y1,y4
             .= a.(n+1)*(M*F).(n+1) by b4,y5;
          hence thesis by B5,B6,N1;
         end;
         consider sumy be Function of NAT,ExtREAL such that
SUMY:     Sum y = sumy.len y & sumy.0 = 0. &
          for k be Nat st k < len y holds sumy.(k+1) = sumy.k + y.(k+1)
           by CONVFUN1:def 5;
SUMY1:   sumy.1 = sumy.0 + y.(0+1) by SUMY,y1
               .= 0. + y.1 by SUMY
               .= 0. by y2,SUPINF_2:18;
SUM2:    Sum y = sumy.2 by SUMY,y1
               .= sumy.(1+1)
               .= sumy.1 + y.(1+1) by SUMY,y1
               .= 0. + x.(n+1) by SUMY1,y3
               .= x.(n+1) by SUPINF_2:18;
         for u,v be set st u <> v holds G.u misses G.v
         proof
          let u,v be set;
          assume
DJ1:      u <> v;
          per cases;
           suppose u in dom G & v in dom G; then
DJ3:        (u = 1 or u = 2) & (v = 1 or v = 2) by G1,FINSEQ_1:4,TARSKI:def 2;
            per cases by DJ1,DJ3;
             suppose
DJ4:          u = 1 & v = 2;
              assume G.u meets G.v; then
               consider z be set such that
DJ5:           z in G.u & z in G.v by XBOOLE_0:3;
               consider A be set such that
DJ6:           z in A & A in rng F1 by DJ4,G3,DJ5,TARSKI:def 4;
               consider k' be set such that
DJ7:           k' in dom F1 & A = F1.k' by DJ6,FUNCT_1:def 5;
               reconsider k' as Nat by DJ7;
               k' in dom F /\ Seg n by DJ7,FUNCT_1:68; then
               k' in dom F & k' in Seg n by XBOOLE_0:def 3; then
               k' <= n by FINSEQ_1:3; then
               k' < n+1 by NAT_1:38; then
DJ8:           F.k' misses F.(n+1) by PROB_2:def 3;
               z in F.k' & z in F.(n+1) by DJ6,DJ5,DJ4,G4,DJ7,FUNCT_1:70;
               hence contradiction by DJ8,XBOOLE_0:3;
             end;
             suppose
DJ4:          u = 2 & v = 1;
              assume G.u meets G.v; then
              consider z be set such that
DJ5:          z in G.u & z in G.v by XBOOLE_0:3;
              consider A be set such that
DJ6:          z in A & A in rng F1 by DJ4,G3,DJ5,TARSKI:def 4;
              consider k' be set such that
DJ7:          k' in dom F1 & A = F1.k' by DJ6,FUNCT_1:def 5;
              reconsider k' as Nat by DJ7;
              k' in dom F /\ Seg n by DJ7,FUNCT_1:68; then
              k' in dom F & k' in Seg n by XBOOLE_0:def 3; then
              k' <= n by FINSEQ_1:3; then
              k' < n+1 by NAT_1:38; then
DJ8:          F.k' misses F.(n+1) by PROB_2:def 3;
              z in F.k' & z in F.(n+1) by DJ6,DJ5,DJ4,G4,DJ7,FUNCT_1:70;
              hence contradiction by DJ8,XBOOLE_0:3;
             end;
           end;
           suppose not u in dom G or not v in dom G; then
            G.u = {} or G.v = {} by FUNCT_1:def 4;
            hence thesis by XBOOLE_1:65;
           end;
         end; then
         reconsider G as Finite_Sep_Sequence of S by PROB_2:def 3;
X1:      dom f = union rng F by B4,MESFUNC3:def 1
              .= (union rng F1) \/ F.(n+1) by U1
              .= union {union rng F1,F.(n+1)} by ZFMISC_1:93
              .= union rng G by RNGG1,FINSEQ_2:147;
X2:      dom G = Seg len G by FINSEQ_1:def 3
              .= Seg 2 by G2a
              .= Seg len b by b2a
              .= dom b by FINSEQ_1:def 3;
         G,b are_Re-presentation_of f &
         b.1 = 0. &
         (for m be Nat st 2 <= m & m in dom b holds
         0. <' b.m & b.m <' +infty) & dom y = dom G &
         (for m be Nat st m in dom y holds y.m = b.m*(M*G).m)
         proof
Y1:       for k be Nat st k in dom G
           for v be set st v in G.k holds f.v = b.k
          proof
           let k be Nat;
           assume
X3:        k in dom G;
           let v be set;
           assume
X4:        v in G.k;
           per cases by X3,G1,FINSEQ_1:4,TARSKI:def 2;
            suppose k = 1;
             hence thesis by G3,X4,K1a,B10;
            end;
            suppose k = 2;
             hence thesis by G4,X4,b4,N1,B4,MESFUNC3:def 1;
            end;
          end;
          hence G,b are_Re-presentation_of f by X1,X2,MESFUNC3:def 1;
          thus b.1 = 0. by b3;
          thus for k be Nat st 2 <= k & k in dom b holds
            0. <' b.k & b.k <' +infty
          proof
           let k be Nat;
           assume
X8:        2 <= k & k in dom b; then
X10:       k = 1 or k = 2 by b1,FINSEQ_1:4,TARSKI:def 2;
X14:       union rng F = {} \/ F.(n+1) by K1a,B10,U1
                      .= F.(n+1); then
           G.k <> {} by X10,X8,G4,B4,B2,MESFUNC3:def 1; then
           consider v be set such that
X12:       v in G.k by XBOOLE_0:def 1;
X15:       f.v = b.k by Y1,X12,X8,G1,b1;
X16:       v in dom f by X12,X10,X8,G4,X14,B4,MESFUNC3:def 1; then
           0. <=' b.k & b.k <> 0. by B3,X15,X10,X8,b4,NONZERO;
           hence 0. <' b.k by SUPINF_1:22;
           dom f c= X by RELSET_1:12; then
           reconsider v as Element of X by X16;
           |. f.v .| <' +infty by B8,X16,MESFUNC2:def 1;
           hence b.k <' +infty by X15,EXTREAL2:58;
          end;
          thus dom y = Seg len y by FINSEQ_1:def 3 .= Seg 2 by y1
                    .= Seg len G by G2a .= dom G by FINSEQ_1:def 3;
          thus for k be Nat st k in dom y holds y.k = b.k*(M*G).k
          proof
           let k be Nat;
           assume k in dom y; then
           k in Seg len y by FINSEQ_1:def 3;
           hence thesis by y1;
          end;
         end;
         hence integral(X,S,M,f) = Sum(y) by B1,B2,B3,MESFUNC3:def 2
           .= x.(n+1) by SUM2
           .= Sum(x) by SUM1;
        end;
        case
ZERO:    a.(n+1) = 0.;
         1 <= n+1 & n+1 <= len x by NAT_1:29,K9a,FINSEQ_1:8; then
         n+1 in dom x by FINSEQ_3:27; then
XN1:     x.(n+1) = a.(n+1)*(M*F).(n+1) by B6 .= 0. by ZERO,EXTREAL1:22;
         defpred PG[Nat,set] means
         ($1 = 1 implies $2 = union rng F) &
         ($1 = 2 implies $2 = {});
PG:      for k be Nat st k in Seg 2
          ex v being Element of S st PG[k,v]
         proof
          let k be Nat;
          assume
PG1:      k in Seg 2;
          per cases by PG1,FINSEQ_1:4,TARSKI:def 2;
           suppose
PG2:        k = 1;
            set v = union rng F;
            reconsider v as Element of S by MESFUNC2:34;
            take v;
            thus (k = 1 implies v = union rng F) &
             (k = 2 implies v = {}) by PG2;
           end;
           suppose
PG3:        k = 2;
            reconsider v = {} as Element of S by MEASURE1:45;
            take v;
            thus (k = 1 implies v = union rng F) &
             (k = 2 implies v = {}) by PG3;
           end;
         end;
         consider G be FinSequence of S such that
G1:      dom G = Seg 2 &
         for k be Nat st k in Seg 2 holds PG[k,G.k] from FINSEQ_1:sch 5(PG);
G2a:     1 in Seg 2 & 2 in dom G & 2 in Seg 2
           by G1,FINSEQ_1:4,TARSKI:def 2; then
G2:      G.1 = union rng F & G.2 = {} & M.(G.2) = (M*G).2
           by G1,FUNCT_1:23; then
         reconsider EMPTY = G.2 as Element of S by MEASURE1:45;
G3:      (M*G).2 = 0. by G2,MEASURE1:def 11;
         defpred Pb[Nat,set] means
         ($1 = 1 implies $2 = 0.) &
         ($1 = 2 implies $2 = 1.);
Pb:      for k be Nat st k in Seg 2
          ex v be Element of ExtREAL st Pb[k,v]
         proof
          let k be Nat;
          assume
Pb1:      k in Seg 2;
          per cases by Pb1,FINSEQ_1:4,TARSKI:def 2;
           suppose
Pb2:        k = 1;
            set v = 0.;
            reconsider v as Element of ExtREAL;
            take v;
            thus (k = 1 implies v = 0.) & (k = 2 implies v = 1.) by Pb2;
           end;
           suppose
Pb3:        k = 2;
            set v = 1.;
            reconsider v as Element of ExtREAL;
            take v;
            thus (k = 1 implies v = 0.) & (k = 2 implies v = 1.) by Pb3;
           end;
         end;
         consider b be FinSequence of ExtREAL such that
b1:       dom b = Seg 2 &
          for k be Nat st k in Seg 2 holds Pb[k,b.k] from FINSEQ_1:sch 5(Pb);
b3:      1 in Seg 2 & 2 in Seg 2 by FINSEQ_1:4,TARSKI:def 2; then
b3a:     b.1 = 0. & b.2 = 1. by b1;
         deffunc Fy(Nat) = b.$1*(M*G).$1;
         consider y be FinSequence of ExtREAL such that
y1:      len y = 2 &
         for k be Nat st k in Seg 2 holds y.k = Fy(k) from FINSEQ_2:sch 1;
y2:      y.1 = 0. & y.2 = 0.
         proof
          1 in Seg 2 by FINSEQ_1:4,TARSKI:def 2; then
          y.1 = b.1*(M*G).1 by y1;
          hence y.1 = 0. by b3a,EXTREAL1:22;
          2 in Seg 2 by FINSEQ_1:4,TARSKI:def 2; then
          y.2 = b.2*(M*G).2 by y1;
          hence y.2 = 0. by G3,EXTREAL1:22;
         end;
         consider sumy be Function of NAT,ExtREAL such that
SUMY:     Sum(y) = sumy.len y & sumy.0 = 0. &
          for k be Nat st k < len y holds sumy.(k+1) = sumy.k + y.(k+1)
           by CONVFUN1:def 5;
SUMY1:   sumy.1 = sumy.0 + y.(0+1) by SUMY,y1
               .= 0. + y.1 by SUMY
               .= 0. by y2,SUPINF_2:18;
SUM3:    Sum y = sumy.2 by SUMY,y1
              .= sumy.1 + y.(1+1) by SUMY,y1
              .= 0. + 0. by y2,SUMY1
              .= 0. by SUPINF_2:18;
         for u,v be set st u <> v holds G.u misses G.v
         proof
          let u,v be set;
          assume
DJ1:      u <> v;
          per cases;
           suppose
DJ2:        u in dom G & v in dom G; then
DJ3:        (u = 1 or u = 2) & (v = 1 or v = 2) by G1,FINSEQ_1:4,TARSKI:def 2;
            per cases by DJ1,DJ3;
             suppose u = 1 & v = 2; then
              G.u = union rng F & G.v = {} by DJ2,G1;
              hence thesis by XBOOLE_1:65;
             end;
             suppose u = 2 & v = 1; then
              G.v = union rng F & G.u = {} by DJ2,G1;
              hence thesis by XBOOLE_1:65;
             end;
           end;
           suppose not u in dom G or not v in dom G; then
            G.u = {} or G.v = {} by FUNCT_1:def 4;
            hence thesis by XBOOLE_1:65;
           end;
         end; then
         reconsider G as Finite_Sep_Sequence of S by PROB_2:def 3;
         G,b are_Re-presentation_of f & b.1 = 0. &
         (for m be Nat st 2 <= m & m in dom b holds
           0. <' b.m & b.m <' +infty) & dom y = dom G &
         (for m be Nat st m in dom y holds y.m = b.m*(M*G).m)
         proof
          Seg len G = dom G by FINSEQ_1:def 3 .= Seg 2 by G1; then
          len G = 2 by FINSEQ_1:8; then
          G = <* union rng F, {} *> by G2,FINSEQ_1:61; then
          rng G = {union rng F, {}} by FINSEQ_2:147; then
X1:       union rng G = union rng F \/ {} by ZFMISC_1:93
                     .= union rng F
                     .= dom f by B4,MESFUNC3:def 1;
          for k be Nat st k in dom G
           for v be set st v in G.k holds f.v=b.k
          proof
           let k be Nat;
           assume
X3a:       k in dom G;
           let v be set;
           assume
X4:        v in G.k;
           per cases by X3a,G1,FINSEQ_1:4,TARSKI:def 2;
            suppose
X5:          k = 1; then
             f.v = a.(n+1) by G2,U1,K1a,B10,X4,N1,B4,MESFUNC3:def 1
                .= 0. by ZERO;
             hence thesis by b3,b1,X5;
            end;
            suppose k = 2;
             hence thesis by G2a,G1,X4;
            end;
          end;
          hence G,b are_Re-presentation_of f by X1,G1,b1,MESFUNC3:def 1;
          thus b.1 = 0. by b3,b1;
          thus for k be Nat st 2 <= k & k in dom b holds
            0. <' b.k & b.k <' +infty
          proof
           let k be Nat;
           assume 2 <= k & k in dom b; then
           2 <= k & (k = 1 or k = 2) by b1,FINSEQ_1:4,TARSKI:def 2;
           hence thesis by b3a,MESFUNC1:def 8,EXTREAL1:18,EXTREAL2:20;
          end;
          thus dom y = Seg len y by FINSEQ_1:def 3 .= Seg 2 by y1
                    .= dom G by G1;
          thus for k be Nat st k in dom y holds y.k = b.k*(M*G).k
          proof
           let k be Nat;
           assume k in dom y; then
           k in Seg len y by FINSEQ_1:def 3;
           hence thesis by y1;
          end;
         end;
         hence integral(X,S,M,f)
            = Sum y by B1,B2,B3,MESFUNC3:def 2
           .= x.(n+1) by XN1,SUM3
           .= Sum(x) by SUM1;
        end;
       end;
       hence integral(X,S,M,f) = Sum(x);
      end;
      suppose
C2:    dom f1 <> {};
C6:    for i be Nat st i in dom x1 holds x1.i=a1.i*(M*F1).i
       proof
        let i be Nat;
        assume
C61:    i in dom x1;
C62:    dom x1 c= dom x & dom a1 c= dom a by FUNCT_1:76; then
        x.i = a.i * (M*F).i by B6,C61; then
C63:    x1.i = a.i * (M*F).i by C61,FUNCT_1:68
            .= a1.i * (M*F).i by C32,C5,C61,FUNCT_1:68;
        (M*F).i = M.(F.i) by C62,C61,B5,FUNCT_1:23
               .= M.(F1.i) by C5,C61,FUNCT_1:68
               .= (M*F1).i by C5,C61,FUNCT_1:23;
        hence thesis by C63;
       end;
C64a:  dom F = Seg len F by FINSEQ_1:def 3 .= Seg (n+1) by B7; then
C65:   dom F1 = (Seg (n+1)) /\ Seg n by FUNCT_1:68;
       n <= n+1 by NAT_1:29; then
       Seg n c= Seg (n+1) by FINSEQ_1:7; then
C7a:   dom F1 = Seg n by C65,XBOOLE_1:28; then
C7:    len F1 = n by FINSEQ_1:def 3;
       consider G be Finite_Sep_Sequence of S,
       b,y be FinSequence of ExtREAL such that
E1:    G,b are_Re-presentation_of f1 and
E2:    b.1 =0. and
E3:    for n be Nat st 2 <= n & n in dom b holds
        0. <' b.n & b.n <' +infty and
E4:    dom y = dom G and
E5:    for n be Nat st n in dom y holds y.n=b.n*(M*G).n and
E6:    integral(X,S,M,f1)=Sum(y) by MESFUNC3:def 2,C1,C2,C3;
       now per cases;
        case
CC1:     F.(n+1) = {} or a.(n+1) = 0.;
         defpred PH[Nat,set] means
          ($1 = 1 implies $2 = G.1 \/ F.(n+1)) &
          (2 <= $1 implies $2 = G.$1);
G21:     for k be Nat st k in Seg len G ex x be Element of S st PH[k,x]
         proof
          let k be Nat;
          assume k in Seg len G; then
          k in dom G by FINSEQ_1:def 3; then
H2:       G.k in rng G by FUNCT_1:12;
H4:       rng G c= S by FINSEQ_1:def 4;
          per cases;
           suppose
H3:         k = 1;
            1 <= n+1 by NAT_1:29; then
            n+1 in dom F by B7,FINSEQ_3:27; then
H5:         F.(n+1) in rng F by FUNCT_1:12;
H6:         rng F c= S by FINSEQ_1:def 4;
            set x = G.1 \/ F.(n+1);
            reconsider x as Element of S by H2,H3,H4,H5,H6,MEASURE1:46;
            PH[k,x] by H3;
            hence thesis;
           end;
           suppose
H7:         k <> 1;
            set x = G.k;
            reconsider x as Element of S by H2,H4;
            PH[k,x] by H7;
            hence thesis;
           end;
         end;
         consider H be FinSequence of S such that
G22:     dom H = Seg len G &
         for k be Nat st k in Seg len G holds PH[k,H.k]
           from FINSEQ_1:sch 5(G21);
H8:      union rng G <> {} by C2,E1,MESFUNC3:def 1;
         dom G <> {}
         proof
          assume dom G = {}; then
          G = {} by FINSEQ_1:26;
          hence contradiction by H8,FINSEQ_1:27,ZFMISC_1:2;
         end; then
         G <> {} by FINSEQ_1:26; then
         len G <> 0 by FINSEQ_1:25; then
         0 < len G; then
         0+1 <= len G by NAT_1:38; then
G1a:     1 in Seg len G by FINSEQ_1:3;
         deffunc Z(Nat) = b.$1 * (M*H).$1;
         consider z be FinSequence of ExtREAL such that
H9:      len z = len y &
         for n be Nat st n in Seg len y holds z.n = Z(n)
           from FINSEQ_2:sch 1;
G3:      dom z = dom y by H9,FINSEQ_3:31;
G4:      for n be Nat st n in dom z holds z.n=b.n*(M*H).n
         proof
          let n be Nat;
          assume n in dom z; then
          n in Seg len y by H9,FINSEQ_1:def 3;
          hence thesis by H9;
         end;
         for u,v be set st u <> v holds H.u misses H.v
         proof
          let u,v be set;
          assume
G40:      u <> v;
          per cases;
           suppose
G41a:       u in dom H & v in dom H; then
            reconsider u1=u,v1=v as Nat;
            per cases;
             suppose
G42:          u = 1; then
G42a:         H.u = G.1 \/ F.(n+1) by G41a,G22;
              1 <= v1 by G41a,G22,FINSEQ_1:3; then
              1 < v1 by G40,G42,REAL_1:def 5; then
G43:          1+1 <= v1 by NAT_1:38; then
G44:          H.v = G.v by G22,G41a;
G44a:         G.1 misses G.v by G40,G42,PROB_2:def 3;
              F.(n+1) misses G.v
              proof
               per cases by CC1;
                suppose F.(n+1) = {};
                 hence thesis by XBOOLE_1:65;
                end;
                suppose
G45a:            a.(n+1) = 0.;
                 assume F.(n+1) meets G.v; then
                  consider w be set such that
G45:              w in F.(n+1) & w in G.v by XBOOLE_0:3;
                  1 <= n+1 by NAT_1:29; then
                  n+1 in Seg (n+1) by FINSEQ_1:3; then
                  n+1 in dom F by B7,FINSEQ_1:def 3; then
G47:              f.w = a.(n+1) by B4,G45,MESFUNC3:def 1 .= 0. by G45a;
G46:              v1 in dom G & w in G.v1 by G45,G41a,G22,FINSEQ_1:def 3; then
                  G.v1 in rng G by FUNCT_1:12; then
                  w in union rng G by G45,TARSKI:def 4; then
                  w in dom f1 by E1,MESFUNC3:def 1; then
G48:              f.w = f1.w by FUNCT_1:70 .= b.v1
                    by E1,G46,MESFUNC3:def 1;
                  v1 in dom b by G46,E1,MESFUNC3:def 1;
                  hence contradiction by E3,G43,G47,G48;
               end;
              end;
              hence H.u misses H.v by G42a,G44,G44a,XBOOLE_1:70;
             end;
             suppose
A:            u <> 1;
              1 <= u1 by G41a,G22,FINSEQ_1:3; then
              1 < u1 by A,REAL_1:def 5; then
G51:          1+1 <= u1 by NAT_1:38; then
G52:          H.u = G.u by G22,G41a;
              per cases;
               suppose
G49:            v = 1; then
G50:            H.v = G.1 \/ F.(n+1) by G41a,G22;
G53:            G.1 misses G.u by G40,G49,PROB_2:def 3;
                F.(n+1) misses G.u
                proof
                 per cases by CC1;
                  suppose F.(n+1) = {};
                   hence thesis by XBOOLE_1:65;
                  end;
                  suppose
G54a:              a.(n+1) = 0.;
                   assume F.(n+1) meets G.u; then
                    consider w be set such that
G54:                w in F.(n+1) & w in G.u by XBOOLE_0:3;
                    1 <= n+1 by NAT_1:29; then
                    n+1 in Seg (n+1) by FINSEQ_1:3; then
                    n+1 in dom F by B7,FINSEQ_1:def 3; then
G55:                f.w = a.(n+1) by B4,G54,MESFUNC3:def 1 .= 0. by G54a;
G56:                u1 in dom G & w in G.u1
                      by G54,G41a,G22,FINSEQ_1:def 3; then
                    G.u1 in rng G by FUNCT_1:12; then
                    w in union rng G by G54,TARSKI:def 4; then
                    w in dom f1 by E1,MESFUNC3:def 1; then
G57:                f.w = f1.w by FUNCT_1:70 .= b.u1
                     by E1,G56,MESFUNC3:def 1;
                    u1 in dom b by G56,E1,MESFUNC3:def 1;
                    hence contradiction by E3,G51,G55,G57;
                  end;
                end;
                hence H.u misses H.v by G50,G52,G53,XBOOLE_1:70;
               end;
               suppose
B:              v <> 1;
                1 <= v1 by G41a,G22,FINSEQ_1:3; then
                1 < v1 by B,REAL_1:def 5; then
                1+1 <= v1 by NAT_1:38; then
                H.v = G.v by G41a,G22;
                hence H.u misses H.v by G40,G52,PROB_2:def 3;
               end;
             end;
           end;
           suppose not u in dom H or not v in dom H; then
            H.u = {} or H.v = {} by FUNCT_1:def 4;
            hence H.u misses H.v by XBOOLE_1:65;
           end;
         end; then
         reconsider H as Finite_Sep_Sequence of S by PROB_2:def 3;
H2a:     dom f = union rng H
         proof
          thus dom f c= union rng H
          proof
           let v be set;
           assume v in dom f; then
           v in union rng F by B4,MESFUNC3:def 1; then
           consider A be set such that
L2:         v in A & A in rng F by TARSKI:def 4;
           consider u be set such that
L3:         u in dom F & A = F.u by L2,FUNCT_1:def 5;
           reconsider u as Nat by L3;
           u in Seg len F by L3,FINSEQ_1:def 3; then
L4:        1 <= u & u <= n+1 by B7,FINSEQ_1:3;
           per cases;
            suppose u = n+1; then
             1 in dom H & H.1 = G.1 \/ A by L3,G1a,G22; then
             v in H.1 & H.1 in rng H by L2,XBOOLE_0:def 2,FUNCT_1:12;
             hence thesis by TARSKI:def 4;
            end;
            suppose u <> n+1; then
             u < n+1 by L4,REAL_1:def 5; then
             u <= n by NAT_1:38; then
L7:          u in Seg n by L4,FINSEQ_1:3; then
L8:          F1.u in rng F1 by C7a,FUNCT_1:12;
             A = F1.u by L3,L7,C7a,FUNCT_1:70; then
             v in union rng F1 by L2,L8,TARSKI:def 4; then
             v in union rng G by B10,E1,MESFUNC3:def 1; then
             consider B be set such that
L9:           v in B & B in rng G by TARSKI:def 4;
             consider w be set such that
L10:          w in dom G & B = G.w by L9,FUNCT_1:def 5;
             reconsider w as Nat by L10;
L11:         w in Seg len G by L10,FINSEQ_1:def 3;
             per cases;
              suppose
L12:           w = 1;
               H.1 = G.1 \/ F.(n+1) by G1a,G22; then
L13:           v in H.1 by L9,L10,L12,XBOOLE_0:def 2;
               H.1 in rng H by G1a,G22,FUNCT_1:12;
               hence thesis by L13,TARSKI:def 4;
              end;
              suppose
L14:           w <> 1;
               1 <= w & w <= len G by L10,FINSEQ_3:27; then
               1 < w by L14,REAL_1:def 5; then
               1+1 <= w by NAT_1:38; then
L15:           v in H.w by L9,L10,L11,G22;
               H.w in rng H by L11,G22,FUNCT_1:12;
               hence thesis by L15,TARSKI:def 4;
              end;
            end;
          end;
           let v be set;
           assume v in union rng H; then
           consider A be set such that
L17:       v in A & A in rng H by TARSKI:def 4;
           consider k be set such that
L18:       k in dom H & A = H.k by L17,FUNCT_1:def 5;
           reconsider k as Nat by L18;
           per cases;
            suppose k = 1; then
L20:         H.k = G.1 \/ F.(n+1) by L18,G22;
             per cases by L20,L17,L18,XBOOLE_0:def 2;
              suppose
L21:           v in G.1;
               1 in dom G by G1a,FINSEQ_1:def 3; then
               G.1 in rng G by FUNCT_1:12; then
               v in union rng G by L21,TARSKI:def 4; then
               v in dom f1 by E1,MESFUNC3:def 1; then
               v in dom f /\ (union rng F1) by FUNCT_1:68;
               hence thesis by XBOOLE_0:def 3;
              end;
              suppose
L22:           v in F.(n+1);
               1 <= n+1 & n+1 <= len F by B7,NAT_1:29; then
               n+1 in dom F by FINSEQ_3:27; then
               F.(n+1) in rng F by FUNCT_1:12; then
               v in union rng F by L22,TARSKI:def 4;
               hence thesis by B4,MESFUNC3:def 1;
              end;
            end;
            suppose
L23:         k <> 1;
             1 <= k by L18,G22,FINSEQ_1:3; then
             1 < k by L23,REAL_1:def 5; then
             1+1 <= k by NAT_1:38; then
L24:         v in G.k by L17,L18,G22;
             k in dom G by L18,G22,FINSEQ_1:def 3; then
             G.k in rng G by FUNCT_1:12; then
             v in union rng G by L24,TARSKI:def 4; then
             v in dom f1 by E1,MESFUNC3:def 1; then
             v in dom f /\ (union rng F1) by FUNCT_1:68;
             hence thesis by XBOOLE_0:def 3;
            end;
         end;
H2b:     dom H = Seg len G by G22 .= dom G by FINSEQ_1:def 3
              .= dom b by E1,MESFUNC3:def 1;
H2c:     for m be Nat st m in dom H
          for x be set st x in H.m holds f.x = b.m
         proof
          let m be Nat;
          assume
L1:       m in dom H;
          let x be set;
           assume
L2:        x in H.m;
           per cases;
           suppose
L3:         m = 1; then
L4:         H.m = G.1 \/ F.(n+1) by L1,G22;
             per cases;
              suppose F.(n+1) = {}; then
L5:            x in G.1 by L3,L2,L1,G22;
L6:            m in dom G by L1,G22,FINSEQ_1:def 3; then
L8:            f1.x = b.m by L3,L5,E1,MESFUNC3:def 1;
               G.1 in rng G by L3,L6,FUNCT_1:12; then
               x in union rng G by L5,TARSKI:def 4; then
               x in dom f1 by E1,MESFUNC3:def 1;
               hence f.x = b.m by L8,FUNCT_1:70;
              end;
              suppose
M1:            F.(n+1) <> {};
               per cases by L4,L2,XBOOLE_0:def 2;
                suppose
L5:              x in G.1;
L6:              m in dom G by L1,G22,FINSEQ_1:def 3; then
L8:              f1.x = b.m by L3,L5,E1,MESFUNC3:def 1;
                 G.1 in rng G by L3,L6,FUNCT_1:12; then
                 x in union rng G by L5,TARSKI:def 4; then
                 x in dom f1 by E1,MESFUNC3:def 1;
                 hence f.x = b.m by L8,FUNCT_1:70;
                end;
                suppose
L9:              x in F.(n+1);
                 1 <= n+1 & n+1 <= len F by B7,NAT_1:29; then
                 n+1 in dom F by FINSEQ_3:27; then
                 f.x = a.(n+1) by L9,B4,MESFUNC3:def 1 .= 0. by CC1,M1;
                 hence f.x = b.m by L3,E2;
                end;
              end;
            end;
            suppose
L10:         m <> 1;
L11:         m in dom G by L1,G22,FINSEQ_1:def 3;then
             1 <= m by FINSEQ_3:27; then
             1 < m by L10,REAL_1:def 5; then
             1+1 <= m by NAT_1:38; then
L13:         H.m = G.m by L1,G22; then
L12:         f1.x = b.m by L2,L11,E1,MESFUNC3:def 1;
             G.m in rng G by L11,FUNCT_1:12; then
             x in union rng G by L2,L13,TARSKI:def 4; then
             x in dom f1 by E1,MESFUNC3:def 1;
             hence f.x = b.m by L12,FUNCT_1:70;
            end;
         end;
H2:      H,b are_Re-presentation_of f by H2a,H2b,H2c,MESFUNC3:def 1;
H3:      dom z = dom y by G3 .=dom G by E4 .=dom H by G22,FINSEQ_1:def 3;
H5:      for k be Nat st k in dom z holds z.k = y.k
         proof
          let k be Nat;
          assume
H5a:      k in dom z; then
          k in Seg len y by H9,FINSEQ_1:def 3; then
H5b:      z.k = b.k*(M*H).k by H9;
H5c:      y.k = b.k*(M*G).k by E5,H5a,G3;
          per cases;
           suppose
H51:        k = 1; then
            z.k = 0. by E2,H5b,EXTREAL1:22;
            hence thesis by H51,E2,H5c,EXTREAL1:22;
           end;
           suppose
H52:        k <> 1;
H53:        k in Seg len G by E4,H5a,G3,FINSEQ_1:def 3; then
            1 <= k by FINSEQ_1:3; then
            1 < k by H52,REAL_1:def 5; then
H54:        1+1 <= k by NAT_1:38;
            (M*H).k = M.(H.k) by H5a,H3,FUNCT_1:23
                   .= M.(G.k) by H53,G22,H54
                   .= (M*G).k by E4,H5a,G3,FUNCT_1:23;
            hence thesis by H5b,E5,H5a,G3;
           end;
         end;
         1 <= n+1 & n+1 <= len F by B7,NAT_1:29; then
H6b:     n+1 in dom F by FINSEQ_3:27;
H6:      x.(n+1) = 0.
         proof
          per cases by CC1;
           suppose
P1:         F.(n+1) = {};
H6c:        (M*F).(n+1) = M.(F.(n+1)) by H6b,FUNCT_1:23;
            reconsider EMPTY = {} as Element of S by MEASURE1:45;
H6d:        (M*F).(n+1) = 0. by H6c,P1,MEASURE1:def 11;
            x.(n+1) = a.(n+1)*(M*F).(n+1) by B6,H6b,B5;
            hence thesis by H6d,EXTREAL1:22;
           end;
           suppose
P2:         a.(n+1) = 0.;
            x.(n+1) = a.(n+1)*(M*F).(n+1) by B6,H6b,B5;
            hence thesis by P2,EXTREAL1:22;
           end;
         end;
H7:      not -infty in rng x1 & not -infty in rng <* x.(n+1) *>
         proof
          now assume -infty in rng x1; then
           consider l be set such that
H71:        l in dom x1 & x1.l = -infty by FUNCT_1:def 5;
           reconsider l as Nat by H71;
H72:       x.l = x1.l by H71,FUNCT_1:70 .= -infty by H71;
           l in dom x /\ (Seg n) by H71,FUNCT_1:68; then
H74:       l in dom x by XBOOLE_0:def 3; then
H73:       a.l*(M*F).l = x.l by B6 .= -infty by H72;
           per cases;
            suppose F.l <> {}; then
             consider v be set such that
H75:         v in F.l by XBOOLE_0:def 1;
H76:         a.l = f.v by H75,H74,B5,B4,MESFUNC3:def 1;
H79:         F.l in rng F by H74,B5,FUNCT_1:12; then
             v in union rng F by H75,TARSKI:def 4; then
             v in dom f by B4,MESFUNC3:def 1; then
H78:         0. <=' a.l by H76,B3;
             rng F c= S by FINSEQ_1:def 4; then
             reconsider Fl = F.l as Element of S by H79;
             reconsider EMPTY = {} as Element of S by MEASURE1:21;
             EMPTY c= F.l by XBOOLE_1:2; then
             M.{} <=' M.(Fl) by MEASURE1:62; then
             0. <=' M.(Fl) by MEASURE1:def 11; then
             0. <=' (M*F).l by H74,B5,FUNCT_1:23; then
             0. * (M*F).l <=' a.l*(M*F).l by H78,EXTREAL1:42;
             hence contradiction by H73,SUPINF_2:19,EXTREAL1:22;
            end;
            suppose
H80:         F.l = {};
             (M*F).l = M.(F.l) by H74,B5,FUNCT_1:23
                    .= 0. by H80,MEASURE1:def 11;
             hence contradiction by H73,SUPINF_2:19,EXTREAL1:22;
            end;
          end;
          hence not -infty in rng x1;
          now assume -infty in rng <* x.(n+1) *>; then
           -infty in {x.(n+1)} by FINSEQ_1:56;
           hence contradiction by H6,TARSKI:def 1,SUPINF_2:19;
          end;
          hence not -infty in rng <* x.(n+1) *>;
         end;
         len x = len F by B5,FINSEQ_3:31 .= n+1 by B7; then
         x = x|(n+1) by FINSEQ_1:79
          .= x|Seg(n+1) by FINSEQ_1:def 15
          .= x1^<* x.(n+1) *> by H6b,B5,FINSEQ_5:11; then
H8:      Sum x = Sum(x1)+Sum<* x.(n+1) *> by H7,CONVFUN1:7
              .= Sum(x1)+0. by H6,CONVFUN1:5;
         integral(X,S,M,f)=Sum(z) by B1,B2,B3,MESFUNC3:def 2,H2,E2,E3,H3,G4
           .=Sum(y) by G3,H5,FINSEQ_1:17
           .=integral(X,S,M,f1) by E6
           .=Sum(x1) by C1,C2,C3,C4,C5,C6,C7,AS
           .=Sum(x) by H8,SUPINF_2:18;
         hence integral(X,S,M,f) = Sum(x);
        end;
        case
CC2:     F.(n+1) <> {} & a.(n+1) <> 0.;
P0a:     f is_finite by B1,MESFUNC2:def 5;
         1 <= n+1 & n+1 <= len F by B7,NAT_1:29; then
P1a:     n+1 in dom F by FINSEQ_3:27;
         consider v be set such that
CD2:     v in F.(n+1) by CC2,XBOOLE_0:def 1;
         F.(n+1) in rng F by P1a,FUNCT_1:12; then
         v in union rng F by CD2,TARSKI:def 4; then
CD4:     v in dom f & dom f c= X by B4,MESFUNC3:def 1,RELSET_1:12; then
         reconsider v as Element of X;
CD5:     0. <=' f.v by CD4,B3;
         f.v = a.(n+1) by CD2,P1a,B4,MESFUNC3:def 1; then
CD6:     |. a.(n+1) .| <' +infty by P0a,CD4,MESFUNC2:def 1;
CCC1:    0. <=' a.(n+1) by CD5,CD2,P1a,B4,MESFUNC3:def 1; then
CCC:     0. <' a.(n+1) & a.(n+1) <' +infty
           by CC2,CD6,EXTREAL2:58,SUPINF_1:22;
         defpred PH[Nat,set] means
          ($1 <= len G implies $2 = G.$1) &
          ($1 = len G + 1 implies $2 = F.(n+1));
SC1:     for k be Nat st k in Seg(len G + 1)
          ex v be Element of S st PH[k,v]
         proof
          let k be Nat;
          assume
SC2:       k in Seg(len G + 1);
           per cases;
            suppose
SC3:         k <> len G + 1;
             k <= len G + 1 by SC2,FINSEQ_1:3; then
             k < len G + 1 by SC3,REAL_1:def 5; then
SC4:         k <= len G by NAT_1:38;
             1 <= k by SC2,FINSEQ_1:3; then
             k in dom G by SC4,FINSEQ_3:27; then
             G.k in rng G & rng G c= S by FINSEQ_1:def 4,FUNCT_1:12; then
             reconsider v = G.k as Element of S;
             take v;
             thus thesis by SC3;
            end;
            suppose
SC8:         k = len G + 1;
             F.(n+1) in rng F & rng F c= S
              by P1a,FUNCT_1:12,FINSEQ_1:def 4; then
             reconsider v = F.(n+1) as Element of S;
             take v;
             thus thesis by SC8,NAT_1:38;
            end;
         end;
         consider H be FinSequence of S such that
G0:      dom H = Seg(len G + 1) &
         for k be Nat st k in Seg(len G + 1) holds PH[k,H.k]
           from FINSEQ_1:sch 5(SC1);
G2a:     Seg len H = dom H by FINSEQ_1:def 3 .= Seg(len G + 1) by G0; then
G2:      len H = len G + 1 by FINSEQ_1:8;
         defpred Pc[Nat,set] means
          ($1 <= len b implies $2 = b.$1) &
          ($1 = len b + 1 implies $2 = a.(n+1));
SD1:     for k be Nat st k in Seg(len b + 1)
          ex v be Element of ExtREAL st Pc[k,v]
         proof
          let k be Nat;
          assume k in Seg(len b + 1);
           per cases;
            suppose
SD3:         k <> len b + 1;
             reconsider v = b.k as Element of ExtREAL;
             take v;
             thus thesis by SD3;
            end;
            suppose k = len b + 1; then
SD8:         len b < k by NAT_1:38;
             reconsider v = a.(n+1) as Element of ExtREAL;
             take v;
             thus thesis by SD8;
            end;
         end;
         consider c be FinSequence of ExtREAL such that
G3:      dom c = Seg(len b + 1) &
         for k be Nat st k in Seg(len b + 1) holds Pc[k,c.k]
           from FINSEQ_1:sch 5(SD1);
G5:      len c = len b + 1 by G3,FINSEQ_1:def 3;
         1 <= len b + 1 by NAT_1:29; then
L2a:     1 in Seg(len b + 1) by FINSEQ_1:3;
L2b:     dom G <> {}
         proof
          assume dom G = {}; then
          {} = union rng G by ZFMISC_1:2,RELAT_1:65
            .= dom f1 by E1,MESFUNC3:def 1;
          hence contradiction by C2;
         end;
L2c:     dom G = dom b by E1,MESFUNC3:def 1; then
         b <> {} by L2b,FINSEQ_1:26; then
         len b <> 0 by FINSEQ_1:25; then
         len b in Seg len b by FINSEQ_1:5; then
         1 <= len b by FINSEQ_1:3; then
L2:      c.1 = b.1 by L2a,G3 .=0. by E2;
LBG1:    Seg len G = dom G by FINSEQ_1:def 3
                  .= Seg len b by L2c,FINSEQ_1:def 3; then
LBG:     len G = len b by FINSEQ_1:8;
LGY:     len G = len y by E4,FINSEQ_3:31;
L3:      for m be Nat st 2 <= m & m in dom c holds
          0. <' c.m & c.m <' +infty
         proof
          let m be Nat;
          assume
L31:      2 <= m & m in dom c; then
L32:      m <= len c by FINSEQ_3:27;
          per cases;
           suppose m = len c;
            hence 0. <' c.m & c.m <' +infty by G3,G5,L31,CCC;
           end;
           suppose m <> len c; then
            m < len b + 1 by L32,G5,REAL_1:def 5; then
L35:        m <= len b by NAT_1:38; then
L36:        c.m = b.m by L31,G3;
            1 <= m by L31,XREAL_1:2; then
            m in dom b by L35,FINSEQ_3:27;
            hence 0. <' c.m & c.m <' +infty by E3,L31,L36;
           end;
         end;
         defpred Pz[Nat,set] means
          ($1 <= len y implies $2 = b.$1*(M*H).$1) &
          ($1 = len y + 1 implies $2 = a.(n+1)*(M*F).(n+1));
SE1:     for k be Nat st k in Seg(len y + 1)
          ex v be Element of ExtREAL st Pz[k,v]
         proof
          let k be Nat;
          assume k in Seg(len y + 1);
           per cases;
            suppose
SE3:         k <> len y + 1;
             reconsider v = b.k*(M*H).k as Element of ExtREAL;
             take v;
             thus thesis by SE3;
            end;
            suppose k = len y + 1; then
SE8:         len y < k by NAT_1:38;
             reconsider v = a.(n+1)*(M*F).(n+1) as Element of ExtREAL;
             take v;
             thus thesis by SE8;
            end;
         end;
         consider z be FinSequence of ExtREAL such that
K3a:     dom z = Seg(len y + 1) &
         for k be Nat st k in Seg(len y + 1) holds Pz[k,z.k]
           from FINSEQ_1:sch 5(SE1);
K3:      len z= len y + 1 by K3a,FINSEQ_1:def 3; then
         len z in Seg(len y + 1) by FINSEQ_1:6; then
K5:      z.len z=a.(n+1)*(M*F).(n+1) by K3,K3a;
V6a:     len y = len G by E4,FINSEQ_3:31; then
V6:      len z = len G + 1 by K3a,FINSEQ_1:def 3;
V4:      len z in Seg(len G + 1) by V6,FINSEQ_1:6;
V1:      len z in dom H by G0,V6,FINSEQ_1:6;
V2:      n+1 in dom F by C64a,FINSEQ_1:6;
V3:      (M*F).(n+1) = M.(F.(n+1)) by V2,FUNCT_1:23
                      .= M.(H.len z) by V4,V6,G0
                      .= (M*H).len z by V1,FUNCT_1:23;
K6:      for m be Nat st m in dom z holds z.m = c.m*(M*H).m
         proof
          let m be Nat;
          assume
K6a:      m in dom z;
K6e:      Pc[m,c.m] by G3,K6a,K3a,LBG,V6a;
          per cases;
           suppose m = len y + 1;
            hence thesis by V3,K6a,K3a,V6,V6a,LBG1,FINSEQ_1:8,K6e;
           end;
           suppose
K6f:        m <> len y + 1;
            m <= len y + 1 by K3a,K6a,FINSEQ_1:3; then
            m < len y + 1 by K6f,REAL_1:def 5; then
K6g:        m <= len b by LBG,V6a,NAT_1:38; then
            c.m = b.m by G3,K3a,K6a,LBG,V6a;
            hence thesis by K6g,LBG,V6a,K6a,K3a;
           end;
         end;
         for i,j be set st i <> j holds H.i misses H.j
         proof
          let i,j be set;
          assume
AA1:      i <> j;
          per cases;
           suppose
AA2:        i in dom H & j in dom H; then
            reconsider i,j as Nat;
AA3:        1 <= i & i <= len G + 1 &
            1 <= j & j <= len G + 1 by AA2,G0,FINSEQ_1:3;
AA4:        PH[i,H.i] & PH[j,H.j] by AA2,G0;
AA5:        for k be Nat st 1 <= k & k <= len G holds
             H.k misses F.(n+1)
            proof
             let k be Nat;
             assume
AAA1:        1 <= k & k <= len G; then
             k <= len G + 1 by NAT_1:37; then
             k in Seg(len G + 1) by AAA1,FINSEQ_1:3; then
AAA2:        H.k = G.k by G0,AAA1;
             k in dom G by AAA1,FINSEQ_3:27; then
             G.k in rng G by FUNCT_1:12; then
             G.k c= union rng G by ZFMISC_1:92; then
             G.k c= dom f1 by E1,MESFUNC3:def 1; then
             G.k c= (dom f)/\(union rng F1) by FUNCT_1:68; then
AAA3:        G.k c= union rng F1 by XBOOLE_1:18;
             union rng F1 misses F.(n+1)
             proof
              assume union rng F1 meets F.(n+1); then
               consider v be set such that
AAA5:          v in union rng F1 & v in F.(n+1) by XBOOLE_0:3;
               consider A be set such that
AAA6:          v in A & A in rng F1 by AAA5,TARSKI:def 4;
               consider m be set such that
AAA7:          m in dom F1 & A = F1.m by AAA6,FUNCT_1:def 5;
               reconsider m as Nat by AAA7;
               m in Seg len F1 by AAA7,FINSEQ_1:def 3; then
               1 <= m & m <= n by C7,FINSEQ_1:3; then
AAA9:          m <> n+1 by NAT_1:38;
               F1.m = F.m by AAA7,FUNCT_1:70; then
               A misses F.(n+1) by AAA7,AAA9,PROB_2:def 3; then
               A /\ F.(n+1) = {} by XBOOLE_0:def 7;
               hence contradiction by AAA5,AAA6,XBOOLE_0:def 3;
             end;
             hence thesis by AAA2,AAA3,XBOOLE_1:63;
            end;
            per cases;
             suppose
AB1:          i = len G + 1; then
              j < len G + 1 by AA1,AA3,REAL_1:def 5; then
              j <= len G by NAT_1:38;
              hence thesis by AA3,AA5,AA4,AB1;
             end;
             suppose i <> len G + 1; then
AC2:          i < len G + 1 by AA3,REAL_1:def 5; then
AC1:          i <= len G by NAT_1:38;
              per cases;
               suppose j = len G + 1;
                hence thesis by AA3,AA4,AC1,AA5;
               end;
               suppose j <> len G + 1; then
                j < len G + 1 by AA3,REAL_1:def 5;
                hence thesis by AA1,AC2,AA4,NAT_1:38,PROB_2:def 3;
               end;
             end;
           end;
           suppose
AD1:        not i in dom H or not j in dom H;
            per cases by AD1;
             suppose not i in dom H; then
              H.i = {} by FUNCT_1:def 4;
              hence thesis by XBOOLE_1:65;
             end;
             suppose not j in dom H; then
              H.j = {} by FUNCT_1:def 4;
              hence thesis by XBOOLE_1:65;
             end;
           end;
         end; then
         reconsider H as Finite_Sep_Sequence of S by PROB_2:def 3;
         n+1 in dom F by C64a,FINSEQ_1:6; then
         F|(Seg (n+1)) = F1^<* F.(n+1) *> by FINSEQ_5:11; then
         F1^<* F.(n+1) *> = F|(n+1) by FINSEQ_1:def 15
          .= F by B7,FINSEQ_1:79; then
RNGF:    rng F = rng F1 \/ rng <* F.(n+1) *> by FINSEQ_1:44
              .= rng F1 \/ {F.(n+1)} by FINSEQ_1:55;
RH:      len H = len G + 1 by G2a,FINSEQ_1:8
          .= len G + len <* F.(n+1) *> by FINSEQ_1:56
          .= len (G^<* F.(n+1) *>) by FINSEQ_1:35;
         for k be Nat st 1 <= k & k <= len H holds
          H.k = (G^<* F.(n+1) *>).k
         proof
          let k be Nat;
          assume
Rh1:      1 <= k & k <= len H; then
          k in Seg(len G + 1) by G2a,FINSEQ_1:3; then
Rh2:      PH[k,H.k] by G0;
          per cases;
           suppose k = len G + 1;
            hence H.k = (G^<* F.(n+1) *>).k by Rh2,FINSEQ_1:59;
           end;
           suppose k <> len G + 1; then
Rh5:        k < len G + 1 by Rh1,G2,REAL_1:def 5; then
            k <= len G by NAT_1:38; then
            k in dom G by Rh1,FINSEQ_3:27;
            hence H.k = (G^<* F.(n+1) *>).k by Rh2,Rh5,NAT_1:38,FINSEQ_1:def 7;
           end;
         end; then
         H = G^<* F.(n+1) *> by RH,FINSEQ_1:18; then
RNGH:    rng H = rng G \/ rng <* F.(n+1) *> by FINSEQ_1:44
              .= rng G \/ {F.(n+1)} by FINSEQ_1:55;
H2a:     dom f = union rng F by B4,MESFUNC3:def 1
              .= union (rng F1 \/ {F.(n+1)}) by RNGF
              .= union rng F1 \/ union {F.(n+1)} by ZFMISC_1:96
              .= dom f1 \/ union {F.(n+1)} by B10
              .= union rng G \/ union {F.(n+1)} by E1,MESFUNC3:def 1
              .= union (rng G \/ {F.(n+1)}) by ZFMISC_1:96
              .= union rng H by RNGH;
H2c:     for m be Nat st m in dom H for v be set st v in H.m holds f.v = c.m
         proof
          let m be Nat;
          assume
HL1:       m in dom H; then
HL2:       PH[m,H.m] & Pc[m,c.m] by G0,LBG,G3;
HL2a:      1 <= m & m <= len G + 1 by HL1,G0,FINSEQ_1:3;
           let v be set;
           assume
HL3:       v in H.m;
           per cases;
            suppose
HL4:         m = len G + 1;
             n+1 in dom F by C64a,FINSEQ_1:6;
             hence f.v = c.m by HL2,HL4,LBG1,FINSEQ_1:8,B4,HL3,MESFUNC3:def 1;
            end;
            suppose m <> len G + 1; then
HL8:         m < len G + 1 by HL2a,REAL_1:def 5; then
HL7:         m <= len G by NAT_1:38;
HL9:         H.m in rng H by HL1,FUNCT_1:12;
HL11:        union rng G = dom f1 by E1,MESFUNC3:def 1
                        .= union rng F1 by B10;
             m in Seg len G by HL2a,HL7,FINSEQ_1:3; then
             m in dom G by FINSEQ_1:def 3; then
             G.m in rng G by FUNCT_1:12; then
             v in union rng F1 & v in dom f
               by HL3,HL8,HL2,HL11,TARSKI:def 4,HL9,H2a,NAT_1:38; then
             v in (dom f)/\(union rng F1) by XBOOLE_0:def 3; then
HL12:        v in dom f1 by FUNCT_1:68;
             m in Seg len G by HL2a,HL7,FINSEQ_1:3; then
             m in dom G by FINSEQ_1:def 3; then
             f1.v = b.m by E1,HL3,HL2,HL8,NAT_1:38,MESFUNC3:def 1
                 .= c.m by HL7,LBG,HL1,G0,G3;
             hence f.v = c.m by HL12,FUNCT_1:70;
            end;
          end;
H2:      H,c are_Re-presentation_of f by H2a,G0,LBG,G3,H2c,MESFUNC3:def 1;
H3:      dom z = Seg len z by FINSEQ_1:def 3
              .= Seg(len G + 1) by V6a,K3a,FINSEQ_1:def 3;
H8:      not -infty in rng y &
         not -infty in rng <* a.(n+1)*(M*F).(n+1) *>
         proof
          now assume
H8a:       -infty in rng y or -infty in rng <* a.(n+1)*(M*F).(n+1) *>;
           per cases by H8a;
            suppose -infty in rng y; then
             consider k be set such that
H8b:         k in dom y & -infty = y.k by FUNCT_1:def 5;
             reconsider k as Nat by H8b;
H8c:         y.k = b.k*(M*G).k by H8b,E5;
             k in Seg len y by H8b,FINSEQ_1:def 3; then
H8d:         1 <= k by FINSEQ_1:3;
             per cases;
              suppose k = 1;
               hence contradiction by E2,H8b,H8c,EXTREAL1:22,SUPINF_2:19;
              end;
              suppose k <> 1; then
               1 < k by H8d,REAL_1:def 5; then
               1+1 <= k & k in dom b by NAT_1:38,E4,H8b,E1,MESFUNC3:def 1; then
H8e:           0. <' b.k & b.k <' +infty by E3;
               G.k in rng G & rng G c= S
                 by E4,H8b,FUNCT_1:12,FINSEQ_1:def 4; then
               reconsider Gk = G.k as Element of S;
               reconsider EMPTY = {} as Element of S by MEASURE1:45;
               EMPTY c= Gk by XBOOLE_1:2; then
               M.EMPTY <=' M.(Gk) by MEASURE1:62; then
H8h:           0. <=' M.(Gk) by MEASURE1:def 11;
               (M*G).k = M.(G.k) by E4,H8b,FUNCT_1:23;
               hence contradiction by H8b,H8h,H8c,H8e,EXTREAL2:44,SUPINF_2:19;
              end;
            end;
            suppose -infty in rng <* a.(n+1)*(M*F).(n+1) *>; then
             -infty in { a.(n+1)*(M*F).(n+1) } by FINSEQ_1:55; then
H8i:         a.(n+1)*(M*F).(n+1) = -infty by TARSKI:def 1;
             1 <= n+1 by NAT_1:29; then
H8k:         n+1 in dom F by B7,FINSEQ_3:27; then
             F.(n+1) in rng F & rng F c= S by FUNCT_1:12,FINSEQ_1:def 4; then
             reconsider Fn1 = F.(n+1) as Element of S;
             reconsider EMPTY = {} as Element of S by MEASURE1:45;
             EMPTY c= Fn1 by XBOOLE_1:2; then
             M.EMPTY <=' M.(Fn1) by MEASURE1:62; then
H8j:         0. <=' M.(Fn1) by MEASURE1:def 11;
             (M*F).(n+1) = M.(Fn1) by H8k,FUNCT_1:23;
             hence contradiction by H8i,H8j,EXTREAL2:44,SUPINF_2:19,CCC1;
            end;
          end;
          hence thesis;
         end;
H7a:     len (y^<* a.(n+1)*(M*F).(n+1) *>)
          = len y + 1 by FINSEQ_2:19
         .= len z by K3a,FINSEQ_1:def 3;
         for k be Nat st 1 <= k & k <= len z
          holds z.k = (y^<* a.(n+1)*(M*F).(n+1) *>).k
         proof
          let k be Nat;
          assume
H7b:      1 <= k & k <= len z;
          per cases;
           suppose k = len z;
            hence thesis by K3,K5,FINSEQ_1:59;
           end;
           suppose k <> len z; then
            k < len z by H7b,REAL_1:def 5; then
H7c:        k <= len y by K3,NAT_1:38; then
H7d:        k in dom y by H7b,FINSEQ_3:27; then
H7e:        (y^<* a.(n+1)*(M*F).(n+1) *>).k
             = y.k by FINSEQ_1:def 7
            .= b.k*(M*G).k by E5,H7d
            .= b.k*(M.(G.k)) by H7d,E4,FUNCT_1:23;
H7g:        k in Seg len z by H7b,FINSEQ_1:3; then
H7h:        H.k = G.k by G0,H7c,LGY,K3;
            M.(H.k) = (M*H).k by K3,LGY,H7g,G0,FUNCT_1:23;
            hence thesis by K3,H7g,K3a,H7c,H7h,H7e;
           end;
         end; then
H7:      z = y^<* a.(n+1)*(M*F).(n+1) *> by H7a,FINSEQ_1:18;
H5:      not -infty in rng x1
         proof
          assume -infty in rng x1; then
           consider k be set such that
H5a:       k in dom x1 & -infty = x1.k by FUNCT_1:def 5;
           reconsider k as Nat by H5a;
H5k:       x.k = -infty by H5a,FUNCT_1:70;
           k in (dom x)/\(Seg n) by H5a,FUNCT_1:68; then
H5b:       k in dom x by XBOOLE_0:def 3; then
H5j:       x.k = a.k*(M*F).k by B6;
           reconsider EMPTY = {} as Element of S by MEASURE1:45;
H5h:       F.k in rng F by B5,H5b,FUNCT_1:12;
           rng F c= S by FINSEQ_1:def 4; then
           reconsider Fk = F.k as Element of S by H5h;
           per cases;
            suppose F.k <> {}; then
             consider v be set such that
H5d:         v in F.k by XBOOLE_0:def 1;
H5e:         a.k = f.v by H5d,B5,H5b,B4,MESFUNC3:def 1;
             v in union rng F by H5h,H5d,TARSKI:def 4; then
             v in dom f by B4,MESFUNC3:def 1; then
H5g:         0. <=' a.k by B3,H5e;
             EMPTY c= Fk by XBOOLE_1:2; then
             M.EMPTY <=' M.Fk by MEASURE1:62; then
H5i:         0. <=' M.Fk by MEASURE1:def 11;
             M.Fk = (M*F).k by B5,H5b,FUNCT_1:23;
             hence contradiction by H5k,H5i,H5j,H5g,EXTREAL2:45,SUPINF_2:19;
            end;
            suppose F.k = {}; then
             0. = M.(F.k) by MEASURE1:def 11
               .= (M*F).k by B5,H5b,FUNCT_1:23;
             hence contradiction by H5k,H5j,EXTREAL1:22,SUPINF_2:19;
            end;
         end;
         len x = len F by B5,FINSEQ_3:31 .= n+1 by B7; then
H6:      x = x|(n+1) by FINSEQ_1:79
          .= x|Seg(n+1) by FINSEQ_1:def 15
          .= x1^<* x.(n+1) *> by P1a,B5,FINSEQ_5:11
          .= x1^<* a.(n+1)*(M*F).(n+1) *> by P1a,B5,B6;
         integral(X,S,M,f)=Sum z by B1,B2,B3,MESFUNC3:def 2,H2,L2,L3,H3,G0,K6
            .=Sum(y) + Sum<* a.(n+1)*(M*H).(len z) *> by H8,H7,V3,CONVFUN1:7
            .=Sum(y) + a.(n+1)*(M*H).(len z) by CONVFUN1:5
            .=integral(X,S,M,f1) + a.(n+1)*(M*H).(len z) by E6
            .=Sum(x1) + a.(n+1)*(M*F).(n+1) by V3,C1,C2,C3,C4,C5,C6,C7,AS
            .=Sum(x1) + Sum<* a.(n+1)*(M*F).(n+1) *> by CONVFUN1:5
            .=Sum(x) by H5,H6,H8,CONVFUN1:7;
         hence integral(X,S,M,f) = Sum(x);
        end;
       end;
       hence integral(X,S,M,f) = Sum(x);
      end;
     end;
     hence integral(X,S,M,f) = Sum(x);
   end;
   for n be Nat holds P[n] from NAT_1:sch 1(P1,P2);
   hence thesis;
end;

theorem LM005:
for X be non empty set,
    S be sigma_Field_Subset of X,
    f be PartFunc of X,ExtREAL,
    M be sigma_Measure of S,
    F be Finite_Sep_Sequence of S,
    a,x be FinSequence of ExtREAL
 st f is_simple_func_in S
  & dom f <> {}
  & (for x be set st x in dom f holds 0. <=' f.x)
  & F,a are_Re-presentation_of f
  & dom x = dom F
  & (for n be Nat st n in dom x holds x.n=a.n*(M*F).n)
 holds
    integral(X,S,M,f)=Sum(x)
proof
   let X be non empty set;
   let S be sigma_Field_Subset of X;
   let f be PartFunc of X,ExtREAL;
   let M be sigma_Measure of S;
   let F be Finite_Sep_Sequence of S;
   let a,x be FinSequence of ExtREAL;
   assume that
AF1: f is_simple_func_in S and
AF2: dom f <> {} and
AF3: for x be set st x in dom f holds 0. <=' f.x and
AF4: F,a are_Re-presentation_of f and
AF5: dom x = dom F and
AF6: for n be Nat st n in dom x holds x.n=a.n*(M*F).n;
   len F = len F;
   hence thesis by AF1,AF2,AF3,AF4,AF5,AF6,LM004;
end;

theorem LM006:
for X be non empty set,
    S be sigma_Field_Subset of X,
    f be PartFunc of X,ExtREAL,
    M be sigma_Measure of S
 st f is_simple_func_in S
  & dom f <> {}
  & (for x be set st x in dom f holds 0. <=' f.x)
 holds
    ex F be Finite_Sep_Sequence of S, a,x be FinSequence of ExtREAL
     st F,a are_Re-presentation_of f
      & dom x = dom F
      & (for n be Nat st n in dom x holds x.n=a.n*(M*F).n)
      & integral(X,S,M,f)=Sum(x)
proof
   let X be non empty set,
       S be sigma_Field_Subset of X,
       f be PartFunc of X,ExtREAL,
       M be sigma_Measure of S;
   assume that
AF1: f is_simple_func_in S and
AF2: dom f <> {} and
AF3: for x be set st x in dom f holds 0. <=' f.x;
   consider F be Finite_Sep_Sequence of S,
    a be FinSequence of ExtREAL such that
P1: F,a are_Re-presentation_of f by AF1,MESFUNC3:12;
   ex x be FinSequence of ExtREAL st
    dom x = dom F &
    for n be Nat st n in dom x holds x.n =a.n*(M*F).n
   proof
    deffunc Q(Nat) = a.$1*(M*F).$1;
    consider x be FinSequence of ExtREAL such that
Q1:  len x = len F &
     for k be Nat st k in Seg len F holds x.k=Q(k) from FINSEQ_2:sch 1;
    take x;
Q2: for n be Nat st n in dom x holds x.n = a.n*(M*F).n
    proof
     let n be Nat;
     assume n in dom x; then
     n in Seg len x by FINSEQ_1:def 3;
     hence thesis by Q1;
    end;
Q3: dom x = Seg len x by FINSEQ_1:def 3
         .= Seg len F by Q1
         .= dom F by FINSEQ_1:def 3;
    thus thesis by Q2,Q3;
   end; then
   consider x be FinSequence of ExtREAL such that
P3: dom x = dom F and
P4: (for n be Nat st n in dom x holds x.n=a.n*(M*F).n);
   integral(X,S,M,f)=Sum(x) by AF1,AF2,AF3,P1,P3,P4,LM005;
   hence thesis by P1,P3,P4;
end;

theorem
for X be non empty set,
    S be sigma_Field_Subset of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL
  st f is_simple_func_in S & dom f <> {}
   & (for x be set st x in dom f holds 0. <=' f.x)
   & g is_simple_func_in S & dom g = dom f
   & (for x be set st x in dom g holds 0. <=' g.x)
 holds
    f+g is_simple_func_in S & dom (f+g) <> {}
  & (for x be set st x in dom (f+g) holds 0. <=' (f+g).x)
  & integral(X,S,M,f+g)=integral(X,S,M,f)+integral(X,S,M,g)
proof
   let X be non empty set,
       S be sigma_Field_Subset of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL such that
AF1:f is_simple_func_in S and
AF2:dom f <> {} and
AF3:for x be set st x in dom f holds 0. <=' f.x and
AG1:g is_simple_func_in S and
AG20:dom g = dom f and
AG3:for x be set st x in dom g holds 0. <=' g.x;
   consider F be Finite_Sep_Sequence of S,
   a,x be FinSequence of ExtREAL such that
F1: F,a are_Re-presentation_of f and
    dom x = dom F and
    (for n be Nat st n in dom x holds x.n=a.n*(M*F).n) and
    integral(X,S,M,f)=Sum(x) by AF1,AF2,AF3,LM006;
   consider G be Finite_Sep_Sequence of S,
   b,y be FinSequence of ExtREAL such that
G1: G,b are_Re-presentation_of g and
    dom y = dom G and
   (for n be Nat st n in dom y holds y.n=b.n*(M*G).n) and
   integral(X,S,M,g)=Sum(y) by AG1,AG20,AF2,AG3,LM006;
   set la = len a;
   set lb = len b;
F7:dom f = union rng F by F1,MESFUNC3:def 1;
F8:dom F = dom a by F1,MESFUNC3:def 1;
G7:dom g = union rng G by G1,MESFUNC3:def 1;
G8:dom G= dom b by G1,MESFUNC3:def 1;
   deffunc FG1(Nat) =
   F.(($1-'1) div lb + 1) /\ G.(($1-'1) mod lb + 1);
   consider FG be FinSequence such that
FG1:len FG = la*lb and
FG2:for k be Nat st k in Seg (la*lb) holds FG.k=FG1(k) from FINSEQ_1:sch 2;
   FG is FinSequence of S
   proof
    now let k be Nat;
     assume k in dom FG; then
P00: k in Seg (la*lb) by FG1,FINSEQ_1:def 3; then
P0:  FG.k = F.((k-'1) div lb + 1) /\ G.((k-'1) mod lb + 1) by FG2;
     set i=(k-'1) div lb + 1;
     set j=(k-'1) mod lb + 1;
Q0:  i >= 0+1 & j >= 0+1 by XREAL_1:8;
Q1:  1 <= k & k <= la*lb by P00,FINSEQ_1:3;
     reconsider la'=la,lb'=lb as natural number;
Q8:  lb <> 0 by Q1; then
Q7:  lb > 0; then
     lb >= 0+1 by NAT_1:38; then
     lb' divides (la'*lb') & 1 <= la*lb & 1 <= lb
       by Q1,NAT_1:def 3,XREAL_1:2; then
Q3:  ((la'*lb') -' 1) div lb' = ((la'*lb') div lb') - 1 by NAT_2:17;
     (k -' 1) <= (la*lb -' 1) by Q1,JORDAN3:5; then
     (k -' 1) div lb <= (la*lb -' 1) div lb by NAT_2:26; then
Q4:  (k -' 1) div lb + 1 <= la*lb div lb by Q3,XREAL_1:21;
     reconsider la,lb as Nat;
     i <= la by Q8,Q4,NAT_1:68; then
Pa1: i in Seg la by Q0,FINSEQ_1:3;
     (k -' 1) mod lb < lb by Q7,NAT_1:46; then
     j <= lb by NAT_1:38; then
Pb1: j in Seg lb by Q0,FINSEQ_1:3;
Pa2: i in dom F by F8,FINSEQ_1:def 3,Pa1;
Pb2: j in dom G by G8,FINSEQ_1:def 3,Pb1;
Pa3: rng F c= S by FINSEQ_1:def 4;
Pb3: rng G c= S by FINSEQ_1:def 4;
Pa4: F.i in rng F by Pa2,FUNCT_1:12;
Pb4: G.j in rng G by Pb2,FUNCT_1:12;
Pa5: F.i is_measurable M by Pa3,Pa4,MEASURE1:def 12;
Pb5: G.j is_measurable M by Pb3,Pb4,MEASURE1:def 12;
     F.i /\ G.j is_measurable M by Pa5,Pb5,MEASURE1:66;
     hence FG.k in S by P0,MEASURE1:def 12;
    end;
    hence thesis by FINSEQ_2:14;
   end; then
   reconsider FG as FinSequence of S;
   for k,l be Nat st k in dom FG & l in dom FG & k <> l holds
    FG.k misses FG.l
   proof
    let k,l be Nat;
    assume
AS2: k in dom FG & l in dom FG & k <> l;
    set i=(k-'1) div lb + 1;
    set j=(k-'1) mod lb + 1;
    set n=(l-'1) div lb + 1;
    set m=(l-'1) mod lb + 1;
P00:k in Seg (la*lb) by AS2,FG1,FINSEQ_1:def 3; then
P0: FG.k = F.i /\ G.j by FG2;
P01:l in Seg (la*lb) by AS2,FG1,FINSEQ_1:def 3;
Q0:  i >= 0+1 & j >= 0+1 by XREAL_1:8;
Q1:  1 <= k & k <= la*lb by P00,FINSEQ_1:3;
     reconsider la'=la,lb'=lb as natural number;
Q8:  lb <> 0 by Q1; then
Q7:  lb > 0; then
     lb >= 0+1 by NAT_1:38; then
     lb' divides (la'*lb') & 1 <= la*lb & 1 <= lb
       by Q1,NAT_1:def 3,XREAL_1:2; then
Q3:  ((la'*lb') -' 1) div lb' = ((la'*lb') div lb') - 1 by NAT_2:17;
     (k -' 1) <= (la*lb -' 1) by Q1,JORDAN3:5; then
     (k -' 1) div lb <= (la*lb div lb) - 1 by Q3,NAT_2:26; then
Q4:  (k -' 1) div lb + 1 <= la*lb div lb by XREAL_1:21;
     reconsider la,lb as Nat;
     i <= la by Q4,Q8,NAT_1:68; then
a1:  i in Seg la by Q0,FINSEQ_1:3;
     (k -' 1) mod lb < lb by Q7,NAT_1:46; then
     j <= lb by NAT_1:38; then
Pb1:j in Seg lb by Q0,FINSEQ_1:3;
Pa2:i in dom F by a1,F8,FINSEQ_1:def 3;
Pb2:j in dom G by G8,FINSEQ_1:def 3,Pb1;
R0:  n >= 0+1 & m >= 0+1 by XREAL_1:8;
R1:  1 <= l & l <= la*lb by P01,FINSEQ_1:3;
     (l -' 1) <= (la*lb -' 1) by R1,JORDAN3:5; then
     (l -' 1) div lb <= (la*lb div lb) - 1 by Q3,NAT_2:26; then
R4:  (l -' 1) div lb + 1 <= la*lb div lb by XREAL_1:21;
     n <= la by R4,Q8,NAT_1:68; then
Pc1:n in Seg la by R0,FINSEQ_1:3;
     (l -' 1) mod lb < lb by Q7,NAT_1:46; then
     m <= lb by NAT_1:38; then
Pd1:m in Seg lb by R0,FINSEQ_1:3;
Pc2:n in dom F by F8,FINSEQ_1:def 3,Pc1;
Pd2:m in dom G by G8,FINSEQ_1:def 3,Pd1;
Q0: i <> n or j <> m
    proof
     assume
QAS:  i=n & j=m;
     (k-'1)=((k-'1) div lb)*lb + ((k-'1) mod lb) by Q7,NAT_1:47
          .= (i-1)*lb+(j-1); then
     (k-'1)+1=(i-1)*lb+(j-1)+1; then
QAS1:k - 1 + 1 = (i-1)*lb + j by Q1,BINARITH:50;
     (l-'1)=lb*((l-'1) div lb) + ((l-'1) mod lb) by Q7,NAT_1:47
          .= (n-1)*lb+(m-1); then
     (l-'1)+1=(n-1)*lb+(m-1)+1; then
QAS2:l - 1 + 1 = (n-1)*lb+m by R1,BINARITH:50;
     k=(i-1)*lb+j by QAS1
     .=(n-1)*lb+m by QAS
     .=l by QAS2;
     hence contradiction by AS2;
    end;
    per cases by Q0;
     suppose i <> n; then
C11:  F.i misses F.n by Pa2,Pc2,MESFUNC3:4;
      FG.k /\ FG.l= (F.i /\ G.j) /\ (F.n /\ G.m) by P01,P0,FG2
                 .= F.i /\ (G.j /\ (F.n /\ G.m)) by XBOOLE_1:16
                 .= F.i /\ ((F.n /\ G.j) /\ G.m) by XBOOLE_1:16
                 .= F.i /\ (F.n /\ (G.j /\ G.m)) by XBOOLE_1:16
                 .= (F.i /\ F.n) /\ (G.j /\ G.m) by XBOOLE_1:16
                 .= {} /\ (G.j /\ G.m) by C11,XBOOLE_0:def 7
                 .= {};
      hence FG.k misses FG.l by XBOOLE_0:def 7;
     end;
     suppose j <> m; then
C21:  G.j misses G.m by Pb2,Pd2,MESFUNC3:4;
      FG.k /\ FG.l= (F.i /\ G.j) /\ (F.n /\ G.m) by P01,P0,FG2
                 .= F.i /\ (G.j /\ (F.n /\ G.m)) by XBOOLE_1:16
                 .= F.i /\ ((F.n /\ G.j) /\ G.m) by XBOOLE_1:16
                 .= F.i /\ (F.n /\ (G.j /\ G.m)) by XBOOLE_1:16
                 .= (F.i /\ F.n) /\ (G.j /\ G.m) by XBOOLE_1:16
                 .= (F.i /\ F.n) /\ {} by C21,XBOOLE_0:def 7
                 .= {};
      hence FG.k misses FG.l by XBOOLE_0:def 7;
     end;
   end; then
   reconsider FG as Finite_Sep_Sequence of S by MESFUNC3:4;
   deffunc A1(Nat) = a.(($1-' 1) div lb +1);
   consider a1 be FinSequence of ExtREAL such that
AA1:len a1 = len FG and
AA2:for k be Nat st k in Seg len FG holds a1.k=A1(k) from FINSEQ_2:sch 1;
   ex x1 be FinSequence of ExtREAL st
    dom x1 = dom FG &
    for n be Nat st n in dom x1 holds x1.n =a1.n*(M*FG).n
   proof
    deffunc X1(Nat) = a1.$1*(M*FG).$1;
    consider x1 be FinSequence of ExtREAL such that
Q1:  len x1 = len FG  &
     for k be Nat st k in Seg len FG holds x1.k=X1(k)
      from FINSEQ_2:sch 1;
    take x1;
    for n be Nat st n in dom x1 holds x1.n = a1.n*(M*FG).n
    proof
     let n be Nat;
     assume n in dom x1; then
     n in Seg len x1 by FINSEQ_1:def 3;
     hence thesis by Q1;
    end;
    hence thesis by Q1,FINSEQ_3:31;
   end; then
   consider x1 be FinSequence of ExtREAL such that
F41:dom x1 = dom FG and
F51:for n be Nat st n in dom x1 holds x1.n=a1.n*(M*FG).n;
   deffunc B1(Nat) = b.(($1-' 1) mod lb +1);
   consider b1 be FinSequence of ExtREAL such that
AB1:len b1 = len FG and
AB2:for k be Nat st k in Seg len FG holds b1.k=B1(k) from FINSEQ_2:sch 1;
   ex y1 be FinSequence of ExtREAL st
    dom y1 = dom FG &
    for n be Nat st n in dom y1 holds y1.n =b1.n*(M*FG).n
   proof
    deffunc Y1(Nat) = b1.$1*(M*FG).$1;
    consider y1 be FinSequence of ExtREAL such that
Q1:  len y1 = len FG  &
     for k be Nat st k in Seg len FG holds y1.k=Y1(k) from FINSEQ_2:sch 1;
    take y1;
Q2: for n be Nat st n in dom y1 holds y1.n = b1.n*(M*FG).n
    proof
     let n be Nat;
     assume n in dom y1; then
     n in Seg len y1 by FINSEQ_1:def 3;
     hence thesis by Q1;
    end;
Q3: dom y1 = Seg len y1 by FINSEQ_1:def 3
          .= Seg len FG by Q1
          .= dom FG by FINSEQ_1:def 3;
    thus thesis by Q2,Q3;
   end; then
   consider y1 be FinSequence of ExtREAL such that
G41:dom y1 = dom FG and
G51:for n be Nat st n in dom y1 holds y1.n=b1.n*(M*FG).n;
   deffunc C1(Nat) = a1.$1+b1.$1;
   consider c1 be FinSequence of ExtREAL such that
AC1:len c1 = len FG and
AC2:for k be Nat st k in Seg len FG holds c1.k=C1(k) from FINSEQ_2:sch 1;
   ex z1 be FinSequence of ExtREAL st
    dom z1 = dom FG &
    for n be Nat st n in dom z1 holds z1.n =c1.n*(M*FG).n
   proof
    deffunc Z1(Nat) = c1.$1*(M*FG).$1;
    consider z1 be FinSequence of ExtREAL such that
Q1:  len z1 = len FG  &
     for k be Nat st k in Seg len FG holds z1.k=Z1(k) from FINSEQ_2:sch 1;
    take z1;
 for n be Nat st n in dom z1 holds z1.n = c1.n*(M*FG).n
    proof
     let n be Nat;
     assume n in dom z1; then
     n in Seg len z1 by FINSEQ_1:def 3;
     hence thesis by Q1;
    end;
    hence thesis by Q1,FINSEQ_3:31;
   end; then
   consider z1 be FinSequence of ExtREAL such that
FG41:dom z1 = dom FG and
FG51:for n be Nat st n in dom z1 holds z1.n=c1.n*(M*FG).n;
F00:dom f = union rng FG
   proof
    thus dom f c= union rng FG
    proof
     let z be set;
     assume AS0:z in dom f; then
     consider Y be set such that
K0:  z in Y & Y in rng F by F7,TARSKI:def 4;
     consider i be set such that
K1:  i in dom F & Y = F.i by K0,FUNCT_1:def 5;
     reconsider i as Nat by K1;
     consider Z be set such that
L0:  z in Z & Z in rng G by G7,AS0,AG20,TARSKI:def 4;
     consider j be set such that
L1:  j in dom G & Z = G.j by L0,FUNCT_1:def 5;
     reconsider j as Nat by L1;
     set k=(i-1)*lb+j;
     i in Seg len a by F8,K1,FINSEQ_1:def 3; then
LL0: 1 <= i & i <= la by FINSEQ_1:3; then
     consider i' be Nat such that
LL1: i = 1 + i' by NAT_1:28;
     i'*lb + j is Nat; then
     reconsider k as Nat by LL1;
     j in Seg len b by L1,G8,FINSEQ_1:def 3; then
LL5: 1 <= j & j <= lb by FINSEQ_1:3; then
     consider j' be Nat such that
LL3: j = 1 + j' by NAT_1:28;
     i'*lb >= 0; then
     k >= 0 + j by LL1,XREAL_1:8; then
L6:  k >= 1 by LL5,XREAL_1:2; then
LL6: k -' 1 = k - 1 by BINARITH:50
           .= i'*lb + j' by LL1,LL3;
LL7: j' < lb by LL5,LL3,NAT_1:38; then
L2:  i = (k-'1) div lb +1 by LL1,LL6,NAT_1:def 1;
L3:  j = (k-'1) mod lb +1 by LL3,LL6,LL7,NAT_1:def 2;
     i-1 <= la-1 by LL0,XREAL_1:11; then
     (i-1)*lb <= (la - 1)*lb by XREAL_1:66; then
L4:  k <= (la - 1) * lb + j by XREAL_1:8;
L5:  (la - 1) * lb + j <= (la - 1) * lb + lb by LL5,XREAL_1:8;
     k <= la*lb by L4,L5,XREAL_1:2; then
M0:  k in Seg (la*lb) by L6,FINSEQ_1:3; then
M1:  k in dom FG by FG1,FINSEQ_1:def 3;
     z in F.i /\ G.j by K0,K1,L0,L1,XBOOLE_0:def 3; then
M4:  z in FG.k by M0,L2,L3,FG2;
     FG.k in rng FG by M1,FUNCT_1:def 5;
    hence thesis by M4,TARSKI:def 4;
    end;
     let z be set;
     assume z in union rng FG; then
     consider Y be set such that
K0:   z in Y & Y in rng FG by TARSKI:def 4;
     consider k be set such that
K1:   k in dom FG & Y = FG.k by K0,FUNCT_1:def 5;
     reconsider k as Nat by K1;
     set i=(k-'1) div lb +1;
     set j=(k-'1) mod lb +1;
     k in Seg (la*lb) by FG1,K1,FINSEQ_1:def 3; then
     FG.k=F.i /\ G.j by FG2; then
M2:  z in F.i by K0,K1,XBOOLE_0:def 3;
     reconsider i as Nat;
     k in Seg len FG by K1,FINSEQ_1:def 3; then
M3:  1 <= k & k <= la*lb by FG1,FINSEQ_1:3;
Q0:  i >= 0+1 & j >= 0+1 by XREAL_1:8;
     reconsider la'=la,lb'=lb as natural number;
Q8:  lb <> 0 by M3; then
     lb > 0; then
     lb >= 0+1 by NAT_1:38; then
     lb' divides (la'*lb') & 1 <= la*lb & 1 <= lb
       by M3,NAT_1:def 3,XREAL_1:2; then
Q3:  ((la'*lb') -' 1) div lb' = ((la'*lb') div lb') - 1 by NAT_2:17;
     (k -' 1) <= (la*lb -' 1) by M3,JORDAN3:5; then
     (k -' 1) div lb <= (la*lb div lb) - 1 by Q3,NAT_2:26; then
Q6:  i <= la*lb div lb by XREAL_1:21;
     la*lb div lb = la by Q8,NAT_1:68; then
     i in Seg la by Q0,Q6,FINSEQ_1:3; then
     i in  dom F by F8,FINSEQ_1:def 3; then
     F.i in rng F by FUNCT_1:def 5;
     hence thesis by F7,M2,TARSKI:def 4;
  end;
H2: g is_finite by AG1,MESFUNC2:def 5;
SFGD1: dom (f+g) = dom f /\ dom g by MESFUNC2:2,H2
             .= dom f /\ dom f by AG20
             .= dom f;
SFGD2: dom (f+g) = dom f /\ dom g by MESFUNC2:2,H2
             .= dom g /\ dom g by AG20
             .= dom g;
SFG0: f+g is_finite
    proof
      now let x be Element of X;
        assume A0: x in dom (f+g);
        f is_finite by AF1,MESFUNC2:def 5; then
        P1: |. f.x .| <' +infty by A0,SFGD1,MESFUNC2:def 1;
        g is_finite by AG1,MESFUNC2:def 5; then
        P2: |. g.x .| <' +infty by SFGD2,A0,MESFUNC2:def 1;
        P3: |. (f+g).x .| = |. f.x + g.x .| by A0,MESFUNC1:def 3;
        not (f.x = +infty or f.x = -infty) &
        not (g.x = +infty or g.x = -infty) by P1,P2,EXTREAL2:67; then
        P4: |. (f+g).x .| <=' |. f.x .| + |. g.x .| by P3,EXTREAL2:61;
        |. f.x .| + |. g.x .| <> +infty by P1,P2,SUPINF_2:8; then
        |. f.x .| + |. g.x .| <' +infty by SUPINF_1:24;
        hence |. (f+g).x .| <' +infty by P4,SUPINF_1:29;
      end;
      hence thesis by MESFUNC2:def 1;
    end;
SFG1: dom (f+g) = dom f by SFGD1
               .= union rng FG by F00;
SFG2: for k being Nat,x,y being Element of X st
          k in dom FG & x in FG.k & y in FG.k holds (f+g).x = (f+g).y
    proof
      let k be Nat;
      let x,y be Element of X;
      assume 
   A: k in dom FG & x in FG.k & y in FG.k; then
      k in Seg (la*lb) by FG1,FINSEQ_1:def 3; then
P0:   FG.k = F.( (k-'1) div lb + 1 ) /\ G.( (k-'1) mod lb  + 1 ) by FG2;
      set i=(k-'1) div lb + 1;
      set j=(k-'1) mod lb + 1;
     k in Seg len FG by A,FINSEQ_1:def 3; then
M3:  1 <= k & k <= la*lb by FG1,FINSEQ_1:3;
Q0:  i >= 0+1 & j >= 0+1 by XREAL_1:8;
     reconsider la'=la,lb'=lb as natural number;
Q8:  lb <> 0 by M3; then
Q7:  lb > 0; then
     lb >= 0+1 by NAT_1:38; then
     lb' divides (la'*lb') & 1 <= la*lb & 1 <= lb
       by M3,NAT_1:def 3,XREAL_1:2; then
Q3:  ((la'*lb') -' 1) div lb' = ((la'*lb') div lb') - 1 by NAT_2:17;
     (k -' 1) <= (la*lb -' 1) by M3,JORDAN3:5; then
     (k -' 1) div lb <= (la*lb div lb) - 1 by Q3,NAT_2:26; then
     (k -' 1) div lb + 1 <= la*lb div lb by XREAL_1:21; then
     i <= la by Q8,NAT_1:68; then
Pa1: i in Seg la by Q0,FINSEQ_1:3;
     (k -' 1) mod lb < lb by Q7,NAT_1:46; then
     j <= lb by NAT_1:38; then
Pb1: j in Seg lb by Q0,FINSEQ_1:3;
Pa2: i in dom F by F8,FINSEQ_1:def 3,Pa1;
Pb2: j in dom G by G8,FINSEQ_1:def 3,Pb1;
X2: x in F.i by A,P0,XBOOLE_0:def 3;
X3: x in G.j by A,P0,XBOOLE_0:def 3;
X4: f.x=a.i by X2,Pa2,F1,MESFUNC3:def 1;
Y2: y in F.i by A,P0,XBOOLE_0:def 3;
Y3: y in G.j by A,P0,XBOOLE_0:def 3;
Y4: f.y=a.i by Y2,Pa2,F1,MESFUNC3:def 1;
FF: FG.k in rng FG by A,FUNCT_1:def 5; then
X0: x in dom (f+g) by SFG1,A,TARSKI:def 4;
Y0: y in dom (f+g) by SFG1,A,FF,TARSKI:def 4;
    thus (f+g).x= f.x+g.x by X0,MESFUNC1:def 3
               .= a.i+b.j by X4,X3,Pb2,G1,MESFUNC3:def 1
               .= f.y+g.y by Y4,Y3,Pb2,G1,MESFUNC3:def 1
               .= (f+g).y by Y0,MESFUNC1:def 3;
    end;
   thus
   AFG1: f+g is_simple_func_in S by SFG0,SFG1,SFG2,MESFUNC2:def 5;
  thus dom (f+g) <> {} by SFGD1,AF2;
  thus
AFG3: for x be set st x in dom (f+g) holds 0. <=' (f+g).x
   proof
   let x be set;
   assume A: x in dom (f+g); then
P2:  0. <=' f.x by AF3,SFGD1;
     0. <=' g.x by AG3,SFGD2,A; then
R1:  0. <=' f.x + g.x by P2,EXTREAL2:7;
     dom f c= X by RELSET_1:12;
     hence 0. <=' (f + g).x by A,SFGD1,R1,MESFUNC1:def 3;
   end;
PREF11:
    for k be Nat st k in dom FG
     for x be set st x in FG.k holds f.x=a1.k
    proof
     let k be Nat;
     assume 
A:   k in dom FG;
     let x be set;
      assume
A0:   x in FG.k;
      k in Seg (la*lb) by A,FG1,FINSEQ_1:def 3; then
P0:   FG.k = F.((k-'1) div lb + 1) /\ G.((k-'1) mod lb + 1) by FG2;
      set i=(k-'1) div lb + 1;
      set j=(k-'1) mod lb + 1;
      k in Seg len FG by A,FINSEQ_1:def 3; then
M3:   1 <= k & k <= la*lb by FG1,FINSEQ_1:3;
Q0:   i >= 0+1 & j >= 0+1 by XREAL_1:8;
      reconsider la'=la,lb'=lb as natural number;
Q8:   lb <> 0 by M3; then
      lb > 0; then
      lb >= 0+1 by NAT_1:38; then
      lb' divides (la'*lb') & 1 <= la*lb & 1 <= lb 
       by M3,NAT_1:def 3,XREAL_1:2; then
Q3:   ((la'*lb') -' 1) div lb' = ((la'*lb') div lb') - 1 by NAT_2:17;
      (k -' 1) <= (la*lb -' 1) by M3,JORDAN3:5; then
      (k -' 1) div lb <= (la*lb -' 1) div lb by NAT_2:26; then
Q6:   i <= la*lb div lb by Q3,XREAL_1:21;
      la*lb div lb = la by Q8,NAT_1:68; then
Pa1:  i in Seg la by Q6,Q0,FINSEQ_1:3;
Pa2:  i in dom F by F8,FINSEQ_1:def 3,Pa1;
X2:   x in F.i by A0,P0,XBOOLE_0:def 3;
X4:   k in Seg len FG by A,FINSEQ_1:def 3;
      thus f.x=a.i by X2,Pa2,F1,MESFUNC3:def 1 .=a1.k by X4,AA2;
    end;
F11: FG,a1 are_Re-presentation_of f
  proof
  thus dom f  = union rng FG by F00;
  thus dom FG = Seg len FG by FINSEQ_1:def 3
            .= Seg len a1 by AA1
            .= dom a1 by FINSEQ_1:def 3;
  thus thesis by PREF11;
  end;
PREG11: for k be Nat st k in dom FG
         for x be set st x in FG.k holds g.x=b1.k
    proof
     let k be Nat;
      assume A: k in dom FG;
      let x be set;
        assume
    A0: x in FG.k;
        k in Seg (la*lb) by A,FG1,FINSEQ_1:def 3; then
    P0: FG.k = F.( (k-'1) div lb + 1 ) /\ G.( (k-'1) mod lb + 1 ) by FG2;
        set i=(k-'1) div lb + 1;
        set j=(k-'1) mod lb + 1;
     k in Seg len FG by A,FINSEQ_1:def 3; then
M3:  1 <= k & k <= la*lb by FG1,FINSEQ_1:3;
Q0:  i >= 0+1 & j >= 0+1 by XREAL_1:8;
     reconsider la'=la,lb'=lb as natural number;
     lb <> 0 by M3; then
Q7:  lb > 0;
     (k -' 1) mod lb < lb by Q7,NAT_1:46; then
     j <= lb by NAT_1:38; then
     Pb1: j in Seg lb by Q0,FINSEQ_1:3;
     Pb2: j in dom G by G8,FINSEQ_1:def 3,Pb1;
     X3: x in G.j by A0,P0,XBOOLE_0:def 3;
     X4: k in Seg len FG by A,FINSEQ_1:def 3;
         thus g.x=b.j by X3,Pb2,G1,MESFUNC3:def 1
                  .=b1.k by X4,AB2;
    end;
G11: FG,b1 are_Re-presentation_of g
   proof
   thus dom g = union rng FG by F00,AG20;
   thus dom FG = Seg len FG by FINSEQ_1:def 3
             .= Seg len b1 by AB1
             .= dom b1 by FINSEQ_1:def 3;
   thus thesis by PREG11;
   end;
PREFG11:
  for k be Nat st k in dom FG
      for x be set st x in FG.k holds (f+g).x=c1.k
   proof
      let k be Nat;
      assume 
   A: k in dom FG;
      let x be set;
      assume 
A1:     x in FG.k;
        FG.k in rng FG by A,FUNCT_1:def 5; then
    X0: x in dom (f+g) by SFG1,A1,TARSKI:def 4;
    X4: k in Seg len FG by A,FINSEQ_1:def 3;
        dom (f+g) c= X by RELSET_1:12;
        hence (f+g).x= f.x+g.x by X0,MESFUNC1:def 3
                  .=a1.k+g.x by A,A1,PREF11
                  .=a1.k+b1.k by A,A1,PREG11
                  .=c1.k by X4,AC2;
  end;
FG11: FG,c1 are_Re-presentation_of (f+g)
   proof
   thus dom (f+g) = union rng FG by SFGD1,F00;
   thus dom FG = dom c1 by AC1,FINSEQ_3:31;
   thus thesis by PREFG11;
   end;
P1: integral(X,S,M,f)=Sum x1 by AF1,AF2,AF3,F11,F41,F51,LM005;
P2: integral(X,S,M,g)=Sum y1 by AG1,AG20,AF2,AG3,G11,G41,G51,LM005;
P3: integral(X,S,M,f+g)=Sum z1
              by AFG1,SFGD1,AF2,AFG3,FG11,FG41,FG51,LM005;
P7: dom x1=dom FG by F41
         .=dom y1 by G41;
P5: for i be Nat st i in dom x1 holds 0. <=' x1.i
  proof
   let i be Nat;
   assume 
A:  i in dom x1; then
P1: x1.i=a1.i*(M*FG).i by F51; 
K3: i in Seg len FG by A,F41,FINSEQ_1:def 3;
    set i' = (i -' 1) div lb + 1;
M3: 1 <= i & i <= la*lb by K3,FG1,FINSEQ_1:3;
Q0: i' >= 0+1 by XREAL_1:8;
    reconsider la'=la,lb'=lb as natural number;
Q8: lb <> 0 by M3; then
    lb > 0; then
    lb >= 0+1 by NAT_1:38; then
    lb' divides (la'*lb') & 1 <= la*lb & 1 <= lb 
      by M3,NAT_1:def 3,XREAL_1:2; then
Q3: ((la'*lb') -' 1) div lb' = ((la'*lb') div lb') - 1 by NAT_2:17;
    i -' 1 <= (la*lb -' 1) by M3,JORDAN3:5; then
    (i -' 1) div lb <= (la*lb div lb) - 1 by Q3,NAT_2:26; then
Q6: i' <= la*lb div lb by XREAL_1:21;
    la*lb div lb = la by Q8,NAT_1:68; then
    i' in Seg la by Q6,Q0,FINSEQ_1:3; then
K2: i' in dom F by F8,FINSEQ_1:def 3;
    per cases;
     suppose F.i' <> {}; then
      consider x' be set such that
NE2:   x' in F.i' by XBOOLE_0:def 1;
NE3:  f.x' = a.i' by K2,NE2,F1,MESFUNC3:def 1 .= a1.i by K3,AA2;
      F.i' in rng F by K2,FUNCT_1:12; then
      F.i' c= dom f by F7,ZFMISC_1:92; then
NE4:  0. <=' a1.i by NE2,AF3,NE3;
NE5:  FG.i in rng FG by A,F41,FUNCT_1:12;
      rng FG c= S by FINSEQ_1:def 4; then
      reconsider FGi = FG.i as Element of S by NE5;
      reconsider EMPTY = {} as Element of S by MEASURE1:21;
      EMPTY c= FGi by XBOOLE_1:2; then
NE6:  M.({}) <=' M.FGi by MEASURE1:62;
      M.{} = 0. by MEASURE1:def 11; then
      0. <=' (M*FG).i by NE6,A,F41,FUNCT_1:23;
      hence 0. <=' x1.i by P1,NE4,EXTREAL2:45;
     end;
     suppose
E1:   F.i' = {};
      FG.i = F.i' /\ G.((i-'1) mod lb + 1) by K3,FG1,FG2; then
      M.(FG.i) = 0. by E1,MEASURE1:def 11; then
      (M*FG).i = 0. by A,F41,FUNCT_1:23;
      hence 0. <=' x1.i by P1,EXTREAL1:22;
     end;
   end;
P6:for i be Nat st i in dom y1 holds 0. <=' y1.i
   proof
    let i be Nat;
    assume A: i in dom y1;
P1: y1.i=b1.i*(M*FG).i by G51,A; 
K3: i in Seg len FG by A,G41,FINSEQ_1:def 3;
    set i' = (i -' 1) mod lb + 1;
M3: 1 <= i & i <= la*lb by K3,FG1,FINSEQ_1:3;
Q0: i' >= 0+1 by XREAL_1:8;
    reconsider la'=la,lb'=lb as natural number;
    lb <> 0 by M3; then
Q7: lb > 0;
    (i -' 1) mod lb < lb by Q7,NAT_1:46; then
    i' <= lb by NAT_1:38; then
    i' in Seg lb by Q0,FINSEQ_1:3; then
K2: i' in dom G by G8,FINSEQ_1:def 3;
    per cases;
     suppose G.i' <> {}; then
      consider x' be set such that
NE2:   x' in G.i' by XBOOLE_0:def 1;
NE3:  g.x' = b.i' by K2,NE2,G1,MESFUNC3:def 1 .= b1.i by K3,AB2;
      G.i' in rng G by K2,FUNCT_1:12; then
      G.i' c= dom g by G7,ZFMISC_1:92; then
NE4:  0. <=' b1.i by NE2,AG3,NE3;
NE5:  FG.i in rng FG by A,G41,FUNCT_1:12;
      rng FG c= S by FINSEQ_1:def 4; then
      reconsider FGi = FG.i as Element of S by NE5;
      reconsider EMPTY = {} as Element of S by MEASURE1:21;
      EMPTY c= FGi by XBOOLE_1:2; then
NE6:  M.({}) <=' M.FGi by MEASURE1:62;
      M.{} = 0. by MEASURE1:def 11; then
      0. <=' (M*FG).i by NE6,A,G41,FUNCT_1:23;
      hence 0. <=' y1.i by P1,NE4,EXTREAL2:45;
     end;
     suppose
E1:   G.i' = {};
      FG.i = F.((i-'1) div lb + 1) /\ G.i' by K3,FG1,FG2; then
      M.(FG.i) = 0. by E1,MEASURE1:def 11; then
      (M*FG).i = 0. by A,G41,FUNCT_1:23;
      hence 0. <=' y1.i by P1,EXTREAL1:22;
     end;
   end;
   z1=x1+y1
   proof
    not -infty in rng x1
    proof
     assume -infty in rng x1; then
     consider i be set such that
V0:   i in dom x1 & x1.i = -infty by FUNCT_1:def 5;
     thus contradiction by V0,P5,SUPINF_2:19;
    end; then
U1: x1"{-infty} /\ y1"{+infty}
       = {} /\ y1"{+infty} by FUNCT_1:142
       .={};
    not -infty in rng y1
    proof
     assume -infty in rng y1; then
     consider i be set such that
V0:   i in dom y1 & y1.i = -infty by FUNCT_1:def 5;
     thus contradiction by V0,P6,SUPINF_2:19;
    end; then
U2: x1"{+infty} /\ y1"{-infty}
       =x1"{+infty} /\ {} by FUNCT_1:142
       .={};
P1: dom (x1+y1)
     =(dom x1 /\ dom y1) \ ({} \/ {}) by U1,U2,MESFUNC1:def 3
    .=dom x1 /\ dom y1
    .=dom x1 /\ dom x1 by F41,G41
    .=dom x1 
    .=dom FG by F41
    .=dom z1 by FG41;
P2: for k be Nat st k in dom z1 holds z1.k = (x1+y1).k
    proof
     let k be Nat;
     assume 
A:    k in dom z1; then
P4:  k in Seg len FG by FG41,FINSEQ_1:def 3;
     set p=(k-'1) div lb + 1;
     set q=(k-'1) mod lb + 1;
M3:  1 <= k & k <= la*lb by FG1,P4,FINSEQ_1:3;
Q0:  p >= 0+1 & q >= 0+1 by XREAL_1:8;
     reconsider la'=la,lb'=lb as natural number;
Q7:  lb <> 0 by M3; then
Q8:  lb > 0; then
     lb >= 0+1 by NAT_1:38; then
     lb' divides (la'*lb') & 1 <= la*lb & 1 <= lb 
       by M3,NAT_1:def 3,XREAL_1:2; then
Q3:  ((la'*lb') -' 1) div lb' = ((la'*lb') div lb') - 1 by NAT_2:17;
     (k -' 1) <= (la*lb -' 1) by M3,JORDAN3:5; then
     (k -' 1) div lb <= (la*lb div lb) - 1 by Q3,NAT_2:26; then
Q6:  p <= la*lb div lb by XREAL_1:21;
     p <= la by Q6,Q7,NAT_1:68; then
Pa1: p in Seg la by Q0,FINSEQ_1:3;
     (k -' 1) mod lb < lb by Q8,NAT_1:46; then
     q <= lb by NAT_1:38; then
Pb1: q in Seg lb by Q0,FINSEQ_1:3;
Q1:  p in dom F by F8,FINSEQ_1:def 3,Pa1;
Q2:  q in dom G by G8,FINSEQ_1:def 3,Pb1;
Z1:  for x be Element of ExtREAL st 0. <=' x holds
      0. = x or 0. <' x
     proof
      let x be Element of ExtREAL;
      assume 
V1:   0. <=' x;
      per cases;
       suppose x in REAL; then
        reconsider a = x as Real;
        0 <= a by V1,EXTREAL1:19;
        hence thesis by EXTREAL1:18,SUPINF_2:def 1;
       end;
       suppose not x in REAL; then
        x in {-infty,+infty} by XBOOLE_0:def 2,SUPINF_1:def 6;
       hence thesis by V1,SUPINF_2:19,TARSKI:def 2;
       end;
     end;
L1:  (a1.k+b1.k)*(M*FG).k = a1.k*(M*FG).k + b1.k*(M*FG).k
     proof
      per cases;
       suppose FG.k <> {}; then
        F.p /\ G.q <> {} by P4,FG1,FG2; then
        consider v be set such that
S2:     v in F.p /\ G.q by XBOOLE_0:def 1;
S3:     v in F.p & v in G.q by S2,XBOOLE_0:def 3; then
S4:     a.p = f.v by F1,Q1,MESFUNC3:def 1;
        F.p in rng F by Q1,FUNCT_1:12; then
        v in dom f by F7,S3,TARSKI:def 4; then
        0. <=' a.p by S4,AF3; then
        0. <=' a1.k by AA2,P4; then
S5:     0. = a1.k or 0. <' a1.k by Z1;
S6:     b.q = g.v by G1,Q2,S3,MESFUNC3:def 1;
        G.q in rng G by Q2,FUNCT_1:12; then
        v in dom g by G7,S3,TARSKI:def 4; then
        0. <=' b.q by S6,AG3; then
        0. <=' b1.k by AB2,P4; then
        0. = b1.k or 0. <' b1.k by Z1;
        hence thesis by S5,EXTREAL1:29;
       end;
       suppose FG.k = {}; then
        M.(FG.k) = 0. by MEASURE1:def 11; then
        (M*FG).k = 0. by A,FG41,FUNCT_1:23;
        hence thesis by SUPINF_2:19,EXTREAL1:29;
       end;
     end;
     thus z1.k = c1.k*(M*FG).k by FG51,A
              .= (a1.k+b1.k)*(M*FG).k by P4,AC2
              .= a1.k*(M*FG).k+b1.k*(M*FG).k by L1
              .= x1.k + b1.k*(M*FG).k by F51,A,FG41,F41
              .= x1.k + y1.k by G51,A,FG41,G41
              .= (x1+y1).k by P1,A,MESFUNC1:def 3;
    end;
    dom (x1+y1) = dom z1 by P1 .= Seg len z1 by FINSEQ_1:def 3; then
    x1+y1 is FinSequence by FINSEQ_1:def 2;
    hence thesis by P1,P2,FINSEQ_1:17;
   end;
   hence thesis by P1,P2,P3,P5,P6,P7,LM000;
end;

theorem
for X be non empty set,
    S be sigma_Field_Subset of X,
    M be sigma_Measure of S, 
    f,g be PartFunc of X,ExtREAL, c be R_eal 
  st f is_simple_func_in S
   & dom f <> {}
   & (for x be set st x in dom f holds 0. <=' f.x)
   & 0. <=' c & c <' +infty
   & dom g = dom f
   & (for x be set st x in dom g holds g.x=c*f.x)
 holds 
    integral(X,S,M,g)=c*integral(X,S,M,f) 
proof
   let X be non empty set,
       S be sigma_Field_Subset of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL,
       c be R_eal such that
AF1:f is_simple_func_in S and 
AF2:dom f <> {} and
AF3:for x be set st x in dom f holds 0. <=' f.x and
AF4:0. <=' c & c <' +infty and
AF5:dom g = dom f and
AF6:for x be set st x in dom g holds g.x=c*f.x;
   consider F be Finite_Sep_Sequence of S,
            a be FinSequence of ExtREAL,
            x be FinSequence of ExtREAL such that
F1: F,a are_Re-presentation_of f and
F4: dom x = dom F and
F5: for n be Nat st n in dom x holds x.n=a.n*(M*F).n and
F6: integral(X,S,M,f)=Sum(x) by AF1,AF2,AF3,LM006;
P0:f is_finite by AF1,MESFUNC2:def 5;
   for x be Element of X st x in dom g holds |. g.x .| <' +infty
   proof
    let x be Element of X;
    assume 
A:  x in dom g; then
    |. f.x .| <' +infty by AF5,P0,MESFUNC2:def 1; then
    f.x <> -infty & f.x <> +infty by EXTREAL2:67; then
    c*f.x <> +infty & c*f.x <> -infty by AF4,SUPINF_2:19,EXTREAL1:27,28; then
    g.x <> +infty & g.x <> -infty by A,AF6; then
    -infty <' g.x & g.x <' +infty by SUPINF_1:23,24; then
    -(+infty) <' g.x & g.x <' +infty by SUPINF_2:def 3;
    hence thesis by EXTREAL2:59;
   end; then
P1:g is_finite by MESFUNC2:def 1;
P2:ex G be Finite_Sep_Sequence of S st 
    (dom g = union rng G &
     for n be Nat, x,y be Element of X st
      n in dom G & x in G.n & y in G.n holds g.x = g.y)
   proof
    consider G be Finite_Sep_Sequence of S such that
P21: dom f = union rng G and
P22: for n be Nat, x,y be Element of X st
      n in dom G & x in G.n & y in G.n holds f.x = f.y by AF1,MESFUNC2:def 5;
P24:for n be Nat, x,y be Element of X st
     n in dom G & x in G.n & y in G.n holds g.x = g.y
    proof
     let n be Nat;
     let x,y be Element of X;
     assume 
P25: n in dom G & x in G.n & y in G.n; then
     G.n in rng G by FUNCT_1:12; then
     x in dom g & y in dom g by AF5,P21,P25,TARSKI:def 4; then
     g.x = c*f.x & g.y = c*f.y by AF6;
     hence thesis by P25,P22;
    end;
    take G;
    thus thesis by AF5,P21,P24;
   end;
BF1:g is_simple_func_in S by P1,P2,MESFUNC2:def 5;
BF3:for x be set st x in dom g holds 0. <=' g.x
   proof
    let x be set;
    assume 
A:  x in dom g; then
    0. <=' f.x by AF5,AF3; then
    0. <=' c*f.x by AF4,EXTREAL2:45;
    hence thesis by A,AF6;
   end;
   consider G be Finite_Sep_Sequence of S,
            b be FinSequence of ExtREAL;
   deffunc bMG(Nat) = b.$1 * (M*G).$1;
   consider y be FinSequence such that
K1: len y = len G &
    for n be Nat st n in Seg len G holds y.n = bMG(n)
     from FINSEQ_1:sch 2;
   rng y c= ExtREAL
   proof
     let v be set;
     assume v in rng y; then
     consider k be set such that
K2:  k in dom y & v = y.k by FUNCT_1:def 5;
     reconsider k as Nat by K2;
     k in Seg len G by K1,K2,FINSEQ_1:def 3; then
     v = b.k * (M*G).k by K1,K2;
     hence thesis;
   end; then
   reconsider y as FinSequence of ExtREAL by FINSEQ_1:def 4;
     ex b be FinSequence of ExtREAL st
      dom b = dom a &
      for n be Nat st n in dom b holds b.n=c*a.n
     proof
      deffunc ca(Nat) = c*a.$1;
      consider b be FinSequence such that
G11:  len b = len a &
      for n be Nat st n in Seg len a holds b.n = ca(n) from FINSEQ_1:sch 2;
G12:  dom b = Seg len b by FINSEQ_1:def 3;
      rng b c= ExtREAL
      proof
        let v be set;
        assume v in rng b; then
        consider k be set such that
K2:     k in dom b & v = b.k by FUNCT_1:def 5;
        reconsider k as Nat by K2;
        v = c*a.k by G11,G12,K2;
        hence thesis;
      end; then
      reconsider b as FinSequence of ExtREAL by FINSEQ_1:def 4;
      take b;
      thus thesis by G11,G12,FINSEQ_1:def 3;
     end; then
     consider b be FinSequence of ExtREAL such that 
G1:  dom b = dom a and
G2:  for n be Nat st n in dom b holds b.n=c*a.n;
     ex z be FinSequence of ExtREAL st
     dom z = dom x & for n be Nat st n in dom z holds z.n=c*x.n
     proof
      deffunc cx(Nat) = c*x.$1;
      consider z be FinSequence such that
G11:  len z = len x &
      for n be Nat st n in Seg len x holds z.n = cx(n) from FINSEQ_1:sch 2;
G12:  dom z = Seg len z by FINSEQ_1:def 3;
      rng z c= ExtREAL
      proof
        let v be set;
        assume v in rng z; then
        consider k be set such that
K2:     k in dom z & v = z.k by FUNCT_1:def 5;
        reconsider k as Nat by K2;
        v = c*x.k by G11,G12,K2;
        hence thesis;
      end; then
      reconsider z as FinSequence of ExtREAL by FINSEQ_1:def 4;
      take z;
      thus thesis by G11,G12,FINSEQ_1:def 3;
     end; then
     consider z be FinSequence of ExtREAL such that 
G3:   dom z = dom x and
G4:   for n be Nat st n in dom z holds z.n=c*x.n;
H1:  F,b are_Re-presentation_of g
     proof
P1:   dom g = dom f by AF5 .=union rng F by F1,MESFUNC3:def 1;
P2:   dom F = dom a by F1,MESFUNC3:def 1 .= dom b by G1;
      now let n be Nat;
       assume 
A:     n in dom F;
       let x be set;
       assume
B:     x in F.n;
       F.n in rng F by A,FUNCT_1:12; then
B2:    x in dom g by P1,B,TARSKI:def 4;
B3:    n in dom b by A,F1,MESFUNC3:def 1,G1;
       thus g.x= c*f.x by B2,AF6
              .= c*a.n by B,F1,A,MESFUNC3:def 1 .= b.n by B3,G2;
      end;
      hence thesis by P1,P2,MESFUNC3:def 1;
     end;
H4:  dom z = dom x by G3 .=dom F by F4;
H5:  for n be Nat st n in dom z holds z.n=b.n*(M*F).n 
     proof
      let n be Nat;
      assume 
A:    n in dom z;
A1:   dom a = dom F by F1,MESFUNC3:def 1;
      thus z.n = c*x.n by G4,A .=c*(a.n*(M*F).n) by A,F5,G3
              .= c*a.n*((M*F).n) by EXTREAL1:23
              .=b.n*(M*F).n by G2,A,G1,A1,H4;
     end;
     thus integral(X,S,M,g)=Sum z by BF1,AF2,AF5,BF3,H1,H4,H5,LM005
                 .=c*Sum x by AF4,SUPINF_2:19,G3,G4,MESFUNC3:10
                 .=c*integral(X,S,M,f) by F6;
end;
