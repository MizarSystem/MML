:: Linearity of {L}ebesgue Integral of Simple Valued Function
::  by Noboru Endou and Yasunari Shidama
::
:: Received September 14, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies PARTFUN1, SUPINF_1, MEASURE1, RELAT_1, FUNCT_1, COMPLEX1, BOOLE,
      ARYTM_1, TARSKI, ARYTM_3, NAT_1, RLVECT_1, GROUP_1, FINSEQ_1, INTEGRA1,
      MESFUNC2, MESFUNC3, ARYTM, ORDINAL2, PROB_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XXREAL_0,
      BINARITH, RELAT_1, FUNCT_1, PARTFUN1, FUNCT_2, FINSEQ_1, NAT_1, NAT_D,
      PROB_1, SUPINF_1, SUPINF_2, MEASURE1, EXTREAL1, MESFUNC1, MESFUNC2,
      MESFUNC3, CONVFUN1;
 constructors SETFAM_1, PARTFUN1, REAL_1, NAT_1, NAT_D, FINSEQOP, FINSOP_1,
      EXTREAL1, MESFUNC1, BINARITH, MESFUNC2, CONVFUN1, MESFUNC3;
 registrations XBOOLE_0, ORDINAL1, RELSET_1, ARYTM_3, NUMBERS, XXREAL_0,
      XREAL_0, NAT_1, FINSEQ_1, MEASURE1;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;
 definitions MESFUNC3, TARSKI, XBOOLE_0, SUPINF_2;
 theorems MEASURE1, TARSKI, FUNCT_1, NAT_1, REAL_1, SUPINF_2, RELSET_1,
      EXTREAL1, EXTREAL2, MESFUNC1, ZFMISC_1, FINSEQ_1, FINSEQ_2, FINSEQ_5,
      PROB_2, XBOOLE_0, XBOOLE_1, RELAT_1, MESFUNC2, CONVFUN1, MESFUNC3,
      BINARITH, XREAL_1, NAT_2, FINSEQ_3, XXREAL_0, PROB_1, FINSUB_1, ORDINAL1,
      NAT_D;
 schemes FINSEQ_1, FINSEQ_2, NAT_1;

begin  :: Linearity of Lebesgue Integral of Simple Valued Function

theorem Th1:
for F,G,H be FinSequence of ExtREAL
 st (for i be Nat st i in dom F holds 0. <= F.i)
  & (for i be Nat st i in dom G holds 0. <= G.i)
   & dom F = dom G & H = F + G
 holds
  Sum(H)=Sum(F)+Sum(G)
proof
   let F,G,H be FinSequence of ExtREAL;
   assume that
A1:for i be Nat st i in dom F holds 0. <= F.i and
A2:for i be Nat st i in dom G holds 0. <= G.i and
A3:dom F = dom G and
A4:H = F + G;
   consider f be Function of NAT,ExtREAL such that
A5: Sum(F) = f.(len F) & f.0 = 0. &
    for i be Element of NAT st i < len F holds f.(i+1) = f.i + F.(i+1)
    by CONVFUN1:def 5;
   consider g be Function of NAT,ExtREAL such that
A6: Sum(G) = g.(len G) & g.0 = 0. &
    for i be Element of NAT st i < len G holds g.(i+1) = g.i + G.(i+1)
    by CONVFUN1:def 5;
   consider h be Function of NAT,ExtREAL such that
A7: Sum(H) = h.(len H) & h.0 = 0. &
    for i be Element of NAT st i < len H holds h.(i+1) = h.i + H.(i+1)
    by CONVFUN1:def 5;
   for y be set st y in rng F holds not y in {-infty}
   proof
    let y be set;
    assume y in rng F; then
    consider x be set such that
A8:  x in dom F & y = F.x by FUNCT_1:def 5;
    reconsider x as Element of NAT by A8;
    0. <= F.x by A1,A8;
    hence thesis by A8,TARSKI:def 1;
   end; then
   rng F misses {-infty} by XBOOLE_0:3; then
A9:F"{-infty} = {} by RELAT_1:173;
   for y be set st y in rng G holds not y in {-infty}
   proof
    let y be set;
    assume y in rng G; then
    consider x be set such that
A10: x in dom G & y = G.x by FUNCT_1:def 5;
    reconsider x as Element of NAT by A10;
    0. <= G.x by A2,A10;
    hence thesis by A10,TARSKI:def 1;
   end; then
   rng G misses {-infty} by XBOOLE_0:3; then
A11:G"{-infty} = {} by RELAT_1:173;
A12:dom H
     = (dom F /\ dom G)\((F"{-infty}/\G"{+infty})\/(F"{+infty}/\G"{-infty}))
       by A4,MESFUNC1:def 3
    .= dom F by  A9,A11,A3; then
A13:len H = len F by FINSEQ_3:31;
A14:len H = len G by A12,A3,FINSEQ_3:31;
    defpred P[Nat] means $1 <= len H implies h.$1 = f.$1 + g.$1;
A15:P[0] by A5,A6,A7,SUPINF_2:18;
A16:for k being Nat st P[k] holds P[k + 1]
   proof
    let k be Nat;
    assume A17:P[k];
    assume A18:k+1 <= len H;
    reconsider k as Element of NAT by ORDINAL1:def 13;
    1 <= k+1 by NAT_1:11; then
A19: k+1 in dom H by A18,FINSEQ_3:27;
A20:k < len H by A18,NAT_1:13; then
A21:f.(k+1) = f.k + F.(k+1) & g.(k+1) = g.k + G.(k+1) & h.(k+1) = h.k + H.(k+1)
     by A5,A6,A7,A13,A14;
A22:h.(k+1) = (f.k + g.k) + H.(k+1) by A17,A20,A7
           .= (f.k + g.k) + (F.(k+1) + G.(k+1)) by A19,A4,MESFUNC1:def 3;
A23:f.k <> -infty & g.k <> -infty & F.(k+1) <> -infty & G.(k+1) <> -infty
    proof
     defpred Pf[Nat] means
     $1 <= len H implies f.$1 <> -infty;
A24:  Pf[0] by A5;
A25:  for m be Nat st Pf[m] holds Pf[m+1]
     proof
       let m be Nat;
       assume A26:Pf[m];
       assume A27:m+1 <= len H;
       reconsider m as Element of NAT by ORDINAL1:def 13;
       m < len H by NAT_1:13,A27; then
A28:    f.(m+1) = f.m + F.(m+1) by A5,A13;
       1 <= m+1 by NAT_1:11; then
       m+1 in dom H by A27,FINSEQ_3:27; then
       -infty < 0. & 0. <= F.(m+1) by A1,A12;
       hence thesis by A26,A27,A28,NAT_1:13,SUPINF_2:9;
     end;
     for i be Nat holds Pf[i] from NAT_1:sch 2(A24,A25);
     hence f.k <> -infty by A20;
     defpred Pg[Nat] means
     $1 <= len H implies g.$1 <> -infty;
A29:  Pg[0] by A6;
A30:  for m be Nat st Pg[m] holds Pg[m+1]
     proof
       let m be Nat;
       assume A31:Pg[m];
       assume A32:m+1 <= len H;
       reconsider m as Element of NAT by ORDINAL1:def 13;
       m < len H by NAT_1:13,A32; then
A33:    g.(m+1) = g.m + G.(m+1) by A6,A14;
       1 <= m+1 by NAT_1:11; then
       m+1 in dom H by A32,FINSEQ_3:27; then
       -infty < 0. & 0. <= G.(m+1) by A2,A3,A12;
       hence thesis by A31,A32,A33,NAT_1:13,SUPINF_2:9;
     end;
     for i be Nat holds Pg[i] from NAT_1:sch 2(A29,A30);
     hence g.k <> -infty by A20;
     thus F.(k+1) <> -infty by A1,A19,A12;
     thus G.(k+1) <> -infty by A2,A3,A19,A12;
    end; then
A34:f.k + g.k <> -infty & F.(k+1) + G.(k+1) <> -infty &
    f.k + F.(k+1) <> -infty by SUPINF_2:9; then
    h.(k+1) = ((f.k + g.k) + F.(k+1)) + G.(k+1) by A22,A23,EXTREAL1:8
           .= (f.k + F.(k+1) + g.k) + G.(k+1) by  A23,EXTREAL1:8
           .= f.(k+1) + g.(k+1) by  A23,A34,EXTREAL1:8,A21;
    hence thesis;
   end;
A35:for i be Nat holds P[i] from NAT_1:sch 2(A15,A16);
   Sum H = f.(len F) + g.(len G) by  A7,A35,A13,A14;
   hence thesis by A5,A6;
end;

theorem Th2:
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S
 holds
   for n be Nat,
       f be PartFunc of X,ExtREAL,
       F be Finite_Sep_Sequence of S,
       a,x be FinSequence of ExtREAL
    st f is_simple_func_in S
     & dom f <> {}
     & (for x be set st x in dom f holds 0. <= f.x)
     & F,a are_Re-presentation_of f
     & dom x = dom F
     & (for i be Nat st i in dom x holds x.i=a.i*(M*F).i)
     & len F = n
   holds
    integral(X,S,M,f)=Sum(x)
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   defpred P[Nat] means
   for f be PartFunc of X,ExtREAL, F be Finite_Sep_Sequence of S,
       a,x be FinSequence of ExtREAL st
        f is_simple_func_in S
      & dom f <> {}
      & (for x be set st x in dom f holds 0. <= f.x)
      & F,a are_Re-presentation_of f
      & dom x = dom F
      & (for i be Nat st i in dom x holds x.i=a.i*(M*F).i)
      & len F = $1
      holds integral(X,S,M,f)=Sum(x);
A1:P[0]
   proof
    let f be PartFunc of X,ExtREAL;
    let F be Finite_Sep_Sequence of S;
    let a,x be FinSequence of ExtREAL;
    assume that
     f is_simple_func_in S and
A2:  dom f <> {} and
     (for x be set st x in dom f holds 0. <= f.x) and
A3:  F,a are_Re-presentation_of f and
     dom x = dom F and
     (for i be Nat st i in dom x holds x.i = a.i*(M*F).i) and
A4:  len F = 0;
    dom F = {} by  FINSEQ_1:def 3,A4,FINSEQ_1:4; then
    union rng F = {} by ZFMISC_1:2,RELAT_1:65;
    hence thesis by A2,A3,MESFUNC3:def 1;
   end;
A5:for n be Nat st P[n] holds P[n+1]
   proof
    let n be Nat;
    assume
A6:  P[n];
     let f be PartFunc of X,ExtREAL, F be Finite_Sep_Sequence of S,
        a,x be FinSequence of ExtREAL such that
A7:   f is_simple_func_in S and
A8:   dom f <> {} and
A9:   (for x be set st x in dom f holds 0. <= f.x) and
A10:   F,a are_Re-presentation_of f and
A11:   dom x = dom F and
A12:   (for i be Nat st i in dom x holds x.i=a.i*(M*F).i) and
A13:   len F = n+1;
     reconsider n as Element of NAT by ORDINAL1:def 13;
     set F1=F|(Seg n);
     set a1=a|(Seg n);
     set x1=x|(Seg n);
     set f1=f|(union rng F1);
     reconsider F1 as Finite_Sep_Sequence of S by MESFUNC2:36;
     reconsider a1 as FinSequence of ExtREAL by FINSEQ_1:23;
     reconsider x1 as FinSequence of ExtREAL by FINSEQ_1:23;
A14:  f is_finite by A7,MESFUNC2:def 5;
     for x be Element of X st x in dom f1 holds |. f1.x .| < +infty
     proof
      let x be Element of X;
      assume A15:x in dom f1; then
      x in (dom f)/\(union rng F1) by FUNCT_1:68; then
A16:   x in dom f by XBOOLE_0:def 3;
      f1.x = f.x by A15,FUNCT_1:70;
      hence thesis by A16,A14,MESFUNC2:def 1;
     end; then
A17:  f1 is_finite by MESFUNC2:def 1;
A18: dom f1 = union rng F1
     proof
A19:   dom f1 = (dom f) /\ (union rng F1) by FUNCT_1:68
            .= (union rng F) /\ (union rng F1) by A10,MESFUNC3:def 1;
      rng F1 c= rng F by RELAT_1:99; then
      union rng F1 c= union rng F by ZFMISC_1:95;
      hence thesis by A19,XBOOLE_1:28;
     end;
A20:  f1 is_simple_func_in S
     proof
      ex F1' be Finite_Sep_Sequence of S st
       (dom f1 = union rng F1' &
        for n be Nat, x,y be Element of X st
         n in dom F1' & x in F1'.n & y in F1'.n holds f1.x = f1.y)
      proof
       take F1' = F1;
       for n be Nat, x,y be Element of X st
        n in dom F1 & x in F1.n & y in F1.n holds f1.x = f1.y
       proof
        let n be Nat;
        let x,y be Element of X;
        assume
A21:     n in dom F1 & x in F1.n & y in F1.n; then
        F1.n in rng F1 by FUNCT_1:12; then
        F1.n c= dom f1 by A18,ZFMISC_1:92; then
A22:     f1.x = f.x & f1.y = f.y by A21,FUNCT_1:70;
A23:     F1.n = F.n by A21,FUNCT_1:70;
        dom F1 c= dom F by RELAT_1:89; then
        f.x = a.n & f.y = a.n by A21,A23,A10,MESFUNC3:def 1;
        hence thesis by A22;
       end;
       hence thesis by A18;
      end;
      hence thesis by A17,MESFUNC2:def 5;
     end;
A24:  for x be set st x in dom f1 holds 0. <= f1.x
     proof
      let x be set;
      assume
A25:  x in dom f1;
      dom f1 c= dom f by RELAT_1:89; then
      x in dom f & f1.x = f.x by A25,FUNCT_1:70;
      hence thesis by A9;
     end;
A26: dom F1 = dom F /\ Seg n by FUNCT_1:68
           .= dom a /\ Seg n by A10,MESFUNC3:def 1
           .= dom a1 by FUNCT_1:68;
     for n be Nat st n in dom F1
      for x be set st x in F1.n holds f1.x = a1.n
     proof
      let n be Nat;
      assume A27:n in dom F1;
      dom F1 c= dom F & dom a1 c= dom a by RELAT_1:89; then
A28:  n in dom F & F1.n = F.n & n in dom a & a1.n = a.n by A27,A26,FUNCT_1:70;
      let x be set;
      assume A29:x in F1.n;
      F1.n in rng F1 by A27,FUNCT_1:12; then
      F1.n c= dom f1 by A18,ZFMISC_1:92; then
      f1.x = f.x by A29,FUNCT_1:70;
      hence thesis by A28,A29,A10,MESFUNC3:def 1;
     end; then
A30:  F1,a1 are_Re-presentation_of f1 by A18,A26,MESFUNC3:def 1;
A31:  dom x1 = dom F /\ Seg n by  FUNCT_1:68,A11
           .= dom F1 by FUNCT_1:68;
     now per cases;
      suppose
A32:   dom f1 = {};
A33:    for m be Nat st m in dom F1 holds F1.m = {}
       proof
        let m be Nat;
        assume m in dom F1; then
        F1.m in rng F1 by FUNCT_1:12; then
        F1.m c= {} by A32,A18,ZFMISC_1:92;
        hence thesis by XBOOLE_1:3;
       end;
A34:    for m be Nat st m in dom F1 holds x.m = 0.
       proof
        let m be Nat;
        assume
A35:    m in dom F1;
        m in dom F /\ (Seg n) by A35,FUNCT_1:68; then
A36:    m in dom x by A11,XBOOLE_0:def 3;
        F1.m = {} by A33,A35; then
A37:    F.m = {} by A35,FUNCT_1:70;
        reconsider EMPTY = {} as Element of S by PROB_1:10;
        M.EMPTY = 0. by MEASURE1:def 11; then
A38:    (M*F).m = 0. by A37,A36,A11,FUNCT_1:23;
        x.m = a.m*(M*F).m by A36,A12;
        hence thesis by A38,EXTREAL1:22;
       end;
       consider sumx be Function of NAT,ExtREAL such that
A39:     Sum x = sumx.(len x) & sumx.0 = 0. &
        for m be Element of NAT st m < len x holds
        sumx.(m+1) = sumx.m + x.(m+1)
         by CONVFUN1:def 5;
A40:   Seg len x = dom F by  FINSEQ_1:def 3,A11
                .= Seg(n+1) by  FINSEQ_1:def 3,A13; then
A41:    len x = n+1 by FINSEQ_1:8;
       defpred PSumx[Nat] means $1 < len x implies sumx.$1 = 0.;
A42:    PSumx[0] by A39;
A43:    for m be Nat st PSumx[m] holds PSumx[m+1]
       proof
        let m be Nat;
        assume
A44:    PSumx[m];
        assume
A45:    m+1 < len x;
   m <= m+1 by NAT_1:11; then
A46:    m < len x by A45,XXREAL_0:2;
A47:    1 <= m+1 & m+1 + 1 <= len x by A45,NAT_1:11,13; then
        m+1 in Seg len x by A45,FINSEQ_1:3; then
A48:    m+1 in dom F by A11,FINSEQ_1:def 3;
        m+1 <= n by A41,A45,NAT_1:13; then
        m+1 in Seg n by A47,FINSEQ_1:3; then
        m+1 in dom F /\ Seg n by A48,XBOOLE_0:def 3; then
A49:    m+1 in dom F1 by FUNCT_1:68;
        m in NAT by ORDINAL1:def 13;
        then sumx.(m+1) = 0. + x.(m+1) by  A39,A46,A44
                  .= x.(m+1) by SUPINF_2:18;
        hence thesis by A49,A34;
       end;
A50:    for m be Nat holds PSumx[m] from NAT_1:sch 2(A42,A43);
A51:    n < len x by A41,NAT_1:13;
A52:  Sum x = sumx.(n+1) by  A39,A40,FINSEQ_1:8
            .= sumx.n + x.(n+1) by A51,A39
            .= 0. + x.(n+1) by A51,A50
            .= x.(n+1) by SUPINF_2:18;
       1 <= n+1 & n+1 <= len F by A13,NAT_1:11; then
A53:    n+1 in dom F by FINSEQ_3:27;
A54:    union rng F = (union rng F1) \/ F.(n+1)
       proof
        thus union rng F c= (union rng F1) \/ F.(n+1)
        proof
         let v be set;
         assume v in union rng F; then
         consider A be set such that
A55:      v in A & A in rng F by TARSKI:def 4;
         consider i be set such that
A56:      i in dom F & A = F.i by A55,FUNCT_1:def 5;
         reconsider i as Element of NAT by A56;
         i in Seg len F by A56,FINSEQ_1:def 3; then
A57:      1 <= i & i <= n+1 by A13,FINSEQ_1:3;
         per cases;
          suppose i = n+1;
           hence thesis by A55,A56,XBOOLE_0:def 2;
          end;
          suppose i <> n+1; then
           i < n+1 by A57,REAL_1:def 5; then
           i <= n by NAT_1:13; then
           i in Seg n by A57,FINSEQ_1:3; then
           i in dom F /\ Seg n by A56,XBOOLE_0:def 3; then
           i in dom F1 by FUNCT_1:68; then
           F1.i = A & F1.i in rng F1 by A56,FUNCT_1:12,70; then
           v in union rng F1 by A55,TARSKI:def 4;
           hence thesis by XBOOLE_0:def 2;
          end;
        end;
         let v be set;
         assume
A58:      v in (union rng F1) \/ F.(n+1);
         per cases by A58,XBOOLE_0:def 2;
          suppose v in union rng F1; then
           consider A be set such that
A59:        v in A & A in rng F1 by TARSKI:def 4;
           consider i be set such that
A60:        i in dom F1 & A = F1.i by A59,FUNCT_1:def 5;
           i in dom F /\ Seg n by A60,FUNCT_1:68; then
A61:        i in dom F by XBOOLE_0:def 3;
           F.i = A by A60,FUNCT_1:70; then
           A in rng F by A61,FUNCT_1:12;
           hence v in union rng F by A59,TARSKI:def 4;
          end;
          suppose
A62:       v in F.(n+1);
           F.(n+1) in rng F by A53,FUNCT_1:12;
           hence v in union rng F by A62,TARSKI:def 4;
          end;
       end;
       now per cases;
        case
A63: a.(n+1) <> 0.;
         defpred PG[Nat,set] means
         ($1 = 1 implies $2 = union rng F1) &
         ($1 = 2 implies $2 = F.(n+1));
A64:      for k be Element of NAT st k in Seg 2
          ex x being Element of S st PG[k,x]
         proof
          let k be Element of NAT;
          assume
A65:      k in Seg 2;
          per cases by A65,FINSEQ_1:4,TARSKI:def 2;
           suppose
A66:        k = 1;
            set x = union rng F1;
            reconsider x as Element of S by MESFUNC2:34;
            take x;
            thus (k = 1 implies x = union rng F1) &
             (k = 2 implies x = F.(n+1)) by A66;
           end;
           suppose
A67:        k = 2;
            set x = F.(n+1);
            F.(n+1) in rng F & rng F c= S by A53,FUNCT_1:12,FINSEQ_1:def 4;
then
            reconsider x as Element of S;
            take x;
            thus (k = 1 implies x = union rng F1) &
             (k = 2 implies x = F.(n+1)) by A67;
           end;
         end;
         consider G be FinSequence of S such that
A68:      dom G = Seg 2 &
         for k be Element of NAT st k in Seg 2 holds PG[k,G.k]
         from FINSEQ_1:sch 5(A64);
A69:     Seg len G = Seg 2 by  FINSEQ_1:def 3,A68; then
A70:      len G = 2 by FINSEQ_1:8;
         1 in Seg 2 by FINSEQ_1:4,TARSKI:def 2;then
A71:      G.1 = union rng F1 by A68;
         2 in Seg 2 by FINSEQ_1:4,TARSKI:def 2;then
A72:      G.2 = F.(n+1) by A68;
A73:   G = <* union rng F1, F.(n+1) *> by A70,A71,A72,FINSEQ_1:61;
         defpred Pb[Nat,set] means
         ($1 = 1 implies $2 = 0.) &
         ($1 = 2 implies $2 = a.(n+1));
A74:      for k be Element of NAT st k in Seg 2
          ex x being Element of ExtREAL st Pb[k,x]
         proof
          let k be Element of NAT;
          assume
A75:      k in Seg 2;
          per cases by A75,FINSEQ_1:4,TARSKI:def 2;
           suppose
A76:        k = 1;
            set x = 0.;
            reconsider x as Element of ExtREAL;
            take x;
            thus (k = 1 implies x = 0.) &
             (k = 2 implies x = a.(n+1)) by A76;
           end;
           suppose
A77:        k = 2;
            set x = a.(n+1);
            reconsider x as Element of ExtREAL;
            take x;
            thus (k = 1 implies x = 0.) &
             (k = 2 implies x = a.(n+1)) by A77;
           end;
         end;
         consider b be FinSequence of ExtREAL such that
A78:      dom b = Seg 2 &
         for k be Element of NAT st k in Seg 2 holds Pb[k,b.k]
         from FINSEQ_1:sch 5(A74);
A79:     Seg len b = Seg 2 by  FINSEQ_1:def 3,A78;
         1 in Seg 2 by FINSEQ_1:4,TARSKI:def 2;then
A80:      b.1 = 0. by A78;
         2 in Seg 2 by FINSEQ_1:4,TARSKI:def 2; then
A81:      b.2 = a.(n+1) by A78;
         deffunc Fy(Nat) = b.$1*(M*G).$1;
         consider y be FinSequence of ExtREAL such that
A82:      len y = 2 &
          for m be Element of NAT st m in Seg 2 holds y.m = Fy(m)
          from FINSEQ_2:sch 1;
         1 in Seg 2 by FINSEQ_1:4,TARSKI:def 2; then
         y.1 = b.1*(M*G).1 by A82;then
A83:      y.1 = 0. by A80,EXTREAL1:22;
A84:      y.2 = x.(n+1)
         proof
A85:       2 in Seg 2 by FINSEQ_1:4,TARSKI:def 2; then
A86:       (M*G).2 = M.(F.(n+1)) by  A68,FUNCT_1:23,A72
                 .= (M*F).(n+1) by A53,FUNCT_1:23;
          y.2 = a.(n+1)*(M*F).(n+1) by  A82,A85,A81,A86;
          hence thesis by A11,A12,A53;
         end;
         consider sumy be Function of NAT,ExtREAL such that
A87:     Sum y = sumy.len y & sumy.0 = 0. &
         for k be Element of NAT st k < len y holds
         sumy.(k+1) = sumy.k + y.(k+1)
           by CONVFUN1:def 5;
A88:   sumy.1 = sumy.0 + y.(0+1) by A87,A82
               .= 0. by  A87,A83,SUPINF_2:18;
A89:    Sum y = sumy.(1+1) by A82,A87
               .= 0. + x.(n+1) by  A82,A87,A88,A84
               .= x.(n+1) by SUPINF_2:18;
         for u,v be set st u <> v holds G.u misses G.v
         proof
          let u,v be set;
          assume
A90:      u <> v;
          per cases;
           suppose u in dom G & v in dom G; then
A91:        (u = 1 or u = 2) & (v = 1 or v = 2) by A68,FINSEQ_1:4,TARSKI:def 2;
            per cases by A90,A91;
             suppose
A92:          u = 1 & v = 2;
              assume G.u meets G.v; then
               consider z be set such that
A93:           z in G.u & z in G.v by XBOOLE_0:3;
               consider A be set such that
A94:           z in A & A in rng F1 by A92,A71,A93,TARSKI:def 4;
               consider k' be set such that
A95:           k' in dom F1 & A = F1.k' by A94,FUNCT_1:def 5;
               reconsider k' as Element of NAT by A95;
               k' in dom F /\ Seg n by A95,FUNCT_1:68; then
               k' in dom F & k' in Seg n by XBOOLE_0:def 3; then
               k' <= n by FINSEQ_1:3; then
               k' < n+1 by NAT_1:13; then
A96:           F.k' misses F.(n+1) by PROB_2:def 3;
               z in F.k' & z in F.(n+1) by A94,A93,A92,A72,A95,FUNCT_1:70;
               hence contradiction by A96,XBOOLE_0:3;
             end;
             suppose
A97:          u = 2 & v = 1;
              assume G.u meets G.v; then
              consider z be set such that
A98:          z in G.u & z in G.v by XBOOLE_0:3;
              consider A be set such that
A99:          z in A & A in rng F1 by A97,A71,A98,TARSKI:def 4;
              consider k' be set such that
A100:          k' in dom F1 & A = F1.k' by A99,FUNCT_1:def 5;
              reconsider k' as Element of NAT by A100;
              k' in dom F /\ Seg n by A100,FUNCT_1:68; then
              k' in dom F & k' in Seg n by XBOOLE_0:def 3; then
              k' <= n by FINSEQ_1:3; then
              k' < n+1 by NAT_1:13; then
A101:          F.k' misses F.(n+1) by PROB_2:def 3;
              z in F.k' & z in F.(n+1) by A99,A98,A97,A72,A100,FUNCT_1:70;
              hence contradiction by A101,XBOOLE_0:3;
             end;
           end;
           suppose not u in dom G or not v in dom G; then
            G.u = {} or G.v = {} by FUNCT_1:def 4;
            hence thesis by XBOOLE_1:65;
           end;
         end; then
         reconsider G as Finite_Sep_Sequence of S by PROB_2:def 3;
A102:      dom f = (union rng F1) \/ F.(n+1) by  A10,MESFUNC3:def 1,A54
              .= union {union rng F1,F.(n+1)} by ZFMISC_1:93
              .= union rng G by A73,FINSEQ_2:147;
A103:      dom G = Seg 2 by  FINSEQ_1:def 3,A69
              .= dom b by  A79,FINSEQ_1:def 3;
         G,b are_Re-presentation_of f &
         b.1 = 0. &
         (for m be Nat st 2 <= m & m in dom b holds
         0. < b.m & b.m < +infty) & dom y = dom G &
         (for m be Nat st m in dom y holds y.m = b.m*(M*G).m)
         proof
A104:       for k be Nat st k in dom G
           for v be set st v in G.k holds f.v = b.k
          proof
           let k be Nat;
           assume
A105:        k in dom G;
           let v be set;
           assume
A106:        v in G.k;
           per cases by A105,A68,FINSEQ_1:4,TARSKI:def 2;
            suppose k = 1;
             hence thesis by A71,A106,A32,A18;
            end;
            suppose k = 2;
             hence thesis by A72,A106,A81,A53,A10,MESFUNC3:def 1;
            end;
          end;
          hence G,b are_Re-presentation_of f by A102,A103,MESFUNC3:def 1;
          thus b.1 = 0. by A80;
          thus for k be Nat st 2 <= k & k in dom b holds
            0. < b.k & b.k < +infty
          proof
           let k be Nat;
           assume
A107:        2 <= k & k in dom b; then
A108:       k = 1 or k = 2 by A78,FINSEQ_1:4,TARSKI:def 2;
A109:       union rng F = F.(n+1) by A18,A32,A54; then
           G.k <> {} by A108,A107,A72,A10,A8,MESFUNC3:def 1; then
           consider v be set such that
A110:       v in G.k by XBOOLE_0:def 1;
A111:       f.v = b.k by A104,A110,A107,A68,A78;
A112:       v in dom f by A110,A108,A107,A72,A109,A10,MESFUNC3:def 1; then
           0. <= b.k & b.k <> 0. by A9,A111,A108,A107,A81,A63;
           hence 0. < b.k;
           dom f c= X by RELSET_1:12; then
           reconsider v as Element of X by A112;
           |. f.v .| < +infty by A14,A112,MESFUNC2:def 1;
           hence b.k < +infty by A111,EXTREAL2:58;
          end;
          thus dom y = Seg 2 by  FINSEQ_1:def 3,A82
                    .= dom G by  A69,FINSEQ_1:def 3;
          thus for k be Nat st k in dom y holds y.k = b.k*(M*G).k
          proof
           let k be Nat;
           assume k in dom y; then
           k in Seg len y by FINSEQ_1:def 3;
           hence thesis by A82;
          end;
         end;
         hence integral(X,S,M,f) = Sum(x) by  A7,A8,A9,A89,MESFUNC3:def 2,A52;
        end;
        case
A113:    a.(n+1) = 0.;
         1 <= n+1 & n+1 <= len x by NAT_1:11,A40,FINSEQ_1:8; then
         n+1 in dom x by FINSEQ_3:27; then
A114:     x.(n+1) = a.(n+1)*(M*F).(n+1) by A12 .= 0. by A113,EXTREAL1:22;
         defpred PG[Nat,set] means
         ($1 = 1 implies $2 = union rng F) &
         ($1 = 2 implies $2 = {});
A115:    for k be Element of NAT st k in Seg 2
          ex v being Element of S st PG[k,v]
         proof
          let k be Element of NAT;
          assume
A116:      k in Seg 2;
          per cases by A116,FINSEQ_1:4,TARSKI:def 2;
           suppose
A117:        k = 1;
            set v = union rng F;
            reconsider v as Element of S by MESFUNC2:34;
            take v;
            thus (k = 1 implies v = union rng F) &
             (k = 2 implies v = {}) by A117;
           end;
           suppose
A118:        k = 2;
            reconsider v = {} as Element of S by PROB_1:10;
            take v;
            thus (k = 1 implies v = union rng F) &
             (k = 2 implies v = {}) by A118;
           end;
         end;
         consider G be FinSequence of S such that
A119:      dom G = Seg 2 &
         for k be Element of NAT st k in Seg 2 holds PG[k,G.k]
         from FINSEQ_1:sch 5(A115);
A120:     1 in Seg 2 & 2 in dom G & 2 in Seg 2
           by A119,FINSEQ_1:4,TARSKI:def 2; then
A121:      G.1 = union rng F & G.2 = {} & M.(G.2) = (M*G).2
           by A119,FUNCT_1:23; then
         reconsider EMPTY = G.2 as Element of S by PROB_1:10;
A122:      (M*G).2 = 0. by A121,MEASURE1:def 11;
         defpred Pb[Nat,set] means
         ($1 = 1 implies $2 = 0.) &
         ($1 = 2 implies $2 = 1.);
A123:    for k be Element of NAT st k in Seg 2
          ex v be Element of ExtREAL st Pb[k,v]
         proof
          let k be Element of NAT;
          assume
A124:      k in Seg 2;
          per cases by A124,FINSEQ_1:4,TARSKI:def 2;
           suppose
A125:        k = 1;
            set v = 0.;
            reconsider v as Element of ExtREAL;
            take v;
            thus (k = 1 implies v = 0.) & (k = 2 implies v = 1.) by A125;
           end;
           suppose
A126:        k = 2;
            set v = 1.;
            reconsider v as Element of ExtREAL;
            take v;
            thus (k = 1 implies v = 0.) & (k = 2 implies v = 1.) by A126;
           end;
         end;
         consider b be FinSequence of ExtREAL such that
A127:       dom b = Seg 2 &
          for k be Element of NAT st k in Seg 2 holds Pb[k,b.k]
          from FINSEQ_1:sch 5(A123);
A128:      1 in Seg 2 & 2 in Seg 2 by FINSEQ_1:4,TARSKI:def 2; then
A129:     b.1 = 0. & b.2 = 1. by A127;
         deffunc Fy(Nat) = b.$1*(M*G).$1;
         consider y be FinSequence of ExtREAL such that
A130:      len y = 2 &
         for k be Element of NAT st k in Seg 2 holds y.k = Fy(k)
         from FINSEQ_2:sch 1;
A131:      y.1 = 0. & y.2 = 0.
         proof
          1 in Seg 2 by FINSEQ_1:4,TARSKI:def 2; then
          y.1 = b.1*(M*G).1 by A130;
          hence y.1 = 0. by A129,EXTREAL1:22;
          2 in Seg 2 by FINSEQ_1:4,TARSKI:def 2; then
          y.2 = b.2*(M*G).2 by A130;
          hence y.2 = 0. by A122,EXTREAL1:22;
         end;
         consider sumy be Function of NAT,ExtREAL such that
A132:     Sum(y) = sumy.len y & sumy.0 = 0. &
          for k be Element of NAT st k < len y holds
          sumy.(k+1) = sumy.k + y.(k+1)
           by CONVFUN1:def 5;
A133:   sumy.1 = sumy.0 + y.(0+1) by A132,A130
               .= 0. by  A132,A131,SUPINF_2:18;
A134:    Sum y = sumy.1 + y.(1+1) by  A130,A132
              .= 0. by  A131,A133,SUPINF_2:18;
         for u,v be set st u <> v holds G.u misses G.v
         proof
          let u,v be set;
          assume
A135:      u <> v;
          per cases;
           suppose
A136:        u in dom G & v in dom G; then
A137:        (u = 1 or u = 2) & (v = 1 or v = 2) by A119,FINSEQ_1:4,TARSKI:def
2;
            per cases by A135,A137;
             suppose u = 1 & v = 2; then
              G.u = union rng F & G.v = {} by A136,A119;
              hence thesis by XBOOLE_1:65;
             end;
             suppose u = 2 & v = 1; then
              G.v = union rng F & G.u = {} by A136,A119;
              hence thesis by XBOOLE_1:65;
             end;
           end;
           suppose not u in dom G or not v in dom G; then
            G.u = {} or G.v = {} by FUNCT_1:def 4;
            hence thesis by XBOOLE_1:65;
           end;
         end; then
         reconsider G as Finite_Sep_Sequence of S by PROB_2:def 3;
         G,b are_Re-presentation_of f & b.1 = 0. &
         (for m be Nat st 2 <= m & m in dom b holds
           0. < b.m & b.m < +infty) & dom y = dom G &
         (for m be Nat st m in dom y holds y.m = b.m*(M*G).m)
         proof
          Seg len G = Seg 2 by  FINSEQ_1:def 3,A119; then
          len G = 2 by FINSEQ_1:8; then
          G = <* union rng F, {} *> by A121,FINSEQ_1:61; then
          rng G = {union rng F, {}} by FINSEQ_2:147; then
A138:       union rng G = union rng F \/ {} by ZFMISC_1:93
                     .= dom f by A10,MESFUNC3:def 1;
          for k be Nat st k in dom G
           for v be set st v in G.k holds f.v=b.k
          proof
           let k be Nat;
           assume
A139:       k in dom G;
           let v be set;
           assume
A140:        v in G.k;
           per cases by A139,A119,FINSEQ_1:4,TARSKI:def 2;
            suppose
A141:          k = 1; then
             f.v = 0. by  A10,A18,A32,A53,A54,A121,A140,MESFUNC3:def 1,A113;
             hence thesis by A128,A127,A141;
            end;
            suppose k = 2;
             hence thesis by A120,A119,A140;
            end;
          end;
          hence G,b are_Re-presentation_of f by A138,A119,A127,MESFUNC3:def 1;
          thus b.1 = 0. by A128,A127;
          thus for k be Nat st 2 <= k & k in dom b holds
            0. < b.k & b.k < +infty
          proof
           let k be Nat;
           assume 2 <= k & k in dom b; then
           2 <= k & (k = 1 or k = 2) by A127,FINSEQ_1:4,TARSKI:def 2;
           hence thesis by A129,MESFUNC1:def 8,EXTREAL2:20;
          end;
          thus dom y = dom G by  A130,FINSEQ_1:def 3,A119;
          thus for k be Nat st k in dom y holds y.k = b.k*(M*G).k
          proof
           let k be Nat;
           assume k in dom y; then
           k in Seg len y by FINSEQ_1:def 3;
           hence thesis by A130;
          end;
         end;
         hence integral(X,S,M,f)
            = Sum(x) by  A7,A8,A9,A114,A134,MESFUNC3:def 2,A52;
        end;
       end;
       hence integral(X,S,M,f) = Sum(x);
      end;
      suppose
A142:    dom f1 <> {};
A143:    for i be Nat st i in dom x1 holds x1.i=a1.i*(M*F1).i
       proof
        let i be Nat;
        assume
A144:    i in dom x1;
A145:    dom x1 c= dom x & dom a1 c= dom a by RELAT_1:89; then
        x.i = a.i * (M*F).i by A12,A144; then
A146:    x1.i = a.i * (M*F).i by A144,FUNCT_1:68
            .= a1.i * (M*F).i by A26,A31,A144,FUNCT_1:68;
        (M*F).i = M.(F.i) by A145,A144,A11,FUNCT_1:23
               .= M.(F1.i) by A31,A144,FUNCT_1:68
               .= (M*F1).i by A31,A144,FUNCT_1:23;
        hence thesis by A146;
       end;
A147:  dom F = Seg (n+1) by  FINSEQ_1:def 3,A13; then
A148:   dom F1 = (Seg (n+1)) /\ Seg n by FUNCT_1:68;
       n <= n+1 by NAT_1:11; then
       Seg n c= Seg (n+1) by FINSEQ_1:7; then
A149:   dom F1 = Seg n by A148,XBOOLE_1:28; then
A150:    len F1 = n by FINSEQ_1:def 3;
       consider G be Finite_Sep_Sequence of S,
       b,y be FinSequence of ExtREAL such that
A151:    G,b are_Re-presentation_of f1 and
A152:    b.1 =0. and
A153:    for n be Nat st 2 <= n & n in dom b holds
        0. < b.n & b.n < +infty and
A154:    dom y = dom G and
A155:    for n be Nat st n in dom y holds y.n=b.n*(M*G).n and
A156:    integral(X,S,M,f1)=Sum(y) by MESFUNC3:def 2,A20,A142,A24;
       now per cases;
        case
A157:     F.(n+1) = {} or a.(n+1) = 0.;
         defpred PH[Nat,set] means
          ($1 = 1 implies $2 = G.1 \/ F.(n+1)) &
          (2 <= $1 implies $2 = G.$1);
A158:     for k be Element of NAT st k in Seg len G ex x be Element of S st
          PH[k,x]
         proof
          let k be Element of NAT;
          assume k in Seg len G; then
          k in dom G by FINSEQ_1:def 3; then
A159:       G.k in rng G by FUNCT_1:12;
A160:       rng G c= S by FINSEQ_1:def 4;
          per cases;
           suppose
A161:         k = 1;
            1 <= n+1 by NAT_1:11; then
            n+1 in dom F by A13,FINSEQ_3:27; then
A162:         F.(n+1) in rng F by FUNCT_1:12;
A163:         rng F c= S by FINSEQ_1:def 4;
            set x = G.1 \/ F.(n+1);
            reconsider x as Element of S
             by A159,A161,A160,A162,A163,FINSUB_1:def 1;
            PH[k,x] by A161;
            hence thesis;
           end;
           suppose
A164:         k <> 1;
            set x = G.k;
            reconsider x as Element of S by A159,A160;
            PH[k,x] by A164;
            hence thesis;
           end;
         end;
         consider H be FinSequence of S such that
A165:     dom H = Seg len G &
         for k be Element of NAT st k in Seg len G holds PH[k,H.k]
           from FINSEQ_1:sch 5(A158);
A166:      union rng G <> {} by A142,A151,MESFUNC3:def 1;
         dom G <> {}
         proof
          assume dom G = {}; then
          G = {} by FINSEQ_1:26;
          hence contradiction by A166,FINSEQ_1:27,ZFMISC_1:2;
         end; then
         G <> {} by FINSEQ_1:26; then
         len G <> 0 by FINSEQ_1:25; then
         0 < len G; then
         0+1 <= len G by NAT_1:13; then
A167:     1 in Seg len G by FINSEQ_1:3;
         deffunc Z(Nat) = b.$1 * (M*H).$1;
         consider z be FinSequence of ExtREAL such that
A168:      len z = len y &
         for n be Element of NAT st n in Seg len y holds z.n = Z(n)
           from FINSEQ_2:sch 1;
A169:      dom z = dom y by A168,FINSEQ_3:31;
A170:      for n be Nat st n in dom z holds z.n=b.n*(M*H).n
         proof
          let n be Nat;
          assume n in dom z; then
          n in Seg len y by A168,FINSEQ_1:def 3;
          hence thesis by A168;
         end;
         for u,v be set st u <> v holds H.u misses H.v
         proof
          let u,v be set;
          assume
A171:      u <> v;
          per cases;
           suppose
A172:       u in dom H & v in dom H; then
            reconsider u1=u,v1=v as Element of NAT;
            per cases;
             suppose
A173:          u = 1; then
A174:         H.u = G.1 \/ F.(n+1) by A172,A165;
              1 <= v1 by A172,A165,FINSEQ_1:3; then
              1 < v1 by A171,A173,REAL_1:def 5; then
A175:          1+1 <= v1 by NAT_1:13; then
A176:          H.v = G.v by A165,A172;
A177:         G.1 misses G.v by A171,A173,PROB_2:def 3;
              F.(n+1) misses G.v
              proof
               per cases by A157;
                suppose F.(n+1) = {};
                 hence thesis by XBOOLE_1:65;
                end;
                suppose
A178:            a.(n+1) = 0.;
                 assume F.(n+1) meets G.v; then
                  consider w be set such that
A179:              w in F.(n+1) & w in G.v by XBOOLE_0:3;
                  1 <= n+1 by NAT_1:11; then
                  n+1 in Seg (n+1) by FINSEQ_1:3; then
                  n+1 in dom F by A13,FINSEQ_1:def 3; then
A180:              f.w = 0. by  A10,A179,MESFUNC3:def 1,A178;
A181:              v1 in dom G & w in G.v1 by A179,A172,A165,FINSEQ_1:def 3;
then
                  G.v1 in rng G by FUNCT_1:12; then
                  w in union rng G by A179,TARSKI:def 4; then
                  w in dom f1 by A151,MESFUNC3:def 1; then
A182:              f.w = f1.w by FUNCT_1:70 .= b.v1
                    by A151,A181,MESFUNC3:def 1;
                  v1 in dom b by A181,A151,MESFUNC3:def 1;
                  hence contradiction by A153,A175,A180,A182;
               end;
              end;
              hence H.u misses H.v by A174,A176,A177,XBOOLE_1:70;
             end;
             suppose
A183:            u <> 1;
              1 <= u1 by A172,A165,FINSEQ_1:3; then
              1 < u1 by A183,REAL_1:def 5; then
A184:          1+1 <= u1 by NAT_1:13; then
A185:          H.u = G.u by A165,A172;
              per cases;
               suppose
A186:            v = 1; then
A187:            H.v = G.1 \/ F.(n+1) by A172,A165;
A188:            G.1 misses G.u by A171,A186,PROB_2:def 3;
                F.(n+1) misses G.u
                proof
                 per cases by A157;
                  suppose F.(n+1) = {};
                   hence thesis by XBOOLE_1:65;
                  end;
                  suppose
A189:              a.(n+1) = 0.;
                   assume F.(n+1) meets G.u; then
                    consider w be set such that
A190:                w in F.(n+1) & w in G.u by XBOOLE_0:3;
                    1 <= n+1 by NAT_1:11; then
                    n+1 in Seg (n+1) by FINSEQ_1:3; then
                    n+1 in dom F by A13,FINSEQ_1:def 3; then
A191:                f.w = 0. by  A10,A190,MESFUNC3:def 1,A189;
A192:                u1 in dom G & w in G.u1
                      by A190,A172,A165,FINSEQ_1:def 3; then
                    G.u1 in rng G by FUNCT_1:12; then
                    w in union rng G by A190,TARSKI:def 4; then
                    w in dom f1 by A151,MESFUNC3:def 1; then
A193:                f.w = f1.w by FUNCT_1:70 .= b.u1
                     by A151,A192,MESFUNC3:def 1;
                    u1 in dom b by A192,A151,MESFUNC3:def 1;
                    hence contradiction by A153,A184,A191,A193;
                  end;
                end;
                hence H.u misses H.v by A187,A185,A188,XBOOLE_1:70;
               end;
               suppose
A194:              v <> 1;
                1 <= v1 by A172,A165,FINSEQ_1:3; then
                1 < v1 by A194,REAL_1:def 5; then
                1+1 <= v1 by NAT_1:13; then
                H.v = G.v by A172,A165;
                hence H.u misses H.v by A171,A185,PROB_2:def 3;
               end;
             end;
           end;
           suppose not u in dom H or not v in dom H; then
            H.u = {} or H.v = {} by FUNCT_1:def 4;
            hence H.u misses H.v by XBOOLE_1:65;
           end;
         end; then
         reconsider H as Finite_Sep_Sequence of S by PROB_2:def 3;
A195:     dom f = union rng H
         proof
          thus dom f c= union rng H
          proof
           let v be set;
           assume v in dom f; then
           v in union rng F by A10,MESFUNC3:def 1; then
           consider A be set such that
A196:         v in A & A in rng F by TARSKI:def 4;
           consider u be set such that
A197:         u in dom F & A = F.u by A196,FUNCT_1:def 5;
           reconsider u as Element of NAT by A197;
           u in Seg len F by A197,FINSEQ_1:def 3; then
A198:        1 <= u & u <= n+1 by A13,FINSEQ_1:3;
           per cases;
            suppose u = n+1; then
             1 in dom H & H.1 = G.1 \/ A by A197,A167,A165; then
             v in H.1 & H.1 in rng H by A196,XBOOLE_0:def 2,FUNCT_1:12;
             hence thesis by TARSKI:def 4;
            end;
            suppose u <> n+1; then
             u < n+1 by A198,REAL_1:def 5; then
             u <= n by NAT_1:13; then
A199:          u in Seg n by A198,FINSEQ_1:3; then
A200:          F1.u in rng F1 by A149,FUNCT_1:12;
             A = F1.u by A197,A199,A149,FUNCT_1:70; then
             v in union rng F1 by A196,A200,TARSKI:def 4; then
             v in union rng G by A18,A151,MESFUNC3:def 1; then
             consider B be set such that
A201:           v in B & B in rng G by TARSKI:def 4;
             consider w be set such that
A202:          w in dom G & B = G.w by A201,FUNCT_1:def 5;
             reconsider w as Element of NAT by A202;
A203:         w in Seg len G by A202,FINSEQ_1:def 3;
             per cases;
              suppose
A204:           w = 1;
               H.1 = G.1 \/ F.(n+1) by A167,A165; then
A205:           v in H.1 by A201,A202,A204,XBOOLE_0:def 2;
               H.1 in rng H by A167,A165,FUNCT_1:12;
               hence thesis by A205,TARSKI:def 4;
              end;
              suppose
A206:           w <> 1;
               1 <= w & w <= len G by A202,FINSEQ_3:27; then
               1 < w by A206,REAL_1:def 5; then
               1+1 <= w by NAT_1:13; then
A207:           v in H.w by A201,A202,A203,A165;
               H.w in rng H by A203,A165,FUNCT_1:12;
               hence thesis by A207,TARSKI:def 4;
              end;
            end;
          end;
           let v be set;
           assume v in union rng H; then
           consider A be set such that
A208:       v in A & A in rng H by TARSKI:def 4;
           consider k be set such that
A209:       k in dom H & A = H.k by A208,FUNCT_1:def 5;
           reconsider k as Element of NAT by A209;
           per cases;
            suppose k = 1; then
A210:         H.k = G.1 \/ F.(n+1) by A209,A165;
             per cases by A210,A208,A209,XBOOLE_0:def 2;
              suppose
A211:           v in G.1;
               1 in dom G by A167,FINSEQ_1:def 3; then
               G.1 in rng G by FUNCT_1:12; then
               v in union rng G by A211,TARSKI:def 4; then
               v in dom f1 by A151,MESFUNC3:def 1; then
               v in dom f /\ (union rng F1) by FUNCT_1:68;
               hence thesis by XBOOLE_0:def 3;
              end;
              suppose
A212:           v in F.(n+1);
               1 <= n+1 & n+1 <= len F by A13,NAT_1:11; then
               n+1 in dom F by FINSEQ_3:27; then
               F.(n+1) in rng F by FUNCT_1:12; then
               v in union rng F by A212,TARSKI:def 4;
               hence thesis by A10,MESFUNC3:def 1;
              end;
            end;
            suppose
A213:         k <> 1;
             1 <= k by A209,A165,FINSEQ_1:3; then
             1 < k by A213,REAL_1:def 5; then
             1+1 <= k by NAT_1:13; then
A214:         v in G.k by A208,A209,A165;
             k in dom G by A209,A165,FINSEQ_1:def 3; then
             G.k in rng G by FUNCT_1:12; then
             v in union rng G by A214,TARSKI:def 4; then
             v in dom f1 by A151,MESFUNC3:def 1; then
             v in dom f /\ (union rng F1) by FUNCT_1:68;
             hence thesis by XBOOLE_0:def 3;
            end;
         end;
A215:     dom H = dom G by  A165,FINSEQ_1:def 3
              .= dom b by A151,MESFUNC3:def 1;
A216:     for m be Nat st m in dom H
          for x be set st x in H.m holds f.x = b.m
         proof
          let m be Nat;
          assume
A217:       m in dom H;
          let x be set;
           assume
A218:        x in H.m;
           per cases;
           suppose
A219:         m = 1; then
A220:         H.m = G.1 \/ F.(n+1) by A217,A165;
             per cases;
              suppose F.(n+1) = {}; then
A221:            x in G.1 by A219,A218,A217,A165;
A222:            m in dom G by A217,A165,FINSEQ_1:def 3; then
A223:            f1.x = b.m by A219,A221,A151,MESFUNC3:def 1;
               G.1 in rng G by A219,A222,FUNCT_1:12; then
               x in union rng G by A221,TARSKI:def 4; then
               x in dom f1 by A151,MESFUNC3:def 1;
               hence f.x = b.m by A223,FUNCT_1:70;
              end;
              suppose
            F.(n+1) <> {};
               per cases by A220,A218,XBOOLE_0:def 2;
                suppose
A224:              x in G.1;
A225:              m in dom G by A217,A165,FINSEQ_1:def 3; then
A226:              f1.x = b.m by A219,A224,A151,MESFUNC3:def 1;
                 G.1 in rng G by A219,A225,FUNCT_1:12; then
                 x in union rng G by A224,TARSKI:def 4; then
                 x in dom f1 by A151,MESFUNC3:def 1;
                 hence f.x = b.m by A226,FUNCT_1:70;
                end;
                suppose
A227:              x in F.(n+1);
                 1 <= n+1 & n+1 <= len F by A13,NAT_1:11; then
                 n+1 in dom F by FINSEQ_3:27; then
                 f.x = 0. by  A10,A227,MESFUNC3:def 1,A157;
                 hence f.x = b.m by A219,A152;
                end;
              end;
            end;
            suppose
A228:         m <> 1;
A229:         m in dom G by A217,A165,FINSEQ_1:def 3;then
             1 <= m by FINSEQ_3:27; then
             1 < m by A228,REAL_1:def 5; then
             1+1 <= m by NAT_1:13; then
A230:         H.m = G.m by A217,A165; then
A231:         f1.x = b.m by A218,A229,A151,MESFUNC3:def 1;
             G.m in rng G by A229,FUNCT_1:12; then
             x in union rng G by A218,A230,TARSKI:def 4; then
             x in dom f1 by A151,MESFUNC3:def 1;
             hence f.x = b.m by A231,FUNCT_1:70;
            end;
         end;
A232:      H,b are_Re-presentation_of f by A195,A215,A216,MESFUNC3:def 1;
A233:      dom z =dom H by  A154,A169,A165,FINSEQ_1:def 3
;
A234:      for k be Element of NAT st k in dom z holds z.k = y.k
         proof
          let k be Element of NAT;
          assume
A235:      k in dom z; then
          k in Seg len y by A168,FINSEQ_1:def 3; then
A236:      z.k = b.k*(M*H).k by A168;
A237:      y.k = b.k*(M*G).k by A155,A235,A169;
          per cases;
           suppose
A238:        k = 1; then
            z.k = 0. by A152,A236,EXTREAL1:22;
            hence thesis by A238,A152,A237,EXTREAL1:22;
           end;
           suppose
A239:        k <> 1;
A240:        k in Seg len G by A154,A235,A169,FINSEQ_1:def 3; then
            1 <= k by FINSEQ_1:3; then
            1 < k by A239,REAL_1:def 5; then
A241:        1+1 <= k by NAT_1:13;
            (M*H).k = M.(H.k) by A235,A233,FUNCT_1:23
                   .= M.(G.k) by A240,A165,A241
                   .= (M*G).k by A154,A235,A169,FUNCT_1:23;
            hence thesis by A236,A155,A235,A169;
           end;
         end;
         1 <= n+1 & n+1 <= len F by A13,NAT_1:11; then
A242:     n+1 in dom F by FINSEQ_3:27;
A243:      x.(n+1) = 0.
         proof
          per cases by A157;
           suppose
A244:         F.(n+1) = {};
A245:        (M*F).(n+1) = M.(F.(n+1)) by A242,FUNCT_1:23;
            reconsider EMPTY = {} as Element of S by PROB_1:10;
A246:        (M*F).(n+1) = 0. by A245,A244,MEASURE1:def 11;
            x.(n+1) = a.(n+1)*(M*F).(n+1) by A12,A242,A11;
            hence thesis by A246,EXTREAL1:22;
           end;
           suppose
A247:         a.(n+1) = 0.;
            x.(n+1) = a.(n+1)*(M*F).(n+1) by A12,A242,A11;
            hence thesis by A247,EXTREAL1:22;
           end;
         end;
A248:      not -infty in rng x1 & not -infty in rng <* x.(n+1) *>
         proof
          now assume -infty in rng x1; then
           consider l be set such that
A249:        l in dom x1 & x1.l = -infty by FUNCT_1:def 5;
           reconsider l as Element of NAT by A249;
A250:       x.l = -infty by  A249,FUNCT_1:70;
           l in dom x /\ (Seg n) by A249,FUNCT_1:68; then
A251:       l in dom x by XBOOLE_0:def 3; then
A252:       a.l*(M*F).l = -infty by  A12,A250;
           per cases;
            suppose F.l <> {}; then
             consider v be set such that
A253:         v in F.l by XBOOLE_0:def 1;
A254:         a.l = f.v by A253,A251,A11,A10,MESFUNC3:def 1;
A255:         F.l in rng F by A251,A11,FUNCT_1:12; then
             v in union rng F by A253,TARSKI:def 4; then
             v in dom f by A10,MESFUNC3:def 1; then
A256:         0. <= a.l by A254,A9;
             rng F c= S by FINSEQ_1:def 4; then
             reconsider Fl = F.l as Element of S by A255;
             reconsider EMPTY = {} as Element of S by MEASURE1:21;
             EMPTY c= F.l by XBOOLE_1:2; then
             M.{} <= M.(Fl) by MEASURE1:62; then
             0. <= M.(Fl) by MEASURE1:def 11; then
             0. <= (M*F).l by A251,A11,FUNCT_1:23; then
             0. * (M*F).l <= a.l*(M*F).l by A256,EXTREAL1:42;
             hence contradiction by A252,EXTREAL1:22;
            end;
            suppose
A257:         F.l = {};
             (M*F).l = M.(F.l) by A251,A11,FUNCT_1:23
                    .= 0. by A257,MEASURE1:def 11;
             hence contradiction by A252,EXTREAL1:22;
            end;
          end;
          hence not -infty in rng x1;
          now assume -infty in rng <* x.(n+1) *>; then
           -infty in {x.(n+1)} by FINSEQ_1:56;
           hence contradiction by A243,TARSKI:def 1;
          end;
          hence not -infty in rng <* x.(n+1) *>;
         end;
         len x = n+1 by  A11,FINSEQ_3:31,A13; then
         x = x|(n+1) by FINSEQ_1:79
          .= x|Seg(n+1) by FINSEQ_1:def 15
          .= x1^<* x.(n+1) *> by A242,A11,FINSEQ_5:11; then
A258:      Sum x = Sum(x1)+Sum<* x.(n+1) *> by A248,CONVFUN1:7
              .= Sum(x1)+0. by A243,CONVFUN1:5;
         integral(X,S,M,f)=Sum(z) by A7,A8,A9,MESFUNC3:def 2,A232,A152,A153,
A233,A170
           .=integral(X,S,M,f1) by  A169,A234,FINSEQ_1:17,A156
           .=Sum(x1) by A20,A142,A24,A30,A31,A143,A150,A6
           .=Sum(x) by A258,SUPINF_2:18;
         hence integral(X,S,M,f) = Sum(x);
        end;
        case
A259:     F.(n+1) <> {} & a.(n+1) <> 0.;
A260:     f is_finite by A7,MESFUNC2:def 5;
         1 <= n+1 & n+1 <= len F by A13,NAT_1:11; then
A261:     n+1 in dom F by FINSEQ_3:27;
         consider v be set such that
A262:     v in F.(n+1) by A259,XBOOLE_0:def 1;
         F.(n+1) in rng F by A261,FUNCT_1:12; then
         v in union rng F by A262,TARSKI:def 4; then
A263:     v in dom f & dom f c= X by A10,MESFUNC3:def 1,RELSET_1:12; then
         reconsider v as Element of X;
A264:     0. <= f.v by A263,A9;
         f.v = a.(n+1) by A262,A261,A10,MESFUNC3:def 1; then
A265:     |. a.(n+1) .| < +infty by A260,A263,MESFUNC2:def 1;
A266:    0. <= a.(n+1) by A264,A262,A261,A10,MESFUNC3:def 1; then
A267:     0. < a.(n+1) & a.(n+1) < +infty
           by A259,A265,EXTREAL2:58;
         defpred PH[Nat,set] means
          ($1 <= len G implies $2 = G.$1) &
          ($1 = len G + 1 implies $2 = F.(n+1));
A268:     for k be Element of NAT st k in Seg(len G + 1)
          ex v be Element of S st PH[k,v]
         proof
          let k be Element of NAT;
          assume
A269:       k in Seg(len G + 1);
           per cases;
            suppose
A270:         k <> len G + 1;
             k <= len G + 1 by A269,FINSEQ_1:3; then
             k < len G + 1 by A270,REAL_1:def 5; then
A271:         k <= len G by NAT_1:13;
             1 <= k by A269,FINSEQ_1:3; then
             k in dom G by A271,FINSEQ_3:27; then
             G.k in rng G & rng G c= S by FINSEQ_1:def 4,FUNCT_1:12; then
             reconsider v = G.k as Element of S;
             take v;
             thus thesis by A270;
            end;
            suppose
A272:         k = len G + 1;
             F.(n+1) in rng F & rng F c= S
              by A261,FUNCT_1:12,FINSEQ_1:def 4; then
             reconsider v = F.(n+1) as Element of S;
             take v;
             thus thesis by A272,NAT_1:13;
            end;
         end;
         consider H be FinSequence of S such that
A273:      dom H = Seg(len G + 1) &
         for k be Element of NAT st k in Seg(len G + 1) holds PH[k,H.k]
           from FINSEQ_1:sch 5(A268);
A274:     Seg len H = Seg(len G + 1) by  FINSEQ_1:def 3,A273; then
A275:      len H = len G + 1 by FINSEQ_1:8;
         defpred Pc[Nat,set] means
          ($1 <= len b implies $2 = b.$1) &
          ($1 = len b + 1 implies $2 = a.(n+1));
A276:     for k be Element of NAT st k in Seg(len b + 1)
          ex v be Element of ExtREAL st Pc[k,v]
         proof
          let k be Element of NAT;
          assume k in Seg(len b + 1);
           per cases;
            suppose
A277:         k <> len b + 1;
             reconsider v = b.k as Element of ExtREAL;
             take v;
             thus thesis by A277;
            end;
            suppose k = len b + 1; then
A278:         len b < k by NAT_1:13;
             reconsider v = a.(n+1) as Element of ExtREAL;
             take v;
             thus thesis by A278;
            end;
         end;
         consider c be FinSequence of ExtREAL such that
A279:      dom c = Seg(len b + 1) &
         for k be Element of NAT st k in Seg(len b + 1) holds Pc[k,c.k]
           from FINSEQ_1:sch 5(A276);
A280:      len c = len b + 1 by A279,FINSEQ_1:def 3;
         1 <= len b + 1 by NAT_1:11; then
A281:     1 in Seg(len b + 1) by FINSEQ_1:3;
A282:     dom G <> {}
         proof
          assume dom G = {}; then
          {} = union rng G by ZFMISC_1:2,RELAT_1:65
            .= dom f1 by A151,MESFUNC3:def 1;
          hence contradiction by A142;
         end;
A283:     dom G = dom b by A151,MESFUNC3:def 1; then
         b <> {} by A282,FINSEQ_1:26; then
         len b <> 0 by FINSEQ_1:25; then
         len b in Seg len b by FINSEQ_1:5; then
         1 <= len b by FINSEQ_1:3; then
A284:      c.1 =0. by  A279,A281,A152;
A285:    Seg len G = dom G by FINSEQ_1:def 3
                  .= Seg len b by A283,FINSEQ_1:def 3; then
A286:     len G = len b by FINSEQ_1:8;
A287:     len G = len y by A154,FINSEQ_3:31;
A288:      for m be Nat st 2 <= m & m in dom c holds
          0. < c.m & c.m < +infty
         proof
          let m be Nat;
          assume
A289:      2 <= m & m in dom c; then
A290:      m <= len c by FINSEQ_3:27;
          per cases;
           suppose m = len c;
            hence 0. < c.m & c.m < +infty by A279,A280,A289,A267;
           end;
           suppose m <> len c; then
            m < len b + 1 by A290,A280,REAL_1:def 5; then
A291:        m <= len b by NAT_1:13; then
A292:        c.m = b.m by A289,A279;
            1 <= m by A289,XXREAL_0:2; then
            m in dom b by A291,FINSEQ_3:27;
            hence 0. < c.m & c.m < +infty by A153,A289,A292;
           end;
         end;
         defpred Pz[Nat,set] means
          ($1 <= len y implies $2 = b.$1*(M*H).$1) &
          ($1 = len y + 1 implies $2 = a.(n+1)*(M*F).(n+1));
A293:     for k be Element of NAT st k in Seg(len y + 1)
          ex v be Element of ExtREAL st Pz[k,v]
         proof
          let k be Element of NAT;
          assume k in Seg(len y + 1);
           per cases;
            suppose
A294:         k <> len y + 1;
             reconsider v = b.k*(M*H).k as Element of ExtREAL;
             take v;
             thus thesis by A294;
            end;
            suppose k = len y + 1; then
A295:         len y < k by NAT_1:13;
             reconsider v = a.(n+1)*(M*F).(n+1) as Element of ExtREAL;
             take v;
             thus thesis by A295;
            end;
         end;
         consider z be FinSequence of ExtREAL such that
A296:     dom z = Seg(len y + 1) &
         for k be Element of NAT st k in Seg(len y + 1) holds Pz[k,z.k]
           from FINSEQ_1:sch 5(A293);
A297:      len z= len y + 1 by A296,FINSEQ_1:def 3; then
         len z in Seg(len y + 1) by FINSEQ_1:6; then
A298:      z.len z=a.(n+1)*(M*F).(n+1) by A297,A296;
A299:     len y = len G by A154,FINSEQ_3:31; then
A300:      len z = len G + 1 by A296,FINSEQ_1:def 3;
A301:      len z in Seg(len G + 1) by A300,FINSEQ_1:6;
A302:      len z in dom H by A273,A300,FINSEQ_1:6;
A303:      n+1 in dom F by A147,FINSEQ_1:6;
A304:      (M*F).(n+1) = M.(F.(n+1)) by A303,FUNCT_1:23
                      .= M.(H.len z) by A301,A300,A273
                      .= (M*H).len z by A302,FUNCT_1:23;
A305:      for m be Nat st m in dom z holds z.m = c.m*(M*H).m
         proof
          let m be Nat;
          assume
A306:      m in dom z;
A307:      Pc[m,c.m] by A279,A306,A296,A286,A299;
          per cases;
           suppose m = len y + 1;
            hence thesis by A304,A306,A296,A300,A299,A285,FINSEQ_1:8,A307;
           end;
           suppose
A308:        m <> len y + 1;
            m <= len y + 1 by A296,A306,FINSEQ_1:3; then
            m < len y + 1 by A308,REAL_1:def 5; then
A309:        m <= len b by A286,A299,NAT_1:13; then
            c.m = b.m by A279,A296,A306,A286,A299;
            hence thesis by A309,A286,A299,A306,A296;
           end;
         end;
         for i,j be set st i <> j holds H.i misses H.j
         proof
          let i,j be set;
          assume
A310:      i <> j;
          per cases;
           suppose
A311:        i in dom H & j in dom H; then
            reconsider i,j as Element of NAT;
A312:        1 <= i & i <= len G + 1 &
            1 <= j & j <= len G + 1 by A311,A273,FINSEQ_1:3;
A313:        PH[i,H.i] & PH[j,H.j] by A311,A273;
A314:        for k be Nat st 1 <= k & k <= len G holds
             H.k misses F.(n+1)
            proof
             let k be Nat;
             assume
A315:        1 <= k & k <= len G; then
             k <= len G + 1 by NAT_1:12; then
             k in Seg(len G + 1) by A315,FINSEQ_1:3; then
A316:        H.k = G.k by A273,A315;
             k in dom G by A315,FINSEQ_3:27; then
             G.k in rng G by FUNCT_1:12; then
             G.k c= union rng G by ZFMISC_1:92; then
             G.k c= dom f1 by A151,MESFUNC3:def 1; then
             G.k c= (dom f)/\(union rng F1) by FUNCT_1:68; then
A317:        G.k c= union rng F1 by XBOOLE_1:18;
             union rng F1 misses F.(n+1)
             proof
              assume union rng F1 meets F.(n+1); then
               consider v be set such that
A318:          v in union rng F1 & v in F.(n+1) by XBOOLE_0:3;
               consider A be set such that
A319:          v in A & A in rng F1 by A318,TARSKI:def 4;
               consider m be set such that
A320:          m in dom F1 & A = F1.m by A319,FUNCT_1:def 5;
               reconsider m as Element of NAT by A320;
               m in Seg len F1 by A320,FINSEQ_1:def 3; then
               1 <= m & m <= n by A150,FINSEQ_1:3; then
A321:          m <> n+1 by NAT_1:13;
               F1.m = F.m by A320,FUNCT_1:70; then
               A misses F.(n+1) by A320,A321,PROB_2:def 3; then
               A /\ F.(n+1) = {} by XBOOLE_0:def 7;
               hence contradiction by A318,A319,XBOOLE_0:def 3;
             end;
             hence thesis by A316,A317,XBOOLE_1:63;
            end;
            per cases;
             suppose
A322:          i = len G + 1; then
              j < len G + 1 by A310,A312,REAL_1:def 5; then
              j <= len G by NAT_1:13;
              hence thesis by A312,A314,A313,A322;
             end;
             suppose i <> len G + 1; then
A323:          i < len G + 1 by A312,REAL_1:def 5; then
A324:          i <= len G by NAT_1:13;
              per cases;
               suppose j = len G + 1;
                hence thesis by A312,A313,A324,A314;
               end;
               suppose j <> len G + 1; then
                j < len G + 1 by A312,REAL_1:def 5;
                hence thesis by A310,A323,A313,NAT_1:13,PROB_2:def 3;
               end;
             end;
           end;
           suppose
A325:        not i in dom H or not j in dom H;
            per cases by A325;
             suppose not i in dom H; then
              H.i = {} by FUNCT_1:def 4;
              hence thesis by XBOOLE_1:65;
             end;
             suppose not j in dom H; then
              H.j = {} by FUNCT_1:def 4;
              hence thesis by XBOOLE_1:65;
             end;
           end;
         end; then
         reconsider H as Finite_Sep_Sequence of S by PROB_2:def 3;
         n+1 in dom F by A147,FINSEQ_1:6; then
         F|(Seg (n+1)) = F1^<* F.(n+1) *> by FINSEQ_5:11; then
         F1^<* F.(n+1) *> = F|(n+1) by FINSEQ_1:def 15
          .= F by A13,FINSEQ_1:79; then
A326:    rng F = rng F1 \/ rng <* F.(n+1) *> by FINSEQ_1:44
              .= rng F1 \/ {F.(n+1)} by FINSEQ_1:55;
A327:      len H = len G + 1 by A274,FINSEQ_1:8
          .= len G + len <* F.(n+1) *> by FINSEQ_1:56
          .= len (G^<* F.(n+1) *>) by FINSEQ_1:35;
         for k be Element of NAT st 1 <= k & k <= len H holds
          H.k = (G^<* F.(n+1) *>).k
         proof
          let k be Element of NAT;
          assume
A328:      1 <= k & k <= len H; then
          k in Seg(len G + 1) by A274,FINSEQ_1:3; then
A329:      PH[k,H.k] by A273;
          per cases;
           suppose k = len G + 1;
            hence H.k = (G^<* F.(n+1) *>).k by A329,FINSEQ_1:59;
           end;
           suppose k <> len G + 1; then
A330:        k < len G + 1 by A328,A275,REAL_1:def 5; then
            k <= len G by NAT_1:13; then
            k in dom G by A328,FINSEQ_3:27;
            hence H.k = (G^<* F.(n+1) *>).k by A329,A330,NAT_1:13,FINSEQ_1:def
7;
           end;
         end; then
         H = G^<* F.(n+1) *> by A327,FINSEQ_1:18; then
A331:    rng H = rng G \/ rng <* F.(n+1) *> by FINSEQ_1:44
              .= rng G \/ {F.(n+1)} by FINSEQ_1:55;
A332:     dom f = union (rng F1 \/ {F.(n+1)}) by  A10,MESFUNC3:def 1,A326
              .= dom f1 \/ union {F.(n+1)} by  ZFMISC_1:96,A18
              .= union rng G \/ union {F.(n+1)} by A151,MESFUNC3:def 1
              .= union rng H by  ZFMISC_1:96,A331;
A333:     for m be Nat st m in dom H for v be set st v in H.m holds f.v = c.m
         proof
          let m be Nat;
          assume
A334:       m in dom H; then
A335:       PH[m,H.m] & Pc[m,c.m] by A273,A286,A279;
A336:      1 <= m & m <= len G + 1 by A334,A273,FINSEQ_1:3;
           let v be set;
           assume
A337:       v in H.m;
           per cases;
            suppose
A338:         m = len G + 1;
             n+1 in dom F by A147,FINSEQ_1:6;
             hence f.v = c.m by A335,A338,A285,FINSEQ_1:8,A10,A337,MESFUNC3:def
1;
            end;
            suppose m <> len G + 1; then
A339:         m < len G + 1 by A336,REAL_1:def 5; then
A340:         m <= len G by NAT_1:13;
A341:         H.m in rng H by A334,FUNCT_1:12;
A342:        union rng G = union rng F1 by  A151,MESFUNC3:def 1,A18;
             m in Seg len G by A336,A340,FINSEQ_1:3; then
             m in dom G by FINSEQ_1:def 3; then
             G.m in rng G by FUNCT_1:12; then
             v in union rng F1 & v in dom f
               by A337,A339,A335,A342,TARSKI:def 4,A341,A332,NAT_1:13; then
             v in (dom f)/\(union rng F1) by XBOOLE_0:def 3; then
A343:        v in dom f1 by FUNCT_1:68;
             m in Seg len G by A336,A340,FINSEQ_1:3; then
             m in dom G by FINSEQ_1:def 3; then
             f1.v = c.m by  A151,A335,A337,A339,MESFUNC3:def 1,NAT_1:13,A286;
             hence f.v = c.m by A343,FUNCT_1:70;
            end;
          end;
A344:      H,c are_Re-presentation_of f by A332,A273,A286,A279,A333,MESFUNC3:
def 1;
A345:      dom z = Seg(len G + 1) by  A299,A296;
A346:      not -infty in rng y &
         not -infty in rng <* a.(n+1)*(M*F).(n+1) *>
         proof
          now assume
A347:       -infty in rng y or -infty in rng <* a.(n+1)*(M*F).(n+1) *>;
           per cases by A347;
            suppose -infty in rng y; then
             consider k be set such that
A348:         k in dom y & -infty = y.k by FUNCT_1:def 5;
             reconsider k as Element of NAT by A348;
A349:         y.k = b.k*(M*G).k by A348,A155;
             k in Seg len y by A348,FINSEQ_1:def 3; then
A350:         1 <= k by FINSEQ_1:3;
             per cases;
              suppose k = 1;
               hence contradiction by A152,A348,A349,EXTREAL1:22;
              end;
              suppose k <> 1; then
               1 < k by A350,REAL_1:def 5; then
               1+1 <= k & k in dom b by NAT_1:13,A154,A348,A151,MESFUNC3:def 1;
then
A351:           0. < b.k & b.k < +infty by A153;
               G.k in rng G & rng G c= S
                 by A154,A348,FUNCT_1:12,FINSEQ_1:def 4; then
               reconsider Gk = G.k as Element of S;
               reconsider EMPTY = {} as Element of S by PROB_1:10;
               EMPTY c= Gk by XBOOLE_1:2; then
               M.EMPTY <= M.(Gk) by MEASURE1:62; then
A352:           0. <= M.(Gk) by MEASURE1:def 11;
               (M*G).k = M.(G.k) by A154,A348,FUNCT_1:23;
               hence contradiction by A348,A352,A349,A351,EXTREAL2:44;
              end;
            end;
            suppose -infty in rng <* a.(n+1)*(M*F).(n+1) *>; then
             -infty in { a.(n+1)*(M*F).(n+1) } by FINSEQ_1:55; then
A353:         a.(n+1)*(M*F).(n+1) = -infty by TARSKI:def 1;
             1 <= n+1 by NAT_1:11; then
A354:         n+1 in dom F by A13,FINSEQ_3:27; then
             F.(n+1) in rng F & rng F c= S by FUNCT_1:12,FINSEQ_1:def 4; then
             reconsider Fn1 = F.(n+1) as Element of S;
             reconsider EMPTY = {} as Element of S by PROB_1:10;
             EMPTY c= Fn1 by XBOOLE_1:2; then
             M.EMPTY <= M.(Fn1) by MEASURE1:62; then
A355:         0. <= M.(Fn1) by MEASURE1:def 11;
             (M*F).(n+1) = M.(Fn1) by A354,FUNCT_1:23;
             hence contradiction by A353,A355,EXTREAL2:44,A266;
            end;
          end;
          hence thesis;
         end;
A356:     len (y^<* a.(n+1)*(M*F).(n+1) *>)
          = len y + 1 by FINSEQ_2:19
         .= len z by A296,FINSEQ_1:def 3;
         for k be Element of NAT st 1 <= k & k <= len z
          holds z.k = (y^<* a.(n+1)*(M*F).(n+1) *>).k
         proof
          let k be Element of NAT;
          assume
A357:      1 <= k & k <= len z;
          per cases;
           suppose k = len z;
            hence thesis by A297,A298,FINSEQ_1:59;
           end;
           suppose k <> len z; then
            k < len z by A357,REAL_1:def 5; then
A358:        k <= len y by A297,NAT_1:13; then
A359:        k in dom y by A357,FINSEQ_3:27; then
A360:        (y^<* a.(n+1)*(M*F).(n+1) *>).k
             = y.k by FINSEQ_1:def 7
            .= b.k*(M*G).k by A155,A359
            .= b.k*(M.(G.k)) by A359,A154,FUNCT_1:23;
A361:        k in Seg len z by A357,FINSEQ_1:3; then
A362:        H.k = G.k by A273,A358,A287,A297;
            M.(H.k) = (M*H).k by A297,A287,A361,A273,FUNCT_1:23;
            hence thesis by A297,A361,A296,A358,A362,A360;
           end;
         end; then
A363:      z = y^<* a.(n+1)*(M*F).(n+1) *> by A356,FINSEQ_1:18;
A364:      not -infty in rng x1
         proof
          assume -infty in rng x1; then
           consider k be set such that
A365:       k in dom x1 & -infty = x1.k by FUNCT_1:def 5;
           reconsider k as Element of NAT by A365;
A366:       x.k = -infty by A365,FUNCT_1:70;
           k in (dom x)/\(Seg n) by A365,FUNCT_1:68; then
A367:       k in dom x by XBOOLE_0:def 3; then
A368:       x.k = a.k*(M*F).k by A12;
           reconsider EMPTY = {} as Element of S by PROB_1:10;
A369:       F.k in rng F by A11,A367,FUNCT_1:12;
           rng F c= S by FINSEQ_1:def 4; then
           reconsider Fk = F.k as Element of S by A369;
           per cases;
            suppose F.k <> {}; then
             consider v be set such that
A370:         v in F.k by XBOOLE_0:def 1;
A371:         a.k = f.v by A370,A11,A367,A10,MESFUNC3:def 1;
             v in union rng F by A369,A370,TARSKI:def 4; then
             v in dom f by A10,MESFUNC3:def 1; then
A372:         0. <= a.k by A9,A371;
             EMPTY c= Fk by XBOOLE_1:2; then
             M.EMPTY <= M.Fk by MEASURE1:62; then
A373:         0. <= M.Fk by MEASURE1:def 11;
             M.Fk = (M*F).k by A11,A367,FUNCT_1:23;
             hence contradiction by A366,A373,A368,A372,EXTREAL2:45;
            end;
            suppose F.k = {}; then
             0. = M.(F.k) by MEASURE1:def 11
               .= (M*F).k by A11,A367,FUNCT_1:23;
             hence contradiction by A366,A368,EXTREAL1:22;
            end;
         end;
         len x = n+1 by  A11,FINSEQ_3:31,A13; then
A374:      x = x|(n+1) by FINSEQ_1:79
          .= x|Seg(n+1) by FINSEQ_1:def 15
          .= x1^<* x.(n+1) *> by A261,A11,FINSEQ_5:11
          .= x1^<* a.(n+1)*(M*F).(n+1) *> by A261,A11,A12;
         integral(X,S,M,f)=Sum z by A7,A8,A9,MESFUNC3:def 2,A344,A284,A288,A345
,A273,A305
            .=Sum(y) + Sum<* a.(n+1)*(M*H).(len z) *> by A346,A363,A304,
CONVFUN1:7
            .=integral(X,S,M,f1) + a.(n+1)*(M*H).(len z) by  CONVFUN1:5,A156
            .=Sum(x1) + a.(n+1)*(M*F).(n+1) by A304,A20,A142,A24,A30,A31,A143,
A150,A6
            .=Sum(x1) + Sum<* a.(n+1)*(M*F).(n+1) *> by CONVFUN1:5
            .=Sum(x) by A364,A374,A346,CONVFUN1:7;
         hence integral(X,S,M,f) = Sum(x);
        end;
       end;
       hence integral(X,S,M,f) = Sum(x);
      end;
     end;
     hence integral(X,S,M,f) = Sum(x);
   end;
   for n be Nat holds P[n] from NAT_1:sch 2(A1,A5);
   hence thesis;
end;

theorem Th3:
for X be non empty set,
    S be SigmaField of X,
    f be PartFunc of X,ExtREAL,
    M be sigma_Measure of S,
    F be Finite_Sep_Sequence of S,
    a,x be FinSequence of ExtREAL
 st f is_simple_func_in S
  & dom f <> {}
  & (for x be set st x in dom f holds 0. <= f.x)
  & F,a are_Re-presentation_of f
  & dom x = dom F
  & (for n be Nat st n in dom x holds x.n=a.n*(M*F).n)
 holds
    integral(X,S,M,f)=Sum(x)
proof
   let X be non empty set;
   let S be SigmaField of X;
   let f be PartFunc of X,ExtREAL;
   let M be sigma_Measure of S;
   let F be Finite_Sep_Sequence of S;
   let a,x be FinSequence of ExtREAL;
   assume that
A1: f is_simple_func_in S and
A2: dom f <> {} and
A3: for x be set st x in dom f holds 0. <= f.x and
A4: F,a are_Re-presentation_of f and
A5: dom x = dom F and
A6: for n be Nat st n in dom x holds x.n=a.n*(M*F).n;
   len F = len F;
   hence thesis by A1,A2,A3,A4,A5,A6,Th2;
end;

theorem Th4:
for X be non empty set,
    S be SigmaField of X,
    f be PartFunc of X,ExtREAL,
    M be sigma_Measure of S
 st f is_simple_func_in S
  & dom f <> {}
  & (for x be set st x in dom f holds 0. <= f.x)
 holds
    ex F be Finite_Sep_Sequence of S, a,x be FinSequence of ExtREAL
     st F,a are_Re-presentation_of f
      & dom x = dom F
      & (for n be Nat st n in dom x holds x.n=a.n*(M*F).n)
      & integral(X,S,M,f)=Sum(x)
proof
   let X be non empty set,
       S be SigmaField of X,
       f be PartFunc of X,ExtREAL,
       M be sigma_Measure of S;
   assume that
A1: f is_simple_func_in S and
A2: dom f <> {} and
A3: for x be set st x in dom f holds 0. <= f.x;
   consider F be Finite_Sep_Sequence of S,
    a be FinSequence of ExtREAL such that
A4: F,a are_Re-presentation_of f by A1,MESFUNC3:12;
   ex x be FinSequence of ExtREAL st
    dom x = dom F &
    for n be Nat st n in dom x holds x.n =a.n*(M*F).n
   proof
    deffunc Q(Nat) = a.$1*(M*F).$1;
    consider x be FinSequence of ExtREAL such that
A5:  len x = len F &
     for k be Element of NAT st k in Seg len F holds x.k=Q(k)
     from FINSEQ_2:sch 1;
    take x;
A6: for n be Nat st n in dom x holds x.n = a.n*(M*F).n
    proof
     let n be Nat;
     assume n in dom x; then
     n in Seg len x by FINSEQ_1:def 3;
     hence thesis by A5;
    end;
A7: dom x = Seg len F by  FINSEQ_1:def 3,A5
         .= dom F by FINSEQ_1:def 3;
    thus thesis by A6,A7;
   end; then
   consider x be FinSequence of ExtREAL such that
A8: dom x = dom F and
A9: (for n be Nat st n in dom x holds x.n=a.n*(M*F).n);
   integral(X,S,M,f)=Sum(x) by A1,A2,A3,A4,A8,A9,Th3;
   hence thesis by A4,A8,A9;
end;

theorem
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL
  st f is_simple_func_in S & dom f <> {}
   & (for x be set st x in dom f holds 0. <= f.x)
   & g is_simple_func_in S & dom g = dom f
   & (for x be set st x in dom g holds 0. <= g.x)
 holds
    f+g is_simple_func_in S & dom (f+g) <> {}
  & (for x be set st x in dom (f+g) holds 0. <= (f+g).x)
  & integral(X,S,M,f+g)=integral(X,S,M,f)+integral(X,S,M,g)
  proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL such that
A1:f is_simple_func_in S and
A2:dom f <> {} and
A3:for x be set st x in dom f holds 0. <= f.x and
A4:g is_simple_func_in S and
A5:dom g = dom f and
A6:for x be set st x in dom g holds 0. <= g.x;
   consider F be Finite_Sep_Sequence of S,
   a,x be FinSequence of ExtREAL such that
A7: F,a are_Re-presentation_of f and
    dom x = dom F and
    (for n be Nat st n in dom x holds x.n=a.n*(M*F).n) and
    integral(X,S,M,f)=Sum(x) by A1,A2,A3,Th4;
   consider G be Finite_Sep_Sequence of S,
   b,y be FinSequence of ExtREAL such that
A8: G,b are_Re-presentation_of g and
    dom y = dom G and
   (for n be Nat st n in dom y holds y.n=b.n*(M*G).n) and
   integral(X,S,M,g)=Sum(y) by A4,A5,A2,A6,Th4;
   set la = len a;
   set lb = len b;
A9:dom f = union rng F by A7,MESFUNC3:def 1;
A10:dom F = dom a by A7,MESFUNC3:def 1;
A11:dom g = union rng G by A8,MESFUNC3:def 1;
A12:dom G= dom b by A8,MESFUNC3:def 1;
   deffunc FG1(Nat) =
   F.(($1-'1) div lb + 1) /\ G.(($1-'1) mod lb + 1);
   consider FG be FinSequence such that
A13:len FG = la*lb and
A14: for k be Element of NAT st k in Seg (la*lb) holds FG.k=FG1(k)
    from FINSEQ_1:sch 2;
   FG is FinSequence of S
   proof
    now let k be Element of NAT;
     assume k in dom FG; then
A15: k in Seg (la*lb) by A13,FINSEQ_1:def 3; then
A16:  FG.k = F.((k-'1) div lb + 1) /\ G.((k-'1) mod lb + 1) by A14;
     set i=(k-'1) div lb + 1;
     set j=(k-'1) mod lb + 1;
A17:  i >= 0+1 & j >= 0+1 by XREAL_1:8;
A18:  1 <= k & k <= la*lb by A15,FINSEQ_1:3;
     reconsider la'=la,lb'=lb as natural number;
A19:  lb <> 0 by A18; then
A20:  lb > 0; then
     lb >= 0+1 by NAT_1:13; then
     lb' divides (la'*lb') & 1 <= la*lb & 1 <= lb
       by A18,NAT_D:def 3,XXREAL_0:2; then
A21:  ((la'*lb') -' 1) div lb' = ((la'*lb') div lb') - 1 by NAT_2:17;
     (k -' 1) <= (la*lb -' 1) by A18,BINARITH:60; then
     (k -' 1) div lb <= (la*lb -' 1) div lb by NAT_2:26; then
A22:  (k -' 1) div lb + 1 <= la*lb div lb by A21,XREAL_1:21;
     reconsider la,lb as Nat;
     i <= la by A19,A22,NAT_D:18; then
A23: i in Seg la by A17,FINSEQ_1:3;
     (k -' 1) mod lb < lb by A20,NAT_D:1; then
     j <= lb by NAT_1:13; then
A24: j in Seg lb by A17,FINSEQ_1:3;
A25: i in dom F by A10,FINSEQ_1:def 3,A23;
A26: j in dom G by A12,FINSEQ_1:def 3,A24;
A27: rng F c= S by FINSEQ_1:def 4;
A28: rng G c= S by FINSEQ_1:def 4;
A29: F.i in rng F by A25,FUNCT_1:12;
A30: G.j in rng G by A26,FUNCT_1:12;
A31: F.i is_measurable M by A27,A29,MEASURE1:def 12;
A32: G.j is_measurable M by A28,A30,MEASURE1:def 12;
     F.i /\ G.j is_measurable M by A31,A32,MEASURE1:66;
     hence FG.k in S by A16,MEASURE1:def 12;
    end;
    hence thesis by FINSEQ_2:14;
   end; then
   reconsider FG as FinSequence of S;
   for k,l be Nat st k in dom FG & l in dom FG & k <> l holds
    FG.k misses FG.l
   proof
    let k,l be Nat;
    assume
A33: k in dom FG & l in dom FG & k <> l;
    set i=(k-'1) div lb + 1;
    set j=(k-'1) mod lb + 1;
    set n=(l-'1) div lb + 1;
    set m=(l-'1) mod lb + 1;
A34:k in Seg (la*lb) by A33,A13,FINSEQ_1:def 3; then
A35: FG.k = F.i /\ G.j by A14;
A36:l in Seg (la*lb) by A33,A13,FINSEQ_1:def 3;
A37:  i >= 0+1 & j >= 0+1 by XREAL_1:8;
A38:  1 <= k & k <= la*lb by A34,FINSEQ_1:3;
     reconsider la'=la,lb'=lb as natural number;
A39:  lb <> 0 by A38; then
A40:  lb > 0; then
     lb >= 0+1 by NAT_1:13; then
     lb' divides (la'*lb') & 1 <= la*lb & 1 <= lb
       by A38,NAT_D:def 3,XXREAL_0:2; then
A41:  ((la'*lb') -' 1) div lb' = ((la'*lb') div lb') - 1 by NAT_2:17;
     (k -' 1) <= (la*lb -' 1) by A38,BINARITH:60; then
     (k -' 1) div lb <= (la*lb div lb) - 1 by A41,NAT_2:26; then
A42:  (k -' 1) div lb + 1 <= la*lb div lb by XREAL_1:21;
     reconsider la,lb as Nat;
     i <= la by A42,A39,NAT_D:18; then
A43:  i in Seg la by A37,FINSEQ_1:3;
     (k -' 1) mod lb < lb by A40,NAT_D:1; then
     j <= lb by NAT_1:13; then
A44:j in Seg lb by A37,FINSEQ_1:3;
A45:i in dom F by A43,A10,FINSEQ_1:def 3;
A46:j in dom G by A12,FINSEQ_1:def 3,A44;
A47:  n >= 0+1 & m >= 0+1 by XREAL_1:8;
A48:  1 <= l & l <= la*lb by A36,FINSEQ_1:3;
     (l -' 1) <= (la*lb -' 1) by A48,BINARITH:60; then
     (l -' 1) div lb <= (la*lb div lb) - 1 by A41,NAT_2:26; then
A49:  (l -' 1) div lb + 1 <= la*lb div lb by XREAL_1:21;
     n <= la by A49,A39,NAT_D:18; then
A50:n in Seg la by A47,FINSEQ_1:3;
     (l -' 1) mod lb < lb by A40,NAT_D:1; then
     m <= lb by NAT_1:13; then
A51:m in Seg lb by A47,FINSEQ_1:3;
A52:n in dom F by A10,FINSEQ_1:def 3,A50;
A53:m in dom G by A12,FINSEQ_1:def 3,A51;
A54: i <> n or j <> m
    proof
     assume
A55:  i=n & j=m;
     (k-'1)= (i-1)*lb+(j-1) by A40,NAT_D:2; then
     (k-'1)+1=(i-1)*lb+(j-1)+1; then
A56:k - 1 + 1 = (i-1)*lb + j by A38,BINARITH:50;
     (l-'1)= (n-1)*lb+(m-1) by A40,NAT_D:2; then
     (l-'1)+1=(n-1)*lb+(m-1)+1; then
A57:l - 1 + 1 = (n-1)*lb+m by A48,BINARITH:50;
     k=l by  A55,A56,A57;
     hence contradiction by A33;
    end;
    per cases by A54;
     suppose i <> n; then
A58:  F.i misses F.n by A45,A52,MESFUNC3:4;
      FG.k /\ FG.l= (F.i /\ G.j) /\ (F.n /\ G.m) by A36,A35,A14
                 .= F.i /\ (G.j /\ (F.n /\ G.m)) by XBOOLE_1:16
                 .= F.i /\ (F.n /\ (G.j /\ G.m)) by  XBOOLE_1:16
                 .= (F.i /\ F.n) /\ (G.j /\ G.m) by XBOOLE_1:16
                 .= {} /\ (G.j /\ G.m) by A58,XBOOLE_0:def 7
                 .= {};
      hence FG.k misses FG.l by XBOOLE_0:def 7;
     end;
     suppose j <> m; then
A59:  G.j misses G.m by A46,A53,MESFUNC3:4;
      FG.k /\ FG.l= (F.i /\ G.j) /\ (F.n /\ G.m) by A36,A35,A14
                 .= F.i /\ (G.j /\ (F.n /\ G.m)) by XBOOLE_1:16
                 .= F.i /\ (F.n /\ (G.j /\ G.m)) by  XBOOLE_1:16
                 .= (F.i /\ F.n) /\ (G.j /\ G.m) by XBOOLE_1:16
                 .= (F.i /\ F.n) /\ {} by A59,XBOOLE_0:def 7
                 .= {};
      hence FG.k misses FG.l by XBOOLE_0:def 7;
     end;
   end; then
   reconsider FG as Finite_Sep_Sequence of S by MESFUNC3:4;
   deffunc A1(Nat) = a.(($1-' 1) div lb +1);
   consider a1 be FinSequence of ExtREAL such that
A60:len a1 = len FG and
A61:for k be Element of NAT st k in Seg len FG holds a1.k=A1(k)
    from FINSEQ_2:sch 1;
   ex x1 be FinSequence of ExtREAL st
    dom x1 = dom FG &
    for n be Nat st n in dom x1 holds x1.n =a1.n*(M*FG).n
   proof
    deffunc X1(Nat) = a1.$1*(M*FG).$1;
    consider x1 be FinSequence of ExtREAL such that
A62:  len x1 = len FG  &
     for k be Element of NAT st k in Seg len FG holds x1.k=X1(k)
      from FINSEQ_2:sch 1;
    take x1;
    for n be Nat st n in dom x1 holds x1.n = a1.n*(M*FG).n
    proof
     let n be Nat;
     assume n in dom x1; then
     n in Seg len x1 by FINSEQ_1:def 3;
     hence thesis by A62;
    end;
    hence thesis by A62,FINSEQ_3:31;
   end; then
   consider x1 be FinSequence of ExtREAL such that
A63:dom x1 = dom FG and
A64:for n be Nat st n in dom x1 holds x1.n=a1.n*(M*FG).n;
   deffunc B1(Nat) = b.(($1-' 1) mod lb +1);
   consider b1 be FinSequence of ExtREAL such that
A65:len b1 = len FG and
A66:for k be Element of NAT st k in Seg len FG holds b1.k=B1(k)
    from FINSEQ_2:sch 1;
   ex y1 be FinSequence of ExtREAL st
    dom y1 = dom FG &
    for n be Nat st n in dom y1 holds y1.n =b1.n*(M*FG).n
   proof
    deffunc Y1(Nat) = b1.$1*(M*FG).$1;
    consider y1 be FinSequence of ExtREAL such that
A67:  len y1 = len FG  &
     for k be Element of NAT st k in Seg len FG holds y1.k=Y1(k)
     from FINSEQ_2:sch 1;
    take y1;
A68: for n be Nat st n in dom y1 holds y1.n = b1.n*(M*FG).n
    proof
     let n be Nat;
     assume n in dom y1; then
     n in Seg len y1 by FINSEQ_1:def 3;
     hence thesis by A67;
    end;
A69: dom y1 = Seg len FG by  FINSEQ_1:def 3,A67
          .= dom FG by FINSEQ_1:def 3;
    thus thesis by A68,A69;
   end; then
   consider y1 be FinSequence of ExtREAL such that
A70:dom y1 = dom FG and
A71:for n be Nat st n in dom y1 holds y1.n=b1.n*(M*FG).n;
   deffunc C1(Nat) = a1.$1+b1.$1;
   consider c1 be FinSequence of ExtREAL such that
A72:len c1 = len FG and
A73:for k be Element of NAT st k in Seg len FG holds c1.k=C1(k)
    from FINSEQ_2:sch 1;
   ex z1 be FinSequence of ExtREAL st
    dom z1 = dom FG &
    for n be Nat st n in dom z1 holds z1.n =c1.n*(M*FG).n
   proof
    deffunc Z1(Nat) = c1.$1*(M*FG).$1;
    consider z1 be FinSequence of ExtREAL such that
A74:  len z1 = len FG  &
     for k be Element of NAT st k in Seg len FG holds z1.k=Z1(k)
     from FINSEQ_2:sch 1;
    take z1;
 for n be Nat st n in dom z1 holds z1.n = c1.n*(M*FG).n
    proof
     let n be Nat;
     assume n in dom z1; then
     n in Seg len z1 by FINSEQ_1:def 3;
     hence thesis by A74;
    end;
    hence thesis by A74,FINSEQ_3:31;
   end; then
   consider z1 be FinSequence of ExtREAL such that
A75:dom z1 = dom FG and
A76:for n be Nat st n in dom z1 holds z1.n=c1.n*(M*FG).n;
A77:dom f = union rng FG
   proof
    thus dom f c= union rng FG
    proof
     let z be set;
     assume A78:z in dom f; then
     consider Y be set such that
A79:  z in Y & Y in rng F by A9,TARSKI:def 4;
     consider i be set such that
A80:  i in dom F & Y = F.i by A79,FUNCT_1:def 5;
     reconsider i as Element of NAT by A80;
     consider Z be set such that
A81:  z in Z & Z in rng G by A11,A78,A5,TARSKI:def 4;
     consider j be set such that
A82:  j in dom G & Z = G.j by A81,FUNCT_1:def 5;
     reconsider j as Element of NAT by A82;
     set k=(i-1)*lb+j;
     i in Seg len a by A10,A80,FINSEQ_1:def 3; then
A83: 1 <= i & i <= la by FINSEQ_1:3; then
     consider i' being Nat such that
A84: i = 1 + i' by NAT_1:10;
     i'*lb + j is Nat; then
     reconsider k as Nat by A84;
     j in Seg len b by A82,A12,FINSEQ_1:def 3; then
A85: 1 <= j & j <= lb by FINSEQ_1:3; then
     consider j' being Nat such that
A86: j = 1 + j' by NAT_1:10;
     i'*lb >= 0; then
     k >= 0 + j by A84,XREAL_1:8; then
A87:  k >= 1 by A85,XXREAL_0:2; then
A88: k -' 1 = k - 1 by BINARITH:50
           .= i'*lb + j' by A84,A86;
A89: j' < lb by A85,A86,NAT_1:13; then
A90:  i = (k-'1) div lb +1 by A84,A88,NAT_D:def 1;
A91:  j = (k-'1) mod lb +1 by A86,A88,A89,NAT_D:def 2;
     i-1 <= la-1 by A83,XREAL_1:11; then
     (i-1)*lb <= (la - 1)*lb by XREAL_1:66; then
A92:  k <= (la - 1) * lb + j by XREAL_1:8;
A93:  (la - 1) * lb + j <= (la - 1) * lb + lb by A85,XREAL_1:8;
     k <= la*lb by A92,A93,XXREAL_0:2; then
A94:  k in Seg (la*lb) by A87,FINSEQ_1:3; then
A95:  k in dom FG by A13,FINSEQ_1:def 3;
     z in F.i /\ G.j by A79,A80,A81,A82,XBOOLE_0:def 3; then
A96:  z in FG.k by A94,A90,A91,A14;
     FG.k in rng FG by A95,FUNCT_1:def 5;
    hence thesis by A96,TARSKI:def 4;
    end;
     let z be set;
     assume z in union rng FG; then
     consider Y be set such that
A97:   z in Y & Y in rng FG by TARSKI:def 4;
     consider k be set such that
A98:   k in dom FG & Y = FG.k by A97,FUNCT_1:def 5;
     reconsider k as Element of NAT by A98;
     set i=(k-'1) div lb +1;
     set j=(k-'1) mod lb +1;
     k in Seg (la*lb) by A13,A98,FINSEQ_1:def 3; then
     FG.k=F.i /\ G.j by A14; then
A99:  z in F.i by A97,A98,XBOOLE_0:def 3;
     reconsider i as Nat;
     k in Seg len FG by A98,FINSEQ_1:def 3; then
A100:  1 <= k & k <= la*lb by A13,FINSEQ_1:3;
A101:  i >= 0+1 & j >= 0+1 by XREAL_1:8;
     reconsider la'=la,lb'=lb as natural number;
A102:  lb <> 0 by A100; then
     lb > 0; then
     lb >= 0+1 by NAT_1:13; then
     lb' divides (la'*lb') & 1 <= la*lb & 1 <= lb
       by A100,NAT_D:def 3,XXREAL_0:2; then
A103:  ((la'*lb') -' 1) div lb' = ((la'*lb') div lb') - 1 by NAT_2:17;
     (k -' 1) <= (la*lb -' 1) by A100,BINARITH:60; then
     (k -' 1) div lb <= (la*lb div lb) - 1 by A103,NAT_2:26; then
A104:  i <= la*lb div lb by XREAL_1:21;
     la*lb div lb = la by A102,NAT_D:18; then
     i in Seg la by A101,A104,FINSEQ_1:3; then
     i in  dom F by A10,FINSEQ_1:def 3; then
     F.i in rng F by FUNCT_1:def 5;
     hence thesis by A9,A99,TARSKI:def 4;
  end;
A105: g is_finite by A4,MESFUNC2:def 5;
A106: dom (f+g) = dom f /\ dom f by  A105,MESFUNC2:2,A5
             .= dom f;
A107: dom (f+g) = dom g /\ dom g by  A105,MESFUNC2:2,A5
             .= dom g;
A108: f+g is_finite
    proof
      now let x be Element of X;
        assume A109: x in dom (f+g);
        f is_finite by A1,MESFUNC2:def 5; then
        A110: |. f.x .| < +infty by A109,A106,MESFUNC2:def 1;
        g is_finite by A4,MESFUNC2:def 5; then
        A111: |. g.x .| < +infty by A107,A109,MESFUNC2:def 1;
        A112: |. (f+g).x .| = |. f.x + g.x .| by A109,MESFUNC1:def 3;
        not (f.x = +infty or f.x = -infty) &
        not (g.x = +infty or g.x = -infty) by A110,A111,EXTREAL2:67; then
        A113: |. (f+g).x .| <= |. f.x .| + |. g.x .| by A112,EXTREAL2:61;
        |. f.x .| + |. g.x .| <> +infty by A110,A111,SUPINF_2:8; then
        |. f.x .| + |. g.x .| < +infty by XXREAL_0:4;
        hence |. (f+g).x .| < +infty by A113,XXREAL_0:2;
      end;
      hence thesis by MESFUNC2:def 1;
    end;
A114: dom (f+g) = union rng FG by  A106,A77;
A115: for k being Nat,x,y being Element of X st
          k in dom FG & x in FG.k & y in FG.k holds (f+g).x = (f+g).y
    proof
      let k be Nat;
      let x,y be Element of X;
      assume
   A116: k in dom FG & x in FG.k & y in FG.k; then
      k in Seg (la*lb) by A13,FINSEQ_1:def 3; then
A117:   FG.k = F.( (k-'1) div lb + 1 ) /\ G.( (k-'1) mod lb  + 1 ) by A14;
      set i=(k-'1) div lb + 1;
      set j=(k-'1) mod lb + 1;
     k in Seg len FG by A116,FINSEQ_1:def 3; then
A118:  1 <= k & k <= la*lb by A13,FINSEQ_1:3;
A119:  i >= 0+1 & j >= 0+1 by XREAL_1:8;
     reconsider la'=la,lb'=lb as natural number;
A120:  lb <> 0 by A118; then
A121:  lb > 0; then
     lb >= 0+1 by NAT_1:13; then
     lb' divides (la'*lb') & 1 <= la*lb & 1 <= lb
       by A118,NAT_D:def 3,XXREAL_0:2; then
A122:  ((la'*lb') -' 1) div lb' = ((la'*lb') div lb') - 1 by NAT_2:17;
     (k -' 1) <= (la*lb -' 1) by A118,BINARITH:60; then
     (k -' 1) div lb <= (la*lb div lb) - 1 by A122,NAT_2:26; then
     (k -' 1) div lb + 1 <= la*lb div lb by XREAL_1:21; then
     i <= la by A120,NAT_D:18; then
A123: i in Seg la by A119,FINSEQ_1:3;
     (k -' 1) mod lb < lb by A121,NAT_D:1; then
     j <= lb by NAT_1:13; then
A124: j in Seg lb by A119,FINSEQ_1:3;
A125: i in dom F by A10,FINSEQ_1:def 3,A123;
A126: j in dom G by A12,FINSEQ_1:def 3,A124;
A127: x in F.i by A116,A117,XBOOLE_0:def 3;
A128: x in G.j by A116,A117,XBOOLE_0:def 3;
A129: f.x=a.i by A127,A125,A7,MESFUNC3:def 1;
A130: y in F.i by A116,A117,XBOOLE_0:def 3;
A131: y in G.j by A116,A117,XBOOLE_0:def 3;
A132: f.y=a.i by A130,A125,A7,MESFUNC3:def 1;
A133: FG.k in rng FG by A116,FUNCT_1:def 5; then
A134: x in dom (f+g) by A114,A116,TARSKI:def 4;
A135: y in dom (f+g) by A114,A116,A133,TARSKI:def 4;
    thus (f+g).x= f.x+g.x by A134,MESFUNC1:def 3
               .= a.i+b.j by A129,A128,A126,A8,MESFUNC3:def 1
               .= f.y+g.y by A132,A131,A126,A8,MESFUNC3:def 1
               .= (f+g).y by A135,MESFUNC1:def 3;
    end;
   thus
   A136: f+g is_simple_func_in S by A108,A114,A115,MESFUNC2:def 5;
  thus dom (f+g) <> {} by A106,A2;
  thus
A137: for x be set st x in dom (f+g) holds 0. <= (f+g).x
   proof
   let x be set;
   assume A138: x in dom (f+g); then
A139:  0. <= f.x by A3,A106;
     0. <= g.x by A6,A107,A138; then
A140:  0. <= f.x + g.x by A139,EXTREAL2:7;
     dom f c= X by RELSET_1:12;
     hence 0. <= (f + g).x by A138,A106,A140,MESFUNC1:def 3;
   end;
A141:
    for k be Nat st k in dom FG
     for x be set st x in FG.k holds f.x=a1.k
    proof
     let k be Nat;
     assume
A142:   k in dom FG;
     let x be set;
      assume
A143:   x in FG.k;
      k in Seg (la*lb) by A142,A13,FINSEQ_1:def 3; then
A144:   FG.k = F.((k-'1) div lb + 1) /\ G.((k-'1) mod lb + 1) by A14;
      set i=(k-'1) div lb + 1;
      set j=(k-'1) mod lb + 1;
      k in Seg len FG by A142,FINSEQ_1:def 3; then
A145:   1 <= k & k <= la*lb by A13,FINSEQ_1:3;
A146:   i >= 0+1 & j >= 0+1 by XREAL_1:8;
      reconsider la'=la,lb'=lb as natural number;
A147:   lb <> 0 by A145; then
      lb > 0; then
      lb >= 0+1 by NAT_1:13; then
      lb' divides (la'*lb') & 1 <= la*lb & 1 <= lb
       by A145,NAT_D:def 3,XXREAL_0:2; then
A148:   ((la'*lb') -' 1) div lb' = ((la'*lb') div lb') - 1 by NAT_2:17;
      (k -' 1) <= (la*lb -' 1) by A145,BINARITH:60; then
      (k -' 1) div lb <= (la*lb -' 1) div lb by NAT_2:26; then
A149:   i <= la*lb div lb by A148,XREAL_1:21;
      la*lb div lb = la by A147,NAT_D:18; then
A150:  i in Seg la by A149,A146,FINSEQ_1:3;
A151:  i in dom F by A10,FINSEQ_1:def 3,A150;
A152:   x in F.i by A143,A144,XBOOLE_0:def 3;
A153:   k in Seg len FG by A142,FINSEQ_1:def 3;
      thus f.x=a.i by A152,A151,A7,MESFUNC3:def 1 .=a1.k by A153,A61;
    end;
A154: FG,a1 are_Re-presentation_of f
  proof
  thus dom f  = union rng FG by A77;
  thus dom FG = Seg len a1 by  FINSEQ_1:def 3,A60
            .= dom a1 by FINSEQ_1:def 3;
  thus thesis by A141;
  end;
A155: for k be Nat st k in dom FG
         for x be set st x in FG.k holds g.x=b1.k
    proof
     let k be Nat;
      assume A156: k in dom FG;
      let x be set;
        assume
    A157: x in FG.k;
        k in Seg (la*lb) by A156,A13,FINSEQ_1:def 3; then
    A158: FG.k = F.( (k-'1) div lb + 1 ) /\ G.( (k-'1) mod lb + 1 ) by A14;
        set i=(k-'1) div lb + 1;
        set j=(k-'1) mod lb + 1;
     k in Seg len FG by A156,FINSEQ_1:def 3; then
A159:  1 <= k & k <= la*lb by A13,FINSEQ_1:3;
A160:  i >= 0+1 & j >= 0+1 by XREAL_1:8;
     reconsider la'=la,lb'=lb as natural number;
     lb <> 0 by A159; then
A161:  lb > 0;
     (k -' 1) mod lb < lb by A161,NAT_D:1; then
     j <= lb by NAT_1:13; then
     A162: j in Seg lb by A160,FINSEQ_1:3;
     A163: j in dom G by A12,FINSEQ_1:def 3,A162;
     A164: x in G.j by A157,A158,XBOOLE_0:def 3;
     A165: k in Seg len FG by A156,FINSEQ_1:def 3;
         thus g.x=b.j by A164,A163,A8,MESFUNC3:def 1
                  .=b1.k by A165,A66;
    end;
A166: FG,b1 are_Re-presentation_of g
   proof
   thus dom g = union rng FG by A77,A5;
   thus dom FG = Seg len b1 by  FINSEQ_1:def 3,A65
             .= dom b1 by FINSEQ_1:def 3;
   thus thesis by A155;
   end;
A167: for k be Nat st k in dom FG
      for x be set st x in FG.k holds (f+g).x=c1.k
   proof
      let k be Nat;
      assume
   A168: k in dom FG;
      let x be set;
      assume
A169:     x in FG.k;
        FG.k in rng FG by A168,FUNCT_1:def 5; then
    A170: x in dom (f+g) by A114,A169,TARSKI:def 4;
    A171: k in Seg len FG by A168,FINSEQ_1:def 3;
        dom (f+g) c= X by RELSET_1:12;
        hence (f+g).x= f.x+g.x by A170,MESFUNC1:def 3
                  .=a1.k+g.x by A168,A169,A141
                  .=a1.k+b1.k by A168,A169,A155
                  .=c1.k by A171,A73;
  end;
A172: FG,c1 are_Re-presentation_of (f+g)
   proof
   thus dom (f+g) = union rng FG by A106,A77;
   thus dom FG = dom c1 by A72,FINSEQ_3:31;
   thus thesis by A167;
   end;
A173: integral(X,S,M,f)=Sum x1 by A1,A2,A3,A154,A63,A64,Th3;
A174: integral(X,S,M,g)=Sum y1 by A4,A5,A2,A6,A166,A70,A71,Th3;
A175: integral(X,S,M,f+g)=Sum z1
              by A136,A106,A2,A137,A172,A75,A76,Th3;
A176: dom x1=dom y1 by  A63,A70;
A177: for i be Nat st i in dom x1 holds 0. <= x1.i
  proof
   let i be Nat;
   assume
A178:  i in dom x1; then
A179: x1.i=a1.i*(M*FG).i by A64;
A180: i in Seg len FG by A178,A63,FINSEQ_1:def 3;
    set i' = (i -' 1) div lb + 1;
A181: 1 <= i & i <= la*lb by A180,A13,FINSEQ_1:3;
A182: i' >= 0+1 by XREAL_1:8;
    reconsider la'=la,lb'=lb as natural number;
A183: lb <> 0 by A181; then
    lb > 0; then
    lb >= 0+1 by NAT_1:13; then
    lb' divides (la'*lb') & 1 <= la*lb & 1 <= lb
      by A181,NAT_D:def 3,XXREAL_0:2; then
A184: ((la'*lb') -' 1) div lb' = ((la'*lb') div lb') - 1 by NAT_2:17;
    i -' 1 <= (la*lb -' 1) by A181,BINARITH:60; then
    (i -' 1) div lb <= (la*lb div lb) - 1 by A184,NAT_2:26; then
A185: i' <= la*lb div lb by XREAL_1:21;
    la*lb div lb = la by A183,NAT_D:18; then
    i' in Seg la by A185,A182,FINSEQ_1:3; then
A186: i' in dom F by A10,FINSEQ_1:def 3;
    per cases;
     suppose F.i' <> {}; then
      consider x' be set such that
A187:   x' in F.i' by XBOOLE_0:def 1;
A188:  f.x' = a.i' by A186,A187,A7,MESFUNC3:def 1 .= a1.i by A180,A61;
      F.i' in rng F by A186,FUNCT_1:12; then
      F.i' c= dom f by A9,ZFMISC_1:92; then
A189:  0. <= a1.i by A187,A3,A188;
A190:  FG.i in rng FG by A178,A63,FUNCT_1:12;
      rng FG c= S by FINSEQ_1:def 4; then
      reconsider FGi = FG.i as Element of S by A190;
      reconsider EMPTY = {} as Element of S by MEASURE1:21;
      EMPTY c= FGi by XBOOLE_1:2; then
A191:  M.({}) <= M.FGi by MEASURE1:62;
      M.{} = 0. by MEASURE1:def 11; then
      0. <= (M*FG).i by A191,A178,A63,FUNCT_1:23;
      hence 0. <= x1.i by A179,A189,EXTREAL2:45;
     end;
     suppose
A192:   F.i' = {};
      FG.i = F.i' /\ G.((i-'1) mod lb + 1) by A180,A13,A14; then
      M.(FG.i) = 0. by A192,MEASURE1:def 11; then
      (M*FG).i = 0. by A178,A63,FUNCT_1:23;
      hence 0. <= x1.i by A179,EXTREAL1:22;
     end;
   end;
A193:for i be Nat st i in dom y1 holds 0. <= y1.i
   proof
    let i be Nat;
    assume A194: i in dom y1;
A195: y1.i=b1.i*(M*FG).i by A71,A194;
A196: i in Seg len FG by A194,A70,FINSEQ_1:def 3;
    set i' = (i -' 1) mod lb + 1;
A197: 1 <= i & i <= la*lb by A196,A13,FINSEQ_1:3;
A198: i' >= 0+1 by XREAL_1:8;
    reconsider la'=la,lb'=lb as natural number;
    lb <> 0 by A197; then
A199: lb > 0;
    (i -' 1) mod lb < lb by A199,NAT_D:1; then
    i' <= lb by NAT_1:13; then
    i' in Seg lb by A198,FINSEQ_1:3; then
A200: i' in dom G by A12,FINSEQ_1:def 3;
    per cases;
     suppose G.i' <> {}; then
      consider x' be set such that
A201:   x' in G.i' by XBOOLE_0:def 1;
A202:  g.x' = b.i' by A200,A201,A8,MESFUNC3:def 1 .= b1.i by A196,A66;
      G.i' in rng G by A200,FUNCT_1:12; then
      G.i' c= dom g by A11,ZFMISC_1:92; then
A203:  0. <= b1.i by A201,A6,A202;
A204:  FG.i in rng FG by A194,A70,FUNCT_1:12;
      rng FG c= S by FINSEQ_1:def 4; then
      reconsider FGi = FG.i as Element of S by A204;
      reconsider EMPTY = {} as Element of S by MEASURE1:21;
      EMPTY c= FGi by XBOOLE_1:2; then
A205:  M.({}) <= M.FGi by MEASURE1:62;
      M.{} = 0. by MEASURE1:def 11; then
      0. <= (M*FG).i by A205,A194,A70,FUNCT_1:23;
      hence 0. <= y1.i by A195,A203,EXTREAL2:45;
     end;
     suppose
A206:   G.i' = {};
      FG.i = F.((i-'1) div lb + 1) /\ G.i' by A196,A13,A14; then
      M.(FG.i) = 0. by A206,MEASURE1:def 11; then
      (M*FG).i = 0. by A194,A70,FUNCT_1:23;
      hence 0. <= y1.i by A195,EXTREAL1:22;
     end;
   end;
   z1=x1+y1
   proof
    not -infty in rng x1
    proof
     assume -infty in rng x1; then
     consider i be set such that
A207: i in dom x1 & x1.i = -infty by FUNCT_1:def 5;
      i is Nat by A207,ORDINAL1:def 13;
      hence contradiction by A207,A177;
    end; then
A208: x1"{-infty} /\ y1"{+infty}
       = {} /\ y1"{+infty} by FUNCT_1:142
       .={};
    not -infty in rng y1
    proof
     assume -infty in rng y1; then
     consider i be set such that
A209:   i in dom y1 & y1.i = -infty by FUNCT_1:def 5;
     i is Nat by A209,ORDINAL1:def 13;
     hence contradiction by A209,A193;
    end; then
A210: x1"{+infty} /\ y1"{-infty}
       =x1"{+infty} /\ {} by FUNCT_1:142
       .={};
A211: dom (x1+y1)
     =(dom x1 /\ dom y1) \ ({} \/ {}) by A208,A210,MESFUNC1:def 3
    .=dom z1 by  A63,A70,A75;
A212: for k be Element of NAT st k in dom z1 holds z1.k = (x1+y1).k
    proof
     let k be Element of NAT;
     assume
A213:    k in dom z1; then
A214:  k in Seg len FG by A75,FINSEQ_1:def 3;
     set p=(k-'1) div lb + 1;
     set q=(k-'1) mod lb + 1;
A215:  1 <= k & k <= la*lb by A13,A214,FINSEQ_1:3;
A216:  p >= 0+1 & q >= 0+1 by XREAL_1:8;
     reconsider la'=la,lb'=lb as natural number;
A217:  lb <> 0 by A215; then
A218:  lb > 0; then
     lb >= 0+1 by NAT_1:13; then
     lb' divides (la'*lb') & 1 <= la*lb & 1 <= lb
       by A215,NAT_D:def 3,XXREAL_0:2; then
A219:  ((la'*lb') -' 1) div lb' = ((la'*lb') div lb') - 1 by NAT_2:17;
     (k -' 1) <= (la*lb -' 1) by A215,BINARITH:60; then
     (k -' 1) div lb <= (la*lb div lb) - 1 by A219,NAT_2:26; then
A220:  p <= la*lb div lb by XREAL_1:21;
     p <= la by A220,A217,NAT_D:18; then
A221: p in Seg la by A216,FINSEQ_1:3;
     (k -' 1) mod lb < lb by A218,NAT_D:1; then
     q <= lb by NAT_1:13; then
A222: q in Seg lb by A216,FINSEQ_1:3;
A223:  p in dom F by A10,FINSEQ_1:def 3,A221;
A224:  q in dom G by A12,FINSEQ_1:def 3,A222;
A225:  (a1.k+b1.k)*(M*FG).k = a1.k*(M*FG).k + b1.k*(M*FG).k
     proof
      per cases;
       suppose FG.k <> {}; then
        F.p /\ G.q <> {} by A214,A13,A14; then
        consider v be set such that
A226:     v in F.p /\ G.q by XBOOLE_0:def 1;
A227:     v in F.p & v in G.q by A226,XBOOLE_0:def 3; then
A228:     a.p = f.v by A7,A223,MESFUNC3:def 1;
        F.p in rng F by A223,FUNCT_1:12; then
        v in dom f by A9,A227,TARSKI:def 4; then
        0. <= a.p by A228,A3; then
        0. <= a1.k by A61,A214; then
A229:     0. = a1.k or 0. < a1.k;
A230:     b.q = g.v by A8,A224,A227,MESFUNC3:def 1;
        G.q in rng G by A224,FUNCT_1:12; then
        v in dom g by A11,A227,TARSKI:def 4; then
        0. <= b.q by A230,A6; then
        0. <= b1.k by A66,A214; then
        0. = b1.k or 0. < b1.k;
        hence thesis by A229,EXTREAL1:29;
       end;
       suppose FG.k = {}; then
        M.(FG.k) = 0. by MEASURE1:def 11; then
        (M*FG).k = 0. by A213,A75,FUNCT_1:23;
        hence thesis by EXTREAL1:29;
       end;
     end;
     thus z1.k = c1.k*(M*FG).k by A76,A213
              .= a1.k*(M*FG).k+b1.k*(M*FG).k by  A73,A214,A225
              .= x1.k + b1.k*(M*FG).k by A64,A213,A75,A63
              .= x1.k + y1.k by A71,A213,A75,A70
              .= (x1+y1).k by A211,A213,MESFUNC1:def 3;
    end;
    dom (x1+y1) = Seg len z1 by  A211,FINSEQ_1:def 3; then
    x1+y1 is FinSequence by FINSEQ_1:def 2;
    hence thesis by A211,A212,FINSEQ_1:17;
  end;
  hence thesis by A173,A174,A175,A177,A193,A176,Th1;
end;

theorem
for X be non empty set,
    S be SigmaField of X,
    M be sigma_Measure of S,
    f,g be PartFunc of X,ExtREAL, c be R_eal
  st f is_simple_func_in S
   & dom f <> {}
   & (for x be set st x in dom f holds 0. <= f.x)
   & 0. <= c & c < +infty
   & dom g = dom f
   & (for x be set st x in dom g holds g.x=c*f.x)
 holds
    integral(X,S,M,g)=c*integral(X,S,M,f)
proof
   let X be non empty set,
       S be SigmaField of X,
       M be sigma_Measure of S,
       f,g be PartFunc of X,ExtREAL,
       c be R_eal such that
A1:f is_simple_func_in S and
A2:dom f <> {} and
A3:for x be set st x in dom f holds 0. <= f.x and
A4:0. <= c & c < +infty and
A5:dom g = dom f and
A6:for x be set st x in dom g holds g.x=c*f.x;
   consider F be Finite_Sep_Sequence of S,
            a be FinSequence of ExtREAL,
            x be FinSequence of ExtREAL such that
A7: F,a are_Re-presentation_of f and
A8: dom x = dom F and
A9: for n be Nat st n in dom x holds x.n=a.n*(M*F).n and
A10: integral(X,S,M,f)=Sum(x) by A1,A2,A3,Th4;
A11:f is_finite by A1,MESFUNC2:def 5;
   for x be Element of X st x in dom g holds |. g.x .| < +infty
   proof
    let x be Element of X;
    assume
A12:  x in dom g; then
    |. f.x .| < +infty by A5,A11,MESFUNC2:def 1; then
    f.x <> -infty & f.x <> +infty by EXTREAL2:67; then
    c*f.x <> +infty & c*f.x <> -infty by A4,EXTREAL1:27,28; then
    g.x <> +infty & g.x <> -infty by A12,A6; then
    -infty < g.x & g.x < +infty by XXREAL_0:6,4; then
    -(+infty) < g.x & g.x < +infty by SUPINF_2:def 3;
    hence thesis by EXTREAL2:59;
   end; then
A13:g is_finite by MESFUNC2:def 1;
A14:ex G be Finite_Sep_Sequence of S st
    (dom g = union rng G &
     for n be Nat, x,y be Element of X st
      n in dom G & x in G.n & y in G.n holds g.x = g.y)
   proof
    consider G be Finite_Sep_Sequence of S such that
A15: dom f = union rng G and
A16: for n be Nat, x,y be Element of X st
      n in dom G & x in G.n & y in G.n holds f.x = f.y by A1,MESFUNC2:def 5;
A17:for n be Nat, x,y be Element of X st
     n in dom G & x in G.n & y in G.n holds g.x = g.y
    proof
     let n be Nat;
     let x,y be Element of X;
     assume
A18: n in dom G & x in G.n & y in G.n; then
     G.n in rng G by FUNCT_1:12; then
     x in dom g & y in dom g by A5,A15,A18,TARSKI:def 4; then
     g.x = c*f.x & g.y = c*f.y by A6;
     hence thesis by A18,A16;
    end;
    take G;
    thus thesis by A5,A15,A17;
   end;
A19:g is_simple_func_in S by A13,A14,MESFUNC2:def 5;
A20:for x be set st x in dom g holds 0. <= g.x
   proof
    let x be set;
    assume
A21:  x in dom g; then
    0. <= f.x by A5,A3; then
    0. <= c*f.x by A4,EXTREAL2:45;
    hence thesis by A21,A6;
   end;
   consider G be Finite_Sep_Sequence of S,
            b be FinSequence of ExtREAL;
   deffunc bMG(Nat) = b.$1 * (M*G).$1;
   consider y be FinSequence such that
A22: len y = len G &
    for n be Element of NAT st n in Seg len G holds y.n = bMG(n)
     from FINSEQ_1:sch 2;
   rng y c= ExtREAL
   proof
     let v be set;
     assume v in rng y; then
     consider k be set such that
A23:  k in dom y & v = y.k by FUNCT_1:def 5;
     reconsider k as Element of NAT by A23;
     k in Seg len G by A22,A23,FINSEQ_1:def 3; then
     v = b.k * (M*G).k by A22,A23;
     hence thesis;
   end; then
   reconsider y as FinSequence of ExtREAL by FINSEQ_1:def 4;
     ex b be FinSequence of ExtREAL st
      dom b = dom a &
      for n be Nat st n in dom b holds b.n=c*a.n
     proof
      deffunc ca(Nat) = c*a.$1;
      consider b be FinSequence such that
A24:  len b = len a &
      for n be Element of NAT st n in Seg len a holds b.n = ca(n)
      from FINSEQ_1:sch 2;
A25:  dom b = Seg len b by FINSEQ_1:def 3;
      rng b c= ExtREAL
      proof
        let v be set;
        assume v in rng b; then
        consider k be set such that
A26:     k in dom b & v = b.k by FUNCT_1:def 5;
        reconsider k as Element of NAT by A26;
        v = c*a.k by A24,A25,A26;
        hence thesis;
      end; then
      reconsider b as FinSequence of ExtREAL by FINSEQ_1:def 4;
      take b;
      thus thesis by A24,A25,FINSEQ_1:def 3;
     end; then
     consider b be FinSequence of ExtREAL such that
A27:  dom b = dom a and
A28:  for n be Nat st n in dom b holds b.n=c*a.n;
     ex z be FinSequence of ExtREAL st
     dom z = dom x & for n be Nat st n in dom z holds z.n=c*x.n
     proof
      deffunc cx(Nat) = c*x.$1;
      consider z be FinSequence such that
A29:  len z = len x &
      for n be Element of NAT st n in Seg len x holds z.n = cx(n)
      from FINSEQ_1:sch 2;
A30:  dom z = Seg len z by FINSEQ_1:def 3;
      rng z c= ExtREAL
      proof
        let v be set;
        assume v in rng z; then
        consider k be set such that
A31:     k in dom z & v = z.k by FUNCT_1:def 5;
        reconsider k as Element of NAT by A31;
        v = c*x.k by A29,A30,A31;
        hence thesis;
      end; then
      reconsider z as FinSequence of ExtREAL by FINSEQ_1:def 4;
      take z;
      thus thesis by A29,A30,FINSEQ_1:def 3;
     end; then
     consider z be FinSequence of ExtREAL such that
A32:   dom z = dom x and
A33:   for n be Nat st n in dom z holds z.n=c*x.n;
A34:  F,b are_Re-presentation_of g
     proof
A35:   dom g =union rng F by  A5,A7,MESFUNC3:def 1;
A36:   dom F = dom b by  A7,MESFUNC3:def 1,A27;
      now let n be Nat;
       assume
A37:     n in dom F;
       let x be set;
       assume
A38:     x in F.n;
       F.n in rng F by A37,FUNCT_1:12; then
A39:    x in dom g by A35,A38,TARSKI:def 4;
A40:    n in dom b by A37,A7,MESFUNC3:def 1,A27;
       thus g.x= c*f.x by A39,A6
              .= c*a.n by A38,A7,A37,MESFUNC3:def 1 .= b.n by A40,A28;
      end;
      hence thesis by A35,A36,MESFUNC3:def 1;
     end;
A41:  dom z =dom F by  A32,A8;
A42:  for n be Nat st n in dom z holds z.n=b.n*(M*F).n
     proof
      let n be Nat;
      assume
A43:    n in dom z;
A44:   dom a = dom F by A7,MESFUNC3:def 1;
      thus z.n = c*x.n by A33,A43 .=c*(a.n*(M*F).n) by A43,A9,A32
              .= c*a.n*((M*F).n) by EXTREAL1:23
              .=b.n*(M*F).n by A28,A43,A27,A44,A41;
     end;
     thus integral(X,S,M,g)=Sum z by A19,A2,A5,A20,A34,A41,A42,Th3
                 .=c*integral(X,S,M,f) by  A4,A32,A33,MESFUNC3:10,A10;
end;
