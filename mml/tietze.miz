:: Tietze {E}xtension {T}heorem
::  by Artur Korni{\l}owicz , Grzegorz Bancerek and Adam Naumowicz
:: 
:: Received September 14, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies FUNCT_1, ARYTM, TIETZE, RELAT_1, BOOLE, SEQ_1, COMPTS_1,
      PRE_TOPC, TOPMETR, ARYTM_1, ORDINAL2, ABSVALUE, ARYTM_3, FUNCOP_1,
      TARSKI, LIMFUNC1, RCOMP_1, TOPS_2, SETFAM_1, SEQFUNC, SERIES_1, PARTFUN1,
      GROUP_1, FUNCT_2, PSCOMP_1, TMAP_1, TDGROUP, METRIC_1, POWER, ASYMPT_0,
      COMPLEX1, SEQ_2, PREPOWER, SUPINF_2, SEQM_3, RLVECT_1, SUBSET_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, ORDINAL2, XCMPLX_0, NUMBERS,
      XREAL_0, REAL_1, NAT_1, COMPLEX1, NEWTON, PREPOWER, POWER, RELAT_1,
      FUNCT_1, RELSET_1, PARTFUN1, FUNCT_2, SEQ_1, SEQ_2, SEQM_3, SERIES_1,
      LIMFUNC1, RCOMP_1, SEQFUNC, STRUCT_0, PRE_TOPC, TOPS_2, BORSUK_1,
      COMPTS_1, TOPMETR, TSEP_1, TMAP_1, PSCOMP_1, TOPREALB, METRIC_1,
      PCOMPS_1, PARTFUN3;
 constructors SERIES_1, SEQM_3, PREPOWER, LIMFUNC1, TOPREALB, URYSOHN3, TMAP_1,
      PROB_1, YELLOW13, CONNSP_1, NAT_1, T_0TOPSP, PARTFUN3;
 registrations XREAL_0, SEQ_1, PRE_TOPC, BORSUK_1, RELSET_1, RCOMP_3, TOPREALB,
      FCONT_3, STRUCT_0, XBOOLE_0, BORSUK_6, NAT_1, MEMBERED, ORDINAL2,
      FUNCT_2, PSCOMP_1, BORSUK_3, WAYBEL12, FUNCT_1, METRIC_1, NEWTON,
      SUBSET_1;
 requirements BOOLE, SUBSET, NUMERALS, REAL, ARITHM;
 definitions TARSKI, XBOOLE_0, SEQM_3, SEQFUNC, PRE_TOPC, BORSUK_1, COMPTS_1;
 theorems PRE_TOPC, LIMFUNC1, RCOMP_1, XBOOLE_0, TARSKI, FUNCT_1, RELAT_1,
      TOPREALB, URYSOHN3, JORDAN1, TSEP_1, JGRAPH_5, XREAL_0, TOPREALA,
      TOPMETR, ABSVALUE, FUNCT_2, XREAL_1, TOPS_2, SEQ_1, BORSUK_5, TMAP_1,
      TOPS_1, XBOOLE_1, BORSUK_1, FUNCOP_1, EXTENS_1, NEWTON, HEINE, TOPREAL6,
      JGRAPH_3, GRFUNC_1, RFUNCT_1, SEQFUNC, METRIC_1, GOBOARD6, RSSPACE,
      SEQ_2, SEQ_4, XCMPLX_1, SERIES_1, COMPLEX1, XCMPLX_0, ORDINAL2, POWER,
      PREPOWER, NAT_1, SEQM_3, UNIFORM1, PARTFUN1, MESFUNC1, JCT_MISC,
      RELSET_1;
 schemes NAT_1, RECDEF_1, FUNCT_2;

begin
  
 reserve r, s for real number,
         k, n, m for natural number,
         X for set,
         f, g, h, l for real-yielding Function;

theorem LemC:
  for a,b,c being real number st |.a-b.| <= c
  holds b-c <= a & a <= b+c
  proof
    let a,b,c be real number;
    assume
A0: |.a-b.| <= c;
A3: |.a-b.| >= 0 by COMPLEX1:132; then
A2: b <= b+c & b >= b-c by A0,XREAL_1:33,45;
    per cases;
    suppose a-b >= 0; then
A4:   |.a-b.| = a-b by ABSVALUE:def 1; then
      a >= 0+b by A3,XREAL_1:21;
      hence b-c <= a & a <= b+c by A0,A2,A4,XREAL_1:2,22;
    end;
    suppose a-b < 0; then
A5:   |.a-b.| = -(a-b) by ABSVALUE:def 1 .= b-a; then
      0+a <= b by A3,XREAL_1:21;
      hence b-c <= a & a <= b+c by A0,A2,A5,XREAL_1:2,14;
    end;
  end;
  
theorem Th1:
  r < s implies left_closed_halfline(r) misses right_closed_halfline(s)
  proof
    assume
A1: r < s;
    thus left_closed_halfline(r) /\ right_closed_halfline(s) = [.s,r.]
    by LIMFUNC1:19
    .= {} by A1,RCOMP_1:13;
  end;
  
theorem Th2:
  r <= s implies left_open_halfline(r) misses right_open_halfline(s)
  proof
    assume
A1: r <= s;
    thus left_open_halfline(r) /\ right_open_halfline(s) = ].s,r.[
    by LIMFUNC1:18
    .= {} by A1,RCOMP_1:12;
  end;

theorem Tmm1:
  f c= g implies h-f c= h-g
  proof
    assume
a0: f c= g; then
a2: dom f c= dom g by GRFUNC_1:8;
a3: dom(h-g) = dom h /\ dom g by SEQ_1:def 4;
a4: dom(h-f) = dom h /\ dom f by SEQ_1:def 4;
    then
a1: dom(h-f) c= dom(h-g) by a2,a3,XBOOLE_1:27;
    now
      let x be set;
      assume
a5:   x in dom(h-f);
      then
a6:   x in dom f by a4,XBOOLE_0:def 3;
      thus (h-f).x = h.x - f.x by a5,SEQ_1:def 4
      .= h.x - g.x by a0,a6,GRFUNC_1:8
      .= (h-g).x by a1,a5,SEQ_1:def 4;
    end;
    hence thesis by a1,GRFUNC_1:8;
  end;

theorem
  f c= g implies f-h c= g-h
  proof
    assume
a0: f c= g; then
a2: dom f c= dom g by GRFUNC_1:8;
a3: dom(f-h) = dom f /\ dom h by SEQ_1:def 4;
    dom(g-h) = dom g /\ dom h by SEQ_1:def 4; then
a1: dom(f-h) c= dom(g-h) by a2,a3,XBOOLE_1:27;
    now
      let x be set;
      assume
a5:   x in dom(f-h);
      then
a6:   x in dom f by a3,XBOOLE_0:def 3;
      thus (f-h).x = f.x - h.x by a5,SEQ_1:def 4
      .= g.x - h.x by a0,a6,GRFUNC_1:8
      .= (g-h).x by a1,a5,SEQ_1:def 4;
    end;
    hence thesis by a1,GRFUNC_1:8;
  end;
  
definition
  let f be real-yielding Function, r be real number, X be set;
  pred f,X is_absolutely_bounded_by r means :dab:
  for x being set st x in X /\ dom f holds abs(f.x) <= r;
end;

registration
  cluster summable constant convergent Real_Sequence;
  existence
  proof
    set f = NAT --> 0;
    reconsider f as Real_Sequence by FUNCOP_1:57;
    take f;
    for n being Nat holds f.n = 0 by FUNCOP_1:13;
    hence f is summable by RSSPACE:20;
    thus f is constant proof take r = 0; thus thesis by FUNCOP_1:13; end;
    hence thesis by SEQ_4:39;
  end;
end;

theorem LemE:
  for T1 being empty TopSpace, T2 being TopSpace
  for f being map of T1,T2 holds f is continuous
  proof
    let T1 be empty TopSpace, T2 being TopSpace;
    let f be map of T1,T2;
    let A be Subset of T2;
    f"A = {} T1;
    hence thesis by TOPS_1:22;
  end;

registration
  let T1 be TopSpace;
  let T2 be non empty TopSpace;
  cluster continuous map of T1,T2;
  existence
  proof consider f being map of T1,T2;
    per cases;
    suppose T1 is empty; then
      f is continuous by LemE;
      hence thesis;
    end;
    suppose T1 is non empty; then
      reconsider T1 as non empty TopSpace;
      consider f being continuous map of T1,T2;
      f = f;
      hence thesis;
    end;
  end;
end;

theorem
  for f,g being summable Real_Sequence
  st for n being Nat holds f.n <= g.n
  holds Sum f <= Sum g
  proof
    let f,g be summable Real_Sequence;
A0: Partial_Sums f is convergent & Partial_Sums g is convergent
    by SERIES_1:def 2;
A1: Sum f = lim Partial_Sums f & Sum g = lim Partial_Sums g by SERIES_1:def 3;
    assume for n being Nat holds f.n <= g.n; then
    for n being Nat holds (Partial_Sums f).n <= (Partial_Sums g).n
    by SERIES_1:17;
    hence Sum f <= Sum g by A0,A1,SEQ_2:32;
  end;

theorem LemA:
  for f being Real_Sequence st f is absolutely_summable
  holds abs Sum f <= Sum abs f
  proof
    let f be Real_Sequence;
    assume f is absolutely_summable; then
    abs f is summable & f is summable by SERIES_1:def 5,40; then
B1: Partial_Sums abs f is convergent & Partial_Sums f is convergent
    by SERIES_1:def 2; then
B2: abs Partial_Sums f is convergent by SEQ_4:26;
    defpred P[Nat] means (abs Partial_Sums f).$1 <= (Partial_Sums abs f).$1;
    (abs Partial_Sums f).0 = abs ((Partial_Sums f).0) by SEQ_1:16
    .= abs (f.0) by SERIES_1:def 1 .= (abs f).0 by SEQ_1:16; then
A1: P[0] by SERIES_1:def 1;
A2: now let n be Nat;
      assume P[n]; then
      |.(Partial_Sums f).n.| <= (Partial_Sums abs f).n by SEQ_1:16; then
      |.(Partial_Sums f).n+f.(n+1).| <= |.(Partial_Sums f).n.|+|.f.(n+1).| &
      |.(Partial_Sums f).n.|+|.f.(n+1).| <= (Partial_Sums abs f).n+|.f.(n+1).|
      by XREAL_1:8,COMPLEX1:142; then
      |.(Partial_Sums f).n+f.(n+1).| <= (Partial_Sums abs f).n+|.f.(n+1).|
      by XREAL_1:2; then
      |.(Partial_Sums f).(n+1).| <= (Partial_Sums abs f).n+|.f.(n+1).|
      by SERIES_1:def 1; then
      (abs Partial_Sums f).(n+1) <= (Partial_Sums abs f).n+|.f.(n+1).|
      by SEQ_1:16; then
      (abs Partial_Sums f).(n+1) <= (Partial_Sums abs f).n+(abs f).(n+1)
      by SEQ_1:16;
      hence P[n+1] by SERIES_1:def 1;
    end;
    for n being Nat holds P[n] from NAT_1:sch 1(A1,A2); then
    lim abs Partial_Sums f <= lim Partial_Sums abs f by B1,B2,SEQ_2:32; then
    abs lim Partial_Sums f <= lim Partial_Sums abs f by B1,SEQ_4:27; then
    abs lim Partial_Sums f <= Sum abs f by SERIES_1:def 3;
    hence abs Sum f <= Sum abs f by SERIES_1:def 3;
  end;

theorem Lem1:
  for f being Real_Sequence
  for a,r being positive (real number) st r < 1 &
  for n being natural number holds |.f.n-f.(n+1).| <= a*(r to_power n)
  holds f is convergent & 
  for n being natural number holds |.(lim f)-(f.n).| <= a*(r to_power n)/(1-r)
  proof
    let f be Real_Sequence;
    let a,r be positive (real number);
    assume
Z1: r < 1;
    assume
Z2: for n being natural number holds |.f.n-f.(n+1).| <= a*(r to_power n);
    deffunc S(Nat, Real) = f.($1+1)-f.$1;
    consider g being Function of NAT, REAL such that
A3: g.0 = f.0 & for n being Nat holds g.(n+1) = S(n,g.n) from RECDEF_1:sch 4;
    now
      let n be Nat;
      thus f.(n+1) = f.(n+1)-f.n+f.n
      .= f.n+g.(n+1) by A3;
    end; then
A4: f = Partial_Sums g by A3,SERIES_1:def 1;
B7: |.r.| = r by COMPLEX1:129; then
B3: r GeoSeq is summable by Z1,SERIES_1:28; then
A5: (a*r")(#)(r GeoSeq) is summable by SERIES_1:13;
A6: now
      let n be Nat;
      (abs g).n = abs (g.n) by SEQ_1:16;
      hence 0 <= (abs g).n by COMPLEX1:132;
    end;
    now set m = 1;
      let n be Nat; assume m <= n; then
      consider k being Nat such that
A7:   n = 1+k by NAT_1:28;
      g.n = S(k,g.k) by A3,A7; then
      (abs g).n = |.f.n-f.k.| by A7,SEQ_1:16
      .= |.f.k-f.n.| by COMPLEX1:146; then
A8:   (abs g).n <= a*(r to_power k) by Z2,A7;
      a*1*(r to_power k) = a*(r"*r)*(r to_power k) by XCMPLX_0:def 7
      .= (a*r")*(r*(r to_power k))
      .= (a*r")*((r to_power 1)*(r to_power k)) by POWER:30
      .= (a*r")*(r to_power n) by A7,POWER:32
      .= (a*r")*(r |^ n) by POWER:46
      .= (a*r")*((r GeoSeq).n) by PREPOWER:def 1;
      hence (abs g).n <= ((a*r")(#)(r GeoSeq)).n by A8,SEQ_1:13;
    end; then
    abs g is summable by A5,A6,SERIES_1:22; then
    g is absolutely_summable by SERIES_1:def 5; then
B2: g is summable by SERIES_1:40;
    hence f is convergent by A4,SERIES_1:def 2;
    let n be natural number;
    reconsider n' = n as Nat by ORDINAL2:def 21;
    lim f = Sum g by A4,SERIES_1:def 3; then
B8: lim f = f.n+Sum(g^\(n'+1)) by B2,A4,SERIES_1:18;
B5: (a*(r to_power n))(#)(r GeoSeq) is summable by B3,SERIES_1:13;
    now let k be Nat;
      (abs (g^\(n'+1))).k = |.(g^\(n'+1)).k.| by SEQ_1:16;
      hence 0 <= (abs (g^\(n'+1))).k by COMPLEX1:132;
      (abs (g^\(n'+1))).k = |.(g^\(n'+1)).k.| by SEQ_1:16
      .= |.g.(n'+1+k).| by SEQM_3:def 9
      .= |.f.(n'+k+1)-f.(n'+k).| by A3
      .= |.f.(n'+k)-f.(n'+k+1).| by UNIFORM1:13; then
      (abs (g^\(n'+1))).k <= a*(r to_power (n'+k)) by Z2; then
      (abs (g^\(n'+1))).k <= a*((r to_power n')*(r to_power k))
      by POWER:32; then
      (abs (g^\(n'+1))).k <= a*(r to_power n')*(r to_power k); then
      (abs (g^\(n'+1))).k <= a*(r to_power n')*(r|^k) by POWER:46; then
      (abs (g^\(n'+1))).k <= a*(r to_power n')*((r GeoSeq).k)
      by PREPOWER:def 1;
      hence (abs (g^\(n'+1))).k <= ((a*(r to_power n))(#)(r GeoSeq)).k
      by SEQ_1:13;
    end; then
    Sum abs (g^\(n'+1)) <= Sum ((a*(r to_power n))(#)(r GeoSeq)) &
    abs (g^\(n'+1)) is summable by B5,SERIES_1:24; then
B6: Sum abs (g^\(n'+1)) <= Sum ((a*(r to_power n))(#)(r GeoSeq)) &
    (g^\(n'+1)) is absolutely_summable by SERIES_1:def 5;
B9: Sum ((a*(r to_power n))(#)(r GeoSeq))
    = (a*(r to_power n))*Sum (r GeoSeq) by B3,SERIES_1:13
    .= (a*(r to_power n))*(1/(1-r)) by Z1,B7,SERIES_1:28
    .= (a*(r to_power n))/(1-r) by XCMPLX_1:100;
    |.(lim f)-(f.n).| <= Sum abs (g^\(n'+1)) by B8,B6,LemA;
    hence thesis by B6,B9,XREAL_1:2;
  end;

theorem
  for f being Real_Sequence
  for a,r being positive (real number) st r < 1 &
  for n being natural number holds |.f.n-f.(n+1).| <= a*(r to_power n)
  holds lim f >= f.0-a/(1-r) & lim f <= f.0+a/(1-r)
  proof
    let f be Real_Sequence;
    let a,r be positive (real number);
    assume
A0: r < 1;
    assume
A1: for n being natural number holds |.f.n-f.(n+1).| <= a*(r to_power n);
    r to_power 0 = 1 by POWER:29; then
    |.(lim f)-(f.0).| <= a*1/(1-r) by A0,A1,Lem1;
    hence thesis by LemC;
  end;

theorem Lem2:
  for X,Z being non empty set
  for F being Functional_Sequence of X,REAL st Z common_on_dom F
  for a,r being positive (real number) st r < 1 &
  for n being natural number holds
  (F.n)-(F.(n+1)), Z is_absolutely_bounded_by a*(r to_power n)
  holds F is_unif_conv_on Z &
  for n being natural number holds
  lim(F,Z)-(F.n), Z is_absolutely_bounded_by a*(r to_power n)/(1-r)
  proof
    let X,Z be non empty set;
    let F be Functional_Sequence of X,REAL such that
Z0: Z common_on_dom F;
    let a,r be positive (real number) such that
Z1: r < 1;
    assume
Z2: for n being natural number holds
    (F.n)-(F.(n+1)), Z is_absolutely_bounded_by a*(r to_power n);
    Z c= dom(F.0) & dom(F.0) c= X by Z0,SEQFUNC:def 10; then
    reconsider Z' = Z as non empty Subset of X by XBOOLE_1:1;
    deffunc ff(Element of Z') = lim(F # $1);
    consider f being Function of Z',REAL such that
A1: for x being Element of Z' holds f.x = ff(x) from FUNCT_2:sch 4;
    reconsider f as PartFunc of X,REAL by PARTFUN1:32;
    thus F is_unif_conv_on Z
    proof
      thus Z common_on_dom F by Z0;
      take f; thus Z = dom f by FUNCT_2:def 1;
      let p be Real such that
A7:   p > 0;
A8:   1-r > 0 by Z1,XREAL_1:52; then
      p*(1-r) > 0 by A7,XREAL_1:131; then
A9:   p*(1-r)/a > 0 by XREAL_1:141;
      1+log(r, p*(1-r)/a) is Real by XREAL_0:def 1; then
      consider k being Nat such that
A6:   k >= 1+log(r, p*(1-r)/a) by MESFUNC1:11;
      k > log(r, p*(1-r)/a) by A6,XREAL_1:41; then
      r to_power k < r to_power log(r, p*(1-r)/a) by Z1,POWER:45; then
      r to_power k < p*(1-r)/a & a <> 0 by Z1,A9,POWER:def 3; then
      a*(r to_power k) < a*(p*(1-r)/a) & a*(p*(1-r)/a) = p*(1-r)*(a/a) &
      a/a = 1 by XREAL_1:70,XCMPLX_1:60,76; then
      a*(r to_power k)/(1-r) < (p*(1-r))/(1-r) by A8,XREAL_1:76; then
A3:   a*(r to_power k)/(1-r) < p by A8,XCMPLX_1:90;
      take k;
      let n be Nat, x be Element of X;
      assume
A2:   n>=k & x in Z; then
      n = k or n > k by XREAL_1:1; then
      r to_power n <= r to_power k by Z1,POWER:45; then
      a*(r to_power n) <= a*(r to_power k) & 1-r > 1-1
      by Z1,XREAL_1:66,XREAL_1:12; then
A5:   a*(r to_power n)/(1-r) <= a*(r to_power k)/(1-r) by XREAL_1:74;
      now
        let n be natural number;
Z3:     n is Nat & n+1 is Nat by ORDINAL2:def 21; then
        Z c= dom (F.n) & Z c= dom (F.(n+1)) by Z0,SEQFUNC:def 10; then
        x in dom (F.n) /\ dom (F.(n+1)) by A2,XBOOLE_0:def 3; then
        x in dom ((F.n)-(F.(n+1))) by SEQ_1:def 4; then
        ((F.n)-(F.(n+1))).x = (F.n).x-(F.(n+1)).x &
        (F.n)-(F.(n+1)), Z is_absolutely_bounded_by a*(r to_power n) &
        x in Z /\ dom ((F.n)-(F.(n+1)))
        by A2,Z2,XBOOLE_0:def 3,SEQ_1:def 4; then
        |.(F.n).x-(F.(n+1)).x.| <= a*(r to_power n) &
        (F#x).n = (F.n).x & (F#x).(n+1) = F.(n+1).x
        by Z3,dab,SEQFUNC:def 11;
        hence |.(F#x).n-(F#x).(n+1).| <= a*(r to_power n);
      end; then
      |.(lim (F#x)) - ((F#x).n).| <= a*(r to_power n)/(1-r) by Z1,Lem1; then
A4:   |.(lim (F#x)) - ((F#x).n).| <= a*(r to_power k)/(1-r) by A5,XREAL_1:2;
      (F.n).x = (F#x).n & f.x = lim (F#x) &
      |.(F.n).x - f.x.| = |.(f.x) - (F.n).x.|
      by A1,A2,SEQFUNC:def 11,COMPLEX1:146;
      hence |.(F.n).x - f.x.| < p by A3,A4,XREAL_1:2;
    end; then
B1: F is_point_conv_on Z by SEQFUNC:23;
    let n' be natural number, z being set;
    reconsider n = n' as Nat by ORDINAL2:def 21;
    assume z in Z /\ dom (lim(F,Z)-(F.n')); then
B3: z in Z' & z in dom (lim(F,Z)-(F.n')) by XBOOLE_0:def 3; then
    reconsider x = z as Element of X;
B2: Z = dom lim(F,Z) by B1,SEQFUNC:def 14;
    now
      let n be natural number;
Z3:   n is Nat & n+1 is Nat by ORDINAL2:def 21; then
      Z c= dom (F.n) & Z c= dom (F.(n+1)) by Z0,SEQFUNC:def 10; then
      z in dom (F.n) /\ dom (F.(n+1)) by B3,XBOOLE_0:def 3; then
      x in dom ((F.n)-(F.(n+1))) &
      (F#x).n = (F.n).x & (F#x).(n+1) = F.(n+1).x
      by Z3,SEQFUNC:def 11,SEQ_1:def 4; then
      x in Z /\ dom ((F.n)-(F.(n+1))) &
      (F.n)-(F.(n+1)), Z is_absolutely_bounded_by a*(r to_power n) &
      ((F.n)-(F.(n+1))).x = (F#x).n-(F#x).(n+1)
      by B3,Z2,XBOOLE_0:def 3,SEQ_1:def 4;
      hence |.(F#x).n-(F#x).(n+1).| <= a*(r to_power n) by dab;
    end; then
    |.(lim (F#x)) - ((F#x).n).| <= a*(r to_power n)/(1-r) by Z1,Lem1; then
    |.lim(F,Z).x-(F#x).n.| <= a*(r to_power n)/(1-r)
    by B1,B2,B3,SEQFUNC:def 14; then
    |.lim(F,Z).x-(F.n).x.| <= a*(r to_power n)/(1-r) by SEQFUNC:def 11;
    hence thesis by B3,SEQ_1:def 4;
  end;

theorem Lem2a:
  for X,Z being non empty set
  for F being Functional_Sequence of X,REAL
  st Z common_on_dom F
  for a,r being positive (real number) st r < 1 &
  for n being natural number holds
  (F.n)-(F.(n+1)), Z is_absolutely_bounded_by a*(r to_power n)
  for z being Element of Z holds
  lim(F,Z).z >= F.0 .z-a/(1-r) & lim(F,Z).z <= F.0 .z+a/(1-r)
  proof
    let X,Z be non empty set;
    let F be Functional_Sequence of X,REAL;
    assume
A0: Z common_on_dom F;
    let a,r be positive (real number);
    assume
A1: r < 1;
    assume
A2: for n being natural number
    holds (F.n)-(F.(n+1)), Z is_absolutely_bounded_by a*(r to_power n);
    let z be Element of Z;
    F is_unif_conv_on Z by A0,A1,A2,Lem2; then
    F is_point_conv_on Z by SEQFUNC:23; then
    z in Z & Z c= dom (F.0) & dom lim(F,Z) = Z
    by A0,SEQFUNC:def 10,def 14; then
    z in dom (lim(F,Z)) /\ dom (F.0) by XBOOLE_0:def 3; then
A3: z in dom (lim(F,Z)-(F.0)) &
    r to_power 0 = 1 by POWER:29,SEQ_1:def 4; then
    lim(F,Z)-(F.0), Z is_absolutely_bounded_by a*1/(1-r) &
    z in Z /\ dom (lim(F,Z)-(F.0)) by A0,A1,A2,Lem2,XBOOLE_0:def 3; then
    |.(lim(F,Z)-(F.0)).z.| <= a*1/(1-r) by dab; then
    |.lim(F,Z).z-(F.0).z.| <= a*1/(1-r) by A3,SEQ_1:def 4;
    hence thesis by LemC;
  end;

theorem Lem3:
  for X,Z being non empty set
  for F being Functional_Sequence of X,REAL
  st Z common_on_dom F
  for a,r being positive (real number)
  for f being Function of Z, REAL st r < 1 &
  for n being natural number holds
  (F.n)-f, Z is_absolutely_bounded_by a*(r to_power n)
  holds F is_point_conv_on Z & lim(F,Z) = f
  proof
    let X,Z be non empty set;
    let F be Functional_Sequence of X,REAL;
    assume
A1: Z common_on_dom F;
    let a,r be positive (real number);
    let f be Function of Z, REAL;
    assume
A2: r < 1;
    assume
A3: for n being natural number
    holds (F.n)-f, Z is_absolutely_bounded_by a*(r to_power n);
A5: dom f = Z by FUNCT_2:def 1;
    Z c= dom (F.0) & dom (F.0) c= X by A1,SEQFUNC:def 10; then
    Z c= X by XBOOLE_1:1; then
    reconsider g = f as PartFunc of X,REAL by A5,RELSET_1:13;
N:  now
      let x be Element of X such that
A6:   x in Z;
      let p be Real such that
A7:   p > 0;
A8:   1-r > 0 by A2,XREAL_1:52; then
      p*(1-r) > 0 by A7,XREAL_1:131; then
A9:   p*(1-r)/a > 0 by XREAL_1:141;
      1+log(r, p*(1-r)/a) is Real by XREAL_0:def 1; then
      consider k being Nat such that
B6:   k >= 1+log(r, p*(1-r)/a) by MESFUNC1:11;
      k > log(r, p*(1-r)/a) by B6,XREAL_1:41; then
      r to_power k < r to_power log(r, p*(1-r)/a) by A2,POWER:45; then
      r to_power k < p*(1-r)/a & a <> 0 by A2,A9,POWER:def 3; then
      a*(r to_power k) < a*(p*(1-r)/a) & a*(p*(1-r)/a) = p*(1-r)*(a/a) &
      a/a = 1 by XREAL_1:70,XCMPLX_1:60,76; then
      a*(r to_power k)/(1-r) < (p*(1-r))/(1-r) by A8,XREAL_1:76; then
B3:   a*(r to_power k)/(1-r) < p by A8,XCMPLX_1:90;
      take k;
      let n be Nat; assume n >= k; then
      n = k or n > k by XREAL_1:1; then
      r to_power n <= r to_power k by A2,POWER:45; then
      a*(r to_power n) <= a*(r to_power k) & 1-r > 1-1
      by A2,XREAL_1:66,XREAL_1:12; then
C5:   a*(r to_power n)/(1-r) <= a*(r to_power k)/(1-r) by XREAL_1:74;
      Z c= dom (F.n) by A1,SEQFUNC:def 10; then
      x in dom (F.n) /\ dom f by A5,A6,XBOOLE_0:def 3; then
      x in dom ((F.n)-f) by SEQ_1:def 4; then
      x in Z /\ dom ((F.n)-f) & ((F.n)-f).x = (F.n).x-f.x &
      (F.n)-f, Z is_absolutely_bounded_by a*(r to_power n)
      by A3,A6,SEQ_1:def 4,XBOOLE_0:def 3; then
C1:   |.(F.n).x-f.x.| <= a*(r to_power n) by dab;
      r to_power n >= 0 by POWER:39; then
      1-r <= 1 & a*(r to_power n) >= a*0 by XREAL_1:45,66; then
      a*(r to_power n)*(1-r) <= a*(r to_power n)*1 & 0 < 1 by XREAL_1:66; then
      a*(r to_power n)/1 <= a*(r to_power n)/(1-r) by A8,XREAL_1:104; then
      a*(r to_power n) <= a*(r to_power k)/(1-r) by C5,XREAL_1:2; then
      abs((F.n).x - g.x) <= a*(r to_power k)/(1-r) by C1,XREAL_1:2;
      hence abs((F.n).x - g.x) < p by B3,XREAL_1:2;
    end;
    thus
A4: F is_point_conv_on Z
    proof
      thus Z common_on_dom F by A1;
      take g; thus Z = dom g by FUNCT_2:def 1;
      thus thesis by N;
    end;
    now
      let x be Element of X;
      assume
B1:   x in dom g; then
B2:   F#x is convergent by A4,A5,SEQFUNC:21;
      for p being real number st 0 < p ex n being Nat st
      for m being Nat st n <= m holds abs((F#x).m-g.x) < p
      proof
        let p be real number;
        reconsider p' = p as Real by XREAL_0:def 1;
        assume 0 < p; then
        consider n being Nat such that
A6:     for m being Nat st n <= m holds abs((F.m).x-g.x) < p' by A5,B1,N;
        take n; let m be Nat;
        (F.m).x = (F#x).m by SEQFUNC:def 11;
        hence n <= m implies abs((F#x).m-g.x) < p by A6;
      end;
      hence g.x = lim(F#x) by B2,SEQ_2:def 7;
    end;
    hence lim(F,Z) = f by A4,A5,SEQFUNC:def 14;
  end;

:: Topology
  
registration
  let S, T be TopStruct, A be empty Subset of S, f be map of S,T;
  cluster f|A -> empty;
  coherence
  proof
    per cases;
    suppose T is non empty;
      then f|A is map of S|A,T by JGRAPH_3:12;
      then dom(f|A) = the carrier of S|A by FUNCT_2:def 1;
      hence thesis by RELAT_1:64;
    end;
    suppose T is empty;
      then reconsider T as empty TopStruct;
      rng f c= the carrier of T;
      then rng(f|A) = {} by XBOOLE_1:3;
      hence thesis by RELAT_1:64;
    end;
  end;
end;

registration
  let T be TopSpace, A be closed Subset of T;
  cluster T|A -> closed;
  coherence
  proof
    let X be Subset of T;
    thus thesis by JORDAN1:1;
  end;
end;

theorem Th4:
  for X, Y being non empty TopSpace, X1, X2 being non empty SubSpace of X
  for f1 being map of X1,Y, f2 being map of X2,Y st
  X1 misses X2 or f1|(X1 meet X2) = f2|(X1 meet X2)
  for x being Point of X holds
  (x in the carrier of X1 implies (f1 union f2).x = f1.x) &
  (x in the carrier of X2 implies (f1 union f2).x = f2.x)
  proof
    let X, Y be non empty TopSpace, X1, X2 be non empty SubSpace of X;
    let f1 be map of X1,Y, f2 be map of X2,Y;
    assume
A1: X1 misses X2 or f1|(X1 meet X2) = f2|(X1 meet X2);
    set F = f1 union f2;
    let x be Point of X;
A2: X1 is SubSpace of X1 union X2 by TSEP_1:22;
A3: X2 is SubSpace of X1 union X2 by TSEP_1:22;
    hereby
      assume x in the carrier of X1;
      hence F.x = (F|the carrier of X1).x by FUNCT_1:72
      .= (F|X1).x by A2,TMAP_1:def 4
      .= f1.x by A1,TMAP_1:def 12;
    end;
    assume x in the carrier of X2;
    hence F.x = (F|the carrier of X2).x by FUNCT_1:72
    .= (F|X2).x by A3,TMAP_1:def 4
    .= f2.x by A1,TMAP_1:def 12;
  end;
  
theorem Th5:
  for X, Y being non empty TopSpace, X1, X2 being non empty SubSpace of X
  for f1 being map of X1,Y, f2 being map of X2,Y st
  X1 misses X2 or f1|(X1 meet X2) = f2|(X1 meet X2) holds
  rng (f1 union f2) c= rng f1 \/ rng f2
  proof
    let X, Y be non empty TopSpace, X1, X2 be non empty SubSpace of X;
    let f1 be map of X1,Y, f2 be map of X2,Y;
    assume
A1: X1 misses X2 or f1|(X1 meet X2) = f2|(X1 meet X2);
    set F = f1 union f2;
    thus rng F c= rng f1 \/ rng f2
    proof
      let y be set;
      assume y in rng F;
      then consider x being set such that
A2:   x in dom F and
A3:   F.x = y by FUNCT_1:def 5;
A4:   the carrier of X1 = dom f1 & the carrier of X2 = dom f2 by FUNCT_2:def 1;
A5:   the carrier of X1 union X2 = (the carrier of X1) \/ the carrier of X2
      by TSEP_1:def 2;
A6:   x is Point of X by A2,TOPMETR:2;
      per cases by A5,A2,XBOOLE_0:def 2;
      suppose
A7:     x in the carrier of X1;
        then
A8:     f1.x in rng f1 by A4,FUNCT_1:def 5;
        F.x = f1.x by A6,A1,A7,Th4;
        hence thesis by A3,A8,XBOOLE_0:def 2;
      end;
      suppose
A9:     x in the carrier of X2;
        then
A10:    f2.x in rng f2 by A4,FUNCT_1:def 5;
        F.x = f2.x by A6,A1,A9,Th4;
        hence thesis by A3,A10,XBOOLE_0:def 2;
      end;
    end;
  end;
  
theorem Th6:
  for X, Y being non empty TopSpace, X1, X2 being non empty SubSpace of X
  for f1 being map of X1,Y, f2 being map of X2,Y st
  X1 misses X2 or f1|(X1 meet X2) = f2|(X1 meet X2) holds
  (for A being Subset of X1 holds (f1 union f2).:A = f1.:A) &
  (for A being Subset of X2 holds (f1 union f2).:A = f2.:A)
  proof
    let X, Y be non empty TopSpace, X1, X2 be non empty SubSpace of X;
    let f1 be map of X1,Y, f2 be map of X2,Y;
    assume
A1: X1 misses X2 or f1|(X1 meet X2) = f2|(X1 meet X2);
    set F = f1 union f2;
A2: the carrier of X1 union X2 = (the carrier of X1) \/ the carrier of X2
    by TSEP_1:def 2;
    hereby
      let A be Subset of X1;
      thus (f1 union f2).:A = f1.:A
      proof
        hereby
          let y be set;
          assume y in (f1 union f2).:A;
          then consider x being Element of X1 union X2 such that
A3:       x in A and
A4:       y = F.x by FUNCT_2:116;
          x is Point of X by TOPMETR:2;
          then F.x = f1.x by A1,A3,Th4;
          hence y in f1.:A by A3,A4,FUNCT_2:43;
        end;
        let y be set;
        assume y in f1.:A;
        then consider x being Element of X1 such that
A5:     x in A and
A6:     y = f1.x by FUNCT_2:116;
        x is Point of X by TOPMETR:2;
        then
A7:     F.x = f1.x by A1,Th4;
        x in the carrier of X1 union X2 by A2,XBOOLE_0:def 2;
        hence y in F.:A by A5,A6,A7,FUNCT_2:43;
      end;
    end;
    let A be Subset of X2;
    hereby
      let y be set;
      assume y in (f1 union f2).:A;
      then consider x being Element of X1 union X2 such that
A8:   x in A and
A9:   y = F.x by FUNCT_2:116;
      x is Point of X by TOPMETR:2;
      then F.x = f2.x by A1,A8,Th4;
      hence y in f2.:A by A8,A9,FUNCT_2:43;
    end;
    let y be set;
    assume y in f2.:A;
    then consider x being Element of X2 such that
A10: x in A and
A11: y = f2.x by FUNCT_2:116;
     x is Point of X by TOPMETR:2;
     then
A12: F.x = f2.x by A1,Th4;
     x in the carrier of X1 union X2 by A2,XBOOLE_0:def 2;
     hence y in F.:A by A10,A11,A12,FUNCT_2:43;
   end;
   
theorem Tzaw1:
  f c= g & g,X is_absolutely_bounded_by r implies
  f,X is_absolutely_bounded_by r
  proof
    assume that
a1: f c= g and
a2: g,X is_absolutely_bounded_by r;
    let x be set;
    assume
a5: x in X /\ dom f;
    then
a4: x in dom f by XBOOLE_0:def 3;
a3: dom f c= dom g by a1,GRFUNC_1:8;
    x in X by a5,XBOOLE_0:def 3;
    then x in X /\ dom g by a4,a3,XBOOLE_0:def 3;
    then abs(g.x) <= r by a2,dab;
    hence abs(f.x) <= r by a1,a4,GRFUNC_1:8;
  end;

theorem Tzaw2:
  (X c= dom f or dom g c= dom f) & f|X = g|X & f,X is_absolutely_bounded_by r
  implies g,X is_absolutely_bounded_by r
  proof
    assume that
a1: X c= dom f or dom g c= dom f and
a4: f|X = g|X and
a3: f,X is_absolutely_bounded_by r;
    let x be set;
    assume x in X /\ dom g;
    then
a5: x in X & x in dom g by XBOOLE_0:def 3;
    then
a6: x in X /\ dom f by a1,XBOOLE_0:def 3;
    f.x = (f|X).x by a5,FUNCT_1:72
    .= g.x by a4,a5,FUNCT_1:72;
    hence thesis by a3,a6,dab;
  end;

 reserve T for non empty TopSpace,
         A for closed Subset of T;
  
theorem Th7:
  r > 0 & T is being_T4 implies
  for f being continuous map of T|A, R^1 st f,A is_absolutely_bounded_by r
  ex g being continuous map of T, R^1 st g,dom g is_absolutely_bounded_by r/3 &
  f-g,A is_absolutely_bounded_by 2*r/3
  proof
    assume that
A1: r > 0 and
A2: T is being_T4;
    let f be continuous map of T|A, R^1 such that
A3: for x being set st x in A /\ dom f holds abs(f.x) <= r;
    set A1 = left_closed_halfline(-r/3);
    set A2 = right_closed_halfline(r/3);
    set C1 = R^1(left_closed_halfline(-r/3));
    set C2 = R^1(right_closed_halfline(r/3));
A5: A1 = C1 & A2 = C2 by TOPREALB:def 3;
A6: --r/3 > 0 by A1,XREAL_1:141;
    then
A7: -r/3 < r/3 by XREAL_1:60;
    then A1 misses A2 by Th1;
    then
A8: f"A1 misses f"A2 by JCT_MISC:4;
    f"C1 is closed & f"C2 is closed by PRE_TOPC:def 12;
    then reconsider D1 = f"C1, D2 = f"C2 as closed Subset of T
    by TSEP_1:12,PRE_TOPC:39;
    consider F being map of T, R^1 such that
A9: F is continuous and
A10: for x being Point of T holds
     0 <= F.x & F.x <= 1 & (x in D1 implies F.x = 0) &
     (x in D2 implies F.x = 1) by A2,A5,A8,URYSOHN3:23;
     reconsider r1 = r as Real by XREAL_0:def 1;
     set e = 2*r1/3;
     0 < 2*r by A1,XREAL_1:131;
     then
A11: e > 0 by XREAL_1:141;
     then consider h being map of
     Closed-Interval-TSpace(0,1),Closed-Interval-TSpace(e*0+-r1/3,e*1+-r1/3)
     such that
A12: h is_homeomorphism and
A13: for w being Real st w in [. 0,1 .] holds h.w = e*w+-r1/3 by JGRAPH_5:39;
A14: the carrier of Closed-Interval-TSpace(-r/3,r/3) = [.-r/3,r/3.]
     by A7,TOPMETR:25;
A15: the carrier of Closed-Interval-TSpace(0,1) = [. 0,1 .] by TOPMETR:25;
     set g1 = h*F;
A16: dom F = the carrier of T by FUNCT_2:def 1;
A17: dom h = the carrier of Closed-Interval-TSpace(0,1) by FUNCT_2:def 1;
A18: rng F c= [. 0,1 .]
     proof
       let y be set;
       assume y in rng F;
       then consider x being set such that
A19:   x in dom F and
A20:   F.x = y by FUNCT_1:def 5;
       0 <= F.x & F.x <= 1 by A19,A10;
       hence thesis by A20,RCOMP_1:48;
     end;
     then
A21: dom g1 = the carrier of T by A16,A17,A15,RELAT_1:46;
A22: rng g1 c= the carrier of Closed-Interval-TSpace(-r/3,r/3);
     then reconsider g1 as map of T, Closed-Interval-TSpace(-r/3,r/3)
     by A21,FUNCT_2:4;
A23: rng g1 c= the carrier of Closed-Interval-TSpace(-r/3,r/3);
A44: h is continuous by A12,TOPS_2:def 5;
     reconsider g = g1 as map of T, R^1 by TOPREALA:28;
     reconsider F1 = F as map of T, Closed-Interval-TSpace(0,1)
     by A15,A18,FUNCT_2:8;
     F1 is continuous by A9,TOPMETR:8;
     then h*F1 is continuous by A44,TOPS_2:58;
     then reconsider g as continuous map of T, R^1 by TOPMETR:7;
     take g;
     thus g,dom g is_absolutely_bounded_by r/3
     proof
       let x be set;
       assume x in dom g /\ dom g;
       then g.x in rng g by FUNCT_1:def 5;
       then -r/3 <= g.x & g.x <= r/3 by A14,A23,RCOMP_1:48;
       hence abs(g.x) <= r/3 by ABSVALUE:12;
     end;
     thus f-g,A is_absolutely_bounded_by 2*r/3
     proof
       let x be set such that
A24:   x in A /\ dom(f-g);
A25:   dom(f-g) = dom f /\ dom g by SEQ_1:def 4;
A26:   x in dom(f-g) by A24,XBOOLE_0:def 3;
       then
A27:   x in dom f by A25,XBOOLE_0:def 3;
A28:   x in A by A24,XBOOLE_0:def 3;
       then x in A /\ dom f by A27,XBOOLE_0:def 3;
       then
A29:   abs(f.x) <= r by A3;
       then
A30:   -r <= f.x by ABSVALUE:12;
A31:   0 in [. 0,1 .] by RCOMP_1:48;
A32:   1 in [. 0,1 .] by RCOMP_1:48;
A33:   (f-g).x = f.x - g.x by A26,SEQ_1:def 4;
       per cases;
       suppose
A34:     f.x <= -r/3;
         then f.x in A1 by BORSUK_5:16;
         then x in f"A1 by A27,FUNCT_1:def 13;
         then F.x = 0 by A5,A10;
         then
A35:     g.x = h.0 by A21,A28,FUNCT_1:22
         .= -r1/3 by A13,A31;
         -r = -2*r/3 - r/3;
         then
A36:     -2*r/3 <= f.x + r/3 by A30,XREAL_1:22;
         f.x + r/3 <= -r/3 + r/3 by A34,XREAL_1:8;
         hence abs((f-g).x) <= 2*r/3 by A33,A36,A35,A11,ABSVALUE:12;
       end;
       suppose
A37:     r/3 <= f.x;
         then f.x in A2 by BORSUK_5:15;
         then x in f"A2 by A27,FUNCT_1:def 13;
         then F.x = 1 by A5,A10;
         then
A38:     g.x = h.1 by A21,A28,FUNCT_1:22
         .= r1/3 by A13,A32;
         -2*r/3 + r/3 <= f.x by A6,XREAL_1:60,A37;
         then
A39:     -2*r/3 <= f.x - r/3 by XREAL_1:21;
         f.x <= r/3 + 2*r/3 by A29,ABSVALUE:12;
         then f.x - r/3 <= 2*r/3 by XREAL_1:22;
         hence abs((f-g).x) <= 2*r/3 by A33,A39,A38,ABSVALUE:12;
       end;
       suppose
A40:     -r/3 < f.x & f.x < r/3;
A41:     -2*r/3 = -r/3-r/3;
         g.x in rng g by A28,A21,FUNCT_1:def 5;
         then
A42:     -r/3 <= g.x & g.x <= r/3 by A22,A14,RCOMP_1:48;
         then
A43:     -2*r/3 <= f.x - g.x by A40,A41,XREAL_1:15;
         f.x - g.x <= r/3 - -r/3 by A40,A42,XREAL_1:16;
         hence abs((f-g).x) <= 2*r/3 by A33,A43,ABSVALUE:12;
       end;
     end;
   end;
   
:: Urysohn Lemma, simple case
theorem Th8:
  (for A, B being non empty closed Subset of T st A misses B
  ex f being continuous map of T, R^1 st f.:A = {0} & f.:B = {1})
  implies T is_T4
  proof
    assume
A1: for A, B being non empty closed Subset of T st A misses B
    ex f being continuous map of T, R^1 st f.:A = {0} & f.:B = {1};
    let W, V be Subset of T such that
A2: W <> {} & V <> {} and
A3: W is closed & V is closed and
A4: W misses V;
    consider f being continuous map of T, R^1 such that
A5: f.:W = {0} and
A6: f.:V = {1} by A1,A2,A3,A4;
    set P = f"R^1(left_open_halfline(1/2));
    set Q = f"R^1(right_open_halfline(1/2));
A7: R^1(left_open_halfline(1/2)) = left_open_halfline(1/2) by TOPREALB:def 3;
A8: R^1(right_open_halfline(1/2)) = right_open_halfline(1/2) by TOPREALB:def 3;
    take P, Q;
    thus P is open & Q is open by TOPS_2:55;
A9: dom f = the carrier of T by FUNCT_2:def 1;
    thus W c= P
    proof
      let a be set;
      assume
A10:  a in W;
      then f.a in f.:W by FUNCT_2:43;
      then
A11:  f.a = 0 by A5,TARSKI:def 1;
      0 in left_open_halfline(1/2) by BORSUK_5:17;
      hence thesis by A7,A11,A10,A9,FUNCT_1:def 13;
    end;
    thus V c= Q
    proof
      let a be set;
      assume
A12:  a in V;
      then f.a in f.:V by FUNCT_2:43;
      then
A13:  f.a = 1 by A6,TARSKI:def 1;
      1 in right_open_halfline(1/2) by BORSUK_5:14;
      hence thesis by A8,A13,A12,A9,FUNCT_1:def 13;
    end;
    left_open_halfline(1/2) misses right_open_halfline(1/2) by Th2;
    hence P misses Q by A7,A8,JCT_MISC:4;
  end;
    
theorem lemequ:
  for f being map of T,R^1, x being Point of T holds
  f is_continuous_at x iff
  for e being real number st e>0 ex H being Subset of T st
  H is open & x in H & for y being Point of T st y in H holds abs(f.y-f.x)<e
  proof
    let f be map of T,R^1, x be Point of T;
r2: the carrier of RealSpace = REAL & the carrier of R^1 = REAL
    by TOPMETR:def 7,TOPMETR:16,METRIC_1:def 14;
    thus f is_continuous_at x implies
    for e being real number st e>0 ex H being Subset of T st
    H is open & x in H & for y being Point of T st y in H holds abs(f.y-f.x)<e
    proof
      assume f: f is_continuous_at x;
      let e be real number such that
      e: e>0;
      reconsider fx=f.x as Point of RealSpace by r2;
      reconsider G=Ball(fx,e) as Subset of R^1 by r2;
      go: G is open by TOPMETR:21,TOPMETR:def 7;
      fx in G by GOBOARD6:4,e; then
      consider H being Subset of T such that
      h: H is open & x in H & f.:H c= G by go,f,TMAP_1:48;
      take H;
      thus H is open & x in H by h;
      let y be Point of T such that
      y: y in H;
      dom f=the carrier of T by FUNCT_2:def 1; then
Fy:   f.y in f.:H by FUNCT_1:def 12,y; then
      f.y in G by h; then
      reconsider fy=f.y as Point of RealSpace;
      dist(fy,fx) < e by Fy,h,METRIC_1:12;
      hence abs(f.y-f.x)<e by TOPMETR:15;
    end;
    assume i2: for e being real number st e>0 ex H being Subset of T st
    H is open & x in H & for y being Point of T st y in H holds abs(f.y-f.x)<e;
    now
      let G be Subset of R^1 such that
g:    G is open & f.x in G;
      reconsider fx=f.x as Point of RealSpace by r2;
      consider r being real number such that
r:    r>0 & Ball(fx,r) c= G by TOPMETR:def 7,g,TOPMETR:22;
      consider H being Subset of T such that
h:    H is open & x in H &
      for y being Point of T st y in H holds abs(f.y-f.x)<r by i2,r;
      take H;
      thus H is open & x in H by h;
      thus f.:H c= G
      proof
        let a be set;
        assume a in f.:H; then
        consider y being set such that
        y: y in dom f & y in H & a=f.y by FUNCT_1:def 12;
        reconsider y as Point of T by y;
        abs(f.y-f.x)<r by h,y; then
        abs(-(f.y-f.x))< r by COMPLEX1:138; then
        ax: abs(f.x-f.y)<r;
        reconsider fy=f.y as Point of RealSpace by r2;
        dist(fx,fy)<r by ax,TOPMETR:15; then
        fy in Ball(fx,r) by METRIC_1:12;
        hence a in G by y,r;
      end;
    end;
    hence f is_continuous_at x by TMAP_1:48;
  end;
 
theorem lemuni:
  for F being Functional_Sequence of the carrier of T, REAL
  st F is_unif_conv_on the carrier of T &
  for i being Nat holds F.i is continuous map of T, R^1
  holds lim(F, the carrier of T) is continuous map of T, R^1
  proof
    let F be Functional_Sequence of the carrier of T, REAL such that
    F: F is_unif_conv_on the carrier of T &
    for i being Nat holds F.i is continuous map of T, R^1;
    the carrier of T common_on_dom F & F is_point_conv_on the carrier of T &
    for r being Real st 0<r
    ex k being Nat st
    for n being Nat for x being Point of T st n>=k & x in the carrier of T
    holds abs((F.n).x-(lim(F,the carrier of T)).x)<r by F,SEQFUNC:44; then
d:  dom lim(F, the carrier of T) = the carrier of T &
    for x being Element of the carrier of T
    st x in dom lim(F, the carrier of T)
    holds (lim(F, the carrier of T)).x = lim(F#x) by SEQFUNC:def 14;
    reconsider l=lim(F, the carrier of T) as map of T,R^1
    by d,FUNCT_2:def 1,TOPMETR:24;
    now
      let p be Point of T;
      now
        let e be real number such that
        e: e>0;
        reconsider e3=e/3 as Real by XREAL_0:def 1;
        e3: e3>0 by e,XREAL_1:141;
        consider k being Nat such that
k:      for n being Nat for x being Point of T st n>=k & x in the carrier of T
        holds abs((F.n).x-(lim(F,the carrier of T)).x)<e3 by F,SEQFUNC:44,e3;
        reconsider Fk=F.k as continuous map of T, R^1 by F;
        Fk is_continuous_at p by TMAP_1:49; then
        consider H being Subset of T such that
H:      H is open & p in H & for y being Point of T st y in H
        holds abs(Fk.y-Fk.p)<e3 by lemequ,e3; 
        take H;
        thus H is open & p in H by H;
        let y be Point of T such that
        y: y in H;
i2:     abs(Fk.y-Fk.p)<e3 by H,y;
        abs(Fk.p-l.p)<e3 & abs(Fk.y-l.y)<e3 by k; then
i13:    abs(Fk.p-l.p)<e3 & abs(-(Fk.y-l.y))<e3 by COMPLEX1:138;
i14:    abs((Fk.p-l.p)+(-Fk.y+l.y)) <= abs(Fk.p-l.p)+abs(-(Fk.y-l.y))
        by COMPLEX1:142;
        abs(Fk.p-l.p)+abs(-(Fk.y-l.y)) < e3+e3 by i13,XREAL_1:10; then
ao:     abs((Fk.p-l.p)+(-Fk.y+l.y)) < 2*e3 by i14,XREAL_1:2;
ax:     abs(Fk.y-Fk.p+((Fk.p-l.p)+(-Fk.y+l.y))) <=
        abs(Fk.y-Fk.p)+abs((Fk.p-l.p)+(-Fk.y+l.y)) by COMPLEX1:142;
        abs(Fk.y-Fk.p)+abs((Fk.p-l.p)+(-Fk.y+l.y)) < e3+2*e3
        by i2,ao,XREAL_1:10;
        hence abs(l.y-l.p)<e by ax,XREAL_1:2; 
      end;
      hence l is_continuous_at p by lemequ;
    end;
    hence lim(F, the carrier of T) is continuous map of T, R^1 by TMAP_1:49;
  end;

theorem LemH:
  for T being non empty TopSpace
  for f being map of T, R^1
  for r being positive (real number) holds
  f, the carrier of T is_absolutely_bounded_by r iff
  f is map of T, Closed-Interval-TSpace(-r,r)
  proof
    let T be non empty TopSpace;
    let f be map of T, R^1;
    let r be positive (real number);
    d: dom f = the carrier of T by FUNCT_2:def 1;
    thus  f, the carrier of T is_absolutely_bounded_by r implies
    f is map of T, Closed-Interval-TSpace(-r,r)
    proof
      assume f: f, the carrier of T is_absolutely_bounded_by r;
      now
        let x be set;
        assume x in the carrier of T; then
        x in (the carrier of T) /\ dom f by d; then
        abs(f.x)<=r by f,dab; then
        2*abs(f.x)<=2*r by XREAL_1:66; then
        abs(2)*abs(f.x)<=2*r by ABSVALUE:def 1; then
        abs(-2)*abs(f.x)<=2*r by COMPLEX1:138; then
        abs((-2)*f.x)<=r--r by COMPLEX1:151; then
        abs(-r+r-2*f.x)<=r--r; then
        f.x in [.-r,r.] by RCOMP_1:9;
        hence f.x in the carrier of Closed-Interval-TSpace(-r,r) by TOPMETR:25;
      end;
      hence f is map of T, Closed-Interval-TSpace(-r,r) by d,FUNCT_2:5;
    end;
    assume f: f is map of T, Closed-Interval-TSpace(-r,r);
    let x be set;
    assume x in (the carrier of T) /\ dom f; then
    f.x in the carrier of Closed-Interval-TSpace(-r,r) by f,d,FUNCT_2:7; then
    f.x in [.-r,r.] by TOPMETR:25; then
    abs(-r+r-2*f.x)<=r--r by RCOMP_1:9; then
    abs((-2)*f.x)<=r--r; then
    abs(-2)*abs(f.x)<=2*r by COMPLEX1:151; then
    abs(2)*abs(f.x)<=2*r by COMPLEX1:138; then
    2*abs(f.x)<=2*r by ABSVALUE:def 1; then
    (2*abs(f.x))/2<=(2*r)/2 by XREAL_1:74;
    hence abs(f.x) <= r;
  end;

theorem LemG:
  f-g, X is_absolutely_bounded_by r implies
  g-f, X is_absolutely_bounded_by r
  proof
    d: dom (f-g)= dom f /\ dom g by SEQ_1:def 4 .= dom (g-f) by SEQ_1:def 4;
    assume fg: f-g, X is_absolutely_bounded_by r;
    let x be set;
    assume x: x in X /\ dom (g-f); then
    xd: x in dom (g-f) by XBOOLE_0:def 3;
    abs((f-g).x) <= r by x,dab,fg,d; then
    abs(f.x-g.x) <= r by SEQ_1:def 4,xd,d; then
    abs(-(f.x-g.x)) <= r by COMPLEX1:138; then
    abs(g.x-f.x) <= r;
    hence abs((g-f).x) <= r by SEQ_1:def 4,xd;
  end;

:: Tietze Extension Theorem
theorem
  T is being_T4 implies
  for A for f being map of T|A, Closed-Interval-TSpace(-1,1) st f is continuous
  ex g being continuous map of T, Closed-Interval-TSpace(-1,1) st g|A = f
  proof
    assume
A1: T is being_T4;
    let A; let f be map of T|A, Closed-Interval-TSpace(-1,1) such that
A2: f is continuous;
g2: the carrier of Closed-Interval-TSpace(-1,1) = [. -1,1 .] by TOPMETR:25;
w2: the carrier of T|A = A by JORDAN1:1;
    per cases;
    suppose A is empty;
      then reconsider A1 = A as empty Subset of T;
h1:   dom f = the carrier of T|A1 by FUNCT_2:def 1;
      set g = T --> R^1(0);
      g = (the carrier of T) --> R^1(0) by BORSUK_1:def 3
      .= (the carrier of T) --> 0 by TOPREALB:def 2;
      then
h2:   rng g = {0} by FUNCOP_1:14;
      rng g c= the carrier of Closed-Interval-TSpace(-1,1)
      proof
        let y be set;
        assume y in rng g;
        then y = 0 by h2,TARSKI:def 1;
        hence thesis by g2,RCOMP_1:48;
      end;
      then reconsider g as map of T, Closed-Interval-TSpace(-1,1) by FUNCT_2:8;
      reconsider g as continuous map of T, Closed-Interval-TSpace(-1,1)
      by TOPMETR:8;
      take g;
      g|A1 is empty;
      hence g|A = f by h1,RELAT_1:64;
    end;
    suppose A is non empty;
      then reconsider A1 = A as non empty Subset of T;
      set DF = Funcs(the carrier of T,the carrier of R^1);
      set D = {q where q is Element of DF: q is continuous map of T,R^1};
      T --> R^1(0) is Element of DF by FUNCT_2:11;
      then T --> R^1(0) in D;
      then reconsider D as non empty set;
      reconsider f1 = f as map of T|A1,R^1 by TOPREALA:28;
      f1 is continuous by A2,TOPMETR:7;
      then reconsider f1 = f as continuous map of T|A1,R^1;
      f1,A is_absolutely_bounded_by 1
      proof
        let x be set;
        assume x in A /\ dom f1;
        then x in dom f1 by XBOOLE_0:def 3;
        then
g1:     f1.x in rng f1 by FUNCT_1:def 5;
        rng f1 c= the carrier of Closed-Interval-TSpace(-1,1) by RELSET_1:12;
        then -1 <= f1.x & f1.x <= 1 by g1,g2,RCOMP_1:48;
        hence abs(f1.x) <= 1 by ABSVALUE:12;
      end;
      then consider g0 being continuous map of T, R^1 such that
a4:   g0,dom g0 is_absolutely_bounded_by 1/3 and
a5:   f1-g0,A is_absolutely_bounded_by 2*1/3 by A1,Th7;
      g0 in DF by FUNCT_2:11;
      then g0 in D;
      then reconsider g0d = g0 as Element of D;
      defpred Z[Element of NAT,set,set] means
      ex E2 being continuous map of T,R^1 st E2 = $2 &
      (f-E2,A is_absolutely_bounded_by 2/3*(2/3)|^$1 implies
      ex g being continuous map of T,R^1 st $3 = E2+g &
      g,the carrier of T is_absolutely_bounded_by 1/3*(2/3)|^($1+1) &
      f-E2-g,A is_absolutely_bounded_by 2/3*(2/3)|^($1+1));
b1:   for n being Nat for x being Element of D
      ex y being Element of D st Z[n,x,y]
      proof
        let n be Nat, x be Element of D;
        x in D;
        then consider E2 being Element of DF such that
c1:     E2 = x & E2 is continuous map of T, R^1;
        reconsider E2 as continuous map of T, R^1 by c1;
        per cases;
        suppose
s1:       not f-E2,A is_absolutely_bounded_by 2/3*(2/3)|^n;
          take x, E2;
          thus thesis by s1,c1;
        end;
        suppose
s2:       f-E2,A is_absolutely_bounded_by 2/3*(2/3)|^n;
          reconsider E2a = E2 as RealMap of T by TOPMETR:24;
          reconsider E2a as continuous RealMap of T by TOPREAL6:83;
          reconsider fa = f1 as RealMap of T|A1 by TOPMETR:24;
          reconsider fa as continuous RealMap of T|A1 by TOPREAL6:83;
          reconsider E2b = E2|A as map of T|A1, R^1 by JGRAPH_3:12;
          reconsider E2b as continuous map of T|A1, R^1 by TOPMETR:10;
          E2b c= E2 by RELAT_1:88;
          then f-E2b c= f-E2 by Tmm1;
          then
S2:       f-E2b,A is_absolutely_bounded_by 2/3*(2/3)|^n by s2,Tzaw1;
          set k = f1-E2b;
g3:       dom k = dom f1 /\ dom E2b by SEQ_1:def 4
          .= (the carrier of T|A) /\ dom E2b by FUNCT_2:def 1
          .= (the carrier of T|A) /\ the carrier of T|A by FUNCT_2:def 1;
          rng k c= the carrier of R^1 by TOPMETR:24;
          then reconsider k as map of T|A1, R^1 by g3,FUNCT_2:4;
          reconsider f1c = f1, E2c = E2b as RealMap of T|A1 by TOPMETR:24;
          reconsider f1c, E2c as continuous RealMap of T|A1 by TOPREAL6:83;
          f1c-E2c is continuous;
          then
b2:       k is continuous by TOPREAL6:83;
          set r = 2/3*(2/3)|^n;
          (2/3)|^n > 0 by HEINE:5;
          then r > 0 by XREAL_1:131;
          then consider g being continuous map of T, R^1 such that
b3:       g,dom g is_absolutely_bounded_by r/3 and
b6:       k-g,A is_absolutely_bounded_by 2*r/3 by b2,A1,S2,Th7;
          reconsider ga = g as RealMap of T by TOPMETR:24;
          reconsider ga as continuous RealMap of T by TOPREAL6:83;
          set y = E2a+ga;
          reconsider y1 = y as map of T, R^1 by TOPMETR:24;
r1:       y1 in DF by FUNCT_2:11;
          y1 is continuous map of T, R^1 by TOPREAL6:83;
          then y in D by r1;
          then reconsider y as Element of D;
          take y, E2;
          thus E2 = x by c1;
          assume f-E2,A is_absolutely_bounded_by 2/3*(2/3)|^n;
          take g; 
          thus y = E2+g;
b7:       2/3*(2/3)|^n = (2/3)|^(n+1) by NEWTON:11;
          hence g,the carrier of T is_absolutely_bounded_by 1/3*(2/3)|^(n+1)
          by b3,FUNCT_2:def 1;
h1:       dom f = the carrier of T|A by FUNCT_2:def 1;
h5:       dom E2b = the carrier of T|A by FUNCT_2:def 1;
h3:       dom g = the carrier of T by FUNCT_2:def 1;
h4:       dom(f-E2b-g) = dom(f-E2b) /\ dom g by SEQ_1:def 4
          .= dom f /\ dom E2b /\ dom g by SEQ_1:def 4
          .= A by h1,h5,h3,w2,XBOOLE_1:28;
          (f-E2b-g)|A = ((f-E2b)|A) - g by RFUNCT_1:63
          .= f - E2b|A - g by RFUNCT_1:63
          .= f - E2|A - g by RELAT_1:101
          .= (f-E2)|A - g by RFUNCT_1:63
          .= (f-E2-g)|A by RFUNCT_1:63;
          hence f-E2-g,A is_absolutely_bounded_by 2/3*(2/3)|^(n+1)
          by b6,b7,h4,Tzaw2;
        end;
      end;
      consider F being Function of NAT,D such that
F0:   F.0 = g0d and
Fn:   for n being Element of NAT holds Z[n,F.n,F.(n+1)]
      from RECDEF_1:sch 2(b1);
df:   dom F = NAT by FUNCT_2:def 1;
r2:   the carrier of RealSpace = REAL & the carrier of R^1 = REAL
      by TOPMETR:16,METRIC_1:def 14,TOPMETR:def 7;
      now
        let n be Nat;
        Z[n,F.n,F.(n+1)] by Fn;
        hence F.n is PartFunc of the carrier of T,REAL by r2;
      end; then
      reconsider F as Functional_Sequence of the carrier of T,REAL
      by df,SEQFUNC:1;
      now
        let n be Nat;
        Z[n,F.n,F.(n+1)] by Fn;
        hence the carrier of T c= dom(F.n) by FUNCT_2:def 1;
      end; then
T1:   the carrier of T common_on_dom F by SEQFUNC:def 10;
      2/9 > 0 & 2/3 > 0; then
T3:   2/9 is positive & 2/3 is positive;
      defpred P[Nat] means
       F.$1 is continuous map of T,R^1 &
       F.$1-F.($1+1), the carrier of T
       is_absolutely_bounded_by (2/9)*((2/3) to_power $1) &
       F.$1-f, A1 is_absolutely_bounded_by (2/3)*((2/3) to_power $1);
      consider E2 being continuous map of T,R^1 such that
B1:   E2 = F.0 &
      (f-E2,A is_absolutely_bounded_by 2/3*(2/3)|^0 implies
      ex g being continuous map of T,R^1 st F.(0+1) = E2+g &
      g,the carrier of T is_absolutely_bounded_by 1/3*(2/3)|^(0+1) &
      f-E2-g,A is_absolutely_bounded_by 2/3*(2/3)|^(0+1)) by Fn;
B2:   2*1/3 = (2/3)*1 & (2/3) to_power 0 = 1 & (2/3)|^0 = 1
      by POWER:29,NEWTON:9;
      consider g1 being continuous map of T,R^1 such that
B3:   F.(0+1) = E2+g1 &
      g1,the carrier of T is_absolutely_bounded_by 1/3*(2/3)|^(0+1) &
      f-E2-g1,A is_absolutely_bounded_by 2/3*(2/3)|^(0+1) by B1,B2,F0,a5;
n:    1/3*(2/3)|^1 = 1/3*(2/3) by NEWTON:10;
de:   dom g1 = the carrier of T by FUNCT_2:def 1
      .= dom E2 by FUNCT_2:def 1; 
df:   dom (g1+E2) = (dom g1 /\ dom E2) by SEQ_1:def 3
      .= dom g1 by de;
dg:   dom (g1+E2-E2) = dom (g1+E2) /\ dom E2 by SEQ_1:def 4
      .= (dom g1 /\ dom E2) /\ dom E2 by SEQ_1:def 3
      .= dom g1 by de;
      now
        let a be set;
        assume ad: a in dom g1;
        hence (g1+E2-E2).a = (g1+E2).a - E2.a by SEQ_1:def 4,dg
        .= g1.a+E2.a-E2.a by SEQ_1:def 3,df,ad
        .= g1.a;
      end; then  
      1/3*(2/3)|^(0+1) = (2/9)*1 & F.(0+1)-F.0 = g1
        by n,B1,B3,FUNCT_1:9,dg; then
z:    P[0] by F0,a5,B2,LemG,B3;
i:    now
        let n be Nat;
        consider E2 being continuous map of T,R^1 such that
        e2: E2 = F.n &
        (f-E2,A is_absolutely_bounded_by 2/3*(2/3)|^n implies
        ex g being continuous map of T,R^1 st F.(n+1) = E2+g &
        g,the carrier of T is_absolutely_bounded_by 1/3*(2/3)|^(n+1) &
        f-E2-g,A is_absolutely_bounded_by 2/3*(2/3)|^(n+1)) by Fn;
        assume P[n]; then
        F.n-f, A1 is_absolutely_bounded_by (2/3)*((2/3)|^n) by POWER:46; then
        consider g being continuous map of T,R^1 such that
        g: F.(n+1) = E2+g &
        g,the carrier of T is_absolutely_bounded_by 1/3*(2/3)|^(n+1) &
         f-E2-g,A is_absolutely_bounded_by 2/3*(2/3)|^(n+1) by e2,LemG;
        reconsider E2'=E2 as continuous RealMap of T by r2,TOPREAL6:83;
        reconsider g'=g as continuous RealMap of T by r2,TOPREAL6:83;
        e2g: E2'+g' is continuous RealMap of T;
        f1: F.(n+1) is continuous map of T,R^1 by g,TOPREAL6:83,e2g,r2;
        consider E3 being continuous map of T,R^1 such that
e3:     E3 = F.(n+1) &
        (f-E3,A is_absolutely_bounded_by 2/3*(2/3)|^(n+1) implies
        ex g being continuous map of T,R^1 st F.((n+1)+1) = E3+g &
        g,the carrier of T is_absolutely_bounded_by 1/3*(2/3)|^((n+1)+1) &
        f-E3-g,A is_absolutely_bounded_by 2/3*(2/3)|^((n+1)+1)) by Fn;
fe:     f-E2-g,A is_absolutely_bounded_by 2/3*((2/3) to_power (n+1))
        by g,POWER:46;
de:     dom f = the carrier of T|A1 by FUNCT_2:def 1 .= [#](T|A1)
        by PRE_TOPC:12
        .= A by PRE_TOPC:def 10;
dg:     dom (f-E2-g) = dom (f-E2) /\ dom g by SEQ_1:def 4
        .= (dom f) /\ (dom E2) /\ dom g by SEQ_1:def 4
        .= (dom f) /\ (the carrier of T) /\ dom g by FUNCT_2:def 1
        .= (dom f) /\ dom g by de,XBOOLE_1:28
        .= (dom f) /\ the carrier of T by FUNCT_2:def 1
        .= A by de,XBOOLE_1:28;
dh:     dom (f-(E2+g)) = (dom f) /\ dom (E2+g) by SEQ_1:def 4
        .= A /\ (dom E2 /\ dom g) by de,SEQ_1:def 3
        .= A /\ (dom E2 /\ the carrier of T) by FUNCT_2:def 1
        .= A /\ ((the carrier of T) /\ the carrier of T) by FUNCT_2:def 1
        .= A by XBOOLE_1:28;
df:     dom (f-E2) = A/\dom E2 by de,SEQ_1:def 4 .= A /\ the carrier of T
        by FUNCT_2:def 1
        .= A by XBOOLE_1:28;
        dom E2 = the carrier of T & dom g = the carrier of T
        by FUNCT_2:def 1; then
dd:     dom (E2+g) = (the carrier of T) /\ the carrier of T by SEQ_1:def 3; 
        now
          let a be set;
          assume ad: a in A;
          hence (f-E2-g).a = (f-E2).a - g.a by dg,SEQ_1:def 4
          .= f.a - E2.a - g.a by ad,df,SEQ_1:def 4
          .= f.a - (E2.a + g.a)
          .= f.a - (E2+g).a by ad,dd,SEQ_1:def 3
          .= (f-(E2+g)).a by ad,dh,SEQ_1:def 4;
        end; then  
fy:     f-F.(n+1), A1 is_absolutely_bounded_by (2/3)*((2/3) to_power (n+1))
        by fe,g,dg,dh,FUNCT_1:9; then
fx:     F.(n+1)-f, A1 is_absolutely_bounded_by (2/3)*((2/3) to_power (n+1))
        by LemG;
        consider gx being continuous map of T,R^1 such that
gx:     F.((n+1)+1) = E3+gx &
        gx,the carrier of T is_absolutely_bounded_by 1/3*(2/3)|^((n+1)+1) &
        f-E3-gx,A is_absolutely_bounded_by 2/3*(2/3)|^((n+1)+1)
          by e3,fy,POWER:46;
tp:     (2/9)*((2/3) to_power (n+1)) = 1/3*(2/3)*((2/3) |^ (n+1)) by POWER:46
        .= 1/3*((2/3)*((2/3) |^ (n+1)))
        .= 1/3*((2/3)|^((n+1)+1)) by NEWTON:11;
de:     dom gx = the carrier of T by FUNCT_2:def 1
        .= dom E3 by FUNCT_2:def 1; 
df:     dom (gx+E3) = (dom gx /\ dom E3) by SEQ_1:def 3
        .= dom gx by de;
dg:     dom (gx+E3-E3) = dom (gx+E3) /\ dom E3 by SEQ_1:def 4
        .= (dom gx /\ dom E3) /\ dom E3 by SEQ_1:def 3
        .= dom gx by de;
        now
          let a be set;
          assume ad: a in dom gx;
          hence (gx+E3-E3).a = (gx+E3).a - E3.a by SEQ_1:def 4,dg
          .= gx.a+E3.a-E3.a by SEQ_1:def 3,df,ad
          .= gx.a;
        end; then  
        F.((n+1)+1)-F.(n+1) = gx by gx,e3,dg,FUNCT_1:9;
        hence P[n+1] by f1,fx,LemG,gx,tp;
      end;
T:    for n being Nat holds P[n] from NAT_1:sch 1(z,i);
T7:   now let n be natural number; n is Nat by ORDINAL2:def 21;
        hence (F.n)-(F.(n+1)), the carrier of T
        is_absolutely_bounded_by (2/9)*((2/3) to_power n) by T;
      end; then
S:    F is_unif_conv_on the carrier of T by T1,T3,Lem2; then
      reconsider h = lim(F, the carrier of T) as continuous map of T, R^1
      by T,lemuni;
T4:   now let n be natural number;
        n is Nat by ORDINAL2:def 21;
        hence (F.n)-f, A1 is_absolutely_bounded_by (2/3)*((2/3) to_power n)
        by T;
      end;
T5:   A1 common_on_dom F by T1,SEQFUNC:24;
      dom f = the carrier of T|A1 &
      rng f c= REAL by SEQ_1:def 1,FUNCT_2:def 1; then
T6:   f is Function of A1,REAL by w2,FUNCT_2:4;
      F is_point_conv_on the carrier of T by S,SEQFUNC:23; then
R:    h|A1 = lim(F, A1) by SEQFUNC:25 .= f by T5,T3,T4,T6,Lem3;
      h, the carrier of T is_absolutely_bounded_by 1
      proof let x be set;
        assume x in (the carrier of T) /\ dom h; then
        reconsider z = x as Element of T by XBOOLE_0:def 3;
        dom (F.0) = the carrier of T by F0,FUNCT_2:def 1; then
        z in (the carrier of T) /\ dom (F.0) &
        F.0, the carrier of T is_absolutely_bounded_by 1/3 by a4,F0; then
        |.F.0 .z.| <= 1/3 by dab; then
        F.0 .z >= -1/3 & F.0 .z <= 1/3 by ABSVALUE:12; then
        F.0 .z-(2/9)/(1-2/3) >= -1/3-(2/9)/(1-2/3) &
        F.0 .z+(2/9)/(1-2/3) <= 1/3+(2/9)/(1-2/3) &
        h.z >= F.0 .z-(2/9)/(1-2/3) & h.z <= F.0 .z+(2/9)/(1-2/3)
        by T1,T7,T3,Lem2a,XREAL_1:9,11; then
        h.z >= -1 & h.z <= 1 by XREAL_1:2;
        hence thesis by ABSVALUE:12;
      end; then
      reconsider h as map of T, Closed-Interval-TSpace(-1,1) by LemH;
      R^1 [.-1,1 .] = [.-1,1 .] by TOPREALB:def 3
      .= [#] Closed-Interval-TSpace(-1,1) by g2,PRE_TOPC:def 3; then
      Closed-Interval-TSpace(-1,1) = R^1|R^1 [.-1,1 .]
      by PRE_TOPC:def 10; then
      h is continuous by TOPMETR:9;
      hence thesis by R;
    end;
  end;

theorem
  (for A being non empty closed Subset of T
  for f being continuous map of T|A, Closed-Interval-TSpace(-1,1)
  ex g being continuous map of T, Closed-Interval-TSpace(-1,1) st g|A = f)
  implies T is being_T4
  proof
    assume
A3: for A being non empty closed Subset of T
    for f being continuous map of T|A, Closed-Interval-TSpace(-1,1)
    ex g being continuous map of T, Closed-Interval-TSpace(-1,1) st g|A = f;
    for C, D being non empty closed Subset of T st C misses D holds
    ex f being continuous map of T, R^1 st f.:C = {0} & f.:D = {1}
    proof
      let C, D being non empty closed Subset of T such that
A4:   C misses D;
      set f1 = T --> R^1(0);
      set f2 = T --> R^1(1);
      set g1 = f1|(T|C), g2 = f2|(T|D);
A5:   g1 is continuous & g2 is continuous by TMAP_1:68;
      set f = g1 union g2;
A6:   the carrier of T|C = C & the carrier of T|D = D by JORDAN1:1;
      the carrier of T|(C\/D) = C \/ D by JORDAN1:1;
      then
A7:   T|(C\/D) = (T|C) union (T|D) by A6,TSEP_1:def 2;
A8:   the carrier of Closed-Interval-TSpace(-1,1) = [. -1,1 .] by TOPMETR:25;
A9:   T|C misses T|D by A4,A6,TSEP_1:def 3;
      then
A10:  rng f c= rng g1 \/ rng g2 by Th5;
      g1 = f1|the carrier of T|C & g2 = f2|the carrier of T|D by TMAP_1:def 3;
      then rng g1 c= rng f1 & rng g2 c= rng f2 by RELAT_1:99;
      then rng g1 \/ rng g2 c= rng f1 \/ rng f2 by XBOOLE_1:13;
      then
A11:  rng f c= rng f1 \/ rng f2 by A10,XBOOLE_1:1;
A12:  f1 = (the carrier of T) --> R^1(0) by BORSUK_1:def 3
      .= (the carrier of T) --> 0 by TOPREALB:def 2;
      then
A13:  rng f1 = {0} by FUNCOP_1:14;
A14:  f2 = (the carrier of T) --> R^1(1) by BORSUK_1:def 3
      .= (the carrier of T) --> 1 by TOPREALB:def 2;
      then
A15:  rng f2 = {1} by FUNCOP_1:14;
      rng f c= [. -1,1 .]
      proof
        let x be set;
        assume x in rng f;
        then x in {0} or x in {1} by A13,A15,A11,XBOOLE_0:def 2;
        then x = 0 or x = 1 by TARSKI:def 1;
        hence thesis by RCOMP_1:48;
      end;
      then reconsider h = f as map of T|(C\/D), Closed-Interval-TSpace(-1,1)
      by A7,A8,FUNCT_2:8;
      f is continuous map of (T|C) union (T|D), R^1 by A5,A9,TMAP_1:148;
      then
A16:  h is continuous by A7,TOPMETR:8;
      C \/ D is closed by TOPS_1:36;
      then consider g being continuous map of T, Closed-Interval-TSpace(-1,1)
      such that
A17:  g|(C\/D) = f by A3,A16;
      reconsider F = g as continuous map of T, R^1 by TOPMETR:7,TOPREALA:28;
      take F;
A19:  C c= C;
A20:  f1.:C = {0}
      proof
        thus f1.:C c= {0} by A12,BORSUK_1:6;
        let y be set;
        assume y in {0};
        then
A21:    y = 0 by TARSKI:def 1;
A22:    dom f1 = the carrier of T by A12,FUNCOP_1:19;
        consider c being set such that
A23:    c in C by XBOOLE_0:def 1;
        f1.c = 0 by A12,A23,FUNCOP_1:13;
        hence thesis by A23,A22,A21,FUNCT_1:def 12;
      end;
      C c= C \/ D by XBOOLE_1:7;
      hence F.:C = f.:C by A17,TMAP_1:4
      .= g1.:C by A9,A19,A6,Th6
      .= (f1|C).:C by A6,TMAP_1:def 3
      .= {0} by A20,EXTENS_1:1;
A24:  D c= D;
A25:  f2.:D = {1}
      proof
        thus f2.:D c= {1} by A14,BORSUK_1:6;
        let y be set;
        assume y in {1};
        then
A26:    y = 1 by TARSKI:def 1;
A27:    dom f2 = the carrier of T by A14,FUNCOP_1:19;
        consider c being set such that
A28:    c in D by XBOOLE_0:def 1;
        f2.c = 1 by A14,A28,FUNCOP_1:13;
        hence thesis by A28,A27,A26,FUNCT_1:def 12;
      end;
      D c= C \/ D by XBOOLE_1:7;
      hence F.:D = f.:D by A17,TMAP_1:4
      .= g2.:D by A9,A24,A6,Th6
      .= (f2|D).:D by A6,TMAP_1:def 3
      .= {1} by A25,EXTENS_1:1;
    end;
    hence thesis by Th8;
  end;
