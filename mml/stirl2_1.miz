:: Stirling Numbers of the Second Kind
::  by Karol P\c{a}k
::
:: Received March 15, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies NUMBERS, SUBSET_1, XBOOLE_0, XXREAL_0, NAT_1, ORDINAL1, CARD_1,
      TARSKI, ARYTM_3, ARYTM_1, FUNCT_1, RELAT_1, FUNCT_2, INT_1, FINSET_1,
      AFINSQ_1, BINOP_1, FINSEQ_1, FINSOP_1, CARD_3, BINOP_2, ZFMISC_1, NEWTON,
      REALSET1, STIRL2_1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, CARD_1, NUMBERS, RELAT_1,
      FUNCT_1, RELSET_1, ORDINAL1, SEQ_4, PARTFUN1, FINSET_1, AFINSQ_1,
      AFINSQ_2, XXREAL_0, FUNCT_2, NAT_1, INT_1, BINOP_1, BINOP_2, NEWTON,
      DOMAIN_1;
 constructors WELLORD2, DOMAIN_1, SETWISEO, REAL_1, BINOP_2, SEQ_4, NEWTON,
      AFINSQ_1, AFINSQ_2, RELSET_1, PARTFUN3, SEQ_1;
 registrations XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, ORDINAL1, FUNCT_2,
      FINSET_1, XXREAL_0, XREAL_0, NAT_1, INT_1, BINOP_2, MEMBERED, AFINSQ_1,
      CARD_1, VALUED_0, XXREAL_2, RELSET_1, AFINSQ_2;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, XBOOLE_0, FUNCT_1;
 theorems AFINSQ_1, TARSKI, FUNCT_1, XBOOLE_0, ZFMISC_1, RELAT_1, XBOOLE_1,
      NAT_1, FUNCT_2, AXIOMS, XCMPLX_1, CARD_2, INT_1, ORDINAL1, WELLORD2,
      FRAENKEL, SUBSET_1, ENUMSET1, CARD_1, BINOP_2, NEWTON, WSIERP_1, XREAL_1,
      XXREAL_0, XXREAL_2, AFINSQ_2;
 schemes FUNCT_2, NAT_1, INT_1;

begin

reserve k, l, m, n, i, j for Nat,
  K, N for non empty Subset of NAT,
  Ke, Ne, Me for Subset of NAT,
  X,Y for set;

theorem Th1:
  min N = min* N
proof
A1: for n being ext-real number st n in N holds min* N <= n by NAT_1:def 1;
  min* N in N by NAT_1:def 1;
  hence thesis by A1,XXREAL_2:def 7;
end;

theorem Th2:
  min(min K,min N) = min(K\/N)
proof
  set m=min(min N,min K);
A1: for k be ext-real number st k in N\/K holds m<=k
  proof
    let k be ext-real number;
    assume k in N\/K;
    then k in N or k in K by XBOOLE_0:def 3;
    then
A2: min N<=k or min K <=k by XXREAL_2:def 7;
A3: m<=min K by XXREAL_0:17;
    m<=min N by XXREAL_0:17;
    hence thesis by A2,A3,XXREAL_0:2;
  end;
  m=min N or m=min K by XXREAL_0:15;
  then m in N or m in K by XXREAL_2:def 7;
  then m in N\/K by XBOOLE_0:def 3;
  hence thesis by A1,XXREAL_2:def 7;
end;

theorem
  min(min* Ke,min* Ne) <= min* (Ke\/Ne)
proof
  now
    per cases;
    suppose
      Ke is empty or Ne is empty;
      then min* Ke=0 & min* Ne >=0 or min* Ne=0 & min*Ke>=0 by
      NAT_1:def 1;
      hence thesis by XXREAL_0:def 9;
    end;
    suppose
      Ke is non empty & Ne is non empty;
      then reconsider K=Ke,N=Ne as non empty Subset of NAT;
A1:   min N=min* Ne by Th1;
A2:   min(K\/N)=min*(Ke\/Ne) by Th1;
      min K=min* Ke by Th1;
      hence thesis by A1,A2,Th2;
    end;
  end;
  hence thesis;
end;

theorem
  not min* Ne in Ne/\Ke implies min* Ne = min* (Ne\Ke)
proof
  assume
A1: not min* Ne in Ne/\Ke;
  now
    per cases;
    suppose
      Ne is empty;
      hence thesis;
    end;
    suppose
      Ne is non empty;
      then
A2:   min* Ne in Ne by NAT_1:def 1;
      then not min* Ne in Ke by A1,XBOOLE_0:def 4;
      then
A3:   min* Ne in Ne\Ke by A2,XBOOLE_0:def 5;
      then
A4:   min* (Ne\Ke)<=min* Ne by NAT_1:def 1;
A5:   Ne\Ke c=Ne by XBOOLE_1:36;
      min* (Ne\Ke) in Ne\Ke by A3,NAT_1:def 1;
      then min* Ne<=min* (Ne\Ke) by A5,NAT_1:def 1;
      hence thesis by A4,XXREAL_0:1;
    end;
  end;
  hence thesis;
end;

theorem Th5:
for n be Element of NAT holds min* {n} = n & min {n} = n
proof
 let n be Element of NAT;
A1: min* {n} in {n} by NAT_1:def 1;
  min*{n}=min {n} by Th1;
  hence thesis by A1,TARSKI:def 1;
end;

theorem Th6:
for n,k be Element of NAT holds min* {n,k} = min(n,k) & min {n,k} = min(n,k)
proof
let n,k be Element of NAT;
A1: min {n} = n by Th5;
  {n,k}={n}\/{k} by ENUMSET1:41;
  then
A2: min {n,k} = min(min {n},min {k}) by Th2;
  min {n,k} = min* {n,k} by Th1;
  hence thesis by A2,A1,Th5;
end;

theorem
for n,k,l be Element of NAT holds min* {n,k,l} = min(n,min(k,l))
proof
let n,k,l be Element of NAT;
A1: min {n,k,l}=min* {n,k,l} by Th1;
  {n,k,l}={n}\/{k,l} by ENUMSET1:42;
  then
A2: min{n,k,l} = min(min {n},min {k,l}) by Th2;
  min {k,l}=min(k,l) by Th6;
  hence thesis by A2,A1,Th5;
end;

theorem Th8:
  n is Subset of NAT
proof
  now
    let x be set;
    assume x in {l where l is Element of NAT: l<n};
    then ex l be Element of NAT st x=l & l<n;
    hence x in NAT;
  end;
  then {l where l is Element of NAT:l<n} c= NAT by TARSKI:def 3;
  hence thesis by AXIOMS:30;
end;

registration
  let n;
  cluster -> natural Element of n;
  coherence;
end;

theorem
  N c= n implies n-1 is Element of NAT
proof
A1: min* N in N by NAT_1:def 1;
  assume N c= n;
  then min*N in n by A1;
  then min* N in {l where l is Element of NAT:l<n} by AXIOMS:30;
  then ex l be Element of NAT st min* N = l & l<n;
  hence thesis by NAT_1:20;
end;

theorem Th10:
  k in n implies k<=n-1 & n-1 is Element of NAT
proof
  assume k in n;
  then k in {l where l is Element of NAT:l<n} by AXIOMS:30;
  then
A1: ex l be Element of NAT st k=l & l<n;
  then
A2: n-1 is Element of NAT by NAT_1:20;
  k<(n-1)+1 by A1;
  hence thesis by A2,NAT_1:13;
end;

theorem
  min* n = 0
proof
  now
    per cases;
    suppose
      0=n;
      hence thesis by NAT_1:def 1;
    end;
    suppose
      0<n;
      then 0 in {l where l is Element of NAT:l<n};
      then
A1:   0 in n by AXIOMS:30;
      n is Subset of NAT by Th8;
      hence thesis by A1,NAT_1:def 1;
    end;
  end;
  hence thesis;
end;

theorem Th12:
  N c= n implies min* N <= n-1
proof
A1: min* N in N by NAT_1:def 1;
  assume N c= n;
  hence thesis by A1,Th10;
end;

theorem
  N c= n & N <> {n-1} implies min* N < n-1
proof
  assume that
A1: N c= n and
A2: N<>{n-1} and
A3: min* N >=n-1;
  now
    let k be set such that
A4: k in N;
    reconsider k9=k as Element of NAT by A4;
    min* N <=k9 by A4,NAT_1:def 1;
    then
A5: n-1 <=k9 by A3,XXREAL_0:2;
    k9 <= n-1 by A1,A4,Th10;
    then n-1 = k by A5,XXREAL_0:1;
    hence k in {n-1} by TARSKI:def 1;
  end;
  then N c= {n-1} by TARSKI:def 3;
  hence thesis by A2,ZFMISC_1:39;
end;

theorem Th14:
  Ne c= n & n>0 implies min* Ne <= n-1
proof
  assume that
A1: Ne c= n and
A2: n>0;
  now
    per cases;
    suppose
      Ne is non empty;
      hence thesis by A1,Th12;
    end;
    suppose
      Ne is empty;
      then min* Ne =0 by NAT_1:def 1;
      hence thesis by A2,NAT_1:20;
    end;
  end;
  hence thesis;
end;

reserve f for Function of n,k;

definition
  let n,X;
  let f be Function of n,X;
  let x be set;
  redefine func f"x -> Subset of NAT;
  coherence
  proof
    now
A1:   n is Subset of NAT by Th8;
      let y be set;
      assume y in f"x;
      hence y in NAT by A1,TARSKI:def 3;
    end;
    hence thesis by TARSKI:def 3;
  end;
end;

definition
  let X,k;
  let f be Function of X,k;
  let x be set;
  redefine func f.x -> Element of k;
  coherence
  proof
    per cases;
    suppose
      x in dom f;
      then f.x in rng f by FUNCT_1:12;
      hence f.x is Element of k;
    end;
    suppose
A1:   not x in dom f;
A2:   k=0 or k>0;
      f.x=0 by A1,FUNCT_1:def 4;
      hence thesis by A2,NAT_1:45,SUBSET_1:def 2;
    end;
  end;
end;

registration
  let X,Ne;
  let f be Function of X,Ne,x be set;
  cluster f.x -> natural;
  coherence;
end;

definition
  let n,k be Nat;
  let f be Function of n,k;
  attr f is "increasing means
  :Def1:
  (n = 0 iff k = 0) & for l,m st l in rng f
  & m in rng f & l < m holds min* f"{l} < min* f"{m};
end;

theorem Th15:
  n=0 & k=0 implies f is onto "increasing
proof
  assume that
A1: n=0 and
A2: k=0;
A3: for l,m st l in k & m in k & l < m holds min* f"{l} < min* f"{m} by A2;
  rng f = {} by A1;
  hence thesis by A1,A2,A3,Def1,FUNCT_2:def 3;
end;

theorem Th16:
  n>0 implies min* f"{m} <= n-1
proof
A1: f"{m} c= n
  proof
    let x be set;
    assume x in f"{m};
    then x in dom f by FUNCT_1:def 13;
    hence thesis;
  end;
  assume n>0;
  hence thesis by A1,Th14;
end;

theorem Th17:
  f is onto implies n>=k
proof
  assume
A1: f is onto;
  now
    per cases;
    suppose
      k=0;
      hence thesis;
    end;
    suppose
A2:   k<>0;
A3:   rng f = k by A1,FUNCT_2:def 3;
      dom f =n by A2,FUNCT_2:def 1;
      then card k c= card n by A3,CARD_1:28;
      then
A4:   card k <= card n by NAT_1:40;
      card n = n by CARD_1:def 5;
      hence thesis by A4,CARD_1:def 5;
    end;
  end;
  hence thesis;
end;

theorem Th18:
  f is onto "increasing implies for m st m < k holds m <= min* f"{ m}
proof
  defpred M[Nat] means $1<k implies $1<=min* f"{$1};
  assume that
A1: f is onto "increasing;
A2: for m st M[m] holds M[m + 1]
  proof
A3: k=rng f by A1,FUNCT_2:def 3;
    let m such that
A4: M[m];
    assume
A5: m+1<k;
    m<m+1 by NAT_1:19;
    then m < k by A5,XXREAL_0:2;
    then
A6: m in rng f by A3,NAT_1:45;
A7: m < m+1 by NAT_1:19;
    m+1 in rng f by A5,A3,NAT_1:45;
    then min* f"{m}< min* f"{m+1} by A1,A6,A7,Def1;
    then m < min* f"{m+1} by A4,A5,A7,XXREAL_0:2;
    hence thesis by NAT_1:13;
  end;
A8: M[0];
  for m holds M[m] from NAT_1:sch 2(A8,A2);
  hence thesis;
end;

theorem Th19:
  f is onto "increasing implies for m st m < k holds min* f"{m} <= n-k+m
proof
  assume that
A1: f is onto "increasing;
  now
    per cases;
    suppose
      k=0;
      hence thesis;
    end;
    suppose
      k>0;
      then reconsider k1=k-1 as Element of NAT by NAT_1:20;
      defpred I[Integer] means 0<=$1 implies min* f"{$1} <= n-k+$1;
A2:   k1<k1+1 by NAT_1:13;
A3:   for m be Integer st m <= k1 holds I[m] implies I[m-1]
      proof
        reconsider nk=n-k as Element of NAT by A1,Th17,NAT_1:21;
A4:     k = rng f by A1,FUNCT_2:def 3;
        let m be Integer;
        assume m <= k1;
        then
A5:     m < k by A2,XXREAL_0:2;
        assume
A6:     I[m];
        assume 0<=m-1;
        then reconsider m1=m-1 as Element of NAT by INT_1:16;
A7:     m1<m1+1 by NAT_1:19;
        then m1<k by A5,XXREAL_0:2;
        then
A8:     m1 in k by NAT_1:45;
        m1+1 in k by A5,NAT_1:45;
        then min* f"{m1}<min* f"{m1+1} by A1,A7,A8,A4,Def1;
        then min* f"{m1} < (nk+m1)+1 by A6,XXREAL_0:2;
        hence thesis by NAT_1:13;
      end;
A9:   I[k1]
      proof
        assume 0<= k1;
        k=0 iff n=0 by A1,Def1;
        then min* f"{k1}<=n-1 by Th16;
        hence thesis;
      end;
A10:  for m be Integer st m <= k1 holds I[m] from INT_1:sch 3(A9,A3);
      now
        let m;
        assume m < k;
        then m<k1+1;
        then m<=k1 by NAT_1:13;
        hence min* f"{m} <= n-k+m by A10;
      end;
      hence thesis;
    end;
  end;
  hence thesis;
end;

theorem Th20:
  f is onto "increasing & n = k implies f = id n
proof
  assume that
A1: f is onto "increasing and
A2: n=k;
  now
    per cases;
    suppose
A3:   n=0;
A4:   for x be set st x in 0 holds f.x=x;
      dom f=0 by A3;
      hence thesis by A3,A4,FUNCT_1:34;
    end;
    suppose
A5:   n>0;
A6:   now
        let m9 be set such that
A7:     m9 in n;
        n is Subset of NAT by Th8;
        then reconsider m=m9 as Element of NAT by A7;
        m in rng f by A1,A2,A7,FUNCT_2:def 3;
        then
A8:     ex x be set st x in dom f & f.x =m by FUNCT_1:def 5;
        m in {m} by TARSKI:def 1;
        then reconsider F=f"{m} as non empty Subset of NAT by A8,FUNCT_1:def
        13;
A9:     m < k by A2,A7,NAT_1:45;
        then
A10:    m <= min* f"{m} by A1,Th18;
        n-k+m =m by A2;
        then min* f"{m}<= m by A1,A9,Th19;
        then
A11:    min* F=m by A10,XXREAL_0:1;
        min* F in F by NAT_1:def 1;
        then f.m in {m} by A11,FUNCT_1:def 13;
        hence f.m9=m9 by TARSKI:def 1;
      end;
      dom f = n by A2,A5,FUNCT_2:def 1;
      hence thesis by A6,FUNCT_1:34;
    end;
  end;
  hence thesis;
end;

theorem
  f = id n & n > 0 implies f is "increasing
proof
  assume that
A1: f = id n and
A2: n>0;
A3: ex x be set st x in n by A2,XBOOLE_0:def 1;
A4: now
    let l,m such that
A5: l in rng f and
A6: m in rng f and
A7: l<m;
A8: ex x be set st f"{l} = {x} by A1,A5,FUNCT_1:144;
A9: l in {l} by TARSKI:def 1;
    consider l9 be set such that
A10: l9 in dom f and
A11: f.l9=l by A5,FUNCT_1:def 5;
    l = l9 by A1,A10,A11,FUNCT_1:35;
    then l in f"{l} by A10,A11,A9,FUNCT_1:def 13;
    then f"{l}={l} & l in NAT by A8,TARSKI:def 1;
    then
A12: min* f"{l} = l by Th5;
A13: m in {m} by TARSKI:def 1;
A14: ex x be set st f"{m} = {x} by A1,A6,FUNCT_1:144;
    consider m9 be set such that
A15: m9 in dom f and
A16: f.m9=m by A6,FUNCT_1:def 5;
    m = m9 by A1,A15,A16,FUNCT_1:35;
    then m in f"{m} by A15,A16,A13,FUNCT_1:def 13;
    then f"{m}={m} & m in NAT by A14,TARSKI:def 1;
    hence min* f"{l} < min* f"{m} by A7,A12,Th5;
  end;
  rng f=n by A1,RELAT_1:71;
  hence thesis by A3,A4,Def1;
end;

theorem
  (n=0 iff k=0) implies ex f be Function of n,k st f is "increasing
proof
  assume
A1: n=0 iff k=0;
  now
    per cases;
    suppose
A2:   n=0;
      set f={};
A3:   dom f = n by A2;
      rng f= k by A1,A2;
      then reconsider f as Function of n,k by A3,FUNCT_2:3;
      f is "increasing by A1,Th15;
      hence thesis;
    end;
    suppose
      n>0;
      then consider f be Function such that
A4:   dom f=n and
A5:   rng f={0} by FUNCT_1:15;
      reconsider f as Function of n,{0} by A4,A5,FUNCT_2:3;
      rng f c= k
      proof
        let x be set such that
A7:     x in rng f;
        x=0 by A7,TARSKI:def 1;
        hence thesis by A1,A7,NAT_1:45;
      end;
      then reconsider f as Function of n,k by FUNCT_2:8;
   for l,m st l in rng f & m in rng f & l < m holds min* f"{l} < min*
      f"{m} by A5,TARSKI:def 1;
      then f is "increasing by A1,Def1;
      hence thesis;
    end;
  end;
  hence thesis;
end;

theorem Th23:
  (n=0 iff k=0) & n>=k implies ex f be Function of n,k st f is onto "increasing
proof
  assume that
A1: n=0 iff k=0 and
A2: n>=k;
  now
    per cases;
    suppose
A3:   n=0;
      set f={};
A4:   dom f = n by A3;
      rng f= k by A1,A3;
      then reconsider f as Function of n,k by A4,FUNCT_2:3;
      f is onto "increasing by A1,Th15;
      hence thesis;
    end;
    suppose
A5:   n>0;
      then reconsider k1=k-1 as Element of NAT by A1,NAT_1:20;
      reconsider k,n as non empty Element of NAT by A1,A5,ORDINAL1:def 13;
      defpred F[Element of n,Element of k] means $2=min($1,k1);
A6:   for x be Element of n ex y be Element of k st F[x,y]
      proof
        let x be Element of n;
A7:     k1<k1+1 by NAT_1:13;
        min(x,k1)<=k1 by XXREAL_0:17;
        then min(x,k1)<k by A7,XXREAL_0:2;
        then min(x,k1) in k by NAT_1:45;
        hence thesis;
      end;
      consider f be Function of n,k such that
A8:   for m be Element of n holds F[m,f.m] from FUNCT_2:sch 3(A6);
      now
        let m9 be set such that
A9:     m9 in k;
        k is Subset of NAT by Th8;
        then reconsider m=m9 as Element of NAT by A9;
A10:    m<k1+1 by A9,NAT_1:45;
        then m <n by A2,XXREAL_0:2;
        then
A11:    m in n by NAT_1:45;
        then
A12:    m in dom f by FUNCT_2:def 1;
        m<=k1 by A10,NAT_1:13;
        then min(m,k1)=m by XXREAL_0:def 9;
        then f.m=m by A8,A11;
        hence m9 in rng f by A12,FUNCT_1:def 5;
      end;
      then k c= rng f by TARSKI:def 3;
      then k=rng f by XBOOLE_0:def 10;
      then
A13:  f is onto by FUNCT_2:def 3;
A14:  for m st m in rng f holds min* f"{m} =m
      proof
        let m;
        assume m in rng f;
        then
A15:    m<k1+1 by NAT_1:45;
        then
A16:    m<=k1 by NAT_1:13;
        m <n by A2,A15,XXREAL_0:2;
        then
A17:    m in n by NAT_1:45;
        then
A18:    m in dom f by FUNCT_2:def 1;
        m<=k1 by A15,NAT_1:13;
        then min(m,k1)=m by XXREAL_0:def 9;
        then f.m=m by A8,A17;
        then
A19:    f.m in {m} by TARSKI:def 1;
        then
A20:    m in f"{m} by A18,FUNCT_1:def 13;
A21:    f"{m} is non empty by A19,A18,FUNCT_1:def 13;
        now
          per cases by A16,XXREAL_0:1;
          suppose
A22:        m<k1;
            now
A23:          n is Subset of NAT by Th8;
              let l9 be set such that
A24:          l9 in f"{m};
              l9 in dom f by A24,FUNCT_1:def 13;
              then l9 in n;
              then reconsider l=l9 as Element of NAT by A23;
              f.l in {m} by A24,FUNCT_1:def 13;
              then
A25:          f.l=m by TARSKI:def 1;
              l in dom f by A24,FUNCT_1:def 13;
              then min(l,k1)=m by A8,A25;
              then l=m by A22,XXREAL_0:15;
              hence l9 in {m} by TARSKI:def 1;
            end;
            then
A26:        f"{m} c= {m} by TARSKI:def 3;
            min* f"{m} in f"{m} by A21,NAT_1:def 1;
            hence thesis by A26,TARSKI:def 1;
          end;
          suppose
A27:        m=k1;
            for l be Nat st l in f"{m} holds m <= l
            proof
              let l be Nat such that
A28:          l in f"{m};
              f.l in {m} by A28,FUNCT_1:def 13;
              then
A29:          f.l=m by TARSKI:def 1;
              l in dom f by A28,FUNCT_1:def 13;
              then f.l=min(l,m) by A8,A27;
              hence thesis by A29,XXREAL_0:def 9;
            end;
            hence thesis by A20,NAT_1:def 1;
          end;
        end;
        hence thesis;
      end;
      now
        let l,m such that
A30:    l in rng f and
A31:    m in rng f and
A32:    l < m;
        min* f"{l}=l by A14,A30;
        hence min* f"{l} < min* f"{m} by A14,A31,A32;
      end;
      then f is "increasing by Def1;
      hence thesis by A13;
    end;
  end;
  hence thesis;
end;

scheme
  Sch1{n,k() -> Nat,P[set]}: {f where f is Function of n(),k():P[f]} is finite
proof
  set F= {f where f is Function of n(),k():P[f]};
  now
    per cases;
    suppose
A1:   k()=0;
      F c= {{}}
      proof
        let x be set;
        assume x in F;
        then consider f be Function of n(),k() such that
A2:     x=f and
        P[f];
        f ={} by A1;
        hence thesis by A2,TARSKI:def 1;
      end;
      hence thesis;
    end;
    suppose
A3:   k()>0;
A4:   F c= Funcs(n(),k())
      proof
        let x be set;
        assume x in F;
        then ex f be Function of n(),k() st x=f & P[f];
        hence thesis by A3,FUNCT_2:11;
      end;
      Funcs(n(),k()) is finite by FRAENKEL:16;
      hence thesis by A4;
    end;
  end;
  hence thesis;
end;

theorem Th24:
  for n,k holds {f where f is Function of n,k:f is onto "increasing} is finite
proof
  let n,k;
  defpred P[Function of n,k] means $1 is onto "increasing;
  {f where f is Function of n,k:P[f]} is finite from Sch1;
  hence thesis;
end;

theorem Th25:
  for n,k holds card {f where f is Function of n,k:f is  onto "increasing}
    is Element of NAT
proof
  let n,k;
  set F= {f where f is Function of n,k:f is onto "increasing};
  consider m being Nat such that
A1: F,m are_equipotent by Th24,CARD_1:74;
  card F=card m by A1,CARD_1:21;
  hence thesis;
end;

:: Stirling Numbers of the second kind

definition
  let n,k be Nat;
  func n block k -> Element of NAT equals
  card {f where f is Function of n,k:f is onto "increasing};
  coherence by Th25;
end;

theorem Th26:
  n block n = 1
proof
  set F={f where f is Function of n,n:f is onto "increasing};
A1: F c= {id n}
  proof
    let x be set;
    assume x in F;
    then consider f be Function of n,n such that
A2: x=f and
A3: f is onto "increasing;
    f=id n by A3,Th20;
    hence thesis by A2,TARSKI:def 1;
  end;
  n=0 iff n=0;
  then consider f be Function of n,n such that
A4: f is onto "increasing by Th23;
  f in F by A4;
  then F={id n} by A1,ZFMISC_1:39;
  hence thesis by CARD_1:50;
end;

theorem Th27:
  k<>0 implies 0 block k = 0
proof
  set F={f where f is Function of 0,k:f is onto "increasing};
  assume
A1: k<>0;
  F ={}
  proof
    assume F<>{};
    then consider x be set such that
A2: x in F by XBOOLE_0:def 1;
    ex f be Function of 0,k st x=f & f is onto "increasing by A2;
    hence thesis by A1,Def1;
  end;
  hence thesis;
end;

theorem
  0 block k = 1 iff k=0 by Th26,Th27;

theorem Th29:
  n<k implies n block k = 0
proof
  set F={f where f is Function of n,k:f is onto "increasing};
  assume
A1: n<k;
  F ={}
  proof
    assume F<>{};
    then consider x be set such that
A2: x in F by XBOOLE_0:def 1;
    ex f be Function of n,k st x=f & f is onto "increasing by A2;
    hence contradiction by A1,Th17;
  end;
  hence thesis;
end;

theorem
  n block 0 = 1 iff n=0
proof
  n block 0 = 1 implies n=0
  proof
    set F={f where f is Function of n,0:f is onto "increasing};
A1: card {{}}= 1 by CARD_1:50;
    assume n block 0=1;
    then consider x be set such that
A2: F={x} by A1,CARD_1:49;
    x in F by A2,TARSKI:def 1;
    then ex f be Function of n,0 st x=f & f is onto "increasing;
    hence thesis by Def1;
  end;
  hence thesis by Th26;
end;

theorem Th31:
  n<>0 implies n block 0 = 0
proof
  set F={f where f is Function of n,0:f is onto "increasing};
  assume
A1: n<>0;
  F ={}
  proof
    assume F<>{};
    then consider x be set such that
A2: x in F by XBOOLE_0:def 1;
    ex f be Function of n,0 st x=f & f is onto "increasing by A2;
    hence contradiction by A1,Def1;
  end;
  hence thesis;
end;

theorem Th32:
  n<>0 implies n block 1 = 1
proof
  set F={g where g is Function of n,1:g is onto "increasing};
  assume n<>0;
  then n >=1+0 by NAT_1:13;
  then consider f be Function of n,1 such that
A1: f is onto "increasing by Th23;
A2: F c= {f}
  proof
    let x be set;
    assume x in F;
    then consider g be Function of n,1 such that
A3: x=g and
    g is onto "increasing;
    f=g by CARD_1:87,FUNCT_2:66;
    hence thesis by A3,TARSKI:def 1;
  end;
  f in F by A1;
  then F={f} by A2,ZFMISC_1:39;
  hence thesis by CARD_1:50;
end;

theorem
  1<=k & k <=n or k=n iff n block k > 0
proof
  thus 1<=k & k <=n or k=n implies n block k > 0
  proof
    set F={g where g is Function of n,k:g is onto "increasing};
    assume that
A1: 1<=k & k <=n or k=n;
    k=0 iff n=0 by A1;
    then consider f such that
A2: f is onto "increasing by A1,Th23;
    f in F by A2;
    hence thesis;
  end;
  thus n block k > 0 implies 1<=k & k <=n or k=n
  proof
    assume
A3: n block k >0;
    assume
A4: not ( 1<=k & k <=n or k=n);
    then 1+0>k or k>n;
    then k=0 & n<>k or k>n by A4,NAT_1:13;
    hence contradiction by A3,Th29,Th31;
  end;
end;

reserve x,y for set;

scheme
  Sch2{X,Y,X1,Y1()->set,f()->Function of X(),Y(),F(set)->set}: ex h be
Function of X1(),Y1() st h|X() = f() & for x st x in X1()\X() holds h.x = F(x)
provided
A1: for x st x in X1()\X() holds F(x) in Y1() and
A2: X() c= X1() & Y() c= Y1() and
A3: Y() is empty implies X() is empty
proof
  defpred P[set,set] means ($1 in X() implies $2 = f().$1) & ($1 in X1()\X()
  implies $2=F($1));
A4: for x st x in X1() ex y st y in Y1() & P[x,y]
  proof
    let x such that
A5: x in X1();
    now
      per cases;
      suppose
A6:     x in X();
        then x in dom f() by A3,FUNCT_2:def 1;
        then f().x in rng f() by FUNCT_1:def 5;
        then
A7:     f().x in Y1() by A2,TARSKI:def 3;
        not x in X1()\X() by A6,XBOOLE_0:def 5;
        hence thesis by A7;
      end;
      suppose
A8:     not x in X();
        then x in X1()\X() by A5,XBOOLE_0:def 5;
        then F(x) in Y1() by A1;
        hence thesis by A8;
      end;
    end;
    hence thesis;
  end;
  consider h be Function of X1(),Y1() such that
A9: for x st x in X1() holds P[x,h.x] from FUNCT_2:sch 1(A4);
A10: dom f() = dom h /\ X()
  proof
    now
      per cases;
      suppose
        Y() is empty;
        hence thesis by A3;
      end;
      suppose
A11:    Y() is non empty;
        then Y1() is non empty by A2;
        then
A12:    dom h =X1() by FUNCT_2:def 1;
        dom f()=X() by A11,FUNCT_2:def 1;
        hence thesis by A2,A12,XBOOLE_1:28;
      end;
    end;
    hence thesis;
  end;
  take h;
  for x st x in dom f() holds h.x = f().x
  proof
    let x;
    assume x in dom f();
    then x in X();
    hence thesis by A2,A9;
  end;
  hence h|X() = f() by A10,FUNCT_1:68;
  thus thesis by A9;
end;

scheme
  Sch3{X,Y,X1,Y1()->set,F(set)->set,P[set,set,set]}: card{f where f is
Function of X(),Y(): P[f,X(),Y()]}= card{f where f is Function of X1(),Y1(): P[
  f,X1(),Y1()]& rng (f|X()) c=Y()& (for x st x in X1()\X() holds f.x=F(x))}
provided
A1: for x st x in X1()\X() holds F(x) in Y1() and
A2: X() c= X1() & Y() c= Y1() and
A3: Y() is empty implies X() is empty and
A4: for f be Function of X1(),Y1() st (for x st x in X1()\X() holds F(x)
=f.x) holds P[f,X1(),Y1()] iff P[f|X(),X(),Y()]
proof
  defpred FF[set,set] means for f be Function of X(),Y(),h be Function of X1()
  ,Y1() st f=$1 & h=$2 holds h|X()=f;
  set F2={f where f is Function of X1(),Y1(): P[f,X1(),Y1()]& rng (f|X()) c=Y(
  )& for x st x in X1()\X() holds f.x=F(x)};
  set F1={f where f is Function of X(),Y(): P[f,X(),Y()]};
A5: for f9 be set st f9 in F1 ex g9 be set st g9 in F2 & FF[f9,g9]
  proof
    let f9 be set;
    assume f9 in F1;
    then consider f be Function of X(),Y() such that
A6: f=f9 and
A7: P[f,X(),Y()];
    consider h be Function of X1(),Y1() such that
A8: h|X() = f & for x st x in X1()\X() holds h.x=F(x) from Sch2(A1,A2
    , A3);
A9: FF[f9,h] by A6,A8;
A10: rng f c= Y();
    P[h,X1(),Y1()] by A4,A7,A8;
    then h in F2 by A8,A10;
    hence thesis by A9;
  end;
  consider ff be Function of F1,F2 such that
A11: for x be set st x in F1 holds FF[x,ff.x] from FUNCT_2:sch 1(A5);
A12: Y1() is empty implies X1() is empty
  proof
    assume
A13: Y1() is empty;
    assume X1() is non empty;
    then ex x st x in X1() by XBOOLE_0:def 1;
    hence contradiction by A1,A2,A3,A13;
  end;
  now
    per cases;
    suppose
A14:  Y1() is empty;
      set Empty={};
A15:  F2 c= {{}}
      proof
        let x;
        assume x in F2;
        then
        ex f be Function of X1(),Y1() st f=x &( P[f,X1(),Y1()])& rng (f
        |X()) c=Y() & for x st x in X1()\X() holds f.x=F(x);
        then x={} by A14;
        hence thesis by TARSKI:def 1;
      end;
A16:  F1 c= {{}}
      proof
        let x;
        assume x in F1;
        then ex f be Function of X(),Y() st f=x & P[f,X(),Y()];
        then x={} by A2,A14;
        hence thesis by TARSKI:def 1;
      end;
      now
        per cases;
        suppose
A17:      P[{},{},{}];
A18:      rng {}={};
A19:      Y() is empty by A2,A14;
          dom {}={};
          then {} is Function of X(),Y() by A3,A19,A18,FUNCT_2:3;
          then {} in F1 by A3,A17,A19;
          then
A20:      F1={{}} by A16,ZFMISC_1:39;
A21:      rng {}={};
          dom {}={};
          then reconsider Empty as Function of X1(),Y1() by A12,A14,
          A21,FUNCT_2:
          3;
A22:      for x st x in X1()\X() holds Empty.x=F(x) by A12;
          rng (Empty|X()) c= Y() by A2,A14;
          then {} in F2 by A12,A14,A17,A22;
          hence thesis by A15,A20,ZFMISC_1:39;
        end;
        suppose
A23:      not P[{},{},{}];
A24:      not {} in F1
          proof
            assume {} in F1;
            then ex f be Function of X(),Y() st f={} & P[f,X(),Y()];
            hence contradiction by A2,A3,A14,A23;
          end;
A25:      F2={} or F2={{}} by A15,ZFMISC_1:39;
A26:      not {} in F2
          proof
            assume {} in F2;
            then ex f be Function of X1(),Y1() st f={} & P[f,X1(),Y1()] & rng
            (f|X()) c= Y() & for x st x in X1()\X() holds f.x=F(x);
            hence contradiction by A12,A14,A23;
          end;
          F1={} or F1={{}} by A16,ZFMISC_1:39;
          hence thesis by A24,A26,A25,TARSKI:def 1;
        end;
      end;
      hence thesis;
    end;
    suppose
A27:  Y1() is non empty;
      now
        per cases;
        suppose
A28:      F2 is empty;
          F1 is empty
          proof
            assume F1 is non empty;
            then consider x such that
A29:        x in F1 by XBOOLE_0:def 1;
            consider f be Function of X(),Y() such that
            f=x and
A30:        P[f,X(),Y()] by A29;
A31:        rng f c= Y();
            consider h be Function of X1(),Y1() such that
A32:        h|X() = f & for x st x in X1()\X() holds h.x=F(x) from
            Sch2(A1,A2,A3);
            P[h,X1(),Y1()] by A4,A30,A32;
            then h in F2 by A32,A31;
            hence thesis by A28;
          end;
          hence thesis by A28;
        end;
        suppose
A33:      F2 is non empty;
          F2 c= rng ff
          proof
            let y;
            assume y in F2;
            then consider f be Function of X1(),Y1() such that
A34:        f=y and
A35:        P[f,X1(),Y1()] and
A36:        rng (f|X()) c=Y() and
A37:        for x st x in X1()\X() holds f.x=F(x);
A38:        dom (f|X())=dom f /\ X() by RELAT_1:90;
            dom f =X1() by A27,FUNCT_2:def 1;
            then
A39:        dom (f|X())=X() by A2,A38,XBOOLE_1:28;
            then reconsider h=f|X() as Function of X(),rng(f|X()) by
            FUNCT_2:3;
            rng (f|X()) is empty implies X() is empty by A39,RELAT_1:65;
            then reconsider h as Function of X(),Y() by A36,FUNCT_2:8;
            P[f|X(),X(),Y()] by A4,A35,A37;
            then
A40:        h in F1;
A41:        dom ff=F1 by A33,FUNCT_2:def 1;
            then ff.h in rng ff by A40,FUNCT_1:def 5;
            then ff.h in F2;
            then consider ffh be Function of X1(),Y1() such that
A42:        ffh=ff.h and
            P[ffh,X1(),Y1()] and
            rng (ffh|X()) c=Y() and
A43:        for x st x in X1()\X() holds ffh.x=F(x);
            now
              let x such that
A44:          x in X1();
              now
                per cases;
                suppose
                  x in X();
                  then
A45:              x in dom h by A3,FUNCT_2:def 1;
                  ffh|X()=h by A11,A40,A42;
                  then h.x=ffh.x by A45,FUNCT_1:70;
                  hence ffh.x=f.x by A45,FUNCT_1:70;
                end;
                suppose
                  not x in X();
                  then
A46:              x in X1()\X() by A44,XBOOLE_0:def 5;
                  then ffh.x=F(x) by A43;
                  hence ffh.x=f.x by A37,A46;
                end;
              end;
              hence ffh.x=f.x;
            end;
            then ffh=f by FUNCT_2:18;
            hence thesis by A34,A40,A41,A42,FUNCT_1:def 5;
          end;
          then
A47:      rng ff=F2 by XBOOLE_0:def 10;
          for x1,x2 be set st x1 in F1 & x2 in F1 & ff.x1=ff.x2 holds x1= x2
          proof
            let x1,x2 be set such that
A48:        x1 in F1 and
A49:        x2 in F1 and
A50:        ff.x1=ff.x2;
A51:        ex f2 be Function of X(),Y() st x2=f2 & P[f2,X(),Y()] by
            A49;
            dom ff= F1 by A33,FUNCT_2:def 1;
            then ff.x1 in rng ff by A48,FUNCT_1:def 5;
            then ff.x1 in F2;
            then consider F1 be Function of X1(),Y1() such that
A52:        ff.x1=F1 and
            P[F1,X1(),Y1()] and
            rng (F1|X()) c=Y() and
            for x st x in X1()\X() holds F1.x=F(x);
            consider f1 be Function of X(),Y() such that
A53:        x1=f1 and
            P[f1,X(),Y()] by A48;
            F1|X()=f1 by A11,A48,A53,A52;
            hence thesis by A11,A49,A50,A53,A51,A52;
            consider F2 be Function of X1(),Y1();
          end;
          then
A54:      ff is one-to-one by A33,FUNCT_2:25;
          dom ff=F1 by A33,FUNCT_2:def 1;
          then F1,F2 are_equipotent by A47,A54,WELLORD2:def 4;
          hence thesis by CARD_1:21;
        end;
      end;
      hence thesis;
    end;
  end;
  hence thesis;
end;

scheme
  Sch4{X,Y,x,y()->set,P[set,set,set]}: card{f where f is Function of X(),Y():
P[f,X(),Y()]}= card{f where f is Function of (X()\/{x()}),(Y()\/{y()}): P[f,X()
  \/{x()},Y()\/{y()}]& rng (f|X()) c=Y() & f.x()=y()}
provided
A1: Y() is empty implies X() is empty and
A2: not x() in X() and
A3: for f be Function of X()\/{x()},Y()\/{y()} st f.x()=y() holds P[f,X(
)\/{x()},Y()\/{y()}] iff P[f|X(),X(),Y()]
proof
  set Y1=Y()\/{y()};
  set X1=X()\/{x()};
  deffunc F(set)=y();
A4: for f be Function of X1,Y1 holds (for x st x in X1\X() holds F(x)=f.x)
  iff f.x()=y()
  proof
    let f be Function of X1,Y1;
A5: X1\X()={x()}\X() by XBOOLE_1:40
      .={x()} by A2,ZFMISC_1:67;
    thus (for x st x in X1\X() holds F(x)=f.x) implies f.x()=y()
    proof
A6:   x() in {x()} by TARSKI:def 1;
      assume for x st x in X1\X() holds F(x)=f.x;
      hence thesis by A5,A6;
    end;
    thus thesis by A5,TARSKI:def 1;
  end;
A7: for f be Function of X1,Y1 st (for x st x in X1\X() holds F(x)=f.x)
  holds P[f,X1,Y1] iff P[f|X(),X(),Y()]
  proof
    let f be Function of X1,Y1;
    assume for x st x in X1\X() holds F(x)=f.x;
    then y()=f.x() by A4;
    hence thesis by A3;
  end;
  set F2={f where f is Function of X1,Y1: P[f,X1,Y1]& rng (f|X()) c=Y()& f.x()
  =y()};
  set F1={f where f is Function of X1,Y1: P[f,X1,Y1]& rng (f|X()) c=Y()& for x
  st x in X1\X() holds f.x=F(x)};
A8: for x st x in X1\X() holds F(x) in Y1
  proof
    let x such that
    x in X1\X();
A9: {y()} c=Y1 by XBOOLE_1:7;
    y() in {y()} by TARSKI:def 1;
    hence thesis by A9;
  end;
A10: F1 c= F2
  proof
    let x;
    assume x in F1;
    then consider f be Function of X1,Y1 such that
A11: x=f and
A12: P[f,X1,Y1] and
A13: rng (f|X()) c=Y() and
A14: for x st x in X1\X() holds f.x=F(x);
    f.x()=y() by A4,A14;
    hence thesis by A11,A12,A13;
  end;
A15: F2 c= F1
  proof
    let x;
    assume x in F2;
    then consider f be Function of X1,Y1 such that
A16: x=f and
A17: P[f,X1,Y1] and
A18: rng (f|X()) c=Y() and
A19: f.x()=y();
    for x st x in X1\X() holds F(x)=f.x by A4,A19;
    hence thesis by A16,A17,A18;
  end;
A20: X()c=X1 & Y()c=Y1 by XBOOLE_1:7;
  card{f where f is Function of X(),Y():P[f,X(),Y()]}= card F1 from Sch3(
  A8,A20,A1,A7);
  hence thesis by A10,A15,XBOOLE_0:def 10;
end;

theorem Th34:
  for f be Function of n+1,k+1 st f is onto "increasing & f"{f.n}=
  {n} holds f.n=k
proof
  let f be Function of n+1,k+1 such that
A1: f is onto "increasing and
A2: f"{f.n}={n};
  assume
A3: f.n<>k;
  now
    per cases by A3,XXREAL_0:1;
    suppose
A4:   f.n>k;
      f.n<k+1 by NAT_1:45;
      hence contradiction by A4,NAT_1:13;
    end;
    suppose
A5:   f.n<k;
A7:   min* f"{k} <=(n+1)-1 by Th16;
A8:   rng f = k+1 by A1,FUNCT_2:def 3;
      k<k+1 by NAT_1:13;
      then
   k in rng f by A8,NAT_1:45;
       then min* f"{f.n} < min* f"{k} & k in NAT & n in NAT
  by A1,A5,A8,Def1,ORDINAL1:def 13;
      hence contradiction by A2,A7,Th5;
    end;
  end;
  hence contradiction;
end;

theorem Th35:
  for f be Function of n+1,k st k<>0 & f"{f.n}<>{n} holds ex m st
  m in f"{f.n} & m<>n
proof
  let f be Function of n+1,k such that
A1: k<>0 and
A2: f"{f.n}<>{n};
A3: n<n+1 by NAT_1:13;
A4: f.n in {f.n} by TARSKI:def 1;
  dom f =n+1 by A1,FUNCT_2:def 1;
  then n in dom f by A3,NAT_1:45;
  then n in f"{f.n} by A4,FUNCT_1:def 13;
  then ex m be set st m in f"{f.n} & m<>n by A2,ZFMISC_1:41;
  hence thesis;
end;

theorem Th36:
  for f be Function of n,k, g be Function of n+m,k+l st g is
"increasing & f=g|n holds for i,j st i in rng f & j in rng f & i<j holds min* f
  "{i} < min* f"{j}
proof
  let f be Function of n,k, g be Function of n+m,k+l such that
A1: g is "increasing and
A2: f=g|n;
  let i,j such that
A3: i in rng f and
A4: j in rng f and
A5: i<j;
A6: for k1 be Element of NAT st k1 in rng f holds k1 in rng g& min* f"{k1}=
  min* g"{k1}
  proof
A7: n is Subset of NAT by Th8;
    let k1 be Element of NAT such that
A8: k1 in rng f;
    consider x be set such that
A9: x in dom f and
A10: f.x=k1 by A8,FUNCT_1:def 5;
A11: dom f = n by A8,FUNCT_2:def 1;
    x in n by A9;
    then reconsider x9=x as Element of NAT by A7;
A12: x9 <n by A9,NAT_1:45;
A13: f.x9=g.x9 by A2,A9,FUNCT_1:70;
A14: dom g =n+m by A8,FUNCT_2:def 1;
    n <= n+m by NAT_1:11;
    then
A15: n c= n+m by NAT_1:40;
A16: now
      let n1 be Nat such that
A17:  n1 in f"{k1};
A18:  n1 in n by A11,A17,FUNCT_1:def 13;
      f.n1 in {k1} by A17,FUNCT_1:def 13;
      then g.n1 in {k1} by A2,A11,A18,FUNCT_1:70;
      then n1 in g"{k1} by A14,A15,A18,FUNCT_1:def 13;
      hence min* g"{k1}<=n1 by NAT_1:def 1;
    end;
    k1 in {k1} by TARSKI:def 1;
    then
A19: x9 in g"{k1} by A9,A10,A11,A14,A15,A13,FUNCT_1:def 13;
    then min* g"{k1} <= x9 by NAT_1:def 1;
    then min* g"{k1} < n by A12,XXREAL_0:2;
    then
A20: min* g"{k1} in dom f by A11,NAT_1:45;
    min* g"{k1} in g"{k1} by A19,NAT_1:def 1;
    then g.(min* g"{k1}) in {k1} by FUNCT_1:def 13;
    then f.(min* g"{k1}) in {k1} by A2,A20,FUNCT_1:70;
    then min* g"{k1} in f"{k1} by A20,FUNCT_1:def 13;
    hence thesis by A9,A10,A11,A14,A15,A13,A16,FUNCT_1:def 5,NAT_1:def 1;
  end;
D:  i in NAT & j in NAT by ORDINAL1:def 13;
then A21: j in rng g by A4,A6;
A22: min* f"{j}=min* g"{ j} by A4,A6,D;
A23: min* f"{i}=min* g"{i} by A3,A6,D;
  i in rng g by A3,A6,D;
  hence thesis by A1,A5,A21,A23,A22,Def1;
end;

theorem Th37:
  for f be Function of n+1,k+1 st f is onto "increasing & f"{f.n}=
  {n} holds rng (f|n) c= k & for g be Function of n,k st g=f|n holds g is onto
  "increasing
proof
  let f be Function of n+1,k+1 such that
A1: f is onto "increasing and
A2: f"{f.n}={n};
  now
    per cases;
    suppose
A3:   n=0;
      then 0+1>=k+1 by A1,Th17;
      then k=0 by XREAL_1:8;
      hence thesis by A3,Th15;
    end;
    suppose
A4:   n>0;
      thus
A5:   rng (f|n) c= k
      proof
        let fi be set such that
A6:     fi in rng (f|n);
A7:     fi in k+1 by A6;
        k+1 is Subset of NAT by Th8;
        then reconsider fi as Element of NAT by A7;
        consider i be set such that
A8:     i in dom (f|n) and
A9:     (f|n).i=fi by A6,FUNCT_1:def 5;
        i in dom f /\n by A8,RELAT_1:90;
        then
A10:    i in n by XBOOLE_0:def 4;
        n is Subset of NAT by Th8;
        then reconsider i as Element of NAT by A10;
A11:    f.i<k
        proof
          f.i<k+1 by NAT_1:45;
          then
A12:      f.i<=k by NAT_1:13;
          assume f.i>=k;
          then
A13:      f.i=k by A12,XXREAL_0:1;
A14:      f.i in {f.i} by TARSKI:def 1;
A15:      f.n=k by A1,A2,Th34;
A16:      i in dom f/\n by A8,RELAT_1:90;
          then i in dom f by XBOOLE_0:def 4;
          then i in f"{f.n} by A13,A14,A15,FUNCT_1:def 13;
          then i >= min* f"{f.n} & i in NAT & n in NAT
            by NAT_1:def 1,ORDINAL1:def 13;
          then
A17:      i >= n by A2,Th5;
          i in n by A16,XBOOLE_0:def 4;
          hence contradiction by A17,NAT_1:45;
        end;
        f.i=(f|n).i by A8,FUNCT_1:70;
        hence thesis by A9,A11,NAT_1:45;
      end;
      thus for g be Function of n,k st g=f|n holds g is onto "increasing
      proof
        let g be Function of n,k such that
A18:    g=f|n;
        k c=rng g
        proof
          n<n+1 by NAT_1:13;
          then
A19:      n c= n+1 by NAT_1:40;
          dom f=n+1 by FUNCT_2:def 1;
          then
A20:      n=dom f /\n by A19,XBOOLE_1:28;
          let k1 be set such that
A21:      k1 in k;
          k is Subset of NAT by Th8;
          then reconsider k9=k1 as Element of NAT by A21;
          k9<k by A21,NAT_1:45;
          then k9 < k+1 by NAT_1:13;
          then
A22:      k9 in k+1 by NAT_1:45;
A23:      dom f=n+1 by FUNCT_2:def 1;
          rng f = k+1 by A1,FUNCT_2:def 3;
          then consider n1 be set such that
A24:      n1 in dom f and
A25:      f.n1=k9 by A22,FUNCT_1:def 5;
          n+1 is Subset of NAT by Th8;
          then reconsider n1 as Element of NAT by A24,A23;
          n1<n+1 by A24,NAT_1:45;
          then
A26:      n1<=n by NAT_1:13;
          f.n=k by A1,A2,Th34;
          then n1<>n by A21,A25;
          then
A27:      n1 < n by A26,XXREAL_0:1;
          dom f /\n=dom (f|n) by RELAT_1:90;
          then
A28:      n1 in dom g by A18,A27,A20,NAT_1:45;
          then g.n1 in rng g by FUNCT_1:def 5;
          hence thesis by A18,A25,A28,FUNCT_1:70;
        end;
        then k=rng g by XBOOLE_0:def 10;
        hence g is onto by FUNCT_2:def 3;
A29:    dom f /\n=dom (f|n) by RELAT_1:90;
        n<n+1 by NAT_1:13;
        then
A30:    n c= n+1 by NAT_1:40;
        dom f=n+1 by FUNCT_2:def 1;
        then
A31:    n=dom f /\n by A30,XBOOLE_1:28;
        0 in n by A4,NAT_1:45;
        then (f|n).0 in rng (f|n) by A31,A29,FUNCT_1:def 5;
        then
A32:    n=0 iff k=0 by A5;
        for i,j st i in rng g & j in rng g & i<j holds min* g"{i} < min*
        g"{j} by A1,A18,Th36;
        hence g is "increasing by A32,Def1;
      end;
    end;
  end;
  hence thesis;
end;

theorem Th38:
  for f be Function of n+1,k,g be Function of n,k st f is onto
  "increasing & f"{f.n}<>{n} & f|n =g holds g is onto "increasing
proof
  let f be Function of n+1,k, g be Function of n,k such that
A1: f is onto "increasing and
A2: f"{f.n}<>{n} and
A3: f|n =g;
  now
    per cases;
    suppose
      k=0;
      hence thesis by A1,Def1;
    end;
    suppose
A4:   k>0;
A6:   rng f = k by A1,FUNCT_2:def 3;
      now
          k=k+0;
          then
A10:      for i,j st i in rng g & j in rng g & i<j holds min* g"{i} <
          min* g"{j} by A1,A3,Th36;
A11:      k c= rng g
          proof
A12:        n+1 is Subset of NAT by Th8;
            let k1 be set such that
A13:        k1 in k;
            consider x be set such that
A14:        x in dom f and
A15:        f.x=k1 by A6,A13,FUNCT_1:def 5;
            dom f =n+1 by A13,FUNCT_2:def 1;
            then reconsider x as Element of NAT by A14,A12;
            x<n+1 by A14,NAT_1:45;
            then
A16:        x<=n by NAT_1:13;
            now
              per cases by A16,XXREAL_0:1;
              suppose
A17:            x<n;
A18:            dom g = n by A4,FUNCT_2:def 1;
A19:            x in n by A17,NAT_1:45;
                then g.x=f.x by A3,A18,FUNCT_1:70;
                hence thesis by A15,A19,A18,FUNCT_1:def 5;
              end;
              suppose
                x=n;
                then consider m such that
A20:            m in f"{k1} and
A21:            m<>n by A2,A4,A15,Th35;
                f.m in {k1} by A20,FUNCT_1:def 13;
                then
A22:            f.m=k1 by TARSKI:def 1;
                m in dom f by A20,FUNCT_1:def 13;
                then m<n+1 by NAT_1:45;
                then m<=n by NAT_1:13;
                then m < n by A21,XXREAL_0:1;
                then
A23:            m in n by NAT_1:45;
A24:            n=dom g by A4,FUNCT_2:def 1;
                then g.m=f.m by A3,A23,FUNCT_1:70;
                hence thesis by A23,A24,A22,FUNCT_1:def 5;
              end;
            end;
            hence thesis;
          end;
          then
A25:      rng g =k by XBOOLE_0:def 10;
          n=0 iff k=0 by A4,A11;
          hence thesis by A25,A10,Def1,FUNCT_2:def 3;
        end;
      hence thesis;
    end;
  end;
  hence thesis;
end;

theorem Th39:
  for f be Function of n,k, g be Function of n+1,k+m st f is onto
"increasing & f=g|n holds for i,j st i in rng g & j in rng g & i<j holds min* g
  "{i} < min* g"{j}
proof
  let f be Function of n,k, g be Function of n+1,k+m such that
A1: f is onto "increasing and
A2: f=g|n;
A3: for i st i<n holds f.i=g.i
  proof
    k=0 iff n=0 by A1,Def1;
    then
A4: dom f=n by FUNCT_2:def 1;
    let i;
    assume i <n;
    then i in n by NAT_1:45;
    hence thesis by A2,A4,FUNCT_1:70;
  end;
A5: for l st l in rng g & not l in rng f holds l=g.n
  proof
A6: n+1 is Subset of NAT by Th8;
    let l such that
A7: l in rng g and
A8: not l in rng f;
    consider x be set such that
A9: x in dom g and
A10: g.x=l by A7,FUNCT_1:def 5;
    assume
A11: l<>g.n;
    dom g=n+1 by A7,FUNCT_2:def 1;
    then reconsider x as Element of NAT by A9,A6;
    x<n+1 by A9,NAT_1:45;
    then x<=n by NAT_1:13;
    then
A12: x<n by A11,A10,XXREAL_0:1;
    then
A13: x in n by NAT_1:45;
    k<>0 by A1,A12,Def1;
    then
A14: dom f=n by FUNCT_2:def 1;
    f.x=g.x by A3,A12;
    hence contradiction by A8,A10,A14,A13,FUNCT_1:def 5;
  end;
A15: for l st l in rng g & not l in rng f holds min* g"{l}=n
  proof
A16: n <n+1 by NAT_1:13;
    let l such that
A17: l in rng g and
A18: not l in rng f;
A19: l in {l} by TARSKI:def 1;
    dom g=n+1 by A17,FUNCT_2:def 1;
    then
A20: n in dom g by A16,NAT_1:45;
    g.n=l by A5,A17,A18;
    then n in g"{l} by A20,A19,FUNCT_1:def 13;
    then min* g"{l} in g"{l} by NAT_1:def 1;
    then
A21: g.min* g"{l} in {l} by FUNCT_1:def 13;
    assume
A22: min* g"{l}<>n;
    min* g"{l} <= (n+1)-1 by Th16;
    then
A23: min* g"{l}< n by A22,XXREAL_0:1;
    then k<>0 by A1,Def1;
    then
A24: dom f=n by FUNCT_2:def 1;
    min* g"{l} in n by A23,NAT_1:45;
    then
A25: f.min* g"{l} in rng f by A24,FUNCT_1:def 5;
    f.min* g"{l}=g.min* g"{l} by A3,A23;
    hence contradiction by A18,A21,A25,TARSKI:def 1;
  end;
A26: for k1 be Element of NAT st k1 in rng f holds min* g"{k1}=min* f"{k1}
  proof
    n <= n+1 by NAT_1:11;
    then
A27: n c= n+1 by NAT_1:40;
    let k1 be Element of NAT such that
A28: k1 in rng f;
    consider x be set such that
A29: x in dom f and
A30: f.x=k1 by A28,FUNCT_1:def 5;
A31: x in n by A29;
A32: dom g =n+1 by A28,FUNCT_2:def 1;
    n is Subset of NAT by Th8;
    then reconsider x as Element of NAT by A31;
    k1 in {k1} by TARSKI:def 1;
    then
A33: x in f"{k1} by A29,A30,FUNCT_1:def 13;
    then
A34: min* f"{k1} <= x by NAT_1:def 1;
A35: x <n by A29,NAT_1:45;
    then
A36: min* f"{k1} < n by A34,XXREAL_0:2;
A37: dom f = n by A28,FUNCT_2:def 1;
A38: now
      let n1 be Nat such that
A39:  n1 in g"{k1};
      n1 in n+1 by A32,A39,FUNCT_1:def 13;
      then n1<n+1 by NAT_1:45;
      then
A40:  n1<=n by NAT_1:13;
      now
        per cases by A40,XXREAL_0:1;
        suppose
A41:      n1<n;
          g.n1 in {k1} by A39,FUNCT_1:def 13;
          then
A42:      f.n1 in {k1} by A3,A41;
          n1 in dom f by A37,A41,NAT_1:45;
          then n1 in f"{k1} by A42,FUNCT_1:def 13;
          hence min* f"{k1}<=n1 by NAT_1:def 1;
        end;
        suppose
          n1=n;
          hence min* f"{k1} <= n1 by A34,A35,XXREAL_0:2;
        end;
      end;
      hence min* f"{k1} <= n1;
    end;
    min* f"{k1} in f"{k1} by A33,NAT_1:def 1;
    then f.(min* f"{k1}) in {k1} by FUNCT_1:def 13;
    then
A43: g.(min* f"{k1}) in {k1} by A3,A36;
    min* f"{k1} in n by A36,NAT_1:45;
    then min* f"{k1} in g"{k1} by A32,A27,A43,FUNCT_1:def 13;
    hence thesis by A38,NAT_1:def 1;
  end;
  let i,j such that
A44: i in rng g and
A45: j in rng g and
A46: i<j;
A47: for l st l in rng g & not l in rng f holds l>=k
  proof
    let l such that
    l in rng g and
A48: not l in rng f;
    assume l<k;
    then l in k by NAT_1:45;
    hence thesis by A1,A48,FUNCT_2:def 3;
  end;
F:i in NAT & j in NAT by ORDINAL1:def 13;
  now
    per cases;
    suppose
A49:  i in rng f & j in rng f;
      then
A50:  min* g"{j}=min* f"{j} by A26,F;
      min* g"{i}=min* f"{i} by A26,A49,F;
      hence thesis by A1,A46,A49,A50,Def1;
    end;
    suppose
A51:  i in rng f & not j in rng f;
      then
A52:  n<>0;
      then min* f"{i} <= n-1 by Th16;
      then
A53:  min* g"{i} <=n-1 by A26,A51,F;
      n-1 is Element of NAT by A52,NAT_1:20;
      then
A54:  (n-1)<(n-1)+1 by NAT_1:13;
      min* g"{j}=n by A45,A15,A51;
      hence thesis by A53,A54,XXREAL_0:2;
    end;
    suppose
A55:  not i in rng f & j in rng f;
      then
A56:  j< k by NAT_1:45;
      i>=k by A44,A47,A55;
      hence thesis by A46,A56,XXREAL_0:2;
    end;
    suppose
A57:  not i in rng f & not j in rng f;
      then i=g.n by A44,A5;
      hence thesis by A45,A46,A5,A57;
    end;
  end;
  hence thesis;
end;

theorem Th40:
  for f be Function of n,k, g be Function of n+1,k+1 st f is onto
  "increasing & f=g|n & g.n = k holds g is onto "increasing & g"{g.n}={n}
proof
  let f be Function of n,k, g be Function of n+1,k+1 such that
A1: f is onto "increasing and
A2: f=g|n and
A3: g.n = k;
  k+1 c= rng g
  proof
    let x9 be set such that
A4: x9 in k+1;
    k+1 is Subset of NAT by Th8;
    then reconsider x=x9 as Element of NAT by A4;
    x<k+1 by A4,NAT_1:45;
    then
A5: x<=k by NAT_1:13;
    now
      per cases by A5,XXREAL_0:1;
      suppose
A6:     x<k;
A7:     rng f=k by A1,FUNCT_2:def 3;
        x in k by A6,NAT_1:45;
        then consider y be set such that
A8:     y in dom f and
A9:     f.y =x by A7,FUNCT_1:def 5;
A10:    dom g = n+1 by FUNCT_2:def 1;
        n=0 iff k=0 by A1,Def1;
        then
A11:    dom f = n by FUNCT_2:def 1;
        n<=n+1 by NAT_1:11;
        then
A12:    n c= n+1 by NAT_1:40;
        f.y=g.y by A2,A8,FUNCT_1:70;
        hence thesis by A8,A9,A11,A12,A10,FUNCT_1:def 5;
      end;
      suppose
A13:    x=k;
        n<n+1 by NAT_1:13;
        then
A14:    n in n+1 by NAT_1:45;
        dom g =n+1 by FUNCT_2:def 1;
        hence thesis by A3,A13,A14,FUNCT_1:def 5;
      end;
    end;
    hence thesis;
  end;
  then k+1=rng g by XBOOLE_0:def 10;
  hence g is onto by FUNCT_2:def 3;
  for i,j st i in rng g & j in rng g & i<j holds min* g"{i} < min* g"{j}
  by A1,A2,Th39;
  hence g is "increasing by Def1;
  thus g"{g.n}={n}
  proof
    assume g"{g.n}<>{n};
    then consider m such that
A15: m in g"{g.n} and
A16: m<>n by Th35;
    g.m in {g.n} by A15,FUNCT_1:def 13;
    then
A17: g.m=k by A3,TARSKI:def 1;
    m in dom g by A15,FUNCT_1:def 13;
    then m<n+1 by NAT_1:45;
    then m<=n by NAT_1:13;
    then
A18: m<n by A16,XXREAL_0:1;
    n=0 iff k=0 by A1,Def1;
    then dom f=n by FUNCT_2:def 1;
    then
A19: m in dom f by A18,NAT_1:45;
    then
A20: f.m in rng f by FUNCT_1:def 5;
    f.m=g.m by A2,A19,FUNCT_1:70;
    hence contradiction by A20,A17,NAT_1:45;
  end;
end;

theorem Th41:
  for f be Function of n,k, g be Function of n+1,k st f is onto
  "increasing & f=g|n & g.n < k holds g is onto "increasing & g"{g.n}<>{n}
proof
  let f be Function of n,k, g be Function of n+1,k such that
A1: f is onto "increasing and
A2: f=g|n and
A3: g.n < k;
  k=rng f by A1,FUNCT_2:def 3;
  then consider x such that
A4: x in dom f and
A5: f.x=g.n by A3,FUNCT_1:def 5;
  g.n=g.x by A2,A4,A5,FUNCT_1:70;
  then
A6: g.x in {g.n} by TARSKI:def 1;
  k c= rng g
  proof
    n<=n+1 by NAT_1:13;
    then
A7: n c= n+1 by NAT_1:40;
    n=0 iff k=0 by A1,Def1;
    then
A8: dom f = n by FUNCT_2:def 1;
    let x9 be set such that
A9: x9 in k;
    k is Subset of NAT by Th8;
    then reconsider x=x9 as Element of NAT by A9;
    rng f=k by A1,FUNCT_2:def 3;
    then consider y be set such that
A10: y in dom f and
A11: f.y =x by A9,FUNCT_1:def 5;
A12: dom g = n+1 by A3,FUNCT_2:def 1;
    f.y=g.y by A2,A10,FUNCT_1:70;
    hence thesis by A10,A11,A8,A7,A12,FUNCT_1:def 5;
  end;
  then k=rng g by XBOOLE_0:def 10;
  hence g is onto by FUNCT_2:def 3;
  k=k+0;
  then for i,j st i in rng g & j in rng g & i<j holds min* g"{i} < min* g"{j}
  by A1,A2,Th39;
  hence g is "increasing by A3,Def1;
  n<=n+1 by NAT_1:11;
  then
A13: n c= n+1 by NAT_1:40;
A14: x in n by A4;
  then
A15: x<>n;
  dom g=n+1 by A3,FUNCT_2:def 1;
  then x in g"{g.n} by A14,A13,A6,FUNCT_1:def 13;
  hence thesis by A15,TARSKI:def 1;
end;

Lm1: k<n implies n\/{k}=n
proof
  assume k<n;
  then k in n by NAT_1:45;
  then {k} c= n by ZFMISC_1:37;
  hence thesis by XBOOLE_1:12;
end;

theorem Th42:
  card{f where f is Function of n+1,k+1: f is onto "increasing & f
  "{f.n}={n}}= card{f where f is Function of n,k: f is onto "increasing}
proof
  set F1={f where f is Function of n+1,k+1: f is onto "increasing & f"{f.n}={n
  }};
  set F2={f where f is Function of n,k: f is onto "increasing};
  now
    per cases;
    suppose
A1:   k=0 & n<>0;
A2:   F1 is empty
      proof
        assume F1 is non empty;
        then consider x be set such that
A3:     x in F1 by XBOOLE_0:def 1;
        consider f be Function of n+1,k+1 such that
        x=f and
        f is onto "increasing and
A4:     f"{f.n}={n} by A3;
        0 in n+1 by NAT_1:45;
        then
A5:     0 in dom f by FUNCT_2:def 1;
A6:     0 in {0} by TARSKI:def 1;
A7:     f.0=0 by A1,CARD_1:87,TARSKI:def 1;
        f.n=0 by A1,CARD_1:87,TARSKI:def 1;
        then 0 in {n} by A4,A7,A5,A6,FUNCT_1:def 13;
        hence thesis by A1,TARSKI:def 1;
      end;
      n block k =0 by A1,Th31;
      hence thesis by A2;
    end;
    suppose
A8:   k=0 implies n=0;
      defpred P[set,set,set] means for i,j st i=$2 & j=$3 holds ex f be
Function of i,j st f=$1 & f is onto "increasing & (n < i implies f"{f.n}={n});
A9:   not n in n;
      set FF2={f where f is Function of n,k: P[f,n,k]};
      set FF1={f where f is Function of (n\/{n}),(k\/{k}): P[f,n\/{n},k\/{k}]&
      rng (f|n) c=k & f.n=k};
A10:  for f be Function of n\/{n},k\/{k} st f.n=k holds P[f,n\/{n},k\/{k}
      ] iff P[f|n,n,k]
      proof
        let f9 be Function of n\/{n},k\/{k} such that
A11:    f9.n=k;
        thus P[f9,n\/{n},k\/{k}] implies P[f9|n,n,k]
        proof
          n<=n+1 by NAT_1:11;
          then
A12:      n c=n+1 by NAT_1:40;
A13:      n+1=n\/{n} by AFINSQ_1:4;
A14:      k+1=k\/{k} by AFINSQ_1:4;
          assume P[f9,n\/{n},k\/{k}];
          then consider f be Function of n+1,k+1 such that
A15:      f=f9 and
A16:      f is onto "increasing and
A17:      n < n+1 implies f"{f.n}={n} by A13,A14;
A18:      rng (f|n) c= k by A16,A17,Th37,NAT_1:13;
A19:      dom (f|n)=dom f /\ n by RELAT_1:90;
          dom f=n+1 by FUNCT_2:def 1;
          then dom (f|n)=n by A12,A19,XBOOLE_1:28;
          then reconsider fn=f|n as Function of n,k by A18,FUNCT_2:4;
          let i,j such that
A20:      i=n and
A21:      j=k;
          reconsider fi=fn as Function of i,j by A20,A21;
          fi is onto "increasing by A16,A17,A20,A21,Th37,NAT_1:13;
          hence thesis by A15,A20;
        end;
        thus P[f9|n,n,k] implies P[f9,n\/{n},k\/{k}]
        proof
          n\/{n}=n+1 by AFINSQ_1:4;
          then reconsider f=f9 as Function of n+1,k+1 by AFINSQ_1:4;
          assume P[f9|n,n,k];
          then
A22:      ex fn be Function of n,k st fn=f9|n & fn is onto "increasing
          &( n < n implies fn"{fn.n}={n});
          let i,j such that
A23:      i=n\/{n} and
A24:      j=k\/{k};
          reconsider f1=f as Function of i,j by A23,A24;
A25:      n < i implies f1"{f1.n}={n} by A11,A22,Th40;
A26:      k+1 =j by A24,AFINSQ_1:4;
          n+1=i by A23,AFINSQ_1:4;
          then f1 is onto "increasing by A11,A22,A26,Th40;
          hence thesis by A25;
        end;
      end;
A27:  k is empty implies n is empty by A8;
A28:  card FF2 = card FF1 from Sch4(A27,A9,A10);
A29:  F2 c=FF2
      proof
        let x;
        assume x in F2;
        then
A30:    ex f be Function of n,k st x=f & f is onto "increasing;
        then P[x,n,k];
        hence thesis by A30;
      end;
A31:  F1 c=FF1
      proof
        let x;
        assume x in F1;
        then consider f be Function of n+1,k+1 such that
A32:    f=x and
A33:    f is onto "increasing and
A34:    f"{f.n}={n};
A35:    rng (f|n) c= k by A33,A34,Th37;
A36:    P[f,n\/{n},k\/{k}]
        proof
          let i,j such that
A37:      i=n\/{n} and
A38:      j=k\/{k};
A39:      j=k+1 by A38,AFINSQ_1:4;
          i=n+1 by A37,AFINSQ_1:4;
          hence thesis by A33,A34,A39;
        end;
A40:    k+1=k\/{k} by AFINSQ_1:4;
A41:    n+1=n\/{n} by AFINSQ_1:4;
        f.n=k by A33,A34,Th34;
        hence thesis by A32,A36,A35,A41,A40;
      end;
A42:  FF2 c= F2
      proof
        let x;
        assume x in FF2;
        then consider f be Function of n,k such that
A43:    x=f and
A44:    P[f,n,k];
        ex g be Function of n,k st g=f & g is onto "increasing & (n < n
        implies g"{g.n}={n}) by A44;
        hence thesis by A43;
      end;
      FF1 c=F1
      proof
        let x;
        assume x in FF1;
        then consider f be Function of (n\/{n}),(k\/{k})such that
A45:    x=f and
A46:    P[f,n\/{n},k\/{k}] and
        rng (f|n) c=k and
        f.n=k;
A47:    k+1=k\/{k} by AFINSQ_1:4;
        n+1=n\/{n} by AFINSQ_1:4;
        then
        ex f9 be Function of n+1,k+1 st f=f9 & f9 is onto "increasing & (
        n < n+1 implies f9"{f9.n}={n}) by A46,A47;
        hence thesis by A45,NAT_1:13;
      end;
      then F1=FF1 by A31,XBOOLE_0:def 10;
      hence thesis by A28,A29,A42,XBOOLE_0:def 10;
    end;
  end;
  hence thesis;
end;

theorem Th43:
  for l st l<k holds card{f where f is Function of n+1,k: f is
onto "increasing & f"{f.n}<>{n} & f.n=l}= card{f where f is Function of n,k: f
  is onto "increasing}
proof
  let l such that
A1: l<k;
  set F2={f where f is Function of n,k: f is onto "increasing};
  set F1={f where f is Function of n+1,k: f is onto "increasing & f"{f.n}<>{n}
  & f.n=l};
  now
    per cases;
    suppose
      k=0 & n<>0;
      hence thesis by A1;
    end;
    suppose
A2:   k=0 implies n=0;
      defpred P[set,set,set] means for i,j st i=$2 & j=$3 holds ex f be
Function of i,j st f=$1 & f is onto "increasing & (n < i implies f"{f.n}<>{n});
A3:   not n in n;
      set FF2={f where f is Function of n,k: P[f,n,k]};
      set FF1={f where f is Function of (n\/{n}),(k\/{l}): P[f,n\/{n},k\/{l}]&
      rng (f|n) c=k & f.n=l};
A4:   for f be Function of n\/{n},k\/{l} st f.n=l holds P[f,n\/{n},k\/{l}]
      iff P[f|n,n,k]
      proof
        let f9 be Function of n\/{n},k\/{l} such that
A5:     f9.n=l;
        thus P[f9,n\/{n},k\/{l}] implies P[f9|n,n,k]
        proof
          n<=n+1 by NAT_1:13;
          then
A6:       n c=n+1 by NAT_1:40;
          assume
A7:       P[f9,n\/{n},k\/{l}];
A8:       n+1=n\/{n} by AFINSQ_1:4;
          k=k\/{l} by A1,Lm1;
          then consider f be Function of n+1,k such that
A9:       f=f9 and
A10:      f is onto "increasing and
A11:      n < n+1 implies f"{f.n}<>{n} by A7,A8;
A12:      dom (f|n)=dom f /\ n by RELAT_1:90;
A13:      rng (f|n) c= k;
          dom f=n+1 by A1,FUNCT_2:def 1;
          then dom (f|n)=n by A6,A12,XBOOLE_1:28;
          then reconsider fn=f|n as Function of n,k by A13,FUNCT_2:4;
          let i,j such that
A14:      i=n and
A15:      j=k;
          reconsider fi=fn as Function of i,j by A14,A15;
          fi is onto "increasing by A10,A11,A14,A15,Th38,NAT_1:13;
          hence thesis by A9,A14;
        end;
        thus P[f9|n,n,k] implies P[f9,n\/{n},k\/{l}]
        proof
          n\/{n}=n+1 by AFINSQ_1:4;
          then reconsider f=f9 as Function of n+1,k by A1,Lm1;
          assume P[f9|n,n,k];
          then
A16:      ex fn be Function of n,k st fn=f9|n & fn is onto "increasing
          &( n < n implies fn"{fn.n}<>{n});
          let i,j such that
A17:      i=n\/{n} and
A18:      j=k\/{l};
          reconsider f1=f as Function of i,j by A17,A18;
A19:      n < i implies f1"{f1.n}<>{n} by A1,A5,A16,Th41;
A20:      n+1=i by A17,AFINSQ_1:4;
          k =j by A1,A18,Lm1;
          then f1 is onto "increasing by A1,A5,A16,A20,Th41;
          hence thesis by A19;
        end;
      end;
A21:  k is empty implies n is empty by A2;
A22:  card FF2 = card FF1 from Sch4(A21,A3,A4);
A23:  F2 c=FF2
      proof
        let x;
        assume x in F2;
        then
A24:    ex f be Function of n,k st x=f & f is onto "increasing;
        then P[x,n,k];
        hence thesis by A24;
      end;
A25:  F1 c=FF1
      proof
        let x;
        assume x in F1;
        then consider f be Function of n+1,k such that
A26:    f=x and
A27:    f is onto "increasing and
A28:    f"{f.n}<>{n} and
A29:    f.n=l;
A30:    P[f,n\/{n},k\/{l}]
        proof
          let i,j such that
A31:      i=n\/{n} and
A32:      j=k\/{l};
A33:      i=n+1 by A31,AFINSQ_1:4;
          j=k by A1,A32,Lm1;
          hence thesis by A27,A28,A33;
        end;
A34:    k=k\/{l} by A1,Lm1;
A35:    n+1=n\/{n} by AFINSQ_1:4;
        rng (f|n) c= k;
        hence thesis by A26,A29,A30,A35,A34;
      end;
A36:  FF2 c= F2
      proof
        let x;
        assume x in FF2;
        then consider f be Function of n,k such that
A37:    x=f and
A38:    P[f,n,k];
        ex g be Function of n,k st g=f & g is onto "increasing & (n < n
        implies g"{g.n}<>{n}) by A38;
        hence thesis by A37;
      end;
      FF1 c=F1
      proof
A39:    n+1=n\/{n} by AFINSQ_1:4;
        let x;
        assume x in FF1;
        then consider f be Function of (n\/{n}),(k\/{l})such that
A40:    x=f and
A41:    P[f,n\/{n},k\/{l}] and
        rng (f|n) c=k and
A42:    f.n=l;
        k=k\/{l} by A1,Lm1;
        then ex f9 be Function of n+1,k st f=f9 & f9 is onto "increasing & (n
        < n+1 implies f9"{f9.n}<>{n}) by A41,A39;
        hence thesis by A40,A42,NAT_1:13;
      end;
      then F1=FF1 by A25,XBOOLE_0:def 10;
      hence thesis by A22,A23,A36,XBOOLE_0:def 10;
    end;
  end;
  hence thesis;
end;



definition
  canceled;
end;

canceled 6;

definition
  canceled;
end;

canceled 4;

theorem Th54:
  for f be Function, n holds union (rng (f|n)) \/ f.n=union rng (f |(n+1))
proof
  let f be Function,n;
  thus union (rng (f|n))\/f.n c=union rng (f|(n+1))
  proof
    let x such that
A1: x in union (rng (f|n))\/f.n;
    now
      per cases by A1,XBOOLE_0:def 3;
      suppose
        x in union rng (f|n);
        then consider Y be set such that
A2:     x in Y and
A3:     Y in rng (f|n) by TARSKI:def 4;
        consider X be set such that
A4:     X in dom (f|n) and
A5:     (f|n).X=Y by A3,FUNCT_1:def 5;
A6:     (f|n).X=f.X by A4,FUNCT_1:70;
        n<=n+1 by NAT_1:11;
        then n c= n+1 by NAT_1:40;
        then
A7:     dom f /\n c= dom f /\(n+1) by XBOOLE_1:26;
        X in dom f /\n by A4,RELAT_1:90;
        then X in dom f /\(n+1) by A7;
        then
A8:     X in dom (f|(n+1)) by RELAT_1:90;
        then
A9:     (f|(n+1)).X=f.X by FUNCT_1:70;
        (f|(n+1)).X in rng (f|(n+1)) by A8,FUNCT_1:def 5;
        hence thesis by A2,A5,A9,A6,TARSKI:def 4;
      end;
      suppose
A10:    x in f.n;
        n<n+1 by NAT_1:13;
        then
A11:    n in n+1 by NAT_1:45;
        n in dom f by A10,FUNCT_1:def 4;
        then n in dom f /\ (n+1) by A11,XBOOLE_0:def 4;
        then
A12:    n in dom (f|(n+1)) by RELAT_1:90;
        then
A13:    (f|(n+1)).n=f.n by FUNCT_1:70;
        (f|(n+1)).n in rng (f|(n+1)) by A12,FUNCT_1:def 5;
        hence thesis by A10,A13,TARSKI:def 4;
      end;
    end;
    hence thesis;
  end;
  thus union rng (f|(n+1))c=union (rng (f|n))\/f.n
  proof
    let x;
    assume x in union rng (f|(n+1));
    then consider Y be set such that
A14: x in Y and
A15: Y in rng (f|(n+1)) by TARSKI:def 4;
    consider X be set such that
A16: X in dom (f|(n+1)) and
A17: (f|(n+1)).X=Y by A15,FUNCT_1:def 5;
A18: X in dom f /\(n+1) by A16,RELAT_1:90;
    then
A19: X in n+1 by XBOOLE_0:def 4;
A20: X in dom f by A18,XBOOLE_0:def 4;
    n+1 is Subset of NAT by Th8;
    then reconsider X as Element of NAT by A19;
    X < n+1 by A19,NAT_1:45;
    then
A21: X<=n by NAT_1:13;
    now
      per cases by A21,XXREAL_0:1;
      suppose
        X < n;
        then X in n by NAT_1:45;
        then X in dom f/\n by A20,XBOOLE_0:def 4;
        then
A22:    X in dom (f|n) by RELAT_1:90;
        then
A23:    (f|n).X in rng (f|n) by FUNCT_1:def 5;
A24:    f.X=(f|(n+1)).X by A16,FUNCT_1:70;
        (f|n).X=f.X by A22,FUNCT_1:70;
        then x in union rng (f|n) by A14,A17,A24,A23,TARSKI:def 4;
        hence thesis by XBOOLE_0:def 3;
      end;
      suppose
A25:    X=n;
        f.X=(f|(n+1)).X by A16,FUNCT_1:70;
        hence thesis by A14,A17,A25,XBOOLE_0:def 3;
      end;
    end;
    hence thesis;
  end;
end;

scheme Sch6{D()->non empty set,n()->Nat,P[set,set]}:
  ex p be XFinSequence of D() st dom p = n() & for k st k in n() holds P[k,p.k]
provided
A1: for k st k in n() ex x be Element of D() st P[k,x]
proof
A2: for k be set st k in n() ex x be set st x in D() & P[k,x]
  proof
    let k be set such that
A3: k in n();
    n() is Subset of NAT by Th8;
    then reconsider k9=k as Element of NAT by A3;
    ex x be Element of D() st P[k9,x] by A1,A3;
    hence thesis;
  end;
  consider f be Function of n(),D() such that
A4: for x st x in n() holds P[x,f.x] from FUNCT_2:sch 1(A2);
  dom f=n() by FUNCT_2:def 1;
  then reconsider p=f as XFinSequence of D() by AFINSQ_1:7;
  take p;
  thus thesis by A4,FUNCT_2:def 1;
end;

Sch7: now let D be non empty set, F be XFinSequence of D;
assume that
A1: for i st i in dom F holds F.i is finite and
A2: for i,j st i in dom F & j in dom F & i<>j holds F.i misses F .j;
   thus ex CardF be XFinSequence of NAT st dom CardF = dom F &
   (for i st i in dom CardF holds CardF.i=card (F.i)) & card union rng F =
   Sum(CardF)
proof
  defpred FF[Nat,set] means $2=card (F.$1);
A3: for k st k in len F ex x be Element of NAT st FF[k,x]
  proof
    let k;
    assume k in len F;
    then consider m being Nat such that
A4: (F.k),m are_equipotent by A1,CARD_1:74;
    card (F.k)=card m by A4,CARD_1:21;
    hence thesis;
  end;
  consider CardF be XFinSequence of NAT such that
A5: dom CardF=len F & for k st k in len F holds FF[k,CardF.k] from
  Sch6(A3);
  take CardF;
  thus dom CardF = dom F by A5;
  thus for i st i in dom CardF holds CardF.i=card (F.i) by A5;
H:    addnat "**" CardF  = Sum CardF by AFINSQ_2:63;
    per cases;
    suppose
A6:   len CardF=0;then
  union rng F is empty by A5,RELAT_1:65,ZFMISC_1:2;
      hence thesis by A6,AFINSQ_2:def 9,BINOP_2:5,H;
    end;
    suppose
A7:   len CardF<>0;
      then consider f be Function of NAT,NAT such that
A8:   f.0 = CardF.0 and
A9:   for n be Nat
      st n+1<len CardF holds f.(n+1)=addnat.(f.n,CardF.(n + 1)) and
A10:  addnat "**" CardF = f.(len CardF-1) by AFINSQ_2:def 9;
      defpred P[Nat] means $1<len CardF implies card union rng (F
      |($1+1))=f.$1 & union rng (F|($1+1)) is finite;
A11:  for k st P[k] holds P[k + 1]
      proof
        let k such that
A12:    P[k];
        set k1=k+1;
        set Fk1=F|k1;
        set rFk1=rng Fk1;
        assume that
A13:    k1<len CardF;
        reconsider urFk1=union rFk1 as finite set by A12,A13,NAT_1:13;
A14:    f.k1=addnat.(f.k,CardF.k1) by A9,A13;
A15:    union (rng Fk1) misses F.k1
        proof
          assume union (rng Fk1) meets F.k1;
          then consider x such that
A16:      x in union (rng Fk1)/\F.k1 by XBOOLE_0:4;
A17:      x in F.k1 by A16,XBOOLE_0:def 4;
A18:      k1 in dom F by A5,A13,NAT_1:45;
          x in union (rng Fk1) by A16,XBOOLE_0:def 4;
          then consider Y be set such that
A19:      x in Y and
A20:      Y in rng Fk1 by TARSKI:def 4;
          consider X be set such that
A21:      X in dom Fk1 and
A22:      Fk1.X=Y by A20,FUNCT_1:def 5;
          reconsider X as Element of NAT by A21;
A23:      Fk1.X=F.X by A21,FUNCT_1:70;
A24:      X in dom F/\k1 by A21,RELAT_1:90;
          then X in k1 by XBOOLE_0:def 4;
          then
A25:      X<>k1;
          X in dom F by A24,XBOOLE_0:def 4;
          then Y misses F.k1 by A2,A22,A18,A25,A23;
          hence contradiction by A19,A17,XBOOLE_0:3;
        end;
        k1 in dom F by A5,A13,NAT_1:45;
        then reconsider Fk1=F.k1 as finite set by A1;
        k1 in len F by A5,A13,NAT_1:45;
        then card Fk1=CardF.k1 by A5;
        then
A26:    f.k1=f.k + card Fk1 by A14,BINOP_2:def 23;
        card (urFk1 \/ Fk1) = f.k + card Fk1 by A12,A13,A15,CARD_2:53,
        NAT_1:13;
        hence thesis by A26,Th54;
      end;
      reconsider C1=len CardF-1 as Element of NAT by A7,NAT_1:20;
A27:  C1 <C1+1 by NAT_1:13;
A28:  F|len CardF =F by A5,RELAT_1:97;
A29:  P[0]
      proof
        assume 0<len CardF;
A30:    union rng (F|(0+1)) = union rng (F|(0 qua Ordinal))\/F.0 by Th54;
        0 in len CardF by A7,NAT_1:45;
        hence thesis by A1,A5,A8,A30,ZFMISC_1:2;
      end;
      for k be Nat holds P[k] from NAT_1:sch 2(A29,A11);
      hence thesis by A10,A27,A28,H;
    end;
end;
end;

scheme
  Sch8{X,Y()->finite set,x()->set,P[set],f()->Function of card Y(),Y()}: ex F
be XFinSequence of NAT st dom F = card Y() & card{g where g is Function of X(),
Y():P[g]} = Sum(F) & for i st i in dom F holds F.i = card{g where g is Function
  of X(),Y(): P[g] & g.x()=f().i}
provided
A1: f() is onto one-to-one and
A2: Y() is non empty and
A3: x() in X()
proof
  defpred FF[Nat,set] means $2={g where g is Function of X(),Y():P[
  g]& g.x()=f().$1};
  consider n being Nat such that
A4: Y(),n are_equipotent by CARD_1:74;
  reconsider n as Element of NAT by ORDINAL1:def 13;
A5: for k st k in n ex x be Subset of Funcs(X(),Y()) st FF[k,x]
  proof
    let k such that
    k in n;
    set F0={g where g is Function of X(),Y():P[g]& g.x()=f().k};
    F0 c= Funcs(X(),Y())
    proof
      let x be set;
      assume x in F0;
      then ex g be Function of X(),Y() st x=g & P[g]& g.x()=f().k;
      hence thesis by A2,FUNCT_2:11;
    end;
    hence thesis;
  end;
  consider F be XFinSequence of bool Funcs(X(),Y()) such that
A6: dom F = n & for k st k in n holds FF[k,F.k] from Sch6(A5);
A7: for i,j st i in dom F & j in dom F & i<>j holds F.i misses F.j
  proof
    let i,j such that
A8: i in dom F and
A9: j in dom F and
A10: i<>j;
    assume F.i meets F.j;
    then consider x such that
A11: x in F.i/\F.j by XBOOLE_0:4;
    x in F.i by A11,XBOOLE_0:def 4;
    then
    x in {g where g is Function of X(),Y():P[g]& g.x()=f().i} by A6,A8;
    then
A12: ex gi be Function of X(),Y() st x=gi &( P[gi])& gi.x()=f ().i;
A13: card Y()=card n by A4,CARD_1:21;
A14: card n=n by CARD_1:def 5;
    x in F.j by A11,XBOOLE_0:def 4;
    then
    x in {g where g is Function of X(),Y():P[g]& g.x()=f().j} by A6,A9;
    then
A15: ex gj be Function of X(),Y() st x=gj &( P[gj])& gj.x()=f ().j;
    dom f()= card Y() by A2,FUNCT_2:def 1;
    hence contradiction by A1,A6,A8,A9,A10,A12,A15,A13,A14,FUNCT_1:def 8;
  end;
A16: for i st i in dom F holds F.i is finite
  proof
    let i;
    assume i in dom F;
    then
A17: F.i in rng F by FUNCT_1:def 5;
A18: Funcs(X(),Y()) is finite by FRAENKEL:16;
    rng F c= bool Funcs(X(),Y()) by RELAT_1:def 19;
    hence thesis by A17,A18;
  end;
  consider CardF be XFinSequence of NAT such that
A19: dom CardF = dom F and
A20: for i st i in dom CardF holds CardF.i=card (F.i) and
A21: card union rng F = Sum(CardF) by Sch7,A16,A7;
  take CardF;
  thus dom CardF = card Y() by A4,A6,A19,CARD_1:def 5;
  thus card{g where g is Function of X(),Y():P[g]} = Sum(CardF)
  proof
    set G={g where g is Function of X(),Y():P[g]};
A22: G c= union rng F
    proof
      let x;
      assume x in G;
      then consider g be Function of X(),Y() such that
A23:  x=g and
A24:  P[g];
A25:  card n=n by CARD_1:def 5;
A26:  rng f()=Y() by A1,FUNCT_2:def 3;
A27:  card Y()=card n by A4,CARD_1:21;
      dom g=X() by A2,FUNCT_2:def 1;
      then g.x() in rng g by A3,FUNCT_1:def 5;
      then consider y such that
A28:  y in dom f() and
A29:  f().y=g.x() by A26,FUNCT_1:def 5;
      F.y={g1 where g1 is Function of X(),Y():P[g1] & g1.x()=f().y} by A6
      ,A27,A25,A28;
      then
A30:  g in F.y by A24,A29;
      F.y in rng F by A6,A28,A27,A25,FUNCT_1:def 5;
      hence thesis by A23,A30,TARSKI:def 4;
    end;
    union rng F c=G
    proof
      let x;
      assume x in union rng F;
      then consider Y be set such that
A31:  x in Y and
A32:  Y in rng F by TARSKI:def 4;
      consider X be set such that
A33:  X in dom F and
A34:  F.X=Y by A32,FUNCT_1:def 5;
      Y={g where g is Function of X(),Y():P[g] & g.x()=f().X} by A6,A33,
      A34;
      then ex gX be Function of X(),Y() st x=gX& P[gX] &gX.x()=f().X by A31;
      hence thesis;
    end;
    hence thesis by A21,A22,XBOOLE_0:def 10;
  end;
  for i st i in dom CardF holds CardF.i = card{g where g is Function of X
  (),Y():P[g] & g.x()=f().i}
  proof
    let i such that
A35: i in dom CardF;
    F.i={g where g is Function of X(),Y():P[g] & g.x()=f().i} by A6,A19,
    A35;
    hence thesis by A20,A35;
  end;
  hence thesis;
end;

theorem Th55:
k * (n block k)= card{f where f is
  Function of n+1,k: f is onto "increasing & f"{f.n}<>{n}}
proof
  now
    per cases;
    suppose
A1:   k=0;
      set F1={f where f is Function of n+1,k: f is onto "increasing};
      set F={f where f is Function of n+1,k: f is onto "increasing & f"{f.n}<>
      {n}};
A2:   F c= F1
      proof
        let x;
        assume x in F;
        then
        ex f be Function of n+1,k st x=f & f is onto "increasing & f"{f.n}
        <>{n};
        hence thesis;
      end;
      card F1=(n+1) block k;
      then F1 is empty by A1,Th31;
      hence thesis by A1,A2;
    end;
    suppose
      k>0;
      then
A3:   k is non empty;
      set G1={g where g is Function of n+1,k: g is onto "increasing & g"{g.n}
      <>{n}};
      defpred P[set] means ex f be Function of n+1,k st f=$1& f is onto
      "increasing&f"{f.n}<>{n};
      n<n+1 by NAT_1:13;
      then
A5:   n in n+1 by NAT_1:45;
      card k,k are_equipotent by CARD_1:def 5;
      then consider f be Function such that
A6:   f is one-to-one and
A7:   dom f = card k and
A8:   rng f = k by WELLORD2:def 4;
      reconsider f as Function of card k,k by A7,A8,FUNCT_2:3;
A9:   f is onto one-to-one by A6,A8,FUNCT_2:def 3;
      consider F be XFinSequence of NAT such that
A10:  dom F = card k and
A11:  card{g where g is Function of n+1,k:P[g]} = Sum(F) and
A12:  for i st i in dom F holds F.i = card{g where g is Function of n
      +1,k:P[g] & g.n=f.i} from Sch8(A9,A3,A5);
A14:  for i be Nat st i in dom F holds F.i = n block k
      proof
        set F2={g where g is Function of n,k:g is onto "increasing};
        let i be Nat such that
A15:    i in dom F;
A16:    f.i in rng f by A7,A10,A15,FUNCT_1:def 5;
        k is Subset of NAT by Th8;
        then reconsider fi=f.i as Element of NAT by A8,A16;
A17:    fi<k by A16,NAT_1:45;
        set F1={g where g is Function of n+1,k:P[g] & g.n=fi};
        set F={g where g is Function of n+1,k:g is onto "increasing & g"{g.n}
        <>{n} & g.n=fi};
A18:    F1 c= F
        proof
          let x;
          assume x in F1;
          then ex g be Function of n+1,k st x=g & P[g] & g.n=fi;
          hence thesis;
        end;
        F c= F1
        proof
          let x;
          assume x in F;
          then
          ex g be Function of n+1,k st x=g &g is onto "increasing & g"{g.
          n}<>{n} & g.n=fi;
          hence thesis;
        end;
        then F=F1 by A18,XBOOLE_0:def 10;
        then card F1=card F2 by A17,Th43;
        hence thesis by A12,A15;
      end;
      then for i be Nat st i in dom F holds F.i <= n block k;
      then
A19:  Sum F <= len F * (n block k) by AFINSQ_2:71;
      set G={g where g is Function of n+1,k:P[g]};
A20:  G1 c= G
      proof
        let x;
        assume x in G1;
        then
        ex g be Function of n+1,k st x=g &g is onto "increasing & g"{g.n}
        <>{n};
        hence thesis;
      end;
A21:  G c= G1
      proof
        let x;
        assume x in G;
        then ex g be Function of n+1,k st x=g & P[g];
        hence thesis;
      end;
      for i be Nat st i in dom F holds F.i >= n block k by A14;
      then
A22:  Sum F >= len F * (n block k) by AFINSQ_2:72;
      card k =k by CARD_1:def 5;
      then Sum F= k*(n block k) by A10,A19,A22,XXREAL_0:1;
      hence thesis by A11,A20,A21,XBOOLE_0:def 10;
    end;
  end;
  hence thesis;
end;
 
:: Recursive dependence

theorem Th56:
(n+1) block (k+1) = (k+1)*(n block (k+1) ) + (n block k)
proof
  set F={f where f is Function of n+1,k+1:f is onto "increasing};
  set F1={f where f is Function of n+1,k+1:f is onto "increasing&f"{f.n}={n}};
  set F2={f where f is Function of n+1,k+1:f is onto "increasing&f"{f.n}<>{n}};
A1: F c= F1\/F2
  proof
    let x;
    assume x in F;
    then consider f be Function of n+1,k+1 such that
A2: f =x and
A3: f is onto "increasing;
    f"{f.n}={n} or f"{f.n}<>{n};
    then f in F1 or f in F2 by A3;
    hence thesis by A2,XBOOLE_0:def 3;
  end;
  F1\/F2 c= F
  proof
    let x;
    assume x in F1\/F2;
    then x in F1 or x in F2 by XBOOLE_0:def 3;
    then
    (ex f be Function of n+1,k+1 st f = x & f is onto "increasing & f"{f.
n}={n}) or ex f be Function of n+1,k+1 st f = x & f is onto "increasing & f"{f.
    n}<>{n};
    hence thesis;
  end;
  then
A4: F1\/F2=F by A1,XBOOLE_0:def 10;
A5: F1 misses F2
  proof
    assume F1 meets F2;
    then consider x such that
A6: x in F1/\F2 by XBOOLE_0:4;
    x in F2 by A6,XBOOLE_0:def 4;
    then
A7: ex f be Function of n+1,k+1 st f = x & f is onto "increasing & f"{f.n
    } <>{n};
    x in F1 by A6,XBOOLE_0:def 4;
    then
    ex f be Function of n+1,k+1 st f = x & f is onto "increasing & f"{f.n
    } ={n};
    hence contradiction by A7;
  end;
A8: F2 c= Funcs(n+1,k+1)
  proof
    let x;
    assume x in F2;
    then
    ex f be Function of n+1,k+1 st f = x & f is onto "increasing & f"{f.n
    }<>{n};
    hence thesis by FUNCT_2:11;
  end;
A9: F1 c= Funcs(n+1,k+1)
  proof
    let x;
    assume x in F1;
    then
    ex f be Function of n+1,k+1 st f = x & f is onto "increasing & f"{f.n
    }={n };
    hence thesis by FUNCT_2:11;
  end;
  Funcs(n+1,k+1) is finite by FRAENKEL:16;
  then reconsider F1,F2 as finite set by A9,A8;
  reconsider k1=k+1 as Element of NAT;
A10: card F2= k1 * (n block k1) by Th55;
  card F1= n block k by Th42;
  hence thesis by A4,A5,A10,CARD_2:53;
end;

theorem Th57:
n>=1 implies n block 2 = 1/2 * (2 |^ n - 2 )
proof
  defpred P[Nat] means $1 block 2 = 1/2 * (2 |^ $1-2);
A1: for k be Nat st k>=1 & P[k] holds P[k + 1]
  proof
    let k be Nat such that
A2: k >=1 and
A3: P[k];
    (k+1) block 2 = 2*( k block (1+1)) + (k block 1) by Th56
      .=2 * (1/2 * (2 |^ k - 2)) + 1 by A2,A3,Th32
      .=1/2* (2* 2 |^ k - 2)
      .=1/2* (2 |^ (k+1)-2) by NEWTON:11;
    hence thesis;
  end;
  2 |^1= 2 by NEWTON:10;
  then
A5: P[1] by Th29;
  for k be Nat st k>=1 holds P[k] from NAT_1:sch 8(A5,A1);
  hence thesis;
end;

theorem Th58:
  n >= 2 implies n block 3 = 1/6 * ( 3 |^ n - 3 * 2 |^ n + 3 )
proof
  defpred P[Nat] means $1 block 3=1/6*(3|^$1-3*2|^$1+3);
A1: for k be Nat st k>=2 & P[k] holds P[k+1]
  proof
    let k be Nat such that
A2: k>=2 and
A3: P[k];
    k block 2 = 1/2 * (2 |^ k - 2) by A2,Th57,XXREAL_0:2;
    hence (k+1) block 3 = 3*(k block (2+1)) +(1/2*(2|^k-2)) by Th56
      .=1/6*((3*3|^k)-3*2*2|^k +3) by A3
      .=1/6*(3|^(k+1)-3*(2*2|^k) +3) by NEWTON:11
      .=1/6*(3|^(k+1)-3*2|^(k+1) +3) by NEWTON:11;
  end;
  1/6*(3|^2-3*2|^2+3)=1/6*(3*3-3*2|^2+3) by WSIERP_1:2
    .=1/6*(3*3-3*(2*2)+3) by WSIERP_1:2
    .=2 block 3 by Th29;
  then
A4: P[2];
  for k be Nat st k>=2 holds P[k] from NAT_1:sch 8(A4,A1);
  hence thesis;
end;

Lm2: n|^3=n*n*n
proof
  reconsider n as Element of NAT by ORDINAL1:def 13;
  n|^(2+1)=(n|^2)*n by NEWTON:11
    .=n*n*n by WSIERP_1:2;
hence thesis;
end;

theorem
  n >= 3 implies n block 4 = 1/24 * ( 4 |^ n - 4 * 3 |^ n + 6* 2|^ n - 4 )
proof
  defpred P[Nat] means $1 block 4= 1/24 * ( 4 |^ $1 - 4 * 3 |^ $1 + 6* 2|^ $1
  - 4 );
A1: for k be Nat st k>=3 & P[k] holds P[k+1]
  proof
    let k be Nat such that
A2: k>=3 and
A3: P[k];
    k block 3 = 1/6 * (3|^k-3*2|^k+3) by A2,Th58,XXREAL_0:2;
    hence (k+1) block 4 = 4*(k block (3+1)) +(1/6*(3|^k-3*2|^k+3)) by Th56
      .=1/24*((4*4|^k)- 4*(3*3|^k)+ 6*(2*2|^k) - 4) by A3
      .=1/24*(4|^(k+1)- 4*(3*3|^k)+ 6*(2*2|^k) - 4) by NEWTON:11
      .=1/24*(4|^(k+1)- 4*3|^(k+1)+ 6*(2*2|^k) - 4) by NEWTON:11
      .=1/24*(4|^(k+1)- 4*(3|^(k+1))+ 6*2|^(k+1) - 4) by NEWTON:11;
  end;
  1/24*(4|^3-4*3|^3+6*2|^3-4)= 1/24*(4*4*4-4*3|^3+6*2|^3-4) by Lm2
    .=1/24*(64-4*(3*3*3)+6*2|^3-4) by Lm2
    .=1/24*(64-4*27+6*(2*2*2)-4) by Lm2
    .=3 block 4 by Th29;
  then
A4: P[3];
  for k be Nat st k>=3 holds P[k] from NAT_1:sch 8(A4,A1);
  hence thesis;
end;

theorem Th60:
  3! = 6 & 4! = 24
proof
  thus
A1: 3! = (2+1)! .= 2*3 by NEWTON:20,21
    .= 6;
  thus 4! = (3+1)! .= 6*4 by A1,NEWTON:21
    .= 24;
end;

theorem Th61:
  n choose 1=n & n choose 2=n*(n-1)/2 & n choose 3=n*(n-1)*(n-2)/6
  & n choose 4=n*(n-1)*(n-2)*(n-3)/24
proof
  now
    per cases by NAT_1:28;
    suppose
      n=0;
      hence thesis by NEWTON:def 3;
    end;
    suppose
      n=1;
      hence thesis by NEWTON:31,def 3;
    end;
    suppose
      n=2;
      hence thesis by NEWTON:31,33,def 3;
    end;
    suppose
A1:   n=3;
      then reconsider n1=n-1,n2=n-2 as Element of NAT by NAT_1:20;
A2:   (n2+1)!=(n2)!*(n2+1) by NEWTON:21;
      n2!<>0 by NEWTON:23;
      then
A3:   (n2!)/(n2!)=1 by XCMPLX_1:60;
      (n1+1)!=(n1)!*n by NEWTON:21;
      then n choose 2=((n2)!*((n-1)*n))/((n2!)*(2!)) by A1,
      A2,NEWTON:def 3
        .=((n2!)/(n2!)*((n-1)*n))/2 by NEWTON:20,XCMPLX_1:84
        .=((n-1)*n)/2 by A3;
      hence thesis by A1,NEWTON:31,33,def 3;
    end;
    suppose
A4:   n>3;
      then n>=3+1 by NAT_1:13;
      then reconsider n1=n-1,n2=n-2,n3=n-3,n4=n-4 as Element of NAT by
      NAT_1:21
      ,XXREAL_0:2;
A5:   (n1+1)!=(n1)!*n by NEWTON:21;
A6:   (n2+1)!=(n2)!*(n2+1) by NEWTON:21;
      n2!<>0 by NEWTON:23;
      then
A7:   (n2!)/(n2!)=1 by XCMPLX_1:60;
      n>=2 by A4,XXREAL_0:2;
      then
A8:   n choose 2=((n2)!*(n1*n))/((n2!)*(2!)) by A5,A6,NEWTON:def 3
        .=((n2!)/(n2!)*(n1*n))/2 by NEWTON:20,XCMPLX_1:84
        .=n*n1/2 by A7;
      n4!<>0 by NEWTON:23;
      then
A9:   (n4!)/(n4!)=1 by XCMPLX_1:60;
A10:  (n4+1)!=(n4)!*(n4+1) by NEWTON:21;
      n3!<>0 by NEWTON:23;
      then
A11:  (n3!)/(n3!)=1 by XCMPLX_1:60;
A12:  (n3+1)!=(n3)!*(n3+1) by NEWTON:21;
      then
A13:  n choose 3=((n3)!*(n2*n1*n))/((n3!)*(3!)) by A4,A5,A6,
      NEWTON:def 3
        .=((n3!)/(n3!)*(n2*n1*n))/6 by Th60,XCMPLX_1:84
        .=n*n1*n2/6 by A11;
      n>=3+1 by A4,NAT_1:13;
      then
n choose 4=((n4!)*(n3*n2*n1*n))/((n4!)*(4!)) by A5,A6,A12,A10, NEWTON:def 3
        .=((n4!)/(n4!)*(n3*n2*n1*n))/24 by Th60,XCMPLX_1:84
        .=n*n1*n2*n3/24 by A9;
      hence thesis by A4,A8,A13,NEWTON:33,XXREAL_0:2;
    end;
  end;
  hence thesis;
end;

theorem Th62:
  (n + 1) block n = (n + 1) choose 2
proof
  defpred P[Nat] means ($1+1) block $1 = ($1+1) choose 2;
A1: for k st P[k] holds P[k+1]
  proof
    let k such that
A2: P[k];
    set k1=k+1;
    thus (k1+1) block k1=k1*(k1 block k1)+(k1 block k) by Th56
      .=k1*1+(k1 choose 2) by A2,Th26
      .=k1+k1*(k1-1)/2 by Th61
      .=(k1+1)*(k1+1-1)/2
      .=(k1+1) choose 2 by Th61;
  end;
  1 block 0 =0 by Th31;
  then
A3: P[0] by NEWTON:def 3;
  for k holds P[k] from NAT_1:sch 2(A3,A1);
  hence thesis;
end;

theorem
  (n + 2) block n = 3*((n + 2) choose 4) + ((n + 2) choose 3)
proof
  defpred P[Nat] means ($1+2)block $1=3*(($1+2)choose 4)+(($1+2)
  choose 3);
A1: 2 choose 3=0 by NEWTON:def 3;
A2: for k st P[k] holds P[k+1]
  proof
    let k such that
A3: P[k];
    set k1=k+1;
    set k2=k+2;
    set k3=k2+1;
A4: k1*((k1+1) block k1)=k1*(k2 choose 2) by Th62
      .=k1*(k2*(k2-1)/2) by Th61
      .=k2*(k2-1)*(k1*12/24);
    k2 block k=3*(k2*(k2-1)*(k2-2)*(k2-3)/24)+(k2 choose 3) by A3,Th61
      .=3*(k2*(k2-1)*(k2-2)*(k2-3)/24)+(k2*(k2-1)*(k2-2)/6) by Th61
      .=k2*(k2-1)*(3*(k2-2)*(k2-3)/24 +4*(k2-2)/24);
    then k3 block k1= k2*k1*(k1*12/24)+k2*k1*(3*k*(k2-3)/24+4*k/24) by A4,
    Th56
      .=3*(k3*(k3-1)*(k3-2)*(k3-3)/24)+k3*k2*k1/6
      .=3*(k3 choose 4) +(k3*(k3-1)*(k3-2)/6) by Th61
      .=3*(k3 choose 4)+(k3 choose 3) by Th61;
    hence thesis;
  end;
  2 choose 4=0 by NEWTON:def 3;
  then
A5: P[0] by A1,Th31;
  for k holds P[k] from NAT_1:sch 2(A5,A2);
  hence thesis;
end;

theorem Th64:
  for F be Function,y holds rng (F|(dom F\F"{y}))=rng F\{y} & for
  x st x<>y holds (F|(dom F\F"{y}))"{x}=F"{x}
proof
  let F be Function,y;
  set D=dom F\F"{y};
A1: rng(F|D) c= rng F\{y}
  proof
    let Fx be set;
    assume Fx in rng (F|D);
    then consider x such that
A2: x in dom (F|D) and
A3: Fx=(F|D).x by FUNCT_1:def 5;
A4: x in dom F /\ D by A2,RELAT_1:90;
    then x in (dom F/\dom F)\F"{y} by XBOOLE_1:49;
    then not x in F"{y} by XBOOLE_0:def 5;
    then not F.x in {y} by A4,FUNCT_1:def 13;
    then
A5: not Fx in {y} by A2,A3,FUNCT_1:70;
    F.x in rng F by A4,FUNCT_1:def 5;
    then Fx in rng F by A2,A3,FUNCT_1:70;
    hence thesis by A5,XBOOLE_0:def 5;
  end;
  rng F\{y} c=rng (F|D)
  proof
    let Fx be set such that
A6: Fx in rng F\{y};
    consider x such that
A7: x in dom F and
A8: F.x=Fx by A6,FUNCT_1:def 5;
    not Fx in {y} by A6,XBOOLE_0:def 5;
    then not x in F"{y} by A8,FUNCT_1:def 13;
    then x in D by A7,XBOOLE_0:def 5;
    then x in dom F /\D by XBOOLE_0:def 4;
    then
A9: x in dom (F|D) by RELAT_1:90;
    then (F|D).x in rng (F|D) by FUNCT_1:def 5;
    hence thesis by A8,A9,FUNCT_1:70;
  end;
  hence rng (F|D)=rng F\{y} by A1,XBOOLE_0:def 10;
  let x such that
A10: x<>y;
  now
    let z be set such that
A11: z in F"{x};
    F.z in {x} by A11,FUNCT_1:def 13;
    then F.z <> y by A10,TARSKI:def 1;
    then not F.z in {y} by TARSKI:def 1;
    then
A12: not z in F"{y} by FUNCT_1:def 13;
    z in dom F by A11,FUNCT_1:def 13;
    hence z in dom F\F"{y} by A12,XBOOLE_0:def 5;
  end;
  then F"{x} c= D by TARSKI:def 3;
  hence thesis by FUNCT_2:175;
end;

theorem Th65:
  card X=k+1 & x in X implies card (X\{x})=k
proof
  assume that
A1: card X=k+1 and
A2: x in X;
  reconsider X9=X as finite set by A1;
  set Xx=X9\{x};
  {x} c= X by A2,ZFMISC_1:37;
  then {x} /\X ={x} by XBOOLE_1:28;
  then Xx \/ {x} = X by XBOOLE_1:51;
  then
A3: card {x}+ card Xx= k+1 by A1,CARD_2:53,XBOOLE_1:79;
  card {x}=1 by CARD_1:50;
  hence thesis by A3;
end;

scheme
  Sch9{P[set],Q[set,Function]}: for F be Function st rng F is finite holds P[F
  ]
provided
A1: P[{}] and
A2: for F be Function st for x st x in rng F & Q[x,F] holds P[F|(dom F\F
"{x})] holds P[F]
proof
  defpred R[Nat] means for F be Function st card rng F=$1 holds P[F
  ];
A3: for n st R[n] holds R[n+1]
  proof
    let n such that
A4: R[n];
    let F be Function such that
A5: card rng F=n+1;
    for x st x in rng F & Q[x,F] holds P[F|(dom F\F"{x})]
    proof
      let x such that
A6:   x in rng F and
      Q[x,F];
      set D=dom F\F"{x};
      card (rng F\{x})=n by A5,A6,Th65;
      then card rng (F|D)=n by Th64;
      hence thesis by A4;
    end;
    hence thesis by A2;
  end;
  let F be Function;
  assume rng F is finite;
  then consider n being Nat such that
A7: rng F,n are_equipotent by CARD_1:74;
A8: card rng F=n by A7,CARD_1:def 5;
A10: R[0]
  proof
    let F be Function;
    assume card rng F=0;
    then rng F={};
    hence thesis by A1,RELAT_1:64;
  end;
  for k holds R[k] from NAT_1:sch 2(A10,A3);
  hence thesis by A8;
end;

theorem Th66:
  for N be Subset of NAT st N is finite ex k st for n st n in N holds n<=k
proof
  let N be Subset of NAT;
  assume N is finite;
  then consider n being Nat such that
A1: N,n are_equipotent by CARD_1:74;
  consider F be Function such that
  F is one-to-one and
A2: dom F = n and
A3: rng F = N by A1,WELLORD2:def 4;
  reconsider F as XFinSequence by A2,AFINSQ_1:7;
  reconsider F as XFinSequence of NAT by A3,RELAT_1:def 19;
  reconsider k=Sum F as Element of NAT by ORDINAL1:def 13;
  take k;
  let n such that
A4: n in N;
  F <>0 by A3,A4;
  then
A5: len F>0;
  ex x st x in dom F & F.x=n by A3,A4,FUNCT_1:def 5;
  hence thesis by A5,AFINSQ_2:73;
end;

theorem Th67:
  for X,Y,x,y st (Y is empty implies X is empty) & not x in X for
  F be Function of X,Y ex G be Function of X\/{x},Y\/{y} st G|X = F & G.x=y
proof
  let X,Y,x,y such that
A1: Y is empty implies X is empty and
A2: not x in X;
  set Y1=Y\/{y};
  set X1=X\/{x};
  deffunc F(set)=y;
  let F be Function of X,Y;
  y in {y} by TARSKI:def 1;
  then
A3: for x9 be set st x9 in X1\X holds F(x9) in Y1 by XBOOLE_0:def 3;
A4: X c= X1 & Y c= Y1 by XBOOLE_1:7;
  consider G be Function of X1,Y1 such that
A5: G|X = F & for x9 be set st x9 in X1\X holds G.x9 = F(x9) from Sch2(
  A3,A4,A1);
  x in {x} by TARSKI:def 1;
  then x in X1 by XBOOLE_0:def 3;
  then x in X1\X by A2,XBOOLE_0:def 5;
  then G.x=y by A5;
  hence thesis by A5;
end;

theorem Th68:
  for X,Y,x,y st (Y is empty implies X is empty) for F be Function
  of X,Y,G be Function of X\/{x},Y\/{y} st G|X=F & G.x=y holds ( F is onto
  implies G is onto) & ( not y in Y & F is one-to-one implies G is one-to-one)
proof
  let X,Y,x,y such that
A1: Y is empty implies X is empty;
  let F be Function of X,Y,G be Function of (X\/{x}),(Y\/{y}) such that
A2: G|X=F and
A3: G.x=y;
  thus F is onto implies G is onto
  proof
    assume
A4: F is onto;
    Y\/{y} c= rng G
    proof
      let Fx be set such that
A5:   Fx in Y\/{y};
      now
        per cases by A5,XBOOLE_0:def 3;
        suppose
          Fx in Y;
          then Fx in rng F by A4,FUNCT_2:def 3;
          then consider x9 be set such that
A6:       x9 in dom F and
A7:       F.x9=Fx by FUNCT_1:def 5;
A8:       x9 in X by A6;
A9:       dom G=X\/{x} by FUNCT_2:def 1;
A10:      X c= X\/{x} by XBOOLE_1:7;
          G.x9=F.x9 by A2,A6,FUNCT_1:70;
          hence thesis by A7,A8,A10,A9,FUNCT_1:def 5;
        end;
        suppose
A11:      Fx in {y};
A12:      dom G =X\/{x} by FUNCT_2:def 1;
A13:      {x}c=X\/{x} by XBOOLE_1:7;
A14:      x in {x} by TARSKI:def 1;
          Fx=y by A11,TARSKI:def 1;
          hence thesis by A3,A12,A14,A13,FUNCT_1:def 5;
        end;
      end;
      hence thesis;
    end;
    then Y\/{y}=rng G by XBOOLE_0:def 10;
    hence thesis by FUNCT_2:def 3;
  end;
  thus not y in Y & F is one-to-one implies G is one-to-one
  proof
    assume that
A15: not y in Y and
A16: F is one-to-one;
    let x1,x2 be set such that
A17: x1 in dom G and
A18: x2 in dom G and
A19: G.x1=G.x2;
A20: for x9 be set st x9 in X holds x9 in dom F & F.x9=G.x9 & G.x9<>y
    proof
      let x9 be set such that
A21:  x9 in X;
A22:  x9 in dom F by A1,A21,FUNCT_2:def 1;
      then
A23:  F.x9 in rng F by FUNCT_1:def 5;
      F.x9=G.x9 by A2,A22,FUNCT_1:70;
      hence thesis by A15,A21,A23,FUNCT_2:def 1;
    end;
    now
      per cases by A17,A18,XBOOLE_0:def 3;
      suppose
A24:    x1 in X & x2 in X;
        then
A25:    F.x1=G.x1 by A20;
A26:    x2 in dom F by A20,A24;
A27:    F.x2=G.x2 by A20,A24;
        x1 in dom F by A20,A24;
        hence thesis by A16,A19,A26,A25,A27,FUNCT_1:def 8;
      end;
      suppose
A28:    x1 in X & x2 in {x};
        then G.x2=y by A3,TARSKI:def 1;
        hence thesis by A19,A20,A28;
      end;
      suppose
A29:    x1 in {x} & x2 in X;
        then G.x1=y by A3,TARSKI:def 1;
        hence thesis by A19,A20,A29;
      end;
      suppose
A30:    x1 in {x} & x2 in {x};
        then x=x1 by TARSKI:def 1;
        hence thesis by A30,TARSKI:def 1;
      end;
    end;
    hence thesis;
  end;
end;

theorem Th69:
  for N be finite Subset of NAT holds ex Order be Function of N,
card N st Order is bijective & for n,k st n in dom Order & k in dom Order & n<k
  holds Order.n < Order.k
proof
  defpred P[Nat] means for N be finite Subset of NAT st card N = $1
holds ex F be Function of N,card N st F is bijective & for n,k st n in dom F &
  k in dom F & n<k holds F.n < F.k;
A1: for k st P[k] holds P[k+1]
  proof
    let k such that
A2: P[k];
    let N be finite Subset of NAT such that
A3: card N =k+1;
    defpred M[set] means $1 in N;
    ex x st x in N by A3,CARD_1:47,XBOOLE_0:def 1;
    then
A4: ex n be Nat st M[n];
    consider m9 be Nat such that
A5: for n st n in N holds n<=m9 by Th66;
A6: for n be Nat st M[n] holds n<=m9 by A5;
    consider m be Nat such that
A7: M[m] & for n be Nat st M[n] holds n <= m from NAT_1:sch 6(A6,A4
    );
    set Nm=N\{m};
    consider F be Function of Nm,card Nm such that
A8: F is bijective and
A9: for n,k st n in dom F & k in dom F & n<k holds F.n < F.k by A2,
    A3,A7,Th65;
A10: card Nm=k by A3,A7,Th65;
    then
A11: (card Nm)\/{k}=card N by A3,AFINSQ_1:4;
A12: card Nm is empty implies Nm is empty;
    m in {m} by TARSKI:def 1;
    then not m in Nm by XBOOLE_0:def 5;
    then consider G be Function of Nm\/{m},card Nm\/{k} such that
A13: G|Nm=F and
A14: G.m=k by A12,Th67;
    N=Nm\/{m} by A7,ZFMISC_1:140;
    then reconsider G9=G as Function of N,card N by A3,A10,AFINSQ_1:4;
    take G9;
    not k in card Nm by A10;
    then G is one-to-one onto by A8,A12,A13,A14,Th68;
    hence G9 is bijective by A11;
    thus for n,k st n in dom G9 & k in dom G9 & n<k holds G9.n < G9.k
    proof
A15:  for i st i in Nm holds i < m & G9.i <k
      proof
        let i such that
A16:    i in Nm;
        not i in {m} by A16,XBOOLE_0:def 5;
        then
A17:    i<>m by TARSKI:def 1;
        M[i] by A16,XBOOLE_0:def 5;
        then
A18:    i <= m by A7;
        i in dom F by A16,FUNCT_2:def 1;
        then
A19:    F.i=G9.i by A13,FUNCT_1:70;
        card Nm=k by A3,A7,Th65;
        hence thesis by A16,A19,A18,A17,NAT_1:45,XXREAL_0:1;
      end;
      let i,j such that
A20:  i in dom G9 and
A21:  j in dom G9 and
A22:  i<j;
A23:  dom G9=Nm\/{m} by FUNCT_2:def 1;
      now
        per cases by A20,A21,A23,XBOOLE_0:def 3;
        suppose
A24:      i in Nm & j in Nm;
          then
A25:      j in dom F by FUNCT_2:def 1;
          then
A26:      F.j=G9.j by A13,FUNCT_1:70;
A27:      i in dom F by A24,FUNCT_2:def 1;
          then F.i=G9.i by A13,FUNCT_1:70;
          hence thesis by A9,A22,A27,A25,A26;
        end;
        suppose
A28:      i in Nm & j in {m};
          then G9.i<k by A15;
          hence thesis by A14,A28,TARSKI:def 1;
        end;
        suppose
A29:      i in {m} & j in Nm;
          then i=m by TARSKI:def 1;
          hence thesis by A22,A15,A29;
        end;
        suppose
A30:      i in {m} & j in {m};
          then i=m by TARSKI:def 1;
          hence thesis by A22,A30,TARSKI:def 1;
        end;
      end;
      hence thesis;
    end;
  end;
A31: P[0]
  proof
    set P={};
A32: rng P={};
A33: dom P={};
    let N be finite Subset of NAT such that
A34: card N=0;
    N is empty by A34;
    then reconsider P as Function of N,card N by A34,A32,A33,FUNCT_2:3;
    take P;
    rng P={};
    then P is one-to-one onto by A34,FUNCT_2:def 3;
    hence P is bijective;
    thus thesis;
  end;
  for k holds P[k] from NAT_1:sch 2(A31,A1);
  hence thesis;
end;

Lm3: for X be finite set,x st x in X holds card (X\{x}) < card X
proof
  let X be finite set,x such that
A1: x in X;
  card X>0 by A1;
  then reconsider c1=card X-1 as Element of NAT by NAT_1:20;
  c1<c1+1 by NAT_1:13;
  hence thesis by A1,Th65;
end;

theorem Th70:
  for X,Y be finite set,F be Function of X,Y st card X=card Y
  holds F is onto iff F is one-to-one
proof
  let X,Y be finite set,F be Function of X,Y such that
A1: card X=card Y;
  thus F is onto implies F is one-to-one
  proof
    assume
A2: F is onto;
    assume F is not one-to-one;
    then consider x1,x2 be set such that
A3: x1 in dom F and
A4: x2 in dom F and
A5: F.x1=F.x2 and
A6: x1<>x2 by FUNCT_1:def 8;
    reconsider Xx=X\{x1} as finite set;
    Y c= F.:Xx
    proof
      let Fy be set;
      assume Fy in Y;
      then Fy in rng F by A2,FUNCT_2:def 3;
      then consider y such that
A7:   y in dom F and
A8:   F.y=Fy by FUNCT_1:def 5;
      now
        per cases;
        suppose
A9:       y=x1;
          x2 in Xx by A4,A6,ZFMISC_1:64;
          hence thesis by A4,A5,A8,A9,FUNCT_1:def 12;
        end;
        suppose
          y<>x1;
          then y in Xx by A7,ZFMISC_1:64;
          hence thesis by A7,A8,FUNCT_1:def 12;
        end;
      end;
      hence thesis;
    end;
    then
A10: card Y c= card Xx by CARD_2:2;
    card Xx < card X by A3,Lm3;
    hence thesis by A1,A10,NAT_1:40;
  end;
  thus F is one-to-one implies F is onto
  proof
    assume F is one-to-one;
    then dom F,F.:dom F are_equipotent by CARD_1:60;
    then
A11: card dom F=card (F.:dom F) by CARD_1:21;
    assume F is not onto;
    then not rng F = Y by FUNCT_2:def 3;
    then not Y c= rng F by XBOOLE_0:def 10;
    then consider y such that
A12: y in Y and
A13: not y in rng F by TARSKI:def 3;
A14: card rng F <=card (Y\{y}) by A13,NAT_1:44,ZFMISC_1:40;
A15: F.:dom F= rng F by RELAT_1:146;
    card (Y\{y})< card Y by A12,Lm3;
    hence thesis by A1,A12,A14,A11,A15,FUNCT_2:def 1;
  end;
end;

Lm4: for n be Element of NAT,N be finite Subset of NAT, F be Function of N,
card N st n in N & F is bijective & for n,k st n in dom F & k in dom F & n<k
holds F.n < F.k ex P be Permutation of N st for k st k in N holds (k < n
implies P.k=F".((F.k)+1)) & (k = n implies P.k=F".0) & (k > n implies P.k=k)
proof
  let n be Element of NAT,N be finite Subset of NAT, F be Function of N,card N
  such that
A1: n in N and
A2: F is bijective and
A3: for n,k st n in dom F & k in dom F & n<k holds F.n < F.k;
  rng F=card N by A2,FUNCT_2:def 3;
  then reconsider F9=F" as Function of card N,N by A2,FUNCT_2:31;
  defpred P[set,set] means for k st k=$1 holds (k < n implies $2=F".((F.k)+1))
  & (k = n implies $2=F".0) & (k > n implies $2=k);
A4: dom F=N by A1,FUNCT_2:def 1;
A5: dom F9=card N by A1,FUNCT_2:def 1;
A6: for x st x in N ex y st y in N & P[x,y]
  proof
    let x9 be set such that
A7: x9 in N;
    reconsider x=x9 as Element of NAT by A7;
    now
      per cases by XXREAL_0:1;
      suppose
A8:     x < n;
        then F.x < F.n by A1,A3,A4,A7;
        then
A9:     (F.x)+1 <=F.n by NAT_1:13;
        F.n < card N by A1,NAT_1:45;
        then (F.x)+1 < card N by A9,XXREAL_0:2;
        then (F.x)+1 in dom F9 by A5,NAT_1:45;
        then
A10:    F9.((F.x)+1) in rng F9 by FUNCT_1:def 5;
        set FF=F".((F.x)+1);
        P[x9,FF] by A8;
        hence thesis by A10;
      end;
      suppose
A11:    x = n;
        0 in dom F9 by A1,A5,NAT_1:45;
        then
A12:    F9.0 in rng F9 by FUNCT_1:def 5;
        P[x9,F".0] by A11;
        hence thesis by A12;
      end;
      suppose
        x>n;
        then P[x,x];
        hence thesis by A7;
      end;
    end;
    hence thesis;
  end;
  consider P be Function of N,N such that
A13: for x st x in N holds P[x,P.x] from FUNCT_2:sch 1(A6);
  N c= rng P
  proof
    let Px9 be set such that
A14: Px9 in N;
    reconsider Px=Px9 as Element of NAT by A14;
    now
      per cases;
      suppose
A15:    Px<=n;
        rng (F9)=N by A2,A4,FUNCT_1:55;
        then consider x such that
A16:    x in dom F9 and
A17:    F9.x=Px by A14,FUNCT_1:def 5;
        card N is Subset of NAT by Th8;
        then reconsider x as Element of NAT by A5,A16;
        now
          per cases;
          suppose
A18:        x=0;
A19:        dom P=N by A1,FUNCT_2:def 1;
            P.n=F".0 by A1,A13;
            hence thesis by A1,A17,A18,A19,FUNCT_1:def 5;
          end;
          suppose
            x>0;
            then reconsider x1=x-1 as Element of NAT by NAT_1:20;
A20:        x1<x1+1 by NAT_1:13;
            x < card N by A16,NAT_1:45;
            then x1 < card N by A20,XXREAL_0:2;
            then
A21:        x1 in card N by NAT_1:45;
            card N=rng F by A2,FUNCT_2:def 3;
            then consider y such that
A22:        y in dom F and
A23:        F.y=x1 by A21,FUNCT_1:def 5;
            reconsider y as Element of NAT by A4,A22;
A24:        y in dom P by A4,A22,FUNCT_2:def 1;
A25:        y<n
            proof
              assume y>=n;
              then y>n or y=n by XXREAL_0:1;
              then
A26:          x1>=F.n by A1,A3,A4,A22,A23;
              x in rng F by A2,A16,FUNCT_1:55;
              then
A27:          F.Px=x by A2,A17,FUNCT_1:54;
              Px <n or Px=n by A15,XXREAL_0:1;
              then
A28:          F.Px<=F.n by A1,A3,A4,A14;
              x1+1>x1 by NAT_1:13;
              hence contradiction by A26,A27,A28,XXREAL_0:2;
            end;
            F".(F.y+1)=Px by A17,A23;
            then P.y=Px by A13,A22,A25;
            hence thesis by A24,FUNCT_1:def 5;
          end;
        end;
        hence thesis;
      end;
      suppose
A29:    Px>n;
A30:    Px in dom P by A14,FUNCT_2:def 1;
        Px=P.Px by A13,A14,A29;
        hence thesis by A30,FUNCT_1:def 5;
      end;
    end;
    hence thesis;
  end;
  then N=rng P by XBOOLE_0:def 10;
  then
A31: P is onto by FUNCT_2:def 3;
  card N=card N;
  then P is onto one-to-one by A31,Th70;
  then reconsider P as Permutation of N;
  take P;
  thus thesis by A13;
end;

theorem Th71:
  for F,G be Function,y st y in rng (G*F) & G is one-to-one holds
  ex x st x in dom G & x in rng F & G"{y}={x} & F"{x}=(G*F)"{y}
proof
  let F,G be Function,y such that
A1: y in rng (G*F) and
A2: G is one-to-one;
  consider x such that
A3: x in dom (G*F) and
A4: (G*F).x=y by A1,FUNCT_1:def 5;
A5: F.x in dom G by A3,FUNCT_1:21;
A6: G.(F.x)=y by A3,A4,FUNCT_1:22;
  then G.(F.x) in {y} by TARSKI:def 1;
  then
A7: F.x in G"{y} by A5,FUNCT_1:def 13;
A8: F"{F.x} c= (G*F)"{y}
  proof
    let d be set such that
A9: d in F"{F.x};
A10: d in dom F by A9,FUNCT_1:def 13;
    F.d in {F.x} by A9,FUNCT_1:def 13;
    then
A11: F.d = F.x by TARSKI:def 1;
    then G.(F.d) in {y} by A6,TARSKI:def 1;
    then
A12: (G*F).d in {y} by A10,FUNCT_1:23;
    d in dom (G*F) by A5,A10,A11,FUNCT_1:21;
    hence thesis by A12,FUNCT_1:def 13;
  end;
  y in rng G by A1,FUNCT_1:25;
  then consider Fx be set such that
A13: G"{y}={Fx} by A2,FUNCT_1:144;
  x in dom F by A3,FUNCT_1:21;
  then
A14: F.x in rng F by FUNCT_1:def 5;
A15: F.x in dom G by A3,FUNCT_1:21;
  (G*F)"{y} c= F"{F.x}
  proof
    let d be set such that
A16: d in (G*F)"{y};
A17: d in dom (G*F) by A16,FUNCT_1:def 13;
    then
A18: d in dom F by FUNCT_1:21;
    (G*F).d in {y} by A16,FUNCT_1:def 13;
    then
A19: G.(F.d) in {y} by A17,FUNCT_1:22;
A20: F.d in dom G by A17,FUNCT_1:21;
    F.x=Fx by A13,A7,TARSKI:def 1;
    then F.d in {F.x} by A13,A20,A19,FUNCT_1:def 13;
    hence thesis by A18,FUNCT_1:def 13;
  end;
  then
A21: F"{F.x} = (G*F)"{y} by A8,XBOOLE_0:def 10;
  G"{y}={F.x} by A13,A7,TARSKI:def 1;
  hence thesis by A15,A14,A21;
end;

definition
  let Ne,Ke;
  let f be Function of Ne,Ke;
  attr f is "increasing means
  :Def5:
  for l,m st l in rng f & m in rng f & l < m holds min* f"{l} < min* f"{m};
end;

theorem Th72:
  for F be Function of Ne,Ke st F is "increasing holds min* rng F= F.min* dom F
proof
  let F be Function of Ne,Ke such that
A1: F is "increasing;
  now
    per cases;
    suppose
A2:   rng F is empty;
      then
A3:   min* rng F=0 by NAT_1:def 1;
      dom F={} by A2,RELAT_1:65;
      hence thesis by A3,FUNCT_1:def 4;
    end;
    suppose
A4:   rng F is non empty;
      then reconsider rngF=rng F,Ke as non empty Subset of NAT by XBOOLE_1:1;
      Ke is non empty;
      then reconsider domF=dom F as non empty Subset of NAT by A4,
      FUNCT_2:def 1,
      RELAT_1:65;
      set md=min* domF;
      set mr=min* rngF;
      mr=F.md
      proof
A5:     md in dom F by NAT_1:def 1;
        then F.md in rngF by FUNCT_1:def 5;
        then
A6:     mr <=F.md by NAT_1:def 1;
        assume mr<>F.md;
        then
A7:     mr < F.md by A6,XXREAL_0:1;
A8:     md in domF by NAT_1:def 1;
A9:     md in dom F by NAT_1:def 1;
        mr in rngF by NAT_1:def 1;
        then consider x such that
A10:    x in dom F and
A11:    F.x=mr by FUNCT_1:def 5;
A12:    F.md in {F.md} by TARSKI:def 1;
        F.x in {mr} by A11,TARSKI:def 1;
        then reconsider
        Fmr=F"{mr},Fmd=F"{F.md} as non empty Subset of NAT by A10,
        A12,A9,FUNCT_1:def 13,XBOOLE_1:1;
A13:    mr in rngF by NAT_1:def 1;
        min* Fmr in Fmr by NAT_1:def 1;
        then min* Fmr in domF by FUNCT_1:def 13;
        then
A14:    min* Fmr >= md by NAT_1:def 1;
        F.md in {F.md} by TARSKI:def 1;
        then md in Fmd by A8,FUNCT_1:def 13;
        then
A15:    md >= min* Fmd by NAT_1:def 1;
        F.md in rng F by A5,FUNCT_1:def 5;
        then min* F"{mr}< min* F"{F.md} by A1,A7,A13,Def5;
        hence contradiction by A14,A15,XXREAL_0:2;
      end;
      hence thesis;
    end;
  end;
  hence thesis;
end;
 
:: existence of an expansion

theorem
  for F be Function of Ne,Ke st rng F is finite ex I be Function of Ne,
  Ke,P be Permutation of rng F st F=P*I & rng F=rng I & I is "increasing
proof
  defpred Q[set,Function] means $1=$2.min* dom $2;
  defpred P[set] means for Ne,Ke be Subset of NAT,F be Function of Ne,Ke st F=
$1 & rng F is finite ex P be Permutation of rng F,G be Function of Ne,Ke st F=P
*G & rng F = rng G & for i,j st i in rng G & j in rng G & i<j holds min* G"{i}
  < min* G"{j};
A1: P[{}]
  proof
    let Ne,Me be Subset of NAT,F be Function of Ne,Me such that
A2: F={} and
    rng F is finite;
    reconsider R=rng F as empty set by A2;
    set P={};
A3: rng P={};
    dom P={};
    then reconsider P as Function of R,R by A3,FUNCT_2:3;
    rng R= {};
    then P is one-to-one onto by FUNCT_2:def 3;
    then reconsider P as Permutation of rng F;
    take P,F;
    rng F=R;
    then F={};
    hence thesis;
  end;
A4: for F be Function st for x st x in rng F & Q[x,F] holds P[F|(dom F\F"{x}
  )] holds P[F]
  proof
    let F9 be Function such that
A5: for x st x in rng F9 & Q[x,F9] holds P[F9|(dom F9\F9"{x})];
    let N,K be Subset of NAT,F be Function of N,K such that
A6: F=F9 and
A7: rng F is finite;
    now
      per cases;
      suppose
        rng F9 is empty;
        then F9={};
        hence thesis by A1,A6;
      end;
      suppose
A8:     rng F9 is non empty;
        then reconsider domF=dom F as non empty Subset of NAT by A6,
        RELAT_1:65
        ,XBOOLE_1:1;
        reconsider K as non empty Subset of NAT by A6,A8;
        set m=min* domF;
        set D=dom F\F"{F.m};
        min* domF in domF by NAT_1:def 1;
        then
A9:     F.m in rng F by FUNCT_1:def 5;
        now
          per cases;
          suppose
            rng (F|D) is empty;
            then rng F\{F.m}={} by Th64;
            then
A10:        rng F={F.m} by A9,ZFMISC_1:66;
A11:        for i,j st i in rng F & j in rng F & i<j holds min* F"{i} <
            min* F"{j}
            proof
              let i,j such that
A12:          i in rng F and
A13:          j in rng F and
A14:          i<j;
              i =F.m by A10,A12,TARSKI:def 1;
              hence thesis by A10,A13,A14,TARSKI:def 1;
            end;
            set P=id rng F;
            rng P=rng F by RELAT_1:71;
            then P is one-to-one onto by FUNCT_2:def 3;
            then reconsider P as Permutation of rng F;
            F is Function of dom F,rng F by FUNCT_2:3;
            then P*F = F by FUNCT_2:23;
            hence thesis by A11;
          end;
          suppose
A15:        rng (F|D) is non empty;
            rng (F|D) c= rng F by RELAT_1:99;
            then reconsider
            rFD=rng (F|D) as non empty finite Subset of NAT by A7,
            A15,XBOOLE_1:1;
            deffunc G(set)=F.m;
            reconsider dFD=dom (F|D) as Subset of NAT by XBOOLE_1:1;
            reconsider FD=F|D as Function of dFD,rFD by FUNCT_2:3;
A16:        rFD is empty implies dFD is empty;
            reconsider rF=rng F as non empty finite Subset of NAT by A7,A9,
            XBOOLE_1:1;
A17:        dFD c= N & rFD c= K;
            consider P be Permutation of rng FD, G be Function of dFD,rFD such
            that
A18:        FD=P*G and
A19:        rng FD = rng G and
A20:        for i,j st i in rng G & j in rng G & i<j holds min* G"{i}
            < min* G"{j} by A5,A6,A9;
A21:        for x st x in N\dFD holds G(x) in K by A9;
            consider G2 be Function of N,K such that
A22:        G2|dFD=G & for x st x in N\dFD holds G2.x = G(x) from
            Sch2(A21,A17,A16);
A23:        rng G2 c= rng F
            proof
              let Gx be set;
              assume Gx in rng G2;
              then consider x such that
A24:          x in dom G2 and
A25:          G2.x=Gx by FUNCT_1:def 5;
              dom G2=N by FUNCT_2:def 1;
              then dom G2/\dFD \/(N\dFD)=dom G2 by XBOOLE_1:51;
              then dom G=dom G2/\dFD & x in dom G2/\dFD or x in N\dFD by A22,
              A24,RELAT_1:90,XBOOLE_0:def 3;
              then
              G.x in rng FD & G.x=G2.x & rng FD=rng F\{F.m} or G2.x=F.m &
              m in domF by A19,A22,Th64,FUNCT_1:70,def 5,NAT_1:def 1;
              hence thesis by A25,FUNCT_1:def 5,XBOOLE_0:def 5;
            end;
A26:        rng FD =rng F\{F.m} by Th64;
            dom FD = dom F /\(dom F\F"{F.m}) by RELAT_1:90;
            then
A27:        dFD=dom F\F"{F.m} by XBOOLE_1:28,36;
A28:        F"{F.m}c=G2"{F.m}
            proof
              let x such that
A29:          x in F"{F.m};
              not x in dom F\F"{F.m} by A29,XBOOLE_0:def 5;
              then x in N\dFD by A27,A29,XBOOLE_0:def 5;
              then G2.x=F.m by A22;
              then
A30:          G2.x in {F.m} by TARSKI:def 1;
              x in N by A29;
              then x in dom G2 by FUNCT_2:def 1;
              hence thesis by A30,FUNCT_1:def 13;
            end;
            rng F c= rng G2
            proof
              rng FD=rng F\{F.m} by Th64;
              then
A31:          rng F=rng FD\/{F.m} by A9,ZFMISC_1:140;
              let Fx be set such that
A32:          Fx in rng F;
              now
                per cases by A32,A31,XBOOLE_0:def 3;
                suppose
A33:              Fx in rng FD;
                  rng (G2|dFD) c=rng G2 by RELAT_1:99;
                  hence thesis by A19,A22,A33;
                end;
                suppose
A34:              Fx in {F.m};
A35:              m in dom F by NAT_1:def 1;
                  then m in N;
                  then m in dom G2 by FUNCT_2:def 1;
                  then
A36:              G2.m in rng G2 by FUNCT_1:def 5;
                  F.m in {F.m} by TARSKI:def 1;
                  then m in F"{F.m} by A35,FUNCT_1:def 13;
                  then not m in dom F\F"{F.m} by XBOOLE_0:def 5;
                  then
A37:              m in N\dFD by A27,A35,XBOOLE_0:def 5;
                  Fx=F.m by A34,TARSKI:def 1;
                  hence thesis by A22,A37,A36;
                end;
              end;
              hence thesis;
            end;
            then
A38:        rng G2=rng F by A23,XBOOLE_0:def 10;
            not F.m in rng F\{F.m} by ZFMISC_1:64;
            then not F.m in rng FD by Th64;
            then consider
            P2 be Function of rng FD\/{F.m},rng FD\/{F.m} such that
A39:        P2|(rng FD) = P and
A40:        P2.(F.m)=(F.m) by Th67;
            not F.m in rng F\{F.m} by ZFMISC_1:64;
            then
A41:        P2 is one-to-one onto by A39,A40,A26,Th68;
            rng FD\/{F.m} = rng F by A9,A26,ZFMISC_1:140;
            then reconsider P2 as Permutation of rng F by A41;
            consider Orde be Function of rF,card rF such that
A42:        Orde is bijective and
A43:        for n,k st n in dom Orde & k in dom Orde & n<k holds Orde
            .n < Orde.k by Th69;
            rng Orde=card rF by A42,FUNCT_2:def 3;
            then reconsider Orde9=Orde" as Function of card rF,rF by A42,
            FUNCT_2:31;
            consider P1 be Permutation of rF such that
A44:        for k st k in rF holds (k < F.m implies P1.k=Orde".((Orde
.k)+1)) & (k = F.m implies P1.k=Orde".0) & (k > F.m implies P1.k=k) by A9,A42,
            A43,Lm4;
            dom G2=N by FUNCT_2:def 1;
            then
A45:        G2 is Function of N,rF by A38,FUNCT_2:3;
            reconsider P21=P2*P1" as Function of rF,rF;
            reconsider P21 as Permutation of rF;
            dom P1=rF by FUNCT_2:def 1;
            then
A46:        P1"*P1=id rF by FUNCT_1:61;
            rng (P1*G2) c= K by XBOOLE_1:1;
            then reconsider PG=P1*G2 as Function of N,K by A45,FUNCT_2:8;
            dom G2=N by FUNCT_2:def 1;
            then G2 is Function of N,rF by A38,FUNCT_2:3;
            then (id rF)*G2 = G2 by FUNCT_2:23;
            then
A47:        P1"*(P1*G2)=G2 by A46,RELAT_1:55;
            G2"{F.m}c=F"{F.m}
            proof
              let x such that
A48:          x in G2"{F.m};
A49:          x in N\dFD
              proof
                assume not x in N\dFD;
                then x in dom G2 & dom G2=N & not x in N or x in dFD by A48,
                XBOOLE_0:def 5;
                then
A50:            x in dom G by FUNCT_2:def 1;
                then
A51:            G.x in rng G by FUNCT_1:def 5;
A52:            rng FD=rng F\{F.m} by Th64;
                G.x=G2.x by A22,A50,FUNCT_1:70;
                then not G2.x in {F.m} by A51,A52,XBOOLE_0:def 5;
                hence thesis by A48,FUNCT_1:def 13;
              end;
              then
A53:          not x in dom F\F"{F.m} by A27,XBOOLE_0:def 5;
              dom F=N by A9,FUNCT_2:def 1;
              then x in dom F by A49,XBOOLE_0:def 5;
              hence thesis by A53,XBOOLE_0:def 5;
            end;
            then
A54:        G2"{F.m}=F"{F.m} by A28,XBOOLE_0:def 10;
A55:        for n st n in rng FD holds G"{n}=G2"{n}
            proof
              K is non empty;
              then dom F=N by FUNCT_2:def 1;
              then
A56:          dFD= dom G2\G2"{F.m} by A27,A54,FUNCT_2:def 1;
A57:          rng FD=rng F\{F.m} by Th64;
              let n;
              assume n in rng FD;
              then not n in {F.m} by A57,XBOOLE_0:def 5;
              then n<>F.m by TARSKI:def 1;
              hence thesis by A22,A56,Th64;
            end;
A58:        for i,j st i in rng PG & j in rng PG & i<j holds min* PG"{i}
            < min* PG"{j}
            proof
A59:          for l st l in rF & l < F.m holds Orde.l+1 in rng Orde &
Orde.l+1 is Element of NAT & Orde.l+1<=Orde.(F.m) & Orde.(F.m) is Element of
              NAT & dom Orde=rF
              proof
A60:            Orde.(F.m) < card rF by NAT_1:45;
                let l such that
A61:            l in rF and
A62:            l < F.m;
                dom Orde=rF by FUNCT_2:def 1;
                then
A63:            Orde.l < Orde.(F.m) by A9,A43,A61,A62;
                then Orde.l+1<=Orde.(F.m) by NAT_1:13;
                then
A64:            Orde.l+1 < card rF by A60,XXREAL_0:2;
A65:            card rF is Subset of NAT by Th8;
A66:            Orde.(F.m) in card rF;
                card rF=rng Orde by A42,FUNCT_2:def 3;
                hence thesis by A63,A66,A65,A64,FUNCT_2:def 1,NAT_1:13
                ,45;
              end;
A67:          for n,k st n in dom Orde & k in dom Orde & Orde.n<Orde.k
              holds n < k
              proof
                let n,k such that
A68:            n in dom Orde and
A69:            k in dom Orde and
A70:            Orde.n<Orde.k;
                assume n>=k;
                then n>k by A70,XXREAL_0:1;
                hence contradiction by A43,A68,A69,A70;
              end;
A71:          for n,k st n in rng Orde & k in rng Orde holds n < k
              implies Orde9.n<Orde9.k
              proof
                let n,k such that
A72:            n in rng Orde and
A73:            k in rng Orde;
A74:            n = Orde.(Orde9.n) by A42,A72,FUNCT_1:57;
A75:            dom Orde = rng Orde9 by A42,FUNCT_1:55;
A76:            k = Orde.(Orde9.k) by A42,A73,FUNCT_1:57;
                assume
A77:            n < k;
A78:            rng Orde = dom Orde9 by A42,FUNCT_1:55;
                then
A79:            Orde9.n in dom Orde by A72,A75,FUNCT_1:def 5;
                Orde9.k in dom Orde by A73,A78,A75,FUNCT_1:def 5;
                hence thesis by A67,A77,A74,A76,A79;
              end;
              rng FD =rng F\{F.m} by Th64;
              then
A80:          rng FD\/{F.m} = rng G2 by A9,A38, ZFMISC_1:140;
              let i,j such that
A81:          i in rng PG and
A82:          j in rng PG and
A83:          i<j;
              consider i1 be set such that
A84:          i1 in dom P1 and
A85:          i1 in rng G2 and
A86:          P1"{i}={i1} and
A87:          G2"{i1}=PG"{i} by A81,Th71;
              consider j1 be set such that
A88:          j1 in dom P1 and
A89:          j1 in rng G2 and
A90:          P1"{j}={j1} and
A91:          G2"{j1}=PG"{j} by A82,Th71;
              dom P1=rF by FUNCT_2:def 1;
              then reconsider i1,j1 as Element of NAT by A84,A88;
A92:          i1 in P1"{i} by A86,TARSKI:def 1;
              then P1.i1 in {i} by FUNCT_1:def 13;
              then
A93:          P1.i1=i by TARSKI:def 1;
A94:          j1 in P1"{j} by A90,TARSKI:def 1;
              then
A95:          P1.j1 in {j} by FUNCT_1:def 13;
              then
A96:          P1.j1=j by TARSKI:def 1;
A97:          dom Orde=rF by FUNCT_2:def 1;
              now
                per cases by XXREAL_0:1;
                suppose
A98:              i1 < F.m & j1<F.m;
A99:              i1 in rng FD or i1 in {F.m} by A85,A80,XBOOLE_0:def
                  3;
                  then
A100:             G"{i1}=PG"{i} by A55,A87,A98,TARSKI:def 1;
A101:             j1 in rng FD or j1 in {F.m} by A89,A80,XBOOLE_0:def
                  3;
                  i1<j1
                  proof
                    assume i1>=j1;
                    then i1>j1 by A83,A93,A96,XXREAL_0:1;
                    then Orde.i1 >Orde.j1 by A43,A92,A94,A97;
                    then
A102:               Orde.i1+1 >Orde.j1+1 by XREAL_1:10;
A103:               Orde.i1+1 in rng Orde by A84,A59,A98;
A104:               Orde9.(Orde.j1+1)= j by A44,A94,A96,A98;
A105:               Orde.j1+1 in rng Orde by A88,A59,A98;
                    Orde9.(Orde.i1+1)=i by A44,A92,A93,A98;
                    hence contradiction by A83,A71,A102,A103,A105,A104;
                  end;
                  then min* G"{i1}<min* G"{j1} by A19,A20,A98,A99,A101
                  ,TARSKI:def 1;
                  hence thesis by A55,A91,A98,A101,A100,TARSKI:def 1;
                end;
                suppose
A106:             i1 = F.m & j1<>F.m;
                  consider x such that
A107:             x in dom G2 and
A108:             G2.x=j1 by A89,FUNCT_1:def 5;
                  G2.x in {j1} by A108,TARSKI:def 1;
                  then PG"{j} is non empty Subset of NAT by A91,A107,
                  FUNCT_1:def 13,XBOOLE_1:1;
                  then
A109:             min* PG"{j} in PG"{j} by NAT_1:def 1;
                  j1 in rng FD or j1 in {F.m} by A89,A80,XBOOLE_0:def
                  3;
                  then
A110:             G"{j1} =PG"{j} by A55,A91,A106,TARSKI:def 1;
                  then min* PG"{j} in dom F by A27,A109,XBOOLE_0:def 5;
                  then
A111:             m<=min* PG"{j} by NAT_1:def 1;
A112:             m in domF by NAT_1:def 1;
                  F.m in {F.m} by TARSKI:def 1;
                  then
A113:             m in F"{F.m} by A112,FUNCT_1:def 13;
                  then m<>min* PG"{j} by A27,A110,A109,XBOOLE_0:def 5;
                  then
A114:             m<min* PG"{j} by A111,XXREAL_0:1;
                  PG"{i} is Subset of NAT by XBOOLE_1:1;
                  then min* PG"{i} <= m by A28,A87,A106,A113,NAT_1:def
                  1;
                  hence thesis by A114,XXREAL_0:2;
                end;
                suppose
A115:             i1 < F.m & j1=F.m;
                  card rF=rng Orde by A42,FUNCT_2:def 3;
                  then
A116:             0 in rng Orde by NAT_1:45;
                  Orde.i1+1 in rng Orde by A84,A59,A115;
                  then
A117:             Orde9.(Orde.i1+1)>Orde9.0 by A71,A116;
A118:             P1.j1=Orde9.0 by A44,A94,A115;
                  Orde9.(Orde.i1+1)=i by A44,A92,A93,A115;
                  hence thesis by A83,A95,A117,A118,TARSKI:def 1;
                end;
                suppose
                  i1 = F.m & j1=F.m;
                  hence thesis by A83,A95,A93,TARSKI:def 1;
                end;
                suppose
A119:             i1 > F.m & j1>F.m;
A120:             i1 in rng FD or i1 in {F.m} by A85,A80,XBOOLE_0:def
                  3;
                  then
A121:             G"{i1}=PG"{i} by A55,A87,A119,TARSKI:def 1;
A122:             P1.j1=j1 by A44,A88,A119;
A123:             j1 in rng FD or j1 in {F.m} by A89,A80,XBOOLE_0:def
                  3;
                  P1.i1=i1 by A44,A84,A119;
                  then min* G"{i1}< min* G"{j1} by A19,A20,A83,A93,A96,
                  A119,A122,A120,A123,TARSKI:def 1;
                  hence thesis by A55,A91,A119,A123,A121,TARSKI:def 1;
                end;
                suppose
A124:             i1 > F.m & j1=F.m;
A125:             dom Orde=rF by FUNCT_2:def 1;
                  rng (Orde")=dom Orde by A42,FUNCT_1:55;
                  then consider x such that
A126:             x in dom Orde9 and
A127:             Orde9.x=i1 by A84,A125,FUNCT_1:def 5;
A128:             x in card rF by A126;
                  card rF is Subset of NAT by Th8;
                  then reconsider x as Element of NAT by A128;
                  P1.i1=i1 by A44,A84,A124;
                  then
A129:             Orde9.x < Orde9.0 by A44,A83,A88,A93,A96,A124,A127;
A130:             card rF=rng Orde by A42,FUNCT_2:def 3;
                  then 0 in rng Orde by NAT_1:45;
                  then x <=0 by A71,A126,A130,A129;
                  hence thesis by A129;
                end;
                suppose
A131:             i1 < F.m & j1>F.m;
A132:             i1 in rng FD or i1 in {F.m} by A85,A80,XBOOLE_0:def
                  3;
                  then
A133:             G"{i1}=PG"{i} by A55,A87,A131,TARSKI:def 1;
A134:             j1 in rng FD or j1 in {F.m} by A89,A80,XBOOLE_0:def
                  3;
                  i1<j1 by A131,XXREAL_0:2;
                  then min* G"{i1}<min* G"{j1} by A19,A20,A131,A132,A134
                  ,TARSKI:def 1;
                  hence thesis by A55,A91,A131,A134,A133,TARSKI:def 1;
                end;
                suppose
A135:             i1 > F.m & j1<F.m;
                  then dom Orde=rF by A88,A59;
                  then
A136:             Orde.(F.m) in rng Orde by A9,FUNCT_1:def 5;
                  Orde.j1+1 <= Orde.(F.m) by A88,A59,A135;
                  then
A137:             Orde.j1+1 < Orde.(F.m) or Orde.j1+1 = Orde.(F.m) by
                  XXREAL_0:1;
                  F.m in dom Orde by A9,A88,A59,A135;
                  then
A138:             Orde9.(Orde.(F.m))=F.m by A42,FUNCT_1:56;
A139:             P1.i1=i1 by A44,A84,A135;
A140:             Orde9.(Orde.j1+1)=P1.j1 by A44,A88,A135;
                  Orde.j1+1 in rng Orde by A88,A59,A135;
                  then P1.j1<=F.m by A71,A137,A136,A138,A140;
                  hence thesis by A83,A93,A96,A135,A139,XXREAL_0:2;
                end;
              end;
              hence thesis;
            end;
            K is non empty;
            then
A142:       dom F=N by FUNCT_2:def 1;
A143:       for x st x in dom F holds F.x = P2.(G2.x)
            proof
              let x such that
A144:         x in dom F;
              now
                per cases by A144,XBOOLE_0:def 5;
                suppose
A145:             x in N\dFD;
                  then
A146:             not x in dom F\F"{F.m} by A27,XBOOLE_0:def 5;
                  x in dom F by A142,A145,XBOOLE_0:def 5;
                  then x in F"{F.m} by A146,XBOOLE_0:def 5;
                  then
A147:             F.x in {F.m} by FUNCT_1:def 13;
                  P2.(G2.x)=F.m by A22,A40,A145;
                  hence thesis by A147,TARSKI:def 1;
                end;
                suppose
A148:             x in dFD;
                  then
A149:             F.x=FD.x by FUNCT_1:70;
A150:             FD.x=P.(G.x) by A18,A148,FUNCT_1:22;
A151:             dom P=rng FD by FUNCT_2:def 1;
A152:             x in dom G by A148,FUNCT_2:def 1;
                  then
A153:             G.x in rng FD by A19,FUNCT_1:def 5;
                  G.x=G2.x by A22,A152,FUNCT_1:70;
                  hence thesis by A39,A149,A150,A153,A151,FUNCT_1:70;
                end;
              end;
              hence thesis;
            end;
A154:       dom G2=N by FUNCT_2:def 1;
            for x holds x in dom F iff x in dom G2&G2.x in dom P2
            proof
              let x;
              thus x in dom F implies x in dom G2 & G2.x in dom P2
              proof
                assume
A155:           x in dom F;
                then dom P2=rng F by A154,FUNCT_2:def 1;
                hence thesis by A38,A154,A155,FUNCT_1:def 5;
              end;
              thus thesis by A142;
            end;
            then F=P2*G2 by A143,FUNCT_1:20;
            then
A156:       F=P21*PG by A47,RELAT_1:55;
            rng P1=rF by FUNCT_2:def 3;
            then rng (P1*G2)=rF by A38,A45,FUNCT_2:20;
            hence thesis by A156,A58;
          end;
        end;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
A157: for F be Function st rng F is finite holds P[F] from Sch9(A1,A4);
  let F be Function of Ne,Ke;
  assume rng F is finite;
  then consider P be Permutation of rng F,G be Function of Ne,Ke such that
A158: F=P*G and
A159: rng F=rng G and
A160: for i,j st i in rng G& j in rng G & i<j holds min* G"{i}<min* G"{j
  } by A157;
  G is "increasing by A160,Def5;
  hence thesis by A158,A159;
end;
 
:: uniqueness of expansion

theorem Th74:
  for F be Function of Ne,Ke st rng F is finite for I1,I2 be
  Function of Ne,Me,P1,P2 be Function st P1 is one-to-one & P2 is one-to-one &
  rng I1=rng I2 & rng I1=dom P1 & dom P1=dom P2 & F=P1*I1 & F=P2*I2 & I1 is
  "increasing & I2 is "increasing holds P1=P2 & I1=I2
proof
  defpred Q[set,Function] means $1=$2.(min* dom $2);
  defpred P[set] means for Ne,Ke,Me be Subset of NAT for F be Function of Ne,
  Ke st F=$1 for I1,I2 be Function of Ne,Me, P1,P2 be Function st P1 is
one-to-one & P2 is one-to-one & rng I1=rng I2 & rng I1=dom P1 & dom P1=dom P2 &
F=P1*I1 & F=P2*I2 & I1 is "increasing & I2 is "increasing holds P1=P2 & I1=I2;
A1: P[{}]
  proof
    let Ne,Ke,Me be Subset of NAT;
    let F be Function of Ne,Ke such that
A2: F={};
    let I1,I2 be Function of Ne,Me, P1,P2 be Function such that
    P1 is one-to-one and
    P2 is one-to-one and
A3: rng I1=rng I2 and
A4: rng I1=dom P1 and
A5: dom P1=dom P2 and
A6: F=P1*I1 and
    F=P2*I2 and
    I1 is "increasing and
    I2 is "increasing;
    dom I1={} by A2,A4,A6,RELAT_1:46,60;
    then
A7: I1={};
    rng P1={} by A2,A4,A6,RELAT_1:47,60;
    then P1={};
    hence thesis by A3,A5,A7;
  end;
A8: for F be Function st for x st x in rng F & Q[x,F] holds P[F|(dom F\F"{x
  })] holds P[F]
  proof
    let F9 be Function such that
A9: for x st x in rng F9 & Q[x,F9] holds P[F9|(dom F9\F9"{x})];
    now
      per cases;
      suppose
        F9={};
        hence thesis by A1;
      end;
      suppose
A10:    F9<>{};
        let Ne,Ke,Me be Subset of NAT;
        let F be Function of Ne,Ke such that
A11:    F=F9;
        Ke is non empty by A10,A11;
        then reconsider domF=dom F as non empty Subset of NAT by A10,A11,
        FUNCT_2:def 1;
        set m=min* dom F;
        let I1,I2 be Function of Ne,Me, P1,P2 be Function such that
A12:    P1 is one-to-one and
A13:    P2 is one-to-one and
A14:    rng I1=rng I2 and
A15:    rng I1=dom P1 and
A16:    dom P1=dom P2 and
A17:    F=P1*I1 and
A18:    F=P2*I2 and
A19:    I1 is "increasing and
A20:    I2 is "increasing;
        dom I1=dom F by A15,A17,RELAT_1:46;
        then
A21:    min* rng I1=I1.m by A19,Th72;
        reconsider I=rng I1\{I1.m} as Subset of NAT by XBOOLE_1:1;
        reconsider D=dom F\F"{F.m} as Subset of NAT by XBOOLE_1:1;
A22:    for I be Function of Ne,Me,P be Function st P is one-to-one & rng
I=dom P & F=P*I & I is "increasing holds dom (I|D)=D & rng (I|D)=rng I\{I.m} &
dom (P|(rng I\{I.m}))=rng I\{I.m} & F|D=(P|(rng I\{I.m}))*(I|D) & P|(rng I\{I.m
}) is one-to-one & for M be Subset of NAT st M=rng I\{I.m} for I1 be Function
        of D,M st I1=I|D holds I1 is "increasing
        proof
A23:      F.m in {F.m} by TARSKI:def 1;
          let I be Function of Ne,Me,P be Function such that
A24:      P is one-to-one and
A25:      rng I=dom P and
A26:      F=P*I and
A27:      I is "increasing;
A28:      dom P/\(rng I\{I.m})=rng I\{I.m} by A25,XBOOLE_1:28,36;
          m in domF by NAT_1:def 1;
          then F.m in rng F by FUNCT_1:def 5;
          then consider x such that
          x in dom P and
          x in rng I and
          P"{F.m}={x} and
A29:      I"{x}=F"{F.m} by A24,A26,Th71;
A30:      dom (P|(rng I\{I.m}))=dom P/\(rng I\{I.m}) by RELAT_1:90;
A31:      dom F=dom I by A25,A26,RELAT_1:46;
          then
A32:      dom I/\D=D by XBOOLE_1:28,36;
          m in domF by NAT_1:def 1;
          then m in I"{x} by A29,A23,FUNCT_1:def 13;
          then I.m in {x} by FUNCT_1:def 13;
          then
A33:      I.m=x by TARSKI:def 1;
A34:      for M be Subset of NAT st M=rng I\{I.m} for I1 be Function of D
          ,M st I1=I|D holds I1 is "increasing
          proof
            let M be Subset of NAT such that
            M=rng I\{I.m};
            let I1 be Function of D,M such that
A35:        I1=I|D;
A36:        rng I1=rng I\{I.m} by A29,A33,A31,A35,Th64;
            let l,n such that
A37:        l in rng I1 and
A38:        n in rng I1 and
A39:        l < n;
A40:        n in rng I by A38,A36,ZFMISC_1:64;
            n<>I.m by A38,A36,ZFMISC_1:64;
            then
A41:        I1"{n}=I"{n} by A29,A33,A31,A35,Th64;
            l<>I.m by A37,A36,ZFMISC_1:64;
            then
A42:        I1"{l}=I"{l} by A29,A33,A31,A35,Th64;
            l in rng I by A37,A36,ZFMISC_1:64;
            hence thesis by A27,A39,A40,A42,A41,Def5;
          end;
          set rI=rng I\{I.m};
A43:      dom (I|D)=dom I/\D by RELAT_1:90;
A44:      rng (I|D)=rng I\{I.m} by A29,A33,A31,Th64;
A45:      for x st x in dom (F|D) holds (F|D).x = (P|rI).((I|D).x)
          proof
            let x such that
A46:        x in dom (F|D);
A47:        x in dom F/\D by A46,RELAT_1:90;
            then
A48:        x in dom F by XBOOLE_0:def 4;
            (I|D).x in dom (P|rI) by A31,A43,A30,A28,A44,A47,FUNCT_1:def
            5;
            then
A49:        (P|rI).((I|D).x)=P.((I|D).x) by FUNCT_1:70;
A50:        (F|D).x=F.x by A46,FUNCT_1:70;
            I.x=(I|D).x by A31,A43,A47,FUNCT_1:70;
            hence thesis by A26,A48,A49,A50,FUNCT_1:22;
          end;
          dom F/\D=D by XBOOLE_1:28,36;
          then dom (F|D)=D by RELAT_1:90;
          then for x holds x in dom (F|D) iff x in dom (I|D) & (I|D).x in dom
          (P|rI) by A43,A32,A30,A28,A44, FUNCT_1:def 5;
          hence thesis by A24,A29,A33,A31,A32,A28,A45,A34,Th64,FUNCT_1:20 ,
          84,RELAT_1:90;
        end;
        then
A51:    P1|I is one-to-one by A12,A15,A17,A19;
        dom I2=dom F by A14,A15,A16,A18,RELAT_1:46;
        then
A52:    min* rng I2=I2.m by A20,Th72;
        then
A53:    P2|I is one-to-one by A13,A14,A15,A16,A18,A20,A22,A21;
A54:    dom (I2|D)=D by A13,A14,A15,A16,A18,A20,A22;
A55:    dom (I1|D)=D by A12,A15,A17,A19,A22;
A56:    rng (I1|D)=I by A12,A15,A17,A19,A22;
        rng (I2|D)=I by A13,A14,A15,A16,A18,A20,A22,A21,A52;
        then reconsider I1D=I1|D,I2D=I2|D as Function of D,I by A55,A54,A56,
        FUNCT_2:3;
A57:    I2D is "increasing by A13,A14,A15,A16,A18,A20,A22,A21,A52;
A58:    rng I1D=I by A12,A15,A17,A19,A22;
        then
A59:    rng I1D=dom (P1|I) by A12,A15,A17,A19,A22;
        reconsider rFD=rng (F|D) as Subset of NAT by XBOOLE_1:1;
        dom F/\D=D by XBOOLE_1:28,36;
        then dom (F|D)=D by RELAT_1:90;
        then reconsider FD=F|D as Function of D,rFD by FUNCT_2:3;
A60:    FD=(P1|I)*I1D by A12,A15,A17,A19,A22;
A61:    FD=(P2|I)*I2D by A13,A14,A15,A16,A18,A20,A22,A21,A52;
        m in domF by NAT_1:def 1;
        then
A62:    F.m in rng F by FUNCT_1:def 5;
        dom (P1|I)=I by A12,A15,A17,A19,A22;
        then
A63:    dom (P1|I)=dom (P2|I) by A13,A14,A15,A16,A18,A20,A22,A21,A52;
A64:    I1D is "increasing by A12,A15,A17,A19,A22;
A65:    rng I1D=rng I2D by A13,A14,A15,A16,A18,A20,A22,A21,A52,A58;
        for x st x in dom P1 holds P1.x = P2.x
        proof
A66:      m in domF by NAT_1:def 1;
          dom I1=dom F by A15,A17,RELAT_1:46;
          then I1.m in rng I1 by A66,FUNCT_1:def 5;
          then
A67:      dom P1=I\/{I1.m} by A15,ZFMISC_1:140;
          let x such that
A68:      x in dom P1;
          now
            per cases by A68,A67,XBOOLE_0:def 3;
            suppose
A69:          x in I;
              dom P1/\I=I by A15,XBOOLE_1:28,36;
              then x in dom (P1|I) by A69,RELAT_1:90;
              then
A70:          (P1|I).x=P1.x by FUNCT_1:70;
              dom P2/\I=I by A15,A16,XBOOLE_1:28,36;
              then x in dom (P2|I) by A69,RELAT_1:90;
              then (P2|I).x=P2.x by FUNCT_1:70;
              hence
              thesis by A9,A11,A62,A51,A53,A65,A59,A63,A60,A61,A64,A57,
              A70;
            end;
            suppose
A71:          x in {I1.m};
A72:          m in domF by NAT_1:def 1;
A73:          x=I1.m by A71,TARSKI:def 1;
              then F.m=P1.x by A17,A72,FUNCT_1:22;
              hence thesis by A14,A18,A21,A52,A73,A72,FUNCT_1:22;
            end;
          end;
          hence thesis;
        end;
        then
A74:    P1=P2 by A16,FUNCT_1:def 17;
        I2 is Function of dom I2,rng I2 by FUNCT_2:3;
        then
A75:    I2=(id rng I2)*I2 by FUNCT_2:23;
        I1 is Function of dom I1,rng I1 by FUNCT_2:3;
        then
A76:    I1=(id rng I1)*I1 by FUNCT_2:23;
        P1"*P1 = id dom P1 by A12,FUNCT_1:61;
        then
A77:    I1=P1"*(P1*I1) by A15,A76,RELAT_1:55;
        P2"*P2 = id dom P2 by A13,FUNCT_1:61;
        hence P1=P2 & I1=I2 by A14,A15,A17,A18,A74,A75,A77,RELAT_1:55;
      end;
    end;
    hence thesis;
  end;
  for F be Function st rng F is finite holds P[F] from Sch9(A1,A8 );
  hence thesis;
end;

theorem
  for F be Function of Ne,Ke st rng F is finite for I1,I2 be Function of
Ne,Ke, P1,P2 be Permutation of rng F st F=P1*I1 & F=P2*I2 & rng F=rng I1 & rng
  F=rng I2 & I1 is "increasing & I2 is "increasing holds P1=P2 & I1=I2
proof
  let F be Function of Ne,Ke such that
A1: rng F is finite;
  let I1,I2 be Function of Ne,Ke, P1,P2 be Permutation of rng F such that
A2: F=P1*I1 and
A3: F=P2*I2 and
A4: rng F=rng I1 and
A5: rng F=rng I2 and
A6: I1 is "increasing and
A7: I2 is "increasing;
A8: rng F ={} implies rng F={};
  then
A9: dom P2=rng F by FUNCT_2:def 1;
  dom P1=rng F by A8,FUNCT_2:def 1;
  hence thesis by A1,A2,A3,A4,A5,A6,A7,A9,Th74;
end;

theorem
  for D be non empty set, F be XFinSequence of D st
  (for i st i in dom F holds F.i is finite) &
  (for i,j st i in dom F & j in dom F & i<>j holds F.i misses F.j)
  ex CardF be XFinSequence of NAT st dom CardF = dom F &
  (for i st i in dom CardF holds CardF.i=card (F.i)) &
  card union rng F = Sum(CardF) by Sch7;
