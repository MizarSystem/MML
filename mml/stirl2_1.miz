:: Stirling Numbers of the Second Kind
::  by Karol P\c{a}k
:: 
:: Received March 15, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies FUNCT_1, BOOLE, FINSEQ_1, FINSET_1, RELAT_1, ORDINAL2, CARD_1,
      TARSKI, ALGSEQ_1, ARYTM_1, AFINSQ_1, FUNCT_2, HENMODEL, INT_1, STIRL2_1,
      SGRAPH1, SQUARE_1, BINOP_1, RLVECT_1, MONOID_0, SETWISEO, FINSOP_1,
      ARYTM_3, GROUP_1, NEWTON, QC_LANG1, FUNCOP_1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, NUMBERS, XREAL_0, RELAT_1,
      FUNCT_1, RELSET_1, ORDINAL1, ORDINAL2, REAL_1, NAT_1, PARTFUN1, FINSET_1,
      CARD_1, AFINSQ_1, XCMPLX_0, FUNCT_2, CQC_SIM1, INT_1, WELLORD2, ENUMSET1,
      SQUARE_1, HENMODEL, BINOP_1, BINOP_2, CARD_2, NEWTON, PREPOWER, SETWISEO,
      FUNCOP_1;
 constructors REAL_1, NAT_1, WELLORD2, FUNCT_7, XREAL_0, MEMBERED, AFINSQ_1,
      RELAT_1, CQC_SIM1, PARTFUN1, FUNCT_2, FUNCT_1, CARD_1, ORDINAL2,
      XBOOLE_0, ZFMISC_1, ORDINAL1, TARSKI, SQUARE_1, RELAT_2, HENMODEL,
      BINOP_1, SETWISEO, FINSOP_1, BINOP_2, CARD_2, PREPOWER, POWER, FUNCOP_1;
 registrations FUNCT_1, RELAT_1, RELSET_1, CARD_1, SUBSET_1, NAT_1, XREAL_0,
      MEMBERED, NUMBERS, ORDINAL2, AFINSQ_1, BINOP_2, XBOOLE_0, FINSET_1,
      INT_1, CHAIN_1, FUNCT_2, FUNCOP_1;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions AFINSQ_1, TARSKI, FINSET_1, XBOOLE_0, FUNCT_1, SUBSET_1, ZFMISC_1;
 theorems AFINSQ_1, TARSKI, FINSEQ_1, FUNCT_1, XBOOLE_0, ZFMISC_1, RELAT_1,
      XBOOLE_1, NAT_1, FUNCT_2, RELSET_1, FINSET_1, CQC_THE1, AXIOMS, XCMPLX_1,
      REAL_1, CARD_2, INT_1, ORDINAL1, WELLORD2, EULER_1, FRAENKEL, SUBSET_1,
      SQUARE_1, CQC_SIM1, HENMODEL, ENUMSET1, CARD_1, BINOP_1, CARD_5, BINOP_2,
      NEWTON, WSIERP_1, SETWISEO, TMAP_1, FUNCOP_1;
 schemes FUNCT_1, FUNCT_2, NAT_1, INT_1, INT_2;

begin
 
 reserve k, l, m, n, i, j for Nat,
         K, N for non empty Subset of NAT,
         Ke, Ne, Me for Subset of NAT,
         X,Y for set;
         
definition let k;
  redefine func {k} -> Subset of NAT;
  coherence by SUBSET_1:55;
  let l;
  redefine func {k,l} -> Subset of NAT;
  coherence by SUBSET_1:56;
  let m;
  redefine func {k,l,m} -> non empty Subset of NAT;
  coherence by SUBSET_1:57,ENUMSET1:def 1;
end;

theorem Th1:
  min N = min* N
proof
  min* N in N & for n st n in N holds min* N <= n by HENMODEL:def 1;
  hence thesis by CQC_SIM1:def 8;
end;

theorem Th2:
  min(min K,min N) = min(K\/N)
proof
     set m=min(min N,min K);
   A1:m<=min (N\/K) 
     proof
        m=min N or m=min K by SQUARE_1:38;
        then m in N or m in K by CQC_SIM1:def 8;
        then A2:m in N\/K by XBOOLE_0:def 2;
        for k st k in N\/K holds m<=k
          proof
            let k such that A3:k in N\/K;
            k in N or k in K by A3,XBOOLE_0:def 2;
            then (min N<=k or min K <=k) & m<=min N &m<=min K
              by CQC_SIM1:def 8,SQUARE_1:35;
            hence thesis by AXIOMS:22;
          end;
        hence thesis by A2,CQC_SIM1:def 8;
      end;   
   min (N\/K) <= min(min N,min K)
     proof
       N c= N\/K & K c= N\/K by XBOOLE_1:7;
       then min (N\/K)<=min N & min (N\/K)<=min K by CQC_SIM1:19;
       hence thesis by SQUARE_1:39;
     end;
  hence thesis by A1,AXIOMS:21;
end;

theorem
  min(min* Ke,min* Ne) <= min* (Ke\/Ne)
proof
  now per cases;
    suppose Ke is empty or Ne is empty;
      then (min* Ke=0 & min* Ne >=0) or (min* Ne=0 & min*Ke>=0)
        by HENMODEL:def 1, NAT_1:18;
      then min(min* Ke,min* Ne)=0 by SQUARE_1:def 1;
      hence thesis by NAT_1:18;
    end;
    suppose Ke is non empty & Ne is non empty;
      then reconsider K=Ke,N=Ne as non empty Subset of NAT;
      min K=min* Ke & min N=min* Ne & min(K\/N)=min*(Ke\/Ne) by Th1;
      hence thesis by Th2;
    end;
  end;
  hence thesis;
end;

theorem
  not min* Ne in Ne/\Ke implies min* Ne = min* (Ne\Ke)
proof
  assume A1:not min* Ne in Ne/\Ke;
  now per cases;
    suppose Ne is empty; 
      hence thesis; 
    end;
    suppose Ne is non empty;
      then A2:min* Ne in Ne by HENMODEL:def 1;
      then not min* Ne in Ke by A1,XBOOLE_0:def 3;
      then A3:min* Ne in Ne\Ke by A2,XBOOLE_0:def 4;
      then min* (Ne\Ke) in Ne\Ke & Ne\Ke c=Ne by XBOOLE_1:36,HENMODEL:def 1;
      then min* (Ne\Ke)<=min* Ne & min* Ne<=min* (Ne\Ke) by A3,HENMODEL:def 1;
      hence thesis by AXIOMS:21;   
    end;
  end;
  hence thesis;   
end;

theorem Th5:
  min* {n} = n & min {n} = n
proof
  min* {n} in {n} & min*{n}=min {n} by Th1,HENMODEL:def 1;
  hence thesis by TARSKI:def 1;
end;

theorem Th6:
  min* {n,k} = min(n,k) & min {n,k} = min(n,k)
proof
  {n,k}={n}\/{k} by ENUMSET1:41;
  then min {n,k} = min(min {n},min {k})
     & min {n,k} = min* {n,k} & min {n} = n by Th1,Th2,Th5;
  hence thesis by Th5;
end;

theorem
  min* {n,k,l} = min(n,min(k,l))
proof
  {n,k,l}={n}\/{k,l} by ENUMSET1:42;
  then min{n,k,l} = min(min {n},min {k,l}) & min {k,l}=min(k,l)
     & min {n,k,l}=min* {n,k,l} by Th1,Th2,Th6;
  hence thesis by Th5;
end;

theorem Th8:
  n is Subset of NAT
proof
  now let x be set such that A1:x in {l: l<n};
    ex l st x=l & l<n by A1;
    hence x in NAT;
  end;  
  then {l:l<n} c= NAT by TARSKI:def 3;
  hence thesis by AXIOMS:30;
end;

registration
  let n;
  cluster -> natural Element of n;
  coherence
proof
  per cases by NAT_1:18;
    suppose A1:n=0;
      let k be Element of n;
      thus thesis by A1,SUBSET_1:def 2;
    end;
    suppose A1:n>0;
      let k be Element of n;
      k in n & n is Subset of NAT by A1,Th8;
      then k is Nat;
      hence thesis;
    end;    
end;
end;
  
theorem
  N c= n implies n-1 is Nat
proof
  assume A1:N c= n;
  min* N in N by HENMODEL:def 1;
  then min*N in n by A1;
  then min* N in {l:l<n} by AXIOMS:30;
  then ex l st min* N = l & l<n;
  hence thesis by NAT_1:60;
end;

theorem Th10:
  k in n implies k<=n-1 & n-1 is Nat
proof
  assume k in n;
  then k in {l:l<n} by AXIOMS:30;
  then ex l st k=l & l<n;
  then k<(n-1)+1 & n-1 is Nat by NAT_1:60;
  hence thesis by NAT_1:38;
end;

theorem
  min* n = 0
proof
  now per cases by NAT_1:18;
    suppose 0=n;
      hence thesis by HENMODEL:def 1;
    end;
    suppose 0<n;
      then 0 in {l:l<n};
      then 0 in n & n is Subset of NAT by Th8,AXIOMS:30;
      then min* n <=0 by HENMODEL:def 1;            
      hence thesis by NAT_1:18;
    end;
  end;
  hence thesis;
end;    
        
theorem Th12:
  N c= n implies min* N <= n-1
proof
  assume A1:N c= n;
  min* N in N by HENMODEL:def 1;
  hence thesis by A1,Th10;
end;
  
theorem
  N c= n & N <> {n-1} implies min* N < n-1
proof
  assume that A1:N c= n and A2:N<>{n-1} and A3:min* N >=n-1;
  now let k be set such that A4:k in N;
    reconsider k'=k as Nat by A4;
    min* N <=k' & k' in n by A1,A4,HENMODEL:def 1;
    then n-1 <=k' & k' <= n-1 by A3,Th10,AXIOMS:22;
    then n-1 = k by AXIOMS:21;
    hence k in {n-1} by TARSKI:def 1;
  end;
  then N c= {n-1} by TARSKI:def 3;
  hence thesis by A2,ZFMISC_1:39;  
end;

theorem Th14:
  Ne c= n & n>0 implies min* Ne <= n-1
proof
  assume that A1:Ne c= n and A2:n>0;
  now per cases;
    suppose Ne is non empty;
      hence thesis by A1,Th12;
    end;
    suppose Ne is empty;
      then min* Ne =0 & n-1 is Nat by A2,HENMODEL:def 1,NAT_1:60;
      hence thesis by NAT_1:18;
    end;
  end;
  hence thesis;
end;

reserve f,g for Function of n,k;  
      
definition let n,X; let f be Function of n,X; let x be set;
  redefine func f"x -> Subset of NAT;
  coherence
  proof
    now let y be set such that A1:y in f"x;
      y in dom f & f.y in x & n is Subset of NAT by A1,Th8,FUNCT_1:def 13;
      hence y in NAT by TARSKI:def 3;      
    end;
    hence thesis by TARSKI:def 3;
end;
end;

definition let X,k; let f be Function of X,k; let x be set;
  redefine func f.x -> Element of k;
  coherence
  proof
    per cases;
      suppose x in dom f;
        then f.x in rng f & rng f c= k by FUNCT_1:12;
        hence f.x is Element of k;
      end;
      suppose not x in dom f;
        then f.x=0 & (k=0 or k>0) by FUNCT_1:def 4,NAT_1:18;
        hence thesis by SUBSET_1:def 2,EULER_1:1;
      end;
  end;
end;

registration
  let X,Ne;
  let f be Function of X,Ne,x be set;
  cluster f.x -> natural;
coherence
proof
  per cases;
  suppose x in dom f;
    then f.x in rng f & rng f is Subset of NAT by FUNCT_1:def 5,XBOOLE_1:1;
    then f.x is Nat;
    hence thesis;
  end;
  suppose not x in dom f;
  hence thesis by FUNCT_1:def 4;
end;
end;
end;
  
definition
  let n,k;let f be Function of n,k;
  attr f is "increasing means :Def1:
   (n = 0 iff k = 0) &
   for l,m st l in rng f & m in rng f & l < m holds min* f"{l} < min* f"{m};
end;
  
theorem Th15:
  n=0 & k=0 implies f is onto "increasing
proof
  assume A1:n=0 & k=0;
  then rng f = {} & for l,m st l in k & m in k & l < m holds
                                 min* f"{l} < min* f"{m} by XBOOLE_1:3;
  hence thesis by A1,Def1,FUNCT_2:def 3;
end;

theorem Th16:
  n>0 implies min* f"{m} <= n-1
proof
  assume A1:n>0;
  f"{m} c= n
  proof
    let x be set such that A6:x in f"{m};
    x in dom f by A6,FUNCT_1:def 13;
    hence thesis; 
  end;
  hence thesis by A1,Th14;
end;
  
theorem Th17:
  f is onto implies n>=k
proof
  assume A1:f is onto;
  now per cases;
    suppose k=0;      
      hence thesis by NAT_1:18;
    end;
    suppose k<>0;
      then dom f =n & rng f = k by A1,FUNCT_2:def 1,def 3;
      then Card k <=` Card n by CARD_1:28; 
      then card k <= card n & card n = n by CARD_2:57,FINSEQ_1:78;
      hence thesis by FINSEQ_1:78;
    end;
  end;
  hence thesis;
end;

theorem Th18:
  f is onto "increasing implies
    for m st m < k holds m <= min* f"{m}
proof
  assume that A1:f is onto "increasing;  
  now per cases by NAT_1:18;
    suppose k=0;
      hence thesis by NAT_1:18;
    end;
    suppose k>0;
      defpred M[Nat] means
        $1<k implies $1<=min* f"{$1};
      A2:M[0] by EULER_1:1;
      A3:for m st M[m] holds M[m + 1]
        proof
          let m such that A4:M[m];
          assume A5:m+1<k;
          m<m+1 by NAT_1:59;
          then m < k & k=rng f by A1,A5,AXIOMS:22,FUNCT_2:def 3;
          then m <= min* f"{m} & m in rng f & m+1 in rng f & m < m+1
            by A4,A5,EULER_1:1,NAT_1:59;
          then min* f"{m}< min* f"{m+1} & m < k by A1,A5,Def1,AXIOMS:22;
          then m < min* f"{m+1} by A4,AXIOMS:22;
          hence m+1 <=min* f"{m+1} by NAT_1:38;            
        end;       
      for m holds M[m] from NAT_1:sch 1(A2,A3);                  
      hence thesis;
    end;
  end;
  hence thesis;
end;  

theorem Th19:
  f is onto "increasing implies
    for m st m < k holds min* f"{m} <= n-k+m
proof
  assume that A1:f is onto "increasing;
  now per cases by NAT_1:18;
    suppose k=0;
      hence thesis by NAT_1:18;
    end;
    suppose A2:k>0;
      defpred I[Integer] means
        0<=$1 implies min* f"{$1} <= n-k+$1;         
      reconsider k1=k-1 as Nat by A2,NAT_1:60;        
      A3:0<=k1 & k1<k1+1 by NAT_1:18,38;
      A4:I[k1]
        proof
          assume 0<= k1;
          k=0 iff n=0 by A1,Def1;
          then n>0 by A2,NAT_1:18;
          then min* f"{k1}<=n-1 by Th16;
          hence thesis;         
        end;        
      A5:for m be Integer st m <= k1 holds I[m] implies I[m-1]
        proof
          let m be Integer such that A6:m <= k1;
          assume A7:I[m];
          n>=k by A1,Th17;
          then reconsider nk=n-k as Nat by NAT_1:61;
          assume that A8:0<=m-1;
          reconsider m1=m-1 as Nat by A8,INT_1:16;          
          A9:m1<m1+1 & m < k by A3,A6,NAT_1:59,AXIOMS:22;
          then m1<k by AXIOMS:22;
          then m1<k & m1 in k & m1+1 in k & k = rng f
            by A1,A9,EULER_1:1,FUNCT_2:def 3;
          then min* f"{m1}<min* f"{m1+1} & min* f"{m1+1}<=n-k+m
            by A1,A7,A8,A9,Def1;
          then min* f"{m1} < (nk+m1)+1 by AXIOMS:22;
          hence thesis by NAT_1:38;
        end;
      A6:for m be Integer st m <= k1 holds I[m] from INT_1:sch 3(A4,A5);
      now let m such that A7:m < k;
        m<k1+1 by A7;
        then 0<=m & m<=k1 by NAT_1:18,38;
        hence min* f"{m} <= n-k+m by A6;
      end; 
      hence thesis;
    end;
  end;
  hence thesis;
end;

theorem Th20:
  f is onto "increasing & n = k implies f = id n
proof
  assume that A1:f is onto "increasing and A2:n=k;
   now per cases by NAT_1:18;
    suppose A3:n=0;
      then dom f=0 & for x be set st x in 0 holds f.x=x by FUNCT_2:def 1;
      hence thesis by A3,FUNCT_1:34;
    end;
    suppose A3:n>0;
      A4:now let m' be set such that A5:m' in n;
          n is Subset of NAT by Th8; 
          then reconsider m=m' as Nat by A5;        
          m in rng f by A1,A2,A5,FUNCT_2:def 3;
          then m in {m} & ex x be set st x in dom f & f.x =m
            by FUNCT_1:def 5,TARSKI:def 1;
          then reconsider F=f"{m} as non empty Subset of NAT by FUNCT_1:def 13;
          0<=m & m < k & n-k+m =m by A2,A5,EULER_1:1;
          then m <= min* f"{m} & min* f"{m}<= m by A1,Th18,Th19;
          then min* F=m & min* F in F by HENMODEL:def 1,AXIOMS:21;
          then f.m in {m} by FUNCT_1:def 13;
          hence f.m'=m' by TARSKI:def 1;
        end;
      dom f = n by A2,A3,FUNCT_2:def 1;   
      hence thesis by A4,FUNCT_1:34;
    end;
  end;
  hence thesis;
end;  

theorem
  f = id n & n > 0 implies f is "increasing
proof
  assume that A1:f = id n and A2:n>0;  
  rng f=n by A1,RELAT_1:71;
  then A3:n c= k & ex x be set st x in n by A2,XBOOLE_0:def 1;
  now let l,m such that A4:l in rng f & m in rng f & l<m;
    consider l' be set such that
      A5:l' in dom f & f.l'=l by A4,FUNCT_1:def 5;
    consider m' be set such that
    A6:m' in dom f & f.m'=m by A4,FUNCT_1:def 5;
    l = l'& l in {l} & m = m'& m in {m} & f is one-to-one
      by A1,A5,A6,TARSKI:def 1,FUNCT_1:35,52;
    then l in f"{l} & m in f"{m} &
      (ex x be set st f"{l} = {x}) & ex x be set st f"{m} = {x}
       by A4,A5,A6,FUNCT_1:def 13,144;
    then f"{l}={l} & f"{m}={m} by TARSKI:def 1;
    then min* f"{l} = l & min* f"{m}=m by Th5;
    hence min* f"{l} < min* f"{m} by A4;
   end;
   hence thesis by A3,Def1;
 end;
 
theorem
  (n=0 iff k=0) implies
    ex f be Function of n,k st f is "increasing
proof
  assume A1:n=0 iff k=0;
  now per cases by NAT_1:18;
    suppose A2:n=0;set f={};
      rng f= k & dom f = n by A1,A2;
      then reconsider f as Function of n,k by FUNCT_2:3;
      f is "increasing by A1,A2,Th15;
      hence thesis;
    end;
    suppose A2:n>0;
      then consider f be Function such that
        A3:dom f=n & rng f={0} by FUNCT_1:15;
      reconsider f as Function of n,{0} by A3,FUNCT_2:3;  
      A5:now let l,m such that A6:l in rng f & m in rng f & l < m;
          l=0 & m =0 by A6,TARSKI:def 1;
          hence min* f"{l} < min* f"{m} by A6;                    
      end;    
      rng f c= k
      proof
        let x be set such that A6:x in rng f;
        x in {0} & 0<k by A1,A2,A6,NAT_1:18;
        then x=0 & 0 in k by TARSKI:def 1,EULER_1:1;
        hence thesis;
      end;    
      then reconsider f as Function of n,k by FUNCT_2:8;
      for l,m st l in rng f & m in rng f & l < m
      holds min* f"{l} < min* f"{m} by A5;
      then f is "increasing by A1,Def1;
      hence thesis;    
    end;
  end;
  hence thesis;
end;

theorem Th23:
  (n=0 iff k=0) & n>=k implies
    ex f be Function of n,k st f is onto "increasing
proof
  assume A1:(n=0 iff k=0) & n>=k;
  now per cases by NAT_1:18;
    suppose A2:n=0;set f={};
      rng f= k & dom f = n by A1,A2;
      then reconsider f as Function of n,k by FUNCT_2:3;
      f is onto "increasing by A1,A2,Th15;
      hence thesis;
    end;
    suppose A2:n>0;
      then k>0 by A1,NAT_1:18;
      then reconsider k1=k-1 as Nat by NAT_1:60;
      reconsider k,n as non empty Nat by A1,A2;
      defpred F[Element of n,Element of k] means $2=min($1,k1);
      A3:for x be Element of n ex y be Element of k st F[x,y]
        proof
          let x be Element of n;
          min(x,k1)<=k1 & k1<k1+1 by SQUARE_1:35,NAT_1:38;
          then min(x,k1)<k & min(x,k1) is natural by AXIOMS:22,SQUARE_1:38;
          then min(x,k1) in k by EULER_1:1;
          hence ex y be Element of k st F[x,y];
        end;  
      consider f be Function of n,k such that
        A4:for m be Element of n holds F[m,f.m] from FUNCT_2:sch 3(A3);
      A5:f is onto
        proof
          now let m' be set such that A6:m' in k;
            k is Subset of NAT by Th8;
            then reconsider m=m' as Nat by A6;
            m<k1+1 by A6,EULER_1:1;
            then m <n & m<=k1 by A1,AXIOMS:22,NAT_1:38;
            then m in n & min(m,k1)=m by EULER_1:1,SQUARE_1:def 1;
            then f.m=m & m in dom f by A4,FUNCT_2:def 1;
            hence m' in rng f by FUNCT_1:def 5;
          end;
          then k c= rng f & rng f c= k by TARSKI:def 3;
          then k=rng f by XBOOLE_0:def 10;
          hence thesis by FUNCT_2:def 3;
        end;
      A6:for m st m in rng f holds min* f"{m} =m
        proof
          let m such that A7: m in rng f;
          A8:m<k1+1 by A7,EULER_1:1;
          then m <n & m<=k1 by A1,AXIOMS:22,NAT_1:38;
          then m in n & min(m,k1)=m by EULER_1:1,SQUARE_1:def 1;
          then f.m=m & m in dom f by A4,FUNCT_2:def 1;
          then f.m in {m} & m in dom f by TARSKI:def 1;
          then A9:f"{m} is non empty & m in f"{m} by FUNCT_1:def 13;
          A10:m<=k1 by A8,NAT_1:38;
          now per cases by A10,REAL_1:def 5;
            suppose A11:m<k1;
              now let l' be set such that A12:l' in f"{m};
                l' in dom f by A12,FUNCT_1:def 13;
                then l' in n & n is Subset of NAT by Th8;       
                then reconsider l=l' as Nat;
                l in dom f & f.l in {m} by A12,FUNCT_1:def 13;
                then l in n & f.l=m by TARSKI:def 1;
                then min(l,k1)=m by A4;
                then l=m by SQUARE_1:38,A11;
                hence l' in {m} by TARSKI:def 1;
              end;
              then f"{m} c= {m} & min* f"{m} in f"{m}
                by A9,TARSKI:def 3,HENMODEL:def 1;
              hence thesis by TARSKI:def 1;
            end;
            suppose A11:m=k1;
              for l st l in f"{m} holds m <= l
              proof
                let l such that A12:l in f"{m};
                l in dom f & dom f =n & f.l in {m}
                  by A12,FUNCT_1:def 13,FUNCT_2:def 1;
                then f.l=min(l,m) & f.l=m by A4,A11,TARSKI:def 1;
                hence thesis by SQUARE_1:def 1;
              end;
              hence thesis by A9,HENMODEL:def 1;
            end;
          end;
          hence thesis;
        end;
      now let l,m such that A7:l in rng f & m in rng f & l < m;
        min* f"{l}=l & min* f"{m}=m by A6,A7;
        hence min* f"{l} < min* f"{m} by A7;        
      end;   
      then f is "increasing by Def1;
      hence thesis by A5; 
    end;
  end;
  hence thesis;
end;

scheme Sch1{n,k() -> Nat,P[set]}:
  {f where f is Function of n(),k():P[f]} is finite 
proof
  set F= {f where f is Function of n(),k():P[f]};
  now per cases by NAT_1:18;
    suppose A1:k()=0;
      F c= {{}}
      proof
        let x be set such that A2:x in F;        
        consider f be Function of n(),k() such that
          A3:x=f & P[f] by A2;
        rng f={} by A1,XBOOLE_1:3;
        then f ={} by RELAT_1:64;
        hence thesis by A3,TARSKI:def 1;
      end;     
      hence thesis by FINSET_1:13;      
    end;
    suppose A1:k()>0;
      A2:F c= Funcs(n(),k())
      proof
        let x be set such that A3:x in F;
        ex f be Function of n(),k() st x=f & P[f] by A3;
        hence thesis by A1,FUNCT_2:11;     
      end;
      Funcs(n(),k()) is finite by FRAENKEL:16;
      hence thesis by A2,FINSET_1:13;   
    end;
  end;
  hence thesis;
end;

theorem Th24:
  for n,k holds
    {f where f is Function of n,k:f is onto "increasing} is finite
proof
  let n,k;  
  defpred P[Function of n,k] means $1 is onto "increasing;
  {f where f is Function of n,k:P[f]} is finite from Sch1;
  hence thesis;
end;
    
theorem Th25:
  for n,k holds
     Card {f where f is Function of n,k:f is onto "increasing} is Nat
proof
  let n,k;
  set F= {f where f is Function of n,k:f is onto "increasing};
  F is finite by Th24;
  then consider m such that
    A2:F,m are_equipotent by CARD_1:74;
  Card F=Card m & Card m=m by A2,CARD_1:21,66;
  hence thesis; 
end;

:::::::::::::::::::::::::::::::::::::
::   Number Stirling second kind   ::
:::::::::::::::::::::::::::::::::::::

definition
  let n,k;
  func n block k -> Nat equals:Def2:
    Card {f where f is Function of n,k:f is onto "increasing};
  coherence by Th25;
end;

theorem Th26:
  n block n = one
proof
  set F={f where f is Function of n,n:f is onto "increasing};
  A1:F c= {id n}
  proof
    let x be set such that A2:x in F;
    consider f be Function of n,n such that
      A3:x=f & f is onto "increasing by A2;
    f=id n by A3,Th20;
    hence thesis by A3,TARSKI:def 1;
  end;
  (n=0 iff n=0 ) & n>=n;
  then consider f be Function of n,n such that
    A3:f is onto "increasing by Th23;
  f in F by A3;
  then F={id n} by A1,ZFMISC_1:39;
  then Card F =one by CARD_1:50;
  hence thesis by Def2;
end;

theorem Th27:
  k<>0 implies 0 block k = 0
proof
  assume A1:k<>0;  
  set F={f where f is Function of 0,k:f is onto "increasing};
  F ={}
  proof
    assume F<>{};
    then consider x be set such that
      A2:x in F by XBOOLE_0:def 1;
    consider f be Function of 0,k such that
      A3:x=f & f is onto "increasing by A2;
    thus thesis by A1,A3,Def1;
  end;
  hence thesis by Def2,CARD_1:47;
end;

theorem
  0 block k = one iff k=0 by Th26,Th27;

theorem Th29:
  n<k implies n block k = 0
proof
  assume A1:n<k;
  set F={f where f is Function of n,k:f is onto "increasing};
  F ={}
  proof
    assume F<>{};
    then consider x be set such that
      A2:x in F by XBOOLE_0:def 1;
    consider f be Function of n,k such that
      A3:x=f & f is onto "increasing by A2;
    thus contradiction by A1,A3,Th17;
  end;
  hence thesis by Def2,CARD_1:47;
end;
  
theorem
  n block 0 = one iff n=0
proof
  n block 0 = one implies n=0
  proof
    set F={f where f is Function of n,0:f is onto "increasing};
    assume n block 0=one;
    then Card F =one & Card {{}}= one by Def2,CARD_1:50;
    then consider x be set such that
      A1:F={x} by CARD_1:49;
    x in F by A1,TARSKI:def 1;
    then consider f be Function of n,0 such that
      A2:x=f & f is onto "increasing;
    thus thesis by A2,Def1;
  end;
  hence thesis by Th26;
end;  

theorem Th31:
  n<>0 implies n block 0 = 0
proof
  assume A1:n<>0;
  set F={f where f is Function of n,0:f is onto "increasing};
  F ={}
  proof
    assume F<>{};
    then consider x be set such that
      A2:x in F by XBOOLE_0:def 1;
    consider f be Function of n,0 such that
      A3:x=f & f is onto "increasing by A2;
    thus contradiction by A1,A3,Def1;
  end;
  hence thesis by CARD_1:47,Def2;
end;
  
theorem Th32:
  n<>0 implies n block 1 = one
proof
  assume n<>0;
  then n>0 by NAT_1:18;
  then (n=0 iff 1=0) & n >=1+0 by NAT_1:38;
  then consider f be Function of n,1 such that
    A2:f is onto "increasing by Th23;
  set F={g where g is Function of n,1:g is onto "increasing};
  A3:F c= {f}
  proof
    let x be set such that A4:x in F;
    consider g be Function of n,1 such that
      A5:x=g & g is onto "increasing by A4;  
    f=g by CARD_5:1,FUNCT_2:66;  
    hence x in {f} by A5,TARSKI:def 1;
  end;
  f in F by A2;
  then F={f} by A3,ZFMISC_1:39;
  then Card F =one by CARD_1:50;
  hence thesis by Def2;
end;

Th33:
  Card X = 0 iff X is empty by CARD_2:59,CARD_1:47;

theorem
  (1<=k & k <=n) or k=n iff n block k > 0
proof
  thus (1<=k & k <=n) or k=n implies n block k > 0
  proof
    assume (1<=k & k <=n) or k=n;
    then (k=0 iff n=0) & k <=n;
    then consider f such that
      A1:f is onto "increasing by Th23;
    set F={g where g is Function of n,k:g is onto "increasing};
    f in F by A1;
    then Card F <>0 by Th33;
    then n block k <>0 by Def2;
    hence thesis by NAT_1:18;
  end;
  thus n block k > 0 implies (1<=k & k <=n) or k=n
  proof
    assume A1:n block k >0;
    assume not ((1<=k & k <=n) or k=n);
    then (1+0>k or k>n) & k<>n;
    then (0>=k & k>=0 or k>n) & k<>n by NAT_1:18,38;
    then (k=0 & n<>k) or k>n;
    hence contradiction by A1,Th29,Th31;
  end;  
end;

reserve x,y for set;

scheme Sch2{X,Y,X1,Y1()->set,f()->Function of X(),Y(),F(set)->set}:
  ex h be Function of X1(),Y1() st
   h|X() = f() & for x st x in X1()\X() holds h.x = F(x)
provided
A1:for x st x in X1()\X() holds F(x) in Y1() and
A2:X() c= X1() & Y() c= Y1() and
A3:Y() is empty implies X() is empty
proof
  defpred P[set,set] means
    ($1 in X() implies $2 = f().$1) &
    ($1 in X1()\X() implies $2=F($1));
  A4:for x st x in X1() ex y st y in Y1() & P[x,y]  
  proof
    let x such that A5:x in X1();
    now per cases;
      suppose x in X();
        then x in dom f() by A3,FUNCT_2:def 1;
        then f().x in rng f() & not x in X1()\X()
          by FUNCT_1:def 5,XBOOLE_0:def 4;
        then P[x,f().x] & f().x in Y1() by A2,TARSKI:def 3;        
        hence thesis;
      end;
      suppose A6:not x in X();
        then x in X1()\X() by A5,XBOOLE_0:def 4;
        then P[x,F(x)] & F(x) in Y1() by A1,A6;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
  consider h be Function of X1(),Y1() such that
    A5:for x st x in X1() holds P[x,h.x] from FUNCT_2:sch 1(A4);
  take h;
  thus h|X() = f()
  proof
    A6:dom f() = dom h /\ X()
    proof
      now per cases;
        suppose Y() is empty;
          hence thesis by A3,FUNCT_2:def 1;
        end;
        suppose A7:Y() is non empty;
          then Y1() is non empty by XBOOLE_1:3,A2;
          then dom f()=X() & dom h =X1() by A7,FUNCT_2:def 1;          
          hence thesis by A2,XBOOLE_1:28;
        end;
      end;
      hence thesis;      
    end;
    for x st x in dom f() holds h.x = f().x
    proof
      let x such that A7:x in dom f();
      x in X() by A7;
      hence thesis by A2,A5;
    end;
    hence thesis by A6,FUNCT_1:68;
    end;
    thus for x st x in X1()\X() holds h.x = F(x)
    proof
      let x such that A6:x in X1()\X();
      x in X1() by A6,XBOOLE_0:def 4;
      hence thesis by A6,A5;
    end;  
end;

scheme Sch3{X,Y,X1,Y1()->set,F(set)->set,P[set,set,set]}:
  Card{f where f is Function of X(),Y():
                P[f,X(),Y()]}=
  Card{f where f is Function of X1(),Y1():
                P[f,X1(),Y1()]&
                rng (f|X()) c=Y()&                
                (for x st x in X1()\X() holds f.x=F(x))}
provided
Z1:for x st x in X1()\X() holds F(x) in Y1() and
Z2:X() c= X1() & Y() c= Y1() and
Z3:Y() is empty implies X() is empty and
Z5:for f be Function of X1(),Y1() st
     (for x st x in X1()\X() holds F(x)=f.x)
  holds
    P[f,X1(),Y1()] iff P[f|X(),X(),Y()]
proof
  set F1={f where f is Function of X(),Y():
                               P[f,X(),Y()]};
  set F2={f where f is Function of X1(),Y1():
                               P[f,X1(),Y1()]&
                               rng (f|X()) c=Y()& 
                               for x st x in X1()\X() holds f.x=F(x)};
Z4:Y1() is empty implies X1() is empty                            
  proof
    assume A1:Y1() is empty;
    assume X1() is non empty;
    then consider x such that
      A2:x in X1() by XBOOLE_0:def 1;
    thus contradiction by A1,A2,Z1,Z2,Z3,XBOOLE_1:3;
  end;                                                              
   defpred FF[set,set] means
     for f be Function of X(),Y(),h be Function of X1(),Y1()
        st f=$1 & h=$2
          holds h|X()=f;
  A1:for f' be set st f' in F1 ex g' be set st g' in F2 & FF[f',g']
  proof
    let f' be set such that A2:f' in F1;
    consider f be Function of X(),Y() such that
      A3:f=f' & P[f,X(),Y()] by A2;
    consider h be Function of X1(),Y1() such that
      A4:h|X() = f & for x st x in X1()\X() holds h.x=F(x) from Sch2(Z1,Z2,Z3);
    P[h,X1(),Y1()] & rng f c= Y() by A3,A4,Z5;
    then h in F2 & FF[f',h] by A3,A4;
    hence thesis;
  end;
  consider ff be Function of F1,F2 such that
    A2:for x be set st x in F1 holds FF[x,ff.x] from FUNCT_2:sch 1(A1);
  now per cases;
    suppose A3:Y1() is empty;
      A4:F1 c= {{}}
      proof
        let x such that A5:x in F1;
        consider f be Function of X(),Y() such that
          A6:f=x & P[f,X(),Y()] by A5;
        Y() is empty by A3,Z2,XBOOLE_1:3;
        then rng f={} by XBOOLE_1:3;
        then x={} by A6,RELAT_1:64;
        hence thesis by TARSKI:def 1;
      end;
      A5:F2 c= {{}}
      proof
        let x such that A6:x in F2;
        consider f be Function of X1(),Y1() such that
          A7:f=x & P[f,X1(),Y1()] & rng (f|X()) c=Y() &
             for x st x in X1()\X() holds f.x=F(x) by A6;
        rng f={} by A3,XBOOLE_1:3;
        then x={} by A7,RELAT_1:64;
        hence thesis by TARSKI:def 1;
      end;
      set Empty={};
      now per cases;
        suppose A6:P[{},{},{}];
          Y() is empty & dom {}={} & rng {}={} by A3,Z2,XBOOLE_1:3;
          then reconsider Empty as Function of X1(),Y1() by A3,Z4,FUNCT_2:3;
          Y() is empty & dom {}={} & rng {}={} & {}|X() c= {}
          by A3,Z2,XBOOLE_1:3,RELAT_1:88;
          then {} is Function of X(),Y() & rng (Empty|X()) c= Y() &
               X()={} & Y()={} & (for x st x in X1()\X() holds Empty.x=F(x))
               by A3,Z3,Z4,FUNCT_2:3;
          then {} in F1 & {} in F2 by A6,A3,Z4;
          then F1={{}} & F2={{}} by A4,A5,ZFMISC_1:39;
          hence thesis; 
          end;
          suppose A6:not P[{},{},{}];
            A7:not {} in F1
            proof
              assume {} in F1;
              then Y() is empty & X() is empty &
                ex f be Function of X(),Y() st f={} & P[f,X(),Y()]
                by Z3,A3,Z2,XBOOLE_1:3;
              hence contradiction by A6;
            end;
            A8:not {} in F2
            proof
              assume {} in F2;
              then X1()={} & Y1()={} & ex f be Function of X1(),Y1()
                st f={} & P[f,X1(),Y1()] & rng (f|X()) c=Y() &
                for x st x in X1()\X() holds f.x=F(x) by A3,Z4;
              hence contradiction by A6;
            end;
            (F1={} or F1={{}}) & (F2={} or F2={{}}) by A4,A5,ZFMISC_1:39;
            hence thesis by A7,A8,TARSKI:def 1;
          end;
        end; 
        hence thesis;
      end;
      suppose A3:Y1() is non empty;
        now per cases;
          suppose A4:F2 is empty;
            F1 is empty
            proof
              assume F1 is non empty;
              then consider x such that
                A5:x in F1 by XBOOLE_0:def 1;
              consider f be Function of X(),Y() such that
                A6:f=x & P[f,X(),Y()] by A5;
              consider h be Function of X1(),Y1() such that
                A7:h|X() = f & for x st x in X1()\X() holds h.x=F(x)
                from Sch2(Z1,Z2,Z3);
              P[h,X1(),Y1()] & rng f c= Y() by A6,A7,Z5;
              then h in F2 by A7;
              hence thesis by A4;  
            end;
            hence thesis by A4;
          end;
          suppose A4:F2 is non empty;
            F2 c= rng ff
            proof
              let y such that A5:y in F2;
              consider f be Function of X1(),Y1() such that
                A6:f=y & P[f,X1(),Y1()]& rng (f|X()) c=Y()& 
                   for x st x in X1()\X() holds f.x=F(x) by A5;
              dom (f|X())=dom f /\ X() & dom f =X1()
                by A3,FUNCT_1:68,FUNCT_2:def 1;
              then A7:dom (f|X())=X() by Z2,XBOOLE_1:28;
              then reconsider h=f|X() as Function of X(),rng(f|X())
                by FUNCT_2:3;              
              rng (f|X()) is empty implies X() is empty by A7,RELAT_1:65;
              then reconsider h as Function of X(),Y() by A6,FUNCT_2:8;
              P[f|X(),X(),Y()] by A6,Z5;
              then A8:h in F1 & dom ff=F1 by A4,FUNCT_2:def 1;
              then ff.h in rng ff by FUNCT_1:def 5;
              then ff.h in F2;
              then consider ffh be Function of X1(),Y1() such that
                A9:ffh=ff.h & P[ffh,X1(),Y1()]& rng (ffh|X()) c=Y()& 
                  for x st x in X1()\X() holds ffh.x=F(x);
              now let x such that A10:x in X1();
                now per cases;
                  suppose x in X();
                    then x in dom h & ffh|X()=h by Z3,FUNCT_2:def 1,A2,A8,A9;
                    then h.x=ffh.x & f.x=h.x by FUNCT_1:68;
                    hence ffh.x=f.x;
                  end;
                  suppose not x in X();
                    then x in X1()\X() by A10,XBOOLE_0:def 4;
                    then ffh.x=F(x) & f.x=F(x) by A6,A9;
                    hence ffh.x=f.x;
                  end;
                end;
                hence ffh.x=f.x;
              end;      
              then ffh=f by FUNCT_2:18;
              hence thesis by A6,A8,A9,FUNCT_1:def 5; 
            end;
            then A5:rng ff=F2 & dom ff=F1 by A4,XBOOLE_0:def 10,FUNCT_2:def 1;
            for x1,x2 be set st x1 in F1 & x2 in F1 & ff.x1=ff.x2 holds x1=x2
              proof
                let x1,x2 be set such that A6:x1 in F1& x2 in F1 & ff.x1=ff.x2;
                consider f1 be Function of X(),Y() such that
                  A7:x1=f1 &P[f1,X(),Y()] by A6;
                consider f2 be Function of X(),Y() such that
                  A8:x2=f2 & P[f2,X(),Y()] by A6;      
                dom ff= F1 by A4,FUNCT_2:def 1;
                then ff.x1 in rng ff & ff.x2 in rng ff by A6,FUNCT_1:def 5;
                then A9:ff.x1 in F2 & ff.x2 in F2;
                then consider F1 be Function of X1(),Y1() such that
                  A10:ff.x1=F1 & P[F1,X1(),Y1()]& rng (F1|X()) c=Y()& 
                      for x st x in X1()\X() holds F1.x=F(x);
                consider F2 be Function of X1(),Y1() such that
                  A11:ff.x2=F2 & P[F2,X1(),Y1()]& rng (F2|X()) c=Y()& 
                      for x st x in X1()\X() holds F2.x=F(x) by A9;
                F1|X()=f1 & F2|X()=f2 & F1=F2 by A2,A6,A7,A8,A10,A11;
                hence thesis by A7,A8;                                
              end;
            then ff is one-to-one by A4, FUNCT_2:25;
            then F1,F2 are_equipotent by A5,WELLORD2:def 4;  
            hence thesis by CARD_1:21;
          end;
        end;  
        hence thesis;
      end;
    end;
    hence thesis;
end;

scheme Sch4{X,Y,x,y()->set,P[set,set,set]}:
  Card{f where f is Function of X(),Y():
                P[f,X(),Y()]}=
  Card{f where f is Function of (X()\/{x()}),(Y()\/{y()}):
                P[f,X()\/{x()},Y()\/{y()}]&
                rng (f|X()) c=Y() &                
                f.x()=y()}
provided
Z1:Y() is empty implies X() is empty and
Z2:not x() in X() and
Z3:for f be Function of X()\/{x()},Y()\/{y()}
st f.x()=y()
  holds
    P[f,X()\/{x()},Y()\/{y()}] iff P[f|X(),X(),Y()] 
proof
  deffunc F(set)=y();
  set X1=X()\/{x()};
  set Y1=Y()\/{y()};
  A1:for x st x in X1\X() holds F(x) in Y1
  proof
    let x such that x in X1\X();
    y() in {y()} & {y()} c=Y1 by TARSKI:def 1,XBOOLE_1:7;
    hence thesis;
  end;
  A2:X()c=X1 & Y()c=Y1 by XBOOLE_1:7;
  A3:for f be Function of X1,Y1 holds
    (for x st x in X1\X() holds F(x)=f.x) iff f.x()=y()
    proof
      let f be Function of X1,Y1;
      A4:X1\X()={x()}\X() by XBOOLE_1:40
      .={x()} by Z2,ZFMISC_1:67;
      thus (for x st x in X1\X() holds F(x)=f.x) implies f.x()=y()
      proof
        assume A5:(for x st x in X1\X() holds F(x)=f.x);
        x() in {x()} by TARSKI:def 1;
        hence thesis by A4,A5;
      end;
      thus f.x()=y() implies (for x st x in X1\X() holds F(x)=f.x)
        by A4,TARSKI:def 1;      
    end;
  A4:for f be Function of X1,Y1 st
     (for x st x in X1\X() holds F(x)=f.x)
  holds
    P[f,X1,Y1] iff P[f|X(),X(),Y()]
    proof
      let f be Function of X1,Y1 such that
        A5:for x st x in X1\X() holds F(x)=f.x;
      y()=f.x() by A3,A5;
      hence thesis by Z3;
    end;
  set F1={f where f is Function of X1,Y1:
                       P[f,X1,Y1]&
                       rng (f|X()) c=Y()&
                       for x st x in X1\X() holds f.x=F(x)};
  set F2={f where f is Function of X1,Y1:
                       P[f,X1,Y1]&
                       rng (f|X()) c=Y()&
                       f.x()=y()};    
  A5:Card{f where f is Function of X(),Y():P[f,X(),Y()]}= Card F1
                from Sch3(A1,A2,Z1,A4);  
  A6:F1 c= F2
  proof
    let x such that A7:x in F1;
    consider f be Function of X1,Y1 such that
      A8:x=f & P[f,X1,Y1] & rng (f|X()) c=Y() &
      for x st x in X1\X() holds f.x=F(x) by A7;
    f.x()=y() by A3,A8;
    hence thesis by A8; 
  end;
  F2 c= F1
  proof
    let x such that A7:x in F2;
    consider f be Function of X1,Y1 such that
      A8:x=f & P[f,X1,Y1] & rng (f|X()) c=Y() & f.x()=y() by A7;
    for x st x in X1\X() holds F(x)=f.x by A3,A8;
    hence thesis by A8; 
  end;
  hence thesis by A5,A6,XBOOLE_0:def 10;
end;

theorem Th35:
  for f be Function of n+1,k+1 st
    f is onto "increasing & f"{f.n}={n} holds f.n=k
  proof
    let f be Function of n+1,k+1 such that
    A1:f is onto "increasing & f"{f.n}={n};   
    assume A2:f.n<>k;
    now per cases by A2,REAL_1:def 5;
      suppose A3:f.n>k;
        f.n<k+1 by EULER_1:1;
        hence contradiction by A3,NAT_1:38;
      end;
      suppose A3:f.n<k;
        then f.n<k+1 & k<k+1 & rng f = k+1 & f.n in k+1 &
          k+1 is Subset of NAT by A1,NAT_1:38,FUNCT_2:def 3,Th8;
        then f.n in rng f & k in rng f & f.n in NAT by EULER_1:1;
        then A4:min* f"{f.n} < min* f"{k} & min* f"{f.n} = n
          by A1,A3,Def1,Th5;
        n+1 >0 by NAT_1:18;
        then min* f"{k} <=(n+1)-1 by Th16;        
        hence contradiction by A4;
      end;
    end;
    hence contradiction;
  end;
    
theorem Th36:
  for f be Function of n+1,k st
    k<>0 & f"{f.n}<>{n} holds
      ex m st m in f"{f.n} & m<>n
proof
  let f be Function of n+1,k such that
    A1:k<>0 and
    A2: f"{f.n}<>{n};
  n<n+1 & dom f =n+1 by A1,NAT_1:38,FUNCT_2:def 1;
  then n in dom f & f.n in {f.n} by EULER_1:1,TARSKI:def 1;
  then n in f"{f.n} by FUNCT_1:def 13;
  then ex m be set st m in f"{f.n} & m<>n by A2,ZFMISC_1:41;
  hence thesis;
end;

theorem Th37:
  for f be Function of n,k, g be Function of n+m,k+l
    st g is "increasing & f=g|n
    holds
      for i,j st i in rng f & j in rng f & i<j holds min* f"{i} < min* f"{j}
  proof
    let f be Function of n,k, g be Function of n+m,k+l
      such that A1:g is "increasing and A2: f=g|n;
    let i,j such that A3:i in rng f & j in rng f & i<j;
    A4:for k1 be Nat st k1 in rng f holds k1 in rng  g& min* f"{k1}=min* g"{k1}
       proof
         let k1 be Nat such that A5:k1 in rng f;         
         consider x be set such that
           A6:x in dom f & f.x=k1 by A5,FUNCT_1:def 5;
         k<=k+l by NAT_1:29;
         then k c= k+l by CARD_1:56;  
         then k1 in k & k1 in k+l & n <= n+m by A5,TARSKI:def 3,NAT_1:29;
         then A7:dom f = n & dom g =n+m & n c= n+m by FUNCT_2:def 1,CARD_1:56;
         x in n & n is Subset of NAT by A6,Th8;
         then reconsider x'=x as Nat;
         A8:f.x'=g.x' & f.x'= k1 & k1 in {k1} & x' in dom g
           by A2,A6,A7,TARSKI:def 1,FUNCT_1:68;
         then x' in f"{k1} & x' in g"{k1} & k1 in rng g
           by A6,FUNCT_1:def 13,def 5;
         then min* g"{k1} <= x' & x' <n & min* g"{k1} in g"{k1}
           by A6,HENMODEL:def 1,EULER_1:1;
         then min* g"{k1} < n & g.(min* g"{k1}) in {k1}
           by AXIOMS:22,FUNCT_1:def 13;
         then min* g"{k1} in dom f & g.(min* g"{k1}) in {k1}
           by A7,EULER_1:1;
         then min* g"{k1} in n & f.(min* g"{k1}) in {k1} by A2,FUNCT_1:68;
         then A9:min* g"{k1} in f"{k1} by A7,FUNCT_1:def 13;
         now let n1 be Nat such that A10:n1 in f"{k1};
           n1 in n by A7,A10,FUNCT_1:def 13;
           then n1 in dom f & n1 in dom g & f.n1 in {k1}
             by A7,A10,FUNCT_1:def 13;  
           then g.n1 in {k1} & n1 in dom g by A2,FUNCT_1:68;
           then n1 in g"{k1} by FUNCT_1:def 13;
           hence min* g"{k1}<=n1 by HENMODEL:def 1;           
         end;
         hence thesis by A8,A9, HENMODEL:def 1,FUNCT_1:def 5;                 
       end;
   i in rng g & j in rng g & min* f"{i}=min* g"{i} &  min* f"{j}=min* g"{j}
     by A3,A4;
   hence thesis by A1,A3,Def1;
 end;
    
theorem Th38:
  for f be Function of n+1,k+1 st
    f is onto "increasing & f"{f.n}={n} holds
      rng (f|n) c= k & 
      for g be Function of n,k st g=f|n
        holds g is onto "increasing
proof
  let f be Function of n+1,k+1 such that
  A1:f is onto "increasing & f"{f.n}={n};
  now per cases by NAT_1:18;
    suppose A2:n=0;
      then 0+1>=k+1 & dom (f|n)=dom f /\0 by A1,Th17,FUNCT_1:68;
      then 0>=k & dom (f|n) ={} by REAL_1:53;
      then k=0 & rng (f|n)=0 by NAT_1:18,RELAT_1:65;
      hence thesis by A2,Th15;
     end;
     suppose A2:n>0;
       thus A3:rng (f|n) c= k
         proof
           let fi be set such that A4:fi in rng (f|n);
           fi in k+1 & k+1 is Subset of NAT by A4,Th8;
           then reconsider fi as Nat;
           consider i be set such that
             A5:i in dom (f|n) & (f|n).i=fi by A4,FUNCT_1:def 5;
           i in dom f /\n by A5,FUNCT_1:68;
           then i in n & n is Subset of NAT by XBOOLE_0:def 3,Th8;
           then reconsider i as Nat;
           f.i<k
           proof
             assume A6:f.i>=k;  
             i in dom f/\n by A5,FUNCT_1:68;
             then A7:i in dom f & f.i<k+1 & i in n 
               by EULER_1:1,XBOOLE_0:def 3;
             then f.i<=k by NAT_1:38;
             then f.i=k & f.i in {f.i} & f.n=k
               by A1,A6,AXIOMS:21,TARSKI:def 1,Th35;
             then i in f"{f.n} by A7,FUNCT_1:def 13;
             then i >= min* f"{f.n} by HENMODEL:def 1;
             then i >= n by Th5,A1;
             hence contradiction by A7,EULER_1:1;
           end;
           then f.i=(f|n).i & f.i in k by A5,FUNCT_1:68,EULER_1:1;
           hence thesis by A5;           
         end;
       thus for g be Function of n,k st g=f|n
          holds g is onto "increasing
          proof
            let g be Function of n,k such that A4:g=f|n;
            thus g is onto
              proof
                k c=rng g
                 proof
                   let k1 be set such that A5:k1 in k;
                   k is Subset of NAT by Th8;
                   then reconsider k'=k1 as Nat by A5;
                   k'<k by A5,EULER_1:1;
                   then k' < k+1 by NAT_1:38;
                   then k' in k+1 & rng f = k+1
                     by A1,EULER_1:1,FUNCT_2:def 3;
                   then consider n1 be set such that
                     A6:n1 in dom f & f.n1=k' by FUNCT_1:def 5;
                   dom f=n+1 & n+1 is Subset of NAT by FUNCT_2:def 1,Th8;
                   then reconsider n1 as Nat by A6;
                   n1<n+1 & f.n=k & n<n+1
                     by A1,A6,EULER_1:1,Th35,NAT_1:38;
                   then n1<=n & n1<>n  & n c= n+1 & dom f=n+1
                     by A5,A6,NAT_1:38,CARD_1:56,FUNCT_2:def 1;
                   then n1 < n & n=dom f /\n & dom f /\n=dom (f|n) 
                      by REAL_1:def 5,FUNCT_1:68,XBOOLE_1:28;
                   then n1 in dom g by A4,EULER_1:1;
                   then g.n1 in rng g & g.n1=f.n1 by A4,FUNCT_1:def 5,68;
                   hence thesis by A6;
                 end;
                then k=rng g by XBOOLE_0:def 10;
                hence thesis by FUNCT_2:def 3;                      
              end;
            thus g is "increasing
              proof
                n=0 iff k=0
                proof
                  n<n+1 by NAT_1:38;
                  then n c= n+1 & dom f=n+1
                    by CARD_1:56,FUNCT_2:def 1;
                  then n=dom f /\n & dom f /\n=dom (f|n) & 0 in n  
                    by FUNCT_1:68,XBOOLE_1:28,A2,EULER_1:1;
                  then (f|n).0 in rng (f|n) by FUNCT_1:def 5;
                  hence thesis by A2,A3;
                end;
                then (n=0 iff k=0) & for i,j st i in rng g & j in rng g & i<j
                holds min* g"{i} < min* g"{j} by A1,A4,Th37;
                hence thesis by Def1;
              end;  
           end;
      end;
    end;
  hence thesis;
end;
     
theorem Th39:
  for f be Function of n+1,k,g be Function of n,k st
    f is onto "increasing & f"{f.n}<>{n} & f|n =g 
    holds g is onto "increasing
proof
  let f be Function of n+1,k, g be Function of n,k such that
    A1:f is onto "increasing and
    A2:f"{f.n}<>{n} and
    A3:f|n =g;
  now per cases by NAT_1:18;
    suppose k=0;
      hence thesis by A1,Def1;
    end;
    suppose A4:k>0;
      then k>0 & n<n+1 by NAT_1:38;
      then dom f=n+1 & n in n+1 by FUNCT_2:def 1,EULER_1:1;
      then A5:f.n in rng f & rng f = k by A1,FUNCT_1:def 5,FUNCT_2:def 3;
      now per cases by NAT_1:18;
        suppose A6:n=0;
          then dom f=1 & 1 ={0}
            by CARD_5:1,FUNCT_2:def 1,A4;
          then f.0 in rng f & {f.0}=k & Card k =k & Card {f.0} = one
            by A5,CARD_1:66,50,FUNCT_1:14;
          then A7:k=1 & f"{f.0} <>{} by CARD_2:20,FUNCT_1:142;
          now let x be set such that A8:x in f"{f.0};
            x in dom f by A8,FUNCT_1:def 13;
            hence x in {0} by A6,CARD_5:1;
          end;
          then f"{f.0} c= {0} & f"{f.0}<>{0} by A2,A6,TARSKI:def 3;
          hence thesis by ZFMISC_1:39,A7;
        end;
        suppose A6:n>0;
          k c= rng g
            proof
              let k1 be set such that A7:k1 in k;
              k is Subset of NAT by Th8;
              then reconsider k'=k1 as Nat by A7;
              consider x be set such that
                A8:x in dom f & f.x=k1 by A5,A7,FUNCT_1:def 5;
              dom f =n+1 & n+1 is Subset of NAT by A7,FUNCT_2:def 1,Th8;
              then reconsider x as Nat by A8;
              x<n+1 by A8,EULER_1:1;
              then A9:x<=n by NAT_1:38;
              now per cases by A9,REAL_1:def 5;
                suppose x<n;
                  then x in n & dom g = n by A4,EULER_1:1,FUNCT_2:def 1;
                  then x in dom g & g.x=f.x by A3,FUNCT_1:68;
                  hence k1 in rng g by A8,FUNCT_1:def 5;
                end;
                suppose x=n;
                  then consider m such that
                    A10:m in f"{k1} & m<>n by Th36, A2,  A4,A8;
                  m in dom f by A10,FUNCT_1:def 13; 
                  then m<n+1 by EULER_1:1;
                  then m<=n by NAT_1:38;
                  then m < n by A10,REAL_1:def 5;
                  then m in n & n=dom g  & f.m in {k1}
                    by A4,EULER_1:1,FUNCT_2:def 1,A10,FUNCT_1:def 13;
                  then m in dom g & g.m=f.m & f.m=k1
                    by A3,FUNCT_1:68,TARSKI:def 1;
                  hence k1 in rng g by FUNCT_1:def 5;
                end;
              end;
              hence thesis;
            end;
            then rng g =k & k=k+0 by XBOOLE_0:def 10;
            then g is onto & (n=0 iff k=0) &
              for i,j st i in rng g & j in rng g & i<j
              holds min* g"{i} < min* g"{j} by A1,A3,A4,Th37,A6,FUNCT_2:def 3;
            hence thesis by Def1;
          end;
        end;
      hence thesis;
    end;
  end;
  hence thesis;
end;
       
theorem Th40:
  for f be Function of n,k, g be Function of n+1,k+m
    st f is onto "increasing & f=g|n
    holds
      for i,j st i in rng g & j in rng g & i<j holds min* g"{i} < min* g"{j}
proof
  let f be Function of n,k, g be Function of n+1,k+m
  such that
  A1:f is onto "increasing and
  A2:f=g|n;
  A2':for i st i<n holds f.i=g.i
  proof
    let i such that A3:i <n;
    k=0 iff n=0 by A1,Def1;
    then i in n & dom f=n by A3,EULER_1:1,FUNCT_2:def 1;
    hence f.i=g.i by A2,FUNCT_1:68;    
  end; 
  let i,j such that A3:i in rng g & j in rng g & i<j;
  A4:for l st l in rng g & not l in rng f holds l=g.n
  proof
    let l such that A4:l in rng g & not l in rng f;
    assume A5:l<>g.n;
    consider x be set such that
      A6:x in dom g & g.x=l by A4,FUNCT_1:def 5;            
    dom g=n+1 & n+1 is Subset of NAT by FUNCT_2:def 1,Th8,A4;
    then reconsider x as Nat by A6;        
    x<n+1 by A6,EULER_1:1;
    then x<=n by NAT_1:38;
    then A7:x<n by A5,A6,REAL_1:def 5;
    then n>0 by NAT_1:18;
    then k<>0 by A1,Def1;
    then dom f=n & x in n & f.x=g.x by A2',FUNCT_2:def 1,A7,EULER_1:1;
    hence contradiction by A4,A6,FUNCT_1:def 5;
  end;
  A5:for l st l in rng g & not l in rng f holds min* g"{l}=n
  proof
    let l such that A6:l in rng g & not l in rng f;
    assume A7: min* g"{l}<>n;    
    n <n+1 & dom g=n+1 by A6,NAT_1:38,FUNCT_2:def 1;
    then n+1>0 & n in dom g & l in {l} & g.n=l by A4,A6,EULER_1:1,TARSKI:def 1;
    then min* g"{l} <= (n+1)-1  & n in g"{l} by Th16,FUNCT_1:def 13;
    then min* g"{l}< n & min* g"{l} in g"{l} by A7,REAL_1:def 5,HENMODEL:def 1;
    then A8:min* g"{l} in n & g.min* g"{l} in {l} & 0<n
      & f.min* g"{l}=g.min* g"{l} by A2',EULER_1:1,FUNCT_1:def 13;
    then k<>0 by A1,Def1;
    then dom f=n by FUNCT_2:def 1;
    then f.min* g"{l} in rng f by A8,FUNCT_1:def 5;
    hence contradiction by A6,A8,TARSKI:def 1;  
  end;
  A6:for l st l in rng g & not l in rng f holds l>=k
  proof
    let l such that A6:l in rng g & not l in rng f;
    assume l<k;
    then l in k & rng f=k by A1,FUNCT_2:def 3,EULER_1:1;
    hence thesis by A6;    
  end;
  A7:for k1 be Nat st k1 in rng f holds min* g"{k1}=min* f"{k1}
  proof
    let k1 be Nat such that A7:k1 in rng f;
    consider x be set such that
      A8:x in dom f & f.x=k1 by A7,FUNCT_1:def 5;
    k<=k+m by NAT_1:29;
    then k c= k+m by CARD_1:56;
    then k1 in k & k1 in k+m & n <= n+1 by A7,TARSKI:def 3,NAT_1:29;
    then A9:dom f = n & dom g =n+1 & n c= n+1 by FUNCT_2:def 1,CARD_1:56;
    x in n & n is Subset of NAT by A8,Th8;
    then reconsider x as Nat;
    x < n by A8,EULER_1:1;
    then f.x=g.x & f.x= k1 & k1 in {k1} & x in dom g
       by A2',A8,A9,TARSKI:def 1;
    then x in f"{k1} & x in g"{k1} & k1 in rng g
       by A8,FUNCT_1:def 13,def 5;
    then A11:min* f"{k1} <= x & x <n & min* f"{k1} in f"{k1}
      by A8,HENMODEL:def 1,EULER_1:1;
    then min* f"{k1} < n & f.(min* f"{k1}) in {k1}
       by AXIOMS:22,FUNCT_1:def 13;
    then min* f"{k1} in n & g.(min* f"{k1}) in {k1} by A2',EULER_1:1;
    then A12:min* f"{k1} in g"{k1} by A9,FUNCT_1:def 13;
    now let n1 be Nat such that A13:n1 in g"{k1};
      n1 in n+1 by A9,A13,FUNCT_1:def 13;
      then n1<n+1 by EULER_1:1;
      then A14:n1<=n by NAT_1:38;
      now per cases by A14,REAL_1:def 5;
        suppose n1<n;
           then n1<n & n1 in dom f & g.n1 in {k1}
             by A9,A13,EULER_1:1,FUNCT_1:def 13;  
           then f.n1 in {k1} & n1 in dom f by A2';
           then n1 in f"{k1} by FUNCT_1:def 13;
           hence min* f"{k1}<=n1 by HENMODEL:def 1;           
         end;
         suppose n1=n;
           hence min* f"{k1} <= n1 by A11,AXIOMS:22;
         end;
       end;
       hence min* f"{k1} <= n1;
     end;                  
     hence thesis by A12, HENMODEL:def 1;                 
   end;         
  now per cases;
    suppose A8:i in rng f & j in rng f;
      then min* g"{i}=min* f"{i} &  min* g"{j}=min* f"{j} by A7;
      hence thesis by A1,A3,A8,Def1;
    end;
    suppose A8:i in rng f & not j in rng f;
      then n<>0 by A1,Def1;
      then n>0 by NAT_1:18;
      then min* f"{i} <= n-1 & min* g"{i}=min* f"{i} & n-1 is Nat
        by A8,A7,Th16,NAT_1:60;
      then min* g"{i} <=n-1 & (n-1)<(n-1)+1 & min* g"{j}=n
        by A3,A5,A8,NAT_1:38;
      hence thesis by AXIOMS:22;
    end;    
    suppose not i in rng f  & j in rng f;
      then i>=k & j< k by A3,A6,EULER_1:1;
      hence thesis by A3,AXIOMS:22;
    end;
    suppose not i in rng f & not j in rng f;
      then i=g.n & j=g.n by A3,A4; 
      hence thesis by A3;
    end;
  end;
  hence thesis;
end;

theorem Th41:  
  for f be Function of n,k, g be Function of n+1,k+1
      st f is onto "increasing & f=g|n & g.n = k
    holds
      g is onto "increasing & g"{g.n}={n}
proof
  let f be Function of n,k, g be Function of n+1,k+1 such that
  A1:f is onto "increasing and
  A2:f=g|n and
  A3:g.n = k;
  thus g is onto
  proof
    k+1 c= rng g
    proof
      let x' be set such that A4:x' in k+1;
      k+1 is Subset of NAT by Th8;
      then reconsider x=x' as Nat by A4;
      x<k+1 by A4,EULER_1:1;
      then A5:x<=k by NAT_1:38;
      now per cases by A5,REAL_1:def 5;
        suppose x<k;
          then x in k & rng f=k by A1,FUNCT_2:def 3,EULER_1:1;
          then consider y be set such that
            A6:y in dom f & f.y =x by FUNCT_1:def 5;
          (n=0 iff k=0) & n<=n+1 by A1,Def1,NAT_1:29;  
          then dom f = n & n c= n+1 & dom g = n+1
            by FUNCT_2:def 1,CARD_1:56;
          then y in dom g & f.y=g.y by A2,A6,FUNCT_1:68;
          hence thesis by A6,FUNCT_1:def 5;
        end;
        suppose A6:x=k;
          n<n+1 by NAT_1:38;
          then n in n+1 & dom g =n+1 by EULER_1:1,FUNCT_2:def 1;
          hence thesis by A3,A6,FUNCT_1:def 5;
        end;
      end;
      hence thesis;
    end;
    then k+1=rng g by XBOOLE_0:def 10;
    hence thesis by FUNCT_2:def 3;
  end;
  for i,j st i in rng g & j in rng g & i<j holds
    min* g"{i} < min* g"{j} by A1,A2,Th40;
  hence g is "increasing by Def1;
  thus g"{g.n}={n}
    proof
      assume g"{g.n}<>{n};
      then consider m such that
        A4:m in g"{g.n} & m<>n by Th36;
      m in dom g & dom g=n+1 by A4,FUNCT_1:def 13,FUNCT_2:def 1;
      then m<n+1 by EULER_1:1;
      then m<=n & (n=0 iff k=0) by A1,Def1,NAT_1:38;
      then m<n & dom f=n  by A4,FUNCT_2:def 1,REAL_1:def 5;
      then m in dom f & g.m in {g.n} by A4,EULER_1:1,FUNCT_1:def 13;
      then f.m in rng f & f.m=g.m & g.m=k
        by A2,FUNCT_1:68,def 5,TARSKI:def 1,A3;
      hence contradiction by EULER_1:1;
    end;
end;

theorem Th42:  
  for f be Function of n,k, g be Function of n+1,k
      st f is onto "increasing & f=g|n & g.n < k
    holds
      g is onto "increasing & g"{g.n}<>{n}
proof
  let f be Function of n,k, g be Function of n+1,k such that
  A1:f is onto "increasing and
  A2:f=g|n and
  A3:g.n < k;
  thus g is onto
  proof
    k c= rng g
    proof
      let x' be set such that A4:x' in k;
      k is Subset of NAT by Th8;
      then reconsider x=x' as Nat by A4;
      x in k & rng f=k by A1,A4,FUNCT_2:def 3;
      then consider y be set such that
        A5:y in dom f & f.y =x by FUNCT_1:def 5;
      (n=0 iff k=0) & n<=n+1 & k<>0 by A1,A3,Def1,NAT_1:18,38;  
      then dom f = n & n c= n+1 & dom g = n+1
        by FUNCT_2:def 1,CARD_1:56;
      then y in dom g & f.y=g.y by A2,A5,FUNCT_1:68;
      hence thesis by A5,FUNCT_1:def 5;         
    end;
    then k=rng g by XBOOLE_0:def 10;
    hence thesis by FUNCT_2:def 3;
  end;
  k<>0 & k=k+0 by A3,NAT_1:18;
  then (n+1=0 iff k=0) & for i,j st i in rng g & j in rng g & i<j holds
    min* g"{i} < min* g"{j} by A1,A2,Th40;
  hence g is "increasing by Def1;        
  g.n in k & k=rng f by A1,A3,EULER_1:1,FUNCT_2:def 3;
  then consider x such that
    A4:x in dom f & f.x=g.n by FUNCT_1:def 5;
  k<>0 by A3,NAT_1:18;  
  then n<=n+1 & dom f=n & k<>0 by NAT_1:29,FUNCT_2:def 1;
  then x in n & n c=  n+1 & dom g=n+1 & g.n=g.x
    by A2,FUNCT_1:68,A4,CARD_1:56,FUNCT_2:def 1;  
  then x in dom g & g.x in {g.n} & x<>n by TARSKI:def 1;
  then x in g"{g.n} & not x in {n} by FUNCT_1:def 13,TARSKI:def 1;
  hence g"{g.n}<>{n}; 
end;

Lm1:n+1=n\/{n} by AFINSQ_1:4;

Lm2:k<n implies n\/{k}=n
  proof
    assume k<n;
    then k in n by EULER_1:1;
    then {k} c= n by ZFMISC_1:37;
    hence thesis by XBOOLE_1:12;
  end;

theorem Th43:
  Card{f where f is Function of n+1,k+1:
    f is onto "increasing & f"{f.n}={n}}=
  Card{f where f is Function of n,k: f is onto "increasing}
proof
  set F1={f where f is Function of n+1,k+1:
          f is onto "increasing &
          f"{f.n}={n}};
  set F2={f where f is Function of n,k: f is onto "increasing};
  now per cases;
    suppose A1:k=0 & n<>0;
      F1 is empty
        proof
          assume F1 is non empty;
          then consider x be set such that
            A2:x in F1 by XBOOLE_0:def 1;
          consider f be Function of n+1,k+1 such that
            A3:x=f & f is onto "increasing & f"{f.n}={n} by A2;
          0<n+1 & n<n+1 by NAT_1:18,38;
          then k+1={0} & 0 in n+1 & n in n+1 & dom f = n+1
            by A1,CARD_5:1,EULER_1:1,FUNCT_2:def 1;
          then f.n=0 & f.0=0 & 0 in dom f & 0 in {0} by TARSKI:def 1;
          then 0 in {n} by A3,FUNCT_1:def 13;
          hence thesis by A1,TARSKI:def 1;              
        end;
      then Card F1=0 & n block k =0 by A1,Th33,Th31;
      hence thesis by Def2;
    end;
    suppose k=0 implies n=0;
      then A1:k is empty implies n is empty;
      defpred P[set,set,set] means
        for i,j st i=$2 & j=$3 holds
          ex f be Function of i,j st
            f=$1 & 
            f is onto "increasing &
            (n < i implies f"{f.n}={n});
      A2:not n in n;
      A3:for f be Function of n\/{n},k\/{k}
           st f.n=k
             holds P[f,n\/{n},k\/{k}] iff P[f|n,n,k]
         proof
           let f' be Function of n\/{n},k\/{k} such that A4:f'.n=k;
           thus P[f',n\/{n},k\/{k}] implies P[f'|n,n,k]
           proof
             assume A5:P[f',n\/{n},k\/{k}];
             n+1=n\/{n} & k+1=k\/{k} by Lm1;
             then consider f be Function of n+1,k+1 such that
               A6:f=f' & f is onto "increasing &
               (n < n+1 implies f"{f.n}={n}) by A5;
             let i,j such that A7:i=n & j=k;             
             n<=n+1 by NAT_1:29;
             then dom f=n+1 & n c=n+1 & dom (f|n)=dom f /\ n
               by FUNCT_2:def 1,CARD_1:56,FUNCT_1:68;
             then dom (f|n)=n & rng (f|n) c= k by A6,NAT_1:38,XBOOLE_1:28,Th38;
             then reconsider fn=f|n as Function of n,k by FUNCT_2:4;
             reconsider fi=fn as Function of i,j by A7;
             fi=f'|n & fi is onto "increasing & (n < i implies fi"{fi.n}={n})
               by A7,A6,NAT_1:38,Th38;
             hence thesis;
           end;         
           thus P[f'|n,n,k] implies P[f',n\/{n},k\/{k}]
           proof
             assume P[f'|n,n,k];
             then consider fn be Function of n,k such that
               A5:fn=f'|n  and 
               A6:fn is onto "increasing and
                 (n < n implies fn"{fn.n}={n});
             let i,j such that A7:i=n\/{n} & j=k\/{k};
             n\/{n}=n+1 & k\/{k}=k+1 by Lm1;
             then reconsider f=f' as Function of n+1,k+1;
             reconsider f1=f as Function of i,j by A7;
             n+1=i & k+1 =j by A7,Lm1;
             then f1 is onto "increasing & (n < i implies f1"{f1.n}={n})
               by A4,A5,A6,Th41;
             hence thesis;
           end;        
         end;  
      set FF1={f where f is Function of (n\/{n}),(k\/{k}):
                P[f,n\/{n},k\/{k}]&
                rng (f|n) c=k &
                f.n=k};       
      set FF2={f where f is Function of n,k: P[f,n,k]};
      A4:Card FF2 = Card FF1 from Sch4(A1,A2,A3);
      A5:F1=FF1
      proof
        thus F1 c=FF1
          proof
            let x such that A6:x in F1;
            consider f be Function of n+1,k+1 such that
              A7:f=x & f is onto "increasing & f"{f.n}={n} by A6;
            P[f,n\/{n},k\/{k}]
              proof
                let i,j such that A8:i=n\/{n} & j=k\/{k};
                i=n+1 & j=k+1 by A8,Lm1;
                then reconsider f'=f as Function of i,j;
                i=n+1 & j=k+1 by A8,Lm1;
                hence thesis by A7;
              end;
            then P[f,n\/{n},k\/{k}] & f.n=k & rng (f|n) c= k
              & n+1=n\/{n} & k+1=k\/{k} by A7,Th38,Th35,Lm1;
            hence thesis by A7;
          end;
        thus FF1 c=F1
          proof
            let x such that A6:x in FF1;
            consider f be Function of (n\/{n}),(k\/{k})such that
              A7:x=f & P[f,n\/{n},k\/{k}] & rng (f|n) c=k & f.n=k by A6;
            n+1=n\/{n} & k+1=k\/{k} by Lm1;
            then ex f' be Function of n+1,k+1 st f=f' & 
              f' is onto "increasing & (n < n+1 implies f'"{f'.n}={n}) by A7;
            hence thesis by A7,NAT_1:38;  
          end;
      end;
      F2=FF2
      proof
        thus F2 c=FF2
          proof
            let x such that A6:x in F2;
            consider f be Function of n,k such that
              A7:x=f & f is onto "increasing by A6;
            P[x,n,k] by A7;
            hence thesis by A7;
          end;
        thus FF2 c= F2
          proof
            let x such that A6:x in FF2;
            consider f be Function of n,k such that
              A7:x=f & P[f,n,k] by A6;
            ex g be Function of n,k st g=f & 
              g is onto "increasing & (n < n implies g"{g.n}={n}) by A7;
            hence thesis by A7;
          end;  
        end;
      hence thesis by A4,A5;
    end;
  end;
  hence thesis;
end;

theorem Th44:
  for l st l<k holds
    Card{f where f is Function of n+1,k:
      f is onto "increasing & f"{f.n}<>{n} & f.n=l}=
    Card{f where f is Function of n,k: f is onto "increasing}
proof
  let l such that A0:l<k;      
  set F1={f where f is Function of n+1,k:
          f is onto "increasing &
          f"{f.n}<>{n}&
          f.n=l};
  set F2={f where f is Function of n,k: f is onto "increasing};
  now per cases;
    suppose k=0 & n<>0;
      hence thesis by A0,NAT_1:18;
    end;
    suppose k=0 implies n=0;
      then A1:k is empty implies n is empty;
      defpred P[set,set,set] means
        for i,j st i=$2 & j=$3 holds
          ex f be Function of i,j st
            f=$1 & 
            f is onto "increasing &
            (n < i implies f"{f.n}<>{n});
      A2:not n in n;
      A3:for f be Function of n\/{n},k\/{l}
           st f.n=l
             holds P[f,n\/{n},k\/{l}] iff P[f|n,n,k]
         proof
           let f' be Function of n\/{n},k\/{l} such that A4:f'.n=l;
           thus P[f',n\/{n},k\/{l}] implies P[f'|n,n,k]
           proof
             assume A5:P[f',n\/{n},k\/{l}];
             n+1=n\/{n} & k=k\/{l} by A0,Lm1,Lm2;
             then consider f be Function of n+1,k such that
               A6:f=f' & f is onto "increasing &
               (n < n+1 implies f"{f.n}<>{n}) by A5;
             let i,j such that A7:i=n & j=k;             
             n<=n+1 & k<>0 by A0,NAT_1:18,38;
             then dom f=n+1 & n c=n+1 & dom (f|n)=dom f /\ n
               by FUNCT_2:def 1,CARD_1:56,FUNCT_1:68;
             then dom (f|n)=n & rng (f|n) c= k by XBOOLE_1:28;
             then reconsider fn=f|n as Function of n,k by FUNCT_2:4;
             reconsider fi=fn as Function of i,j by A7;
             fi=f'|n & fi is onto "increasing & (n < i implies fi"{fi.n}<>{n})
               by A7,A6,NAT_1:38,Th39;
             hence thesis;
           end;         
           thus P[f'|n,n,k] implies P[f',n\/{n},k\/{l}]
           proof
             assume P[f'|n,n,k];
             then consider fn be Function of n,k such that
               A5:fn=f'|n  and 
               A6:fn is onto "increasing and
                 (n < n implies fn"{fn.n}<>{n});
             let i,j such that A7:i=n\/{n} & j=k\/{l};  
             n\/{n}=n+1 & k\/{l}=k by A0,Lm1,Lm2;
             then reconsider f=f' as Function of n+1,k;
             reconsider f1=f as Function of i,j by A7;             
             n+1=i & k =j by A0,A7,Lm1,Lm2;
             then f1 is onto "increasing & (n < i implies f1"{f1.n}<>{n})
               by A0,A4,A5,A6,Th42;
             hence thesis;
           end;        
         end;  
      set FF1={f where f is Function of (n\/{n}),(k\/{l}):
                P[f,n\/{n},k\/{l}]&
                rng (f|n) c=k &
                f.n=l};
      set FF2={f where f is Function of n,k: P[f,n,k]};
      A4:Card FF2 = Card FF1 from Sch4(A1,A2,A3);
      A5:F1=FF1
      proof
        thus F1 c=FF1
          proof
            let x such that A6:x in F1;
            consider f be Function of n+1,k such that
              A7:f=x & f is onto "increasing & f"{f.n}<>{n}& f.n=l by A6;
            P[f,n\/{n},k\/{l}]
              proof
                let i,j such that A8:i=n\/{n} & j=k\/{l};
                i=n+1 & j=k by A0,A8,Lm1,Lm2;
                then reconsider f'=f as Function of i,j;
                i=n+1 & j=k by A0,A8,Lm1,Lm2;
                hence thesis by A7;
              end;
            then P[f,n\/{n},k\/{l}] & f.n=l & rng (f|n) c= k
              & n+1=n\/{n} & k=k\/{l} by A0,A7,Lm1,Lm2;
            hence thesis by A7;
          end;
        thus FF1 c=F1
          proof
            let x such that A6:x in FF1;
            consider f be Function of (n\/{n}),(k\/{l})such that
              A7:x=f & P[f,n\/{n},k\/{l}] & rng (f|n) c=k & f.n=l by A6;
            n+1=n\/{n} & k=k\/{l} by A0,Lm1,Lm2;
            then ex f' be Function of n+1,k st f=f' & 
              f' is onto "increasing & (n < n+1 implies f'"{f'.n}<>{n}) by A7;
            hence thesis by A7,NAT_1:38;  
          end;
      end;
      F2=FF2
      proof
        thus F2 c=FF2
          proof
            let x such that A6:x in F2;
            consider f be Function of n,k such that
              A7:x=f & f is onto "increasing by A6;
            P[x,n,k] by A7;
            hence thesis by A7;
          end;
        thus FF2 c= F2
          proof
            let x such that A6:x in FF2;
            consider f be Function of n,k such that
              A7:x=f & P[f,n,k] by A6;
            ex g be Function of n,k st g=f & 
              g is onto "increasing & (n < n implies g"{g.n}<>{n}) by A7;
            hence thesis by A7;
          end;  
        end;
      hence thesis by A4,A5;               
    end;
  end;
  hence thesis;
end;

reserve D for non empty set,
        F,G,H for XFinSequence of D,
        Fn for XFinSequence of NAT,
        b for BinOp of D,
        d,d1,d2 for Element of D;

definition let D,F,b;
 assume A1:b has_a_unity or len F >= 1;
 func b "**" F -> Element of D means :Def3:
   it = the_unity_wrt b if b has_a_unity & len F = 0 otherwise
    ex f be Function of NAT,D st f.0 = F.0 &
     (for n st n+1 < len F holds
     f.(n + 1) = b.(f.n,F.(n + 1))) & it = f.(len F-1);
 existence
 proof
   now per cases;
     suppose len F = 0;
      hence thesis by A1;
     end;
     suppose A2: len F <> 0;
       defpred P[Nat] means
         for F st len F = $1 & len F <> 0
           ex d be Element of D,f be Function of NAT,D st
             f.0 = F.0 &
             (for n st n+1 < len F holds f.(n + 1) = b.(f.n,F.(n + 1))) &
             d = f.(len F-1);
       A3:P[0];
       A4:for k st P[k] holds P[k + 1]
         proof
           let k such that A5:P[k];
           let F such that A6:len F = k + 1 & len F <> 0;
           reconsider G = F|k as XFinSequence of D by AFINSQ_1:16;
           k<k+1 by NAT_1:38;
           then A7:len G = k by A6,AFINSQ_1:14;
           now per cases;
             suppose A8: len G = 0;
             0 in len F by A6,A7,A8,TARSKI:def 1,CARD_5:1;
             then 0 in dom F by AFINSQ_1:def 1;
             then
X:            F.0 in rng F by FUNCT_1:def 5;
             rng F c= D by ORDINAL1:def 8;
             then F.0 in D by X;
             then reconsider f = NAT --> F.0 as Function of NAT,D
                by FUNCOP_1:57;
A10:            for x st x in NAT holds f.x = F.0 by FUNCOP_1:13;
               take d = f.0,f;
               thus f.0 = F.0 by A10;
               thus for n st  n+1 < len F holds
                 f.(n + 1) = b.(f.n,F.(n + 1)) by A6,A7,A8,NAT_1:39;
               k<k+1 by NAT_1:38;
               hence d = f.(len F-1) by A6,A8,AFINSQ_1:14;
             end;
             suppose A8:len G <> 0;
               then consider
                 d be Element of D,f be Function of NAT,D such that
                 A9: f.0 = G.0 and
                 A10:(for n st n+1<len G holds f.(n + 1)=b.(f.n,G.(n + 1)))and
                 A11:d = f.(len G-1) by A5,A7;
               k < len F & len F= dom F by A6,NAT_1:38,AFINSQ_1:def 1; 
               then k in dom F by EULER_1:1;
               then F.k in rng F & rng F c= D
                 by FUNCT_1:def 5,ORDINAL1:def 8;
               then reconsider d1 = F.k as Element of D;
               deffunc F(Element of NAT) = f.$1;
               consider h be Function of NAT,D such that
                 A12: h.k = b.(d,d1) and
                 A13: for n be Element of NAT st n <> k holds
                   h.n = F(n) from FUNCT_2:sch 10;
               take a = h.k, h;                   
               0 < k by A7,A8,NAT_1:18;
               then h.0=f.0 & 0 in len G & len G=dom G
                 by A13,A7,EULER_1:1,AFINSQ_1:def 1;
               hence h.0 =F.0 by A9,FUNCT_1:70;
               thus for n st n+1 < len F holds
                 h.(n + 1) = b.(h.n,F.(n + 1))
               proof
                 let n such that A14:n+1 < len F;
                 A15:n+1 <= len G by NAT_1:38,A6,A7,A14;
                 now per cases by A15,REAL_1:def 5;
                   suppose A16: n+1 = len G;
                     n+1=k & (n+1)-1=len G - 1 & n<k & (n+1)-1=n
                       by A7,A16,NAT_1:38;
                     hence thesis by A11,A12,A13;                    
                   end;
                   suppose A14:n+1 < len G;
                     n+1 in len G by A14,EULER_1:1;
                     then n+1 in dom G & n<=n+1 by AFINSQ_1:def 1,NAT_1:29;
                     then G.(n+1)=F.(n+1) & f.n=h.n & f.(n+1)=h.(n+1)
                       by A13,A7,A14,FUNCT_1:68;
                     hence thesis by A10,A14;
                   end;
                 end;
                 hence thesis;
               end;              
               thus a = h.(len F-1) by A6;
             end;
           end;
           hence thesis;
         end;
       for k holds P[k] from NAT_1:sch 1(A3,A4);
       hence thesis by A2;
     end;
    end;
    hence thesis;  
  end;
 uniqueness
 proof
   let d1,d2 be Element of D;
   thus b has_a_unity & len F=0 & d1 = the_unity_wrt b & d2 = the_unity_wrt b
     implies d1 = d2;
   assume A2:not b has_a_unity or len F <> 0;
   given f1 be Function of NAT,D such that
     A3:f1.0 = F.0 and
     A4:for n st n+1<len F holds f1.(n + 1) = b.(f1.n,F.(n + 1)) and
     A5:d1 = f1.(len F-1);
   given f2 be Function of NAT,D such that
     A6:f2.0 = F.0 and
     A7:for n st n+1<len F holds f2.(n + 1) = b.(f2.n,F.(n + 1)) and
     A8:d2 = f2.(len F-1);    
   defpred P[Nat] means $1+1 <= len F implies f1.$1 = f2.$1;
   A9:P[0] by A3,A6;
   A10:for n st P[n] holds P[n + 1]
     proof
       let n such that A11:P[n];
       assume (n+1)+1 <= len F;
       then n+1<=len F & n+1<len F by NAT_1:38;
       then f1.n=f2.n & f2.(n+1)=b.(f2.n,F.(n+1)) & f1.(n+1)=b.(f1.n,F.(n+1))
         by A11,A4,A7;
       hence thesis;
     end;
   A11:for n holds P[n] from NAT_1:sch 1(A9,A10);
   0< len F by A1,A2,NAT_1:18;
   then (len F-1) +1 =len F & len F-1 is Nat by NAT_1:60;
   hence d1=d2 by A5,A8,A11;
 end;
 consistency;
end;

theorem Th45:
  b "**" <%d%> = d
proof
  len<%d%> = 1 by AFINSQ_1:37;
  then ex f be Function of NAT,D st f.0=<%d%>.0&
    (for n st n+1 < len <%d%> holds f.(n+1) = b.(f.n,<%d%>.(n+1)))&
    b "**" <%d%>=f.(1-1) by Def3;
  hence thesis by AFINSQ_1:38;
end;

theorem Th46:
  b has_a_unity or len F > 0 implies b "**" (F ^ <% d %>) = b.(b "**" F,d)
proof
  assume A1:b has_a_unity or len F > 0;
  now per cases;
    suppose A2:len F<0+1;
      then len F<=0 by NAT_1:38;
      then A3:len F=0 by NAT_1:18;
      then F={} by Th33;
      then b "**" F = the_unity_wrt b & F ^ <% d %>=<%d%>
        by A1,A3,Def3,AFINSQ_1:32;
      then b "**" (F ^<% d %>)=d & b.(b "**" F,d)=d
        by A1,A2,Th45,SETWISEO:23, NAT_1:38;
      hence thesis;
    end;
    suppose A2:len F>=1;
      then consider f be Function of NAT,D such that
        A3:f.0 = F.0 and
        A4:for n st n+1 < len F holds f.(n+1)=b.(f.n,F.(n+1)) and
        A5:b "**" F = f.(len F-1) by Def3;
      reconsider lenF1=len F-1 as Nat by A2,NAT_1:61;         
      set G = F ^ <% d %>;
      A6:len G=len F+len <%d%> by AFINSQ_1:20
          .=len F+1 by AFINSQ_1:37;
      then 1 <= len G by NAT_1:37;
      then consider f1 be Function of NAT,D such that
        A7:f1.0 = G.0 and
        A8:for n st n+1 < len G holds f1.(n+1)=b.(f1.n,G.(n+1)) and
        A9:b "**" G = f1.(len G-1) by Def3;  
      defpred P[Nat] means $1+1 < len G implies f.$1 = f1.$1;
      0 in len F by A2,EULER_1:1;
      then 0 in dom F by AFINSQ_1:def 1;
      then A10:P[0] by A3,A7,AFINSQ_1:def 4;
      A11:for n st P[n] holds P[n + 1]
      proof
        let n such that A12:P[n];
        set n1=n+1;assume n1+1<len G;
        then n1<len G & (n1+1)+(-1)<(len F+1)+(-1) by A6,NAT_1:38,REAL_1:67;
        then A13:f1.n1=b.(f1.n,G.(n+1)) & f.n1=b.(f.n,F.n1) & n1 in len F &
          f.n=f1.n by A4,A8,A12,EULER_1:1;
        then n1 in dom F by AFINSQ_1:def 1;
        hence thesis by A13,AFINSQ_1:def 4;
      end;
      A12:for n holds P[n] from NAT_1:sch 1(A10,A11);
      lenF1+1<len G & lenF1+1=len G-1 by A6,NAT_1:38;
      then b "**" G = b.(f1.(lenF1),G.(lenF1+1)) & G.(len F)=d &
        f1.(lenF1)=f.(lenF1) by A12,A8,A9,AFINSQ_1:40;            
      hence thesis by A5;
    end;
  end;
  hence thesis;
end;

theorem Th47:
  F <> <%>D implies ex G,d st F = G^<%d%>
proof
  assume F <> <%>D;
  then consider G be XFinSequence,x such that
    A1:F=G^<%x%> by AFINSQ_1:44;
  len <%x%> =1 by AFINSQ_1:37;
  then A2:len G + 1 = len F & len G < len G+1 & dom F=len F
    by A1,AFINSQ_1:def 1,20,NAT_1:38;
  A3:rng G c= D
    proof
      let y such that A4:y in rng G;
      consider x such that
        A5:x in dom G & G.x=y by A4,FUNCT_1:def 5;
      dom G=len G by AFINSQ_1:def 1;
      then dom G c= dom F  & x is Nat by A2,A5,CARD_1:56;
      then F.x in rng F & rng F c= D & F.x=G.x
        by A1,A5,FUNCT_1:def 5,ORDINAL1:def 8,AFINSQ_1:def 4;
      hence thesis by A5;
    end;
  len G in dom F by A2,EULER_1:1;
  then F.(len G) in rng F & rng F c= D by FUNCT_1:def 5,ORDINAL1:def 8;
  then G is XFinSequence of D & x is Element of D
    by A1,A3,AFINSQ_1:40,ORDINAL1:def 8;
  hence thesis by A1;  
end;
    
scheme Sch5{D()->non empty set, P[set]}:
  for F be XFinSequence of D() holds P[F]
 provided
   A1:P[<%>D()] and
   A2:for F be XFinSequence of D(),d be Element of D()
      st P[F] holds P[F^<%d%>]
proof
  defpred R[set] means
  for F be XFinSequence of D() st len F = $1 holds P[F];
  A3:R[0] by A1,Th33;
  A4:for n st R[n] holds R[n+1]
    proof
      let n such that A5:for F be XFinSequence of D() st len F=n holds P[F];
      let F be XFinSequence of D();
      assume A6:len F = n + 1;
      then F<><%>D() by Th33;
      then consider G be XFinSequence of D(), d be Element of D() such that
        A7:F = G^<%d%> by Th47;
      len<%d%>=1 by AFINSQ_1:38;
      then len F = len G + 1 by A7,AFINSQ_1:20;
      then len G = n by A6,XCMPLX_1:2;
      then P[G] by A5;
      hence thesis by A2,A7;
    end;
  A8:for i holds R[i] from NAT_1:sch 1(A3,A4);
  let F be XFinSequence of D();
  len F=len F;
  hence thesis by A8;
end;
          
theorem
 b is associative & (b has_a_unity or len F >= 1 & len G >= 1) implies
   b "**" (F ^ G) = b.(b "**" F,b "**" G)
proof
  defpred P[XFinSequence of D] means
    for F,b st b is associative & (b has_a_unity or len F >= 1 & len $1 >= 1)
      holds b "**" (F^$1)=b.(b "**" F,b "**" $1);
  A1:P[<%>D]
    proof
      let F,b;
      assume A2:b is associative &
        (b has_a_unity or len F >= 1 & len <%>D >= 1);
      thus b "**" (F ^ <%>D) = b "**" F by AFINSQ_1:32
                      .= b.(b "**" F,the_unity_wrt b) by A2,Th33,SETWISEO:23
                      .= b.(b "**" F,b "**" <%>D) by A2,Def3,CARD_1:47;
    end;
  A2:for G,d st P[G] holds P[G ^ <%d%>]
    proof
      let G,d such that A3:P[G];
      let F,b such that A4:b is associative &
        (b has_a_unity or len F >= 1 & len(G ^ <% d %>) >= 1);
      now per cases;
        suppose A5:len G<>0;
          A6:b "**" (F ^ (G ^ <% d %>)) = b.(b "**" (F ^ G),d)
          proof
            len G>0 by A5,NAT_1:18;
            then b has_a_unity or len F>=1&len (F^G)=len F+len G &
              len F+len G >len F+0 by A4,AFINSQ_1:20,REAL_1:67;
            then b.(b "**" (F ^ G),d)=b "**" ((F ^ G)^<%d%>) by Th46;
            hence thesis by AFINSQ_1:30;
          end;
          A7:len G>=1 by A5,NAT_1:39;
          then b "**" (F ^ (G ^ <% d %>))=b.(b.(b "**" F,b "**" G),d)
            by A3,A4,A6
                  .= b.(b "**" F,b.(b "**" G,d)) by A4,BINOP_1:def 3
                  .= b.(b "**" F,b "**" (G ^ <% d %>)) by A7,Th46;          
          hence thesis;
        end;
        suppose len G=0;
           then A5: G = {} by Th33;
           hence b "**" (F ^(G ^ <% d %>)) = b "**"(F^<% d %>) by AFINSQ_1:32
                  .= b.(b "**" F,d) by A4,Th46
                  .= b.(b "**" F,b "**" <%d%>) by Th45
                  .= b.(b "**" F,b "**" (G ^ <% d %>)) by A5,AFINSQ_1:32;
        end;
      end;
      hence thesis;      
    end;
  for G holds P[G] from Sch5(A1,A2);
  hence thesis;
end;
  
definition let D; let d,d1;
  redefine func <%d,d1%> -> XFinSequence of D;
  coherence
  proof
    <%d,d1%>=<%d%>^<%d1%> by AFINSQ_1:def 6;
    hence thesis;
  end;
  let d2;
  redefine func <%d,d1,d2%> -> XFinSequence of D;
  coherence
  proof
    <%d,d1,d2%>=<%d%>^<%d1%>^<%d2%> by AFINSQ_1:def 7;
    hence thesis;
  end;
end;  
  
theorem
  b "**" <%d1,d2%> = b.(d1,d2)
  proof
    len <%d1,d2%>=2 by AFINSQ_1:42;
    then consider f be Function of NAT,D such that
      A1:f.0 = <%d1,d2%>.0 and
      A2:for n st n+1 < 2 holds f.(n + 1) = b.(f.n,<%d1,d2%>.(n + 1)) and
      A3:b "**" <%d1,d2%> = f.(2-1) by Def3;
    f.(0+1)=b.(f.0,<%d1,d2%>.(0+1)) & <%d1,d2%>.0=d1 & <%d1,d2%>.1=d2
      by A2,AFINSQ_1:42;
    hence thesis by A1,A3;
  end;
  
theorem
  b "**" <%d,d1,d2%> = b.(b.(d,d1),d2)
  proof
    set F=<%d,d1,d2%>;
    len F=3 by AFINSQ_1:43;
    then consider f be Function of NAT,D such that
      A1:f.0 = F.0 and
      A2:for n st n+1 < 3 holds f.(n + 1) = b.(f.n,F.(n + 1)) and
      A3:b "**" F = f.(3-1) by Def3;
    f.(0+1)=b.(f.0,F.(0+1)) & f.(1+1)=b.(f.1,F.(1+1)) & F.0=d & F.1=d1 & F.2=d2
      by A2,AFINSQ_1:43;
    hence thesis by A1,A3;
  end;

definition let Fn;
  func Sum(Fn) -> Nat equals :Def4:
    addnat "**" Fn;
  coherence;
end;

definition let Fn,x;
  redefine func Fn.x -> Nat;
  coherence
  proof
    per cases;
      suppose x in dom Fn;
        then Fn.x in rng Fn & rng Fn c= NAT by FUNCT_1:def 5,ORDINAL1:def 8;
        hence Fn.x is Nat;
      end;
      suppose not x in dom Fn;
        hence thesis by FUNCT_1:def 4,NAT_1:18;
      end;
    end;
  end;      
  
theorem Th51:
  (for n st n in dom Fn holds Fn.n <= k)
    implies
      Sum (Fn)<=len Fn * k
proof
  assume A1:for n st n in dom Fn holds Fn.n <= k;
  now per cases by NAT_1:18;
    suppose dom Fn=0;
      then len Fn=0 &  Sum Fn= addnat "**" Fn by AFINSQ_1:def 1,Def4;
      hence thesis by BINOP_2:5,Def3;
    end;
    suppose dom Fn>0;
      then A2:len Fn>0 by AFINSQ_1:def 1;
      then consider f be Function of NAT,NAT such that
        A3:f.0 = Fn.0 and
        A4:(for n st n+1 < len Fn holds f.(n + 1) = addnat.(f.n,Fn.(n + 1)))and
        A5:addnat "**" Fn= f.(len Fn-1) by Def3;
      defpred P[Nat] means
        $1 < len Fn implies f.$1 <=($1+1)*k;
      A6:P[0]
        proof
          assume 0<len Fn;
          then 0< dom Fn by AFINSQ_1:def 1;
          then 0 in dom Fn by EULER_1:1;
          hence thesis by A1,A3;
        end;
      A7:for m st P[m] holds P[m+1]
        proof
          let m such that A8:P[m];
          assume m + 1 < len Fn;
          then m < len Fn & m+1 in len Fn & len Fn=dom Fn &
            f.(m+1)=addnat.(f.m,Fn.(m+1))
            by NAT_1:38,A4,EULER_1:1,AFINSQ_1:def 1;
          then f.m <=(m+1)*k & Fn.(m+1) <= k &
            f.(m + 1) = f.m + Fn.(m+1) by A1,A8,BINOP_2:def 23;
          then f.(m+1) <=(m+1) * k +k by REAL_1:55;
          hence thesis;
        end;
      A8:for m holds P[m] from NAT_1:sch 1(A6,A7);
      reconsider lenFn1=len Fn -1 as Nat by A2,NAT_1:60;
      lenFn1<lenFn1+1 by NAT_1:38;
      then addnat "**" Fn <= (lenFn1+1) * k by A5,A8;
      hence thesis by Def4;
    end;
  end;
  hence thesis;
end;

theorem Th52:
  (for n st n in dom Fn holds Fn.n >= k)
    implies
      Sum (Fn) >= len Fn * k
proof
  assume A1:for n st n in dom Fn holds Fn.n >= k;
  now per cases by NAT_1:18;
    suppose dom Fn=0;
      then len Fn=0 &  Sum Fn= addnat "**" Fn by AFINSQ_1:def 1,Def4;
      hence thesis by BINOP_2:5,Def3;
    end;
    suppose dom Fn>0;
      then A2:len Fn>0 by AFINSQ_1:def 1;
      then consider f be Function of NAT,NAT such that
        A3:f.0 = Fn.0 and
        A4:(for n st n+1 < len Fn holds f.(n + 1) = addnat.(f.n,Fn.(n + 1)))and
        A5:addnat "**" Fn= f.(len Fn-1) by Def3;
      defpred P[Nat] means
        $1 < len Fn implies f.$1 >= ($1+1)*k;
      A6:P[0]
        proof
          assume 0<len Fn;
          then 0< dom Fn by AFINSQ_1:def 1;
          then 0 in dom Fn by EULER_1:1;
          hence thesis by A1,A3;
        end;
      A7:for m st P[m] holds P[m+1]
        proof
          let m such that A8:P[m];
          assume m + 1 < len Fn;
          then m < len Fn & m+1 in len Fn & len Fn=dom Fn &
            f.(m+1)=addnat.(f.m,Fn.(m+1))
            by NAT_1:38,A4,EULER_1:1,AFINSQ_1:def 1;
          then f.m >=(m+1)*k & Fn.(m+1) >= k &
            f.(m + 1) = f.m + Fn.(m+1) by A1,A8,BINOP_2:def 23;
          then f.(m+1) >=(m+1) * k +k by REAL_1:55;
          hence thesis;
        end;
      A8:for m holds P[m] from NAT_1:sch 1(A6,A7);
      reconsider lenFn1=len Fn -1 as Nat by A2,NAT_1:60;
      lenFn1<lenFn1+1 by NAT_1:38;
      then addnat "**" Fn >= (lenFn1+1) * k by A5,A8;
      hence thesis by Def4;
    end;
  end;
  hence thesis;      
end;

theorem Th53:
  (len Fn > 0 & ex x st x in dom Fn & Fn.x = k)
    implies
      Sum Fn >= k
proof
  assume that A1:len Fn > 0 and A2: ex x st x in dom Fn & Fn.x = k;
  consider x such that
    A3:x in dom Fn & Fn.x = k by A2;
  reconsider x as Nat by A3;
  reconsider lenFn1=len Fn-1 as Nat by A1,NAT_1:60;  
  consider f be Function of NAT,NAT such that
    A4:f.0 = Fn.0 and
    A5:for n st n+1 < len Fn holds f.(n + 1) = addnat.(f.n,Fn.(n + 1))and
    A6:addnat "**" Fn= f.(len Fn-1) by A1,Def3; 
  defpred P[Nat] means
    x <= $1 & $1 < len Fn implies f.$1 >= k;
  A7:P[x]
    proof
      now per cases by NAT_1:18;
        suppose A8:x=0;
          assume x <= x & x < len Fn;
          thus f.x>=k by A3,A4,A8;
        end;
        suppose x>0;
          then reconsider x1=x-1 as Nat by NAT_1:60;
          assume x <= x & x < len Fn;
          then x1+1 < len Fn;
          then f.x = addnat.(f.x1,Fn.x) by A5;
          then f.x=f.x1+Fn.x & f.x1>=0 by NAT_1:18,BINOP_2:def 23;
          then f.x>=k+0 by A3,REAL_1:55;
          hence f.x>=k;
        end;
      end;
      hence thesis;
    end;
  A8:for m st m>=x & P[m] holds P[m+1]
    proof
      let m such that A9:m>=x & P[m];
      assume x <= m+1 & m+1 < len Fn;
      then f.(m + 1) = addnat.(f.m,Fn.(m + 1)) & m <len Fn by A5,NAT_1:38;
      then f.(m+1)=f.m+Fn.(m+1) & f.m >=k & Fn.(m+1)>=0
        by A9,BINOP_2:def 23,NAT_1:18;
      then f.(m+1) >= k+0 by REAL_1:55;
      hence thesis;
    end;
  A9:for m st m>=x holds P[m] from INT_2:sch 1(A7,A8);  
  x < dom Fn & dom Fn=lenFn1+1 by A3,EULER_1:1,AFINSQ_1:def 1;
  then x<=lenFn1 & lenFn1 < lenFn1+1 by NAT_1:38;
  then addnat "**" Fn >=k by A6,A9;
  hence thesis by Def4;  
end;

theorem
  Sum Fn=0 iff (len Fn=0 or for n st n in dom Fn holds Fn.n=0)
proof
  A1:(len Fn=0 or for n st n in dom Fn holds Fn.n=0) implies Sum Fn=0
  proof
    assume A2:len Fn=0 or for n st n in dom Fn holds Fn.n=0;
    now per cases;
      suppose len Fn=0;
        then len Fn=0 & Sum Fn= addnat "**" Fn by Def4;
        hence thesis by BINOP_2:5,Def3;
      end;
      suppose len Fn<>0;
        then (for n st n in dom Fn holds Fn.n<=0) &
        (for n st n in dom Fn holds Fn.n >= 0) by A2;
        then Sum Fn <= len Fn*0 & Sum Fn >= len Fn*0 by Th51,Th52;
        hence thesis;
      end;
    end;
    hence thesis;    
  end;
  Sum Fn=0 implies (len Fn=0 or for n st n in dom Fn holds Fn.n=0)
  proof
    assume A2:Sum Fn=0;
    now per cases by NAT_1:18;
      suppose len Fn=0;
        hence thesis;
      end;
      suppose A3:len Fn>0;
        assume not (len Fn=0 or for n st n in dom Fn holds Fn.n=0);
        then consider n such that
          A4:n in dom Fn & Fn.n <>0;
        Sum Fn >=Fn.n by A3,A4,Th53;
        hence contradiction by A2,A4,NAT_1:18; 
      end;
    end;
    hence thesis;
  end;
  hence thesis by A1;
end;

theorem Th55:
  for f be Function, n holds
    union (rng (f|n)) \/ f.n=union rng (f|(n+1))
proof
  let f be Function,n;
  thus union (rng (f|n))\/f.n c=union rng (f|(n+1))
    proof
      let x such that A2:x in union (rng (f|n))\/f.n;
      now per cases by A2,XBOOLE_0:def 2;
        suppose x in union rng (f|n);
          then consider Y be set such that
            A3:x in Y & Y in rng (f|n) by TARSKI:def 4;
          consider X be set such that
            A4:X in dom (f|n) & (f|n).X=Y by A3,FUNCT_1:def 5;
          n<=n+1 by NAT_1:29;
          then n c= n+1 by CARD_1:56;
          then X in dom f /\n & dom f /\n c= dom f /\(n+1)
              by XBOOLE_1:26,A4,FUNCT_1:68;
          then X in dom f /\(n+1);
          then X in dom (f|(n+1)) by FUNCT_1:68;
          then (f|(n+1)).X in rng (f|(n+1)) &
            (f|(n+1)).X=f.X & (f|n).X=f.X by A4,FUNCT_1:def 5,68;
          hence thesis by A3,A4,TARSKI:def 4;
        end;
        suppose A3:x in f.n;
          n<n+1 by NAT_1:38;
          then n in dom f & n in n+1 by A3,FUNCT_1:def 4,EULER_1:1;
          then n in dom f /\ (n+1) by XBOOLE_0:def 3;
          then n in dom (f|(n+1)) by FUNCT_1:68;
          then (f|(n+1)).n in rng (f|(n+1)) &
            (f|(n+1)).n=f.n by FUNCT_1:def 5,68;         
          hence thesis by A3,TARSKI:def 4;
        end;
      end;
      hence thesis;
    end;
    thus union rng (f|(n+1))c=union (rng (f|n))\/f.n
    proof
      let x such that A2:x in union rng (f|(n+1));
      consider Y be set such that
        A3:x in Y & Y in rng (f|(n+1)) by A2,TARSKI:def 4;
      consider X be set such that
        A4:X in dom (f|(n+1)) & (f|(n+1)).X=Y by A3,FUNCT_1:def 5;
      X in dom f /\(n+1) by A4,FUNCT_1:68;
      then A5:X in n+1 & X in dom f by XBOOLE_0:def 3;
      n+1 is Subset of NAT by Th8;
      then reconsider X as Nat by A5;
      X < n+1 by A5,EULER_1:1;
      then A6:X<=n by NAT_1:38;
      now per cases by A6,REAL_1:def 5;
        suppose X < n;
          then X in n by EULER_1:1;
          then X in dom f/\n by A5,XBOOLE_0:def 3;
          then X in dom (f|n) by FUNCT_1:68;
          then (f|n).X=f.X & f.X=(f|(n+1)).X &
            (f|n).X in rng (f|n) by A4,FUNCT_1:68,def 5;
          then x in union rng (f|n) by A3,A4,TARSKI:def 4;
          hence thesis by XBOOLE_0:def 2;
        end;
        suppose X=n;
          then X=n & f.X=(f|(n+1)).X by A4,FUNCT_1:68;
          hence thesis by A3,A4,XBOOLE_0:def 2;
        end;
      end;
      hence thesis;
    end;
end;

scheme Sch6{D()->non empty set,n()->Nat,P[set,set]}:
  ex p be XFinSequence of D() st dom p = n() &
                     for k st k in n() holds P[k,p.k]
 provided
  A1:for k st k in n() ex x be Element of D() st P[k,x]
proof
  A2:for k be set st k in n() ex x be set st x in D() & P[k,x]
    proof
      let k be set such that A3:k in n();
      n() is Subset of NAT by Th8;
      then reconsider k'=k as Nat by A3;
      ex x be Element of D() st P[k',x] by A1,A3;
      hence thesis;
    end;
  consider f be Function of n(),D() such that
  A3:for x st x in n() holds P[x,f.x] from FUNCT_2:sch 1(A2);
  dom f=n() by FUNCT_2:def 1;
  then reconsider p=f as XFinSequence by AFINSQ_1:7;
  take p;
  rng f c= D() & dom f=n() by FUNCT_2:def 1;
  hence thesis by A3,ORDINAL1:def 8;  
end;

scheme Sch7{D()->non empty set,F()-> XFinSequence of D()}:
    ex CardF be XFinSequence of NAT st
      dom CardF = dom F()
      & (for i st i in dom CardF holds CardF.i=Card (F().i))
      & Card union rng F() = Sum(CardF)
provided      
  A1:for i st i in dom F() holds F().i is finite and
  A2:for i,j st i in dom F() & j in dom F() & i<>j holds F().i misses F().j
  proof
    defpred FF[Nat,set] means $2=Card (F().$1);
    A3:for k st k in len F() ex x be Element of NAT st FF[k,x]
      proof
        let k such that A4:k in len F();        
        k in dom F() by A4,AFINSQ_1:def 1;
        then F().k is finite by A1;
        then consider m such that
          A5:(F().k),m are_equipotent by CARD_1:74;
        Card (F().k)=Card m & Card m =m by A5,CARD_1:21,66;
        hence thesis;    
      end;
    consider CardF be XFinSequence of NAT such that
      A4:dom CardF=len F() & for k st k in len F() holds FF[k,CardF.k]
         from Sch6(A3);
    take CardF;
    thus A5:dom CardF = dom F() by A4,AFINSQ_1:def 1;
    thus for i st i in dom CardF holds CardF.i=Card (F().i) by A4;  
    now per cases;
      suppose A6:len CardF=0;
        then dom CardF={} by RELAT_1:60,Th33;
        then union rng F() is empty & addnat "**" CardF = the_unity_wrt addnat
          by A6,ZFMISC_1:2,RELAT_1:65,A5,Def3;
        then Card (union rng F())=0 & Sum(CardF)=0
          by Th33,Def4,BINOP_2:5;
        hence Card union rng F() = Sum(CardF);
      end;
      suppose A6:len CardF<>0;
        then consider f be Function of NAT,NAT such that
          A7:f.0 = CardF.0 &
            (for n st n+1<len CardF holds f.(n+1)=addnat.(f.n,CardF.(n + 1)))&
             addnat "**" CardF = f.(len CardF-1) by Def3;           
        defpred P[Nat] means
          $1<len CardF implies
          Card union rng (F()|($1+1))=f.$1 &
          union rng (F()|($1+1)) is finite;
        A8:P[0]
        proof
          assume 0<len CardF;
          dom F()/\0={};
          then dom (F()|0)={} & 0<len CardF by A6,FUNCT_1:68,NAT_1:18;
          then union rng (F()|0)={} & 0 in len CardF &
            union rng (F()|(0+1)) = union rng (F()|(0))\/F().0
            by Th55,ZFMISC_1:2,EULER_1:1,RELAT_1:65;
          then union rng (F()|(0+1))=F().0 & 0 in dom CardF
                 by AFINSQ_1:def 1;
          hence thesis by A1,A4,A5,A7;
          end;
        A9:for k st P[k] holds P[k + 1]
        proof
          let k such that A10:P[k];
          set k1=k+1;
          assume that A11:k1<len CardF;           
          set Fk1=F()|k1;
          A12:union (rng Fk1) misses F().k1
            proof
              assume union (rng Fk1) meets F().k1;
              then consider x such that
                A13:x in union (rng Fk1)/\F().k1 by XBOOLE_0:4;
              x in union (rng Fk1) by A13,XBOOLE_0:def 3;
              then consider Y be set such that
                A14:x in Y & Y in rng Fk1 by TARSKI:def 4;
              consider X be set such that
                A15:X in dom Fk1 & Fk1.X=Y by A14,FUNCT_1:def 5;
              X in dom F()/\k1 by A15,FUNCT_1:68;
              then X in k1 & k1 is Subset of NAT by XBOOLE_0:def 3,Th8;
              then reconsider X as Nat;
              X in dom F()/\k1 by A15,FUNCT_1:68;
              then A16:X in dom F() & X in k1 by XBOOLE_0:def 3;
              then k1<len CardF & X <k1 by A11,EULER_1:1;
              then k1 in len CardF & X<=k & k<k1
                  by NAT_1:38,EULER_1:1;
              then k1 in dom F() & X<>k1 by A5,AFINSQ_1:def 1;
              then Fk1.X=F().X & F().X misses F().k1
                  by A2,A15,A16,FUNCT_1:68;
              then Y misses F().k1 & x in F().k1 by A13,A15,XBOOLE_0:def 3;
              hence contradiction by A14,XBOOLE_0:3; 
            end;
          set rFk1=rng Fk1;set rFk2=rng (F()|(k1+1));
          reconsider urFk1=union rFk1 as finite set by A10,A11,NAT_1:38;
          k1 in len CardF by A11,EULER_1:1;
          then k1 in dom F() by A5,AFINSQ_1:def 1;
          then reconsider Fk1=F().k1 as finite set by A1;
              urFk1 \/Fk1=union rFk2 by Th55;
          then reconsider urFk2=union rFk2 as finite set;
          len CardF = len F() by A4,AFINSQ_1:def 1;
          then k<len CardF & k1 in len F() & k1 <len CardF
                by A11,NAT_1:38,EULER_1:1;
          then f.k1=addnat.(f.k,CardF.k1) & card Fk1=CardF.k1 by A4,A7;
          then f.k1=f.k + card Fk1 & card (urFk1 \/ Fk1) = f.k + card Fk1
            by BINOP_2:def 23,A10,A11,A12,NAT_1:38,CARD_2:53;
          hence thesis by Th55;                         
        end;
        A10:for k being Nat holds P[k] from NAT_1:sch 1(A8,A9);
        0<len CardF by A6,NAT_1:18;
        then reconsider C1=len CardF-1 as Nat by NAT_1:60;
        dom F() c= len CardF by A5,AFINSQ_1:def 1;         
        then C1 <C1+1 & F()|len CardF =F() by RELAT_1:97,NAT_1:38;
        then Card union rng F()=addnat "**" CardF by A10,A7;         
        hence thesis by Def4;
      end;
    end;
  hence thesis;
end;

scheme Sch8{X,Y()->finite set,x()->set,P[set],f()->Function of card Y(),Y()}:
    ex F be XFinSequence of NAT st
      dom F = card Y() &
      Card{g where g is Function of X(),Y():P[g]} = Sum(F) &
      for i st i in dom F holds F.i = Card{g where g is Function of X(),Y():
                                         P[g] & g.x()=f().i}
provided
  A0:f() is onto one-to-one and                                       
  A1:Y() is non empty and
  A2:x() in X()
proof
  consider n such that
    A3:Y(),n are_equipotent by CARD_1:74;    
  defpred FF[Nat,set] means
    $2={g where g is Function of X(),Y():P[g]& g.x()=f().$1};
  A4:for k st k in n ex x be Subset of Funcs(X(),Y()) st FF[k,x]
  proof
    let k such that k in n;        
    set F0={g where g is Function of X(),Y():P[g]& g.x()=f().k};
    F0 c= Funcs(X(),Y())
    proof
      let x be set such that A6:x in F0;
      ex g be Function of X(),Y() st x=g & P[g]& g.x()=f().k by A6;
      hence thesis by A1,FUNCT_2:11;    
    end;
    hence thesis;
  end;
  consider F be XFinSequence of bool Funcs(X(),Y()) such that
    A5:dom F = n & for k st k in n holds FF[k,F.k] from Sch6(A4);
  A6:for i st i in dom F holds F.i is finite
  proof
    let i such that A7:i in dom F;
    F.i in rng F & rng F c= bool Funcs(X(),Y())
      by A7,FUNCT_1:def 5,ORDINAL1:def 8;
    then F.i c= Funcs(X(),Y()) & Funcs(X(),Y()) is finite
      by FRAENKEL:16;
    hence thesis by FINSET_1:13;
  end;
  A7:for i,j st i in dom F & j in dom F & i<>j holds F.i misses F.j
  proof
    let i,j such that A8:i in dom F & j in dom F & i<>j;
    assume F.i meets F.j;
    then consider x such that
      A9:x in F.i/\F.j by XBOOLE_0:4;
    x in F.i by A9,XBOOLE_0:def 3;
    then x in {g where g is Function of X(),Y():P[g]& g.x()=f().i}
      by A5,A8;  
    then consider gi be Function of X(),Y() such that
      A10:x=gi & P[gi]& gi.x()=f().i;  
    x in F.j by A9,XBOOLE_0:def 3;
    then x in {g where g is Function of X(),Y():P[g]& g.x()=f().j}
      by A5,A8;  
    then consider gj be Function of X(),Y() such that
      A11:x=gj & P[gj]& gj.x()=f().j;       
    dom f()= card Y() & card Y()=card n & card n=n
      by A1,A3,FUNCT_2:def 1,CARD_1:21,66;
    hence contradiction by A0,A8,A5,A10,A11,FUNCT_1:def 8;      
  end;
  consider CardF be XFinSequence of NAT such that
    A8:dom CardF = dom F and
    A9: (for i st i in dom CardF holds CardF.i=Card (F.i)) and
    A10: Card union rng F = Sum(CardF) from Sch7(A6,A7);
  take CardF;
  card Y()=card n & card n=n by A3,CARD_1:21,66;
  hence dom CardF = card Y() by A8,A5;
  A11:for i st i in dom CardF holds
      CardF.i = Card{g where g is Function of X(),Y():P[g] & g.x()=f().i}
  proof
    let i such that A12:i in dom CardF;
    F.i={g where g is Function of X(),Y():P[g] & g.x()=f().i}
     by A8,A5,A12;
    hence thesis by A9,A12;
  end;
  thus Card{g where g is Function of X(),Y():P[g]} = Sum(CardF)
  proof
    set G={g where g is Function of X(),Y():P[g]};
    A12:union rng F c=G
    proof
      let x such that A13:x in union rng F;
      consider Y be set such that
        A14:x in Y & Y in rng F by A13,TARSKI:def 4;
      consider X be set such that
        A15:X in dom F & F.X=Y by A14,FUNCT_1:def 5;
      Y={g where g is Function of X(),Y():P[g] & g.x()=f().X} by A5,A15;
      then ex gX be Function of X(),Y() st x=gX& P[gX] &gX.x()=f().X by A14;
      hence thesis;
    end;
    G c= union rng F
    proof
      let x such that A13:x in G;
      consider g be Function of X(),Y() such that
        A14:x=g  & P[g] by A13;
      dom g=X() by A1,FUNCT_2:def 1;
      then g.x() in rng g by A2,FUNCT_1:def 5;
      then g.x() in Y() & rng f()=Y() by A0,FUNCT_2:def 3;
      then consider y such that
        A15:y in dom f() & f().y=g.x() by FUNCT_1:def 5;
      y in card Y() & card Y()=card n & card n=n
        by A3,CARD_1:21,66,A15;
      then F.y={g1 where g1 is Function of X(),Y():P[g1] & g1.x()=f().y}
        & F.y in rng F by A5,FUNCT_1:def 5;
      then g in F.y & F.y in rng F by A14,A15;
      hence thesis by A14,TARSKI:def 4;
    end;
    hence thesis by A10,A12,XBOOLE_0:def 10;
  end;
  thus for i st i in dom CardF holds
  CardF.i=Card{g where g is Function of X(),Y():P[g]&g.x()=f().i} by A11;
end;

theorem Th56:
  k * (n block k)=
  Card{f where f is Function of n+1,k: f is onto "increasing & f"{f.n}<>{n}}
proof
  now per cases by NAT_1:18;
    suppose A1:k=0;
      set F={f where f is Function of n+1,k:
                             f is onto "increasing & f"{f.n}<>{n}};
      set F1={f where f is Function of n+1,k: f is onto "increasing};
      A2:F c= F1
      proof
        let x such that A3:x in F;
        ex f be Function of n+1,k st x=f
          & f is onto "increasing & f"{f.n}<>{n} by A3;
        hence thesis;
      end;
      Card F1=(n+1) block k by Def2;
      then Card F1=0 by A1,Th31;
      then F1 is empty by Th33;
      then F is empty by A2,XBOOLE_1:3;          
      hence thesis by A1,Th33;
    end;
    suppose A1:k>0;
      defpred P[set] means
        ex f be Function of n+1,k st f=$1& f is onto "increasing&f"{f.n}<>{n};
      card k,k are_equipotent & card k=k by CARD_1:66;
      then consider f be Function such that
        A2:f is one-to-one & dom f = card k & rng f = k by WELLORD2:def 4;
      reconsider f as Function of card k,k by A2,FUNCT_2:3;
      A3:f is onto one-to-one by A2,FUNCT_2:def 3;
      A4:k is non empty by A1;
      n<n+1 by NAT_1:38;
      then A5:n in n+1 by EULER_1:1;
      consider F be XFinSequence of NAT such that
        A6:dom F = card k and
        A7:Card{g where g is Function of n+1,k:P[g]} = Sum(F) and
        A8:for i st i in dom F holds
              F.i = Card{g where g is Function of n+1,k:P[g] & g.n=f.i}
              from Sch8(A3,A4,A5);
      for i st i in dom F holds F.i = n block k
        proof
          let i such that A10:i in dom F;
          A11:f.i in rng f & rng f=k & k is Subset of NAT
            by A2,A6,A10,FUNCT_1:def 5,Th8;
          then reconsider fi=f.i as Nat;
          set F={g where g is Function of n+1,k:g is onto "increasing &
                                                g"{g.n}<>{n} & g.n=fi};
          set F1={g where g is Function of n+1,k:P[g] & g.n=fi};
          set F2={g where g is Function of n,k:g is onto "increasing};
          A12:F c= F1
            proof
               let x such that A13:x in F;
               ex g be Function of n+1,k st
                x=g &g is onto "increasing & g"{g.n}<>{n} & g.n=fi by A13;
               hence thesis;
            end;
          F1 c= F
            proof
              let x such that A13:x in F1;
              ex g be Function of n+1,k st
                x=g & P[g] & g.n=fi by A13;
              hence thesis;
            end;
          then F=F1 & fi<k by A11,A12,EULER_1:1,XBOOLE_0:def 10;
          then Card F1=Card F2 & Card F2= n block k by Th44,Def2;
          hence thesis by A8,A10;
        end;        
        then (for i st i in dom F holds F.i <= n block k)&
        (for i st i in dom F holds F.i >= n block k);
        then Sum F <= len F * (n block k) &
             Sum F >= len F * (n block k) & len F = card k & card k =k
          by A6,AFINSQ_1:def 1,CARD_1:66,Th51,Th52;
        then A9:Sum F= k*(n block k) by AXIOMS:21;
        set G={g where g is Function of n+1,k:P[g]};
        set G1={g where g is Function of n+1,k:
                     g is onto "increasing & g"{g.n}<>{n}};   
        A10:G1 c= G
            proof
               let x such that A11:x in G1;
               ex g be Function of n+1,k st
                x=g &g is onto "increasing & g"{g.n}<>{n} by A11;
               hence thesis;
            end;
          G c= G1
            proof
              let x such that A11:x in G;
              ex g be Function of n+1,k st x=g & P[g] by A11;
              hence thesis;
            end;
        hence thesis by A7,A9,A10,
          XBOOLE_0:def 10;
     end;
   end;
   hence thesis;
 end;

:::::::::::::::::::::::::::::::::::
::     Recursive dependence      ::
::  number Stirling second kind  ::
:::::::::::::::::::::::::::::::::::

theorem Th57:
  (n+1) block (k+1) = (k+1)*(n block (k+1)) + (n block k)
proof
  set F={f where f is Function of n+1,k+1:f is onto "increasing};
  set F1={f where f is Function of n+1,k+1:f is onto "increasing&f"{f.n}={n}};
  set F2={f where f is Function of n+1,k+1:f is onto "increasing&f"{f.n}<>{n}};
  A1:F c= F1\/F2
  proof
    let x such that A2:x in F;
    consider f be Function of n+1,k+1 such that
    A3:f =x & f is onto "increasing by A2;
    f"{f.n}={n} or f"{f.n}<>{n};
    then f in F1 or f in F2 by A3; 
    hence thesis by A3,XBOOLE_0:def 2;
  end;
  F1\/F2 c= F
  proof
    let x such that A2:x in F1\/F2;
    x in F1 or x in F2 by A2,XBOOLE_0:def 2;
    then (ex f be Function of n+1,k+1 st f = x & f is onto "increasing & 
      f"{f.n}={n})
      or (ex f be Function of n+1,k+1 st f = x & f is onto "increasing & 
      f"{f.n}<>{n});
    hence thesis;    
  end;
  then A2:F1\/F2=F by A1,XBOOLE_0:def 10;
  A3:F1 misses F2
  proof
    assume F1 meets F2;
    then consider x such that
      A4:x in F1/\F2 by XBOOLE_0:4;
    x in F1 & x in F2 by A4,XBOOLE_0:def 3;
    then (ex f be Function of n+1,k+1 st f = x & f is onto "increasing & 
      f"{f.n}={n}) & (ex f be Function of n+1,k+1 st f = x &
      f is onto "increasing & f"{f.n}<>{n});
    hence contradiction;
  end;
  A4:F1 c= Funcs(n+1,k+1)
  proof
    let x such that A5:x in F1;
    ex f be Function of n+1,k+1 st f = x & f is onto "increasing & f"{f.n}={n} 
      by A5;
    hence thesis by FUNCT_2:11;
  end;
  A5:F2 c= Funcs(n+1,k+1)
  proof
    let x such that A5:x in F2;
    ex f be Function of n+1,k+1 st f = x & f is onto "increasing &
      f"{f.n}<>{n} by A5;
    hence thesis by FUNCT_2:11;
  end;
  Funcs(n+1,k+1) is finite by FRAENKEL:16;
  then reconsider F1,F2 as finite set by A4,A5,FINSET_1:13;  
  card F1=Card{f where f is Function of n,k: f is onto "increasing}
    by Th43;
  then card F1= n block k & card F2= (k+1) * (n block (k+1)) &
    Card F =(n+1) block (k+1) by Th56,Def2;  
  hence thesis by A2,A3,CARD_2:53;
end;

theorem Th58:        
  n>=1 implies n block 2 = 1/2 * (2 |^ n - 2)
proof
  defpred P[Nat] means ($1) block 2 = 1/2 * (2 |^ $1-2);
  A1:P[1]
    proof
      2 |^1= 2 by NEWTON:10;
      hence thesis by Th29;
    end;
  A2:for k st k>=1 & P[k] holds P[k + 1]
    proof
      let k such that A3:k >=1 & P[k];
      (k+1) block 2 = 2*( k block (1+1)) + ( k block 1) by Th57
      .=2 * (1/2 * (2 |^ k - 2)) + 1 by A3,Th32,CARD_2:20
      .=1/2* (2* 2 |^ k - 2)
      .=1/2* (2 |^ (k+1)-2) by NEWTON:11;
      hence thesis;                 
    end;
  for k st k>=1 holds P[k] from INT_2:sch 1(A1,A2);
  hence thesis;   
end;

theorem Th59:
  n >= 2 implies n block 3 = 1/6 * ( 3 |^ n - 3 * 2 |^ n + 3 )
proof
  defpred P[Nat] means $1 block 3=1/6*(3|^$1-3*2|^$1+3);
  A1:P[2]
    proof
      1/6*(3|^2-3*2|^2+3)=1/6*(3*3-3*2|^2+3) by WSIERP_1:2
      .=1/6*(3*3-3*(2*2)+3) by WSIERP_1:2
      .=2 block 3 by Th29;
      hence thesis;  
    end;
  A2:for k st k>=2 & P[k] holds P[k+1]
    proof
      let k such that A3:k>=2 & P[k];
      k>=1 by A3,AXIOMS:22;
      then k block 2 = 1/2 * (2 |^ k - 2) by Th58;
      hence (k+1) block 3 = 3*(k block (2+1)) +(1/2*(2|^k-2)) by Th57
      .=3* (1/6* (3|^k-3*2|^k+3)) + (1/2*(2|^k-2)) by A3
      .=1/6*((3*3|^k)-3*2*2|^k +3)
      .=1/6*(3|^(k+1)-3*(2*2|^k) +3) by NEWTON:11
      .=1/6*(3|^(k+1)-3*2|^(k+1) +3) by NEWTON:11;           
    end;
  for k st k>=2 holds P[k] from INT_2:sch 1(A1,A2);
  hence thesis;     
end;

Lm03: n|^3=n*n*n
proof
  thus n|^3=n|^(2+1)
  .=n|^2*n by NEWTON:11
  .=n*n*n by WSIERP_1:2;  
end; 

theorem
  n >= 3 implies n block 4 = 1/24 * ( 4 |^ n - 4 * 3 |^ n + 6* 2|^ n - 4 )
proof
  defpred P[Nat] means $1 block 4=
    1/24 * ( 4 |^ $1 - 4 * 3 |^ $1 + 6* 2|^ $1 - 4 );
  A1:P[3]
    proof
      1/24*(4|^3-4*3|^3+6*2|^3-4)=
        1/24*(4*4*4-4*3|^3+6*2|^3-4) by Lm03
      .=1/24*(64-4*(3*3*3)+6*2|^3-4) by Lm03
      .=1/24*(64-4*27+6*(2*2*2)-4) by Lm03   
      .=3 block 4 by Th29;
      hence thesis;  
    end;
  A2:for k st k>=3 & P[k] holds P[k+1]
    proof
      let k such that A3:k>=3 & P[k];
      k>=2 by A3,AXIOMS:22;
      then k block 3 = 1/6 * (3|^k-3*2|^k+3) by Th59;
      hence (k+1) block 4 = 4*(k block (3+1)) +(1/6*(3|^k-3*2|^k+3)) by Th57
      .=4*(1/24*(4|^k-4*3|^k+6*2|^k-4)) +(1/24*4*(3|^k-3*2|^k+3)) by A3
      .=1/24*((4*4|^k)- 4*(3*3|^k)+ 6*(2*2|^k) - 4)
      .=1/24*(4|^(k+1)- 4*(3*3|^k)+ 6*(2*2|^k) - 4) by NEWTON:11
      .=1/24*(4|^(k+1)- 4*3|^(k+1)+ 6*(2*2|^k) - 4) by NEWTON:11
      .=1/24*(4|^(k+1)- 4*(3|^(k+1))+ 6*2|^(k+1) - 4) by NEWTON:11;           
    end; 
  for k st k>=3 holds P[k] from INT_2:sch 1(A1,A2);
  hence thesis;    
end;

theorem Th61:
  3! = 6 & 4! = 24
proof
  (2+1)!=2!*3 & (3+1)!=3!*4 by NEWTON:21;
  hence thesis by NEWTON:20;
end;

theorem Th62:
  n choose 1=n &
  n choose 2=n*(n-1)/2 &
  n choose 3=n*(n-1)*(n-2)/6 &
  n choose 4=n*(n-1)*(n-2)*(n-3)/24
proof
  now per cases by CQC_THE1:4;
    suppose n=0;
      hence thesis by NEWTON:def 3;
    end;    
    suppose n=1;
      hence thesis by NEWTON:31,def 3;
    end;
    suppose n=2;
      hence thesis by NEWTON:31,33,def 3;
    end;    
    suppose A1:n=3;
      then reconsider n1=n-1,n2=n-2 as Nat by NAT_1:60;
      n2!<>0 by NEWTON:23;
      then A2:(n2!)/(n2!)=1 by XCMPLX_1:60;
      (n1+1)!=(n1)!*n & (n2+1)!=(n2)!*(n2+1) by NEWTON:21;
      then n!=(n2)!*(n-1)*n;     
      then n choose 2=((n2)!*((n-1)*n))/((n2!)*(2!)) by A1,NEWTON:def 3
      .=((n2!)/(n2!)*((n-1)*n))/2 by XCMPLX_1:84,NEWTON:20
      .=((n-1)*n)/2 by A2;
      hence thesis by A1,NEWTON:31,33,def 3;
    end;
    suppose A1:n>3;
      then n>1 & n>2 & n>3 & n>=3+1 by NAT_1:38,AXIOMS:22;
      then reconsider n1=n-1,n2=n-2,n3=n-3,n4=n-4 as Nat by NAT_1:61;
      n2!<>0 & n3!<>0 & n4!<>0 by NEWTON:23;
      then A2:(n2!)/(n2!)=1 & (n3!)/(n3!)=1 & (n4!)/(n4!)=1 by XCMPLX_1:60;
      A3:(n1+1)!=(n1)!*n & (n2+1)!=(n2)!*(n2+1) & (n3+1)!=(n3)!*(n3+1) &
        (n4+1)!=(n4)!*(n4+1) by NEWTON:21;
      n>=2 by A1,AXIOMS:22;
      then A4:n choose 2=((n2)!*n1*n)/((n2!)*(2!)) by A3,NEWTON:def 3
      .=((n2)!*(n1*n))/((n2!)*(2!))
      .=((n2!)/(n2!)*(n1*n))/2 by XCMPLX_1:84,NEWTON:20
      .=n*n1/2 by A2;
      A5:n choose 3=((n3)!*n2*n1*n)/((n3!)*(3!))
        by A1,A3,NEWTON:def 3
      .=((n3)!*(n2*n1*n))/((n3!)*(3!))
      .=((n3!)/(n3!)*(n2*n1*n))/6 by XCMPLX_1:84,Th61
      .=n*n1*n2/6 by A2;
      n>=3+1 by A1,NAT_1:38;
      then A6:n choose 4=((n4)!*n3*n2*n1*n)/((n4!)*(4!))
        by A3,NEWTON:def 3
      .=((n4!)*(n3*n2*n1*n))/((n4!)*(4!))
      .=((n4!)/(n4!)*(n3*n2*n1*n))/24 by XCMPLX_1:84,Th61
      .=n*n1*n2*n3/24 by A2;
      n>=1 by A1,AXIOMS:22;
      hence thesis by A4,A5,A6,NEWTON:33;
    end;
  end;
  hence thesis;      
end;

theorem Th63:
  (n + 1) block n = (n + 1) choose 2
proof
  defpred P[Nat] means ($1+1) block $1 = ($1+1) choose 2;
  A1:P[0]
    proof
      1 block 0 =0 & 1 choose 2 =0 by Th31,NEWTON:def 3;
      hence thesis;
    end;
  A2:for k st P[k] holds P[k+1]
    proof
      let k such that A3:P[k];set k1=k+1;
      thus (k1+1) block k1=k1*(k1 block k1)+(k1 block k) by Th57
      .=k1*1+(k1 choose 2) by A3,Th26,CARD_2:20
      .=k1+k1*(k1-1)/2 by Th62
      .=(k1+1)*(k1+1-1)/2
      .=(k1+1) choose 2 by Th62;
    end;
  for k holds P[k] from NAT_1:sch 1(A1,A2);
  hence thesis;
end;    
        
theorem
  (n + 2) block n = 3*((n + 2) choose 4) + ((n + 2) choose 3)
proof
  defpred P[Nat] means ($1+2)block $1=3*(($1+2)choose 4)+(($1+2) choose 3);
  A1:P[0]
    proof
      2 choose 4=0 & 2 choose 3=0 & 2 block 0=0 by NEWTON:def 3,Th31;
      hence thesis;
    end;
  A2:for k st P[k] holds P[k+1]
    proof
      let k such that A3:P[k];set k2=k+2;set k1=k+1;set k3=k2+1;
      A4:k2 block k=3*(k2*(k2-1)*(k2-2)*(k2-3)/24)+(k2 choose 3) by A3,Th62
      .=3*(k2*(k2-1)*(k2-2)*(k2-3)/24)+(k2*(k2-1)*(k2-2)/6) by Th62
      .=k2*(k2-1)*(3*(k2-2)*(k2-3)/24 +4*(k2-2)/24);
      k1*((k1+1) block k1)=k1*(k2 choose 2) by Th63
      .=k1*(k2*(k2-1)/2) by Th62
      .=k2*(k2-1)*(k1*12/24);
      then k3 block k1=
        k2*k1*(k1*12/24)+k2*k1*(3*k*(k2-3)/24+4*k/24) by A4,Th57
      .=3*(k3*(k3-1)*(k3-2)*(k3-3)/24)+k3*k2*k1/6
      .=3*(k3 choose 4) +(k3*(k3-1)*(k3-2)/6) by Th62
      .=3*(k3 choose 4)+(k3 choose 3) by Th62;     
      hence thesis;
    end;
  for k holds P[k] from NAT_1:sch 1(A1,A2);
  hence thesis;
end;
      
theorem Th66:
  for F be Function,y holds
    rng (F|(dom F\F"{y}))=rng F\{y} &
    for x st x<>y holds (F|(dom F\F"{y}))"{x}=F"{x}
proof
  let F be Function,y;
  set D=dom F\F"{y};
  thus rng (F|D)=rng F\{y}
    proof
      A1:rng F\{y} c=rng (F|D)
        proof
          let Fx be set such that A2:Fx in rng F\{y};
          Fx in rng F by A2,XBOOLE_0:def 4;
          then consider x such that
            A3:x in dom F & F.x=Fx by FUNCT_1:def 5;
          not Fx in {y} by A2,XBOOLE_0:def 4;
          then not x in F"{y} by A3,FUNCT_1:def 13;
          then x in D by A3,XBOOLE_0:def 4;
          then x in dom F /\D by A3,XBOOLE_0:def 3;
          then x in dom (F|D) by FUNCT_1:68;
          then (F|D).x in rng (F|D)&(F|D).x=F.x by FUNCT_1:70,def 5;
          hence thesis by A3;
        end;
      rng(F|D) c= rng F\{y}
        proof
          let Fx be set such that A2:Fx in rng (F|D);
          consider x such that
            A3:x in dom (F|D) & Fx=(F|D).x by A2,FUNCT_1:def 5;
          x in dom F /\ D by A3,FUNCT_1:68;
          then x in (dom F/\dom F)\F"{y} by XBOOLE_1:49;
          then x in dom F & not x in F"{y} by XBOOLE_0:def 4;
          then F.x in rng F & not F.x in {y} by FUNCT_1:def 5,def 13;
          then Fx in rng F & not Fx in {y} by A3,FUNCT_1:68;
          hence thesis by XBOOLE_0:def 4;
        end;
      hence thesis  by A1,XBOOLE_0:def 10;
    end;
    let x such that A1:x<>y;
    now let z be set such that A2:z in F"{x};
      A3:z in dom F & F.z in {x} by A2,FUNCT_1:def 13;
      then F.z <> y by A1,TARSKI:def 1;
      then not F.z in {y} by TARSKI:def 1;
      then not z in F"{y} by FUNCT_1:def 13;
      hence z in dom F\F"{y} by A3,XBOOLE_0:def 4;
    end;
    then F"{x} c= D by TARSKI:def 3;  
    hence F"{x} = (F|D)"{x} by TMAP_1:5;
  end;
  
theorem Th67:
  Card X=k+1 & x in X implies Card (X\{x})=k
  proof
    assume A1:Card X=k+1 & x in X;
    then Card X= Card (k+1) by CARD_1:66;
    then X,(k+1)are_equipotent by CARD_1:21;
    then reconsider X'=X as finite set by CARD_1:68;
    reconsider Xx=X'\{x} as finite set;
    {x} c= X by A1,ZFMISC_1:37;
    then {x} /\X ={x} by XBOOLE_1:28;
    then {x} misses Xx & Xx \/ {x} = X by XBOOLE_1:79,51;
    then card {x}+ card Xx= k+1 & card {x}=one by A1,CARD_2:53,CARD_1:50;
    hence thesis by XCMPLX_1:2,CARD_2:20;
  end;  
  
scheme Sch9{P[set],Q[set,Function]}:
  for F be Function st rng F is finite holds P[F]
provided  
A1:P[{}] and
A2:for F be Function st
     for x st x in rng F & Q[x,F] holds P[F|(dom F\F"{x})] holds P[F]
proof
  defpred R[Nat] means
    for F be Function st Card rng F=$1 holds P[F];
  A3:R[0]
  proof
    let F be Function such that A4:Card rng F=0;
    rng F={} by A4,Th33;
    hence thesis by A1,RELAT_1:64;
  end;
  A4:for n st R[n] holds R[n+1]
  proof
    let n such that A5:R[n];
    let F be Function such that A7:Card rng F=n+1;   
    for x st x in rng F & Q[x,F] holds P[F|(dom F\F"{x})]
      proof
        let x such that A8:x in rng F & Q[x,F];
        set D=dom F\F"{x};
        Card (rng F\{x})=n by A7,A8,Th67;
        then Card rng (F|D)=n by Th66;
        hence thesis by A5;
      end;     
    hence thesis by A2;
  end;
  let F be Function such that A5:rng F is finite;
  consider n such that
    A6:rng F,n are_equipotent by A5,CARD_1:74;
  Card n=n by CARD_1:66;
  then A7:Card rng F=n by A6,CARD_1:21;
  for k holds R[k] from NAT_1:sch 1(A3,A4);
  hence thesis by A7;          
end;     
    
theorem Th68:
  for N be Subset of NAT st N is finite
    ex k st for n st n in N holds n<=k
proof
  let N be Subset of NAT such that A1:N is finite;
  consider n such that
    A2:N,n are_equipotent by A1,CARD_1:74;
  consider F be Function such that
    A3:F is one-to-one & dom F = n & rng F = N by A2,WELLORD2:def 4;
  reconsider F as XFinSequence by A3,AFINSQ_1:7;
  reconsider F as XFinSequence of NAT by A3,ORDINAL1:def 8;
  take k=Sum(F);  
  let n such that A4:n in N;
  Card F <>0 by Th33,A3,A4,RELAT_1:60;
  then len F>0 & ex x st x in dom F & F.x=n by A3,A4,FUNCT_1:def 5,NAT_1:18;
  hence thesis by Th53;
end;
  
theorem  Th69:
  for X,Y,x,y st (Y is empty implies X is empty) & not x in X
    for F be Function of X,Y ex G be Function of X\/{x},Y\/{y} st
      G|X = F & G.x=y
proof
  let X,Y,x,y such that
    A1:Y is empty implies X is empty and
    A2:not x in X;
  let F be Function of X,Y;
  deffunc F(set)=y;set X1=X\/{x};set Y1=Y\/{y};
  A3:X c= X1 & Y c= Y1 by XBOOLE_1:7;
  A4:for x' be set st x' in X1\X holds F(x') in Y1
  proof
    y in {y} by TARSKI:def 1;
    hence thesis by XBOOLE_0:def 2;
  end;
  consider G be Function of X1,Y1 such that
    A5:G|X = F & for x' be set st x' in X1\X holds G.x' = F(x')
    from Sch2(A4,A3,A1);
  x in {x} by TARSKI:def 1;
  then x in X1 by XBOOLE_0:def 2;
  then x in X1\X by A2,XBOOLE_0:def 4;
  then G.x=y by A5;
  hence thesis by A5;
end;
  
theorem Th70:
  for X,Y,x,y st (Y is empty implies X is empty)
    for F be Function of X,Y,G be Function of X\/{x},Y\/{y} st
      G|X=F & G.x=y holds
        ( F is onto implies G is onto) &
        ( not y in Y & F is one-to-one implies G is one-to-one)
proof
  let X,Y,x,y such that
    A1:Y is empty implies X is empty;
  let F be Function of X,Y,G be Function of (X\/{x}),(Y\/{y}) such that
    A3:G|X=F and
    A4:G.x=y;
  thus F is onto implies G is onto
    proof
      assume A5:F is onto;
      Y\/{y} c= rng G
      proof
        let Fx be set such that A6:Fx in Y\/{y};
        now per cases by A6,XBOOLE_0:def 2;
          suppose Fx in Y;
            then Fx in rng F by A5,FUNCT_2:def 3;
            then consider x' be set such that
              A7:x' in dom F & F.x'=Fx by FUNCT_1:def 5;  
            x' in X & X c= X\/{x} & dom G=X\/{x}
              by A7,FUNCT_2:def 1,XBOOLE_1:7;
            then x' in dom G & G.x'=F.x'
              by A3,A7,FUNCT_1:68;
            hence thesis by A7,FUNCT_1:def 5;
          end;
          suppose Fx in {y};
            then Fx=y & dom G =X\/{x} & x in {x} & {x}c=X\/{x}
              by TARSKI:def 1,FUNCT_2:def 1,XBOOLE_1:7;
            hence thesis by A4,FUNCT_1:def 5;
          end;
        end;
        hence thesis;
      end;
      then Y\/{y}=rng G by XBOOLE_0:def 10;
      hence thesis by FUNCT_2:def 3;
    end;
  thus not y in Y & F is one-to-one implies G is one-to-one
    proof
      assume A5:not y in Y & F is one-to-one;
      let x1,x2 be set such that A6:x1 in dom G & x2 in dom G & G.x1=G.x2;
      A8:for x' be set st x' in X holds
        x' in dom F & F.x'=G.x' & G.x'<>y
        proof
          let x' be set such that A9:x' in X;
          x' in dom F by A1,A9,FUNCT_2:def 1;
          then F.x'=G.x' & F.x' in rng F by A3,FUNCT_1:def 5,68;
          hence thesis by A5,A9,FUNCT_2:def 1;
        end;
      now per cases by A6,XBOOLE_0:def 2;
        suppose x1 in X & x2 in X;
          then x1 in dom F & x2 in dom F & F.x1=G.x1 & F.x2=G.x2 by A8;
          hence thesis by A6,A5,FUNCT_1:def 8;
        end;
        suppose x1 in X & x2 in {x};
          then G.x2=y & G.x1<>y by A4,A8,TARSKI:def 1;
          hence thesis by A6;
        end;
        suppose x1 in {x} & x2 in X;
          then G.x1=y & G.x2<>y by A4,A8,TARSKI:def 1;
          hence thesis by A6;
        end;
        suppose x1 in {x} & x2 in {x};
          then x=x1 & x=x2 by TARSKI:def 1;
          hence thesis;
        end;
      end;
      hence thesis;
    end;  
  end;
  
Lm4:x in X implies (X\{x})\/{x}=X by ZFMISC_1:140;

theorem Th71:
  for N be finite Subset of NAT holds
    ex Order be Function of N,card N st
      Order is bijective &
      for n,k st n in dom Order & k in dom Order & n<k holds Order.n < Order.k
proof
  defpred P[Nat] means
   for N be finite Subset of NAT st card N = $1 holds
    ex F be Function of N,card N st
      F is  bijective &
      for n,k st n in dom F & k in dom F & n<k holds F.n < F.k;
  A1:P[0]
  proof
    let N be finite Subset of NAT such that A2:card N=0;
    set P={};
    rng P={} & dom P={}& N is empty by A2,Th33;
    then reconsider P as Function of N,card N by A2,FUNCT_2:3;
    take P;
    N is empty by A2,Th33;
    then dom P={} & rng P={} by A2,XBOOLE_1:3;
    then P is one-to-one onto by A2,FUNCT_2:def 3;
    hence P is bijective by FUNCT_2:def 4;
    thus for i,j st i in dom P & j in dom P & i<j holds P.i<P.j by A2,Th33; 
  end;
  A2:for k st P[k] holds P[k+1]
  proof
    let k such that A3:P[k];
    let N be finite Subset of NAT such that A4:card N =k+1;
    defpred M[Nat] means $1 in N;
    consider m' be Nat such that
      A5:for n st n in N holds n<=m' by Th68;
    A6:for n st M[n] holds n<=m' by A5;
    N is non empty by A4,Th33;
    then consider x such that
      A7:x in N by XBOOLE_0:def 1;
    A8:ex n st M[n] by A7;
    consider m such that
      A9:M[m] & for n st M[n] holds n <= m from NAT_1:sch 6(A6,A8);
    set Nm=N\{m};
    card Nm=k by A4,A9,Th67;
    then consider F be Function of Nm,card Nm such that
      A10:F is bijective and
      A11:for n,k st n in dom F & k in dom F & n<k holds F.n < F.k by A3;
    m in {m} by TARSKI:def 1;
    then A12:not m in Nm & (card Nm is empty implies Nm is empty)
      by XBOOLE_0:def 4,Th33;
    then consider G be Function of Nm\/{m},card Nm\/{k} such that
    A13:G|Nm=F & G.m=k by Th69;
    card Nm=k by A4,A9,Th67;
    then F is one-to-one onto & not k in card Nm &
      (card Nm)\/{k}=k+1 by A10,FUNCT_2:def 4,Lm1;
    then A14:G is one-to-one onto & N=Nm\/{m} & (card Nm)\/{k}=card N
      by A4,A9,A12,A13,Lm4,Th70;
    then reconsider G'=G as Function of N,card N;
    take G';
    thus G' is bijective by A14,FUNCT_2:def 4;
    thus for n,k st n in dom G' & k in dom G' & n<k holds G'.n < G'.k
    proof
      let i,j such that A15:i in dom G' & j in dom G' & i<j;
      A16:for i st i in Nm holds i < m & G'.i <k
      proof
        let i such that A17:i in Nm;
        card Nm<>0 by A17,Th33;
        then i in dom F by A17,FUNCT_2:def 1;
        then M[i] & not i in {m} & F.i=G'.i & F.i in rng F & card Nm=k
          by A4,A9,Th67,A13,XBOOLE_0:def 4,FUNCT_1:68,def 5;
        then i <= m & i<>m & G'.i in k by A9,TARSKI:def 1;
        hence i < m & G'.i < k by EULER_1:1,REAL_1:def 5;
      end;
      A17:dom G'=Nm\/{m} by FUNCT_2:def 1;
      now per cases by A15,A17,XBOOLE_0:def 2;
        suppose A17:i in Nm & j in Nm;
          then card Nm is non empty by Th33;
          then A18:i in dom F & j in dom F 
            by A17,FUNCT_2:def 1;
          then F.i=G'.i & F.j=G'.j by FUNCT_1:68,A13;
          hence thesis by A11,A15,A18;
        end;
        suppose i in Nm & j in {m};
          then G'.i<k & G'.j=k by A13,A16,TARSKI:def 1;
          hence thesis;
        end;
        suppose i in {m} & j in Nm;
          then i=m & j<m by A16,TARSKI:def 1;
          hence thesis by A15;
        end;
        suppose i in {m} & j in {m};
          then i=m & j=m by TARSKI:def 1;
          hence thesis by A15;
        end;
      end;
      hence thesis;
    end;
  end;
  for k holds P[k] from NAT_1:sch 1(A1,A2);
  hence thesis;
end;

Lm5:for X be finite set,x st x in X holds card (X\{x}) < card X
proof
  let X be finite set,x such that A1:x in X;
  card X <>0 by A1,Th33;
  then card X>0 by NAT_1:18;
  then reconsider c1=card X-1 as Nat by NAT_1:60;
  card X=c1+1;
  then card (X\{x})=c1 & c1<c1+1 by A1,Th67,NAT_1:38;
  hence thesis;
end;

theorem Th72:
  for X,Y be finite set,F be Function of X,Y st card X=card Y holds
  F is onto iff F is one-to-one
proof
  let X,Y be finite set,F be Function of X,Y such that A1:card X=card Y;
  thus F is onto implies F is one-to-one
  proof
    assume A2:F is onto;
    assume F is not one-to-one;
    then consider x1,x2 be set such that
      A3:x1 in dom F & x2 in dom F & F.x1=F.x2 & x1<>x2 by FUNCT_1:def 8;
    reconsider Xx=X\{x1} as finite set;   
    Y c= F.:Xx
    proof
      let Fy be set such that A4:Fy in Y;
      Fy in rng F by A2,A4,FUNCT_2:def 3;
      then consider y such that
        A5:y in dom F & F.y=Fy by FUNCT_1:def 5;
      now per cases;
        suppose y=x1;
          then Fy=F.x2 & x2 in Xx by A3,A5,ZFMISC_1:64;
          hence thesis by A3,FUNCT_1:def 12;
        end;
        suppose y<>x1;
          then y in Xx by A5,ZFMISC_1:64;
          hence thesis by A5,FUNCT_1:def 12;
        end;
      end;
      hence thesis;
    end;
    then Card Y <=` Card Xx & x1 in X by A3,CARD_2:2;
    then card Y <= card Xx & card Xx < card X by CARD_2:57,Lm5;
    hence thesis by A1;
  end;
  thus F is one-to-one implies F is onto
  proof
    assume A2:F is one-to-one;
    assume F is not onto;
    then not rng F = Y by FUNCT_2:def 3;
    then not Y c= rng F by XBOOLE_0:def 10;
    then consider y such that
      A3:y in Y & not y in rng F by TARSKI:def 3;
    rng F c= Y\{y} & dom F,F.:dom F are_equipotent
      by A2,CARD_1:60,A3,ZFMISC_1:40;
    then card rng F <=card (Y\{y}) & card (Y\{y})< card Y &
      card dom F=card (F.:dom F) & F.:dom F= rng F & dom F=X 
      by CARD_1:81,RELAT_1:146,A3,Lm5,CARD_1:80,FUNCT_2:def 1;
    hence thesis by A1;
  end;
end;

Lm6:
  for n be Nat,N be finite Subset of NAT,
      F be Function of N,card N st
    n in N & F is bijective &
    for n,k st n in dom F & k in dom F & n<k holds F.n < F.k
  ex P be Permutation of N st
    for k st k in N holds
      (k < n implies P.k=F".((F.k)+1)) &
      (k = n implies P.k=F".0) &
      (k > n implies P.k=k)
proof
  let n be Nat,N be finite Subset of NAT,
      F be Function of N,card N such that
    A1:n in N and
    A3:F is  bijective and
    A4:for n,k st n in dom F & k in dom F & n<k holds F.n < F.k;
    defpred P[set,set] means
      for k st k=$1 holds
        (k < n implies $2=F".((F.k)+1)) &
        (k = n implies $2=F".0) &
        (k > n implies $2=k);
    F is onto by A3,FUNCT_2:def 4;
    then F is one-to-one & rng F=card N by A3,FUNCT_2:def 3,def 4;
    then reconsider F'=F" as Function of card N,N by FUNCT_2:31;
    card N is non empty by A1,Th33;    
    then A5:dom F=N & dom F'=card N by A1,FUNCT_2:def 1;   
    A6:for x st x in N ex y st y in N & P[x,y]
      proof
        let x' be set such that A7:x' in N;
        reconsider x=x' as Nat by A7;
        now per cases by REAL_1:def 5;
          suppose A8:x < n;
            set FF=F".((F.x)+1);
            F.x < F.n & F.n in rng F & F is onto
              by A1,A3,A4,A5,A7,A8,FUNCT_1:def 5,FUNCT_2:def 4;
            then (F.x)+1 <=F.n & F.n < card N by EULER_1:1, NAT_1:38;
            then (F.x)+1 < card N by AXIOMS:22;
            then (F.x)+1 in dom F' by A5,EULER_1:1;
            then F'.((F.x)+1) in rng F' & P[x',FF] by A8,FUNCT_1:def 5;
            hence thesis;
          end;
          suppose A8:x = n;
            card N<>0 by A1,Th33;
            then 0< card N by NAT_1:18;
            then 0 in dom F' by A5,EULER_1:1;
            then F'.0 in rng F' by FUNCT_1:def 5;
            then F'.0 in N & P[x',F".0] by A8;
            hence thesis;
          end;
          suppose x>n;
            then x in N & P[x,x] by A7;
            hence thesis;
          end;
        end;
        hence thesis;  
      end;
    consider P be Function of N,N such that
      A7:for x st x in N holds P[x,P.x] from FUNCT_2:sch 1(A6);
    N c= rng P
    proof
      let Px' be set such that A8:Px' in N;
      reconsider Px=Px' as Nat by A8;
      now per cases;
        suppose A9:Px<=n;
          F is one-to-one by A3,FUNCT_2:def 4;
          then rng (F')=N by A5,FUNCT_1:55;
          then consider x such that
            A10:x in dom F' & F'.x=Px by A8,FUNCT_1:def 5;
          card N is Subset of NAT & F is one-to-one by A3,FUNCT_2:def 4,Th8;
          then reconsider x as Nat by A10, A5;
          now per cases by NAT_1:18;
            suppose A11:x=0;
              dom P=N by A1,FUNCT_2:def 1;
              then P.n=F".0 & F'.0=Px & P.n in rng P
                by A1,A7,A10,A11,FUNCT_1:def 5;
              hence thesis;
            end;
            suppose x>0;
              then reconsider x1=x-1 as Nat by NAT_1:60;
               x1<x1+1 & x < card N
              by NAT_1:38,EULER_1:1,A10;
              then x1 < card N & F is onto by AXIOMS:22,A3,FUNCT_2:def 4;
              then x1 in card N & card N=rng F by EULER_1:1,FUNCT_2:def 3;
              then consider y such that
                A11:y in dom F & F.y=x1 by FUNCT_1:def 5;
              reconsider y as Nat by A5,A11;   
              A12:y<n
              proof
                assume y>=n;
                then (y>n or y=n) & F is one-to-one
                  by A3,REAL_1:def 5,FUNCT_2:def 4;
                then x1>=F.n & x1+1>x1 & x in rng F & F is one-to-one &
                  Px in dom F & n in dom F & (Px <n or Px=n)
                  by A8,A9,A10,FUNCT_1:55,A1,A5,A11,A4,NAT_1:38,REAL_1:def 5;
                then x>F.n & F.Px=x & F.Px<=F.n
                  by A4,A10,FUNCT_1:54,AXIOMS:22;
                hence contradiction;
              end;
              F.y=x1 by A11;
              then F".(F.y+1)=Px & y in dom P
                by A5,A11,FUNCT_2:def 1,A10;
              then P.y=Px & P.y in rng P by A7,A12,FUNCT_1:def 5;
              hence thesis;
            end;
          end;
          hence thesis;
        end;
        suppose Px>n;
          then Px in dom P & Px=P.Px by A8,A7,FUNCT_2:def 1;
          hence thesis by FUNCT_1:def 5;
        end;
      end;
      hence thesis;        
    end;
    then N=rng P by XBOOLE_0:def 10;
    then P is onto & card N=card N by FUNCT_2:def 3;
    then P is onto one-to-one by Th72;
    then reconsider P as Permutation of N by FUNCT_2:def 4;
    take P;
    thus thesis by A7;
end;

theorem Th73:
  for F,G be Function,y st y in rng (G*F) & G is one-to-one holds
    ex x st x in dom G & x in rng F & G"{y}={x} & F"{x}=(G*F)"{y}
proof
  let F,G be Function,y such that
  A1:y in rng (G*F) and
  A2:G is one-to-one;
  consider x such that
    A3:x in dom (G*F) & (G*F).x=y by A1,FUNCT_1:def 5;
  A4:F.x in dom G & G.(F.x)=y by A3,FUNCT_1:22,21;
  y in rng G by A1,FUNCT_1:25;
  then consider Fx be set such that
    A5:G"{y}={Fx} by A2,FUNCT_1:144;
  G.(F.x) in {y} & F.x in dom G by A4,TARSKI:def 1;
  then A6:F.x in G"{y} by FUNCT_1:def 13;
  A7:F"{F.x} c= (G*F)"{y}
  proof
    let d be set such that A8:d in F"{F.x};
    F.d in {F.x} by A8,FUNCT_1:def 13;
    then d in dom F & F.d = F.x by A8,TARSKI:def 1,FUNCT_1:def 13;
    then d in dom F & d in dom (G*F) & G.(F.d) in {y}
      by A4,TARSKI:def 1,FUNCT_1:21;
    then d in dom (G*F) & (G*F).d in {y}  by FUNCT_1:23;
    hence thesis by FUNCT_1:def 13;  
  end;
  (G*F)"{y} c= F"{F.x}
  proof
    let d be set such that A8:d in (G*F)"{y};
    d in dom (G*F) & (G*F).d in {y} by A8,FUNCT_1:def 13;
    then A9:d in dom F & F.d in dom G & G.(F.d) in {y} & F.x=Fx
      by A5,A6,FUNCT_1:21,22,TARSKI:def 1;
    then F.d in {F.x} by A5,FUNCT_1:def 13;
    hence thesis by A9,FUNCT_1:def 13;    
  end;
  then x in dom F & F"{F.x} = (G*F)"{y}
    by A7,XBOOLE_0:def 10,A3,FUNCT_1:21;
  then F.x in dom G & F.x in rng F & G"{y}={F.x} & F"{F.x} = (G*F)"{y}
    by A3,A6,A5,TARSKI:def 1,FUNCT_1:21,def 5;
  hence thesis;
end;

::::::::::::::::::::::::::::::::::::
::   Generalization "increasing   ::
::::::::::::::::::::::::::::::::::::

definition
  let Ne,Ke;let f be Function of Ne,Ke;
  attr f is "increasing means :Def5:
  for l,m st l in rng f & m in rng f & l < m holds min* f"{l} < min* f"{m};
end;

theorem Th74:
  for F be Function of Ne,Ke st F is "increasing
    holds min* rng F=F.min* dom F
proof
  let F be Function of Ne,Ke such that
  A1:F is "increasing;
  now per cases;
    suppose A2:rng F is empty;
      then dom F={} by RELAT_1:65;
      then min* dom F=0 & min* rng F=0 & F.0=0
        by A2,HENMODEL:def 1,FUNCT_1:def 4;
      hence thesis;
    end;
    suppose A2:rng F is non empty;
      then reconsider rngF=rng F,Ke as non empty Subset of NAT by XBOOLE_1:1,3;
      Ke is non empty;
      then reconsider domF=dom F as non empty Subset of NAT
        by A2,FUNCT_2:def 1,RELAT_1:65;
      set md=min* domF;set mr=min* rngF;
      mr=F.md
      proof
        assume A3:mr<>F.md;
        A4:md in dom F by HENMODEL:def 1;
        then F.md in rngF by FUNCT_1:def 5;
        then mr <=F.md by HENMODEL:def 1;
        then mr < F.md & mr in rngF & F.md in rng F
          by A3,A4,REAL_1:def 5,HENMODEL:def 1,FUNCT_1:def 5;
        then A5:min* F"{mr}< min* F"{F.md} by A1,Def5;
        mr in rngF by HENMODEL:def 1;
        then consider x such that
          A6:x in dom F & F.x=mr by FUNCT_1:def 5;
        F.x in {mr} & F.md in {F.md} & md in dom F
          by A6,TARSKI:def 1,HENMODEL:def 1;
        then reconsider Fmr=F"{mr},Fmd=F"{F.md} as non empty Subset of NAT
          by XBOOLE_1:1,A6,FUNCT_1:def 13;
        min* Fmr in Fmr & F.md in {F.md} & md in domF
          by HENMODEL:def 1,TARSKI:def 1;
        then min* Fmr in domF & md in Fmd by FUNCT_1:def 13;
        then min* Fmr >= md & md >= min* Fmd by HENMODEL:def 1;
        hence contradiction by A5,AXIOMS:22;
      end;
      hence thesis;
    end;
  end;
  hence thesis;
end;

:: existence of an expansion 

theorem
  for F be Function of Ne,Ke st rng F is finite
    ex I be Function of Ne,Ke,P be Permutation of rng F st
      F=P*I & rng F=rng I & I is "increasing
proof
  defpred P[set] means
    for Ne,Ke be Subset of NAT,F be Function of Ne,Ke st F=$1 & rng F is finite
    ex P be Permutation of rng F,G be Function of Ne,Ke st
      F=P*G & rng F = rng G &
      for i,j st i in rng G & j in rng G & i<j holds min* G"{i} < min* G"{j};
  A1:P[{}]
  proof
    let Ne,Me be Subset of NAT,F be Function of Ne,Me such that
    A2:F={}& rng F is finite;
    reconsider R=rng F as empty set by A2,RELAT_1:60;
    set P={};
    rng P={} & dom P={};
    then reconsider P as Function of R,R by FUNCT_2:3;
    dom P={} & rng R= {} by FUNCT_2:def 1;
    then P is one-to-one onto by FUNCT_2:def 3;
    then reconsider P as Permutation of rng F by FUNCT_2:def 4;    
    take P,F;
    A3:for i,j st i in rng F & j in rng F & i<j holds min* F"{i} < min* F"{j}
      proof
        let i,j such that A4:i in rng F & j in rng F & i<j;
        i in R & j in R by A4;
        hence thesis;
      end;
    rng F=R;
    then F={} by RELAT_1:64;
    hence thesis by A3,RELAT_1:62;    
  end;
  defpred Q[set,Function] means $1=$2.min* dom $2;
  A2:for F be Function st
     for x st x in rng F & Q[x,F] holds P[F|(dom F\F"{x})] holds P[F]
     proof
       let F' be Function such that
       A3:for x st x in rng F' & Q[x,F'] holds P[F'|(dom F'\F'"{x})];
       let N,K be Subset of NAT,F be Function of N,K such that
             A4:F=F' & rng F is finite;
       now per cases;
         suppose rng F' is empty;
           then F'={} by RELAT_1:64;
           hence thesis by A1,A4;   
         end;
         suppose A5:rng F' is non empty;
           reconsider K as non empty Subset of NAT by A4,A5,XBOOLE_1:3;
           reconsider domF=dom F as non empty Subset of NAT by
             A4,A5,XBOOLE_1:1,RELAT_1:65;
           set m=min* domF;
           set D=dom F\F"{F.m};
           min* domF in domF by HENMODEL:def 1;
           then A6:F.m in rng F by FUNCT_1:def 5;
           now per cases;
             suppose rng (F|D) is empty;
               then rng F\{F.m}={} by Th66;
               then A7:rng F={F.m} by A6,ZFMISC_1:66;
               set P=id rng F;
               rng P=rng F by RELAT_1:71;
               then P is one-to-one onto by FUNCT_1:52,FUNCT_2:def 3;
               then reconsider P as Permutation of rng F by FUNCT_2:def 4;
               F is Function of dom F,rng F & rng F <>0 by FUNCT_2:3,A6;  
               then A8:P*F = F by FUNCT_2:23;                                  
               for i,j st i in rng F & j in rng F & i<j holds
                 min* F"{i} < min* F"{j}
               proof
                 let i,j such that A9:i in rng F & j in rng F & i<j;
                 i =F.m & j=F.m by A7,A9,TARSKI:def 1;
                 hence thesis by A9; 
               end;  
               hence thesis by A8;
             end;
             suppose A7:rng (F|D) is non empty;              
               rng (F|D) c= rng F by RELAT_1:99;
               then reconsider rFD=rng (F|D) as non empty finite Subset of NAT
                 by A7,XBOOLE_1:1,A4,FINSET_1:13;
               reconsider rF=rng F as non empty finite Subset of NAT
                 by A6,XBOOLE_1:1,A4;
               reconsider dFD=dom (F|D) as Subset of NAT by XBOOLE_1:1;
               reconsider FD=F|D as Function of dFD,rFD by FUNCT_2:3;
               A8:dFD=dom F\F"{F.m}
               proof
                 dFD= dom FD & dom FD = dom F /\(dom F\F"{F.m}) &
                   (dom F\F"{F.m}) c= dom F
                   by FUNCT_1:68,XBOOLE_1:36;
                 hence thesis by XBOOLE_1:28;
               end;       
               consider P be Permutation of rng FD,
                 G be Function of dFD,rFD such that
                 A9:FD=P*G & rng FD = rng G and
                 A10:for i,j st i in rng G & j in rng G & i<j holds
                    min* G"{i} < min* G"{j} by A3,A4,A6;
               consider Orde be Function of rF,card rF such that
                 A11:Orde is  bijective and
                 A12:for n,k st n in dom Orde & k in dom Orde & n<k holds
                         Orde.n < Orde.k by Th71;
               Orde is one-to-one onto by A11,FUNCT_2:def 4;
               then Orde is one-to-one & rng Orde=card rF by FUNCT_2:def 3;
               then reconsider Orde'=Orde" as Function of card rF,rF
               by FUNCT_2:31;
               consider P1 be Permutation of rF such that
                 A13:for k st k in rF holds
                 (k < F.m implies P1.k=Orde".((Orde.k)+1)) &
                 (k = F.m implies P1.k=Orde".0) &
                 (k > F.m implies P1.k=k) by A6,A11,A12,Lm6;
               deffunc G(set)=F.m;
               A14:for x st x in N\dFD holds G(x) in K by A6;
               A15:dFD c= N & rFD c= K;
               A16:rFD is empty implies dFD is empty;    
               consider G2 be Function of N,K such that
                 A17:G2|dFD=G & for x st x in N\dFD holds G2.x = G(x)
                 from Sch2(A14,A15,A16);
               not F.m in rng F\{F.m} by ZFMISC_1:64;
               then not F.m in rng FD by Th66;
               then consider P2 be Function of rng FD\/{F.m},rng FD\/{F.m}
                  such that
                  A19:P2|(rng FD) = P & P2.(F.m)=(F.m) by Th69;
               P is one-to-one onto & rng FD =rng F\{F.m} &
               not F.m in rng F\{F.m} by ZFMISC_1:64,Th66;
               then P2 is one-to-one onto & rng FD\/{F.m} = rng F
                 by A6,Lm4,A19,Th70;
               then reconsider P2 as Permutation of rng F by FUNCT_2:def 4; 
               A20:rng G2 c= rng F
               proof
                 let Gx be set such that A21:Gx in rng G2;
                 consider x such that
                   A22:x in dom G2 & G2.x=Gx by A21,FUNCT_1:def 5;
                 dom G2=N by FUNCT_2:def 1;
                 then dom G2/\dFD \/(N\dFD)=dom G2 by XBOOLE_1:51;
                 then dom G=dom G2/\dFD & x in dom G2/\dFD or x in N\dFD
                   by A22,XBOOLE_0:def 2,A17,FUNCT_1:68;
                 then G.x in rng FD & G.x=G2.x & rng FD=rng F\{F.m}
                   or G2.x=F.m & m in domF
                   by A9,A17,FUNCT_1:68,def 5,Th66,HENMODEL:def 1;
                 hence thesis by A22,XBOOLE_0:def 4,FUNCT_1:def 5;
               end;
               rng F c= rng G2
               proof
                 let Fx be set such that A20:Fx in rng F;
                 rng FD=rng F\{F.m} by Th66;
                 then A21:rng F=rng FD\/{F.m} by A6,Lm4;
                 now per cases by A20,A21,XBOOLE_0:def 2;
                   suppose Fx in rng FD;
                     then Fx in rng G & rng (G2|dFD) c=rng G2 by A9,RELAT_1:99;
                     hence thesis by A17;
                   end;
                   suppose A22:Fx in {F.m};
                     m in dom F & F.m in {F.m} & K is non empty
                       by HENMODEL:def 1,TARSKI:def 1;
                     then m in F"{F.m}&dom F=N by FUNCT_1:def 13,FUNCT_2:def 1;
                     then not m in dom F\F"{F.m} & m in N & N=dom G2 
                       by XBOOLE_0:def 4,FUNCT_2:def 1;
                     then m in N\dFD & Fx=F.m & m in dom G2
                       by A22,A8,XBOOLE_0:def 4,TARSKI:def 1;
                     then G2.m=Fx & G2.m in rng G2 by A17,FUNCT_1:def 5;
                     hence thesis;
                   end;
                 end;
                 hence thesis;
               end;   
               then A21:rng G2=rng F by A20,XBOOLE_0:def 10;
               A22:F=P2*G2
                 proof
                   K is non empty;
                   then A23:dom F=N & dom G2=N by FUNCT_2:def 1;
                   A24:for x holds x in dom F iff x in dom G2&G2.x in dom P2
                     proof
                       let x;
                       thus x in dom F implies x in dom G2 & G2.x in dom P2
                       proof
                         assume x in dom F;
                         then x in dom G2 & dom P2=rng F
                           by A6,A23,FUNCT_2:def 1;
                         hence thesis by A21,FUNCT_1:def 5;
                       end;
                       thus x in dom G2&G2.x in dom P2 implies x in dom F
                         by A23;
                     end;
                   for x st x in dom F holds F.x = P2.(G2.x)
                     proof
                       let x such that A25:x in dom F;
                       now per cases by A25,XBOOLE_0:def 4;
                         suppose x in N\dFD;
                           then A26:P2.(G2.x)=F.m & x in dom F &
                             not x in dom F\F"{F.m}
                             by A17,A23,A8,A19,XBOOLE_0:def 4;
                           then x in F"{F.m} by XBOOLE_0:def 4;
                           then F.x in {F.m} by FUNCT_1:def 13;
                           hence thesis by A26,TARSKI:def 1;
                         end;
                         suppose x in dFD;
                           then x in dom FD & x in dom G
                             by FUNCT_2:def 1;
                           then F.x=FD.x & G.x=G2.x & FD.x=P.(G.x) &
                             G.x in rng FD & dom P=rng FD
                             by A9,A17,FUNCT_1:22,68,def 5,FUNCT_2:def 1;
                           hence thesis by A19,FUNCT_1:68;
                         end;
                       end;
                       hence thesis;  
                     end;
                   hence F=P2*G2 by A24,FUNCT_1:20;
                 end;
               A23:G2"{F.m}=F"{F.m}
               proof
                 thus G2"{F.m}c=F"{F.m}
                 proof
                   let x such that A24:x in G2"{F.m}; 
                   x in N\dFD
                   proof
                     assume not x in N\dFD;
                     then x in dom G2 & dom G2=N & not x in N or x in dFD
                        by A24,XBOOLE_0:def 4;
                     then x in dom G by FUNCT_2:def 1;
                     then G.x in rng G & G.x=G2.x by A17,FUNCT_1:68,def 5;
                     then G2.x in rng FD & rng FD=rng F\{F.m} by Th66;
                     then not G2.x in {F.m} by XBOOLE_0:def 4;
                     hence thesis by A24,FUNCT_1:def 13;   
                   end;
                   then x in N\(dom F\F"{F.m}) &dom F=N by A8,A6,FUNCT_2:def 1;
                   then x in dom F & not x in dom F\F"{F.m} by XBOOLE_0:def 4;
                   hence x in F"{F.m} by XBOOLE_0:def 4;    
                 end;
                 thus F"{F.m}c=G2"{F.m}
                 proof
                   let x such that A24:x in F"{F.m};
                   not x in dom F\F"{F.m} & dom F=N
                     by A24,XBOOLE_0:def 4,A6,FUNCT_2:def 1;
                   then x in N\dFD & x in N by A24,XBOOLE_0:def 4,A8;
                   then G2.x=F.m & x in dom G2 by A17,FUNCT_2:def 1;
                   then G2.x in {F.m} & x in dom G2 by TARSKI:def 1;
                   hence thesis by FUNCT_1:def 13;
                 end;
               end;  
               A24:for n st n in rng FD holds G"{n}=G2"{n}
                 proof
                   let n such that A25:n in rng FD;
                   K is non empty;
                   then dFD=dom F\F"{F.m} & dom F=N & dom G2=N &
                     rng FD=rng F\{F.m} by FUNCT_2:def 1,A8,Th66;
                   then dFD= dom G2\G2"{F.m} & not n in {F.m}
                     by A23,A25,XBOOLE_0:def 4;
                   then G2|(dom G2\G2"{F.m})=G & n<>F.m by A17,TARSKI:def 1;
                   hence G"{n}=G2"{n} by Th66;
                 end;
                reconsider P21=P2*P1" as Function of rF,rF by FUNCT_2:19;   
                rng (P1")=dom P1 & dom P1=rF & rng P2=rF
                  by FUNCT_2:def 1,def 3,FUNCT_1:55;
                then rng P21=rF by FUNCT_2:20;
                then P21 is onto one-to-one by FUNCT_1:46,FUNCT_2:def 3;
                then reconsider P21 as Permutation of rF by FUNCT_2:def 4;
                dom G2=N by FUNCT_2:def 1;
                then G2 is Function of N,rF & rng P1=rF & rng G2=rF
                  by A21,FUNCT_2:3,def 3;
                then A25:(P1*G2) is Function of N,rF & rng (P1*G2) c= K &
                  rng (P1*G2)=rF 
                  by FUNCT_2:19,20,XBOOLE_1:1;
                then reconsider PG=P1*G2 as Function of N,K by FUNCT_2:8;     
                dom G2=N by FUNCT_2:def 1;
                then G2 is Function of N,rF & dom P1=rF by A21,FUNCT_2:def 1,3;
                then (id rF)*G2 = G2 & P1"*P1=id rF
                  by FUNCT_2:23,FUNCT_1:61;  
                then P1"*(P1*G2)=G2 by RELAT_1:55;
                then A26:F=P21*PG by A22,RELAT_1:55;
                for i,j st i in rng PG & j in rng PG & i<j holds
                  min* PG"{i} < min* PG"{j}
                proof
                  let i,j such that A27:i in rng PG & j in rng PG & i<j;
                  consider i1 be set such that
                    A28:i1 in dom P1 & i1 in rng G2 &P1"{i}={i1} &
                      G2"{i1}=PG"{i} by A27,Th73;
                  consider j1 be set such that
                    A29:j1 in dom P1 & j1 in rng G2 & P1"{j}={j1} &
                        G2"{j1}=PG"{j} by A27,Th73;              
                  dom P1=rF by FUNCT_2:def 1;
                  then reconsider i1,j1 as Nat by A28,A29;
                  rng FD =rng F\{F.m} & not F.m in rng F\{F.m}
                    by ZFMISC_1:64,Th66;
                  then A30:rng FD\/{F.m} = rng G2 by A6,A21,Lm4;
                  A31:for n,k st n in dom Orde & k in dom Orde & Orde.n<Orde.k
                    holds n < k
                  proof
                    let n,k such that A32:
                      n in dom Orde & k in dom Orde & Orde.n<Orde.k;
                    assume n>=k;
                    then n>k by A32,REAL_1:def 5;
                    hence contradiction by A12,A32;
                  end;
                  A32:for n,k st n in rng Orde & k in rng Orde holds
                  n < k implies Orde'.n<Orde'.k 
                  proof
                    let n,k such that A33:n in rng Orde & k in rng Orde;
                    assume A34:n < k;
                    A35:Orde is one-to-one onto by A11,FUNCT_2:def 4;
                    then rng Orde = dom Orde' & dom Orde = rng Orde' &
                      rng Orde=card rF
                      by FUNCT_1:55,FUNCT_2:def 3; 
                    then n = Orde.(Orde'.n) & k = Orde.(Orde'.k) &
                    Orde'.k in dom Orde & Orde'.n in dom Orde &
                     dom Orde is Subset of NAT by XBOOLE_1:1,
                      A33,A35,FUNCT_1:57,def 5;
                    hence Orde'.n < Orde'.k by A31,A34;
                  end; 
                 i1 in P1"{i} & j1 in P1"{j} by A28,A29,TARSKI:def 1;
                 then A33':i1 in dom P1 & P1.i1 in {i} & j1 in dom P1 &
                 P1.j1 in {j} & dom P1=rF & (card rF={} implies rF={})
                   by FUNCT_1:def 13,FUNCT_2:def 1,Th33;
                 then A33:P1.i1=i & P1.j1=j & i1 in rF & j1 in rF & dom Orde=rF
                 by TARSKI:def 1,FUNCT_2:def 1;
                 A34:for l st l in rF & l < F.m holds
                   Orde.l+1 in rng Orde & Orde.l+1 is Nat &
                   Orde.l+1<=Orde.(F.m) & Orde.(F.m) is Nat & dom Orde=rF
                   proof
                     let l such that A35:l in rF & l < F.m;
                     Orde is onto & (card rF={} implies rF={})
                       by A11,Th33,FUNCT_2:def 4;
                     then A36:dom Orde=rF & card rF=rng Orde
                       by FUNCT_2:def 1,def 3;
                     then A37':Orde.l < Orde.(F.m) & Orde.(F.m) in card rF &
                       card rF is Subset of NAT
                       by A6,A12,A35,FUNCT_1:def 5,Th8;
                     then Orde.l+1<=Orde.(F.m) & Orde.(F.m) < card rF &
                       Orde.(F.m) is Nat
                       by NAT_1:38,EULER_1:1;
                     then Orde.l+1 < card rF by AXIOMS:22;
                     then Orde.l+1 in rng Orde & Orde.l+1 in card rF &
                       card rF is Subset of NAT by A36,EULER_1:1,Th8;
                     hence thesis by A37',FUNCT_2:def 1,NAT_1:38;
                   end;
                  now per cases by REAL_1:def 5;
                    suppose A35:i1 < F.m & j1<F.m;
                      i1<j1
                      proof
                        assume i1>=j1;
                        then i1>j1 by A33,A27,REAL_1:def 5;
                        then Orde.i1 >Orde.j1 by A33,A12;
                        then Orde.i1+1 >Orde.j1+1 & Orde.i1+1 in rng Orde &
                          Orde.j1+1 in rng Orde & Orde.i1+1 is Nat &
                          Orde.j1+1 is Nat
                          by A34,A35,REAL_1:67,A28,A29;
                        then Orde'.(Orde.i1+1)>Orde'.(Orde.j1+1) &
                          Orde'.(Orde.i1+1)=i & Orde'.(Orde.j1+1)= j
                          by A32,A33,A35,A13;
                        hence contradiction by A27;
                      end;
                      then i1<j1 &(i1 in rng FD or i1 in {F.m})&
                        not i1 in {F.m} &(j1 in rng FD or j1 in {F.m})
                        & not j1 in {F.m}
                        by A28,A29,A30,A35,XBOOLE_0:def 2,TARSKI:def 1;
                      then min* G"{i1}<min* G"{j1}&G"{i1}=PG"{i}&G"{j1}=PG"{j}
                        by A9,A10,A24,A28,A29;
                      hence thesis;
                    end;
                    suppose A35:i1 = F.m & j1<>F.m;
                      then (j1 in rng FD or j1 in {F.m})&
                        not j1 in {F.m} & F.m in {F.m} & m in domF
                        by HENMODEL:def 1,A29,A30,XBOOLE_0:def 2,TARSKI:def 1;
                      then A36:m in F"{F.m} & F"{F.m}=PG"{i} & G"{j1}=PG"{j}& 
                      PG"{i} is Subset of NAT&G"{j1} c= dom G&dFD=dom F\F"{F.m}
                      by A35,A8,A28,A29,A23,A24,RELAT_1:167,
                        XBOOLE_1:1,FUNCT_1:def 13;
                      consider x such that
                        A37:x in dom G2 & G2.x=j1 by A29,FUNCT_1:def 5;
                      G2.x in {j1} by A37,TARSKI:def 1;  
                      then PG"{j} is non empty Subset of NAT
                        by XBOOLE_1:1,A37,FUNCT_1:def 13,A29;
                      then min* PG"{j} in PG"{j} by HENMODEL:def 1;
                      then min* PG"{j} in dom F & not min* PG"{j} in F"{F.m} &
                        m in F"{F.m} by A36,XBOOLE_0:def 4;
                      then m<=min* PG"{j} & m<>min* PG"{j} by HENMODEL:def 1;
                      then m<min* PG"{j} & min* PG"{i} <= m
                        by A36,HENMODEL:def 1,REAL_1:def 5;
                      hence thesis by AXIOMS:22;
                    end;
                    suppose A35:i1 < F.m & j1=F.m;
                      (i1 in rng FD or i1 in {F.m}) & not i1 in {F.m} &
                      card rF <>0 & Orde is onto & rng FD c= rF
                        by A28,A30,A35,XBOOLE_0:def 2,TARSKI:def 1,A11,
                        FUNCT_2:def 4,Th33,RELAT_1:99;
                      then i1 in rF & card rF >0  & card rF=rng Orde
                        by NAT_1:18,FUNCT_2:def 3;
                      then Orde.i1+1 > 0 & Orde.i1+1 in rng Orde &
                        Orde.i1+1 is Nat & 0 in rng Orde by A34,A35,EULER_1:1;
                      then Orde'.(Orde.i1+1)>Orde'.0 & P1.j1=Orde'.0 &
                        Orde'.(Orde.i1+1)=i by A13,A32,A33,A35; 
                      hence thesis by A27,A33',TARSKI:def 1;
                    end;
                    suppose i1 = F.m & j1=F.m;
                      hence thesis by A27,A33;
                    end;
                    suppose A35:i1 > F.m & j1>F.m;
                      P1.i1=i1 & P1.j1=j1 & (i1 in rng FD or i1 in {F.m}) &
                        not i1 in {F.m} & (j1 in rng FD or j1 in {F.m}) &
                        not j1 in {F.m}
                        by A30,A13,A35,A28,A29,TARSKI:def 1,XBOOLE_0:def 2;
                      then min* G"{i1}< min* G"{j1}&G"{i1}=PG"{i}&G"{j1}=PG"{j}
                        by A10,A24,A28,A29,A9,A33,A27;
                      hence thesis;
                    end;
                    suppose A35:i1 > F.m & j1=F.m;
                      A36:P1.i1=i1 & P1.j1=Orde".0 by A13,A35,A28,A29;
                      Orde is one-to-one onto & card rF is non empty
                        by A11,FUNCT_2:def 4,Th33;
                      then rng (Orde")=dom Orde & dom (Orde")=rng Orde &
                        rng Orde=card rF & dom Orde=rF & i1 in rF
                        by A28,FUNCT_1:55,FUNCT_2:def 1,def 3;
                      then consider x such that
                        A38:x in dom Orde' & Orde'.x=i1 by FUNCT_1:def 5;
                      x in card rF & card rF is Subset of NAT by A38,Th8;
                      then reconsider x as Nat;
                      card rF <>0 & Orde is onto by A11,FUNCT_2:def 4,Th33;
                     then card rF>0&card rF=rng Orde by NAT_1:18,FUNCT_2:def 3;
                      then 0 in rng Orde & x in rng Orde & Orde'.x < Orde'.0
                        by A27,A33,A36,A38,EULER_1:1;
                      then x <=0 & Orde'.x <> Orde'.0 by A32;
                      then x<0 by REAL_1:def 5;
                      hence thesis by NAT_1:18;                
                    end;
                    suppose i1 < F.m & j1>F.m;
                      then i1<j1 &(i1 in rng FD or i1 in {F.m})&
                        not i1 in {F.m} &(j1 in rng FD or j1 in {F.m})
                        & not j1 in {F.m}
                      by A28,A29,A30,XBOOLE_0:def 2,TARSKI:def 1,AXIOMS:22;
                      then min* G"{i1}<min* G"{j1}&G"{i1}=PG"{i}&G"{j1}=PG"{j}
                        by A9,A10,A24,A28,A29;
                      hence thesis;
                    end;
                    suppose A35:i1 > F.m & j1<F.m;  
                      Orde.j1+1 in rng Orde &  Orde.j1+1 is Nat &
                      Orde.j1+1 <= Orde.(F.m)&Orde.(F.m) is Nat & dom Orde=rF
                         by A29,A34,A35;
                      then Orde.j1+1 in rng Orde &  Orde.j1+1 is Nat &
                        (Orde.j1+1 < Orde.(F.m) or Orde.j1+1 = Orde.(F.m))&
                        Orde.(F.m) is Nat & Orde.(F.m) in rng Orde &
                        F.m in dom Orde & Orde is one-to-one & j1 in rF
                        by A6,A29,REAL_1:def 5,A11,FUNCT_2:def 4,FUNCT_1:def 5;
                      then (Orde'.(Orde.j1+1)<Orde'.(Orde.(F.m)) or
                        Orde'.(Orde.j1+1)=Orde'.(Orde.(F.m))) &
                        Orde'.(Orde.(F.m))=F.m & Orde'.(Orde.j1+1)=P1.j1
                        by A13,A32,A35,FUNCT_1:56;
                      then P1.j1<=F.m & P1.i1=i1 by A13,A28,A35;
                      hence thesis by A27,A33,A35,AXIOMS:22;
                    end;
                  end;
                  hence thesis;
                 end;    
                 hence thesis by A25,A26;
                 end;
              end;
           hence thesis;            
         end;
       end;
       hence thesis;
     end;  
  A3:for F be Function st rng F is finite holds P[F] from Sch9(A1,A2);
  let F be Function of Ne,Ke such that A4:rng F is finite; 
  consider P be Permutation of rng F,G be Function of Ne,Ke such that
    A5:F=P*G & rng F=rng G &
    for i,j st i in rng G& j in rng G & i<j holds
                                         min* G"{i}<min* G"{j} by A3,A4;
  G is "increasing by A5,Def5;
  hence thesis by A5;
end;

:: uniqueness of expansion 

theorem Th76:
  for F be Function of Ne,Ke st rng F is finite
    for I1,I2 be Function of Ne,Me,P1,P2 be Function st
      P1 is one-to-one & P2 is one-to-one &
      rng I1=rng I2 & rng I1=dom P1 & dom P1=dom P2 &
      F=P1*I1 & F=P2*I2 &
      I1 is "increasing & I2 is "increasing
    holds
      P1=P2 & I1=I2
proof
  defpred P[set] means
    for Ne,Ke,Me be Subset of NAT
      for F be Function of Ne,Ke st F=$1
        for I1,I2 be Function of Ne,Me, P1,P2 be Function st
          P1 is one-to-one & P2 is one-to-one &
          rng I1=rng I2 & rng I1=dom P1 & dom P1=dom P2 &
          F=P1*I1 & F=P2*I2 & 
          I1 is "increasing & I2 is "increasing
      holds
        P1=P2 & I1=I2;
  A1:P[{}]
  proof
    let Ne,Ke,Me be Subset of NAT;
    let F be Function of Ne,Ke such that
      A2:F={};
    let I1,I2 be Function of Ne,Me, P1,P2 be Function such that
      P1 is one-to-one & P2 is one-to-one and
      A4:rng I1=rng I2 & rng I1=dom P1 & dom P1=dom P2 and
      A5:F=P1*I1 & F=P2*I2 and 
      I1 is "increasing & I2 is "increasing;
    rng P1={} & rng P2={} & dom I1={} & dom I2={} by A2,A4,A5,RELAT_1:46,47,60;
    then I1={} & I2={} & P1={} & P2={} by RELAT_1:64;
    hence thesis;
  end;
  defpred Q[set,Function] means $1=$2.(min* dom $2);
  A2:for F be Function st
     for x st x in rng F & Q[x,F] holds P[F|(dom F\F"{x})] holds P[F]
  proof
    let F' be Function such that
    A3:for x st x in rng F' & Q[x,F'] holds P[F'|(dom F'\F'"{x})];
    now per cases;
      suppose F'={};
        hence thesis by A1;
      end;
      suppose A4:F'<>{};
        let Ne,Ke,Me be Subset of NAT;
        let F be Function of Ne,Ke such that A5:F=F';
        let I1,I2 be Function of Ne,Me, P1,P2 be Function such that
        A6:P1 is one-to-one & P2 is one-to-one and
        A7:rng I1=rng I2 & rng I1=dom P1 & dom P1=dom P2 and
        A8:F=P1*I1 & F=P2*I2 and
        A9:I1 is "increasing & I2 is "increasing;
        rng F c= Ke & rng F<>{} by A4,A5,RELAT_1:64;
        then A10:Ke is non empty & rng F<>{} by XBOOLE_1:3;  
        then reconsider domF=dom F as non empty Subset of NAT
          by FUNCT_2:def 1,RELAT_1:65;  
        set m=min* dom F;
        reconsider D=dom F\F"{F.m} as Subset of NAT by XBOOLE_1:1;
        A11:for I be Function of Ne,Me,P be Function st
          P is one-to-one & rng I=dom P & F=P*I & I is "increasing
        holds
          dom (I|D)=D & rng (I|D)=rng I\{I.m} &
          dom (P|(rng I\{I.m}))=rng I\{I.m} &
          F|D=(P|(rng I\{I.m}))*(I|D) &
          P|(rng I\{I.m}) is one-to-one &
          for M be Subset of NAT st M=rng I\{I.m}
            for I1 be Function of D,M st I1=I|D holds I1 is "increasing
         proof
           let I be Function of Ne,Me,P be Function such that
           A12:P is one-to-one and
           A13:rng I=dom P and
           A14:F=P*I and
           A15:I is "increasing;
           m in domF by HENMODEL:def 1;
           then F.m in rng F by FUNCT_1:def 5;
           then consider x such that
             A16:x in dom P & x in rng I & P"{F.m}={x} & I"{x}=F"{F.m}
               by A12,A14,Th73;
           m in domF & F.m in {F.m} by HENMODEL:def 1,TARSKI:def 1;
           then m in I"{x} by A16,FUNCT_1:def 13;
           then I.m in {x} by FUNCT_1:def 13;
           then A17:I.m=x & dom F=dom I by A13,A14,RELAT_1:46,TARSKI:def 1; 
           then D c= dom I & rng I\{I.m} c= rng I by XBOOLE_1:36;
           then A18:dom (I|D)=dom I/\D & dom I/\D=D &
           dom (P|(rng I\{I.m}))=dom P/\(rng I\{I.m}) &
           dom P/\(rng I\{I.m})=rng I\{I.m} &
           rng (I|D)=rng I\{I.m} by A13,A16,A17,FUNCT_1:68,XBOOLE_1:28,Th66;
           set rI=rng I\{I.m};
           A19:F|D=(P|rI)*(I|D)
           proof
             D c= dom F by XBOOLE_1:36;
             then A20:dom F/\D=D by XBOOLE_1:28;
             then dom (F|D)=D by FUNCT_1:68;
             then A21:for x holds x in dom (F|D) iff
                  x in dom (I|D) & (I|D).x in dom (P|rI) by A18,FUNCT_1:def 5;
             for x st x in dom (F|D) holds (F|D).x = (P|rI).((I|D).x)
             proof
               let x such that A22:x in dom (F|D);
               x in dom F/\D by A22,FUNCT_1:68;
               then x in dom (I|D) & (I|D).x in dom (P|rI) & x in dom F
                 by A18,A20,FUNCT_1:def 5,XBOOLE_0:def 3;
               then I.x=(I|D).x & (P|rI).((I|D).x)=P.((I|D).x) & (F|D).x=F.x &
                 F.x=P.(I.x) by A14,A22,FUNCT_1:68,22;
               hence thesis;
             end;
             hence thesis by A21,FUNCT_1:20;
           end;
           for M be Subset of NAT st M=rng I\{I.m}
             for I1 be Function of D,M st I1=I|D holds I1 is "increasing
           proof
             let M be Subset of NAT such that M=rng I\{I.m};
             let I1 be Function of D,M such that A20:I1=I|D;
             let l,n such that A21:l in rng I1 & n in rng I1 & l < n;
             rng I1=rng I\{I.m} by A20,Th66,A16,A17;
             then l in rng I & l<>I.m & n in rng I & n<>I.m & D=dom I\I"{I.m}
               by A16,A17,A21,ZFMISC_1:64;
             then min* I"{l} < min* I"{n} & I1"{l}=I"{l} & I1"{n}=I"{n}
               by A15,A21,Def5,A20,Th66;
             hence thesis;
           end;
           hence thesis by A12,A18,A19,FUNCT_1:84;
         end; 
         dom I1=dom F & dom I2=dom F & dom F=Ne
           by A7,A8,A10,RELAT_1:46,FUNCT_2:def 1;
         then A12:min* rng I1=I1.m & min* rng I2=I2.m by A9,Th74;
         rng I1 is Subset of NAT & rng I1\{I1.m} c= rng I1 by XBOOLE_1:1,36;
         then reconsider I=rng I1\{I1.m} as Subset of NAT by XBOOLE_1:1;
         dom (I1|D)=D & dom (I2|D)=D & rng (I1|D)=I & rng (I2|D)=I
           by A6,A7,A8,A9,A11,A12; 
         then reconsider I1D=I1|D,I2D=I2|D as Function of D,I by FUNCT_2:3;
         reconsider rFD=rng (F|D) as Subset of NAT by XBOOLE_1:1;
         D c= dom F by XBOOLE_1:36;
         then dom F/\D=D by XBOOLE_1:28;
         then dom (F|D)=D by FUNCT_1:68;
         then reconsider FD=F|D as Function of D,rFD by FUNCT_2:3; 
         m in domF by HENMODEL:def 1;
         then dom I1D=D & dom I2D=D & rng I1D=I & rng I2D=I & dom (P1|I)=I &
           dom (P2|I)=I & F.m in rng F by FUNCT_1:def 5,A6,A7,A8,A9,A11,A12;  
         then A13:P1|I is one-to-one & P2|I is one-to-one & rng I1D=rng I2D &
           rng I1D=dom (P1|I)&dom (P1|I)=dom (P2|I)&FD=(P1|I)*I1D&FD=(P2|I)*I2D
           & I1D is "increasing & I2D is "increasing & P[FD]
           by A3,A5,A6,A7,A8,A9,A11,A12;
        A14:P1=P2
        proof
          for x st x in dom P1 holds P1.x = P2.x
          proof
            let x such that A15:x in dom P1;
            m in domF & dom I1=dom F by A7,A8,RELAT_1:46,HENMODEL:def 1;
            then I1.m in rng I1 by FUNCT_1:def 5;
            then A16:dom P1=I\/{I1.m} by Lm4,A7;
            now per cases by A15,A16,XBOOLE_0:def 2;
              suppose A17:x in I;
                I c= dom P1 by A7,XBOOLE_1:36;
                then dom P1/\I=I & dom P2/\I=I by A7,XBOOLE_1:28;
                then x in dom (P1|I) & x in dom (P2|I)by A17,FUNCT_1:68;
                then (P1|I).x=P1.x & (P2|I).x=P2.x by FUNCT_1:68;
                hence thesis by A13;
              end;
              suppose x in {I1.m};
                then x=I1.m & m in domF by TARSKI:def 1,HENMODEL:def 1;
                then F.m=P1.x & F.m=P2.x by A7,A8,A12,FUNCT_1:22;
                hence thesis;
              end;
            end;
            hence thesis;
          end;
          hence thesis by A7,FUNCT_1:9;
        end;
        dom I1=dom F & m in domF
           by A7,A8,RELAT_1:46,HENMODEL:def 1;
        then I1.m in rng I1 by FUNCT_1:def 5;
        then I1 is Function of dom I1,rng I1 &
        I2 is Function of dom I2,rng I2& rng I1 is non empty by FUNCT_2:3;
        then P1"*P1 = id dom P1 & I1=(id rng I1)*I1 &
        P2"*P2 = id dom P2 & I2=(id rng I2)*I2 by A6,FUNCT_1:61,FUNCT_2:23;
        then I1=P1"*(P1*I1) & I2=P2"*(P2*I2) by A7,RELAT_1:55;
        hence P1=P2 & I1=I2 by A8,A14;
      end;
    end;
    hence thesis;
  end;
  for F be Function st rng F is finite holds P[F] from Sch9(A1,A2);
  hence thesis;
end;  

theorem
  for F be Function of Ne,Ke st rng F is finite
    for I1,I2 be Function of Ne,Ke, P1,P2 be Permutation of rng F st
      F=P1*I1 & F=P2*I2 & rng F=rng I1 & rng F=rng I2 &
      I1 is "increasing & I2 is "increasing
    holds
      P1=P2 & I1=I2
proof
  let F be Function of Ne,Ke such that A1:rng F is finite;
  let I1,I2 be Function of Ne,Ke, P1,P2 be Permutation of rng F such that
  A2:F=P1*I1 & F=P2*I2 and
  A3:rng F=rng I1 & rng F=rng I2 and
  A4:I1 is "increasing & I2 is "increasing;
  rng F ={} implies rng F={};
  then dom P1=rng F & dom P2=rng F by FUNCT_2:def 1;
  hence thesis by A1,A2,A3,A4,Th76;
end;

