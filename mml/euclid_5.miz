:: Cross Products and Tripple Vector Products in 3-dimensional Euclidian Space
::  by Kanchun, Hiroshi Yamazaki and Yatsuka Nakamura
::
:: Received August 8, 2003
:: Copyright (c) 2003 Association of Mizar Users
 
environ

 vocabularies NUMBERS, REAL_1, SUBSET_1, PRE_TOPC, EUCLID, FINSEQ_1, STRUCT_0,
      FINSEQ_2, MCART_1, FUNCT_1, XREAL_0, ORDINAL1, SUPINF_2, CARD_1, ARYTM_3,
      RELAT_1, ARYTM_1, RVSUM_1, BINOP_2, CARD_3, EUCLID_5;
 notations SUBSET_1, ORDINAL1, NUMBERS, XREAL_0, FUNCT_1, BINOP_1, BINOP_2,
      FINSEQ_1, FINSEQ_2, FINSEQ_4, SEQ_1, FINSEQOP, PRE_TOPC, RVSUM_1,
      RLVECT_1, EUCLID, STRUCT_0, REAL_1;
 constructors XXREAL_0, REAL_1, BINOP_2, FINSEQOP, FINSEQ_4, MONOID_0, SEQ_1,
      EUCLID;
 registrations RELSET_1, NUMBERS, XREAL_0, MEMBERED, MONOID_0, EUCLID,
      VALUED_0, FINSEQ_2;
 requirements REAL, SUBSET, NUMERALS, ARITHM, BOOLE;
 definitions ALGSTR_0;
 theorems EUCLID, RVSUM_1, FINSEQ_2, FINSEQ_1, BINOP_2, XREAL_0, VALUED_1;

begin

reserve x,y,z for Real,
  x3,y3 for Element of REAL,
  p for Point of TOP-REAL 3;

theorem Th1:
  ex x, y, z st p= <* x, y, z *>
proof
  the carrier of TOP-REAL 3 = REAL 3 by EUCLID:25;
  then p is Element of 3-tuples_on REAL by EUCLID:def 1;
  then p in 3-tuples_on REAL;
  then reconsider p as Tuple of 3,REAL by FINSEQ_2:151;
  ex x,y,z st p=<*x,y,z*> by FINSEQ_2:123;
  hence thesis;
end;

definition
  let p;
  func p`1 -> Real equals
  p.1;
  coherence;
  func p`2 -> Real equals
  p.2;
  coherence;
  func p`3 -> Real equals
  p.3;
  coherence;
end;

notation
  let x, y, z be real number;
  synonym |[ x,y,z ]| for <*x,y,z*>;
end;

definition
  let x, y, z be real number;
  redefine func |[ x,y,z ]| -> Point of TOP-REAL 3;
  coherence
  proof
    reconsider x,y,z as Real by XREAL_0:def 1;
    <*x,y,z*> is Element of 3-tuples_on REAL by FINSEQ_2:124;
    then <*x,y,z*> is Element of REAL 3 by EUCLID:def 1;
    hence thesis by EUCLID:25;
  end;
end;

theorem
  |[ x,y,z ]|`1 = x & |[x,y,z]|`2 = y & |[x,y,z]|`3 = z by FINSEQ_1:62;

theorem Th3:
  p = |[ p`1, p`2, p`3 ]|
proof
  consider x,y,z such that
A1: p = <*x,y,z*> by Th1;
  p`1 = x & p`2 = y by A1,FINSEQ_1:62;
  hence thesis by A1,FINSEQ_1:62;
end;

theorem Th4:
  0.TOP-REAL 3 = |[ 0,0,0 ]|
proof
  0.TOP-REAL 3 = 0*3 by EUCLID:74
    .= 3 |-> 0 by EUCLID:def 4
    .= <* 0, 0, 0 *> by FINSEQ_2:76;
  hence thesis;
end;

reserve p1,p2,p3,p4 for Point of TOP-REAL 3,
  x1,x2,y1,y2,z1,z2 for Real;

theorem Th5:
  p1 + p2 = |[ p1`1 + p2`1, p1`2 + p2`2, p1`3 + p2`3]|
proof
  reconsider p19=p1, p29=p2 as Element of REAL 3 by EUCLID:25;
  len(p19+p29) = 3 by FINSEQ_1:def 18;
  then
A1: dom(p19+p29) = Seg 3 by FINSEQ_1:def 3;
  then 2 in dom(p19+p29) by FINSEQ_1:3;
  then (p19+p29).2 = p19.2 + p29.2 by VALUED_1:def 1;
  then
A2: (p1+p2)`2 = p1`2 + p2`2 by EUCLID:68;
  3 in dom(p19+p29) by A1,FINSEQ_1:3;
  then (p19+p29).3 = p19.3 + p29.3 by VALUED_1:def 1;
  then
A3: (p1+p2)`3 = p1`3 + p2`3 by EUCLID:68;
  1 in dom(p19+p29) by A1,FINSEQ_1:3;
  then (p19+p29).1 = p19.1 + p29.1 by VALUED_1:def 1;
  then (p1+p2)`1 = p1`1 + p2`1 by EUCLID:68;
  hence thesis by A2,A3,Th3;
end;

theorem Th6:
  |[x1,y1,z1]| + |[x2,y2,z2]| = |[x1+x2, y1+y2, z1+z2]|
proof
A1: |[x1, y1, z1]|`3 = z1 & |[x2, y2, z2]|`1 = x2 by FINSEQ_1:62;
A2: |[x2, y2, z2]|`2 = y2 & |[x2, y2, z2]|`3 = z2 by FINSEQ_1:62;
  |[x1, y1, z1]|`1 = x1 & |[x1, y1, z1]|`2 = y1 by FINSEQ_1:62;
  hence thesis by A1,A2,Th5;
end;

theorem Th7:
  x*p = |[ x*p`1, x*p`2, x*p`3]|
proof
  reconsider q = p as Element of REAL 3 by EUCLID:25;
  (x*q).2 = x*(q.2) by RVSUM_1:66;
  then
A1: (x*p)`2 = x*(p`2) by EUCLID:69;
  (x*q).3 = x*(q.3) by RVSUM_1:66;
  then
A2: (x*p)`3 = x*(p`3) by EUCLID:69;
  (x*q).1 = x*(q.1) by RVSUM_1:66;
  then (x*p)`1 = x*(p`1) by EUCLID:69;
  hence thesis by A1,A2,Th3;
end;

theorem Th8:
  x * |[x1,y1,z1]| = |[ x*x1, x*y1, x*z1]|
proof
A1: |[x1,y1,z1]|`3 = z1 by FINSEQ_1:62;
  |[x1,y1,z1]|`1 = x1 & |[x1,y1,z1]|`2 = y1 by FINSEQ_1:62;
  hence thesis by A1,Th7;
end;

theorem
  (x*p)`1 = x * p`1 & (x*p)`2 = x * p`2 & (x*p)`3 = x * p`3
proof
A1: (x*p)`3 = |[ x*p`1 ,x*p`2 ,x*p`3]|`3 by Th7;
  (x*p)`1 = |[ x*p`1 ,x*p`2 ,x*p`3]|`1 & (x*p)`2 = |[ x*p`1 ,x*p`2 ,x*p`3
  ]|`2 by Th7;
  hence thesis by A1,FINSEQ_1:62;
end;

theorem Th10:
  -p = |[ -p`1, -p`2, -p`3]|
proof
  thus -p = (-1)*p by EUCLID:43
    .= |[ (-1)*p`1, (-1)*p`2, (-1)*p`3]| by Th7
    .= |[ -p`1, -p`2, -p`3]|;
end;

theorem
  -|[x1,y1,z1]| = |[ -x1, -y1, -z1]|
proof
A1: |[x1,y1,z1]|`3 = z1 by FINSEQ_1:62;
  |[x1,y1,z1]|`1 = x1 & |[x1,y1,z1]|`2 = y1 by FINSEQ_1:62;
  hence thesis by A1,Th10;
end;

theorem Th12:
  p1 - p2 = |[ p1`1 - p2`1, p1`2 - p2`2, p1`3 - p2`3]|
proof
A1: -p2 = |[ -p2`1, -p2`2, -p2`3]| by Th10;
  then
A2: (-p2)`3 = -p2`3 by FINSEQ_1:62;
  (-p2)`1 = -p2`1 & (-p2)`2 = -p2`2 by A1,FINSEQ_1:62;
  hence p1 - p2 = |[ p1`1 + -p2`1, p1`2 + -p2`2, p1`3 + -p2`3]| by A2,Th5
    .= |[ p1`1 - p2`1, p1`2 - p2`2, p1`3 - p2`3]|;
end;

theorem Th13:
  |[x1, y1, z1]| - |[x2, y2, z2]| = |[ x1-x2, y1-y2, z1-z2]|
proof
A1: |[x1, y1, z1]|`3 = z1 & |[x2, y2, z2]|`1 = x2 by FINSEQ_1:62;
A2: |[x2, y2, z2]|`2 = y2 & |[x2, y2, z2]|`3 = z2 by FINSEQ_1:62;
  |[x1, y1, z1]|`1 = x1 & |[x1, y1, z1]|`2 = y1 by FINSEQ_1:62;
  hence thesis by A1,A2,Th12;
end;

definition
  let p1, p2;
  canceled;
  func p1 <X> p2 -> Point of TOP-REAL 3 equals
  |[ (p1`2 * p2`3) - (p1`3 * p2`2
  ) , (p1`3 * p2`1) - (p1`1 * p2`3) , (p1`1 * p2`2) - (p1`2 * p2`1) ]|;
  correctness;
end;

theorem
  p = |[x, y, z]| implies p`1 = x & p`2 = y & p`3 = z by FINSEQ_1:62;

theorem Th15:
  |[x1, y1, z1]| <X> |[x2, y2, z2]| = |[ (y1 * z2) - (z1 * y2) , (
  z1 * x2) - (x1 * z2) , (x1 * y2) - (y1 * x2) ]|
proof
  consider p1 such that
A1: p1 = |[x1, y1, z1]|;
A2: p1`3 = z1 by A1,FINSEQ_1:62;
  consider p2 such that
A3: p2 = |[x2, y2, z2]|;
A4: p2`3 = z2 by A3,FINSEQ_1:62;
A5: p2`1 = x2 & p2`2 = y2 by A3,FINSEQ_1:62;
  p1`1 = x1 & p1`2 = y1 by A1,FINSEQ_1:62;
  hence thesis by A1,A2,A3,A5,A4;
end;

theorem
  (x*p1) <X> p2 = x* (p1 <X> p2) & (x*p1) <X> p2 = p1 <X> (x*p2)
proof
A1: (x*p1) <X> p2 = |[ x*p1`1 ,x*p1`2 ,x*p1`3]| <X> p2 by Th7
    .= |[ x*p1`1 ,x*p1`2 ,x*p1`3]| <X> |[ p2`1,p2`2,p2`3]| by Th3
    .= |[ (x*p1`2 * p2`3) - (x*p1`3 * p2`2) , (x*p1`3 * p2`1) - (x*p1`1 * p2
  `3) , (x*p1`1 * p2`2) - (x*p1`2 * p2`1) ]| by Th15;
  then
A2: (x*p1) <X> p2 = |[ x * ( (p1`2 * p2`3) - (p1`3 * p2`2) ), x * ( (p1`3 *
  p2`1) - (p1`1 * p2`3) ), x * ( (p1`1 * p2`2) - (p1`2 * p2`1) ) ]|
    .= x * (p1 <X> p2) by Th8;
  (x*p1) <X> p2 = |[ p1`2 * (x * p2`3) - p1`3 * (x * p2`2), p1`3 * (x * p2
  `1) - p1`1 * (x * p2`3), p1`1 * (x * p2`2) - p1`2 * (x * p2`1) ]| by A1
    .= |[ p1`1, p1`2, p1`3]| <X> |[ x*p2`1 ,x*p2`2 ,x*p2`3]| by Th15
    .= p1 <X> |[ x*p2`1 ,x*p2`2 ,x*p2`3]| by Th3
    .= p1 <X> (x*p2) by Th7;
  hence thesis by A2;
end;

theorem Th17:
  p1<X>p2 = - p2<X>p1
proof
  - p2 <X> p1 = (-1)*(p2 <X> p1) by EUCLID:43
    .= |[ (-1) * ( (p2`2 * p1`3) - (p2`3 * p1`2) ), (-1) * ( (p2`3 * p1`1) -
  (p2`1 * p1`3) ), (-1) * ( (p2`1 * p1`2) - (p2`2 * p1`1) ) ]| by Th8
    .= p1 <X> p2;
  hence thesis;
end;

theorem
  (-p1) <X> p2 = p1 <X> (-p2)
proof
  (-p1) <X> p2 = |[ -p1`1, -p1`2, -p1`3 ]| <X> p2 by Th10
    .= |[ -p1`1, -p1`2, -p1`3 ]| <X> |[ p2`1, p2`2, p2`3 ]| by Th3
    .= |[ ((-p1`2) * p2`3) - ((-p1`3) * p2`2) , ((-p1`3) * p2`1) - ((-p1`1)
  * p2`3) , ((-p1`1) * p2`2) - ((-p1`2) * p2`1) ]| by Th15
    .= |[ (p1`2 * (-p2`3)) - (p1`3 * (-p2`2)) , (p1`3 * (-p2`1)) - (p1`1 * (
  -p2`3)) , (p1`1 * (-p2`2)) - (p1`2 * (-p2`1)) ]|
    .= |[ p1`1, p1`2, p1`3 ]| <X> |[ -p2`1, -p2`2, -p2`3 ]| by Th15
    .= |[ p1`1, p1`2, p1`3 ]| <X> -p2 by Th10;
  hence thesis by Th3;
end;

theorem
  |[0, 0, 0]| <X> |[x, y, z]| = 0.TOP-REAL 3
proof
  |[0, 0, 0]| <X> |[x, y, z]| = |[ (0 * z) - (0 * y) , (0 * x) - (0 * z) ,
  (0 * y) - (0 * x) ]| by Th15
    .= |[ 0 * (z - y) , 0 * (x - z) , 0 * (y - x) ]|
    .= 0 * |[ (z - y), (x - z), (y - x) ]| by Th8;
  hence thesis by EUCLID:33;
end;

theorem
  |[x1, 0, 0]| <X> |[x2, 0, 0]| = 0.TOP-REAL 3
proof
  |[x1, 0, 0]| <X> |[x2, 0, 0]| = |[ (0 * 0) - (0 * 0) , (0 * x2) - (x1 *
  0) , (x1 * 0) - (0 * x2) ]| by Th15
    .= |[ 0 * (0 - 0), 0 * (x2 - x1), 0 * (x1 - x2) ]|
    .= 0 * |[ (0 - 0), (x2 - x1), (x1 - x2) ]| by Th8
    .= 0.TOP-REAL 3 by EUCLID:33;
  hence thesis;
end;

theorem
  |[0, y1, 0]| <X> |[0, y2, 0]| = 0.TOP-REAL 3
proof
  |[0, y1, 0]| <X> |[0, y2, 0]| = |[ (y1 * 0) - (0 * y2), (0 * 0) - (0 * 0
  ), (0 * y2) - (y1 * 0) ]| by Th15
    .= |[ 0 * (y1 - y2), 0 * (0 - 0), 0 * (y2 - y1) ]|
    .= 0 * |[ (y1 - y2), (0 - 0), (y2 - y1) ]| by Th8
    .= 0.TOP-REAL 3 by EUCLID:33;
  hence thesis;
end;

theorem
  |[0, 0, z1]| <X> |[0, 0, z2]| = 0.TOP-REAL 3
proof
  |[0, 0, z1]| <X> |[0, 0, z2]| = |[ (0 * z2) - (z1 * 0), (z1 * 0) - (0 *
  z2), (0 * 0) - (0 * 0) ]| by Th15
    .= |[ 0 * (z2 - z1), 0 * (z1 - z2), 0 * (0 * 0) ]|
    .= 0 * |[ (z2 - z1), (z1 - z2), (0 - 0) ]| by Th8
    .= 0.TOP-REAL 3 by EUCLID:33;
  hence thesis;
end;

theorem Th23:
  p1 <X> (p2+p3) = ( p1 <X> p2 ) + ( p1 <X> p3 )
proof
A1: ( p1 <X> p2 ) + ( p1 <X> p3 ) = |[ (p1`2 * p2`3 - p1`3 * p2`2) + (p1`2 *
p3`3 - p1`3 * p3`2), (p1`3 * p2`1 - p1`1 * p2`3) + (p1`3 * p3`1 - p1`1 * p3`3),
  (p1`1 * p2`2 - p1`2 * p2`1) + (p1`1 * p3`2 - p1`2 * p3`1) ]| by Th6
    .= |[ p1`2 * p2`3 - p1`3 * p2`2 + p1`2 * p3`3 - p1`3 * p3`2, p1`3 * p2`1
  - p1`1 * p2`3 + p1`3 * p3`1 - p1`1 * p3`3, p1`1 * p2`2 - p1`2 * p2`1 + p1`1 *
  p3`2 - p1`2 * p3`1 ]|;
A2: p2+p3 = |[ p2`1 + p3`1, p2`2 + p3`2, p2`3 + p3`3]| by Th5;
  then
A3: (p2+p3)`3 = p2`3 + p3`3 by FINSEQ_1:62;
  (p2+p3)`1 = p2`1 + p3`1 & (p2+p3)`2 = p2`2 + p3`2 by A2,FINSEQ_1:62;
  hence thesis by A3,A1;
end;

theorem Th24:
  (p1+p2) <X> p3 = ( p1 <X> p3 ) + ( p2 <X> p3 )
proof
  (p1+p2) <X> p3 = - ( p3 <X> (p1+p2) ) by Th17
    .= - ( ( p3 <X> p1 ) + ( p3 <X> p2 ) ) by Th23
    .= - ( ( p3 <X> p1 ) - ( p2 <X> p3 ) ) by Th17
    .= - ( p3 <X> p1 ) + ( p2 <X> p3 ) by EUCLID:48;
  hence thesis by Th17;
end;

theorem
  p1 <X> p1 = 0.TOP-REAL 3 by Th4;

theorem
  (p1+p2) <X> (p3+p4) = p1<X>p3 + p1<X>p4 + p2<X>p3 + p2<X>p4
proof
  (p1+p2) <X> (p3+p4) = ( p1 <X> (p3+p4) ) + ( p2 <X> (p3+p4) ) by Th24
    .= ( p1 <X> p3 + p1 <X> p4 ) + ( p2 <X> (p3+p4) ) by Th23
    .= ( p1 <X> p3 + p1 <X> p4 ) + (p2 <X> p3 + p2 <X> p4 ) by Th23;
  hence thesis by EUCLID:30;
end;
 
::
:: Inner Product for Point of TOP-REAL 3
::

theorem Th27:
  p = <* p`1, p`2, p`3 *>
proof
  reconsider f = p as FinSequence;
  consider x, y, z being Element of REAL such that
A1: p = <* x, y, z *> by Th1;
  <* x, y, z *> = <* f.1, y, z *> by A1,FINSEQ_1:62
    .= <* f.1, f.2, z *> by A1,FINSEQ_1:62
    .= <* p`1, p`2, p`3 *> by A1,FINSEQ_1:62;
  hence thesis by A1;
end;

theorem Th28:
  for f1, f2 be FinSequence of REAL st len f1 = 3 & len f2 = 3
  holds mlt(f1, f2) = <* f1.1*f2.1, f1.2*f2.2, f1.3*f2.3 *>
proof
  let f1, f2 be FinSequence of REAL such that
A1: len f1 = 3 and
A2: len f2 = 3;
A3: f2 = <* f2.1, f2.2, f2.3 *> by A2,FINSEQ_1:62;
  mlt(f1, f2) = multreal.:(f1, f2) by RVSUM_1:def 9
    .= multreal.:(<* f1.1, f1.2, f1.3 *>, <* f2.1, f2.2, f2.3 *>) by A1,A3,
FINSEQ_1:62
    .= <* multreal.(f1.1, f2.1), multreal.(f1.2, f2.2), multreal.(f1.3, f2.3
  )*> by FINSEQ_2:90
    .= <* f1.1*f2.1, multreal.(f1.2, f2.2), multreal.(f1.3, f2.3)*> by
BINOP_2:def 11
    .= <* f1.1*f2.1, f1.2*f2.2, multreal.(f1.3, f2.3)*> by BINOP_2:def 11;
  hence thesis by BINOP_2:def 11;
end;

theorem Th29:
  |(p1,p2)| = p1`1*p2`1 + p1`2*p2`2 + p1`3*p2`3
proof
  reconsider f1=p1, f2=p2 as FinSequence of REAL by EUCLID:27;
A1: len f1 = len <* p1`1, p1`2, p1`3 *> by Th27
    .= 3 by FINSEQ_1:62;
A2: len f2 = len <* p2`1, p2`2, p2`3 *> by Th27
    .= 3 by FINSEQ_1:62;
  |(p1,p2)| = Sum mlt(f1, f2) by RVSUM_1:def 17
    .= Sum <* f1.1*f2.1, f1.2*f2.2, f1.3*f2.3 *> by A1,A2,Th28
    .= p1`1*p2`1 + p1`2*p2`2 + p1`3*f2.3 by RVSUM_1:108;
  hence thesis;
end;

theorem Th30:
  |( |[ x1, x2, x3 ]|, |[ y1, y2, y3 ]| )| = x1*y1 + x2*y2 + x3*y3
proof
  consider p1 such that
A1: p1 = |[x1, x2, x3]|;
A2: p1`3 = x3 by A1,FINSEQ_1:62;
  consider p2 such that
A3: p2 = |[y1, y2, y3]|;
A4: p2`3 = y3 by A3,FINSEQ_1:62;
A5: p2`1 = y1 & p2`2 = y2 by A3,FINSEQ_1:62;
  p1`1 = x1 & p1`2 = x2 by A1,FINSEQ_1:62;
  hence thesis by A1,A2,A3,A5,A4,Th29;
end;
 
::
:: Scalar and Vector : Triple Products
::

definition
  let p1, p2, p3;
  func |{ p1,p2,p3 }| -> real number equals
  |(p1, p2<X>p3)|;
  coherence;
end;

theorem
  |{ p1, p1, p2 }| = 0 & |{ p2, p1, p2 }| = 0
proof
A1: |{ p2, p1, p2 }| = p2`1*(p1<X>p2)`1 + p2`2*(p1<X>p2)`2 + p2`3*(p1<X>p2)
  `3 by Th29
    .= p2`1*((p1`2 * p2`3) - (p1`3 * p2`2)) + p2`2*(p1<X>p2)`2 + p2`3*(p1<X>
  p2)`3 by FINSEQ_1:62
    .= p2`1*( (p1`2 * p2`3) - (p1`3 * p2`2) ) + p2`2*( (p1`3 * p2`1) - (p1`1
  * p2`3) ) + p2`3*(p1<X>p2)`3 by FINSEQ_1:62
    .= ( p2`1*(p1`2 * p2`3) - p2`1*(p1`3 * p2`2) ) + p2`2*( (p1`3 * p2`1) -
  (p1`1 * p2`3) ) + p2`3*( (p1`1 * p2`2) - (p1`2 * p2`1) ) by FINSEQ_1:62
    .= 0 - p2`2*(p1`1 * p2`3) + p2`2*(p1`1 * p2`3);
  |{ p1, p1, p2 }| = p1`1*(p1<X>p2)`1 + p1`2*(p1<X>p2)`2 + p1`3*(p1<X>p2)
  `3 by Th29
    .= p1`1*((p1`2 * p2`3) - (p1`3 * p2`2)) + p1`2*(p1<X>p2)`2 + p1`3*(p1<X>
  p2)`3 by FINSEQ_1:62
    .= p1`1*( (p1`2 * p2`3) - (p1`3 * p2`2) ) + p1`2*( (p1`3 * p2`1) - (p1`1
  * p2`3) ) + p1`3*(p1<X>p2)`3 by FINSEQ_1:62
    .= ( p1`1*(p1`2 * p2`3) - p1`1*(p1`3 * p2`2) ) + p1`2*( (p1`3 * p2`1) -
  (p1`1 * p2`3) ) + p1`3*( (p1`1 * p2`2) - (p1`2 * p2`1) ) by FINSEQ_1:62
    .= 0;
  hence thesis by A1;
end;

theorem
  p1 <X> ( p2<X>p3 ) = ( |(p1,p3)| * p2 ) - ( |(p1,p2)| * p3 )
proof
A1: (p1`2*((p2`1 * p3`2) - (p2`2 * p3`1))) - (p1`3*((p2`3 * p3`1) - (p2`1 *
p3`3) )) = (p1`2*p3`2 + p1`3*p3`3 + p1`1*p3`1)*p2`1 - (p1`2*p2`2 + p1`3*p2`3 +
p1`1* p2`1)*p3`1 & (p1`3*((p2`2 * p3`3) - (p2`3 * p3`2))) - (p1`1*((p2`1 * p3`2
) - ( p2`2 * p3`1))) = (p1`3*p3`3 + p1`1*p3`1 + p1`2*p3`2)*p2`2 - (p1`3*p2`3 +
  p1`1* p2`1 + p1`2*p2`2)*p3`2;
A2: (p1`1*((p2`3 * p3`1) - (p2`1 * p3`3))) - (p1`2*((p2`2 * p3`3) - (p2`3 *
  p3`2))) = (p1`1*p3`1 + p1`2*p3`2 + p1`3*p3`3)*p2`3 - (p1`1*p2`1 + p1`2*p2`2 +
  p1`3*p2`3)*p3`3;
  p1 <X> (p2 <X> p3) = |[ p1`1, p1`2, p1`3 ]| <X> (|[ (p2`2 * p3`3) - (p2
`3 * p3`2) , (p2`3 * p3`1) - (p2`1 * p3`3) , (p2`1 * p3`2) - (p2`2 * p3`1) ]|)
  by Th3
    .= |[(p1`2*((p2`1 * p3`2) - (p2`2 * p3`1))) - (p1`3*((p2`3 * p3`1) - (p2
`1 * p3`3))), (p1`3*((p2`2 * p3`3) - (p2`3 * p3`2))) - (p1`1*((p2`1 * p3`2) - (
p2`2 * p3`1))), (p1`1*((p2`3 * p3`1) - (p2`1 * p3`3))) - (p1`2*((p2`2 * p3`3) -
  (p2`3 * p3`2)))]| by Th15;
  then
  p1 <X> (p2 <X> p3) = |[ (p1`1*p3`1 + p1`2*p3`2 + p1`3*p3`3)*p2`1, (p1`1*
p3`1 + p1`2*p3`2 + p1`3*p3`3)*p2`2, (p1`1*p3`1 + p1`2*p3`2 + p1`3*p3`3)*p2`3 ]|
- |[ (p1`1*p2`1 + p1`2*p2`2 + p1`3*p2`3)*p3`1, (p1`1*p2`1 + p1`2*p2`2 + p1`3*p2
  `3)*p3`2, (p1`1*p2`1 + p1`2*p2`2 + p1`3*p2`3)*p3`3 ]| by A1,A2,Th13
    .= (p1`1*p3`1 + p1`2*p3`2 + p1`3*p3`3) * |[p2`1, p2`2, p2`3 ]| - |[ (p1
`1*p2`1 + p1`2*p2`2 + p1`3*p2`3)*p3`1, (p1`1*p2`1 + p1`2*p2`2 + p1`3*p2`3)*p3`2
  , (p1`1*p2`1 + p1`2*p2`2 + p1`3*p2`3)*p3`3 ]| by Th8
    .= (p1`1*p3`1 + p1`2*p3`2 + p1`3*p3`3) * |[ p2`1, p2`2, p2`3 ]| - (p1`1*
  p2`1 + p1`2*p2`2 + p1`3*p2`3) * |[ p3`1, p3`2, p3`3 ]| by Th8
    .= |(p1, p3)| * |[ p2`1, p2`2, p2`3 ]| - (p1`1*p2`1 + p1`2*p2`2 + p1`3*
  p2`3) * |[ p3`1, p3`2, p3`3 ]| by Th29
    .= |(p1, p3)| * |[ p2`1, p2`2, p2`3 ]| - |(p1, p2 )| * |[ p3`1, p3`2, p3
  `3 ]| by Th29
    .= |(p1, p3)| * p2 - |(p1, p2 )| * |[ p3`1, p3`2, p3`3 ]| by Th3;
  hence thesis by Th3;
end;

theorem Th33:
  |{ p1, p2, p3 }| = |{ p2, p3, p1 }|
proof
  |{ p1, p2, p3 }| = |(|[ p1`1, p1`2, p1`3 ]|, |[ (p2`2*p3`3) - (p2`3*p3`2
  ), (p2`3*p3`1) - (p2`1*p3`3), (p2`1*p3`2) - (p2`2*p3`1) ]|)| by Th3
    .= p1`1*((p2`2*p3`3) - (p2`3*p3`2)) + p1`2*((p2`3*p3`1) - (p2`1*p3`3)) +
  p1`3*((p2`1*p3`2) - (p2`2*p3`1)) by Th30
    .= p2`1*(p3`2*p1`3 - p3`3*p1`2) + p2`2*(p3`3*p1`1 - p3`1*p1`3) + p2`3*(
  p3`1*p1`2 - p3`2*p1`1)
    .= |( |[ p2`1, p2`2, p2`3 ]|, |[ p3`2*p1`3 - p3`3*p1`2, p3`3*p1`1 - p3`1
  *p1`3, p3`1*p1`2 - p3`2*p1`1 ]| )| by Th30
    .= |( p2, p3 <X> p1 )| by Th3;
  hence thesis;
end;

theorem
  |{ p1, p2, p3 }| = |{ p3, p1, p2 }| by Th33;

theorem
  |{ p1, p2, p3 }| = |( p1<X>p2, p3 )|
proof
  |{ p1, p2, p3 }| = |(|[ p1`1, p1`2, p1`3 ]|, |[ (p2`2*p3`3) - (p2`3*p3`2
  ), (p2`3*p3`1) - (p2`1*p3`3), (p2`1*p3`2) - (p2`2*p3`1) ]|)| by Th3
    .= p1`1*((p2`2*p3`3) - (p2`3*p3`2)) + p1`2*((p2`3*p3`1) - (p2`1*p3`3)) +
  p1`3*((p2`1*p3`2) - (p2`2*p3`1)) by Th30
    .= p2`2*(p1`1*p3`3) - p2`3*(p1`1*p3`2) + (p2`3*(p1`2*p3`1) - p2`1*(p1`2*
  p3`3)) + (p2`1*(p1`3*p3`2) - p2`2*(p1`3*p3`1));
  then
  |{ p1, p2, p3 }| = (p2`3*p1`2 - p2`2*p1`3)*p3`1 + (p2`1*p1`3 - p2`3*p1`1
  )*p3`2 + (p2`2*p1`1 - p2`1*p1`2)*p3`3
    .= |( p1 <X> p2, |[ p3`1, p3`2, p3`3 ]| )| by Th30
    .= |( p1 <X> p2, p3 )| by Th3;
  hence thesis;
end;
