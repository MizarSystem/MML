:: Subsequences of Standard Special Circular Sequences in $ { \cal E
:: } ^2_ { \rm T } $
::  by Yatsuka Nakamura , Roman Matuszewski and Adam Grabowski
::
:: Received May 12, 1997
:: Copyright (c) 1997 Association of Mizar Users

environ

 vocabularies ARYTM_1, NAT_1, RELAT_1, ARYTM_3, FINSEQ_1, FINSEQ_6, FUNCT_1,
      FINSEQ_5, RFINSEQ, EUCLID, TOPREAL1, JORDAN3, SEQM_3, GOBOARD5, TARSKI,
      BOOLE, PRE_TOPC, TOPREAL4, TOPREAL2, MCART_1, JORDAN4, FINSEQ_4;
 notations TARSKI, XBOOLE_0, XREAL_0, REAL_1, NAT_1, BINARITH, RELAT_1,
      FINSEQ_1, FUNCT_1, FINSEQ_4, FINSEQ_5, STRUCT_0, TOPREAL1, TOPREAL2,
      TOPREAL4, GOBOARD1, GOBOARD5, PRE_TOPC, EUCLID, FINSEQ_6, RFINSEQ,
      JORDAN3;
 constructors GOBOARD9, BINARITH, TOPREAL4, TOPREAL2, REAL_1, RFINSEQ, JORDAN3,
      FINSEQ_4, MEMBERED, NAT_1;
 registrations STRUCT_0, GOBOARD5, RELSET_1, EUCLID, SPPOL_2, XREAL_0, ARYTM_3,
      MEMBERED;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI;
 theorems TARSKI, AXIOMS, JORDAN3, TOPREAL4, EUCLID, TOPREAL1, TOPREAL2,
      FUNCT_1, SPPOL_1, GOBOARD5, FINSEQ_1, GOBOARD7, NAT_1, SPPOL_2, REAL_1,
      BINARITH, REAL_2, SQUARE_1, FINSEQ_3, GR_CY_1, FINSEQ_2, FINSEQ_4,
      GOBOARD9, FINSEQ_5, RFINSEQ, FINSEQ_6, ENUMSET1, SCMFSA_7, ZFMISC_1,
      TOPREAL3, AMI_5, XBOOLE_0, XBOOLE_1, XCMPLX_0, XCMPLX_1;

begin :: Preliminaries

reserve i, i1, i2, i3, j, k, n for Nat,
        r, r1, r2, s, s1 for Real;

theorem Th1: n-'i=0 implies n<=i
proof assume A1:n-'i=0;
 assume i<n; then i+1<=n by NAT_1:38; then i+1-i<=n-i by REAL_1:49;
  then 0<n-i;
  hence contradiction by A1,BINARITH:def 3;
end;

theorem Th2:i<=j implies j+k-'i=j+k-i
proof assume i<=j; then i<=j+k by NAT_1:37;
 hence thesis by SCMFSA_7:3;
end;

theorem Th3:i<=j implies j+k-'i=j-'i+k
proof assume A1:i<=j; then i<=j+k by NAT_1:37;
 hence j+k-'i=j+k-i by SCMFSA_7:3 .=j-i+k
    .=j-'i+k by A1,SCMFSA_7:3;
end;

theorem Th4:i1<>0 & i2=i3*i1 implies i3<=i2
proof assume A1:i1<>0 & i2=i3*i1; then i1>0 by NAT_1:19;
  then i1>=0+1 by NAT_1:38;
  then i1-'1+1=i1 by AMI_5:4;
  then i2=i3*(i1-'1)+i3*1 by A1;
 hence i3<=i2 by NAT_1:29;
end;

theorem  i1<i2 implies i1 div i2=0
proof assume A1: i1<i2;
     now per cases by NAT_1:def 1;
  case (ex j1 be Nat st i1=i2*(i1 div i2)+j1 & j1<i2);
   then consider j1 be Nat such that A2:i1=i2*(i1 div i2)+j1 & j1<i2;
   A3:i1-'j1=i2*(i1 div i2) by A2,BINARITH:39;
      now assume i1 div i2<>0;
    then A4:i1-'j1>=i2 by A3,Th4;
       i1>=i1-'j1 by JORDAN3:13;
    hence contradiction by A1,A4,AXIOMS:22;
   end;
   hence i1 div i2=0;
  end;
  case (i1 div i2)=0 & i2=0;
   hence i1 div i2=0;
  end;
  end;
 hence thesis;
end;

theorem Th6: 0<j & j<i & i<j+j implies i mod j<>0
proof assume A1:0<j & j<i & i<j+j;
 assume A2:i mod j=0;
  A3:i-j=i-'j by A1,SCMFSA_7:3;
     i=i-j+j;
  then A4:i mod j=(i-'j+j) mod j by A1,SCMFSA_7:3
  .=((i-'j)+ (j mod j)) mod j by GR_CY_1:3
  .=(i-'j+0)mod j by GR_CY_1:5
  .=(i-'j)mod j;
     i-j<j+j-j by A1,REAL_1:54;
  then (i-'j) mod j=i-'j by A3,GR_CY_1:4;
 hence contradiction by A1,A2,A4,Th1;
end;

theorem Th7: 0<j & j<=i & i<j+j implies i mod j=i-j & i mod j=i-'j
proof assume A1:0<j & j<=i & i<j+j;
  then A2:i-j=i-'j by SCMFSA_7:3;
     i=i-j+j;
  then A3:i mod j=(i-'j+j) mod j by A1,SCMFSA_7:3
    .=((i-'j)+(j mod j))mod j by GR_CY_1:3
  .=((i-'j)+0)mod j by GR_CY_1:5 .=(i-'j)mod j;
     i-j<j+j-j by A1,REAL_1:54;
 hence thesis by A2,A3,GR_CY_1:4;
end;

theorem Th8: (j+j)mod j=0
proof thus (j+j)mod j= (2*j) mod j
  .= 0 by NAT_1:63;
end;

theorem Th9: 0<k & k<=j & k mod j=0 implies k=j
proof assume A1:0<k & k<=j & k mod j=0;
 then k >= j by GR_CY_1:4;
 hence k=j by A1,REAL_1:def 5;
end;

begin :: Some facts about cutting of finite sequences

reserve D for non empty set,
        f1 for FinSequence of D;

canceled 4;

theorem Th14: for f1 st f1 is circular & 1<=len f1 holds f1.1=f1.len f1
proof let f1;assume A1:f1 is circular & 1<=len f1;
 then A2:f1/.1=f1/.len f1 by FINSEQ_6:def 1;
    f1.1=f1/.1 by A1,FINSEQ_4:24;
 hence thesis by A1,A2,FINSEQ_4:24;
end;

theorem  for f1,i1,i2 st i1<=i2 holds
  (f1|i1)|i2=f1|i1 & (f1|i2)|i1=f1|i1
proof let f1,i1,i2;assume A1:i1<=i2;
    len (f1|i1)<=i1 by FINSEQ_5:19;
 then len (f1|i1)<=i2 by A1,AXIOMS:22;
 hence (f1|i1)|i2=f1|i1 by FINSEQ_1:79;
  A2:f1|i2=f1|(Seg i2) by FINSEQ_1:def 15;
  A3:f1|i1=f1|(Seg i1) by FINSEQ_1:def 15;
  A4:(f1|i2)|i1=(f1|i2)|Seg i1 by FINSEQ_1:def 15;
     Seg i1 c= Seg i2 by A1,FINSEQ_1:7;
 hence thesis by A2,A3,A4,FUNCT_1:82;
end;

theorem Th16: (<*>D)|i=<*>D
proof len (<*>D)=0 by FINSEQ_1:32;
  then len (<*>D)<=i by NAT_1:18;
 hence thesis by FINSEQ_1:79;
end;

theorem Th17: Rev (<*>D)=<*>D
proof len (Rev(<*>D))=len (<*>D) by FINSEQ_5:def 3 .=0 by FINSEQ_1:32;
 hence thesis by FINSEQ_1:32;
end;

theorem Th18: for f1,k st k<len f1 holds
   (f1/^k).len (f1/^k)=f1.len f1 & (f1/^k)/.len (f1/^k)=f1/.len f1
proof let f1,k;assume A1:k<len f1;
  then k+1<=len f1 by NAT_1:38;
  then A2: k+1-k<=len f1-k by REAL_1:49;
     0<=k by NAT_1:18;
then A3: 0+1<=len f1 by A1,NAT_1:38;
  A4:1<=len (f1/^k) by A1,A2,RFINSEQ:def 2;
  then len (f1/^k) in Seg len (f1/^k) by FINSEQ_1:3;
then A5: len (f1/^k) in dom (f1/^k) & k<=len f1 by A1,FINSEQ_1:def 3;
     len (f1/^k)+k=len f1-k+k by A1,RFINSEQ:def 2 .=len f1;
 hence A6:(f1/^k).len (f1/^k)=f1.len f1 by A5,RFINSEQ:def 2;
     (f1/^k)/.len (f1/^k)=(f1/^k).len (f1/^k) by A4,FINSEQ_4:24;
 hence thesis by A3,A6,FINSEQ_4:24;
end;

theorem Th19: for g being FinSequence of TOP-REAL 2,i
st g is_S-Seq & i+1<len g holds g/^i is_S-Seq
proof let g be FinSequence of TOP-REAL 2,i;
  assume A1:g is_S-Seq & i+1<len g;
   A2:1<=i+1 by NAT_1:29;
   then A3:1<len g by A1,AXIOMS:22;
      mid(g,i+1,len g)=g/^(i+1-'1) by A1,JORDAN3:26
                  .=g/^i by BINARITH:39;
 hence thesis by A1,A2,A3,JORDAN3:39;
end;

theorem Th20: for f1,i1,i2 st 1<=i2 & i2<=i1 & i1<=len f1
  holds len mid(f1,i2,i1)=i1-'i2+1
proof let f1,i1,i2;assume A1:1<= i2 & i2<=i1 & i1<= len f1;
  then A2:1<=i1 by AXIOMS:22;
     i2<=len f1 by A1,AXIOMS:22;
 hence thesis by A1,A2,JORDAN3:27;
end;

theorem Th21: for f1,i1,i2 st 1<=i2 & i2<=i1 & i1<=len f1
  holds len mid(f1,i1,i2)=i1-'i2+1
proof let f1,i1,i2;assume A1:1<= i2 & i2<=i1 & i1<= len f1;
  per cases by A1,REAL_1:def 5;
  suppose i1=i2;
   hence len mid(f1,i1,i2)=i1-'i2+1 by A1,JORDAN3:27;
  end;
  suppose A2:i2<i1; A3:1<=i1 by A1,AXIOMS:22;
       i2<=len f1 by A1,AXIOMS:22;
   hence len mid(f1,i1,i2)=i1-'i2+1 by A1,A2,A3,JORDAN3:27;
end;
end;

theorem Th22: for f1,i1,i2,j st 1<=i1 & i1<=i2 & i2<=len f1
 holds mid(f1,i1,i2).(len mid(f1,i1,i2))=f1.i2
proof let f1,i1,i2,j;assume A1:1<=i1 & i1<=i2 & i2<=len f1;
  then A2:1<=i2 by AXIOMS:22;
  A3:i1<=len f1 by A1,AXIOMS:22;
  then len mid(f1,i1,i2)=i2-'i1+1 by A1,A2,JORDAN3:27;
  then 1<=len mid(f1,i1,i2) by NAT_1:29;
  then A4:mid(f1,i1,i2).(len mid(f1,i1,i2))
  =f1.(len mid(f1,i1,i2)+i1-'1) by A1,A2,A3,JORDAN3:27
  .=f1.(i2-'i1+1+i1-'1) by A1,A2,A3,JORDAN3:27;
     i2-'i1+1+i1=i2-i1+1+i1 by A1,SCMFSA_7:3 .=i2+1;
 hence thesis by A4,BINARITH:39;
end;

theorem Th23: for f1,i1,i2,j st 1<=i1 & i1<=len f1 & 1<=i2 & i2<=len f1
 holds mid(f1,i1,i2).(len mid(f1,i1,i2))=f1.i2
proof let f1,i1,i2,j;assume A1:1<=i1 & i1<=len f1 & 1<=i2 & i2<=len f1;
 per cases;
 suppose i1<=i2;
  hence mid(f1,i1,i2).(len mid(f1,i1,i2))=f1.i2 by A1,Th22;
 end;
 suppose A2:i1>i2;
  then len mid(f1,i1,i2)=i1-'i2+1 by A1,JORDAN3:27;
  then 1<=len mid(f1,i1,i2) by NAT_1:29;
  then A3:mid(f1,i1,i2).(len mid(f1,i1,i2))
  =f1.(i1-'len mid(f1,i1,i2)+1) by A1,A2,JORDAN3:27
     .=f1.(i1-'(i1-'i2+1)+1) by A1,A2,JORDAN3:27;
     0<=i2-1 by A1,SQUARE_1:12;
  then i1-0>=i1-(i2-1) by REAL_2:106;
  then i1>=i1-i2+1;
  then i1-'i2+1<=i1 by A2,SCMFSA_7:3;
  then i1-'(i1-'i2+1)+1=i1-(i1-'i2+1)+1 by SCMFSA_7:3
  .=i1-(i1-'i2)
  .=i1-(i1-i2) by A2,SCMFSA_7:3
  .=i2;
  hence thesis by A3;
end;
end;

theorem Th24: for f1,i1,i2,j st 1<=i2 & i2<=i1 & i1<=len f1 & 1<=j
  & j<=i1-'i2+1 holds mid(f1,i1,i2).j=f1.(i1-'j+1)
proof let f1,i1,i2,j;assume
  A1:1<= i2 & i2<=i1 & i1<= len f1 & 1<=j
    & j<=i1-'i2+1;
   then A2:j<=len mid(f1,i1,i2) by Th21;
  per cases by A1,REAL_1:def 5;
  suppose A3:i1=i2;
then A4:  i1-'i2+1=0+1 by GOBOARD9:1 .=1;
    then j+i1-'1=1+i1-'1 by A1,AXIOMS:21
      .=1+i1-1 by Th2 .=i1-1+1
    .=i1-'1+1 by A1,A3,SCMFSA_7:3 .=i1-'j+1 by A1,A4,AXIOMS:21;
   hence mid(f1,i1,i2).j=f1.(i1-'j+1) by A1,A2,A3,JORDAN3:27;
  end;
  suppose A5:i2<i1; A6:1<=i1 by A1,AXIOMS:22;
       i2<=len f1 by A1,AXIOMS:22;
   hence mid(f1,i1,i2).j=f1.(i1-'j+1) by A1,A2,A5,A6,JORDAN3:27;
end;
end;

theorem Th25:for f1,i1,i2 st 1<=i2 & i2<=i1 & i1<=len f1 & 1<=j & j<=i1-'i2+1
  holds mid(f1,i1,i2).j=mid(f1,i2,i1).(i1-i2+1-j+1)
    & i1-i2+1-j+1=i1-'i2+1-'j+1
proof let f1,i1,i2;assume A1: 1<=i2 & i2<=i1 & i1<=len f1 & 1<=j & j<=i1-'i2+1;
  then A2:1<=i1 by AXIOMS:22;
  A3:i2<=len f1 by A1,AXIOMS:22;
    A4: j-j<=i1-'i2+1-j by A1,REAL_1:49;
     1-j<=j-j by A1,REAL_1:49;
  then i1-'i2+(1-j)<=i1-'i2+0 by REAL_1:55;
  then i1-'i2+1-'j<=i1-'i2 by A4,BINARITH:def 3;
  then A5:i1-'i2+1-'j+1<=i1-'i2+1 by AXIOMS:24;
    A6: i1-'i2+1=i1-i2+1 by A1,SCMFSA_7:3;
     i2-1>=0 by A1,SQUARE_1:12;
  then i1-(i2-1)<=i1-0 by REAL_2:106;
then A7: j<=i1 by A1,A6,AXIOMS:22;
  A8:(i1-'i2+1-'j+1)
  =i1-'i2+1-j+1 by A1,SCMFSA_7:3
  .=i1-i2+1-j+1 by A1,SCMFSA_7:3;
 A9:1<=(i1-'i2+1-'j+1) by NAT_1:29;
    now per cases by A1,REAL_1:def 5;
 case A10:i1>i2;
      len mid(f1,i2,i1)=i1-'i2+1 by A1,A2,A3,JORDAN3:27;
   then A11:mid(f1,i2,i1).(i1-'i2+1-'j+1)=f1.((i1-'i2+1-'j+1)+i2-'1)
                          by A1,A2,A3,A5,A9,JORDAN3:27;
   A12:(i1-'i2+1-'j+1)+i2-'1
    =(i1-'i2+1-'j+1)+i2-1 by A1,Th2
   .=i1-j+1 by A8
   .=i1-'j+1 by A7,SCMFSA_7:3;
      len mid(f1,i1,i2)=i1-'i2+1 by A1,A2,A3,A10,JORDAN3:27;
  hence mid(f1,i1,i2).j=mid(f1,i2,i1).(i1-i2+1-j+1)
  & i1-i2+1-j+1=i1-'i2+1-'j+1 by A1,A2,A3,A8,A10,A11,A12,JORDAN3:27;
 end;
 case A13:i1=i2;
then A14: i1-'i2+1=0+1 by GOBOARD9:1 .=1;
  then i1-i2+1-j+1=0+1-1+1 by A1,A13,AXIOMS:21 .=1;
  hence mid(f1,i1,i2).j=mid(f1,i2,i1).(i1-i2+1-j+1)
  & i1-i2+1-j+1=i1-'i2+1-'j+1 by A1,A8,A13,A14,AXIOMS:21;
 end;
 end;
 hence thesis;
end;

theorem  for f1,i1,i2 st 1<=i1 & i1<=i2 & i2<=len f1 & 1<=j & j<=i2-'i1+1
  holds mid(f1,i1,i2).j=mid(f1,i2,i1).(i2-i1+1-j+1)
    & i2-i1+1-j+1=i2-'i1+1-'j+1
proof let f1,i1,i2;assume A1:1<=i1 & i1<=i2 & i2<=len f1 & 1<=j & j<=i2-'i1+1;
  set k=i2-'i1+1-'j+1;
  A2:1<=k by NAT_1:29;
     j-1>=0 by A1,SQUARE_1:12;
then A3: k+0<=k+(j-1) by REAL_1:55;
     i2-'i1+1-j=i2-'i1+1-'j by A1,SCMFSA_7:3;
then A4: i2-'i1+1-'j+1+(j-1)=i2-'i1+1-(j-1)+(j-1) by XCMPLX_1:37
  .=i2-'i1+1;
  i2-i1+1-j+1=i2-'i1+1-j+1 by A1,SCMFSA_7:3
  .=i2-'i1+1-'j+1 by A1,SCMFSA_7:3;
  then i2-i1+1-k+1=i2-i1+1-((i2-i1+1)-(j-1))+1 by XCMPLX_1:37
  .=j;
 hence thesis by A1,A2,A3,A4,Th25;
end;

theorem Th27: for f1,k st 1<=k & k<=len f1
  holds mid(f1,k,k)=<*f1/.k*> & len mid(f1,k,k)=1
proof let f1,k;assume A1:1<=k & k<=len f1;
   A2:len (f1/^(k-'1))=len f1-'(k-'1) by JORDAN3:19;
      k-'1+1=k by A1,AMI_5:4;
   then A3:(f1/^(k-'1)).1=f1.k by A1,JORDAN3:23;
   A4:f1/.k=f1.k by A1,FINSEQ_4:24;
      k-'1+1<=len f1 by A1,AMI_5:4;
   then k-'1+1-(k-'1)<=len f1-(k-'1) by REAL_1:49;
   then A5:1<=1 & 1<=len (f1/^(k-'1)) by A2,JORDAN3:1;
  then 0<len (f1/^(k-'1));
  then A6:f1/^(k-'1) is non empty by FINSEQ_1:25;
     k-'k+1=k-k+1 by SCMFSA_7:3 .=1;
  then mid(f1,k,k)=(f1/^(k-'1))|1 by JORDAN3:def 1
  .=<*(f1/^(k-'1))/.1*> by A6,FINSEQ_5:23;
 hence thesis by A3,A4,A5,FINSEQ_1:56,FINSEQ_4:24;
end;

theorem Th28: mid(f1,0,0)=f1|1
proof
     0-'0+1=0-0+1 by SCMFSA_7:3 .=1;
  then A1:mid(f1,0,0)=(f1/^(0-'1))|1 by JORDAN3:def 1;  0-1<0;
  then 0-'1=0 by BINARITH:def 3;
 hence thesis by A1,FINSEQ_5:31;
end;

theorem Th29: for f1,k st len f1<k
  holds mid(f1,k,k)=<*>D
proof let f1,k;assume A1:len f1<k;
     0<=len f1 by NAT_1:18;
then A2: 0+1<=k by A1,NAT_1:38;
     k-'k+1=k-k+1 by SCMFSA_7:3 .=1;
  then A3:mid(f1,k,k)=(f1/^(k-'1))|1 by JORDAN3:def 1;
     len f1+1<=k by A1,NAT_1:38;
  then len f1+1-1<=k-1 by REAL_1:49;
  then len f1<=k-'1 by A2,SCMFSA_7:3;
  then f1/^(k-'1)=<*>D by FINSEQ_5:35;
 hence thesis by A3,Th16;
end;

theorem Th30: for f1,i1,i2 holds mid(f1,i1,i2)=Rev mid(f1,i2,i1)
proof let f1; let k1,k2 be Nat;
     now per cases;
  case A1:k1<=k2;
    then A2:mid(f1,k1,k2)=(f1/^(k1-'1))|(k2-'k1+1)
                           by JORDAN3:def 1;
       now per cases by A1,REAL_1:def 5;
    case k1<k2;
      then mid(f1,k2,k1)=Rev mid(f1,k1,k2) by A2,JORDAN3:def 1;
     hence mid(f1,k1,k2)=Rev mid(f1,k2,k1) by FINSEQ_6:29;
    end;
    case A3:k1=k2;
         k1=0 or k1>0 by NAT_1:19;
then A4:    k1=0 or 0+1<=k1 by NAT_1:38;
         now per cases by A4;
      case k1=0;
        then A5:mid(f1,k1,k2)=f1|1 by A3,Th28;
           now per cases;
        case len f1=0; then f1=<*>D by FINSEQ_1:32;
          then f1|1=<*>D by Th16;
         hence mid(f1,k1,k2)=Rev mid(f1,k2,k1) by A3,A5,Th17;
        end;
        case len f1<>0;
          then f1 <> <*>D by FINSEQ_1:32;
          then f1|1=<*f1/.1*> by FINSEQ_5:23;
         hence mid(f1,k1,k2)=Rev mid(f1,k2,k1)
            by A3,A5,FINSEQ_5:63;
        end;
        end;
       hence mid(f1,k1,k2)=Rev mid(f1,k2,k1);
      end;
      case 1<=k1 & k1<= len f1;
        then mid(f1,k1,k1)=<*f1/.k1*> by Th27;
       hence mid(f1,k1,k2)=Rev mid(f1,k2,k1) by A3,FINSEQ_5:63;
      end;
      case len f1<k1; then mid(f1,k1,k1)=<*>D by Th29;
       hence mid(f1,k1,k2)=Rev mid(f1,k2,k1) by A3,Th17;
      end;
      end;
     hence mid(f1,k1,k2)=Rev mid(f1,k2,k1);
    end;
    end;
   hence mid(f1,k1,k2)=Rev mid(f1,k2,k1);
  end;
  case A6:k1>k2;
    then mid(f1,k1,k2)=Rev ((f1/^(k2-'1))|(k1-'k2+1)) by JORDAN3:def 1;
   hence mid(f1,k1,k2)=Rev mid(f1,k2,k1) by A6,JORDAN3:def 1;
  end;
  end;
 hence thesis;
end;

theorem Th31: for f being FinSequence of TOP-REAL 2,i1,i2,i
  st 1<=i1 & i1<i2 & i2<=len f & 1<=i & i<i2-'i1+1
  holds LSeg(mid(f,i1,i2),i)=LSeg(f,i+i1-'1)
proof let f be FinSequence of TOP-REAL 2,i1,i2,i;
  assume A1: 1<=i1 & i1<i2 & i2<=len f & 1<=i & i<i2-'i1+1;
then A2: i<len mid(f,i1,i2) by Th20;
  then A3:i+1<=len mid(f,i1,i2) by NAT_1:38;
     i2-i1+1=i2-'i1+1 by A1,SCMFSA_7:3;
  then i+i1<i2-i1+1+i1 by A1,REAL_1:53;
  then i+i1+1-1<=i2+1-1 by NAT_1:38;
then A4:  i+i1-1+1<=len f by A1,AXIOMS:22;
     1+1<=i+i1 by A1,REAL_1:55;
  then 1+1-1<=i+i1-1 by REAL_1:49;
  then A5:1<=i+i1-'1 & i+i1-'1+1<=len f by A1,A4,Th2;
  then A6:LSeg(f,i+i1-'1)=LSeg(f/.(i+i1-'1),f/.(i+i1-'1+1)) by TOPREAL1:def 5;
  A7:1<=i & i+1<=len mid(f,i1,i2) by A1,A2,NAT_1:38;
  A8:1<i+i1-'1+1 by A5,NAT_1:38;
  A9:i+i1-'1<len f by A5,NAT_1:38;
  A10:i+i1-'1+1<=len f by A1,A4,Th2;
  A11:i+i1-'1+1=i+i1-1+1 by A1,Th2 .=i+1+i1-1 .=i+1+i1-'1 by A1,Th2;
  A12:mid(f,i1,i2)/.i=mid(f,i1,i2).i by A1,A2,FINSEQ_4:24
                   .=f.(i+i1-'1) by A1,JORDAN3:31
                   .=f/.(i+i1-'1) by A5,A9,FINSEQ_4:24;
  A13:i+1<=i2-'i1+1 by A1,NAT_1:38;
  A14:1<=1+i by NAT_1:29;
  then mid(f,i1,i2)/.(i+1)=mid(f,i1,i2).(i+1) by A7,FINSEQ_4:24
                   .=f.(i+1+i1-'1) by A1,A13,A14,JORDAN3:31
                   .=f/.(i+1+i1-'1) by A8,A10,A11,FINSEQ_4:24;
 hence thesis by A1,A3,A6,A11,A12,TOPREAL1:def 5;
end;

theorem Th32: for f being FinSequence of TOP-REAL 2,i1,i2,i
  st 1<=i1 & i1<i2 & i2<=len f & 1<=i & i<i2-'i1+1
  holds LSeg(mid(f,i2,i1),i)=LSeg(f,i2-'i)
proof let f be FinSequence of TOP-REAL 2,i1,i2,i;
  assume A1: 1<=i1 & i1<i2 & i2<=len f & 1<=i & i<i2-'i1+1;
   then A2:len mid(f,i2,i1)=i2-'i1+1 by Th21;
   A3:i<len mid(f,i2,i1) by A1,Th21;
   A4:i+(len mid(f,i2,i1)-'i)
   =len mid(f,i2,i1) by A1,A2,AMI_5:4;
      i+1<=len mid(f,i2,i1) by A3,NAT_1:38;
   then A5: i+1-i<=len mid(f,i2,i1)-i by REAL_1:49;
      len mid(f,i2,i1)+1<=len mid(f,i2,i1)+i by A1,AXIOMS:24;
   then len mid(f,i2,i1)<len mid(f,i2,i1)+i by NAT_1:38;
   then len mid(f,i2,i1)-i<len mid(f,i2,i1)+i-i by REAL_1:54;
   then A6:1<=len mid(f,i2,i1)-'i & len mid(f,i2,i1)-'i<i2-'i1+1
                            by A2,A5,JORDAN3:1;
      i2<=i2+(i1-'1) by NAT_1:29;
   then i2<=i2+(i1-1) by A1,SCMFSA_7:3;
   then i2-(i1-1)<=i2+(i1-1)-(i1-1) by REAL_1:49;
   then i2-i1+1<=i2;
   then i2-'i1+1<=i2 by A1,SCMFSA_7:3;
   then A7:i<i2 by A1,AXIOMS:22;
   A8:i2-'i1+1-'i+i1-'1
   =i2-'i1+1-'i+i1-1 by A1,Th2
   .=i2-'i1+1-i+i1-1 by A1,SCMFSA_7:3
   .=i2-'i1-i+i1
   .=i2-i1-i+i1 by A1,SCMFSA_7:3
   .=i2-i
   .=i2-'i by A7,SCMFSA_7:3;
   thus LSeg(mid(f,i2,i1),i)
   =LSeg(Rev mid(f,i2,i1),len mid(f,i2,i1)-'i) by A4,SPPOL_2:2
   .=LSeg(mid(f,i1,i2),len mid(f,i2,i1)-'i) by Th30
   .=LSeg(f,len mid(f,i2,i1)-'i+i1-'1) by A1,A6,Th31
   .=LSeg(f,i2-'i) by A1,A8,Th21;
end;

begin :: Dividing of special circular sequences into parts

definition let n be Nat, f be FinSequence;
 func S_Drop(n,f) -> Nat equals
 :Def1: (n mod (len f -'1)) if n mod (len f -'1)<>0
   otherwise len f -'1;
 correctness;
end;

theorem Th33: for f being FinSequence holds S_Drop(len f-'1,f)=len f-'1
proof let f be FinSequence;
     (len f-'1) mod (len f-'1)=0 by GR_CY_1:5;
 hence thesis by Def1;
end;

theorem Th34: for n being Nat, f being FinSequence
 st 1<=n & n<=len f-'1 holds S_Drop(n,f)=n
proof let n be Nat,f be FinSequence;
 assume A1: 1<=n & n<=len f-'1;
  then A2:n<>0;
  per cases by A1,REAL_1:def 5;
  suppose n<len f-'1;
    then n mod (len f-'1)=n by GR_CY_1:4;
   hence thesis by A2,Def1;
  end;
  suppose n=len f-'1;
   hence thesis by Th33;
end;
end;

theorem Th35: for n being Nat,f being FinSequence holds
 S_Drop(n,f)=S_Drop(n+(len f-'1),f) &
 S_Drop(n,f)=S_Drop(len f-'1+n,f)
proof let n be Nat,f be FinSequence;
   A1:(n+(len f-'1))mod (len f-'1)
    = (n+(len f-'1)*1)mod (len f-'1)
    .=n mod (len f-'1) by GR_CY_1:1;
  per cases;
  suppose A2:n mod (len f-'1)<>0;
    then S_Drop(n,f)=n mod (len f-'1) by Def1;
   hence thesis by A1,A2,Def1;
  end;
  suppose A3:n mod (len f-'1)=0;
    then S_Drop(n,f)=len f-'1 by Def1;
   hence thesis by A1,A3,Def1;
end;
end;

definition let f be non constant standard special_circular_sequence,
  g be FinSequence of TOP-REAL 2,i1,i2 be Nat;
 pred g is_a_part>_of f,i1,i2 means
 :Def2: 1<=i1 & i1+1<=len f &
  1<=i2 & i2+1<=len f & g.len g=f.i2 & 1<=len g & len g<len f &
  (for i being Nat st 1<=i & i<=len g holds g.i=f.S_Drop((i1+i)-'1,f));
end;

definition let f be non constant standard special_circular_sequence,
  g be FinSequence of TOP-REAL 2,i1,i2 be Nat;
 pred g is_a_part<_of f,i1,i2 means
 :Def3: 1<=i1 & i1+1<=len f &
  1<=i2 & i2+1<=len f & g.len g=f.i2 & 1<=len g & len g<len f &
  (for i being Nat st 1<=i & i<=len g holds g.i=f.S_Drop(len f +i1-'i,f));
end;

definition let f be non constant standard special_circular_sequence,
  g be FinSequence of TOP-REAL 2,i1,i2 be Nat;
 pred g is_a_part_of f,i1,i2 means
 :Def4: g is_a_part>_of f,i1,i2 or g is_a_part<_of f,i1,i2;
end;

theorem
     for f being non constant standard special_circular_sequence,
  g being FinSequence of TOP-REAL 2,i1,i2 being Nat st
  g is_a_part_of f,i1,i2 holds
  1<=i1 & i1+1<=len f &
  1<=i2 & i2+1<=len f & g.len g=f.i2 & 1<=len g & len g<len f &
  ((for i being Nat st 1<=i & i<=len g holds g.i=f.S_Drop((i1+i)-'1,f))or
  (for i being Nat st 1<=i & i<=len g holds g.i=f.S_Drop(len f +i1-'i,f)))
proof let f be non constant standard special_circular_sequence,
  g be FinSequence of TOP-REAL 2,i1,i2 be Nat;assume
A1:  g is_a_part_of f,i1,i2;
     now per cases by A1,Def4;
  case g is_a_part>_of f,i1,i2;
  hence
     1<=i1 & i1+1<=len f &
  1<=i2 & i2+1<=len f & g.len g=f.i2 & 1<=len g & len g<len f &
  ((for i being Nat st 1<=i & i<=len g holds g.i=f.S_Drop((i1+i)-'1,f))or
  (for i being Nat st 1<=i & i<=len g holds g.i=f.S_Drop(len f +i1-'i,f)))
   by Def2;
  end;
  case g is_a_part<_of f,i1,i2;
  hence
     1<=i1 & i1+1<=len f &
  1<=i2 & i2+1<=len f & g.len g=f.i2 & 1<=len g & len g<len f &
  ((for i being Nat st 1<=i & i<=len g holds g.i=f.S_Drop((i1+i)-'1,f))or
  (for i being Nat st 1<=i & i<=len g holds g.i=f.S_Drop(len f +i1-'i,f)))
   by Def3;
  end;
  end;
  hence thesis;
end;

theorem Th37:for f being non constant standard special_circular_sequence,
  g being FinSequence of TOP-REAL 2,i1,i2 being Nat st
  g is_a_part>_of f,i1,i2 & i1<=i2 holds len g=i2-'i1+1 & g=mid(f,i1,i2)
proof let f be non constant standard special_circular_sequence,
  g be FinSequence of TOP-REAL 2,i1,i2 be Nat;
  assume A1:g is_a_part>_of f,i1,i2 & i1<=i2;
   then A2:1<=i1 & i1+1<=len f &
   1<=i2 & i2+1<=len f & g.len g=f.i2 & 1<=len g & len g<len f &
   (for i being Nat st 1<=i & i<=len g holds g.i=f.S_Drop((i1+i)-'1,f))
                           by Def2;
   then A3: i1+1-1<=len f-1 by REAL_1:49;
   then 1<=len f-1 by A2,AXIOMS:22;
   then A4:len f-1=len f-'1 by JORDAN3:1;
   then A5:0<>len f-'1 by A2,A3;
      i1<=i1+1 by NAT_1:29; then A6:i1<=len f by A2,AXIOMS:22;
      i2<=i2+1 by NAT_1:29; then A7:i2<=len f by A2,AXIOMS:22;
     A8:f.i2=f.S_Drop((i1+len g)-'1,f) by A2;
A9:   now per cases;
      case (i1+len g)-'1 mod (len f -'1)<>0;
        then 0<(i1+len g)-'1 mod (len f -'1) by NAT_1:19;
        then 0+1<=(i1+len g)-'1 mod (len f -'1) by NAT_1:38;
       hence 1<=S_Drop((i1+len g)-'1,f) by Def1;
      end;
      case (i1+len g)-'1 mod (len f -'1)=0;
        then A10:S_Drop((i1+len g)-'1,f)=len f -'1 by Def1;
           i1<=len f -'1 by A2,JORDAN3:12;
       hence 1<=S_Drop((i1+len g)-'1,f) by A2,A10,AXIOMS:22;
      end;
      end;
A11:   now per cases;
      case (i1+len g)-'1 mod (len f -'1)<>0;
        then A12:S_Drop((i1+len g)-'1,f)=((i1+len g)-'1 mod (len f -'1)) by
Def1;
         A13: i1<=len f -'1 by A2,JORDAN3:12;
        then A14:1<=len f -'1 by A2,AXIOMS:22;
          0<len f -'1 by A2,A13;
        then A15:((i1+len g)-'1 mod (len f -'1)) <len f-'1 by NAT_1:46;
           len f -'1<len f by A14,JORDAN3:14;
       hence S_Drop((i1+len g)-'1,f)< len f by A12,A15,AXIOMS:22;
      end;
      case (i1+len g)-'1 mod (len f -'1)=0;
        then A16:S_Drop((i1+len g)-'1,f)=len f -'1 by Def1;
           i1<=len f -'1 by A2,JORDAN3:12;
        then 1<=len f -'1 by A2,AXIOMS:22;
       hence S_Drop((i1+len g)-'1,f)< len f by A16,JORDAN3:14;
      end;
      end;
     A17:i2<=len f -'1 by A2,JORDAN3:12;
     then 1<=len f -'1 by A2,AXIOMS:22;
     then len f -'1<len f by JORDAN3:14;
     then A18:i2<len f by A17,AXIOMS:22;
        now per cases by REAL_1:def 5;
     case A19:i2<S_Drop((i1+len g)-'1,f);
       then A20:f/.i2<>f/.(S_Drop((i1+len g)-'1,f)) by A2,A11,GOBOARD7:38;
          i2<=len f by A2,JORDAN3:9;
       then A21:f/.i2=f.i2 by A2,FINSEQ_4:24;
          1<=S_Drop((i1+len g)-'1,f) by A2,A19,AXIOMS:22;
      hence contradiction by A8,A11,A20,A21,FINSEQ_4:24;
     end;
     case i2>S_Drop((i1+len g)-'1,f);
       then A22:f/.i2<>f/.(S_Drop((i1+len g)-'1,f)) by A9,A18,GOBOARD7:38;
          i2<=len f by A2,JORDAN3:9;
       then f/.i2=f.i2 by A2,FINSEQ_4:24;
      hence contradiction by A8,A9,A11,A22,FINSEQ_4:24;
     end;
     case A23:i2=S_Drop((i1+len g)-'1,f);
         now per cases;
      case A24: (i1+len g)-'1 mod (len f -'1)<>0;
           (ex n being Nat st i1+len g -'1
             =(len f-'1)*n+((i1+len g-'1) mod (len f -'1)) &
             ((i1+len g-'1) mod (len f -'1))<len f -'1) by A5,NAT_1:def 2;
               then consider n being Nat such that A25: i1+len g -'1
             =(len f-'1)*n+((i1+len g)-'1 mod (len f -'1));
        A26:i1+len g -'1=(len f-'1)*n+i2 by A23,A24,A25,Def1;
           now per cases;
        case n=0;
          then A27:i2=i1+len g -'1 by A23,A24,A25,Def1;
             1<=i1+len g by A2,NAT_1:37;
          then A28: i1+len g-'1=i1+len g-1 by SCMFSA_7:3;
then A29:        i2+1-i1 = len g by A27;
A30:       i2-i1+1=len g by A27,A28;
          then len g=i2-'i1+1 by A1,SCMFSA_7:3;
          then A31:len g = len g & len mid(f,i1,i2) = len g
              by A1,A2,A6,A7,JORDAN3:27;
             for j st j in Seg len g holds g.j = mid(f,i1,i2).j
          proof let j;assume j in Seg len g;
            then A32:1<=j & j<=len g by FINSEQ_1:3;
            then A33:g.j=f.S_Drop((i1+j)-'1,f) by A1,Def2;
            A34:i1+j-'1=i1+j-1 by A32,Th2;
               i1+j>=1+1 by A2,A32,REAL_1:55;
            then i1+j-1>=1+1-1 by REAL_1:49;
            then A35:i1+j-'1<>0 by A34;
                j+i1<=i2-i1+1+i1 by A27,A28,A32,AXIOMS:24;
             then A36:i1+j<=len f by A2,AXIOMS:22;
                now per cases;
             case A37:i1+j=len f;
               then (i1+j-'1) mod (len f-'1)=0 by GR_CY_1:5;
              hence S_Drop(i1+j-'1,f)=i1+j-'1 by A37,Def1;
             end;
             case i1+j<>len f; then i1+j<len f by A36,REAL_1:def 5;
               then i1+j-1<len f-1 by REAL_1:54;
               then i1+j-'1<len f-'1 by A2,A4,Th2;
               then (i1+j-'1)mod (len f-'1)=i1+j-'1 by GR_CY_1:4;
             hence S_Drop((i1+j)-'1,f)=i1+j-'1 by A35,Def1;
             end;
             end;
           hence g.j = mid(f,i1,i2).j
             by A1,A2,A7,A29,A32,A33,JORDAN3:31;
          end;
         hence len g=i2-'i1+1 & g=mid(f,i1,i2)
            by A1,A30,A31,FINSEQ_2:10,SCMFSA_7:3;
        end;
        case n<>0; then 0<n by NAT_1:19; then A38: 0+1<=n by NAT_1:38;
             0<=len f-'1 by NAT_1:18;
          then A39: (len f-'1)*n>=(len f-'1)*1 by A38,AXIOMS:25;
             len g+1<=len f by A2,NAT_1:38;
          then A40: len g+1-1<=len f-1 by REAL_1:49;
             0<=len g by NAT_1:18;
          then len g<=len f-'1 by A40,BINARITH:def 3;
          then len g+i1<=len f-'1+i2 by A1,REAL_1:55;
          then i1+len g<len f-'1+i2+1 by NAT_1:38;
          then A41: i1+len g-1<len f-'1+i2+1-1 by REAL_1:92;
             i1<=i1+len g by NAT_1:29;
          then 1<=i1+len g by A2,AXIOMS:22;
          then i1+len g-1=i1+len g-'1 by SCMFSA_7:3;
          then i1+len g-'1-i2<len f-'1+i2-i2 by A41,REAL_1:92;
         hence contradiction by A26,A39;
        end;
        end;
       hence len g=i2-'i1+1 & g=mid(f,i1,i2);
      end;
      case A42:(i1+len g)-'1 mod (len f -'1)=0;
           (ex n being Nat st i1+len g -'1
             =(len f-'1)*n+((i1+len g-'1) mod (len f -'1)) &
             ((i1+len g-'1) mod (len f -'1))<len f -'1) by A5,NAT_1:def 2;
               then consider n being Nat such that A43: i1+len g -'1
             =(len f-'1)*n+((i1+len g-'1) mod (len f -'1));
        A44:i1+len g -'1=i2*n by A23,A42,A43,Def1;
           now assume n=0;
          then A45: i1+len g<=1 by A42,A43,Th1;
             i1+len g>=1+1 by A2,REAL_1:55;
         hence contradiction by A45,AXIOMS:22;
        end;
        then n>0 by NAT_1:19;
then A46:     n>=0+1 by NAT_1:38;
           now assume n>1; then n>=1+1 by NAT_1:38;
         then A47:i1+len g -'1>=(len f-'1)*(1+1) by A42,A43,NAT_1:20;
            len g -1<len f-1 by A2,REAL_1:54;
         then i1+(len g -1)<len f-'1+(len f-'1) by A3,A4,REAL_1:67;
         then A48:i1+len g -1<len f-'1+(len f-'1);
            1<=len g+i1 by A2,NAT_1:37;
        hence contradiction by A47,A48,SCMFSA_7:3;
        end;
      then n=1 by A46,AXIOMS:21;
        then A49: i1+len g-1=i2 by A2,A44,JORDAN3:1;
        then A50:len g-1=i2-i1;
A51:     i2-i1=i2-'i1 by A1,SCMFSA_7:3;
        then A52:len g=i2-'i1+1 by A50;
        then A53:len g = len g & len mid(f,i1,i2) = len g
           by A1,A2,A6,A7,JORDAN3:27;
           for j st j in Seg len g holds g.j = mid(f,i1,i2).j
          proof let j;assume j in Seg len g;
            then A54:1<=j & j<=len g by FINSEQ_1:3;
            then A55:g.j=f.S_Drop((i1+j)-'1,f) by A1,Def2;
            A56:i1+j-'1=i1+j-1 by A54,Th2;
               i1+j>=1+1 by A2,A54,REAL_1:55;
            then i1+j-1>=1+1-1 by REAL_1:49;
            then A57:i1+j-'1<>0 by A56;
              j+i1<=i2-i1+1+i1 by A49,A54,AXIOMS:24;
             then A58:i1+j<=len f by A2,AXIOMS:22;
                now per cases;
             case A59:i1+j=len f;
               then (i1+j-'1) mod (len f-'1)=0 by GR_CY_1:5;
              hence S_Drop(i1+j-'1,f)=i1+j-'1 by A59,Def1;

             end;
             case i1+j<>len f; then i1+j<len f by A58,REAL_1:def 5;
               then i1+j-1<len f-1 by REAL_1:54;
               then A60:i1+j-'1<len f-'1 by A2,A4,Th2;
              then (i1+j-'1) mod (len f-'1)<>0 by A57,GR_CY_1:4;
              then S_Drop((i1+j)-'1,f)=(i1+j-'1) mod (len f-'1) by Def1;
              hence S_Drop((i1+j)-'1,f)=i1+j-'1 by A60,GR_CY_1:4;
             end;
             end;
           hence g.j = mid(f,i1,i2).j by A1,A2,A7,A52,A54,A55,JORDAN3:31;
        end;
       hence len g=i2-'i1+1 & g=mid(f,i1,i2)
          by A50,A51,A53,FINSEQ_2:10,XCMPLX_1:27;
      end;
      end;
      hence len g=i2-'i1+1 & g=mid(f,i1,i2);
     end;
     end;
    hence thesis;
end;

theorem Th38:for f being non constant standard special_circular_sequence,
  g being FinSequence of TOP-REAL 2,i1,i2 being Nat st
  g is_a_part>_of f,i1,i2 & i1>i2 holds len g=len f+i2-'i1
    & g=mid(f,i1,len f-'1)^(f|i2)
    & g=mid(f,i1,len f-'1)^mid(f,1,i2)
proof let f be non constant standard special_circular_sequence,
  g be FinSequence of TOP-REAL 2,i1,i2 be Nat;
  assume A1:g is_a_part>_of f,i1,i2 & i1>i2;
   then A2:1<=i1 & i1+1<=len f &
   1<=i2 & i2+1<=len f & g.len g=f.i2 & 1<=len g & len g<len f &
   (for i being Nat st 1<=i & i<=len g holds g.i=f.S_Drop((i1+i)-'1,f))
                           by Def2;
A3:i1<=i1+1 by NAT_1:37;
   then A4:i1<=len f by A2,AXIOMS:22;
     A5: i1+1-1<=len f-1 by A2,REAL_1:49;
   then A6:1<=len f-1 by A2,AXIOMS:22;
   then A7:len f-1=len f-'1 by JORDAN3:1;
   then A8:0<len f-'1 by A2,A5;
   A9:0<>len f-'1 by A2,A5,A7;
       i1<=len f -'1 by A2,JORDAN3:12;
    then 1<=len f -'1 by A2,AXIOMS:22;
   then A10:len f -'1<len f by JORDAN3:14;
 i2<=i2+1 by NAT_1:29;
   then A11:i2<=len f by A2,AXIOMS:22;
     A12:f.i2=f.S_Drop((i1+len g)-'1,f) by A2;
A13:  now per cases;
      case (i1+len g)-'1 mod (len f -'1)<>0;
        then 0<(i1+len g)-'1 mod (len f -'1) by NAT_1:19;
        then 0+1<=(i1+len g)-'1 mod (len f -'1) by NAT_1:38;
       hence 1<=S_Drop((i1+len g)-'1,f) by Def1;
      end;
      case (i1+len g)-'1 mod (len f -'1)=0;
        then A14:S_Drop((i1+len g)-'1,f)=len f -'1 by Def1;
           i1<=len f -'1 by A2,JORDAN3:12;
       hence 1<=S_Drop((i1+len g)-'1,f) by A2,A14,AXIOMS:22;
      end;
      end;
A15:  now per cases;
      case (i1+len g)-'1 mod (len f -'1)<>0;
        then A16:S_Drop((i1+len g)-'1,f)=((i1+len g)-'1 mod (len f -'1)) by
Def1;
         A17: i1<=len f -'1 by A2,JORDAN3:12;
        then A18:1<=len f -'1 by A2,AXIOMS:22;
          0<len f -'1 by A2,A17;
        then A19:((i1+len g)-'1 mod (len f -'1)) <len f-'1 by NAT_1:46;
           len f -'1<len f by A18,JORDAN3:14;
       hence S_Drop((i1+len g)-'1,f)< len f by A16,A19,AXIOMS:22;
      end;
      case (i1+len g)-'1 mod (len f -'1)=0;
        then A20:S_Drop((i1+len g)-'1,f)=len f -'1 by Def1;
           i1<=len f -'1 by A2,JORDAN3:12;
        then 1<=len f -'1 by A2,AXIOMS:22;
       hence S_Drop((i1+len g)-'1,f)< len f by A20,JORDAN3:14;
      end;
      end;
     A21:i2<=len f -'1 by A2,JORDAN3:12;
     then A22:1<=len f -'1 by A2,AXIOMS:22;
     then len f -'1<len f by JORDAN3:14;
     then A23:i2<len f by A21,AXIOMS:22;
        now per cases by REAL_1:def 5;
     case A24:i2<S_Drop((i1+len g)-'1,f);
       then A25:f/.i2<>f/.(S_Drop((i1+len g)-'1,f)) by A2,A15,GOBOARD7:38;
          i2<=len f by A2,JORDAN3:9;
       then A26:f/.i2=f.i2 by A2,FINSEQ_4:24;
          1<=S_Drop((i1+len g)-'1,f) by A2,A24,AXIOMS:22;
      hence contradiction by A12,A15,A25,A26,FINSEQ_4:24;
     end;
     case i2>S_Drop((i1+len g)-'1,f);
       then A27:f/.i2<>f/.(S_Drop((i1+len g)-'1,f)) by A13,A23,GOBOARD7:38;
          i2<=len f by A2,JORDAN3:9;
       then f/.i2=f.i2 by A2,FINSEQ_4:24;
      hence contradiction by A12,A13,A15,A27,FINSEQ_4:24;
     end;
     case A28:i2=S_Drop((i1+len g)-'1,f);
         now per cases;
      case A29: (i1+len g)-'1 mod (len f -'1)<>0;
           (ex n being Nat st i1+len g -'1
             =(len f-'1)*n+((i1+len g-'1) mod (len f -'1)) &
             ((i1+len g-'1) mod (len f -'1))<len f -'1)or
             ((i1+len g-'1) mod (len f -'1))=0 & len f-'1=0
             by NAT_1:def 2;
        then consider n being Nat such that A30: i1+len g -'1
             =(len f-'1)*n+((i1+len g)-'1 mod (len f -'1))
               by A2,A5,A7;
        A31:i1+len g -'1=(len f-'1)*n+i2 by A28,A29,A30,Def1;
           now per cases;
        case n=0;
          then i2=i1+len g -'1 by A28,A29,A30,Def1;
          then A32:i2+1=i1+len g -1+1 by A2,JORDAN3:1
             .=i1+len g;
          A33:i2+1<=i1 by A1,NAT_1:38;
             0<len g by A2;
          then i2+1+0<i1+len g by A33,REAL_1:67;
         hence contradiction by A32;
        end;
        case n<>0; then 0<n by NAT_1:19;
then A34:        0+1<=n by NAT_1:38;
             now per cases by A34,REAL_1:def 5;
          case A35:1=n;
               len f-'1=len f-1 by A22,JORDAN3:1;
            then i1+len g-1=len f-1+i2 by A2,A31,A35,Th2;
            then i1+len g=len f-1+i2+1 by XCMPLX_1:27;
            then A36:len g=len f+i2-i1 by XCMPLX_1:26;
            A37:len (mid(f,i1,len f-'1)^(f|i2))
                       = len (mid(f,i1,len f-'1))+ len(f|i2)
                                   by FINSEQ_1:35;
A38:         1<=i1 & i1<=len f & 1<=len f-'1 & len f-'1<=len f & i1<=len f-'1
                 by A2,A3,A5,A6,AXIOMS:22,JORDAN3:1,13;
            then A39:len (mid(f,i1,len f-'1))=len f-'1-'i1+1 by JORDAN3:27;
            A40:len f-'1-'i1=len f-'1-i1 by A5,A7,SCMFSA_7:3;
            A41:len f-'1-'i1+1 =len f-1-i1+1 by A5,A7,SCMFSA_7:3
            .=len f-i1;
            then len (mid(f,i1,len f-'1)^(f|i2))=len f-i1+i2
                 by A11,A37,A39,FINSEQ_1:80
                                           .=len f+i2-i1
                                           .=len f+i2-'i1 by A4,Th2;
            then A42:len g = len g & len (mid(f,i1,len f-'1)^(f|i2))=len g
               by A4,A36,Th2;
               for j st j in Seg len g holds g.j =(mid(f,i1,len f-'1)^(f|i2)).j
            proof let j;assume A43: j in Seg len g;
              then A44:1<=j & j<=len g by FINSEQ_1:3;
              then A45:g.j=f.S_Drop((i1+j)-'1,f) by A1,Def2;
              A46:i1+j-'1=i1+j-1 by A44,Th2;
                 i1+j>=1+1 by A2,A44,REAL_1:55;
              then i1+j-1>=1+1-1 by REAL_1:49;
              then A47:i1+j-'1<>0 by A46;
                 now per cases;
              case A48:j<=len mid(f,i1,len f-'1);
                then j in dom mid(f,i1,len f-'1) by A44,FINSEQ_3:27;
                then A49:(mid(f,i1,len f-'1)^(f|i2)).j =mid(f,i1,len f-'1).j
                                          by FINSEQ_1:def 7;
                A50:1<=i1 & i1<=len f-'1 & len f-'1<=len f & 1<=j & j<=
len f-'1-i1+1
                     by A38,A40,A43,A48,FINSEQ_1:3,JORDAN3:27;
                   j+i1<=len f-'1-i1+1+i1
                   by A39,A40,A48,AXIOMS:24;
                then A51: i1+j<=len f-1+1 by A7;
                  now per cases;
               case A52:i1+j=len f;
                 then (i1+j-'1) mod (len f-'1)=0 by GR_CY_1:5;
                hence S_Drop(i1+j-'1,f)=i1+j-'1 by A52,Def1;
               end;
               case i1+j<>len f; then i1+j<len f by A51,REAL_1:def 5;
                 then i1+j-1<len f-1 by REAL_1:54;
                 then A53:i1+j-'1<len f-'1 by A2,A7,Th2;
                then (i1+j-'1) mod (len f-'1)<>0 by A47,GR_CY_1:4;
                then S_Drop((i1+j)-'1,f)=(i1+j-'1) mod (len f-'1) by Def1;
                hence S_Drop((i1+j)-'1,f)=i1+j-'1 by A53,GR_CY_1:4;
               end;
               end;
               hence g.j = (mid(f,i1,len f-'1)^(f|i2)).j
                 by A45,A49,A50,JORDAN3:31;
              end;
              case A54:j>len mid(f,i1,len f-'1);
                then j+i1>len f-i1+i1 by A39,A41,REAL_1:53;
                then A55: i1+j-len f>len f-len f by REAL_1:54;
                then A56:i1+j-'len f=i1+j-len f by BINARITH:def 3;
                A57:(mid(f,i1,len f-'1)^(f|i2)).j
                      =(f|i2).(j-len mid(f,i1,len f-'1))
                                  by A42,A44,A54,JORDAN3:15;
                A58:j-len mid(f,i1,len f-'1)=j-(len f-i1) by A38,A41,JORDAN3:27
                .=j+i1-len f;
                   j<=len f+i2-i1 by A36,A43,FINSEQ_1:3;
                then j+i1<=len f+i2-i1+i1 by AXIOMS:24;
                then j+i1-len f<=len f+i2-len f by REAL_1:49;
                then A59:(f|i2).(j-len mid(f,i1,len f-'1))
                =f.(i1+j-'len f) by A56,A58,JORDAN3:20;
                   len mid(f,i1,len f-'1)+1<=j by A54,NAT_1:38;
                then A60: len mid(f,i1,len f-'1)+1-len mid(f,i1,len f-'1)
                     <=j -len mid(f,i1,len f-'1) by REAL_1:49;
                  A61:i1+j-1=i1+j-'1 by A2,Th2;
                     1+len f<=i1+j-len f+len f by A39,A41,A60,AXIOMS:24;
                  then 1+len f -1<=i1+j-1 by REAL_1:49;
                  then A62:len f-'1<i1+j-'1 by A10,A61,AXIOMS:22;
                    A63: i1+1-1<=len f-1 by A2,REAL_1:49;
                     j<len f by A2,A44,AXIOMS:22;
                  then j<=len f-1 by SPPOL_1:6;
                  then A64:i1+j<=(len f-'1)+(len f-'1) by A7,A63,REAL_1:55;
                     i1+j<i1+j+1 by NAT_1:38;
                  then i1+j-1<i1+j+1-1 by REAL_1:54;
                  then A65:i1+j-'1<(len f-'1)+(len f-'1) by A61,A64,AXIOMS:22;
                   now per cases;
                case (i1+j-'1) mod (len f -'1)=0;
                 hence contradiction by A8,A62,A65,Th6;
                end;
                case (i1+j-'1) mod (len f -'1)<>0;
                 then A66:S_Drop(i1+j-'1,f)= (i1+j-'1) mod (len f -'1) by Def1;
                     i1+j-'len f+(len f-'1)=i1+j-'len f+(len f-1)
                    by A6,JORDAN3:1
                  .=i1+j-len f+(len f-1) by A55,BINARITH:def 3
                  .=i1+j-1
                  .=i1+j-'1 by A44,Th2;
                  then A67:(i1+j-'1) mod (len f-'1)
                   =(i1+j-'len f+((len f-'1)mod (len f-'1)))
                      mod (len f-'1) by GR_CY_1:3
                  .=(i1+j-'len f+0)
                      mod (len f-'1) by GR_CY_1:5
                  .=(i1+j-'len f) mod (len f-'1);
                     i1+j-(len f-'1)<=(len f-'1)+(len f-'1)-(len f-'1)
                        by A64,REAL_1:49;
                  then i1+j-(len f-1)<=(len f-'1) by A6,JORDAN3:1;
                  then i1+j-'len f+1<=(len f-'1) by A56;
                  then i1+j-'len f<(len f-'1) by NAT_1:38;
                 hence g.j = (mid(f,i1,len f-'1)^(f|i2)).j
                   by A45,A57,A59,A66,A67,GR_CY_1:4;
                end;
                end;
               hence g.j = (mid(f,i1,len f-'1)^(f|i2)).j;
              end;
              end;
             hence g.j = (mid(f,i1,len f-'1)^(f|i2)).j;
            end;
           hence len g=len f+i2-'i1 & g=mid(f,i1,len f-'1)^(f|i2)
                                           by A4,A36,A42,Th2,FINSEQ_2:10;
           end;
           case 1<n;
            then 1+1<=n by NAT_1:38;
            then (len f-'1)*n>=(len f-'1)*(1+1) by A8,AXIOMS:25;
            then A68:i1+len g-'1>=(len f-'1)*(1+1)+i2 by A31,AXIOMS:24;
              A69: i1+1-1<=len f-1 by A2,REAL_1:49;
               len g-1<len f-'1 by A2,A7,REAL_1:54;
            then A70: i1+(len g-1)<len f-'1+(len f-'1) by A7,A69,REAL_1:67;
            A71:i1+len g-1=i1+len g-'1 by A2,Th2;
               (len f-'1)*(1+1)<=(len f-'1)*(1+1)+i2 by NAT_1:29;
           hence contradiction by A68,A70,A71,AXIOMS:22;
           end;
           end;
         hence len g=len f+i2-'i1 & g=mid(f,i1,len f-'1)^(f|i2);
        end;
        end;
       hence len g=len f+i2-'i1 & g=mid(f,i1,len f-'1)^(f|i2);
      end;
      case A72:(i1+len g)-'1 mod (len f -'1)=0;
           (ex n being Nat st i1+len g -'1
             =(len f-'1)*n+((i1+len g-'1) mod (len f -'1)) &
             ((i1+len g-'1) mod (len f -'1))<len f -'1) by A9,NAT_1:def 2;
        then consider n being Nat such that A73: i1+len g -'1
             =(len f-'1)*n+((i1+len g-'1) mod (len f -'1));
           now assume n=0;
          then A74: i1+len g<=1 by A72,A73,Th1;
             i1+len g>=1+1 by A2,REAL_1:55;
         hence contradiction by A74,AXIOMS:22;
        end;
        then n>0 by NAT_1:19;
then A75:    n>=0+1 by NAT_1:38;
           now assume n>1; then n>=1+1 by NAT_1:38;
         then A76:i1+len g -'1>=(len f-'1)*(1+1) by A72,A73,NAT_1:20;
            len g -1<len f-1 by A2,REAL_1:54;
         then i1+(len g -1)<len f-'1+(len f-'1) by A5,A7,REAL_1:67;
         then A77:i1+len g -1<len f-'1+(len f-'1);
            1<=len g+i1 by A2,NAT_1:37;
        hence contradiction by A76,A77,SCMFSA_7:3;
        end;
        then n=1 by A75,AXIOMS:21;
        then i1+len g-'1=i2 by A28,A72,A73,Def1;
        then A78: i1+len g-1=i2 by A2,JORDAN3:1;
A79:    1-1<=len g-1 by A2,REAL_1:49;
           i2-i2<i1-i2 by A1,REAL_1:54;
        then 0<i1-i2;
        then -0>-(i1-i2) by REAL_1:50;
       hence contradiction by A78,A79;
      end;
      end;
      hence len g=len f+i2-'i1 & g=mid(f,i1,len f-'1)^(f|i2);
     end;
     end;
    hence thesis by A2,JORDAN3:25;
end;

theorem Th39:for f being non constant standard special_circular_sequence,
  g being FinSequence of TOP-REAL 2,i1,i2 being Nat st
  g is_a_part<_of f,i1,i2 & i1>=i2 holds len g=i1-'i2+1 & g=mid(f,i1,i2)
proof let f be non constant standard special_circular_sequence,
  g be FinSequence of TOP-REAL 2,i1,i2 be Nat;
  assume A1:g is_a_part<_of f,i1,i2 & i1>=i2;
   then A2:1<=i1 & i1+1<=len f &
   1<=i2 & i2+1<=len f & g.len g=f.i2 & 1<=len g & len g<len f &
   (for i being Nat st 1<=i & i<=len g holds g.i=f.S_Drop((len f+i1-'i),f))
                           by Def3;
   then A3: i2+1-1<=len f-1 by REAL_1:49;
   then A4:1<=len f-1 by A2,AXIOMS:22;
   then A5:len f-1=len f-'1 by JORDAN3:1;
   then A6:0<len f-'1 by A2,A3;
   A7:0<>len f-'1 by A2,A3,A5;
      i1<=i1+1 by NAT_1:29; then A8:i1<=len f by A2,AXIOMS:22;
     A9:f.i2=f.S_Drop((len f+i1-'len g),f) by A2;
A10:  now per cases;
      case (len f+i1-'len g) mod (len f -'1)<>0;
        then 0<(len f+i1-'len g) mod (len f -'1) by NAT_1:19;
        then 0+1<=(len f+i1-'len g) mod (len f -'1) by NAT_1:38;
       hence 1<=S_Drop((len f+i1-'len g),f) by Def1;
      end;
      case (len f+i1-'len g) mod (len f -'1)=0;
        then A11:S_Drop((len f+i1-'len g),f)=len f -'1 by Def1;
           i2<=len f -'1 by A2,JORDAN3:12;
       hence 1<=S_Drop((len f+i1-'len g),f) by A2,A11,AXIOMS:22;
      end;
      end;
A12:  now per cases;
      case (len f+i1-'len g) mod (len f -'1)<>0;
        then A13:S_Drop((len f+i1-'len g),f)
        =((len f+i1-'len g) mod (len f -'1)) by Def1;
         A14: i2<=len f -'1 by A2,JORDAN3:12;
        then A15:1<=len f -'1 by A2,AXIOMS:22;
          0<len f -'1 by A2,A14;
        then A16:((len f+i1-'len g) mod (len f -'1)) <len f-'1 by NAT_1:46;
           len f -'1<len f by A15,JORDAN3:14;
       hence S_Drop((len f+i1-'len g),f)< len f by A13,A16,AXIOMS:22;
      end;
      case (len f+i1-'len g) mod (len f -'1)=0;
        then A17:S_Drop((len f+i1-'len g),f)=len f -'1 by Def1;
           i2<=len f -'1 by A2,JORDAN3:12;
        then 1<=len f -'1 by A2,AXIOMS:22;
       hence S_Drop((len f+i1-'len g),f)< len f by A17,JORDAN3:14;
      end;
      end;
     A18:i2<=len f -'1 by A2,JORDAN3:12;
     then 1<=len f -'1 by A2,AXIOMS:22;
     then len f -'1<len f by JORDAN3:14;
     then A19:i2<len f by A18,AXIOMS:22;
        now per cases by REAL_1:def 5;
     case A20:i2<S_Drop((len f+i1-'len g),f);
       then A21:f/.i2<>f/.(S_Drop((len f+i1-'len g),f)) by A2,A12,GOBOARD7:38;
          i2<=len f by A2,NAT_1:38;
       then A22:f/.i2=f.i2 by A2,FINSEQ_4:24;
          1<=S_Drop((len f+i1-'len g),f) by A2,A20,AXIOMS:22;
      hence contradiction by A9,A12,A21,A22,FINSEQ_4:24;
     end;
     case i2>S_Drop((len f+i1-'len g),f);
       then A23:f/.i2<>f/.(S_Drop((len f+i1-'len g),f)) by A10,A19,GOBOARD7:38;
          i2<=len f by A2,NAT_1:38;
       then f/.i2=f.i2 by A2,FINSEQ_4:24;
      hence contradiction by A9,A10,A12,A23,FINSEQ_4:24;
     end;
     case A24:i2=S_Drop((len f+i1-'len g),f);
         now per cases;
      case A25: (len f+i1-'len g) mod (len f -'1)<>0;
           (ex n being Nat st len f+i1-'len g
             =(len f-'1)*n+((len f+i1-'len g) mod (len f -'1)) &
             ((len f+i1-'len g) mod (len f -'1))<len f -'1)
               by A7,NAT_1:def 2;
        then consider n being Nat such that A26: len f+i1-'len g
             =(len f-'1)*n+((len f+i1-'len g) mod (len f -'1));
        A27:len f+i1-'len g=(len f-'1)*n+i2 by A24,A25,A26,Def1;
           n>=0 by NAT_1:18;
then A28:      n=0 or n>=0+1 by NAT_1:38;
           now per cases by A28,REAL_1:def 5;
        case n=0;
          then i2=len f+i1-'len g by A24,A25,A26,Def1;
          then i2=len f+i1-len g by A2,Th2;
          then i2+len g=i1+len f;
         hence contradiction by A1,A2,REAL_1:67;
        end;
        case n=1;
          then A29:len f-1+i2=len f+i1-'len g by A5,A24,A25,A26,Def1;
             len f+i1-'len g=len f+i1-len g by A2,Th2;
          then len f-(1-i2)=len f+i1-len g by A29;
          then len f-(1-i2)-len f=(i1-len g);
          then len f+-(1-i2)-len f=(i1-len g);
          then -(1-i2)=(i1-len g) by XCMPLX_1:26;
          then -(1-i2)+len g=i1;
then A30:       len g=i1-i2+1;
          then A31:len g=i1-'i2+1 by A1,SCMFSA_7:3;
          then A32:len g = len g & len mid(f,i1,i2) = len g by A1,A2,A8,Th21;
             for j st j in Seg len g holds g.j =mid(f,i1,i2).j
          proof let j;assume j in Seg len g;
            then A33:1<=j & j<=len g by FINSEQ_1:3;
            then A34:g.j=f.S_Drop(len f+i1-'j,f) by A1,Def3;
            A35:j<=i1-'i2+1 by A1,A30,A33,SCMFSA_7:3;
            A36:i1-'i2+1=i1-i2+1 by A1,SCMFSA_7:3
              .=i1-(i2-1);
               1-1<=i2-1 by A2,REAL_1:49;
            then i1-0>=i1-(i2-1) by REAL_2:106;
            then A37:j<=i1 by A35,A36,AXIOMS:22;
            then A38:i1-'j+1=i1-j+1 by SCMFSA_7:3;
              i1-j>=0 by A37,SQUARE_1:12;
            then i1-j+1>=0+1 by AXIOMS:24;
            then A39:i1-'j+1<>0 by A38;
                now per cases;
             case len f+i1-'j=len f-'1;
               then len f+i1-j=len f-'1 by A37,Th2 .=len f-1 by A4,JORDAN3:1;
               then len f+(i1-j)-len f=len f-1 -len f by XCMPLX_1:29;
               then i1-j=-1 by XCMPLX_1:26;
              hence contradiction by A37,SQUARE_1:12;
             end;
             case len f+i1-'j<>len f-'1;
               A40:len f+i1-'j=len f+i1-j by A37,Th2;
                 now
                  0<j by A33;
               then i1+1+0<len f+j by A2,REAL_1:67;
               then i1+1-j<len f+j-j by REAL_1:54;
               then i1+1-j-1<len f-1 by REAL_1:54;
               then i1-j<len f-1;
               then i1-'j<len f-'1 by A5,A37,SCMFSA_7:3;
then A41:            i1-'j+1<=len f-'1 by NAT_1:38;
                  now per cases by A41,REAL_1:def 5;
               case A42:i1-'j+1=len f-'1;
                    len f+i1-'j=len f-1+1+i1-j by A37,Th2
                 .=len f-'1+(i1-j+1) by A5
                 .=len f-'1+(len f-'1) by A37,A42,SCMFSA_7:3;
                  then (len f+i1-'j)mod (len f-'1)=0 by Th8;
                 hence S_Drop(len f+i1-'j,f)=i1-'j+1 by A42,Def1;
                end;
                case A43:i1-'j+1<len f-'1;
A44:              len f=len f-1+1 .=len f-'1+1 by A4,JORDAN3:1;
                     len f-'1+1+i1-'j=len f-'1+1+i1-j by A37,Th2
                  .=len f-'1+(1+(i1-j))
                  .=len f-'1+(1+(i1-'j)) by A37,SCMFSA_7:3;
                  then (len f+i1-'j)mod (len f-'1)
                   =(((len f-'1)mod (len f-'1))+(1+(i1-'j)))mod (len f-'1)
                                by A44,GR_CY_1:2
                  .=(0+(1+(i1-'j)))mod (len f-'1) by GR_CY_1:5
                  .=(i1-'j+1) by A43,GR_CY_1:4;
                  then A45:S_Drop(len f+i1-'j,f)=(len f+i1-'j)mod (len f-'1)
                                     by A39,Def1;
                   len f-1+1+i1-j=(len f-1)+(i1-j+1);
                  then A46:len f+i1-'j=i1-'j+1+(len f-'1) by A4,A38,A40,
JORDAN3:1;
                     (len f-'1)mod(len f-'1)=0 by GR_CY_1:5;
                  then S_Drop(len f+i1-'j,f)=(i1-'j+1+0)mod (len f-'1)
                                by A45,A46,GR_CY_1:2
                               .=(i1-'j+1)mod (len f-'1);
                hence S_Drop(len f+i1-'j,f)=i1-'j+1 by A43,GR_CY_1:4;
                end;
                end;
                hence S_Drop(len f+i1-'j,f)=i1-'j+1;
               end;
               hence S_Drop(len f+i1-'j,f)=i1-'j+1;
             end;
             end;
           hence g.j = mid(f,i1,i2).j by A1,A2,A8,A31,A33,A34,Th24;
          end;
         hence len g=i1-'i2+1 & g=mid(f,i1,i2) by A1,A30,A32,FINSEQ_2:10
,SCMFSA_7:3;
        end;
        case n>1;
          then A47:1+1<=n by NAT_1:38;
          len f-'1>=0 by NAT_1:18;
          then A48: len f+i1-'len g-i2>=(len f-'1)*(1+1) by A27,A47,AXIOMS:25;
             0<len g by A2;
          then i1+1+0<i1+1+len g by REAL_1:67;
          then i1+1-len g<i1+1+len g-len g by REAL_1:92;
          then i1-len g+1-i2<i1+1-i2 by REAL_1:92;
          then A49: len f-1+(i1-len g-i2+1)<len f-1+(i1+1-i2) by REAL_1:67;
          A50:i1+1-i2<=len f-i2 by A2,REAL_1:49;
             len f-i2<=len f-1 by A2,REAL_2:106;
          then i1+1-i2<=len f-1 by A50,AXIOMS:22;
          then len f-1+(i1+1-i2)<=len f-1+(len f-1) by REAL_1:55;
          then len f+i1-len g-i2<len f-1+(len f-1) by A49,AXIOMS:22;
         hence contradiction by A2,A5,A48,Th2;
        end;
        end;
       hence len g=i1-'i2+1 & g=mid(f,i1,i2);
      end;
      case A51:(len f+i1-'len g) mod (len f -'1)=0;
           (ex n being Nat st len f+i1-'len g
             =(len f-'1)*n+((len f+i1-'len g) mod (len f -'1)) &
             ((len f+i1-'len g) mod (len f -'1))<len f -'1)or
             ((len f+i1-'len g) mod (len f -'1))=0 & len f-'1=0
             by NAT_1:def 2;
        then consider n being Nat such that A52: len f+i1-'len g
             =(len f-'1)*n+((len f+i1-'len g) mod (len f -'1))
              by A2,A3,A5;
        A53:len f+i1-'len g=i2*n by A24,A51,A52,Def1;
           now assume n=0;
          then A54: len g>=len f+i1 by A51,A52,Th1;
             len f<=len f +i1 by NAT_1:29;
         hence contradiction by A2,A54,AXIOMS:22;
        end;
        then n>0 by NAT_1:19;
then A55:     n>=0+1 by NAT_1:38;
A56:      now per cases by A55,REAL_1:def 5;
        case n>1;
then A57:         n>=1+1 by NAT_1:38;
               i1+1+1<=len f+len g by A2,REAL_1:55;
            then 1+(i1+1)-len g<=len f+len g-len g by REAL_1:49;
            then 1+((i1+1)-len g)-1<=len f-1 by REAL_1:49;
            then len f-1+((i1+1)-len g)<=len f-1+(len f-1) by AXIOMS:24;
            then len f+i1-len g<=len f-1+(len f-1);
            then A58:len f+i1-'len g<=len f-'1+(len f-'1) by A2,A5,Th2;
            A59:len f-'1+(len f-'1)=(len f-'1)*(1+1);
             now per cases by A57,REAL_1:def 5;
          case n>1+1;
           hence contradiction by A6,A51,A52,A58,A59,REAL_1:70;
          end;
          case A60:n=1+1;
            then len f-1+(1+i1)-len g=len f-'1+(len f-'1) by A2,A51,A52,Th2;
            then len f-1+((1+i1)-len g)=len f-'1+(len f-'1) by XCMPLX_1:29;
            then (1+i1)-len g=(len f-'1) by A5,XCMPLX_1:2;
            then A61:(1+i1)-(len f-'1)=len g by XCMPLX_1:18;
            then 1+(len f-'1)<=(1+i1)-(len f-'1)+(len f-'1) by A2,AXIOMS:24;
            then 1+(len f-1)<=(1+i1) by A4,JORDAN3:1;
            then A62:len f=i1+1 by A2,AXIOMS:21;
then A63:        i1+1-i1=len g by A61,BINARITH:39;
               i2*(1+1)=i1*(1+1) by A51,A52,A53,A60,A62,BINARITH:39;
            then i2=i1 by XCMPLX_1:5;
            then i1-'i2=0 by GOBOARD9:1;
           hence len g=i1-'i2+1 by A63,XCMPLX_1:26;
          end;
          end;
         hence len g=i1-'i2+1;
        end;
        case A64: n=1;
             len f+i1-'len g=len f+i1-len g by A2,Th2;
          then len f+i1=i2+len g by A53,A64,XCMPLX_1:27;
         hence contradiction by A1,A2,REAL_1:67;
        end;
        end;
          then A65:len g = len g & len mid(f,i1,i2) = len g by A1,A2,A8,Th21;
             for j st j in Seg len g holds g.j =mid(f,i1,i2).j
          proof let j;assume j in Seg len g;
            then A66:1<=j & j<=len g by FINSEQ_1:3;
            then A67:g.j=f.S_Drop(len f+i1-'j,f) by A1,Def3;
            A68:i1-'i2+1=i1-i2+1 by A1,SCMFSA_7:3
               .=i1-(i2-1);
               1-1<=i2-1 by A2,REAL_1:49;
            then i1-0>=i1-(i2-1) by REAL_2:106;
            then A69:j<=i1 by A56,A66,A68,AXIOMS:22;
            then A70:i1-'j+1=i1-j+1 by SCMFSA_7:3;
              i1-j>=0 by A69,SQUARE_1:12;
            then i1-j+1>=0+1 by AXIOMS:24;
            then A71:i1-'j+1<>0 by A70;
                now per cases;
             case len f+i1-'j=len f-'1;
               then len f+i1-j=len f-'1 by A69,Th2 .=len f-1 by A4,JORDAN3:1;
               then len f+(i1-j)-len f=len f-1 -len f by XCMPLX_1:29;
               then i1-j=-1 by XCMPLX_1:26;
              hence contradiction by A69,SQUARE_1:12;
             end;
             case len f+i1-'j<>len f-'1;
               A72:len f+i1-'j=len f+i1-j by A69,Th2;
                 now
                  0<j by A66;
               then i1+1+0<len f+j by A2,REAL_1:67;
               then i1+1-j<len f+j-j by REAL_1:54;
               then i1+1-j-1<len f-1 by REAL_1:54;
               then i1-j<len f-1;
               then i1-'j<len f-'1 by A5,A69,SCMFSA_7:3;
then A73:            i1-'j+1<=len f-'1 by NAT_1:38;
                  now per cases by A73,REAL_1:def 5;
               case A74:i1-'j+1=len f-'1;
                    len f+i1-'j=len f-1+1+i1-j by A69,Th2
                 .=len f-'1+(i1-j+1) by A5
                 .=len f-'1+(len f-'1) by A69,A74,SCMFSA_7:3;
                  then (len f+i1-'j)mod (len f-'1)=0 by Th8;
                 hence S_Drop(len f+i1-'j,f)=i1-'j+1 by A74,Def1;
                end;
                case A75:i1-'j+1<len f-'1;
A76:              len f=len f-1+1 .=len f-'1+1 by A4,JORDAN3:1;
                     len f-'1+1+i1-'j=len f-'1+1+i1-j by A69,Th2
                  .=len f-'1+(1+(i1-j))
                  .=len f-'1+(1+(i1-'j)) by A69,SCMFSA_7:3;
                  then (len f+i1-'j)mod (len f-'1)
                  =(((len f-'1)mod (len f-'1))+(1+(i1-'j)))mod (len f-'1)
                                by A76,GR_CY_1:2
                  .=(0+(1+(i1-'j)))mod (len f-'1) by GR_CY_1:5
                  .=(i1-'j+1) by A75,GR_CY_1:4;
                  then A77:S_Drop(len f+i1-'j,f)=(len f+i1-'j)mod (len f-'1)
                                     by A71,Def1;
                   len f-1+1+i1-j=(len f-1)+(i1-j+1);
                  then A78:len f+i1-'j=i1-'j+1+(len f-'1) by A4,A70,A72,
JORDAN3:1;
                     (len f-'1)mod(len f-'1)=0 by GR_CY_1:5;
                  then S_Drop(len f+i1-'j,f)=(i1-'j+1+0)mod (len f-'1)
                                by A77,A78,GR_CY_1:2
                               .=(i1-'j+1)mod (len f-'1);
                hence S_Drop(len f+i1-'j,f)=i1-'j+1 by A75,GR_CY_1:4;
                end;
                end;
                hence S_Drop(len f+i1-'j,f)=i1-'j+1;
               end;
               hence S_Drop(len f+i1-'j,f)=i1-'j+1;
             end;
             end;
           hence g.j = mid(f,i1,i2).j by A1,A2,A8,A56,A66,A67,Th24;
          end;
       hence len g=i1-'i2+1 & g=mid(f,i1,i2) by A56,A65,FINSEQ_2:10;
      end;
      end;
      hence len g=i1-'i2+1 & g=mid(f,i1,i2);
     end;
     end;
    hence thesis;
end;

theorem Th40:for f being non constant standard special_circular_sequence,
  g being FinSequence of TOP-REAL 2,i1,i2 being Nat st
  g is_a_part<_of f,i1,i2 & i1<i2 holds len g=len f+i1-'i2
    & g=mid(f,i1,1)^mid(f,len f-'1,i2)
proof let f be non constant standard special_circular_sequence,
  g be FinSequence of TOP-REAL 2,i1,i2 be Nat;
  assume A1:g is_a_part<_of f,i1,i2 & i1<i2;
   then A2:1<=i1 & i1+1<=len f &
   1<=i2 & i2+1<=len f & g.len g=f.i2 & 1<=len g & len g<len f &
   (for i being Nat st 1<=i & i<=len g holds g.i=f.S_Drop((len f+i1-'i),f))
                           by Def3;
      i1<=i1+1 by NAT_1:37;
   then A3:i1<=len f by A2,AXIOMS:22;
     A4: i1+1-1<=len f-1 by A2,REAL_1:49;
   then A5:1<=len f-1 by A2,AXIOMS:22;
   then A6:len f-1=len f-'1 by JORDAN3:1;
   then A7:0<len f-'1 by A2,A4;
   A8:0<>len f-'1 by A2,A4,A6;
     A9: i2+1-1<=len f-1 by A2,REAL_1:49;
       i1<=len f -'1 by A2,JORDAN3:12;
   then 1<=len f -'1 by A2,AXIOMS:22;
   then A10:len f -'1<len f by JORDAN3:14;
      i1<=i1+1 by NAT_1:29; then A11:i1<=len f by A2,AXIOMS:22;
      i2<=i2+1 by NAT_1:29; then A12:i2<=len f by A2,AXIOMS:22;
     A13:f.i2=f.S_Drop(len f+i1-'len g,f) by A2;
A14:   now per cases;
      case len f+i1-'len g mod (len f -'1)<>0;
        then 0<len f+i1-'len g mod (len f -'1) by NAT_1:19;
        then 0+1<=len f+i1-'len g mod (len f -'1) by NAT_1:38;
       hence 1<=S_Drop(len f+i1-'len g,f) by Def1;
      end;
      case len f+i1-'len g mod (len f -'1)=0;
        then A15:S_Drop(len f+i1-'len g,f)=len f -'1 by Def1;
           i1<=len f -'1 by A2,JORDAN3:12;
       hence 1<=S_Drop(len f+i1-'len g,f) by A2,A15,AXIOMS:22;
      end;
      end;
A16:   now per cases;
      case len f+i1-'len g mod (len f -'1)<>0;
        then A17:S_Drop(len f+i1-'len g,f)
        =(len f+i1-'len g mod (len f -'1)) by Def1;
         A18: i1<=len f -'1 by A2,JORDAN3:12;
        then A19:1<=len f -'1 by A2,AXIOMS:22;
          0<len f -'1 by A2,A18;
        then A20:(len f+i1-'len g mod (len f -'1)) <len f-'1 by NAT_1:46;
           len f -'1<len f by A19,JORDAN3:14;
       hence S_Drop(len f+i1-'len g,f)< len f by A17,A20,AXIOMS:22;
      end;
      case len f+i1-'len g mod (len f -'1)=0;
        then A21:S_Drop(len f+i1-'len g,f)=len f -'1 by Def1;
           i1<=len f -'1 by A2,JORDAN3:12;
        then 1<=len f -'1 by A2,AXIOMS:22;
       hence S_Drop(len f+i1-'len g,f)< len f by A21,JORDAN3:14;
      end;
      end;
     A22:i2<=len f -'1 by A2,JORDAN3:12;
     then 1<=len f -'1 by A2,AXIOMS:22;
     then len f -'1<len f by JORDAN3:14;
     then A23:i2<len f by A22,AXIOMS:22;
        now per cases by REAL_1:def 5;
     case A24:i2<S_Drop(len f+i1-'len g,f);
       then A25:f/.i2<>f/.(S_Drop(len f+i1-'len g,f)) by A2,A16,GOBOARD7:38;
          i2<=len f by A2,NAT_1:38;
       then A26:f/.i2=f.i2 by A2,FINSEQ_4:24;
          1<=S_Drop(len f+i1-'len g,f) by A2,A24,AXIOMS:22;
      hence contradiction by A13,A16,A25,A26,FINSEQ_4:24;
     end;
     case i2>S_Drop(len f+i1-'len g,f);
       then A27:f/.i2<>f/.(S_Drop(len f+i1-'len g,f)) by A14,A23,GOBOARD7:38;
          i2<=len f by A2,NAT_1:38;
       then f/.i2=f.i2 by A2,FINSEQ_4:24;
      hence contradiction by A13,A14,A16,A27,FINSEQ_4:24;
     end;
     case A28:i2=S_Drop(len f+i1-'len g,f);
         now per cases;
      case A29: len f+i1-'len g mod (len f -'1)<>0;
           (ex n being Nat st len f+i1-'len g
             =(len f-'1)*n+((len f+i1-'len g) mod (len f -'1)) &
             ((len f+i1-'len g) mod (len f -'1))<len f -'1)
               by A8,NAT_1:def 2;
        then consider n being Nat such that A30: len f+i1-'len g
           =(len f-'1)*n+(len f+i1-'len g mod (len f -'1));
        A31:len f+i1-'len g=(len f-'1)*n+i2 by A28,A29,A30,Def1;
           now per cases;
        case n=0;
          then A32: i2=len f+i1-'len g by A28,A29,A30,Def1 .=len f+i1-len g by
A2,Th2
          .=len f-len g+i1;
          then A33:len g=len f+i1-i2;
            A34:len (mid(f,i1,1)^mid(f,len f-'1,i2))
                       = len (mid(f,i1,1))+ len(mid(f,len f-'1,i2))
                                   by FINSEQ_1:35;
            A35:len (mid(f,i1,1))=i1-'1+1 by A2,A3,Th21
                                .=i1 by A2,AMI_5:4;
               len (mid(f,len f-'1,i2))=len f-'1-'i2+1
              by A2,A6,A9,A10,Th21;
            then len (mid(f,len f-'1,i2))=len f-'1-i2+1 by A6,A9,SCMFSA_7:3;
            then len (mid(f,i1,1)^mid(f,len f-'1,i2))=i1+(len f-i2)
               by A6,A34,A35,XCMPLX_1:229
                                           .=i1+len f-i2
                                           .=len f+i1-'i2 by A12,Th2;
            then A36:len g = len g
            & len (mid(f,i1,1)^mid(f,len f-'1,i2))=len g by A12,A33,Th2;
               for j st j in Seg len g holds
              g.j = (mid(f,i1,1)^mid(f,len f-'1,i2)).j
            proof let j;
              assume A37: j in Seg len g;
              then A38:1<=j & j<=len g by FINSEQ_1:3;
              then A39:g.j=f.S_Drop(len f+i1-'j,f) by A1,Def3;
              A40:j<len f by A2,A38,AXIOMS:22;
              then A41:len f+i1-'j=len f+i1-j by Th2;
                 now per cases;
              case A42:j<=len mid(f,i1,1);
                then j in dom mid(f,i1,1) by A38,FINSEQ_3:27;
                then A43:(mid(f,i1,1)^mid(f,len f-'1,i2)).j =mid(f,i1,1).j
                                          by FINSEQ_1:def 7;
                A44:len mid(f,i1,1)=i1-'1+1 by A2,A11,Th21;
                   1<=i1 & i1<=len f-'1 & len f-'1<=len f & 1<=j & j<=i1
                  by A1,A4,A5,A35,A37,A42,Def3,FINSEQ_1:3,JORDAN3:1,13;
                then len f-1+j<=len f+i1 by A6,REAL_1:55;
                then A45: len f-1+j-j<=len f+i1-j by REAL_1:49;
                then A46:len f-'1<=len f+i1-'j by A6,A35,A42,Th2;
                  now per cases;
               case len f+i1-'j=len f-'1;
                 then len f+i1-j=len f-1 by A6,A35,A42,Th2;
                 then len f+(i1-j)=len f-1 by XCMPLX_1:29;
                 then i1-j=len f+-1-len f by XCMPLX_1:26;
                 then i1=-1+j by XCMPLX_1:27;
                 then i1+1=j;
               hence g.j = (mid(f,i1,1)^mid(f,len f-'1,i2)).j
                 by A35,A42,NAT_1:38;
               end;
               case len f+i1-'j<>len f-'1;
                 then A47:len f+i1-'j>len f-'1 by A6,A41,A45,REAL_1:def 5;
                 A48:j-1>=0 by A38,SQUARE_1:12;
                 then j-1=j-'1 by BINARITH:def 3;
                 then A49:len f<=len f+(j-1) by NAT_1:29;
                    now per cases;
                 case A50:i1+1=len f & j-1=0;
                  A51:j=j-1+1 .=1 by A50;
                     len f+i1-'j=len f-1+1+i1-j by A40,Th2
                  .=len f-1+len f-1 by A50,A51,XCMPLX_1:1
                  .=len f-'1+(len f-'1) by A6,XCMPLX_1:29;
                  then (len f+i1-'j)mod (len f-'1)=0 by Th8;
                  then S_Drop(len f+i1-'j,f) =j+i1-'1 by A50,A51,Def1;
                 then A52:S_Drop(len f+i1-'j,f)
                      =1+i1-1 by A51,Th2
                     .=i1-j+1 by A51;
                 A53: i1-j+1=i1-'j+1 by A35,A42,SCMFSA_7:3;
                 A54:1<=i1-'j+1 by NAT_1:29;
                 A55:j-1=j-'1 by A38,SCMFSA_7:3;
                    i1<=i1+(j-'1) by NAT_1:29;
                 then i1-(j-'1)<=i1 by REAL_1:86;
                 then A56:i1-'j+1<=i1 by A53,A55,XCMPLX_1:37;
                    mid(f,i1,1).j=mid(f,1,i1).(i1-1+1-j+1)
                    by A2,A11,A38,A42,A44,Th25
                             .=mid(f,1,i1).(i1-'j+1) by A35,A42,SCMFSA_7:3
                             .=f.(i1-'j+1) by A11,A54,A56,JORDAN3:32;
                 hence g.j = (mid(f,i1,1)^mid(f,len f-'1,i2)).j
                   by A35,A39,A42,A43,A52,SCMFSA_7:3;
                 end;
                 case A57:i1+1<>len f or j-1<>0;
                     now per cases by A57;
                  case i1+1<>len f; then i1+1<len f by A2,REAL_1:def 5;
                   hence i1+1<len f+(j-1) by A49,AXIOMS:22;
                  end;
                  case j-1<>0;
                   then len f<len f+(j-1) by A48,REAL_1:69;
                   hence i1+1<len f+(j-1) by A2,AXIOMS:22;
                  end;
                  end;
                 then i1+1-j<len f-1+j-j by REAL_1:54;
                 then len f-1+(i1+1-j)<len f-1+(len f-1) by REAL_1:67;
                 then len f-1+i1+1-j<len f-1+(len f-1);
                 then A58:len f+i1-'j<len f-'1+(len f-'1)
                   by A6,A41,XCMPLX_1:227;
                 then (len f+i1-'j) mod (len f-'1)<>0 by A7,A47,Th6;
then A59:            S_Drop(len f+i1-'j,f)
                 =(len f+i1-'j) mod (len f-'1) by Def1
                 .=len f+i1-j-(len f-1) by A6,A7,A41,A46,A58,Th7
                 .=(i1-j)+1;
                 A60:  i1-j+1=i1-'j+1 by A35,A42,SCMFSA_7:3;
                 A61:1<=i1-'j+1 by NAT_1:29;
                 A62:j-1=j-'1 by A38,SCMFSA_7:3;
                    i1<=i1+(j-'1) by NAT_1:29;
                 then i1-(j-'1)<=i1 by REAL_1:86;
                 then A63:i1-'j+1<=i1 by A60,A62,XCMPLX_1:37;
                    mid(f,i1,1).j=mid(f,1,i1).(i1-1+1-j+1)
                      by A2,A11,A38,A42,A44,Th25
                             .=mid(f,1,i1).(i1-'j+1) by A35,A42,SCMFSA_7:3
                             .=f.(i1-'j+1) by A11,A61,A63,JORDAN3:32;
                hence g.j = (mid(f,i1,1)^mid(f,len f-'1,i2)).j
                  by A35,A39,A42,A43,A59,SCMFSA_7:3;
                end;
                end;
               hence g.j = (mid(f,i1,1)^mid(f,len f-'1,i2)).j;
               end;
               end;
               hence g.j = (mid(f,i1,1)^mid(f,len f-'1,i2)).j;
              end;
              case A64:j>len mid(f,i1,1);
                then A65:i1+1<=j by A35,NAT_1:38;
                A66:(mid(f,i1,1)^(mid(f,len f-'1,i2))).j
                      =(mid(f,len f-'1,i2)).(j-len mid(f,i1,1))
                                  by A36,A38,A64,JORDAN3:15;
                A67:j<=len f+i1-i2 by A32,A37,FINSEQ_1:3;
                   i2-i1>=0 by A1,SQUARE_1:12;
                then len f+0<=len f+(i2-i1) by REAL_1:55;
                then len f-(i2-i1)<=len f+(i2-i1)-(i2-i1) by REAL_1:49;
                then A68:j<=len f by A67,AXIOMS:22;
                    i1+1<=j by A35,A64,NAT_1:38;
                 then A69: i1+1-i1<=j-i1 by REAL_1:49;
                    i2+1-1<=len f-1 by A2,REAL_1:49;
                  then A70: i2-i2<=len f-1-i2 by REAL_1:49;
A71:               j-i1<=len f+i1-i2-i1 by A32,A38,REAL_1:49;
                     len f-i2<=len f-1 by A2,REAL_1:92;
                  then j-i1<=len f-'1 by A6,A71,AXIOMS:22;
                  then A72:j-'i1<=len f-'1 by A35,A64,SCMFSA_7:3;
                 A73: len f-i2=len f-1-i2+1
                  .=len f-'1-'i2+1 by A6,A70,BINARITH:def 3;
                    0<j-i1 by A69;
then A74:             j-i1 =j-'len mid(f,i1,1) by A35,BINARITH:def 3;
A75:j-len mid(f,i1,1)<=len f-'1-'i2+1 by A35,A71,A73,XCMPLX_1:228;
                  len f-'1-'(j-'i1)+1=len f-'1-(j-'i1)+1
                     by A72,SCMFSA_7:3
                .=len f-1-(j-i1)+1 by A6,A35,A64,SCMFSA_7:3
                .=len f+i1-j .=len f+i1-'j by A68,Th2;
                then A76:(mid(f,i1,1)^(mid(f,len f-'1,i2))).j
                =f.(len f+i1-'j) by A2,A6,A9,A10,A35,A66,A69,A74,A75,Th24;
                  A77:len f+i1-j=len f+i1-'j by A40,Th2;
                     0<i1 by A2;
                  then len f+0<len f+i1 by REAL_1:67;
                  then j<len f+i1 by A68,AXIOMS:22;
                  then j-j<len f+i1-j by REAL_1:54;
                  then A78:0<len f+i1-'j by A68,Th2;
                     i1+1<=j by A35,A64,NAT_1:38;
                  then len f+(i1+1)<=len f+j by AXIOMS:24;
                  then len f+i1+1-1<=len f+j-1 by REAL_1:49;
                  then len f+i1-j<=len f+j-1-j by REAL_1:49;
                  then len f+i1-j<=len f-1;
                  then A79:len f-'1>=len f+i1-'j by A6,A68,Th2;
                   now per cases;
                case A80:(len f+i1-'j) mod (len f -'1)=0;
                  then len f+i1-'j=len f-'1 by A78,A79,Th9;
                 hence g.j = (mid(f,i1,1)^mid(f,len f-'1,i2)).j
                                 by A39,A76,A80,Def1;
                end;
                case A81:(len f+i1-'j) mod (len f -'1)<>0;
                 then A82:S_Drop(len f+i1-'j,f)
                 = (len f+i1-'j) mod (len f -'1) by Def1;
                     len f+(i1+1)<=len f+j by A65,REAL_1:55;
                  then len f+i1+1-j<=len f+j-j by REAL_1:49;
                  then A83: len f+i1+1-j-1<=len f-1 by REAL_1:49;
                     len f+i1-j=len f-'1 implies
                  contradiction by A77,A81,GR_CY_1:5;
                  then len f+i1-'j<(len f-'1) by A6,A77,A83,REAL_1:def 5;
                 hence g.j = (mid(f,i1,1)^mid(f,len f-'1,i2)).j
                                 by A39,A76,A82,GR_CY_1:4;
                end;
                end;
               hence g.j = (mid(f,i1,1)^mid(f,len f-'1,i2)).j;
              end;
              end;
             hence g.j = (mid(f,i1,1)^mid(f,len f-'1,i2)).j;
            end;
           hence len g=len f+i1-'i2 & g=mid(f,i1,1)^mid(f,len f-'1,i2)
                                           by A12,A33,A36,Th2,FINSEQ_2:10;
        end;
        case n<>0; then 0<n by NAT_1:19;
          then A84: 0+1<=n by NAT_1:38;
             now per cases by A84,REAL_1:def 5;
          case A85:1=n;
               len f+i1-'len g=len f+i1-len g by A2,Th2;
            then A86:i1-len g=i2+(len f-1)-len f by A6,A31,A85,XCMPLX_1:228
            .=i2-1;
            A87:i1+1<=i2 by A1,NAT_1:38;
               0<len g by A2;
            then i1+1+0<i2+len g by A87,REAL_1:67;
            then i1+1-len g<i2+len g-len g by REAL_1:54;
            then i1+1-len g-1<i2-1 by REAL_1:54;
            hence contradiction by A86,XCMPLX_1:228;
           end;
           case 1<n;
            then 1+1<=n by NAT_1:38;
            then (len f-'1)*n>=(len f-'1)*(1+1) by A7,AXIOMS:25;
            then A88:len f+i1-'len g>=(len f-'1)*(1+1)+i2
            by A31,AXIOMS:24;
               i1+1-1<=len f-1 by A2,REAL_1:49;
            then 1+i1<=len g+(len f-'1) by A2,A6,REAL_1:55;
            then len f-1+(1+i1)<=len f-1+(len g+(len f-'1)) by AXIOMS:24
;
            then A89: len f+i1-len g<=len f-1+len g+(len f-'1)-len g by
REAL_1:49;
            A90:len f+i1-len g=len f+i1-'len g by A2,Th2;
               0<i2 by A2;
            then (len f-'1)*(1+1)<(len f-'1)*(1+1)+i2 by REAL_1:69;
           hence contradiction by A6,A88,A89,A90,AXIOMS:22;
           end;
           end;
        hence contradiction;
        end;
        end;
       hence len g=len f+i1-'i2 & g=mid(f,i1,1)^mid(f,len f-'1,i2);
      end;
      case A91:len f+i1-'len g mod (len f -'1)=0;
           (ex n being Nat st len f+i1-'len g
             =(len f-'1)*n+((len f+i1-'len g) mod (len f -'1)) &
             ((len f+i1-'len g) mod (len f -'1))<len f -'1)
               by A8,NAT_1:def 2;
        then consider n being Nat such that A92: len f+i1-'len g
             =(len f-'1)*n+((len f+i1-'len g) mod (len f -'1));
        A93:len f+i1-'len g=i2*n by A28,A91,A92,Def1;
           now assume n=0;
          then A94: len f+i1<=len g by A91,A92,Th1;
             len f<=len f+i1 by NAT_1:29;
         hence contradiction by A2,A94,AXIOMS:22;
        end;
        then n>0 by NAT_1:19;
then A95:    n>=0+1 by NAT_1:38;
           now assume n>1; then n>=1+1 by NAT_1:38;
         then A96:len f+i1-'len g>=(len f-'1)*(1+1) by A91,A92,NAT_1:20;
            now assume i1+1<len f;
           then i1+1+1<len f+len g by A2,REAL_1:67;
           then i1+1+1-1<len f+len g-1 by REAL_1:54;
           then i1+1-len g<len f+len g-1-len g by REAL_1:54;
           then len f-1+(i1+1-len g)<len f-1+(len f-1) by REAL_1:53;
           then len f+i1-len g<len f-1+(len f-1);
          hence contradiction by A2,A6,A96,Th2;
          end; then i1+1=len f by A2,REAL_1:def 5;
         then i2+1>len f-1+1 by A1,REAL_1:53;
        hence contradiction by A1,Def3;
        end;
        then n=1 by A95,AXIOMS:21;
then A97:    len f+i1-len g=i2 by A2,A93,Th2;
        then A98:len f+i1-i2=len g;
            A99:len (mid(f,i1,1))=i1-'1+1 by A2,A3,Th21
                                .=i1 by A2,AMI_5:4;
               len (mid(f,len f-'1,i2))=len f-'1-'i2+1
              by A2,A6,A9,A10,Th21;
            then len (mid(f,len f-'1,i2))=len f-1-i2+1 by A6,A9,SCMFSA_7:3;
            then len (mid(f,i1,1)^mid(f,len f-'1,i2))=i1+(len f-i2) by A99,
FINSEQ_1:35
                                           .=i1+len f-i2
                                           .=len f+i1-'i2 by A12,Th2;
            then A100:len g = len g
            & len (mid(f,i1,1)^mid(f,len f-'1,i2))=len g by A12,A98,Th2;
               for j st j in Seg len g holds
              g.j = (mid(f,i1,1)^mid(f,len f-'1,i2)).j
            proof let j;assume A101: j in Seg len g;
              then A102:1<=j & j<=len g by FINSEQ_1:3;
              then A103:g.j=f.S_Drop(len f+i1-'j,f) by A1,Def3;
              A104:j<len f by A2,A102,AXIOMS:22;
              then A105:len f+i1-'j=len f+i1-j by Th2;
                 now per cases;
              case A106:j<=len mid(f,i1,1);
                then j in dom mid(f,i1,1) by A102,FINSEQ_3:27;
                then A107:(mid(f,i1,1)^mid(f,len f-'1,i2)).j =mid(f,i1,1).j
                                          by FINSEQ_1:def 7;
                A108:len mid(f,i1,1)=i1-'1+1 by A2,A11,Th21;
                   len f-'1<=len f by JORDAN3:13;
                then len f-1+j<=len f+i1 by A6,A99,A106,REAL_1:55;
                then A109: len f-1+j-j<=len f+i1-j by REAL_1:49;
                then A110:len f-'1<=len f+i1-'j
                  by A6,A99,A106,Th2;
                  now per cases;
               case len f+i1-'j=len f-'1;
                 then len f+i1-j=len f-1 by A6,A99,A106,Th2;
                 then len f+(i1-j)=len f-1 by XCMPLX_1:29;
                 then i1-j=len f+-1-len f by XCMPLX_1:26;
                 then i1=-1+j by XCMPLX_1:27;
                 then i1+1=j;
               hence g.j = (mid(f,i1,1)^mid(f,len f-'1,i2)).j
                 by A99,A106,NAT_1:38;
               end;
               case len f+i1-'j<>len f-'1;
                 then A111:len f+i1-'j>len f-'1
                   by A6,A105,A109,REAL_1:def 5;
                 A112:j-1>=0 by A102,SQUARE_1:12;
                 then j-1=j-'1 by BINARITH:def 3;
                 then A113:len f<=len f+(j-1) by NAT_1:29;
                    now per cases;
                 case A114:i1+1=len f & j-1=0;
                  A115:j=j-1+1 .=1 by A114;
                     len f+i1-'j=len f-1+1+i1-j by A104,Th2
                  .=len f-1+len f-1 by A114,A115,XCMPLX_1:1
                  .=len f-'1+(len f-'1) by A6,XCMPLX_1:29;
                  then (len f+i1-'j)mod (len f-'1)=0 by Th8;
                  then S_Drop(len f+i1-'j,f) =j+i1-'1 by A114,A115,Def1;
                 then A116:S_Drop(len f+i1-'j,f)
                      =i1+1-1 by A115,Th2
                     .=i1-j+1 by A115;
                 A117:  i1-j+1=i1-'j+1 by A99,A106,SCMFSA_7:3;
                 A118:f.S_Drop(len f+i1-'j,f)=f.(i1-'j+1)
                   by A99,A106,A116,SCMFSA_7:3;
                 A119:1<=i1-'j+1 by NAT_1:29;
                 A120:j-1=j-'1 by A102,SCMFSA_7:3;
                    i1<=i1+(j-'1) by NAT_1:29;
                 then i1-(j-'1)<=i1 by REAL_1:86;
                 then A121:i1-'j+1<=i1 by A117,A120,XCMPLX_1:37;
                    mid(f,i1,1).j=mid(f,1,i1).(i1-1+1-j+1)
                   by A2,A11,A102,A106,A108,Th25
                             .=mid(f,1,i1).(i1-'j+1) by A99,A106,SCMFSA_7:3
                             .=f.(i1-'j+1) by A11,A119,A121,JORDAN3:32;
                 hence g.j = (mid(f,i1,1)^mid(f,len f-'1,i2)).j
                   by A1,A102,A107,A118,Def3;
                 end;
                 case A122:i1+1<>len f or j-1<>0;
                     now per cases by A122;
                  case i1+1<>len f; then i1+1<len f by A2,REAL_1:def 5;
                   hence i1+1<len f+(j-1) by A113,AXIOMS:22;
                  end;
                  case j-1<>0;
                   then len f<len f+(j-1) by A112,REAL_1:69;
                   hence i1+1<len f+(j-1) by A2,AXIOMS:22;
                  end;
                  end;
                 then i1+1-j<len f-1+j-j by REAL_1:54;
                 then len f-1+(i1+1-j)<len f-1+(len f-1) by REAL_1:67;
                 then len f-1+i1+1-j<len f-1+(len f-1);
                 then A123:len f+i1-'j<len f-'1+(len f-'1)
                 by A6,A105,XCMPLX_1:227;
                 then (len f+i1-'j) mod (len f-'1)<>0 by A7,A111,Th6;
then A124:             S_Drop(len f+i1-'j,f)
                 =(len f+i1-'j) mod (len f-'1) by Def1
                 .=len f+i1-'j-(len f-'1) by A7,A110,A123,Th7
                 .=len f+(i1-j)-(len f-1) by A5,A105,JORDAN3:1
                 .=(i1-j)+1;
                 A125:  i1-j+1=i1-'j+1 by A99,A106,SCMFSA_7:3;
                 A126:1<=i1-'j+1 by NAT_1:29;
                 A127:j-1=j-'1 by A102,SCMFSA_7:3;
                    i1<=i1+(j-'1) by NAT_1:29;
                 then i1-(j-'1)<=i1 by REAL_1:86;
                 then A128:i1-'j+1<=i1 by A125,A127,XCMPLX_1:37;
                    mid(f,i1,1).j=mid(f,1,i1).(i1-1+1-j+1)
                   by A2,A11,A102,A106,A108,Th25
                             .=mid(f,1,i1).(i1-'j+1) by A99,A106,SCMFSA_7:3
                             .=f.(i1-'j+1) by A11,A126,A128,JORDAN3:32;
                hence g.j = (mid(f,i1,1)^mid(f,len f-'1,i2)).j
                  by A99,A103,A106,A107,A124,SCMFSA_7:3;
                end;
                end;
               hence g.j = (mid(f,i1,1)^mid(f,len f-'1,i2)).j;
               end;
               end;
               hence g.j = (mid(f,i1,1)^mid(f,len f-'1,i2)).j;
              end;
              case A129:j>len mid(f,i1,1);
                then A130:i1+1<=j by A99,NAT_1:38;
                A131:(mid(f,i1,1)^(mid(f,len f-'1,i2))).j
                      =(mid(f,len f-'1,i2)).(j-len mid(f,i1,1))
                                  by A100,A102,A129,JORDAN3:15;
                A132:j<=len f+i1-i2 by A97,A101,FINSEQ_1:3;
                   i2-i1>=0 by A1,SQUARE_1:12;
                then len f+0<=len f+(i2-i1) by REAL_1:55;
                then len f-(i2-i1)<=len f+(i2-i1)-(i2-i1) by REAL_1:49;
                then A133:j<=len f by A132,AXIOMS:22;
                    i1+1<=j by A99,A129,NAT_1:38;
                 then A134: i1+1-i1<=j-i1 by REAL_1:49;
                    i2+1-1<=len f-1 by A2,REAL_1:49;
                 then A135: i2-i2<=len f-1-i2 by REAL_1:49;
A136:               j-i1<=len f+i1-i2-i1 by A97,A102,REAL_1:49;
                     len f-i2<=len f-1 by A2,REAL_1:92;
                  then j-i1<=len f-'1 by A6,A136,AXIOMS:22;
                  then A137:j-'i1<=len f-'1 by A99,A129,SCMFSA_7:3;
                 A138: len f-i2=len f-1-i2+1
                  .=len f-'1-'i2+1 by A6,A135,BINARITH:def 3;
                    0<j-i1 by A134;
then A139:             j-i1 =j-'len mid(f,i1,1) by A99,BINARITH:def 3;
A140:         j-len mid(f,i1,1)<=len f-'1-'i2+1
                by A99,A136,A138,XCMPLX_1:228;
                 len f-'1-'(j-'i1)+1=len f-'1-(j-'i1)+1
                  by A137,SCMFSA_7:3
                .=len f-1-(j-i1)+1 by A6,A99,A129,SCMFSA_7:3
                .=len f+i1-j .=len f+i1-'j by A133,Th2;
                then A141:(mid(f,i1,1)^(mid(f,len f-'1,i2))).j
                =f.(len f+i1-'j) by A2,A6,A9,A10,A99,A131,A134,A139,A140,Th24;
                  A142:len f+i1-j=len f+i1-'j by A104,Th2;
                     0<i1 by A2;
                  then len f+0<len f+i1 by REAL_1:67;
                  then j<len f+i1 by A133,AXIOMS:22;
                  then j-j<len f+i1-j by REAL_1:54;
                  then A143:0<len f+i1-'j by A133,Th2;
                     i1+1<=j by A99,A129,NAT_1:38;
                  then len f+(i1+1)<=len f+j by AXIOMS:24;
                  then len f+i1+1-1<=len f+j-1 by REAL_1:49;
                  then len f+i1-j<=len f+j-1-j by REAL_1:49;
                  then len f+i1-j<=len f-1;
                  then A144:len f-'1>=len f+i1-'j by A6,A133,Th2;
                   now per cases;
                case A145:(len f+i1-'j) mod (len f -'1)=0;
                  then len f+i1-'j=len f-'1 by A143,A144,Th9;
                 hence g.j = (mid(f,i1,1)^mid(f,len f-'1,i2)).j
                                 by A103,A141,A145,Def1;
                end;
                case A146:(len f+i1-'j) mod (len f -'1)<>0;
                 then A147:S_Drop(len f+i1-'j,f)
                 = (len f+i1-'j) mod (len f -'1) by Def1;
                     len f+(i1+1)<=len f+j by A130,REAL_1:55;
                  then len f+i1+1-j<=len f+j-j by REAL_1:49;
                  then A148: len f+i1+1-j-1<=len f-1 by REAL_1:49;
                     len f+i1-j=len f-'1 implies
                    contradiction by A142,A146,GR_CY_1:5;
                  then len f+i1-'j<(len f-'1) by A6,A142,A148,REAL_1:def 5;
                 hence g.j = (mid(f,i1,1)^mid(f,len f-'1,i2)).j
                                 by A103,A141,A147,GR_CY_1:4;
                end;
                end;
               hence g.j = (mid(f,i1,1)^mid(f,len f-'1,i2)).j;
              end;
              end;
             hence g.j = (mid(f,i1,1)^mid(f,len f-'1,i2)).j;
            end;
           hence len g=len f+i1-'i2 & g=mid(f,i1,1)^mid(f,len f-'1,i2)
                                           by A12,A98,A100,Th2,FINSEQ_2:10;
      end;
      end;
      hence len g=len f+i1-'i2 & g=mid(f,i1,1)^mid(f,len f-'1,i2);
     end;
     end;
    hence thesis;
end;

theorem Th41:for f being non constant standard special_circular_sequence,
  g being FinSequence of TOP-REAL 2,i1,i2 being Nat st
  g is_a_part>_of f,i1,i2 holds Rev g is_a_part<_of f,i2,i1
proof let f be non constant standard special_circular_sequence,
  g be FinSequence of TOP-REAL 2,i1,i2 be Nat;
  assume A1:g is_a_part>_of f,i1,i2;
  then A2:1<=i1 & i1+1<=len f &
  1<=i2 & i2+1<=len f & g.len g=f.i2 & 1<=len g & len g<len f &
  (for i being Nat st 1<=i & i<=len g holds g.i=f.S_Drop((i1+i)-'1,f))
                            by Def2;
  then A3:1<len f by AXIOMS:22;
  A4:i1<len f by A2,NAT_1:38;
  A5:len g=len (Rev g) by FINSEQ_5:def 3;
  A6:(Rev g).len (Rev g)=(Rev g).len g by FINSEQ_5:def 3
     .=g.(len g-len g+1) by A2,JORDAN3:24
  .=f.S_Drop((i1+1-'1),f) by A2
  .=f.S_Drop(i1,f) by BINARITH:39;
  A7:i1>0 by A2;
       i1+1-1<=len f-1 by A2,REAL_1:49;
    then A8:i1<=len f-'1 by A3,SCMFSA_7:3;
  A9:now per cases;
  case A10:i1 mod (len f-'1)<>0;
    then A11:S_Drop(i1,f)=i1 mod (len f-'1) by Def1;
       i1 <> len f-'1 by A10,GR_CY_1:5;
    then i1<len f-'1 by A8,REAL_1:def 5;
   hence (Rev g).len (Rev g)=f.i1 by A6,A11,GR_CY_1:4;
  end;
  case A12:i1 mod (len f-'1)=0;
    then S_Drop(i1,f)=len f-'1 by Def1;
   hence (Rev g).len (Rev g)=f.i1 by A6,A7,A8,A12,Th9;
  end;
  end;
     for i being Nat st 1<=i & i<=len (Rev g) holds
  (Rev g).i=f.S_Drop(len f +i2-'i,f)
   proof let i be Nat;assume A13:1<=i & i<=len (Rev g);
        len g<=len g+(i-'1) by NAT_1:29;
     then len g<=len g+(i-1) by A13,SCMFSA_7:3;
     then len g-(i-1)<=len g+(i-1)-(i-1) by REAL_1:49;
     then len g-i+1<=len g;
     then A14:1<=len g-'i+1 & len g-'i+1<=len g
                 by A5,A13,NAT_1:29,SCMFSA_7:3;
     A15:(Rev g).i=g.(len g-i+1) by A5,A13,JORDAN3:24
     .=g.(len g-'i+1) by A5,A13,SCMFSA_7:3
     .=f.S_Drop((i1+(len g-'i+1)-'1),f) by A1,A14,Def2;
     A16:i<len f by A2,A5,A13,AXIOMS:22;
        now per cases;
     case A17:i1<=i2;
       then len f-1+len g=len f-1+(i2-'i1+1) by A1,Th37;
       then len f-1+len g=len f+(i2-'i1);
       then len f-1+len g=len f+(i2-i1) by A17,SCMFSA_7:3;
       then len f-1+len g+i1=len f+i2-i1+i1;
       then len f+i2-i=len f-1+(i1+len g)-i by XCMPLX_1:1;
       then len f+i2-'i=len f-1+(i1+(len g-i)) by A16,Th2;
       then len f+i2-'i=len f-1+(i1+(len g-'i)+1-1) by A5,A13,SCMFSA_7:3;
       then len f+i2-'i=len f-1+(i1+(len g-'i+1)-'1) by Th2;
       then len f+i2-'i=(i1+(len g-'i+1)-'1)+(len f-'1) by A3,SCMFSA_7:3;
      hence (Rev g).i=f.S_Drop(len f +i2-'i,f) by A15,Th35;
     end;
     case i1>i2; then len g=len f+i2-'i1 by A1,Th38;
       then len g+i1=len f+i2-i1+i1 by A4,Th2;
       then len f+i2-'i=i1+len g-i by A5,A13,Th2;
       then len f+i2-'i=i1+(len g-i);
       then len f+i2-'i=i1+(len g-'i)+1-1 by A5,A13,SCMFSA_7:3;
      hence (Rev g).i=f.S_Drop(len f +i2-'i,f) by A15,Th2;
     end;
     end;
    hence (Rev g).i=f.S_Drop(len f +i2-'i,f);
   end;
 hence thesis by A2,A5,A9,Def3;
end;

theorem Th42:for f being non constant standard special_circular_sequence,
  g being FinSequence of TOP-REAL 2,i1,i2 being Nat st
  g is_a_part<_of f,i1,i2 holds Rev g is_a_part>_of f,i2,i1
proof let f be non constant standard special_circular_sequence,
  g be FinSequence of TOP-REAL 2,i1,i2 be Nat;
  assume A1:g is_a_part<_of f,i1,i2;
  then A2:1<=i1 & i1+1<=len f &
  1<=i2 & i2+1<=len f & g.len g=f.i2
  & 1<=len g & len g<len f &
  (for i being Nat st 1<=i & i<=len g holds
  g.i=f.S_Drop(len f+i1-'i,f)) by Def3;
  then A3:1<len f by AXIOMS:22;
  A4:i2<len f by A2,NAT_1:38;
  A5:len g=len (Rev g) by FINSEQ_5:def 3;
  A6:(Rev g).len (Rev g)=(Rev g).len g by FINSEQ_5:def 3
    .=g.(len g-len g+1) by A2,JORDAN3:24
  .=f.S_Drop(len f+i1-'1,f) by A2
  .=f.S_Drop(len f-'1+i1,f) by A3,Th3;
  A7:i1>0 by A2;
       i1+1-1<=len f-1 by A2,REAL_1:49;
    then A8:i1<=len f-'1 by A3,SCMFSA_7:3;
    A9:S_Drop(len f-'1+i1,f)=S_Drop(i1,f) by Th35;
  A10:now per cases;
  case A11:i1 mod (len f-'1)<>0;
    then A12:S_Drop(i1,f)=i1 mod (len f-'1) by Def1;
       i1 <> len f-'1 by A11,GR_CY_1:5;
    then i1<len f-'1 by A8,REAL_1:def 5;
   hence (Rev g).len (Rev g)=f.i1 by A6,A9,A12,GR_CY_1:4;
  end;
  case A13:i1 mod (len f-'1)=0;
    then S_Drop(i1,f)=len f-'1 by Def1;
   hence (Rev g).len (Rev g)=f.i1 by A6,A7,A8,A9,A13,Th9;
  end;
  end;
     for i being Nat st 1<=i & i<=len (Rev g) holds
  (Rev g).i=f.S_Drop((i2+i)-'1,f)
   proof let i be Nat;assume A14:1<=i & i<=len (Rev g);
        len g<=len g+(i-'1) by NAT_1:29;
     then len g<=len g+(i-1) by A14,SCMFSA_7:3;
     then len g-(i-1)<=len g+(i-1)-(i-1) by REAL_1:49;
     then len g-i+1<=len g;
     then A15:1<=len g-'i+1 & len g-'i+1<=len g
           by A5,A14,NAT_1:29,SCMFSA_7:3;
     A16:(Rev g).i=g.(len g-i+1) by A5,A14,JORDAN3:24
     .=g.(len g-'i+1) by A5,A14,SCMFSA_7:3
     .=f.S_Drop(len f+i1-'(len g-'i+1),f) by A1,A15,Def3;
       A17:len g+1<=len f by A2,NAT_1:38;
          len g+1<=len g+1+i by NAT_1:29;
       then len g+1-i<=len g+1+i-i by REAL_1:49;
       then len g-i+1<=len g+1;
       then len g-'i+1<=len g+1 by A5,A14,SCMFSA_7:3;
       then A18:len g-'i+1<=len f by A17,AXIOMS:22;
        now per cases;
     case i1<i2;
       then len g=len f+i1-'i2 by A1,Th40;
       then A19:len g=len f+i1-i2 by A4,Th2;
          len f=len f-'1+1 by A3,AMI_5:4;
       then -len f+i2+i=i2+i-1-(len f-'1);
       then i1-len g+i=i2+i-'1-(len f-'1) by A2,A19,Th2;
       then i1-(len g-i)=i2+i-'1-(len f-'1) by XCMPLX_1:37;
       then i1-(len g-'i)=i2+i-'1-(len f-'1) by A5,A14,SCMFSA_7:3;
       then i1+1-(len g-'i)-1=i2+i-'1-(len f-'1) by XCMPLX_1:228;
       then i1+1-(len g-'i+1)=i2+i-'1-(len f-'1) by XCMPLX_1:36;
       then 1+(i1-(len g-'i+1))=i2+i-'1-(len f-'1) by XCMPLX_1:29;
       then len f-'1+(1+(i1-(len g-'i+1)))=len f-'1+(i2+i-'1)-(len f-'1);
       then len f-'1+1+(i1-(len g-'i+1))=len f-'1+(i2+i-'1)-(len f-'1)
                                         by XCMPLX_1:1;
       then len f-'1+1+i1-(len g-'i+1)=len f-'1+(i2+i-'1)-(len f-'1)
                                         by XCMPLX_1:29;
       then len f+i1-(len g-'i+1)=len f-'1+(i2+i-'1)-(len f-'1)
                                  by A3,AMI_5:4;
      hence (Rev g).i=f.S_Drop((i2+i)-'1,f) by A16,A18,Th2;
     end;
     case A20:i1>=i2;
       then len g=i1-'i2+1 by A1,Th39;
       then len g=i1-i2+1 by A20,SCMFSA_7:3;
       then i1-(len g-i)=(i2+i)-1;
       then i1-(len g-i)=(i2+i)-'1 by A14,Th2;
       then i1-(len g-'i)=(i2+i)-'1 by A5,A14,SCMFSA_7:3;
       then len f-1+(i1-(len g-'i))=(i2+i)-'1+(len f-'1) by A3,SCMFSA_7:3;
       then len f-1+i1-(len g-'i)=(i2+i)-'1+(len f-'1) by XCMPLX_1:29;
       then len f+i1-1-(len g-'i)=(i2+i)-'1+(len f-'1);
       then len f+i1-(len g-'i+1)=(i2+i)-'1+(len f-'1) by XCMPLX_1:36;
       then (Rev g).i=f.S_Drop((i2+i)-'1+(len f-'1),f) by A16,A18,Th2
                .=f.S_Drop((i2+i)-'1,f) by Th35;
      hence (Rev g).i=f.S_Drop((i2+i)-'1,f);
     end;
     end;
    hence (Rev g).i=f.S_Drop((i2+i)-'1,f);
   end;
 hence thesis by A2,A5,A10,Def2;
end;

theorem Th43:for f being non constant standard special_circular_sequence,
  i1,i2 being Nat st 1<=i1 & i1<=i2 & i2<len f
  holds mid(f,i1,i2) is_a_part>_of f,i1,i2
proof let f be non constant standard special_circular_sequence,
  i1,i2 be Nat;
  assume A1:1<=i1 & i1<=i2 & i2<len f;
  then A2:1<=i2 by AXIOMS:22;
  then A3:1<len f by A1,AXIOMS:22;
  A4:i1<len f by A1,AXIOMS:22;
  then A5:i1+1<=len f by NAT_1:38;
  A6:i2+1<=len f by A1,NAT_1:38;
  A7:mid(f,i1,i2).len mid(f,i1,i2)=f.i2 by A1,Th22;
  A8:len mid(f,i1,i2)=i2-'i1+1 by A1,A2,A4,JORDAN3:27;
  then A9:1<=len mid(f,i1,i2) by NAT_1:29;
  A10:i2-'i1+1=i2-i1+1 by A1,SCMFSA_7:3;
     0<i1 by A1;
  then i2+1<i2+1+i1 by REAL_1:69;
  then i2+1-i1<i2+1+i1-i1 by REAL_1:54;
  then A11:len mid(f,i1,i2)<len f by A6,A8,A10,AXIOMS:22;
     for i being Nat st 1<=i & i<=len mid(f,i1,i2) holds
  mid(f,i1,i2).i=f.S_Drop((i1+i)-'1,f)
  proof let i be Nat;assume A12: 1<=i & i<=len mid(f,i1,i2);
    then i+i1<=i2-i1+1+i1 by A8,A10,AXIOMS:24;
    then i1+i<=len f by A6,AXIOMS:22;
    then i1+i-1<=len f-1 by REAL_1:49;
    then i1+i-1<=len f-'1 by A3,SCMFSA_7:3;
    then A13:i1+i-'1<=len f-'1 by A1,Th2;
       1+1<=i1+i by A1,A12,REAL_1:55;
    then 1+1-1<=i1+i-1 by REAL_1:49;
    then 1<=i1+i-'1 by A1,Th2;
    then S_Drop((i1+i)-'1,f)=(i1+i)-'1 by A13,Th34;
   hence mid(f,i1,i2).i=f.S_Drop((i1+i)-'1,f) by A1,A2,A4,A12,JORDAN3:27;
  end;
 hence thesis by A1,A2,A5,A6,A7,A9,A11,Def2;
end;

theorem Th44:for f being non constant standard special_circular_sequence,
  i1,i2 being Nat st 1<=i1 & i1<=i2 & i2<len f
  holds mid(f,i2,i1) is_a_part<_of f,i2,i1
proof let f be non constant standard special_circular_sequence,
  i1,i2 be Nat;
  assume 1<=i1 & i1<=i2 & i2<len f;
   then mid(f,i1,i2) is_a_part>_of f,i1,i2 by Th43;
   then Rev mid(f,i1,i2) is_a_part<_of f,i2,i1 by Th41;
 hence thesis by Th30;
end;

theorem Th45:for f being non constant standard special_circular_sequence,
  i1,i2 being Nat st 1<=i2 & i1>i2 & i1<len f
  holds mid(f,i1,len f-'1)^mid(f,1,i2) is_a_part>_of f,i1,i2
proof let f be non constant standard special_circular_sequence,
  i1,i2 be Nat;assume A1:1<=i2 & i1>i2 & i1<len f;
  then A2:1<=i1 by AXIOMS:22;
  then A3:1<len f by A1,AXIOMS:22;
  A4:i2<len f by A1,AXIOMS:22;
  then A5:i2+1<=len f by NAT_1:38;
  A6:i1+1<=len f by A1,NAT_1:38;
  then i1+1-1<=len f-1 by REAL_1:49;
  then A7:i1<=len f-'1 by A3,SCMFSA_7:3;
  then A8:1<=len f-'1 by A2,AXIOMS:22;
     i2+1-1<=len f-1 by A5,REAL_1:49;
  then A9:i2<=len f-'1 by A3,SCMFSA_7:3;
     len f<len f+1 by NAT_1:38;
  then len f-1<len f+1-1 by REAL_1:54;
  then A10:len f-'1<len f by A3,SCMFSA_7:3;
  A11:len (mid(f,i1,len f-'1)^mid(f,1,i2))
  =len mid(f,i1,len f-'1)+ len mid(f,1,i2) by FINSEQ_1:35;
  A12:len mid(f,1,i2)=len (f|i2) by A1,JORDAN3:25 .=i2 by A4,FINSEQ_1:80;
  then 0<len mid(f,1,i2) by A1;
  then len mid(f,i1,len f-'1)< len mid(f,i1,len f-'1)+ len mid(f,1,i2)
                          by REAL_1:69;
  then A13:(mid(f,i1,len f-'1)^mid(f,1,i2)).len (mid(f,i1,len f-'1)^mid(f,1,i2)
)
  =mid(f,1,i2).(len mid(f,i1,len f-'1)+ len mid(f,1,i2)
      -len mid(f,i1,len f-'1)) by A11,JORDAN3:15
  .=f.i2 by A1,A4,Th22;
     len mid(f,1,i2)<=len (mid(f,i1,len f-'1)^mid(f,1,i2)) by A11,NAT_1:29;
  then A14:1<=len (mid(f,i1,len f-'1)^mid(f,1,i2)) by A1,A12,AXIOMS:22;
     len mid(f,i1,len f-'1)=len f-'1-'i1+1 by A1,A2,A7,A8,A10,JORDAN3:27
  .=len f-'1-i1+1 by A7,SCMFSA_7:3
  .=len f-1-i1+1 by A3,SCMFSA_7:3
  .=len f-i1;
  then A15:len (mid(f,i1,len f-'1)^mid(f,1,i2))=len f-i1 +i2 by A12,FINSEQ_1:35
  .=len f-(i1-i2);
     i1-i2>0 by A1,SQUARE_1:11;
  then len f<len f+(i1-i2) by REAL_1:69;
  then A16: len f-(i1-i2)<len f+(i1-i2)-(i1-i2) by REAL_1:54;
     for i being Nat st 1<=i & i<=len (mid(f,i1,len f-'1)^mid(f,1,i2)) holds
  (mid(f,i1,len f-'1)^mid(f,1,i2)).i
  =f.S_Drop((i1+i)-'1,f)
   proof let i be Nat;assume
     A17: 1<=i & i<=len (mid(f,i1,len f-'1)^mid(f,1,i2));
     A18:len mid(f,i1,len f-'1)=len f-'1-'i1+1
                             by A1,A2,A7,A8,A10,JORDAN3:27;
     A19: len f-'1-'i1+1=len f-'1-i1+1 by A7,SCMFSA_7:3
      .=len f-1-i1+1 by A3,SCMFSA_7:3
      .=len f-i1;
     A20:len f-i1=len f-'i1 by A1,SCMFSA_7:3;
     A21:i1+i-'1=i1+i-1 by A17,Th2;
        now per cases;
     case A22:i<=len mid(f,i1,len f-'1);
        then i in dom mid(f,i1,len f-'1) by A17,FINSEQ_3:27;
        then A23:(mid(f,i1,len f-'1)^mid(f,1,i2)).i=mid(f,i1,len f-'1).i
                                      by FINSEQ_1:def 7;
        A24:mid(f,i1,len f-'1).i=f.(i1+i-'1)
                          by A1,A2,A7,A8,A10,A17,A22,JORDAN3:27;
           0<=i-1 by A17,SQUARE_1:12;
then A25:     1+0<=i1+(i-1) by A2,REAL_1:55;
           i1+i<=i1+(len f-'1-'i1+1) by A18,A22,AXIOMS:24;
        then i1+i<=(len f-'1-i1+1)+i1 by A7,SCMFSA_7:3;
        then i1+i<=len f-'1+1;
        then i1+i<=len f by A3,AMI_5:4;
        then i1+i-'1<=len f-1 by A21,REAL_1:49;
        then 1<=i1+i-'1 & i1+i-'1<=len f-'1
           by A3,A21,A25,SCMFSA_7:3;
      hence (mid(f,i1,len f-'1)^mid(f,1,i2)).i=f.S_Drop((i1+i)-'1,f)
                                      by A23,A24,Th34;
     end;
     case A26:i>len mid(f,i1,len f-'1);
        A27:len f-i1=len f-'i1 by A1,SCMFSA_7:3;
           i>=len f-'i1+1 by A18,A19,A20,A26,NAT_1:38;
        then A28: i-(len f-'i1)>=len f-'i1+1-(len f-'i1) by REAL_1:49;
         i-(len f-i1)<=len f-i1+i2-(len f-i1) by A15,A17,REAL_1:49;
        then A29:1<=i-'(len f-'i1) & i-'(len f-'i1)<=i2 by A27,A28,JORDAN3:1;
        then A30:i-'(len f-'i1)<=len f-'1 by A9,AXIOMS:22;
        A31:(mid(f,i1,len f-'1)^mid(f,1,i2)).i
              =mid(f,1,i2).(i-len mid(f,i1,len f-'1)) by A17,A26,JORDAN3:15
              .=mid(f,1,i2).(i-'(len f-'i1))
            by A18,A19,A20,A26,SCMFSA_7:3
              .=f.(i-'(len f-'i1)) by A4,A29,JORDAN3:32;
           len f-'1+(i-'(len f-'i1))=len f-'1+(i-(len f-'i1))
          by A18,A19,A20,A26,SCMFSA_7:3
        .=len f-'1+i-(len f-'i1)
        .=len f-1+i-(len f-'i1) by A3,SCMFSA_7:3
        .=len f-1+i-(len f-i1) by A1,SCMFSA_7:3
        .=(i+i1)-1
        .=i1+i-'1 by A17,Th2;
        then S_Drop((i1+i)-'1,f) =S_Drop(i-'(len f-'i1),f) by Th35
        .=i-'(len f-'i1) by A29,A30,Th34;
      hence (mid(f,i1,len f-'1)^mid(f,1,i2)).i=f.S_Drop((i1+i)-'1,f) by A31;
     end;
     end;
    hence (mid(f,i1,len f-'1)^mid(f,1,i2)).i=f.S_Drop((i1+i)-'1,f);
   end;
 hence thesis by A1,A2,A5,A6,A13,A14,A15,A16,Def2;
end;

theorem Th46:for f being non constant standard special_circular_sequence,
  i1,i2 being Nat st 1<=i1 & i1<i2 & i2<len f
  holds mid(f,i1,1)^mid(f,len f-'1,i2) is_a_part<_of f,i1,i2
proof let f be non constant standard special_circular_sequence,
  i1,i2 be Nat;assume 1<=i1 & i1<i2 & i2<len f;
then A1:  mid(f,i2,len f-'1)^mid(f,1,i1) is_a_part>_of f,i2,i1 by Th45;
      Rev (mid(f,i2,len f-'1)^mid(f,1,i1))
   =Rev(mid(f,1,i1))^Rev(mid(f,i2,len f-'1)) by FINSEQ_5:67
   .=mid(f,i1,1)^Rev(mid(f,i2,len f-'1)) by Th30
   .=mid(f,i1,1)^mid(f,len f-'1,i2) by Th30;
 hence thesis by A1,Th41;
end;

theorem Th47:for h being FinSequence of TOP-REAL 2,i1,i2 st
1<=i1 & i1<=len h & 1<=i2 & i2<=len h holds L~mid(h,i1,i2) c= L~h
proof let h be FinSequence of TOP-REAL 2,i1,i2;
 assume A1:1<=i1 & i1<=len h & 1<=i2 & i2<=len h;
 thus L~mid(h,i1,i2) c= L~h
 proof let x be set;assume
   A2: x in L~mid(h,i1,i2);
      now per cases;
   case A3:i1<=i2;
        x in union { LSeg(mid(h,i1,i2),i) : 1 <= i & i+1 <= len mid(h,i1,i2) }
                           by A2,TOPREAL1:def 6;
     then consider Y being set such that A4: x in Y
      & Y in { LSeg(mid(h,i1,i2),i) : 1 <= i & i+1 <= len mid(h,i1,i2) }
        by TARSKI:def 4;
     consider i such that A5:Y=LSeg(mid(h,i1,i2),i)
            & (1 <= i & i+1 <= len mid(h,i1,i2)) by A4;
     A6:LSeg(mid(h,i1,i2),i)=LSeg(mid(h,i1,i2)/.i,mid(h,i1,i2)/.(i+1))
                             by A5,TOPREAL1:def 5;
        i<=i+1 by NAT_1:29;
     then A7:i<=len mid(h,i1,i2) by A5,AXIOMS:22;
     then A8:mid(h,i1,i2).i=h.(i+i1-'1) by A1,A3,A5,JORDAN3:27;
     A9:len mid(h,i1,i2)=i2-'i1+1 by A1,A3,JORDAN3:27;
     A10:1<=i+1 & i+1<=len mid(h,i1,i2) by A5,NAT_1:29;
     then A11:mid(h,i1,i2).(i+1)=h.(i+1+i1-'1) by A1,A3,JORDAN3:27;
     A12:mid(h,i1,i2)/.i=mid(h,i1,i2).i by A5,A7,FINSEQ_4:24;
     A13:mid(h,i1,i2)/.(i+1)=mid(h,i1,i2).(i+1) by A10,FINSEQ_4:24;
        1+1<=i+i1 by A1,A5,REAL_1:55;
then A14: 1+1-1<=i+i1-1 by REAL_1:49;
        i+1-1<=i2-'i1+1-1 by A5,A9,REAL_1:49;
     then i<=i2-i1 by A3,SCMFSA_7:3;
     then A15: i+i1<=i2-i1+i1 by AXIOMS:24;
     then A16: i+i1<=len h by A1,AXIOMS:22;
     then A17:1<=i+i1-'1 & i+i1-'1<=len h by A1,A14,Th2,JORDAN3:7;
     then A18:h/.(i+i1-'1)=h.(i+i1-'1) by FINSEQ_4:24;
     A19:i+1+i1-'1=i+1+i1-1 by A1,Th2 .=i+i1;
     then A20:i+1+i1-'1=i+i1-1+1 .=i+i1-'1+1 by A1,Th2;
        i+i1-'1<=i+i1-'1+1 by NAT_1:29;
     then i+i1-'1<=i+i1-1+1 by A1,Th2;
     then A21:1<=i+i1 & i+i1<=len h by A1,A15,A17,AXIOMS:22;
     then LSeg(mid(h,i1,i2),i)=LSeg(h/.(i+i1-'1),h/.(i+1+i1-'1))
                                  by A6,A8,A11,A12,A13,A18,A19,FINSEQ_4:24
                    .=LSeg(h,i+i1-'1) by A14,A16,A19,A20,TOPREAL1:def 5;
     then LSeg(mid(h,i1,i2),i) in { LSeg(h,j) : 1 <= j & j+1 <= len h}
       by A14,A19,A20,A21;
     then x in union { LSeg(h,j) : 1 <= j & j+1 <= len h} by A4,A5,TARSKI:def 4
;
    hence x in L~h by TOPREAL1:def 6;
   end;
   case A22:i1>i2;
        mid(h,i1,i2)=Rev mid(h,i2,i1) by Th30;
     then x in L~mid(h,i2,i1) by A2,SPPOL_2:22;
     then x in union { LSeg(mid(h,i2,i1),i) : 1 <= i & i+1 <= len mid(h,i2,i1)
}
                           by TOPREAL1:def 6;
     then consider Y being set such that A23: x in Y
      & Y in { LSeg(mid(h,i2,i1),i) : 1 <= i & i+1 <= len mid(h,i2,i1) }
       by TARSKI:def 4;
     consider i such that A24:Y=LSeg(mid(h,i2,i1),i)
            & (1 <= i & i+1 <= len mid(h,i2,i1)) by A23;
     A25:LSeg(mid(h,i2,i1),i)=LSeg(mid(h,i2,i1)/.i,mid(h,i2,i1)/.(i+1))
                             by A24,TOPREAL1:def 5;
        i<=i+1 by NAT_1:29;
     then A26:i<=len mid(h,i2,i1) by A24,AXIOMS:22;
     then A27:mid(h,i2,i1).i=h.(i+i2-'1) by A1,A22,A24,JORDAN3:27;
     A28:len mid(h,i2,i1)=i1-'i2+1 by A1,A22,JORDAN3:27;
     A29:1<=i+1 & i+1<=len mid(h,i2,i1) by A24,NAT_1:29;
     then A30:mid(h,i2,i1).(i+1)=h.(i+1+i2-'1) by A1,A22,JORDAN3:27;
     A31:mid(h,i2,i1)/.i=mid(h,i2,i1).i by A24,A26,FINSEQ_4:24;
     A32:mid(h,i2,i1)/.(i+1)=mid(h,i2,i1).(i+1) by A29,FINSEQ_4:24;
        1+1<=i+i2 by A1,A24,REAL_1:55;
then A33: 1+1-1<=i+i2-1 by REAL_1:49;
     then A34:1<=i+i2-'1 by A1,Th2;
        i+1-1<=i1-'i2+1-1 by A24,A28,REAL_1:49;
     then i<=i1-i2 by A22,SCMFSA_7:3;
     then A35: i+i2<=i1-i2+i2 by AXIOMS:24;
     then A36:i+i2<=len h by A1,AXIOMS:22;
then  i+i2-'1<=len h by JORDAN3:7;
     then A37:h/.(i+i2-'1)=h.(i+i2-'1) by A34,FINSEQ_4:24;
     A38:i+1+i2-'1=i+1+i2-1 by A1,Th2 .=i+i2;
     then A39:i+1+i2-'1=i+i2-1+1 .=i+i2-'1+1 by A1,Th2;
        i+i2-'1<=i+i2-'1+1 by NAT_1:29;
     then i+i2-'1<=i+i2-1+1 by A1,Th2;
     then A40:1<=i+i2 & i+i2<=len h by A1,A34,A35,AXIOMS:22;
     then LSeg(mid(h,i2,i1),i)=LSeg(h/.(i+i2-'1),h/.(i+1+i2-'1))
                                  by A25,A27,A30,A31,A32,A37,A38,FINSEQ_4:24
                       .=LSeg(h,i+i2-'1) by A33,A36,A38,A39,TOPREAL1:def 5;
     then LSeg(mid(h,i2,i1),i) in { LSeg(h,j) : 1 <= j & j+1 <= len h}
        by A33,A38,A39,A40;
     then x in union { LSeg(h,j) : 1 <= j & j+1 <= len h} by A23,A24,
TARSKI:def 4;
    hence x in L~h by TOPREAL1:def 6;
   end;
   end;
  hence x in L~h;
 end;
end;

theorem Th48:for g being FinSequence of D holds
 g is one-to-one iff for i1,i2 st 1<=i1 & i1<=len g & 1<=i2 & i2<=len g &
 (g.i1=g.i2 or g/.i1=g/.i2) holds i1=i2
 proof let g be FinSequence of D;
   A1:g is one-to-one implies
   for i1,i2 st 1<=i1 & i1<=len g & 1<=i2 & i2<=len g &
   (g.i1=g.i2 or g/.i1=g/.i2) holds i1=i2
   proof assume A2: g is one-to-one;
    thus for i1,i2 st 1<=i1 & i1<=len g & 1<=i2 & i2<=len g &
      (g.i1=g.i2 or g/.i1=g/.i2) holds i1=i2
      proof let i1,i2;assume A3:1<=i1 & i1<=len g & 1<=i2 & i2<=len g &
       (g.i1=g.i2 or g/.i1=g/.i2);
       then A4:i1 in dom g by FINSEQ_3:27;
       A5:i2 in dom g by A3,FINSEQ_3:27;
       A6:g/.i1=g.i1 by A3,FINSEQ_4:24;
          g/.i2=g.i2 by A3,FINSEQ_4:24;
       hence i1=i2 by A2,A3,A4,A5,A6,FUNCT_1:def 8;
      end;
   end;
      (for i1,i2 st 1<=i1 & i1<=len g & 1<=i2 & i2<=len g &
   (g.i1=g.i2 or g/.i1=g/.i2) holds i1=i2)
   implies g is one-to-one
   proof assume A7:(for i1,i2 st 1<=i1 & i1<=len g & 1<=i2 & i2<=len g &
     (g.i1=g.i2 or g/.i1=g/.i2) holds i1=i2);
       for x1,x2 being set st x1 in dom g & x2 in dom g & g.x1=g.x2 holds x1=x2
     proof let x1,x2 be set;
      assume A8:x1 in dom g & x2 in dom g & g.x1=g.x2;
       then reconsider n1=x1 as Nat;
       reconsider n2=x2 as Nat by A8;
       A9:1<=n1 & n1<=len g by A8,FINSEQ_3:27;
          1<=n2 & n2<=len g by A8,FINSEQ_3:27;
      hence x1=x2 by A7,A8,A9;
     end;
    hence g is one-to-one by FUNCT_1:def 8;
   end;
  hence thesis by A1;
 end;

theorem Th49:for f being non constant standard special_circular_sequence,
 i2 st 1<i2 & i2+1<=len f holds f|i2 is_S-Seq
proof let f be non constant standard special_circular_sequence,i2;
 assume A1: 1<i2 & i2+1<=len f;
  then i2<=len f by NAT_1:38;
  then A2:len (f|i2)=i2 by FINSEQ_1:80;
  then A3:1+1<=len (f|i2) by A1,NAT_1:38;
     for i,j st i+1 < j holds LSeg(f|i2,i) misses LSeg(f|i2,j)
  proof let i,j;assume A4:i+1 < j;
       now per cases;
    case A5:1<=i & j+1<=len (f|i2);
     then A6:LSeg(f|i2,j)=LSeg(f,j) by SPPOL_2:3;
        j<=len (f|i2) by A5,NAT_1:38;
     then i+1<=len (f|i2) by A4,AXIOMS:22;
     then A7:LSeg(f|i2,i) /\ LSeg(f|i2,j) = LSeg(f,i) /\ LSeg(f,j) by A6,
SPPOL_2:3;
        i2<=len f by A1,NAT_1:38;
     then len (f|i2)=i2 by FINSEQ_1:80;
     then j+1<i2+1 by A5,NAT_1:38;
     then j+1<len f by A1,AXIOMS:22;
    then LSeg(f,i) misses LSeg(f,j) by A4,GOBOARD5:def 4;
    then LSeg(f|i2,i) /\ LSeg(f|i2,j) = {} by A7,XBOOLE_0:def 7;
    hence LSeg(f|i2,i) misses LSeg(f|i2,j) by XBOOLE_0:def 7;
    end;
    case A8:1>i or j+1>len (f|i2);
        now per cases by A8;
     case 1>i;
      then LSeg(f|i2,i)={} by TOPREAL1:def 5;
     then LSeg(f|i2,i) /\ LSeg(f|i2,j) = {};
     hence LSeg(f|i2,i) misses LSeg(f|i2,j) by XBOOLE_0:def 7;
     end;
     case j+1>len (f|i2);
      then LSeg(f|i2,j)={} by TOPREAL1:def 5;
     then LSeg(f|i2,i) /\ LSeg(f|i2,j) = {};
     hence LSeg(f|i2,i) misses LSeg(f|i2,j) by XBOOLE_0:def 7;
     end;
     end;
    hence LSeg(f|i2,i) misses LSeg(f|i2,j);
    end;
    end;
   hence LSeg(f|i2,i) misses LSeg(f|i2,j);
  end;
  then A9:f|i2 is s.n.c. by TOPREAL1:def 9;
     f|i2 is one-to-one
  proof
      for n1,n2 being Nat st 1<=n1 & n1<=len (f|i2) & 1<=n2 & n2<=len (f|i2)
    &((f|i2).n1=(f|i2).n2 or (f|i2)/.n1=(f|i2)/.n2) holds n1=n2
    proof let n1,n2 be Nat;assume A10: 1<=n1 & n1<=len (f|i2)
      & 1<=n2 & n2<=
len (f|i2) & ((f|i2).n1=(f|i2).n2 or (f|i2)/.n1=(f|i2)/.n2);
      then A11:n1 in dom (f|i2) by FINSEQ_3:27;
      A12:n2 in dom (f|i2) by A10,FINSEQ_3:27;
         now per cases by REAL_1:def 5;
      case A13:n1<n2;
          n2+1<=i2+1 by A2,A10,AXIOMS:24;
       then n2+1<=len f by A1,AXIOMS:22;
       then A14:n2<len f by NAT_1:38;
          len f>4 by GOBOARD7:36;
       then A15:f/.n1<>f/.n2 by A10,A13,A14,GOBOARD7:37;
       A16:(f|i2)/.n1=f/.n1 by A11,FINSEQ_4:85;
       A17:(f|i2)/.n2=f/.n2 by A12,FINSEQ_4:85;
          (f|i2)/.n1=(f|i2).n1 by A10,FINSEQ_4:24;
       hence contradiction by A10,A15,A16,A17,FINSEQ_4:24;
      end;
      case n1=n2;
       hence n1=n2;
      end;
      case A18:n2<n1;
          n1+1<=i2+1 by A2,A10,AXIOMS:24;
       then n1+1<=len f by A1,AXIOMS:22;
       then A19:n1<len f by NAT_1:38;
          len f>4 by GOBOARD7:36;
       then A20:f/.n2<>f/.n1 by A10,A18,A19,GOBOARD7:37;
       A21:(f|i2)/.n2=f/.n2 by A12,FINSEQ_4:85;
       A22:(f|i2)/.n1=f/.n1 by A11,FINSEQ_4:85;
          (f|i2)/.n2=(f|i2).n2 by A10,FINSEQ_4:24;
       hence contradiction by A10,A20,A21,A22,FINSEQ_4:24;
      end;
      end;
     hence n1=n2;
    end;
   hence thesis by Th48;
  end;
 hence thesis by A3,A9,TOPREAL1:def 10;
end;

theorem Th50:for f being non constant standard special_circular_sequence,
 i2 st 1<=i2 & i2+1<len f holds f/^i2 is_S-Seq
proof let f be non constant standard special_circular_sequence,i2;
 assume A1: 1<=i2 & i2+1<len f;
  then A2:i2<len f by NAT_1:38;
  then A3:len (f/^i2)=len f-i2 by RFINSEQ:def 2;
     i2+1-i2<len f-i2 by A1,REAL_1:54;
  then A4:1+1<=len (f/^i2) by A3,NAT_1:38;
     for i,j st i+1 < j holds LSeg(f/^i2,i) misses LSeg(f/^i2,j)
  proof let i,j;assume A5:i+1 < j;
    then A6:i<j by NAT_1:38;
       now per cases;
    case A7:1<=i & j<len (f/^i2); then 1<j by A6,AXIOMS:22;
     then A8:LSeg(f/^i2,j)=LSeg(f,i2+j) by A2,SPPOL_2:4;
     A9:j<len f-i2 by A2,A7,RFINSEQ:def 2;
     A10:LSeg(f/^i2,i) /\ LSeg(f/^i2,j) = LSeg(f,i2+i) /\ LSeg(f,i2+j)
        by A2,A7,A8,SPPOL_2:4;
        i2+(i+1)<i2+j by A5,REAL_1:53;
     then A11:i2+i+1<i2+j;
A12:  1+1<=i2+i by A1,A7,REAL_1:55;
        j+i2<len f-i2+i2 by A9,REAL_1:53;
     then 1<i2+i & i2+j<len f by A12,AXIOMS:22;
    then LSeg(f,i2+i) misses LSeg(f,i2+j) by A11,GOBOARD5:def 4;
    then LSeg(f,i2+i) /\ LSeg(f,i2+j) = {} by XBOOLE_0:def 7;
    hence LSeg(f/^i2,i) misses LSeg(f/^i2,j) by A10,XBOOLE_0:def 7;
    end;
    case A13:1>i or j>=len (f/^i2);
        now per cases by A13;
     case 1>i;
      then LSeg(f/^i2,i)={} by TOPREAL1:def 5;
     then LSeg(f/^i2,i) /\ LSeg(f/^i2,j) = {};
     hence LSeg(f/^i2,i) misses LSeg(f/^i2,j) by XBOOLE_0:def 7;
     end;
     case j>=len (f/^i2);
      then j+1>len (f/^i2) by NAT_1:38;
      then LSeg(f/^i2,j)={} by TOPREAL1:def 5;
     then LSeg(f/^i2,i) /\ LSeg(f/^i2,j) = {};
     hence LSeg(f/^i2,i) misses LSeg(f/^i2,j) by XBOOLE_0:def 7;
     end;
     end;
    hence LSeg(f/^i2,i) misses LSeg(f/^i2,j);
    end;
    end;
   hence LSeg(f/^i2,i) misses LSeg(f/^i2,j);
  end;
  then A14:f/^i2 is s.n.c. by TOPREAL1:def 9;
     f/^i2 is one-to-one
  proof
      for n1,n2 being Nat st 1<=n1 & n1<=len (f/^i2) & 1<=n2 & n2<=len (f/^i2)
    &((f/^i2).n1=(f/^i2).n2 or (f/^i2)/.n1=(f/^i2)/.n2) holds n1=n2
    proof let n1,n2 be Nat;assume A15: 1<=n1 & n1<=len (f/^i2)
      & 1<=n2 & n2<=len (f/^i2) & ((f/^i2).n1=(f/^i2).n2 or
      (f/^i2)/.n1=(f/^i2)/.n2);
      then A16:n1 in dom (f/^i2) by FINSEQ_3:27;
      A17:n2 in dom (f/^i2) by A15,FINSEQ_3:27;
         now per cases by REAL_1:def 5;
      case A18:n1<n2;
A19:    n2+i2<=len f-i2+i2 by A3,A15,AXIOMS:24;
       A20:i2+1<=i2+n1 by A15,AXIOMS:24;
          1<i2+1 by A1,NAT_1:38;
       then A21:1<i2+n1 by A20,AXIOMS:22;
       i2+n1<i2+n2 by A18,REAL_1:53;
       then A22:f/.(i2+n1)<>f/.(i2+n2) by A19,A21,GOBOARD7:39;
       A23:(f/^i2)/.n1=f/.(i2+n1) by A16,FINSEQ_5:30;
       A24:(f/^i2)/.n2=f/.(i2+n2) by A17,FINSEQ_5:30;
          (f/^i2)/.n1=(f/^i2).(n1) by A15,FINSEQ_4:24;
       hence contradiction by A15,A22,A23,A24,FINSEQ_4:24;
      end;
      case n1=n2;
       hence n1=n2;
      end;
      case A25:n2<n1;
A26:    n1+i2<=len f-i2+i2 by A3,A15,AXIOMS:24;
       A27:i2+1<=i2+n2 by A15,AXIOMS:24;
          1<i2+1 by A1,NAT_1:38;
       then A28:1<i2+n2 by A27,AXIOMS:22;
       i2+n2<i2+n1 by A25,REAL_1:53;
       then A29:f/.(i2+n2)<>f/.(i2+n1) by A26,A28,GOBOARD7:39;
          n2 in dom (f/^i2) by A15,FINSEQ_3:27;
       then A30:(f/^i2)/.n2=f/.(i2+n2) by FINSEQ_5:30;
          n1 in dom (f/^i2) by A15,FINSEQ_3:27;
       then A31:(f/^i2)/.n1=f/.(i2+n1) by FINSEQ_5:30;
          (f/^i2)/.n2=(f/^i2).n2 by A15,FINSEQ_4:24;
       hence contradiction by A15,A29,A30,A31,FINSEQ_4:24;
      end;
      end;
     hence n1=n2;
    end;
   hence thesis by Th48;
  end;
 hence thesis by A4,A14,TOPREAL1:def 10;
end;

theorem Th51:for f being non constant standard special_circular_sequence,
i1,i2 st 1<=i1 & i1<i2 & i2+1<=len f holds mid(f,i1,i2) is_S-Seq
proof let f be non constant standard special_circular_sequence,
i1,i2;assume A1:1<=i1 & i1<i2 & i2+1<=len f;
 then A2:1<i2 by AXIOMS:22;
 A3:i2<len f by A1,NAT_1:38;
    i1<i1+1 by NAT_1:38; then i1-1<i1+1-1 by REAL_1:54; then i1-1<i2 by A1,
AXIOMS:22;
then A4: i1-'1<i2 by A1,SCMFSA_7:3;
 A5:i2-'i1+1=i2-i1+1 by A1,SCMFSA_7:3 .=i2-(i1-1)
 .=i2-(i1-'1) by A1,SCMFSA_7:3.=i2-'(i1-'1)
    by A4,SCMFSA_7:3;
     mid(f,i1,i2)=(f/^(i1-'1))|(i2-'i1+1) by A1,JORDAN3:def 1;
  then A6:mid(f,i1,i2)=(f|i2)/^(i1-'1) by A5,JORDAN3:21;
  A7:f|i2 is_S-Seq by A1,A2,Th49;
    i1-1+1<len (f|i2) by A1,A3,FINSEQ_1:80;
   then i1-'1+1<len (f|i2) by A1,SCMFSA_7:3;
 hence thesis by A6,A7,Th19;
end;

theorem Th52:for f being non constant standard special_circular_sequence,
i1,i2 st 1<i1 & i1<i2 & i2<=len f holds mid(f,i1,i2) is_S-Seq
proof let f be non constant standard special_circular_sequence,
i1,i2;assume A1:1<i1 & i1<i2 & i2<=len f;
 then A2:i1<len f by AXIOMS:22;
    i2-i1>0 by A1,SQUARE_1:11;
 then i2-'i1>0 by BINARITH:def 3;
then A3: i2-'i1>=0+1 by NAT_1:38;
    1+1<=i1 by A1,NAT_1:38;
 then 1+1-1<=i1-1 by REAL_1:49;
 then A4:1<=i1-'1 by JORDAN3:1;
 A5:i1-'1+1<len f by A1,A2,AMI_5:4;
    i1<i1+1 by NAT_1:38; then i1-1<i1+1-1 by REAL_1:54; then i1-1<i2 by A1,
AXIOMS:22;
 then A6:i1-'1<i2 by A1,SCMFSA_7:3;
 A7: mid(f,i1,i2)=(f/^(i1-'1))|(i2-'i1+1) by A1,JORDAN3:def 1;
 A8:i1-'1+1<len f by A1,A2,AMI_5:4;
 A9:f/^(i1-'1) is_S-Seq by A4,A5,Th50;
   A10: i1-'1<len f by A1,A6,AXIOMS:22;
 then A11:len (f/^(i1-'1))=len f-(i1-'1) by RFINSEQ:def 2;
A12: i1-'1+1-(i1-'1)<len f-(i1-'1) by A8,REAL_1:54;
    i2-(i1-'1)<=len f-(i1-'1) by A1,REAL_1:49;
 then i2-(i1-1)<=len (f/^(i1-'1)) by A1,A11,SCMFSA_7:3;
 then i2-i1+1<=len (f/^(i1-'1));
 then A13:1<=1 & 1<=len (f/^(i1-'1)) & 1<=i2-'i1+1
 & i2-'i1+1<=len (f/^(i1-'1)) & 1<>i2-'i1+1
     by A3,A10,A12,JORDAN3:1,NAT_1:38,RFINSEQ:def 2;
 then mid(f/^(i1-'1),1,i2-'i1+1) is_S-Seq by A9,JORDAN3:39;
 hence thesis by A7,A13,JORDAN3:25;
end;

theorem Th53: for p0,p,q1,q2 being Point of TOP-REAL 2 st
 p0 in LSeg(p,q1) & p0 in LSeg(p,q2) & p<>p0 holds
 q1 in LSeg(p,q2) or q2 in LSeg(p,q1)
proof let p0,p,q1,q2 be Point of TOP-REAL 2;
 assume A1: p0 in LSeg(p,q1) & p0 in LSeg(p,q2) & p<>p0;
  then p0 in { (1-r)*p + r*q1: 0 <= r & r <= 1 } by TOPREAL1:def 3;
  then consider r such that A2: p0=(1-r)*p + r*q1 &( 0 <= r & r <= 1);
     p0 in { (1-s)*p + s*q2: 0 <= s & s <= 1 } by A1,TOPREAL1:def 3;
  then consider s such that A3: p0=(1-s)*p + s*q2 &( 0 <= s & s <= 1);
  A4:1-r>=0 by A2,SQUARE_1:12;
  A5:1-s>=0 by A3,SQUARE_1:12;
  A6:p0-(1-r)*p=r*q1 by A2,EUCLID:52;
  A7:p0-r*q1=(1-r)*p by A2,EUCLID:52;
  A8:p0-(1-s)*p=s*q2 by A3,EUCLID:52;
  A9:p0-s*q2=(1-s)*p by A3,EUCLID:52;
  A10:now assume r=s;
    then r*q1+((1-r)*p-(1-r)*p) =r*q2+(1-r)*p-(1-r)*p by A2,A3,EUCLID:49;
    then r*q1+0.REAL 2 =r*q2+(1-r)*p-(1-r)*p by EUCLID:46;
    then r*q1+0.REAL 2 =r*q2 by EUCLID:52;
    then A11:r*q1=r*q2 by EUCLID:31;
    A12:q1 in LSeg(p,q1) by TOPREAL1:6;
       now per cases;
    case r<>0;
     hence q1 in LSeg(p,q2) or q2 in LSeg(p,q1) by A11,A12,EUCLID:38;
    end;
    case r=0; then p0=(1-0)*p+0.REAL 2 by A2,EUCLID:33;
      then p0=(1-0)*p by EUCLID:31;
     hence contradiction by A1,EUCLID:33;
    end;
    end;
   hence q1 in LSeg(p,q2) or q2 in LSeg(p,q1);
  end;
     now assume A13: r<>s;
       now per cases by A13,REAL_1:def 5;
    case A14:r<s;
         (1-r)*p0-(1-r)*((1-s)*p)=(1-r)*(s*q2) by A8,EUCLID:53;
      then (1-r)*p0-((1-r)*(1-s))*p=(1-r)*(s*q2) by EUCLID:34;
      then (1-r)*p0-((1-r)*(1-s))*p=((1-r)*s)*q2 by EUCLID:34;
      then (1-r)*p0-(1-s)*((1-r)*p)=((1-r)*s)*q2 by EUCLID:34;
      then (1-r)*p0-((1-s)*p0-(1-s)*(r*q1))=((1-r)*s)*q2 by A7,EUCLID:53;
      then (1-r)*p0-(1-s)*p0+(1-s)*(r*q1)=((1-r)*s)*q2 by EUCLID:51;
      then ((1-r)-(1-s))*p0+(1-s)*(r*q1)=((1-r)*s)*q2 by EUCLID:54;
      then A15: (s+1-r-1)*p0+(1-s)*(r*q1)=((1-r)*s)*q2;
      A16:now assume (1-r)*s=0;
then A17:      1-r+r=0+r or s=0 by XCMPLX_1:6;
           now per cases by A17,XCMPLX_1:27;
        case r=1;
         hence contradiction by A3,A14;
        end;
        case s=0;
         hence contradiction by A2,A14;
        end;
        end;
       hence contradiction;
      end; then 1=((1-r)*s)/((1-r)*s) by XCMPLX_1:60;
      then 1=((1-r)*s)*((1-r)*s)" by XCMPLX_0:def 9;
      then (((1-r)*s)*((1-r)*s)")*((s-r)*p0+(1-s)*(r*q1))=((1-r)*s)*q2
                    by A15,EUCLID:33;
      then ((1-r)*s)*(((1-r)*s)"*((s-r)*p0+(1-s)*(r*q1)))=((1-r)*s)*q2
                    by EUCLID:34;
      then q2=((1-r)*s)"*((s-r)*p0+(1-s)*(r*q1)) by A16,EUCLID:38;
      then q2=((1-r)*s)"*((s-r)*p0+((1-s)*r)*q1) by EUCLID:34;
      then q2=((1-r)*s)"*((s-r)*p0)+((1-r)*s)"*(((1-s)*r)*q1)
                    by EUCLID:36;
      then q2=(((1-r)*s)"*(s-r))*p0+((1-r)*s)"*(((1-s)*r)*q1)
                    by EUCLID:34;
      then A18:q2=(((1-r)*s)"*(s-r))*p0+(((1-r)*s)"*((1-s)*r))*q1
                    by EUCLID:34;
      set s1=(((1-r)*s)"*((1-s)*r));
      A19:0<=(1-s)*r by A2,A5,REAL_2:121;
       A20: 0<=(1-r)*s by A3,A4,REAL_2:121;
then A21:    0<((1-r)*s)" by A16,REAL_1:72;
       r-s*r+s*r<=s-s*r+s*r by A14;
      then 1*r-s*r<=(1-r)*s by REAL_1:53;
      then ((1-s)*r)/((1-r)*s)*((1-r)*s)<=1*((1-r)*s) by A16,XCMPLX_1:88;
      then ((1-s)*r)/((1-r)*s)<=1 by A16,A20,REAL_1:70;
      then A22:0<=s1 & s1<=1 by A19,A21,REAL_2:121,XCMPLX_0:def 9;
         (((1-r)*s)"*(s-r))=(((1-r)*s)-((1-r)*s-(s-r)))/((1-r)*s) by
XCMPLX_0:def 9
      .= ((1-r)*s)/((1-r)*s)-((1-r)*s-(s-r))/((1-r)*s)
         by XCMPLX_1:121
      .= 1-(r+(s-r*s)-s)/((1-r)*s) by A16,XCMPLX_1:60
      .=1-s1 by XCMPLX_0:def 9;
      then q2 in {(1-r2)*p0 + r2*q1: 0 <= r2 & r2 <= 1 } by A18,A22;
      then A23:q2 in LSeg(p0,q1) by TOPREAL1:def 3;
         q1 in LSeg(p,q1) by TOPREAL1:6;
      then LSeg(p0,q1) c= LSeg(p,q1) by A1,TOPREAL1:12;
     hence q1 in LSeg(p,q2) or q2 in LSeg(p,q1) by A23;
    end;
    case A24:s<r;
         (1-s)*p0-(1-s)*((1-r)*p)=(1-s)*(r*q1) by A6,EUCLID:53;
      then (1-s)*p0-((1-s)*(1-r))*p=(1-s)*(r*q1) by EUCLID:34;
      then (1-s)*p0-((1-s)*(1-r))*p=((1-s)*r)*q1 by EUCLID:34;
      then (1-s)*p0-(1-r)*((1-s)*p)=((1-s)*r)*q1 by EUCLID:34;
      then (1-s)*p0-((1-r)*p0-(1-r)*(s*q2))=((1-s)*r)*q1 by A9,EUCLID:53;
      then (1-s)*p0-(1-r)*p0+(1-r)*(s*q2)=((1-s)*r)*q1 by EUCLID:51;
      then ((1-s)-(1-r))*p0+(1-r)*(s*q2)=((1-s)*r)*q1 by EUCLID:54;
      then A25: (r+1-s-1)*p0+(1-r)*(s*q2)=((1-s)*r)*q1;
      A26:now assume (1-s)*r=0;
then A27:      1-s+s=0+s or r=0 by XCMPLX_1:6;
           now per cases by A27,XCMPLX_1:27;
        case s=1;
         hence contradiction by A2,A24;
        end;
        case r=0;
         hence contradiction by A3,A24;
        end;
        end;
       hence contradiction;
      end; then 1=((1-s)*r)/((1-s)*r) by XCMPLX_1:60;
      then 1=((1-s)*r)*((1-s)*r)" by XCMPLX_0:def 9;
      then (((1-s)*r)*((1-s)*r)")*((r-s)*p0+(1-r)*(s*q2))=((1-s)*r)*q1
                    by A25,EUCLID:33;
      then ((1-s)*r)*(((1-s)*r)"*((r-s)*p0+(1-r)*(s*q2)))=((1-s)*r)*q1
                    by EUCLID:34;
      then q1=((1-s)*r)"*((r-s)*p0+(1-r)*(s*q2)) by A26,EUCLID:38;
      then q1=((1-s)*r)"*((r-s)*p0+((1-r)*s)*q2) by EUCLID:34;
      then q1=((1-s)*r)"*((r-s)*p0)+((1-s)*r)"*(((1-r)*s)*q2)
                    by EUCLID:36;
      then q1=(((1-s)*r)"*(r-s))*p0+((1-s)*r)"*(((1-r)*s)*q2)
                    by EUCLID:34;
      then A28:q1=(((1-s)*r)"*(r-s))*p0+(((1-s)*r)"*((1-r)*s))*q2
                    by EUCLID:34;
      set s1=(((1-s)*r)"*((1-r)*s));
      A29:0<=(1-r)*s by A3,A4,REAL_2:121;
       A30: 0<=(1-s)*r by A2,A5,REAL_2:121;
then A31:    0<((1-s)*r)" by A26,REAL_1:72;
       s-r*s+r*s<=r-r*s+r*s by A24;
      then 1*s-r*s<=(1-s)*r by REAL_1:53;
      then ((1-r)*s)/((1-s)*r)*((1-s)*r)<=1*((1-s)*r) by A26,XCMPLX_1:88;
      then ((1-r)*s)/((1-s)*r)<=1 by A26,A30,REAL_1:70;
      then A32:0<=s1 & s1<=1 by A29,A31,REAL_2:121,XCMPLX_0:def 9;
         (((1-s)*r)"*(r-s))=(((1-s)*r)-((1-s)*r-(r-s)))/((1-s)*r) by
XCMPLX_0:def 9
      .= ((1-s)*r)/((1-s)*r)-((1-s)*r-(r-s))/((1-s)*r)
         by XCMPLX_1:121
      .= 1-(s+(r-s*r)-r)/((1-s)*r) by A26,XCMPLX_1:60
      .=1-s1 by XCMPLX_0:def 9;
      then q1 in {(1-r2)*p0 + r2*q2: 0 <= r2 & r2 <= 1 } by A28,A32;
      then A33:q1 in LSeg(p0,q2) by TOPREAL1:def 3;
         q2 in LSeg(p,q2) by TOPREAL1:6;
      then LSeg(p0,q2) c= LSeg(p,q2) by A1,TOPREAL1:12;
     hence q1 in LSeg(p,q2) or q2 in LSeg(p,q1) by A33;
    end;
    end;
   hence q1 in LSeg(p,q2) or q2 in LSeg(p,q1);
  end;
 hence thesis by A10;
end;

theorem Th54:for f being non constant standard special_circular_sequence
holds LSeg(f,1)/\ LSeg(f,len f-'1)={f.1}
proof let f be non constant standard special_circular_sequence;
  A1:len f>4 by GOBOARD7:36;
  then A2:2<len f by AXIOMS:22;
  A3:3<len f by A1,AXIOMS:22;
  A4: 3+1-1<len f-1 by A1,REAL_1:54;
     1<=1 & 1+1<=len f by A1,AXIOMS:22;
  then A5:f/.1 in LSeg(f,1) by TOPREAL1:27;
  A6:1<len f by A1,AXIOMS:22;
  then A7:len f-1=len f-'1 by SCMFSA_7:3;
A8:  1+1-1<=len f-1 by A2,REAL_1:54;
  A9:len f-1+1=len f;
  A10:1<=len f-'1 & len f-'1+1<= len f by A7,A8,XCMPLX_1:27;
  A11:f/.len f in LSeg(f,len f-'1) by A7,A8,A9,TOPREAL1:27;
  A12:len f-'1+1=len f-1+1 by A6,SCMFSA_7:3 .=len f;
  A13:f.1=f/.1 by A6,FINSEQ_4:24;
     f/.1=f/.len f by FINSEQ_6:def 1;
  then f.1 in LSeg(f,1)/\ LSeg(f,len f-'1)
    by A5,A11,A13,XBOOLE_0:def 3;
  then A14:{f.1} c= LSeg(f,1)/\ LSeg(f,len f-'1) by ZFMISC_1:37;
      LSeg(f,1)/\ LSeg(f,len f-'1) c={f.1}
    proof let x be set;assume A15: x in LSeg(f,1)/\ LSeg(f,len f-'1);
      then A16:x in LSeg(f,1) & x in LSeg(f,len f-'1) by XBOOLE_0:def 3;
      reconsider p=x as Point of TOP-REAL 2 by A15;
      A17:1+1<=len f by A1,AXIOMS:22;
      A18:LSeg(f/.1,f/.(1+1))=LSeg(f,1) by A2,TOPREAL1:def 5;
      A19:LSeg(f/.(1+1),f/.(1+1+1))=LSeg(f,1+1) by A3,TOPREAL1:def 5;
      A20:p in LSeg(f/.1,f/.(1+1)) by A16,A17,TOPREAL1:def 5;
         len f-'1+1=len f-1+1 by A6,SCMFSA_7:3 .=len f;
      then f/.(len f-'1+1)=f/.1 by FINSEQ_6:def 1;
      then A21:p in LSeg(f/.1,f/.(len f-'1)) by A7,A8,A12,A16,TOPREAL1:def 5;
         2+1-1<=len f-1 by A3,REAL_1:54;
      then A22: 1+1-1<=len f-'1 -1 by A7,REAL_1:49;
         len f<len f+1 by NAT_1:38;
      then A23: len f-1<len f+1-1 by REAL_1:54;
A24:   len f-'1-'1+1=len f-'1-1+1 by A7,A8,SCMFSA_7:3
      .=len f-'1;
      A25:LSeg(f/.(len f-'1-'1),f/.(len f-'1))=LSeg(f,len f-'1-'1)
                            by A7,A22,A23,A24,TOPREAL1:def 5;
      A26:LSeg(f/.(len f-'1),f/.(len f-'1+1))=LSeg(f,len f-'1)
                            by A10,TOPREAL1:def 5;
         now per cases;
      case A27: p<>f/.1;
           now per cases by A20,A21,A27,Th53;
        case A28: f/.(1+1) in LSeg(f/.1,f/.(len f-'1));
             len f-'1+1=len f-1+1 by A6,SCMFSA_7:3
               .=len f;
then A29:       f/.(len f-'1+1)=f/.1 by FINSEQ_6:def 1;
            f/.(1+1) in LSeg(f/.(1+1),f/.(1+1+1)) by TOPREAL1:6;
         then LSeg(f/.(1+1),f/.(1+1+1))/\ LSeg(f/.(len f-'1),f/.(len f-'1+1))
         <>{} by A28,A29,XBOOLE_0:def 3;
         then LSeg(f/.(1+1),f/.(1+1+1)) meets
           LSeg(f/.(len f-'1),f/.(len f-'1+1)) by XBOOLE_0:def 7;
         hence contradiction by A4,A7,A19,A23,A26,GOBOARD5:def 4;
        end;
        case A30:f/.(len f-'1) in LSeg(f/.1,f/.(1+1));
            f/.(len f-'1) in LSeg(f/.(len f-'1-'1),f/.(len f-'1))
                       by TOPREAL1:6;
         then LSeg(f,1)/\ LSeg(f,len f-'1-'1)<>{} by A18,A25,A30,XBOOLE_0:def 3
;
         then A31:LSeg(f,1) meets LSeg(f,len f-'1-'1) by XBOOLE_0:def 7;
            3+1-1<len f-1 by A1,REAL_1:54;
         then A32: 2+1-1<len f-1-1 by REAL_1:54;
            len f-1-1+1<len f by A23;
         then 1+1<len f-'1-'1 & len f-'1-'1+1<len f by A7,A8,A32,SCMFSA_7:3;
        hence contradiction by A31,GOBOARD5:def 4;
        end;
        end;
       hence contradiction;
      end;
      case p=f/.1;
        then x in {f/.1} by TARSKI:def 1;
       hence x in {f.1} by A6,FINSEQ_4:24;
      end;
      end;
     hence x in {f.1};
    end;
 hence thesis by A14,XBOOLE_0:def 10;
end;

theorem Th55:for f being non constant standard special_circular_sequence,
  i1,i2 being Nat,g1,g2 being FinSequence of TOP-REAL 2
   st 1<=i1 & i1<i2 & i2<len f & g1=mid(f,i1,i2)
   & g2=mid(f,i1,1)^mid(f,len f-'1,i2)
  holds (L~g1)/\(L~g2)={f.i1,f.i2} & (L~g1) \/ (L~g2)=L~f
proof let f be non constant standard special_circular_sequence,
  i1,i2 be Nat,g1,g2 be FinSequence of TOP-REAL 2;
  assume A1: 1<=i1 & i1<i2 & i2<len f & g1=mid(f,i1,i2)
   & g2=mid(f,i1,1)^mid(f,len f-'1,i2);
   then A2:1<i2 by AXIOMS:22;
   A3:i1<len f by A1,AXIOMS:22;
   then A4:1<len f by A1,AXIOMS:22;
   A5:i1+1<=len f by A3,NAT_1:38;
   A6:i2+1<=len f by A1,NAT_1:38;
   then A7: i2+1-1<=len f-1 by REAL_1:49;
   then A8:i2<=len f-'1 by A4,SCMFSA_7:3;
   then A9:1<len f-'1 by A2,AXIOMS:22;
      i2+1-i2<=len f-i2 by A6,REAL_1:49;
   then A10:1<=len f-'i2 by JORDAN3:1;
   A11:len f-'1+1=len f by A4,AMI_5:4;
A12:   len f-'1<len f-'1+1 by NAT_1:38;
   then A13:len f-'1<len f by A4,AMI_5:4;
   A14:len mid(f,i1,i2)=i2-'i1+1 by A1,A2,A3,JORDAN3:27;
   A15:0<i2-i1 by A1,SQUARE_1:11;
   then 0<i2-'i1 by A1,SCMFSA_7:3;
   then 0+1<=i2-'i1 by NAT_1:38;
   then 1<len g1 by A1,A14,NAT_1:38;
   then A16:1+1<=len g1 by NAT_1:38;
   A17:len mid(f,i1,1)=i1-'1+1 by A1,A3,Th21;
   then A18:len mid(f,i1,1)=i1 by A1,AMI_5:4;
      0<=i1-'1 by NAT_1:18;
   then A19:0<i1-'1+1 by NAT_1:38;
then A20: mid(f,i1,1)<><*>(TOP-REAL 2) by A17,FINSEQ_1:32;
A21:  0+1<=len mid(f,i1,1) by A17,A19,NAT_1:38;
   A22:len mid(f,len f-'1,i2)=len f-'1-'i2+1 by A2,A8,A13,Th21;
   A23:len f-'1-'i2+1=len f-'1-i2+1 by A8,SCMFSA_7:3 .=len f-1-i2+1
        by A4,SCMFSA_7:3 .=len f-i2;
      0<=len f-'1-'i2 by NAT_1:18;
   then A24:0<len f-'1-'i2+1 by NAT_1:38;
then A25:  mid(f,len f-'1,i2)<><*>(TOP-REAL 2) by A22,FINSEQ_1:32;
A26:   0+1<=len f-'1-'i2+1 by A24,NAT_1:38;
   A27:len g2 =len mid(f,i1,1) + len mid(f,len f-'1,i2) by A1,FINSEQ_1:35
        .=i1-'1+1+(len f-'1-'i2+1) by A1,A3,A22,Th21
        .=i1+(len f-'1-'i2+1) by A1,AMI_5:4;
   then A28:1+1<=len g2 by A1,A26,REAL_1:55;
  A29:L~(mid(f,i1,1)^mid(f,len f-'1,i2))
  = L~mid(f,i1,1)
   \/ LSeg(mid(f,i1,1)/.len mid(f,i1,1),mid(f,len f-'1,i2)/.1)
   \/ L~mid(f,len f-'1,i2) by A20,A25,SPPOL_2:23;
   A30:(L~g1)/\(L~g2) c= {f.i1,f.i2}
    proof let x be set;assume A31: x in (L~g1)/\(L~g2);
      then A32:x in L~g1 & x in L~g2 by XBOOLE_0:def 3;
      reconsider p=x as Point of TOP-REAL 2 by A31;
      A33:x in L~mid(f,i1,i2) & x in L~(mid(f,i1,1)^mid(f,len f-'1,i2))
                       by A1,A31,XBOOLE_0:def 3;
      consider i such that A34:1 <= i & i+1 <= len mid(f,i1,i2)
       & p in LSeg(mid(f,i1,i2),i) by A1,A32,SPPOL_2:13;
      A35:i<i2-'i1+1 by A14,A34,NAT_1:38;
      then A36:LSeg(mid(f,i1,i2),i)=LSeg(f,i+i1-'1) by A1,A34,Th31;
         i+1<=i2-'i1+1 by A1,A2,A3,A34,JORDAN3:27;
      then A37: i+1-1<=i2-'i1+1-1 by REAL_1:49;
A38:    x in L~mid(f,i1,1)
       \/ LSeg(mid(f,i1,1)/.len mid(f,i1,1),mid(f,len f-'1,i2)/.1)
      or x in L~mid(f,len f-'1,i2) by A29,A33,XBOOLE_0:def 2;
         now per cases by A38,XBOOLE_0:def 2;
      case x in L~mid(f,i1,1);
        then consider k such that A39:1 <= k & k+1 <= len mid(f,i1,1)
         & p in LSeg(mid(f,i1,1),k) by SPPOL_2:13;
           now per cases;
        case i1<>1;
          then A40:1<=1 & 1<i1 & i1<=len f & 1<=k & k<i1-'1+1
             by A1,A17,A39,AXIOMS:22,NAT_1:38,REAL_1:def 5;
          then A41:LSeg(mid(f,i1,1),k)=LSeg(f,i1-'k) by Th32;
             i1-'1+1=i1 by A1,AMI_5:4;
          then A42:i1-'k=i1-k by A40,SCMFSA_7:3;
          A43:i+i1-'1=i+i1-1 by A1,Th2;
          set k3=i1-'k,i3=i+i1-'1;
          A44:x in LSeg(f,k3)/\ LSeg(f,i3) by A34,A36,A39,A41,XBOOLE_0:def 3;
A45:        LSeg(f,k3) meets LSeg(f,i3) by A34,A36,A39,A41,XBOOLE_0:3;
             1+1<=i+k by A34,A39,REAL_1:55;
          then 1<i+k by AXIOMS:22;
          then 1-k<i+k-k by REAL_1:54;
          then 1-k-1<i-1 by REAL_1:54;
          then i1+(1-k-1)<i1+(i-1) by REAL_1:53;
          then A46:k3+1<=i3 by A42,A43,NAT_1:38;
A47:        now assume k3+1<>i3;
            then A48:k3+1<i3 by A46,REAL_1:def 5;
               i<i2-i1+1 by A1,A35,SCMFSA_7:3;
            then A49: i+i1<i2-i1+1+i1 by REAL_1:53;
               i2+1<=len f by A1,NAT_1:38;
            then i+i1-1+1<len f by A49,AXIOMS:22;
           hence contradiction by A43,A45,A48,GOBOARD5:def 4;
          end;
          then i1+1-k=i1+i-1 by A42,A43,XCMPLX_1:29;
          then i1+(1-k)=i1+(i-1) by XCMPLX_1:29;
          then 1-k=i1+(i-1)-i1 by XCMPLX_1:26;
          then 1=i-1+k by XCMPLX_1:27;
          then A50: 1+1=i+k-1+1 by XCMPLX_1:29;
A51:        now assume A52: not(i=1 & k=1);
               now per cases by A52;
            case i<>1;
              then 1<i by A34,REAL_1:def 5;
             hence contradiction by A39,A50,REAL_1:67;
            end;
            case k<>1;
              then 1<k by A39,REAL_1:def 5;
             hence contradiction by A34,A50,REAL_1:67;
            end;
            end;
           hence contradiction;
          end;
          then A53:i3=i1 by BINARITH:39;
             1+1<=i1 by A40,NAT_1:38;
          then 1+1-1<=i1-1 by REAL_1:49;
          then A54:1<=i1-'1 by JORDAN3:1;
             k3+2=i1-'1+(1+1) by A51 .=i1-'1+1+1
          .=i1+1 by A1,AMI_5:4;
          then x in {f/.(k3+1)} by A5,A44,A47,A51,A54,TOPREAL1:def 8;
          then A55:x=f/.i1 by A47,A53,TARSKI:def 1;
             f/.i1=f.i1 by A1,A3,FINSEQ_4:24;
         hence x in {f.i1,f.i2} by A55,TARSKI:def 2;
        end;
        case A56:i1=1;
          A57:len mid(f,1,1)=1 by A4,Th27;
             1+1<=k+1 by A39,AXIOMS:24;
         hence contradiction by A39,A56,A57,AXIOMS:22;
        end;
        end;
        hence x in {f.i1,f.i2};
      end;
      case A58:
        x in LSeg(mid(f,i1,1)/.len mid(f,i1,1),mid(f,len f-'1,i2)/.1);
        A59:mid(f,i1,1)/.len mid(f,i1,1)
        =mid(f,i1,1).len mid(f,i1,1) by A21,FINSEQ_4:24
        .=f.1 by A1,A3,A4,Th23
        .=f/.1 by A4,FINSEQ_4:24 .=f/.len f by FINSEQ_6:def 1;
           i2+1-i2<=len f-i2 by A6,REAL_1:49;
        then mid(f,len f-'1,i2)/.1=mid(f,len f-'1,i2).1 by A22,A23,FINSEQ_4:24
        .=f.(len f-'1) by A1,A2,A9,A13,JORDAN3:27
        .=f/.(len f-'1) by A9,A13,FINSEQ_4:24;
        then x in LSeg(f,len f-'1) by A9,A11,A58,A59,TOPREAL1:def 5;
    then A60:x in LSeg(f,i+i1-'1)/\ LSeg(f,len f-'1) by A34,A36,XBOOLE_0:def 3;
then A61:      LSeg(f,i+i1-'1) meets LSeg(f,len f-'1) by XBOOLE_0:4;
           now per cases;
        case A62:1<i1 or 1<i;
             i<=i2-i1 by A1,A37,SCMFSA_7:3;
          then i+i1<=i2-i1+i1 by AXIOMS:24;
          then i+i1<len f by A1,AXIOMS:22;
          then i+i1-1<len f-1 by REAL_1:54;
          then i+i1-'1<len f-1 by A1,Th2;
          then i+i1-'1<len f-'1 by A4,SCMFSA_7:3;
          then A63:i+i1-'1+1<=len f-'1 by NAT_1:38;
A64:      now assume A65:i+i1-'1+1<len f-'1;
            A66:1+1<i+i1 by A1,A34,A62,REAL_1:67;
            then 1<i+i1 by AXIOMS:22;
            then 1+1<i+i1-'1+1 by A66,AMI_5:4;
            then 1+1-1<i+i1-'1+1-1 by REAL_1:54;
            then i+i1-'1>1 & len f-'1<len f by A4,A12,AMI_5:4;
           hence contradiction by A61,A65,GOBOARD5:def 4;
          end;
          then A67:i+i1-'1+1=len f-'1 by A63,REAL_1:def 5;
          set i3=i+i1-'1;
             1+1<=i+i1 by A1,A34,REAL_1:55;
then A68:      1+1-1<=i+i1-1 by REAL_1:49;
             len f-1+1<=len f;
          then len f-'1+1<=len f by A4,SCMFSA_7:3;
          then A69:1<=i3 & i3+2<=len f by A1,A67,A68,Th2,XCMPLX_1:1;
          A70:i3+1=len f-'1 by A63,A64,REAL_1:def 5;
            A71: i+i1-'1=i+i1-1 by A1,Th2;
             now assume A72: i2<len f-'1;
               i+i1<=i2-'i1+i1 by A37,AXIOMS:24;
            then i+i1<=i2-i1+i1 by A15,BINARITH:def 3;
           hence contradiction by A63,A64,A71,A72,REAL_1:def 5;
          end;
          then A73:i3+1=i2 by A8,A70,AXIOMS:21;
             x in {f/.(i3+1)} by A60,A69,A70,TOPREAL1:def 8;
          then A74:x=f/.i2 by A73,TARSKI:def 1;
             f/.i2=f.i2 by A1,A2,FINSEQ_4:24;
         hence x in {f.i1,f.i2} by A74,TARSKI:def 2;
        end;
        case A75: not(1<i1 or 1<i);
          then A76:i1=1 by A1,AXIOMS:21;
             i=1 by A34,A75,AXIOMS:21;
          then A77:i+i1-'1=1+1-'1 by A1,A75,AXIOMS:21 .=1 by BINARITH:39;
             LSeg(f,1)/\ LSeg(f,len f-'1)={f.1}
                    by Th54;
          then x=f.1 by A60,A77,TARSKI:def 1;
         hence x in {f.i1,f.i2} by A76,TARSKI:def 2;
        end;
        end;
       hence x in {f.i1,f.i2};
      end;
      case x in L~mid(f,len f-'1,i2);
        then consider k such that A78:1 <= k & k+1 <= len mid(f,len f-'1,i2)
         & p in LSeg(mid(f,len f-'1,i2),k) by SPPOL_2:13;
        A79:k<len f-'1-'i2+1 by A22,A78,NAT_1:38;
        A80: k<len f-i2 by A22,A23,A78,NAT_1:38;
           k<len f-'i2 by A1,A23,A79,SCMFSA_7:3;
        then k+1<=len f-'i2 by NAT_1:38;
        then k+1-1<=len f-'i2-1 by REAL_1:49;
        then A81:k<=len f-'i2-'1 by A10,SCMFSA_7:3;
        A82:len f-'i2-'1=len f-'i2-1 by A10,SCMFSA_7:3
        .=len f-i2-1 by A1,SCMFSA_7:3 .=len f-1-i2
        .=len f-'1-i2 by A4,SCMFSA_7:3
        .=len f-'1-'i2 by A8,SCMFSA_7:3;
           k+i2<len f-i2+i2 by A80,REAL_1:53;
        then A83: k+i2-k<len f-k by REAL_1:54;
        then A84:1<len f-k by A2,AXIOMS:22;
        then 1+k<len f-k+k by REAL_1:53;
        then 1+k-1<len f-1 by REAL_1:54;
        then A85:k<len f-'1 by A4,SCMFSA_7:3;
A86:      0<len f-k by A2,A83;
        1<len f-'k by A84,BINARITH:def 3;
        then A87:len f-'k-'1=len f-'k-1 by SCMFSA_7:3
        .=len f-k-1 by A86,BINARITH:def 3 .=len f-1-k
        .=len f-'1-k by A4,SCMFSA_7:3
        .=len f-'1-'k by A85,SCMFSA_7:3;
        A88:k<=len f-'1-'i2 by A79,NAT_1:38;
           now per cases;
        case i2<>len f-'1;
          then 1<=i2 & i2<len f-'1 & len f-'1<=len f & 1<=k & k<len f-'1-'i2+1
                by A1,A4,A8,A12,A22,A78,AMI_5:4,AXIOMS:22,NAT_1:38,REAL_1:def 5
;
          then A89:LSeg(mid(f,len f-'1,i2),k)=LSeg(f,len f-'1-'k) by Th32;
             k+(i2-1)<len f-i2+(i2-1) by A80,REAL_1:53;
          then A90:k+(i2-'1)<len f-1 by A2,SCMFSA_7:3;
             k<=k+(i2-'1) by NAT_1:29;
          then A91:k<len f-1 by A90,AXIOMS:22;
          then k<len f-'1 by A4,SCMFSA_7:3;
          then A92:len f-'1-'k=len f-'1-k by SCMFSA_7:3;
             k+1<len f-1+1 by A91,REAL_1:53;
          then A93: k+1-k<len f-k by REAL_1:54;
             len f<len f+1 by NAT_1:38;
          then len f-1<len f+1-1 by REAL_1:54;
          then k<len f by A91,AXIOMS:22;
          then A94:len f-'k=len f-k by SCMFSA_7:3;
          A95:1<len f-'k by A93,JORDAN3:1;
          A96:i+i1-'1=i+i1-1 by A1,Th2;
          set k3=len f-'1-'k,i3=i+i1-'1;
          A97:x in LSeg(f,i3)/\ LSeg(f,k3) by A34,A36,A78,A89,XBOOLE_0:def 3;
A98:        LSeg(f,i3) meets LSeg(f,k3) by A34,A36,A78,A89,XBOOLE_0:3;
             i2+(len f-'1-i2)<len f by A4,A12,AMI_5:4;
          then i2-i1+i1+(len f-'1-'i2)<len f by A8,SCMFSA_7:3;
          then A99:(i2-'i1)+i1+(len f-'1-'i2)<len f by A1,SCMFSA_7:3;
             i+i1<=(i2-'i1)+i1 by A37,AXIOMS:24;
          then A100:i+i1+k<=(i2-'i1)+i1+k by AXIOMS:24;
             (i2-'i1)+i1+k<=(i2-'i1)+i1+(len f-'1-'i2) by A88,AXIOMS:24;
          then i+i1+k<=(i2-'i1)+i1+(len f-'1-'i2) by A100,AXIOMS:22;
          then i+i1+k<len f by A99,AXIOMS:22;
          then i+i1+k-k<len f-k by REAL_1:54;
          then i+i1-1<len f-'k-1 by A94,REAL_1:54;
          then i+i1-1<len f-'k-'1 by A95,SCMFSA_7:3;
          then i+i1-'1<len f-'1-'k by A34,A87,Th2;
          then A101:i3+1<=k3 by NAT_1:38;
A102:      now assume not(i3+1=k3);
            then A103:i3+1<k3 by A101,REAL_1:def 5;
               len f-'1+1<=len f-'1+k by A78,AXIOMS:24;
            then len f-'1+1-k<=len f-'1+k-k by REAL_1:49;
            then k3+1<len f by A13,A92,AXIOMS:22;
           hence contradiction by A98,A103,GOBOARD5:def 4;
          end;
          then len f-1-k=i1+i-1+1 by A4,A92,A96,SCMFSA_7:3;
          then len f-k=i1+i+1 by XCMPLX_1:229;
          then A104: len f=i1+i+1+k by XCMPLX_1:27;
A105:       now assume A106: not(i=i2-'i1 & k=len f-'1-'i2);
               now per cases by A106;
            case i<>i2-'i1;
              then i<i2-'i1 by A37,REAL_1:def 5;
              then i+k<i2-'i1+(len f-'i2-'1) by A81,REAL_1:67;
              then i+k<i2-i1+(len f-'i2-'1) by A1,SCMFSA_7:3;
              then i+k<i2-i1+(len f-'i2-1) by A10,SCMFSA_7:3;
              then i+k<i2-i1+(len f-i2-1) by A1,SCMFSA_7:3;
             hence contradiction by A104;
            end;
            case k<>len f-'1-'i2;
              then k<len f-'1-'i2 by A88,REAL_1:def 5;
              then i+k<i2-'i1+(len f-'i2-'1) by A37,A82,REAL_1:67;
              then i+k<i2-i1+(len f-'i2-'1) by A1,SCMFSA_7:3;
              then i+k<i2-i1+(len f-'i2-1) by A10,SCMFSA_7:3;
              then i+k<i2-i1+(len f-i2-1) by A1,SCMFSA_7:3;
             hence contradiction by A104;
            end;
            end;
           hence contradiction;
          end;
          A107:len f-'1=len f-1 by A4,SCMFSA_7:3;
             len f<=len f+i2 by NAT_1:29;
          then len f-'1+1<=len f+i2 by A4,AMI_5:4;
          then len f-'1+1-i2<=len f+i2-i2 by REAL_1:49;
          then len f-'1+1-i2-1<=len f-1 by REAL_1:49;
          then len f-'1-i2<=len f-1;
          then len f-'1-'i2<=len f-'1 by A7,A107,SCMFSA_7:3;
          then A108:k3 =len f-'1-(len f-'1-'i2) by A105,SCMFSA_7:3
          .=len f-1-(len f-1-i2) by A7,A107,SCMFSA_7:3
          .=i2;
          A109:i3 =i2-'i1+i1-1 by A1,A105,Th2
          .=i2-i1+i1-1 by A1,SCMFSA_7:3
          .=i2-1;
          then A110:i3+2=i2+1;
             1+1<=i2 by A2,NAT_1:38;
          then 1+1-1<=i2-1 by REAL_1:49;
          then x in {f/.(i3+1)} by A6,A97,A102,A109,A110,TOPREAL1:def 8;
          then x=f/.i2 by A102,A108,TARSKI:def 1;
          then x=f.i2 by A1,A2,FINSEQ_4:24;
         hence x in {f.i1,f.i2} by TARSKI:def 2;
        end;
        case A111:i2=len f-'1;
          A112:len mid(f,len f-'1,len f-'1)=1 by A9,A13,Th27;
             1+1<=k+1 by A78,AXIOMS:24;
         hence contradiction by A78,A111,A112,AXIOMS:22;
        end;
        end;
       hence x in {f.i1,f.i2};
      end;
      end;
     hence x in {f.i1,f.i2};
    end;
  A113:{f.i1} c= L~g1
   proof let x be set;assume x in {f.i1};
     then A114:x=f.i1 by TARSKI:def 1;
        g1.1 =f.i1 by A1,A2,A3,JORDAN3:27;
    hence x in L~g1 by A16,A114,JORDAN3:34;
   end;
     {f.i2} c= L~g1
   proof let x be set;assume x in {f.i2};
     then A115:x=f.i2 by TARSKI:def 1;
        g1.len g1=f.i2 by A1,A2,A3,Th23;
    hence x in L~g1 by A16,A115,JORDAN3:34;
   end;
  then {f.i1} \/ {f.i2} c= L~g1 by A113,XBOOLE_1:8;
  then A116:{f.i1,f.i2} c= L~g1 by ENUMSET1:41;
  A117:{f.i1} c= L~g2
   proof let x be set;assume x in {f.i1};
     then A118:x=f.i1 by TARSKI:def 1;
        g2.1 =mid(f,i1,1).1 by A1,A21,SCMFSA_7:9
     .=f.i1 by A1,A3,A4,JORDAN3:27;
    hence x in L~g2 by A28,A118,JORDAN3:34;
   end;
     {f.i2} c= L~g2
   proof let x be set;assume x in {f.i2};
     then A119:x=f.i2 by TARSKI:def 1;
        g2.len g2
      =mid(f,len f-'1,i2).(len f-'1-'i2+1) by A1,A18,A22,A26,A27,SCMFSA_7:10
     .=f.i2 by A1,A2,A9,A13,A22,Th23;
    hence x in L~g2 by A28,A119,JORDAN3:34;
   end;
  then {f.i1} \/ {f.i2} c= L~g2 by A117,XBOOLE_1:8;
  then {f.i1,f.i2} c= L~g2 by ENUMSET1:41;
then A120:  {f.i1,f.i2} c= (L~g1)/\(L~g2) by A116,XBOOLE_1:19;
  A121:L~g1 c= L~f by A1,A2,A3,Th47;
  A122:L~mid(f,i1,1) c= L~f by A1,A3,A4,Th47;
  A123:L~mid(f,len f-'1,i2) c= L~f by A1,A2,A9,A13,Th47;
     len f-'1+1=len f by A4,AMI_5:4;
  then A124:LSeg(f,len f-'1) =LSeg(f/.(len f-'1+1),f/.(len f-'1))
              by A9,TOPREAL1:def 5;
  A125:f/.(len f-'1+1)
  =f/.len f by A4,AMI_5:4
  .=f/.1 by FINSEQ_6:def 1
  .=f.1 by A4,FINSEQ_4:24
  .=mid(f,i1,1).len mid(f,i1,1) by A1,A3,A4,Th23
  .=mid(f,i1,1)/.len mid(f,i1,1) by A21,FINSEQ_4:24;
  A126:1<=len mid(f,len f-'1,i2) by A22,NAT_1:29;
  A127:f/.(len f-'1)
  =f.(len f-'1) by A9,A13,FINSEQ_4:24
  .=mid(f,len f-'1,i2).1 by A1,A2,A9,A13,JORDAN3:27
  .=mid(f,len f-'1,i2)/.1 by A126,FINSEQ_4:24;
  then LSeg(mid(f,i1,1)/.len mid(f,i1,1),mid(f,len f-'1,i2)/.1)
   c= L~f by A124,A125,TOPREAL3:26;
   then L~mid(f,i1,1)
   \/ LSeg(mid(f,i1,1)/.len mid(f,i1,1),mid(f,len f-'1,i2)/.1)
   c= L~f by A122,XBOOLE_1:8;
  then L~g2 c= L~f by A1,A29,A123,XBOOLE_1:8;
  then A128:(L~g1) \/ (L~g2) c= L~f by A121,XBOOLE_1:8;
     L~f c= (L~g1) \/ (L~g2)
   proof let x be set;assume A129:x in L~f;
     then reconsider p=x as Point of TOP-REAL 2;
     consider i such that A130:1 <= i & i+1 <= len f & p in LSeg(f,i)
        by A129,SPPOL_2:13;
        now per cases;
     case A131:i1<=i & i<i2;
        then 0<=i-i1 by SQUARE_1:12;
then A132:    0+1<=i-i1+1 by AXIOMS:24;
           i-i1<i2-i1 by A131,REAL_1:54;
        then i-i1+1<i2-i1+1 by REAL_1:53;
        then i-'i1+1<i2-i1+1 by A131,SCMFSA_7:3;
        then A133:1<=i-'i1+1 & i-'i1+1<i2-'i1+1
           by A1,A131,A132,SCMFSA_7:3;
           i-'i1+1+i1-'1=i-'i1+1+i1-1 by A1,Th2
          .=i-'i1+i1
        .=i by A131,AMI_5:4;
       then x in LSeg(mid(f,i1,i2),i-'i1+1) by A1,A130,A133,Th31;
       then x in L~mid(f,i1,i2) by SPPOL_2:17;
      hence x in (L~g1) \/ (L~g2) by A1,XBOOLE_0:def 2;
     end;
     case A134:i<i1;
       then A135:1<i1 by A130,AXIOMS:22;
          i1+1<=i1+i by A130,AXIOMS:24;
       then i1<i1+i by NAT_1:38;
       then i1-i<i1+i-i by REAL_1:54;
       then i1-'i<i1-1+1 by A134,SCMFSA_7:3;
       then A136:i1-'i<i1-'1+1 by A1,SCMFSA_7:3;
          i+1<=i1 by A134,NAT_1:38;
       then i+1-i<=i1-i by REAL_1:49;
       then A137:1<=i1-'i by JORDAN3:1;
          i1<=i1+i by NAT_1:29; then i1-i<=i1+i-i by REAL_1:49; then i1-'i<=
i1 by A134,SCMFSA_7:3;
       then i1-'(i1-'i)=i1-(i1-'i) by SCMFSA_7:3
       .=i1-(i1-i) by A134,SCMFSA_7:3
       .=i;
       then x in LSeg(mid(f,i1,1),i1-'i) by A3,A130,A135,A136,A137,Th32;
       then x in L~mid(f,i1,1) by SPPOL_2:17;
       then x in L~mid(f,i1,1)
       \/ LSeg(mid(f,i1,1)/.len mid(f,i1,1),mid(f,len f-'1,i2)/.1)
                              by XBOOLE_0:def 2;
       then x in L~(mid(f,i1,1)^mid(f,len f-'1,i2)) by A29,XBOOLE_0:def 2;
      hence x in (L~g1) \/ (L~g2) by A1,XBOOLE_0:def 2;
     end;
     case A138:i2<=i;
          now per cases by A130,REAL_1:def 5;
       case i+1=len f;
         then i=len f-1 by XCMPLX_1:26;
         then i=len f-'1 by A4,SCMFSA_7:3;
        hence x in LSeg(mid(f,i1,1)/.len mid(f,i1,1),
mid(f,len f-'1,i2)/.1)
          \/ L~mid(f,len f-'1,i2) by A124,A125,A127,A130,XBOOLE_0:def 2;
       end;
       case i+1<len f;
         then i+1+1<=len f by NAT_1:38;
         then i+1+1-1<=len f-1 by REAL_1:49;
         then A139:i+1<=len f-'1 by A4,SCMFSA_7:3;
         then A140:i<len f-'1 by NAT_1:38;
         then A141:i2<len f-'1 by A138,AXIOMS:22;
            i+1-i<=len f-'1-i by A139,REAL_1:49;
         then A142:1<=len f-'1-'i by JORDAN3:1;
            i2<i+1 by A138,NAT_1:38;
         then len f-'1+i2<len f-'1+(i+1) by REAL_1:53;
         then len f-'1+i2-i2<len f-'1+(i+1)-i2 by REAL_1:54;
         then len f-'1-i<len f-'1+1+i-i2-i by REAL_1:54;
         then len f-'1-i<len f-'1-i2+1;
         then len f-'1-i<len f-'1-'i2+1 by A141,SCMFSA_7:3;
         then A143:len f-'1-'i<len f-'1-'i2+1 by A140,SCMFSA_7:3;
         A144:len f-'1-(len f-'1-'i)
         =len f-'1-(len f-'1-i) by A140,SCMFSA_7:3
         .=i;
         then 0<=len f-'1-(len f-'1-'i) by A130;
         then len f-'1-'(len f-'1-'i)=i by A144,BINARITH:def 3;
         then x in LSeg(mid(f,len f-'1,i2),len f-'1-'i)
                         by A2,A13,A130,A141,A142,A143,Th32;
         then x in L~mid(f,len f-'1,i2) by SPPOL_2:17;
        hence x in LSeg(mid(f,i1,1)/.len mid(f,i1,1),
           mid(f,len f-'1,i2)/.1)
         \/ L~mid(f,len f-'1,i2) by XBOOLE_0:def 2;
       end;
       end;
       then x in L~mid(f,i1,1)
       \/ (LSeg(mid(f,i1,1)/.len mid(f,i1,1),mid(f,len f-'1,i2)/.1)
       \/ L~mid(f,len f-'1,i2)) by XBOOLE_0:def 2;
       then x in L~(mid(f,i1,1)^mid(f,len f-'1,i2)) by A29,XBOOLE_1:4;
      hence x in (L~g1) \/ (L~g2) by A1,XBOOLE_0:def 2;
     end;
     end;
    hence x in (L~g1) \/ (L~g2);
   end;
 hence thesis by A30,A120,A128,XBOOLE_0:def 10;
end;

theorem Th56:for f being non constant standard special_circular_sequence,
  g being FinSequence of TOP-REAL 2,i1,i2 being Nat st
  g is_a_part>_of f,i1,i2 & i1<i2 holds
  L~g is_S-P_arc_joining f/.i1,f/.i2
proof let f be non constant standard special_circular_sequence,
  g be FinSequence of TOP-REAL 2,i1,i2 be Nat;
  assume A1:g is_a_part>_of f,i1,i2 & i1<i2;
   then A2:len g=i2-'i1+1 & g=mid(f,i1,i2) by Th37;
   A3:1<=i1 & i1+1<=len f &
    1<=i2 & i2+1<=len f & g.len g=f.i2 & 1<=len g & len g<len f &
    (for i being Nat st 1<=i & i<=len g holds g.i=f.S_Drop((i1+i)-'1,f))
                     by A1,Def2;
   then A4:g is_S-Seq by A1,A2,Th51;
   A5:i1<len f by A3,NAT_1:38;
   A6:i2<len f by A3,NAT_1:38;
   then A7:g.1=f.i1 by A2,A3,A5,JORDAN3:27;
   A8:f/.i1=f.i1 by A3,A5,FINSEQ_4:24;
      g/.len g=g.len g by A3,FINSEQ_4:24;
   then f/.i1=g/.1 & f/.i2=g/.len g by A3,A6,A7,A8,FINSEQ_4:24;
 hence thesis by A4,TOPREAL4:def 1;
end;

Lm1:for f being non constant standard special_circular_sequence,
  g being FinSequence of TOP-REAL 2,i1,i2 being Nat st
  g is_a_part>_of f,i1,i2 & i1>i2 holds
  L~g is_S-P_arc_joining f/.i1,f/.i2
proof let f be non constant standard special_circular_sequence,
  g be FinSequence of TOP-REAL 2,i1,i2 be Nat;
  assume A1: g is_a_part>_of f,i1,i2 & i1>i2;
   then A2:len g=len f+i2-'i1
    & g=mid(f,i1,len f-'1)^(f|i2)
    & g=mid(f,i1,len f-'1)^mid(f,1,i2) by Th38;
   A3:1<=i1 & i1+1<=len f &
    1<=i2 & i2+1<=len f & g.len g=f.i2 & 1<=len g & len g<len f &
    (for i being Nat st 1<=i & i<=len g holds g.i=f.S_Drop((i1+i)-'1,f))
                     by A1,Def2;
    then A4:1<len f by AXIOMS:22;
    then A5:len f-'1=len f-1 by SCMFSA_7:3;
    A6:i1-1=i1-'1 by A3,SCMFSA_7:3;
    A7:len f-'1+1=len f by A5;
    A8:1+1<=len f by A4,NAT_1:38;
    A9:i1<len f by A3,NAT_1:38;
    then A10:i1-'1<=len f by JORDAN3:7;
    A11:i2<len f by A3,NAT_1:38;
       i1+1-i1<=len f-i1 by A3,REAL_1:49;
    then A12:1<=len f-'i1 by JORDAN3:1;
      A13: i1+1-1<=len f-1 by A3,REAL_1:49;
    then A14:1<=len f-'1 by A3,A5,AXIOMS:22;
      A15: len f-'1<len f-'1+1 by NAT_1:38;
    set f1=mid(f,i1,len f);
       1<i1 by A1,A3,AXIOMS:22;
    then A16:f1 is_S-Seq by A9,Th52;
    A17:f1.len f1=f.(len f) by A3,A9,Th22;
    A18:mid(f,1,i2).1=f.1 by A3,A4,A11,JORDAN3:27;
    A19:f.(len f)=f/.len f by A4,FINSEQ_4:24;
    A20:f/.1=f/.len f by FINSEQ_6:def 1;
       f.1=f/.1 by A4,FINSEQ_4:24;
    then A21:f1.len f1=mid(f,1,i2).1 by A17,A18,A19,FINSEQ_6:def 1;
    A22:len f1=len f-'i1+1 by A3,A4,A9,JORDAN3:27;
    then A23:1<=len f1 by NAT_1:29;
       len f1<len f1+1 by NAT_1:38;
    then A24: len f1-1<len f1+1-1 by REAL_1:54;
       i1+1-i1<=len f-i1 by A3,REAL_1:49;
    then 1<=len f-'i1 by JORDAN3:1;
    then A25:1<=len f1 & 1<=len f1-'1 & len f1-'1<=len f1
        by A22,A24,BINARITH:39,NAT_1:29;
    then A26:len mid(f1,1,len f1-'1)=len f1-'1-'1+1 by JORDAN3:27
    .=len f-'i1+1-'1-'1+1 by A3,A4,A9,JORDAN3:27;
    A27:len f-'i1-'1+1=len f-'i1-1+1 by A12,SCMFSA_7:3
    .=len f-i1 by A9,SCMFSA_7:3;
    A28:len mid(f,i1,len f-'1)=len f-'1-'i1+1
                    by A3,A5,A9,A13,A14,A15,JORDAN3:27
    .=len f-1-i1+1 by A5,A13,SCMFSA_7:3
                  .=len f-i1;
    then A29:len mid(f1,1,len f1-'1)=len mid(f,i1,len f-'1) by A26,A27,
BINARITH:39;
       for k st 1<=k & k<=len mid(f,i1,len f-'1) holds
     mid(f1,1,len f1-'1).k=mid(f,i1,len f-'1).k
       proof let k;assume A30:1<=k & k<=len mid(f,i1,len f-'1);
         then A31:k<=len f-'i1 by A9,A28,SCMFSA_7:3;
            len mid(f,i1,len f)=len f-'i1+1 by A3,A4,A9,JORDAN3:27;
         then A32:k<len mid(f,i1,len f) by A31,NAT_1:38;
            mid(f1,1,len f1-'1).k=f1.(k+1-'1)
                               by A25,A29,A30,JORDAN3:27
         .=f1.k by BINARITH:39
         .=f.(k+i1-'1) by A3,A4,A9,A30,A32,JORDAN3:27;
        hence mid(f1,1,len f1-'1).k=mid(f,i1,len f-'1).k
          by A3,A5,A9,A13,A14,A15,A30,JORDAN3:27;
       end;
    then A33:mid(f1,1,len f1-'1)=mid(f,i1,len f-'1) by A29,FINSEQ_1:18;
    A34:L~(f/^(i1-'1))
    = union { LSeg((f/^(i1-'1)),i) : 1 <= i & i+1 <= len (f/^(i1-'1)) }
                                 by TOPREAL1:def 6;
    A35:L~(f|i2)= union { LSeg((f|i2),i) : 1 <= i & i+1 <= len (f|i2) }
                                 by TOPREAL1:def 6;
       now per cases by A3,REAL_1:def 5;
    case A36:i2>1;
       union { LSeg((f/^(i1-'1)),i) : 1 <= i & i+1 <= len (f/^(i1-'1)) }
    /\ union { LSeg((f|i2),j) : 1 <= j & j+1 <= len (f|i2) } ={f.1}
     proof
       A37:len (f/^(i1-'1))=len f-'(i1-'1) by JORDAN3:19;
          i1-i1<len f-i1 by A9,REAL_1:54;
       then i1-i1+1<len f-i1+1 by REAL_1:53;
       then 1<len f-(i1-1);
       then 1<len f-(i1-'1) by A3,SCMFSA_7:3;
       then A38:1<len (f/^(i1-'1)) by A37,JORDAN3:1;
       then 1+1<=len (f/^(i1-'1)) by NAT_1:38;
then A39:   1+1-1<=len (f/^(i1-'1))-1 by REAL_1:49;
       then A40:1<=len (f/^(i1-'1))-'1 by JORDAN3:1;
       A41:len (f/^(i1-'1))-'1+1=len (f/^(i1-'1))-1+1
             by A38,SCMFSA_7:3
                            .=len (f/^(i1-'1));
       then A42:1<=len (f/^(i1-'1))-'1 & len (f/^(i1-'1))-'1+1<=len (f/^(i1-'1)
)
                               by A39,JORDAN3:1;
       A43:LSeg((f/^(i1-'1)),len (f/^(i1-'1))-'1)
       =LSeg((f/^(i1-'1))/.(len (f/^(i1-'1))-'1)
       ,(f/^(i1-'1))/.(len (f/^(i1-'1))-'1+1)) by A40,A41,TOPREAL1:def 5;
       A44:i1-1<len f-1 by A9,REAL_1:54;
       then i1-1<len f by A5,A15,AXIOMS:22;
       then A45:i1-'1<len f by A3,SCMFSA_7:3;
       then A46:(f/^(i1-'1))/.(len (f/^(i1-'1))-'1+1)=f/.len f by A41,Th18;
          i1-'1<=len f-1 by A3,A44,SCMFSA_7:3;
       then A47:i1-'1<=len f-'1 by A4,SCMFSA_7:3;
       A48:f/.len f in LSeg((f/^(i1-'1))/.(len (f/^(i1-'1))-'1),
f/.len f)
                              by TOPREAL1:6;
       A49:f.1=f/.1 by A4,FINSEQ_4:24;
       then f.1 in LSeg((f/^(i1-'1)),len (f/^(i1-'1))-'1) &
       LSeg((f/^(i1-'1)),len (f/^(i1-'1))-'1)
           in { LSeg((f/^(i1-'1)),i):1<=i & i+1 <= len (f/^(i1-'1))}
             by A42,A43,A46,A48,FINSEQ_6:def 1;
       then A50:f.1 in union { LSeg((f/^(i1-'1)),i):1<=i & i+1 <= len (f/^(i1-'
1))}
                                  by TARSKI:def 4;
       A51:len (f|2)=2 by A8,FINSEQ_1:80;
       then len (f|2)-'1=2-1 by JORDAN3:1 .=1;
       then A52:LSeg((f|2),len (f|2)-'1) = LSeg((f|2)/.(len (f|2)-'1)
            ,(f|2)/.(len (f|2)-'1+1)) by A51,TOPREAL1:def 5;
       A53:len (f|2)-'1=1+1-'1 by A8,FINSEQ_1:80 .=1 by BINARITH:39;
       A54:(f|2)/.1=(f|2).1 by A51,FINSEQ_4:24;
       A55:(f|2).1=f.1 by JORDAN3:20;
       A56:(f|i2).1=f.1 by A3,JORDAN3:20;
       A57:len (f|i2)=i2 by A11,FINSEQ_1:80;
       then A58:(f|2)/.1=(f|i2)/.1 by A3,A54,A55,A56,FINSEQ_4:24;
       A59:1+1<=len (f|i2) by A36,A57,NAT_1:38;
       A60:(f|2).(1+1)=f.(1+1) by JORDAN3:20;
       A61:(f|2)/.(1+1)=(f|2).(1+1) by A51,FINSEQ_4:24;
          (f|i2).(1+1)=f.(1+1) by A57,A59,JORDAN3:20;
       then A62:(f|2)/.(1+1)=(f|i2)/.(1+1) by A59,A60,A61,FINSEQ_4:24;
       A63:LSeg(f|i2,1)=LSeg((f|i2)/.1,(f|i2)/.(1+1)) by A59,TOPREAL1:def 5;
       A64:(f|2)/.(len (f|2)-'1)=f/.1 by A49,A53,A54,JORDAN3:20;
          1<=1 & 1+1<=len (f|i2) by A36,A57,NAT_1:38;
       then f.1 in LSeg((f|2),len (f|2)-'1) & LSeg((f|2),len (f|2)-'1)
          in { LSeg((f|i2),i) : 1<=i & i+1 <= len (f|i2)}
             by A49,A52,A53,A58,A62,A63,A64,TOPREAL1:6;
       then f.1 in union { LSeg(f|i2,j) : 1 <= j & j+1 <= len (f|i2) }
                                  by TARSKI:def 4;
       then f.1 in union { LSeg((f/^(i1-'1)),i):1<=i & i+1 <= len (f/^(i1-'1))
}
       /\ union { LSeg((f|i2),j) : 1 <= j & j+1 <= len (f|i2)}
                                         by A50,XBOOLE_0:def 3;
       then A65:{f.1} c= union { LSeg((f/^(i1-'1)),i):1<=i & i+1 <=
 len (f/^(i1-'1)) }
       /\ union { LSeg((f|i2),j) : 1 <= j & j+1 <= len (f|i2) } by ZFMISC_1:37;
          union { LSeg((f/^(i1-'1)),i) : 1 <= i & i+1 <= len (f/^(i1-'1)) }
       /\ union { LSeg((f|i2),j) : 1 <= j & j+1 <= len (f|i2) } c= {f.1}
       proof let x be set; assume A66:x in
       union { LSeg((f/^(i1-'1)),i) : 1 <= i & i+1 <= len (f/^(i1-'1)) }
       /\ union { LSeg((f|i2),j) : 1 <= j & j+1 <= len (f|i2) };
       then x in union { LSeg((f/^(i1-'1)),i) :
                     1 <= i & i+1 <= len (f/^(i1-'1)) } by XBOOLE_0:def 3;
       then consider Y1 being set such that
       A67:x in Y1 & Y1 in { LSeg(f/^(i1-'1),i):1<=i & i+1 <= len (f/^(i1-'1))}
             by TARSKI:def 4;
       consider j1 be Nat such that
       A68:  Y1=LSeg((f/^(i1-'1)),j1) &(1<=j1 & j1+1 <=
 len (f/^(i1-'1))) by A67;
          x in union { LSeg((f|i2),j) : 1 <= j & j+1 <= len (f|i2) }
                             by A66,XBOOLE_0:def 3;
       then consider Y2 being set such that
       A69: x in Y2 &
           Y2 in { LSeg(f|i2,i):1<=i & i+1 <= len (f|i2)} by TARSKI:def 4;
       consider j2 be Nat such that
       A70:  Y2=LSeg((f|i2),j2) &(1<=j2 & j2+1 <= len (f|i2)) by A69;
       A71:LSeg(f/^(i1-'1),j1)=LSeg((f/^(i1-'1))/.j1,(f/^(i1-'1))/.(j1+1))
                                by A68,TOPREAL1:def 5;
          0<=i1-'1 by NAT_1:18;
       then A72:0+1<=(i1-'1)+j1 by A68,REAL_1:55;
       A73:j1+1<=len f-'(i1-'1) by A68,JORDAN3:19;
          j1+1+(i1-'1)<=len f-'(i1-'1)+(i1-'1) by A37,A68,AXIOMS:24;
       then A74: j1+1+(i1-'1)<=len f by A45,AMI_5:4;
       then A75:LSeg(f,(i1-'1)+j1)=LSeg(f/.((i1-'1)+j1),f/.((i1-'1)+j1+1))
                                  by A72,TOPREAL1:def 5;
          j1 <= len (f/^(i1-'1)) by A68,NAT_1:38;
       then A76:(f/^(i1-'1))/.j1=(f/^(i1-'1)).j1 by A68,FINSEQ_4:24;
          (i1-'1)+j1<=(i1-'1)+j1+1 by NAT_1:29;
       then A77:1<=j1 & j1+(i1-'1)<=len f by A68,A74,AXIOMS:22;
       then (f/^(i1-'1)).j1=f.((i1-'1)+j1) by JORDAN3:23;
       then A78:(f/^(i1-'1))/.j1=f/.((i1-'1)+j1) by A72,A76,A77,FINSEQ_4:24;
       A79:1<=1+j1 by NAT_1:29;
          j1+1+(i1-'1)<=len f-'(i1-'1)+(i1-'1) by A73,AXIOMS:24;
       then j1+1+(i1-'1)<=len f by A45,AMI_5:4;
       then A80:(f/^(i1-'1)).(j1+1)=f.((i1-'1)+j1+1) by A79,JORDAN3:23;
          1<=1+j1 by NAT_1:29;
       then A81:(f/^(i1-'1))/.(j1+1)=(f/^(i1-'1)).(j1+1) by A68,FINSEQ_4:24;
          1<=1+((i1-'1)+j1) by NAT_1:29;
       then A82:LSeg(f/^(i1-'1),j1)=LSeg(f,(i1-'1)+j1)
          by A71,A74,A75,A78,A80,A81,FINSEQ_4:24;
       A83:1<=j2+1 & j2+1<=len (f|i2) by A70,NAT_1:29;
       A84:len (f|i2)<=len f by FINSEQ_5:18;
       then A85: j2+1<=len f by A70,AXIOMS:22;
       A86:j2<len (f|i2) by A70,NAT_1:38;
       then A87:j2<len f by A84,AXIOMS:22;
       A88:(f|i2)/.j2=(f|i2).j2 by A70,A86,FINSEQ_4:24
          .= f.j2 by A57,A86,JORDAN3:20
          .= f/.j2 by A70,A87,FINSEQ_4:24;
       A89:(f|i2)/.(j2+1)=(f|i2).(j2+1) by A83,FINSEQ_4:24
       .=f.(j2+1) by A57,A70,JORDAN3:20
       .=f/.(j2+1) by A83,A85,FINSEQ_4:24;
       A90:LSeg(f|i2,j2)=LSeg((f|i2)/.j2,
(f|i2)/.(j2+1)) by A70,TOPREAL1:def 5;
          1<=j2 & j2+1<=len f by A70,A84,AXIOMS:22;
       then A91:LSeg(f|i2,j2)=LSeg(f,j2) by A88,A89,A90,TOPREAL1:def 5;
A92:   now assume A93: not (j2=1 & i1-'1+j1=len f-'1);
           len (f/^(i1-'1))=len f-(i1-'1) by A10,A37,SCMFSA_7:3;
        then A94: j1+1+(i1-'1) <= len f-(i1-'1)+(i1-'1) by A68,AXIOMS:24;
        then A95:(i1-'1)+j1+1 <= len f;
        A96: (i1-'1)+j1+1-1 <= len f-1 by A94,REAL_1:49;
        A97:j2+1<i2+1 by A57,A70,NAT_1:38;
        A98:i2+1<=i1 by A1,NAT_1:38;
           i1-'1+1=i1 by A3,AMI_5:4;
        then A99:j2+1<i1-'1+1 by A97,A98,AXIOMS:22;
           i1-'1+1<=i1-'1+j1 by A68,AXIOMS:24;
        then A100:j2+1<(i1-'1)+j1 & (i1-'1)+j1<len f by A95,A99,AXIOMS:22
,NAT_1:38;
           now per cases by A5,A70,A93,A96,REAL_1:def 5;
        case j2>1;
         then LSeg(f,(i1-'1)+j1) misses LSeg(f,j2) by A100,GOBOARD5:def 4;
         hence LSeg(f,(i1-'1)+j1) /\ LSeg(f,j2)={} by XBOOLE_0:def 7;
        end;
        case (i1-'1)+j1 < len f-'1;
          then (i1-'1)+j1+1 < len f-'1+1 by REAL_1:53;
          then LSeg(f,(i1-'1)+j1) misses LSeg(f,j2) by A5,A100,GOBOARD5:def 4;
         hence LSeg(f,(i1-'1)+j1) /\ LSeg(f,j2)={} by XBOOLE_0:def 7;
        end;
        end;
       hence contradiction by A67,A68,A69,A70,A82,A91,XBOOLE_0:def 3;
      end;
       then A101:j1=len f-'1-(i1-'1) by XCMPLX_1:26
       .=len f-'1-'(i1-'1) by A47,SCMFSA_7:3;
A102:   len f-'1-(i1-'1)+(i1-'1)<=len f by A5,A15;
       then A103:len f-'1-'(i1-'1)+(i1-'1)<=len f by A47,SCMFSA_7:3;
          i1+1-1<=len f-1 by A3,REAL_1:49;
       then i1-(i1-1)<=len f-1-(i1-1) by REAL_1:49;
then A104:    1<=len f-1-(i1-1);
       then A105:1<=len f-'1-'(i1-'1) by A5,A6,JORDAN3:1;
       A106:1<=len f-'1-'(i1-'1) & len f-'1-'(i1-'1)+(i1-'1)<=len f
          by A5,A6,A102,A104,JORDAN3:1;
       A107:len f-'1-(i1-'1)<len f-(i1-'1) by A5,A15,REAL_1:54;
          len (f/^(i1-'1))=len f-(i1-'1) by A10,A37,SCMFSA_7:3;
       then A108:len f-'1-'(i1-'1)<len (f/^(i1-'1))
             by A47,A107,SCMFSA_7:3;
       then A109:(f/^(i1-'1))/.(len f-'1-'(i1-'1))=(f/^(i1-'1)).(len f-'1-'(i1
-'1))
                             by A105,FINSEQ_4:24
       .=f.(len f-'1-'(i1-'1)+(i1-'1)) by A103,A105,JORDAN3:23
       .=f.(len f-'1-(i1-'1)+(i1-'1)) by A47,SCMFSA_7:3
       .=f/.(len f-'1) by A5,A14,A15,FINSEQ_4:24;
A110:   len f-'1-'(i1-'1)+1+(i1-'1)=
       len f-'1-(i1-'1)+1+(i1-'1) by A47,SCMFSA_7:3
       .=len f-'1+1  .=len f-1+1 by A4,SCMFSA_7:3
       .=len f;
       A111:1<len f-'1-'(i1-'1)+1 by A105,NAT_1:38;
       A112:len f-'1-'(i1-'1)+1<=len (f/^(i1-'1)) by A108,NAT_1:38;
       then A113:(f/^(i1-'1))/.(len f-'1-'(i1-'1)+1)
        =(f/^(i1-'1)).(len f-'1-'(i1-'1)+1) by A111,FINSEQ_4:24
       .=f.(len f-'1-'(i1-'1)+1+(i1-'1)) by A110,A111,JORDAN3:23
       .=f.(1+((len f-'1-'(i1-'1))+(i1-'1)))
       .=f.(len f-'1-(i1-'1)+(i1-'1)+1) by A47,SCMFSA_7:3
       .=f.len f by A4,AMI_5:4
       .=f/.len f by A4,FINSEQ_4:24;
       A114:LSeg(f/^(i1-'1),j1)
        =LSeg((f/^(i1-'1))/.(len f-'1-'(i1-'1)),
       (f/^(i1-'1))/.(len f-'1-'(i1-'1)+1)) by A101,A106,A112,TOPREAL1:def 5
       .=LSeg(f,len f-'1) by A7,A14,A109,A113,TOPREAL1:def 5;
           LSeg((f|i2),j2) = LSeg(f,1) by A59,A92,SPPOL_2:3;
        then LSeg((f/^(i1-'1)),j1) /\ LSeg((f|i2),j2) ={f.1} by A114,Th54;
       hence x in {f.1} by A67,A68,A69,A70,XBOOLE_0:def 3;
       end;
      hence thesis by A65,XBOOLE_0:def 10;
     end;
    then L~(f/^(i1-'1)) /\ L~mid(f,1,i2) ={f.1} by A3,A34,A35,JORDAN3:25;
    then A115:L~f1 /\ L~mid(f,1,i2)={mid(f,1,i2).1} by A9,A18,JORDAN3:26;
         mid(f,1,i2) is_S-Seq by A3,A36,Th51;
     hence mid(f1,1,len f1-'1)^mid(f,1,i2) is_S-Seq
                         by A16,A21,A115,JORDAN3:80;
    end;
    case i2=1;
      then A116: mid(f,1,i2)=f|1 by JORDAN3:25
      .=<*f/.1*> by FINSEQ_5:23;
      A117:<*f/.1*>=<*f1/.len f1*> by A17,A19,A20,A23,FINSEQ_4:24;
      A118:mid(f1,1,len f1-'1)=f1|(len f1-'1) by A25,JORDAN3:25;
         len f1-'1+1=len f1 by A22,BINARITH:39;
     hence mid(f1,1,len f1-'1)^mid(f,1,i2) is_S-Seq
       by A16,A116,A117,A118,FINSEQ_5:24;
    end;
    end;
    then A119: g is_S-Seq by A1,A33,Th38;
   A120:i1<len f by A3,NAT_1:38;
      len mid(f,i1,len f-'1)=len f-'1-'i1+1 by A3,A5,A9,A13,A14,A15,JORDAN3:27;
   then 1<=len mid(f,i1,len f-'1) by NAT_1:29;
   then 1 in Seg len mid(f,i1,len f-'1) by FINSEQ_1:3;
   then 1 in dom mid(f,i1,len f-'1) by FINSEQ_1:def 3;
   then g.1=mid(f,i1,len f-'1).1 by A2,FINSEQ_1:def 7;
   then A121:g.1=f.i1 by A3,A5,A9,A14,A15,JORDAN3:27;
   A122:g/.len g=g.len g by A3,FINSEQ_4:24;
      f/.i1=f.i1 by A3,A120,FINSEQ_4:24;
   then f/.i1=g/.1 & f/.i2=g/.len g by A3,A11,A121,A122,FINSEQ_4:24;
 hence thesis by A119,TOPREAL4:def 1;
end;

theorem  for f being non constant standard special_circular_sequence,
  g being FinSequence of TOP-REAL 2,i1,i2 being Nat st
  g is_a_part<_of f,i1,i2 & i1>i2 holds
  L~g is_S-P_arc_joining f/.i1,f/.i2
proof let f be non constant standard special_circular_sequence,
  g be FinSequence of TOP-REAL 2,i1,i2 be Nat;
  assume A1:g is_a_part<_of f,i1,i2 & i1>i2;
   then Rev g is_a_part>_of f,i2,i1 by Th42;
then A2:L~Rev g is_S-P_arc_joining f/.i2,f/.i1 by A1,Th56;
   reconsider p2=f/.i2,p1=f/.i1 as Point of TOP-REAL 2;
   reconsider P=L~g as Subset of TOP-REAL 2;
      P is_S-P_arc_joining p2,p1 by A2,SPPOL_2:22;
 hence thesis by SPPOL_2:53;
end;

theorem Th58:for f being non constant standard special_circular_sequence,
  g being FinSequence of TOP-REAL 2,i1,i2 being Nat st
  g is_a_part>_of f,i1,i2 & i1<>i2 holds
  L~g is_S-P_arc_joining f/.i1,f/.i2
proof let f be non constant standard special_circular_sequence,
  g be FinSequence of TOP-REAL 2,i1,i2 be Nat;
  assume A1:g is_a_part>_of f,i1,i2 & i1<>i2;
     now per cases;
  case i1<i2;
  hence L~g is_S-P_arc_joining f/.i1,f/.i2 by A1,Th56;
 end;
 case i1>=i2;
   then i1>i2 by A1,REAL_1:def 5;
 hence L~g is_S-P_arc_joining f/.i1,f/.i2 by A1,Lm1;
 end;
 end;
 hence L~g is_S-P_arc_joining f/.i1,f/.i2;
end;

theorem Th59:for f being non constant standard special_circular_sequence,
  g being FinSequence of TOP-REAL 2,i1,i2 being Nat st
  g is_a_part<_of f,i1,i2 & i1<>i2 holds
  L~g is_S-P_arc_joining f/.i1,f/.i2
proof let f be non constant standard special_circular_sequence,
  g be FinSequence of TOP-REAL 2,i1,i2 be Nat;
  assume A1:g is_a_part<_of f,i1,i2 & i1<>i2;
     now per cases;
  case A2:i1>i2;
      Rev g is_a_part>_of f,i2,i1 by A1,Th42;
then A3:L~Rev g is_S-P_arc_joining f/.i2,f/.i1 by A2,Th56;
   reconsider p2=f/.i2,p1=f/.i1 as Point of TOP-REAL 2;
   reconsider P=L~g as Subset of TOP-REAL 2;
      P is_S-P_arc_joining p2,p1 by A3,SPPOL_2:22;
 hence L~g is_S-P_arc_joining f/.i1,f/.i2 by SPPOL_2:53;
 end;
 case i1<=i2;
   then A4:i1<i2 by A1,REAL_1:def 5;
      Rev g is_a_part>_of f,i2,i1 by A1,Th42;
then A5:L~(Rev g) is_S-P_arc_joining f/.i2,f/.i1 by A4,Lm1;
   reconsider p2=f/.i2,p1=f/.i1 as Point of TOP-REAL 2;
   reconsider P=L~g as Subset of TOP-REAL 2;
      P is_S-P_arc_joining p2,p1 by A5,SPPOL_2:22;
 hence L~g is_S-P_arc_joining f/.i1,f/.i2 by SPPOL_2:53;
 end;
 end;
 hence thesis;
end;

theorem Th60:for f being non constant standard special_circular_sequence,
 g being FinSequence of TOP-REAL 2,i1,i2 being Nat
  st g is_a_part_of f,i1,i2 & i1<>i2
 holds L~g is_S-P_arc_joining f/.i1,f/.i2
proof let f be non constant standard special_circular_sequence,
 g be FinSequence of TOP-REAL 2,i1,i2 be Nat;
 assume A1:g is_a_part_of f,i1,i2 & i1<>i2;
      now per cases by A1,Def4;
   case g is_a_part>_of f,i1,i2;
    hence L~g is_S-P_arc_joining f/.i1,f/.i2 by A1,Th58;
   end;
   case g is_a_part<_of f,i1,i2;
    hence L~g is_S-P_arc_joining f/.i1,f/.i2 by A1,Th59;
   end;
   end;
 hence thesis;
end;

theorem  for f being non constant standard special_circular_sequence,
 g being FinSequence of TOP-REAL 2,i1,i2 being Nat
  st g is_a_part_of f,i1,i2 & g.1<>g.len g
 holds L~g is_S-P_arc_joining f/.i1,f/.i2
proof let f be non constant standard special_circular_sequence,
 g be FinSequence of TOP-REAL 2,i1,i2 be Nat;
 assume A1:g is_a_part_of f,i1,i2 & g.1<>g.len g;
      now per cases by A1,Def4;
   case A2: g is_a_part>_of f,i1,i2;
     then A3:1<=i1 & i1+1<=len f &
     1<=i2 & i2+1<=len f & g.len g=f.i2 & 1<=len g & len g<len f &
     (for i being Nat st 1<=i & i<=len g holds g.i=f.S_Drop((i1+i)-'1,f))
                                  by Def2;
     then A4:1<len f by AXIOMS:22;
        i1+1-1<=len f-1 by A3,REAL_1:49;
     then A5:i1<=len f-'1 by A4,SCMFSA_7:3;
        now assume A6: i1=i2;
          g.1=f.S_Drop((i1+1)-'1,f) by A3 .=f.S_Drop(i1,f) by BINARITH:39
       .=f.i1 by A3,A5,Th34;
      hence contradiction by A1,A2,A6,Def2;
     end;
    hence L~g is_S-P_arc_joining f/.i1,f/.i2 by A1,Th60;
   end;
   case A7: g is_a_part<_of f,i1,i2;
     then A8:1<=i1 & i1+1<=len f &
     1<=i2 & i2+1<=len f & g.len g=f.i2 & 1<=len g & len g<len f &
     (for i being Nat st 1<=i & i<=len g holds g.i=f.S_Drop(len f +i1-'i,f))
                                   by Def3;
     then A9:1<len f by AXIOMS:22;
        i1+1-1<=len f-1 by A8,REAL_1:49;
     then A10:i1<=len f-'1 by A9,SCMFSA_7:3;
        now assume A11: i1=i2;
          g.1=f.S_Drop(len f+i1-'1,f) by A8
       .=f.S_Drop(len f-'1+1+i1-'1,f) by A9,AMI_5:4
       .=f.S_Drop(len f-'1+i1+1-'1,f)
       .=f.S_Drop(len f-'1+i1,f) by BINARITH:39
       .=f.S_Drop(i1,f) by Th35
       .=f.i1 by A8,A10,Th34;
      hence contradiction by A1,A7,A11,Def3;
     end;
    hence L~g is_S-P_arc_joining f/.i1,f/.i2 by A1,Th60;
   end;
   end;
 hence thesis;
end;

theorem Th62:for f being non constant standard special_circular_sequence,
  i1,i2 being Nat st
  1<=i1 & i1+1<=len f & 1<=i2 & i2+1<=len f & i1<>i2 holds
  ex g1,g2 being FinSequence of TOP-REAL 2 st
  g1 is_a_part_of f,i1,i2 & g2 is_a_part_of f,i1,i2
  & (L~g1)/\(L~g2)={f.i1,f.i2} & (L~g1) \/ (L~g2)=L~f &
  L~g1 is_S-P_arc_joining f/.i1,f/.i2 &
  L~g2 is_S-P_arc_joining f/.i1,f/.i2 &
  for g being FinSequence of TOP-REAL 2 st g is_a_part_of f,i1,i2 holds
  g=g1 or g=g2
proof let f be non constant standard special_circular_sequence,
  i1,i2 be Nat;
  assume A1:1<=i1 & i1+1<=len f & 1<=i2 & i2+1<=len f & i1<>i2;
      now per cases;
   case A2: i1<=i2;
     then A3:i1<i2 by A1,REAL_1:def 5;
     A4:i2<len f by A1,NAT_1:38;
     set h1=mid(f,i1,i2),h2=mid(f,i1,1)^mid(f,len f-'1,i2);
     A5:mid(f,i1,i2) is_a_part>_of f,i1,i2
       by A1,A2,A4,Th43;
        mid(f,i1,1)^mid(f,len f-'1,i2)
      is_a_part<_of f,i1,i2 by A1,A3,A4,Th46;
    then A6:h1 is_a_part_of f,i1,i2 & h2 is_a_part_of f,i1,i2 by A5,Def4;
    A7:(L~h1)/\(L~h2)={f.i1,f.i2} & (L~h1) \/ (L~h2)=L~f
                                  by A1,A3,A4,Th55;
    A8:L~h1 is_S-P_arc_joining f/.i1,f/.i2 by A3,A5,Th56;
    A9:L~h2 is_S-P_arc_joining f/.i1,f/.i2 by A1,A6,Th60;
       for g being FinSequence of TOP-REAL 2 st g is_a_part_of f,i1,i2 holds
    g=h1 or g=h2
    proof let g be FinSequence of TOP-REAL 2;
     assume A10: g is_a_part_of f,i1,i2;
         now per cases by A10,Def4;
      case g is_a_part>_of f,i1,i2;
       hence g=h1 or g=h2 by A2,Th37;
      end;
      case g is_a_part<_of f,i1,i2;
       hence g=h1 or g=h2 by A3,Th40;
      end;
      end;
     hence g=h1 or g=h2;
    end;
    hence
       ex g1,g2 being FinSequence of TOP-REAL 2 st
    g1 is_a_part_of f,i1,i2 & g2 is_a_part_of f,i1,i2
    & (L~g1)/\(L~g2)={f.i1,f.i2} & (L~g1) \/ (L~g2)=L~f &
    L~g1 is_S-P_arc_joining f/.i1,f/.i2 &
    L~g2 is_S-P_arc_joining f/.i1,f/.i2 &
    for g being FinSequence of TOP-REAL 2 st g is_a_part_of f,i1,i2 holds
    g=g1 or g=g2 by A6,A7,A8,A9;
   end;
   case A11:i1>i2;
     A12:i1<len f by A1,NAT_1:38;
     set h1=mid(f,i2,i1),h2=mid(f,i2,1)^mid(f,len f-'1,i1);
     set h3=Rev h1,h4=Rev h2;
     A13:(L~h1)/\(L~h2)={f.i2,f.i1} & (L~h1) \/ (L~h2)=L~f by A1,A11,A12,Th55;
     A14:L~h1=L~h3 by SPPOL_2:22;
        mid(f,i2,i1) is_a_part>_of f,i2,i1 by A1,A11,A12,Th43;
     then A15:Rev mid(f,i2,i1) is_a_part<_of f,i1,i2 by Th41;
        mid(f,i2,1)^mid(f,len f-'1,i1)
      is_a_part<_of f,i2,i1 by A1,A11,A12,Th46;
     then Rev (mid(f,i2,1)^mid(f,len f-'1,i1))
      is_a_part>_of f,i1,i2 by Th42;
    then A16:h3 is_a_part_of f,i1,i2 & h4 is_a_part_of f,i1,i2 by A15,Def4;
    A17:(L~h3)/\(L~h4)={f.i1,f.i2} & (L~h3) \/
 (L~h4)=L~f by A13,A14,SPPOL_2:22;
    A18:L~h3 is_S-P_arc_joining f/.i1,f/.i2 by A11,A15,Th59;
    A19:L~h4 is_S-P_arc_joining f/.i1,f/.i2 by A1,A16,Th60;
       for g being FinSequence of TOP-REAL 2 st g is_a_part_of f,i1,i2 holds
    g=h3 or g=h4
    proof let g be FinSequence of TOP-REAL 2;
     assume A20: g is_a_part_of f,i1,i2;
         now per cases by A20,Def4;
      case g is_a_part>_of f,i1,i2;
        then Rev g is_a_part<_of f,i2,i1 by Th41;
        then Rev g= mid(f,i2,1)^mid(f,len f-'1,i1) by A11,Th40;
       hence g=h3 or g=h4 by FINSEQ_6:29;
      end;
      case g is_a_part<_of f,i1,i2;
        then Rev g is_a_part>_of f,i2,i1 by Th42;
        then Rev g= mid(f,i2,i1) by A11,Th37;
       hence g=h3 or g=h4 by FINSEQ_6:29;
      end;
      end;
     hence g=h3 or g=h4;
    end;
    hence
       ex g1,g2 being FinSequence of TOP-REAL 2 st
    g1 is_a_part_of f,i1,i2 & g2 is_a_part_of f,i1,i2
    & (L~g1)/\(L~g2)={f.i1,f.i2} & (L~g1) \/ (L~g2)=L~f &
    L~g1 is_S-P_arc_joining f/.i1,f/.i2 &
    L~g2 is_S-P_arc_joining f/.i1,f/.i2 &
    for g being FinSequence of TOP-REAL 2 st g is_a_part_of f,i1,i2 holds
    g=g1 or g=g2 by A16,A17,A18,A19;
   end;
   end;
 hence thesis;
end;

reserve g, g1, g2 for FinSequence of TOP-REAL 2;

theorem
   for f being non constant standard special_circular_sequence,
    P being non empty Subset of TOP-REAL 2 st P = L~f
 holds P is_simple_closed_curve
proof let f be non constant standard special_circular_sequence,
          P be non empty Subset of TOP-REAL 2 such that
A1:     P = L~f;
     len f>4 by GOBOARD7:36;
  then A2:1<=1 & 1<=1+1 & 1<=len f & 1+1<len f & 1+1+1<=len f & 1<>1+1
     by AXIOMS:22;
  set i1=1,i2=1+1;
  consider g1,g2 being FinSequence of TOP-REAL 2 such that
  A3:g1 is_a_part_of f,i1,i2 & g2 is_a_part_of f,i1,i2
  & (L~g1)/\(L~g2)={f.i1,f.i2} & (L~g1) \/ (L~g2)=L~f &
   L~g1 is_S-P_arc_joining f/.i1,f/.i2 &
   L~g2 is_S-P_arc_joining f/.i1,f/.i2 &
  for g being FinSequence of TOP-REAL 2 st g is_a_part_of f,i1,i2 holds
  g=g1 or g=g2 by A2,Th62;
  reconsider L1 = L~g1, L2 = L~g2 as
   non empty Subset of TOP-REAL 2 by A3,TARSKI:def 2;
  A4:L1 is_an_arc_of f/.i1,f/.i2 by A3,TOPREAL4:3;
  A5:L2 is_an_arc_of f/.i1,f/.i2 by A3,TOPREAL4:3;
  A6:f.i1=f/.i1 by A2,FINSEQ_4:24;
  A7:f.i2=f/.i2 by A2,FINSEQ_4:24;
  A8:f/.i1<>f/.i2 by A2,GOBOARD7:38;
     f/.i1 in (L~g1)/\(L~g2) by A3,A6,TARSKI:def 2;
  then f/.i1 in (L~g1) by XBOOLE_0:def 3;
  then A9:f/.i1 in P by A1,A3,XBOOLE_0:def 2;
     f/.i2 in (L~g1)/\(L~g2) by A3,A7,TARSKI:def 2;
  then f/.i2 in (L~g2) by XBOOLE_0:def 3;
  then f/.i2 in P by A1,A3,XBOOLE_0:def 2;
 hence thesis by A1,A3,A4,A5,A6,A7,A8,A9,TOPREAL2:6;
end;

theorem Th64: for f being non constant standard special_circular_sequence,
 g1,g2 st g1 is_a_part>_of f,i1,i2 & g2 is_a_part>_of f,i1,i2
 holds g1=g2
proof let f be non constant standard special_circular_sequence,
  g1,g2;assume A1:g1 is_a_part>_of f,i1,i2 & g2 is_a_part>_of f,i1,i2;
     now per cases;
  case A2:i1<=i2;
    then g1=mid(f,i1,i2) by A1,Th37;
   hence g1=g2 by A1,A2,Th37;
  end;
  case A3:i1>i2;
    then g1=mid(f,i1,len f-'1)^mid(f,1,i2) by A1,Th38;
   hence g1=g2 by A1,A3,Th38;
  end;
  end;
 hence g1=g2;
end;

theorem Th65:for f being non constant standard special_circular_sequence,
 g1,g2 st g1 is_a_part<_of f,i1,i2 & g2 is_a_part<_of f,i1,i2
 holds g1=g2
proof let f be non constant standard special_circular_sequence,
  g1,g2;assume A1:g1 is_a_part<_of f,i1,i2 & g2 is_a_part<_of f,i1,i2;
  per cases;
  suppose A2:i1>=i2;
    then g1=mid(f,i1,i2) by A1,Th39;
   hence g1=g2 by A1,A2,Th39;
  end;
  suppose A3:i1<i2;
    then g1=mid(f,i1,1)^mid(f,len f-'1,i2) by A1,Th40;
   hence g1=g2 by A1,A3,Th40;
end;
end;

theorem Th66:for f being non constant standard special_circular_sequence,
 g1,g2 st i1<>i2 & g1 is_a_part>_of f,i1,i2 & g2 is_a_part<_of f,i1,i2
 holds g1.2<>g2.2
proof let f be non constant standard special_circular_sequence,
 g1,g2;assume A1:i1<>i2 &
   g1 is_a_part>_of f,i1,i2 & g2 is_a_part<_of f,i1,i2;
 then A2:1<=i1 & i1+1<=len f & 1<=i2 & i2+1<=len f &
 g1 is_a_part>_of f,i1,i2 & g2 is_a_part<_of f,i1,i2 by Def2;
   A3: 1<=1+i1 by NAT_1:29;
 then A4:1<=len f by A2,AXIOMS:22;
 then A5:len f-'1=len f-1 by SCMFSA_7:3;
   A6: len f-'1<len f-'1+1 by NAT_1:38;
 then A7:len f-'1<len f by A5;
 A8:i2<len f by A2,NAT_1:38;
 A9:i1<len f by A2,NAT_1:38;
     now per cases;
  case A10: i1<=i2;
       now per cases by A10,REAL_1:def 5;
    case i1=i2;
     hence contradiction by A1;
    end;
    case A11:i1<i2;
      then A12:g2=mid(f,i1,1)^mid(f,len f-'1,i2) by A1,Th40;
         i1+1-1<=len f-1 by A2,REAL_1:49;
      then A13:1<=len f-'1 by A2,A5,AXIOMS:22;
      A14:len mid(f,i1,1)=i1-'1+1 by A2,A9,Th21;
         now per cases by A2,REAL_1:def 5;
      case A15:1<i1;
        then A16:1+1<=i1 by NAT_1:38;
         1+1<=i1-1+1 by A15,NAT_1:38;
        then A17:2<=len mid(f,i1,1) by A2,A14,SCMFSA_7:3;
        A18:g2.2=(mid(f,i1,1)^mid(f,len f-'1,i2)).2 by A1,A11,Th40
        .=mid(f,i1,1).2 by A17,SCMFSA_7:9
        .=f.(i1-'2+1) by A4,A9,A15,A17,JORDAN3:27
        .=f.(i1-(1+1)+1) by A16,SCMFSA_7:3
        .=f.(i1-1)
        .=f.(i1-'1) by A2,SCMFSA_7:3;
           i1+1<=i2 by A11,NAT_1:38;
        then 1+i1-i1<=i2-i1 by REAL_1:49;
        then 1<=i2-'i1 by JORDAN3:1;
        then 1+1<=i2-'i1+1 by AXIOMS:24;
        then A19:2<=len mid(f,i1,i2) by A2,A8,A9,A11,JORDAN3:27;
        A20:g1.2=mid(f,i1,i2).2 by A1,A11,Th37
        .=f.(2+i1-'1) by A2,A8,A9,A11,A19,JORDAN3:27
        .=f.(1+1+i1-1) by Th2
        .=f.(i1+1);
           1+1<=i1 by A15,NAT_1:38;
then A21:    1+1-1<=i1-1 by REAL_1:49;
        then A22:1<=i1-'1 by JORDAN3:1;
A23:     now assume A24:1=i1-'1 & i1+1=len f; then 1=i1-1 by JORDAN3:1;
          then len f=1+1+1 by A24,XCMPLX_1:27;
         hence contradiction by GOBOARD7:36;
        end;
           now per cases by A23;
        case A25: 1<>i1-'1;
             i1-'1<i1-'1+1 by NAT_1:38;
          then i1-'1<i1 by A2,AMI_5:4;
          then A26:1<i1-'1 & i1-'1<i1+1 & i1+1<=len f
             by A1,A22,A25,Def2,NAT_1:38,REAL_1:def 5;
          then A27:f/.(i1-'1)<>f/.(i1+1) by GOBOARD7:39;
             i1-'1<len f by A26,AXIOMS:22;
          then A28:f.(i1-'1)=f/.(i1-'1) by A22,FINSEQ_4:24;
             1<i1+1 by A2,NAT_1:38;
         hence thesis by A2,A18,A20,A27,A28,FINSEQ_4:24;
        end;
        case A29: i1+1<>len f;
             i1-'1<i1-'1+1 by NAT_1:38;
          then i1-'1<i1 by A2,AMI_5:4;
          then A30:1<=i1-'1 & i1-'1<i1+1 & i1+1<len f
             by A2,A21,A29,JORDAN3:1,NAT_1:38,REAL_1:def 5;
          then A31:f/.(i1-'1)<>f/.(i1+1) by GOBOARD7:38;
             i1-'1<len f by A30,AXIOMS:22;
          then A32:f.(i1-'1)=f/.(i1-'1) by A30,FINSEQ_4:24;
             1<i1+1 by A2,NAT_1:38;
         hence thesis by A2,A18,A20,A31,A32,FINSEQ_4:24;
        end;
        end;
       hence thesis;
      end;
      case A33:1=i1;
        A34:len mid(f,i1,1)=i1-'1+1 by A2,A9,Th21
        .=0+1 by A33,GOBOARD9:1 .=1;
        A35:len g2=len f+i1-'i2 by A1,A11,Th40
        .=len f+1-i2 by A8,A33,Th2;
           i2+1+1<=len f+1 by A2,AXIOMS:24;
        then 1+i2+1-i2<=len f+1-i2 by REAL_1:49;
        then A36:1+1<=len g2 by A35;
        A37:len mid(f,i1,1)=i1-'1+1 by A2,A9,Th21
        .=0+1 by A33,GOBOARD9:1 .=1;
        A38:g2.2 =mid(f,len f-'1,i2).(2-len mid(f,i1,1))
                      by A12,A34,A36,JORDAN3:15
        .=f.(len f-'1) by A2,A5,A6,A8,A13,A37,JORDAN3:27;
           i1+1<=i2 by A11,NAT_1:38;
        then 1+i1-i1<=i2-i1 by REAL_1:49;
        then 1<=i2-'i1 by JORDAN3:1;
        then 1+1<=i2-'i1+1 by AXIOMS:24;
        then A39:2<=len mid(f,i1,i2) by A2,A8,A9,A11,JORDAN3:27;
        A40:g1.2=mid(f,i1,i2).2 by A1,A11,Th37
            .=f.(2+i1-'1) by A2,A8,A9,A11,A39,JORDAN3:27
        .=f.(1+1+i1-1) by Th2
        .=f.(i1+1);
           len f>4 by GOBOARD7:36;
        then len f-1>3+1-1 by REAL_1:54;
        then i1+1<len f-'1 by A5,A33,AXIOMS:22;
        then A41:f/.(i1+1)<>f/.(len f-'1) by A5,A6,A33,GOBOARD7:39;
        A42:f.(len f-'1)=f/.(len f-'1) by A5,A6,A13,FINSEQ_4:24;
           1<i1+1 by A2,NAT_1:38;
       hence thesis by A2,A38,A40,A41,A42,FINSEQ_4:24;
      end;
      end;
     hence thesis;
    end;
    end;
   hence thesis;
  end;
  case A43:i1>i2;
    then A44:g1=mid(f,i1,len f-'1)^mid(f,1,i2) by A1,Th38;
       1<i1 by A2,A43,AXIOMS:22;
    then A45:1+1<=i1 by NAT_1:38;
      A46: i1+1-1<=len f-1 by A2,REAL_1:49;
    then 1<=len f-'1 by A2,A5,AXIOMS:22;
    then A47:len mid(f,i1,len f-'1)=len f-'1-'i1+1 by A2,A5,A6,A9,A46,
JORDAN3:27;
      A48: i1+1-1<=len f-1 by A2,REAL_1:49;
A49: now per cases by A46,REAL_1:def 5;
    case i1<len f-1; then i1+1<=len f-'1 by A5,NAT_1:38;
      then A50: 1+(i1+1)<=1+(len f-'1) by AXIOMS:24;
      then A51: 1+(i1+1)-i1<=1+(len f-'1)-i1 by REAL_1:49;
       1+1+i1-i1<=1+(len f-'1)-i1 by A50,REAL_1:49;
  then 1+1<=1+((len f-'1)-i1);
      then A52:2<=len mid(f,i1,len f-'1) by A5,A47,A48,SCMFSA_7:3;
     thus g1.2=(mid(f,i1,len f-'1)^mid(f,1,i2)).2 by A1,A43,Th38
      .=mid(f,i1,len f-'1).2 by A52,SCMFSA_7:9
      .=f.(1+1+i1-1) by A2,A5,A6,A48,A51,JORDAN3:31
      .=f.(i1+1);
    end;
    case A53:i1=len f-1;
      then A54:len mid(f,i1,len f-'1)=len f-'1-'(len f-'1)+1
        by A2,A5,A7,JORDAN3:27
      .=0+1 by GOBOARD9:1 .=1;
      A55:len g1=len f-'1+1+i2-'(len f-'1) by A1,A5,A43,A53,Th38
      .=len f-'1+(1+i2)-'(len f-'1)
      .=1+i2 by BINARITH:39;
A56:   1+1<=i2+1 by A2,AXIOMS:24;
      A57:len mid(f,i1,len f-'1)=len f-'1-'(len f-'1)+1
         by A2,A5,A6,A53,JORDAN3:27
      .=0+1 by GOBOARD9:1 .=1;
      thus g1.2 =mid(f,1,i2).(2-len mid(f,i1,len f-'1))
             by A44,A54,A55,A56,JORDAN3:15
      .=f.1 by A2,A4,A8,A57,JORDAN3:27
      .=f.(i1+1) by A3,A53,Th14;
    end;
    end;
       i2+1<=i1 by A43,NAT_1:38;
    then 1+i2-i2<=i1-i2 by REAL_1:49;
    then 1<=i1-'i2 by JORDAN3:1;
    then 1+1<=i1-'i2+1 by AXIOMS:24;
    then A58:2<=len mid(f,i1,i2) by A2,A8,A9,A43,JORDAN3:27;
    A59:g2.2=mid(f,i1,i2).2 by A1,A43,Th39
    .=f.(i1-'2+1) by A2,A8,A9,A43,A58,JORDAN3:27
    .=f.(i1-(1+1)+1) by A45,SCMFSA_7:3
    .=f.(i1-1) .=f.(i1-'1) by A2,SCMFSA_7:3;
       1<i1 by A2,A43,AXIOMS:22;
    then 1+1<=i1 by NAT_1:38;
then A60:1+1-1<=i1-1 by REAL_1:49;
    then A61:1<=i1-'1 by JORDAN3:1;
A62: now assume A63:1=i1-'1 & i1+1=len f; then 1=i1-1 by JORDAN3:1;
      then len f=1+1+1 by A63,XCMPLX_1:27;
     hence contradiction by GOBOARD7:36;
    end;
       now per cases by A62;
    case A64: 1<>i1-'1;
         i1-'1<i1-'1+1 by NAT_1:38;
      then i1-'1<i1 by A2,AMI_5:4;
      then A65:1<i1-'1 & i1-'1<i1+1 & i1+1<=len f
        by A1,A61,A64,Def2,NAT_1:38,REAL_1:def 5;
      then A66:f/.(i1-'1)<>f/.(i1+1) by GOBOARD7:39;
         i1-'1<len f by A65,AXIOMS:22;
      then A67:f.(i1-'1)=f/.(i1-'1) by A61,FINSEQ_4:24;
         1<i1+1 by A2,NAT_1:38;
     hence thesis by A2,A49,A59,A66,A67,FINSEQ_4:24;
    end;
    case A68: i1+1<>len f;
         i1-'1<i1-'1+1 by NAT_1:38;
      then i1-'1<i1 by A2,AMI_5:4;
      then A69:1<=i1-'1 & i1-'1<i1+1 & i1+1<len f
         by A2,A60,A68,JORDAN3:1,NAT_1:38,REAL_1:def 5;
      then A70:f/.(i1-'1)<>f/.(i1+1) by GOBOARD7:38;
         i1-'1<len f by A69,AXIOMS:22;
      then A71:f.(i1-'1)=f/.(i1-'1) by A69,FINSEQ_4:24;
         1<i1+1 by A2,NAT_1:38;
     hence thesis by A2,A49,A59,A70,A71,FINSEQ_4:24;
    end;
    end;
   hence thesis;
  end;
  end;
 hence thesis;
end;

theorem Th67:for f being non constant standard special_circular_sequence,
 g1,g2 st i1<>i2 & g1 is_a_part_of f,i1,i2 & g2 is_a_part_of f,i1,i2
 & g1.2=g2.2 holds g1=g2
proof let f be non constant standard special_circular_sequence,
  g1,g2;assume A1:i1<>i2 & g1 is_a_part_of f,i1,i2 & g2 is_a_part_of f,i1,i2
   & g1.2=g2.2;
  per cases by A1,Def4;
  suppose A2:g1 is_a_part>_of f,i1,i2;
       now per cases by A1,Def4;
    case g2 is_a_part>_of f,i1,i2;
     hence g1=g2 by A2,Th64;
    end;
    case g2 is_a_part<_of f,i1,i2;
     hence contradiction by A1,A2,Th66;
    end;
    end;
   hence g1=g2;
  end;
  suppose A3:g1 is_a_part<_of f,i1,i2;
       now per cases by A1,Def4;
    case g2 is_a_part>_of f,i1,i2;
     hence contradiction by A1,A3,Th66;
    end;
    case g2 is_a_part<_of f,i1,i2;
     hence g1=g2 by A3,Th65;
    end;
    end;
   hence g1=g2;
end;
end;

definition let f be non constant standard special_circular_sequence,
 i1,i2 be Nat;
 assume A1:1<=i1 & i1+1<=len f & 1<=i2 & i2+1<=len f & i1<>i2;
 func Lower (f,i1,i2) -> FinSequence of TOP-REAL 2 means
    it is_a_part_of f,i1,i2 &
   ((f/.(i1+1))`1<(f/.i1)`1 or (f/.(i1+1))`2<(f/.i1)`2 implies it.2=f.(i1+1))&
   ((f/.(i1+1))`1>=(f/.i1)`1 & (f/.(i1+1))`2>=(f/.i1)`2
                implies it.2=f.S_Drop(i1-'1,f));
correctness
proof
 A2:i1<len f by A1,NAT_1:38;
 then A3:1<len f by A1,AXIOMS:22;
 then 1+1<=len f by NAT_1:38;
 then A4: 1+1-1<=len f-1 by REAL_1:49;
 then A5:1<=len f-'1 by JORDAN3:1;
 A6:i2<len f by A1,NAT_1:38;
 A7:i1-'1=i1-1 by A1,SCMFSA_7:3;
 A8:len f-'1=len f-1 by A3,SCMFSA_7:3;
   A9: i2+1-1<=len f-1 by A1,REAL_1:49;
   A10: i1+1-1<=len f-1 by A1,REAL_1:49;
    len f<len f+1 by NAT_1:38;
 then A11: len f-1<len f+1-1 by REAL_1:54;
      now per cases;
   case A12: i1<=i2;
     then A13:i1<i2 by A1,REAL_1:def 5;
        now per cases;
     case A14:(f/.(i1+1))`1<(f/.i1)`1 or (f/.(i1+1))`2<(f/.i1)`2;
       set g=mid(f,i1,i2);
A15:    g is_a_part>_of f,i1,i2 by A1,A6,A12,Th43;
       A16:len mid(f,i1,i2)=i2-'i1+1 by A1,A2,A6,A12,JORDAN3:27;
          i1+1<=i2 by A13,NAT_1:38;
       then (1+i1)+1<=i2+1 by AXIOMS:24;
       then 2+i1-i1<=i2+1-i1 by REAL_1:49;
       then 2+i1-i1<=i2-i1+1;
       then 1<2 & 2<=len mid(f,i1,i2) by A12,A16,SCMFSA_7:3;
       then g.2=f.(2+i1-'1)
          by A1,A2,A6,A12,JORDAN3:27
       .=f.(i1+1+1-1) by Th2 .=f.(i1+1);
      hence g is_a_part_of f,i1,i2 &
       ((f/.(i1+1))`1<(f/.i1)`1 or (f/.(i1+1))`2<(f/.i1)`2
                                    implies g.2=f.(i1+1))&
       ((f/.(i1+1))`1>=(f/.i1)`1 & (f/.(i1+1))`2>=(f/.i1)`2
                implies g.2=f.S_Drop(i1-'1,f)) by A14,A15,Def4;
     end;
     case A17:(f/.(i1+1))`1>=(f/.i1)`1 & (f/.(i1+1))`2>=(f/.i1)`2;
       set g=mid(f,i1,1)^mid(f,len f-'1,i2);
A18:    g is_a_part<_of f,i1,i2 by A1,A6,A13,Th46;
       A19:len mid(f,i1,1)=i1-'1+1 by A1,A2,Th21;
          now per cases;
       case A20:1<i1;
         then A21:1+1<=i1 by NAT_1:38;
         then A22:f.(i1-'(1+1)+1)=f.(i1-(1+1)+1) by SCMFSA_7:3
         .=f.(i1-1) .=f.(i1-'1) by A1,SCMFSA_7:3;
            1+1<=i1-1+1 by A20,NAT_1:38;
         then A23:2<=len mid(f,i1,1) by A1,A19,SCMFSA_7:3;
         A24:g.2 =mid(f,i1,1).2 by A7,A19,A21,SCMFSA_7:9
         .=f.(i1-'(1+1)+1) by A2,A3,A20,A23,JORDAN3:27;
            1+1-1<=i1-1 by A21,REAL_1:49;
         then 1<=i1-'1 & i1-'1<=len f-'1 by A2,A7,A8,REAL_1:54;
        hence g is_a_part_of f,i1,i2 &
         ((f/.(i1+1))`1<(f/.i1)`1 or (f/.(i1+1))`2<(f/.i1)`2
                       implies g.2=f.(i1+1))&
         ((f/.(i1+1))`1>=(f/.i1)`1 & (f/.(i1+1))`2>=(f/.i1)`2
                implies g.2=f.S_Drop(i1-'1,f)) by A17,A18,A22,A24,Def4,Th34;
       end;
       case A25: 1>=i1;
         then i1=1 by A1,AXIOMS:21;
         then A26:i1-'1=0 by GOBOARD9:1;
         A27:len mid(f,i1,1)=len mid(f,1,1) by A1,A25,AXIOMS:21
           .=1 by A3,Th27;
         A28:len mid(f,len f-'1,i2)=len f-'1-'i2+1 by A1,A8,A9,A11,Th21
         .=len f-1-i2+1 by A8,A9,SCMFSA_7:3
         .=len f-i2;
            len f-i2>=i2+1-i2 by A1,REAL_1:49;
then A29:      1+1<=len mid(f,i1,1)+len mid(f,len f-'1,i2)
              by A27,A28,AXIOMS:24;
         A30:1<=len f-'1-'i2+1 by NAT_1:29;
         A31:g.2 =mid(f,len f-'1,i2).(2-len mid(f,i1,1)) by A27,A29,JORDAN3:15
         .=mid(f,len f-'1,i2).(2-(i1-'1+1)) by A1,A2,Th21
         .=mid(f,len f-'1,i2).(1+1-(1-'1+1)) by A1,A25,AXIOMS:21
         .=mid(f,len f-'1,i2).(1+1-(0+1)) by GOBOARD9:1
         .=f.(len f-'1-'1+1) by A1,A8,A9,A11,A30,Th24
         .=f.(len f-'1) by A5,AMI_5:4;
            S_Drop(i1-'1,f)=S_Drop(i1-'1+(len f-'1),f) by Th35
                        .=len f-'1 by A5,A26,Th34;
        hence g is_a_part_of f,i1,i2 &
         ((f/.(i1+1))`1<(f/.i1)`1 or (f/.(i1+1))`2<(f/.i1)`2
                       implies g.2=f.(i1+1))&
         ((f/.(i1+1))`1>=(f/.i1)`1 & (f/.(i1+1))`2>=(f/.i1)`2
                implies g.2=f.S_Drop(i1-'1,f)) by A17,A18,A31,Def4;
       end;
       end;
       hence g is_a_part_of f,i1,i2 &
       ((f/.(i1+1))`1<(f/.i1)`1 or (f/.(i1+1))`2<(f/.i1)`2
                       implies g.2=f.(i1+1))&
       ((f/.(i1+1))`1>=(f/.i1)`1 & (f/.(i1+1))`2>=(f/.i1)`2
                implies g.2=f.S_Drop(i1-'1,f));
     end;
     end;
      hence ex g st g is_a_part_of f,i1,i2 &
       ((f/.(i1+1))`1<(f/.i1)`1 or (f/.(i1+1))`2<(f/.i1)`2
                       implies g.2=f.(i1+1))&
       ((f/.(i1+1))`1>=(f/.i1)`1 & (f/.(i1+1))`2>=(f/.i1)`2
                implies g.2=f.S_Drop(i1-'1,f));
   end;
   case A32:i1>i2; then i1>1 by A1,AXIOMS:22;
       then A33:1+1<=i1 by NAT_1:38; then A34: 1+1-1<=i1-1 by REAL_1:49;
        now per cases;
     case A35:(f/.(i1+1))`1>=(f/.i1)`1 & (f/.(i1+1))`2>=(f/.i1)`2;
       set g=mid(f,i1,i2);
A36:    g is_a_part<_of f,i1,i2 by A1,A2,A32,Th44;
       A37:len mid(f,i1,i2)=i1-'i2+1 by A1,A2,A6,A32,JORDAN3:27;
          i2+1<=i1 by A32,NAT_1:38;
       then (1+i2)+1<=i1+1 by AXIOMS:24;
       then 2+i2-i2<=i1+1-i2 by REAL_1:49;
       then 2+i2-i2<=i1-i2+1;
       then 1<2 & 2<=len mid(f,i1,i2) by A32,A37,SCMFSA_7:3;
       then A38:g.2=f.(i1-'2+1) by A1,A2,A6,A32,JORDAN3:27
       .=f.(i1-(1+1)+1) by A33,SCMFSA_7:3
       .=f.(i1-1)  .=f.(i1-'1) by A1,SCMFSA_7:3;
          i1-1<len f-1 by A2,REAL_1:54;
      hence g is_a_part_of f,i1,i2 &
       ((f/.(i1+1))`1<(f/.i1)`1 or (f/.(i1+1))`2<(f/.i1)`2
                                    implies g.2=f.(i1+1))&
       ((f/.(i1+1))`1>=(f/.i1)`1 & (f/.(i1+1))`2>=(f/.i1)`2
                implies g.2=f.S_Drop(i1-'1,f))
                  by A7,A8,A34,A35,A36,A38,Def4,Th34;
     end;
     case A39:(f/.(i1+1))`1<(f/.i1)`1 or (f/.(i1+1))`2<(f/.i1)`2;
       set g=mid(f,i1,len f-'1)^mid(f,1,i2);
A40:    g is_a_part>_of f,i1,i2 by A1,A2,A32,Th45;
       A41:len mid(f,i1,len f-'1)=len f-'1-'i1+1
                           by A1,A2,A4,A8,A10,A11,JORDAN3:27
       .=len f-1-i1+1 by A8,A10,SCMFSA_7:3
       .=len f-i1;
          now per cases;
       case i1+1<len f;
         then i1+1+1<=len f by NAT_1:38;
         then A42: i1+2-i1<=len f-i1 by REAL_1:49;
         then g.2 =mid(f,i1,len f-'1).2 by A41,SCMFSA_7:9
         .=f.(2+i1-'1) by A1,A2,A4,A8,A10,A11,A41,A42,JORDAN3:27
         .=f.(1+1+i1-1) by Th2
         .=f.(i1+1);
        hence g is_a_part_of f,i1,i2 &
         ((f/.(i1+1))`1<(f/.i1)`1 or (f/.(i1+1))`2<(f/.i1)`2
                       implies g.2=f.(i1+1))&
         ((f/.(i1+1))`1>=(f/.i1)`1 & (f/.(i1+1))`2>=(f/.i1)`2
                implies g.2=f.S_Drop(i1-'1,f)) by A39,A40,Def4;
       end;
       case A43: i1+1>=len f;
         then i1+1=len f by A1,AXIOMS:21;
         then A44:len mid(f,i1,len f-'1)=len mid(f,len f-'1,len f-'1)
           by A8,XCMPLX_1:26
         .=1 by A4,A8,A11,Th27;
            len mid(f,1,i2)=i2-'1+1 by A1,A3,A6,JORDAN3:27
          .=i2-1+1 by A1,SCMFSA_7:3 .=i2;
then 1+1<=len mid(f,i1,len f-'1)+len mid(f,1,i2)
              by A1,A44,AXIOMS:24;
         then g.2 =mid(f,1,i2).(2-(i1+1-i1)) by A44,JORDAN3:15
         .=f.1 by A1,A3,A6,JORDAN3:27 .=f/.1 by A3,FINSEQ_4:24
         .=f/.len f by FINSEQ_6:def 1
         .=f.len f by A3,FINSEQ_4:24;
        hence g is_a_part_of f,i1,i2 &
         ((f/.(i1+1))`1<(f/.i1)`1 or (f/.(i1+1))`2<(f/.i1)`2
                       implies g.2=f.(i1+1))&
         ((f/.(i1+1))`1>=(f/.i1)`1 & (f/.(i1+1))`2>=(f/.i1)`2
                implies g.2=f.S_Drop(i1-'1,f))
                  by A1,A39,A40,A43,Def4,AXIOMS:21;
       end;
       end;
       hence g is_a_part_of f,i1,i2 &
       ((f/.(i1+1))`1<(f/.i1)`1 or (f/.(i1+1))`2<(f/.i1)`2
                       implies g.2=f.(i1+1))&
       ((f/.(i1+1))`1>=(f/.i1)`1 & (f/.(i1+1))`2>=(f/.i1)`2
                implies g.2=f.S_Drop(i1-'1,f));
     end;
     end;
       hence ex g st g is_a_part_of f,i1,i2 &
       ((f/.(i1+1))`1<(f/.i1)`1 or (f/.(i1+1))`2<(f/.i1)`2
                       implies g.2=f.(i1+1))&
       ((f/.(i1+1))`1>=(f/.i1)`1 & (f/.(i1+1))`2>=(f/.i1)`2
                implies g.2=f.S_Drop(i1-'1,f));
   end;
   end;
   hence ex g st g is_a_part_of f,i1,i2 &
   ((f/.(i1+1))`1<(f/.i1)`1 or (f/.(i1+1))`2<(f/.i1)`2 implies g.2=f.(i1+1))&
   ((f/.(i1+1))`1>=(f/.i1)`1 & (f/.(i1+1))`2>=(f/.i1)`2
                implies g.2=f.S_Drop(i1-'1,f));
   thus thesis by A1,Th67;
 end;

 func Upper (f,i1,i2) -> FinSequence of TOP-REAL 2 means
     it is_a_part_of f,i1,i2 &
   ((f/.(i1+1))`1>(f/.i1)`1 or (f/.(i1+1))`2>(f/.i1)`2 implies it.2=f.(i1+1))&
   ((f/.(i1+1))`1<=(f/.i1)`1 & (f/.(i1+1))`2<=(f/.i1)`2
                implies it.2=f.S_Drop(i1-'1,f));
 correctness
 proof
 A45:i1<len f by A1,NAT_1:38;
 then A46:1<len f by A1,AXIOMS:22;
 then 1+1<=len f by NAT_1:38;
 then A47: 1+1-1<=len f-1 by REAL_1:49;
 then A48:1<=len f-'1 by JORDAN3:1;
 A49:i2<len f by A1,NAT_1:38;
 A50:i1-'1=i1-1 by A1,SCMFSA_7:3;
 A51:len f-'1=len f-1 by A46,SCMFSA_7:3;
   A52: i2+1-1<=len f-1 by A1,REAL_1:49;
   A53: i1+1-1<=len f-1 by A1,REAL_1:49;
    len f<len f+1 by NAT_1:38;
 then A54: len f-1<len f+1-1 by REAL_1:54;
      now per cases;
   case A55: i1<=i2;
     then A56:i1<i2 by A1,REAL_1:def 5;
        now per cases;
     case A57:(f/.(i1+1))`1>(f/.i1)`1 or (f/.(i1+1))`2>(f/.i1)`2;
       set g=mid(f,i1,i2);
A58:    g is_a_part>_of f,i1,i2 by A1,A49,A55,Th43;
       A59:len mid(f,i1,i2)=i2-'i1+1 by A1,A45,A49,A55,JORDAN3:27;
          i1+1<=i2 by A56,NAT_1:38;
       then (1+i1)+1<=i2+1 by AXIOMS:24;
       then 2+i1-i1<=i2+1-i1 by REAL_1:49;
       then 2+i1-i1<=i2-i1+1;
       then 1<2 & 2<=len mid(f,i1,i2) by A55,A59,SCMFSA_7:3;
       then g.2=f.(2+i1-'1)
         by A1,A45,A49,A55,JORDAN3:27
       .=f.(i1+1+1-1) by Th2 .=f.(i1+1);
      hence g is_a_part_of f,i1,i2 &
       ((f/.(i1+1))`1>(f/.i1)`1 or (f/.(i1+1))`2>(f/.i1)`2
                                    implies g.2=f.(i1+1))&
       ((f/.(i1+1))`1<=(f/.i1)`1 & (f/.(i1+1))`2<=(f/.i1)`2
                implies g.2=f.S_Drop(i1-'1,f)) by A57,A58,Def4;
     end;
     case A60:(f/.(i1+1))`1<=(f/.i1)`1 & (f/.(i1+1))`2<=(f/.i1)`2;
       set g=mid(f,i1,1)^mid(f,len f-'1,i2);
A61:    g is_a_part<_of f,i1,i2 by A1,A49,A56,Th46;
       A62:len mid(f,i1,1)=i1-'1+1 by A1,A45,Th21;
          now per cases;
       case A63:1<i1;
         then A64:1+1<=i1 by NAT_1:38;
         then A65:f.(i1-'(1+1)+1)=f.(i1-(1+1)+1) by SCMFSA_7:3
         .=f.(i1-1) .=f.(i1-'1) by A1,SCMFSA_7:3;
            1+1<=i1-1+1 by A63,NAT_1:38;
         then A66:2<=len mid(f,i1,1) by A1,A62,SCMFSA_7:3;
         A67:g.2 =mid(f,i1,1).2 by A50,A62,A64,SCMFSA_7:9
         .=f.(i1-'(1+1)+1) by A45,A46,A63,A66,JORDAN3:27;
            1+1-1<=i1-1 by A64,REAL_1:49;
         then 1<=i1-'1 & i1-'1<=len f-'1 by A45,A50,A51,REAL_1:54;
        hence g is_a_part_of f,i1,i2 &
         ((f/.(i1+1))`1>(f/.i1)`1 or (f/.(i1+1))`2>(f/.i1)`2
                       implies g.2=f.(i1+1))&
         ((f/.(i1+1))`1<=(f/.i1)`1 & (f/.(i1+1))`2<=(f/.i1)`2
                implies g.2=f.S_Drop(i1-'1,f)) by A60,A61,A65,A67,Def4,Th34;
       end;
       case A68: 1>=i1;
         then i1=1 by A1,AXIOMS:21;
         then A69:i1-'1=0 by GOBOARD9:1;
         A70:len mid(f,i1,1)=len mid(f,1,1) by A1,A68,AXIOMS:21
           .=1 by A46,Th27;
         A71:len mid(f,len f-'1,i2)=len f-'1-'i2+1 by A1,A51,A52,A54,Th21
         .=len f-1-i2+1 by A51,A52,SCMFSA_7:3
         .=len f-i2;
            len f-i2>=i2+1-i2 by A1,REAL_1:49;
then A72:      1+1<=len mid(f,i1,1)+len mid(f,len f-'1,i2)
              by A70,A71,AXIOMS:24;
           A73: i2+1-1<=len f-1 by A1,REAL_1:49;
         A74:1<=len f-'1-'i2+1 by NAT_1:29;
         A75:g.2 =mid(f,len f-'1,i2).(2-len mid(f,i1,1)) by A70,A72,JORDAN3:15
         .=mid(f,len f-'1,i2).(2-(i1-'1+1)) by A1,A45,Th21
         .=mid(f,len f-'1,i2).(1+1-(1-'1+1)) by A1,A68,AXIOMS:21
         .=mid(f,len f-'1,i2).(1+1-(0+1)) by GOBOARD9:1
         .=f.(len f-'1-'1+1) by A1,A51,A54,A73,A74,Th24
         .=f.(len f-'1) by A48,AMI_5:4;
            S_Drop(i1-'1,f)=S_Drop(i1-'1+(len f-'1),f) by Th35
                        .=len f-'1 by A48,A69,Th34;
        hence g is_a_part_of f,i1,i2 &
         ((f/.(i1+1))`1>(f/.i1)`1 or (f/.(i1+1))`2>(f/.i1)`2
                       implies g.2=f.(i1+1))&
         ((f/.(i1+1))`1<=(f/.i1)`1 & (f/.(i1+1))`2<=(f/.i1)`2
                implies g.2=f.S_Drop(i1-'1,f)) by A60,A61,A75,Def4;
       end;
       end;
       hence g is_a_part_of f,i1,i2 &
       ((f/.(i1+1))`1>(f/.i1)`1 or (f/.(i1+1))`2>(f/.i1)`2
                       implies g.2=f.(i1+1))&
       ((f/.(i1+1))`1<=(f/.i1)`1 & (f/.(i1+1))`2<=(f/.i1)`2
                implies g.2=f.S_Drop(i1-'1,f));
     end;
     end;
      hence ex g st g is_a_part_of f,i1,i2 &
       ((f/.(i1+1))`1>(f/.i1)`1 or (f/.(i1+1))`2>(f/.i1)`2
                       implies g.2=f.(i1+1))&
       ((f/.(i1+1))`1<=(f/.i1)`1 & (f/.(i1+1))`2<=(f/.i1)`2
                implies g.2=f.S_Drop(i1-'1,f));
   end;
   case A76:i1>i2; then i1>1 by A1,AXIOMS:22;
       then A77:1+1<=i1 by NAT_1:38;
then A78:     1+1-1<=i1-1 by REAL_1:49;
        now per cases;
     case A79:(f/.(i1+1))`1<=(f/.i1)`1 & (f/.(i1+1))`2<=(f/.i1)`2;
       set g=mid(f,i1,i2);
A80:    g is_a_part<_of f,i1,i2 by A1,A45,A76,Th44;
       A81:len mid(f,i1,i2)=i1-'i2+1 by A1,A45,A49,A76,JORDAN3:27;
          i2+1<=i1 by A76,NAT_1:38;
       then (1+i2)+1<=i1+1 by AXIOMS:24;
       then 2+i2-i2<=i1+1-i2 by REAL_1:49;
       then 2+i2-i2<=i1-i2+1;
       then 2<=len mid(f,i1,i2) by A76,A81,SCMFSA_7:3;
       then A82:g.2=f.(i1-'2+1) by A1,A45,A49,A76,JORDAN3:27
       .=f.(i1-(1+1)+1) by A77,SCMFSA_7:3
       .=f.(i1-1)  .=f.(i1-'1) by A1,SCMFSA_7:3;
          1<=i1-'1 & i1-'1<=len f-'1 by A45,A50,A51,A78,REAL_1:49;
      hence g is_a_part_of f,i1,i2 &
       ((f/.(i1+1))`1>(f/.i1)`1 or (f/.(i1+1))`2>(f/.i1)`2
                                    implies g.2=f.(i1+1))&
       ((f/.(i1+1))`1<=(f/.i1)`1 & (f/.(i1+1))`2<=(f/.i1)`2
                implies g.2=f.S_Drop(i1-'1,f)) by A79,A80,A82,Def4,Th34;
     end;
     case A83:(f/.(i1+1))`1>(f/.i1)`1 or (f/.(i1+1))`2>(f/.i1)`2;
       set g=mid(f,i1,len f-'1)^mid(f,1,i2);
A84:    g is_a_part>_of f,i1,i2 by A1,A45,A76,Th45;
       A85:len mid(f,i1,len f-'1)=len f-'1-'i1+1
                           by A1,A45,A47,A51,A53,A54,JORDAN3:27
       .=len f-1-i1+1 by A51,A53,SCMFSA_7:3
       .=len f-i1;
          now per cases;
       case i1+1<len f;
         then i1+1+1<=len f by NAT_1:38;
         then A86: i1+2-i1<=len f-i1 by REAL_1:49;
         then g.2 =mid(f,i1,len f-'1).2 by A85,SCMFSA_7:9
         .=f.(2+i1-'1) by A1,A45,A47,A51,A53,A54,A85,A86,JORDAN3:27
         .=f.(1+1+i1-1) by Th2
         .=f.(1+i1);
        hence g is_a_part_of f,i1,i2 &
         ((f/.(i1+1))`1>(f/.i1)`1 or (f/.(i1+1))`2>(f/.i1)`2
                       implies g.2=f.(i1+1)) &
         ((f/.(i1+1))`1<=(f/.i1)`1 & (f/.(i1+1))`2<=(f/.i1)`2
                implies g.2=f.S_Drop(i1-'1,f)) by A83,A84,Def4;
       end;
       case A87: i1+1>=len f;
         then i1+1=len f by A1,AXIOMS:21;
         then A88:len mid(f,i1,len f-'1)=len mid(f,len f-'1,len f-'1)
           by A51,XCMPLX_1:26
            .=1 by A47,A51,A54,Th27;
            len mid(f,1,i2)=i2-'1+1 by A1,A46,A49,JORDAN3:27
            .=i2-1+1 by A1,SCMFSA_7:3 .=i2;
then 1+1<=len mid(f,i1,len f-'1)+len mid(f,1,i2)
              by A1,A88,AXIOMS:24;
         then g.2 =mid(f,1,i2).(2-(i1+1-i1)) by A88,JORDAN3:15
         .=f.1 by A1,A46,A49,JORDAN3:27 .=f/.1 by A46,FINSEQ_4:24
         .=f/.len f by FINSEQ_6:def 1
         .=f.len f by A46,FINSEQ_4:24;
        hence g is_a_part_of f,i1,i2 &
         ((f/.(i1+1))`1>(f/.i1)`1 or (f/.(i1+1))`2>(f/.i1)`2
                       implies g.2=f.(i1+1)) &
         ((f/.(i1+1))`1<=(f/.i1)`1 & (f/.(i1+1))`2<=(f/.i1)`2
               implies g.2=f.S_Drop(i1-'1,f))
                   by A1,A83,A84,A87,Def4,AXIOMS:21;
       end;
       end;
       hence g is_a_part_of f,i1,i2 &
       ((f/.(i1+1))`1>(f/.i1)`1 or (f/.(i1+1))`2>(f/.i1)`2
                       implies g.2=f.(i1+1))&
       ((f/.(i1+1))`1<=(f/.i1)`1 & (f/.(i1+1))`2<=(f/.i1)`2
                implies g.2=f.S_Drop(i1-'1,f));
     end;
     end;
       hence ex g st g is_a_part_of f,i1,i2 &
       ((f/.(i1+1))`1>(f/.i1)`1 or (f/.(i1+1))`2>(f/.i1)`2
                       implies g.2=f.(i1+1))&
       ((f/.(i1+1))`1<=(f/.i1)`1 & (f/.(i1+1))`2<=(f/.i1)`2
                implies g.2=f.S_Drop(i1-'1,f));
   end;
   end;
   hence thesis by A1,Th67;
 end;
end;

