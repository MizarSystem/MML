:: Lattice of Subgroups of a Group. Frattini Subgroup
::  by Wojciech A. Trybulec
::
:: Received August 22, 1990
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies FINSEQ_1, ARYTM_1, REALSET1, GROUP_2, SETFAM_1, GROUP_1, BOOLE,
      RELAT_1, INT_1, GROUP_3, QC_LANG1, ABSVALUE, FINSOP_1, BINOP_1, SETWISEO,
      FUNCT_1, FINSEQ_2, FUNCT_2, RLSUB_1, TARSKI, LATTICES, NAT_1, SUBSET_1,
      RLSUB_2, GROUP_4, CARD_3, FINSEQ_4, ARYTM, ALGSTR_0, VECTSP_1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, XCMPLX_0, FINSOP_1, NUMBERS,
      INT_1, SETWISEO, SETFAM_1, FUNCT_1, PARTFUN1, FUNCT_2, FINSEQ_1,
      FINSEQ_3, FINSEQ_4, BINOP_1, STRUCT_0, ALGSTR_0, GROUP_2, GROUP_3,
      LATTICES, GROUP_1, DOMAIN_1, XXREAL_0, NAT_1, NAT_D, INT_2;
 constructors PARTFUN1, SETFAM_1, BINOP_1, SETWISEO, XXREAL_0, NAT_1, NAT_D,
      FINSEQ_3, FINSEQ_4, FINSOP_1, REALSET1, LATTICES, GROUP_3;
 registrations XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, NUMBERS, XREAL_0, INT_1,
      FINSEQ_1, STRUCT_0, LATTICES, GROUP_1, GROUP_2, GROUP_3, ORDINAL1,
      CARD_1;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, XBOOLE_0, GROUP_2, SUBSET_1, ALGSTR_0;
 theorems BINOP_1, FINSEQ_1, FINSEQ_2, FINSEQ_3, FINSEQ_4, FINSOP_1, FUNCT_1,
      GROUP_1, GROUP_2, GROUP_3, INT_1, LATTICES, NAT_1, RLSUB_2,
      SETFAM_1, SUBSET_1, TARSKI, ZFMISC_1, RELAT_1, XBOOLE_0, XBOOLE_1,
      ORDERS_1, XREAL_1, XXREAL_0, NAT_D, STRUCT_0, PARTFUN1, CARD_1, CARD_2;
 schemes BINOP_1, DOMAIN_1, FINSEQ_1, FINSEQ_2, NAT_1, ORDERS_1, XBOOLE_0;

begin

definition
  let D be non empty set;
  let F be FinSequence of D;
  let X be set;
  redefine func F - X -> FinSequence of D;
  coherence by FINSEQ_3:93;
end;

scheme MeetSbgEx{G() -> Group, P[set]}: ex H being strict Subgroup of G() st
  the carrier of H = meet{A where A is Subset of G() :
  ex K being strict Subgroup of G() st A = the carrier of K & P[K]}
provided
A1: ex H being strict Subgroup of G() st P[H]
proof
  set X = {A where A is Subset of G(): ex K being strict Subgroup of G()
  st A = the carrier of K & P[K]};
  consider T being strict Subgroup of G() such that
A2: P[T] by A1;
A3: carr T in X by A2;
  then reconsider Y = meet X as Subset of G() by SETFAM_1:8;
  now
    let Z be set;
    assume Z in X;
    then consider A being Subset of G() such that
A4: Z = A and
A5: ex K being strict Subgroup of G() st A = the carrier of K & P[K];
    consider H being Subgroup of G() such that
A6: A = the carrier of H & P[H] by A5;
    1_G() in H by GROUP_2:55;
    hence 1_G() in Z by A4,A6,STRUCT_0:def 5;
  end;
  then
A7: Y <> {} by A3,SETFAM_1:def 1;
A8: now
    let a,b be Element of G();
    assume
A9: a in Y & b in Y;
    now
      let Z be set;
      assume
A10:  Z in X;
      then consider A being Subset of G() such that
A11:  A = Z and
A12:  ex H being strict Subgroup of G() st A = the carrier of H & P[H];
      consider H being Subgroup of G() such that
A13:  A = the carrier of H & P[H] by A12;
      a in the carrier of H & b in the carrier of H
      by A9,A10,A11,A13,SETFAM_1:def 1;
      then a in H & b in H by STRUCT_0:def 5;
      then a * b in H by GROUP_2:59;
      hence a * b in Z by A11,A13,STRUCT_0:def 5;
    end;
    hence a * b in Y by A3,SETFAM_1:def 1;
  end;
  now
    let a be Element of G();
    assume
A14: a in Y;
    now
      let Z be set;
      assume
A15:  Z in X;
      then consider A being Subset of G() such that
A16:  A = Z and
A17:  ex H being strict Subgroup of G() st A = the carrier of H & P[H];
      consider H being Subgroup of G() such that
A18:  A = the carrier of H & P[H] by A17;
      a in the carrier of H by A14,A15,A16,A18,SETFAM_1:def 1;
      then a in H by STRUCT_0:def 5;
      then a" in H by GROUP_2:60;
      hence a" in Z by A16,A18,STRUCT_0:def 5;
    end;
    hence a" in Y by A3,SETFAM_1:def 1;
  end;
  hence thesis by A7,A8,GROUP_2:61;
end;

reserve x,y,y1,y2,X,Y for set,
  k,l,n for Element of NAT,
  i,i1,i2,i3,j for Integer,
  G for Group,
  a,b,c,d for Element of G,
  A,B,C for Subset of G,
  H,H1,H2,H3 for Subgroup of G,
  h for Element of H,
  F,F1,F2 for FinSequence of the carrier of G,
  I,I1,I2 for FinSequence of INT;

scheme SubgrSep{G() -> Group,P[set]}: ex X st X c= Subgroups G() &
  for H being strict Subgroup of G() holds H in X iff P[H]
proof
  defpred R[set] means ex H being Subgroup of G() st $1 = H & P[H];
  consider X such that
A1: x in X iff x in Subgroups G() & R[x] from XBOOLE_0:sch 1;
  take X;
  thus X c= Subgroups G()
  proof
    let x;
    assume x in X;
    hence x in Subgroups G() by A1;
  end;
  let H be strict Subgroup of G();
  thus H in X implies P[H]
  proof
    assume H in X;
    then ex H1 being Subgroup of G() st H = H1 & P[H1] by A1;
    hence thesis;
  end;
  assume
A2: P[H];
  H in Subgroups G() by GROUP_3:def 1;
  hence thesis by A1,A2;
end;

definition
  let i;
  canceled;
  func @i -> Element of INT equals

  i;
  coherence by INT_1:def 2;
end;

canceled 2;

theorem Th3:
  a = h implies a |^ n = h |^ n
proof
  assume
A1: a = h;
  defpred P[Element of NAT] means a |^ $1 = h |^ $1;
  a |^ 0 = 1_G by GROUP_1:43
    .= 1_H by GROUP_2:53
    .= h |^ 0 by GROUP_1:43;
  then
A2: P[0];
A3: now
    let n;
    assume
A4: P[n];
    a |^ (n + 1) = a |^ n * a by GROUP_1:49
      .= h |^ n * h by A1,A4,GROUP_2:52
      .= h |^ (n + 1) by GROUP_1:49;
    hence P[n+1];
  end;
  for n holds P[n] from NAT_1:sch 1(A2,A3);
  hence thesis;
end;

theorem
  a = h implies a |^ i = h |^ i
proof
  assume
A1: a = h;
  now per cases;
    suppose
A2:   i >= 0;
      hence a |^ i = a |^ abs( i ) by GROUP_1:55
        .= h |^ abs( i ) by A1,Th3
        .= h |^ i by A2,GROUP_1:55;
    end;
    suppose
A3:   i < 0;
A4:   a |^ abs( i ) = h |^ abs( i ) by A1,Th3;
      thus a |^ i = (a |^ abs( i ))" by A3,GROUP_1:56
        .= (h |^ abs( i ))" by A4,GROUP_2:57
        .= h |^ i by A3,GROUP_1:56;
    end;
  end;
  hence thesis;
end;

theorem Th5:
  a in H implies a |^ n in H
proof
  assume
A1: a in H;
  defpred P[Element of NAT] means a |^ $1 in H;
  a |^ 0 = 1_G by GROUP_1:43;
  then
A2: P[0] by GROUP_2:55;
A3: now
    let n;
    assume
A4: P[n];
    a |^ (n + 1) = a |^ n * a by GROUP_1:49;
    hence P[n+1] by A1,A4,GROUP_2:59;
  end;
  for n holds P[n] from NAT_1:sch 1(A2,A3);
  hence thesis;
end;

theorem Th6:
  a in H implies a |^ i in H
proof
  assume
A1: a in H;
  now per cases;
    suppose i >= 0;
      then a |^ i = a |^ abs( i ) by GROUP_1:55;
      hence thesis by A1,Th5;
    end;
    suppose i < 0;
      then a |^ i = (a |^ abs( i ))" & a |^ abs( i ) in H by A1,Th5,GROUP_1:56;
      hence thesis by GROUP_2:60;
    end;
  end;
  hence thesis;
end;

definition
  let G be non empty multMagma;
  let F be FinSequence of the carrier of G;
  func Product F -> Element of G equals

  (the multF of G) "**" F;
  correctness;
end;

canceled;

theorem Th8:
  for G being associative unital (non empty multMagma),
  F1,F2 being FinSequence of the carrier of G
  holds Product(F1 ^ F2) = Product(F1) * Product(F2)
proof
  let G be associative unital (non empty multMagma),
  F1,F2 be FinSequence of the carrier of G;
  set g = the multF of G;
  g is associative & g has_a_unity by GROUP_1:31,34;
  hence Product(F1 ^ F2) = Product F1 * Product F2 by FINSOP_1:6;
end;

theorem Th9:
  for G being unital (non empty multMagma),
  F being FinSequence of the carrier of G, a being Element of G
  holds Product(F ^ <* a *>) = Product(F) * a by FINSOP_1:5,GROUP_1:34;

theorem Th10:
  for G being associative unital (non empty multMagma),
  F being FinSequence of the carrier of G, a being Element of G
  holds Product(<* a *> ^ F) = a * Product(F)
proof
  let G be associative unital (non empty multMagma),
  F be FinSequence of the carrier of G, a be Element of G;
  set g = the multF of G;
  g is associative & g has_a_unity by GROUP_1:31,34;
  hence Product(<* a *> ^ F) = a * Product F by FINSOP_1:7;
end;

theorem Th11:
  for G being unital (non empty multMagma)
  holds Product <*> the carrier of G = 1_G
proof
  let G be unital (non empty multMagma);
  set g = the multF of G;
  len <*> the carrier of G = 0 & g has_a_unity by GROUP_1:34;
  hence Product <*> the carrier of G = the_unity_wrt g by FINSOP_1:def 1
    .= 1_G by GROUP_1:33;
end;

theorem
  for G being non empty multMagma, a being Element of G holds
  Product<* a *> = a by FINSOP_1:12;

theorem
  for G being non empty multMagma, a,b being Element of G holds
  Product<* a,b *> = a * b by FINSOP_1:13;

theorem
  Product<* a,b,c *> = a * b * c & Product<* a,b,c *> = a * (b * c)
proof <* a,b,c *> = <* a *> ^ <* b,c *> & <* a,b,c *> = <* a,b *> ^ <* c *> &
  Product<* a *> = a & Product<* c *> = c & Product<* a,b *> = a * b &
  Product<* b,c *> = b * c by FINSEQ_1:60,FINSOP_1:12,13;
  hence thesis by Th8;
end;

Lm1: now
  let G,a;
  thus Product(0 |-> a) = Product <*> the carrier of G by FINSEQ_2:72
    .= 1_G by Th11
    .= a |^ 0 by GROUP_1:43;
end;

Lm2: now
  let G,a;
  let n;
  assume
A1: Product(n |-> a) = a |^ n;
  thus Product((n + 1) |-> a) = Product((n |-> a) ^ <* a *>) by FINSEQ_2:74
    .= Product(n |-> a) * Product<* a *> by Th8
    .= a |^ n * a by A1,FINSOP_1:12
    .= a |^ (n + 1) by GROUP_1:49;
end;

theorem
  Product(n |-> a) = a |^ n
proof
  defpred P[Element of NAT] means Product($1 |-> a) = a |^ $1;
A1: P[0] by Lm1;
A2: for n being Element of NAT st P[n] holds P[n+1] by Lm2;
  for n being Element of NAT holds P[n] from NAT_1:sch 1(A1,A2);
  hence thesis;
end;

theorem
  Product(F - {1_G}) = Product(F)
proof
  defpred P[FinSequence of the carrier of G] means
  Product($1 - {1_G}) = Product$1;
A1: P[<*> the carrier of G] by FINSEQ_3:81;
A2: for F,a st P[F] holds P[F ^ <* a *>]
  proof
    let F,a;
    assume
A3: P[F];
A4: Product((F ^ <* a *>) - {1_G}) =
    Product(((F - {1_G}) ^ (<* a *> - {1_G}))) by FINSEQ_3:80
      .= Product F * Product(<* a *> - {1_G}) by A3,Th8;
    now per cases;
      suppose
A5:     a = 1_G;
        then a in {1_G} by TARSKI:def 1;
        then <* a *> - {1_G} = <*> the carrier of G by FINSEQ_3:83;
        then Product(<* a *> - {1_G}) = 1_G by Th11;
        hence Product(F ^ <* a *> - {1_G}) = Product(F ^ <* a *>)
        by A4,A5,Th9;
      end;
      suppose a <> 1_G;
        then not a in {1_G} by TARSKI:def 1;
        then <* a *> - {1_G} = <* a *> by FINSEQ_3:82;
        hence Product(F ^ <* a *> - {1_G}) = Product(F ^ <* a *>) by A4,Th8;
      end;
    end;
    hence thesis;
  end;
  for F holds P[F] from FINSEQ_2:sch 2(A1,A2);
  hence thesis;
end;

Lm3: for F1 being FinSequence, y being Element of NAT st y in dom F1 holds
len F1 - y + 1 is Element of NAT & len F1 - y + 1 >= 1 &
len F1 - y + 1 <= len F1
proof
  let F1 be FinSequence, y be Element of NAT;
  assume
A1: y in dom F1;
  now
    assume len F1 - y + 1 < 0;
    then 1 < 0 qua Nat - (len F1 - y) by XREAL_1:22;
    then 1 < y - len F1;
    then len F1 + 1 < y & y <= len F1 by A1,FINSEQ_3:27,XREAL_1:22;
    hence contradiction by NAT_1:12;
  end;
  then reconsider n = len F1 - y + 1 as Element of NAT by INT_1:16;
  y + 0 <= len F1 by A1,FINSEQ_3:27;
  then
A2: 0 + 1 = 1 & 0 <= len F1 - y by XREAL_1:21;
  n - 1 <= len F1 - y & y >= 1 by A1,FINSEQ_3:27;
  then n - 1 - y <= len F1 - y - 1 by XREAL_1:15;
  then n - (y + 1) <= len F1 - (y + 1);
  hence thesis by A2,XREAL_1:8,11;
end;

theorem Th17:
  len F1 = len F2 &
  (for k st k in dom F1 holds F2.(len F1 - k + 1) = (F1/.k)") implies
  Product(F1) = Product(F2)"
proof
  defpred P[Element of NAT] means for F1,F2 st len F1 = $1 & len F1 = len F2 &
  (for k st k in dom F1 holds F2.(len F1 - k + 1) = (F1/.k)") holds
  Product(F1) = Product(F2)";
A1: P[0]
  proof
    let F1,F2;
    assume len F1 = 0 & len F1 = len F2;
    then F1 = <*> the carrier of G & F2 = <*> the carrier of G;
    then Product F1 = 1_G & Product F2 = 1_G & 1_G = (1_G)"
    by Th11,GROUP_1:16;
    hence thesis;
  end;
A2: for n st P[n] holds P[n + 1]
  proof
    let n;
    assume
A3: P[n];
    let F1,F2;
    assume that
A4: len F1 = n + 1 & len F1 = len F2 and
A5: for k st k in dom F1 holds F2.(len F1 - k + 1) = (F1/.k)";
    reconsider p = F1 | Seg n
    as FinSequence of the carrier of G by FINSEQ_1:23;
    1 <= n + 1 by NAT_1:12;
    then n + 1 in dom F1 by A4,FINSEQ_3:27;
    then F1.(n + 1) in rng F1 & rng F1 c= the carrier of G
    by FINSEQ_1:def 4,FUNCT_1:def 5;
    then reconsider a = F1.(n + 1) as Element of G;
A6: len p = n by A4,FINSEQ_3:59;
    deffunc F(Nat) = F2.($1 + 1);
    consider q being FinSequence such that
A7: len q = len p and
A8: for k be Nat st k in dom q holds q.k = F(k) from FINSEQ_1:sch 2;
A9: dom q = dom p by A7,FINSEQ_3:31;
    rng q c= the carrier of G
    proof
      let x;
      assume x in rng q;
      then consider y such that
A10:  y in dom q & x = q.y by FUNCT_1:def 5;
      reconsider y as Element of NAT by A10;
A11:  x = F2.(y + 1) by A8,A10;
A12:  1 <= y + 1 by NAT_1:12;
      y <= len p by A7,A10,FINSEQ_3:27;
      then y + 1 <= len F2 by A4,A6,XREAL_1:9;
      then y + 1 in dom F2 by A12,FINSEQ_3:27;
      then x in rng F2 & rng F2 c= the carrier of G
      by A11,FINSEQ_1:def 4,FUNCT_1:def 5;
      hence thesis;
    end;
    then reconsider q as FinSequence of the carrier of G by FINSEQ_1:def 4;
A13: len F2 = len<* a" *> + len q by A4,A6,A7,FINSEQ_1:56;
A14: now
      let k;
      assume k in dom<* a" *>;
      then
A15:  k in {1} by FINSEQ_1:4,def 8;
      len F2 >= 1 by A4,NAT_1:12;
      then
A16:  len F2 in dom F1 by A4,FINSEQ_3:27;
      then F2.(len F1 - len F1 + 1) = (F1/.len F1)" by A4,A5;
      then (F1/.len F1)" = F2.k by A15,TARSKI:def 1;
      then F2.k = a" & k = 1 by A4,A15,A16,PARTFUN1:def 8,TARSKI:def 1;
      hence <* a" *>.k = F2.k by FINSEQ_1:def 8;
    end;
    now
      let k;
      assume k in dom q;
      then k in dom q & len<* a" *> = 1 & k + 1 = 1 + k by FINSEQ_1:56;
      hence F2.(len<* a" *> + k) = q.k by A8;
    end;
    then
A17: F2 = <* a" *> ^ q by A13,A14,FINSEQ_3:43;
    now
      let k;
      assume
A18:  k in dom p;
      then reconsider i = len p - k + 1 as Element of NAT by Lm3;
      1 <= i & i <= len p by A18,Lm3;
      then i in dom p by FINSEQ_3:27;
      then
A19:  q.i = F2.(i + 1) by A8,A9;
      len p <= len F1 & k <= len p by A4,A6,A18,FINSEQ_3:27,NAT_1:12;
      then 1 <= k & k <= len F1 by A18,FINSEQ_3:27,XXREAL_0:2;
      then
A20:  k in dom F1 by FINSEQ_3:27;
A21:  i + 1 = len F1 - k + 1 by A4,A6;
      F1/.k = F1.k by A20,PARTFUN1:def 8
        .= p.k by A18,FUNCT_1:70
        .= p/.k by A18,PARTFUN1:def 8;
      hence q.(len p - k + 1) = (p/.k)" by A5,A19,A20,A21;
    end;
    then
A22: Product p = Product q" by A3,A6,A7;
    F1 = p ^ <* a *> by A4,FINSEQ_3:61;
    then
A23: Product F1 = Product p * a by Th9;
    Product F2 = a" * Product q by A17,Th10
      .= a" * Product p" by A22,GROUP_1:19
      .= (Product p * a)" by GROUP_1:25;
    hence thesis by A23,GROUP_1:19;
  end;
  for k holds P[k] from NAT_1:sch 1(A1,A2);
  hence thesis;
end;

theorem
  G is commutative Group implies
  for P being Permutation of dom F1 st F2 = F1 * P holds
  Product(F1) = Product(F2)
proof
  set g = the multF of G;
  assume G is commutative Group;
  then
A1: g is commutative & g has_a_unity & g is associative
  by GROUP_1:31,34,GROUP_3:2;
  let P be Permutation of dom F1;
  assume F2 = F1 * P;
  hence Product(F1) = Product(F2) by A1,FINSOP_1:8;
end;

theorem
  G is commutative Group & F1 is one-to-one & F2 is one-to-one & rng F1 = rng
  F2 implies Product(F1) = Product(F2)
proof
  set g = the multF of G;
  assume G is commutative Group;
  then
A1: g is commutative & g has_a_unity & g is associative
  by GROUP_1:31,34,GROUP_3:2;
  assume F1 is one-to-one & F2 is one-to-one & rng F1 = rng F2;
  hence Product(F1) = Product(F2) by A1,FINSOP_1:9;
end;

theorem
  G is commutative Group & len F = len F1 & len F = len F2 &
  (for k st k in dom F holds F.k = (F1/.k) * (F2/.k)) implies
  Product(F) = Product(F1) * Product(F2)
proof
  set g = the multF of G;
  assume G is commutative Group;
  then
A1: g is commutative & g has_a_unity & g is associative
  by GROUP_1:31,34,GROUP_3:2;
  assume
A2: len F = len F1 & len F = len F2;
  assume
A3: for k st k in dom F holds F.k = (F1/.k) * (F2/.k);
  now
    let k;
    assume
A4: k in dom F;
A5: dom F = Seg len F & dom F1 = Seg len F1 & dom F2 = Seg len F2
    by FINSEQ_1:def 3;
    thus F.k = (F1/.k) * (F2/.k) by A3,A4
      .= g.(F1.k,F2/.k) by A2,A4,A5,PARTFUN1:def 8
      .= g.(F1.k,F2.k) by A2,A4,A5,PARTFUN1:def 8;
  end;
  hence Product(F) = Product(F1) * Product(F2) by A1,A2,FINSOP_1:10;
end;

theorem Th21:
  rng F c= carr H implies Product(F) in H
proof
  defpred P[FinSequence of the carrier of G] means
  rng $1 c= carr H implies Product $1 in H;
A1: P[<*> the carrier of G]
  proof
    assume rng <*> the carrier of G c= carr H;
    1_G in H by GROUP_2:55;
    hence Product(<*> the carrier of G) in H by Th11;
  end;
A2: now
    let F,d;
    assume
A3: P[F];
    thus P[F^<*d*>]
    proof
      assume
A4:   rng(F ^ <* d *>) c= carr H;
      rng F c= rng(F ^ <* d *>) & rng<* d *> c= rng(F ^ <* d *>)
      by FINSEQ_1:42,43;
      then rng F c= carr H & rng<* d *> c= carr H & rng<* d *> = {d} &
      d in {d} by A4,FINSEQ_1:56,TARSKI:def 1,XBOOLE_1:1;
      then d in H & Product(F) in
      H & Product(F ^ <* d *>) = Product(F) * d by A3,Th9,STRUCT_0:def 5;
      hence Product(F ^ <* d *>) in H by GROUP_2:59;
    end;
  end;
  for F holds P[F] from FINSEQ_2:sch 2(A1,A2);
  hence thesis;
end;

definition
  let G,I,F;
  func F |^ I -> FinSequence of the carrier of G means
  :Def4:
  len it = len F &
  for k st k in dom F holds it.k = (F/.k) |^ @(I/.k);
  existence
  proof
    deffunc F(Nat) = (F/.$1) |^ @(I/.$1);
    consider p being FinSequence such that
A1: len p = len F and
A2: for k be Nat st k in dom p holds p.k = F(k) from FINSEQ_1:sch 2;
A3: dom p = dom F by A1,FINSEQ_3:31;
    rng p c= the carrier of G
    proof
      let x;
      assume x in rng p;
      then consider y such that
A4:   y in dom p & p.y = x by FUNCT_1:def 5;
      reconsider y as Element of NAT by A4;
      x = (F/.y) |^ @(I/.y) & (F/.y) |^ @(I/.y) in the carrier of G
      by A2,A4;
      hence thesis;
    end;
    then reconsider p as FinSequence of the carrier of G by FINSEQ_1:def 4;
    take p;
    thus thesis by A1,A2,A3;
  end;
  uniqueness
  proof
    let F1,F2;
    assume that
A5: len F1 = len F and
A6: for k st k in dom F holds F1.k = (F/.k) |^ @(I/.k);
    assume that
A7: len F2 = len F and
A8: for k st k in dom F holds F2.k = (F/.k) |^ @(I/.k);
X: dom F1 = Seg len F by FINSEQ_1:def 3,A5;
    now
      let k be Nat;
      assume k in dom F1;
      then
A9:  k in dom F by FINSEQ_1:def 3,X;
      hence F1.k = (F/.k) |^ @(I/.k) by A6
        .= F2.k by A8,A9;
    end;
    hence thesis by A5,A7,FINSEQ_2:10;
  end;
end;

canceled 3;

theorem Th25:
  len F1 = len I1 & len F2 = len I2 implies
  (F1 ^ F2) |^ (I1 ^ I2) = (F1 |^ I1) ^ (F2 |^ I2)
proof
  assume
A1: len F1 = len I1 & len F2 = len I2;
  set q = F1 |^ I1;
  set r = F2 |^ I2;
A2: len(q ^ r) = len q + len r by FINSEQ_1:35
    .= len F1 + len r by Def4
    .= len F1 + len F2 by Def4
    .= len(F1 ^ F2) by FINSEQ_1:35;
  len(F1 ^ F2) = len F1 + len F2 by FINSEQ_1:35
    .= len(I1 ^ I2) by A1,FINSEQ_1:35;
  then
A3: dom (F1 ^ F2) = dom (I1 ^ I2) by FINSEQ_3:31;
  now
    let k;
    assume
A4: k in dom(F1 ^ F2);
    now per cases by A4,FINSEQ_1:38;
      suppose
A5:     k in dom F1;
        then
A6:     k in dom I1 by A1,FINSEQ_3:31;
        then
A7:     I1/.k = I1.k by PARTFUN1:def 8;
A8:     (I1 ^ I2).k = (I1 ^ I2)/.k by A3,A4,PARTFUN1:def 8;
        len q = len F1 by Def4;
        then k in dom q by A5,FINSEQ_3:31;
        then
A9:     (q ^ r).k = q.k by FINSEQ_1:def 7
          .= (F1/.k) |^ @(I1/.k) by A5,Def4;
        F1/.k = F1.k by A5,PARTFUN1:def 8
          .= (F1 ^ F2).k by A5,FINSEQ_1:def 7
          .= (F1 ^ F2)/.k by A4,PARTFUN1:def 8;
        hence (q ^ r).k = ((F1 ^ F2)/.k) |^ @((I1 ^ I2)/.k)
        by A6,A7,A8,A9,FINSEQ_1:def 7;
      end;
      suppose ex n be Nat st n in dom F2 & k = len F1 + n;
        then consider n such that
A10:    n in dom F2 and
A11:    k = len F1 + n;
A12:    len r = len F2 & len q = len F1 by Def4;
        then
A13:    n in dom r & n in dom I2 by A1,A10,FINSEQ_3:31;
        then
A14:    (q ^ r).k = r.n & (I1 ^ I2).k = I2.n & (F1 ^ F2).k = F2.n &
        n in dom F2 & (F1 ^ F2).k = (F1 ^ F2)/.k & F2.n = F2/.n
        by A1,A4,A10,A11,A12,FINSEQ_1:def 7,PARTFUN1:def 8;
A15:    (I1 ^ I2)/.k = (I1 ^ I2).k by A3,A4,PARTFUN1:def 8;
        I2/.n = I2.n by A13,PARTFUN1:def 8;
        hence (q ^ r).k = ((F1 ^ F2)/.k) |^ @((I1 ^ I2)/.k) by A14,A15,Def4;
      end;
    end;
    hence (q ^ r).k = ((F1 ^ F2)/.k) |^ @((I1 ^ I2)/.k);
  end;
  hence thesis by A2,Def4;
end;

theorem Th26:
  rng F c= carr H implies Product(F |^ I) in H
proof
  assume that
A1: rng F c= carr H;
  rng(F |^ I) c= carr H
  proof
    let x;
    assume x in rng(F |^ I);
    then consider y such that
A2: y in dom(F |^ I) & x = (F |^ I).y by FUNCT_1:def 5;
    reconsider y as Element of NAT by A2;
    len(F |^ I) = len F by Def4;
    then
A3: y in dom F by A2,FINSEQ_3:31;
    then
A4: x = (F/.y) |^ @(I/.y) by A2,Def4;
    F.y in rng F & F.y = F/.y by A3,FUNCT_1:def 5,PARTFUN1:def 8;
    then F/.y in H by A1,STRUCT_0:def 5;
    then x in H by A4,Th6;
    hence thesis by STRUCT_0:def 5;
  end;
  hence thesis by Th21;
end;

theorem Th27:
  (<*> the carrier of G) |^ (<*> INT) = {}
proof len <*> the carrier of G = 0 & len <*> INT = 0;
  then len((<*> the carrier of G) |^ (<*> INT)) = 0 by Def4;
  hence thesis;
end;

theorem Th28:
  <* a *> |^ <* @i *> = <* a |^ i *>
proof
A1: len<* a *> = 1 & len<* @i *> = 1 & len<* a |^ i*> = 1 by FINSEQ_1:56;
  now
    let k;
    assume
A2: k in dom <* a *>;
A3: i = @i & @i = <* @i *>/.1 by FINSEQ_4:25;
    dom <* a *> = Seg len <* a *> by FINSEQ_1:def 3;
    then
A4: k = 1 by A1,A2,FINSEQ_1:4,TARSKI:def 1;
    hence <* a |^ i *>.k = a |^ i by FINSEQ_1:57
      .= (<* a *>/.k) |^ @(<* @i *>/.k) by A3,A4,FINSEQ_4:25;
  end;
  hence thesis by A1,Def4;
end;

theorem Th29:
  <* a,b *> |^ <* @i,@j *> = <* a |^ i,b |^ j *>
proof len<* a *> = 1 & len<* b *> = 1 & len <* @i *> = 1 & len<* @j *> = 1 &
  <* a,b *> = <* a *> ^ <* b *> & <* @i,@j *> = <* @i *> ^ <* @j *>
  by FINSEQ_1:56,def 9;
  hence <* a,b *> |^ <* @i,@j *> =
  (<* a *> |^ <* @i *>) ^ (<* b *> |^ <* @j *>) by Th25
    .= <* a |^ i *> ^ (<* b *> |^ <* @j *>) by Th28
    .= <* a |^ i *> ^ <* b |^ j *> by Th28
    .= <* a |^ i,b |^ j *> by FINSEQ_1:def 9;
end;

theorem
  <* a,b,c *> |^ <* @i1,@i2,@i3 *> = <* a |^ i1,b |^ i2,c |^ i3 *>
proof len<* a *> = 1 & len<* b,c *> = 2 &
  len <* @i1 *> = 1 & len<* @i2,@i3 *> = 2 &
  <* a,b,c *> = <* a *> ^ <* b,c *> &
  <* @i1,@i2,@i3 *> = <* @i1 *> ^ <* @i2,@i3 *> by FINSEQ_1:56,60,61;
  hence <* a,b,c *> |^ <* @i1,@i2,@i3 *> =
  (<* a *> |^ <* @i1 *>) ^ (<* b,c *> |^ <* @i2,@i3 *>) by Th25
    .= <* a |^ i1 *> ^ (<* b,c *> |^ <* @i2,@i3 *>) by Th28
    .= <* a |^ i1 *> ^ <* b |^ i2,c |^ i3 *> by Th29
    .= <* a |^ i1,b |^ i2,c |^ i3 *> by FINSEQ_1:60;
end;

theorem
  F |^ (len F |-> @(1)) = F
proof
  defpred P[FinSequence of the carrier of G] means
  $1 |^ (len $1 |-> @(1)) = $1;
A1: P[<*> the carrier of G]
  proof
    set A = <*> the carrier of G;
    thus A |^ (len A |-> @(1)) = A |^ (0 |-> @(1))
      .= A |^ (<*> INT) by FINSEQ_2:72
      .= A by Th27;
  end;
A2: for F,a st P[F] holds P[F ^ <* a *>]
  proof
    let F,a;
    set A = F ^ <* a *>;
    assume
A3: P[F];
A4: len A = len F + len<* a *> by FINSEQ_1:35;
A5: len<* a *> = 1 by FINSEQ_1:57;
A6: len<* @(1) *> = 1 by FINSEQ_1:57;
A7: len(len F |-> @(1)) = len F by FINSEQ_2:69;
    thus A |^ (len A |-> @(1)) = A |^ ((len F |-> @(1)) ^ <* @(1) *>)
    by A4,A5,FINSEQ_2:74
      .= (F |^ (len F |-> @(1))) ^ (<* a *> |^ <* @(1) *>)
    by A5,A6,A7,Th25
      .= F ^ <* a |^ 1 *> by A3,Th28
      .= F ^ <* a *> by GROUP_1:44;
  end;
  for F holds P[F] from FINSEQ_2:sch 2(A1,A2);
  hence thesis;
end;

theorem
  F |^ (len F |-> @(0)) = len F |-> 1_G
proof
  defpred P[FinSequence of the carrier of G] means
  $1 |^ (len $1 |-> @(0)) = len $1 |-> 1_G;
A1: P[<*> the carrier of G]
  proof
    set A = <*> the carrier of G;
    thus A |^ (len A |-> @(0)) = A |^ (0 |-> @(0))
      .= A |^ (<*> INT) by FINSEQ_2:72
      .= {} by Th27
      .= len A |-> 1_G by FINSEQ_2:72;
  end;
A3: for F,a st P[F] holds P[F ^ <* a *>]
  proof
    let F,a;
    set A = F ^ <* a *>;
    assume
A4: P[F];
A5: len A = len F + len<* a *> by FINSEQ_1:35;
A6: len<* a *> = 1 by FINSEQ_1:57;
A7: len<* @(0) *> = 1 by FINSEQ_1:57;
A8: len(len F |-> @(0)) = len F by FINSEQ_2:69;
    thus A |^ (len A |-> @(0)) = A |^ ((len F |-> @(0)) ^ <* @(0) *>)
    by A5,A6,FINSEQ_2:74
      .= (F |^ (len F |-> @(0))) ^ (<* a *> |^ <* @(0) *>)
    by A6,A7,A8,Th25
      .= (len F |-> 1_G) ^ <* a |^ 0 *> by A4,Th28
      .= (len F |-> 1_G) ^ <* 1_G *> by GROUP_1:43
      .= (len A |-> 1_G) by A5,A6,FINSEQ_2:74;
  end;
  for F holds P[F] from FINSEQ_2:sch 2(A1,A3);
  hence thesis;
end;

theorem
  len I = n implies (n |-> 1_G) |^ I = n |-> 1_G
proof
  defpred P[Element of NAT] means
  for I st len I = $1 holds ($1 |-> 1_G) |^ I = $1 |-> 1_G;
A1: P[0]
  proof
    let I;
    assume len I = 0;
    then I = <*> INT & (0 |-> 1_G) = {} & {} = <*> the carrier of G
    by FINSEQ_2:72;
    hence (0 |-> 1_G) |^ I = 0 |-> 1_G by Th27;
  end;
A2: for k st P[k] holds P[k + 1]
  proof
    let k;
    assume
A3: P[k];
    let I;
    assume
A4: len I = k + 1;
    reconsider J = I | Seg k as FinSequence of INT by FINSEQ_1:23;
A5: len J = k by A4,FINSEQ_3:59;
    then
A6: (k |-> 1_G) |^ J = k |-> 1_G by A3;
    1 <= k + 1 by NAT_1:12;
    then k + 1 in dom I by A4,FINSEQ_3:27;
    then I.(k + 1) in rng I & rng I c= INT by FINSEQ_1:def 4,FUNCT_1:def 5;
    then reconsider i = I.(k + 1) as Integer;
A7: I = J ^ <* @i *> by A4,FINSEQ_3:61;
A8: (k + 1) |-> 1_G = (k |-> 1_G) ^ <* 1_G *> by FINSEQ_2:74;
    len(k |-> 1_G) = k & len<* 1_G *> = 1 & len<* @i *> = 1
    by FINSEQ_1:57,FINSEQ_2:69;
    then ((k + 1) |-> 1_G) |^ I = ((k |-> 1_G) |^ J) ^ (<* 1_G *> |^ <* @i *>)
    by A5,A7,A8,Th25
      .= (k |-> 1_G) ^ <* (1_G) |^ i *> by A6,Th28
      .= (k |-> 1_G) ^ <* 1_G *> by GROUP_1:61;
    hence thesis by FINSEQ_2:74;
  end;
  for k holds P[k] from NAT_1:sch 1(A1,A2);
  hence thesis;
end;

definition
  let G,A;
  func gr A -> strict Subgroup of G means
  :Def5:
  A c= the carrier of it &
  (for H being strict Subgroup of G
  st A c= the carrier of H holds it is Subgroup of H);
  existence
  proof
    defpred P[set] means ex H st $1 = carr H & A c= $1;
    consider X such that
A1: Y in X iff Y in bool the carrier of G & P[Y] from XBOOLE_0:sch 1;
    set M = meet X;
A2: the carrier of G in bool the carrier of G &
    the carrier of the multMagma of G = the carrier of (Omega).G &
    A c= the carrier of G & carr (Omega).G = the carrier of (Omega).
    G by ZFMISC_1:def 1;
    then
A3: the carrier of G in X by A1;
A4: X <> {} by A1,A2;
    now
      let Y;
      assume Y in X;
      then consider H such that
A5:   Y = carr H and A c= Y by A1;
      1_G in H by GROUP_2:55;
      hence 1_G in Y by A5,STRUCT_0:def 5;
    end;
    then
A6: M <> {} by A4,SETFAM_1:def 1;
    M c= the carrier of G
    proof consider x such that
A7:   x in X by A3;
      consider H such that
A8:   x = carr H and A c= x by A1,A7;
      let y;
      assume y in M;
      then y in carr H & carr H c= the carrier of G by A7,A8,SETFAM_1:def 1;
      hence thesis;
    end;
    then reconsider M as Subset of G;
A9: now
      let a,b;
      assume
A10:  a in M & b in M;
      now
        let Y;
        assume
A11:    Y in X;
        then consider H such that
A12:    Y = carr H and A c= Y by A1;
        a in carr H & b in carr H by A10,A11,A12,SETFAM_1:def 1;
        hence a * b in Y by A12,GROUP_2:89;
      end;
      hence a * b in M by A4,SETFAM_1:def 1;
    end;
    now
      let a;
      assume
A13:  a in M;
      now
        let Y;
        assume
A14:    Y in X;
        then consider H such that
A15:    Y = carr H and A c= Y by A1;
        a in carr H by A13,A14,A15,SETFAM_1:def 1;
        hence a" in Y by A15,GROUP_2:90;
      end;
      hence a" in M by A4,SETFAM_1:def 1;
    end;
    then consider H being strict Subgroup of G such that
A16: the carrier of H = M by A6,A9,GROUP_2:61;
    take H;
    now
      let Y;
      assume Y in X;
      then ex H st Y = carr H & A c= Y by A1;
      hence A c= Y;
    end;
    hence A c= the carrier of H by A4,A16,SETFAM_1:6;
    let H1 be strict Subgroup of G;
    assume
A17: A c= the carrier of H1;
    the carrier of H1 = carr H1;
    then the carrier of H1 in X by A1,A17;
    hence H is Subgroup of H1 by A16,GROUP_2:66,SETFAM_1:4;
  end;
  uniqueness
  proof
    let H1,H2 be strict Subgroup of G;
    assume A c= the carrier of H1 & (for H being strict Subgroup of G
    st A c= the carrier of H holds H1 is Subgroup of H) &
    A c= the carrier of H2 & (for H being strict Subgroup of G
    st A c= the carrier of H holds H2 is Subgroup of H);
    then H1 is Subgroup of H2 & H2 is Subgroup of H1;
    hence thesis by GROUP_2:64;
  end;
end;

canceled 3;

theorem Th37:
  a in gr A iff ex F,I st len F = len I & rng F c= A & Product(F |^ I) = a
proof
  thus a in gr A implies ex F,I st len F = len I & rng F c= A &
  Product(F |^ I) = a
  proof
    assume
A1: a in gr A;
    defpred P[set] means
    ex F,I st $1 = Product (F |^ I) & len F = len I & rng F c= A;
    reconsider B = {b : P[b]} as Subset of G from DOMAIN_1:sch 7;
    1_G = Product <*> the carrier of G & <*> the carrier of G = {} &
    (<*> the carrier of G) |^ (<*> INT) = {} &
    rng <*> the carrier of G = {} & {} c= A & len {} = 0 & len<*>INT = 0
    by Th11,Th27,XBOOLE_1:2;
    then
A2: 1_G in B;
A3: now
      let c,d;
      assume that
A4:   c in B and
A5:   d in B;
A6:   ex d1 being Element of G st c = d1 &
      ex F,I st d1 = Product(F |^ I) & len F = len I & rng F c= A by A4;
A7:   ex d2 being Element of G st d = d2 &
      ex F,I st d2 = Product(F |^ I) & len F = len I & rng F c= A by A5;
      consider F1,I1 such that
A8:   c = Product(F1 |^ I1) and
A9:   len F1 = len I1 and
A10:  rng F1 c= A by A6;
      consider F2,I2 such that
A11:  d = Product(F2 |^ I2) and
A12:  len F2 = len I2 and
A13:  rng F2 c= A by A7;
A14:  c * d = Product((F1 |^ I1) ^ (F2 |^ I2)) by A8,A11,Th8
        .= Product((F1 ^ F2) |^ (I1 ^ I2)) by A9,A12,Th25;
      rng(F1 ^ F2) = rng F1 \/ rng F2 by FINSEQ_1:44;
      then
A15:  rng(F1 ^ F2) c= A by A10,A13,XBOOLE_1:8;
      len(F1 ^ F2) = len I1 + len I2 by A9,A12,FINSEQ_1:35
        .= len(I1 ^ I2) by FINSEQ_1:35;
      hence c * d in B by A14,A15;
    end;
    now
      let c;
      assume c in B;
      then ex d1 being Element of G st c = d1 &
      ex F,I st d1 = Product(F |^ I) & len F = len I & rng F c= A;
      then consider F1,I1 such that
A16:  c = Product(F1 |^ I1) and
A17:  len F1 = len I1 and
A18:  rng F1 c= A;
A19:  dom F1 = dom I1 by A17,FINSEQ_3:31;
      deffunc F(Nat) = F1.(len F1 - $1 + 1);
      consider F2 being FinSequence such that
A20:  len F2 = len F1 and
A21:  for k be Nat st k in dom F2 holds F2.k = F(k) from FINSEQ_1:sch 2;
A22:  Seg len F1 = dom F1 by FINSEQ_1:def 3;
      defpred P[Nat,set] means ex i st i = I1.(len I1 - $1 + 1) & $2 = - i;
A24:  for k be Nat st k in Seg len I1 ex x st P[k,x]
      proof
        let k be Nat;
        assume k in Seg len I1;
        then
A25:    k in dom I1 by FINSEQ_1:def 3;
        then reconsider n = len I1 - k + 1 as Element of NAT by Lm3;
        1 <= n & n <= len I1 by A25,Lm3;
        then n in dom I1 by FINSEQ_3:27;
        then I1.n in rng I1 & rng I1 c= INT by FINSEQ_1:def 4,FUNCT_1:def 5;
        then reconsider i = I1.n as Element of INT qua non empty set;
        reconsider i as Integer;
        reconsider x = - i as set;
        take x,i;
        thus thesis;
      end;
      consider I2 being FinSequence such that
A26:  dom I2 = Seg len I1 and
A27:  for k be Nat st k in Seg len I1 holds P[k,I2.k]
      from FINSEQ_1:sch 1(A24);
A28:  Seg len I1 = dom I1 by FINSEQ_1:def 3;
A29:  len F2 = len I2 by A17,A20,A26,FINSEQ_1:def 3;
A30:  dom F2 = dom I2 by A17,A20,A26,FINSEQ_1:def 3;
A31:  rng F2 c= rng F1
      proof
        let x;
        assume x in rng F2;
        then consider y such that
A32:    y in dom F2 & F2.y = x by FUNCT_1:def 5;
        reconsider y as Element of NAT by A32;
A33:    x = F1.(len F1 - y + 1) by A21,A32;
        reconsider n = len F1 - y + 1 as Element of NAT by A20,A32,Lm3;
A34:    1 <= n by A20,A32,Lm3;
        n <= len F1 by A20,A32,Lm3;
        then n in dom F1 by A34,FINSEQ_3:27;
        hence thesis by A33,FUNCT_1:def 5;
      end;
      then
A35:  rng F2 c= A by A18,XBOOLE_1:1;
      rng F1 c= the carrier of G by FINSEQ_1:def 4;
      then rng F2 c= the carrier of G by A31,XBOOLE_1:1;
      then reconsider F2 as FinSequence of the carrier of G by FINSEQ_1:def 4;
      rng I2 c= INT
      proof
        let x;
        assume x in rng I2;
        then consider y such that
A36:    y in dom I2 and
A37:    x = I2.y by FUNCT_1:def 5;
        reconsider y as Element of NAT by A36;
        ex i st i = I1.(len I1 - y + 1) & x = - i by A26,A27,A36,A37;
        hence thesis by INT_1:def 2;
      end;
      then reconsider I2 as FinSequence of INT by FINSEQ_1:def 4;
      set p = F1 |^ I1;
      set q = F2 |^ I2;
A38:  len p = len F1 & len q = len F2 by Def4;
      then
A39:  dom p = dom F1 & dom q = dom F2 by FINSEQ_3:31;
      now
        let k;
        assume
A40:    k in dom q;
        then reconsider n = len p - k + 1 as Element of NAT by A20,A38, Lm3;
        1 <= n & len p >= n by A20,A38,A40,Lm3;
        then
A41:    n in dom I2 & n in Seg len I2 by A20,A29,A38,FINSEQ_1:3, FINSEQ_3:27;
        dom q = dom I1 by A17,A20,A38,FINSEQ_3:31;
        then consider i such that
A42:    i = I1.n & I2.k = - i by A17,A27,A28,A38,A40;
        I2.k = I2/.k & I2/.k = @(I2/.k) by A30,A39,A40,PARTFUN1:def 8;
        then q.k = (F2/.k) |^ (- i) & F2/.k = F2.k & F2.k = F1.n &
        F1/.n= F1.n by A17,A21,A22,A26,A38,A39,A40,A41,A42,Def4,
        PARTFUN1:def 8;
        then
A43:    q.k = ((F1/.n) |^ i)" by GROUP_1:70;
        I1.n = I1/.n & I1/.n = @(I1/.n) by A26,A28,A41,PARTFUN1:def 8;
        then p.n = (F1/.n) |^ i by A19,A26,A28,A41,A42,Def4;
        then (p/.n)" = ((F1/.n) |^ i)" & q/.k = q.k
        by A19,A26,A28,A39,A40,A41,PARTFUN1:def 8;
        then p/.n = (q/.k)" by A43,GROUP_1:19;
        hence (q/.k)" = p.(len p - k + 1)
        by A19,A26,A28,A39,A41,PARTFUN1:def 8;
      end;
      then Product p" = Product q by A20,A38,Th17;
      hence c" in B by A16,A29,A35;
    end;
    then consider H being strict Subgroup of G such that
A44: the carrier of H = B by A2,A3,GROUP_2:61;
    A c= B
    proof
      let x;
      assume
A45:  x in A;
      then reconsider a = x as Element of G;
      reconsider p = 1 as Integer;
A46:  len<* a *> = 1 & len<* @p *> = 1 by FINSEQ_1:56;
A47:  Product(<* a *> |^ <* @p *>) = Product<* a |^ 1 *> by Th28
        .= a |^ 1 by FINSOP_1:12
        .= a by GROUP_1:44;
      rng<* a *> = {a} & {a} c= A by A45,FINSEQ_1:56,ZFMISC_1:37;
      hence thesis by A46,A47;
    end;
    then gr A is Subgroup of H by A44,Def5;
    then a in H by A1,GROUP_2:49;
    then a in B by A44,STRUCT_0:def 5;
    then ex b st
    b = a & ex F,I st b = Product(F |^ I) & len F = len I & rng F c= A;
    hence thesis;
  end;
  given F,I such that len F = len I and
A48: rng F c= A and
A49: Product(F |^ I) = a;
  A c= the carrier of gr A by Def5;
  then rng F c= carr gr A by A48,XBOOLE_1:1;
  hence thesis by A49,Th26;
end;

theorem Th38:
  a in A implies a in gr A
proof
  assume
A1: a in A;
  A c= the carrier of gr A by Def5;
  hence thesis by A1,STRUCT_0:def 5;
end;

theorem
  gr {} the carrier of G = (1).G
proof
A1: {} the carrier of G c= the carrier of (1).G by XBOOLE_1:2;
  for H being strict Subgroup of G
  st {} the carrier of G c= the carrier of H holds
  (1).G is Subgroup of H by GROUP_2:77;
  hence thesis by A1,Def5;
end;

theorem Th40:
  for H being strict Subgroup of G holds gr carr H = H
proof
  let H be strict Subgroup of G;
  for H1 be strict Subgroup of G st carr H c= the carrier of H1 holds
  H is Subgroup of H1 by GROUP_2:66;
  hence thesis by Def5;
end;

theorem Th41:
  A c= B implies gr A is Subgroup of gr B
proof
  assume
A1: A c= B;
  now
    let a;
    assume a in gr A;
    then consider F,I such that
A2: len F = len I & rng F c= A & Product(F |^ I) = a by Th37;
    rng F c= B by A1,A2,XBOOLE_1:1;
    hence a in gr B by A2,Th37;
  end;
  hence thesis by GROUP_2:67;
end;

theorem
  gr(A /\ B) is Subgroup of gr A /\ gr B
proof
  now
    let a;
    assume a in gr(A /\ B);
    then consider F,I such that
A1: len F = len I & rng F c= A /\ B & Product(F |^ I) = a by Th37;
    A /\ B c= A & A /\ B c= B by XBOOLE_1:17;
    then rng F c= A & rng F c= B by A1,XBOOLE_1:1;
    then a in gr A & a in gr B by A1,Th37;
    hence a in gr A /\ gr B by GROUP_2:99;
  end;
  hence thesis by GROUP_2:67;
end;

theorem Th43:
  the carrier of gr A = meet{B : ex H being strict Subgroup of G
  st B = the carrier of H & A c= carr H}
proof
  defpred P[Subgroup of G] means A c= carr $1;
  A c= the carrier of G & the carrier of (Omega).G = carr (Omega).G &
  (Omega).G = the multMagma of G;
  then
A1: ex H being strict Subgroup of G st P[H];
  consider H being strict Subgroup of G such that
A2: the carrier of H = meet{B : ex H being strict Subgroup of G
  st B = the carrier of H & P[H]} from MeetSbgEx(A1);
  set X = {B :ex H being strict Subgroup of G
  st B = the carrier of H & A c= carr H};
A3: carr (Omega).G in X;
  now
    let Y;
    assume Y in X;
    then ex B st Y = B & ex H being strict Subgroup of G
    st B = the carrier of H & A c= carr H;
    hence A c= Y;
  end;
  then
A4: A c= the carrier of H by A2,A3,SETFAM_1:6;
  now
    let H1 be strict Subgroup of G;
    assume
A5: A c= the carrier of H1;
    the carrier of H1 = carr H1;
    then the carrier of H1 in X by A5;
    hence H is Subgroup of H1 by A2,GROUP_2:66,SETFAM_1:4;
  end;
  hence thesis by A2,A4,Def5;
end;

theorem Th44:
  gr A = gr(A \ {1_G})
proof
  set X = {B : ex H being strict Subgroup of G
  st B = the carrier of H & A c= carr H};
  set Y = {C : ex H being strict Subgroup of G
  st C = the carrier of H & A \ {1_G} c= carr H};
A1: X = Y
  proof
    thus X c= Y
    proof
      let x;
      assume x in X;
      then consider B such that
A2:   x = B and
A3:   ex H being strict Subgroup of G st B = the carrier of H & A c= carr H;
      consider H being strict Subgroup of G such that
A4:   B = the carrier of H and
A5:   A c= carr H by A3;
      A \ {1_G} c= A by XBOOLE_1:36;
      then A \ {1_G} c= carr H by A5,XBOOLE_1:1;
      hence thesis by A2,A4;
    end;
    let x;
    assume x in Y;
    then consider B such that
A6: x = B and
A7: ex H being strict Subgroup of G
    st B = the carrier of H & A \ {1_G} c= carr H;
    consider H being strict Subgroup of G such that
A8: B = the carrier of H and
A9: A \ {1_G} c= carr H by A7;
    1_G in H by GROUP_2:55;
    then 1_G in carr H by STRUCT_0:def 5;
    then {1_G} c= carr H by ZFMISC_1:37;
    then
A10: (A \ {1_G}) \/ {1_G} c= carr H by A9,XBOOLE_1:8;
    now per cases;
      suppose 1_G in A;
        then {1_G} c= A by ZFMISC_1:37;
        hence A c= carr H by A10,XBOOLE_1:45;
      end;
      suppose not 1_G in A;
        hence A c= carr H by A9,ZFMISC_1:65;
      end;
    end;
    hence thesis by A6,A8;
  end;
  the carrier of gr A = meet X by Th43
    .= the carrier of gr(A \ {1_G}) by A1,Th43;
  hence thesis by GROUP_2:68;
end;

definition
  let G,a;
  attr a is generating means
  :Def6:
  not for A st gr A = the multMagma of G
  holds gr(A \ {a}) = the multMagma of G;
end;

canceled;

theorem
  1_G is non generating
proof
  let A;
  assume gr A = the multMagma of G;
  hence thesis by Th44;
end;

definition
  let G, H;
  attr H is maximal means
  :Def7:
  the multMagma of H <> the multMagma of G &
  for K being strict Subgroup of G st
  the multMagma of H <> K & H is Subgroup of K holds K = the multMagma of G;
end;

canceled;

theorem Th48:
  for G being strict Group, H being strict Subgroup of G,
  a being Element of G holds
  H is maximal & not a in H implies gr(carr H \/ {a}) = G
proof
  let G be strict Group, H be strict Subgroup of G, a be Element of G;
  assume that
A1: H is maximal and
A2: not a in H;
  a in {a} by TARSKI:def 1;
  then carr H c= carr H \/ {a} & a in carr H \/ {a}
  by XBOOLE_0:def 2,XBOOLE_1:7;
  then gr carr H is Subgroup of gr(carr H \/ {a}) &
  a in gr(carr H \/ {a}) by Th38,Th41;
  then H is Subgroup of gr(carr H \/ {a}) & H <> gr(carr H \/ {a}) by A2,Th40;
  hence thesis by A1,Def7;
end;

::
::  Frattini subgroup.
::

definition
  let G be Group;
  func Phi(G) -> strict Subgroup of G means
  :Def8:
  the carrier of it =
  meet{A where A is Subset of G : ex H being strict Subgroup of G
  st A = the carrier of H & H is maximal} if
  ex H being strict Subgroup of G st H is maximal otherwise
  it = the multMagma of G;
  existence
  proof
    defpred P[Subgroup of G] means $1 is maximal;
    now per cases;
      suppose
A1:     ex H being strict Subgroup of G st P[H];
        ex H being strict Subgroup of G st the carrier of H =
        meet{A where A is Subset of G:      ex K being strict Subgroup of G
        st A = the carrier of K & P[K]} from MeetSbgEx(A1);
        hence thesis by A1;
      end;
      suppose
A2:     for H being strict Subgroup of G holds H is not maximal;
        (Omega).G = the multMagma of G & (Omega).G is Subgroup of G;
        hence thesis by A2;
      end;
    end;
    hence thesis;
  end;
  uniqueness by GROUP_2:68;
  correctness;
end;

canceled 3;

theorem Th52:
  for G being Group holds
  (ex H being strict Subgroup of G st H is maximal) implies (a in Phi(G) iff
  for H being strict Subgroup of G st H is maximal holds a in H)
proof
  let G be Group;
  assume
A1: ex H being strict Subgroup of G st H is maximal;
  set X = {A where A is Subset of G: ex K being strict Subgroup of G
  st A = the carrier of K & K is maximal};
  thus a in Phi(G) implies
  for H being strict Subgroup of G st H is maximal holds a in H
  proof
    assume a in Phi(G);
    then a in the carrier of Phi(G) by STRUCT_0:def 5;
    then
A2: a in meet X by A1,Def8;
    let H be strict Subgroup of G;
    assume H is maximal;
    then carr H in X;
    then a in carr H by A2,SETFAM_1:def 1;
    hence thesis by STRUCT_0:def 5;
  end;
  consider H being strict Subgroup of G such that
A3: H is maximal by A1;
A4: carr H in X by A3;
  assume
A5: for H being strict Subgroup of G st H is maximal holds a in H;
  now
    let Y;
    assume Y in X;
    then consider A being Subset of G such that
A6: Y = A and
A7: ex H being strict Subgroup of G st A = the carrier of H & H is maximal;
    consider H being strict Subgroup of G such that
A8: A = the carrier of H & H is maximal by A7;
    A = carr H & a in H by A5,A8;
    hence a in Y by A6,STRUCT_0:def 5;
  end;
  then a in meet X by A4,SETFAM_1:def 1;
  then a in the carrier of Phi(G) by A1,Def8;
  hence thesis by STRUCT_0:def 5;
end;

theorem
  for G being Group, a being Element of G holds
  (for H being strict Subgroup of G holds H is not maximal) implies a in Phi(G)
proof
  let G be Group, a be Element of G;
  assume for H being strict Subgroup of G holds H is not maximal;
  then Phi(G) = the multMagma of G by Def8;
  hence thesis by STRUCT_0:def 5;
end;

theorem Th54:
  for G being Group, H being strict Subgroup of G holds
  H is maximal implies Phi(G) is Subgroup of H
proof
  let G be Group, H be strict Subgroup of G;
  assume H is maximal;
  then for a be Element of G holds a in Phi(G) implies a in H by Th52;
  hence thesis by GROUP_2:67;
end;

theorem Th55:
  for G being strict Group holds
  the carrier of Phi(G) = {a where a is Element of G: a is non generating}
proof
  let G be strict Group;
  set A = {a where a is Element of G: a is non generating};
  thus the carrier of Phi(G) c= A
  proof
    let x;
    assume
A1: x in the carrier of Phi(G);
    assume
A2: not x in A;
    x in Phi(G) by A1,STRUCT_0:def 5;
    then x in G by GROUP_2:49;
    then reconsider a = x as Element of G by STRUCT_0:def 5;
    a is generating by A2;
    then consider B being Subset of G such that
A3: gr B = G and
A4: gr(B \ {a}) <> G by Def6;
    set M = B \ {a};
    defpred P[Subgroup of G] means M c= carr $1 & not a in $1;
    consider X such that
A5: X c= Subgroups G and
A6: for H being strict Subgroup of G holds H in X iff P[H] from SubgrSep;
A7: M c= carr gr M by Def5;
    now
      assume
A8:   a in gr M;
      now
        let b be Element of G;
        b in gr B by A3,STRUCT_0:def 5;
        then consider F being FinSequence of the carrier of G,
        I such that len I = len F and
A9:     rng F c= B and
A10:    b = Product(F |^ I) by Th37;
        rng(F |^ I) c= carr gr M
        proof
          let x;
          assume x in rng(F |^ I);
          then consider y such that
A11:      y in dom(F |^ I) and
A12:      (F |^ I).y = x by FUNCT_1:def 5;
          reconsider y as Element of NAT by A11;
          len(F |^ I) = len F by Def4;
          then
A13:      y in dom F by A11,FINSEQ_3:31;
          then
A14:      x = (F/.y) |^ @(I/.y) by A12,Def4;
          now per cases;
            suppose F/.y = a;
              then x in gr M by A8,A14,Th6;
              hence thesis by STRUCT_0:def 5;
            end;
            suppose
A15:          F/.y <> a;
              F/.y = F.y & F.y in rng F by A13,FUNCT_1:def 5,PARTFUN1:def 8;
              then F/.y in B & not F/.y in {a} by A9,A15,TARSKI:def 1;
              then F/.y in M by XBOOLE_0:def 4;
              then x in gr M by A14,Th6,Th38;
              hence thesis by STRUCT_0:def 5;
            end;
          end;
          hence thesis;
        end;
        hence b in gr M by A10,Th21;
      end;
      hence contradiction by A4,GROUP_2:71;
    end;
    then reconsider X as non empty set by A6,A7;
    defpred P[set,set] means ex H1,H2 being strict Subgroup of G st
    $1 = H1 & $2 = H2 & H1 is Subgroup of H2;
A16: now
      let x be Element of X;
      x in Subgroups G by A5,TARSKI:def 3;
      hence x is strict Subgroup of G by GROUP_3:def 1;
    end;
A17: for x being Element of X holds P[x,x]
    proof
      let x be Element of X;
      reconsider H = x as strict Subgroup of G by A16;
      H is Subgroup of H by GROUP_2:63;
      hence thesis;
    end;
A18: for x,y being Element of X st P[x,y] & P[y,x] holds x = y by GROUP_2:64;
A19: for x,y,z being Element of X st P[x,y] & P[y,z] holds P[x,z]
    proof
      let x,y,z be Element of X;
      given H1,H2 being strict Subgroup of G such that
A20:  x = H1 & y = H2 & H1 is Subgroup of H2;
      given H3,H4 being strict Subgroup of G such that
A21:  y = H3 & z = H4 & H3 is Subgroup of H4;
      H1 is Subgroup of H4 by A20,A21,GROUP_2:65;
      hence thesis by A20,A21;
    end;
A22: for Y st Y c= X & (for x,y being Element of X st x in Y & y in Y holds
    P[x,y] or P[y,x]) ex y being Element of X st
    for x being Element of X st x in Y holds P[x,y]
    proof
      let Y;
      assume
A23:  Y c= X;
      assume
A24:  for x,y being Element of X st x in Y & y in Y holds P[x,y] or P[y,x];
      set C = {D where D is Subset of G :
      ex H being strict Subgroup of G st H in Y & D = carr H};
      now
        let Z be set;
        assume Z in C;
        then ex D being Subset of G st Z = D &
        ex H being strict Subgroup of G st H in Y & D = carr H;
        hence Z c= the carrier of G;
      end;
      then reconsider E = union C as Subset of G by ZFMISC_1:94;
      now per cases;
        suppose
A25:      Y = {};
          consider y being Element of X;
          take y;
          let x be Element of X;
          assume x in Y;
          hence P[x,y] by A25;
        end;
        suppose
A26:      Y <> {};
          consider s being Element of Y;
A27:      s in X by A23,A26,TARSKI:def 3;
          then reconsider s as strict Subgroup of G by A5,GROUP_3:def 1;
A28:      carr s in C by A26;
          then carr s c= E & M c= carr s by A6,A27,ZFMISC_1:92;
          then
A29:      M c= E by XBOOLE_1:1;
A30:      E <> {} by A28,ORDERS_1:91;
A31:      now
            let a,b be Element of G;
            assume that
A32:        a in E and
A33:        b in E;
            consider Z being set such that
A34:        a in Z and
A35:        Z in C by A32, TARSKI:def 4;
            consider D being Subset of G such that
A36:        Z = D and
A37:        ex H being strict Subgroup of G st H in Y & D = carr H by A35;
            consider H1 being Subgroup of G such that
A38:        H1 in Y & D = carr H1 by A37;
            consider Z1 being set such that
A39:        b in Z1 and
A40:        Z1 in C by A33,TARSKI:def 4;
            consider B being Subset of G such that
A41:        Z1 = B and
A42:        ex H being strict Subgroup of G st H in Y & B = carr H by A40;
            consider H2 being Subgroup of G such that
A43:        H2 in Y & B = carr H2 by A42;
            now per cases by A23,A24,A38,A43;
              suppose P[H1,H2];
                then carr H1 c= the carrier of H2 by GROUP_2:def 5;
                then a * b in carr H2 by A34,A36,A38,A39,A41,A43, GROUP_2:89;
                hence a * b in E by A40,A41,A43,TARSKI:def 4;
              end;
              suppose P[H2,H1];
                then carr H2 c= the carrier of H1 by GROUP_2:def 5;
                then a * b in carr H1 by A34,A36,A38,A39,A41,A43, GROUP_2:89;
                hence a * b in E by A35,A36,A38,TARSKI:def 4;
              end;
            end;
            hence a * b in E;
          end;
          now
            let a be Element of G;
            assume a in E;
            then consider Z being set such that
A44:        a in Z and
A45:        Z in C by TARSKI:def 4;
            consider D being Subset of G such that
A46:        Z = D and
A47:        ex H being strict Subgroup of G st H in Y & D = carr H by A45;
            consider H1 being Subgroup of G such that
A48:        H1 in Y & D = carr H1 by A47;
            a" in carr H1 by A44,A46,A48,GROUP_2:90;
            hence a" in E by A45,A46,A48,TARSKI:def 4;
          end;
          then consider H being strict Subgroup of G such that
A49:      the carrier of H = E by A30,A31,GROUP_2:61;
A50:      now
            assume a in H;
            then a in E by A49,STRUCT_0:def 5;
            then consider Z being set such that
A51:        a in Z and
A52:        Z in C by TARSKI:def 4;
            consider D being Subset of G such that
A53:        Z = D and
A54:        ex H being strict Subgroup of G st H in Y & D = carr H by A52;
            consider H1 being strict Subgroup of G such that
A55:        H1 in Y & D = carr H1 by A54;
            not a in H1 by A6,A23,A55;
            hence contradiction by A51,A53,A55,STRUCT_0:def 5;
          end;
          the carrier of H = carr H;
          then reconsider y = H as Element of X by A6,A29,A49,A50;
          take y;
          let x be Element of X;
          assume
A56:      x in Y;
          x in Subgroups G by A5,TARSKI:def 3;
          then reconsider K = x as strict Subgroup of G by GROUP_3:def 1;
          take K,H;
          thus x = K & y = H;
          carr K = the carrier of K;
          then the carrier of K in C by A56;
          hence K is Subgroup of H by A49,GROUP_2:66,ZFMISC_1:92;
        end;
      end;
      hence thesis;
    end;
    consider s being Element of X such that
A57: for y being Element of X st s <> y holds not P[s,y] from ORDERS_1:sch 1
    (A17,A18,A19,A22);
    reconsider H = s as strict Subgroup of G by A16;
    H is maximal
    proof
      not a in H by A6;
      hence the multMagma of H <> the multMagma of G by STRUCT_0:def 5;
      let K be strict Subgroup of G;
      assume that
A58:  K <> the multMagma of H and
A59:  H is Subgroup of K and
A60:  K <> the multMagma of G;
      the carrier of H = carr H & the carrier of K = carr K &
      the carrier of H c= the carrier of K & M c= carr H
      by A6,A59,GROUP_2:def 5;
      then
A61:  M c= carr K by XBOOLE_1:1;
      now
        assume a in K;
        then a in carr K by STRUCT_0:def 5;
        then {a} c= carr K by ZFMISC_1:37;
        then
A62:    M \/ {a} c= carr K by A61,XBOOLE_1:8;
        B c= M \/ {a}
        proof
          let x;
          assume
A63:      x in B;
          now per cases;
            suppose x = a;
              then x in {a} by TARSKI:def 1;
              hence thesis by XBOOLE_0:def 2;
            end;
            suppose x <> a;
              then not x in {a} by TARSKI:def 1;
              then x in M by A63,XBOOLE_0:def 4;
              hence thesis by XBOOLE_0:def 2;
            end;
          end;
          hence thesis;
        end;
        then gr B is Subgroup of gr carr K by A62,Th41,XBOOLE_1:1;
        then G is Subgroup of K by A3,Th40;
        hence contradiction by A60,GROUP_2:64;
      end;
      then reconsider v = K as Element of X by A6,A61;
      not P[s,v] by A57,A58;
      hence thesis by A59;
    end;
    then Phi(G) is Subgroup of H by Th54;
    then the carrier of Phi(G) c= the carrier of H by GROUP_2:def 5;
    then x in H & not a in H by A1,A6,STRUCT_0:def 5;
    hence thesis;
  end;
  let x;
  assume x in A;
  then consider a being Element of G such that
A64: x = a and
A65: a is non generating;
  now per cases;
    suppose for H being strict Subgroup of G holds H is not maximal;
      then Phi(G) = G by Def8;
      hence thesis by A64;
    end;
    suppose
A66:  ex H being strict Subgroup of G st H is maximal;
      now
        let H be strict Subgroup of G;
        assume
A67:    H is maximal;
        now
          assume
A68:      not a in H;
          then
A69:      gr(carr H \/ {a}) = G by A67,Th48;
A70:      carr H misses {a}
          proof
            thus carr H /\ {a} c= {}
            proof
              let x;
              assume x in carr H /\ {a};
              then x in carr H & x in {a} by XBOOLE_0:def 3;
              then x in carr H & x = a by TARSKI:def 1;
              hence thesis by A68,STRUCT_0:def 5;
            end;
            thus {} c= carr H /\ {a} by XBOOLE_1:2;
          end;
          (carr H \/ {a}) \ {a} = carr H \ {a} by XBOOLE_1:40
            .= carr H by A70,XBOOLE_1:83;
          then gr((carr H \/ {a}) \ {a}) = H & H <> G by A67,Def7,Th40;
          hence contradiction by A65,A69,Def6;
        end;
        hence a in H;
      end;
      then a in Phi(G) by A66,Th52;
      hence thesis by A64,STRUCT_0:def 5;
    end;
  end;
  hence thesis;
end;

theorem
  for G being strict Group, a being Element of G holds
  a in Phi(G) iff a is non generating
proof
  let G be strict Group, a be Element of G;
  thus a in Phi(G) implies a is non generating
  proof
    assume a in Phi(G);
    then a in the carrier of Phi(G) by STRUCT_0:def 5;
    then a in {b where b is Element of G:  b is non generating} by Th55;
    then ex b being Element of G st a = b & b is non generating;
    hence thesis;
  end;
  assume a is non generating;
  then a in {b where b is Element of G : b is non generating};
  then a in the carrier of Phi(G) by Th55;
  hence thesis by STRUCT_0:def 5;
end;

definition
  let G,H1,H2;
  func H1 * H2 -> Subset of G equals

  carr H1 * carr H2;
  correctness;
end;

theorem
  H1 * H2 = carr H1 * carr H2 & H1 * H2 = H1 * carr H2 &
  H1 * H2 = carr H1 * H2;

canceled;

theorem
  H1 * H2 * H3 = H1 * (H2 * H3)
proof
  thus H1 * H2 * H3 = carr H1 * (carr H2 * H3) by GROUP_2:116
    .= H1 * (H2 * H3);
end;

theorem
  a * H1 * H2 = a * (H1 * H2)
proof
  thus a * H1 * H2 = a * (carr H1 * H2) by GROUP_2:116
    .= a * (H1 * H2);
end;

theorem
  H1 * H2 * a = H1 * (H2 * a)
proof
  thus H1 * H2 * a = H1 * carr H2 * a
    .= H1 * (H2 * a) by GROUP_2:118;
end;

theorem
  A * H1 * H2 = A * (H1 * H2)
proof
  thus A * H1 * H2 = A * (H1 * carr H2) by GROUP_2:119
    .= A * (H1 * H2);
end;

theorem
  H1 * H2 * A = H1 * (H2 * A)
proof
  thus H1 * H2 * A = H1 * carr H2 * A
    .= H1 * (H2 * A) by GROUP_2:118;
end;

definition
  let G,H1,H2;
  func H1 "\/" H2 -> strict Subgroup of G equals

  gr(carr H1 \/ carr H2);
  correctness;
end;

canceled 3;

theorem
  a in H1 "\/" H2 iff
  ex F,I st len F = len I & rng F c= carr H1 \/ carr H2 & a = Product(F |^ I)
  by Th37;

theorem
  H1 "\/" H2 = gr(H1 * H2)
proof
  set H = gr(carr H1 * carr H2);
  carr H1 \/ carr H2 c= carr H1 * carr H2
  proof
    let x;
    assume
A1: x in carr H1 \/ carr H2;
    then reconsider a = x as Element of G;
    now per cases by A1,XBOOLE_0:def 2;
      suppose
A2:     x in carr H1;
        1_G in H2 by GROUP_2:55;
        then 1_G in carr H2 & a * 1_G = a by GROUP_1:def 5,STRUCT_0:def 5;
        hence thesis by A2;
      end;
      suppose
A3:     x in carr H2;
        1_G in H1 by GROUP_2:55;
        then 1_G in carr H1 & 1_G * a = a by GROUP_1:def 5,STRUCT_0:def 5;
        hence thesis by A3;
      end;
    end;
    hence thesis;
  end;
  then
A4: H1 "\/" H2 is Subgroup of H by Th41;
  now
    let a;
    assume a in H;
    then consider F,I such that
A5: len F = len I and
A6: rng F c= carr H1 * carr H2 and
A7: a = Product(F |^ I) by Th37;
    rng(F |^ I) c= carr(H1 "\/" H2)
    proof
      let x;
      set f = F |^ I;
      assume x in rng f;
      then consider y such that
A8:   y in dom f and
A9:   f.y = x by FUNCT_1:def 5;
      reconsider y as Element of NAT by A8;
A10:  len f = len F by Def4;
      then
A11:  y in dom I by A5,A8,FINSEQ_3:31;
      then I.y in rng I & rng I c= INT by FINSEQ_1:def 4,FUNCT_1:def 5;
      then reconsider i = I.y as Integer;
A12:  y in dom F by A8,A10,FINSEQ_3:31;
      I/.y = I.y & @(I/.y) = I/.y by A11,PARTFUN1:def 8;
      then
A13:  x = (F/.y) |^ i by A9,A12,Def4;
      y in dom F by A8,A10,FINSEQ_3:31;
      then F/.y = F.y & F.y in rng F by FUNCT_1:def 5,PARTFUN1:def 8;
      then consider b,c such that
A14:  F/.y = b * c and
A15:  b in carr H1 and
A16:  c in carr H2 by A6,GROUP_2:12;
      now per cases;
        suppose i >= 0;
          then reconsider n = i as Element of NAT by INT_1:16;
          defpred P[Nat,set] means ($1 mod 2 = 1 implies $2 = b) &
          ($1 mod 2 = 0 implies $2 = c);
A18:      for k be Nat st k in Seg(2 * n) ex x st P[k,x]
          proof
            let k be Nat;
            assume k in Seg(2 * n);
            now per cases by NAT_D:12;
              suppose
A19:            k mod 2 = 1;
                reconsider x = b as set;
                take x;
                thus (k mod 2 = 1 implies x = b) &
                (k mod 2 = 0 implies x = c) by A19;
              end;
              suppose
A20:            k mod 2 = 0;
                reconsider x = c as set;
                take x;
                thus (k mod 2 = 1 implies x = b) &
                (k mod 2 = 0 implies x = c) by A20;
              end;
            end;
            hence thesis;
          end;
          consider p being FinSequence such that
A21:      dom p = Seg(2 * n) and
A22:      for k be Nat st k in Seg(2 * n) holds P[k,p.k]
          from FINSEQ_1:sch 1(A18);
A23:      len p = 2 * n by A21,FINSEQ_1:def 3;
A24:      rng p c= {b,c}
          proof
            let x;
            assume x in rng p;
            then consider y such that
A25:        y in dom p and
A26:        p.y = x by FUNCT_1:def 5;
            reconsider y as Element of NAT by A25;
            now per cases by NAT_D:12;
              suppose y mod 2 = 0;
                then x = c by A21,A22,A25,A26;
                hence thesis by TARSKI:def 2;
              end;
              suppose y mod 2 = 1;
                then x = b by A21,A22,A25,A26;
                hence thesis by TARSKI:def 2;
              end;
            end;
            hence thesis;
          end;
          then rng p c= the carrier of G by XBOOLE_1:1;
          then reconsider p as FinSequence of the carrier of G
          by FINSEQ_1:def 4;
          defpred Q[Nat] means $1 <= 2 * n & $1 mod 2 = 0 implies
          for F1 st F1 = p | Seg $1 holds Product(F1) = (F/.y) |^ ($1 div 2);
A27:      for k being Nat st for l being Nat st l < k holds Q[l] holds Q[k]
          proof
            let k be Nat;
            assume
A28:        for l being Nat st l < k holds Q[l];
            assume that
A29:        k <= 2 * n and
A30:        k mod 2 = 0;
            let F1;
            assume
A31:        F1 = p | Seg k;
            now per cases;
              suppose
A32:            k = 0;
                then F1 = <*> the carrier of G by A31,FINSEQ_1:4 ,RELAT_1:110;
                then
A33:            Product F1 = 1_G by Th11;
                2 * 0 = 0;
                then 0 div 2 = 0 by NAT_D:18;
                hence thesis by A32,A33,GROUP_1:43;
              end;
              suppose k <> 0;
                then
A34:            k >= 1 by NAT_1:14;
                k <> 1 by A30,NAT_D:14;
                then k > 1 by A34,XXREAL_0:1;
                then k >= 1 + 1 by NAT_1:13;
                then k - 2 >= 2 - 2 by XREAL_1:11;
                then reconsider m = k - 2 as Element of NAT by INT_1:16;
A35:            m + 2 = k;
                then
A36:            m < k by NAT_1:16;
A37:            m <= 2 * n by A29,A35,NAT_1:16,XXREAL_0:2;
                1 * 2 = 2;
                then
A38:            m mod 2 = 0 by A30,NAT_D:15;
                reconsider q = p | Seg m
                as FinSequence of the carrier of G by FINSEQ_1:23;
A39:            Product q = (F/.y) |^ (m div 2) by A28,A36,A37,A38;
A40:            ex j being Nat st 2 * n = k + j by A29,NAT_1:10;
                ex o being Nat st 2 * n = m + o by A37,NAT_1:10;
                then
A41:            len F1 = k & len q = m by A23,A31,A40, FINSEQ_3:59;
A42:            len F1 = m + 2 by A23,A31,A40,FINSEQ_3:59
                  .= len q + len<* b,c *> by A41,FINSEQ_1:61;
A43:            now
                  let l;
                  assume
A44:              l in dom q;
                  then 1 <= l & l <= len q & len q <= len F1
                  by A35,A41,FINSEQ_3:27,NAT_1:16;
                  then 1 <= l & l <= len F1 by XXREAL_0:2;
                  then l in dom F1 by FINSEQ_3:27;
                  then F1.l = p.l & q.l = p.l by A31,A44,FUNCT_1:70;
                  hence F1.l = q.l;
                end;
                now
                  let l;
                  assume l in dom<* b,c *>;
                  then
A45:              l in {1,2} by FINSEQ_1:4,FINSEQ_3:29;
                  now per cases by A45,TARSKI:def 2;
                    suppose
A46:                  l = 1;
                      then
A47:                  <* b,c *>.l = b by FINSEQ_1:61;
                      m + 1 <= k & 1 <= m + 1 by A36,NAT_1:12,13;
                      then
A48:                  m + 1 in dom F1 by A41, FINSEQ_3:27;
                      then
A49:                  F1.(len q + l) = p.(m + 1) by A31,A41,A46,FUNCT_1:70;
A50:                  m + 1 mod 2 = 1 by A38,NAT_D:16;
                      dom F1 c= dom p by A31,RELAT_1:89;
                      hence F1.(len q + l) = <* b,c *>.l by A21,A22
                      ,A47,A48,A49,A50;
                    end;
                    suppose
A51:                  l = 2;
                      then
A52:                  <* b,c *>.l = c by FINSEQ_1:61;
                      1 <= m + (1 + 1) by NAT_1:12;
                      then
A53:                  m + 2 in dom F1 by A41, FINSEQ_3:27;
                      then
A54:                  F1.(len q + l) = p.(m + 2) by A31,A41,A51,FUNCT_1:70;
                      dom F1 c= dom p by A31,RELAT_1:89;
                      hence F1.(len q + l) = <* b,c *>.l by A21,A22
                      ,A30,A52,A53,A54;
                    end;
                  end;
                  hence F1.(len q + l) = <* b,c *>.l;
                end;
                then F1 = q ^ <* b,c *> by A42,A43,FINSEQ_3:43;
                then
A55:            Product F1 = Product q * Product<* b,c *> by Th8
                  .= Product q * (F/.y) by A14,FINSOP_1:13
                  .= (F/.y) |^ ((m div 2) + 1) by A39,GROUP_1:49;
                m div 2*1 + 1 = (m div 2) + (2 div 2) by NAT_D:18
                  .= k div 2 by A35,A38,NAT_D:19;
                hence thesis by A55;
              end;
            end;
            hence thesis;
          end;
A56:      for k being Nat holds Q[k] from NAT_1:sch 4(A27);
A57:      2 * n mod 2 = 0 by NAT_D:13;
A58:      2 * n div 2 = n by NAT_D:18;
          len p = 2 * n by A21,FINSEQ_1:def 3;
          then p = p | Seg(2 * n) by FINSEQ_3:55;
          then
A59:      x = Product(p) by A13,A56,A57,A58;
          b in carr H1 \/ carr H2 & c in carr H1 \/ carr H2 by A15,A16,
          XBOOLE_0:def 2;
          then {b,c} c= carr H1 \/ carr H2 by ZFMISC_1:38;
          then
A60:      rng p c= carr H1 \/ carr H2 by A24,XBOOLE_1:1;
          carr H1 \/ carr H2 c= the carrier of gr(carr H1 \/ carr H2) &
          carr gr(carr H1 \/ carr H2) =
          the carrier of gr(carr H1 \/ carr H2) by Def5;
          then x in gr(carr H1 \/ carr H2) by A59,A60,Th21,XBOOLE_1:1;
          hence thesis by STRUCT_0:def 5;
        end;
        suppose
A61:      i < 0;
          set n = abs( i );
          defpred P[Nat,set] means ($1 mod 2 = 1 implies $2 = c") &
          ($1 mod 2 = 0 implies $2 = b");
A63:      for k be Nat st k in Seg(2 * n) ex x st P[k,x]
          proof
            let k be Nat;
            assume k in Seg(2 * n);
            now per cases by NAT_D:12;
              suppose
A64:            k mod 2 = 1;
                reconsider x = c" as set;
                take x;
                thus (k mod 2 = 1 implies x = c") &
                (k mod 2 = 0 implies x = b") by A64;
              end;
              suppose
A65:            k mod 2 = 0;
                reconsider x = b" as set;
                take x;
                thus (k mod 2 = 1 implies x = c") &
                (k mod 2 = 0 implies x = b") by A65;
              end;
            end;
            hence thesis;
          end;
          consider p being FinSequence such that
A66:      dom p = Seg(2 * n) and
A67:      for k be Nat st k in Seg(2 * n) holds P[k,p.k]
          from FINSEQ_1:sch 1(A63);
A68:      len p = 2 * n by A66,FINSEQ_1:def 3;
A69:      rng p c= {b",c"}
          proof
            let x;
            assume x in rng p;
            then consider y such that
A70:        y in dom p and
A71:        p.y = x by FUNCT_1:def 5;
            reconsider y as Element of NAT by A70;
            now per cases by NAT_D:12;
              suppose y mod 2 = 0;
                then x = b" by A66,A67,A70,A71;
                hence thesis by TARSKI:def 2;
              end;
              suppose y mod 2 = 1;
                then x = c" by A66,A67,A70,A71;
                hence thesis by TARSKI:def 2;
              end;
            end;
            hence thesis;
          end;
          then rng p c= the carrier of G by XBOOLE_1:1;
          then reconsider p as FinSequence of the carrier of G
          by FINSEQ_1:def 4;
          defpred Q[Nat] means $1 <= 2 * n & $1 mod 2 = 0 implies
          for F1 st F1 = p | Seg $1 holds
          Product(F1) = ((F/.y) |^ ($1 div 2))";
A72:      for k being Nat st for l being Nat st l < k holds Q[l] holds Q[k]
          proof
            let k be Nat;
            assume
A73:        for l being Nat st l < k holds Q[l];
            assume that
A74:        k <= 2 * n and
A75:        k mod 2 = 0;
            let F1;
            assume
A76:        F1 = p | Seg k;
            now per cases;
              suppose
A77:            k = 0;
                then F1 = <*> the carrier of G by A76,FINSEQ_1:4 ,RELAT_1:110;
                then
A78:            Product F1 = 1_G by Th11;
                2 * 0 = 0;
                then 0 div 2 = 0 by NAT_D:18;
                then (F/.y) |^ (k div 2) = 1_G by A77,GROUP_1:43;
                hence thesis by A78,GROUP_1:16;
              end;
              suppose k <> 0;
                then
A79:            k >= 1 by NAT_1:14;
                k <> 1 by A75,NAT_D:14;
                then k > 1 by A79,XXREAL_0:1;
                then k >= 1 + 1 by NAT_1:13;
                then k - 2 >= 2 - 2 by XREAL_1:11;
                then reconsider m = k - 2 as Element of NAT by INT_1:16;
A80:            m + 2 = k;
                then
A81:            m < k by NAT_1:16;
A82:            m <= 2 * n by A74,A80,NAT_1:16,XXREAL_0:2;
                1 * 2 = 2;
                then
A83:            m mod 2 = 0 by A75,NAT_D:15;
                reconsider q = p | Seg m
                as FinSequence of the carrier of G by FINSEQ_1:23;
A84:            Product q = ((F/.y) |^ (m div 2))" by A73,A81,A82,A83;
A85:            ex j being Nat st 2 * n = k + j by A74,NAT_1:10;
                ex o being Nat st 2 * n = m + o by A82,NAT_1:10;
                then
A86:            len F1 = k & len q = m by A68,A76,A85, FINSEQ_3:59;
A87:            len F1 = m + 2 by A68,A76,A85,FINSEQ_3:59
                  .= len q + len<* c",b" *> by A86,FINSEQ_1:61;
A88:            now
                  let l;
                  assume
A89:              l in dom q;
                  then 1 <= l & l <= len q & len q <= len F1
                  by A80,A86,FINSEQ_3:27,NAT_1:16;
                  then 1 <= l & l <= len F1 by XXREAL_0:2;
                  then l in dom F1 by FINSEQ_3:27;
                  then F1.l = p.l & q.l = p.l by A76,A89,FUNCT_1:70;
                  hence F1.l = q.l;
                end;
                now
                  let l;
                  assume l in dom<* c",b" *>;
                  then
A90:              l in {1,2} by FINSEQ_1:4,FINSEQ_3:29;
                  now per cases by A90,TARSKI:def 2;
                    suppose
A91:                  l = 1;
                      then
A92:                  <* c",b" *>.l = c" by FINSEQ_1 :61;
                      m + 1 <= k & 1 <= m + 1 by A81,NAT_1:12,13;
                      then
A93:                  m + 1 in dom F1 by A86, FINSEQ_3:27;
                      then
A94:                  F1.(len q + l) = p.(m + 1) by A76,A86,A91,FUNCT_1:70;
A95:                  m + 1 mod 2 = 1 by A83,NAT_D:16;
                      dom F1 c= dom p by A76,RELAT_1:89;

hence F1.(len q + l) = <* c",b" *>.l by A66,A67,A92,A93,A94,A95;
                    end;
                    suppose
A96:                  l = 2;
                      then
A97:                  <* c",b" *>.l = b" by FINSEQ_1 :61;
                      1 <= m + (1 + 1) by NAT_1:12;
                      then
A98:                  m + 2 in dom F1 by A86, FINSEQ_3:27;
                      then
A99:                  F1.(len q + l) = p.(m + 2) by A76,A86,A96,FUNCT_1:70;
                      dom F1 c= dom p by A76,RELAT_1:89;

hence F1.(len q + l) = <* c",b" *>.l by A66,A67,A75,A97,A98,A99;
                    end;
                  end;
                  hence F1.(len q + l) = <* c",b" *>.l;
                end;
                then F1 = q ^ <* c",b" *> by A87,A88,FINSEQ_3:43;
                then
A100:           Product F1 = Product q * Product<* c",b" *> by Th8
                  .= Product q * (c" * b") by FINSOP_1:13
                  .= Product q * (F/.y)" by A14,GROUP_1:25
                  .= ((F/.y) * ((F/.y) |^ (m div 2)))" by A84,GROUP_1:25
                  .= ((F/.y) |^ ((m div 2) + 1))" by GROUP_1:49;
                m div 2*1 + 1 = (m div 2) + (2 div 2) by NAT_D:18
                  .= k div 2 by A80,A83,NAT_D:19;
                hence thesis by A100;
              end;
            end;
            hence thesis;
          end;
A101:     for k being Nat holds Q[k] from NAT_1:sch 4(A72);
A102:     2 * n mod 2 = 0 by NAT_D:13;
A103:     2 * n div 2 = n by NAT_D:18;
          len p = 2 * n by A66,FINSEQ_1:def 3;
          then
A104:     p = p | Seg(2 * n) by FINSEQ_3:55;
          x = ((F/.y) |^ n)" by A13,A61,GROUP_1:56;
          then
A105:     x = Product(p) by A101,A102,A103,A104;
          b" in carr H1 & c" in carr H2 by A15,A16,GROUP_2:90;
          then b" in carr H1 \/ carr H2 &
          c" in carr H1 \/ carr H2 by XBOOLE_0:def 2;
          then {b",c"} c= carr H1 \/ carr H2 by ZFMISC_1:38;
          then
A106:     rng p c= carr H1 \/ carr H2 by A69,XBOOLE_1:1;
          carr H1 \/ carr H2 c= the carrier of gr(carr H1 \/ carr H2) &
          carr gr(carr H1 \/ carr H2) =
          the carrier of gr(carr H1 \/ carr H2) by Def5;
          then x in gr(carr H1 \/ carr H2) by A105,A106,Th21,XBOOLE_1:1;
          hence thesis by STRUCT_0:def 5;
        end;
      end;
      hence thesis;
    end;
    hence a in H1 "\/" H2 by A7,Th21;
  end;
  then H is Subgroup of H1 "\/" H2 by GROUP_2:67;
  hence gr(H1 * H2) = H1 "\/" H2 by A4,GROUP_2:64;
end;

theorem Th69:
  H1 * H2 = H2 * H1 implies the carrier of H1 "\/" H2 = H1 * H2
proof
  assume H1 * H2 = H2 * H1;
  then consider H being strict Subgroup of G such that
A1: the carrier of H = carr H1 * carr H2 by GROUP_2:93;
  carr H1 \/ carr H2 c= carr H1 * carr H2
  proof
    let x;
    assume
A2: x in carr H1 \/ carr H2;
    then reconsider a = x as Element of G;
    now per cases by A2,XBOOLE_0:def 2;
      suppose
A3:     x in carr H1;
        1_G in H2 by GROUP_2:55;
        then 1_G in carr H2 & a * 1_G = a by GROUP_1:def 5,STRUCT_0:def 5;
        hence thesis by A3;
      end;
      suppose
A4:     x in carr H2;
        1_G in H1 by GROUP_2:55;
        then 1_G in carr H1 & 1_G * a = a by GROUP_1:def 5,STRUCT_0:def 5;
        hence thesis by A4;
      end;
    end;
    hence thesis;
  end;
  then
A5: H1 "\/" H2 is Subgroup of H by A1,Def5;
  now
    let a;
    assume a in H;
    then a in carr H1 * carr H2 by A1,STRUCT_0:def 5;
    then consider b,c such that
A6: a = b * c and
A7: b in carr H1 & c in carr H2;
    reconsider p = 1 as Integer;
A8: len<* b,c *> = 2 & len<* @p,@p *> = 2 by FINSEQ_1:61;
A9: rng<* b,c *> = {b,c} by FINSEQ_2:147;
    b in carr H1 \/ carr H2 & c in carr H1 \/ carr H2 by A7, XBOOLE_0:def 2;
    then
A10: {b,c} c= carr H1 \/ carr H2 by ZFMISC_1:38;
    a = Product<* b *> * c by A6,FINSOP_1:12
      .= Product<* b *> * Product<* c *> by FINSOP_1:12
      .= Product(<* b *> ^ <* c *>) by Th8
      .= Product<* b,c *> by FINSEQ_1:def 9
      .= Product<* b |^ p, c *> by GROUP_1:44
      .= Product<* b |^ p, c |^ p *> by GROUP_1:44
      .= Product(<* b,c *> |^ <* @p,@p *>) by Th29;
    hence a in H1 "\/" H2 by A8,A9,A10,Th37;
  end;
  then H is Subgroup of H1 "\/" H2 by GROUP_2:67;
  hence thesis by A1,A5,GROUP_2:64;
end;

theorem
  G is commutative Group implies the carrier of H1 "\/" H2 = H1 * H2
proof
  assume G is commutative Group;
  then H1 * H2 = H2 * H1 by GROUP_2:29;
  hence thesis by Th69;
end;

theorem Th71:
  for N1,N2 being strict normal Subgroup of G
  holds the carrier of N1 "\/" N2 = N1 * N2
proof
  let N1,N2 be strict normal Subgroup of G;
  N1 * N2 = N2 * N1 by GROUP_3:148;
  hence thesis by Th69;
end;

theorem
  for N1,N2 being strict normal Subgroup of G
  holds N1 "\/" N2 is normal Subgroup of G
proof
  let N1,N2 be strict normal Subgroup of G;
  consider N being strict normal Subgroup of G such that
A1: the carrier of N = carr N1 * carr N2 by GROUP_3:149;
  the carrier of N1 "\/" N2 = N1 * N2 & N1 * N2 = carr N1 * carr N2 by Th71;
  hence thesis by A1,GROUP_2:68;
end;

theorem
  for H being strict Subgroup of G holds H "\/" H = H by Th40;

theorem
  H1 "\/" H2 = H2 "\/" H1;

Lm4: H1 is Subgroup of H1 "\/" H2
proof carr H1 c= carr H1 \/ carr H2 &
  carr H1 \/ carr H2 c= the carrier of gr(carr H1 \/ carr H2) &
  H1 "\/" H2 = gr(carr H1 \/ carr H2) &
  the carrier of H1 = carr H1 by Def5,XBOOLE_1:7;
  hence thesis by GROUP_2:66,XBOOLE_1:1;
end;

Lm5: H1 "\/" H2 "\/" H3 is Subgroup of H1 "\/" (H2 "\/" H3)
proof
  now
    let a;
    assume a in H1 "\/" H2 "\/" H3;
    then consider F,I such that
A1: len F = len I and
A2: rng F c= carr(H1 "\/" H2) \/ carr H3 and
A3: a = Product(F |^ I) by Th37;
    rng F c= carr gr(carr H1 \/ carr(H2 "\/" H3))
    proof
      let x;
      assume
A4:   x in rng F;
      now per cases by A2,A4,XBOOLE_0:def 2;
        suppose
A5:       x in carr(H1 "\/" H2);
          then
A6:       x in H1 "\/" H2 by STRUCT_0:def 5;
          reconsider b = x as Element of G by A5;
          consider F1,I1 such that
A7:       len F1 = len I1 and
A8:       rng F1 c= carr H1 \/ carr H2 and
A9:       b = Product(F1 |^ I1) by A6,Th37;
          H2 is Subgroup of H2 "\/" H3 by Lm4;
          then the carrier of H2 c= the carrier of H2 "\/" H3 &
          the carrier of H2 = carr H2 & the carrier of H3 = carr H3 &
          H2 "\/" H3 = gr(carr H2 \/ carr H3) by GROUP_2:def 5;
          then carr H1 \/ carr H2 c= carr H1 \/ carr(H2 "\/" H3)
          by XBOOLE_1:9;
          then rng F1 c= carr H1 \/ carr(H2 "\/" H3) by A8,XBOOLE_1:1;
          then b in H1 "\/" (H2 "\/" H3) by A7,A9,Th37;
          hence thesis by STRUCT_0:def 5;
        end;
        suppose x in carr H3;
          then x in H3 & H3 is Subgroup of H3 "\/" H2 by Lm4,STRUCT_0:def 5;
          then x in H3 "\/" H2 by GROUP_2:49;
          then x in carr(H2 "\/" H3) by STRUCT_0:def 5;
          then x in carr H1 \/ carr(H2 "\/" H3) & carr H1 \/ carr(H2 "\/" H3)
          c= the carrier of gr(carr H1 \/ carr(H2 "\/" H3))
          by Def5,XBOOLE_0:def 2;
          hence thesis;
        end;
      end;
      hence thesis;
    end;
    then a in gr carr gr(carr H1 \/ carr(H2 "\/" H3)) by A1,A3,Th37;
    hence a in H1 "\/" (H2 "\/" H3) by Th40;
  end;
  hence thesis by GROUP_2:67;
end;

theorem Th75:
  H1 "\/" H2 "\/" H3 = H1 "\/" (H2 "\/" H3)
proof
A1: H1 "\/" H2 "\/" H3 is Subgroup of H1 "\/" (H2 "\/" H3) by Lm5;
  H2 "\/" H3 "\/" H1 is Subgroup of H2 "\/" (H3 "\/" H1) by Lm5;
  then H2 "\/" H3 "\/" H1 is Subgroup of H3 "\/" H1 "\/" H2 &
  H3 "\/" H1 "\/" H2 is Subgroup of H3 "\/" (H1 "\/" H2) by Lm5;
  then H1 "\/" (H2 "\/" H3) is Subgroup of H3 "\/" (H1 "\/" H2) by GROUP_2:
  65;
  hence thesis by A1,GROUP_2:64;
end;

theorem
  for H being strict Subgroup of G holds (1).G "\/" H = H & H "\/" (1).G = H
proof
  let H be strict Subgroup of G;
A1: carr(1).G = {1_G} by GROUP_2:def 7;
  1_G in H by GROUP_2:55;
  then 1_G in carr H by STRUCT_0:def 5;
  then {1_G} c= carr H by ZFMISC_1:37;
  then
A2: {1_G} \/ carr H = carr H by XBOOLE_1:12;
  hence (1).G "\/" H = H by A1,Th40;
  thus thesis by A1,A2,Th40;
end;

theorem Th77:
  (Omega).G "\/" H = (Omega).G & H "\/" (Omega).G = (Omega).G
proof
A1: (the carrier of (Omega).
  G) \/ carr H = [#] the carrier of G by SUBSET_1:28;
  hence (Omega).G "\/" H = (Omega).G by Th40;
  thus thesis by A1,Th40;
end;

theorem Th78:
  H1 is Subgroup of H1 "\/" H2 & H2 is Subgroup of H1 "\/" H2
proof H1 "\/" H2 = H2 "\/" H1;
  hence thesis by Lm4;
end;

theorem Th79:
  for H2 being strict Subgroup of G holds
  H1 is Subgroup of H2 iff H1 "\/" H2 = H2
proof
  let H2 be strict Subgroup of G;
  thus H1 is Subgroup of H2 implies H1 "\/" H2 = H2
  proof
    assume H1 is Subgroup of H2;
    then the carrier of H1 c= the carrier of H2 &
    the carrier of H1 = carr H1 & carr H2 = the carrier of H2
    by GROUP_2:def 5;
    hence H1 "\/" H2 = gr carr H2 by XBOOLE_1:12
      .= H2 by Th40;
  end;
  thus thesis by Th78;
end;

theorem
  H1 is Subgroup of H2 implies H1 is Subgroup of H2 "\/" H3
proof H2 is Subgroup of H2 "\/" H3 by Th78;
  hence thesis by GROUP_2:65;
end;

theorem
  for H3 being strict Subgroup of G holds
  H1 is Subgroup of H3 & H2 is Subgroup of H3 implies H1 "\/"
  H2 is Subgroup of H3
proof
  let H3 be strict Subgroup of G;
  assume that
A1: H1 is Subgroup of H3 and
A2: H2 is Subgroup of H3;
  now
    let a;
    assume a in H1 "\/" H2;
    then consider F,I such that
A3: len F = len I & rng F c= carr H1 \/ carr H2 & a = Product(F |^ I) by Th37;
    the carrier of H1 c= the carrier of H3 &
    the carrier of H2 c= the carrier of H3 &
    the carrier of H1 = carr H1 & the carrier of H2 = carr H2 &
    the carrier of H3 = carr H3 by A1,A2,GROUP_2:def 5;
    then carr H1 \/ carr H2 c= carr H3 by XBOOLE_1:8;
    then rng F c= carr H3 by A3,XBOOLE_1:1;
    then a in gr carr H3 by A3,Th37;
    hence a in H3 by Th40;
  end;
  hence thesis by GROUP_2:67;
end;

theorem
  for H3,H2 being strict Subgroup of G holds
  H1 is Subgroup of H2 implies H1 "\/" H3 is Subgroup of H2 "\/" H3
proof
  let H3,H2 be strict Subgroup of G;
  assume
A1: H1 is Subgroup of H2;
  (H1 "\/" H3) "\/" (H2 "\/" H3) = H1 "\/" H3 "\/" H2 "\/" H3 by Th75
    .= H1 "\/" (H3 "\/" H2) "\/" H3 by Th75
    .= H1 "\/" (H2 "\/" H3) "\/" H3
    .= H1 "\/" H2 "\/" H3 "\/" H3 by Th75
    .= H2 "\/" H3 "\/" H3 by A1,Th79
    .= H2 "\/" (H3 "\/" H3) by Th75
    .= H2 "\/" H3 by Th40;
  hence thesis by Th79;
end;

theorem
  H1 /\ H2 is Subgroup of H1 "\/" H2
proof H1 /\ H2 is Subgroup of H1 & H1 is Subgroup of H1 "\/" H2
  by Th78,GROUP_2:106;
  hence thesis by GROUP_2:65;
end;

theorem Th84:
  for H2 being strict Subgroup of G holds (H1 /\ H2) "\/" H2 = H2
proof
  let H2 be strict Subgroup of G;
  H1 /\ H2 is Subgroup of H2 by GROUP_2:106;
  hence thesis by Th79;
end;

theorem Th85:
  for H1 being strict Subgroup of G holds H1 /\ (H1 "\/" H2) = H1
proof
  let H1 be strict Subgroup of G;
  H1 is Subgroup of H1 "\/" H2 by Th78;
  hence thesis by GROUP_2:107;
end;

theorem
  for H1,H2 being strict Subgroup of G holds H1 "\/" H2 = H2 iff H1 /\ H2 = H1
proof
  let H1,H2 be strict Subgroup of G;
  (H1 "\/" H2 = H2 iff H1 is Subgroup of H2) &
  (H1 /\ H2 = H1 iff H1 is Subgroup of H2) by Th79,GROUP_2:107;
  hence thesis;
end;

reserve S1,S2 for Element of Subgroups G;

definition
  let G;
  func SubJoin G -> BinOp of Subgroups G means
  :Def11:
  for S1,S2,H1,H2 st S1 = H1 & S2 = H2 holds it.(S1,S2) = H1 "\/" H2;
  existence
  proof
    defpred P[set,set,set] means
    for H1,H2 st $1 = H1 & $2 = H2 holds $3 = H1 "\/" H2;
A1: for S1,S2 ex B being Element of Subgroups G st P[S1,S2,B]
    proof
      let S1,S2;
      reconsider H1 = S1, H2 = S2 as Subgroup of G by GROUP_3:def 1;
      reconsider C = H1 "\/" H2 as Element of Subgroups(G) by GROUP_3:def 1;
      take C;
      thus thesis;
    end;
    ex o being BinOp of Subgroups G st
    for a,b being Element of Subgroups G holds P[a,b,o.(a,b)]
    from BINOP_1:sch 3(A1);
    hence thesis;
  end;
  uniqueness
  proof
    let o1,o2 be BinOp of Subgroups(G);
    assume
A2: for S1,S2,H1,H2 st S1 = H1 & S2 = H2 holds o1.(S1,S2) = H1 "\/" H2;
    assume
A3: for S1,S2,H1,H2 st S1 = H1 & S2 = H2 holds o2.(S1,S2) = H1 "\/" H2;
    now
      let x,y be set;
      assume
A4:   x in Subgroups(G) & y in Subgroups(G);
      then reconsider A = x, B = y as Element of Subgroups(G);
      reconsider H1 = x, H2 = y as Subgroup of G by A4,GROUP_3:def 1;
      o1.(A,B) = H1 "\/" H2 & o2.(A,B) = H1 "\/" H2 by A2,A3;
      hence o1.(x,y) = o2.(x,y);
    end;
    hence thesis by BINOP_1:1;
  end;
end;

definition
  let G;
  func SubMeet G -> BinOp of Subgroups G means
  :Def12:
  for S1,S2,H1,H2 st S1 = H1 & S2 = H2 holds it.(S1,S2) = H1 /\ H2;
  existence
  proof
    defpred P[set,set,set] means
    for H1,H2 st $1 = H1 & $2 = H2 holds $3 = H1 /\ H2;
A1: for S1,S2 ex B being Element of Subgroups G st P[S1,S2,B]
    proof
      let S1,S2;
      reconsider H1 = S1, H2 = S2 as Subgroup of G by GROUP_3:def 1;
      reconsider C = H1 /\ H2 as Element of Subgroups(G) by GROUP_3:def 1;
      take C;
      thus thesis;
    end;
    ex o being BinOp of Subgroups G st
    for a,b being Element of Subgroups G holds P[a,b,o.(a,b)]
    from BINOP_1:sch 3(A1);
    hence thesis;
  end;
  uniqueness
  proof
    let o1,o2 be BinOp of Subgroups(G);
    assume
A2: for S1,S2,H1,H2 st S1 = H1 & S2 = H2 holds o1.(S1,S2) = H1 /\ H2;
    assume
A3: for S1,S2,H1,H2 st S1 = H1 & S2 = H2 holds o2.(S1,S2) = H1 /\ H2;
    now
      let x,y be set;
      assume
A4:   x in Subgroups(G) & y in Subgroups(G);
      then reconsider A = x, B = y as Element of Subgroups(G);
      reconsider H1 = x, H2 = y as Subgroup of G by A4,GROUP_3:def 1;
      o1.(A,B) = H1 /\ H2 & o2.(A,B) = H1 /\ H2 by A2,A3;
      hence o1.(x,y) = o2.(x,y);
    end;
    hence thesis by BINOP_1:1;
  end;
end;

Lm6: for G being Group holds
LattStr (# Subgroups G, SubJoin G, SubMeet G #) is Lattice &
LattStr (# Subgroups G, SubJoin G, SubMeet G #) is 0_Lattice &
LattStr (# Subgroups G, SubJoin G, SubMeet G #) is 1_Lattice
proof
  let G be Group;
  set L = LattStr (# Subgroups G, SubJoin G, SubMeet G #);
A1: now
    let A,B be Element of L;
    let H1,H2 be Subgroup of G;
    assume
A2: A = H1 & B = H2;
    reconsider A'= A, B'= B as Element of Subgroups G;
    thus A "/\" B = SubMeet(G).(A',B') by LATTICES:def 2
      .= H1 /\ H2 by A2,Def12;
  end;
A3: now
    let A,B be Element of L;
    let H1,H2 be Subgroup of G;
    assume
A4: A = H1 & B = H2;
    reconsider A'= A, B'= B as Element of Subgroups G;
    thus A "\/" B = SubJoin(G).(A',B') by LATTICES:def 1
      .= H1 "\/" H2 by A4,Def11;
  end;
  now
    let A,B,C be Element of L;
    reconsider H1 = A, H2 = B, H3 = C as strict Subgroup of G
    by GROUP_3:def 1;
    thus A "\/" B = H1 "\/" H2 by A3
      .= H2 "\/" H1
      .= B "\/" A by A3;
A5: H1 "\/" H2 = A "\/" B & H2 "\/" H3 = B "\/" C by A3;
    hence A "\/" B "\/" C = H1 "\/" H2 "\/" H3 by A3
      .= H1 "\/" (H2 "\/" H3) by Th75
      .= A "\/" (B "\/" C) by A3,A5;
A6: H1 /\ H2 = A "/\" B by A1;
    hence (A "/\" B) "\/" B = (H1 /\ H2) "\/" H2 by A3
      .= B by Th84;
    thus A "/\" B = H2 /\ H1 by A1
      .= B "/\" A by A1;
A7: H2 /\ H3 = B "/\" C by A1;
    thus A "/\" B "/\" C = H1 /\ H2 /\ H3 by A1,A6
      .= H1 /\ (H2 /\ H3) by GROUP_2:102
      .= A "/\" (B "/\" C) by A1,A7;
    thus A "/\" (A "\/" B) = H1 /\ (H1 "\/" H2) by A1,A5
      .= A by Th85;
  end;
  then
A8: L is join-commutative join-associative meet-absorbing
  meet-commutative meet-associative join-absorbing
  by LATTICES:def 4,def 5,def 6,def 7,def 8,def 9;
  hence L is Lattice;
  reconsider L as Lattice by A8;
  reconsider E = (1).G as Element of L by GROUP_3:def 1;
  now
    let A be Element of L;
    reconsider H = A as Subgroup of G by GROUP_3:def 1;
    thus E "/\" A = (1).G /\ H by A1
      .= E by GROUP_2:103;
    hence A "/\" E = E;
  end;
  hence LattStr (# Subgroups G, SubJoin G, SubMeet G #) is 0_Lattice
  by LATTICES:def 13;
  reconsider F = (Omega).G as Element of L by GROUP_3:def 1;
  now
    let A be Element of L;
    reconsider H = A as Subgroup of G by GROUP_3:def 1;
    thus F "\/" A = (Omega).G "\/" H by A3
      .= F by Th77;
    hence A "\/" F = F;
  end;
  hence LattStr (# Subgroups G, SubJoin G, SubMeet G #) is 1_Lattice
  by LATTICES:def 14;
end;

definition
  let G be Group;
  func lattice G -> strict Lattice equals
  LattStr (# Subgroups G, SubJoin G, SubMeet G #);
  coherence by Lm6;
end;

canceled 5;

theorem
  for G being Group holds the carrier of lattice G = Subgroups G;

theorem
  for G being Group holds the L_join of lattice G = SubJoin G;

theorem
  for G being Group holds the L_meet of lattice G = SubMeet G;

registration
  let G be Group;
  cluster lattice G -> lower-bounded upper-bounded;
  coherence by Lm6;
end;

canceled 3;

theorem
  for G being Group holds Bottom lattice G = (1).G
proof
  let G be Group;
  set L = lattice G;
  reconsider E = (1).G as Element of L by GROUP_3:def 1;
  now
    let A be Element of L;
    reconsider H = A as Subgroup of G by GROUP_3:def 1;
    thus A "/\" E = SubMeet(G).(A,E) by LATTICES:def 2
      .= H /\ (1).G by Def12
      .= E by GROUP_2:103;
  end;
  hence thesis by RLSUB_2:84;
end;

theorem
  for G being Group holds Top lattice G = (Omega).G
proof
  let G be Group;
  set L = lattice G;
  reconsider E = (Omega). G as Element of L by GROUP_3:def 1;
  now
    let A be Element of L;
    reconsider H = A as Subgroup of G by GROUP_3:def 1;
    thus A "\/" E = SubJoin(G).(A,E) by LATTICES:def 1
      .= H "\/" (Omega).G by Def11
      .= E by Th77;
  end;
  hence thesis by RLSUB_2:85;
end;

