:: Some Properties of Circles on the Plane
::  by Artur Korni{\l}owicz and Yasunari Shidama
::
:: Received October 18, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies PRE_TOPC, EUCLID, RELAT_1, ARYTM, ZF_LANG, SUBSET_1, CARD_3,
      SEQ_1, TOPS_1, METRIC_1, INT_1, FUNCT_1, SIN_COS, BORSUK_2, ORDINAL2,
      ARYTM_1, MCART_1, COMPLEX1, SQUARE_1, T_0TOPSP, TOPS_2, ARYTM_3, RCOMP_1,
      ASYMPT_0, ABSVALUE, FUNCOP_1, REALSET1, TOPMETR, JGRAPH_2, SGRAPH1,
      BOOLE, SETFAM_1, TARSKI, COMPTS_1, TOPREAL1, TOPREAL2, FCONT_1, TAXONOM2,
      FINSEQ_1, TOPREALB, PARTFUN1, TMAP_1, CONNSP_2, XREAL_0, JGRAPH_6,
      SIN_COS6, BORSUK_4, FUNCT_5, TOPREALA, NAT_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, SETFAM_1, ORDINAL1, REALSET2, FUNCT_1,
      RELSET_1, PARTFUN1, FUNCT_2, SEQ_1, NUMBERS, XCMPLX_0, XREAL_0, REAL_1,
      COMPLEX1, SQUARE_1, INT_1, CARD_3, FINSEQ_1, FUNCT_4, RCOMP_1, RCOMP_2,
      FCONT_1, DOMAIN_1, STRUCT_0, PRE_TOPC, GRCAT_1, TOPS_1, BORSUK_1, TSEP_1,
      TOPS_2, COMPTS_1, CONNSP_2, T_0TOPSP, TMAP_1, TOPREAL1, TOPREAL2,
      METRIC_1, TOPMETR, EUCLID, TOPRNS_1, PCOMPS_1, BORSUK_2, SIN_COS,
      JGRAPH_6, BORSUK_3, JORDAN16, TAXONOM2, BORSUK_4, TOPREAL9, TOPALG_2,
      SIN_COS6, PSCOMP_1, TOPREALA, XXREAL_0;
 constructors ARYTM_0, REAL_1, SQUARE_1, CARD_3, RCOMP_1, FCONT_1, COMSEQ_3,
      SIN_COS, RCOMP_2, TOPS_1, CONNSP_1, TOPS_2, COMPTS_1, REALSET2, GRCAT_1,
      TMAP_1, TOPRNS_1, TOPREAL1, TOPREAL2, MONOID_0, PSCOMP_1, TAXONOM2,
      JORDAN2C, BORSUK_3, SIN_COS6, BORSUK_4, JORDAN16, JGRAPH_6, TOPREAL9,
      TOPREALA, TOPALG_2, NAT_D;
 registrations STRUCT_0, EUCLID, RELSET_1, XREAL_0, REAL_1, BORSUK_1, PRE_TOPC,
      XCMPLX_0, FUNCT_1, TOPREAL9, NAT_1, INT_1, SIN_COS, SQUARE_1, NUMBERS,
      TEX_2, JORDAN16, TOPALG_2, TOPS_1, TOPREAL6, BORSUK_6, TOPALG_3,
      FINSEQ_1, TOPGRP_1, RCOMP_1, SIN_COS6, PSCOMP_1, BORSUK_3, TOPREAL2,
      TOPREALA, MONOID_0, SEQ_1, SUBSET_1, TOPREAL1, XXREAL_0, FUNCT_2;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM, REAL;
 definitions TARSKI, XBOOLE_0, FUNCT_1, FUNCT_2, PRE_TOPC, TOPS_2, T_0TOPSP,
      TMAP_1, STRUCT_0, TAXONOM2, TSEP_1, BORSUK_1, JORDAN16, TOPREALA,
      SQUARE_1, TOPREAL9, SUBSET_1, FINSEQ_2, GRCAT_1;
 theorems PRE_TOPC, XREAL_0, FUNCT_2, INT_1, SIN_COS, EUCLID, JGRAPH_1,
      SQUARE_1, TOPREAL9, TOPALG_1, GOBOARD6, JORDAN2C, TOPS_2, GRCAT_1,
      BORSUK_3, FUNCT_1, XCMPLX_1, REAL_2, TOPRNS_1, ABSVALUE, TOPMETR,
      JGRAPH_2, BORSUK_4, RELAT_1, FUNCOP_1, RCOMP_2, TOPREAL3, TOPREAL6,
      JORDAN16, TARSKI, PCOMPS_1, ZFMISC_1, TSEP_1, HILBERT3, TMAP_1, XBOOLE_1,
      SPPOL_2, COMPLEX2, JORDAN6, REAL_1, COMPTS_1, FCONT_1, JGRAPH_3,
      COMPTRIG, CONNSP_2, XBOOLE_0, RCOMP_1, REALSET2, TOPS_1, GOBOARD9,
      TOPALG_3, TOPGRP_1, T_0TOPSP, JGRAPH_6, SEQ_1, TOPREAL2, STRUCT_0,
      SIN_COS6, BORSUK_5, PSCOMP_1, RELSET_1, TOPREALA, XREAL_1, XXREAL_0;
 schemes FUNCT_2, INT_1;

begin :: Preliminaries

set P2 = 2*PI;
set o = |[0,0]|;
set R = the carrier of R^1;
Lm1: 0 in INT by INT_1:def 1;
reconsider p0 = -1 as negative (real number);
reconsider p1 = 1 as positive (real number);

set CIT = Closed-Interval-TSpace(-1,1);
set cCIT = the carrier of CIT;
Lm2: cCIT = [. -1,1 .] by TOPMETR:25;
Lm3: 1-0 <= 1;
Lm4: 3/2-1/2 <= 1;

reserve
  n for Element of NAT,
  i for Integer,
  a, b, r for real number,
  x for Point of TOP-REAL n;

registration
  cluster ].0,1.[ -> non empty;
coherence;
  cluster [.-1,1.] -> non empty;
coherence;
  cluster ].1/2,3/2.[ -> non empty;
coherence
proof
  ].1/2,1/2+p1.[ is non empty;
  hence thesis;
end;
end;

Lm5: PI/2 < PI/1 by REAL_2:200;
Lm6: 1*PI < 3/2*PI by XREAL_1:70;
Lm7: 3/2*PI < 2*PI by XREAL_1:70;
Lm8: dom sin = REAL by SIN_COS:def 20;

Lm9:
for X being non empty TopSpace, Y being non empty SubSpace of X,
    Z being non empty SubSpace of Y,
    p being Point of Z holds p is Point of X
proof
  let X be non empty TopSpace,
      Y be non empty SubSpace of X,
      Z be non empty SubSpace of Y,
      p be Point of Z;
  p is Point of Y by TOPMETR:2;
  hence p is Point of X by TOPMETR:2;
end;

Lm10:
for X being TopSpace, Y being SubSpace of X,
    Z being SubSpace of Y,
    A being Subset of Z holds A is Subset of X
proof
  let X be TopSpace,
      Y be SubSpace of X,
      Z be SubSpace of Y,
      A be Subset of Z;
A1: the carrier of Y is Subset of X by TSEP_1:1;
  the carrier of Z is Subset of Y by TSEP_1:1;
  then A is Subset of Y by XBOOLE_1:1;
  hence A is Subset of X by A1,XBOOLE_1:1;
end;

registration
  cluster sin -> continuous;
coherence
proof
  thus sin is_continuous_on dom sin by COMPTRIG:43,SIN_COS:def 20;
end;
  cluster cos -> continuous;
coherence
proof
  thus cos is_continuous_on dom cos by COMPTRIG:44,SIN_COS:def 22;
end;
  cluster arcsin -> continuous;
coherence
proof
  thus arcsin is_continuous_on dom arcsin by SIN_COS6:64,85;
end;
  cluster arccos -> continuous;
coherence
proof
  thus arccos is_continuous_on dom arccos by SIN_COS6:88,109;
end;
end;

theorem Th1:
  sin(a*r+b) = (sin*AffineMap(a,b)).r
proof
A1: r is Real by XREAL_0:def 1;
  thus sin(a*r+b) = sin.(a*r+b) by SIN_COS:def 21
    .= sin.(AffineMap(a,b).r) by JORDAN16:def 3
    .= (sin*AffineMap(a,b)).r by A1,FUNCT_2:21;
end;

theorem Th2:
  cos(a*r+b) = (cos*AffineMap(a,b)).r
proof
A1: r is Real by XREAL_0:def 1;
  thus cos(a*r+b) = cos.(a*r+b) by SIN_COS:def 23
    .= cos.(AffineMap(a,b).r) by JORDAN16:def 3
    .= (cos*AffineMap(a,b)).r by A1,FUNCT_2:21;
end;

registration
  let a be non zero (real number), b be real number;
  cluster AffineMap(a,b) -> onto one-to-one;
coherence
proof
  thus rng AffineMap(a,b) = REAL by JORDAN16:32;
  thus thesis by JORDAN16:27;
end;
end;

definition
  let a, b be real number;
  func IntIntervals(a,b) equals
  { ]. a+n, b+n .[ where n is Element of INT: not contradiction };
coherence;
end;

theorem
  for x being set holds x in IntIntervals(a,b) iff
  ex n being Element of INT st x = ]. a+n, b+n .[;

registration
  let a, b be real number;
  cluster IntIntervals(a,b) -> non empty;
coherence
proof
  ]. a+0, b+0 .[ in IntIntervals(a,b) by Lm1;
  hence thesis;
end;
end;

theorem
  b-a <= 1 implies IntIntervals(a,b) is mutually-disjoint
proof
  assume
A1: b-a <= 1;
  let x, y be set;
  assume x in IntIntervals(a,b);
  then consider nx being Element of INT such that
A2: x = ].a+nx,b+nx.[;
  assume y in IntIntervals(a,b);
  then consider ny being Element of INT such that
A3: y = ].a+ny,b+ny.[;
  assume
A4: x <> y;
  assume x meets y;
  then consider z being set such that
A5: z in x and
A6: z in y by XBOOLE_0:3;
  reconsider z as Real by A2,A5;
A7: a+nx < z & z < b+nx by A2,A5,RCOMP_1:47;
A8: a+ny < z & z < b+ny by A3,A6,RCOMP_1:47;
A9: now
    let k be Element of NAT;
A10: b-a+a <= 1+a by A1,XREAL_1:8;
    a+1+0 <= a+1+k by XREAL_1:8;
    hence a+(k+1) >= b by A10,XXREAL_0:2;
  end;
  per cases by REAL_1:def 5;
  suppose nx = ny;
  hence contradiction by A2,A3,A4;
  end;
  suppose nx < ny;
  then nx+1 <= ny by INT_1:20;
  then reconsider k = ny-(nx+1) as Element of NAT by INT_1:18;
  a+nx+1+k < b+nx by A7,A8,XXREAL_0:2;
  then a+nx+(k+1)-nx < b+nx-nx by REAL_1:92;
  then a+(k+1) < b;
  hence contradiction by A9;
  end;
  suppose nx > ny;
  then ny+1 <= nx by INT_1:20;
  then reconsider k = nx-(ny+1) as Element of NAT by INT_1:18;
  a+ny+1+k < b+ny by A7,A8,XXREAL_0:2;
  then a+ny+(k+1)-ny < b+ny-ny by REAL_1:92;
  then a+(k+1) < b;
  hence contradiction by A9;
  end;
end;

definition
  let a, b be real number;
  redefine func IntIntervals(a,b) -> Subset-Family of R^1;
coherence
proof
  IntIntervals(a,b) c= bool the carrier of R^1
  proof
    let x be set;
    assume x in IntIntervals(a,b);
    then ex n being Element of INT st x = ].a+n,b+n.[;
    hence thesis by TOPMETR:24;
  end;
  hence thesis;
end;
end;

definition
  let a, b be real number;
  redefine func IntIntervals(a,b) -> open Subset-Family of R^1;
coherence
proof
  IntIntervals(a,b) is open
  proof
    let A be Subset of R^1;
    assume A in IntIntervals(a,b);
    then ex n being Element of INT st A = ].a+n,b+n.[;
    hence thesis by JORDAN6:46;
  end;
  hence thesis;
end;
end;

begin :: Correspondence between REAL and R^1

definition
  let r be real number;
  func R^1(r) -> Point of R^1 equals
  r;
coherence by TOPMETR:24,XREAL_0:def 1;
end;

definition
  let A be Subset of REAL;
  func R^1(A) -> Subset of R^1 equals
  A;
coherence by TOPMETR:24;
end;

registration
  let A be non empty Subset of REAL;
  cluster R^1(A) -> non empty;
coherence;
end;

registration
  let A be open Subset of REAL;
  cluster R^1(A) -> open;
coherence by BORSUK_5:62;
end;

registration
  let A be closed Subset of REAL;
  cluster R^1(A) -> closed;
coherence by TOPREAL6:79;
end;

registration
  let A be open Subset of REAL;
  cluster R^1 | R^1(A) -> open;
coherence
proof
  let X be Subset of R^1 such that
A1: X = the carrier of R^1 | R^1(A);
  the carrier of R^1 | R^1(A) = A by PRE_TOPC:29;
  hence X is open by A1;
end;
end;

registration
  let A be closed Subset of REAL;
  cluster R^1 | R^1(A) -> closed;
coherence
proof
  let X be Subset of R^1 such that
A1: X = the carrier of R^1 | R^1(A);
  the carrier of R^1 | R^1(A) = A by PRE_TOPC:29;
  hence X is closed by A1;
end;
end;

definition
  let f be PartFunc of REAL,REAL;
  func R^1(f) -> Function of R^1 | R^1(dom f), R^1 | R^1(rng f) equals
  f;
coherence
proof
A1: the carrier of R^1 | R^1(dom f) = R^1(dom f) &
    the carrier of R^1 | R^1(rng f) = R^1(rng f) by PRE_TOPC:29;
  thus thesis by A1,FUNCT_2:4;
end;
end;

registration
  let f be PartFunc of REAL,REAL;
  cluster R^1(f) -> onto;
coherence
proof
  thus rng R^1(f) = the carrier of R^1|R^1(rng f) by PRE_TOPC:29;
end;
end;

registration
  let f be one-to-one PartFunc of REAL,REAL;
  cluster R^1(f) -> one-to-one;
coherence;
end;

theorem Th5:
  R^1 | (R^1 [#]REAL) = R^1
proof
  [#]R^1 = R^1([#]REAL) by TOPMETR:24;
  hence thesis by PRE_TOPC:def 10;
end;

theorem Th6:
  for f being PartFunc of REAL,REAL st dom f = REAL holds
  R^1|(R^1 dom f) = R^1
proof
  let f be PartFunc of REAL,REAL such that
A1:  dom f = REAL;
  [#]R^1 = R^1(dom f) by  TOPMETR:24,A1;
  hence R^1 | R^1(dom f) = R^1 by PRE_TOPC:def 10;
end;

theorem Th7:
  for f being Function of REAL,REAL holds f is Function
 of R^1, R^1 | R^1(rng f)
proof
  let f be Function of REAL,REAL;
  REAL = dom f by FUNCT_2:def 1;
  then R^1 | R^1(dom f) = R^1 by Th6;
  then R^1(f) is Function of R^1, R^1 | R^1(rng f);
  hence thesis;
end;

theorem Th8:
  for f being Function of REAL,REAL holds f is Function of R^1, R^1
proof
  let f be Function of REAL,REAL;
  dom f = REAL by FUNCT_2:def 1;
  then reconsider B = rng f as non empty Subset of REAL by RELAT_1:65;
  f is Function of R^1, R^1 | R^1(B) by Th7;
  hence thesis by TOPREALA:28;
end;

Lm11:
sin is Function of R^1,R^1
proof
A1: sin = R^1(sin);
  dom sin = REAL by SIN_COS:def 20;
  then R^1|(R^1 dom sin) = R^1 by Th6;
  then sin is Function of R^1,R^1|R^1 [.-1,1.] by A1,COMPTRIG:46;
  hence thesis by TOPREALA:28;
end;

Lm12:
cos is Function of R^1,R^1
proof
A1: cos = R^1(cos);
  dom cos = REAL by SIN_COS:def 22;
  then R^1|(R^1 dom cos) = R^1 by Th6;
  then cos is Function of R^1,R^1|R^1 [.-1,1.] by A1,COMPTRIG:47;
  hence thesis by TOPREALA:28;
end;

registration
  let f be continuous PartFunc of REAL,REAL;
  cluster R^1(f) -> continuous;
coherence
proof
  set g = R^1(f);
  per cases;
  suppose dom f is non empty;
  then reconsider A = dom f, B = rng f as non empty Subset of REAL
    by RELAT_1:65;
  reconsider g as Function of R^1|R^1(A),R^1|R^1(B);
  reconsider h = g as Function of R^1|R^1(A),R^1 by TOPREALA:28;
  for x being Point of R^1|R^1(A) holds h is_continuous_at x
  proof
    let x be Point of R^1|R^1(A);
    let G be a_neighborhood of h.x;
A1: the carrier of R^1|R^1(A) = A by PRE_TOPC:29;
    consider Z being Neighbourhood of f.x such that
A2:   Z c= G by TOPREALA:41;
A3: f is_continuous_on dom f by JORDAN16:def 1;
A4: f|dom f = f by RELAT_1:98;
    f|dom f is_continuous_in x by A1,A3,FCONT_1:def 2;
    then
    consider N being Neighbourhood of x such that
A5:   f.:N c= Z by A4,FCONT_1:5;
    consider g being real number such that
A6:   0 < g and
A7:   N = ].x-g,x+g.[ by RCOMP_1:def 7;
    reconsider NN = N as open Subset of R^1 by A7,JORDAN6:46,TOPMETR:24;
    reconsider xx = x as Point of R^1 by TOPMETR:2;
    reconsider M = NN /\ [#](R^1|R^1(A)) as Subset of R^1|R^1(A);
A8: NN = Int NN by TOPS_1:55;
    x-g < x-0 & x+0 < x+g by A6,XREAL_1:8,REAL_1:92;
    then xx in Int NN by A7,A8,RCOMP_1:47;
    then NN is open a_neighborhood of xx by CONNSP_2:def 1;
    then reconsider M as open a_neighborhood of x by TOPREALA:26;
    take M;
    M c= NN by XBOOLE_1:17;
    then h.:M c= h.:NN by RELAT_1:156;
    then h.:M c= Z by A5,XBOOLE_1:1;
    hence h.:M c= G by A2,XBOOLE_1:1;
  end;
  then h is continuous by TMAP_1:49;
  hence thesis by TOPMETR:8;
  end;
  suppose dom f is empty;
  then f is empty by RELAT_1:64;
  then R^1(f) is empty;
  hence thesis by TOPREALA:27;
  end;
end;
end;

set A = R^1(].0,1.[);

Lm13:
now
  let a be non zero (real number),
      b be real number;
A1: dom AffineMap(a,b) = REAL by FUNCT_2:def 1;
A2: rng AffineMap(a,b) = REAL by JORDAN16:32;
A3: [#]R^1 = REAL by TOPMETR:24;
  R^1| [#](R^1) = R^1 by TSEP_1:3;
  hence R^1 = R^1|R^1(dom AffineMap(a,b)) & R^1 = R^1|R^1(rng AffineMap(a,b))
    by A1,A2,A3;
end;

registration
  let a be non zero (real number),
      b be real number;
  cluster R^1(AffineMap(a,b)) -> open;
coherence
proof
  let A be Subset of R^1|R^1(dom AffineMap(a,b));
A1: a is Real & b is Real by XREAL_0:def 1;
A2: R^1 = R^1|R^1(dom AffineMap(a,b)) & R^1 = R^1|R^1(rng AffineMap(a,b))
    by Lm13;
  R^1(AffineMap(a,b)) is_homeomorphism by A1,A2,JORDAN16:35;
  hence thesis by A2,TOPGRP_1:25;
end;
end;

begin :: Circles

definition
  let S be SubSpace of TOP-REAL 2;
  attr S is being_simple_closed_curve means :Def5:
  the carrier of S is Simple_closed_curve;
end;

registration
  cluster being_simple_closed_curve ->
     non empty arcwise_connected compact SubSpace of TOP-REAL 2;
coherence
proof
  let S be SubSpace of TOP-REAL 2;
  assume
A1: the carrier of S is Simple_closed_curve;
  then reconsider A = the carrier of S as Simple_closed_curve;
  A is non empty;
  hence the carrier of S is non empty;
  thus S is arcwise_connected by A1,TOPALG_3:11;
  [#]S = A;
  then [#]S is compact by COMPTS_1:11;
  hence S is compact by COMPTS_1:10;
end;
end;

registration
  let r be positive (real number),
      x be Point of TOP-REAL 2;
  cluster Sphere(x,r) -> being_simple_closed_curve;
coherence
proof
  reconsider a = x as Point of Euclid 2 by TOPREAL3:13;
A1: x = |[x`1,x`2]| by EUCLID:57;
  Sphere(x,r) = Sphere(a,r) by TOPREAL9:15
   .= circle(x`1,x`2,r) by A1,TOPREAL9:49
   .= {w where w is Point of TOP-REAL 2: |.w- |[x`1,x`2]| .| = r}
      by JGRAPH_6:def 5;
  hence thesis by JGRAPH_6:32;
end;
end;

definition
  let n be Element of NAT, x be Point of TOP-REAL n, r be real number;
  func Tcircle(x,r) -> SubSpace of TOP-REAL n equals
  (TOP-REAL n) | Sphere(x,r);
coherence;
end;

registration
  let n be non empty Element of NAT, x be Point of TOP-REAL n,
      r be non negative (real number);
  cluster Tcircle(x,r) -> strict non empty;
coherence;
end;

theorem Th9:
  the carrier of Tcircle(x,r) = Sphere(x,r)
proof
  [#]Tcircle(x,r) = Sphere(x,r) by PRE_TOPC:def 10;
  hence the carrier of Tcircle(x,r) = Sphere(x,r);
end;

registration
  let n be Element of NAT, x be Point of TOP-REAL n,
      r be empty (real number);
  cluster Tcircle(x,r) -> trivial;
coherence
proof
  reconsider e = x as Point of Euclid n by TOPREAL3:13;
  the carrier of Tcircle(x,r) = Sphere(x,r) by Th9
  .= Sphere(e,r) by TOPREAL9:15
  .= {e} by TOPREAL6:62;
  hence thesis by REALSET2:def 5;
end;
end;

theorem Th10:
  Tcircle(0.REAL 2,r) is SubSpace of Trectangle(-r,r,-r,r)
proof
  set C = Tcircle(0.REAL 2,r);
  set T = Trectangle(-r,r,-r,r);
  the carrier of C c= the carrier of T
  proof
    let x be set;
    assume
A1:   x in the carrier of C;
    then C is non empty by STRUCT_0:def 1;
    then reconsider x as Point of TOP-REAL 2 by A1,TOPMETR:2;
A2: closed_inside_of_rectangle(-r,r,-r,r) =
      {p where p is Point of TOP-REAL 2:
        -r <= p`1 & p`1 <= r & -r <= p`2 & p`2 <= r} by JGRAPH_6:def 2;
A3: the carrier of Trectangle(-r,r,-r,r) =
      closed_inside_of_rectangle(-r,r,-r,r) by PRE_TOPC:29;
    the carrier of C = Sphere(0.REAL 2,r) by Th9;
    then
A4: |. x .| = r by A1,TOPREAL9:12;
    abs(x`1) <= |. x .| & abs(x`2) <= |. x .| by JGRAPH_1:50;
    then -r <= x`1 & x`1 <= r & -r <= x`2 & x`2 <= r by A4,ABSVALUE:12;
    hence thesis by A2,A3;
  end;
  hence thesis by TSEP_1:4;
end;

registration
  let x be Point of TOP-REAL 2,
      r be positive (real number);
  cluster Tcircle(x,r) -> being_simple_closed_curve;
coherence
proof
  the carrier of Tcircle(x,r) = Sphere(x,r) by Th9;
  hence the carrier of Tcircle(x,r) is Simple_closed_curve;
end;
end;

registration
  cluster being_simple_closed_curve strict SubSpace of TOP-REAL 2;
existence
proof
  consider x being Point of TOP-REAL 2, r be positive (real number);
  take Tcircle(x,r);
  thus thesis;
end;
end;

theorem
  for S, T being being_simple_closed_curve SubSpace of TOP-REAL 2 holds
  S,T are_homeomorphic
proof
  let S, T be being_simple_closed_curve SubSpace of TOP-REAL 2;
  the TopStruct of S, the TopStruct of T are_homeomorphic
  proof
    reconsider A = the carrier of the TopStruct of S
      as Simple_closed_curve by Def5;
    reconsider B = the carrier of the TopStruct of T
      as Simple_closed_curve by Def5;
    consider f being Function of (TOP-REAL 2)|R^2-unit_square, (TOP-REAL 2)|A
     such that
A1:   f is_homeomorphism by TOPREAL2:def 1;
    consider g being Function of (TOP-REAL 2)|R^2-unit_square, (TOP-REAL 2)|B
     such that
A2:   g is_homeomorphism by TOPREAL2:def 1;
A3: the TopStruct of S is strict SubSpace of TOP-REAL 2 by TMAP_1:11;
    [#]the TopStruct of S = A;
    then
A4: the TopStruct of S = (TOP-REAL 2)|A by A3,PRE_TOPC:def 10;
A5: the TopStruct of T is strict SubSpace of TOP-REAL 2 by TMAP_1:11;
    [#]the TopStruct of T = B;
    then
A6: the TopStruct of T = (TOP-REAL 2)|B by A5,PRE_TOPC:def 10;
    then reconsider h = g*f" as Function
      of the TopStruct of S, the TopStruct of T by A4;
    take h;
    f" is_homeomorphism by A1,TOPS_2:70;
    hence h is_homeomorphism by A2,A4,A6,TOPS_2:71;
  end;
  hence thesis by TOPREALA:36;
end;

:: Unit circle

definition
  let n be Element of NAT;
  func Tunit_circle(n) -> SubSpace of TOP-REAL n equals
  Tcircle(0.REAL n,1);
coherence;
end;

set TUC = Tunit_circle(2);
set cS1 = the carrier of TUC;
Lm14: cS1 = Sphere(|[0,0]|,1) by Th9,EUCLID:58;

registration
  let n be non empty Element of NAT;
  cluster Tunit_circle(n) -> non empty;
coherence;
end;

theorem Th12:
  for n being non empty Element of NAT, x being Point of TOP-REAL n holds
  x is Point of Tunit_circle(n) implies |. x .| = 1
proof
  let n be non empty Element of NAT,
      x be Point of TOP-REAL n;
  reconsider j = 1 as non negative (real number);
  assume x is Point of Tunit_circle(n);
  then x is Point of Tcircle(0.REAL n,j);
  then x in the carrier of Tcircle(0.REAL n,j);
  then x in Sphere(0.REAL n,1) by Th9;
  hence |. x .| = 1 by TOPREAL9:12;
end;

theorem Th13:
  for x being Point of TOP-REAL 2 st x is Point of Tunit_circle(2) holds
  -1 <= x`1 & x`1 <= 1 & -1 <= x`2 & x`2 <= 1
proof
  let x be Point of TOP-REAL 2 such that
A1: x is Point of Tunit_circle(2);
  consider a, b being Real such that
A2:  x = <*a,b*> by EUCLID:55;
  x = |[a,b]| by A2;
    then
A3: a = x`1 & b = x`2 by EUCLID:56;
A4: 1^2 = |. x .|^2 by A1,Th12
       .= a^2+b^2 by A3,JGRAPH_3:10;
  0 <= b^2 & 0 <= a^2 by XREAL_1:65;
  then -b^2 <= -0 & -a^2 <= -0 by XREAL_1:26;
  then a^2-1 <= 0 & b^2-1 <= 0 by A4;
  hence thesis by A3,SQUARE_1:112;
end;

theorem Th14:
  for x being Point of TOP-REAL 2 st x is Point of Tunit_circle(2) & x`1 = 1
  holds x`2 = 0
proof
  let x be Point of TOP-REAL 2 such that
A1: x is Point of Tunit_circle(2) and
A2: x`1 = 1;
  1^2 = |. x .|^2 by A1,Th12
     .= x`1^2+x`2^2 by JGRAPH_3:10;
  then 1-1 = 1+x`2^2-1 by A2;
  hence x`2 = 0 by XCMPLX_1:6;
end;

theorem Th15:
  for x being Point of TOP-REAL 2 st x is Point of Tunit_circle(2) & x`1 = -1
  holds x`2 = 0
proof
  let x be Point of TOP-REAL 2 such that
A1: x is Point of Tunit_circle(2) and
A2: x`1 = -1;
  1^2 = |. x .|^2 by A1,Th12
     .= x`1^2+x`2^2 by JGRAPH_3:10;
  then 1-1 = 1+x`2^2-1 by A2;
  hence x`2 = 0 by XCMPLX_1:6;
end;

theorem
  for x being Point of TOP-REAL 2 st x is Point of Tunit_circle(2) & x`2 = 1
  holds x`1 = 0
proof
  let x be Point of TOP-REAL 2 such that
A1: x is Point of Tunit_circle(2) and
A2: x`2 = 1;
  1^2 = |. x .|^2 by A1,Th12
     .= x`1^2+x`2^2 by JGRAPH_3:10;
  then 1-1 = 1+x`1^2-1 by A2;
  hence x`1 = 0 by XCMPLX_1:6;
end;

theorem
  for x being Point of TOP-REAL 2 st x is Point of Tunit_circle(2) & x`2 = -1
  holds x`1 = 0
proof
  let x be Point of TOP-REAL 2 such that
A1: x is Point of Tunit_circle(2) and
A2: x`2 = -1;
  1^2 = |. x .|^2 by A1,Th12
     .= x`1^2+x`2^2 by JGRAPH_3:10;
  then 1-1 = 1+x`1^2-1 by A2;
  hence x`1 = 0 by XCMPLX_1:6;
end;

set TREC = Trectangle(p0,p1,p0,p1);

theorem
  Tunit_circle(2) is SubSpace of Trectangle(-1,1,-1,1) by Th10;

theorem Th19:
for n being non empty Element of NAT,
    r being positive (real number),
    x being Point of TOP-REAL n,
    f being Function of Tunit_circle(n), Tcircle(x,r) st
  for a being Point of Tunit_circle(n),
      b being Point of TOP-REAL n st a = b holds f.a = r*b+x
  holds f is_homeomorphism
proof
  let n be non empty Element of NAT,
      r be positive (real number),
      x be Point of TOP-REAL n,
      f being Function of Tunit_circle(n), Tcircle(x,r) such that
A1: for a being Point of Tunit_circle(n),
        b being Point of TOP-REAL n st a = b holds f.a = r*b+x;
  set U = Tunit_circle(n), C = Tcircle(x,r);
  defpred P[Point of TOP-REAL n,set] means $2 = r*$1+x;
A2: 1/r is Real by XREAL_0:def 1;
A3: 1/r*r = 1 by XCMPLX_1:88;
A4: the carrier of U = Sphere(0.REAL n,1) by Th9;
A5: the carrier of C = Sphere(x,r) by Th9;
A6: dom f = the carrier of U by FUNCT_2:def 1;
  hence dom f = [#]U;
  thus
A7: rng f = [#]C
  proof
    thus rng f c= [#]C;
    let b be set;
    assume
A8:   b in [#]C;
    then reconsider c = b as Point of TOP-REAL n by TOPMETR:2;
    set a = 1/r * (c - x);
    |.a-0.REAL n.| = |.a.| by JORDAN2C:13
      .= abs(1/r)*|. c-x .| by A2,TOPRNS_1:8
      .= 1/r*|. c-x .| by ABSVALUE:def 1
      .= 1/r*r by A5,A8,TOPREAL9:9;
    then
A9: a in Sphere(0.REAL n,1) by A3;
    then f.a = r*a+x by A1,A4
      .= r*(1/r)*(c-x)+x by EUCLID:34
      .= c-x+x by A3,EUCLID:33
      .= b by EUCLID:52;
    hence b in rng f by A4,A6,A9,FUNCT_1:def 5;
  end;
  thus
A10: f is one-to-one
  proof
    let a, b be set such that
A11:   a in dom f and
A12:   b in dom f and
A13:   f.a = f.b;
    reconsider a1 = a, b1 = b as Point of TOP-REAL n by A4,A6,A11,A12;
A14: f.a1 = r*a1+x by A1,A11;
    f.b1 = r*b1+x by A1,A12;
    then r*a1 = r*b1+x-x by A13,A14,EUCLID:52;
    then r*a1 = r*b1 by EUCLID:52;
    hence a = b by EUCLID:38;
  end;
A15: for u being Point of TOP-REAL n ex y being Point of TOP-REAL n st P[u,y];
  consider F being Function of TOP-REAL n, TOP-REAL n such that
A16: for x being Point of TOP-REAL n holds P[x,F.x] from FUNCT_2:sch 3(A15);
  set f1 = id TOP-REAL n;
  set f2 = (TOP-REAL n) --> x;
  for p being Point of TOP-REAL n holds F.p = r * f1.p + 1 * f2.p
  proof
    let p be Point of TOP-REAL n;
    thus r * f1.p + 1 * f2.p = r*f1.p + f2.p by EUCLID:33
      .= r*p + f2.p by FUNCT_1:35
      .= r*p + x by FUNCOP_1:13
      .= F.p by A16;
  end;
  then
A17: F is continuous by TOPALG_1:17;
  dom F = the carrier of TOP-REAL n by FUNCT_2:def 1;
  then
A18: dom (F|Sphere(0.REAL n,1)) = Sphere(0.REAL n,1) by RELAT_1:91;
  for a being set st a in dom f holds f.a = (F|Sphere(0.REAL n,1)).a
  proof
    let a be set such that
A19:   a in dom f;
    reconsider y = a as Point of TOP-REAL n by A19,TOPMETR:2;
    thus f.a = r*y+x by A1,A19
      .= F.y by A16
      .= (F|Sphere(0.REAL n,1)).a by A4,A19,FUNCT_1:72;
  end;
  then f = F|Sphere(0.REAL n,1) by A4,A6,A18,FUNCT_1:9;
  hence f is continuous by A17,BORSUK_4:69;
  defpred R[Point of TOP-REAL n,set] means $2 = 1/r*($1-x);
A20: for u being Point of TOP-REAL n ex y being Point of TOP-REAL n st R[u,y];
  consider G being Function of TOP-REAL n, TOP-REAL n such that
A21: for a being Point of TOP-REAL n holds R[a,G.a] from FUNCT_2:sch 3(A20);
A22: dom (f") = the carrier of C by FUNCT_2:def 1;
  dom G = the carrier of TOP-REAL n by FUNCT_2:def 1;
  then
A23: dom (G|Sphere(x,r)) = Sphere(x,r) by RELAT_1:91;
  for p being Point of TOP-REAL n holds G.p = 1/r * f1.p + (-1/r) * f2.p
  proof
    let p be Point of TOP-REAL n;
    thus 1/r * f1.p + (-1/r) * f2.p = 1/r*p + (-1/r)*f2.p by FUNCT_1:35
      .= 1/r*p + (-1/r)*x by FUNCOP_1:13
      .= 1/r*p + -1/r*x by EUCLID:44
      .= 1/r*p - 1/r*x by EUCLID:45
      .= 1/r*(p-x) by EUCLID:53
      .= G.p by A21;
  end;
  then
A24: G is continuous by TOPALG_1:17;
  for a being set st a in dom (f") holds f".a = (G|Sphere(x,r)).a
  proof
    let a be set such that
A25:   a in dom (f");
    reconsider y = a as Point of TOP-REAL n by A25,TOPMETR:2;
    reconsider ff = f as Function;
    set e = 1/r * (y - x);
    |.e-0.REAL n.| = |.e.| by JORDAN2C:13
      .= abs(1/r)*|. y-x .| by A2,TOPRNS_1:8
      .= 1/r*|. y-x .| by ABSVALUE:def 1
      .= 1/r*r by A5,A25,TOPREAL9:9;
    then
A26: 1/r * (y - x) in Sphere(0.REAL n,1) by A3;
    then f.e = r*e+x by A1,A4
      .= r*(1/r)*(y-x)+x by EUCLID:34
      .= y-x+x by A3,EUCLID:33
      .= y by EUCLID:52;
    then ff".a = 1/r * (y - x) by A4,A6,A10,A26,FUNCT_1:54;
    hence f".a = 1/r*(y-x) by A7,A10,TOPS_2:def 4
      .= G.y by A21
      .= (G|Sphere(x,r)).a by A5,A25,FUNCT_1:72;
  end;
  then f" = G|Sphere(x,r) by A5,A22,A23,FUNCT_1:9;
  hence f" is continuous by A24,BORSUK_4:69;
end;

registration
  cluster Tunit_circle(2) -> being_simple_closed_curve;
coherence;
end;

Lm15:
for n being non empty Element of NAT,
    r being positive (real number),
    x being Point of TOP-REAL n holds
Tunit_circle(n), Tcircle(x,r) are_homeomorphic
proof
  let n be non empty Element of NAT,
      r be positive (real number),
      x be Point of TOP-REAL n;
  set U = Tunit_circle(n), C = Tcircle(x,r);
  defpred P[Point of U,set] means ex w being Point of TOP-REAL n
    st w = $1 & $2 = r*w+x;
A1: r is Real by XREAL_0:def 1;
A2: the carrier of C = Sphere(x,r) by Th9;
A3: for u being Point of U ex y being Point of C st P[u,y]
proof
  let u be Point of U;
  reconsider v = u as Point of TOP-REAL n by TOPMETR:2;
  set y = r*v+x;
  |. y-x .| = |. r*v .| by EUCLID:52
   .= abs(r)*|. v .| by A1,TOPRNS_1:8
   .= r*|. v .| by ABSVALUE:def 1
   .= r*1 by Th12;
  then reconsider y as Point of C by A2,TOPREAL9:9;
  take y;
  thus P[u,y];
end;
  consider f being Function of U,C such that
A4: for x being Point of U holds P[x,f.x] from FUNCT_2:sch 3(A3);
  take f;
  for a being Point of U, b being Point of TOP-REAL n st a = b holds
  f.a = r*b+x
  proof
    let a be Point of U,
        b be Point of TOP-REAL n;
    P[a,f.a] by A4;
    hence thesis;
  end;
  hence f is_homeomorphism by Th19;
end;

theorem
  for n being non empty Element of NAT,
      r, s being positive (real number),
      x, y being Point of TOP-REAL n holds
  Tcircle(x,r), Tcircle(y,s) are_homeomorphic
proof
  let n be non empty Element of NAT,
      r, s be positive (real number),
      x, y be Point of TOP-REAL n;
  Tcircle(x,r), Tunit_circle(n) are_homeomorphic &
  Tunit_circle(n), Tcircle(y,s) are_homeomorphic by Lm15;
  hence Tcircle(x,r), Tcircle(y,s) are_homeomorphic by BORSUK_3:3;
end;

registration
  let x be Point of TOP-REAL 2,
      r be non negative (real number);
  cluster Tcircle(x,r) -> arcwise_connected;
coherence
proof
  per cases;
  suppose r is positive;
  then reconsider r as positive (real number);
  Tcircle(x,r) is arcwise_connected;
  hence thesis;
  end;
  suppose r is non positive;
  then reconsider r as non negative non positive (real number);
  Tcircle(x,r) is trivial;
  hence thesis;
  end;
end;
end;

:: Open unit circle

definition
  func c[10] -> Point of Tunit_circle(2) equals
  |[1,0]|;
coherence
proof
A1: |[1,0]|`1 = 1 & |[1,0]|`2 = 0 by EUCLID:56;
  |. |[1+0,0+0]| - o .| = |.|[1,0]|+o- o.| by EUCLID:60
   .= |.|[1,0]|+(o-o).| by EUCLID:49
   .= |.|[1,0]|+(0.REAL 2).| by EUCLID:46
   .= |.|[1,0]|.| by EUCLID:31
   .= sqrt(1^2+0^2) by A1,JGRAPH_1:47
   .= 1 by SQUARE_1:89;
  hence thesis by Lm14,TOPREAL9:9;
end;
  func c[-10] -> Point of Tunit_circle(2) equals
  |[-1,0]|;
coherence
proof
A2: |[-1,0]|`1 = -1 & |[-1,0]|`2 = 0 by EUCLID:56;
  |. |[-1+0,0+0]| - o .| = |.|[-1,0]|+o- o.| by EUCLID:60
   .= |.|[-1,0]|+(o-o).| by EUCLID:49
   .= |.|[-1,0]|+(0.REAL 2).| by EUCLID:46
   .= |.|[-1,0]|.| by EUCLID:31
   .= sqrt((-1)^2+0^2) by A2,JGRAPH_1:47
   .= sqrt(1^2+0^2)
   .= 1 by SQUARE_1:89;
  hence thesis by Lm14,TOPREAL9:9;
end;
end;

 Lm16:
  c[10] <> c[-10] by SPPOL_2:1;

definition
  let p be Point of Tunit_circle(2);
  func Topen_unit_circle(p) -> strict SubSpace of Tunit_circle(2) means :Def10:
  the carrier of it = (the carrier of Tunit_circle(2)) \ {p};
existence
proof
  reconsider A = cS1 \ {p} as Subset of TUC;
  take TUC|A;
  thus thesis by PRE_TOPC:29;
end;
uniqueness by TSEP_1:5;
end;

registration
  let p be Point of Tunit_circle(2);
  cluster Topen_unit_circle(p) -> non empty;
coherence
proof
  set X = Topen_unit_circle(p);
A1: the carrier of X = cS1 \ {p} by Def10;
  per cases;
  suppose
A2: p = c[10];
  reconsider r = p as Point of TOP-REAL 2 by TOPMETR:2;
  set x = |[0,1]|;
A3: x`1 = 0 & x`2 = 1 by EUCLID:56;
  |. |[0+0,1+0]| - o .| = |.x+o- o.| by EUCLID:60
   .= |.x+(o-o).| by EUCLID:49
   .= |.x+(0.REAL 2).| by EUCLID:46
   .= |. x .| by EUCLID:31
   .= sqrt(1^2+0^2) by A3,JGRAPH_1:47
   .= 1 by SQUARE_1:89;
  then
A4: x in cS1 by Lm14;
  r`1 = 1 by A2,EUCLID:56;
  then not x in {p} by A3,TARSKI:def 1;
  hence the carrier of X is non empty by A1,A4,XBOOLE_0:def 4;
  end;
  suppose p <> c[10];
  then not c[10] in {p} by TARSKI:def 1;
  hence the carrier of X is non empty by A1,XBOOLE_0:def 4;
  end;
end;
end;

canceled;

theorem Th22:
  for p being Point of Tunit_circle(2) holds
    p is not Point of Topen_unit_circle(p)
proof
  let p be Point of Tunit_circle(2);
A1: the carrier of Topen_unit_circle(p) =
    (the carrier of Tunit_circle(2)) \ {p} by Def10;
  p in {p} by TARSKI:def 1;
  hence thesis by A1,XBOOLE_0:def 4;
end;

theorem Th23:
  for p being Point of Tunit_circle(2) holds
  Topen_unit_circle(p) = (Tunit_circle(2)) | (([#]Tunit_circle(2)) \ {p})
proof
  let p be Point of Tunit_circle(2);
  [#]Topen_unit_circle(p) = ([#]Tunit_circle(2)) \ {p} by Def10;
  hence Topen_unit_circle(p) = (Tunit_circle(2)) | (([#]Tunit_circle(2)) \ {p})
    by PRE_TOPC:def 10;
end;

theorem Th24:
  for p, q being Point of Tunit_circle(2) st p <> q holds
  q is Point of Topen_unit_circle(p)
proof
  let p, q be Point of Tunit_circle(2) such that
A1: p <> q;
  the carrier of Topen_unit_circle(p) = (the carrier of Tunit_circle(2)) \ {p}
    by Def10;
  hence q is Point of Topen_unit_circle(p) by A1,ZFMISC_1:64;
end;

theorem Th25:
  for p being Point of TOP-REAL 2 st
    p is Point of Topen_unit_circle(c[10]) & p`2 = 0 holds
  p = c[-10]
proof
  let p be Point of TOP-REAL 2 such that
A1: p is Point of Topen_unit_circle(c[10]) and
A2: p`2 = 0;
  p is Point of TUC by A1,TOPMETR:2;
  then 1^2 = |. p .|^2 by Th12
     .= p`1^2+p`2^2 by JGRAPH_3:10;
  then
A3: p`1 = 1 or p`1 = -1 by A2,SQUARE_1:110;
  now
    assume p`1 = 1;
    then p = c[10] by A2,EUCLID:57;
    hence contradiction by A1,Th22;
  end;
  hence thesis by A2,A3,EUCLID:57;
end;

theorem Th26:
  for p being Point of TOP-REAL 2 st
    p is Point of Topen_unit_circle(c[-10]) & p`2 = 0 holds
  p = c[10]
proof
  let p be Point of TOP-REAL 2 such that
A1: p is Point of Topen_unit_circle(c[-10]) and
A2: p`2 = 0;
  p is Point of TUC by A1,TOPMETR:2;
  then 1^2 = |. p .|^2 by Th12
     .= p`1^2+p`2^2 by JGRAPH_3:10;
  then
A3: p`1 = 1 or p`1 = -1 by A2,SQUARE_1:110;
  now
    assume p`1 = -1;
    then p = c[-10] by A2,EUCLID:57;
    hence contradiction by A1,Th22;
  end;
  hence thesis by A2,A3,EUCLID:57;
end;

set TOUC = Topen_unit_circle(c[10]);
set TOUCm = Topen_unit_circle(c[-10]);
set X = the carrier of TOUC;
set Xm = the carrier of TOUCm;
set Y = the carrier of (R^1|R^1(].0,0+p1.[));
set Ym = the carrier of (R^1|R^1(].1/2,1/2+p1.[));
Lm17: X = [#]TOUC;
Lm18: Xm = [#]TOUCm;

theorem Th27:
  for p being Point of Tunit_circle(2),
      x being Point of TOP-REAL 2 st x is Point of Topen_unit_circle(p) holds
  -1 <= x`1 & x`1 <= 1 & -1 <= x`2 & x`2 <= 1
proof
  let p be Point of Tunit_circle(2),
      x be Point of TOP-REAL 2;
  assume x is Point of Topen_unit_circle(p);
  then
A1: x in the carrier of Topen_unit_circle(p);
  the carrier of Topen_unit_circle(p) is Subset of cS1 by TSEP_1:1;
  hence thesis by A1,Th13;
end;

theorem Th28:
  for x being Point of TOP-REAL 2 st x is Point of Topen_unit_circle(c[10])
  holds -1 <= x`1 & x`1 < 1
proof
  let x be Point of TOP-REAL 2;
  assume
A1: x is Point of Topen_unit_circle(c[10]);
  then
A2: x`1 <= 1 by Th27;
  now
    assume
A3:  x`1 = 1;
A4: the carrier of TOUC = cS1 \ {c[10]} by Def10;
  then
A5: x in cS1 by A1,XBOOLE_0:def 4;
A6: not x in {c[10]} by A1,A4,XBOOLE_0:def 4;
    x = |[x`1,x`2]| by EUCLID:57;
    then x = c[10] by A3,A5,Th14;
    hence contradiction by A6,TARSKI:def 1;
  end;
  hence thesis by A1,A2,Th27,REAL_1:def 5;
end;

theorem Th29:
  for x being Point of TOP-REAL 2 st x is Point of Topen_unit_circle(c[-10])
  holds -1 < x`1 & x`1 <= 1
proof
  let x be Point of TOP-REAL 2;
  assume
A1: x is Point of Topen_unit_circle(c[-10]);
  then
A2: -1 <= x`1 by Th27;
  now
    assume
A3:  x`1 = -1;
A4: the carrier of TOUCm = cS1 \ {c[-10]} by Def10;
  then
A5: x in cS1 by A1,XBOOLE_0:def 4;
A6: not x in {c[-10]} by A1,A4,XBOOLE_0:def 4;
    x = |[x`1,x`2]| by EUCLID:57;
    then x = c[-10] by A3,A5,Th15;
    hence contradiction by A6,TARSKI:def 1;
  end;
  hence thesis by A1,A2,Th27,REAL_1:def 5;
end;

registration
  let p be Point of Tunit_circle(2);
  cluster Topen_unit_circle(p) -> open;
coherence
proof
  let A be Subset of TUC such that
A1: A = the carrier of Topen_unit_circle(p);
A2: TUC is being_T2 by TOPMETR:3;
  A` = cS1 \ (cS1 \ {p}) by A1,Def10
    .= cS1 /\ {p} by XBOOLE_1:48
    .= {p} by ZFMISC_1:52;
  then A` is closed by A2,PCOMPS_1:10;
  hence A is open by TOPS_1:30;
end;
end;

theorem
  for p being Point of Tunit_circle(2) holds
  Topen_unit_circle(p), I(01) are_homeomorphic
proof
  let p be Point of Tunit_circle(2);
  set P = Topen_unit_circle(p);
  set D = Sphere(0.REAL 2,p1);
  reconsider p2 = p as Point of TOP-REAL 2 by TOPMETR:2;
A1: [#]TUC = D by Lm14,EUCLID:58;
 D\{p} c= D by XBOOLE_1:36;
  then reconsider A = D\{p} as Subset of Tcircle(0.REAL 2,1)
    by Th9;
  reconsider A1 = A as Subset of (TOP-REAL 2) | D;
  P = Tcircle(0.REAL 2,1) | A by  Th23,A1
   .= (TOP-REAL 2) | (D \ {p2}) by GOBOARD9:4;
  hence P,I(01) are_homeomorphic by Lm14,BORSUK_4:77,EUCLID:58;
end;

theorem
  for p, q being Point of Tunit_circle(2) holds
  Topen_unit_circle(p), Topen_unit_circle(q) are_homeomorphic
proof
  let p, q be Point of Tunit_circle(2);
  set P = Topen_unit_circle(p);
  set Q = Topen_unit_circle(q);
  set D = Sphere(0.REAL 2,p1);
  reconsider p2 = p, q2 = q as Point of TOP-REAL 2 by TOPMETR:2;
A1: [#]TUC = D by Lm14,EUCLID:58;
 D\{p} c= D & D\{q} c= D by XBOOLE_1:36;
  then reconsider A = D\{p}, B = D\{q} as Subset of Tcircle(0.REAL 2,1)
    by Th9;
  reconsider A1 = A, B1 = B as Subset of (TOP-REAL 2) | D;
A2: P = Tcircle(0.REAL 2,1) | A by  Th23,A1
     .= (TOP-REAL 2) | (D \ {p2}) by GOBOARD9:4;
  Q = Tcircle(0.REAL 2,1) | B by  Th23,A1
     .= (TOP-REAL 2) | (D \ {q2}) by GOBOARD9:4;
  hence P,Q are_homeomorphic by A2,Lm14,BORSUK_4:78,EUCLID:58;
end;

begin :: Correspondence between the real line and circles

definition
  func CircleMap -> Function of R^1, Tunit_circle(2) means :Def11:
  for x being real number holds it.x = |[ cos(2*PI*x), sin(2*PI*x) ]|;
existence
proof
  defpred P[real number,set] means $2 = |[ cos(2*PI*$1), sin(2*PI*$1) ]|;
A1: for x being Element of R^1
   ex y being Element of cS1 st P[x,y]
  proof
    let x be Element of R^1;
    set y = |[ cos(2*PI*x), sin(2*PI*x) ]|;
    |. y - o .| = |. y .| by EUCLID:58,JORDAN2C:13
      .= sqrt((y`1)^2+(y`2)^2) by JGRAPH_1:47
      .= sqrt((cos(2*PI*x))^2+(y`2)^2) by EUCLID:56
      .= sqrt((cos(2*PI*x))^2+(sin(2*PI*x))^2) by EUCLID:56
      .= 1 by SIN_COS:32,SQUARE_1:83;
    then y is Element of cS1 by Lm14,TOPREAL9:9;
    hence thesis;
  end;
  consider f being Function of R, cS1 such that
A2: for x being Element of R^1 holds P[x,f.x] from FUNCT_2:sch 3(A1);
  take f;
  let x be real number;
  x is Point of R^1 by TOPMETR:24,XREAL_0:def 1;
  hence thesis by A2;
end;
uniqueness
proof
  let f, g be Function of R^1, TUC such that
A3: for x being real number holds f.x = |[cos(2*PI*x),sin(2*PI*x)]|
  and
A4: for x being real number holds g.x = |[cos(2*PI*x),sin(2*PI*x)]|;
  for x being Point of R^1 holds f.x = g.x
  proof
    let x be Point of R^1;
    thus f.x = |[cos(2*PI*x),sin(2*PI*x)]| by A3
      .= g.x by A4;
  end;
  hence f = g by FUNCT_2:113;
end;
end;

Lm19: dom CircleMap = REAL by FUNCT_2:def 1,TOPMETR:24;

theorem Th32:
  CircleMap.r = CircleMap.(r+i)
proof
  defpred P[Integer] means CircleMap.r = CircleMap.(r+$1);
A1: P[0];
A2: for i holds P[i] implies P[i-1] & P[i+1]
  proof
    let i such that
A3:   P[i];
    thus CircleMap.(r+(i-1)) = |[ cos(2*PI*(r+i-1)), sin(2*PI*(r+i-1)) ]|
    by Def11
    .= |[ cos(2*PI*(r+i)), sin(2*PI*(r+i)+2*PI*(-1)) ]| by COMPLEX2:10
    .= |[ cos(2*PI*(r+i)), sin(2*PI*(r+i)) ]| by COMPLEX2:9
    .= CircleMap.r by A3,Def11;
    thus CircleMap.(r+(i+1)) = |[ cos(2*PI*(r+i+1)), sin(2*PI*(r+i+1)) ]|
    by Def11
    .= |[ cos(2*PI*(r+i)), sin(2*PI*(r+i)+2*PI*1) ]| by COMPLEX2:10
    .= |[ cos(2*PI*(r+i)), sin(2*PI*(r+i)) ]| by COMPLEX2:9
    .= CircleMap.r by A3,Def11;
  end;
  for i holds P[i] from INT_1:sch 4(A1,A2);
  hence thesis;
end;

theorem Th33:
  CircleMap.i = c[10]
proof
  thus CircleMap.i = |[ cos(2*PI*i+0), sin(2*PI*i) ]| by Def11
    .= |[ cos(0), sin(2*PI*i+0) ]| by COMPLEX2:10
    .= c[10] by COMPLEX2:9,SIN_COS:34;
end;

theorem Th34:
  CircleMap"{c[10]} = INT
proof
  hereby
    let i be set;
    assume
A1:   i in CircleMap"{c[10]};
    then CircleMap.i in {c[10]} by FUNCT_2:46;
    then
A2: CircleMap.i = c[10] by TARSKI:def 1;
    reconsider x = i as Real by A1,TOPMETR:24;
    |[ cos(2*PI*x), sin(2*PI*x) ]| = |[1,0]| by A2,Def11;
    then cos(2*PI*x) = 1 by SPPOL_2:1;
    hence i in INT by SIN_COS6:44;
  end;
  let i be set;
  assume i in INT;
  then reconsider i as Integer by INT_1:def 2;
A3: i in R by TOPMETR:24,XREAL_0:def 1;
  CircleMap.i = c[10] by Th33;
  then CircleMap.i in {c[10]} by TARSKI:def 1;
  hence thesis by A3,FUNCT_2:46;
end;

Lm20:
CircleMap.(1/2) = |[-1,0]|
proof
  thus CircleMap.(1/2) = |[ cos(2*PI*(1/2)), sin(2*PI*(1/2)) ]| by Def11
  .= |[-1,0]| by SIN_COS:82;
end;

Lm21:
CircleMap.1 = |[1,0]| by Th33;

theorem Th35:
  frac r = 1/2 implies CircleMap.r = |[-1,0]|
proof
  assume
A1: frac r = 1/2;
  thus CircleMap.r = CircleMap.(r+-[\r/]) by Th32
    .= CircleMap.(r-[\r/])
    .= |[-1,0]| by A1,Lm20,INT_1:def 6;
end;

theorem
  frac r = 1/4 implies CircleMap.r = |[0,1]|
proof
  assume frac r = 1/4;
  then
A1: r-[\r/] = 1/4 by INT_1:def 6;
  thus CircleMap.r = CircleMap.(r+-[\r/]) by Th32
    .= |[ cos(2*PI*(1/4)), sin(2*PI*(1/4)) ]| by A1,Def11
    .= |[0,1]| by SIN_COS:82;
end;

theorem
  frac r = 3/4 implies CircleMap.r = |[0,-1]|
proof
  assume frac r = 3/4;
  then
A1: r-[\r/] = 3/4 by INT_1:def 6;
  thus CircleMap.r = CircleMap.(r+-[\r/]) by Th32
    .= |[ cos(2*PI*(3/4)), sin(2*PI*(3/4)) ]| by A1,Def11
    .= |[0,-1]| by SIN_COS:82;
end;

Lm22:
  CircleMap.r = |[ (cos*AffineMap(2*PI,0)).r, (sin*AffineMap(2*PI,0)).r ]|
proof
  thus CircleMap.r = |[ cos(2*PI*r+0), sin(2*PI*r) ]| by Def11
    .= |[ (cos*AffineMap(2*PI,0)).r, sin(2*PI*r+0) ]| by Th2
    .= |[ (cos*AffineMap(2*PI,0)).r, (sin*AffineMap(2*PI,0)).r ]| by Th1;
end;

theorem
  for i, j being Integer holds
  CircleMap.r =
    |[ (cos*AffineMap(2*PI,2*PI*i)).r, (sin*AffineMap(2*PI,2*PI*j)).r ]|
proof
  let i, j be Integer;
  thus CircleMap.r = |[ cos(2*PI*r+0), sin(2*PI*r) ]| by Def11
    .= |[ cos(2*PI*r+2*PI*i), sin(2*PI*r+0) ]| by COMPLEX2:10
    .= |[ cos(2*PI*r+2*PI*i), sin(2*PI*r+2*PI*j) ]| by COMPLEX2:9
    .= |[ (cos*AffineMap(2*PI,2*PI*i)).r, sin(2*PI*r+2*PI*j) ]| by Th2
    .= |[ (cos*AffineMap(2*PI,2*PI*i)).r, (sin*AffineMap(2*PI,2*PI*j)).r ]|
       by Th1;
end;

registration
  cluster CircleMap -> continuous;
coherence
proof
A1: AffineMap(2*PI,0) = R^1(AffineMap(2*PI,0));
  dom sin = REAL by SIN_COS:def 20;
  then
A2: R^1|(R^1 dom sin) = R^1 by Th6;
  dom cos = REAL by SIN_COS:def 22;
  then
A3: R^1|(R^1 dom cos) = R^1 by Th6;
  set sR = R^1(sin), cR = R^1(cos);
  reconsider sR, cR as Function of R^1,R^1 by Lm11,Lm12;
  reconsider l = AffineMap(2*PI,0) as Function of R^1,R^1 by Th8;
  dom AffineMap(2*PI,0) = REAL by FUNCT_2:def 1;
  then
A4: R^1|(R^1 dom AffineMap(2*PI,0)) = R^1 by Th6;
A5: rng AffineMap(2*PI,0) = [#]REAL by JORDAN16:32;
  set s = sR*l;
  set c = cR*l;
  reconsider g = CircleMap as Function of R^1,TOP-REAL 2 by TOPREALA:28;
  for p being Point of R^1, V being Subset of TOP-REAL 2
   st g.p in V & V is open
  ex W being Subset of R^1 st p in W & W is open & g.:W c= V
  proof
    let p be Point of R^1,
        V be Subset of TOP-REAL 2 such that
A6:   g.p in V and
A7:   V is open;
A8: V = Int V by A7,TOPS_1:55;
    reconsider e = g.p as Point of Euclid 2 by TOPREAL3:13;
    consider r being real number such that
A9:   r > 0 and
A10:   Ball(e,r) c= V by A6,A8,GOBOARD6:8;
    set A = ].(g.p)`1-r/sqrt 2,(g.p)`1+r/sqrt 2.[;
    set B = ].(g.p)`2-r/sqrt 2,(g.p)`2+r/sqrt 2.[;
    set F = (1,2) --> (A,B);
    sqrt 2 > 0 by SQUARE_1:84;
    then
A11: r/sqrt 2 > 0 by A9,REAL_2:127;
A12: product F c= Ball(e,r) by TOPREAL6:49;
    reconsider A, B as Subset of R^1 by TOPMETR:24;
A13: B is open by JORDAN6:46;
A14: g.p = |[ c.p, s.p ]| by Lm22;
    then
A15: (g.p)`2 = s.p by EUCLID:56;
    sR is continuous by A2,TOPMETR:7;
    then
A16: s is continuous by A1,A4,A5,Th5,TOPS_2:58;
    s.p in B by A11,A15,TOPREAL6:20;
    then consider Ws being Subset of R^1 such that
A17:   p in Ws and
A18:   Ws is open and
A19:   s.:Ws c= B by A13,A16,JGRAPH_2:20;
A20: A is open by JORDAN6:46;
A21: (g.p)`1 = c.p by A14,EUCLID:56;
    cR is continuous by A3,TOPMETR:7;
    then
A22: c is continuous by A1,A4,A5,Th5,TOPS_2:58;
    c.p in A by A11,A21,TOPREAL6:20;
    then consider Wc being Subset of R^1 such that
A23:  p in Wc and
A24:  Wc is open and
A25:  c.:Wc c= A by A20,A22,JGRAPH_2:20;
    set W = Ws /\ Wc;
    take W;
    thus p in W by A17,A23,XBOOLE_0:def 3;
    thus W is open by A18,A24,TOPS_1:38;
    let y be set;
    assume y in g.:W;
    then consider x being Element of R^1 such that
A26:  x in W and
A27:  y = g.x by FUNCT_2:116;
A28: g.x = |[ c.x, s.x ]| by Lm22;
    x in Ws by A26,XBOOLE_0:def 3;
    then
A29: s.x in s.:Ws by FUNCT_2:43;
    x in Wc by A26,XBOOLE_0:def 3;
    then
A30: c.x in c.:Wc by FUNCT_2:43;
    |[c.x,s.x]| = (1,2) --> (c.x,s.x) by TOPREALA:49;
    then |[c.x,s.x]| in product F by A19,A25,A29,A30,HILBERT3:11;
    then |[c.x,s.x]| in Ball(e,r) by A12;
    hence y in V by A10,A27,A28;
  end;
  then g is continuous by JGRAPH_2:20;
  hence thesis by TOPMETR:8;
end;
end;

Lm23:
  for A being Subset of R^1 holds CircleMap|A is Function
 of R^1|A, Tunit_circle(2)
proof
  let A be Subset of R^1;
A1: dom (CircleMap|A) = A by Lm19,RELAT_1:91,TOPMETR:24
  .= the carrier of R^1|A by PRE_TOPC:29;
  rng (CircleMap|A) c= cS1;
  hence CircleMap|A is Function of R^1|A, TUC by A1,FUNCT_2:4;
end;

Lm24:
-1 <= r & r <= 1 implies 0 <= arccos r/(2*PI) & arccos r/(2*PI) <= 1/2
proof
  assume -1 <= r & r <= 1;
  then 0 <= arccos r & arccos r <= PI by SIN_COS6:101;
  then 0/(2*PI) <= arccos r/(2*PI) &
    arccos r/(2*PI) <= 1*PI/(2*PI) by REAL_1:73;
  hence thesis by XCMPLX_1:92;
end;

theorem Th39:
  for A being Subset of R^1, f being Function of R^1|A, Tunit_circle(2)
   st [.0,1.[ c= A & f = CircleMap | A
  holds f is onto
proof
  let A be Subset of R^1,
      f be Function of R^1|A, Tunit_circle(2) such that
A1: [.0,1.[ c= A and
A2: f = CircleMap | A;
  thus rng f c= cS1;
  let y be set;
  assume
A3: y in cS1;
  then reconsider z = y as Point of TOP-REAL 2 by TOPMETR:2;
  set z1 = z`1, z2 = z`2;
  set x = arccos z1/(2*PI);
A4: dom f = A by A2,Lm19,RELAT_1:91,TOPMETR:24;
A5: |. z .| = 1 by A3,Th12;
A6: z1^2 + z2^2 = |. z .|^2 by JGRAPH_1:46;
A7: -1 <= z1 & z1 <= 1 by A3,Th13;
A8: 0 <= x by A7,Lm24;
   x <= 1/2 by A7,Lm24;
   then
A9: x < 1 by XXREAL_0:2;
  per cases;
  suppose
A10: z2 < 0;
A11: now
    assume x = 0;
    then arccos z1 = 0 by XCMPLX_1:50;
    then z1 = 1 by A7,SIN_COS6:98;
    then 1 + z2^2 - 1 = 0 by A5,A6;
    hence contradiction by A10,XCMPLX_1:6;
  end;
  1-0 > 1-x & 1-x > 1-1 by A8,A9,A11,REAL_1:92;
  then
A12: 1-x in [.0,1.[ by RCOMP_2:3;
  then f.(1-x) = CircleMap.(-x+1) by A1,A2,FUNCT_1:72
    .= CircleMap.-x by Th32
    .= |[cos(-2*PI*x), sin(2*PI*(-x))]| by Def11
    .= |[cos(2*PI*x), sin(2*PI*(-x))]| by SIN_COS:34
    .= |[cos arccos z1, sin(-2*PI*x)]| by XCMPLX_1:88
    .= |[cos arccos z1, -sin(2*PI*x)]| by SIN_COS:34
    .= |[cos arccos z1, -sin arccos z1]| by XCMPLX_1:88
    .= |[z1, -sin arccos z1]| by A7,SIN_COS6:93
    .= |[z1, --z2]| by A5,A6,A10,SIN_COS6:105
    .= y by EUCLID:57;
  hence thesis by A1,A4,A12,FUNCT_1:def 5;
  end;
  suppose
A13: z2 >= 0;
A14: x in [.0,1.[ by A8,A9,RCOMP_2:3;
  then f.x = CircleMap.x by A1,A2,FUNCT_1:72
    .= |[cos(2*PI*x), sin(2*PI*x)]| by Def11
    .= |[cos arccos z1, sin(2*PI*x)]| by XCMPLX_1:88
    .= |[cos arccos z1, sin arccos z1]| by XCMPLX_1:88
    .= |[z1, sin arccos z1]| by A7,SIN_COS6:93
    .= |[z1, z2]| by A5,A6,A13,SIN_COS6:104
    .= y by EUCLID:57;
  hence thesis by A1,A4,A14,FUNCT_1:def 5;
  end;
end;

registration
  cluster CircleMap -> onto;
coherence
proof
A1: [#]R^1 = REAL by TOPMETR:24;
A2: R^1 | [#]R^1 = R^1 by TSEP_1:3;
  CircleMap|REAL = CircleMap by Lm19,RELAT_1:98;
  hence thesis by A1,A2,Th39;
end;
end;

Lm25:
CircleMap | [.0,1.[ is one-to-one
proof
  set f = CircleMap | [.0,1.[;
A1: dom f = [.0,1.[ by Lm19,RELAT_1:91;
  let x1, y1 be set;
  assume
A2: x1 in dom f;
  then reconsider x = x1 as Real by A1;
  assume
A3: y1 in dom f;
  then reconsider y = y1 as Real by A1;
  assume
A4: f.x1 = f.y1;
A5: f.x = CircleMap.x by A1,A2,FUNCT_1:72
     .= |[cos(2*PI*x),sin(2*PI*x)]| by Def11;
A6: f.y = CircleMap.y by A1,A3,FUNCT_1:72
     .= |[cos(2*PI*y),sin(2*PI*y)]| by Def11;
  then
A7: cos(2*PI*x) = cos(2*PI*y) by A4,A5,SPPOL_2:1;
A8: sin(2*PI*x) = sin(2*PI*y) by A4,A5,A6,SPPOL_2:1;
A9: sin(2*PI*x) = sin.(2*PI*x) by SIN_COS:def 21;
A10: sin(2*PI*y) = sin.(2*PI*y) by SIN_COS:def 21;
A11: cos(2*PI*x) = cos.(2*PI*x) by SIN_COS:def 23;
A12: cos(2*PI*y) = cos.(2*PI*y) by SIN_COS:def 23;
A13: [.0,PI/2.] c= [.-PI/2,PI/2.] by RCOMP_1:49;
A14: sin | [.PI/2,3/2*PI.] is one-to-one;
  per cases by A1,A2,RCOMP_2:3;
  suppose 0 <= x & x <= 1/4;
  then 2*PI*0 <= 2*PI*x & 2*PI*x <= 2*PI*(1/4) by XREAL_1:66;
  then
A15: 2*PI*x in [.0,2*PI*1/4.] by RCOMP_1:48;
  then
A16: cos(2*PI*x) >= 0 by A11,A13,COMPTRIG:28;
  [.0,PI/2.] c= [.0,PI.] by Lm5,RCOMP_1:49;
  then
A17: sin(2*PI*x) >= 0 by A9,A15,COMPTRIG:24;
  per cases by A1,A3,RCOMP_2:3;
  suppose 0 <= y & y <= 1/4;
  then 2*PI*0 <= 2*PI*y & 2*PI*y <= 2*PI*(1/4) by XREAL_1:66;
  then
A18: 2*PI*y in [.0,2*PI*1/4.] by RCOMP_1:48;
  set g = sin | [.0,PI/2.];
A19: dom g = [.0,PI/2.] by Lm8,RELAT_1:91;
  g.(2*PI*x) = sin.(2*PI*x) by A15,FUNCT_1:72
     .= sin.(2*PI*y) by A8,A9,SIN_COS:def 21
     .= g.(2*PI*y) by A18,FUNCT_1:72;
  then 2*PI*x = 2*PI*y by A15,A18,A19,FUNCT_1:def 8;
  then x = 2*PI*y/P2 by XCMPLX_1:90;
  hence thesis by XCMPLX_1:90;
  end;
  suppose 1/4 < y & y < 3/4;
  then 2*PI*(1/4) < 2*PI*y & 2*PI*y < 2*PI*(3/4) by XREAL_1:70;
  then 2*PI*y in ].PI/2,3/2*PI.[ by RCOMP_1:47;
  hence thesis by A7,A12,A16,COMPTRIG:29;
  end;
  suppose 3/4 <= y & y < 1;
  then 2*PI*(3/4) <= 2*PI*y & 2*PI*y < 2*PI*1 by XREAL_1:66,70;
  then
A20: 2*PI*y in [.3/2*PI,2*PI.[ by RCOMP_2:3;
  [.3/2*PI,2*PI.[ c= ].PI,2*PI.[ by Lm6,TOPREALA:14;
  hence thesis by A8,A10,A17,A20,COMPTRIG:25;
  end;
  end;
  suppose 1/4 < x & x <= 1/2;
  then 2*PI*(1/4) < 2*PI*x & 2*PI*x <= 2*PI*(1/2) by XREAL_1:66,70;
  then
A21: 2*PI*x in ].PI/2,2*PI*(1/2).] by RCOMP_2:4;
  ].PI/2,PI.] c= ].PI/2,3/2*PI.[ by Lm6,TOPREALA:18;
  then
A22: cos(2*PI*x) < 0 by A11,A21,COMPTRIG:29;
  ].PI/2,PI.] c= [.0,PI.] by TOPREALA:15;
  then
A23: sin(2*PI*x) >= 0 by A9,A21,COMPTRIG:24;
  per cases by A1,A3,RCOMP_2:3;
  suppose 0 <= y & y <= 1/4;
  then 2*PI*0 <= 2*PI*y & 2*PI*y <= 2*PI*(1/4) by XREAL_1:66;
  then 2*PI*y in [.0,2*PI*(1/4).] by RCOMP_1:48;
  hence thesis by A7,A12,A13,A22,COMPTRIG:28;
  end;
  suppose 1/4 < y & y <= 1/2;
  then 2*PI*(1/4) < 2*PI*y & 2*PI*y <= 2*PI*(1/2) by XREAL_1:66,70;
  then
A24: 2*PI*y in ].2*PI*(1/4),2*PI*(1/2).] by RCOMP_2:4;
  set g = sin | ].PI/2,PI.];
  ].PI/2,PI.] c= [.PI/2,3/2*PI.] by Lm6,TOPREALA:15;
  then
A25: g is one-to-one by A14,SIN_COS6:2;
A26: dom g = ].PI/2,PI.] by Lm8,RELAT_1:91;
  g.(2*PI*x) = sin.(2*PI*x) by A21,FUNCT_1:72
     .= sin.(2*PI*y) by A8,A9,SIN_COS:def 21
     .= g.(2*PI*y) by A24,FUNCT_1:72;
  then 2*PI*x = 2*PI*y by A21,A24,A25,A26,FUNCT_1:def 8;
  then x = 2*PI*y/P2 by XCMPLX_1:90;
  hence thesis by XCMPLX_1:90;
  end;
  suppose 1/2 < y & y < 1;
  then 2*PI*(1/2) < 2*PI*y & 2*PI*y < 2*PI*1 by XREAL_1:70;
  then 2*PI*y in ].PI,2*PI.[ by RCOMP_1:47;
  hence thesis by A8,A10,A23,COMPTRIG:25;
  end;
  end;
  suppose 1/2 < x & x <= 3/4;
  then 2*PI*(1/2) < 2*PI*x & 2*PI*x <= 2*PI*(3/4) by XREAL_1:66,70;
  then
A27: 2*PI*x in ].PI,2*PI*(3/4).] by RCOMP_2:4;
  ].PI,3/2*PI.] c= ].PI,2*PI.[ by Lm7,TOPREALA:18;
  then
A28: sin(2*PI*x) < 0 by A9,A27,COMPTRIG:25;
  ].PI,3/2*PI.] c= [.PI/2,3/2*PI.] by Lm5,TOPREALA:15;
  then
A29: cos(2*PI*x) <= 0 by A11,A27,COMPTRIG:30;
  per cases by A1,A3,RCOMP_2:3;
  suppose 0 <= y & y <= 1/2;
  then 2*PI*0 <= 2*PI*y & 2*PI*y <= 2*PI*(1/2) by XREAL_1:66;
  then 2*PI*y in [.0,PI.] by RCOMP_1:48;
  hence thesis by A8,A10,A28,COMPTRIG:24;
  end;
  suppose 1/2 < y & y <= 3/4;
  then 2*PI*(1/2) < 2*PI*y & 2*PI*y <= 2*PI*(3/4) by XREAL_1:66,70;
  then
A30: 2*PI*y in ].PI,2*PI*(3/4).] by RCOMP_2:4;
  set g = sin | ].PI,3/2*PI.];
  ].PI,3/2*PI.] c= [.PI/2,3/2*PI.] by Lm5,TOPREALA:15;
  then
A31: g is one-to-one by A14,SIN_COS6:2;
A32: dom g = ].PI,3/2*PI.] by Lm8,RELAT_1:91;
  g.(2*PI*x) = sin.(2*PI*x) by A27,FUNCT_1:72
     .= sin.(2*PI*y) by A8,A9,SIN_COS:def 21
     .= g.(2*PI*y) by A30,FUNCT_1:72;
  then 2*PI*x = 2*PI*y by A27,A30,A31,A32,FUNCT_1:def 8;
  then x = 2*PI*y/P2 by XCMPLX_1:90;
  hence thesis by XCMPLX_1:90;
  end;
  suppose 3/4 < y & y < 1;
  then 2*PI*(3/4) < 2*PI*y & 2*PI*y < 2*PI*1 by XREAL_1:70;
  then 2*PI*y in ].3/2*PI,2*PI.[ by RCOMP_1:47;
  hence thesis by A7,A12,A29,COMPTRIG:31;
  end;
  end;
  suppose 3/4 < x & x < 1;
  then 2*PI*(3/4) < 2*PI*x & 2*PI*x < 2*PI*1 by XREAL_1:70;
  then
A33: 2*PI*x in ].3/2*PI,2*PI.[ by RCOMP_1:47;
  ].3/2*PI,2*PI.[ c= ].PI,2*PI.[ by Lm6,RCOMP_1:52;
  then
A34: sin(2*PI*x) < 0 by A9,A33,COMPTRIG:25;
A35: cos(2*PI*x) > 0 by A11,A33,COMPTRIG:31;
  per cases by A1,A3,RCOMP_2:3;
  suppose 0 <= y & y <= 1/2;
  then 2*PI*0 <= 2*PI*y & 2*PI*y <= 2*PI*(1/2) by XREAL_1:66;
  then 2*PI*y in [.0,PI.] by RCOMP_1:48;
  hence thesis by A8,A10,A34,COMPTRIG:24;
  end;
  suppose 1/2 < y & y <= 3/4;
  then 2*PI*(1/2) < 2*PI*y & 2*PI*y <= 2*PI*(3/4) by XREAL_1:66,70;
  then
A36: 2*PI*y in ].PI,3/2*PI.] by RCOMP_2:4;
  ].PI,3/2*PI.] c= [.PI/2,3/2*PI.] by Lm5,TOPREALA:15;
  hence thesis by A7,A12,A35,A36,COMPTRIG:30;
  end;
  suppose 3/4 < y & y < 1;
  then 2*PI*(3/4) < 2*PI*y & 2*PI*y < 2*PI*1 by XREAL_1:70;
  then
A37: 2*PI*y in ].3/2*PI,2*PI.[ by RCOMP_1:47;
  set g = sin | ].3/2*PI,2*PI.[;
A38: dom g = ].3/2*PI,2*PI.[ by Lm8,RELAT_1:91;
  g.(2*PI*x) = sin.(2*PI*x) by A33,FUNCT_1:72
     .= sin.(2*PI*y) by A8,A9,SIN_COS:def 21
     .= g.(2*PI*y) by A37,FUNCT_1:72;
  then 2*PI*x = 2*PI*y by A33,A37,A38,FUNCT_1:def 8;
  then x = 2*PI*y/P2 by XCMPLX_1:90;
  hence thesis by XCMPLX_1:90;
  end;
  end;
end;

registration
  let r be real number;
  cluster CircleMap | [.r,r+1.[ -> one-to-one;
coherence
proof
  set f = CircleMap | [.r,r+1.[;
  set g = CircleMap | [.0,1.[;
A1: dom f = [.r,r+1.[ by Lm19,RELAT_1:91;
A2: dom g = [.0,1.[ by Lm19,RELAT_1:91;
  let x, y be set;
  assume that
A3: x in dom f and
A4: y in dom f and
A5: f.x = f.y;
  reconsider x, y as Real by A1,A3,A4;
  set x1 = frac x;
  set y1 = frac y;
A6: x1 = x-[\x/] by INT_1:def 6;
A7: y1 = y-[\y/] by INT_1:def 6;
   0 <= x1 & x1 < 1 by INT_1:69;
   then
A8: x1 in [.0,1.[ by RCOMP_2:3;
   0 <= y1 & y1 < 1 by INT_1:69;
   then
A9: y1 in [.0,1.[ by RCOMP_2:3;
A10: r <= x & x < r+1 by A1,A3,RCOMP_2:3;
A11: r <= y & y < r+1 by A1,A4,RCOMP_2:3;
A12: f.x = CircleMap.x by A3,FUNCT_1:70
     .= CircleMap.(x+-[\x/]) by Th32
     .= g.x1 by A2,A6,A8,FUNCT_1:70;
  f.y = CircleMap.y by A4,FUNCT_1:70
     .= CircleMap.(y+-[\y/]) by Th32
     .= g.y1 by A2,A7,A9,FUNCT_1:70;
  then x1 = y1 by A2,A5,A8,A9,A12,Lm25,FUNCT_1:def 8;
  hence thesis by A10,A11,TOPREALA:7;
end;
end;

registration
  let r be real number;
  cluster CircleMap | ].r,r+1.[ -> one-to-one;
coherence
proof
A1: CircleMap | [.r,r+1.[ is one-to-one;
  ].r,r+1.[ c= [.r,r+1.[ by TOPREALA:20;
  hence thesis by A1,SIN_COS6:2;
end;
end;

theorem Th40:
  b-a <= 1 implies
  for d being set st d in IntIntervals(a,b) holds CircleMap|d is one-to-one
proof
  assume that
A1: b-a <= 1;
  let d be set;
  assume d in IntIntervals(a,b);
  then consider n being Element of INT such that
A2: d = ].a+n,b+n.[;
A3: CircleMap | [.a+n,a+n+1.[ is one-to-one;
  b-a+(a+n) <= 1+(a+n) by A1,XREAL_1:8;
  then ].a+n,b+n.[ c= [.a+n,a+n+1.[ by TOPREALA:20;
  hence thesis by A2,A3,SIN_COS6:2;
end;

theorem Th41:
  for d being set st d in IntIntervals(a,b)
  holds CircleMap.:d = CircleMap.:union IntIntervals(a,b)
proof
  set D = IntIntervals(a,b);
  let d be set;
  assume
A1: d in D;
  then d c= union D by ZFMISC_1:92;
  hence CircleMap.:d c= CircleMap.:union D by RELAT_1:156;
  let y be set;
  assume y in CircleMap.:union D;
  then consider x being Element of R^1 such that
A2:  x in union D and
A3:  y = CircleMap.x by FUNCT_2:116;
  consider Z being set such that
A4: x in Z and
A5: Z in D by A2,TARSKI:def 4;
  consider n being Element of INT such that
A6: Z = ]. a+n, b+n .[ by A5;
  consider i being Element of INT such that
A7: d = ]. a+i, b+i .[ by A1;
  set k = x+i-n;
A8: k in R by TOPMETR:24,XREAL_0:def 1;
  a+n < x & x < b+n by A4,A6,RCOMP_1:47;
  then a+n+i < x+i & x+i < b+n+i by XREAL_1:8;
  then a+n+i-n < x+i-n & x+i-n < b+n+i-n by XREAL_1:11;
  then
A9: k in d by A7,RCOMP_1:47;
  CircleMap.k = CircleMap.(x+(i-n))
    .= y by A3,Th32;
  hence y in CircleMap.:d by A8,A9,FUNCT_2:43;
end;

definition
  let r be Point of R^1;
  func CircleMap(r) ->
       Function of R^1 | R^1(].r,r+1.[), Topen_unit_circle(CircleMap.r) equals

  CircleMap | ].r,r+1.[;
coherence
proof
  set X = Topen_unit_circle(CircleMap.r);
  set A = ].r,r+1.[;
  set B = [.r,r+1.[;
  set f = CircleMap | A;
  set g = CircleMap | B;
A1: A c= B by TOPREALA:20;
A2: dom f = A by Lm19,RELAT_1:91;
A3: the carrier of X = cS1 \ {CircleMap.r} by Def10;
A4: rng f c= the carrier of X
  proof
    let y be set;
    assume
A5:   y in rng f;
    now
      assume
A6:    y = CircleMap.r;
      consider x being set such that
A7:     x in dom f and
A8:     f.x = y by A5,FUNCT_1:def 5;
      r+0 < r+1 by XREAL_1:10;
      then
A9:  r in B by RCOMP_2:3;
A10:  dom g = B by Lm19,RELAT_1:91;
      g.x = CircleMap.x by A1,A2,A7,FUNCT_1:72
         .= CircleMap.r by A2,A6,A7,A8,FUNCT_1:72
         .= g.r by A9,FUNCT_1:72;
      then x = r by A1,A2,A7,A9,A10,FUNCT_1:def 8;
      hence contradiction by A2,A7,RCOMP_1:47;
    end;
    then not y in {CircleMap.r} by TARSKI:def 1;
    hence thesis by A3,A5,XBOOLE_0:def 4;
  end;
  the carrier of R^1 | R^1(A) = A by PRE_TOPC:29;
  hence thesis by A2,A4,FUNCT_2:4;
end;
end;

Lm26:
rng (AffineMap(1,-a) | ].r+a,r+a+1.[) = ].r,r+1.[
proof
  set F = AffineMap(1,-a);
  set f = F | ].r+a,r+a+1.[;
  dom F = REAL by FUNCT_2:def 1;
  then
A1: dom f = ].r+a,r+a+1.[ by RELAT_1:91;
  thus rng f = ].r,r+1.[
  proof
    thus rng f c= ].r,r+1.[
    proof
      let y be set;
      assume y in rng f;
      then consider x being set such that
A2:     x in dom f and
A3:     f.x = y by FUNCT_1:def 5;
      reconsider x as Real by A2;
A4:   y = F.x by A2,A3,FUNCT_1:70
       .= 1*x+-a by JORDAN16:def 3;
      r+a < x & x < r+a+1 by A1,A2,RCOMP_1:47;
      then r+a-a < x-a & x-a < r+a+1-a by XREAL_1:11;
      hence thesis by A4,RCOMP_1:47;
    end;
    let y be set;
    assume
A5:   y in ].r,r+1.[;
    then reconsider y as Real;
    r < y & y < r+1 by A5,RCOMP_1:47;
    then r+a < y+a & y+a < r+1+a by XREAL_1:8;
    then
A6: y+a in ].r+a,r+a+1.[ by RCOMP_1:47;
    then f.(y+a) = F.(y+a) by FUNCT_1:72
      .= 1*(y+a)+-a by JORDAN16:def 3
      .= y;
    hence thesis by A1,A6,FUNCT_1:def 5;
  end;
end;

theorem Th42:
  CircleMap(R^1(a+i)) = CircleMap(R^1(a)) * (AffineMap(1,-i) | ].a+i,a+i+1.[)
proof
  set W = ].a,a+1.[;
  set Q = ].a+i,a+i+1.[;
  set h = CircleMap(R^1(a+i));
  set g = CircleMap(R^1(a));
  set F = AffineMap(1,-i);
  set f = F|Q;
A1: dom h = Q by Lm19,RELAT_1:91;
A2: dom g = W by Lm19,RELAT_1:91;
  dom F = REAL by FUNCT_2:def 1;
  then
A3: dom f = Q by RELAT_1:91;
  rng f = ].a,a+1.[ by Lm26;
  then
A4: dom (g*f) = dom f by A2,RELAT_1:46;
  for x being set st x in dom h holds h.x = (g*f).x
  proof
    let x be set;
    assume
A5:   x in dom h;
    reconsider y = x as Real by A1,A5;
    a+i < y & y < a+i+1 by A1,A5,RCOMP_1:47;
    then a+i-i < y-i & y-i < a+i+1-i by XREAL_1:11;
    then
A6: y-i in W by RCOMP_1:47;
    thus (g*f).x = g.(f.x) by A1,A3,A5,FUNCT_1:23
      .= g.(F.x) by A1,A5,FUNCT_1:72
      .= g.(1*y+-i) by JORDAN16:def 3
      .= CircleMap.(y+-i) by A6,FUNCT_1:72
      .= CircleMap.y by Th32
      .= h.x by A1,A5,FUNCT_1:72;
  end;
  hence thesis by A1,A3,A4,FUNCT_1:9;
end;

registration
  let r be Point of R^1;
  cluster CircleMap(r) -> one-to-one onto continuous;
coherence
proof
  thus CircleMap(r) is one-to-one;
  thus CircleMap(r) is onto
  proof
  set A = ].r,r+1.[;
  set f = CircleMap | A;
  set TOUC = Topen_unit_circle(CircleMap.r);
  set X = the carrier of TOUC;
  thus rng CircleMap(r) c= X;
  let y be set;
  assume
A1: y in X;
  reconsider z = y as Point of TOP-REAL 2 by A1,Lm9;
  set z1 = z`1, z2 = z`2;
  set x = arccos z1/(2*PI);
A2: [\r/] <= r by INT_1:def 4;
A3: dom f = ].r,r+1.[ by Lm19,RELAT_1:91;
  z is Point of TUC by A1,TOPMETR:2;
  then
A4: |. z .| = 1 by Th12;
A5: z1^2 + z2^2 = |. z .|^2 by JGRAPH_1:46;
A6: -1 <= z1 & z1 <= 1 by A1,Th27;
A7: 0 <= x by A6,Lm24;
   x <= 1/2 by A6,Lm24;
   then
A8: x < 1 by XXREAL_0:2;
A9: x-x < 1-x by A8,REAL_1:92;
  per cases;
  suppose
A10: z2 < 0;
A11: CircleMap.-x = |[cos(-2*PI*x), sin(2*PI*(-x))]| by Def11
    .= |[cos(2*PI*x), sin(2*PI*(-x))]| by SIN_COS:34
    .= |[cos arccos z1, sin(-2*PI*x)]| by XCMPLX_1:88
    .= |[cos arccos z1, -sin(2*PI*x)]| by SIN_COS:34
    .= |[cos arccos z1, -sin arccos z1]| by XCMPLX_1:88
    .= |[z1, -sin arccos z1]| by A6,SIN_COS6:93
    .= |[z1, --z2]| by A4,A5,A10,SIN_COS6:105
    .= y by EUCLID:57;
  per cases;
  suppose
A12: 1-x+[\r/] in A;
  then f.(1-x+[\r/]) = CircleMap.(-x+([\r/]+1)) by FUNCT_1:72
    .= CircleMap.-x by Th32;
  hence thesis by A3,A11,A12,FUNCT_1:def 5;
  end;
  suppose
A13: not 1-x+[\r/] in A;
  now
    assume x = 0;
    then arccos z1 = 0 by XCMPLX_1:50;
    then z1 = 1 by A6,SIN_COS6:98;
    then 1 + z2^2 - 1 = 0 by A4,A5;
    hence contradiction by A10,XCMPLX_1:6;
  end;
  then [\r/]-x < r-0 by A2,A7,REAL_1:92;
  then -x+[\r/]+1 < r+1 by XREAL_1:8;
  then
A14: r >= 1-x+[\r/] by A13,RCOMP_1:47;
A15: now
    assume -x+[\r/]+1 = r;
    then CircleMap.r = CircleMap.(-x+([\r/]+1))
      .= CircleMap.-x by Th32;
    hence contradiction by A1,A11,Th22;
  end;
A16: r < [\r/]+1 by INT_1:52;
  [\r/]+1+0 < [\r/]+1+(1-x) by A9,XREAL_1:8;
  then
A17: r < 2-x+[\r/] by A16,XXREAL_0:2;
  1-x+[\r/] < r by A14,A15,REAL_1:def 5;
  then 1-x+[\r/]+1 < r+1 by XREAL_1:8;
  then
A18: -x+[\r/]+2 in A by A17,RCOMP_1:47;
  then f.(-x+([\r/]+2)) = CircleMap.(-x+([\r/]+2)) by FUNCT_1:72
    .= CircleMap.-x by Th32;
  hence thesis by A3,A11,A18,FUNCT_1:def 5;
  end;
  end;
  suppose
A19: z2 >= 0;
A20: CircleMap.x = |[cos(2*PI*x), sin(2*PI*x)]| by Def11
    .= |[cos arccos z1, sin(2*PI*x)]| by XCMPLX_1:88
    .= |[cos arccos z1, sin arccos z1]| by XCMPLX_1:88
    .= |[z1, sin arccos z1]| by A6,SIN_COS6:93
    .= |[z1, z2]| by A4,A5,A19,SIN_COS6:104
    .= y by EUCLID:57;
  per cases;
  suppose
A21: x+[\r/] in A;
  then f.(x+[\r/]) = CircleMap.(x+[\r/]) by FUNCT_1:72
    .= CircleMap.x by Th32;
  hence thesis by A3,A20,A21,FUNCT_1:def 5;
  end;
  suppose
A22: not x+[\r/] in A;
A23: r < [\r/]+1 by INT_1:52;
  0+([\r/]+1) <= x+([\r/]+1) by A7,XREAL_1:8;
  then
A24: r < x+[\r/]+1 by A23,XXREAL_0:2;
  x+[\r/] < 1+r by A2,A8,XREAL_1:10;
  then x+[\r/] <= r by A22,RCOMP_1:47;
  then
A25: x+[\r/]+1 <= r+1 by XREAL_1:8;
  now
    assume x+[\r/]+1 = r+1;
    then CircleMap.r = CircleMap.x by Th32;
    hence contradiction by A1,A20,Th22;
  end;
  then x+[\r/]+1 < r+1 by A25,REAL_1:def 5;
  then
A26: x+[\r/]+1 in A by A24,RCOMP_1:47;
  then f.(x+[\r/]+1) = CircleMap.(x+([\r/]+1)) by FUNCT_1:72
    .= CircleMap.x by Th32;
  hence thesis by A3,A20,A26,FUNCT_1:def 5;
  end;
  end;
end;
A27: Topen_unit_circle(CircleMap.r) =
    (Tunit_circle(2)) | (([#]Tunit_circle(2)) \ {CircleMap.r}) by Th23;
  thus thesis by A27,TOPREALA:29;
end;
end;

definition
  func Circle2IntervalR -> Function
 of Topen_unit_circle(c[10]), R^1 | R^1(].0,1.[)
    means :Def13:
  for p being Point of Topen_unit_circle(c[10])
    ex x, y being real number st p = |[x,y]| &
    (y >= 0 implies it.p = arccos x/(2*PI)) &
    (y <= 0 implies it.p = 1-arccos x/(2*PI));
existence
proof
  reconsider A as non empty Subset of R^1;
  defpred P[set,set] means
    ex x, y being real number st $1 = |[x,y]| &
    (y >= 0 implies $2 = arccos x/P2) &
    (y <= 0 implies $2 = 1-arccos x/P2);
A1: Y = A by PRE_TOPC:29;
A2: for x being Element of X ex y being Element of Y st P[x,y]
  proof
    let x be Element of X;
A3: X = cS1 \ {c[10]} by Def10;
    then
A4: x in cS1 by XBOOLE_0:def 4;
A5: cS1 is Subset of TOP-REAL 2 by TSEP_1:1;
    then consider a, b being Real such that
A6:   x = <*a,b*> by A4,EUCLID:55;
A7: not x in {c[10]} by A3,XBOOLE_0:def 4;
    set k = arccos a;
    reconsider x1 = x as Point of TOP-REAL 2 by A4,A5;
A8: x = |[a,b]| by A6;
    then
A9: a = x1`1 & b = x1`2 by EUCLID:56;
    then
A10: -1 <= a & a <= 1 by Th27;
    then
A11: 0 <= k & k <= PI by SIN_COS6:101;
A12: 0 <= k/P2 & k/P2 <= 1/2 by A10,Lm24;
A13: 1^2 = |. x1 .|^2 by A4,Th12
       .= a^2+b^2 by A9,JGRAPH_3:10;
A14: now
      assume
A15:     k = 0;
      then
A16:   a = 1 by A10,SIN_COS6:98;
      1-1 = 1+b^2-1 by A10,A13,A15,SIN_COS6:98;
      then b = 0 by XCMPLX_1:6;
      then x = c[10] by A6,A16;
      hence contradiction by A7,TARSKI:def 1;
    end;
    per cases;
    suppose
A17:   b = 0;
A18: a <> 1 by A7,A8,A17,TARSKI:def 1;
    set y = k/P2;
    y <> 0 by A14,XCMPLX_1:50;
    then 0 < y & y < 1 by A12,XXREAL_0:2;
    then reconsider y as Element of Y by A1,RCOMP_1:47;
    take y, a, b;
    thus x = |[a,b]| by A6;
    thus b >= 0 implies y = arccos a/P2;
    assume b <= 0;
    thus y = 1*PI/P2 by A13,A17,A18,SIN_COS6:95,SQUARE_1:110
      .= 1-1/2 by XCMPLX_1:92
      .= 1-1*PI/P2 by XCMPLX_1:92
      .= 1-arccos a/P2 by A13,A17,A18,SIN_COS6:95,SQUARE_1:110;
    end;
    suppose
A19:   b > 0;
    set y = k/P2;
    y <> 0 by A14,XCMPLX_1:50;
    then 0 < y & y < 1 by A12,XXREAL_0:2;
    then reconsider y as Element of Y by A1,RCOMP_1:47;
    take y, a, b;
    thus thesis by A6,A19;
    end;
    suppose
A20:   b < 0;
    set y = 1-k/P2;
A21: (P2-k)/P2 = P2/P2-k/P2 by XCMPLX_1:121
      .= y by XCMPLX_1:60;
    1*k < P2 by A11,REAL_2:199;
    then k-k < P2-k & P2-k < P2-0 by A11,A14,REAL_1:92;
    then 0/P2 < y & y < P2/P2 by A21,REAL_1:73;
    then 0 < y & y < 1 by XCMPLX_1:60;
    then reconsider y as Element of Y by A1,RCOMP_1:47;
    take y, a, b;
    thus thesis by A6,A20;
    end;
  end;
  ex G being Function of TOUC, R^1|R^1(].0,0+p1.[) st
    for p being Point of TOUC holds P[p,G.p] from FUNCT_2:sch 3(A2);
  hence thesis;
end;
uniqueness
proof
  let f, g be Function of TOUC, R^1 | A such that
A22: for p being Point of TOUC
    ex x, y being real number st p = |[x,y]| &
    (y >= 0 implies f.p = arccos x/(2*PI)) &
    (y <= 0 implies f.p = 1-arccos x/(2*PI)) and
A23: for p being Point of TOUC
    ex x, y being real number st p = |[x,y]| &
    (y >= 0 implies g.p = arccos x/(2*PI)) &
    (y <= 0 implies g.p = 1-arccos x/(2*PI));
  now
    let p be Point of TOUC;
A24: ex x1, y1 being real number st p = |[x1,y1]| &
     (y1 >= 0 implies f.p = arccos x1/(2*PI)) &
     (y1 <= 0 implies f.p = 1-arccos x1/(2*PI)) by A22;
    ex x2, y2 being real number st p = |[x2,y2]| &
     (y2 >= 0 implies g.p = arccos x2/(2*PI)) &
     (y2 <= 0 implies g.p = 1-arccos x2/(2*PI)) by A23;
    hence f.p = g.p by A24,SPPOL_2:1;
  end;
  hence thesis by FUNCT_2:113;
end;
end;

set A1 = R^1(].1/2,1/2+p1.[);

definition
  func Circle2IntervalL ->
    Function of Topen_unit_circle(c[-10]), R^1 | R^1(].1/2,3/2.[)
    means :Def14:
  for p being Point of Topen_unit_circle(c[-10])
    ex x, y being real number st p = |[x,y]| &
    (y >= 0 implies it.p = 1+arccos x/(2*PI)) &
    (y <= 0 implies it.p = 1-arccos x/(2*PI));
existence
proof
  reconsider A1 as non empty Subset of R^1;
  defpred P[set,set] means
    ex x, y being real number st $1 = |[x,y]| &
    (y >= 0 implies $2 = 1+arccos x/P2) &
    (y <= 0 implies $2 = 1-arccos x/P2);
A1: Ym = A1 by PRE_TOPC:29;
A2: for x being Element of Xm ex y being Element of Ym st P[x,y]
  proof
    let x be Element of Xm;
A3: Xm = cS1 \ {c[-10]} by Def10;
    then
A4: x in cS1 by XBOOLE_0:def 4;
A5: cS1 is Subset of TOP-REAL 2 by TSEP_1:1;
    then consider a, b being Real such that
A6:   x = <*a,b*> by A4,EUCLID:55;
A7: not x in {c[-10]} by A3,XBOOLE_0:def 4;
    set k = arccos a;
    reconsider x1 = x as Point of TOP-REAL 2 by A4,A5;
A8: x = |[a,b]| by A6;
    then
A9: a = x1`1 & b = x1`2 by EUCLID:56;
    then
A10: -1 <= a & a <= 1 by Th27;
A11: 0 <= k/P2 & k/P2 <= 1/2 by A10,Lm24;
A12: 1^2 = |. x1 .|^2 by A4,Th12
       .= a^2+b^2 by A9,JGRAPH_3:10;
A13: now
      assume
A14:     k = PI;
      then
A15:   a = -1 by A10,SIN_COS6:100;
      1-1 = (-1)^2+b^2-1 by A10,A12,A14,SIN_COS6:100
         .= 1^2+b^2-1;
      then b = 0 by XCMPLX_1:6;
      then x = c[-10] by A6,A15;
      hence contradiction by A7,TARSKI:def 1;
    end;
A16: now
      assume k/P2 = 1/2;
      then k/P2*2 = 1/2*2;
      then k/PI = 1 by XCMPLX_1:93;
      hence contradiction by A13,XCMPLX_1:58;
    end;
A17: now
      let y be real number;
      assume
A18:    y = 1+k/P2;
      then
A19:  1+0 <= y & y <= 1+1/2 by A11,XREAL_1:8;
      y <> 1+1/2 by A16,A18;
      then 1/2 < y & y < 3/2 by A19,XXREAL_0:2,REAL_1:def 5;
      hence y is Element of Ym by A1,RCOMP_1:47;
    end;
    per cases;
    suppose
A20:   b = 0;
    then a <> -1 by A7,A8,TARSKI:def 1;
    then
A21: a = 1 by A12,A20,SQUARE_1:110;
    reconsider y = 1+k/P2 as Element of Ym by A17;
    take y, a, b;
    thus x = |[a,b]| by A6;
    thus thesis by A21,SIN_COS6:97;
    end;
    suppose
A22:   b > 0;
    reconsider y = 1+k/P2 as Element of Ym by A17;
    take y, a, b;
    thus thesis by A6,A22;
    end;
    suppose
A23:   b < 0;
    set y = 1-k/P2;
A24: 1-0 >= y & y >= 1-1/2 by A11,REAL_1:92;
    y <> 1/2 by A16;
    then 1/2 < y & y < 3/2 by A24,XXREAL_0:2,REAL_1:def 5;
    then reconsider y as Element of Ym by A1,RCOMP_1:47;
    take y, a, b;
    thus thesis by A6,A23;
    end;
  end;
  ex G being Function of TOUCm, R^1|R^1(].1/2,1/2+p1.[) st
    for p being Point of TOUCm holds P[p,G.p] from FUNCT_2:sch 3(A2);
  hence thesis;
end;
uniqueness
proof
  let f, g be Function of TOUCm, R^1 | R^1(].1/2,3/2.[) such that
A25: for p being Point of TOUCm
    ex x, y being real number st p = |[x,y]| &
    (y >= 0 implies f.p = 1+arccos x/(2*PI)) &
    (y <= 0 implies f.p = 1-arccos x/(2*PI)) and
A26: for p being Point of TOUCm
    ex x, y being real number st p = |[x,y]| &
    (y >= 0 implies g.p = 1+arccos x/(2*PI)) &
    (y <= 0 implies g.p = 1-arccos x/(2*PI));
  now
    let p be Point of TOUCm;
A27: ex x1, y1 being real number st p = |[x1,y1]| &
     (y1 >= 0 implies f.p = 1+arccos x1/(2*PI)) &
     (y1 <= 0 implies f.p = 1-arccos x1/(2*PI)) by A25;
    ex x2, y2 being real number st p = |[x2,y2]| &
     (y2 >= 0 implies g.p = 1+arccos x2/(2*PI)) &
     (y2 <= 0 implies g.p = 1-arccos x2/(2*PI)) by A26;
    hence f.p = g.p by A27,SPPOL_2:1;
  end;
  hence thesis by FUNCT_2:113;
end;
end;

set C = Circle2IntervalR;
set Cm = Circle2IntervalL;

theorem Th43:
  (CircleMap(R^1(0)))" = Circle2IntervalR
proof
  set f = CircleMap(R^1(0));
  reconsider r0 = 0 as Point of R^1 by TOPMETR:24;
  set F = AffineMap(2*PI,0);
  reconsider A as non empty Subset of R^1;
A1: CircleMap.r0 = c[10] by Th33;
  then reconsider f as Function of R^1|A, TOUC;
  set Y = the carrier of (R^1|A);
A2: Y = A by PRE_TOPC:29;
A3: rng f = X by A1,FUNCT_2:def 3;
A4: dom (C*f) = Y by FUNCT_2:def 1;
A5: dom id Y = Y by RELAT_1:71;
  now
    let a be set;
    assume
A6:   a in dom (C*f);
    reconsider b = a as Point of R^1|A by A6;
    consider x, y being real number such that
A7:   f.b = |[x,y]| and
A8:   y >= 0 implies C.(f.b) = arccos x/P2 and
A9:  y <= 0 implies C.(f.b) = 1-arccos x/P2 by Def13;
    reconsider c = b as Real by XREAL_0:def 1;
    f.b = CircleMap.b by A2,FUNCT_1:72
       .= |[ (cos*F).c, (sin*F).c ]| by Lm22;
    then x = (cos*F).c & y = (sin*F).c by A7,SPPOL_2:1;
    then
A10: x = cos.(F.c) & y = sin.(F.c) by FUNCT_2:21;
    then
A11: x = cos(F.c) & y = sin(F.c) by SIN_COS:def 21,def 23;
A12: F.c = 2*PI*c+0 by JORDAN16:def 3;
     then
A13: F.c/(2*PI*1) = c/1 by XCMPLX_1:92;
A14: F.(1-c) = 2*PI*(1-c)+0 by JORDAN16:def 3;
     then
A15: F.(1-c)/(2*PI*1) = (1-c)/1 by XCMPLX_1:92;
A16: 0 < c & c < 1 by A2,RCOMP_1:47;
A17: now per cases;
      suppose
A18:    y >= 0;
      then not F.c in ].PI,2*PI.[ by A10,COMPTRIG:25;
      then F.c <= PI or F.c >= 2*PI by RCOMP_1:47;
      then F.c/P2 <= (1*PI)/P2 or F.c/P2 >= 2*PI/P2 by REAL_1:73;
      then 0 <= c & c <= 1/2 by A13,A16,XCMPLX_1:60,92;
      then 2*PI*0 <= 2*PI*c+0 & 2*PI*c <= 2*PI*(1/2) by XREAL_1:66;
      hence C.(f.b) = F.c/P2 by A8,A11,A12,A18,SIN_COS6:94
        .= b by A12,XCMPLX_1:90;
      end;
      suppose
A19:    y < 0;
A20:  cos.(F.(1-c)) = cos(-2*PI*c+2*PI*1) by A14,SIN_COS:def 23
        .= cos(-2*PI*c) by COMPLEX2:10
        .= cos(2*PI*c) by SIN_COS:34;
      not F.c in [.0,PI.] by A10,A19,COMPTRIG:24;
      then F.c < 0 or F.c > PI by RCOMP_1:48;
      then F.c/P2 < 0/P2 or F.c/P2 > (1*PI)/P2 by REAL_1:73;
      then c < 0 or c > 1/2 by A13,XCMPLX_1:92;
      then 1-c < 1-1/2 & 1-c > 1-1 by A16,REAL_1:92;
      then
A21:  2*PI*0 <= 2*PI*(1-c)+0 & 2*PI*(1-c) <= 2*PI*(1/2) by XREAL_1:66;
      arccos x = arccos cos F.(1-c) by A11,A12,A20,SIN_COS:def 23
        .= F.(1-c) by A14,A21,SIN_COS6:94;
      hence C.(f.b) = b by A9,A15,A19;
      end;
    end;
    thus (C*f).a = C.(f.b) by A6,FUNCT_1:22
      .= (id Y).a by A17,FUNCT_1:35;
  end;
  then C*f = id Y by A4,A5,FUNCT_1:9;
  then C = f qua Function" by A3,FUNCT_2:36;
  hence thesis by A1,A3,TOPS_2:def 4;
end;

theorem Th44:
  (CircleMap(R^1(1/2)))" = Circle2IntervalL
proof
  set f = CircleMap(R^1(1/2));
  set G = AffineMap(2*PI,0);
  reconsider A1 as non empty Subset of R^1;
  reconsider f as Function of R^1|A1, TOUCm by Lm20;
  set Y = the carrier of (R^1|A1);
A1: Y = A1 by PRE_TOPC:29;
A2: rng f = Xm by Lm20,FUNCT_2:def 3;
A3: dom (Cm*f) = Ym by FUNCT_2:def 1;
A4: dom id Ym = Ym by RELAT_1:71;
  now
    let a be set;
    assume
A5:   a in dom (Cm*f);
    then reconsider b = a as Point of R^1|A1;
    consider x, y being real number such that
A6:   f.b = |[x,y]| and
A7:   y >= 0 implies Cm.(f.b) = 1+arccos x/P2 and
A8:  y <= 0 implies Cm.(f.b) = 1-arccos x/P2 by Def14;
    reconsider c = b as Real by XREAL_0:def 1;
A9: f.b = CircleMap.b by A1,FUNCT_1:72
       .= |[ cos(2*PI*c), sin(2*PI*c) ]| by Def11;
    then
A10: x = cos(2*PI*c) by A6,SPPOL_2:1
      .= cos(2*PI*c+2*PI*(-1)) by COMPLEX2:10
      .= cos(2*PI*(c-1));
A11: y = sin(2*PI*c) by A6,A9,SPPOL_2:1;
A12: G.(1-c) = 2*PI*(1-c)+0 by JORDAN16:def 3;
     then
A13: G.(1-c)/(2*PI*1) = (1-c)/1 by XCMPLX_1:92;
A14: 1/2 < c & c < 3/2 by A1,RCOMP_1:47;
    then c-1 < 3/2-1 by XREAL_1:11;
    then
A15: 2*PI*(c-1) <= 2*PI*(1/2) by XREAL_1:66;
    2*PI*c <= 2*PI*(1/2+1) by A14,XREAL_1:66;
    then
A16: 2*PI*c <= PI+2*PI*1;
    2*PI*(1/2) < 2*PI*c by A14,XREAL_1:70;
    then
A17: PI+2*PI*0 < 2*PI*c;
A18: now per cases;
      suppose
A19:     c >= 1;
      then
A20:  2*PI*c >= 2*PI*1 by XREAL_1:66;
      1-1 <= c-1 by A19,XREAL_1:11;
      then
A21:  2*PI*0 <= 2*PI*(c-1)+0 by XREAL_1:66;
      thus Cm.(f.b) = 1+(2*PI*(c-1))/P2 by  A7,A10,A11,A16,A20,SIN_COS6:16,
A15,A21,SIN_COS6:94
        .= 1+(c-1) by XCMPLX_1:90
        .= b;
      end;
      suppose
A22:     c < 1;
      then 2*PI*c < 2*PI*1 by XREAL_1:70;
      then
A23:  2*PI*c < 2*PI+2*PI*0;
A24:  1-c < 1-1/2 by A14,REAL_1:92;
      1-1 <= 1-c by A22,REAL_1:92;
      then
A25:  2*PI*0 <= 2*PI*(1-c)+0 by XREAL_1:66;
A26:  2*PI*(1-c) <= 2*PI*(1/2) by A24,XREAL_1:66;
      arccos x = arccos cos(2*PI*c) by A6,A9,SPPOL_2:1
        .= arccos cos(-2*PI*c) by SIN_COS:34
        .= arccos cos(2*PI*(-c)+2*PI*1) by COMPLEX2:10
        .= G.(1-c) by A12,A25,A26,SIN_COS6:94;
      hence Cm.(f.b) = b by A8,A11,A13,A17,A23,SIN_COS6:12;
    end;
    end;
    thus (Cm*f).a = Cm.(f.b) by A5,FUNCT_1:22
      .= (id Ym).a by A18,FUNCT_1:35;
  end;
  then Cm*f = id Ym by A3,A4,FUNCT_1:9;
  then Cm = f qua Function" by A2,FUNCT_2:36;
  hence thesis by A2,Lm20,TOPS_2:def 4;
end;

set A = ].0,1.[;
set Q = [.-1,1.[;
set E = ].0,PI.];
set j = 1/P2;
reconsider Q, E as non empty Subset of REAL;
Lm27: the carrier of R^1 | R^1(Q) = R^1(Q) by PRE_TOPC:29;
Lm28: the carrier of R^1 | R^1(E) = R^1(E) by PRE_TOPC:29;
Lm29: the carrier of R^1 | R^1(A) = R^1(A) by PRE_TOPC:29;
set Af = AffineMap(j,0) | R^1(E);
  dom AffineMap(j,0) = R by FUNCT_2:def 1,TOPMETR:24;
  then
Lm30: dom Af = R^1(E) by RELAT_1:91;
  rng Af c= A
  proof
    let y be set;
    assume y in rng Af;
    then consider x being set such that
A1:   x in dom Af and
A2:   Af.x = y by FUNCT_1:def 5;
    reconsider x as Real by A1;
A3: y = AffineMap(j,0).x by A1,A2,Lm30,FUNCT_1:72
     .= j*x+0 by JORDAN16:def 3
     .= x/P2 by XCMPLX_1:100;
    then reconsider y as Real;
    0 < x & x <= PI by A1,Lm30,RCOMP_2:4;
    then 0/P2 < x/P2 & x/P2 <= PI/P2 by REAL_1:73;
    then 0 < y & y <= (1*PI)/P2 by A3;
    then 0 < y & y <= 1/2 by XCMPLX_1:92;
    then 0 < y & y < 1 by XXREAL_0:2;
    hence thesis by RCOMP_1:47;
  end;
then reconsider Af as Function of R^1 | R^1(E), R^1 | R^1(A)
  by Lm28,Lm29,Lm30,FUNCT_2:4;
Lm31: R^1(AffineMap(j,0)) = AffineMap(j,0);
Lm32: dom AffineMap(j,0) = REAL by FUNCT_2:def 1;
Lm33: rng AffineMap(j,0) = REAL by JORDAN16:32;
Lm34: [#]R^1 = REAL by TOPMETR:24;
R^1| [#](R^1) = R^1 by TSEP_1:3;
 then
  R^1 = R^1|R^1(dom AffineMap(j,0)) & R^1 = R^1|R^1(rng AffineMap(j,0))
 by Lm32,Lm33,Lm34;
then reconsider Af as continuous Function of R^1 | R^1(E), R^1 | R^1(A)
by Lm31,TOPREALA:29;

set L = (R^1(AffineMap(-1,1))) | R^1(A);
Lm35: dom AffineMap(-1,1) = REAL by FUNCT_2:def 1;
  then
Lm36: dom L = A by RELAT_1:91;
  rng L c= A
  proof
    let y be set;
    assume y in rng L;
    then consider x being set such that
A1:  x in dom L and
A2:  L.x = y by FUNCT_1:def 5;
    reconsider x as Real by A1,Lm36;
A3: y = AffineMap(-1,1).x by A1,A2,Lm36,FUNCT_1:72
     .= (-1)*x+1 by JORDAN16:def 3;
    then reconsider y as Real;
    0 < x & x < 1 by A1,Lm36,RCOMP_1:47;
    then 1-1 < 1-x & 1-x < 1-0 by REAL_1:92;
    then 0 < y & y < 1 by A3;
    hence thesis by RCOMP_1:47;
  end;
  then reconsider L as Function of R^1 | R^1(A), R^1 | R^1(A)
  by Lm29,Lm36,FUNCT_2:4;
Lm37: rng AffineMap(-1,1) = REAL by JORDAN16:32;
Lm38: [#]R^1 = REAL by TOPMETR:24;
Lm39: R^1| [#](R^1) = R^1 by TSEP_1:3;
  then R^1 = R^1|R^1(dom AffineMap(-1,1)) & R^1 = R^1|R^1(rng AffineMap(-1,1))
    by Lm35,Lm37,Lm38;
  then reconsider L as continuous Function of R^1 | R^1(A), R^1 | R^1(A)
  by TOPREALA:29;

  reconsider ac = R^1(arccos) as
   continuous Function of R^1 | R^1[.-1,1.], R^1 | R^1[.0,PI.]
     by SIN_COS6:87,88;

  set c = ac | R^1(Q);
  Q c= [.-1,1.] by TOPREALA:11;
  then
Lm40: dom c = Q by RELAT_1:91,SIN_COS6:88;
Lm41: rng c c= E
  proof
    let y be set;
    assume
A1:   y in rng c;
    then consider x being set such that
A2:   x in dom c and
A3:   c.x = y by FUNCT_1:def 5;
    reconsider x as Real by A2,Lm40;
A4: rng c c= rng ac by RELAT_1:99;
    y in [.0,PI.] by A1,A4,SIN_COS6:87;
    then reconsider y as Real;
A5: 0 <= y & y <= PI by A1,A4,SIN_COS6:87,RCOMP_1:48;
A6: -1 <= x & x < 1 by A2,Lm40,RCOMP_2:3;
    y = arccos.x by A2,A3,Lm40,FUNCT_1:72
     .= arccos x by SIN_COS6:def 4;
    then y <> 0 by A6,SIN_COS6:98;
    hence thesis by A5,RCOMP_2:4;
  end;
  then reconsider c as Function of R^1 | R^1(Q), R^1 | R^1(E)
    by Lm27,Lm28,Lm40,FUNCT_2:4;
  the carrier of R^1 | R^1[.-1,1.] = [.-1,1.] by PRE_TOPC:29;
  then reconsider QQ = R^1(Q) as Subset of R^1 | R^1[.-1,1.] by
TOPREALA:11;
  the carrier of R^1 | R^1[.0,PI.] = [.0,PI.] by PRE_TOPC:29;
  then reconsider EE = R^1(E) as Subset of R^1 | R^1[.0,PI.] by
TOPREALA:15;
  (R^1 | R^1[.-1,1.]) | QQ = R^1 | R^1(Q) &
    (R^1 | R^1[.0,PI.]) | EE = R^1 | R^1(E) by GOBOARD9:4;
  then
Lm42: c is continuous by TOPREALA:29;

reconsider p = proj1 as Function of TOP-REAL 2,R^1 by TOPMETR:24;
Lm43: dom p = the carrier of TOP-REAL 2 by FUNCT_2:def 1;
Lm44: p is continuous by TOPREAL6:83;

Lm45:
 for aX1 being Subset of TOUC st
  aX1 = {q where q is Point of TOP-REAL 2: q in X & 0 <= q`2} holds
  Circle2IntervalR | (TOUC|aX1) is continuous
proof
  let aX1 being Subset of TOUC such that
A1:  aX1 = {q where q is Point of TOP-REAL 2: q in X & 0 <= q`2};
  reconsider c1 = c[-10] as Point of TOP-REAL 2;
A2: c1`2 = 0 by EUCLID:56;
  c[-10] is Point of Topen_unit_circle(c[10]) by Lm16,Th24;
  then c[-10] in aX1 by A1,A2;
  then reconsider aX1 as non empty Subset of TOUC;
  set X1 = TOUC | aX1;
A3: aX1 = the carrier of X1 by PRE_TOPC:29;
A4: [#]X1 is Subset of TUC by Lm10;
  cS1 is Subset of TOP-REAL 2 by TSEP_1:1;
  then reconsider B = [#]X1 as non empty Subset of TOP-REAL 2 by A4,XBOOLE_1:1;
A5: B = aX1 by A3;
  X1 is SubSpace of TUC by TSEP_1:7;
  then X1 is SubSpace of TOP-REAL 2 by TSEP_1:7;
  then
A6: (TOP-REAL 2) | B = X1 by PRE_TOPC:def 10;
  set f = p|B;
A7: the carrier of (TOP-REAL 2) | B = B by PRE_TOPC:29;
A8: dom f = B by Lm43,RELAT_1:91;
  rng f c= Q
  proof
    let y be set;
    assume y in rng f;
    then consider x being set such that
A9:  x in dom f and
A10:  f.x = y by FUNCT_1:def 5;
    consider q being Point of TOP-REAL 2 such that
A11:  q = x and
A12:  q in X and 0 <= q`2 by A1,A5,A8,A9;
A13: y = p.x by A8,A9,A10,FUNCT_1:72
      .= q`1 by A11,PSCOMP_1:def 28;
    -1 <= q`1 & q`1 < 1 by A12,Th28;
    hence thesis by A13,RCOMP_2:3;
  end;
  then reconsider f as Function of (TOP-REAL 2) | B, R^1 | R^1(Q)
    by A7,A8,Lm27,FUNCT_2:4;
  f is continuous by Lm44,TOPREALA:29;
  then c*f is continuous by Lm42,TOPS_2:58;
  then
A14: Af*(c*f) is continuous by TOPS_2:58;
  for a being Point of X1 holds (C|X1).a = (Af*(c*f)).a
  proof
    let a be Point of X1;
    reconsider b = a as Point of TOUC by TOPMETR:2;
    consider x, y being real number such that
A15:   b = |[x,y]| and
A16:   y >= 0 implies C.b = arccos x/P2 and
      y <= 0 implies C.b = 1-arccos x/P2 by Def13;
    a in aX1 by A3;
    then
A17: ex q being Point of TOP-REAL 2 st a = q & q in X & 0 <= q`2 by A1;
A18: |[x,y]|`1 = x by EUCLID:56;
A19: -1 <= |[x,y]|`1 by A15,Th27;
    |[x,y]|`1 < 1 by A15,Th28;
    then
A20: x in Q by A18,A19,RCOMP_2:3;
    then arccos.x = c.x by FUNCT_1:72;
    then
A21: arccos.x in rng c by A20,Lm40,FUNCT_1:def 5;
    thus (C|X1).a = arccos x/P2 by A15,A16,A17,EUCLID:56,FUNCT_1:72
      .= arccos.x/P2 by SIN_COS6:def 4
      .= 1/P2 * (arccos.x) + 0 by XCMPLX_1:100
      .= AffineMap(j,0).(arccos.x) by JORDAN16:def 3
      .= Af.(arccos.x) by A21,Lm41,FUNCT_1:72
      .= Af.(c.x) by A20,FUNCT_1:72
      .= Af.(c.(|[x,y]|`1)) by EUCLID:56
      .= Af.(c.(p.a)) by A15,PSCOMP_1:def 28
      .= Af.(c.(f.a)) by FUNCT_1:72
      .= Af.((c*f).a) by A6,FUNCT_2:21
      .= (Af*(c*f)).a by A6,FUNCT_2:21;
  end;
  hence thesis by A6,A14,FUNCT_2:113;
end;

Lm46:
 for aX1 being Subset of TOUC st
  aX1 = {q where q is Point of TOP-REAL 2: q in X & 0 >= q`2} holds
  Circle2IntervalR | (TOUC|aX1) is continuous
proof
  let aX1 being Subset of TOUC such that
A1:  aX1 = {q where q is Point of TOP-REAL 2: q in X & 0 >= q`2};
  reconsider c1 = c[-10] as Point of TOP-REAL 2;
A2: c1`2 = 0 by EUCLID:56;
  c[-10] is Point of Topen_unit_circle(c[10]) by Lm16,Th24;
  then c[-10] in aX1 by A1,A2;
  then reconsider aX1 as non empty Subset of TOUC;
  set X1 = TOUC | aX1;
A3: aX1 = the carrier of X1 by PRE_TOPC:29;
A4: [#]X1 is Subset of TUC by Lm10;
  cS1 is Subset of TOP-REAL 2 by TSEP_1:1;
  then reconsider B = [#]X1 as non empty Subset of TOP-REAL 2 by A4,XBOOLE_1:1;
A5: B = aX1 by A3;
  X1 is SubSpace of TUC by TSEP_1:7;
  then X1 is SubSpace of TOP-REAL 2 by TSEP_1:7;
  then
A6: (TOP-REAL 2) | B = X1 by PRE_TOPC:def 10;
  set f = p|B;
A7: the carrier of (TOP-REAL 2) | B = B by PRE_TOPC:29;
A8: dom f = B by Lm43,RELAT_1:91;
  rng f c= Q
  proof
    let y be set;
    assume y in rng f;
    then consider x being set such that
A9:  x in dom f and
A10:  f.x = y by FUNCT_1:def 5;
    consider q being Point of TOP-REAL 2 such that
A11:  q = x and
A12:  q in X and 0 >= q`2 by A1,A5,A8,A9;
A13: y = p.x by A8,A9,A10,FUNCT_1:72
      .= q`1 by A11,PSCOMP_1:def 28;
    -1 <= q`1 & q`1 < 1 by A12,Th28;
    hence thesis by A13,RCOMP_2:3;
  end;
  then reconsider f as Function of (TOP-REAL 2) | B, R^1 | R^1(Q)
    by A7,A8,Lm27,FUNCT_2:4;
  f is continuous by Lm44,TOPREALA:29;
  then c*f is continuous by Lm42,TOPS_2:58;
  then Af*(c*f) is continuous by TOPS_2:58;
  then
A14: L*(Af*(c*f)) is continuous by TOPS_2:58;
  for a being Point of X1 holds (C|X1).a = (L*(Af*(c*f))).a
  proof
    let a be Point of X1;
    reconsider b = a as Point of TOUC by TOPMETR:2;
    consider x, y being real number such that
A15:   b = |[x,y]| and
      y >= 0 implies C.b = arccos x/P2 and
A16:   y <= 0 implies C.b = 1-arccos x/P2 by Def13;
    a in aX1 by A3;
    then
A17: ex q being Point of TOP-REAL 2 st a = q & q in X & 0 >= q`2 by A1;
A18: |[x,y]|`1 = x by EUCLID:56;
A19: -1 <= |[x,y]|`1 by A15,Th27;
    |[x,y]|`1 < 1 by A15,Th28;
    then
A20: x in Q by A18,A19,RCOMP_2:3;
    then arccos.x = c.x by FUNCT_1:72;
    then
A21: arccos.x in rng c by A20,Lm40,FUNCT_1:def 5;
A22: arccos x/P2 = arccos.x/P2 by SIN_COS6:def 4
      .= (j * (arccos.x) + 0) by XCMPLX_1:100
      .= AffineMap(j,0).(arccos.x) by JORDAN16:def 3
      .= Af.(arccos.x) by A21,Lm41,FUNCT_1:72
      .= Af.(c.x) by A20,FUNCT_1:72
      .= Af.(c.(|[x,y]|`1)) by EUCLID:56
      .= Af.(c.(p.a)) by A15,PSCOMP_1:def 28
      .= Af.(c.(f.a)) by FUNCT_1:72
      .= Af.((c*f).a) by A6,FUNCT_2:21
      .= (Af*(c*f)).a by A6,FUNCT_2:21;
    dom (Af*(c*f)) = the carrier of (TOP-REAL 2)|B by FUNCT_2:def 1;
    then
A23: (Af*(c*f)).a in rng (Af*(c*f)) by A7,FUNCT_1:def 5;
    then (Af*(c*f)).a in A by Lm29;
    then reconsider r = (Af*(c*f)).a as Real;
    thus (C|X1).a = (-1)*r+1 by  A15,A16,A17,EUCLID:56,FUNCT_1:72,A22
      .= (R^1(AffineMap(-1,1))).r by JORDAN16:def 3
      .= L.r by A23,Lm29,FUNCT_1:72
      .= (L*(Af*(c*f))).a by A6,FUNCT_2:21;
  end;
  hence thesis by A6,A14,FUNCT_2:113;
end;

Lm47:
 for p being Point of Topen_unit_circle(c[10]) st p = c[-10] holds
  Circle2IntervalR is_continuous_at p
proof
  let p be Point of Topen_unit_circle(c[10]) such that
A1: p = c[-10];
  set aX1 = {q where q is Point of TOP-REAL 2: q in X & 0 <= q`2};
  set aX2 = {q where q is Point of TOP-REAL 2: q in X & 0 >= q`2};
A2: aX1 c= X
  proof
    let x be set;
    assume x in aX1;
    then ex q being Point of TOP-REAL 2 st x = q & q in X & 0 <= q`2;
    hence thesis;
  end;
A3: aX2 c= X
  proof
    let x be set;
    assume x in aX2;
    then ex q being Point of TOP-REAL 2 st x = q & q in X & 0 >= q`2;
    hence thesis;
  end;
  reconsider c1 = c[-10] as Point of TOP-REAL 2;
A4: c1`2 = 0 by EUCLID:56;
  c[-10] is Point of Topen_unit_circle(c[10]) by Lm16,Th24;
  then
A5: c[-10] in aX1 & c[-10] in aX2 by A4;
  then reconsider aX1, aX2 as non empty Subset of TOUC by A2,A3;
  set X1 = TOUC | aX1;
  set X2 = TOUC | aX2;
A6: the carrier of X1 = aX1 by PRE_TOPC:29;
  reconsider x1 = p as Point of X1 by A1,A5,PRE_TOPC:29;
A7: the carrier of X2 = aX2 by PRE_TOPC:29;
  reconsider x2 = p as Point of X2 by A1,A5,PRE_TOPC:29;
A8: TOUC is SubSpace of TOUC by TSEP_1:2;
  X = aX1 \/ aX2
  proof
    thus X c= aX1 \/ aX2
    proof
      let a be set;
      assume
A9:     a in X;
      then reconsider a as Point of TOP-REAL 2 by Lm9;
      0 >= a`2 or 0 <= a`2;
      then a in aX1 or a in aX2 by A9;
      hence thesis by XBOOLE_0:def 2;
    end;
    thus thesis;
  end;
  then
A10: TOUC = X1 union X2 by A6,A7,A8,TSEP_1:def 2;
  C|X1 is continuous by Lm45;
  then
A11: C|X1 is_continuous_at x1 by TMAP_1:49;
  C|X2 is continuous by Lm46;
  then C|X2 is_continuous_at x2 by TMAP_1:49;
  hence thesis by A10,A11,TMAP_1:125;
end;

  set h1 = REAL --> 1;
Lm48: dom h1 = REAL by FUNCOP_1:19;
  rng h1 c= REAL by XBOOLE_1:1;
  then reconsider h1 as PartFunc of REAL,REAL by Lm48,RELSET_1:11;

Lm49:
Circle2IntervalR is continuous
proof
  set Z = R^1 | R^1(].0,0+p1.[);
  set I = ].0,p1.];
  set J = [.p0,0.[;
  set K = [.-1,1.];
  set h = j(#)arccos;
  for p being Point of TOUC holds C is_continuous_at p
  proof
    let p be Point of TOUC;
    consider x, y being real number such that
A1:   p = |[x,y]| and
A2:   y >= 0 implies C.p = arccos x/P2 and
A3:   y <= 0 implies C.p = 1-arccos x/P2 by Def13;
  reconsider q = p as Point of TOP-REAL 2 by Lm9;
  x = q`1 by A1,EUCLID:56;
  then -1 <= x & x <= 1 by Th27;
  then
A4: x in K by RCOMP_1:48;
  then
A5: (h|K).x = h.x by FUNCT_1:72;
A6: the carrier of Z = ].0,0+p1.[ by PRE_TOPC:29;
A7: dom h = dom arccos by SEQ_1:def 6;
  then
A8: h.x = arccos.x*j by A4,SEQ_1:def 6,SIN_COS6:88
    .= 1*arccos.x/P2 by XCMPLX_1:75;
A9: h is_continuous_on K by FCONT_1:21,SIN_COS6:109;
  then
A10: h|K is_continuous_in x by A4,FCONT_1:def 2;
  Tcircle(0.REAL 2,1) is SubSpace of TREC by Th10;
  then TUC is SubSpace of TREC;
  then
A11: TOUC is SubSpace of TREC by TSEP_1:7;
A12: K = [#]CIT by Lm2;
A13: y = q`2 by A1,EUCLID:56;
  per cases;
  suppose y = 0;
  then p = c[-10] by A13,Th25;
  hence thesis by Lm47;
  end;
  suppose
A14: y < 0;
  for V being Subset of Z st V is open & C.p in V
    ex W being Subset of TOUC st W is open & p in W & C.:W c= V
  proof
    let V be Subset of Z such that
A15:  V is open and
A16:  C.p in V;
A17: C.p = 1-arccos.x/P2 by A3,A14,SIN_COS6:def 4;
  reconsider V1 = V as Subset of REAL by A6,XBOOLE_1:1;
  reconsider V2 = V1 as Subset of R^1 by TOPMETR:24;
  V2 is open by A15,TSEP_1:17;
  then reconsider V1 as open Subset of REAL by BORSUK_5:62;
  consider N1 being Neighbourhood of C.p such that
A18: N1 c= V1 by A16,RCOMP_1:39;
  set hh = h1-h;
A19: (hh|K).x = hh.x by A4,FUNCT_1:72;
A20: dom hh = dom h1 /\ dom h by SEQ_1:def 4
        .= REAL /\ dom h by FUNCOP_1:19
        .= K by A7,SIN_COS6:88,XBOOLE_1:28;
  then
A21: hh.x = h1.x - h.x by A4,SEQ_1:def 4
    .= 1-1*arccos.x/P2 by A4,A8,FUNCOP_1:13;
  h1 is_continuous_on REAL by TOPREALA:38;
  then h1 is_continuous_on K by FCONT_1:17;
  then hh is_continuous_on K by A9,FCONT_1:19;
  then hh|K is_continuous_in x by A4,FCONT_1:def 2;
  then consider N being Neighbourhood of x such that
A22: (hh|K).:N c= N1 by A17,A19,A21,FCONT_1:5;
  set N3 = N /\ K;
A23: N3 c= K by XBOOLE_1:17;
  reconsider N3, J as Subset of CIT by Lm2,TOPREALA:11,XBOOLE_1:17;
  set W = product ((1,2) --> (N3,J)) /\ X;
  y >= -1 by A13,Th27;
  then
A24: y in J by A14,RCOMP_2:3;
A25: p = (1,2) --> (x,y) by A1,TOPREALA:49;
  reconsider W as Subset of TOUC by XBOOLE_1:17;
  take W;
A26: N3 c= N by XBOOLE_1:17;
  reconsider KK = product ((1,2) --> (N3,J)) as Subset of TREC by TOPREALA:60;
  R^1(N) = N;
  then reconsider M3 = N3 as open Subset of CIT by A12,TOPS_2:32;
  reconsider I3 = J as open Subset of CIT by TOPREALA:47;
  KK = product ((1,2) --> (M3,I3));
  then KK is open by TOPREALA:61;
  hence W is open by A11,Lm17,TOPS_2:32;
  x in N by RCOMP_1:37;
  then x in N3 by A4,XBOOLE_0:def 3;
  then p in product ((1,2) --> (N3,J)) by A24,A25,HILBERT3:11;
  hence p in W by XBOOLE_0:def 3;
  let m be set;
  assume m in C.:W;
  then consider c being Element of TOUC such that
A27: c in W and
A28: m = C.c by FUNCT_2:116;
  consider c1, c2 being real number such that
A29: c = |[c1,c2]| and
     c2 >= 0 implies C.c = arccos c1/P2 and
A30: c2 <= 0 implies C.c = 1-arccos c1/P2 by Def13;
A31: c in product ((1,2) --> (N3,J)) by A27,XBOOLE_0:def 3;
  then
A32: c.1 in N3 by TOPREALA:24;
A33: dom (hh|K) = K by A20,RELAT_1:91;
A34: (hh|K).:N3 c= (hh|K).:N by A26,RELAT_1:156;
A35: (hh|K).(c.1) = hh.(c.1) by A23,A32,FUNCT_1:72
    .= h1.(c.1) - h.(c.1) by A20,A23,A32,SEQ_1:def 4
    .= 1 - h.(c.1) by A32,FUNCOP_1:13
    .= 1-arccos.(c.1)*j by A7,A23,A32,SEQ_1:def 6,SIN_COS6:88
    .= 1-arccos.c1*j by A29,TOPREALA:50
    .= 1-arccos c1*j by SIN_COS6:def 4;
  c.2 in J by A31,TOPREALA:24;
  then c2 in J by A29,TOPREALA:50;
  then 1-1*arccos c1*j = m by A28,A30,RCOMP_2:3,XCMPLX_1:75;
  then m in (hh|K).:N3 by A23,A32,A33,A35,FUNCT_1:def 12;
  then m in (hh|K).:N by A34;
  then m in N1 by A22;
  hence m in V by A18;
  end;
  hence thesis by TMAP_1:48;
  end;
  suppose
A36:  y > 0;
  for V being Subset of Z st V is open & C.p in V
    ex W being Subset of TOUC st W is open & p in W & C.:W c= V
  proof
    let V be Subset of Z such that
A37:  V is open and
A38:  C.p in V;
A39: C.p = arccos.x/P2 by A2,A36,SIN_COS6:def 4;
  reconsider V1 = V as Subset of REAL by A6,XBOOLE_1:1;
  reconsider V2 = V1 as Subset of R^1 by TOPMETR:24;
  V2 is open by A37,TSEP_1:17;
  then reconsider V1 as open Subset of REAL by BORSUK_5:62;
  consider N1 being Neighbourhood of C.p such that
A40: N1 c= V1 by A38,RCOMP_1:39;
  consider N being Neighbourhood of x such that
A41: (h|K).:N c= N1 by A5,A8,A10,A39,FCONT_1:5;
  set N3 = N /\ K;
A42: N3 c= K by XBOOLE_1:17;
  reconsider N3, I as Subset of CIT by Lm2,TOPREALA:15,XBOOLE_1:17;
  set W = product ((1,2) --> (N3,I)) /\ X;
  y <= 1 by A13,Th27;
  then
A43: y in I by A36,RCOMP_2:4;
A44: p = (1,2) --> (x,y) by A1,TOPREALA:49;
  reconsider W as Subset of TOUC by XBOOLE_1:17;
  take W;
A45: N3 c= N by XBOOLE_1:17;
  reconsider KK = product ((1,2) --> (N3,I)) as Subset of TREC by TOPREALA:60;
  R^1(N) = N;
  then reconsider M3 = N3 as open Subset of CIT by A12,TOPS_2:32;
  reconsider I3 = I as open Subset of CIT by TOPREALA:46;
  KK = product ((1,2) --> (M3,I3));
  then KK is open by TOPREALA:61;
  hence W is open by A11,Lm17,TOPS_2:32;
  x in N by RCOMP_1:37;
  then x in N3 by A4,XBOOLE_0:def 3;
  then p in product ((1,2) --> (N3,I)) by A43,A44,HILBERT3:11;
  hence p in W by XBOOLE_0:def 3;
  let m be set;
  assume m in C.:W;
  then consider c being Element of TOUC such that
A46: c in W and
A47: m = C.c by FUNCT_2:116;
  consider c1, c2 being real number such that
A48: c = |[c1,c2]| and
A49: c2 >= 0 implies C.c = arccos c1/P2 and
     c2 <= 0 implies C.c = 1-arccos c1/P2 by Def13;
A50: c in product ((1,2) --> (N3,I)) by A46,XBOOLE_0:def 3;
  then
A51: c.1 in N3 by TOPREALA:24;
A52: dom (h|K) = K by A7,RELAT_1:91,SIN_COS6:88;
A53: (h|K).:N3 c= (h|K).:N by A45,RELAT_1:156;
A54: (h|K).(c.1) = h.(c.1) by A42,A51,FUNCT_1:72
    .= arccos.(c.1)*j by A7,A42,A51,SEQ_1:def 6,SIN_COS6:88
    .= arccos.c1*j by A48,TOPREALA:50
    .= arccos c1*j by SIN_COS6:def 4;
  c.2 in I by A50,TOPREALA:24;
  then c2 in I by A48,TOPREALA:50;
  then 1*arccos c1*j = m by A47,A49,RCOMP_2:4,XCMPLX_1:75;
  then m in (h|K).:N3 by A42,A51,A52,A54,FUNCT_1:def 12;
  then m in (h|K).:N by A53;
  then m in N1 by A41;
  hence m in V by A40;
  end;
  hence thesis by TMAP_1:48;
  end;
  end;
  hence thesis by TMAP_1:49;
end;

set A = ].1/2,1/2+p1.[;
set Q = ].-1,1.];
set E = [.0,PI.[;
reconsider Q, E as non empty Subset of REAL;
Lm50: the carrier of R^1 | R^1(Q) = R^1(Q) by PRE_TOPC:29;
Lm51: the carrier of R^1 | R^1(E) = R^1(E) by PRE_TOPC:29;
Lm52: the carrier of R^1 | R^1(A) = R^1(A) by PRE_TOPC:29;
set Af = AffineMap(-j,1) | R^1(E);
  dom AffineMap(-j,1) = R by FUNCT_2:def 1,TOPMETR:24;
  then
Lm53: dom Af = R^1(E) by RELAT_1:91;
  rng Af c= A
  proof
    let y be set;
    assume y in rng Af;
    then consider x being set such that
A1:   x in dom Af and
A2:   Af.x = y by FUNCT_1:def 5;
    reconsider x as Real by A1;
A3: y = AffineMap(-j,1).x by A1,A2,Lm53,FUNCT_1:72
     .= (-j)*x+1 by JORDAN16:def 3
     .= -j*x+1
     .= -x/P2+1 by XCMPLX_1:100;
    then reconsider y as Real;
    0 <= x & x < PI by A1,Lm53,RCOMP_2:3;
    then 0/P2 <= x/P2 & x/P2 < (1*PI)/P2 by REAL_1:73;
    then 0 <= x/P2 & x/P2 < 1/2 by XCMPLX_1:92;
    then -0 >= -x/P2 & -x/P2 > -1/2 by XREAL_1:26;
    then 0+1 >= -x/P2+1 & -x/P2+1 > -1/2+1 by XREAL_1:8;
    then 1/2 < y & y < 3/2 by A3,XXREAL_0:2;
    hence thesis by RCOMP_1:47;
  end;
then reconsider Af as Function of R^1 | R^1(E), R^1 | R^1(A)
  by Lm51,Lm52,Lm53,FUNCT_2:4;
Lm54: R^1(AffineMap(-j,1)) = AffineMap(-j,1);
Lm55: dom AffineMap(-j,1) = REAL by FUNCT_2:def 1;
rng AffineMap(-j,1) = REAL by JORDAN16:32;
then R^1 = R^1|R^1(dom AffineMap(-j,1)) & R^1 = R^1|R^1(rng AffineMap(-j,1))
 by Lm38,Lm39,Lm55;
then reconsider Af as continuous Function of R^1 | R^1(E), R^1 | R^1(A)
by Lm54,TOPREALA:29;

set Af1 = AffineMap(j,1) | R^1(E);
  dom AffineMap(j,1) = R by FUNCT_2:def 1,TOPMETR:24;
  then
Lm56: dom Af1 = R^1(E) by RELAT_1:91;
  rng Af1 c= A
  proof
    let y be set;
    assume y in rng Af1;
    then consider x being set such that
A1:   x in dom Af1 and
A2:   Af1.x = y by FUNCT_1:def 5;
    reconsider x as Real by A1;
A3: y = AffineMap(j,1).x by A1,A2,Lm56,FUNCT_1:72
     .= j*x+1 by JORDAN16:def 3
     .= x/P2+1 by XCMPLX_1:100;
    then reconsider y as Real;
    0 <= x & x < PI by A1,Lm56,RCOMP_2:3;
    then 0/P2 <= x/P2 & x/P2 < (1*PI)/P2 by REAL_1:73;
    then 0 <= x/P2 & x/P2 < 1/2 by XCMPLX_1:92;
    then 0+1 <= x/P2+1 & x/P2+1 < 1/2+1 by XREAL_1:8;
    then 1/2 < y & y < 3/2 by A3,XXREAL_0:2;
    hence thesis by RCOMP_1:47;
  end;
then reconsider Af1 as Function of R^1 | R^1(E), R^1 | R^1(A)
  by Lm51,Lm52,Lm56,FUNCT_2:4;
Lm57: R^1(AffineMap(j,1)) = AffineMap(j,1);
Lm58: dom AffineMap(j,1) = REAL by FUNCT_2:def 1;
rng AffineMap(j,1) = REAL by JORDAN16:32;
then R^1 = R^1|R^1(dom AffineMap(j,1)) & R^1 = R^1|R^1(rng AffineMap(j,1))
 by Lm38,Lm39,Lm58;
then
reconsider Af1 as continuous Function of R^1 | R^1(E), R^1 | R^1(A)
by Lm57,TOPREALA:29;

  set c = ac | R^1(Q);
  Q c= [.-1,1.] by TOPREALA:15;
  then
Lm59: dom c = Q by RELAT_1:91,SIN_COS6:88;
Lm60: rng c c= E
  proof
    let y be set;
    assume
A1:   y in rng c;
    then consider x being set such that
A2:   x in dom c and
A3:   c.x = y by FUNCT_1:def 5;
    reconsider x as Real by A2,Lm59;
A4: rng c c= rng ac by RELAT_1:99;
    y in [.0,PI.] by A1,A4,SIN_COS6:87;
    then reconsider y as Real;
A5: 0 <= y & y <= PI by A1,A4,SIN_COS6:87,RCOMP_1:48;
A6: -1 < x & x <= 1 by A2,Lm59,RCOMP_2:4;
    y = arccos.x by A2,A3,Lm59,FUNCT_1:72
     .= arccos x by SIN_COS6:def 4;
    then y <> PI by A6,SIN_COS6:100;
    then y < PI by A5,REAL_1:def 5;
    hence thesis by A5,RCOMP_2:3;
  end;
  then reconsider c as Function of R^1 | R^1(Q), R^1 | R^1(E)
    by Lm50,Lm51,Lm59,FUNCT_2:4;
  the carrier of R^1 | R^1[.-1,1.] = [.-1,1.] by PRE_TOPC:29;
  then reconsider QQ = R^1(Q) as Subset of R^1 | R^1[.-1,1.] by
TOPREALA:15;
  the carrier of R^1 | R^1[.0,PI.] = [.0,PI.] by PRE_TOPC:29;
  then reconsider EE = R^1(E) as Subset of R^1 | R^1[.0,PI.] by
TOPREALA:11;
  (R^1 | R^1[.-1,1.]) | QQ = R^1 | R^1(Q) &
    (R^1 | R^1[.0,PI.]) | EE = R^1 | R^1(E) by GOBOARD9:4;
  then
Lm61: c is continuous by TOPREALA:29;

Lm62:
 for aX1 being Subset of TOUCm st
  aX1 = {q where q is Point of TOP-REAL 2: q in Xm & 0 <= q`2} holds
  Circle2IntervalL | (TOUCm|aX1) is continuous
proof
  let aX1 being Subset of TOUCm such that
A1:  aX1 = {q where q is Point of TOP-REAL 2: q in Xm & 0 <= q`2};
  reconsider c1 = c[10] as Point of TOP-REAL 2;
A2: c1`2 = 0 by EUCLID:56;
  c[10] is Point of Topen_unit_circle(c[-10]) by Lm16,Th24;
  then c[10] in aX1 by A1,A2;
  then reconsider aX1 as non empty Subset of TOUCm;
  set X1 = TOUCm | aX1;
A3: aX1 = the carrier of X1 by PRE_TOPC:29;
A4: [#]X1 is Subset of TUC by Lm10;
  cS1 is Subset of TOP-REAL 2 by TSEP_1:1;
  then reconsider B = [#]X1 as non empty Subset of TOP-REAL 2 by A4,XBOOLE_1:1;
A5: B = aX1 by A3;
  X1 is SubSpace of TUC by TSEP_1:7;
  then X1 is SubSpace of TOP-REAL 2 by TSEP_1:7;
  then
A6: (TOP-REAL 2) | B = X1 by PRE_TOPC:def 10;
  set f = p|B;
A7: the carrier of (TOP-REAL 2) | B = B by PRE_TOPC:29;
A8: dom f = B by Lm43,RELAT_1:91;
  rng f c= Q
  proof
    let y be set;
    assume y in rng f;
    then consider x being set such that
A9:  x in dom f and
A10:  f.x = y by FUNCT_1:def 5;
    consider q being Point of TOP-REAL 2 such that
A11:  q = x and
A12:  q in Xm and 0 <= q`2 by A1,A5,A8,A9;
A13: y = p.x by A8,A9,A10,FUNCT_1:72
      .= q`1 by A11,PSCOMP_1:def 28;
    -1 < q`1 & q`1 <= 1 by A12,Th29;
    hence thesis by A13,RCOMP_2:4;
  end;
  then reconsider f as Function of (TOP-REAL 2) | B, R^1 | R^1(Q)
    by A7,A8,Lm50,FUNCT_2:4;
  f is continuous by Lm44,TOPREALA:29;
  then c*f is continuous by Lm61,TOPS_2:58;
  then
A14: Af1*(c*f) is continuous by TOPS_2:58;
  for a being Point of X1 holds (Cm|X1).a = (Af1*(c*f)).a
  proof
    let a be Point of X1;
    reconsider b = a as Point of TOUCm by TOPMETR:2;
    consider x, y being real number such that
A15:   b = |[x,y]| and
A16:   y >= 0 implies Cm.b = 1+arccos x/P2 and
      y <= 0 implies Cm.b = 1-arccos x/P2 by Def14;
    a in aX1 by A3;
    then
A17: ex q being Point of TOP-REAL 2 st a = q & q in Xm & 0 <= q`2 by A1;
A18: |[x,y]|`1 = x by EUCLID:56;
A19: -1 < |[x,y]|`1 by A15,Th29;
    |[x,y]|`1 <= 1 by A15,Th29;
    then
A20: x in Q by A18,A19,RCOMP_2:4;
    then arccos.x = c.x by FUNCT_1:72;
    then
A21: arccos.x in rng c by A20,Lm59,FUNCT_1:def 5;
    thus (Cm|X1).a = 1+arccos x/P2 by A15,A16,A17,EUCLID:56,FUNCT_1:72
      .= 1+arccos.x/P2 by SIN_COS6:def 4
      .= 1+j*(arccos.x) by XCMPLX_1:100
      .= AffineMap(j,1).(arccos.x) by JORDAN16:def 3
      .= Af1.(arccos.x) by A21,Lm60,FUNCT_1:72
      .= Af1.(c.x) by A20,FUNCT_1:72
      .= Af1.(c.(|[x,y]|`1)) by EUCLID:56
      .= Af1.(c.(p.a)) by A15,PSCOMP_1:def 28
      .= Af1.(c.(f.a)) by FUNCT_1:72
      .= Af1.((c*f).a) by A6,FUNCT_2:21
      .= (Af1*(c*f)).a by A6,FUNCT_2:21;
  end;
  hence thesis by A6,A14,FUNCT_2:113;
end;

Lm63:
 for aX1 being Subset of TOUCm st
  aX1 = {q where q is Point of TOP-REAL 2: q in Xm & 0 >= q`2} holds
  Circle2IntervalL | (TOUCm|aX1) is continuous
proof
  let aX1 being Subset of TOUCm such that
A1:  aX1 = {q where q is Point of TOP-REAL 2: q in Xm & 0 >= q`2};
  reconsider c1 = c[10] as Point of TOP-REAL 2;
A2: c1`2 = 0 by EUCLID:56;
  c[10] is Point of Topen_unit_circle(c[-10]) by Lm16,Th24;
  then c[10] in aX1 by A1,A2;
  then reconsider aX1 as non empty Subset of TOUCm;
  set X1 = TOUCm | aX1;
A3: aX1 = the carrier of X1 by PRE_TOPC:29;
A4: [#]X1 is Subset of TUC by Lm10;
  cS1 is Subset of TOP-REAL 2 by TSEP_1:1;
  then reconsider B = [#]X1 as non empty Subset of TOP-REAL 2 by A4,XBOOLE_1:1;
A5: B = aX1 by A3;
  X1 is SubSpace of TUC by TSEP_1:7;
  then X1 is SubSpace of TOP-REAL 2 by TSEP_1:7;
  then
A6: (TOP-REAL 2) | B = X1 by PRE_TOPC:def 10;
  set f = p|B;
A7: the carrier of (TOP-REAL 2) | B = B by PRE_TOPC:29;
A8: dom f = B by Lm43,RELAT_1:91;
  rng f c= Q
  proof
    let y be set;
    assume y in rng f;
    then consider x being set such that
A9:  x in dom f and
A10:  f.x = y by FUNCT_1:def 5;
    consider q being Point of TOP-REAL 2 such that
A11:  q = x and
A12:  q in Xm and 0 >= q`2 by A1,A5,A8,A9;
A13: y = p.x by A8,A9,A10,FUNCT_1:72
      .= q`1 by A11,PSCOMP_1:def 28;
    -1 < q`1 & q`1 <= 1 by A12,Th29;
    hence thesis by A13,RCOMP_2:4;
  end;
  then reconsider f as Function of (TOP-REAL 2) | B, R^1 | R^1(Q)
    by A7,A8,Lm50,FUNCT_2:4;
  f is continuous by Lm44,TOPREALA:29;
  then c*f is continuous by Lm61,TOPS_2:58;
  then
A14: Af*(c*f) is continuous by TOPS_2:58;
  for a being Point of X1 holds (Cm|X1).a = (Af*(c*f)).a
  proof
    let a be Point of X1;
    reconsider b = a as Point of TOUCm by TOPMETR:2;
    consider x, y being real number such that
A15:   b = |[x,y]| and
      y >= 0 implies Cm.b = 1+arccos x/P2 and
A16:   y <= 0 implies Cm.b = 1-arccos x/P2 by Def14;
    a in aX1 by A3;
    then
A17: ex q being Point of TOP-REAL 2 st a = q & q in Xm & 0 >= q`2 by A1;
A18: |[x,y]|`1 = x by EUCLID:56;
A19: -1 < |[x,y]|`1 by A15,Th29;
    |[x,y]|`1 <= 1 by A15,Th29;
    then
A20: x in Q by A18,A19,RCOMP_2:4;
    then arccos.x = c.x by FUNCT_1:72;
    then
A21: arccos.x in rng c by A20,Lm59,FUNCT_1:def 5;
    thus (Cm|X1).a = 1-arccos x/P2 by A15,A16,A17,EUCLID:56,FUNCT_1:72
      .= 1-arccos.x/P2 by SIN_COS6:def 4
      .= 1-j*(arccos.x) by XCMPLX_1:100
      .= (-j)*(arccos.x)+1
      .= AffineMap(-j,1).(arccos.x) by JORDAN16:def 3
      .= Af.(arccos.x) by A21,Lm60,FUNCT_1:72
      .= Af.(c.x) by A20,FUNCT_1:72
      .= Af.(c.(|[x,y]|`1)) by EUCLID:56
      .= Af.(c.(p.a)) by A15,PSCOMP_1:def 28
      .= Af.(c.(f.a)) by FUNCT_1:72
      .= Af.((c*f).a) by A6,FUNCT_2:21
      .= (Af*(c*f)).a by A6,FUNCT_2:21;
  end;
  hence thesis by A6,A14,FUNCT_2:113;
end;

Lm64:
 for p being Point of Topen_unit_circle(c[-10]) st p = c[10] holds
  Circle2IntervalL is_continuous_at p
proof
  let p be Point of Topen_unit_circle(c[-10]) such that
A1: p = c[10];
  set aX1 = {q where q is Point of TOP-REAL 2: q in Xm & 0 <= q`2};
  set aX2 = {q where q is Point of TOP-REAL 2: q in Xm & 0 >= q`2};
A2: aX1 c= Xm
  proof
    let x be set;
    assume x in aX1;
    then ex q being Point of TOP-REAL 2 st x = q & q in Xm & 0 <= q`2;
    hence thesis;
  end;
A3: aX2 c= Xm
  proof
    let x be set;
    assume x in aX2;
    then ex q being Point of TOP-REAL 2 st x = q & q in Xm & 0 >= q`2;
    hence thesis;
  end;
  reconsider c1 = c[10] as Point of TOP-REAL 2;
A4: c1`2 = 0 by EUCLID:56;
  c[10] is Point of Topen_unit_circle(c[-10]) by Lm16,Th24;
  then
A5: c[10] in aX1 & c[10] in aX2 by A4;
  then reconsider aX1, aX2 as non empty Subset of TOUCm by A2,A3;
  set X1 = TOUCm | aX1;
  set X2 = TOUCm | aX2;
A6: the carrier of X1 = aX1 by PRE_TOPC:29;
  reconsider x1 = p as Point of X1 by A1,A5,PRE_TOPC:29;
A7: the carrier of X2 = aX2 by PRE_TOPC:29;
  reconsider x2 = p as Point of X2 by A1,A5,PRE_TOPC:29;
A8: TOUCm is SubSpace of TOUCm by TSEP_1:2;
  Xm = aX1 \/ aX2
  proof
    thus Xm c= aX1 \/ aX2
    proof
      let a be set;
      assume
A9:     a in Xm;
      then reconsider a as Point of TOP-REAL 2 by Lm9;
      0 >= a`2 or 0 <= a`2;
      then a in aX1 or a in aX2 by A9;
      hence thesis by XBOOLE_0:def 2;
    end;
    thus thesis;
  end;
  then
A10: TOUCm = X1 union X2 by A6,A7,A8,TSEP_1:def 2;
  Cm|X1 is continuous by Lm62;
  then
A11: Cm|X1 is_continuous_at x1 by TMAP_1:49;
  Cm|X2 is continuous by Lm63;
  then Cm|X2 is_continuous_at x2 by TMAP_1:49;
  hence thesis by A10,A11,TMAP_1:125;
end;

Lm65: Circle2IntervalL is continuous
proof
  set Z = R^1 | R^1(].1/2,1/2+p1.[);
  set I = ].0,p1.];
  set J = [.p0,0.[;
  set K = [.-1,1.];
  set h = j(#)arccos;
  for p being Point of TOUCm holds Cm is_continuous_at p
  proof
    let p be Point of TOUCm;
    consider x, y being real number such that
A1:   p = |[x,y]| and
A2:   y >= 0 implies Cm.p = 1+arccos x/P2 and
A3:   y <= 0 implies Cm.p = 1-arccos x/P2 by Def14;
  reconsider q = p as Point of TOP-REAL 2 by Lm9;
  x = q`1 by A1,EUCLID:56;
  then -1 <= x & x <= 1 by Th27;
  then
A4: x in K by RCOMP_1:48;
A5: the carrier of Z = ].1/2,1/2+p1.[ by PRE_TOPC:29;
A6: dom h = dom arccos by SEQ_1:def 6;
  then
A7: h.x = arccos.x*j by A4,SEQ_1:def 6,SIN_COS6:88
    .= 1*arccos.x/P2 by XCMPLX_1:75;
A8: h is_continuous_on K by FCONT_1:21,SIN_COS6:109;
  Tcircle(0.REAL 2,1) is SubSpace of TREC by Th10;
  then TUC is SubSpace of TREC;
  then
A9: TOUCm is SubSpace of TREC by TSEP_1:7;
A10: K = [#]CIT by Lm2;
A11: y = q`2 by A1,EUCLID:56;
  per cases;
  suppose y = 0;
  then p = c[10] by A11,Th26;
  hence thesis by Lm64;
  end;
  suppose
A12: y < 0;
  for V being Subset of Z st V is open & Cm.p in V
    ex W being Subset of TOUCm st W is open & p in W & Cm.:W c= V
  proof
    let V be Subset of Z such that
A13:  V is open and
A14:  Cm.p in V;
A15: Cm.p = 1-arccos.x/P2 by A3,A12,SIN_COS6:def 4;
  reconsider V1 = V as Subset of REAL by A5,XBOOLE_1:1;
  reconsider V2 = V1 as Subset of R^1 by TOPMETR:24;
  V2 is open by A13,TSEP_1:17;
  then reconsider V1 as open Subset of REAL by BORSUK_5:62;
  consider N1 being Neighbourhood of Cm.p such that
A16: N1 c= V1 by A14,RCOMP_1:39;
  set hh = h1-h;
A17: (hh|K).x = hh.x by A4,FUNCT_1:72;
A18: dom hh = dom h1 /\ dom h by SEQ_1:def 4
        .= REAL /\ dom h by FUNCOP_1:19
        .= K by A6,SIN_COS6:88,XBOOLE_1:28;
  then
A19: hh.x = h1.x - h.x by A4,SEQ_1:def 4
    .= 1-1*arccos.x/P2 by A4,A7,FUNCOP_1:13;
  h1 is_continuous_on REAL by TOPREALA:38;
  then h1 is_continuous_on K by FCONT_1:17;
  then hh is_continuous_on K by A8,FCONT_1:19;
  then hh|K is_continuous_in x by A4,FCONT_1:def 2;
  then consider N being Neighbourhood of x such that
A20: (hh|K).:N c= N1 by A15,A17,A19,FCONT_1:5;
  set N3 = N /\ K;
A21: N3 c= K by XBOOLE_1:17;
  reconsider N3, J as Subset of CIT by Lm2,TOPREALA:11,XBOOLE_1:17;
  set W = product ((1,2) --> (N3,J)) /\ Xm;
  y >= -1 by A11,Th27;
  then
A22: y in J by A12,RCOMP_2:3;
A23: p = (1,2) --> (x,y) by A1,TOPREALA:49;
  reconsider W as Subset of TOUCm by XBOOLE_1:17;
  take W;
A24: N3 c= N by XBOOLE_1:17;
  reconsider KK = product ((1,2) --> (N3,J)) as Subset of TREC by TOPREALA:60;
  R^1(N) = N;
  then reconsider M3 = N3 as open Subset of CIT by A10,TOPS_2:32;
  reconsider I3 = J as open Subset of CIT by TOPREALA:47;
  KK = product ((1,2) --> (M3,I3));
  then KK is open by TOPREALA:61;
  hence W is open by A9,Lm18,TOPS_2:32;
  x in N by RCOMP_1:37;
  then x in N3 by A4,XBOOLE_0:def 3;
  then p in product ((1,2) --> (N3,J)) by A22,A23,HILBERT3:11;
  hence p in W by XBOOLE_0:def 3;
  let m be set;
  assume m in Cm.:W;
  then consider c being Element of TOUCm such that
A25: c in W and
A26: m = Cm.c by FUNCT_2:116;
  consider c1, c2 being real number such that
A27: c = |[c1,c2]| and
     c2 >= 0 implies Cm.c = 1+arccos c1/P2 and
A28: c2 <= 0 implies Cm.c = 1-arccos c1/P2 by Def14;
A29: c in product ((1,2) --> (N3,J)) by A25,XBOOLE_0:def 3;
  then
A30: c.1 in N3 by TOPREALA:24;
A31: dom (hh|K) = K by A18,RELAT_1:91;
A32: (hh|K).:N3 c= (hh|K).:N by A24,RELAT_1:156;
A33: (hh|K).(c.1) = hh.(c.1) by A21,A30,FUNCT_1:72
    .= h1.(c.1) - h.(c.1) by A18,A21,A30,SEQ_1:def 4
    .= 1 - h.(c.1) by A30,FUNCOP_1:13
    .= 1-arccos.(c.1)*j by A6,A21,A30,SEQ_1:def 6,SIN_COS6:88
    .= 1-arccos.c1*j by A27,TOPREALA:50
    .= 1-arccos c1*j by SIN_COS6:def 4;
  c.2 in J by A29,TOPREALA:24;
  then c2 in J by A27,TOPREALA:50;
  then 1-1*arccos c1*j = m by A26,A28,RCOMP_2:3,XCMPLX_1:75;
  then m in (hh|K).:N3 by A21,A30,A31,A33,FUNCT_1:def 12;
  then m in (hh|K).:N by A32;
  then m in N1 by A20;
  hence m in V by A16;
  end;
  hence thesis by TMAP_1:48;
  end;
  suppose
A34: y > 0;
  for V being Subset of Z st V is open & Cm.p in V
    ex W being Subset of TOUCm st W is open & p in W & Cm.:W c= V
  proof
    let V be Subset of Z such that
A35:  V is open and
A36:  Cm.p in V;
A37: Cm.p = 1+arccos.x/P2 by A2,A34,SIN_COS6:def 4;
  reconsider V1 = V as Subset of REAL by A5,XBOOLE_1:1;
  reconsider V2 = V1 as Subset of R^1 by TOPMETR:24;
  V2 is open by A35,TSEP_1:17;
  then reconsider V1 as open Subset of REAL by BORSUK_5:62;
  consider N1 being Neighbourhood of Cm.p such that
A38: N1 c= V1 by A36,RCOMP_1:39;
  set hh = h1+h;
A39: (hh|K).x = hh.x by A4,FUNCT_1:72;
A40: dom hh = dom h1 /\ dom h by SEQ_1:def 3
        .= REAL /\ dom h by FUNCOP_1:19
        .= K by A6,SIN_COS6:88,XBOOLE_1:28;
  then
A41: hh.x = h1.x + h.x by A4,SEQ_1:def 3
    .= 1+1*arccos.x/P2 by A4,A7,FUNCOP_1:13;
  h1 is_continuous_on REAL by TOPREALA:38;
  then h1 is_continuous_on K by FCONT_1:17;
  then hh is_continuous_on K by A8,FCONT_1:19;
  then hh|K is_continuous_in x by A4,FCONT_1:def 2;
  then consider N being Neighbourhood of x such that
A42: (hh|K).:N c= N1 by A37,A39,A41,FCONT_1:5;
  set N3 = N /\ K;
A43: N3 c= K by XBOOLE_1:17;
  reconsider N3, I as Subset of CIT by Lm2,TOPREALA:15,XBOOLE_1:17;
  set W = product ((1,2) --> (N3,I)) /\ Xm;
  y <= 1 by A11,Th27;
  then
A44: y in I by A34,RCOMP_2:4;
A45: p = (1,2) --> (x,y) by A1,TOPREALA:49;
  reconsider W as Subset of TOUCm by XBOOLE_1:17;
  take W;
A46: N3 c= N by XBOOLE_1:17;
  reconsider KK = product ((1,2) --> (N3,I)) as Subset of TREC by TOPREALA:60;
  R^1(N) = N;
  then reconsider M3 = N3 as open Subset of CIT by A10,TOPS_2:32;
  reconsider I3 = I as open Subset of CIT by TOPREALA:46;
  KK = product ((1,2) --> (M3,I3));
  then KK is open by TOPREALA:61;
  hence W is open by A9,Lm18,TOPS_2:32;
  x in N by RCOMP_1:37;
  then x in N3 by A4,XBOOLE_0:def 3;
  then p in product ((1,2) --> (N3,I)) by A44,A45,HILBERT3:11;
  hence p in W by XBOOLE_0:def 3;
  let m be set;
  assume m in Cm.:W;
  then consider c being Element of TOUCm such that
A47: c in W and
A48: m = Cm.c by FUNCT_2:116;
  consider c1, c2 being real number such that
A49: c = |[c1,c2]| and
A50: c2 >= 0 implies Cm.c = 1+arccos c1/P2 and
     c2 <= 0 implies Cm.c = 1-arccos c1/P2 by Def14;
A51: c in product ((1,2) --> (N3,I)) by A47,XBOOLE_0:def 3;
  then
A52: c.1 in N3 by TOPREALA:24;
A53: dom (hh|K) = K by A40,RELAT_1:91;
A54: (hh|K).:N3 c= (hh|K).:N by A46,RELAT_1:156;
A55: (hh|K).(c.1) = hh.(c.1) by A43,A52,FUNCT_1:72
    .= h1.(c.1) + h.(c.1) by A40,A43,A52,SEQ_1:def 3
    .= 1 + h.(c.1) by A52,FUNCOP_1:13
    .= 1+arccos.(c.1)*j by A6,A43,A52,SEQ_1:def 6,SIN_COS6:88
    .= 1+arccos.c1*j by A49,TOPREALA:50
    .= 1+arccos c1*j by SIN_COS6:def 4;
  c.2 in I by A51,TOPREALA:24;
  then c2 in I by A49,TOPREALA:50;
  then 1+1*arccos c1*j = m by A48,A50,RCOMP_2:4,XCMPLX_1:75;
  then m in (hh|K).:N3 by A43,A52,A53,A55,FUNCT_1:def 12;
  then m in (hh|K).:N by A54;
  then m in N1 by A42;
  hence m in V by A38;
  end;
  hence thesis by TMAP_1:48;
  end;
  end;
  hence thesis by TMAP_1:49;
end;

registration
  cluster Circle2IntervalR -> one-to-one onto continuous;
coherence
proof
  CircleMap.R^1(0) = c[10] by Th33;
  hence C is one-to-one onto by Th43,GRCAT_1:56;
  thus thesis by Lm49;
end;
  cluster Circle2IntervalL -> one-to-one onto continuous;
coherence by Lm65,Lm20,Th44,GRCAT_1:56;
end;

Lm66:
CircleMap(R^1(0)) is open
proof
  CircleMap.R^1(0) = c[10] by Th33;
  hence thesis by Th43,TOPREALA:35;
end;

Lm67:
CircleMap(R^1(1/2)) is open by Lm20,Th44,TOPREALA:35;

registration
  let i be Integer;
  cluster CircleMap(R^1(i)) -> open;
coherence
proof
  set F = AffineMap(1,-i);
  set f = F | ].0+i,0+i+p1.[;
A1: the carrier of R^1|R^1(].0,1.[) = R^1(].0,1.[) by PRE_TOPC:29;
A2: the carrier of R^1|R^1(].i,i+1.[) = R^1(].i,i+1.[) by PRE_TOPC:29;
  dom F = REAL by FUNCT_2:def 1;
  then
A3: dom f = ].i,i+1.[ by RELAT_1:91;
A4: rng f = ].0,0+1.[ by Lm26;
  then reconsider f as Function of R^1|R^1(].i,i+p1.[), R^1|R^1(].0,0+p1.[)
    by A1,A2,A3,FUNCT_2:4;
A5: CircleMap(R^1(0+i)) = CircleMap(R^1(0)) * f by Th42;
A6: CircleMap.R^1(i) = c[10] by Th33
     .= CircleMap.R^1(0) by Th33;
A7: R^1 | R^1(dom F) = R^1 & R^1 | R^1(rng F) = R^1 by Lm13;
A8: R^1(F) = F;
  f is onto by A1,A4,FUNCT_2:def 3;
  then f is open by A7,A8,TOPREALA:31;
  hence thesis by A5,A6,Lm66,TOPREALA:32;
end;
  cluster CircleMap(R^1(1/2+i)) -> open;
coherence
proof
  set F = AffineMap(1,-i);
  set f = F | ].1/2+i,1/2+i+p1.[;
A9: the carrier of R^1|R^1(].1/2,3/2.[) = R^1(].1/2,3/2.[) by PRE_TOPC:29;
A10: the carrier of R^1|R^1(].1/2+i,1/2+i+1.[) = R^1(].1/2+i,1/2+i+1.[)
   by PRE_TOPC:29;
  dom F = REAL by FUNCT_2:def 1;
  then
A11: dom f = ].1/2+i,1/2+i+1.[ by RELAT_1:91;
A12: rng f = ].1/2,1/2+1.[ by Lm26;
  then reconsider f as
    Function of R^1|R^1(].1/2+i,1/2+i+p1.[), R^1|R^1(].1/2,1/2+p1.[)
      by A9,A10,A11,FUNCT_2:4;
A13: CircleMap(R^1(1/2+i)) = CircleMap(R^1(1/2)) * f by Th42;
  0 <= 1/2 & 1/2-1 < 0;
  then [\1/2/] = 0 by INT_1:def 4;
  then
A14: 1/2-[\1/2/] = 1/2;
  frac(1/2+i) = frac(1/2) by TOPREALA:1
    .= 1/2 by A14,INT_1:def 6;
  then
A15: CircleMap.R^1(1/2+i) = CircleMap.R^1(1/2+0) by  Th35,Lm20;
A16: R^1 | R^1(dom F) = R^1 & R^1 | R^1(rng F) = R^1 by Lm13;
A17: R^1(F) = F;
  f is onto by A9,A12,FUNCT_2:def 3;
  then f is open by A16,A17,TOPREALA:31;
  hence thesis by A13,A15,Lm67,TOPREALA:32;
end;
end;

registration
  cluster Circle2IntervalR -> open;
coherence
proof
  CircleMap.R^1(0) = c[10] by Th33;
  hence thesis by Th43,TOPREALA:34;
end;
  cluster Circle2IntervalL -> open;
coherence by Lm20,Th44,TOPREALA:34;
end;

canceled;

theorem
  CircleMap(R^1(1/2)) is_homeomorphism
proof
  reconsider r = 0 as Integer;
  CircleMap(R^1(1/2+r)) is open;
  hence thesis by TOPREALA:37;
end;

canceled 2;

theorem
  ex F being Subset-Family of Tunit_circle(2) st
   F = { CircleMap.:].0,1.[, CircleMap.:].1/2,3/2.[ } &
   F is_a_cover_of Tunit_circle(2) & F is open &
   for U being Subset of Tunit_circle(2) holds
   ( U = CircleMap.:].0,1.[ implies
     union IntIntervals(0,1) = (CircleMap)"U &
     for d being Subset of R^1 st d in IntIntervals(0,1)
      for f being Function of R^1 | d, (Tunit_circle(2)) | U st f = CircleMap|d
       holds f is_homeomorphism ) &
   ( U = CircleMap.:].1/2,3/2.[ implies
     union IntIntervals(1/2,3/2) = (CircleMap)"U &
     for d being Subset of R^1 st d in IntIntervals(1/2,3/2)
      for f being Function of R^1 | d, (Tunit_circle(2)) | U st f = CircleMap|d
       holds f is_homeomorphism )
proof
  set D1 = IntIntervals(0,1);
  set D2 = IntIntervals(1/2,3/2);
A1: c[10] in {c[10]} by TARSKI:def 1;
  set F1 = CircleMap.:union D1;
  set F2 = CircleMap.:union D2;
  set F = {F1,F2};
  reconsider F as Subset-Family of TUC;
  take F;
A2: ].0+0,1+0.[ in D1 by Lm1;
  then
A3: F1 = CircleMap.:].0,1.[ by Th41;
  ].1/2+0,3/2+0.[ in D2 by Lm1;
  then
A4: F2 = CircleMap.:].1/2,3/2.[ by Th41;
  hence F = {CircleMap.:].0,1.[, CircleMap.:].1/2,3/2.[} by A2,Th41;
  thus F is_a_cover_of TUC
  proof
    thus [#]TUC c= union F
    proof
      let a be set;
      assume
A5:     a in [#]TUC;
      reconsider A = [.0,0+1.[ as Subset of R^1 by TOPMETR:24;
      reconsider f = CircleMap | A as Function of R^1|A,TUC by Lm23;
      f is onto by Th39;
      then rng f = cS1 by FUNCT_2:def 3;
      then consider x being set such that
A6:     x in dom f and
A7:     f.x = a by A5,FUNCT_1:def 5;
A8:   dom f = A by Lm19,RELAT_1:91;
      then reconsider x as Real by A6;
A9:   CircleMap.x = f.x by A6,FUNCT_1:70;
A10:   F2 in F by TARSKI:def 2;
      per cases by A6,A8,RCOMP_2:3;
      suppose
A11:     x = 0;
      0 in A by RCOMP_2:3;
      then
A12:   f.0 = CircleMap.0 by FUNCT_1:72
        .= c[10] by Th33
        .= CircleMap.1 by Th33;
      1 in ].1/2,3/2.[ by RCOMP_1:47;
      then a in CircleMap.:].1/2,3/2.[ by A7,A11,A12,Lm19,FUNCT_1:def 12;
      hence a in union F by A4,A10,TARSKI:def 4;
      end;
      suppose 0 < x & x < 1;
      then
A13:   x in ].0,1.[ by RCOMP_1:47;
      ].0+0,1+0.[ in D1 by Lm1;
      then x in union D1 by A13,TARSKI:def 4;
      then
A14:   a in F1 by A7,A9,Lm19,FUNCT_1:def 12;
      F1 in F by TARSKI:def 2;
      hence a in union F by A14,TARSKI:def 4;
      end;
      suppose x >= 1 & x < 1;
      hence a in union F;
      end;
    end;
    thus thesis;
  end;
  thus F is open
  proof
    let P be Subset of TUC;
A15: CircleMap.:].0,1.[ = (CircleMap(R^1(0))).:].0,1.[ by RELAT_1:162;
A16: CircleMap.:].1/2,3/2.[ = (CircleMap(R^1(1/2))).:].1/2,3/2.[ by RELAT_1:162
;
    reconsider r = 0 as Integer;
A17: now
      let A be Subset of REAL;
A18:  the carrier of R^1 | R^1(A) = A by PRE_TOPC:29;
      A c= A;
      hence A is Subset of R^1 | R^1(A) by A18;
    end;
    then reconsider M = ].0,1.[ as Subset of R^1 | R^1(].r,r+1.[);
    reconsider N = ].1/2,3/2.[ as Subset of R^1 | R^1(].1/2+r,1/2+r+1.[)
    by A17;
A19: now
      let A be open Subset of REAL;
      reconsider B = A as Subset of R^1 | R^1(A) by A17;
A20:  the carrier of R^1 | R^1(A) = A by PRE_TOPC:29;
      B = [#](R^1 | R^1(A)) /\ R^1(A) by A20;
      hence A is open Subset of R^1 | R^1(A) by TOPS_2:32;
    end;
    then M is open;
    then
A21: (CircleMap(R^1(r))).:M is open by T_0TOPSP:def 2;
    N is open by A19;
    then (CircleMap(R^1(1/2+r))).:N is open by T_0TOPSP:def 2;
    then F1 is open & F2 is open by A3,A4,A15,A16,A21,TSEP_1:17;
    hence thesis by TARSKI:def 2;
  end;
  let U be Subset of TUC;
  thus U = CircleMap.:].0,1.[ implies
     union IntIntervals(0,1) = (CircleMap)"U &
     for d being Subset of R^1 st d in IntIntervals(0,1)
      for f being Function of R^1 | d, (Tunit_circle(2)) | U st f = CircleMap|d
       holds f is_homeomorphism
  proof
    assume
A22:  U = CircleMap.:].0,1.[;
    ].0+0,1+0.[ in D1 by Lm1;
    then
A23: CircleMap.:].0,1.[ = CircleMap.:union D1 by Th41;
  now
    let x1, x2 be Element of R^1;
    assume x1 in union D1;
    then consider Z being set such that
A24:   x1 in Z and
A25:   Z in D1 by TARSKI:def 4;
    consider n being Element of INT such that
A26:   Z = ].0+n,1+n.[ by A25;
    assume
A27:   CircleMap.x1 = CircleMap.x2;
    set k = [\x2/];
    set K = ].0+k,1+k.[;
    k in INT by INT_1:def 2;
    then
A28: K in D1;
A29: n < x1 by A24,A26,RCOMP_1:47;
    x1 < 1+n by A24,A26,RCOMP_1:47;
    then
A30: x1-1 < 1+n-1 by XREAL_1:11;
    then
A31: [\x1/] = n by A29,INT_1:def 4;
A32: k <= x2 by INT_1:def 4;
    not x1 is integer by A29,A31,INT_1:48;
    then
A33: not x1 in INT by INT_1:def 2;
    now
      assume k = x2;
      then CircleMap.x1 = c[10] by A27,Th33;
      hence contradiction by A1,A33,Lm19,Th34,FUNCT_1:def 13,TOPMETR:24;
    end;
    then
A34: k < x2 by A32,REAL_1:def 5;
A35: CircleMap.x1 = CircleMap.(x1+-n) & CircleMap.x2 = CircleMap.(x2+-k) by
Th32;
    0 < x1+-n by A29,REAL_2:105;
    then
A36: 2*PI*0 <= 2*PI*(x1-n) by XREAL_1:66;
    x1-1-n < n-n by A30,XREAL_1:11;
    then x1-n-1+1 < 0+1 by XREAL_1:10;
    then
A37: 2*PI*(x1-n) < 2*PI*1 by XREAL_1:70;
    0 <= x2+-k by A34,REAL_2:105;
    then
A38: 2*PI*0 <= 2*PI*(x2-k) by XREAL_1:66;
    x2-1 < k by INT_1:def 4;
    then x2-1-k < k-k by XREAL_1:11;
    then x2-1-k+1 < 0+1 by XREAL_1:8;
    then
A39: 2*PI*(x2-k) < 2*PI*1 by XREAL_1:70;
    CircleMap.(x1-n) = |[ cos(2*PI*(x1-n)), sin(2*PI*(x1-n)) ]| &
      CircleMap.(x2-k) = |[ cos(2*PI*(x2-k)), sin(2*PI*(x2-k)) ]| by Def11;
    then cos(2*PI*(x1-n)) = cos(2*PI*(x2-k)) &
         sin(2*PI*(x1-n)) = sin(2*PI*(x2-k)) by A27,A35,SPPOL_2:1;
    then 2*PI*(x1-n) = 2*PI*(x2-k) by A36,A37,A38,A39,COMPLEX2:12;
    then x1-n = x2-k by XCMPLX_1:5;
    then
A40: x2 = x1-n+k;
    x1 < 1+n by A24,A26,RCOMP_1:47;
    then x1-n < 1 by XREAL_1:21;
    then x2 < 1+k by A40,XREAL_1:8;
    then x2 in K by A34,RCOMP_1:47;
    hence x2 in union D1 by A28,TARSKI:def 4;
  end;
  hence union D1 = (CircleMap)"U by A22,A23,T_0TOPSP:2;
  let d be Subset of R^1;
  assume
A41: d in D1;
  let f be Function of R^1 | d, TUC | U;
  assume
A42: f = CircleMap|d;
  consider n being Element of INT such that
A43:  d = ].0+n,1+n.[ by A41;
  0+n < 1/2+n & 1/2+n < 1+n by XREAL_1:8;
  then reconsider d1 = d as non empty Subset of R^1 by A43,RCOMP_1:47;
A44: 1/2 in ].0+0,1+0.[ by RCOMP_1:47;
  reconsider U1 = U as non empty Subset of TUC
    by A22,A44,Lm19,Lm20,FUNCT_1:def 12;
  reconsider f1 = f as Function of R^1 | d1, TUC | U1;
A45: [#](TUC | U) = U by PRE_TOPC:def 10;
A46: F1 = CircleMap.:d by A41,Th41;
A47: f is one-to-one by A41,A42,Lm3,Th40;
A48: f is continuous by A42,TOPREALA:29;
  reconsider J = ].n,n+p1.[ as non empty Subset of R^1 by TOPMETR:24;
  reconsider F = CircleMap|J as Function of R^1|J,TUC by Lm23;
  d c= J by A43;
  then reconsider d2 = d as Subset of R^1|J by PRE_TOPC:29;
A49: R^1|J|d2 = R^1|d by A43,PRE_TOPC:28;
A50: CircleMap|d = CircleMap|J|d1 by A43,RELAT_1:103;
A51: f1 is onto
  proof
    thus rng f1 c= the carrier of TUC | U1;
    let b be set;
    assume b in the carrier of TUC | U1;
    then consider a being Element of R^1 such that
A52:   a in d and
A53:   b = CircleMap.a by A22,A23,A45,A46,FUNCT_2:116;
A54: dom (CircleMap|d) = d by Lm19,RELAT_1:91,TOPMETR:24;
    (CircleMap|d).a = CircleMap.a by A52,FUNCT_1:72;
    hence b in rng f1 by A42,A52,A53,A54,FUNCT_1:def 5;
  end;
  reconsider nn = n as Point of R^1 by TOPMETR:24,XREAL_0:def 1;
  CircleMap(R^1(n)) is open;
  then
A55: F is open by TOPREALA:33;
  CircleMap | ].n,n+1.[ is one-to-one;
  then f is open by A42,A49,A50,A51,A55,TOPREALA:31;
  hence f is_homeomorphism by A47,A48,A51,TOPREALA:37;
  end;
  assume
A56:  U = CircleMap.:].1/2,3/2.[;
  now
    let x1, x2 be Element of R^1;
    assume x1 in union D2;
    then consider Z being set such that
A57:   x1 in Z and
A58:   Z in D2 by TARSKI:def 4;
    consider n being Element of INT such that
A59:   Z = ].1/2+n,3/2+n.[ by A58;
    assume
A60:   CircleMap.x1 = CircleMap.x2;
A61: 1/2+n < x1 by A57,A59,RCOMP_1:47;
    0+n < 1/2+n by XREAL_1:10;
    then
A62: n < x1 by A61,XXREAL_0:2;
A63: x1 < 3/2+n by A57,A59,RCOMP_1:47;
    then
A64: x1-n < 3/2 by XREAL_1:21;
    set k = [\x2/];
A65: k <= x2 by INT_1:def 4;
    per cases by REAL_1:def 5;
    suppose x1 = 1+n;
    then CircleMap.x2 = c[10] by A60,Th33;
    then reconsider w = x2 as Element of INT
      by A1,Lm19,Th34,FUNCT_1:def 13,TOPMETR:24;
    w-1 in INT by INT_1:def 2;
    then
A66: ].1/2+(w-1),3/2+(w-1).[ in D2;
A67: -1/2+w < 0+w by XREAL_1:10;
    0+w < 1/2+w by XREAL_1:10;
    then x2 in ].1/2+(w-1),3/2+(w-1).[ by A67,RCOMP_1:47;
    hence x2 in union D2 by A66,TARSKI:def 4;
    end;
    suppose
A68:   x1 < 1+n;
    set K = ].1/2+k,3/2+k.[;
    k in INT by INT_1:def 2;
    then
A69: K in D2;
A70: CircleMap.x1 = CircleMap.(x1+-n) & CircleMap.x2 = CircleMap.(x2+-k) by
Th32;
    CircleMap.(x1-n) = |[ cos(2*PI*(x1-n)), sin(2*PI*(x1-n)) ]| &
      CircleMap.(x2-k) = |[ cos(2*PI*(x2-k)), sin(2*PI*(x2-k)) ]| by Def11;
    then
A71: cos(2*PI*(x1-n)) = cos(2*PI*(x2-k)) &
      sin(2*PI*(x1-n)) = sin(2*PI*(x2-k)) by A60,A70,SPPOL_2:1;
    0 < x1+-n by A62,REAL_2:105;
    then
A72: 2*PI*0 <= 2*PI*(x1-n) by XREAL_1:66;
    x1-1 < n+1-1 by A68,XREAL_1:11;
    then x1-1-n < n-n by XREAL_1:11;
    then x1-n-1+1 < 0+1 by XREAL_1:10;
    then
A73: 2*PI*(x1-n) < 2*PI*1 by XREAL_1:70;
    x2-x2 <= x2-k by A65,REAL_1:92;
    then
A74: 2*PI*0 <= 2*PI*(x2-k) by XREAL_1:66;
    x2-1 < k by INT_1:def 4;
    then x2-1-k < k-k by XREAL_1:11;
    then x2-1-k+1 < 0+1 by XREAL_1:8;
    then 2*PI*(x2-k) < 2*PI*1 by XREAL_1:70;
    then 2*PI*(x1-n) = 2*PI*(x2-k) by A71,A72,A73,A74,COMPLEX2:12;
    then
A75: x1-n = x2-k by XCMPLX_1:5;
    then
A76: x2 = x1-n+k;
    1/2+n-n < x1-n by A61,XREAL_1:11;
    then
A77: 1/2+k < x1-n+k by XREAL_1:10;
    x2 < 3/2+k by A64,A76,XREAL_1:8;
    then x2 in K by A75,A77,RCOMP_1:47;
    hence x2 in union D2 by A69,TARSKI:def 4;
    end;
    suppose
A78:   x1 > 1+n;
    set K = ].1/2+(k-1),3/2+(k-1).[;
    k-1 in INT by INT_1:def 2;
    then
A79: K in D2;
A80: CircleMap.x1 = CircleMap.(x1+(-1-n)) & CircleMap.x2 = CircleMap.(x2+-k)
by Th32;
    CircleMap.(x1-1-n) = |[ cos(2*PI*(x1-1-n)), sin(2*PI*(x1-1-n)) ]| &
      CircleMap.(x2-k) = |[ cos(2*PI*(x2-k)), sin(2*PI*(x2-k)) ]| by Def11;
    then
A81: cos(2*PI*(x1-1-n)) = cos(2*PI*(x2-k)) &
      sin(2*PI*(x1-1-n)) = sin(2*PI*(x2-k)) by A60,A80,SPPOL_2:1;
A82: x1-1 < 3/2+n-1 by A63,XREAL_1:11;
A83: n+1-1 < x1-1 by A78,XREAL_1:11;
    then n-n < x1-1-n by XREAL_1:11;
    then
A84: 2*PI*0 <= 2*PI*(x1-1-n) by XREAL_1:66;
    x1-1-n < 1/2+n-n by A82,XREAL_1:11;
    then x1-1-n < 1 by XXREAL_0:2;
    then
A85: 2*PI*(x1-1-n) < 2*PI*1 by XREAL_1:70;
    x2-x2 <= x2-k by A65,REAL_1:92;
    then
A86: 2*PI*0 <= 2*PI*(x2-k) by XREAL_1:66;
    x2-1 < k by INT_1:def 4;
    then x2-1-k < k-k by XREAL_1:11;
    then x2-1-k+1 < 0+1 by XREAL_1:8;
    then 2*PI*(x2-k) < 2*PI*1 by XREAL_1:70;
    then 2*PI*(x1-1-n) = 2*PI*(x2-k) by A81,A84,A85,A86,COMPLEX2:12;
    then
A87: x1-1-n = x2-k by XCMPLX_1:5;
    then
A88: x2 = x1-1-n+k;
A89: n-n < x1-1-n by A83,XREAL_1:11;
    -1/2 < 0;
    then -1/2 < x1-1-n by A89;
    then
A90: -1/2+k < x1-1-n+k by XREAL_1:10;
    x1-n-1 < 3/2-1 by A64,XREAL_1:11;
    then x2 < 1/2+k by A88,XREAL_1:8;
    then x2 in K by A87,A90,RCOMP_1:47;
    hence x2 in union D2 by A79,TARSKI:def 4;
    end;
  end;
  hence union D2 = (CircleMap)"U by A4,A56,T_0TOPSP:2;
  let d be Subset of R^1;
  assume
A91: d in D2;
  let f be Function of R^1 | d, TUC | U;
  assume
A92: f = CircleMap|d;
  consider n being Element of INT such that
A93:  d = ].1/2+n,3/2+n.[ by A91;
  1/2+n < 1+n & 1+n < 3/2+n by XREAL_1:8;
  then reconsider d1 = d as non empty Subset of R^1 by A93,RCOMP_1:47;
A94: 1 in ].1/2+0,3/2+0.[ by RCOMP_1:47;
  reconsider U1 = U as non empty Subset of TUC
    by A56,A94,Lm19,Lm21,FUNCT_1:def 12;
  reconsider f1 = f as Function of R^1 | d1, TUC | U1;
A95: [#](TUC | U) = U by PRE_TOPC:def 10;
A96: F2 = CircleMap.:d by A91,Th41;
A97: f is one-to-one by A91,A92,Lm4,Th40;
A98: f is continuous by A92,TOPREALA:29;
  reconsider J = ].1/2+n,1/2+n+p1.[ as non empty Subset of R^1 by TOPMETR:24;
  reconsider F = CircleMap|J as Function of R^1|J,TUC by Lm23;
  d c= J by A93;
  then reconsider d2 = d as Subset of R^1|J by PRE_TOPC:29;
A99: R^1|J|d2 = R^1|d by A93,PRE_TOPC:28;
A100: CircleMap|d = CircleMap|J|d1 by A93,RELAT_1:103;
A101: f1 is onto
  proof
    thus rng f1 c= the carrier of TUC | U1;
    let b be set;
    assume b in the carrier of TUC | U1;
    then consider a being Element of R^1 such that
A102:   a in d and
A103:   b = CircleMap.a by A4,A56,A95,A96,FUNCT_2:116;
A104: dom (CircleMap|d) = d by Lm19,RELAT_1:91,TOPMETR:24;
    (CircleMap|d).a = CircleMap.a by A102,FUNCT_1:72;
    hence b in rng f1 by A92,A102,A103,A104,FUNCT_1:def 5;
  end;
  reconsider nn = 1/2+n as Point of R^1 by TOPMETR:24,XREAL_0:def 1;
  CircleMap(R^1(1/2+n)) is open;
  then
A105: F is open by TOPREALA:33;
  CircleMap | ].1/2+n,1/2+n+1.[ is one-to-one;
  then f is open by A92,A99,A100,A101,A105,TOPREALA:31;
  hence f is_homeomorphism by A97,A98,A101,TOPREALA:37;
end;

