:: Some Properties of Circles on the Plane
::  by Artur Korni{\l}owicz and Yasunari Shidama
:: 
:: Received October 18, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabulary PRE_TOPC, EUCLID, RELAT_1, ARYTM, ZF_LANG, SUBSET_1, CARD_3, SEQ_1,
      TOPS_1, METRIC_1, INT_1, FUNCT_1, SIN_COS, BORSUK_2, ORDINAL2, ARYTM_1,
      MCART_1, COMPLEX1, SQUARE_1, T_0TOPSP, TOPS_2, ARYTM_3, RCOMP_1,
      ASYMPT_0, ABSVALUE, FUNCOP_1, REALSET1, TOPMETR, JGRAPH_2, SGRAPH1,
      BOOLE, SETFAM_1, TARSKI, COMPTS_1, TOPREAL1, TOPREAL2, FCONT_1, TAXONOM2,
      FINSEQ_1, TOPREALB, PARTFUN1, TMAP_1, CONNSP_2, XREAL_0, JGRAPH_6,
      SIN_COS6, BORSUK_4, FUNCT_5, TOPREALA;
 notation TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, REALSET2, FUNCT_1, RELSET_1,
      PARTFUN1, FUNCT_2, SEQ_1, NUMBERS, XCMPLX_0, XREAL_0, REAL_1, COMPLEX1,
      SQUARE_1, NAT_1, INT_1, CARD_3, FINSEQ_1, FUNCT_4, RCOMP_1, RCOMP_2,
      FCONT_1, STRUCT_0, PRE_TOPC, GRCAT_1, TOPS_1, BORSUK_1, TSEP_1, TOPS_2,
      COMPTS_1, CONNSP_2, T_0TOPSP, TMAP_1, TOPREAL1, TOPREAL2, METRIC_1,
      TOPMETR, EUCLID, TOPRNS_1, PCOMPS_1, BORSUK_2, SIN_COS, JGRAPH_6,
      BORSUK_3, JORDAN16, TAXONOM2, BORSUK_4, TOPREAL9, TOPALG_2, SIN_COS6,
      PSCOMP_1, TOPREALA;
 constructors TOPRNS_1, JORDAN2C, GRCAT_1, SIN_COS, JORDAN16, REAL_1, TOPS_2,
      TOPS_1, TOPREAL9, BORSUK_3, YELLOW_2, CONNSP_1, COMPLEX1, ARYTM_0,
      COMPTS_1, YELLOW_8, TOPREAL2, FCONT_1, TAXONOM2, REALSET2, CARD_3,
      COMSEQ_3, TMAP_1, COMPUT_1, RCOMP_2, JGRAPH_6, SIN_COS6, INTEGRA2,
      BORSUK_4, PSCOMP_1, TOPREAL1, TOPREALA, MONOID_0;
 clusters STRUCT_0, EUCLID, RELSET_1, XREAL_0, REAL_1, BORSUK_1, PRE_TOPC,
      XCMPLX_0, FUNCT_1, SCMRING3, TOPREAL9, BINARITH, INT_1, SIN_COS,
      SQUARE_1, TEX_2, JORDAN16, TOPALG_2, TOPS_1, TOPREAL6, BORSUK_6,
      TOPALG_3, FINSEQ_1, FUNCT_4, TOPGRP_1, RCOMP_1, SIN_COS6, PSCOMP_1,
      BORSUK_3, TOPREAL2, TOPREALA, MONOID_0;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM, REAL;
 definitions TARSKI, XBOOLE_0, FUNCT_1, FUNCT_2, PRE_TOPC, TOPS_2, T_0TOPSP,
      TMAP_1, STRUCT_0, TAXONOM2, TSEP_1, BORSUK_1, JORDAN16;
 theorems PRE_TOPC, XREAL_0, FUNCT_2, BORSUK_1, INT_1, SIN_COS, EUCLID,
      JGRAPH_1, SQUARE_1, TOPREAL9, TOPALG_1, GOBOARD6, JORDAN2C, TOPS_2,
      GRCAT_1, BORSUK_3, FUNCT_1, XCMPLX_1, REAL_2, TOPRNS_1, ABSVALUE,
      TOPMETR, JGRAPH_2, BORSUK_4, RELAT_1, FUNCOP_1, RCOMP_2, TOPREAL3,
      TOPREAL6, JORDAN16, TARSKI, PCOMPS_1, ZFMISC_1, TSEP_1, HILBERT3, TMAP_1,
      XBOOLE_1, SPPOL_2, COMPLEX2, JORDAN6, REAL_1, COMPTS_1, FCONT_1, JORDAN1,
      JGRAPH_3, COMPTRIG, SETFAM_1, CONNSP_2, XBOOLE_0, NAT_1, RCOMP_1,
      TOPREAL5, AXIOMS, REALSET2, TOPS_1, GOBOARD9, TOPALG_3, TOPGRP_1,
      T_0TOPSP, TREAL_1, SUBSET_1, JGRAPH_6, SEQ_1, TOPREAL2, STRUCT_0,
      EXTENS_1, SIN_COS6, BORSUK_5, WAYBEL29, PSCOMP_1, RELSET_1, TOPREALA;
 schemes FUNCT_2, INT_1;

begin :: Preliminaries

set P2 = 2*PI;
set o = |[0,0]|;
set R = the carrier of R^1;
OO: 0 in INT by INT_1:def 1;
reconsider p0 = -1 as negative (real number);
reconsider p1 = 1 as positive (real number);

set CIT = Closed-Interval-TSpace(-1,1);
set cCIT = the carrier of CIT;
LmCIT1: cCIT = [. -1,1 .] by TOPMETR:25;
Lmy1: 1-0 <= 1;
Lmy2: 3/2-1/2 <= 1;

reserve
  n for Nat,
  i for Integer,
  a, b, r for real number,
  x for Point of TOP-REAL n;

registration
  cluster ].0,1.[ -> non empty;
coherence;
  cluster [.-1,1.] -> non empty;
coherence;
  cluster ].1/2,3/2.[ -> non empty;
coherence
proof 
  ].1/2,1/2+p1.[ is non empty;
  hence thesis;
end;
end;

Lmp0: PI/2 < PI/1 by REAL_2:200;
Lmp5: 1*PI < 3/2*PI by REAL_1:70;
Lmp6: 3/2*PI < 2*PI by REAL_1:70;
Lmds: dom sin = REAL by SIN_COS:def 20;

Lm50:
for X being non empty TopSpace, Y being non empty SubSpace of X,
    Z being non empty SubSpace of Y,
    p being Point of Z holds p is Point of X
proof
  let X be non empty TopSpace,
      Y be non empty SubSpace of X,
      Z be non empty SubSpace of Y,
      p be Point of Z;
  p is Point of Y by TOPMETR:2;
  hence p is Point of X by TOPMETR:2;
end;

Lm51:
for X being TopSpace, Y being SubSpace of X,
    Z being SubSpace of Y,
    A being Subset of Z holds A is Subset of X 
proof 
  let X be TopSpace,
      Y be SubSpace of X,
      Z be SubSpace of Y,
      A be Subset of Z;
a1: the carrier of Y is Subset of X by TSEP_1:1;
  the carrier of Z is Subset of Y by TSEP_1:1;
  then A is Subset of Y by XBOOLE_1:1;
  hence A is Subset of X by a1,XBOOLE_1:1;
end;

registration
  cluster sin -> continuous;
coherence
proof 
  thus sin is_continuous_on dom sin by SIN_COS:def 20,COMPTRIG:43;
end;
  cluster cos -> continuous;
coherence
proof 
  thus cos is_continuous_on dom cos by SIN_COS:def 22,COMPTRIG:44;
end;
  cluster arcsin -> continuous;
coherence
proof 
  thus arcsin is_continuous_on dom arcsin by SIN_COS6:64,85;
end;
  cluster arccos -> continuous;
coherence
proof 
  thus arccos is_continuous_on dom arccos by SIN_COS6:88,109;
end;
end;

theorem Tsa:
  sin(a*r+b) = (sin*AffineMap(a,b)).r
proof 
a1: r is Real by XREAL_0:def 1;
  thus sin(a*r+b) = sin.(a*r+b) by SIN_COS:def 21
    .= sin.(AffineMap(a,b).r) by JORDAN16:def 3
    .= (sin*AffineMap(a,b)).r by a1,FUNCT_2:21;
end;

theorem Tca:
  cos(a*r+b) = (cos*AffineMap(a,b)).r
proof 
a1: r is Real by XREAL_0:def 1;
  thus cos(a*r+b) = cos.(a*r+b) by SIN_COS:def 23
    .= cos.(AffineMap(a,b).r) by JORDAN16:def 3
    .= (cos*AffineMap(a,b)).r by a1,FUNCT_2:21;
end;

registration
  let a be non zero (real number), b be real number;
  cluster AffineMap(a,b) -> onto one-to-one;
coherence
proof 
  thus rng AffineMap(a,b) = REAL by JORDAN16:32;
  thus thesis by JORDAN16:27;
end;
end;

definition
  let a, b be real number;
  func IntIntervals(a,b) equals :dci:
  { ]. a+n, b+n .[ where n is Element of INT: not contradiction };
coherence;
end;

theorem Texci:
  for x being set holds x in IntIntervals(a,b) iff
  ex n being Element of INT st x = ]. a+n, b+n .[
proof 
  let x be set;
a1: IntIntervals(a,b) =
   {]. a+n, b+n .[ where n is Element of INT: not contradiction} by dci;
  hereby 
    assume x in IntIntervals(a,b);
    then consider n being Element of INT such that
a2:   x = ]. a+n, b+n .[ by a1;
    take n;
    thus x = ]. a+n, b+n .[ by a2;
  end;
  assume ex n being Element of INT st x = ]. a+n, b+n .[;
  hence x in IntIntervals(a,b) by a1;
end;

registration
  let a, b be real number;
  cluster IntIntervals(a,b) -> non empty;
coherence
proof 
  ]. a+0, b+0 .[ in IntIntervals(a,b) by OO,Texci;
  hence thesis;
end;
end;

theorem
  b-a <= 1 implies IntIntervals(a,b) is mutually-disjoint
proof 
  assume
a0: b-a <= 1;
  let x, y be set;
  assume x in IntIntervals(a,b);
  then consider nx being Element of INT such that
a1: x = ].a+nx,b+nx.[ by Texci;
  assume y in IntIntervals(a,b);
  then consider ny being Element of INT such that
a2: y = ].a+ny,b+ny.[ by Texci;
  assume
a5: x <> y;
  assume x meets y;
  then consider z being set such that
a3: z in x and
a4: z in y by XBOOLE_0:3;
  reconsider z as Real by a1,a3;
a7: a+nx < z & z < b+nx by a1,a3,JORDAN6:45;
a8: a+ny < z & z < b+ny by a2,a4,JORDAN6:45;
a10: now
    let k be Nat;
a12: k >= 0 by NAT_1:18;
e11: b-a+a <= 1+a by a0,AXIOMS:24;
    a+1+0 <= a+1+k by a12,AXIOMS:24;
    hence a+(k+1) >= b by e11,AXIOMS:22;
  end;
  per cases by REAL_1:def 5;
  suppose nx = ny;
  hence contradiction by a1,a2,a5;
  end;
  suppose nx < ny;
  then nx+1 <= ny by INT_1:20;
  then reconsider k = ny-(nx+1) as Nat by INT_1:18;
  a+nx+1+k < b+nx by a7,a8,AXIOMS:22;
  then a+nx+(k+1)-nx < b+nx-nx by REAL_1:92;
  then a+(k+1) < b;
  hence contradiction by a10;
  end;
  suppose nx > ny;
  then ny+1 <= nx by INT_1:20;
  then reconsider k = nx-(ny+1) as Nat by INT_1:18;
  a+ny+1+k < b+ny by a7,a8,AXIOMS:22;
  then a+ny+(k+1)-ny < b+ny-ny by REAL_1:92;
  then a+(k+1) < b;
  hence contradiction by a10;
  end;
end;

definition
  let a, b be real number;
  redefine func IntIntervals(a,b) -> Subset-Family of R^1;
coherence
proof 
  IntIntervals(a,b) c= bool the carrier of R^1
  proof
    let x be set;
    assume x in IntIntervals(a,b);
    then ex n being Element of INT st x = ].a+n,b+n.[ by Texci;
    hence thesis by TOPMETR:24;
  end;
  hence thesis by SETFAM_1:def 7;
end;  
end;

definition
  let a, b be real number;
  redefine func IntIntervals(a,b) -> open Subset-Family of R^1;
coherence
proof 
  IntIntervals(a,b) is open
  proof
    let A be Subset of R^1;
    assume A in IntIntervals(a,b);
    then ex n being Element of INT st A = ].a+n,b+n.[ by Texci;
    hence thesis by JORDAN6:46;
  end;
  hence thesis;
end;
end;

begin :: Correspondence between REAL and R^1

definition
  let r be real number;
  func R^1(r) -> Point of R^1 equals :dr1:
  r;
coherence by XREAL_0:def 1,TOPMETR:24;
end;

definition
  let A be Subset of REAL;
  func R^1(A) -> Subset of R^1 equals :dr2:
  A;
coherence by TOPMETR:24;
end;

registration
  let A be non empty Subset of REAL;
  cluster R^1(A) -> non empty;
coherence by dr2;
end;

registration
  let A be open Subset of REAL;
  cluster R^1(A) -> open;
coherence
proof 
  R^1(A) = A by dr2;
  hence thesis by BORSUK_5:62;
end;
end;

registration
  let A be closed Subset of REAL;
  cluster R^1(A) -> closed;
coherence
proof 
  R^1(A) = A by dr2;
  hence thesis by TOPREAL6:79;
end;
end;

registration
  let A be open Subset of REAL;
  cluster R^1 | R^1(A) -> open;
coherence
proof 
  let X be Subset of R^1 such that
a1: X = the carrier of R^1 | R^1(A);
  the carrier of R^1 | R^1(A) = R^1(A) by JORDAN1:1
    .= A by dr2;
  hence X is open by a1,BORSUK_5:62;
end;
end;

registration
  let A be closed Subset of REAL;
  cluster R^1 | R^1(A) -> closed;
coherence
proof 
  let X be Subset of R^1 such that
a1: X = the carrier of R^1 | R^1(A);
  the carrier of R^1 | R^1(A) = R^1(A) by JORDAN1:1
    .= A by dr2;
  hence X is closed by a1,TOPREAL6:79;
end;
end;

definition
  let f be PartFunc of REAL,REAL;
  func R^1(f) -> map of R^1 | R^1(dom f), R^1 | R^1(rng f) equals :dr3:
  f;
coherence
proof 
a1: the carrier of R^1 | R^1(dom f) = R^1(dom f) &
    the carrier of R^1 | R^1(rng f) = R^1(rng f) by JORDAN1:1;
  dom f = R^1(dom f) & rng f = R^1(rng f) by dr2;
  hence thesis by a1,FUNCT_2:4;
end;
end;

registration
  let f be PartFunc of REAL,REAL;
  cluster R^1(f) -> onto;
coherence
proof 
  thus rng R^1(f) = rng f by dr3
    .= R^1(rng f) by dr2
    .= the carrier of R^1|R^1(rng f) by JORDAN1:1;
end;
end;

registration
  let f be one-to-one PartFunc of REAL,REAL;
  cluster R^1(f) -> one-to-one;
coherence by dr3;
end;

theorem Lmdom1a:
  R^1 | (R^1 [#]REAL) = R^1
proof 
  [#]R^1 = REAL by TOPMETR:24,PRE_TOPC:12
    .= [#]REAL by SUBSET_1:def 4
    .= R^1([#]REAL) by dr2;
  hence thesis by PRE_TOPC:def 10;
end;

theorem Lmdom1:
  for f being PartFunc of REAL,REAL st dom f = REAL holds
  R^1|(R^1 dom f) = R^1
proof 
  let f be PartFunc of REAL,REAL such that
a1:  dom f = REAL;
  [#]R^1 = REAL by TOPMETR:24,PRE_TOPC:12
    .= R^1(dom f) by a1,dr2;
  hence R^1 | R^1(dom f) = R^1 by PRE_TOPC:def 10;
end;

theorem TR1a:
  for f being Function of REAL,REAL holds f is map of R^1, R^1 | R^1(rng f)
proof 
  let f be Function of REAL,REAL;
  REAL = dom f by FUNCT_2:def 1;
  then R^1 | R^1(dom f) = R^1 by Lmdom1;
  then R^1(f) is map of R^1, R^1 | R^1(rng f);
  hence thesis by dr3;
end;

theorem TR1b:
  for f being Function of REAL,REAL holds f is map of R^1, R^1
proof 
  let f be Function of REAL,REAL;
  dom f = REAL by FUNCT_2:def 1;
  then reconsider B = rng f as non empty Subset of REAL by RELAT_1:65;
  f is map of R^1, R^1 | R^1(B) by TR1a;
  hence thesis by TOPREALA:28;
end;

Lmmap10:
sin is map of R^1,R^1
proof 
a1: sin = R^1(sin) by dr3;  
  dom sin = REAL by SIN_COS:def 20;
  then R^1|(R^1 dom sin) = R^1 by Lmdom1;
  then sin is map of R^1,R^1|R^1 [.-1,1.] by a1,COMPTRIG:46;
  hence thesis by TOPREALA:28;
end;

Lmmap1:
cos is map of R^1,R^1
proof 
a1: cos = R^1(cos) by dr3;  
  dom cos = REAL by SIN_COS:def 22;
  then R^1|(R^1 dom cos) = R^1 by Lmdom1;
  then cos is map of R^1,R^1|R^1 [.-1,1.] by a1,COMPTRIG:47;
  hence thesis by TOPREALA:28;
end;

registration
  let f be continuous PartFunc of REAL,REAL;
  cluster R^1(f) -> continuous;
coherence
proof 
  set g = R^1(f);
  per cases;
  suppose dom f is non empty;
  then reconsider A = dom f, B = rng f as non empty Subset of REAL
    by RELAT_1:65;
  reconsider g as map of R^1|R^1(A),R^1|R^1(B);
  reconsider h = g as map of R^1|R^1(A),R^1 by TOPREALA:28;
  for x being Point of R^1|R^1(A) holds h is_continuous_at x
  proof
    let x be Point of R^1|R^1(A);
    let G be a_neighborhood of h.x;
a1: f = g by dr3;
b1: [#](R^1|R^1(A)) = the carrier of R^1|R^1(A) by PRE_TOPC:12;
b2: the carrier of R^1|R^1(A) = R^1(A) by JORDAN1:1
     .= A by dr2;
    consider Z being Neighbourhood of f.x such that
a4:   Z c= G by a1,TOPREALA:41;
a0: f is_continuous_on dom f by JORDAN16:def 1;
d1: f|dom f = f by RELAT_1:98;
    f|dom f is_continuous_in x by a0,b2,FCONT_1:def 2;
    then
    consider N being Neighbourhood of x such that
a3:   f.:N c= Z by d1,FCONT_1:5;
    consider g being real number such that
c1:   0 < g and
c2:   N = ].x-g,x+g.[ by RCOMP_1:def 7;
    reconsider NN = N as open Subset of R^1 by c2,JORDAN6:46,TOPMETR:24;
    reconsider xx = x as Point of R^1 by TOPMETR:2;
    reconsider M = NN /\ [#](R^1|R^1(A)) as Subset of R^1|R^1(A)
      by b1,XBOOLE_1:17;
c3: NN = Int NN by TOPS_1:55;
    x-g < x-0 & x+0 < x+g by c1,REAL_1:53,92;
    then xx in Int NN by c2,c3,JORDAN6:45;
    then NN is open a_neighborhood of xx by CONNSP_2:def 1;
    then reconsider M as open a_neighborhood of x by TOPREALA:26;
    take M;
    M c= NN by XBOOLE_1:17;
    then h.:M c= h.:NN by RELAT_1:156;
    then h.:M c= Z by a1,a3,XBOOLE_1:1;
    hence h.:M c= G by a4,XBOOLE_1:1;
  end;
  then h is continuous by TMAP_1:49;
  hence thesis by TOPMETR:8;
  end;
  suppose dom f is empty;
  then f is empty by RELAT_1:64;
  then R^1(f) is empty by dr3;
  hence thesis by TOPREALA:27;
  end;
end;
end;

set A = R^1(].0,1.[);

LmAff:
now
  let a be non zero (real number),
      b be real number;
f12: dom AffineMap(a,b) = REAL by FUNCT_2:def 1;
er8: rng AffineMap(a,b) = REAL by JORDAN16:32;
er9: [#]R^1 = REAL by PRE_TOPC:12,TOPMETR:24;
  R^1| [#](R^1) = R^1 by TSEP_1:3;
  hence R^1 = R^1|R^1(dom AffineMap(a,b)) & R^1 = R^1|R^1(rng AffineMap(a,b))
    by er8,er9,dr2,f12;
end;

registration
  let a be non zero (real number),
      b be real number;
  cluster R^1(AffineMap(a,b)) -> open;
coherence
proof 
  let A be Subset of R^1|R^1(dom AffineMap(a,b));
a1: a is Real & b is Real by XREAL_0:def 1;
a2: R^1 = R^1|R^1(dom AffineMap(a,b)) & R^1 = R^1|R^1(rng AffineMap(a,b))
    by LmAff;
  AffineMap(a,b) = R^1(AffineMap(a,b)) by dr3;
  then R^1(AffineMap(a,b)) is_homeomorphism by a1,a2,JORDAN16:35;
  hence thesis by a2,TOPGRP_1:25;
end;
end;

begin :: Circles

definition
  let S be SubSpace of TOP-REAL 2;
  attr S is being_simple_closed_curve means :dscc:
  the carrier of S is Simple_closed_curve;
end;

registration
  cluster being_simple_closed_curve ->
     non empty arcwise_connected compact SubSpace of TOP-REAL 2;
coherence
proof 
  let S be SubSpace of TOP-REAL 2;
  assume
a1: the carrier of S is Simple_closed_curve;
  then reconsider A = the carrier of S as Simple_closed_curve;
  A is non empty;
  hence the carrier of S is non empty;
  thus S is arcwise_connected by a1,TOPALG_3:11;
  [#]S = A by PRE_TOPC:12;
  then [#]S is compact by COMPTS_1:11;
  hence S is compact by COMPTS_1:10;
end;
end;

registration
  let r be positive (real number),
      x be Point of TOP-REAL 2;
  cluster Sphere(x,r) -> being_simple_closed_curve;
coherence
proof 
  reconsider a = x as Point of Euclid 2 by TOPREAL3:13;
a1: x = |[x`1,x`2]| by EUCLID:57;
  Sphere(x,r) = Sphere(a,r) by TOPREAL9:15
   .= circle(x`1,x`2,r) by a1,TOPREAL9:49
   .= {w where w is Point of TOP-REAL 2: |.w- |[x`1,x`2]| .| = r}
      by JGRAPH_6:def 5;
  hence thesis by JGRAPH_6:32;
end;
end;

definition
  let n be Nat, x be Point of TOP-REAL n, r be real number;
  func Tcircle(x,r) -> SubSpace of TOP-REAL n equals :dc:
  (TOP-REAL n) | Sphere(x,r);
coherence;
end;
 
registration
  let n be non empty Nat, x be Point of TOP-REAL n,
      r be non negative (real number);
  cluster Tcircle(x,r) -> strict non empty;
coherence
proof 
  Tcircle(x,r) = (TOP-REAL n) | Sphere(x,r) by dc;
  hence thesis;
end;
end;

theorem Tcarc:
  the carrier of Tcircle(x,r) = Sphere(x,r)
proof 
  Tcircle(x,r) = (TOP-REAL n) | Sphere(x,r) by dc;
  then [#]Tcircle(x,r) = Sphere(x,r) by PRE_TOPC:def 10;
  hence the carrier of Tcircle(x,r) = Sphere(x,r) by PRE_TOPC:12;
end;

registration
  let n be Nat, x be Point of TOP-REAL n,
      r be empty (real number);
  cluster Tcircle(x,r) -> trivial;
coherence
proof 
  reconsider e = x as Point of Euclid n by TOPREAL3:13;
  the carrier of Tcircle(x,r) = Sphere(x,r) by Tcarc
  .= Sphere(e,r) by TOPREAL9:15
  .= {e} by TOPREAL6:62;
  hence thesis by REALSET2:def 5;
end;
end;

theorem Trec0:
  Tcircle(0.REAL 2,r) is SubSpace of Trectangle(-r,r,-r,r)
proof 
  set C = Tcircle(0.REAL 2,r);
  set T = Trectangle(-r,r,-r,r);
  the carrier of C c= the carrier of T
  proof
    let x be set;
    assume
a0:   x in the carrier of C;
    then C is non empty by STRUCT_0:def 1;
    then reconsider x as Point of TOP-REAL 2 by a0,TOPMETR:2;
a1: closed_inside_of_rectangle(-r,r,-r,r) =
      {p where p is Point of TOP-REAL 2:
        -r <= p`1 & p`1 <= r & -r <= p`2 & p`2 <= r} by JGRAPH_6:def 2;
a2: the carrier of Trectangle(-r,r,-r,r) =
      closed_inside_of_rectangle(-r,r,-r,r) by TOPREALA:53;
    the carrier of C = Sphere(0.REAL 2,r) by Tcarc;
    then
a3: |. x .| = r by a0,TOPREAL9:12;
    abs(x`1) <= |. x .| & abs(x`2) <= |. x .| by JGRAPH_1:50;
    then -r <= x`1 & x`1 <= r & -r <= x`2 & x`2 <= r by a3,ABSVALUE:12;
    hence thesis by a1,a2;
  end;
  hence thesis by TSEP_1:4;
end;

registration
  let x be Point of TOP-REAL 2,
      r be positive (real number);
  cluster Tcircle(x,r) -> being_simple_closed_curve;
coherence
proof 
  the carrier of Tcircle(x,r) = Sphere(x,r) by Tcarc;
  hence the carrier of Tcircle(x,r) is Simple_closed_curve;
end;
end;

registration
  cluster being_simple_closed_curve strict SubSpace of TOP-REAL 2;
existence
proof 
  consider x being Point of TOP-REAL 2, r be positive (real number);
  take Tcircle(x,r);
  thus thesis;
end;
end;

theorem
  for S, T being being_simple_closed_curve SubSpace of TOP-REAL 2 holds
  S,T are_homeomorphic
proof 
  let S, T be being_simple_closed_curve SubSpace of TOP-REAL 2;
  the TopStruct of S, the TopStruct of T are_homeomorphic
  proof
    reconsider A = the carrier of the TopStruct of S
      as Simple_closed_curve by dscc;
    reconsider B = the carrier of the TopStruct of T
      as Simple_closed_curve by dscc;
    consider f being map of (TOP-REAL 2)|R^2-unit_square, (TOP-REAL 2)|A
     such that
a1:   f is_homeomorphism by TOPREAL2:def 1;
    consider g being map of (TOP-REAL 2)|R^2-unit_square, (TOP-REAL 2)|B
     such that
a2:   g is_homeomorphism by TOPREAL2:def 1;
a3: the TopStruct of S is strict SubSpace of TOP-REAL 2 by TMAP_1:11;
    [#]the TopStruct of S = A by PRE_TOPC:12;
    then
a5: the TopStruct of S = (TOP-REAL 2)|A by a3,PRE_TOPC:def 10;
a4: the TopStruct of T is strict SubSpace of TOP-REAL 2 by TMAP_1:11;
    [#]the TopStruct of T = B by PRE_TOPC:12;
    then
a6: the TopStruct of T = (TOP-REAL 2)|B by a4,PRE_TOPC:def 10;
    then reconsider h = g*f" as map of the TopStruct of S, the TopStruct of T
      by a5;
    take h;
    f" is_homeomorphism by a1,TOPS_2:70;
    hence h is_homeomorphism by a2,a5,a6,TOPS_2:71;
  end;
  hence thesis by TOPREALA:36;
end;

:: Unit circle

definition
  let n be Nat;
  func Tunit_circle(n) -> SubSpace of TOP-REAL n equals :dunit:
  Tcircle(0.REAL n,1);
coherence;
end;

set TUC = Tunit_circle(2);
set cS1 = the carrier of TUC;
LmcS1: now
  TUC = Tcircle(0.REAL 2,1) by dunit;
  hence cS1 = Sphere(|[0,0]|,1) by Tcarc,EUCLID:58;
end;

registration
  let n be non empty Nat;
  cluster Tunit_circle(n) -> non empty;
coherence
proof 
  Tunit_circle(n) = Tcircle(0.REAL n,p1) by dunit;
  hence thesis;
end;
end;

theorem Tu1:
  for n being non empty Nat, x being Point of TOP-REAL n holds
  x is Point of Tunit_circle(n) implies |. x .| = 1
proof 
  let n be non empty Nat,
      x be Point of TOP-REAL n;
  reconsider j = 1 as non negative (real number);
  assume x is Point of Tunit_circle(n);
  then x is Point of Tcircle(0.REAL n,j) by dunit;
  then x in the carrier of Tcircle(0.REAL n,j);
  then x in Sphere(0.REAL n,1) by Tcarc;
  hence |. x .| = 1 by TOPREAL9:12;
end;

theorem Tu2:
  for x being Point of TOP-REAL 2 st x is Point of Tunit_circle(2) holds
  -1 <= x`1 & x`1 <= 1 & -1 <= x`2 & x`2 <= 1
proof 
  let x be Point of TOP-REAL 2 such that
a1: x is Point of Tunit_circle(2);
  consider a, b being Real such that
a2:  x = <*a,b*> by EUCLID:55;
  x = |[a,b]| by a2,EUCLID:def 16;
    then
a3: a = x`1 & b = x`2 by EUCLID:56;
a4: 1^2 = |. x .|^2 by a1,Tu1
       .= a^2+b^2 by a3,JGRAPH_3:10;
  0 <= b^2 & 0 <= a^2 by SQUARE_1:72;
  then -b^2 <= -0 & -a^2 <= -0 by REAL_1:50;
  then a^2-1 <= 0 & b^2-1 <= 0 by a4,SQUARE_1:59;
  hence thesis by a3,JGRAPH_3:5;
end;

theorem Tu6:
  for x being Point of TOP-REAL 2 st x is Point of Tunit_circle(2) & x`1 = 1
  holds x`2 = 0
proof 
  let x be Point of TOP-REAL 2 such that
c4: x is Point of Tunit_circle(2) and
a2: x`1 = 1;
  1^2 = |. x .|^2 by c4,Tu1
     .= x`1^2+x`2^2 by JGRAPH_3:10;
  then 1-1 = 1+x`2^2-1 by a2,SQUARE_1:59;
  hence x`2 = 0 by SQUARE_1:73;
end;

theorem Tu7:
  for x being Point of TOP-REAL 2 st x is Point of Tunit_circle(2) & x`1 = -1
  holds x`2 = 0
proof 
  let x be Point of TOP-REAL 2 such that
c4: x is Point of Tunit_circle(2) and
a2: x`1 = -1;
  1^2 = |. x .|^2 by c4,Tu1
     .= x`1^2+x`2^2 by JGRAPH_3:10;
  then 1-1 = 1+x`2^2-1 by a2,SQUARE_1:59,61;
  hence x`2 = 0 by SQUARE_1:73;
end;

theorem
  for x being Point of TOP-REAL 2 st x is Point of Tunit_circle(2) & x`2 = 1
  holds x`1 = 0
proof 
  let x be Point of TOP-REAL 2 such that
c4: x is Point of Tunit_circle(2) and
a2: x`2 = 1;
  1^2 = |. x .|^2 by c4,Tu1
     .= x`1^2+x`2^2 by JGRAPH_3:10;
  then 1-1 = 1+x`1^2-1 by a2,SQUARE_1:59;
  hence x`1 = 0 by SQUARE_1:73;
end;

theorem
  for x being Point of TOP-REAL 2 st x is Point of Tunit_circle(2) & x`2 = -1
  holds x`1 = 0
proof 
  let x be Point of TOP-REAL 2 such that
c4: x is Point of Tunit_circle(2) and
a2: x`2 = -1;
  1^2 = |. x .|^2 by c4,Tu1
     .= x`1^2+x`2^2 by JGRAPH_3:10;
  then 1-1 = 1+x`1^2-1 by a2,SQUARE_1:59,61;
  hence x`1 = 0 by SQUARE_1:73;
end;

set TREC = Trectangle(p0,p1,p0,p1);

theorem
  Tunit_circle(2) is SubSpace of Trectangle(-1,1,-1,1)
proof 
  Tcircle(0.REAL 2,1) is SubSpace of TREC by Trec0;
  hence thesis by dunit;
end;

theorem Lm4:
for n being non empty Nat,
    r being positive (real number),
    x being Point of TOP-REAL n,
    f being map of Tunit_circle(n), Tcircle(x,r) st 
  for a being Point of Tunit_circle(n),
      b being Point of TOP-REAL n st a = b holds f.a = r*b+x
  holds f is_homeomorphism
proof 
  let n be non empty Nat,
      r be positive (real number),
      x be Point of TOP-REAL n,
      f being map of Tunit_circle(n), Tcircle(x,r) such that 
a1: for a being Point of Tunit_circle(n),
        b being Point of TOP-REAL n st a = b holds f.a = r*b+x;
  set U = Tunit_circle(n), C = Tcircle(x,r);
  defpred P[Point of TOP-REAL n,set] means $2 = r*$1+x;
v5: 1/r is Real by XREAL_0:def 1;
v2: 1/r*r = 1 by XCMPLX_1:88;
  U = Tcircle(0.REAL n,1) by dunit;
  then
v3: the carrier of U = Sphere(0.REAL n,1) by Tcarc;
b2: the carrier of C = Sphere(x,r) by Tcarc;
g3: the carrier of C = [#]C by PRE_TOPC:12;
a3: dom f = the carrier of U by FUNCT_2:def 1;
  hence dom f = [#]U by PRE_TOPC:12;
  thus
z2: rng f = [#]C
  proof
    thus rng f c= [#]C by g3;
    let b be set;
    assume
g2:   b in [#]C;
    then reconsider c = b as Point of TOP-REAL n by TOPMETR:2;
    set a = 1/r * (c - x);
    |.a-0.REAL n.| = |.a.| by JORDAN2C:13
      .= abs(1/r)*|. c-x .| by v5,TOPRNS_1:8
      .= 1/r*|. c-x .| by ABSVALUE:def 1
      .= 1/r*r by b2,g2,TOPREAL9:9;
    then
a2: a in Sphere(0.REAL n,1) by v2,TOPREAL9:9;
    then f.a = r*a+x by a1,v3
      .= r*(1/r)*(c-x)+x by EUCLID:34
      .= c-x+x by v2,EUCLID:33
      .= b by EUCLID:52;
    hence b in rng f by a2,a3,v3,FUNCT_1:def 5;
  end;
  thus
z1: f is one-to-one
  proof
    let a, b be set such that
c1:   a in dom f and
c2:   b in dom f and
c3:   f.a = f.b;
    reconsider a1 = a, b1 = b as Point of TOP-REAL n by a3,v3,c1,c2;
c4: f.a1 = r*a1+x by a1,c1;
    f.b1 = r*b1+x by a1,c2;
    then r*a1 = r*b1+x-x by c3,c4,EUCLID:52;
    then r*a1 = r*b1 by EUCLID:52;
    hence a = b by EUCLID:38;
  end;
A0: for u being Point of TOP-REAL n ex y being Point of TOP-REAL n st P[u,y];
  consider F being map of TOP-REAL n, TOP-REAL n such that
A6: for x being Point of TOP-REAL n holds P[x,F.x] from FUNCT_2:sch 3(A0);
  set f1 = id TOP-REAL n;
  set f2 = (TOP-REAL n) --> x;
  for p being Point of TOP-REAL n holds F.p = r * f1.p + 1 * f2.p
  proof
    let p be Point of TOP-REAL n;
k3: f2 = (the carrier of TOP-REAL n) --> x by BORSUK_1:def 3;    
    thus r * f1.p + 1 * f2.p = r*f1.p + f2.p by EUCLID:33
      .= r*p + f2.p by GRCAT_1:11
      .= r*p + x by k3,FUNCOP_1:13
      .= F.p by A6;
  end;
  then
A2: F is continuous by TOPALG_1:17;
A1: U = Tcircle(0.REAL n,1) by dunit
     .= (TOP-REAL n)|Sphere(0.REAL n,1) by dc;
  dom F = the carrier of TOP-REAL n by FUNCT_2:def 1;
  then
A4: dom (F|Sphere(0.REAL n,1)) = Sphere(0.REAL n,1) by RELAT_1:91;
  for a being set st a in dom f holds f.a = (F|Sphere(0.REAL n,1)).a
  proof
    let a be set such that
A5:   a in dom f;
    reconsider y = a as Point of TOP-REAL n by A5,TOPMETR:2;
    thus f.a = r*y+x by a1,A5
      .= F.y by A6
      .= (F|Sphere(0.REAL n,1)).a by v3,A5,FUNCT_1:72;
  end;
  then f = F|Sphere(0.REAL n,1) by v3,a3,A4,FUNCT_1:9;
  hence f is continuous by A2,A1,BORSUK_4:69;
  defpred R[Point of TOP-REAL n,set] means $2 = 1/r*($1-x);
A0: for u being Point of TOP-REAL n ex y being Point of TOP-REAL n st R[u,y];
  consider G being map of TOP-REAL n, TOP-REAL n such that
A6: for a being Point of TOP-REAL n holds R[a,G.a] from FUNCT_2:sch 3(A0);
t3: dom (f") = the carrier of C by FUNCT_2:def 1;
  dom G = the carrier of TOP-REAL n by FUNCT_2:def 1;
  then
A4: dom (G|Sphere(x,r)) = Sphere(x,r) by RELAT_1:91;
  for p being Point of TOP-REAL n holds G.p = 1/r * f1.p + (-1/r) * f2.p
  proof
    let p be Point of TOP-REAL n;
k3: f2 = (the carrier of TOP-REAL n) --> x by BORSUK_1:def 3;    
    thus 1/r * f1.p + (-1/r) * f2.p = 1/r*p + (-1/r)*f2.p by GRCAT_1:11
      .= 1/r*p + (-1/r)*x by k3,FUNCOP_1:13
      .= 1/r*p + -1/r*x by EUCLID:44
      .= 1/r*p - 1/r*x by EUCLID:45
      .= 1/r*(p-x) by EUCLID:53
      .= G.p by A6;
  end;
  then
A2: G is continuous by TOPALG_1:17;
A1: C = (TOP-REAL n)|Sphere(x,r) by dc;
  for a being set st a in dom (f") holds f".a = (G|Sphere(x,r)).a
  proof
    let a be set such that
A5:   a in dom (f");
    reconsider y = a as Point of TOP-REAL n by A5,TOPMETR:2;
    reconsider ff = f as Function;
    set e = 1/r * (y - x);
    |.e-0.REAL n.| = |.e.| by JORDAN2C:13
      .= abs(1/r)*|. y-x .| by v5,TOPRNS_1:8
      .= 1/r*|. y-x .| by ABSVALUE:def 1
      .= 1/r*r by b2,A5,TOPREAL9:9;
    then
r2: 1/r * (y - x) in Sphere(0.REAL n,1) by v2,TOPREAL9:9;
    then f.e = r*e+x by a1,v3
      .= r*(1/r)*(y-x)+x by EUCLID:34
      .= y-x+x by v2,EUCLID:33
      .= y by EUCLID:52;
    then ff".a = 1/r * (y - x) by z1,r2,a3,v3,FUNCT_1:54;
    hence f".a = 1/r*(y-x) by z1,z2,TOPS_2:def 4
      .= G.y by A6
      .= (G|Sphere(x,r)).a by b2,A5,FUNCT_1:72;
  end;
  then f" = G|Sphere(x,r) by b2,t3,A4,FUNCT_1:9;
  hence f" is continuous by A2,A1,BORSUK_4:69;
end;

registration
  cluster Tunit_circle(2) -> being_simple_closed_curve;
coherence
proof 
  Tunit_circle(2) = Tcircle(0.REAL 2,p1) by dunit;
  hence thesis;
end;
end;

Lm3:
for n being non empty Nat,
    r being positive (real number),
    x being Point of TOP-REAL n holds
Tunit_circle(n), Tcircle(x,r) are_homeomorphic
proof 
  let n be non empty Nat,
      r be positive (real number),
      x be Point of TOP-REAL n;
  set U = Tunit_circle(n), C = Tcircle(x,r);
  defpred P[Point of U,set] means ex w being Point of TOP-REAL n
    st w = $1 & $2 = r*w+x;
v1: r is Real by XREAL_0:def 1;
b2: the carrier of C = Sphere(x,r) by Tcarc;
a0: for u being Point of U ex y being Point of C st P[u,y]
proof
  let u be Point of U;
  reconsider v = u as Point of TOP-REAL n by TOPMETR:2;
  set y = r*v+x;
  |. y-x .| = |. r*v .| by EUCLID:52
   .= abs(r)*|. v .| by v1,TOPRNS_1:8
   .= r*|. v .| by ABSVALUE:def 1
   .= r*1 by Tu1;
  then reconsider y as Point of C by b2,TOPREAL9:9;
  take y;
  thus P[u,y];
end;
  consider f being map of U,C such that
a1: for x being Point of U holds P[x,f.x] from FUNCT_2:sch 3(a0);
  take f;
  for a being Point of U, b being Point of TOP-REAL n st a = b holds
  f.a = r*b+x
  proof
    let a be Point of U,
        b be Point of TOP-REAL n;
    P[a,f.a] by a1;
    hence thesis;
  end;
  hence f is_homeomorphism by Lm4;
end;

theorem
  for n being non empty Nat,
      r, s being positive (real number),
      x, y being Point of TOP-REAL n holds
  Tcircle(x,r), Tcircle(y,s) are_homeomorphic
proof 
  let n be non empty Nat,
      r, s be positive (real number),
      x, y be Point of TOP-REAL n;
  Tcircle(x,r), Tunit_circle(n) are_homeomorphic &
  Tunit_circle(n), Tcircle(y,s) are_homeomorphic by Lm3;
  hence Tcircle(x,r), Tcircle(y,s) are_homeomorphic by BORSUK_3:3;
end;

registration
  let x be Point of TOP-REAL 2,
      r be non negative (real number);
  cluster Tcircle(x,r) -> arcwise_connected;
coherence
proof 
  per cases;
  suppose r is positive;
  then reconsider r as positive (real number);
  Tcircle(x,r) is arcwise_connected;
  hence thesis;
  end;
  suppose r is non positive;
  then reconsider r as non negative non positive (real number);
  Tcircle(x,r) is trivial;
  hence thesis;
  end;
end;
end;

:: Open unit circle

definition
  func c[10] -> Point of Tunit_circle(2) equals :dc10:
  |[1,0]|;
coherence
proof 
A2: |[1,0]|`1 = 1 & |[1,0]|`2 = 0 by EUCLID:56;
  |. |[1+0,0+0]| - o .| = |.|[1,0]|+o- o.| by EUCLID:60
   .= |.|[1,0]|+(o-o).| by EUCLID:49
   .= |.|[1,0]|+(0.REAL 2).| by EUCLID:46
   .= |.|[1,0]|.| by EUCLID:31
   .= sqrt(1^2+0^2) by A2,JGRAPH_1:47
   .= 1 by SQUARE_1:60,89;
  hence thesis by LmcS1,TOPREAL9:9;
end;
  func c[-10] -> Point of Tunit_circle(2) equals :dc10x:
  |[-1,0]|;
coherence
proof 
A2: |[-1,0]|`1 = -1 & |[-1,0]|`2 = 0 by EUCLID:56;
  |. |[-1+0,0+0]| - o .| = |.|[-1,0]|+o- o.| by EUCLID:60
   .= |.|[-1,0]|+(o-o).| by EUCLID:49
   .= |.|[-1,0]|+(0.REAL 2).| by EUCLID:46
   .= |.|[-1,0]|.| by EUCLID:31
   .= sqrt((-1)^2+0^2) by A2,JGRAPH_1:47
   .= sqrt(1^2+0^2) by SQUARE_1:61
   .= 1 by SQUARE_1:60,89;
  hence thesis by LmcS1,TOPREAL9:9;
end;
end;

theorem Tw10:
  c[10] <> c[-10] by dc10,dc10x,SPPOL_2:1;

definition
  let p be Point of Tunit_circle(2);
  func Topen_unit_circle(p) -> strict SubSpace of Tunit_circle(2) means :dop:
  the carrier of it = (the carrier of Tunit_circle(2)) \ {p};
existence
proof 
  reconsider A = cS1 \ {p} as Subset of TUC by XBOOLE_1:36;
  take TUC|A;
  thus thesis by JORDAN1:1;
end;
uniqueness by TSEP_1:5;
end;

registration
  let p be Point of Tunit_circle(2);
  cluster Topen_unit_circle(p) -> non empty;
coherence
proof 
  set X = Topen_unit_circle(p);
a1: the carrier of X = cS1 \ {p} by dop;
  per cases;
  suppose
a4: p = c[10];
  reconsider r = p as Point of TOP-REAL 2 by TOPMETR:2;
  set x = |[0,1]|;
A2: x`1 = 0 & x`2 = 1 by EUCLID:56;
  |. |[0+0,1+0]| - o .| = |.x+o- o.| by EUCLID:60
   .= |.x+(o-o).| by EUCLID:49
   .= |.x+(0.REAL 2).| by EUCLID:46
   .= |. x .| by EUCLID:31
   .= sqrt(1^2+0^2) by A2,JGRAPH_1:47
   .= 1 by SQUARE_1:60,89;
  then
a2: x in cS1 by LmcS1,TOPREAL9:9;
  r`1 = 1 by a4,dc10,EUCLID:56;
  then not x in {p} by A2,TARSKI:def 1;
  hence the carrier of X is non empty by a1,a2,XBOOLE_0:def 4;
  end;
  suppose p <> c[10];
  then not c[10] in {p} by TARSKI:def 1;
  hence the carrier of X is non empty by a1,XBOOLE_0:def 4;
  end;
end;
end;

theorem Tpnp:
  for p being Point of Tunit_circle(2) holds
    p is not Point of Topen_unit_circle(p)
proof 
  let p be Point of Tunit_circle(2);
a1: the carrier of Topen_unit_circle(p) =
    (the carrier of Tunit_circle(2)) \ {p} by dop;
  p in {p} by TARSKI:def 1;
  hence thesis by a1,XBOOLE_0:def 4;
end;

theorem Topen6:
  for p being Point of Tunit_circle(2) holds
  Topen_unit_circle(p) = (Tunit_circle(2)) | (([#]Tunit_circle(2)) \ {p})
proof 
  let p be Point of Tunit_circle(2);
  [#]Topen_unit_circle(p) = the carrier of Topen_unit_circle(p) by PRE_TOPC:12
   .= (the carrier of Tunit_circle(2)) \ {p} by dop
   .= ([#]Tunit_circle(2)) \ {p} by PRE_TOPC:12;
  hence Topen_unit_circle(p) = (Tunit_circle(2)) | (([#]Tunit_circle(2)) \ {p})
    by PRE_TOPC:def 10;
end;

theorem Tw11:
  for p, q being Point of Tunit_circle(2) st p <> q holds
  q is Point of Topen_unit_circle(p)
proof 
  let p, q be Point of Tunit_circle(2) such that
a1: p <> q;
  the carrier of Topen_unit_circle(p) = (the carrier of Tunit_circle(2)) \ {p}
    by dop;
  hence q is Point of Topen_unit_circle(p) by a1,ZFMISC_1:64;
end;

theorem Tw4:
  for p being Point of TOP-REAL 2 st
    p is Point of Topen_unit_circle(c[10]) & p`2 = 0 holds
  p = c[-10]
proof 
  let p be Point of TOP-REAL 2 such that
a1: p is Point of Topen_unit_circle(c[10]) and
a2: p`2 = 0;
  p is Point of TUC by a1,TOPMETR:2;
  then 1^2 = |. p .|^2 by Tu1
     .= p`1^2+p`2^2 by JGRAPH_3:10;
  then
a3: p`1 = 1 or p`1 = -1 by a2,SQUARE_1:59,60,JGRAPH_3:2;
  now
    assume p`1 = 1;
    then p = c[10] by a2,dc10,EUCLID:57;
    hence contradiction by a1,Tpnp;
  end;
  hence thesis by dc10x,a2,a3,EUCLID:57;
end;

theorem Tw4a:
  for p being Point of TOP-REAL 2 st
    p is Point of Topen_unit_circle(c[-10]) & p`2 = 0 holds
  p = c[10]
proof 
  let p be Point of TOP-REAL 2 such that
a1: p is Point of Topen_unit_circle(c[-10]) and
a2: p`2 = 0;
  p is Point of TUC by a1,TOPMETR:2;
  then 1^2 = |. p .|^2 by Tu1
     .= p`1^2+p`2^2 by JGRAPH_3:10;
  then
a3: p`1 = 1 or p`1 = -1 by a2,SQUARE_1:59,60,JGRAPH_3:2;
  now
    assume p`1 = -1;
    then p = c[-10] by a2,dc10x,EUCLID:57;
    hence contradiction by a1,Tpnp;
  end;
  hence thesis by dc10,a2,a3,EUCLID:57;
end;

set TOUC = Topen_unit_circle(c[10]);
set TOUCm = Topen_unit_circle(c[-10]);
set X = the carrier of TOUC;
set Xm = the carrier of TOUCm;
set Y = the carrier of (R^1|R^1(].0,0+p1.[));
set Ym = the carrier of (R^1|R^1(].1/2,1/2+p1.[));
Lmb0: X = [#]TOUC by PRE_TOPC:12;
Lmb0m: Xm = [#]TOUCm by PRE_TOPC:12;

theorem Tu3:
  for p being Point of Tunit_circle(2),
      x being Point of TOP-REAL 2 st x is Point of Topen_unit_circle(p) holds
  -1 <= x`1 & x`1 <= 1 & -1 <= x`2 & x`2 <= 1
proof 
  let p be Point of Tunit_circle(2),
      x be Point of TOP-REAL 2;
  assume x is Point of Topen_unit_circle(p);
  then
a1: x in the carrier of Topen_unit_circle(p);
  the carrier of Topen_unit_circle(p) is Subset of cS1 by TSEP_1:1;
  hence thesis by a1,Tu2;
end;

theorem Tu4:
  for x being Point of TOP-REAL 2 st x is Point of Topen_unit_circle(c[10])
  holds -1 <= x`1 & x`1 < 1
proof 
  let x be Point of TOP-REAL 2;
  assume
a0: x is Point of Topen_unit_circle(c[10]);
  then
a3: x`1 <= 1 by Tu3;
  now
    assume
d10:  x`1 = 1;
c2: the carrier of TOUC = cS1 \ {c[10]} by dop;
  then
c4: x in cS1 by a0,XBOOLE_0:def 4;
c8: not x in {c[10]} by a0,c2,XBOOLE_0:def 4;
    x = |[x`1,x`2]| by EUCLID:57;
    then x = c[10] by c4,d10,Tu6,dc10;
    hence contradiction by c8,TARSKI:def 1;
  end;
  hence thesis by a0,a3,Tu3,REAL_1:def 5;
end;

theorem Tu4a:
  for x being Point of TOP-REAL 2 st x is Point of Topen_unit_circle(c[-10])
  holds -1 < x`1 & x`1 <= 1
proof 
  let x be Point of TOP-REAL 2;
  assume
a0: x is Point of Topen_unit_circle(c[-10]);
  then
a2: -1 <= x`1 by Tu3;
  now
    assume
d10:  x`1 = -1;
c2: the carrier of TOUCm = cS1 \ {c[-10]} by dop;
  then
c4: x in cS1 by a0,XBOOLE_0:def 4;
c8: not x in {c[-10]} by a0,c2,XBOOLE_0:def 4;
    x = |[x`1,x`2]| by EUCLID:57;
    then x = c[-10] by c4,d10,Tu7,dc10x;
    hence contradiction by c8,TARSKI:def 1;
  end;
  hence thesis by a2,a0,Tu3,REAL_1:def 5;
end;

registration
  let p be Point of Tunit_circle(2);
  cluster Topen_unit_circle(p) -> open;
coherence
proof 
  let A be Subset of TUC such that
a1: A = the carrier of Topen_unit_circle(p);
a2: TUC is being_T2 by TOPMETR:3;
  A` = cS1 \ A by SUBSET_1:def 5
    .= cS1 \ (cS1 \ {p}) by a1,dop
    .= cS1 /\ {p} by XBOOLE_1:48
    .= {p} by ZFMISC_1:52;
  then A` is closed by a2,PCOMPS_1:10;
  hence A is open by TOPS_1:30;
end;
end;

theorem 
  for p being Point of Tunit_circle(2) holds
  Topen_unit_circle(p), I(01) are_homeomorphic
proof 
  let p be Point of Tunit_circle(2);
  set P = Topen_unit_circle(p);
  set D = Sphere(0.REAL 2,p1);
  reconsider p2 = p as Point of TOP-REAL 2 by TOPMETR:2;
a3: [#]TUC = D by LmcS1,PRE_TOPC:12,EUCLID:58;
a5: D\{p} c= D by XBOOLE_1:36;
  then reconsider A = D\{p} as Subset of Tcircle(0.REAL 2,1)
    by Tcarc;
  reconsider A1 = A as Subset of (TOP-REAL 2) | D by a5,JORDAN1:1;
  P = TUC | (([#]TUC) \ {p}) by Topen6
   .= Tcircle(0.REAL 2,1) | A by a3,dunit
   .= (TOP-REAL 2) | D | A1 by dc
   .= (TOP-REAL 2) | (D \ {p2}) by GOBOARD9:4;
  hence P,I(01) are_homeomorphic by LmcS1,EUCLID:58,BORSUK_4:77;
end;

theorem
  for p, q being Point of Tunit_circle(2) holds
  Topen_unit_circle(p), Topen_unit_circle(q) are_homeomorphic
proof 
  let p, q be Point of Tunit_circle(2);
  set P = Topen_unit_circle(p);
  set Q = Topen_unit_circle(q);
  set D = Sphere(0.REAL 2,p1);
  reconsider p2 = p, q2 = q as Point of TOP-REAL 2 by TOPMETR:2;
a3: [#]TUC = D by LmcS1,PRE_TOPC:12,EUCLID:58;
a5: D\{p} c= D & D\{q} c= D by XBOOLE_1:36;
  then reconsider A = D\{p}, B = D\{q} as Subset of Tcircle(0.REAL 2,1)
    by Tcarc;
  reconsider A1 = A, B1 = B as Subset of (TOP-REAL 2) | D by a5,JORDAN1:1;
a1: P = TUC | (([#]TUC) \ {p}) by Topen6
     .= Tcircle(0.REAL 2,1) | A by a3,dunit
     .= (TOP-REAL 2) | D | A1 by dc
     .= (TOP-REAL 2) | (D \ {p2}) by GOBOARD9:4;
  Q = TUC | (([#]TUC) \ {q}) by Topen6
     .= Tcircle(0.REAL 2,1) | B by a3,dunit
     .= (TOP-REAL 2) | D | B1 by dc
     .= (TOP-REAL 2) | (D \ {q2}) by GOBOARD9:4;
  hence P,Q are_homeomorphic by a1,LmcS1,EUCLID:58,BORSUK_4:78;
end;

begin :: Correspondence between the real line and circles

definition
  func CircleMap -> map of R^1, Tunit_circle(2) means :dcm:
  for x being real number holds it.x = |[ cos(2*PI*x), sin(2*PI*x) ]|;
existence
proof 
  defpred P[real number,set] means $2 = |[ cos(2*PI*$1), sin(2*PI*$1) ]|;
a1: for x being Element of R^1
   ex y being Element of cS1 st P[x,y]
  proof
    let x be Element of R^1;
    set y = |[ cos(2*PI*x), sin(2*PI*x) ]|;
    |. y - o .| = |. y .| by JORDAN2C:13,EUCLID:58
      .= sqrt((y`1)^2+(y`2)^2) by JGRAPH_1:47
      .= sqrt((cos(2*PI*x))^2+(y`2)^2) by EUCLID:56
      .= sqrt((cos(2*PI*x))^2+(sin(2*PI*x))^2) by EUCLID:56
      .= 1 by SIN_COS:32,SQUARE_1:83;
    then y is Element of cS1 by LmcS1,TOPREAL9:9;
    hence thesis;
  end;
  consider f being Function of R, cS1 such that
a2: for x being Element of R^1 holds P[x,f.x] from FUNCT_2:sch 3(a1);
  take f;
  let x be real number;
  x is Point of R^1 by TOPMETR:24,XREAL_0:def 1;
  hence thesis by a2;
end;
uniqueness
proof 
  let f, g be map of R^1, TUC such that
a1: for x being real number holds f.x = |[cos(2*PI*x),sin(2*PI*x)]|
  and
a2: for x being real number holds g.x = |[cos(2*PI*x),sin(2*PI*x)]|;
  for x being Point of R^1 holds f.x = g.x
  proof
    let x be Point of R^1;
    thus f.x = |[cos(2*PI*x),sin(2*PI*x)]| by a1
      .= g.x by a2;
  end;
  hence f = g by FUNCT_2:113;  
end;
end;

Lmd3: dom CircleMap = REAL by FUNCT_2:def 1,TOPMETR:24;

theorem Tri:
  CircleMap.r = CircleMap.(r+i)
proof 
  defpred P[Integer] means CircleMap.r = CircleMap.(r+$1);
a1: P[0];
a2: for i holds P[i] implies P[i-1] & P[i+1]
  proof
    let i such that
a3:   P[i];
    thus CircleMap.(r+(i-1)) = |[ cos(2*PI*(r+i-1)), sin(2*PI*(r+i-1)) ]|
    by dcm
    .= |[ cos(2*PI*(r+i)), sin(2*PI*(r+i)+2*PI*(-1)) ]| by COMPLEX2:10
    .= |[ cos(2*PI*(r+i)), sin(2*PI*(r+i)) ]| by COMPLEX2:9
    .= CircleMap.r by a3,dcm;
    thus CircleMap.(r+(i+1)) = |[ cos(2*PI*(r+i+1)), sin(2*PI*(r+i+1)) ]|
    by dcm
    .= |[ cos(2*PI*(r+i)), sin(2*PI*(r+i)+2*PI*1) ]| by COMPLEX2:10
    .= |[ cos(2*PI*(r+i)), sin(2*PI*(r+i)) ]| by COMPLEX2:9
    .= CircleMap.r by a3,dcm;
  end;
  for i holds P[i] from INT_1:sch 4(a1,a2);
  hence thesis;
end;

theorem Tc10a:
  CircleMap.i = c[10]
proof 
  thus CircleMap.i = |[ cos(2*PI*i+0), sin(2*PI*i) ]| by dcm
    .= |[ cos(0), sin(2*PI*i+0) ]| by COMPLEX2:10
    .= c[10] by dc10,COMPLEX2:9,SIN_COS:34;
end;

theorem Tpc:
  CircleMap"{c[10]} = INT
proof 
  hereby
    let i be set;
    assume
b1:   i in CircleMap"{c[10]};
    then CircleMap.i in {c[10]} by FUNCT_2:46;
    then
a0: CircleMap.i = c[10] by TARSKI:def 1;
    reconsider x = i as Real by b1,TOPMETR:24;
    |[ cos(2*PI*x), sin(2*PI*x) ]| = |[1,0]| by a0,dcm,dc10;
    then cos(2*PI*x) = 1 by SPPOL_2:1;
    hence i in INT by SIN_COS6:44;
  end;
  let i be set;
  assume i in INT;
  then reconsider i as Integer by INT_1:def 2;
a1: i in R by XREAL_0:def 1,TOPMETR:24;
  CircleMap.i = c[10] by Tc10a;
  then CircleMap.i in {c[10]} by TARSKI:def 1;
  hence thesis by a1,FUNCT_2:46;
end;

Lmcpi:
CircleMap.(1/2) = |[-1,0]|
proof 
  thus CircleMap.(1/2) = |[ cos(2*PI*(1/2)), sin(2*PI*(1/2)) ]| by dcm
  .= |[ cos(PI), sin(PI) ]|
  .= |[-1,0]| by SIN_COS:82;
end;

Lmcpi2:
CircleMap.1 = |[1,0]| by Tc10a,dc10;

theorem Tw2:
  frac r = 1/2 implies CircleMap.r = |[-1,0]|
proof 
  assume
a1: frac r = 1/2;
  thus CircleMap.r = CircleMap.(r+-[\r/]) by Tri
    .= CircleMap.(r-[\r/])
    .= |[-1,0]| by a1,Lmcpi,INT_1:def 6;
end;

theorem
  frac r = 1/4 implies CircleMap.r = |[0,1]|
proof 
  assume frac r = 1/4;
  then
a1: r-[\r/] = 1/4 by INT_1:def 6;
  thus CircleMap.r = CircleMap.(r+-[\r/]) by Tri
    .= CircleMap.(r-[\r/])
    .= |[ cos(2*PI*(1/4)), sin(2*PI*(1/4)) ]| by a1,dcm
    .= |[ cos(PI/2), sin(PI/2) ]|
    .= |[0,1]| by SIN_COS:82;
end;

theorem
  frac r = 3/4 implies CircleMap.r = |[0,-1]|
proof 
  assume frac r = 3/4;
  then
a1: r-[\r/] = 3/4 by INT_1:def 6;
  thus CircleMap.r = CircleMap.(r+-[\r/]) by Tri
    .= CircleMap.(r-[\r/])
    .= |[ cos(2*PI*(3/4)), sin(2*PI*(3/4)) ]| by a1,dcm
    .= |[0,-1]| by SIN_COS:82;
end;

Tcm:
  CircleMap.r = |[ (cos*AffineMap(2*PI,0)).r, (sin*AffineMap(2*PI,0)).r ]|
proof 
  thus CircleMap.r = |[ cos(2*PI*r+0), sin(2*PI*r) ]| by dcm
    .= |[ (cos*AffineMap(2*PI,0)).r, sin(2*PI*r+0) ]| by Tca
    .= |[ (cos*AffineMap(2*PI,0)).r, (sin*AffineMap(2*PI,0)).r ]| by Tsa;
end;

theorem
  for i, j being Integer holds
  CircleMap.r =
    |[ (cos*AffineMap(2*PI,2*PI*i)).r, (sin*AffineMap(2*PI,2*PI*j)).r ]|
proof 
  let i, j be Integer;
  thus CircleMap.r = |[ cos(2*PI*r+0), sin(2*PI*r) ]| by dcm
    .= |[ cos(2*PI*r+2*PI*i), sin(2*PI*r+0) ]| by COMPLEX2:10
    .= |[ cos(2*PI*r+2*PI*i), sin(2*PI*r+2*PI*j) ]| by COMPLEX2:9
    .= |[ (cos*AffineMap(2*PI,2*PI*i)).r, sin(2*PI*r+2*PI*j) ]| by Tca
    .= |[ (cos*AffineMap(2*PI,2*PI*i)).r, (sin*AffineMap(2*PI,2*PI*j)).r ]|
       by Tsa;
end;

registration
  cluster CircleMap -> continuous;
coherence 
proof 
r1: sin = R^1(sin) by dr3;
r2: cos = R^1(cos) by dr3;
r3: AffineMap(2*PI,0) = R^1(AffineMap(2*PI,0)) by dr3;
  dom sin = REAL by SIN_COS:def 20;
  then
ww: R^1|(R^1 dom sin) = R^1 by Lmdom1;
  dom cos = REAL by SIN_COS:def 22;
  then
ee: R^1|(R^1 dom cos) = R^1 by Lmdom1;
  set sR = R^1(sin), cR = R^1(cos);
  reconsider sR, cR as map of R^1,R^1 by dr3,Lmmap10,Lmmap1;
  reconsider l = AffineMap(2*PI,0) as map of R^1,R^1 by TR1b;
  dom AffineMap(2*PI,0) = REAL by FUNCT_2:def 1;
  then
cc: R^1|(R^1 dom AffineMap(2*PI,0)) = R^1 by Lmdom1;
gg: rng AffineMap(2*PI,0) = REAL by JORDAN16:32
    .= [#]REAL by SUBSET_1:def 4;
  set s = sR*l;
  set c = cR*l;
  reconsider g = CircleMap as map of R^1,TOP-REAL 2 by TOPREALA:28;
  for p being Point of R^1, V being Subset of TOP-REAL 2
   st g.p in V & V is open
  ex W being Subset of R^1 st p in W & W is open & g.:W c= V
  proof
    let p be Point of R^1,
        V be Subset of TOP-REAL 2 such that
a1:   g.p in V and
a2:   V is open;
a4: V = Int V by a2,TOPS_1:55;
    reconsider e = g.p as Point of Euclid 2 by TOPREAL3:13;
    consider r being real number such that
a5:   r > 0 and
a6:   Ball(e,r) c= V by a1,a4,GOBOARD6:8;
    set A = ].(g.p)`1-r/sqrt 2,(g.p)`1+r/sqrt 2.[;
    set B = ].(g.p)`2-r/sqrt 2,(g.p)`2+r/sqrt 2.[;
    set F = (1,2) --> (A,B);
    sqrt 2 > 0 by SQUARE_1:84;
    then
b4: r/sqrt 2 > 0 by a5,REAL_2:127;
d1: product F c= Ball(e,r) by TOPREAL6:49;
    reconsider A, B as Subset of R^1 by TOPMETR:24;
a10: B is open by JORDAN6:46;
b3: g.p = |[ c.p, s.p ]| by r1,r2,Tcm;
    then
c1: (g.p)`2 = s.p by EUCLID:56;
    sR is continuous by ww,TOPMETR:7;
    then
yu: s is continuous by gg,r3,cc,Lmdom1a,TOPS_2:58;
    s.p in B by c1,b4,TOPREAL6:20;
    then consider Ws being Subset of R^1 such that
a7:   p in Ws and
a8:   Ws is open and
a9:   s.:Ws c= B by yu,a10,JGRAPH_2:20;
a12: A is open by JORDAN6:46;
c4: (g.p)`1 = c.p by b3,EUCLID:56;
    cR is continuous by ee,TOPMETR:7;
    then
yy: c is continuous by gg,r3,cc,Lmdom1a,TOPS_2:58;
    c.p in A by c4,b4,TOPREAL6:20;
    then consider Wc being Subset of R^1 such that
a14:  p in Wc and
a15:  Wc is open and
a16:  c.:Wc c= A by yy,a12,JGRAPH_2:20;
    set W = Ws /\ Wc;
    take W;
    thus p in W by a7,a14,XBOOLE_0:def 3;
    thus W is open by a8,a15,TOPS_1:38;
    let y be set;
    assume y in g.:W;
    then consider x being Element of R^1 such that
a17:  x in W and
a18:  y = g.x by FUNCT_2:116;
a19: g.x = |[ c.x, s.x ]| by r1,r2,Tcm;
    x in Ws by a17,XBOOLE_0:def 3;
    then
s2: s.x in s.:Ws by FUNCT_2:43;
    x in Wc by a17,XBOOLE_0:def 3;
    then
s1: c.x in c.:Wc by FUNCT_2:43;
    |[c.x,s.x]| = (1,2) --> (c.x,s.x) by TOPREALA:49;
    then |[c.x,s.x]| in product F by s1,s2,a9,a16,HILBERT3:11;
    then |[c.x,s.x]| in Ball(e,r) by d1;
    hence y in V by a6,a18,a19;
  end;
  then g is continuous by JGRAPH_2:20;
  hence thesis by TOPMETR:8;
end;
end;

Ta1:
  for A being Subset of R^1 holds CircleMap|A is map of R^1|A, Tunit_circle(2)
proof 
  let A be Subset of R^1;
a2: dom (CircleMap|A) = A by TOPMETR:24,Lmd3,RELAT_1:91
  .= the carrier of R^1|A by JORDAN1:1;
  rng (CircleMap|A) c= cS1;
  hence CircleMap|A is map of R^1|A, TUC by a2,FUNCT_2:4;
end;

Lm20:
-1 <= r & r <= 1 implies 0 <= arccos r/(2*PI) & arccos r/(2*PI) <= 1/2
proof 
  assume -1 <= r & r <= 1;
  then 0 <= arccos r & arccos r <= PI by SIN_COS6:101;
  then 0/(2*PI) <= arccos r/(2*PI) &
    arccos r/(2*PI) <= 1*PI/(2*PI) by REAL_1:73;
  hence thesis by XCMPLX_1:92;
end;

theorem Ta4:
  for A being Subset of R^1, f being map of R^1|A, Tunit_circle(2)
   st [.0,1.[ c= A & f = CircleMap | A
  holds f is onto
proof 
  let A be Subset of R^1,
      f be map of R^1|A, Tunit_circle(2) such that
a1: [.0,1.[ c= A and
a2: f = CircleMap | A;
  thus rng f c= cS1;
  let y be set;
  assume
c1: y in cS1;
  then reconsider z = y as Point of TOP-REAL 2 by TOPMETR:2;
  set z1 = z`1, z2 = z`2;
  set x = arccos z1/(2*PI);
a3: dom f = A by a2,Lmd3,TOPMETR:24,RELAT_1:91;
a6: |. z .| = 1 by c1,Tu1;
a7: z1^2 + z2^2 = |. z .|^2 by JGRAPH_1:46;
a5: -1 <= z1 & z1 <= 1 by c1,Tu2;
b3: 0 <= x by a5,Lm20;
   x <= 1/2 by a5,Lm20;
   then
b4: x < 1 by AXIOMS:22;
  per cases;
  suppose
a8: z2 < 0;
b2: now
    assume x = 0;
    then arccos z1 = 0 by XCMPLX_1:50;
    then z1 = 1 by a5,SIN_COS6:98;
    then 1 + z2^2 - 1 = 0 by a6,a7,SQUARE_1:59;
    hence contradiction by a8,SQUARE_1:73;
  end;
  1-0 > 1-x & 1-x > 1-1 by b2,b3,b4,REAL_1:92;
  then
a4: 1-x in [.0,1.[ by RCOMP_2:3;
  then f.(1-x) = CircleMap.(-x+1) by a1,a2,FUNCT_1:72
    .= CircleMap.-x by Tri
    .= |[cos(-2*PI*x), sin(2*PI*(-x))]| by dcm
    .= |[cos(2*PI*x), sin(2*PI*(-x))]| by SIN_COS:34
    .= |[cos arccos z1, sin(-2*PI*x)]| by XCMPLX_1:88
    .= |[cos arccos z1, -sin(2*PI*x)]| by SIN_COS:34
    .= |[cos arccos z1, -sin arccos z1]| by XCMPLX_1:88
    .= |[z1, -sin arccos z1]| by a5,SIN_COS6:93
    .= |[z1, --z2]| by a6,a7,a8,SIN_COS6:105,SQUARE_1:59
    .= y by EUCLID:57;
  hence thesis by a1,a3,a4,FUNCT_1:def 5;
  end;
  suppose
a8: z2 >= 0;
a4: x in [.0,1.[ by b3,b4,RCOMP_2:3;
  then f.x = CircleMap.x by a1,a2,FUNCT_1:72
    .= |[cos(2*PI*x), sin(2*PI*x)]| by dcm
    .= |[cos arccos z1, sin(2*PI*x)]| by XCMPLX_1:88
    .= |[cos arccos z1, sin arccos z1]| by XCMPLX_1:88
    .= |[z1, sin arccos z1]| by a5,SIN_COS6:93
    .= |[z1, z2]| by a6,a7,a8,SIN_COS6:104,SQUARE_1:59
    .= y by EUCLID:57;
  hence thesis by a1,a3,a4,FUNCT_1:def 5;
  end;
end;

registration
  cluster CircleMap -> onto;
coherence
proof 
a0: [#]R^1 = REAL by TOPMETR:24,PRE_TOPC:12;
a2: R^1 | [#]R^1 = R^1 by TSEP_1:3;
  CircleMap|REAL = CircleMap by Lmd3,RELAT_1:98;
  hence thesis by a0,a2,Ta4;
end;
end;

Lmc11:
CircleMap | [.0,1.[ is one-to-one
proof 
  set f = CircleMap | [.0,1.[;
ad: dom f = [.0,1.[ by Lmd3,RELAT_1:91;
  let x1, y1 be set;
  assume
ax: x1 in dom f;
  then reconsider x = x1 as Real by ad;
  assume
ay: y1 in dom f;
  then reconsider y = y1 as Real by ad;
  assume
ar: f.x1 = f.y1;
a1: f.x = CircleMap.x by ad,ax,FUNCT_1:72
     .= |[cos(2*PI*x),sin(2*PI*x)]| by dcm;
a2: f.y = CircleMap.y by ad,ay,FUNCT_1:72
     .= |[cos(2*PI*y),sin(2*PI*y)]| by dcm;
  then
a4: cos(2*PI*x) = cos(2*PI*y) by ar,a1,SPPOL_2:1;
a3: sin(2*PI*x) = sin(2*PI*y) by ar,a1,a2,SPPOL_2:1;
d1: sin(2*PI*x) = sin.(2*PI*x) by SIN_COS:def 21;
d2: sin(2*PI*y) = sin.(2*PI*y) by SIN_COS:def 21;
d3: cos(2*PI*x) = cos.(2*PI*x) by SIN_COS:def 23;
d4: cos(2*PI*y) = cos.(2*PI*y) by SIN_COS:def 23;
z1: [.0,PI/2.] c= [.-PI/2,PI/2.] by TREAL_1:1;
t5: sin | [.PI/2,3/2*PI.] is one-to-one;
  per cases by ad,ax,RCOMP_2:3;
  suppose 0 <= x & x <= 1/4;
  then 2*PI*0 <= 2*PI*x & 2*PI*x <= 2*PI*(1/4) by AXIOMS:25;
  then
c1: 2*PI*x in [.0,2*PI*1/4.] by TOPREAL5:1;
  then
e1: cos(2*PI*x) >= 0 by z1,d3,COMPTRIG:28;
  [.0,PI/2.] c= [.0,PI.] by Lmp0,TREAL_1:1;
  then
e2: sin(2*PI*x) >= 0 by c1,d1,COMPTRIG:24;
  per cases by ad,ay,RCOMP_2:3;
  suppose 0 <= y & y <= 1/4;
  then 2*PI*0 <= 2*PI*y & 2*PI*y <= 2*PI*(1/4) by AXIOMS:25;
  then
c2: 2*PI*y in [.0,2*PI*1/4.] by TOPREAL5:1;
  set g = sin | [.0,PI/2.];
b2: dom g = [.0,PI/2.] by Lmds,RELAT_1:91;
  g.(2*PI*x) = sin.(2*PI*x) by c1,FUNCT_1:72
     .= sin.(2*PI*y) by a3,d1,SIN_COS:def 21
     .= g.(2*PI*y) by c2,FUNCT_1:72;
  then 2*PI*x = 2*PI*y by c1,c2,b2,FUNCT_1:def 8;
  then x = 2*PI*y/P2 by XCMPLX_1:90;
  hence thesis by XCMPLX_1:90;
  end;
  suppose 1/4 < y & y < 3/4;
  then 2*PI*(1/4) < 2*PI*y & 2*PI*y < 2*PI*(3/4) by REAL_1:70;
  then 2*PI*y in ].PI/2,3/2*PI.[ by JORDAN6:45;
  hence thesis by a4,e1,d4,COMPTRIG:29;
  end;
  suppose 3/4 <= y & y < 1;
  then 2*PI*(3/4) <= 2*PI*y & 2*PI*y < 2*PI*1 by REAL_1:70,AXIOMS:25;
  then
c2: 2*PI*y in [.3/2*PI,2*PI.[ by RCOMP_2:3;
  [.3/2*PI,2*PI.[ c= ].PI,2*PI.[ by Lmp5,TOPREALA:14;
  hence thesis by a3,e2,c2,d2,COMPTRIG:25;
  end;
  end;
  suppose 1/4 < x & x <= 1/2;
  then 2*PI*(1/4) < 2*PI*x & 2*PI*x <= 2*PI*(1/2) by REAL_1:70,AXIOMS:25;
  then
c1: 2*PI*x in ].PI/2,2*PI*(1/2).] by RCOMP_2:4;
  ].PI/2,PI.] c= ].PI/2,3/2*PI.[ by Lmp5,TOPREALA:18;
  then
f1: cos(2*PI*x) < 0 by d3,c1,COMPTRIG:29;
  ].PI/2,PI.] c= [.0,PI.] by TOPREALA:15;
  then
f2: sin(2*PI*x) >= 0 by c1,d1,COMPTRIG:24;
  per cases by ad,ay,RCOMP_2:3;
  suppose 0 <= y & y <= 1/4;
  then 2*PI*0 <= 2*PI*y & 2*PI*y <= 2*PI*(1/4) by AXIOMS:25;
  then 2*PI*y in [.0,2*PI*(1/4).] by TOPREAL5:1;
  hence thesis by f1,a4,d4,z1,COMPTRIG:28;
  end;
  suppose 1/4 < y & y <= 1/2;
  then 2*PI*(1/4) < 2*PI*y & 2*PI*y <= 2*PI*(1/2) by REAL_1:70,AXIOMS:25;
  then
c2: 2*PI*y in ].2*PI*(1/4),2*PI*(1/2).] by RCOMP_2:4;
  set g = sin | ].PI/2,PI.];
  ].PI/2,PI.] c= [.PI/2,3/2*PI.] by Lmp5,TOPREALA:15;
  then
b1: g is one-to-one by t5,SIN_COS6:2;
b2: dom g = ].PI/2,PI.] by Lmds,RELAT_1:91;
  g.(2*PI*x) = sin.(2*PI*x) by c1,FUNCT_1:72
     .= sin.(2*PI*y) by a3,d1,SIN_COS:def 21
     .= g.(2*PI*y) by c2,FUNCT_1:72;
  then 2*PI*x = 2*PI*y by c1,c2,b1,b2,FUNCT_1:def 8;
  then x = 2*PI*y/P2 by XCMPLX_1:90;
  hence thesis by XCMPLX_1:90;
  end;
  suppose 1/2 < y & y < 1;
  then 2*PI*(1/2) < 2*PI*y & 2*PI*y < 2*PI*1 by REAL_1:70;
  then 2*PI*y in ].PI,2*PI.[ by JORDAN6:45;
  hence thesis by f2,a3,d2,COMPTRIG:25;
  end;
  end;
  suppose 1/2 < x & x <= 3/4;
  then 2*PI*(1/2) < 2*PI*x & 2*PI*x <= 2*PI*(3/4) by REAL_1:70,AXIOMS:25;
  then
c1: 2*PI*x in ].PI,2*PI*(3/4).] by RCOMP_2:4;
  ].PI,3/2*PI.] c= ].PI,2*PI.[ by Lmp6,TOPREALA:18;
  then
f2: sin(2*PI*x) < 0 by c1,d1,COMPTRIG:25;
  ].PI,3/2*PI.] c= [.PI/2,3/2*PI.] by Lmp0,TOPREALA:15;
  then
f3: cos(2*PI*x) <= 0 by c1,d3,COMPTRIG:30;
  per cases by ad,ay,RCOMP_2:3;
  suppose 0 <= y & y <= 1/2;
  then 2*PI*0 <= 2*PI*y & 2*PI*y <= 2*PI*(1/2) by AXIOMS:25;
  then 2*PI*y in [.0,PI.] by TOPREAL5:1;
  hence thesis by f2,a3,d2,COMPTRIG:24;
  end;
  suppose 1/2 < y & y <= 3/4;
  then 2*PI*(1/2) < 2*PI*y & 2*PI*y <= 2*PI*(3/4) by REAL_1:70,AXIOMS:25;
  then
c2: 2*PI*y in ].PI,2*PI*(3/4).] by RCOMP_2:4;
  set g = sin | ].PI,3/2*PI.];
  ].PI,3/2*PI.] c= [.PI/2,3/2*PI.] by Lmp0,TOPREALA:15;
  then
b1: g is one-to-one by t5,SIN_COS6:2;
b2: dom g = ].PI,3/2*PI.] by Lmds,RELAT_1:91;
  g.(2*PI*x) = sin.(2*PI*x) by c1,FUNCT_1:72
     .= sin.(2*PI*y) by a3,d1,SIN_COS:def 21
     .= g.(2*PI*y) by c2,FUNCT_1:72;
  then 2*PI*x = 2*PI*y by c1,c2,b1,b2,FUNCT_1:def 8;
  then x = 2*PI*y/P2 by XCMPLX_1:90;
  hence thesis by XCMPLX_1:90;
  end;
  suppose 3/4 < y & y < 1;
  then 2*PI*(3/4) < 2*PI*y & 2*PI*y < 2*PI*1 by REAL_1:70;
  then 2*PI*y in ].3/2*PI,2*PI.[ by JORDAN6:45;
  hence thesis by f3,a4,d4,COMPTRIG:31;
  end;
  end;
  suppose 3/4 < x & x < 1;
  then 2*PI*(3/4) < 2*PI*x & 2*PI*x < 2*PI*1 by REAL_1:70;
  then
c1: 2*PI*x in ].3/2*PI,2*PI.[ by JORDAN6:45;
  ].3/2*PI,2*PI.[ c= ].PI,2*PI.[ by Lmp5,JGRAPH_3:9;
  then
f3: sin(2*PI*x) < 0 by c1,d1,COMPTRIG:25;
f1: cos(2*PI*x) > 0 by c1,d3,COMPTRIG:31;
  per cases by ad,ay,RCOMP_2:3;
  suppose 0 <= y & y <= 1/2;
  then 2*PI*0 <= 2*PI*y & 2*PI*y <= 2*PI*(1/2) by AXIOMS:25;
  then 2*PI*y in [.0,PI.] by TOPREAL5:1;
  hence thesis by f3,a3,d2,COMPTRIG:24;
  end;
  suppose 1/2 < y & y <= 3/4;
  then 2*PI*(1/2) < 2*PI*y & 2*PI*y <= 2*PI*(3/4) by REAL_1:70,AXIOMS:25;
  then
k1: 2*PI*y in ].PI,3/2*PI.] by RCOMP_2:4;
  ].PI,3/2*PI.] c= [.PI/2,3/2*PI.] by Lmp0,TOPREALA:15;
  hence thesis by k1,f1,a4,d4,COMPTRIG:30;
  end;
  suppose 3/4 < y & y < 1;
  then 2*PI*(3/4) < 2*PI*y & 2*PI*y < 2*PI*1 by REAL_1:70;
  then
c2: 2*PI*y in ].3/2*PI,2*PI.[ by JORDAN6:45;
  set g = sin | ].3/2*PI,2*PI.[;
b2: dom g = ].3/2*PI,2*PI.[ by Lmds,RELAT_1:91;
  g.(2*PI*x) = sin.(2*PI*x) by c1,FUNCT_1:72
     .= sin.(2*PI*y) by a3,d1,SIN_COS:def 21
     .= g.(2*PI*y) by c2,FUNCT_1:72;
  then 2*PI*x = 2*PI*y by c1,c2,b2,FUNCT_1:def 8;
  then x = 2*PI*y/P2 by XCMPLX_1:90;
  hence thesis by XCMPLX_1:90;
  end;
  end;
end;

registration
  let r be real number;
  cluster CircleMap | [.r,r+1.[ -> one-to-one;
coherence
proof 
  set f = CircleMap | [.r,r+1.[;
  set g = CircleMap | [.0,1.[;
ad: dom f = [.r,r+1.[ by Lmd3,RELAT_1:91;
ag: dom g = [.0,1.[ by Lmd3,RELAT_1:91;
  let x, y be set;
  assume that
ax: x in dom f and
ay: y in dom f and
ar: f.x = f.y;
  reconsider x, y as Real by ad,ax,ay;
  set x1 = frac x;
  set y1 = frac y;
a4: x1 = x-[\x/] by INT_1:def 6;
a5: y1 = y-[\y/] by INT_1:def 6;
   0 <= x1 & x1 < 1 by INT_1:69;
   then
a1: x1 in [.0,1.[ by RCOMP_2:3;
   0 <= y1 & y1 < 1 by INT_1:69;
   then
a2: y1 in [.0,1.[ by RCOMP_2:3;
a6: r <= x & x < r+1 by ad,ax,RCOMP_2:3;
a7: r <= y & y < r+1 by ad,ay,RCOMP_2:3;
a3: f.x = CircleMap.x by ax,FUNCT_1:70
     .= CircleMap.(x+-[\x/]) by Tri
     .= g.x1 by a1,ag,a4,FUNCT_1:70;
  f.y = CircleMap.y by ay,FUNCT_1:70
     .= CircleMap.(y+-[\y/]) by Tri
     .= g.y1 by a2,ag,a5,FUNCT_1:70;
  then x1 = y1 by a1,a2,a3,ar,ag,Lmc11,FUNCT_1:def 8; 
  hence thesis by a6,a7,TOPREALA:7;
end;
end;

registration
  let r be real number;
  cluster CircleMap | ].r,r+1.[ -> one-to-one;
coherence
proof 
a1: CircleMap | [.r,r+1.[ is one-to-one;
  ].r,r+1.[ c= [.r,r+1.[ by TOPREALA:20;
  hence thesis by a1,SIN_COS6:2;
end;
end;

theorem TTa2:
  b-a <= 1 implies
  for d being set st d in IntIntervals(a,b) holds CircleMap|d is one-to-one
proof 
  assume that
a6: b-a <= 1;
  let d be set;
  assume d in IntIntervals(a,b);
  then consider n being Element of INT such that
a1: d = ].a+n,b+n.[ by Texci;
a2: CircleMap | [.a+n,a+n+1.[ is one-to-one;
  b-a+(a+n) <= 1+(a+n) by a6,AXIOMS:24;
  then ].a+n,b+n.[ c= [.a+n,a+n+1.[ by TOPREALA:20;
  hence thesis by a1,a2,SIN_COS6:2;
end;

theorem TTa1:
  for d being set st d in IntIntervals(a,b)
  holds CircleMap.:d = CircleMap.:union IntIntervals(a,b)
proof 
  set D = IntIntervals(a,b);
  let d be set;
  assume
a0: d in D;
  then d c= union D by ZFMISC_1:92;
  hence CircleMap.:d c= CircleMap.:union D by RELAT_1:156;
  let y be set;
  assume y in CircleMap.:union D;
  then consider x being Element of R^1 such that
a1:  x in union D and
a2:  y = CircleMap.x by FUNCT_2:116;
  consider Z being set such that
a3: x in Z and
a4: Z in D by a1,TARSKI:def 4;
  consider n being Element of INT such that
a5: Z = ]. a+n, b+n .[ by a4,Texci;
  consider i being Element of INT such that
a6: d = ]. a+i, b+i .[ by a0,Texci;
  set k = x+i-n;
a7: k in R by TOPMETR:24,XREAL_0:def 1;
  a+n < x & x < b+n by a3,a5,JORDAN6:45;
  then a+n+i < x+i & x+i < b+n+i by REAL_1:53;
  then a+n+i-n < x+i-n & x+i-n < b+n+i-n by REAL_1:54;
  then
a8: k in d by a6,JORDAN6:45;
  CircleMap.k = CircleMap.(x+(i-n))
    .= y by a2,Tri;
  hence y in CircleMap.:d by a7,a8,FUNCT_2:43;
end;

definition
  let r be Point of R^1;
  func CircleMap(r) ->
       map of R^1 | R^1(].r,r+1.[), Topen_unit_circle(CircleMap.r) equals
:def10:
  CircleMap | ].r,r+1.[;
coherence
proof 
  set X = Topen_unit_circle(CircleMap.r);
  set A = ].r,r+1.[;
  set B = [.r,r+1.[;
  set f = CircleMap | A;
  set g = CircleMap | B;
a0: A c= B by TOPREALA:20;
a1: dom f = A by Lmd3,RELAT_1:91;
a3: the carrier of X = cS1 \ {CircleMap.r} by dop;
a2: rng f c= the carrier of X
  proof
    let y be set;
    assume
a6:   y in rng f;
    now
      assume
a12:    y = CircleMap.r;
      consider x being set such that
a7:     x in dom f and
a8:     f.x = y by a6,FUNCT_1:def 5;
      r+0 < r+1 by REAL_1:67;
      then
a10:  r in B by RCOMP_2:3;
a11:  dom g = B by Lmd3,RELAT_1:91;
      g.x = CircleMap.x by a0,a1,a7,FUNCT_1:72
         .= CircleMap.r by a12,a8,a1,a7,FUNCT_1:72
         .= g.r by a10,FUNCT_1:72;
      then x = r by a0,a1,a7,a10,a11,FUNCT_1:def 8;
      hence contradiction by a1,a7,JORDAN6:45;
    end;
    then not y in {CircleMap.r} by TARSKI:def 1;
    hence thesis by a3,a6,XBOOLE_0:def 4;
  end;
  the carrier of R^1 | R^1(A) = R^1(A) by JORDAN1:1
    .= A by dr2;
  hence thesis by a1,a2,FUNCT_2:4;
end;
end;

LmAff2:
rng (AffineMap(1,-a) | ].r+a,r+a+1.[) = ].r,r+1.[
proof 
  set F = AffineMap(1,-a);
  set f = F | ].r+a,r+a+1.[;
  dom F = REAL by FUNCT_2:def 1;
  then
a3: dom f = ].r+a,r+a+1.[ by RELAT_1:91;
  thus rng f = ].r,r+1.[
  proof
    thus rng f c= ].r,r+1.[
    proof
      let y be set;
      assume y in rng f;
      then consider x being set such that
a4:     x in dom f and
a5:     f.x = y by FUNCT_1:def 5;
      reconsider x as Real by a4;
a7:   y = F.x by a4,a5,FUNCT_1:70
       .= 1*x+-a by JORDAN16:def 3;
      r+a < x & x < r+a+1 by a3,a4,JORDAN6:45;
      then r+a-a < x-a & x-a < r+a+1-a by REAL_1:54;
      hence thesis by a7,JORDAN6:45;
    end;
    let y be set;
    assume
m1:   y in ].r,r+1.[;
    then reconsider y as Real;
    r < y & y < r+1 by m1,JORDAN6:45;
    then r+a < y+a & y+a < r+1+a by REAL_1:53;
    then
m2: y+a in ].r+a,r+a+1.[ by JORDAN6:45;
    then f.(y+a) = F.(y+a) by FUNCT_1:72
      .= 1*(y+a)+-a by JORDAN16:def 3
      .= y;
    hence thesis by m2,a3,FUNCT_1:def 5;
  end;
end;

theorem Tzl1m:
  CircleMap(R^1(a+i)) = CircleMap(R^1(a)) * (AffineMap(1,-i) | ].a+i,a+i+1.[)
proof 
  set W = ].a,a+1.[;
  set Q = ].a+i,a+i+1.[;
  set h = CircleMap(R^1(a+i));
  set g = CircleMap(R^1(a));
  set F = AffineMap(1,-i);
  set f = F|Q;
  a+i = R^1(a+i) by dr1;
  then
Z3: h = CircleMap|Q by def10;
  then
a1: dom h = Q by Lmd3,RELAT_1:91;
  a = R^1(a) by dr1;
  then
Z2: g = CircleMap|W  by def10;
  then
a6: dom g = W by Lmd3,RELAT_1:91;
  dom F = REAL by FUNCT_2:def 1;
  then
a3: dom f = Q by RELAT_1:91;
  rng f = ].a,a+1.[ by LmAff2;
  then
a2: dom (g*f) = dom f by a6,RELAT_1:46;
  for x being set st x in dom h holds h.x = (g*f).x
  proof
    let x be set;
    assume
Z0:   x in dom h;
    reconsider y = x as Real by a1,Z0;
    a+i < y & y < a+i+1 by a1,Z0,JORDAN6:45;
    then a+i-i < y-i & y-i < a+i+1-i by REAL_1:54;
    then
Z1: y-i in W by JORDAN6:45;
    thus (g*f).x = g.(f.x) by a1,a3,Z0,FUNCT_1:23
      .= g.(F.x) by a1,Z0,FUNCT_1:72
      .= g.(1*y+-i) by JORDAN16:def 3
      .= CircleMap.(y+-i) by Z1,Z2,FUNCT_1:72
      .= CircleMap.y by Tri
      .= h.x by a1,Z0,Z3,FUNCT_1:72;
  end;
  hence thesis by a1,a2,a3,FUNCT_1:9;
end;

registration
  let r be Point of R^1;
  cluster CircleMap(r) -> one-to-one onto continuous;
coherence
proof 
a1: ].r+0,r+1+0.[ in IntIntervals(r,r+1) by OO,Texci;
  r+1-r = 1;
  then CircleMap | ].r+0,r+1+0.[ is one-to-one by a1,TTa2;
  hence CircleMap(r) is one-to-one by def10;
  thus CircleMap(r) is onto
  proof
  set A = ].r,r+1.[;
  set f = CircleMap | A;
k1: f = CircleMap(r) by def10;
  set TOUC = Topen_unit_circle(CircleMap.r);
  set X = the carrier of TOUC;
  thus rng CircleMap(r) c= X;
  let y be set;
  assume
c1: y in X;
  reconsider z = y as Point of TOP-REAL 2 by c1,Lm50;
  set z1 = z`1, z2 = z`2;
  set x = arccos z1/(2*PI);
e6: [\r/] <= r by INT_1:def 4;
a3: dom f = ].r,r+1.[ by Lmd3,RELAT_1:91;
  z is Point of TUC by c1,TOPMETR:2;
  then
a6: |. z .| = 1 by Tu1;
a7: z1^2 + z2^2 = |. z .|^2 by JGRAPH_1:46;
a5: -1 <= z1 & z1 <= 1 by c1,Tu3;
b3: 0 <= x by a5,Lm20;
   x <= 1/2 by a5,Lm20;
   then
b4: x < 1 by AXIOMS:22;
e10: x-x < 1-x by b4,REAL_1:92;
  per cases;
  suppose
a8: z2 < 0;
i2: CircleMap.-x = |[cos(-2*PI*x), sin(2*PI*(-x))]| by dcm
    .= |[cos(2*PI*x), sin(2*PI*(-x))]| by SIN_COS:34
    .= |[cos arccos z1, sin(-2*PI*x)]| by XCMPLX_1:88
    .= |[cos arccos z1, -sin(2*PI*x)]| by SIN_COS:34
    .= |[cos arccos z1, -sin arccos z1]| by XCMPLX_1:88
    .= |[z1, -sin arccos z1]| by a5,SIN_COS6:93
    .= |[z1, --z2]| by a6,a7,a8,SIN_COS6:105,SQUARE_1:59
    .= y by EUCLID:57;
  per cases;
  suppose
a4: 1-x+[\r/] in A;
  then f.(1-x+[\r/]) = CircleMap.(-x+([\r/]+1)) by FUNCT_1:72
    .= CircleMap.-x by Tri;
  hence thesis by a3,a4,k1,i2,FUNCT_1:def 5;
  end;
  suppose
g4: not 1-x+[\r/] in A;
  now
    assume x = 0;
    then arccos z1 = 0 by XCMPLX_1:50;
    then z1 = 1 by a5,SIN_COS6:98;
    then 1 + z2^2 - 1 = 0 by a6,a7,SQUARE_1:59;
    hence contradiction by a8,SQUARE_1:73;
  end;
  then [\r/]-x < r-0 by e6,b3,REAL_1:92;
  then -x+[\r/]+1 < r+1 by REAL_1:53;
  then
e12: r >= 1-x+[\r/] by g4,JORDAN6:45;
e13: now
    assume -x+[\r/]+1 = r;
    then CircleMap.r = CircleMap.(-x+([\r/]+1))
      .= CircleMap.-x by Tri;
    hence contradiction by c1,i2,Tpnp;
  end;
e11: r < [\r/]+1 by INT_1:52;
  [\r/]+1+0 < [\r/]+1+(1-x) by e10,REAL_1:53;
  then
g1: r < 2-x+[\r/] by e11,AXIOMS:22;
  1-x+[\r/] < r by e12,e13,REAL_1:def 5;
  then 1-x+[\r/]+1 < r+1 by REAL_1:53;
  then
a4: -x+[\r/]+2 in A by g1,JORDAN6:45;
  then f.(-x+([\r/]+2)) = CircleMap.(-x+([\r/]+2)) by FUNCT_1:72
    .= CircleMap.-x by Tri;
  hence thesis by a3,a4,k1,i2,FUNCT_1:def 5;
  end;
  end;
  suppose
a8: z2 >= 0;
i3: CircleMap.x = |[cos(2*PI*x), sin(2*PI*x)]| by dcm
    .= |[cos arccos z1, sin(2*PI*x)]| by XCMPLX_1:88
    .= |[cos arccos z1, sin arccos z1]| by XCMPLX_1:88
    .= |[z1, sin arccos z1]| by a5,SIN_COS6:93
    .= |[z1, z2]| by a6,a7,a8,SIN_COS6:104,SQUARE_1:59
    .= y by EUCLID:57;
  per cases;
  suppose
a4: x+[\r/] in A;
  then f.(x+[\r/]) = CircleMap.(x+[\r/]) by FUNCT_1:72
    .= CircleMap.x by Tri;
  hence thesis by a3,a4,k1,i3,FUNCT_1:def 5;
  end;
  suppose
g4: not x+[\r/] in A;
g3: r < [\r/]+1 by INT_1:52;
  0+([\r/]+1) <= x+([\r/]+1) by b3,AXIOMS:24;
  then
g1: r < x+[\r/]+1 by g3,AXIOMS:22;
  x+[\r/] < 1+r by b4,e6,REAL_1:67;
  then x+[\r/] <= r by g4,JORDAN6:45;
  then
g5: x+[\r/]+1 <= r+1 by AXIOMS:24;
  now
    assume x+[\r/]+1 = r+1;
    then CircleMap.r = CircleMap.(x+[\r/]) by XCMPLX_1:2
      .= CircleMap.x by Tri;
    hence contradiction by c1,i3,Tpnp;
  end;
  then x+[\r/]+1 < r+1 by g5,REAL_1:def 5;
  then
a4: x+[\r/]+1 in A by g1,JORDAN6:45;
  then f.(x+[\r/]+1) = CircleMap.(x+([\r/]+1)) by FUNCT_1:72
    .= CircleMap.x by Tri;
  hence thesis by a3,a4,k1,i3,FUNCT_1:def 5;
  end;
  end;
end;
a1: R^1(].r,r+1.[) = ].r,r+1.[ by dr2;
a2: Topen_unit_circle(CircleMap.r) =
    (Tunit_circle(2)) | (([#]Tunit_circle(2)) \ {CircleMap.r}) by Topen6;
  CircleMap(r) = CircleMap | ].r,r+1.[ by def10;
  hence thesis by a1,a2,TOPREALA:29;
end;
end;

definition
  func Circle2IntervalR -> map of Topen_unit_circle(c[10]), R^1 | R^1(].0,1.[)
    means :dc2i:
  for p being Point of Topen_unit_circle(c[10])
    ex x, y being real number st p = |[x,y]| &
    (y >= 0 implies it.p = arccos x/(2*PI)) &
    (y <= 0 implies it.p = 1-arccos x/(2*PI));
existence
proof 
b20: A = ].0,0+p1.[ by dr2;
  reconsider A as non empty Subset of R^1;
  defpred P[set,set] means
    ex x, y being real number st $1 = |[x,y]| &
    (y >= 0 implies $2 = arccos x/P2) &
    (y <= 0 implies $2 = 1-arccos x/P2);
b6: Y = A by JORDAN1:1;
c1: for x being Element of X ex y being Element of Y st P[x,y]
  proof
    let x be Element of X;
c2: X = cS1 \ {c[10]} by dop;
    then
c4: x in cS1 by XBOOLE_0:def 4;
c11: cS1 is Subset of the carrier of TOP-REAL 2 by TSEP_1:1;
    then consider a, b being Real such that
c3:   x = <*a,b*> by c4,EUCLID:55;
c8: not x in {c[10]} by c2,XBOOLE_0:def 4;
    set k = arccos a;
    reconsider x1 = x as Point of TOP-REAL 2 by c4,c11;
c51: x = |[a,b]| by c3,EUCLID:def 16;
    then
c13: a = x1`1 & b = x1`2 by EUCLID:56;
    then
d10: -1 <= a & a <= 1 by Tu3;
    then
c20: 0 <= k & k <= PI by SIN_COS6:101;
c7: 0 <= k/P2 & k/P2 <= 1/2 by d10,Lm20;
c14: 1^2 = |. x1 .|^2 by c4,Tu1
       .= a^2+b^2 by c13,JGRAPH_3:10;
c23: now
      assume
d1:     k = 0;
      then
c9:   a = 1 by d10,SIN_COS6:98;
      1-1 = 1+b^2-1 by d1,c14,d10,SIN_COS6:98,SQUARE_1:59;
      then b = 0 by SQUARE_1:73;
      then x = c[10] by c3,c9,dc10,EUCLID:def 16;
      hence contradiction by c8,TARSKI:def 1;
    end;
    per cases;
    suppose
c5:   b = 0;
c52: a <> 1 by c5,c51,dc10,c8,TARSKI:def 1;
    set y = k/P2;
    y <> 0 by c23,XCMPLX_1:50;
    then 0 < y & y < 1 by c7,AXIOMS:22;
    then reconsider y as Element of Y by b20,b6,JORDAN6:45;
    take y, a, b;
    thus x = |[a,b]| by c3,EUCLID:def 16;
    thus b >= 0 implies y = arccos a/P2;
    assume b <= 0;
    thus y = 1*PI/P2 by c5,c14,c52,JGRAPH_3:2,SQUARE_1:59,60,SIN_COS6:95
      .= 1-1/2 by XCMPLX_1:92
      .= 1-1*PI/P2 by XCMPLX_1:92
      .= 1-arccos a/P2 by c5,c14,c52,JGRAPH_3:2,SQUARE_1:59,60,SIN_COS6:95;
    end;
    suppose
c5:   b > 0;
    set y = k/P2;
    y <> 0 by c23,XCMPLX_1:50;
    then 0 < y & y < 1 by c7,AXIOMS:22;
    then reconsider y as Element of Y by b20,b6,JORDAN6:45;
    take y, a, b;
    thus thesis by c5,c3,EUCLID:def 16;
    end;
    suppose
c6:   b < 0;
    set y = 1-k/P2;
d1: (P2-k)/P2 = P2/P2-k/P2 by XCMPLX_1:121
      .= y by XCMPLX_1:60;
    1*k < P2 by c20,REAL_2:199;
    then k-k < P2-k & P2-k < P2-0 by c20,c23,REAL_1:92;
    then 0/P2 < y & y < P2/P2 by d1,REAL_1:73;
    then 0 < y & y < 1 by XCMPLX_1:60;
    then reconsider y as Element of Y by b20,b6,JORDAN6:45;
    take y, a, b;
    thus thesis by c6,c3,EUCLID:def 16;
    end;
  end;
  ex G being map of TOUC, R^1|R^1(].0,0+p1.[) st
    for p being Point of TOUC holds P[p,G.p] from FUNCT_2:sch 3(c1);
  hence thesis;
end;
uniqueness
proof 
  let f, g be map of TOUC, R^1 | A such that
a1: for p being Point of TOUC
    ex x, y being real number st p = |[x,y]| &
    (y >= 0 implies f.p = arccos x/(2*PI)) &
    (y <= 0 implies f.p = 1-arccos x/(2*PI)) and
a2: for p being Point of TOUC
    ex x, y being real number st p = |[x,y]| &
    (y >= 0 implies g.p = arccos x/(2*PI)) &
    (y <= 0 implies g.p = 1-arccos x/(2*PI));
  now
    let p be Point of TOUC;
a3: ex x1, y1 being real number st p = |[x1,y1]| &
     (y1 >= 0 implies f.p = arccos x1/(2*PI)) &
     (y1 <= 0 implies f.p = 1-arccos x1/(2*PI)) by a1;
    ex x2, y2 being real number st p = |[x2,y2]| &
     (y2 >= 0 implies g.p = arccos x2/(2*PI)) &
     (y2 <= 0 implies g.p = 1-arccos x2/(2*PI)) by a2;
    hence f.p = g.p by a3,SPPOL_2:1;
  end;
  hence thesis by FUNCT_2:113;
end;
end;

set A1 = R^1(].1/2,1/2+p1.[);

definition
  func Circle2IntervalL ->
    map of Topen_unit_circle(c[-10]), R^1 | R^1(].1/2,3/2.[)
    means :dc2im:
  for p being Point of Topen_unit_circle(c[-10])
    ex x, y being real number st p = |[x,y]| &
    (y >= 0 implies it.p = 1+arccos x/(2*PI)) &
    (y <= 0 implies it.p = 1-arccos x/(2*PI));
existence
proof 
b20: A1 = ].1/2,1/2+p1.[ by dr2;
  reconsider A1 as non empty Subset of R^1;
  defpred P[set,set] means
    ex x, y being real number st $1 = |[x,y]| &
    (y >= 0 implies $2 = 1+arccos x/P2) &
    (y <= 0 implies $2 = 1-arccos x/P2);
b6: Ym = A1 by JORDAN1:1;
c1: for x being Element of Xm ex y being Element of Ym st P[x,y]
  proof
    let x be Element of Xm;
c2: Xm = cS1 \ {c[-10]} by dop;
    then
c4: x in cS1 by XBOOLE_0:def 4;
c11: cS1 is Subset of the carrier of TOP-REAL 2 by TSEP_1:1;
    then consider a, b being Real such that
c3:   x = <*a,b*> by c4,EUCLID:55;
c8: not x in {c[-10]} by c2,XBOOLE_0:def 4;
    set k = arccos a;
    reconsider x1 = x as Point of TOP-REAL 2 by c4,c11;
c51: x = |[a,b]| by c3,EUCLID:def 16;
    then
c13: a = x1`1 & b = x1`2 by EUCLID:56;
    then
d10: -1 <= a & a <= 1 by Tu3;
c7: 0 <= k/P2 & k/P2 <= 1/2 by d10,Lm20;
c14: 1^2 = |. x1 .|^2 by c4,Tu1
       .= a^2+b^2 by c13,JGRAPH_3:10;
c23: now
      assume
d1:     k = PI;
      then
c9:   a = -1 by d10,SIN_COS6:100;
      1-1 = (-1)^2+b^2-1 by d1,c14,d10,SIN_COS6:100,SQUARE_1:59
         .= 1^2+b^2-1 by SQUARE_1:61;
      then b = 0 by SQUARE_1:59,73;
      then x = c[-10] by c3,c9,dc10x,EUCLID:def 16;
      hence contradiction by c8,TARSKI:def 1;
    end;
wq3: now
      assume k/P2 = 1/2;
      then k/P2*2 = 1/2*2;
      then k/PI = 1 by XCMPLX_1:93;
      hence contradiction by c23,XCMPLX_1:58;
    end;
wq4: now
      let y be real number;
      assume
wq5:    y = 1+k/P2;
      then
wq2:  1+0 <= y & y <= 1+1/2 by c7,AXIOMS:24;
      y <> 1+1/2 by wq3,wq5,XCMPLX_1:2;
      then 1/2 < y & y < 3/2 by wq2,REAL_1:def 5,AXIOMS:22;
      hence y is Element of Ym by b20,b6,JORDAN6:45;
    end;
    per cases;
    suppose
c5:   b = 0;
    then a <> -1 by c51,dc10x,c8,TARSKI:def 1;
    then
c53: a = 1 by c5,c14,JGRAPH_3:2,SQUARE_1:59,60;
    reconsider y = 1+k/P2 as Element of Ym by wq4;
    take y, a, b;
    thus x = |[a,b]| by c3,EUCLID:def 16;
    thus thesis by c53,SIN_COS6:97;
    end;
    suppose
c5:   b > 0;
    reconsider y = 1+k/P2 as Element of Ym by wq4;
    take y, a, b;
    thus thesis by c5,c3,EUCLID:def 16;
    end;
    suppose
c6:   b < 0;
    set y = 1-k/P2;
wq1: 1-0 >= y & y >= 1-1/2 by c7,REAL_1:92;
    now
      assume y = 1/2;
      then 1-k/P2-1 = 1/2-1;
      then 1-1-k/P2 = -1/2 by XCMPLX_1:21;
      then -k/P2 = -1/2 by XCMPLX_1:150;
      hence contradiction by wq3,XCMPLX_1:134;
    end;
    then 1/2 < y & y < 3/2 by wq1,REAL_1:def 5,AXIOMS:22;
    then reconsider y as Element of Ym by b20,b6,JORDAN6:45;
    take y, a, b;
    thus thesis by c6,c3,EUCLID:def 16;
    end;
  end;
  ex G being map of TOUCm, R^1|R^1(].1/2,1/2+p1.[) st
    for p being Point of TOUCm holds P[p,G.p] from FUNCT_2:sch 3(c1);
  hence thesis;
end;
uniqueness
proof 
  let f, g be map of TOUCm, R^1 | R^1(].1/2,3/2.[) such that
a1: for p being Point of TOUCm
    ex x, y being real number st p = |[x,y]| &
    (y >= 0 implies f.p = 1+arccos x/(2*PI)) &
    (y <= 0 implies f.p = 1-arccos x/(2*PI)) and
a2: for p being Point of TOUCm
    ex x, y being real number st p = |[x,y]| &
    (y >= 0 implies g.p = 1+arccos x/(2*PI)) &
    (y <= 0 implies g.p = 1-arccos x/(2*PI));
  now
    let p be Point of TOUCm;
a3: ex x1, y1 being real number st p = |[x1,y1]| &
     (y1 >= 0 implies f.p = 1+arccos x1/(2*PI)) &
     (y1 <= 0 implies f.p = 1-arccos x1/(2*PI)) by a1;
    ex x2, y2 being real number st p = |[x2,y2]| &
     (y2 >= 0 implies g.p = 1+arccos x2/(2*PI)) &
     (y2 <= 0 implies g.p = 1-arccos x2/(2*PI)) by a2;
    hence f.p = g.p by a3,SPPOL_2:1;
  end;
  hence thesis by FUNCT_2:113;
end;
end;

set C = Circle2IntervalR;
set Cm = Circle2IntervalL;

theorem Ta21:
  (CircleMap(R^1(0)))" = Circle2IntervalR
proof 
  set f = CircleMap(R^1(0));
  reconsider r0 = 0 as Point of R^1 by TOPMETR:24;
r1: R^1(0) = 0 by dr1;
a1: A = ].0,1.[ by dr2;
  then
a2: f = CircleMap | A by r1,def10;
  set F = AffineMap(2*PI,0);
  reconsider A as non empty Subset of R^1;
r2: CircleMap.r0 = c[10] by Tc10a;
  then reconsider f as map of R^1|A, TOUC by r1;
  set Y = the carrier of (R^1|A);
b6: Y = A by JORDAN1:1;
b1: rng f = X by r1,r2,FUNCT_2:def 3;
b3: dom (C*f) = Y by FUNCT_2:def 1;
b4: dom id Y = Y by RELAT_1:71;
  now
    let a be set;
    assume
b5:   a in dom (C*f);
    reconsider b = a as Point of R^1|A by b5;
    consider x, y being real number such that
b8:   f.b = |[x,y]| and
b9:   y >= 0 implies C.(f.b) = arccos x/P2 and
b10:  y <= 0 implies C.(f.b) = 1-arccos x/P2 by dc2i;
    reconsider c = b as Real by XREAL_0:def 1;
    f.b = CircleMap.b by a2,b6,FUNCT_1:72
       .= |[ (cos*F).c, (sin*F).c ]| by Tcm;
    then x = (cos*F).c & y = (sin*F).c by b8,SPPOL_2:1;
    then
b13: x = cos.(F.c) & y = sin.(F.c) by FUNCT_2:21;
    then
B13: x = cos(F.c) & y = sin(F.c) by SIN_COS:def 21,def 23;
b16: F.c = 2*PI*c+0 by JORDAN16:def 3;
     then
b18: F.c/(2*PI*1) = c/1 by XCMPLX_1:92;
b22: F.(1-c) = 2*PI*(1-c)+0 by JORDAN16:def 3;
     then
b27: F.(1-c)/(2*PI*1) = (1-c)/1 by XCMPLX_1:92;
b17: 0 < c & c < 1 by a1,b6,JORDAN6:45;
b11: now per cases;
      suppose
b14:    y >= 0;
      then not F.c in ].PI,2*PI.[ by b13,COMPTRIG:25;
      then F.c <= PI or F.c >= 2*PI by JORDAN6:45;
      then F.c/P2 <= (1*PI)/P2 or F.c/P2 >= 2*PI/P2 by REAL_1:73;
      then 0 <= c & c <= 1/2 by b17,b18,XCMPLX_1:60,92;
      then 2*PI*0 <= 2*PI*c+0 & 2*PI*c <= 2*PI*(1/2) by AXIOMS:25;
      hence C.(f.b) = F.c/P2 by b9,b14,B13,b16,SIN_COS6:94
        .= b by b16,XCMPLX_1:90;
      end;
      suppose
b14:    y < 0;
b25:  cos.(F.(1-c)) = cos(-2*PI*c+2*PI*1) by b22,SIN_COS:def 23
        .= cos(-2*PI*c) by COMPLEX2:10
        .= cos(2*PI*c) by SIN_COS:34;
      not F.c in [.0,PI.] by b13,b14,COMPTRIG:24;
      then F.c < 0 or F.c > PI by TOPREAL5:1;
      then F.c/P2 < 0/P2 or F.c/P2 > (1*PI)/P2 by REAL_1:73;
      then c < 0 or c > 1/2 by b18,XCMPLX_1:92;
      then 1-c < 1-1/2 & 1-c > 1-1 by b17,REAL_1:92;
      then
b21:  2*PI*0 <= 2*PI*(1-c)+0 & 2*PI*(1-c) <= 2*PI*(1/2) by AXIOMS:25;
      arccos x = arccos cos F.(1-c) by B13,b16,b25,SIN_COS:def 23
        .= F.(1-c) by b21,b22,SIN_COS6:94;
      hence C.(f.b) = 1-(1-c) by b27,b10,b14
        .= b;
      end;
    end;
    thus (C*f).a = C.(f.b) by b5,FUNCT_1:22
      .= (id Y).a by b11,FUNCT_1:35;
  end;
  then C*f = id Y by b3,b4,FUNCT_1:9;
  then C = f qua Function" by b1,FUNCT_2:36;
  hence thesis by r1,r2,b1,Lmb0,TOPS_2:def 4;
end;

theorem Ta21m:
  (CircleMap(R^1(1/2)))" = Circle2IntervalL
proof 
  set f = CircleMap(R^1(1/2));
r1: R^1(1/2) = 1/2 by dr1;
a1: A1 = ].1/2,3/2.[ by dr2;
  then
a2: f = CircleMap | A1 by r1,def10;
  set G = AffineMap(2*PI,0);
  reconsider A1 as non empty Subset of R^1;
  reconsider f as map of R^1|A1, TOUCm by r1,Lmcpi,dc10x;
  set Y = the carrier of (R^1|A1);
b6: Y = A1 by JORDAN1:1;
b1: rng f = Xm by r1,Lmcpi,dc10x,FUNCT_2:def 3;
b0: Xm = [#]TOUCm by PRE_TOPC:12;
b3: dom (Cm*f) = Ym by FUNCT_2:def 1;
b4: dom id Ym = Ym by RELAT_1:71;
  now
    let a be set;
    assume
b5:   a in dom (Cm*f);
    then reconsider b = a as Point of R^1|A1;
    consider x, y being real number such that
b8:   f.b = |[x,y]| and
b9:   y >= 0 implies Cm.(f.b) = 1+arccos x/P2 and
b10:  y <= 0 implies Cm.(f.b) = 1-arccos x/P2 by dc2im;
    reconsider c = b as Real by XREAL_0:def 1;
er2: f.b = CircleMap.b by a2,b6,FUNCT_1:72
       .= |[ cos(2*PI*c), sin(2*PI*c) ]| by dcm;
    then
er3: x = cos(2*PI*c) by b8,SPPOL_2:1
      .= cos(2*PI*c+2*PI*(-1)) by COMPLEX2:10
      .= cos(2*PI*(c-1));
er4: y = sin(2*PI*c) by er2,b8,SPPOL_2:1;
b22: G.(1-c) = 2*PI*(1-c)+0 by JORDAN16:def 3;
     then
b27: G.(1-c)/(2*PI*1) = (1-c)/1 by XCMPLX_1:92;
b17: 1/2 < c & c < 3/2 by a1,b6,JORDAN6:45;
    then c-1 < 3/2-1 by REAL_1:54;
    then
ep1: 2*PI*(c-1) <= 2*PI*(1/2) by AXIOMS:25;
    2*PI*c <= 2*PI*(1/2+1) by b17,AXIOMS:25;
    then
tu2: 2*PI*c <= PI+2*PI*1;
    2*PI*(1/2) < 2*PI*c by b17,REAL_1:70;
    then
tu3: PI+2*PI*0 < 2*PI*c;
b11: now per cases;
      suppose
m1:     c >= 1;
      then
b14:  2*PI*c >= 2*PI*1 by AXIOMS:25;
      1-1 <= c-1 by m1,REAL_1:49;
      then
er6:  2*PI*0 <= 2*PI*(c-1)+0 by AXIOMS:25;
      thus Cm.(f.b) = 1+arccos cos(2*PI*(c-1))/P2
        by er3,b9,b14,er4,tu2,SIN_COS6:16
        .= 1+(2*PI*(c-1))/P2 by er6,ep1,SIN_COS6:94
        .= 1+(c-1) by XCMPLX_1:90
        .= b;
      end;
      suppose
m1:     c < 1;
      then 2*PI*c < 2*PI*1 by REAL_1:70;
      then
b14:  2*PI*c < 2*PI+2*PI*0;
aq2:  1-c < 1-1/2 by b17,REAL_1:92;
      1-1 <= 1-c by m1,REAL_1:92;
      then
b21:  2*PI*0 <= 2*PI*(1-c)+0 by AXIOMS:25;
aq4:  2*PI*(1-c) <= 2*PI*(1/2) by aq2,AXIOMS:25;
      arccos x = arccos cos(2*PI*c) by er2,b8,SPPOL_2:1
        .= arccos cos(-2*PI*c) by SIN_COS:34
        .= arccos cos(2*PI*(-c)+2*PI*1) by COMPLEX2:10
        .= G.(1-c) by b21,aq4,b22,SIN_COS6:94;
      hence Cm.(f.b) = 1-(1-c) by b27,b10,b14,er4,tu3,SIN_COS6:12
        .= b;
    end;
    end;
    thus (Cm*f).a = Cm.(f.b) by b5,FUNCT_1:22
      .= (id Ym).a by b11,FUNCT_1:35;
  end;
  then Cm*f = id Ym by b3,b4,FUNCT_1:9;
  then Cm = f qua Function" by b1,FUNCT_2:36;
  hence thesis by r1,b0,b1,Lmcpi,dc10x,TOPS_2:def 4;
end;

set A = ].0,1.[;
set Q = [.-1,1.[;
set E = ].0,PI.];
set j = 1/P2;
reconsider Q, E as non empty Subset of REAL;
er: R^1(A) = A by dr2;
e1: R^1(E) = E by dr2;
e4: R^1(Q) = Q by dr2;
z07: the carrier of R^1 | R^1(Q) = R^1(Q) by JORDAN1:1;
z08: the carrier of R^1 | R^1(E) = R^1(E) by JORDAN1:1;
b3: the carrier of R^1 | R^1(A) = R^1(A) by JORDAN1:1;
set Af = AffineMap(j,0) | R^1(E);
  dom AffineMap(j,0) = R by FUNCT_2:def 1,TOPMETR:24;
  then
f1: dom Af = R^1(E) by RELAT_1:91;
  rng Af c= A
  proof
    let y be set;
    assume y in rng Af;
    then consider x being set such that
a1:   x in dom Af and
a2:   Af.x = y by FUNCT_1:def 5;
    reconsider x as Real by a1;
a3: y = AffineMap(j,0).x by a1,a2,f1,FUNCT_1:72
     .= j*x+0 by JORDAN16:def 3
     .= x/P2 by XCMPLX_1:100;
    then reconsider y as Real;
    0 < x & x <= PI by a1,e1,f1,RCOMP_2:4;
    then 0/P2 < x/P2 & x/P2 <= PI/P2 by REAL_1:73;
    then 0 < y & y <= (1*PI)/P2 by a3;
    then 0 < y & y <= 1/2 by XCMPLX_1:92;
    then 0 < y & y < 1 by AXIOMS:22;
    hence thesis by JORDAN6:45;
  end;
then reconsider Af as map of R^1 | R^1(E), R^1 | R^1(A)
  by f1,z08,b3,er,FUNCT_2:4;
ty1: R^1(AffineMap(j,0)) = AffineMap(j,0) by dr3;
f12: dom AffineMap(j,0) = REAL by FUNCT_2:def 1;
er8: rng AffineMap(j,0) = REAL by JORDAN16:32;
er9: [#]R^1 = REAL by PRE_TOPC:12,TOPMETR:24;
R^1| [#](R^1) = R^1 by TSEP_1:3;
 then
  R^1 = R^1|R^1(dom AffineMap(j,0)) & R^1 = R^1|R^1(rng AffineMap(j,0))
 by er8,er9,dr2,f12;
then reconsider Af as continuous map of R^1 | R^1(E), R^1 | R^1(A)
by ty1,TOPREALA:29;

set L = (R^1(AffineMap(-1,1))) | R^1(A);
f11: AffineMap(-1,1) = R^1(AffineMap(-1,1)) by dr3;
f12: dom AffineMap(-1,1) = REAL by FUNCT_2:def 1;
  then
f10: dom L = A by er,f11,RELAT_1:91;
  rng L c= A
  proof
    let y be set;
    assume y in rng L;
    then consider x being set such that
f13:  x in dom L and
f14:  L.x = y by FUNCT_1:def 5;
    reconsider x as Real by f10,f13;
f15: y = AffineMap(-1,1).x by er,f11,f10,f14,f13,FUNCT_1:72
     .= (-1)*x+1 by JORDAN16:def 3;
    then reconsider y as Real;
    0 < x & x < 1 by f10,f13,JORDAN6:45;
    then 1-1 < 1-x & 1-x < 1-0 by REAL_1:92;
    then 0 < y & y < 1 by f15;
    hence thesis by JORDAN6:45;
  end;
  then reconsider L as map of R^1 | R^1(A), R^1 | R^1(A)
  by b3,er,f10,FUNCT_2:4;
er8: rng AffineMap(-1,1) = REAL by JORDAN16:32;
er9: [#]R^1 = REAL by PRE_TOPC:12,TOPMETR:24;
ak2: R^1| [#](R^1) = R^1 by TSEP_1:3;
  then R^1 = R^1|R^1(dom AffineMap(-1,1)) & R^1 = R^1|R^1(rng AffineMap(-1,1))
    by er8,er9,dr2,f12;
  then reconsider L as continuous map of R^1 | R^1(A), R^1 | R^1(A)
  by TOPREALA:29;

  reconsider ac = R^1(arccos) as
   continuous map of R^1 | R^1[.-1,1.], R^1 | R^1[.0,PI.]
     by SIN_COS6:87,88;
qw: ac = arccos by dr3;

  set c = ac | R^1(Q);
  Q c= [.-1,1.] by TOPREALA:11;
  then
e3: dom c = Q by qw,e4,RELAT_1:91,SIN_COS6:88;
e7: rng c c= E
  proof
    let y be set;
    assume
a0:   y in rng c;
    then consider x being set such that
a1:   x in dom c and
a2:   c.x = y by FUNCT_1:def 5;
    reconsider x as Real by a1,e3;
a3: rng c c= rng ac by RELAT_1:99;
    y in [.0,PI.] by qw,a0,a3,SIN_COS6:87;
    then reconsider y as Real;
a5: 0 <= y & y <= PI by qw,a0,a3,TOPREAL5:1,SIN_COS6:87;
a6: -1 <= x & x < 1 by a1,e3,RCOMP_2:3;
    y = arccos.x by qw,a1,a2,e3,e4,FUNCT_1:72
     .= arccos x by SIN_COS6:def 4;
    then y <> 0 by a6,SIN_COS6:98;
    hence thesis by a5,RCOMP_2:4;
  end;
  then reconsider c as map of R^1 | R^1(Q), R^1 | R^1(E)
    by e3,e1,e4,z07,z08,FUNCT_2:4;
  the carrier of R^1 | R^1[.-1,1.] = R^1[.-1,1.] by JORDAN1:1
    .= [.-1,1.] by dr2;
  then reconsider QQ = R^1(Q) as Subset of R^1 | R^1[.-1,1.] by e4,TOPREALA:11;
  the carrier of R^1 | R^1[.0,PI.] = R^1[.0,PI.] by JORDAN1:1
    .= [.0,PI.] by dr2;
  then reconsider EE = R^1(E) as Subset of R^1 | R^1[.0,PI.] by e1,TOPREALA:15;
  (R^1 | R^1[.-1,1.]) | QQ = R^1 | R^1(Q) &
    (R^1 | R^1[.0,PI.]) | EE = R^1 | R^1(E) by GOBOARD9:4;
  then
ccc: c is continuous by TOPREALA:29;

reconsider p = proj1 as map of TOP-REAL 2,R^1 by TOPMETR:24;
ppp: dom p = the carrier of TOP-REAL 2 by FUNCT_2:def 1;
pp1: p is continuous by TOPREAL6:83;

Lmco1:
 for aX1 being Subset of TOUC st
  aX1 = {q where q is Point of TOP-REAL 2: q in X & 0 <= q`2} holds
  Circle2IntervalR | (TOUC|aX1) is continuous
proof 
  let aX1 being Subset of TOUC such that
z02:  aX1 = {q where q is Point of TOP-REAL 2: q in X & 0 <= q`2};
  reconsider c1 = c[-10] as Point of TOP-REAL 2 by TOPMETR:2;
r2: c1`2 = 0 by dc10x,EUCLID:56;
  c[-10] is Point of Topen_unit_circle(c[10]) by Tw10,Tw11;
  then c[-10] in aX1 by r2,z02;
  then reconsider aX1 as non empty Subset of TOUC;
  set X1 = TOUC | aX1;
z01: aX1 = the carrier of X1 by JORDAN1:1;
e2: [#]X1 is Subset of TUC by Lm51;
  cS1 is Subset of TOP-REAL 2 by TSEP_1:1;
  then reconsider B = [#]X1 as non empty Subset of TOP-REAL 2 by e2,XBOOLE_1:1;
a7: B = aX1 by z01,PRE_TOPC:12;
  X1 is SubSpace of TUC by TSEP_1:7;
  then X1 is SubSpace of TOP-REAL 2 by TSEP_1:7;
  then
a0: (TOP-REAL 2) | B = X1 by PRE_TOPC:def 10;
  set f = p|B;
w11: the carrier of (TOP-REAL 2) | B = B by JORDAN1:1;
w10: dom f = B by ppp,RELAT_1:91;
  rng f c= Q
  proof
    let y be set;
    assume y in rng f;
    then consider x being set such that
w12:  x in dom f and
w13:  f.x = y by FUNCT_1:def 5;
    consider q being Point of TOP-REAL 2 such that
w15:  q = x and
w16:  q in X and 0 <= q`2 by z02,w12,w10,a7;
w14: y = p.x by w10,w12,w13,FUNCT_1:72
      .= q`1 by w15,PSCOMP_1:def 28;
    -1 <= q`1 & q`1 < 1 by w16,Tu4;
    hence thesis by w14,RCOMP_2:3;
  end;
  then reconsider f as map of (TOP-REAL 2) | B, R^1 | R^1(Q)
    by w10,w11,z07,e4,FUNCT_2:4;
  f is continuous by pp1,TOPREALA:29;
  then c*f is continuous by ccc,TOPS_2:58;
  then
q2: Af*(c*f) is continuous by TOPS_2:58;
  for a being Point of X1 holds (C|X1).a = (Af*(c*f)).a
  proof
    let a be Point of X1;
    reconsider b = a as Point of TOUC by TOPMETR:2;
    consider x, y being real number such that
q3:   b = |[x,y]| and
q4:   y >= 0 implies C.b = arccos x/P2 and
      y <= 0 implies C.b = 1-arccos x/P2 by dc2i;
    a in aX1 by z01;
    then
q7: ex q being Point of TOP-REAL 2 st a = q & q in X & 0 <= q`2 by z02;
w1: |[x,y]|`1 = x by EUCLID:56;
w2: -1 <= |[x,y]|`1 by q3,Tu3;
    |[x,y]|`1 < 1 by q3,Tu4;
    then
z04: x in Q by w1,w2,RCOMP_2:3;
    then arccos.x = c.x by qw,e4,FUNCT_1:72;
    then
z06: arccos.x in rng c by e3,z04,FUNCT_1:def 5;
    thus (C|X1).a = (C|the carrier of X1).a by TMAP_1:def 3
      .= arccos x/P2 by q4,q3,q7,EUCLID:56,FUNCT_1:72
      .= arccos.x/P2 by SIN_COS6:def 4
      .= 1/P2 * (arccos.x) + 0 by XCMPLX_1:100
      .= AffineMap(j,0).(arccos.x) by JORDAN16:def 3
      .= Af.(arccos.x) by e1,z06,e7,FUNCT_1:72
      .= Af.(c.x) by qw,z04,e4,FUNCT_1:72
      .= Af.(c.(|[x,y]|`1)) by EUCLID:56
      .= Af.(c.(p.a)) by q3,PSCOMP_1:def 28
      .= Af.(c.(f.a)) by a7,z01,FUNCT_1:72
      .= Af.((c*f).a) by a0,FUNCT_2:21
      .= (Af*(c*f)).a by a0,FUNCT_2:21;
  end;
  hence thesis by a0,q2,FUNCT_2:113;
end;

Lmco2:
 for aX1 being Subset of TOUC st
  aX1 = {q where q is Point of TOP-REAL 2: q in X & 0 >= q`2} holds
  Circle2IntervalR | (TOUC|aX1) is continuous
proof 
  let aX1 being Subset of TOUC such that
z02:  aX1 = {q where q is Point of TOP-REAL 2: q in X & 0 >= q`2};
  reconsider c1 = c[-10] as Point of TOP-REAL 2 by TOPMETR:2;
r2: c1`2 = 0 by dc10x,EUCLID:56;
  c[-10] is Point of Topen_unit_circle(c[10]) by Tw10,Tw11;
  then c[-10] in aX1 by r2,z02;
  then reconsider aX1 as non empty Subset of TOUC;
  set X1 = TOUC | aX1;
z01: aX1 = the carrier of X1 by JORDAN1:1;
e2: [#]X1 is Subset of TUC by Lm51;
  cS1 is Subset of TOP-REAL 2 by TSEP_1:1;
  then reconsider B = [#]X1 as non empty Subset of TOP-REAL 2 by e2,XBOOLE_1:1;
a7: B = aX1 by z01,PRE_TOPC:12;
  X1 is SubSpace of TUC by TSEP_1:7;
  then X1 is SubSpace of TOP-REAL 2 by TSEP_1:7;
  then
a0: (TOP-REAL 2) | B = X1 by PRE_TOPC:def 10;
  set f = p|B;
w11: the carrier of (TOP-REAL 2) | B = B by JORDAN1:1;
w10: dom f = B by ppp,RELAT_1:91;
  rng f c= Q
  proof
    let y be set;
    assume y in rng f;
    then consider x being set such that
w12:  x in dom f and
w13:  f.x = y by FUNCT_1:def 5;
    consider q being Point of TOP-REAL 2 such that
w15:  q = x and
w16:  q in X and 0 >= q`2 by z02,w12,w10,a7;
w14: y = p.x by w10,w12,w13,FUNCT_1:72
      .= q`1 by w15,PSCOMP_1:def 28;
    -1 <= q`1 & q`1 < 1 by w16,Tu4;
    hence thesis by w14,RCOMP_2:3;
  end;
  then reconsider f as map of (TOP-REAL 2) | B, R^1 | R^1(Q)
    by w10,w11,z07,e4,FUNCT_2:4;
  f is continuous by pp1,TOPREALA:29;
  then c*f is continuous by ccc,TOPS_2:58;
  then Af*(c*f) is continuous by TOPS_2:58;
  then
e01: L*(Af*(c*f)) is continuous by TOPS_2:58;
  for a being Point of X1 holds (C|X1).a = (L*(Af*(c*f))).a
  proof
    let a be Point of X1;
    reconsider b = a as Point of TOUC by TOPMETR:2;
    consider x, y being real number such that
q3:   b = |[x,y]| and
      y >= 0 implies C.b = arccos x/P2 and
q4:   y <= 0 implies C.b = 1-arccos x/P2 by dc2i;
    a in aX1 by z01;
    then
q7: ex q being Point of TOP-REAL 2 st a = q & q in X & 0 >= q`2 by z02;
w1: |[x,y]|`1 = x by EUCLID:56;
w2: -1 <= |[x,y]|`1 by q3,Tu3;
    |[x,y]|`1 < 1 by q3,Tu4;
    then
z04: x in Q by w1,w2,RCOMP_2:3;
    then arccos.x = c.x by qw,e4,FUNCT_1:72;
    then
z06: arccos.x in rng c by e3,z04,FUNCT_1:def 5;
kk: arccos x/P2 = arccos.x/P2 by SIN_COS6:def 4
      .= (j * (arccos.x) + 0) by XCMPLX_1:100
      .= AffineMap(j,0).(arccos.x) by JORDAN16:def 3
      .= Af.(arccos.x) by e1,z06,e7,FUNCT_1:72
      .= Af.(c.x) by qw,z04,e4,FUNCT_1:72
      .= Af.(c.(|[x,y]|`1)) by EUCLID:56
      .= Af.(c.(p.a)) by q3,PSCOMP_1:def 28
      .= Af.(c.(f.a)) by a7,z01,FUNCT_1:72
      .= Af.((c*f).a) by a0,FUNCT_2:21
      .= (Af*(c*f)).a by a0,FUNCT_2:21;
    dom (Af*(c*f)) = the carrier of (TOP-REAL 2)|B by FUNCT_2:def 1;
    then
er1: (Af*(c*f)).a in rng (Af*(c*f)) by a7,z01,w11,FUNCT_1:def 5;
    then (Af*(c*f)).a in A by b3,er;
    then reconsider r = (Af*(c*f)).a as Real;
    thus (C|X1).a = (C|the carrier of X1).a by TMAP_1:def 3
      .= 1-arccos x/P2 by q4,q3,q7,EUCLID:56,FUNCT_1:72
      .= (-1)*r+1 by kk
      .= AffineMap(-1,1).r by JORDAN16:def 3
      .= (R^1(AffineMap(-1,1))).r by dr3
      .= L.r by er1,b3,FUNCT_1:72
      .= (L*(Af*(c*f))).a by a0,FUNCT_2:21;
  end;
  hence thesis by a0,e01,FUNCT_2:113;
end;

Lmco:
 for p being Point of Topen_unit_circle(c[10]) st p = c[-10] holds
  Circle2IntervalR is_continuous_at p
proof 
  let p be Point of Topen_unit_circle(c[10]) such that
a0: p = c[-10];
  set aX1 = {q where q is Point of TOP-REAL 2: q in X & 0 <= q`2};
  set aX2 = {q where q is Point of TOP-REAL 2: q in X & 0 >= q`2};
a3: aX1 c= X
  proof
    let x be set;
    assume x in aX1;
    then ex q being Point of TOP-REAL 2 st x = q & q in X & 0 <= q`2;
    hence thesis;
  end;
a4: aX2 c= X
  proof
    let x be set;
    assume x in aX2;
    then ex q being Point of TOP-REAL 2 st x = q & q in X & 0 >= q`2;
    hence thesis;
  end;
  reconsider c1 = c[-10] as Point of TOP-REAL 2 by TOPMETR:2;
r2: c1`2 = 0 by dc10x,EUCLID:56;
  c[-10] is Point of Topen_unit_circle(c[10]) by Tw10,Tw11;
  then
r3: c[-10] in aX1 & c[-10] in aX2 by r2;
  then reconsider aX1, aX2 as non empty Subset of TOUC by a3,a4;
  set X1 = TOUC | aX1;
  set X2 = TOUC | aX2;
r6: the carrier of X1 = aX1 by JORDAN1:1;
  reconsider x1 = p as Point of X1 by a0,r3,JORDAN1:1;
r7: the carrier of X2 = aX2 by JORDAN1:1;
  reconsider x2 = p as Point of X2 by a0,r3,JORDAN1:1;
r5: TOUC is SubSpace of TOUC by TSEP_1:2;
  X = aX1 \/ aX2
  proof
    thus X c= aX1 \/ aX2
    proof
      let a be set;
      assume
r8:     a in X;
      then reconsider a as Point of TOP-REAL 2 by Lm50;
      0 >= a`2 or 0 <= a`2;
      then a in aX1 or a in aX2 by r8;
      hence thesis by XBOOLE_0:def 2;
    end;
    thus thesis;
  end;
  then
a1: TOUC = X1 union X2 by r6,r7,r5,TSEP_1:def 2;
  C|X1 is continuous by Lmco1;
  then
a2: C|X1 is_continuous_at x1 by TMAP_1:49;
  C|X2 is continuous by Lmco2;
  then C|X2 is_continuous_at x2 by TMAP_1:49;
  hence thesis by a1,a2,TMAP_1:125;
end;

  set h1 = REAL --> 1;
t02: dom h1 = REAL by FUNCOP_1:19;
  rng h1 c= REAL by XBOOLE_1:1;
  then reconsider h1 as PartFunc of REAL,REAL by t02,RELSET_1:11;

Lm100:
Circle2IntervalR is continuous
proof 
  set Z = R^1 | R^1(].0,0+p1.[);
  set I = ].0,p1.];
  set J = [.p0,0.[;
  set K = [.-1,1.];
  set h = j(#)arccos;
  for p being Point of TOUC holds C is_continuous_at p
  proof
    let p be Point of TOUC;
    consider x, y being real number such that
a3:   p = |[x,y]| and
a4:   y >= 0 implies C.p = arccos x/P2 and
A4:   y <= 0 implies C.p = 1-arccos x/P2 by dc2i;
  reconsider q = p as Point of TOP-REAL 2 by Lm50;
  x = q`1 by a3,EUCLID:56;
  then -1 <= x & x <= 1 by Tu3;
  then
a11: x in K by TOPREAL5:1;
  then
a12: (h|K).x = h.x by FUNCT_1:72;
d01: the carrier of Z = R^1(].0,0+p1.[) by JORDAN1:1
    .= ].0,0+p1.[ by dr2;
a22: dom h = dom arccos by SEQ_1:def 6;
  then
a14: h.x = arccos.x*j by a11,SIN_COS6:88,SEQ_1:def 6
    .= 1*arccos.x/P2 by XCMPLX_1:75;
t05: h is_continuous_on K by SIN_COS6:109,FCONT_1:21;
  then
d02: h|K is_continuous_in x by a11,FCONT_1:def 2;
  Tcircle(0.REAL 2,1) is SubSpace of TREC by Trec0;
  then TUC is SubSpace of TREC by dunit;
  then
b2: TOUC is SubSpace of TREC by TSEP_1:7;
d03: K = [#]CIT by LmCIT1,PRE_TOPC:12;
rt: y = q`2 by a3,EUCLID:56;
  per cases;
  suppose y = 0;
  then p = c[-10] by rt,Tw4;
  hence thesis by Lmco;
  end;
  suppose
s1: y < 0;
  for V being Subset of Z st V is open & C.p in V
    ex W being Subset of TOUC st W is open & p in W & C.:W c= V
  proof
    let V be Subset of Z such that
a2:  V is open and
a1:  C.p in V;
a13: C.p = 1-arccos.x/P2 by A4,s1,SIN_COS6:def 4;
  reconsider V1 = V as Subset of REAL by d01,XBOOLE_1:1;
  reconsider V2 = V1 as Subset of R^1 by TOPMETR:24;
  V2 is open by a2,TSEP_1:17;
  then reconsider V1 as open Subset of REAL by BORSUK_5:62;
  consider N1 being Neighbourhood of C.p such that
a10: N1 c= V1 by a1,RCOMP_1:39;
  set hh = h1-h;
a12: (hh|K).x = hh.x by a11,FUNCT_1:72;
t01: dom hh = dom h1 /\ dom h by SEQ_1:def 4
        .= REAL /\ dom h by FUNCOP_1:19
        .= K by a22,XBOOLE_1:28,SIN_COS6:88;
  then
A14: hh.x = h1.x - h.x by a11,SEQ_1:def 4
    .= 1-1*arccos.x/P2 by a14,a11,FUNCOP_1:13;
  h1 is_continuous_on REAL by TOPREALA:38;
  then h1 is_continuous_on K by FCONT_1:17;
  then hh is_continuous_on K by t05,FCONT_1:19;
  then hh|K is_continuous_in x by a11,FCONT_1:def 2;
  then consider N being Neighbourhood of x such that
a15: (hh|K).:N c= N1 by a12,a13,A14,FCONT_1:5;
  set N3 = N /\ K;
b3: N3 c= K by XBOOLE_1:17;
  reconsider N3, J as Subset of CIT by TOPREALA:11,LmCIT1,XBOOLE_1:17;
  set W = product ((1,2) --> (N3,J)) /\ X;
  y >= -1 by rt,Tu3;
  then
a36: y in J by s1,RCOMP_2:3;
a37: p = (1,2) --> (x,y) by a3,TOPREALA:49;
  reconsider W as Subset of TOUC by XBOOLE_1:17;
  take W;
b10: N3 c= N by XBOOLE_1:17;
  reconsider KK = product ((1,2) --> (N3,J)) as Subset of TREC by TOPREALA:60;
  R^1(N) = N by dr2;
  then reconsider M3 = N3 as open Subset of CIT by d03,TOPS_2:32;
  reconsider I3 = J as open Subset of CIT by TOPREALA:47;
  KK = product ((1,2) --> (M3,I3));
  then KK is open by TOPREALA:61;
  hence W is open by b2,Lmb0,TOPS_2:32;
  x in N by RCOMP_1:37;
  then x in N3 by a11,XBOOLE_0:def 3;
  then p in product ((1,2) --> (N3,J)) by a36,a37,HILBERT3:11;
  hence p in W by XBOOLE_0:def 3;
  let m be set;
  assume m in C.:W;
  then consider c being Element of TOUC such that
a17: c in W and
a18: m = C.c by FUNCT_2:116;
  consider c1, c2 being real number such that
a33: c = |[c1,c2]| and
     c2 >= 0 implies C.c = arccos c1/P2 and
a34: c2 <= 0 implies C.c = 1-arccos c1/P2 by dc2i;
b7: c in product ((1,2) --> (N3,J)) by a17,XBOOLE_0:def 3;
  then
a19: c.1 in N3 by TOPREALA:24;
a21: dom (hh|K) = K by t01,RELAT_1:91;
b4: (hh|K).:N3 c= (hh|K).:N by b10,RELAT_1:156;
b5: (hh|K).(c.1) = hh.(c.1) by b3,a19,FUNCT_1:72
    .= h1.(c.1) - h.(c.1) by a19,b3,t01,SEQ_1:def 4
    .= 1 - h.(c.1) by a19,FUNCOP_1:13
    .= 1-arccos.(c.1)*j by a19,b3,a22,SEQ_1:def 6,SIN_COS6:88
    .= 1-arccos.c1*j by a33,TOPREALA:50
    .= 1-arccos c1*j by SIN_COS6:def 4;
  c.2 in J by b7,TOPREALA:24;
  then c2 in J by a33,TOPREALA:50;
  then 1-1*arccos c1*j = m by a18,a34,XCMPLX_1:75,RCOMP_2:3;
  then m in (hh|K).:N3 by b5,a19,b3,a21,FUNCT_1:def 12;
  then m in (hh|K).:N by b4;
  then m in N1 by a15;
  hence m in V by a10;
  end;
  hence thesis by TMAP_1:48;
  end;
  suppose
s1:  y > 0;
  for V being Subset of Z st V is open & C.p in V
    ex W being Subset of TOUC st W is open & p in W & C.:W c= V
  proof
    let V be Subset of Z such that
a2:  V is open and
a1:  C.p in V;
a13: C.p = arccos.x/P2 by a4,s1,SIN_COS6:def 4;
  reconsider V1 = V as Subset of REAL by d01,XBOOLE_1:1;
  reconsider V2 = V1 as Subset of R^1 by TOPMETR:24;
  V2 is open by a2,TSEP_1:17;
  then reconsider V1 as open Subset of REAL by BORSUK_5:62;
  consider N1 being Neighbourhood of C.p such that
a10: N1 c= V1 by a1,RCOMP_1:39;
  consider N being Neighbourhood of x such that
a15: (h|K).:N c= N1 by a12,a13,a14,d02,FCONT_1:5;
  set N3 = N /\ K;
b3: N3 c= K by XBOOLE_1:17;
  reconsider N3, I as Subset of CIT by TOPREALA:15,LmCIT1,XBOOLE_1:17;
  set W = product ((1,2) --> (N3,I)) /\ X;
  y <= 1 by rt,Tu3;
  then
a36: y in I by s1,RCOMP_2:4;
a37: p = (1,2) --> (x,y) by a3,TOPREALA:49;
  reconsider W as Subset of TOUC by XBOOLE_1:17;
  take W;
b10: N3 c= N by XBOOLE_1:17;
  reconsider KK = product ((1,2) --> (N3,I)) as Subset of TREC by TOPREALA:60;
  R^1(N) = N by dr2;
  then reconsider M3 = N3 as open Subset of CIT by d03,TOPS_2:32;
  reconsider I3 = I as open Subset of CIT by TOPREALA:46;
  KK = product ((1,2) --> (M3,I3));
  then KK is open by TOPREALA:61;
  hence W is open by b2,Lmb0,TOPS_2:32;
  x in N by RCOMP_1:37;
  then x in N3 by a11,XBOOLE_0:def 3;
  then p in product ((1,2) --> (N3,I)) by a36,a37,HILBERT3:11;
  hence p in W by XBOOLE_0:def 3;
  let m be set;
  assume m in C.:W;
  then consider c being Element of TOUC such that
a17: c in W and
a18: m = C.c by FUNCT_2:116;
  consider c1, c2 being real number such that
a33: c = |[c1,c2]| and
a34: c2 >= 0 implies C.c = arccos c1/P2 and
     c2 <= 0 implies C.c = 1-arccos c1/P2 by dc2i;
b7: c in product ((1,2) --> (N3,I)) by a17,XBOOLE_0:def 3;
  then
a19: c.1 in N3 by TOPREALA:24;
a21: dom (h|K) = K by a22,RELAT_1:91,SIN_COS6:88;
b4: (h|K).:N3 c= (h|K).:N by b10,RELAT_1:156;
b5: (h|K).(c.1) = h.(c.1) by b3,a19,FUNCT_1:72
    .= arccos.(c.1)*j by a19,b3,a22,SEQ_1:def 6,SIN_COS6:88
    .= arccos.c1*j by a33,TOPREALA:50
    .= arccos c1*j by SIN_COS6:def 4;
  c.2 in I by b7,TOPREALA:24;
  then c2 in I by a33,TOPREALA:50;
  then 1*arccos c1*j = m by a18,a34,XCMPLX_1:75,RCOMP_2:4;
  then m in (h|K).:N3 by b5,a19,b3,a21,FUNCT_1:def 12;
  then m in (h|K).:N by b4;
  then m in N1 by a15;
  hence m in V by a10;
  end;
  hence thesis by TMAP_1:48;
  end;
  end;
  hence thesis by TMAP_1:49;
end;

set A = ].1/2,1/2+p1.[;
set Q = ].-1,1.];
set E = [.0,PI.[;
reconsider Q, E as non empty Subset of REAL;
er: R^1(A) = A by dr2;
e1: R^1(E) = E by dr2;
e4: R^1(Q) = Q by dr2;
z07: the carrier of R^1 | R^1(Q) = R^1(Q) by JORDAN1:1;
z08: the carrier of R^1 | R^1(E) = R^1(E) by JORDAN1:1;
b3: the carrier of R^1 | R^1(A) = R^1(A) by JORDAN1:1;
set Af = AffineMap(-j,1) | R^1(E);
  dom AffineMap(-j,1) = R by FUNCT_2:def 1,TOPMETR:24;
  then
f1: dom Af = R^1(E) by RELAT_1:91;
  rng Af c= A
  proof
    let y be set;
    assume y in rng Af;
    then consider x being set such that
a1:   x in dom Af and
a2:   Af.x = y by FUNCT_1:def 5;
    reconsider x as Real by a1;
a3: y = AffineMap(-j,1).x by a1,a2,f1,FUNCT_1:72
     .= (-j)*x+1 by JORDAN16:def 3
     .= -j*x+1
     .= -x/P2+1 by XCMPLX_1:100;
    then reconsider y as Real;
    0 <= x & x < PI by a1,e1,f1,RCOMP_2:3;
    then 0/P2 <= x/P2 & x/P2 < (1*PI)/P2 by REAL_1:73;
    then 0 <= x/P2 & x/P2 < 1/2 by XCMPLX_1:92;
    then -0 >= -x/P2 & -x/P2 > -1/2 by REAL_1:50;
    then 0+1 >= -x/P2+1 & -x/P2+1 > -1/2+1 by AXIOMS:24,REAL_1:53;
    then 1/2 < y & y < 3/2 by a3,AXIOMS:22;
    hence thesis by JORDAN6:45;
  end;
then reconsider Af as map of R^1 | R^1(E), R^1 | R^1(A)
  by f1,z08,b3,er,FUNCT_2:4;
ty1: R^1(AffineMap(-j,1)) = AffineMap(-j,1) by dr3;
f12: dom AffineMap(-j,1) = REAL by FUNCT_2:def 1;
rng AffineMap(-j,1) = REAL by JORDAN16:32;
then R^1 = R^1|R^1(dom AffineMap(-j,1)) & R^1 = R^1|R^1(rng AffineMap(-j,1))
 by ak2,er9,dr2,f12;
then reconsider Af as continuous map of R^1 | R^1(E), R^1 | R^1(A)
by ty1,TOPREALA:29;

set Af1 = AffineMap(j,1) | R^1(E);
  dom AffineMap(j,1) = R by FUNCT_2:def 1,TOPMETR:24;
  then
f1: dom Af1 = R^1(E) by RELAT_1:91;
  rng Af1 c= A
  proof
    let y be set;
    assume y in rng Af1;
    then consider x being set such that
a1:   x in dom Af1 and
a2:   Af1.x = y by FUNCT_1:def 5;
    reconsider x as Real by a1;
a3: y = AffineMap(j,1).x by a1,a2,f1,FUNCT_1:72
     .= j*x+1 by JORDAN16:def 3
     .= x/P2+1 by XCMPLX_1:100;
    then reconsider y as Real;
    0 <= x & x < PI by a1,e1,f1,RCOMP_2:3;
    then 0/P2 <= x/P2 & x/P2 < (1*PI)/P2 by REAL_1:73;
    then 0 <= x/P2 & x/P2 < 1/2 by XCMPLX_1:92;
    then 0+1 <= x/P2+1 & x/P2+1 < 1/2+1 by AXIOMS:24,REAL_1:53;
    then 1/2 < y & y < 3/2 by a3,AXIOMS:22;
    hence thesis by JORDAN6:45;
  end;
then reconsider Af1 as map of R^1 | R^1(E), R^1 | R^1(A)
  by f1,z08,b3,er,FUNCT_2:4;
ty1: R^1(AffineMap(j,1)) = AffineMap(j,1) by dr3;
f12: dom AffineMap(j,1) = REAL by FUNCT_2:def 1;
rng AffineMap(j,1) = REAL by JORDAN16:32;
then R^1 = R^1|R^1(dom AffineMap(j,1)) & R^1 = R^1|R^1(rng AffineMap(j,1))
 by ak2,er9,dr2,f12;
then
reconsider Af1 as continuous map of R^1 | R^1(E), R^1 | R^1(A)
by ty1,TOPREALA:29;

  set c = ac | R^1(Q);
  Q c= [.-1,1.] by TOPREALA:15;
  then
e3: dom c = Q by qw,e4,RELAT_1:91,SIN_COS6:88;
e7: rng c c= E
  proof
    let y be set;
    assume
a0:   y in rng c;
    then consider x being set such that
a1:   x in dom c and
a2:   c.x = y by FUNCT_1:def 5;
    reconsider x as Real by a1,e3;
a3: rng c c= rng ac by RELAT_1:99;
    y in [.0,PI.] by qw,a0,a3,SIN_COS6:87;
    then reconsider y as Real;
a5: 0 <= y & y <= PI by qw,a0,a3,TOPREAL5:1,SIN_COS6:87;
a6: -1 < x & x <= 1 by a1,e3,RCOMP_2:4;
    y = arccos.x by qw,a1,a2,e3,e4,FUNCT_1:72
     .= arccos x by SIN_COS6:def 4;
    then y <> PI by a6,SIN_COS6:100;
    then y < PI by a5,REAL_1:def 5;
    hence thesis by a5,RCOMP_2:3;
  end;
  then reconsider c as map of R^1 | R^1(Q), R^1 | R^1(E)
    by e3,e1,e4,z07,z08,FUNCT_2:4;
  the carrier of R^1 | R^1[.-1,1.] = R^1[.-1,1.] by JORDAN1:1
    .= [.-1,1.] by dr2;
  then reconsider QQ = R^1(Q) as Subset of R^1 | R^1[.-1,1.] by e4,TOPREALA:15;
  the carrier of R^1 | R^1[.0,PI.] = R^1[.0,PI.] by JORDAN1:1
    .= [.0,PI.] by dr2;
  then reconsider EE = R^1(E) as Subset of R^1 | R^1[.0,PI.] by e1,TOPREALA:11;
  (R^1 | R^1[.-1,1.]) | QQ = R^1 | R^1(Q) &
    (R^1 | R^1[.0,PI.]) | EE = R^1 | R^1(E) by GOBOARD9:4;
  then
ccc: c is continuous by TOPREALA:29;

Lmco1m:
 for aX1 being Subset of TOUCm st
  aX1 = {q where q is Point of TOP-REAL 2: q in Xm & 0 <= q`2} holds
  Circle2IntervalL | (TOUCm|aX1) is continuous
proof 
  let aX1 being Subset of TOUCm such that
z02:  aX1 = {q where q is Point of TOP-REAL 2: q in Xm & 0 <= q`2};
  reconsider c1 = c[10] as Point of TOP-REAL 2 by TOPMETR:2;
r2: c1`2 = 0 by dc10,EUCLID:56;
  c[10] is Point of Topen_unit_circle(c[-10]) by Tw10,Tw11;
  then c[10] in aX1 by r2,z02;
  then reconsider aX1 as non empty Subset of TOUCm;
  set X1 = TOUCm | aX1;
z01: aX1 = the carrier of X1 by JORDAN1:1;
e2: [#]X1 is Subset of TUC by Lm51;
  cS1 is Subset of TOP-REAL 2 by TSEP_1:1;
  then reconsider B = [#]X1 as non empty Subset of TOP-REAL 2 by e2,XBOOLE_1:1;
a7: B = aX1 by z01,PRE_TOPC:12;
  X1 is SubSpace of TUC by TSEP_1:7;
  then X1 is SubSpace of TOP-REAL 2 by TSEP_1:7;
  then
a0: (TOP-REAL 2) | B = X1 by PRE_TOPC:def 10;
  set f = p|B;
w11: the carrier of (TOP-REAL 2) | B = B by JORDAN1:1;
w10: dom f = B by ppp,RELAT_1:91;
  rng f c= Q
  proof
    let y be set;
    assume y in rng f;
    then consider x being set such that
w12:  x in dom f and
w13:  f.x = y by FUNCT_1:def 5;
    consider q being Point of TOP-REAL 2 such that
w15:  q = x and
w16:  q in Xm and 0 <= q`2 by z02,w12,w10,a7;
w14: y = p.x by w10,w12,w13,FUNCT_1:72
      .= q`1 by w15,PSCOMP_1:def 28;
    -1 < q`1 & q`1 <= 1 by w16,Tu4a;
    hence thesis by w14,RCOMP_2:4;
  end;
  then reconsider f as map of (TOP-REAL 2) | B, R^1 | R^1(Q)
    by w10,w11,z07,e4,FUNCT_2:4;
  f is continuous by pp1,TOPREALA:29;
  then c*f is continuous by ccc,TOPS_2:58;
  then
e01: Af1*(c*f) is continuous by TOPS_2:58;
  for a being Point of X1 holds (Cm|X1).a = (Af1*(c*f)).a
  proof
    let a be Point of X1;
    reconsider b = a as Point of TOUCm by TOPMETR:2;
    consider x, y being real number such that
q3:   b = |[x,y]| and
q4:   y >= 0 implies Cm.b = 1+arccos x/P2 and
      y <= 0 implies Cm.b = 1-arccos x/P2 by dc2im;
    a in aX1 by z01;
    then
q7: ex q being Point of TOP-REAL 2 st a = q & q in Xm & 0 <= q`2 by z02;
w1: |[x,y]|`1 = x by EUCLID:56;
w2: -1 < |[x,y]|`1 by q3,Tu4a;
    |[x,y]|`1 <= 1 by q3,Tu4a;
    then
z04: x in Q by w1,w2,RCOMP_2:4;
    then arccos.x = c.x by qw,e4,FUNCT_1:72;
    then
z06: arccos.x in rng c by e3,z04,FUNCT_1:def 5;
    thus (Cm|X1).a = (Cm|the carrier of X1).a by TMAP_1:def 3
      .= 1+arccos x/P2 by q4,q3,q7,EUCLID:56,FUNCT_1:72
      .= 1+arccos.x/P2 by SIN_COS6:def 4
      .= 1+j*(arccos.x) by XCMPLX_1:100
      .= AffineMap(j,1).(arccos.x) by JORDAN16:def 3
      .= Af1.(arccos.x) by e1,z06,e7,FUNCT_1:72
      .= Af1.(c.x) by qw,z04,e4,FUNCT_1:72
      .= Af1.(c.(|[x,y]|`1)) by EUCLID:56
      .= Af1.(c.(p.a)) by q3,PSCOMP_1:def 28
      .= Af1.(c.(f.a)) by a7,z01,FUNCT_1:72
      .= Af1.((c*f).a) by a0,FUNCT_2:21
      .= (Af1*(c*f)).a by a0,FUNCT_2:21;
  end;
  hence thesis by a0,e01,FUNCT_2:113;
end;

Lmco2m:
 for aX1 being Subset of TOUCm st
  aX1 = {q where q is Point of TOP-REAL 2: q in Xm & 0 >= q`2} holds
  Circle2IntervalL | (TOUCm|aX1) is continuous
proof 
  let aX1 being Subset of TOUCm such that
z02:  aX1 = {q where q is Point of TOP-REAL 2: q in Xm & 0 >= q`2};
  reconsider c1 = c[10] as Point of TOP-REAL 2 by TOPMETR:2;
r2: c1`2 = 0 by dc10,EUCLID:56;
  c[10] is Point of Topen_unit_circle(c[-10]) by Tw10,Tw11;
  then c[10] in aX1 by r2,z02;
  then reconsider aX1 as non empty Subset of TOUCm;
  set X1 = TOUCm | aX1;
z01: aX1 = the carrier of X1 by JORDAN1:1;
e2: [#]X1 is Subset of TUC by Lm51;
  cS1 is Subset of TOP-REAL 2 by TSEP_1:1;
  then reconsider B = [#]X1 as non empty Subset of TOP-REAL 2 by e2,XBOOLE_1:1;
a7: B = aX1 by z01,PRE_TOPC:12;
  X1 is SubSpace of TUC by TSEP_1:7;
  then X1 is SubSpace of TOP-REAL 2 by TSEP_1:7;
  then
a0: (TOP-REAL 2) | B = X1 by PRE_TOPC:def 10;
  set f = p|B;
w11: the carrier of (TOP-REAL 2) | B = B by JORDAN1:1;
w10: dom f = B by ppp,RELAT_1:91;
  rng f c= Q
  proof
    let y be set;
    assume y in rng f;
    then consider x being set such that
w12:  x in dom f and
w13:  f.x = y by FUNCT_1:def 5;
    consider q being Point of TOP-REAL 2 such that
w15:  q = x and
w16:  q in Xm and 0 >= q`2 by z02,w12,w10,a7;
w14: y = p.x by w10,w12,w13,FUNCT_1:72
      .= q`1 by w15,PSCOMP_1:def 28;
    -1 < q`1 & q`1 <= 1 by w16,Tu4a;
    hence thesis by w14,RCOMP_2:4;
  end;
  then reconsider f as map of (TOP-REAL 2) | B, R^1 | R^1(Q)
    by w10,w11,z07,e4,FUNCT_2:4;
  f is continuous by pp1,TOPREALA:29;
  then c*f is continuous by ccc,TOPS_2:58;
  then
e01: Af*(c*f) is continuous by TOPS_2:58;
  for a being Point of X1 holds (Cm|X1).a = (Af*(c*f)).a
  proof
    let a be Point of X1;
    reconsider b = a as Point of TOUCm by TOPMETR:2;
    consider x, y being real number such that
q3:   b = |[x,y]| and
      y >= 0 implies Cm.b = 1+arccos x/P2 and
q4:   y <= 0 implies Cm.b = 1-arccos x/P2 by dc2im;
    a in aX1 by z01;
    then
q7: ex q being Point of TOP-REAL 2 st a = q & q in Xm & 0 >= q`2 by z02;
w1: |[x,y]|`1 = x by EUCLID:56;
w2: -1 < |[x,y]|`1 by q3,Tu4a;
    |[x,y]|`1 <= 1 by q3,Tu4a;
    then
z04: x in Q by w1,w2,RCOMP_2:4;
    then arccos.x = c.x by qw,e4,FUNCT_1:72;
    then
z06: arccos.x in rng c by e3,z04,FUNCT_1:def 5;
    thus (Cm|X1).a = (Cm|the carrier of X1).a by TMAP_1:def 3
      .= 1-arccos x/P2 by q4,q3,q7,EUCLID:56,FUNCT_1:72
      .= 1-arccos.x/P2 by SIN_COS6:def 4
      .= 1-j*(arccos.x) by XCMPLX_1:100
      .= (-j)*(arccos.x)+1
      .= AffineMap(-j,1).(arccos.x) by JORDAN16:def 3
      .= Af.(arccos.x) by e1,z06,e7,FUNCT_1:72
      .= Af.(c.x) by qw,z04,e4,FUNCT_1:72
      .= Af.(c.(|[x,y]|`1)) by EUCLID:56
      .= Af.(c.(p.a)) by q3,PSCOMP_1:def 28
      .= Af.(c.(f.a)) by a7,z01,FUNCT_1:72
      .= Af.((c*f).a) by a0,FUNCT_2:21
      .= (Af*(c*f)).a by a0,FUNCT_2:21;
  end;
  hence thesis by a0,e01,FUNCT_2:113;
end;

Lmcom:
 for p being Point of Topen_unit_circle(c[-10]) st p = c[10] holds
  Circle2IntervalL is_continuous_at p
proof 
  let p be Point of Topen_unit_circle(c[-10]) such that
a0: p = c[10];
  set aX1 = {q where q is Point of TOP-REAL 2: q in Xm & 0 <= q`2};
  set aX2 = {q where q is Point of TOP-REAL 2: q in Xm & 0 >= q`2};
a3: aX1 c= Xm
  proof
    let x be set;
    assume x in aX1;
    then ex q being Point of TOP-REAL 2 st x = q & q in Xm & 0 <= q`2;
    hence thesis;
  end;
a4: aX2 c= Xm
  proof
    let x be set;
    assume x in aX2;
    then ex q being Point of TOP-REAL 2 st x = q & q in Xm & 0 >= q`2;
    hence thesis;
  end;
  reconsider c1 = c[10] as Point of TOP-REAL 2 by TOPMETR:2;
r2: c1`2 = 0 by dc10,EUCLID:56;
  c[10] is Point of Topen_unit_circle(c[-10]) by Tw10,Tw11;
  then
r3: c[10] in aX1 & c[10] in aX2 by r2;
  then reconsider aX1, aX2 as non empty Subset of TOUCm by a3,a4;
  set X1 = TOUCm | aX1;
  set X2 = TOUCm | aX2;
r6: the carrier of X1 = aX1 by JORDAN1:1;
  reconsider x1 = p as Point of X1 by a0,r3,JORDAN1:1;
r7: the carrier of X2 = aX2 by JORDAN1:1;
  reconsider x2 = p as Point of X2 by a0,r3,JORDAN1:1;
r5: TOUCm is SubSpace of TOUCm by TSEP_1:2;
  Xm = aX1 \/ aX2
  proof
    thus Xm c= aX1 \/ aX2
    proof
      let a be set;
      assume
r8:     a in Xm;
      then reconsider a as Point of TOP-REAL 2 by Lm50;
      0 >= a`2 or 0 <= a`2;
      then a in aX1 or a in aX2 by r8;
      hence thesis by XBOOLE_0:def 2;
    end;
    thus thesis;
  end;
  then
a1: TOUCm = X1 union X2 by r6,r7,r5,TSEP_1:def 2;
  Cm|X1 is continuous by Lmco1m;
  then
a2: Cm|X1 is_continuous_at x1 by TMAP_1:49;
  Cm|X2 is continuous by Lmco2m;
  then Cm|X2 is_continuous_at x2 by TMAP_1:49;
  hence thesis by a1,a2,TMAP_1:125;
end;

Lm200: Circle2IntervalL is continuous
proof 
  set Z = R^1 | R^1(].1/2,1/2+p1.[);
  set I = ].0,p1.];
  set J = [.p0,0.[;
  set K = [.-1,1.];
  set h = j(#)arccos;
  for p being Point of TOUCm holds Cm is_continuous_at p
  proof
    let p be Point of TOUCm;
    consider x, y being real number such that
a3:   p = |[x,y]| and
a4:   y >= 0 implies Cm.p = 1+arccos x/P2 and
A4:   y <= 0 implies Cm.p = 1-arccos x/P2 by dc2im;
  reconsider q = p as Point of TOP-REAL 2 by Lm50;
  x = q`1 by a3,EUCLID:56;
  then -1 <= x & x <= 1 by Tu3;
  then
a11: x in K by TOPREAL5:1;
d01: the carrier of Z = R^1(].1/2,1/2+p1.[) by JORDAN1:1
    .= ].1/2,1/2+p1.[ by dr2;
a22: dom h = dom arccos by SEQ_1:def 6;
  then
a14: h.x = arccos.x*j by a11,SIN_COS6:88,SEQ_1:def 6
    .= 1*arccos.x/P2 by XCMPLX_1:75;
t05: h is_continuous_on K by SIN_COS6:109,FCONT_1:21;
  Tcircle(0.REAL 2,1) is SubSpace of TREC by Trec0;
  then TUC is SubSpace of TREC by dunit;
  then
b2: TOUCm is SubSpace of TREC by TSEP_1:7;
d03: K = [#]CIT by LmCIT1,PRE_TOPC:12;
rt: y = q`2 by a3,EUCLID:56;
  per cases;
  suppose y = 0;
  then p = c[10] by rt,Tw4a;
  hence thesis by Lmcom;
  end;
  suppose
s1: y < 0;
  for V being Subset of Z st V is open & Cm.p in V
    ex W being Subset of TOUCm st W is open & p in W & Cm.:W c= V
  proof
    let V be Subset of Z such that
a2:  V is open and
a1:  Cm.p in V;
a13: Cm.p = 1-arccos.x/P2 by A4,s1,SIN_COS6:def 4;
  reconsider V1 = V as Subset of REAL by d01,XBOOLE_1:1;
  reconsider V2 = V1 as Subset of R^1 by TOPMETR:24;
  V2 is open by a2,TSEP_1:17;
  then reconsider V1 as open Subset of REAL by BORSUK_5:62;
  consider N1 being Neighbourhood of Cm.p such that
a10: N1 c= V1 by a1,RCOMP_1:39;
  set hh = h1-h;
a12: (hh|K).x = hh.x by a11,FUNCT_1:72;
t01: dom hh = dom h1 /\ dom h by SEQ_1:def 4
        .= REAL /\ dom h by FUNCOP_1:19
        .= K by a22,XBOOLE_1:28,SIN_COS6:88;
  then
A14: hh.x = h1.x - h.x by a11,SEQ_1:def 4
    .= 1-1*arccos.x/P2 by a14,a11,FUNCOP_1:13;
  h1 is_continuous_on REAL by TOPREALA:38;
  then h1 is_continuous_on K by FCONT_1:17;
  then hh is_continuous_on K by t05,FCONT_1:19;
  then hh|K is_continuous_in x by a11,FCONT_1:def 2;
  then consider N being Neighbourhood of x such that
a15: (hh|K).:N c= N1 by a12,a13,A14,FCONT_1:5;
  set N3 = N /\ K;
b3: N3 c= K by XBOOLE_1:17;
  reconsider N3, J as Subset of CIT by TOPREALA:11,LmCIT1,XBOOLE_1:17;
  set W = product ((1,2) --> (N3,J)) /\ Xm;
  y >= -1 by rt,Tu3;
  then
a36: y in J by s1,RCOMP_2:3;
a37: p = (1,2) --> (x,y) by a3,TOPREALA:49;
  reconsider W as Subset of TOUCm by XBOOLE_1:17;
  take W;
b10: N3 c= N by XBOOLE_1:17;
  reconsider KK = product ((1,2) --> (N3,J)) as Subset of TREC by TOPREALA:60;
  R^1(N) = N by dr2;
  then reconsider M3 = N3 as open Subset of CIT by d03,TOPS_2:32;
  reconsider I3 = J as open Subset of CIT by TOPREALA:47;
  KK = product ((1,2) --> (M3,I3));
  then KK is open by TOPREALA:61;
  hence W is open by b2,Lmb0m,TOPS_2:32;
  x in N by RCOMP_1:37;
  then x in N3 by a11,XBOOLE_0:def 3;
  then p in product ((1,2) --> (N3,J)) by a36,a37,HILBERT3:11;
  hence p in W by XBOOLE_0:def 3;
  let m be set;
  assume m in Cm.:W;
  then consider c being Element of TOUCm such that
a17: c in W and
a18: m = Cm.c by FUNCT_2:116;
  consider c1, c2 being real number such that
a33: c = |[c1,c2]| and
     c2 >= 0 implies Cm.c = 1+arccos c1/P2 and
a34: c2 <= 0 implies Cm.c = 1-arccos c1/P2 by dc2im;
b7: c in product ((1,2) --> (N3,J)) by a17,XBOOLE_0:def 3;
  then
a19: c.1 in N3 by TOPREALA:24;
a21: dom (hh|K) = K by t01,RELAT_1:91;
b4: (hh|K).:N3 c= (hh|K).:N by b10,RELAT_1:156;
b5: (hh|K).(c.1) = hh.(c.1) by b3,a19,FUNCT_1:72
    .= h1.(c.1) - h.(c.1) by a19,b3,t01,SEQ_1:def 4
    .= 1 - h.(c.1) by a19,FUNCOP_1:13
    .= 1-arccos.(c.1)*j by a19,b3,a22,SEQ_1:def 6,SIN_COS6:88
    .= 1-arccos.c1*j by a33,TOPREALA:50
    .= 1-arccos c1*j by SIN_COS6:def 4;
  c.2 in J by b7,TOPREALA:24;
  then c2 in J by a33,TOPREALA:50;
  then 1-1*arccos c1*j = m by a18,a34,XCMPLX_1:75,RCOMP_2:3;
  then m in (hh|K).:N3 by b5,a19,b3,a21,FUNCT_1:def 12;
  then m in (hh|K).:N by b4;
  then m in N1 by a15;
  hence m in V by a10;
  end;
  hence thesis by TMAP_1:48;
  end;
  suppose
s1: y > 0;
  for V being Subset of Z st V is open & Cm.p in V
    ex W being Subset of TOUCm st W is open & p in W & Cm.:W c= V
  proof
    let V be Subset of Z such that
a2:  V is open and
a1:  Cm.p in V;
a13: Cm.p = 1+arccos.x/P2 by a4,s1,SIN_COS6:def 4;
  reconsider V1 = V as Subset of REAL by d01,XBOOLE_1:1;
  reconsider V2 = V1 as Subset of R^1 by TOPMETR:24;
  V2 is open by a2,TSEP_1:17;
  then reconsider V1 as open Subset of REAL by BORSUK_5:62;
  consider N1 being Neighbourhood of Cm.p such that
a10: N1 c= V1 by a1,RCOMP_1:39;
  set hh = h1+h;
a12: (hh|K).x = hh.x by a11,FUNCT_1:72;
t01: dom hh = dom h1 /\ dom h by SEQ_1:def 3
        .= REAL /\ dom h by FUNCOP_1:19
        .= K by a22,XBOOLE_1:28,SIN_COS6:88;
  then
A14: hh.x = h1.x + h.x by a11,SEQ_1:def 3
    .= 1+1*arccos.x/P2 by a14,a11,FUNCOP_1:13;
  h1 is_continuous_on REAL by TOPREALA:38;
  then h1 is_continuous_on K by FCONT_1:17;
  then hh is_continuous_on K by t05,FCONT_1:19;
  then hh|K is_continuous_in x by a11,FCONT_1:def 2;
  then consider N being Neighbourhood of x such that
a15: (hh|K).:N c= N1 by a12,a13,A14,FCONT_1:5;
  set N3 = N /\ K;
b3: N3 c= K by XBOOLE_1:17;
  reconsider N3, I as Subset of CIT by TOPREALA:15,LmCIT1,XBOOLE_1:17;
  set W = product ((1,2) --> (N3,I)) /\ Xm;
  y <= 1 by rt,Tu3;
  then
a36: y in I by s1,RCOMP_2:4;
a37: p = (1,2) --> (x,y) by a3,TOPREALA:49;
  reconsider W as Subset of TOUCm by XBOOLE_1:17;
  take W;
b10: N3 c= N by XBOOLE_1:17;
  reconsider KK = product ((1,2) --> (N3,I)) as Subset of TREC by TOPREALA:60;
  R^1(N) = N by dr2;
  then reconsider M3 = N3 as open Subset of CIT by d03,TOPS_2:32;
  reconsider I3 = I as open Subset of CIT by TOPREALA:46;
  KK = product ((1,2) --> (M3,I3));
  then KK is open by TOPREALA:61;
  hence W is open by b2,Lmb0m,TOPS_2:32;
  x in N by RCOMP_1:37;
  then x in N3 by a11,XBOOLE_0:def 3;
  then p in product ((1,2) --> (N3,I)) by a36,a37,HILBERT3:11;
  hence p in W by XBOOLE_0:def 3;
  let m be set;
  assume m in Cm.:W;
  then consider c being Element of TOUCm such that
a17: c in W and
a18: m = Cm.c by FUNCT_2:116;
  consider c1, c2 being real number such that
a33: c = |[c1,c2]| and
a34: c2 >= 0 implies Cm.c = 1+arccos c1/P2 and
     c2 <= 0 implies Cm.c = 1-arccos c1/P2 by dc2im;
b7: c in product ((1,2) --> (N3,I)) by a17,XBOOLE_0:def 3;
  then
a19: c.1 in N3 by TOPREALA:24;
a21: dom (hh|K) = K by t01,RELAT_1:91;
b4: (hh|K).:N3 c= (hh|K).:N by b10,RELAT_1:156;
b5: (hh|K).(c.1) = hh.(c.1) by b3,a19,FUNCT_1:72
    .= h1.(c.1) + h.(c.1) by a19,b3,t01,SEQ_1:def 3
    .= 1 + h.(c.1) by a19,FUNCOP_1:13
    .= 1+arccos.(c.1)*j by a19,b3,a22,SEQ_1:def 6,SIN_COS6:88
    .= 1+arccos.c1*j by a33,TOPREALA:50
    .= 1+arccos c1*j by SIN_COS6:def 4;
  c.2 in I by b7,TOPREALA:24;
  then c2 in I by a33,TOPREALA:50;
  then 1+1*arccos c1*j = m by a18,a34,XCMPLX_1:75,RCOMP_2:4;
  then m in (hh|K).:N3 by b5,a19,b3,a21,FUNCT_1:def 12;
  then m in (hh|K).:N by b4;
  then m in N1 by a15;
  hence m in V by a10;
  end;
  hence thesis by TMAP_1:48;
  end;
  end;
  hence thesis by TMAP_1:49;
end;

registration
  cluster Circle2IntervalR -> one-to-one onto continuous;
coherence
proof
r1: R^1(0) = 0 by dr1;
  then CircleMap.R^1(0) = c[10] by Tc10a;
  hence C is one-to-one onto by r1,Ta21,WAYBEL29:1;
  thus thesis by Lm100;
end;
  cluster Circle2IntervalL -> one-to-one onto continuous;
coherence
proof
  R^1(1/2) = 1/2 by dr1;
  hence Cm is one-to-one onto by Ta21m,dc10x,Lmcpi,WAYBEL29:1;
  thus thesis by Lm200;
end;
end;

LmcM:
CircleMap(R^1(0)) is open
proof 
a1: R^1(0) = 0 by dr1;
  then CircleMap.R^1(0) = c[10] by Tc10a;
  hence thesis by a1,Ta21,TOPREALA:35;
end;

LmcMm:
CircleMap(R^1(1/2)) is open
proof 
  R^1(1/2) = 1/2 by dr1;
  hence thesis by Lmcpi,dc10x,Ta21m,TOPREALA:35;
end;

registration
  let i be Integer;
  cluster CircleMap(R^1(i)) -> open;
coherence
proof 
  set F = AffineMap(1,-i);
  set f = F | ].0+i,0+i+p1.[;
a5: R^1(0) = 0 by dr1;
b1: R^1(i) = i by dr1;
c2: R^1(].0,1.[) = ].0,1.[ by dr2;
b5: R^1(].i,i+1.[) = ].i,i+1.[ by dr2;
c4: the carrier of R^1|R^1(].0,1.[) = R^1(].0,1.[) by JORDAN1:1;
c3: the carrier of R^1|R^1(].i,i+1.[) = R^1(].i,i+1.[) by JORDAN1:1;
  dom F = REAL by FUNCT_2:def 1;
  then
c1: dom f = ].i,i+1.[ by RELAT_1:91;
d1: rng f = ].0,0+1.[ by LmAff2;
  then reconsider f as map of R^1|R^1(].i,i+p1.[), R^1|R^1(].0,0+p1.[)
    by c1,c2,b5,c3,c4,FUNCT_2:4;
a1: CircleMap(R^1(0+i)) = CircleMap(R^1(0)) * f by Tzl1m;
a6: CircleMap.R^1(i) = c[10] by b1,Tc10a
     .= CircleMap.R^1(0) by a5,Tc10a;
b2: R^1 | R^1(dom F) = R^1 & R^1 | R^1(rng F) = R^1 by LmAff;
b4: R^1(F) = F by dr3;
  f is onto by c4,d1,c2,FUNCT_2:def 3;
  then f is open by b2,b4,b5,TOPREALA:31;
  hence thesis by b1,a1,a5,a6,LmcM,TOPREALA:32;
end;
  cluster CircleMap(R^1(1/2+i)) -> open;
coherence
proof 
  set F = AffineMap(1,-i);
  set f = F | ].1/2+i,1/2+i+p1.[;
a5: R^1(1/2) = 1/2 by dr1;
b1: R^1(1/2+i) = 1/2+i by dr1;
c2: R^1(].1/2,3/2.[) = ].1/2,3/2.[ by dr2;
b5: R^1(].1/2+i,1/2+i+1.[) = ].1/2+i,1/2+i+1.[ by dr2;
c4: the carrier of R^1|R^1(].1/2,3/2.[) = R^1(].1/2,3/2.[) by JORDAN1:1;
c3: the carrier of R^1|R^1(].1/2+i,1/2+i+1.[) = R^1(].1/2+i,1/2+i+1.[)
   by JORDAN1:1;
  dom F = REAL by FUNCT_2:def 1;
  then
c1: dom f = ].1/2+i,1/2+i+1.[ by RELAT_1:91;
d1: rng f = ].1/2,1/2+1.[ by LmAff2;
  then reconsider f as
    map of R^1|R^1(].1/2+i,1/2+i+p1.[), R^1|R^1(].1/2,1/2+p1.[)
      by c1,c2,b5,c3,c4,FUNCT_2:4;
a1: CircleMap(R^1(1/2+i)) = CircleMap(R^1(1/2)) * f by Tzl1m;
  0 <= 1/2 & 1/2-1 < 0;
  then [\1/2/] = 0 by INT_1:def 4;
  then
w1: 1/2-[\1/2/] = 1/2;
  frac(1/2+i) = frac(1/2) by TOPREALA:1
    .= 1/2 by w1,INT_1:def 6;
  then
a6: CircleMap.R^1(1/2+i) = c[-10] by b1,Tw2,dc10x
     .= CircleMap.R^1(1/2+0) by dr1,Lmcpi,dc10x;
b2: R^1 | R^1(dom F) = R^1 & R^1 | R^1(rng F) = R^1 by LmAff;
b4: R^1(F) = F by dr3;
  f is onto by c4,d1,c2,FUNCT_2:def 3;
  then f is open by b2,b4,b5,TOPREALA:31;
  hence thesis by b1,a1,a5,a6,LmcMm,TOPREALA:32;
end;
end;

registration
  cluster Circle2IntervalR -> open;
coherence
proof 
a1: R^1(0) = 0 by dr1;
  then CircleMap.R^1(0) = c[10] by Tc10a;
  hence thesis by a1,Ta21,TOPREALA:34;
end;
  cluster Circle2IntervalL -> open;
coherence
proof 
  R^1(1/2) = 1/2 by dr1;
  hence thesis by Lmcpi,dc10x,Ta21m,TOPREALA:34;
end;
end;

theorem
  CircleMap(R^1(0)) is_homeomorphism by TOPREALA:37;

theorem
  CircleMap(R^1(1/2)) is_homeomorphism
proof
  reconsider r = 0 as Integer;
  CircleMap(R^1(1/2+r)) is open;
  hence thesis by TOPREALA:37;
end;

theorem
  Circle2IntervalR is_homeomorphism by TOPREALA:37;

theorem
  Circle2IntervalL is_homeomorphism by TOPREALA:37;

theorem
  ex F being Subset-Family of Tunit_circle(2) st
   F = { CircleMap.:].0,1.[, CircleMap.:].1/2,3/2.[ } &
   F is_a_cover_of Tunit_circle(2) & F is open &
   for U being Subset of Tunit_circle(2) holds
   ( U = CircleMap.:].0,1.[ implies
     union IntIntervals(0,1) = (CircleMap)"U &
     for d being Subset of R^1 st d in IntIntervals(0,1)
      for f being map of R^1 | d, (Tunit_circle(2)) | U st f = CircleMap|d
       holds f is_homeomorphism ) &
   ( U = CircleMap.:].1/2,3/2.[ implies
     union IntIntervals(1/2,3/2) = (CircleMap)"U &
     for d being Subset of R^1 st d in IntIntervals(1/2,3/2)
      for f being map of R^1 | d, (Tunit_circle(2)) | U st f = CircleMap|d
       holds f is_homeomorphism ) 
proof
  set D1 = IntIntervals(0,1);
  set D2 = IntIntervals(1/2,3/2);
z10: c[10] in {c[10]} by TARSKI:def 1;
  set F1 = CircleMap.:union D1;
  set F2 = CircleMap.:union D2;
  set F = {F1,F2};
  F c= bool cS1
  proof
    let a be set;
    assume a in F;
    then a = F1 or a = F2 by TARSKI:def 2;
    hence thesis;
  end;
  then reconsider F as Subset-Family of TUC by SETFAM_1:def 7;
  take F;
wr1: ].0+0,1+0.[ in D1 by OO,Texci;
  then
wo1: F1 = CircleMap.:].0,1.[ by TTa1;
  ].1/2+0,3/2+0.[ in D2 by OO,Texci;
  then
Wr1: F2 = CircleMap.:].1/2,3/2.[ by TTa1;
  hence F = {CircleMap.:].0,1.[, CircleMap.:].1/2,3/2.[} by wr1,TTa1;
  thus F is_a_cover_of TUC
  proof
    thus [#]TUC c= union F
    proof
      let a be set;
      assume
a1:     a in [#]TUC;
      reconsider A = [.0,0+1.[ as Subset of R^1 by TOPMETR:24;
      reconsider f = CircleMap | A as map of R^1|A,TUC by Ta1;
      f is onto by Ta4;
      then rng f = cS1 by FUNCT_2:def 3;
      then consider x being set such that
a2:     x in dom f and
a3:     f.x = a by a1,FUNCT_1:def 5;
b3:   dom f = A by Lmd3,RELAT_1:91;
      then reconsider x as Real by a2;
g1:   CircleMap.x = f.x by a2,FUNCT_1:70;
y2:   F2 in F by TARSKI:def 2;
      per cases by b3,a2,RCOMP_2:3;
      suppose
y4:     x = 0;
      0 in A by RCOMP_2:3;
      then
y5:   f.0 = CircleMap.0 by FUNCT_1:72
        .= c[10] by Tc10a
        .= CircleMap.1 by Tc10a;
      1 in ].1/2,3/2.[ by JORDAN6:45;
      then a in CircleMap.:].1/2,3/2.[ by a3,y4,y5,Lmd3,FUNCT_1:def 12;
      hence a in union F by Wr1,y2,TARSKI:def 4;
      end;
      suppose 0 < x & x < 1;
      then
f1:   x in ].0,1.[ by JORDAN6:45;
      ].0+0,1+0.[ in D1 by OO,Texci;
      then x in union D1 by f1,TARSKI:def 4;
      then
a4:   a in F1 by g1,a3,Lmd3,FUNCT_1:def 12;
      F1 in F by TARSKI:def 2;
      hence a in union F by a4,TARSKI:def 4;
      end;
      suppose x >= 1 & x < 1;
      hence a in union F;
      end;
    end;
    now
      let a be set;
      assume a in F;
      then
a1:   a = F1 or a = F2 by TARSKI:def 2;
      F1 c= cS1 & F2 c= cS1;
      hence a c= [#]TUC by a1,PRE_TOPC:12;
    end;
    hence thesis by ZFMISC_1:94;
  end;
  thus F is open
  proof
    let P be Subset of TUC;
wr2: R^1(0) = 0 by dr1;
Wr2: R^1(1/2) = 1/2 by dr1;
tr6: CircleMap.:].0,1.[ = CircleMap | ].0,0+1.[.:].0,1.[ by EXTENS_1:1
      .= (CircleMap(R^1(0))).:].0,1.[ by wr2,def10;
Tr6: CircleMap.:].1/2,3/2.[ = CircleMap | ].1/2,1/2+1.[.:].1/2,3/2.[
by EXTENS_1:1
      .= (CircleMap(R^1(1/2))).:].1/2,3/2.[ by Wr2,def10;
    reconsider r = 0 as Integer;
r10: now
      let A be Subset of REAL;
r11:  the carrier of R^1 | R^1(A) = R^1(A) by JORDAN1:1
        .= A by dr2;
      A c= A;
      hence A is Subset of R^1 | R^1(A) by r11;
    end;
    then reconsider M = ].0,1.[ as Subset of R^1 | R^1(].r,r+1.[);
    reconsider N = ].1/2,3/2.[ as Subset of R^1 | R^1(].1/2+r,1/2+r+1.[)
    by r10;
r12: now
      let A be open Subset of REAL;
      reconsider B = A as Subset of R^1 | R^1(A) by r10;
r11:  the carrier of R^1 | R^1(A) = R^1(A) by JORDAN1:1
        .= A by dr2;
      B = B /\ A
       .= [#](R^1 | R^1(A)) /\ A by r11,PRE_TOPC:12
       .= [#](R^1 | R^1(A)) /\ R^1(A) by dr2;
      hence A is open Subset of R^1 | R^1(A) by TOPS_2:32;
    end;
    then M is open;
    then
tr9: (CircleMap(R^1(r))).:M is open by wr2,T_0TOPSP:def 2;
    N is open by r12;
    then (CircleMap(R^1(1/2+r))).:N is open by Wr2,T_0TOPSP:def 2;
    then F1 is open & F2 is open by wo1,tr6,Tr6,Wr1,tr9,TSEP_1:17;
    hence thesis by TARSKI:def 2;
  end;
  let U be Subset of TUC;
  thus U = CircleMap.:].0,1.[ implies
     union IntIntervals(0,1) = (CircleMap)"U &
     for d being Subset of R^1 st d in IntIntervals(0,1)
      for f being map of R^1 | d, (Tunit_circle(2)) | U st f = CircleMap|d
       holds f is_homeomorphism 
  proof
    assume
f1:  U = CircleMap.:].0,1.[;
    ].0+0,1+0.[ in D1 by OO,Texci;
    then
F1: CircleMap.:].0,1.[ = CircleMap.:union D1 by TTa1;
  now
    let x1, x2 be Element of R^1;
    assume x1 in union D1;
    then consider Z being set such that
z1:   x1 in Z and
z2:   Z in D1 by TARSKI:def 4;
    consider n being Element of INT such that
z3:   Z = ].0+n,1+n.[ by z2,Texci;
    assume
ww:   CircleMap.x1 = CircleMap.x2;
    set k = [\x2/];
    set K = ].0+k,1+k.[;
    k in INT by INT_1:def 2;
    then
z4: K in D1 by Texci;
z6: n < x1 by z1,z3,JORDAN6:45;
    x1 < 1+n by z1,z3,JORDAN6:45;
    then
z13: x1-1 < 1+n-1 by REAL_1:54;
    then
z11: [\x1/] = n by z6,INT_1:def 4;
z8: k <= x2 by INT_1:def 4;
    not x1 is integer by z6,z11,INT_1:48;
    then
z9: not x1 in INT by INT_1:def 2;
    now
      assume k = x2;
      then CircleMap.x1 = c[10] by ww,Tc10a;
      hence contradiction by z9,z10,Lmd3,Tpc,TOPMETR:24,FUNCT_1:def 13;
    end;
    then
z5: k < x2 by z8,REAL_1:def 5;
bb: CircleMap.x1 = CircleMap.(x1+-n) & CircleMap.x2 = CircleMap.(x2+-k) by Tri;
    0 < x1+-n by z6,REAL_2:105;
    then
d1: 2*PI*0 <= 2*PI*(x1-n) by AXIOMS:25;
    x1-1-n < n-n by z13,REAL_1:54;
    then x1-n-1+1 < 0+1 by REAL_1:67;
    then
d2: 2*PI*(x1-n) < 2*PI*1 by REAL_1:70;
    0 <= x2+-k by z5,REAL_2:105;
    then
d3: 2*PI*0 <= 2*PI*(x2-k) by AXIOMS:25;
    x2-1 < k by INT_1:def 4;
    then x2-1-k < k-k by REAL_1:54;
    then x2-1-k+1 < 0+1 by REAL_1:53;
    then
d4: 2*PI*(x2-k) < 2*PI*1 by REAL_1:70;
    CircleMap.(x1-n) = |[ cos(2*PI*(x1-n)), sin(2*PI*(x1-n)) ]| &
      CircleMap.(x2-k) = |[ cos(2*PI*(x2-k)), sin(2*PI*(x2-k)) ]| by dcm;
    then cos(2*PI*(x1-n)) = cos(2*PI*(x2-k)) &
         sin(2*PI*(x1-n)) = sin(2*PI*(x2-k)) by ww,bb,SPPOL_2:1;
    then 2*PI*(x1-n) = 2*PI*(x2-k) by d1,d2,d3,d4,COMPLEX2:12;
    then x1-n = x2-k by XCMPLX_1:5;
    then
d6: x2 = x1-n+k;
    x1 < 1+n by z1,z3,JORDAN6:45;
    then x1-n < 1 by REAL_1:84;
    then x2 < 1+k by d6,REAL_1:53;
    then x2 in K by z5,JORDAN6:45;
    hence x2 in union D1 by z4,TARSKI:def 4;
  end;
  hence union D1 = (CircleMap)"U by F1,f1,T_0TOPSP:2;
  let d be Subset of R^1;
  assume
Z1: d in D1;
  let f be map of R^1 | d, TUC | U;
  assume
Z2: f = CircleMap|d;
  consider n being Element of INT such that
Z4:  d = ].0+n,1+n.[ by Z1,Texci;
  0+n < 1/2+n & 1/2+n < 1+n by REAL_1:53;
  then reconsider d1 = d as non empty Subset of R^1 by Z4,JORDAN6:45;
k4: 1/2 in ].0+0,1+0.[ by JORDAN6:45;
  reconsider U1 = U as non empty Subset of TUC
    by f1,Lmcpi,Lmd3,k4,FUNCT_1:def 12;
  reconsider f1 = f as map of R^1 | d1, TUC | U1;
Z5: [#](TUC | U) = U by PRE_TOPC:def 10;
Z13: F1 = CircleMap.:d by Z1,TTa1;
l1: f is one-to-one by Z1,Z2,Lmy1,TTa2;
l2: f is continuous by Z2,TOPREALA:29;
  reconsider J = ].n,n+p1.[ as non empty Subset of R^1 by TOPMETR:24;
  reconsider F = CircleMap|J as map of R^1|J,TUC by Ta1;
  d c= J by Z4;
  then reconsider d2 = d as Subset of R^1|J by JORDAN1:1;
k1: R^1|J|d2 = R^1|d by Z4,JORDAN6:47;
k3: CircleMap|d = CircleMap|J|d1 by Z4,RELAT_1:103;
m1: [#](TUC | U) = the carrier of TUC | U by PRE_TOPC:12;
k4: f1 is onto
  proof
    thus rng f1 c= the carrier of TUC | U1;
    let b be set;
    assume b in the carrier of TUC | U1;
    then consider a being Element of R^1 such that
Z6:   a in d and
Z7:   b = CircleMap.a by m1,F1,f1,Z5,Z13,FUNCT_2:116;
Z12: dom (CircleMap|d) = d by Lmd3,TOPMETR:24,RELAT_1:91;
    (CircleMap|d).a = CircleMap.a by Z6,FUNCT_1:72;
    hence b in rng f1 by Z2,Z6,Z7,Z12,FUNCT_1:def 5;
  end;
  reconsider nn = n as Point of R^1 by TOPMETR:24,XREAL_0:def 1;
tr1: F = CircleMap(nn) by def10;
tr3: J = R^1(].n,n+p1.[) by dr2;
tr4: R^1(n) = nn by dr1;
  CircleMap(R^1(n)) is open;
  then
k5: F is open by tr1,tr3,tr4,TOPREALA:33;
  CircleMap | ].n,n+1.[ is one-to-one;
  then f is open by k1,k3,k4,k5,Z2,TOPREALA:31;
  hence f is_homeomorphism by l1,l2,k4,TOPREALA:37;
  end;
  assume
f1:  U = CircleMap.:].1/2,3/2.[;
  now
    let x1, x2 be Element of R^1;
    assume x1 in union D2;
    then consider Z being set such that
z1:   x1 in Z and
z2:   Z in D2 by TARSKI:def 4;
    consider n being Element of INT such that
z3:   Z = ].1/2+n,3/2+n.[ by z2,Texci;
    assume
ww:   CircleMap.x1 = CircleMap.x2;
s6: 1/2+n < x1 by z1,z3,JORDAN6:45;
    0+n < 1/2+n by REAL_1:67;
    then
z6: n < x1 by s6,AXIOMS:22;
i4: x1 < 3/2+n by z1,z3,JORDAN6:45;
    then
i1: x1-n < 3/2 by REAL_1:84;
    set k = [\x2/];
z8: k <= x2 by INT_1:def 4;
    per cases by REAL_1:def 5;
    suppose x1 = 1+n;
    then CircleMap.x2 = c[10] by ww,Tc10a;
    then reconsider w = x2 as Element of INT
      by z10,Lmd3,Tpc,TOPMETR:24,FUNCT_1:def 13;
    w-1 in INT by INT_1:def 2;
    then
k1: ].1/2+(w-1),3/2+(w-1).[ in D2 by Texci;
k2: -1/2+w < 0+w by REAL_1:67;
    0+w < 1/2+w by REAL_1:67;
    then x2 in ].1/2+(w-1),3/2+(w-1).[ by k2,JORDAN6:45;
    hence x2 in union D2 by k1,TARSKI:def 4;
    end;
    suppose
t1:   x1 < 1+n;
    set K = ].1/2+k,3/2+k.[;
    k in INT by INT_1:def 2;
    then
z4: K in D2 by Texci;
bb: CircleMap.x1 = CircleMap.(x1+-n) & CircleMap.x2 = CircleMap.(x2+-k) by Tri;
    CircleMap.(x1-n) = |[ cos(2*PI*(x1-n)), sin(2*PI*(x1-n)) ]| &
      CircleMap.(x2-k) = |[ cos(2*PI*(x2-k)), sin(2*PI*(x2-k)) ]| by dcm;
    then
e1: cos(2*PI*(x1-n)) = cos(2*PI*(x2-k)) &
      sin(2*PI*(x1-n)) = sin(2*PI*(x2-k)) by ww,bb,SPPOL_2:1;
    0 < x1+-n by z6,REAL_2:105;
    then
d1: 2*PI*0 <= 2*PI*(x1-n) by AXIOMS:25;
    x1-1 < n+1-1 by t1,REAL_1:54;
    then x1-1-n < n-n by REAL_1:54;
    then x1-n-1+1 < 0+1 by REAL_1:67;
    then
d2: 2*PI*(x1-n) < 2*PI*1 by REAL_1:70;
    x2-x2 <= x2-k by z8,REAL_1:92;
    then
d3: 2*PI*0 <= 2*PI*(x2-k) by AXIOMS:25;
    x2-1 < k by INT_1:def 4;
    then x2-1-k < k-k by REAL_1:54;
    then x2-1-k+1 < 0+1 by REAL_1:53;
    then 2*PI*(x2-k) < 2*PI*1 by REAL_1:70;
    then 2*PI*(x1-n) = 2*PI*(x2-k) by d1,d2,d3,e1,COMPLEX2:12;
    then
D6: x1-n = x2-k by XCMPLX_1:5;
    then
d6: x2 = x1-n+k;
    1/2+n-n < x1-n by s6,REAL_1:54;
    then
w5: 1/2+k < x1-n+k by REAL_1:67;
    x2 < 3/2+k by d6,i1,REAL_1:53;
    then x2 in K by w5,D6,JORDAN6:45;
    hence x2 in union D2 by z4,TARSKI:def 4;
    end;
    suppose
ss:   x1 > 1+n;
    set K = ].1/2+(k-1),3/2+(k-1).[;
    k-1 in INT by INT_1:def 2;
    then
z4: K in D2 by Texci;
bb: CircleMap.x1 = CircleMap.(x1+(-1-n)) & CircleMap.x2 = CircleMap.(x2+-k)
by Tri;
    CircleMap.(x1-1-n) = |[ cos(2*PI*(x1-1-n)), sin(2*PI*(x1-1-n)) ]| &
      CircleMap.(x2-k) = |[ cos(2*PI*(x2-k)), sin(2*PI*(x2-k)) ]| by dcm;
    then
e1: cos(2*PI*(x1-1-n)) = cos(2*PI*(x2-k)) &
      sin(2*PI*(x1-1-n)) = sin(2*PI*(x2-k)) by ww,bb,SPPOL_2:1;
i3: x1-1 < 3/2+n-1 by i4,REAL_1:54;
i2: n+1-1 < x1-1 by ss,REAL_1:54;
    then n-n < x1-1-n by REAL_1:54;
    then
d1: 2*PI*0 <= 2*PI*(x1-1-n) by AXIOMS:25;
    x1-1-n < 1/2+n-n by i3,REAL_1:54;
    then x1-1-n < 1 by AXIOMS:22;
    then
d2: 2*PI*(x1-1-n) < 2*PI*1 by REAL_1:70;
    x2-x2 <= x2-k by z8,REAL_1:92;
    then
d3: 2*PI*0 <= 2*PI*(x2-k) by AXIOMS:25;
    x2-1 < k by INT_1:def 4;
    then x2-1-k < k-k by REAL_1:54;
    then x2-1-k+1 < 0+1 by REAL_1:53;
    then 2*PI*(x2-k) < 2*PI*1 by REAL_1:70;
    then 2*PI*(x1-1-n) = 2*PI*(x2-k) by d1,d2,d3,e1,COMPLEX2:12;
    then
D6: x1-1-n = x2-k by XCMPLX_1:5;
    then
d6: x2 = x1-1-n+k;
yu: n-n < x1-1-n by i2,REAL_1:54;
    -1/2 < 0;
    then -1/2 < x1-1-n by yu;
    then
W5: -1/2+k < x1-1-n+k by REAL_1:67;
    x1-n-1 < 3/2-1 by i1,REAL_1:54;
    then x2 < 1/2+k by d6,REAL_1:53;
    then x2 in K by W5,D6,JORDAN6:45;
    hence x2 in union D2 by z4,TARSKI:def 4;
    end;
  end;
  hence union D2 = (CircleMap)"U by Wr1,f1,T_0TOPSP:2;
  let d be Subset of R^1;
  assume
Z1: d in D2;
  let f be map of R^1 | d, TUC | U;
  assume
Z2: f = CircleMap|d;
  consider n being Element of INT such that
Z4:  d = ].1/2+n,3/2+n.[ by Z1,Texci;
  1/2+n < 1+n & 1+n < 3/2+n by REAL_1:53;
  then reconsider d1 = d as non empty Subset of R^1 by Z4,JORDAN6:45;
k4: 1 in ].1/2+0,3/2+0.[ by JORDAN6:45;
  reconsider U1 = U as non empty Subset of TUC
    by f1,Lmcpi2,Lmd3,k4,FUNCT_1:def 12;
  reconsider f1 = f as map of R^1 | d1, TUC | U1;
Z5: [#](TUC | U) = U by PRE_TOPC:def 10;
Z13: F2 = CircleMap.:d by Z1,TTa1;
l1: f is one-to-one by Z1,Z2,Lmy2,TTa2;
l2: f is continuous by Z2,TOPREALA:29;
  reconsider J = ].1/2+n,1/2+n+p1.[ as non empty Subset of R^1 by TOPMETR:24;
  reconsider F = CircleMap|J as map of R^1|J,TUC by Ta1;
  d c= J by Z4;
  then reconsider d2 = d as Subset of R^1|J by JORDAN1:1;
k1: R^1|J|d2 = R^1|d by Z4,JORDAN6:47;
k3: CircleMap|d = CircleMap|J|d1 by Z4,RELAT_1:103;
m1: [#](TUC | U) = the carrier of TUC | U by PRE_TOPC:12;
k4: f1 is onto
  proof
    thus rng f1 c= the carrier of TUC | U1;
    let b be set;
    assume b in the carrier of TUC | U1;
    then consider a being Element of R^1 such that
Z6:   a in d and
Z7:   b = CircleMap.a by m1,Wr1,f1,Z5,Z13,FUNCT_2:116;
Z12: dom (CircleMap|d) = d by Lmd3,TOPMETR:24,RELAT_1:91;
    (CircleMap|d).a = CircleMap.a by Z6,FUNCT_1:72;
    hence b in rng f1 by Z2,Z6,Z7,Z12,FUNCT_1:def 5;
  end;
  reconsider nn = 1/2+n as Point of R^1 by TOPMETR:24,XREAL_0:def 1;
tr1: F = CircleMap(nn) by def10;
tr3: J = R^1(].1/2+n,1/2+n+p1.[) by dr2;
tr4: R^1(1/2+n) = nn by dr1;
  CircleMap(R^1(1/2+n)) is open;
  then
k5: F is open by tr1,tr3,tr4,TOPREALA:33;
  CircleMap | ].1/2+n,1/2+n+1.[ is one-to-one;
  then f is open by k1,k3,k4,k5,Z2,TOPREALA:31;
  hence f is_homeomorphism by l1,l2,k4,TOPREALA:37;
end;

