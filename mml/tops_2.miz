:: Families of Subsets, Subspaces and Mappings in Topological Spaces
::  by Agata Darmochwa{\l}
::
:: Received June 21, 1989
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies SUBSET_1, NUMBERS, PRE_TOPC, SETFAM_1, STRUCT_0, TARSKI,
      ZFMISC_1, XBOOLE_0, FINSET_1, FUNCT_1, RELAT_1, RCOMP_1, FINSEQ_1, NAT_1,
      XXREAL_0, ARYTM_3, CARD_1, ORDINAL2, VALUED_1, RELAT_2, CONNSP_1, TOPS_2;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, NUMBERS, XXREAL_0, RELAT_1,
      FUNCT_1, RELSET_1, PARTFUN1, FUNCT_2, FUNCT_3, NAT_1, FINSEQ_1, FINSET_1,
      SETFAM_1, STRUCT_0, PRE_TOPC, CONNSP_1;
 constructors SETFAM_1, PARTFUN1, FUNCT_3, XXREAL_0, XREAL_0, NAT_1, MEMBERED,
      FINSEQ_1, CONNSP_1, RELSET_1;
 registrations XBOOLE_0, SUBSET_1, RELSET_1, FUNCT_2, MEMBERED, STRUCT_0,
      PRE_TOPC, XREAL_0, FINSEQ_1;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, PRE_TOPC, SUBSET_1, STRUCT_0, RELAT_1;
 theorems TARSKI, FINSET_1, SETFAM_1, FUNCT_1, FUNCT_2, FUNCT_3, NAT_1,
      FINSEQ_1, ZFMISC_1, PRE_TOPC, TOPS_1, RELAT_1, XBOOLE_0, XBOOLE_1,
      XREAL_1, CONNSP_1;
 schemes CLASSES1, NAT_1, SUBSET_1;

begin

reserve x, y for set,

  T for TopStruct,
  GX for TopSpace,
  P, Q, M, N for Subset of T,
  F, G for Subset-Family of T,
  W, Z for Subset-Family of GX,
  A for SubSpace of T;

::
::    A FAMILY OF SETS IN TOPOLOGICAL SPACES
::

theorem
  for T being 1-sorted, F being Subset-Family of T holds F c= bool [#]T;

canceled;

theorem Th3:
  for T being 1-sorted, F being Subset-Family of T, X being set st
  X c= F holds X is Subset-Family of T
proof
  let T be 1-sorted, F be Subset-Family of T, X be set;
  assume
A1: X c= F;
  X c= bool the carrier of T
  proof
    let y;
    assume y in X;
    then y in F by A1;
    hence thesis;
  end;
  hence thesis;
end;

canceled;

theorem
  for T being non empty 1-sorted, F being Subset-Family of T st F is
  Cover of T holds F <> {}
proof
  let T be non empty 1-sorted, F be Subset-Family of T;
  consider x being Element of union F;
  assume F is Cover of T;
  then union F = the carrier of T by SETFAM_1:60;
  then ex A being set st x in A & A in F by TARSKI:def 4;
  hence thesis;
end;

theorem
  for T being 1-sorted, F, G being Subset-Family of T holds union F \
  union G c= union(F \ G)
proof
  let T be 1-sorted, F, G be Subset-Family of T;
  let x;
  assume
A1: x in union F \ union G;
  then x in union F by XBOOLE_0:def 5;
  then consider A being set such that
A2: x in A and
A3: A in F by TARSKI:def 4;
  not x in union G by A1,XBOOLE_0:def 5;
  then not A in G by A2,TARSKI:def 4;
  then A in F \ G by A3,XBOOLE_0:def 5;
  hence thesis by A2,TARSKI:def 4;
end;

canceled 3;

theorem
  for T being set, F being Subset-Family of T holds F <> {} iff
  COMPLEMENT(F) <> {}
proof
  let T be set, F be Subset-Family of T;
  thus F <> {} implies COMPLEMENT(F) <> {} by SETFAM_1:46;
  assume COMPLEMENT(F) <> {};
  then COMPLEMENT(COMPLEMENT(F)) <> {} by SETFAM_1:46;
  hence thesis;
end;

theorem Th11:
  for T being set, F being Subset-Family of T holds F <> {}
  implies meet COMPLEMENT(F) = (union F)`
proof
  let T be set, F be Subset-Family of T;
  assume F <> {};
  then meet COMPLEMENT(F) = [#]T \ union F by SETFAM_1:47;
  hence thesis;
end;

theorem Th12:
  for T being set, F being Subset-Family of T holds F <> {}
  implies union COMPLEMENT(F) = (meet F)`
proof
  let T be set, F be Subset-Family of T;
  assume F <> {};
  then union COMPLEMENT(F) = [#]T \ meet F by SETFAM_1:48
    .= T \ meet F;
  hence thesis;
end;

Lm1: for T being 1-sorted, F being Subset-Family of T st COMPLEMENT(F) is
finite holds F is finite
proof
  let T be 1-sorted, F be Subset-Family of T;
  defpred X[set,set] means for X being Subset of T st X = $1 holds $2 = X`;
A1: for x st x in COMPLEMENT(F) ex y st X[x,y]
  proof
    let x;
    assume x in COMPLEMENT(F);
    then reconsider X=x as Subset of T;
    reconsider y=X` as set;
    take y;
    thus thesis;
  end;
  consider f being Function such that
A2: dom f = COMPLEMENT(F) and
A3: for x st x in COMPLEMENT(F) holds X[x,f.x] from CLASSES1:sch 1(A1);
  x in rng f iff x in F
  proof
    hereby
      assume x in rng f;
      then consider y such that
A4:   y in dom f and
A5:   x=f.y by FUNCT_1:def 5;
      reconsider Y=y as Subset of T by A2,A4;
      Y` in F by A2,A4,SETFAM_1:def 8;
      hence x in F by A2,A3,A4,A5;
    end;
    assume
A6: x in F;
    then reconsider X=x as Subset of T;
A7: X`` = X;
    then X` in COMPLEMENT(F) by A6,SETFAM_1:def 8;
    then
A8: f.(X`) = X`` by A3;
    X` in dom f by A2,A6,A7,SETFAM_1:def 8;
    hence thesis by A8,FUNCT_1:def 5;
  end;
  then rng f = F by TARSKI:2;
  then
A9: f.:(COMPLEMENT(F)) = F by A2,RELAT_1:146;
  assume COMPLEMENT(F) is finite;
  hence thesis by A9,FINSET_1:17;
end;

theorem Th13:
  for T being 1-sorted, F being Subset-Family of T holds
  COMPLEMENT(F) is finite iff F is finite
proof
  let T be 1-sorted, F be Subset-Family of T;
  thus COMPLEMENT(F) is finite implies F is finite by Lm1;
  assume F is finite;
  then COMPLEMENT COMPLEMENT F is finite;
  hence thesis by Lm1;
end;

::
::       CLOSED AND OPEN FAMILIES
::

definition
  let T be TopStruct, F be Subset-Family of T;
  attr F is open means
  :Def1:
  for P being Subset of T holds P in F implies P is open;
  attr F is closed means
  :Def2:
  for P being Subset of T holds P in F implies P is closed;
end;

canceled 2;

theorem Th16:
  F is closed iff COMPLEMENT(F) is open
proof
  thus F is closed implies COMPLEMENT(F) is open
  proof
    assume
A1: F is closed;
    let P;
    assume P in COMPLEMENT(F);
    then P` in F by SETFAM_1:def 8;
    then P` is closed by A1,Def2;
    hence thesis by TOPS_1:30;
  end;
  assume
A2: COMPLEMENT(F) is open;
  let P such that
A3: P in F;
  P``=P;
  then P` in COMPLEMENT(F) by A3,SETFAM_1:def 8;
  then P` is open by A2,Def1;
  hence thesis by TOPS_1:29;
end;

theorem
  F is open iff COMPLEMENT(F) is closed
proof
  F = COMPLEMENT COMPLEMENT F;
  hence thesis by Th16;
end;

theorem
  F c= G & G is open implies F is open
proof
  assume
A1: F c= G & G is open;
  let P;
  assume P in F;
  hence thesis by A1,Def1;
end;

theorem
  F c= G & G is closed implies F is closed
proof
  assume
A1: F c= G & G is closed;
  let P;
  assume P in F;
  hence thesis by A1,Def2;
end;

theorem
  F is open & G is open implies F \/ G is open
proof
  assume
A1: F is open & G is open;
  let P;
  assume P in F \/ G;
  then P in F or P in G by XBOOLE_0:def 3;
  hence thesis by A1,Def1;
end;

theorem
  F is open implies F /\ G is open
proof
  assume
A1: F is open;
  let P;
  assume P in F /\ G;
  then P in F by XBOOLE_0:def 4;
  hence thesis by A1,Def1;
end;

theorem
  F is open implies F \ G is open
proof
  assume
A1: F is open;
  let P;
  assume P in F \ G;
  then P in F by XBOOLE_0:def 5;
  hence thesis by A1,Def1;
end;

theorem
  F is closed & G is closed implies F \/ G is closed
proof
  assume
A1: F is closed & G is closed;
  let P;
  assume P in F \/ G;
  then P in F or P in G by XBOOLE_0:def 3;
  hence thesis by A1,Def2;
end;

theorem
  F is closed implies F /\ G is closed
proof
  assume
A1: F is closed;
  let P;
  assume P in F /\ G;
  then P in F by XBOOLE_0:def 4;
  hence thesis by A1,Def2;
end;

theorem
  F is closed implies F \ G is closed
proof
  assume
A1: F is closed;
  let P;
  assume P in F \ G;
  then P in F by XBOOLE_0:def 5;
  hence thesis by A1,Def2;
end;

theorem Th26:
  W is open implies union W is open
proof
  assume
A1: W is open;
  W c= the topology of GX
  proof
    let x;
    assume
A2: x in W;
    then reconsider X=x as Subset of GX;
    X is open by A1,A2,Def1;
    hence thesis by PRE_TOPC:def 5;
  end;
  then union W in the topology of GX by PRE_TOPC:def 1;
  hence thesis by PRE_TOPC:def 5;
end;

theorem Th27:
  W is open & W is finite implies meet W is open
proof
  assume that
A1: W is open and
A2: W is finite;
  consider p being FinSequence such that
A3: rng p = W by A2,FINSEQ_1:73;
  consider n being Nat such that
A4: dom p = Seg n by FINSEQ_1:def 2;
  defpred X[Nat] means for Z st Z = p.:(Seg $1) & $1 <= n & 1 <= n holds meet
  Z is open;
A5: for k be Nat holds X[k] implies X[k+1]
  proof
    let k be Nat;
    assume
A6: for Z st Z = p.:(Seg k) & k <= n & 1 <= n holds meet Z is open;
    let Z such that
A7: Z = p.:(Seg(k+1));
    assume that
A8: k+1 <= n and
A9: 1 <= n;
A10: now
      reconsider G2 = Im(p,k+1) as Subset-Family of GX by A3,Th3,RELAT_1:144;
      reconsider G1 = p.:(Seg k) as Subset-Family of GX by A3,Th3,RELAT_1:144;
      assume
A12:  0 < k;
      k+1 <= n+1 by A8,NAT_1:12;
      then k <= n by XREAL_1:8;
      then Seg k c= dom p by A4,FINSEQ_1:7;
      then
A13:  G1 <> {} by A12,RELAT_1:152;
      k+1 <= n+1 by A8,NAT_1:12;
      then k <= n by XREAL_1:8;
      then
A14:  meet G1 is open by A6,A9;
      0 <= k & 0+1 = 1 by NAT_1:2;
      then 1 <= k+1 by XREAL_1:9;
      then
A15:  k+1 in dom p by A4,A8,FINSEQ_1:3;
      then G2 = {p.(k+1)} by FUNCT_1:117;
      then
A16:  meet G2 = p.(k+1) by SETFAM_1:11;
      {k+1} c= dom p by A15,ZFMISC_1:37;
      then
A17:  G2 <> {} by RELAT_1:152;
      p.(k+1) in W by A3,A15,FUNCT_1:def 5;
      then
A18:  meet G2 is open by A1,A16,Def1;
      p.:(Seg(k+1)) = p.:(Seg k \/ {k+1}) by FINSEQ_1:11
        .= p.:(Seg k) \/ p.:{k+1} by RELAT_1:153;
      then meet Z = meet G1 /\ meet G2 by A7,A13,A17,SETFAM_1:10;
      hence thesis by A14,A18,TOPS_1:38;
    end;
    now
      assume
A19:  k=0;
      then
A20:  1 in dom p by A4,A8,FINSEQ_1:3;
      then Im(p,1) = {p.1} by FUNCT_1:117;
      then meet Z = p.1 by A7,A19,FINSEQ_1:4,SETFAM_1:11;
      then meet Z in W by A3,A20,FUNCT_1:def 5;
      hence thesis by A1,Def1;
    end;
    hence thesis by A10,NAT_1:3;
  end;
A21: X[0]
  proof
    let Z;
    assume that
A22: Z = p.:(Seg 0) and
    0 <= n;
A23: {} in the topology of GX by PRE_TOPC:5;
    meet Z = {} by A22,RELAT_1:149,SETFAM_1:2;
    hence thesis by A23,PRE_TOPC:def 5;
  end;
A24: for k be Nat holds X[k] from NAT_1:sch 2(A21,A5);
A25: now
    assume
A26: 1 <= n;
    W = p.:(Seg n) by A3,A4,RELAT_1:146;
    hence thesis by A24,A26;
  end;
A27: now
    assume n = 0;
    then Seg n = {};
    then W = p.:{} by A3,A4,RELAT_1:146;
    then
A28: meet W = {} by RELAT_1:149,SETFAM_1:2;
    {} in the topology of GX by PRE_TOPC:5;
    hence thesis by A28,PRE_TOPC:def 5;
  end;
  now
    assume n <> 0;
    then
A29: 0 < n by NAT_1:3;
    1 = 0+1;
    hence 1 <= n by A29,NAT_1:13;
  end;
  hence thesis by A25,A27;
end;

theorem
  W is closed & W is finite implies union W is closed
proof
  reconsider C = COMPLEMENT(W) as Subset-Family of GX;
  assume W is closed & W is finite;
  then COMPLEMENT(W) is open & COMPLEMENT(W) is finite by Th13,Th16;
  then
A1: meet C is open by Th27;
A2: now
    assume W <> {};
    then meet COMPLEMENT(W) = (union W)` by Th11;
    hence thesis by A1,TOPS_1:29;
  end;
   W = {} implies thesis by ZFMISC_1:2;
  hence thesis by A2;
end;

theorem
  W is closed implies meet W is closed
proof
  reconsider C = COMPLEMENT(W) as Subset-Family of GX;
  assume W is closed;
  then COMPLEMENT(W) is open by Th16;
  then
A1: union C is open by Th26;
A2: now
    assume W <> {};
    then union COMPLEMENT(W) = (meet W)` by Th12;
    hence thesis by A1,TOPS_1:29;
  end;
  now
    assume W = {};
    then meet W = {}GX by SETFAM_1:def 1;
    hence thesis;
  end;
  hence thesis by A2;
end;

::
::      SUBSPACES OF A TOPOLOGICAL SPACE
::

canceled;

theorem
  for F being Subset-Family of A holds F is Subset-Family of T
proof
  let F be Subset-Family of A;
  [#] A c= [#]T by PRE_TOPC:def 9;
  then bool the carrier of A c= bool the carrier of T by ZFMISC_1:79;
  hence thesis by XBOOLE_1:1;
end;

theorem Th32:
  for B being Subset of A holds B is open iff ex C being Subset of
  T st C is open & C /\ [#](A) = B
proof
  let B be Subset of A;
  hereby
    assume B is open;
    then B in the topology of A by PRE_TOPC:def 5;
    then consider C being Subset of T such that
A1: C in the topology of T & C /\ [#](A) = B by PRE_TOPC:def 9;
    reconsider C as Subset of T;
    take C;
    thus C is open & C /\ [#] A = B by A1,PRE_TOPC:def 5;
  end;
  given C being Subset of T such that
A2: C is open and
A3: C /\ [#](A) = B;
  C in the topology of T by A2,PRE_TOPC:def 5;
  then B in the topology of A by A3,PRE_TOPC:def 9;
  hence thesis by PRE_TOPC:def 5;
end;

theorem Th33:
  Q is open implies for P being Subset of A st P=Q holds P is open
proof
  assume
A1: Q is open;
  let P be Subset of A;
  assume P=Q;
  then Q /\ [#] A = P by XBOOLE_1:28;
  hence thesis by A1,Th32;
end;

theorem Th34:
  Q is closed implies for P being Subset of A st P=Q holds P is closed
proof
  assume
A1: Q is closed;
  let P be Subset of A;
  assume P=Q;
  then Q /\ [#] A = P by XBOOLE_1:28;
  hence thesis by A1,PRE_TOPC:43;
end;

theorem
  F is open implies for G being Subset-Family of A st G=F holds G is open
proof
  assume
A1: F is open;
  let G be Subset-Family of A;
  assume
A2: G=F;
  let P be Subset of A such that
A3: P in G;
  reconsider PP=P as Subset of T by PRE_TOPC:39;
  PP is open by A1,A2,A3,Def1;
  hence thesis by Th33;
end;

theorem
  F is closed implies for G being Subset-Family of A st G=F holds G is closed
proof
  assume
A1: F is closed;
  let G be Subset-Family of A;
  assume
A2: G=F;
  let P be Subset of A such that
A3: P in G;
  reconsider PP=P as Subset of T by PRE_TOPC:39;
  PP is closed by A1,A2,A3,Def2;
  hence thesis by Th34;
end;

canceled;

theorem Th38:
  M /\ N is Subset of T|N
proof
  M /\ N c= N by XBOOLE_1:17;
  then M /\ N c= [#](T|N) by PRE_TOPC:def 10;
  hence thesis;
end;

::
::     RESTRICTION OF A FAMILY
::

definition
  let T be TopStruct, P be Subset of T, F be Subset-Family of T;
  func F|P -> Subset-Family of T|P means
  :Def3:
  for Q being Subset of T|P
  holds Q in it iff ex R being Subset of T st R in F & R /\ P = Q;
  existence
  proof
    thus ex G being Subset-Family of T|P st for Q being Subset of T|P holds Q
    in G iff ex R being Subset of T st R in F & R /\ P = Q
    proof
      defpred X[Subset of T|P] means ex R being Subset of T st R in F & R /\ P
      = $1;
      ex G being Subset-Family of T|P st for Q being Subset of T|P holds Q
      in G iff X[Q] from SUBSET_1:sch 3;
      hence thesis;
    end;
  end;
  uniqueness
  proof
    thus for G,H being Subset-Family of T|P st (for Q being Subset of T|P
holds Q in G iff ex R being Subset of T st R in F & R /\ P = Q) & (for Q being
Subset of T|P holds Q in H iff ex R being Subset of T st R in F & R /\ P = Q)
    holds G = H
    proof
      let G,H be Subset-Family of T|P such that
A1:   for Q being Subset of T|P holds Q in G iff ex R being Subset of
      T st R in F & R /\ P = Q and
A2:   for Q being Subset of T|P holds Q in H iff ex R being Subset of
      T st R in F & R /\ P = Q;
      x in G iff x in H
      proof
        hereby
          assume
A3:       x in G;
          then reconsider X=x as Subset of T|P;
          ex R being Subset of T st R in F & R /\ P = X by A1,A3;
          hence x in H by A2;
        end;
        assume
A4:     x in H;
        then reconsider X=x as Subset of T|P;
        ex R being Subset of T st R in F & R /\ P = X by A2,A4;
        hence thesis by A1;
      end;
      hence thesis by TARSKI:2;
    end;
  end;
end;

canceled;

theorem
  F c= G implies F|M c= G|M
proof
  assume
A1: F c= G;
  let x;
  assume
A2: x in F|M;
  then reconsider X=x as Subset of T|M;
  ex R being Subset of T st R in F & R /\ M = X by A2,Def3;
  hence thesis by A1,Def3;
end;

theorem Th41:
  Q in F implies Q /\ M in F|M
proof
  reconsider QQ = Q /\ M as Subset of T|M by Th38;
A1: Q /\ M = QQ;
  assume Q in F;
  hence thesis by A1,Def3;
end;

theorem
  Q c= union F implies Q /\ M c= union(F|M)
proof
  assume
A1: Q c= union F;
  now
    assume M <> {};
    thus Q /\ M c= union(F|M)
    proof
      let x;
      assume
A2:   x in Q /\ M;
      then x in Q by XBOOLE_0:def 4;
      then consider Z being set such that
A3:   x in Z and
A4:   Z in F by A1,TARSKI:def 4;
      reconsider ZZ=Z as Subset of T by A4;
      ZZ /\ M in F|M by A4,Th41;
      then reconsider ZP = ZZ /\ M as Subset of T|M;
A5:   ZP in F|M by A4,Th41;
      x in M by A2,XBOOLE_0:def 4;
      then x in ZP by A3,XBOOLE_0:def 4;
      hence thesis by A5,TARSKI:def 4;
    end;
  end;
  hence thesis by XBOOLE_1:2;
end;

theorem
  M c= union F implies M = union (F|M)
proof
  assume
A1: M c= union F;
  x in M iff x in union(F|M)
  proof
    hereby
      assume
A2:   x in M;
      then consider A being set such that
A3:   x in A and
A4:   A in F by A1,TARSKI:def 4;
      reconsider A9=A as Subset of T by A4;
      A /\ M c= M by XBOOLE_1:17;
      then A /\ M c= [#](T|M) by PRE_TOPC:def 10;
      then reconsider B=A9 /\ M as Subset of T|M;
A5:   B in F|M by A4,Def3;
      x in A /\ M by A2,A3,XBOOLE_0:def 4;
      hence x in union(F|M) by A5,TARSKI:def 4;
    end;
    assume x in union(F|M);
    then consider A being set such that
A6: x in A and
A7: A in F|M by TARSKI:def 4;
    reconsider B = A as Subset of T|M by A7;
    ex R being Subset of T st R in F & R /\ M = B by A7,Def3;
    hence thesis by A6,XBOOLE_0:def 4;
  end;
  hence thesis by TARSKI:2;
end;

theorem Th44:
  union(F|M) c= union F
proof
  let x;
  assume x in union(F|M);
  then consider A being set such that
A1: x in A and
A2: A in F|M by TARSKI:def 4;
  reconsider Q = A as Subset of T|M by A2;
  consider R being Subset of T such that
A3: R in F and
A4: R /\ M = Q by A2,Def3;
  x in R by A1,A4,XBOOLE_0:def 4;
  hence thesis by A3,TARSKI:def 4;
end;

theorem
  M c= union (F|M) implies M c= union F
proof
  assume
A1: M c= union(F|M);
  union(F|M) c= union F by Th44;
  hence thesis by A1,XBOOLE_1:1;
end;

theorem
  F is finite implies F|M is finite
proof
  defpred X[set,set] means for X being Subset of T st X = $1 holds $2 = X /\ M;
A1: for x st x in F ex y st X[x,y]
  proof
    let x;
    assume x in F;
    then reconsider X=x as Subset of T;
    reconsider y=X /\ M as set;
    take y;
    thus thesis;
  end;
  consider f being Function such that
A2: dom f = F and
A3: for x st x in F holds X[x,f.x] from CLASSES1:sch 1(A1);
  x in rng f iff x in F|M
  proof
    hereby
      assume x in rng f;
      then consider y such that
A4:   y in dom f and
A5:   x=f.y by FUNCT_1:def 5;
      reconsider Y=y as Subset of T by A2,A4;
      Y /\ M c= M by XBOOLE_1:17;
      then Y /\ M c= [#](T|M) by PRE_TOPC:def 10;
      then reconsider X=f.y as Subset of T|M by A2,A3,A4;
      f.y = Y /\ M by A2,A3,A4;
      then X in F|M by A2,A4,Def3;
      hence x in F|M by A5;
    end;
    assume
A6: x in F|M;
    then reconsider X=x as Subset of T|M;
    consider R being Subset of T such that
A7: R in F and
A8: R /\ M = X by A6,Def3;
    f.R = R /\ M by A3,A7;
    hence thesis by A2,A7,A8,FUNCT_1:def 5;
  end;
  then rng f = F|M by TARSKI:2;
  then
A9: f.:(F) = F|M by A2,RELAT_1:146;
  assume F is finite;
  hence thesis by A9,FINSET_1:17;
end;

theorem
  F is open implies F|M is open
proof
  assume
A1: F is open;
  let Q be Subset of T|M;
  assume Q in F|M;
  then consider R being Subset of T such that
A2: R in F and
A3: R /\ M = Q by Def3;
  reconsider R as Subset of T;
A4: Q = R /\ [#](T|M) by A3,PRE_TOPC:def 10;
  R is open by A1,A2,Def1;
  hence thesis by A4,Th32;
end;

theorem
  F is closed implies F|M is closed
proof
  assume
A1: F is closed;
  let Q be Subset of T|M;
  assume Q in F|M;
  then consider R being Subset of T such that
A2: R in F and
A3: R /\ M = Q by Def3;
  reconsider R as Subset of T;
A4: Q = R /\ [#](T|M) by A3,PRE_TOPC:def 10;
  R is closed by A1,A2,Def2;
  hence thesis by A4,PRE_TOPC:43;
end;

theorem
  for F being Subset-Family of A st F is open ex G being Subset-Family
  of T st G is open & for AA being Subset of T st AA = [#] A holds F = G|AA
proof
  let F be Subset-Family of A;
  assume
A1: F is open;
  [#] A c= [#]T by PRE_TOPC:def 9;
  then reconsider At = [#] A as Subset of T;
  defpred X[Subset of T] means ex X1 being Subset of T st X1 = $1 & X1 is open
  & $1 /\ At in F;
  consider G being Subset-Family of T such that
A2: for X being Subset of T holds X in G iff X[X] from SUBSET_1:sch 3;
  take G;
  thus G is open
  proof
    let H be Subset of T;
    assume H in G;
    then ex X1 being Subset of T st X1 = H & X1 is open & H /\ At in F by A2;
    hence thesis;
  end;
  let AA be Subset of T;
  assume
A3: AA = [#] A;
  then F c= bool AA;
  then F c= bool [#](T|AA) by PRE_TOPC:def 10;
  then reconsider FF = F as Subset-Family of T|AA;
  for X being Subset of T|AA holds X in FF iff ex X9 being Subset of T st
  X9 in G & X9 /\ AA=X
  proof
    let X be Subset of T|AA;
    thus X in FF implies ex X9 being Subset of T st X9 in G & X9 /\ AA=X
    proof
      assume
A4:   X in FF;
      then reconsider XX=X as Subset of A;
      XX is open by A1,A4,Def1;
      then consider Y being Subset of T such that
A5:   Y is open & Y /\ [#] A = XX by Th32;
      take Y;
      thus thesis by A2,A3,A4,A5;
    end;
    given X9 being Subset of T such that
A6: X9 in G and
A7: X9 /\ AA=X;
    ex X1 being Subset of T st X1 = X9 & X1 is open & X9 /\ At in F by A2
    ,A6;
    hence thesis by A3,A7;
  end;
  hence thesis by Def3;
end;

theorem
  ex f being Function st dom f = F & rng f = F|P & for x st x in F for Q
  st Q = x holds f.x = Q /\ P
proof
  defpred X[set,set] means for Q st Q=$1 holds $2=Q /\ P;
A1: for x st x in F ex y st X[x,y]
  proof
    let x;
    assume x in F;
    then reconsider Q=x as Subset of T;
    reconsider y=Q /\ P as set;
    take y;
    thus thesis;
  end;
  consider f being Function such that
A2: dom f = F and
A3: for x st x in F holds X[x,f.x] from CLASSES1:sch 1(A1);
  take f;
  thus dom f = F by A2;
  x in rng f iff x in F|P
  proof
    hereby
      assume x in rng f;
      then consider y such that
A4:   y in dom f and
A5:   f.y=x by FUNCT_1:def 5;
      reconsider Y=y as Subset of T by A2,A4;
      Y /\ P c= P by XBOOLE_1:17;
      then Y /\ P c= [#](T|P) by PRE_TOPC:def 10;
      then reconsider X=x as Subset of T|P by A2,A3,A4,A5;
      X = Y /\ P by A2,A3,A4,A5;
      hence x in F|P by A2,A4,Def3;
    end;
    assume
A6: x in F|P;
    then reconsider X=x as Subset of T|P;
    consider Q be Subset of T such that
A7: Q in F and
A8: Q /\ P = X by A6,Def3;
    reconsider p=Q as set;
    f.p = x by A3,A7,A8;
    hence thesis by A2,A7,FUNCT_1:def 5;
  end;
  hence rng f = F|P by TARSKI:2;
  thus thesis by A3;
end;

canceled;

theorem Th52:
  for X,Y being 1-sorted, f being Function of X, Y st [#]Y = {}
  implies [#]X = {} holds f"([#]Y) = [#]X
proof
  let X,Y be 1-sorted, f be Function of X, Y such that
A1: [#]Y={} implies [#]X={};
  f"(rng f) = dom f by RELAT_1:169
    .= [#]X by A1,FUNCT_2:def 1;
  then [#]X c= f"([#]Y) by RELAT_1:178;
  hence thesis by XBOOLE_0:def 10;
end;

canceled;

theorem
  for T being 1-sorted, S being non empty 1-sorted, f being Function of
  T, S, H being Subset-Family of S holds ("f).:H is Subset-Family of T
proof
  let T be 1-sorted, S be non empty 1-sorted, f be Function of T, S, H be
  Subset-Family of S;
  ("f).:H c= rng "f & rng "f c= bool dom f by FUNCT_3:25,RELAT_1:144;
  then ("f).:H c= bool dom f by XBOOLE_1:1;
  hence thesis by FUNCT_2:def 1;
end;

::
::          CONTINUOUS MAPPING
::

reserve S for non empty TopStruct,
  f for Function of T, S,
  H for Subset-Family of S;

theorem Th55:
  for X,Y being TopStruct, f being Function of X,Y st [#]Y = {}
  implies [#]X = {} holds f is continuous iff for P being Subset of Y st P is
  open holds f"P is open
proof
  let X,Y be TopStruct, f be Function of X,Y;
  assume
A1: [#]Y={} implies [#]X={};
  hereby
    assume
A2: f is continuous;
    let P1 be Subset of Y;
    assume P1 is open;
    then P1` is closed by TOPS_1:30;
    then
A3: f"(P1`) is closed by A2,PRE_TOPC:def 12;
    f"(P1`) = f"([#]Y) \ f"P1 by FUNCT_1:138
      .= [#]X \ f"P1 by A1,Th52
      .= (f"P1)`;
    hence f"P1 is open by A3,TOPS_1:30;
  end;
  assume
A4: for P1 being Subset of Y st P1 is open holds f"P1 is open;
  let P1 be Subset of Y;
  assume P1 is closed;
  then P1` is open by TOPS_1:29;
  then
A5: f"(P1`) is open by A4;
  f"(P1`) = f"([#]Y) \ f"P1 by FUNCT_1:138
    .= [#]X \ f"P1 by A1,Th52
    .= (f"P1)`;
  hence thesis by A5,TOPS_1:29;
end;

theorem Th56:
  for T being TopSpace, S being TopSpace, f being Function of T, S
holds f is continuous iff for P1 being Subset of S holds Cl(f"P1) c= f"(Cl P1)
proof
  let T be TopSpace, S be TopSpace, f be Function of T, S;
  hereby
    assume
A1: f is continuous;
    let P1 be Subset of S;
    Cl(Cl P1) = Cl P1;
    then Cl P1 is closed by PRE_TOPC:52;
    then
A2: f"(Cl P1) is closed by A1,PRE_TOPC:def 12;
    f"P1 c= f"(Cl P1) by PRE_TOPC:48,RELAT_1:178;
    then Cl(f"P1) c= Cl(f"(Cl P1)) by PRE_TOPC:49;
    hence Cl(f"P1) c= f"(Cl P1) by A2,PRE_TOPC:52;
  end;
  assume
A3: for P1 being Subset of S holds Cl(f"P1) c= f"(Cl P1);
  let P1 be Subset of S;
  assume P1 is closed;
  then Cl P1 = P1 by PRE_TOPC:52;
  then f"P1 c= Cl(f"P1) & Cl(f"P1) c= f"P1 by A3,PRE_TOPC:48;
  then f"P1 = Cl(f"P1) by XBOOLE_0:def 10;
  hence thesis by PRE_TOPC:52;
end;

theorem Th57:
  for T being TopSpace, S being non empty TopSpace, f being
Function of T, S holds f is continuous iff for P being Subset of T holds f.:(Cl
  P) c= Cl(f.:P)
proof
  let T be TopSpace, S be non empty TopSpace, f be Function of T, S;
  hereby
    assume
A1: f is continuous;
    let P be Subset of T;
    P c= [#]T;
    then P c= dom f by FUNCT_2:def 1;
    then
A2: Cl P c= Cl(f"(f.:P)) by FUNCT_1:146,PRE_TOPC:49;
    Cl(f"(f.:P)) c= f"(Cl(f.:P)) by A1,Th56;
    then Cl P c= f"(Cl(f.:P)) by A2,XBOOLE_1:1;
    then
A3: f.:(Cl P) c= f.:(f"(Cl(f.:P))) by RELAT_1:156;
    f.:(f"(Cl(f.:P))) c= Cl(f.:P) by FUNCT_1:145;
    hence f.:(Cl P) c= Cl(f.:P) by A3,XBOOLE_1:1;
  end;
  assume
A4: for P being Subset of T holds f.:(Cl P) c= Cl(f.:P);
  now
    let P1 be Subset of S;
    Cl(f"P1) c= [#]T;
    then Cl(f"P1) c= dom f by FUNCT_2:def 1;
    then
A5: Cl(f"P1) c= f"(f.:Cl(f"P1)) by FUNCT_1:146;
    f.:(Cl(f"P1)) c= Cl(f.:(f"P1)) & Cl(f.:(f"P1)) c= Cl P1 by A4,FUNCT_1
    :145 ,PRE_TOPC:49;
    then f.:(Cl(f"P1)) c= Cl P1 by XBOOLE_1:1;
    then f"(f.:(Cl(f"P1))) c= f"(Cl P1) by RELAT_1:178;
    hence Cl(f"P1) c= f"(Cl P1) by A5,XBOOLE_1:1;
  end;
  hence thesis by Th56;
end;

theorem Th58:
  for T,V being TopStruct,S being non empty TopStruct, f being
  Function of T,S, g being Function of S,V holds f is continuous & g is
  continuous implies g*f is continuous
proof
  let T,V be TopStruct,S be non empty TopStruct;
  let f be Function of T,S, g be Function of S,V;
  assume that
A1: f is continuous and
A2: g is continuous;
  let P be Subset of V;
  assume P is closed;
  then (g*f)"P = f"(g"P) & g"P is closed by A2,
  PRE_TOPC:def 12,RELAT_1:181;
  hence thesis by A1,PRE_TOPC:def 12;
end;

theorem
  f is continuous & H is open implies for F st F=("f).:H holds F is open
proof
  assume that
A1: f is continuous and
A2: H is open;
  let F such that
A3: F=("f).:H;
  let X be Subset of T;
  assume X in F;
  then consider y being set such that
A4: y in dom "f and
A5: y in H and
A6: X=("f).y by A3,FUNCT_1:def 12;
  reconsider Y=y as Subset of S by A5;
A7: X = f"Y by A4,A6,FUNCT_3:24;
A8: [#]S={} implies [#]T={};
  Y is open by A2,A5,Def1;
  hence thesis by A1,A8,A7,Th55;
end;

theorem
  for T, S being TopStruct, f being Function of T, S, H being
  Subset-Family of S st f is continuous & H is closed holds for F being
  Subset-Family of T st F=("f).:H holds F is closed
proof
  let T, S be TopStruct, f be Function of T, S, H be Subset-Family of S;
  assume that
A1: f is continuous and
A2: H is closed;
  let F be Subset-Family of T such that
A3: F=("f).:H;
  let X be Subset of T;
  assume X in F;
  then consider y being set such that
A4: y in dom "f and
A5: y in H and
A6: X=("f).y by A3,FUNCT_1:def 12;
  reconsider Y=y as Subset of S by A5;
A7: X = f"Y by A4,A6,FUNCT_3:24;
  Y is closed by A2,A5,Def2;
  hence thesis by A1,A7,PRE_TOPC:def 12;
end;

definition
  let S, T be set, f be Function of S,T;
  assume
A1: rng f = T & f is one-to-one;
  func f/" -> Function of T,S equals
  :Def4:
  f";
  coherence by A1,FUNCT_2:31;
end;

notation
  let S, T be set, f be Function of S,T;
  synonym f" for f/";
end;

canceled;

theorem Th62:
  for T being 1-sorted, S being non empty 1-sorted, f being
Function of T,S st rng f = [#]S & f is one-to-one holds dom(f") = [#]S & rng(f"
  ) = [#]T
proof
  let T be 1-sorted, S be non empty 1-sorted, f be Function of T,S;
  assume that
A1: rng f = [#]S and
A2: f is one-to-one;
  thus dom(f") = dom((f qua Function)") by A1,A2,Def4
    .= [#]S by A1,A2,FUNCT_1:54;
  thus rng(f") = rng((f qua Function)") by A1,A2,Def4
    .= dom f by A2,FUNCT_1:55
    .= [#]T by FUNCT_2:def 1;
end;

theorem Th63:
  for T, S being 1-sorted, f being Function of T,S st rng f = [#]S
  & f is one-to-one holds f" is one-to-one
proof
  let T, S be 1-sorted, f be Function of T,S;
  assume that
A1: rng f = [#]S and
A2: f is one-to-one;
  (f qua Function)" is one-to-one by A2,FUNCT_1:62;
  hence thesis by A1,A2,Def4;
end;

theorem Th64:
  for T being 1-sorted, S being non empty 1-sorted, f being
  Function of T,S st rng f = [#]S & f is one-to-one holds (f")" = f
proof
  let T be 1-sorted, S be non empty 1-sorted, f be Function of T,S;
  assume that
A1: rng f = [#]S and
A2: f is one-to-one;
  f = ((f qua Function)")" by A2,FUNCT_1:65;
  then
A3: f = (f" qua Function)" by A1,A2,Def4;
  rng(f") = [#]T by A1,A2,Th62;
  hence thesis by A1,A2,A3,Def4,Th63;
end;

theorem
  for T, S being 1-sorted, f being Function of T,S st rng f = [#]S & f
  is one-to-one holds f"*f = id dom f & f*f" = id rng f
proof
  let T, S be 1-sorted, f be Function of T,S;
  assume that
A1: rng f = [#]S and
A2: f is one-to-one;
  (f qua Function)"*f = id dom f & f*(f qua Function)" = id rng f by A2,
  FUNCT_1:61;
  hence thesis by A1,A2,Def4;
end;

theorem Th66:
  for T being 1-sorted, S, V being non empty 1-sorted, f being
  Function of T,S, g being Function of S,V st rng f = [#]S & f is one-to-one &
  dom g = [#]S & rng g = [#]V & g is one-to-one holds (g*f)" = f"*g"
proof
  let T be 1-sorted, S, V be non empty 1-sorted;
  let f be Function of T,S;
  let g be Function of S,V;
  assume that
A1: rng f = [#]S and
A2: f is one-to-one;
  assume that
A3: dom g = [#]S and
A4: rng g = [#] V and
A5: g is one-to-one;
  rng(g*f) = [#] V & g*f is one-to-one by A1,A2,A3,A4,A5,FUNCT_1:46 ,
  RELAT_1:47;
  then
A6: (g*f)" = ((g*f) qua Function)" by Def4;
A7: f" = (f qua Function)" by A1,A2,Def4;
  g" = (g qua Function)" by A4,A5,Def4;
  hence thesis by A2,A5,A7,A6,FUNCT_1:66;
end;

theorem Th67:
  for T, S being 1-sorted, f being Function of T, S, P being
  Subset of T st rng f = [#]S & f is one-to-one holds f.:P = (f")"P
proof
  let T, S be 1-sorted, f be Function of T, S, P be Subset of T;
  assume that
A1: rng f = [#]S and
A2: f is one-to-one;
  f.:P = ((f qua Function)")"P by A2,FUNCT_1:154;
  hence thesis by A1,A2,Def4;
end;

theorem Th68:
  for T, S being 1-sorted, f being Function of T,S, P1 being
  Subset of S st rng f = [#]S & f is one-to-one holds f"P1 = (f").:P1
proof
  let T, S be 1-sorted, f be Function of T, S, P1 be Subset of S;
  assume that
A1: rng f = [#]S and
A2: f is one-to-one;
  f"P1 = ((f qua Function)").:P1 by A2,FUNCT_1:155;
  hence thesis by A1,A2,Def4;
end;
 
::
::           HOMEOMORPHISM
::

definition
  let S, T be TopStruct, f be Function of S, T;
  attr f is being_homeomorphism means
  :Def5:
  dom f = [#]S & rng f = [#]T & f
  is one-to-one & f is continuous & f" is continuous;
end;

canceled;

theorem
  f is being_homeomorphism implies f" is being_homeomorphism
proof
  assume
A1: f is being_homeomorphism;
  then
A2: rng f = [#](S) & f is one-to-one by Def5;
  hence dom(f") = [#]S & rng (f") = [#](T) by Th62;
  thus f" is one-to-one by A2,Th63;
  thus f" is continuous by A1,Def5;
  f is continuous by A1,Def5;
  hence thesis by A2,Th64;
end;

theorem
  for T, S, V being non empty TopStruct, f being Function of T,S, g
  being Function of S,V st f is being_homeomorphism & g is being_homeomorphism
  holds g*f is being_homeomorphism
proof
  let T, S, V be non empty TopStruct;
  let f be Function of T,S;
  let g be Function of S,V;
  assume that
A1: f is being_homeomorphism and
A2: g is being_homeomorphism;
A3: rng f = [#](S) & dom g = [#]S by A1,A2,Def5;
A4: rng g = [#](V) by A2,Def5;
  dom f = [#]T by A1,Def5;
  hence dom(g*f) = [#]T & rng(g*f) = [#] V by A3,A4,RELAT_1:46,47;
A5: f is one-to-one & g is one-to-one by A1,A2,Def5;
  hence g*f is one-to-one by FUNCT_1:46;
  f is continuous & g is continuous by A1,A2,Def5;
  hence g*f is continuous by Th58;
  f" is continuous & g" is continuous by A1,A2,Def5;
  then f"*g" is continuous by Th58;
  hence thesis by A3,A4,A5,Th66;
end;

theorem
  f is being_homeomorphism iff dom f = [#]T & rng f = [#]S & f is
  one-to-one & for P holds P is closed iff f.:P is closed
proof
  hereby
    assume
A1: f is being_homeomorphism;
    hence
A2: dom f = [#]T & rng f = [#]S & f is one-to-one by Def5;
    let P;
A3: f" is continuous by A1,Def5;
    hereby
      assume
A4:   P is closed;
      (f")"P = ((f qua Function)")"P by A2,Def4
        .= f.:P by A2,FUNCT_1:154;
      hence f.:P is closed by A3,A4,PRE_TOPC:def 12;
    end;
    assume
A5: f.:P is closed;
    f is continuous by A1,Def5;
    then
A6: f"(f.:P) is closed by A5,PRE_TOPC:def 12;
    dom f = [#]T by FUNCT_2:def 1;
    then
A7: P c= f"(f.:P) by FUNCT_1:146;
    f"(f.:P) c= P by A2,FUNCT_1:152;
    hence P is closed by A6,A7,XBOOLE_0:def 10;
  end;
  assume that
A8: dom f = [#]T and
A9: rng f = [#]S and
A10: f is one-to-one;
  assume
A11: for P being Subset of T holds P is closed iff f.:P is closed;
A12: f is continuous
  proof
    let B be Subset of S such that
A13: B is closed;
    set D = f"B;
    B = f.:D by A9,FUNCT_1:147;
    hence thesis by A11,A13;
  end;
  f" is continuous
  proof
    let B be Subset of T such that
A14: B is closed;
    (f")"B = ((f qua Function)")"B by A9,A10,Def4
      .= f.:B by A10,FUNCT_1:154;
    hence thesis by A11,A14;
  end;
  hence thesis by A8,A9,A10,A12,Def5;
end;

reserve T for non empty TopSpace,
  S for TopSpace,
  P1 for Subset of S,
  f for Function of T, S;

theorem
  f is being_homeomorphism iff dom f = [#]T & rng f = [#]S & f is
  one-to-one & for P1 holds f"(Cl P1) = Cl(f"P1)
proof
  hereby
    assume
A1: f is being_homeomorphism;
    hence
A2: dom f = [#]T & rng f = [#]S & f is one-to-one by Def5;
    let P1;
    f is continuous by A1,Def5;
    then
A3: Cl(f"P1) c= f"(Cl P1) by Th56;
    f" is continuous by A1,Def5;
    then
A4: (f").:(Cl P1) c= Cl((f").:P1) by Th57;
    f"(Cl P1) = (f").:(Cl P1) & f"P1 = (f").:P1 by A2,Th68;
    hence f"(Cl P1) = Cl(f"P1) by A3,A4,XBOOLE_0:def 10;
  end;
  assume that
A5: dom f = [#]T and
A6: rng f = [#]S & f is one-to-one;
  assume
A7: for P1 holds f"(Cl P1) = Cl(f"P1);
  thus dom f = [#]T & rng f = [#]S & f is one-to-one by A5,A6;
  for P1 holds Cl(f"P1) c= f"(Cl P1) by A7;
  hence f is continuous by Th56;
  now
    let P1;
    (f").:(Cl P1) = f"(Cl P1) & (f").:P1 = f"P1 by A6,Th68;
    hence (f").:(Cl P1) c= Cl((f").:P1) by A7;
  end;
  hence thesis by Th57;
end;

reserve T for TopSpace,
  S for non empty TopSpace,
  P for Subset of T,
  f for Function of T, S;

theorem
  f is being_homeomorphism iff dom f = [#]T & rng f = [#]S & f is
  one-to-one & for P holds f.:(Cl P) = Cl(f.:P)
proof
  hereby
    assume
A1: f is being_homeomorphism;
    hence
A2: dom f = [#]T & rng f = [#]S & f is one-to-one by Def5;
    let P;
    f is continuous by A1,Def5;
    then
A3: f.:(Cl P) c= Cl(f.:P) by Th57;
    f" is continuous by A1,Def5;
    then
A4: Cl((f")"P) c= (f")"(Cl P) by Th56;
    (f")"P = f.:P & (f")"(Cl P) = f.:(Cl P) by A2,Th67;
    hence f.:(Cl P) = Cl(f.:P) by A3,A4,XBOOLE_0:def 10;
  end;
  assume that
A5: dom f = [#]T and
A6: rng f = [#]S & f is one-to-one;
  assume
A7: for P holds f.:(Cl P) = Cl(f.:P);
  thus dom f = [#]T & rng f = [#]S & f is one-to-one by A5,A6;
  for P holds f.:(Cl P) c= Cl(f.:P) by A7;
  hence f is continuous by Th57;
  now
    let P;
    (f")"P = f.:P & (f")"(Cl P) = f.:(Cl P) by A6,Th67;
    hence Cl((f")"P) c= (f")"(Cl P) by A7;
  end;
  hence thesis by Th56;
end;

theorem Th75: :: TOPREAL5:5, AK, 21.02.2006
  for X,Y being non empty TopSpace for f being Function of X,Y, A
  being Subset of X st f is continuous & A is connected holds f.:A is connected
proof
  let X,Y be non empty TopSpace;
  let f be Function of X,Y, A be Subset of X;
  assume
A1: f is continuous;
  assume
A2: A is connected;
  assume not f.:A is connected;
  then consider P,Q being Subset of Y such that
A3: f.:A = P \/ Q and
A4: P,Q are_separated and
A5: P <> {}(Y) and
A6: Q <> {}(Y) by CONNSP_1:16;
  reconsider P1=f"P,Q1=f"Q as Subset of X;
  set P2=P1/\A,Q2=Q1/\A;
A7: f"({}Y) = {} by RELAT_1:171;
  consider y being Element of P;
  y in f.:A by A3,A5,XBOOLE_0:def 3;
  then consider x such that
A8: x in dom f and
A9: x in A and
A10: y=f.x by FUNCT_1:def 12;
  x in f"P by A5,A8,A10,FUNCT_1:def 13;
  then
A11: P2<>{} by A9,XBOOLE_0:def 4;
A12: the carrier of X=dom f by FUNCT_2:def 1;
  P misses Cl Q by A4,CONNSP_1:def 1;
  then
A13: f"(P) /\ f"(Cl Q) = f"(P /\ Cl Q) & f"(P /\ Cl Q)=f"({}Y) by
  FUNCT_1
  :137 ,XBOOLE_0:def 7;
  Cl Q1 c=f"(Cl Q) by A1,Th56;
  then P1 /\ Cl Q1 = {} by A13,A7,XBOOLE_1:3,26;
  then
A14: P1 misses Cl Q1 by XBOOLE_0:def 7;
  Cl P misses Q by A4,CONNSP_1:def 1;
  then
A15: f"(Cl P) /\ f"Q = f"(Cl P /\ Q) & f"(Cl P /\ Q)=f"({}Y) by
  FUNCT_1:
  137 ,XBOOLE_0:def 7;
  Cl P1 c=f"(Cl P) by A1,Th56;
  then Cl P1 /\ Q1 = {}X by A15,A7,XBOOLE_1:3,26;
  then Cl P1 misses Q1 by XBOOLE_0:def 7;
  then
A16: P1,Q1 are_separated by A14,CONNSP_1:def 1;
  consider z being Element of Q;
  z in P \/ Q by A6,XBOOLE_0:def 3;
  then consider x1 being set such that
A17: x1 in dom f and
A18: x1 in A and
A19: z=f.x1 by A3,FUNCT_1:def 12;
  x1 in f"Q by A6,A17,A19,FUNCT_1:def 13;
  then
A20: Q2<>{} by A18,XBOOLE_0:def 4;
  f"(f.:A)=f"P \/ f"Q by A3,RELAT_1:175;
  then
A21: A=(P1 \/ Q1)/\A by A12,FUNCT_1:146,XBOOLE_1:28
    .=P2 \/ Q2 by XBOOLE_1:23;
  P2 c=P1 & Q2 c=Q1 by XBOOLE_1:17;
  then ex P3,Q3 being Subset of X st A = P3 \/ Q3 & P3,Q3 are_separated & P3
  <> {}(X) & Q3 <> {}(X) by A16,A21,A11,A20, CONNSP_1:8;
  hence contradiction by A2,CONNSP_1:16;
end;

theorem :: JORDAN18:2, AK, 21.02.2006
  for S,T being non empty TopSpace, f being Function of S,T, A being
Subset of T st f is being_homeomorphism & A is connected holds f"A is connected
proof
  let S,T be non empty TopSpace, f be Function of S,T, A be Subset of T such
  that
A1: f is being_homeomorphism and
A2: A is connected;
  f" is continuous by A1,Def5;
  then
A3: f".:A is connected by A2,Th75;
  rng f = [#]T & f is one-to-one by A1,Def5;
  hence thesis by A3,Th68;
end;

begin :: Addenda
:: from JORDAN1, 2008.07.07, A.T.

reserve GX,GY for non empty TopSpace;

theorem
  for GX being non empty TopSpace st (for x,y being Point of GX ex GY st
(GY is connected & ex f being Function of GY,GX st f is continuous & x in rng(f
  )& y in rng(f))) holds GX is connected
proof
  let GX;
  assume
A1: for x,y being Point of GX ex GY st (GY is connected & ex f being
  Function of GY,GX st f is continuous & x in rng f & y in rng f);
  for A,B being Subset of GX st [#](GX) = A \/ B & A <> {}(GX) & B <> {}(
  GX) & A is open & B is open holds A meets B
  proof
    let A,B be Subset of GX;
    assume that
A2: [#](GX) = A \/ B and
A3: A <> {}(GX) and
A4: B <> {}(GX) and
A5: A is open & B is open;
    consider v being Element of B;
    consider u being Element of A;
    reconsider y=v as Point of GX by A2,A4,XBOOLE_0:def 3;
    reconsider x=u as Point of GX by A2,A3,XBOOLE_0:def 3;
    consider GY such that
A6: GY is connected and
A7: ex f being Function of GY,GX st f is continuous & x in rng f & y
    in rng f by A1;
    consider f being Function of GY,GX such that
A8: f is continuous and
A9: x in rng f and
A10: y in rng f by A7;
    f"([#] GX)=[#] GY by Th52;
    then
A11: f"A \/ f"B = [#] GY by A2,RELAT_1:175;
    rng f /\ B <> {} by A4,A10,XBOOLE_0:def 4;
    then rng f meets B by XBOOLE_0:def 7;
    then
A12: f"B <> {}(GY) by RELAT_1:173;
    rng f /\ A <> {} by A3,A9,XBOOLE_0:def 4;
    then rng f meets A by XBOOLE_0:def 7;
    then
A13: f"A <> {}(GY) by RELAT_1:173;
    [#]GX <> {};
    then f"A is open & f"B is open by A5,A8,Th55;
    then f"A meets f"B by A6,A11,A13,A12,CONNSP_1:12;
    then f"A /\ f"B <> {} by XBOOLE_0:def 7;
    then f"(A /\ B) <> {} by FUNCT_1:137;
    then rng f meets (A /\ B) by RELAT_1:173;
    then ex u1 being set st u1 in rng f & u1 in A /\ B by XBOOLE_0:3;
    hence thesis by XBOOLE_0:4;
  end;
  hence thesis by CONNSP_1:12;
end;
 
:: Added by AK, 2009.09.20

theorem Th1:
  for X being TopStruct, F being Subset-Family of X holds
  F is open iff F c= the topology of X
  proof
    let X be TopStruct, F be Subset-Family of X;
    thus F is open implies F c= the topology of X
    proof
      assume
A1:   F is open;
      let A be set;
      assume
A2:   A in F;
      then reconsider A as Subset of X;
      A is open by A1,A2,Def1;
      hence thesis by PRE_TOPC:def 5;
    end;
    assume
A3: F c= the topology of X;
    let A be Subset of X;
    thus thesis by A3,PRE_TOPC:def 5;
  end;

theorem
  for X being TopStruct, F being Subset-Family of X holds
  F is closed iff F c= COMPLEMENT the topology of X
  proof
    let X be TopStruct, F be Subset-Family of X;
    thus F is closed implies F c= COMPLEMENT the topology of X
    proof
      assume
A1:   F is closed;
      let A be set;
      assume
A2:   A in F;
      then reconsider A as Subset of X;
      A is closed by A1,A2,Def2;
      then A` is open by TOPS_1:29;
      then A` in the topology of X by PRE_TOPC:def 5;
      hence thesis by SETFAM_1:def 8;
    end;
    assume
A3: F c= COMPLEMENT the topology of X;
    let A be Subset of X;
    assume A in F;
    then A` in the topology of X by A3,SETFAM_1:def 8;
    then A` is open by PRE_TOPC:def 5;
    hence thesis by TOPS_1:29;
  end;

registration
  let X be TopStruct;
  cluster the topology of X -> open;
  coherence by Th1;
  cluster open Subset-Family of X;
  existence
  proof
    take the topology of X;
    thus thesis;
  end;
end;
