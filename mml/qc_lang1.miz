:: A First Order Language
::  by Piotr Rudnicki and Andrzej Trybulec
::
:: Received August 8, 1989
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies NUMBERS, XBOOLE_0, SUBSET_1, ZFMISC_1, TARSKI, XXREAL_0,
      MARGREL1, MCART_1, ARYTM_3, NAT_1, FINSEQ_1, RELAT_1, ORDINAL4, CARD_1,
      REALSET1, XBOOLEAN, BVFUNC_2, ZF_LANG, CLASSES2, FUNCT_1, FUNCOP_1,
      RCOMP_1, QC_LANG1;
 notations TARSKI, XBOOLE_0, ENUMSET1, ZFMISC_1, SUBSET_1, NUMBERS, XXREAL_0,
      MCART_1, NAT_1, RELAT_1, FUNCT_1, RELSET_1, FUNCT_2, FUNCOP_1, FINSEQ_1;
 constructors ENUMSET1, FUNCOP_1, XXREAL_0, XREAL_0, NAT_1, FINSEQ_1, RELSET_1;
 registrations XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, ORDINAL1, XREAL_0,
      FINSEQ_1, CARD_1;
 requirements NUMERALS, REAL, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, XBOOLE_0, FINSEQ_1;
 theorems ZFMISC_1, SUBSET_1, TARSKI, ENUMSET1, FINSEQ_1, MCART_1, NAT_1,
      FUNCT_1, FUNCT_2, RELSET_1, XBOOLE_0, XBOOLE_1, FUNCOP_1, XXREAL_0,
      ORDINAL1;
 schemes NAT_1, FUNCT_2, CLASSES1, XBOOLE_0;

begin

:: Preliminaries

theorem
  for D1 being non empty set, D2 being set, k being Element of D1 holds
  [: {k}, D2 :] c= [: D1, D2 :]
proof
  let D1 be non empty set, D2 be set, k be Element of D1;
  {k} is Subset of D1 by SUBSET_1:63;
  hence thesis by ZFMISC_1:118;
end;

theorem Th2:
  for D1 being non empty set, D2 being set, k1, k2, k3 being
  Element of D1 holds [: {k1, k2, k3}, D2 :] c= [: D1, D2 :]
proof
  let D1 be non empty set, D2 be set, k1, k2, k3 be Element of D1;
  {k1, k2, k3} is Subset of D1 by SUBSET_1:57;
  hence thesis by ZFMISC_1:118;
end;

reserve k, l, m, n for Element of NAT;

definition
  func QC-variables -> set equals
  [: { 4, 5, 6 }, NAT :];
  coherence;
end;

registration
  cluster QC-variables -> non empty;
  coherence;
end;

canceled;

theorem Th4:
  QC-variables c= [: NAT, NAT :] by Th2;

definition
  mode QC-variable is Element of QC-variables;
  func bound_QC-variables -> Subset of QC-variables equals
  [: {4}, NAT :];
  coherence
  proof
    4 in {4, 5, 6} by ENUMSET1:def 1;
    then {4} c= {4, 5, 6} by ZFMISC_1:37;
    hence thesis by ZFMISC_1:118;
  end;
  func fixed_QC-variables -> Subset of QC-variables equals
  [: {5}, NAT :];
  coherence
  proof
    5 in {4, 5, 6} by ENUMSET1:def 1;
    then {5} c= {4, 5, 6} by ZFMISC_1:37;
    hence thesis by ZFMISC_1:118;
  end;
  func free_QC-variables -> Subset of QC-variables equals
  [: {6}, NAT :];
  coherence
  proof
    6 in {4, 5, 6} by ENUMSET1:def 1;
    then {6} c= {4, 5, 6} by ZFMISC_1:37;
    hence thesis by ZFMISC_1:118;
  end;
  func QC-pred_symbols -> set equals
  { [k, l]: 7 <= k };
  coherence;
end;

registration
  cluster bound_QC-variables -> non empty;
  coherence;
  cluster fixed_QC-variables -> non empty;
  coherence;
  cluster free_QC-variables -> non empty;
  coherence;
  cluster QC-pred_symbols -> non empty;
  coherence
  proof
    [7, 7] in { [k, l]: 7 <= k };
    hence thesis;
  end;
end;

canceled 5;

theorem Th10:
  QC-pred_symbols c= [: NAT, NAT :]
proof
  let x be set;
  assume x in QC-pred_symbols;
  then ex k, l being Element of NAT st x = [k, l] & 7 <= k;
  hence thesis by ZFMISC_1:def 2;
end;

definition
  mode QC-pred_symbol is Element of QC-pred_symbols;
end;

definition
  let P be Element of QC-pred_symbols;
  func the_arity_of P -> Element of NAT means
  :Def6:
  P`1 = 7+it;
  existence
  proof
    P in {[k, l] : 7 <= k};
    then consider k, l such that
A1: P = [k, l] and
A2: 7 <= k;
    consider w being Nat such that
A3: k=7+w by A2,NAT_1:10;
    w in NAT by ORDINAL1:def 13;
    hence thesis by A1,A3,MCART_1:7;
  end;
  uniqueness;
end;

reserve P for QC-pred_symbol;

definition
  let k;
  func k-ary_QC-pred_symbols -> Subset of QC-pred_symbols equals
  { P :
  the_arity_of P = k };
  coherence
  proof
    set Y = {7+k};
    [: {7+k}, NAT :] c= QC-pred_symbols
    proof
      let x be set;
      assume
A1:   x in [: {7+k}, NAT :];
      then reconsider x1 = x`1, x2 = x`2 as Element of NAT by MCART_1:12;
      x`1 = 7+k by A1,MCART_1:12;
      then 7 <= x1 by NAT_1:12;
      then [x1, x2] in { [m, n] : 7 <= m };
      hence thesis by A1,MCART_1:23;
    end;
    then reconsider X = [: Y, NAT :] as Subset of QC-pred_symbols;
    X = { P : the_arity_of P = k }
    proof
      thus X c= { P : the_arity_of P = k }
      proof
        let x be set;
        assume
A2:     x in X;
        then reconsider Q = x as QC-pred_symbol;
        x`1 in Y by A2,MCART_1:10;
        then x`1 = 7+k by TARSKI:def 1;
        then the_arity_of Q = k by Def6;
        hence thesis;
      end;
      let x be set;
      assume x in { P : the_arity_of P = k };
      then consider P such that
A3:   x = P and
A4:   the_arity_of P = k;
      P`1 = 7+k by A4,Def6;
      then
A5:   P`1 in Y by TARSKI:def 1;
A6:   P in [: NAT, NAT :] by Th10,TARSKI:def 3;
      then P`2 in NAT by MCART_1:10;
      then [P`1, P`2] in X by A5,ZFMISC_1:106;
      hence thesis by A3,A6,MCART_1:23;
    end;
    hence thesis;
  end;
end;

registration
  let k;
  cluster k-ary_QC-pred_symbols -> non empty;
  coherence
  proof
    set Y = {7+k};
    [: {7+k}, NAT :] c= QC-pred_symbols
    proof
      let x be set;
      assume
A1:   x in [: {7+k}, NAT :];
      then reconsider x1 = x`1, x2 = x`2 as Element of NAT by MCART_1:12;
      x`1 = 7+k by A1,MCART_1:12;
      then 7 <= x1 by NAT_1:12;
      then [x1, x2] in { [m, n] : 7 <= m };
      hence thesis by A1,MCART_1:23;
    end;
    then reconsider X = [: Y, NAT :] as non empty Subset of QC-pred_symbols;
    X = { P : the_arity_of P = k }
    proof
      thus X c= { P : the_arity_of P = k }
      proof
        let x be set;
        assume
A2:     x in X;
        then reconsider Q = x as QC-pred_symbol;
        x`1 in Y by A2,MCART_1:10;
        then x`1 = 7+k by TARSKI:def 1;
        then the_arity_of Q = k by Def6;
        hence thesis;
      end;
      let x be set;
      assume x in { P : the_arity_of P = k };
      then consider P such that
A3:   x = P and
A4:   the_arity_of P = k;
      P`1 = 7+k by A4,Def6;
      then
A5:   P`1 in Y by TARSKI:def 1;
A6:   P in [: NAT, NAT :] by Th10,TARSKI:def 3;
      then P`2 in NAT by MCART_1:10;
      then [P`1, P`2] in X by A5,ZFMISC_1:106;
      hence thesis by A3,A6,MCART_1:23;
    end;
    hence thesis;
  end;
end;

definition
  mode bound_QC-variable is Element of bound_QC-variables;
  mode fixed_QC-variable is Element of fixed_QC-variables;
  mode free_QC-variable is Element of free_QC-variables;
  let k;
  mode QC-pred_symbol of k is Element of k-ary_QC-pred_symbols;
end;

registration
  let k be Element of NAT;
  cluster k-long FinSequence of QC-variables;
  existence
  proof
    consider p being FinSequence of QC-variables such that
A1: len p = k by FINSEQ_1:24;
    take p;
    thus len p = k by A1;
  end;
end;

definition
  let k be Element of NAT;
  mode QC-variable_list of k is k-long FinSequence of QC-variables;
end;

definition
  let D be set;
  canceled;
  attr D is QC-closed means
  :Def9:
   D is Subset of [:NAT, NAT:]* &  :: Includes atomic formulae
  (for k being Element of NAT, p being (QC-pred_symbol of k),
  ll being QC-variable_list of k holds <*p*>^ll in D) &

:: Is closed under VERUM, 'not', '&', and quantification
  <*[0, 0]*> in D & (for p being FinSequence of [:NAT,NAT:]
  st p in D holds <*[1, 0]*>^p in D) &
  (for p, q being FinSequence of [:NAT, NAT:] st p in D & q in D
  holds <*[2, 0]*>^p^q in D) & (for x being bound_QC-variable,
  p being FinSequence of [:NAT, NAT:]
  st p in D holds <*[3, 0]*>^<*x*>^p in D);
end;

Lm1: for k, l being Element of NAT holds <*[k, l]*> is FinSequence of [:NAT,
NAT:]
proof
  let k, l be Element of NAT;
  [k, l] in [:NAT, NAT:] by ZFMISC_1:def 2;
  then rng <*[k, l]*> = {[k, l]} & {[k, l]} c= [:NAT, NAT:] by
  FINSEQ_1:56 ,
  ZFMISC_1:37;
  hence thesis by FINSEQ_1:def 4;
end;

Lm2: for k being Element of NAT, p being (QC-pred_symbol of k), ll being (
QC-variable_list of k) holds <*p*>^ll is FinSequence of [:NAT, NAT:]
proof
  let k be Element of NAT, p be (QC-pred_symbol of k), ll be QC-variable_list
  of k;
  QC-variables c= [:NAT,NAT:] by Th2;
  then
A1: rng ll c= [:NAT,NAT:] by XBOOLE_1:1;
  k-ary_QC-pred_symbols c= [:NAT,NAT:] by Th10,XBOOLE_1:1;
  then rng <*p*> c= [:NAT,NAT:] by XBOOLE_1:1;
  then rng <*p*> \/ rng ll c= [:NAT,NAT:] by A1,XBOOLE_1:8;
  then rng (<*p*>^ll) c= [:NAT,NAT:] by FINSEQ_1:44;
  hence thesis by FINSEQ_1:def 4;
end;

Lm3: for x being bound_QC-variable, p being FinSequence of [:NAT, NAT:] holds
<*[3, 0]*>^<*x*>^p is FinSequence of [:NAT, NAT:]
proof
  reconsider y = <*[3, 0]*> as FinSequence of [:NAT, NAT:] by Lm1;
  let x be bound_QC-variable, p be FinSequence of [:NAT, NAT:];
  bound_QC-variables c= [:NAT,NAT:] by Th4,XBOOLE_1:1;
  then rng <*x*> c= [:NAT,NAT:] by XBOOLE_1:1;
  then reconsider z = <*x*> as FinSequence of [:NAT, NAT:] by FINSEQ_1:def 4;
  y^z^p is FinSequence of [:NAT, NAT:];
  hence thesis;
end;

definition
  func QC-WFF -> non empty set means
  :Def10:
  it is QC-closed & for D being non empty set st D is QC-closed holds it c= D;
  existence
  proof
    <*[0, 0]*> is FinSequence of [:NAT, NAT:] by Lm1;
    then
A1: <*[0, 0]*> in [:NAT, NAT:]* by FINSEQ_1:def 11;
    defpred P[set] means for D being non empty set st D is QC-closed holds $1
    in D;
    consider D0 being set such that
A2: for x being set holds x in D0 iff x in [:NAT, NAT:]* & P[x] from
    XBOOLE_0:sch 1;
A3: for D being non empty set st D is QC-closed holds <*[0, 0]*> in D by
    Def9;
    then reconsider D0 as non empty set by A2,A1;
    take D0;
    D0 c= [:NAT, NAT:]*
    proof
      let x be set;
      thus thesis by A2;
    end;
    hence D0 is Subset of [:NAT, NAT:]*;
    thus for k being Element of NAT, p being (QC-pred_symbol of k), ll being
    QC-variable_list of k holds <*p*>^ll in D0
    proof
      let k be Element of NAT, p be (QC-pred_symbol of k), ll be
      QC-variable_list of k;
      <*p*>^ll is FinSequence of [:NAT, NAT:] by Lm2;
      then
A4:   <*p*>^ll in [:NAT, NAT:]* by FINSEQ_1:def 11;
      for D being non empty set st D is QC-closed holds <*p*>^ll in D by
      Def9;
      hence thesis by A2,A4;
    end;
    thus <*[0, 0]*> in D0 by A2,A1,A3;
    thus for p being FinSequence of [:NAT, NAT:] st p in D0 holds <*[1, 0]*>^p
    in D0
    proof
      reconsider h = <*[1, 0]*> as FinSequence of [:NAT, NAT:] by Lm1;
      let p be FinSequence of [:NAT, NAT:];
      assume
A5:   p in D0;
A6:   for D being non empty set st D is QC-closed holds <*[1, 0]*>^p in D
      proof
        let D be non empty set;
        assume
A7:     D is QC-closed;
        then p in D by A2,A5;
        hence thesis by A7,Def9;
      end;
      h^p is FinSequence of [:NAT, NAT:];
      then <*[1, 0]*>^p in [:NAT, NAT:]* by FINSEQ_1:def 11;
      hence thesis by A2,A6;
    end;
    thus for p, q being FinSequence of [:NAT, NAT:] st p in D0 & q in D0 holds
    <*[2, 0]*>^p^q in D0
    proof
      reconsider h = <*[2, 0]*> as FinSequence of [:NAT, NAT:] by Lm1;
      let p, q be FinSequence of [:NAT, NAT:] such that
A8:   p in D0 & q in D0;
A9:   for D being non empty set st D is QC-closed holds <*[2, 0]*>^p^q in D
      proof
        let D be non empty set;
        assume
A10:    D is QC-closed;
        then p in D & q in D by A2,A8;
        hence thesis by A10,Def9;
      end;
      h^p^q is FinSequence of [:NAT, NAT:];
      then <*[2, 0]*>^p^q in [:NAT, NAT:]* by FINSEQ_1:def 11;
      hence thesis by A2,A9;
    end;
    thus for x being bound_QC-variable, p being FinSequence of [:NAT, NAT:] st
    p in D0 holds <*[3, 0]*>^<*x*>^p in D0
    proof
      let x be bound_QC-variable, p be FinSequence of [:NAT, NAT:];
      assume
A11:  p in D0;
A12:  for D being non empty set st D is QC-closed holds <*[3, 0]*>^<*x*>^
      p in D
      proof
        let D be non empty set;
        assume
A13:    D is QC-closed;
        then p in D by A2,A11;
        hence thesis by A13,Def9;
      end;
      <*[3, 0]*>^<*x*>^p is FinSequence of [:NAT, NAT:] by Lm3;
      then <*[3, 0]*>^<*x*>^p in [:NAT, NAT:]* by FINSEQ_1:def 11;
      hence thesis by A2,A12;
    end;
    let D be non empty set such that
A14: D is QC-closed;
    let x be set;
    assume x in D0;
    hence thesis by A2,A14;
  end;
  uniqueness
  proof
    let D1, D2 be non empty set;
    assume D1 is QC-closed & ( for D being non empty set st D is QC-closed
holds D1 c= D)& D2 is QC-closed & for D being non empty set st D is QC-closed
    holds D2 c= D;
    then D1 c= D2 & D2 c= D1;
    hence thesis by XBOOLE_0:def 10;
  end;
end;

canceled 10;

theorem Th21:
  QC-WFF is QC-closed by Def10;

definition
  mode QC-formula is Element of QC-WFF;
end;

definition
  let P be QC-pred_symbol,l be FinSequence of QC-variables;
  assume
A1: the_arity_of P = len l;
  func P!l -> Element of QC-WFF equals
  :Def11:
  <*P*>^l;
  coherence
  proof
    set k = len l;
    set QCP = {Q where Q is QC-pred_symbol: the_arity_of Q = k };
    P in QCP by A1;
    then reconsider P as QC-pred_symbol of k;
    reconsider l as QC-variable_list of k by FINSEQ_1:def 18;
    reconsider X = <*P*>^l as FinSequence of [:NAT,NAT:] by Lm2;
    X is QC-formula by Def9,Th21;
    hence thesis;
  end;
end;

canceled;

theorem Th23:
  for k being Element of NAT, p being QC-pred_symbol of k, ll be
  QC-variable_list of k holds p!ll = <*p*>^ll
proof
  let k be Element of NAT, p be QC-pred_symbol of k, ll be QC-variable_list of
  k;
  set QCP = {Q where Q is QC-pred_symbol: the_arity_of Q = k };
  p in QCP;
  then
A1: ex Q being QC-pred_symbol st p = Q & the_arity_of Q = k;
  len ll = k by FINSEQ_1:def 18;
  hence thesis by A1,Def11;
end;

definition
  let p be Element of QC-WFF;
  func @p -> FinSequence of [:NAT, NAT:] equals
  p;
  coherence
  proof
    QC-WFF is Subset of [:NAT, NAT:]* & p in QC-WFF by Def9,Th21;
    hence thesis by FINSEQ_1:def 11;
  end;
end;

definition
  func VERUM -> QC-formula equals
  <*[0, 0]*>;
  correctness by Def9,Th21;
  let p be Element of QC-WFF;
  func 'not' p -> QC-formula equals
  <*[1, 0]*>^@p;
  coherence by Def9,Th21;
  correctness;
  let q be Element of QC-WFF;
  func p '&' q -> QC-formula equals
  <*[2, 0]*>^@p^@q;
  correctness by Def9,Th21;
end;

definition
  let x be bound_QC-variable, p be Element of QC-WFF;
  func All(x, p) -> QC-formula equals
  <*[3, 0]*>^<*x*>^@p;
  coherence by Def9,Th21;
end;

reserve F for Element of QC-WFF;

scheme
  QCInd { Prop[Element of QC-WFF] }: for F being Element of QC-WFF holds Prop[
  F]
provided
A1: for k being Element of NAT, P being (QC-pred_symbol of k), ll being
QC-variable_list of k holds Prop[P!ll] and
A2: Prop[VERUM] and
A3: for p being Element of QC-WFF st Prop[p] holds Prop['not' p] and
A4: for p, q being Element of QC-WFF st Prop[p] & Prop[q] holds Prop[p
'&' q] and
A5: for x being bound_QC-variable, p being Element of QC-WFF st Prop[p]
holds Prop[All(x, p)]
proof
  VERUM in { F : Prop[F] } by A2;
  then reconsider X = { F : Prop[F] } as non empty set;
A6: for k being Element of NAT, P being (QC-pred_symbol of k), ll being
  QC-variable_list of k holds <*P*>^ll in X
  proof
    let k be Element of NAT, P be (QC-pred_symbol of k), ll be
    QC-variable_list of k;
    Prop[P!ll] by A1;
    then P!ll in X;
    hence thesis by Th23;
  end;
A7: for x being bound_QC-variable, p being FinSequence of [:NAT, NAT:] st p
  in X holds <*[3, 0]*>^<*x*>^p in X
  proof
    let x be bound_QC-variable, p be FinSequence of [:NAT, NAT:];
    assume p in X;
    then consider p9 being Element of QC-WFF such that
A8: p = p9 and
A9: Prop[p9];
    Prop[All(x, p9)] by A5,A9;
    hence thesis by A8;
  end;
A10: for p, q being FinSequence of [:NAT, NAT:] st p in X & q in X holds <*[
  2, 0]*>^p^q in X
  proof
    let p, q be FinSequence of [:NAT, NAT:];
    assume p in X;
    then consider p9 being Element of QC-WFF such that
A11: p = p9 and
A12: Prop[p9];
    assume q in X;
    then consider q9 being Element of QC-WFF such that
A13: q = q9 and
A14: Prop[q9];
    Prop[p9 '&' q9] by A4,A12,A14;
    hence thesis by A11,A13;
  end;
A15: for p being FinSequence of [:NAT, NAT:] st p in X holds <*[1, 0]*>^p in X
  proof
    let p be FinSequence of [:NAT, NAT:];
    assume p in X;
    then consider p9 being Element of QC-WFF such that
A16: p = p9 and
A17: Prop[p9];
    Prop['not' p9] by A3,A17;
    hence thesis by A16;
  end;
  let F9 be Element of QC-WFF;
A18: X c= [:NAT, NAT:]*
  proof
    let x be set;
    assume x in X;
    then consider p being Element of QC-WFF such that
A19: x = p and
    Prop[p];
    p = @p;
    hence thesis by A19,FINSEQ_1:def 11;
  end;
  <*[0, 0]*> in X by A2;
  then X is QC-closed by A18,A6,A15,A10,A7,Def9;
  then QC-WFF c= X by Def10;
  then F9 in X by TARSKI:def 3;
  then ex F99 being Element of QC-WFF st F9 = F99 & Prop[F99];
  hence thesis;
end;

definition
  let F be Element of QC-WFF;
  attr F is atomic means
  :Def17:
  ex k being Element of NAT, p being (
  QC-pred_symbol of k), ll being QC-variable_list of k st F = p!ll;
  attr F is negative means
  :Def18:
  ex p being Element of QC-WFF st F = 'not' p;
  attr F is conjunctive means
  :Def19:
  ex p, q being Element of QC-WFF st F = p '&' q;
  attr F is universal means
  :Def20:
  ex x being bound_QC-variable, p being Element of QC-WFF st F = All(x, p);
end;

canceled 9;

theorem Th33:
  for F being Element of QC-WFF holds F = VERUM or F is atomic or
  F is negative or F is conjunctive or F is universal
proof
  defpred P[Element of QC-WFF] means $1 = VERUM or $1 is atomic or $1 is
  negative or $1 is conjunctive or $1 is universal;
A1: P[VERUM];
A2: for p being Element of QC-WFF st P[p] holds P['not' p] by Def18;
A3: for x being bound_QC-variable, p being Element of QC-WFF st P[p] holds P
  [All(x, p)] by Def20;
A4: for p, q being Element of QC-WFF st P[p] & P[q] holds P[p '&' q] by
  Def19;
A5: for k being Element of NAT, p being (QC-pred_symbol of k), ll being
  QC-variable_list of k holds P[p!ll] by Def17;
  thus for F being Element of QC-WFF holds P[F] from QCInd (A5, A1, A2, A4, A3
  );
end;

theorem Th34:
  for F being Element of QC-WFF holds 1 <= len @F
proof
  let F be Element of QC-WFF;
  now
    per cases by Th33;
    suppose
      F = VERUM;
      hence thesis by FINSEQ_1:56;
    end;
    suppose
      F is atomic;
      then consider
      k being Element of NAT, p being (QC-pred_symbol of k), ll being
      QC-variable_list of k such that
A1:   F = p!ll by Def17;
      @F = <*p*>^ll by A1,Th23;
      then len @F = len <*p*> + len ll by FINSEQ_1:35
        .= 1 + len ll by FINSEQ_1:56;
      hence thesis by NAT_1:11;
    end;
    suppose
      F is negative;
      then consider p being Element of QC-WFF such that
A2:   F = 'not' p by Def18;
      len @F = len <*[1, 0]*> + len @p by A2,FINSEQ_1:35
        .= 1 + len @p by FINSEQ_1:56;
      hence thesis by NAT_1:11;
    end;
    suppose
      F is conjunctive;
      then consider p, q being Element of QC-WFF such that
A3:   F = p '&' q by Def19;
      @F = <*[2, 0]*>^(@p^@q) by A3,FINSEQ_1:45;
      then len @F = len <*[2, 0]*> + len (@p^@q) by FINSEQ_1:35
        .= 1 + len (@p^@q) by FINSEQ_1:56;
      hence thesis by NAT_1:11;
    end;
    suppose
      F is universal;
      then consider
      x being bound_QC-variable, p being Element of QC-WFF such that
A4:   F = All(x, p) by Def20;
      @F = <*[3, 0]*>^(<*x*>^@p) by A4,FINSEQ_1:45;
      then len @F = len <*[3, 0]*> + len (<*x*>^@p) by FINSEQ_1:35
        .= 1 + len (<*x*>^@p) by FINSEQ_1:56;
      hence thesis by NAT_1:11;
    end;
  end;
  hence thesis;
end;

reserve Q for QC-pred_symbol;

theorem Th35:
  for k being Element of NAT, P being QC-pred_symbol of k holds
  the_arity_of P = k
proof
  let k be Element of NAT, P be QC-pred_symbol of k;
  reconsider P as Element of k-ary_QC-pred_symbols;
  P in { Q : the_arity_of Q = k };
  then ex Q st P = Q & the_arity_of Q = k;
  hence thesis;
end;

reserve F, G for (Element of QC-WFF),

  s for FinSequence;

theorem Th36:
  ((@F.1)`1 = 0 implies F = VERUM) & ((@F.1)`1 = 1 implies F is
  negative) & ((@F.1)`1 = 2 implies F is conjunctive) & ((@F.1)`1 = 3 implies F
  is universal) & ((ex k being Element of NAT st @F.1 is QC-pred_symbol of k)
  implies F is atomic)
proof
A1: now
    per cases by Th33;
    case
      F is atomic;
      then consider
      k being Element of NAT, P being (QC-pred_symbol of k), ll being
      QC-variable_list of k such that
A2:   F = P!ll by Def17;
      @F = <*P*>^ll by A2,Th23;
      then @F.1 = P by FINSEQ_1:58;
      hence ex k being Element of NAT st @F.1 is QC-pred_symbol of k;
    end;
    case
      F = VERUM;
      hence (@F.1)`1 = [0,0]`1 by FINSEQ_1:def 8
        .= 0 by MCART_1:7;
    end;
    case
      F is negative;
      then ex p being Element of QC-WFF st F = 'not' p by Def18;
      then @F.1 = [1, 0] by FINSEQ_1:58;
      hence (@F.1)`1 = 1 by MCART_1:7;
    end;
    case
      F is conjunctive;
      then consider p, q being Element of QC-WFF such that
A3:   F = p '&' q by Def19;
      @F = <*[2, 0]*>^(@p^@q) by A3,FINSEQ_1:45;
      then @F.1 = [2, 0] by FINSEQ_1:58;
      hence (@F.1)`1 = 2 by MCART_1:7;
    end;
    case
      F is universal;
      then consider
      x being bound_QC-variable, p being Element of QC-WFF such that
A4:   F = All(x, p) by Def20;
      @F = <*[3, 0]*>^(<*x*>^@p) by A4,FINSEQ_1:45;
      then @F.1 = [3, 0] by FINSEQ_1:58;
      hence (@F.1)`1 = 3 by MCART_1:7;
    end;
  end;
  now
    let k be Element of NAT, P be QC-pred_symbol of k;
    reconsider P9 = P as QC-pred_symbol;
    P9`1 = 7+the_arity_of P9 by Def6;
    hence P`1 <> 0 & P`1 <> 1 & P`1 <> 2 & P`1 <> 3 by NAT_1:11;
  end;
  hence thesis by A1;
end;

theorem Th37:
  @F = @G^s implies @F = @G
proof
  defpred P[set] means for F,G,s st len @F = $1 & @F = @G^s holds @F = @G;
A1: for n being Nat st for k being Nat st k < n holds P[k] holds P[n]
  proof
    let n be Nat such that
A2: for k being Nat st k < n holds for F, G, s st len @F = k & @F = @G
    ^s holds @F = @G;
    let F, G be (Element of QC-WFF), s be FinSequence such that
A3: len @F = n and
A4: @F = @G^s;
    dom @G = Seg len @G & 1 <= len @G by Th34,FINSEQ_1:def 3;
    then 1 in dom @G;
    then
A5: @F.1 = @G.1 by A4,FINSEQ_1:def 7;
A6: len (@G^s) = len @G + len s by FINSEQ_1:35;
    now
      per cases by Th33;
      suppose
A7:     F = VERUM;
A8:     1 <= len @G by Th34;
A9:     len @F = 1 by A7,FINSEQ_1:56;
        then len @G <= 1 by A4,A6,NAT_1:11;
        then 1 + 0 = 1 + len s by A4,A6,A9,A8,XXREAL_0:1;
        then s = {};
        hence thesis by A4,FINSEQ_1:47;
      end;
      suppose
        F is atomic;
        then consider
        k being Element of NAT, P being (QC-pred_symbol of k), ll
        being QC-variable_list of k such that
A10:    F = P!ll by Def17;
A11:    @F = <*P*>^ll by A10,Th23;
        then
A12:    @G.1 = P by A5,FINSEQ_1:58;
        then G is atomic by Th36;
        then consider
        k9 being Element of NAT, P9 being (QC-pred_symbol of k9), ll9
        being QC-variable_list of k9 such that
A13:    G = P9!ll9 by Def17;
A14:    @G = <*P9*>^ll9 by A13,Th23;
        then
A15:    @G.1 = P9 by FINSEQ_1:58;
        then <*P*>^ll = <*P*>^(ll9^s) by A4,A11,A12,A14,FINSEQ_1:45;
        then ll = ll9^s by FINSEQ_1:46;
        then
A16:    len ll + 0 = len ll9 + len s by FINSEQ_1:35;
        len ll9 = k9 by FINSEQ_1:def 18
          .= the_arity_of P by A12,A15,Th35
          .= k by Th35
          .= len ll by FINSEQ_1:def 18;
        then s = {} by A16;
        hence thesis by A4,FINSEQ_1:47;
      end;
      suppose
        F is negative;
        then consider p being Element of QC-WFF such that
A17:    F = 'not' p by Def18;
        @F.1 = [1, 0] by A17,FINSEQ_1:58;
        then (@G.1)`1 = 1 by A5,MCART_1:7;
        then G is negative by Th36;
        then consider p9 being Element of QC-WFF such that
A18:    G = 'not' p9 by Def18;
        <*[1, 0]*>^@p = <*[1, 0]*>^(@p9^s) by A4,A17,A18,FINSEQ_1:45;
        then
A19:    @p = @p9^s by FINSEQ_1:46;
        len @F = len @p + len <*[1, 0]*> by A17,FINSEQ_1:35
          .= len @p + 1 by FINSEQ_1:57;
        then len @p < len @F by NAT_1:13;
        then @p = @p9 by A2,A3,A19;
        then @p9^{} = @p9^s by A19,FINSEQ_1:47;
        then s = {} by FINSEQ_1:46;
        hence thesis by A4,FINSEQ_1:47;
      end;
      suppose
        F is conjunctive;
        then consider p, q being Element of QC-WFF such that
A20:    F = p '&' q by Def19;
A21:    @F = <*[2, 0]*>^(@p^@q) by A20,FINSEQ_1:45;
        then
A22:    len @F = len (@p^@q) + len <*[2, 0]*> by FINSEQ_1:35
          .= len (@p^@q) + 1 by FINSEQ_1:57;
        then
A23:    len @F = len @p + len @q + 1 by FINSEQ_1:35;
        @F.1 = [2, 0] by A21,FINSEQ_1:58;
        then (@G.1)`1 = 2 by A5,MCART_1:7;
        then G is conjunctive by Th36;
        then consider p9, q9 being Element of QC-WFF such that
A24:    G = p9 '&' q9 by Def19;
A25:    len @p9 <= len @p9 + len (@q9^s) by NAT_1:11;
A26:    @G = <*[2, 0]*>^(@p9^@q9) by A24,FINSEQ_1:45;
        then <*[2, 0]*>^(@p^@q) = <*[2, 0]*>^(@p9^@q9^s) by A4,A21,
        FINSEQ_1:45;
        then
A27:    @p^@q = @p9^@q9^s by FINSEQ_1:46
          .= @p9^(@q9^s) by FINSEQ_1:45;
        then len @F = len @p9 + len (@q9^s) + 1 by A22,FINSEQ_1:35;
        then
A28:    len @p9 < len @F by A25,NAT_1:13;
        len @q <= len @p + len @q by NAT_1:11;
        then
A29:    len @q < len @F by A23,NAT_1:13;
        len @p <= len @p + len @q by NAT_1:11;
        then
A30:    len @p < len @F by A23,NAT_1:13;
        len @p <= len @p9 or len @p9 <= len @p;
        then consider a, b, c, d being FinSequence such that
A31:    a = @p & b = @p9 or a = @p9 & b = @p and
A32:    len a <= len b & a^c = b^d by A27;
        ex t being FinSequence st a^t = b by A32,FINSEQ_1:64;
        then
A33:    @p = @p9 by A2,A3,A31,A30,A28;
        then @q = @q9^s by A27,FINSEQ_1:46;
        hence thesis by A2,A3,A21,A26,A33,A29;
      end;
      suppose
        F is universal;
        then consider
        x being bound_QC-variable, p being Element of QC-WFF such
        that
A34:    F = All(x, p) by Def20;
A35:    @F = <*[3, 0]*>^(<*x*>^@p) by A34,FINSEQ_1:45;
        then len @F = len (<*x*>^@p) + len <*[3, 0]*> by FINSEQ_1:35
          .= len (<*x*>^@p) + 1 by FINSEQ_1:57
          .= len <*x*> + len @p + 1 by FINSEQ_1:35
          .= 1 + len @p + 1 by FINSEQ_1:57;
        then len @p + 1 <= len @F by NAT_1:13;
        then
A36:    len @p < len @F by NAT_1:13;
        @F.1 = [3, 0] by A35,FINSEQ_1:58;
        then (@G.1)`1 = 3 by A5,MCART_1:7;
        then G is universal by Th36;
        then consider
        x9 being bound_QC-variable, p9 being Element of QC-WFF such
        that
A37:    G = All(x9, p9) by Def20;
        <*[3, 0]*>^<*x*>^@p = <*[3, 0]*>^(<*x9*>^@p9)^s by A4,A34,A37,
        FINSEQ_1:45
          .= <*[3, 0]*>^(<*x9*>^@p9^s) by FINSEQ_1:45;
        then
A38:    <*x*>^@p = <*x9*>^@p9^s by A34,A35,FINSEQ_1:46
          .= <*x9*>^(@p9^s) by FINSEQ_1:45;
        then
A39:    x = (<*x9*>^(@p9^s)).1 by FINSEQ_1:58
          .= x9 by FINSEQ_1:58;
        then @p = @p9^s by A38,FINSEQ_1:46;
        hence thesis by A2,A3,A34,A37,A39,A36;
      end;
    end;
    hence thesis;
  end;
A40: for n being Nat holds P[n] from NAT_1:sch 4(A1);
  len @F = len @F;
  hence thesis by A40;
end;

definition
  let F be Element of QC-WFF such that
A1: F is atomic;
  func the_pred_symbol_of F -> QC-pred_symbol means
  :Def21:
  ex k being Element
of NAT, ll being (QC-variable_list of k), P being QC-pred_symbol of k st it = P
  & F = P!ll;
  existence
  proof
    ex k being Element of NAT, P being (QC-pred_symbol of k), ll being
    QC-variable_list of k st F = P!ll by A1,Def17;
    hence thesis;
  end;
  uniqueness
  proof
    let P1, P2 be QC-pred_symbol;
    given k1 being Element of NAT, ll1 being (QC-variable_list of k1), P19
    being QC-pred_symbol of k1 such that
A2: P1 = P19 & F = P19!ll1;
    given k2 being Element of NAT, ll2 being (QC-variable_list of k2), P29
    being QC-pred_symbol of k2 such that
A3: P2 = P29 & F = P29!ll2;
A4: <*P1*>^ll1 = F by A2,Th23
      .= <*P2*>^ll2 by A3,Th23;
    thus P1 = (<*P1*>^ll1).1 by FINSEQ_1:58
      .= P2 by A4,FINSEQ_1:58;
  end;
end;

definition
  let F be Element of QC-WFF such that
A1: F is atomic;
  func the_arguments_of F -> FinSequence of QC-variables means
  :Def22:
  ex k
being Element of NAT, P being (QC-pred_symbol of k), ll being QC-variable_list
  of k st it = ll & F = P!ll;
  existence
  proof
    consider k being Element of NAT, P being (QC-pred_symbol of k), ll being
    QC-variable_list of k such that
A2: F = P!ll by A1,Def17;
    reconsider ll as FinSequence of QC-variables;
    take ll;
    thus thesis by A2;
  end;
  uniqueness
  proof
    let ll1, ll2 be FinSequence of QC-variables;
    given k1 being Element of NAT, P1 being (QC-pred_symbol of k1), ll19 being
    QC-variable_list of k1 such that
A3: ll1 = ll19 and
A4: F = P1!ll19;
A5: F = <*P1*>^ll19 by A4,Th23;
    given k2 being Element of NAT, P2 being (QC-pred_symbol of k2), ll29 being
    QC-variable_list of k2 such that
A6: ll2 = ll29 and
A7: F = P2!ll29;
A8: F = <*P2*>^ll29 by A7,Th23;
    P1 = the_pred_symbol_of F by A1,A4,Def21
      .= P2 by A1,A7,Def21;
    hence thesis by A3,A6,A5,A8,FINSEQ_1:46;
  end;
end;

definition
  let F be Element of QC-WFF such that
A1: F is negative;
  func the_argument_of F -> QC-formula means
  :Def23:
  F = 'not' it;
  existence by A1,Def18;
  uniqueness by FINSEQ_1:46;
end;

definition
  let F be Element of QC-WFF such that
A1: F is conjunctive;
  func the_left_argument_of F -> QC-formula means
  :Def24:
  ex q being Element of QC-WFF st F = it '&' q;
  existence by A1,Def19;
  uniqueness
  proof
    let p1, p2 be QC-formula;
    given q1 being Element of QC-WFF such that
A2: F = p1 '&' q1;
    given q2 being Element of QC-WFF such that
A3: F = p2 '&' q2;
    <*[2, 0]*>^(@p1^@q1) = p2 '&' q2 by A2,A3,FINSEQ_1:45
      .= <*[2, 0]*>^(@p2^@q2) by FINSEQ_1:45;
    then
A4: @p1^@q1 = @p2^@q2 by FINSEQ_1:46;
    len @p1 <= len @p2 or len @p2 <= len @p1;
    then consider a, b, c, d being FinSequence such that
A5: a = @p1 & b = @p2 or a = @p2 & b = @p1 and
A6: len a <= len b & a^c = b^d by A4;
    ex t being FinSequence st a^t = b by A6,FINSEQ_1:64;
    hence thesis by A5,Th37;
  end;
end;

definition
  let F be Element of QC-WFF such that
A1: F is conjunctive;
  func the_right_argument_of F -> QC-formula means
  :Def25:
  ex p being Element of QC-WFF st F = p '&' it;
  existence
  proof
    ex p, q being Element of QC-WFF st F = p '&' q by A1,Def19;
    hence thesis;
  end;
  uniqueness
  proof
    let q1, q2 be QC-formula;
    given p1 being Element of QC-WFF such that
A2: F = p1 '&' q1;
    given p2 being Element of QC-WFF such that
A3: F = p2 '&' q2;
    <*[2, 0]*>^(@p1^@q1) = p2 '&' q2 by A2,A3,FINSEQ_1:45
      .= <*[2, 0]*>^(@p2^@q2) by FINSEQ_1:45;
    then
A4: @p1^@q1 = @p2^@q2 by FINSEQ_1:46;
    p1 = the_left_argument_of F by A1,A2,Def24
      .= p2 by A1,A3,Def24;
    hence thesis by A4,FINSEQ_1:46;
  end;
end;

definition
  let F be Element of QC-WFF such that
A1: F is universal;
  func bound_in F -> bound_QC-variable means
  :Def26:
  ex p being Element of QC-WFF st F = All(it, p);
  existence by A1,Def20;
  uniqueness
  proof
    let x1, x2 be bound_QC-variable;
    given p1 being Element of QC-WFF such that
A2: F = All(x1, p1);
    given p2 being Element of QC-WFF such that
A3: F = All(x2, p2);
    <*[3, 0]*>^(<*x1*>^@p1) = All(x2, p2) by A2,A3,FINSEQ_1:45
      .= <*[3, 0]*>^(<*x2*>^@p2) by FINSEQ_1:45;
    then
A4: <*x1*>^@p1 = <*x2*>^@p2 by FINSEQ_1:46;
    thus x1 = (<*x1*>^@p1).1 by FINSEQ_1:58
      .= x2 by A4,FINSEQ_1:58;
  end;
  func the_scope_of F -> QC-formula means
  :Def27:
  ex x being bound_QC-variable st F = All(x, it);
  existence
  proof
    ex x being bound_QC-variable, p being Element of QC-WFF st F = All(x,
    p) by A1,Def20;
    hence thesis;
  end;
  uniqueness
  proof
    let p1, p2 be QC-formula;
    given x1 being bound_QC-variable such that
A5: F = All(x1, p1);
    given x2 being bound_QC-variable such that
A6: F = All(x2, p2);
    <*[3, 0]*>^(<*x1*>^@p1) = All(x2, p2) by A5,A6,FINSEQ_1:45
      .= <*[3, 0]*>^(<*x2*>^@p2) by FINSEQ_1:45;
    then
A7: <*x1*>^@p1 = <*x2*>^@p2 by FINSEQ_1:46;
    x1 = (<*x1*>^@p1).1 by FINSEQ_1:58
      .= x2 by A7,FINSEQ_1:58;
    hence thesis by A7,FINSEQ_1:46;
  end;
end;

reserve p for Element of QC-WFF;

canceled 7;

theorem Th45:
  p is negative implies len @the_argument_of p < len @p
proof
  assume
A1: p is negative;
  then consider q being Element of QC-WFF such that
A2: p = 'not' q by Def18;
  len @p = len <*[1, 0]*> + len @q by A2,FINSEQ_1:35
    .= len @q + 1 by FINSEQ_1:57;
  then len @q < len @p by NAT_1:13;
  hence thesis by A1,A2,Def23;
end;

theorem Th46:
  p is conjunctive implies len @the_left_argument_of p < len @p &
  len @the_right_argument_of p < len @p
proof
  assume
A1: p is conjunctive;
  then consider l, q being Element of QC-WFF such that
A2: p = l '&' q by Def19;
  p = <*[2, 0]*>^(@l^@q) by A2,FINSEQ_1:45;
  then
A3: len @p = len <*[2, 0]*> + len (@l^@q) by FINSEQ_1:35
    .= len (@l^@q) + 1 by FINSEQ_1:57;
A4: len @q + len @l = len (@l^@q) by FINSEQ_1:35;
  then len @q <= len (@l^@q) by NAT_1:11;
  then
A5: len @q < len @p by A3,NAT_1:13;
  len @l <= len (@l^@q) by A4,NAT_1:11;
  then len @l < len @p by A3,NAT_1:13;
  hence thesis by A1,A2,A5,Def24,Def25;
end;

theorem Th47:
  p is universal implies len @the_scope_of p < len @p
proof
  assume
A1: p is universal;
  then consider x being bound_QC-variable, q being Element of QC-WFF such that
A2: p = All(x, q) by Def20;
  len @q + len <*x*> = len (<*x*>^@q) by FINSEQ_1:35;
  then
A3: len @q <= len (<*x*>^@q) by NAT_1:11;
  p = <*[3, 0]*>^(<*x*>^@q) by A2,FINSEQ_1:45;
  then len @p = len <*[3, 0]*> + len (<*x*>^@q) by FINSEQ_1:35
    .= len (<*x*>^@q) + 1 by FINSEQ_1:57;
  then len @q < len @p by A3,NAT_1:13;
  hence thesis by A1,A2,Def27;
end;

scheme
  QCInd2 { P[Element of QC-WFF] }: for p being Element of QC-WFF holds P[p]
provided
A1: for p being Element of QC-WFF holds (p is atomic implies P[p]) & P[
VERUM] & (p is negative & P[the_argument_of p] implies P[p]) & (p is
conjunctive & P[the_left_argument_of p] & P[the_right_argument_of p] implies P[
p]) & (p is universal & P[the_scope_of p] implies P[p])
proof
A2: now
    let x be bound_QC-variable, p be Element of QC-WFF such that
A3: P[p];
A4: All(x, p) is universal by Def20;
    then p = the_scope_of All(x, p) by Def27;
    hence P[All(x, p)] by A1,A3,A4;
  end;
A5: now
    let p be Element of QC-WFF such that
A6: P[p];
A7: 'not' p is negative by Def18;
    then p= the_argument_of 'not' p by Def23;
    hence P['not' p] by A1,A6,A7;
  end;
A8: now
    let p, q be Element of QC-WFF such that
A9: ( P[p])& P[q];
A10: p '&' q is conjunctive by Def19;
    then
    p = the_left_argument_of (p '&' q) & q = the_right_argument_of (p '&'
    q) by Def24,Def25;
    hence P[p '&' q] by A1,A9,A10;
  end;
A11: now
    let k be Element of NAT, P be (QC-pred_symbol of k), ll be
    QC-variable_list of k;
    P!ll is atomic by Def17;
    hence P[P!ll] by A1;
  end;
A12: P[VERUM] by A1;
  thus for p be Element of QC-WFF holds P[p] from QCInd (A11, A12, A5, A8, A2
  );
end;

reserve F for Element of QC-WFF;

theorem Th48:
  for k being Element of NAT, P being QC-pred_symbol of k holds P
  `1 <> 0 & P`1 <> 1 & P`1 <> 2 & P`1 <> 3
proof
  let k be Element of NAT, P be QC-pred_symbol of k;
  reconsider P9 = P as QC-pred_symbol;
  P9`1 = 7+the_arity_of P9 by Def6;
  hence thesis by NAT_1:11;
end;

theorem Th49:
  (@VERUM.1)`1 = 0 & (F is atomic implies ex k being Element of
NAT st @F.1 is QC-pred_symbol of k) & (F is negative implies (@F.1)`1 = 1) & (F
  is conjunctive implies (@F.1)`1 = 2) & (F is universal implies (@F.1)`1 = 3)
proof
  thus (@VERUM.1)`1 = [0,0]`1 by FINSEQ_1:def 8
    .= 0 by MCART_1:7;
  thus F is atomic implies ex k being Element of NAT st @F.1 is QC-pred_symbol
  of k
  proof
    assume F is atomic;
    then consider
    k being Element of NAT, P being (QC-pred_symbol of k), ll being
    QC-variable_list of k such that
A1: F = P!ll by Def17;
    @F = <*P*>^ll by A1,Th23;
    then @F.1 = P by FINSEQ_1:58;
    hence thesis;
  end;
  thus F is negative implies (@F.1)`1 = 1
  proof
    assume F is negative;
    then ex p being Element of QC-WFF st F = 'not' p by Def18;
    then @F.1 = [1, 0] by FINSEQ_1:58;
    hence thesis by MCART_1:7;
  end;
  thus F is conjunctive implies (@F.1)`1 = 2
  proof
    assume F is conjunctive;
    then consider p, q being Element of QC-WFF such that
A2: F = p '&' q by Def19;
    @F = <*[2, 0]*>^(@p^@q) by A2,FINSEQ_1:45;
    then @F.1 = [2, 0] by FINSEQ_1:58;
    hence thesis by MCART_1:7;
  end;
  thus F is universal implies (@F.1)`1 = 3
  proof
    assume F is universal;
    then consider
    x being bound_QC-variable, p being Element of QC-WFF such that
A3: F = All(x, p) by Def20;
    @F = <*[3, 0]*>^(<*x*>^@p) by A3,FINSEQ_1:45;
    then @F.1 = [3, 0] by FINSEQ_1:58;
    hence thesis by MCART_1:7;
  end;
end;

theorem Th50:
  F is atomic implies (@F.1)`1 <> 0 & (@F.1)`1 <> 1 & (@F.1)`1 <>
  2 & (@F.1)`1 <> 3
proof
  assume F is atomic;
  then ex k being Element of NAT st @F.1 is QC-pred_symbol of k by Th49;
  hence thesis by Th48;
end;

reserve p for Element of QC-WFF;

theorem Th51:
  not (VERUM is atomic or VERUM is negative or VERUM is
  conjunctive or VERUM is universal) & not (ex p st p is atomic & p is negative
  or p is atomic & p is conjunctive or p is atomic & p is universal or p is
  negative & p is conjunctive or p is negative & p is universal or p is
  conjunctive & p is universal)
proof
  thus not (VERUM is atomic or VERUM is negative or VERUM is conjunctive or
  VERUM is universal) by Th49,Th50;
  let p be Element of QC-WFF;
A1: p is conjunctive implies (@p.1)`1 = 2 by Th49;
A2: p is universal implies (@p.1)`1 = 3 by Th49;
  p is negative implies (@p.1)`1 = 1 by Th49;
  hence thesis by A1,A2,Th50;
end;

scheme
  QCFuncEx { D() -> non empty set, V() -> (Element of D()), A(Element of
QC-WFF) -> (Element of D()), N(Element of D()) -> (Element of D()), C((Element
of D()), Element of D()) -> (Element of D()), Q((Element of QC-WFF), Element of
D()) -> Element of D()} : ex F being Function of QC-WFF, D() st F.VERUM = V() &
  for p being Element of QC-WFF holds (p is atomic implies F.p = A(p)) & (p is
negative implies F.p = N(F.the_argument_of p)) & (p is conjunctive implies F.p
  = C(F.the_left_argument_of p, F.the_right_argument_of p)) & (p is universal
  implies F.p = Q(p, F.the_scope_of p)) proof
  defpred Pfn[Function of QC-WFF, D(), Element of NAT] means $1.VERUM = V() &
for p being Element of QC-WFF st len @p <= $2 holds (p is atomic implies $1.p =
  A(p)) & (p is negative implies $1.p = N($1.the_argument_of p)) & (p is
  conjunctive implies $1.p = C($1.the_left_argument_of p, $1.
the_right_argument_of p)) & (p is universal implies $1.p = Q(p, $1.the_scope_of
  p));
  defpred Pfgp[(Element of D()), (Function of QC-WFF, D()), Element of QC-WFF]
  means ($3 = VERUM implies $1 = V()) & ($3 is atomic implies $1 = A($3)) & ($3
is negative implies $1 = N($2.the_argument_of $3)) & ($3 is conjunctive implies
  $1 = C($2.the_left_argument_of $3, $2.the_right_argument_of $3)) & ($3 is
  universal implies $1 = Q($3, $2.the_scope_of $3));
  defpred S[Element of NAT] means ex F being Function of QC-WFF, D() st Pfn[F,
  $1];
  defpred Qfn[set, set] means ex p being Element of QC-WFF st p = $1 & for g
being Function of QC-WFF, D() st Pfn[g, len @p qua Element of NAT] holds $2 = g
  .p;
A1: for n be Element of NAT st S[n] holds S[n+1]
  proof
    let n be Element of NAT;
    given F being Function of QC-WFF, D() such that
A2: Pfn[F, n];
    defpred R[Element of QC-WFF,Element of D()] means (len @$1 <> n+1 implies
    $2 = F.$1) & (len @$1 = n+1 implies Pfgp[$2,F,$1]);
A3: for p be Element of QC-WFF ex y be Element of D() st R[p,y]
    proof
      let p be Element of QC-WFF;
      now
        per cases by Th33;
        case
          len @p <> n+1;
          take y = F.p;
          thus y =F.p;
        end;
        case
A4:       len @p = n+1 & p = VERUM;
          take y = V();
          thus Pfgp[y, F, p] by A4,Th51;
        end;
        case
A5:       len @p = n+1 & p is atomic;
          take y = A(p);
          thus Pfgp[y, F, p] by A5,Th51;
        end;
        case
A6:       len @p = n+1 & p is negative;
          take y = N(F.the_argument_of p);
          thus Pfgp[y, F, p] by A6,Th51;
        end;
        case
A7:       len @p = n+1 & p is conjunctive;
          take y = C(F.the_left_argument_of p, F.the_right_argument_of p);
          thus Pfgp[y, F, p] by A7,Th51;
        end;
        case
A8:       len @p = n+1 & p is universal;
          take y = Q(p, F.the_scope_of p);
          thus Pfgp[y, F, p] by A8,Th51;
        end;
      end;
      hence thesis;
    end;
    consider G being Function of QC-WFF, D() such that
A9: for p being Element of QC-WFF holds R[p,G.p] from FUNCT_2:sch 3 (
    A3);
    take H = G;
    thus Pfn[H, n+1]
    proof
      thus H.VERUM = V()
      proof
        per cases;
        suppose
          len @VERUM <> n+1;
          hence thesis by A2,A9;
        end;
        suppose
          len @VERUM = n+1;
          hence thesis by A9;
        end;
      end;
      let p be Element of QC-WFF such that
A10:  len @p <= n+1;
      thus p is atomic implies H.p = A(p)
      proof
        now
          per cases;
          suppose
            len @p <> n+1;
            then len @p <= n & H.p = F.p by A9,A10,NAT_1:8;
            hence thesis by A2;
          end;
          suppose
            len @p = n+1;
            hence thesis by A9;
          end;
        end;
        hence thesis;
      end;
      thus p is negative implies H.p = N(H.the_argument_of p)
      proof
        assume
A11:    p is negative;
        then len @the_argument_of p <> n+1 by A10,Th45;
        then
A12:    H.the_argument_of p = F.the_argument_of p by A9;
        now
          per cases;
          suppose
            len @p <> n+1;
            then len @p <= n & H.p = F.p by A9,A10,NAT_1:8;
            hence thesis by A2,A11,A12;
          end;
          suppose
            len @p = n+1;
            hence thesis by A9,A11,A12;
          end;
        end;
        hence thesis;
      end;
      thus p is conjunctive implies H.p = C(H.the_left_argument_of p, H.
      the_right_argument_of p)
      proof
        assume
A13:    p is conjunctive;
        then len @the_right_argument_of p <> n+1 by A10,Th46;
        then
A14:    H.the_right_argument_of p = F.the_right_argument_of p by A9;
        len @the_left_argument_of p <> n+1 by A10,A13,Th46;
        then
A15:    H.the_left_argument_of p = F.the_left_argument_of p by A9;
        now
          per cases;
          suppose
            len @p <> n+1;
            then len @p <= n & H.p = F.p by A9,A10,NAT_1:8;
            hence thesis by A2,A13,A15,A14;
          end;
          suppose
            len @p = n+1;
            hence thesis by A9,A13,A15,A14;
          end;
        end;
        hence thesis;
      end;
      thus p is universal implies H.p = Q(p, H.the_scope_of p)
      proof
        assume
A16:    p is universal;
        then len @the_scope_of p <> n+1 by A10,Th47;
        then
A17:    H.the_scope_of p = F.the_scope_of p by A9;
        now
          per cases;
          suppose
            len @p <> n+1;
            then len @p <= n & H.p = F.p by A9,A10,NAT_1:8;
            hence thesis by A2,A16,A17;
          end;
          suppose
            len @p = n+1;
            hence thesis by A9,A16,A17;
          end;
        end;
        hence thesis;
      end;
    end;
  end;
A18: S[0]
  proof
    reconsider F = QC-WFF --> V() as Function of QC-WFF, D();
    take F;
    thus F.VERUM = V() by FUNCOP_1:13;
    let p be Element of QC-WFF such that
A19: len @p <= 0;
    1 <= len @p by Th34;
    hence thesis by A19,XXREAL_0:2;
  end;
A20: for n being Element of NAT holds S[n] from NAT_1:sch 1 (A18, A1);
  then
A21: ex G being Function of QC-WFF, D() st Pfn[G, len @VERUM qua Element of
  NAT];
A22: for x being set st x in QC-WFF ex y being set st Qfn[x, y]
  proof
    let x be set;
    assume x in QC-WFF;
    then reconsider x9 = x as Element of QC-WFF;
    consider F being Function of QC-WFF, D() such that
A23: Pfn[F, len @x9 qua Element of NAT] by A20;
    take F.x, x9;
    thus x = x9;
    let G be Function of QC-WFF, D() such that
A24: Pfn[G, len @x9 qua Element of NAT];
    defpred Prop[Element of QC-WFF] means len @$1 <= len@x9 implies F.$1 = G.
    $1;
A25: now
      let p be Element of QC-WFF;
      thus p is atomic implies Prop[p]
      proof
        assume
A26:    p is atomic & len @p <= len@x9;
        hence F.p = A(p) by A23
          .= G.p by A24,A26;
      end;
      thus Prop[VERUM] by A23,A24;
      thus p is negative & Prop[the_argument_of p] implies Prop[p]
      proof
        assume that
A27:    p is negative and
A28:    Prop[the_argument_of p] and
A29:    len @p <= len @x9;
        len @the_argument_of p < len @p by A27,Th45;
        hence F.p = N(G.the_argument_of p) by A23,A27,A28,A29,XXREAL_0:2
          .= G.p by A24,A27,A29;
      end;
      thus p is conjunctive & Prop[the_left_argument_of p] & Prop[
      the_right_argument_of p] implies Prop[p]
      proof
        assume that
A30:    p is conjunctive and
A31:    ( Prop[the_left_argument_of p])& Prop[the_right_argument_of p ] and
A32:    len @p <= len @x9;
        len @the_left_argument_of p < len @p & len @the_right_argument_of
        p < len @p by A30,Th46;
        hence F.p = C(G.the_left_argument_of p, G.the_right_argument_of p) by
        A23,A30,A31,A32,XXREAL_0:2
          .= G.p by A24,A30,A32;
      end;
      thus p is universal & Prop[the_scope_of p] implies Prop[p]
      proof
        assume that
A33:    p is universal and
A34:    Prop[the_scope_of p] and
A35:    len @p <= len @x9;
        len @the_scope_of p < len @p by A33,Th47;
        hence F.p = Q(p, G.the_scope_of p) by A23,A33,A34,A35,XXREAL_0:2
          .= G.p by A24,A33,A35;
      end;
    end;
    for p being Element of QC-WFF holds Prop[p] from QCInd2 (A25);
    hence thesis;
  end;
  consider F being Function such that
A36: dom F = QC-WFF and
A37: for x being set st x in QC-WFF holds Qfn[x, F.x] from CLASSES1:sch
  1 (A22);
  rng F c= D()
  proof
    let y be set;
    assume y in rng F;
    then consider x being set such that
A38: x in QC-WFF & y = F.x by A36,FUNCT_1:def 5;
    consider p being Element of QC-WFF such that
    p = x and
A39: for g being Function of QC-WFF, D() st Pfn[g, len @p qua Element
    of NAT] holds y = g.p by A37,A38;
    consider G being Function of QC-WFF, D() such that
A40: Pfn[G, len @p qua Element of NAT] by A20;
    y = G.p by A39,A40;
    hence thesis;
  end;
  then reconsider F as Function of QC-WFF, D() by A36,
  FUNCT_2:def 1,RELSET_1:11;
  take F;
  Qfn[VERUM, F.VERUM] by A37;
  hence F.VERUM = V() by A21;
  let p be Element of QC-WFF;
  consider p1 being Element of QC-WFF such that
A41: p1 = p and
A42: for g being Function of QC-WFF, D() st Pfn[g, len @p1 qua Element
  of NAT] holds F.p = g.p1 by A37;
  consider G being Function of QC-WFF, D() such that
A43: Pfn[G, len @p1 qua Element of NAT] by A20;
  set p9 = the_scope_of p;
A44: ex p1 being Element of QC-WFF st p1 = p9 & for g being Function of
  QC-WFF, D() st Pfn[g, len @p1 qua Element of NAT] holds F.p9 = g.p1 by A37;
A45: F.p = G.p by A41,A42,A43;
  hence p is atomic implies F.p = A(p) by A41,A43;
A46: for k being Element of NAT st k < len @p holds Pfn[G, k]
  proof
    let k be Element of NAT;
    assume
A47: k < len @p;
    thus G.VERUM = V() by A43;
    let p9 be Element of QC-WFF;
    assume len @p9 <= k;
    then len @p9 <= len@p by A47,XXREAL_0:2;
    hence thesis by A41,A43;
  end;
  thus p is negative implies F.p = N(F.the_argument_of p)
  proof
    set p9 = the_argument_of p;
    set k = len @p9;
A48: ex p1 being Element of QC-WFF st p1 = p9 & for g being Function of
    QC-WFF, D() st Pfn[g, len @p1 qua Element of NAT] holds F.p9 = g.p1 by A37;
    assume
A49: p is negative;
    then k < len @p by Th45;
    then Pfn[G, k qua Element of NAT] by A46;
    then F.p9 = G.p9 by A48;
    hence thesis by A41,A43,A45,A49;
  end;
  thus p is conjunctive implies F.p = C(F.the_left_argument_of p, F.
  the_right_argument_of p)
  proof
    set p99 = the_right_argument_of p;
    set p9 = the_left_argument_of p;
    set k9 = len @p9;
    set k99 = len @p99;
A50: ex p2 being Element of QC-WFF st p2 = p99 & for g being Function of
QC-WFF, D() st Pfn[g, len @p2 qua Element of NAT] holds F.p99 = g.p2 by A37;
    assume
A51: p is conjunctive;
    then k9 < len @p by Th46;
    then
A52: Pfn[G, k9 qua Element of NAT] by A46;
    k99 < len @p by A51,Th46;
    then Pfn[G, k99 qua Element of NAT] by A46;
    then
A53: F.p99 = G.p99 by A50;
    ex p1 being Element of QC-WFF st p1 = p9 & for g being Function of
    QC-WFF, D() st Pfn[g, len @p1 qua Element of NAT] holds F.p9 = g.p1 by A37;
    then F.p9 = G.p9 by A52;
    hence thesis by A41,A43,A45,A51,A53;
  end;
  set k = len @p9;
  assume
A54: p is universal;
  then k < len @p by Th47;
  then Pfn[G, k qua Element of NAT] by A46;
  then F.p9 = G.p9 by A44;
  hence thesis by A41,A43,A45,A54;
end;

reserve j,k for Element of NAT;

definition
  let ll be FinSequence of QC-variables;
  func still_not-bound_in ll -> Subset of bound_QC-variables equals
  { ll.k : 1
  <= k & k <= len ll & ll.k in bound_QC-variables };
  coherence
  proof
    set IT = { ll.k : 1 <= k & k <= len ll & ll.k in bound_QC-variables };
    now
      let x be set;
      assume x in IT;
      then
      ex k being Element of NAT st x = ll.k & 1 <= k & k <= len ll & ll.k
      in bound_QC-variables;
      hence x in bound_QC-variables;
    end;
    hence thesis by TARSKI:def 3;
  end;
end;

reserve k for Element of NAT;

definition
  let p be QC-formula;
  func still_not-bound_in p -> Subset of bound_QC-variables means
  ex F being
  Function of QC-WFF, bool bound_QC-variables st it = F.p & for p being Element
of QC-WFF holds F.VERUM = {} & (p is atomic implies F.p = { (the_arguments_of p
  ).k : 1 <= k & k <= len the_arguments_of p & (the_arguments_of p).k in
bound_QC-variables }) & (p is negative implies F.p = F.the_argument_of p) & (p
  is conjunctive implies F.p = (F.the_left_argument_of p) \/ (F.
the_right_argument_of p)) & (p is universal implies F.p = (F.the_scope_of p) \
  {bound_in p});
  existence
  proof
    deffunc Q(Element of QC-WFF, Subset of bound_QC-variables) = $2 \ {
    bound_in $1};
    deffunc C(Subset of bound_QC-variables, Subset of bound_QC-variables) = $1
    \/ $2;
    deffunc N(Subset of bound_QC-variables) = $1;
    deffunc A(Element of QC-WFF) = still_not-bound_in (the_arguments_of $1);
    reconsider Emp = {} as Subset of bound_QC-variables by XBOOLE_1:2;
    consider F being Function of QC-WFF, bool bound_QC-variables such that
A1: F.VERUM = Emp & for p being Element of QC-WFF holds (p is atomic
implies F.p = A(p)) & (p is negative implies F.p = N(F.the_argument_of p)) & (p
is conjunctive implies F.p = C(F.the_left_argument_of p,F.the_right_argument_of
    p)) & (p is universal implies F.p = Q(p,F.the_scope_of p)) from QCFuncEx;
    take F.p, F;
    thus F.p = F.p;
    let p be Element of QC-WFF;
    thus F.VERUM = {} by A1;
    thus p is atomic implies F.p = { (the_arguments_of p).k : 1 <= k & k <=
    len the_arguments_of p & (the_arguments_of p).k in bound_QC-variables }
    proof
      assume p is atomic;
      then F.p = still_not-bound_in (the_arguments_of p) by A1;
      hence thesis;
    end;
    thus p is negative implies F.p = F.the_argument_of p by A1;
    thus p is conjunctive implies F.p = (F.the_left_argument_of p) \/ (F.
    the_right_argument_of p) by A1;
    assume p is universal;
    hence thesis by A1;
  end;
  uniqueness
  proof
    let IT,IT9 be Subset of bound_QC-variables;
    given F1 being Function of QC-WFF, bool bound_QC-variables such that
A2: IT = F1.p and
A3: for p being Element of QC-WFF holds F1.VERUM = {} & (p is atomic
implies F1.p = { (the_arguments_of p).k : 1 <= k & k <= len the_arguments_of p
& (the_arguments_of p).k in bound_QC-variables }) & (p is negative implies F1.p
    = F1.the_argument_of p) & (p is conjunctive implies F1.p = (F1.
    the_left_argument_of p) \/ (F1.the_right_argument_of p)) & (p is universal
    implies F1.p = (F1.the_scope_of p) \ {bound_in p});
    given F2 being Function of QC-WFF, bool bound_QC-variables such that
A4: IT9 = F2.p and
A5: for p being Element of QC-WFF holds F2.VERUM = {} & (p is atomic
implies F2.p = { (the_arguments_of p).k : 1 <= k & k <= len the_arguments_of p
& (the_arguments_of p).k in bound_QC-variables }) & (p is negative implies F2.p
    = F2.the_argument_of p) & (p is conjunctive implies F2.p = (F2.
    the_left_argument_of p) \/ (F2.the_right_argument_of p)) & (p is universal
    implies F2.p = (F2.the_scope_of p) \ {bound_in p});
    defpred Prop[Element of QC-WFF] means F1.$1 = F2.$1;
A6: for k being Element of NAT, P being (QC-pred_symbol of k), ll being
    QC-variable_list of k holds Prop[P!ll]
    proof
      let k be Element of NAT, P be (QC-pred_symbol of k), ll be
      QC-variable_list of k;
A7:   P!ll is atomic by Def17;
      then
A8:   the_arguments_of P!ll = ll by Def22;
      hence F1.(P!ll) = { ll.j : 1 <= j & j <= len ll & ll.j in
      bound_QC-variables } by A3,A7
        .= F2.(P!ll) by A5,A7,A8;
    end;
A9: for p being Element of QC-WFF st Prop[p] holds Prop['not' p]
    proof
      let p be Element of QC-WFF such that
A10:  F1.p = F2.p;
A11:  'not' p is negative by Def18;
      then
A12:  the_argument_of 'not' p = p by Def23;
      hence F1.'not' p = F2.p by A3,A10,A11
        .= F2.'not' p by A5,A11,A12;
    end;
A13: for x being bound_QC-variable, p being Element of QC-WFF holds Prop[p
    ] implies Prop[All(x, p)]
    proof
      let x be bound_QC-variable, p be Element of QC-WFF such that
A14:  F1.p = F2.p;
A15:  All(x,p) is universal by Def20;
      then
A16:  the_scope_of All(x, p) = p & bound_in All(x, p) = x by Def26,
      Def27;
      hence F1.All(x, p) = (F2.p) \ { x } by A3,A14,A15
        .= F2.All(x, p) by A5,A15,A16;
    end;
A17: for p, q being Element of QC-WFF st Prop[p] & Prop[q] holds Prop[p '&' q]
    proof
      let p, q be Element of QC-WFF such that
A18:  F1.p = F2.p & F1.q = F2.q;
A19:  p '&' q is conjunctive by Def19;
      then
A20:  the_left_argument_of (p '&' q) = p & the_right_argument_of (p '&' q
      ) = q by Def24,Def25;
      hence F1.(p '&' q) = (F2.p) \/ (F2.q) by A3,A18,A19
        .= F2.(p '&' q) by A5,A19,A20;
    end;
A21: Prop[VERUM] by A3,A5;
    for p be Element of QC-WFF holds Prop[p] from QCInd(A6,A21,A9, A17,
    A13);
    hence thesis by A2,A4;
  end;
end;

definition
  let p be QC-formula;
  attr p is closed means
  still_not-bound_in p = {};
end;
