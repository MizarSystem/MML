:: Properties of Trigonometric Function
::  by Takashi Mitsuishi and Yuguang Yang
::
:: Received March 13, 1999
:: Copyright (c) 1999 Association of Mizar Users

environ

 vocabularies ARYTM, RCOMP_1, ARYTM_3, SIN_COS, ARYTM_1, SQUARE_1, RFUNCT_2,
      FDIFF_1, FUNCT_1, PARTFUN1, RELAT_1, ORDINAL2, VECTSP_1, SEQ_1, SEQ_2,
      SEQM_3, BOOLE, PRE_TOPC, FCONT_1, SIN_COS2, FINSEQ_4, GROUP_1, FUNCOP_1,
      NAT_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0,
      REAL_1, RELSET_1, FCONT_1, FDIFF_1, NAT_1, FINSEQ_4, FUNCOP_1, SQUARE_1,
      NEWTON, PARTFUN1, SEQ_2, SEQM_3, RCOMP_1, RFUNCT_1, RFUNCT_2, SIN_COS,
      SEQ_1, BINOP_2, XXREAL_0;
 constructors PARTFUN1, FUNCOP_1, REAL_1, SQUARE_1, NAT_1, BINOP_2, SEQ_2,
      RCOMP_1, FINSEQ_4, RFUNCT_1, RFUNCT_2, FCONT_1, FDIFF_1, SIN_COS;
 registrations XBOOLE_0, RELSET_1, NUMBERS, XXREAL_0, XREAL_0, SQUARE_1, NAT_1,
      MEMBERED, SEQ_1, RCOMP_1, FDIFF_1, SIN_COS;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions SQUARE_1, SUBSET_1;
 theorems REAL_1, REAL_2, SEQ_1, SEQM_3, SEQ_4, SQUARE_1, FINSEQ_4, RCOMP_1,
      FUNCT_1, FDIFF_1, FDIFF_2, FCONT_3, TARSKI, FUNCT_2, RFUNCT_1, RFUNCT_2,
      PARTFUN1, ROLLE, SIN_COS, NEWTON, XREAL_0, RELAT_1, XBOOLE_0, XCMPLX_0,
      XCMPLX_1, BINOP_2, FUNCOP_1, XREAL_1;
 schemes NAT_1, PARTFUN2;

begin :: Monotone increasing and monotone decreasing of sine and cosine.

 reserve p,q,r,th for real number;
 reserve n for Element of NAT;

Lm1:
0 < PI/2 & PI/2 < PI & PI < 3/2*PI & 3/2*PI < 2*PI
proof
 thus A1: 0 < PI/2
 proof
  PI in ].0, 4.[ by SIN_COS:def 32;
 then 0< PI & PI < 4 by RCOMP_1:47;
 then 0/2 <PI/2 by REAL_1:73;
 hence 0< PI/2;
 end;
 thus A2: PI/2 < PI
 proof
     0 + PI/2 < PI/2 + PI/2 by A1,XREAL_1:8;
 hence PI/2 < PI;
 end;
 thus A3: PI < 3/2*PI
 proof
   PI/2 + PI/2 < PI + PI/2 by A2,XREAL_1:8;
 hence thesis;
 end;
A4: PI + PI/2 = 3/2*PI;
   PI/2 + 3/2*PI = 2*PI;
  hence 3/2*PI < 2*PI by A3,A4,XREAL_1:8;
end;

theorem Th1:
  p>=0 & r>=0 implies p+r>=2*sqrt(p*r)
 proof
 assume A1:p>=0 & r>=0;
 A2:(sqrt p - sqrt r)^2>=0 by XREAL_1:65;
       (sqrt p - sqrt r)^2
  =(sqrt p)^2 - 2*sqrt p*sqrt r + (sqrt r)^2
 .=p-2*sqrt p*sqrt r + (sqrt r)^2 by A1,SQUARE_1:def 4
 .=p -2*sqrt p*sqrt r +r by A1,SQUARE_1:def 4
 .=p+r-2*sqrt p*sqrt r;
 then 0+2*sqrt p*sqrt r <= p+r by A2,XREAL_1:21;
 then 2*(sqrt p*sqrt r) <= p+r;
 hence 2*sqrt(p*r) <= p+r by A1,SQUARE_1:97;
 end;

theorem
   sin is_increasing_on ].0,PI/2.[
proof
A1: sin is_differentiable_on ].0,PI/2.[ by FDIFF_1:34,SIN_COS:73;
    for th be Real st th in ].0,PI/2.[ holds 0 < diff(sin,th)
 proof
 let th be Real; assume
     th in ].0,PI/2.[;
 then cos.th > 0 by SIN_COS:85;
 hence diff(sin,th) > 0 by SIN_COS:73;
 end;
hence thesis by A1,Lm1,ROLLE:9;
end;

Lm2:
for th st th in ].0,PI/2.[ holds 0 < sin.th
proof
let th; assume th in ].0,PI/2.[;
then 0 < th & th < PI/2 by RCOMP_1:47;
then 0-th<0 & -th>-PI/2 by XREAL_1:26,REAL_2:105;
then -th+PI/2 < 0+PI/2 & -th+PI/2 > -PI/2+ PI/2 by XREAL_1:8;
then PI/2-th in ].0,PI/2.[ by RCOMP_1:47;
then cos.(PI/2-th) > 0 by SIN_COS:85;
hence sin.th > 0 by SIN_COS:83;
end;

theorem
   sin is_decreasing_on ].PI/2,PI.[
proof
A1: sin is_differentiable_on ].PI/2,PI.[ by FDIFF_1:34,SIN_COS:73;
    for th1 be Real st th1 in ].PI/2,PI.[ holds 0 > diff(sin,th1)
 proof
 let th1 be Real; assume th1 in ].PI/2,PI.[;
 then PI/2 < th1 & th1 < PI by RCOMP_1:47;
 then PI/2 - PI/2 < th1 - PI/2
 & th1 - PI/2 < PI - PI/2 by XREAL_1:11;
 then A2: th1-PI/2 in ].0,PI/2.[ by RCOMP_1:47;
A3: diff(sin,(th1))=cos.(PI/2+(th1-PI/2)) by SIN_COS:73
               .=-sin.(th1-PI/2) by SIN_COS:83;
     sin.(th1-PI/2) > 0 by A2,Lm2;
 then 0-sin.(th1-PI/2) < 0 by REAL_2:105;
 hence diff(sin,(th1)) < 0 by A3;
 end;
hence thesis by A1,Lm1,ROLLE:10;
end;

theorem
   cos is_decreasing_on ].0,PI/2.[
proof
 A1: cos is_differentiable_on ].0,PI/2.[ by FDIFF_1:34,SIN_COS:72;
     for th be Real st th in ].0,PI/2.[ holds diff(cos,th) < 0
  proof
  let th be Real such that
  A2: th in ].0,PI/2.[;
  A3: diff(cos,th) = -sin.(th) by SIN_COS:72;
    0 < sin.(th) by A2,Lm2;
  then 0-sin.(th) < 0 by REAL_2:105;
  hence diff(cos,th) < 0 by A3;
  end;
hence thesis by A1,Lm1,ROLLE:10;
end;

theorem
   cos is_decreasing_on ].PI/2,PI.[
proof
A1: cos is_differentiable_on ].PI/2,PI.[ by FDIFF_1:34,SIN_COS:72;
     for th be Real st th in ].PI/2,PI.[ holds diff(cos,th) < 0
  proof
  let th be Real; assume th in ].PI/2,PI.[;
   then PI/2 < th & th < PI by RCOMP_1:47;
   then PI/2 - PI/2 < th - PI/2
   & th - PI/2 < PI - PI/2 by XREAL_1:11;
   then A2: th-PI/2 in ].0,PI/2.[ by RCOMP_1:47;
   A3:diff(cos,(th))=-sin.(PI/2+(th-PI/2)) by SIN_COS:72
                 .=-cos.(th-PI/2) by SIN_COS:83;
       cos.(th-PI/2) > 0 by A2,SIN_COS:85;
   then 0-cos.(th-PI/2) < 0 by REAL_2:105;
   hence diff(cos,(th)) < 0 by A3;
  end;
hence thesis by A1,Lm1,ROLLE:10;
end;

theorem
   sin is_decreasing_on ].PI,3/2*PI.[
proof
A1: sin is_differentiable_on ].PI,3/2*PI.[ by FDIFF_1:34,SIN_COS:73;
    for th be Real st th in ].PI,3/2*PI.[ holds diff(sin,(th))<0
 proof
 let th be Real such that
 A2:th in ].PI,3/2*PI.[;
 A3:diff(sin,(th))= cos.(PI+(th-PI)) by SIN_COS:73
             .= -cos.(th-PI) by SIN_COS:83;
     PI < th & th < 3/2*PI by A2,RCOMP_1:47;
 then PI-PI < th-PI & th-PI < 3/2*PI-PI by XREAL_1:11;
 then th-PI in ].0,PI/2.[ by RCOMP_1:47;
 then cos.(th-PI) > 0 by SIN_COS:85;
 then 0-cos.(th-PI) < 0 by REAL_2:105;
 hence diff(sin,(th)) < 0 by A3;
 end;
hence thesis by A1,Lm1,ROLLE:10;
end;

theorem
   sin is_increasing_on ].3/2*PI,2*PI.[
proof
A1: sin is_differentiable_on ].3/2*PI,2*PI.[ by FDIFF_1:34,SIN_COS:73;
    for th be Real st th in ].3/2*PI,2*PI.[ holds diff(sin,(th))>0
 proof
 let th be Real such that
 A2:th in ].3/2*PI,2*PI.[;
 A3:diff(sin,(th)) = cos.(PI + (PI/2+(th-3/2*PI))) by SIN_COS:73
             .= -cos.(PI/2+(th-3/2*PI)) by SIN_COS:83
             .= -(-sin.(th-3/2*PI)) by SIN_COS:83
             .= sin.(th-3/2*PI);
     3/2*PI < th & th < 2*PI by A2,RCOMP_1:47;
 then 3/2*PI-3/2*PI < th-3/2*PI
   & th-3/2*PI < 2*PI-3/2*PI by XREAL_1:11;
 then th-3/2*PI in ].0,PI/2.[ by RCOMP_1:47;
 hence diff(sin,(th)) > 0 by A3,Lm2;
 end;
hence thesis by A1,Lm1,ROLLE:9;
end;

theorem
   cos is_increasing_on ].PI,3/2*PI.[
proof
A1: cos is_differentiable_on ].PI,3/2*PI.[ by FDIFF_1:34,SIN_COS:72;
    for th be Real st th in ].PI,3/2*PI.[ holds diff(cos,(th))>0
 proof
 let th be Real such that
 A2:th in ].PI,3/2*PI.[;
 A3:diff(cos,(th)) = -sin.(PI+(th-PI)) by SIN_COS:72
             .= -(-sin.(th-PI)) by SIN_COS:83
             .= sin.(th-PI);
     PI < th & th < 3/2*PI by A2,RCOMP_1:47;
 then PI-PI < th-PI & th-PI < 3/2*PI-PI by XREAL_1:11;
 then th-PI in ].0,PI/2.[ by RCOMP_1:47;
 hence diff(cos,(th)) > 0 by A3,Lm2;
end;
hence thesis by A1,Lm1,ROLLE:9;
end;

theorem
    cos is_increasing_on ].3/2*PI,2*PI.[
proof
A1: cos is_differentiable_on ].3/2*PI,2*PI.[ by FDIFF_1:34,SIN_COS:72;
    for th be Real st th in ].3/2*PI,2*PI.[ holds diff(cos,(th))>0
 proof
 let th be Real such that
 A2:th in ].3/2*PI,2*PI.[;
 A3: diff(cos,(th)) = -sin.(PI + (PI/2+(th-3/2*PI))) by SIN_COS:72
               .= -(-sin.(PI/2+(th-3/2*PI))) by SIN_COS:83
               .= cos.(th-3/2*PI) by SIN_COS:83;
     3/2*PI < th & th < 2*PI by A2,RCOMP_1:47;
 then 3/2*PI-3/2*PI < th-3/2*PI
   & th-3/2*PI < 2*PI-3/2*PI by XREAL_1:11;
 then th-3/2*PI in ].0,PI/2.[ by RCOMP_1:47;
 hence diff(cos,(th)) > 0 by A3,SIN_COS:85;
 end;
hence thesis by A1,Lm1,ROLLE:9;
end;

theorem
  for n being Nat holds sin.th = sin.(2*PI*n + th)
proof let n be Nat;
  defpred X[Nat] means for th holds sin.th = sin.(2*PI*$1 + th);
A1: X[0];
A2:for n being Nat st X[n] holds X[n+1]
 proof
 let n be Nat such that
 A3:for th holds sin.th = sin.(2*PI*n + th);
    for th holds sin.th = sin.(2*PI*(n+1) + th)
  proof
  let th;
      sin.(2*PI*(n+1) + th) = sin.((2*PI*n+th) + 2*PI)
                        .= sin.(2*PI*n + th) by SIN_COS:83
                        .= sin.th by A3;
  hence thesis;
  end;
  hence thesis;
 end;
   for n being Nat holds X[n] from NAT_1:sch 2(A1,A2);
 hence thesis;
end;

theorem
   for n being Nat holds cos.th = cos.(2*PI*n + th)
proof let n be Nat;
 defpred X[Nat] means for th holds cos.th = cos.(2*PI*$1 + th);
A1: X[0];
A2: for n being Nat st X[n] holds X[n+1]
proof
 let n be Nat such that
 A3:for th holds cos.th = cos.(2*PI*n + th);
    for th holds cos.th = cos.(2*PI*(n+1) + th)
  proof
  let th;
      cos.(2*PI*(n+1) + th) = cos.((2*PI*n+th) + 2*PI)
                        .= cos.(2*PI*n + th) by SIN_COS:83
                        .= cos.th by A3;
  hence thesis;
  end;
  hence thesis;
 end;
   for n being Nat holds X[n] from NAT_1:sch 2(A1,A2);
 hence thesis;
end;

begin ::Hyperbolic sine, hyperbolic cosine and hyperbolic tangent.

definition
  func sinh -> PartFunc of REAL, REAL means :Def1:
  dom it= REAL & for d being real number holds it.d=(exp_R.d - exp_R.(-d))/2;
  existence
  proof
    defpred X[set] means $1 in REAL;
    deffunc U(Real) = (exp_R.($1) - exp_R.(-$1))/2;
   consider f being PartFunc of REAL, REAL such that
   A1: for d be Element of REAL holds d in dom f iff X[d] and
   A2: for d be Element of REAL st d in dom f holds f/.d = U(d) from
PARTFUN2:sch 2;
   take f;
   thus dom(f)=REAL
    proof
      for x be set st x in REAL holds x in dom f by A1;
     then REAL c=dom(f) by TARSKI:def 3;
     hence dom(f)=REAL by XBOOLE_0:def 10;
    end;
     let d be real number;
    d is Real by XREAL_0:def 1;
      then A3: d in dom f by A1;
      then f/.d = (exp_R.d - exp_R.(-d))/2 by A2;
      hence f.d = (exp_R.d - exp_R.(-d))/2 by A3,FINSEQ_4:def 4;
    end;
 uniqueness
  proof
   let f1,f2 be PartFunc of REAL, REAL;
   assume
   A4: dom f1= REAL & for d being real number holds
   f1.d=(exp_R.d - exp_R.(-d))/2;
   assume
   A5: dom f2= REAL & for d being real number holds
   f2.d=(exp_R.d - exp_R.(-d))/2;
       for d being Element of REAL st d in REAL holds f1.d = f2.d
    proof
     let d be Element of REAL such that
         d in REAL;
     thus f1.d=(exp_R.d - exp_R.(-d))/2 by A4
             .=f2.d by A5;
    end;
   hence f1=f2 by A4,A5,PARTFUN1:34;
  end;
 end;

definition let d be number;
  func sinh(d) equals
   sinh.d;
  coherence;
end;

registration let d be number;
  cluster sinh(d) -> real;
  coherence;
end;

definition let d be number;
  redefine func sinh(d) -> Real;
  coherence;
end;

definition
  func cosh -> PartFunc of REAL, REAL means :Def3:
  dom it = REAL & for d being real number holds it.d=(exp_R.d + exp_R.(-d))/2;
  existence
  proof
    defpred X[set] means $1 in REAL;
    deffunc U(Real) = (exp_R.$1 + exp_R.(-$1))/2;
   consider f being PartFunc of REAL, REAL such that
   A1:   for d be Element of REAL holds d in dom f iff X[d] and
   A2:   for d be Element of REAL st d in dom f holds f/.d = U(d)
    from PARTFUN2:sch 2;
   take f;
   thus dom(f)=REAL
    proof
      for x be set st x in REAL holds x in dom f by A1;
     then REAL c=dom(f) by TARSKI:def 3;
     hence dom(f)=REAL by XBOOLE_0:def 10;
    end;
    let d be real number;
   d is Real by XREAL_0:def 1;
      then A3: d in dom f by A1;
      then f/.d = (exp_R.d + exp_R.(-d))/2 by A2;
     hence f.d = (exp_R.d + exp_R.(-d))/2 by A3,FINSEQ_4:def 4;
  end;
 uniqueness
  proof
   let f1,f2 be PartFunc of REAL, REAL;
   assume
   A4: dom f1= REAL & for d being real number holds
   f1.d=(exp_R.d + exp_R.(-d))/2;
   assume
   A5: dom f2= REAL & for d being real number holds
   f2.d=(exp_R.d + exp_R.(-d))/2;
     for d being Element of REAL st d in REAL holds f1.d = f2.d
    proof
     let d be Element of REAL such that
      d in REAL;
     thus f1.d=(exp_R.d + exp_R.(-d))/2 by A4
             .=f2.d by A5;
    end;
   hence f1=f2 by A4,A5,PARTFUN1:34;
  end;
 end;

definition let d be number;
  func cosh(d) equals
   cosh.d;
  coherence;
end;

registration let d be number;
  cluster cosh(d) -> real;
  coherence;
end;

definition let d be number;
  redefine func cosh(d) -> Real;
  coherence;
end;

definition
  func tanh -> PartFunc of REAL, REAL means :Def5:
  dom it= REAL &
  for d being real number holds
    it.d=(exp_R.d - exp_R.(-d))/(exp_R.d + exp_R.(-d));
  existence
   proof
    defpred X[set] means $1 in REAL;
    deffunc U(Real) = (exp_R.$1 - exp_R.(-$1))/(exp_R.$1 + exp_R.(-$1));
    consider f being PartFunc of REAL, REAL such that
    A1: for d be Element of REAL holds d in dom f iff X[d] and
    A2: for d be Element of REAL st d in dom f holds f/.d = U(d)
                      from PARTFUN2:sch 2;
    take f;
    thus dom(f)=REAL
     proof
       for x be set st x in REAL holds x in dom f by A1;
      then REAL c=dom(f) by TARSKI:def 3;
      hence dom(f)=REAL by XBOOLE_0:def 10;
     end;
     let d be real number;
   d is Real by XREAL_0:def 1;
       then A3: d in dom f by A1;
       then f/.d = (exp_R.d - exp_R.(-d))/(exp_R.d + exp_R.(-d)) by A2;
        hence f.d = (exp_R.d - exp_R.(-d))/(exp_R.d + exp_R.(-d))
        by A3,FINSEQ_4:def 4;
   end;
  uniqueness
   proof
    let f1,f2 be PartFunc of REAL, REAL;
    assume
    A4: dom f1= REAL & for d being real number holds
        f1.d=(exp_R.(d) - exp_R.(-d))/(exp_R.d + exp_R.(-d));
    assume
    A5: dom f2= REAL & for d being real number holds
        f2.d=(exp_R.(d) - exp_R.(-d))/(exp_R.(d) + exp_R.(-d));
        for d being Element of REAL st d in REAL holds f1.d = f2.d
     proof
      let d be Element of REAL such that
       d in REAL;
      thus f1.d=(exp_R.(d) - exp_R.(-d))/(exp_R.(d) + exp_R.(-d)) by A4
              .=f2.d by A5;
     end;
    hence f1=f2 by A4,A5,PARTFUN1:34;
   end;
 end;

definition let d be number;
  func tanh(d) equals
   tanh.d;
  coherence;
end;

registration let d be number;
  cluster tanh(d) -> real;
  coherence;
end;

definition let d be number;
  redefine func tanh(d) -> Real;
  coherence;
end;

theorem Th12:
  exp_R.(p+q) = exp_R.(p) * exp_R.(q)
 proof
     exp_R.(p+q) = exp_R(p+q) by SIN_COS:def 27
          .= exp_R(p) * exp_R(q) by SIN_COS:55
          .= exp_R.(p) * exp_R(q) by SIN_COS:def 27
          .= exp_R.(p) * exp_R.(q) by SIN_COS:def 27;
 hence thesis;
 end;

theorem Th13:
  exp_R.0 = 1 by SIN_COS:56,def 27;

theorem Th14:
  (cosh.p)^2-(sinh.p)^2=1 &
  (cosh.p)*(cosh.p)-(sinh.p)*(sinh.p)=1
 proof
  A1: (cosh.p)*(cosh.p)
    = ((exp_R.(p) + exp_R.(-p))/2)*(cosh.p) by Def3
   .= ((exp_R.(p) + exp_R.(-p))/2)*((exp_R.(p) + exp_R.(-p))/2) by Def3
   .= ((exp_R.(p))*(exp_R.(p))+(exp_R.(p))*(exp_R.(-p))
      +(exp_R.(-p))*(exp_R.(p))+(exp_R.(-p))*(exp_R.(-p)))/(2*2)
   .= ((exp_R.(p+p))+(exp_R.(p))*(exp_R.(-p))
      +(exp_R.(-p))*(exp_R.(p))+(exp_R.(-p))*(exp_R.(-p)))/4 by Th12
   .= ((exp_R.(p+p))+(exp_R.(p+-p))
      +(exp_R.(-p))*(exp_R.(p))+(exp_R.(-p))*(exp_R.(-p)))/4 by Th12
   .= ((exp_R.(p+p))+(exp_R.(p+-p))
      +(exp_R.(-p))*(exp_R.(p))+(exp_R.(-p+-p)))/4 by Th12
   .= ((exp_R.(p+p)) + 1 + 1 + (exp_R.(-p+-p)))/4 by Th12,Th13
   .= ((exp_R.(p+p)) + 2 + (exp_R.(-p+-p)))/4;
       (sinh.p)*(sinh.p)
    = ((exp_R.(p) - exp_R.(-p))/2)*(sinh.p) by Def1
   .= ((exp_R.(p) - exp_R.(-p))/2)*((exp_R.(p) - exp_R.(-p))/2) by Def1
   .= ((exp_R.(p))*(exp_R.(p))-(exp_R.(p))*(exp_R.(-p))
      -(exp_R.(-p))*(exp_R.(p))+(exp_R.(-p))*(exp_R.(-p)))/(2*2)
   .= ((exp_R.(p+p))-(exp_R.(p))*(exp_R.(-p))
      -(exp_R.(-p))*(exp_R.(p))+(exp_R.(-p))*(exp_R.(-p)))/4 by Th12
   .= ((exp_R.(p+p))-(exp_R.(p+-p))
      -(exp_R.(-p))*(exp_R.(p))+(exp_R.(-p))*(exp_R.(-p)))/4 by Th12
   .= ((exp_R.(p+p))-(exp_R.(p+-p))
      -(exp_R.(-p))*(exp_R.(p))+(exp_R.(-p+-p)))/4 by Th12
   .= ((exp_R.(p+p)) - 1 - 1 + (exp_R.(-p+-p)))/4 by Th12,Th13;
 hence thesis by A1;
 end;

Lm3:
cosh.(p+r)=(cosh.p)*(cosh.r) + (sinh.p)*(sinh.r)
proof
      (cosh.p)*(cosh.r) + (sinh.p)*(sinh.r)
 =((exp_R.(p) + exp_R.(-p))/2)*(cosh.r)
  +(sinh.p)*(sinh.r) by Def3
.=((exp_R.(p) + exp_R.(-p))/2)*((exp_R.(r) + exp_R.(-r))/2)
  +(sinh.p)*(sinh.r) by Def3
.=((exp_R.(p) + exp_R.(-p))/2)*((exp_R.(r) + exp_R.(-r))/2)
  +((exp_R.(p) - exp_R.(-p))/2)*(sinh.r) by Def1
.=((exp_R.(p) + exp_R.(-p))/2)*((exp_R.(r) + exp_R.(-r))/2)
  +((exp_R.(p) - exp_R.(-p))/2)*((exp_R.(r) - exp_R.(-r))/2) by Def1
.=( 2*((exp_R.(p))*(exp_R.(r))) + 2*((exp_R.(-p))*(exp_R.(-r))) )/4
.=( 2*(exp_R.(p+r)) + 2*((exp_R.(-p))*(exp_R.(-r))) )/4 by Th12
.=( 2*(exp_R.(p+r)) + 2*(exp_R.(-p+-r)) )/4 by Th12
.=(exp_R.(p+r)+exp_R.(-(p+r)))/2
.=cosh.(p+r) by Def3;
hence thesis;
end;

Lm4:
sinh.(p+r)=(sinh.p)*(cosh.r) + (cosh.p)*(sinh.r)
proof
     (sinh.p)*(cosh.r) + (cosh.p)*(sinh.r)
 =((exp_R.(p) - exp_R.(-p))/2)*(cosh.r) + (cosh.p)*(sinh.r) by Def1
.=((exp_R.(p) - exp_R.(-p))/2)*((exp_R.(r) + exp_R.(-r))/2)
   + (cosh.p)*(sinh.r) by Def3
.=((exp_R.(p) - exp_R.(-p))/2)*((exp_R.(r) + exp_R.(-r))/2)
   + ((exp_R.(p) + exp_R.(-p))/2)*(sinh.r) by Def3
.=((exp_R.(p) - exp_R.(-p))/2)*((exp_R.(r) + exp_R.(-r))/2)
   + ((exp_R.(p) + exp_R.(-p))/2)*((exp_R.(r) - exp_R.(-r))/2) by Def1
.=( 2*( (exp_R.(p))*(exp_R.(r)) )+2*( -((exp_R.(-p))*(exp_R.(-r))) ) )/4
.=( 2*(exp_R.(p+r))+2*( -(exp_R.(-p))*(exp_R.(-r)) ) )/4 by Th12
.=( 2*(exp_R.(p+r))+2*( -(exp_R.(-p+-r)) ) )/4 by Th12
.=(exp_R.(p+r)-exp_R.(-(p+r)))/2
.=sinh.(p+r) by Def1;
hence thesis;
end;

theorem Th15:
  cosh.p <> 0 & cosh.p > 0 & cosh.0 = 1
proof
 A1: cosh.0 = 1
  proof
      cosh.0 = (exp_R.0+exp_R.(-0))/2 by Def3
        .= 1 by SIN_COS:56,def 27;
  hence thesis;
  end;
     cosh.p > 0
  proof
      exp_R.p > 0 & exp_R.(-p) > 0 by SIN_COS:59;
  then exp_R.p + exp_R.(-p) > 0+0 by XREAL_1:10;
  then (exp_R.p+exp_R.(-p))/2 > 0 by REAL_2:127;
  hence thesis by Def3;
  end;
  hence thesis by A1;
end;

theorem Th16:
  sinh.0 = 0
proof
    sinh.0 = (exp_R.0-exp_R.(-0))/2 by Def1
      .= 0;
hence thesis;
end;

theorem Th17:
  tanh.p = (sinh.p)/(cosh.p)
proof
      (sinh.p)/(cosh.p)
 =((exp_R.(p) - exp_R.(-p))/2)/(cosh.p) by Def1
.=((exp_R.(p) - exp_R.(-p))/2)/((exp_R.(p) + exp_R.(-p))/2) by Def3
.=(exp_R.(p) - exp_R.(-p))/(exp_R.(p) + exp_R.(-p)) by XCMPLX_1:55
.=tanh.p by Def5;
hence thesis;
end;

Lm5: for a1 be real number holds r<>0 & q<>0 & r*q + p*a1 <> 0 implies
(p*q + r*a1)/(r*q + p*a1) = (p/r + a1/q)/(1 + (p/r)*(a1/q))
 proof
 let a1 be real number;
 assume A1: r<>0 & q<>0 & r*q + p*a1 <> 0;
 then A2: r<>0 & q<>0 & r*q <> 0 & r*q + p*a1 <> 0 by XCMPLX_1:6;
   then (p*q + r*a1)/(r*q + p*a1)
  =( (p*q + r*a1)/(r*q) )/( (r*q + p*a1)/(r*q) ) by XCMPLX_1:55
 .=( (p*q)/(r*q) + (r*a1)/(r*q) )/( (r*q + p*a1)/(r*q) ) by XCMPLX_1:63
 .=( (p*q)/(r*q) + (r*a1)/(r*q) )/
   ( (r*q)/(r*q) + (p*a1)/(r*q) ) by XCMPLX_1:63
 .=( p/r + (a1*r)/(q*r) )/( (r*q)/(r*q) + (p*a1)/(r*q) ) by A1,XCMPLX_1:92
 .=( p/r + a1/q )/( (r*q)/(r*q) + (p*a1)/(r*q) ) by A1,XCMPLX_1:92
 .=( p/r + a1/q )/( 1 + (p*a1)/(r*q) ) by A2,XCMPLX_1:60
 .=( p/r + a1/q )/( 1 + (p/r)*(a1/q) ) by XCMPLX_1:77;
 hence thesis;
end;

Lm6:
tanh.(p+r) = (tanh.p + tanh.r)/(1+ (tanh.p)*(tanh.r))
proof
A1: cosh.r <> 0 & cosh.p <> 0 &
    (cosh.p)*(cosh.r) + (sinh.p)*(sinh.r) <> 0
 proof
       (cosh.p)*(cosh.r) + (sinh.p)*(sinh.r)
 =cosh.(p+r) by Lm3;
 hence thesis by Th15;
 end;
      tanh.(p+r)
 =(sinh.(p+r))/(cosh.(p+r)) by Th17
.=( (sinh.p)*(cosh.r) + (cosh.p)*(sinh.r) )/(cosh.(p+r)) by Lm4
.=( (sinh.p)*(cosh.r) + (cosh.p)*(sinh.r) )/
  ( (cosh.p)*(cosh.r) + (sinh.p)*(sinh.r) ) by Lm3
.=( (sinh.p)/(cosh.p) + (sinh.r)/(cosh.r) )/
  ( 1 + ( (sinh.p)/(cosh.p) )*( (sinh.r)/(cosh.r) ) ) by A1,Lm5
.=( tanh.p + (sinh.r)/(cosh.r) )/
  ( 1 + ( (sinh.p)/(cosh.p) )*( (sinh.r)/(cosh.r) ) ) by Th17
.=( tanh.p + tanh.r )/
  ( 1 + ( (sinh.p)/(cosh.p) )*( (sinh.r)/(cosh.r) ) ) by Th17
.=( tanh.p + tanh.r )/
  ( 1 + ( tanh.p )*( (sinh.r)/(cosh.r) ) ) by Th17
.=( tanh.p + tanh.r )/( 1 + ( tanh.p )*( tanh.r ) ) by Th17;
hence thesis;
end;

theorem Th18:
(sinh.p)^2 = 1/2*(cosh.(2*p) - 1) &
(cosh.p)^2 = 1/2*(cosh.(2*p) + 1)
proof
A1: (sinh.p)^2 = 1/2*(cosh.(2*p) - 1)
 proof
   (sinh.p)^2 =( (exp_R.(p) - exp_R.(-p))/2 )*(sinh.p) by Def1
 .=( (exp_R.(p) - exp_R.(-p))/2 )*( (exp_R.(p) - exp_R.(-p))/2 ) by Def1
 .=( (exp_R.(p))*(exp_R.(p))-(exp_R.(p))*(exp_R.(-p))
   -(exp_R.(-p))*(exp_R.(p))+(exp_R.(-p))*(exp_R.(-p)) )/4
 .=( (exp_R.(p+p))-(exp_R.(p))*(exp_R.(-p))
   -(exp_R.(p))*(exp_R.(-p))+(exp_R.(-p))*(exp_R.(-p)) )/4 by Th12
 .=( (exp_R.(p+p))-(exp_R.(p))*(exp_R.(-p))
   -(exp_R.(p))*(exp_R.(-p))+(exp_R.(-p+ -p)) )/4 by Th12
 .=( (exp_R.(p+p))-(exp_R.(p+ -p))
   -(exp_R.(p))*(exp_R.(-p))+(exp_R.(-p+ -p)) )/4 by Th12
 .=( ((exp_R.(p+p))+ -(exp_R.(p+ -p))
    -(exp_R.(p+ -p)) )+(exp_R.(-p+ -p)) )/4 by Th12
 .=( exp_R.(2*p) + 2*(-(exp_R.(0))) +exp_R.(-p+-p) )/4
 .=( exp_R.(2*p) + 2*(-1) +exp_R.(-(2*p)) )/4 by SIN_COS:56,def 27
 .=1/2*( ( exp_R.(2*p) +exp_R.(-(2*p)) )/2 ) + ((-1)*2)/(2*2)
 .=1/2*(cosh.(2*p))+ 1/2*((2*(-1))/2) by Def3
 .=1/2*( cosh.(2*p) - 1 );
 hence thesis;
 end;
   (cosh.p)^2 = 1/2*(cosh.(2*p) + 1)
 proof
      (cosh.p)^2
   =( (exp_R.(p) + exp_R.(-p))/2 )*(cosh.p) by Def3
  .=( (exp_R.(p) + exp_R.(-p))/2 )*( (exp_R.(p) + exp_R.(-p))/2 ) by Def3
  .=( (exp_R.(p))*(exp_R.(p))+(exp_R.(p))*(exp_R.(-p))
    +(exp_R.(-p))*(exp_R.(p))+(exp_R.(-p))*(exp_R.(-p)) )/4
  .=( (exp_R.(p+p))+(exp_R.(p))*(exp_R.(-p))
    +(exp_R.(p))*(exp_R.(-p))+(exp_R.(-p))*(exp_R.(-p)) )/4 by Th12
  .=( (exp_R.(p+p))+(exp_R.(p))*(exp_R.(-p))
    +(exp_R.(p))*(exp_R.(-p))+(exp_R.(-p+ -p)) )/4 by Th12
  .=( (exp_R.(p+p))+(exp_R.(p+ -p))
    +(exp_R.(p))*(exp_R.(-p))+(exp_R.(-p+ -p)) )/4 by Th12
  .=( (exp_R.(p+p))+(exp_R.(p+ -p))
    +(exp_R.(p+ -p))+(exp_R.(-p+ -p)) )/4 by Th12
  .=( exp_R.(2*p) + 2*(exp_R.(0)) +exp_R.(-p+-p) )/4
  .=( exp_R.(2*p) + 2*1 +exp_R.(-(2*p)) )/4 by SIN_COS:56,def 27
  .=1/2*( ( exp_R.(2*p) +exp_R.(-(2*p)) )/2 ) + (1*2)/(2*2)
  .=1/2*(cosh.(2*p))+ 1/2*((2*1/2)) by Def3
  .=1/2*( cosh.(2*p)+ 1 );
 hence thesis;
 end;
hence thesis by A1;
end;

Lm7:
sinh.(2*p) = 2*(sinh.p)*(cosh.p) &
cosh.(2*p) = 2*(cosh.p)^2 - 1
proof
A1: sinh.(2*p) = 2*(sinh.p)*(cosh.p)
 proof
       2*(sinh.p)*(cosh.p)
  =2*( (exp_R.(p) - exp_R.(-p))/2 )*(cosh.p) by Def1
 .=2*( (exp_R.(p) - exp_R.(-p))/2 )*( (exp_R.(p) + exp_R.(-p))/2 ) by Def3
 .=2/4*((exp_R.p)^2 - (exp_R.(-p))^2)
 .=2/4*( exp_R.(p+p)- (exp_R.(-p))*(exp_R.(-p)) ) by Th12
 .=2/4*(exp_R.(2*p)- exp_R.(-p+ -p)) by Th12
 .=( ( exp_R.(2*p)- exp_R.(-(2*p)) )/2 )*1
 .=sinh.(2*p) by Def1;
 hence thesis;
 end;
    cosh.(2*p) = 2*(cosh.p)^2 - 1
 proof
     2*(cosh.p)^2 = cosh.(2*p) + 1
  proof
        2*(cosh.p)^2
   =2*( 1/2*(cosh.(2*p) + 1) ) by Th18
  .=cosh.(2*p) + 1;
  hence thesis;
  end;
 hence cosh.(2*p) = 2*(cosh.p)^2 - 1;
 end;
hence thesis by A1;
end;

theorem Th19:
cosh.(-p) = cosh.p &
sinh.(-p) = -sinh.p &
tanh.(-p) = -tanh.p
proof
 A1: cosh.(-p) = cosh.p
 proof
     cosh.(-p) = (exp_R.(-p) + exp_R.(-(-p)))/2 by Def3
          .= cosh.p by Def3;
 hence thesis;
 end;
 A2: sinh.(-p) = -sinh.p
 proof
     sinh.(-p) = (exp_R.(-p) - exp_R.(-(-p)))/2 by Def1
          .= -(exp_R.(p) - exp_R.(-p))/2
          .= -sinh.p by Def1;
 hence thesis;
 end;
   tanh.(-p) = -tanh.p
 proof
     tanh.(-p) = (-sinh.p)/(cosh.(-p)) by A2,Th17
          .= -(sinh.p)/(cosh.p) by A1,XCMPLX_1:188
          .= -tanh.p by Th17;
 hence thesis;
 end;
hence thesis by A1,A2;
end;

Lm8:
cosh.(p-r)=(cosh.p)*(cosh.r) - (sinh.p)*(sinh.r)
proof
      cosh.(p-r)=cosh.(p+ -r)
.=(cosh.p)*(cosh.-r) + (sinh.p)*(sinh.-r) by Lm3
.=(cosh.p)*(cosh.r) + (sinh.p)*(sinh.-r) by Th19
.=(cosh.p)*(cosh.r) + (sinh.p)*(-sinh.r) by Th19
.=(cosh.p)*(cosh.r) - ((sinh.p)*(sinh.r));
hence thesis;
end;

theorem
    cosh.(p+r)=(cosh.p)*(cosh.r) + (sinh.p)*(sinh.r) &
cosh.(p-r)=(cosh.p)*(cosh.r) - (sinh.p)*(sinh.r) by Lm3,Lm8;

Lm9:
sinh.(p-r)=(sinh.p)*(cosh.r) - (cosh.p)*(sinh.r)
proof
      sinh.(p-r)=sinh.(p+-r)
.=(sinh.p)*(cosh.-r) +(cosh.p)*(sinh.-r) by Lm4
.=(sinh.p)*(cosh.r) + (cosh.p)*(sinh.-r) by Th19
.=(sinh.p)*(cosh.r) + (cosh.p)*(-sinh.r) by Th19
.=(sinh.p)*(cosh.r) -((cosh.p)*(sinh.r));
hence thesis;
end;

theorem
    sinh.(p+r)=(sinh.p)*(cosh.r) + (cosh.p)*(sinh.r) &
sinh.(p-r)=(sinh.p)*(cosh.r) - (cosh.p)*(sinh.r) by Lm4,Lm9;

Lm10:
tanh.(p-r) = (tanh.p - tanh.r)/(1- (tanh.p)*(tanh.r))
proof
      tanh.(p-r)=tanh.(p+-r)
.=(tanh.p + tanh.-r)/(1+ (tanh.p)*(tanh.-r)) by Lm6
.=(tanh.p + -tanh.r)/(1+ (tanh.p)*(tanh.-r)) by Th19
.=(tanh.p - tanh.r)/(1+ (tanh.p)*(-tanh.r)) by Th19
.=(tanh.p - tanh.r)/(1 -(tanh.p*tanh.r));
hence thesis;
end;

theorem
    tanh.(p+r) = (tanh.p + tanh.r)/(1+ (tanh.p)*(tanh.r)) &
tanh.(p-r) = (tanh.p - tanh.r)/(1- (tanh.p)*(tanh.r))
by Lm6,Lm10;

theorem
   sinh.(2*p) = 2*(sinh.p)*(cosh.p) &
 cosh.(2*p) = 2*(cosh.p)^2 - 1 &
 tanh.(2*p) = (2*tanh.p)/(1+(tanh.p)^2)
 proof
       tanh.(2*p)=tanh.(p+p)
 .=(tanh.p + tanh.p)/(1+ (tanh.p)*(tanh.p)) by Lm6
 .=(2*tanh.p)/(1+ (tanh.p)^2);
 hence thesis by Lm7;
 end;

theorem Th24:
(sinh.p)^2 - (sinh.q)^2 = (sinh.(p+q))*(sinh.(p-q)) &
(sinh.(p+q))*(sinh.(p-q)) = (cosh.p)^2 - (cosh.q)^2 &
(sinh.p)^2 - (sinh.q)^2 = (cosh.p)^2 - (cosh.q)^2
proof
 A1:
 (sinh.(p+q))*(sinh.(p-q)) = (sinh.p)^2*(cosh.q)^2 - (sinh.q)^2*(cosh.p)^2
 proof
       (sinh.(p+q))*(sinh.(p-q))
  =((sinh.p)*(cosh.q) + (cosh.p)*(sinh.q))*(sinh.(p+(-q))) by Lm4
 .=((sinh.p)*(cosh.q) + (cosh.p)*(sinh.q))
   *((sinh.p)*(cosh.(-q)) + (cosh.p)*(sinh.(-q))) by Lm4
 .=((sinh.p)*(cosh.q) + (cosh.p)*(sinh.q))
   *((sinh.p)*(cosh.q) + (cosh.p)*(sinh.(-q))) by Th19
 .=((sinh.p)*(cosh.q) + (cosh.p)*(sinh.q))
   *((sinh.p)*(cosh.q) + (cosh.p)*(-sinh.q)) by Th19
 .= (sinh.p)^2*(cosh.q)^2-(sinh.q)^2*(cosh.p)^2;
 hence thesis;
 end;
 A2: (sinh.p)^2 - (sinh.q)^2 = (sinh.(p+q))*(sinh.(p-q))
 proof
     (sinh.(p+q))*(sinh.(p-q))
  =(sinh.p)^2*( (cosh.q)^2 -(sinh.q)^2 )
   + ((sinh.q)^2*(sinh.p)^2)-(sinh.q)^2*(cosh.p)^2 by A1
 .=(sinh.p)^2*1 + (sinh.p)^2*(sinh.q)^2-(sinh.q)^2*(cosh.p)^2 by Th14
 .=(sinh.p)^2 + (sinh.q)^2*(-((cosh.p)^2-(sinh.p)^2))
 .=(sinh.p)^2 + (sinh.q)^2*(-1) by Th14
 .=(sinh.p)^2 - (sinh.q)^2;
 hence thesis;
 end;
     (sinh.(p+q))*(sinh.(p-q)) = (cosh.p)^2 - (cosh.q)^2
 proof
     (sinh.(p+q))*(sinh.(p-q))
  =(cosh.q)^2*( -((cosh.p)^2-(sinh.p)^2) )
   +( ((cosh.p)^2*(cosh.q)^2)+ -(sinh.q)^2*(cosh.p)^2 ) by A1
 .=(cosh.q)^2*( -1 )
   +( (cosh.p)^2*( (cosh.q)^2-(sinh.q)^2 ) ) by Th14
 .=(cosh.q)^2*( -1 )+ ((cosh.p)^2*1) by Th14
 .=(cosh.p)^2 -(cosh.q)^2;
 hence thesis;
 end;
hence thesis by A2;
end;

theorem Th25:
(sinh.p)^2 + (cosh.q)^2 = (cosh.(p+q))*(cosh.(p-q)) &
(cosh.(p+q))*(cosh.(p-q)) = (cosh.p)^2 + (sinh.q)^2 &
(sinh.p)^2 + (cosh.q)^2 = (cosh.p)^2 + (sinh.q)^2
proof
 A1:
 (cosh.(p+q))*(cosh.(p-q)) = (cosh.p)^2*(cosh.q)^2- (sinh.p)^2*(sinh.q)^2
 proof
       (cosh.(p+q))*(cosh.(p-q))
  =( (cosh.p)*(cosh.q) + (sinh.p)*(sinh.q) )*
   (cosh.(p + -q)) by Lm3
 .=( ( (cosh.p)*(cosh.q) ) + ( (sinh.p)*(sinh.q) ) )*
   ( ( (cosh.p)*(cosh.-q) ) + ( (sinh.p)*(sinh.-q) ) ) by Lm3
 .=( (cosh.p)*(cosh.q) )*( (cosh.p)*(cosh.-q) )
  +( (cosh.p)*(cosh.q) )*( (sinh.p)*(sinh.-q) )
  +( (sinh.p)*(sinh.q) )*( (cosh.p)*(cosh.-q) )
  +( (sinh.p)*(sinh.q) )*( (sinh.p)*(sinh.-q) )
 .=( (cosh.p)*(cosh.q) )*( (cosh.p)*(cosh.q) )
  +( (cosh.p)*(cosh.q) )*( (sinh.-q)*(sinh.p) )
  +( (sinh.p)*(sinh.q) )*( (cosh.p)*(cosh.-q) )
  +( (sinh.p)*(sinh.q) )*( (sinh.-q)*(sinh.p) ) by Th19
 .=( (cosh.p)*(cosh.q) )^2
  +( (sinh.-q)*(sinh.p) )*( (cosh.p)*(cosh.q) )
  +( (sinh.p)*(sinh.q) )*( (cosh.p)*(cosh.q) )
  +( (sinh.-q)*(sinh.p) )*( (sinh.p)*(sinh.q) ) by Th19
 .=( (cosh.p)*(cosh.q) )^2
  +( (-sinh.q)*(sinh.p) )*( (cosh.p)*(cosh.q) )
  +( (sinh.p)*(sinh.q) )*( (cosh.p)*(cosh.q) )
  +( (sinh.-q)*(sinh.p) )*( (sinh.p)*(sinh.q) ) by Th19
 .=( (cosh.p)*(cosh.q) )^2 + 0
  +( (-1)*(sinh.q)*(sinh.p) )*( (sinh.p)*(sinh.q) ) by Th19
 .=(cosh.p)^2*(cosh.q)^2 - (sinh.q)^2*(sinh.p)^2;
 hence thesis;
 end;
 A2:(sinh.p)^2 + (cosh.q)^2 = (cosh.(p+q))*(cosh.(p-q))
 proof
       (cosh.(p+q))*(cosh.(p-q))
  =(cosh.q)^2*((cosh.p)^2-(sinh.p)^2)
  +( (cosh.q)^2*(sinh.p)^2 + -(sinh.p)^2*(sinh.q)^2 ) by A1
 .=(cosh.q)^2*1
  +( (cosh.q)^2*(sinh.p)^2-(sinh.p)^2*(sinh.q)^2 ) by Th14
 .=(cosh.q)^2+(sinh.p)^2*( (cosh.q)^2-(sinh.q)^2 )
 .=(cosh.q)^2+(sinh.p)^2*1 by Th14
 .=(cosh.q)^2+(sinh.p)^2;
 hence thesis;
 end;
    (cosh.(p+q))*(cosh.(p-q)) = (cosh.p)^2 + (sinh.q)^2
 proof
       (cosh.(p+q))*(cosh.(p-q))
  =(cosh.p)^2*( (cosh.q)^2-(sinh.q)^2 )
  + ((cosh.p)^2*(sinh.q)^2) + -((sinh.q)^2*(sinh.p)^2) by A1
 .=(cosh.p)^2*1
  + ((cosh.p)^2*(sinh.q)^2) + -((sinh.q)^2*(sinh.p)^2) by Th14
 .=(cosh.p)^2+(sinh.q)^2*((cosh.p)^2-(sinh.p)^2)
 .=(cosh.p)^2+(sinh.q)^2*1 by Th14
 .=(cosh.p)^2+(sinh.q)^2;
 hence thesis;
 end;
 hence thesis by A2;
end;

theorem
     sinh.p + sinh.r = 2*(sinh.(p/2+r/2))*(cosh.(p/2-r/2)) &
 sinh.p - sinh.r = 2*(sinh.(p/2-r/2))*(cosh.(p/2+r/2))
 proof
 A1:
   2*(sinh.(p/2+r/2))*(cosh.(p/2-r/2))
  =2*((sinh.(p/2))*(cosh.(r/2))+(cosh.(p/2))*(sinh.(r/2)))
    *(cosh.(p/2-r/2)) by Lm4
 .=2*((sinh.(p/2))*(cosh.(r/2))+(cosh.(p/2))*(sinh.(r/2)))
    *((cosh.(p/2))*(cosh.(r/2))-(sinh.(p/2))*(sinh.(r/2))) by Lm8
 .=2*( ((sinh.(r/2))*(cosh.(r/2)))*((cosh.(p/2))^2-(sinh.(p/2))^2)
     +(sinh.(p/2))*( (cosh.(p/2))*(cosh.(r/2))^2 )
     -(cosh.(p/2))*( (sinh.(p/2))*(sinh.(r/2))^2 ) )
 .=2*( ((sinh.(r/2))*(cosh.(r/2)))*1
     +(sinh.(p/2))*( (cosh.(p/2))*(cosh.(r/2))^2 )
     -(cosh.(p/2))*( (sinh.(p/2))*(sinh.(r/2))^2 ) ) by Th14
 .=2*( ((sinh.(p/2))*(cosh.(p/2)))*((cosh.(r/2))^2 -(sinh.(r/2))^2 )
      +(sinh.(r/2))*(cosh.(r/2)) )
 .=2*( ((sinh.(p/2))*(cosh.(p/2))*1)+(sinh.(r/2))*(cosh.(r/2)) ) by Th14
 .=2*(sinh.(p/2))*(cosh.(p/2)) + 2*((sinh.(r/2))*(cosh.(r/2)))
 .=sinh.(2*(p/2)) + 2*(sinh.(r/2))*(cosh.(r/2)) by Lm7
 .=sinh.p + sinh.(2*(r/2)) by Lm7
 .=sinh.p + sinh.r;
       2*(sinh.(p/2-r/2))*(cosh.(p/2+r/2))
  =2*( (sinh.(p/2))*(cosh.(r/2))-(cosh.(p/2))*(sinh.(r/2)) )
    *(cosh.(p/2+r/2)) by Lm9
 .=2*((sinh.(p/2))*(cosh.(r/2))-(cosh.(p/2))*(sinh.(r/2)))
    *((cosh.(p/2))*(cosh.(r/2))+(sinh.(p/2))*(sinh.(r/2))) by Lm3
 .=2*( ((cosh.(r/2))*(sinh.(r/2)))*(-((cosh.(p/2))^2-(sinh.(p/2))^2))
       +(sinh.(p/2))*( (cosh.(p/2))*(cosh.(r/2))^2 )
       -(cosh.(p/2))*( (sinh.(p/2))*(sinh.(r/2))^2 ) )
 .=2*( ((cosh.(r/2))*(sinh.(r/2)))*(-1)
      +(sinh.(p/2))*( (cosh.(p/2))*(cosh.(r/2))^2 )
      -(cosh.(p/2))*( (sinh.(p/2))*(sinh.(r/2))^2 ) ) by Th14
 .=2*( ((sinh.(p/2))*(cosh.(p/2)))*((cosh.(r/2))^2-(sinh.(r/2))^2)
      +(-1)*((cosh.(r/2))*(sinh.(r/2))) )
 .=2*( ((sinh.(p/2))*(cosh.(p/2)))*1
      +(-1)*((cosh.(r/2))*(sinh.(r/2))) ) by Th14
 .= 2*(sinh.(p/2))*(cosh.(p/2))-2*((sinh.(r/2))*(cosh.(r/2)))
 .=sinh.(2*(p/2)) - 2*(sinh.(r/2))*(cosh.(r/2)) by Lm7
 .=sinh.p - sinh.(2*(r/2)) by Lm7
 .=sinh.p - sinh.r;
 hence thesis by A1;
 end;

 theorem
     cosh.p + cosh.r = 2*(cosh.(p/2+r/2))*(cosh.(p/2-r/2)) &
 cosh.p - cosh.r = 2*(sinh.(p/2-r/2))*(sinh.(p/2+r/2))
 proof
 A1:2*(cosh.(p/2+r/2))*(cosh.(p/2-r/2))
  =2*((cosh.(p/2+r/2))*(cosh.(p/2-r/2)))
 .=2*( (sinh.(p/2))^2+(cosh.(r/2))^2 ) by Th25
 .=2*( 1/2*(cosh.(2*(p/2)) - 1)+(cosh.(r/2))^2 ) by Th18
 .=2*( 1/2*(cosh.p - 1)+1/2*(cosh.(2*(r/2)) + 1) ) by Th18
 .=cosh.r+cosh.p;
       2*(sinh.(p/2-r/2))*(sinh.(p/2+r/2))
  =2*((sinh.(p/2-r/2))*(sinh.(p/2+r/2)))
 .=2*( (cosh.(p/2))^2 - (cosh.(r/2))^2 ) by Th24
 .=2*( 1/2*(cosh.(2*(p/2)) + 1)-(cosh.(r/2))^2 ) by Th18
 .=2*( 1/2*(cosh.p + 1)-1/2*(cosh.(2*(r/2)) + 1) ) by Th18
 .=cosh.p-cosh.r;
 hence thesis by A1;
 end;

 theorem
     tanh.p + tanh.r = (sinh.(p+r))/((cosh.p)*(cosh.r)) &
 tanh.p - tanh.r = (sinh.(p-r))/((cosh.p)*(cosh.r))
 proof
 A1:cosh.p<>0 & cosh.r<>0 by Th15;
 A2: (sinh.(p+r))/((cosh.p)*(cosh.r))
  =( (sinh.p)*(cosh.r)+(cosh.p)*(sinh.r) )/((cosh.p)*(cosh.r)) by Lm4
 .= ((sinh.p)*(cosh.r))/((cosh.p)*(cosh.r))
   +((cosh.p)*(sinh.r))/((cosh.p)*(cosh.r)) by XCMPLX_1:63
 .=(sinh.p)/(cosh.p)+((cosh.p)*(sinh.r))/((cosh.p)*(cosh.r)) by A1,XCMPLX_1:92
 .=(sinh.p)/(cosh.p)+(sinh.r)/(cosh.r) by A1,XCMPLX_1:92
 .=tanh.p+(sinh.r)/(cosh.r) by Th17
 .=tanh.p+tanh.r by Th17;
       (sinh.(p-r))/((cosh.p)*(cosh.r))
  =( (sinh.p)*(cosh.r)-(cosh.p)*(sinh.r) )/((cosh.p)*(cosh.r)) by Lm9
 .= ((sinh.p)*(cosh.r))/((cosh.p)*(cosh.r))
   -((cosh.p)*(sinh.r))/((cosh.p)*(cosh.r)) by XCMPLX_1:121
 .=(sinh.p)/(cosh.p)-((cosh.p)*(sinh.r))/((cosh.p)*(cosh.r)) by A1,XCMPLX_1:92
 .=(sinh.p)/(cosh.p)-(sinh.r)/(cosh.r) by A1,XCMPLX_1:92
 .=tanh.p-(sinh.r)/(cosh.r) by Th17
 .=tanh.p-tanh.r by Th17;
 hence thesis by A2;
 end;

theorem
    (cosh.p + sinh.p) |^ n = cosh.(n*p) + sinh.(n*p)
proof
  defpred X[Element of NAT] means
      for p holds (cosh.p + sinh.p) |^ $1 = cosh.($1*p) + sinh.($1*p);
 A1: X[0] by Th15,Th16,NEWTON:9;
 A2:for n st X[n] holds X[n+1]
 proof
 let n such that
 A3: for p holds (cosh.p + sinh.p) |^ n = cosh.(n*p) + sinh.(n*p);
    for p holds (cosh.p + sinh.p) |^ (n+1) = cosh.((n+1)*p) + sinh.((n+1)*p)
  proof
  let p;
  A4:
  (cosh.(n*p))*(cosh.p)+(sinh.(n*p))*(sinh.p)=cosh.((n+1)*p)
   proof
       (cosh.(n*p))*(cosh.p)+(sinh.(n*p))*(sinh.p)
    =((exp_R.(n*p) + exp_R.(-n*p))/2)*(cosh.p)
    +(sinh.(n*p))*(sinh.p) by Def3
   .=((exp_R.(n*p) + exp_R.(-n*p))/2)*((exp_R.p + exp_R.-p)/2)
    +(sinh.(n*p))*(sinh.p) by Def3
   .=((exp_R.(n*p) + exp_R.(-n*p))/2)*((exp_R.p + exp_R.-p)/2)
    +((exp_R.(n*p) - exp_R.(-n*p))/2)*(sinh.p) by Def1
   .=((exp_R.(n*p))/2 + (exp_R.(-n*p))/2)*((exp_R.p)/2 + (exp_R.-p)/2)
    +((exp_R.(n*p))/2 - (exp_R.(-n*p))/2)*((exp_R.p - exp_R.-p)/2) by Def1
   .=2*( ( (exp_R.(n*p))*(exp_R.p) )/(2*2) )
    +2*(((exp_R.(-n*p))/2)*((exp_R.-p)/2))
   .=2*( exp_R.(p*n+p*1)/(2*2) )
    +2*( ( (exp_R.(-n*p))*(exp_R.-p) )/(2*2) ) by Th12
   .=2*( exp_R.(p*(n+1))/(2*2) )
    +2*( exp_R.(-n*p+-p)/(2*2) ) by Th12
   .=(exp_R.(p*(n+1))+exp_R.(-(p*(n+1))))/2
   .=cosh.(p*(n+1)) by Def3;
   hence thesis;
   end;
  A5:
  (cosh.(n*p))*(sinh.p)+(sinh.(n*p))*(cosh.p)=sinh.((n+1)*p)
   proof
       (cosh.(n*p))*(sinh.p)+(sinh.(n*p))*(cosh.p)
   =((exp_R.(n*p) + exp_R.(-n*p))/2)*(sinh.p)
   +(sinh.(n*p))*(cosh.p) by Def3
  .=((exp_R.(n*p) + exp_R.(-n*p))/2)*((exp_R.p - exp_R.-p)/2)
   +(sinh.(n*p))*(cosh.p) by Def1
  .=((exp_R.(n*p) + exp_R.(-n*p))/2)*((exp_R.p - exp_R.-p)/2)
   +((exp_R.(n*p) - exp_R.(-n*p))/2)*(cosh.p) by Def1
  .=((exp_R.(n*p))/2)*((exp_R.p)/2) - ((exp_R.(n*p))/2)*((exp_R.-p)/2)
   +((exp_R.(-n*p))/2)*((exp_R.p)/2) - ((exp_R.(-n*p))/2)*((exp_R.-p)/2)
   +((exp_R.(n*p) - exp_R.(-n*p))/2)*((exp_R.p + exp_R.-p)/2) by Def3
  .=2*(( (exp_R.(n*p))*(exp_R.p) )/4 )
   +2*( -( ((exp_R.(-n*p))*(exp_R.-p))/(2*2) ))
  .=2*( exp_R.(n*p+p)/4 )
   +2*( -( ((exp_R.(-n*p))*(exp_R.-p))/4 )) by Th12
  .=2*( exp_R.(n*p+p)/4 )
   +2*( -( (exp_R.(-n*p+-p))/4 ) ) by Th12
  .=( exp_R.(p*(n+1))- exp_R.(-(p*(n+1))) )/2
  .=sinh.(p*(n+1)) by Def1;
   hence thesis;
   end;
      (cosh.p + sinh.p) |^ (n+1)
   =(cosh.p + sinh.p) |^ n * (cosh.p + sinh.p) by NEWTON:11
  .=(cosh.(n*p) + sinh.(n*p)) * (cosh.p + sinh.p) by A3
  .=sinh.((n+1)*p)+cosh.((n+1)*p) by A4,A5;
  hence thesis;
  end;
  hence thesis;
 end;
     for n holds X[n] from NAT_1:sch 1(A1,A2);
 hence thesis;
end;

registration
  cluster sinh -> total;
  coherence
  proof
     dom sinh=REAL by Def1;
    hence thesis by PARTFUN1:def 4;
  end;

  cluster cosh -> total;
  coherence
  proof
     dom cosh=REAL by Def3;
    hence thesis by PARTFUN1:def 4;
  end;

  cluster tanh -> total;
  coherence
  proof
     dom tanh=REAL by Def5;
    hence thesis by PARTFUN1:def 4;
  end;
end;

theorem Th30:
dom sinh=REAL & dom cosh=REAL & dom tanh=REAL by Def1,Def3,Def5;

 Lm11:for d being real number holds compreal.d=(-1)*d
 proof
   let d be real number;
       d is Real by XREAL_0:def 1;
   hence compreal.d= -d by BINOP_2:def 7
   .= (-1)*d;
 end;

 Lm12:
 dom(compreal)=REAL & rng(compreal)c=REAL by FUNCT_2:def 1;

  Lm13:
 for f being PartFunc of REAL,REAL st f = compreal holds
 for p holds f is_differentiable_in p & diff(f,p) = -1
 proof
     let f be PartFunc of REAL,REAL such that
    A1:f = compreal;
     let p;
    defpred X[set] means $1 in REAL;
    deffunc U(Real) = -$1;
    consider f1 being PartFunc of REAL,REAL such that
    A2:for p be Element of REAL holds p in dom f1 iff X[p] and
    A3:for p be Element of REAL st p in dom f1 holds f1/.p=U(p) from
PARTFUN2:sch 2;
    A4:f1 is LINEAR
     proof
         dom f1 = REAL
      proof
            for x be set st x in REAL holds x in dom f1 by A2;
        then REAL c=dom f1 by TARSKI:def 3;
       hence dom f1=REAL by XBOOLE_0:def 10;
      end;
    then A5:f1 is total by PARTFUN1:def 4;
    A6: for p be Element of REAL holds
          f1.p = - p
         proof
         let p be Element of REAL;
         A7:  p in dom f1 by A2;
         then f1/.p = -p by A3;
         hence f1.p = -p by A7,FINSEQ_4:def 4;
         end;
        for p be Real holds f1.p=(-1)*p
     proof
      let p be Real;
         f1.p = -p by A6;
      hence f1.p = (-1)*p;
      end;
    hence f1 is LINEAR by A5,FDIFF_1:def 4;
    end;
    reconsider f2 = REAL --> 0 as PartFunc of REAL,REAL by FUNCOP_1:57;
       A8: dom f2 = REAL by FUNCOP_1:19;
   A9:f2 is REST
    proof
  A10:f2 is total by PARTFUN1:def 4,A8;
      f2 is REST-like PartFunc of REAL,REAL
   proof
       for hy1 be convergent_to_0 Real_Sequence holds
          (hy1")(#)(f2*hy1) is convergent & lim ((hy1")(#)(f2*hy1)) = 0
    proof
    let hy1 be convergent_to_0 Real_Sequence;
    A11: for n holds ((hy1")(#)(f2*hy1)).n=0
      proof
      let n;
      A12: ((hy1")(#)(f2*hy1)).n
         = (hy1".n)*((f2*hy1).n) by SEQ_1:12
        .= (hy1.n)"*((f2*hy1).n) by SEQ_1:def 8;
           rng hy1 c= dom f2 by A8;
      then ((hy1")(#)(f2*hy1)).n
         =(hy1.n)"*(f2.(hy1.n)) by A12,RFUNCT_2:21
        .=(hy1.n)"*(0) by FUNCOP_1:13
        .= 0;
      hence thesis;
      end;
   then A13:(hy1")(#)(f2*hy1) is constant by SEQM_3:def 6;
     for n holds lim ((hy1")(#)(f2*hy1)) = 0
    proof
    let n;
          lim ((hy1")(#)(f2*hy1))
     =((hy1")(#)(f2*hy1)).n by A13,SEQ_4:41
    .=0 by A11;
    hence lim ((hy1")(#)(f2*hy1)) = 0;
    end;
   hence thesis by A13,SEQ_4:39;
   end;
   hence thesis by A10,FDIFF_1:def 3;
   end;
   hence thesis;
   end;
  A14: ex N being Neighbourhood of p st N c= dom compreal &
   for r be Real st r in N holds compreal.r - compreal.p = f1.(r-p) + f2.(r-p)
     proof
       A15: for r st r in ].p-1,p+1 .[ holds compreal.r - compreal.p =
          f1.(r-p) + f2.(r-p)
      proof
       let r;
         A16: for p be real number holds f1.p = - p
          proof
          let p be real number;
        p is Real by XREAL_0:def 1;
          then A17:  p in dom f1 by A2;
          then f1/.p = -p by A3;
          hence f1.p = -p by A17,FINSEQ_4:def 4;
          end;
         A18: for d be real number holds f2.d = 0
          proof
          let d be real number;
           d is Real by XREAL_0:def 1;
          hence f2.d = 0 by FUNCOP_1:13;
          end;
          compreal.r-compreal.p
        =(-1)*r - compreal.p by Lm11
       .=(-1)*r -(-1)*p + 0 by Lm11
       .=-(r-p) + f2.(r-p) by A18
       .=f1.(r-p) + f2.(r-p) by A16;
       hence thesis;
       end;
     take ].p-1,p+1 .[;
     thus thesis by A15,Lm12,RCOMP_1:def 7;
    end;
   then A19:f is_differentiable_in p by A1,A4,A9,FDIFF_1:def 5;
   A20: for p be Element of REAL holds f1.p = - p
           proof
            let p be Element of REAL;
            A21:  p in dom f1 by A2;
            then f1/.p = -p by A3;
            hence f1.p = -p by A21,FINSEQ_4:def 4;
          end;
       diff(f,p) = f1.1 by A1,A4,A9,A14,A19,FDIFF_1:def 6
             .=-1 by A20;
   hence thesis by A1,A4,A9,A14,FDIFF_1:def 5;
end;

Lm14:
 for f being PartFunc of REAL,REAL st f = compreal holds
 exp_R*f is_differentiable_in p &
 diff(exp_R*f,p) = (-1)*exp_R.(f.p)
 proof
  let f be PartFunc of REAL,REAL such that
A1: f = compreal;
A2: p is Real by XREAL_0:def 1;
 A3: exp_R is_differentiable_in f.p by SIN_COS:70;
 A4: f is_differentiable_in p by A1,Lm13;
   then diff(exp_R*f,p)
  =diff(exp_R,f.p)*diff(f,p) by A2,A3,FDIFF_2:13
 .=diff(exp_R,f.p)*(-1) by A1,Lm13
 .=exp_R.(f.p)*(-1) by SIN_COS:70;
 hence thesis by A2,A3,A4,FDIFF_2:13;
 end;

 Lm15:
for f being PartFunc of REAL,REAL st f = compreal holds
  exp_R.((-1)*p)= (exp_R*f).p
 proof
  let f be PartFunc of REAL,REAL;
   assume A1:f = compreal;
A2: p is Real by XREAL_0:def 1;
  exp_R.((-1)*p) =exp_R.(f.p) by A1,Lm11
 .=(exp_R*f).p by A1,A2,FUNCT_2:70;
 hence thesis;
 end;

 Lm16:
 for f being PartFunc of REAL,REAL st f = compreal holds
 (exp_R-(exp_R*f)) is_differentiable_in p &
 (exp_R+(exp_R*f)) is_differentiable_in p &
 diff(exp_R-(exp_R*f),p)=exp_R.p+exp_R.(-p) &
 diff(exp_R+(exp_R*f),p)=exp_R.p-exp_R.(-p)
 proof
A1: p is Real by XREAL_0:def 1;
  let f be PartFunc of REAL,REAL;
  assume
A2:  f = compreal;
  then A3: exp_R is_differentiable_in p & exp_R*f is_differentiable_in p
    by Lm14,SIN_COS:70;
  then A4:
    diff(exp_R-(exp_R*f),p)
   =diff(exp_R,p)-diff((exp_R*f),p) by A1,FDIFF_1:22
  .=exp_R.p-diff((exp_R*f),p) by SIN_COS:70
  .=exp_R.p- (-1)*exp_R.(f.p) by A2,Lm14
  .=exp_R.p- (-1)*exp_R.((-1)*p) by A2,Lm11
  .=exp_R.p+exp_R.(-p);
      diff(exp_R+(exp_R*f),p)
   =diff(exp_R,p)+diff((exp_R*f),p) by A1,A3,FDIFF_1:21
  .=exp_R.p+diff((exp_R*f),p) by SIN_COS:70
  .=exp_R.p+ (-1)*exp_R.(f.p) by A2,Lm14
  .=exp_R.p+ (-1)*exp_R.((-1)*p) by A2,Lm11
  .=exp_R.p-exp_R.(-p);
  hence thesis by A1,A3,A4,FDIFF_1:21,22;
 end;

Lm17:
 for f being PartFunc of REAL,REAL st f = compreal holds
 (1/2)(#)(exp_R-exp_R*f) is_differentiable_in p &
 diff(((1/2)(#)(exp_R-exp_R*f)),p) = (1/2)*diff((exp_R-(exp_R*f)),p)
 proof
A1: p is Real by XREAL_0:def 1;
  let f be PartFunc of REAL,REAL;
  assume f = compreal;
  then (exp_R-exp_R*f) is_differentiable_in p by Lm16;
  hence thesis by A1,FDIFF_1:23;
 end;

Lm18:
 for ff being PartFunc of REAL,REAL st ff = compreal holds
  sinh.p=((1/2)(#)(exp_R-exp_R*ff)).p
 proof
A1: p is Real by XREAL_0:def 1;
  let ff be PartFunc of REAL,REAL such that
A2:  ff = compreal;
 A3: dom (exp_R - exp_R*ff) = dom exp_R /\ dom (exp_R*ff) &
  for p be Real st p in dom (exp_R - exp_R*ff) holds
  exp_R.p - (exp_R*ff).p = (exp_R - exp_R*ff).p by SEQ_1:def 4;
 A4:dom (exp_R - exp_R*ff) = REAL
  proof
      dom (exp_R*ff) = REAL by A2,Lm12,RELAT_1:46,SIN_COS:51;
     hence thesis by A3,SIN_COS:51;
  end;
 then A5:dom((1/2)(#)(exp_R-exp_R*ff))=REAL by SEQ_1:def 6;
    sinh.p = (exp_R.p - exp_R.(-p))/2 by Def1
 .=(1/2)*(exp_R.p - exp_R.((-1)*p))
 .=(1/2)*(exp_R.p - (exp_R*ff).p) by A2,Lm15
 .=(1/2)*((exp_R - exp_R*ff).p) by A1,A4,SEQ_1:def 4
 .=((1/2)(#)(exp_R-exp_R*ff)).p by A1,A5,SEQ_1:def 6;
 hence thesis;
 end;

 Lm19:
 for ff being PartFunc of REAL,REAL st ff = compreal holds
 sinh = (1/2)(#)(exp_R-exp_R*ff)
 proof
   let ff be PartFunc of REAL,REAL such that
A1:   ff = compreal;
   A2: dom (exp_R - exp_R*ff) = dom exp_R /\ dom (exp_R*ff) &
    for p be Real st p in dom (exp_R - exp_R*ff) holds
    exp_R.p - (exp_R*ff).p = (exp_R - exp_R*ff).p by SEQ_1:def 4;
      dom (exp_R - exp_R*ff) = REAL
    proof
     dom (exp_R*ff) = REAL by A1,Lm12,RELAT_1:46,SIN_COS:51;
    hence thesis by A2,SIN_COS:51;
    end;
   then A3:REAL=dom(sinh) &
     REAL=dom((1/2)(#)(exp_R-exp_R*ff)) by Def1,SEQ_1:def 6;
      for p being Element of REAL st p in REAL holds
     sinh.p=((1/2)(#)(exp_R-exp_R*ff)).p by A1,Lm18;
      hence sinh=(1/2)(#)(exp_R-exp_R*ff) by A3,PARTFUN1:34;
 thus thesis;
 end;

 theorem Th31:
 sinh is_differentiable_in p & diff(sinh,p)=cosh.p
 proof
   reconsider ff = compreal as PartFunc of REAL,REAL;
A1:sinh = (1/2)(#)(exp_R-exp_R*ff) by Lm19;
    diff(sinh,p) =diff( ((1/2)(#)(exp_R-exp_R*ff)),p ) by Lm19
 .=(1/2)*diff((exp_R-(exp_R*ff)),p) by Lm17
 .=(1/2)*( exp_R.p+exp_R.(-p) ) by Lm16
 .=( exp_R.p+exp_R.(-p) )/2
 .=cosh.p by Def3;
 hence thesis by A1,Lm17;
 end;

 Lm20:
 for ff being PartFunc of REAL,REAL st ff = compreal holds
  (1/2)(#)(exp_R+exp_R*ff) is_differentiable_in p &
  diff(((1/2)(#)(exp_R+exp_R*ff)),p) = (1/2)*diff((exp_R+(exp_R*ff)),p)
  proof
A1: p is Real by XREAL_0:def 1;
   let ff be PartFunc of REAL,REAL; assume ff = compreal;
  then (exp_R+exp_R*ff) is_differentiable_in p by Lm16;
  hence thesis by A1,FDIFF_1:23;
  end;

  Lm21:
   for ff being PartFunc of REAL,REAL st ff = compreal holds
   cosh.p=((1/2)(#)(exp_R+exp_R*ff)).p
  proof
A1: p is Real by XREAL_0:def 1;
   let ff be PartFunc of REAL,REAL such that
A2:   ff = compreal;
A3: dom (exp_R + exp_R*ff) = dom exp_R /\ dom (exp_R*ff) &
   for p be Real st p in dom (exp_R + exp_R*ff) holds
   exp_R.p + (exp_R*ff).p = (exp_R + exp_R*ff).p by SEQ_1:def 3;
   A4:dom (exp_R + exp_R*ff) = REAL
   proof
       dom (exp_R*ff) = REAL by A2,Lm12,RELAT_1:46,SIN_COS:51;
   hence thesis by A3,SIN_COS:51;
   end;
  then A5:dom((1/2)(#)(exp_R+exp_R*ff))=REAL by SEQ_1:def 6;
    cosh.p =(exp_R.p + exp_R.(-p))/2 by Def3
  .=(1/2)*(exp_R.p + exp_R.((-1)*p))
  .=(1/2)*(exp_R.p + (exp_R*ff).p) by A2,Lm15
  .=(1/2)*((exp_R + exp_R*ff).p) by A1,A4,SEQ_1:def 3
  .=((1/2)(#)(exp_R+exp_R*ff)).p by A1,A5,SEQ_1:def 6;
  hence thesis;
  end;

  Lm22:
  for ff being PartFunc of REAL,REAL st ff = compreal holds
  cosh = (1/2)(#)(exp_R+exp_R*ff)
  proof
   let ff be PartFunc of REAL,REAL such that
   A1: ff = compreal;
   A2: dom (exp_R + exp_R*ff) = dom exp_R /\ dom (exp_R*ff) &
     for p be Real st p in dom (exp_R + exp_R*ff) holds
    exp_R.p + (exp_R*ff).p = (exp_R + exp_R*ff).p by SEQ_1:def 3;
       dom (exp_R + exp_R*ff) = REAL
     proof
      dom (exp_R*ff) = REAL by A1,Lm12,RELAT_1:46,SIN_COS:51;
     hence thesis by A2,SIN_COS:51;
     end;
   then A3:REAL=dom(cosh) & REAL=dom((1/2)(#)(exp_R+exp_R*ff))
     by Def3,SEQ_1:def 6;
       for p being Element of REAL st p in REAL holds
      cosh.p=((1/2)(#)(exp_R+exp_R*ff)).p by A1,Lm21;
    hence cosh=(1/2)(#)(exp_R+exp_R*ff) by A3,PARTFUN1:34;
    end;

theorem Th32:
  cosh is_differentiable_in p & diff(cosh,p)=sinh.p
  proof
   reconsider ff = compreal as PartFunc of REAL,REAL;
A1: cosh = (1/2)(#)(exp_R+exp_R*ff) by Lm22;
    diff(cosh,p) =diff( ((1/2)(#)(exp_R+exp_R*ff)),p ) by Lm22
  .=(1/2)*diff((exp_R+(exp_R*ff)),p) by Lm20
  .=(1/2)*( exp_R.p-exp_R.(-p) ) by Lm16
  .=( exp_R.p-exp_R.(-p) )/2
  .=sinh.p by Def1;
  hence thesis by A1,Lm20;
  end;

 Lm23:
  sinh/cosh is_differentiable_in p &
  diff(sinh/cosh,p) = 1/(cosh.p)^2
  proof
A1: p is Real by XREAL_0:def 1;
  A2:cosh.p<>0 by Th15;
  A3:sinh is_differentiable_in p by Th31;
  A4:cosh is_differentiable_in p by Th32;
    then diff(sinh/cosh,p)
   =(diff(sinh,p) * cosh.p - diff(cosh,p)*sinh.p)/(cosh.p)^2
        by A1,A2,A3,FDIFF_2:14
  .=((cosh.p)*(cosh.p) - diff(cosh,p) * sinh.p)/(cosh.p)^2 by Th31
  .=( (cosh.p)^2 - (sinh.p)*(sinh.p) )/(cosh.p)^2 by Th32
  .=1/(cosh.p)^2 by Th14;
   hence thesis by A1,A2,A3,A4,FDIFF_2:14;
  end;

Lm24: tanh=sinh/cosh
  proof
  A1: dom (sinh/cosh) = dom sinh /\ (dom cosh \ cosh"{0}) &
       for p be Real st p in dom (sinh/cosh) holds
       (sinh/cosh).p = sinh.p*(cosh.p)" by RFUNCT_1:def 4;
      not(0 in rng(cosh))
    proof
    assume 0 in rng(cosh);
    then ex p be Real st p in dom(cosh) & 0=cosh.p by PARTFUN1:26;
    hence contradiction by Th15;
    end;
 then A2: cosh"{0}={} by FUNCT_1:142;
     for p being Element of REAL st p in REAL holds
     tanh.p=(sinh/cosh).p
   proof
   let p;
    p is Real by XREAL_0:def 1;
   then (sinh/cosh).p =sinh.p*(cosh.p)" by A1,A2,Th30
   .=(sinh.p)/(cosh.p) by XCMPLX_0:def 9
   .=tanh.p by Th17;
   hence thesis;
   end;
  hence tanh=sinh/cosh by A1,A2,Th30,PARTFUN1:34;
 end;

theorem
   tanh is_differentiable_in p & diff(tanh,p)=1/(cosh.p)^2 by Lm23,Lm24;

theorem Th34:
  sinh is_differentiable_on REAL &
  diff(sinh,p)=cosh.p
  proof
      REAL is open Subset of REAL &
  REAL c= dom(sinh) &
  for p be Real st p in REAL holds sinh is_differentiable_in p
     by Def1,Th31,FCONT_3:4;
  hence thesis by Th31,FDIFF_1:16;
  end;

theorem Th35:
  cosh is_differentiable_on REAL &
  diff(cosh,p)=sinh.p
  proof
    REAL is open Subset of REAL &
  REAL c=dom cosh &
  for r be Real st r in REAL holds cosh is_differentiable_in r
  by Def3,Th32,FCONT_3:4;
  hence thesis by Th32,FDIFF_1:16;
  end;

theorem Th36:
  tanh is_differentiable_on REAL &
  diff(tanh,p)=1/(cosh.p)^2
  proof
      REAL is open Subset of REAL &
  for p be Real st p in REAL holds tanh is_differentiable_in p
     by Lm23,Lm24,FCONT_3:4;
   hence thesis by Lm23,Lm24,Th30,FDIFF_1:16;
  end;

 Lm25:
 exp_R.p + exp_R.-p >= 2
 proof
 A1:exp_R.p >= 0 by SIN_COS:59;
 A2: exp_R.-p>= 0 by SIN_COS:59;
       2*sqrt((exp_R.p) * (exp_R.-p))
  =2*sqrt(exp_R.(p+-p)) by Th12
 .=2*1 by SIN_COS:56,def 27,SQUARE_1:83;
 hence thesis by A1,A2,Th1;
 end;

theorem
   cosh.p >= 1
 proof
  (exp_R.p + exp_R.-p)>=2 by Lm25;
 then (exp_R.p + exp_R.-p)/2 >= 2/2 by REAL_1:73;
 hence thesis by Def3;
 end;

theorem
   sinh is_continuous_in p
 proof
      sinh is_differentiable_in p by Th31;
 hence thesis by FDIFF_1:32;
 end;

theorem
   cosh is_continuous_in p
 proof
     cosh is_differentiable_in p by Th32;
 hence thesis by FDIFF_1:32;
 end;

theorem
   tanh is_continuous_in p
 proof
     tanh is_differentiable_in p by Lm23,Lm24;
 hence thesis by FDIFF_1:32;
 end;

theorem
   sinh is_continuous_on REAL by Th34,FDIFF_1:33;

theorem
   cosh is_continuous_on REAL by Th35,FDIFF_1:33;

theorem
   tanh is_continuous_on REAL by Th36,FDIFF_1:33;

theorem
   tanh.p<1 & tanh.p>-1
 proof
  thus tanh.p<1
  proof
  assume tanh.p>=1;
  then A1: (exp_R.p - exp_R.(-p))/(exp_R.p + exp_R.(-p))>=1 by Def5;
      (exp_R.p + exp_R.-p) >= 2 by Lm25;
  then A2:(exp_R.p - exp_R.(-p))/(exp_R.p + exp_R.(-p))*(exp_R.p + exp_R.(-p))
                    >=1*(exp_R.p + exp_R.(-p)) by A1,XREAL_1:66;
      exp_R.p > 0 & exp_R.(-p) > 0 by SIN_COS:59;
  then exp_R.p + exp_R.(-p) > 0+0 by XREAL_1:10;
  then (exp_R.p - exp_R.(-p))/(exp_R.p + exp_R.(-p))*(exp_R.p + exp_R.(-p))
       =exp_R.p - exp_R.(-p) by XCMPLX_1:88;
  then (exp_R.p - exp_R.(-p))-exp_R.p
        >= (exp_R.p + exp_R.(-p))-exp_R.p by A2,XREAL_1:11;
  then A3: -exp_R.(-p)+exp_R.(-p) >= exp_R.(-p)+exp_R.(-p) by XREAL_1:8;
      exp_R.(-p) > 0 & exp_R.(-p) > 0 by SIN_COS:59;
  then exp_R.(-p) + exp_R.(-p) > 0+0 by XREAL_1:10;
  hence contradiction by A3;
  end;
  assume tanh.p<=-1;
  then A4: (exp_R.p - exp_R.(-p))/(exp_R.p + exp_R.(-p))<=-1 by Def5;
      (exp_R.p + exp_R.-p) >= 2 by Lm25;
  then A5:(exp_R.p - exp_R.(-p))/(exp_R.p + exp_R.(-p))*(exp_R.p + exp_R.(-p))
                    <=(-1)*(exp_R.p + exp_R.(-p)) by A4,XREAL_1:66;
      exp_R.p > 0 & exp_R.(-p) > 0 by SIN_COS:59;
  then exp_R.p + exp_R.(-p) > 0+0 by XREAL_1:10;
  then exp_R.p - exp_R.(-p)<=-(exp_R.p + exp_R.(-p)) by A5,XCMPLX_1:88;
  then exp_R.p - exp_R.(-p)+exp_R.(-p)
           <=-exp_R.p + -exp_R.(-p)+exp_R.(-p) by XREAL_1:8;
  then A6: exp_R.p+exp_R.p<=-exp_R.p+exp_R.p by XREAL_1:8;
      exp_R.p > 0 & exp_R.p > 0 by SIN_COS:59;
  then exp_R.p + exp_R.p > 0+0 by XREAL_1:10;
  hence contradiction by A6;
  end;
