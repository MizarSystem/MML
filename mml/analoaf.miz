:: Analytical Ordered Affine Spaces
::  by Henryk Oryszczyszyn and Krzysztof Pra\.zmowski
::
:: Received April 11, 1990
:: Copyright (c) 1990 Association of Mizar Users
 
environ

 vocabularies NUMBERS, RLVECT_1, REAL_1, CARD_1, ARYTM_3, RELAT_1, ARYTM_1,
      SUPINF_2, STRUCT_0, ZFMISC_1, XBOOLE_0, SUBSET_1, ANALOAF;
 notations TARSKI, XBOOLE_0, ZFMISC_1, DOMAIN_1, XXREAL_0, REAL_1, RELSET_1,
      NUMBERS, STRUCT_0, RLVECT_1;
 constructors XXREAL_0, REAL_1, MEMBERED, REALSET2;
 registrations SUBSET_1, RELSET_1, XXREAL_0, MEMBERED, STRUCT_0, XBOOLE_0;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions STRUCT_0, RLVECT_1;
 theorems RLVECT_1, ZFMISC_1, RELAT_1, FUNCSDOM, RLSUB_2, XCMPLX_0, XCMPLX_1,
      XREAL_1, STRUCT_0;
 schemes RELSET_1;

begin

reserve V for RealLinearSpace;
reserve p,q,u,v,w,y for VECTOR of V;
reserve a,b,c,d for Real;

definition
  let V;
  let u,v,w,y;
  pred u,v // w,y means
  :Def1:
  u=v or w=y or ex a,b st 0<a & 0<b & a*(v-u)=b*( y-w);
end;

canceled 3;

theorem Th4:
  (w-v)+(v-u) = w-u
proof
  thus (w-v)+(v-u) = w-(v-(v-u)) by RLVECT_1:43
    .= w-((v-v)+u) by RLVECT_1:43
    .= w-(0.V+u) by RLVECT_1:28
    .= w-u by RLVECT_1:10;
end;

canceled 4;

theorem Th9:
  y+u = v+w implies y-w = v-u
proof
  assume
A1: y+u=v+w;
  thus y-w = (y+0.V)-w by RLVECT_1:10
    .= (y+(u-u))-w by RLVECT_1:28
    .=((v+w)+(-u))-w by A1,RLVECT_1:def 6
    .= (-u)+((v+w)-w) by RLVECT_1:def 6
    .= v-u by RLSUB_2:78;
end;

theorem Th10:
  a*(u-v) = -(a*(v-u))
proof
  a*(v-u) + a*(u-v) = a*(v-u) + a*(-(v-u)) by RLVECT_1:47
    .= a*(v-u)-a*(v-u) by RLVECT_1:39
    .= 0.V by RLVECT_1:28;
  hence thesis by RLVECT_1:def 13;
end;

theorem Th11:
  (a-b)*(u-v) = (b-a)*(v-u)
proof
  thus (a-b)*(u-v)=(-(b-a))*(-(v-u)) by RLVECT_1:47
    .=(b-a)*(v-u) by RLVECT_1:40;
end;

theorem Th12:
  a<>0 & a*u=v implies u=a"*v
proof
  assume that
A1: a<>0 and
A2: a*u=v;
  thus u=1*u by RLVECT_1:def 11
    .=(a"*a)*u by A1,XCMPLX_0:def 7
    .=a"*v by A2,RLVECT_1:def 10;
end;

theorem Th13:
  (a<>0 & a*u=v implies u=a"*v) & (a<>0 & u=a"*v implies a*u=v)
proof
  now
    assume a<>0 & u=a"*v;
    hence v=(a")"*u by Th12,XCMPLX_1:203
      .=a*u;
  end;
  hence thesis by Th12;
end;

canceled 2;

theorem Th16:
  u,v // w,y & u<>v & w<>y implies ex a,b st a*(v-u)=b*(y-w) & 0<a & 0<b
proof
  assume that
A1: u,v // w,y and
A2: u<>v & w<>y;
  u=v or w=y or ex a,b st 0<a & 0<b & a*(v-u)=b*(y-w) by A1,Def1;
  hence thesis by A2;
end;

theorem Th17:
  u,v // u,v
proof
  1*(v-u)=1*(v-u);
  hence thesis by Def1;
end;

theorem
  u,v // w,w & u,u // v,w by Def1;

theorem Th19:
  u,v // v,u implies u=v
proof
  assume
A1: u,v // v,u;
  assume
A2: u<>v;
  then consider a,b such that
A3: a*(v-u)=b*(u-v) and
A4: 0<a & 0<b by A1,Th16;
  a*(v-u)=-b*(v-u) by A3,Th10;
  then b*(v-u)+a*(v-u)=0.V by RLVECT_1:16;
  then (b+a)*(v-u)=0.V by RLVECT_1:def 9;
  then v-u=0.V or b+a=0 by RLVECT_1:24;
  then 0.V=(-u)+v by A4,XREAL_1:36;
  then v=-(-u) by RLVECT_1:def 13
    .=u by RLVECT_1:30;
  hence contradiction by A2;
end;

theorem Th20:
  p<>q & p,q // u,v & p,q // w,y implies u,v // w,y
proof
  assume that
A1: p<>q and
A2: p,q // u,v and
A3: p,q // w,y;
  now
    assume that
A4: u<>v and
A5: w<>y;
    consider a,b such that
A6: a*(q-p)=b*(v-u) and
A7: 0<a and
A8: 0<b by A1,A2,A4,Th16;
    0<a" by A7,XREAL_1:124;
    then
A9: 0<a"*b by A8,XREAL_1:131;
    consider c,d such that
A10: c*(q-p)=d*(y-w) and
A11: 0<c and
A12: 0<d by A1,A3,A5,Th16;
A13: q-p=(c")*(d*(y-w)) by A10,A11,Th13
      .=(c"*d)*(y-w) by RLVECT_1:def 10;
    0<c" by A11,XREAL_1:124;
    then
A14: 0<c"*d by A12,XREAL_1:131;
    q-p=(a")*(b*(v-u)) by A6,A7,Th13
      .=(a"*b)*(v-u) by RLVECT_1:def 10;
    hence thesis by A13,A9,A14,Def1;
  end;
  hence thesis by Def1;
end;

theorem Th21:
  u,v // w,y implies v,u // y,w & w,y // u,v
proof
  assume
A1: u,v // w,y;
  now
    assume u<>v & w<>y;
    then consider a,b such that
A2: a*(v-u)=b*(y-w) and
A3: 0<a & 0<b by A1,Th16;
    a*(u-v)=-b*(y-w) by A2,Th10
      .=b*(w-y) by Th10;
    hence thesis by A2,A3,Def1;
  end;
  hence thesis by Def1;
end;

theorem Th22:
  u,v // v,w implies u,v // u,w
proof
  assume
A1: u,v // v,w;
  now
    assume u<>v & v<>w;
    then consider a,b such that
A2: a*(v-u)=b*(w-v) and
A3: 0<a and
A4: 0<b by A1,Th16;
A5: 0<a+b by A3,A4,XREAL_1:36;
    b*(w-u)=b*((w-v)+(v-u)) by Th4
      .=a*(v-u)+b*(v-u) by A2,RLVECT_1:def 8
      .=(a+b)*(v-u) by RLVECT_1:def 9;
    hence thesis by A4,A5,Def1;
  end;
  hence thesis by Def1,Th17;
end;

theorem Th23:
  u,v // u,w implies u,v // v,w or u,w // w,v
proof
  assume
A1: u,v // u,w;
  now
    assume u<>v & u<>w;
    then consider a,b such that
A2: a*(v-u)=b*(w-u) and
A3: 0<a and
A4: 0<b by A1,Th16;
    w-v=(w-u)+(u-v) by Th4
      .=(w-u)-(v-u) by RLVECT_1:47;
    then
A5: a*(w-v)=a*(w-u)-b*(w-u) by A2,RLVECT_1:48
      .=(a-b)*(w-u) by RLVECT_1:49
      .=(b-a)*(u-w) by Th11;
    v-w=(v-u)+(u-w) by Th4
      .=(v-u)-(w-u) by RLVECT_1:47;
    then
A6: b*(v-w)=b*(v-u)-a*(v-u) by A2,RLVECT_1:48
      .=(b-a)*(v-u) by RLVECT_1:49
      .=(a-b)*(u-v) by Th11;
A7: now
      assume a<>b;
      then 0<a-b or 0<b-a by XREAL_1:57;
      then v,u // w,v or w,u // v,w by A3,A4,A6,A5,Def1;
      hence thesis by Th21;
    end;
    now
      assume a=b;
      then (-u)+v= (-u)+w by A2,A3,RLVECT_1:50;
      then v=w by RLVECT_1:21;
      hence thesis by Def1;
    end;
    hence thesis by A7;
  end;
  hence thesis by Def1;
end;

theorem Th24:
  v-u=y-w implies u,v // w,y
proof
  assume v-u=y-w;
  then 1*(v-u)=1*(y-w);
  hence thesis by Def1;
end;

theorem Th25:
  y=(v+w)-u implies u,v // w,y & u,w // v,y
proof
  set y=(v+w)-u;
  y+u=v+w by RLSUB_2:78;
  then y-v=w-u & y-w=v-u by Th9;
  hence thesis by Th24;
end;

theorem Th26:
  (ex p,q st p<>q) implies for u,v,w ex y st u,v // w,y & u,w // v ,y & v<>y
proof
  given p,q such that
A1: p<>q;
  let u,v,w;
A2: now
    assume
A3: u<>w;
    take y=(v+w)-u;
A4: now
      assume v=y;
      then v=v+(w-u) by RLVECT_1:def 6;
      then w-u=0.V by RLVECT_1:22;
      hence contradiction by A3,RLVECT_1:35;
    end;
    u,v // w,y & u,w // v,y by Th25;
    hence thesis by A4;
  end;
  now
    assume
A5: u=w;
A6: now
      assume u=v;
      then
A7:   u,v // w,p & u,v // w,q by Def1;
A8:   v<>p or v<>q by A1;
      u,w // v,p & u,w // v,q by A5,Def1;
      hence thesis by A8,A7;
    end;
    u,v // w,u & u,w // v,u by A5,Def1;
    hence thesis by A6;
  end;
  hence thesis by A2;
end;

theorem Th27:
  p<>v & v,p // p,w implies ex y st u,p // p,y & u,v // w,y
proof
  assume
A1: p<>v & v,p // p,w;
A2: now
    assume p<>w;
    then consider a,b such that
A3: a*(p-v)=b*(w-p) and
A4: 0<a and
A5: 0<b by A1,Th16;
    set y=(b"*a)*(p-u)+p;
A6: y-p=(b"*a)*(p-u) by RLSUB_2:78
      .=b"*(a*(p-u)) by RLVECT_1:def 10;
A7: y-w=(y-p)+(p-w) by Th4
      .=(y-p)-(w-p) by RLVECT_1:47;
    v-u=(p-u)+(v-p) by Th4
      .=(p-u)-(p-v) by RLVECT_1:47;
    then a*(v-u)=a*(p-u)-a*(p-v) by RLVECT_1:48
      .=b*(y-p)-b*(w-p) by A3,A5,A6,Th13
      .=b*(y-w) by A7,RLVECT_1:48;
    then
A8: u,v // w,y by A4,A5,Def1;
    0<b" by A5,XREAL_1:124;
    then
A9: 0<b"*a by A4,XREAL_1:131;
    1*(y-p)=y-p by RLVECT_1:def 11
      .=(b"*a)*(p-u) by RLSUB_2:78;
    then u,p // p,y by A9,Def1;
    hence thesis by A8;
  end;
  now
    assume
A10: p=w;
    take y=p;
    thus u,p // p,y & u,v // w,y by A10,Def1;
  end;
  hence thesis by A2;
end;

theorem Th28:
  (for a,b st a*u + b*v=0.V holds a=0 & b=0) implies u<>v & u<>0.V & v<>0.V
proof
  assume
A1: for a,b st a*u + b*v=0.V holds a=0 & b=0;
  thus u<>v
  proof
    assume u=v;
    then u - v = 0.V by RLVECT_1:28;
    then 1*u + (-v) = 0.V by RLVECT_1:def 11;
    then 1*u + ((-1)*v) = 0.V by RLVECT_1:29;
    hence contradiction by A1;
  end;
  thus u<>0.V
  proof
    assume u=0.V;
    then 1*u = 0.V by RLVECT_1:23;
    then 1*u + 0.V = 0.V by RLVECT_1:10;
    then 1*u + 0*v =0.V by RLVECT_1:23;
    hence contradiction by A1;
  end;
  thus v<>0.V
  proof
    assume v=0.V;
    then 1*v = 0.V by RLVECT_1:23;
    then 0.V + 1*v = 0.V by RLVECT_1:10;
    then 0*u + 1*v =0.V by RLVECT_1:23;
    hence contradiction by A1;
  end;
end;

theorem Th29:
  (ex u,v st (for a,b st a*u + b*v=0.V holds a=0 & b=0)) implies
  ex u,v,w,y st not u,v // w,y & not u,v // y,w
proof
  given u,v such that
A1: for a,b st a*u + b*v=0.V holds a=0 & b=0;
A2: u<>0.V & v<>0.V by A1,Th28;
A3: not 0.V,u // v,0.V
  proof
A4: now
      given a,b such that
A5:   0<a and
      0<b and
A6:   a*(u-0.V) = b*(0.V-v);
      a*u = a*(u-0.V) & b*(0.V-v)=b*(-v) by RLVECT_1:26,27;
      then a*u = -(b*v) by A6,RLVECT_1:39;
      then a*u + b*v = 0.V by RLVECT_1:16;
      hence contradiction by A1,A5;
    end;
    assume 0.V,u // v,0.V;
    hence contradiction by A2,A4,Def1;
  end;
  not 0.V,u // 0.V,v
  proof
A7: now
      given a,b such that
A8:   0<a and
      0<b and
A9:   a*(u-0.V) = b*(v-0.V);
      a*u = a*(u-0.V) & b*(v-0.V)=b*v by RLVECT_1:26;
      then 0.V = a*u - (b*v) by A9,RLVECT_1:28
        .= a*u + (b*(-v)) by RLVECT_1:39
        .= a*u + ((-b)*v) by RLVECT_1:38;
      hence contradiction by A1,A8;
    end;
    assume 0.V,u // 0.V,v;
    hence contradiction by A2,A7,Def1;
  end;
  hence thesis by A3;
end;

Lm1: a*(v-u) = b*(w-y) & (a<>0 or b<>0) implies u,v // w,y or u,v // y,w
proof
  assume that
A1: a*(v-u) = b*(w-y) and
A2: a<>0 or b<>0;
A3: now
    assume
A4: b=0;
    then 0.V = a*(v-u) by A1,RLVECT_1:23;
    then v-u = 0.V by A2,A4,RLVECT_1:24;
    then u=v by RLVECT_1:35;
    hence u,v // w,y by Def1;
  end;
A5: now
A6: now
A7:   a*(v-u) = -(-(b*(w-y))) by A1,RLVECT_1:30
        .= -(b*(-(w-y))) by RLVECT_1:39
        .= -(b*(y-w)) by RLVECT_1:47
        .= b*(-(y-w)) by RLVECT_1:39
        .= (-b)*(y-w) by RLVECT_1:38;
      assume that
A8:   0<a and
A9:   b<0;
      0<-b by A9,XREAL_1:60;
      hence u,v // w,y by A8,A7,Def1;
    end;
A10: now
A11:  (-a)*(v-u) = a*(-(v-u)) by RLVECT_1:38
        .= -(b*(w-y)) by A1,RLVECT_1:39
        .=b*(-(w-y)) by RLVECT_1:39
        .= b*(y-w) by RLVECT_1:47;
      assume that
A12:  a<0 and
A13:  0<b;
      0<-a by A12,XREAL_1:60;
      hence u,v // w,y by A13,A11,Def1;
    end;
A14: now
      assume a<0 & b<0;
      then
A15:  0<-a & 0<-b by XREAL_1:60;
      (-a)*(v-u) = a*(-(v-u)) by RLVECT_1:38
        .= -(b*(w-y)) by A1,RLVECT_1:39
        .=b*(-(w-y)) by RLVECT_1:39
        .= (-b)*(w-y) by RLVECT_1:38;
      hence u,v // y,w by A15,Def1;
    end;
    assume a<>0 & b<>0;
    hence thesis by A1,A14,A10,A6,Def1;
  end;
  now
    assume
A16: a=0;
    then 0.V = b*(w-y) by A1,RLVECT_1:23;
    then w-y = 0.V by A2,A16,RLVECT_1:24;
    then w=y by RLVECT_1:35;
    hence u,v // w,y by Def1;
  end;
  hence thesis by A3,A5;
end;

canceled;

theorem Th31:
  (ex p,q st (for w ex a,b st a*p + b*q=w)) implies for u,v,w,y st
not u,v // w,y & not u,v // y,w ex z being VECTOR of V st (u,v // u,z or u,v //
  z,u) & (w,y // w,z or w,y // z,w)
proof
  given p,q such that
A1: for w ex a,b st a*p + b*q=w;
  let u,v,w,y such that
A2: not u,v // w,y and
A3: not u,v // y,w;
  consider r1,s1 being Real such that
A4: r1*p + s1*q = v-u by A1;
  consider r2,s2 being Real such that
A5: r2*p + s2*q = y-w by A1;
  set r = r1*s2 - r2*s1;
A6: now
    assume
A7: r = 0;
A8: now
      assume that
A9:   r1<>0 and
A10:  r2=0;
      s2<>0
      proof
        assume s2=0;
        then y-w = 0.V + 0*q by A5,A10,RLVECT_1:23
          .= 0.V + 0.V by RLVECT_1:23
          .= 0.V by RLVECT_1:10;
        then y=w by RLVECT_1:35;
        hence contradiction by A2,Def1;
      end;
      hence contradiction by A7,A9,A10,XCMPLX_1:6;
    end;
A11: now
      assume
A12:  r1=0;
A13:  s1<>0
      proof
        assume s1=0;
        then v-u = 0.V + 0*q by A4,A12,RLVECT_1:23
          .= 0.V + 0.V by RLVECT_1:23
          .= 0.V by RLVECT_1:10;
        then u=v by RLVECT_1:35;
        hence contradiction by A2,Def1;
      end;
      then
A14:  r2=0 by A7,A12,XCMPLX_1:6;
A15:  s2<>0
      proof
        assume s2=0;
        then y-w = 0.V + 0*q by A5,A14,RLVECT_1:23
          .= 0.V + 0.V by RLVECT_1:23
          .= 0.V by RLVECT_1:10;
        then y=w by RLVECT_1:35;
        hence contradiction by A2,Def1;
      end;
      y-w = 0.V + s2*q by A5,A14,RLVECT_1:23
        .= s2*q by RLVECT_1:10;
      then
A16:  (s2)"*(y-w) = ((s2)"*s2)*q by RLVECT_1:def 10
        .= 1*q by A15,XCMPLX_0:def 7
        .= q by RLVECT_1:def 11;
      v-u = 0.V + s1*q by A4,A12,RLVECT_1:23
        .= s1*q by RLVECT_1:10;
      then
A17:  (s1)"*(v-u) = ((s1)"*s1)*q by RLVECT_1:def 10
        .= 1*q by A13,XCMPLX_0:def 7
        .= q by RLVECT_1:def 11;
      s1"<>0 by A13,XCMPLX_1:203;
      hence contradiction by A2,A3,A17,A16,Lm1;
    end;
A18: now
      assume that
A19:  r1<>0 and
A20:  r2<>0 and
A21:  s1 = 0;
      v-u = r1*p + 0.V by A4,A21,RLVECT_1:23
        .= r1*p by RLVECT_1:10;
      then
A22:  (r1)"*(v-u) = ((r1)"*r1)*p by RLVECT_1:def 10
        .= 1*p by A19,XCMPLX_0:def 7
        .= p by RLVECT_1:def 11;
      s2 = 0 by A7,A19,A21,XCMPLX_1:6;
      then y-w = r2*p + 0.V by A5,RLVECT_1:23
        .= r2*p by RLVECT_1:10;
      then
A23:  (r2)"*(y-w) = ((r2)"*r2)*p by RLVECT_1:def 10
        .= 1*p by A20,XCMPLX_0:def 7
        .= p by RLVECT_1:def 11;
      r1"<>0 by A19,XCMPLX_1:203;
      hence contradiction by A2,A3,A22,A23,Lm1;
    end;
    now
      assume that
A24:  r1<>0 and
      r2<>0 and
      s1<>0 and
      s2<>0;
      r2*(v-u) = r2*(r1*p) + r2*(s1*q) by A4,RLVECT_1:def 8
        .=(r2*r1)*p + r2*(s1*q) by RLVECT_1:def 10
        .= (r1*r2)*p + (r1*s2)*q by A7,RLVECT_1:def 10
        .= r1*(r2*p) + (r1*s2)*q by RLVECT_1:def 10
        .= r1*(r2*p) + r1*(s2*q) by RLVECT_1:def 10
        .= r1*(y-w) by A5,RLVECT_1:def 8;
      hence contradiction by A2,A3,A24,Lm1;
    end;
    hence contradiction by A7,A11,A8,A18,XCMPLX_1:6;
  end;
  consider r3,s3 being Real such that
A25: r3*p + s3*q = u-w by A1;
  set a= r2*s3 - r3*s2, b= r1*s3 - r3*s1;
A26: b*r2 = r1*a + r3*r;
  set z = u + (r"*a)*(v-u);
A27: r*(z-u) = r*z - r*u by RLVECT_1:48
    .= r*u + r*((r"*a)*(v-u)) - r*u by RLVECT_1:def 8
    .= r*u + (r*(r"*a))*(v-u) - r*u by RLVECT_1:def 10
    .= r*u + ((r*r")*a)*(v-u) - r*u
    .= r*u + (1*a)*(v-u) - r*u by A6,XCMPLX_0:def 7
    .= a*(v-u) + (r*u - r*u) by RLVECT_1:def 6
    .= a*(v-u) + 0.V by RLVECT_1:28
    .= a*(v-u) by RLVECT_1:10;
A28: r*(z-w) = r*z - r*w by RLVECT_1:48
    .= r*u + r*((r"*a)*(v-u)) - r*w by RLVECT_1:def 8
    .= r*u + (r*(r"*a))*(v-u) - r*w by RLVECT_1:def 10
    .= r*u + ((r*r")*a)*(v-u) - r*w
    .= r*u + (1*a)*(v-u) - r*w by A6,XCMPLX_0:def 7
    .= a*(v-u) + (r*u - r*w) by RLVECT_1:def 6
    .= a*(r1*p + s1*q) + r*(r3*p + s3*q) by A4,A25,RLVECT_1:48
    .= a*(r1*p) + a*(s1*q) + r*(r3*p + s3*q) by RLVECT_1:def 8
    .= a*(r1*p) + a*(s1*q) + (r*(r3*p) + r*(s3*q)) by RLVECT_1:def 8
    .= (a*r1)*p + a*(s1*q) + (r*(r3*p) + r*(s3*q)) by RLVECT_1:def 10
    .= (a*r1)*p + (a*s1)*q + (r*(r3*p) + r*(s3*q)) by RLVECT_1:def 10
    .= (a*r1)*p + (a*s1)*q + ((r*r3)*p + r*(s3*q)) by RLVECT_1:def 10
    .= (a*r1)*p + (a*s1)*q + ((r*s3)*q + (r*r3)*p) by RLVECT_1:def 10
    .= (a*r1)*p + (a*s1)*q + (r*s3)*q + (r*r3)*p by RLVECT_1:def 6
    .= ((a*s1)*q + (r*s3)*q) + (a*r1)*p + (r*r3)*p by RLVECT_1:def 6
    .= ((a*s1)*q + (r*s3)*q) + ((a*r1)*p + (r*r3)*p) by RLVECT_1:def 6
    .= (a*s1 + r*s3)*q + ((a*r1)*p + (r*r3)*p) by RLVECT_1:def 9
    .= (b*s2)*q + (b*r2)*p by A26,RLVECT_1:def 9
    .= b*(s2*q) + (b*r2)*p by RLVECT_1:def 10
    .= b*(s2*q) + b*(r2*p) by RLVECT_1:def 10
    .= b*(y-w) by A5,RLVECT_1:def 8;
A29: now
    assume that
A30: a=0 and
A31: b<>0;
    r*(z-u)=0.V by A27,A30,RLVECT_1:23;
    then z-u=0.V by A6,RLVECT_1:24;
    then z=u by RLVECT_1:35;
    then
A32: u,v // u,z by Def1;
    w,y // w,z or w,y // z,w by A28,A31,Lm1;
    hence thesis by A32;
  end;
A33: b*s2 = s1*a + s3*r;
A34: now
    assume a=0 & b=0;
    then r3=0 & s3=0 by A6,A26,A33,XCMPLX_1:6;
    then 0.V + 0*q = u-w by A25,RLVECT_1:23;
    then 0.V + 0.V = u-w by RLVECT_1:23;
    then 0.V=u-w by RLVECT_1:10;
    then u=w by RLVECT_1:35;
    then
A35: w,y // w,u by Def1;
    u,v // u,u by Def1;
    hence thesis by A35;
  end;
A36: now
    assume that
A37: a<>0 and
A38: b=0;
    r*(z-w)=0.V by A28,A38,RLVECT_1:23;
    then z-w=0.V by A6,RLVECT_1:24;
    then z=w by RLVECT_1:35;
    then
A39: w,y // w,z by Def1;
    u,v // u,z or u,v // z,u by A27,A37,Lm1;
    hence thesis by A39;
  end;
  now
    assume that
A40: a<>0 and
A41: b<>0;
A42: w,y // w,z or w,y // z,w by A28,A41,Lm1;
    u,v // u,z or u,v // z,u by A27,A40,Lm1;
    hence thesis by A42;
  end;
  hence thesis by A34,A29,A36;
end;

definition
  struct(1-sorted) AffinStruct (#carrier -> set, CONGR -> Relation of [:the
    carrier,the carrier:]#);
end;

registration
  cluster non empty strict AffinStruct;
  existence
  proof
    consider A being non empty set, R be Relation of [:A,A:];
    take AffinStruct(#A,R#);
    thus the carrier of AffinStruct(#A,R#) is non empty;
    thus thesis;
  end;
end;

reserve AS for non empty AffinStruct;
reserve a,b,c,d for Element of AS;
reserve x,z for set;

definition
  let AS,a,b,c,d;
  pred a,b // c,d means
  :Def2:
  [[a,b],[c,d]] in the CONGR of AS;
end;

definition
  let V;
  func DirPar(V) -> Relation of [:the carrier of V,the carrier of V:] means
  :
  Def3: [x,z] in it iff ex u,v,w,y st x=[u,v] & z=[w,y] & u,v // w,y;
  existence
  proof
    defpred P[set,set] means ex u,v,w,y st $1=[u,v] & $2=[w,y] & u,v // w,y;
    set VV = [:the carrier of V,the carrier of V:];
    consider P being Relation of VV,VV such that
A1: [x,z] in P iff x in VV & z in VV & P[x,z] from RELSET_1:sch 1;
    take P;
    let x,z;
    thus [x,z] in P implies ex u,v,w,y st x=[u,v] & z=[w,y] & u,v // w,y by A1;
    assume ex u,v,w,y st x=[u,v] & z=[w,y] & u,v // w,y;
    hence thesis by A1;
  end;
  uniqueness
  proof
    let P,Q be Relation of [:the carrier of V,the carrier of V:] such that
A2: [x,z] in P iff ex u,v,w,y st x=[u,v] & z=[w,y] & u,v // w,y and
A3: [x,z] in Q iff ex u,v,w,y st x=[u,v] & z=[w,y] & u,v // w,y;
    for x,z holds [x,z] in P iff [x,z] in Q
    proof
      let x,z;
      [x,z] in P iff ex u,v,w,y st x=[u,v] & z=[w,y] & u,v // w,y by A2;
      hence thesis by A3;
    end;
    hence thesis by RELAT_1:def 2;
  end;
end;

canceled;

theorem Th33:
  [[u,v],[w,y]] in DirPar(V) iff u,v // w,y
proof
  thus [[u,v],[w,y]] in DirPar(V) implies u,v // w,y
  proof
    assume [[u,v],[w,y]] in DirPar(V);
    then consider u9,v9,w9,y9 being VECTOR of V such that
A1: [u,v]=[u9,v9] and
A2: [w,y]=[w9,y9] and
A3: u9,v9 // w9,y9 by Def3;
A4: w = w9 by A2,ZFMISC_1:33;
    u = u9 & v = v9 by A1,ZFMISC_1:33;
    hence thesis by A2,A3,A4,ZFMISC_1:33;
  end;
  thus thesis by Def3;
end;

definition
  let V;
  func OASpace(V) -> strict AffinStruct equals
  AffinStruct (#the carrier of V,
    DirPar(V)#);
  correctness;
end;

registration
  let V;
  cluster OASpace V -> non empty;
  coherence;
end;

canceled;

theorem Th35:
  (ex u,v st for a,b being Real st a*u + b*v = 0.V holds a=0 & b=0
  ) implies (ex a,b being Element of OASpace(V) st a<>b) & (for a,b,c,d,p,q,r,s
being Element of OASpace(V) holds a,b // c,c & (a,b // b,a implies a=b) & (a<>b
& a,b // p,q & a,b // r,s implies p,q // r,s) & (a,b // c,d implies b,a // d,c)
& (a,b // b,c implies a,b // a,c) & (a,b // a,c implies a,b // b,c or a,c // c,
b)) & (ex a,b,c,d being Element of OASpace(V) st not a,b // c,d & not a,b // d,
c) & (for a,b,c being Element of OASpace(V) ex d being Element of OASpace(V) st
a,b // c,d & a,c // b,d & b<>d) & for p,a,b,c being Element of OASpace(V) st p
  <>b & b,p // p,c ex d being Element of OASpace(V) st a,p // p,d & a,b // c,d
proof
  given u,v such that
A1: for a,b being Real st a*u + b*v = 0.V holds a=0 & b=0;
  set S = OASpace(V);
A2: u<>v by A1,Th28;
  hence ex a,b being Element of S st a<>b;
  thus for a,b,c,d,p,q,r,s being Element of S holds a,b // c,c & (a,b // b,a
implies a=b) & (a<>b & a,b // p,q & a,b // r,s implies p,q // r,s) & (a,b // c,
d implies b,a // d,c) & (a,b // b,c implies a,b // a,c) & (a,b // a,c implies a
  ,b // b,c or a,c // c,b)
  proof
    let a,b,c,d,p,q,r,s be Element of S;
    reconsider a9=a,b9=b,c9=c,d9=d,p9=p,q9=q,r9=r,s9=s as Element of V;
    a9,b9 // c9,c9 by Def1;
    hence [[a,b],[c,c]] in the CONGR of S by Def3;
    thus a,b // b,a implies a=b
    proof
      assume [[a,b],[b,a]] in the CONGR of S;
      then a9,b9 // b9,a9 by Th33;
      hence thesis by Th19;
    end;
    thus a<>b & a,b // p,q & a,b // r,s implies p,q // r,s
    proof
      assume that
A3:   a<>b and
A4:   [[a,b],[p,q]] in the CONGR of S & [[a,b],[r,s]] in the CONGR of S;
      a9,b9 // p9,q9 & a9,b9 // r9,s9 by A4,Th33;
      then p9,q9 // r9,s9 by A3,Th20;
      then [[p,q],[r,s]] in the CONGR of S by Th33;
      hence thesis by Def2;
    end;
    thus a,b // c,d implies b,a // d,c
    proof
      assume [[a,b],[c,d]] in the CONGR of S;
      then a9,b9 // c9,d9 by Th33;
      then b9,a9 // d9,c9 by Th21;
      then [[b,a],[d,c]] in the CONGR of S by Th33;
      hence thesis by Def2;
    end;
    thus a,b // b,c implies a,b // a,c
    proof
      assume [[a,b],[b,c]] in the CONGR of S;
      then a9,b9 // b9,c9 by Th33;
      then a9,b9 // a9,c9 by Th22;
      then [[a,b],[a,c]] in the CONGR of S by Th33;
      hence thesis by Def2;
    end;
    thus a,b // a,c implies a,b // b,c or a,c // c,b
    proof
      assume [[a,b],[a,c]] in the CONGR of S;
      then a9,b9 // a9,c9 by Th33;
      then a9,b9 // b9,c9 or a9,c9 // c9,b9 by Th23;
      then [[a,b],[b,c]] in the CONGR of S or [[a,c],[c,b]] in the CONGR of S
      by Th33;
      hence thesis by Def2;
    end;
  end;
  thus ex a,b,c,d being Element of S st not a,b // c,d & not a,b // d,c
  proof
    consider a9,b9,c9,d9 being VECTOR of V such that
A5: not a9,b9 // c9,d9 and
A6: not a9,b9 // d9,c9 by A1,Th29;
    reconsider a=a9,b=b9,c = c9,d=d9 as Element of S;
    not [[a,b],[d,c]] in the CONGR of S by A6,Th33;
    then
A7: not a,b // d,c by Def2;
    not [[a,b],[c,d]] in the CONGR of S by A5,Th33;
    then not a,b // c,d by Def2;
    hence thesis by A7;
  end;
  thus for a,b,c being Element of S ex d being Element of S st a,b // c,d & a,
  c // b,d & b<>d
  proof
    let a,b,c be Element of S;
    reconsider a9=a,b9=b,c9=c as Element of V;
    consider d9 being VECTOR of V such that
A8: a9,b9 // c9,d9 and
A9: a9,c9 // b9,d9 and
A10: b9<>d9 by A2,Th26;
    reconsider d=d9 as Element of S;
    [[a,c],[b,d]] in the CONGR of S by A9,Th33;
    then
A11: a,c // b,d by Def2;
    [[a,b],[c,d]] in the CONGR of S by A8,Th33;
    then a,b // c,d by Def2;
    hence thesis by A10,A11;
  end;
  thus for p,a,b,c being Element of S st p<>b & b,p // p,c holds ex d being
  Element of S st a,p // p,d & a,b // c,d
  proof
    let p,a,b,c be Element of S;
    assume that
A12: p<>b and
A13: [[b,p],[p,c]] in the CONGR of S;
    reconsider p9=p,a9=a,b9=b,c9=c as Element of V;
    b9,p9 // p9,c9 by A13,Th33;
    then consider d9 being VECTOR of V such that
A14: a9,p9 // p9,d9 and
A15: a9,b9 // c9,d9 by A12,Th27;
    reconsider d=d9 as Element of S;
    [[a,b],[c,d]] in the CONGR of S by A15,Th33;
    then
A16: a,b // c,d by Def2;
    [[a,p],[p,d]] in the CONGR of S by A14,Th33;
    then a,p // p,d by Def2;
    hence thesis by A16;
  end;
end;

theorem Th36:
  (ex p,q being VECTOR of V st (for w being VECTOR of V ex a,b
being Real st a*p + b*q=w)) implies for a,b,c,d being Element of OASpace(V) st
not a,b // c,d & not a,b // d,c ex t being Element of OASpace(V) st (a,b // a,t
  or a,b // t,a) & (c,d // c,t or c,d // t,c)
proof
  assume
A1: ex p,q being VECTOR of V st for w being VECTOR of V ex a,b being
  Real st a*p + b*q=w;
  set S = OASpace(V);
  let a,b,c,d be Element of OASpace(V);
  reconsider a9=a,b9=b,c9 = c,d9=d as Element of V;
  assume
  ( not [[a,b],[c,d]] in the CONGR of S)& not [[a,b],[d,c]] in the CONGR of S;
  then ( not a9,b9 // c9,d9)& not a9,b9 // d9,c9 by Th33;
  then consider t9 being VECTOR of V such that
A2: a9,b9 // a9,t9 or a9,b9 // t9,a9 and
A3: c9,d9 // c9,t9 or c9,d9 // t9,c9 by A1,Th31;
  reconsider t=t9 as Element of S;
  [[c,d],[c,t]] in the CONGR of S or [[c,d],[t,c]] in the CONGR of S by A3,Th33
;
  then
A4: c,d // c,t or c,d // t,c by Def2;
  [[a,b],[a,t]] in the CONGR of S or [[a,b],[t,a]] in the CONGR of S by A2,Th33
;
  then a,b // a,t or a,b // t,a by Def2;
  hence thesis by A4;
end;

definition
  let IT be non empty AffinStruct;
  attr IT is OAffinSpace-like means
  :Def5:
  (for a,b,c,d,p,q,r,s being Element
of IT holds a,b // c,c & (a,b // b,a implies a=b) & (a<>b & a,b // p,q & a,b //
r,s implies p,q // r,s) & (a,b // c,d implies b,a // d,c) & (a,b // b,c implies
  a,b // a,c) & (a,b // a,c implies a,b // b,c or a,c // c,b)) & (ex a,b,c,d
  being Element of IT st not a,b // c,d & not a,b // d,c) & (for a,b,c being
Element of IT ex d being Element of IT st a,b // c,d & a,c // b,d & b<>d) & for
p,a,b,c being Element of IT st p<>b & b,p // p,c ex d being Element of IT st a,
  p // p,d & a,b // c,d;
end;

registration
  cluster strict non trivial OAffinSpace-like (non empty AffinStruct);
  existence
  proof
    consider V,u,v such that
A1: for a,b being Real st a*u + b*v = 0.V holds a=0 & b=0 and
    for w ex a,b being Real st w = a*u + b*v by FUNCSDOM:37;
A2: ( ex a,b,c,d being Element of OASpace(V) st not a,b // c,d & not a,b
// d,c)& for a,b,c being Element of OASpace(V) ex d being Element of OASpace(V
    ) st a,b // c,d & a,c // b,d & b<>d by A1,Th35;
A3: for p,a,b,c being Element of OASpace(V) st p<>b & b,p // p,c ex d
    being Element of OASpace(V) st a,p // p,d & a,b // c,d by A1,Th35;
    ( ex a,b being Element of OASpace(V) st a<>b)& for a,b,c,d,p,q,r,s
being Element of OASpace(V) holds a,b // c,c & (a,b // b,a implies a=b) & (a<>b
& a,b // p,q & a,b // r,s implies p,q // r,s) & (a, b // c,d implies b,a // d,c
) & (a,b // b,c implies a,b // a,c) & (a,b // a,c implies a,b // b,c or a,c //
    c,b) by A1,Th35;
    then OASpace(V) is non trivial OAffinSpace-like by A2,A3,Def5,
STRUCT_0:def 10;
    hence thesis;
  end;
end;

definition
  mode OAffinSpace is non trivial OAffinSpace-like (non empty AffinStruct);
end;

theorem
  (ex a,b being Element of AS st a<>b) & (for a,b,c,d,p,q,r,s being
Element of AS holds a,b // c,c & (a,b // b,a implies a=b) & (a<>b & a,b // p,q
& a,b // r,s implies p,q // r,s) & (a,b // c,d implies b,a // d,c) & (a,b // b,
c implies a,b // a,c) & (a,b // a,c implies a,b // b,c or a,c // c,b)) & (ex a,
  b,c,d being Element of AS st not a,b // c,d & not a,b // d,c) & (for a,b,c
being Element of AS ex d being Element of AS st a,b // c,d & a,c // b,d & b<>d)
  & (for p,a,b,c being Element of AS st p<>b & b,p // p,c ex d being Element of
  AS st a,p // p,d & a,b // c,d) iff AS is OAffinSpace by Def5,STRUCT_0:def 10;

theorem Th38:
  (ex u,v st for a,b being Real st a*u + b*v = 0.V holds a=0 & b=0
  ) implies OASpace(V) is OAffinSpace
proof
  assume
A1: ex u,v st for a,b being Real st a*u + b*v = 0.V holds a=0 & b=0;
  then
A2: ( ex a,b,c,d being Element of OASpace(V) st not a,b // c,d & not a,b //
  d,c)& for a,b,c being Element of OASpace(V) ex d being Element of OASpace(V)
  st a,b // c,d & a,c // b,d & b<>d by Th35;
A3: for p,a,b,c being Element of OASpace(V) st p<>b & b,p // p,c ex d being
  Element of OASpace(V) st a,p // p,d & a,b // c,d by A1,Th35;
  ( ex a,b being Element of OASpace(V) st a<>b)& for a,b,c,d,p,q,r,s being
Element of OASpace(V) holds a,b // c,c & (a,b // b,a implies a=b) & (a<>b & a,b
// p,q & a,b // r,s implies p,q // r,s) & (a, b // c,d implies b,a // d,c) & (a
  ,b // b,c implies a,b // a,c) & (a,b // a,c implies a,b // b,c or a,c // c,b)
  by A1,Th35;
  hence thesis by A2,A3,Def5,STRUCT_0:def 10;
end;

definition
  let IT be OAffinSpace;
  attr IT is 2-dimensional means
  :Def6:
  for a,b,c,d being Element of IT st not
a,b // c,d & not a,b // d,c holds ex p being Element of IT st (a,b // a,p or a,
  b // p,a) & (c,d // c,p or c,d // p,c);
end;

registration
  cluster strict 2-dimensional OAffinSpace;
  existence
  proof
    consider V such that
A1: ex u,v st (for a,b being Real st a*u + b*v = 0.V holds a=0 & b=0)
    & for w ex a,b being Real st w = a*u + b*v by FUNCSDOM:37;
    reconsider S = OASpace(V) as OAffinSpace by A1,Th38;
    for a,b,c,d being Element of S st not a,b // c,d & not a,b // d,c
holds ex p being Element of S st (a,b // a,p or a,b // p,a) & (c,d // c,p or c,
    d // p,c) by A1,Th36;
    then S is 2-dimensional by Def6;
    hence thesis;
  end;
end;

definition
  mode OAffinPlane is 2-dimensional OAffinSpace;
end;

canceled 11;

theorem
  (ex a,b being Element of AS st a<>b) & (for a,b,c,d,p,q,r,s being
Element of AS holds a,b // c,c & (a,b // b,a implies a=b) & (a<>b & a,b // p,q
& a,b // r,s implies p,q // r,s) & (a,b // c,d implies b,a // d,c) & (a,b // b,
c implies a,b // a,c) & (a,b // a,c implies a,b // b,c or a,c // c,b)) & (ex a,
  b,c,d being Element of AS st not a,b // c,d & not a,b // d,c) & (for a,b,c
being Element of AS ex d being Element of AS st a,b // c,d & a,c // b,d & b<>d)
  & (for p,a,b,c being Element of AS st p<>b & b,p // p,c ex d being Element of
AS st a,p // p,d & a,b // c,d) & (for a,b,c,d being Element of AS st not a,b //
c,d & not a,b // d,c holds ex p being Element of AS st (a,b // a,p or a,b // p,
  a) & (c,d // c,p or c,d // p,c)) iff AS is OAffinPlane by Def5,Def6,
STRUCT_0:def 10;

theorem
  (ex u,v st (for a,b being Real st a*u + b*v = 0.V holds a=0 & b=0) & (
  for w ex a,b being Real st w = a*u + b*v)) implies OASpace(V) is OAffinPlane
proof
  set S=OASpace(V);
  assume
A1: ex u,v st (for a,b being Real st a*u + b*v = 0.V holds a=0 & b=0) &
  for w ex a,b being Real st w = a*u + b*v;
  then
  for a,b,c,d being Element of S st not a,b // c,d & not a,b // d,c holds
ex p being Element of S st (a,b // a,p or a,b // p,a) & (c,d // c,p or c,d // p
  ,c) by Th36;
  hence thesis by A1,Def6,Th38;
end;
