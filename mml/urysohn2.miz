:: Some Properties of Dyadic Numbers and Intervals
::  by J\'ozef Bia{\l}as and Yatsuka Nakamura
::
:: Received February 16, 2001
:: Copyright (c) 2001 Association of Mizar Users

environ

 vocabularies MEASURE5, BOOLE, SUPINF_1, ARYTM_1, ARYTM_3, RCOMP_1, RELAT_1,
      GROUP_1, INT_1, URYSOHN1, PRALG_2, ORDINAL2, ABSVALUE, ARYTM, PROB_1,
      LIMFUNC1, MSUALG_3, XXREAL_2, SEQ_4, SEQ_2, MEMBERED, RELAT_2, TBSP_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, XXREAL_0, XREAL_0, COMPLEX1,
      REAL_1, NUMBERS, MEMBERED, NEWTON, INT_1, NAT_1, XXREAL_1, XXREAL_2,
      SUPINF_1, MEASURE5, PROB_1, URYSOHN1, INTEGRA2, LIMFUNC1, SEQ_4,
      SUPINF_2, EXTREAL1;
 constructors DOMAIN_1, REAL_1, NAT_1, PROB_1, LIMFUNC1, NEWTON, SUPINF_2,
      URYSOHN1, INTEGRA2, SUPINF_1, EXTREAL1, SEQ_4, PSCOMP_1, MEASURE5;
 registrations XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XXREAL_0, XREAL_0, NAT_1,
      INT_1, MEMBERED, XXREAL_1, SEQ_4, XXREAL_2, MEASURE6, XXREAL_3;
 requirements NUMERALS, SUBSET, REAL, BOOLE, ARITHM;
 definitions TARSKI, XBOOLE_0, PROB_1, LIMFUNC1, XXREAL_2, XCMPLX_0;
 theorems TARSKI, NAT_1, SEQ_4, ABSVALUE, URYSOHN1, XREAL_0, MEASURE5,
      MEASURE6, INT_1, INTEGRA2, XBOOLE_0, XBOOLE_1, XCMPLX_0, XCMPLX_1,
      XREAL_1, XXREAL_0, NEWTON, ORDINAL1, XXREAL_1, MEMBERED, XXREAL_2,
      EXTREAL1, XXREAL_3;
 schemes NAT_1;

begin

::                    Properties of the Intervals

canceled;

theorem Th2:
  for A being Subset of REAL holds for x being Real st x <> 0 holds
  x" ** (x ** A) = A
proof
  let A be Subset of REAL;
  let x be Real;
  assume
A1: x <> 0;
  thus x" ** (x ** A) c= A
  proof
    let y be set;
    assume
A2: y in x" ** (x ** A);
    then reconsider y as Real;
    consider z being Real such that
A3: z in x ** A & y = x" * z by A2,INTEGRA2:def 2;
    consider t being Real such that
A4: t in A & z = x * t by A3,INTEGRA2:def 2;
    y = (x" * x) * t by A3,A4
      .= 1 * t by A1,XCMPLX_0:def 7
      .= t;
    hence thesis by A4;
  end;
  let y be set;
  assume
A5: y in A;
  then reconsider y as Real;
  set t = y / x";
A6: y = x" * t by A1,XCMPLX_1:88,203;
   t in x ** A by A5,INTEGRA2:def 2;
  hence thesis by A6,INTEGRA2:def 2;
end;

theorem Th3:
  for x being Real st x <> 0 holds for A being Subset of REAL holds
  A = REAL implies x ** A = A
proof
  let x be Real;
  assume
A1: x <> 0;
  let A be Subset of REAL;
  assume
A2: A = REAL;
  for y being set st y in A holds y in x ** A
  proof
    let y be set;
    assume y in A;
    then reconsider y as Real;
    reconsider z = y/x as Real;
    y = x * z by A1,XCMPLX_1:88;
    hence thesis by A2,INTEGRA2:def 2;
  end;
  then A c= x ** A by TARSKI:def 3;
  hence thesis by A2,XBOOLE_0:def 10;
end;

theorem Th4:
  for A being Subset of REAL st A <> {} holds 0 ** A = {0}
proof
  let A be Subset of REAL;
  assume
A1: A <> {};
A2: {0} c= 0 ** A
  proof
    let y be set;
    assume
A3: y in {0};
    consider t being set such that
A4: t in A by A1,XBOOLE_0:def 1;
    reconsider t as Element of A by A4;
    reconsider t as Real by A4;
    y = 0 * t by A3,TARSKI:def 1;
    hence thesis by A4,INTEGRA2:def 2;
  end;
  0 ** A c= {0}
  proof
    let y be set;
    assume
A5: y in 0 ** A;
    then reconsider y as Real;
    consider z being Real such that
A6: z in A & y = 0 * z by A5,INTEGRA2:def 2;
    thus thesis by A6,TARSKI:def 1;
  end;
  hence thesis by A2,XBOOLE_0:def 10;
end;

theorem Th5:
  for x being Real holds x ** {} = {}
proof
  let x be Real;
  x ** {} c= {}
  proof
    let y be set;
    assume
A2: y in x ** {};
    then reconsider y as Real;
    consider z being Real such that
A3: z in {} & y = x * z by A2,INTEGRA2:def 2;
    thus thesis by A3;
  end;
  hence thesis;
end;

theorem Th6:
  for a,b being R_eal st a <= b holds
  (a = -infty & b = -infty) or (a = -infty & b in REAL) or
  (a = -infty & b = +infty) or (a in REAL & b in REAL) or
  (a in REAL & b = +infty) or (a = +infty & b = +infty)
proof
  let a,b be R_eal;
  assume
A1: a <= b;
  (a in REAL or a in {-infty,+infty}) & (b in REAL or b in {-infty,+infty} )
  by XBOOLE_0:def 3,XXREAL_0:def 4;
  then (a in REAL or a = -infty or a = +infty) &
  (b in REAL or b = -infty or b = +infty) by TARSKI:def 2;
  hence thesis by A1,XXREAL_0:9,12;
end;

canceled;

theorem Th8:
  for A being Interval holds 0 ** A is connected
proof
  let A be Interval;
  per cases;
  suppose A = {};
    hence thesis by Th5;
  end;
  suppose A <> {};
    then 0 ** A = {0} by Th4;
    then 0 ** A = [.0,0 .] by XXREAL_1:17;
    hence thesis;
  end;
end;

theorem Th9:
  for A being Interval holds for x being Real st x<>0 holds
  A is open_interval implies x ** A is open_interval
proof
  let A be Interval;
  let x be Real;
  assume
A1: x <> 0;
  assume
A2: A is open_interval;
  then consider a,b being R_eal such that
A3: a <= b & A = ].a,b.[ by MEASURE5:def 5;
  now per cases;
    case
A4:   x < 0;
      now per cases by A3,Th6;
        case a = -infty & b = -infty;
          then
A5:       ].a,b.[ = {};
          then x ** A = {} by A3,Th5;
          hence thesis by A3,A5,MEASURE5:def 5;
        end;
        case
A6:       a = -infty & b in REAL;
          then reconsider s = b as Real;
          set c = +infty;
          x * s is R_eal by XXREAL_0:def 1;
          then consider d being R_eal such that
A7:       d = x * s;
A8:       x ** A = ].d,c.[
          proof
A9:         x ** A c= ].d,c.[
            proof
              let q be set;
              assume
A10:          q in x ** A;
              then reconsider q as Real;
              consider z2 being Real such that
A11:          z2 in A & q = x * z2 by A10,INTEGRA2:def 2;
              reconsider z2 as R_eal by XXREAL_0:def 1;
              reconsider q as R_eal by XXREAL_0:def 1;
              a < z2 & z2 < b by A3,A11,MEASURE5:def 2;
              then consider r,o being Real such that
A12:          r = z2 & o = b & r <= o by A6;
A13:          r < o by A3,A11,A12,MEASURE5:def 2;
              reconsider o1 = x * o, r1 = x * r as R_eal by XXREAL_0:def 1;
A14:          o1 < r1 by A4,A13,XREAL_1:71;
              q < +infty by XXREAL_0:9;
              hence thesis by A7,A11,A12,A14,MEASURE5:def 2;
            end;
            ].d,c.[ c= x ** A
            proof
              let q be set;
              assume
A15:          q in ].d,c.[;
              then reconsider q as Real;
              reconsider q1 = q as R_eal by XXREAL_0:def 1;
A16:          d < q1 & q1 < c & q1 in REAL by A15,MEASURE5:def 2;
A17:          q = x * (q / x) by A1,XCMPLX_1:88;
              set q2 = q / x;
              q2 in A
              proof
                reconsider q3 = q2 as R_eal by XXREAL_0:def 1;
                a < q3 & q3 < b & q3 in REAL
                proof
                  x * q2 = q by A1,XCMPLX_1:88;
                  hence thesis by A4,A6,A7,A16,XREAL_1:67,XXREAL_0:12;
                end;
                hence thesis by A3,MEASURE5:def 2;
              end;
              hence thesis by A17,INTEGRA2:def 2;
            end;
            hence thesis by A9,XBOOLE_0:def 10;
          end;
          d <= c by XXREAL_0:4;
          hence thesis by A8,MEASURE5:def 5;
        end;
        case a = -infty & b = +infty;
          hence thesis by A2,A3,A4,Th3,XXREAL_1:224;
        end;
        case
A18:      a in REAL & b in REAL;
          then reconsider s = a, r = b as Real;
          reconsider d = x * s, g = x * r as R_eal by XXREAL_0:def 1;
A19:      x ** A = ].g,d.[
          proof
A20:        x ** A c= ].g,d.[
            proof
              let q be set;
              assume
A21:          q in x ** A;
              then reconsider q as Real;
              consider z2 being Real such that
A22:          z2 in A & q = x * z2 by A21,INTEGRA2:def 2;
              reconsider z2 as R_eal by XXREAL_0:def 1;
A23:          a < z2 & z2 < b by A3,A22,MEASURE5:def 2;
              then consider 1o,1r being Real such that
A24:          1o= a & 1r = z2 & 1o <= 1r by A18;
              consider 2o,2r being Real such that
A25:          2o= z2 & 2r = b & 2o <= 2r by A18,A23;
A26:          1o < 1r by A3,A22,A24,MEASURE5:def 2;
A27:          2o< 2r by A3,A22,A25,MEASURE5:def 2;
              reconsider 1o1 = x * 1o, 1r1 = x * 1r,
              2o1 = x * 2o, 2r1 = x * 2r as R_eal by XXREAL_0:def 1;
A28:          1r1 < 1o1 by A4,A26,XREAL_1:71;
              2r1 < 2o1 by A4,A27,XREAL_1:71;
              hence thesis by A22,A24,A25,A28,MEASURE5:def 2;
            end;
            ].g,d.[ c= x ** A
            proof
              let q be set;
              assume
A29:          q in ].g,d.[;
              then reconsider q as Real;
              reconsider q1 = q as R_eal by XXREAL_0:def 1;
A30:          g < q1 & q1 < d & q1 in REAL by A29,MEASURE5:def 2;
A31:          q = x * (q / x) by A1,XCMPLX_1:88;
              set q2 = q / x;
              q2 in A
              proof
                reconsider q3 = q2 as R_eal by XXREAL_0:def 1;
                a < q3 & q3 < b & q3 in REAL
                proof
A32:              q3 < b
                  proof
                    q/x < (x * r)/x by A4,A30,XREAL_1:77;
                    hence thesis by A4,XCMPLX_1:90;
                  end;
                  a < q3
                  proof
                    x * q2 = q by A1,XCMPLX_1:88;
                    hence thesis by A4,A30,XREAL_1:67;
                  end;
                  hence thesis by A32;
                end;
                hence thesis by A3,MEASURE5:def 2;
              end;
              hence thesis by A31,INTEGRA2:def 2;
            end;
            hence thesis by A20,XBOOLE_0:def 10;
          end;
          x * r <= x * s by A3,A4,XREAL_1:67;
          hence thesis by A19,MEASURE5:def 5;
        end;
        case
A33:      a in REAL & b = +infty;
          then reconsider s = a as Real;
          set c = -infty;
          reconsider d = x * s as R_eal by XXREAL_0:def 1;
A34:      x ** A = ].c,d.[
          proof
A35:        x ** A c= ].c,d.[
            proof
              let q be set;
              assume
A36:          q in x ** A;
              then reconsider q as Real;
              consider z2 being Real such that
A37:          z2 in A & q = x * z2 by A36,INTEGRA2:def 2;
              reconsider z2,q as R_eal by XXREAL_0:def 1;
              a < z2 & z2 < b by A3,A37,MEASURE5:def 2;
              then consider o,r being Real such that
A38:          o = a & r = z2 & o <= r by A33;
A39:          o < r by A3,A37,A38,MEASURE5:def 2;
              reconsider o1 = x * o, r1 = x * r as R_eal by XXREAL_0:def 1;
A40:          r1 < o1 by A4,A39,XREAL_1:71;
              -infty < q by XXREAL_0:12;
              hence thesis by A37,A38,A40,MEASURE5:def 2;
            end;
            ].c,d.[ c= x ** A
            proof
              let q be set;
              assume
A41:          q in ].c,d.[;
              then reconsider q as Real;
              reconsider q1 = q as R_eal by XXREAL_0:def 1;
A42:          q = x * (q / x) by A1,XCMPLX_1:88;
              set q2 = q / x;
              q2 in A
              proof
                reconsider q3 = q2 as R_eal by XXREAL_0:def 1;
                a < q3 & q3 < b & q3 in REAL
                proof
                  a < q3
                  proof
                    q1 <= d & q1 <> d by A41,MEASURE5:def 2;
                    then x * q2 < x * s by A42,XXREAL_0:1;
                    hence thesis by A4,XREAL_1:67;
                  end;
                  hence thesis by A33,XXREAL_0:9;
                end;
                hence thesis by A3,MEASURE5:def 2;
              end;
              hence thesis by A42,INTEGRA2:def 2;
            end;
            hence thesis by A35,XBOOLE_0:def 10;
          end;
          c <= d by XXREAL_0:5;
          hence thesis by A34,MEASURE5:def 5;
        end;
        case a = +infty & b = +infty;
          then
A43:      ].a,b.[ = {};
          then x ** A = {} by A3,Th5;
          hence thesis by A3,A43,MEASURE5:def 5;
        end;
      end;
      hence thesis;
    end;
    case x = 0;
      hence thesis by A1;
    end;
    case
A44:  0 < x;
      now per cases by A3,Th6;
        case a = -infty & b = -infty;
          then
A45:      ].a,b.[ = {};
          then x ** A = {} by A3,Th5;
          hence thesis by A3,A45,MEASURE5:def 5;
        end;
        case
A46:      a = -infty & b in REAL;
          then reconsider s = b as Real;
          set c = -infty;
          reconsider d = x * s as R_eal by XXREAL_0:def 1;
A47:      x ** A = ].c,d.[
          proof
A48:        x ** A c= ].c,d.[
            proof
              let q be set;
              assume
A49:          q in x ** A;
              then reconsider q as Real;
              consider z2 being Real such that
A50:          z2 in A & q = x * z2 by A49,INTEGRA2:def 2;
              reconsider z2,q as R_eal by XXREAL_0:def 1;
              a < z2 & z2 < b by A3,A50,MEASURE5:def 2;
              then consider r,o being Real such that
A51:          r = z2 & o = b & r <= o by A46;
A52:          r < o by A3,A50,A51,MEASURE5:def 2;
              reconsider o1 = x * o, r1 = x * r as R_eal by XXREAL_0:def 1;
A53:          r1 < o1 by A44,A52,XREAL_1:70;
              -infty < q by XXREAL_0:12;
              hence thesis by A50,A51,A53,MEASURE5:def 2;
            end;
            ].c,d.[ c= x ** A
            proof
              let q be set;
              assume
A54:          q in ].c,d.[;
              then reconsider q as Real;
              reconsider q1 = q as R_eal by XXREAL_0:def 1;
A55:          c < q1 & q1 < d & q1 in REAL by A54,MEASURE5:def 2;
A56:          q = x * (q / x) by A1,XCMPLX_1:88;
              set q2 = q / x;
              q2 in A
              proof
                reconsider q3 = q2 as R_eal by XXREAL_0:def 1;
                a < q3 & q3 < b & q3 in REAL
                proof
                  q3 < b
                  proof
                    x * q2 = q by A1,XCMPLX_1:88;
                    hence thesis by A44,A55,XREAL_1:66;
                  end;
                  hence thesis by A46,XXREAL_0:12;
                end;
                hence thesis by A3,MEASURE5:def 2;
              end;
              hence thesis by A56,INTEGRA2:def 2;
            end;
            hence thesis by A48,XBOOLE_0:def 10;
          end;
          c <= d by XXREAL_0:5;
          hence thesis by A47,MEASURE5:def 5;
        end;
        case a = -infty & b = +infty;
          hence thesis by A2,A3,A44,Th3,XXREAL_1:224;
        end;
        case
A57:      a in REAL & b in REAL;
          then reconsider s = a, r = b as Real;
          reconsider d = x * s as R_eal by XXREAL_0:def 1;
          reconsider g = x * r as R_eal by XXREAL_0:def 1;
A58:      x ** A = ].d,g.[
          proof
A59:        x ** A c= ].d,g.[
            proof
              let q be set;
              assume
A60:          q in x ** A;
              then reconsider q as Real;
              consider z2 being Real such that
A61:          z2 in A & q = x * z2 by A60,INTEGRA2:def 2;
              reconsider z2 as R_eal by XXREAL_0:def 1;
A62:          a < z2 & z2 < b by A3,A61,MEASURE5:def 2;
              then consider 1o,1r being Real such that
A63:          1o= a & 1r = z2 & 1o <= 1r by A57;
              consider 2o,2r being Real such that
A64:          2o= z2 & 2r = b & 2o <= 2r by A57,A62;
A65:          1o< 1r by A3,A61,A63,MEASURE5:def 2;
A66:          2o< 2r by A3,A61,A64,MEASURE5:def 2;
              reconsider 1o1 = x * 1o, 1r1 = x * 1r as R_eal by XXREAL_0:def 1;
              reconsider 2o1 = x * 2o, 2r1 = x * 2r as R_eal by XXREAL_0:def 1;
A67:          1o1 < 1r1 by A44,A65,XREAL_1:70;
              2o1 < 2r1 by A44,A66,XREAL_1:70;
              hence thesis by A61,A63,A64,A67,MEASURE5:def 2;
            end;
            ].d,g.[ c= x ** A
            proof
              let q be set;
              assume
A68:          q in ].d,g.[;
              then reconsider q as Real;
              q is R_eal by XXREAL_0:def 1;
              then consider q1 being R_eal such that
A69:          q1 = q;
A70:          d < q1 & q1 < g & q1 in REAL by A68,A69,MEASURE5:def 2;
A71:          q = x * (q / x) by A1,XCMPLX_1:88;
              set q2 = q / x;
              q2 in A
              proof
                reconsider q3 = q2 as R_eal by XXREAL_0:def 1;
                a < q3 & q3 < b & q3 in REAL
                proof
A72:              q3 < b
                  proof
                    q/x < (x * r)/x by A44,A69,A70,XREAL_1:76;
                    hence thesis by A44,XCMPLX_1:90;
                  end;
                  a < q3
                  proof
                    x * q2 = q by A1,XCMPLX_1:88;
                    hence thesis by A44,A69,A70,XREAL_1:66;
                  end;
                  hence thesis by A72;
                end;
                hence thesis by A3,MEASURE5:def 2;
              end;
              hence thesis by A71,INTEGRA2:def 2;
            end;
            hence thesis by A59,XBOOLE_0:def 10;
          end;
          x * s <= x * r by A3,A44,XREAL_1:66;
          hence thesis by A58,MEASURE5:def 5;
        end;
        case
A73:      a in REAL & b = +infty;
          then reconsider s = a as Real;
          set c = +infty;
          reconsider d = x * s as R_eal by XXREAL_0:def 1;
A74:      x ** A = ].d,c.[
          proof
A75:        x ** A c= ].d,c.[
            proof
              let q be set;
              assume
A76:          q in x ** A;
              then reconsider q as Real;
              consider z2 being Real such that
A77:          z2 in A & q = x * z2 by A76,INTEGRA2:def 2;
              reconsider z2 as R_eal by XXREAL_0:def 1;
              reconsider q as R_eal by XXREAL_0:def 1;
              a < z2 & z2 < b by A3,A77,MEASURE5:def 2;
              then consider o,r being Real such that
A78:          o = a & r = z2 & o <= r by A73;
A79:          o < r by A3,A77,A78,MEASURE5:def 2;
              reconsider o1 = x * o, r1 = x * r as R_eal by XXREAL_0:def 1;
A80:          o1 < r1 by A44,A79,XREAL_1:70;
              q < +infty by XXREAL_0:9;
              hence thesis by A77,A78,A80,MEASURE5:def 2;
            end;
            ].d,c.[ c= x ** A
            proof
              let q be set;
              assume
A81:          q in ].d,c.[;
              then reconsider q as Real;
              reconsider q1 = q as R_eal by XXREAL_0:def 1;
A82:          d < q1 & q1 < c & q1 in REAL by A81,MEASURE5:def 2;
A83:          q = x * (q / x) by A1,XCMPLX_1:88;
              set q2 = q / x;
              q2 in A
              proof
                reconsider q3 = q2 as R_eal by XXREAL_0:def 1;
                a < q3 & q3 < b & q3 in REAL
                by A44,A73,A82,A83,XREAL_1:66,XXREAL_0:9;
                hence thesis by A3,MEASURE5:def 2;
              end;
              hence thesis by A83,INTEGRA2:def 2;
            end;
            hence thesis by A75,XBOOLE_0:def 10;
          end;
          d <= c by XXREAL_0:4;
          hence thesis by A74,MEASURE5:def 5;
        end;
        case a = +infty & b = +infty;
          then
A84:      ].a,b.[ = {};
          then x ** A = {} by A3,Th5;
          hence thesis by A3,A84,MEASURE5:def 5;
        end;
      end;
      hence thesis;
    end;
  end;
  hence thesis;
end;

theorem Th10:
  for A being Interval holds for x being Real st x<>0 holds
  A is closed_interval implies x ** A is closed_interval
proof
  let A be Interval;
  let x be Real;
  assume
A1: x <> 0;
  assume
    A is closed_interval;
  then consider a,b being real number such that
A3: a <= b & A = [.a,b.] by MEASURE5:def 6;
  reconsider a,b as R_eal by XXREAL_0:def 1;
  now per cases;
    case
A4:   x < 0;
      now
A17:      a in REAL & b in REAL by Th6;
          reconsider s = a, r = b as Real by Th6;
          reconsider d = x * s as R_eal by XXREAL_0:def 1;
          reconsider g = x * r as R_eal by XXREAL_0:def 1;
A18:      x ** A = [.g,d.]
          proof
A19:        x ** A c= [.g,d.]
            proof
              let q be set;
              assume
A20:          q in x ** A;
              then reconsider q as Real;
              consider z2 being Real such that
A21:          z2 in A & q = x * z2 by A20,INTEGRA2:def 2;
              reconsider z2 as R_eal by XXREAL_0:def 1;
A22:          a <= z2 & z2 <= b by A3,A21,XXREAL_1:1;
              then consider 1o,1r being Real such that
A23:          1o= a & 1r = z2 & 1o <= 1r by A17;
              consider 2o,2r being Real such that
A24:          2o= z2 & 2r = b & 2o <= 2r by A17,A22;
A25:          x * 2r <= x * 2o by A4,A24,XREAL_1:67;
              x * 1o is R_eal & x * 1r is R_eal by XXREAL_0:def 1;
              then consider 1o1,1r1 being R_eal such that
A26:          1o1 = x * 1o & 1r1 = x * 1r;
              1r1 <= 1o1 by A4,A23,A26,XREAL_1:67;
              hence thesis by A21,A23,A24,A25,A26,XXREAL_1:1;
            end;
            [.g,d.] c= x ** A
            proof
              let q be set;
              assume
A27:          q in [.g,d.];
              then reconsider q as Real by XREAL_0:def 1;
              reconsider q1 = q as R_eal by XXREAL_0:def 1;
A28:          g <= q1 & q1 <= d & q1 in REAL by A27,XXREAL_1:1;
A29:          q = x * (q / x) by A1,XCMPLX_1:88;
              set q2 = q / x;
              q2 in A
              proof
                reconsider q3 = q2 as R_eal by XXREAL_0:def 1;
                a <= q3 & q3 <= b & q3 in REAL
                proof
A30:              q3 <= b
                  proof
                    consider p,o being Real such that
A31:                p = g & o = q1 & p <= o by A28;
                    o/x <= p/x by A4,A31,XREAL_1:75;
                    hence thesis by A4,A31,XCMPLX_1:90;
                  end;
                  a <= q3
                  proof
                    q1 <= d by A27,XXREAL_1:1;
                    then consider r,o being Real such that
A32:                r = q1 & o = d & r <= o;
                    x * q2 = q by A1,XCMPLX_1:88;
                    hence thesis by A4,A32,XREAL_1:71;
                  end;
                  hence thesis by A30;
                end;
                hence thesis by A3,XXREAL_1:1;
              end;
              hence thesis by A29,INTEGRA2:def 2;
            end;
            hence thesis by A19,XBOOLE_0:def 10;
          end;
          x * r <= x * s by A3,A4,XREAL_1:67;
          hence thesis by A18,MEASURE5:def 6;
      end;
      hence thesis;
    end;
    case x = 0;
      hence thesis by A1;
    end;
    case
A44:  0 < x;
      now per cases by Th6;
        case
A58:      a in REAL & b in REAL;
          then reconsider s = a as Real;
          reconsider r = b as Real by A58;
          x * s is R_eal by XXREAL_0:def 1;
          then consider d being R_eal such that
A59:      d = x * s;
          reconsider g = x * r as R_eal by XXREAL_0:def 1;
A60:      x ** A = [.d,g.]
          proof
A61:        x ** A c= [.d,g.]
            proof
              let q be set;
              assume
A62:          q in x ** A;
              then reconsider q as Real;
              consider z2 being Real such that
A63:          z2 in A & q = x * z2 by A62,INTEGRA2:def 2;
              reconsider z2 as R_eal by XXREAL_0:def 1;
A64:          a <= z2 & z2 <= b by A3,A63,XXREAL_1:1;
              then consider 1o,1r being Real such that
A65:          1o= a & 1r = z2 & 1o <= 1r by A58;
              consider 2o,2r being Real such that
A66:          2o= z2 & 2r = b & 2o <= 2r by A58,A64;
A67:          x * 2o <= x * 2r by A44,A66,XREAL_1:66;
              x * 1o is R_eal & x * 1r is R_eal by XXREAL_0:def 1;
              then consider 1o1,1r1 being R_eal such that
A68:          1o1 = x * 1o & 1r1 = x * 1r;
              1o1 <= 1r1 by A44,A65,A68,XREAL_1:66;
              hence thesis by A59,A63,A65,A66,A67,A68,XXREAL_1:1;
            end;
            [.d,g.] c= x ** A
            proof
              let q be set;
              assume
A69:          q in [.d,g.];
              then reconsider q as Real by A59,XREAL_0:def 1;
              q is R_eal by XXREAL_0:def 1;
              then consider q1 being R_eal such that
A70:          q1 = q;
A71:          q = x * (q / x) by A1,XCMPLX_1:88;
              set q2 = q / x;
              q2 in A
              proof
                q2 is R_eal by XXREAL_0:def 1;
                then consider q3 being R_eal such that
A72:            q3 = q2;
                a <= q3 & q3 <= b & q3 in REAL
                proof
A73:              q3 <= b
                  proof
                    q1 <= g by A69,A70,XXREAL_1:1;
                    then consider p,o being Real such that
A74:                p = q1 & o = g & p <= o by A70;
                    p/x <= o/x by A44,A74,XREAL_1:74;
                    hence thesis by A44,A70,A72,A74,XCMPLX_1:90;
                  end;
                  a <= q3
                  proof
                    d <= q1 by A69,A70,XXREAL_1:1;
                    then consider ri,o being Real such that
A75:                ri = d & o = q1 & ri <= o by A59,A70;
                    thus thesis by A44,A59,A70,A71,A72,A75,XREAL_1:70;
                  end;
                  hence thesis by A72,A73;
                end;
                hence thesis by A3,A72,XXREAL_1:1;
              end;
              hence thesis by A71,INTEGRA2:def 2;
            end;
            hence thesis by A61,XBOOLE_0:def 10;
          end;
          x * s <= x * r by A3,A44,XREAL_1:66;
          hence thesis by A59,A60,MEASURE5:def 6;
        end;
      end;
      hence thesis;
    end;
  end;
  hence thesis;
end;

theorem Th11:
  for A being Interval holds for x being Real st 0 < x holds
  A is right_open_interval implies x ** A is right_open_interval
proof
  let A be Interval;
  let x be Real;
  assume
A1: 0 < x;
  assume
    A is right_open_interval;
  then consider a being real number,b being R_eal such that
A3: a <= b & A = [.a,b.[ by MEASURE5:def 7;
  reconsider a as R_eal by XXREAL_0:def 1;
  now per cases by A3,Th6;
    case a = -infty & b = -infty;
      hence thesis;
    end;
    case
      a = -infty & b in REAL;
      hence thesis;
    end;
    case a = -infty & b = +infty;
      hence thesis;
    end;
    case
A18:  a in REAL & b in REAL;
      then consider s being Real such that
A19:  s = a;
      consider r being Real such that
A20:  r = b by A18;
      x * s is R_eal by XXREAL_0:def 1;
      then consider d being R_eal such that
A21:  d = x * s;
      x * r is R_eal by XXREAL_0:def 1;
      then consider g being R_eal such that
A22:  g = x * r;
A23:  x ** A = [.d,g.[
      proof
A24:    x ** A c= [.d,g.[
        proof
          let q be set;
          assume
A25:      q in x ** A;
          then reconsider q as Real;
          consider z2 being Real such that
A26:      z2 in A & q = x * z2 by A25,INTEGRA2:def 2;
          reconsider z2 as R_eal by XXREAL_0:def 1;
A27:      a <= z2 & z2 < b by A3,A26,XXREAL_1:3;
A28:      a <= z2 & z2 <= b & z2 <> b by A3,A26,XXREAL_1:3;
          consider 1o,1r being Real such that
A29:      1o= a & 1r = z2 & 1o <= 1r by A18,A27;
          consider 2o,2r being Real such that
A30:      2o= z2 & 2r = b & 2o <= 2r by A18,A28;
A31:      x * 1o <= x * 1r by A1,A29,XREAL_1:66;
A32:      2o< 2r by A3,A26,A30,XXREAL_1:3;
          x * 2o is R_eal & x * 2r is R_eal by XXREAL_0:def 1;
          then consider 2o1,2r1 being R_eal such that
A33:      2o1 = x * 2o & 2r1 = x * 2r;
          2o1 < 2r1 by A1,A32,A33,XREAL_1:70;
          hence thesis by A19,A20,A21,A22,A26,A29,A30,A31,A33,XXREAL_1:3;
        end;
        [.d,g.[ c= x ** A
        proof
          let q be set;
          assume
A34:      q in [.d,g.[;
          then reconsider q as Real by A21,XREAL_0:def 1;
          q is R_eal by XXREAL_0:def 1;
          then consider q1 being R_eal such that
A35:      q1 = q;
A36:      q = x * (q / x) by A1,XCMPLX_1:88;
          set q2 = q / x;
          q2 in A
          proof
            q2 is R_eal by XXREAL_0:def 1;
            then consider q3 being R_eal such that
A37:        q3 = q2;
            a <= q3 & q3 < b & q3 in REAL
            proof
A38:          q3 < b
              proof
                q1 <= g & q1 <> g by A34,A35,XXREAL_1:3;
                then consider p,o being Real such that
A39:            p = q1 & o = g & p <= o by A22,A35;
                p < o by A34,A35,A39,XXREAL_1:3;
                then p/x < o/x by A1,XREAL_1:76;
                hence thesis by A1,A20,A22,A35,A37,A39,XCMPLX_1:90;
              end;
              a <= q3
              proof
                d <= q1 by A34,A35,XXREAL_1:3;
                then consider r,o being Real such that
A40:            r = d & o = q1 & r <= o by A21,A35;
                x * q2 = q by A1,XCMPLX_1:88;
                hence thesis by A1,A19,A21,A35,A37,A40,XREAL_1:70;
              end;
              hence thesis by A37,A38;
            end;
            hence thesis by A3,A37,XXREAL_1:3;
          end;
          hence thesis by A36,INTEGRA2:def 2;
        end;
        hence thesis by A24,XBOOLE_0:def 10;
      end;
      x * s <= x * r by A1,A3,A19,A20,XREAL_1:66;
      hence thesis by A21,A22,A23,MEASURE5:def 7;
    end;
    case
A41:  a in REAL & b = +infty;
      then consider s being Real such that
A42:  s = a;
      consider c being R_eal such that
A43:  c = +infty;
      x * s is R_eal by XXREAL_0:def 1;
      then consider d being R_eal such that
A44:  d = x * s;
A45:  x ** A = [.d,c.[
      proof
A46:    x ** A c= [.d,c.[
        proof
          let q be set;
          assume
A47:      q in x ** A;
          then reconsider q as Real;
          consider z2 being Real such that
A48:      z2 in A & q = x * z2 by A47,INTEGRA2:def 2;
          reconsider z2 as R_eal by XXREAL_0:def 1;
          reconsider q as R_eal by XXREAL_0:def 1;
          a <= z2 & z2 < b by A3,A48,XXREAL_1:3;
          then consider o,r being Real such that
A49:      o = a & r = z2 & o <= r by A41;
A50:      x * o <= x * r by A1,A49,XREAL_1:66;
          q < +infty by XXREAL_0:9;
          hence thesis by A42,A43,A44,A48,A49,A50,XXREAL_1:3;
        end;
        [.d,c.[ c= x ** A
        proof
          let q be set;
          assume
A51:      q in [.d,c.[;
          then reconsider q as Real by A44,XREAL_0:def 1;
          q is R_eal by XXREAL_0:def 1;
          then consider q1 being R_eal such that
A52:      q1 = q;
A53:      q = x * (q / x) by A1,XCMPLX_1:88;
          set q2 = q / x;
          q2 in A
          proof
            q2 is R_eal by XXREAL_0:def 1;
            then consider q3 being R_eal such that
A54:        q3 = q2;
            a <= q3 & q3 < b & q3 in REAL
            proof
              a <= q3
              proof
                d <= q1 by A51,A52,XXREAL_1:3;
                then consider r,o being Real such that
A55:            r = d & o = q1 & r <= o by A44,A52;
                x * q2 = q by A1,XCMPLX_1:88;
                hence thesis by A1,A42,A44,A52,A54,A55,XREAL_1:70;
              end;
              hence thesis by A41,A54,XXREAL_0:9;
            end;
            hence thesis by A3,A54,XXREAL_1:3;
          end;
          hence thesis by A53,INTEGRA2:def 2;
        end;
        hence thesis by A46,XBOOLE_0:def 10;
      end;
      d <= c by A43,XXREAL_0:4;
      hence thesis by A45,A44,MEASURE5:def 7;
    end;
    case a = +infty & b = +infty;
      hence thesis;
    end;
  end;
  hence thesis;
end;

theorem Th12:
  for A being Interval holds for x being Real st x < 0 holds
  A is right_open_interval implies x ** A is left_open_interval
proof
  let A be Interval;
  let x be Real;
  assume
A1: x < 0;
  assume A is right_open_interval;
  then consider a being real number,b being R_eal such that
A2: a <= b & A = [.a,b.[ by MEASURE5:def 7;
  reconsider a as R_eal by XXREAL_0:def 1;
  now per cases by A2,Th6;
    case
      a = -infty & b = -infty;
      hence thesis;
    end;
    case
      a = -infty & b in REAL;
      hence thesis;
    end;
    case a = -infty & b = +infty;
      hence thesis;
    end;
    case
A20:  a in REAL & b in REAL;
      then consider s being Real such that
A21:  s = a;
      consider r being Real such that
A22:  r = b by A20;
      x * s is R_eal by XXREAL_0:def 1;
      then consider d being R_eal such that
A23:  d = x * s;
      x * r is R_eal by XXREAL_0:def 1;
      then consider g being R_eal such that
A24:  g = x * r;
A25:  x ** A = ].g,d.]
      proof
A26:    x ** A c= ].g,d.]
        proof
          let q be set;
          assume
A27:      q in x ** A;
          then reconsider q as Real;
          consider z2 being Real such that
A28:      z2 in A & q = x * z2 by A27,INTEGRA2:def 2;
          reconsider z2 as R_eal by XXREAL_0:def 1;
A29:      a <= z2 & z2 < b by A2,A28,XXREAL_1:3;
A30:      a <= z2 & z2 <= b & z2 <> b by A2,A28,XXREAL_1:3;
          consider 1o,1r being Real such that
A31:      1o= a & 1r = z2 & 1o <= 1r by A20,A29;
          consider 2o,2r being Real such that
A32:      2o= z2 & 2r = b & 2o <= 2r by A20,A30;
A33:      x * 1r <= x * 1o by A1,A31,XREAL_1:67;
A34:      2o< 2r by A2,A28,A32,XXREAL_1:3;
          x * 2o is R_eal & x * 2r is R_eal by XXREAL_0:def 1;
          then consider 2o1,2r1 being R_eal such that
A35:      2o1 = x * 2o & 2r1 = x * 2r;
          2r1 < 2o1 by A1,A34,A35,XREAL_1:71;
          hence thesis by A21,A22,A23,A24,A28,A31,A32,A33,A35,XXREAL_1:2;
        end;
        ].g,d.] c= x ** A
        proof
          let q be set;
          assume
A36:      q in ].g,d.];
          then reconsider q as Real by A23,XREAL_0:def 1;
          q is R_eal by XXREAL_0:def 1;
          then consider q1 being R_eal such that
A37:      q1 = q;
A38:      g < q1 & q1 <= d & q1 in REAL by A36,A37,XXREAL_1:2;
A39:      q = x * (q / x) by A1,XCMPLX_1:88;
          set q2 = q / x;
          q2 in A
          proof
            q2 is R_eal by XXREAL_0:def 1;
            then consider q3 being R_eal such that
A40:        q3 = q2;
            a <= q3 & q3 < b & q3 in REAL
            proof
A41:          q3 < b
              proof
A42:            g < q1 by A36,A37,XXREAL_1:2;
                consider p,o being Real such that
A43:            p = g & o = q1 & p <= o by A24,A38;
                o/x < p/x by A1,A42,A43,XREAL_1:77;
                hence thesis by A1,A22,A24,A37,A40,A43,XCMPLX_1:90;
              end;
              a <= q3
              proof
                q1 <= d by A36,A37,XXREAL_1:2;
                then consider r,o being Real such that
A44:            r = q1 & o = d & r <= o by A23,A37;
                x * q2 = q by A1,XCMPLX_1:88;
                hence thesis by A1,A21,A23,A37,A40,A44,XREAL_1:71;
              end;
              hence thesis by A40,A41;
            end;
            hence thesis by A2,A40,XXREAL_1:3;
          end;
          hence thesis by A39,INTEGRA2:def 2;
        end;
        hence thesis by A26,XBOOLE_0:def 10;
      end;
      x * r <= x * s by A1,A2,A21,A22,XREAL_1:67;
      hence thesis by A23,A24,A25,MEASURE5:def 8;
    end;
    case
A45:  a in REAL & b = +infty;
      then consider s being Real such that
A46:  s = a;
      consider c being R_eal such that
A47:  c = -infty;
      x * s is R_eal by XXREAL_0:def 1;
      then consider d being R_eal such that
A48:  d = x * s;
A49:  x ** A = ].c,d.]
      proof
A50:    x ** A c= ].c,d.]
        proof
          let q be set;
          assume
A51:      q in x ** A;
          then reconsider q as Real;
          consider z2 being Real such that
A52:      z2 in A & q = x * z2 by A51,INTEGRA2:def 2;
          reconsider z2 as R_eal by XXREAL_0:def 1;
          reconsider q as R_eal by XXREAL_0:def 1;
          a <= z2 & z2 < b by A2,A52,XXREAL_1:3;
          then consider o,r being Real such that
A53:      o = a & r = z2 & o <= r by A45;
A54:      x * r <= x * o by A1,A53,XREAL_1:67;
          -infty < q by XXREAL_0:12;
          hence thesis by A46,A47,A48,A52,A53,A54,XXREAL_1:2;
        end;
        ].c,d.] c= x ** A
        proof
          let q be set;
          assume
A55:      q in ].c,d.];
          then reconsider q as Real by A48,XREAL_0:def 1;
          reconsider q1 = q as R_eal by XXREAL_0:def 1;
          set q2 = q / x;
A56:      q2 in A
          proof
            reconsider q3 = q2 as R_eal by XXREAL_0:def 1;
            a <= q3 & q3 < b & q3 in REAL
            proof
              a <= q3
              proof
                q1 <= d by A55,XXREAL_1:2;
                then consider r,o being Real such that
A57:            r = q1 & o = d & r <= o by A48;
                x * q2 = q by A1,XCMPLX_1:88;
                hence thesis by A1,A46,A48,A57,XREAL_1:71;
              end;
              hence thesis by A45,XXREAL_0:9;
            end;
            hence thesis by A2,XXREAL_1:3;
          end;
          q = x * q2 by A1,XCMPLX_1:88;
          hence thesis by A56,INTEGRA2:def 2;
        end;
        hence thesis by A50,XBOOLE_0:def 10;
      end;
      c <= d by A47,XXREAL_0:5;
      hence thesis by A49,A48,MEASURE5:def 8;
    end;
    case
      a = +infty & b = +infty;
      hence thesis;
    end;
  end;
  hence thesis;
end;

theorem Th13:
  for A being Interval holds for x being Real st 0 < x holds
  A is left_open_interval implies x ** A is left_open_interval
proof
  let A be Interval;
  let x be Real;
  assume
A1: 0 < x;
  assume
    A is left_open_interval;
  then consider a being R_eal,b being real number such that
A3: a <= b & A = ].a,b.] by MEASURE5:def 8;
  reconsider b as R_eal by XXREAL_0:def 1;
  now per cases by A3,Th6;
    case a = -infty & b = -infty;
      hence thesis;
    end;
    case
A4:   a = -infty & b in REAL;
      then consider s being Real such that
A5:   s = b;
      consider c being R_eal such that
A6:   c = -infty;
      x * s is R_eal by XXREAL_0:def 1;
      then consider d being R_eal such that
A7:   d = x * s;
A8:   x ** A = ].c,d.]
      proof
A9:     x ** A c= ].c,d.]
        proof
          let q be set;
          assume
A10:      q in x ** A;
          then reconsider q as Real;
          consider z2 being Real such that
A11:      z2 in A & q = x * z2 by A10,INTEGRA2:def 2;
          reconsider z2 as R_eal by XXREAL_0:def 1;
          reconsider q as R_eal by XXREAL_0:def 1;
          a < z2 & z2 <= b by A3,A11,XXREAL_1:2;
          then consider r,o being Real such that
A12:      r = z2 & o = b & r <= o by A4;
A13:      x * r <= x * o by A1,A12,XREAL_1:66;
          -infty < q by XXREAL_0:12;
          hence thesis by A5,A6,A7,A11,A12,A13,XXREAL_1:2;
        end;
        ].c,d.] c= x ** A
        proof
          let q be set;
          assume
A14:      q in ].c,d.];
          then reconsider q as Real by A7,XREAL_0:def 1;
          reconsider q1 = q as R_eal by XXREAL_0:def 1;
A15:      q = x * (q / x) by A1,XCMPLX_1:88;
          set q2 = q / x;
          q2 in A
          proof
            q2 is R_eal by XXREAL_0:def 1;
            then consider q3 being R_eal such that
A16:        q3 = q2;
            a < q3 & q3 <= b & q3 in REAL
            proof
              q3 <= b
              proof
                q1 <= d by A14,XXREAL_1:2;
                then consider r,o being Real such that
A17:            r = q1 & o = d & r <= o by A7;
                x * q2 = q by A1,XCMPLX_1:88;
                hence thesis by A1,A5,A7,A16,A17,XREAL_1:70;
              end;
              hence thesis by A4,A16,XXREAL_0:12;
            end;
            hence thesis by A3,A16,XXREAL_1:2;
          end;
          hence thesis by A15,INTEGRA2:def 2;
        end;
        hence thesis by A9,XBOOLE_0:def 10;
      end;
      c <= d by A6,XXREAL_0:5;
      hence thesis by A8,A7,MEASURE5:def 8;
    end;
    case a = -infty & b = +infty;
      hence thesis;
    end;
    case
A18:  a in REAL & b in REAL;
      then reconsider s = a as Real;
      consider r being Real such that
A19:  r = b by A18;
      x * s is R_eal by XXREAL_0:def 1;
      then consider d being R_eal such that
A20:  d = x * s;
      x * r is R_eal by XXREAL_0:def 1;
      then consider g being R_eal such that
A21:  g = x * r;
A22:  x ** A = ].d,g.]
      proof
A23:    x ** A c= ].d,g.]
        proof
          let q be set;
          assume
A24:      q in x ** A;
          then reconsider q as Real;
          consider z2 being Real such that
A25:      z2 in A & q = x * z2 by A24,INTEGRA2:def 2;
          reconsider z2 as R_eal by XXREAL_0:def 1;
A26:      a <= z2 & z2 <> a & z2 <= b by A3,A25,XXREAL_1:2;
          then consider 1o,1r being Real such that
A27:      1o= a & 1r = z2 & 1o <= 1r by A18;
          consider 2o,2r being Real such that
A28:      2o= z2 & 2r = b & 2o <= 2r by A18,A26;
          1o< 1r by A3,A25,A27,XXREAL_1:2;
          then
A29:      x * 1o < x * 1r by A1,XREAL_1:70;
          x * 2o is R_eal & x * 2r is R_eal by XXREAL_0:def 1;
          then consider 2o1,2r1 being R_eal such that
A30:      2o1 = x * 2o & 2r1 = x * 2r;
          2o1 <= 2r1 by A1,A28,A30,XREAL_1:66;
          hence thesis by A19,A20,A21,A25,A27,A28,A29,A30,XXREAL_1:2;
        end;
        ].d,g.] c= x ** A
        proof
          let q be set;
          assume
A31:      q in ].d,g.];
          then reconsider q as Real by A21,XREAL_0:def 1;
          reconsider q1 = q as R_eal by XXREAL_0:def 1;
A32:      q = x * (q / x) by A1,XCMPLX_1:88;
          set q2 = q / x;
          q2 in A
          proof
            q2 is R_eal by XXREAL_0:def 1;
            then consider q3 being R_eal such that
A33:        q3 = q2;
            a < q3 & q3 <= b & q3 in REAL
            proof
A34:          q3 <= b
              proof
                q1 <= g by A31,XXREAL_1:2;
                then consider p,o being Real such that
A35:            p = q1 & o = g & p <= o by A21;
                p/x <= o/x by A1,A35,XREAL_1:74;
                hence thesis by A1,A19,A21,A33,A35,XCMPLX_1:90;
              end;
              a < q3
              proof
A36:            d < q1 by A31,XXREAL_1:2;
                x * q2 = q by A1,XCMPLX_1:88;
                hence thesis by A1,A20,A33,A36,XREAL_1:66;
              end;
              hence thesis by A33,A34;
            end;
            hence thesis by A3,A33,XXREAL_1:2;
          end;
          hence thesis by A32,INTEGRA2:def 2;
        end;
        hence thesis by A23,XBOOLE_0:def 10;
      end;
      x * s <= x * r by A1,A3,A19,XREAL_1:66;
      hence thesis by A20,A21,A22,MEASURE5:def 8;
    end;
    case
      a in REAL & b = +infty;
      hence thesis;
    end;
    case a = +infty & b = +infty;
     hence thesis;
    end;
  end;
  hence thesis;
end;

theorem Th14:
  for A being Interval holds for x being Real st x < 0 holds
  A is left_open_interval implies x ** A is right_open_interval
proof
  let A be Interval;
  let x be Real;
  assume
A1: x < 0;
  assume A is left_open_interval;
  then consider a being R_eal,b being real number such that
A2: a <= b & A = ].a,b.] by MEASURE5:def 8;
  reconsider b as R_eal by XXREAL_0:def 1;
  now per cases by A2,Th6;
    case
      a = -infty & b = -infty;
      hence thesis;
    end;
    case
A4:   a = -infty & b in REAL;
      then consider s being Real such that
A5:   s = b;
      consider c being R_eal such that
A6:   c = +infty;
      x * s is R_eal by XXREAL_0:def 1;
      then consider d being R_eal such that
A7:   d = x * s;
A8:   x ** A = [.d,c.[
      proof
A9:     x ** A c= [.d,c.[
        proof
          let q be set;
          assume
A10:      q in x ** A;
          then reconsider q as Real;
          consider z2 being Real such that
A11:      z2 in A & q = x * z2 by A10,INTEGRA2:def 2;
          reconsider z2 as R_eal by XXREAL_0:def 1;
          reconsider q as R_eal by XXREAL_0:def 1;
          a < z2 & z2 <= b by A2,A11,XXREAL_1:2;
          then consider r,o being Real such that
A12:      r = z2 & o = b & r <= o by A4;
A13:      x * o <= x * r by A1,A12,XREAL_1:67;
          q < +infty by XXREAL_0:9;
          hence thesis by A5,A6,A7,A11,A12,A13,XXREAL_1:3;
        end;
        [.d,c.[ c= x ** A
        proof
          let q be set;
          assume
A14:      q in [.d,c.[;
          then reconsider q as Real by A7,XREAL_0:def 1;
          q is R_eal by XXREAL_0:def 1;
          then consider q1 being R_eal such that
A15:      q1 = q;
          consider q2 being Real such that
A16:      q2 = q / x;
A17:      q2 in A
          proof
            q2 is R_eal by XXREAL_0:def 1;
            then consider q3 being R_eal such that
A18:        q3 = q2;
            a < q3 & q3 <= b & q3 in REAL
            proof
              q3 <= b
              proof
                d <= q1 by A14,A15,XXREAL_1:3;
                then consider r,o being Real such that
A19:            r = d & o = q1 & r <= o by A7,A15;
                x * q2 = q by A1,A16,XCMPLX_1:88;
                hence thesis by A1,A5,A7,A15,A18,A19,XREAL_1:71;
              end;
              hence thesis by A4,A18,XXREAL_0:12;
            end;
            hence thesis by A2,A18,XXREAL_1:2;
          end;
          q = x * q2 by A1,A16,XCMPLX_1:88;
          hence thesis by A17,INTEGRA2:def 2;
        end;
        hence thesis by A9,XBOOLE_0:def 10;
      end;
      d <= c by A6,XXREAL_0:4;
      hence thesis by A8,A7,MEASURE5:def 7;
    end;
    case a = -infty & b = +infty;
      hence thesis;
    end;
    case
A20:  a in REAL & b in REAL;
      then reconsider s = a, r = b as Real;
      x * s is R_eal by XXREAL_0:def 1;
      then consider d being R_eal such that
A21:  d = x * s;
      x * r is R_eal by XXREAL_0:def 1;
      then consider g being R_eal such that
A22:  g = x * r;
A23:  x ** A = [.g,d.[
      proof
A24:    x ** A c= [.g,d.[
        proof
          let q be set;
          assume
A25:      q in x ** A;
          then reconsider q as Real;
          consider z2 being Real such that
A26:      z2 in A & q = x * z2 by A25,INTEGRA2:def 2;
          reconsider z2 as R_eal by XXREAL_0:def 1;
A27:      a <= z2 & z2 <> a & z2 <= b by A2,A26,XXREAL_1:2;
          then consider 1o,1r being Real such that
A28:      1o= a & 1r = z2 & 1o <= 1r by A20;
          consider 2o,2r being Real such that
A29:      2o= z2 & 2r = b & 2o <= 2r by A20,A27;
          1o< 1r by A2,A26,A28,XXREAL_1:2;
          then
A30:      x * 1r < x * 1o by A1,XREAL_1:71;
          x * 2o is R_eal & x * 2r is R_eal by XXREAL_0:def 1;
          then consider 2o1,2r1 being R_eal such that
A31:      2o1 = x * 2o & 2r1 = x * 2r;
          2r1 <= 2o1 by A1,A29,A31,XREAL_1:67;
          hence thesis by A21,A22,A26,A28,A29,A30,A31,XXREAL_1:3;
        end;
        [.g,d.[ c= x ** A
        proof
          let q be set;
          assume
A32:      q in [.g,d.[;
          then reconsider q as Real by A22,XREAL_0:def 1;
          q is R_eal by XXREAL_0:def 1;
          then consider q1 being R_eal such that
A33:      q1 = q;
A34:      g <= q1 & q1 < d & q1 in REAL by A32,A33,XXREAL_1:3;
A35:      q = x * (q / x) by A1,XCMPLX_1:88;
          consider q2 being Real such that
A36:      q2 = q / x;
          q2 in A
          proof
            q2 is R_eal by XXREAL_0:def 1;
            then consider q3 being R_eal such that
A37:        q3 = q2;
            a < q3 & q3 <= b & q3 in REAL
            proof
A38:          q3 <= b
              proof
                g <= q1 by A32,A33,XXREAL_1:3;
                then consider p,o being Real such that
A39:            p = g & o = q1 & p <= o by A22,A33;
                o/x <= p/x by A1,A39,XREAL_1:75;
                hence thesis by A1,A22,A33,A36,A37,A39,XCMPLX_1:90;
              end;
              a < q3
              proof
                x * q2 = q by A1,A36,XCMPLX_1:88;
                hence thesis by A1,A21,A33,A34,A37,XREAL_1:67;
              end;
              hence thesis by A37,A38;
            end;
            hence thesis by A2,A37,XXREAL_1:2;
          end;
          hence thesis by A35,A36,INTEGRA2:def 2;
        end;
        hence thesis by A24,XBOOLE_0:def 10;
      end;
      x * r <= x * s by A1,A2,XREAL_1:67;
      hence thesis by A21,A22,A23,MEASURE5:def 7;
    end;
    case
      a in REAL & b = +infty;
      hence thesis;
    end;
    case
      a = +infty & b = +infty;
      hence thesis;
    end;
  end;
  hence thesis;
end;

theorem
  for A being non empty Interval holds for x being Real st 0 < x
  for B being non empty Interval st B = x ** A holds
  A = [.inf A,sup A.] implies (B = [.inf B,sup B.] &
  for s,t being Real st s = inf A & t = sup A
   holds inf B = x * s & sup B = x * t)
proof
  let A be non empty Interval;
  let x be Real;
  assume
A2: 0 < x;
  let B be non empty Interval;
  assume
A3: B = x ** A;
  A = [.inf A,sup A.] implies (B = [.inf B,sup B.] &
  for s,t being Real st s = inf A & t = sup A
   holds inf B = x * s & sup B = x * t)
  proof
    assume
A7: A = [.inf A,sup A.];
A8: inf A <= sup A by XXREAL_2:40;
    then
Y:   inf A in A & sup A in A by A7,XXREAL_1:1;
A9: for s,t being Real st s = inf A & t = sup A
 holds (inf B = x * s & sup B = x * t)
    proof
      let s,t be Real;
      assume
A10:  s = inf A & t = sup A;
      inf B = x * s & sup B = x * t
      proof
        x * s is R_eal by XXREAL_0:def 1;
        then consider d being R_eal such that
A11:    d = x * s;
        x * t is R_eal by XXREAL_0:def 1;
        then consider g being R_eal such that
A12:    g = x * t;
A13:    x ** A = [.d,g.]
        proof
A14:      x ** A c= [.d,g.]
          proof
            let q be set;
            assume
A15:        q in x ** A;
            then reconsider q as Real;
            consider z2 being Real such that
A16:        z2 in A & q = x * z2 by A15,INTEGRA2:def 2;
            reconsider z2 as R_eal by XXREAL_0:def 1;
A17:        inf A <= z2 & z2 <= sup A by A7,A16,XXREAL_1:1;
            then consider 1o,1r being Real such that
A18:        1o= inf A & 1r = z2 & 1o <= 1r by A10;
            consider 2o,2r being Real such that
A19:        2o= z2 & 2r = sup A & 2o <= 2r by A10,A17;
A20:        x * 1o <= x * 1r by A2,A18,XREAL_1:66;
            x * 2o is R_eal & x * 2r is R_eal by XXREAL_0:def 1;
            then consider 2o1,2r1 being R_eal such that
A21:        2o1 = x * 2o & 2r1 = x * 2r;
            2o1 <= 2r1 by A2,A19,A21,XREAL_1:66;
            hence thesis by A10,A11,A12,A16,A18,A19,A20,A21,XXREAL_1:1;
          end;
          [.d,g.] c= x ** A
          proof
            let q be set;
            assume
A22:        q in [.d,g.];
            then reconsider q as Real by A11,A12,XREAL_0:def 1;
            reconsider q1 = q as R_eal by XXREAL_0:def 1;
            set q2 = q / x;
A23:        q2 in A
            proof
              q2 is R_eal by XXREAL_0:def 1;
              then consider q3 being R_eal such that
A24:          q3 = q2;
              inf A <= q3 & q3 <= sup A & q3 in REAL
              proof
A25:            q3 <= sup A
                proof
                  q1 <= g by A22,XXREAL_1:1;
                  then consider p,o being Real such that
A26:              p = q1 & o = g & p <= o by A12;
                  p/x <= o/x by A2,A26,XREAL_1:74;
                  hence thesis by A2,A10,A12,A24,A26,XCMPLX_1:90;
                end;
                inf A <= q3
                proof
                  d <= q1 by A22,XXREAL_1:1;
                  then consider t,o being Real such that
A27:              t = d & o = q1 & t <= o by A11;
                  x * q2 = q by A2,XCMPLX_1:88;
                  hence thesis by A2,A10,A11,A24,A27,XREAL_1:70;
                end;
                hence thesis by A24,A25;
              end;
              hence thesis by A7,A24,XXREAL_1:1;
            end;
            q = x * q2 by A2,XCMPLX_1:88;
            hence thesis by A23,INTEGRA2:def 2;
          end;
          hence thesis by A14,XBOOLE_0:def 10;
        end;
        s <= t by A10,XXREAL_2:40;
        then x * s <= x * t by A2,XREAL_1:66;
        hence thesis by A3,A11,A12,A13,MEASURE6:40,46;
      end;
      hence thesis;
    end;

    A is closed_interval by A7,A8,Y,MEASURE5:def 6;
    then x ** A is closed_interval by Th10,A2;
    hence thesis by A3,A9,MEASURE6:49;
  end;
  hence thesis;
end;

theorem
  for A being non empty Interval holds for x being Real st 0 < x
  for B being non empty Interval st B = x ** A holds
  A = ].inf A,sup A.] implies (B = ].inf B,sup B.] &
  for s,t being Real st s = inf A & t = sup A
   holds inf B = x * s & sup B = x * t)
proof
  let A be non empty Interval;
  let x be Real;
  assume
A2: 0 < x;
  let B be non empty Interval;
  assume
A3: B = x ** A;
    assume
A7: A = ].inf A,sup A.];
A8: inf A <= sup A by XXREAL_2:40;
    inf A <> sup A by A7;
    then inf A < sup A by A8,XXREAL_0:1;
    then sup A in A by A7,XXREAL_1:2;
    then reconsider b = sup A as real number;
X:  A = ].inf A,b.] by A7;
A9: for s,t being Real st s = inf A & t = sup A
 holds (inf B = x * s & sup B = x * t)
    proof
      let s,t be Real;
      assume
A10:  s = inf A & t = sup A;
      inf B = x * s & sup B = x * t
      proof
        x * s is R_eal by XXREAL_0:def 1;
        then consider d being R_eal such that
A11:    d = x * s;
        x * t is R_eal by XXREAL_0:def 1;
        then consider g being R_eal such that
A12:    g = x * t;
A13:    x ** A = ].d,g.]
        proof
A14:      x ** A c= ].d,g.]
          proof
            let q be set;
            assume
A15:        q in x ** A;
            then reconsider q as Real;
            consider z2 being Real such that
A16:        z2 in A & q = x * z2 by A15,INTEGRA2:def 2;
            reconsider z2 as R_eal by XXREAL_0:def 1;
            reconsider q as R_eal by XXREAL_0:def 1;
A17:        inf A <= z2 & z2 <> inf A & z2 <= sup A by A7,A16,XXREAL_1:2;
            then consider 1o,1r being Real such that
A18:        1o= inf A & 1r = z2 & 1o <= 1r by A10;
            consider 2o,2r being Real such that
A19:        2o= z2 & 2r = sup A & 2o <= 2r by A10,A17;
A20:        1o< 1r by A7,A16,A18,XXREAL_1:2;
A21:        x * 2o <= x * 2r by A2,A19,XREAL_1:66;
            d < q by A2,A10,A11,A16,A18,A20,XREAL_1:70;
            hence thesis by A10,A12,A16,A19,A21,XXREAL_1:2;
          end;
          ].d,g.] c= x ** A
          proof
            let q be set;
            assume
A22:        q in ].d,g.];
            then reconsider q as Real by A12,XREAL_0:def 1;
            reconsider q1 = q as R_eal by XXREAL_0:def 1;
            set q2 = q / x;
A23:        q2 in A
            proof
              reconsider q3 = q2 as R_eal by XXREAL_0:def 1;
              inf A < q3 & q3 <= sup A & q3 in REAL
              proof
A24:            q3 <= sup A
                proof
                  q1 <= g by A22,XXREAL_1:2;
                  then consider p,o being Real such that
A25:              p = q1 & o = g & p <= o by A12;
                  p/x <= o/x by A2,A25,XREAL_1:74;
                  hence thesis by A2,A10,A12,A25,XCMPLX_1:90;
                end;
                inf A < q3
                proof
A26:              d < q1 by A22,XXREAL_1:2;
                  x * q2 = q by A2,XCMPLX_1:88;
                  hence thesis by A2,A10,A11,A26,XREAL_1:66;
                end;
                hence thesis by A24;
              end;
              hence thesis by A7,XXREAL_1:2;
            end;
            q = x * q2 by A2,XCMPLX_1:88;
            hence thesis by A23,INTEGRA2:def 2;
          end;
          hence thesis by A14,XBOOLE_0:def 10;
        end;
        s <= t by A10,XXREAL_2:40;
        then x * s <= x * t by A2,XREAL_1:66;
        hence thesis by A3,A11,A12,A13,MEASURE6:39,45;
      end;
      hence thesis;
    end;
    A is left_open_interval by A8,X,MEASURE5:def 8;
    then B is left_open_interval by A3,Th13,A2;
    hence thesis by A9,MEASURE6:51;
end;

theorem
  for A being non empty Interval holds for x being Real st 0 < x
  for B being non empty Interval st B = x ** A holds
  A = ].inf A,sup A.[ implies (B = ].inf B,sup B.[ &
  for s,t being Real st s = inf A & t = sup A
   holds inf B = x * s & sup B = x * t)
proof
  let A be non empty Interval;
  let x be Real;
  assume
A2: 0 < x;
  let B be non empty Interval;
  assume
A3: B = x ** A;
  A = ].inf A,sup A.[ implies (B = ].inf B,sup B.[ &
  for s,t being Real st s = inf A & t = sup A
   holds inf B = x * s & sup B = x * t)
  proof
    assume
A7: A = ].inf A,sup A.[;
A8: inf A <= sup A by XXREAL_2:40;
A9: for s,t being Real st s = inf A & t = sup A holds
    (inf B = x * s & sup B = x * t & B is open_interval)
    proof
      let s,t be Real;
      assume
A10:  s = inf A & t = sup A;
      inf B = x * s & sup B = x * t & B is open_interval
      proof
        x * s is R_eal by XXREAL_0:def 1;
        then consider d being R_eal such that
A11:    d = x * s;
        x * t is R_eal by XXREAL_0:def 1;
        then consider g being R_eal such that
A12:    g = x * t;
A13:    x ** A = ].d,g.[
        proof
A14:      x ** A c= ].d,g.[
          proof
            let q be set;
            assume
A15:        q in x ** A;
            then reconsider q as Real;
            consider z2 being Real such that
A16:        z2 in A & q = x * z2 by A15,INTEGRA2:def 2;
            reconsider z2 as R_eal by XXREAL_0:def 1;
A17:        inf A <= z2 & z2 <> inf A & z2 <= sup A & z2 <> sup A by A7,A16,
MEASURE5:def 2;
            then consider 1o,1r being Real such that
A18:        1o= inf A & 1r = z2 & 1o <= 1r by A10;
            consider 2o,2r being Real such that
A19:        2o= z2 & 2r = sup A & 2o <= 2r by A10,A17;
            1o< 1r by A7,A16,A18,MEASURE5:def 2;
            then
A20:        x * 1o < x * 1r by A2,XREAL_1:70;
A21:        2o< 2r by A7,A16,A19,MEASURE5:def 2;
            x * 2o is R_eal & x * 2r is R_eal by XXREAL_0:def 1;
            then consider 2o1,2r1 being R_eal such that
A22:        2o1 = x * 2o & 2r1 = x * 2r;
            2o1 < 2r1 by A2,A21,A22,XREAL_1:70;
            hence thesis by A10,A11,A12,A16,A18,A19,A20,A22,MEASURE5:def 2;
          end;
          ].d,g.[ c= x ** A
          proof
            let q be set;
            assume
A23:        q in ].d,g.[;
            then reconsider q as Real;
            q is R_eal by XXREAL_0:def 1;
            then consider q1 being R_eal such that
A24:        q1 = q;
A25:        d < q1 & q1 < g & q1 in REAL by A23,A24,MEASURE5:def 2;
A26:        q = x * (q / x) by A2,XCMPLX_1:88;
            set q2 = q / x;
            q2 in A
            proof
              q2 is R_eal by XXREAL_0:def 1;
              then consider q3 being R_eal such that
A27:          q3 = q2;
              inf A < q3 & q3 < sup A & q3 in REAL
              proof
A28:            q3 < sup A
                proof
                  consider p,o being Real such that
A29:              p = q1 & o = g & p <= o by A12,A25;
                  p/x < o/x by A2,A25,A29,XREAL_1:76;
                  hence thesis by A2,A10,A12,A24,A27,A29,XCMPLX_1:90;
                end;
                inf A < q3
                proof
A30:              d < q1 by A23,A24,MEASURE5:def 2;
                  x * q2 = q by A2,XCMPLX_1:88;
                  hence thesis by A2,A10,A11,A24,A27,A30,XREAL_1:66;
                end;
                hence thesis by A27,A28;
              end;
              hence thesis by A7,A27,MEASURE5:def 2;
            end;
            hence thesis by A26,INTEGRA2:def 2;
          end;
          hence thesis by A14,XBOOLE_0:def 10;
        end;
        s <= t by A10,XXREAL_2:40;
        then x * s <= x * t by A2,XREAL_1:66;
        hence thesis by A3,A11,A12,A13,MEASURE5:def 5,MEASURE6:38,44;
      end;
      hence thesis;
    end;
    x <> 0 & A is open_interval by A2,A7,A8,MEASURE5:def 5;
    then x ** A is open_interval by Th9;
    hence thesis by A3,A9,MEASURE6:48;
  end;
  hence thesis;
end;

theorem
  for A being non empty Interval holds for x being Real st 0 < x
  for B being non empty Interval st B = x ** A holds
  A = [.inf A,sup A.[ implies (B = [.inf B,sup B.[ &
  for s,t being Real st s = inf A & t = sup A
   holds inf B = x * s & sup B = x * t)
proof
  let A be non empty Interval;
  let x be Real;
  assume
A2: 0 < x;
  let B be non empty Interval;
  assume
A3: B = x ** A;
  assume
A7: A = [.inf A,sup A.[;
A8: inf A <= sup A by XXREAL_2:40;
    inf A <> sup A by A7;
    then inf A < sup A by A8,XXREAL_0:1;
    then inf A in A by A7,XXREAL_1:3;
    then reconsider a = inf A as real number;
X:  A = [.a,sup A.[ by A7;
A9: for s,t being Real st s = inf A & t = sup A holds
  (inf B = x * s & sup B = x * t & B is right_open_interval)
  proof
    let s,t be Real;
    assume
A10: s = inf A & t = sup A;
    inf B = x * s & sup B = x * t & B is right_open_interval
    proof
      x * s is R_eal by XXREAL_0:def 1;
      then consider d being R_eal such that
A11:  d = x * s;
      x * t is R_eal by XXREAL_0:def 1;
      then consider g being R_eal such that
A12:  g = x * t;
A13:  x ** A = [.d,g.[
      proof
A14:    x ** A c= [.d,g.[
        proof
          let q be set;
          assume
A15:      q in x ** A;
          then reconsider q as Real;
          consider z2 being Real such that
A16:      z2 in A & q = x * z2 by A15,INTEGRA2:def 2;
          reconsider z2 as R_eal by XXREAL_0:def 1;
A17:      inf A <= z2 & z2 <= sup A & z2 <> sup A by A7,A16,XXREAL_1:3;
          then consider 1o,1r being Real such that
A18:      1o= inf A & 1r = z2 & 1o <= 1r by A10;
          consider 2o,2r being Real such that
A19:      2o= z2 & 2r = sup A & 2o <= 2r by A10,A17;
A20:      x * 1o <= x * 1r by A2,A18,XREAL_1:66;
A21:      2o< 2r by A7,A16,A19,XXREAL_1:3;
          x * 2o is R_eal & x * 2r is R_eal by XXREAL_0:def 1;
          then consider 2o1,2r1 being R_eal such that
A22:      2o1 = x * 2o & 2r1 = x * 2r;
          2o1 < 2r1 by A2,A21,A22,XREAL_1:70;
          hence thesis by A10,A11,A12,A16,A18,A19,A20,A22,XXREAL_1:3;
        end;
        [.d,g.[ c= x ** A
        proof
          let q be set;
          assume
A23:      q in [.d,g.[;
          then reconsider q as Real by A11,XREAL_0:def 1;
          reconsider q1 = q as R_eal by XXREAL_0:def 1;
A24:      d <= q1 & q1 < g & q1 in REAL by A23,XXREAL_1:3;
A25:      q = x * (q / x) by A2,XCMPLX_1:88;
          set q2 = q / x;
          q2 in A
          proof
            reconsider q3 = q2 as R_eal by XXREAL_0:def 1;
            inf A <= q3 & q3 < sup A & q3 in REAL
            proof
A26:          q3 < sup A
              proof
A27:            q1 < g by A23,XXREAL_1:3;
                consider p,o being Real such that
A28:            p = q1 & o = g & p <= o by A12,A24;
                p/x < o/x by A2,A27,A28,XREAL_1:76;
                hence thesis by A2,A10,A12,A28,XCMPLX_1:90;
              end;
              d <= q1 by A23,XXREAL_1:3;
              then consider t,o being Real such that
A29:          t = d & o = q1 & t <= o by A11;
              x * q2 = q by A2,XCMPLX_1:88;
              hence thesis by A2,A10,A11,A26,A29,XREAL_1:70;
            end;
            hence thesis by A7,XXREAL_1:3;
          end;
          hence thesis by A25,INTEGRA2:def 2;
        end;
        hence thesis by A14,XBOOLE_0:def 10;
      end;
        s <= t by A10,XXREAL_2:40;
      then x * s <= x * t by A2,XREAL_1:66;
      hence thesis by A3,A11,A12,A13,MEASURE5:def 7,MEASURE6:41,47;
    end;
    hence thesis;
  end;
  A is right_open_interval by A8,X,MEASURE5:def 7;
  then x ** A is right_open_interval by A2,Th11;
  hence thesis by A3,A9,MEASURE6:50;
end;

theorem Th19:
  for A being Interval holds for x being Real holds x ** A is Interval
proof
  let A be Interval;
  let x be Real;
  per cases;
  suppose x = 0;
    hence thesis by Th8;
  end;
  suppose
A1: x <> 0;
    now per cases by MEASURE5:def 9;
      case A is open_interval;
        then x ** A is open_interval by A1,Th9;
        hence thesis by MEASURE5:def 9;
      end;
      case A is closed_interval;
        then x ** A is closed_interval by A1,Th10;
        hence thesis by MEASURE5:def 9;
      end;
      case
A2:     A is right_open_interval;
        per cases by A1;
        case x < 0;
          then x ** A is left_open_interval by A2,Th12;
          hence thesis by MEASURE5:def 9;
        end;
        case 0 < x;
          then x ** A is right_open_interval by A2,Th11;
          hence thesis by MEASURE5:def 9;
        end;
      end;
      case
A3:     A is left_open_interval;
        now per cases by A1;
          case x < 0;
            then x ** A is right_open_interval by A3,Th14;
            hence thesis by MEASURE5:def 9;
          end;
          case 0 < x;
            then x ** A is left_open_interval by A3,Th13;
            hence thesis by MEASURE5:def 9;
          end;
        end;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
end;

registration
  let A be connected Subset of REAL;
  let x be Real;
  cluster x ** A -> connected;
  correctness by Th19;
end;

Lx1:
 for A being non empty Subset of REAL, x being Real
  st x > 0 & x**A is bounded_above
  holds A is bounded_above
proof
 let A be non empty Subset of REAL, x be Real such that
Z1: x > 0 and
Z2: x**A is bounded_above;
  consider r being real number such that
W: r in A by MEMBERED:9;
R:  sup(x**A) <> +infty by Z2,XXREAL_2:77;
 per cases by R,XXREAL_0:14;
 suppose
S:  sup(x**A) = -infty;
 take 0;
T: -infty is UpperBound of x**A by S,XXREAL_2:def 3;
  x*r in x**A by W,INTEGRA2:def 2;
  hence thesis by T,XXREAL_0:6,XXREAL_2:def 1;
 end;
 suppose sup(x**A) in REAL;
  then reconsider r = sup(x**A) as Real;
 take r/x;
 let z be ext-realnumber;
 assume
Z: z in A;
   then reconsider z1=z as Real;
   x" ** (x ** A) = A by Z1,Th2;
   then consider s being Real such that
W1: s in x**A and
W2: z = x" * s by Z,INTEGRA2:def 2;
  s <= r by W1,XXREAL_2:4;
  then s/x <= r/x by Z1,XREAL_1:74;
 hence thesis by W2;
 end;
end;

theorem Tx1:
 for A being non empty Subset of REAL, x being Real, y being R_eal
   st x = y & 0 <= y
  holds sup(x ** A) = y * sup A
proof let A be non empty Subset of REAL, x being Real, y being R_eal such that
Z1: x = y and
Z2: 0 <= y;
   reconsider Y = x ** A as non empty Subset of REAL by INTEGRA2:7;
 per cases;
 suppose
SS:  A is not bounded_above;
  per cases by Z2;
  suppose
S:  y = 0;
   then x ** A = {0} by Z1,INTEGRA2:42;
  hence sup(x ** A) = 0 by XXREAL_2:11
  .= y * sup A by S;
  end;
  suppose
S:  y > 0;
   Y is not bounded_above by Lx1,S,SS,Z1;
  hence sup(x ** A) = +infty by XXREAL_2:77
        .= y * +infty by S,XXREAL_3:def 5
        .= y * sup A by SS,XXREAL_2:77;
  end;
 end;
 suppose
    A is bounded_above;
  then reconsider X = A as non empty bounded_above (real-membered set);
   reconsider u = upper_bound X as Real by XREAL_0:def 1;
   x ** X is bounded_above by Z1,Z2,INTEGRA2:9;
   then reconsider Y as non empty bounded_above (real-membered set);
  thus sup(x ** A) = upper_bound Y
    .= x * u by Z1,Z2,INTEGRA2:13
    .= y * sup A by Z1,EXTREAL1:13;
  end;
end;

Lx2:
 for A being non empty Subset of REAL, x being Real
  st x > 0 & x**A is bounded_below
  holds A is bounded_below
proof
 let A be non empty Subset of REAL, x be Real such that
Z1: x > 0 and
Z2: x**A is bounded_below;
  consider r being real number such that
W: r in A by MEMBERED:9;
R:  inf(x**A) <> -infty by Z2,XXREAL_2:78;
 per cases by R,XXREAL_0:14;
 suppose
S:  inf(x**A) = +infty;
 take 0;
T: +infty is LowerBound of x**A by S,XXREAL_2:def 4;
  x*r in x**A by W,INTEGRA2:def 2;
  hence thesis by T,XXREAL_0:4,XXREAL_2:def 2;
 end;
 suppose inf(x**A) in REAL;
  then reconsider r = inf(x**A) as Real;
 take r/x;
 let z be ext-realnumber;
 assume
Z: z in A;
   then reconsider z1=z as Real;
   x" ** (x ** A) = A by Z1,Th2;
   then consider s being Real such that
W1: s in x**A and
W2: z = x" * s by Z,INTEGRA2:def 2;
  r/x <= s/x by Z1,W1,XREAL_1:74,XXREAL_2:3;
 hence thesis by W2;
 end;
end;

theorem Tx2:
 for A being non empty Subset of REAL, x being Real, y being R_eal
  st x = y & 0 <= y
  holds inf(x ** A) = y * inf A
proof let A be non empty Subset of REAL, x being Real, y being R_eal such that
Z1: x = y and
Z2: 0 <= y;
   reconsider Y = x ** A as non empty Subset of REAL by INTEGRA2:7;
 per cases;
 suppose
SS:  A is not bounded_below;
  per cases by Z2;
  suppose
S:  y = 0;
   then x ** A = {0} by Z1,INTEGRA2:42;
  hence inf(x ** A) = 0 by XXREAL_2:13
  .= y * inf A by S;
  end;
  suppose
S:  y > 0;
   Y is not bounded_below by Lx2,S,SS,Z1;
  hence inf(x ** A) = -infty by XXREAL_2:78
        .= y * -infty by S,XXREAL_3:def 5
        .= y * inf A by SS,XXREAL_2:78;
  end;
 end;
 suppose
    A is bounded_below;
  then reconsider X = A as non empty bounded_below (real-membered set);
   reconsider u = lower_bound X as Real by XREAL_0:def 1;
   x ** X is bounded_below by Z1,Z2,INTEGRA2:11;
   then reconsider Y as non empty bounded_below (real-membered set);
  thus inf(x ** A) = lower_bound Y
    .= x * u by Z1,Z2,INTEGRA2:15
    .= y * inf A by Z1,EXTREAL1:13;
  end;
end;

theorem
  for A being Interval for x being Real st 0 <= x
  for y being Real st y = diameter(A) holds x * y = diameter(x ** A)
proof
  let A be Interval;
  let x be Real such that
Z1: 0 <= x;
  let y be Real such that
Z2: y = diameter(A);
  per cases;
  suppose
S:  A is empty;
X: now assume x ** A is non empty;
     then consider y being real number such that
W:    y in x ** A by MEMBERED:9;
     reconsider y as Real by XREAL_0:def 1;
     ex z being Real st z in A & y = x * z by W,INTEGRA2:def 2;
    hence contradiction by S;
   end;
   thus x * y = 0 by S,Z2,MEASURE5:60
      .= diameter(x ** A) by X,MEASURE5:60;
  end;
  suppose
S:  A is non empty;
   then reconsider AA = A as non empty Subset of REAL;
    consider z being real number such that
W:    z in A by S,MEMBERED:9;
      reconsider z as Real by XREAL_0:def 1;
XX:   x * z in x ** A by W,INTEGRA2:def 2;
   reconsider z = x as R_eal by XXREAL_0:def 1;
    reconsider u = x as R_eal by XXREAL_0:def 1;
Y2: inf(x ** AA) = u * inf AA by Tx2,Z1;
  thus x * y = z * diameter A by Z2,EXTREAL1:13
     .= z * (sup A - inf A) by S,MEASURE5:def 10
     .= z * sup A - (z * inf A) by XXREAL_3:112
     .= sup(x ** A) - inf(x ** A) by Tx1,Z1,Y2
     .= diameter(x ** A) by XX,MEASURE5:def 10;
  end;
end;

theorem Th23:
  for eps being Real st 0 < eps holds
  ex n being Element of NAT st 1 < 2|^n * eps
proof
  let eps be Real;
  assume
A1: 0 < eps;
  consider n being Element of NAT such that
A2: 1 / eps < n by SEQ_4:10;
  1/eps < 2|^n by A2,NEWTON:105,XXREAL_0:2;
  then
A3: 1/eps * eps < 2|^n * eps by A1,XREAL_1:70;
  take n;
  thus thesis by A1,A3,XCMPLX_1:88;
end;

theorem Th24:
  for a,b being Real st 0 <= a & 1 < b - a holds
  ex n being Element of NAT st a < n & n < b
proof
  let a,b be Real;
  assume that
A1: 0 <= a and
A2: 1 < b - a;
A3:  a < [\a/]+1 by INT_1:52;
   then reconsider n = [\a/]+1 as Element of NAT by A1,INT_1:16;
  take n;
  thus a < n by A3;
A4:  1+a < b by A2,XREAL_1:22;
   [\a/] <= a by INT_1:def 4;
   then [\a/]+1 <= a+1 by XREAL_1:8;
  hence thesis by A4,XXREAL_0:2;
end;

canceled 2;

theorem
  for n being Element of NAT holds dyadic(n) c= DYADIC
proof
  let n be Element of NAT;
  for x being set holds x in dyadic(n) implies x in DYADIC by URYSOHN1:def 4;
  hence thesis by TARSKI:def 3;
end;

theorem Th28:
  for a,b being Real st a < b & 0 <= a & b <= 1 holds
  ex c being Real st c in DYADIC & a < c & c < b
proof
  let a,b be Real;
  assume
A1: a < b & 0 <= a & b <= 1;
  set eps = b - a;
  consider n being Element of NAT such that
A2: 1 < 2|^n * eps by Th23,A1,XREAL_1:52;
  set aa = 2|^n * a, bb = 2|^n * b;
A3: 1 < bb - aa by A2;
A4: 0 < 2|^n by NEWTON:102;
  consider m being Element of NAT such that
A5: aa < m & m < bb by A3,Th24,A1;
  a < m / 2|^n & m / 2|^n < b by A4,A5,XREAL_1:83,85;
  then m / 2|^n < 1 by A1,XXREAL_0:2;
  then
A6: 0 <= m & m < 2|^n by A4,XREAL_1:183;
  set x = m / 2|^n;
A7: x in dyadic(n) by A6,URYSOHN1:def 3;
  take x;
  thus thesis by A4,A5,A7,URYSOHN1:def 4,XREAL_1:83,85;
end;

theorem Th29:
  for a,b being Real st a < b ex c being Real st c in DOM & a < c & c < b
proof
  let a,b be Real;
  assume
A1: a < b;
  per cases;
  suppose
A2: a < 0 & b <= 1;
    now per cases;
      case
A3:     b <= 0;
        consider c being real number such that
A4:     a < c & c < b by A1,XREAL_1:7;
A5:     halfline 0 = {g where g is Real : g<0} by XXREAL_1:229;
        reconsider c as Real by XREAL_0:def 1;
        c in halfline 0 by A3,A4,A5;
        then c in (halfline 0) \/ DYADIC by XBOOLE_0:def 3;
        then c in DOM by URYSOHN1:def 5,XBOOLE_0:def 3;
        hence thesis by A4;
      end;
      case
A6:     0 < b;
        set a1 = 0;
        consider c being Real such that
A7:     c in DYADIC & a1 < c & c < b by A2,A6,Th28;
        c in (halfline 0) \/ DYADIC by A7,XBOOLE_0:def 3;
        then c in DOM by URYSOHN1:def 5,XBOOLE_0:def 3;
        hence thesis by A2,A7;
      end;
    end;
    hence thesis;
  end;
  suppose
A8: a < 0 & 1 < b;
    consider a1,b1 being Real such that
A9: a1 = 0 & b1 = 1;
    consider c being Real such that
A10: c in DYADIC & a1 < c & c < b1 by A9,Th28;
A11: c in (halfline 0) \/ DYADIC by A10,XBOOLE_0:def 3;
    take c;
    thus thesis by A8,A9,A10,A11,URYSOHN1:def 5,XBOOLE_0:def 3,XXREAL_0:2;
  end;
  suppose 0 <= a & b <= 1;
    then consider c being Real such that
A12: c in DYADIC & a < c & c < b by A1,Th28;
A13: c in (halfline 0) \/ DYADIC by A12,XBOOLE_0:def 3;
    take c;
    thus thesis by A12,A13,URYSOHN1:def 5,XBOOLE_0:def 3;
  end;
  suppose
A14: 0 <= a & 1 < b;
    now per cases;
      case
A15:    1 <= a;
        consider c being real number such that
A16:    a < c & c < b by A1,XREAL_1:7;
A17:    right_open_halfline 1 = {g where g is Real : 1<g} by XXREAL_1:230;
        reconsider c as Real by XREAL_0:def 1;
        1 < c by A15,A16,XXREAL_0:2;
        then c in right_open_halfline 1 by A17;
        then c in DYADIC \/ (right_open_halfline 1) by XBOOLE_0:def 3;
        then c in (halfline 0) \/ (DYADIC \/ right_open_halfline 1)
        by XBOOLE_0:def 3;
        then c in DOM by URYSOHN1:def 5,XBOOLE_1:4;
        hence thesis by A16;
      end;
      case
A18:    a < 1;
        set b1 = 1;
        consider c being Real such that
A19:    c in DYADIC & a < c & c < b1 by A14,A18,Th28;
        c in (halfline 0) \/ DYADIC by A19,XBOOLE_0:def 3;
        then
A20:    c in DOM by URYSOHN1:def 5,XBOOLE_0:def 3;
        a < c & c < b by A14,A19,XXREAL_0:2;
        hence thesis by A20;
      end;
    end;
    hence thesis;
  end;
end;

theorem
  for A being non empty Subset of ExtREAL holds
  for a,b being R_eal st A c= [.a,b.] holds a <= inf A & sup A <= b
proof
  let A be non empty Subset of ExtREAL;
  let a,b be R_eal;
  assume
A1: A c= [.a,b.];
  reconsider B = [.a,b.] as non empty Subset of ExtREAL
  by A1,MEMBERED:2;
A3: a <= inf A
  proof
    for x being ext-real number holds (x in B implies a <= x)
    by XXREAL_1:1;
    then a is LowerBound of B by XXREAL_2:def 2;
    then a is LowerBound of A by A1,XXREAL_2:5;
    hence thesis by XXREAL_2:def 4;
  end;
  sup A <= b
  proof
    for x being ext-real number st x in B holds x <= b by XXREAL_1:1;
    then b is UpperBound of B by XXREAL_2:def 1;
    then b is UpperBound of A by A1,XXREAL_2:6;
    hence thesis by XXREAL_2:def 3;
  end;
  hence thesis by A3;
end;

theorem
  0 in DYADIC & 1 in DYADIC
proof
  0 in dyadic(0) & 1 in dyadic(0) by URYSOHN1:12;
  hence thesis by URYSOHN1:def 4;
end;

theorem
  DYADIC c= [. 0,1 .]
proof
  let x be set;
  assume
A2: x in DYADIC;
  then reconsider x as Real;
  consider n being Element of NAT such that
A3: x in dyadic(n) by A2,URYSOHN1:def 4;
  reconsider x as R_eal by XXREAL_0:def 1;
  0 <= x & x <= 1 by A3,URYSOHN1:5;
  hence thesis by XXREAL_1:1;
end;

theorem
  for n,k being Element of NAT st n <= k holds dyadic(n) c= dyadic(k)
proof
  let n,k be Element of NAT;
  assume
A1: n <= k;
A2: for s being Element of NAT holds dyadic(n) c= dyadic(n+s)
  proof
    defpred P[Element of NAT] means dyadic(n) c= dyadic(n+$1);
A3: P[0];
A4: for k being Element of NAT st P[k] holds P[(k+1)]
    proof
      let k be Element of NAT;
      assume
A5:   dyadic(n) c= dyadic(n+k);
      dyadic(n+k) c= dyadic(n+k+1) by URYSOHN1:11;
      hence thesis by A5,XBOOLE_1:1;
    end;
    for k be Element of NAT holds P[k] from NAT_1:sch 1(A3,A4);
    hence thesis;
  end;
  consider s being Nat such that
A6: k = n + s by A1,NAT_1:10;
  s in NAT by ORDINAL1:def 13;
  hence thesis by A2,A6;
end;

theorem Th34: :: JGRAPH_1:31
  for a,b,c,d being Real st a < c & c < b & a < d & d < b holds
  abs(d - c) < b - a
proof
  let a,b,c,d be Real;
  assume
A1: a < c & c < b & a < d & d < b;
  then
A2: a + d < c + b by XREAL_1:10;
  then
A3: d - c < b - a by XREAL_1:23;
A4: c + a < b + d by A1,XREAL_1:10;
  then c - d <> b - a & c - d <= b - a by XREAL_1:23;
  then -(b - a) <= -(c - d) by XREAL_1:26;
  then
A5: abs(d - c) <= b - a by A3,ABSVALUE:12;
  abs(d - c) <> b - a
  proof
    assume
A6: abs(d - c) = b - a;
A7: d - c = b - a or d - c = -(b - a)
    proof
      per cases;
      suppose 0 <= d - c;
        hence thesis by A6,ABSVALUE:def 1;
      end;
      suppose not 0 <= d - c;
        then b - a = -(d - c) by A6,ABSVALUE:def 1;
        hence thesis;
      end;
    end;
    now per cases by A7;
      case d - c = b - a;
        hence thesis by A2;
      end;
      case d - c = -(b - a);
        hence thesis by A4;
      end;
    end;
    hence thesis;
  end;
  hence thesis by A5,XXREAL_0:1;
end;

theorem
  for eps being Real st 0 < eps holds for d being Real st 0 < d & d <= 1 holds
  ex r1,r2 being Real st r1 in DYADIC \/ (right_open_halfline 1) &
  r2 in DYADIC \/ (right_open_halfline 1) &
  0 < r1 & r1 < d & d < r2 & r2 - r1 < eps
proof
  let eps be Real;
  assume
A1: 0 < eps;
  let d be Real;
  assume
A2: 0 < d & d <= 1;
  consider eps1 being real number such that
A3: 0 < eps1 & eps1 < eps by A1,XREAL_1:7;
  reconsider eps1 as Real by XREAL_0:def 1;
  per cases;
  suppose
A4: eps1 < d;
    d - eps1 < d - 0 by A3,XREAL_1:17;
    then ex c being Real st c in DOM & d - eps1 < c & c < d by Th29;
    then consider r1 being Real such that
A5: d - eps1 < r1 & r1 < d & r1 in DOM;
    eps - eps < eps - eps1 by A3,XREAL_1:17;
    then d + 0 < d + (eps - eps1) by XREAL_1:10;
    then ex c being Real st c in DOM & d < c & c < d + (eps - eps1) by Th29;
    then consider r2 being Real such that
A6: d < r2 & r2 < d + (eps - eps1) & r2 in DOM;
    take r1,r2;
A7: 0 < d - eps1 by A4,XREAL_1:52;
    (r1 in (halfline 0) \/ DYADIC or r1 in right_open_halfline 1) &
    (r2 in halfline 0 \/ DYADIC or r2 in right_open_halfline 1)
    by A5,A6,URYSOHN1:def 5,XBOOLE_0:def 3;
    then
A8: (r1 in halfline 0 or r1 in DYADIC or r1 in right_open_halfline 1) &
    (r2 in halfline 0 or r2 in DYADIC or
    r2 in right_open_halfline 1) by XBOOLE_0:def 3;
A9: r1 < r2 by A5,A6,XXREAL_0:2;
    then
A10: r1 < d + (eps - eps1) by A6,XXREAL_0:2;
A11: d - eps1 < r2 by A5,A9,XXREAL_0:2;
    (d + (eps - eps1)) - (d - eps1) = eps;
    then
A12: abs(r2 - r1) < eps by A5,A6,A10,A11,Th34;
    0 <= r2 - r1 by A9,XREAL_1:50;
    hence thesis by A5,A6,A7,A8,A12,ABSVALUE:def 1,XBOOLE_0:def 3,XXREAL_1:233;
  end;
  suppose
A13: d <= eps1;
    consider r1 being Real such that
A14: r1 in DOM & 0 < r1 & r1 < d by A2,Th29;
    0 + d < r1 + eps1 by A13,A14,XREAL_1:10;
    then ex c being Real st c in DOM & d < c & c < r1 + eps1 by Th29;
    then consider r2 being Real such that
A15: d < r2 & r2 < r1 + eps1 & r2 in DOM;
    take r1,r2;
A16: not r1 in halfline 0 by A14,XXREAL_1:233;
A17: not r2 in halfline 0 by A2,A15,XXREAL_1:233;
    (r1 in halfline 0 \/ DYADIC or r1 in right_open_halfline 1) &
    (r2 in halfline 0 \/ DYADIC or r2 in right_open_halfline 1)
    by A14,A15,URYSOHN1:def 5,XBOOLE_0:def 3;
    then
A18: (r1 in DYADIC or r1 in right_open_halfline 1) &
    (r2 in DYADIC or r2 in right_open_halfline 1) by A16,A17,XBOOLE_0:def 3;
    r2 - r1 < r1 + eps1 - r1 by A15,XREAL_1:11;
    hence thesis by A3,A14,A15,A18,XBOOLE_0:def 3,XXREAL_0:2;
  end;
end;

begin :: Addenda

:: missing, 2008.06.10, A.T.

theorem
 for A being non empty Subset of REAL, x being Real
  st x > 0 & x**A is bounded_above
  holds A is bounded_above by Lx1;

theorem
 for A being non empty Subset of REAL, x being Real
  st x > 0 & x**A is bounded_below
  holds A is bounded_below by Lx2;
