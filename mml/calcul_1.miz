:: A Sequent Calculus for First-Order Logic
::  by Patrick Braselmann and Peter Koepke
::
:: Received September 25, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabulary RELAT_1, CQC_THE1, CQC_LANG, QC_LANG1, FINSEQ_1, ZF_LANG, ZF_MODEL,
      BOOLE, FUNCT_1, FUNCT_4, CALCUL_1, FINSET_1, VALUAT_1, FUNCOP_1, CAT_1,
      MCART_1, QMAX_1, ORDINAL2, FINSEQ_2, CARD_1, TARSKI, SUBSTUT1, SUBLEMMA;
 notation TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, ORDINAL1, ARYTM_2, FINSEQ_1,
      FUNCT_1, QC_LANG1, QC_LANG2, QC_LANG3, PARTFUN1, NUMBERS, ORDINAL2,
      FINSEQ_2, CARD_1, CARD_2, XCMPLX_0, XREAL_0, REAL_1, NAT_1, CQC_LANG,
      RELAT_1, FINSET_1, FUNCT_4, VALUAT_1, CQC_THE1, FUNCOP_1, FINSUB_1,
      RELSET_1, FUNCT_2, FRAENKEL, MARGREL1, CQC_SIM1, DOMAIN_1, MCART_1,
      SUBSTUT1, SUBSTUT2, SUBLEMMA;
 constructors TARSKI, QC_LANG3, DOMAIN_1, CQC_THE1, ARYTM_0, CQC_THE3, CARD_2,
      RECDEF_1, CQC_SIM1, FINSEQ_1, PARTFUN1, MARGREL1, ARYTM_2, CQC_LANG,
      NAT_1, MCART_1, FUNCT_1, SUBSTUT1, SUBSTUT2, SUBLEMMA, FUNCT_4, FUNCT_2,
      FINSEQ_2, CARD_1, REAL_1, QC_LANG2, MEMBERED, XREAL_0, XCMPLX_0,
      XBOOLE_0, NUMBERS, ORDINAL3, WELLORD2;
 clusters NAT_1, FINSEQ_1, ARYTM_3, RELSET_1, CQC_LANG, SUBLEMMA, XREAL_0,
      ZFMISC_1, REAL_1, XCMPLX_0, XBOOLE_0, FUNCOP_1, SUBSET_1, QC_LANG1,
      MARGREL1, FUNCT_1, SUBSTUT1, PARTFUN1, FINSET_1, RELAT_1, NUMBERS,
      ORDINAL2;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, XBOOLE_0, CQC_THE3, MARGREL1, FUNCT_1, CQC_LANG;
 theorems TARSKI, FUNCT_1, MCART_1, XBOOLE_0, XBOOLE_1, CQC_LANG, QC_LANG1,
      ZFMISC_1, RELAT_1, QC_LANG3, QC_LANG2, SUBSTUT1, FUNCT_4, SUBLEMMA,
      NAT_1, FINSEQ_1, FINSEQ_3, REAL_1, AXIOMS, VALUAT_1, FINSEQ_2, SUBSTUT2,
      FUNCOP_1, CQC_THE1, FINSET_1, CARD_2, XCMPLX_1, CQC_SIM1, CARD_1, CARD_4,
      GRFUNC_1, AMI_5, GRAPH_2, GRAPH_5;
 schemes XBOOLE_0, NAT_1, FUNCT_1;

begin :: Preliminaries

 reserve a,b,c,d for set,
         i,j,k,l,m,n for Nat,
         p,q,r for Element of CQC-WFF,
         x,y,y0 for bound_QC-variable,
         X for Subset of CQC-WFF,
         A for non empty set,
         J for interpretation of A,
         v,w for Element of Valuations_in A,
         Sub for CQC_Substitution,
         S for Element of CQC-Sub-WFF,
         xSQ for second_Q_comp of [S,x],
         f,f1,f2,g,h,h1 for FinSequence of CQC-WFF;

registration let g be FinSequence, N be set;
  cluster g|N -> FinSubsequence-like;
  coherence by FINSEQ_1:69;
end;

definition let D be non empty set, f be FinSequence of D;
  func Ant(f) -> FinSequence of D means
  :Def1: for i st len f = i+1 holds it = f|(Seg i) if len f > 0
         otherwise it = {};
  existence
  proof
A1:  len f > 0 implies ex g being FinSequence of D st
        for i st len f = i+1 holds g = f|(Seg i)
    proof
      assume len f > 0; then
      consider j such that A2: len f = j+1 by NAT_1:22;
      take g = f|(Seg j);
      reconsider g as FinSequence by FINSEQ_1:19;
      now let a such that A3: a in rng g;
        rng g c= rng f by RELAT_1:99; then
        a in rng f by A3;
        hence a in D;
      end; then
      rng g c= D by TARSKI:def 3; then
      reconsider g as FinSequence of D by FINSEQ_1:def 4;
      for i st len f = i+1 holds g = f|(Seg i) by A2,XCMPLX_1:2;
      hence thesis;
    end;
    not len f > 0 implies ex g being FinSequence of D st g = {}
    proof
      assume not len f > 0;
      take g = {};
      thus thesis by FINSEQ_1:29;
    end;
    hence thesis by A1;
  end;
  uniqueness
  proof
    let g,h be FinSequence of D;
    len f > 0 & (for i st len f = i+1 holds g = f|(Seg i)) &
                (for i st len f = i+1 holds h = f|(Seg i)) implies g = h
    proof
      assume A1: len f > 0 & (for i st len f = i+1 holds g = f|(Seg i)) &
                         (for i st len f = i+1 holds h = f|(Seg i)); then
      consider j such that A2: len f = j+1 by NAT_1:22;
       g = f|(Seg j) & h = f|(Seg j) by A1,A2;
       hence thesis;
    end;
    hence thesis;
  end;
  consistency;
end;

definition let D be non empty set, f be FinSequence of D;
  assume A1: len f > 0;
  func Suc(f) -> Element of D equals :Def2:
    f.(len f);
  coherence
  proof
    0+1 <= len f & len f <= len f by A1,NAT_1:38; then
    len f in dom f by FINSEQ_3:27; then
    f.(len f) in rng f by FUNCT_1:12;
    hence thesis;
  end;
end;

definition let D be non empty set, p be Element of D,
               f be FinSequence of D;
  pred p is_tail_of f means
  :Def3: ex i st i in dom f & f.i = p;
end;

definition let f,g;
  pred f is_Subsequence_of g means
  :Def6: ex N being Subset of NAT st f c= Seq (g|N);
end;

theorem Th4:
  f is_Subsequence_of g implies rng f c= rng g &
    ex N being Subset of NAT st rng f c= rng (g|N)
  proof
    assume f is_Subsequence_of g; then
    consider N being Subset of NAT such that A1: f c= Seq (g|N) by Def6;
A5: now let a be set such that A2: a in rng f;
      consider n such that A3: n in dom f & f.n = a by A2,FINSEQ_2:11;
A4:   dom f c= dom Seq (g|N) by A1,RELAT_1:25;
      [n,f.n] in f by A3,FUNCT_1:8; then
      (Seq (g|N)).n = a by A1,A3,FUNCT_1:8; then
A5:   a in rng (Seq (g|N)) by A3,A4,FUNCT_1:12;
      rng (Seq (g|N)) = rng (Sgm (dom (g|N)) * (g|N)) by FINSEQ_1:def 14; then
      rng (Seq (g|N)) c= rng (g|N) by RELAT_1:45;
      hence a in rng (g|N) by A5;
    end; then
A6: rng f c= rng (g|N) by TARSKI:def 3;
    rng (g|N) c= rng g by RELAT_1:99;
    hence rng f c= rng g by A6,XBOOLE_1:1;
    take N;
    thus thesis by A5,TARSKI:def 3;
  end;

theorem Th6:
  len f > 0 implies len Ant(f)+1 = len f & len Ant(f) < len f
   proof
     assume A1: len f > 0; then
     consider i such that A2: len f = i+1 by NAT_1:22;
     Ant(f) = f|(Seg i) by A1,A2,Def1; then
     dom Ant(f) = dom f /\ Seg i by FUNCT_1:68; then
     Seg (len Ant(f)) = dom f /\ Seg i by FINSEQ_1:def 3; then
A3:  Seg (len Ant(f)) = Seg (len f) /\ Seg i by FINSEQ_1:def 3;
     i <= len f by A2,NAT_1:29; then
     Seg i c= Seg (len f) by FINSEQ_1:7; then
A5:  Seg (len Ant(f)) = Seg i by A3,XBOOLE_1:28; then
A4:  len Ant(f) = i by FINSEQ_1:8;
     thus len Ant(f)+1 = len f by A5,A2,FINSEQ_1:8;
     thus thesis by A2,A4,NAT_1:38;
   end;

theorem Th7:
  len f > 0 implies f = Ant(f)^<*Suc(f)*> & rng f = rng Ant(f) \/ {Suc(f)}
  proof
    assume A1: len f > 0; then
A2: len f = len Ant(f)+1 by Th6; then
    len f = len Ant(f) + len <*Suc(f)*> by FINSEQ_1:56; then
A3: len f = len (Ant(f)^<*Suc(f)*>) by FINSEQ_1:35;
A13: now let j such that A4: j in Seg len f;
A5:   1 <= j & j <= len Ant(f)+1 by A2,A4,FINSEQ_1:3;
A7:   now assume j <= len Ant(f); then
A8:     j in dom Ant(f) by A5,FINSEQ_3:27;
        Ant(f) = f|Seg len Ant(f) by A1,A2,Def1; then
        Ant(f) = f|dom Ant(f) by FINSEQ_1:def 3; then
        f.j = (Ant(f)).j by A8,FUNCT_1:72;
        hence f.j = (Ant(f)^<*Suc(f)*>).j by A8,FINSEQ_1:def 7;
      end;
      now assume A10: j = len Ant(f)+1; then
        j = len f by A1,Th6; then
        f.j = Suc(f) by A1,Def2; then
A11:    f.j = <*Suc(f)*>.1 by FINSEQ_1:57;
        1 in Seg 1 by FINSEQ_1:3; then
        1 in dom <*Suc(f)*> by FINSEQ_1:55;
        hence f.j = (Ant(f)^<*Suc(f)*>).j by A10,A11,FINSEQ_1:def 7;
      end;
      hence f.j = (Ant(f)^<*Suc(f)*>).j by A5,A7,NAT_1:26;
    end; then
     f = (Ant(f)^<*Suc(f)*>) by A3,FINSEQ_2:10; then
     rng f = rng Ant(f) \/ rng <*Suc(f)*> by FINSEQ_1:44;
     hence thesis by A3,A13,FINSEQ_1:55,FINSEQ_2:10;
  end;

theorem Th9:
  len f > 1 implies len Ant(f) > 0
   proof
     assume A1: len f > 1;
     len Ant(f)+1 > 1 by A1,Th6; then
     1 <= len Ant(f) by NAT_1:38;
     hence thesis;
   end;

theorem Th24:
  Suc(f^<*p*>) = p & Ant(f^<*p*>) = f
    proof
      set fin = f^<*p*>;
A1:   len fin = len f + 1 by FINSEQ_2:19; then
A2:   0 < len fin by NAT_1:19;
      thus Suc(f^<*p*>) = p
      proof
        fin.(len fin) = p by A1,FINSEQ_1:59;
        hence thesis by A2,Def2;
      end;
      thus Ant(f^<*p*>) = f
      proof
        set fin = f^<*p*>;
        now let a be set; assume a in f; then
          consider k such that A3: k in dom f & a = [k,f.k] by FINSEQ_1:16;
A4:       k in dom fin by A3,FINSEQ_2:18;
          f.k = fin.k by A3,FINSEQ_1:def 7;
          hence a in fin by A3,A4,FUNCT_1:8;
        end; then
        f c= fin by TARSKI:def 3; then
A6:     f = fin|(dom f) by GRFUNC_1:64;
        f = fin|(Seg len f) by A6,FINSEQ_1:def 3;
        hence thesis by A1,A2,Def1;
      end;
  end;

 reserve fin,fin1 for FinSequence;

theorem Th32:
  len fin <= len (fin^fin1) & len fin1 <= len (fin^fin1) &
    (fin <> {} implies 1 <= len fin & len fin1 < len (fin1^fin))
  proof
    len (fin^fin1) = len fin + len fin1 by FINSEQ_1:35;
    hence len fin <= len (fin^fin1) & len fin1 <= len (fin^fin1) by NAT_1:37;
    assume fin <> {}; then
    len fin <> 0 by FINSEQ_1:25; then
    0 < len fin by NAT_1:19; then
A1: 0+1 <= len fin by NAT_1:38; then
    len fin1 + 1 <= len fin + len fin1 by AXIOMS:24; then
    len fin1 + 1 <= len (fin1^fin) by FINSEQ_1:35;
    hence thesis by A1,NAT_1:38;
  end;

theorem Th41:
  Seq ((f^g)|dom f) = (f^g)|dom f
  proof
    (f^g)|dom f = f by FINSEQ_1:33;
    hence thesis by GRAPH_2:24;
  end;

theorem Th42:
  f is_Subsequence_of f^g
  proof
    set a = len f;
    take N = Seg a;
    reconsider f1 = (f^g)|N as FinSequence by FINSEQ_1:19;
A1: N = dom f by FINSEQ_1:def 3; then
    f c= f1 by FINSEQ_1:33;
    hence thesis by A1,Th41;
  end;

theorem Th43:
  1 < len (fin^<*b*>^<*c*>)
  proof
    len (fin^<*b*>^<*c*>) = len (fin^<*b*>) + len <*c*> by FINSEQ_1:35; then
    len (fin^<*b*>^<*c*>) = len (fin^<*b*>) + 1 by FINSEQ_1:56; then
    len (fin^<*b*>^<*c*>) = len fin + len <*b*> + 1 by FINSEQ_1:35; then
    len (fin^<*b*>^<*c*>) = (len fin + 1) + 1 by FINSEQ_1:56; then
    len (fin^<*b*>^<*c*>) = len fin + (1 + 1); then
    1 + 1 <= len (fin^<*b*>^<*c*>) by NAT_1:29;
    hence thesis by NAT_1:38;
  end;

theorem Th45:
  1 <= len (fin^<*b*>) & len (fin^<*b*>) in dom (fin^<*b*>)
  proof
    len (fin^<*b*>) = len fin + 1 by FINSEQ_2:19; then
    1 <= len (fin^<*b*>) by NAT_1:29;
    hence thesis by FINSEQ_3:27;
  end;

theorem Th47:
  0 < m implies len (Sgm (Seg n \/ {n+m})) = n+1
  proof
    assume A1: 0 < m; then
    not n+m in Seg n by FINSEQ_3:11; then
    card (Seg n \/ {n+m}) = card (Seg n) + 1 by CARD_2:54; then
A2: card (Seg n \/ {n+m}) = n + 1 by FINSEQ_1:78;
    0+1 <= m by A1,NAT_1:38; then
    1 <= m & m <= n+m by NAT_1:29; then
    1 <= m+n by AXIOMS:22; then
    n+m in Seg (n+m) by FINSEQ_1:3; then
A3: {n+m} c= Seg (n+m) by ZFMISC_1:37;
    Seg n c= Seg (n+m) by FINSEQ_3:19; then
    Seg n \/ {n+m} c= Seg (n+m) by A3,XBOOLE_1:8;
    hence thesis by A2,FINSEQ_3:44;
  end;

theorem Th48:
  0 < m implies dom (Sgm (Seg n \/ {n+m})) = Seg (n+1)
  proof
    assume 0 < m; then
    len (Sgm (Seg n \/ {n+m})) = n+1 by Th47;
    hence thesis by FINSEQ_1:def 3;
  end;

theorem Th49:
  0 < len f implies f is_Subsequence_of Ant(f)^g^<*Suc(f)*>
  proof
    set n = len Ant(f);
    set m = len g;
    set N = Seg n \/ {n+(m+1)};
    set f1 = Ant(f)^g^<*Suc(f)*>;
    reconsider f2 = f1|N as FinSubsequence;
    assume A15: 0 < len f;
A43: now let b such that A1: b in f;
      consider c,d such that A2: b = [c,d] by A1,RELAT_1:def 1;
A3:   c in dom f & d = f.c by A1,A2,FUNCT_1:8;
      now let b such that A4: b in N;
        reconsider i = b as Nat by A4;
A6:     now assume i in Seg n; then
A8:       1 <= i & i <= n by FINSEQ_1:3;
          f1 = Ant(f)^(g^<*Suc(f)*>) by FINSEQ_1:45; then
          n <= len f1 by Th32; then
          1 <= i & i <= len f1 by A8,AXIOMS:22;
          hence i in dom f1 by FINSEQ_3:27;
        end;
        now assume i in {n+(m+1)}; then
A10:      i = (n+m)+1 by TARSKI:def 1;
          len f1 = len (Ant(f)^g) + len <*Suc(f)*> by FINSEQ_1:35; then
          len f1 = n + m + len <*Suc(f)*> by FINSEQ_1:35; then
          1 <= i & i <= len f1 by A10,NAT_1:29,FINSEQ_1:56;
          hence i in dom f1 by FINSEQ_3:27;
        end;
        hence b in dom f1 by A4,A6,XBOOLE_0:def 2;
      end; then
A11:  N c= dom f1 by TARSKI:def 3;
      dom f2 = dom f1 /\ N by RELAT_1:90; then
A12:  dom f2 = N by A11,XBOOLE_1:28;
A13:  0 < m+1 by NAT_1:19; then
A14:  dom Sgm (dom f2) = Seg (n+1) by A12,Th48;
      now let i;
        i in dom f iff 1 <= i & i <= len f by FINSEQ_3:27; then
        i in dom f iff 1 <= i & i <= n+1 by A15,Th6;
        hence i in dom f iff i in Seg (n+1) by FINSEQ_1:3;
      end; then
      for b holds b in dom f iff b in Seg (n+1); then
A16:  dom Sgm (dom f2) = dom f by A14,TARSKI:2;
      reconsider i = c as Nat by A3;
A18:  0+1 <= m+1 by A13,NAT_1:38;
      m+1 <= n+(m+1) by NAT_1:29; then
      1 <= n+(m+1) by A18,AXIOMS:22; then
      n+(m+1) in Seg (n+(m+1)) by FINSEQ_1:3; then
A20:  {n+(m+1)} c= Seg (n+(m+1)) by ZFMISC_1:37;
      now let i,j such that A21: i in Seg n & j in {n+(m+1)};
A22:    i <= n by A21,FINSEQ_1:3;
        n+1 <= n+1+m by NAT_1:29; then
        n < n+(m+1) by NAT_1:38; then
        n < j by A21,TARSKI:def 1;
        hence i < j by A22,AXIOMS:22;
      end; then
A24:  Sgm (dom f2) = Sgm(Seg n)^Sgm{n+(m+1)} by A12,A20,FINSEQ_3:48;
      Sgm (dom f2) = Sgm(Seg n)^<*n+(m+1)*> by A24,FINSEQ_3:50; then
A25:  Sgm (dom f2) = (idseq n)^<*n+(m+1)*> by FINSEQ_3:54;
A26:  now assume A27: i in Seg n; then
        i in dom idseq n by FINSEQ_2:54; then
        Sgm (dom f2).i = (idseq n).i by A25,FINSEQ_1:def 7; then
A28:    Sgm (dom f2).i = i by A27,FINSEQ_2:56;
A29:    i in dom Sgm (dom f2) by A1,A2,A16,FUNCT_1:8;
        Seq f2 = Sgm (dom f2) * f2 by FINSEQ_1:def 14; then
A30:    Seq f2.i = f2.i by A28,A29,FUNCT_1:23;
A31:    f1 = Ant(f)^(g^<*Suc(f)*>) & Seg n c= N by XBOOLE_1:7,FINSEQ_1:45;
X1:     Seg len Ant f = dom Ant f by FINSEQ_1:def 3;
        Seq f2.i = (f2|Seg n).i by A27,A30,FUNCT_1:72; then
        Seq f2.i = (f1|Seg n).i by A31,RELAT_1:103; then
A32:    Seq f2.i = (Ant(f)).i by A31,X1,FINSEQ_1:33;
A33:    i in dom Ant(f) by A27,FINSEQ_1:def 3;
        f = Ant(f)^<*Suc(f)*> by A15,Th7;
        hence Seq f2.i = f.i by A32,A33,FINSEQ_1:def 7;
      end;
A42:  now assume A34: i = n+1; then
        f.i = f.(len f) by A15,Th6; then
A35:    f.i = Suc(f) by A15,Def2;
        len idseq n = n by FINSEQ_2:55; then
A36:    Sgm (dom f2).i = n+(m+1) by A25,A34,FINSEQ_1:59;
A37:    i in dom Sgm (dom f2) by A1,A2,A16,FUNCT_1:8;
        Seq f2 = Sgm (dom f2) * f2 by FINSEQ_1:def 14; then
A38:    Seq f2.i = f2.(n+(m+1)) by A36,A37,FUNCT_1:23;
A39:    n+(m+1) in {n+(m+1)} by TARSKI:def 1;
        {n+(m+1)} c= N by XBOOLE_1:7; then
A40:    Seq f2.i = f1.((n+m)+1) by A38,A39,FUNCT_1:72;
A41:    len (Ant(f)^g) = n + m by FINSEQ_1:35;
        1 in Seg 1 by FINSEQ_1:3; then
        1 in dom <*Suc(f)*> by FINSEQ_1:55; then
        Seq f2.i = <*Suc(f)*>.1 by A40,A41,FINSEQ_1:def 7;
        hence Seq f2.i = f.i by A35,FINSEQ_1:57;
      end;
      rng Sgm (dom (f1|N)) = dom (f1|N) by FINSEQ_1:71; then
      dom f = dom (Sgm (dom (f1|N)) * (f1|N)) by A16,RELAT_1:46; then
      dom f = dom Seq f2 by FINSEQ_1:def 14;
      hence b in Seq f2 by A2,A3,A14,A16,A26,A42,FINSEQ_2:8,FUNCT_1:8;
    end;
    take N;
    thus thesis by A43,TARSKI:def 3;
  end;

theorem Th50:
  1 in dom <*c,d*> & 2 in dom <*c,d*> &
        (f^<*c,d*>).(len f + 1) = c & (f^<*c,d*>).(len f + 2) = d
  proof
    1 <= 2; then
A1: 1 <= len <*c,d*> by FINSEQ_1:61; then
    1 in dom <*c,d*> by FINSEQ_3:27; then
A2: (f^<*c,d*>).(len f+1) = <*c,d*>.1 by FINSEQ_1:def 7;
A3: 2 <= len <*c,d*> by FINSEQ_1:61; then
    2 in dom <*c,d*> by FINSEQ_3:27; then
    (f^<*c,d*>).(len f+2) = <*c,d*>.2 by FINSEQ_1:def 7;
    hence thesis by A1,A2,A3,FINSEQ_1:61,FINSEQ_3:27;
  end;

begin :: A Sequent calculus

definition let f;
  func still_not-bound_in f -> Element of bool bound_QC-variables means
  :Def4: a in it iff ex i,p st i in dom f & p = f.i &
             a in still_not-bound_in p;
  existence
  proof
    defpred P[set] means ex i,p st i in dom f & p = f.i &
                                                  $1 in still_not-bound_in p;
    consider X being set such that A1: for a holds a in X iff
       a in bound_QC-variables & P[a] from XBOOLE_0:sch 1;
    take X;
    for a st a in X holds a in bound_QC-variables by A1;
    hence X is Element of bool bound_QC-variables by TARSKI:def 3;
    thus a in X iff ex i,p st i in dom f & p = f.i &
                                              a in still_not-bound_in p by A1;
  end;
  uniqueness
  proof
    let X,Y be Element of bool bound_QC-variables;
    assume
A1:   (for a holds a in X iff ex i,p st i in dom f & p = f.i &
                                          a in still_not-bound_in p) &
      (for a holds a in Y iff ex i,p st i in dom f & p = f.i &
                                          a in still_not-bound_in p);
    now let a;
      (a in X iff ex i,p st i in dom f & p = f.i &
                                           a in still_not-bound_in p) &
      (a in Y iff ex i,p st i in dom f & p = f.i &
                                           a in still_not-bound_in p) by A1;
      hence a in X iff a in Y;
    end;
    hence X = Y by TARSKI:2;
  end;
end;

definition
  func set_of_CQC-WFF-seq means
  :Def5: a in it iff a is FinSequence of CQC-WFF;
  existence
  proof
    defpred P[set] means $1 is FinSequence of CQC-WFF;
    consider X being set such that
A1: for a holds a in X iff a in bool [:NAT,CQC-WFF:] & P[a]
                                               from XBOOLE_0:sch 1;
    take X;
    thus a in X iff P[a] by A1;
  end;
  uniqueness
  proof
    let X,Y be set such that
A4:  (a in X iff a is FinSequence of CQC-WFF) and
A5:  (a in Y iff a is FinSequence of CQC-WFF);
    now let a;
      (a in X iff a is FinSequence of CQC-WFF) by A4;
      hence a in X iff a in Y by A5;
    end;
    hence thesis by TARSKI:2;
  end;
end;

 reserve PR,PR1,PR2,PR3 for
                    FinSequence of [:set_of_CQC-WFF-seq,Proof_Step_Kinds:];

definition let PR; let n be Nat;
  pred PR,n is_a_correct_step means
  :Def7: ex f st Suc(f) is_tail_of Ant(f) & (PR.n)`1 = f if (PR.n)`2 = 0,
  ex f st (PR.n)`1 = f^<*VERUM*>                         if (PR.n)`2 = 1,
  ex i,f,g st 1 <= i & i < n & Ant(f) is_Subsequence_of Ant(g) &
      Suc(f) = Suc(g) & (PR.i)`1 = f & (PR.n)`1 = g      if (PR.n)`2 = 2,
  ex i,j,f,g st 1 <= i & i < n & 1 <= j & j < i & len f > 1 & len g > 1 &
   Ant(Ant(f)) = Ant(Ant(g)) & 'not' Suc(Ant(f)) = Suc(Ant(g)) &
   Suc(f) = Suc(g) & f = (PR.j)`1 &
          g = (PR.i)`1 & Ant(Ant(f))^<*Suc(f)*> = (PR.n)`1 if (PR.n)`2 = 3,
  ex i,j,f,g,p st 1 <= i & i < n & 1 <= j & j < i & len f > 1 &
      Ant(f) = Ant(g) & Suc(Ant(f)) = 'not' p & 'not' Suc(f) = Suc(g) &
      f = (PR.j)`1 & g = (PR.i)`1 & Ant(Ant(f))^<*p*> = (PR.n)`1
                                                  if (PR.n)`2 = 4,
  ex i,j,f,g st 1 <= i & i < n & 1 <= j & j < i & Ant(f) = Ant(g) &
       f = (PR.j)`1 & g = (PR.i)`1 &
         Ant(f)^<*(Suc(f)) '&' (Suc(g))*> = (PR.n)`1 if (PR.n)`2 = 5,
  ex i,f,p,q st 1 <= i & i < n & p '&' q = Suc(f) & f = (PR.i)`1 &
                                Ant(f)^<*p*> = (PR.n)`1 if (PR.n)`2 = 6,
  ex i,f,p,q st 1 <= i & i < n & p '&' q = Suc(f) & f = (PR.i)`1 &
                                Ant(f)^<*q*>= (PR.n)`1 if (PR.n)`2 = 7,
  ex i,f,p,x,y st 1 <= i & i < n & Suc(f) = All(x,p) &
            f = (PR.i)`1 & Ant(f)^<*p.(x,y)*> = (PR.n)`1 if (PR.n)`2 = 8,
  ex i,f,p,x,y st 1 <= i & i < n & Suc(f) = p.(x,y) &
   not y in still_not-bound_in (Ant(f)) & not y in still_not-bound_in All(x,p)
    & f = (PR.i)`1 & Ant(f)^<*All(x,p)*> = (PR.n)`1 if (PR.n)`2 = 9;
  consistency;
end;

definition let PR;
  attr PR is a_proof means
  :Def8: PR <> {} & for n st 1 <= n & n <= len PR holds PR,n is_a_correct_step;
end;

definition let f;
  pred |- f means
  :Def9: ex PR st PR is a_proof & f = (PR.(len PR))`1;
end;

definition let p,X;
  pred p is_formal_provable_from X means
  :Def10: ex f st rng Ant(f) c= X & Suc(f) = p & |- f;
end;

definition let X; let A; let J; let v;
  pred J,v |= X means :Def11:
    p in X implies J,v |= p;
end;

definition let X,p;
  pred X |= p means
    J,v |= X implies J,v |= p;
end;

definition let p;
  pred |= p means
    {}(CQC-WFF) |= p;
end;

definition let f, A, J, v;
  pred J,v |= f means :Def14:
    J,v |= rng(f);
end;

definition let f, p;
  pred f |= p means :Def15:
    J,v |= f implies J,v |= p;
end;

theorem Th1:
  Suc(f) is_tail_of Ant(f) implies Ant(f) |= Suc(f)
   proof
     assume Suc(f) is_tail_of Ant(f); then
     consider i such that A1: i in dom Ant(f) & (Ant(f)).i = Suc(f) by Def3;
     let A,J,v such that A2: J,v |= rng(Ant(f));
     Suc(f) in rng(Ant(f)) by A1,FUNCT_1:12;
     hence thesis by A2,Def11;
   end;

theorem Th5:
  Ant(f) is_Subsequence_of Ant(g) & Suc(f) = Suc(g) &
      Ant(f) |= Suc(f) implies Ant(g) |= Suc(g)
   proof
     assume A1: Ant(f) is_Subsequence_of Ant(g) & Suc(f) = Suc(g) &
                                           Ant(f) |= Suc(f);
     let A,J,v such that A2: J,v |= rng(Ant(g));
     now let p; assume A3: p in rng(Ant(f));
        rng(Ant(f)) c= rng(Ant(g)) by A1,Th4;
        hence J,v |= p by A2,A3,Def11;
     end; then
     J,v |= rng(Ant(f)) by Def11; then
     J,v |= Ant(f) by Def14;
     hence thesis by A1,Def15;
   end;

theorem Th8:
  len f > 0 implies (J,v |= Ant(f) & J,v |= Suc(f) iff J,v |= f)
   proof
     assume A1: len f > 0;
     thus J,v |= Ant(f) & J,v |= Suc(f) implies J,v |= f
     proof
       assume J,v |= Ant(f) & J,v |= Suc(f); then
A2:    J,v |= rng(Ant(f)) & J,v |= Suc(f) by Def14;
       let p such that A3: p in rng(f);
       p in rng(Ant(f)) \/ {Suc(f)} by A1,A3,Th7; then
       p in rng(Ant(f)) or p in {Suc(f)} by XBOOLE_0:def 2;
       hence thesis by A2,Def11,TARSKI:def 1;
     end;
     thus J,v |= f implies J,v |= Ant(f) & J,v |= Suc(f)
     proof
       assume A5: J,v |= rng(f);
       thus J,v |= rng(Ant(f))
       proof
         let p such that A6: p in rng(Ant(f));
         rng(Ant(f)) c= rng(Ant(f)) \/ {Suc(f)} by XBOOLE_1:7; then
         p in rng(Ant(f)) \/ {Suc(f)} by A6; then
         p in rng(f) by A1,Th7;
         hence thesis by A5,Def11;
       end;
       thus J,v |= Suc(f)
       proof
A7:      Suc(f) = f.(len f) by A1,Def2;
         0+1 <= len f & len f <= len f by A1,NAT_1:38; then
         len f in dom f by FINSEQ_3:27; then
         Suc(f) in rng(f) by A7,FUNCT_1:12;
         hence thesis by A5,Def11;
       end;
     end;
   end;

theorem Th10:
  len f > 1 & len g > 1 & Ant(Ant(f)) = Ant(Ant(g)) &
    'not' Suc(Ant(f)) = Suc(Ant(g)) & Suc(f) = Suc(g) & Ant(f) |= Suc(f) &
        Ant(g) |= Suc(g) implies Ant(Ant(f)) |= Suc(f)
   proof
     assume A1: len f > 1 & len g > 1 & Ant(Ant(f)) = Ant(Ant(g)) &
                'not' Suc(Ant(f)) = Suc(Ant(g)) & Suc(f) = Suc(g) &
                Ant(f) |= Suc(f) & Ant(g) |= Suc(g); then
A2:  len Ant(f) > 0 & len Ant(g) > 0 by Th9;
     let A,J,v such that A3: J,v |= Ant(Ant(f));
A4:  now assume J,v |= Suc(Ant(f)); then
       J,v |= Ant(f) by A2,A3,Th8;
       hence J,v |= Suc(f) by A1,Def15;
     end;
     now assume not J,v |= Suc(Ant(f)); then
       J,v |= Suc(Ant(g)) by A1,VALUAT_1:28; then
       J,v |= Ant(g) by A1,A2,A3,Th8;
       hence thesis by A1,Def15;
     end;
     hence thesis by A4;
   end;

theorem Th11:
  len f > 1 & Ant(f) = Ant(g) & 'not' p = Suc(Ant(f)) &
    'not' Suc(f) = Suc(g) & Ant(f) |= Suc(f) & Ant(g) |= Suc(g) implies
        Ant(Ant(f)) |= p
  proof
    assume A1: len f > 1 & Ant(f) = Ant(g) & 'not' p = Suc(Ant(f)) &
            'not' Suc(f) = Suc(g) & Ant(f) |= Suc(f) & Ant(g) |= Suc(g); then
A2: len Ant(f) > 0 by Th9;
    let A,J,v such that A3: J,v |= Ant(Ant(f));
A4: now given A,J,v such that A5: J,v |= Ant(f);
      J,v |= Suc(f) & J,v |= 'not' Suc(f) by A1,A5,Def15;
      hence contradiction by VALUAT_1:28;
    end;
    now assume J,v |= Suc(Ant(f)); then
      J,v |= Ant(f) by A2,A3,Th8;
      hence contradiction by A4;
    end;
    hence J,v |= p by A1,VALUAT_1:28;
  end;

theorem Th14:
  Ant(f) = Ant(g) & Ant(f) |= Suc(f) &
     Ant(g) |= Suc(g) implies Ant(f) |= (Suc(f)) '&' (Suc(g))
   proof
     assume A1: Ant(f) = Ant(g) & Ant(f) |= Suc(f) & Ant(g) |= Suc(g);
     let A,J,v such that A4: J,v |= Ant(f);
     J,v |= Suc(f) & J,v |= Suc(g) by A1,A4,Def15;
     hence thesis by VALUAT_1:29;
   end;

theorem Th15:
  Suc(f) = p '&' q & Ant(f) |= p '&' q implies Ant(f) |= p
  proof
    assume A1: Suc(f) =  p '&' q & Ant(f) |= p '&' q;
    let A,J,v such that A2: J,v |= Ant(f);
    J,v |= p '&' q by A1,A2,Def15;
    hence thesis by VALUAT_1:29;
  end;

theorem Th16:
  Suc(f) = p '&' q & Ant(f) |= p '&' q implies Ant(f) |= q
  proof
    assume A1: Suc(f) =  p '&' q & Ant(f) |= p '&' q;
    let A,J,v such that A2: J,v |= Ant(f);
    J,v |= p '&' q by A1,A2,Def15;
    hence thesis by VALUAT_1:29;
  end;

theorem Th17:
  J,v |= [p,Sub] iff J,v |= p
  proof
    J,v |= [p,Sub] iff J,v |= [p,Sub]`1 by SUBLEMMA:def 3;
    hence thesis by MCART_1:7;
  end;

 reserve a for Element of A;

theorem Th18:
  J,v |= p.(x,y) iff ex a st v.y = a & J,v.(x|a) |= p
  proof
A2: J,v |= CQC_Sub([p,Sbst(x,y)]) iff
             J,v.Val_S(v,[p,Sbst(x,y)]) |= [p,Sbst(x,y)] by SUBLEMMA:93;
A3: J,v.Val_S(v,[p,Sbst(x,y)]) |= [p,Sbst(x,y)] iff
             J,v.Val_S(v,[p,Sbst(x,y)]) |= p by Th17;
    Val_S(v,[p,Sbst(x,y)]) = (@[p,Sbst(x,y)]`2)*v by SUBLEMMA:def 2; then
A4: Val_S(v,[p,Sbst(x,y)]) = @Sbst(x,y)*v by MCART_1:7;
    @Sbst(x,y) = Sbst(x,y) by SUBSTUT1:def 2; then
    Val_S(v,[p,Sbst(x,y)]) = x .--> y*v by A4,SUBSTUT2:def 1; then
A5: Val_S(v,[p,Sbst(x,y)]) = ({x} --> y)*v by CQC_LANG:def 2;
    y in bound_QC-variables; then
    y in dom v by SUBLEMMA:59; then
A6: Val_S(v,[p,Sbst(x,y)]) = {x} --> v.y by A5,FUNCOP_1:23;
    set a = v.y;
    Val_S(v,[p,Sbst(x,y)]) = x .--> a by A6,CQC_LANG:def 2; then
    Val_S(v,[p,Sbst(x,y)]) = (x|a) by SUBLEMMA:def 9;
    hence thesis by A2,A3,SUBSTUT2:def 2;
  end;

theorem Th19:
  Suc(f) = All(x,p) & Ant(f) |= Suc(f) implies
                                      for y holds Ant(f) |= p.(x,y)
  proof
    assume A1: Suc(f) = All(x,p) & Ant(f) |= Suc(f);
    let y,A,J,v such that A2: J,v |= Ant(f);
A3: J,v |= All(x,p) by A1,A2,Def15;
    ex a st v.y = a & J,v.(x|a) |= p
    proof
      take a = v.y;
      thus thesis by A3,SUBLEMMA:51;
    end;
    hence thesis by Th18;
  end;

theorem Th20:
  for X being set st X c= bound_QC-variables holds
     not x in X implies v.(x|a)|X = v|X
  proof
    let X be set such that A1: X c= bound_QC-variables & not x in X;
    set f1 = v.(x|a)|X;
    set f2 = v|X;
A2: dom f1 = dom f2 by A1,SUBLEMMA:64;
    now let b such that A3: b in dom f1;
      x <> b by A1,A3,SUBLEMMA:64; then
A4:   v.(x|a).b = v.b by SUBLEMMA:49;
      v.(x|a).b = f1.b by A3,FUNCT_1:68;
      hence f1.b = f2.b by A2,A3,A4,FUNCT_1:68;
    end;
    hence thesis by A2,FUNCT_1:9;
  end;

theorem Th21:
  for v,w holds v|still_not-bound_in f = w|still_not-bound_in f
                                           implies (J,v |= f iff J,w |= f)
  proof
    let v,w such that A1: v|still_not-bound_in f = w|still_not-bound_in f;
A2: for i,p st i in dom f & p = f.i holds J,v |= p iff J,w |= p
    proof
      let i,p such that A3: i in dom f & p = f.i;
      for b st b in still_not-bound_in p holds b in still_not-bound_in f
                                                               by A3,Def4; then
      still_not-bound_in p c= still_not-bound_in f by TARSKI:def 3; then
      v|still_not-bound_in p = w|still_not-bound_in p by A1,AMI_5:5;
      hence thesis by SUBLEMMA:70;
    end;
    thus J,v |= f implies J,w |= f
    proof
      assume J,v |= f; then
A4:   J,v |= rng(f) by Def14;
      let p such that A5: p in rng(f);
      consider i such that A6: i in dom f & p = f.i by A5,FINSEQ_2:11;
      J,v |= p by A4,A5,Def11;
      hence thesis by A6,A2;
    end;
    thus J,w |= f implies J,v |= f
    proof
      assume J,w |= f; then
A4:   J,w |= rng(f) by Def14;
      let p such that A5: p in rng(f);
      consider i such that A6: i in dom f & p = f.i by A5,FINSEQ_2:11;
      J,w |= p by A4,A5,Def11;
      hence thesis by A6,A2;
    end;
  end;

theorem Th22:
  not y in still_not-bound_in All(x,p) implies
     v.(y|a).(x|a)|still_not-bound_in p = v.(x|a)|still_not-bound_in p
  proof
    assume A1: not y in still_not-bound_in All(x,p);
    v.(y|a) = v+*(y|a) by SUBLEMMA:def 1; then
    v.(y|a).(x|a) = v+*(y|a)+*(x|a) by SUBLEMMA:def 1; then
A2: v.(y|a).(x|a) = v+*((y|a)+*(x|a)) by FUNCT_4:15;
    now assume A4: x <> y;
      not y in (still_not-bound_in p) \ {x} by A1,QC_LANG3:16; then
A5:   not y in still_not-bound_in p by A4,ZFMISC_1:64;
      dom (x|a) = {x} & dom (y|a) = {y} by SUBLEMMA:59; then
      dom (x|a) misses dom (y|a) by A4,ZFMISC_1:17; then
      v.(y|a).(x|a) = v+*((x|a)+*(y|a)) by A2,FUNCT_4:36; then
      v.(y|a).(x|a) = v+*(x|a)+*(y|a) by FUNCT_4:15; then
      v.(y|a).(x|a) = v.(x|a)+*(y|a) by SUBLEMMA:def 1; then
      v.(y|a).(x|a) = v.(x|a).(y|a) by SUBLEMMA:def 1;
      hence thesis by A5,Th20;
    end;
    hence thesis by A2,SUBLEMMA:def 1;
  end;

theorem Th23:
  Suc(f) = p.(x,y) & Ant(f) |= Suc(f) &
    not y in still_not-bound_in Ant(f) & not y in still_not-bound_in All(x,p)
       implies Ant(f) |= All(x,p)
    proof
      assume A1: Suc(f) = p.(x,y) & Ant(f) |= Suc(f) &
         not y in still_not-bound_in (Ant(f)) &
         not y in still_not-bound_in All(x,p);
      let A,J,v such that A2: J,v |= Ant(f);
      for a holds J,v.(x|a) |= p
      proof
        let a;
        v.(y|a)|still_not-bound_in (Ant(f)) = v|still_not-bound_in (Ant(f))
                                                             by A1,Th20; then
        J,v.(y|a) |= Ant(f) by A2,Th21; then
        J,v.(y|a) |= p.(x,y) by A1,Def15; then
        consider a1 being Element of A such that
A4:     v.(y|a).y = a1 & J,v.(y|a).(x|a1) |= p by Th18;
A6:     J,v.(y|a).(x|a) |= p by A4,SUBLEMMA:50;
        v.(y|a).(x|a)|still_not-bound_in p = v.(x|a)|still_not-bound_in p
                         by A1,Th22;
        hence J,v.(x|a) |= p by A6,SUBLEMMA:70;
      end;
      hence thesis by SUBLEMMA:51;
    end;

theorem Th2:
  Ant(f^<*VERUM*>) |= Suc(f^<*VERUM*>)
   proof
     let A,J,v such that J,v |= Ant(f^<*VERUM*>);
     Ant(f^<*VERUM*>) = f & Suc(f^<*VERUM*>) = VERUM by Th24;
     hence thesis by VALUAT_1:44;
   end;

theorem Th25:
  1 <= n & n <= len PR implies
  (PR.n)`2 = 0 or (PR.n)`2 = 1 or (PR.n)`2 = 2 or (PR.n)`2 = 3 or
  (PR.n)`2 = 4 or (PR.n)`2 = 5 or (PR.n)`2 = 6 or (PR.n)`2 = 7 or
  (PR.n)`2 = 8 or (PR.n)`2 = 9
  proof
    assume A1: 1 <= n & n <= len PR;
    n in dom PR by A1,FINSEQ_3:27; then
    PR.n in rng PR by FUNCT_1:def 5; then
    (PR.n)`2 in {k: k <= 9} by MCART_1:10,CQC_THE1:def 3; then
    ex k st k = (PR.n)`2 & k <= 9;
    hence thesis by CQC_THE1:10;
  end;

Th26: for f being FinSequence st f <> {} holds 1 <= len f by GRAPH_5:8;

Lm1: 1 <= n & n <= len PR implies (PR.n)`1 is FinSequence of CQC-WFF
   proof
     assume A1: 1 <= n & n <= len PR;
     n in dom PR by A1,FINSEQ_3:27; then
     PR.n in rng PR by FUNCT_1:def 5; then
     (PR.n)`1 in set_of_CQC-WFF-seq by MCART_1:10;
     hence thesis by Def5;
   end;

:: Theorem on the Correctness (Ebb et al, Chapter IV, Theorem 6.2)

theorem
  p is_formal_provable_from X implies X |= p
  proof
    assume p is_formal_provable_from X; then
    consider f such that A1: rng Ant(f) c= X & Suc(f) = p & |- f by Def10;
    consider PR such that A2: PR is a_proof & f = (PR.(len PR))`1 by A1,Def9;
    let A,J,v such that A3: J,v |= X;
      defpred P[Nat] means $1 <= len PR implies for m st 1 <= m & m <= $1 holds
                                    ex g st g = (PR.m)`1 & Ant(g) |= Suc(g);
A4:   P[0]
      proof
        assume 0 <= len PR;
        let m such that A5: 1 <= m & m <= 0;
        1 <= 0 by A5;
        hence thesis;
      end;
A6:   for k holds P[k] implies P[k+1]
      proof
        let k such that A7: P[k];
        assume A8: k+1 <= len PR;
        let m such that A9: 1 <= m & m <= k+1;
A10:    1 <= m & m <= len PR by A8,A9,AXIOMS:22;
A11:    k <= k+1 by NAT_1:29;
A13:    m <= k implies ex g st g = (PR.m)`1 & Ant(g) |= Suc(g)
                                       by A7,A9,A8,A11,AXIOMS:22;
        now assume A14: m = k+1;
          take g = (PR.m)`1;
          thus g = (PR.m)`1;
          reconsider g = (PR.m)`1 as FinSequence of CQC-WFF by Lm1,A10;
A15:      PR,m is_a_correct_step by A2,A10,Def8;
          now per cases by A10,Th25;
            suppose (PR.m)`2 = 0; then
             consider f such that A16: Suc(f) is_tail_of Ant(f) & (PR.m)`1 = f
                                                         by A15,Def7;
             thus Ant(g) |= Suc(g) by A16,Th1;
            end;
            suppose (PR.m)`2 = 1; then
             consider f such that A48: g = f^<*VERUM*> by A15,Def7;
             thus Ant(g) |= Suc(g) by A48,Th2;
            end;
            suppose (PR.m)`2 = 2; then
             consider i,f,f1 such that A17: 1 <= i & i < m &
                      Ant(f) is_Subsequence_of Ant(f1) & Suc(f) = Suc(f1) &
                      (PR.i)`1 = f & (PR.m)`1 = f1 by A15,Def7;
             1 <= i & i <= k by A14,A17,NAT_1:38; then
             consider h such that A18: h = (PR.i)`1 & Ant(h) |= Suc(h)
                                                 by A7,A8,A11,AXIOMS:22;
             thus Ant(g) |= Suc(g) by A17,A18,Th5;
            end;
            suppose (PR.m)`2 = 3; then
             consider i,j,f,f1 such that A19: 1 <= i & i < m & 1 <= j & j < i &
                len f > 1 & len f1 > 1 & Ant(Ant(f)) = Ant(Ant(f1)) &
                'not' Suc(Ant(f)) = Suc(Ant(f1))
                & Suc(f) = Suc(f1) & f = (PR.j)`1 & f1 = (PR.i)`1 &
                            Ant(Ant(f))^<*Suc(f)*> = (PR.m)`1 by A15,Def7;
A20:         1 <= i & i <= k by A14,A19,NAT_1:38; then
             consider h such that A21: h = (PR.i)`1 & Ant(h) |= Suc(h)
                                                by A7,A8,A11,AXIOMS:22;
             1 <= j & j <= k by A19,A20,AXIOMS:22; then
             consider h1 such that A22: h1 = (PR.j)`1 & Ant(h1) |= Suc(h1)
                                                by A7,A8,A11,AXIOMS:22;
             Ant(g) = Ant(Ant(f)) & Suc(g) = Suc(f) by A19,Th24;
             hence Ant(g) |= Suc(g) by A19,A21,A22,Th10;
            end;
            suppose (PR.m)`2 = 4; then
             consider i,j,f,f1,p such that A24: 1 <= i & i < m & 1 <= j &
               j < i & len f > 1 & Ant(f) = Ant(f1) & Suc(Ant(f)) = 'not' p &
                'not' Suc(f) = Suc(f1) & f = (PR.j)`1 & f1 = (PR.i)`1 &
                                     Ant(Ant(f))^<*p*> = (PR.m)`1 by A15,Def7;
A20:         1 <= i & i <= k by A14,A24,NAT_1:38; then
             consider h such that A25: h = (PR.i)`1 & Ant(h) |= Suc(h)
                                                      by A7,A8,A11,AXIOMS:22;
             1 <= j & j <= k by A24,A20,AXIOMS:22; then
             consider h1 such that A26: h1 = (PR.j)`1 & Ant(h1) |= Suc(h1)
                                                      by A7,A8,A11,AXIOMS:22;
             Ant(g) = Ant(Ant(f)) & Suc(g) = p by A24,Th24;
             hence Ant(g) |= Suc(g) by A24,A25,A26,Th11;
            end;
            suppose (PR.m)`2 = 5; then
             consider i,j,f,f1 such that A28: 1 <= i & i < m & 1 <= j & j < i
             & Ant(f) = Ant(f1) & f = (PR.j)`1 & f1 = (PR.i)`1 &
                   Ant(f)^<*(Suc(f)) '&' (Suc(f1))*> = (PR.m)`1 by A15,Def7;
A29:         1 <= i & i <= k by A14,A28,NAT_1:38; then
             consider h such that A30: h = (PR.i)`1 & Ant(h) |= Suc(h)
                                                        by A7,A8,A11,AXIOMS:22;
             1 <= j & j <= k by A28,A29,AXIOMS:22; then
             consider h1 such that A31: h1 = (PR.j)`1 & Ant(h1) |= Suc(h1)
                                                       by A7,A8,A11,AXIOMS:22;
             Ant(g) = Ant(f) & Suc(g) = (Suc(f)) '&' (Suc(f1)) by A28,Th24;
             hence Ant(g) |= Suc(g) by A28,A30,A31,Th14;
            end;
            suppose (PR.m)`2 = 6; then
             consider i,f,p,q such that A33: 1 <= i & i < m & p '&' q = Suc(f)
                    & f = (PR.i)`1 & Ant(f)^<*p*> = (PR.m)`1 by A15,Def7;
             1 <= i & i <= k by A14,A33,NAT_1:38; then
             consider h such that A34: h = (PR.i)`1 & Ant(h) |= Suc(h)
                                                      by A7,A8,A11,AXIOMS:22;
             Ant(g) = Ant(f) & Suc(g) = p by A33,Th24;
             hence Ant(g) |= Suc(g) by A33,A34,Th15;
            end;
            suppose (PR.m)`2 = 7; then
             consider i,f,p,q such that A36: 1 <= i & i < m & p '&' q = Suc(f)
                    & f = (PR.i)`1 & Ant(f)^<*q*>= (PR.m)`1 by A15,Def7;
             1 <= i & i <= k by A14,A36,NAT_1:38; then
             consider h such that A37: h = (PR.i)`1 & Ant(h) |= Suc(h)
                                                       by A7,A8,A11,AXIOMS:22;
             Ant(g) = Ant(f) & Suc(g) = q by A36,Th24;
             hence Ant(g) |= Suc(g) by A36,A37,Th16;
            end;
            suppose (PR.m)`2 = 8; then
             consider i,f,p,x,y such that A39: 1 <= i & i < m &
               Suc(f) = All(x,p) & f = (PR.i)`1 & Ant(f)^<*p.(x,y)*> = (PR.m)`1
                                                             by A15,Def7;
             1 <= i & i <= k by A14,A39,NAT_1:38; then
             consider h such that A40: h = (PR.i)`1 & Ant(h) |= Suc(h)
                                                       by A7,A8,A11,AXIOMS:22;
             Ant(g) = Ant(f) & Suc(g) = p.(x,y) by A39,Th24;
             hence Ant(g) |= Suc(g) by A39,A40,Th19;
            end;
            suppose (PR.m)`2 = 9; then
             consider i,f,p,x,y such that A42: 1 <= i & i < m &
                Suc(f) = p.(x,y) & not y in still_not-bound_in (Ant(f)) &
                not y in still_not-bound_in All(x,p) & f = (PR.i)`1 &
                Ant(f)^<*All(x,p)*> = (PR.m)`1 by A15,Def7;
             1 <= i & i <= k by A14,A42,NAT_1:38; then
             consider h such that A43: h = (PR.i)`1 & Ant(h) |= Suc(h)
               by A7,A8,A11,AXIOMS:22;
             Ant(g) = Ant(f) & Suc(g) = All(x,p) by A42,Th24;
             hence Ant(g) |= Suc(g) by A42,A43,Th23;
            end;
          end;
          hence thesis;
        end;
        hence thesis by A9,A13,NAT_1:26;
      end;
A45:  for k holds P[k] from NAT_1:sch 1(A4,A6);
      PR <> {} by A2,Def8; then
      1 <= len PR & len PR <= len PR by Th26; then
      consider g such that A46: g = (PR.(len PR))`1 & Ant(g) |= Suc(g) by A45;
      for p st p in rng(Ant(f)) holds J,v |= p by A1,A3,Def11; then
      J,v |= rng(Ant(f)) by Def11; then
      J,v |= Ant(g) by A2,A46,Def14;
      hence thesis by A1,A2,A46,Def15;
    end;

begin :: Derived Rules

theorem Th29:
  Suc(f) is_tail_of Ant(f) implies |- f
  proof
    assume A1: Suc(f) is_tail_of Ant(f);
    set PR = <*[f,0]*>;
A2: rng PR = {[f,0]} by FINSEQ_1:55;
    now let a be set such that A3: a in rng PR;
A4:   a = [f,0] by A2,A3,TARSKI:def 1;
      f in set_of_CQC-WFF-seq & 0 in Proof_Step_Kinds by Def5,CQC_THE1:43;
      hence a in [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by A4,ZFMISC_1:106;
    end; then
    rng PR c= [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by TARSKI:def 3; then
    reconsider PR as FinSequence of [:set_of_CQC-WFF-seq,Proof_Step_Kinds:]
                                                           by FINSEQ_1:def 4;
    now let n such that A6: 1 <= n & n <= len PR;
      1 <= n & n <= 1 by A6,FINSEQ_1:56; then
      n = 1 by AXIOMS:21; then
      PR.n = [f,0] by FINSEQ_1:57; then
      (PR.n)`1 = f & (PR.n)`2 = 0 by MCART_1:7;
      hence PR,n is_a_correct_step by A1,Def7;
    end; then
A6: PR is a_proof by Def8;
    PR.1 = [f,0] by FINSEQ_1:57; then
    PR.(len PR) = [f,0] by FINSEQ_1:57; then
    (PR.(len PR))`1 = f by MCART_1:7;
    hence thesis by A6,Def9;
  end;

theorem Th30:
  1 <= n & n <= len PR implies
         (PR,n is_a_correct_step iff PR^PR1,n is_a_correct_step)
   proof
     assume A1: 1 <= n & n <= len PR; then
     n in dom PR by FINSEQ_3:27; then
A2:  (PR^PR1).n = PR.n by FINSEQ_1:def 7;
     len(PR^PR1) = len PR + len PR1 by FINSEQ_1:35; then
     len PR <= len(PR^PR1) by NAT_1:29; then
A3:  1 <= n & n <= len(PR^PR1) by A1,AXIOMS:22;
     thus PR,n is_a_correct_step implies PR^PR1,n is_a_correct_step
     proof
       assume A4: PR,n is_a_correct_step;
       per cases by A3,Th25;
         case ((PR^PR1).n)`2 = 0;
         hence thesis by A2,A4,Def7;
         end;
         case ((PR^PR1).n)`2 = 1;
         hence thesis by A2,A4,Def7;
         end;
         case ((PR^PR1).n)`2 = 2; then
          consider i,f,g such that A5: 1 <= i & i < n and
                       A6: Ant(f) is_Subsequence_of Ant(g) & Suc(f) = Suc(g) &
                                  (PR.i)`1 = f & (PR.n)`1 = g by A2,A4,Def7;
          i <= len PR by A1,A5,AXIOMS:22; then
          i in dom PR by A5,FINSEQ_3:27; then
          PR.i = (PR^PR1).i by FINSEQ_1:def 7;
          hence thesis by A2,A5,A6;
         end;
         case ((PR^PR1).n)`2 = 3; then
          consider i,j,f,g such that A7: 1 <= i & i < n & 1 <= j & j < i and
            A8: len f > 1 & len g > 1 &
                Ant(Ant(f)) = Ant(Ant(g)) & 'not' Suc(Ant(f)) = Suc(Ant(g)) &
                Suc(f) = Suc(g) & f = (PR.j)`1 &
                g = (PR.i)`1 & Ant(Ant(f))^<*Suc(f)*> = (PR.n)`1 by A2,A4,Def7;
          i <= len PR & j <= n by A1,A7,AXIOMS:22; then
          i in Seg(len PR) & j <= len PR by A7,AXIOMS:22,FINSEQ_1:3; then
          i in dom PR & j in dom PR by A7,FINSEQ_3:27,FINSEQ_1:def 3; then
          PR.i = (PR^PR1).i & PR.j = (PR^PR1).j by FINSEQ_1:def 7;
          hence thesis by A2,A7,A8;
         end;
         case ((PR^PR1).n)`2 = 4; then
          consider i,j,f,g,p such that A9: 1 <= i & i < n & 1 <= j & j < i and
      A10: len f > 1 & Ant(f) = Ant(g) & Suc(Ant(f)) = 'not' p & 'not' Suc(f)
               = Suc(g) & f = (PR.j)`1 & g = (PR.i)`1 &
               Ant(Ant(f))^<*p*> = (PR.n)`1 by A2,A4,Def7;
          i <= len PR & j <= n by A1,A9,AXIOMS:22; then
          i in Seg(len PR) & j <= len PR by A9,AXIOMS:22,FINSEQ_1:3; then
          i in dom PR & j in dom PR by A9,FINSEQ_3:27,FINSEQ_1:def 3; then
          PR.i = (PR^PR1).i & PR.j = (PR^PR1).j by FINSEQ_1:def 7;
          hence thesis by A2,A9,A10;
         end;
         case ((PR^PR1).n)`2 = 5; then
          consider i,j,f,g such that A11: 1 <= i & i < n & 1 <= j & j < i and
A12:      Ant(f) = Ant(g) & f = (PR.j)`1 & g = (PR.i)`1 &
                     Ant(f)^<*(Suc(f)) '&' (Suc(g))*> = (PR.n)`1 by A2,A4,Def7;
          i <= len PR & j <= n by A1,A11,AXIOMS:22; then
          i in Seg(len PR) & j <= len PR by A11,AXIOMS:22,FINSEQ_1:3; then
          i in dom PR & j in dom PR by A11,FINSEQ_3:27,FINSEQ_1:def 3; then
          PR.i = (PR^PR1).i & PR.j = (PR^PR1).j by FINSEQ_1:def 7;
          hence thesis by A2,A11,A12;
         end;
         case ((PR^PR1).n)`2 = 6; then
          consider i,f,p,q such that A13: 1 <= i & i < n and
           A14: p '&' q = Suc(f) & f = (PR.i)`1 & Ant(f)^<*p*> = (PR.n)`1
                                                               by A2,A4,Def7;
          i <= len PR by A1,A13,AXIOMS:22; then
          i in dom PR by A13,FINSEQ_3:27; then
          PR.i = (PR^PR1).i by FINSEQ_1:def 7;
          hence thesis by A2,A13,A14;
         end;
         case ((PR^PR1).n)`2 = 7; then
          consider i,f,p,q such that A15: 1 <= i & i < n and
           A16: p '&' q = Suc(f) & f = (PR.i)`1 & Ant(f)^<*q*> = (PR.n)`1
                                                               by A2,A4,Def7;
          i <= len PR by A1,A15,AXIOMS:22; then
          i in dom PR by A15,FINSEQ_3:27; then
          PR.i = (PR^PR1).i by FINSEQ_1:def 7;
          hence thesis by A2,A15,A16;
         end;
         case ((PR^PR1).n)`2 = 8; then
          consider i,f,p,x,y such that A17: 1 <= i & i < n and
     A18: Suc(f) = All(x,p) & f = (PR.i)`1 &
                                Ant(f)^<*p.(x,y)*> = (PR.n)`1 by A2,A4,Def7;
          i <= len PR by A1,A17,AXIOMS:22; then
          i in dom PR by A17,FINSEQ_3:27; then
          PR.i = (PR^PR1).i by FINSEQ_1:def 7;
          hence thesis by A2,A17,A18;
         end;
         case ((PR^PR1).n)`2 = 9; then
          consider i,f,p,x,y such that A19: 1 <= i & i < n and
     A20: Suc(f) = p.(x,y) & not y in still_not-bound_in (Ant(f)) &
               not y in still_not-bound_in All(x,p) & f = (PR.i)`1 &
                             Ant(f)^<*All(x,p)*> = (PR.n)`1 by A2,A4,Def7;
          i <= len PR by A1,A19,AXIOMS:22; then
          i in dom PR by A19,FINSEQ_3:27; then
          PR.i = (PR^PR1).i by FINSEQ_1:def 7;
          hence thesis by A2,A19,A20;
         end;
     end;
     assume A21: PR^PR1,n is_a_correct_step;
     per cases by A1,Th25;
      case (PR.n)`2 = 0;
      hence thesis by A21,A2,Def7;
      end;
      case (PR.n)`2 = 1;
      hence thesis by A21,A2,Def7;
      end;
      case (PR.n)`2 = 2; then
       consider i,f,g such that A22: 1 <= i & i < n and
                       A23: Ant(f) is_Subsequence_of Ant(g) & Suc(f) = Suc(g) &
                       ((PR^PR1).i)`1 = f & ((PR^PR1).n)`1 = g by A2,A21,Def7;
       i <= len PR by A1,A22,AXIOMS:22; then
       i in dom PR by A22,FINSEQ_3:27; then
       PR.i = (PR^PR1).i by FINSEQ_1:def 7;
       hence thesis by A2,A22,A23;
      end;
      case (PR.n)`2 = 3; then
       consider i,j,f,f1 such that A24: 1 <= i & i < n & 1 <= j & j < i and
                                   A25: len f > 1 & len f1 > 1 & Ant(Ant(f)) =
                 Ant(Ant(f1)) & 'not' Suc(Ant(f)) = Suc(Ant(f1))
             &   Suc(f) = Suc(f1) & f = ((PR^PR1).j)`1 & f1 = ((PR^PR1).i)`1 &
                 Ant(Ant(f))^<*Suc(f)*> = ((PR^PR1).n)`1 by A2,A21,Def7;
       i <= len PR & j <= n by A1,A24,AXIOMS:22; then
       i in Seg(len PR) & j <= len PR by A24,AXIOMS:22,FINSEQ_1:3; then
       i in dom PR & j in dom PR by A24,FINSEQ_3:27,FINSEQ_1:def 3; then
       PR.i = (PR^PR1).i & PR.j = (PR^PR1).j by FINSEQ_1:def 7;
       hence thesis by A2,A24,A25;
      end;
      case (PR.n)`2 = 4; then
       consider i,j,f,g,p such that A26: 1 <= i & i < n & 1 <= j & j < i and
                                    A27: len f > 1 & Ant(f) = Ant(g) &
                Suc(Ant(f)) = 'not' p & 'not' Suc(f) = Suc(g) &
                f = ((PR^PR1).j)`1 & g = ((PR^PR1).i)`1 &
                Ant(Ant(f))^<*p*> = ((PR^PR1).n)`1  by A2,A21,Def7;
       i <= len PR & j <= n by A1,A26,AXIOMS:22; then
       i in Seg(len PR) & j <= len PR by A26,AXIOMS:22,FINSEQ_1:3; then
       i in dom PR & j in dom PR by A26,FINSEQ_3:27,FINSEQ_1:def 3; then
       PR.i = (PR^PR1).i & PR.j = (PR^PR1).j by FINSEQ_1:def 7;
       hence thesis by A2,A26,A27;
      end;
      case (PR.n)`2 = 5; then
       consider i,j,f,g such that A28: 1 <= i & i < n & 1 <= j & j < i and
                                  A29: Ant(f) = Ant(g) & f = ((PR^PR1).j)`1 &
                g = ((PR^PR1).i)`1 & Ant(f)^<*(Suc(f)) '&' (Suc(g))*> =
                                             ((PR^PR1).n)`1 by A2,A21,Def7;
       i <= len PR & j <= n by A1,A28,AXIOMS:22; then
       i in Seg(len PR) & j <= len PR by A28,AXIOMS:22,FINSEQ_1:3; then
       i in dom PR & j in dom PR by A28,FINSEQ_3:27,FINSEQ_1:def 3; then
       PR.i = (PR^PR1).i & PR.j = (PR^PR1).j by FINSEQ_1:def 7;
       hence thesis by A2,A28,A29;
      end;
      case (PR.n)`2 = 6; then
       consider i,f,p,q such that A30: 1 <= i & i < n and
                                  A31: p '&' q = Suc(f) & f = ((PR^PR1).i)`1 &
                                  Ant(f)^<*p*> = ((PR^PR1).n)`1 by A2,A21,Def7;
       i <= len PR by A1,A30,AXIOMS:22; then
       i in dom PR by A30,FINSEQ_3:27; then
       PR.i = (PR^PR1).i by FINSEQ_1:def 7;
       hence thesis by A2,A30,A31;
      end;
      case (PR.n)`2 = 7; then
       consider i,f,p,q such that A32: 1 <= i & i < n and
                                  A33: p '&' q = Suc(f) & f = ((PR^PR1).i)`1 &
                                  Ant(f)^<*q*>= ((PR^PR1).n)`1 by A2,A21,Def7;
       i <= len PR by A1,A32,AXIOMS:22; then
       i in dom PR by A32,FINSEQ_3:27; then
       PR.i = (PR^PR1).i by FINSEQ_1:def 7;
       hence thesis by A2,A32,A33;
      end;
      case (PR.n)`2 = 8; then
       consider i,f,p,x,y such that A34: 1 <= i & i < n and
                                  A35: Suc(f) = All(x,p) & f = ((PR^PR1).i)`1 &
                                  Ant(f)^<*p.(x,y)*> = ((PR^PR1).n)`1
                                                               by A2,A21,Def7;
       i <= len PR by A1,A34,AXIOMS:22; then
       i in dom PR by A34,FINSEQ_3:27; then
       PR.i = (PR^PR1).i by FINSEQ_1:def 7;
       hence thesis by A2,A34,A35;
      end;
      case (PR.n)`2 = 9; then
       consider i,f,p,x,y such that A36: 1 <= i & i < n and
                                    A37: Suc(f) = p.(x,y) &
               not y in still_not-bound_in (Ant(f)) &
               not y in still_not-bound_in All(x,p) & f = ((PR^PR1).i)`1 &
               Ant(f)^<*All(x,p)*> = ((PR^PR1).n)`1 by A2,A21,Def7;
       i <= len PR by A1,A36,AXIOMS:22; then
       i in dom PR by A36,FINSEQ_3:27; then
       PR.i = (PR^PR1).i by FINSEQ_1:def 7;
       hence thesis by A2,A36,A37;
      end;
   end;

theorem Th31:
  1 <= n & n <= len PR1 & PR1,n is_a_correct_step implies
    (PR^PR1),(n+len PR) is_a_correct_step
  proof
    assume that A1: 1 <= n & n <= len PR1 and
                A2: PR1,n is_a_correct_step;
    n in dom PR1 by A1,FINSEQ_3:27; then
A3: PR1.n = (PR^PR1).(n+len PR) by FINSEQ_1:def 7;
    n + len PR <= len PR + len PR1 by A1,AXIOMS:24; then
A4: 1 <= n+len PR & n+len PR <= len(PR^PR1) by A1,FINSEQ_1:35,NAT_1:37;
    per cases by A4,Th25;
       case ((PR^PR1).(n+len PR))`2  = 0;
       hence thesis by A2,A3,Def7;
       end;
       case ((PR^PR1).(n+len PR))`2 = 1;
       hence thesis by A2,A3,Def7;
       end;
       case ((PR^PR1).(n+len PR))`2 = 2; then
        consider i,f,g such that A5: 1 <= i & i < n and
A6:     Ant(f) is_Subsequence_of Ant(g) & Suc(f) = Suc(g) &
                                 (PR1.i)`1 = f & (PR1.n)`1 = g by A2,A3,Def7;
A7:     1 <= len PR+i & len PR+i < n+len PR by A5,NAT_1:37,REAL_1:53;
        i <= len PR1 by A1,A5,AXIOMS:22; then
        i in dom PR1 by A5,FINSEQ_3:27; then
        PR1.i = (PR^PR1).(len PR+i) by FINSEQ_1:def 7;
        hence thesis by A3,A6,A7;
       end;
       case ((PR^PR1).(n+len PR))`2 = 3; then
        consider i,j,f,f1 such that A7: 1 <= i & i < n & 1 <= j & j < i and
                                   A8: len f > 1 & len f1 > 1 & Ant(Ant(f)) =
                 Ant(Ant(f1)) & 'not' Suc(Ant(f)) = Suc(Ant(f1)) &
                 Suc(f) = Suc(f1) & f = (PR1.j)`1 & f1 = (PR1.i)`1 &
                 Ant(Ant(f))^<*Suc(f)*> = (PR1.n)`1 by A2,A3,Def7;
A9:     1 <= len PR+i & len PR+i < n+len PR by A7,NAT_1:37,REAL_1:53;
A10:    1 <= len PR+j & len PR+j < i+len PR by A7,NAT_1:37,REAL_1:53;
AA:     i <= len PR1 & j <= n by A1,A7,AXIOMS:22; then
        i in Seg(len PR1) & j <= len PR1 by A7,AXIOMS:22,FINSEQ_1:3; then
        i in dom PR1 & j in dom PR1 by AA,A7,FINSEQ_3:27; then
        PR1.i = (PR^PR1).(len PR+i) & PR1.j = (PR^PR1).(len PR+j)
                                                     by FINSEQ_1:def 7;
        hence thesis by A3,A8,A9,A10;
       end;
       case ((PR^PR1).(n+len PR))`2 = 4; then
        consider i,j,f,g,p such that A9: 1 <= i & i < n & 1 <= j & j < i and
                                     A10: len f > 1 & Ant(f) = Ant(g) &
                      Suc(Ant(f)) = 'not' p & 'not' Suc(f) = Suc(g) &
                  f = (PR1.j)`1 & g = (PR1.i)`1 & Ant(Ant(f))^<*p*> = (PR1.n)`1
                                                     by A2,A3,Def7;
A11:    1 <= len PR+i & len PR+i < n+len PR by A9,NAT_1:37,REAL_1:53;
A12:    1 <= len PR+j & len PR+j < i+len PR by A9,NAT_1:37,REAL_1:53;
AA:     i <= len PR1 & j <= n by A1,A9,AXIOMS:22; then
        i in Seg(len PR1) & j <= len PR1 by A9,AXIOMS:22,FINSEQ_1:3; then
        i in dom PR1 & j in dom PR1 by A9,AA,FINSEQ_3:27; then
        PR1.i = (PR^PR1).(len PR+i) & PR1.j = (PR^PR1).(len PR+j)
                                                             by FINSEQ_1:def 7;
        hence thesis by A3,A10,A11,A12;
       end;
       case ((PR^PR1).(n+len PR))`2 = 5; then
        consider i,j,f,g such that A11: 1 <= i & i < n & 1 <= j & j < i and
                                   A12: Ant(f) = Ant(g) & f = (PR1.j)`1 &
                 g = (PR1.i)`1 & Ant(f)^<*(Suc(f)) '&' (Suc(g))*> = (PR1.n)`1
                                                   by A2,A3,Def7;
A13:    1 <= len PR+i & len PR+i < n+len PR by A11,NAT_1:37,REAL_1:53;
A14:    1 <= len PR+j & len PR+j < i+len PR by A11,NAT_1:37,REAL_1:53;
AA:     i <= len PR1 & j <= n by A1,A11,AXIOMS:22; then
        i in Seg(len PR1) & j <= len PR1 by A11,AXIOMS:22,FINSEQ_1:3; then
        i in dom PR1 & j in dom PR1 by A11,AA,FINSEQ_3:27; then
        PR1.i = (PR^PR1).(len PR+i) & PR1.j = (PR^PR1).(len PR+j)
                                                            by FINSEQ_1:def 7;
        hence thesis by A3,A12,A13,A14;
       end;
       case ((PR^PR1).(n+len PR))`2 = 6; then
        consider i,f,p,q such that A13: 1 <= i & i < n and
                                   A14: p '&' q = Suc(f) & f = (PR1.i)`1 &
                                    Ant(f)^<*p*> = (PR1.n)`1 by A2,A3,Def7;
A15:    1 <= len PR+i & len PR+i < n+len PR by A13,NAT_1:37,REAL_1:53;
        i <= len PR1 by A1,A13,AXIOMS:22; then
        i in dom PR1 by A13,FINSEQ_3:27; then
        PR1.i = (PR^PR1).(len PR+i) by FINSEQ_1:def 7;
        hence thesis by A3,A14,A15;
       end;
       case ((PR^PR1).(n+len PR))`2 = 7; then
        consider i,f,p,q such that A15: 1 <= i & i < n and
                                   A16: p '&' q = Suc(f) & f = (PR1.i)`1 &
                                Ant(f)^<*q*>= (PR1.n)`1 by A2,A3,Def7;
A17:    1 <= len PR+i & len PR+i < n+len PR by A15,NAT_1:37,REAL_1:53;
        i <= len PR1 by A1,A15,AXIOMS:22; then
        i in dom PR1 by A15,FINSEQ_3:27; then
        PR1.i = (PR^PR1).(len PR+i) by FINSEQ_1:def 7;
        hence thesis by A3,A16,A17;
       end;
       case ((PR^PR1).(n+len PR))`2 = 8; then
        consider i,f,p,x,y such that A17: 1 <= i & i < n and
                                     A18: Suc(f) = All(x,p) &
                 f = (PR1.i)`1 & Ant(f)^<*p.(x,y)*> = (PR1.n)`1 by A2,A3,Def7;
A19:    1 <= len PR+i & len PR+i < n+len PR by A17,NAT_1:37,REAL_1:53;
        i <= len PR1 by A1,A17,AXIOMS:22; then
        i in dom PR1 by A17,FINSEQ_3:27; then
        PR1.i = (PR^PR1).(len PR+i) by FINSEQ_1:def 7;
        hence thesis by A3,A18,A19;
       end;
       case ((PR^PR1).(n+len PR))`2 = 9; then
        consider i,f,p,x,y such that A20: 1 <= i & i < n and
                                     A21: Suc(f) = p.(x,y) &
                                    not y in still_not-bound_in (Ant(f)) &
                                    not y in still_not-bound_in All(x,p) &
        f = (PR1.i)`1 & Ant(f)^<*All(x,p)*> = (PR1.n)`1 by A2,A3,Def7;
A22:    1 <= len PR+i & len PR+i < n+len PR by A20,NAT_1:37,REAL_1:53;
        i <= len PR1 by A1,A20,AXIOMS:22; then
        i in dom PR1 by A20,FINSEQ_3:27; then
        PR1.i = (PR^PR1).(len PR+i) by FINSEQ_1:def 7;
        hence thesis by A3,A21,A22;
       end;
  end;

theorem Th33:
  Ant(f) is_Subsequence_of Ant(g) & Suc(f) = Suc(g) & |- f implies |- g
  proof
    assume A1: Ant(f) is_Subsequence_of Ant(g) & Suc(f) = Suc(g) & |- f; then
    consider PR such that A2: PR is a_proof & (PR.(len PR))`1 = f by Def9;
A3: g in set_of_CQC-WFF-seq & 2 in Proof_Step_Kinds by Def5,CQC_THE1:43;
    now let a be set such that A4: a in rng <*[g,2]*>;
      a in {[g,2]} by A4,FINSEQ_1:55; then
      a = [g,2] by TARSKI:def 1;
      hence a in [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by A3,ZFMISC_1:106;
    end; then
    rng <*[g,2]*> c= [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by TARSKI:def 3;
    then reconsider PR1 = <*[g,2]*> as FinSequence of
                     [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by FINSEQ_1:def 4;
    set PR2 = PR^PR1;
    reconsider PR2 as FinSequence of [:set_of_CQC-WFF-seq,Proof_Step_Kinds:];
A5: PR <> {} by A2,Def8;
A6: PR2 <> {} by FINSEQ_1:48;
    now let n such that A7: 1 <= n & n <= len PR2;
A8:   n in dom PR2 by A7,FINSEQ_3:27;
A9:   now assume n in dom PR; then
A10:    1 <= n & n <= len PR by FINSEQ_3:27; then
        PR,n is_a_correct_step by A2,Def8;
        hence PR2,n is_a_correct_step by A10,Th30;
      end;
      now given k such that A11: k in dom PR1 & n = len PR + k;
        k in Seg 1 by A11,FINSEQ_1:55; then
A12:    k = 1 by FINSEQ_1:4,TARSKI:def 1; then
        PR1.k = [g,2]  by FINSEQ_1:57; then
        (PR1.k)`1 = g & (PR1.k)`2 = 2 by MCART_1:7; then
A13:    (PR2.n)`1 = g & (PR2.n)`2 = 2 by A11,FINSEQ_1:def 7;
A14:    1 <= len PR & len PR < n by A5,A11,A12,Th32,NAT_1:38; then
        len PR in dom PR by FINSEQ_3:27; then
        f = (PR2.(len PR))`1 by A2,FINSEQ_1:def 7;
        hence PR2,n is_a_correct_step by A1,A13,A14,Def7;
      end;
      hence PR2,n is_a_correct_step by A8,A9,FINSEQ_1:38;
    end; then
A15: PR2 is a_proof by A6,Def8;
    PR2.(len PR2) = PR2.(len PR + len PR1) by FINSEQ_1:35; then
A16: PR2.(len PR2) = PR2.(len PR + 1) by FINSEQ_1:56;
    1 in Seg 1 by FINSEQ_1:4,TARSKI:def 1; then
    1 in dom PR1 by FINSEQ_1:55; then
    PR2.(len PR2) = PR1.1 by A16,FINSEQ_1:def 7; then
    PR2.(len PR2) = [g,2] by FINSEQ_1:57; then
    (PR2.(len PR2))`1 = g by MCART_1:7;
    hence thesis by A15,Def9;
  end;

theorem Th34:
  1 < len f & 1 < len g & Ant(Ant(f)) = Ant(Ant(g)) &
    'not' Suc(Ant(f)) = Suc(Ant(g)) & Suc(f) = Suc(g) & |- f & |- g implies
       |- Ant(Ant(f))^<*Suc(f)*>
  proof
    assume A1: 1 < len f & 1 < len g & Ant(Ant(f)) = Ant(Ant(g)) &
          'not' Suc(Ant(f)) = Suc(Ant(g)) & Suc(f) = Suc(g) & |- f & |- g; then
    consider PR such that A2: PR is a_proof & f = (PR.(len PR))`1 by Def9;
    consider PR1 such that A3: PR1 is a_proof & g = (PR1.(len PR1))`1
                                                                    by A1,Def9;
A4: Ant(Ant(f))^<*Suc(f)*> in set_of_CQC-WFF-seq &
                                    3 in Proof_Step_Kinds by Def5,CQC_THE1:43;
    now let a be set such that A5: a in rng <*[Ant(Ant(f))^<*Suc(f)*>,3]*>;
      a in {[Ant(Ant(f))^<*Suc(f)*>,3]} by A5,FINSEQ_1:55; then
      a = [Ant(Ant(f))^<*Suc(f)*>,3] by TARSKI:def 1;
      hence a in [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by A4,ZFMISC_1:106;
    end; then
    rng <*[Ant(Ant(f))^<*Suc(f)*>,3]*> c=
                       [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by TARSKI:def 3;
    then reconsider PR2 = <*[Ant(Ant(f))^<*Suc(f)*>,3]*> as FinSequence of
                     [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by FINSEQ_1:def 4;
    set PR3 = PR^PR1^PR2;
    reconsider PR3 as FinSequence of [:set_of_CQC-WFF-seq,Proof_Step_Kinds:];
A6: PR <> {} by A2,Def8; then
A7: PR^PR1 <> {} by FINSEQ_1:48;
A8: PR3 <> {} by FINSEQ_1:48;
    now let n such that A9: 1 <= n & n <= len PR3;
A10:  n in dom PR3 by A9,FINSEQ_3:27;
A11:  now assume A12: n in dom (PR^PR1);
A13:    now assume n in dom PR; then
A14:      1 <= n & n <= len PR by FINSEQ_3:27; then
          PR,n is_a_correct_step by A2,Def8; then
A15:      PR^PR1,n is_a_correct_step by A14,Th30;
          len PR <= len (PR^PR1) by Th32; then
          1 <= n & n <= len (PR^PR1) by A14,AXIOMS:22;
          hence PR3,n is_a_correct_step by A15,Th30;
        end;
        now given k such that A16: k in dom PR1 & n = len PR + k;
A17:      1 <= k & k <= len PR1 by A16,FINSEQ_3:27; then
          PR1,k is_a_correct_step by A3,Def8; then
A18:      (PR^PR1),n is_a_correct_step by A16,A17,Th31;
A19:      n <= len PR1 + len PR by A16,A17,AXIOMS:24;
          k <= n by A16,NAT_1:29; then
          1 <= n & n <= len (PR^PR1) by A17,A19,FINSEQ_1:35,AXIOMS:22;
          hence PR3,n is_a_correct_step by A18,Th30;
        end;
        hence PR3,n is_a_correct_step by A12,A13,FINSEQ_1:38;
      end;
      now given k such that A20: k in dom PR2 & n = len (PR^PR1) + k;
        k in Seg 1 by A20,FINSEQ_1:55; then
A21:    k = 1 by FINSEQ_1:4,TARSKI:def 1; then
        PR2.k = [Ant(Ant(f))^<*Suc(f)*>,3] by FINSEQ_1:57; then
        (PR2.k)`1 = Ant(Ant(f))^<*Suc(f)*> & (PR2.k)`2 = 3 by MCART_1:7; then
A22:    (PR3.n)`1 = Ant(Ant(f))^<*Suc(f)*> & (PR3.n)`2 = 3
                                                        by A20,FINSEQ_1:def 7;
A23:    PR1 <> {} by A3,Def8; then
A24:    1 <= len PR & len PR < len (PR^PR1) by A6,Th32; then
        len PR in dom PR by FINSEQ_3:27; then
A26:    f = ((PR^PR1).(len PR))`1 by A2,FINSEQ_1:def 7;
        len PR in dom (PR^PR1) by A24,FINSEQ_3:27; then
A27:    f = (PR3.(len PR))`1 by A26,FINSEQ_1:def 7;
A28:    1 <= len (PR^PR1) & len (PR^PR1) < n by A7,A20,A21,Th32,NAT_1:38;
        1 <= len PR1 by A23,Th32; then
        len PR1 in dom PR1 by FINSEQ_3:27; then
        g = ((PR^PR1).(len PR + len PR1))`1 by A3,FINSEQ_1:def 7; then
A29:    g = ((PR^PR1).(len (PR^PR1)))`1 by FINSEQ_1:35;
        1 <= len (PR^PR1) by A7,Th32; then
        len (PR^PR1) in dom (PR^PR1) by FINSEQ_3:27; then
        g = (PR3.(len (PR^PR1)))`1 by A29,FINSEQ_1:def 7;
        hence PR3,n is_a_correct_step by A1,A22,A24,A27,A28,Def7;
      end;
      hence PR3,n is_a_correct_step by A10,A11,FINSEQ_1:38;
    end; then
A30: PR3 is a_proof by A8,Def8;
    PR3.(len PR3) = PR3.(len (PR^PR1) + len PR2) by FINSEQ_1:35; then
A31: PR3.(len PR3) = PR3.(len (PR^PR1) + 1) by FINSEQ_1:56;
    1 in Seg 1 by FINSEQ_1:4,TARSKI:def 1; then
    1 in dom PR2 by FINSEQ_1:55; then
    PR3.(len PR3) = PR2.1 by A31,FINSEQ_1:def 7; then
    PR3.(len PR3) = [Ant(Ant(f))^<*Suc(f)*>,3] by FINSEQ_1:57; then
    (PR3.(len PR3))`1 = Ant(Ant(f))^<*Suc(f)*> by MCART_1:7;
    hence thesis by A30,Def9;
  end;

theorem Th35:
  len f > 1 & Ant(f) = Ant(g) & Suc(Ant(f)) = 'not' p &
    'not' Suc(f) = Suc(g) & |- f & |- g implies |- Ant(Ant(f))^<*p*>
  proof
    assume A1: len f > 1 & Ant(f) = Ant(g) & Suc(Ant(f)) = 'not' p &
           'not' Suc(f) = Suc(g) & |- f & |- g; then
    consider PR such that A2: PR is a_proof & f = (PR.(len PR))`1 by Def9;
    consider PR1 such that A3: PR1 is a_proof & g = (PR1.(len PR1))`1
                                                  by A1,Def9;
A4: Ant(Ant(f))^<*p*> in set_of_CQC-WFF-seq &
                                    4 in Proof_Step_Kinds by Def5,CQC_THE1:43;
    now let a be set such that A5: a in rng <*[Ant(Ant(f))^<*p*>,4]*>;
      a in {[Ant(Ant(f))^<*p*>,4]} by A5,FINSEQ_1:55; then
      a = [Ant(Ant(f))^<*p*>,4] by TARSKI:def 1;
      hence a in [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by A4,ZFMISC_1:106;
    end; then
    rng <*[Ant(Ant(f))^<*p*>,4]*> c=
                       [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by TARSKI:def 3;
    then reconsider PR2 = <*[Ant(Ant(f))^<*p*>,4]*> as FinSequence of
                     [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by FINSEQ_1:def 4;
    set PR3 = PR^PR1^PR2;
    reconsider PR3 as FinSequence of [:set_of_CQC-WFF-seq,Proof_Step_Kinds:];
A6: PR <> {} by A2,Def8; then
A7: PR^PR1 <> {} by FINSEQ_1:48;
A8: PR3 <> {} by FINSEQ_1:48;
    now let n such that A9: 1 <= n & n <= len PR3;
A10:  n in dom PR3 by A9,FINSEQ_3:27;
A11:  now assume A12: n in dom (PR^PR1);
A13:    now assume n in dom PR; then
A14:      1 <= n & n <= len PR by FINSEQ_3:27; then
          PR,n is_a_correct_step by A2,Def8; then
A15:      PR^PR1,n is_a_correct_step by A14,Th30;
          len PR <= len (PR^PR1) by Th32; then
          1 <= n & n <= len (PR^PR1) by A14,AXIOMS:22;
          hence PR3,n is_a_correct_step by A15,Th30;
        end;
        now given k such that A16: k in dom PR1 & n = len PR + k;
A17:      1 <= k & k <= len PR1 by A16,FINSEQ_3:27; then
          PR1,k is_a_correct_step by A3,Def8; then
A18:      (PR^PR1),n is_a_correct_step by A16,A17,Th31;
A19:      n <= len PR1 + len PR by A16,A17,AXIOMS:24;
          k <= n by A16,NAT_1:29; then
          1 <= n & n <= len (PR^PR1) by A17,A19,FINSEQ_1:35,AXIOMS:22;
          hence PR3,n is_a_correct_step by A18,Th30;
        end;
        hence PR3,n is_a_correct_step by A12,A13,FINSEQ_1:38;
      end;
      now given k such that A20: k in dom PR2 & n = len (PR^PR1) + k;
        k in Seg 1 by A20,FINSEQ_1:55; then
A21:    k = 1 by FINSEQ_1:4,TARSKI:def 1; then
        PR2.k = [Ant(Ant(f))^<*p*>,4] by FINSEQ_1:57; then
        (PR2.k)`1 = Ant(Ant(f))^<*p*> & (PR2.k)`2 = 4 by MCART_1:7; then
A22:    (PR3.n)`1 = Ant(Ant(f))^<*p*> & (PR3.n)`2 = 4 by A20,FINSEQ_1:def 7;
A23:    PR1 <> {} by A3,Def8; then
A24:    1 <= len PR & len PR < len (PR^PR1) by A6,Th32; then
        len PR in dom PR by FINSEQ_3:27; then
A26:    f = ((PR^PR1).(len PR))`1 by A2,FINSEQ_1:def 7;
        len PR in dom (PR^PR1) by A24,FINSEQ_3:27; then
A27:    f = (PR3.(len PR))`1 by A26,FINSEQ_1:def 7;
A28:    1 <= len (PR^PR1) & len (PR^PR1) < n by A7,A20,A21,Th32,NAT_1:38;
        1 <= len PR1 by A23,Th32; then
        len PR1 in dom PR1 by FINSEQ_3:27; then
        g = ((PR^PR1).(len PR + len PR1))`1 by A3,FINSEQ_1:def 7; then
A29:    g = ((PR^PR1).(len (PR^PR1)))`1 by FINSEQ_1:35;
        1 <= len (PR^PR1) by A7,Th32; then
        len (PR^PR1) in dom (PR^PR1) by FINSEQ_3:27; then
        g = (PR3.(len (PR^PR1)))`1 by A29,FINSEQ_1:def 7;
        hence PR3,n is_a_correct_step by A1,A22,A24,A27,A28,Def7;
      end;
      hence PR3,n is_a_correct_step by A10,A11,FINSEQ_1:38;
    end; then
A30: PR3 is a_proof by A8,Def8;
    PR3.(len PR3) = PR3.(len (PR^PR1) + len PR2) by FINSEQ_1:35; then
A31: PR3.(len PR3) = PR3.(len (PR^PR1) + 1) by FINSEQ_1:56;
    1 in Seg 1 by FINSEQ_1:4,TARSKI:def 1; then
    1 in dom PR2 by FINSEQ_1:55; then
    PR3.(len PR3) = PR2.1 by A31,FINSEQ_1:def 7; then
    PR3.(len PR3) = [Ant(Ant(f))^<*p*>,4] by FINSEQ_1:57; then
    (PR3.(len PR3))`1 = Ant(Ant(f))^<*p*> by MCART_1:7;
    hence thesis by A30,Def9;
  end;

theorem Th36:
  Ant(f) = Ant(g) & |- f & |- g implies
    |- Ant(f)^<*(Suc(f)) '&' (Suc(g))*>
  proof
    assume A1: Ant(f) = Ant(g) & |- f & |- g; then
    consider PR such that A2: PR is a_proof & f = (PR.(len PR))`1 by Def9;
    consider PR1 such that A3: PR1 is a_proof & g = (PR1.(len PR1))`1
                                                                    by A1,Def9;
A4: Ant(f)^<*(Suc(f)) '&' (Suc(g))*> in set_of_CQC-WFF-seq &
                                    5 in Proof_Step_Kinds by Def5,CQC_THE1:43;
    now let a be set such that
                        A5: a in rng <*[Ant(f)^<*(Suc(f)) '&' (Suc(g))*>,5]*>;
      a in {[Ant(f)^<*(Suc(f)) '&' (Suc(g))*>,5]} by A5,FINSEQ_1:55; then
      a = [Ant(f)^<*(Suc(f)) '&' (Suc(g))*>,5] by TARSKI:def 1;
      hence a in [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by A4,ZFMISC_1:106;
    end; then
    rng <*[Ant(f)^<*(Suc(f)) '&' (Suc(g))*>,5]*> c=
                       [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by TARSKI:def 3;
    then reconsider PR2 = <*[Ant(f)^<*(Suc(f)) '&' (Suc(g))*>,5]*> as
      FinSequence of [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by FINSEQ_1:def 4;
    set PR3 = PR^PR1^PR2;
    reconsider PR3 as FinSequence of [:set_of_CQC-WFF-seq,Proof_Step_Kinds:];
A6: PR <> {} by A2,Def8; then
A7: PR^PR1 <> {} by FINSEQ_1:48;
A8: PR3 <> {} by FINSEQ_1:48;
    now let n such that A9: 1 <= n & n <= len PR3;
A10:  n in dom PR3 by A9,FINSEQ_3:27;
A11:  now assume A12: n in dom (PR^PR1);
A13:    now assume n in dom PR; then
A14:      1 <= n & n <= len PR by FINSEQ_3:27; then
          PR,n is_a_correct_step by A2,Def8; then
A15:      PR^PR1,n is_a_correct_step by A14,Th30;
          len PR <= len (PR^PR1) by Th32; then
          1 <= n & n <= len (PR^PR1) by A14,AXIOMS:22;
          hence PR3,n is_a_correct_step by A15,Th30;
        end;
        now given k such that A16: k in dom PR1 & n = len PR + k;
A17:      1 <= k & k <= len PR1 by A16,FINSEQ_3:27; then
          PR1,k is_a_correct_step by A3,Def8; then
A18:      (PR^PR1),n is_a_correct_step by A16,A17,Th31;
A19:      n <= len PR1 + len PR by A16,A17,AXIOMS:24;
          k <= n by A16,NAT_1:29; then
          1 <= n & n <= len (PR^PR1) by A17,A19,FINSEQ_1:35,AXIOMS:22;
          hence PR3,n is_a_correct_step by A18,Th30;
        end;
        hence PR3,n is_a_correct_step by A12,A13,FINSEQ_1:38;
      end;
      now given k such that A20: k in dom PR2 & n = len (PR^PR1) + k;
        k in Seg 1 by A20,FINSEQ_1:55; then
A21:    k = 1 by FINSEQ_1:4,TARSKI:def 1; then
        PR2.k = [Ant(f)^<*(Suc(f)) '&' (Suc(g))*>,5] by FINSEQ_1:57; then
        (PR2.k)`1 = Ant(f)^<*(Suc(f)) '&' (Suc(g))*> & (PR2.k)`2 = 5
                                                            by MCART_1:7; then
A22:    (PR3.n)`1 = Ant(f)^<*(Suc(f)) '&' (Suc(g))*> & (PR3.n)`2 = 5
                                                         by A20,FINSEQ_1:def 7;
A23:    PR1 <> {} by A3,Def8; then
A24:    1 <= len PR & len PR < len (PR^PR1) by A6,Th32; then
        len PR in dom PR by FINSEQ_3:27; then
A26:    f = ((PR^PR1).(len PR))`1 by A2,FINSEQ_1:def 7;
        len PR in dom (PR^PR1) by A24,FINSEQ_3:27; then
A27:    f = (PR3.(len PR))`1 by A26,FINSEQ_1:def 7;
A28:    1 <= len (PR^PR1) & len (PR^PR1) < n by A7,A20,A21,Th32,NAT_1:38;
        1 <= len PR1 by A23,Th32; then
        len PR1 in dom PR1 by FINSEQ_3:27; then
        g = ((PR^PR1).(len PR + len PR1))`1 by A3,FINSEQ_1:def 7; then
A29:    g = ((PR^PR1).(len (PR^PR1)))`1 by FINSEQ_1:35;
        1 <= len (PR^PR1) by A7,Th32; then
        len (PR^PR1) in dom (PR^PR1) by FINSEQ_3:27; then
        g = (PR3.(len (PR^PR1)))`1 by A29,FINSEQ_1:def 7;
        hence PR3,n is_a_correct_step by A1,A22,A24,A27,A28,Def7;
      end;
      hence PR3,n is_a_correct_step by A10,A11,FINSEQ_1:38;
    end; then
A30: PR3 is a_proof by A8,Def8;
    PR3.(len PR3) = PR3.(len (PR^PR1) + len PR2) by FINSEQ_1:35; then
A31: PR3.(len PR3) = PR3.(len (PR^PR1) + 1) by FINSEQ_1:56;
    1 in Seg 1 by FINSEQ_1:4,TARSKI:def 1; then
    1 in dom PR2 by FINSEQ_1:55; then
    PR3.(len PR3) = PR2.1 by A31,FINSEQ_1:def 7; then
    PR3.(len PR3) = [Ant(f)^<*(Suc(f)) '&' (Suc(g))*>,5] by FINSEQ_1:57; then
    (PR3.(len PR3))`1 = Ant(f)^<*(Suc(f)) '&' (Suc(g))*> by MCART_1:7;
    hence thesis by A30,Def9;
  end;

theorem Th37:
  p '&' q = Suc(f) & |- f implies |- Ant(f)^<*p*>
  proof
    assume A1: p '&' q = Suc(f) & |- f; then
    consider PR such that A2: PR is a_proof & (PR.(len PR))`1 = f by Def9;
A3:  Ant(f)^<*p*> in set_of_CQC-WFF-seq &
                                     6 in Proof_Step_Kinds by Def5,CQC_THE1:43;
    now let a be set such that A4: a in rng <*[Ant(f)^<*p*>,6]*>;
      a in {[Ant(f)^<*p*>,6]} by A4,FINSEQ_1:55; then
      a = [Ant(f)^<*p*>,6] by TARSKI:def 1;
      hence a in [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by A3,ZFMISC_1:106;
    end; then
    rng <*[Ant(f)^<*p*>,6]*> c=
                       [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by TARSKI:def 3;
    then reconsider PR1 = <*[Ant(f)^<*p*>,6]*> as FinSequence of
                     [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by FINSEQ_1:def 4;
    set PR2 = PR^PR1;
    reconsider PR2 as FinSequence of [:set_of_CQC-WFF-seq,Proof_Step_Kinds:];
A5: PR <> {} by A2,Def8;
A6: PR2 <> {} by FINSEQ_1:48;
    now let n such that A7: 1 <= n & n <= len PR2;
A8:   n in dom PR2 by A7,FINSEQ_3:27;
A9:   now assume n in dom PR; then
A10:    1 <= n & n <= len PR by FINSEQ_3:27; then
        PR,n is_a_correct_step by A2,Def8;
        hence PR2,n is_a_correct_step by A10,Th30;
      end;
      now given k such that A11: k in dom PR1 & n = len PR + k;
        k in Seg 1 by A11,FINSEQ_1:55; then
A12:    k = 1 by FINSEQ_1:4,TARSKI:def 1; then
        PR1.k = [Ant(f)^<*p*>,6] by FINSEQ_1:57; then
        (PR1.k)`1 = Ant(f)^<*p*> & (PR1.k)`2 = 6 by MCART_1:7; then
A13:    (PR2.n)`1 = Ant(f)^<*p*> & (PR2.n)`2 = 6 by A11,FINSEQ_1:def 7;
A14:    1 <= len PR & len PR < n by A5,A11,A12,Th32,NAT_1:38; then
        len PR in dom PR by FINSEQ_3:27; then
        f = (PR2.(len PR))`1 by A2,FINSEQ_1:def 7;
        hence PR2,n is_a_correct_step by A1,A13,A14,Def7;
      end;
      hence PR2,n is_a_correct_step by A8,A9,FINSEQ_1:38;
    end; then
A15: PR2 is a_proof by A6,Def8;
    PR2.(len PR2) = PR2.(len PR + len PR1) by FINSEQ_1:35; then
A16: PR2.(len PR2) = PR2.(len PR + 1) by FINSEQ_1:56;
    1 in Seg 1 by FINSEQ_1:4,TARSKI:def 1; then
    1 in dom PR1 by FINSEQ_1:55; then
    PR2.(len PR2) = PR1.1 by A16,FINSEQ_1:def 7; then
    PR2.(len PR2) = [Ant(f)^<*p*>,6] by FINSEQ_1:57; then
    (PR2.(len PR2))`1 = Ant(f)^<*p*> by MCART_1:7;
    hence thesis by A15,Def9;
  end;

theorem Th38:
  p '&' q = Suc(f) & |- f implies |- Ant(f)^<*q*>
  proof
    assume A1: p '&' q = Suc(f) & |- f; then
    consider PR such that A2: PR is a_proof & (PR.(len PR))`1 = f by Def9;
A3:  Ant(f)^<*q*> in set_of_CQC-WFF-seq &
                                     7 in Proof_Step_Kinds by Def5,CQC_THE1:43;
    now let a be set such that A4: a in rng <*[Ant(f)^<*q*>,7]*>;
      a in {[Ant(f)^<*q*>,7]} by A4,FINSEQ_1:55; then
      a = [Ant(f)^<*q*>,7] by TARSKI:def 1;
      hence a in [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by A3,ZFMISC_1:106;
    end; then
    rng <*[Ant(f)^<*q*>,7]*> c=
                       [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by TARSKI:def 3;
    then reconsider PR1 = <*[Ant(f)^<*q*>,7]*> as FinSequence of
                     [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by FINSEQ_1:def 4;
    set PR2 = PR^PR1;
    reconsider PR2 as FinSequence of [:set_of_CQC-WFF-seq,Proof_Step_Kinds:];
A5: PR <> {} by A2,Def8;
A6: PR2 <> {} by FINSEQ_1:48;
    now let n such that A7: 1 <= n & n <= len PR2;
A8:   n in dom PR2 by A7,FINSEQ_3:27;
A9:   now assume n in dom PR; then
A10:    1 <= n & n <= len PR by FINSEQ_3:27; then
        PR,n is_a_correct_step by A2,Def8;
        hence PR2,n is_a_correct_step by A10,Th30;
      end;
      now given k such that A11: k in dom PR1 & n = len PR + k;
        k in Seg 1 by A11,FINSEQ_1:55; then
A12:    k = 1 by FINSEQ_1:4,TARSKI:def 1; then
        PR1.k = [Ant(f)^<*q*>,7] by FINSEQ_1:57; then
        (PR1.k)`1 = Ant(f)^<*q*> & (PR1.k)`2 = 7 by MCART_1:7; then
A13:    (PR2.n)`1 = Ant(f)^<*q*> & (PR2.n)`2 = 7 by A11,FINSEQ_1:def 7;
A14:    1 <= len PR & len PR < n by A5,A11,A12,Th32,NAT_1:38; then
        len PR in dom PR by FINSEQ_3:27; then
        f = (PR2.(len PR))`1 by A2,FINSEQ_1:def 7;
        hence PR2,n is_a_correct_step by A1,A13,A14,Def7;
      end;
      hence PR2,n is_a_correct_step by A8,A9,FINSEQ_1:38;
    end; then
A15: PR2 is a_proof by A6,Def8;
    PR2.(len PR2) = PR2.(len PR + len PR1) by FINSEQ_1:35; then
A16: PR2.(len PR2) = PR2.(len PR + 1) by FINSEQ_1:56;
    1 in Seg 1 by FINSEQ_1:4,TARSKI:def 1; then
    1 in dom PR1 by FINSEQ_1:55; then
    PR2.(len PR2) = PR1.1 by A16,FINSEQ_1:def 7; then
    PR2.(len PR2) = [Ant(f)^<*q*>,7] by FINSEQ_1:57; then
    (PR2.(len PR2))`1 = Ant(f)^<*q*> by MCART_1:7;
    hence thesis by A15,Def9;
  end;

theorem Th39:
  Suc(f) = All(x,p) & |- f implies |- Ant(f)^<*p.(x,y)*>
  proof
    assume A1: Suc(f) = All(x,p) & |- f; then
    consider PR such that A2: PR is a_proof & (PR.(len PR))`1 = f by Def9;
A3: Ant(f)^<*p.(x,y)*> in set_of_CQC-WFF-seq &
                                     8 in Proof_Step_Kinds by Def5,CQC_THE1:43;
    now let a be set such that A4: a in rng <*[Ant(f)^<*p.(x,y)*>,8]*>;
      a in {[Ant(f)^<*p.(x,y)*>,8]} by A4,FINSEQ_1:55; then
      a = [Ant(f)^<*p.(x,y)*>,8] by TARSKI:def 1;
      hence a in [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by A3,ZFMISC_1:106;
    end; then
    rng <*[Ant(f)^<*p.(x,y)*>,8]*> c=
                       [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by TARSKI:def 3;
    then reconsider PR1 = <*[Ant(f)^<*p.(x,y)*>,8]*> as FinSequence of
                     [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by FINSEQ_1:def 4;
    set PR2 = PR^PR1;
    reconsider PR2 as FinSequence of [:set_of_CQC-WFF-seq,Proof_Step_Kinds:];
A5: PR <> {} by A2,Def8;
A6: PR2 <> {} by FINSEQ_1:48;
    now let n such that A7: 1 <= n & n <= len PR2;
A8:   n in dom PR2 by A7,FINSEQ_3:27;
A9:   now assume n in dom PR; then
A10:    1 <= n & n <= len PR by FINSEQ_3:27; then
        PR,n is_a_correct_step by A2,Def8;
        hence PR2,n is_a_correct_step by A10,Th30;
      end;
      now given k such that A11: k in dom PR1 & n = len PR + k;
        k in Seg 1 by A11,FINSEQ_1:55; then
A12:    k = 1 by FINSEQ_1:4,TARSKI:def 1; then
        PR1.k = [Ant(f)^<*p.(x,y)*>,8] by FINSEQ_1:57; then
        (PR1.k)`1 = Ant(f)^<*p.(x,y)*> & (PR1.k)`2 = 8 by MCART_1:7; then
A13:    (PR2.n)`1 = Ant(f)^<*p.(x,y)*> & (PR2.n)`2 = 8 by A11,FINSEQ_1:def 7;
A14:    1 <= len PR & len PR < n by A5,A11,A12,Th32,NAT_1:38; then
        len PR in dom PR by FINSEQ_3:27; then
        f = (PR2.(len PR))`1 by A2,FINSEQ_1:def 7;
        hence PR2,n is_a_correct_step by A1,A13,A14,Def7;
      end;
      hence PR2,n is_a_correct_step by A8,A9,FINSEQ_1:38;
    end; then
A15: PR2 is a_proof by A6,Def8;
    PR2.(len PR2) = PR2.(len PR + len PR1) by FINSEQ_1:35; then
A16: PR2.(len PR2) = PR2.(len PR + 1) by FINSEQ_1:56;
    1 in Seg 1 by FINSEQ_1:4,TARSKI:def 1; then
    1 in dom PR1 by FINSEQ_1:55; then
    PR2.(len PR2) = PR1.1 by A16,FINSEQ_1:def 7; then
    PR2.(len PR2) = [Ant(f)^<*p.(x,y)*>,8] by FINSEQ_1:57; then
    (PR2.(len PR2))`1 = Ant(f)^<*p.(x,y)*> by MCART_1:7;
    hence thesis by A15,Def9;
  end;

theorem Th40:
  Suc(f) = p.(x,y) & not y in still_not-bound_in Ant(f) &
    not y in still_not-bound_in All(x,p) & |- f implies
       |- Ant(f)^<*All(x,p)*>
  proof
    assume A1: Suc(f) = p.(x,y) & not y in still_not-bound_in (Ant(f)) &
               not y in still_not-bound_in All(x,p) & |- f; then
    consider PR such that A2: PR is a_proof & (PR.(len PR))`1 = f by Def9;
A3: Ant(f)^<*All(x,p)*> in set_of_CQC-WFF-seq &
                                     9 in Proof_Step_Kinds by Def5,CQC_THE1:43;
    now let a be set such that A4: a in rng <*[Ant(f)^<*All(x,p)*>,9]*>;
      a in {[Ant(f)^<*All(x,p)*>,9]} by A4,FINSEQ_1:55; then
      a = [Ant(f)^<*All(x,p)*>,9] by TARSKI:def 1;
      hence a in [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by A3,ZFMISC_1:106;
    end; then
    rng <*[Ant(f)^<*All(x,p)*>,9]*> c=
                       [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by TARSKI:def 3;
    then reconsider PR1 = <*[Ant(f)^<*All(x,p)*>,9]*> as FinSequence of
                     [:set_of_CQC-WFF-seq,Proof_Step_Kinds:] by FINSEQ_1:def 4;
    set PR2 = PR^PR1;
    reconsider PR2 as FinSequence of [:set_of_CQC-WFF-seq,Proof_Step_Kinds:];
A5: PR <> {} by A2,Def8;
A6: PR2 <> {} by FINSEQ_1:48;
    now let n such that A7: 1 <= n & n <= len PR2;
A8:   n in dom PR2 by A7,FINSEQ_3:27;
A9:   now assume n in dom PR; then
A10:    1 <= n & n <= len PR by FINSEQ_3:27; then
        PR,n is_a_correct_step by A2,Def8;
        hence PR2,n is_a_correct_step by A10,Th30;
      end;
      now given k such that A11: k in dom PR1 & n = len PR + k;
        k in Seg 1 by A11,FINSEQ_1:55; then
A12:    k = 1 by FINSEQ_1:4,TARSKI:def 1; then
        PR1.k = [Ant(f)^<*All(x,p)*>,9] by FINSEQ_1:57; then
        (PR1.k)`1 = Ant(f)^<*All(x,p)*> & (PR1.k)`2 = 9 by MCART_1:7; then
A13:    (PR2.n)`1 = Ant(f)^<*All(x,p)*> & (PR2.n)`2 = 9 by A11,FINSEQ_1:def 7;
A14:    1 <= len PR & len PR < n by A5,A11,A12,Th32,NAT_1:38; then
        len PR in dom PR by FINSEQ_3:27; then
        f = (PR2.(len PR))`1 by A2,FINSEQ_1:def 7;
        hence PR2,n is_a_correct_step by A1,A13,A14,Def7;
      end;
      hence PR2,n is_a_correct_step by A8,A9,FINSEQ_1:38;
    end; then
A15: PR2 is a_proof by A6,Def8;
    PR2.(len PR2) = PR2.(len PR + len PR1) by FINSEQ_1:35; then
A16: PR2.(len PR2) = PR2.(len PR + 1) by FINSEQ_1:56;
    1 in Seg 1 by FINSEQ_1:4,TARSKI:def 1; then
    1 in dom PR1 by FINSEQ_1:55; then
    PR2.(len PR2) = PR1.1 by A16,FINSEQ_1:def 7; then
    PR2.(len PR2) = [Ant(f)^<*All(x,p)*>,9] by FINSEQ_1:57; then
    (PR2.(len PR2))`1 = Ant(f)^<*All(x,p)*> by MCART_1:7;
    hence thesis by A15,Def9;
  end;

theorem Th44:
  |- f & |- Ant(f)^<*'not' Suc(f)*> implies |- Ant(f)^<*p*>
  proof
    assume A1: |- f & |- Ant(f)^<*'not' Suc(f)*>;
    set f1 = Ant(f)^<*'not' p*>^<*Suc(f)*>;
    Ant(f1) = Ant(f)^<*'not' p*> by Th24; then
    Ant(f) is_Subsequence_of Ant(f1) & Suc(f) = Suc(f1) by Th24,Th42; then
A2: |- f1 by A1,Th33;
    set f2 = Ant(f)^<*'not' Suc(f)*>;
    set f3 = Ant(f)^<*'not' p*>^<*'not' Suc(f)*>;
    Ant(f3) = Ant(f)^<*'not' p*> & Ant(f2) = Ant(f) by Th24; then
A3: Ant(f2) is_Subsequence_of Ant(f3) by Th42;
    Suc(f2) = 'not' Suc(f) by Th24; then
    Suc(f2) = Suc(f3) by Th24; then
A4: |- f3 by A1,A3,Th33;
A5: Ant(f1) = Ant(f)^<*'not' p*> by Th24; then
A6: Ant(f1) = Ant(f3) by Th24;
A7: Suc(Ant(f1)) = 'not' p by A5,Th24;
    Suc(f1) = Suc(f) by Th24; then
A8: 'not' Suc(f1) = Suc(f3) by Th24;
    1< len f1 by Th43; then
    |- Ant(Ant(f1))^<*p*> by A2,A4,A6,A7,A8,Th35;
    hence thesis by A5,Th24;
  end;

theorem Th46:
  1 <= len f & |- f & |- f^<*p*> implies |- Ant(f)^<*p*>
  proof
    assume A1: 1 <= len f & |- f & |- f^<*p*>;
    set f1 = Ant(f)^<*'not' Suc(f)*>^<*Suc(f)*>;
    Ant(f1) = Ant(f)^<*'not' Suc(f)*> by Th24; then
    Ant(f) is_Subsequence_of Ant(f1) & Suc(f1) = Suc(f) by Th24,Th42; then
A2: |- f1 by A1,Th33;
    set f2 = Ant(f)^<*'not' Suc(f)*>^<*'not' Suc(f)*>;
    (Ant(f)^<*'not' Suc(f)*>).(len Ant(f) + 1) = 'not' Suc(f) by FINSEQ_1:59;
    then (Ant(f2)).(len Ant(f) + 1) = 'not' Suc(f) by Th24; then
A3: (Ant(f2)).(len Ant(f) + 1) = Suc(f2) by Th24;
A4: Ant(f2) = Ant(f)^<*'not' Suc(f)*> by Th24; then
A5: (Ant(f2)).(len Ant(f2)) = Suc(f2) by A3,FINSEQ_2:19;
    len Ant(f2) in dom Ant(f2) by A4,Th45; then
    Suc(f2) is_tail_of Ant(f2) by A5,Def3; then
A6: |- f2 by Th29;
    f2 = Ant(f1)^<*'not' Suc(f)*> by Th24; then
    f2 = Ant(f1)^<*'not' Suc(f1)*> by Th24; then
A7: |- Ant(f1)^<*p*> by A2,A6,Th44;
    set f3 = f^<*p*>;
    set f4 = Ant(f1)^<*p*>;
    1+1 <= len f + 1 by A1,AXIOMS:24; then
    1+1 <= len f + len <*p*> by FINSEQ_1:56; then
    1+1 <= len f3 by FINSEQ_1:35; then
A8: 1 < len f3 by NAT_1:38;
A9: Ant(f1) = Ant(f)^<*'not' Suc(f)*> by Th24; then
    1 <= len Ant(f1) by Th45; then
    1+1 <= len Ant(f1) + 1 by AXIOMS:24; then
    1+1 <= len Ant(f1) + len <*p*> by FINSEQ_1:56; then
    1+1 <= len f4 by FINSEQ_1:35; then
A10: 1 < len f4 by NAT_1:38;
    Ant(f4) = Ant(f1) by Th24; then
    Ant(Ant(f4)) = Ant(f) by A9,Th24; then
A11: Ant(Ant(f4)) = Ant(Ant(f3)) by Th24;
    Suc(Ant(f1)) = 'not' Suc(f) by A9,Th24; then
    Suc(Ant(f1)) = 'not' Suc(Ant(f3)) by Th24; then
A12: Suc(Ant(f4)) = 'not' Suc(Ant(f3)) by Th24;
    Suc(f3) = p & Suc(f4) = p by Th24; then
    |- Ant(Ant(f3))^<*Suc(f3)*> by A1,A7,A8,A10,A11,A12,Th34; then
    |- Ant(f)^<*Suc(f3)*> by Th24;
    hence thesis by Th24;
  end;

theorem Th51:
  |- f^<*p*>^<*q*> implies |- f^<*'not' q*>^<*'not' p*>
  proof
    set f1 = f^<*p*>^<*q*>;
    set f2 = Ant(Ant(f1))^<*'not' q*>^<*p*>^<*q*>;
    assume A1: |- f1;
A2: Ant(f1) = f^<*p*> & Ant(f2) = Ant(Ant(f1))^<*'not' q*>^<*p*> by Th24; then
A3: Ant(Ant(f1)) = f & Suc(Ant(f1)) = p & Suc(f1) = q by Th24; then
A4: Suc(f1) = Suc(f2) by Th24;
    0+1 <= len Ant(f1) by A2,Th45; then
    0 < len Ant(f1); then
    Ant(f1) is_Subsequence_of Ant(f2) by A2,A3,Th49; then
A6: |- f2 by A1,A4,Th33;
    set f3 = Ant(Ant(f1))^<*'not' q*>^<*p*>^<*'not' q*>;
    Ant(f3) = Ant(Ant(f1))^<*'not' q*>^<*p*> by Th24; then
    Ant(f3) = Ant(Ant(f1))^(<*'not' q*>^<*p*>) by FINSEQ_1:45; then
A7: Ant(f3) = Ant(Ant(f1))^<*'not' q,p*> by FINSEQ_1:def 9; then
    (Ant(f3)).(len f+1) = 'not' q by A3,Th50; then
A8: (Ant(f3)).(len f+1) = Suc(f3) by Th24;
    1 in dom <*'not' q,p*> by Th50; then
    len f+1 in dom (Ant(f3)) by A3,A7,FINSEQ_1:41; then
A9: Suc(f3) is_tail_of Ant(f3) by A8,Def3;
    Suc(f2) = q & Ant(f2) = Ant(Ant(f1))^<*'not' q*>^<*p*> by Th24; then
    |- Ant(f2)^<*'not' Suc(f2)*> by A9,Th29; then
A11: |- Ant(f2)^<* 'not' p*> by A6,Th44;
    set f4 = Ant(Ant(f1))^<*'not' q*>^<*'not' p*>^<*'not' p*>;
A12: Ant(f4) = Ant(Ant(f1))^<*'not' q*>^<*'not' p*> by Th24; then
    Ant(f4) = Ant(Ant(f1))^(<*'not' q*>^<*'not' p*>) by FINSEQ_1:45; then
A13: Ant(f4) = Ant(Ant(f1))^<*'not' q,'not' p*> by FINSEQ_1:def 9; then
    (Ant(f4)).(len f+2) = 'not' p by A3,Th50; then
A14: (Ant(f4)).(len f+2) = Suc(f4) by Th24;
    2 in dom <*'not' q,'not' p*> by Th50; then
    len f+2 in dom (Ant(f4)) by A3,A13,FINSEQ_1:41; then
    Suc(f4) is_tail_of Ant(f4) by A14,Def3; then
A15: |- f4 by Th29;
A16: 1 < len (Ant(f2)^<* 'not' p*>) & 1 < len f4 by A2,Th43;
A17: Ant(Ant(f2)^<* 'not' p*>) = Ant(Ant(f1))^<*'not' q*>^<*p*> by A2,Th24;then
A18: Ant(Ant(Ant(f2)^<* 'not' p*>)) = Ant(Ant(f1))^<*'not' q*> by Th24; then
A19: Ant(Ant(f4)) = Ant(Ant(Ant(f2)^<* 'not' p*>)) by A12,Th24;
     Suc(Ant(Ant(f2)^<* 'not' p*>)) = p by A17,Th24; then
A20: 'not' Suc(Ant(Ant(f2)^<* 'not' p*>)) = Suc(Ant(f4)) by A12,Th24;
A21: Suc(Ant(f2)^<* 'not' p*>) = 'not' p by Th24; then
     Suc(Ant(f2)^<* 'not' p*>) = Suc(f4) by Th24;
     hence thesis by A3,A11,A15,A16,A18,A19,A20,A21,Th34;
  end;

theorem
  |- f^<*'not' p*>^<*'not' q*> implies |- f^<*q*>^<*p*>
  proof
    set f1 = f^<*'not' p*>^<*'not' q*>;
    set f2 = Ant(Ant(f1))^<*q*>^<*'not' p*>^<*'not' q*>;
    assume A1: |- f1;
A2: Ant(f1) = f^<*'not' p*> & Ant(f2) = Ant(Ant(f1))^<*q*>^<*'not' p*>
                                                                  by Th24; then
A3: Ant(Ant(f1)) = f & Suc(Ant(f1)) = 'not' p & Suc(f1) = 'not' q by Th24; then
A4: Suc(f1) = Suc(f2) by Th24;
    0+1 <= len Ant(f1) by A2,Th45; then
    0 < len Ant(f1); then
    Ant(f1) is_Subsequence_of Ant(f2) by A2,A3,Th49; then
A6: |- f2 by A1,A4,Th33;
    set f3 = Ant(Ant(f1))^<*q*>^<*'not' p*>^<*q*>;
A7: Ant(f3) = Ant(Ant(f1))^<*q*>^<*'not' p*> by Th24; then
    Ant(f3) = Ant(Ant(f1))^(<*q*>^<*'not' p*>) by FINSEQ_1:45; then
A8: Ant(f3) = Ant(Ant(f1))^<*q,'not' p*> by FINSEQ_1:def 9; then
    (Ant(f3)).(len f+1) = q by A3,Th50; then
A9: (Ant(f3)).(len f+1) = Suc(f3) by Th24;
    1 in dom <*q,'not' p*> by Th50; then
    len f+1 in dom (Ant(f3)) by A3,A8,FINSEQ_1:41; then
    Suc(f3) is_tail_of Ant(f3) by A9,Def3; then
A10: |- f3 by Th29;
    Suc(f2) = 'not' q & Ant(f2) = Ant(Ant(f1))^<*q*>^<*'not' p*> by Th24; then
A11: Suc(f2) = 'not' Suc(f3) & Ant(f2) = Ant(f3) by Th24;
    0+1 <= len f2 by Th45; then
    0 < len f2; then
    |- Ant(f3)^<*'not' Suc(f3)*> by A6,A11,Th7; then
A12: |- Ant(f3)^<*p*> by A10,Th44;
    set f4 = Ant(Ant(f1))^<*q*>^<*p*>^<*p*>;
A13: Ant(f4) = Ant(Ant(f1))^<*q*>^<*p*> by Th24; then
    Ant(f4) = Ant(Ant(f1))^(<*q*>^<*p*>) by FINSEQ_1:45; then
A14: Ant(f4) = Ant(Ant(f1))^<*q,p*> by FINSEQ_1:def 9; then
    (Ant(f4)).(len f+2) = p by A3,Th50; then
A15: (Ant(f4)).(len f+2) = Suc(f4) by Th24;
    2 in dom <*q,p*> by Th50; then
    len f+2 in dom (Ant(f4)) by A3,A14,FINSEQ_1:41; then
    Suc(f4) is_tail_of Ant(f4) by A15,Def3; then
A16: |- f4 by Th29;
A17: 1 < len (Ant(f3)^<*p*>) & 1 < len f4 by A7,Th43;
A18: Ant(Ant(f3)^<*p*>) = Ant(Ant(f1))^<*q*>^<*'not' p*> by A7,Th24; then
A19: Ant(Ant(Ant(f3)^<*p*>)) = Ant(Ant(f1))^<*q*> by Th24; then
A20: Ant(Ant(f4)) = Ant(Ant(Ant(f3)^<*p*>)) by A13,Th24;
     Suc(Ant(Ant(f3)^<*p*>)) = 'not' p by A18,Th24; then
A21: Suc(Ant(Ant(f3)^<*p*>)) = 'not' Suc(Ant(f4)) by A13,Th24;
A22: Suc(Ant(f3)^<*p*>) = p by Th24; then
     Suc(Ant(f3)^<*p*>) = Suc(f4) by Th24;
     hence thesis by A3,A12,A16,A17,A19,A20,A21,A22,Th34;
  end;

theorem Th53:
  |- f^<*'not' p*>^<*q*> implies |- f^<*'not' q*>^<*p*>
  proof
    set f1 = f^<*'not' p*>^<*q*>;
    set f2 = Ant(Ant(f1))^<*'not' q*>^<*'not' p*>^<*q*>;
    assume A1: |- f1;
A2: Ant(f1) = f^<*'not' p*> & Ant(f2) = Ant(Ant(f1))^<*'not' q*>^<*'not' p*>
                                                                by Th24; then
A3: Ant(Ant(f1)) = f & Suc(Ant(f1)) = 'not' p & Suc(f1) = q by Th24; then
A4: Suc(f1) = Suc(f2) by Th24;
    0+1 <= len Ant(f1) by A2,Th45; then
    0 < len Ant(f1); then
    Ant(f1) is_Subsequence_of Ant(f2) by A2,A3,Th49; then
A6: |- f2 by A1,A4,Th33;
    set f3 = Ant(Ant(f1))^<*'not' q*>^<*'not' p*>^<*'not' q*>;
    Ant(f3) = Ant(Ant(f1))^<*'not' q*>^<*'not' p*> by Th24; then
    Ant(f3) = Ant(Ant(f1))^(<*'not' q*>^<*'not' p*>) by FINSEQ_1:45; then
A7: Ant(f3) = Ant(Ant(f1))^<*'not' q,'not' p*> by FINSEQ_1:def 9; then
    (Ant(f3)).(len f+1) = 'not' q by A3,Th50; then
A8: (Ant(f3)).(len f+1) = Suc(f3) by Th24;
    1 in dom <*'not' q,'not' p*> by Th50; then
    len f+1 in dom (Ant(f3)) by A3,A7,FINSEQ_1:41; then
A9: Suc(f3) is_tail_of Ant(f3) by A8,Def3;
    Suc(f2) = q & Ant(f2) = Ant(Ant(f1))^<*'not' q*>^<*'not' p*> by Th24; then
    |- Ant(f2)^<*'not' Suc(f2)*> by A9,Th29; then
A11: |- Ant(f2)^<*p*> by A6,Th44;
    set f4 = Ant(Ant(f1))^<*'not' q*>^<*p*>^<*p*>;
A12: Ant(f4) = Ant(Ant(f1))^<*'not' q*>^<*p*> by Th24; then
    Ant(f4) = Ant(Ant(f1))^(<*'not' q*>^<*p*>) by FINSEQ_1:45; then
A13: Ant(f4) = Ant(Ant(f1))^<*'not' q,p*> by FINSEQ_1:def 9; then
    (Ant(f4)).(len f+2) = p by A3,Th50; then
A14: (Ant(f4)).(len f+2) = Suc(f4) by Th24;
    2 in dom <*'not' q,p*> by Th50; then
    len f+2 in dom (Ant(f4)) by A3,A13,FINSEQ_1:41; then
    Suc(f4) is_tail_of Ant(f4) by A14,Def3; then
A15: |- f4 by Th29;
A16: 1 < len (Ant(f2)^<* p*>) & 1 < len f4 by A2,Th43;
A17: Ant(Ant(f2)^<*p*>) = Ant(Ant(f1))^<*'not' q*>^<*'not' p*> by A2,Th24;then
     Ant(Ant(Ant(f2)^<*p*>)) = Ant(Ant(f1))^<*'not' q*> by Th24; then
A19: Ant(Ant(f4)) = Ant(Ant(Ant(f2)^<*p*>)) by A12,Th24;
     Suc(Ant(Ant(f2)^<* p*>)) = 'not' p by A17,Th24; then
A20: Suc(Ant(Ant(f2)^<*p*>)) = 'not' Suc(Ant(f4)) by A12,Th24;
A21: Suc(f4) = p by Th24; then
     Suc(Ant(f2)^<*p*>) = Suc(f4) by Th24; then
     |- Ant(Ant(f4))^<*p*> by A11,A15,A16,A19,A20,A21,Th34;
     hence thesis by A3,A12,Th24;
  end;

theorem Th54:
  |- f^<*p*>^<*'not' q*> implies |- f^<*q*>^<*'not' p*>
  proof
    set f1 = f^<*p*>^<*'not' q*>;
    set f2 = Ant(Ant(f1))^<*q*>^<*p*>^<*'not' q*>;
    assume A1: |- f1;
A2: Ant(f1) = f^<*p*> & Ant(f2) = Ant(Ant(f1))^<*q*>^<*p*> by Th24; then
A3: Ant(Ant(f1)) = f & Suc(Ant(f1)) = p & Suc(f1) = 'not' q by Th24; then
A4: Suc(f1) = Suc(f2) by Th24;
    0+1 <= len Ant(f1) by A2,Th45; then
    0 < len Ant(f1); then
    Ant(f1) is_Subsequence_of Ant(f2) by A2,A3,Th49; then
A6: |- f2 by A1,A4,Th33;
    set f3 = Ant(Ant(f1))^<*q*>^<*p*>^<*q*>;
A7: Ant(f3) = Ant(Ant(f1))^<*q*>^<*p*> by Th24; then
    Ant(f3) = Ant(Ant(f1))^(<*q*>^<*p*>) by FINSEQ_1:45; then
A8: Ant(f3) = Ant(Ant(f1))^<*q,p*> by FINSEQ_1:def 9; then
    (Ant(f3)).(len f+1) = q by A3,Th50; then
A9: (Ant(f3)).(len f+1) = Suc(f3) by Th24;
    1 in dom <*q,p*> by Th50; then
    len f+1 in dom (Ant(f3)) by A3,A8,FINSEQ_1:41; then
    Suc(f3) is_tail_of Ant(f3) by A9,Def3; then
A10: |- f3 by Th29;
    Suc(f2) = 'not' q & Ant(f2) = Ant(Ant(f1))^<*q*>^<*p*> by Th24; then
A11: Suc(f2) = 'not' Suc(f3) & Ant(f2) = Ant(f3) by Th24;
    0+1 <= len f2 by Th45; then
    0 < len f2; then
    |- Ant(f3)^<*'not' Suc(f3)*> by A6,A11,Th7; then
A12: |- Ant(f3)^<*'not' p*> by A10,Th44;
    set f4 = Ant(Ant(f1))^<*q*>^<*'not' p*>^<*'not' p*>;
A13: Ant(f4) = Ant(Ant(f1))^<*q*>^<*'not' p*> by Th24; then
    Ant(f4) = Ant(Ant(f1))^(<*q*>^<*'not' p*>) by FINSEQ_1:45; then
A14: Ant(f4) = Ant(Ant(f1))^<*q,'not' p*> by FINSEQ_1:def 9; then
    (Ant(f4)).(len f+2) = 'not' p by A3,Th50; then
A15: (Ant(f4)).(len f+2) = Suc(f4) by Th24;
    2 in dom <*q,'not' p*> by Th50; then
    len f+2 in dom (Ant(f4)) by A3,A14,FINSEQ_1:41; then
    Suc(f4) is_tail_of Ant(f4) by A15,Def3; then
A16: |- f4 by Th29;
A17: 1 < len (Ant(f3)^<*'not' p*>) & 1 < len f4 by A7,Th43;
A18: Ant(Ant(f3)^<*'not' p*>) = Ant(Ant(f1))^<*q*>^<*p*> by A7,Th24; then
A19: Ant(Ant(Ant(f3)^<*'not' p*>)) = Ant(Ant(f1))^<*q*> by Th24; then
A20: Ant(Ant(f4)) = Ant(Ant(Ant(f3)^<*'not' p*>)) by A13,Th24;
     Suc(Ant(Ant(f3)^<*'not' p*>)) = p by A18,Th24; then
A21: 'not' Suc(Ant(Ant(f3)^<*'not' p*>)) = Suc(Ant(f4)) by A13,Th24;
A22: Suc(Ant(f3)^<*'not' p*>) = 'not' p by Th24; then
     Suc(Ant(f3)^<*'not' p*>) = Suc(f4) by Th24;
     hence thesis by A3,A12,A16,A17,A19,A20,A21,A22,Th34;
  end;

theorem
  |- f^<*p*>^<*r*> & |- f^<*q*>^<*r*> implies |- f^<*p 'or' q*>^<*r*>
  proof
    set f1 = f^<*'not' r*>^<*'not' p*>;
    set f2 = f^<*'not' r*>^<*'not' q*>;
    assume |- f^<*p*>^<*r*> & |- f^<*q*>^<*r*>; then
A1: |- f1 & |- f2  by Th51;
A2: Suc(f1) = 'not' p & Suc(f2) = 'not' q & Ant(f1) = f^<*'not' r*> by Th24;
    then Ant(f1) = Ant(f2) by Th24; then
    |- f^<*'not' r*>^<*'not' p '&' 'not' q*> by A1,A2,Th36; then
    |- f^<*'not' ('not' p '&' 'not' q)*>^<*r*> by Th53;
    hence thesis by QC_LANG2:def 3;
  end;

theorem
  |- f^<*p*> implies |- f^<*p 'or' q*>
  proof
    set f1 = f^<*'not' p '&' 'not' q*>^<*'not' p '&' 'not' q*>;
    assume A1: |- f^<*p*>;
A2: Ant(f1) = f^<*'not' p '&' 'not' q*> &
    Suc(f1) = 'not' p '&' 'not' q by Th24; then
A3: (Ant(f1)).(len f+1) = 'not' p '&' 'not' q by FINSEQ_1:59;
    len f+1 = len f + len <*'not' p '&' 'not' q*> by FINSEQ_1:56; then
    len f+1 = len Ant(f1) by A2,FINSEQ_1:35; then
    len f+1 in dom Ant(f1) by A2,Th45; then
    Suc(f1) is_tail_of Ant(f1) by A2,A3,Def3; then
    |- f1 by Th29; then
    |- f^<*'not' p '&' 'not' q*>^<*'not' p*> by A2,Th37; then
    |- f^<*p*>^<*'not' ('not' p '&' 'not' q)*> by Th54; then
A4: |- f^<*p*>^<*p 'or' q*> by QC_LANG2:def 3;
    1 <= len (f^<*p*>) by Th45; then
    |- Ant(f^<*p*>)^<*p 'or' q*> by A1,A4,Th46;
    hence thesis by Th24;
  end;

theorem
  |- f^<*q*> implies |- f^<*p 'or' q*>
  proof
    set f1 = f^<*'not' p '&' 'not' q*>^<*'not' p '&' 'not' q*>;
    assume A1: |- f^<*q*>;
A2: Ant(f1) = f^<*'not' p '&' 'not' q*> &
    Suc(f1) = 'not' p '&' 'not' q by Th24; then
A3: (Ant(f1)).(len f+1) = 'not' p '&' 'not' q by FINSEQ_1:59;
    len f+1 = len f + len <*'not' p '&' 'not' q*> by FINSEQ_1:56; then
    len f+1 = len Ant(f1) by A2,FINSEQ_1:35; then
    len f+1 in dom Ant(f1) by A2,Th45; then
    Suc(f1) is_tail_of Ant(f1) by A2,A3,Def3; then
    |- f1 by Th29; then
    |- f^<*'not' p '&' 'not' q*>^<*'not' q*> by A2,Th38; then
    |- f^<*q*>^<*'not' ('not' p '&' 'not' q)*> by Th54; then
A4: |- f^<*q*>^<*p 'or' q*> by QC_LANG2:def 3;
    1 <= len (f^<*q*>) by Th45; then
    |- Ant(f^<*q*>)^<*p 'or' q*> by A1,A4,Th46;
    hence thesis by Th24;
  end;

theorem Th58:
  |- f^<*p*>^<*r*> & |- f^<*q*>^<*r*> implies |- f^<*p 'or' q*>^<*r*>
  proof
    assume |- f^<*p*>^<*r*> & |- f^<*q*>^<*r*>; then
A1: |- f^<*'not' r*>^<*'not' p*> & |- f^<*'not' r*>^<*'not' q*> by Th51;
    set f1 = f^<*'not' r*>^<*'not' p*>;
    set f2 = f^<*'not' r*>^<*'not' q*>;
A2: Ant(f1) = f^<*'not' r*> & Suc(f1) = 'not' p &
    Ant(f2) = f^<*'not' r*> & Suc(f2) = 'not' q by Th24; then
    |- Ant(f1)^<*'not' p '&' 'not' q*> by A1,Th36; then
    |- f^<*'not' ('not' p '&' 'not' q)*>^<*r*> by A2,Th53;
    hence thesis by QC_LANG2:def 3;
  end;

theorem Th59:
  |- f^<*p*> implies |- f^<*'not' 'not' p*>
  proof
    assume A1: |- f^<*p*>;
    set f1 = f^<*p*>^<*'not' p*>^<*p*>;
A2: Ant(f1) = f^<*p*>^<*'not' p*> & Suc(f1) = p by Th24; then
    Ant(f1) = f^(<*p*>^<*'not' p*>) by FINSEQ_1:45; then
A3: Ant(f1) = f^<*p,'not' p *> by FINSEQ_1:def 9; then
A4: (Ant(f1)).(len f+1) = p by Th50;
    len f+2 = len f + len <*p,'not' p *> by FINSEQ_1:61; then
    len f+2 = len Ant(f1) by A3,FINSEQ_1:35; then
    1 <= len f+1 & len f+1 <= len Ant(f1) by NAT_1:29,AXIOMS:24; then
    len f+1 in dom Ant(f1) by FINSEQ_3:27; then
    Suc(f1) is_tail_of Ant(f1) by A2,A4,Def3; then
A6: |- f1 by Th29;
    set f2 = f^<*p*>^<*'not' p*>^<*'not' p*>;
A7: Ant(f2) = f^<*p*>^<*'not' p*> & Suc(f2) =  'not' p by Th24; then
    Ant(f2) = f^(<*p*>^<*'not' p*>) by FINSEQ_1:45; then
A8: Ant(f2) = f^<*p,'not' p *> by FINSEQ_1:def 9; then
A9: (Ant(f2)).(len f+2) = 'not' p by Th50;
    len f+2 = len f + len <*p,'not' p *> by FINSEQ_1:61; then
    len f+2 = len Ant(f2) by A8,FINSEQ_1:35; then
    len f+2 in dom Ant(f2) by A7,Th45; then
    Suc(f2) is_tail_of Ant(f2) by A7,A9,Def3; then
A10: |- f2 by Th29;
    0+1 <= len f2 by Th45; then
    0 < len f2; then
A11: f2 = Ant(f2)^<*Suc(f2)*> by Th7;
    Ant(f1) = Ant(f2) & 'not' Suc(f1) = Suc(f2) by A7,Th24; then
A12: |- Ant(f1)^<*'not' 'not' p*> by A6,A10,A11,Th44;
    set f3 = Ant(f1)^<*'not' 'not' p*>;
    set f4 = f^<*p*>^<*'not' 'not' p*>^<*'not' 'not' p*>;
A13: Ant(f4) = f^<*p*>^<*'not' 'not' p*> &
                                         Suc(f4) = 'not' 'not' p by Th24; then
     Ant(f4) = f^(<*p*>^<*'not' 'not' p*>) by FINSEQ_1:45; then
A14: Ant(f4) = f^<*p,'not' 'not' p *> by FINSEQ_1:def 9; then
A15: (Ant(f4)).(len f+2) = 'not' 'not' p by Th50;
    len f+2 = len f + len <*p,'not' 'not' p *> by FINSEQ_1:61; then
    len f+2 = len Ant(f4) by A14,FINSEQ_1:35; then
    len f+2 in dom Ant(f4) by A13,Th45; then
    Suc(f4) is_tail_of Ant(f4) by A13,A15,Def3; then
A16: |- f4 by Th29;
A17: Ant(f3) = Ant(f1) & Suc(f3) = Suc(f4) by A13,Th24; then
A18: Ant(Ant(f3)) = f^<*p*> & Suc(Ant(f3)) = 'not' p by A2,Th24;
    then A19: Ant(Ant(f3)) = Ant(Ant(f4)) & 'not' Suc(Ant(f3)) = Suc(Ant(f4))
                                                                   by A13,Th24;
    1 < len f3 & 1 < len f4 by A2,Th43; then
A20: |- f^<*p*>^<*'not' 'not' p*> by A12,A13,A16,A17,A18,A19,Th34;
    set f5 =  f^<*p*>;
    1 <= len f5 by Th45; then
    |- Ant(f5)^<*'not' 'not' p*> by A1,A20,Th46;
    hence thesis by Th24;
  end;

theorem Th60:
  |- f^<*'not' 'not' p*> implies |- f^<*p*>
  proof
    assume A1: |- f^<*'not' 'not' p*>;
    set f1 = f^<*'not' 'not' p*>^<*'not' p*>^<*'not' p*>;
A2: Ant(f1) = f^<*'not' 'not' p*>^<*'not' p*> & Suc(f1) = 'not' p by Th24; then
    Ant(f1) = f^(<*'not' 'not' p*>^<*'not' p*>) by FINSEQ_1:45; then
A3: Ant(f1) = f^<*'not' 'not' p,'not' p *> by FINSEQ_1:def 9; then
A4: (Ant(f1)).(len f+2) = 'not' p by Th50;
    len f+2 = len f + len <*'not' 'not' p,'not' p *> by FINSEQ_1:61; then
    len f+2 = len Ant(f1) by A3,FINSEQ_1:35; then
    len f+2 in dom Ant(f1) by A2,Th45; then
    Suc(f1) is_tail_of Ant(f1) by A2,A4,Def3; then
A5: |- f1 by Th29;
    set f2 = f^<*'not' 'not' p*>^<*'not' p*>^<*'not' 'not' p*>;
A6: Ant(f2) = f^<*'not' 'not' p*>^<*'not' p*> & Suc(f2) = 'not' 'not' p
                                                                  by Th24; then
    Ant(f2) = f^(<*'not' 'not' p*>^<*'not' p*>) by FINSEQ_1:45; then
A7: Ant(f2) = f^<*'not' 'not' p,'not' p *> by FINSEQ_1:def 9; then
A8: (Ant(f2)).(len f+1) = 'not' 'not' p by Th50;
    len f+2 = len f + len <*'not' 'not' p,'not' p *> by FINSEQ_1:61; then
    len f+2 = len Ant(f2) by A7,FINSEQ_1:35; then
    1 <= len f+1 & len f+1 <= len Ant(f2) by NAT_1:29,AXIOMS:24; then
    len f+1 in dom Ant(f2) by FINSEQ_3:27; then
    Suc(f2) is_tail_of Ant(f2) by A6,A8,Def3; then
A10: |- f2 by Th29;
    0+1 <= len f2 by Th45; then
    0 < len f2; then
A11: f2 = Ant(f2)^<*Suc(f2)*> by Th7;
    Ant(f1) = Ant(f2) & 'not' Suc(f1) = Suc(f2) by A6,Th24; then
A12: |- Ant(f1)^<*p*> by A5,A10,A11,Th44;
    set f3 = Ant(f1)^<*p*>;
    set f4 = f^<*'not' 'not' p*>^<*p*>^<*p*>;
A13: Ant(f4) = f^<*'not' 'not' p*>^<*p*> & Suc(f4) = p by Th24; then
     Ant(f4) = f^(<*'not' 'not' p*>^<*p*>) by FINSEQ_1:45; then
A14: Ant(f4) = f^<*'not' 'not' p,p *> by FINSEQ_1:def 9; then
A15: (Ant(f4)).(len f+2) = p by Th50;
    len f+2 = len f + len <*'not' 'not' p,p *> by FINSEQ_1:61; then
    len f+2 = len Ant(f4) by A14,FINSEQ_1:35; then
    len f+2 in dom Ant(f4) by A13,Th45; then
    Suc(f4) is_tail_of Ant(f4) by A13,A15,Def3; then
A16: |- f4 by Th29;
A17: Ant(f3) = Ant(f1) & Suc(f3) = Suc(f4) by A13,Th24; then
A18: Ant(Ant(f3)) = f^<*'not' 'not' p*> & Suc(Ant(f3)) = 'not' p by A2,Th24;
    then A19: Ant(Ant(f3)) = Ant(Ant(f4)) & Suc(Ant(f3)) = 'not' Suc(Ant(f4))
                                                                   by A13,Th24;
    1 < len f3 & 1 < len f4 by A2,Th43; then
A20: |- f^<*'not' 'not' p*>^<*p*> by A12,A13,A16,A17,A18,A19,Th34;
    set f5 =  f^<*'not' 'not' p*>;
    1 <= len f5 by Th45; then
    |- Ant(f5)^<*p*> by A1,A20,Th46;
    hence thesis by Th24;
  end;

theorem
  |- f^<*p => q*> & |- f^<*p*> implies |- f^<*q*>
  proof
    assume A1: |- f^<*p => q*> & |- f^<*p*>;
    set f1 = f^<*'not' p*>^<*p*>;
A2: Ant(f1) = f^<*'not' p*> & Ant(f^<*p*>) = f & Suc(f^<*p*>) = p by Th24; then
    Ant(f^<*p*>) is_Subsequence_of Ant(f1) & Suc(f^<*p*>) = Suc(f1)
                                                           by Th24,Th42; then
A3: |- f1 by A1,Th33;
    set f2 = f^<*'not' p*>^<*'not' p*>;
A4: Ant(f2) = f^<*'not' p*> & Suc(f2) = 'not' p by Th24; then
A5: (Ant(f2)).(len f+1) = 'not' p by FINSEQ_1:59;
    len f+1 = len f + len <*'not' p *> by FINSEQ_1:56; then
    len f+1 = len Ant(f2) by A4,FINSEQ_1:35; then
    len f+1 in dom Ant(f2) by A4,Th45; then
    Suc(f2) is_tail_of Ant(f2) by A4,A5,Def3; then
A6: |- f2 by Th29;
    0+1 <= len f2 by Th45; then
A7: 0 < len f2;
    Ant(f1) = Ant(f2) & 'not' Suc(f1) = Suc(f2) by A4,Th24; then
    |- Ant(f1)^<*'not' Suc(f1)*> by A6,A7,Th7; then
A8: |- Ant(f1)^<*q*> by A3,Th44;
    set f3 = f^<*q*>^<*q*>;
A9: Ant(f3) =  f^<*q*> & Suc(f3) = q by Th24; then
A10: (Ant(f3)).(len f+1) = q by FINSEQ_1:59;
    len f+1 = len f + len <*q*> by FINSEQ_1:56; then
    len f+1 = len Ant(f3) by A9,FINSEQ_1:35; then
    len f+1 in dom Ant(f3) by A9,Th45; then
    Suc(f3) is_tail_of Ant(f3) by A9,A10,Def3; then
     |- f3 by Th29; then 
     |- f^<*'not' p 'or' q*>^<*q*> by A2,A8,Th58; then
     |- f^<*'not' ('not' 'not' p '&' 'not' q)*>^<*q*> by QC_LANG2:def 3; then
A11: |- f^<*'not' q*>^<*'not' 'not' p '&' 'not' q*> by Th53;
     set f4 = f^<*'not' q*>^<*'not' 'not' p '&' 'not' q*>;
     Suc(f4) = 'not' 'not' p '&' 'not' q by Th24; then
A12: |- Ant(f4)^<*'not' 'not' p*> & 
     |- Ant(f4)^<*'not' q*> by A11,Th37,Th38; then
A13: |- Ant(f4)^<*p*> by Th60; 
     set f5 = Ant(f4)^<*p*>;
     set f6 = Ant(f4)^<*'not' q*>;
     Ant(f5) = Ant(f4) & Suc(f5) = p & Ant(f6) = Ant(f4) & 
     Suc(f6) = 'not' q by Th24; then
     |- Ant(f4)^<*p '&' 'not' q*> by A12,A13,Th36; then
     |- f^<*'not' q*>^<*p '&' 'not' q*> by Th24; then
     |- f^<*'not' (p '&' 'not' q)*>^<*q*> by Th53; then
A14: |- f^<*p => q*>^<*q*> by QC_LANG2:def 2;
     1 <= len (f^<*p => q*>) by Th45; then
     |- Ant(f^<*p => q*>)^<*q*> by A1,A14,Th46; 
     hence thesis by Th24;
  end; 

theorem Th62:
  ('not' p).(x,y) = 'not' (p.(x,y))
  proof
A1: ('not' p).(x,y) = CQC_Sub(['not' p,Sbst(x,y)]) by SUBSTUT2:def 2;
    set S = [p,Sbst(x,y)];
    S`1 = p & S`2 = Sbst(x,y) by MCART_1:7; then
    Sub_not S = ['not' p,Sbst(x,y)] by SUBSTUT1:def 20; then
    ('not' p).(x,y) = 'not' CQC_Sub(S) by A1,SUBSTUT1:29;
    hence thesis by SUBSTUT2:def 2;
  end;

theorem
  (ex y st |- f^<*p.(x,y)*>) implies |- f^<*Ex(x,p)*>
  proof
    given y such that A1: |- f^<*p.(x,y)*>;
    set f1 = f^<*All(x,'not' p)*>^<*All(x,'not' p)*>;
A2: Ant(f1) = f^<*All(x,'not' p)*> & Suc(f1) = All(x,'not' p) by Th24; then
A3: (Ant(f1)).(len f+1) = All(x,'not' p) by FINSEQ_1:59;
    len f+1 = len f + len <*All(x,'not' p)*> by FINSEQ_1:56; then
    len f+1 = len Ant(f1) by A2,FINSEQ_1:35; then
    len f+1 in dom Ant(f1) by A2,Th45; then
    Suc(f1) is_tail_of Ant(f1) by A2,A3,Def3; then
    |- f1 by Th29; then
    |- f^<*All(x,'not' p)*>^<*('not' p).(x,y)*> by A2,Th39; then
    |- f^<*All(x,'not' p)*>^<*'not' (p.(x,y))*> by Th62; then
    |- f^<*p.(x,y)*>^<*'not' All(x,'not' p)*> by Th54; then
A5: |- f^<*p.(x,y)*>^<*Ex(x,p)*> by QC_LANG2:def 5;
    1 <= len (f^<*p.(x,y)*>) by Th45; then
    |- Ant(f^<*p.(x,y)*>)^<*Ex(x,p)*> by A1,A5,Th46;
    hence thesis by Th24;
  end;

theorem Th64:
  still_not-bound_in (f^g) = still_not-bound_in f \/ still_not-bound_in g
  proof
    thus still_not-bound_in (f^g) c= 
                               still_not-bound_in f \/ still_not-bound_in g
    proof
      let b such that A1: b in still_not-bound_in (f^g);
      consider i,p such that 
A2:        i in dom (f^g) & p = (f^g).i & b in still_not-bound_in p by A1,Def4;
A3:   now assume A4: i in dom f; then
A5:     (f^g).i = f.i by FINSEQ_1:def 7;
A6:     b in still_not-bound_in f by A2,A4,A5,Def4;
        still_not-bound_in f c= still_not-bound_in f \/ still_not-bound_in g
                                                       by XBOOLE_1:7;
        hence thesis by A6;
      end;
      now given n such that A7: n in dom g & i = len f + n;
A8:     (f^g).i = g.n by A7,FINSEQ_1:def 7;           
A9:     b in still_not-bound_in g by A2,A7,A8,Def4;
        still_not-bound_in g c= still_not-bound_in f \/ still_not-bound_in g
                                                                 by XBOOLE_1:7;
        hence thesis by A9;   
      end; 
      hence thesis by A2,A3,FINSEQ_1:38;     
    end;
    thus still_not-bound_in f \/ still_not-bound_in g c= 
                                            still_not-bound_in (f^g)
    proof
      let b such that A1: b in still_not-bound_in f \/ still_not-bound_in g;
A3:   now assume b in still_not-bound_in f; then
        consider i,p such that 
A4:       i in dom f & p = f.i & b in still_not-bound_in p by Def4; 
A5:       dom f c= dom (f^g) by FINSEQ_1:39;
          p = (f^g).i by A4,FINSEQ_1:def 7;
          hence thesis by A4,A5,Def4;        
      end;
      now assume b in still_not-bound_in g; then
         consider i,p such that 
A6:       i in dom g & p = g.i & b in still_not-bound_in p by Def4; 
A7:      len f + i in dom (f^g) by A6,FINSEQ_1:41;
         p = (f^g).(len f + i) by A6,FINSEQ_1:def 7;
         hence thesis by A6,A7,Def4;
      end;
      hence thesis by A1,A3,XBOOLE_0:def 2;
    end;
  end;

theorem Th65:
  still_not-bound_in <*p*> = still_not-bound_in p
  proof
A1: now let b such that A2: b in still_not-bound_in <*p*>;
      consider i,q such that 
A3:       i in dom <*p*> & q = <*p*>.i & b in still_not-bound_in q by A2,Def4; 
      i in Seg 1 by A3,FINSEQ_1:55; then
      i = 1 by FINSEQ_1:4,TARSKI:def 1;
      hence b in still_not-bound_in p by A3,FINSEQ_1:57;
    end;
    now let b such that A4: b in still_not-bound_in p;
      1 in Seg 1 by FINSEQ_1:4,TARSKI:def 1; then
A5:   1 in dom <*p*> by FINSEQ_1:55;
      p = <*p*>.1 by FINSEQ_1:57;
      hence b in still_not-bound_in <*p*> by A4,A5,Def4;
    end;
    hence thesis by A1,TARSKI:2;
  end;
      
theorem
  |- f^<*p.(x,y)*>^<*q*> &
         not y in still_not-bound_in (f^<*Ex(x,p)*>^<*q*>) implies
           |- f^<*Ex(x,p)*>^<*q*>
  proof
    assume A1: |- f^<*p.(x,y)*>^<*q*> &
               not y in still_not-bound_in (f^<*Ex(x,p)*>^<*q*>); then
    |- f^<*'not' q*>^<*'not' (p.(x,y))*> by Th51; then
A2: |- f^<*'not' q*>^<*('not' p).(x,y)*> by Th62;
    set f1 = f^<*'not' q*>^<*('not' p).(x,y)*>;
    not y in still_not-bound_in (f^<*Ex(x,p)*>) \/ still_not-bound_in <*q*>
                                                              by A1,Th64; then
A3: not y in still_not-bound_in (f^<*Ex(x,p)*>) &
    not y in still_not-bound_in <*q*> by XBOOLE_0:def 2; then
    not y in still_not-bound_in q by Th65; then
A4: not y in still_not-bound_in f \/ still_not-bound_in <*Ex(x,p)*> &
    not y in still_not-bound_in 'not' q by A3,Th64,QC_LANG3:11; then
A5: not y in still_not-bound_in f &
    not y in still_not-bound_in <*Ex(x,p)*> by XBOOLE_0:def 2;
    not y in still_not-bound_in <*'not' q*> by A4,Th65; then
    not y in still_not-bound_in f \/ 
                  still_not-bound_in <*'not' q*> by A5,XBOOLE_0:def 2; then
    not y in still_not-bound_in (f^<*'not' q*>) by Th64; then
A6: not y in still_not-bound_in Ant(f1) & Suc(f1) = ('not' p).(x,y) by Th24;
    not y in still_not-bound_in Ex(x,p) by A5,Th65; then
    not y in still_not-bound_in p \ {x} by QC_LANG3:23; then
    not y in still_not-bound_in 'not' p \ {x} by QC_LANG3:11; then
    not y in still_not-bound_in All(x,'not' p) by QC_LANG3:16; then
    |- Ant(f1)^<*All(x,'not' p)*> by A2,A6,Th40; then
    |- f^<*'not' q*>^<*All(x,'not' p)*> by Th24; then
    |- f^<*'not' All(x,'not' p)*>^<*q*> by Th53;
    hence thesis by QC_LANG2:def 5;
  end;

theorem Th67:
  still_not-bound_in f =
    union {still_not-bound_in p : ex i st i in dom f & p = f.i}
  proof
    defpred P[set] means ex p st $1 = still_not-bound_in p & 
                                ex i st i in dom f & p = f.i;
    set X = {still_not-bound_in p : ex i st i in dom f & p  = f.i};
A1: now let b such that A2: b in still_not-bound_in f;
      consider i,p such that A3: i in dom f & p = f.i &
                                       b in still_not-bound_in p by A2,Def4;
      still_not-bound_in p in X by A3;
      hence b in union X by A3,TARSKI:def 4;
    end;
    now let b such that A4: b in union X;
      consider Y being set such that A5: b in Y & Y in X by A4,TARSKI:def 4;
      P[Y] by A5;
      hence b in still_not-bound_in f by A5,Def4;
    end;
    hence thesis by A1,TARSKI:2;
  end;

theorem Th68:
  still_not-bound_in f is finite
  proof
    defpred P[set,set] means ex p st $2 = still_not-bound_in p & p = f.$1;
    set X = {still_not-bound_in p : ex i st i in dom f & p  = f.i};
A1: for a,b1,b2 being set st a in dom f & P[a,b1] & P[a,b2] holds b1 = b2; 
A2: for a being set st a in dom f ex b st P[a,b]
    proof
      let a be set such that A1: a in dom f;
      f.a in rng f by A1,FUNCT_1:12; then
      reconsider p = f.a as Element of CQC-WFF;
      take b = still_not-bound_in p;
      thus thesis;
    end;
    consider F2 being Function such that A4: dom F2 = dom f & 
    for b st b in dom f holds P[b,F2.b] from FUNCT_1:sch 2(A1,A2);
    consider n such that A5: dom f = Seg n by FINSEQ_1:def 2;
    consider F1 being Function such that 
A6:             rng F1 = Seg n & dom F1 in omega by FINSET_1:def 1;
    set F = F1*F2;
A8: dom F in omega by A4,A5,A6,RELAT_1:46; 
    set X = {still_not-bound_in p : ex i st i in dom f & p = f.i};  
A9: now let b such that A10: b in rng F;
      b in rng F2 by A4,A5,A6,A10,RELAT_1:47; then
      consider a being set such that
A11:  a in dom F2 & b = F2.a by FUNCT_1:def 5;
      reconsider a as Nat by A4,A11;
      a in dom f & P[a,F2.a] by A4,A11;
      hence b in X by A11;
    end;   
    now let b such that A12: b in X;    
      consider p such that 
A13:  b = still_not-bound_in p & ex i st i in dom f & p = f.i by A12;
      consider i such that
A14:  i in dom f & p = f.i by A13;
      P[i,F2.i] by A4,A14; then
      b in rng F2 by A4,A13,A14,FUNCT_1:12;
      hence b in rng F by A4,A5,A6,RELAT_1:47;
    end; then
    rng F = X by A9,TARSKI:2; then
A15: X is finite by A8,FINSET_1:def 1;
    now let b such that A16: b in X;
        consider p such that 
A17:    b = still_not-bound_in p & ex i st i in dom f & p = f.i by A16;
      thus b is finite by A17,CQC_SIM1:20;
    end; then
    union X is finite by A15,FINSET_1:25;
    hence thesis by Th67;
  end;

theorem Th69:
  Card bound_QC-variables = alef 0 & not bound_QC-variables is finite
  proof
    Card bound_QC-variables  = Card [:NAT,{4}:] by CARD_2:11,QC_LANG1:def 2;
    then Card bound_QC-variables = alef 0 by CARD_1:38,CARD_4:15,81;
    hence thesis by CARD_4:1,CARD_4:16;
  end;

theorem Th70:
  ex x st not x in still_not-bound_in f
  proof
    still_not-bound_in f c= bound_QC-variables &
    still_not-bound_in f <> bound_QC-variables by Th68,Th69;
    then A2: not ( for b holds b in still_not-bound_in f iff
                               b in bound_QC-variables) by TARSKI:2;
    consider b such that A3: not b in still_not-bound_in f & 
                                                 b in bound_QC-variables by A2;
    reconsider x = b as bound_QC-variable by A3;
    take x;
    thus thesis by A3; 
  end;

theorem Th71:
  |- f^<*All(x,p)*> implies |- f^<*All(x,'not' 'not' p)*>
  proof
    assume A1: |- f^<*All(x,p)*>;
A2: Ant(f^<*All(x,p)*>) = f & Suc(f^<*All(x,p)*>) = All(x,p) by Th24;
    consider y0 such that 
A3:               not y0 in still_not-bound_in (f^<*All(x,p)*>) by Th70;
    |- f^<*p.(x,y0)*> by A1,A2,Th39; then
    |- f^<*'not' 'not' (p.(x,y0))*> by Th59; then
    |- f^<*'not' (('not' p).(x,y0))*> by Th62; then
A4: |- f^<*('not' 'not' p).(x,y0)*> by Th62;
    set f1 = f^<*('not' 'not' p).(x,y0)*>;
    not y0 in still_not-bound_in f \/ still_not-bound_in <*All(x,p)*> 
                                                                    by A3,Th64;
    then not y0 in still_not-bound_in f & 
         not y0 in still_not-bound_in <*All(x,p)*> by XBOOLE_0:def 2; then
A5: not y0 in still_not-bound_in Ant(f1) & 
    Suc(f1) = ('not' 'not' p).(x,y0) &
    not y0 in still_not-bound_in All(x,p) by Th24,Th65; then
    not y0 in still_not-bound_in p \ {x} by QC_LANG3:16; then
    not y0 in still_not-bound_in 'not' p \ {x} by QC_LANG3:11; then
    not y0 in still_not-bound_in 'not' 'not' p \ {x} by QC_LANG3:11; then
    not y0 in still_not-bound_in All(x,'not' 'not' p) by QC_LANG3:16; then
    |- Ant(f1)^<* All(x,'not' 'not' p)*> by A4,A5,Th40;
    hence thesis by Th24;
  end;

theorem Th72:
  |- f^<*All(x,'not' 'not' p)*> implies |- f^<*All(x,p)*>
  proof
    assume A1: |- f^<*All(x,'not' 'not' p)*>;
A2: Ant(f^<*All(x,'not' 'not' p)*>) = f & 
    Suc(f^<*All(x,'not' 'not' p)*>) = All(x,'not' 'not' p) by Th24;
    consider y0 such that 
A3: not y0 in still_not-bound_in (f^<*All(x,p)*>) by Th70;
    |- f^<*('not' 'not' p).(x,y0)*> by A1,A2,Th39; then
    |- f^<*'not' (('not' p).(x,y0))*> by Th62; then
    |- f^<*'not' 'not' (p.(x,y0))*> by Th62; then
A4: |- f^<*p.(x,y0)*> by Th60;
    set f1 = f^<*p.(x,y0)*>;
    not y0 in still_not-bound_in f \/ still_not-bound_in <*All(x,p)*> 
                                                                    by A3,Th64;
    then not y0 in still_not-bound_in f & 
         not y0 in still_not-bound_in <*All(x,p)*> by XBOOLE_0:def 2; then
A5: not y0 in still_not-bound_in Ant(f1) & 
    Suc(f1) = p.(x,y0) & 
    not y0 in still_not-bound_in All(x,p) by Th24,Th65;
    |- Ant(f1)^<* All(x,p)*> by A4,A5,Th40;
    hence thesis by Th24;
  end;

theorem
  |- f^<*All(x,p)*> iff |- f^<*'not' Ex(x,'not' p)*>
  proof
    thus |- f^<*All(x,p)*> implies |- f^<*'not' Ex(x,'not' p)*>
    proof
      assume |- f^<*All(x,p)*>; then
      |- f^<*All(x,'not' 'not' p)*> by Th71; then
      |- f^<*'not' 'not' All(x,'not' 'not' p)*> by Th59;
      hence thesis by QC_LANG2:def 5;
    end;
    assume |- f^<*'not' Ex(x,'not' p)*>; then
    |- f^<*'not' 'not' All(x,'not' 'not' p)*> by QC_LANG2:def 5; then
    |- f^<*All(x,'not' 'not' p)*> by Th60;
    hence thesis by Th72;
  end;
