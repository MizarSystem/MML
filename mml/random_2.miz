:: Probability Measure on Discrete Spaces and Algebra of Real Valued Random
:: Variables
::  by Hiroyuki Okazaki and Yasunari Shidama
::
:: Received March 16, 2010
:: Copyright (c) 2010 Association of Mizar Users
 
environ

 vocabularies FUNCT_1, COMPLEX1, RELAT_1, ZFMISC_1, XXREAL_0, PARTFUN1,
      ARYTM_1, XBOOLE_0, ORDINAL4, INT_1, SQUARE_1, ARYTM_3, RLVECT_1,
      SUPINF_2, RSSPACE, FUNCOP_1, FINSOP_1, NAT_1, FUNCT_4, BINOP_2, PROB_1,
      MEASURE6, MESFUNC1, TARSKI, MESFUNC5, IDEAL_1, SUBSET_1, RANDOM_1,
      RANDOM_2, C0SP1, FINSET_1, FINSEQ_1, ORDINAL2, RPR_1, CARD_1, SEQ_2,
      POWER, FUNCT_3, PROB_4, UPROOTS, FINSEQ_2, CARD_3, FUNCT_2, RFINSEQ,
      XREAL_0, FUNCSDOM, POLYALG1, BHSP_5, NUMBERS, REAL_1, INTEGRA5, VALUED_1,
      ORDINAL1, EQREL_1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, RELAT_1, FUNCT_1, RELSET_1,
      PARTFUN1, FUNCT_2, FUNCT_4, FUNCOP_1, FINSEQ_1, ORDINAL1, REAL_1, NAT_1,
      CARD_1, FINSET_1, NUMBERS, XREAL_0, XXREAL_0, NAT_D, COMPLEX1, VALUED_1,
      RFINSEQ, FINSOP_1, STRUCT_0, ALGSTR_0, IDEAL_1, VECTSP_1, BINOP_1,
      BINOP_2, RLVECT_1, FUNCSDOM, SUPINF_2, SEQ_1, SEQ_2, SQUARE_1, GROUP_1,
      POWER, PROB_1, PROB_4, EXTREAL1, MESFUNC1, MEASURE6, MESFUNC5, MESFUNC6,
      MESFUNC2, RVSUM_1, UPROOTS, MESFUN6C, C0SP1, RANDOM_1, BHSP_5;
 constructors EXTREAL1, REAL_1, IDEAL_1, MEASURE6, MESFUNC2, MESFUNC5, SEQ_2,
      MESFUNC6, SEQ_1, SUPINF_1, REALSET1, RVSUM_1, MESFUNC1, RELSET_1,
      RANDOM_1, C0SP1, RFINSEQ, POWER, FINSOP_1, BINARITH, FINSEQOP, SEQ_4,
      CONVFUN1, SEQFUNC, SETWISEO, FINSEQ_4, PARTFUN3, PROB_4, WELLORD2,
      SQUARE_1, NAT_D, UPROOTS, MESFUN6C, BHSP_5;
 registrations XBOOLE_0, ORDINAL1, NUMBERS, XREAL_0, MEMBERED, VALUED_0, NAT_1,
      SUBSET_1, XCMPLX_0, RLVECT_1, XXREAL_0, MEASURE1, FINSEQ_1, RELAT_1,
      XXREAL_3, C0SP1, FUNCT_2, CARD_1, FINSET_1, BINOP_2, FUNCT_1, UPROOTS,
      MESFUN6C, FUNCSDOM, BHSP_5;
 requirements NUMERALS, BOOLE, SUBSET, REAL, ARITHM;
 definitions TARSKI, BINOP_1, STRUCT_0, ALGSTR_0, RLVECT_1, MEASURE6, MESFUNC5,
      FINSEQ_2, FINSEQ_1, PROB_4, FUNCSDOM, VALUED_1, BHSP_5, RVSUM_1,
      SQUARE_1;
 theorems FUNCT_1, FUNCT_2, PARTFUN1, COMPLEX1, XBOOLE_0, TARSKI, RELAT_1,
      VALUED_1, IDEAL_1, ZFMISC_1, RLVECT_1, FUNCOP_1, XBOOLE_1, MESFUNC2,
      EXTREAL1, MEASURE1, MESFUNC6, PROB_1, XXREAL_0, ABSVALUE, MESFUNC7,
      RELSET_1, XXREAL_3, XREAL_0, RANDOM_1, C0SP1, FINSEQ_4, POWER, BINOP_2,
      RFINSEQ, CARD_2, FINSEQ_1, FUNCT_4, FINSOP_1, NAT_1, NAT_2, NAT_D, INT_3,
      XREAL_1, SQUARE_1, FINSEQ_2, INT_1, FUNCT_3, CARD_1, FINSEQ_3, FINSEQ_5,
      UPROOTS, MESFUN6C, FUNCSDOM, BHSP_5, RVSUM_1, FIB_NUM3, SUBSET;
 schemes FINSEQ_1, FUNCT_2, NAT_1, FUNCT_3;

begin

 reserve Omega for non empty set;
 reserve r for Real;
 reserve Sigma for SigmaField of Omega;
 reserve P for Probability of Sigma;

theorem TRNG01:
  for f be one-to-one Function, A,B be Subset of dom f st A misses B holds
  rng (f|A) misses rng (f|B)
  proof
    let f be one-to-one Function, A,B be Subset of dom f;
    assume ASC0:A misses B;
    assume rng (f|A) meets rng (f|B);then
    consider y be set such that
    ASC1: y in rng (f|A) & y in rng (f|B) by XBOOLE_0:3;
    consider xa be set such that
    ASC2: xa in dom (f|A) & y = (f|A).xa by ASC1,FUNCT_1:def 5;
    consider xb be set such that
    ASC3: xb in dom (f|B) & y = (f|B).xb by ASC1,FUNCT_1:def 5;
    ASC4:xa in dom f & xb in dom f by RELAT_1:86,ASC2,ASC3;
    y=f.xa &y=f.xb by ASC2,ASC3,FUNCT_1:70;then
    ASC5:xa=xb by FUNCT_1:def 8,ASC4;
    dom (f|A) c= A & dom (f|B) c=B by RELAT_1:87;
    then xa in A /\ B by XBOOLE_0:def 4,ASC5,ASC2,ASC3;
    hence contradiction by ASC0,XBOOLE_0:4;
  end;

theorem TRNG02:
  for f,g be Function holds rng (f*g) c= rng (f|(rng g))
  proof
    let f,g be Function;
    let y be set;
    assume y in rng(f*g);then
    consider x be set such that
C1: x in dom (f*g) & y=(f*g).x by FUNCT_1:def 5;
C2: x in dom g & g.x in dom f by FUNCT_1:21,C1;
    reconsider z=g.x as set;
    f.z in rng (f|(rng g)) by FUNCT_1:73,C2,FUNCT_1:12;
    hence thesis by FUNCT_1:22,C1;
  end;

registration let Omega,Sigma;
  cluster nonnegative Real-Valued-Random-Variable of Sigma;
  existence
  proof
    consider X be Real-Valued-Random-Variable of Sigma;
    now let x be set;
      assume x in dom abs X;
      then (abs X).x = abs(X.x) by VALUED_1:def 11;
      hence 0 <= (abs X).x by COMPLEX1:132;
    end; then
    abs X is nonnegative by MESFUNC6:52;
    hence thesis;
  end;
end;

registration let Omega,Sigma;
  let X be Real-Valued-Random-Variable of Sigma;
  cluster abs X -> nonnegative;
  coherence
  proof
    now let x be set;
      assume x in dom (abs X);
      then (abs X).x = abs(X.x) by VALUED_1:def 11;
      hence 0 <= (abs X).x by COMPLEX1:132;
    end;
    hence thesis by MESFUNC6:52;
  end;
end;

theorem CHILM0:
  (Omega --> 1) = chi(Omega, Omega)
  proof
    set E0 = (Omega --> 1);
    K210: dom (chi(Omega,Omega)) = Omega by FUNCT_3:def 3;
    K211: dom E0 = Omega by FUNCT_2:def 1;
    now let x be set;
      assume AS1:x in dom (chi(Omega,Omega));
      per cases;
      suppose x in Omega;
        hence (chi(Omega,Omega)).x = 1 by FUNCT_3:def 3
        .=E0.x by FUNCOP_1:13,AS1;
      end;
      suppose not x in Omega;
        hence (chi(Omega,Omega)).x=E0.x by AS1;
      end;
    end;
    hence thesis by K210,K211,FUNCT_1:9;
  end;

theorem CHILM1:
  (Omega --> r) is Real-Valued-Random-Variable of Sigma
  proof
    set E0 = (Omega --> 1);
    set E = (Omega --> r);
    reconsider S= Omega as Element of Sigma by MEASURE1:21;
    A100: dom E0 = Omega & rng E0 c= {1} by FUNCOP_1:19;
    reconsider E0 as Function of Omega, REAL by FUNCT_2:9;
    K21: R_EAL E0 =chi(S,Omega) by CHILM0;
    chi(S,Omega) is_measurable_on S by MESFUNC2:32;
    then E0 is_measurable_on S by MESFUNC6:def 6,K21; then
    K22: E0 is Real-Valued-Random-Variable of Sigma by RANDOM_1:def 2;
    K221: dom E = dom E0 by A100,FUNCT_2:def 1;
    now let x be set;
      assume
      AS1:x in dom E;
      hence E.x = r*1 by FUNCOP_1:13
      .= r*(E0.x) by AS1,FUNCOP_1:13;
    end; then
    E = r(#) E0 by K221,VALUED_1:def 5;
    hence thesis by K22,RANDOM_1:20;
  end;

theorem THSQE:
  for X be non empty set, f be PartFunc of X,REAL holds
  f to_power 2 = (-f) to_power 2 &
  f to_power 2 = (abs(f)) to_power 2
  proof
    let X be non empty set, f be PartFunc of X,REAL;
    dom (-f) = dom f by VALUED_1:8;
    then dom((-f) to_power 2)= dom f by MESFUN6C:def 6;
    then LDOM3:dom(f to_power 2)= dom((-f) to_power 2) by MESFUN6C:def 6;
    dom abs f = dom f by VALUED_1:def 11;
    then LDOM6:dom((abs f) to_power 2)= dom f by MESFUN6C:def 6;
    then LDOM6P:
    dom(f to_power 2)= dom((abs(f)) to_power 2) by MESFUN6C:def 6;
    for x be Element of X st x in dom(f to_power 2)
    holds (f to_power 2).x=((-f) to_power 2).x&
    (f to_power 2).x =((abs(f)) to_power 2).x
    proof
      let x be Element of X;
      assume LDOMP: x in dom(f to_power 2);
      then LDOM: x in dom((-f) to_power 2) &
      x in dom(f to_power 2) & x in dom f & x in dom (-f) &
      x in dom((abs(f)) to_power 2) by LDOM6,MESFUN6C:def 6,LDOM3;
      C1: ((-f) to_power 2).x = (((-f).x) to_power 2)
      by MESFUN6C:def 6,LDOMP,LDOM3
      .= ((-(f.x)) to_power 2) by VALUED_1:8
      .= ((f.x) to_power 2) by FIB_NUM3:3
      .=(f to_power 2).x by MESFUN6C:def 6,LDOMP;
      ((abs(f)) to_power 2).x = (f to_power 2).x
      proof
        LABP: ((abs(f)) to_power 2).x = ((abs(f)).x) to_power 2
        by MESFUN6C:def 6,LDOM
        .= (abs(f.x)) to_power 2 by VALUED_1:18;
        now per cases by LABP,ABSVALUE:1;
          case ((abs(f)) to_power 2).x = (f.x) to_power 2;
            hence thesis by MESFUN6C:def 6,LDOMP;
          end;
          case ((abs(f)) to_power 2).x = (-(f.x)) to_power 2; then
            ((abs(f)) to_power 2).x =((f.x) to_power 2) by FIB_NUM3:3
            .=(f to_power 2).x by MESFUN6C:def 6,LDOMP;
            hence thesis;
          end;
        end;
        hence thesis;
      end;
      hence thesis by C1;
    end;then
    (for x be Element of X st x in dom(f to_power 2)
    holds (f to_power 2).x=((-f) to_power 2).x)&
    (for x be Element of X st x in dom(f to_power 2) holds
    (f to_power 2).x =((abs(f)) to_power 2).x);
    hence thesis by PARTFUN1:34,LDOM3,LDOM6P;
  end;

theorem THSQ:
  for X be non empty set, f,g be PartFunc of X,REAL holds
  (f+g) to_power 2 = (f to_power 2) + 2(#)(f(#)g) + (g to_power 2) &
  (f-g) to_power 2 = (f to_power 2) - 2(#)(f(#)g) + (g to_power 2)
  proof
    let X be non empty set, f,g be PartFunc of X,REAL;
    L1: dom ((f to_power 2)) = dom f
    & for x be Element of X st x in dom ((f to_power 2)) holds
    ((f to_power 2)).x = (f.x) to_power 2 by MESFUN6C:def 6;
    L2: dom (g to_power 2) = dom g
    & for x be Element of X st x in dom (g to_power 2) holds
    (g to_power 2).x = (g.x) to_power 2 by MESFUN6C:def 6;
    L3: dom (2(#)(f(#)g)) = dom (f(#)g) by VALUED_1:def 5
    .= dom f /\ dom g by VALUED_1:def 4;
    L4: now let x be Element of X;
    assume x in (dom f /\ dom g);
    thus (2(#)(f(#)g)).x = 2*((f(#)g).x) by VALUED_1:6
    .= 2*((f.x)*(g.x)) by VALUED_1:5
    .= 2*(f.x)*(g.x);
  end;
  L6: dom ((f+g) to_power 2) = dom (f+g) by MESFUN6C:def 6
  .= dom f /\ dom g by VALUED_1:def 1;
  L6M:dom ((f-g) to_power 2) = dom (f-g) by MESFUN6C:def 6
  .= dom f /\ dom g by VALUED_1:12;
  L70: dom ((f to_power 2) + 2(#)(f(#)g))
  = dom f /\ dom (2(#)(f(#)g)) by L1,VALUED_1:def 1
  .= dom f /\ dom g by XBOOLE_1:17, XBOOLE_1:28,L3;
  L70M: dom ((f to_power 2) - 2(#)(f(#)g))
  = dom f /\ dom (2(#)(f(#)g)) by L1,VALUED_1:12
  .= dom f /\ dom g by XBOOLE_1:17, XBOOLE_1:28,L3;
  L7: dom ((f to_power 2) + 2(#)(f(#)g) + (g to_power 2))
  = dom ((f to_power 2) + 2(#)(f(#)g)) /\ dom g by L2,VALUED_1:def 1
  .= (dom f) /\ dom ( 2(#)(f(#)g)) /\ dom g by L1,VALUED_1:def 1
  .= (dom f /\ dom g) /\ dom g by XBOOLE_1:17, XBOOLE_1:28,L3
  .= dom f /\ dom g by XBOOLE_1:17, XBOOLE_1:28;
  L7M: dom ((f to_power 2) - 2(#)(f(#)g) + (g to_power 2))
  = dom ((f to_power 2) - 2(#)(f(#)g)) /\ dom g by L2,VALUED_1:def 1
  .= dom f /\ dom g by XBOOLE_1:17, XBOOLE_1:28,L70M;
  now let x be Element of X;
    assume L81:x in dom f /\ dom g; then
    L82:x in dom (f+g) by VALUED_1:def 1;
    L83:x in dom f by L81,XBOOLE_0:def 4;
    L84:x in dom g by L81,XBOOLE_0:def 4;
    L82M: x in dom (f-g) by VALUED_1:12,L81;then
    L80M:x in dom ((f-g) to_power 2) by MESFUN6C:def 6;
    H1: ((f+g) to_power 2).x = ((f+g).x) to_power 2 by MESFUN6C:def 6,L81,L6
    .= (f.x + g.x) to_power 2 by L82,VALUED_1:def 1
    .= (f.x + g.x) ^2 by POWER:53
    .= (f.x)^2 + 2*(f.x)*(g.x) + (g.x)^2
    .= (f.x) to_power 2 + 2*(f.x)*(g.x) + (g.x)^2 by POWER:53
    .= (f.x)to_power 2 + 2*(f.x)*(g.x) + (g.x) to_power 2 by POWER:53
    .= (f to_power 2).x + 2*(f.x)*(g.x) + (g.x) to_power 2 by L1,L83
    .= (f to_power 2).x + 2*(f.x)*(g.x) + (g to_power 2).x by L2,L84
    .= (f to_power 2).x + (2(#)(f(#)g)).x + (g to_power 2).x by L4,L81
    .= ((f to_power 2)+ (2(#)(f(#)g))).x + (g to_power 2).x
     by L70,L81,VALUED_1:def 1
    .= ((f to_power 2)+ (2(#)(f(#)g)) + (g to_power 2)).x
     by L7,L81,VALUED_1:def 1;
    ((f-g) to_power 2).x = ((f-g).x) to_power 2 by MESFUN6C:def 6,L80M
    .= (f.x - g.x) to_power 2 by L82M,VALUED_1:13
    .= (f.x - g.x) ^2 by POWER:53
    .= (f.x)^2 - 2*(f.x)*(g.x) + (g.x)^2
    .= (f.x) to_power 2 - 2*(f.x)*(g.x) + (g.x)^2 by POWER:53
    .= (f.x)to_power 2 - 2*(f.x)*(g.x) + (g.x) to_power 2 by POWER:53
    .= (f to_power 2).x - 2*(f.x)*(g.x) + (g.x) to_power 2 by L1,L83
    .= (f to_power 2).x - 2*(f.x)*(g.x) + (g to_power 2).x by L2,L84
    .= (f to_power 2).x - (2(#)(f(#)g)).x + (g to_power 2).x by L4,L81
    .= ((f to_power 2) - (2(#)(f(#)g))).x + (g to_power 2).x
    by L70M,L81,VALUED_1:13
    .= ((f to_power 2) - (2(#)(f(#)g)) + (g to_power 2)).x
    by L7M,L81,VALUED_1:def 1;
    hence ((f+g) to_power 2).x
    = ((f to_power 2) + (2(#)(f(#)g)) + (g to_power 2)).x&
    ((f-g) to_power 2).x
    =((f to_power 2) - (2(#)(f(#)g)) + (g to_power 2)).x by H1;
  end; then
  (for x be Element of X st x in dom ((f+g) to_power 2) holds
  ((f+g) to_power 2).x
  = ((f to_power 2) + (2(#)(f(#)g)) + (g to_power 2)).x) &
  (for x be Element of X st x in dom ((f-g) to_power 2) holds
  ((f-g) to_power 2).x
  =((f to_power 2) - (2(#)(f(#)g)) + (g to_power 2)).x) by L6,L6M;
  hence thesis by PARTFUN1:34,L6,L7,L7M,L6M;
end;

definition let Omega,Sigma,P;
  let X be Real-Valued-Random-Variable of Sigma;
  assume AS: X is_integrable_on P
  & (abs(X) to_power 2) is_integrable_on P2M(P);
  func variance (X,P) -> Element of REAL means :defvar:
  ex Y be Real-Valued-Random-Variable of Sigma,
  E be Real-Valued-Random-Variable of Sigma
  st E=(Omega --> expect(X,P)) & Y = X-E &
  Y is_integrable_on P & abs(Y) to_power 2 is_integrable_on P2M(P) &
  it=Integral(P2M(P),abs(Y) to_power 2);
  correctness
  proof
    consider S be Element of Sigma such that
    A0: S=Omega & X is_measurable_on S by RANDOM_1:def 2;
    (P2M(P)).S <=1 by PROB_1:71; then
    A1: (P2M(P)).S < +infty by XXREAL_0:9,XXREAL_0:2;
    set r = expect(X,P);
    set E0= (Omega -->1);
    set E = (Omega --> expect(X,P));
    A100: dom E0 = Omega & rng E0 c= {1} by FUNCOP_1:19;
    reconsider E0 as
    Real-Valued-Random-Variable of Sigma by CHILM1;
    K21: R_EAL E0 =chi(S,Omega) by A0,CHILM0;
    K221: dom E = dom E0 by A100,FUNCT_2:def 1;
    now let x be set;
      assume AS1: x in dom E;
      hence E.x = r*1 by FUNCOP_1:13
      .= r*(E0.x) by AS1,FUNCOP_1:13;
    end; then
    K0: E = r(#) E0 by K221,VALUED_1:def 5;
    reconsider E as Real-Valued-Random-Variable of Sigma by CHILM1;
    set Y= X-E;
    reconsider Y as Real-Valued-Random-Variable of Sigma;
    chi(S,Omega) is_integrable_on (P2M(P)) by MESFUNC7:24,A1;
    then
    K2: E0 is_integrable_on P2M(P) by K21,MESFUNC6:def 9;
    then
    KK2: r(#) E0 is_integrable_on P2M(P) by MESFUNC6:102;
    KK5: (-1) is Real by XREAL_0:def 1;
    then
    KK3: (-1)(#)( r(#) E0) is_integrable_on P2M(P) by KK2,MESFUNC6:102;
    KK4: X is_integrable_on P2M(P) by AS,RANDOM_1:def 3;
    then Y is_integrable_on P2M(P) by K0,KK3,MESFUNC6:100;
    then
    KK: Y is_integrable_on P by RANDOM_1:def 3;
    (2*r)(#)X is_integrable_on P2M(P) by KK4,MESFUNC6:102;
    then
    (-1) (#)((2*r)(#)X) is_integrable_on P2M(P) by KK5,MESFUNC6:102;
    then
    K11: (abs(X) to_power 2) - (2*r)(#)X
    is_integrable_on P2M(P) by AS,MESFUNC6:100;
    DK0:
    (X to_power 2) - 2(#)((r(#)E0)(#)X) + ((r(#)E0) to_power 2)
    =(X to_power 2) -(2*r)(#)X + (r*r)(#)E0
    proof
      DK0L1:dom (X to_power 2) = dom X by MESFUN6C:def 6;
      DK0L2:dom (2(#)((r(#)E0)(#)X)) = dom ((r(#)E0)(#)X) by VALUED_1:def 5
      .= dom (r(#)E0) /\ dom X by VALUED_1:def 4
      .= Omega /\ dom X by A100,VALUED_1:def 5;
      DK0L22:
      dom ((r(#)E0) to_power 2) = dom (r(#)E0) by MESFUN6C:def 6
      .= Omega by A100,VALUED_1:def 5;
      DK0L3:dom ((r*r)(#)E0) = Omega by A100,VALUED_1:def 5;
      DK0L33:
      dom ((2*r)(#)X)= dom X by VALUED_1:def 5;
      DK0L4:
      dom ((X to_power 2) - (2(#)((r(#)E0)(#)X)))
      =dom X /\ (Omega /\ dom X) by DK0L1,DK0L2,VALUED_1:12
      .=(dom X /\ dom X) /\ Omega by XBOOLE_1:16
      .= dom X /\ Omega;
      DK0L44:
      dom ((X to_power 2) - ((2*r)(#)X))
      = dom (X to_power 2) /\ dom ((2*r)(#)X) by VALUED_1:12
      .=dom X by DK0L1,DK0L33;
      DK0L5:
      dom((X to_power 2) - 2(#)((r(#)E0)(#)X) + ((r(#)E0) to_power 2))
      =dom ((X to_power 2) - (2(#)((r(#)E0)(#)X)))
      /\ dom ((r(#)E0) to_power 2) by VALUED_1:def 1
      .=dom X /\( Omega /\ Omega) by XBOOLE_1:16,DK0L22,DK0L4
      .= dom X /\ Omega;
      then DK0L6:
      dom((X to_power 2) - 2(#)((r(#)E0)(#)X) + ((r(#)E0) to_power 2))
      =dom((X to_power 2) -(2*r)(#)X + (r*r)(#)E0)
      by DK0L3,DK0L44,VALUED_1:def 1;
      for x be Element of Omega st x in
      dom((X to_power 2) - 2(#)((r(#)E0)(#)X) + ((r(#)E0) to_power 2))
      holds
      ((X to_power 2) - 2(#)((r(#)E0)(#)X) + ((r(#)E0) to_power 2)).x
      =((X to_power 2) -(2*r)(#)X + (r*r)(#)E0).x
      proof
        let x be Element of Omega;
        assume CAS:
        x in dom((X to_power 2) - 2(#)((r(#)E0)(#)X) + ((r(#)E0) to_power 2));
        then CAS2: x in dom X & x in Omega by XBOOLE_0:def 4,DK0L5;
        DK:
        ((X to_power 2) - 2(#)((r(#)E0)(#)X) + ((r(#)E0) to_power 2)).x
        =((X to_power 2) - 2(#)((r(#)E0)(#)X)).x + ((r(#)E0) to_power 2).x
        by VALUED_1:def 1,CAS
        .=(X to_power 2).x -( 2(#)((r(#)E0)(#)X)).x + ((r(#)E0) to_power 2).x
        by VALUED_1:13,DK0L4,DK0L5,CAS
        .=(X to_power 2).x -2*(((r(#)E0)(#)X).x) + ((r(#)E0) to_power 2).x
        by VALUED_1:6
        .=(X to_power 2).x -2*(((r(#)E0).x)*(X.x)) + ((r(#)E0) to_power 2).x
        by VALUED_1:5
        .=(X to_power 2).x -2*((r*(E0.x))*(X.x)) + ((r(#)E0) to_power 2).x
        by VALUED_1:6
        .=(X to_power 2).x -2*((r*(1))*(X.x)) + ((r(#)E0) to_power 2).x
        by FUNCOP_1:13
        .=(X to_power 2).x -(2*r)*(X.x)+ (((r(#)E0).x) to_power 2)
        by MESFUN6C:def 6,DK0L22
        .=(X to_power 2).x -(2*r)*(X.x)+ ((r*(E0.x)) to_power 2)
        by VALUED_1:6
        .=(X to_power 2).x -(2*r)*(X.x)+ ((r*1) to_power 2) by FUNCOP_1:13
        .=(X to_power 2).x -(2*r)*(X.x)+ r^2 by POWER:53
        .=(X to_power 2).x -(2*r)*(X.x)+ r*r;
        ((X to_power 2) -(2*r)(#)X + (r*r)(#)E0).x
        =((X to_power 2) -(2*r)(#)X).x + ((r*r)(#)E0).x
        by VALUED_1:def 1,CAS,DK0L6
        .=(X to_power 2).x -((2*r)(#)X).x + ((r*r)(#)E0).x
        by DK0L44,CAS2,VALUED_1:13
        .=(X to_power 2).x -(2*r)*(X.x) + ((r*r)(#)E0).x by VALUED_1:6
        .=(X to_power 2).x -(2*r)*(X.x) + (r*r)*(E0.x) by VALUED_1:6
        .=(X to_power 2).x -(2*r)*(X.x) + (r*r)*(1) by FUNCOP_1:13
        .=(X to_power 2).x -(2*r)*(X.x) + r*r;
        hence thesis by DK;
      end;
      hence thesis by PARTFUN1:34,DK0L6;
    end;
    K5: (abs(Y) to_power 2) =((X- (r(#)E0)) to_power 2) by THSQE,K0
    .= (X to_power 2) - 2(#)((r(#)E0)(#)X)
    + ((r(#)E0) to_power 2) by THSQ
    .= (abs(X) to_power 2) -(2*r)(#)X + (r*r)(#)E0 by THSQE,DK0;
    K17: (r*r)(#)E0 is_integrable_on P2M(P) by K2,MESFUNC6:102; then
    (abs(Y) to_power 2) is_integrable_on P2M(P) by K11,K5,MESFUNC6:100; then
    -infty < Integral(P2M(P),abs(Y) to_power 2)
    & Integral(P2M(P),abs(Y) to_power 2) < +infty by MESFUNC6:90;
    then Integral(P2M(P),abs(Y) to_power 2) is Element of REAL by XXREAL_0:14;
    hence thesis by K17,K11,K5,MESFUNC6:100,KK;
  end;
end;

begin :: Chebyshev's inequality

theorem
  for Omega,Sigma,P,r for X be Real-Valued-Random-Variable of Sigma st
  0 < r & X is nonnegative & X is_integrable_on P &
  (abs(X) to_power 2) is_integrable_on P2M(P) holds
  P.({t where t is Element of Omega : r <= |. X.t - expect(X,P) .| })
  <= variance(X,P)/( r to_power 2)
  proof
    let Omega,Sigma,P,r;
    let X be Real-Valued-Random-Variable of Sigma;
    assume
    AS: 0 < r & X is nonnegative & X is_integrable_on P &
    (abs(X) to_power 2) is_integrable_on P2M(P);
CC1:{t where t is Element of Omega : r <= |. X.t - expect(X,P) .| } c=
    {t where t is Element of Omega :
    (r to_power 2) <= ( abs( X.t - expect(X,P))) to_power 2 }
    proof
      let s be set;
      assume s in {t where t is Element of Omega :
      r <= |. X.t - expect(X,P) .| };
      then AC1: ex ss be Element of Omega st
      s=ss & r <= |. X.ss - expect(X,P) .|;
      AC2: r^2 = (r to_power 2) & (abs( X.s - expect(X,P)))^2
      = ( abs( X.s - expect(X,P))) to_power 2 by POWER:53;
      r^2 <= (abs( X.s - expect(X,P)))^2 by SQUARE_1:77,AS,AC1;
      hence thesis by AC1,AC2;
    end;
    {t where t is Element of Omega :
    (r to_power 2) <= ( abs( X.t - expect(X,P))) to_power 2 } c=
    {t where t is Element of Omega : r <= |. X.t - expect(X,P) .| }
    proof
      let s be set;
      assume s in {t where t is Element of Omega :
      (r to_power 2) <= ( abs( X.t - expect(X,P) )) to_power 2 };
      then AC1: ex ss be Element of Omega st s=ss &
      (r to_power 2) <= ( abs( X.ss - expect(X,P) )) to_power 2;
      AC4: 0<= abs( X.s - expect(X,P) ) by COMPLEX1:132;
       r^2 = (r to_power 2) & (abs( X.s - expect(X,P) ))^2
      = ( abs( X.s - expect(X,P) )) to_power 2 by POWER:53;
      then r <= abs( X.s - expect(X,P) ) by SQUARE_1:117,AC4,AC1;
      hence thesis by AC1;
    end; then
    P1: {t where t is Element of Omega : r <= |. X.t - expect(X,P) .| }
    = {t where t is Element of Omega :
    (r to_power 2) <= ( abs( X.t - expect(X,P) )) to_power 2 }
    by CC1,XBOOLE_0:def 10;
    consider Y be Real-Valued-Random-Variable of Sigma,
    E be Real-Valued-Random-Variable of Sigma such that
    P2: E=(Omega --> expect(X,P)) & Y= X-E & Y is_integrable_on P &
    abs(Y) to_power 2 is_integrable_on P2M(P) &
    variance(X,P)=Integral(P2M(P),abs(Y) to_power 2) by defvar,AS;
    reconsider Z = abs(Y) to_power 2
    as Real-Valued-Random-Variable of Sigma by RANDOM_1:23;
    Q3: Z is_integrable_on P by P2,RANDOM_1:def 3;
    then Q4: P.({t where t is Element of Omega : (r to_power 2) <= Z.t } )
    <= expect (Z,P)/(r to_power 2) by AS,POWER:39,RANDOM_1:36;
    V1: expect (Z,P)=variance(X,P) by P2,Q3,RANDOM_1:def 4;
    AC5:dom X = Omega by FUNCT_2:def 1;
    AC6:dom (Omega --> expect(X,P))= Omega by FUNCOP_1:19;
    AC7:dom (X-(Omega --> expect(X,P)))
    =(dom X) /\ (dom (Omega --> expect(X,P))) by VALUED_1:12
    .=Omega by AC5,AC6; then
    AC8:dom(abs(X-(Omega --> expect(X,P)))) =Omega by VALUED_1:def 11;
    then
    AC9:dom((abs(X-(Omega --> expect(X,P)))) to_power 2) =Omega
    by MESFUN6C:def 6;
CC2:{t where t is Element of Omega :
    (r to_power 2) <= ( abs( X.t - expect(X,P) )) to_power 2 } c=
    {t where t is Element of Omega : (r to_power 2) <= Z.t }
    proof
      let s be set;
      assume s in {t where t is Element of Omega :
      (r to_power 2) <= ( abs( X.t - expect(X,P) )) to_power 2 };
      then AC3:ex ss be Element of Omega st s=ss &
      (r to_power 2) <= ( abs( X.ss - expect(X,P) )) to_power 2;
      then Z.s=((abs(X-(Omega --> expect(X,P)))).s) to_power 2
      by MESFUN6C:def 6,AC9,P2
      .=(abs((X -(Omega --> expect(X,P))).s)) to_power 2
      by AC8,AC3,VALUED_1:def 11
      .=(abs(X.s -(Omega --> expect(X,P)).s)) to_power 2
      by AC7,AC3,VALUED_1:13
      .=(abs(X.s - expect(X,P))) to_power 2 by AC3,FUNCOP_1:13;
      hence thesis by AC3;
    end;
    {t where t is Element of Omega : (r to_power 2) <= Z.t }
    c= {t where t is Element of Omega :
    (r to_power 2) <= ( abs( X.t - expect(X,P) )) to_power 2 }
    proof
      let s be set;
      assume s in {t where t is Element of Omega : (r to_power 2) <= Z.t };
      then AC3: ex ss be Element of Omega st s=ss & (r to_power 2) <= Z.ss;
      then Z.s=((abs(X-(Omega --> expect(X,P)))).s) to_power 2
      by MESFUN6C:def 6,AC9,P2
      .=(abs((X -(Omega --> expect(X,P))).s)) to_power 2
      by AC8,AC3,VALUED_1:def 11
      .=(abs(X.s -(Omega --> expect(X,P)).s)) to_power 2
      by AC7,AC3,VALUED_1:13
      .=(abs(X.s - expect(X,P))) to_power 2 by AC3,FUNCOP_1:13;
      hence thesis by AC3;
    end;
    hence thesis by V1,Q4,P1,CC2,XBOOLE_0:def 10;
  end;

begin :: Product Probability

theorem LMXY03:
  for Omega be non empty finite set, f be Function of Omega,REAL,
  P be Function of bool Omega, REAL st
  (for x be set st x c= Omega holds 0 <= P.x & P.x <= 1) &
  P.Omega = 1 & for z be finite Subset of Omega
  holds P.z = setopfunc(z,Omega,REAL,f,addreal)
  holds P is Probability of Trivial-SigmaField Omega
  proof
    let Omega be non empty finite set,
    f be Function of Omega,REAL,
    P be Function of bool Omega, REAL;
    assume that
    AS1: for x be set st x c= Omega holds 0 <= P.x & P.x <= 1 and
    AS2: P.Omega = 1 and
    AS3: for z be finite Subset of Omega
    holds P.z = setopfunc(z,Omega,REAL,f,addreal);
    A2: for A,B being Event of Trivial-SigmaField (Omega)
    st A misses B holds P.(A \/ B) = P.A + P.B
    proof
      let A,B be Event of Trivial-SigmaField (Omega);
      assume
      A3: A misses B;
      reconsider A0 =A,B0=B as finite Subset of Omega;
      A31: Omega = dom f by FUNCT_2:def 1;
      thus P.(A \/ B) = setopfunc((A0 \/ B0),Omega,REAL,f,addreal) by AS3
      .= (addreal).(setopfunc(A0,Omega,REAL,f,addreal),
      setopfunc(B0,Omega,REAL,f,addreal)) by A3,A31,BHSP_5:14
      .= (addreal).(setopfunc(A0,Omega,REAL,f,addreal),(P.B)) by AS3
      .= (addreal).((P.A),(P.B)) by AS3
      .= (P.A) + (P.B) by BINOP_2:def 9;
    end;
    A4: for A being Event of Trivial-SigmaField (Omega) holds 0 <= P.A by AS1;
     for ASeq being SetSequence of Trivial-SigmaField (Omega) st ASeq is
    non-ascending holds P * ASeq is convergent & lim (P * ASeq) = P.(
    Intersection ASeq)
    proof
      let ASeq being SetSequence of Trivial-SigmaField (Omega);
      assume ASeq is non-ascending;
      then consider N be Element of NAT such that
      A6: for m be Element of NAT st N<=m holds Intersection ASeq = ASeq.m
      by RANDOM_1:15;
      now
        let m be Element of NAT;
        assume
        A7: N <= m;
        m in NAT;
        then m in dom ASeq by FUNCT_2:def 1;
        hence (P * ASeq).m = P.(ASeq.m) by FUNCT_1:23
        .= P.(Intersection ASeq) by A6,A7;
      end;
      hence thesis by PROB_1:3;
    end;
    hence thesis by A4,A2,PROB_1:def 13,AS2;
  end;

LMXY04:
  for Omega1, Omega2 be non empty finite set, P1 be Probability of
  Trivial-SigmaField (Omega1), P2 be Probability of
  Trivial-SigmaField (Omega2)
  ex Q be Function of [:Omega1,Omega2:],REAL
  st for x,y be set st x in Omega1 & y in Omega2
  holds Q.(x,y) = (P1.{x})*(P2.{y})
  proof
    let Omega1, Omega2 be non empty finite set, P1 be Probability of
    Trivial-SigmaField (Omega1), P2 be Probability of
    Trivial-SigmaField (Omega2);
    deffunc Q0(set,set)= (P1.{$1})*(P2.{$2});
    consider f being Function such that
    A1: dom f =
    [:Omega1, Omega2:] & for x,y be set st x in Omega1 & y in Omega2
    holds f.(x,y) = Q0(x,y) from FUNCT_3:sch 2;
    for z be set st z in [:Omega1,Omega2:] holds f.z in REAL
    proof
      let z be set;
      assume z in [:Omega1,Omega2:]; then
      consider x,y be set such that
      A2: x in Omega1 & y in Omega2 & z =[x,y] by ZFMISC_1:def 2;
      f.z =f.(x,y) by A2
      .=Q0(x,y) by A1,A2;
      hence f.z in REAL;
    end;
    then
    reconsider f as Function of [:Omega1,Omega2:],REAL by FUNCT_2:5,A1;
    take f;
    thus thesis by A1;
  end;

LMXY05:
  for Omega1, Omega2 be non empty finite set, P1 be Probability of
  Trivial-SigmaField (Omega1), P2 be Probability of
  Trivial-SigmaField (Omega2),
  Q1,Q2 be Function of [:Omega1,Omega2:],REAL
  st (for x,y be set st x in Omega1 & y in Omega2
  holds Q1.(x,y) = (P1.{x})*(P2.{y}) ) &
  (for x,y be set st x in Omega1 & y in Omega2
  holds Q2.(x,y) = (P1.{x})*(P2.{y}) )
  holds Q1=Q2
  proof
    let Omega1, Omega2 be non empty finite set, P1 be Probability of
    Trivial-SigmaField (Omega1), P2 be Probability of
    Trivial-SigmaField (Omega2),
    Q1,Q2 be Function of [:Omega1,Omega2:],REAL;
    assume AS: (for x,y be set st x in Omega1 & y in Omega2
    holds Q1.(x,y) = (P1.{x})*(P2.{y}) ) &
    (for x,y be set st x in Omega1 & y in Omega2
    holds Q2.(x,y) = (P1.{x})*(P2.{y}) );
    P1: dom Q1 = [:Omega1,Omega2:] &
    dom Q2 = [:Omega1,Omega2:] by FUNCT_2:def 1;
    now let z be set;
      assume z in dom Q1;
      then consider x,y be set such that
      P2: x in Omega1 & y in Omega2 & z = [x,y] by ZFMISC_1:def 2;
      thus Q1.z= Q1.(x,y) by P2
      .= (P1.{x})*(P2.{y}) by P2,AS
      .= Q2.(x,y) by P2,AS
      .=Q2.z by P2;
    end;
    hence thesis by FUNCT_1:9,P1;
  end;

LMXY07:
  for Omega1, Omega2 be non empty finite set, P1 be Probability of
  Trivial-SigmaField (Omega1), P2 be Probability of
  Trivial-SigmaField (Omega2),
  Q be Function of [:Omega1,Omega2:],REAL
  ex P be Function of (bool [:Omega1,Omega2:]),REAL st
  for z be finite Subset of [:Omega1,Omega2:]
  holds P.z = setopfunc(z,[:Omega1,Omega2:],REAL,Q,addreal)
  proof
    let Omega1, Omega2 be non empty finite set, P1 be Probability of
    Trivial-SigmaField (Omega1), P2 be Probability of
    Trivial-SigmaField (Omega2),
    Q be Function of [:Omega1,Omega2:],REAL;
    defpred PF[set,set] means
    ex z be finite Subset of [:Omega1,Omega2:]
    st $1=z &
    $2 = setopfunc(z,[:Omega1,Omega2:],REAL,Q,addreal);
    LP1: for x be set st x in bool [:Omega1,Omega2:]
    holds ex y be set st y in REAL & PF[x,y]
    proof
      let x be set;
      assume  x in bool [:Omega1,Omega2:];
      then reconsider z=x as finite Subset of [:Omega1,Omega2:];
      setopfunc(z,[:Omega1,Omega2:],REAL,Q,addreal) in REAL;
      hence thesis;
    end;
    consider P be Function of (bool [:Omega1,Omega2:]),REAL such that
    LP2:for x be set st x in (bool [:Omega1,Omega2:]) holds PF[x,P.x]
    from FUNCT_2:sch 1(LP1);
    take P;
    thus for z be finite Subset of [:Omega1,Omega2:]
    holds P.z = setopfunc(z,[:Omega1,Omega2:],REAL,Q,addreal)
    proof
      let z be finite Subset of [:Omega1,Omega2:];
      ex z1 be finite Subset of [:Omega1,Omega2:] st z=z1 &
      P.z= setopfunc(z1,[:Omega1,Omega2:],REAL,Q,addreal) by LP2;
      hence P.z= setopfunc(z,[:Omega1,Omega2:],REAL,Q,addreal);
    end;
  end;

  LMXY08:
  for Omega1, Omega2 be non empty finite set, P1 be Probability of
  Trivial-SigmaField (Omega1), P2 be Probability of
  Trivial-SigmaField (Omega2),
  Q be Function of [:Omega1,Omega2:],REAL
  for P1,P2 be Function of (bool [:Omega1,Omega2:]),REAL st
  (for z be finite Subset of [:Omega1,Omega2:]
  holds P1.z = setopfunc(z,[:Omega1,Omega2:],REAL,Q,addreal)) &
  (for z be finite Subset of [:Omega1,Omega2:]
  holds P2.z = setopfunc(z,[:Omega1,Omega2:],REAL,Q,addreal))
  holds P1=P2
  proof
    let Omega1, Omega2 be non empty finite set, P1 be Probability of
    Trivial-SigmaField (Omega1), P2 be Probability of
    Trivial-SigmaField (Omega2),
    Q be Function of [:Omega1,Omega2:],REAL;
    let P1,P2 be Function of (bool [:Omega1,Omega2:]),REAL;
    assume AS1:
    for z be finite Subset of [:Omega1,Omega2:]
    holds P1.z = setopfunc(z,[:Omega1,Omega2:],REAL,Q,addreal);
    assume AS2:
    for z be finite Subset of [:Omega1,Omega2:]
    holds P2.z = setopfunc(z,[:Omega1,Omega2:],REAL,Q,addreal);
    now let x be set;
      assume  x in bool [:Omega1,Omega2:];
      then reconsider z=x as finite Subset of [:Omega1,Omega2:];
      thus P1.x = setopfunc(z,[:Omega1,Omega2:],REAL,Q,addreal) by AS1
      .= P2.x by AS2;
    end;
    hence P1=P2 by FUNCT_2:18;
  end;

  LMXXX1:
  for DK, DX be non empty set, F be Function of DX, DK,
  p,q be FinSequence of DX,
  fp,fq be FinSequence of DK st fp=F*p & fq=F*q holds F*(p^q) = fp^fq
  proof
    let DK, DX be non empty set,
    F be Function of DX, DK,
    p,q be FinSequence of DX,
    fp,fq be FinSequence of DK;
    assume AS:fp=F*p & fq=F*q;
    then P0: dom fp = dom p & dom fq = dom q by FINSEQ_3:129;
    P1: len fp =len p & len fq =len q by FINSEQ_3:129,AS; then
    P2: dom (fp^fq)=Seg (len p + len q) by FINSEQ_1:def 7;
    P3: dom (F*(p^q))= dom (p^q) by FINSEQ_3:129
    .=Seg (len p + len q) by FINSEQ_1:def 7;
    for x be set st x in dom (F*(p^q)) holds (fp^fq).x=(F*(p^q)).x
    proof
      let x be set;
      assume A1: x in dom (F*(p^q));
      then reconsider n=x as Element of NAT;
      A4: 1<= n & n <= len p + len q by A1,P3,FINSEQ_1:3;
      A2: (F*(p^q)).n = F.((p^q).n) by A1,FINSEQ_3:129;
      per cases;
      suppose n <= len p;
        then n in Seg (len p) by A4; then
        A22: n in dom p by FINSEQ_1:def 3;
        hence (F*(p^q)).x =F.((p.n)) by FINSEQ_1:def 7,A2
        .=fp.n by AS,A22,P0,FINSEQ_3:129
        .= (fp^fq).x by A22,P0,FINSEQ_1:def 7;
      end;
      suppose A23: not n <= len p; then
        len p < n & n <=len (p^q) by A4,FINSEQ_1:35; then
        A24: (p^q).n=q.(n-len p) by FINSEQ_1:37;
        A241: n-len p <= len p+ len q - len p by XREAL_1:11,A4;
        A242: len p - len p < n - len p by A23,XREAL_1:11; then
        A243: n-len p is Element of NAT by INT_1:16; then
        1<=n-len p by A242,NAT_1:14; then
        n-len p in Seg len q by A241,A243;
        then
        A25: n-len p in dom q by FINSEQ_1:def 3;
        A26:len fp < n & n <= len (fp^fq) by P1,A23,FINSEQ_1:35,A4;
        thus (F*(p^q)).x =fq.(n-len p) by AS,A25,P0,FINSEQ_3:129,A24,A2
        .= (fp^fq).x by A26,P1,FINSEQ_1:37;
      end;
    end;
    hence thesis by P2,P3,FUNCT_1:9;
  end;

theorem LMXY092:
  for DX be non empty set, F be Function of DX,REAL,
  Y be finite Subset of DX holds
  ex p being FinSequence of DX st p is one-to-one &
  rng p = Y & setopfunc(Y,DX,REAL,F,addreal)=Sum(Func_Seq(F,p))
  proof
    let DX be non empty set,
    F be Function of DX,REAL,
    Y be finite Subset of DX;
    dom F = DX by FUNCT_2:def 1;
    then consider p being FinSequence of DX such that
    A2: p is one-to-one & rng p = Y & setopfunc(Y,DX,REAL,F,addreal)
    = (addreal) "**" Func_Seq(F,p) by BHSP_5:def 5;
    Sum(Func_Seq(F,p)) = (addreal) "**" Func_Seq(F,p);
    hence thesis by A2;
  end;

theorem LMXY092A:
  for DX be non empty set, F be Function of DX,REAL, Y be finite Subset of DX,
  p being FinSequence of DX
  st p is one-to-one & rng p = Y holds
  setopfunc(Y,DX,REAL,F,addreal)=Sum(Func_Seq(F,p))
  proof
    let DX be non empty set,
    F be Function of DX,REAL,
    Y be finite Subset of DX,
    p being FinSequence of DX;
    assume A1: p is one-to-one & rng p = Y;
     dom F = DX by FUNCT_2:def 1;
    hence thesis by BHSP_5:def 5,A1;
  end;

THL1FS2:
  for p be Function st dom p = Seg 1 holds p = <*p.1*>
  proof
    let p be Function;
    assume AS:dom p = Seg 1;
    consider x be set such that L1:x= p.1;
    L2: rng p = {x} by L1,FUNCT_1:14,FINSEQ_1:4,AS;
    p is FinSequence-like by FINSEQ_1:def 2,AS;then
    consider pp be FinSequence such that
    L3:pp=p;
    thus thesis by L1,L3,FINSEQ_1:55,L2,AS;
  end;

theorem LMXY090A:
  for DX1,DX2 be non empty set, F1 be Function of DX1,REAL,
  F2 be Function of DX2,REAL,
  G be Function of [:DX1,DX2:], REAL,
  Y1 be non empty finite Subset of DX1 holds
  for p1 being FinSequence of DX1 st
  p1 is one-to-one & rng p1 = Y1 holds
  for p2 being FinSequence of DX2,
  p3 being FinSequence of [:DX1,DX2:],
  Y2 be non empty finite Subset of DX2,
  Y3 be finite Subset of [:DX1,DX2:] st
  p2 is one-to-one & rng p2 = Y2 &
  p3 is one-to-one & rng p3 = Y3 & Y3= [:Y1,Y2:] &
  for x,y be set st x in Y1 & y in Y2
  holds G.(x,y)= (F1.x)*(F2.y) holds
  Sum(Func_Seq(G,p3))= Sum(Func_Seq(F1,p1))*Sum(Func_Seq(F2,p2))
  proof
    let DX1,DX2 be non empty set,
    F1 be Function of DX1,REAL,
    F2 be Function of DX2,REAL,
    G be Function of [:DX1,DX2:],REAL,
    Y1 be non empty finite Subset of DX1;
    let p1 be FinSequence of DX1;
    assume
    AS01: p1 is one-to-one & rng p1 = Y1;
    defpred F[Nat] means for p2 being FinSequence of DX2,
    p3 being FinSequence of [:DX1,DX2:],
    Y2 be non empty finite Subset of DX2,
    Y3 be finite Subset of [:DX1,DX2:] st
    len p2=$1 & p2 is one-to-one & rng p2 = Y2 &
    p3 is one-to-one & rng p3 = Y3 & Y3= [:Y1,Y2:] &
    for x,y be set st x in Y1 & y in Y2 holds G.(x,y)= (F1.x)*(F2.y)
    holds
    Sum(Func_Seq(G,p3))= Sum(Func_Seq(F1,p1))*Sum(Func_Seq(F2,p2));
    consider erp1 be set such that
    XP1: erp1 in (rng p1) by XBOOLE_0:def 1,AS01;
    CXDP11:ex x be set st x in dom p1 & erp1 = p1.x by FUNCT_1:def 5,XP1;
    PP0: F[0]
    proof
      let p2 be FinSequence of DX2,
      p3 be FinSequence of [:DX1,DX2:],
      Y2 be non empty finite Subset of DX2,
      Y3 be finite Subset of [:DX1,DX2:];
      assume
      AS: len p2=0 & p2 is one-to-one & rng p2 = Y2 &
      p3 is one-to-one & rng p3 = Y3 &
      Y3= [:Y1,Y2:] & for x,y be set st x in Y1 & y in Y2
      holds G.(x,y)= (F1.x)*(F2.y);
      then p2 = {};
      hence Sum(Func_Seq(G,p3))
      = Sum(Func_Seq(F1,p1))*Sum(Func_Seq(F2,p2)) by AS;
    end;
    PPF1: F[1]
    proof
      let p2 be FinSequence of DX2,
      p3 be FinSequence of [:DX1,DX2:],
      Y2 be non empty finite Subset of DX2,
      Y3 be finite Subset of [:DX1,DX2:];
      assume
      AS: len p2=1 & p2 is one-to-one & rng p2 = Y2 &
      p3 is one-to-one & rng p3 = Y3 &
      Y3= [:Y1,Y2:] &
      for x,y be set st x in Y1 & y in Y2
      holds G.(x,y)= (F1.x)*(F2.y);
      then CP2001:p2=<*(p2.1)*> by FINSEQ_1:57;
      then CY2:Y2={p2.1} by FINSEQ_1:55,AS;
      set w = p2.1;
      set z = (F2*p2).1;
      dom F2 = DX2 by FUNCT_2:def 1;
      then rng p2 c= dom F2;
      then dom (F2*p2) =dom p2 by RELAT_1:46;
      then CY3:dom (F2*p2) =Seg 1 by CP2001,FINSEQ_1:55;
      then Func_Seq(F2,p2)=<*z*> by THL1FS2; then
      X1: Sum(Func_Seq(F2,p2)) = z by RVSUM_1:103;
      X2: Y3= [:Y1,{w}:] by AS,CP2001,FINSEQ_1:55;
      C1:len p1 =card(Y1) by FINSEQ_4:77,AS01;
      C2: len p3 = card (rng p3) by FINSEQ_4:77,AS
      .=card(Y1) by X2,CARD_2:13,AS;
      AAA: dom p1=Seg (card(Y1)) by C1,FINSEQ_1:def 3
      .= dom p3 by C2,FINSEQ_1:def 3;
      deffunc Q33F(Nat) = [p1.$1,w ];
      consider q3 be FinSequence such that
      X21: len q3 = len p3 and
      X22: for k be Nat st k in dom q3 holds q3.k=Q33F(k) from FINSEQ_1:sch 2;
      A14: dom q3 = Seg(len p3) by X21,FINSEQ_1:def 3;
      A15: dom p3 = Seg(len p3) by FINSEQ_1:def 3;
      now
        let k be Nat;
        assume
        A16: k in dom q3; then
        A24: p1.k in Y1 by AS01,FUNCT_1:12,A14,A15,AAA;
        p2.1 in Y2 by TARSKI:def 1,CY2;
        then [ p1.k,w ] in [:Y1,Y2:] by ZFMISC_1:106,A24;
        hence q3.k in [:Y1,Y2:] by A16,X22;
      end;
      then
      q3 is FinSequence of [:Y1,Y2:] by FINSEQ_2:14; then
      X9: rng q3 c= [:Y1,Y2:] by FINSEQ_1:def 4;
      [:Y1,Y2:] c= [:DX1,DX2:] by ZFMISC_1:119;
      then
      rng q3 c= [:DX1,DX2:] by X9,XBOOLE_1:1;
      then
      reconsider q3 as FinSequence of [:DX1,DX2:] by FINSEQ_1:def 4;
      now let x1,x2 be set;
        assume AD1:x1 in dom q3 & x2 in dom q3 & q3.x1=q3.x2;
        then
        AD1a: x1 in dom p1 & x2 in dom p1 by X21,FINSEQ_1:def 3,A15,AAA;
        reconsider n1=x1,n2=x2 as Element of NAT by AD1;
        [p1.n1,w] = q3.n1 by X22,AD1
        .=[p1.n2,w] by X22,AD1; then
        p1.n1=p1.n2 by ZFMISC_1:33;
        hence x1=x2 by AS01,AD1a,FUNCT_1:def 8;
      end;
      then
      XJ0:q3 is one-to-one by FUNCT_1:def 8;
      XJ1: rng q3 = [:Y1,Y2:]
      proof
        now let z be set;
          assume z in [:Y1,Y2:];
          then consider y1,y2 be set such that
          J11: y1 in Y1 & y2 in Y2 & z=[y1,y2] by ZFMISC_1:def 2;
          consider n1 be set such that
          J22: n1 in dom p1 & y1=p1.n1 by AS01,J11,FUNCT_1:def 5;
          reconsider n1 as Element of NAT by J22;
          J25:n1 in dom q3 by J22,X21,FINSEQ_1:def 3,A15,AAA;
          y2=w by CY2,TARSKI:def 1,J11;
          then q3.n1=z by J11,J22,X22,J25;
          hence z in rng q3 by FUNCT_1:12,J25;
        end; then
        [:Y1,Y2:] c= rng q3 by TARSKI:def 3;
        hence thesis by X9,XBOOLE_0:def 10;
      end;
      then consider P being Permutation of dom p3 such that
      H11:
      q3 = p3*P & dom P = dom p3 & rng P = dom p3 by XJ0,BHSP_5:1,AS;
      H13: Func_Seq(G,q3) = Func_Seq(G,p3)*P by RELAT_1:55,H11;
      dom G = [:DX1,DX2:] by FUNCT_2:def 1;then
      rng p3 c= dom G; then
      dom Func_Seq(G,p3) = dom p3 by RELAT_1:46;
      then
X5: Sum(Func_Seq(G,q3)) = Sum(Func_Seq(G,p3)) by H13,FINSOP_1:8;
      XXX: dom G =[:DX1,DX2:] by FUNCT_2:def 1;
      dom F1=DX1 by FUNCT_2:def 1;
      then
      XV12: dom(F1*p1) = dom p1 by RELAT_1:46,AS01
      .=Seg (len p1) by FINSEQ_1:def 3;
      XV3: dom (G*q3) = dom q3 by XXX,XJ1,RELAT_1:46
      .=Seg (len p1) by C1,C2,X21,FINSEQ_1:def 3;
      then
      XV7: dom (G*q3) = dom (z (#)(F1*p1) ) by VALUED_1:def 5,XV12;
      now let x be set;
        assume XV4: x in dom (G*q3);
        then reconsider nx=x as Element of NAT;
        dom (G*q3) = dom q3 by XXX,XJ1,RELAT_1:46
        .=Seg (len q3) by FINSEQ_1:def 3; then
        J22: nx in dom q3 by FINSEQ_1:def 3,XV4;
        1 <= nx & nx <= len p1 by FINSEQ_1:3,XV4,XV3; then
        H3333:p1/.nx=p1.nx by FINSEQ_4:24;
        XV9003:
        q3.nx = [ p1.nx, w ] by X22,J22;
        U3:nx in dom p1 by XV4,XV3,FINSEQ_1:def 3; then
        XV9: q3.nx = [ p1/.nx, w ] by XV9003,PARTFUN1:def 8;
        p1.nx in Y1 by U3,FUNCT_1:12,AS01; then
        test:
        p1/.nx in Y1 & w in Y2 by U3,PARTFUN1:def 8,CY2,TARSKI:def 1;
        1 in dom(F2*p2) by CY3; then
        test1: z =F2.w by FUNCT_1:22;
        thus (G*q3).x =G.(p1/.nx,w) by XV9,XV4,FUNCT_1:22
        .=F1.(p1/.nx) * z by test1,AS,test
        .= ((F1*p1).nx) *z by H3333,XV3,XV12,XV4,FUNCT_1:22
        .= (z (#)(F1*p1)).x by VALUED_1:6;
      end; then
      Func_Seq(G,q3) = z * (Func_Seq(F1,p1)) by XV7,FUNCT_1:9;
      hence thesis by X1,X5,RVSUM_1:117;
    end;
    PP1: for n be Element of NAT st F[n] holds F[n+1]
    proof
      let n be Element of NAT;
      assume A1: F[n];
      now per cases;
        case n=0;
          hence F[n+1] by PPF1;
        end;
        case CASENNE:n>0;
          now let p2 be FinSequence of DX2,
            p3 be FinSequence of [:DX1,DX2:],
            Y2 be non empty finite Subset of DX2,
            Y3 be finite Subset of [:DX1,DX2:];
            assume
            AS1: len p2=n+1 & p2 is one-to-one & rng p2 = Y2 &
            p3 is one-to-one & rng p3 = Y3 &
            Y3= [:Y1,Y2:] &
            for x,y be set st x in Y1 & y in Y2
            holds G.(x,y)= (F1.x)*(F2.y);
            set lb = len p1;
            set la = len p2;
            deffunc FG1(Nat)
            = [ p1.(($1-'1) mod lb + 1), p2.(($1-'1) div lb + 1) ];
            consider FG be FinSequence such that
            A12: len FG = la*lb and
            A13: for k be Nat st k in dom FG holds FG.k=FG1(k)
            from FINSEQ_1:sch 2;
            A14: dom FG = Seg(la*lb) by A12,FINSEQ_1:def 3;
            A15: dom p1= Seg lb by FINSEQ_1:def 3;
            AAA: now
            reconsider lap=la,lbp=lb as natural number;
            let k be Nat;
            set i=(k-'1) div lb + 1;
            set j=(k-'1) mod lb + 1;
            assume k in dom FG; then
            A17: k in Seg (la*lb) by A12,FINSEQ_1:def 3; then
            A18: k <= la*lb by FINSEQ_1:3;
            then (k -' 1) <= (la*lb -' 1) by NAT_D:42;
            then
            A19: (k -' 1) div lb <= (la*lb -' 1) div lb by NAT_2:26;
            1 <= k by A17,FINSEQ_1:3;
            then
            A20: lbp divides (lap*lbp) & 1 <= la*lb by
            A18,NAT_D:def 3,XXREAL_0:2;
            A21: lb <> 0 by A17;
            then lb >= 0+1 by NAT_1:13;
            then ((lap*lbp) -' 1) div lbp = ((lap*lbp) div lbp) - 1 by
            A20, NAT_2:17;
            then
            A22: (k -' 1) div lb + 1 <= la*lb div lb by A19,XREAL_1:21;
            reconsider la,lb as Nat;
            i >= 0+1 & i <= la by A21,A22,NAT_D:18,XREAL_1:8;
            then i in Seg la;
            hence i in dom p2 by FINSEQ_1:def 3;
            (k -' 1) mod lb < lb by A21,NAT_D:1;
            then j >= 0+1 & j <= lb by NAT_1:13;
            then j in Seg lb;
            hence j in dom p1 by FINSEQ_1:def 3;
          end;
          now
            let k be Nat;
            set i=(k-'1) div lb + 1;
            set j=(k-'1) mod lb + 1;
            assume
            A16: k in dom FG;
            then
            A23: p2.i in rng p2 by FUNCT_1:12,AAA;
             p1.j in rng p1 by FUNCT_1:12,AAA,A16;
            then [ p1.j,p2.i ] in [:DX1,DX2:] by A23,ZFMISC_1:106;
            hence FG.k in [:DX1,DX2:] by A13,A16;
          end;
          then reconsider q3=FG as FinSequence of [:DX1,DX2:] by FINSEQ_2:14;
          LENP1AA:len p1 =card(Y1) by AS01,FINSEQ_4:77;
          now let x1,x2 be set;
            assume AD1:x1 in dom q3 & x2 in dom q3 & q3.x1=q3.x2;
            then
            AD1a:x1 in Seg (len q3) & x2 in Seg (len q3) by FINSEQ_1:def 3;
            reconsider n1=x1,n2=x2 as Element of NAT by AD1;
            AD2: q3.n1=[ p1.((n1-'1) mod lb + 1), p2.((n1-'1) div lb + 1) ]
            by A13,AD1;
            AD3: q3.n2=[ p1.((n2-'1) mod lb + 1), p2.((n2-'1) div lb + 1) ]
            by A13,AD1;
            then AD4:p1.((n1-'1) mod lb + 1) = p1.((n2-'1) mod lb + 1)
            by ZFMISC_1:33,AD1,AD2;
            (n1-'1) mod lb + 1 in dom p1 &
            (n2-'1) mod lb + 1 in dom p1 by AD1,AAA; then
            AD5P: (n1-'1) mod lb + 1 = (n2-'1) mod lb + 1
            by AS01,AD4,FUNCT_1:def 8;
            AD6:p2.((n1-'1) div lb + 1) = p2.((n2-'1) div lb + 1)
            by ZFMISC_1:33,AD1,AD2,AD3;
            (n1-'1) div lb + 1 in dom p2 &
            (n2-'1) div lb + 1 in dom p2 by AD1,AAA; then
            AD7P: (n1-'1) div lb + 1 = (n2-'1) div lb + 1
            by AS1,AD6,FUNCT_1:def 8;
            n1=n2
            proof
              X1: 1<=n1 & n1 <=len q3 by FINSEQ_1:3,AD1a;
              X2: 1<=n2 & n2 <=len q3 by FINSEQ_1:3,AD1a;
              0 < lb by A15,CXDP11;
              then X3P:
              (n1-'1) = lb*((n1-'1) div lb) + ((n1-'1) mod lb)&
              (n2-'1) = lb*((n1-'1) div lb) + ((n1-'1) mod lb)
              by AD5P,AD7P,NAT_D:2;
              X4: n1-'1 +1= n1 +1 -'1 by NAT_D:38,X1
              .= n1 by NAT_D:34;
              n2-'1 +1= n2 +1 -'1 by NAT_D:38,X2
              .= n2 by NAT_D:34;
              hence thesis by X3P,X4;
            end;
            hence x1=x2;
          end;
          then
          J0:q3 is one-to-one by FUNCT_1:def 8;
          J1: rng q3 = [:Y1,Y2:]
          proof
            now let z be set;
              assume z in [:Y1,Y2:]; then
              consider y1,y2 be set such that
              J11: y1 in Y1 & y2 in Y2 & z=[y1,y2] by ZFMISC_1:def 2;
              consider n1 be set such that
              J22: n1 in dom p1 & y1=p1.n1 by AS01,J11,FUNCT_1:def 5;
              J22A: n1 in Seg (len p1) by FINSEQ_1:def 3,J22;
              reconsider n1 as Element of NAT by J22;
              consider n2 be set such that
              J23: n2 in dom p2 & y2=p2.n2 by AS1,J11,FUNCT_1:def 5;
              J23A: n2 in Seg (len p2) by FINSEQ_1:def 3,J23;
              reconsider n2 as Element of NAT by J23;
              J24A: 1<=n1 & n1 <=len p1 by J22A,FINSEQ_1:3;
              J24B: 1<=n2 & n2 <=len p2 by J23A,FINSEQ_1:3;
              reconsider n11 = n1-1 as Element of NAT by INT_1:18,J24A;
              reconsider n21 = n2-1 as Element of NAT by INT_1:18,J24B;
              set k1=n11 + lb*n21;
              J25A: n11 <= len p1 -1 by J24A,XREAL_1:11;
              len p1 -1 < len p1 by XREAL_1:46;
              then
              J26A: n11 < len p1 by J25A,XXREAL_0:2;
              J27A: k1 div lb = n11 div lb + n21 by LENP1AA,INT_3:8
              .=0 + n21 by J26A,NAT_D:27
              .=n21;
              J27B: k1 mod lb = n11 mod lb by INT_3:8
              .=n11 by J26A,NAT_D:24;
              set k=k1+1;
              J24C:1 <=k by NAT_1:14;
              J24D:k-'1 =k-1 by XREAL_1:235,NAT_1:14
              .=k1;
              then J24E: n1=(k-'1) mod lb + 1 by J27B;
              J24F: n2=(k-'1) div lb + 1 by J24D,J27A;
              J24G: n11+1 + lb*n21 <= len p1 + lb*n21 by XREAL_1:8,J24A;
               n21 <= len p2 -1 by J24B,XREAL_1:11;
              then lb*n21 <= lb*(len p2 -1) by  XREAL_1:66;
              then
              len p1 + lb*n21 <= len p1 +lb*(len p2 -1) by XREAL_1:8;
              then
               k<= lb*la by J24G,XXREAL_0:2;
              then k in Seg (len FG) by A12,J24C; then
              J25:k in dom FG by FINSEQ_1:def 3;
              then q3.k =z by J11,J22,J23,A13,J24E,J24F;
              hence z in rng q3 by FUNCT_1:12,J25;
            end;
            then
            L1: [:Y1,Y2:] c= rng q3 by TARSKI:def 3;
            now let z be set;
              assume z in rng q3;
              then consider n1 be set such that
              J22: n1 in dom q3 & z=q3.n1 by FUNCT_1:def 5;
              reconsider n1 as Element of NAT by J22;
              J28:z= [ p1.((n1-'1) mod lb + 1), p2.((n1-'1) div lb + 1) ]
              by J22,A13;
              J23: p1.((n1-'1) mod lb + 1) in Y1 by AS01,FUNCT_1:12,J22,AAA;
              (n1-'1) div lb + 1 in dom p2 by J22,AAA;
              then
              p2.((n1-'1) div lb + 1) in Y2 by AS1,FUNCT_1:12;
              hence z in [:Y1,Y2:] by J28,J23,ZFMISC_1:def 2;
            end; then
            rng q3 c= [:Y1,Y2:] by TARSKI:def 3;
            hence thesis by L1,XBOOLE_0:def 10;
          end;
          set q30=q3|(lb*n);
           lb*n <=lb*la by XREAL_1:66,NAT_1:11,AS1;
          then LENQ30: len q30=lb*n by FINSEQ_1:21,A12;
          set q31=q3 /^ (lb*n);
          reconsider q30 as FinSequence of [:DX1,DX2:];
          reconsider q31 as FinSequence of [:DX1,DX2:];
          H0: q3=q30^q31 by RFINSEQ:21;
          set p20=p2|n;
          reconsider p20 as FinSequence of DX2;
          XX2:len p20=n by FINSEQ_3:59,AS1;
          then
          XX3:dom p20 is non empty by CASENNE,FINSEQ_1:def 3;
          H1: p20 is one-to-one by FUNCT_1:84,AS1;
          reconsider Y20 = rng p20
          as non empty finite Subset of DX2 by XX3,RELAT_1:65;
          H3: q30 is one-to-one by J0,FUNCT_1:84;
          H4: rng q30 = [:Y1,Y20:]
          proof
            now let z be set;
              assume z in [:Y1,Y20:];
              then
              consider y1,y2 be set such that
              J11: y1 in Y1 & y2 in Y20 & z=[y1,y2] by ZFMISC_1:def 2;
              consider n1 be set such that
              J22: n1 in dom p1 & y1=p1.n1 by AS01,J11,FUNCT_1:def 5;
              J22A:n1 in Seg (len p1) by FINSEQ_1:def 3,J22;
              reconsider n1 as Element of NAT by J22;
              consider n2 be set such that
              J23: n2 in dom p20 & y2=p20.n2 by J11,FUNCT_1:def 5;
              J23A:n2 in Seg (len p20) by FINSEQ_1:def 3,J23;
              reconsider n2 as Element of NAT by J23;
              J230: y2=p2.n2 by J23,FUNCT_1:70;
              J24A: 1<=n1 & n1 <=len p1 by J22A,FINSEQ_1:3;
              J24B: 1<=n2 & n2 <=len p20 by J23A,FINSEQ_1:3;
              reconsider n11 = n1-1 as Element of NAT by INT_1:18,J24A;
              reconsider n21 = n2-1 as Element of NAT by INT_1:18,J24B;
              set k1=n11 + lb*n21;
              J25A: n11 <= len p1 -1 by J24A,XREAL_1:11;
              len p1 -1 < len p1 by XREAL_1:46;
              then
              J26A: n11 < len p1 by J25A,XXREAL_0:2;
              J27A: k1 div lb = n11 div lb + n21 by LENP1AA,INT_3:8
              .=0 + n21 by J26A,NAT_D:27
              .=n21;
              J27B: k1 mod lb = n11 mod lb by INT_3:8
              .=n11 by J26A,NAT_D:24;
              set k=k1+1;
              J24C:1 <=k by NAT_1:14;
              J24D:k-'1 =k-1 by XREAL_1:235,NAT_1:14
              .=k1;
              then J24E: n1=(k-'1) mod lb + 1 by J27B;
              J24F: n2=(k-'1) div lb + 1 by J24D,J27A;
              J24G:
              n11+1 + lb*n21 <= len p1 + lb*n21 by XREAL_1:8,J24A;
               n21 <= len p20 -1 by J24B,XREAL_1:11;
              then lb*n21 <= lb*(len p20 -1) by  XREAL_1:66;
              then
              len p1 + lb*n21 <= len p1 +lb*(len p20 -1) by XREAL_1:8;
              then
               k<= lb*n by J24G,XXREAL_0:2,XX2;
              then k in Seg (len q30) by J24C,LENQ30;
              then
              J25:k in dom q30 by FINSEQ_1:def 3;
              then
              J250: k in dom q3 by RELAT_1:86;
              q30.k =q3.k by J25,FUNCT_1:70
              .=z by J11,J22,J230,A13,J250,J24E,J24F;
              hence z in rng q30 by FUNCT_1:12,J25;
            end;
            then
            L1: [:Y1,Y20:] c= rng q30 by TARSKI:def 3;
            now let z be set;
              assume z in rng q30; then
              consider n1 be set such that
              J22: n1 in dom q30 & z=q30.n1 by FUNCT_1:def 5;
              XJ22:n1 in Seg (len q30) by FINSEQ_1:def 3,J22;
              reconsider n1 as Element of NAT by J22;
              J220: n1 in dom q3 by J22,RELAT_1:86;
              z=q3.n1 by J22,FUNCT_1:70; then
              J28:z= [ p1.((n1-'1) mod lb + 1), p2.((n1-'1) div lb + 1) ]
              by A13,J220;
              J23: p1.((n1-'1) mod lb + 1) in Y1 by AS01,FUNCT_1:12,J220,AAA;
              XA22: 1<=n1 & n1 <=lb*n by XJ22,LENQ30,FINSEQ_1:3;
              XA230: lb divides lb*n by INT_1:def 9;
              XA231: 1 <=lb by NAT_1:14,LENP1AA;
              1 <=lb*n by NAT_1:14,LENP1AA,CASENNE; then
              XA24: ( (lb*n)-' 1) div lb =(lb*n) div lb -1
              by XA230,XA231,NAT_2:17
              .=n-1 by NAT_D:20,LENP1AA;
              n1-'1 <= (lb*n)-' 1 by NAT_D:42,XA22; then
              (n1-'1) div lb <= ( (lb*n)-' 1 ) div lb by NAT_2:26; then
              XA25: (n1-'1) div lb + 1 <= ( n-1) + 1 by XREAL_1:8,XA24;
              (n1-'1) div lb + 1 in dom p2 by J220,AAA; then
              (n1-'1) div lb + 1 in Seg len p2 by FINSEQ_1:def 3; then
              1<=(n1-'1) div lb + 1 & (n1-'1) div lb + 1 <=n
              by XA25,FINSEQ_1:3; then
              J231:(n1-'1) div lb + 1 in Seg n;
              (n1-'1) div lb + 1 in dom p2 by J220,AAA; then
              J230:(n1-'1) div lb + 1 in dom p20 by J231,RELAT_1:86; then
              p20.((n1-'1) div lb + 1) in Y20 by FUNCT_1:12; then
              p2.((n1-'1) div lb + 1) in Y20 by J230,FUNCT_1:70;
              hence z in [:Y1,Y20:] by J28,J23,ZFMISC_1:def 2;
            end; then
            rng q30 c= [:Y1,Y20:] by TARSKI:def 3;
            hence thesis by L1,XBOOLE_0:def 10;
          end;
          now let x,y be set;
          assume H51:x in Y1 & y in Y20;
          Y20 c= rng p2 by RELAT_1:99;
          hence G.(x,y)= (F1.x)*(F2.y) by AS1,H51;
        end;
        then H6: Sum(Func_Seq(G,q30))
        = Sum(Func_Seq(F1,p1))*Sum(Func_Seq(F2,p20)) by A1,H1,H3,H4,XX2;
        dom F1=DX1 by FUNCT_2:def 1; then
        XV12: dom(F1*p1) = dom p1 by RELAT_1:46,AS01
        .=Seg (len p1) by FINSEQ_1:def 3;
        DDD1: dom G = [:DX1,DX2:] by FUNCT_2:def 1;
        len q3 = n*lb + lb by AS1,A12; then
        HH011: n*lb <= len q3 by NAT_1:11; then
        HH02: len q31 = (len q3) - (lb*n) by RFINSEQ:def 2
        .= lb by AS1,A12;
        XV3P: [:Y1,Y2:] c= [:DX1,DX2:] & rng q31 c= rng q3
        by AS1,FINSEQ_5:36; then
        XV3: dom (G*q31) = dom q31 by DDD1,RELAT_1:46
        .=Seg (len p1) by HH02,FINSEQ_1:def 3; then
        XV7: dom (G*q31) = dom (((Func_Seq(F2,p2))/.(n+1)) (#)(F1*p1))
        by XV12,VALUED_1:def 5;
        now let x be set;
          assume XV4: x in dom (G*q31);
          then reconsider nx=x as Element of NAT;
          J22NXP:dom (G*q31) = dom q31 by DDD1,RELAT_1:46,XV3P
          .=Seg (len q31) by FINSEQ_1:def 3;
          UU1: 1 <= nx & nx <= len p1 by FINSEQ_1:3,XV4,XV3; then
          H3333:p1/.nx=p1.nx by FINSEQ_4:24;
          H3344:1 <= n+1 & n+1 <= len p2 by AS1,XREAL_1:33;
          then U2:n+1 in Seg (len p2);
          then H4433:n+1 in dom p2 by FINSEQ_1:def 3;
          H3344a:p2/.(n+1)= p2.(n+1) by FINSEQ_4:24,H3344;
          dom F2 = DX2 by FUNCT_2:def 1;
          then rng p2 c= dom F2;
          then
          H44332: n+1 in dom (F2*p2) by H4433,RELAT_1:46;
          H4444:F2.(p2/.(n + 1))= (F2*p2).(n+1) by H4433,FUNCT_1:23,H3344a
          .= ((Func_Seq(F2,p2))/.(n+1)) by PARTFUN1:def 8,H44332;
          XLLL1: 1 <= nx & nx <= lb by FINSEQ_1:3,J22NXP,XV4,HH02;
          then
          XLLL2P:nx+lb*n <= lb + lb*n by XREAL_1:8;
          XV800P:nx <= nx+lb*n by NAT_1:11;
          then 1<= nx+lb*n & nx+lb*n <= lb*la
          by XLLL2P,AS1,XXREAL_0:2,XLLL1; then
          nx+lb*n in dom FG by A14; then
          XV9005:q3.(nx+lb*n) = [ p1.(((nx+lb*n)-'1) mod lb + 1),
          p2.(((nx+lb*n)-'1) div lb + 1) ] by A13;
          XV9000P:nx in dom q31 by J22NXP,XV4,FINSEQ_1:def 3;
          XV7001:(nx+lb*n)-'1 =(nx+lb*n)-1
          by XV800P,XXREAL_0:2,XLLL1,XREAL_1:235
          .=(nx-1) +lb*n
          .=(nx-'1) +lb*n by UU1, XREAL_1:235;
          nx-1 < nx by XREAL_1:46;
          then
          nx-1 < lb by XLLL1,XXREAL_0:2;
          then
          XV7002: (nx-'1) < lb by UU1, XREAL_1:235;
          XVX:((nx-'1) +lb*n) div lb +1
          = (nx-'1) div lb + n +1 by LENP1AA,INT_3:8
          .=0 + n+1 by XV7002,NAT_D:27;
          XV93P:((nx-'1) +lb*n) mod lb +1 = (nx-'1) mod lb +1 by INT_3:8
          .=(nx-'1) +1 by XV7002,NAT_D:24
          .=nx-1 +1 by UU1, XREAL_1:235;
          U3:nx in dom p1 & (n + 1) in dom p2 by XV4,XV3,U2,FINSEQ_1:def 3;
          then
          p1/.nx = p1.nx & p2.(n + 1)= p2/.(n + 1) by PARTFUN1:def 8;
          then
          XV9: q31.nx = [ p1/.nx, p2/.(n + 1) ]
          by XV9000P,XV9005,RFINSEQ:def 2,HH011,XV7001,XVX,XV93P;
          p1.nx in Y1 & p2.(n + 1) in Y2 by U3,FUNCT_1:12,AS1,AS01; then
          test: p1/.nx in Y1 & p2/.(n + 1) in Y2 by U3,PARTFUN1:def 8;
          thus (G*q31).x =G.(p1/.nx, p2/.(n + 1)) by XV9,XV4,FUNCT_1:22
          .=F1.(p1/.nx ) * F2.(p2/.(n + 1)) by AS1,test
          .= ((F1*p1).nx) *((Func_Seq(F2,p2))/.(n+1))
          by H4444,H3333,XV12,XV4,XV3,FUNCT_1:22
          .= (((Func_Seq(F2,p2))/.(n+1)) (#)(F1*p1) ).x by VALUED_1:6;
        end;
        then Func_Seq(G,q31) =
        ((Func_Seq(F2,p2))/.(n+1)) * (Func_Seq(F1,p1)) by XV7,FUNCT_1:9;
        then
        H7: Sum(Func_Seq(G,q31)) = Sum(Func_Seq(F1,p1))
        *((Func_Seq(F2,p2))/.(n+1)) by RVSUM_1:117;
        H8: Func_Seq(G,q3) = Func_Seq(G,q30) ^ Func_Seq(G,q31) by H0,LMXXX1;
        dom F2 = DX2 by FUNCT_2:def 1;
        then rng p2 c= dom F2;
        then dom (F2*p2) =dom p2 by RELAT_1:46;
        then dom (Func_Seq(F2,p2)) = Seg(len p2) by FINSEQ_1:def 3;
        then
        H90: len (Func_Seq(F2,p2)) = n+1 by AS1,FINSEQ_1:def 3;
         (Func_Seq(F2,p2)) | n = Func_Seq(F2,p20) by RELAT_1:112;
        then H9: Func_Seq(F2,p2) = (Func_Seq(F2,p20))
        ^ <* (Func_Seq(F2,p2))/.(n+1) *> by FINSEQ_5:24,H90;
        H10: Sum(Func_Seq(G,q3))
        =Sum(Func_Seq(G,q30)) + Sum(Func_Seq(G,q31)) by RVSUM_1:105,H8
        .= Sum(Func_Seq(F1,p1))*(Sum(Func_Seq(F2,p20))
        + (Func_Seq(F2,p2))/.(n+1) ) by H6,H7
        .= Sum(Func_Seq(F1,p1))* Sum(Func_Seq(F2,p2)) by RVSUM_1:104,H9;
        consider P being Permutation of dom p3 such that
        H11:
        q3 = p3*P & dom P = dom p3 & rng P = dom p3 by J0,J1,BHSP_5:1,AS1;
        H13: Func_Seq(G,q3) = Func_Seq(G,p3)*P by RELAT_1:55,H11;
        dom G = [:DX1,DX2:] by FUNCT_2:def 1;then
        rng p3 c= dom G;then
        dom Func_Seq(G,p3) = dom p3 by RELAT_1:46;
        hence
Sum(Func_Seq(G,p3)) = Sum(Func_Seq(F1,p1))* Sum(Func_Seq(F2,p2))
        by H10,H13,FINSOP_1:8;
      end;
      hence F[n+1];
    end;
  end;
  hence F[n+1];
end;
L1: for n be Element of NAT holds F[n] from NAT_1:sch 1(PP0,PP1);
now let p2 be FinSequence of DX2,
  p3 be FinSequence of [:DX1,DX2:],
  Y2 be non empty finite Subset of DX2,
  Y3 be finite Subset of [:DX1,DX2:];
  assume L2:
  p2 is one-to-one & rng p2 = Y2 & p3 is one-to-one & rng p3 = Y3 &
  Y3= [:Y1,Y2:] &
  for x,y be set st x in Y1 & y in Y2 holds G.(x,y)= (F1.x)*(F2.y);
  len p2 is Element of NAT;
  hence Sum(Func_Seq(G,p3))= Sum(Func_Seq(F1,p1))*Sum(Func_Seq(F2,p2))
  by L1,L2;
end;
hence thesis;
end;

theorem LMXY090:
  for DX1,DX2 be non empty set, F1 be Function of DX1,REAL,
  F2 be Function of DX2,REAL,
  G be Function of [:DX1,DX2:], REAL,
  Y1 be non empty finite Subset of DX1,
  Y2 be non empty finite Subset of DX2,
  Y3 be finite Subset of [:DX1,DX2:] st Y3= [:Y1,Y2:] &
  for x,y be set st x in Y1 & y in Y2 holds G.(x,y)= (F1.x)*(F2.y)
  holds
  setopfunc(Y3,[:DX1,DX2:],REAL,G,addreal)
  =setopfunc(Y1,DX1,REAL,F1,addreal) * setopfunc(Y2,DX2,REAL,F2,addreal)
  proof
    let DX1,DX2 be non empty set,
    F1 be Function of DX1,REAL,
    F2 be Function of DX2,REAL,
    G be Function of [:DX1,DX2:],REAL,
    Y1 be non empty finite Subset of DX1,
    Y2 be non empty finite Subset of DX2,
    Y3 be finite Subset of [:DX1,DX2:];
    assume AS:
    Y3= [:Y1,Y2:] & for x,y be set st x in Y1 & y in Y2
    holds G.(x,y)= (F1.x)*(F2.y);
    consider p1 being FinSequence of DX1 such that
    Q1: p1 is one-to-one & rng p1 = Y1 & setopfunc(Y1,DX1,REAL,F1,addreal)
    =Sum(Func_Seq(F1,p1)) by LMXY092;
    consider p2 being FinSequence of DX2 such that
    Q2: p2 is one-to-one & rng p2 = Y2 & setopfunc(Y2,DX2,REAL,F2,addreal)
    =Sum(Func_Seq(F2,p2)) by LMXY092;
    consider p3 being FinSequence of [:DX1,DX2:] such that
    Q3: p3 is one-to-one & rng p3 = Y3 &
    setopfunc(Y3,[:DX1,DX2:],REAL,G,addreal)
    =Sum(Func_Seq(G,p3)) by LMXY092;
    thus thesis by AS,Q1,Q2,Q3,LMXY090A;
  end;

theorem LMXY093:
  for DX be non empty set, F be Function of DX,REAL,
  Y be finite Subset of DX st
  for x be set st x in Y holds 0<= F.x holds
  0 <= setopfunc(Y,DX,REAL,F,addreal)
  proof
    let DX be non empty set,
    F be Function of DX,REAL,
    Y be finite Subset of DX;
    assume AS:for x be set st x in Y holds 0<= F.x;
    consider p being FinSequence of DX such that
    P1: p is one-to-one & rng p = Y &
    setopfunc(Y,DX,REAL,F,addreal) =Sum(Func_Seq(F,p)) by LMXY092;
    now let i be Nat;
      assume P11: i in dom (Func_Seq(F,p));
      then
      P2: (Func_Seq(F,p)).i = F.(p.i) by FUNCT_1:22;
      i in dom p by P11,FUNCT_1:21;
      hence 0 <= (Func_Seq(F,p)).i by P2,AS,P1,FUNCT_1:12;
    end;
    hence thesis by P1,RVSUM_1:114;
  end;

theorem LMXY094:
  for DX be non empty set, F be Function of DX,REAL,
  Y1,Y2 be finite Subset of DX st
  Y1 c= Y2 & for x be set st x in Y2 holds 0<= F.x holds
  setopfunc(Y1,DX,REAL,F,addreal)
  <= setopfunc(Y2,DX,REAL,F,addreal)
  proof
    let DX be non empty set, F be Function of DX,REAL,
    Y1,Y2 be finite Subset of DX;
    assume AS:
    Y1 c= Y2 & for x be set st x in Y2 holds 0<= F.x;
    consider p1 being FinSequence of DX such that
    PP1: p1 is one-to-one & rng p1 = Y1 & setopfunc(Y1,DX,REAL,F,addreal)
    =Sum(Func_Seq(F,p1)) by LMXY092;
    reconsider Y3 = Y2 \ Y1 as finite Subset of DX;
    consider p2 being FinSequence of DX such that
    PP3: p2 is one-to-one & rng p2 = Y3 &
    setopfunc(Y3,DX,REAL,F,addreal)=Sum(Func_Seq(F,p2)) by LMXY092;
    now let i be Nat;
      assume P11: i in dom (Func_Seq(F,p2)); then
      P2: (Func_Seq(F,p2)).i = F.(p2.i) by FUNCT_1:22;
      i in dom p2 by P11,FUNCT_1:21; then
      PP2: p2.i in Y3 by PP3,FUNCT_1:12;
      Y3 c= Y2 by XBOOLE_1:36;
      hence 0 <= (Func_Seq(F,p2)).i by P2,AS,PP2;
    end; then
    Q2: 0 <= Sum(Func_Seq(F,p2)) by RVSUM_1:114;
    reconsider p3=p1^p2 as FinSequence of DX;
    PP6: rng p3 = rng p1 \/ rng p2 by FINSEQ_1:44
    .=Y1 \/ Y2 by XBOOLE_1:39,PP3,PP1
    .=Y2 by XBOOLE_1:12,AS;
    rng p1 misses rng p2 by XBOOLE_1:79,PP1,PP3;
    then
     p3 is one-to-one by FINSEQ_3:98,PP1,PP3;
    then PP7: setopfunc(Y2,DX,REAL,F,addreal)
    =Sum(Func_Seq(F,p3)) by PP6,LMXY092A;
    QQQ: Func_Seq(F,p3)= Func_Seq(F,p1) ^ Func_Seq(F,p2) by LMXXX1;
    Sum(Func_Seq(F,p1)) + 0
    <= Sum(Func_Seq(F,p1)) + Sum(Func_Seq(F,p2)) by Q2,XREAL_1:8;
    hence thesis by PP1,RVSUM_1:105,QQQ,PP7;
  end;

theorem RANDOM113:
  for Omega be non empty finite set,
  P be Probability of Trivial-SigmaField (Omega),
  Y be non empty finite Subset of Omega,
  f be Function of Omega,REAL
  ex G being FinSequence of REAL, s being FinSequence of Y
  st
  len G = card (Y) & s is one-to-one
  & rng s = Y & len s = card (Y)
  & (for n being Nat st n in dom G holds G.n
  = (f.(s.n)) * P.{s.n})
  & Integral(P2M(P),f|Y) = Sum G
  proof
    let Omega be non empty finite set,
    P be Probability of Trivial-SigmaField (Omega),
    Y be non empty finite Subset of Omega,
    f be Function of Omega,REAL;
    set YN= Omega \ Y;
    ADRYN:
    dom (YN --> 0) =YN & rng (YN --> 0) c=REAL by FUNCOP_1:19;
    ADFRY:
    dom (f +* (YN --> 0))= dom f \/ dom (YN --> 0) by FUNCT_4:def 1
    .=Omega \/ YN by FUNCT_2:def 1,ADRYN
    .=Omega by XBOOLE_1:12;
    rng (f +* (YN --> 0)) c= REAL;
    then reconsider g= f +* (YN --> 0) as Function of Omega,REAL
    by ADFRY,FUNCT_2:4;
    consider G being
    FinSequence of REAL, s being FinSequence of Omega such that
    P1: len G = card (Omega) & s is one-to-one
    & rng s = Omega & len s = card (Omega)
    & (for n being Nat st n in dom G
    holds G.n = g.(s.n) * P.{s.n})
    & Integral(P2M(P),g) = Sum G by RANDOM_1:13;
    reconsider g as Real-Valued-Random-Variable
    of Trivial-SigmaField (Omega) by RANDOM_1:29;
    consider E be Element of Trivial-SigmaField (Omega);
    DD20a:dom g = Omega by FUNCT_2:def 1;
    g is_integrable_on P by RANDOM_1:30; then
    DD3: g is_integrable_on P2M(P) by RANDOM_1:def 3;
    DD3a: Y misses YN by XBOOLE_1:79;
    DD4: Integral(P2M(P),g|(Y \/ YN)) =
    Integral(P2M(P),g|Y) + Integral(P2M(P),g|YN)
    by DD3,MESFUNC6:92,XBOOLE_1:79;
    Y \/ YN = Y \/ Omega by XBOOLE_1:39
    .= Omega by XBOOLE_1:12;
    then DD5: g|(Y \/ YN) = g by FUNCT_2:40;
    DD6: g|Y = (f|Y) by FUNCT_4:76,ADRYN,DD3a;
    Q20: dom (g|YN) = YN by RELAT_1:91,DD20a;
    Q20A: for x be set st x in dom (g|YN) holds (g|YN).x = 0
    proof
      let x be set;
      assume AQ20aaa:x in dom (g|YN);
      then AQ20a:x in YN & (g|YN).x = g.x by RELAT_1:91,DD20a,FUNCT_1:70;
      g.x=(YN --> 0).x by FUNCT_4:14,AQ20aaa,ADRYN,Q20;
      hence thesis by AQ20a,FUNCOP_1:13;
    end;
    then
    Integral(P2M(P),g|YN) = R_EAL(0) * (P2M(P)).(YN) by MESFUNC6:97,Q20
    .= R_EAL(0);
    then
    P2: Integral(P2M(P),g) = Integral(P2M P,(f|Y)) by DD6,XXREAL_3:4,DD4,DD5;
    set N1= s" Y;
    s is Function of dom s, rng s by FUNCT_2:3;then
    N1 is finite Subset of dom s by FUNCT_2:47;then
    reconsider N1 as finite Subset of Seg (len G)
    by P1,FINSEQ_1:def 3;
    rng (canFS N1) c= N1 by FINSEQ_1:def 4;
    then
    rng (canFS N1) c= Seg (len G) by XBOOLE_1:1;
    then
    FSCANFS1:canFS(N1) is FinSequence of Seg (len G) by FINSEQ_1:def 4;
    dom s = Seg len G by FINSEQ_1:def 3,P1;
    then
    SF0:s is Function of Seg (len G),Omega by FUNCT_2:3,P1;
    then reconsider t1= s*canFS(N1)
    as FinSequence of Omega by FINSEQ_2:36,FSCANFS1;
    RNGT1C:rng t1 = s.:(rng canFS N1) by RELAT_1:160
    .=s.: N1 by FUNCT_2:def 3
    .=Y by P1,FUNCT_1:147;
    set N2= Seg (len G) \ N1;
    reconsider N2 as finite Subset of Seg len G by XBOOLE_1:36;
    rng (canFS(N2)) c= N2 by FINSEQ_1:def 4;
    then rng (canFS(N2)) c= Seg len G by XBOOLE_1:1;
    then
    canFS N2 is FinSequence of Seg len G by FINSEQ_1:def 4;
    then
    reconsider t2= s*canFS(N2) as FinSequence of Omega by SF0,FINSEQ_2:36;
    reconsider t=t1 ^ t2 as FinSequence of Omega;
    MISS1P: rng (canFS(N1)) =N1 by FUNCT_2:def 3;
    MISS2P:rng (canFS(N2)) =N2 by FUNCT_2:def 3;
    CDT1: N1 is finite Subset of (dom s) &
    N2 is finite Subset of (dom s) by FINSEQ_1:def 3,P1;
    then rng (s|N1) misses rng (s|N2) by TRNG01,P1,XBOOLE_1:79;
    then rng t1 misses rng (s|N2) by XBOOLE_1:63,MISS1P,TRNG02;
    then MISS3:rng t1 misses rng t2 by XBOOLE_1:63,MISS2P,TRNG02;
    then P3: t is one-to-one by FINSEQ_3:98,P1;
    len (canFS(N1)) = card N1 by UPROOTS:5;then
    DD1:dom (canFS(N1))= Seg(card N1) by FINSEQ_1:def 3;
    rng (canFS(N1)) is Subset of dom s by CDT1,FUNCT_2:def 3;then
    dom t1 = Seg(card N1) by DD1,RELAT_1:46;then
    DD1L:len t1 = card N1 by FINSEQ_1:def 3;
    len (canFS(N2)) = card N2 by UPROOTS:5;then
    DD2:dom canFS N2 = Seg card N2 by FINSEQ_1:def 3;
    rng (canFS(N2)) is Subset of dom s by CDT1,FUNCT_2:def 3;then
    dom t2 = Seg(card N2) by DD2,RELAT_1:46;then
    DD2L:len t2 = card N2 by FINSEQ_1:def 3;
    DDD:N1 \/ N2 = (Seg len G) \/ N1 by XBOOLE_1:39
    .= Seg len G by XBOOLE_1:12
    .= dom s by FINSEQ_1:def 3,P1;
    dom t = Seg (len t1 + len t2) by FINSEQ_1:def 7
    .=Seg(card (N1 \/ N2)) by CARD_2:53,XBOOLE_1:79,DD1L,DD2L;
    then
    DDDDZ:len t = card (N1 \/ N2) by FINSEQ_1:def 3;
    then
    DDD0:len t = len s by CARD_1:104,DDD;
    DDD1:Seg len s = Seg len t by DDDDZ,CARD_1:104,DDD;
    P5: dom s = Seg(len s) by FINSEQ_1:def 3
    .=dom t by FINSEQ_1:def 3,DDD1;
    DDD2:card (dom t) = card Omega by P1,DDD0,CARD_1:104;
    t is Function of dom t, Omega by FINSEQ_2:30;then
    rng s = rng t by P1,P3,FINSEQ_4:78,DDD2; then
    consider PN being Permutation of dom s such that
    P6: t = s*PN & dom PN = dom s & rng PN = dom s by BHSP_5:1,P1,P3;
    defpred FF1[set,set] means
    ( t.$1 in Y implies $2 = (f.(t.$1)) * P.{t.$1}) &
    ( (not t.$1 in Y) implies $2 = (g.(t.$1)) * P.{t.$1});
    P7A:now let k be Nat;
    assume k in Seg card Omega;
    now per cases;
      suppose t.k in Y;
        hence ex x being Element of REAL st FF1[k,x];
      end;
      suppose not t.k in Y;
        hence ex x being Element of REAL st FF1[k,x];
      end;
    end;
    hence ex x being Element of REAL st FF1[k,x];
  end;
  consider F be FinSequence of REAL such that
  P7B: dom F = Seg card (Omega) and
  P7C: for n be Nat st n in Seg card (Omega)
  holds FF1[n,F.n] from FINSEQ_1:sch 5(P7A);
  XV0: dom s = Seg (len G) by FINSEQ_1:def 3,P1
  .= dom G by FINSEQ_1:def 3;
  then XV2: dom(G*PN) = dom PN by RELAT_1:46,P6;
  XV3: dom F =dom s by P1,FINSEQ_1:def 3,P7B;
  now let x be set;
    assume XV4: x in dom F;
    then reconsider nx=x as Element of NAT;
    YV1: nx in dom t by XV4,P1,FINSEQ_1:def 3,P7B,P5;
    YV2:t.nx =s.(PN.nx) by P6,FUNCT_1:22,XV4,XV3,P5;
    PN.nx in dom G by XV0,FUNCT_1:21,YV1,P6; then
    YV5: G.(PN.nx) = g.(s.(PN.nx)) * P.{s.(PN.nx)} by P1;
    now per cases;
      suppose C1:t.nx in Y;
        hence F.nx = (f.(t.nx)) * P.{t.nx} by P7C,P7B,XV4
        .= ((f|Y).(s.(PN.nx))) * P.{s.(PN.nx)} by YV2,C1,FUNCT_1:72
        .= (g.(s.(PN.nx))) * P.{s.(PN.nx)} by C1,YV2,FUNCT_1:72,DD6
        .= (G*PN).nx by FUNCT_1:23,XV3,XV4,P6,YV5;
      end;
      suppose not t.nx in Y;
        hence F.nx = (g.(s.(PN.nx))) * P.{s.(PN.nx)} by YV2,P7C,P7B,XV4
        .= (G*PN).nx by FUNCT_1:23,XV3,XV4,P6,YV5;
      end;
    end;
    hence F.x=(G*PN).x;
  end;
  then
  F=G*PN by P6,XV2,XV3,FUNCT_1:9;
  then P9: Sum G = Sum F by XV0,FINSOP_1:8;
  reconsider t1 as FinSequence of Y by RNGT1C,FINSEQ_1:def 4;
  reconsider F1= F |(len t1) as FinSequence of REAL;
  reconsider F2= F /^(len t1) as FinSequence of REAL;
  P10: F = F1^F2 by RFINSEQ:21;
  A2: len t1=card(Y) by RNGT1C,FINSEQ_4:77,P1;
  P98: len F = card (Omega) by P7B,FINSEQ_1:def 3;
  P201:len F = len t by DDD0,P1,P7B,FINSEQ_1:def 3;
  P200: len t= len t1 + len t2 by FINSEQ_1:35;
  then
  P203:len t1 <= len t by NAT_1:11;
  then
  P202: len F2 = len F - len t1 by P201,RFINSEQ:def 2
  .= len t2 by P200,P201;
  then P205: dom F2 = Seg len t2 by FINSEQ_1:def 3
  .= dom ((len t2) |->0) by FUNCOP_1:19;
  now let m be Nat;
    assume P300: m in dom F2;
    then P302:m in Seg len t2 by P202,FINSEQ_1:def 3;
    then
    P303: m in dom t2 by FINSEQ_1:def 3;
    LLL1: 1 <= m & m <= len t2 by P302,FINSEQ_1:3;
    m <= m+len t1 by NAT_1:11;
    then
    1<= m+len t1 & m+len t1 <= len t by P200,XREAL_1:8,XXREAL_0:2,LLL1;
    then
    YV20: m+len t1 in Seg card (Omega) by P1,DDD0;
    YV21: t.(m+len t1) = t2.m by FINSEQ_1:def 7,P303;
    MISS4: rng t1 /\ rng t2 = {} by XBOOLE_0:def 7,MISS3;
    P305: t2.m in rng t2 by FUNCT_1:12,P303;
    then
     not t2.m in rng t1 by MISS4,XBOOLE_0:def 4;
    then P308: t2.m in dom (g|YN) by Q20,XBOOLE_0:def 5,RNGT1C,P305;
    then P309: g.(t2.m) = (g|YN).(t2.m) by FUNCT_1:70
    .= 0 by Q20A,P308;
     not t.(m+len t1) in Y by RNGT1C,MISS4,XBOOLE_0:def 4,P305,YV21;
    then
C10:F.(m+len t1) = (g.(t.(m+len t1))) * P.{t.(m+len t1)} by P7C,YV20
    .= 0 by P309,YV21;
    thus F2.m = F.(m+len t1) by P201,P203,RFINSEQ:def 2,P300
    .= ((len t2) |->0).m by P302,FINSEQ_2:71,C10;
  end; then
  P11: F2 = ((len t2) |->0) by P205,FINSEQ_1:17;
  card Y c= card Omega by CARD_1:27; then
  P101: card(Y) <= card Omega by NAT_1:40;
  P12: Sum(F) = Sum F1 + Sum F2 by RVSUM_1:105,P10
  .= Sum F1 + 0 by P11,RVSUM_1:111
  .= Sum F1;
  A1: len F1 = card Y by FINSEQ_1:80,P98,A2,P101;
   for n being Nat st n in dom F1 holds F1.n = (f.(t1.n)) * P.{t1.n}
  proof
    let n be Nat;
    assume n in dom F1; then
    A43: n in Seg (len t1) by A1,A2,FINSEQ_1:def 3; then
    A45:n in dom t1 by FINSEQ_1:def 3;
    then
    A44: t.n=t1.n by FINSEQ_1:def 7;
    then
    A42: t.n in Y by RNGT1C,A45,FUNCT_1:12;
    A77P:Seg (len t1) c= Seg card(Omega) by FINSEQ_1:7,A2,P101;
    thus F1.n = F.n by A43,FUNCT_1:72
    .= (f.(t1.n)) * P.{t1.n} by A44,A77P,A43,P7C,A42;
  end;
  hence thesis by A1,A2,RNGT1C,P12,P9,P1,P2;
end;

LMXY10A:
for Omega1, Omega2 be non empty finite set, P1 be Probability of
Trivial-SigmaField (Omega1), P2 be Probability of
Trivial-SigmaField (Omega2),
Q be Function of [:Omega1,Omega2:],REAL,
P be Function of (bool [:Omega1,Omega2:]),REAL,
Y1 be non empty finite Subset of Omega1,
Y2 be non empty finite Subset of Omega2 st
(for x,y be set st x in Omega1 & y in Omega2
holds Q.(x,y) = (P1.{x})*(P2.{y}) ) &
(for z be finite Subset of [:Omega1,Omega2:]
holds P.z = setopfunc(z,[:Omega1,Omega2:],REAL,Q,addreal)) holds
P.([:Y1,Y2:]) = (P1.Y1)*(P2.Y2)
proof
  let Omega1, Omega2 be non empty finite set, P1 be Probability of
  Trivial-SigmaField (Omega1), P2 be Probability of
  Trivial-SigmaField (Omega2),
  Q be Function of [:Omega1,Omega2:],REAL,
  P be Function of (bool [:Omega1,Omega2:]),REAL,
  Y1 be non empty finite Subset of Omega1,
  Y2 be non empty finite Subset of Omega2;
  assume AS:
  (for x,y be set st x in Omega1 & y in Omega2
  holds Q.(x,y) = (P1.{x})*(P2.{y}) ) &
  (for z be finite Subset of [:Omega1,Omega2:]
  holds P.z = setopfunc(z,[:Omega1,Omega2:],REAL,Q,addreal));
  deffunc FF1(set)=P1.{$1};
  P01: for x be set st x in Y1 holds FF1(x) in REAL;
  consider F1 being Function of Y1,REAL such that
  P11: for x be set st x in Y1 holds F1.x = FF1(x) from FUNCT_2:sch 2(P01);
  deffunc FF2(set)=P2.{$1};
  P02: for x be set st x in Y2 holds FF2(x) in REAL;
  consider F2 being Function of Y2,REAL such that
  P21: for x be set st x in Y2 holds F2.x = FF2(x) from FUNCT_2:sch 2(P02);
  now let x be set;
    assume x in {{},1};
    then x = 0 or x = 1 by TARSKI:def 2;
    hence x in REAL;
  end; then
  HMY:{{},1} c=REAL by TARSKI:def 3;
  HMY1:dom chi(Y1,Omega1) = Omega1 &
  rng chi(Y1,Omega1) c= {{},1} by FUNCT_3:def 3,FUNCT_3:48;then
  chi(Y1,Omega1) is Function of Omega1,{{},1}
  by FUNCT_2:def 1,RELSET_1:11; then
  reconsider f1= chi(Y1,Omega1) as Function of Omega1,REAL
  by FUNCT_2:9,HMY;
  Q20: dom (f1|Y1) = dom f1 /\ Y1 by RELAT_1:90
  .=Y1 by XBOOLE_1:28,HMY1;
  for x be set st x in dom (f1|Y1) holds (f1|Y1).x = 1
  proof
    let x be set;
    assume ASQ201: x in dom (f1|Y1);
    then (f1|Y1).x = f1.x by FUNCT_1:70
    .= 1 by ASQ201,Q20,FUNCT_3:def 3;
    hence thesis;
  end;
  then Q30:Integral(P2M(P1),f1|Y1) = R_EAL(1) * (P2M(P1)).(dom (f1|Y1))
  by MESFUNC6:97
  .= 1 * (P1.Y1) by EXTREAL1:13,Q20
  .= P1.Y1;
  consider G1 being FinSequence of REAL, s1 being FinSequence of Y1
  such that
  R1: len G1 = card (Y1) & s1 is one-to-one & rng s1 = Y1 & len s1 = card (Y1)
  & (for n being Nat st n in dom G1 holds G1.n
  = (f1.(s1.n)) * P1.{s1.n})
  & Integral(P2M(P1),f1|Y1) = Sum G1 by RANDOM113;
  Y1 c= Y1; then
  reconsider YY1=Y1 as finite Subset of Y1;
  dom F1=Y1 by FUNCT_2:def 1; then
  XV2: dom(F1*s1) = dom s1 by RELAT_1:46,R1;
  XV3: dom G1 = Seg len s1 by R1,FINSEQ_1:def 3
  .=dom s1 by FINSEQ_1:def 3;
  now let x be set;
    assume XV4: x in dom G1;
    then reconsider nx=x as Element of NAT;
    XV6:s1.nx in Y1 by R1,XV3,XV4,FUNCT_1:12;
    thus G1.x = (f1.(s1.nx)) * P1.{s1.nx} by R1,XV4
    .= 1*P1.{s1.nx} by XV6,FUNCT_3:def 3
    .= F1.(s1.nx) by P11,R1,XV3,XV4,FUNCT_1:12
    .=(F1*s1).x by FUNCT_1:23,XV3,XV4;
  end;
  then
  G1=Func_Seq(F1,s1) by XV2,XV3,FUNCT_1:9;
  then
  R4:setopfunc(YY1,Y1,REAL,F1,addreal) =Sum G1 by R1,LMXY092A;
  HMY2:dom chi(Y2,Omega2) = Omega2 &
  rng chi(Y2,Omega2) c= {{},1} by FUNCT_3:def 3,FUNCT_3:48;then
  chi(Y2,Omega2) is Function of Omega2,{{},1} by FUNCT_2:def 1,RELSET_1:11;
  then reconsider f2=chi(Y2,Omega2)
  as Function of Omega2,REAL by FUNCT_2:9,HMY;
  Q20: dom (f2|Y2) = dom f2 /\ Y2 by RELAT_1:90
  .=Y2 by XBOOLE_1:28,HMY2;
  for x be set st x in dom (f2|Y2) holds (f2|Y2).x = 1
  proof
    let x be set;
    assume ASQ201: x in dom (f2|Y2);
    then (f2|Y2).x = f2.x by FUNCT_1:70
    .= 1 by ASQ201,Q20,FUNCT_3:def 3;
    hence thesis;
  end;
  then
  Q3:Integral(P2M(P2),f2|Y2) = R_EAL(1) * (P2M(P2)).Y2 by MESFUNC6:97,Q20
  .= 1 * (P2.Y2) by EXTREAL1:13
  .= P2.Y2;
  consider G2 being FinSequence of REAL, s2 being FinSequence of Y2
  such that
  RR1: len G2 = card (Y2) & s2 is one-to-one & rng s2 = Y2 & len s2 = card (Y2)
  & (for n being Nat st n in dom G2 holds G2.n
  = (f2.(s2.n)) * P2.{s2.n})
  & Integral(P2M(P2),f2|Y2) = Sum G2 by RANDOM113;
  Y2 c= Y2; then
  reconsider YY2=Y2 as finite Subset of Y2;
  dom F2=Y2 by FUNCT_2:def 1; then
  XV2: dom(F2*s2) = dom s2 by RELAT_1:46,RR1;
  XV3: dom G2= Seg (len s2) by RR1,FINSEQ_1:def 3
  .=dom s2 by FINSEQ_1:def 3;
  now let x be set;
    assume XV4: x in dom G2; then
    reconsider nx=x as Element of NAT;
    XV6:s2.nx in Y2 by RR1,XV3,XV4,FUNCT_1:12;
    thus G2.x = (f2.(s2.nx)) * P2.{s2.nx} by RR1,XV4
    .= 1*P2.{s2.nx} by XV6,FUNCT_3:def 3
    .= F2.(s2.nx) by P21,RR1,XV3,XV4,FUNCT_1:12
    .=(F2*s2).x by FUNCT_1:23,XV3,XV4;
  end; then
  G2 = Func_Seq(F2,s2) by XV2,XV3,FUNCT_1:9; then
  S4:setopfunc(YY2,Y2,REAL,F2,addreal) =Sum G2 by RR1,LMXY092A;
  reconsider Y3= [:Y1,Y2:] as finite Subset of [:Y1,Y2:]
  by ZFMISC_1:119;
  reconsider Y33= [:Y1,Y2:] as finite Subset of [:Omega1,Omega2:]
  by ZFMISC_1:119;
  Y1Y2O1O2: [:Y1,Y2:] c= [:Omega1,Omega2:] by ZFMISC_1:119;
  then
  reconsider Q0=Q|[:Y1,Y2:] as Function of [:Y1,Y2:],REAL by FUNCT_2:38;
  T1: now let x,y be set;
  assume PXY0: x in Y1 & y in Y2; then
  [x,y] in [:Y1,Y2:] by ZFMISC_1:def 2;
  then [x,y] in dom Q0 by FUNCT_2:def 1;
  hence Q0.(x,y) =Q.(x,y) by FUNCT_1:70
  .= (P1.{x})*(P2.{y}) by AS,PXY0
  .= (F1.x)*(P2.{y}) by PXY0,P11
  .= (F1.x)*(F2.y) by PXY0,P21;
end;
Y3 c= dom Q0 by FUNCT_2:def 1;
then
consider pp1 being FinSequence of [:Y1,Y2:] such that
DY1: pp1 is one-to-one & rng pp1 = Y3 & setopfunc(Y3,[:Y1,Y2:],REAL,Q0,addreal)
= addreal "**" Func_Seq(Q0,pp1) by BHSP_5:def 5;
CRPP1:rng pp1 c= [:Omega1,Omega2:] by XBOOLE_1:1,Y1Y2O1O2;
then reconsider pp2=pp1
as FinSequence of [:Omega1,Omega2:] by FINSEQ_1:def 4;
rng pp1 c= dom Q by CRPP1,FUNCT_2:def 1;
then ZZ1: dom(Q*pp1)= dom pp1 by RELAT_1:46;
DQQQQP: dom Q0=[:Y1,Y2:] by FUNCT_2:def 1;
for x be set st x in dom (Q0*pp1) holds (Q0*pp1).x=(Q*pp1).x
proof
  let x be set;
  assume x in dom (Q0*pp1);
  then ASDQ0:(Q0*pp1).x =Q0.(pp1.x) &x in dom pp1 by FUNCT_1:21,FUNCT_1:22;
  then (pp1.x) in rng pp1 by FUNCT_1:12;
  then Q0.(pp1.x)=Q.(pp1.x) by FUNCT_1:72;
  hence thesis by ASDQ0,FUNCT_1:23;
end;then
DY2: Func_Seq(Q0,pp1) = Func_Seq(Q,pp2)
by FUNCT_1:9,DQQQQP,ZZ1,CRPP1,RELAT_1:46;
 dom Q=[:Omega1,Omega2:] by FUNCT_2:def 1;
then DY4:
setopfunc(Y3,[:Y1,Y2:],REAL,Q0,addreal)
= setopfunc(Y33,[:Omega1,Omega2:],REAL,Q,addreal)
by BHSP_5:def 5,DY1,DY2;
thus P.([:Y1,Y2:] ) = setopfunc(Y33,[:Omega1,Omega2:],REAL,Q,addreal) by AS
.= (P1.Y1)*(P2.Y2) by Q3,RR1,Q30,R1,S4,R4,LMXY090,T1,DY4;
end;

LMXY10:
for Omega1, Omega2 be non empty finite set,
    P1 be Probability of Trivial-SigmaField Omega1,
    P2 be Probability of Trivial-SigmaField Omega2,
    Q be Function of [:Omega1,Omega2:],REAL,
    P be Function of (bool [:Omega1,Omega2:]),REAL st
(for x,y be set st x in Omega1 & y in Omega2
holds Q.(x,y) = (P1.{x})*(P2.{y}) ) &
(for z be finite Subset of [:Omega1,Omega2:]
holds P.z = setopfunc(z,[:Omega1,Omega2:],REAL,Q,addreal)) holds
P.([:Omega1,Omega2:]) = 1
proof
  let Omega1, Omega2 be non empty finite set, P1 be Probability of
  Trivial-SigmaField (Omega1), P2 be Probability of
  Trivial-SigmaField (Omega2),
  Q be Function of [:Omega1,Omega2:],REAL,
  P be Function of (bool [:Omega1,Omega2:]),REAL;
  assume AS:
  (for x,y be set st x in Omega1 & y in Omega2
  holds Q.(x,y) = (P1.{x})*(P2.{y}) ) &
  (for z be finite Subset of [:Omega1,Omega2:]
  holds P.z = setopfunc(z,[:Omega1,Omega2:],REAL,Q,addreal));
  deffunc FF1(set)=P1.{$1};
  P01: for x be set st x in Omega1 holds FF1(x) in REAL;
  consider F1 being Function of Omega1,REAL such that
  P11: for x be set st x in Omega1 holds F1.x = FF1(x) from FUNCT_2:sch 2(P01);
  deffunc FF2(set)=P2.{$1};
  P02: for x be set st x in Omega2 holds FF2(x) in REAL;
  consider F2 being Function of Omega2,REAL such that
  P21: for x be set st x in Omega2 holds F2.x = FF2(x) from FUNCT_2:sch 2(P02);
  Q20f1:dom (Omega1-->1) = Omega1 by FUNCOP_1:19;
  rng (Omega1-->1) c=REAL; then
  reconsider f1=(Omega1-->1) as Function of Omega1,REAL by Q20f1,FUNCT_2:4;
  for x be set st x in dom (Omega1-->1) holds (Omega1-->1).x = 1
  by FUNCOP_1:13; then
  Q3:Integral(P2M(P1),f1) = R_EAL(1) * ((P2M(P1)).(Omega1))
  by Q20f1,MESFUNC6:97
  .=1 *(P1.(Omega1)) by EXTREAL1:13
  .= 1 by PROB_1:def 13;
  consider G1 being FinSequence of REAL,
           s1 being FinSequence of Omega1 such that
  R1: len G1 = card (Omega1) & s1 is one-to-one
  & rng s1 = Omega1 & len s1 = card (Omega1)
  & (for n being Nat st n in dom G1 holds G1.n
  = (f1.(s1.n)) * P1.{s1.n})
  & Integral(P2M(P1),f1) = Sum G1 by RANDOM_1:13;
  Omega1 c= Omega1; then
  reconsider Y1=Omega1 as finite Subset of Omega1;
  dom F1=Y1 by FUNCT_2:def 1; then
  XV2: dom(F1*s1) = dom s1 by RELAT_1:46,R1;
  XV3: dom G1= Seg (len s1) by R1,FINSEQ_1:def 3
  .=dom s1 by FINSEQ_1:def 3;
  now let x be set;
    assume XV4: x in dom G1; then
    reconsider nx=x as Element of NAT;
    thus G1.x = (f1.(s1.nx)) * P1.{s1.nx} by R1,XV4
    .= 1*P1.{s1.nx} by R1,XV3,XV4,FUNCT_1:12,FUNCOP_1:13
    .= F1.(s1.nx) by P11,R1,XV3,XV4,FUNCT_1:12
    .= (F1*s1).x by FUNCT_1:23,XV3,XV4;
  end; then
  G1=Func_Seq(F1,s1) by XV2,XV3,FUNCT_1:9; then
  R4:setopfunc(Y1,Omega1,REAL,F1,addreal)=1 by Q3,R1,LMXY092A;
  Q20:dom (Omega2-->1) = Omega2 by FUNCOP_1:19;
  rng (Omega2-->1) c= REAL;then
  reconsider f2=(Omega2-->1) as Function of Omega2,REAL by Q20,FUNCT_2:4;
  for x be set st x in dom (Omega2-->1) holds (Omega2-->1).x = 1
  by FUNCOP_1:13; then
  Q3:Integral(P2M(P2),f2) = R_EAL(1) * ((P2M(P2)).(Omega2)) by Q20,MESFUNC6:97
  .= 1 *(P2.(Omega2)) by EXTREAL1:13
  .= 1 by PROB_1:def 13;
  consider G2 being FinSequence of REAL, s2 being FinSequence of Omega2
  such that
  R1: len G2 = card (Omega2) & s2 is one-to-one
  & rng s2 = Omega2 & len s2 = card (Omega2)
  & (for n being Nat st n in dom G2 holds G2.n
  = (f2.(s2.n)) * P2.{s2.n})
  & Integral(P2M(P2),f2) = Sum G2 by RANDOM_1:13;
  Omega2 c= Omega2; then
  reconsider Y2=Omega2 as finite Subset of Omega2;
  dom F2=Y2 by FUNCT_2:def 1; then
  XV2: dom(F2*s2) = dom s2 by RELAT_1:46,R1;
  XV3: dom G2= Seg (len s2) by R1,FINSEQ_1:def 3
  .=dom s2 by FINSEQ_1:def 3;
  now let x be set;
    assume XV4: x in dom G2;
    then reconsider nx=x as Element of NAT;
    thus G2.x = (f2.(s2.nx)) * P2.{s2.nx} by R1,XV4
    .= 1*P2.{s2.nx} by R1,XV3,XV4,FUNCT_1:12,FUNCOP_1:13
    .= F2.(s2.nx) by P21,R1,XV3,XV4,FUNCT_1:12
    .=(F2*s2).x by FUNCT_1:23,XV3,XV4;
  end; then
  S4P:G2 =Func_Seq(F2,s2) by XV2,XV3,FUNCT_1:9;
  reconsider Y3= [:Y1,Y2:] as finite Subset of [:Omega1,Omega2:]
  by ZFMISC_1:119;
  T1: now let x,y be set;
  assume PXYP: x in Y1 & y in Y2;
  hence Q.(x,y)= (P1.{x})*(P2.{y}) by AS
  .= (F1.x)*(P2.{y}) by PXYP,P11
  .= (F1.x)*(F2.y) by PXYP,P21;
end;
thus P.([:Omega1,Omega2:] )
= setopfunc(Y3,[:Omega1,Omega2:],REAL,Q,addreal) by AS
.=setopfunc(Y1,Omega1,REAL,F1,addreal) *
setopfunc(Y2,Omega2,REAL,F2,addreal) by LMXY090,T1
.= 1 by S4P,Q3,R1,LMXY092A,R4;
end;

LMXY09:
for Omega1, Omega2 be non empty finite set,
    P1 be Probability of Trivial-SigmaField Omega1,
    P2 be Probability of Trivial-SigmaField Omega2,
    Q be Function of [:Omega1,Omega2:],REAL,
    P be Function of bool [:Omega1,Omega2:],REAL st
(for x,y be set st x in Omega1 & y in Omega2
holds Q.(x,y) = (P1.{x})*(P2.{y})) &
(for z be finite Subset of [:Omega1,Omega2:]
holds P.z = setopfunc(z,[:Omega1,Omega2:],REAL,Q,addreal)) holds
for x be set st x c= [:Omega1,Omega2:]
holds 0 <= P.x & P.x <= 1
proof
  let Omega1, Omega2 be non empty finite set, P1 be Probability of
  Trivial-SigmaField (Omega1), P2 be Probability of
  Trivial-SigmaField (Omega2),
  Q be Function of [:Omega1,Omega2:],REAL,
  P be Function of (bool [:Omega1,Omega2:]),REAL;
  assume AS:
  (for x,y be set st x in Omega1 & y in Omega2
  holds Q.(x,y) = (P1.{x})*(P2.{y}) ) &
  (for z be finite Subset of [:Omega1,Omega2:]
  holds P.z = setopfunc(z,[:Omega1,Omega2:],REAL,Q,addreal));
  reconsider Y12=[:Omega1,Omega2:] as
  finite Subset of [:Omega1,Omega2:] by SUBSET:3;
  A1:now let z be set;
  assume z in [:Omega1,Omega2:]; then
  consider x,y be set such that
  P11: x in Omega1 & y in Omega2 & z=[x,y] by ZFMISC_1:def 2;
  for xx be set st xx in {x} holds xx in Omega1 by P11,TARSKI:def 1;then
  EXY: {x} is Event of Trivial-SigmaField (Omega1)&
  for xx be set st xx in {y} holds xx in Omega2
  by P11,TARSKI:def 1,TARSKI:def 3;then
  EXY1:{y} is Event of Trivial-SigmaField (Omega2) by TARSKI:def 3;
  P12: Q.z=Q.(x,y) by P11
  .= (P1.{x})*(P2.{y}) by P11,AS;
  0 <= (P1.{x}) & 0 <= (P2.{y}) by EXY,EXY1,PROB_1:def 13;
  hence 0 <= Q.z by P12;
end;
let x be set;
assume x c= [:Omega1,Omega2:]; then
reconsider Y=x as finite Subset of [:Omega1,Omega2:];
for z be set st z in Y holds 0 <=Q.z by A1; then
0<= setopfunc(Y,[:Omega1,Omega2:],REAL,Q,addreal) by LMXY093;
hence 0<= P.x by AS;
A12: setopfunc(Y,[:Omega1,Omega2:],REAL,Q,addreal) <=
setopfunc(Y12,[:Omega1,Omega2:],REAL,Q,addreal) by A1,LMXY094;
setopfunc(Y12,[:Omega1,Omega2:],REAL,Q,addreal)=P.([:Omega1,Omega2:]) by AS;
then setopfunc(Y,[:Omega1,Omega2:],REAL,Q,addreal) <= 1 by A12,AS,LMXY10;
hence P.x <= 1 by AS;
end;

definition
  let Omega1, Omega2 be non empty finite set,
      P1 be Probability of Trivial-SigmaField Omega1,
      P2 be Probability of Trivial-SigmaField Omega2;
  func Product-Probability (Omega1,Omega2,P1,P2)
  -> Probability of Trivial-SigmaField ([:Omega1,Omega2:]) means :DefPRD:
  ex Q be Function of [:Omega1,Omega2:],REAL st
  (for x,y be set st x in Omega1 & y in Omega2
  holds Q.(x,y) = (P1.{x})*(P2.{y}) ) &
  (for z be finite Subset of [:Omega1,Omega2:]
  holds it.z = setopfunc(z,[:Omega1,Omega2:],REAL,Q,addreal));
  existence
  proof
    consider Q be Function of [:Omega1,Omega2:],REAL such that
P1: for x,y be set st x in Omega1 & y in Omega2
    holds Q.(x,y) = (P1.{x})*(P2.{y}) by LMXY04;
    consider P be Function of (bool [:Omega1,Omega2:]),REAL such that
P2: for z be finite Subset of [:Omega1,Omega2:]
    holds P.z = setopfunc(z,[:Omega1,Omega2:],REAL,Q,addreal) by LMXY07;
P3: for x be set st x c= [:Omega1,Omega2:]
    holds 0 <= P.x & P.x <= 1 by LMXY09,P1,P2;
 P.([:Omega1,Omega2:]) = 1 by P1,P2,LMXY10;
    then P is Probability of Trivial-SigmaField ([:Omega1,Omega2:])
      by P2,P3,LMXY03;
    hence thesis by P1,P2;
  end;
  uniqueness
  proof
    let PP1,PP2 be Probability of Trivial-SigmaField ([:Omega1,Omega2:]);
    assume
    A1: ex Q1 be Function of [:Omega1,Omega2:],REAL st
    (for x,y be set st x in Omega1 & y in Omega2
    holds Q1.(x,y) = (P1.{x})*(P2.{y}) ) &
    (for z be finite Subset of [:Omega1,Omega2:]
    holds PP1.z = setopfunc(z,[:Omega1,Omega2:],REAL,Q1,addreal));
    assume
    A2: ex Q2 be Function of [:Omega1,Omega2:],REAL st
    (for x,y be set st x in Omega1 & y in Omega2
    holds Q2.(x,y) = (P1.{x})*(P2.{y}) ) &
    (for z be finite Subset of [:Omega1,Omega2:]
    holds PP2.z = setopfunc(z,[:Omega1,Omega2:],REAL,Q2,addreal));
    consider Q1 be Function of [:Omega1,Omega2:],REAL such that
    A11: (for x,y be set st x in Omega1 & y in Omega2
    holds Q1.(x,y) = (P1.{x})*(P2.{y}) ) &
    (for z be finite Subset of [:Omega1,Omega2:]
    holds PP1.z = setopfunc(z,[:Omega1,Omega2:],REAL,Q1,addreal)) by A1;
    consider Q2 be Function of [:Omega1,Omega2:],REAL such that
    A12: (for x,y be set st x in Omega1 & y in Omega2
    holds Q2.(x,y) = (P1.{x})*(P2.{y}) ) &
    (for z be finite Subset of [:Omega1,Omega2:]
    holds PP2.z = setopfunc(z,[:Omega1,Omega2:],REAL,Q2,addreal)) by A2;
    Q1=Q2 by A11,A12, LMXY05;
    hence PP1=PP2 by LMXY08,A11,A12;
  end;
end;

theorem XX1:
  for Omega1, Omega2 be non empty finite set,
  P1 be Probability of Trivial-SigmaField Omega1,
  P2 be Probability of Trivial-SigmaField Omega2,
  Y1 be non empty finite Subset of Omega1,
  Y2 be non empty finite Subset of Omega2 holds
  Product-Probability (Omega1,Omega2,P1,P2).([:Y1,Y2:]) = (P1.Y1)*(P2.Y2)
  proof
    let Omega1, Omega2 be non empty finite set,
    P1 be Probability of Trivial-SigmaField (Omega1),
    P2 be Probability of Trivial-SigmaField (Omega2),
    Y1 be non empty finite Subset of Omega1,
    Y2 be non empty finite Subset of Omega2;
    set P=Product-Probability (Omega1,Omega2,P1,P2);
    consider Q be Function of [:Omega1,Omega2:],REAL such that
    A1: (for x,y be set st x in Omega1 & y in Omega2
    holds Q.(x,y) = (P1.{x})*(P2.{y}) ) &
    (for z be finite Subset of [:Omega1,Omega2:]
    holds P.z = setopfunc(z,[:Omega1,Omega2:],REAL,Q,addreal)) by DefPRD;
    thus P.([:Y1,Y2:]) = (P1.Y1)*(P2.Y2) by LMXY10A,A1;
  end;

theorem
  for Omega1, Omega2 be non empty finite set,
  P1 be Probability of Trivial-SigmaField (Omega1),
  P2 be Probability of Trivial-SigmaField (Omega2),
  y1, y2 be set st y1 in Omega1 & y2 in Omega2 holds
  Product-Probability (Omega1,Omega2,P1,P2).({[y1,y2]})
  = (P1.{y1})*(P2.{y2})
  proof
    let Omega1, Omega2 be non empty finite set,
    P1 be Probability of Trivial-SigmaField Omega1,
    P2 be Probability of Trivial-SigmaField Omega2,
    y1, y2 be set;
    assume
AS: y1 in Omega1 & y2 in Omega2; then
A1: {y1} is finite Subset of Omega1 by ZFMISC_1:37;
    for yy be set st yy in {y2} holds yy in Omega2 by AS,TARSKI:def 1;then
A2: {y2} is finite Subset of Omega2 by TARSKI:def 3;
    [:{y1},{y2}:] = {[y1,y2]} by ZFMISC_1:35;
    hence Product-Probability (Omega1,Omega2,P1,P2).({[y1,y2]})
    = (P1.{y1})*(P2.{y2}) by XX1,A1,A2;
  end;

begin :: Algebra of Real Valued Random Variables

definition
  let Omega be non empty set;
  let Sigma be SigmaField of Omega;
  func Real-Valued-Random-Variables-Set Sigma
    -> non empty Subset of RAlgebra Omega equals
  { f where f is Real-Valued-Random-Variable of Sigma : not contradiction };
  correctness
  proof
A1: { f where f
    is Real-Valued-Random-Variable of Sigma : not contradiction } is non empty
    proof
      consider g be Real-Valued-Random-Variable of Sigma;
      g in { f where f
      is Real-Valued-Random-Variable of Sigma : not contradiction };
      hence thesis;
    end;
    { f where f is Real-Valued-Random-Variable of Sigma :not contradiction }
    c= Funcs(Omega,REAL)
    proof
      let x be set;
      assume x in { f where f
      is Real-Valued-Random-Variable of Sigma : not contradiction };
      then consider f be Real-Valued-Random-Variable of Sigma such that
      A2: x=f;
      thus thesis by FUNCT_2:11,A2;
    end;
    hence thesis by A1;
  end;
end;

Th9:
  Real-Valued-Random-Variables-Set Sigma is additively-linearly-closed
  multiplicatively-closed
  proof
    set W = Real-Valued-Random-Variables-Set Sigma;
    set V = RAlgebra Omega;
A1: for v,u be Element of V st v in W & u in W holds v * u in W
    proof
      let v,u be Element of V such that
      A2: v in W and
      A3: u in W;
      consider u1 be Real-Valued-Random-Variable of Sigma such that
      A4: u1=u by A3;
      reconsider h = v*u as Element of Funcs(Omega,REAL);
      consider v1 be Real-Valued-Random-Variable of Sigma such that
      A6: v1=v by A2;
      dom v1 /\ dom u1 = Omega /\ dom u1 by FUNCT_2:def 1; then
      A9: (ex f being Function st h = f & dom f = Omega & rng f c= REAL) &
      dom v1 /\ dom u1 = Omega /\ Omega by FUNCT_2:def 1,def 2;
      for x be set st x in dom h holds h.x = v1.x *u1.x by A6,A4,FUNCSDOM:11;
      then v*u=v1(#)u1 by A9,VALUED_1:def 4;
      hence thesis;
    end;
    for v,u be Element of V st v in W & u in W holds v + u in W
    proof
      let v,u be Element of V such that
      A10: v in W and
      A11: u in W;
      consider u1 be Real-Valued-Random-Variable of Sigma such that
      A12: u1=u by A11;
      reconsider h = v+u as Element of Funcs(Omega,REAL);
      consider v1 be Real-Valued-Random-Variable of Sigma such that
      A14: v1=v by A10;
      dom v1 /\ dom u1 = Omega /\ dom u1 by FUNCT_2:def 1;
      then
      A17: (ex f being Function st h = f & dom f = Omega & rng f c= REAL)&
      dom v1 /\ dom u1 = Omega /\ Omega by FUNCT_2:def 1,def 2;
      for x be set st x in dom h holds h.x = v1.x + u1.x by A14,A12,FUNCSDOM:
      10;
      then v+u=v1+u1 by A17,VALUED_1:def 1;
      hence thesis;
    end; then
A18: W is add-closed by IDEAL_1:def 1;
A19: RAlgebra Omega is RealLinearSpace by C0SP1:7;
    for v be Element of V st v in W holds -v in W
    proof
      let v be Element of V;
      assume v in W; then
      consider v1 be Real-Valued-Random-Variable of Sigma such that
A20:  v1=v;
A201: -1 is Real by XREAL_0:def 1;
then A22:  (-v1) is Real-Valued-Random-Variable of Sigma by RANDOM_1:20;
      reconsider h = -v as Element of Funcs(Omega,REAL);
A23:  h=(-1)*v by A19,RLVECT_1:29;
A24:  now let x be set;
      assume x in dom h;
      then reconsider y=x as Element of Omega;
      thus h.x = (-1)*(v1.y) by A23,A20,A201,FUNCSDOM:15
      .= -v1.x;
    end;
    (ex f being Function st h = f & dom f = Omega & rng f c= REAL)
    & dom v1 = Omega by FUNCT_2:def 1,def 2;
    then -v=-v1 by A24,VALUED_1:9;
    hence thesis by A22;
  end; then
A25: W is having-inverse by C0SP1:def 1;
  for a be Real, u be Element of V st u in W holds a * u in W
  proof
    let a be Real, u be Element of V;
    assume u in W; then
    consider u1 be Real-Valued-Random-Variable of Sigma such that
    A26: u1=u;
    reconsider h = a*u as Element of Funcs(Omega,REAL);
    A29: (ex f being Function st h = f & dom f = Omega
    & rng f c= REAL) & dom u1 = Omega by FUNCT_2:def 1,def 2;
    for x be set st x in dom h holds h.x = a*(u1.x) by A26,FUNCSDOM:15;
    then a*u=a(#)u1 by A29,VALUED_1:def 5;
    hence thesis;
  end;
  hence Real-Valued-Random-Variables-Set Sigma
    is additively-linearly-closed by A18,A25,C0SP1:def 10;
  reconsider g = RealFuncUnit Omega as Function of Omega,REAL;
  g is Real-Valued-Random-Variable of Sigma by CHILM1;
  then 1.V in W;
  hence thesis by A1,C0SP1:def 4;
end;

registration let Omega,Sigma;
  cluster Real-Valued-Random-Variables-Set Sigma
    -> additively-linearly-closed multiplicatively-closed;
  coherence by Th9;
end;

definition
  let Omega,Sigma;
  func R_Algebra_of_Real-Valued-Random-Variables Sigma -> Algebra equals
  AlgebraStr (# Real-Valued-Random-Variables-Set Sigma,
     mult_(Real-Valued-Random-Variables-Set Sigma,RAlgebra Omega),
     Add_(Real-Valued-Random-Variables-Set Sigma,RAlgebra Omega),
     Mult_(Real-Valued-Random-Variables-Set Sigma,RAlgebra Omega),
     One_(Real-Valued-Random-Variables-Set Sigma,RAlgebra Omega),
     Zero_(Real-Valued-Random-Variables-Set Sigma,RAlgebra Omega) #);
  coherence by C0SP1:6;
end;

registration let Omega, Sigma;
  cluster R_Algebra_of_Real-Valued-Random-Variables Sigma -> scalar-unital;
  coherence
  proof
    let v be VECTOR of R_Algebra_of_Real-Valued-Random-Variables Sigma;
    reconsider v1=v as VECTOR of RAlgebra Omega by TARSKI:def 3;
    R_Algebra_of_Real-Valued-Random-Variables
      Sigma is Subalgebra of RAlgebra Omega by C0SP1:6;
    then 1 * v = 1*v1 by C0SP1:8;
    hence 1 * v = v by FUNCSDOM:23;
  end;
end;
