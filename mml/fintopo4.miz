:: Continuous Mappings between Finite and One-Dimensional Finite Topological
:: Spaces
::  by Hiroshi Imura , Masami Tanaka and Yatsuka Nakamura
::
:: Received July 13, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies FINSEQ_1, BOOLE, FUNCT_1, RELAT_1, ARYTM_1, RELAT_2, FIN_TOPO,
      CONNSP_1, SQUARE_1, FINTOPO3, FINTOPO4;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, XCMPLX_0, XREAL_0, NAT_1,
      STRUCT_0, RELAT_1, FUNCT_1, FUNCT_2, FINSEQ_1, FIN_TOPO, FINTOPO3,
      BINARITH, ENUMSET1, LIMFUNC1, RFUNCT_3;
 constructors NAT_1, CQC_LANG, DOMAIN_1, GROUP_1, FINSEQ_4, MEMBERED, XBOOLE_0,
      FIN_TOPO, FINTOPO3, SQUARE_1, BINARITH, ENUMSET1, LIMFUNC1, RFUNCT_3;
 registrations SUBSET_1, RELSET_1, STRUCT_0, NAT_1, XREAL_0, XBOOLE_0,
      FIN_TOPO;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI;
 theorems TARSKI, FINSEQ_1, NAT_1, AXIOMS, REAL_1, FUNCT_1, FUNCT_2, XBOOLE_0,
      XBOOLE_1, FIN_TOPO, FINTOPO3, RELAT_1, SQUARE_1, BINARITH, ENUMSET1,
      GOBOARD9, STRUCT_0;
 schemes NAT_1, FUNCT_2;

begin

definition
  let FT be non empty FT_Space_Str,A,B be Subset of FT;
  pred A,B are_separated means
:Def1: A^b misses B & A misses (B^b);
end;

theorem Th1:
  for FT being filled (non empty FT_Space_Str),
      A being Subset of FT, n,m being Nat st
    n<=m holds Finf(A,n) c= Finf(A,m)
proof
  let FT be filled (non empty FT_Space_Str), A be Subset of FT, n,m be Nat;
  assume n<=m;then A1: m-n>=0 by SQUARE_1:12;
  defpred P[Nat] means Finf(A,n) c= Finf(A,n+$1);
A2: P[0];
A3: for k being Nat st P[k] holds P[k+1]
  proof
    let k be Nat;
    assume A4:P[k];
    Finf(A,n+k) c= Finf(A,n+k+1) by FINTOPO3:37;
    hence P[k+1] by A4,XBOOLE_1:1;
  end;
A5: for m2 being Nat holds P[m2] from NAT_1:sch 1(A2,A3);
  n+(m-'n)=n+(m-n) by A1,BINARITH:def 3
         .=m;
  hence Finf(A,n) c= Finf(A,m) by A5;
end;

theorem
  for FT being filled (non empty FT_Space_Str),
      A being Subset of FT, n,m being Nat st
    n<=m holds Fcl(A,n) c= Fcl(A,m)
proof
  let FT be filled (non empty FT_Space_Str),A be Subset of FT, n,m be Nat;
  assume n<=m;then
A1: m-n>=0 by SQUARE_1:12;
  defpred P[Nat] means Fcl(A,n) c= Fcl(A,n+$1);
A2: P[0];
A3: for k being Nat st P[k] holds P[k+1]
  proof
    let k be Nat;
    assume P[k];then
A4: Fcl(A,n) c= Fcl(A,n+k);
    Fcl(A,n+k) c= Fcl(A,n+k+1) by FINTOPO3:25;
    hence P[k+1] by A4,XBOOLE_1:1;
  end;
  for m2 being Nat holds P[m2] from NAT_1:sch 1(A2,A3);then
A5: P[m-'n];
  n+(m-'n)=n+(m-n) by A1,BINARITH:def 3
         .=m;
  hence Fcl(A,n) c= Fcl(A,m) by A5;
end;

theorem
  for FT being filled (non empty FT_Space_Str),
      A being Subset of FT, n,m being Nat st
    n<=m holds Fdfl(A,m) c= Fdfl(A,n)
proof
  let FT be filled (non empty FT_Space_Str),A be Subset of FT, n,m be Nat;
  assume n<=m;then
A1: m-n>=0 by SQUARE_1:12;
  defpred P[Nat] means Fdfl(A,n+$1) c= Fdfl(A,n);
A2: P[0];
A3: for k being Nat st P[k] holds P[k+1]
  proof let k be Nat;
    assume P[k];then
A4: Fdfl(A,n+k) c= Fdfl(A,n);
    Fdfl(A,n+k+1) c= Fdfl(A,n+k) by FINTOPO3:44;
    hence P[k+1] by A4,XBOOLE_1:1;
  end;
  for m2 being Nat holds P[m2] from NAT_1:sch 1(A2,A3);then
A5: P[m-'n];
  n+(m-'n)=n+(m-n) by A1,BINARITH:def 3
         .=m;
  hence Fdfl(A,m) c= Fdfl(A,n) by A5;
end;

theorem
  for FT being filled (non empty FT_Space_Str), A being Subset of FT,
      n,m being Nat st
    n<=m holds Fint(A,m) c= Fint(A,n)
proof
  let FT be filled (non empty FT_Space_Str),A be Subset of FT, n,m be Nat;
  assume n<=m;then
A1: m-n>=0 by SQUARE_1:12;
  defpred P[Nat] means Fint(A,n+$1) c= Fint(A,n);
A2: P[0];
A3: for k being Nat st P[k] holds P[k+1]
  proof
    let k be Nat;
    assume P[k];then
A4: Fint(A,n+k) c= Fint(A,n);
    Fint(A,n+k+1) c= Fint(A,n+k) by FINTOPO3:26;
    hence P[k+1] by A4,XBOOLE_1:1;
  end;
  for m2 being Nat holds P[m2] from NAT_1:sch 1(A2,A3);then
A5: P[m-'n];
  n+(m-'n)=n+(m-n) by A1,BINARITH:def 3
         .=m;
  hence Fint(A,m) c= Fint(A,n) by A5;
end;

theorem
  for FT being non empty FT_Space_Str,A,B being Subset of FT st
    A,B are_separated holds B,A are_separated
proof
  let FT be non empty FT_Space_Str,A,B be Subset of FT;
  assume A,B are_separated;then
  A^b misses B & A misses (B^b) by Def1;
  hence B,A are_separated by Def1;
end;

theorem Th6:
  for FT being filled (non empty FT_Space_Str),
      A,B being Subset of FT st
    A,B are_separated holds A misses B
proof
  let FT be filled (non empty FT_Space_Str),
  A,B be Subset of FT;
  assume A,B are_separated;then
A1: A^b misses B & A misses (B^b) by Def1;
  A c= A^b by FIN_TOPO:18;
  hence A misses B by A1,XBOOLE_1:63;
end;

theorem
  for FT being non empty FT_Space_Str,
      A,B being Subset of FT st
    FT is symmetric holds
    A,B are_separated iff A^f misses B & A misses (B^f)
proof
  let FT be non empty FT_Space_Str,
  A,B be Subset of FT;
  assume A1: FT is symmetric;then
A2: A^b=A^f by FIN_TOPO:17;
    B^b=B^f by A1,FIN_TOPO:17;
  hence thesis by A2,Def1;
end;

theorem Th8:
  for FT being filled (non empty FT_Space_Str),
      A,B being Subset of FT st
    FT is symmetric & A^b misses B holds A misses (B^b)
proof
  let FT be filled (non empty FT_Space_Str),
  A,B be Subset of FT;
  assume A1: FT is symmetric & A^b misses B;
  now assume A2: A meets (B^b);
    consider x being set such that
A3: x in A & x in B^b by A2,XBOOLE_0:3;
A4: A^b= {x2 where x2 is Element of FT:U_FT x2 meets A}
                                        by FIN_TOPO:def 10;
    B^b= {x2 where x2 is Element of FT:U_FT x2 meets B}
                                    by FIN_TOPO:def 10;then
    consider x2 being Element of FT such that
A5: x2=x & U_FT x2 meets B by A3;
A6: U_FT x2 /\ B <>{} by A5,XBOOLE_0:def 7;
    consider y being Element of U_FT x2 /\ B;
A7: y in U_FT x2 & y in B by A6,XBOOLE_0:def 3;then
    reconsider y2=y as Element of FT;
    x2 in U_FT y2 by A1,A7,FIN_TOPO:def 14;then
    x2 in U_FT y2 /\ A by A3,A5,XBOOLE_0:def 3;then
    U_FT y2 meets A by XBOOLE_0:def 7;then
    y2 in A^b by A4;
    hence contradiction by A1,A7,XBOOLE_0:3;
  end;
  hence thesis;
end;

theorem Th9:
  for FT being filled (non empty FT_Space_Str),
      A,B being Subset of FT st
    FT is symmetric & A misses (B^b) holds A^b misses B by Th8;

theorem
  for FT being filled (non empty FT_Space_Str),
      A,B being Subset of FT st
    FT is symmetric holds
    A,B are_separated iff A^b misses B
proof
  let FT be filled (non empty FT_Space_Str),
  A,B be Subset of FT;
  assume A1: FT is symmetric;
  thus A,B are_separated implies A^b misses B by Def1;
  thus A^b misses B implies A,B are_separated
  proof assume A2: A^b misses B;then
    A misses (B^b) by A1,Th8;
    hence A,B are_separated by A2,Def1;
  end;
end;

theorem
  for FT being filled (non empty FT_Space_Str),
      A,B being Subset of FT st
    FT is symmetric holds
    A,B are_separated iff A misses (B^b)
proof
  let FT be filled (non empty FT_Space_Str),
  A,B be Subset of FT;
  assume A1: FT is symmetric;
  thus A,B are_separated implies A misses (B^b) by Def1;
  thus A misses (B^b) implies A,B are_separated
  proof
    assume A2: A misses (B^b);then
    A^b misses B by A1,Th9;
    hence A,B are_separated by A2,Def1;
  end;
end;

theorem Th12:
  for FT being filled (non empty FT_Space_Str),
      IT being Subset of FT st
    FT is symmetric holds IT is connected iff
   (for A, B being Subset of FT st
      IT = A \/ B & A,B are_separated holds
      A = IT or B = IT)
proof
  let FT be filled(non empty FT_Space_Str),IT be Subset of FT;
  assume A1: FT is symmetric;
A2: now assume IT is connected;then
A3: for A,B being Subset of FT st
      IT = A \/ B & A <> {} & B <> {} & A misses B holds
      A^b meets B by FIN_TOPO:def 17;
    thus
    (for A, B being Subset of FT st
       IT = A \/ B & A,B are_separated holds A = IT or B = IT)
    proof
      let A, B be Subset of FT;
      assume A4: IT = A \/ B & A,B are_separated;then
A5:   A misses B by Th6;
      now assume A6: A<>{};
        now assume B<>{};then
          A^b meets B by A3,A4,A5,A6;
          hence contradiction by A4,Def1;
        end;
        hence A = IT or B = IT by A4;
      end;
      hence A = IT or B = IT by A4;
    end;
  end;
  now assume
A7: (for A, B being Subset of FT st
       IT = A \/ B & A,B are_separated holds
       A = IT or B = IT);
    for A,B being Subset of FT st
      IT = A \/ B & A <> {} & B <> {} & A misses B holds
      A^b meets B
    proof
      let A,B be Subset of FT;
      assume A8: IT = A \/ B & A <> {} & B <> {} & A misses B;
      now assume A9:A^b misses B; then
A10:    A^b /\ B={} by XBOOLE_0:def 7;
        A c= A^b by FIN_TOPO:18;then
        A /\ B c= A^b /\ B by XBOOLE_1:26;then
A11:    A /\ B ={} by A10,XBOOLE_1:3;
        now assume A12: A meets (B^b);
          consider x being set such that
A13:      x in A & x in B^b by A12,XBOOLE_0:3;
A14:      A^b= {x2 where x2 is Element of FT:U_FT x2 meets A}
                                     by FIN_TOPO:def 10;
          B^b= {x2 where x2 is Element of FT:U_FT x2 meets B}
                                     by FIN_TOPO:def 10;then
          consider x2 being Element of FT such that
A15:      x2=x & U_FT x2 meets B by A13;
A16:      U_FT x2 /\ B <>{} by A15,XBOOLE_0:def 7;
          consider y being Element of U_FT x2 /\ B;
A17:      y in U_FT x2 & y in B by A16,XBOOLE_0:def 3;then
          reconsider y2=y as Element of FT;
          x2 in U_FT y2 by A1,A17,FIN_TOPO:def 14;then
          x2 in U_FT y2 /\ A by A13,A15,XBOOLE_0:def 3;then
          U_FT y2 meets A by XBOOLE_0:def 7;then
          y2 in A^b by A14;
          hence contradiction by A9,A17,XBOOLE_0:3;
        end;then
        A,B are_separated by A9,Def1;then
        A=IT or B=IT by A7,A8;
        hence contradiction by A8,A11,XBOOLE_1:21;
      end;
      hence A^b meets B;
    end;
    hence IT is connected by FIN_TOPO:def 17;
  end;
  hence thesis by A2;
end;

theorem
  for FT being filled (non empty FT_Space_Str),
      B being Subset of FT st FT is symmetric holds
    B is connected iff
    not (ex C being Subset of FT st
    C<>{} & B\C <>{} & C c= B & (C^b) misses (B\C))
proof
  let FT be filled (non empty FT_Space_Str),
  B be Subset of FT;
  assume A1: FT is symmetric;
  thus B is connected implies not (ex C being Subset of FT st
    C<>{} & B\C <>{} & C c= B & (C^b) misses (B\C))
  proof
    assume A2: B is connected;
    for C being Subset of FT st C c= B & (C^b) misses (B\C) holds
      C={} or B\C={}
    proof
      let C be Subset of FT;
      assume A3: C c= B & (C^b) misses (B\C);
A4:   C \/ (B\C)=C \/ B by XBOOLE_1:39
               .=B by A3,XBOOLE_1:12;
      C^b misses (B\C) & C misses ((B\C)^b) by A1,A3,Th8;then
      C,B\C are_separated by Def1;then
      C = B or B\C = B by A1,A2,A4,Th12;
      hence C={} or B\C={} by A3,XBOOLE_1:37,38;
    end;
    hence not (ex C being Subset of FT st C<>{} & B\C <>{} &
    C c= B & (C^b) misses (B\C));
  end;
  thus not (ex C being Subset of FT st C<>{} & B\C <>{} &
   C c= B & (C^b) misses (B\C)) implies B is connected
  proof
    assume not (ex C being Subset of FT st
      C<>{} & B\C <>{} & C c= B & (C^b) misses (B\C));then
A5: for C being Subset of FT st
      C c= B & (C^b) misses (B\C) holds C={} or B\C={};
    for A, B2 being Subset of FT st
      B = A \/ B2 & A,B2 are_separated holds
    A = B or B2 = B
    proof
      let A, B2 be Subset of FT;
      assume A6: B = A \/ B2 & A,B2 are_separated;then
A7:   A c= B by XBOOLE_1:7;
A8:   A^b misses B2 & A misses (B2^b) by A6,Def1;
A9:   (A \/ B2) \A =B2\A by XBOOLE_1:40;
      B\A c= B2 by A6,A9,XBOOLE_1:36;then
      A^b misses (B\A) by A8,XBOOLE_1:63;then
      A={} or B\A={} by A5,A7;then
      B=B2 or B c= A by A6,XBOOLE_1:37;
      hence A = B or B2 = B by A7,XBOOLE_0:def 10;
    end;
    hence B is connected by A1,Th12;
  end;
end;

definition
  let FT1,FT2 be non empty FT_Space_Str,
  f be Function of (the carrier of FT1),(the carrier of FT2),n be Nat;
  pred f is_continuous n means
:Def2: for x being Element of FT1,y being Element of FT2 st
   x in the carrier of FT1 & y=f.x holds
  f.:( U_FT(x,0)) c= U_FT(y,n);
end;

theorem
  for FT1 being non empty FT_Space_Str,
      FT2 being filled (non empty FT_Space_Str), n being Nat,
      f being Function of (the carrier of FT1),(the carrier of FT2) st
    f is_continuous 0 holds f is_continuous n
proof
  let FT1 be non empty FT_Space_Str,
  FT2 be filled (non empty FT_Space_Str),n be Nat,
  f be Function of (the carrier of FT1),(the carrier of FT2);
  assume f is_continuous 0;then
A1: for x being Element of FT1,y being Element of FT2 st
      x in the carrier of FT1 & y=f.x holds
      f.:( U_FT(x,0)) c= U_FT(y,0) by Def2;
  for x being Element of FT1,y being Element of FT2 st
      x in the carrier of FT1 & y=f.x holds
    f.:( U_FT(x,0)) c= U_FT(y,n)
  proof
    let x be Element of FT1,y be Element of FT2;
    assume A2: x in the carrier of FT1 & y=f.x;
A3: U_FT y =U_FT(y,0) by FINTOPO3:47;
    U_FT(y,n)=Finf((U_FT y),n) by FINTOPO3:def 10;then
A4: U_FT(y,0) c= U_FT(y,n) by A3,FINTOPO3:36;
    f.:( U_FT(x,0)) c= U_FT(y,0) by A1,A2;
    hence f.:( U_FT(x,0)) c= U_FT(y,n) by A4,XBOOLE_1:1;
  end;
  hence f is_continuous n by Def2;
end;

theorem
  for FT1 being non empty FT_Space_Str,
      FT2 being filled (non empty FT_Space_Str), n0,n being Nat,
      f being Function of (the carrier of FT1),(the carrier of FT2) st
    f is_continuous n0 & n0<=n holds f is_continuous n
proof
  let FT1 be non empty FT_Space_Str,
  FT2 be filled (non empty FT_Space_Str),n0,n be Nat,
  f be Function of (the carrier of FT1),(the carrier of FT2);
  assume A1: f is_continuous n0 & n0<=n;then
A2: for x being Element of FT1,y being Element of FT2 st
      x in the carrier of FT1 & y=f.x holds
   f.:( U_FT(x,0)) c= U_FT(y,n0) by Def2;
  for x being Element of FT1,y being Element of FT2 st
    x in the carrier of FT1 & y=f.x holds
    f.:( U_FT(x,0)) c= U_FT(y,n)
  proof
    let x be Element of FT1,y be Element of FT2;
    assume A3: x in the carrier of FT1 & y=f.x;
A4: U_FT(y,n0)=Finf((U_FT y),n0) by FINTOPO3:def 10;
    U_FT(y,n)=Finf((U_FT y),n) by FINTOPO3:def 10;then
A5: U_FT(y,n0) c= U_FT(y,n) by A1,A4,Th1;
    f.:( U_FT(x,0)) c= U_FT(y,n0) by A2,A3;
    hence f.:( U_FT(x,0)) c= U_FT(y,n) by A5,XBOOLE_1:1;
  end;
  hence f is_continuous n by Def2;
end;

theorem Th16:
  for FT1,FT2 being non empty FT_Space_Str,
      A being Subset of FT1,B being Subset of FT2,
      f being Function of (the carrier of FT1),(the carrier of FT2) st
    f is_continuous 0 & B=f.:A holds
    f.:(A^b) c= B^b
proof
  let FT1,FT2 be non empty FT_Space_Str,A be Subset of FT1,
  B be Subset of FT2,
  f be Function of (the carrier of FT1),(the carrier of FT2);
  assume A1: f is_continuous 0 & B=f.:A;
  thus f.:(A^b) c= B^b
  proof
    let y be set;assume y in f.:(A^b);then
    consider x being set such that
A2: x in dom f & x in A^b & y=f.x by FUNCT_1:def 12;
    reconsider x0=x as Element of FT1 by A2;
    reconsider y0=y as Element of FT2 by A2,FUNCT_2:7;
    x in {z where z is Element of FT1:U_FT z meets A}
                           by A2,FIN_TOPO:def 10;then
    consider z being Element of FT1 such that
A3: z=x & U_FT z meets A;
A4: U_FT x0 /\ A<>{} by A3,XBOOLE_0:def 7;
    f.:( U_FT(x0,0)) c= U_FT(y0,0) by A1,A2,Def2;then
    f.:(U_FT x0) c=  U_FT(y0,0) by FINTOPO3:47;then
A5: f.:(U_FT x0) c=  U_FT y0 by FINTOPO3:47;
    dom f=the carrier of FT1 by FUNCT_2:def 1;then
A6: f.:((U_FT x0) /\ A)<>{} by A4,RELAT_1:152;
A7: f.:((U_FT x0) /\ A) c= (f.:((U_FT x0)))/\ (f.:A) by RELAT_1:154;
    (f.:((U_FT x0)))/\ (f.:A) c= (U_FT y0) /\ (f.:A) by A5,XBOOLE_1:26;then
    f.:((U_FT x0) /\ A) c= (U_FT y0) /\ (f.:A) by A7,XBOOLE_1:1;then
    (U_FT y0) /\ (f.:A)<>{} by A6,XBOOLE_1:3;then
    U_FT y0 meets B by A1,XBOOLE_0:def 7;then
    y in {z2 where z2 is Element of FT2:U_FT z2 meets B};
    hence y in B^b by FIN_TOPO:def 10;
  end;
end;

theorem
  for FT1,FT2 being non empty FT_Space_Str,A being Subset of FT1,
      B being Subset of FT2,
      f being Function of (the carrier of FT1),(the carrier of FT2) st
    A is connected & f is_continuous 0 & B=f.:A holds B is connected
proof
  let FT1,FT2 be non empty FT_Space_Str,A be Subset of FT1,
  B be Subset of FT2,
  f be Function of (the carrier of FT1),(the carrier of FT2);
  assume A1: A is connected & f is_continuous 0 & B=f.:A;then
A2: for B1,C1 being Subset of FT1 st
    A = B1 \/ C1 & B1 <> {} & C1 <> {} & B1 misses C1 holds
   B1^b meets C1 by FIN_TOPO:def 17;
  for B2,C2 being Subset of FT2 st
    B = B2 \/ C2 & B2 <> {} & C2 <> {} & B2 misses C2 holds
   B2^b meets C2
  proof let B2,C2 be Subset of FT2;
    assume A3: B = B2 \/ C2 & B2 <> {} & C2 <> {} & B2 misses C2;
    reconsider B1 = f"B2 as Subset of FT1;
A4: f.:B1 c= B2
    proof
      let y be set;
      assume y in f.:B1;then
      consider x2 being set such that
A5:   x2 in dom f & x2 in B1 & y=f.x2 by FUNCT_1:def 12;
      thus y in B2 by A5,FUNCT_1:def 13;
    end;
    reconsider B20 = f.:B1 as Subset of FT2;
A6: B20^b c= B2^b by A4,FIN_TOPO:19;
    reconsider B10=B1/\A as Subset of FT1;
    reconsider C1=f"C2 as Subset of FT1;
    reconsider C10=C1 /\ A as Subset of FT1;
    A c= the carrier of FT1;then
A7: A c= dom f by FUNCT_2:def 1;
    (f"B2) \/ (f"C2)=f"(f.:A) by A1,A3,RELAT_1:175;then
    A c= B1 \/ C1 by A7,FUNCT_1:146;then
    A c= A/\(B1\/C1) by XBOOLE_1:19;then
A8: A c= B10 \/ C10 by XBOOLE_1:23;
A9: B10 c= A by XBOOLE_1:17;
    C10 c= A by XBOOLE_1:17;then
    B10 \/ C10 c= A by A9,XBOOLE_1:8;then
A10: A=B10 \/ C10 by A8,XBOOLE_0:def 10;
A11: B10 c= B1 by XBOOLE_1:17;
A12: C10 c= C1 by XBOOLE_1:17;then
A13: B10/\C10 c= B1/\C1 by A11,XBOOLE_1:27;
    consider x5 being Element of B2;
A14: x5 in B2 by A3;
    x5 in B by A3,XBOOLE_0:def 2;then
    consider z5 being set such that
A15: z5 in dom f & z5 in A & x5=f.z5 by A1,FUNCT_1:def 12;
    z5 in f"B2 by A14,A15,FUNCT_1:def 13;then
A16: B10<>{} by A15,XBOOLE_0:def 3;
    consider x6 being Element of C2;
A17: x6 in C2 by A3;
    x6 in B by A3,XBOOLE_0:def 2;then
    consider z6 being set such that
A18: z6 in dom f & z6 in A & x6=f.z6 by A1,FUNCT_1:def 12;
    z6 in f"C2 by A17,A18,FUNCT_1:def 13;then
A19: C10<>{} by A18,XBOOLE_0:def 3;
    B2 /\ C2 = {} by A3,XBOOLE_0:def 7;then
    f"(B2 /\ C2) = {} by RELAT_1:171;then
    (f"B2) /\ (f"C2) = {} by FUNCT_1:137;then
    B10 /\ C10= {} by A13,XBOOLE_1:3;then
    B10 misses C10 by XBOOLE_0:def 7;then
    B10^b meets C10 by A2,A10,A16,A19;then
A20: B10^b /\ C10 <> {} by XBOOLE_0:def 7;
    B10^b c= B1^b by A11,FIN_TOPO:19;then
    B10^b /\ C10 c= B1^b /\ C1 by A12,XBOOLE_1:27;then
A21: B1^b /\ C1 <> {} by A20,XBOOLE_1:3;
    dom f=the carrier of FT1 by FUNCT_2:def 1;then
A22: f.:(B1^b /\ C1) <> {} by A21,RELAT_1:152;
    f.:(B1^b /\ C1) c= (f.:(B1^b)) /\ f.:(C1) by RELAT_1:154;then
A23: (f.:(B1^b)) /\ (f.:C1) <> {} by A22,XBOOLE_1:3;
    f.:C1 c= C2 by FUNCT_1:145;then
A24: (f.:(B1^b)) /\ (f.:C1) c= (f.:(B1^b)) /\ C2 by XBOOLE_1:26;
    f.:(B1^b) c= B20^b by A1,Th16;then
    f.:(B1^b) c= B2^b by A6,XBOOLE_1:1;then
    (f.:(B1^b)) /\ C2 c= B2^b /\ C2 by XBOOLE_1:26;then
    (f.:(B1^b)) /\ (f.:C1) c= B2^b /\ C2 by A24,XBOOLE_1:1;then
    B2^b /\ C2 <>{} by A23,XBOOLE_1:3;
    hence B2^b meets C2 by XBOOLE_0:def 7;
  end;
  hence B is connected by FIN_TOPO:def 17;
end;

::1 dimensional linear FT_Str
definition let n be Nat;
  func Nbdl1 n -> Function of Seg n,bool (Seg n) means
:Def3: dom it =Seg n & for i being Nat st
   i in Seg n holds it.i={i,max(i-'1,1),min(i+1,n)};
existence
  proof
    defpred P[set,set] means for i being Nat st i=$1 holds
    $2={i,max(i-'1,1),min(i+1,n)};
A1: for x being set st x in Seg n
        ex y being set st y in bool (Seg n) & P[x,y]
    proof
      let x being set;
      assume A2: x in Seg n;then
      reconsider i=x as Nat;
      n>0 by A2,FINSEQ_1:4,NAT_1:19;then
A3:   0+1<=n by NAT_1:38;
      set y0={i,max(i-'1,1),min(i+1,n)};
      y0 c=Seg n
      proof
        let z1 being set;
        assume z1 in y0;then
A4:     z1=i or z1=max(i-'1,1) or z1=min(i+1,n) by ENUMSET1:def 1;
A5:     now assume A6: z1=max(i-'1,1);
A7:       i-'1<=i by GOBOARD9:2;
          i<=n by A2,FINSEQ_1:3;then
          i-'1<=n by A7,AXIOMS:22;then
          1<= max(i-'1,1) & max(i-'1,1)<=n by A3,SQUARE_1:50;
          hence z1 in Seg n by A6,FINSEQ_1:3;
        end;
        now assume A8: z1=min(i+1,n);
          1<=i+1 by NAT_1:37;then
A9:      1<=min(i+1,n) by A3,SQUARE_1:39;
          min(i+1,n)<=n by SQUARE_1:35;
          hence z1 in Seg n by A8,A9,FINSEQ_1:3;
        end;
        hence z1 in Seg n by A2,A4,A5;
      end;then
A10:  y0 in bool (Seg n);
      P[x,y0];
      hence ex y being set st y in bool (Seg n) & P[x,y] by A10;
    end;
    consider f2 being Function of Seg n,bool(Seg n) such that
A11: for x3 being set st
    x3 in Seg n holds P[x3,f2.x3] from FUNCT_2:sch 1(A1);
A12: for i being Nat st i in Seg n holds
    f2.i={i,max(i-'1,1),min(i+1,n)} by A11;
    dom f2=Seg n by FUNCT_2:def 1;
    hence ex f being Function of Seg n,bool (Seg n) st
      dom f =Seg n & for i being Nat st i in Seg n holds
      f.i={i,max(i-'1,1),min(i+1,n)} by A12;
  end;
uniqueness
  proof
    thus for f1,f2 being Function of Seg n,bool (Seg n) st
     (dom f1 =Seg n & for i being Nat st i in Seg n holds
      f1.i={i,max(i-'1,1),min(i+1,n)})&
     (dom f2 =Seg n & for i being Nat st i in Seg n holds
      f2.i={i,max(i-'1,1),min(i+1,n)})
     holds f1=f2
    proof
      let f1,f2 be Function of Seg n,bool (Seg n);
      assume A13: (dom f1 =Seg n & for i being Nat st i in Seg n holds
      f1.i={i,max(i-'1,1),min(i+1,n)})&
      (dom f2 =Seg n & for i being Nat st i in Seg n holds
      f2.i={i,max(i-'1,1),min(i+1,n)});
      for x being set st x in dom f1 holds f1.x=f2.x
      proof
        let x be set;
        assume A14: x in dom f1;then
        reconsider i2=x as Nat by A13;
        f1.i2={i2,max(i2-'1,1),min(i2+1,n)} by A13,A14;
        hence f1.x=f2.x by A13,A14;
      end;
      hence f1=f2 by A13,FUNCT_1:9;
    end;
  end;
end;

definition let n be Nat;
 assume A1: n>0;
 func FTSL1 n -> non empty FT_Space_Str equals
:Def4:  FT_Space_Str(# Seg n,Nbdl1 n #);
correctness
  proof
    0+1<=n by A1,NAT_1:38;then
    Seg n<>{} by FINSEQ_1:3;
    hence FT_Space_Str(# Seg n,Nbdl1 n #) is non empty FT_Space_Str
                                                 by STRUCT_0:def 1;
  end;
end;

theorem
  for n being Nat st n>0 holds (FTSL1 n) is filled
proof
  let n be Nat;assume n>0;then
  A1: (FTSL1 n)=FT_Space_Str(# Seg n,Nbdl1 n #) by Def4;
  for x being Element of (FTSL1 n) holds x in U_FT x
  proof let x be Element of (FTSL1 n);
    x in Seg n by A1;then
    reconsider i=x as Nat;
    (the Nbd of (FTSL1 n)).x={i,max(i-'1,1),min(i+1,n)} by A1,Def3;then
    U_FT x= {i,max(i-'1,1),min(i+1,n)} by FIN_TOPO:def 1;
    hence x in U_FT x by ENUMSET1:def 1;
  end;
  hence (FTSL1 n) is filled by FIN_TOPO:def 3;
end;

theorem
  for n being Nat st n>0 holds (FTSL1 n) is symmetric
proof
  let n be Nat;
  assume n>0;then
A1: (FTSL1 n)=FT_Space_Str(# Seg n,Nbdl1 n #) by Def4;
  for x, y being Element of (FTSL1 n) holds
    y in U_FT x implies x in U_FT y
  proof
    let x, y be Element of (FTSL1 n);
A2: x in Seg n by A1;then
    reconsider i=x as Nat;
A3: 1<=i & i<=n by A2,FINSEQ_1:3;
    y in Seg n by A1;then
    reconsider j=y as Nat;
    (the Nbd of (FTSL1 n)).x={i,max(i-'1,1),min(i+1,n)} by A1,Def3;then
A4: U_FT x= {i,max(i-'1,1),min(i+1,n)} by FIN_TOPO:def 1;
    (the Nbd of (FTSL1 n)).y={j,max(j-'1,1),min(j+1,n)}
                                 by A1,Def3;then
A5: U_FT y= {j,max(j-'1,1),min(j+1,n)} by FIN_TOPO:def 1;
    now assume A6: y in U_FT x;then
A7: y=i or y=max(i-'1,1) or y=min(i+1,n) by A4,ENUMSET1:def 1;
A8: now assume A9: y=max(i-'1,1);
      now per cases;
        case A10: i-'1>=1;then
A11:      y=i-'1 by A9,SQUARE_1:def 2;
          now per cases;
            case i-1>=0;then
              j=i-1 by A11,BINARITH:def 3;
              hence x =j or x=max(j-'1,1) or x= min(j+1,n)
                                     by A3,SQUARE_1:def 1;
              end;
              case i-1<0;then i-'1=0 by BINARITH:def 3;
                hence contradiction by A10;
              end;
            end;
            hence x =j or x=max(j-'1,1) or x= min(j+1,n);
          end;
          case A12: i-'1<1;then
A13:        y=1 by A9,SQUARE_1:def 2;
            now assume i>1;then
A14:          i-1>0 by SQUARE_1:11;then
              i-'1=i-1 by BINARITH:def 3;then
              i-'1>=0+1 by A14,NAT_1:38;
              hence contradiction by A12;
            end;
            hence x =j or x=max(j-'1,1) or x= min(j+1,n) by A3,A13,AXIOMS:21;
          end;
        end;
        hence x in U_FT y by A5,ENUMSET1:def 1;
      end;
      now assume y=min(i+1,n);
A15:    y=i or y=max(i-'1,1) or y=min(i+1,n) by A4,A6,ENUMSET1:def 1;
        now per cases by A15;
          case y=i;
            hence x =j or x=max(j-'1,1) or x= min(j+1,n);
          end;
          case A16: y=max(i-'1,1);
            now per cases;
              case A17: i-'1>=1;then
A18:            y=i-'1 by A16,SQUARE_1:def 2;
                now per cases;
                  case i-1>=0;then
                   j=i-1 by A18,BINARITH:def 3;
                   hence x =j or x=max(j-'1,1) or x= min(j+1,n)
                                           by A3,SQUARE_1:def 1;
                  end;
                  case i-1<0;then i-'1=0 by BINARITH:def 3;
                    hence contradiction by A17;
                  end;
                end;
                hence x =j or x=max(j-'1,1) or x= min(j+1,n);
              end;
              case A19: i-'1<1;then
A20:            y=1 by A16,SQUARE_1:def 2;
                now assume i>1;then
A21:               i-1>0 by SQUARE_1:11;then
                  i-'1=i-1 by BINARITH:def 3;then
                  i-'1>=0+1 by A21,NAT_1:38;
                  hence contradiction by A19;
                end;
                hence x =j or x=max(j-'1,1) or x= min(j+1,n)
                                           by A3,A20,AXIOMS:21;
              end;
            end;
            hence x =j or x=max(j-'1,1) or x= min(j+1,n);
          end;
          case A22: y=min(i+1,n);
            now per cases;
              case i+1<=n;then
                y=i+1 by A22,SQUARE_1:def 1;then
A23:            j-1=i;then
                j-1>0 by A3;then
A24:            j-1=j-'1 by BINARITH:def 3;
                now per cases;
                  case j-1>=1;
                    hence x =j or x=max(j-'1,1) or x= min(j+1,n)
                                    by A23,A24,SQUARE_1:def 2;
                  end;
                  case j-1<1;
                    hence contradiction by A3,A23;
                  end;
                end;
                hence x =j or x=max(j-'1,1) or x= min(j+1,n);
              end;
              case A25: i+1>n;then
                y=n by A22,SQUARE_1:def 1;then
                j+1>n by NAT_1:38;then
A26:            min(j+1,n)=n by SQUARE_1:def 1;
                i>=n by A25,NAT_1:38;
                hence x =j or x=max(j-'1,1) or x= min(j+1,n)
                                           by A3,A26,AXIOMS:21;
              end;
            end;
            hence x =j or x=max(j-'1,1) or x= min(j+1,n);
          end;
        end;
        hence x in U_FT y by A5,ENUMSET1:def 1;
      end;
      hence x in U_FT y by A6,A7,A8;
    end;
    hence y in U_FT x implies x in U_FT y;
  end;
  hence (FTSL1 n) is symmetric by FIN_TOPO:def 14;
end;

::1 dimensional cyclic FT_Str
definition let n be Nat;
  func Nbdc1 n -> Function of Seg n,bool (Seg n) means
:Def5: dom it =Seg n & for i being Nat st i in Seg n holds
   (1<i & i<n implies it.i={i,i-'1,i+1})&
   (i=1 & i<n implies it.i={i,n,i+1}) &
   (1<i & i=n implies it.i={i,i-'1,1}) &
   (i=1 & i=n implies it.i={i});
existence
  proof
    defpred P[set,set] means for i being Nat st i=$1 holds
    (1<i & i<n implies $2={i,i-'1,i+1})&
    (i=1 & i<n implies $2={i,n,i+1}) &
    (1<i & i=n implies $2={i,i-'1,1}) &
    (i=1 & i=n implies $2={i});
A1: for x being set st x in Seg n ex y being set st y in bool (Seg n) & P[x,y]
  proof
    let x be set;
    assume A2: x in Seg n;then
    reconsider i2=x as Nat;
A3: 1<=i2 & i2<=n by A2,FINSEQ_1:3;then
A4: 1<i2 or i2=1 by REAL_1:def 5;
A5: i2<n or i2=n by A3,REAL_1:def 5;
    now per cases by A4,A5;
      case A6: 1<i2 & i2<n;
        set y0={i2,i2-'1,i2+1};
        i2-1>0 by A6,SQUARE_1:11;then
        i2-'1>0 by BINARITH:def 3;then
A7:     i2-'1>=0+1 by NAT_1:38;
        i2-'1<=i2 by GOBOARD9:2;then
        i2-'1<n by A6,AXIOMS:22;then
A8:     i2-'1 in Seg n by A7,FINSEQ_1:3;
A9:     i2+1<=n by A6,NAT_1:38;
        i2<=i2+1 by NAT_1:37;then
        1<i2+1 by A6,AXIOMS:22;then
A10:    i2+1 in Seg n by A9,FINSEQ_1:3;
        y0 c= Seg n
        proof let z1 being set;
          assume z1 in y0;
          hence z1 in Seg n by A2,A8,A10,ENUMSET1:def 1;
        end;then
A11:    y0 in bool (Seg n);
        P[x,y0] by A6;
        hence ex y being set st y in bool (Seg n) & P[x,y] by A11;
      end;
      case A12: i2=1 & i2<n;
        set y0={i2,n,i2+1};
A13:    n in Seg n by A12,FINSEQ_1:3;
A14:    i2+1<=n by A12,NAT_1:38;
A15:    i2+1 in Seg n by A12,A14,FINSEQ_1:3;
        y0 c= Seg n
        proof let z1 being set;
          assume z1 in y0;
          hence z1 in Seg n by A2,A13,A15,ENUMSET1:def 1;
        end;then
A16:    y0 in bool (Seg n);
        P[x,y0] by A12;
        hence ex y being set st y in bool (Seg n) & P[x,y] by A16;
      end;
      case A17: 1<i2 & i2=n;
        set y0={i2,i2-'1,1};
        i2-1>0 by A17,SQUARE_1:11;then
        i2-'1>0 by BINARITH:def 3;then
A18:    i2-'1>=0+1 by NAT_1:38;
        i2-'1<=n by A17,GOBOARD9:2;then
A19:    i2-'1 in Seg n by A18,FINSEQ_1:3;
A20:    1 in Seg n by A17,FINSEQ_1:3;
        y0 c= Seg n
        proof
          let z1 being set;
          assume z1 in y0;
          hence z1 in Seg n by A2,A19,A20,ENUMSET1:def 1;
        end;then
A21:    y0 in bool (Seg n);
        P[x,y0] by A17;
        hence ex y being set st y in bool (Seg n) & P[x,y] by A21;
      end;
      case A22: i2=1 & i2=n;
        set y0={i2};
        y0 c= Seg n
        proof
          let z1 be set;
          assume z1 in y0;
          hence z1 in Seg n by A2,TARSKI:def 1;
        end;then
A23:    y0 in bool (Seg n);
        P[x,y0] by A22;
        hence ex y being set st y in bool (Seg n) & P[x,y] by A23;
      end;
    end;
    hence ex y being set st y in bool (Seg n) & P[x,y];
  end;
  consider f2 being Function of Seg n,bool(Seg n) such that
A24: for x3 being set st
    x3 in Seg n holds P[x3,f2.x3] from FUNCT_2:sch 1(A1);
A25: for i being Nat st i in Seg n holds
   (1<i & i<n implies f2.i={i,i-'1,i+1})&
   (i=1 & i<n implies f2.i={i,n,i+1}) &
   (1<i & i=n implies f2.i={i,i-'1,1}) &
   (i=1 & i=n implies f2.i={i}) by A24;
  dom f2=Seg n by FUNCT_2:def 1;
  hence ex f being Function of Seg n,bool (Seg n) st
   dom f =Seg n & for i being Nat st i in Seg n holds
   (1<i & i<n implies f.i={i,i-'1,i+1})&
   (i=1 & i<n implies f.i={i,n,i+1}) &
   (1<i & i=n implies f.i={i,i-'1,1}) &
   (i=1 & i=n implies f.i={i}) by A25;
  end;
uniqueness
  proof
    let f1,f2 be Function of Seg n,bool (Seg n);
    assume A26:
    (dom f1 =Seg n & for i being Nat st i in Seg n holds
     (1<i & i<n implies f1.i={i,i-'1,i+1})&
     (i=1 & i<n implies f1.i={i,n,i+1}) &
     (1<i & i=n implies f1.i={i,i-'1,1}) &
     (i=1 & i=n implies f1.i={i}))&
    (dom f2 =Seg n & for i being Nat st i in Seg n holds
     (1<i & i<n implies f2.i={i,i-'1,i+1})&
     (i=1 & i<n implies f2.i={i,n,i+1}) &
     (1<i & i=n implies f2.i={i,i-'1,1}) &
     (i=1 & i=n implies f2.i={i}));
    for x being set st x in dom f1 holds f1.x=f2.x
    proof
      let x be set;
      assume A27: x in dom f1;then
      reconsider i2=x as Nat by A26;
A28:  1<=i2 & i2<=n by A26,A27,FINSEQ_1:3;then
A29:  1<i2 or i2=1 by REAL_1:def 5;
A30:  i2<n or i2=n by A28,REAL_1:def 5;
      now per cases by A29,A30;
        case A31: 1<i2 & i2<n;
          set y0={i2,i2-'1,i2+1};
          f1.x=y0 by A26,A27,A31;
          hence f1.x=f2.x by A26,A27,A31;
        end;
        case A32: i2=1 & i2<n;
          set y0={i2,n,i2+1};
          f1.x=y0 by A26,A27,A32;
          hence f1.x=f2.x by A26,A27,A32;
        end;
        case A33: 1<i2 & i2=n;
          set y0={i2,i2-'1,1};
          f1.x=y0 by A26,A27,A33;
          hence f1.x=f2.x by A26,A27,A33;
        end;
        case A34: i2=1 & i2=n;
          set y0={i2};
          f1.x=y0 by A26,A27,A34;
          hence f1.x=f2.x by A26,A27,A34;
        end;
      end;
      hence f1.x=f2.x;
    end;
    hence f1=f2 by A26,FUNCT_1:9;
  end;
end;

definition let n be Nat;
  assume A1: n>0;
  func FTSC1 n -> non empty FT_Space_Str equals
:Def6: FT_Space_Str(# Seg n,Nbdc1 n #);
correctness
  proof
    0+1<=n by A1,NAT_1:38;then
    Seg n<>{} by FINSEQ_1:3;
    hence FT_Space_Str(# Seg n,Nbdc1 n #) is non empty FT_Space_Str
                                              by STRUCT_0:def 1;
  end;
end;

theorem
  for n being Nat st n>0 holds (FTSC1 n) is filled
proof
  let n be Nat;assume n>0;then
A1: (FTSC1 n)=FT_Space_Str(# Seg n,Nbdc1 n #) by Def6;
  for x being Element of (FTSC1 n) holds x in U_FT x
  proof
    let x be Element of (FTSC1 n);
A2: x in Seg n by A1;then
    reconsider i=x as Nat;
A3: the Nbd of (FTSC1 n)=Nbdc1 n by A1;
    set f=Nbdc1 n;
A4: 1<=i & i<=n by A2,FINSEQ_1:3;then
A5: 1<i or i=1 by REAL_1:def 5;
A6: i<n or i=n by A4,REAL_1:def 5;
A7: (1<i & i<n implies f.i={i,i-'1,i+1})&
    (i=1 & i<n implies f.i={i,n,i+1}) &
    (1<i & i=n implies f.i={i,i-'1,1}) &
    (i=1 & i=n implies f.i={i}) by A2,Def5;
    now per cases by A5,A6;
      case 1<i & i<n;then
        U_FT x= {i,i-'1,i+1} by A3,A7,FIN_TOPO:def 1;
        hence x in U_FT x by ENUMSET1:def 1;
      end;
      case i=1 & i<n;then
        U_FT x= {i,n,i+1} by A3,A7,FIN_TOPO:def 1;
        hence x in U_FT x by ENUMSET1:def 1;
      end;
      case 1<i & i=n;then
        U_FT x= {i,i-'1,1} by A3,A7,FIN_TOPO:def 1;
        hence x in U_FT x by ENUMSET1:def 1;
      end;
      case i=1 & i=n;then
        U_FT x= {i} by A3,A7,FIN_TOPO:def 1;
        hence x in U_FT x by TARSKI:def 1;
      end;
    end;
    hence x in U_FT x;
  end;
  hence (FTSC1 n) is filled by FIN_TOPO:def 3;
end;

theorem
  for n being Nat st n>0 holds (FTSC1 n) is symmetric
proof
  let n be Nat;
  assume n>0;then
A1: (FTSC1 n)=FT_Space_Str(# Seg n,Nbdc1 n #) by Def6;
  for x, y being Element of (FTSC1 n) holds
    y in U_FT x implies x in U_FT y
  proof
    let x, y be Element of (FTSC1 n);
A2: x in Seg n by A1;then
    reconsider i=x as Nat;
A3: y in Seg n by A1;then
    reconsider j=y as Nat;
A4: 1<=j & j<=n by A3,FINSEQ_1:3;
A5: the Nbd of (FTSC1 n)=Nbdc1 n by A1;
    set f=Nbdc1 n;
A6: 1<=i & i<=n by A2,FINSEQ_1:3;then
A7: 1<i or i=1 by REAL_1:def 5;
A8: i<n or i=n by A6,REAL_1:def 5;
A9: (1<i & i<n implies f.i={i,i-'1,i+1})&
    (i=1 & i<n implies f.i={i,n,i+1}) &
    (1<i & i=n implies f.i={i,i-'1,1}) &
    (i=1 & i=n implies f.i={i}) by A2,Def5;
    now assume A10: y in U_FT x;
      now per cases by A7,A8;
        case A11: 1<i & i<n;then
          U_FT x= {i,i-'1,i+1} by A5,A9,FIN_TOPO:def 1;then
A12:      y=i or y=i-'1 or y=i+1 by A10,ENUMSET1:def 1;
A13:      now assume A14: y=i-'1;
            i-1>0 by A11,SQUARE_1:11;then
            i-'1=i-1 by BINARITH:def 3;then
A15:        j+1=i by A14;
A16:        x=j or x=j-'1 or x=j+1 by A15;
            now per cases by A16;
              case A17: x=j;then
                (the Nbd of (FTSC1 n)).y={j,j-'1,j+1}
                            by A3,A5,A11,Def5;then
                U_FT y = {j,j-'1,j+1} by FIN_TOPO:def 1;
                hence x in U_FT y by A17,ENUMSET1:def 1;
              end;
              case x=j-'1;then
A18:            i=(i-'1)-'1 by A14;
                now assume i<>0;then i>0 by NAT_1:19;then
A19:              i>=0+1 by NAT_1:38;then
                  i-1>=1-1 by REAL_1:49;then
A20:              i-1=i-'1 by BINARITH:def 3;
                  now assume A21: i=1;then
A22:                i-1=0;
                    i-'1-1<0 by A20,A22;
                    hence contradiction by A18,A21,BINARITH:def 3;
                  end;then
                  i>1 by A19,REAL_1:def 5;then
                  i-1>1-1 by REAL_1:54;then
                  i-'1>=0+1 by A20,NAT_1:38;then
                  i-'1-1>=0 by SQUARE_1:12;then
                  i-'1-'1=i-'1-1 by BINARITH:def 3;then
                  i+1=i-1 by A18,A20;then
A23:              i+1+1=i;
                  i<i+1 by NAT_1:38;
                  hence contradiction by A23,NAT_1:38;
                end;
                hence x in U_FT y by A11;
              end;
              case x=j+1;then
                i>0 by NAT_1:19;then
                i>=0+1 by NAT_1:38;then
                i-1>=0 by SQUARE_1:12;then
                y=i-1 by A14,BINARITH:def 3;then
A24:            j+1=x;then
A25:            j<n by A6,NAT_1:38;
A26:            now assume j=1;then
                  (the Nbd of (FTSC1 n)).y={j,n,j+1}
                           by A3,A5,A25,Def5;then
                  U_FT y = {j,n,j+1} by FIN_TOPO:def 1;
                  hence x in U_FT y by A24,ENUMSET1:def 1;
                end;
                now assume j<>1;then
                  j>1 by A4,REAL_1:def 5;then
                  (the Nbd of (FTSC1 n)).y={j,j-'1,j+1}
                              by A3,A5,A25,Def5;then
                  U_FT y = {j,j-'1,j+1} by FIN_TOPO:def 1;
                  hence x in U_FT y by A24,ENUMSET1:def 1;
                end;
                hence x in U_FT y by A26;
              end;
            end;
            hence x in U_FT y;
          end;
          now assume
A27:        y=i+1;then
A28:        j-1=x;then
            j-1>0 by A11;then
A29:        x=j or x=j-'1 or x=j+1 by A28,BINARITH:def 3;
            now per cases by A29;
              case A30: x=j;then
                (the Nbd of (FTSC1 n)).y={j,j-'1,j+1}
                          by A3,A5,A11,Def5;then
                U_FT y = {j,j-'1,j+1} by FIN_TOPO:def 1;
                hence x in U_FT y by A30,ENUMSET1:def 1;
              end;
              case A31: x=j-'1;
                now assume j=1;then
                  j-1=0;then
                  x=0 by A31,BINARITH:def 3;
                  hence contradiction by A11;
                end;then
A32:            j>1 by A4,REAL_1:def 5;
A33:            now assume j=n;then
                  (the Nbd of (FTSC1 n)).y={j,j-'1,1}
                         by A3,A5,A32,Def5;then
                  U_FT y = {j,j-'1,1} by FIN_TOPO:def 1;
                  hence x in U_FT y by A31,ENUMSET1:def 1;
                end;
                now assume j<>n;then
                  j<n by A4,REAL_1:def 5;then
                  (the Nbd of (FTSC1 n)).y={j,j-'1,j+1}
                             by A3,A5,A32,Def5;then
                  U_FT y = {j,j-'1,j+1} by FIN_TOPO:def 1;
                  hence x in U_FT y by A31,ENUMSET1:def 1;
                end;
                hence x in U_FT y by A33;
              end;
              case x=j+1;then
A34:            i=(i+1)+1 by A27;
                i<i+1 by NAT_1:38;
                hence contradiction by A34,NAT_1:38;
              end;
            end;
            hence x in U_FT y;
          end;
          hence x in U_FT y by A10,A12,A13;
        end;
        case  A35: i=1 & i<n;then
          U_FT x= {i,n,i+1} by A5,A9,FIN_TOPO:def 1;then
A36:      y=i & y<>n or y=n or y=i+1& y<>n by A10,ENUMSET1:def 1;
          now per cases by A36;
            case A37: y=i & y<>n;then
              U_FT y= {j,n,j+1} by A5,A9,A35,FIN_TOPO:def 1;
              hence x in U_FT y by A37,ENUMSET1:def 1;
            end;
            case y=n;then
             (the Nbd of (FTSC1 n)).y={j,j-'1,1} by A3,A5,A35,Def5;then
              U_FT y= {j,j-'1,1} by FIN_TOPO:def 1;
              hence x in U_FT y by A35,ENUMSET1:def 1;
            end;
            case A38: y=i+1 & y<>n;then
A39:          j<n by A4,REAL_1:def 5;
              j-1=i by A38;then
A40:          j-'1=i by A35,BINARITH:def 3;
              (the Nbd of (FTSC1 n)).y={j,j-'1,j+1}
                             by A3,A5,A35,A38,A39,Def5;then
              U_FT y= {j,j-'1,j+1} by FIN_TOPO:def 1;
              hence x in U_FT y by A40,ENUMSET1:def 1;
            end;
          end;
          hence x in U_FT y;
        end;
        case A41: 1<i & i=n;then
          U_FT x= {i,i-'1,1} by A5,A9,FIN_TOPO:def 1;then
A42:      y=i or y=i-'1 or y=1 by A10,ENUMSET1:def 1;
          now per cases by A42;
            case A43: y=i & y<>1;then
              U_FT y= {j,j-'1,1} by A5,A9,A41,FIN_TOPO:def 1;
              hence x in U_FT y by A43,ENUMSET1:def 1;
            end;
            case A44: y=1;
              (the Nbd of (FTSC1 n)).y={j,n,j+1}
                  by A3,A5,A41,A44,Def5;then
              U_FT y= {j,n,j+1} by FIN_TOPO:def 1;
              hence x in U_FT y by A41,ENUMSET1:def 1;
            end;
            case A45: y=i-'1 & y<>1;then
A46:          1<j by A4,REAL_1:def 5;
A47:          j=n-'1 by A41,A45;
              n-1>0 by A41,SQUARE_1:11;then
A48:          n-1=n-'1 by BINARITH:def 3;
A49:          n-1+1=n;
A50:          j+1=i by A41,A47,A48;
              j<n by A47,A48,A49,NAT_1:38;then
              (the Nbd of (FTSC1 n)).y={j,j-'1,j+1} by A3,A5,A46,Def5;then
              U_FT y= {j,j-'1,j+1} by FIN_TOPO:def 1;
              hence x in U_FT y by A50,ENUMSET1:def 1;
            end;
          end;
          hence x in U_FT y;
        end;
        case i=1 & i=n;then
          U_FT x= {i} by A5,A9,FIN_TOPO:def 1;then
          j=i by A10,TARSKI:def 1;
          hence x in U_FT y by A10;
        end;
      end;
      hence x in U_FT y;
    end;
    hence y in U_FT x implies x in U_FT y;
  end;
  hence (FTSC1 n) is symmetric by FIN_TOPO:def 14;
end;
