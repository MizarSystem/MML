:: Continuous Mappings between Finite and One-Dimensional Finite Topological
:: Spaces
::  by Hiroshi Imura , Masami Tanaka and Yatsuka Nakamura
::
:: Received July 13, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies FINSEQ_1, BOOLE, FUNCT_1, RELAT_1, ARYTM_1, RELAT_2, FIN_TOPO,
      CONNSP_1, SQUARE_1, FINTOPO3, FINTOPO4, ORDERS_1, COMPLEX1, ARYTM;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, RELAT_1, NUMBERS, XCMPLX_0,
      XXREAL_0, NAT_1, FUNCT_1, RELSET_1, FUNCT_2, FINSEQ_1, STRUCT_0,
      ORDERS_2, FIN_TOPO, FINTOPO3, BINARITH, ENUMSET1, LIMFUNC1;
 constructors ENUMSET1, NAT_1, EQREL_1, LIMFUNC1, BINARITH, FIN_TOPO, FINTOPO3;
 registrations XBOOLE_0, SUBSET_1, RELSET_1, XXREAL_0, XREAL_0, NAT_1,
      STRUCT_0, FIN_TOPO, ORDINAL1;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, FIN_TOPO, FINSEQ_1;
 theorems TARSKI, FINSEQ_1, NAT_1, REAL_1, FUNCT_1, FUNCT_2, XBOOLE_0,
      XBOOLE_1, FIN_TOPO, FINTOPO3, RELAT_1, SQUARE_1, BINARITH, ENUMSET1,
      STRUCT_0, XREAL_1, XXREAL_0, ORDINAL1, RELSET_1;
 schemes NAT_1, FUNCT_2, RELSET_1;

begin

definition let FT be non empty RelStr, A,B be Subset of FT;
  pred A,B are_separated means :Def1:
    A^b misses B & A misses B^b;
  symmetry;
end;

theorem Th1:
  for FT being filled (non empty RelStr),
      A being Subset of FT, n,m being Element of NAT st
    n<=m holds Finf(A,n) c= Finf(A,m)
proof
  let FT be filled (non empty RelStr), A be Subset of FT,
  n,m be Element of NAT;
  assume n<=m; then A1: m-n>=0 by XREAL_1:50;
  defpred P[Element of NAT] means Finf(A,n) c= Finf(A,n+$1);
A2: P[0];
A3: for k being Element of NAT st P[k] holds P[k+1]
  proof
    let k be Element of NAT;
    assume A4:P[k];
     Finf(A,n+k) c= Finf(A,n+k+1) by FINTOPO3:37;
    hence P[k+1] by A4,XBOOLE_1:1;
  end;
A5: for m2 being Element of NAT holds P[m2] from NAT_1:sch 1(A2,A3);
   n+(m-'n)=n+(m-n) by A1,BINARITH:def 3
         .=m;
  hence Finf(A,n) c= Finf(A,m) by A5;
end;

theorem
   for FT being filled (non empty RelStr),
      A being Subset of FT, n,m being Element of NAT st
    n<=m holds Fcl(A,n) c= Fcl(A,m)
proof
  let FT be filled (non empty RelStr),A be Subset of FT,
  n,m be Element of NAT;
  assume n<=m;
then A1: m-n>=0 by XREAL_1:50;
  defpred P[Element of NAT] means Fcl(A,n) c= Fcl(A,n+$1);
A2: P[0];
A3: for k being Element of NAT st P[k] holds P[k+1]
  proof
    let k be Element of NAT;
    assume A4:P[k];
     Fcl(A,n+k) c= Fcl(A,n+k+1) by FINTOPO3:25;
    hence P[k+1] by A4,XBOOLE_1:1;
  end;
  A5: for m2 being Element of NAT holds P[m2] from NAT_1:sch 1(A2,A3);
   n+(m-'n)=n+(m-n) by A1,BINARITH:def 3
         .=m;
  hence Fcl(A,n) c= Fcl(A,m) by A5;
end;

theorem
   for FT being filled (non empty RelStr),
      A being Subset of FT, n,m being Element of NAT st
    n<=m holds Fdfl(A,m) c= Fdfl(A,n)
proof
  let FT be filled (non empty RelStr),A be Subset of FT,
  n,m be Element of NAT;
  assume n<=m;
then A1: m-n>=0 by XREAL_1:50;
  defpred P[Element of NAT] means Fdfl(A,n+$1) c= Fdfl(A,n);
A2: P[0];
A3: for k being Element of NAT st P[k] holds P[k+1]
  proof let k be Element of NAT;
    assume A4:P[k];
     Fdfl(A,n+k+1) c= Fdfl(A,n+k) by FINTOPO3:44;
    hence P[k+1] by A4,XBOOLE_1:1;
  end;
  A5: for m2 being Element of NAT holds P[m2] from NAT_1:sch 1(A2,A3);
   n+(m-'n)=n+(m-n) by A1,BINARITH:def 3
         .=m;
  hence Fdfl(A,m) c= Fdfl(A,n) by A5;
end;

theorem
   for FT being filled (non empty RelStr), A being Subset of FT,
      n,m being Element of NAT st
    n<=m holds Fint(A,m) c= Fint(A,n)
proof
  let FT be filled (non empty RelStr),A be Subset of FT,
  n,m be Element of NAT;
  assume n<=m;
then A1: m-n>=0 by XREAL_1:50;
  defpred P[Element of NAT] means Fint(A,n+$1) c= Fint(A,n);
A2: P[0];
A3: for k being Element of NAT st P[k] holds P[k+1]
  proof
    let k be Element of NAT;
    assume A4:P[k];
     Fint(A,n+k+1) c= Fint(A,n+k) by FINTOPO3:26;
    hence P[k+1] by A4,XBOOLE_1:1;
  end;
  A5: for m2 being Element of NAT holds P[m2] from NAT_1:sch 1(A2,A3);
   n+(m-'n)=n+(m-n) by A1,BINARITH:def 3
         .=m;
  hence Fint(A,m) c= Fint(A,n) by A5;
end;

theorem
   for FT being non empty RelStr,A,B being Subset of FT st
    A,B are_separated holds B,A are_separated;

theorem Th6:
  for FT being filled (non empty RelStr),
      A,B being Subset of FT st
    A,B are_separated holds A misses B
proof
  let FT be filled (non empty RelStr),
  A,B be Subset of FT;
  assume A,B are_separated;
then A1: A^b misses B & A misses (B^b) by Def1;
   A c= A^b by FIN_TOPO:18;
  hence A misses B by A1,XBOOLE_1:63;
end;

theorem
   for FT being non empty RelStr,
      A,B being Subset of FT st
    FT is symmetric holds
    A,B are_separated iff A^f misses B & A misses (B^f)
proof
  let FT be non empty RelStr,
  A,B be Subset of FT;
  assume A1: FT is symmetric;
then A2: A^b=A^f by FIN_TOPO:17;
     B^b=B^f by A1,FIN_TOPO:17;
  hence thesis by A2,Def1;
end;

theorem Th8:
  for FT being filled (non empty RelStr),
      A,B being Subset of FT st
    FT is symmetric & A^b misses B holds A misses (B^b)
proof
  let FT be filled (non empty RelStr),
  A,B be Subset of FT;
  assume A1: FT is symmetric & A^b misses B;
   now assume A meets (B^b);
    then consider x being set such that
A2: x in A & x in B^b by XBOOLE_0:3;
    consider x2 being Element of FT such that
A3: x2=x & U_FT x2 meets B by A2;
A4: U_FT x2 /\ B <>{} by A3,XBOOLE_0:def 7;
    consider y being Element of U_FT x2 /\ B;
A5: y in U_FT x2 & y in B by A4,XBOOLE_0:def 3;
    then reconsider y2=y as Element of FT;
     x2 in U_FT y2 by A1,A5,FIN_TOPO:def 15;
    then x2 in U_FT y2 /\ A by A2,A3,XBOOLE_0:def 3;
    then U_FT y2 meets A by XBOOLE_0:def 7;
    then y2 in A^b;
    hence contradiction by A1,A5,XBOOLE_0:3;
  end;
  hence thesis;
end;

theorem
   for FT being filled (non empty RelStr),
      A,B being Subset of FT st
    FT is symmetric & A misses (B^b) holds A^b misses B by Th8;

theorem
   for FT being filled (non empty RelStr),
      A,B being Subset of FT st
    FT is symmetric holds
    A,B are_separated iff A^b misses B
proof
  let FT be filled (non empty RelStr),
  A,B be Subset of FT;
  assume A1: FT is symmetric;
  thus A,B are_separated implies A^b misses B by Def1;
  thus A^b misses B implies A,B are_separated
  proof assume A2: A^b misses B;
    then A misses (B^b) by A1,Th8;
    hence A,B are_separated by A2,Def1;
  end;
end;

theorem
   for FT being filled (non empty RelStr),
      A,B being Subset of FT st
    FT is symmetric holds
    A,B are_separated iff A misses (B^b)
proof
  let FT be filled (non empty RelStr),
  A,B be Subset of FT;
  assume A1: FT is symmetric;
  thus A,B are_separated implies A misses (B^b) by Def1;
  thus A misses (B^b) implies A,B are_separated
  proof
    assume A2: A misses (B^b);
    then A^b misses B by A1,Th8;
    hence A,B are_separated by A2,Def1;
  end;
end;

theorem Th12:
  for FT being filled (non empty RelStr),
      IT being Subset of FT st
    FT is symmetric holds IT is connected iff
   (for A, B being Subset of FT st
      IT = A \/ B & A,B are_separated holds
      A = IT or B = IT)
proof
  let FT be filled(non empty RelStr),IT be Subset of FT;
  assume A1: FT is symmetric;
A2: now assume A3: IT is connected;
    thus
     (for A, B being Subset of FT st
       IT = A \/ B & A,B are_separated holds A = IT or B = IT)
    proof
      let A, B be Subset of FT;
      assume A4: IT = A \/ B & A,B are_separated;
then A5:   A misses B by Th6;
       now assume A6: A<>{};
         now assume B<>{};
          then A^b meets B by A3,A4,A5,A6,FIN_TOPO:def 18;
          hence contradiction by A4,Def1;
        end;
        hence A = IT or B = IT by A4;
      end;
      hence A = IT or B = IT by A4;
    end;
  end;
   now assume
A7: (for A, B being Subset of FT st
       IT = A \/ B & A,B are_separated holds
       A = IT or B = IT);
     for A,B being Subset of FT st
      IT = A \/ B & A <> {} & B <> {} & A misses B holds
      A^b meets B
    proof
      let A,B be Subset of FT;
      assume A8: IT = A \/ B & A <> {} & B <> {} & A misses B;
       now assume A9:A^b misses B;
then A10:    A^b /\ B={} by XBOOLE_0:def 7;
         A c= A^b by FIN_TOPO:18;
        then A /\ B c= A^b /\ B by XBOOLE_1:26;
then A11:    A /\ B ={} by A10,XBOOLE_1:3;
         now assume A meets (B^b);
          then consider x being set such that
A12:      x in A & x in B^b by XBOOLE_0:3;
          consider x2 being Element of FT such that
A13:      x2=x & U_FT x2 meets B by A12;
A14:      U_FT x2 /\ B <>{} by A13,XBOOLE_0:def 7;
          consider y being Element of U_FT x2 /\ B;
A15:      y in U_FT x2 & y in B by A14,XBOOLE_0:def 3;
          then reconsider y2=y as Element of FT;
           x2 in U_FT y2 by A1,A15,FIN_TOPO:def 15;
          then x2 in U_FT y2 /\ A by A12,A13,XBOOLE_0:def 3;
          then U_FT y2 meets A by XBOOLE_0:def 7;
          then y2 in A^b;
          hence contradiction by A9,A15,XBOOLE_0:3;
        end;
        then A,B are_separated by A9,Def1;
        then A=IT or B=IT by A7,A8;
        hence contradiction by A8,A11,XBOOLE_1:21;
      end;
      hence A^b meets B;
    end;
    hence IT is connected by FIN_TOPO:def 18;
  end;
  hence thesis by A2;
end;

theorem
   for FT being filled (non empty RelStr),
      B being Subset of FT st FT is symmetric holds
    B is connected iff
    not (ex C being Subset of FT st
    C<>{} & B\C <>{} & C c= B & (C^b) misses (B\C))
proof
  let FT be filled (non empty RelStr),
  B be Subset of FT;
  assume A1: FT is symmetric;
  thus B is connected implies not (ex C being Subset of FT st
    C<>{} & B\C <>{} & C c= B & (C^b) misses (B\C))
  proof
    assume A2: B is connected;
     for C being Subset of FT st C c= B & (C^b) misses (B\C) holds
      C={} or B\C={}
    proof
      let C be Subset of FT;
      assume A3: C c= B & (C^b) misses (B\C);
A4:   C \/ (B\C)=C \/ B by XBOOLE_1:39
               .=B by A3,XBOOLE_1:12;
       C^b misses (B\C) & C misses ((B\C)^b) by A1,A3,Th8;
      then C,B\C are_separated by Def1;
      then C = B or B\C = B by A1,A2,A4,Th12;
      hence C={} or B\C={} by A3,XBOOLE_1:37,38;
    end;
    hence not (ex C being Subset of FT st C<>{} & B\C <>{} &
    C c= B & (C^b) misses (B\C));
  end;
  thus not (ex C being Subset of FT st C<>{} & B\C <>{} &
   C c= B & (C^b) misses (B\C)) implies B is connected
  proof
    assume A5: not (ex C being Subset of FT st
      C<>{} & B\C <>{} & C c= B & (C^b) misses (B\C));
     for A, B2 being Subset of FT st
      B = A \/ B2 & A,B2 are_separated holds
    A = B or B2 = B
    proof
      let A, B2 be Subset of FT;
      assume A6: B = A \/ B2 & A,B2 are_separated;
then A7:   A c= B by XBOOLE_1:7;
A8:   A^b misses B2 & A misses (B2^b) by A6,Def1;
   (A \/ B2) \A =B2\A by XBOOLE_1:40;
      then B\A c= B2 by A6,XBOOLE_1:36;
      then A^b misses (B\A) by A8,XBOOLE_1:63;
      then A={} or B\A={} by A5,A7;
      then B=B2 or B c= A by A6,XBOOLE_1:37;
      hence A = B or B2 = B by A7,XBOOLE_0:def 10;
    end;
    hence B is connected by A1,Th12;
  end;
end;

definition let FT1,FT2 be non empty RelStr,
               f be Function of FT1, FT2,
               n be Element of NAT;
  pred f is_continuous n means :Def2:
  for x being Element of FT1,y being Element of FT2 st
   x in the carrier of FT1 & y=f.x holds f.:(U_FT(x,0)) c= U_FT(y,n);
end;

theorem
   for FT1 being non empty RelStr,
      FT2 being filled (non empty RelStr), n being Element of NAT,
      f being Function of FT1, FT2 st
    f is_continuous 0 holds f is_continuous n
proof
  let FT1 be non empty RelStr,
  FT2 be filled (non empty RelStr),n be Element of NAT,
  f be Function of FT1, FT2;
  assume A1: f is_continuous 0;
   for x being Element of FT1,y being Element of FT2 st
      x in the carrier of FT1 & y=f.x holds
    f.:( U_FT(x,0)) c= U_FT(y,n)
  proof
    let x be Element of FT1,y be Element of FT2;
    assume A2: x in the carrier of FT1 & y=f.x;
A3: U_FT y =U_FT(y,0) by FINTOPO3:47;
     U_FT(y,n)=Finf((U_FT y),n) by FINTOPO3:def 10;
then A4: U_FT(y,0) c= U_FT(y,n) by A3,FINTOPO3:36;
     f.:( U_FT(x,0)) c= U_FT(y,0) by A1,A2,Def2;
    hence f.:( U_FT(x,0)) c= U_FT(y,n) by A4,XBOOLE_1:1;
  end;
  hence f is_continuous n by Def2;
end;

theorem
   for FT1 being non empty RelStr,
      FT2 being filled (non empty RelStr), n0,n being Element of NAT,
      f being Function of FT1, FT2 st
    f is_continuous n0 & n0<=n holds f is_continuous n
proof
  let FT1 be non empty RelStr,
  FT2 be filled (non empty RelStr),n0,n be Element of NAT,
  f be Function of FT1, FT2;
  assume A1: f is_continuous n0 & n0<=n;
   for x being Element of FT1,y being Element of FT2 st
    x in the carrier of FT1 & y=f.x holds
    f.:( U_FT(x,0)) c= U_FT(y,n)
  proof
    let x be Element of FT1,y be Element of FT2;
    assume A2: x in the carrier of FT1 & y=f.x;
A3: U_FT(y,n0)=Finf((U_FT y),n0) by FINTOPO3:def 10;
     U_FT(y,n)=Finf((U_FT y),n) by FINTOPO3:def 10;
then A4: U_FT(y,n0) c= U_FT(y,n) by A1,A3,Th1;
     f.:( U_FT(x,0)) c= U_FT(y,n0) by A1,A2,Def2;
    hence f.:( U_FT(x,0)) c= U_FT(y,n) by A4,XBOOLE_1:1;
  end;
  hence f is_continuous n by Def2;
end;

theorem Th16:
  for FT1,FT2 being non empty RelStr,
      A being Subset of FT1,B being Subset of FT2,
      f being Function of FT1, FT2 st
    f is_continuous 0 & B=f.:A holds
    f.:(A^b) c= B^b
proof
  let FT1,FT2 be non empty RelStr,A be Subset of FT1,
  B be Subset of FT2,
  f be Function of FT1, FT2;
  assume A1: f is_continuous 0 & B=f.:A;
  thus f.:(A^b) c= B^b
  proof
    let y be set;assume y in f.:(A^b);
    then consider x being set such that
A2: x in dom f & x in A^b & y=f.x by FUNCT_1:def 12;
    reconsider x0=x as Element of FT1 by A2;
    reconsider y0=y as Element of FT2 by A2,FUNCT_2:7;
    consider z being Element of FT1 such that
A3: z=x & U_FT z meets A by A2;
A4: U_FT x0 /\ A<>{} by A3,XBOOLE_0:def 7;
     f.:( U_FT(x0,0)) c= U_FT(y0,0) by A1,A2,Def2;
    then f.:(U_FT x0) c=  U_FT(y0,0) by FINTOPO3:47;
then A5: f.:(U_FT x0) c=  U_FT y0 by FINTOPO3:47;
     dom f=the carrier of FT1 by FUNCT_2:def 1;
then A6: f.:((U_FT x0) /\ A)<>{} by A4,RELAT_1:152;
A7: f.:((U_FT x0) /\ A) c= (f.:((U_FT x0)))/\ (f.:A) by RELAT_1:154;
     (f.:((U_FT x0)))/\ (f.:A) c= (U_FT y0) /\ (f.:A) by A5,XBOOLE_1:26;
    then f.:((U_FT x0) /\ A) c= (U_FT y0) /\ (f.:A) by A7,XBOOLE_1:1;
    then (U_FT y0) /\ (f.:A)<>{} by A6,XBOOLE_1:3;
    then U_FT y0 meets B by A1,XBOOLE_0:def 7;
    hence y in B^b;
  end;
end;

theorem
   for FT1,FT2 being non empty RelStr,A being Subset of FT1,
      B being Subset of FT2,
      f being Function of FT1, FT2 st
    A is connected & f is_continuous 0 & B=f.:A holds B is connected
proof
  let FT1,FT2 be non empty RelStr,A be Subset of FT1,
  B be Subset of FT2,
  f be Function of FT1, FT2;
  assume A1: A is connected & f is_continuous 0 & B=f.:A;
   for B2,C2 being Subset of FT2 st
    B = B2 \/ C2 & B2 <> {} & C2 <> {} & B2 misses C2 holds
   B2^b meets C2
  proof let B2,C2 be Subset of FT2;
    assume A2: B = B2 \/ C2 & B2 <> {} & C2 <> {} & B2 misses C2;
    reconsider B1 = f"B2 as Subset of FT1;
A3: f.:B1 c= B2
    proof
      let y be set;
      assume y in f.:B1;
      then consider x2 being set such that
A4:   x2 in dom f & x2 in B1 & y=f.x2 by FUNCT_1:def 12;
      thus y in B2 by A4,FUNCT_1:def 13;
    end;
    reconsider B20 = f.:B1 as Subset of FT2;
A5: B20^b c= B2^b by A3,FIN_TOPO:19;
    reconsider B10=B1/\A as Subset of FT1;
    reconsider C1=f"C2 as Subset of FT1;
    reconsider C10=C1 /\ A as Subset of FT1;
     A c= the carrier of FT1;
then A6: A c= dom f by FUNCT_2:def 1;
     (f"B2) \/ (f"C2)=f"(f.:A) by A1,A2,RELAT_1:175;
    then A c= B1 \/ C1 by A6,FUNCT_1:146;
    then A c= A/\(B1\/C1) by XBOOLE_1:19;
then A7: A c= B10 \/ C10 by XBOOLE_1:23;
A8: B10 c= A by XBOOLE_1:17;
     C10 c= A by XBOOLE_1:17;
    then B10 \/ C10 c= A by A8,XBOOLE_1:8;
then A9: A=B10 \/ C10 by A7,XBOOLE_0:def 10;
A10: B10 c= B1 by XBOOLE_1:17;
A11: C10 c= C1 by XBOOLE_1:17;
then A12: B10/\C10 c= B1/\C1 by A10,XBOOLE_1:27;
    consider x5 being Element of B2;
     x5 in B by A2,XBOOLE_0:def 2;
    then consider z5 being set such that
A13: z5 in dom f & z5 in A & x5=f.z5 by A1,FUNCT_1:def 12;
     z5 in f"B2 by A2,A13,FUNCT_1:def 13;
then A14: B10<>{} by A13,XBOOLE_0:def 3;
    consider x6 being Element of C2;
     x6 in B by A2,XBOOLE_0:def 2;
    then consider z6 being set such that
A15: z6 in dom f & z6 in A & x6=f.z6 by A1,FUNCT_1:def 12;
     z6 in f"C2 by A2,A15,FUNCT_1:def 13;
then A16: C10<>{} by A15,XBOOLE_0:def 3;
     B2 /\ C2 = {} by A2,XBOOLE_0:def 7;
    then f"(B2 /\ C2) = {} by RELAT_1:171;
    then (f"B2) /\ (f"C2) = {} by FUNCT_1:137;
    then B10 /\ C10= {} by A12,XBOOLE_1:3;
    then B10 misses C10 by XBOOLE_0:def 7;
    then B10^b meets C10 by A1,A9,A14,A16,FIN_TOPO:def 18;
then A17: B10^b /\ C10 <> {} by XBOOLE_0:def 7;
     B10^b c= B1^b by A10,FIN_TOPO:19;
    then B10^b /\ C10 c= B1^b /\ C1 by A11,XBOOLE_1:27;
then A18: B1^b /\ C1 <> {} by A17,XBOOLE_1:3;
     dom f=the carrier of FT1 by FUNCT_2:def 1;
then A19: f.:(B1^b /\ C1) <> {} by A18,RELAT_1:152;
     f.:(B1^b /\ C1) c= (f.:(B1^b)) /\ f.:(C1) by RELAT_1:154;
then A20: (f.:(B1^b)) /\ (f.:C1) <> {} by A19,XBOOLE_1:3;
     f.:C1 c= C2 by FUNCT_1:145;
then A21: (f.:(B1^b)) /\ (f.:C1) c= (f.:(B1^b)) /\ C2 by XBOOLE_1:26;
     f.:(B1^b) c= B20^b by A1,Th16;
    then f.:(B1^b) c= B2^b by A5,XBOOLE_1:1;
    then (f.:(B1^b)) /\ C2 c= B2^b /\ C2 by XBOOLE_1:26;
    then (f.:(B1^b)) /\ (f.:C1) c= B2^b /\ C2 by A21,XBOOLE_1:1;
    then B2^b /\ C2 <>{} by A20,XBOOLE_1:3;
    hence B2^b meets C2 by XBOOLE_0:def 7;
  end;
  hence B is connected by FIN_TOPO:def 18;
end;

::1 dimensional linear FT_Str
definition let n be Nat;
  func Nbdl1 n -> Relation of Seg n means
:Def3: for i being Element of NAT st i in Seg n
   holds Im(it,i)={i,max(i-'1,1),min(i+1,n)};
existence
  proof
    deffunc F(Nat) = {$1,max($1-'1,1),min($1+1,n)};
A1: for x being Element of NAT st x in Seg n holds F(x) c= Seg n
    proof
      let i be Element of NAT;
      assume A2: i in Seg n;
       then n>0 by FINSEQ_1:4,NAT_1:3;
then A3:   0+1<=n by NAT_1:13;
      set y0={i,max(i-'1,1),min(i+1,n)};
     thus y0 c=Seg n
      proof
        let z1 be set;
        assume A4: z1 in y0;
then     z1=i or z1=max(i-'1,1) or z1=min(i+1,n) by ENUMSET1:def 1;
        then z1=i or z1=i-'1 or z1=1 or z1=i+1 or z1=n by XXREAL_0:15,16
;
        then reconsider z2=z1 as Element of NAT by ORDINAL1:def 13;
A5:     now assume A6: z1=max(i-'1,1);
A7:       i-'1<=i by BINARITH:52;
           i<=n by A2,FINSEQ_1:3;
          then i-'1<=n by A7,XXREAL_0:2;
          then 1<= max(i-'1,1) & max(i-'1,1)<=n by A3,SQUARE_1:50;
          hence z1 in Seg n by A6;
        end;
         now assume A8: z1=min(i+1,n);
           1<=i+1 by NAT_1:12;
then A9:      1<=min(i+1,n) by A3,SQUARE_1:39;
           min(i+1,n)<=n by XXREAL_0:17;
          hence z2 in Seg n by A8,A9;
        end;
        hence z1 in Seg n by A2,A4,A5,ENUMSET1:def 1;
      end;
    end;
    consider f being Relation of Seg n such that
A10: for x3 being Element of NAT st x3 in Seg n
       holds Im(f,x3) = F(x3) from RELSET_1:sch 3(A1);
    take f;
    thus
       for i being Element of NAT st i in Seg n holds
      Im(f,i)={i,max(i-'1,1),min(i+1,n)} by A10;
  end;
uniqueness
  proof
    thus for f1,f2 being Relation of Seg n st
     (for i being Element of NAT st i in Seg n holds
      Im(f1,i)={i,max(i-'1,1),min(i+1,n)})&
     (for i being Element of NAT st i in Seg n holds
      Im(f2,i)={i,max(i-'1,1),min(i+1,n)})
     holds f1=f2
    proof
      let f1,f2 be Relation of Seg n;
      assume A11: (for i being Element of NAT st i in Seg n
      holds
      Im(f1,i)={i,max(i-'1,1),min(i+1,n)})&
      (for i being Element of NAT st i in Seg n holds
      Im(f2,i)={i,max(i-'1,1),min(i+1,n)});
       for x being set st x in Seg n holds Im(f1,x)=Im(f2,x)
      proof
       let x be set;
       assume A12: x in Seg n;
        then reconsider i2=x as Element of NAT;
         Im(f1,i2)={i2,max(i2-'1,1),min(i2+1,n)} by A11,A12;
        hence Im(f1,x)=Im(f2,x) by A11,A12;
      end;
      hence f1=f2 by RELSET_1:54;
    end;
  end;
end;

definition let n be Nat;
 assume A1: n>0;
 func FTSL1 n -> non empty RelStr equals
:Def4:  RelStr(# Seg n,Nbdl1 n #);
correctness
  proof
     0+1<=n by A1,NAT_1:13;
    then Seg n<>{} by FINSEQ_1:3;
    hence RelStr(# Seg n,Nbdl1 n #) is non empty RelStr
                                                 by STRUCT_0:def 1;
  end;
end;

theorem
   for n being Nat st n>0 holds FTSL1 n is filled
proof
  let n be Nat;assume n>0;
then A1: FTSL1 n=RelStr(# Seg n,Nbdl1 n #) by Def4;
  let x be Element of FTSL1 n;
   x in Seg n by A1;
  then reconsider i=x as Element of NAT;
   U_FT x= {i,max(i-'1,1),min(i+1,n)} by A1,Def3;
  hence x in U_FT x by ENUMSET1:def 1;
end;

theorem
   for n being Nat st n>0 holds FTSL1 n is symmetric
proof
  let n be Nat;
  assume n>0;
then A1: FTSL1 n=RelStr(# Seg n,Nbdl1 n #) by Def4;
    let x, y be Element of FTSL1 n;
 x in Seg n by A1;
    then reconsider i=x as Element of NAT;
A2: 1<=i & i<=n by A1,FINSEQ_1:3;
     y in Seg n by A1;
    then reconsider j=y as Element of NAT;
A3: U_FT x= {i,max(i-'1,1),min(i+1,n)} by A1,Def3;
A4: U_FT y= {j,max(j-'1,1),min(j+1,n)} by A1,Def3;
     now assume A5: y in U_FT x;
then A6: y=i or y=max(i-'1,1) or y=min(i+1,n) by A3,ENUMSET1:def 1;
A7: now assume A8: y=max(i-'1,1);
       now per cases;
        case A9: i-'1>=1;
then A10:      y=i-'1 by A8,XXREAL_0:def 9;
           now per cases;
            case i-1>=0;
              then j=i-1 by A10,BINARITH:def 3;
              hence x =j or x=max(j-'1,1) or x= min(j+1,n)
                                     by A2,XXREAL_0:def 8;
              end;
              case i-1<0;
                hence contradiction by A9,BINARITH:def 3;
              end;
            end;
            hence x =j or x=max(j-'1,1) or x= min(j+1,n);
          end;
          case A11: i-'1<1;
then A12:        y=1 by A8,XXREAL_0:def 9;
             now assume i>1;
then A13:          i-1>0 by XREAL_1:52;
              then i-'1=i-1 by BINARITH:def 3;
              then i-'1>=0+1 by A13,NAT_1:13;
              hence contradiction by A11;
            end;
            hence x =j or x=max(j-'1,1) or x= min(j+1,n) by A2,A12,XXREAL_0:1;
          end;
        end;
        hence x in U_FT y by A4,ENUMSET1:def 1;
      end;
       now assume y=min(i+1,n);
         now per cases by A3,A5,ENUMSET1:def 1;
          case y=i;
            hence x =j or x=max(j-'1,1) or x= min(j+1,n);
          end;
          case A14: y=max(i-'1,1);
             now per cases;
              case A15: i-'1>=1;
then A16:            y=i-'1 by A14,XXREAL_0:def 9;
                 now per cases;
                  case i-1>=0;
                   then j=i-1 by A16,BINARITH:def 3;
                   hence x =j or x=max(j-'1,1) or x= min(j+1,n)
                                           by A2,XXREAL_0:def 8;
                  end;
                  case i-1<0;
                    hence contradiction by A15,BINARITH:def 3;
                  end;
                end;
                hence x =j or x=max(j-'1,1) or x= min(j+1,n);
              end;
              case A17: i-'1<1;
then A18:            y=1 by A14,XXREAL_0:def 9;
                 now assume i>1;
then A19:               i-1>0 by XREAL_1:52;
                  then i-'1=i-1 by BINARITH:def 3;
                  then i-'1>=0+1 by A19,NAT_1:13;
                  hence contradiction by A17;
                end;
                hence x =j or x=max(j-'1,1) or x= min(j+1,n)
                                           by A2,A18,XXREAL_0:1;
              end;
            end;
            hence x =j or x=max(j-'1,1) or x= min(j+1,n);
          end;
          case A20: y=min(i+1,n);
             now per cases;
              case i+1<=n;
then A21:            y=i+1 by A20,XXREAL_0:def 8;
then A22:            j-1=j-'1 by BINARITH:def 3;
                 now per cases;
                  case j-1>=1;
                    hence x =j or x=max(j-'1,1) or x= min(j+1,n)
                                    by A21,A22,XXREAL_0:def 9;
                  end;
                  case j-1<1;
                    hence contradiction by A1,A21,FINSEQ_1:3;
                  end;
                end;
                hence x =j or x=max(j-'1,1) or x= min(j+1,n);
              end;
              case A23: i+1>n;
                then y=n by A20,XXREAL_0:def 8;
                then j+1>n by NAT_1:13;
then A24:            min(j+1,n)=n by XXREAL_0:def 8;
                 i>=n by A23,NAT_1:13;
                hence x =j or x=max(j-'1,1) or x= min(j+1,n)
                                           by A2,A24,XXREAL_0:1;
              end;
            end;
            hence x =j or x=max(j-'1,1) or x= min(j+1,n);
          end;
        end;
        hence x in U_FT y by A4,ENUMSET1:def 1;
      end;
      hence x in U_FT y by A5,A6,A7;
    end;
    hence thesis;
end;

::1 dimensional cyclic FT_Str
definition let n be Nat;
  func Nbdc1 n -> Relation of Seg n means
:Def5: for i being Element of NAT st i in Seg n holds
   (1<i & i<n implies Im(it,i)={i,i-'1,i+1})&
   (i=1 & i<n implies Im(it,i)={i,n,i+1}) &
   (1<i & i=n implies Im(it,i)={i,i-'1,1}) &
   (i=1 & i=n implies Im(it,i)={i});
existence
  proof
    defpred P[set,set] means for i being Element of NAT st i=$1 holds
    (1<i & i<n implies $2={i,i-'1,i+1})&
    (i=1 & i<n implies $2={i,n,i+1}) &
    (1<i & i=n implies $2={i,i-'1,1}) &
    (i=1 & i=n implies $2={i});
A1: for x being set st x in Seg n ex y being set st y in bool Seg n & P[x,y]
  proof
    let x be set;
    assume A2: x in Seg n;
    then reconsider i2=x as Element of NAT;
A3: 1<=i2 & i2<=n by A2,FINSEQ_1:3;
     now per cases by A3,REAL_1:def 5;
      case A4: 1<i2 & i2<n;
        set y0={i2,i2-'1,i2+1};
         i2-1>0 by A4,XREAL_1:52;
        then i2-'1>0 by BINARITH:def 3;
then A5:     i2-'1>=0+1 by NAT_1:13;
         i2-'1<=i2 by BINARITH:52;
        then i2-'1<n by A4,XXREAL_0:2;
then A6:     i2-'1 in Seg n by A5;
A7:     i2+1<=n by A4,NAT_1:13;
         i2<=i2+1 by NAT_1:12;
        then 1<i2+1 by A4,XXREAL_0:2;
then A8:    i2+1 in Seg n by A7;
        A9: y0 c= Seg n
        proof let z1 be set;
          assume z1 in y0;
          hence z1 in Seg n by A2,A6,A8,ENUMSET1:def 1;
        end;
         P[x,y0] by A4;
        hence ex y being set st y in bool Seg n & P[x,y] by A9;
      end;
      case A10: i2=1 & i2<n;
        set y0={i2,n,i2+1};
A11:    n in Seg n by A10,FINSEQ_1:3;
    i2+1<=n by A10,NAT_1:13;
then A12:    i2+1 in Seg n by A10;
        A13: y0 c= Seg n
        proof let z1 be set;
          assume z1 in y0;
          hence z1 in Seg n by A2,A11,A12,ENUMSET1:def 1;
        end;
         P[x,y0] by A10;
        hence ex y being set st y in bool Seg n & P[x,y] by A13;
      end;
      case A14: 1<i2 & i2=n;
        set y0={i2,i2-'1,1};
         i2-1>0 by A14,XREAL_1:52;
        then i2-'1>0 by BINARITH:def 3;
then A15:    i2-'1>=0+1 by NAT_1:13;
         i2-'1<=n by A14,BINARITH:52;
then A16:    i2-'1 in Seg n by A15;
A17:    1 in Seg n by A14;
        A18: y0 c= Seg n
        proof
          let z1 be set;
          assume z1 in y0;
          hence z1 in Seg n by A2,A16,A17,ENUMSET1:def 1;
        end;
         P[x,y0] by A14;
        hence ex y being set st y in bool Seg n & P[x,y] by A18;
      end;
      case A19: i2=1 & i2=n;
        set y0={i2};
        A20: y0 c= Seg n
        proof
          let z1 be set;
          assume z1 in y0;
          hence z1 in Seg n by A2,TARSKI:def 1;
        end;
         P[x,y0] by A19;
        hence ex y being set st y in bool Seg n & P[x,y] by A20;
      end;
    end;
    hence ex y being set st y in bool Seg n & P[x,y];
  end;
  consider f2 being Function of Seg n,bool Seg n such that
A21: for x3 being set st
    x3 in Seg n holds P[x3,f2.x3] from FUNCT_2:sch 1(A1);
   consider R being Relation of Seg n such that
A22:  for i being set st i in Seg n holds Im(R,i) = f2.i by FUNCT_2:170;
  take R;
   let i being Element of NAT;
    assume
A23:   i in Seg n;
     then Im(R,i) = f2.i by A22;
   hence (1<i & i<n implies Im(R,i)={i,i-'1,i+1})&
   (i=1 & i<n implies Im(R,i)={i,n,i+1}) &
   (1<i & i=n implies Im(R,i)={i,i-'1,1}) &
   (i=1 & i=n implies Im(R,i)={i}) by A21,A23;
  end;
uniqueness
  proof
    let f1,f2 be Relation of Seg n;
    assume A24:
    (for i being Element of NAT st i in Seg n holds
     (1<i & i<n implies Im(f1,i)={i,i-'1,i+1})&
     (i=1 & i<n implies Im(f1,i)={i,n,i+1}) &
     (1<i & i=n implies Im(f1,i)={i,i-'1,1}) &
     (i=1 & i=n implies Im(f1,i)={i}))&
    (for i being Element of NAT st i in Seg n holds
     (1<i & i<n implies Im(f2,i)={i,i-'1,i+1})&
     (i=1 & i<n implies Im(f2,i)={i,n,i+1}) &
     (1<i & i=n implies Im(f2,i)={i,i-'1,1}) &
     (i=1 & i=n implies Im(f2,i)={i}));
     for x being set st x in Seg n holds Im(f1,x)=Im(f2,x)
    proof
     let x be set;
     assume A25: x in Seg n;
      then reconsider i2=x as Element of NAT;
A26:  1<=i2 & i2<=n by A25,FINSEQ_1:3;
      per cases by A26,REAL_1:def 5;
        suppose A27: 1<i2 & i2<n;
          set y0={i2,i2-'1,i2+1};
           Im(f1,x)=y0 by A24,A25,A27;
          hence Im(f1,x)=Im(f2,x) by A24,A25,A27;
        end;
        suppose A28: i2=1 & i2<n;
          set y0={i2,n,i2+1};
           Im(f1,x)=y0 by A24,A25,A28;
          hence Im(f1,x)=Im(f2,x) by A24,A25,A28;
        end;
        suppose A29: 1<i2 & i2=n;
          set y0={i2,i2-'1,1};
           Im(f1,x)=y0 by A24,A25,A29;
          hence Im(f1,x)=Im(f2,x) by A24,A25,A29;
        end;
        suppose A30: i2=1 & i2=n;
          set y0={i2};
           Im(f1,x)=y0 by A24,A25,A30;
          hence Im(f1,x)=Im(f2,x) by A24,A25,A30;
        end;
    end;
    hence f1=f2 by RELSET_1:54;
  end;
end;

definition let n be Nat;
  assume A1: n>0;
  func FTSC1 n -> non empty RelStr equals
:Def6: RelStr(# Seg n,Nbdc1 n #);
correctness
  proof
     0+1<=n by A1,NAT_1:13;
    then Seg n<>{} by FINSEQ_1:3;
    hence RelStr(# Seg n,Nbdc1 n #) is non empty RelStr
                                              by STRUCT_0:def 1;
  end;
end;

theorem
   for n being Element of NAT st n>0 holds FTSC1 n is filled
proof
  let n be Element of NAT;assume n>0;
then A1: FTSC1 n=RelStr(# Seg n,Nbdc1 n #) by Def6;
    let x be Element of FTSC1 n;
 x in Seg n by A1;
    then reconsider i=x as Element of NAT;
    set f=Nbdc1 n;
A2: 1<=i & i<=n by A1,FINSEQ_1:3;
A3: (1<i & i<n implies Im(f,i)={i,i-'1,i+1})&
    (i=1 & i<n implies Im(f,i)={i,n,i+1}) &
    (1<i & i=n implies Im(f,i)={i,i-'1,1}) &
    (i=1 & i=n implies Im(f,i)={i}) by A1,Def5;
    per cases by A2,REAL_1:def 5;
      suppose 1<i & i<n;
        hence x in U_FT x by A1,A3,ENUMSET1:def 1;
      end;
      suppose i=1 & i<n;
        hence x in U_FT x by A1,A3,ENUMSET1:def 1;
      end;
      suppose 1<i & i=n;
        hence x in U_FT x by A1,A3,ENUMSET1:def 1;
      end;
      suppose i=1 & i=n;
        hence x in U_FT x by A1,A3,TARSKI:def 1;
      end;
end;

theorem
   for n being Element of NAT st n>0 holds FTSC1 n is symmetric
proof
  let n be Element of NAT;
  assume n>0;
then A1: FTSC1 n=RelStr(# Seg n,Nbdc1 n #) by Def6;
    let x, y be Element of FTSC1 n;
 x in Seg n by A1;
    then reconsider i=x as Element of NAT;
 y in Seg n by A1;
    then reconsider j=y as Element of NAT;
A2: 1<=j & j<=n by A1,FINSEQ_1:3;
    set f=Nbdc1 n;
A3: 1<=i & i<=n by A1,FINSEQ_1:3;
A4: (1<i & i<n implies Im(f,i)={i,i-'1,i+1})&
    (i=1 & i<n implies Im(f,i)={i,n,i+1}) &
    (1<i & i=n implies Im(f,i)={i,i-'1,1}) &
    (i=1 & i=n implies Im(f,i)={i}) by A1,Def5;
    assume A5: y in U_FT x;
      per cases by A3,REAL_1:def 5;
        suppose A6: 1<i & i<n;
then A7:      y=i or y=i-'1 or y=i+1 by A1,A4,A5,ENUMSET1:def 1;
A8:      now assume A9: y=i-'1;
             i-1>0 by A6,XREAL_1:52;
            then A10: i-'1=i-1 by BINARITH:def 3;
            per cases by A9,A10;
              suppose A11: x=j;
                then Im(the InternalRel of FTSC1 n,y)={j,j-'1,j+1}
                 by A1,A6,Def5;
                hence x in U_FT y by A11,ENUMSET1:def 1;
              end;
              suppose A12: x=j-'1;
then A13:            i=(i-'1)-'1 by A9;
                 now assume i<>0; then i>0;
then A14:              i>=0+1 by NAT_1:13;
                  then i-1>=1-1 by XREAL_1:11;
then A15:              i-1=i-'1 by BINARITH:def 3;
                   now assume A16: i=1;
                    then i-'1-1<0 by A15;
                    hence contradiction by A9,A12,A16,BINARITH:def 3;
                  end;
                  then i>1 by A14,REAL_1:def 5;
                  then i-1>1-1 by XREAL_1:11;
                  then i-'1>=0+1 by A15,NAT_1:13;
                  then i-'1-1>=0 by XREAL_1:50;
                  then i-'1-'1=i-'1-1 by BINARITH:def 3;
                  hence contradiction by A13,A15;
                end;
                hence x in U_FT y by A6;
              end;
              suppose A17: x=j+1;
then A18:            j<n by A3,NAT_1:13;
A19:            now assume j=1;
                  then Im(the InternalRel of FTSC1 n,y)={j,n,j+1}
                       by A1,A18,Def5;
                  hence x in U_FT y by A17,ENUMSET1:def 1;
                end;
                now assume j<>1;
                 then j>1 by A2,REAL_1:def 5;
                 then Im(the InternalRel of FTSC1 n,y)={j,j-'1,j+1}
                      by A1,A18,Def5;
                 hence x in U_FT y by A17,ENUMSET1:def 1;
               end;
               hence x in U_FT y by A19;
              end;
          end;
           now assume
A20:        y=i+1;
            then A21: j-1=x;
             now per cases by A6,A21,BINARITH:def 3;
              case A22: x=j;
                then Im(the InternalRel of FTSC1 n,y)={j,j-'1,j+1}
                    by A1,A6,Def5;
                hence x in U_FT y by A22,ENUMSET1:def 1;
              end;
              case A23: x=j-'1;
                 now assume j=1;
                  then j-1=0;
                  hence contradiction by A6,A23,BINARITH:def 3;
                end;
then A24:            j>1 by A2,REAL_1:def 5;
A25:            now assume j=n;
                  then Im(the InternalRel of FTSC1 n,y)={j,j-'1,1}
                   by A1,A24,Def5;
                  hence x in U_FT y by A23,ENUMSET1:def 1;
                end;
                 now assume j<>n;
                  then j<n by A2,REAL_1:def 5;
                  then Im(the InternalRel of FTSC1 n,y)={j,j-'1,j+1}
                   by A1,A24,Def5;
                  hence x in U_FT y by A23,ENUMSET1:def 1;
                end;
                hence x in U_FT y by A25;
              end;
              case x=j+1;
                hence contradiction by A20;
              end;
            end;
            hence x in U_FT y;
          end;
          hence x in U_FT y by A5,A7,A8;
        end;
        suppose A26: i=1 & i<n;
            per cases by A1,A4,A5,A26,ENUMSET1:def 1;
            suppose y=i & y<>n;
              hence x in U_FT y by A1,A4,A26,ENUMSET1:def 1;
            end;
            suppose y=n;
             then Im(the InternalRel of FTSC1 n,y)={j,j-'1,1} by A1,A26,Def5;
              hence x in U_FT y by A26,ENUMSET1:def 1;
            end;
            suppose A27: y=i+1 & y<>n;
then A28:          j<n by A2,REAL_1:def 5;
               j-1=i by A27;
then A29:          j-'1=i by BINARITH:def 3;
               Im(the InternalRel of FTSC1 n,y)={j,j-'1,j+1}
               by A1,A26,A27,A28,Def5;
              hence x in U_FT y by A29,ENUMSET1:def 1;
            end;
        end;
        suppose A30: 1<i & i=n;
          per cases by A1,A4,A5,A30,ENUMSET1:def 1;
            suppose y=i & y<>1;
              hence x in U_FT y by A1,A4,A30,ENUMSET1:def 1;
            end;
            suppose y=1;
              then Im(the InternalRel of FTSC1 n,y)={j,n,j+1}
               by A1,A30,Def5;
              hence x in U_FT y by A30,ENUMSET1:def 1;
            end;
            suppose A31: y=i-'1 & y<>1;
then A32:          1<j by A2,REAL_1:def 5;
               n-1>0 by A30,XREAL_1:52;
then A33:          n-1=n-'1 by BINARITH:def 3;
          n-1+1=n;
              then j<n by A30,A31,A33,NAT_1:13;
              then Im(the InternalRel of FTSC1 n,y)={j,j-'1,j+1} by A1,A32,Def5
;
              hence x in U_FT y by A30,A31,A33,ENUMSET1:def 1;
            end;
        end;
        suppose i=1 & i=n;
          then j=i by A1,A4,A5,TARSKI:def 1;
          hence x in U_FT y by A5;
        end;
end;
