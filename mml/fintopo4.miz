:: Continuous Mappings between Finite and One-Dimensional Finite Topological 
:: Spaces
::  by Hiroshi Imura , Masami Tanaka and Yatsuka Nakamura
:: 
:: Received July 13, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabulary FINSEQ_1, BOOLE, FUNCT_1, RELAT_1, ARYTM_1, RELAT_2, FIN_TOPO,
      CONNSP_1, SQUARE_1, FINTOPO3, FINTOPO4;
 notation TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0,
      XREAL_0, NAT_1, STRUCT_0, RELSET_1, RELAT_1, FUNCT_1, FUNCT_2, CQC_LANG,
      DOMAIN_1, GROUP_1, FINSEQ_1, FINSEQ_4, FIN_TOPO, FINTOPO3, SQUARE_1,
      BINARITH, ENUMSET1, LIMFUNC1, RFUNCT_3;
 constructors NAT_1, CQC_LANG, DOMAIN_1, GROUP_1, FINSEQ_4, MEMBERED, XBOOLE_0,
      FIN_TOPO, FINTOPO3, SQUARE_1, BINARITH, ENUMSET1, LIMFUNC1, RFUNCT_3;
 clusters SUBSET_1, INT_1, RELSET_1, STRUCT_0, CQC_LANG, FINSEQ_1, NAT_1,
      XREAL_0, MEMBERED, ZFMISC_1, XBOOLE_0, NUMBERS, ORDINAL2, FIN_TOPO;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, GROUP_1, STRUCT_0, ORDINAL1, XBOOLE_0;
 theorems TARSKI, FINSEQ_1, NAT_1, AXIOMS, REAL_1, FUNCT_1, FUNCT_2, XBOOLE_0,
      XBOOLE_1, XCMPLX_1, FIN_TOPO, FINTOPO3, RELAT_1, SQUARE_1, BINARITH,
      ENUMSET1, GOBOARD9, STRUCT_0;
 schemes NAT_1, FUNCT_2;

begin

definition
  let FT be non empty FT_Space_Str,A,B be Subset of FT;
  pred A,B are_separated means
:AA002: A^b misses B & A misses (B^b);
end;

theorem AA004:
  for FT being filled (non empty FT_Space_Str), 
      A being Subset of FT, n,m being Nat st
    n<=m holds Finf(A,n) c= Finf(A,m)
proof
  let FT be filled (non empty FT_Space_Str), A be Subset of FT, n,m be Nat;
  assume n<=m;then A1: m-n>=0 by SQUARE_1:12;
  defpred P[Nat] means Finf(A,n) c= Finf(A,n+$1);
A2: P[0];
A3: for k being Nat st P[k] holds P[k+1]
  proof
    let k be Nat;
    assume A4:P[k];
    Finf(A,n+k) c= Finf(A,n+k+1) by FINTOPO3:37;
    hence P[k+1] by A4,XBOOLE_1:1;
  end;
A6: for m2 being Nat holds P[m2] from NAT_1:sch 1(A2,A3);
  n+(m-'n)=n+(m-n) by A1,BINARITH:def 3
         .=m;
  hence Finf(A,n) c= Finf(A,m) by A6;
end;

theorem
  for FT being filled (non empty FT_Space_Str),
      A being Subset of FT, n,m being Nat st
    n<=m holds Fcl(A,n) c= Fcl(A,m)
proof
  let FT be filled (non empty FT_Space_Str),A be Subset of FT, n,m be Nat;
  assume n<=m;then
A1: m-n>=0 by SQUARE_1:12;
  defpred P[Nat] means Fcl(A,n) c= Fcl(A,n+$1);
A2: P[0];
A3: for k being Nat st P[k] holds P[k+1]
  proof
    let k be Nat;
    assume P[k];then
A4: Fcl(A,n) c= Fcl(A,n+k);
    Fcl(A,n+k) c= Fcl(A,n+k+1) by FINTOPO3:25;
    hence P[k+1] by A4,XBOOLE_1:1;
  end;
  for m2 being Nat holds P[m2] from NAT_1:sch 1(A2,A3);then
A6: P[m-'n];
  n+(m-'n)=n+(m-n) by A1,BINARITH:def 3
         .=m;
  hence Fcl(A,n) c= Fcl(A,m) by A6;
end;

theorem
  for FT being filled (non empty FT_Space_Str),
      A being Subset of FT, n,m being Nat st
    n<=m holds Fdfl(A,m) c= Fdfl(A,n)
proof
  let FT be filled (non empty FT_Space_Str),A be Subset of FT, n,m be Nat;
  assume n<=m;then
A1: m-n>=0 by SQUARE_1:12;
  defpred P[Nat] means Fdfl(A,n+$1) c= Fdfl(A,n);
A2: P[0];
A3: for k being Nat st P[k] holds P[k+1]
  proof let k be Nat;
    assume P[k];then
A4: Fdfl(A,n+k) c= Fdfl(A,n);
    Fdfl(A,n+k+1) c= Fdfl(A,n+k) by FINTOPO3:44;
    hence P[k+1] by A4,XBOOLE_1:1;
  end;
  for m2 being Nat holds P[m2] from NAT_1:sch 1(A2,A3);then
A6: P[m-'n];
  n+(m-'n)=n+(m-n) by A1,BINARITH:def 3
         .=m;
  hence Fdfl(A,m) c= Fdfl(A,n) by A6;
end;

theorem
  for FT being filled (non empty FT_Space_Str), A being Subset of FT,
      n,m being Nat st
    n<=m holds Fint(A,m) c= Fint(A,n)
proof
  let FT be filled (non empty FT_Space_Str),A be Subset of FT, n,m be Nat;
  assume n<=m;then
A1: m-n>=0 by SQUARE_1:12;
  defpred P[Nat] means Fint(A,n+$1) c= Fint(A,n);
A2: P[0];
A3: for k being Nat st P[k] holds P[k+1]
  proof
    let k be Nat;
    assume P[k];then
A4: Fint(A,n+k) c= Fint(A,n);
    Fint(A,n+k+1) c= Fint(A,n+k) by FINTOPO3:26;
    hence P[k+1] by A4,XBOOLE_1:1;
  end;
  for m2 being Nat holds P[m2] from NAT_1:sch 1(A2,A3);then
A6: P[m-'n];
  n+(m-'n)=n+(m-n) by A1,BINARITH:def 3
         .=m;
  hence Fint(A,m) c= Fint(A,n) by A6;
end;

theorem
  for FT being non empty FT_Space_Str,A,B being Subset of FT st
    A,B are_separated holds B,A are_separated
proof
  let FT be non empty FT_Space_Str,A,B be Subset of FT;
  assume A,B are_separated;then
  A^b misses B & A misses (B^b) by AA002;
  hence B,A are_separated by AA002;
end;

theorem AA20:
  for FT being filled (non empty FT_Space_Str),
      A,B being Subset of FT st
    A,B are_separated holds A misses B
proof
  let FT be filled (non empty FT_Space_Str),
  A,B be Subset of FT;
  assume A,B are_separated;then
A1: A^b misses B & A misses (B^b) by AA002;
  A c= A^b by FIN_TOPO:18;
  hence A misses B by A1,XBOOLE_1:63;
end;

theorem 
  for FT being non empty FT_Space_Str,
      A,B being Subset of FT st
    FT is symmetric holds 
    A,B are_separated iff A^f misses B & A misses (B^f)
proof
  let FT be non empty FT_Space_Str,
  A,B be Subset of FT;
  assume A1: FT is symmetric;then
A2: A^b=A^f by FIN_TOPO:17;
    B^b=B^f by FIN_TOPO:17,A1;
  hence thesis by A2,AA002;
end;

theorem AA60:
  for FT being filled (non empty FT_Space_Str),
      A,B being Subset of FT st
    FT is symmetric & A^b misses B holds A misses (B^b)
proof
  let FT be filled (non empty FT_Space_Str),
  A,B be Subset of FT;
  assume A1: FT is symmetric & A^b misses B;
  now assume A2: A meets (B^b);
    consider x being set such that
A3: x in A & x in B^b by XBOOLE_0:3,A2;
A4: A^b= {x2 where x2 is Element of FT:U_FT x2 meets A}
                                        by FIN_TOPO:def 10;
    B^b= {x2 where x2 is Element of FT:U_FT x2 meets B}
                                    by FIN_TOPO:def 10;then
    consider x2 being Element of FT such that
A5: x2=x & U_FT x2 meets B by A3;
A6: U_FT x2 /\ B <>{} by XBOOLE_0:def 7,A5;
    consider y being Element of U_FT x2 /\ B;
A7: y in U_FT x2 & y in B by XBOOLE_0:def 3,A6;then
    reconsider y2=y as Element of FT;
    x2 in U_FT y2 by A1,A7,FIN_TOPO:def 14;then
    x2 in U_FT y2 /\ A by A3,A5,XBOOLE_0:def 3;then
    U_FT y2 meets A by XBOOLE_0:def 7;then
    y2 in A^b by A4;
    hence contradiction by A1,A7,XBOOLE_0:3;
  end;
  hence thesis;
end;

theorem AA70:
  for FT being filled (non empty FT_Space_Str),
      A,B being Subset of FT st
    FT is symmetric & A misses (B^b) holds A^b misses B by AA60;

theorem
  for FT being filled (non empty FT_Space_Str),
      A,B being Subset of FT st
    FT is symmetric holds
    A,B are_separated iff A^b misses B
proof
  let FT be filled (non empty FT_Space_Str),
  A,B be Subset of FT;
  assume A1: FT is symmetric;
  thus A,B are_separated implies A^b misses B by AA002;
  thus A^b misses B implies A,B are_separated
  proof assume A2: A^b misses B;then
    A misses (B^b) by AA60,A1;
    hence A,B are_separated by AA002,A2;
  end;
end;

theorem
  for FT being filled (non empty FT_Space_Str),
      A,B being Subset of FT st
    FT is symmetric holds
    A,B are_separated iff A misses (B^b)
proof
  let FT be filled (non empty FT_Space_Str),
  A,B be Subset of FT;
  assume A1: FT is symmetric;
  thus A,B are_separated implies A misses (B^b) by AA002;
  thus A misses (B^b) implies A,B are_separated
  proof
    assume A2: A misses (B^b);then
    A^b misses B by AA70,A1;
    hence A,B are_separated by AA002,A2;
  end;
end;

theorem AA80:
  for FT being filled (non empty FT_Space_Str),
      IT being Subset of FT st
    FT is symmetric holds IT is connected iff
   (for A, B being Subset of FT st
      IT = A \/ B & A,B are_separated holds
      A = IT or B = IT)
proof
  let FT be filled(non empty FT_Space_Str),IT be Subset of FT;
  assume A1: FT is symmetric;
A2: now assume IT is connected;then
A3: for A,B being Subset of FT st
      IT = A \/ B & A <> {} & B <> {} & A misses B holds
      A^b meets B by FIN_TOPO:def 17;
    thus
    (for A, B being Subset of FT st
       IT = A \/ B & A,B are_separated holds A = IT or B = IT)
    proof
      let A, B be Subset of FT;
      assume A4: IT = A \/ B & A,B are_separated;then
A5:   A misses B by AA20;
      now assume A7: A<>{};
        now assume B<>{};then
A8:       A^b meets B by A3,A7,A5,A4;
          hence contradiction by AA002,A4;
        end;
        hence A = IT or B = IT by A4;
      end;
      hence A = IT or B = IT by A4;
    end;
  end;
  now assume
A9: (for A, B being Subset of FT st
       IT = A \/ B & A,B are_separated holds
       A = IT or B = IT);
    for A,B being Subset of FT st
      IT = A \/ B & A <> {} & B <> {} & A misses B holds
      A^b meets B
    proof
      let A,B be Subset of FT;
      assume A10: IT = A \/ B & A <> {} & B <> {} & A misses B;
      now assume A11:A^b misses B; then
a11:    A^b /\ B={} by XBOOLE_0:def 7;
        A c= A^b by FIN_TOPO:18;then
        A /\ B c= A^b /\ B by XBOOLE_1:26;then
A12:    A /\ B ={} by a11,XBOOLE_1:3;
        now assume A13: A meets (B^b);
          consider x being set such that
A14:      x in A & x in B^b by XBOOLE_0:3,A13;
A15:      A^b= {x2 where x2 is Element of FT:U_FT x2 meets A}
                                     by FIN_TOPO:def 10;
          B^b= {x2 where x2 is Element of FT:U_FT x2 meets B}
                                     by FIN_TOPO:def 10;then
          consider x2 being Element of FT such that
A16:      x2=x & U_FT x2 meets B by A14;
A17:      U_FT x2 /\ B <>{} by XBOOLE_0:def 7,A16;
          consider y being Element of U_FT x2 /\ B;
A18:      y in U_FT x2 & y in B by XBOOLE_0:def 3,A17;then
          reconsider y2=y as Element of FT;
          x2 in U_FT y2 by A1,A18,FIN_TOPO:def 14;then
          x2 in U_FT y2 /\ A by A14,A16,XBOOLE_0:def 3;then
          U_FT y2 meets A by XBOOLE_0:def 7;then
          y2 in A^b by A15;
          hence contradiction by A11,A18,XBOOLE_0:3;
        end;then
        A,B are_separated by AA002,A11;then
        A=IT or B=IT by A9,A10;
        hence contradiction by A10,A12,XBOOLE_1:21;
      end;
      hence A^b meets B;
    end;
    hence IT is connected by FIN_TOPO:def 17;
  end;
  hence thesis by A2;
end;

theorem
  for FT being filled (non empty FT_Space_Str),
      B being Subset of FT st FT is symmetric holds
    B is connected iff
    not (ex C being Subset of FT st
    C<>{} & B\C <>{} & C c= B & (C^b) misses (B\C))
proof
  let FT be filled (non empty FT_Space_Str),
  B be Subset of FT;
  assume A1: FT is symmetric;
  thus B is connected implies not (ex C being Subset of FT st
    C<>{} & B\C <>{} & C c= B & (C^b) misses (B\C))
  proof
    assume A2: B is connected;
    for C being Subset of FT st C c= B & (C^b) misses (B\C) holds
      C={} or B\C={}
    proof
      let C be Subset of FT;
      assume A3: C c= B & (C^b) misses (B\C);
A4:   C \/ (B\C)=C \/ B by XBOOLE_1:39
               .=B by A3,XBOOLE_1:12;
      C^b misses (B\C) & C misses ((B\C)^b) by A3,AA60,A1;then
      C,B\C are_separated by AA002;then
      C = B or B\C = B by AA80,A2,A4,A1;
      hence C={} or B\C={} by A3,XBOOLE_1:38,XBOOLE_1:37;
    end;
    hence not (ex C being Subset of FT st C<>{} & B\C <>{} &
    C c= B & (C^b) misses (B\C));
  end;
  thus not (ex C being Subset of FT st C<>{} & B\C <>{} &
   C c= B & (C^b) misses (B\C)) implies B is connected
  proof
    assume not (ex C being Subset of FT st
      C<>{} & B\C <>{} & C c= B & (C^b) misses (B\C));then
A5: for C being Subset of FT st
      C c= B & (C^b) misses (B\C) holds C={} or B\C={};
    for A, B2 being Subset of FT st
      B = A \/ B2 & A,B2 are_separated holds
    A = B or B2 = B
    proof
      let A, B2 be Subset of FT;
      assume A6: B = A \/ B2 & A,B2 are_separated;then
A7:   A c= B by XBOOLE_1:7;
A8:   A^b misses B2 & A misses (B2^b) by A6,AA002;
A9:   (A \/ B2) \A =B2\A by XBOOLE_1:40;
      B\A c= B2 by A9,A6,XBOOLE_1:36;then
      A^b misses (B\A) by A8,XBOOLE_1:63;then
      A={} or B\A={} by A5,A7;then
      B=B2 or B c= A by XBOOLE_1:37,A6;
      hence A = B or B2 = B by A7,XBOOLE_0:def 10;
    end;
    hence B is connected by AA80,A1;
  end;
end;

definition
  let FT1,FT2 be non empty FT_Space_Str,
  f be Function of (the carrier of FT1),(the carrier of FT2),n be Nat;
  pred f is_continuous n means
:AA600: for x being Element of FT1,y being Element of FT2 st
   x in the carrier of FT1 & y=f.x holds
  f.:( U_FT(x,0)) c= U_FT(y,n);
end;

theorem
  for FT1 being non empty FT_Space_Str,
      FT2 being filled (non empty FT_Space_Str), n being Nat,
      f being Function of (the carrier of FT1),(the carrier of FT2) st
    f is_continuous 0 holds f is_continuous n
proof
  let FT1 be non empty FT_Space_Str,
  FT2 be filled (non empty FT_Space_Str),n be Nat,
  f be Function of (the carrier of FT1),(the carrier of FT2);
  assume f is_continuous 0;then
A1: for x being Element of FT1,y being Element of FT2 st
      x in the carrier of FT1 & y=f.x holds
      f.:( U_FT(x,0)) c= U_FT(y,0) by AA600;
  for x being Element of FT1,y being Element of FT2 st
      x in the carrier of FT1 & y=f.x holds
    f.:( U_FT(x,0)) c= U_FT(y,n)
  proof
    let x be Element of FT1,y be Element of FT2;
    assume A2: x in the carrier of FT1 & y=f.x;
A3: U_FT y =U_FT(y,0) by FINTOPO3:47;
    U_FT(y,n)=Finf((U_FT y),n) by FINTOPO3:def 10;then
A4: U_FT(y,0) c= U_FT(y,n) by A3,FINTOPO3:36;
    f.:( U_FT(x,0)) c= U_FT(y,0) by A1,A2;
    hence f.:( U_FT(x,0)) c= U_FT(y,n) by A4,XBOOLE_1:1;
  end;
  hence f is_continuous n by AA600;
end;

theorem
  for FT1 being non empty FT_Space_Str,
      FT2 being filled (non empty FT_Space_Str), n0,n being Nat,
      f being Function of (the carrier of FT1),(the carrier of FT2) st
    f is_continuous n0 & n0<=n holds f is_continuous n
proof
  let FT1 be non empty FT_Space_Str,
  FT2 be filled (non empty FT_Space_Str),n0,n be Nat,
  f be Function of (the carrier of FT1),(the carrier of FT2);
  assume A1: f is_continuous n0 & n0<=n;then
A2: for x being Element of FT1,y being Element of FT2 st
      x in the carrier of FT1 & y=f.x holds
   f.:( U_FT(x,0)) c= U_FT(y,n0) by AA600;
  for x being Element of FT1,y being Element of FT2 st
    x in the carrier of FT1 & y=f.x holds
    f.:( U_FT(x,0)) c= U_FT(y,n)
  proof
    let x be Element of FT1,y be Element of FT2;
    assume A3: x in the carrier of FT1 & y=f.x;
A4: U_FT(y,n0)=Finf((U_FT y),n0) by FINTOPO3:def 10;
    U_FT(y,n)=Finf((U_FT y),n) by FINTOPO3:def 10;then
A5: U_FT(y,n0) c= U_FT(y,n) by A4,AA004,A1;
    f.:( U_FT(x,0)) c= U_FT(y,n0) by A2,A3;
    hence f.:( U_FT(x,0)) c= U_FT(y,n) by A5,XBOOLE_1:1;
  end;
  hence f is_continuous n by AA600;
end;

theorem AA900:
  for FT1,FT2 being non empty FT_Space_Str,
      A being Subset of FT1,B being Subset of FT2,
      f being Function of (the carrier of FT1),(the carrier of FT2) st
    f is_continuous 0 & B=f.:A holds
    f.:(A^b) c= B^b
proof
  let FT1,FT2 be non empty FT_Space_Str,A be Subset of FT1,
  B be Subset of FT2,
  f be Function of (the carrier of FT1),(the carrier of FT2);
  assume A1: f is_continuous 0 & B=f.:A;
  thus f.:(A^b) c= B^b
  proof
    let y be set;assume y in f.:(A^b);then
    consider x being set such that
A2: x in dom f & x in A^b & y=f.x by FUNCT_1:def 12;
    reconsider x0=x as Element of FT1 by A2;
    reconsider y0=y as Element of FT2 by FUNCT_2:7,A2;
    x in {z where z is Element of FT1:U_FT z meets A}
                           by FIN_TOPO:def 10,A2;then
    consider z being Element of FT1 such that
A3: z=x & U_FT z meets A;
A4: U_FT x0 /\ A<>{} by A3,XBOOLE_0:def 7;
    f.:( U_FT(x0,0)) c= U_FT(y0,0) by A2,A1,AA600;then
    f.:(U_FT x0) c=  U_FT(y0,0) by FINTOPO3:47;then
A5: f.:(U_FT x0) c=  U_FT y0 by FINTOPO3:47;
    dom f=the carrier of FT1 by FUNCT_2:def 1;then
A6: f.:((U_FT x0) /\ A)<>{} by A4,RELAT_1:152;
A7: f.:((U_FT x0) /\ A) c= (f.:((U_FT x0)))/\ (f.:A) by RELAT_1:154;
    (f.:((U_FT x0)))/\ (f.:A) c= (U_FT y0) /\ (f.:A) by A5,XBOOLE_1:26;then
    f.:((U_FT x0) /\ A) c= (U_FT y0) /\ (f.:A) by A7,XBOOLE_1:1;then
    (U_FT y0) /\ (f.:A)<>{} by A6,XBOOLE_1:3;then
    U_FT y0 meets B by A1,XBOOLE_0:def 7;then
    y in {z2 where z2 is Element of FT2:U_FT z2 meets B};
    hence y in B^b by FIN_TOPO:def 10;
  end;
end;

theorem
  for FT1,FT2 being non empty FT_Space_Str,A being Subset of FT1,
      B being Subset of FT2,
      f being Function of (the carrier of FT1),(the carrier of FT2) st
    A is connected & f is_continuous 0 & B=f.:A holds B is connected
proof
  let FT1,FT2 be non empty FT_Space_Str,A be Subset of FT1,
  B be Subset of FT2,
  f be Function of (the carrier of FT1),(the carrier of FT2);
  assume A1: A is connected & f is_continuous 0 & B=f.:A;then
A2: for B1,C1 being Subset of FT1 st
    A = B1 \/ C1 & B1 <> {} & C1 <> {} & B1 misses C1 holds
   B1^b meets C1 by FIN_TOPO:def 17;
  for B2,C2 being Subset of FT2 st
    B = B2 \/ C2 & B2 <> {} & C2 <> {} & B2 misses C2 holds
   B2^b meets C2
  proof let B2,C2 be Subset of FT2;
    assume A4: B = B2 \/ C2 & B2 <> {} & C2 <> {} & B2 misses C2;
    reconsider B1 = f"B2 as Subset of FT1;
A5: f.:B1 c= B2
    proof
      let y be set;
      assume y in f.:B1;then
      consider x2 being set such that
A6:   x2 in dom f & x2 in B1 & y=f.x2 by FUNCT_1:def 12;
      thus y in B2 by A6,FUNCT_1:def 13;
    end;
    reconsider B20 = f.:B1 as Subset of FT2;
A7: B20^b c= B2^b by A5,FIN_TOPO:19;
    reconsider B10=B1/\A as Subset of FT1;
    reconsider C1=f"C2 as Subset of FT1;
    reconsider C10=C1 /\ A as Subset of FT1;
    A c= the carrier of FT1;then
A8: A c= dom f by FUNCT_2:def 1;
    (f"B2) \/ (f"C2)=f"(f.:A) by A1,A4,RELAT_1:175;then
    A c= B1 \/ C1 by A8,FUNCT_1:146;then
    A c= A/\(B1\/C1) by XBOOLE_1:19;then
A10: A c= B10 \/ C10 by XBOOLE_1:23;
A11: B10 c= A by XBOOLE_1:17;
    C10 c= A by XBOOLE_1:17;then
    B10 \/ C10 c= A by A11,XBOOLE_1:8;then
A12: A=B10 \/ C10 by A10,XBOOLE_0:def 10;
A13: B10 c= B1 by XBOOLE_1:17;
A14: C10 c= C1 by XBOOLE_1:17;then
A15: B10/\C10 c= B1/\C1 by A13,XBOOLE_1:27;
    consider x5 being Element of B2;
A16: x5 in B2 by A4;
    x5 in B by A4,XBOOLE_0:def 2;then
    consider z5 being set such that
A17: z5 in dom f & z5 in A & x5=f.z5 by A1,FUNCT_1:def 12;
    z5 in f"B2 by A16,A17,FUNCT_1:def 13;then
A18: B10<>{} by A17,XBOOLE_0:def 3;
    consider x6 being Element of C2;
A19: x6 in C2 by A4;
    x6 in B by A4,XBOOLE_0:def 2;then
    consider z6 being set such that
A20: z6 in dom f & z6 in A & x6=f.z6 by A1,FUNCT_1:def 12;
    z6 in f"C2 by A19,A20,FUNCT_1:def 13;then
A21: C10<>{} by A20,XBOOLE_0:def 3;
    B2 /\ C2 = {} by A4,XBOOLE_0:def 7;then
    f"(B2 /\ C2) = {} by RELAT_1:171;then
    (f"B2) /\ (f"C2) = {} by FUNCT_1:137;then
    B10 /\ C10= {} by A15,XBOOLE_1:3;then
    B10 misses C10 by XBOOLE_0:def 7;then
    B10^b meets C10 by A2,A18,A21,A12;then
A22: B10^b /\ C10 <> {} by XBOOLE_0:def 7;
    B10^b c= B1^b by FIN_TOPO:19,A13;then
    B10^b /\ C10 c= B1^b /\ C1 by A14,XBOOLE_1:27;then
A23: B1^b /\ C1 <> {} by A22,XBOOLE_1:3;
    dom f=the carrier of FT1 by FUNCT_2:def 1;then
A24: f.:(B1^b /\ C1) <> {} by A23,RELAT_1:152;
    f.:(B1^b /\ C1) c= (f.:(B1^b)) /\ f.:(C1) by RELAT_1:154;then
A25: (f.:(B1^b)) /\ (f.:C1) <> {} by A24,XBOOLE_1:3;
    f.:C1 c= C2 by FUNCT_1:145;then
A26: (f.:(B1^b)) /\ (f.:C1) c= (f.:(B1^b)) /\ C2 by XBOOLE_1:26;
    f.:(B1^b) c= B20^b by A1,AA900;then
    f.:(B1^b) c= B2^b by A7,XBOOLE_1:1;then
    (f.:(B1^b)) /\ C2 c= B2^b /\ C2 by XBOOLE_1:26;then
    (f.:(B1^b)) /\ (f.:C1) c= B2^b /\ C2 by A26,XBOOLE_1:1;then
    B2^b /\ C2 <>{} by A25,XBOOLE_1:3;
    hence B2^b meets C2 by XBOOLE_0:def 7;
  end;
  hence B is connected by FIN_TOPO:def 17;
end;

::1 dimensional linear FT_Str
definition let n be Nat;
  func Nbdl1 n -> Function of Seg n,bool (Seg n) means
:AA2010: dom it =Seg n & for i being Nat st
   i in Seg n holds it.i={i,max(i-'1,1),min(i+1,n)};
existence
  proof
    defpred P[set,set] means for i being Nat st i=$1 holds
    $2={i,max(i-'1,1),min(i+1,n)};
A1: for x being set st x in Seg n 
        ex y being set st y in bool (Seg n) & P[x,y]
    proof
      let x being set;
      assume A2: x in Seg n;then
      reconsider i=x as Nat;
      n>0 by NAT_1:19,FINSEQ_1:4,A2;then
A3:   0+1<=n by NAT_1:38;
      set y0={i,max(i-'1,1),min(i+1,n)};
      y0 c=Seg n
      proof
        let z1 being set;
        assume z1 in y0;then
A4:     z1=i or z1=max(i-'1,1) or z1=min(i+1,n) by ENUMSET1:def 1;
A6:     now assume A7: z1=max(i-'1,1);
A9:       i-'1<=i by GOBOARD9:2;
          i<=n by A2,FINSEQ_1:3;then
          i-'1<=n by A9,AXIOMS:22;then
          1<= max(i-'1,1) & max(i-'1,1)<=n by SQUARE_1:50,A3;
          hence z1 in Seg n by A7,FINSEQ_1:3;
        end;
        now assume A12: z1=min(i+1,n);
          1<=i+1 by NAT_1:37;then
A13:      1<=min(i+1,n) by A3,SQUARE_1:39;
          min(i+1,n)<=n by SQUARE_1:35;
          hence z1 in Seg n by A12,FINSEQ_1:3,A13;
        end;
        hence z1 in Seg n by A2,A4,A6;
      end;then
A14:  y0 in bool (Seg n);
      P[x,y0];
      hence ex y being set st y in bool (Seg n) & P[x,y] by A14;
    end;
    consider f2 being Function of Seg n,bool(Seg n) such that
A15: for x3 being set st
    x3 in Seg n holds P[x3,f2.x3] from FUNCT_2:sch 1(A1);
A16: for i being Nat st i in Seg n holds
    f2.i={i,max(i-'1,1),min(i+1,n)} by A15;
    dom f2=Seg n by FUNCT_2:def 1;
    hence ex f being Function of Seg n,bool (Seg n) st
      dom f =Seg n & for i being Nat st i in Seg n holds
      f.i={i,max(i-'1,1),min(i+1,n)} by A16;
  end;
uniqueness
  proof
    thus for f1,f2 being Function of Seg n,bool (Seg n) st
     (dom f1 =Seg n & for i being Nat st i in Seg n holds
      f1.i={i,max(i-'1,1),min(i+1,n)})&
     (dom f2 =Seg n & for i being Nat st i in Seg n holds
      f2.i={i,max(i-'1,1),min(i+1,n)})
     holds f1=f2
    proof
      let f1,f2 be Function of Seg n,bool (Seg n);
      assume A17: (dom f1 =Seg n & for i being Nat st i in Seg n holds
      f1.i={i,max(i-'1,1),min(i+1,n)})&
      (dom f2 =Seg n & for i being Nat st i in Seg n holds
      f2.i={i,max(i-'1,1),min(i+1,n)});
      for x being set st x in dom f1 holds f1.x=f2.x
      proof
        let x be set;
        assume A18: x in dom f1;then
        reconsider i2=x as Nat by A17;
        f1.i2={i2,max(i2-'1,1),min(i2+1,n)} by A17,A18;
        hence f1.x=f2.x by A17,A18;
      end;
      hence f1=f2 by A17,FUNCT_1:9;
    end;
  end;
end;

definition let n be Nat;
 assume A1: n>0;
 func FTSL1 n -> non empty FT_Space_Str equals
:AA100:  FT_Space_Str(# Seg n,Nbdl1 n #);
correctness
  proof
    0+1<=n by NAT_1:38,A1;then
    Seg n<>{} by FINSEQ_1:3;
    hence FT_Space_Str(# Seg n,Nbdl1 n #) is non empty FT_Space_Str
                                                 by STRUCT_0:def 1;
  end;
end;

theorem
  for n being Nat st n>0 holds (FTSL1 n) is filled
proof
  let n be Nat;assume n>0;then
  A1: (FTSL1 n)=FT_Space_Str(# Seg n,Nbdl1 n #) by AA100;
  for x being Element of (FTSL1 n) holds x in U_FT x
  proof let x be Element of (FTSL1 n);
    x in Seg n by A1;then
    reconsider i=x as Nat;
    (the Nbd of (FTSL1 n)).x={i,max(i-'1,1),min(i+1,n)} by AA2010,A1;then
    U_FT x= {i,max(i-'1,1),min(i+1,n)} by FIN_TOPO:def 1;
    hence x in U_FT x by ENUMSET1:def 1;
  end;
  hence (FTSL1 n) is filled by FIN_TOPO:def 3;
end;

theorem
  for n being Nat st n>0 holds (FTSL1 n) is symmetric
proof
  let n be Nat;
  assume n>0;then
A1: (FTSL1 n)=FT_Space_Str(# Seg n,Nbdl1 n #) by AA100;
  for x, y being Element of (FTSL1 n) holds
    y in U_FT x implies x in U_FT y
  proof
    let x, y be Element of (FTSL1 n);
A2: x in Seg n by A1;then
    reconsider i=x as Nat;
A3: 1<=i & i<=n by A2,FINSEQ_1:3;
    y in Seg n by A1;then
    reconsider j=y as Nat;
    (the Nbd of (FTSL1 n)).x={i,max(i-'1,1),min(i+1,n)} by AA2010,A1;then
A6: U_FT x= {i,max(i-'1,1),min(i+1,n)} by FIN_TOPO:def 1;
    (the Nbd of (FTSL1 n)).y={j,max(j-'1,1),min(j+1,n)}
                                 by A1,AA2010;then
A7: U_FT y= {j,max(j-'1,1),min(j+1,n)} by FIN_TOPO:def 1;
    now assume A8: y in U_FT x;then
A10: y=i or y=max(i-'1,1) or y=min(i+1,n) by ENUMSET1:def 1,A6;
A12: now assume A13: y=max(i-'1,1);
      now per cases;
        case A14: i-'1>=1;then
A15:      y=i-'1 by A13,SQUARE_1:def 2;
          now per cases;
            case i-1>=0;then 
              j=i-1 by A15,BINARITH:def 3;
              hence x =j or x=max(j-'1,1) or x= min(j+1,n)
                                     by SQUARE_1:def 1,A3;
              end;
              case i-1<0;then i-'1=0 by BINARITH:def 3;
                hence contradiction by A14;
              end;
            end;
            hence x =j or x=max(j-'1,1) or x= min(j+1,n);
          end;
          case A17: i-'1<1;then
A18:        y=1 by A13,SQUARE_1:def 2;
            now assume i>1;then
A19:          i-1>0 by SQUARE_1:11;then
              i-'1=i-1 by BINARITH:def 3;then
              i-'1>=0+1 by A19,NAT_1:38;
              hence contradiction by A17;
            end;
            hence x =j or x=max(j-'1,1) or x= min(j+1,n) by A18,A3,AXIOMS:21;
          end;
        end;
        hence x in U_FT y by A7,ENUMSET1:def 1;
      end;
      now assume y=min(i+1,n);
A20:    y=i or y=max(i-'1,1) or y=min(i+1,n) by ENUMSET1:def 1,A6,A8;
        now per cases by A20;
          case y=i;
            hence x =j or x=max(j-'1,1) or x= min(j+1,n);
          end;
          case A21: y=max(i-'1,1);
            now per cases;
              case A22: i-'1>=1;then
A23:            y=i-'1 by A21,SQUARE_1:def 2;
                now per cases;
                  case i-1>=0;then
                   j=i-1 by A23,BINARITH:def 3;
                   hence x =j or x=max(j-'1,1) or x= min(j+1,n)
                                           by SQUARE_1:def 1,A3;
                  end;
                  case i-1<0;then i-'1=0 by BINARITH:def 3;
                    hence contradiction by A22;
                  end;
                end;
                hence x =j or x=max(j-'1,1) or x= min(j+1,n);
              end;
              case A24: i-'1<1;then
A25:            y=1 by A21,SQUARE_1:def 2;
                now assume i>1;then
A26:               i-1>0 by SQUARE_1:11;then
                  i-'1=i-1 by BINARITH:def 3;then
                  i-'1>=0+1 by A26,NAT_1:38;
                  hence contradiction by A24;
                end;
                hence x =j or x=max(j-'1,1) or x= min(j+1,n)
                                           by A25,A3,AXIOMS:21;
              end;
            end;
            hence x =j or x=max(j-'1,1) or x= min(j+1,n);
          end;
          case A27: y=min(i+1,n);
            now per cases;
              case i+1<=n;then
                y=i+1 by A27,SQUARE_1:def 1;then
A28:            j-1=i by XCMPLX_1:26;then
                j-1>0 by A3,AXIOMS:22;then
A29:            j-1=j-'1 by BINARITH:def 3;
                now per cases;
                  case j-1>=1;
                    hence x =j or x=max(j-'1,1) or x= min(j+1,n)
                                    by A29,A28,SQUARE_1:def 2;
                  end;
                  case j-1<1;
                    hence contradiction by A3,A28;
                  end;
                end;
                hence x =j or x=max(j-'1,1) or x= min(j+1,n);
              end;
              case A30: i+1>n;then
                y=n by A27,SQUARE_1:def 1;then
                j+1>n by NAT_1:38;then
A31:            min(j+1,n)=n by SQUARE_1:def 1;
                i>=n by NAT_1:38,A30;
                hence x =j or x=max(j-'1,1) or x= min(j+1,n)
                                           by A31,A3,AXIOMS:21;
              end;
            end;
            hence x =j or x=max(j-'1,1) or x= min(j+1,n);
          end;
        end;
        hence x in U_FT y by A7,ENUMSET1:def 1;
      end;
      hence x in U_FT y by A10,A8,A12;
    end;
    hence y in U_FT x implies x in U_FT y;
  end;
  hence (FTSL1 n) is symmetric by FIN_TOPO:def 14;
end;

::1 dimensional cyclic FT_Str
definition let n be Nat;
  func Nbdc1 n -> Function of Seg n,bool (Seg n) means
:AA2010: dom it =Seg n & for i being Nat st i in Seg n holds
   (1<i & i<n implies it.i={i,i-'1,i+1})&
   (i=1 & i<n implies it.i={i,n,i+1}) &
   (1<i & i=n implies it.i={i,i-'1,1}) &
   (i=1 & i=n implies it.i={i});
existence
  proof
    defpred P[set,set] means for i being Nat st i=$1 holds
    (1<i & i<n implies $2={i,i-'1,i+1})&
    (i=1 & i<n implies $2={i,n,i+1}) &
    (1<i & i=n implies $2={i,i-'1,1}) &
    (i=1 & i=n implies $2={i});
A1: for x being set st x in Seg n ex y being set st y in bool (Seg n) & P[x,y]
  proof
    let x be set;
    assume A2: x in Seg n;then
    reconsider i2=x as Nat;
A3: 1<=i2 & i2<=n by A2,FINSEQ_1:3;then
A4: 1<i2 or i2=1 by REAL_1:def 5;
A5: i2<n or i2=n by A3,REAL_1:def 5;
    now per cases by A4,A5;
      case A6: 1<i2 & i2<n;
        set y0={i2,i2-'1,i2+1};
        i2-1>0 by A6,SQUARE_1:11;then
        i2-'1>0 by BINARITH:def 3;then
A7:     i2-'1>=0+1 by NAT_1:38;
        i2-'1<=i2 by GOBOARD9:2;then
        i2-'1<n by A6,AXIOMS:22;then
A8:     i2-'1 in Seg n by A7,FINSEQ_1:3;
A9:     i2+1<=n by A6,NAT_1:38;
        i2<=i2+1 by NAT_1:37;then
        1<i2+1 by A6,AXIOMS:22;then
A10:    i2+1 in Seg n by A9,FINSEQ_1:3;
        y0 c= Seg n
        proof let z1 being set;
          assume z1 in y0;
          hence z1 in Seg n by A2,A8,A10,ENUMSET1:def 1;
        end;then
A11:    y0 in bool (Seg n);
        P[x,y0] by A6;
        hence ex y being set st y in bool (Seg n) & P[x,y] by A11;
      end;
      case A12: i2=1 & i2<n;
        set y0={i2,n,i2+1};
A13:    n in Seg n by A12,FINSEQ_1:3;
A14:    i2+1<=n by A12,NAT_1:38;
A15:    i2+1 in Seg n by A14,FINSEQ_1:3,A12;
        y0 c= Seg n
        proof let z1 being set;
          assume z1 in y0;
          hence z1 in Seg n by A2,A13,A15,ENUMSET1:def 1;
        end;then
A16:    y0 in bool (Seg n);
        P[x,y0] by A12;
        hence ex y being set st y in bool (Seg n) & P[x,y] by A16;
      end;
      case A17: 1<i2 & i2=n;
        set y0={i2,i2-'1,1};
        i2-1>0 by A17,SQUARE_1:11;then
        i2-'1>0 by BINARITH:def 3;then
A18:    i2-'1>=0+1 by NAT_1:38;
        i2-'1<=n by A17,GOBOARD9:2;then
A19:    i2-'1 in Seg n by A18,FINSEQ_1:3;
A20:    1 in Seg n by A17,FINSEQ_1:3;
        y0 c= Seg n
        proof
          let z1 being set;
          assume z1 in y0;
          hence z1 in Seg n by A2,A19,A20,ENUMSET1:def 1;
        end;then
A21:    y0 in bool (Seg n);
        P[x,y0] by A17;
        hence ex y being set st y in bool (Seg n) & P[x,y] by A21;
      end;
      case A22: i2=1 & i2=n;
        set y0={i2};
        y0 c= Seg n
        proof
          let z1 be set;
          assume z1 in y0;
          hence z1 in Seg n by A2,TARSKI:def 1;
        end;then
A23:    y0 in bool (Seg n);
        P[x,y0] by A22;
        hence ex y being set st y in bool (Seg n) & P[x,y] by A23;
      end;
    end;
    hence ex y being set st y in bool (Seg n) & P[x,y];
  end;
  consider f2 being Function of Seg n,bool(Seg n) such that
A24: for x3 being set st
    x3 in Seg n holds P[x3,f2.x3] from FUNCT_2:sch 1(A1);
A25: for i being Nat st i in Seg n holds
   (1<i & i<n implies f2.i={i,i-'1,i+1})&
   (i=1 & i<n implies f2.i={i,n,i+1}) &
   (1<i & i=n implies f2.i={i,i-'1,1}) &
   (i=1 & i=n implies f2.i={i}) by A24;
  dom f2=Seg n by FUNCT_2:def 1;
  hence ex f being Function of Seg n,bool (Seg n) st
   dom f =Seg n & for i being Nat st i in Seg n holds
   (1<i & i<n implies f.i={i,i-'1,i+1})&
   (i=1 & i<n implies f.i={i,n,i+1}) &
   (1<i & i=n implies f.i={i,i-'1,1}) &
   (i=1 & i=n implies f.i={i}) by A25;
  end;
uniqueness
  proof
    let f1,f2 be Function of Seg n,bool (Seg n);
    assume A26:
    (dom f1 =Seg n & for i being Nat st i in Seg n holds
     (1<i & i<n implies f1.i={i,i-'1,i+1})&
     (i=1 & i<n implies f1.i={i,n,i+1}) &
     (1<i & i=n implies f1.i={i,i-'1,1}) &
     (i=1 & i=n implies f1.i={i}))&
    (dom f2 =Seg n & for i being Nat st i in Seg n holds
     (1<i & i<n implies f2.i={i,i-'1,i+1})&
     (i=1 & i<n implies f2.i={i,n,i+1}) &
     (1<i & i=n implies f2.i={i,i-'1,1}) &
     (i=1 & i=n implies f2.i={i}));
    for x being set st x in dom f1 holds f1.x=f2.x
    proof
      let x be set;
      assume A27: x in dom f1;then
      reconsider i2=x as Nat by A26;
A28:  1<=i2 & i2<=n by A26,A27,FINSEQ_1:3;then
A29:  1<i2 or i2=1 by REAL_1:def 5;
A30:  i2<n or i2=n by A28,REAL_1:def 5;
      now per cases by A29,A30;
        case A31: 1<i2 & i2<n;
          set y0={i2,i2-'1,i2+1};
          f1.x=y0 by A26,A27,A31;
          hence f1.x=f2.x by A27,A26,A31;
        end;
        case A32: i2=1 & i2<n;
          set y0={i2,n,i2+1};
          f1.x=y0 by A26,A32,A27;
          hence f1.x=f2.x by A27,A26,A32;
        end;
        case A33: 1<i2 & i2=n;
          set y0={i2,i2-'1,1};
          f1.x=y0 by A26,A33,A27;
          hence f1.x=f2.x by A26,A33,A27;
        end;
        case A34: i2=1 & i2=n;
          set y0={i2};
          f1.x=y0 by A26,A34,A27;
          hence f1.x=f2.x by A26,A34,A27;
        end;
      end;
      hence f1.x=f2.x;
    end;
    hence f1=f2 by A26,FUNCT_1:9;
  end;
end;

definition let n be Nat;
  assume A1: n>0;
  func FTSC1 n -> non empty FT_Space_Str equals
:AA200: FT_Space_Str(# Seg n,Nbdc1 n #);
correctness
  proof
    0+1<=n by A1,NAT_1:38;then
    Seg n<>{} by FINSEQ_1:3;
    hence FT_Space_Str(# Seg n,Nbdc1 n #) is non empty FT_Space_Str
                                              by STRUCT_0:def 1;
  end;
end;

theorem
  for n being Nat st n>0 holds (FTSC1 n) is filled
proof
  let n be Nat;assume n>0;then
A1: (FTSC1 n)=FT_Space_Str(# Seg n,Nbdc1 n #) by AA200;
  for x being Element of (FTSC1 n) holds x in U_FT x
  proof
    let x be Element of (FTSC1 n);
A2: x in Seg n by A1;then
    reconsider i=x as Nat;
A3: the Nbd of (FTSC1 n)=Nbdc1 n by A1;
    set f=Nbdc1 n;
A4: 1<=i & i<=n by A2,FINSEQ_1:3;then
A5: 1<i or i=1 by REAL_1:def 5;
A6: i<n or i=n by A4,REAL_1:def 5;
A7: (1<i & i<n implies f.i={i,i-'1,i+1})&
    (i=1 & i<n implies f.i={i,n,i+1}) &
    (1<i & i=n implies f.i={i,i-'1,1}) &
    (i=1 & i=n implies f.i={i}) by A2,AA2010;
    now per cases by A5,A6;
      case 1<i & i<n;then
        U_FT x= {i,i-'1,i+1} by FIN_TOPO:def 1,A3,A7;
        hence x in U_FT x by ENUMSET1:def 1;
      end;
      case i=1 & i<n;then
        U_FT x= {i,n,i+1} by FIN_TOPO:def 1,A3,A7;
        hence x in U_FT x by ENUMSET1:def 1;
      end;
      case 1<i & i=n;then
        U_FT x= {i,i-'1,1} by FIN_TOPO:def 1,A3,A7;
        hence x in U_FT x by ENUMSET1:def 1;
      end;
      case i=1 & i=n;then
        U_FT x= {i} by FIN_TOPO:def 1,A3,A7;
        hence x in U_FT x by TARSKI:def 1;
      end;
    end;
    hence x in U_FT x;
  end;
  hence (FTSC1 n) is filled by FIN_TOPO:def 3;
end;

theorem
  for n being Nat st n>0 holds (FTSC1 n) is symmetric
proof
  let n be Nat;
  assume n>0;then
A1: (FTSC1 n)=FT_Space_Str(# Seg n,Nbdc1 n #) by AA200;
  for x, y being Element of (FTSC1 n) holds
    y in U_FT x implies x in U_FT y
  proof
    let x, y be Element of (FTSC1 n);
A2: x in Seg n by A1;then
    reconsider i=x as Nat;
A3: y in Seg n by A1;then
    reconsider j=y as Nat;
A4: 1<=j & j<=n by A3,FINSEQ_1:3;
A5: the Nbd of (FTSC1 n)=Nbdc1 n by A1;
    set f=Nbdc1 n;
A6: 1<=i & i<=n by A2,FINSEQ_1:3;then
A7: 1<i or i=1 by REAL_1:def 5;
A8: i<n or i=n by A6,REAL_1:def 5;
A9: (1<i & i<n implies f.i={i,i-'1,i+1})&
    (i=1 & i<n implies f.i={i,n,i+1}) &
    (1<i & i=n implies f.i={i,i-'1,1}) &
    (i=1 & i=n implies f.i={i}) by A2,AA2010;
    now assume A10: y in U_FT x;
      now per cases by A7,A8;
        case A11: 1<i & i<n;then
          U_FT x= {i,i-'1,i+1} by FIN_TOPO:def 1,A5,A9;then
A14:      y=i or y=i-'1 or y=i+1 by ENUMSET1:def 1,A10;
A17:      now assume A18: y=i-'1;
            i-1>0 by A11,SQUARE_1:11;then
            i-'1=i-1 by BINARITH:def 3;then
A19:        j+1=i by A18;
A20:        x=j or x=j-'1 or x=j+1 by A19;
            now per cases by A20;
              case A21: x=j;then
                (the Nbd of (FTSC1 n)).y={j,j-'1,j+1}
                            by A5,A11,AA2010,A3;then
                U_FT y = {j,j-'1,j+1} by FIN_TOPO:def 1;
                hence x in U_FT y by A21,ENUMSET1:def 1;
              end;
              case x=j-'1;then
A22:            i=(i-'1)-'1 by A18;
                now assume i<>0;then i>0 by NAT_1:19;then
A24:              i>=0+1 by NAT_1:38;then
                  i-1>=1-1 by REAL_1:49;then
A25:              i-1=i-'1 by BINARITH:def 3;
                  now assume A26: i=1;then
A27:                i-1=0;
                    i-'1-1<0 by A27,A25;
                    hence contradiction by A26,A22,BINARITH:def 3;
                  end;then
                  i>1 by A24,REAL_1:def 5;then
                  i-1>1-1 by REAL_1:54;then
                  i-'1>=0+1 by NAT_1:38,A25;then
                  i-'1-1>=0 by SQUARE_1:12;then
                  i-'1-'1=i-'1-1 by BINARITH:def 3;then
                  i+1=i-1 by A22,A25;then
A28:              i+1+1=i;
                  i<i+1 by NAT_1:38;
                  hence contradiction by A28,NAT_1:38;
                end;
                hence x in U_FT y by A11;
              end;
              case x=j+1;then
                i>0 by NAT_1:19;then
                i>=0+1 by NAT_1:38;then
                i-1>=0 by SQUARE_1:12;then
                y=i-1 by A18,BINARITH:def 3;then
A33:            j+1=x by XCMPLX_1:27;then
A34:            j<n by A6,NAT_1:38;
A35:            now assume j=1;then
                  (the Nbd of (FTSC1 n)).y={j,n,j+1}
                           by A5,AA2010,A3,A34;then
                  U_FT y = {j,n,j+1} by FIN_TOPO:def 1;
                  hence x in U_FT y by A33,ENUMSET1:def 1;
                end;
                now assume j<>1;then
                  j>1 by REAL_1:def 5,A4;then
                  (the Nbd of (FTSC1 n)).y={j,j-'1,j+1}
                              by A34,A5,AA2010,A3;then
                  U_FT y = {j,j-'1,j+1} by FIN_TOPO:def 1;
                  hence x in U_FT y by A33,ENUMSET1:def 1;
                end;
                hence x in U_FT y by A35;
              end;
            end;
            hence x in U_FT y;
          end;
          now assume 
A36:        y=i+1;then
A37:        j-1=x by XCMPLX_1:26;then 
            j-1>0 by A11,AXIOMS:22;then
A38:        x=j or x=j-'1 or x=j+1 by A37,BINARITH:def 3;
            now per cases by A38;
              case A39: x=j;then
                (the Nbd of (FTSC1 n)).y={j,j-'1,j+1}
                          by A5,A11,AA2010,A3;then
                U_FT y = {j,j-'1,j+1} by FIN_TOPO:def 1;
                hence x in U_FT y by A39,ENUMSET1:def 1;
              end;
              case A40: x=j-'1;
                now assume j=1;then
                  j-1=0;then
                  x=0 by A40,BINARITH:def 3;
                  hence contradiction by A11;
                end;then
A42:            j>1 by REAL_1:def 5,A4;
A43:            now assume j=n;then
                  (the Nbd of (FTSC1 n)).y={j,j-'1,1}
                         by A5,A42,AA2010,A3;then
                  U_FT y = {j,j-'1,1} by FIN_TOPO:def 1;
                  hence x in U_FT y by A40,ENUMSET1:def 1;
                end;
                now assume j<>n;then
                  j<n by REAL_1:def 5,A4;then
                  (the Nbd of (FTSC1 n)).y={j,j-'1,j+1}
                             by A5,A42,AA2010,A3;then
                  U_FT y = {j,j-'1,j+1} by FIN_TOPO:def 1;
                  hence x in U_FT y by A40,ENUMSET1:def 1;
                end;
                hence x in U_FT y by A43;
              end;
              case x=j+1;then
A44:            i=(i+1)+1 by A36;
                i<i+1 by NAT_1:38;
                hence contradiction by A44,NAT_1:38;
              end;
            end;
            hence x in U_FT y;
          end;
          hence x in U_FT y by A14,A10,A17;
        end;
        case  A46: i=1 & i<n;then
          U_FT x= {i,n,i+1} by FIN_TOPO:def 1,A5,A9;then
A47:      y=i & y<>n or y=n or y=i+1& y<>n by ENUMSET1:def 1,A10;
          now per cases by A47;
            case A48: y=i & y<>n;then
              U_FT y= {j,n,j+1} by FIN_TOPO:def 1,A5,A9,A46;
              hence x in U_FT y by A48,ENUMSET1:def 1;
            end;
            case y=n;then
             (the Nbd of (FTSC1 n)).y={j,j-'1,1} by A46,A3,AA2010,A5;then
              U_FT y= {j,j-'1,1} by FIN_TOPO:def 1;
              hence x in U_FT y by A46,ENUMSET1:def 1;
            end;
            case A50: y=i+1 & y<>n;then
A51:          j<n by A4,REAL_1:def 5;
              j-1=i by A50,XCMPLX_1:26;then
A53:          j-'1=i by BINARITH:def 3,A46;
              (the Nbd of (FTSC1 n)).y={j,j-'1,j+1}
                             by A46,A51,A3,AA2010,A5,A50;then
              U_FT y= {j,j-'1,j+1} by FIN_TOPO:def 1;
              hence x in U_FT y by A53,ENUMSET1:def 1;
            end;
          end;
          hence x in U_FT y;
        end;
        case A54: 1<i & i=n;then
          U_FT x= {i,i-'1,1} by FIN_TOPO:def 1,A5,A9;then
A55:      y=i or y=i-'1 or y=1 by ENUMSET1:def 1,A10;
          now per cases by A55;
            case A56: y=i & y<>1;then
              U_FT y= {j,j-'1,1} by FIN_TOPO:def 1,A5,A9,A54;
              hence x in U_FT y by A56,ENUMSET1:def 1;
            end;
            case A57: y=1;
              (the Nbd of (FTSC1 n)).y={j,n,j+1}
                  by A57,A3,AA2010,A5,A54;then
              U_FT y= {j,n,j+1} by FIN_TOPO:def 1;
              hence x in U_FT y by A54,ENUMSET1:def 1;
            end;
            case A58: y=i-'1 & y<>1;then
A59:          1<j by A4,REAL_1:def 5;
A60:          j=n-'1 by A58,A54;
              n-1>0 by SQUARE_1:11,A54;then
A61:          n-1=n-'1 by BINARITH:def 3;
A62:          n-1+1=n;then
A63:          j+1=i by A61,A60,A54;
              j<n by A61,A62,A60,NAT_1:38;then
              (the Nbd of (FTSC1 n)).y={j,j-'1,j+1} by A59,A3,AA2010,A5;then
              U_FT y= {j,j-'1,j+1} by FIN_TOPO:def 1;
              hence x in U_FT y by A63,ENUMSET1:def 1;
            end;
          end;
          hence x in U_FT y;
        end;
        case i=1 & i=n;then
          U_FT x= {i} by FIN_TOPO:def 1,A5,A9;then
          j=i by A10,TARSKI:def 1;
          hence x in U_FT y by A10;
        end;
      end;
      hence x in U_FT y;
    end;
    hence y in U_FT x implies x in U_FT y;
  end;
  hence (FTSC1 n) is symmetric by FIN_TOPO:def 14;
end;
