:: The Inner Product and Conjugate of Matrix of Complex Numbers
::  by Wenpai Chang , Hiroshi Yamazaki and Yatsuka Nakamura
::
:: Received October 10, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies FINSEQ_1, FINSEQ_2, ARYTM_1, FUNCT_1, RELAT_1, FUNCOP_1, RVSUM_1,
      COMPLSP1, COMPLEX1, EUCLID_2, ARYTM_3, XCMPLX_0, ARYTM, RLVECT_1,
      MATRIX_1, TREES_1, BOOLE, QC_LANG1, INCSP_1, MATRIXC1, SETWISEO,
      FINSEQ_4, MATRIX_5, COMPLFLD, VECTSP_1;
 notations TARSKI, SUBSET_1, NUMBERS, XCMPLX_0, XREAL_0, RELAT_1, FUNCT_1,
      FUNCT_2, ZFMISC_1, NAT_1, PARTFUN1, BINOP_1, FUNCOP_1, FINSEQ_1,
      FINSEQOP, COMPLSP1, COMPLEX1, RVSUM_1, FINSEQ_2, BINOP_2, EUCLID_2,
      ORDINAL1, MATRIX_5, MATRIX_1, MATRIX_3, MATRIX_4, RLVECT_1, COMPLSP2,
      SETWOP_2, COMPLFLD, BINARITH, FINSEQ_4, STRUCT_0, GROUP_1;
 constructors NAT_1, SETWISEO, MEMBERED, PARTFUN1, FINSOP_1, EUCLID_2,
      MATRIX_5, MATRIX_4, MATRIX_3, COMPLSP2, BINARITH, FINSEQ_4;
 registrations RELSET_1, FINSEQ_2, NAT_1, FUNCT_1, FUNCT_2, BINOP_2, COMPLEX1,
      XREAL_0, XCMPLX_0, NUMBERS, RELAT_1, ORDINAL2, COMPLFLD;
 requirements NUMERALS, BOOLE, SUBSET, REAL, ARITHM;
 theorems FINSEQ_1, FUNCT_1, NAT_1, MATRIX_1, FINSEQ_2, COMPLEX1, BINOP_2,
      FINSEQ_3, XCMPLX_0, ZFMISC_1, COMPLSP1, MATRIX_2, FINSEQOP, COMPLSP2,
      FUNCOP_1, FUNCT_2, FINSOP_1, SETWOP_2, REAL_1, BINARITH, FINSEQ_5,
      FINSEQ_4, JORDAN3, XREAL_1, MATRIX_3, COMPLFLD, MATRIX_4, MATRIX_5,
      EUCLID_2, RVSUM_1, NUMBERS, XBOOLE_1;
 schemes MATRIX_1, FINSEQ_2, FINSEQ_1, FUNCT_2, NAT_1;

begin

 reserve i,j,n,k for Nat,
         a for Element of COMPLEX,
         R1,R2 for Element of i-tuples_on COMPLEX;

definition let M be Matrix of COMPLEX;
  func M*' -> Matrix of COMPLEX means :AA10:
   len it=len M & width it=width M & for i,j being Nat st [i,j] in Indices M
     holds it*(i,j) = (M*(i,j))*';
 existence
 proof
  deffunc F(Nat,Nat) = (M*($1,$2))*';
  consider M1 being Matrix of len M,width M,COMPLEX such that
A1:for i,j st [i,j] in Indices M1 holds M1*(i,j) = F(i,j)
    from MATRIX_1:sch 1;
  take M1;
A2:len M1=len M by MATRIX_1:def 3;
A3:now per cases;
      case A4:len M= 0;
        then width M1=0 by A2,MATRIX_1:def 4;
        hence width M1=width M by A4,MATRIX_1:def 4;
      end;
      case len M>0;
        hence width M1=width M by MATRIX_1:24;
      end;
     end;
     dom M = dom M1 by A2,FINSEQ_3:31;
     then Indices M1=[:dom M,Seg width M:] &
       Indices M =[:dom M,Seg width M:] by A3,MATRIX_1:def 5;
  hence thesis by A1,A3,MATRIX_1:def 3;
 end;
 uniqueness
 proof
  let M1,M2 be Matrix of COMPLEX;
    assume that A5:len M1=len M & width M1 =width M &
            for i,j st [i,j] in Indices M holds M1*(i,j)=(M*(i,j))*' and
A6: len M2=len M & width M2 =width M &
            for i,j st [i,j] in Indices M holds M2*(i,j)=(M*(i,j))*';
    now let i,j;
      assume A7:[i,j] in Indices M1;
      dom M1 = dom M by A5,FINSEQ_3:31;
      then A8: Indices M1=[:dom M,Seg width M:] &
      Indices M = [:dom M,Seg width M:]by A5,MATRIX_1:def 5;
      hence M1*(i,j)=(M*(i,j))*' by A5,A7
                   .=M2*(i,j) by A6,A7,A8;
    end;
  hence M1=M2 by A5,A6,MATRIX_1:21;
 end;
end;

theorem Th1:
  for M being Matrix of COMPLEX holds
    [i,j] in Indices M iff 1<= i & i<=len M & 1<=j & j<=width M
proof let M be Matrix of COMPLEX;
A1: [i,j] in Indices M implies 1<= i & i<=len M & 1<=j & j<=width M
  proof
    assume [i,j] in Indices M;then
    [i,j] in [:dom M,Seg width M:] by MATRIX_1:def 5;then
    [i,j] in [:Seg len M,Seg width M:] by FINSEQ_1:def 3;then
    i in Seg len M & j in Seg width M by ZFMISC_1:106;
    hence thesis by FINSEQ_1:3;
  end;
  1<= i & i<=len M & 1<=j & j<=width M implies [i,j] in Indices M
  proof
    assume 1<= i & i<=len M & 1<=j & j<=width M;then
    i in dom M & j in Seg width M by FINSEQ_1:3,FINSEQ_3:27;then
    [i,j] in [:dom M,Seg width M:] by ZFMISC_1:106;
    hence thesis by MATRIX_1:def 5;
  end;
  hence thesis by A1;
end;

theorem Th2:
  for M being Matrix of COMPLEX holds (M*')*' = M
proof let M be Matrix of COMPLEX;
A1: len ((M*')*') = len (M*') by AA10;
A2: len (M*') = len M by AA10;
A3: width ((M*')*') = width (M*') by AA10;
A4: width (M*') = width M by AA10;
    now let i,j;
      assume [i,j] in Indices (M*')*';then
A5:   1<=i & i<=len (M*') & 1<=j & j<=width (M*') by A1,A3,Th1;then
A6:   [i,j] in Indices (M*') by Th1;
A7:   [i,j] in Indices M by A2,A4,A5,Th1;
      (M*')*(i,j) = (M*(i,j))*' by AA10,A7;
      hence (M*')*'*(i,j) = ((M*(i,j))*')*' by A6,AA10
                         .= M*(i,j);
    end;
    hence thesis by MATRIX_1:21,A1,A2,A3,A4;
end;

theorem Th3:
  for a being complex number,M being Matrix of COMPLEX holds
    len (a*M)=len M & width (a*M)=width M
proof let a be complex number,M be Matrix of COMPLEX;
  a in COMPLEX by XCMPLX_0:def 2;then
  reconsider ea=a as Element of F_Complex by COMPLFLD:def 1;
A1: len (a*M)=len COMPLEX2Field (a*M) by MATRIX_5:7
      .= len COMPLEX2Field Field2COMPLEX (ea*(COMPLEX2Field M))
         by MATRIX_5:def 7
      .= len (ea*(COMPLEX2Field M)) by MATRIX_5:6
      .= len COMPLEX2Field M by MATRIX_3:def 5
      .= len M by MATRIX_5:def 1;
  width (a*M)=width COMPLEX2Field (a*M) by MATRIX_5:7
      .= width COMPLEX2Field Field2COMPLEX (ea*(COMPLEX2Field M))
         by MATRIX_5:def 7
      .= width (ea*(COMPLEX2Field M)) by MATRIX_5:6
      .= width COMPLEX2Field M by MATRIX_3:def 5
      .= width M by MATRIX_5:def 1;
  hence thesis by A1;
end;

theorem Th4:
  for i,j being Nat,a being complex number,M being Matrix of COMPLEX st
    len (a*M) =len M & width (a*M)=width M & [i,j] in Indices M holds
      (a*M)*(i,j) = a*(M*(i,j))
proof let i,j be Nat,a be complex number,M be Matrix of COMPLEX;
  assume A1: len (a*M) =len M & width (a*M)=width M & [i,j] in Indices M;
  a in COMPLEX by XCMPLX_0:def 2;then
  reconsider aa=a as Element of F_Complex by COMPLFLD:def 1;
A2: COMPLEX2Field(a*M)
    = COMPLEX2Field Field2COMPLEX (aa*(COMPLEX2Field M)) by MATRIX_5:def 7
   .= aa*(COMPLEX2Field M) by MATRIX_5:6;
  reconsider m1=COMPLEX2Field M as Matrix of COMPLEX by COMPLFLD:def 1;
A3: M*(i,j) = m1*(i,j) by MATRIX_5:def 1
        .= COMPLEX2Field(M)*(i,j) by COMPLFLD:def 1;
  reconsider m=COMPLEX2Field(a*M) as Matrix of COMPLEX by COMPLFLD:def 1;
A4: [i,j] in Indices COMPLEX2Field M by MATRIX_5:def 1,A1;
  (a*M)*(i,j)= m*(i,j) by MATRIX_5:def 1
   .= (aa*(COMPLEX2Field M))*(i,j) by A2,COMPLFLD:def 1
   .= aa*((COMPLEX2Field M)*(i,j)) by MATRIX_3:def 5,A4
   .= a*(COMPLEX2Field (M)*(i,j)) by COMPLFLD:6;
  hence thesis by A3;
end;

theorem Th5:
  for a being complex number,M being Matrix of COMPLEX holds
    (a*M)*' = a*'*(M*')
proof
  let a be complex number,M be Matrix of COMPLEX;
  reconsider aa = a as Element of COMPLEX by XCMPLX_0:def 2;
A1: len (a*M) = len M by Th3;
A2: len ((a*M)*') = len (a*M) by AA10;
B1: len (a*'*(M*')) = len (M*') by Th3;then
  len (a*'*(M*')) = len M by AA10;then
A3: len ((a*M)*')=len (a*'*(M*')) by Th3,A2;
A4: len M = len (M*') by AA10;
A5: width (a*M) = width M by Th3;
A6: width ((a*M)*') = width (a*M) by AA10;
B3: width (a*'*(M*')) = width (M*') by Th3;then
  width (a*'*(M*')) = width M by AA10;then
A7: width ((a*M)*')=width (a*'*(M*')) by Th3,A6;
A8: width M = width (M*') by AA10;
  now let i,j;
  assume [i,j] in Indices (a*M)*';then
A9: 1<= i & i<=len (a*M) & 1<=j & j<=width (a*M) by A2,A6,Th1;then
A10: [i,j] in Indices (a*M) by Th1;
A11: [i,j] in Indices M by Th1,A1,A5,A9;
A12: [i,j] in Indices (M*') by A1,A4,A5,A8,A9,Th1;
  ((a*M)*')*(i,j) = ((a*M)*(i,j))*' by AA10,A10;
  hence (a*M)*'*(i,j) = (aa*(M*(i,j)))*' by Th4,A11,A1,A5
        .= aa*'*(M*(i,j))*' by COMPLEX1:121
        .= a*'*((M*')*(i,j)) by A11,AA10
        .= (a*'*(M*'))*(i,j) by Th4,A12,B1,B3;
  end;
  hence thesis by MATRIX_1:21,A3,A7;
end;

theorem Th6:
  for M1,M2 being Matrix of COMPLEX holds
    len (M1+M2)=len M1 & width (M1+M2)=width M1
proof let M1,M2 be Matrix of COMPLEX;
A2: len (M1+M2)=len COMPLEX2Field (M1+M2) by MATRIX_5:7
      .= len COMPLEX2Field Field2COMPLEX
          ((COMPLEX2Field M1)+(COMPLEX2Field M2)) by MATRIX_5:def 3
      .= len ((COMPLEX2Field M1)+(COMPLEX2Field M2)) by MATRIX_5:6
      .= len COMPLEX2Field M1 by MATRIX_3:def 3
      .= len M1 by MATRIX_5:def 1;
    width (M1+M2)=width COMPLEX2Field (M1+M2) by MATRIX_5:7
      .= width COMPLEX2Field
          Field2COMPLEX ((COMPLEX2Field M1)+(COMPLEX2Field M2))
          by MATRIX_5:def 3
      .= width ((COMPLEX2Field M1)+(COMPLEX2Field M2)) by MATRIX_5:6
      .= width COMPLEX2Field M1 by MATRIX_3:def 3
      .= width M1 by MATRIX_5:def 1;
  hence thesis by A2;
end;

theorem Th7:
  for i,j being Nat,M1,M2 being Matrix of COMPLEX st
    len M1 =len M2 & width M1=width M2 & [i,j] in Indices M1 holds
      (M1+M2)*(i,j) = M1*(i,j) + M2*(i,j)
proof let i,j be Nat,M1,M2 be Matrix of COMPLEX;
    assume A1: len M1 =len M2 & width M1=width M2 & [i,j] in Indices M1;
A2: COMPLEX2Field(M1+M2)
      = COMPLEX2Field Field2COMPLEX ((COMPLEX2Field M1)+(COMPLEX2Field M2))
         by MATRIX_5:def 3
     .= (COMPLEX2Field M1)+(COMPLEX2Field M2) by MATRIX_5:6;
A3: [i,j] in Indices COMPLEX2Field M1 by MATRIX_5:def 1,A1;
    set m=COMPLEX2Field(M1+M2);
    reconsider m'=m as Matrix of COMPLEX by COMPLFLD:def 1;
    reconsider m1=COMPLEX2Field M1, m2=COMPLEX2Field M2 as Matrix of COMPLEX
      by COMPLFLD:def 1;
A4: M1*(i,j) = m1*(i,j) by MATRIX_5:def 1
       .=COMPLEX2Field(M1)*(i,j) by COMPLFLD:def 1;
A5: M2*(i,j) = m2*(i,j) by MATRIX_5:def 1
       .=COMPLEX2Field(M2)*(i,j) by COMPLFLD:def 1;
    (M1+M2)*(i,j) = m'*(i,j) by MATRIX_5:def 1
    .= m*(i,j) by COMPLFLD:def 1
    .=(COMPLEX2Field M1)*(i,j)+(COMPLEX2Field M2)*(i,j)
      by A3,MATRIX_3:def 3,A2;
  hence thesis by A4,A5,COMPLFLD:3;
end;

theorem
  for M1,M2 being Matrix of COMPLEX st len M1=len M2 & width M1=width M2
    holds (M1 + M2)*' = M1*' + M2*'
proof let M1,M2 be Matrix of COMPLEX;
  assume A1: len M1=len M2& width M1=width M2;
A2: len ((M1 + M2)*') = len (M1 + M2) by AA10;
A3: len (M1 + M2) = len M1 by Th6;
A5: len (M1*') = len M1 by AA10;
A5a: len (M2*') = len M2 by AA10;
A5b: width (M1*') = width M1 by AA10;
A5c: width (M2*') = width M2 by AA10;
A4: len (M1*' + M2*') = len (M1*') by Th6;
A6: width ((M1 + M2)*') = width (M1 + M2) by AA10;
A7: width (M1 + M2) = width M1 by Th6;
A8: width (M1*' + M2*') = width (M1*') by Th6;
A9: width (M1*') = width M1 by AA10;
  now let i,j;
  assume [i,j] in Indices (M1+M2)*';then
A10: 1<= i & i<=len (M1+M2) & 1<=j & j<=width (M1+M2) by A2,A6,Th1;then
A11: [i,j] in Indices (M1+M2) by Th1;
A12: [i,j] in Indices M1 by Th1,A3,A7,A10;
  1<= i & i<=len (M1*') & 1<=j & j<=width (M1*') by AA10,A3,A7,A10;then
A13: [i,j] in Indices (M1*') by Th1;
A14: [i,j] in Indices M2 by Th1,A3,A7,A10,A1;
  ((M1+M2)*')*(i,j) = ((M1+M2)*(i,j))*' by AA10,A11;
  hence (M1+M2)*'*(i,j) = (M1*(i,j)+M2*(i,j))*' by Th7,A12,A1
                     .= (M1*(i,j))*'+(M2*(i,j))*' by COMPLEX1:118
                     .= M1*'*(i,j)+(M2*(i,j))*' by AA10,A12
                     .= (M1*')*(i,j)+(M2*')*(i,j) by AA10,A14
                     .= (M1*'+M2*')*(i,j) by Th7,A13,A1,A5,A5a,A5b,A5c;
  end;
  hence thesis by MATRIX_1:21,A2,A3,A4,A5,A6,A7,A8,A9;
end;

theorem Th9:
  for M being Matrix of COMPLEX holds
    len (-M)=len M & width (-M)=width M
proof let M be Matrix of COMPLEX;
    A2: len (-M)=len COMPLEX2Field (-M) by MATRIX_5:7
      .= len COMPLEX2Field Field2COMPLEX (-COMPLEX2Field M)
          by MATRIX_5:def 4
      .= len (-COMPLEX2Field M) by MATRIX_5:6
      .= len COMPLEX2Field M by MATRIX_3:def 2
      .= len M by MATRIX_5:def 1;
    width (-M)=width COMPLEX2Field (-M) by MATRIX_5:7
      .= width COMPLEX2Field Field2COMPLEX (-COMPLEX2Field M)
          by MATRIX_5:def 4
      .= width (-COMPLEX2Field M) by MATRIX_5:6
      .= width COMPLEX2Field M by MATRIX_3:def 2
      .= width M by MATRIX_5:def 1;
  hence thesis by A2;
end;

theorem Th10:
  for i,j being Nat,M being Matrix of COMPLEX st
    len (-M)=len M & width (-M)=width M & [i,j] in Indices M holds
      (-M)*(i,j)= -(M*(i,j))
proof let i,j be Nat,M be Matrix of COMPLEX;
  assume A1: len (-M)=len M & width (-M)=width M & [i,j] in Indices M;
A2: COMPLEX2Field(-M)
    = COMPLEX2Field Field2COMPLEX (-COMPLEX2Field M) by MATRIX_5:def 4
   .= -(COMPLEX2Field M) by MATRIX_5:6;
A3: [i,j] in Indices COMPLEX2Field M by MATRIX_5:def 1,A1;
  reconsider m1=COMPLEX2Field M as Matrix of COMPLEX by COMPLFLD:def 1;
A4: M*(i,j) =m1*(i,j) by MATRIX_5:def 1
       .=COMPLEX2Field(M)*(i,j) by COMPLFLD:def 1;
  reconsider m=COMPLEX2Field(-M) as Matrix of COMPLEX by COMPLFLD:def 1;
  (-M)*(i,j) = m*(i,j) by MATRIX_5:def 1
   .= (-(COMPLEX2Field M))*(i,j) by A2,COMPLFLD:def 1
   .= -((COMPLEX2Field M)*(i,j)) by MATRIX_3:def 2,A3;
  hence thesis by COMPLFLD:4,A4;
end;

theorem Th11:
  for M being Matrix of COMPLEX holds
    (-1)*M = -M
proof let M be Matrix of COMPLEX;
A1: len (-M) = len M by Th9;
A2: len ((-1)*M) = len M by Th3;
A3: width (-M) = width M by Th9;
A4: width ((-1)*M) = width M by Th3;
  now let i,j;
    assume [i,j] in Indices ((-1)*M);then
A5: 1<= i & i<=len M & 1<=j & j<=width M by A2,A4,Th1;
A6: [i,j] in Indices M by Th1,A5;
    hence ((-1)*M)*(i,j) = (-1)*(M*(i,j)) by Th4,A2,A4
         .= -(M*(i,j))
         .= (-M)*(i,j) by Th10,A6,A1,A3;
  end;
  hence thesis by MATRIX_1:21,A1,A2,A3,A4;
end;

theorem
  for M being Matrix of COMPLEX holds
    (-M)*' = -(M*')
proof let M be Matrix of COMPLEX;
  (-M)*'= ((-1)*M)*' by Th11
       .= ((-1r)*')*(M*') by COMPLEX1:def 7,Th5
       .= (-1)*(M*') by COMPLEX1:def 7,COMPLEX1:115,119
       .= -M*' by Th11;
  hence thesis;
end;

theorem Th13:
  for M1,M2 being Matrix of COMPLEX holds
    len (M1-M2)=len M1 & width (M1-M2)=width M1
proof let M1,M2 be Matrix of COMPLEX;
A2: len (M1-M2)=len (COMPLEX2Field (M1-M2)) by MATRIX_5:7
      .= len COMPLEX2Field Field2COMPLEX
         ((COMPLEX2Field M1)-(COMPLEX2Field M2)) by MATRIX_5:def 5
      .= len ((COMPLEX2Field M1)-(COMPLEX2Field M2)) by MATRIX_5:6
      .= len ((COMPLEX2Field M1)+-(COMPLEX2Field M2)) by MATRIX_4:def 1
      .= len COMPLEX2Field M1 by MATRIX_3:def 3
      .= len M1 by MATRIX_5:def 1;
    width (M1-M2)=width COMPLEX2Field (M1-M2) by MATRIX_5:7
      .= width COMPLEX2Field
          Field2COMPLEX ((COMPLEX2Field M1)-(COMPLEX2Field M2))
          by MATRIX_5:def 5
      .= width ((COMPLEX2Field M1)-(COMPLEX2Field M2)) by MATRIX_5:6
      .= width ((COMPLEX2Field M1)+-(COMPLEX2Field M2))
          by MATRIX_4:def 1
      .= width COMPLEX2Field M1 by MATRIX_3:def 3
      .= width M1 by MATRIX_5:def 1;
  hence thesis by A2;
end;

theorem Th14:
  for i,j being Nat,M1,M2 be Matrix of COMPLEX st
    len M1=len M2 & width M1=width M2 &
      [i,j] in Indices M1 holds
        (M1-M2)*(i,j)=(M1*(i,j)) - (M2*(i,j))
proof let i,j be Nat,M1,M2 be Matrix of COMPLEX;
  assume A1: len M1 =len M2 & width M1=width M2 &
             [i,j] in Indices M1;
    A2: 1<= i & i<=len M2 & 1<=j & j<=width M2 by Th1,A1;
    A3: [i,j] in Indices M2 by Th1,A2;
    A4: COMPLEX2Field(M1-M2)
      = COMPLEX2Field Field2COMPLEX ((COMPLEX2Field M1)-(COMPLEX2Field M2))
         by MATRIX_5:def 5
     .= (COMPLEX2Field M1)-(COMPLEX2Field M2) by MATRIX_5:6;
    A5: [i,j] in Indices COMPLEX2Field M1 by MATRIX_5:def 1,A1;
    A6: [i,j] in Indices COMPLEX2Field M2 by A3,MATRIX_5:def 1;
    set m=COMPLEX2Field(M1-M2);
    reconsider m'=m as Matrix of COMPLEX by COMPLFLD:def 1;
    reconsider m1=COMPLEX2Field M1 as Matrix of COMPLEX by COMPLFLD:def 1;
    reconsider m2=COMPLEX2Field M2 as Matrix of COMPLEX by COMPLFLD:def 1;
    A7: M1*(i,j) = m1*(i,j) by MATRIX_5:def 1
       .= COMPLEX2Field(M1)*(i,j) by COMPLFLD:def 1;
    A8: M2*(i,j) = m2*(i,j) by MATRIX_5:def 1
       .= COMPLEX2Field(M2)*(i,j) by COMPLFLD:def 1;
    A9: -(M2*(i,j))=-(COMPLEX2Field(M2)*(i,j)) by COMPLFLD:4,A8;
    A10: M1*(i,j)-M2*(i,j)= M1*(i,j)+ -M2*(i,j)
       .= COMPLEX2Field(M1)*(i,j)+ -COMPLEX2Field(M2)*(i,j)
          by A7,A9,COMPLFLD:3;
    (M1-M2)*(i,j) = m'*(i,j) by MATRIX_5:def 1
    .= m*(i,j) by COMPLFLD:def 1
    .= ((COMPLEX2Field M1)+(-(COMPLEX2Field M2)))*(i,j) by MATRIX_4:def 1,A4
    .= (COMPLEX2Field M1)*(i,j)+(-COMPLEX2Field M2)*(i,j)
       by A5,MATRIX_3:def 3
    .= (COMPLEX2Field M1)*(i,j)+-((COMPLEX2Field M2)*(i,j))
       by MATRIX_3:def 2,A6;
  hence thesis by A10;
end;

theorem
  for M1,M2 being Matrix of COMPLEX st len M1=len M2 & width M1=width M2
    holds (M1 - M2)*' = M1*' - M2*'
proof let M1,M2 be Matrix of COMPLEX;
  assume A1: len M1=len M2& width M1=width M2;
A2: len ((M1 - M2)*') = len (M1 - M2) by AA10;
A5: len (M1*') = len M1 by AA10;
A5a: len (M2*') = len M2 by AA10;
A5b: width (M1*') = width M1 by AA10;
A5c: width (M2*') = width M2 by AA10;
A3: len (M1 - M2) = len M1 by Th13;
A4: len (M1*' - M2*') = len (M1*') by Th13;
A6: width ((M1 - M2)*') = width (M1 - M2) by AA10;
A7: width (M1 - M2) = width M1 by Th13;
A8: width (M1*' - M2*') = width (M1*') by Th13;
A9: width (M1*') = width M1 by AA10;
  now let i,j;
  assume [i,j] in Indices (M1-M2)*';then
A10: 1<= i & i<=len (M1-M2) & 1<=j & j<=width (M1-M2) by A2,A6,Th1;then
A11: [i,j] in Indices (M1-M2) by Th1;
A12: [i,j] in Indices M1 by Th1,A3,A7,A10;
  1<= i & i<=len (M1*') & 1<=j & j<=width (M1*') by AA10,A3,A7,A10;then
A13: [i,j] in Indices (M1*') by Th1;
A14: [i,j] in Indices M2 by Th1,A3,A7,A10,A1;
  ((M1-M2)*')*(i,j) = ((M1-M2)*(i,j))*' by AA10,A11;
  hence (M1-M2)*'*(i,j) = (M1*(i,j)-M2*(i,j))*' by Th14,A12,A1
          .= (M1*(i,j))*'-(M2*(i,j))*' by COMPLEX1:120
          .= M1*'*(i,j)-(M2*(i,j))*' by AA10,A12
          .= (M1*')*(i,j)-(M2*')*(i,j) by AA10,A14
          .= (M1*'-M2*')*(i,j) by Th14,A13,A1,A5,A5a,A5b,A5c;
  end;
  hence thesis by MATRIX_1:21,A2,A3,A4,A5,A6,A7,A8,A9;
end;

definition let M be Matrix of COMPLEX;
  func M@" -> Matrix of COMPLEX equals
    (M@)*';
  coherence;
end;

definition let x be FinSequence of COMPLEX;
  assume A1:len x > 0;
  func FinSeq2Matrix x -> Matrix of COMPLEX means
    len it = len x & width it = 1 &
    for j st j in Seg len x holds it.j =<*x.j*>;
  existence
  proof
   deffunc F(Nat) = <*x.$1*>;
   reconsider n=len x as Nat;
   consider M3 being FinSequence such that
A2: len M3=n & for j st j in Seg n holds M3.j = F(j) from FINSEQ_1:sch 2;
   for y being set st y in rng M3 ex p being FinSequence of COMPLEX
     st y = p & len p = 1
   proof let y be set;
     assume y in rng M3;
     then consider z be set such that A3: z in dom M3 & y=M3.z
       by FUNCT_1:def 5;
     reconsider z as Nat by A3;
     z in Seg len M3 by A3,FINSEQ_1:def 3;then
     A4: M3.z = <*x.z*> by A2;
     len <*x.z*>=1 by FINSEQ_1:56;
     hence ex p being FinSequence of COMPLEX st y = p & len p = 1 by A3,A4;
   end;then
   reconsider M2=M3 as Matrix of COMPLEX by MATRIX_1:9;
   for p being FinSequence of COMPLEX st p in rng M2 holds len p = 1
   proof
     let p be FinSequence of COMPLEX;
     assume p in rng M2;
     then consider i such that A5:i in dom M2 and A6:M2.i =p by FINSEQ_2:11;
A7:  i in Seg len M2 by A5,FINSEQ_1:def 3;
     len <*x.i*>=1 by FINSEQ_1:56;
     hence thesis by A6,A2,A7;
   end;then
   reconsider M1=M2 as Matrix of len M2,1,COMPLEX by MATRIX_1:def 3;
   width M1 = 1 by MATRIX_1:24,A2,A1;
   hence thesis by A2;
 end;
 uniqueness
 proof
  let M1,M2 be Matrix of COMPLEX;
  assume that
A8: len M1 = len x & width M1 = 1 and
A9: for k st k in Seg len x holds M1.k =<*x.k*> and
A10: len M2 = len x & width M2 = 1 and
A11: for k st k in Seg len x holds M2.k =<*x.k*>;
  now let k;
    assume A6:k in Seg len x;
    hence M1.k=<*x.k*> by A9
             .=M2.k by A11,A6;
  end;
  hence M1=M2 by A8,A10,FINSEQ_2:10;
 end;
end;

definition let M be Matrix of COMPLEX;
  func Matrix2FinSeq M -> FinSequence of COMPLEX equals
    Col(M,1);
  coherence;
end;

definition let F1,F2 be FinSequence of COMPLEX;
  func mlt(F1,F2) -> FinSequence of COMPLEX equals
    multcomplex.:(F1,F2);
  coherence;
  commutativity
  proof let F,F1,F2 be FinSequence of COMPLEX;
   assume
A1: F = multcomplex.:(F1,F2);
A2: dom multcomplex = [:COMPLEX,COMPLEX:] by FUNCT_2:def 1;
A3: rng F1 c= COMPLEX by FINSEQ_1:def 4;
A4: rng F2 c= COMPLEX by FINSEQ_1:def 4;
    then A5:  [:rng F2, rng F1:] c= dom multcomplex by A2,A3,ZFMISC_1:119;
       [:rng F1, rng F2:] c= dom multcomplex by A2,A3,A4,ZFMISC_1:119;
    then A6:  dom(multcomplex.:(F1,F2)) = dom F1 /\ dom F2 by FUNCOP_1:84
          .= dom(multcomplex.:(F2,F1)) by A5,FUNCOP_1:84;
     for z being set st z in dom(multcomplex.:(F2,F1))
       holds F.z = multcomplex.(F2.z,F1.z)
     proof let z be set such that
A7:    z in dom(multcomplex.:(F2,F1));
       set F1z = F1.z, F2z =F2.z;
       thus F.z = multcomplex.(F1.z,F2.z) by A1,A6,A7,FUNCOP_1:28
           .= F1z * F2z by BINOP_2:def 5
           .= multcomplex.(F2.z,F1.z) by BINOP_2:def 5;
     end;
   hence F = multcomplex.:(F2,F1) by A1,A6,FUNCOP_1:27;
  end;
end;

definition let F be FinSequence of COMPLEX;
  func Sum F -> Element of COMPLEX equals
    addcomplex $$ F;
  coherence;
end;

definition let M be Matrix of COMPLEX;let F be FinSequence of COMPLEX;
  func M*F -> FinSequence of COMPLEX means :AA120:
    len it = len M &
    for i st i in Seg len M holds it.i=Sum mlt(Line(M,i),F);
  existence
  proof
   deffunc V(Nat) = Sum mlt(Line(M,$1),F);
   consider z being FinSequence of COMPLEX such that
A2:len z=len M & for i st i in Seg len M holds z.i=V(i) from FINSEQ_2:sch 1;
   take z;
   thus thesis by A2;
 end;
 uniqueness
 proof
   let p1,p2 be FinSequence of COMPLEX;
   assume that
A3: len p1 = len M &
       for i st i in Seg len M holds p1.i=Sum mlt(Line(M,i),F) and
A4: len p2 = len M &
       for i st i in Seg len M holds p2.i=Sum mlt(Line(M,i),F);
   now let i;
     assume A6: i in Seg len M;
     then p1.i=Sum mlt(Line(M,i),F) by A3;
     hence p1.i = p2.i by A4,A6;
   end;
   hence p1=p2 by A3,A4,FINSEQ_2:10;
 end;
end;

Lm2:for a being Element of COMPLEX,F being FinSequence of COMPLEX holds
 a*F = (multcomplex[;](a,id COMPLEX))*F
 proof let a be Element of COMPLEX,F be FinSequence of COMPLEX;
  a multcomplex = multcomplex[;](a,id COMPLEX) by COMPLSP1:def 5;
  hence thesis by COMPLSP1:def 10;
 end;

theorem Th17:
  a*mlt(R1,R2) = mlt(a*R1,R2)
proof
  thus a*mlt(R1,R2)
      = (multcomplex[;](a,id COMPLEX))*(mlt(R1,R2)) by Lm2
     .= (multcomplex[;](a,id COMPLEX))*(multcomplex.:(R1,R2))
     .= multcomplex.:((multcomplex[;](a,id COMPLEX))*R1,R2)
        by FINSEQOP:27
     .= multcomplex.:(a*R1,R2) by Lm2
     .= mlt(a*R1,R2);
end;

definition let M be Matrix of COMPLEX;
           let a be complex number;
  func M*a -> Matrix of COMPLEX equals
    a*M;
  coherence;
end;

theorem
  for a being Element of COMPLEX,M being Matrix of COMPLEX holds
    (M*a)*' = a*'*(M*') by Th5;

theorem Th19:
  for x,y being FinSequence of COMPLEX st len x = len y holds
    len mlt(x,y)=len x & len mlt(x,y)=len y by FINSEQ_2:86;

theorem Th20:
  for F1,F2 being FinSequence of COMPLEX,i being Nat holds
    i in dom mlt(F1,F2) implies mlt(F1,F2).i = F1.i * F2.i
proof let F1,F2 be FinSequence of COMPLEX,i be Nat;
  assume
A1: i in dom mlt(F1,F2);
  set r1 = F1.i, r2 = F2.i;
  mlt(F1,F2) = multcomplex.:(F1,F2) & i in dom mlt(F1,F2) by A1;
  then mlt(F1,F2).i = multcomplex.(r1,r2) by FUNCOP_1:28;
  hence thesis by BINOP_2:def 5;
end;

definition let i,R1,R2;
  redefine func mlt(R1,R2) -> Element of i-tuples_on COMPLEX;
  coherence by FINSEQ_2:140;
end;

theorem Th21:
  mlt(R1,R2).j = R1.j * R2.j
 proof per cases;
  suppose
A1:not j in Seg i;
   then A2:  not j in dom R2 by FINSEQ_2:144;
       not j in dom mlt(R1,R2) by A1,FINSEQ_2:144;
   hence mlt(R1,R2).j = R1.j *0 by FUNCT_1:def 4
       .= R1.j * R2.j by A2,FUNCT_1:def 4;
  end;
  suppose j in Seg i;
  then j in dom mlt(R1,R2) by FINSEQ_2:144;
  hence thesis by Th20;
 end;
 end;

theorem Th22:
  for a,b being Element of COMPLEX holds
    addcomplex.(a,(b*'))*'=addcomplex.((a*'),b)
proof let a,b be Element of COMPLEX;
  addcomplex.(a,(b*'))*'=(a+b*')*' by BINOP_2:def 3
  .= a*'+(b*')*' by COMPLEX1:118
  .= addcomplex.(a*',b) by BINOP_2:def 3;
  hence thesis;
end;

theorem Th23:
  for F being FinSequence of COMPLEX
   ex G being Function of NAT,COMPLEX st
  for n being Nat st 1<= n & n<=len F holds G.n=F.n
proof let F be FinSequence of COMPLEX;
  defpred P[set,set] means ($1 in Seg len F implies $2=F.$1) &
            (not $1 in Seg len F implies $2=0);
  A7: for x being set st x in NAT ex y being set st y in COMPLEX & P[x,y]
   proof let x be set;
     assume x in NAT;
      per cases;
      suppose x in Seg len F;
       hence ex y being set st y in COMPLEX & P[x,y];
      end;
      suppose not x in Seg len F;then
        C2: not x in dom F by FINSEQ_1:def 3;
        P[x,F.x] by C2,FUNCT_1:def 4;
        hence ex y being set st y in COMPLEX & P[x,y];
      end;
   end;
   ex G1 being Function of NAT,COMPLEX st for x being set
      st x in NAT holds P[x,G1.x] from FUNCT_2:sch 1(A7);then
   consider G2 being Function of NAT,COMPLEX such that
A8: for x being set st x in NAT holds
     (x in Seg len F implies G2.x=F.x) &
        (not x in Seg len F implies G2.x=0);
   for n being Nat st 1<= n & n<=len F holds G2.n=F.n
   proof let n be Nat;
     assume B1: 1<= n & n<=len F;
     n in Seg len F by FINSEQ_1:3,B1;
     hence thesis by A8;
   end;
  hence thesis;
end;

theorem Th24:
  for F being FinSequence of COMPLEX st len (F*') >= 1 holds
    addcomplex $$ (F*') = (addcomplex $$ F)*'
proof let F be FinSequence of COMPLEX;
  assume A1: len (F*') >= 1;then
A2: len F >=1 by COMPLSP2:def 1;
A3: len F = len (F*') by COMPLSP2:def 1;
    set d=(addcomplex $$ (F*'))*';
    consider f22 being Function of NAT,COMPLEX such that
A4: f22.1 = (F*').1 and
A5: (for n st 0 <> n & n < len (F*') holds
         f22.(n + 1) = addcomplex.(f22.n,(F*').(n + 1))) and
A6: addcomplex $$ (F*') = f22.(len (F*')) by FINSOP_1:2,A1;
    defpred P[set,set] means $2= f22.$1;
A7: for k st k in Seg len (F*') ex x being Element of COMPLEX st P[k,x]
    proof let k;
      f22.k is Element of COMPLEX;
      hence thesis;
    end;
    ex f2 being FinSequence of COMPLEX st dom f2 = Seg len (F*') &
     for k st k in Seg len (F*') holds P[k,f2.k] from FINSEQ_1:sch 5(A7);then
    consider f2 being FinSequence of COMPLEX such that
    A8: dom f2 = Seg len (F*') &
         for k st k in Seg len (F*') holds P[k,f2.k];
    A9: len f2 = len (F*') by FINSEQ_1:def 3,A8;
    A10: 1 in Seg len (F*') by A1,FINSEQ_1:3;
    A11: for n st 0 <> n & n < len (F*') holds
      f2.(n + 1) = addcomplex.(f2.n,(F*').(n + 1))
      proof let n;
       assume A12: 0 <> n & n < len (F*');
        0<n by A12;then
        A13: 0+1<=n by NAT_1:38;
        A14: 1<=n+1 by NAT_1:38,A13;
        A15: n+1<=len (F*') by A12,NAT_1:38;
        A16: n in Seg len (F*') by FINSEQ_1:3,A13,A12;
        (n+1) in Seg len (F*') by FINSEQ_1:3,A15,A14;then
        f2.(n + 1) = f22.(n + 1) by A8
        .= addcomplex.(f22.n,(F*').(n + 1)) by A5,A12
        .= addcomplex.(f2.n,(F*').(n + 1)) by A8,A16;
       hence thesis;
      end;
    A17: (len (F*')) in Seg len (F*') by A1,FINSEQ_1:3;
    A18: F.1=((F.1)*')*' .= ((F*').1)*' by A2,COMPLSP2:def 1
       .= (f2.1)*' by A8,A4,A10
       .= (f2*').1 by COMPLSP2:def 1,A1,A9;
    A19: for n st 0 <> n & n < len F holds
        (f2*').(n + 1) = addcomplex.((f2*').n,F.(n + 1))
      proof let n;
       assume A20: 0 <> n & n < len F;
       0<n by A20;then
A21:   0+1<=n by NAT_1:38;then
A22:   1<=n+1 by NAT_1:38;
A23:   n+1<=len F by A20,NAT_1:38;
A24:   n+1<=len f2 by FINSEQ_1:def 3,A8,A3,A23;
A25:   n<=len f2 by A20,FINSEQ_1:def 3,A8,A3;
       (f2*').(n + 1) = (f2.(n+1))*' by A24,A22,COMPLSP2:def 1
        .= (addcomplex.(f2.n,(F*').(n + 1)))*' by A20,A3,A11
        .= (addcomplex.(f2.n,(F.(n + 1))*'))*' by A22,A23,COMPLSP2:def 1
        .= addcomplex.((f2.n)*',F.(n + 1)) by Th22;
       hence thesis by A25,A21,COMPLSP2:def 1;
      end;
A26: (f2*').(len F)=(f2*').(len (F*')) by COMPLSP2:def 1
       .= (f2.(len (F*')))*' by COMPLSP2:def 1,A1,A9
       .= d by A8,A6,A17;
    consider f' being Function of NAT,COMPLEX such that
A100: for n being Nat st 1<= n & n<=len (f2*')
                               holds f'.n=(f2*').n by Th23;
A50: len (f2*')=len f2 by COMPLSP2:def 1;
A50a: 1<=len (f2*') by FINSEQ_1:def 3,A8,A1,A50;
A18b: f'.1=(f2*').1 by A100,A50a;
A19b: for n st 0 <> n & n < len F holds
     f'.(n + 1) = addcomplex.(f'.n,F.(n + 1))
     proof let n;
      assume D1: 0 <> n & n < len F;then
      n>0;then
D4:   0+1<=n by NAT_1:38;
D5:   n<=len (f2*') by D1,A9,COMPLSP2:def 1,A3;
D2:   1<=n+1 by NAT_1:29;
      n+1<=len (f2*') by D1,NAT_1:38,A50,A9,A3;then
D3:   f'.(n+1)=(f2*').(n+1) by A100,D2;
      f'.n=(f2*').n by D4,D5,A100;
      hence f'.(n + 1) = addcomplex.(f'.n,F.(n + 1)) by D1,A19,D3;
     end;
     d=f'.(len F) by A26,A1,A100,A3,A9,A50;then
    d = addcomplex $$ F by A2,FINSOP_1:3,A18b,A18,A19b;
  hence addcomplex $$ (F*') = (addcomplex $$ F)*';
end;

theorem Th25:
  for F being FinSequence of COMPLEX st len F >= 1 holds
    Sum(F*') = (Sum F)*'
proof let F be FinSequence of COMPLEX;
   assume len F >= 1;then
A2:len (F*') >= 1 by COMPLSP2:def 1;
   Sum(F*') = addcomplex $$ (F*')
           .=(addcomplex $$ F)*' by Th24,A2
           .=(Sum F)*';
   hence thesis;
end;

theorem Th26:
  for x,y being FinSequence of COMPLEX st len x=len y holds
   (mlt(x,(y*')))*' = mlt(y,(x*'))
proof let x,y be FinSequence of COMPLEX;
   assume A1: len x=len y;
   A2: len (mlt(x,(y*'))) = len ((mlt(x,(y*')))*') by COMPLSP2:def 1;
   A3: len (y*') = len y by COMPLSP2:def 1;
   A4: len (x*') = len x by COMPLSP2:def 1;
   A5: len mlt(x,(y*')) = len x by Th19,A1,A3;
   A6: len mlt(x,(y*')) = len (y*') by Th19,A1,A3;
   A7: len ((mlt(x,(y*')))*') = len mlt(y,(x*')) by A4,A5,A2,Th19,A1;
   reconsider x'=x as Element of (len x)-tuples_on COMPLEX by FINSEQ_2:110;
   reconsider y'=y*' as Element of (len (y*'))-tuples_on COMPLEX
        by FINSEQ_2:110;
   reconsider y1'=y as Element of (len y)-tuples_on COMPLEX by FINSEQ_2:110;
   reconsider x1'=x*' as Element of (len (x*'))-tuples_on COMPLEX
        by FINSEQ_2:110;
   set a'=mlt(x,(y*'));
  now let i; assume
  1 <= i & i <= len ((mlt(x,(y*')))*');then
  A8: 1 <= i & i <= len (mlt(x,(y*'))) by COMPLSP2:def 1;
  hence (mlt(x,(y*')))*'.i = ((mlt(x,(y*'))).i)*' by COMPLSP2:def 1
                .= (((x'.i)*((y').i)))*' by Th21,A1,A3
                .= (x.i)*'*((y*').i)*' by COMPLEX1:121
                .= (x.i)*'*((y*')*'.i) by COMPLSP2:def 1,A8,A6
                .= (x.i)*'*(y.i) by COMPLSP2:22
                .= ((x*').i)*(y.i) by COMPLSP2:def 1,A8,A5
                .= mlt(x1',y1').i by Th21,A1,A4
                .= mlt(y,(x*')).i;
  end;
 hence thesis by FINSEQ_1:18,A7;
end;

theorem Th27:
  for x,y being FinSequence of COMPLEX,a being Element of COMPLEX
   st len x=len y holds (mlt(x,a*y)) = a*mlt(x,y)
proof
  let x,y be FinSequence of COMPLEX,a be Element of COMPLEX;
  assume A1: len x=len y;
  reconsider x'=x as Element of len x-tuples_on COMPLEX by FINSEQ_2:110;
  reconsider y'=y as Element of len y-tuples_on COMPLEX by FINSEQ_2:110;
  mlt(x,a*y) = a*mlt(x',y') by Th17,A1 .= a*mlt(x,y);
  hence thesis;
end;

theorem Th28:
  for x,y being FinSequence of COMPLEX,a being Element of COMPLEX
   st len x=len y holds mlt(a*x,y) = a*mlt(x,y)
proof
  let x,y be FinSequence of COMPLEX,a be Element of COMPLEX;
  assume A1: len x=len y;
  reconsider x'=x as Element of len x-tuples_on COMPLEX by FINSEQ_2:110;
  reconsider y'=y as Element of len y-tuples_on COMPLEX by FINSEQ_2:110;
  mlt(a*x,y) = a*mlt(x',y') by Th17,A1 .= a*mlt(x,y);
  hence thesis;
end;

theorem Th29:
  for x,y being FinSequence of COMPLEX st len x=len y holds
   (mlt(x,y))*' = mlt(x*',y*')
proof let x,y be FinSequence of COMPLEX;
  assume A1: len x=len y;
  A2: len ((mlt(x,y))*') = len (mlt(x,y)) by COMPLSP2:def 1;
  A3: len (y*') = len y by COMPLSP2:def 1;
  A4: len (x*') = len x by COMPLSP2:def 1;
  A5: len (mlt(x*',y*')) = len (x*') by Th19,A1,A4,A3;
  A7: len ((mlt(x,y))*') = len (mlt(x*',y*')) by A4,A5,A2,Th19,A1;
  for i st 1 <=i & i <= len ((mlt(x,y))*') holds
   (mlt(x,(y)))*'.i=mlt(x*',y*').i
  proof let i;
    (mlt(x,y))*'.i = ((mlt(y,(x*')*')*').i) by COMPLSP2:22
       .= (mlt(x*',y*').i) by Th26,A1,A4;
    hence thesis;
  end;
 hence thesis by FINSEQ_1:18,A7;
end;

Lm3:
  for a,b being Element of COMPLEX holds
   (multcomplex[;](a,id COMPLEX)).b = a*b
 proof let a,b be Element of COMPLEX;
  thus (multcomplex[;](a,id COMPLEX)).b
    = multcomplex.(a,(id COMPLEX).b) by FUNCOP_1:66
   .= multcomplex.(a,b) by FUNCT_1:35
   .= a*b by BINOP_2:def 5;
 end;

theorem Th30:
  for F being FinSequence of COMPLEX,a being Element of COMPLEX holds
   Sum(a*F) = a*(Sum F)
 proof let F be FinSequence of COMPLEX,a be Element of COMPLEX;
   set rM = multcomplex[;](a,id COMPLEX);
   thus Sum (a*F) = addcomplex $$(a*F)
    .= addcomplex $$(rM*F) by Lm2
    .= rM.(addcomplex $$F) by COMPLSP1:15,COMPLSP1:7,SETWOP_2:41
    .= rM.(Sum F)
    .= a*(Sum F) by Lm3;
 end;

definition let x be FinSequence of REAL;
  func FR2FC(x) -> FinSequence of COMPLEX equals
    x;
  coherence
  proof
    rng x c= REAL by FINSEQ_1:def 4;
    then rng x c= COMPLEX by XBOOLE_1:1,NUMBERS:11;
    hence thesis by FINSEQ_1:def 4;
  end;
end;

theorem Th31:
  for R being FinSequence of REAL,
      F being FinSequence of COMPLEX st R = F & len R >= 1 holds
    addreal $$ R = addcomplex $$ F
proof
  let R be FinSequence of REAL,F be FinSequence of COMPLEX;
  assume A1: R = F & len R >= 1;
  set d = addreal $$ R;
  consider f22 be Function of NAT, REAL such that
  A4: f22.1 = R.1 and
  A5: for n st 0 <> n & n < len R holds
      f22.(n + 1) = addreal.(f22.n,R.(n + 1)) and
  A6: addreal $$ R = f22.len R by FINSOP_1:2,A1;
  defpred P[set,set] means $2= f22.$1;
A7: for k st k in Seg len R ex x being Element of REAL st P[k,x]
  proof let k;
    f22.k is Element of REAL;
    hence thesis;
  end;
  ex f2 being FinSequence of REAL st dom f2 = Seg len R &
   for k st k in Seg len R holds P[k,f2.k] from FINSEQ_1:sch 5(A7);then
  consider f2 being FinSequence of REAL such that
  A8: dom f2 = Seg len R & for k st k in Seg len R holds P[k,f2.k];
  A9: len f2 = len R by FINSEQ_1:def 3,A8;
  A10: 1 in Seg len R by A1,FINSEQ_1:3;
  A11: for n st 0 <> n & n < len R holds
       f2.(n + 1) = addreal.(f2.n,R.(n + 1))
    proof let n;
     assume A12: 0 <> n & n < len R;
      0<n by A12;then
      A13: 0+1<=n by NAT_1:38;
      A14: 1<=n+1 by NAT_1:38,A13;
      A15: n+1<=len R by A12,NAT_1:38;
      A16: n in Seg len R by FINSEQ_1:3,A13,A12;
      (n+1) in Seg len R by FINSEQ_1:3,A15,A14;then
      f2.(n + 1) = f22.(n + 1) by A8
      .= addreal.(f22.n,R.(n + 1)) by A5,A12
      .= addreal.(f2.n,R.(n + 1)) by A8,A16;
     hence thesis;
    end;
    A17: len R in Seg len R by A1,FINSEQ_1:3;
    A18: F.1 = (f2.1) by A8,A4,A10,A1
       .= (FR2FC f2).1;
    A19: for n st 0 <> n & n < len R holds
        (FR2FC f2).(n + 1) = addreal.((FR2FC f2).n,R.(n + 1)) by A11;
A26: (FR2FC f2).len F = f2.len R by A1
       .= d by A8,A6,A17;
    consider f' being Function of NAT,COMPLEX such that
    A100: for n being Nat st 1<= n & n<=len FR2FC f2
                               holds f'.n=(FR2FC f2).n by Th23;
    A50b: 1<=len FR2FC f2 by A9,A1;
    A18b: f'.1=(FR2FC f2).1 by A100,A50b;
    A19b: for n st 0 <> n & n < len F holds
    f'.(n + 1) = addcomplex.(f'.n,F.(n + 1))
     proof let n;
      assume D1: 0 <> n & n < len F;then
       n>0;then
       D4: 0+1<=n by NAT_1:38;
       D5: n<=len FR2FC f2 by D1,FINSEQ_1:def 3,A8,A1;
       D2: 1<=n+1 by NAT_1:29;
       n+1<=len (FR2FC f2) by D1,NAT_1:38,A9,A1;then
       D3: f'.(n+1)=(FR2FC f2).(n+1) by A100,D2;
       D6: f'.n=(FR2FC f2).n by D4,D5,A100;
       addcomplex.(f'.n,F.(n + 1))
       =addreal.((f2).n,R.(n + 1)) by COMPLSP2:44,D6,A1;
      hence f'.(n + 1) = addcomplex.(f'.n,F.(n + 1)) by D1,A19,D3,A1;
     end;
     (FR2FC f2).(len F)=f'.(len F) by A1,A100,A9;
  hence addreal $$ R = addcomplex $$ F
    by A1,FINSOP_1:3,A18b,A19b,A26,A18;
end;

theorem Th32:
  for x being FinSequence of REAL,
      y being FinSequence of COMPLEX st x=y & len x >=1
  holds Sum x = Sum y
proof let x be FinSequence of REAL,y be FinSequence of COMPLEX;
  assume A1: x=y & len x >=1;
  Sum x = addreal $$ x by RVSUM_1:def 10
  .=addcomplex $$ y by Th31,A1
  .=Sum y;
  hence thesis;
end;

theorem Th33:
  for F1,F2 being FinSequence of COMPLEX  st len F1=len F2 holds
    Sum(F1 - F2) = Sum F1 - Sum F2
proof let F1,F2 be FinSequence of COMPLEX;
  assume A1: len F1=len F2;
   reconsider x2=F1 as Element of (len F1)-tuples_on COMPLEX by FINSEQ_2:110;
   reconsider y2=F2 as Element of (len F2)-tuples_on COMPLEX by FINSEQ_2:110;
   Sum(F1 - F2) = addcomplex $$(F1 - F2)
    .= addcomplex $$(diffcomplex.:(F1,F2)) by COMPLSP1:def 8
    .= diffcomplex.(addcomplex$$x2,addcomplex$$y2)
       by SETWOP_2:48,A1,COMPLSP1:def 3,COMPLSP1:7,COMPLSP1:8
    .= diffcomplex.(Sum F1,addcomplex$$F2)
    .= diffcomplex.(Sum F1,Sum F2)
    .= Sum F1 - Sum F2 by BINOP_2:def 4;
  hence thesis;
 end;

theorem Th34:
  for F1,F2 being FinSequence of COMPLEX,i being Nat holds
    i in dom (F1+F2) implies (F1+F2).i = F1.i + F2.i
 proof let F1,F2 be FinSequence of COMPLEX,i be Nat;
  assume
  A1: i in dom (F1+F2);
  set r1 = F1.i, r2 = F2.i;
  F1 + F2 = addcomplex.:(F1,F2) & i in dom(F1+F2) by A1,COMPLSP1:def 7;
  then (F1 + F2).i = addcomplex.(r1,r2) by FUNCOP_1:28;
  hence thesis by BINOP_2:def 3;
 end;

theorem Th35:
  for F1,F2 being FinSequence of COMPLEX,i being Nat holds
    i in dom (F1-F2) implies (F1-F2).i = F1.i - F2.i
proof let F1,F2 be FinSequence of COMPLEX,i be Nat;
  assume A1: i in dom (F1-F2);
  set r1 = F1.i, r2 = F2.i;
  F1 - F2 = diffcomplex.:(F1,F2) & i in dom(F1-F2) by A1,COMPLSP1:def 8;
  then (F1 - F2).i = diffcomplex.(r1,r2) by FUNCOP_1:28;
  hence thesis by BINOP_2:def 4;
end;

theorem
  for x,y,z being FinSequence of COMPLEX st len x=len y & len y=len z holds
    mlt((x-y),z) = mlt(x,z)-mlt(y,z)
proof let x,y,z be FinSequence of COMPLEX;
  assume len x=len y & len y=len z;
  then reconsider x2=x, y2=y, z2=z as Element of (len x)-tuples_on COMPLEX
      by FINSEQ_2:110;
  A1: dom mlt(x-y,z)=Seg len mlt(x2-y2,z2) by FINSEQ_1:def 3
                 .=Seg len x by FINSEQ_2:109
                 .=Seg len (mlt(x2,z2)-mlt(y2,z2)) by FINSEQ_2:109
                 .= dom (mlt(x2,z2)-mlt(y2,z2)) by FINSEQ_1:def 3;
  A2: dom mlt(x,z)=Seg len mlt(x2,z2) by FINSEQ_1:def 3
                 .=Seg len x by FINSEQ_2:109
                 .=Seg len (mlt(x2,z2)-mlt(y2,z2)) by FINSEQ_2:109
                 .= dom (mlt(x2,z2)-mlt(y2,z2)) by FINSEQ_1:def 3;
  A3: dom (mlt(y,z))=Seg len(mlt(y2,z2)) by FINSEQ_1:def 3
                 .=Seg len x by FINSEQ_2:109
                 .=Seg len (mlt(x2,z2)-mlt(y2,z2)) by FINSEQ_2:109
                 .= dom (mlt(x2,z2)-mlt(y2,z2)) by FINSEQ_1:def 3;
     for i being Nat st i in dom mlt(x-y,z) holds
    mlt(x-y,z).i=(mlt(x,z)-mlt(y,z)).i
   proof let i be Nat;
    assume A4: i in dom mlt(x-y,z);
        len (x2-y2)=len x by FINSEQ_2:109;
     then A5: dom (x2-y2)=Seg len x by FINSEQ_1:def 3
     .=Seg len(mlt(x2,z2)) by FINSEQ_2:109
     .=dom (mlt(x,z)) by FINSEQ_1:def 3;
     set a=x.i, b=y.i, d=(x-y).i, e=z.i;
     A6: d=a-b by A1,A2,A4,A5,Th35;
    thus mlt(x-y,z).i=d*e by A4,Th20
     .=a*e-b*e by A6
     .=mlt(x,z).i -b*e by A1,A2,A4,Th20
     .=mlt(x,z).i -mlt(y,z).i by A1,A3,A4,Th20
     .=(mlt(x,z)-mlt(y,z)).i by A1,A4,Th35;
   end;
 hence thesis by A1,FINSEQ_1:17;
end;

theorem Th37:
  for x,y,z being FinSequence of COMPLEX st len x=len y & len y=len z holds
    mlt(x,y-z) = mlt(x,y)-mlt(x,z)
proof let x,y,z be FinSequence of COMPLEX;
  assume len x=len y & len y=len z;
  then reconsider x2=x, y2=y, z2=z as Element of (len x)-tuples_on COMPLEX
      by FINSEQ_2:110;
  A1: dom (mlt(x,y-z))=Seg len(mlt(x2,y2-z2)) by FINSEQ_1:def 3
      .=Seg len x by FINSEQ_2:109
      .=Seg len (mlt(x2,y2)-mlt(x2,z2)) by FINSEQ_2:109
      .=dom (mlt(x2,y2)-mlt(x2,z2)) by FINSEQ_1:def 3;
  A2: dom mlt(x,y)=Seg len(mlt(x2,y2)) by FINSEQ_1:def 3
      .=Seg len x by FINSEQ_2:109
      .=Seg len (mlt(x2,y2)-mlt(x2,z2)) by FINSEQ_2:109
      .=dom (mlt(x2,y2)-mlt(x2,z2)) by FINSEQ_1:def 3;
  A3: dom mlt(x,z)=Seg len mlt(x2,z2) by FINSEQ_1:def 3
      .=Seg len x by FINSEQ_2:109
      .=Seg len (mlt(x2,y2)-mlt(x2,z2)) by FINSEQ_2:109
      .= dom (mlt(x2,y2)-mlt(x2,z2)) by FINSEQ_1:def 3;
  for i being Nat st i in dom (mlt(x,y-z)) holds
    mlt(x,y-z).i=(mlt(x,y)-mlt(x,z)).i
  proof let i be Nat;
    assume A4: i in dom mlt(x,y-z);
        len (y2-z2)=len x by FINSEQ_2:109;
     then A5: dom (y2-z2)=Seg len x by FINSEQ_1:def 3
     .=Seg len mlt(x2,y2) by FINSEQ_2:109
     .=dom mlt(x,y) by FINSEQ_1:def 3;
     set a=y.i, b=z.i, d=(y-z).i, e=x.i;
     A6: d=a-b by A1,A2,A4,A5,Th35;
    thus mlt(x,y-z).i=e*d by A4,Th20
     .=e*a-e*b by A6
     .=mlt(x,y).i -e*b by A1,A2,A4,Th20
     .=mlt(x,y).i -mlt(x,z).i by A1,A3,A4,Th20
     .=(mlt(x,y)-mlt(x,z)).i by A1,A4,Th35;
  end;
 hence thesis by A1,FINSEQ_1:17;
end;

theorem
  for x,y,z being FinSequence of COMPLEX st len x=len y & len y=len z holds
   mlt(x,y+z) = mlt(x,y)+mlt(x,z)
proof let x,y,z be FinSequence of COMPLEX;
  assume len x=len y & len y=len z;
  then reconsider x2=x, y2=y, z2=z as Element of (len x)-tuples_on COMPLEX
      by FINSEQ_2:110;
  A1: dom (mlt(x,y+z))=Seg len(mlt(x2,y2+z2)) by FINSEQ_1:def 3
      .=Seg len x by FINSEQ_2:109
      .=Seg len (mlt(x2,y2)+mlt(x2,z2)) by FINSEQ_2:109
      .=dom (mlt(x2,y2)+mlt(x2,z2)) by FINSEQ_1:def 3;
  A2: dom mlt(x,y)=Seg len mlt(x2,y2) by FINSEQ_1:def 3
      .=Seg len x by FINSEQ_2:109
      .=Seg len (mlt(x2,y2)+mlt(x2,z2)) by FINSEQ_2:109
      .=dom (mlt(x2,y2)+mlt(x2,z2)) by FINSEQ_1:def 3;
  A3: dom mlt(x,z)=Seg len mlt(x2,z2) by FINSEQ_1:def 3
      .=Seg len x by FINSEQ_2:109
      .=Seg len (mlt(x2,y2)+mlt(x2,z2)) by FINSEQ_2:109
      .= dom (mlt(x2,y2)+mlt(x2,z2)) by FINSEQ_1:def 3;
  for i being Nat st i in dom mlt(x,y+z) holds
    mlt(x,y+z).i=(mlt(x,y)+mlt(x,z)).i
  proof let i be Nat;
    assume A4: i in dom mlt(x,y+z);
     len (y2+z2)=len x by FINSEQ_2:109;
     then A5: dom (y2+z2)=Seg len x by FINSEQ_1:def 3
     .=Seg len mlt(x2,y2) by FINSEQ_2:109
     .=dom mlt(x,y) by FINSEQ_1:def 3;
     set a=y.i, b=z.i, d=(y+z).i, e=x.i;
     A6: d=a+b by A1,A2,A4,A5,Th34;
    thus mlt(x,y+z).i=e*d by A4,Th20
     .=e*a+e*b by A6
     .=mlt(x,y).i +e*b by A1,A2,A4,Th20
     .=mlt(x,y).i +mlt(x,z).i by A1,A3,A4,Th20
     .=(mlt(x,y)+mlt(x,z)).i by A1,A4,Th34;
  end;
 hence thesis by A1,FINSEQ_1:17;
end;

theorem Th39:
  for x,y,z being FinSequence of COMPLEX st len x=len y & len y=len z holds
   mlt((x+y),z) = mlt(x,z)+mlt(y,z)
proof let x,y,z be FinSequence of COMPLEX;
  assume len x=len y & len y=len z;
  then reconsider x2=x, y2=y, z2=z as Element of (len x)-tuples_on COMPLEX
      by FINSEQ_2:110;
  A1: dom mlt(x+y,z)=Seg len mlt(x2+y2,z2) by FINSEQ_1:def 3
      .=Seg len x by FINSEQ_2:109
      .=Seg len (mlt(x2,z2)+mlt(y2,z2)) by FINSEQ_2:109
      .= dom (mlt(x2,z2)+mlt(y2,z2)) by FINSEQ_1:def 3;
  A2: dom mlt(x,z)=Seg len mlt(x2,z2) by FINSEQ_1:def 3
      .=Seg len x by FINSEQ_2:109
      .=Seg len (mlt(x2,z2)+mlt(y2,z2)) by FINSEQ_2:109
      .= dom (mlt(x2,z2)+mlt(y2,z2)) by FINSEQ_1:def 3;
  A3: dom mlt(y,z)=Seg len mlt(y2,z2) by FINSEQ_1:def 3
      .=Seg len x by FINSEQ_2:109
      .=Seg len (mlt(x2,z2)+mlt(y2,z2)) by FINSEQ_2:109
      .= dom (mlt(x2,z2)+mlt(y2,z2)) by FINSEQ_1:def 3;
  for i being Nat st i in dom mlt(x+y,z) holds
    mlt(x+y,z).i=(mlt(x,z)+mlt(y,z)).i
  proof let i be Nat;
    assume A4: i in dom mlt(x+y,z);
        len (x2+y2)=len x by FINSEQ_2:109;
     then A5: dom (x2+y2)=Seg len x by FINSEQ_1:def 3
     .=Seg len mlt(x2,z2) by FINSEQ_2:109
     .=dom mlt(x,z) by FINSEQ_1:def 3;
     set a=x.i, b=y.i, d=(x+y).i, e=z.i;
     A6: d=a+b by A1,A2,A4,A5,Th34;
    thus mlt(x+y,z).i=d*e by A4,Th20
     .=a*e+b*e by A6
     .=mlt(x,z).i +b*e by A1,A2,A4,Th20
     .=mlt(x,z).i +mlt(y,z).i by A1,A3,A4,Th20
     .=(mlt(x,z)+mlt(y,z)).i by A1,A4,Th34;
  end;
 hence thesis by A1,FINSEQ_1:17;
end;

theorem Th40:
  for F1,F2 being FinSequence of COMPLEX  st len F1=len F2 holds
   Sum(F1 + F2) = Sum F1 + Sum F2
proof let F1,F2 be FinSequence of COMPLEX;
  assume A1: len F1=len F2;
   reconsider x2=F1 as Element of (len F1)-tuples_on COMPLEX by FINSEQ_2:110;
   reconsider y2=F2 as Element of (len F2)-tuples_on COMPLEX by FINSEQ_2:110;
   Sum(F1 + F2) = addcomplex $$(F1 + F2)
    .= addcomplex $$(addcomplex.:(F1,F2)) by COMPLSP1:def 7
    .= addcomplex.(addcomplex$$x2,addcomplex$$y2) by SETWOP_2:46,A1
    .= addcomplex.(Sum F1,addcomplex$$F2)
    .= addcomplex.(Sum F1,Sum F2)
    .= Sum F1 + Sum F2 by BINOP_2:def 3;
  hence thesis;
 end;

theorem Th41:
  for x1,y1 being FinSequence of COMPLEX
  for x2,y2 being FinSequence of REAL
  st x1 = x2 & y1 = y2 & len x1=len y2 holds
    multcomplex.:(x1,y1) = multreal.:(x2,y2)
proof
  let x1,y1 be FinSequence of COMPLEX;
  let x2,y2 be FinSequence of REAL;
  assume A1: x1 = x2 & y1 = y2 & len x1=len y2;
  for i st i in dom x1 holds
    multcomplex.(x1.i,y1.i)=multreal.(x2.i,y2.i)
  proof
    let i;
    x1.i * y1.i = multcomplex.(x1.i,y1.i) by BINOP_2:def 5;
    hence thesis by A1,BINOP_2:def 11;
  end;
  hence thesis by A1,COMPLSP2:45;
end;

theorem Th42:
  for x,y being FinSequence of REAL st len x=len y holds
    FR2FC mlt(x,y)=mlt(FR2FC x,FR2FC y)
proof
  let x,y be FinSequence of REAL;
  assume A1: len x=len y;
  FR2FC mlt(x,y)= mlt(x,y)
  .=multreal.:(x,y) by RVSUM_1:def 9
  .=multcomplex.:(FR2FC x,FR2FC y) by Th41,A1
  .=mlt(FR2FC x,FR2FC y);
  hence thesis;
end;

theorem Th43:
  for x,y being FinSequence of COMPLEX st len x=len y & len x > 0 holds
    |( x,y )| = Sum mlt(x,(y*'))
proof let x,y be FinSequence of COMPLEX;
   assume A1: len x=len y & len x > 0;
   A2: len x >= 1+0 by A1,NAT_1:38;
   A3: len Re x=len x by COMPLSP2:48;
   A4: len Re y=len y by COMPLSP2:48;
   A5: len Im x=len x by COMPLSP2:48;
   A6: len Im y=len y by COMPLSP2:48;
   reconsider x2=x as Element of (len x)-tuples_on COMPLEX by FINSEQ_2:110;
   reconsider y2=y as Element of (len y)-tuples_on COMPLEX by FINSEQ_2:110;
   A7: mlt(Im x,Im y)=FR2FC(mlt(Im x,Im y))
       .= mlt(FR2FC(Im x),FR2FC(Im y)) by Th42,A5,A6,A1;
   A8: mlt(Im x,Re y)=FR2FC(mlt(Im x,Re y))
       .= mlt(FR2FC(Im x),FR2FC(Re y)) by Th42,A4,A5,A1;
   A9: mlt(Re x,Re y)=FR2FC(mlt(Re x,Re y))
       .= mlt(FR2FC Re x,FR2FC Re y) by Th42,A4,A3,A1;
   A10: mlt(Re x,Im y)=FR2FC mlt(Re x,Im y)
       .= mlt(FR2FC Re x,FR2FC Im y) by Th42,A6,A3,A1;
   set Rex=FR2FC Re x;
   set Rey=FR2FC Re y;
   set Imy=FR2FC Im y;
   set Imx=FR2FC Im x;
   A11: len ((<i>)*(Imy))=len Imy by COMPLSP2:3
       .= len Im y .= len y by COMPLSP2:48;
   A12: len ((<i>)*(Imx))=len Imx by COMPLSP2:3
       .= len Im x .= len x by COMPLSP2:48
       .= len Re x by COMPLSP2:48
       .= len Rex;
   A13: len ((<i>)*(Imx))=len Imx by COMPLSP2:3
      .= len Im x
      .= len y by A1,COMPLSP2:48 .= len (y*') by COMPLSP2:def 1;
   A14: len Imx=len Im x
      .= len y by A1,COMPLSP2:48 .= len (y*') by COMPLSP2:def 1;
   A15: len Rey = len Re y .= len y by COMPLSP2:48;
   A16: len Rex = len Re x .= len x by COMPLSP2:48;
   A17: len Imy = len Im y .= len y by COMPLSP2:48;
   A18: len Imx = len Im x .= len x by COMPLSP2:48;
   A19: len (-(mlt(Imx,Imy)))=len (mlt(Imx,Imy)) by COMPLSP2:5;
   A20: len (mlt(Imx,Imy)) = len Imx by Th19,A18,A17,A1
       .= len Im x
       .= len x by COMPLSP2:48;
   A21: len (mlt(Imx,Rey)) = len Imx by Th19,A18,A15,A1
       .= len Im x
       .= len x by COMPLSP2:48;
   A22: len mlt(Rex,Rey)
        = len Rex by Th19,A16,A15,A1
       .= len Re x
       .= len x by COMPLSP2:48;
   A23: len mlt(Rex,Imy)
        = len Rex by Th19,A16,A17,A1
       .= len Re x
       .= len x by COMPLSP2:48;
   A24: len (<i>*(mlt(Rex,Imy)))
       = len mlt(Rex,Imy) by COMPLSP2:3
      .= len Rex by Th19,A16,A17,A1
      .= len Re x
      .= len x by COMPLSP2:48;
   A25: len (<i>*mlt(Imx,Rey))
       = len (mlt(Imx,Rey)) by COMPLSP2:3
      .= len Imx by Th19,A18,A15,A1
      .= len Im x
      .= len x by COMPLSP2:48;
   A26: len ((mlt(Rex,Rey) - ((<i>*(mlt(Rex,Imy))))))
       = len (<i>*mlt(Imx,Rey)) by A25,COMPLSP2:7,A22,A24;
   A27: len ((mlt(Rex,Rey)) - (<i>*(mlt(Rex,Imy))))
       = len (<i>*mlt(Imx,Rey)) by A25,COMPLSP2:7,A22,A24;
   A28: len (((mlt(Rex,Rey)
       - (<i>*(mlt(Rex,Imy)))) + ((<i>*mlt(Imx,Rey)))))
       = len ((mlt(Rex,Rey))
       - (<i>*(mlt(Rex,Imy)))) by A27,COMPLSP2:6
      .= len mlt(Imx,Imy) by A20,COMPLSP2:7,A22,A24;
   A29: len mlt(Rex,<i>*Imy)=len Rex by Th19,A11,A16,A1;
   A30: len mlt(Imx,Rey)
       =len mlt(Imx,<i>*Imy) by Th19,A11,A18,A1,A21;
   A31: len (mlt(Rex,Rey)-mlt(Rex,<i>*Imy))=len (mlt(Rex,Rey))
       by COMPLSP2:7,A22,A29,A16;
   A32: len (x*') = len x by COMPLSP2:def 1;
   A33: Rex = Re x .= 1/2*(x+x*') by COMPLSP2:def 2;
   A34: Imx = Im x .= (-1/2*<i>)*(x-x*') by COMPLSP2:def 3;
   A35: <i>*Imx = <i>*(-1/2*<i>)*(x-x*') by COMPLSP2:53,A34
      .= (1/2)*(x-x*');
   A36: len (x+x*') = len x by A32,COMPLSP2:6;
   A37: len (x*'+x) = len x by A32,COMPLSP2:6;
   A38: len (x-x*') = len x by COMPLSP2:7,A32;
   A39: x = -(-x) by COMPLSP2:38
       .= -(-1)*x  by COMPLSP2:12
       .= (-1)*((-1)*x) by COMPLSP2:12
       .= ((-1)*(-1))*x by COMPLSP2:53
       .= 1*x;
   A40: len x = len x;
   A41: Rex + <i>*Imx
      = 1/2*((x+x*') + (x-x*')) by COMPLSP2:30,A36,A38,A33,A35
     .= 1/2*((x+x*') + x -x*') by COMPLSP2:37,A32,A36
     .= 1/2*(x+(x*' + x) -x*') by COMPLSP2:28,A32
     .= 1/2*(x+(x*' + x -x*')) by COMPLSP2:37,A32,A37
     .= 1/2*(x+(x + x*' -x*')) by COMPLSP2:29,A32
     .= 1/2*(x+(x + (x*' -x*'))) by COMPLSP2:37,A32
     .= 1/2*(x+(x + (x*' +-x*'))) by COMPLSP2:31,A32
     .= 1/2*(x+(x + (0c (len (x*'))))) by COMPLSP2:34
     .= 1/2*(x+(x + (0c (len x)))) by COMPLSP2:def 1
     .= 1/2*(x+x) by COMPLSP2:33
     .= 1/2*x+1/2*x by COMPLSP2:30,A40
     .= (1/2+1/2)*x by COMPLSP2:63
     .= x by A39;
   A42: len (y*') = len y by COMPLSP2:def 1;
   A43: Rey = Re y .= 1/2*(y+y*') by COMPLSP2:def 2;
   A44: Imy = Im y .= (-1/2*<i>)*(y-y*') by COMPLSP2:def 3;
   A45: <i>*Imy = <i>*(-1/2*<i>)*(y-y*') by COMPLSP2:53,A44
      .= (1/2)*(y-y*');
   A46: len (y+y*') = len y by A42,COMPLSP2:6;
   A47: len (y-y*') = len y by COMPLSP2:7,A42;
   A48: y*' = -(-y*')  by COMPLSP2:38
       .= -((-1)*y*') by COMPLSP2:12
       .= (-1)*((-1)*y*') by COMPLSP2:12
       .= ((-1)*(-1))*y*' by COMPLSP2:53
       .= 1*y*';
   A49: len (y*') = len (y*');
   A50: Rey - <i>*Imy
      = 1/2*((y+y*') - (y-y*')) by COMPLSP2:43,A46,A47,A43,A45
     .= 1/2*((y+y*') - y +y*') by COMPLSP2:40,A42,A46
     .= 1/2*((y*'+y) - y +y*') by COMPLSP2:29,A42
     .= 1/2*(y*'+(y - y) +y*') by COMPLSP2:37,A42
     .= 1/2*(y*'+(y +- y) +y*') by COMPLSP2:31,A42
     .= 1/2*(y*'+(0c (len y)) +y*') by COMPLSP2:34
     .= 1/2*(y*'+y*') by COMPLSP2:33,A42
     .= 1/2*y*'+1/2*y*' by COMPLSP2:30,A49
     .= (1/2+1/2)*y*' by COMPLSP2:63
     .= y*' by A48;
  |( x,y )| = |(Re x,Re y)| - <i>*(|(Re x,Im y)|)
       + <i>*(|(Im x,Re y)|) + |(Im x,Im y)| by COMPLSP2:def 4
 .= Sum mlt(Re x,Re y) - <i>*(|(Re x,Im y)|)
       + <i>*(|(Im x,Re y)|) + |(Im x,Im y)| by EUCLID_2:def 1
 .= Sum mlt(Re x,Re y) - <i>*(Sum mlt(Re x,Im y))
       + <i>*(|(Im x,Re y)|) + |(Im x,Im y)| by EUCLID_2:def 1
 .= Sum mlt(Re x,Re y) - <i>*(Sum mlt(Re x,Im y))
       + <i>*(Sum mlt(Im x,Re y)) + |(Im x,Im y)| by EUCLID_2:def 1
 .= Sum mlt(Re x,Re y) - <i>*(Sum mlt(Re x,Im y))
       + <i>*(Sum mlt(Im x,Re y)) + Sum mlt(Im x,Im y) by EUCLID_2:def 1
 .= Sum (mlt(Rex,Rey)) - <i>*(Sum mlt(Re x,Im y))
       + <i>*(Sum mlt(Im x,Re y)) + Sum mlt(Im x,Im y) by A9,Th32,A22,A2
 .= Sum (mlt(Rex,Rey)) - <i>*(Sum mlt(Rex,Imy))
       + <i>*(Sum mlt(Im x,Re y)) + Sum mlt(Im x,Im y) by A10,Th32,A23,A2
 .= Sum (mlt(Rex,Rey)) - <i>*(Sum mlt(Rex,Imy))
       + <i>*(Sum mlt(Imx,Rey)) + Sum mlt(Im x,Im y) by A8,Th32,A21,A2
 .= Sum (mlt(Rex,Rey)) - <i>*(Sum mlt(Rex,Imy))
       + <i>*(Sum mlt(Imx,Rey))
       + Sum mlt(Imx,Imy) by A7,Th32,A20,A2
 .= Sum (mlt(Rex,Rey)) - (Sum(<i>*(mlt(Rex,Imy)))) + <i>*(Sum mlt(Imx,Rey))
       + Sum mlt(Imx,Imy) by Th30
 .= Sum (mlt(Rex,Rey)) - (Sum(<i>*(mlt(Rex,Imy))))
       + (Sum (<i>*mlt(Imx,Rey))) + Sum mlt(Imx,Imy) by Th30
 .= Sum ((mlt(Rex,Rey)) - ((<i>*(mlt(Rex,Imy)))))
       + (Sum (<i>*mlt(Imx,Rey)))
       + Sum mlt(Imx,Imy) by Th33,A22,A24
 .= Sum (((mlt(Rex,Rey)) - ((<i>*( mlt(Rex,Imy)))))
       + ((<i>*mlt(Imx,Rey)))) + Sum mlt(Imx,Imy) by Th40,A26
 .= Sum (mlt(Rex,Rey)- <i>*(mlt(Rex,Imy))
       + <i>*mlt(Imx,Rey) + mlt(Imx,Imy)) by Th40,A28
 .= Sum (mlt(Rex,Rey)- (mlt(Rex,<i>*Imy))
       + <i>*mlt(Imx,Rey) + mlt(Imx,Imy)) by Th27,A17,A16,A1
 .= Sum (((mlt(Rex,Rey)- (mlt(Rex,<i>*Imy))))
       + (<i>*mlt(Imx,Rey) + mlt(Imx,Imy)))
        by COMPLSP2:28,A31,A25,A20,A22
 .= Sum (mlt(Rex,Rey)-mlt(Rex,<i>*Imy)
       + (<i>*(mlt(Imx,Rey))+(--mlt(Imx,Imy)))) by COMPLSP2:38
 .= Sum (mlt(Rex,Rey)-mlt(Rex,<i>*Imy)
       + (<i>*(mlt(Imx,Rey))-(-mlt(Imx,Imy))))
        by COMPLSP2:10,A25,A20,A19
 .= Sum (mlt(Rex,Rey)-mlt(Rex,<i>*Imy)
       + (<i>*(mlt(Imx,Rey))-<i>*<i>*(mlt(Imx,Imy))))  by COMPLSP2:12
 .= Sum (mlt(Rex,Rey)-mlt(Rex,<i>*Imy)
       + (<i>*(mlt(Imx,Rey))-<i>*(<i>*(mlt(Imx,Imy))))) by COMPLSP2:53
 .= Sum (mlt(Rex,Rey)-mlt(Rex,<i>*Imy)
       + (<i>*(mlt(Imx,Rey))-<i>*(mlt(Imx,<i>*Imy)))) by Th27,A17,A18,A1
 .= Sum (mlt(Rex,Rey)-mlt(Rex,<i>*Imy) + <i>*(mlt(Imx,Rey)-mlt(Imx,<i>*Imy)))
     by COMPLSP2:43,A30
 .= Sum (mlt(Rex,Rey)-mlt(Rex,<i>*Imy) + <i>*mlt(Imx,y*'))
     by A50,Th37,A15,A18,A11,A1
 .= Sum (mlt(Rex,Rey-<i>*Imy) + <i>*mlt(Imx,y*')) by Th37,A16,A15,A11,A1
 .= Sum ((mlt(Rex,y*'))+(mlt((<i>)*(Imx),y*'))) by Th28,A14,A50
 .= Sum mlt(x,y*') by A41,Th39,A12,A13;
  hence thesis;
end;

theorem
  for A,B being Matrix of COMPLEX st
  len A=len B & width A=width B holds Indices A=Indices B
proof let A,B be Matrix of COMPLEX;
  assume A1: len A=len B & width A=width B;
A2: Indices A = [:dom A,Seg width A:] by MATRIX_1:def 5;
A3: Indices B = [:dom B,Seg width B:] by MATRIX_1:def 5;
  dom A = Seg len A by FINSEQ_1:def 3;
  hence Indices A = Indices B by A1,A2,A3,FINSEQ_1:def 3;
end;

theorem
  for i,j being Nat,M1,M2 being Matrix of COMPLEX
  st len M1=len M2 & width M1=width M2 & j in Seg len M1 holds
    Line(M1+M2,j)=Line(M1,j)+Line(M2,j)
proof let i,j be Nat,M1,M2 be Matrix of COMPLEX;
  assume A1: len M1=len M2 & width M1=width M2 & j in Seg len M1;
  A2: len (M1+M2)=len M1 & width (M1+M2)=width M1 by Th6;
  A3: len Line(M2,j)=width M1 by A1,MATRIX_1:def 8
       .= len Line(M1,j) by MATRIX_1:def 8;
  A4: len (Line(M1,j)+Line(M2,j))=len (Line(M1,j)) by COMPLSP2:6,A3
       .= width M1 by MATRIX_1:def 8;
  A5: len (Line(M1+M2,j))=width (M1+M2) by MATRIX_1:def 8
       .= width M1 by Th6;
  now let i; assume
  A6: 1 <= i & i <= len Line(M1+M2,j);
  A7: i in Seg width M1 by FINSEQ_1:3,A6,A5;
  A8: i in Seg width M2 by FINSEQ_1:3,A6,A5,A1;
  A9: i in Seg len (Line(M1,j)+Line(M2,j)) by A4,FINSEQ_1:3,A6,A5;
  A10: i in dom (Line(M1,j)+Line(M2,j)) by FINSEQ_1:def 3,A9;
  A11: j in Seg len M1 & i in Seg width M1 by FINSEQ_1:3,A6,A5,A1;
  A12: [j,i] in [:Seg len M1,Seg width M1:] by ZFMISC_1:106,A11;
  A13: [j,i] in [:dom M1,Seg width M1:] by FINSEQ_1:def 3,A12;
  A14: [j,i] in Indices M1 by MATRIX_1:def 5,A13;
  i in Seg width (M1+M2) by FINSEQ_1:3,A6,A5,A2;
  hence (Line(M1+M2,j)).i
    = (M1+M2)*(j,i) by MATRIX_1:def 8
   .= M1*(j,i)+M2*(j,i) by A14,Th7,A1
   .= M1*(j,i)+(Line(M2,j)).i by A8,MATRIX_1:def 8
   .= (Line(M1,j)).i+(Line(M2,j)).i by A7,MATRIX_1:def 8
   .= (Line(M1,j)+Line(M2,j)).i by A10,Th34;
  end;
  hence thesis by FINSEQ_1:18,A4,A5;
end;

theorem Th46:
  for M being Matrix of COMPLEX st i in Seg len M holds
    Line(M,i)=(Line(M*',i))*'
proof  let M be Matrix of COMPLEX;
  assume A1: i in Seg len M;
  A2: len Line(M,i)=width M by MATRIX_1:def 8;
  A3: len ((Line(M*',i))*')=len (Line(M*',i)) by COMPLSP2:def 1
      .= width (M*') by MATRIX_1:def 8 .= width M by AA10;
  for j st 1 <= j & j <= len (Line(M,i)) holds
   (Line(M,i)).j=((Line(M*',i))*').j
  proof let j; assume
    A4: 1 <= j & j <= len Line(M,i);
    A5: 1 <= j & j <= width M by A4,MATRIX_1:def 8;
    A6: 1 <= j & j <= len ((Line(M*',i))*') by A4,MATRIX_1:def 8,A3;
    A7: 1 <= j & j <= len Line(M*',i) by A6,COMPLSP2:def 1;
    A8: 1 <= i & i <= len M by FINSEQ_1:3,A1;
    A9: [i,j] in Indices M by A8,A5,Th1;
    A10: j in Seg width M by A4,FINSEQ_1:3,A2;
    A11: j in Seg width (M*') by A10,AA10;
    (Line(M,i)).j = ((M*(i,j))*')*' by MATRIX_1:def 8,A10
      .= ((M*')*(i,j))*' by AA10,A9
      .= ((Line(M*',i)).j)*' by MATRIX_1:def 8,A11
      .= ((Line(M*',i))*').j by COMPLSP2:def 1,A7;
    hence thesis;
  end;
 hence thesis by FINSEQ_1:18,A2,A3;
end;

theorem Th47:
  for F being FinSequence of COMPLEX,M being Matrix of COMPLEX
  st len F=width M holds
   mlt(F,(Line((M*'),i))*') = mlt(Line((M*'),i),(F*'))*'
proof
  let F be FinSequence of COMPLEX,M be Matrix of COMPLEX;
  assume A1: len F=width M;
  len (Line((M*'),i))=width (M*') by MATRIX_1:def 8
      .= width M by AA10;
  hence thesis by Th26,A1;
end;

theorem Th48:
  for F being FinSequence of COMPLEX,M being Matrix of COMPLEX
   st len F = width M & len F >= 1 holds
    (M*F)*'=(M*')*(F*')
proof
  let F be FinSequence of COMPLEX,M be Matrix of COMPLEX;
  assume A1: len F = width M & len F >= 1;
  A2: len ((M*F)*')=len (M*F) by COMPLSP2:def 1 .= len M by AA120;
  A3: len (M*') = len M by AA10;
  A4: len (F*') = len F by COMPLSP2:def 1;
  A5: width (M*')=width M by AA10;
  A6: len (M*'*(F*'))=len (M*') by AA120
      .= len M by AA10;
  now let i; assume
  A7: 1 <= i & i <= len ((M*F)*');
  A8: i in Seg len M by A7,FINSEQ_1:3,A2;
  A9: i in Seg len (M*') by A7,FINSEQ_1:3,A2,A3;
  A10: len (Line((M*'),i)) = len (F*') by MATRIX_1:def 8,A5,A1,A4;
  A12: len (mlt(Line((M*'),i),(F*'))) >= 1 by Th19,A10,A4,A1;
  1 <= i & i <= len ((M*F)) by A7,COMPLSP2:def 1;
  hence ((M*F)*').i = ((M*F).i)*' by COMPLSP2:def 1
    .= Sum(mlt(F,Line(M,i)))*' by AA120,A8
    .= Sum(mlt(F,(Line((M*'),i))*'))*' by Th46,A8
    .= Sum((mlt(Line((M*'),i),(F*')))*')*' by Th47,A1
    .= (Sum(mlt(Line((M*'),i),(F*'))))*'*' by Th25,A12
    .= ((M*')*(F*')).i by AA120,A9;
  end;
 hence thesis by FINSEQ_1:18,A6,A2;
end;

theorem
  for F1,F2,F3 being FinSequence of COMPLEX
  st len F1 = len F2 & len F2 = len F3 holds
    mlt(F1,mlt(F2,F3)) = mlt(mlt(F1,F2),F3)
proof let F1,F2,F3 be FinSequence of COMPLEX;
  assume A1: len F1=len F2 & len F2=len F3;
  reconsider f1=F1 as Element of (len F1)-tuples_on COMPLEX by FINSEQ_2:110;
  reconsider f2=F2 as Element of (len F2)-tuples_on COMPLEX by FINSEQ_2:110;
  reconsider f3=F3 as Element of (len F3)-tuples_on COMPLEX by FINSEQ_2:110;
  thus mlt(F1,mlt(F2,F3)) = multcomplex.:(F1,mlt(F2,F3))
       .= multcomplex.:(F1,multcomplex.:(F2,F3))
       .= multcomplex.:(multcomplex.:(f1,f2),f3) by FINSEQOP:29,A1
       .= multcomplex.:(mlt(F1,F2),F3)
       .= mlt(mlt(F1,F2),F3);
end;

theorem
  for F being FinSequence of COMPLEX holds
   Sum -F = -(Sum F)
proof let F be FinSequence of COMPLEX;
  thus Sum -F = addcomplex $$(-F)
   .= addcomplex $$(compcomplex*F) by COMPLSP1:def 9
   .= compcomplex.(addcomplex $$ F) by COMPLSP1:7,COMPLSP1:8,SETWOP_2:42
   .= compcomplex.(Sum F)
   .= -(Sum F) by BINOP_2:def 1;
end;

theorem Th51:
  for z being Element of COMPLEX holds
    Sum <*z*> = z by FINSOP_1:12;

theorem Th52:
  for F1,F2 being FinSequence of COMPLEX holds
    Sum(F1^F2) = Sum F1 + Sum F2
 proof let F1,F2 be FinSequence of COMPLEX;
  thus Sum(F1^F2) = addcomplex $$ (F1^F2)
   .= addcomplex.(addcomplex $$ F1,addcomplex $$ F2) by FINSOP_1:6
   .= addcomplex.(Sum F1,addcomplex $$ F2)
   .= addcomplex.(Sum F1,Sum F2)
   .= Sum F1 + Sum F2 by BINOP_2:def 3;
 end;

definition let M be Matrix of COMPLEX;
  func LineSum M -> FinSequence of COMPLEX means
  :SS100: len it=len M &
  for i being Nat st i in Seg len M holds it.i=Sum Line(M,i);
  existence
  proof
    deffunc F(Nat) = Sum Line(M,$1);
    ex z being FinSequence of COMPLEX st
     len z = len M & for j st j in Seg len M holds z.j = F(j)
       from FINSEQ_2:sch 1;
    hence thesis;
  end;
  uniqueness
    proof
     let p1,p2 be FinSequence of COMPLEX;
     assume that A1:len p1=len M and
                 A2:for j st j in Seg len M holds p1.j = Sum Line(M,j) and
                 A3:len p2=len M and
                 A4:for j st j in Seg len M holds p2.j = Sum Line(M,j);
     for j st j in Seg len M holds p1.j=p2.j
     proof
      let j;
      assume j in Seg len M;
      then p1.j = Sum Line(M,j) & p2.j = Sum Line(M,j) by A2,A4;
      hence thesis;
     end;
     hence thesis by A1,A3,FINSEQ_2:10;
    end;
end;

definition let M be Matrix of COMPLEX;
  func ColSum M -> FinSequence of COMPLEX means :SS110:
    len it=width M &
    for j being Nat st j in Seg width M holds it.j=Sum (Col(M,j));
  existence
  proof
    deffunc F(Nat) = Sum Col(M,$1);
    thus ex f being FinSequence of COMPLEX st len f = width M &
      for j st j in Seg width M holds f.j = F(j) from FINSEQ_2:sch 1;
    end;
  uniqueness
    proof
     let p1,p2 be FinSequence of COMPLEX;
     assume that A1:len p1=width M and
                 A2:for j st j in Seg width M holds p1.j = Sum (Col(M,j)) and
                 A3:len p2=width M and
                 A4:for j st j in Seg width M holds p2.j = Sum (Col(M,j));
     for j st j in Seg width M holds p1.j=p2.j
     proof
      let j;
      assume j in Seg width M;
      then p1.j = (Sum (Col(M,j))) & p2.j = (Sum (Col(M,j))) by A2,A4;
      hence thesis;
     end;
     hence thesis by A1,A3,FINSEQ_2:10;
    end;
end;

theorem Th53:
  for F being FinSequence of COMPLEX holds
    len F = 1 implies Sum F = F.1
proof let F be FinSequence of COMPLEX;
  assume A1: len F = 1;
  F = <* F.1 *> by A1,FINSEQ_1:57;
  hence thesis by Th51;
end;

theorem Th54:
  for f,g being FinSequence of COMPLEX,n being Nat
  st len f = n + 1 & g = f|n holds
    Sum f = Sum g + f/.(len f)
proof let  f,g be FinSequence of COMPLEX,n be Nat;
  assume A1: len f = n + 1 & g = f|n;
  set n'=Seg n;
  B1: g=f|n' by FINSEQ_1:def 15,A1;
  set q = <*f/.len f*>;
  set p = g^q;
  A2: n <= len f by A1,NAT_1:29;
  A3: len q = 1 by FINSEQ_1:56;
    len(g^q) = len g + len q by FINSEQ_1:35
          .= len g + 1 by FINSEQ_1:57
          .= len f by A1,A2,FINSEQ_1:80;
  then A4: dom f = Seg len(g^q) by FINSEQ_1:def 3 .= dom(g^q)
     by FINSEQ_1:def 3;
  A5: dom f = Seg(n+1) by A1,FINSEQ_1:def 3;
  now let u be set;
    assume A6: u in dom f;
    then u in { k where k is Nat : 1 <= k & k <= n+1 } by A5,FINSEQ_1:def 1;
    then consider i being Nat such that
    A7: u = i & 1 <= i & i <= n+1;
    now per cases;
      case A8: i = n+1;
       then A9: len g + 1 <= i by A1,A2,FINSEQ_1:80;
         i <= len g + len q by A1,A2,A3,A8,FINSEQ_1:80;
       hence p.i = q.(i-len g) by A9,FINSEQ_1:36
             .= q.(n+1-n) by A1,A2,A8,FINSEQ_1:80
             .= f/.(n+1) by A1,FINSEQ_1:57
             .= f.i by A6,A7,A8,FINSEQ_4:def 4;
      end;
     case i <> n+1;
      then i < n+1 by A7,REAL_1:def 5;
      then i <= n by NAT_1:38;
      then i in { k where k is Nat : 1 <= k & k <= n } by A7;
      then i in Seg n by FINSEQ_1:def 1;
      then A10: i in dom g by B1,A2,FINSEQ_1:21;
      then p.i = g.i by FINSEQ_1:def 7;
      hence p.i = f.i by B1,A10,FUNCT_1:68;
     end;
    end;
    hence f.u = p.u by A7;
  end;
  then f = g^<*(f/.(len f))*> by A4,FUNCT_1:9;
  hence Sum f = Sum g + Sum <*(f/.len f)*> by Th52
           .= Sum g + (f/.len f) by Th51;
end;

theorem Th55:
  for M being Matrix of COMPLEX st len M > 0 holds
    Sum LineSum M = Sum ColSum M
proof let M be Matrix of COMPLEX;
  assume A1: len M > 0;
  A2: 0+1 <= len M by A1,NAT_1:26;
  0+1-1<=len M-1 by XREAL_1:11,A2;then
  A3: len M-'1=len M-1 by BINARITH:def 3;
  defpred P[Nat] means
    for N being Matrix of COMPLEX st $1+1=len N
     holds Sum LineSum N=Sum ColSum N;
  for N being Matrix of COMPLEX st 0+1=len N
     holds Sum LineSum N=Sum ColSum N
   proof let N be Matrix of COMPLEX;
     assume A6: 0+1=len N;then
    A7: len LineSum N=1 by SS100;
    A9: len ColSum N=width N by SS110;
    A10: len Line(N,1)=width N by MATRIX_1:def 8;
    A11: 1 in Seg 1 by FINSEQ_1:5;
    A12: for j st 1<=j & j<= width N holds (ColSum(N)).j=Line(N,1).j
    proof let j;
      assume A13: 1<=j & j<= width N;
      A14: j in Seg width N by A13,FINSEQ_1:3;
      A17: len (Col(N,j)) = 1 by MATRIX_1:def 9,A6;
      A18: 1 in dom N by A11,FINSEQ_1:def 3,A6;
      (ColSum N).j=Sum Col(N,j) by SS110,A14
      .= (Col(N,j)).1 by Th53,A17
      .= N*(1,j) by A18,MATRIX_1:def 9
      .= Line(N,1).j by MATRIX_1:def 8,A14;
      hence thesis;
    end;
    A20: 1 in Seg len N by FINSEQ_1:5,A6;
    Sum ColSum N = Sum Line(N,1) by A9,A10,FINSEQ_1:18,A12
      .=(LineSum N).1 by SS100,A20
      .= Sum LineSum N by Th53,A7;
   hence Sum LineSum N=Sum ColSum N;
  end;then
  A21: P[0];
  A22: for k being Nat st P[k] holds P[k+1]
  proof let k be Nat;
   assume B1: P[k];
    for N being Matrix of COMPLEX st k+1+1=len N
     holds Sum LineSum N=Sum ColSum N
     proof let N be Matrix of COMPLEX;
      assume B2: k+1+1=len N;
        B3: len N >= k+1 by NAT_1:29,B2;
        B4: 1+k>=1 by NAT_1:29;
        B5: len N>=1 by NAT_1:29,B2;
        set k1=k+1;
        consider n such that
        B6: for x being set st x in rng N
        ex p being FinSequence of COMPLEX st x = p & len p = n by MATRIX_1:9;
        B7: rng (N|k1) c= rng N by FINSEQ_5:21;
        B8: for x being set st x in rng (N|k1)
        ex p being FinSequence of COMPLEX
        st x=p & len p=n by B6,B7;
        reconsider N2=N|k1 as Matrix of COMPLEX by MATRIX_1:9,B8;
        B9: len N2=k+1 by FINSEQ_1:80,B3;
        B10: len N2>=1 by B4,FINSEQ_1:80,B3;
        consider n3 being Nat such that
        B11: for x2 being set st x2 in rng N holds
           ex s being FinSequence st s=x2 & len s=n3 by MATRIX_1:def 1;
        1 in Seg len N by B5,FINSEQ_1:3;then
        1 in dom N by FINSEQ_1:def 3;then
        B12: N.1 in rng N by FUNCT_1:12;then
        consider s2 being FinSequence such that
        B13: s2=N.1 & len s2=n3 by B11;
        1 in Seg len N2 by B10,FINSEQ_1:3;then
        1 in dom N2 by FINSEQ_1:def 3;then
        B14: N2.1 in rng N2 by FUNCT_1:12;then
        consider s3 being FinSequence such that
        B15: s3=N2.1 & len s3=n3 by B11,B7;
        B16: width N2=n3 by MATRIX_1:def 4,B10,B15,B14;
        B17: width N2= width N by MATRIX_1:def 4,B2,B12,B13,B16;
        B18: for j st 1<=j & j<=width N holds
          (ColSum N2).j+N*(k+1+1,j)=(ColSum N).j
        proof let j;
          assume B19:1<=j & j<=width N;
          B20: j in Seg width N by FINSEQ_1:3,B19;
          B21: k+1+1 in Seg len N by B2,FINSEQ_1:6;
          B22: k+1+1 in dom N by FINSEQ_1:def 3,B21;
          B23: k+1+1 in Seg len Col(N,j) by B21,MATRIX_1:def 9;
          B24: k+1+1 in dom Col(N,j) by FINSEQ_1:def 3,B23;
          B25: len (Col(N,j))=len N by MATRIX_1:def 9;
          set f=Col(N,j);
          set g=Col(N2,j);
          B26: len g = len N2 by MATRIX_1:def 9
              .= k1 by FINSEQ_1:80,B3;
          B27: k1 <= len f by B3,MATRIX_1:def 9;
          B28: g=f|k1
          proof
            B29: len (f|k1)= len g by B26,FINSEQ_1:80,B27;
            for n be Nat st 1 <=n & n <= len (f|k1) holds
              (f|k1).n=g.n
            proof let n be Nat;
              assume B30: 1 <= n & n <= len (f|k1);
              B31: 1 <= n & n <= k1 by FINSEQ_1:80,B27,B30;
              B32: 1 <= n & n <= k1+1 by B31,NAT_1:38;
              B33: n in Seg len (f|k1) by FINSEQ_1:3,B30;
              B34: n in dom(f|k1) by FINSEQ_1:def 3,B33;
              B35: n in dom(f|Seg k1) by FINSEQ_1:def 15,B34;
              B36: n in Seg len N by B2,B32,FINSEQ_1:3;
              B37: n in dom N by FINSEQ_1:def 3,B36;
              B38: n in Seg k1 by B31,FINSEQ_1:3;
              B39: n in dom N2 by FINSEQ_1:def 3,B9,B38;
              B40: 1<= n & n<=len N & 1<=j & j<=width N by B31,NAT_1:38,B2,B19;
              B41: [n,j] in Indices N by Th1,B40;
              B42: 1<= n & n<=len N2 & 1<=j & j<=width N2
                by FINSEQ_1:80,B27,B30,B9,MATRIX_1:def 4,
                   B2,B12,B13,B16,B19;
              B43: [n,j] in Indices N2 by Th1,B42;
              consider pn being FinSequence of COMPLEX such that
              B44: pn=N.n & N*(n,j)=pn.j by MATRIX_1:def 6,B41;
              B45: N2.n=N.n by JORDAN3:20,B31;
              (f|k1).n=(f|Seg k1).n by FINSEQ_1:def 15
                    .=(Col(N,j)).n by FUNCT_1:70,B35
                    .= N*(n,j) by MATRIX_1:def 9,B37
                    .= N2*(n,j) by B43,MATRIX_1:def 6,B44,B45
                    .= g.n by B39,MATRIX_1:def 9;
              hence thesis;
            end;
            hence thesis by FINSEQ_1:18,B29;
          end;
          (ColSum N2).j+N*(k+1+1,j)
           = Sum (Col(N2,j))+N*((k+1+1),j) by SS110,B20,B17
          .= Sum (Col(N2,j))+Col(N,j).(k+1+1) by MATRIX_1:def 9,B22
          .= Sum (Col(N2,j))+Col(N,j)/.(k+1+1) by FINSEQ_4:def 4,B24
          .= Sum (Col(N,j)) by Th54,B2,B25,B28
          .=(ColSum N).j by SS110,B20;
          hence thesis;
        end;
        B46: k+1+1 in Seg len N by B2,FINSEQ_1:5;
        B47: k+1+1 in Seg len LineSum N by B46,SS100;
        B48: k+1+1 in dom LineSum N by B47,FINSEQ_1:def 3;
        B49: len ColSum N2=width N2 by SS110;
        B50: len ColSum N2=width N2 by SS110
            .= width N by MATRIX_1:def 4,B2,B12,B13,B16
            .= len Line(N,(k+1+1)) by MATRIX_1:def 8;
        B51: len Line(N,(k+1+1))=width N by MATRIX_1:def 8;
        B52: len (ColSum(N2)+Line(N,(k+1+1)))
             = width N by COMPLSP2:6,B49,B51,B17
            .= len ColSum N by SS110;
        B53: for j st 1<=j & j<= len ColSum N holds
           (ColSum(N2)+Line(N,k+1+1)).j=(ColSum N).j
        proof let j;
          assume B54: 1<=j & j<= len ColSum N;
          B55: j in Seg len ColSum N by B54,FINSEQ_1:3;
          B56: j in Seg len ((ColSum(N2)+Line(N,k+1+1)))
               by B54,FINSEQ_1:3,B52;
          B57: j in dom ((ColSum(N2)+Line(N,k+1+1)))
               by B56,FINSEQ_1:def 3;
          B58: j in Seg width N2 by B17,SS110,B55;
          B59: 1<=j & j<=width N by FINSEQ_1:3,B58,B17;
          (ColSum(N2)+Line(N,(k+1+1))).j
           = (ColSum N2).j+(Line(N,k+1+1)).j by B57,Th34
          .= (ColSum N2).j+N*(k+1+1,j) by MATRIX_1:def 8,B58,B17
          .= (ColSum N).j by B18,B59;
          hence thesis;
        end;
        set g1=LineSum N2;
        set f3=LineSum N;
        B60: len LineSum N = len N by SS100;
        B61: len f3= k1+1 by B2,SS100;
        B62: k1 <= len f3 by NAT_1:29,B61;
        B63: len g1 = len N2 by SS100
              .= k1 by FINSEQ_1:80,B3;
        g1=f3|k1
        proof
          B64: len (f3|k1) = len g1 by B63,FINSEQ_1:80,B62;
          for n being Nat st 1 <= n & n <= len (f3|k1) holds (f3|k1).n=g1.n
          proof let n be Nat;
            assume B65: 1 <= n & n <= len (f3|k1);
       B66: 1 <= n & n <= k1+1 by B64,B65,NAT_1:38,B63;
       B67: n in Seg len (f3|k1) by FINSEQ_1:3,B65;
       B68: n in dom(f3|k1) by FINSEQ_1:def 3,B67;
       B69: n in dom(f3|Seg k1) by FINSEQ_1:def 15,B68;
       B70: n in Seg len N by B2,B66,FINSEQ_1:3;
       B71: n in Seg k1 by B64,B65,FINSEQ_1:3,B63;
       B72: n in Seg len N2 by FINSEQ_1:80,B3,B71;
       B73: Line(N,n)=Line(N2,n)
            proof
         B74: len Line(N,n) = width N by MATRIX_1:def 8
                    .= width N2 by MATRIX_1:def 4,B2,B12,B13,B16
                    .= len (Line(N2,n)) by MATRIX_1:def 8;
              for n1 being Nat st 1 <= n1 & n1 <= len (Line(N,n)) holds
                Line(N,n).n1=Line(N2,n).n1
              proof let n1 be Nat;
                  assume B75: 1 <= n1 & n1 <= len (Line(N,n));
                  B76: n1 in Seg (len (Line(N,n))) by FINSEQ_1:3,B75;
                  B77: n1 in Seg width N by MATRIX_1:def 8,B76;
                  B78: 1 <= n1 & n1 <= width N by B77,FINSEQ_1:3;
                  B79: n1 in Seg width N by MATRIX_1:def 8,B76;
                  B80: n1 in Seg width N2 by MATRIX_1:def 8,B76,B17;
                  B81: 1<= n & n<=len N & 1<=n1 & n1<=width N
                    by B77,FINSEQ_1:3,B64,B65,NAT_1:38,B63,B2;
                  B82: [n,n1] in Indices N by Th1,B78,B66,B2;
                  B83: 1<= n & n<=len N2 & 1<=n1 & n1<=width N2
                    by B64,B65,SS100,MATRIX_1:def 4,B12,B13,B16,B81;
                  B84: [n,n1] in Indices (N2) by Th1,B83;
                  consider pn being FinSequence of COMPLEX such that
                  B85: pn=N.n & N*(n,n1)=pn.n1 by MATRIX_1:def 6,B82;
                  B86: N2.n=N.n by JORDAN3:20,B64,B65,B63;
                  Line(N,n).n1=N*(n,n1) by MATRIX_1:def 8,B79
                        .=N2*(n,n1) by B84,MATRIX_1:def 6,B86,B85
                        .=Line(N2,n).n1 by MATRIX_1:def 8,B80;
                  hence thesis;
                end;
                hence thesis by FINSEQ_1:18,B74;
              end;
            (f3|k1).n=(f3|Seg k1).n by FINSEQ_1:def 15
                    .=(LineSum(N)).n by FUNCT_1:70,B69
                    .= Sum (Line(N2,n)) by B73,SS100,B70
                    .= g1.n by B72,SS100;
            hence thesis;
          end;
          hence thesis by FINSEQ_1:18,B64;
        end;then
        Sum LineSum N
          =Sum (LineSum N2)+(LineSum N)/.(k+1+1) by Th54,B60,B2
         .=Sum (LineSum N2)+(LineSum N).(k+1+1) by FINSEQ_4:def 4,B48
         .=Sum (ColSum N2)+(LineSum N).(k+1+1) by B1,B9
         .=Sum (ColSum N2)+Sum (Line(N,(k+1+1))) by SS100,B46
         .=Sum (ColSum(N2)+Line(N,(k+1+1))) by Th40,B50
         .=Sum ColSum N by B52,FINSEQ_1:18,B53;
      hence Sum LineSum N=Sum ColSum N;
     end;
   hence thesis;
  end;
 A99: for i holds P[i] from NAT_1:sch 1(A21,A22);
 A88: (len M)-'1+1=len M implies
 Sum LineSum M=Sum ColSum M by A99;
 thus thesis by A3,A88;
end;

definition let M be Matrix of COMPLEX;
  func SumAll M -> Element of COMPLEX equals
    Sum LineSum M;
  coherence;
end;

theorem Th56:
  for M being Matrix of COMPLEX holds
    ColSum M = LineSum(M@)
proof let M be Matrix of COMPLEX;
  A1: len (LineSum(M@))=len (M@) by SS100;
  A2: len ColSum M=width M by SS110;
  A3: len ColSum M=len LineSum(M@) by MATRIX_1:def 7,A1,A2;
  now let i; assume
    A4: 1 <= i & i <= len ColSum M;
    A5: 1 <= i & i <= len LineSum(M@) by A4,MATRIX_1:def 7,A1,A2;
    A6: i in Seg len LineSum(M@) by FINSEQ_1:3,A5;
    A7: i in Seg len (M@) by SS100,A6;
    A8: i in Seg width M by FINSEQ_1:3,A4,A2;
    hence (ColSum M).i = Sum Col(M,i) by SS110
      .= Sum (Line(M@,i)) by MATRIX_2:17,A8
      .= (LineSum(M@)).i by SS100,A7;
  end;
 hence thesis by FINSEQ_1:18,A3;
end;

theorem Th57:
  for M being Matrix of COMPLEX st len M > 0 holds
    SumAll M = SumAll (M@)
proof let M be Matrix of COMPLEX;
  assume A1: len M > 0;
  SumAll M = Sum LineSum M
  .= Sum ColSum M by Th55,A1
  .= Sum LineSum(M@) by Th56
  .= SumAll(M@);
  hence thesis;
end;

definition let x,y be FinSequence of COMPLEX,M be Matrix of COMPLEX;
  assume A0: len x=len M & len y=width M;
  func QuadraticForm(x,M,y) -> Matrix of COMPLEX means :SS220:
    len it = len x & width it=len y &
    for i,j being Nat st [i,j] in Indices M holds
      it*(i,j)=(x.i)*(M*(i,j))*((y.j)*');
existence
 proof
  deffunc F(Nat,Nat) = (x.$1)*(M*($1,$2))*((y.$2)*');
  consider M1 being Matrix of len M,width M,COMPLEX such that
A1:for i,j st [i,j] in Indices M1 holds M1*(i,j) = F(i,j)
    from MATRIX_1:sch 1;
  take M1;
A2:len M1=len x by MATRIX_1:def 3,A0;
A3:now per cases;
      case A4:len M= 0;
        then width M1=0 by A2,MATRIX_1:def 4,A0;
        hence width M1=len y by A4,MATRIX_1:def 4,A0;
      end;
      case len M>0;
        hence width M1=len y by MATRIX_1:24,A0;
      end;
     end;
  dom M = dom M1 by A2,FINSEQ_3:31,A0;then
  Indices M1=[:dom M,Seg width M:] &
    Indices M =[:dom M,Seg width M:] by A3,MATRIX_1:def 5,A0;
  hence thesis by A1,MATRIX_1:def 3,A0,A3;
 end;
  uniqueness
  proof
   let M1,M2 be Matrix of COMPLEX;
   assume that
A5: len M1=len x & width M1=len y & for i,j st [i,j] in Indices M holds
           M1*(i,j)=(x.i)*(M*(i,j))*((y.j)*') and
A6: len M2=len x & width M2=len y & for i,j st [i,j] in Indices M holds
           M2*(i,j)=(x.i)*(M*(i,j))*((y.j)*');
    now let i,j;
      assume A7:[i,j] in Indices M1;
      dom M1 = dom M by A5,FINSEQ_3:31,A0;
    then A8: Indices M1=[:dom M,Seg width M:] &
             Indices M= [:dom M,Seg width M:] by A5,MATRIX_1:def 5,A0;
      hence M1*(i,j)=(x.i)*(M*(i,j))*((y.j)*') by A5,A7
                  .=M2*(i,j) by A6,A7,A8;
    end;
    hence M1=M2 by A5,A6,MATRIX_1:21;
  end;
end;

theorem Th58:
  for x,y being FinSequence of COMPLEX,M being Matrix of COMPLEX st
   len x = len M & len y = width M & len x > 0& len y > 0 holds
    (QuadraticForm(x,M,y))@ = (QuadraticForm(y,M@",x))*'
proof let x,y be FinSequence of COMPLEX,M be Matrix of COMPLEX;
  assume A1: len x=len M & len y =width M & len x > 0& len y > 0;
A2: len (x*') = len x by COMPLSP2:def 1;
A3: len (M@")= len ((M@)*')
      .= len (M@) by AA10
      .= len y by A1,MATRIX_1:def 7;
A4: width (M@")= width ((M@)*') .= width (M@) by AA10
      .= len x by MATRIX_2:12,A1;
A5: len ((QuadraticForm(x,M,y))@) = width (QuadraticForm(x,M,y))
      by MATRIX_1:def 7 .= len y by SS220,A1;
A6: len ((QuadraticForm(x,M,y))@) = width (QuadraticForm(x,M,y))
      by MATRIX_1:def 7;
A7: len ((QuadraticForm(y,M@",x))*')=len (QuadraticForm(y,M@",x))
      by AA10 .= len y by SS220,A3,A4;
A8: width (QuadraticForm(x,M,y))=len y by SS220,A1;
A9: len (M@")=len ((M@)*') .= len (M@) by AA10;
A10: len (M@")=width M by A9,MATRIX_1:def 7;
A11: len QuadraticForm(y,M@",x) = len y by SS220,A3,A4;
A12: width ((QuadraticForm(x,M,y))@)
       = len (QuadraticForm(x,M,y)) by MATRIX_2:12,A1,A8;
A13: width ((QuadraticForm(x,M,y))@)
       = len (QuadraticForm(x,M,y)) by MATRIX_2:12,A1,A8
      .= len x by SS220,A1;
A14: width ((QuadraticForm(y,M@",x))*')
      = width (QuadraticForm(y,M@",x)) by AA10
     .= len x by SS220,A3,A4;
A15: width (QuadraticForm(y,M@",x))= len x by SS220,A3,A4;
  for i,j st [i,j] in Indices ((QuadraticForm(x,M,y))@) holds
    ((QuadraticForm(y,M@",x))*')*(i,j)
     = ((QuadraticForm(x,M,y))@)*(i,j)
  proof let i,j;
    assume [i,j] in Indices ((QuadraticForm(x,M,y))@);then
    A16: 1<=i & i<=width QuadraticForm(x,M,y) &
      1<=j & j<=len QuadraticForm(x,M,y) by A6,A12,Th1;
    A17: 1<=i & i<=len y &
      1<=j & j<=len x by SS220,A16,A1;
    A18: 1<=i & i<=width M &
      1<=j & j<=len M by SS220,A16,A1;
    A19: [j,i] in Indices M by A18,Th1;
    A20: 1<=i & i<=len QuadraticForm(y,M@",x) &
      1<=j & j<=width QuadraticForm(y,M@",x) by SS220,A16,A1,A11,A15;
    A21: [i,j] in Indices QuadraticForm(y,M@",x) by A20,Th1;
    A22: 1<=j & j<=len x by SS220,A16,A1;
    A23: 1<=i & i<=len (M@") &
      1<=j & j<=width (M@") by A10,SS220,A16,A1,A4;
    A24: [i,j] in Indices (M@") by A23,Th1;
    A25: [j,i] in Indices (QuadraticForm(x,M,y)) by A16,Th1;
    ((QuadraticForm(y,M@",x))*')*(i,j)
       = ((QuadraticForm(y,M@",x))*(i,j)*') by AA10,A21
      .= ((y.i)*((M@")*(i,j))*((x.j)*'))*' by SS220,A24,A3,A4
      .= ((y.i)*((M@")*(i,j))*((x*'.j)))*' by COMPLSP2:def 1,A22
      .= (((y.i)*((M@")*(i,j)))*')*((x*'.j)*') by COMPLEX1:121
      .= (((y.i)*((M@")*(i,j)))*')*((x*')*'.j) by COMPLSP2:def 1,A17,A2
      .= ((y.i)*')*(((M@")*(i,j)*'))*((x*')*'.j) by COMPLEX1:121
      .= ((y.i)*')*(((M@")*')*(i,j))*((x*')*'.j) by AA10,A24
      .= ((y.i)*')*(((M@")*')*(i,j))*(x.j) by COMPLSP2:22
      .= ((y.i)*')*((((M@)*')*')*(i,j))*(x.j)
      .= ((y.i)*')*((M@)*(i,j))*(x.j) by Th2
      .= ((y.i)*')*(M*(j,i))*(x.j) by MATRIX_1:def 7,A19
      .= (x.j)*(M*(j,i))*((y.i)*')
      .= (QuadraticForm(x,M,y))*(j,i) by SS220,A19,A1
      .= ((QuadraticForm(x,M,y))@)*(i,j) by MATRIX_1:def 7,A25;
      hence thesis;
    end;
  hence thesis by MATRIX_1:21,A5,A7,A13,A14;
end;

theorem Th59:
  for x,y being FinSequence of COMPLEX,M being Matrix of COMPLEX st
   len x=len M & len y =width M holds
    (QuadraticForm(x,M,y))*'=QuadraticForm(x*',M*',y*')
proof let x,y be FinSequence of COMPLEX,M be Matrix of COMPLEX;
  assume A1: len x=len M & len y =width M;
  A2: len (y*') = len y by COMPLSP2:def 1;
  A3: len (M*') = len M by AA10;
  A4: len (x*') = len (M*') by COMPLSP2:def 1,A3,A1;
  A5: len (y*') = width (M*') by AA10,A1,A2;
  A6: len ((QuadraticForm(x,M,y))*') = len QuadraticForm(x,M,y) by AA10;
  A7: width ((QuadraticForm(x,M,y))*')=width QuadraticForm(x,M,y) by AA10;
  A8: len ((QuadraticForm(x,M,y))*') = len QuadraticForm(x,M,y) by AA10
      .= len M by SS220,A1;
  A9: len (QuadraticForm(x*',M*',y*')) = len (x*') by SS220,A4,A5
      .= len M by A1,COMPLSP2:def 1;
  A10: width ((QuadraticForm(x,M,y))*')=width (QuadraticForm(x,M,y)) by AA10
      .= len y by SS220,A1;
  A11: width QuadraticForm(x*',M*',y*')=len (y*') by SS220,A4,A5
      .= len y by COMPLSP2:def 1;
  for i,j st [i,j] in Indices ((QuadraticForm(x,M,y))*') holds
    ((QuadraticForm(x,M,y))*')*(i,j)
     = (QuadraticForm(x*',M*',y*'))*(i,j)
  proof let i,j;
    assume [i,j] in Indices ((QuadraticForm(x,M,y))*');then
    A12: 1<=i & i<=len (QuadraticForm(x,M,y)) &
    1<=j & j<=width (QuadraticForm(x,M,y)) by A6,A7,Th1;
    A13: [i,j] in Indices QuadraticForm(x,M,y) by Th1,A12;
    A14: 1<=i & i<=len x & 1<=j & j<=len y by A12,SS220,A1;
    A15: 1<=i & i<=len M & 1<=j & j<=width M by A12,SS220,A1;
    A16: 1<=i & i<=len (M*') & 1<=j & j<=width (M*') by A15,AA10;
    A17: [i,j] in Indices (M*') by A16,Th1;
    A18: 1<=i & i<=len M & 1<=j & j<=width M by A12,A1,SS220;
    A19: [i,j] in Indices M by Th1,A18;
    ((QuadraticForm(x,M,y))*')*(i,j)
        =((QuadraticForm(x,M,y))*(i,j))*' by AA10,A13
       .=((x.i)*(M*(i,j))*((y.j)*'))*' by SS220,A19,A1
       .=((x.i)*(M*(i,j))*((y*'.j)))*' by COMPLSP2:def 1,A14
       .= (((x.i)*(M*(i,j)))*')*((y*'.j)*') by COMPLEX1:121
       .= (((x.i)*(M*(i,j)))*')*((y*')*'.j) by COMPLSP2:def 1,A14,A2
       .= ((x.i)*')*((M*(i,j)*'))*(((y*')*'.j)) by COMPLEX1:121
       .= ((x.i)*')*((M*')*(i,j))*(((y*')*'.j)) by AA10,A19
       .= ((x.i)*')*((M*')*(i,j))*(((y*').j)*') by COMPLSP2:def 1,A14,A2
       .= ((x*').i)*((M*')*(i,j))*(((y*').j)*') by COMPLSP2:def 1,A14
       .= (QuadraticForm(x*',M*',y*'))*(i,j) by SS220,A17,A5,A4;
      hence thesis;
    end;
  hence thesis by MATRIX_1:21,A8,A9,A10,A11;
end;

theorem Th60:
  for x,y being FinSequence of COMPLEX st len x=len y & 0 < len y holds
    |(x,y)|=(|(y,x)|)*'
proof let x,y be FinSequence of COMPLEX;
   assume A1: len x=len y & 0 < len y;
   A2: 0+1 <= len x by A1,NAT_1:26;
   A3: len (x*') = len (x) by COMPLSP2:def 1;
   A4: len (mlt(y,(x*'))) = len y by Th19,A1,A3;
   (|(y,x)|)*' = (Sum (mlt(y,(x*'))))*' by Th43,A1
  .= Sum (mlt(y,(x*'))*') by Th25,A1,A4,A2
  .= Sum (mlt(x,(y*'))) by Th26,A1
  .= |(x, y)| by Th43,A1;
    hence thesis;
end;

theorem Th61:
  for x,y being FinSequence of COMPLEX st len x=len y & 0 < len y holds
    (|(x,y)|)*'=|(x*',y*')|
proof let x,y be FinSequence of COMPLEX;
  assume A1: len x=len y & 0 < len y;
A2: 0+1 <= len x by A1,NAT_1:26;
A3: len (y*') = len y by COMPLSP2:def 1;
A4: len (mlt(x,y*')) = len x by Th19,A1,A3;
A5: len (x*') = len x by COMPLSP2:def 1;
   |(x*',y*')| = Sum (mlt(x*',(y*')*')) by Th43,A1,A3,A5
  .= Sum ((mlt(x,y*'))*') by Th29,A1,A3
  .= (Sum (mlt(x,y*')))*' by Th25,A2,A4
  .= (|(x, y)|)*' by Th43,A1;
  hence thesis;
end;

theorem
  for M being Matrix of COMPLEX st width M > 0 holds
    (M@)*' =(M*')@
proof let M be Matrix of COMPLEX;
  assume A1: width M > 0;
  A2: len ((M@)*') = len (M@) by AA10;
  A3: width ((M@)*') = width (M@) by AA10;
  A4: len ((M@)*') = len (M@) by AA10 .= width M by MATRIX_1:def 7;
  A5: len ((M*')@) = width (M*') by MATRIX_1:def 7 .= width M by AA10;
  A6: width ((M@)*') = width (M@) by AA10 .= len M by MATRIX_2:12,A1;
  A7: width (M*') = width M by AA10;
  A8: width ((M*')@) = len (M*') by MATRIX_2:12,A7,A1 .= len M by AA10;
  for i,j st [i,j] in Indices ((M@)*') holds
    ((M@)*')*(i,j) = ((M*')@)*(i,j)
  proof let i,j;
    assume A9: [i,j] in Indices ((M@)*');
    A10: 1<= i & i<=len (M@) & 1<=j & j<=width (M@) by A2,A3,Th1,A9;
    A11: 1<= i & i<=len ((M@)*') & 1<=j & j<=width ((M@)*') by Th1,A9;
    A12: 1<= j & j<=len (M*') & 1<=i & i<=width (M*') by A11,AA10,A4,A6;
    A13: [i,j] in Indices (M@) by Th1,A10;
    A14: [j,i] in Indices (M) by MATRIX_1:def 7,A13;
    A15: [j,i] in Indices (M*') by Th1,A12;
    ((M@)*')*(i,j) = ((M@)*(i,j))*' by A13,AA10
        .= (M*(j,i))*' by MATRIX_1:def 7,A14
        .= ((M*'))*(j,i) by AA10,A14
        .= ((M*')@)*(i,j) by MATRIX_1:def 7,A15;
    hence thesis;
  end;
  hence thesis by MATRIX_1:21,A4,A5,A6,A8;
end;

theorem Th63:
  for x,y being FinSequence of COMPLEX,M being Matrix of COMPLEX st
   len x=width M & len y = len M & len x>0 & len y>0 holds
    |(x,(M@")*y)| = SumAll QuadraticForm(x,M@,y)
proof let x,y be FinSequence of COMPLEX,M be Matrix of COMPLEX;
 assume A1: len x=width M & len y = len M & len x>0 & len y>0;
  A2: len (M@)=len x by MATRIX_1:def 7,A1;
  A3: len (M@")=len ((M@)*') .=len (M@) by AA10
       .=len x by MATRIX_1:def 7,A1;
  A4: 0+1<=len y by A1,NAT_1:38;
  A5: width (M@) = len y by A1,MATRIX_2:12;
  A5a: width ((M@)*')=len y by AA10,A5;
  A7: len ((M@")*y)= len x by A3,AA120;
  A8: len (((M@")*y)*')= len x by A7,COMPLSP2:def 1;
  A10: dom LineSum QuadraticForm(x,M@,y)
    = Seg len LineSum QuadraticForm(x,M@,y) by FINSEQ_1:def 3
   .= Seg len QuadraticForm(x,M@,y) by SS100;
  A11: len LineSum(QuadraticForm(x,M@,y))
      = len QuadraticForm(x,M@,y) by SS100
     .= len x by A2,A5,SS220;then
  A12: len LineSum(QuadraticForm(x,M@,y))
     = len mlt(x,((M@")*y)*') by A8,Th19;
  A13: for i st 1<=i & i<=len (LineSum(QuadraticForm(x,M@,y))) holds
       ( LineSum(QuadraticForm(x,M@,y))).i = (mlt(x,((M@")*y)*')).i
    proof let i;assume
      A14: 1<=i & i<=len LineSum(QuadraticForm(x,M@,y));
      i in Seg len LineSum(QuadraticForm(x,M@,y)) by FINSEQ_1:3,A14;then
      A15: i in dom LineSum(QuadraticForm(x,M@,y)) by FINSEQ_1:def 3;
       i in Seg len mlt(x,((M@")*y)*') by A12,FINSEQ_1:3,A14;then
       A16: i in dom mlt(x,((M@")*y)*') by FINSEQ_1:def 3;
       A17: (mlt(x,((M@")*y)*')).i
           =(x.i)*((((M@")*y)*').i) by A16,Th20
          .=(x.i)*((((M@")*y).i)*') by A14,A7,A11,COMPLSP2:def 1;
      A18: len (M@)=len QuadraticForm(x,M@,y) by SS220,A2,A5;
      A19: len (M@)=len LineSum(QuadraticForm(x,M@,y)) by SS100,A18;
      A20: i in Seg len (M@") by FINSEQ_1:3,A14,A2,A3,A19;
      A21: i in Seg len ((M@)*') by A20;
      A22: i in Seg len (M@) by A21,AA10;
      A23: 1 <= i & i <= len (M@) by A22,FINSEQ_1:3;
      A24: len (Line(M@",i))=width (M@") by MATRIX_1:def 8
                              .=len y by A5a;
      A25: len mlt(Line(M@",i),y)>=1 by A4,Th19,A24;
      A26: (mlt(x,((M@")*y)*')).i
         = (x.i)*((Sum(mlt(Line((M@"),i),y)))*') by A17,A20,AA120
        .=(x.i)*(Sum((mlt(Line((M@"),i),y))*')) by Th25,A25
        .=Sum((x.i)*(mlt(Line((M@"),i),y))*') by Th30;
        A27: len y = width QuadraticForm(x,M@,y) by SS220,A2,A5
                  .=len Line(QuadraticForm(x,M@,y),i) by MATRIX_1:def 8;
        A28: len Line((M@"),i)=width (M@") by MATRIX_1:def 8
             .=len y by A5a;
        A29: len ((x.i)*(mlt(Line((M@"),i),y))*')
              =len ((mlt(Line((M@"),i),y))*') by COMPLSP2:3
             .=len (mlt(Line((M@"),i),y)) by COMPLSP2:def 1
             .=len (Line(QuadraticForm(x,M@,y),i)) by A27,A28,Th19;
        A30: for j being Nat st 1<=j & j<= len (Line(QuadraticForm(x,M@,y),i))
         holds ((x.i)*(mlt(Line((M@"),i),y))*').j
             =(Line(QuadraticForm(x,M@,y),i)).j
         proof let j be Nat;
           assume EE1: 1<=j & j<= len (Line(QuadraticForm(x,M@,y),i));
           A31: j in Seg len (Line(QuadraticForm(x,M@,y),i))
                                   by FINSEQ_1:3,EE1;
           A32: j in Seg width QuadraticForm(x,M@,y)
              by MATRIX_1:def 8,A31;
           A33: len (Line(M@,i))=width (M@) by MATRIX_1:def 8
               .=len y by A1,MATRIX_2:12
               .=len (y*') by COMPLSP2:def 1;
           j in Seg len y by FINSEQ_1:3,EE1,A27;then
           j in Seg len (y*') by COMPLSP2:def 1;then
           j in Seg len mlt(Line(M@,i),y*') by A33,Th19;then
           A34: j in dom mlt(Line(M@,i),y*') by FINSEQ_1:def 3;
           A35: j in Seg width (M@) by FINSEQ_1:3,EE1,A27,A5;
           A36: 1 <= j & j <= width (M@) by EE1,A27,A1,MATRIX_2:12;
           A37: [i,j] in Indices (M@) by A36,A23,Th1;
           ((x.i)*(mlt(Line((M@"),i),y))*').j
            =(x.i)*(((mlt(Line((M@"),i),y))*').j) by COMPLSP2:16
           .=(x.i)*((mlt((Line((M@"),i))*',y*')).j) by Th29,A24
           .=(x.i)*((mlt((Line(((M@)*'),i))*',y*')).j)
           .=(x.i)*((mlt(Line(M@,i),y*')).j) by Th46,A2,A3,A20
           .=(x.i)*(((Line(M@,i)).j)*((y*').j)) by Th20,A34
           .=(x.i)*(((Line(M@,i)).j)*((y.j)*')) by COMPLSP2:def 1,EE1,A27
           .=(x.i)*((Line(M@,i)).j)*((y.j)*')
           .=(x.i)*((M@)*(i,j))*((y.j)*') by MATRIX_1:def 8,A35
           .=(QuadraticForm(x,M@,y))*(i,j) by SS220,A2,A5,A37;
          hence ((x.i)*(mlt(Line((M@"),i),y))*').j
                   =(Line (QuadraticForm(x,M@,y),i)).j by A32,MATRIX_1:def 8;
         end;
        (mlt(x,((M@")*y)*')).i=Sum (Line(QuadraticForm(x,M@,y),i))
         by A26,FINSEQ_1:18,A29,A30;
      hence (LineSum(QuadraticForm(x,M@,y))).i = (mlt(x,((M@")*y)*')).i
                   by SS100,A10,A15;
    end;
    Sum LineSum(QuadraticForm(x,M@,y))=Sum mlt(x,((M@")*y)*')
     by FINSEQ_1:18,A12,A13;
 hence |(x,(M@")*y)| = SumAll QuadraticForm(x,M@,y)
    by Th43,A1,A7;
end;

theorem Th64:
  for x,y being FinSequence of COMPLEX,M being Matrix of COMPLEX st
   len y=len M & len x =width M & len x>0 & len y>0 & len M > 0 holds
    |(M*x,y)| = SumAll QuadraticForm(x,M@,y)
proof let x,y be FinSequence of COMPLEX,
  M be Matrix of COMPLEX;
  assume A1: len y=len M & len x =width M & len x>0 & len y>0& len M > 0;
   A2: 0+1<=len x by A1,NAT_1:38;
   A3: len (M*x)=len M by AA120;
   A4: len (x*')=len x by COMPLSP2:def 1;
   A5: len (y*')=len y by COMPLSP2:def 1;
   A6: len (M@)=width M by MATRIX_1:def 7;
   A7: width (M@)=len M by MATRIX_2:12,A1;
   A8: len (y*')= len M by A1,COMPLSP2:def 1;
   A9: len (x*')= width M by A1,COMPLSP2:def 1;
   A10: len QuadraticForm(y*',M,x*') = len M by A8,A9,SS220;
   A11: (M@)@"=((M@)@)*' .=M*' by MATRIX_2:15,A1;
   thus |(M*x,y)| = (|(y,M*x)|)*' by Th60,A1,A3
            .= (|(y*',(M*x)*')|) by Th61,A3,A1
            .= |(y*',((M*')*(x*')) )| by Th48,A1,A2
            .= SumAll QuadraticForm(y*',(M@)@,x*')
               by Th63,A1,A4,A5,A6,A7,A11
            .= SumAll QuadraticForm(y*',M,x*') by MATRIX_2:15,A1
            .= SumAll((QuadraticForm(y*',M,x*'))@) by Th57,A1,A10
            .= SumAll((QuadraticForm(x*',(M@"),y*'))*') by Th58,A1,A4,A5
            .= SumAll((QuadraticForm(x*',(M@)*',y*'))*')
            .= SumAll(((QuadraticForm(x,(M@),y))*')*') by Th59,A1,A6,A7
            .= SumAll QuadraticForm(x,M@,y) by Th2;
end;

theorem Th65:
  for x,y being FinSequence of COMPLEX, M being Matrix of COMPLEX st
    len x=width M & len y =len M & width M>0 & len M>0 holds
      |(M*x,y)| = |(x,(M@")*y)|
proof let x,y be FinSequence of COMPLEX, M be Matrix of COMPLEX;
 assume A1: len x=width M & len y =len M & width M>0 & len M >0;
  |(x,(M@")*y)| = SumAll(QuadraticForm(x,M@,y)) by Th63,A1;
 hence |(M*x,y)| = |(x,(M@")*y)| by Th64,A1;
end;

theorem
  for x,y being FinSequence of COMPLEX,M being Matrix of COMPLEX st
   len x=len M & len y =width M & width M>0 & len M>0 & len x > 0 holds
    |(x,M*y)| = |((M@")*x,y)|
proof let x,y be FinSequence of COMPLEX, M be Matrix of COMPLEX;
  assume A1: len x=len M & len y =width M & width M>0 & len M>0 &
             len x > 0;
A2: len ((M@")*x)=len (M@") by AA120 .=len ((M@)*')
                  .=len (M@) by AA10
                  .=width M by MATRIX_1:def 7;
  len (M*y) =len x by A1,AA120;
  hence |(x,M*y)| = (|(M*y,x)|)*' by Th60,A1
                .= (|(y,(M@")*x)|)*' by Th65,A1
                .= |((M@")*x,y)| by A2,A1,Th60;
end;
