:: The $\sigma$-additive Measure Theory
::  by J\'ozef Bia{\l}as
::
:: Received October 15, 1990
:: Copyright (c) 1990 Association of Mizar Users
 
environ

 vocabularies NUMBERS, TARSKI, XBOOLE_0, SUBSET_1, SETFAM_1, ZFMISC_1, PROB_1,
      FINSUB_1, FUNCT_1, SUPINF_2, XXREAL_0, MSSUBFAM, ARYTM_3, VALUED_0,
      FUNCOP_1, ARYTM_1, RELAT_1, CARD_3, CARD_1, NAT_1, EQREL_1, PROB_2,
      SUPINF_1, MEASURE1;
 notations TARSKI, XBOOLE_0, ENUMSET1, ZFMISC_1, SUBSET_1, NUMBERS, XXREAL_0,
      RELAT_1, FINSUB_1, FUNCT_1, RELSET_1, PARTFUN1, FUNCT_2, NAT_1, SETFAM_1,
      FUNCOP_1, PROB_1, PROB_2, VALUED_0, SUPINF_1, SUPINF_2;
 constructors ENUMSET1, PARTFUN1, FUNCOP_1, FINSUB_1, REAL_1, NAT_1, PROB_2,
      SUPINF_2, SUPINF_1, RELSET_1;
 registrations XBOOLE_0, SUBSET_1, ORDINAL1, RELSET_1, FUNCT_2, NUMBERS,
      MEMBERED, PROB_1, VALUED_0, FINSUB_1;
 requirements NUMERALS, REAL, SUBSET, BOOLE, ARITHM;
 definitions SUPINF_2, TARSKI, FINSUB_1, PROB_2, PROB_1, XBOOLE_0, SUBSET_1,
      FUNCT_1, RELAT_1, XXREAL_3;
 theorems TARSKI, FUNCT_1, FUNCT_2, ENUMSET1, ZFMISC_1, NAT_1, SETFAM_1,
      SUPINF_2, RELAT_1, FUNCOP_1, FINSUB_1, PROB_2, PROB_1, SUBSET_1,
      XBOOLE_0, XBOOLE_1, XXREAL_0, CARD_3, ORDINAL1, VALUED_0, XXREAL_3;
 schemes FUNCT_2, XBOOLE_0;

begin :: Some useful theorems about sets and R_eal numbers

reserve X for set;

theorem Th1:
  for X,Y being set holds union {X,Y,{}} = union {X,Y}
proof
  let X,Y be set;
  thus union {X,Y,{}} = union ({X,Y} \/ {{}}) by ENUMSET1:43
    .= union {X,Y} \/ union {{}} by ZFMISC_1:96
    .= union {X,Y} \/ {} by ZFMISC_1:31
    .= union {X,Y};
end;

canceled 5;

theorem
  for A,B being Subset of X holds {A,B} is Subset-Family of X
proof
  let A,B be Subset of X;
  set C = {A,B};
  C c= bool X
  proof
    let x be set;
    assume x in C;
    then x = A or x = B by TARSKI:def 2;
    hence thesis;
  end;
  hence thesis;
end;

theorem
  for A,B,C being Subset of X holds {A,B,C} is Subset-Family of X
proof
  let A,B,C be Subset of X;
  set D = {A,B,C};
  D c= bool X
  proof
    let x be set;
    assume x in D;
    then x = A or x = B or x = C by ENUMSET1:def 1;
    hence thesis;
  end;
  hence thesis;
end;

scheme
  DomsetFamEx {A() -> set,P[set]}: ex F being non empty Subset-Family of A()
  st for B being set holds B in F iff B c= A() & P[B]
provided
A1: ex B being set st B c= A() & P[B]
proof
  defpred X[set] means ex Z being set st $1 = Z & P[Z];
  consider X being set such that
A2: for x being set holds x in X iff x in bool A() & X[x] from XBOOLE_0:
  sch 1;
  X c= bool A()
  proof
    let x be set;
    assume x in X;
    hence thesis by A2;
  end;
  then reconsider X as non empty Subset-Family of A() by A1,A2;
  take X;
  for B being set holds B in X iff B c= A() & P[B]
  proof
    let B be set;
    thus B in X implies B c= A() & P[B]
    proof
      assume
A3:   B in X;
      then ex Z being set st B = Z & P[Z] by A2;
      hence thesis by A3;
    end;
    assume B c= A() & P[B];
    hence thesis by A2;
  end;
  hence thesis;
end;

notation
  let X be set;
  let S be non empty Subset-Family of X;
  synonym X\S for COMPLEMENT S;
end;

registration
  let X be set;
  let S be non empty Subset-Family of X;
  cluster X\S -> non empty;
  coherence
  proof
    consider x being Subset of X such that
A1: x in S by SUBSET_1:10;
    x`` in S by A1;
    hence thesis by SETFAM_1:def 8;
  end;
end;

canceled 6;

theorem Th15:
  for S being non empty Subset-Family of X holds meet S = X \
  union (X \ S) & union S = X \ meet (X \ S)
proof
  let S be non empty Subset-Family of X;
A1: X \ union (X \ S) c= meet S
  proof
    let x be set;
    assume
A2: x in X \ union (X \ S);
    then
A3: not x in union (X \ S) by XBOOLE_0:def 5;
    for Y being set st Y in S holds x in Y
    proof
      let Y be set;
      assume that
A4:   Y in S and
A5:   not x in Y;
      reconsider Y as Subset of X by A4;
      Y`` in S by A4;
      then
A6:   Y` in X \ S by SETFAM_1:def 8;
      x in Y` by A2,A5,SUBSET_1:50;
      hence contradiction by A3,A6,TARSKI:def 4;
    end;
    hence thesis by SETFAM_1:def 1;
  end;
  meet S c= X \ union (X \ S)
  proof
    let x be set;
    assume
A7: x in meet S;
    not x in union (X \ S)
    proof
      assume x in union (X \ S);
      then consider Z being set such that
A8:   x in Z and
A9:   Z in X \ S by TARSKI:def 4;
      reconsider Z as Subset of X by A9;
      Z` in S & not x in Z` by A8,A9,SETFAM_1:def 8,XBOOLE_0:def 5;
      hence thesis by A7,SETFAM_1:def 1;
    end;
    hence thesis by A7,XBOOLE_0:def 5;
  end;
  hence meet S = X \ union (X \ S) by A1,XBOOLE_0:def 10;
  thus union S c= X \ meet (X \ S)
  proof
    let x be set;
    assume x in union S;
    then consider Y being set such that
A10: x in Y and
A11: Y in S by TARSKI:def 4;
    reconsider Y as Subset of X by A11;
    not x in meet (X \ S)
    proof
      Y`` in S by A11;
      then
A12:  Y` in X \ S by SETFAM_1:def 8;
      assume
A13:  x in meet (X \ S);
      not x in Y` by A10,XBOOLE_0:def 5;
      hence thesis by A13,A12,SETFAM_1:def 1;
    end;
    hence thesis by A10,A11,XBOOLE_0:def 5;
  end;
  let x be set;
  assume
A14: x in X \ meet (X \ S);
  then not x in meet (X \ S) by XBOOLE_0:def 5;
  then consider Z being set such that
A15: Z in X \ S and
A16: not x in Z by SETFAM_1:def 1;
  reconsider Z as Subset of X by A15;
A17: Z` in S by A15,SETFAM_1:def 8;
  x in Z` by A14,A16,SUBSET_1:50;
  hence thesis by A17,TARSKI:def 4;
end;
 
::
::        Field Subset of X and nonnegative measure
::

definition
  let X be set;
  let IT be Subset-Family of X;
  canceled 2;
  redefine attr IT is compl-closed means
  :Def3:
  for A being set holds A in IT implies X\A in IT;
  compatibility
  proof
    hereby
      assume
A1:   IT is compl-closed;
      let A be set;
      assume
A2:   A in IT;
      then reconsider A9 = A as Subset of X;
      A9` in IT by A1,A2,PROB_1:def 1;
      hence X\A in IT;
    end;
    assume
A3: for A being set holds A in IT implies X\A in IT;
    let A be Subset of X;
    assume A in IT;
    hence thesis by A3;
  end;
end;

registration
  let X be set;
  cluster cup-closed compl-closed -> cap-closed Subset-Family of X;
  coherence
proof
  let S be Subset-Family of X;
  assume
A1: S is cup-closed compl-closed;
  let A,B be set;
  assume that
A2: A in S and
A3: B in S;
  X \ A in S & X \ B in S by A1,A2,A3,Def3;
  then
A4: (X \ A) \/ (X \ B) in S by A1,FINSUB_1:def 1;
  A /\ B c= A by XBOOLE_1:17;
  then A /\ B = X /\ (A /\ B) by A2,XBOOLE_1:1,28
    .= X \ (X \ (A /\ B)) by XBOOLE_1:48
    .= X \ ((X \ A) \/ (X \ B)) by XBOOLE_1:54;
  hence thesis by A1,A4,Def3;
end;
  cluster cap-closed compl-closed -> cup-closed Subset-Family of X;
  coherence
  proof
    let S be Subset-Family of X;
    assume
A1: S is cap-closed compl-closed;
    let A,B be set;
    assume
A2: A in S & B in S;
    then X \ A in S & X \ B in S by A1,Def3;
    then
A3: (X \ A) /\ (X \ B) in S by A1,FINSUB_1:def 2;
    A \/ B = X /\ (A \/ B) by A2,XBOOLE_1:8,28
      .= X \ (X \ (A \/ B)) by XBOOLE_1:48
      .= X \ ((X \ A) /\ (X \ B)) by XBOOLE_1:53;
    hence thesis by A1,A3,Def3;
  end;
end;

canceled;

theorem
  for S being Field_Subset of X holds S = X \ S
proof
  let S be Field_Subset of X;
  for A being set holds A in S iff A in X \ S
  proof
    let A be set;
    hereby
      assume
A1:   A in S;
      then reconsider B = A as Subset of X;
      B` in S by A1,PROB_1:def 1;
      hence A in X \ S by SETFAM_1:def 8;
    end;
    assume
A2: A in X \ S;
    then reconsider B = A as Subset of X;
    B` in S by A2,SETFAM_1:def 8;
    then B`` in S by PROB_1:def 1;
    hence thesis;
  end;
  hence thesis by TARSKI:2;
end;

canceled 2;

registration let X;
 cluster compl-closed cap-closed -> diff-closed Subset-Family of X;
 coherence
proof
  let S be Subset-Family of X such that
Z1: S is compl-closed and
Z2: S is cap-closed;
  let A,B be set;
  assume that
A1: A in S and
A2: B in S;
A3: A /\ (X \ B) = (A /\ X) \ B by XBOOLE_1:49
    .= A \ B by A1,XBOOLE_1:28;
   X \ B in S by A2,Z1,Def3;
  hence thesis by A1,A3,Z2,FINSUB_1:def 2;
end;
end;

theorem
  for S being Field_Subset of X holds for A,B being set st A in S
  & B in S holds A \ B in S by FINSUB_1:def 3;

theorem
  for S being Field_Subset of X holds {} in S & X in S by PROB_1:10,11;

definition
  let X be non empty set, F be Function of X,ExtREAL;
  redefine attr F is nonnegative means
  :Def4:
  for A being Element of X holds 0. <= F.A;
  compatibility by SUPINF_2:58;
end;

canceled 2;

definition
  let X be set, S be Field_Subset of X;
  let F be Function of S,ExtREAL;
  attr F is additive means
  :Def5:
  for A,B being Element of S st A misses B holds F.(A \/ B) = F.A + F.B;
end;

registration
  let X be set, S be Field_Subset of X;
  cluster nonnegative additive zeroed Function of S,ExtREAL;
  existence
  proof
    reconsider M = S --> 0. as Function of S,ExtREAL;
A1: for A,B being Element of S st A misses B holds M.(A \/ B) = M.A + M.B
    proof
      let A,B be Element of S;
      assume A misses B;
A2:   M.A = 0. & M.B = 0. by FUNCOP_1:13;
      reconsider A,B as set;
      reconsider A,B as Element of S;
      0.= M.A + M.B by A2,XXREAL_3:4;
      hence thesis by FUNCOP_1:13;
    end;
    take M;
    ( for x being Element of S holds 0.<= M.x)& M.{} = 0. by FUNCOP_1:13 ,
    PROB_1:10;
    hence thesis by A1,Def4,Def5,VALUED_0:def 19;
  end;
end;

definition
  let X be set, S be Field_Subset of X;
  mode Measure of S is nonnegative additive zeroed Function of S,ExtREAL;
end;

canceled;

theorem Th25:
  for S being Field_Subset of X, M being Measure of S, A,B being
  Element of S holds A c= B implies M.A <= M.B
proof
  let S be Field_Subset of X, M be Measure of S, A,B be Element of S;
  reconsider C = B \ A as Element of S;
A1: 0.<= M.C by Def4;
  A misses C by XBOOLE_1:79;
  then
A2: M.(A \/ C) = M.A + M.C by Def5;
  assume A c= B;
  then M.B = M.A + M.C by A2,XBOOLE_1:45;
  hence thesis by A1,XXREAL_3:42;
end;

theorem Th26:
  for S being Field_Subset of X, M being Measure of S, A,B being
  Element of S holds A c= B & M.A < +infty implies M.(B \ A) = M.B - M.A
proof
  let S be Field_Subset of X, M be Measure of S, A,B be Element of S;
  reconsider C = B \ A as Element of S;
  assume that
A1: A c= B and
A2: M.A < +infty;
A3: 0.<= M.A by Def4;
  A misses C by XBOOLE_1:79;
  then M.(A \/ C) = M.A + M.C by Def5;
  then M.B = M.A + M.C by A1,XBOOLE_1:45;
  hence thesis by A2,A3,XXREAL_3:29;
end;

registration
  let X be set;
  cluster non empty compl-closed cap-closed Subset-Family of X;
  existence
  proof
    consider S being non empty compl-closed cap-closed Subset-Family of X;
    take S;
    thus thesis;
  end;
end;

definition
  let X be set, S be non empty cup-closed Subset-Family of X, A,B be Element
  of S;
  redefine func A \/ B -> Element of S;
  coherence by FINSUB_1:def 1;
end;

definition
  let X be set, S be Field_Subset of X, A,B be Element of S;
  redefine func A /\ B -> Element of S;
  coherence by FINSUB_1:def 2;
  redefine func A \ B -> Element of S;
  coherence by FINSUB_1:def 3;
end;

theorem Th27:
  for S being Field_Subset of X, M being Measure of S, A,B being
  Element of S holds M.(A \/ B) <= M.A + M.B
proof
  let S be Field_Subset of X, M be Measure of S, A,B be Element of S;
  set C = B \ A;
A1: A misses C by XBOOLE_1:79;
A2: M.C <= M.B by Th25,XBOOLE_1:36;
  M.(A \/ B) = M.(A \/ C) by XBOOLE_1:39
    .= M.A + M.C by A1,Def5;
  hence thesis by A2,XXREAL_3:38;
end;

canceled;

theorem
  for S being Field_Subset of X, M being Measure of S holds {} in S & X
in S & for A,B being set st A in S & B in S holds X \ A in S & A \/ B in S & A
  /\ B in S by FINSUB_1:def 1, def 2,PROB_1:10,11, Def3;

definition
  canceled;
  let X be set, S be Field_Subset of X, M be Measure of S;
  mode measure_zero of M -> Element of S means
    :Def7:
    M.it = 0.;
  existence
  proof
    reconsider A = {} as Element of S by PROB_1:10;
    take A;
    thus thesis by VALUED_0:def 19;
  end;
end;

canceled;

theorem
  for S being Field_Subset of X, M being Measure of S, A being Element
  of S, B being measure_zero of M st A c= B holds A is measure_zero of M
proof
  let S be Field_Subset of X, M be Measure of S, A be Element of S, B be
  measure_zero of M;
  assume A c= B;
  then M.A <= M.B by Th25;
  then
A1: M.A <= 0. by Def7;
  0.<= M.A by Def4;
  then M.A = 0.by A1,XXREAL_0:1;
  hence thesis by Def7;
end;

theorem
  for S being Field_Subset of X, M being Measure of S, A,B being
measure_zero of M holds A \/ B is measure_zero of M & A /\ B is measure_zero of
  M & A \ B is measure_zero of M
proof
  let S be Field_Subset of X, M be Measure of S, A,B be measure_zero of M;
A1: 0.<= M.(A /\ B) by Def4;
A2: 0.<= M.(A \ B) by Def4;
A3: M.A = 0. by Def7;
  then M.(A \ B) <= 0.by Th25,XBOOLE_1:36;
  then
A4: M.(A \ B) = 0.by A2,XXREAL_0:1;
  M.B = 0. by Def7;
  then M.(A \/ B) <= 0.+ 0.by A3,Th27;
  then
A5: M.(A \/ B) <= 0.by XXREAL_3:4;
  0.<= M.(A \/ B) by Def4;
  then
A6: M.(A \/ B) = 0.by A5,XXREAL_0:1;
  M.(A /\ B) <= 0.by A3,Th25,XBOOLE_1:17;
  then M.(A /\ B) = 0.by A1,XXREAL_0:1;
  hence thesis by A6,A4,Def7;
end;

theorem
  for S being Field_Subset of X, M being Measure of S, A being Element
of S, B being measure_zero of M holds M.(A \/ B) = M.A & M.(A /\ B) = 0.& M.(A
  \ B) = M.A
proof
  let S be Field_Subset of X, M be Measure of S, A be Element of S, B be
  measure_zero of M;
A1: M.A = M.((A /\ B) \/ (A \ B)) by XBOOLE_1:51;
A2: M.B = 0.by Def7;
  then
A3: M.(A /\ B) <= 0.by Th25,XBOOLE_1:17;
A4: 0.<= M.(A /\ B) by Def4;
  then M.(A /\ B) = 0.by A3,XXREAL_0:1;
  then M.A <= 0.+ M.(A \ B) by A1,Th27;
  then
A5: M.A <= M.(A \ B) by XXREAL_3:4;
  M.(A \/ B) <= M.A + 0.by A2,Th27;
  then
A6: M.(A \/ B) <= M.A by XXREAL_3:4;
  M.A <= M.(A \/ B) & M.(A \ B) <= M.A by Th25,XBOOLE_1:7,36;
  hence thesis by A4,A6,A3,A5,XXREAL_0:1;
end;
 
::
::       sigma_Field Subset of X and sigma_additive nonnegative measure
::

theorem Th34:
  for A being Subset of X ex F being Function of NAT,bool X st rng F = {A}
proof
  let A be Subset of X;
  reconsider F = NAT --> A as Function of NAT, bool X;
  take F;
  thus thesis by FUNCOP_1:14;
end;

theorem Th35:
  for A being set ex F being Function of NAT,{A} st for n being
  Element of NAT holds F.n = A
proof
  let A be set;
  reconsider F = NAT --> A as Function of NAT, {A};
  take F;
  thus thesis by TARSKI:def 1;
end;

registration
  let X be set;
  cluster non empty countable Subset-Family of X;
  existence
  proof
    reconsider S = {{}} as Subset-Family of X by SETFAM_1:61;
    take S;
    thus S is non empty;
    {} is Subset of X by XBOOLE_1:2;
    hence S is empty or ex F being Function of NAT,bool X st S = rng F by
    Th34;
  end;
end;

definition
  let X be set;
  mode N_Sub_set_fam of X is non empty countable Subset-Family of X;
end;

canceled 2;

theorem Th38:
  for A,B,C being Subset of X ex F being Function of NAT,bool X st
rng F = {A,B,C} & F.0 = A & F.1 = B & for n being Element of NAT st 1 < n holds
  F.n = C
proof
  let A,B,C be Subset of X;
  defpred P0[Element of NAT,set] means ($1 = 0 implies $2 = A) & ($1 = 1
  implies $2 = B) & (1 < $1 implies $2 = C);
  defpred P[set] means ex n being Element of NAT st ex X being set st ($1 = [n
  ,X] & P0[n,X]);
  ex GRAF being set st for x being set holds x in GRAF iff x in [:NAT,bool
  X:] & P[x] from XBOOLE_0:sch 1;
  then consider GRAF being set such that
A1: for x being set holds x in GRAF iff x in [:NAT,bool X:] & P[x];
A2: GRAF is Function-like
  proof
    let x,y1,y2 be set;
    assume that
A3: [x,y1] in GRAF and
A4: [x,y2] in GRAF;
A5: P[[x,y1]] by A1,A3;
A6: [x,y2] in [:NAT,bool X:] by A1,A4;
A7: P[[x,y2]] by A1,A4;
    reconsider x as Element of NAT by A6,ZFMISC_1:106;
    per cases by NAT_1:26;
    suppose
A8:   x = 0;
      then y1 = A by A5,ZFMISC_1:33;
      hence thesis by A7,A8,ZFMISC_1:33;
    end;
    suppose
A9:   x = 1;
      then y1 = B by A5,ZFMISC_1:33;
      hence thesis by A7,A9,ZFMISC_1:33;
    end;
    suppose
A10:  x > 1;
      then y1 = C by A5,ZFMISC_1:33;
      hence thesis by A7,A10,ZFMISC_1:33;
    end;
  end;
A11: for x being set holds x in GRAF iff (x = [0,A] or x = [1,B] or ex n
  being Element of NAT st x = [n,C] & 1 < n )
  proof
    let x be set;
A12: (x = [0,A] or x = [1,B] or ex n being Element of NAT st x = [n,C] & 1
    < n ) implies x in GRAF
    proof
A13:  (ex n being Element of NAT st (x = [n,C] & 1 < n)) implies x in GRAF
      proof
        given n being Element of NAT such that
A14:    x = [n,C] and
A15:    1 < n;
        ( P0[n,C])& x in [:NAT,bool X:] by A14,A15,ZFMISC_1:106;
        hence thesis by A1,A14;
      end;
A16:  x = [1,B] implies x in GRAF
      proof
        assume
A17:    x = [1,B];
        then x in [:NAT,bool X:] by ZFMISC_1:106;
        hence thesis by A1,A17;
      end;
A18:  x = [0,A] implies x in GRAF
      proof
        assume
A19:    x = [0,A];
        then x in [:NAT,bool X:] by ZFMISC_1:106;
        hence thesis by A1,A19;
      end;
      assume
      x = [0,A] or x = [1,B] or ex n being Element of NAT st x = [n,C] & 1 < n;
      hence thesis by A18,A16,A13;
    end;
    x in GRAF implies (x = [0,A] or x = [1,B] or ex n being Element of NAT
    st x = [n,C] & 1 < n )
    proof
      assume x in GRAF;
      then
      ex n being Element of NAT st ex X0 being set st x = [n,X0] & P0[n,X0]
      by A1;
      hence thesis by NAT_1:26;
    end;
    hence thesis by A12;
  end;
  GRAF is Relation-like
  proof
    let p be set;
    assume p in GRAF;
    then p = [0,A] or p = [1,B] or ex n being Element of NAT st p = [n,C] & 1
    < n by A11;
    hence thesis;
  end;
  then reconsider F = GRAF as Function by A2;
A20: for x being set holds x in NAT iff ex y being set st [x,y] in F
  proof
    let x be set;
    thus x in NAT implies ex y being set st [x,y] in F
    proof
      assume x in NAT;
      then reconsider x as Element of NAT;
A21:  x = 1 implies ex y being set st [x,y] in F
      proof
        assume
A22:    x = 1;
        take B;
        thus thesis by A11,A22;
      end;
A23:  1 < x implies ex y being set st [x,y] in F
      proof
        assume
A24:    1 < x;
        take C;
        thus thesis by A11,A24;
      end;
      x = 0 implies ex y being set st [x,y] in F
      proof
        assume
A25:    x = 0;
        take A;
        thus thesis by A11,A25;
      end;
      hence thesis by A21,A23,NAT_1:26;
    end;
    given y being set such that
A26: [x,y] in F;
    [x,y] in [:NAT,bool X:] by A1,A26;
    hence thesis by ZFMISC_1:106;
  end;
  then
A27: dom F = NAT by RELAT_1:def 4;
A28: for y being set holds y in {A,B,C} iff ex x being set st x in dom F & y
  = F.x
  proof
    let y be set;
    thus y in {A,B,C} implies ex x being set st x in dom F & y = F.x
    proof
      assume
A29:  y in {A,B,C};
      per cases by A29,ENUMSET1:def 1;
      suppose
A30:    y = A;
        take 0;
        [0,y] in F by A11,A30;
        hence thesis by FUNCT_1:8;
      end;
      suppose
A31:    y = B;
        take 1;
        [1,y] in F by A11,A31;
        hence thesis by FUNCT_1:8;
      end;
      suppose
A32:    y = C;
        take 2;
        [2,y] in F by A11,A32;
        hence thesis by FUNCT_1:8;
      end;
    end;
    given x being set such that
A33: x in dom F and
A34: y = F.x;
    reconsider x as Element of NAT by A20,A33,RELAT_1:def 4;
    per cases by NAT_1:26;
    suppose
A35:  x = 0;
      [0,A] in F by A11;
      then A = F.x by A35,FUNCT_1:8;
      hence thesis by A34,ENUMSET1:def 1;
    end;
    suppose
A36:  x = 1;
      [1,B] in F by A11;
      then B = F.x by A36,FUNCT_1:8;
      hence thesis by A34,ENUMSET1:def 1;
    end;
    suppose
      1 < x;
      then [x,C] in F by A11;
      then y = C by A34,FUNCT_1:8;
      hence thesis by ENUMSET1:def 1;
    end;
  end;
  then
A37: rng F = {A,B,C} by FUNCT_1:def 5;
  rng F c= bool X
  proof
    let a be set;
    assume a in rng F;
    then a = A or a = B or a = C by A37,ENUMSET1:def 1;
    hence thesis;
  end;
  then reconsider F as Function of NAT,bool X by A27,FUNCT_2:4;
A38: for n being Element of NAT st 1 < n holds F.n = C
  proof
    let n be Element of NAT;
    assume 1 < n;
    then [n,C] in F by A11;
    hence thesis by FUNCT_1:8;
  end;
  take F;
  [0,A] in F & [1,B] in F by A11;
  hence thesis by A28,A38,FUNCT_1:8,def 5;
end;

theorem
  for A,B being Subset of X holds {A,B,{}} is N_Sub_set_fam of X
proof
  let A,B be Subset of X;
  {} is Subset of X by XBOOLE_1:2;
  then consider C being Subset of X such that
A1: C = {};
  ex F being Function of NAT,bool X st rng F = {A,B,C} & F.0 = A & F.1 = B
  & for n being Element of NAT st 1 < n holds F.n = C by Th38;
  hence thesis by A1,SUPINF_2:def 14;
end;

theorem Th40:
  for A,B being Subset of X ex F being Function of NAT,bool X st
  rng F = {A,B} & F.0 = A & for n being Element of NAT st 0 < n holds F.n = B
proof
  let A,B be Subset of X;
  consider F being Function of NAT,bool X such that
A1: rng F = {A,B,B} & F.0 = A &( F.1 = B & for n being Element of NAT st
  1 < n holds F.n = B ) by Th38;
  take F;
  {A,B,B} = {B,B,A} by ENUMSET1:100
    .= {A,B} by ENUMSET1:70;
  hence thesis by A1,NAT_1:26;
end;

theorem
  for A,B being Subset of X holds {A,B} is N_Sub_set_fam of X
proof
  let A,B be Subset of X;
  ex F being Function of NAT,bool X st (rng F = {A,B} & F.0 = A & for n
  being Element of NAT st 0 < n holds F.n = B) by Th40;
  hence thesis by SUPINF_2:def 14;
end;

theorem Th42:
  for S being N_Sub_set_fam of X holds X \ S is N_Sub_set_fam of X
proof
  let S be N_Sub_set_fam of X;
  consider F being Function of NAT,bool X such that
A1: S = rng F by SUPINF_2:def 14;
  defpred P[set,set] means ex V being Subset of X st V = F.$1 & $2 = V`;
A2: for n being set st n in NAT ex y being set st y in X\S & P[n,y]
  proof
    let n be set;
    assume n in NAT;
    then consider V being set such that
A3: V in S and
A4: V=F.n by A1,FUNCT_2:6;
    reconsider V as Subset of X by A3;
    take V`;
    V`` in S by A3;
    hence thesis by A4,SETFAM_1:def 8;
  end;
A5: ex G being Function of NAT,X \ S st for n being set st n in NAT holds P[
  n,G.n] from FUNCT_2:sch 1(A2);
A6: NAT = dom F by FUNCT_2:def 1;
  ex G being Function of NAT,bool X st X \ S = rng G
  proof
    consider G being Function of NAT,X \ S such that
A7: for n being set st n in NAT holds ex V being Subset of X st V = F
    .n & G.n = V` by A5;
A8: dom G = NAT by FUNCT_2:def 1;
A9: X \ S c= rng G
    proof
      let x be set;
      assume
A10:  x in X \ S;
      then reconsider B = x as Subset of X;
      B` in S by A10,SETFAM_1:def 8;
      then consider n being set such that
A11:  n in NAT and
A12:  F.n = B` by A1,A6,FUNCT_1:def 5;
      ex V being Subset of X st V = F.n & G.n = V` by A7,A11;
      hence thesis by A8,A11,A12,FUNCT_1:def 5;
    end;
    reconsider G as Function of NAT,bool X by FUNCT_2:9;
    take G;
    thus thesis by A9,XBOOLE_0:def 10;
  end;
  hence thesis by SUPINF_2:def 14;
end;

definition
  let X be set;
  let IT be Subset-Family of X;
  canceled;
  attr IT is sigma-additive means
  :Def9:
  for M being N_Sub_set_fam of X st M c= IT holds union M in IT;
end;

registration
  let X be set;
  cluster non empty compl-closed sigma-additive Subset-Family of X;
  existence
  proof
    reconsider S = {{},X} as non empty Subset-Family of X by PROB_1:14;
    take S;
    thus S is non empty;
    thus for A being set holds A in S implies X\A in S
    proof
      let A be set;
A1:   A = {} implies X\A in S by TARSKI:def 2;
A2:   A = X implies X\A in S
      proof
        assume A = X;
        then X\A = {} by XBOOLE_1:37;
        hence thesis by TARSKI:def 2;
      end;
      assume A in S;
      hence thesis by A1,A2,TARSKI:def 2;
    end;
    let M be N_Sub_set_fam of X;
    assume
A3: M c= S;
A4: not X in M implies union M in S
    proof
      assume not X in M;
      then for A being set st A in M holds A c= {} by A3,TARSKI:def 2;
      then union M c= {} by ZFMISC_1:94;
      then union M = {};
      hence thesis by TARSKI:def 2;
    end;
    X in M implies union M in S
    proof
      assume X in M;
      then X c= union M by ZFMISC_1:92;
      then X = union M by XBOOLE_0:def 10;
      hence thesis by TARSKI:def 2;
    end;
    hence thesis by A4;
  end;
end;

registration
  let X;
  cluster compl-closed sigma-multiplicative -> sigma-additive Subset-Family of
    X;
  coherence
  proof
    let F be Subset-Family of X such that
A1: F is compl-closed and
A2: F is sigma-multiplicative;
    let M be non empty countable Subset-Family of X such that
A3: M c= F;
    consider f being SetSequence of X such that
A4: M = rng f by SUPINF_2:def 14;
    for n being Nat holds (Complement f).n in F
    proof
      let n be Nat;
      reconsider n as Element of NAT by ORDINAL1:def 13;
      f.n in M & (Complement f).n = (f.n)` by A4,FUNCT_2:6,PROB_1:def 4;
      hence thesis by A1,A3,PROB_1:def 1;
    end;
    then rng Complement f c= F by NAT_1:53;
    then
A5: Intersection Complement f in F by A2,PROB_1:def 8;
    (Intersection Complement f)` = union M by A4,CARD_3:def 4;
    hence thesis by A1,A5,PROB_1:def 1;
  end;
  cluster compl-closed sigma-additive -> sigma-multiplicative Subset-Family of
    X;
  coherence
  proof
    let F be Subset-Family of X such that
A6: F is compl-closed and
A7: F is sigma-additive;
    let f be SetSequence of X such that
A8: rng f c= F;
    dom Complement f = NAT by FUNCT_2:def 1;
    then reconsider
    M = rng Complement f as non empty countable Subset-Family of X
    by CARD_3:127,RELAT_1:65;
    M c= F
    proof
      let e be set;
      assume e in M;
      then consider n being set such that
A9:   n in NAT and
A10:  e = (Complement f).n by FUNCT_2:17;
      reconsider n as Element of NAT by A9;
A11:  f.n in rng f by NAT_1:52;
      e = (f.n)` by A10,PROB_1:def 4;
      hence thesis by A6,A8,A11,PROB_1:def 1;
    end;
    then Intersection f = (union M)` & union M in F by A7,
    Def9,CARD_3:def
    4;
    hence thesis by A6,PROB_1:def 1;
  end;
end;

registration
  let X be set;
  cluster -> non empty SigmaField of X;
  coherence;
end;

canceled 6;

theorem Th49:
  for S being non empty Subset-Family of X holds (for A being set
  holds A in S implies X\A in S) & (for M being N_Sub_set_fam of X st M c= S
  holds meet M in S) iff S is SigmaField of X
proof
  let S be non empty Subset-Family of X;
  hereby
    assume that
A1: for A being set holds A in S implies X\A in S and
A2: for M being N_Sub_set_fam of X st M c= S holds meet M in S;
    for M being N_Sub_set_fam of X st M c= S holds union M in S
    proof
      let M be N_Sub_set_fam of X;
      assume
A3:   M c= S;
A4:   X \ M c= S
      proof
        let y be set;
        assume
A5:     y in X \ M;
        then reconsider B = y as Subset of X;
        B` in M by A5,SETFAM_1:def 8;
        then B`` in S by A1,A3;
        hence thesis;
      end;
      X \ M is N_Sub_set_fam of X by Th42;
      then X \ meet (X \ M) in S by A1,A2,A4;
      hence thesis by Th15;
    end;
    then reconsider
    S9 = S as non empty compl-closed sigma-additive Subset-Family
    of X by A1,Def3,Def9;
    S9 is SigmaField of X;
    hence S is SigmaField of X;
  end;
  assume
A6: S is SigmaField of X;
  for M being N_Sub_set_fam of X st M c= S holds meet M in S
  proof
    let M be N_Sub_set_fam of X;
    assume
A7: M c= S;
A8: X \ M c= S
    proof
      let y be set;
      assume
A9:   y in X \ M;
      then reconsider B = y as Subset of X;
      B` in M by A9,SETFAM_1:def 8;
      then B`` in S by A6,A7,PROB_1:def 1;
      hence thesis;
    end;
    X \ M is N_Sub_set_fam of X by Th42;
    then union (X \ M) in S by A6,A8,Def9;
    then X \ union (X \ M) in S by A6,Def3;
    hence thesis by Th15;
  end;
  hence thesis by A6,Def3;
end;

registration
  let X be set;
  let S be SigmaField of X;
  cluster disjoint_valued Function of NAT, S;
  existence
  proof
    consider F being Function of NAT,{{}} such that
A1: for n being Element of NAT holds F.n = {} by Th35;
    {} in S by PROB_1:10;
    then {{}} c= S by ZFMISC_1:37;
    then reconsider F as Function of NAT,S by FUNCT_2:9;
    take F;
A2: for n being set holds F.n = {}
    proof
      let n be set;
      per cases;
      suppose
        n in dom F;
        hence thesis by A1;
      end;
      suppose
        not n in dom F;
        hence thesis by FUNCT_1:def 4;
      end;
    end;
    thus for x,y being set st x <> y holds F.x misses F.y
    proof
      let x,y be set;
      F.x = {} by A2;
      hence thesis by XBOOLE_1:65;
    end;
  end;
end;

definition
  let X be set;
  let S be SigmaField of X;
  mode Sep_Sequence of S is disjoint_valued Function of NAT, S;
end;

definition
  let X be set;
  let S be SigmaField of X;
  let F be Function of NAT,S;
  redefine func rng F -> non empty Subset-Family of X;
  coherence
  proof
    0 in NAT;
    then rng F <> {} by FUNCT_2:6;
    hence thesis by XBOOLE_1:1;
  end;
end;

canceled 2;

theorem Th52:
  for S being SigmaField of X, F being Function of NAT,S holds rng
  F is N_Sub_set_fam of X
proof
  let S be SigmaField of X;
  let F be Function of NAT,S;
  ex H being Function of NAT,bool X st rng F = rng H
  proof
    rng F c= bool X;
    then reconsider F as Function of NAT,bool X by FUNCT_2:8;
    take F;
    thus thesis;
  end;
  hence thesis by SUPINF_2:def 14;
end;

theorem Th53:
  for S being SigmaField of X, F being Function of NAT,S holds
  union rng F is Element of S
proof
  let S be SigmaField of X, F be Function of NAT,S;
  rng F is N_Sub_set_fam of X & rng F c= S by Th52,RELAT_1:def 19;
  hence thesis by Def9;
end;

theorem Th54:
  for Y,S being non empty set, F being Function of Y,S, M being
  Function of S,ExtREAL st M is nonnegative holds M*F is nonnegative
proof
  let Y,S be non empty set;
  let F be Function of Y,S;
  let M be Function of S,ExtREAL;
  assume
A1: M is nonnegative;
  for n being Element of Y holds 0.<= (M*F).n
  proof
    let n be Element of Y;
    dom (M*F) = Y by FUNCT_2:def 1;
    then (M*F).n = M.(F.n) by FUNCT_1:22;
    hence thesis by A1,Def4;
  end;
  hence thesis by SUPINF_2:58;
end;

theorem Th55:
  for S being SigmaField of X, a,b being R_eal holds ex M being
Function of S,ExtREAL st for A being Element of S holds (A = {} implies M.A = a
  ) & (A <> {} implies M.A = b)
proof
  let S be SigmaField of X, a,b be R_eal;
  defpred P[set,set] means ($1 = {} implies $2 = a) & ($1 <> {} implies $2 = b
  );
A1: for x being set st x in S ex y being set st y in ExtREAL & P[x,y]
  proof
    let x be set;
    x <> {} implies ex y being set st y in ExtREAL & P[x,y];
    hence thesis;
  end;
  ex F being Function of S,ExtREAL st for x being set st x in S holds P[x,
  F.x] from FUNCT_2:sch 1(A1);
  then consider M being Function of S,ExtREAL such that
A2: for x being set st x in S holds P[x,M.x];
  take M;
  thus thesis by A2;
end;

theorem
  for S being SigmaField of X ex M being Function of S,ExtREAL st for A
  being Element of S holds (A = {} implies M.A = 0.) & (A <> {} implies M.A =
  +infty) by Th55;

theorem Th57:
  for S being SigmaField of X ex M being Function of S,ExtREAL st
  for A being Element of S holds M.A = 0.
proof
  let S be SigmaField of X;
  consider M being Function of S,ExtREAL such that
A1: for A being Element of S holds (A = {} implies M.A = 0.) & (A <> {}
  implies M.A = 0.) by Th55;
  take M;
  thus thesis by A1;
end;

definition
  let X be set;
  let S be SigmaField of X;
  let F be Function of S,ExtREAL;
  canceled;
  attr F is sigma-additive means
  :Def11:
  for s being Sep_Sequence of S holds SUM(F*s) = F.(union rng s);
end;

registration
  let X be set;
  let S be SigmaField of X;
  cluster nonnegative sigma-additive zeroed Function of S,ExtREAL;
  existence
  proof
    consider M being Function of S,ExtREAL such that
A1: for A being Element of S holds M.A = 0. by Th57;
    take M;
    for A being Element of S holds 0.<= M.A by A1;
    hence
A2: M is nonnegative by Def4;
    thus M is sigma-additive
    proof
      let F be Sep_Sequence of S;
A3:   for r being Element of NAT st 0 <= r holds (M*F).r = 0.
      proof
        let r be Element of NAT;
        dom (M*F) = NAT by FUNCT_2:def 1;
        then (M*F).r = M.(F.r) by FUNCT_1:22;
        hence thesis by A1;
      end;
      M*F is nonnegative by A2,Th54;
      then
A4:   SUM(M*F) = Ser(M*F).0 by A3,SUPINF_2:67;
      union rng F is Element of S by Th53;
      then
A5:   M.(union rng F) = 0.by A1;
      Ser(M*F).0 = (M*F).0 by SUPINF_2:63;
      hence thesis by A5,A3,A4;
    end;
    {} is Element of S by PROB_1:10;
    then M.{} = 0. by A1;
    hence thesis by VALUED_0:def 19;
  end;
end;

definition
  let X be set;
  let S be SigmaField of X;
  mode sigma_Measure of S is nonnegative sigma-additive zeroed Function of S,
    ExtREAL;
end;

registration
  let X be set;
  cluster sigma-additive compl-closed -> cup-closed (non empty Subset-Family
    of X);
  coherence;
end;

canceled 2;

theorem Th60:
  for S being SigmaField of X, M being sigma_Measure of S holds M
  is Measure of S
proof
  let S be SigmaField of X, M be sigma_Measure of S;
  for A,B being Element of S holds (A misses B implies M.(A \/ B) = M.A + M.B)
  proof
    set n2 = 1 + 1;
    let A,B be Element of S;
    assume
A1: A misses B;
A2: A in S & B in S;
    reconsider A,B as Subset of X;
    {} is Subset of X by XBOOLE_1:2;
    then consider F being Function of NAT,bool X such that
A3: rng F = {A,B,{}} and
A4: F.0 = A & F.1 = B and
A5: for n being Element of NAT st 1 < n holds F.n = {} by Th38;
    {} in S by PROB_1:10;
    then for x being set holds x in {A,B,{}} implies x in S by A2,
    ENUMSET1:
    def 1;
    then {A,B,{}} c= S by TARSKI:def 3;
    then reconsider F as Function of NAT,S by A3,FUNCT_2:8;
A6: for n,m being Element of NAT st n <> m holds F.n misses F.m
    proof
      let n,m be Element of NAT;
A7:   n = 0 or n = 1 or 1 < n by NAT_1:26;
A8:   m = 0 or m = 1 or 1 < m by NAT_1:26;
A9:   1 < n & m = 1 implies F.n misses F.m
      proof
        assume that
A10:    1 < n and
        m = 1;
        F.n = {} by A5,A10;
        then F.n /\ F.m = {};
        hence thesis by XBOOLE_0:def 7;
      end;
A11:  1 < n & m = 0 implies F.n misses F.m
      proof
        assume that
A12:    1 < n and
        m = 0;
        F.n = {} by A5,A12;
        then F.n /\ F.m = {};
        hence thesis by XBOOLE_0:def 7;
      end;
A13:  1 < n & 1 < m implies F.n misses F.m
      proof
        assume that
A14:    1 < n and
        1 < m;
        F.n = {} by A5,A14;
        then F.n /\ F.m = {};
        hence thesis by XBOOLE_0:def 7;
      end;
A15:  n = 1 & 1 < m implies F.n misses F.m
      proof
        assume that
        n = 1 and
A16:    1 < m;
        F.m = {} by A5,A16;
        then F.n /\ F.m = {};
        hence thesis by XBOOLE_0:def 7;
      end;
A17:  n = 0 & 1 < m implies F.n misses F.m
      proof
        assume that
        n = 0 and
A18:    1 < m;
        F.m = {} by A5,A18;
        then F.n /\ F.m = {};
        hence thesis by XBOOLE_0:def 7;
      end;
      assume n <> m;
      hence thesis by A1,A4,A7,A8,A17,A15,A11,A9,A13;
    end;
    for m,n being set st m <> n holds F.m misses F.n
    proof
      let m,n be set;
      assume
A19:  m <> n;
      per cases;
      suppose
        m in NAT & n in NAT;
        hence thesis by A6,A19;
      end;
      suppose
        not m in NAT or not n in NAT;
        then not m in dom F or not n in dom F;
        then F.m = {} or F.n = {} by FUNCT_1:def 4;
        hence thesis by XBOOLE_1:65;
      end;
    end;
    then reconsider F as disjoint_valued Function of NAT, S by PROB_2:def 3;
    union {A,B,{}} = union {A,B} by Th1;
    then
A20: union rng F = A \/ B by A3,ZFMISC_1:93;
A21: for r being Element of NAT holds (M*F).0 = M.A & (M*F).1 = M.B & (1+1
    <= r implies (M*F).r = 0.)
    proof
      let r be Element of NAT;
A22:  for r being Element of NAT holds (M*F).r = M.(F.r)
      proof
        let r be Element of NAT;
        dom (M*F) = NAT by FUNCT_2:def 1;
        hence thesis by FUNCT_1:22;
      end;
      1 + 1 <= r implies (M*F).r = 0.
      proof
        assume 1 + 1 <= r;
        then 1 < r by NAT_1:13;
        then F.r = {} by A5;
        then (M*F).r = M.{} by A22;
        hence thesis by VALUED_0:def 19;
      end;
      hence thesis by A4,A22;
    end;
    set H = M*F;
A23: 0 + 1 = 0 + 1;
    set y1 = Ser H.1;
A24: H is nonnegative by Th54;
    reconsider A,B as Element of S;
    Ser H.n2 = y1 + H.n2 by SUPINF_2:63;
    then Ser H.n2 = y1 + 0.by A21
      .= Ser H.1 by XXREAL_3:4
      .= Ser H.0 + H.1 by A23,SUPINF_2:63
      .= M.A + M.B by A21,SUPINF_2:63;
    then SUM(M*F) = M.A + M.B by A21,A24,SUPINF_2:67;
    hence thesis by A20,Def11;
  end;
  hence thesis by Def5;
end;

theorem
  for S being SigmaField of X, M being sigma_Measure of S, A,B being
  Element of S st A misses B holds M.(A \/ B) = M.A + M.B
proof
  let S be SigmaField of X, M be sigma_Measure of S, A,B be Element of S;
  assume
A1: A misses B;
  M is Measure of S by Th60;
  hence thesis by A1,Def5;
end;

theorem Th62:
  for S being SigmaField of X, M being sigma_Measure of S, A,B
  being Element of S st A c= B holds M.A <= M.B
proof
  let S be SigmaField of X, M be sigma_Measure of S, A,B be Element of S;
  assume
A1: A c= B;
  M is Measure of S by Th60;
  hence thesis by A1,Th25;
end;

theorem
  for S being SigmaField of X, M being sigma_Measure of S, A,B being
  Element of S st A c= B & M.A < +infty holds M.(B \ A) = M.B - M.A
proof
  let S be SigmaField of X, M be sigma_Measure of S, A,B be Element of S;
  assume
A1: A c= B & M.A < +infty;
  M is Measure of S by Th60;
  hence thesis by A1,Th26;
end;

theorem Th64:
  for S being SigmaField of X, M being sigma_Measure of S, A,B
  being Element of S holds M.(A \/ B) <= M.A + M.B
proof
  let S be SigmaField of X, M be sigma_Measure of S, A,B be Element of S;
  M is Measure of S by Th60;
  hence thesis by Th27;
end;

canceled;

theorem
  for S being SigmaField of X, M being sigma_Measure of S holds {} in S
& X in S & for A,B being set st A in S & B in S holds X \ A in S & A \/ B in S
  & A /\ B in S by Def3,FINSUB_1:def 1, def 2,PROB_1:10,11;

theorem
  for S being SigmaField of X, M being sigma_Measure of S, T being
N_Sub_set_fam of X st (for A being set st A in T holds A in S) holds union T in
  S & meet T in S
proof
  let S be SigmaField of X, M be sigma_Measure of S, T be N_Sub_set_fam of X;
  assume
A1: for A being set st A in T holds A in S;
  T c= S
  proof
    let x be set;
    assume x in T;
    hence thesis by A1;
  end;
  hence thesis by Def9,Th49;
end;

definition
  canceled;
  let X be set, S be SigmaField of X, M be sigma_Measure of S;
  mode measure_zero of M -> Element of S means
    :Def13:
    M.it = 0.;
  existence
  proof
    {} is Element of S by PROB_1:10;
    then consider A being Element of S such that
A1: A = {};
    take A;
    thus thesis by A1,VALUED_0:def 19;
  end;
end;

canceled;

theorem
  for S being SigmaField of X, M being sigma_Measure of S, A being
Element of S, B being measure_zero of M st A c= B holds A is measure_zero of M
proof
  let S be SigmaField of X, M be sigma_Measure of S, A be Element of S, B be
  measure_zero of M;
  assume A c= B;
  then M.A <= M.B by Th62;
  then
A1: M.A <= 0. by Def13;
  0.<= M.A by Def4;
  then M.A = 0.by A1,XXREAL_0:1;
  hence thesis by Def13;
end;

theorem
  for S being SigmaField of X, M being sigma_Measure of S, A,B being
measure_zero of M holds A \/ B is measure_zero of M & A /\ B is measure_zero of
  M & A \ B is measure_zero of M
proof
  let S be SigmaField of X, M be sigma_Measure of S, A,B be measure_zero of M;
A1: 0.<= M.(A /\ B) by Def4;
A2: 0.<= M.(A \ B) by Def4;
A3: M.A = 0. by Def13;
  then M.(A \ B) <= 0.by Th62,XBOOLE_1:36;
  then
A4: M.(A \ B) = 0.by A2,XXREAL_0:1;
  M.B = 0. by Def13;
  then M.(A \/ B) <= 0.+ 0.by A3,Th64;
  then
A5: M.(A \/ B) <= 0.by XXREAL_3:4;
  0.<= M.(A \/ B) by Def4;
  then
A6: M.(A \/ B) = 0.by A5,XXREAL_0:1;
  M.(A /\ B) <= 0.by A3,Th62,XBOOLE_1:17;
  then M.(A /\ B) = 0.by A1,XXREAL_0:1;
  hence thesis by A6,A4,Def13;
end;

theorem
  for S being SigmaField of X, M being sigma_Measure of S, A being
  Element of S, B being measure_zero of M holds M.(A \/ B) = M.A & M.(A /\ B) =
  0.& M.(A \ B) = M.A
proof
  let S be SigmaField of X, M be sigma_Measure of S, A be Element of S, B be
  measure_zero of M;
A1: M.A = M.((A /\ B) \/ (A \ B)) by XBOOLE_1:51;
A2: M.B = 0.by Def13;
  then
A3: M.(A /\ B) <= 0.by Th62,XBOOLE_1:17;
A4: 0.<= M.(A /\ B) by Def4;
  then M.(A /\ B) = 0.by A3,XXREAL_0:1;
  then M.A <= 0.+ M.(A \ B) by A1,Th64;
  then
A5: M.A <= M.(A \ B) by XXREAL_3:4;
  M.(A \/ B) <= M.A + 0.by A2,Th64;
  then
A6: M.(A \/ B) <= M.A by XXREAL_3:4;
  M.A <= M.(A \/ B) & M.(A \ B) <= M.A by Th62,XBOOLE_1:7,36;
  hence thesis by A4,A6,A3,A5,XXREAL_0:1;
end;
