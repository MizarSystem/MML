:: Cardinal Numbers and Finite Sets
::  by Karol P\c{a}k
::
:: Received May 24, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies NUMBERS, SUBSET_1, FUNCT_1, NAT_1, TARSKI, FINSET_1, RELAT_1,
      AFINSQ_1, ARYTM_1, ARYTM_3, FINSEQ_1, XXREAL_0, CARD_1, XBOOLE_0,
      ORDINAL4, CARD_3, BINOP_1, FINSOP_1, FUNCOP_1, BINOP_2, FUNCT_2, INT_1,
      VALUED_1, NEWTON, REALSET1, ZFMISC_1, FUNCT_4, EQREL_1, SETFAM_1,
      STIRL2_1, CARD_FIN;
 notations TARSKI, XBOOLE_0, SUBSET_1, CARD_1, NUMBERS, RELAT_1, FUNCT_1,
      NAT_1, FINSET_1, ORDINAL1, XXREAL_0, NAT_D, AFINSQ_1, VALUED_1, RELSET_1,
      PARTFUN1, DOMAIN_1, FUNCT_2, FUNCT_4, FUNCOP_1, INT_1, ENUMSET1, BINOP_1,
      BINOP_2, RECDEF_1, AFINSQ_2, ZFMISC_1, NEWTON, STIRL2_1, YELLOW20;
 constructors PARTFUN3, BINOM, WELLORD2, DOMAIN_1, SETWISEO, REAL_1, NAT_D,
      YELLOW20, RECDEF_1, BINOP_2, RELSET_1, ORDINAL4, SEQ_1, AFINSQ_1, SEQ_4,
      AFINSQ_2, STIRL2_1;
 registrations XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, ORDINAL1, FUNCT_2,
      FUNCOP_1, FUNCT_4, FINSET_1, FRAENKEL, NUMBERS, XXREAL_0, XREAL_0, NAT_1,
      INT_1, BINOP_2, CARD_1, WSIERP_1, AFINSQ_1, RELSET_1, VALUED_1, VALUED_0,
      AFINSQ_2;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, XBOOLE_0, FUNCT_1, FUNCOP_1, FINSET_1, RELAT_1;
 theorems AFINSQ_1, TARSKI, FUNCT_1, XBOOLE_0, ZFMISC_1, RELAT_1, XBOOLE_1,
      NAT_1, FUNCT_2, FINSET_1, XCMPLX_1, CARD_2, INT_1, WELLORD2, SUBSET_1,
      ENUMSET1, CARD_1, BINOP_2, NEWTON, XREAL_1, FUNCOP_1, STIRL2_1, ALTCAT_1,
      FUNCT_5, IRRAT_1, FUNCT_4, YELLOW20, XXREAL_0, NAT_D, XREAL_0, PRE_POLY,
      AFINSQ_2, ORDINAL1, VALUED_1, VALUED_0;
 schemes FUNCT_2, NAT_1, XBOOLE_0, STIRL2_1;

begin

reserve x, x1, x2, y, z, X9 for set,
  X, Y for finite set,
  n, k, m for Nat,
  f for Function;

theorem Th1: :: TREES_1:19
  X c= Y & card X=card Y implies X=Y
proof
  assume that
A1: X c= Y and
A2: card X=card Y;
  card (Y\X)=card Y-card X by A1,CARD_2:63;
  then Y\X={} by A2;
  then Y c= X by XBOOLE_1:37;
  hence thesis by A1,XBOOLE_0:def 10;
end;

theorem Th2:
  for X,Y,x,y st (Y={} implies X={}) & not x in X holds card Funcs(
  X,Y) = card{F where F is Function of X\/{x},Y\/{y}:rng (F|X) c=Y & F.x=y}
proof
  defpred P[set,set,set] means 1=1;
  let X,Y,x,y;
  assume
A1: Y={} implies X={};
  set F2={f where f is Function of (X\/{x}),(Y\/{y}): P[f,X\/{x},Y\/{y}]&rng (
  f|X) c=Y & f.x=y};
A2: for f be Function of X\/{x},Y\/{y} st f.x=y holds P[f,X\/{x},Y\/{y}] iff
  P[f|X,X,Y];
  set F1={f where f is Function of X,Y:P[f,X,Y]};
  assume
A3: not x in X;
  set F3={F where F is Function of X\/{x},Y\/{y}:rng (F|X) c=Y & F.x=y};
A4: Funcs(X,Y) c= F1
  proof
    let F be set;
    assume F in Funcs(X,Y);
    then F is Function of X,Y by FUNCT_2:121;
    hence thesis;
  end;
A5: F2 c= F3
  proof
    let F be set;
    assume F in F2;
    then
    ex f be Function of (X\/{x}),(Y\/{y}) st f=F & P[f,X\/{x},Y\/{y}]&rng
    (f|X) c=Y & f.x=y;
    hence thesis;
  end;
A6: Y is empty implies X is empty by A1;
A7: card F1=card F2 from STIRL2_1:sch 4(A6,A3,A2);
A8: F3 c= F2
  proof
    let F be set;
    assume F in F3;
    then ex f be Function of (X\/{x}),(Y\/{y}) st f=F & rng (f|X) c=Y & f.x=y;
    hence thesis;
  end;
  F1 c= Funcs(X,Y)
  proof
    let F be set;
    assume F in F1;
    then ex f be Function of X,Y st f=F & P[f,X,Y];
    hence thesis by A1,FUNCT_2:11;
  end;
  then Funcs(X,Y) = F1 by A4,XBOOLE_0:def 10;
  hence thesis by A5,A8,A7,XBOOLE_0:def 10;
end;

theorem Th3:
  for X,Y,x,y st not x in X & y in Y holds card Funcs(X,Y) = card{F
  where F is Function of X\/{x},Y:F.x=y}
proof
  let X,Y,x,y such that
A1: not x in X and
A2: y in Y;
  set F2={F where F is Function of X\/{x},Y:F.x=y};
  set F1={F where F is Function of X\/{x},Y\/{y}:rng (F|X) c=Y & F.x=y};
  {y} c= Y by A2,ZFMISC_1:37;
  then
A3: Y=Y\/{y} by XBOOLE_1:12;
A4: F2 c= F1
  proof
    let f be set;
    assume f in F2;
    then consider F be Function of X\/{x},Y such that
A5: f=F & F.x=y;
    rng (F|X) c=Y;
    hence thesis by A3,A5;
  end;
  F1 c= F2
  proof
    let f be set;
    assume f in F1;
    then ex F be Function of X\/{x},Y\/{y} st f=F & rng (F|X) c=Y & F.x=y;
    hence thesis by A3;
  end;
  then F1=F2 by A4,XBOOLE_0:def 10;
  hence thesis by A1,A2,Th2;
end;

:: card Funcs(X,Y)

theorem Th4:
  (Y={} implies X={}) implies card Funcs(X,Y) = card Y |^ card X
proof
  assume
A1: Y={} implies X={};
  per cases;
  suppose
A2: Y is empty;
    then card Funcs(X,Y)=1 by A1,ALTCAT_1:3,CARD_1:50;
    hence thesis by A1,A2,CARD_1:47,NEWTON:9;
  end;
  suppose
A3: Y is non empty;
    defpred P[Nat] means for X,Y st Y is non empty & card X= $1
    holds card Funcs(X,Y)=card Y |^ card X;
A4: for n st P[n] holds P[n+1]
    proof
      defpred Q[set] means 1=1;
      let n such that
A5:   P[n];
      let X,Y such that
A6:   Y is non empty and
A7:   card X=n+1;
      reconsider nn=n as Element of NAT by ORDINAL1:def 13;
      reconsider cY=card Y |^ nn as Element of NAT;
      card Y,Y are_equipotent by CARD_1:def 5;
      then consider f be Function such that
A8:   f is one-to-one and
A9:   dom f= card Y and
A10:  rng f= Y by WELLORD2:def 4;
      reconsider f as Function of card Y,Y by A9,A10,FUNCT_2:3;
      consider x such that
A11:  x in X by A7,CARD_1:47,XBOOLE_0:def 1;
A12:  f is onto one-to-one by A8,A10,FUNCT_2:def 3;
      consider F be XFinSequence of NAT such that
A13:  dom F = card Y and
A14:  card{g where g is Function of X,Y:Q[g]} = Sum(F) and
A15:  for k st k in dom F holds F.k = card{g where g is Function of X
      ,Y:Q[g] & g.x=f.k} from STIRL2_1:sch 6(A12,A6,A11);
A16:  for k be Nat st k in dom F holds F.k=cY
      proof
        set Xx=X\{x};
        let k be Nat such that
A17:    k in dom F;
A18:    f.k in rng f by A9,A13,A17,FUNCT_1:def 5;
        set F3={g where g is Function of X,Y:Q[g] & g.x=f.k};
        set F2={g where g is Function of Xx\/{x},Y: g.x=f.k};
A19:    F3 c= F2
        proof
          let G be set;
          assume G in F3;
          then
A20:      ex g be Function of X,Y st g=G & Q[g] & g.x=f.k;
          Xx\/{x}=X by A11,ZFMISC_1:140;
          hence thesis by A20;
        end;
        F2 c=F3
        proof
          let G be set;
          assume G in F2;
          then
A21:      ex g be Function of Xx\/{x},Y st g=G & g.x=f.k;
          Xx\/{x}=X by A11,ZFMISC_1:140;
          hence thesis by A21;
        end;
        then
A22:    F2=F3 by A19,XBOOLE_0:def 10;
        card Xx=n by A7,A11,STIRL2_1:65;
        then
A23:    card Funcs(Xx,Y)=cY by A5,A18;
        x in {x} by TARSKI:def 1;
        then not x in Xx by XBOOLE_0:def 5;
        then card Funcs(Xx,Y)=card F2 by A18,Th3;
        hence thesis by A15,A17,A22,A23;
      end;
      then for k be Nat st k in dom F holds F.k >= cY;
      then
A24:  Sum F >= len F*card Y|^n by AFINSQ_2:72;
      set F1={g where g is Function of X,Y:Q[g]};
A25:  Funcs(X,Y) c= F1
      proof
        let G be set;
        assume G in Funcs(X,Y);
        then G is Function of X,Y by FUNCT_2:121;
        hence thesis;
      end;
      F1 c= Funcs(X,Y)
      proof
        let G be set;
        assume G in F1;
        then ex g be Function of X,Y st g=G & Q[g];
        hence thesis by A6,FUNCT_2:11;
      end;
      then
A26:  Funcs(X,Y) = F1 by A25,XBOOLE_0:def 10;
      for k be Nat st k in dom F holds F.k <= cY by A16;
      then Sum F<=len F*card Y|^n by AFINSQ_2:71;
      then Sum F = card Y * card Y |^ n by A13,A24,XXREAL_0:1;
      hence thesis by A7,A14,A26,NEWTON:11;
    end;
A27: P[0]
    proof
      let X,Y such that
      Y is non empty and
A28:  card X=0;
      X is empty by A28;
      then Funcs(X,Y) = {{}} by FUNCT_5:64;
      then card Funcs(X,Y)=1 by CARD_1:50;
      hence thesis by A28,NEWTON:9;
    end;
    for n holds P[n] from NAT_1:sch 2(A27,A4);
    hence thesis by A3;
  end;
end;

theorem Th5:
  for X,Y,x,y st (Y is empty implies X is empty) & not x in X & not
y in Y holds card{F where F is Function of X,Y:F is one-to-one}= card{F where F
  is Function of X\/{x},Y\/{y}:F is one-to-one & F.x=y}
proof
  let X,Y,x,y such that
A1: Y is empty implies X is empty and
A2: not x in X and
A3: not y in Y;
  defpred P[Function,set,set] means $1 is one-to-one & rng ($1|X) c=Y;
A4: for f be Function of X\/{x},Y\/{y} st f.x=y holds P[f,X\/{x},Y\/{y}] iff
  P[f|X,X,Y]
  proof
    let f be Function of X\/{x},Y\/{y} such that
A5: f.x=y;
    thus P[f,X\/{x},Y\/{y}] implies P[f|X,X,Y] by FUNCT_1:84,RELAT_1:101;
    thus P[f|X,X,Y] implies P[f,X\/{x},Y\/{y}]
    proof
      (X\/{x})/\X=X & dom f = X\/{x} by FUNCT_2:def 1,XBOOLE_1:21;
      then
A6:   dom (f|X)=X by RELAT_1:90;
      assume that
A7:   f|X is one-to-one and
A8:   rng (f|X|X) c=Y;
      rng (f|X) c= Y by A8,RELAT_1:101;
      then f|X is Function of X,Y by A6,FUNCT_2:4;
      hence thesis by A1,A3,A5,A7,A8,RELAT_1:101,STIRL2_1:68;
    end;
  end;
  set F3={F where F is Function of X\/{x},Y\/{y}: F is one-to-one & F.x=y};
A9: F3 c= {f where f is Function of (X\/{x}),(Y\/{y}): P[f,X\/{x},Y\/{y}] &
  rng (f|X) c=Y & f.x=y}
  proof
    let z;
    assume z in F3;
    then consider F be Function of X\/{x},Y\/{y} such that
A10: z=F and
A11: F is one-to-one & F.x=y;
    rng (F|X) c=Y
    proof
A12:  dom F=X\/{x} by FUNCT_2:def 1;
      x in {x} by TARSKI:def 1;
      then
A13:  x in dom F by A12,XBOOLE_0:def 3;
      assume not rng (F|X) c=Y;
      then consider fz be set such that
A14:  fz in rng (F|X) and
A15:  not fz in Y by TARSKI:def 3;
      consider z such that
A16:  z in dom (F|X) and
A17:  fz=(F|X).z by A14,FUNCT_1:def 5;
A18:  fz in Y or fz in {y} by A14,XBOOLE_0:def 3;
      dom (F|X)=dom F/\X by RELAT_1:90;
      then
A19:  z in X by A16,XBOOLE_0:def 4;
      F.z=(F|X).z by A16,FUNCT_1:70;
      then y=F.z by A15,A17,A18,TARSKI:def 1;
      hence thesis by A2,A11,A16,A12,A13,A19,FUNCT_1:def 8;
    end;
    hence thesis by A10,A11;
  end;
A20: {f where f is Function of (X\/{x}),(Y\/{y}): P[f,X\/{x},Y\/{y}]&rng (f|
  X) c=Y &f.x=y} c= F3
  proof
    let z;
    assume z in {f where f is Function of (X\/{x}),(Y\/{y}): P[f,X\/{x},Y\/{
    y}] & rng (f|X) c=Y & f.x=y};
    then ex f be Function of (X\/{x}),(Y\/{y}) st z=f & P[f,X\/{x},Y\/{y}] &
    rng (f|X) c=Y & f.x=y;
    hence thesis;
  end;
  set F2={f where f is Function of X,Y:f is one-to-one & rng (f|X) c= Y};
  set F1={F where F is Function of X,Y:F is one-to-one};
A21: F1 c=F2
  proof
    let z;
    assume z in F1;
    then consider F be Function of X,Y such that
A22: z=F & F is one-to-one;
    rng (F|X) c= rng F by RELAT_1:99;
    hence thesis by A22;
  end;
A23: not x in X by A2;
A24: card{f where f is Function of X,Y:P[f,X,Y]}= card{f where f is Function
of (X\/{x}),(Y\/{y}): P[f,X\/{x},Y\/{y}]&rng (f|X) c=Y & f.x=y} from
STIRL2_1:
  sch 4(A1,A23,A4);
  F2 c=F1
  proof
    let z;
    assume z in F2;
    then ex f be Function of X,Y st z=f & f is one-to-one & rng (f|X)c= Y;
    hence thesis;
  end;
  then F2=F1 by A21,XBOOLE_0:def 10;
  hence thesis by A9,A20,A24,XBOOLE_0:def 10;
end;

theorem
  n!/((n-'k)!) is Element of NAT
proof
n in NAT & k in NAT by ORDINAL1:def 13;
then n!/((n-'k)!) is integer by IRRAT_1:37,NAT_D:35;
  hence thesis by INT_1:16;
end;

:: one-to-one

theorem Th7:
  card X <= card Y implies card {F where F is Function of X,Y:F is
  one-to-one} = card Y!/((card Y-'card X)!)
proof
  defpred P[Nat] means for X,Y st card Y=$1 & card X <= card Y
  holds card {F where F is Function of X,Y:F is one-to-one}= card Y!/((card Y-'
  card X)!);
A1: for n st P[n] holds P[n+1]
  proof
    let n such that
A2: P[n];
    let X,Y such that
A3: card Y=n+1 and
A4: card X <= card Y;
    per cases;
    suppose
A5:   X is empty;
      set F1={F where F is Function of X,Y:F is one-to-one};
A6:   F1 c= {{}}
      proof
        let x;
        assume x in F1;
        then ex F be Function of X,Y st x=F & F is one-to-one;
        then x={} by A5;
        hence thesis by TARSKI:def 1;
      end;
A7:   rng {} c=Y by XBOOLE_1:2;
      card Y-card X=card Y by A5,CARD_1:47;
      then
A8:   (card Y-'card X)!=card Y! by XREAL_0:def 2;
      card Y!>0 by NEWTON:23;
      then
A9:   card Y! /((card Y-'card X)!)=1 by A8,XCMPLX_1:60;
      dom {}=X by A5;
      then {} is Function of X,Y by A7,FUNCT_2:4;
      then {} in F1;
      then F1 = {{}} by A6,ZFMISC_1:39;
      hence thesis by A9,CARD_1:50;
    end;
    suppose
      X is non empty;
      then consider x such that
A10:  x in X by XBOOLE_0:def 1;
      defpred F[Function] means $1 is one-to-one;
      card Y,Y are_equipotent by CARD_1:def 5;
      then consider f be Function such that
A12:  f is one-to-one and
A13:  dom f= card Y and
A14:  rng f= Y by WELLORD2:def 4;
      reconsider f as Function of card Y,Y by A13,A14,FUNCT_2:3;
A15:  Y is non empty by A3;
A16:  f is onto one-to-one by A12,A14,FUNCT_2:def 3;
      consider F be XFinSequence of NAT such that
A17:  dom F = card Y and
A18:  card{g where g is Function of X,Y:F[g]} = Sum(F) and
A19:  for k st k in dom F holds F.k = card{g where g is Function of X
      ,Y:F[g] & g.x=f.k} from STIRL2_1:sch 6(A16,A15,A10);
A20:  for k be Nat st k in dom F holds F.k= n!/((card Y-'card X)!)
      proof
        card X >0 by A10;
        then reconsider cX1=card X-1 as Element of NAT by NAT_1:20;
        set Xx=X\{x};
        x in {x} by TARSKI:def 1;
        then
A21:    not x in Xx by XBOOLE_0:def 5;
A22:    X=Xx\/{x} by A10,ZFMISC_1:140;
A23:    (cX1+1)-1<=(n+1)-1 by A3,A4,XREAL_1:11;
        then
A24:    n-cX1>=cX1-cX1 by XREAL_1:11;
        let k be Nat such that
A25:    k in dom F;
A26:    f.k in Y by A13,A14,A17,A25,FUNCT_1:def 5;
        set Yy=Y\{f.k};
A27:    Y=Yy\/{f.k} by A26,ZFMISC_1:140;
        f.k in {f.k} by TARSKI:def 1;
        then
A28:    not f.k in Yy by XBOOLE_0:def 5;
        cX1+1 <=n+1 by A3,A4;
        then
A29:    card Xx =cX1 by A10,STIRL2_1:65;
A30:    card Yy =n by A3,A26,STIRL2_1:65;
        then
A31:    Yy is empty implies Xx is empty by A23,A29,CARD_1:47;
A32:    card {g where g is Function of Xx,Yy:g is one-to-one}= n!/((card
        Yy-' card Xx)!) by A2,A23,A29,A30;
        card Y-card X >= card X -card X by A4,XREAL_1:11;
        then
card Y-'card X=((card Yy+1)-1)-((card Xx+1)-1) by A3,A29,A30,XREAL_0:def 2
          .=card Yy-'card Xx by A29,A30,A24,XREAL_0:def 2;
        then
        card{g where g is Function of X,Y:g is one-to-one & g.x=f.k} = n!
        /((card Y-'card X)!) by A32,A27,A22,A28,A21,A31,Th5;
        hence thesis by A19,A25;
      end;
      then for k be Nat st k in dom F holds F.k >= n!/((card Y-'card X)!);
      then
A33:  Sum F>=len F*(n!/((card Y-'card X)!)) by AFINSQ_2:72;
      for k be Nat st k in dom F holds F.k <= n!/((card Y-'card X)!) by A20;
      then Sum F<=len F*(n!/((card Y-'card X)!)) by AFINSQ_2:71;
      then Sum F = (n+1) *((n!/((card Y-'card X)!))) by A3,A17,A33,XXREAL_0:1
        .=((n+1)*(n!))/((card Y-'card X)!) by XCMPLX_1:75
        .=card Y!/((card Y-'card X)!) by A3,NEWTON:21;
      hence thesis by A18;
    end;
  end;
A34: P[0]
  proof
    let X,Y such that
A35: card Y=0 and
A36: card X <= card Y;
    card Y-card X=0 by A35,A36;
    then
A37: (card Y-'card X)!=1 by NEWTON:18,XREAL_0:def 2;
    set F1={F where F is Function of X,Y:F is one-to-one};
A38: F1 c= {{}}
    proof
      let x;
      assume x in F1;
      then
A39:  ex F be Function of X,Y st x=F & F is one-to-one;
      Y={} by A35;
      then x={} by A39;
      hence thesis by TARSKI:def 1;
    end;
    dom {}=X & rng {}=Y by A35,A36;
    then {} is Function of X,Y by FUNCT_2:3;
    then {} in F1;
    then F1 = {{}} by A38,ZFMISC_1:39;
    hence thesis by A35,A37,CARD_1:50,NEWTON:18;
  end;
  for n holds P[n] from NAT_1:sch 2(A34,A1);
  hence thesis;
end;

:: Permutation of X

theorem Th8:
  card{F where F is Function of X,X:F is Permutation of X}=card X!
proof
  set F1={F where F is Function of X,X:F is one-to-one};
  set F2={F where F is Function of X,X:F is Permutation of X};
A1: F1 c= F2
  proof
    let x;
    assume x in F1;
    then consider F be Function of X,X such that
A2: x=F and
A3: F is one-to-one;
    card X=card X;
    then F is onto by A3,STIRL2_1:70;
    hence thesis by A2,A3;
  end;
  (card X-'card X)!=1 by NEWTON:18,XREAL_1:234;
  then
A4: card X!/((card X-'card X)!)=card X!;
  F2 c= F1
  proof
    let x;
    assume x in F2;
    then ex F be Function of X,X st x=F & F is Permutation of X;
    hence thesis;
  end;
  then F1=F2 by A1,XBOOLE_0:def 10;
  hence thesis by A4,Th7;
end;

definition
  let X,k,x1,x2;
  func Choose(X,k,x1,x2) -> Subset of Funcs(X,{x1,x2}) means
  :Def1:
  x in it iff ex f be Function of X,{x1,x2} st f = x & card (f"{x1})=k;
  existence
  proof
    defpred P[set] means ex f be Function of X,{x1,x2} st $1=f & card (f"{x1})
    =k;
    consider F being set such that
A1: x in F iff x in bool [:X,{x1,x2}:] & P[x] from XBOOLE_0:sch 1;
    F c= Funcs(X,{x1,x2})
    proof
      let x;
      assume x in F;
      then ex f be Function of X,{x1,x2} st x=f & card (f"{x1})=k by A1;
      hence thesis by FUNCT_2:11;
    end;
    then reconsider F as Subset of Funcs(X,{x1,x2});
    take F;
    let x;
    thus x in F implies ex f be Function of X,{x1,x2} st x=f & card (f"{x1}) =
    k by A1;
    given f be Function of X,{x1,x2} such that
A2: x = f & card (f"{x1})=k;
    thus thesis by A1,A2;
  end;
  uniqueness
  proof
    let F1,F2 be Subset of Funcs(X,{x1,x2}) such that
A3: x in F1 iff ex f be Function of X,{x1,x2} st x = f & card (f"{x1}) =k and
A4: x in F2 iff ex f be Function of X,{x1,x2} st x = f & card (f"{x1}) =k;
    x in F1 iff x in F2
    proof
      x in F1 iff ex f be Function of X,{x1,x2} st x=f& card (f"{x1})=k by A3;
      hence thesis by A4;
    end;
    hence thesis by TARSKI:2;
  end;
end;

theorem
  card X<>k implies Choose(X,k,x1,x1) is empty
proof
  assume
A1: card X<>k;
  assume Choose(X,k,x1,x1) is non empty;
  then consider y such that
A2: y in Choose(X,k,x1,x1) by XBOOLE_0:def 1;
  consider f be Function of X,{x1,x1} such that
  f = y and
A3: card (f"{x1}) = k by A2,Def1;
  per cases;
  suppose
A4: rng f is empty;
A5: dom f=X by FUNCT_2:def 1;
    dom f={} by A4,RELAT_1:65;
    hence thesis by A1,A3,A5;
  end;
  suppose
A6: rng f is non empty;
    {x1,x1}={x1} by ENUMSET1:69;
    then rng f={x1} by A6,ZFMISC_1:39;
    then f"{x1}=dom f by RELAT_1:169;
    hence thesis by A1,A3,FUNCT_2:def 1;
  end;
end;

theorem Th10:
  card X < k implies Choose(X,k,x1,x2) is empty
proof
  assume
A1: card X < k;
  assume Choose(X,k,x1,x2) is non empty;
  then consider z such that
A2: z in Choose(X,k,x1,x2) by XBOOLE_0:def 1;
  ex f be Function of X,{x1,x2} st f = z & card (f"{x1}) = k by A2,Def1;
  hence thesis by A1,NAT_1:44;
end;

theorem Th11:
  x1<>x2 implies card Choose(X,0,x1,x2)=1
proof
  assume
A1: x1<>x2;
  per cases;
  suppose
A2: X is empty;
A3: rng {} ={};
    dom {}=X by A2;
    then reconsider Empty={} as Function of X,{x1,x2} by A3,FUNCT_2:4
,XBOOLE_1:2;
A4: Choose(X,0,x1,x2) c= {Empty}
    proof
      let z;
      assume z in Choose(X,0,x1,x2);
      then consider f be Function of X,{x1,x2} such that
A5:   z=f and
      card (f"{x1})=0 by Def1;
      dom f=X by FUNCT_2:def 1;
      then f=Empty;
      hence thesis by A5,TARSKI:def 1;
    end;
    Empty"{x1}={} & card {}=0 by FUNCT_1:142,RELAT_1:60;
    then Empty in Choose(X,0,x1,x2) by Def1;
    then Choose(X,0,x1,x2)={Empty} by A4,ZFMISC_1:39;
    hence thesis by CARD_1:50;
  end;
  suppose
A6: X is non empty;
    then consider f be Function such that
A7: dom f=X and
A8: rng f={x2} by FUNCT_1:15;
    rng f c= {x1,x2} by A8,ZFMISC_1:42;
    then
A9: f is Function of X,{x1,x2} by A7,FUNCT_2:4;
A10: Choose(X,0,x1,x2) c= {f}
    proof
      let z;
      assume z in Choose(X,0,x1,x2);
      then consider g be Function of X,{x1,x2} such that
A11:  z=g and
A12:  card (g"{x1})=0 by Def1;
      g"{x1}={} by A12;
      then not x1 in rng g by FUNCT_1:142;
      then ( not rng g={x1})& not rng g={x1,x2} by TARSKI:def 1,def 2;
      then dom g=X & rng g={x2} by A6,FUNCT_2:def 1,ZFMISC_1:42;
      then g=f by A7,A8,FUNCT_1:17;
      hence thesis by A11,TARSKI:def 1;
    end;
    not x1 in rng f by A1,A8,TARSKI:def 1;
    then
A13: f"{x1}={} by FUNCT_1:142;
    card {}=0;
    then f in Choose(X,0,x1,x2) by A13,A9,Def1;
    then Choose(X,0,x1,x2)={f} by A10,ZFMISC_1:39;
    hence thesis by CARD_1:50;
  end;
end;

theorem Th12:
  card Choose(X,card X,x1,x2)=1
proof
  per cases;
  suppose
A1: X is empty;
A2: rng {} ={};
    dom {}=X by A1;
    then reconsider Empty={} as Function of X,{x1,x2} by A2,FUNCT_2:4
,XBOOLE_1:2;
A3: Choose(X,card X,x1,x2) c= {Empty}
    proof
      let z;
      assume z in Choose(X,card X,x1,x2);
      then consider f be Function of X,{x1,x2} such that
A4:   z=f and
      card (f"{x1})=card X by Def1;
      dom f=X by FUNCT_2:def 1;
      then f=Empty;
      hence thesis by A4,TARSKI:def 1;
    end;
    Empty"{x1}={} by FUNCT_1:142,RELAT_1:60;
    then Empty in Choose(X,card X,x1,x2) by A1,Def1;
    then Choose(X,card X,x1,x2)={Empty} by A3,ZFMISC_1:39;
    hence thesis by CARD_1:50;
  end;
  suppose
A5: X is non empty;
    then consider f be Function such that
A6: dom f=X and
A7: rng f={x1} by FUNCT_1:15;
    rng f c= {x1,x2} by A7,ZFMISC_1:42;
    then
A8: f is Function of X,{x1,x2} by A6,FUNCT_2:4;
A9: Choose(X,card X,x1,x2) c= {f}
    proof
      let z;
      assume z in Choose(X,card X,x1,x2);
      then consider g be Function of X,{x1,x2} such that
A10:  z=g and
A11:  card (g"{x1})=card X by Def1;
A12:  now
        per cases;
        suppose
          x1=x2;
          then {x1,x2}={x1} by ENUMSET1:69;
          hence rng g={x1} by A5,ZFMISC_1:39;
        end;
        suppose
A13:      x1<>x2;
          g"{x2}={}
          proof
            assume g"{x2}<>{};
            then consider z such that
A14:        z in g"{x2} by XBOOLE_0:def 1;
            g.z in {x2} by A14,FUNCT_1:def 13;
            then
A15:        g.z=x2 by TARSKI:def 1;
            g"{x1}=X by A11,Th1;
            then g.z in {x1} by A14,FUNCT_1:def 13;
            hence thesis by A13,A15,TARSKI:def 1;
          end;
          then not x2 in rng g by FUNCT_1:142;
          then ( not rng g={x2})& not rng g={x1,x2} by TARSKI:def 1,def 2;
          hence rng g={x1} by A5,ZFMISC_1:42;
        end;
      end;
      dom g=X by FUNCT_2:def 1;
      then g=f by A6,A7,A12,FUNCT_1:17;
      hence thesis by A10,TARSKI:def 1;
    end;
    card (f"{x1})=card X by A6,A7,RELAT_1:169;
    then f in Choose(X,card X,x1,x2) by A8,Def1;
    then Choose(X,card X,x1,x2)={f} by A9,ZFMISC_1:39;
    hence thesis by CARD_1:50;
  end;
end;

canceled;

theorem Th14:
  not z in X implies card Choose(X,k,x,y)= card{f where f is
  Function of X\/{z},{x,y}:card (f"{x})=k+1 & f.z=x}
proof
  set F1={f where f is Function of X\/{z},{x,y}:card (f"{x})=k+1 & f.z=x};
  defpred P[set,set,set] means for f be Function st f=$1 holds card ((f|X)"{x}
  )=k;
A1: for f be Function of X\/{z},{x,y}\/{x} st f.z=x holds P[f,X\/{z},{x,y}\/
  {x}] iff P[f|X,X,{x,y}]
  proof
    let f be Function of X\/{z},{x,y}\/{x} such that
    f.z=x;
    (f|X)=((f|X)|X) by RELAT_1:102;
    hence thesis;
  end;
  set F3={f where f is Function of X\/{z},{x,y}\/{x}: P[f,X\/{z},{x,y}\/{x}] &
  rng (f|X) c={x,y} & f.z=x};
  set F2={f where f is Function of X,{x,y}:P[f,X,{x,y}]};
  assume
A2: not z in X;
A3: F3 c= F1
  proof
    {x}\/{x,y}={x,x,y} by ENUMSET1:42;
    then
A4: {x,y}\/{x}={x,y} by ENUMSET1:70;
    z in {z} by TARSKI:def 1;
    then
A5: z in X\/{z} by XBOOLE_0:def 3;
    let x1;
    assume x1 in F3;
    then consider f be Function of X\/{z},{x,y}\/{x} such that
A6: x1=f and
A7: P[f,X\/{z},{x,y}\/{x}] and
    rng (f|X) c={x,y} and
A8: f.z=x;
    dom f=X\/{z} & (X\/{z})\{z}=X by A2,FUNCT_2:def 1,ZFMISC_1:141;
    then
A9: {z}\/(f|X)"{x}=f"{x} by A8,A5,AFINSQ_2:78;
    not z in dom f/\X by A2,XBOOLE_0:def 4;
    then not z in dom (f|X) by RELAT_1:90;
    then
A10: not z in (f|X)"{x} by FUNCT_1:def 13;
    card ((f|X)"{x})=k by A7;
    then card (f"{x})=k+1 by A9,A10,CARD_2:54;
    hence thesis by A6,A8,A4;
  end;
A11: F2 c=Choose(X,k,x,y)
  proof
    let x1;
    assume x1 in F2;
    then consider f be Function of X,{x,y} such that
A12: x1=f and
A13: P[f,X,{x,y}];
    dom f=X by FUNCT_2:def 1;
    then f|X=f by RELAT_1:97;
    then card (f"{x})=k by A13;
    hence thesis by A12,Def1;
  end;
A14: Choose(X,k,x,y)c= F2
  proof
    let x1;
    assume x1 in Choose(X,k,x,y);
    then consider f be Function of X,{x,y} such that
A15: x1=f and
A16: card (f"{x})=k by Def1;
    dom f=X by FUNCT_2:def 1;
    then P[f,X,{x,y}] by A16,RELAT_1:97;
    hence thesis by A15;
  end;
A17: {x,y} is empty implies X is empty;
A18: card F2=card F3 from STIRL2_1:sch 4(A17,A2,A1);
  F1 c= F3
  proof
    z in {z} by TARSKI:def 1;
    then
A19: z in X\/{z} by XBOOLE_0:def 3;
    let x1;
    assume x1 in F1;
    then consider f be Function of X\/{z},{x,y} such that
A20: x1=f and
A21: card (f"{x})=k+1 and
A22: f.z=x;
    not z in dom f/\X by A2,XBOOLE_0:def 4;
    then not z in dom (f|X) by RELAT_1:90;
    then
A23: not z in ((f|X)"{x}) by FUNCT_1:def 13;
    dom f=X\/{z} & (X\/{z})\{z}=X by A2,FUNCT_2:def 1,ZFMISC_1:141;
    then ((f|X)"{x})\/{z}=f"{x} by A22,A19,AFINSQ_2:78;
    then card ((f|X)"{x})+1=k+1 by A21,A23,CARD_2:54;
    then
A24: P[f,X\/{z},{x,y}\/{x}];
    {x}\/{x,y}={x,x,y} by ENUMSET1:42;
    then rng (f|X) c={x,y} & f is Function of X\/{z},{x,y}\/{x} by ENUMSET1:70;
    hence thesis by A20,A22,A24;
  end;
  then F1=F3 by A3,XBOOLE_0:def 10;
  hence thesis by A11,A14,A18,XBOOLE_0:def 10;
end;

canceled;

theorem Th16:
  not z in X & x<>y implies card Choose(X,k,x,y)= card{f where f
  is Function of X\/{z},{x,y}:card (f"{x})=k & f.z=y}
proof
  assume that
A1: not z in X and
A2: x<>y;
  defpred P[set,set,set] means for f be Function st f=$1 holds card (f"{x})=k;
A3: for f be Function of X\/{z},{x,y}\/{y} st f.z=y holds P[f,X\/{z},{x,y}\/
  {y}] iff P[f|X,X,{x,y}]
  proof
    let f be Function of X\/{z},{x,y}\/{y} such that
A4: f.z=y;
    (X\/{z})\{z}=X & dom f=X\/{z} by A1,FUNCT_2:def 1,ZFMISC_1:141;
    then (f|X)"{x} = f"{x} by A2,A4,AFINSQ_2:79;
    hence thesis;
  end;
  set F2={f where f is Function of X\/{z},{x,y}\/{y}: P[f,X\/{z},{x,y}\/{y}] &
  rng (f|X) c={x,y} & f.z=y};
  set F1={f where f is Function of X,{x,y}:P[f,X,{x,y}]};
A5: {x,y} is empty implies X is empty;
A6: card F1=card F2 from STIRL2_1:sch 4(A5,A1,A3);
  set F3={f where f is Function of X\/{z},{x,y}:card (f"{x})=k & f.z=y};
A7: F2 c= F3
  proof
    let x1;
    assume x1 in F2;
    then consider f be Function of X\/{z},{x,y}\/{y} such that
A8: x1=f and
A9: P[f,X\/{z},{x,y}\/{y}] and
    rng (f|X) c={x,y} and
A10: f.z=y;
    {x,y}\/{y}={y,y,x} by ENUMSET1:42;
    then
A11: f is Function of X\/{z},{x,y} by ENUMSET1:70;
    card (f"{x})=k by A9;
    hence thesis by A8,A10,A11;
  end;
A12: F3 c= F2
  proof
    let x1;
    assume x1 in F3;
    then consider f be Function of X\/{z},{x,y}such that
A13: f=x1 and
A14: card (f"{x})=k and
A15: f.z=y;
    {x,y}\/{y}={y,y,x} by ENUMSET1:42;
    then
A16: rng (f|X) c={x,y} & f is Function of X\/{z},{x,y}\/{y} by ENUMSET1:70;
    P[f,X\/{z},{x,y}\/{y}] by A14;
    hence thesis by A13,A15,A16;
  end;
A17: Choose(X,k,x,y) c= F1
  proof
    let x1;
    assume x1 in Choose(X,k,x,y);
    then consider f be Function of X,{x,y} such that
A18: x1=f and
A19: card (f"{x})=k by Def1;
    P[f,X,{x,y}] by A19;
    hence thesis by A18;
  end;
  F1 c= Choose(X,k,x,y)
  proof
    let x1;
    assume x1 in F1;
    then consider f be Function of X,{x,y} such that
A20: x1=f and
A21: P[f,X,{x,y}];
    card (f"{x})=k by A21;
    hence thesis by A20,Def1;
  end;
  then Choose(X,k,x,y) = F1 by A17,XBOOLE_0:def 10;
  hence thesis by A7,A12,A6,XBOOLE_0:def 10;
end;

Lm1: x<>y implies {f where f is Function of X\/{z},{x,y}: card (f"{x})=k+1 & f
.z=x}\/{f where f is Function of X\/{z},{x,y}: card (f"{x})=k+1 & f.z=y}=Choose
(X\/{z},k+1,x,y) & {f where f is Function of X\/{z},{x,y}: card (f"{x})=k+1 & f
.z=x} misses {f where f is Function of X\/{z},{x,y}: card (f"{x})=k+1 & f.z=y}
proof
  set F1={f where f is Function of X\/{z},{x,y}:card (f"{x})=k+1 & f.z=x};
  set F2={f where f is Function of X\/{z},{x,y}:card (f"{x})=k+1 & f.z=y};
  assume
A1: x<>y;
A2: F1 misses F2
  proof
    assume F1 meets F2;
    then F1/\F2<>{} by XBOOLE_0:def 7;
    then consider x1 such that
A3: x1 in F1/\F2 by XBOOLE_0:def 1;
    x1 in F2 by A3,XBOOLE_0:def 4;
    then
A4: ex f2 be Function of X\/{z},{x,y} st x1=f2 & card (f2"{x })=k+1 & f2.z =y;
    x1 in F1 by A3,XBOOLE_0:def 4;
    then
    ex f1 be Function of X\/{z},{x,y} st x1=f1 & card (f1"{x })=k+1 & f1.z =x;
    hence thesis by A1,A4;
  end;
A5: F2 c=Choose(X\/{z},k+1,x,y)
  proof
    let x1;
    assume x1 in F2;
    then
    ex f be Function of X\/{z},{x,y} st x1=f & card (f"{x})=k+1 & f.z=y;
    hence thesis by Def1;
  end;
A6: Choose(X\/{z},k+1,x,y) c= F1\/F2
  proof
    let x1;
    assume x1 in Choose(X\/{z},k+1,x,y);
    then consider f be Function of X\/{z},{x,y} such that
A7: f=x1 & card (f"{x})=k+1 by Def1;
    z in {z} by TARSKI:def 1;
    then
A8: z in X\/{z} by XBOOLE_0:def 3;
    dom f=X\/{z} by FUNCT_2:def 1;
    then f.z in rng f by A8,FUNCT_1:def 5;
    then f.z=x or f.z=y by TARSKI:def 2;
    then x1 in F1 or x1 in F2 by A7;
    hence thesis by XBOOLE_0:def 3;
  end;
  F1 c=Choose(X\/{z},k+1,x,y)
  proof
    let x1;
    assume x1 in F1; then
    ex f be Function of X\/{z},{x,y} st x1=f & card (f"{x})=k+1 & f.z=x;
    hence thesis by Def1;
  end;
  then F1\/F2 c= Choose(X\/{z},k+1,x,y) by A5,XBOOLE_1:8;
  hence thesis by A6,A2,XBOOLE_0:def 10;
end;

theorem Th17:
  x<>y & not z in X implies card Choose(X\/{z},k+1,x,y)= card
  Choose(X,k+1,x,y)+ card Choose(X,k,x,y)
proof
  assume that
A1: x<>y and
A2: not z in X;
  set F2={f where f is Function of X\/{z},{x,y}:card (f"{x})=k+1 & f.z=y};
  set F1={f where f is Function of X\/{z},{x,y}:card (f"{x})=k+1 & f.z=x};
A3: F1 \/F2= Choose(X\/{z},k+1,x,y) by A1,Lm1;
  F1 c= F1\/F2 & F2 c= F1\/F2 by XBOOLE_1:7;
  then reconsider F1,F2 as finite set by A3;
A4: card F1=card Choose(X,k,x,y) by A2,Th14;
  card (F1 \/ F2) = card F1 + card F2 & card F2=card Choose(X,k+1,x,y) by A1,A2
,Lm1,Th16,CARD_2:53;
  hence thesis by A1,A4,Lm1;
end;

:: n choose k

theorem Th18:
  x<>y implies card Choose(X,k,x,y)=card X choose k
proof
  defpred P[Nat] means for k,X st k+ card X <= $1 holds card Choose
  (X,k,x,y)=(card X) choose k;
  assume
A1: x<>y;
A2: for n st P[n] holds P[n+1]
  proof
    let n such that
A3: P[n];
    let k,X such that
A4: k+ card X <= n+1;
    per cases by A4,XXREAL_0:1;
    suppose
      k+ card X < n+1;
      then k+card X<=n by NAT_1:13;
      hence thesis by A3;
    end;
    suppose
A5:   k+ card X = n+1;
      per cases;
      suppose
A6:     k=0 & card X>=0;
        then card Choose(X,k,x,y)=1 by A1,Th11;
        hence thesis by A6,NEWTON:29;
      end;
      suppose
A7:     k>0 & card X=0;
        then Choose(X,k,x,y) is empty by Th10;
        hence thesis by A7,NEWTON:def 3;
      end;
      suppose
A8:     k>0 & card X>0;
        then reconsider cXz=(card X)-1 as Element of NAT by NAT_1:20;
        reconsider k1=k-1 as Element of NAT by A8,NAT_1:20;
        consider z such that
A9:     z in X by A8,CARD_1:47,XBOOLE_0:def 1;
        set Xz=X\{z};
        z in {z} by TARSKI:def 1;
        then
A10:    not z in Xz by XBOOLE_0:def 5;
        Xz\/{z}=X by A9,ZFMISC_1:140;
        then
A11:    card Choose(X,k1+1,x,y)= card Choose(Xz,k1+1,x,y)+ card Choose(Xz
        ,k1,x, y) by A1,A10,Th17;
        card X=cXz+1;
        then
A12:    card Xz=cXz by A9,STIRL2_1:65;
        cXz< cXz+1 by NAT_1:13;
        then
A13:    card Xz < card X by A9,STIRL2_1:65;
        then k+card Xz<n+1 by A5,XREAL_1:10;
        then k+card Xz<=n by NAT_1:13;
        then
A14:    card Choose(Xz,k1+1,x,y)=(card Xz) choose (k1+1) by A3;
        k1<k1+1 by NAT_1:13;
        then k1+card Xz<n+1 by A5,A13,XREAL_1:10;
        then k1+card Xz<=n by NAT_1:13;
        then
A15:    card Choose(Xz,k1,x,y)=(card Xz) choose k1 by A3;
        card X=cXz+1;
        hence thesis by A14,A15,A11,A12,NEWTON:32;
      end;
    end;
  end;
A16: P[0]
  proof
    let k,X;
X:  0 choose 0 = 1 by NEWTON:29;
    assume k+ card X <= 0;
    then k+ card X =0 & card X=0;
    hence thesis by A1,Th11,X;
  end;
  for n holds P[n] from NAT_1:sch 2(A16,A2);
  then P[k+card X];
  hence thesis;
end;

theorem Th19:
  x<>y implies (Y-->y)+*(X-->x) in Choose(X\/Y,card X,x,y)
proof
  set F=(Y-->y)+*(X-->x);
  dom (Y-->y)=Y & dom (X-->x)=X by FUNCOP_1:19;
  then
A1: dom F=X\/Y by FUNCT_4:def 1;
  {y} c= {x,y} by ZFMISC_1:12;
  then
A2: rng (Y-->y) c= {x,y} by XBOOLE_1:1;
  {x} c= {x,y} by ZFMISC_1:12;
  then rng (X-->x)c= {x,y} by XBOOLE_1:1;
  then rng F c= rng(X-->x)\/rng(Y-->y) & rng(X-->x)\/rng(Y-->y)c={x,y} by A2,
FUNCT_4:18,XBOOLE_1:8;
  then reconsider F as Function of X\/Y,{x,y} by A1,FUNCT_2:4,XBOOLE_1:1;
  assume
A3: x<>y;
A4: F"{x}c=X
  proof
    let z such that
A5: z in F"{x};
A6: z in X or z in Y by A5,XBOOLE_0:def 3;
    F.z in {x} by A5,FUNCT_1:def 13;
    then
A7: F.z=x by TARSKI:def 1;
    z in dom F by A5,FUNCT_1:def 13;
    then
A8: z in dom (X-->x)\/dom (Y-->y) by FUNCT_4:def 1;
    assume
A9: not z in X;
    X=dom (X-->x) by FUNCOP_1:19;
    then F.z=(Y-->y).z by A9,A8,FUNCT_4:def 1;
    hence contradiction by A3,A9,A6,A7,FUNCOP_1:13;
  end;
  X c= F"{x}
  proof
    let z such that
A10: z in X;
A11: z in dom F by A1,A10,XBOOLE_0:def 3;
    z in dom (X-->x) by A10,FUNCOP_1:19;
    then
A12: F.z=(X-->x).z by FUNCT_4:14;
    (X-->x).z=x by A10,FUNCOP_1:13;
    then F.z in {x} by A12,TARSKI:def 1;
    hence thesis by A11,FUNCT_1:def 13;
  end;
  then X=F"{x} by A4,XBOOLE_0:def 10;
  hence thesis by Def1;
end;

theorem Th20:
  x<>y & X misses Y implies (X-->x)+*(Y-->y) in Choose(X\/Y,card X ,x,y)
proof
  assume that
A1: x<>y and
A2: X misses Y;
  dom (X-->x)=X & dom (Y-->y)=Y by FUNCOP_1:19;
  then (X-->x)+*(Y-->y)=(Y-->y)+*(X-->x) by A2,FUNCT_4:36;
  hence thesis by A1,Th19;
end;

definition
  let F,Ch be Function,y be set;
  func Intersection(F,Ch,y) -> Subset of union rng F means
  :Def2:
  z in it iff z in union rng F & for x st x in dom Ch & Ch.x=y holds z in F.x;
  existence
  proof
    defpred P[set] means for x st x in dom Ch & Ch.x=y holds $1 in F.x;
    consider I be set such that
A1: z in I iff z in union rng F & P[z] from XBOOLE_0:sch 1;
    I c= union rng F
    proof
      let x;
      assume x in I;
      hence thesis by A1;
    end;
    then reconsider I as Subset of union rng F;
    take I;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let I1,I2 be Subset of union rng F such that
A2: z in I1 iff z in union rng F & for x st x in dom Ch & Ch.x=y holds
    z in F.x and
A3: z in I2 iff z in union rng F & for x st x in dom Ch & Ch.x=y holds
    z in F.x;
    z in I1 iff z in I2
    proof
      z in I1 iff z in union rng F & for x st x in dom Ch & Ch.x=y holds z
      in F.x by A2;
      hence thesis by A3;
    end;
    hence thesis by TARSKI:2;
  end;
end;

reserve F,Ch for Function;

theorem Th21:
  for F,Ch st dom F/\Ch"{x} is non empty holds y in Intersection(F
  ,Ch,x) iff for z st z in dom Ch & Ch.z=x holds y in F.z
proof
  let F,Ch such that
A1: dom F/\Ch"{x} is non empty;
  thus y in Intersection(F,Ch,x) implies for z st z in dom Ch & Ch.z=x holds y
  in F.z by Def2;
  thus (for z st z in dom Ch & Ch.z=x holds y in F.z) implies y in
  Intersection(F,Ch,x)
  proof
    consider z such that
A2: z in dom F/\Ch"{x} by A1,XBOOLE_0:def 1;
A3: z in Ch"{x} by A2,XBOOLE_0:def 4;
    then Ch.z in {x} by FUNCT_1:def 13;
    then
A4: Ch.z=x by TARSKI:def 1;
    z in dom F by A2,XBOOLE_0:def 4;
    then
A5: F.z in rng F by FUNCT_1:def 5;
    assume
A6: for z st z in dom Ch & Ch.z=x holds y in F.z;
    z in dom Ch by A3,FUNCT_1:def 13;
    then y in F.z by A6,A4;
    then y in union rng F by A5,TARSKI:def 4;
    hence thesis by A6,Def2;
  end;
end;

theorem Th22:
  Intersection(F,Ch,y) is non empty implies Ch"{y} c= dom F
proof
  assume Intersection(F,Ch,y) is non empty;
  then consider z such that
A1: z in Intersection(F,Ch,y) by XBOOLE_0:def 1;
  assume not Ch"{y} c= dom F;
  then consider x such that
A2: x in Ch"{y} and
A3: not x in dom F by TARSKI:def 3;
  Ch.x in {y} by A2,FUNCT_1:def 13;
  then
A4: Ch.x=y by TARSKI:def 1;
  x in dom Ch by A2,FUNCT_1:def 13;
  then z in F.x by A1,A4,Def2;
  hence thesis by A3,FUNCT_1:def 4;
end;

theorem
  Intersection(F,Ch,y) is non empty implies for x1,x2 st x1 in Ch"{y} &
  x2 in Ch"{y} holds F.x1 meets F.x2
proof
  assume Intersection(F,Ch,y) is non empty;
  then consider z such that
A1: z in Intersection(F,Ch,y) by XBOOLE_0:def 1;
  let x1,x2 such that
A2: x1 in Ch"{y} and
A3: x2 in Ch"{y};
  Ch.x2 in {y} by A3,FUNCT_1:def 13;
  then
A4: Ch.x2=y by TARSKI:def 1;
  Ch.x1 in {y} by A2,FUNCT_1:def 13;
  then
A5: Ch.x1=y by TARSKI:def 1;
  x2 in dom Ch by A3,FUNCT_1:def 13;
  then
A6: z in F.x2 by A1,A4,Def2;
  x1 in dom Ch by A2,FUNCT_1:def 13;
  then z in F.x1 by A1,A5,Def2;
  hence thesis by A6,XBOOLE_0:3;
end;

theorem Th24:
  z in Intersection(F,Ch,y) & y in rng Ch implies ex x st x in dom
  Ch & Ch.x=y & z in F.x
proof
  assume that
A1: z in Intersection(F,Ch,y) and
A2: y in rng Ch;
  Ch"{y} <> {} by A2,FUNCT_1:142;
  then consider x such that
A3: x in Ch"{y} by XBOOLE_0:def 1;
  Ch.x in {y} by A3,FUNCT_1:def 13;
  then
A4: Ch.x=y by TARSKI:def 1;
A5: x in dom Ch by A3,FUNCT_1:def 13;
  x in dom Ch by A3,FUNCT_1:def 13;
  then z in F.x by A1,A4,Def2;
  hence thesis by A4,A5;
end;

theorem
  F is empty or union rng F is empty implies Intersection(F,Ch,y)=union
  rng F by RELAT_1:60,ZFMISC_1:2;

theorem Th26:
  F|Ch"{y}=(Ch"{y}-->union rng F) implies Intersection(F,Ch,y) = union rng F
proof
  set ChF=Ch"{y}-->union rng F;
  assume
A1: F|Ch"{y}=ChF;
  union rng F c= Intersection(F,Ch,y)
  proof
    let z such that
A2: z in union rng F;
    now
      let x such that
A3:   x in dom Ch and
A4:   Ch.x=y;
      Ch.x in {y} by A4,TARSKI:def 1;
      then
A5:   x in Ch"{y} by A3,FUNCT_1:def 13;
      then dom ChF= Ch"{y} & ChF.x=union rng F by FUNCOP_1:13,19;
      hence z in F.x by A1,A2,A5,FUNCT_1:70;
    end;
    hence thesis by A2,Def2;
  end;
  hence thesis by XBOOLE_0:def 10;
end;

theorem
  union rng F is non empty & Intersection(F,Ch,y)=union rng F implies F|
  Ch"{y}=(Ch"{y}-->union rng F)
proof
  set ChF=Ch"{y}-->union rng F;
  assume that
A1: union rng F is non empty and
A2: Intersection(F,Ch,y) = union rng F;
A3: dom F/\Ch"{y} =dom (F|Ch"{y}) by RELAT_1:90;
  dom F/\Ch"{y}=Ch"{y} by A1,A2,Th22,XBOOLE_1:28;
  then
A4: dom (F|Ch"{y})=dom ChF by A3,FUNCOP_1:19;
  assume F|Ch"{y}<>ChF;
  then consider x such that
A5: x in dom (F|Ch"{y}) and
A6: (F|Ch"{y}).x<>ChF.x by A4,FUNCT_1:9;
  x in dom F /\ Ch"{y} by A5,RELAT_1:90;
  then
A7: x in dom F by XBOOLE_0:def 4;
  x in dom F /\ Ch"{y} by A5,RELAT_1:90;
  then
A8: x in Ch"{y} by XBOOLE_0:def 4;
  then
A9: ChF.x=union rng F by FUNCOP_1:13;
  Ch.x in {y} by A8,FUNCT_1:def 13;
  then
A10: Ch.x =y by TARSKI:def 1;
  F.x=(F|Ch"{y}).x by A5,FUNCT_1:70;
  then (F|Ch"{y}).x in rng F by A7,FUNCT_1:def 5;
  then (F|Ch"{y}).x c= ChF.x by A9,ZFMISC_1:92;
  then not union rng F c= (F|Ch"{y}).x by A6,A9,XBOOLE_0:def 10;
  then consider z such that
A11: z in union rng F and
A12: not z in (F|Ch"{y}).x by TARSKI:def 3;
  x in dom Ch by A8,FUNCT_1:def 13;
  then z in F.x by A2,A11,A10,Def2;
  hence thesis by A5,A12,FUNCT_1:70;
end;

theorem Th28:
  Intersection(F,{},y)=union rng F
proof
A1: {}"{y} c= dom {} by RELAT_1:167;
  then F|{}"{y}={};
  then F|{}"{y}=({}"{y}-->union rng F) by A1;
  hence thesis by Th26;
end;

theorem Th29:
  Intersection(F,Ch,y) c= Intersection(F,Ch|X9,y)
proof
  let z such that
A1: z in Intersection(F,Ch,y);
  now
    let x such that
A2: x in dom (Ch|X9) and
A3: Ch|X9.x=y;
    x in dom Ch/\X9 by A2,RELAT_1:90;
    then
A4: x in dom Ch by XBOOLE_0:def 4;
    Ch.x=y by A2,A3,FUNCT_1:70;
    hence z in F.x by A1,A4,Def2;
  end;
  hence thesis by A1,Def2;
end;

theorem Th30:
  Ch"{y}=(Ch|X9)"{y} implies Intersection(F,Ch,y)=Intersection(F, Ch|X9,y)
proof
  assume
A1: Ch"{y}=(Ch|X9)"{y};
A2: Intersection(F,Ch|X9,y) c=Intersection(F,Ch,y)
  proof
    let z such that
A3: z in Intersection(F,Ch|X9,y);
    now
      let x such that
A4:   x in dom Ch and
A5:   Ch.x=y;
      Ch.x in {y} by A5,TARSKI:def 1;
      then
A6:   x in (Ch|X9)"{y} by A1,A4,FUNCT_1:def 13;
      then (Ch|X9).x in {y} by FUNCT_1:def 13;
      then
A7:   (Ch|X9).x = y by TARSKI:def 1;
      x in dom (Ch|X9) by A6,FUNCT_1:def 13;
      hence z in F.x by A3,A7,Def2;
    end;
    hence thesis by A3,Def2;
  end;
  Intersection(F,Ch,y) c= Intersection(F,Ch|X9,y) by Th29;
  hence thesis by A2,XBOOLE_0:def 10;
end;

theorem Th31:
  Intersection(F|X9,Ch,y) c= Intersection(F,Ch,y)
proof
  let z such that
A1: z in Intersection(F|X9,Ch,y);
A2: now
A3: Ch"{y} c= dom (F|X9) by A1,Th22;
    let x such that
A4: x in dom Ch and
A5: Ch.x=y;
    Ch.x in {y} by A5,TARSKI:def 1;
    then
A6: x in Ch"{y} by A4,FUNCT_1:def 13;
    z in F|X9.x by A1,A4,A5,Def2;
    hence z in F.x by A6,A3,FUNCT_1:70;
  end;
  union rng (F|X9) c= union rng F & z in union rng (F|X9) by A1,RELAT_1:99
,ZFMISC_1:95;
  hence thesis by A2,Def2;
end;

theorem Th32:
  y in rng Ch & Ch"{y} c= X9 implies Intersection(F|X9,Ch,y) =
  Intersection(F,Ch,y)
proof
  assume that
A1: y in rng Ch and
A2: Ch"{y} c= X9;
A3: Intersection(F,Ch,y) c=Intersection(F|X9,Ch,y)
  proof
    let z such that
A4: z in Intersection(F,Ch,y);
A5: now
      let x such that
A6:   x in dom Ch and
A7:   Ch.x=y;
      Ch.x in {y} by A7,TARSKI:def 1;
      then
A8:   x in Ch"{y} by A6,FUNCT_1:def 13;
      z in F.x by A4,A6,A7,Def2;
      then x in dom F by FUNCT_1:def 4;
      then x in dom F/\X9 by A2,A8,XBOOLE_0:def 4;
      then
A9:   x in dom (F|X9) by RELAT_1:90;
      z in F.x by A4,A6,A7,Def2;
      hence z in F|X9.x by A9,FUNCT_1:70;
    end;
    consider x such that
A10: x in dom Ch and
A11: Ch.x=y and
A12: z in F.x by A1,A4,Th24;
    Ch.x in {y} by A11,TARSKI:def 1;
    then
A13: x in Ch"{y} by A10,FUNCT_1:def 13;
    x in dom F by A12,FUNCT_1:def 4;
    then x in dom F/\X9 by A2,A13,XBOOLE_0:def 4;
    then x in dom (F|X9) by RELAT_1:90;
    then
A14: F|X9.x in rng (F|X9) by FUNCT_1:def 5;
    z in F|X9.x by A5,A10,A11;
    then z in union rng (F|X9) by A14,TARSKI:def 4;
    hence thesis by A5,Def2;
  end;
  Intersection(F|X9,Ch,y) c=Intersection(F,Ch,y) by Th31;
  hence thesis by A3,XBOOLE_0:def 10;
end;

theorem Th33:
  x in Ch"{y} implies Intersection(F,Ch,y) c= F.x
proof
  assume
A1: x in Ch"{y};
  then
A2: x in dom Ch by FUNCT_1:def 13;
  Ch.x in {y} by A1,FUNCT_1:def 13;
  then
A3: Ch.x=y by TARSKI:def 1;
  let z;
  assume z in Intersection(F,Ch,y);
  hence thesis by A2,A3,Def2;
end;

theorem Th34:
  x in Ch"{y} implies Intersection(F,Ch|(dom Ch\{x}),y)/\F.x=
  Intersection(F,Ch,y)
proof
  set d=dom Ch\{x};
  set Chd=Ch|d;
  set I1=Intersection(F,Ch,y);
  set I2=Intersection(F,Chd,y);
  assume x in Ch"{y};
  then
A1: I1 c= F.x by Th33;
A2: I2/\F.x c= I1
  proof
    let z such that
A3: z in I2/\F.x;
    now
      let x1 such that
A4:   x1 in dom Ch and
A5:   Ch.x1=y;
      per cases by A4,XBOOLE_0:def 5;
      suppose
A6:     x1 in d;
A7:     z in I2 by A3,XBOOLE_0:def 4;
A8:     dom Ch/\d= dom Chd & dom Ch/\d =d by RELAT_1:90,XBOOLE_1:28;
        then Chd.x1=y by A5,A6,FUNCT_1:70;
        hence z in F.x1 by A6,A8,A7,Def2;
      end;
      suppose
        x1 in {x};
        then x1=x by TARSKI:def 1;
        hence z in F.x1 by A3,XBOOLE_0:def 4;
      end;
    end;
    hence thesis by A3,Def2;
  end;
  I1 c= I2 by Th29;
  then I1 c= I2/\F.x by A1,XBOOLE_1:19;
  hence thesis by A2,XBOOLE_0:def 10;
end;

theorem Th35:
  for Ch1,Ch2 be Function st Ch1"{x1}=Ch2"{x2} holds Intersection(
  F,Ch1,x1)=Intersection(F,Ch2,x2)
proof
  let Ch1,Ch2 be Function such that
A1: Ch1"{x1}=Ch2"{x2};
  thus Intersection(F,Ch1,x1)c=Intersection(F,Ch2,x2)
  proof
    let z such that
A2: z in Intersection(F,Ch1,x1);
    now
      let x such that
A3:   x in dom Ch2 and
A4:   Ch2.x=x2;
      Ch2.x in {x2} by A4,TARSKI:def 1;
      then
A5:   x in Ch1"{x1} by A1,A3,FUNCT_1:def 13;
      then Ch1.x in {x1} by FUNCT_1:def 13;
      then
A6:   Ch1.x=x1 by TARSKI:def 1;
      x in dom Ch1 by A5,FUNCT_1:def 13;
      hence z in F.x by A2,A6,Def2;
    end;
    hence thesis by A2,Def2;
  end;
  thus Intersection(F,Ch2,x2)c=Intersection(F,Ch1,x1)
  proof
    let z such that
A7: z in Intersection(F,Ch2,x2);
    now
      let x such that
A8:   x in dom Ch1 and
A9:   Ch1.x=x1;
      Ch1.x in {x1} by A9,TARSKI:def 1;
      then
A10:  x in Ch2"{x2} by A1,A8,FUNCT_1:def 13;
      then Ch2.x in {x2} by FUNCT_1:def 13;
      then
A11:  Ch2.x=x2 by TARSKI:def 1;
      x in dom Ch2 by A10,FUNCT_1:def 13;
      hence z in F.x by A7,A11,Def2;
    end;
    hence thesis by A7,Def2;
  end;
end;

theorem Th36:
  Ch"{y}={} implies Intersection(F,Ch,y)=union rng F
proof
  reconsider E ={} as set;
A1: (Ch|E)"{y} c= Ch"{y} by RELAT_1:88,179;
A2: Ch|E={} & Intersection(F,{},y)=union rng F by Th28;
  assume Ch"{y}={};
  then (Ch|E)"{y} = Ch"{y} by A1;
  hence thesis by A2,Th35;
end;

theorem Th37:
  {x}=Ch"{y} implies Intersection(F,Ch,y)=F.x
proof
A1: (dom Ch\{x}) misses {x} by XBOOLE_1:79;
  assume
A2: {x}=Ch"{y};
  then (Ch|(dom Ch\{x}))"{y}=(dom Ch\{x}) /\ {x} by FUNCT_1:139;
  then (Ch|(dom Ch\{x}))"{y}={} by A1,XBOOLE_0:def 7;
  then
A3: Intersection(F,Ch|(dom Ch\{x}),y)=union rng F by Th36;
  x in Ch"{y} by A2,TARSKI:def 1;
  then
A4: union rng F/\F.x=Intersection(F,Ch,y) by A3,Th34;
  per cases;
  suppose
    x in dom F;
    then F.x in rng F by FUNCT_1:def 5;
    hence thesis by A4,XBOOLE_1:28,ZFMISC_1:92;
  end;
  suppose
    not x in dom F;
    then F.x={} by FUNCT_1:def 4;
    hence thesis by A4;
  end;
end;

theorem Th38:
  {x1,x2}=Ch"{y} implies Intersection(F,Ch,y)=F.x1 /\ F.x2
proof
  assume
A1: {x1,x2}=Ch"{y};
  per cases;
  suppose
A2: x1=x2;
    then Ch"{y}={x1} by A1,ENUMSET1:69;
    hence thesis by A2,Th37;
  end;
  suppose
A3: x1<>x2;
    Ch"{y}/\(dom Ch\{x1})=(Ch"{y}/\dom Ch)\{x1} & Ch"{y}/\dom Ch={x1,x2}
    by A1,RELAT_1:167,XBOOLE_1:28,49;
    then Ch"{y}/\(dom Ch\{x1})={x2} by A3,ZFMISC_1:23;
    then
A4: (Ch|(dom Ch\{x1}))"{y}={x2} by FUNCT_1:139;
    x1 in Ch"{y} by A1,TARSKI:def 2;
    then Intersection(F,Ch|(dom Ch\{x1}),y)/\F.x1=Intersection(F,Ch,y) by Th34;
    hence thesis by A4,Th37;
  end;
end;

theorem
  for F st F is non empty holds y in Intersection(F,(dom F-->x),x) iff
  for z st z in dom F holds y in F.z
proof
  let F such that
A1: F is non empty;
  set Ch=dom F-->x;
  thus y in Intersection(F,Ch,x) implies for z st z in dom F holds y in F.z
  proof
    assume
A2: y in Intersection(F,Ch,x);
    let z;
    assume z in dom F;
    then z in dom Ch & Ch.z=x by FUNCOP_1:13,19;
    hence thesis by A2,Def2;
  end;
  Ch"{x}=dom F by FUNCOP_1:21;
  then
A3: dom F/\Ch"{x}=dom F;
  assume for z st z in dom F holds y in F.z;
  then for z st z in dom Ch & Ch.z=x holds y in F.z;
  hence thesis by A1,A3,Th21;
end;

definition
  let F be Function;
  redefine attr F is finite-yielding means
  :Def3:
  for x holds F.x is finite;
  compatibility
  proof
    hereby
      assume
A1:   F is finite-yielding;
      let x;
      per cases;
      suppose
        x in dom F;
        then F.x in rng F by FUNCT_1:12;
        hence F.x is finite by A1,FINSET_1:def 2;
      end;
      suppose
        not x in dom F;
        hence F.x is finite by FUNCT_1:def 4;
      end;
    end;
    assume
A2: for x holds F.x is finite;
    let x;
    assume x in rng F;
    then ex y st y in dom F & x = F.y by FUNCT_1:def 5;
    hence thesis by A2;
  end;
end;

registration
  cluster non empty finite-yielding Function;
  existence
  proof
    consider F be Function such that
A1: dom F=1 and
A2: rng F={1} by FUNCT_1:15;
    take F;
    now
      let x;
      per cases;
      suppose
        x in dom F;
        then F.x in rng F by FUNCT_1:def 5;
        hence F.x is finite by A2;
      end;
      suppose
        not x in dom F;
        hence F.x is finite by FUNCT_1:def 4;
      end;
    end;
    hence thesis by A1,Def3;
  end;
  cluster empty -> finite-yielding Function;
  coherence
  proof
    let F be Function;
    assume
A3: F is empty;
    let x;
    thus thesis by A3;
  end;
end;

registration
  let F be finite-yielding Function,x be set;
  cluster F.x -> finite;
  coherence by Def3;
end;

registration
  let F be finite-yielding Function,X be set;
  cluster F|X -> finite-yielding;
  coherence
  proof
    let x;
    per cases;
    suppose
      x in dom (F|X);
      then (F|X).x=F.x by FUNCT_1:70;
      hence thesis;
    end;
    suppose
      not x in dom (F|X);
      hence thesis by FUNCT_1:def 4;
    end;
  end;
end;

registration
  let F be finite-yielding Function,G be Function;
  cluster F*G -> finite-yielding;
  coherence
  proof
    let x;
    per cases;
    suppose
      x in dom (F*G);
      then (F*G).x =F.(G.x) by FUNCT_1:22;
      hence thesis;
    end;
    suppose
      not x in dom (F*G);
      hence thesis by FUNCT_1:def 4;
    end;
  end;
  cluster Intersect(F,G) -> finite-yielding;
  coherence
  proof
    let x;
    per cases;
    suppose
      x in dom Intersect(F,G);
      then x in (dom F)/\(dom G) by YELLOW20:def 2;
      then Intersect(F,G).x=F.x/\G.x by YELLOW20:def 2;
      hence thesis;
    end;
    suppose
      not x in dom Intersect(F,G);
      hence thesis by FUNCT_1:def 4;
    end;
  end;
end;

reserve Fy for finite-yielding Function;

theorem
  y in rng Ch implies Intersection(Fy,Ch,y) is finite
proof
  assume y in rng Ch;
  then consider x such that
A1: x in dom Ch and
A2: Ch.x=y by FUNCT_1:def 5;
  Ch.x in {y} by A2,TARSKI:def 1;
  then x in Ch"{y} by A1,FUNCT_1:def 13;
  then Intersection(Fy,Ch,y) c= Fy.x by Th33;
  hence thesis;
end;

theorem Th41:
  dom Fy is finite implies union rng Fy is finite
proof
A1: for F be set st F in rng Fy holds F is finite
  proof
    let F be set;
    assume F in rng Fy;
    then ex x st x in dom Fy & F=Fy.x by FUNCT_1:def 5;
    hence thesis;
  end;
  assume dom Fy is finite;
  then rng Fy is finite by FINSET_1:26;
  hence thesis by A1,FINSET_1:25;
end;



canceled 4;

theorem
  x in Choose(n,k,1,0) iff ex F be XFinSequence of NAT st F = x & dom F
  = n & rng F c= {0,1} & Sum F=k
proof
  thus x in Choose(n,k,1,0) implies ex F be XFinSequence of NAT st F = x & dom
  F = n & rng F c= {0,1} & Sum F=k
  proof
    assume x in Choose(n,k,1,0);
    then consider F be Function of n,{0,1} such that
A1: x=F & card (F"{1})=k by Def1;
A2: rng F c= {0,1};
    dom F=n by FUNCT_2:def 1;
    then reconsider F as XFinSequence by AFINSQ_1:7;
    rng F is Subset of NAT by A2,XBOOLE_1:1;
    then reconsider F as XFinSequence of NAT by RELAT_1:def 19;
    take F;
    Sum F= 1*card (F"{1}) by A2,AFINSQ_2:80;
    hence thesis by A1,A2,FUNCT_2:def 1;
  end;
  given F be XFinSequence of NAT such that
A3: F = x and
A4: dom F = n & rng F c= {0,1} & Sum F=k;
  1*card (F"{1})=k & F is Function of n,{0,1} by A4,AFINSQ_2:80,FUNCT_2:4;
  hence thesis by A3,Def1;
end;

canceled 2;

Lm3: ex P be Function of card X,X st P is one-to-one
proof
  card X,X are_equipotent by CARD_1:def 5;
  then consider P be Function such that
A1: P is one-to-one and
A2: dom P=card X & rng P=X by WELLORD2:def 4;
  P is Function of card X,X by A2,FUNCT_2:3;
  hence thesis by A1;
end;

definition
  let k;
  let F be finite-yielding Function;
  assume
A1: dom F is finite;
  func Card_Intersection(F,k) -> Element of NAT means
  :Def4:
  for x,y be set,X
be finite set, P be Function of card Choose(X,k,x,y),Choose(X,k,x,y) st dom F=X
& P is one-to-one & x<>y ex XFS be XFinSequence of NAT st dom XFS=dom P & (for
z,f st z in dom XFS & f=P.z holds XFS.z=card(Intersection(F,f,x))) & it=Sum XFS
  ;
  existence
  proof
    reconsider D=dom F as finite set by A1;
    set Ch1=Choose(D,k,0,1);
    card Ch1,Ch1 are_equipotent by CARD_1:def 5;
    then consider P1 be Function such that
A2: P1 is one-to-one and
A3: dom P1=card Ch1 and
A4: rng P1=Ch1 by WELLORD2:def 4;
    reconsider P1 as Function of card Ch1,Ch1 by A3,A4,FUNCT_2:3;
    defpred xfs1[set,set] means for f st f=P1.$1 holds $2=card Intersection(F,
    f,0);
A5: for x st x in card Ch1 ex y st y in NAT & xfs1[x,y]
    proof
      let x;
      assume x in card Ch1;
      then x in dom P1 by CARD_1:47,FUNCT_2:def 1;
      then P1.x in rng P1 by FUNCT_1:def 5;
      then consider f be Function of D,{0,1} such that
A6:   f = P1.x and
      card (f"{0}) = k by Def1;
      union rng F is finite by A1,Th41;
      then reconsider I=Intersection(F,f,0) as finite set;
      take card I;
      thus thesis by A6;
    end;
    consider XFS1 be Function of card Ch1,NAT such that
A7: for x st x in card Ch1 holds xfs1[x,XFS1.x] from FUNCT_2:sch 1(A5
    );
A8: dom XFS1 =card Ch1 by FUNCT_2:def 1;
    then reconsider XFS1 as XFinSequence by AFINSQ_1:7;
    reconsider XFS1 as XFinSequence of NAT;
    reconsider S=Sum XFS1 as Element of NAT by ORDINAL1:def 13;
    take S;
    let x,y be set,X be finite set, P be Function of card Choose(X,k,x,y),
    Choose(X,k,x,y) such that
A9: dom F=X and
A10: P is one-to-one and
A11: x<>y;
    defpred perm[set,set] means for f1 be Function of D,{0,1},f be Function of
X,{x,y} st f1=P1.$1 & f=P.$2 holds f1"{0}=f"{x} & for z st z in X holds (f1.z=0
    iff f.z=x)&(f1.z=1 iff f.z=y);
    set Ch=Choose(X,k,x,y);
A12: for x1 st x1 in card Ch1 ex x2 st x2 in card Ch1 & perm[x1,x2]
    proof
      card card Choose(X,k,x,y)=card Choose(X,k,x,y);
      then P is onto by A10,STIRL2_1:70;
      then
A13:  rng P=Ch by FUNCT_2:def 3;
      let x1;
      assume x1 in card Ch1;
      then P1.x1 in rng P1 by A3,FUNCT_1:def 5;
      then consider f1 be Function of D,{0,1} such that
A14:  f1=P1.x1 and
A15:  card (f1"{0})=k by Def1;
      defpred pf[set,set] means (f1.$1=0 iff $2=x)&(f1.$1=1 iff $2=y);
A16:  for d be set st d in X ex fd be set st fd in {x,y} & pf[d,fd]
      proof
        let d be set;
        assume d in X;
        then d in dom f1 by A9,FUNCT_2:def 1;
        then f1.d in rng f1 by FUNCT_1:def 5;
        then
A17:    f1.d=0 or f1.d=1 by TARSKI:def 2;
        x in {x,y} & y in {x,y} by TARSKI:def 2;
        hence thesis by A11,A17;
      end;
      consider f be Function of X,{x,y} such that
A18:  for d be set st d in X holds pf[d,f.d] from FUNCT_2:sch 1(A16);
A19:  dom f1=D by FUNCT_2:def 1;
A20:  f1"{0}c=f"{x}
      proof
        let z;
        assume
A21:    z in f1"{0};
        then f1.z in {0} by FUNCT_1:def 13;
        then
A22:    f1.z=0 by TARSKI:def 1;
A23:    dom f1=dom f by A9,A19,FUNCT_2:def 1;
        then z in dom f by A19,A21;
        then f.z=x by A18,A22;
        then f.z in {x} by TARSKI:def 1;
        hence thesis by A19,A21,A23,FUNCT_1:def 13;
      end;
A24:  f"{x}c=f1"{0}
      proof
        let z;
        assume
A25:    z in f"{x};
        then f.z in {x} by FUNCT_1:def 13;
        then f.z=x by TARSKI:def 1;
        then f1.z=0 by A18,A25;
        then f1.z in {0} by TARSKI:def 1;
        hence thesis by A9,A19,A25,FUNCT_1:def 13;
      end;
      then f"{x}=f1"{0} by A20,XBOOLE_0:def 10;
      then f in Ch by A15,Def1;
      then consider x2 such that
A26:  x2 in dom P and
A27:  P.x2=f by A13,FUNCT_1:def 5;
      take x2;
      card Ch=card X choose k & card Ch1 = card D choose k by A11,Th18;
      hence x2 in card Ch1 by A9,A26;
      let f19 be Function of D,{0,1},f9 be Function of X,{x,y} such that
A28:  f19=P1.x1 & f9=P.x2;
      thus f9"{x}=f19"{0} by A14,A24,A20,A27,A28,XBOOLE_0:def 10;
      let z;
      assume z in X;
      hence thesis by A14,A18,A27,A28;
    end;
    consider Perm be Function of card Ch1,card Ch1 such that
A29: for x1 st x1 in card Ch1 holds perm[x1,Perm.x1] from FUNCT_2:sch
    1(A12 );
    now
A30:  Ch={} implies card Ch={};
      let z1,z2 be set such that
A31:  z1 in dom Perm and
A32:  z2 in dom Perm and
A33:  Perm.z1 = Perm.z2;
      card Ch=card X choose k & card Ch1=card D choose k by A11,Th18;
      then Perm.z1 in card Ch by A9,A31;
      then Perm.z1 in dom P by A30,FUNCT_2:def 1;
      then P.(Perm.z1) in rng P by FUNCT_1:def 5;
      then consider PPermz1 be Function of X,{x,y} such that
A34:  PPermz1=P.(Perm.z1) and
      card(PPermz1"{x})=k by Def1;
      P1.z2 in rng P1 by A3,A32,FUNCT_1:def 5;
      then consider P1z2 be Function of D,{0,1} such that
A35:  P1.z2=P1z2 and
      card (P1z2"{0})=k by Def1;
      P1.z1 in rng P1 by A3,A31,FUNCT_1:def 5;
      then consider P1z1 be Function of D,{0,1} such that
A36:  P1.z1=P1z1 and
      card (P1z1"{0})=k by Def1;
A37:  for z st z in dom P1z1 holds P1z1.z=P1z2.z
      proof
        let z such that
A38:    z in dom P1z1;
A39:    P1z1.z in rng P1z1 by A38,FUNCT_1:def 5;
        per cases by A39,TARSKI:def 2;
        suppose
A40:      P1z1.z=0;
          then PPermz1.z=x by A9,A29,A31,A36,A34,A38;
          hence thesis by A9,A29,A32,A33,A35,A34,A38,A40;
        end;
        suppose
A41:      P1z1.z=1;
          then PPermz1.z=y by A9,A29,A31,A36,A34,A38;
          hence thesis by A9,A29,A32,A33,A35,A34,A38,A41;
        end;
      end;
      dom P1z1=D & dom P1z2=D by FUNCT_2:def 1;
      then P1z1=P1z2 by A37,FUNCT_1:def 17;
      hence z1=z2 by A2,A3,A31,A32,A36,A35,FUNCT_1:def 8;
    end;
    then
A42: Perm is one-to-one by FUNCT_1:def 8;
    card card Ch1=card card Ch1;
    then
A43: Perm is one-to-one onto by A42,STIRL2_1:70;
    defpred xfs[set,set] means for f st f=P.$1 holds $2=card Intersection(F,f,
    x);
A44: for x1 st x1 in card Ch ex x2 st x2 in NAT & xfs[x1,x2]
    proof
      let x1;
      assume x1 in card Ch;
      then x1 in dom P by CARD_1:47,FUNCT_2:def 1;
      then P.x1 in rng P by FUNCT_1:def 5;
      then consider f be Function of X,{x,y} such that
A45:  f = P.x1 and
      card (f"{x}) = k by Def1;
      union rng F is finite by A1,Th41;
      then reconsider I=Intersection(F,f,x) as finite set;
      take card I;
      thus thesis by A45;
    end;
    consider XFS be Function of card Ch,NAT such that
A46: for x1 st x1 in card Ch holds xfs[x1,XFS.x1] from FUNCT_2:sch 1(
    A44);
A47: dom XFS =card Ch by FUNCT_2:def 1;
    then reconsider XFS as XFinSequence by AFINSQ_1:7;
    reconsider XFS as XFinSequence of NAT;
    take XFS;
    Ch={} implies card Ch={};
    then dom P=card Ch by FUNCT_2:def 1;
    hence
A48: dom XFS=dom P by FUNCT_2:def 1;
    hence
    for z,f st z in dom XFS & f=P.z holds XFS.z=card(Intersection(F,f,x))
    by A46;
A49: card Ch1=card D choose k by Th18;
A50: card Ch=card X choose k by A11,Th18;
    then card Ch1=dom XFS by A9,A49,FUNCT_2:def 1;
    then reconsider Perm as Permutation of dom XFS by A43;
A51: dom XFS=dom XFS1 by A9,A47,A49,A50,FUNCT_2:def 1;
A52: for z st z in dom XFS1 holds XFS1.z =(XFS*Perm).z
    proof
      let z such that
A53:  z in dom XFS1;
A54:  z in dom Perm by A8,A53,FUNCT_2:67;
      P.(Perm.z) in rng P by A48,A51,A53,FUNCT_1:def 5;
      then consider p be Function of X,{x,y} such that
A56:  p=P.(Perm.z) and
      card (p"{x})=k by Def1;
A57:  XFS.(Perm.z)=card Intersection(F,p,x) by A46,A47,
          A51,A53,A56;
      P1.z in rng P1 by A3,A8,A53,FUNCT_1:def 5;
      then consider p1 be Function of D,{0,1} such that
A58:  p1=P1.z and
      card (p1"{0})=k by Def1;
      p1"{0}=p"{x} by A8,A29,A53,A56,A58;
      then
A59:  Intersection(F,p1,0)=Intersection(F,p,x) by Th35;
      XFS1.z=card Intersection(F,p1,0) by A7,A8,A53,A58;
      hence thesis by A59,A57,A54,FUNCT_1:23;
    end;
    rng Perm c= dom XFS & dom Perm=dom XFS by FUNCT_2:67;
    then dom XFS1=dom (XFS*Perm) by A51,RELAT_1:46;
    then XFS1=XFS*Perm by A52,FUNCT_1:def 17;
    then
A60: addnat "**" XFS=addnat "**" XFS1 by AFINSQ_2:57;
    addnat "**" XFS1=Sum XFS1 by AFINSQ_2:63;
    hence thesis by A60,AFINSQ_2:63;
  end;
  uniqueness
  proof
    reconsider D=dom F as finite set by A1;
    let n1,n2 be Element of NAT such that
A61: for x,y be set,X be finite set, P be Function of card Choose(X,k
    ,x,y),Choose(X,k,x,y) st dom F=X & P is one-to-one & x<>y ex XFS be
XFinSequence of NAT st dom XFS=dom P & (for z,f st z in dom XFS & f=P.z holds
    XFS.z=card(Intersection(F,f,x))) & n1=Sum XFS and
A62: for x,y be set,X be finite set, P be Function of card Choose(X,k
    ,x,y),Choose(X,k,x,y) st dom F=X & P is one-to-one & x<>y ex XFS be
XFinSequence of NAT st dom XFS=dom P & (for z,f st z in dom XFS & f=P.z holds
    XFS.z=card(Intersection(F,f,x))) & n2=Sum XFS;
    set Ch1=Choose(D,k,0,1);
    card Ch1,Ch1 are_equipotent by CARD_1:def 5;
    then consider P be Function such that
A63: P is one-to-one and
A64: dom P=card Ch1 & rng P=Ch1 by WELLORD2:def 4;
    reconsider P as Function of card Ch1,Ch1 by A64,FUNCT_2:3;
    consider XFS1 be XFinSequence of NAT such that
A65: dom XFS1=dom P and
A66: for z,f st z in dom XFS1 & f=P.z holds XFS1.z=card(Intersection(
    F,f,0)) and
A67: n1=Sum XFS1 by A61,A63;
    consider XFS2 be XFinSequence of NAT such that
A68: dom XFS2=dom P and
A69: for z,f st z in dom XFS2 & f=P.z holds XFS2.z=card(Intersection(
    F,f,0)) and
A70: n2=Sum XFS2 by A62,A63;
    now
      let z such that
A71:  z in dom XFS1;
      P.z in rng P by A65,A71,FUNCT_1:def 5;
      then consider Pz be Function of D,{0,1} such that
A72:  Pz=P.z and
      card (Pz"{0})=k by Def1;
      XFS2.z=card(Intersection(F,Pz,0)) by A65,A68,A69,A71,A72;
      hence XFS2.z=XFS1.z by A66,A71,A72;
    end;
    hence thesis by A65,A67,A68,A70,FUNCT_1:9;
  end;
end;

theorem
  for x,y be set,X be finite set, P be Function of card Choose(X,k,x,y),
Choose(X,k,x,y) st dom Fy=X & P is one-to-one & x<>y for XFS be XFinSequence of
  NAT st dom XFS=dom P & (for z,f st z in dom XFS & f=P.z holds XFS.z=card(
  Intersection(Fy,f,x))) holds Card_Intersection(Fy,k)=Sum XFS
proof
  let x,y be set,X be finite set, P be Function of card Choose(X,k,x,y),Choose
  (X,k,x,y);
  assume dom Fy=X & P is one-to-one & x<>y;
  then consider XFS be XFinSequence of NAT such that
A1: dom XFS=dom P and
A2: for z,f st z in dom XFS & f=P.z holds XFS.z=card(Intersection(Fy,f,x )) and
A3: Card_Intersection(Fy,k)=Sum XFS by Def4;
  let XFS1 be XFinSequence of NAT such that
A4: dom XFS1=dom P and
A5: for z,f st z in dom XFS1 & f=P.z holds XFS1.z=card(Intersection(Fy,f ,x));
  now
    let z such that
A6: z in dom XFS;
    P.z in rng P by A1,A6,FUNCT_1:def 5;
    then consider Pz be Function of X,{x,y} such that
A7: Pz=P.z and
    card (Pz"{x})=k by Def1;
    XFS1.z=card(Intersection(Fy,Pz,x)) by A4,A5,A1,A6,A7;
    hence XFS1.z=XFS.z by A2,A6,A7;
  end;
  hence thesis by A4,A1,A3,FUNCT_1:9;
end;

theorem
  dom Fy is finite & k=0 implies Card_Intersection(Fy,k)=card (union rng Fy)
proof
  assume that
A1: dom Fy is finite and
A2: k=0;
  reconsider X=dom Fy as finite set by A1;
  set Ch=Choose(X,k,0,1);
  consider P be Function of card Ch,Ch such that
A3: P is one-to-one by Lm3;
A4: card Ch=1 by A2,Th11;
  then
A5: dom P=1 by CARD_1:47,FUNCT_2:def 1;
  consider XFS be XFinSequence of NAT such that
A6: dom XFS=dom P and
A7: for z,f st z in dom XFS & f=P.z holds XFS.z=card(Intersection(Fy,f,0 )) and
A8: Card_Intersection(Fy,k)=Sum XFS by A3,Def4;
  len XFS=1 by A6,A4,CARD_1:47,FUNCT_2:def 1;
  then XFS=<%XFS.0%> by AFINSQ_1:38;
  then
A9: addnat "**" XFS=XFS.0 by AFINSQ_2:49;
A10: 0 in 1 by CARD_1:87,TARSKI:def 1;
  then P.0 in rng P by A5,FUNCT_1:def 5;
  then consider P0 be Function of X,{0,1} such that
A11: P0=P.0 and
A12: card (P0"{0})=0 by A2,Def1;
  P0"{0}={} by A12;
  then
A13: Intersection(Fy,P0,0)=union rng Fy by Th36;
  XFS.0=card(Intersection(Fy,P0,0)) by A6,A7,A5,A10,A11;
  hence thesis by A8,A13,A9,AFINSQ_2:63;
end;

theorem Th51:
  dom Fy=X & k > card X implies Card_Intersection(Fy,k)=0
proof
  assume that
A1: dom Fy=X and
A2: k > card X;
  set Ch=Choose(X,k,0,1);
  consider P be Function of card Ch,Ch such that
A3: P is one-to-one by Lm3;
  consider XFS be XFinSequence of NAT such that
A4: dom XFS=dom P and
  for z,f st z in dom XFS & f=P.z holds XFS.z=card(Intersection(Fy,f,0)) and
A5: Card_Intersection(Fy,k)=Sum XFS by A1,A3,Def4;
  Ch is empty by A2,Th10;
  then XFS=0 by A4;
  hence thesis by A5;
end;

theorem Th52:
  for Fy,X st dom Fy=X for P be Function of card X,X st P is
one-to-one holds ex XFS be XFinSequence of NAT st dom XFS=card X & (for z st z
  in dom XFS holds XFS.z=card ((Fy*P).z))& Card_Intersection(Fy,1)=Sum XFS
proof
  let Fy,X such that
A1: dom Fy=X;
  let P be Function of card X,X such that
A2: P is one-to-one;
  per cases;
  suppose
A3: X ={};
    reconsider XFS={} as XFinSequence;
    rng {} c= {} & {} c= NAT;
    then reconsider XFS as XFinSequence of NAT by RELAT_1:def 19;
    take XFS;
    thus card X=dom XFS & for z st z in dom XFS holds XFS.z=card ((Fy*P).z) by
A3,CARD_1:47;
    Sum XFS=0;
    hence thesis by A1,A3,Th51,CARD_1:47;
  end;
  suppose
    X <>{};
    then reconsider cX=card X as non empty set;
    deffunc xfs(Element of cX)=card ((Fy*P).$1);
    consider XFS be Function of cX,NAT such that
A4: for x be Element of cX holds XFS.x = xfs(x) from FUNCT_2:sch 4;
A5: dom XFS=cX by FUNCT_2:def 1;
    then reconsider XFS as XFinSequence by AFINSQ_1:7;
    reconsider XFS as XFinSequence of NAT;
    take XFS;
    thus card X=dom XFS by FUNCT_2:def 1;
    thus for z st z in dom XFS holds XFS.z=card ((Fy*P).z) by A4,A5;
    thus Card_Intersection(Fy,1)=Sum XFS
    proof
      deffunc p1(set)=(P.$1 .-->0) +* ((X\{P.$1})-->1);
A6:   for x st x in cX holds p1(x) in Choose(X,1,0,1)
      proof
        let x;
        assume x in cX;
        then x in dom P by CARD_1:47,FUNCT_2:def 1;
        then P.x in rng P by FUNCT_1:def 5;
        then
A7:     {P.x}\/(X\{P.x})=X by ZFMISC_1:140;
        {P.x} misses (X\{P.x}) & card {P.x}=1 by CARD_1:50,XBOOLE_1:79;
        hence thesis by A7,Th20;
      end;
      consider P1 be Function of cX,Choose(X,1,0,1) such that
A8:   for z st z in cX holds P1.z = p1(z) from FUNCT_2:sch 2(A6);
      Choose(X,1,0,1) c= rng P1
      proof
        card X=card card X;
        then
A9:     P is onto by A2,STIRL2_1:70;
        let z;
        assume z in Choose(X,1,0,1);
        then consider F be Function of X,{0,1} such that
A10:    F=z and
A11:    card (F"{0})=1 by Def1;
        consider x1 such that
A12:    F"{0}={x1} by A11,CARD_2:60;
A13:    x1 in {x1} by TARSKI:def 1;
        then x1 in X by A12;
        then x1 in rng P by A9,FUNCT_2:def 3;
        then consider x2 such that
A14:    x2 in dom P and
A15:    P.x2=x1 by FUNCT_1:def 5;
A16:    P1.x2=F
        proof
          set F1=(X\{P.x2})-->1;
          set F0=P.x2 .-->0;
          set P1x=F0+*F1;
A17:      {P.x2}\/(X\{P.x2})=X by A12,A13,A15,ZFMISC_1:140;
A18:      now
            let d be set such that
A19:        d in dom F;
            now
              per cases by A17,A19,XBOOLE_0:def 3;
              suppose
A20:            d in {P.x2};
A21:            {P.x2} misses (X\{P.x2}) by XBOOLE_1:79;
                dom F0={P.x2} & dom F1=(X\{P.x2}) by FUNCOP_1:19;
                then P1x.d=F0.d by A20,A21,FUNCT_4:17;
                then
A22:            P1x.d=0 by A20,FUNCOP_1:13;
                F.d in {0} by A12,A15,A20,FUNCT_1:def 13;
                hence P1x.d=F.d by A22,TARSKI:def 1;
              end;
              suppose
A23:            d in X\{P.x2};
                then d in dom F1 by FUNCT_2:def 1;
                then P1x.d=F1.d by FUNCT_4:14;
                then
A24:            P1x.d=1 by A23,FUNCOP_1:13;
A25:            X=dom F by FUNCT_2:def 1;
                not d in {x1} by A15,A23,XBOOLE_0:def 5;
                then not F.d in {0} by A12,A23,A25,FUNCT_1:def 13;
                then
A26:            not F.d=0 by TARSKI:def 1;
                F.d in rng F by A23,A25,FUNCT_1:def 5;
                hence P1x.d=F.d by A24,A26,TARSKI:def 2;
              end;
            end;
            hence P1x.d=F.d;
          end;
          dom F0={P.x2} by FUNCOP_1:19;
          then
A27:      X=dom F0\/dom F1 by A17,FUNCT_2:def 1;
          dom F=X & dom P1x=dom F0\/dom F1 by FUNCT_2:def 1,FUNCT_4:def 1;
          then P1x=F by A27,A18,FUNCT_1:9;
          hence thesis by A8,A14;
        end;
        card Choose(X,1,0,1)= card X choose 1 by Th18;
        then card Choose(X,1,0,1)=cX by NAT_1:14,NEWTON:33;
        then dom P1 =cX by CARD_1:47,FUNCT_2:def 1;
        hence thesis by A10,A14,A16,FUNCT_1:def 5;
      end;
      then
A28:  Choose(X,1,0,1) = rng P1 by XBOOLE_0:def 10;
      then
A29:  P1 is onto by FUNCT_2:def 3;
      card Choose(X,1,0,1)= card X choose 1 by Th18;
      then
A30:  card X=card Choose(X,1,0,1) by A28,NAT_1:14,NEWTON:33;
      then reconsider P1 as Function of card Choose(X,1,0,1),Choose(X,1,0,1);
      card card X=card X;
      then P1 is one-to-one by A29,A30,STIRL2_1:70;
      then consider XFS1 be XFinSequence of NAT such that
A31:  dom XFS1=dom P1 and
A32:  for z,f st z in dom XFS1 & f=P1.z holds XFS1.z=card(
      Intersection(Fy,f,0)) and
A33:  Card_Intersection(Fy,1)=Sum XFS1 by A1,Def4;
      Choose(X,1,0,1)={} implies card Choose(X,1,0,1)={};
      then
A34:  dom P1=card Choose(X,1,0,1) by FUNCT_2:def 1;
A35:  for z st z in dom XFS1 holds XFS1.z=XFS.z
      proof
        let z such that
A36:    z in dom XFS1;
        p1(z) in Choose(X,1,0,1) by A6,A30,A31,A36;
        then consider f be Function of X,{0,1} such that
A37:    f=p1(z) and
A38:    card (f"{0})=1 by Def1;
        consider x1 such that
A39:    f"{0}={x1} by A38,CARD_2:60;
        P1.z=p1(z) by A8,A30,A31,A36;
        then
A40:    XFS1.z = card(Intersection(Fy,f,0)) by A32,A36,A37;
A41:    0 in {0} by TARSKI:def 1;
A42:    dom ((X\{P.z})-->1)=X\{P.z} by FUNCOP_1:19;
A43:    P.z in {P.z} by TARSKI:def 1;
        {P.z}=dom (P.z .-->0) by FUNCOP_1:19;
        then
A44:    P.z in dom (P.z .-->0)\/dom((X\{P.z})-->1 ) by A43,XBOOLE_0:def 3;
        ( not P.z in X\{P.z})& (P.z .-->0).(P.z)=0 by A43,FUNCOP_1:13
,XBOOLE_0:def 5;
        then
A45:    p1(z).(P.z)=0 by A44,A42,FUNCT_4:def 1;
        P.z in dom p1(z) by A44,FUNCT_4:def 1;
        then
A46:    P.z in f"{0} by A37,A45,A41,FUNCT_1:def 13;
        then P.z=x1 by A39,TARSKI:def 1;
        then
A47:    card(Intersection(Fy,f,0))=card (Fy.(P.z)) by A39,Th37;
A48:    XFS.z=card ((Fy*P).z) by A4,A30,A31,A36;
        z in dom P by A30,A31,A34,A36,A46,FUNCT_2:def 1;
        hence thesis by A47,A40,A48,FUNCT_1:23;
      end;
      dom XFS1=dom XFS by A30,A31,A34,FUNCT_2:def 1;
      hence thesis by A33,A35,FUNCT_1:def 17;
    end;
  end;
end;

theorem Th53:
  dom Fy=X implies Card_Intersection(Fy,card X)=card Intersection( Fy,X-->x,x)
proof
  set Ch=Choose(X,card X,x,{x});
  consider P be Function of card Ch,Ch such that
A1: P is one-to-one by Lm3;
  x in {x} by TARSKI:def 1;
  then
A2: x<>{x};
  assume dom Fy=X;
  then consider XFS be XFinSequence of NAT such that
A3: dom XFS=dom P and
A4: ( for z,f st z in dom XFS & f=P.z holds XFS.z=card(Intersection(Fy,f
  ,x)))& Card_Intersection(Fy,card X)=Sum XFS by A1,A2,Def4;
A5: card Ch=1 by Th12;
  then consider ch be set such that
A6: Ch={ch} by CARD_2:60;
  x in {x} by TARSKI:def 1;
  then X\/{}=X & {x}<>x;
  then ({}-->{x})+*(X-->x) in Ch by Th19;
  then X-->x in Ch by FUNCT_4:21;
  then
A7: X-->x=ch by A6,TARSKI:def 1;
A8: Ch={} implies card Ch={};
  then
A9: dom P=card Ch by FUNCT_2:def 1;
  then 0 in dom P by A5,CARD_1:87,TARSKI:def 1;
  then P.0 in rng P by FUNCT_1:def 5;
  then
A10: P.0=ch by A6,TARSKI:def 1;
  len XFS=1 by A3,A8,A5,FUNCT_2:def 1;
  then XFS=<%XFS.0%> by AFINSQ_1:38;
  then addnat "**" XFS=XFS.0 by AFINSQ_2:49;
  then
A11: Sum XFS=XFS.0 by AFINSQ_2:63;
  0 in dom XFS by A3,A5,A9,CARD_1:87,TARSKI:def 1;
  hence thesis by A4,A11,A10,A7;
end;

theorem Th54:
  Fy=x.-->X implies Card_Intersection(Fy,1)=card X
proof
  assume
A1: Fy=x.-->X;
  then
A2: dom Fy={x} by FUNCOP_1:19;
A3: x in {x} by TARSKI:def 1;
  then
A4: (x.-->x)"{x}={x} by FUNCOP_1:20;
  Fy.x=X by A1,A3,FUNCOP_1:13;
  then 1=card {x} & Intersection(Fy,x.-->x,x)=X by A4,Th37,CARD_1:50;
  hence thesis by A2,Th53;
end;

theorem
  x<>y & Fy=(x,y)-->(X,Y) implies Card_Intersection(Fy,1)=card X + card
  Y & Card_Intersection(Fy,2)=card (X/\Y)
proof
  assume that
A1: x<>y and
A2: Fy=(x,y)-->(X,Y);
  set P=(0,1)-->(x,y);
A3: dom P={0,1} & rng P={x,y} by FUNCT_4:65,67;
  card {x,y}=2 by A1,CARD_2:76;
  then reconsider P as Function of card {x,y},{x,y} by A3,CARD_1:88,FUNCT_2:3;
A4: card card {x,y}=card {x,y};
A5: P.0=x & Fy.x=X by A1,A2,FUNCT_4:66;
A6: P.1=y & Fy.y=Y by A2,FUNCT_4:66;
A7: dom Fy={x,y} by A2,FUNCT_4:65;
  rng P={x,y} by FUNCT_4:67;
  then P is onto by FUNCT_2:def 3;
  then P is one-to-one by A4,STIRL2_1:70;
  then consider XFS be XFinSequence of NAT such that
A8: dom XFS=card {x,y} and
A9: for z st z in dom XFS holds XFS.z=card ((Fy*P).z) and
A10: Card_Intersection(Fy,1)=Sum XFS by A7,Th52;
  len XFS=2 by A1,A8,CARD_2:76;
  then
A11: XFS=<%XFS.0,XFS.1%> by AFINSQ_1:42;
A12: dom P={0,1} by FUNCT_4:65;
  then 1 in dom P by TARSKI:def 2;
  then
A13: (Fy*P).1=Fy.(P.1) by FUNCT_1:23;
  0 in {0} by TARSKI:def 1;
  then
A14: ({x,y}-->0)"{0}={x,y} by FUNCOP_1:20;
  Fy.x=X & Fy.y=Y by A1,A2,FUNCT_4:66;
  then
A15: Intersection(Fy,{x,y}-->0,0)=X/\Y by A14,Th38;
  0 in dom P by A12,TARSKI:def 2;
  then
A16: (Fy*P).0=Fy.(P.0) by FUNCT_1:23;
A17: dom XFS=2 by A1,A8,CARD_2:76;
  then 1 in dom XFS by CARD_1:88,TARSKI:def 2;
  then
A18: XFS.1=card Y by A9,A6,A13;
  0 in dom XFS by A17,CARD_1:88,TARSKI:def 2;
  then XFS.0=card X by A9,A5,A16;
  then addnat "**" XFS=addnat.(card X,card Y) by A11,A18,AFINSQ_2:50;
  then
A19: addnat "**" XFS=card X + card Y by BINOP_2:def 23;
  card {x,y}=2 & dom Fy={x,y} by A1,A2,CARD_2:76,FUNCT_4:65;
  hence thesis by A10,A19,A15,Th53,AFINSQ_2:63;
end;

theorem Th56:
  for Fy,x st dom Fy is finite & x in dom Fy holds
  Card_Intersection(Fy,1)=Card_Intersection(Fy|(dom Fy\{x}),1)+ card (Fy.x)
proof
  let Fy,x such that
A1: dom Fy is finite and
A2: x in dom Fy;
  reconsider X=dom Fy as finite set by A1;
  card X>0 by A2;
  then reconsider k=card X-1 as Element of NAT by NAT_1:20;
  set Xx=X\{x};
A3: Xx={}implies card Xx={};
  consider Px be Function of card Xx,Xx such that
A4: Px is one-to-one by Lm3;
  not card Xx in card Xx;
  then consider P be Function of card Xx\/{card Xx},Xx\/{x} such that
A5: P|(card Xx) = Px and
A6: P.(card Xx)=x by A3,STIRL2_1:67;
  not x in Xx by ZFMISC_1:64;
  then
A7: P is one-to-one by A4,A3,A5,A6,STIRL2_1:68;
A8: card X=k+1;
  then
A9: card Xx=k by A2,STIRL2_1:65;
  then card X=card Xx\/{card Xx} by A8,AFINSQ_1:4;
  then reconsider P as Function of card X,X by A2,ZFMISC_1:140;
  consider XFS be XFinSequence of NAT such that
A10: dom XFS=card X and
A11: for z st z in dom XFS holds XFS.z=card ((Fy*P).z) and
A12: Card_Intersection(Fy,1)=Sum XFS by A7,Th52;
A13: P.k=x by A2,A6,A8,STIRL2_1:65;
  X/\Xx=Xx by XBOOLE_1:28;
  then dom (Fy|Xx)=Xx by RELAT_1:90;
  then consider XFSx be XFinSequence of NAT such that
A14: dom XFSx=card Xx and
A15: for z st z in dom XFSx holds XFSx.z=card (((Fy|Xx)*Px).z) and
A16: Card_Intersection(Fy|Xx,1)=Sum XFSx by A4,Th52;
  k<k+1 by NAT_1:13;
  then
A17: k c= k+1 by NAT_1:40;
A18: for y st y in dom XFSx holds XFS.y = XFSx.y
  proof
A19: Xx=X/\Xx & X/\Xx=dom (Fy|Xx) by RELAT_1:90,XBOOLE_1:28;
    let y such that
A20: y in dom XFSx;
A21: XFS.y=card ((Fy*P).y) by A14,A9,A10,A11,A17,A20;
A22: dom Px=k by A3,A9,FUNCT_2:def 1;
    then Px.y in rng Px by A14,A9,A20,FUNCT_1:def 5;
    then
A23: (Fy|Xx).(Px.y)=Fy.(Px.y) by A19,FUNCT_1:70;
    dom P=k+1 by CARD_1:47,FUNCT_2:def 1;
    then
A24: (Fy*P).y=Fy.(P.y) by A14,A9,A17,A20,FUNCT_1:23;
    Px.y=P.y by A14,A5,A9,A20,A22,FUNCT_1:70;
    then (Fy*P).y=((Fy|Xx)*Px).y by A14,A9,A20,A22,A24,A23,FUNCT_1:23;
    hence thesis by A15,A20,A21;
  end;
  k<k+1 by NAT_1:13;
  then
A25: k in card X by NAT_1:45;
  then k in dom P by CARD_1:47,FUNCT_2:def 1;
  then
A26: (Fy*P).k=Fy.(P.k) by FUNCT_1:23;
  dom XFS /\k=dom XFSx by A14,A9,A10,A17,XBOOLE_1:28;
  then XFS|k=XFSx by A18,FUNCT_1:68;
  then
A27: Sum XFSx +XFS.k=Sum (XFS|(k+1)) by A10,A25,AFINSQ_2:77;
  XFS.k=card ((Fy*P).k) by A10,A11,A25;
  hence thesis by A16,A10,A12,A27,A26,A13,RELAT_1:98;
end;

theorem Th57:
  dom Intersect(F,dom F-->X9)=dom F & for x st x in dom F holds
  Intersect(F,dom F-->X9).x = F.x /\ X9
proof
  dom(dom F-->X9)= dom F by FUNCOP_1:19;
  then
A1: dom F/\dom(dom F-->X9)=dom F;
  hence dom F=dom Intersect(F,dom F-->X9) by YELLOW20:def 2;
  let x;
  assume
A2: x in dom F;
  then Intersect(F,dom F-->X9).x=F.x/\(dom F-->X9).x by A1,YELLOW20:def 2;
  hence thesis by A2,FUNCOP_1:13;
end;

theorem Th58:
  (union rng F)/\X9=union rng Intersect(F,dom F-->X9)
proof
  set I= Intersect(F,dom F-->X9);
  thus (union rng F)/\X9 c= union rng I
  proof
    let x such that
A1: x in (union rng F)/\X9;
A2: x in X9 by A1,XBOOLE_0:def 4;
    x in union rng F by A1,XBOOLE_0:def 4;
    then consider Fx be set such that
A3: x in Fx and
A4: Fx in rng F by TARSKI:def 4;
    consider x1 such that
A5: x1 in dom F and
A6: F.x1=Fx by A4,FUNCT_1:def 5;
    x1 in dom I by A5,Th57;
    then
A7: I.x1 in rng I by FUNCT_1:def 5;
    I.x1=Fx/\X9 by A5,A6,Th57;
    then x in I.x1 by A3,A2,XBOOLE_0:def 4;
    hence thesis by A7,TARSKI:def 4;
  end;
  thus union rng I c= (union rng F)/\X9
  proof
    let x;
    assume x in union rng I;
    then consider Ix be set such that
A8: x in Ix and
A9: Ix in rng I by TARSKI:def 4;
    consider x1 such that
A10: x1 in dom I and
A11: I.x1=Ix by A9,FUNCT_1:def 5;
A12: x1 in dom F by A10,Th57;
    then
A13: F.x1 in rng F by FUNCT_1:def 5;
A14: I.x1=F.x1/\X9 by A12,Th57;
    then x in F.x1 by A8,A11,XBOOLE_0:def 4;
    then
A15: x in union rng F by A13,TARSKI:def 4;
    x in X9 by A8,A11,A14,XBOOLE_0:def 4;
    hence thesis by A15,XBOOLE_0:def 4;
  end;
end;

theorem Th59:
  Intersection(F,Ch,y)/\X9= Intersection(Intersect(F,dom F-->X9), Ch,y)
proof
  set I=Intersect(F,dom F-->X9);
  set Int1=Intersection(F,Ch,y);
  set Int2=Intersection(I,Ch,y);
  thus Int1/\X9 c= Int2
  proof
    let x such that
A1: x in Int1/\X9;
A2: for z st z in dom Ch & Ch.z=y holds x in I.z
    proof
A3:   x in Int1 by A1,XBOOLE_0:def 4;
      let z;
      assume z in dom Ch & Ch.z=y;
      then
A4:   x in F.z by A3,Def2;
      then
A5:   z in dom F by FUNCT_1:def 4;
      x in X9 by A1,XBOOLE_0:def 4;
      then x in F.z/\X9 by A4,XBOOLE_0:def 4;
      hence thesis by A5,Th57;
    end;
    x in X9 by A1,XBOOLE_0:def 4;
    then x in (union rng F)/\X9 by A1,XBOOLE_0:def 4;
    then x in union rng I by Th58;
    hence thesis by A2,Def2;
  end;
  thus Int2 c= Int1/\X9
  proof
    let x such that
A6: x in Int2;
    x in union rng I by A6;
    then
A7: x in (union rng F)/\X9 by Th58;
    then
A8: x in X9 by XBOOLE_0:def 4;
A9: for z st z in dom Ch & Ch.z=y holds x in F.z
    proof
A10:  dom I=dom F by Th57;
      let z;
      assume z in dom Ch & Ch.z=y;
      then
A11:  x in I.z by A6,Def2;
      then z in dom I by FUNCT_1:def 4;
      then x in F.z/\X9 by A11,A10,Th57;
      hence thesis by XBOOLE_0:def 4;
    end;
    x in union rng F by A7,XBOOLE_0:def 4;
    then x in Int1 by A9,Def2;
    hence thesis by A8,XBOOLE_0:def 4;
  end;
end;

theorem Th60:
  for F, G be XFinSequence st F is one-to-one & G is one-to-one &
  rng F misses rng G holds F^G is one-to-one
proof
  let F, G be XFinSequence such that
A1: F is one-to-one and
A2: G is one-to-one and
A3: rng F misses rng G;
  len F,rng F are_equipotent by A1,WELLORD2:def 4;
  then
A4: card len F=card rng F by CARD_1:21;
  len G,rng G are_equipotent by A2,WELLORD2:def 4;
  then
A5: card len G=card rng G by CARD_1:21;
  reconsider FG=F^G as Function of dom (F^G),rng (F^G) by FUNCT_2:3;
A6: dom (F^G)=len F+len G by AFINSQ_1:def 4;
A7: FG is onto by FUNCT_2:def 3;
  card(rng F\/rng G)=card rng F + card rng G by A3,CARD_2:53;
  then card dom (F^G)=card rng (F^G) by A4,A5,A6,AFINSQ_1:29;
  hence thesis by A7,STIRL2_1:70;
end;

theorem Th61:
  for Fy,X,x,n st dom Fy = X & x in dom Fy & k>0 holds
  Card_Intersection(Fy,k+1)= Card_Intersection(Fy|(dom Fy\{x}),k+1)+
  Card_Intersection(Intersect(Fy|(dom Fy\{x}),(dom Fy\{x})-->Fy.x),k)
proof
  let Fy,X,x,n such that
A1: dom Fy = X and
A2: x in dom Fy and
A3: k>0;
  set Xx=X\{x};
A4: Xx\/{x}=X by A1,A2,ZFMISC_1:140;
  set I=Intersect(Fy|(dom Fy\{x}),(dom Fy\{x})-->Fy.x);
  set X1={f where f is Function of Xx\/{x},{1,0}:card (f"{1})=k+1 & f.x=1};
  set X0={f where f is Function of Xx\/{x},{1,0}:card (f"{1})=k+1 & f.x=0};
  X0\/X1=Choose(Xx\/{x},k+1,1,0) by Lm1;
  then reconsider X0,X1 as finite set by FINSET_1:13,XBOOLE_1:7;
  consider P1 be Function of card X1,X1 such that
A5: P1 is one-to-one by Lm3;
  not x in Xx by ZFMISC_1:64;
  then
A6: card Choose(Xx,k,1,0)=card X1 by Th14;
  defpred p1[set,set] means ex f st f=P1.$1 & f in X1 & $2=f|Xx;
A7: for x1 st x1 in card X1 ex P1x1 be set st P1x1 in Choose(Xx,k,1,0) & p1
  [x1,P1x1]
  proof
    not x in Xx by ZFMISC_1:64;
    then
A8: (Xx\/{x})\{x}=Xx by ZFMISC_1:141;
    let x1;
    assume x1 in card X1;
    then x1 in dom P1 by CARD_1:47,FUNCT_2:def 1;
    then
A9: P1.x1 in rng P1 by FUNCT_1:def 5;
    then P1.x1 in X1;
    then consider P1x1 be Function of Xx\/{x},{1,0} such that
A10: P1.x1=P1x1 and
A11: card (P1x1"{1})=k+1 and
A12: P1x1.x=1;
A13: dom P1x1=Xx\/{x} by FUNCT_2:def 1;
A14: rng (P1x1|Xx) c= {1,0};
    (Xx\/{x})/\Xx=Xx by XBOOLE_1:7,28;
    then dom (P1x1|Xx)=Xx by A13,RELAT_1:90;
    then reconsider Px=P1x1|Xx as Function of Xx,{1,0} by A14,FUNCT_2:4;
A15: not x in Px"{1} by ZFMISC_1:64;
    x in {x} & dom P1x1=Xx\/{x} by FUNCT_2:def 1,TARSKI:def 1;
    then x in dom P1x1 by XBOOLE_0:def 3;
    then P1x1"{1}=Px"{1}\/{x} by A12,A13,A8,AFINSQ_2:78;
    then k+1 =card (Px"{1})+1 by A11,A15,CARD_2:54;
    then Px in Choose(Xx,k,1,0) by Def1;
    hence thesis by A9,A10;
  end;
  consider P1x be Function of card X1,Choose(Xx,k,1,0) such that
A16: for x1 st x1 in card X1 holds p1[x1,P1x.x1] from FUNCT_2:sch 1(A7);
  for x1,x2 st x1 in dom P1x & x2 in dom P1x & P1x.x1 = P1x.x2 holds x1 = x2
  proof
    let x1,x2 such that
A17: x1 in dom P1x and
A18: x2 in dom P1x and
A19: P1x.x1 = P1x.x2;
    consider f2 be Function such that
A20: f2=P1.x2 and
A21: f2 in X1 and
A22: P1x.x2=f2|Xx by A16,A18;
    consider f1 be Function such that
A23: f1=P1.x1 and
A24: f1 in X1 and
A25: P1x.x1=f1|Xx by A16,A17;
A26: ex F be Function of Xx\/{x},{1,0} st f1=F &card (F"{1})=k+1&F.x=1 by A24;
    then
A27: dom f1=Xx\/{x} by FUNCT_2:def 1;
A28: ex F be Function of Xx\/{x},{1,0} st f2=F & card (F"{1})=k+1 & F.x=1
    by A21;
    then
A29: dom f2=Xx\/{x} by FUNCT_2:def 1;
    for z st z in dom f1 holds f1.z = f2.z
    proof
      let z such that
A30:  z in dom f1;
      now
        per cases by A27,A30,XBOOLE_0:def 3;
        suppose
A31:      z in Xx;
          then z in dom f1/\Xx by A30,XBOOLE_0:def 4;
          then
A32:      (f1|Xx).z=f1.z by FUNCT_1:71;
          z in dom f2/\Xx by A27,A29,A30,A31,XBOOLE_0:def 4;
          hence thesis by A19,A25,A22,A32,FUNCT_1:71;
        end;
        suppose
          z in {x};
          then z=x by TARSKI:def 1;
          hence thesis by A26,A28;
        end;
      end;
      hence thesis;
    end;
    then
A33: f1=f2 by A27,A29,FUNCT_1:9;
    X1={} implies card X1={};
    then dom P1=card X1 by FUNCT_2:def 1;
    hence thesis by A5,A17,A18,A23,A20,A33,FUNCT_1:def 8;
  end;
  then
A34: P1x is one-to-one by FUNCT_1:def 8;
  Xx/\X=Xx by XBOOLE_1:28;
  then
A35: dom (Fy|(dom Fy\{x}))=Xx by A1,RELAT_1:90;
  then dom I=Xx by A1,Th57;
  then consider XFS1 be XFinSequence of NAT such that
A36: dom XFS1=dom P1x and
A37: for z,f st z in dom XFS1 & f=P1x.z holds XFS1.z=card(Intersection(
  I,f,1)) and
A38: Card_Intersection(I,k)=Sum XFS1 by A6,A34,Def4;
A39: addnat "**" XFS1=Card_Intersection(I,k) by A38,AFINSQ_2:63;
  not x in Xx by ZFMISC_1:64;
  then
A40: card Choose(Xx,k+1,1,0)=card X0 by Th16;
  set Ch=Choose(X,k+1,1,0);
  consider P0 be Function of card X0,X0 such that
A41: P0 is one-to-one by Lm3;
A42: X1={} implies card X1={};
  then
A43: dom P1=card X1 by FUNCT_2:def 1;
A44: X0={} implies card X0={};
  then dom P0=card X0 by FUNCT_2:def 1;
  then reconsider XP0=P0,XP1=P1 as XFinSequence by A43,AFINSQ_1:7;
A45: card X0=len XP0 by A44,FUNCT_2:def 1;
  defpred p0[set,set] means ex f st f=P0.$1 & f in X0 & $2=f|Xx;
A46: for x0 be set st x0 in card X0 ex P0x0 be set st P0x0 in Choose(Xx,k+1,1
  ,0) & p0[x0,P0x0]
  proof
    let x0 be set;
    assume x0 in card X0;
    then x0 in dom P0 by CARD_1:47,FUNCT_2:def 1;
    then
A47: P0.x0 in rng P0 by FUNCT_1:def 5;
    then P0.x0 in X0;
    then consider P0x0 be Function of Xx\/{x},{1,0} such that
A48: P0.x0=P0x0 and
A49: card (P0x0"{1})=k+1 and
A50: P0x0.x=0;
A51: dom P0x0=Xx\/{x} by FUNCT_2:def 1;
A52: rng (P0x0|Xx) c= {1,0};
    (Xx\/{x})/\Xx=Xx by XBOOLE_1:7,28;
    then dom (P0x0|Xx)=Xx by A51,RELAT_1:90;
    then reconsider Px=P0x0|Xx as Function of Xx,{1,0} by A52,FUNCT_2:4;
    not x in Xx by ZFMISC_1:64;
    then (Xx\/{x})\{x}=Xx by ZFMISC_1:141;
    then P0x0"{1}=Px"{1} by A50,A51,AFINSQ_2:79;
    then Px in Choose(Xx,k+1,1,0) by A49,Def1;
    hence thesis by A47,A48;
  end;
  consider P0x be Function of card X0,Choose(Xx,k+1,1,0) such that
A53: for x1 st x1 in card X0 holds p0[x1,P0x.x1] from FUNCT_2:sch 1(A46
  );
  rng P0 \/ rng P1 c= X0\/X1 by XBOOLE_1:13;
  then rng (XP0^XP1) c= X0\/X1 by AFINSQ_1:29;
  then
A54: rng (XP0^XP1) c= Ch by A4,Lm1;
A55: card X1=len XP1 by A42,FUNCT_2:def 1;
  for x1,x2 st x1 in dom P0x & x2 in dom P0x & P0x.x1 = P0x.x2 holds x1 = x2
  proof
    let x1,x2 such that
A56: x1 in dom P0x and
A57: x2 in dom P0x and
A58: P0x.x1 = P0x.x2;
    consider f2 be Function such that
A59: f2=P0.x2 and
A60: f2 in X0 and
A61: P0x.x2=f2|Xx by A53,A57;
    consider f1 be Function such that
A62: f1=P0.x1 and
A63: f1 in X0 and
A64: P0x.x1=f1|Xx by A53,A56;
A65: ex F be Function of Xx\/{x},{1,0} st f1=F &card (F"{1})=k+1&F.x=0 by A63;
    then
A66: dom f1=Xx\/{x} by FUNCT_2:def 1;
A67: ex F be Function of Xx\/{x},{1,0} st f2=F & card (F"{1})=k+1 & F.x=0
    by A60;
    then
A68: dom f2=Xx\/{x} by FUNCT_2:def 1;
    for z st z in dom f1 holds f1.z = f2.z
    proof
      let z such that
A69:  z in dom f1;
      now
        per cases by A66,A69,XBOOLE_0:def 3;
        suppose
A70:      z in Xx;
          then z in dom f1/\Xx by A69,XBOOLE_0:def 4;
          then
A71:      (f1|Xx).z=f1.z by FUNCT_1:71;
          z in dom f2/\Xx by A66,A68,A69,A70,XBOOLE_0:def 4;
          hence thesis by A58,A64,A61,A71,FUNCT_1:71;
        end;
        suppose
          z in {x};
          then z=x by TARSKI:def 1;
          hence thesis by A65,A67;
        end;
      end;
      hence thesis;
    end;
    then
A72: f1=f2 by A66,A68,FUNCT_1:9;
    X0={} implies card X0={};
    then dom P0=card X0 by FUNCT_2:def 1;
    hence thesis by A41,A56,A57,A62,A59,A72,FUNCT_1:def 8;
  end;
  then P0x is one-to-one by FUNCT_1:def 8;
  then consider XFS0 be XFinSequence of NAT such that
A73: dom XFS0=dom P0x and
A74: for z,f st z in dom XFS0 & f=P0x.z holds XFS0.z=card(Intersection(
  Fy|(dom Fy\{x}),f,1)) and
A75: Card_Intersection(Fy|(dom Fy\{x}),k+1)=Sum XFS0 by A40,A35,Def4;
  Choose(Xx,k+1,1,0)={} implies card Choose(Xx,k+1,1,0)={};
  then
A76: dom P0x=card X0 by A40,FUNCT_2:def 1;
  not x in Xx by ZFMISC_1:64;
  then card X0+card X1=card Ch by A40,A6,A4,Th17;
  then dom (XP0^XP1)=card Ch by A45,A55,AFINSQ_1:def 4;
  then reconsider XP01=XP0^XP1 as Function of card Ch,Ch by A54,FUNCT_2:4;
  rng P0 misses rng P1 by Lm1,XBOOLE_1:64;
  then XP01 is one-to-one by A41,A5,Th60;
  then consider XFS be XFinSequence of NAT such that
A77: dom XFS=dom XP01 and
A78: for z,f st z in dom XFS & f=XP01.z holds XFS.z=card(Intersection(
  Fy,f,1)) and
A79: Card_Intersection(Fy,k+1)=Sum XFS by A1,Def4;
A80: addnat "**" XFS=Card_Intersection(Fy,k+1) by A79,AFINSQ_2:63;
  Choose(Xx,k,1,0)={} implies card Choose(Xx,k,1,0)={};
  then
A81: dom P1x=card X1 by A6,FUNCT_2:def 1;
A82: for n be Nat st n in dom XFS0 holds XFS.n=XFS0.n
  proof
    let n be Nat such that
A83: n in dom XFS0;
    consider fx be Function such that
A84: fx=P0.n and
A85: fx in X0 and
A86: P0x.n=fx|Xx by A53,A73,A83;
A87: XFS0.n=card(Intersection(Fy|Xx,fx|Xx,1)) by A1,A74,A83,A86;
A88: ex fx9 be Function of Xx\/{x},{1,0} st fx=fx9 & card ( fx9"{1})=k+1 &
    fx9.x=0 by A85;
    then consider x1 such that
A89: x1 in fx"{1} by CARD_1:47,XBOOLE_0:def 1;
    fx.x1 in {1} by A89,FUNCT_1:def 13;
    then
A90: fx.x1=1 by TARSKI:def 1;
    x1 in dom fx by A89,FUNCT_1:def 13;
    then
A91: 1 in rng fx by A90,FUNCT_1:def 5;
A92: Xx\/{x}=X by A1,A2,ZFMISC_1:140;
A93: dom XFS0+0 <=dom XFS0+dom XFS1 by XREAL_1:9;
    dom fx=Xx\/{x} by A88,FUNCT_2:def 1;
    then
A94: fx"{1}=(fx|Xx)"{1} by A88,A92,AFINSQ_2:79;
    n< dom XFS0 by A83,NAT_1:45;
    then n<dom XFS0+dom XFS1 by A93,XXREAL_0:2;
    then n < dom XFS by A73,A36,A45,A55,A77,A76,A81,AFINSQ_1:def 4;
    then
A95: n in dom XFS by NAT_1:45;
    XP01.n=XP0.n by A73,A45,A83,AFINSQ_1:def 4;
    then
A96: XFS.n=card(Intersection(Fy,fx,1)) by A78,A84,A95;
    (fx|Xx)"{1} c= dom (fx|Xx) & dom (fx|Xx)c= Xx by RELAT_1:87,167;
    then Intersection(Fy|Xx,fx,1)=Intersection(Fy,fx,1) by A94,A91,Th32,
XBOOLE_1:1;
    hence thesis by A94,A96,A87,Th30;
  end;
  X1={} implies card X1={};
  then
A97: dom P1=card X1 by FUNCT_2:def 1;
A98: for n be Nat st n in dom XFS1 holds XFS.(len XFS0 + n) = XFS1.n
  proof
A99: Xx\/{x}=X by A1,A2,ZFMISC_1:140;
    let n be Nat such that
A100: n in dom XFS1;
    consider fx be Function such that
A101: fx=P1.n and
A102: fx in X1 and
A103: P1x.n=fx|Xx by A16,A36,A100;
    consider fx9 be Function of Xx\/{x},{1,0} such that
A104: fx=fx9 and
A105: card (fx9"{1})=k+1 and
A106: fx9.x=1 by A102;
A107: Intersection(Intersect(Fy|Xx,Xx-->Fy.x),fx|Xx,1)= Intersection(Fy|Xx
    , fx|Xx,1)/\Fy.x by A1,A35,Th59;
A108: dom fx9=Xx\/{x} by FUNCT_2:def 1;
    then
A109: dom fx=X by A1,A2,A104,ZFMISC_1:140;
A110: 1 in rng (fx|Xx) & (fx|Xx)"{1} c= Xx
    proof
A111: (fx|Xx)"{1} c= dom (fx|Xx) & dom (fx|Xx)=dom fx /\Xx by RELAT_1:90,167;
      reconsider fx1=(fx|Xx)"{1} as finite set;
      not x in Xx by ZFMISC_1:64;
      then not x in dom fx/\Xx by XBOOLE_0:def 4;
      then not x in dom (fx|Xx) by RELAT_1:90;
      then
A112: not x in fx1 by FUNCT_1:def 13;
      {x}\/fx1=fx"{1} by A1,A2,A104,A106,A109,AFINSQ_2:78;
      then card fx1+1=k+1 by A104,A105,A112,CARD_2:54;
      then consider y such that
A113: y in fx1 by A3,CARD_1:47,XBOOLE_0:def 1;
      y in dom (fx|Xx) by A113,FUNCT_1:def 13;
      then
A114: (fx|Xx).y in rng (fx|Xx) by FUNCT_1:def 5;
      dom fx /\Xx c= Xx & (fx|Xx).y in {1} by A113,FUNCT_1:def 13,XBOOLE_1:17;
      hence thesis by A111,A114,TARSKI:def 1,XBOOLE_1:1;
    end;
    n< dom XFS1 by A100,NAT_1:45;
    then dom XFS0+n < dom XFS0+dom XFS1 by XREAL_1:10;
    then dom XFS0+n < dom XFS by A73,A36,A45,A55,A77,A76,A81,AFINSQ_1:def 4;
    then
A115: dom XFS0+n in dom XFS by NAT_1:45;
    XP01.(n+len XP0)=fx by A36,A97,A100,A101,AFINSQ_1:def 4;
    then
A116: XFS.(dom XFS0+n)=card(Intersection(Fy,fx,1)) by A73,A45,A78,A76,A115;
    fx.x in {1} by A104,A106,TARSKI:def 1;
    then
A117: x in fx"{1} by A1,A2,A104,A108,A99,FUNCT_1:def 13;
    XFS1.n=card(Intersection (Intersect(Fy|Xx,Xx-->Fy.x),fx|Xx,1)) by A1,A37
,A100,A103;
    then XFS1.n=card(Intersection(Fy,fx|Xx,1)/\Fy.x) by A110,A107,Th32;
    hence thesis by A117,A109,A116,Th34;
  end;
  dom XFS=len XFS0 + len XFS1 by A73,A36,A45,A55,A77,A76,A81,AFINSQ_1:def 4;
  then XFS=XFS0^XFS1 by A82,A98,AFINSQ_1:def 4;
  then
A118: addnat "**" XFS=addnat.(addnat "**" XFS0,addnat "**" XFS1) by AFINSQ_2:54
;
  addnat "**" XFS0=Card_Intersection(Fy|(dom Fy\{x}),k+1) by A75,AFINSQ_2:63
;
  hence thesis by A118,A39,A80,BINOP_2:def 23;
end;

canceled 3;

theorem Th65:
  x in dom F implies union rng F=union rng (F|(dom F\{x}))\/F.x
proof
  set d=dom F\{x};
  set Fd=F|d;
A1: F|dom F=F by RELAT_1:98;
  assume
A2: x in dom F;
  then d\/{x}=dom F by ZFMISC_1:140;
  then F=Fd\/(F|{x}) by A1,RELAT_1:107;
  then
A3: rng F= rng Fd\/rng (F|{x}) by RELAT_1:26;
  Im(F,x)={F.x} by A2,FUNCT_1:117;
  then rng (F|{x})={F.x} by RELAT_1:148;
  then union rng F=union rng Fd \/union {F.x} by A3,ZFMISC_1:96;
  hence thesis by ZFMISC_1:31;
end;

theorem Th66:
  for Fy be finite-yielding Function,X ex XFS be XFinSequence of
  INT st dom XFS = card X & for n st n in dom XFS holds XFS.n=((-1)|^n)*
  Card_Intersection(Fy,n+1)
proof
  let Fy be finite-yielding Function,X;
  defpred P[set,set] means for n st n=$1 holds $2=((-1)|^n)*Card_Intersection(
  Fy,n+1);
A1: for k st k in card X ex x be Element of INT st P[k,x]
  proof
    let k such that
    k in card X;
    reconsider C=((-1)|^k)*Card_Intersection(Fy,k+1) as Element of INT;
    take C;
    thus thesis;
  end;
  consider XFS be XFinSequence of INT such that
A2: dom XFS = card X & for k st k in card X holds P[k,XFS.k] from
  STIRL2_1:sch 5(A1);
  take XFS;
  thus thesis by A2;
end;
 
:: The principle of inclusions and the disconnections

theorem Th67:
  for Fy be finite-yielding Function,X st dom Fy=X for XFS be
XFinSequence of INT st dom XFS= card X & for n st n in dom XFS holds XFS.n=((-1
  )|^n)*Card_Intersection(Fy,n+1) holds card union rng Fy=Sum XFS
proof
  defpred P[Nat] means for Fy be finite-yielding Function,X st dom
Fy=X & card X=$1 for XFS be XFinSequence of INT st dom XFS= card X & for n st n
in dom XFS holds XFS.n=((-1)|^n)*Card_Intersection(Fy,n+1) holds card union rng
  Fy=Sum XFS;
A1: for k st P[k] holds P[k+1]
  proof
    let k such that
A2: P[k];
    let Fy be finite-yielding Function,X such that
A3: dom Fy=X and
A4: card X=k+1;
    rng Fy is finite & for x st x in rng Fy holds x is finite
    proof
      thus rng Fy is finite by A3,FINSET_1:26;
      let x;
      assume x in rng Fy;
      then ex x1 st x1 in dom Fy & Fy.x1=x by FUNCT_1:def 5;
      hence thesis;
    end;
    then reconsider urngFy=union rng Fy as finite set by FINSET_1:25;
    let XFS be XFinSequence of INT such that
A5: dom XFS=card X and
A6: for n st n in dom XFS holds XFS.n=((-1)|^n)*Card_Intersection(Fy, n+1);
    per cases;
    suppose
A7:   k=0;
      then len XFS=1 by A4,A5;
      then
A8:   XFS=<%XFS.0%> by AFINSQ_1:38;
      XFS.0 is Element of INT by INT_1:def 2;
      then
A9:   addint "**" XFS=XFS.0 by A8,AFINSQ_2:49;
      0 in dom XFS by A4,A5,A7,CARD_1:87,TARSKI:def 1;
      then
A10:  XFS.0=((-1)|^0)*Card_Intersection(Fy, 0+1) by A6;
      consider x such that
A11:  dom Fy={x} by A3,A4,A7,CARD_2:60;
A12:  rng Fy={Fy.x} by A11,FUNCT_1:14;
      then
A13:  union rng Fy= Fy.x by ZFMISC_1:31;
      (-1)|^0=1 & Fy=x.-->Fy.x by A11,A12,FUNCOP_1:15,NEWTON:9;
      then XFS.0= card union rng Fy by Th54,A13,A10;
      hence thesis by A9,AFINSQ_2:62;
    end;
    suppose
A14:  k>0;
      consider x such that
A15:  x in dom Fy by A3,A4,CARD_1:47,XBOOLE_0:def 1;
      set Xx=X\{x};
A16:  card Xx=k by A3,A4,A15,STIRL2_1:65;
      set FyX=Fy|Xx;
      urngFy=union rng FyX\/Fy.x by A3,A15,Th65;
      then reconsider urngFyX=union rng FyX as finite set by FINSET_1:13
,XBOOLE_1:7;
      set Fyx=Fy.x;
      set I=Intersect(FyX,dom FyX-->Fy.x);
      consider XFyX be XFinSequence of INT such that
A17:  dom XFyX = card Xx and
A18:  for n st n in dom XFyX holds XFyX.n=((-1)|^n)*Card_Intersection
      (FyX,n+1) by Th66;
      urngFyX/\Fy.x=union rng I by Th58;
      then reconsider urngI=union rng I as finite set;
      consider XI be XFinSequence of INT such that
A19:  dom XI = card Xx and
A20:  for n st n in dom XI holds XI.n=((-1)|^n)*Card_Intersection(I,n
      +1) by Th66;
set XI1 = (-1)(#)XI;
 B:     rng XI1 c= INT by VALUED_0:def 5;
      then reconsider XI1 as XFinSequence of INT by RELAT_1:def 19;
   BBB:card Fyx is Element of INT by INT_1:def 2;
      then
  rng <%card Fyx%> ={card Fyx} & {card Fyx} is Subset of INT by AFINSQ_1:36
,SUBSET_1:55;
       then rng <%card Fyx%>\/rng XI1 c= INT by B,XBOOLE_1:8;
      then
A25:  rng (<%card Fyx%>^XI1) c= INT by AFINSQ_1:29;
      E:0 is Element of INT by INT_1:def 2;
      then
A26:  rng <%0%> ={0} & {0} is Subset of INT by AFINSQ_1:36,SUBSET_1:55;
      reconsider XcF=<%card Fyx%>,X0=<%0%> as XFinSequence of INT
         by BBB
,E;
      rng XFyX c= INT by RELAT_1:def 19;
      then rng XFyX \/rng <%0%> c= INT by A26,XBOOLE_1:8;
      then rng (XFyX^<%0%>) c= INT by AFINSQ_1:29;
      then reconsider
      F1=<%card Fyx%>^XI1,F2=XFyX^<%0%> as XFinSequence of INT by A25,
RELAT_1:def 19;
A27:  card Xx=k by A3,A4,A15,STIRL2_1:65;
      0 is Element of INT by INT_1:def 2;
      then Y1:addint "**" X0 = 0 by AFINSQ_2:49;
      card Fyx is Element of INT by INT_1:def 2;
      then HG:addint "**" XcF = card Fyx by AFINSQ_2:49;
A30:  (-1)*Sum XI=Sum XI1 by AFINSQ_2:76;
B2:addint "**" F1 = addint.(card Fyx,addint "**" XI1) by HG,AFINSQ_2:54
       .= card Fyx+(addint "**" XI1) by BINOP_2:def 20
      .= card Fyx+Sum XI1 by AFINSQ_2:62;
B3:(addint "**" F2)=addint.(addint "**" XFyX,0) by Y1,AFINSQ_2:54
           .= addint "**" XFyX +0 by BINOP_2:def 20
           .=Sum XFyX  by AFINSQ_2:62;
A32:      Sum (F1^F2) = (Sum F1) + Sum F2 by AFINSQ_2:67
            .= (addint "**" F1) + Sum F2 by AFINSQ_2:62
        .= card Fyx+(-1)*Sum XI+Sum XFyX  by AFINSQ_2:62,A30,B2,B3;
A33:  urngFyX\/Fyx = urngFy by A3,A15,Th65;
A34:  urngFyX/\Fyx=urngI by Th58;
A35:  dom FyX=Xx by A3,RELAT_1:91;
      then dom I=Xx by Th57;
      then
A36:  card urngI=Sum XI by A2,A19,A20,A27;
      len <%card Fyx%>=1 & len XI1=card Xx by AFINSQ_1:36,VALUED_1:def 5,A19;
      then
A37:  len F1=k+1 by A16,AFINSQ_1:20;
A38:  for n be Nat st n in dom XFS holds XFS.n=addint.(F1.n,F2.n)
      proof
        let n be Nat such that
A39:    n in dom XFS;
        reconsider N=n as Element of NAT by ORDINAL1:def 13;
        per cases;
        suppose
A40:      n=0;
          0 in k & k=dom XFyX by A3,A4,A14,A15,A17,NAT_1:45,STIRL2_1:65;
          then
A41:      F2.0=XFyX.0 & XFyX.0=((-1)|^0)*Card_Intersection(FyX,0+1) by A18,
AFINSQ_1:def 4;
          F1.0=card Fyx & (-1)|^0=1 by AFINSQ_1:39,NEWTON:9;
          then
A42:      addint.(F1.0,F2.0)=card Fyx+Card_Intersection(FyX,0+1) by A41,
BINOP_2:def 20;
A43:      (-1)|^0=1 by NEWTON:9;
          XFS.0=((-1)|^0)*Card_Intersection(Fy,0+1) by A6,A39,A40;
          hence thesis by A3,A15,A40,A42,A43,Th56;
        end;
        suppose
A44:      n>0;
          then reconsider n1=n-1 as Element of NAT by NAT_1:20;
A45:      len <%card Fyx%>=1 by AFINSQ_1:36;
A46:      card Xx=k by A3,A4,A15,STIRL2_1:65;
A47:      n<k+1 by A4,A5,A39,NAT_1:45;
          then
A48:      n<=k by NAT_1:13;
A49:      n1<n1+1 by NAT_1:13;
          then n1 <k by A48,XXREAL_0:2;
          then n1 in dom XI by A19,A46,NAT_1:45;
          then
A50:      XI1.n1=(-1)*XI.n1 & XI.n1=((-1)|^n1)*Card_Intersection(I,n1+1)
          by A20,VALUED_1:6;
          0+1<=n by A49,NAT_1:13;
          then F1.n=((-1)*((-1)|^n1))*Card_Intersection(I,n1+1) by A37,A47,A45
,A50,AFINSQ_1:22;
          then
A51:      F1.n=((-1)|^(n1+1))*Card_Intersection(I,n1+1) by NEWTON:11;
A52:      XFS.n=((-1)|^n)*Card_Intersection(Fy,n+1) by A6,A39;
          Card_Intersection(Fy,n+1)= Card_Intersection(FyX,n+1)+
          Card_Intersection (I,N) by A3,A15,A35,A44,Th61;
          then
A53:      XFS.n=((-1)|^n)*Card_Intersection(FyX,n+1)+ ((-1)|^n)*
          Card_Intersection(I,N) by A52;
          per cases by A48,XXREAL_0:1;
          suppose
            n<k;
            then
A54:        n in k by NAT_1:45;
            card Xx=k by A3,A4,A15,STIRL2_1:65;
            then XFyX.n=((-1)|^n)*Card_Intersection(FyX,n+1) & F2.n=XFyX.n by
A17,A18,A54,AFINSQ_1:def 4;
            hence thesis by A53,A51,BINOP_2:def 20;
          end;
          suppose
A55:        n=k;
            then n=card Xx by A3,A4,A15,STIRL2_1:65;
            then n+1>card Xx by NAT_1:13;
            then
A56:        Card_Intersection(FyX,n+1)=0 by A35,Th51;
            n=len XFyX by A3,A4,A15,A17,A55,STIRL2_1:65;
            then F2.n=0 by AFINSQ_1:40;
            hence thesis by A53,A51,A56,BINOP_2:def 20;
          end;
        end;
      end;
      card urngFyX=Sum XFyX by A2,A35,A17,A18,A27;
      then
A57:  card urngFy= Sum XFyX+card Fyx-Sum XI by A36,A33,A34,CARD_2:64;
A58:  len <%0%>=1 by AFINSQ_1:36;
      len XFyX=card Xx by A17;
      then
A59:  len F2=k+1 by A58,A16,AFINSQ_1:20;
A60: len XFS=k+1 by A4,A5;
     Sum XFS = addint "**" XFS by AFINSQ_2:62
           .= addint "**" (F1^F2)
                        by A37,A59,A38,AFINSQ_2:58,A60
                      .= Sum (F1^F2) by AFINSQ_2:62;
      hence thesis by A32,A57;
    end;
  end;
A60: P[0]
  proof
    let Fy be finite-yielding Function,X such that
A61: dom Fy=X and
A62: card X=0;
    dom Fy={} by A61,A62;
    then
A63: rng Fy={} by RELAT_1:65;
    let XFS be XFinSequence of INT such that
A64: dom XFS=card X and
    for n st n in dom XFS holds XFS.n=((-1)|^n)*Card_Intersection(Fy,n+1);
    len XFS=0 by A62,A64;
    then addint "**" XFS = the_unity_wrt addint by AFINSQ_2:def 9
            .= 0  by BINOP_2:4;
    hence thesis by A63,ZFMISC_1:2,AFINSQ_2:62,A62;
  end;
  for k holds P[k] from NAT_1:sch 2(A60,A1);
  hence thesis;
end;

theorem Th68:
  for Fy,X,n,k st dom Fy=X holds (ex x,y st x<>y & for f st f in
Choose(X,k,x,y) holds card(Intersection(Fy,f,x))=n) implies Card_Intersection(
  Fy,k)=n*(card X choose k)
proof
  let Fy,X,n,k such that
A1: X=dom Fy;
  assume ex x,y st x<>y & for f st f in Choose(X,k,x,y) holds card(
  Intersection(Fy,f,x))=n;
  then consider x,y such that
A2: x<>y and
A3: for f st f in Choose(X,k,x,y) holds card(Intersection(Fy,f,x))=n;
  set Ch=Choose(X,k,x,y);
  consider P be Function of card Ch,Ch such that
A4: P is one-to-one by Lm3;
  consider XFS be XFinSequence of NAT such that
A5: dom XFS=dom P and
A6: for z,f st z in dom XFS & f=P.z holds XFS.z=card(Intersection(Fy,f,x )) and
A7: Card_Intersection(Fy,k)=Sum XFS by A1,A2,A4,Def4;
  for z st z in dom XFS holds XFS.z = n
  proof
    let z such that
A8: z in dom XFS;
A9: P.z in rng P by A5,A8,FUNCT_1:def 5;
    then consider f be Function of X,{x,y} such that
A10: f=P.z and
    card (f"{x})=k by Def1;
    XFS.z=card(Intersection(Fy,f,x)) by A6,A8,A10;
    hence thesis by A3,A9,A10;
  end;
  then
A11: XFS=dom XFS-->n by FUNCOP_1:17;
  then
A12: rng XFS c= {n} by FUNCOP_1:19;
  Ch={} implies card Ch={};
  then
A13: dom P=card Ch by FUNCT_2:def 1;
  n in {n} by TARSKI:def 1;
  then {n} c= {0,n} & XFS"{n}=dom P by A5,A11,FUNCOP_1:20,ZFMISC_1:12;
  then Sum XFS=n*card card Ch by A12,A13,AFINSQ_2:80,XBOOLE_1:1;
  hence thesis by A2,A7,Th18;
end;

theorem Th69:
  for Fy,X st dom Fy=X for XF be XFinSequence of NAT st dom XF=
card X & for n st n in dom XF holds ex x,y st x<>y & for f st f in Choose(X,n+1
  ,x,y) holds card(Intersection(Fy,f,x))=XF.n holds ex F be XFinSequence of INT
st dom F=card X & card union rng Fy = Sum F & for n st n in dom F holds F.n=((-
  1)|^n)*XF.n*(card X choose (n+1))
proof
  let Fy,X such that
A1: dom Fy=X;
  let XF be XFinSequence of NAT such that
A2: dom XF=card X & for n st n in dom XF holds ex x,y st x<>y & for f st
  f in Choose(X,n+1,x,y) holds card(Intersection(Fy,f,x))=XF.n;
  defpred f[set,set] means for n st n=$1 holds $2=((-1)|^n)*(XF.n)*(card X
  choose (n+1));
A3: for x st x in card X ex y st y in INT & f[x,y]
  proof
A4: card X is Subset of NAT by STIRL2_1:8;
    let x;
    assume x in card X;
    then reconsider x9=x as Element of NAT by A4;
    reconsider xx=((-1)|^x9)*(XF.x9) as Integer;
    reconsider ch=card X choose (x9+1) as Integer;
    take xx*ch;
    thus thesis;
  end;
  consider F be Function of card X,INT such that
A5: for x st x in card X holds f[x,F.x] from FUNCT_2:sch 1(A3);
A6: dom F =card X by FUNCT_2:def 1;
  then reconsider F as XFinSequence by AFINSQ_1:7;
  reconsider F as XFinSequence of INT;
  take F;
  for n st n in dom F holds F.n=((-1)|^n)*Card_Intersection(Fy,n+1)
  proof
    let n such that
A7: n in dom F;
    ex x,y st x<>y & for f st f in Choose(X,n+1,x,y) holds card(
    Intersection(Fy,f,x))=XF.n by A2,A6,A7;
    then
A8: Card_Intersection(Fy,n+1)=(XF.n)*(card X choose (n+1)) by A1,Th68;
    F.n=((-1)|^n)*(XF.n)*(card X choose (n+1)) by A5,A6,A7;
    hence thesis by A8;
  end;
  hence thesis by A1,A5,A6,Th67;
end;

Lm4: for X,Y be finite set st X is non empty & Y is non empty ex F be
XFinSequence of INT st dom F = card Y & (card Y|^ card X)-Sum F = card {f where
f is Function of X,Y:f is onto} & for n st n in dom F holds F.n=((-1)|^n)*(card
Y choose (n+1))*((card Y-n-1) |^ card X)
proof
  let X,Y be finite set such that
A1: X is non empty and
A2: Y is non empty;
  defpred xf[set,set] means for n st n=$1 holds $2=(card Y-n-1) |^ card X;
A3: for x st x in card Y ex y st y in NAT & xf[x,y]
  proof
    let x such that
A4: x in card Y;
    card Y is Subset of NAT by STIRL2_1:8;
    then reconsider n=x as Element of NAT by A4;
    n<card Y by A4,NAT_1:45;
    then n+1<=card Y by NAT_1:13;
    then reconsider k=(card Y)-(n+1) as Element of NAT by NAT_1:21;
    xf[n, k|^ card X];
    hence thesis;
  end;
  consider XF be Function of card Y,NAT such that
A5: for x st x in card Y holds xf[x,XF.x] from FUNCT_2:sch 1(A3);
  set Onto={f where f is Function of X,Y:f is onto};
  deffunc fy(set)={f where f is Function of X,Y:not $1 in rng f};
A6: for x st x in Y holds fy(x) in bool Funcs(X,Y)
  proof
    let x such that
A7: x in Y;
    fy(x) c= Funcs(X,Y)
    proof
      let y;
      assume y in fy(x);
      then ex f be Function of X,Y st y=f & not x in rng f;
      hence thesis by A7,FUNCT_2:11;
    end;
    hence thesis;
  end;
  consider Fy9 be Function of Y,bool Funcs(X,Y) such that
A8: for x st x in Y holds Fy9.x = fy(x) from FUNCT_2:sch 2(A6);
  for y holds Fy9.y is finite
  proof
    let y;
    per cases;
    suppose
      y in dom Fy9;
      then Fy9.y in rng Fy9 by FUNCT_1:def 5;
      hence thesis;
    end;
    suppose
      not y in dom Fy9;
      hence thesis by FUNCT_1:def 4;
    end;
  end;
  then reconsider Fy=Fy9 as finite-yielding Function by Def3;
  union rng Fy9 c= union bool Funcs(X,Y) by ZFMISC_1:95;
  then
A9: union rng Fy c= Funcs(X,Y) by ZFMISC_1:99;
  then reconsider u=union rng Fy as finite set;
A10: dom XF=card Y by FUNCT_2:def 1;
  then reconsider XF as XFinSequence by AFINSQ_1:7;
  reconsider XF as XFinSequence of NAT;
A11: for n st n in dom XF holds ex x,y st x<>y & for f st f in Choose(Y,n+1,
  x,y) holds card(Intersection(Fy,f,x))=XF.n
  proof
    let n such that
A12: n in dom XF;
    take 0,1;
    thus 0<>1;
    let f9 be Function;
    assume f9 in Choose(Y,n+1,0,1);
    then consider f be Function of Y,{0,1} such that
A13: f = f9 and
A14: card (f"{0})=n+1 by Def1;
A15: Intersection(Fy,f,0) c= Funcs(X,Y\f"{0})
    proof
      let z such that
A16:  z in Intersection(Fy,f,0);
      0 in rng f by A14,CARD_1:47,FUNCT_1:142;
      then consider x1 such that
A17:  x1 in dom f and
      f.x1=0 and
A18:  z in Fy.x1 by A16,Th24;
      z in fy(x1) by A8,A17,A18;
      then consider g be Function of X,Y such that
A19:  z=g and
      not x1 in rng g;
A20:  rng g c= Y\f"{0}
      proof
        let gy be set such that
A21:    gy in rng g;
        assume not gy in Y\f"{0};
        then
A22:    gy in f"{0} by A21,XBOOLE_0:def 5;
        then f.gy in {0} by FUNCT_1:def 13;
        then
A23:    f.gy =0 by TARSKI:def 1;
        gy in dom f by A22,FUNCT_1:def 13;
        then g in Fy.gy by A16,A19,A23,Def2;
        then g in fy(gy) by A8,A21;
        then ex h be Function of X,Y st g=h & not gy in rng h;
        hence contradiction by A21;
      end;
      dom g=X by A17,FUNCT_2:def 1;
      hence thesis by A19,A20,FUNCT_2:def 2;
    end;
    then reconsider I=Intersection(Fy,f,0) as finite set;
A24: card (Y\f"{0})=card Y-(n+1) by A14,CARD_2:63;
    Funcs(X,Y\f"{0}) c= Intersection(Fy,f,0)
    proof
      let g9 be set;
      assume g9 in Funcs(X,Y\f"{0});
      then consider g be Function such that
A25:  g9 = g and
A26:  dom g = X and
A27:  rng g c= Y\f"{0} by FUNCT_2:def 2;
      reconsider gg=g as Function of X,Y by A26,A27,FUNCT_2:4,XBOOLE_1:1;
      consider y such that
A28:  y in f"{0} by A14,CARD_1:47,XBOOLE_0:def 1;
      not y in rng g by A27,A28,XBOOLE_0:def 5;
      then
A29:  gg in fy(y);
      dom Fy=Y by FUNCT_2:def 1;
      then
A30:  Fy9.y in rng Fy9 by A28,FUNCT_1:def 5;
A31:  for z st z in dom f & f.z=0 holds g in Fy.z
      proof
        let z such that
A32:    z in dom f and
A33:    f.z=0;
        f.z in {0} by A33,TARSKI:def 1;
        then z in f"{0} by A32,FUNCT_1:def 13;
        then
A34:    not z in rng gg by A27,XBOOLE_0:def 5;
        Fy.z=fy(z) by A8,A32;
        hence thesis by A34;
      end;
      fy(y)=Fy9.y by A8,A28;
      then g in union rng Fy by A30,A29,TARSKI:def 4;
      hence thesis by A25,A31,Def2;
    end;
    then
A35: Funcs(X,Y\f"{0}) = Intersection(Fy,f,0) by A15,XBOOLE_0:def 10;
    now
      per cases;
      suppose
        Y\f"{0}={};
        then card I = 0 & (card Y - n-1)|^card X =0 by A1,A15,A24,CARD_1:47
,NAT_1:14,NEWTON:16;
        hence thesis by A5,A10,A12,A13;
      end;
      suppose
A36:    Y\f"{0} <>{};
        XF.n=(card Y-n-1) |^ card X by A5,A10,A12;
        hence thesis by A13,A35,A24,A36,Th4;
      end;
    end;
    hence thesis;
  end;
  dom XF= card Y & dom Fy = Y by FUNCT_2:def 1;
  then consider F be XFinSequence of INT such that
A37: dom F=card Y and
A38: card union rng Fy = Sum F and
A39: for n st n in dom F holds F.n=((-1)|^n)*XF.n*(card Y choose (n+1))
  by A11,Th69;
  take F;
  thus dom F = card Y by A37;
A40: card(Funcs(X,Y)\u) = card Funcs(X,Y) - card u by A9,CARD_2:63;
A41: Onto c= Funcs(X,Y) \ union rng Fy
  proof
    let x;
    assume x in Onto;
    then consider f be Function of X,Y such that
A42: x=f and
A43: f is onto;
    assume
A44: not x in Funcs(X,Y) \ union rng Fy;
    f in Funcs(X,Y) by A2,FUNCT_2:11;
    then f in union rng Fy by A42,A44,XBOOLE_0:def 5;
    then consider Fyy be set such that
A45: f in Fyy and
A46: Fyy in rng Fy by TARSKI:def 4;
    consider y such that
A47: y in dom Fy and
A48: Fy.y=Fyy by A46,FUNCT_1:def 5;
    y in Y by A47,FUNCT_2:def 1;
    then f in fy(y) by A8,A45,A48;
    then
A49: ex g be Function of X,Y st f=g & not y in rng g;
    y in Y by A47,FUNCT_2:def 1;
    hence contradiction by A43,A49,FUNCT_2:def 3;
  end;
A50: Funcs(X,Y) \ union rng Fy c= Onto
  proof
    let x such that
A51: x in Funcs(X,Y) \ union rng Fy;
    consider f such that
A52: x = f and
A53: dom f = X & rng f c= Y by A51,FUNCT_2:def 2;
    reconsider f as Function of X,Y by A53,FUNCT_2:4;
    assume not x in Onto;
    then not f is onto by A52;
    then rng f<>Y by FUNCT_2:def 3;
    then not Y c= rng f by XBOOLE_0:def 10;
    then consider y such that
A54: y in Y and
A55: not y in rng f by TARSKI:def 3;
    y in dom Fy9 by A54,FUNCT_2:def 1;
    then Fy9.y in rng Fy9 by FUNCT_1:def 5;
    then
A56: fy(y) in rng Fy9 by A8,A54;
    f in fy(y) by A55;
    then f in union rng Fy by A56,TARSKI:def 4;
    hence thesis by A51,A52,XBOOLE_0:def 5;
  end;
  card Funcs(X,Y) =card Y |^ card X by A2,Th4;
  hence card Onto=(card Y |^ card X) - Sum F by A38,A50,A41,A40,XBOOLE_0:def 10
;
  let n such that
A57: n in dom F;
A58: F.n=((-1)|^n)*XF.n*(card Y choose (n+1)) by A39,A57;
  XF.n=(card Y-n-1) |^ card X by A5,A37,A57;
  hence thesis by A58;
end;
 
:: onto

theorem Th70:
  for X,Y be finite set st X is non empty & Y is non empty ex F be
  XFinSequence of INT st dom F = card Y+1 & Sum F = card {f where f is Function
of X,Y:f is onto} & for n st n in dom F holds F.n=((-1)|^n)*(card Y choose n)*(
  (card Y-n) |^ card X)
proof
  let X,Y be finite set such that
A1: X is non empty & Y is non empty;
  reconsider c = card Y|^ card X as Element of INT by INT_1:def 2;
A2: len <%c%>=1 by AFINSQ_1:36;
  set Onto= {f where f is Function of X,Y:f is onto};
  consider F be XFinSequence of INT such that
A3: dom F = card Y and
A4: (card Y|^ card X)-Sum F=card Onto and
A5: for n st n in dom F holds F.n=((-1)|^n)*(card Y choose (n+1))*((card
  Y-n-1) |^ card X) by A1,Lm4;
  set F1 = (-1)(#)F;
  rng F1 c= INT by VALUED_0:def 5;
  then reconsider F1 as XFinSequence of INT by RELAT_1:def 19;
A7:  dom F1 = dom F & dom F = card Y by A3,VALUED_1:def 5;
  reconsider GF1=<%c%>^F1 as XFinSequence of INT;
  take GF1;
  len F1=card Y by A3,VALUED_1:def 5;
  hence
A9: dom GF1 = card Y +1 by A2,AFINSQ_1:def 4;
  (-1)*Sum F =Sum F1 by AFINSQ_2:76;
  then c -Sum F=c +Sum F1 .=addint.(c,Sum F1) by BINOP_2:def 20
    .=addint.(addint "**" <%c%>,Sum F1) by AFINSQ_2:49
   .=addint.(addint "**" <%c%>,addint "**" F1) by AFINSQ_2:62
    .=addint "**" GF1 by AFINSQ_2:54
    .=Sum GF1 by AFINSQ_2:62;
  hence Sum GF1=card Onto by A4;
  let n such that
A10: n in dom GF1;
  now
    per cases;
    suppose
A11:  n=0;
      then ((-1)|^n)=1 & (card Y choose n) =1 by NEWTON:9,29;
      hence thesis by A11,AFINSQ_1:39;
    end;
    suppose
      n>0;
      then reconsider n1=n-1 as Element of NAT by NAT_1:20;
      n < card Y +1 by A9,A10,NAT_1:45;
      then n1+1 <= card Y by NAT_1:13;
      then n1 < card Y by NAT_1:13;
      then
A12:  n1 in dom F1 by A7,NAT_1:45;
      then
A13:  F.n1=((-1)|^n1)*(card Y choose (n1+1))*((card Y-n1-1)|^ card X) by A5
,A7;
      len <% c %>=1 by AFINSQ_1:36;
      then
A14:  GF1.(n1+1)=F1.n1 by A12,AFINSQ_1:def 4;
      then
A15:  (-1)*((-1)|^n1)=(-1)|^n by NEWTON:11;
      GF1.(n1+1)=(-1)*F.n1 by VALUED_1:6,A14;
      hence thesis by A13,A15;
    end;
  end;
  hence thesis;
end;
 
:: n block k

theorem
  for n,k st k <= n ex F be XFinSequence of INT st n block k = 1/(k!) *
Sum F & dom F = k+1 & for m st m in dom F holds F.m=((-1)|^m)*(k choose m)*((k-
  m) |^ n)
proof
  let n,k such that
A1: k <= n;
  now
    per cases;
    suppose
A2:   n=0 & k=0;
      reconsider I=1 as Element of INT by INT_1:def 2;
      set F=<%I%>;
      take F;
      addint "**" <%I%>=1 by AFINSQ_2:49;
      then Sum F =  1 by AFINSQ_2:62;
      hence n block k= 1/(k!) * Sum F by A2,NEWTON:18,STIRL2_1:26;
      thus dom F=k+1 by A2,AFINSQ_1:36;
      let m;
      assume m in dom F;
      then
A3:   m in {0} by AFINSQ_1:36,CARD_1:87;
      then m=0 by TARSKI:def 1;
      then
A4:   (-1) |^m=1 by NEWTON:9;
A5:   ((k-m) |^n)=1 by A2,NEWTON:9;
X:    0 choose 0 = 1 by NEWTON:29;
      m=0 by A3,TARSKI:def 1;
      hence F.m=((-1) |^m)*(k choose m)*((k-m) |^n) by A2,A4,A5,AFINSQ_1:38
,X;
    end;
    suppose
A6:   n<>0 & k=0;
      set F=k+1 -->0;
      0 is Element of INT by INT_1:def 2;
      then
   rng F c= {0} & {0} c= INT by FUNCOP_1:19,ZFMISC_1:37;
      then rng F c= INT by XBOOLE_1:1;
      then reconsider Fi=F as XFinSequence of INT by RELAT_1:def 19;
      reconsider Fn=F as XFinSequence of NAT;
      take Fi;
      rng F c={0} & {0} c={0,0} by ENUMSET1:69,FUNCOP_1:19;
      then
   Sum Fn= 0*card (Fn"{0}) by AFINSQ_2:80,XBOOLE_1:1;
      hence n block k = 1/(k!) * Sum Fi & dom Fi=k+1 by A6,FUNCOP_1:19
,STIRL2_1:31;
      let m such that
A10:  m in dom Fi;
      now
        per cases;
        suppose
          m=0;
          then (k-m) |^ n=0 by A6,NAT_1:14,NEWTON:16;
          hence (k choose m)*((k-m) |^ n)=0;
        end;
        suppose
          m>0;
          then (k choose m)=0 by A6,NEWTON:def 3;
          hence (k choose m)*((k-m) |^ n)=0;
        end;
      end;
      then
A11:  ((-1)|^m)*((k choose m)*((k-m) |^ n))=0;
      m in k+1 by A10,FUNCOP_1:19;
      hence Fi.m=((-1)|^m)*(k choose m)*((k-m) |^ n) by A11,FUNCOP_1:13;
    end;
    suppose
A12:  n<>0 & k<>0;
      set Perm={p where p is Function of k,k:p is Permutation of k};
      card Perm=(card k)! by Th8;
      then reconsider Perm as finite set;
      reconsider Bloc={f where f is Function of n,k:f is onto "increasing} as
      finite set by STIRL2_1:24;
      set Onto={f where f is Function of n,k:f is onto};
      defpred P[set,set] means for p be Function of k,k, f be Function of n,k
      st $1=[p,f] holds $2=p*f;
      reconsider N=n,K=k as non empty Subset of NAT by A12,STIRL2_1:8;
A13:  card [:Perm,Bloc:]= card Perm * card Bloc by CARD_2:65;
A14:  for x st x in [:Perm,Bloc:] ex y st y in Onto & P[x,y]
      proof
        let x;
        assume x in [:Perm,Bloc:];
        then consider p9,f9 be set such that
A15:    p9 in Perm and
A16:    f9 in Bloc and
A17:    x=[p9,f9] by ZFMISC_1:def 2;
        consider f be Function of n,k such that
A18:    f=f9 and
A19:    f is onto "increasing by A16;
A20:    rng f=k by A19,FUNCT_2:def 3;
        consider p be Function of k,k such that
A21:    p=p9 and
A22:    p is Permutation of k by A15;
        reconsider pf=p*f as Function of n,k;
        take pf;
A23:    dom p=k by A12,FUNCT_2:def 1;
        rng p=k by A22,FUNCT_2:def 3;
        then rng (p*f)=k by A20,A23,RELAT_1:47;
        then pf is onto by FUNCT_2:def 3;
        hence pf in Onto;
        let p1 be Function of k,k, f1 be Function of n,k such that
A24:    x=[p1,f1];
        p1=p by A17,A21,A24,ZFMISC_1:33;
        hence thesis by A17,A18,A24,ZFMISC_1:33;
      end;
      consider FP be Function of [:Perm,Bloc:],Onto such that
A25:  for x st x in [:Perm,Bloc:] holds P[x,FP.x] from FUNCT_2:sch 1(
      A14);
A26:  FP is one-to-one
      proof
        let x1,x2 such that
A27:    x1 in dom FP and
A28:    x2 in dom FP and
A29:    FP.x1 = FP.x2;
        consider p19,f19 be set such that
A30:    p19 in Perm and
A31:    f19 in Bloc and
A32:    x1=[p19,f19] by A27,ZFMISC_1:def 2;
        consider p1 be Function of k,k such that
A33:    p19=p1 and
A34:    p1 is Permutation of k by A30;
        consider p29,f29 be set such that
A35:    p29 in Perm and
A36:    f29 in Bloc and
A37:    x2=[p29,f29] by A28,ZFMISC_1:def 2;
        FP.x1 in rng FP by A27,FUNCT_1:def 5;
        then FP.x1 in Onto;
        then consider fp be Function of N,K such that
A38:    FP.x1=fp and
A39:    fp is onto;
A40:    rng fp=K by A39,FUNCT_2:def 3;
        consider p2 be Function of k,k such that
A41:    p29=p2 and
A42:    p2 is Permutation of k by A35;
        consider f2 be Function of n,k such that
A43:    f29=f2 and
A44:    f2 is onto "increasing by A36;
        rng fp =K by A39,FUNCT_2:def 3;
        then reconsider p199=p1,p299=p2 as Permutation of rng fp by A34,A42;
        consider f1 be Function of n,k such that
A45:    f19=f1 and
A46:    f1 is onto "increasing by A31;
        reconsider f199=f1,f299=f2 as Function of N,K;
A47:    rng f2=K by A44,FUNCT_2:def 3;
        for l,m be Nat st l in rng f1 & m in rng f1 & l < m
        holds min* f1"{l} < min* f1"{m} by A46,STIRL2_1:def 1;
        then
A48:    f199 is "increasing by STIRL2_1:def 5;
        for l,m be Nat st l in rng f2 & m in rng f2 & l < m
        holds min* f2"{l} < min* f2"{m} by A44,STIRL2_1:def 1;
        then
A49:    f299 is "increasing by STIRL2_1:def 5;
A50:    fp=p199*f199 by A25,A27,A32,A33,A45,A38;
A51:    rng f1=K by A46,FUNCT_2:def 3;
A52:    fp=p299*f299 by A25,A28,A29,A37,A41,A43,A38;
        then p199=p299 by A48,A49,A51,A47,A50,A40,STIRL2_1:75;
        hence thesis by A32,A33,A45,A37,A41,A43,A48,A49,A51,A47,A50,A52,A40,
STIRL2_1:75;
      end;
      consider h be Function of n,k such that
A53:  h is onto "increasing by A1,A12,STIRL2_1:23;
      Onto c= rng FP
      proof
        let x;
        assume x in Onto;
        then consider f be Function of n,k such that
A54:    f=x and
A55:    f is onto;
        rng f=K by A55,FUNCT_2:def 3;
        then consider I be Function of N,K,P be Permutation of K such that
A56:    f=P*I and
A57:    K=rng I and
A58:    I is "increasing by STIRL2_1:73;
        set p=P;
        reconsider i=I as Function of n,k;
        for l,m be Nat st l in rng I & m in rng I & l < m
        holds min* I"{l} < min* I"{m} by A58,STIRL2_1:def 5;
        then
A59:    i is "increasing by STIRL2_1:def 1;
        i is onto by A57,FUNCT_2:def 3;
        then p in Perm & i in Bloc by A59;
        then
A60:    [p,i] in [:Perm,Bloc:] by ZFMISC_1:def 2;
        h in Onto by A53;
        then
A61:    [p,i] in dom FP by A60,FUNCT_2:def 1;
        FP.([p,i])=f by A25,A56,A60;
        hence thesis by A54,A61,FUNCT_1:def 5;
      end;
      then
A62:  rng FP=Onto by XBOOLE_0:def 10;
      h in Onto by A53;
      then dom FP=[:Perm,Bloc:] by FUNCT_2:def 1;
      then Onto,[:Perm,Bloc:] are_equipotent by A26,A62,WELLORD2:def 4;
      then
A63:  card Onto=card Perm * card Bloc by A13,CARD_1:21;
      k! >0 by NEWTON:23;
      then
A64:  (k!)*card Bloc/(k!)=card Bloc*((k!)/(k!)) & (k!)/(k!)=1 by XCMPLX_1:60,75
;
      consider F be XFinSequence of INT such that
A65:  dom F = card k+1 and
A66:  Sum F = card {f where f is Function of n,k:f is onto} and
A67:  for m st m in dom F holds F.m=((-1)|^m)*(card k choose m)*((
      card k-m) |^ card n) by A12,Th70;
      take F;
      card Perm =(card k)! by Th8;
      then Sum F=(k!)*card Bloc by A66,A63,CARD_1:def 5;
      then n block k=(Sum F*1)/(k!) by A64,STIRL2_1:def 2;
      hence n block k= 1/(k!) * Sum F by XCMPLX_1:75;
      thus dom F = k+1 by A65,CARD_1:def 5;
A68:  card k=k & card n=n by CARD_1:def 5;
      let m;
      assume m in dom F;
      hence F.m=((-1)|^m)*(k choose m)*((k-m)|^ n) by A67,A68;
    end;
    suppose
      n=0 & k<>0;
      hence thesis by A1;
    end;
  end;
  hence thesis;
end;

theorem Th72:
  for X1,Y1,X be finite set st (Y1 is empty implies X1 is empty) &
X c= X1 for F be Function of X1,Y1 st F is one-to-one & card X1=card Y1 holds (
card X1-'card X)!= card{f where f is Function of X1,Y1: f is one-to-one & rng(f
  |(X1\X)) c= F.:(X1\X) & for x st x in X holds f.x=F.x}
proof
  let X1,Y1,X be finite set such that
A1: Y1 is empty implies X1 is empty and
A2: X c= X1;
  set XX=X1\X;
  let F be Function of X1,Y1 such that
A3: F is one-to-one and
A4: card X1=card Y1;
  deffunc F(set)=F.$1;
  defpred P[Function,set,set] means $1 is one-to-one & rng ($1|XX) =F.:XX;
  reconsider FX=F.:XX as finite set;
  set F1={f where f is Function of XX,F.:XX:f is one-to-one};
A5: card XX= card X1 -card X by A2,CARD_2:63;
A6: for f be Function of X1,Y1 st (for x st x in X1\XX holds F(x)=f.x)
  holds P[f,X1,Y1] iff P[f|XX,XX,F.:XX]
  proof
    let f be Function of X1,Y1 such that
A7: for x st x in X1\XX holds F(x)=f.x;
    thus P[f,X1,Y1] implies P[f|XX,XX,F.:XX] by FUNCT_1:84,RELAT_1:101;
    thus P[f|XX,XX,F.:XX] implies P[f,X1,Y1]
    proof
      F is onto by A3,A4,STIRL2_1:70;
      then
A8:   rng F=Y1 by FUNCT_2:def 3;
A9:   rng (f|XX)=f.:XX & F.:((X1\XX)\/XX)=F.:(X1\XX)\/F.:XX by RELAT_1:148,153;
A10:  dom (F|(X1\XX))= dom F /\(X1\XX) & dom F=X1 by A1,FUNCT_2:def 1
,RELAT_1:90;
A11:  dom (f|(X1\XX))=dom f /\(X1\XX) & dom f=X1 by A1,FUNCT_2:def 1,RELAT_1:90
;
      now
A12:    X1\XX=X/\X1 & X/\X1=X by A2,XBOOLE_1:28,48;
        let x such that
A13:    x in dom (F|(X1\XX));
        f.x=(f|(X1\XX)).x by A11,A10,A13,FUNCT_1:70;
        hence F.x=(f|(X1\XX)).x by A7,A10,A13,A12;
      end;
      then f|(X1\XX)=F|(X1\XX) by A11,A10,FUNCT_1:68;
      then
A14:  rng (f|(X1\XX))=F.:(X1\XX) by RELAT_1:148;
A15:  (X1\XX)\/ XX= X1 & rng (f|(X1\XX))=f.:(X1\XX) by RELAT_1:148,XBOOLE_1:45;
A16:  X1=dom F & X1=dom f by A1,FUNCT_2:def 1;
A17:  F.:dom F=rng F by RELAT_1:146;
      assume
A18:  P[f|XX,XX,F.:XX];
      then rng (f|XX)=F.:XX by RELAT_1:101;
      then F.:X1=f.:X1 by A14,A15,A9,RELAT_1:153;
      then rng F=rng f by A16,A17,RELAT_1:146;
      then f is onto by A8,FUNCT_2:def 3;
      hence thesis by A4,A18,RELAT_1:101,STIRL2_1:70;
    end;
  end;
  set F2={f where f is Function of X1,Y1:f is one-to-one & rng(f|XX) c= F.:XX
  & for x st x in X holds f.x=F.x};
  set S2={f where f is Function of X1,Y1:P[f,X1,Y1]&rng (f|XX) c=F.:XX& (for x
  st x in X1\XX holds f.x=F(x))};
A19: X1\XX=X/\X1 & X/\X1=X by A2,XBOOLE_1:28,48;
A20: S2 c= F2
  proof
    let x;
    assume x in S2;
    then ex f be Function of X1,Y1 st x=f & P[f,X1,Y1] & rng (f|XX) c=F.:XX &
    for x st x in X1\XX holds f.x=F(x);
    hence thesis by A19;
  end;
  dom F=X1 by A1,FUNCT_2:def 1;
  then XX,F.:XX are_equipotent by A3,CARD_1:60;
  then
A21: card XX=card (F.:XX) by CARD_1:21;
  then card F1= card XX!/((card FX-'card XX)!) & card FX-'card XX=0 by Th7,
XREAL_1:234;
  then
A22: card F1=(card X1 -' card X)! by A5,NEWTON:18,XREAL_0:def 2;
  set S1={f where f is Function of XX,F.:XX:P[f,XX,F.:XX]};
A23: for x st x in X1\XX holds F(x) in Y1
  proof
A24: X1=dom F by A1,FUNCT_2:def 1;
    let x;
    assume x in X1\XX;
    then F.x in rng F by A24,FUNCT_1:def 5;
    hence thesis;
  end;
A25: F1 c= S1
  proof
    let x;
    assume x in F1;
    then consider f be Function of XX,FX such that
A26: x=f and
A27: f is one-to-one;
    FX={} implies XX={} by A21;
    then dom f=XX by FUNCT_2:def 1;
    then
A28: f|XX=f by RELAT_1:98;
    f is onto by A21,A27,STIRL2_1:70;
    then rng f=FX by FUNCT_2:def 3;
    hence thesis by A26,A27,A28;
  end;
  S1 c= F1
  proof
    let x;
    assume x in S1;
    then ex f be Function of XX,FX st f=x & P[f,XX,F.:XX];
    hence thesis;
  end;
  then
A29: F1=S1 by A25,XBOOLE_0:def 10;
A30: F2 c= S2
  proof
    let x;
    assume x in F2;
    then consider f be Function of X1,Y1 such that
A31: x=f and
A32: f is one-to-one and
A33: rng(f|XX) c= F.:XX and
A34: for x st x in X holds f.x=F.x;
    dom f=X1 by A1,FUNCT_2:def 1;
    then XX,f.:XX are_equipotent by A32,CARD_1:60;
    then card XX=card (f.:XX) by CARD_1:21;
    then card FX=card rng(f|XX) by A21,RELAT_1:148;
    then rng(f|XX)=FX by A33,PRE_POLY:8;
    hence thesis by A19,A31,A32,A34;
  end;
A35: XX c= X1 & F.:XX c= Y1;
  then XX c= dom F by A1,FUNCT_2:def 1;
  then
A36: F.:XX is empty implies XX is empty by RELAT_1:152;
  card S1=card S2 from STIRL2_1:sch 3(A23,A35,A36,A6);
  hence thesis by A20,A30,A22,A29,XBOOLE_0:def 10;
end;

Lm5: for X,Y for F be Function of X,Y st dom F=X & F is one-to-one ex XF be
XFinSequence of INT st dom XF = card X & (card X)!-Sum XF= card {h where h is
Function of X,rng F: h is one-to-one & for x st x in X holds h.x<>F.x}& for n
st n in dom XF holds XF.n=((-1)|^n) * (card X!) / ((n+1)!)
proof
  let X,Y;
  let F be Function of X,Y such that
A1: dom F=X and
A2: F is one-to-one;
  deffunc fy(set)={h where h is Function of X,rng F:h is one-to-one&h.$1=F.$1};
A3: for x st x in X holds fy(x) in bool Funcs(X,rng F)
  proof
    let x such that
A4: x in X;
    fy(x) c= Funcs(X,rng F)
    proof
      let y;
      assume y in fy(x);
      then
A5:   ex h be Function of X,rng F st y=h & h is one-to-one & h.x=F.x;
      rng F<>{} by A1,A4,RELAT_1:65;
      hence thesis by A5,FUNCT_2:11;
    end;
    hence thesis;
  end;
  consider Fy9 be Function of X,bool Funcs(X,rng F) such that
A6: for x st x in X holds Fy9.x = fy(x) from FUNCT_2:sch 2(A3);
  defpred xf[set,set] means for n,k st n=$1 & k=(card X)-(n+1) holds $2=k!;
A7: for x st x in card X ex y st y in NAT & xf[x,y]
  proof
    let x such that
A8: x in card X;
    card X is Subset of NAT by STIRL2_1:8;
    then reconsider n=x as Element of NAT by A8;
    n<card X by A8,NAT_1:45;
    then n+1<=card X by NAT_1:13;
    then reconsider k=(card X)-(n+1) as Element of NAT by NAT_1:21;
    xf[n, k!];
    hence thesis;
  end;
  consider XF be Function of card X,NAT such that
A9: for x st x in card X holds xf[x,XF.x] from FUNCT_2:sch 1(A7);
  for y holds Fy9.y is finite
  proof
    let y;
    now
      per cases;
      suppose
        y in dom Fy9;
        then Fy9.y in rng Fy9 by FUNCT_1:def 5;
        hence thesis;
      end;
      suppose
        not y in dom Fy9;
        hence thesis by FUNCT_1:def 4;
      end;
    end;
    hence thesis;
  end;
  then reconsider Fy=Fy9 as finite-yielding Function by Def3;
  reconsider rngF=rng F as finite set;
A10: dom XF=card X by FUNCT_2:def 1;
  then reconsider XF as XFinSequence by AFINSQ_1:7;
  reconsider XF as XFinSequence of NAT;
A11: for n st n in dom XF holds ex x,y st x<>y & for f st f in Choose(X,n+1,
  x,y) holds card(Intersection(Fy,f,x))=XF.n
  proof
    let n such that
A12: n in dom XF;
    n<card X by A10,A12,NAT_1:45;
    then
A13: n+1<=card X by NAT_1:13;
    then reconsider c = card X-(n+1) as Element of NAT by NAT_1:21;
A14: card X-'(n+1)=c by A13,XREAL_1:235;
    take 0,1;
    thus 0<>1;
    let f9 be Function;
    assume f9 in Choose(X,n+1,0,1);
    then consider f be Function of X,{0,1} such that
A15: f = f9 and
A16: card (f"{0})=n+1 by Def1;
    reconsider f0=f"{0} as finite set;
    set Xf0=X\f0;
    set S={h where h is Function of X,rngF: h is one-to-one & rng(h|Xf0) c= F
    .:Xf0 & for x st x in f0 holds h.x=F.x};
A17: Intersection(Fy,f,0) c= S
    proof
      assume not Intersection(Fy,f,0) c= S;
      then consider z such that
A18:  z in Intersection(Fy,f,0) and
A19:  not z in S by TARSKI:def 3;
      consider x9 be set such that
A20:  x9 in f"{0} by A16,CARD_1:47,XBOOLE_0:def 1;
      f.x9 in {0} by A20,FUNCT_1:def 13;
      then
A21:  f.x9=0 by TARSKI:def 1;
      x9 in dom f by A20,FUNCT_1:def 13;
      then 0 in rng f by A21,FUNCT_1:def 5;
      then consider x such that
A22:  x in dom f and
      f.x=0 and
A23:  z in Fy.x by A18,Th24;
      z in fy(x) by A6,A22,A23;
      then consider h be Function of X,rng F such that
A24:  z=h and
A25:  h is one-to-one and
      h.x=F.x;
A26:  for x1 st x1 in f0 holds h.x1=F.x1
      proof
        let x1 such that
A27:    x1 in f0;
        f.x1 in {0} by A27,FUNCT_1:def 13;
        then
A28:    f.x1 =0 by TARSKI:def 1;
        Fy9.x1=fy(x1) & x1 in dom f by A6,A27,FUNCT_1:def 13;
        then h in fy(x1) by A18,A24,A28,Def2;
        then
        ex h9 be Function of X,rng F st h=h9 & h9 is one-to-one & h9.x1=F .x1;
        hence thesis;
      end;
      rng (h|Xf0) c= F.:Xf0
      proof
        assume not rng (h|Xf0) c= F.:Xf0;
        then consider y such that
A29:    y in rng (h|Xf0) and
A30:    not y in F.:Xf0 by TARSKI:def 3;
        consider x1 such that
A31:    x1 in dom (h|Xf0) and
A32:    (h|Xf0).x1=y by A29,FUNCT_1:def 5;
A33:    h.x1=y by A31,A32,FUNCT_1:70;
        x1 in dom h /\ Xf0 by A31,RELAT_1:90;
        then
A34:    x1 in Xf0 by XBOOLE_0:def 4;
A35:    F.:(X\Xf0)=F.:X\F.:Xf0 by A2,FUNCT_1:123;
        rngF= F.:X by A1,RELAT_1:146;
        then y in F.:X\F.:Xf0 by A29,A30,XBOOLE_0:def 5;
        then consider x2 such that
A36:    x2 in dom F and
A37:    x2 in X\Xf0 and
A38:    y = F.x2 by A35,FUNCT_1:def 12;
        y in rng F by A36,A38,FUNCT_1:def 5;
        then
A39:    X=dom h by FUNCT_2:def 1;
        X\Xf0=X/\f"{0} by XBOOLE_1:48;
        then x2 in f"{0} by A37,XBOOLE_0:def 4;
        then
A40:    h.x2=y by A26,A38;
        not x2 in Xf0 by A37,XBOOLE_0:def 5;
        hence contradiction by A25,A36,A40,A33,A39,A34,FUNCT_1:def 8;
      end;
      hence thesis by A19,A24,A25,A26;
    end;
A41: X,rngF are_equipotent by A1,A2,WELLORD2:def 4;
    then
A42: card rngF=card X by CARD_1:21;
    card rngF=card X by A41,CARD_1:21;
    then
A43: rngF={} implies X is empty;
A44: F is Function of X,rngF by A1,FUNCT_2:3;
    S c= Intersection(Fy,f,0)
    proof
      assume not S c= Intersection(Fy,f,0);
      then consider z such that
A45:  z in S and
A46:  not z in Intersection(Fy,f,0) by TARSKI:def 3;
      consider h be Function of X,rng F such that
A47:  h=z and
A48:  h is one-to-one and
      rng (h|Xf0) c= F.:Xf0 and
A49:  for x st x in f0 holds h.x=F.x by A45;
      consider x such that
A50:  x in f"{0} by A16,CARD_1:47,XBOOLE_0:def 1;
      x in X by A50;
      then x in dom Fy9 by FUNCT_2:def 1;
      then
A51:  Fy9.x in rng Fy9 by FUNCT_1:def 5;
A52:  Fy9.x=fy(x) by A6,A50;
      h.x=F.x by A49,A50;
      then h in Fy9.x by A48,A52;
      then h in union rng Fy9 by A51,TARSKI:def 4;
      then consider y such that
A53:  y in dom f and
A54:  f.y=0 and
A55:  not h in Fy.y by A46,A47,Def2;
      f.y in {0} by A54,TARSKI:def 1;
      then y in f"{0} by A53,FUNCT_1:def 13;
      then h.y=F.y by A49;
      then h in fy(y) by A48;
      hence contradiction by A6,A53,A55;
    end;
    then S = Intersection(Fy,f,0) by A17,XBOOLE_0:def 10;
    then card Intersection(Fy,f,0)=(card X-'(n+1))! by A2,A16,A43,A44,A42,Th72;
    hence thesis by A9,A10,A12,A15,A14;
  end;
A56: X,rngF are_equipotent by A1,A2,WELLORD2:def 4;
  then card rngF=card X by CARD_1:21;
  then
A57: (card rngF-'card X)!=1 & card {f where f is Function of X,rngF:f is
  one-to-one}= card rngF!/(( card rngF-'card X)!) by Th7,NEWTON:18,XREAL_1:234;
  then reconsider
  One={f where f is Function of X,rng F:f is one-to-one} as finite
  set;
  set S={h where h is Function of X,rng F: h is one-to-one & for x st x in X
  holds h.x<>F.x};
  dom XF= card X & dom Fy = X by FUNCT_2:def 1;
  then consider F9 be XFinSequence of INT such that
A58: dom F9=card X and
A59: card union rng Fy = Sum F9 and
A60: for n st n in dom F9 holds F9.n=((-1)|^n)*XF.n*(card X choose (n+1
  )) by A11,Th69;
A61: union rng Fy9 c= One
  proof
    let x;
    assume x in union rng Fy9;
    then consider Fyx be set such that
A62: x in Fyx and
A63: Fyx in rng Fy9 by TARSKI:def 4;
    consider x1 such that
A64: x1 in dom Fy9 & Fy.x1=Fyx by A63,FUNCT_1:def 5;
    x in fy(x1) by A6,A62,A64;
    then ex h be Function of X,rng F st h=x & h is one-to-one & h.x1=F.x1;
    hence thesis;
  end;
  then reconsider u=union rng Fy as finite set;
A65: card(One\u) = card One - card u by A61,CARD_2:63;
  take F9;
  thus dom F9 = card X by A58;
A66: One \ union rng Fy c= S
  proof
    let x such that
A67: x in One \ union rng Fy;
    x in One by A67;
    then consider f be Function of X,rng F such that
A68: f=x and
A69: f is one-to-one;
    assume not x in S;
    then consider x such that
A70: x in X and
A71: f.x=F.x by A68,A69;
    x in dom Fy by A70,FUNCT_2:def 1;
    then Fy.x in rng Fy by FUNCT_1:def 5;
    then
A72: fy(x) in rng Fy by A6,A70;
    f in fy(x) by A69,A71;
    then f in union rng Fy by A72,TARSKI:def 4;
    hence thesis by A67,A68,XBOOLE_0:def 5;
  end;
A73: S c= One \ union rng Fy
  proof
    let x;
    assume x in S;
    then consider f be Function of X,rng F such that
A74: x=f and
A75: f is one-to-one and
A76: for x st x in X holds f.x<>F.x;
    assume
A77: not x in One \ union rng Fy;
    f in One by A75;
    then f in union rng Fy by A74,A77,XBOOLE_0:def 5;
    then consider Fyy be set such that
A78: f in Fyy and
A79: Fyy in rng Fy by TARSKI:def 4;
    consider y such that
A80: y in dom Fy and
A81: Fy.y=Fyy by A79,FUNCT_1:def 5;
    y in X by A80,FUNCT_2:def 1;
    then f in fy(y) by A6,A78,A81;
    then
A82: ex g be Function of X,rng F st f=g & g is one-to-one & g.y=F.y;
    y in X by A80,FUNCT_2:def 1;
    hence contradiction by A76,A82;
  end;
  card One = (card X)! by A56,A57,CARD_1:21;
  hence card S=(card X)! - Sum F9 by A59,A66,A73,A65,XBOOLE_0:def 10;
  let n such that
A83: n in dom F9;
  n<card X by A58,A83,NAT_1:45;
  then
A84: n+1 <=card X by NAT_1:13;
  then reconsider c = card X-(n+1) as Element of NAT by NAT_1:21;
A85: card X choose (n+1)=((card X!))/((c!) * ((n+1)!)) by A84,NEWTON:def 3;
A86: (c!)>0 by NEWTON:23;
  XF.n=c! by A9,A58,A83;
  then
A87: XF.n*(card X choose (n+1))=(c! *(card X!))/((c!) * ((n+1)!)) by A85,
XCMPLX_1:75
    .=(card X!)*(c!/((c!) * ((n+1)!))) by XCMPLX_1:75
    .=(card X!)*((c!) / (c!) / ((n+1)!)) by XCMPLX_1:79
    .=(card X!)*(1 / ((n+1)!)) by A86,XCMPLX_1:60
    .=(card X!*1)/ ((n+1)!) by XCMPLX_1:75;
  F9.n=((-1)|^n)*XF.n*(card X choose (n+1)) by A60,A83
    .=((-1)|^n)*(card X!/ ((n+1)!)) by A87;
  hence thesis by XCMPLX_1:75;
end;

theorem Th73:
  for F be Function st dom F=X & F is one-to-one ex XF be
  XFinSequence of INT st Sum XF=card {h where h is Function of X,rng F: h is
  one-to-one & for x st x in X holds h.x<>F.x}& dom XF = card X +1 & for n st n
  in dom XF holds XF.n=((-1)|^n)*(card X!)/(n!)
proof
  let F9 be Function such that
A1: dom F9=X and
A2: F9 is one-to-one;
  X,rng F9 are_equipotent by A1,A2,WELLORD2:def 4;
  then card X=card rng F9 by CARD_1:21;
  then reconsider rngF=rng F9 as finite set;
  reconsider F=F9 as Function of X,rngF by A1,FUNCT_2:3;
  set S={h where h is Function of X,rng F9: h is one-to-one & for x st x in X
  holds h.x<>F9.x};
  rng F9=rng F;
  then consider Xf be XFinSequence of INT such that
A3: dom Xf = card X and
A4: (card X)!-Sum Xf=card S and
A5: for n st n in dom Xf holds Xf.n=((-1)|^n)*(card X!)/((n+1)!) by A1,A2,Lm5;
  reconsider c = (card X)! as Element of INT by INT_1:def 2;
A6: len <%c%>=1 by AFINSQ_1:36;
  set F1 = (-1)(#)Xf;
A8:  dom F1=card X by A3,VALUED_1:def 5;
rng F1 c= INT by VALUED_0:def 5;
then reconsider F1 as XFinSequence of INT by RELAT_1:def 19;
set XF=<%c%>^F1;
  take XF;
  (-1)*Sum Xf =Sum F1 by AFINSQ_2:76;
  then c -Sum Xf=c +Sum F1 .=addint.(c,Sum F1) by BINOP_2:def 20
    .=addint.(addint "**" <%c%>,Sum F1) by AFINSQ_2:49
    .=addint.(addint "**" <%c%>,addint "**" F1) by AFINSQ_2:62
    .=addint "**" XF by AFINSQ_2:54
    .=Sum XF by AFINSQ_2:62;
  hence Sum XF=card S by A4;
  len F1=card X by A3,VALUED_1:def 5;
  hence
A10: dom XF = card X +1 by A6,AFINSQ_1:def 4;
  let n such that
A11: n in dom XF;
  per cases;
  suppose
A12: n=0;
    then ((-1)|^n)=1 by NEWTON:9;
    hence thesis by A12,AFINSQ_1:39,NEWTON:18;
  end;
  suppose
    n>0;
    then reconsider n1=n-1 as Element of NAT by NAT_1:20;
    n1+1=n;
    then
A13: (-1)*((-1)|^n1)=(-1)|^n by NEWTON:11;
    n < card X +1 by A10,A11,NAT_1:45;
    then n1+1 <= card X by NAT_1:13;
    then n1 < len F1 by A8,NAT_1:13;
    then
A14: n1 in dom F1 by NAT_1:45;
    len <% c %>=1 by AFINSQ_1:36;
    then XF.(n1+1)=F1.n1 by A14,AFINSQ_1:def 4;
    then
A15: XF.(n1+1)=(-1)*Xf.n1 by VALUED_1:6;
    Xf.n1=(((-1)|^n1)*(card X!))/((n1+1)!) by A3,A5,A8,A14;
    then XF.n=((-1)*(((-1)|^n1)*(card X!)))/(n!) by A15,XCMPLX_1:75;
    hence thesis by A13;
  end;
end;
 
:: disorders

theorem
  ex XF be XFinSequence of INT st Sum XF=card {h where h is Function of
X,X: h is one-to-one & for x st x in X holds h.x<>x}& dom XF = card X+1 & for n
  st n in dom XF holds XF.n=((-1)|^n)*(card X!)/(n!)
proof
  set S1={h where h is Function of X,X: h is one-to-one & for x st x in X
  holds h.x<>(id X).x};
  set S2={h where h is Function of X,X: h is one-to-one & for x st x in X
  holds h.x<>x};
A1: S2 c= S1
  proof
    let x;
    assume x in S2;
    then consider h be Function of X,X such that
A2: h=x & h is one-to-one and
A3: for y st y in X holds h.y<>y;
    now
      let y such that
A4:   y in X;
      (id X).y=y by A4,FUNCT_1:34;
      hence (id X).y<>h.y by A3,A4;
    end;
    hence thesis by A2;
  end;
A5: dom id X=X & rng id X=X by RELAT_1:71;
  S1 c= S2
  proof
    let x;
    assume x in S1;
    then consider h be Function of X,X such that
A6: h=x & h is one-to-one and
A7: for y st y in X holds h.y<>(id X).y;
    now
      let y such that
A8:   y in X;
      (id X).y=y by A8,FUNCT_1:34;
      hence h.y<>y by A7,A8;
    end;
    hence thesis by A6;
  end;
  then S1=S2 by A1,XBOOLE_0:def 10;
  hence thesis by A5,Th73;
end;
