:: Cardinal Numbers and Finite Sets
::  by Karol P\c{a}k
:: 
:: Received May 24, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies FUNCT_1, BOOLE, FINSEQ_1, FINSET_1, RELAT_1, ORDINAL2, CARD_1,
      TARSKI, ALGSEQ_1, ARYTM_1, AFINSQ_1, FUNCT_2, FUNCT_4, FUNCOP_1,
      HENMODEL, INT_1, STIRL2_1, SGRAPH1, BINOP_1, RLVECT_1, MONOID_0,
      SETWISEO, FINSOP_1, ARYTM_3, GROUP_1, NEWTON, QC_LANG1, CARD_FIN,
      CANTOR_1, PROB_1, PRGCOR_2, GR_CY_1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, NUMBERS, XREAL_0, RELAT_1,
      FUNCT_1, RELSET_1, ORDINAL1, ORDINAL2, REAL_1, NAT_1, PARTFUN1, FINSET_1,
      CARD_1, AFINSQ_1, XCMPLX_0, FUNCT_2, FUNCT_4, INT_1, WELLORD2, ENUMSET1,
      HENMODEL, BINOP_1, BINOP_2, NEWTON, SETWISEO, STIRL2_1, BINARITH,
      FUNCOP_1, PRGCOR_2, YELLOW20, FINSOP_1, FINSEQ_1;
 constructors REAL_1, NAT_1, WELLORD2, MEMBERED, PARTFUN1, HENMODEL, SETWISEO,
      FINSOP_1, PREPOWER, STIRL2_1, BINARITH, PRGCOR_2, YELLOW20, DOMAIN_1,
      FINSEQOP, GR_CY_1;
 registrations FUNCOP_1, STIRL2_1, FUNCT_1, RELAT_1, RELSET_1, CARD_1,
      SUBSET_1, NAT_1, MEMBERED, ORDINAL2, AFINSQ_1, BINOP_2, XBOOLE_0,
      FINSET_1, INT_1, FUNCT_2, FUNCT_4, FINSEQ_1, WSIERP_1, TREES_2, FRAENKEL;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, XBOOLE_0, FUNCT_1;
 theorems AFINSQ_1, TARSKI, FINSEQ_1, FUNCT_1, XBOOLE_0, ZFMISC_1, RELAT_1,
      XBOOLE_1, NAT_1, FUNCT_2, FINSET_1, AXIOMS, XCMPLX_1, REAL_1, CARD_2,
      INT_1, ORDINAL1, WELLORD2, EULER_1, SUBSET_1, ENUMSET1, CARD_1, CARD_5,
      BINOP_2, NEWTON, SETWISEO, PRGCOR_2, XREAL_1, GR_CY_1, CARD_4, BINARITH,
      FUNCOP_1, STIRL2_1, ALTCAT_1, FUNCT_5, GOBOARD9, IRRAT_1, NAT_2, FUNCT_4,
      FINSOP_1, YELLOW20, TRIANG_1;
 schemes FUNCT_2, NAT_1, XBOOLE_0, STIRL2_1;

begin

 reserve x, x1, x2, y, z, X', Y' for set,
         X, Y for finite set,
         n, k, m for Nat,
         f for Function;

Lm1:x in X' implies (X'\{x})\/{x}=X' by ZFMISC_1:140;
Lm2:not x in X' implies (X'\/{x})\{x}=X' by ZFMISC_1:141;

theorem Th11: :: TREES_1:19
  X c= Y & card X=card Y implies X=Y
proof
  assume A1:X c= Y & card X=card Y;
  card (Y\X)=card Y-card X by A1,CARD_2:63;
  then Y\X={} by A1,CARD_2:59;
  then Y c= X by XBOOLE_1:37;
  hence thesis by A1,XBOOLE_0:def 10;
end;

theorem Th1:
  for X,Y,x,y st (Y={} implies X={}) & not x in X holds
  card Funcs(X,Y) =
    Card{F where F is Function of X\/{x},Y\/{y}:rng (F|X) c=Y & F.x=y}
proof
  let X,Y,x,y;
  assume A1:Y={} implies X={};
  then A1':Y is empty implies X is empty;
  assume A2:not x in X;
  defpred P[set,set,set] means 1=1;
  A3:for f be Function of X\/{x},Y\/{y} st f.x=y
    holds P[f,X\/{x},Y\/{y}] iff P[f|X,X,Y];
  set F1={f where f is Function of X,Y:P[f,X,Y]};
  set F2={f where f is Function of (X\/{x}),(Y\/{y}):
               P[f,X\/{x},Y\/{y}]&rng (f|X) c=Y & f.x=y};
  set F3={F where F is Function of X\/{x},Y\/{y}:rng (F|X) c=Y & F.x=y};
  A4:F1 c= Funcs(X,Y)
  proof let F be set such that A5:F in F1;
    ex f be Function of X,Y st f=F & P[f,X,Y] by A5;
    hence thesis by A1,FUNCT_2:11;
  end;
  Funcs(X,Y) c= F1
  proof let F be set such that A5:F in Funcs(X,Y);
    F is Function of X,Y by A5,FUNCT_2:121;
    hence thesis;
  end;
  then A5:Funcs(X,Y) = F1 by A4,XBOOLE_0:def 10;
A6:F2 c= F3
  proof let F be set such that A7:F in F2;
    ex f be Function of (X\/{x}),(Y\/{y}) st f=F & 
      P[f,X\/{x},Y\/{y}]&rng (f|X) c=Y & f.x=y by A7;
    hence thesis;
  end;
A7:F3 c= F2
  proof let F be set such that A8:F in F3;
    ex f be Function of (X\/{x}),(Y\/{y}) st f=F & 
      rng (f|X) c=Y & f.x=y by A8;
    hence thesis;
  end;
  Card F1=Card F2 from STIRL2_1:sch 4(A1',A2,A3);
  hence thesis by A5,A6,A7,XBOOLE_0:def 10;
end;

theorem Th2:
  for X,Y,x,y st not x in X & y in Y holds
  card Funcs(X,Y) = Card{F where F is Function of X\/{x},Y:F.x=y}
proof
  let X,Y,x,y such that A1:not x in X & y in Y;
  set F1={F where F is Function of X\/{x},Y\/{y}:rng (F|X) c=Y & F.x=y};
  set F2={F where F is Function of X\/{x},Y:F.x=y};
  {y} c= Y by A1,ZFMISC_1:37; 
  then A2:Y=Y\/{y} by XBOOLE_1:12;
  A3:F1 c= F2
  proof let f be set such that A4:f in F1;
    ex F be Function of X\/{x},Y\/{y} st
      f=F & rng (F|X) c=Y & F.x=y by A4;
    hence thesis by A2;  
  end;
  F2 c= F1
  proof let f be set such that A4:f in F2;
    consider F be Function of X\/{x},Y such that
A5: f=F & F.x=y by A4;
    rng (F|X) c=Y;
    hence thesis by A2,A5;
  end;
  then F1=F2 by A3,XBOOLE_0:def 10;
  hence thesis by A1,Th1;
end;  

:: card Funcs(X,Y)

theorem Th3: :: WEDDWITT:4
  (Y={} implies X={}) implies card Funcs(X,Y) = card Y |^ card X
proof
  assume A1:Y={} implies X={};
  per cases;
    suppose Y is empty;
      then card Funcs(X,Y)=one & card Y |^ card X =1
        by CARD_1:50,NEWTON:9,A1,ALTCAT_1:3,CARD_1:47;
      hence thesis by CARD_2:20;
    end;
    suppose A2:Y is non empty;
      defpred P[Nat] means
        for X,Y st Y is non empty & card X= $1 holds
          card Funcs(X,Y)=card Y |^ card X;
   A3:P[0]
      proof
        let X,Y such that A4:Y is non empty & card X=0;
        X is empty by A4,CARD_2:59;
        then Funcs(X,Y) = {{}} by FUNCT_5:64;
        then card Funcs(X,Y)=one & card Y |^ card X = 1
          by A4,CARD_1:50,NEWTON:9;
        hence thesis by CARD_2:20;
      end;
   A4:for n st P[n] holds P[n+1]
      proof
        let n such that A5:P[n];
        let X,Y such that A6:Y is non empty and A7:card X=n+1;
        card Y,Y are_equipotent by CARD_1:def 5;
        then consider f be Function such that
     A8:f is one-to-one & dom f= card Y & rng f= Y by WELLORD2:def 4;
        reconsider f as Function of card Y,Y by A8,FUNCT_2:3;
     A9:f is onto one-to-one by A8,FUNCT_2:def 3;
        consider x such that
    A10:x in X by XBOOLE_0:def 1,A7,CARD_1:47;
        defpred Q[set] means 1=1;
        consider F be XFinSequence of NAT such that
    A11:dom F = card Y and
    A12:Card{g where g is Function of X,Y:Q[g]} = Sum(F) and
    A13:for k st k in dom F holds
          F.k = Card{g where g is Function of X,Y:Q[g] & g.x=f.k}
            from STIRL2_1:sch 8(A9,A6,A10);
        set F1={g where g is Function of X,Y:Q[g]};
    A14:F1 c= Funcs(X,Y)
        proof let G be set such that A15:G in F1;
          ex g be Function of X,Y st g=G & Q[g] by A15;
          hence thesis by A6,FUNCT_2:11;
        end;
        Funcs(X,Y) c= F1
        proof let G be set such that A15:G in Funcs(X,Y);
          G is Function of X,Y by A15,FUNCT_2:121;
          hence thesis;
        end;
        then A15:Funcs(X,Y) = F1 by A14,XBOOLE_0:def 10;
        reconsider cY=card Y |^ n as Nat by NEWTON:99;
        for k st k in dom F holds F.k=cY
        proof
          let k such that A17:k in dom F;
          set Xx=X\{x};
          set F2={g where g is Function of Xx\/{x},Y: g.x=f.k};
          set F3={g where g is Function of X,Y:Q[g] & g.x=f.k};
      A18:F2 c=F3
          proof let G be set such that A19:G in F2;
            Xx\/{x}=X & ex g be Function of Xx\/{x},Y st g=G & g.x=f.k
              by A10,A19,Lm1;
            hence thesis;
          end;
          F3 c= F2
          proof let G be set such that A20:G in F3;
            Xx\/{x}=X & ex g be Function of X,Y st g=G & Q[g] & g.x=f.k
              by A20,A10,Lm1;
            hence thesis;
          end;
          then A19:F2=F3 by A18,XBOOLE_0:def 10;
          x in {x} & f.k in rng f by A8,A11,A17,TARSKI:def 1,FUNCT_1:def 5;
          then not x in Xx & f.k in Y & card Xx=n
            by A7,A10,STIRL2_1:65,XBOOLE_0:def 4;
          then card Funcs(Xx,Y)=Card F2 &
            card Funcs(Xx,Y)=cY by A5,Th2;
          hence thesis by A13,A17,A19;
        end; then
        (for k st k in dom F holds F.k <= cY ) &
        (for k st k in dom F holds F.k >= cY);
        then Sum F<=len F*card Y|^n & Sum F>=len F*card Y|^n & dom F=len F
          by STIRL2_1:50,51,AFINSQ_1:def 1;
        then Sum F = card Y * card Y |^ n by A11,AXIOMS:21;
        hence thesis by A7,A12,A15,NEWTON:11;
      end;
      for n holds P[n] from NAT_1:sch 1(A3,A4);
      hence thesis by A2;
    end;
end;

theorem Th4:
  for X,Y,x,y st (Y is empty implies X is empty) & not x in X & not y in Y
    holds
      Card{F where F is Function of X,Y:F is one-to-one}=
      Card{F where F is Function of X\/{x},Y\/{y}:F is one-to-one & F.x=y}
proof
  let X,Y,x,y such that A1:Y is empty implies X is empty and
                        A2:not x in X & not y in Y;
  defpred P[Function,set,set] means $1 is one-to-one & rng ($1|X) c=Y;
  A3:not x in X by A2;
  A4:for f be Function of X\/{x},Y\/{y} st f.x=y holds
      P[f,X\/{x},Y\/{y}] iff P[f|X,X,Y]
    proof
      let f be Function of X\/{x},Y\/{y} such that A5:f.x=y;
      thus P[f,X\/{x},Y\/{y}] implies P[f|X,X,Y] by RELAT_1:101,FUNCT_1:84;
      thus P[f|X,X,Y] implies P[f,X\/{x},Y\/{y}]
      proof
        assume A6:f|X is one-to-one & rng (f|X|X) c=Y;
        (X\/{x})/\X=X & dom f = X\/{x} by XBOOLE_1:21,FUNCT_2:def 1;
        then dom (f|X)=X & rng (f|X) c= Y by A6,FUNCT_1:68,RELAT_1:101;
        then f|X is Function of X,Y & rng (f|X) c= Y by FUNCT_2:4;
        hence thesis by A1,A2,A5,A6,STIRL2_1:68;
      end;
    end;
  set F1={F where F is Function of X,Y:F is one-to-one};
  set F2={f where f is Function of X,Y:f is one-to-one & rng (f|X) c= Y};
  A5:F1 c=F2
  proof
    let z such that A6:z in F1;
    consider F be Function of X,Y such that
      A7:z=F & F is one-to-one by A6;
    rng (F|X) c= rng F by RELAT_1:99;
    hence thesis by A7; 
  end;
  F2 c=F1
  proof
    let z such that A6:z in F2;
    ex f be Function of X,Y st z=f & f is one-to-one & rng (f|X)c= Y by A6;
    hence thesis;
  end;
  then A6:F2=F1 by A5,XBOOLE_0:def 10; 
  set F3={F where F is Function of X\/{x},Y\/{y}:
                          F is one-to-one & F.x=y};                          
A7:F3 c= {f where f is Function of (X\/{x}),(Y\/{y}):
               P[f,X\/{x},Y\/{y}] & rng (f|X) c=Y & f.x=y}
  proof
    let z such that A7:z in F3;
    consider F be Function of X\/{x},Y\/{y} such that
 A8:z=F & F is one-to-one & F.x=y by A7;
    rng (F|X) c=Y
    proof
      assume not rng (F|X) c=Y;
      then consider fz be set such that
   A9:fz in rng (F|X) & not fz in Y by TARSKI:def 3;
      consider z such that
  A10:z in dom (F|X) & fz=(F|X).z by A9,FUNCT_1:def 5;
      rng (F|X) c= rng F by RELAT_1:99;
      then fz in rng F & x in {x} by A9,TARSKI:def 1;
      then (fz in Y or fz in {y}) & x in X\/{x} & dom F=X\/{x} &
        dom (F|X)=dom F/\X & F.z=(F|X).z
        by A10,XBOOLE_0:def 2,FUNCT_2:def 1,FUNCT_1:68;
      then fz=y & y=F.z & z in dom F & x in dom F & z in X
        by A9,A10,TARSKI:def 1,XBOOLE_0:def 3;
      hence thesis by A2,A8,FUNCT_1:def 8;
    end;
    hence thesis by A8;   
  end;
A8:{f where f is Function of (X\/{x}),(Y\/{y}):
               P[f,X\/{x},Y\/{y}]&rng (f|X) c=Y &f.x=y} c= F3
  proof             
    let z such that A9:z in {f where f is Function of (X\/{x}),(Y\/{y}):
               P[f,X\/{x},Y\/{y}] & rng (f|X) c=Y & f.x=y};
    ex f be Function of (X\/{x}),(Y\/{y}) st
             z=f & P[f,X\/{x},Y\/{y}] & rng (f|X) c=Y & f.x=y by A9;
    hence thesis;
  end;
  Card{f where f is Function of X,Y:P[f,X,Y]}=
      Card{f where f is Function of (X\/{x}),(Y\/{y}):
              P[f,X\/{x},Y\/{y}]&rng (f|X) c=Y & f.x=y}
              from STIRL2_1:sch 4(A1,A3,A4);
  hence thesis by A6,A7,A8,XBOOLE_0:def 10;                               
end;

theorem
  n!/((n-'k)!) is Nat
proof  
  (n-'k)<= n by GOBOARD9:2;
  then n!/((n-'k)!) is integer & n!/((n -'k)!) >0 by IRRAT_1:34,37;
  hence thesis by INT_1:16;      
end;  

:: one-to-one

theorem Th6:
  card X <= card Y implies
    Card {F where F is Function of X,Y:F is one-to-one} =
    card Y!/((card Y-'card X)!)
proof
  defpred P[Nat] means
    for X,Y st card Y=$1 & card X <= card Y holds
      Card {F where F is Function of X,Y:F is one-to-one}=
         card Y!/((card Y-'card X)!);
  A1:P[0]
  proof
    let X,Y such that A2:card Y=0 & card X <= card Y;
    set F1={F where F is Function of X,Y:F is one-to-one};
 A3:F1 = {{}}
    proof
   A4:F1 c= {{}}
      proof
        let x such that A5:x in F1;
        consider F be Function of X,Y such that
          A6:x=F & F is one-to-one by A5;
        Y={} by A2,CARD_2:59;
        then rng F ={} by XBOOLE_1:3;
        then x={} by A6,RELAT_1:64;
        hence thesis by TARSKI:def 1;
      end;
      dom {}={} & rng {}={} & card X =0 by A2,NAT_1:18;
      then dom {}=X & rng {}=Y by A2,CARD_2:59;
      then {} is Function of X,Y by FUNCT_2:3;
      then {} in F1;
      hence thesis by A4,ZFMISC_1:39;
    end;
    card X= 0 by A2,NAT_1:18;
    then card Y-card X=0 by A2;
    then (card Y-'card X)!=1 & card Y!=1 by A2,NEWTON:18,BINARITH:def 3;
    hence thesis by A3,CARD_2:20,CARD_1:50;
  end;
A2:for n st P[n] holds P[n+1]
  proof
    let n such that A3:P[n];
    let X,Y such that A4:card Y=n+1 & card X <= card Y;
    per cases;
      suppose A5:X is empty;
        set F1={F where F is Function of X,Y:F is one-to-one};
        A6:F1 = {{}}
        proof
          A7:F1 c= {{}}
            proof
              let x such that A8:x in F1;
              consider F be Function of X,Y such that
           A9:x=F & F is one-to-one by A8;
              dom F ={} by A5,FUNCT_2:def 1;
              then x={} by A9,RELAT_1:64;
              hence thesis by TARSKI:def 1;
            end;
          dom {}=X & rng {} c=Y by A5,XBOOLE_1:2;
          then {} is Function of X,Y by FUNCT_2:4;
          then {} in F1;
          hence thesis by A7,ZFMISC_1:39;
        end;
        card Y-card X=card Y & card Y >=0 by NAT_1:18,A5,CARD_1:47;
        then (card Y-'card X)!=card Y! & card Y!>0 by NEWTON:23,BINARITH:def 3;
        then card Y! /((card Y-'card X)!)=1 by XCMPLX_1:60;
        hence thesis by A6,CARD_2:20,CARD_1:50;
      end;
      suppose X is non empty;
        then consider x such that
     A5:x in X by XBOOLE_0:def 1;
        card Y,Y are_equipotent by CARD_1:def 5;
        then consider f be Function such that
     A6:f is one-to-one & dom f= card Y & rng f= Y by WELLORD2:def 4;
        reconsider f as Function of card Y,Y by A6,FUNCT_2:3;
     A7:f is onto one-to-one by A6,FUNCT_2:def 3;
     A8:Y is non empty by A4,CARD_1:47;
        defpred F[Function] means $1 is one-to-one;
        consider F be XFinSequence of NAT such that
     A9:dom F = card Y and
    A10:Card{g where g is Function of X,Y:F[g]} = Sum(F) and
    A11:for k st k in dom F holds
            F.k = Card{g where g is Function of X,Y:F[g] & g.x=f.k}
            from STIRL2_1:sch 8(A7,A8,A5);
        card X is non empty by A5, CARD_2:59;        
        then card Y -' card X < n+1 by A4,NAT_2:11;
        then card Y -' card X <= n by NAT_1:38;
        then A12:n!/((card Y-'card X)!) is integer &
             n!/((card Y -'card X)!) >0 by IRRAT_1:34,37;
        for k st k in dom F holds F.k= n!/((card Y-'card X)!)
        proof
          let k such that A13:k in dom F;
      A14:f.k in Y by A6,A9,A13,FUNCT_1:def 5;
          set Xx=X\{x};set Yy=Y\{f.k};
          card X <>0 by A5,CARD_2:59;
          then card X >0 by NAT_1:18;
          then reconsider cX1=card X-1 as Nat by NAT_1:60;
          cX1+1 <=n+1 by A4;
          then A15:(cX1+1)-1<=(n+1)-1 & card Xx =cX1 & card Yy =n
            by A4,A5,A14,REAL_1:49,STIRL2_1:65;
          then A16:n-cX1>=cX1-cX1 &
            Card {g where g is Function of Xx,Yy:g is one-to-one}=
            n!/((card Yy-'card Xx)!) by A3,REAL_1:49;
          card Y-card X >= card X -card X by A4,REAL_1:49;
          then A17:card Y-'card X=((card Yy+1)-1)-((card Xx+1)-1)
                                      by A4,A15,BINARITH:def 3
                                .=card Yy-'card Xx by A15,A16,BINARITH:def 3;
      A18:Yy is empty implies Xx is empty
          proof
            assume Yy is empty;
            then card Xx=0 by A15,NAT_1:18,CARD_1:47;
            hence thesis by CARD_2:59;
          end;
          f.k in {f.k} & x in {x} by TARSKI:def 1;
          then Y=Yy\/{f.k} & X=Xx\/{x} & not f.k in Yy & not x in Xx &
            (Yy is empty implies Xx is empty) by A18,A5,A14,Lm1,XBOOLE_0:def 4;
          then Card{g where g is Function of X,Y:g is one-to-one & g.x=f.k}
               = n!/((card Y-'card X)!) by A16,A17,Th4;
          hence thesis by A13,A11;        
        end;
        then (for k st k in dom F holds F.k >= n!/((card Y-'card X)!)) &
          (for k st k in dom F holds F.k <= n!/((card Y-'card X)!)) &
          n!/((card Y-'card X)!) is Nat by A12,INT_1:16;  
        then Sum F<=len F*(n!/((card Y-'card X)!)) &
          Sum F>=len F*(n!/((card Y-'card X)!)) & dom F=len F
          by STIRL2_1:50,51,AFINSQ_1:def 1;
        then Sum F = (n+1) *((n!/((card Y-'card X)!))) by A4,A9,AXIOMS:21
          .=((n+1)*(n!))/((card Y-'card X)!) by XCMPLX_1:75
          .=card Y!/((card Y-'card X)!) by A4,NEWTON:21;
        hence thesis by A10;
      end;
    end;
  for n holds P[n] from NAT_1:sch 1(A1,A2);
  hence thesis;
end; 

:: Permutation of X

theorem Th7:
  Card{F where F is Function of X,X:F is Permutation of X}=card X!
proof  
  set F1={F where F is Function of X,X:F is one-to-one};
  set F2={F where F is Function of X,X:F is Permutation of X};
A1:F1 c= F2
  proof
    let x such that A2:x in F1;
    consider F be Function of X,X such that
 A3:x=F & F is one-to-one by A2;
    card X=card X;
    then F is onto by A3,STIRL2_1:70;
    then F is bijective by A3,FUNCT_2:def 4;
    hence thesis by A3;
  end;
  F2 c= F1
  proof
    let x such that A2:x in F2;
    ex F be Function of X,X st x=F & F is Permutation of X by A2;
    hence thesis;
  end;
  then A2:F1=F2 by A1,XBOOLE_0:def 10;
  (card X-'card X)!=1 by NEWTON:18,GOBOARD9:1;
  then card X!/((card X-'card X)!)=card X!;
  hence thesis by A2,Th6;
end;  

definition let X,k,x1,x2; 
  func Choose(X,k,x1,x2) -> Subset of Funcs(X,{x1,x2}) means :Def1:
  x in it iff ex f be Function of X,{x1,x2} st f = x & Card (f"{x1})=k;
  existence
    proof
      defpred P[set] means
        ex f be Function of X,{x1,x2} st $1=f & Card (f"{x1})=k;
      consider F being set such that
   A1:x in F iff x in bool [:X,{x1,x2}:] & P[x] from XBOOLE_0:sch 1;
      F c= Funcs(X,{x1,x2})
      proof
        let x such that A2:x in F;
        ex f be Function of X,{x1,x2} st x=f & Card (f"{x1})=k by A1,A2;
        hence thesis by FUNCT_2:11;
      end;
      then reconsider F as Subset of Funcs(X,{x1,x2});
      take F;
      let x;
      thus x in F implies
        ex f be Function of X,{x1,x2} st x=f &  Card (f"{x1}) = k by A1;
      given f be Function of X,{x1,x2} such that
        A2:x = f and A3:Card (f"{x1})=k;
      thus x in F by A1,A2,A3;
    end;  
  uniqueness
    proof
    let F1,F2 be Subset of Funcs(X,{x1,x2}) such that
 A1:x in F1 iff ex f be Function of X,{x1,x2} st x = f & Card (f"{x1})=k and
 A2:x in F2 iff ex f be Function of X,{x1,x2} st x = f & Card (f"{x1})=k;
    x in F1 iff x in F2
      proof
       x in F1 iff ex f be Function of X,{x1,x2} st x=f& Card (f"{x1})=k by A1;
       hence thesis by A2;
      end;
    hence thesis by TARSKI:2;
  end;  
end;

theorem
  card X<>k implies Choose(X,k,x1,x1) is empty
proof
  assume A1:card X<>k;
  assume Choose(X,k,x1,x1) is non empty;
  then consider y such that
A2:y in Choose(X,k,x1,x1) by XBOOLE_0:def 1;
  consider f be Function of X,{x1,x1} such that
A3:f = y & Card (f"{x1}) = k by A2,Def1;
  per cases;
    suppose rng f is empty;
      then Card (f"{x1})=0 & dom f={} & dom f=X
        by CARD_1:47,FUNCT_2:def 1,RELAT_1:65,FUNCT_1:142;
      hence thesis by A1,A3,CARD_2:59;
    end;
    suppose A4:rng f is non empty;
      rng f c={x1,x1} & {x1,x1}={x1} by ENUMSET1:69;
      then rng f={x1} by A4,ZFMISC_1:39;
      then f"{x1}=dom f & dom f=X by RELAT_1:169,FUNCT_2:def 1;
      hence thesis by A1,A3;
    end;
end;

theorem Th9:
  card X < k implies Choose(X,k,x1,x2) is empty
proof
  assume A1:card X < k;
  assume Choose(X,k,x1,x2) is non empty;
  then consider z such that
    A2:z in Choose(X,k,x1,x2) by XBOOLE_0:def 1;
  consider f be Function of X,{x1,x2} such that
    A3:f = z & Card (f"{x1}) = k by A2,Def1;
  thus thesis by A1,A3,CARD_1:80;
end;

theorem Th10:
  x1<>x2 implies card Choose(X,0,x1,x2)=1
proof
  assume A1:x1<>x2;
  per cases;
    suppose A2:X is empty;
      then dom {}=X & rng {} ={} & {} c= {x1,x2} by XBOOLE_1:2;
      then reconsider Empty={} as Function of X,{x1,x2} by FUNCT_2:4;
      Empty"{x1}={} & card {}=0 by CARD_1:47,FUNCT_1:142,RELAT_1:60;
      then A3:Empty in Choose(X,0,x1,x2) by Def1;
      Choose(X,0,x1,x2) c= {Empty}
      proof
        let z such that A4:z in Choose(X,0,x1,x2);
        consider f be Function of X,{x1,x2} such that
          A5:z=f & Card (f"{x1})=0 by A4,Def1;
        dom f=X by FUNCT_2:def 1;
        then f=Empty by A2,RELAT_1:64;
        hence thesis by A5,TARSKI:def 1;
      end;
      then Choose(X,0,x1,x2)={Empty} by A3,ZFMISC_1:39;
      hence thesis by CARD_1:79;
    end;
    suppose A2:X is non empty;
      then consider f be Function such that
   A3:dom f=X & rng f={x2} by FUNCT_1:15;
      not x1 in rng f & rng f c= {x1,x2} by A1,A3,TARSKI:def 1,ZFMISC_1:42;
      then f"{x1}={} & f is Function of X,{x1,x2} & card {}=0
        by A3,FUNCT_1:142,FUNCT_2:4,CARD_1:47;
      then A4:f in Choose(X,0,x1,x2) by Def1;
      Choose(X,0,x1,x2) c= {f}
      proof
        let z such that A5:z in Choose(X,0,x1,x2);
        consider g be Function of X,{x1,x2} such that
     A6:z=g & Card (g"{x1})=0 by A5,Def1;
        g"{x1}={} by A6,CARD_2:59;
        then not x1 in rng g & dom g=X by FUNCT_1:142,FUNCT_2:def 1;
        then rng g<>{} & not rng g={x1} & not rng g={x1,x2}
          by A2,TARSKI:def 1,def 2,RELAT_1:65;
        then dom g=X & rng g={x2} by FUNCT_2:def 1,ZFMISC_1:42;
        then g=f by A3,FUNCT_1:17;
        hence thesis by A6,TARSKI:def 1;
      end;
      then Choose(X,0,x1,x2)={f} by A4,ZFMISC_1:39;
      hence thesis by CARD_1:79;
    end;
end;

theorem Th12:
  card Choose(X,card X,x1,x2)=1
proof
  per cases;
    suppose A2:X is empty;
      then dom {}=X & rng {} ={} & {} c= {x1,x2} by XBOOLE_1:2;
      then reconsider Empty={} as Function of X,{x1,x2} by FUNCT_2:4;
      Empty"{x1}={} & card {}=0 by CARD_1:47,FUNCT_1:142,RELAT_1:60;
      then A3:Empty in Choose(X,card X,x1,x2) by A2,Def1;
      Choose(X,card X,x1,x2) c= {Empty}
      proof
        let z such that A4:z in Choose(X,card X,x1,x2);
        consider f be Function of X,{x1,x2} such that
     A5:z=f & Card (f"{x1})=Card X by A4,Def1;
        dom f=X by FUNCT_2:def 1;
        then f=Empty by A2,RELAT_1:64;
        hence thesis by A5,TARSKI:def 1;
      end;
      then Choose(X,card X,x1,x2)={Empty} by A3,ZFMISC_1:39;
      hence thesis by CARD_1:79;
    end;
    suppose A2:X is non empty;
      then consider f be Function such that
        A3:dom f=X & rng f={x1} by FUNCT_1:15;
      rng f c= {x1,x2} & f"{x1}=X by A3,ZFMISC_1:42,RELAT_1:169;
      then f is Function of X,{x1,x2} & Card (f"{x1})=card X
        by A3,FUNCT_2:4;
      then A4:f in Choose(X,card X,x1,x2) by Def1;
      Choose(X,card X,x1,x2) c= {f}
      proof
        let z such that A5:z in Choose(X,card X,x1,x2);
        consider g be Function of X,{x1,x2} such that
     A6:z=g & Card (g"{x1})=card X by A5,Def1;
        now per cases;
          suppose A1:x1=x2;
            dom g=X by FUNCT_2:def 1;
            then rng g<>{} & rng g c={x1,x2} & {x1,x2}={x1}
              by A1,A2,RELAT_1:65,ENUMSET1:69;
            hence rng g={x1} by ZFMISC_1:39;
          end;
          suppose A1:x1<>x2;
            g"{x2}={}
            proof
              assume g"{x2}<>{};
              then consider z such that
           A7:z in g"{x2} by XBOOLE_0:def 1;
              z in dom g & g.z in {x2} & g"{x1}=X
                by A6,A7,Th11,FUNCT_1:def 13;
              then g.z in {x1} & g.z=x2 by TARSKI:def 1,FUNCT_1:def 13;
              hence thesis by A1,TARSKI:def 1;  
            end;
            then not x2 in rng g & dom g=X  by FUNCT_1:142,FUNCT_2:def 1;
            then rng g<>{} & not rng g={x2} & not rng g={x1,x2}
              by A2,TARSKI:def 1,def 2,RELAT_1:65;
            hence rng g={x1} by ZFMISC_1:42;
          end;
        end;
        then dom g=X & rng g={x1} by FUNCT_2:def 1;
        then g=f by A3,FUNCT_1:17;
        hence thesis by A6,TARSKI:def 1;
      end;
      then Choose(X,card X,x1,x2)={f} by A4,ZFMISC_1:39;
      hence thesis by CARD_1:79;
    end;
end;

theorem Th13:
  f.y=x & y in dom f implies {y}\/(f|(dom f\{y}))"{x}=f"{x}
  proof
    assume A1:f.y=x & y in dom f;
    set d=dom f\{y};
 A2:(f|d)"{x} c= f"{x}
    proof
      let x1 be set such that A6:x1 in (f|d)"{x};
      x1 in dom (f|d) & dom (f|d)=dom f/\d by A6,FUNCT_1:def 13,68;
      then x1 in dom f & f.x1=(f|d).x1 & (f|d).x1 in {x}
        by A6,FUNCT_1:68,def 13,XBOOLE_0:def 3;
      hence thesis by FUNCT_1:def 13;
    end;
    {y} c= f"{x}
    proof
      let z such that A4:z in {y};
      z=y & f.y in {x} by A1,A4,TARSKI:def 1;
      hence thesis by A1,FUNCT_1:def 13;
    end;
    then A3:{y}\/(f|d)"{x} c= f"{x} by A2,XBOOLE_1:8;
    f"{x} c= {y}\/(f|d)"{x}
    proof
      let x1 be set such that A3:x1 in f"{x};
      x1 in dom f & not x1 in {y} or x1=y by A3,FUNCT_1:def 13,TARSKI:def 1;
      then x1 in dom f & x1 in d & dom (f|d)=dom f/\d or x1=y
        by FUNCT_1:68,XBOOLE_0:def 4;
      then x1 in dom (f|d) or x1=y by XBOOLE_0:def 3;
      then x1 in dom (f|d) & f.x1=(f|d).x1 & f.x1 in {x} or x1 in {y}
        by A3,FUNCT_1:68,def 13,TARSKI:def 1;
      then x1 in (f|d)"{x} or x1 in {y} by FUNCT_1:def 13;
      hence thesis by XBOOLE_0:def 2;
    end;
    hence thesis by A3,XBOOLE_0:def 10;
  end;

theorem Th14:
  not z in X implies
    card Choose(X,k,x,y)=
    Card{f where f is Function of X\/{z},{x,y}:Card (f"{x})=k+1 & f.z=x}
proof
  assume A1:not z in X;
  set F1={f where f is Function of X\/{z},{x,y}:Card (f"{x})=k+1 & f.z=x};
A3:{x,y} is empty implies X is empty;
  defpred P[set,set,set] means
    for f be Function st f=$1 holds Card ((f|X)"{x})=k;
  A4:for f be Function of X\/{z},{x,y}\/{x} st f.z=x holds
      P[f,X\/{z},{x,y}\/{x}] iff P[f|X,X,{x,y}]
      proof
        let f be Function of X\/{z},{x,y}\/{x} such that f.z=x;
        (f|X)=((f|X)|X) by RELAT_1:102;
        hence thesis;
      end; 
   set F2={f where f is Function of X,{x,y}:P[f,X,{x,y}]};
   set F3={f where f is Function of X\/{z},{x,y}\/{x}:
              P[f,X\/{z},{x,y}\/{x}] & rng (f|X) c={x,y} & f.z=x};
A5:F2 c=Choose(X,k,x,y)
      proof
        let x1 such that A6:x1 in F2;
        consider f be Function of X,{x,y} such that
          A7:x1=f & P[f,X,{x,y}] by A6;
        dom f=X by FUNCT_2:def 1;
        then f|X=f by RELAT_1:97;
        then Card (f"{x})=k by A7;
        hence thesis by A7,Def1;
      end;
   A6:Choose(X,k,x,y)c= F2
      proof
        let x1 such that A6:x1 in Choose(X,k,x,y);
        consider f be Function of X,{x,y} such that
          A7:x1=f & Card (f"{x})=k by A6,Def1;
        dom f=X by FUNCT_2:def 1;
        then P[f,X,{x,y}] by A7,RELAT_1:97;
        hence thesis by A7;
      end;
   A7:F1 c= F3
      proof
        let x1 such that A6:x1 in F1;
        consider f be Function of X\/{z},{x,y} such that
     A7:x1=f & Card (f"{x})=k+1 & f.z=x by A6;
     A8:{x}\/{x,y}={x,x,y} by ENUMSET1:42;
        z in {z} & not z in dom f/\X by A1,TARSKI:def 1,XBOOLE_0:def 3;
        then z in X\/{z} & dom f=X\/{z} & (X\/{z})\{z}=X &
        not z in dom (f|X) by A1,Lm2,XBOOLE_0:def 2,FUNCT_2:def 1,FUNCT_1:68;
        then ((f|X)"{x})\/{z}=f"{x} & ((f|X)"{x}) is finite
         & not z in ((f|X)"{x}) by A7,Th13,FUNCT_1:def 13;
        then card ((f|X)"{x})+1=k+1 & k=(k+1)-1 & 
          (card ((f|X)"{x})+1)-1=card ((f|X)"{x})+(1-1) by A7,CARD_2:54;
        then Card ((f|X)"{x})=k & {x,y}\/{x}={x,y} by A8,ENUMSET1:70;
        then P[f,X\/{z},{x,y}\/{x}] & rng (f|X) c={x,y} & f.z=x &
        f is Function of X\/{z},{x,y}\/{x} by A7;
        hence thesis by A7;
      end;
   F3 c= F1
   proof
     let x1 such that A8:x1 in F3;
     consider f be Function of X\/{z},{x,y}\/{x} such that
  A9:x1=f & P[f,X\/{z},{x,y}\/{x}] & rng (f|X) c={x,y} & f.z=x by A8;
     z in {z} & not z in dom f/\X by A1,TARSKI:def 1,XBOOLE_0:def 3;
     then z in X\/{z} & dom f=X\/{z} & (X\/{z})\{z}=X &
     not z in dom (f|X) by A1,Lm2,XBOOLE_0:def 2,FUNCT_2:def 1,FUNCT_1:68;
     then {z}\/(f|X)"{x}=f"{x} & not z in (f|X)"{x} & {x}\/{x,y}={x,x,y}&
      card ((f|X)"{x})=k by A9,Th13,FUNCT_1:def 13,ENUMSET1:42;
     then card (f"{x})=k+1 & {x,y}\/{x}={x,y} by CARD_2:54,ENUMSET1:70;
     hence thesis by A9;
   end;
   then A8:F1=F3 by A7,XBOOLE_0:def 10;
   Card F2=Card F3 from STIRL2_1:sch 4(A3,A1,A4);
   hence thesis by A5,A6,A8,XBOOLE_0:def 10;
end;

theorem Th15:
  f.y<>x implies (f|(dom f\{y}))"{x}=f"{x}
  proof
    set d=dom f\{y};
    assume A1:f.y<>x;
 A2:(f|d)"{x} c= f"{x}
    proof
      let x1 be set such that A6:x1 in (f|d)"{x};
      x1 in dom (f|d) & dom (f|d)=dom f/\d by A6,FUNCT_1:def 13,68;
      then x1 in dom f & f.x1=(f|d).x1 & (f|d).x1 in {x}
        by A6,FUNCT_1:68,def 13,XBOOLE_0:def 3;
      hence thesis by FUNCT_1:def 13;
    end;
    f"{x} c= (f|d)"{x}
    proof
      let x1 be set such that A3:x1 in f"{x};
      x1 in dom f & f.x1 in {x} by A3,FUNCT_1:def 13;
      then x1 in dom f & f.x1=x by TARSKI:def 1;
      then x1 in dom f & not x1 in {y} by A1,TARSKI:def 1;
      then x1 in dom f&x1 in d&dom (f|d)=dom f/\d by FUNCT_1:68,XBOOLE_0:def 4;
      then x1 in dom (f|d) by XBOOLE_0:def 3;
      then x1 in dom (f|d) & f.x1=(f|d).x1 & f.x1 in {x}
        by A3,FUNCT_1:68,def 13;
      hence thesis by FUNCT_1:def 13;
    end;
    hence thesis by A2,XBOOLE_0:def 10;
  end;
  
theorem Th16:
  not z in X & x<>y implies
    card Choose(X,k,x,y)=
    Card{f where f is Function of X\/{z},{x,y}:Card (f"{x})=k & f.z=y}
proof
  assume that A1:not z in X and A1':x<>y;
  defpred P[set,set,set] means
    for f be Function st f=$1 holds Card (f"{x})=k;
  A2:{x,y} is empty implies X is empty;
  A3:for f be Function of X\/{z},{x,y}\/{y} st f.z=y
    holds P[f,X\/{z},{x,y}\/{y}] iff P[f|X,X,{x,y}]
  proof
    let f be Function of X\/{z},{x,y}\/{y} such that A4:f.z=y;
    (X\/{z})\{z}=X & dom f=X\/{z} & f.z<>x by A1,A1',A4,Lm2,FUNCT_2:def 1;
    then (f|X)"{x} = f"{x} by Th15;           
    hence thesis;
  end;
  set F1={f where f is Function of X,{x,y}:P[f,X,{x,y}]};
  set F2={f where f is Function of X\/{z},{x,y}\/{y}:
              P[f,X\/{z},{x,y}\/{y}] & rng (f|X) c={x,y} & f.z=y};
  set F3={f where f is Function of X\/{z},{x,y}:Card (f"{x})=k & f.z=y};
  A4:F2 c= F3
  proof
    let x1 such that A5:x1 in F2;
    consider f be Function of X\/{z},{x,y}\/{y} such that
    A6:x1=f & P[f,X\/{z},{x,y}\/{y}] & rng (f|X) c={x,y} & f.z=y by A5;
    {x,y}\/{y}={y,y,x} by ENUMSET1:42;
    then {x,y}\/{y}={x,y} by ENUMSET1:70;
    then Card (f"{x})=k & f.z=y & f is Function of X\/{z},{x,y} by A6;
    hence thesis by A6;    
  end;
  A5:F3 c= F2
  proof
    let x1 such that A5:x1 in F3;
    consider f be Function of X\/{z},{x,y}such that
      A6:f=x1 & Card (f"{x})=k & f.z=y by A5;
    {x,y}\/{y}={y,y,x} by ENUMSET1:42;
    then {x,y}\/{y}={x,y} by ENUMSET1:70;
    then P[f,X\/{z},{x,y}\/{y}] &
      rng (f|X) c={x,y} & f is Function of X\/{z},{x,y}\/{y} by A6;
    hence thesis by A6;  
  end;
  A6:F1 c= Choose(X,k,x,y)
  proof
    let x1 such that A7:x1 in F1;
    consider f be Function of X,{x,y} such that
 A8:x1=f & P[f,X,{x,y}] by A7;
    Card (f"{x})=k by A8;
    hence thesis by A8,Def1;
  end;
  Choose(X,k,x,y) c= F1
  proof
    let x1 such that A7:x1 in Choose(X,k,x,y);
    consider f be Function of X,{x,y} such that
A8: x1=f & Card (f"{x})=k by A7,Def1;
    P[f,X,{x,y}] by A8;
    hence thesis by A8;
  end;
  then A7:Choose(X,k,x,y) = F1 by A6,XBOOLE_0:def 10;
  Card F1=Card F2 from STIRL2_1:sch 4(A2,A1,A3);
  hence thesis by A4,A5,A7,XBOOLE_0:def 10; 
end;

Lm_Th17:
   x<>y & not z in X implies
   {f where f is Function of X\/{z},{x,y}:
            Card (f"{x})=k+1 & f.z=x}\/{f where f is Function of X\/{z},{x,y}:
            Card (f"{x})=k+1 & f.z=y}=Choose(X\/{z},k+1,x,y) &
   {f where f is Function of X\/{z},{x,y}:
            Card (f"{x})=k+1 & f.z=x} misses 
   {f where f is Function of X\/{z},{x,y}:
         Card (f"{x})=k+1 & f.z=y}
proof
  assume A1:x<>y & not z in X;
  set F1={f where f is Function of X\/{z},{x,y}:Card (f"{x})=k+1 & f.z=x};
  set F2={f where f is Function of X\/{z},{x,y}:Card (f"{x})=k+1 & f.z=y};
  A2:F1 c=Choose(X\/{z},k+1,x,y)
  proof
    let x1 such that A3:x1 in F1;
    ex f be Function of X\/{z},{x,y} st x1=f & Card (f"{x})=k+1 & f.z=x by A3;
    hence thesis by Def1;
  end;
  F2 c=Choose(X\/{z},k+1,x,y)
  proof
    let x1 such that A3:x1 in F2;
    ex f be Function of X\/{z},{x,y} st x1=f & Card (f"{x})=k+1 & f.z=y by A3;
    hence thesis by Def1;
  end;
  then A3:F1\/F2 c= Choose(X\/{z},k+1,x,y) by A2,XBOOLE_1:8;
  A4:Choose(X\/{z},k+1,x,y) c= F1\/F2
  proof
    let x1 such that A4:x1 in Choose(X\/{z},k+1,x,y);
    consider f be Function of X\/{z},{x,y} such that
 A5:f=x1 & Card (f"{x})=k+1 by A4,Def1;
    z in {z} by TARSKI:def 1;
    then z in X\/{z} & dom f=X\/{z} by XBOOLE_0:def 2,FUNCT_2:def 1; 
    then f.z in rng f by FUNCT_1:def 5;
    then f.z=x or f.z=y by TARSKI:def 2;
    then x1 in F1 or x1 in F2 by A5;
    hence thesis by XBOOLE_0:def 2;
  end;
  F1 misses F2
  proof
    assume F1 meets F2;
    then F1/\F2<>{} by XBOOLE_0:def 7;
    then consider x1 such that
 A6:x1 in F1/\F2 by XBOOLE_0:def 1;
    x1 in F1 by A6,XBOOLE_0:def 3;
    then consider f1 be Function of X\/{z},{x,y} such that
 A7:x1=f1 & Card (f1"{x})=k+1 & f1.z=x;
    x1 in F2 by A6,XBOOLE_0:def 3;
    then consider f2 be Function of X\/{z},{x,y} such that
 A8:x1=f2 & Card (f2"{x})=k+1 & f2.z=y;
    thus thesis by A7,A8,A1;
  end; 
  hence thesis by A3,A4,XBOOLE_0:def 10;
end;

theorem Th17:
  x<>y & not z in X implies card Choose(X\/{z},k+1,x,y)=
    card Choose(X,k+1,x,y)+ card Choose(X,k,x,y)
proof
  assume A1:x<>y & not z in X;
  set F1={f where f is Function of X\/{z},{x,y}:Card (f"{x})=k+1 & f.z=x};
  set F2={f where f is Function of X\/{z},{x,y}:Card (f"{x})=k+1 & f.z=y};
  F1 \/F2= Choose(X\/{z},k+1,x,y) & F1 c= F1\/F2 & F2 c= F1\/F2
    by A1,XBOOLE_1:7,Lm_Th17;
  then reconsider F1,F2 as finite set by FINSET_1:13;
  F1 misses F2 by A1,Lm_Th17;
  then card (F1 \/ F2) = card F1 + card F2 &
  card F2=card Choose(X,k+1,x,y) &
  card F1=card Choose(X,k,x,y) by A1,Th14,Th16,CARD_2:53;
  hence thesis by A1,Lm_Th17;
end;

:: n choose k

theorem Th19:
  x<>y implies card Choose(X,k,x,y)=card X choose k
proof
  assume A1:x<>y;
  defpred P[Nat] means
    for k,X st k+ card X <= $1 holds
      card Choose(X,k,x,y)=(card X) choose k;
  A2:P[0]    
  proof
    let k,X such that A3:k+ card X <= 0;
    k+ card X =0 by A3,NAT_1:18;
    then card X=0 & k=0 by NAT_1:23;
    hence thesis by A1,Th10,NEWTON:27;
  end;  
A3:for n st P[n] holds P[n+1]
  proof
    let n such that A4:P[n];
    let k,X such that A5:k+ card X <= n+1;
    per cases by A5,REAL_1:def 5;
      suppose k+ card X < n+1;
        then k+card X<=n by NAT_1:38;
        hence thesis by A4;
      end;
      suppose A6:k+ card X = n+1;
        per cases by NAT_1:18;
          suppose k=0 & card X>=0;
            then card Choose(X,k,x,y)=1&card X choose k=1 by A1,Th10,NEWTON:29;
            hence thesis;
          end;
          suppose k>0 & card X=0;
            then Choose(X,k,x,y) is empty & card X choose k =0
              by Th9,NEWTON:def 3;
            hence thesis by CARD_1:47;
          end;
          suppose A7:k>0 & card X>0;
            then reconsider k1=k-1 as Nat by NAT_1:60;
            reconsider cXz=(card X)-1 as Nat by A7,NAT_1:60;
            consider z such that
         A8:z in X by XBOOLE_0:def 1,A7,CARD_1:47;
            set Xz=X\{z};
            card X=cXz+1 & cXz< cXz+1 by NAT_1:38;
            then card Xz < card X & k1<k1+1 by NAT_1:38,A8,STIRL2_1:65;
            then k+card Xz<n+1 & k1+card Xz<n+1 & z in {z}
              by A6,REAL_1:67,TARSKI:def 1;
            then k+card Xz<=n & k1+card Xz<=n & Xz\/{z}=X & not z in Xz &
              card X=cXz+1 by NAT_1:38,A8,Lm1,XBOOLE_0:def 4;
            then card Choose(Xz,k1+1,x,y)=(card Xz) choose (k1+1) &
                 card Choose(Xz,k1,x,y)=(card Xz) choose k1 &
                 card Choose(X,k1+1,x,y)=
                 card Choose(Xz,k1+1,x,y)+ card Choose(Xz,k1,x,y) &
                 card Xz=cXz & card X=cXz+1 by A1,A4,Th17,A8,STIRL2_1:65;
            hence thesis by NEWTON:32;
          end;
      end;
  end;
  for n holds P[n] from NAT_1:sch 1(A2,A3);
  then P[k+card X];
  hence thesis;
end;

theorem Th20:
  x<>y implies (Y-->y)+*(X-->x) in Choose(X\/Y,card X,x,y)
  proof
    assume A1:x<>y;
    set F=(Y-->y)+*(X-->x);
    dom (Y-->y)=Y & dom (X-->x)=X &  rng (X-->x) c= {x} & rng (Y-->y) c= {y}&
      {x} c= {x,y} & {y} c= {x,y} by FUNCOP_1:19,ZFMISC_1:12;
    then A2:dom F=X\/Y & rng (X-->x)c= {x,y} & rng (Y-->y) c= {x,y}
      by FUNCT_4:def 1,XBOOLE_1:1;
    then dom F=X\/Y & rng F c= rng(X-->x)\/rng(Y-->y) &
    rng(X-->x)\/rng(Y-->y)c={x,y} by XBOOLE_1:8,FUNCT_4:18;
    then dom F=X\/Y & rng F c= {x,y} by XBOOLE_1:1;
    then reconsider F as Function of X\/Y,{x,y} by FUNCT_2:4;
    A3:F"{x}c=X
    proof
      let z such that A4:z in F"{x};
      assume A5:not z in X;
      z in dom F by A4,FUNCT_1:def 13;
      then (z in X or z in Y) & z in dom (X-->x)\/dom (Y-->y)
        & X=dom (X-->x) & F.z in {x}
        by A4,XBOOLE_0:def 2,FUNCT_4:def 1,FUNCOP_1:19,FUNCT_1:def 13;
      then z in Y & F.z=(Y-->y).z & F.z=x by A5,FUNCT_4:def 1,TARSKI:def 1;
      hence contradiction by A1,FUNCOP_1:13;     
    end;  
    X c= F"{x}
    proof
      let z such that A4:z in X;
      z in dom (X-->x) by A4,FUNCOP_1:19;
      then F.z=(X-->x).z & (X-->x).z=x by A4,FUNCT_4:14,FUNCOP_1:13;
      then F.z in {x} & z in dom F by A2,A4,XBOOLE_0:def 2,TARSKI:def 1;
      hence thesis by FUNCT_1:def 13;
    end;
    then X=F"{x} by A3,XBOOLE_0:def 10;
    hence thesis by Def1;
  end;

theorem Th21:
  x<>y & X misses Y implies
  (X-->x)+*(Y-->y) in Choose(X\/Y,card X,x,y)
proof
  assume A1:x<>y & X misses Y;
  dom (X-->x)=X & dom (Y-->y)=Y by FUNCOP_1:19;
  then (X-->x)+*(Y-->y)=(Y-->y)+*(X-->x) by A1,FUNCT_4:36;
  hence thesis by A1,Th20;
end;
  
definition
  let F,Ch be Function,y be set;
  func Intersection(F,Ch,y) -> Subset of union rng F means :Def2:
  z in it iff z in union rng F &
              for x st x in dom Ch & Ch.x=y holds z in F.x;
  existence
  proof
    defpred P[set] means
     for x st x in dom Ch & Ch.x=y holds $1 in F.x;
    consider I be set such that
 A1:z in I iff z in union rng F & P[z] from XBOOLE_0:sch 1;
    I c= union rng F
    proof
      let x such that A2:x in I;
      thus thesis by A1,A2;
    end;
    then reconsider I as Subset of union rng F;
    take I;
    thus thesis by A1;  
  end;
  uniqueness
  proof
    let I1,I2 be Subset of union rng F such that
 A1:z in I1 iff z in union rng F &
      for x st x in dom Ch & Ch.x=y holds z in F.x and
 A2:z in I2 iff z in union rng F &
      for x st x in dom Ch & Ch.x=y holds z in F.x;
    z in I1 iff z in I2
    proof
      z in I1 iff z in union rng F &
      for x st x in dom Ch & Ch.x=y holds z in F.x by A1;
      hence thesis by A2;
    end;
    hence thesis by TARSKI:2;
  end;
end;

reserve F,Ch for Function;

theorem Th22:
  for F,Ch st dom F/\Ch"{x} is non empty holds
    y in Intersection(F,Ch,x) iff
      for z st z in dom Ch & Ch.z=x holds y in F.z
proof
  let F,Ch such that A1:dom F/\Ch"{x} is non empty;
  thus y in Intersection(F,Ch,x) implies
    for z st z in dom Ch & Ch.z=x holds y in F.z by Def2;
  thus (for z st z in dom Ch & Ch.z=x holds y in F.z) implies
    y in Intersection(F,Ch,x) 
  proof
    assume A2:for z st z in dom Ch & Ch.z=x holds y in F.z;
    consider z such that
A3: z in dom F/\Ch"{x} by A1,XBOOLE_0:def 1;
    z in Ch"{x} by A3,XBOOLE_0:def 3;
    then z in dom Ch & Ch.z in {x} by FUNCT_1:def 13;
    then z in dom F & z in dom Ch& Ch.z=x by A3,TARSKI:def 1,XBOOLE_0:def 3;
    then F.z in rng F & y in F.z by A2,FUNCT_1:def 5;
    then y in union rng F by TARSKI:def 4;
    hence thesis by A2,Def2;
  end;
end;  

theorem Th23:
  Intersection(F,Ch,y) is non empty implies Ch"{y} c= dom F
proof
  assume Intersection(F,Ch,y) is non empty;
  then consider z such that
A1:z in Intersection(F,Ch,y) by XBOOLE_0:def 1;
  assume not Ch"{y} c= dom F;
  then consider x such that
A3:x in Ch"{y} & not x in dom F by TARSKI:def 3;
  Ch.x in {y} by A3,FUNCT_1:def 13;
  then Ch.x=y & x in dom Ch by A3,FUNCT_1:def 13,TARSKI:def 1;
  then z in F.x by A1,Def2;
  hence thesis by A3,FUNCT_1:def 4;
end;
  
theorem
  Intersection(F,Ch,y) is non empty implies
    for x1,x2 st x1 in Ch"{y} & x2 in Ch"{y} holds F.x1 meets F.x2
proof
  assume Intersection(F,Ch,y) is non empty;
  then consider z such that
A1:z in Intersection(F,Ch,y) by XBOOLE_0:def 1;
  let x1,x2 such that A2:x1 in Ch"{y} & x2 in Ch"{y};  
  Ch.x1 in {y} & Ch.x2 in {y} by A2,FUNCT_1:def 13;
  then Ch.x1=y & x1 in dom Ch & Ch.x2=y & x2 in dom Ch
    by A2,FUNCT_1:def 13,TARSKI:def 1;
  then z in F.x1 & z in F.x2 by A1,Def2;
  hence thesis by XBOOLE_0:3;
end;

theorem Th25:
  z in Intersection(F,Ch,y) & y in rng Ch implies
    ex x st x in dom Ch & Ch.x=y & z in F.x
proof
  assume A1:z in Intersection(F,Ch,y) & y in rng Ch;
  then Ch"{y} <> {} by FUNCT_1:142;
  then consider x such that
A2:x in Ch"{y} by XBOOLE_0:def 1;
  Ch.x in {y} by A2,FUNCT_1:def 13;
  then Ch.x=y & x in dom Ch by A2,FUNCT_1:def 13,TARSKI:def 1;
  then x in dom Ch & Ch.x=y & z in F.x by A1,Def2;
  hence thesis;
end;

theorem
  F is empty or union rng F is empty implies
    Intersection(F,Ch,y)=union rng F
proof
  assume F is empty or union rng F is empty;
  then for z holds z in union rng F iff z in Intersection(F,Ch,y)
    by RELAT_1:60,ZFMISC_1:2;
  hence thesis by TARSKI:2;
end;    

theorem Th27:
  F|Ch"{y}=(Ch"{y}-->union rng F) implies Intersection(F,Ch,y) = union rng F
proof
  set ChF=Ch"{y}-->union rng F;  
  assume A1:F|Ch"{y}=ChF;
  union rng F c= Intersection(F,Ch,y)
  proof
    let z such that A2:z in union rng F;
    now let x such that A3:x in dom Ch & Ch.x=y;
      Ch.x in {y} by A3,TARSKI:def 1;
      then x in Ch"{y} & dom ChF= Ch"{y} by A3,FUNCT_1:def 13,FUNCOP_1:19;
      then ChF.x=union rng F & F.x=ChF.x by A1,FUNCOP_1:13,FUNCT_1:68;
      hence z in F.x by A2;
    end;
    hence thesis by A2,Def2;
  end;
  hence thesis by XBOOLE_0:def 10;  
end;  

theorem
  union rng F is non empty & Intersection(F,Ch,y)=union rng F
    implies F|Ch"{y}=(Ch"{y}-->union rng F)
proof
  set ChF=Ch"{y}-->union rng F;
  assume that A1:union rng F is non empty and
              A2:Intersection(F,Ch,y) = union rng F;
  assume A3:F|Ch"{y}<>ChF;
  Ch"{y} c= dom F by A1,A2,Th23;
  then dom F/\Ch"{y}=Ch"{y} & dom F/\Ch"{y} =dom (F|Ch"{y})
    by XBOOLE_1:28,FUNCT_1:68;
  then dom (F|Ch"{y})=dom ChF by FUNCOP_1:19;
  then consider x such that
A4:x in dom (F|Ch"{y}) & (F|Ch"{y}).x<>ChF.x by A3,FUNCT_1:9;
  x in dom F /\ Ch"{y} by A4,FUNCT_1:68;
  then x in Ch"{y} by XBOOLE_0:def 3;  
  then A5:x in dom Ch & Ch.x in {y} & ChF.x=union rng F
    by FUNCT_1:def 13,FUNCOP_1:13;
  x in dom F /\ Ch"{y} by A4,FUNCT_1:68;
  then x in dom F & F.x=(F|Ch"{y}).x by A4,FUNCT_1:68,XBOOLE_0:def 3;
  then (F|Ch"{y}).x in rng F by FUNCT_1:def 5;
  then (F|Ch"{y}).x c= ChF.x by A5,ZFMISC_1:92;
  then not union rng F c= (F|Ch"{y}).x by A4,A5,XBOOLE_0:def 10;
  then consider z such that
A6:z in union rng F & not z in (F|Ch"{y}).x by TARSKI:def 3;
  x in dom Ch & Ch.x =y by A5,TARSKI:def 1;
  then z in F.x & (F|Ch"{y}).x=F.x by A2,A4,A6,Def2,FUNCT_1:68; 
  hence thesis by A6;
end;

theorem Th29:
  Intersection(F,{},y)=union rng F
proof
  {}"{y} c= dom {} & dom {}={} by RELAT_1:167;
  then {}"{y}={} by XBOOLE_1:3;
  then F|{}"{y}={} & dom({}"{y}-->union rng F)={} by RELAT_1:110,FUNCOP_1:16;
  then F|{}"{y}=({}"{y}-->union rng F) by RELAT_1:64;
  hence thesis by Th27;
end;

theorem Th30:
  Intersection(F,Ch,y) c= Intersection(F,Ch|X',y)
proof
  let z such that A1:z in Intersection(F,Ch,y);
  now let x such that A2:x in dom (Ch|X') & Ch|X'.x=y;
    x in dom Ch/\X' by A2,FUNCT_1:68;
    then x in dom Ch & Ch.x=y by A2,XBOOLE_0:def 3,FUNCT_1:68;
    hence z in F.x by A1,Def2;
  end;  
  hence thesis by A1,Def2;
end;

theorem Th31:
  Ch"{y}=(Ch|X')"{y} implies Intersection(F,Ch,y)=Intersection(F,Ch|X',y)
proof
  assume A1:Ch"{y}=(Ch|X')"{y};
  Intersection(F,Ch|X',y) c=Intersection(F,Ch,y)
  proof
    let z such that A2:z in Intersection(F,Ch|X',y);
    now let x such that A3:x in dom Ch & Ch.x=y;
      Ch.x in {y} by A3,TARSKI:def 1;
      then x in (Ch|X')"{y} by A1,A3,FUNCT_1:def 13;
      then x in dom (Ch|X') & (Ch|X').x in {y} by FUNCT_1:def 13;
      then x in dom (Ch|X') & (Ch|X').x = y by TARSKI:def 1;
      hence z in F.x by A2,Def2;
    end;  
    hence thesis by A2,Def2;
  end;
  then Intersection(F,Ch|X',y) c=Intersection(F,Ch,y) &
   Intersection(F,Ch,y) c= Intersection(F,Ch|X',y) by Th30;
  hence thesis by XBOOLE_0:def 10;
end;

theorem Th32:
  Intersection(F|X',Ch,y) c= Intersection(F,Ch,y)
proof
  let z such that A1:z in Intersection(F|X',Ch,y);
  rng (F|X') c= rng F by RELAT_1:99;
  then A2:union rng (F|X') c= union rng F & z in union rng (F|X')
    by A1,ZFMISC_1:95;
  now let x such that A3:x in dom Ch & Ch.x=y;
    Ch.x in {y} by A3,TARSKI:def 1;
    then x in Ch"{y} & Ch"{y} c= dom (F|X') by A1,A3,Th23,FUNCT_1:def 13;
    then z in F|X'.x & x in dom (F|X') by A1,A3,Def2;
    hence z in F.x by FUNCT_1:68;
  end;
  hence thesis by A2,Def2;
end;

theorem Th33:
  y in rng Ch & Ch"{y} c= X' implies
    Intersection(F|X',Ch,y) = Intersection(F,Ch,y)
proof  
  assume A1:y in rng Ch & Ch"{y} c= X';
  Intersection(F,Ch,y) c=Intersection(F|X',Ch,y)
  proof
    let z such that A2:z in Intersection(F,Ch,y);
    A3:now let x such that A4:x in dom Ch & Ch.x=y;
      z in F.x & Ch.x in {y} by A2,A4,Def2,TARSKI:def 1;
      then x in dom F & x in Ch"{y} by A4,FUNCT_1:def 4,def 13;
      then x in dom F/\X' by A1,XBOOLE_0:def 3;
      then x in dom (F|X') by FUNCT_1:68;
      then z in F.x & F.x=F|X'.x by Def2,A2,A4,FUNCT_1:68;
      hence z in F|X'.x;
    end;
    consider x such that
A4: x in dom Ch & Ch.x=y & z in F.x by A1,A2,Th25;
    z in F.x & Ch.x in {y} by A4,TARSKI:def 1;
    then x in dom F & x in Ch"{y} by A4,FUNCT_1:def 4,def 13;
    then x in dom F/\X' by A1,XBOOLE_0:def 3;
    then x in dom (F|X') by FUNCT_1:68;
    then z in F|X'.x & F|X'.x in rng (F|X') by FUNCT_1:def 5,A3,A4;
    then z in union rng (F|X') by TARSKI:def 4;
    hence thesis by A3,Def2;
  end;
  then Intersection(F|X',Ch,y) c=Intersection(F,Ch,y) &
  Intersection(F,Ch,y) c= Intersection(F|X',Ch,y) by Th32;
  hence thesis by XBOOLE_0:def 10; 
end;

theorem Th34:
  x in Ch"{y} implies Intersection(F,Ch,y) c= F.x
proof  
  assume A1:x in Ch"{y};
  let z such that A2:z in Intersection(F,Ch,y);
  Ch.x in {y} by A1,FUNCT_1:def 13;
  then x in dom Ch & Ch.x=y by A1,FUNCT_1:def 13,TARSKI:def 1;
  hence thesis by A2,Def2;
end;

theorem Th35:
  x in Ch"{y} implies
    Intersection(F,Ch|(dom Ch\{x}),y)/\F.x=Intersection(F,Ch,y)
proof
  assume A1:x in Ch"{y};
  set d=dom Ch\{x};set Chd=Ch|d;
  set I1=Intersection(F,Ch,y);set I2=Intersection(F,Chd,y);
  per cases;
    suppose not x in dom F;
      then F.x={} & not Ch"{y} c= dom F by A1,FUNCT_1:def 4;
      hence thesis by Th23;
    end;
    suppose x in dom F;
      I1 c= F.x & I1 c= I2 by A1,Th30,Th34;
      then A2:I1 c= I2/\F.x by XBOOLE_1:19;
      I2/\F.x c= I1
      proof
        let z such that A3:z in I2/\F.x;
     A4:now let x1 such that A5:x1 in dom Ch & Ch.x1=y;
          per cases by A5,XBOOLE_0:def 4;
            suppose A6:x1 in d;
              d c= dom Ch by XBOOLE_1:36;
              then dom Ch/\d= dom Chd & dom Ch/\d =d by XBOOLE_1:28,FUNCT_1:68;
              then x1 in dom Chd & Chd.x1=y & z in I2
                by A3,A5,A6,FUNCT_1:68,XBOOLE_0:def 3;
              hence z in F.x1 by Def2;
            end;
            suppose x1 in {x};
              then x1=x & z in F.x by A3,TARSKI:def 1,XBOOLE_0:def 3;
              hence z in F.x1;
            end;
        end;
        z in I2 by A3,XBOOLE_0:def 3;
        hence thesis by A4,Def2;
      end;
      hence thesis by A2,XBOOLE_0:def 10;
    end;
end;

theorem Th36:
  for Ch1,Ch2 be Function st Ch1"{x1}=Ch2"{x2} holds
  Intersection(F,Ch1,x1)=Intersection(F,Ch2,x2)
proof
  let Ch1,Ch2 be Function such that A1:Ch1"{x1}=Ch2"{x2};
  thus Intersection(F,Ch1,x1)c=Intersection(F,Ch2,x2)
  proof
    let z such that A2:z in Intersection(F,Ch1,x1);
    now let x such that A3:x in dom Ch2 & Ch2.x=x2;
      Ch2.x in {x2} by A3,TARSKI:def 1;
      then x in Ch1"{x1} by A1,A3,FUNCT_1:def 13;
      then x in dom Ch1 & Ch1.x in {x1} by FUNCT_1:def 13;
      then x in dom Ch1 & Ch1.x=x1 by TARSKI:def 1;
      hence z in F.x by A2,Def2;
    end;
    hence thesis by A2,Def2;
  end;  
  thus Intersection(F,Ch2,x2)c=Intersection(F,Ch1,x1)
  proof
    let z such that A2:z in Intersection(F,Ch2,x2);
    now let x such that A3:x in dom Ch1 & Ch1.x=x1;
      Ch1.x in {x1} by A3,TARSKI:def 1;
      then x in Ch2"{x2} by A1,A3,FUNCT_1:def 13;
      then x in dom Ch2 & Ch2.x in {x2} by FUNCT_1:def 13;
      then x in dom Ch2 & Ch2.x=x2 by TARSKI:def 1;
      hence z in F.x by A2,Def2;
    end;
    hence thesis by A2,Def2;
  end;  
end;  

theorem Th37:
  Ch"{y}={} implies Intersection(F,Ch,y)=union rng F
  proof
    assume A1:Ch"{y}={};
    reconsider E ={} as set;
    Ch|E c= Ch by RELAT_1:88;
    then (Ch|E)"{y} c= Ch"{y} by RELAT_1:179;
    then (Ch|E)"{y} = Ch"{y} & Ch|E={} & Intersection(F,{},y)=union rng F
      by A1,XBOOLE_1:3,Th29,RELAT_1:110;
    hence thesis by Th36;
  end;

theorem Th38:
  {x}=Ch"{y} implies Intersection(F,Ch,y)=F.x
proof
  assume A1:{x}=Ch"{y};  
  (Ch|(dom Ch\{x}))"{y}=(dom Ch\{x}) /\ {x} & (dom Ch\{x}) misses {x}
    by A1,FUNCT_1:139,XBOOLE_1:79;
  then (Ch|(dom Ch\{x}))"{y}={} by XBOOLE_0:def 7;
  then Intersection(F,Ch|(dom Ch\{x}),y)=union rng F & x in Ch"{y}
    by A1,Th37,TARSKI:def 1;  
  then A2:union rng F/\F.x=Intersection(F,Ch,y) by Th35;
  per cases;
    suppose x in dom F;
      then F.x in rng F by FUNCT_1:def 5;
      then F.x c= union rng F by ZFMISC_1:92;
      hence thesis by A2,XBOOLE_1:28;
    end;
    suppose not x in dom F;
      then F.x={} by FUNCT_1:def 4;
      hence thesis by A2;
    end;
end;

theorem Th39:
  {x1,x2}=Ch"{y} implies Intersection(F,Ch,y)=F.x1 /\ F.x2
proof
  assume A1:{x1,x2}=Ch"{y};
  per cases;
  suppose A2:x1=x2;
    then Ch"{y}={x1} by A1,ENUMSET1:69;
    hence thesis by A2,Th38;
  end;
  suppose A2:x1<>x2;
    Ch"{y} c= dom Ch by RELAT_1:167;
    then Ch"{y}/\(dom Ch\{x1})=(Ch"{y}/\dom Ch)\{x1}&Ch"{y}/\dom Ch={x1,x2}
      by A1,XBOOLE_1:28,49;
    then Ch"{y}/\(dom Ch\{x1})={x2} by A2,ZFMISC_1:23;
    then (Ch|(dom Ch\{x1}))"{y}={x2} & x1 in Ch"{y}
      by A1,FUNCT_1:139,TARSKI:def 2;
    then Intersection(F,Ch|(dom Ch\{x1}),y)/\F.x1=Intersection(F,Ch,y) &
      Intersection(F,Ch|(dom Ch\{x1}),y)=F.x2 by Th35,Th38;
    hence thesis;
  end;
end;

theorem
  for F st F is non empty holds
    y in Intersection(F,(dom F-->x),x) iff
      for z st z in dom F holds y in F.z
proof
  let F such that A1:F is non empty;set Ch=dom F-->x;
  thus y in Intersection(F,Ch,x) implies
    for z st z in dom F holds y in F.z
  proof
    assume A2:y in Intersection(F,Ch,x);
    let z such that A3:z in dom F;
    z in dom Ch & Ch.z=x by A3,FUNCOP_1:13,19;
    hence thesis by A2,Def2;
  end;
  assume A2:for z st z in dom F holds y in F.z;
  Ch"{x}=dom F by FUNCOP_1:21;
  then A3:dom F/\Ch"{x}=dom F & dom F<>{} by A1,RELAT_1:64;
  now let z such that A4:z in dom Ch & Ch.z=x;
    z in dom F by A4,FUNCOP_1:19;
    hence y in F.z by A2;
  end;
  hence thesis by A3,Th22;
end;

definition
  let F be Function; :: PRE_CIRC:def 3
  attr F is finite-yielding means :Def3:
    for x holds F.x is finite;
end;

registration
  cluster non empty finite-yielding Function;
  existence
  proof
    consider F be Function such that
 A1:dom F=1 & rng F={1} by FUNCT_1:15;
    take F;
    now let x;
      per cases;
      suppose x in dom F;
        then F.x in rng F by FUNCT_1:def 5;
        hence F.x is finite by A1,TARSKI:def 1;
      end;
      suppose not x in dom F;
        hence F.x is finite by FUNCT_1:def 4;
      end;
    end;
    hence thesis by A1,RELAT_1:60,Def3;
  end;
  cluster empty finite-yielding Function;
  existence
  proof
    reconsider Empty={} as Function;
    now let x;
      dom {}={};
      hence Empty.x is finite by FUNCT_1:def 4;
    end;
    then Empty is finite-yielding by Def3;
    hence ex F be Function st F is empty finite-yielding;
  end;
end;

registration
  let F be finite-yielding Function,x be set;
  cluster F.x -> finite;
  coherence by Def3;
end;

registration
  let F be finite-yielding Function,X be set;
  cluster F|X -> finite-yielding;
  coherence
  proof
    let x;
    per cases;
    suppose x in dom (F|X);
      then (F|X).x=F.x by FUNCT_1:68;
      hence (F|X).x is finite;
    end;
    suppose not x in dom (F|X);
      hence (F|X).x is finite by FUNCT_1:def 4;
    end;
  end;
end;

registration
  let F be finite-yielding Function,G be Function;
  cluster F*G -> finite-yielding;
  coherence
  proof
    let x;
    per cases;
    suppose x in dom (F*G);
      then (F*G).x =F.(G.x) & G.x in dom F by FUNCT_1:21,22;
      hence (F*G).x is finite;
    end;
    suppose not x in dom (F*G);
      hence (F*G).x is finite by FUNCT_1:def 4;
    end;
  end;
  cluster Intersect(F,G) -> finite-yielding;
  coherence
  proof
    let x;
    per cases;
    suppose x in dom Intersect(F,G);
      then x in (dom F)/\(dom G) by YELLOW20:def 2;
      then Intersect(F,G).x=F.x/\G.x & F.x is finite by YELLOW20:def 2;
      hence Intersect(F,G).x is finite;
    end;
    suppose not x in dom Intersect(F,G);
      hence Intersect(F,G).x is finite by FUNCT_1:def 4;
    end;
  end;
end;

 reserve Fy for finite-yielding Function;
  
theorem
  y in rng Ch implies Intersection(Fy,Ch,y) is finite
proof
  assume y in rng Ch;
  then consider x such that
A1:x in dom Ch & Ch.x=y by FUNCT_1:def 5;
  Ch.x in {y} by A1,TARSKI:def 1;
  then x in Ch"{y} by A1,FUNCT_1:def 13;
  then Intersection(Fy,Ch,y) c= Fy.x & Fy.x is finite by Th34;
  hence thesis by FINSET_1:13;
end;

theorem Th42:
  dom Fy is finite implies union rng Fy is finite :: revise CARD_4:63
proof
  assume dom Fy is finite;
  then A1:rng Fy is finite by FINSET_1:26;
  for F be set st F in rng Fy holds F is finite
  proof
    let F be set such that A2:F in rng Fy;
    ex x st x in dom Fy & F=Fy.x by A2,FUNCT_1:def 5;
    hence F is finite;
  end;
  hence thesis by A1,FINSET_1:25;
end;

definition
  let F be XFinSequence,n;
  redefine func F|n -> XFinSequence;
  coherence by AFINSQ_1:12;
end;

definition
  let D be set,F be XFinSequence of D,n;
  redefine func F|n -> XFinSequence of D;
  coherence by AFINSQ_1:16;
end;

Lm3:
  for F be XFinSequence st k <= len F holds
    len (F|k) = k & dom (F|k) = k
proof
  let F be XFinSequence such that A1:k <= len F; 
  per cases by A1,REAL_1:def 5;
    suppose k=len F;
      then dom F=k by AFINSQ_1:def 1;
      then F|k=F & dom F=k & len F =k by RELAT_1:98,AFINSQ_1:def 1;
      hence thesis;
    end;
    suppose k<len F;
      hence thesis by AFINSQ_1:14;
    end;
end;

reserve D for non empty set,
        b for BinOp of D;
   
theorem Th43:
  for F be XFinSequence of D,b,n st
      n in dom F & ( b has_a_unity or n <> 0 ) holds
    b.(b "**" F|n, F.n) = b "**" F|(n+1)
proof
  let F be XFinSequence of D,b,n such that
  A1:n in dom F and
  A2:b has_a_unity or n <> 0;
  defpred P[Nat] means
    $1 in dom F & (b has_a_unity or len (F|$1) >= 1) implies
    b.(b "**" F|$1, F.$1) = b "**" F|($1+1);
  A3:P[0]
  proof
    assume A4:0 in dom F & (b has_a_unity or len (F|0) >= 1);
    dom F>0 by A4,EULER_1:1;
    then len F>0 by AFINSQ_1:def 1;
    then len F>0 & len F>=1 by NAT_1:39;
    then len (F|0)=0 & F.0 in rng F & rng F c= D & len (F|1)=1 & dom (F|1)=1
      & 0 in 1 by A4,FUNCT_1:def 5,ORDINAL1:def 8,Lm3,EULER_1:1;
    then b has_a_unity & b "**" F|0=the_unity_wrt b & F.0 in D &
      (F|1)=<%(F|1).0%> & F.0=(F|1).0
      by A4,STIRL2_1:def 3,AFINSQ_1:38,FUNCT_1:68;
    then b.(b "**" F|0, F.0)=F.0 & b "**" F|(0+1)=F.0
      by SETWISEO:23,STIRL2_1:44;
    hence thesis;   
  end;
  A4:for k st P[k] holds P[k+1]
  proof
    let k such that P[k];
    set k1=k+1;set Fk1=F|k1;set Fk2=F|(k1+1);
    assume A6:k1 in dom F & (b has_a_unity or len Fk1 >= 1);
    then k1 < dom F by EULER_1:1;
    then k1 < dom F & k1+1 <=dom F & dom F=len F by AFINSQ_1:def 1,NAT_1:38;
    then A7:len Fk1=k1 & len Fk2=k1+1 by Lm3;
    then consider f1 be Function of NAT,D such that
 A8:f1.0 = Fk1.0 and
 A9:for n st n+1 < len Fk1 holds f1.(n+1) = b.(f1.n,Fk1.(n + 1)) and
A10:b "**" Fk1= f1.(k1-1) by A6,STIRL2_1:def 3;
    b has_a_unity or len Fk2 >= 1 by A6,A7,NAT_1:38;
    then consider f2 be Function of NAT,D such that
A11:f2.0 = Fk2.0 and
A12:for n st n+1 < len Fk2 holds f2.(n+1) = b.(f2.n,Fk2.(n+1)) and
A13:b "**" Fk2= f2.((k1+1)-1) by A7,STIRL2_1:def 3;
    defpred R[Nat] means
      $1 < k1 implies f1.$1=f2.$1;
    0<k1 & 0<k1+1 & dom F>0 by A6,NAT_1:18;
    then 0 in k1 & 0 in k1+1 & 0 in dom F by EULER_1:1;
    then 0 in dom F/\k1 & 0 in dom F/\(k1+1) by XBOOLE_0:def 3;
    then 0 in dom Fk1 & 0 in dom Fk2 by FUNCT_1:68;
    then Fk1.0=F.0 & Fk2.0=F.0 by FUNCT_1:68;
    then A14:R[0] by A8,A11;
A15:for m be Nat st R[m] holds R[m+1]
    proof
      let m be Nat such that A14:R[m];set m1=m+1;
      assume A16:m1 < k1;
      then m1 <k1 & m1 <k1+1 & k1< dom F by A6,NAT_1:38,EULER_1:1;
      then m1 in k1 & m1 in k1+1 & m1 < dom F by EULER_1:1,AXIOMS:22;
      then m1 in k1 & m1 in k1+1 & m1 in dom F by EULER_1:1;
      then m1 in dom F/\k1 & m1 in dom F/\(k1+1) by XBOOLE_0:def 3;
      then m1 in dom Fk1 & m1 in dom Fk2 by FUNCT_1:68;
      then m1 <len Fk1 & m1 < len Fk2 & f1.m = f2.m & Fk1.m1 = F.m1 &
        Fk2.m1 = F.m1 by A7,A14,A16,FUNCT_1:68,NAT_1:38;
      then f1.m1 = b.(f1.m,Fk1.m1)& f2.m1 = b.(f1.m,Fk1.m1) by A9,A12;
      hence thesis;
    end;
    A16:for m be Nat holds R[m] from NAT_1:sch 1(A14,A15);
    k1<k1+1 by NAT_1:38;
    then k1 in k1+1 by EULER_1:1;
    then k<k1 & k1<k1+1 & k1 in dom F/\(k1+1) & dom F/\(k1+1)=dom Fk2
      by A6,NAT_1:38,XBOOLE_0:def 3,FUNCT_1:68;
    then f1.k=f2.k & f2.k1 = b.(f2.k,Fk2.k1) & F.k1=Fk2.k1
      by A7,A12,A16,FUNCT_1:68;
    hence b.(b "**" F|k1,F.k1) = b "**" F|(k1+1) by A10,A13;
  end;
A5:for k holds P[k] from NAT_1:sch 1(A3,A4);
  dom F >n by A1,EULER_1:1;
  then len F>n by AFINSQ_1:def 1;
  then len (F|n)=n by Lm3;
  then n<>0 implies len (F|n) >= 1 by NAT_1:39; 
  hence thesis by A1,A2,A5;
end; 

theorem
  for F be XFinSequence of D,n st len F=n+1 holds
    F=(F|n)^<%F.n%>
proof
  let F be XFinSequence of D,n such that A1:len F=n+1; 
  consider f be XFinSequence,x such that
A2:F=f^<%x%> by AFINSQ_1:44,A1,CARD_1:47;
A3:len f + len <%x%>=n+1 & len <%x%> =1 & (len f+1)-1= len f & (n+1)-1=n
    by A1,A2,AFINSQ_1:20,38;
  then len f=n & n <= len F by A1,NAT_1:38;
  then A4:dom f=n & dom (F|n)=n by AFINSQ_1:def 1,Lm3;
  for x st x in dom f holds f.x = (F|n).x
  proof
    let x such that A5:x in dom f;
    reconsider k=x as Nat by A5;
    F.k=(F|n).k & f.k=F.k by A2,A4,A5,AFINSQ_1:def 4,FUNCT_1:68;
    hence f.x = (F|n).x;
  end;
  then f=F|n & x=F.len f by A2,A4,FUNCT_1:9,AFINSQ_1:40;
  hence thesis by A2,A3;
end;

theorem Th45:
  for F be XFinSequence of NAT,n st n in dom F holds
    Sum (F|n) + F.n = Sum (F|(n+1))
proof
  let F be XFinSequence of NAT,n such that A1:n in dom F;
  addnat.(addnat "**" F|n, F.n) = addnat "**" F|(n+1) &
  addnat.(addnat "**" F|n, F.n)=addnat "**" F|n + F.n &
  addnat "**" F|n = Sum (F|n) by A1,Th43,BINOP_2:def 23,STIRL2_1:def 4;
  hence thesis by STIRL2_1:def 4;
end;

theorem Th46:
  for F be XFinSequence of NAT,n st rng F c= {0,n}
    holds Sum F = n * card (F"{n})
proof
  let F be XFinSequence of NAT,n such that A1:rng F c= {0,n};
  defpred P[Nat] means
  for F be XFinSequence of NAT,n st dom F=$1 & rng F c= {0,n}
    holds Sum F = n* card (F"{n});
  A2:P[0]
     proof
       let F be XFinSequence of NAT,n such that A3:dom F=0 and rng F c= {0,n};
       F"{n} c= 0 by A3,RELAT_1:167;
       then len F=0 & F"{n}={} & card {} =0
         by A3,AFINSQ_1:def 1,XBOOLE_1:3,CARD_1:47;
       hence thesis by STIRL2_1:53;
     end;
  A3:for k st P[k] holds P[k+1]
    proof
      let k such that A4:P[k];
      let F be XFinSequence of NAT,n such that A5:dom F=k+1 & rng F c= {0,n};
      per cases;
        suppose A6:n<>0;
          k <k+1 by NAT_1:38;
          then k in dom F by A5,EULER_1:1;
          then A7:F.k in rng F & not k in k & k\/{k}=k+1
            by FUNCT_1:def 5,AFINSQ_1:4;
         then A8:F.k in {0,n} & dom F\{k}=k by A5,Lm2;
          per cases by A7,TARSKI:def 2,A5;
            suppose A9:F.k=0;
              then F.k<>n & k <k+1 by NAT_1:38,A6;
              then (F|k)"{n}=F"{n} & k in dom F & F|(k+1)=F & k < dom F &
                dom F=len F & rng (F|k) c= rng F
                by EULER_1:1,AFINSQ_1:def 1,RELAT_1:99,A5,A8,Th15,RELAT_1:98;
              then (F|k)"{n}=F"{n} & dom (F|k)=k & rng (F|k) c= {0,n} &
                Sum (F|k) + 0= Sum F by A5,A9,Th45,Lm3,XBOOLE_1:1;
              hence thesis by A4;
            end;
            suppose A9:F.k=n;
              set Fk=(F|k)"{n};
              k <k+1 by NAT_1:38;
              then A10:k in dom F & F|(k+1)=F &  k < dom F &
                dom F=len F & rng (F|k) c= rng F
                by EULER_1:1,AFINSQ_1:def 1,RELAT_1:99,A5,RELAT_1:98;
              not k in k;
              then not k in dom F/\k by XBOOLE_0:def 3;
              then not k in dom (F|k) by FUNCT_1:68;
              then not k in Fk&{k}\/Fk=F"{n} & rng (F|k) c= {0,n} & dom (F|k)=k
                by A5,A8,A9,A10,Th13,Lm3,XBOOLE_1:1,FUNCT_1:def 13;
              then Sum (F|k)=n* card ((F|k)"{n}) & Sum (F|k)+n= Sum F &
                (card Fk)+1=card (F"{n})& n*(card (Fk))+n=n*((card Fk)+1)
                 by A10,A4,A9,Th45,CARD_2:54;
              hence thesis;
            end;
        end;
        suppose A6:n=0;
          for k st k in dom F holds F.k=0
          proof
            let k;
            assume k in dom F;
            then F.k in rng F by FUNCT_1:def 5;
            hence thesis by A5,A6,TARSKI:def 2;
          end;
          hence thesis by A6,STIRL2_1:53;
        end;
    end;
A4:for k holds P[k] from NAT_1:sch 1(A2,A3);
  ex k st dom F=k by AFINSQ_1:7;
  hence thesis by A1,A4;
end;

theorem
  x in Choose(n,k,1,0) iff ex F be XFinSequence of NAT st
    F = x & dom F = n & rng F c= {0,1} & Sum F=k
proof
  thus x in Choose(n,k,1,0) implies ex F be XFinSequence of NAT st
    F = x & dom F = n & rng F c= {0,1} & Sum F=k
    proof
      assume x in Choose(n,k,1,0);
      then consider F be Function of n,{0,1} such that
   A1:x=F & Card (F"{1})=k by Def1;
   A2:rng F c= {0,1} & dom F=n by FUNCT_2:def 1;
      then reconsider F as XFinSequence by AFINSQ_1:7;
      rng F is Subset of NAT by A2,XBOOLE_1:1;
      then reconsider F as XFinSequence of NAT by ORDINAL1:def 8;
      take F;
      Sum F= 1*card (F"{1}) by A2,Th46;
      hence thesis by A1,A2; 
    end;
    given F be XFinSequence of NAT such that
 A1:F = x & dom F = n & rng F c= {0,1} & Sum F=k;
    1*card (F"{1})=k by A1,Th46;
    then Card (F"{1}) = k &  F is Function of n,{0,1} by A1,FUNCT_2:4;
    hence thesis by A1,Def1;
end;

theorem Th48:
  for F be XFinSequence of D,b st b has_a_unity or len F >= 1
    holds b "**" F = b "**" (XFS2FS F)
proof
  let F be XFinSequence of D,b such that A1:b has_a_unity or len F >= 1;
  per cases by A1;
    suppose A2:len F >=1;
      then consider xf be Function of NAT,D such that
   A3:xf.0 = F.0 and
   A4:(for n st n+1 < len F holds xf.(n + 1) = b.(xf.n,F.(n + 1))) and
  A5: b "**" F = xf.(len F-1) by STIRL2_1:def 3;
      set FS=XFS2FS F;
      len FS >=1 & len F=len FS by A2,PRGCOR_2:def 2;
      then consider f be Function of NAT,D such that
   A6:f.1 = FS.1 and
   A7:(for n st 0<>n & n<len F holds f.(n+1) = b.(f.n,FS.(n+1))) and
   A8:b "**" FS = f.(len F) by FINSOP_1:def 1;
      defpred P[Nat] means $1 < len F implies xf.$1=f.($1+1);
   A9:P[0]
      proof
        assume 0 <len F;
        then 0+1<=len F by NAT_1:38;
        then F.(1-'1)=FS.1 & 1-'1=0 by PRGCOR_2:def 2,GOBOARD9:1;
        hence thesis by A3,A6;
      end;
  A10:for n st P[n] holds P[n+1]
      proof
        let n such that A11:P[n];
        set n1=n+1;
        set n2=n1+1;
        assume A12:n1 < len F;
        n<n1 & 0<=n1 by NAT_1:18,38;
        then n<len F & 0+1<=n2 & n2 <=len F by A12,NAT_1:38,REAL_1:55;
        then xf.n=f.n1 & xf.n1 = b.(xf.n,F.n1) & F.(n2-'1)=FS.n2 & 
        f.(n1+1) = b.(f.n1,FS.(n1+1)) & n2-'1=n1
          by A4,A7,A11,A12,PRGCOR_2:def 2,BINARITH:39;
        hence thesis;
      end;
  A11:for n holds P[n] from NAT_1:sch 1(A9,A10);
      reconsider L1=len F-1 as Nat by A2,NAT_1:61;
      L1<L1+1 by NAT_1:38;
      hence thesis by A5,A8,A11;
    end;
    suppose A2:b has_a_unity & len F<1;
      then len F<=0+1;
      then len F=1 or len F<=0 by NAT_1:26;
      then len F=0 & len F=len (XFS2FS F) by A2,NAT_1:18,PRGCOR_2:def 2;
      then b "**" F=the_unity_wrt b & b "**" (XFS2FS F)=the_unity_wrt b
        by A2,FINSOP_1:def 1,STIRL2_1:def 3;
      hence thesis;
    end;
end;
    
theorem Th49:
  for F,G be XFinSequence of D,P be Permutation of dom F st  
    b is commutative associative &
    (b has_a_unity or len F >= 1) &
    G = F * P holds b "**" F = b "**" G
proof    
  let F,G be XFinSequence of D,P be Permutation of dom F such that  
A1:b is commutative associative and
A2:b has_a_unity or len F >= 1 and
A3:G = F * P;
  dom F=len F by AFINSQ_1:def 1;
  then reconsider d=dom F as Nat;
  defpred p[set,set] means
  for n st $1=n holds $2=P.(n-1)+1;
A4:for x st x in Seg d ex y st y in Seg d & p[x,y]
  proof
    let x such that A5:x in Seg d;
    reconsider x'=x as Nat by A5;
    1+0<=x' by A5,FINSEQ_1:3;
    then reconsider x'1=x'-1 as Nat by NAT_1:61;
    take (P.x'1)+1;
    x'1+1<= d by A5,FINSEQ_1:3;
    then x'1 <d & (dom F ={} implies dom F={}) by NAT_1:38;
    then x'1 in d & dom F=dom P by EULER_1:1,FUNCT_2:def 1;
    then P.x'1 in rng P by FUNCT_1:def 5;
    then P.x'1<d by EULER_1:1;
    then 0+1<=(P.x'1)+1 & (P.x'1)+1 <=d by NAT_1:38,39;
    hence thesis by FINSEQ_1:3;
  end;
  consider P' be Function of Seg d,Seg d such that
    A5:for x st x in Seg d holds p[x,P'.x] from FUNCT_2:sch 1(A4);
  set xF=XFS2FS F;set xG=XFS2FS G;
  now let x1,x2 such that A6:x1 in dom P' & x2 in dom P' & P'.x1=P'.x2;
    dom P'=Seg d by FUNCT_2:67;
    then reconsider X1=x1,X2=x2 as Nat by A6;
    1+0<=X1 & 1+0<=X2 by A6,FINSEQ_1:3;
    then reconsider X1'=X1-1,X2'=X2-1 as Nat by NAT_1:61;
    X1'<X1'+1 & X2'<X2'+1 & X1 <=d & X2<=d by NAT_1:38,FINSEQ_1:3,A6;
    then A7:X1'<d & X2'<d & (dom F ={} implies dom F={}) by AXIOMS:22;
    P'.X1=P.X1'+1 & P'.X2=P.X2'+1 by A6,A5;
    then (P.X1'+1)-1=(P.X2'+1)-1 & (P.X2'+1)-1=P.X2' & (P.X1'+1)-1=P.X1'
      & P'.X1=P.X1'+1 & P'.X2=P.X2'+1 & dom P=dom F by A6,A7,FUNCT_2:def 1;
    then P.X1'=P.X2' & X1' in dom P & X2' in dom P by A7,EULER_1:1;
    then X1-1+1=X2-1+1 & X1-1+1=X1 by FUNCT_1:def 8; 
    hence x1=x2;
  end;
  then P' is one-to-one & card Seg d=card Seg d & len xF =len F
    by FUNCT_1:def 8,PRGCOR_2:def 2;
  then P' is one-to-one onto & dom xF= Seg len F & len F=d 
    by STIRL2_1:70,FINSEQ_1:def 3,AFINSQ_1:def 1;
  then reconsider P' as Permutation of dom xF by FUNCT_2:def 4;
  rng P c= dom F;
  then dom (F*P)=dom P & dom P=dom F by RELAT_1:46,FUNCT_2:67;
  then A7:dom G= dom F & dom F=len F & dom G=len G by A3,AFINSQ_1:def 1;
  xG=xF* P'
  proof
    A8:for x' be set st x' in dom xG holds xG.x' = (xF*P').x'
    proof
      let x' be set such that A9:x' in dom xG;
      reconsider x=x' as Nat by A9;
  A10:dom xG=Seg len xG & len xG=len G by FINSEQ_1:def 3,PRGCOR_2:def 2;
      then 0<x by A9,FINSEQ_1:3;
      then reconsider x1=x-1 as Nat by NAT_1:60;
  A11:1<=x & x<=len F by A7,A9,A10,FINSEQ_1:3;
      then x1<x1+1 & x-'1=x1 & x <=len F by BINARITH:50,NAT_1:38;
      then A12:x-'1 <dom G by A7,AXIOMS:22; 
  A13:P.(x-1)+1=P'.x & x in dom P' by A5,FUNCT_2:67,A7,A9,A10;
      then P.(x-1)+1 in rng P' by FUNCT_1:def 5;
      then P.(x-1)+1 in dom xF & dom xF=Seg len xF & len xF=len F
        by FINSEQ_1:def 3,PRGCOR_2:def 2;
      then 1<=P.(x-1)+1 & P.(x-1)+1<=len F & x-'1=x-1 & P.(x-'1)+1 is Nat &
        x-'1 in dom G by A11,A12,FINSEQ_1:3,BINARITH:50,EULER_1:1;
      then F.((P.(x-'1)+1)-'1)=xF.((P.(x-'1)+1)) & P.(x-'1)+1-'1=P.(x-'1) &
        (F*P).(x-'1)=F.(P.(x-'1)) by BINARITH:39,PRGCOR_2:def 2,A3,FUNCT_1:22;
      then (F*P).(x-'1)=xF.((P.(x-1)+1)) & xG.x=(F*P).(x-'1)
        by A3,A7,A11,PRGCOR_2:def 2,BINARITH:50;
      hence xG.x'=(xF*P').x' by A13,FUNCT_1:23;
    end;
    rng P' c= dom xF;
    then dom (xF* P')=dom P' & dom P'= Seg d & dom xG=Seg len xG & len xG=len F
      by A7,FINSEQ_1:def 3,PRGCOR_2:def 2,FUNCT_2:67,RELAT_1:46;
    hence thesis by A7,A8,FUNCT_1:9; 
  end;
  then xG=xF* P' & (b has_a_unity or len xF >= 1) by A2,PRGCOR_2:def 2;
  then b "**"xG=b"**"xF & b "**"xG=b "**" G by A1,A2,A7,FINSOP_1:8,Th48;
  hence thesis by A2,Th48;    
end;

Lm4:ex P be Function of card X,X st P is one-to-one
proof
  card X,X are_equipotent by CARD_1:def 5;
  then consider P be Function such that
A1:P is one-to-one & dom P=card X & rng P=X by WELLORD2:def 4;
  P is Function of card X,X by A1,FUNCT_2:3;
  hence thesis by A1;  
end;

definition
  let k;let F be finite-yielding Function;
  assume A1:dom F is finite;
  func Card_Intersection(F,k) -> Nat means :Def4:
  for x,y be set,X be finite set,
        P be Function of card Choose(X,k,x,y),Choose(X,k,x,y)
      st dom F=X & P is one-to-one & x<>y
    ex XFS be XFinSequence of NAT st
       dom XFS=dom P &
       (for z,f st z in dom XFS & f=P.z holds
           XFS.z=Card(Intersection(F,f,x))) &
    it=Sum XFS;
  existence
  proof
    reconsider D=dom F as finite set by A1;
    set Ch1=Choose(D,k,0,1);
    card Ch1,Ch1 are_equipotent by CARD_1:def 5;
    then consider P1 be Function such that
 A2:P1 is one-to-one & dom P1=card Ch1 & rng P1=Ch1 by WELLORD2:def 4;
    reconsider P1 as Function of card Ch1,Ch1 by A2,FUNCT_2:3;
    defpred xfs1[set,set] means
      for f st f=P1.$1 holds $2=Card Intersection(F,f,0);
 A3:for x st x in card Ch1 ex y st y in NAT & xfs1[x,y]
    proof
      let x such that A4:x in card Ch1;
      x in dom P1 by A4,FUNCT_2:def 1,CARD_1:47;
      then P1.x in rng P1 by FUNCT_1:def 5;
      then consider f be Function of D,{0,1} such that
        A5:f = P1.x & Card (f"{0}) = k by Def1;
      union rng F is finite by A1,Th42;
      then reconsider I=Intersection(F,f,0) as finite set by FINSET_1:13;
      take card I;
      thus thesis by A5;
    end;
    consider XFS1 be Function of card Ch1,NAT such that
 A4:for x st x in card Ch1 holds xfs1[x,XFS1.x] from FUNCT_2:sch 1(A3);
 A5:dom XFS1 =card Ch1 & rng XFS1 c= NAT by FUNCT_2:def 1;
    then reconsider XFS1 as XFinSequence by AFINSQ_1:7;
    reconsider XFS1 as XFinSequence of NAT by A5,ORDINAL1:def 8;   
  take Sum XFS1;
    let x,y be set,X be finite set,
      P be Function of card Choose(X,k,x,y),Choose(X,k,x,y) such that
   A8:dom F=X and
   A9:P is one-to-one and
  A10:x<>y;
    set Ch=Choose(X,k,x,y); 
    defpred xfs[set,set] means
      for f st f=P.$1 holds $2=Card Intersection(F,f,x);
A11:for x1 st x1 in card Ch ex x2 st x2 in NAT & xfs[x1,x2]
    proof
      let x1 such that A12:x1 in card Ch;
      x1 in dom P by A12,FUNCT_2:def 1,CARD_1:47;
      then P.x1 in rng P by FUNCT_1:def 5;
      then consider f be Function of X,{x,y} such that
        A13:f = P.x1 & Card (f"{x}) = k by Def1;
      union rng F is finite by A1,Th42;
      then reconsider I=Intersection(F,f,x) as finite set by FINSET_1:13;
      take card I;
      thus thesis by A13;
    end;
    consider XFS be Function of card Ch,NAT such that
A12:for x1 st x1 in card Ch holds xfs[x1,XFS.x1] from FUNCT_2:sch 1(A11);
A13:dom XFS =card Ch & rng XFS c= NAT by FUNCT_2:def 1;
    then reconsider XFS as XFinSequence by AFINSQ_1:7;
    reconsider XFS as XFinSequence of NAT by A13,ORDINAL1:def 8;   
  take XFS;
    Ch={} implies card Ch={} by CARD_1:47;
    then dom P=card Ch by FUNCT_2:def 1;
  hence A14:dom XFS=dom P by FUNCT_2:def 1;
  thus for z,f st z in dom XFS & f=P.z holds
      XFS.z=Card(Intersection(F,f,x)) by A12,A14;
    defpred perm[set,set] means
      for f1 be Function of D,{0,1},f be Function of X,{x,y} st
        f1=P1.$1 & f=P.$2 holds f1"{0}=f"{x} & for z st z in X holds
        (f1.z=0 iff f.z=x)&(f1.z=1 iff f.z=y);
    A15:for x1 st x1 in card Ch1 ex x2 st x2 in card Ch1 & perm[x1,x2]
      proof    
        let x1 such that A16:x1 in card Ch1;
        P1.x1 in rng P1 by A2,A16,FUNCT_1:def 5;
        then consider f1 be Function of D,{0,1} such that
    A17:f1=P1.x1 & card (f1"{0})=k by Def1;
        defpred pf[set,set] means
          (f1.$1=0 iff $2=x)&(f1.$1=1 iff $2=y);
    A18:for d be set st d in X ex fd be set st fd in {x,y} & pf[d,fd]
          proof
            let d be set such that A19:d in X;
            d in dom f1 by A19,A8,FUNCT_2:def 1;
            then f1.d in rng f1 by FUNCT_1:def 5;
            then (f1.d=0 or f1.d=1) & x in {x,y} & y in {x,y} by TARSKI:def 2;
            hence thesis by A10;
          end;
        consider f be Function of X,{x,y} such that
    A19:for d be set st d in X holds pf[d,f.d] from FUNCT_2:sch 1(A18);
    A20:f"{x}=f1"{0}
        proof
          A21:dom f1=D & dom f=X by FUNCT_2:def 1; 
          thus f"{x}c=f1"{0}
          proof
            let z;assume z in f"{x};
            then z in dom f & f.z in {x}&dom f1=dom f by A21,A8,FUNCT_1:def 13;
            then z in dom f1 & f.z=x by TARSKI:def 1;
            then z in dom f1 & f1.z=0 by A19,A8;
            then z in dom f1 & f1.z in {0} by TARSKI:def 1;
            hence thesis by FUNCT_1:def 13;
          end;
          thus f1"{0}c=f"{x}
          proof
            let z;assume z in f1"{0};
            then z in dom f1&f1.z in {0}&dom f1=dom f by A21,A8,FUNCT_1:def 13;
            then z in dom f & f1.z=0 by TARSKI:def 1;
            then z in dom f & f.z=x by A19;
            then z in dom f & f.z in {x} by TARSKI:def 1;
            hence thesis by FUNCT_1:def 13;
          end;
        end;
        card card Choose(X,k,x,y)=card Choose(X,k,x,y) by CARD_1:66;
        then P is onto by A9,STIRL2_1:70;
        then f in Ch & rng P=Ch by A17,A20,Def1,FUNCT_2:def 3;
        then consider x2 such that
    A21:x2 in dom P & P.x2=f by FUNCT_1:def 5;
        take x2;
        Ch={} implies card Ch={} by CARD_1:47;
        then dom P=card Ch & card Ch=card X choose k &
          card Ch1 = card D choose k by A10,FUNCT_2:def 1,Th19;
        hence x2 in card Ch1 by A8,A21;
        let f1' be Function of D,{0,1},f' be Function of X,{x,y} such that
    A22:f1'=P1.x1 & f'=P.x2;
        thus f'"{x}=f1'"{0} by A20,A17,A22,A21;
        let z such that A23:z in X;
        thus thesis by A19,A23,A17,A22,A21;
      end;
    consider Perm be Function of card Ch1,card Ch1 such that
      A16:for x1 st x1 in card Ch1 holds perm[x1,Perm.x1]
      from FUNCT_2:sch 1(A15);
    A17':card Ch1=card D choose k & card Ch=card X choose k by A10,Th19;
    then A17:dom XFS=dom XFS1 by A13,A8,FUNCT_2:def 1;
    now let z1,z2 be set such that
      A18:z1 in dom Perm & z2 in dom Perm & Perm.z1 = Perm.z2;
      P1.z1 in rng P1 by FUNCT_1:def 5,A2,A18;
      then consider P1z1 be Function of D,{0,1} such that
        A19:P1.z1=P1z1 & card (P1z1"{0})=k by Def1;
      P1.z2 in rng P1 by FUNCT_1:def 5,A2,A18;
      then consider P1z2 be Function of D,{0,1} such that
        A20:P1.z2=P1z2 & card (P1z2"{0})=k by Def1;
      Perm.z1 in rng Perm & Perm.z2 in rng Perm &
        card Ch=card X choose k & card Ch1=card D choose k
        by A18,A10,Th19,FUNCT_1:def 5;
      then Perm.z1 in card Ch &
         (Ch={} implies card Ch={}) by A8,CARD_1:47;
      then Perm.z1 in dom P by FUNCT_2:def 1;
      then P.(Perm.z1) in rng P by FUNCT_1:def 5;
      then consider PPermz1 be Function of X,{x,y} such that
        A21:PPermz1=P.(Perm.z1) & card(PPermz1"{x})=k by Def1;
      P1z1=P1z2
      proof
        A22:for z st z in dom P1z1 holds P1z1.z=P1z2.z
        proof
          let z such that A23:z in dom P1z1;
      A24:P1z1.z in rng P1z1 by A23,FUNCT_1:def 5;
          per cases by A24,TARSKI:def 2;
            suppose P1z1.z=0;
              then P1z1.z=0 & PPermz1.z=x & (P1z2.z=0 iff PPermz1.z=x)
                by A18,A19,A20,A21,A8,A16,A23;
              hence thesis;
            end;
            suppose P1z1.z=1;
              then P1z1.z=1 & PPermz1.z=y & (P1z2.z=1 iff PPermz1.z=y)
                by A18,A19,A20,A21,A8,A16,A23;
              hence thesis;
            end;
        end;
        dom P1z1=D & dom P1z2=D by FUNCT_2:def 1;
        hence thesis by A22,FUNCT_1:9;
      end;
      hence z1=z2 by A2,A18,A19,A20,FUNCT_1:def 8;
    end;
    then Perm is one-to-one & card card Ch1=card card Ch1 by FUNCT_1:def 8;
    then Perm is one-to-one onto & card Ch1=dom XFS
      by A17',STIRL2_1:70,FUNCT_2:def 1,A8;
    then reconsider Perm as Permutation of dom XFS by FUNCT_2:def 4;
    XFS1=XFS*Perm
    proof
  A18:for z st z in dom XFS1 holds XFS1.z =(XFS*Perm).z
      proof
        let z such that A19:z in dom XFS1;
        z in dom Perm by A19,A17,FUNCT_2:67;
        then A:Perm.z in rng Perm by FUNCT_1:def 5;
        then P.(Perm.z) in rng P by A14,FUNCT_1:def 5;
        then consider p be Function of X,{x,y} such that
    A20:p=P.(Perm.z) & card (p"{x})=k by Def1;
        P1.z in rng P1 by A2,A5,A19,FUNCT_1:def 5;
        then consider p1 be Function of D,{0,1} such that
    A21:p1=P1.z & card (p1"{0})=k by Def1;
        p1"{0}=p"{x} by A20,A21,A5,A19,A16;
        then Intersection(F,p1,0)=Intersection(F,p,x) &
             XFS1.z=Card Intersection(F,p1,0) &
             XFS.(Perm.z)=Card Intersection(F,p,x) & z in dom Perm
           by FUNCT_2:67,A13,A,A5,A19,A4,A12,A20,A21,Th36;
        hence thesis by FUNCT_1:23;
      end;
      rng Perm c= dom XFS & dom Perm=dom XFS by FUNCT_2:67;
      then dom XFS1=dom (XFS*Perm) by A17,RELAT_1:46;
      hence thesis by A18,FUNCT_1:9; 
    end;  
    then addnat "**" XFS=addnat "**" XFS1 & addnat "**" XFS1=Sum XFS1
      by Th49,STIRL2_1:def 4;
    hence thesis by STIRL2_1:def 4;
  end;
  uniqueness
  proof
    let n1,n2 be Nat such that
      A2:for x,y be set,X be finite set,
        P be Function of card Choose(X,k,x,y),Choose(X,k,x,y)
      st dom F=X & P is one-to-one & x<>y
    ex XFS be XFinSequence of NAT st
       dom XFS=dom P &
       (for z,f st z in dom XFS & f=P.z holds
           XFS.z=Card(Intersection(F,f,x))) & n1=Sum XFS and
      A3:for x,y be set,X be finite set,
        P be Function of card Choose(X,k,x,y),Choose(X,k,x,y)
      st dom F=X & P is one-to-one & x<>y
    ex XFS be XFinSequence of NAT st
       dom XFS=dom P &
       (for z,f st z in dom XFS & f=P.z holds
           XFS.z=Card(Intersection(F,f,x))) & n2=Sum XFS;
    reconsider D=dom F as finite set by A1;
    set Ch1=Choose(D,k,0,1);
    card Ch1,Ch1 are_equipotent by CARD_1:def 5;
    then consider P be Function such that
      A4:P is one-to-one & dom P=card Ch1 & rng P=Ch1 by WELLORD2:def 4;
    reconsider P as Function of card Ch1,Ch1 by A4,FUNCT_2:3;
    consider XFS1 be XFinSequence of NAT such that
      A5:dom XFS1=dom P and
      A6:for z,f st z in dom XFS1 & f=P.z holds
                      XFS1.z=Card(Intersection(F,f,0)) and
      A7:n1=Sum XFS1 by A2,A4;
    consider XFS2 be XFinSequence of NAT such that
      A8:dom XFS2=dom P and
      A9:for z,f st z in dom XFS2 & f=P.z holds
                      XFS2.z=Card(Intersection(F,f,0)) and
      A10:n2=Sum XFS2 by A3,A4;   
    now let z such that A11:z in dom XFS1;
      P.z in rng P by A5,A11,FUNCT_1:def 5;
      then consider Pz be Function of D,{0,1} such that
        A12:Pz=P.z & card (Pz"{0})=k by Def1;
      XFS2.z=Card(Intersection(F,Pz,0)) & XFS1.z=Card(Intersection(F,Pz,0))
        by A5,A8,A6,A9,A11,A12;   
      hence XFS2.z=XFS1.z;   
    end;
    hence thesis by A7,A10,A5,A8,FUNCT_1:9;
  end;
end;

theorem
  for x,y be set,X be finite set,
      P be Function of card Choose(X,k,x,y),Choose(X,k,x,y)
     st dom Fy=X & P is one-to-one & x<>y
  for XFS be XFinSequence of NAT st
      dom XFS=dom P &
      (for z,f st z in dom XFS & f=P.z holds
          XFS.z=Card(Intersection(Fy,f,x)))
    holds Card_Intersection(Fy,k)=Sum XFS
proof       
  let x,y be set,X be finite set,
      P be Function of card Choose(X,k,x,y),Choose(X,k,x,y) such that
    A2:dom Fy=X and A3:P is one-to-one and A4:x<>y;
  let XFS1 be XFinSequence of NAT such that
    A5:dom XFS1=dom P and
    A6:for z,f st z in dom XFS1 & f=P.z holds
          XFS1.z=Card(Intersection(Fy,f,x));
   consider XFS be XFinSequence of NAT such that
     A7:dom XFS=dom P and
     A8:for z,f st z in dom XFS & f=P.z holds
           XFS.z=Card(Intersection(Fy,f,x))and
     A9:Card_Intersection(Fy,k)=Sum XFS by A2,A3,A4,Def4;        
   now let z such that A10:z in dom XFS;
     P.z in rng P by A7,A10,FUNCT_1:def 5;
     then consider Pz be Function of X,{x,y} such that
       A11:Pz=P.z & card (Pz"{x})=k by Def1;
     XFS1.z=Card(Intersection(Fy,Pz,x)) &
     XFS.z=Card(Intersection(Fy,Pz,x)) by A5,A6,A7,A8,A10,A11;
     hence XFS1.z=XFS.z;
   end;       
   hence thesis by A9,A5,A7,FUNCT_1:9;
end;  

theorem
  dom Fy is finite & k=0 implies Card_Intersection(Fy,k)=Card (union rng Fy)
proof
  assume A1:dom Fy is finite & k=0;
  then reconsider X=dom Fy as finite set;
  set Ch=Choose(X,k,0,1);
  consider P be Function of card Ch,Ch such that
A2:P is one-to-one by Lm4;
  consider XFS be XFinSequence of NAT such that
A3:dom XFS=dom P and
A4:for z,f st z in dom XFS & f=P.z holds
           XFS.z=Card(Intersection(Fy,f,0)) and
A5:Card_Intersection(Fy,k)=Sum XFS by A2,Def4;
  card Ch=1 by A1,Th10;   
  then A6:dom P=1 & 0 in 1 by FUNCT_2:def 1,TARSKI:def 1,CARD_5:1,CARD_1:47;
  then P.0 in rng P by FUNCT_1:def 5;
  then consider P0 be Function of X,{0,1} such that
    A7:P0=P.0 & card (P0"{0})=0 by A1,Def1;
  P0"{0}={} by A7,CARD_2:59;
  then A8:Intersection(Fy,P0,0)=union rng Fy &
    XFS.0=Card(Intersection(Fy,P0,0)) by A3,A4,A6,A7,Th37; 
  len XFS=1 by A3,A6,AFINSQ_1:def 1;
  then XFS=<%XFS.0%> & XFS.0=Card(Intersection(Fy,P0,0))
    by A3,A6,A7,A4,AFINSQ_1:38;
  then addnat "**" XFS=XFS.0 by STIRL2_1:44;
  hence thesis by A8,A5,STIRL2_1:def 4;
end;

theorem Th52:
  dom Fy=X & k > card X implies Card_Intersection(Fy,k)=0
proof  
  assume A1:dom Fy=X & k > card X;
  set Ch=Choose(X,k,0,1);
  consider P be Function of card Ch,Ch such that
    A2:P is one-to-one by Lm4;
  consider XFS be XFinSequence of NAT such that
    A3:dom XFS=dom P and
    for z,f st z in dom XFS & f=P.z holds
           XFS.z=Card(Intersection(Fy,f,0)) and
    A5:Card_Intersection(Fy,k)=Sum XFS by A1,A2,Def4;
  Ch is empty by A1,Th9;
  then dom P={} by FUNCT_2:def 1,CARD_1:47;
  then XFS=0 by A3,RELAT_1:64;
  hence thesis by A5,STIRL2_1:53,CARD_1:47;
end;

theorem Th53:
  for Fy,X st dom Fy=X
    for P be Function of card X,X st P is one-to-one holds
      ex XFS be XFinSequence of NAT st
       dom XFS=card X &
       (for z st z in dom XFS holds XFS.z=card ((Fy*P).z))&
       Card_Intersection(Fy,1)=Sum XFS
proof
  let Fy,X such that A1:dom Fy=X;
  let P be Function of card X,X such that A2:P is one-to-one;
  per cases;
    suppose A3:X ={};
      reconsider XFS={} as XFinSequence;
      rng {} c= {} & {} c= NAT by XBOOLE_1:2;
      then reconsider XFS as XFinSequence of NAT by ORDINAL1:def 8;
      take XFS;
      thus card X=dom XFS & for z st z in dom XFS holds XFS.z=card ((Fy*P).z)
        by RELAT_1:60,A3,CARD_1:47;
      Sum XFS=0 &Card_Intersection(Fy,1)=0 by A1,STIRL2_1:53,Th52,A3,CARD_1:47;
      hence Card_Intersection(Fy,1)=Sum XFS;
    end;
    suppose X <>{}; 
      then reconsider cX=card X as non empty set by CARD_2:59;
      deffunc xfs(Element of cX)=card ((Fy*P).$1);
      consider XFS be Function of cX,NAT such that
        A3:for x be Element of cX holds XFS.x = xfs(x) from FUNCT_2:sch 4;
      A4:dom XFS=cX & rng XFS c= NAT by FUNCT_2:def 1;
      then reconsider XFS as XFinSequence by AFINSQ_1:7;
      reconsider XFS as XFinSequence of NAT by A4,ORDINAL1:def 8;
      take XFS;
      thus card X=dom XFS by FUNCT_2:def 1;
      thus for z st z in dom XFS holds XFS.z=card ((Fy*P).z) by A3,A4;
      thus Card_Intersection(Fy,1)=Sum XFS
      proof
        deffunc p1(set)=({P.$1}-->0) +* ((X\{P.$1})-->1);
        A5:for x st x in cX holds p1(x) in Choose(X,1,0,1)
        proof
          let x such that A6:x in cX;
          set F0={P.x}-->0;set F1=(X\{P.x})-->1;
          x in dom P by A6,FUNCT_2:def 1,CARD_1:47;
          then P.x in rng P by FUNCT_1:def 5;
          then {P.x}\/(X\{P.x})=X & {P.x} misses (X\{P.x}) & card {P.x}=1
            by Lm1,XBOOLE_1:79,CARD_1:50,CARD_2:20;
          hence p1(x) in Choose(X,1,0,1) by Th21;
        end;  
        consider P1 be Function of cX,Choose(X,1,0,1) such that
          A6:for z st z in cX holds P1.z = p1(z) from FUNCT_2:sch 2(A5);
        Choose(X,1,0,1) c= rng P1
        proof
          let z such that A7:z in Choose(X,1,0,1);
          consider F be Function of X,{0,1} such that
            A8:F=z & card (F"{0})=1 by A7,Def1;
          consider x1 such that
            A9:F"{0}={x1} by A8,CARD_2:60;
          A10:x1 in {x1} by TARSKI:def 1;
          then x1 in dom F & dom F=X & card X=card card X
            by A9,FUNCT_1:def 13,FUNCT_2:def 1,CARD_1:66;
          then x1 in X & P is onto by A2,STIRL2_1:70;
          then x1 in rng P by FUNCT_2:def 3;
          then consider x2 such that
      A11:x2 in dom P & P.x2=x1 by FUNCT_1:def 5;
          cX<>0;
          then card Choose(X,1,0,1)= card X choose 1 & card X >=1
            by Th19,NAT_1:39;
          then card Choose(X,1,0,1)=cX by NEWTON:33;
          then A12:dom P=cX & dom P1 =cX by FUNCT_2:def 1,CARD_1:47; 
          P1.x2=F
          proof
            set F0={P.x2}-->0;set F1=(X\{P.x2})-->1;set P1x=F0+*F1;
            A13:{P.x2}\/(X\{P.x2})=X & dom F0={P.x2}
              by A9,A10,A11,Lm1,FUNCOP_1:19;
            then A14:dom F=X & X=dom F0\/dom F1 & dom P1x=dom F0\/dom F1
              by FUNCOP_1:19,FUNCT_4:def 1,FUNCT_2:def 1;
            now let d be set such that A15:d in dom F;
              now per cases by A13,A15,XBOOLE_0:def 2;
                suppose A16:d in {P.x2};
                  then d=P.x2 & dom F0={P.x2} & dom F1=(X\{P.x2}) &
                    {P.x2} misses (X\{P.x2})
                    by FUNCOP_1:19,XBOOLE_1:79,TARSKI:def 1;
                  then P1x.d=F0.d & d in F"{0} by A9,A11,A16,FUNCT_4:17;
                  then P1x.d=0 & F.d in {0} by A16,FUNCOP_1:13,FUNCT_1:def 13;
                  hence P1x.d=F.d by TARSKI:def 1;
                end;
                suppose A16:d in X\{P.x2};
                  then d in dom F1 & d in X & not d in {x1} & X=dom F
                    by A11,FUNCOP_1:19,XBOOLE_0:def 4,FUNCT_2:def 1;
                  then P1x.d=F1.d & F.d in rng F & not F.d in {0}
                    by A9,FUNCT_4:14,FUNCT_1:def 5,def 13;
                  then P1x.d=1 & F.d in {0,1} & not F.d=0
                    by A16,FUNCOP_1:13,TARSKI:def 1;
                  hence P1x.d=F.d by TARSKI:def 2;
                end;
              end;
              hence P1x.d=F.d;   
            end;
            then P1x=F by A14,FUNCT_1:9;
            hence thesis by A6,A11;
          end;
          hence thesis by A8,A11,A12,FUNCT_1:def 5;
        end;
        then Choose(X,1,0,1) = rng P1 & cX <>0 by XBOOLE_0:def 10;
        then P1 is onto & card Choose(X,1,0,1)= card X choose 1 & card X >=1
          by Th19,NAT_1:39,FUNCT_2:def 3;
        then A7:P1 is onto & card X=card Choose(X,1,0,1) &
          card card X=card X by CARD_1:66,NEWTON:33;
        then reconsider P1 as Function of card Choose(X,1,0,1),Choose(X,1,0,1);
        P1 is one-to-one & dom Fy=X by A1,A7,STIRL2_1:70;
        then consider XFS1 be XFinSequence of NAT such that
          A8:dom XFS1=dom P1 and
          A9:for z,f st z in dom XFS1 & f=P1.z holds
            XFS1.z=Card(Intersection(Fy,f,0)) and
          A10:Card_Intersection(Fy,1)=Sum XFS1 by Def4; 
        XFS=XFS1
        proof
          Choose(X,1,0,1)={} implies card Choose(X,1,0,1)={} by CARD_1:47;
          then A11:dom P1=card Choose(X,1,0,1) by FUNCT_2:def 1;
          then A12:dom XFS1=dom XFS by A7,A8,FUNCT_2:def 1;
          for z st z in dom XFS1 holds XFS1.z=XFS.z
          proof
            let z such that A13:z in dom XFS1;
        A14:P1.z=p1(z) & p1(z) in Choose(X,1,0,1) by A5,A6,A7,A8,A13;
            then consider f be Function of X,{0,1} such that
        A15:f=p1(z) & card (f"{0})=1 by Def1;
            consider x1 such that
        A16:f"{0}={x1} by A15,CARD_2:60;
            P.z in {P.z} & {P.z}=dom ({P.z}-->0) &
              dom p1(z)=dom ({P.z}-->0)\/dom ((X\{P.z})-->1)
              by TARSKI:def 1,FUNCOP_1:19,FUNCT_4:def 1;
            then not P.z in X\{P.z} &P.z in dom ({P.z}-->0)\/dom((X\{P.z})-->1)
              & ({P.z}-->0).(P.z)=0 & dom ((X\{P.z})-->1)=X\{P.z}
              by XBOOLE_0:def 4,def 2,FUNCOP_1:13,19;
            then p1(z).(P.z)=0 & P.z in dom p1(z) & 0 in {0}
              by FUNCT_4:def 1,TARSKI:def 1;
            then P.z in f"{0} by A15,FUNCT_1:def 13;
            then P.z=x1 & z in dom P & dom P= cX
              by A7,A11,A8,A13,FUNCT_2:def 1,A16,TARSKI:def 1;
            then Card(Intersection(Fy,f,0))=Card (Fy.(P.z)) &
              XFS1.z = Card(Intersection(Fy,f,0))&
              XFS.z=card ((Fy*P).z) & Fy.(P.z)=(Fy*P).z
              by A3,FUNCT_1:23,A9,A13,A14,Th38,A15,A16;
            hence thesis;   
          end;  
          hence thesis by A12,FUNCT_1:9;
        end;
        hence thesis by A10;  
      end;   
    end;
end;

theorem Th54:
  dom Fy=X implies
    Card_Intersection(Fy,card X)=Card Intersection(Fy,X-->x,x)
proof
  assume A1:dom Fy=X;set Ch=Choose(X,card X,x,{x});
  consider P be Function of card Ch,Ch such that
    A3:P is one-to-one by Lm4;
  x in {x} by TARSKI:def 1;
  then x<>{x};
  then consider XFS be XFinSequence of NAT such that
    A4:dom XFS=dom P and
    A5:for z,f st z in dom XFS & f=P.z holds
          XFS.z=Card(Intersection(Fy,f,x)) and
    A6:Card_Intersection(Fy,card X)=Sum XFS by A1,A3,Def4;   
  (Ch={} implies card Ch={}) by CARD_1:47;
  then A7:card Ch=1 & dom P=card Ch by Th12,FUNCT_2:def 1;  
  then len XFS=1 by A4,AFINSQ_1:def 1;
  then XFS=<%XFS.0%> by AFINSQ_1:38;
  then addnat "**" XFS=XFS.0 by STIRL2_1:44;
  then A8:Sum XFS=XFS.0 by STIRL2_1:def 4;
  consider ch be set such that
    A9:Ch={ch} by A7,CARD_2:60;
  x in {x} by TARSKI:def 1;
  then X\/{}=X & 0 in {0} & 1={0} & {x}<>x by TARSKI:def 1,CARD_5:1;
  then ({}-->{x})+*(X-->x) in Ch & 0 in dom P by A7,Th20;
  then X-->x in Ch & P.0 in rng P by FUNCT_4:21,FUNCT_1:def 5;
  then P.0=ch &X-->x=ch & 0 in dom XFS by A4,A7,A9,TARSKI:def 1,CARD_5:1;
  hence thesis by A5,A6,A8;
end;

theorem Th55:
  Fy={x}-->X implies Card_Intersection(Fy,1)=card X
  proof
    assume A1:Fy={x}-->X;
    x in {x} by TARSKI:def 1;
    then ({x}-->x)"{x}={x} & dom Fy={x} & 1=card {x} & Fy.x=X
      by A1,FUNCOP_1:20,FUNCOP_1:13,19, CARD_1:79;
    then Intersection(Fy,{x}-->x,x)=X &
    Card_Intersection(Fy,1)=Card Intersection(Fy,{x}-->x,x) by Th38,Th54;
    hence thesis;
  end;

theorem
  x<>y & Fy=(x,y)-->(X,Y) implies
    Card_Intersection(Fy,1)=card X + card Y &
    Card_Intersection(Fy,2)=card (X/\Y)
proof
  assume A1:x<>y & Fy=(x,y)-->(X,Y);
  set P=(0,1)-->(x,y);
  dom P={0,1} & rng P={x,y} & card {x,y}=2 & 2={0,1} 
    by A1,FUNCT_4:65,67,CARD_5:1,CARD_2:76;
  then reconsider P as Function of card {x,y},{x,y} by FUNCT_2:3;
  rng P={x,y} by FUNCT_4:67;
  then P is onto & card card {x,y}=card {x,y} by FUNCT_2:def 3,CARD_1:66;
  then P is one-to-one & dom Fy={x,y} by A1,STIRL2_1:70,FUNCT_4:65;
  then consider XFS be XFinSequence of NAT such that
    A2:dom XFS=card {x,y} and
    A3:for z st z in dom XFS holds XFS.z=card ((Fy*P).z) and
    A4:Card_Intersection(Fy,1)=Sum XFS by Th53;
  dom P={0,1} by FUNCT_4:65;
  then dom XFS=2 & 2={0,1} & 0 in dom P & 1 in dom P 
    by A1,A2,CARD_2:76,CARD_5:1,TARSKI:def 2;
  then len XFS=2 & 0 in dom XFS & 1 in dom XFS & P.0=x & P.1=y &
    Fy.x=X & Fy.y=Y & (Fy*P).0=Fy.(P.0) & (Fy*P).1=Fy.(P.1) 
    by A1,AFINSQ_1:def 1,FUNCT_4:66,TARSKI:def 2,FUNCT_1:23;
  then XFS=<%XFS.0,XFS.1%> & XFS.0=card X & XFS.1=card Y by A3,AFINSQ_1:42;
  then addnat "**" XFS=addnat.(card X,card Y) by STIRL2_1:48;
  then A5:addnat "**" XFS=card X + card Y by BINOP_2:def 23;   
  0 in {0} by TARSKI:def 1;
  then ({x,y}-->0)"{0}={x,y} & card {x,y}=2 & dom Fy={x,y} & Fy.x=X & Fy.y=Y
    by A1,CARD_2:76,FUNCT_4:65,66,FUNCOP_1:20;
  then Intersection(Fy,{x,y}-->0,0)=X/\Y &
    Card_Intersection(Fy,2)=Card Intersection(Fy,{x,y}-->0,0) by Th39,Th54;
  hence thesis by A5,A4,STIRL2_1:def 4;   
end;

theorem Th57:
  for Fy,x st dom Fy is finite & x in dom Fy holds
    Card_Intersection(Fy,1)=Card_Intersection(Fy|(dom Fy\{x}),1)+ card (Fy.x)
proof
  let Fy,x such that
A1:dom Fy is finite and
A2:x in dom Fy;
  reconsider X=dom Fy as finite set by A1;
  set Xx=X\{x};
  consider Px be Function of card Xx,Xx such that
A3:Px is one-to-one by Lm4;
  Xx c= X by XBOOLE_1:36;
  then X/\Xx=Xx by XBOOLE_1:28;
  then dom (Fy|Xx)=Xx by FUNCT_1:68;
  then consider XFSx be XFinSequence of NAT such that
A4:dom XFSx=card Xx and
A5:for z st z in dom XFSx holds XFSx.z=card (((Fy|Xx)*Px).z) and
A6:Card_Intersection(Fy|Xx,1)=Sum XFSx by A3,Th53;
  card X<>0 by A2,CARD_2:59;
  then card X>0 by NAT_1:18;  
  then reconsider k=card X-1 as Nat by NAT_1:60;
A7:not card Xx in card Xx &
    (Xx={}implies card Xx={}) by CARD_1:47;
  then consider P be Function of card Xx\/{card Xx},Xx\/{x} such that
A8:P|(card Xx) = Px & P.(card Xx)=x by STIRL2_1:67;
  not x in Xx by ZFMISC_1:64;
  then A9:P is one-to-one by A3,A7,A8,STIRL2_1:68;
A10:card X=k+1;
  then A11:card Xx=k by A2,STIRL2_1:65;
  then card X=card Xx\/{card Xx} & Xx\/{x}=X by A2,A10,AFINSQ_1:4,Lm1;
  then reconsider P as Function of card X,X;
  consider XFS be XFinSequence of NAT such that
    A12:dom XFS=card X and
    A13:for z st z in dom XFS holds XFS.z=card ((Fy*P).z) and
    A14:Card_Intersection(Fy,1)=Sum XFS by A9,Th53;
  A15:XFS|k=XFSx
  proof
    k<k+1 by NAT_1:38;
    then A16:k c= k+1 by CARD_1:56;
    then A17:dom XFS /\k=dom XFSx by A11,A12,A4,XBOOLE_1:28;
    for y st y in dom XFSx holds XFS.y = XFSx.y
    proof
      let y such that A18:y in dom XFSx;
      A19:dom Px=k & dom P=k+1 by A7,A11,FUNCT_2:def 1,CARD_1:47;
      then A20:Px.y=P.y &(Fy*P).y=Fy.(P.y) & y in dom XFS 
        by A8,A4,A11,A12,A16,A18,FUNCT_1:68,23;
      Px.y in rng Px & Xx c= X by XBOOLE_1:36,A19,FUNCT_1:def 5,A4,A11,A18;
      then Px.y in Xx & Xx=X/\Xx & X/\Xx=dom (Fy|Xx) by XBOOLE_1:28,FUNCT_1:68;
      then ((Fy|Xx)*Px).y=(Fy|Xx).(Px.y) & (Fy|Xx).(Px.y)=Fy.(Px.y)
        by A19,FUNCT_1:23,68,A4,A11,A18;
      then (Fy*P).y=((Fy|Xx)*Px).y & XFS.y=card ((Fy*P).y) by A13,A20;
      hence thesis by A5,A18;
    end;
    hence thesis by A17,FUNCT_1:68;
  end;
  k<k+1 by NAT_1:38;
  then A16:k in card X by EULER_1:1;
  then A17:Sum XFSx +XFS.k=Sum (XFS|(k+1)) & XFS.k=card ((Fy*P).k)
    by A12,A13,A15,Th45;
  k in dom P by A16,FUNCT_2:def 1,CARD_1:47;
  then (Fy*P).k=Fy.(P.k) & P.k=x & XFS|(k+1)=XFS
    by A8,A10,A12,FUNCT_1:23,RELAT_1:98,A2,STIRL2_1:65;
  hence thesis by A6,A14,A17; 
end;

theorem Th58:
  dom Intersect(F,dom F-->X')=dom F &
    for x st x in dom F holds Intersect(F,dom F-->X').x = F.x /\ X'
proof
  dom(dom F-->X')= dom F by FUNCOP_1:19;
  then A1:dom F/\dom(dom F-->X')=dom F;
  hence dom F=dom Intersect(F,dom F-->X') by YELLOW20:def 2;
  let x;
  assume x in dom F;
  then Intersect(F,dom F-->X').x=F.x/\(dom F-->X').x & (dom F-->X').x=X'
    by A1,YELLOW20:def 2,FUNCOP_1:13;
  hence thesis;
end;

theorem Th59:
  (union rng F)/\X'=union rng Intersect(F,dom F-->X')
proof
  set I= Intersect(F,dom F-->X');  
  thus (union rng F)/\X' c= union rng I
    proof
      let x such that A1:x in (union rng F)/\X';
      x in union rng F by A1,XBOOLE_0:def 3;
      then consider Fx be set such that
        A2:x in Fx & Fx in rng F by TARSKI:def 4;
      consider x1 such that
        A3:x1 in dom F & F.x1=Fx by A2,FUNCT_1:def 5;
      x in X' & I.x1=Fx/\X' & x1 in dom I by A1,A3,Th58,XBOOLE_0:def 3;
      then x in I.x1 & I.x1 in rng I by FUNCT_1:def 5,A2,XBOOLE_0:def 3;
      hence thesis by TARSKI:def 4;
    end;
  thus union rng I c= (union rng F)/\X'
    proof
      let x such that A1:x in union rng I;
      consider Ix be set such that
        A2:x in Ix & Ix in rng I by A1,TARSKI:def 4;
      consider x1 such that
        A3:x1 in dom I & I.x1=Ix by A2,FUNCT_1:def 5;
      x1 in dom F by A3,Th58;
      then x1 in dom F & I.x1=F.x1/\X' by Th58;
      then x in F.x1 & F.x1 in rng F & x in X'
        by A2,A3,XBOOLE_0:def 3,FUNCT_1:def 5;
      then x in union rng F & x in X' by TARSKI:def 4;
      hence thesis by XBOOLE_0:def 3;      
    end;
end;       

theorem Th60:
  Intersection(F,Ch,y)/\X'=
     Intersection(Intersect(F,dom F-->X'),Ch,y)
proof
  set I=Intersect(F,dom F-->X');
  set Int1=Intersection(F,Ch,y);set Int2=Intersection(I,Ch,y);
  thus Int1/\X' c= Int2
  proof
    let x such that A1:x in Int1/\X';
    x in Int1 by A1,XBOOLE_0:def 3;
    then x in (union rng F) & x in X' by A1,XBOOLE_0:def 3;
    then x in (union rng F)/\X' by XBOOLE_0:def 3;
    then A2:x in union rng I by Th59;
    for z st z in dom Ch & Ch.z=y holds x in I.z
    proof
      let z such that A3:z in dom Ch & Ch.z=y;
      x in Int1 & x in X' by A1,XBOOLE_0:def 3;
      then x in F.z & x in X' by A3,Def2;
      then x in F.z/\X' & z in dom F by XBOOLE_0:def 3,FUNCT_1:def 4;
      hence thesis by Th58; 
    end;
    hence thesis by A2,Def2;
  end;
  thus Int2 c= Int1/\X'
  proof
    let x such that A1:x in Int2;
    x in union rng I by A1;
    then x in (union rng F)/\X' by Th59;
    then A2:x in union rng F & x in X' by XBOOLE_0:def 3;
    for z st z in dom Ch & Ch.z=y holds x in F.z
    proof
      let z such that A3:z in dom Ch & Ch.z=y;
      x in I.z by A1,A3,Def2;
      then x in I.z & z in dom I & dom I=dom F by FUNCT_1:def 4,Th58;
      then x in F.z/\X' by Th58;
      hence thesis by XBOOLE_0:def 3;
    end;
    then x in Int1 by A2,Def2;
    hence thesis by A2,XBOOLE_0:def 3;   
  end;       
end;
       
theorem Th61:
  for F, G be XFinSequence st
    F is one-to-one & G is one-to-one & rng F misses rng G holds
    F^G is one-to-one
proof        
  let F, G be XFinSequence such that
    A1:F is one-to-one & G is one-to-one and A2:rng F misses rng G;
  reconsider FG=F^G as Function of dom (F^G),rng (F^G) by FUNCT_2:3;
  dom F=len F & dom G=len G by AFINSQ_1:def 1;
  then len F,rng F are_equipotent & len G,rng G are_equipotent
    by A1,WELLORD2:def 4;
  then Card len F=Card rng F & Card len G=Card rng G by CARD_1:21;
  then len F=Card rng F & len G=Card rng G &
    card(rng F\/rng G)=card rng F + card rng G by A2,CARD_1:66,CARD_2:53;
  then dom (F^G)=len F+len G & len F+len G=card rng(F^G) by AFINSQ_1:29,def 4;
  then card dom (F^G)=card rng (F^G)& FG is onto by CARD_1:66,FUNCT_2:def 3;
  hence thesis by STIRL2_1:70; 
end;  

theorem Th62:
  for Fy,X,x,n st dom Fy = X & x in dom Fy & k>0 holds
    Card_Intersection(Fy,k+1)=
      Card_Intersection(Fy|(dom Fy\{x}),k+1)+
      Card_Intersection(Intersect(Fy|(dom Fy\{x}),(dom Fy\{x})-->Fy.x),k)
proof     
  let Fy,X,x,n such that A1:dom Fy = X & x in dom Fy and A2:k>0;
  set Xx=X\{x};
  set X0={f where f is Function of Xx\/{x},{1,0}:Card (f"{1})=k+1 & f.x=0};
  set X1={f where f is Function of Xx\/{x},{1,0}:Card (f"{1})=k+1 & f.x=1};
  not x in Xx by ZFMISC_1:64;
  then X0\/X1=Choose(Xx\/{x},k+1,1,0) by Lm_Th17;
  then X0 c= Choose(Xx\/{x},k+1,1,0) & X1 c= Choose(Xx\/{x},k+1,1,0)
    by XBOOLE_1:7;
  then reconsider X0,X1 as finite set by FINSET_1:13;
  consider P0 be Function of card X0,X0 such that
    A3:P0 is one-to-one by Lm4;
  defpred p0[set,set] means ex f st f=P0.$1 & f in X0 & $2=f|Xx;
  A4:for x0 be set st x0 in card X0
    ex P0x0 be set st P0x0 in Choose(Xx,k+1,1,0) & p0[x0,P0x0]
  proof
    let x0 be set such that A5:x0 in card X0;
    x0 in dom P0 by A5,FUNCT_2:def 1,CARD_1:47;
    then A6:P0.x0 in rng P0 by FUNCT_1:def 5;
    then P0.x0 in X0;
    then consider P0x0 be Function of Xx\/{x},{1,0} such that
      A7:P0.x0=P0x0 & Card (P0x0"{1})=k+1 & P0x0.x=0;
    Xx c=Xx\/{x} by XBOOLE_1:7;
    then A8:(Xx\/{x})/\Xx=Xx & dom P0x0=Xx\/{x} & rng (P0x0|Xx) c= rng P0x0
      by XBOOLE_1:28,FUNCT_2:def 1,RELAT_1:99;
    then dom (P0x0|Xx)=Xx & rng (P0x0|Xx) c= {1,0} by FUNCT_1:68;
    then reconsider Px=P0x0|Xx as Function of Xx,{1,0} by FUNCT_2:4;
    not x in Xx by ZFMISC_1:64;
    then (Xx\/{x})\{x}=Xx by Lm2;
    then P0x0"{1}=Px"{1} by A7,A8,Th15;
    then Px in Choose(Xx,k+1,1,0) by A7,Def1;
    hence thesis by A6,A7;  
  end;
  consider P0x be Function of card X0,Choose(Xx,k+1,1,0) such that
    A5:for x1 st x1 in card X0 holds p0[x1,P0x.x1] from FUNCT_2:sch 1(A4);
  not x in Xx by ZFMISC_1:64;
  then A6:card Choose(Xx,k+1,1,0)=card X0 by Th16;
  for x1,x2 st x1 in dom P0x & x2 in dom P0x & P0x.x1 = P0x.x2 holds x1 = x2
    proof
      let x1,x2 such that A7:x1 in dom P0x & x2 in dom P0x & P0x.x1 = P0x.x2;
      (Choose(Xx,k+1,1,0)={} implies card Choose(Xx,k+1,1,0)={}) &
        (X0={} implies card X0={}) by CARD_1:47;
      then A8:dom P0x=card X0 & dom P0=card X0 by A6,FUNCT_2:def 1;
      consider f1 be Function such that
        A9:f1=P0.x1 & f1 in X0 & P0x.x1=f1|Xx by A5,A7;
      consider f2 be Function such that
        A10:f2=P0.x2 & f2 in X0 & P0x.x2=f2|Xx by A7,A5;
      A11:(ex F be Function of Xx\/{x},{1,0} st f1=F &Card (F"{1})=k+1&F.x=0) &
      ex F be Function of Xx\/{x},{1,0} st f2=F & Card (F"{1})=k+1 & F.x=0
        by A9,A10;
      then A12:dom f1=Xx\/{x} & f1.x=0 & dom f2=Xx\/{x} & f2.x=0
        by FUNCT_2:def 1;
      for z st z in dom f1 holds f1.z = f2.z
      proof
        let z such that A13:z in dom f1;
        now per cases by A12,A13,XBOOLE_0:def 2;
          suppose z in Xx;
            then z in dom f1/\Xx & z in dom f2/\Xx by A12,A13,XBOOLE_0:def 3;
            then (f1|Xx).z=f1.z & (f2|Xx).z=f2.z by FUNCT_1:71;
            hence thesis by A7,A9,A10;
          end;
          suppose z in {x};
            then z=x by TARSKI:def 1;
            hence thesis by A11;
          end;
        end;
        hence thesis;
      end;
      then f1=f2 & x1 in dom P0 & x2 in dom P0 by A7,A8,A12,FUNCT_1:9;
      hence thesis by A3,A9,A10,FUNCT_1:def 8;
    end;
  then A7:P0x is one-to-one by FUNCT_1:def 8;
  Xx c=X by XBOOLE_1:36;
  then Xx/\X=Xx by XBOOLE_1:28;
  then A2':dom (Fy|(dom Fy\{x}))=Xx by A1,FUNCT_1:68;
  then consider XFS0 be XFinSequence of NAT such that
    A8:dom XFS0=dom P0x and
    A9:for z,f st z in dom XFS0 & f=P0x.z holds
           XFS0.z=Card(Intersection(Fy|(dom Fy\{x}),f,1)) and
    A10:Card_Intersection(Fy|(dom Fy\{x}),k+1)=Sum XFS0
           by A6,A7,Def4;      
  consider P1 be Function of card X1,X1 such that
    A11:P1 is one-to-one by Lm4;
  defpred p1[set,set] means ex f st f=P1.$1 & f in X1 & $2=f|Xx;
  A12:for x1 st x1 in card X1
    ex P1x1 be set st P1x1 in Choose(Xx,k,1,0) & p1[x1,P1x1]
  proof
    let x1 such that A13:x1 in card X1;
    x1 in dom P1 by A13,FUNCT_2:def 1,CARD_1:47;
    then A14:P1.x1 in rng P1 by FUNCT_1:def 5;
    then P1.x1 in X1;
    then consider P1x1 be Function of Xx\/{x},{1,0} such that
      A15:P1.x1=P1x1 & Card (P1x1"{1})=k+1 & P1x1.x=1;
    Xx c=Xx\/{x} by XBOOLE_1:7;
    then A16:(Xx\/{x})/\Xx=Xx & dom P1x1=Xx\/{x} & rng (P1x1|Xx) c= rng P1x1
      by XBOOLE_1:28,FUNCT_2:def 1,RELAT_1:99;
    then dom (P1x1|Xx)=Xx & rng (P1x1|Xx) c= {1,0} by FUNCT_1:68;
    then reconsider Px=P1x1|Xx as Function of Xx,{1,0} by FUNCT_2:4;
    not x in Xx & x in {x} & dom P1x1=Xx\/{x}
      by ZFMISC_1:64,TARSKI:def 1,FUNCT_2:def 1;
    then (Xx\/{x})\{x}=Xx & x in dom P1x1 by Lm2,XBOOLE_0:def 2;
    then A17:P1x1"{1}=Px"{1}\/{x} by A15,A16,Th13;
    not x in Px"{1} by ZFMISC_1:64;
    then k+1 =card (Px"{1})+1 & (k+1)-1=k by A15,A17,CARD_2:54;
    then Px in Choose(Xx,k,1,0) by Def1;
    hence thesis by A14,A15;  
  end;
  consider P1x be Function of card X1,Choose(Xx,k,1,0) such that
    A13:for x1 st x1 in card X1 holds p1[x1,P1x.x1] from FUNCT_2:sch 1(A12);
  not x in Xx by ZFMISC_1:64;
  then A14:card Choose(Xx,k,1,0)=card X1 by Th14;
  for x1,x2 st x1 in dom P1x & x2 in dom P1x & P1x.x1 = P1x.x2 holds x1 = x2
    proof
      let x1,x2 such that A15:x1 in dom P1x & x2 in dom P1x & P1x.x1 = P1x.x2;
      (Choose(Xx,k,1,0)={} implies card Choose(Xx,k,1,0)={}) &
        (X1={} implies card X1={}) by CARD_1:47;
      then A16:dom P1x=card X1 & dom P1=card X1 by A14,FUNCT_2:def 1;
      consider f1 be Function such that
        A17:f1=P1.x1 & f1 in X1 & P1x.x1=f1|Xx by A13,A15;
      consider f2 be Function such that
        A18:f2=P1.x2 & f2 in X1 & P1x.x2=f2|Xx by A15,A13;
      A19:(ex F be Function of Xx\/{x},{1,0} st f1=F &Card (F"{1})=k+1&F.x=1) &
      ex F be Function of Xx\/{x},{1,0} st f2=F & Card (F"{1})=k+1 & F.x=1
        by A17,A18;
      then A20:dom f1=Xx\/{x} & f1.x=1 & dom f2=Xx\/{x} & f2.x=1
        by FUNCT_2:def 1;
      for z st z in dom f1 holds f1.z = f2.z
      proof
        let z such that A21:z in dom f1;
        now per cases by A20,A21,XBOOLE_0:def 2;
          suppose z in Xx;
            then z in dom f1/\Xx & z in dom f2/\Xx by A20,A21,XBOOLE_0:def 3;
            then (f1|Xx).z=f1.z & (f2|Xx).z=f2.z by FUNCT_1:71;
            hence thesis by A15,A17,A18;
          end;
          suppose z in {x};
            then z=x by TARSKI:def 1;
            hence thesis by A19;
          end;
        end;
        hence thesis;
      end;
      then f1=f2 & x1 in dom P1 & x2 in dom P1 by A15,A16,A20,FUNCT_1:9;
      hence thesis by A11,A17,A18,FUNCT_1:def 8;
    end;
    then A15:P1x is one-to-one by FUNCT_1:def 8;
    set I=Intersect(Fy|(dom Fy\{x}),(dom Fy\{x})-->Fy.x);
    dom I=Xx by A1,A2',Th58; 
    then consider XFS1 be XFinSequence of NAT such that
      A16:dom XFS1=dom P1x and
      A17:for z,f st z in dom XFS1 & f=P1x.z holds
                XFS1.z=Card(Intersection(I,f,1)) and
      A18:Card_Intersection(I,k)=Sum XFS1 by A15,A14,Def4;
    (X0={} implies card X0={}) & (X1={} implies card X1={}) by CARD_1:47;
    then A19:dom P0=card X0 & dom P1=card X1 by FUNCT_2:def 1;
    then reconsider XP0=P0,XP1=P1 as XFinSequence by AFINSQ_1:7;
    set Ch=Choose(X,k+1,1,0);  
    Xx\/{x}=X & not x in Xx & rng P0 \/ rng P1 c= X0\/X1
      by A1,Lm1,ZFMISC_1:64,XBOOLE_1:13;
    then A20:card X0+card X1=card Ch & card X0=len XP0
      & card X1=len XP1 & rng (XP0^XP1) c= X0\/X1 & X0\/X1=Ch
      by A6,A14,A19,Th17,AFINSQ_1:def 1,29,Lm_Th17;
    then dom (XP0^XP1)=card Ch & rng (XP0^XP1) c= Ch
      by AFINSQ_1:def 4;
    then reconsider XP01=XP0^XP1 as Function of card Ch,Ch by FUNCT_2:4;
    not x in Xx by ZFMISC_1:64;
    then X0 misses X1 by Lm_Th17;
    then rng P0 misses rng P1 by XBOOLE_1:64;
    then XP01 is one-to-one by A3,A11,Th61;
    then consider XFS be XFinSequence of NAT such that
      A21:dom XFS=dom XP01 and
      A22:for z,f st z in dom XFS & f=XP01.z holds
           XFS.z=Card(Intersection(Fy,f,1)) and
      A23:Card_Intersection(Fy,k+1)=Sum XFS by A1,Def4;
    XFS=XFS0^XFS1
    proof
      (Choose(Xx,k+1,1,0)={} implies card Choose(Xx,k+1,1,0)={}) &
      (Choose(Xx,k,1,0)={} implies card Choose(Xx,k,1,0)={}) &
      (X0={} implies card X0={}) & (X1={} implies card X1={}) by CARD_1:47;
      then A24:dom P0x=card X0 & dom P0=card X0 & dom P1x=card X1 &
        dom P1=card X1 by A6,A14,FUNCT_2:def 1;
      then dom (XP0^XP1)=dom XFS0+dom XFS1 & dom XFS0=len XFS0
        by A8,A16,A20,AFINSQ_1:def 1,def 4;
      then A25:dom XFS=len XFS0 + len XFS1 by A21,AFINSQ_1:def 1;
      A26:for n st n in dom XFS0 holds XFS.n=XFS0.n
      proof
        let n such that A27:n in dom XFS0;
        consider fx be Function such that
          A28:fx=P0.n & fx in X0 & P0x.n=fx|Xx by A5,A8,A27;
        consider fx' be Function of Xx\/{x},{1,0} such that
          A29:fx=fx' & Card (fx'"{1})=k+1 & fx'.x=0 by A28;
        dom fx=Xx\/{x}&Xx\/{x}=X & (fx|Xx)"{1} c= dom (fx|Xx)&dom (fx|Xx)c= Xx
          by A1,A29,FUNCT_2:def 1,Lm1,RELAT_1:87,167;
        then A30:fx"{1}=(fx|Xx)"{1} & (fx|Xx)"{1} c= Xx
          by A29,Th15,XBOOLE_1:1;
        consider x1 such that
          A31:x1 in fx"{1} by A29,XBOOLE_0:def 1,CARD_1:47;
        0<=dom XFS1 by NAT_1:18;
        then n< dom XFS0 & dom XFS0+0 <=dom XFS0+dom XFS1
          by A27,EULER_1:1,REAL_1:55;
        then n<dom XFS0+dom XFS1 & fx.x1 in {1}
          by A31,AXIOMS:22,FUNCT_1:def 13;
        then n < dom XFS & XP01.n=XP0.n & fx.x1=1 & x1 in dom fx
      by A21,A24,A8,A16,A20,A27,A31,AFINSQ_1:def 4,TARSKI:def 1,FUNCT_1:def 13;
       then n in dom XFS&XP01.n=fx &1 in rng fx by A28,EULER_1:1,FUNCT_1:def 5;
        then XFS.n=Card(Intersection(Fy,fx,1))
          & XFS0.n=Card(Intersection(Fy|Xx,fx|Xx,1))
          & Intersection(Fy|Xx,fx|Xx,1)=Intersection(Fy|Xx,fx,1)
          & Intersection(Fy|Xx,fx,1)=Intersection(Fy,fx,1)
          by A1,A9,A22,A27,A28,A30,Th31,Th33;
        hence thesis;
      end;
      for n st n in dom XFS1 holds XFS.(len XFS0 + n) = XFS1.n
      proof
        let n such that A27:n in dom XFS1;
        consider fx be Function such that
          A28:fx=P1.n & fx in X1 & P1x.n=fx|Xx by A13,A16,A27;
        consider fx' be Function of Xx\/{x},{1,0} such that
          A29:fx=fx' & Card (fx'"{1})=k+1 & fx'.x=1 by A28;
        A30:x in fx"{1}& dom fx=X
        proof
          A31:dom fx'=Xx\/{x}& Xx\/{x}=X by A1,Lm1,FUNCT_2:def 1;
          then x in dom fx & fx.x in {1} by A1,A29,TARSKI:def 1;
          hence thesis by A29,A31,FUNCT_1:def 13;
        end;
        A31:1 in rng (fx|Xx) & (fx|Xx)"{1} c= Xx
        proof
          A32:(fx|Xx)"{1} c= dom (fx|Xx) & dom (fx|Xx)=dom fx /\Xx &
            dom fx /\Xx c= Xx by RELAT_1:167,FUNCT_1:68,XBOOLE_1:17;
          then reconsider fx1=(fx|Xx)"{1} as finite set by FINSET_1:13;
          not x in Xx by ZFMISC_1:64;
          then not x in dom fx/\Xx by XBOOLE_0:def 3;
          then not x in dom (fx|Xx) by FUNCT_1:68;
          then {x}\/fx1=fx"{1}& not x in fx1 by A1,A29,A30,Th13,FUNCT_1:def 13;
          then card fx1+1=k+1 & (k+1)-1=k & (card fx1+1)-1=card fx1
            by A29,CARD_2:54;
          then consider y such that
            A33:y in fx1 by XBOOLE_0:def 1,A2,CARD_1:47;
          y in dom (fx|Xx) & (fx|Xx).y in {1} by A33,FUNCT_1:def 13;
          then (fx|Xx).y in rng (fx|Xx) & (fx|Xx).y=1
            by FUNCT_1:def 5,TARSKI:def 1;
          hence thesis by A32,XBOOLE_1:1;
        end;  
        n< dom XFS1 & n in dom XP1 by A27,EULER_1:1,A16,A24;
        then dom XFS0+n < dom XFS0+dom XFS1 & n in dom XP1 by XREAL_1:10;
        then dom XFS0+n < dom XFS & XP1.n=XP01.(n+len XP0) & dom XFS0=dom XP0
          by A21,A24,A8,A16,A20,AFINSQ_1:def 4;
        then dom XFS0+n in dom XFS & XP01.(n+len XP0)=fx & len XP0=dom XFS0
          by A28,EULER_1:1,AFINSQ_1:def 1;
        then XFS.(dom XFS0+n)=Card(Intersection(Fy,fx,1)) &
          XFS1.n=Card(Intersection (Intersect(Fy|Xx,Xx-->Fy.x),fx|Xx,1)) &
          Intersection(Intersect(Fy|Xx,Xx-->Fy.x),fx|Xx,1)=
                                Intersection(Fy|Xx,fx|Xx,1)/\Fy.x &
          Intersection(Fy|Xx,fx|Xx,1)=Intersection(Fy,fx|Xx,1)
          by A1,A2',Th60,A17,A22,A27,A28,A31,Th33;
        then XFS.(dom XFS0+n)=Card(Intersection(Fy,fx,1)) &
        XFS1.n=Card(Intersection(Fy,fx|Xx,1)/\Fy.x) &
        Intersection(Fy,fx|Xx,1)/\Fy.x=Intersection(Fy,fx,1) by A30,Th35;  
        hence thesis by AFINSQ_1:def 1;  
      end;
      hence thesis by A25,A26,AFINSQ_1:def 4;
    end;
    then addnat "**" XFS=addnat.(addnat "**" XFS0,addnat "**" XFS1)
      & addnat "**" XFS0=Card_Intersection(Fy|(dom Fy\{x}),k+1)
      & addnat "**" XFS1=Card_Intersection(I,k)
      & addnat "**" XFS=Card_Intersection(Fy,k+1)
      by A10,A18,A23,STIRL2_1:47,def 4;
    hence thesis by BINOP_2:def 23;     
end;

theorem Th63:
  for F, G, bFG be XFinSequence of D st
      b is commutative associative &
      (b has_a_unity or len F >= 1) &
      len F=len G & len F=len bFG &
      (for n st n in dom bFG holds bFG.n=b.(F.n,G.n))
    holds b "**" F^G = b "**" bFG
proof    
  let F, G, bFG be XFinSequence of D such that
    A1:b is commutative associative and
    A2:b has_a_unity or len F >= 1 and
    A3:len F=len G & len F=len bFG and
    A4:for n st n in dom bFG holds bFG.n=b.(F.n,G.n);
  set xF=XFS2FS F;set xG=XFS2FS G;set xb=XFS2FS bFG;
  A5:len xF=len F & len G=len xG & len xb=len bFG by PRGCOR_2:def 2; 
  for k st k in dom xb holds xb.k = b.(xF.k,xG.k)
  proof
    let k such that A6:k in dom xb;
    k in Seg len xb by A6,FINSEQ_1:def 3;
    then k>=1 by FINSEQ_1:3;
    then reconsider k1=k-1 as Nat by NAT_1:61;
    k in Seg len xb by A6,FINSEQ_1:def 3;
    then k1<k1+1 & k<=len xb by FINSEQ_1:3,NAT_1:38;
    then k1<len xb by AXIOMS:22;
    then k1 in len bFG & k in Seg len xb by A5,A6,EULER_1:1,FINSEQ_1:def 3;
    then k1 in dom bFG & 1<=k & k<= len bFG by A5,AFINSQ_1:def 1,FINSEQ_1:3;
    then bFG.k1=b.(F.k1,G.k1) & bFG.(k-'1)=xb.k &
      F.(k-'1)=xF.k & G.(k-'1)=xG.k & k1=k-'1 
      by A3,A4,PRGCOR_2:def 2,BINARITH:50;
    hence thesis;
  end;
  then b "**" xb=b.(b "**" xF,b "**" xG)&b "**" F=b "**" xF &b "**" G=b "**" xG
    by A1,A2,A3,A5,FINSOP_1:10,Th48;
  then b "**" bFG = b.(b "**" F,b "**" G) by A2,A3,Th48;
  hence thesis by A1,A2,A3,STIRL2_1:47;
end;

definition let Fi be XFinSequence of INT;
  func Sum(Fi) -> Integer equals :Def5:
    addint "**" Fi;
  coherence;
end;

definition let Fi be XFinSequence of INT,x;
  redefine func Fi.x -> Integer;
  coherence
  proof
    per cases;
      suppose x in dom Fi;
        then Fi.x in rng Fi & rng Fi c= INT by FUNCT_1:def 5,ORDINAL1:def 8;
        hence Fi.x is Integer by INT_1:def 2;
      end;
      suppose not x in dom Fi;
        hence thesis by FUNCT_1:def 4;
      end;
    end;
  end;

theorem Th64:
  for Fn be XFinSequence of NAT,Fi be XFinSequence of INT st Fi=Fn holds
    Sum Fi=Sum Fn
proof
  defpred P[Nat] means
  for Fn be XFinSequence of NAT,Fi be XFinSequence of INT st Fi=Fn & dom Fn=$1
    holds Sum Fi=Sum Fn;
  A1:P[0]
  proof
    let Fn be XFinSequence of NAT,Fi be XFinSequence of INT such that
      A2:Fi=Fn & dom Fn=0;
    len Fn=0 by A2,AFINSQ_1:def 1;
    then addnat "**" Fn=0 & addint "**" Fi=0 & addnat "**" Fn=Sum Fn
      by A2,BINOP_2:4,BINOP_2:5,STIRL2_1:def 3,def 4;
    hence thesis by Def5;
  end;
  A2:for k st P[k] holds P[k+1]
  proof
    let k such that A3:P[k];
    let Fn be XFinSequence of NAT,Fi be XFinSequence of INT such that
      A4:Fi=Fn & dom Fn=k+1;
    A5:addint.(addint "**" Fi|k, Fi.k)=(addint "**" Fi|k) + Fi.k &
    addnat.(addnat "**" Fn|k, Fn.k)=addnat "**" Fn|k + Fn.k
      by GR_CY_1:14,BINOP_2:def 23;
    k<k+1 by NAT_1:38;
    then k in dom Fn & addnat has_a_unity & addint has_a_unity by A4,EULER_1:1;
    then A6:addnat.(addnat "**" Fn|k, Fn.k) = addnat "**" Fn|(k+1) &
      addint.(addint "**" Fi|k,Fi.k) = addint "**" Fi|(k+1) by A4,Th43;
    k<k+1 & len Fn=k+1 by A4,NAT_1:38,AFINSQ_1:def 1;
    then dom (Fn|k)=k & dom (Fi|k)=k by A4,AFINSQ_1:14;
    then Sum (Fn|k) = Sum (Fi|k) & Sum (Fi|k)=addint "**" (Fi|k) &
      Fn|(k+1)=Fn & Fi|(k+1)=Fi & Sum (Fn|k)=addnat "**" (Fn|k)
      by A3,A4,Def5,RELAT_1:98,STIRL2_1:def 4;
    then Sum Fn=addint "**" Fi by A4,A5,A6,STIRL2_1:def 4;
    hence thesis by Def5;   
  end;
  A3:for k holds P[k] from NAT_1:sch 1(A1,A2);
  let Fn be XFinSequence of NAT,Fi be XFinSequence of INT such that A4:Fi=Fn;
  dom Fn =len Fn by AFINSQ_1:def 1; 
  hence thesis by A3,A4;
end;

theorem Th65:
  for F,Fi be XFinSequence of INT, i be Integer st
      dom F=dom Fi & for n st n in dom F holds i*F.n=Fi.n holds
    i * Sum F = Sum Fi
proof
  defpred P[Nat] means    
    for F,Fi be XFinSequence of INT, i be Integer st dom F=$1 &
      dom F=dom Fi & for n st n in dom F holds i*F.n=Fi.n holds
      i * Sum F = Sum Fi;
  A1:P[0]
    proof
      let F,Fi be XFinSequence of INT, i be Integer such that
      A2:dom F=0 and
      A3:dom F=dom Fi and for n st n in dom F holds i*F.n=Fi.n;
    len F=0 & len Fi=0 by A2,A3,AFINSQ_1:def 1;
    then addint "**" F=0 & addint "**" Fi=0 & addint "**" F=Sum F
      by BINOP_2:4,STIRL2_1:def 3,Def5;
    hence thesis by Def5;      
  end;
  A2:for k st P[k] holds P[k+1]
  proof
    let k such that A3:P[k];
    let F,Fi be XFinSequence of INT, i be Integer such that
      A4:dom F=k+1 and
      A5:dom F=dom Fi and
      A6:for n st n in dom F holds i*F.n=Fi.n;
    k<k+1 & len Fi= k+1 & len F =k+1 by A4,A5,NAT_1:38,AFINSQ_1:def 1;
    then A7:dom (F|k)=k & dom (Fi|k)=k by AFINSQ_1:14;
    A8:for n st n in dom (F|k) holds i*(F|k).n=(Fi|k).n
    proof
      let n such that A9:n in dom (F|k);
      n in dom F & (F|k).n=F.n & (Fi|k).n=Fi.n by A7,A9,RELAT_1:86,FUNCT_1:70;
      hence thesis by A6;
    end;
    A9:addint.(addint "**" Fi|k, Fi.k)=(addint "**" Fi|k) + Fi.k &
    addint.(addint "**" F|k, F.k)=addint "**" F|k + F.k by GR_CY_1:14;
    k<k+1 by NAT_1:38;
    then k in dom F & addnat has_a_unity & addint has_a_unity by A4,EULER_1:1;
    then A10:addint.(addint "**" F|k, F.k) = addint "**" F|(k+1) &
      addint.(addint "**" Fi|k,Fi.k)=addint "**" Fi|(k+1) by A5,Th43;
    k<k+1 & len F=k+1 & len Fi=k+1 by A4,A5,NAT_1:38,AFINSQ_1:def 1;
    then Sum (Fi|k)=addint "**" (Fi|k) & Sum (F|k)=addint "**" (F|k) &
      F|(k+1)=F & Fi|(k+1)=Fi & k in dom F
      by A4,A5,Def5,RELAT_1:98,EULER_1:1;
    then Sum F=Sum (F|k)+F.k & Sum Fi=Sum (Fi|k)+Fi.k &
      i*F.k=Fi.k & i * Sum (F|k)= Sum (Fi|k) 
      by A3,A6,A7,A8,A9,A10,Def5;
    hence thesis by XCMPLX_1:8;
  end;
A3:for k holds P[k] from NAT_1:sch 1(A1,A2);
  let F,Fi be XFinSequence of INT,i be Integer such that
A4:dom F=dom Fi and
A5:for n st n in dom F holds i*F.n=Fi.n;
  dom F=len F by AFINSQ_1:def 1; 
  hence thesis by A3,A4,A5;
end;

theorem Th66:
  x in dom F implies union rng F=union rng (F|(dom F\{x}))\/F.x
proof
  assume A1:x in dom F;
  set d=dom F\{x};set Fd=F|d;
  d\/{x}=dom F & F|dom F=F by A1,Lm1,RELAT_1:98;
  then F=Fd\/(F|{x}) & F.:{x}={F.x} by A1,RELAT_1:107,FUNCT_1:117;
  then rng F= rng Fd\/rng (F|{x}) & rng (F|{x})={F.x} by RELAT_1:26,148;
  then union rng F=union rng Fd \/union {F.x} by ZFMISC_1:96;
  hence thesis by ZFMISC_1:31;
end;  

theorem Th67:
  for Fy be finite-yielding Function,X
    ex XFS be XFinSequence of INT st
         dom XFS = card X &
         for n st n in dom XFS holds XFS.n=((-1)|^n)*Card_Intersection(Fy,n+1)
proof
  let Fy be finite-yielding Function,X;
  defpred P[set,set] means
    for n st n=$1 holds $2=((-1)|^n)*Card_Intersection(Fy,n+1);
  A2:for k st k in card X ex x be Element of INT st P[k,x]
  proof
    let k such that k in card X;
    reconsider C=((-1)|^k)*Card_Intersection(Fy,k+1) as Element of INT
      by INT_1:def 2;
    take C;thus thesis;
  end;
  consider XFS be XFinSequence of INT such that
A3:dom XFS = card X and
A4:for k st k in Card X holds P[k,XFS.k] from STIRL2_1:sch 6(A2);
  take XFS;
  thus thesis by A3,A4;
end;

:: The principle of inclusions and the disconnections

theorem Th68:
  for Fy be finite-yielding Function,X st dom Fy=X
    for XFS be XFinSequence of INT st
          dom XFS= card X &
          for n st n in dom XFS holds XFS.n=((-1)|^n)*Card_Intersection(Fy,n+1)
      holds    
  Card union rng Fy=Sum XFS
proof
  defpred P[Nat] means
  for Fy be finite-yielding Function,X st dom Fy=X & card X=$1
    for XFS be XFinSequence of INT st dom XFS= card X &
      for n st n in dom XFS holds XFS.n=((-1)|^n)*Card_Intersection(Fy,n+1)
      holds    
  Card union rng Fy=Sum XFS;
  A1:P[0]
  proof
    let Fy be finite-yielding Function,X such that A2:dom Fy=X & card X=0;
    let XFS be XFinSequence of INT such that
 A3:dom XFS=card X and
    for n st n in dom XFS holds XFS.n=((-1)|^n)*Card_Intersection(Fy,n+1);
    dom Fy={} & len XFS=0 by A2,A3,CARD_2:59,AFINSQ_1:def 1;
    then rng Fy={} & addint "**" XFS=the_unity_wrt addint
      by RELAT_1:65,STIRL2_1:def 3;
    hence thesis by CARD_1:47,ZFMISC_1:2,Def5,BINOP_2:4;
  end;
A2:for k st P[k] holds P[k+1]
  proof
    let k such that A3:P[k];
    let Fy be finite-yielding Function,X such that A4:dom Fy=X & card X=k+1;
    let XFS be XFinSequence of INT such that
 A5:dom XFS=card X and
 A6:for n st n in dom XFS holds XFS.n=((-1)|^n)*Card_Intersection(Fy,n+1);
    rng Fy is finite & for x st x in rng Fy holds x is finite
    proof
      thus rng Fy is finite by A4,FINSET_1:26;
      let x;assume x in rng Fy;
      then ex x1 st x1 in dom Fy & Fy.x1=x by FUNCT_1:def 5;
      hence thesis;
    end;
    then reconsider urngFy=union rng Fy as finite set by FINSET_1:25;
    per cases by NAT_1:18;
      suppose A7:k=0;
        then len XFS=1 & dom XFS={0} by A4,A5,AFINSQ_1:def 1,CARD_5:1;
        then XFS=<%XFS.0%> & 0 in dom XFS & XFS.0 is Element of INT
          by AFINSQ_1:38,TARSKI:def 1,INT_1:def 2;
        then addint "**" XFS=XFS.0 & XFS.0=((-1)|^0)*Card_Intersection(Fy,0+1)
          & (-1)|^0=1 by A6,STIRL2_1:44,NEWTON:9;
        then A8:Sum XFS=Card_Intersection(Fy,1) by Def5;
        consider x such that
     A9:dom Fy={x} by A4,A7,CARD_2:60;
        rng Fy={Fy.x} by A9,FUNCT_1:14;
        then Fy={x}-->Fy.x & union rng Fy= Fy.x by A9,FUNCOP_1:15,ZFMISC_1:31;
        hence thesis by A8,Th55;
      end;
      suppose K:k>0;
        consider x such that
     A7:x in dom Fy by A4,XBOOLE_0:def 1,CARD_1:47;
        set Xx=X\{x};set FyX=Fy|Xx;set I=Intersect(FyX,dom FyX-->Fy.x);
        urngFy=union rng FyX\/Fy.x by A4,A7,Th66;
        then union rng FyX c= urngFy by XBOOLE_1:7;
        then reconsider urngFyX=union rng FyX as finite set by FINSET_1:13;
        urngFyX/\Fy.x=union rng I by Th59;
        then reconsider urngI=union rng I as finite set;
        Xx c= X by XBOOLE_1:36;
        then A8:dom FyX=Xx by A4,RELAT_1:91;
        consider XFyX be XFinSequence of INT such that
     A9:dom XFyX = card Xx and
    A10:for n st n in dom XFyX holds
          XFyX.n=((-1)|^n)*Card_Intersection(FyX,n+1) by Th67;
        consider XI be XFinSequence of INT such that
    A11:dom XI = card Xx and
    A12:for n st n in dom XI holds
          XI.n=((-1)|^n)*Card_Intersection(I,n+1) by Th67;
        deffunc i(set)=(-1)*XI.$1;
    A13:for x st x in card Xx holds i(x) in INT by INT_1:def 2;
        consider XI1 be Function of card Xx,INT such that
    A14:for x st x in card Xx holds XI1.x=i(x) from FUNCT_2:sch 2(A13);
    A15:dom XI1 =card Xx & rng XI1 c= INT by FUNCT_2:def 1;
        then reconsider XI1 as XFinSequence by AFINSQ_1:7;
        reconsider XI1 as XFinSequence of INT by A15,ORDINAL1:def 8;
        set Fyx=Fy.x;
        card Fyx is Element of INT & 0 is Element of INT by INT_1:def 2;
        then A:rng <%card Fyx%> ={card Fyx} & {card Fyx} is Subset of INT &
          rng <%0%> ={0} & {0} is Subset of INT &
          rng XI1 c= INT & rng XFyX c= INT
            by AFINSQ_1:36,SUBSET_1:55,ORDINAL1:def 8;
        then reconsider XcF=<%card Fyx%>,X_0=<%0%> as
          XFinSequence of INT by ORDINAL1:def 8;
        rng <%card Fyx%>\/rng XI1 c= INT & rng XFyX \/rng <%0%> c= INT
          by A,XBOOLE_1:8;
        then rng (<%card Fyx%>^XI1) c= INT & rng (XFyX^<%0%>) c= INT
          by AFINSQ_1:29;
        then reconsider F1=<%card Fyx%>^XI1,F2=XFyX^<%0%>
          as XFinSequence of INT by ORDINAL1:def 8;
        len <%card Fyx%>=1&len XI1=card Xx & len XFyX=card Xx & len <%0%>=1&
          card Xx=k by A4,A7,A9,A15,AFINSQ_1:def 1,37,STIRL2_1:65;
        then A16:len F1=k+1 & len F2=k+1 & len XFS=k+1
          by A4,A5,AFINSQ_1:def 1,20;
        for n st n in dom XFS holds XFS.n=addint.(F1.n,F2.n)
        proof
          let n such that A17:n in dom XFS;
          per cases by NAT_1:18;
            suppose A18:n=0;
             0 in k & k=dom XFyX by A4,A7,A9,K,EULER_1:1,STIRL2_1:65;
             then F2.0=XFyX.0 & F1.0=card Fyx & (-1)|^0=1&
               XFyX.0=((-1)|^0)*Card_Intersection(FyX,0+1)
               by A10,AFINSQ_1:def 4,39,NEWTON:9;
             then addint.(F1.0,F2.0)=card Fyx+Card_Intersection(FyX,0+1)
               by GR_CY_1:14;
             then addint.(F1.0,F2.0)=Card_Intersection(Fy,1) &
               XFS.0=((-1)|^0)*Card_Intersection(Fy,0+1) & (-1)|^0=1
               by A4,A7,Th57,A6,A17,A18,NEWTON:9;
             hence thesis by A18;
            end;
            suppose A18:n>0;
              then reconsider n1=n-1 as Nat by NAT_1:60;
              Card_Intersection(Fy,n+1)=
              Card_Intersection(FyX,n+1)+Card_Intersection(I,n) &
              XFS.n=((-1)|^n)*Card_Intersection(Fy,n+1)
                by A4,A6,A7,A8,A17,A18,Th62;
              then A19:XFS.n=((-1)|^n)*Card_Intersection(FyX,n+1)+
                ((-1)|^n)*Card_Intersection(I,n);
              n<k+1 by EULER_1:1,A4,A5,A17;
              then A21:n<=k & n1<n1+1 by NAT_1:38;
              then n1 <k & card Xx=k by AXIOMS:22,A4,A7,STIRL2_1:65;
              then 0+1<=n & n< len F1 &
                len <%card Fyx%>=1 & n1 in card Xx
                by A16,A21,NAT_1:38,39,AFINSQ_1:37,EULER_1:1;
              then F1.n=XI1.n1 & XI1.n1=(-1)*XI.n1 &
                XI.n1=((-1)|^n1)*Card_Intersection(I,n1+1)
                by A11,A12,A14,AFINSQ_1:22;
              then F1.n=((-1)*((-1)|^n1))*Card_Intersection(I,n1+1);
              then A22:F1.n=((-1)|^(n1+1))*Card_Intersection(I,n1+1)
                by NEWTON:11;
              per cases by A21,REAL_1:def 5;
                suppose n<k;
                  then n in k & card Xx=k
                    by A4,A7,EULER_1:1,STIRL2_1:65;
                  then XFyX.n=((-1)|^n)*Card_Intersection(FyX,n+1) &
                    F2.n=XFyX.n by A9,A10,AFINSQ_1:def 4;
                  hence thesis by A19,A22,GR_CY_1:14;
                end;
                suppose n=k;
                  then n=card Xx by A4,A7,STIRL2_1:65;
                  then n=len XFyX & n+1>card Xx by A9,AFINSQ_1:def 1,NAT_1:38;
                  then F2.n=0 & Card_Intersection(FyX,n+1)=0
                    by A8,AFINSQ_1:40,Th52;
                  hence thesis by A19,A22,GR_CY_1:14;
                end;
           end;
       end;
       then A17:addint "**" (F1^F2)=addint "**" XFS by A16,Th63;
       for n st n in dom XI holds (-1)*XI.n=XI1.n by A11,A14;
       then A18:(-1)*Sum XI=Sum XI1 by A11,A15,Th65;
       card Fyx is Element of INT & 0 is Element of INT by INT_1:def 2;
       then addint "**" XcF = card Fyx & addint "**" X_0 = 0 by STIRL2_1:44;
       then addint "**" F1=addint.(card Fyx,addint "**" XI1) &
         addint "**" F2=addint.(addint "**" XFyX,0) &
         addint.(addint "**" XFyX,0)=addint "**" XFyX+0
         by STIRL2_1:47,GR_CY_1:14;
       then addint "**" F1+addint "**" F2=
         card Fyx+ addint "**" XI1 +addint "**" XFyX &
         addint "**" XI1 = Sum XI1 &addint "**" XFyX= Sum XFyX &
         addint "**" F1+addint "**" F2=addint.(addint "**" F1,addint "**" F2)
         by GR_CY_1:14,Def5;
       then A19:addint "**"F1^F2=card Fyx+(-1)*Sum XI+Sum XFyX &
         card Xx=k & dom I=Xx
         by A18,STIRL2_1:47,A4,A7,A8,STIRL2_1:65,Th58;
       then card urngFyX=Sum XFyX & card urngI=Sum XI &
         urngFyX\/Fyx = urngFy & urngFyX/\Fyx=urngI
         by A3,A4,A7,A8,A9,A10,A11,A12,Th59,Th66;
       then card urngFy= Sum XFyX+card Fyx-Sum XI by CARD_2:64;
       hence thesis by Def5,A17,A19;
      end;
  end;
  for k holds P[k] from NAT_1:sch 1(A1,A2);
  hence thesis;
end;

theorem Th69:
  for Fy,X,n,k st dom Fy=X holds
    (ex x,y st x<>y &
           for f st f in Choose(X,k,x,y) holds Card(Intersection(Fy,f,x))=n)
  implies
    Card_Intersection(Fy,k)=n*(card X choose k)
proof
  let Fy,X,n,k such that A1:X=dom Fy;
  assume ex x,y st x<>y &
    for f st f in Choose(X,k,x,y) holds Card(Intersection(Fy,f,x))=n;
  then consider x,y such that
A2:x<>y and
A3:for f st f in Choose(X,k,x,y) holds Card(Intersection(Fy,f,x))=n;
  set Ch=Choose(X,k,x,y);
  consider P be Function of card Ch,Ch such that
A4:P is one-to-one by Lm4;
  consider XFS be XFinSequence of NAT such that
    A5:dom XFS=dom P and
    A6:for z,f st z in dom XFS & f=P.z holds
           XFS.z=Card(Intersection(Fy,f,x)) and
    A7:Card_Intersection(Fy,k)=Sum XFS by A1,A2,A4,Def4;
  for z st z in dom XFS holds XFS.z = n
  proof
    let z such that A8:z in dom XFS;
A9: P.z in rng P by A5,A8,FUNCT_1:def 5;
    then consider f be Function of X,{x,y} such that
A10:f=P.z & card (f"{x})=k by Def1;
    XFS.z=Card(Intersection(Fy,f,x))& Card(Intersection(Fy,f,x))=n
      by A3,A6,A8,A9,A10;
    hence thesis;
  end;
  then XFS=dom XFS-->n & n in {n} by FUNCOP_1:17,TARSKI:def 1;
  then XFS"{n}=dom XFS & rng XFS c= {n} & {n} c= {0,n} &
    (Ch={} implies card Ch={})
    by FUNCOP_1:19,20,ZFMISC_1:12,CARD_1:47;
  then XFS"{n}=dom P & rng XFS c= {0,n} & dom P=card Ch
    by A5,XBOOLE_1:1,FUNCT_2:def 1;
  then Sum XFS=n*card card Ch & card Ch=card X choose k by A2,Th46,Th19;
  hence thesis by A7,CARD_1:66;
end;

theorem Th70:
  for Fy,X st dom Fy=X
    for XF be XFinSequence of NAT st
      dom XF=card X &
      for n st n in dom XF holds ex x,y st x<>y &
        for f st f in Choose(X,n+1,x,y) holds Card(Intersection(Fy,f,x))=XF.n
    holds
  ex F be XFinSequence of INT st
     dom F=card X &
     Card union rng Fy = Sum F &
     for n st n in dom F holds F.n=((-1)|^n)*XF.n*(card X choose (n+1))
proof
  let Fy,X such that A1':dom Fy=X;
  let XF be XFinSequence of NAT such that
    A1:dom XF=card X and
    A2:for n st n in dom XF holds ex x,y st x<>y &
        for f st f in Choose(X,n+1,x,y) holds Card(Intersection(Fy,f,x))=XF.n;
  defpred f[set,set] means
    for n st n=$1 holds $2=((-1)|^n)*(XF.n)*(card X choose (n+1));
  A3:for x st x in card X ex y st y in INT & f[x,y]
  proof
    let x such that A4:x in card X;
    card X is Subset of NAT by STIRL2_1:8;
    then reconsider x'=x as Nat by A4; 
    reconsider xx=((-1)|^x')*(XF.x') as Integer;
    reconsider ch=card X choose (x'+1) as Integer by NEWTON:35;
    take xx*ch;
    thus thesis by INT_1:def 2;
  end;
  consider F be Function of card X,INT such that
    A4:for x st x in card X holds f[x,F.x] from FUNCT_2:sch 1(A3);
  A5:dom F =card X & rng F c= INT by FUNCT_2:def 1;
  then reconsider F as XFinSequence by  AFINSQ_1:7;
  reconsider F as XFinSequence of INT by A5,ORDINAL1:def 8;
  take F;
  for n st n in dom F holds F.n=((-1)|^n)*Card_Intersection(Fy,n+1)
  proof
    let n such that A6:n in dom F;
    ex x,y st x<>y & for f st f in Choose(X,n+1,x,y) holds
                          Card(Intersection(Fy,f,x))=XF.n by A1,A2,A5,A6;
    then Card_Intersection(Fy,n+1)=(XF.n)*(card X choose (n+1)) &
     F.n=((-1)|^n)*(XF.n)*(card X choose (n+1))                     
      by A1',A4,A5,A6,Th69;
    hence thesis;
  end;
  hence thesis by A4,A1',A5,Th68;
end;

Lm_Th71:
  for X,Y be finite set st X is non empty & Y is non empty
    ex F be XFinSequence of INT st
      dom F = card Y &
     (card Y|^ card X)-Sum F = Card {f where f is Function of X,Y:f is onto} &
      for n st n in dom F holds
        F.n=((-1)|^n)*(card Y choose (n+1))*((card Y-n-1) |^ card X)
  proof
    let X,Y be finite set such that A1: X is non empty & Y is non empty;
    deffunc fy(set)={f where f is Function of X,Y:not $1 in rng f};
    A2:for x st x in Y holds fy(x) in bool Funcs(X,Y)
    proof
      let x such that A3:x in Y;
      fy(x) c= Funcs(X,Y)
      proof
        let y such that A4:y in fy(x);
        ex f be Function of X,Y st y=f & not x in rng f by A4;
        hence thesis by A3,FUNCT_2:11;
      end;
      hence thesis;
    end;
    consider Fy' be Function of Y,bool Funcs(X,Y) such that
 A3:for x st x in Y holds Fy'.x = fy(x) from FUNCT_2:sch 2(A2);
    for y holds Fy'.y is finite
    proof
      let y;
      per cases;
        suppose y in dom Fy';
          then Fy'.y in rng Fy' by FUNCT_1:def 5;
          hence thesis by FINSET_1:13;
        end;
        suppose not y in dom Fy';
          hence thesis by FUNCT_1:def 4;
        end;
    end;
    then reconsider Fy=Fy' as finite-yielding Function by Def3;
    defpred xf[set,set] means
      for n st n=$1 holds $2=(card Y-n-1) |^ card X;
    A4:for x st x in card Y ex y st y in NAT & xf[x,y]
    proof
      let x such that A5:x in card Y;
      card Y is Subset of NAT by STIRL2_1:8;
      then reconsider n=x as Nat by A5;
      n<card Y by A5,EULER_1:1;
      then n+1<=card Y by NAT_1:38;
      then reconsider k=(card Y)-(n+1) as Nat by NAT_1:61;
      xf[n, k|^ card X] & k|^ card X is Nat by NEWTON:99;
      hence thesis;  
    end;
    consider XF be Function of card Y,NAT such that
 A5:for x st x in card Y holds xf[x,XF.x] from FUNCT_2:sch 1(A4);
 A6:dom XF=card Y & rng XF c= NAT by FUNCT_2:def 1;
    then reconsider XF as XFinSequence by AFINSQ_1:7;
    reconsider XF as XFinSequence of NAT by A6,ORDINAL1:def 8;
 A7:for n st n in dom XF holds ex x,y st x<>y &
      for f st f in Choose(Y,n+1,x,y) holds Card(Intersection(Fy,f,x))=XF.n
    proof
      let n such that A8:n in dom XF;
      take 0,1;
      thus 0<>1;
      let f' be Function such that A9:f' in Choose(Y,n+1,0,1);
      consider f be Function of Y,{0,1} such that
  A10:f = f' & Card (f"{0})=n+1 by A9,Def1;
  A11:Intersection(Fy,f,0) c= Funcs(X,Y\f"{0})
      proof
        let z such that A12:z in Intersection(Fy,f,0);
        0 in rng f by FUNCT_1:142,A10,CARD_1:47;
        then consider x1 such that
          A13:x1 in dom f & f.x1=0 & z in Fy.x1 by A12,Th25;
        z in fy(x1) by A3,A13;
        then consider g be Function of X,Y such that
    A14:z=g & not x1 in rng g;
    A15:rng g c= Y\f"{0}
        proof
          let gy be set such that A17:gy in rng g;
          assume A18:not gy in Y\f"{0};
          gy in f"{0} by A17,A18,XBOOLE_0:def 4;
          then gy in dom f & f.gy in {0} by FUNCT_1:def 13;
          then gy in dom f & f.gy =0  & z in Intersection(Fy,f,0)
            by A12,TARSKI:def 1;
          then g in Fy.gy by A14,Def2;
          then g in fy(gy) by A3,A17;
          then ex h be Function of X,Y st g=h & not gy in rng h;
          hence contradiction by A17; 
        end;
        dom g=X by A13,FUNCT_2:def 1;
        hence thesis by A14,A15,FUNCT_2:def 2;
      end;
 A12':Funcs(X,Y\f"{0}) c= Intersection(Fy,f,0)
      proof
        let g' be set such that A12:g' in Funcs(X,Y\f"{0});
        consider g be Function such that
    A13:g' = g & dom g = X & rng g c= Y\f"{0} by A12,FUNCT_2:def 2;
        consider y such that
    A14:y in f"{0} by XBOOLE_0:def 1,A10,CARD_1:47;
        y in dom f & dom Fy=Y & dom f=Y by A14,FUNCT_1:def 13,FUNCT_2:def 1;
        then A15:fy(y)=Fy'.y & Fy'.y in rng Fy' by A3,FUNCT_1:def 5; 
        Y\f"{0} c= Y by XBOOLE_1:36;
        then rng g c= Y by A13,XBOOLE_1:1;
        then reconsider gg=g as Function of X,Y by A13,FUNCT_2:4;
        not y in rng g by A13,A14,XBOOLE_0:def 4;
        then gg in fy(y);
        then A16:g in union rng Fy by A15,TARSKI:def 4;
        for z st z in dom f & f.z=0 holds g in Fy.z
        proof
          let z such that A17:z in dom f & f.z=0;
          f.z in {0} by A17,TARSKI:def 1;
          then dom f=Y & z in f"{0} by A17,FUNCT_1:def 13,FUNCT_2:def 1;
          then Fy.z=fy(z) & not z in rng gg by A3,A13,XBOOLE_0:def 4;
          hence thesis;
        end;
        hence thesis by A13,A16,Def2;
      end;
      then reconsider I=Intersection(Fy,f,0) as finite set
        by A11,XBOOLE_0:def 10;
  A12:Funcs(X,Y\f"{0}) = Intersection(Fy,f,0) by A11,A12',XBOOLE_0:def 10;
      card X<>0  by A1,CARD_2:59;
      then A13:card X>=1&card (Y\f"{0})=card Y-(n+1) by A10,CARD_2:63,NAT_1:39;
      now per cases;
        suppose Y\f"{0}={};
          then card I = 0 & (card Y - n-1)|^card X =0 by
            A12,CARD_1:47,A13,FUNCT_2:14,NEWTON:16;
          hence thesis by A10,A5,A6,A8;
        end;
        suppose Y\f"{0} <>{};
          then XF.n=(card Y-n-1) |^ card X & card (Y\f"{0}) |^ card X=card I
            by A5,A6,A8,A12,Th3;
          hence thesis by A10,A13;
        end;
      end;
      hence thesis; 
    end;
    dom XF= card Y & dom Fy = Y by FUNCT_2:def 1;
    then consider F be XFinSequence of INT such that
 A8:dom F=card Y and
 A9:Card union rng Fy = Sum F and
A10:for n st n in dom F holds F.n=((-1)|^n)*XF.n*(card Y choose (n+1))
      by A7,Th70;
    set Onto={f where f is Function of X,Y:f is onto};
A11:Funcs(X,Y) \ union rng Fy c= Onto
    proof
      let x such that A12:x in Funcs(X,Y) \ union rng Fy;
      x in Funcs(X,Y) by A12,XBOOLE_0:def 4;
      then consider f such that
  A13:x = f & dom f = X & rng f c= Y by FUNCT_2:def 2;
      reconsider f as Function of X,Y by A13,FUNCT_2:4;
      assume not x in Onto;
      then not f is onto by A13;
      then rng f<>Y by FUNCT_2:def 3;
      then not Y c= rng f by XBOOLE_0:def 10;
      then consider y such that
  A14:y in Y & not y in rng f by TARSKI:def 3;
      y in dom Fy' by A14,FUNCT_2:def 1;
      then Fy'.y in rng Fy' by FUNCT_1:def 5;
      then fy(y) in rng Fy' & f in fy(y) by A3,A14;
      then f in union rng Fy by TARSKI:def 4;
      hence thesis by A12,A13,XBOOLE_0:def 4;  
    end;
    A12:Onto c= Funcs(X,Y) \ union rng Fy
    proof
      let x such that A13:x in Onto;
      consider f be Function of X,Y such that
        A14:x=f & f is onto by A13;
      assume A15:not x in Funcs(X,Y) \ union rng Fy;
      f in Funcs(X,Y) by A1,FUNCT_2:11;
      then f in union rng Fy by A14,A15,XBOOLE_0:def 4;
      then consider Fyy be set such that
        A15:f in Fyy & Fyy in rng Fy by TARSKI:def 4;
      consider y such that
        A16:y in dom Fy & Fy.y=Fyy by A15,FUNCT_1:def 5;
      y in Y by A16,FUNCT_2:def 1;
      then f in fy(y) by A3,A15,A16;
      then y in Y & ex g be Function of X,Y st f=g & not y in rng g
        by A16,FUNCT_2:def 1;
      hence contradiction by A14,FUNCT_2:def 3;  
    end;
    union rng Fy' c= union bool Funcs(X,Y) by  ZFMISC_1:95;
    then A13:union rng Fy c= Funcs(X,Y) by  ZFMISC_1:99;
    then reconsider u=union rng Fy as finite set by FINSET_1:13;
    take F;
    thus dom F = card Y by A8;
    card(Funcs(X,Y)\u) = card Funcs(X,Y) - card u &
      card Funcs(X,Y) =card Y |^ card X & Funcs(X,Y)\u = Onto
      by A1,A11,A12,A13,CARD_2:63,XBOOLE_0:def 10,Th3;
    hence Card Onto=(card Y |^ card X) - Sum F by A9;
    let n such that A14:n in dom F;
    F.n=((-1)|^n)*XF.n*(card Y choose (n+1)) & XF.n=(card Y-n-1) |^ card X
      by A5,A8,A10,A14;
    hence thesis;
  end;

:: onto

theorem Th71:
  for X,Y be finite set st X is non empty & Y is non empty
    ex F be XFinSequence of INT st
      dom F = card Y+1 &
      Sum F = Card {f where f is Function of X,Y:f is onto} &
      for n st n in dom F holds
        F.n=((-1)|^n)*(card Y choose n )*((card Y-n) |^ card X)
proof
  let X,Y be finite set such that A1:X is non empty & Y is non empty;
  set Onto= {f where f is Function of X,Y:f is onto};
   consider F be XFinSequence of INT such that
     A2:dom F = card Y and
     A3:(card Y|^ card X)-Sum F=Card Onto and
     A4:for n st n in dom F holds
    F.n=((-1)|^n)*(card Y choose (n+1))*((card Y-n-1) |^ card X) by A1,Lm_Th71;
  defpred I[set,set] means
    for n st n=$1 holds $2=(-1)*F.n;
  A5:for k st k in card Y ex x be Element of INT st I[k,x]
  proof
    let k such that k in card Y;
    I[k,(-1)*F.k] & (-1)*F.k is Element of INT by INT_1:def 2;
    hence thesis;
  end;
  consider F1 be XFinSequence of INT such that
    A6:dom F1 = card Y and
    A7:for k st k in card Y holds I[k,F1.k] from STIRL2_1:sch 6(A5);   
  reconsider c = card Y|^ card X as Element of INT by INT_1:def 2;   
  reconsider GF1=<%c%>^F1 as XFinSequence of INT;
  take GF1;
  len F1=card Y & len <%c%>=1 by A6,AFINSQ_1:def 1,37;
  hence A8:dom GF1 = card Y +1 by AFINSQ_1:def 4;
  for n st n in dom F holds (-1)*F.n=F1.n by A2,A7;
  then (-1)*Sum F =Sum F1 & (-1)*Sum F= - Sum F by A2,A6,Th65;
  then c -Sum F=c +Sum F1
    .=addint.(c,Sum F1) by GR_CY_1:14
    .=addint.(addint "**" <%c%>,Sum F1) by STIRL2_1:44
    .=addint.(addint "**" <%c%>,addint "**" F1) by Def5
    .=addint "**" GF1 by STIRL2_1:47
    .=Sum GF1 by Def5;
  hence Sum GF1=Card Onto by A3;  
  let n such that A9:n in dom GF1;
  now per cases by NAT_1:18;
    suppose n=0;
      then ((-1)|^n)=1 & (card Y choose n) =1 & ((card Y-n) |^ card X)= c
        & GF1.n=c by NEWTON:9,29,AFINSQ_1:39;
      hence thesis;
    end;
    suppose n>0;
      then reconsider n1=n-1 as Nat by NAT_1:60;
      n < card Y +1 by A8,A9,EULER_1:1;
      then n1+1 <= card Y by NAT_1:38;
      then n1 < card Y by NAT_1:38;
      then n1 in dom F1 & len <% c %>=1 by A6,EULER_1:1,AFINSQ_1:37;
      then GF1.(n1+1)=F1.n1 & n1 in card Y by A6,AFINSQ_1:def 4;
      then GF1.(n1+1)=(-1)*F.n1 & n1+1=n & card Y-n1-1=card Y-n &
        F.n1=((-1)|^n1)*(card Y choose (n1+1) )*( (card Y-n1-1) |^ card X)
        by A2,A4,A7;
      then (-1)*((-1)|^n1)=(-1)|^n &
      GF1.n=((-1)*((-1)|^n1))*(card Y choose n)*((card Y-n) |^ card X) by
      NEWTON:11;
      hence thesis;
    end;
  end;
  hence thesis;
end;

:: n block k

theorem
  for n,k st k <= n ex F be XFinSequence of INT st
    n block k = 1/(k!) * Sum F &
    dom F = k+1 & 
    for m st m in dom F holds F.m=((-1)|^m)*(k choose m)*((k-m) |^ n)
proof
  let n,k such that A0:k <= n;
  now per cases;
    suppose A1:n=0 & k=0;
      reconsider I=1 as Element of INT by INT_1:def 2;
      set F=<%I%>;
      take F;
      n!=1 & Sum F=addint "**" <%I%> & one=1 & addint "**" <%I%>=1
        by A1,NEWTON:18,STIRL2_1:44,CARD_2:20,Def5;
      hence n block k= 1/(k!) * Sum F by A1,STIRL2_1:26;
      thus dom F=k+1 by A1,AFINSQ_1:36;
      let m such that A2:m in dom F;
      m in {0} by A2,AFINSQ_1:36,CARD_5:1;
      then m=0 by TARSKI:def 1;
      then m=0 & (-1) |^m=1 & ((k-m) |^n)=1 & (k choose m)=1 by A1,NEWTON:9,27;
      hence F.m=((-1) |^m)*(k choose m)*((k-m) |^n) by AFINSQ_1:38;
    end;
    suppose A2:n<>0 & k=0;
      set F=k+1 -->0;
      0 is Element of INT by INT_1:def 2;
      then A3:rng F c= {0} & {0} c= INT & {0} c= NAT
        by FUNCOP_1:19,ZFMISC_1:37;
      then dom F=k+1 & rng F c= INT by FUNCOP_1:19,XBOOLE_1:1;
      then reconsider F as XFinSequence by AFINSQ_1:7;
      rng F c= INT by A3,XBOOLE_1:1;
      then reconsider Fi=F as XFinSequence of INT by ORDINAL1:def 8;
      reconsider Fn=F as XFinSequence of NAT by A3,ORDINAL1:def 8;
      take Fi;
      rng F c={0} & {0} c={0,0} by ENUMSET1:69,FUNCOP_1:19;
      then rng F c= {0,0} by XBOOLE_1:1;
      then Sum Fn= 0*card (Fn"{0}) by Th46;
      then Sum Fn=0 & Sum Fn=Sum Fi by Th64;
      hence n block k = 1/(k!) * Sum Fi & dom Fi=k+1
        by A2,STIRL2_1:31,FUNCOP_1:19;
      let m such that A4:m in dom Fi;
      now per cases by NAT_1:18;
        suppose m=0;
          then k-m=0 & n>=1 by A2,NAT_1:39;
          then (k-m) |^ n=0 by NEWTON:16;
          hence (k choose m)*((k-m) |^ n)=0;
        end;
        suppose m>0;
          then (k choose m)=0 by A2,NEWTON:def 3;
          hence (k choose m)*((k-m) |^ n)=0;
        end;
      end;
      then ((-1)|^m)*((k choose m)*((k-m) |^ n))=0& m in k+1 by A4,FUNCOP_1:19;
      hence Fi.m=((-1)|^m)*(k choose m)*((k-m) |^ n) by FUNCOP_1:13;
    end;
    suppose A1:n<>0 & k<>0;
      then consider F be XFinSequence of INT such that
        A2:dom F = card k+1 and
        A3:Sum F = Card {f where f is Function of n,k:f is onto} and
        A4:for m st m in dom F holds
          F.m=((-1)|^m)*(card k choose m)*((card k-m) |^ card n) by Th71;
      take F;  
      set Perm={p where p is Function of k,k:p is Permutation of k};
      Card Perm=(card k)! by Th7;
      then reconsider Perm as finite set by CARD_4:1;
      reconsider Bloc={f where f is Function of n,k:f is onto "increasing}
        as finite set by STIRL2_1:24;
      set Onto={f where f is Function of n,k:f is onto};
      defpred P[set,set] means
        for p be Function of k,k, f be Function of n,k st
          $1=[p,f] holds $2=p*f;
      A5:for x st x in [:Perm,Bloc:] ex y st y in Onto & P[x,y]
      proof
        let x such that A6:x in [:Perm,Bloc:];
        consider p',f' be set such that
          A7:p' in Perm & f' in Bloc & x=[p',f'] by A6,ZFMISC_1:def 2;
        consider p be Function of k,k such that
          A8:p=p' & p is Permutation of k by A7;
        consider f be Function of n,k such that
          A9:f=f' & f is onto "increasing by A7;
        rng p=k & dom f=n & rng f=k & dom p=k
          by A8,A9,A1,FUNCT_2:def 1,def 3;
        then A10:rng (p*f)=k & dom (p*f)=n by RELAT_1:46,47;
        then reconsider pf=p*f as Function of n,k by FUNCT_2:3;
        take pf;
        pf is onto by A10,FUNCT_2:def 3;
        hence pf in Onto;
        let p1 be Function of k,k, f1 be Function of n,k such that
           A11:x=[p1,f1];
        p1=p & f1=f by A7,A8,A9,A11,ZFMISC_1:33;
        hence thesis;    
      end;
      consider FP be Function of [:Perm,Bloc:],Onto such that
        A6:for x st x in [:Perm,Bloc:] holds P[x,FP.x] from FUNCT_2:sch 1(A5);
      reconsider N=n,K=k as non empty Subset of NAT by STIRL2_1:8,A1;  
      A7:FP is one-to-one
      proof
        let x1,x2 such that A8:x1 in dom FP & x2 in dom FP & FP.x1 = FP.x2;
        consider p1',f1' be set such that
          A9:p1' in Perm & f1' in Bloc & x1=[p1',f1'] by A8,ZFMISC_1:def 2;
        consider p1 be Function of k,k such that
          A10:p1'=p1 & p1 is Permutation of k by A9;
        consider f1 be Function of n,k such that
          A11:f1'=f1 & f1 is onto "increasing by A9;
        consider p2',f2' be set such that
          A12:p2' in Perm & f2' in Bloc & x2=[p2',f2'] by A8,ZFMISC_1:def 2; 
        consider p2 be Function of k,k such that
          A13:p2'=p2 & p2 is Permutation of k by A12;
        consider f2 be Function of n,k such that
          A14:f2'=f2 & f2 is onto "increasing by A12;
        FP.x1 in rng FP by A8,FUNCT_1:def 5;
        then FP.x1 in Onto;
        then consider fp be Function of N,K such that
          A15:FP.x1=fp & fp is onto;
        rng fp =K by A15,FUNCT_2:def 3;
        then reconsider p1''=p1,p2''=p2 as Permutation of rng fp by A10,A13;
        reconsider f1''=f1,f2''=f2 as Function of N,K;
        (for l,m be Nat st l in rng f2 & m in rng f2 & l < m holds
          min* f2"{l} < min* f2"{m}) &
        (for l,m be Nat st l in rng f1 & m in rng f1 & l < m holds
          min* f1"{l} < min* f1"{m}) by A11,A14,STIRL2_1:def 1;
        then f1'' is "increasing & f2'' is "increasing & rng f1=K & rng f2=K &
          fp=p1''*f1'' & fp=p2''*f2'' & rng fp=K 
          by STIRL2_1:def 5,FUNCT_2:def 3,A8,A9,A10,A11,A12,A13,A14,A15,A6;
        then p1''=p2'' & f1''=f2'' by STIRL2_1:75;
        hence x1=x2 by A9,A12,A10,A11,A13,A14;
      end;
      consider h be Function of n,k such that
        A8:h is onto "increasing by A0,A1,STIRL2_1:23;
      Onto c= rng FP
      proof
        let x such that A8':x in Onto;
        consider f be Function of n,k such that
     A9:f=x & f is onto by A8';
        f is Function of N,K & rng f=K by A9,FUNCT_2:def 3;
        then consider I be Function of N,K,P be Permutation of K such that
    A10:f=P*I & K=rng I & I is "increasing by STIRL2_1:73;
        set p=P;
        reconsider i=I as Function of n,k;
        for l,m be Nat st l in rng I & m in rng I & l < m holds
          min* I"{l} < min* I"{m} by A10,STIRL2_1:def 5;
        then i is "increasing & i is onto by A10,STIRL2_1:def 1,FUNCT_2:def 3;
        then p in Perm & i in Bloc & h in Onto by A8;
        then [p,i] in [:Perm,Bloc:] & [:Perm,Bloc:]=dom FP
          by ZFMISC_1:def 2,FUNCT_2:def 1;
        then FP.([p,i])=f & [p,i] in dom FP by A6,A10;
        hence thesis by A9,FUNCT_1:def 5;
      end;
      then rng FP=Onto & h in Onto by A8,XBOOLE_0:def 10;
      then rng FP=Onto & dom FP=[:Perm,Bloc:] by FUNCT_2:def 1;
      then Onto,[:Perm,Bloc:] are_equipotent & card [:Perm,Bloc:]=
        card Perm * card Bloc by A7,WELLORD2:def 4,CARD_2:65;
      then Card Onto=card Perm * card Bloc & card Perm =(card k)! & card k=k
        by CARD_1:21,66,Th7;
      then Sum F=(k!)*card Bloc & k! >0 by A3,NEWTON:23;
      then (k!)* card Bloc/(k!)=Sum F /(k!) &
        (k!)*card Bloc/(k!)=card Bloc*((k!)/(k!))&(k!)/(k!)=1
         by XCMPLX_1:60,75;
      then n block k=(Sum F*1)/(k!) by STIRL2_1:def 2;
    hence n block k= 1/(k!) * Sum F by XCMPLX_1:75;
    thus dom F = k+1 by A2,CARD_1:66;
    let m such that A10:m in dom F;
    card k=k & card n=n by CARD_1:66;
    hence F.m=((-1)|^m)*(k choose m)*((k-m)|^ n) by A4,A10;
    end;
     suppose n=0 & k<>0;
      hence thesis by A0, NAT_1:18;
    end;
  end;
  hence thesis;
end;

theorem Th73:
  for X1,Y1,X be finite set st (Y1 is empty implies X1 is empty) & X c= X1
     for F be Function of X1,Y1 st F is one-to-one & card X1=card Y1
     holds
       (card X1-'card X)!=
       Card{f where f is Function of X1,Y1:
                           f is one-to-one & rng(f|(X1\X)) c= F.:(X1\X) &
                           for x st x in X holds f.x=F.x}
proof
  let X1,Y1,X be finite set such that A1:(Y1 is empty implies X1 is empty)
    & X c= X1;
  let F be Function of X1,Y1 such that A2:F is one-to-one & card X1=card Y1;
  set XX=X1\X;
  defpred P[Function,set,set] means $1 is one-to-one & rng ($1|XX) =F.:XX;
  deffunc F(set)=F.$1;
  Z1:for x st x in X1\XX holds F(x) in Y1
  proof
    let x such that A3:x in X1\XX;
    x in X1 & X1=dom F by A1,A3,XBOOLE_0:def 4,FUNCT_2:def 1;
    then F.x in rng F by FUNCT_1:def 5;
    hence thesis;
  end;
  Z2:XX c= X1 & F.:XX c= Y1 by XBOOLE_1:36;
  then XX c= dom F by A1,FUNCT_2:def 1;
  then Z3:F.:XX is empty implies XX is empty by RELAT_1:152;
  Z4:for f be Function of X1,Y1 st
     (for x st x in X1\XX holds F(x)=f.x)
  holds
    P[f,X1,Y1] iff P[f|XX,XX,F.:XX]
  proof
    let f be Function of X1,Y1 such that
      A3:for x st x in X1\XX holds F(x)=f.x;
    thus P[f,X1,Y1] implies P[f|XX,XX,F.:XX] by RELAT_1:101,FUNCT_1:84;
    thus P[f|XX,XX,F.:XX] implies P[f,X1,Y1]
    proof
      assume A4:P[f|XX,XX,F.:XX];
      f|(X1\XX)=F|(X1\XX)
      proof
        A5:dom (f|(X1\XX))=dom f /\(X1\XX) & dom f=X1 & dom (F|(X1\XX))=
          dom F /\(X1\XX) & dom F=X1 by A1,FUNCT_2:def 1,FUNCT_1:68;
        now let x such that A6:x in dom (F|(X1\XX));
          X1\XX=X/\X1 & X/\X1=X by A1,XBOOLE_1:48, XBOOLE_1:28;
          then f.x=(f|(X1\XX)).x & F.x=f.x by A3,A5,A6,FUNCT_1:68;
          hence F.x=(f|(X1\XX)).x;
        end;
        hence thesis by A5,FUNCT_1:68;
      end;
      then rng (f|(X1\XX))=F.:(X1\XX) & rng (f|XX)=F.:XX & (X1\XX)\/ XX= X1&
        rng (f|(X1\XX))=f.:(X1\XX) & rng (f|XX)=f.:XX& 
        F.:((X1\XX)\/XX)=F.:(X1\XX)\/F.:XX&f.:((X1\XX)\/XX)=f.:(X1\XX)\/f.:XX
        by A4,RELAT_1:148,101,153,Z2,XBOOLE_1:45;
      then F.:X1=f.:X1 & X1=dom F & X1=dom f & F is onto & F.:dom F=rng F
        by A1,A2,FUNCT_2:def 1,STIRL2_1:70,RELAT_1:146;
      then rng F=rng f & rng F=Y1 by RELAT_1:146,FUNCT_2:def 3;
      then f is onto by FUNCT_2:def 3;
      hence thesis by A4,RELAT_1:101,A2,STIRL2_1:70;
    end;   
  end;
  set S1={f where f is Function of XX,F.:XX:P[f,XX,F.:XX]};
  set S2={f where f is Function of X1,Y1:P[f,X1,Y1]&rng (f|XX) c=F.:XX&
      (for x st x in X1\XX holds f.x=F(x))};
  set F1={f where f is Function of XX,F.:XX:f is one-to-one};
  set F2={f where f is Function of X1,Y1:f is one-to-one & rng(f|XX) c= F.:XX &
      for x st x in X holds f.x=F.x};
  dom F=X1 by A1,FUNCT_2:def 1;
  then XX,F.:XX are_equipotent by Z2,A2, CARD_1:60;
  then A3:card XX=Card (F.:XX) by CARD_1:21;
  reconsider FX=F.:XX as finite set;
  A4:S1 c= F1
  proof
    let x such that A5:x in S1;
    ex f be Function of XX,FX st f=x & P[f,XX,F.:XX] by A5;
    hence thesis;
  end;
  A5:F1 c= S1
  proof
    let x such that A6:x in F1;
    consider f be Function of XX,FX such that
    A7:x=f & f is one-to-one by A6;
    FX={} implies XX={} by A3,CARD_2:59,CARD_1:47;
    then f is onto & dom f=XX by A3,A7,STIRL2_1:70,FUNCT_2:def 1;
    then rng f=FX & f|XX=f by RELAT_1:98,FUNCT_2:def 3;
    hence thesis by A7;
  end;
  A6:X1\XX=X/\X1 & X/\X1=X by A1,XBOOLE_1:48,XBOOLE_1:28;
  A7:S2 c= F2
  proof
    let x such that A8:x in S2;
    ex f be Function of X1,Y1 st x=f & P[f,X1,Y1] & rng (f|XX) c=F.:XX &
      for x st x in X1\XX holds f.x=F(x) by A8; 
    hence thesis by A6;  
  end;
  A8:F2 c= S2
  proof
    let x such that A9:x in F2;
    consider f be Function of X1,Y1 such that
      A10:x=f & f is one-to-one and
      A11:rng(f|XX) c= F.:XX and
      A12:for x st x in X holds f.x=F.x by A9;
    dom f=X1 by A1,FUNCT_2:def 1;
    then XX,f.:XX are_equipotent by Z2,A10, CARD_1:60;
    then card XX=card (f.:XX) by CARD_1:21;
    then card FX=card rng(f|XX) by A3,RELAT_1:148;
    then rng(f|XX)=FX by A11,TRIANG_1:3;
    hence thesis by A6,A10,A12;
  end;
  Card F1= card XX!/((card FX-'card XX)!) & card FX-'card XX=0 &
    card XX= card X1 -card X & card XX>=0
    by A1,CARD_2:63,NAT_1:18,A3,Th6,BINARITH:51;
  then A9:Card F1=(card X1 -' card X)! & F1=S1 & F2=S2
    by A4,A5,A7,A8,BINARITH:def 3,NEWTON:18,XBOOLE_0:def 10;
  Card S1=Card S2 from STIRL2_1:sch 3(Z1,Z2,Z3,Z4);
  hence thesis by A9;
end; 
  
Lm_Th74:
 for X,Y for F be Function of X,Y st dom F=X & F is one-to-one
  ex XF be XFinSequence of INT st
    dom XF = card X & (card X)!-Sum XF=
                      Card {h where h is Function of X,rng F:
                            h is one-to-one & for x st x in X holds h.x<>F.x}&
    for n st n in dom XF holds XF.n=((-1)|^n) * (card X!) / ((n+1)!) 
proof
  let X,Y;
  let F be Function of X,Y such that A1:dom F=X & F is one-to-one;
 deffunc fy(set)={h where h is Function of X,rng F:h is one-to-one&h.$1=F.$1};
  A2:for x st x in X holds fy(x) in bool Funcs(X,rng F)
  proof
    let x such that A3:x in X;
    fy(x) c= Funcs(X,rng F)
    proof
      let y such that A4:y in fy(x);
      rng F<>{} &
        ex h be Function of X,rng F st y=h & h is one-to-one & h.x=F.x
        by A4,A1,A3,RELAT_1:65;
      hence thesis by FUNCT_2:11;
    end;
    hence thesis;
  end;
  consider Fy' be Function of X,bool Funcs(X,rng F) such that
    A3:for x st x in X holds Fy'.x = fy(x) from FUNCT_2:sch 2(A2);
  for y holds Fy'.y is finite
    proof let y;
      now per cases;
        suppose y in dom Fy';
          then Fy'.y in rng Fy' by FUNCT_1:def 5;
          hence thesis by FINSET_1:13;
        end;
        suppose not y in dom Fy';
          hence thesis by FUNCT_1:def 4;
        end;
      end;
      hence thesis;
    end;
  then reconsider Fy=Fy' as finite-yielding Function by Def3;
  defpred xf[set,set] means
      for n,k st n=$1 & k=(card X)-(n+1) holds $2=k!;
  A4:for x st x in card X ex y st y in NAT & xf[x,y]
  proof
    let x such that A5:x in card X;
    card X is Subset of NAT by STIRL2_1:8;
    then reconsider n=x as Nat by A5;
   n<card X by A5,EULER_1:1;
    then n+1<=card X by NAT_1:38;
    then reconsider k=(card X)-(n+1) as Nat by NAT_1:61;
    xf[n, k!] & k! is Nat;
    hence thesis;  
  end;
  consider XF be Function of card X,NAT such that
    A5:for x st x in card X holds xf[x,XF.x] from FUNCT_2:sch 1(A4);  
  A6:dom XF=card X & rng XF c= NAT by FUNCT_2:def 1;
  then reconsider XF as XFinSequence by AFINSQ_1:7;
  reconsider XF as XFinSequence of NAT by A6,ORDINAL1:def 8;
  reconsider rngF=rng F as finite set;
  A7:for n st n in dom XF holds ex x,y st x<>y &
    for f st f in Choose(X,n+1,x,y) holds Card(Intersection(Fy,f,x))=XF.n
    proof
      let n such that A8:n in dom XF;
      take 0,1;
      thus 0<>1;
      let f' be Function such that A9:f' in Choose(X,n+1,0,1);
      consider f be Function of X,{0,1} such that
      A10:f = f' & Card (f"{0})=n+1 by A9,Def1;
      reconsider f0=f"{0} as finite set;
      set Xf0=X\f0;  
      set S={h where h is Function of X,rngF:
                           h is one-to-one & rng(h|Xf0) c= F.:Xf0 &
                           for x st x in f0 holds h.x=F.x};
      A11:Intersection(Fy,f,0) c= S
      proof
        assume not Intersection(Fy,f,0) c= S;
        then consider z such that
          A12:z in Intersection(Fy,f,0) & not z in  S by TARSKI:def 3;
        consider x' be set such that
          A13:x' in f"{0} by XBOOLE_0:def 1,A10,CARD_1:47;
        f.x' in {0} by A13,FUNCT_1:def 13;
        then f.x'=0 & x' in dom f by A13,FUNCT_1:def 13,TARSKI:def 1;
        then 0 in rng f by FUNCT_1:def 5;
        then consider x such that
          A14:x in dom f & f.x=0 & z in Fy.x by A12,Th25;
        z in fy(x) by A3,A14;
        then consider h be Function of X,rng F such that
          A15:z=h & h is one-to-one & h.x=F.x;  
        A16:for x1 st x1 in f0 holds h.x1=F.x1
          proof
            let x1 such that A17:x1 in f0;
            Fy'.x1=fy(x1) & x1 in dom f & f.x1 in {0} by A3,FUNCT_1:def 13,A17;
            then Fy'.x1=fy(x1) & x1 in dom f & f.x1 =0 by TARSKI:def 1;
            then h in fy(x1) by A12,A15,Def2;
            then ex h' be Function of X,rng F st h=h' & h' is one-to-one &
              h'.x1=F.x1;
            hence thesis;
          end;
        rng (h|Xf0) c= F.:Xf0
          proof
            assume not rng (h|Xf0) c= F.:Xf0;
            then consider y such that
              A18:y in rng (h|Xf0) & not y in F.:Xf0 by TARSKI:def 3;
            consider x1 such that
              A19:x1 in dom (h|Xf0) & (h|Xf0).x1=y by A18,FUNCT_1:def 5;
            y in rngF & rngF= F.:X by A1,A18,RELAT_1:146;
            then y in F.:X\F.:Xf0 & F.:(X\Xf0)=F.:X\F.:Xf0 
              by A1,A18,XBOOLE_0:def 4,FUNCT_1:123;
            then consider x2 such that
            A20:x2 in dom F & x2 in X\Xf0 & y = F.x2 by FUNCT_1:def 12;
             X\Xf0=X/\f"{0} by XBOOLE_1:48;
            then x2 in f"{0} & x1 in dom h /\ Xf0  & y in rng F
             by A19,A20,FUNCT_1:68,def 5,XBOOLE_0:def 3;
            then h.x2=y & x1 in dom h & h.x1=y & X=dom h & x1 in Xf0 &
              not x2 in Xf0
              by A16,A19,A20,FUNCT_1:68,FUNCT_2:def 1,XBOOLE_0:def 3,def 4;
            hence contradiction by A15,A20,FUNCT_1:def 8;  
          end;  
        hence thesis by A15,A16,A12;
      end;    
      A12:S c= Intersection(Fy,f,0)
      proof
       assume not S c= Intersection(Fy,f,0);
       then consider z such that
         A13:z in S & not z in Intersection(Fy,f,0) by TARSKI:def 3;
       consider h be Function of X,rng F such that
         A14:h=z & h is one-to-one & rng (h|Xf0) c= F.:Xf0 and
         A15:for x st x in f0 holds h.x=F.x by A13;
       consider x such that
         A16:x in f"{0} by XBOOLE_0:def 1,A10,CARD_1:47;
       x in X by A16;
       then x in dom Fy' & X\Xf0=X/\f"{0} & x in X/\f"{0}
         by A16,XBOOLE_1:48,XBOOLE_0:def 3,FUNCT_2:def 1;
       then Fy'.x=fy(x) & Fy'.x in rng Fy' & h.x=F.x & h is one-to-one
         by A3,A14,A15,A16,FUNCT_1:def 5;
       then h in Fy'.x & Fy'.x in rng Fy';
       then h in union rng Fy' by TARSKI:def 4;
       then consider y such that
         A17:y in dom f & f.y=0 & not h in Fy.y by A13,A14,Def2;
       f.y in {0} by A17,TARSKI:def 1;
       then y in f"{0} by A17,FUNCT_1:def 13;
       then h.y=F.y & h is one-to-one by A14,A15;
       then h in fy(y) & Fy'.y=fy(y) by A3,A17;
       hence contradiction by A17;
     end;
     n<card X by A6,A8,EULER_1:1;
     then A13:n+1<=card X by NAT_1:38;
     then reconsider c = card X-(n+1) as Nat by NAT_1:61;
     X,rngF are_equipotent by A1,WELLORD2:def 4;
     then card rngF=card X & X\Xf0=X/\f"{0} by CARD_1:21,XBOOLE_1:48;
     then (rngF={} implies X is empty) & F is Function of X,rngF &
       S = Intersection(Fy,f,0) & card rngF=card X & F is one-to-one
       by A1,A11,A12,FUNCT_2:3,CARD_2:59,CARD_1:47,XBOOLE_0:def 10;
     then Card Intersection(Fy,f,0)=(card X-'(n+1))! & card X-'(n+1)=c
       & XF.n=c! by Th73,A10,A13,BINARITH:50,A5,A6,A8; 
     hence thesis by A10; 
  end;
  dom XF= card X & dom Fy = X by FUNCT_2:def 1;
  then consider F' be XFinSequence of INT such that
    A8:dom F'=card X and
    A9:Card union rng Fy = Sum F' and
    A10:for n st n in dom F' holds F'.n=((-1)|^n)*XF.n*(card X choose (n+1))
    by A7,Th70;
  set S={h where h is Function of X,rng F:
         h is one-to-one & for x st x in X holds h.x<>F.x};
  A11':X,rngF are_equipotent by A1,WELLORD2:def 4;
  then A11:card rngF=card X by CARD_1:21;
  then (card rngF-'card X)!=1 by NEWTON:18,BINARITH:51;
  then A12:Card {f where f is Function of X,rngF:f is one-to-one}=
    card rngF!/((card rngF-'card X)!) &
    card rngF!/((card rngF-'card X)!)=card rngF! by A11,Th6;
  then reconsider One={f where f is Function of X,rng F:f is one-to-one} as
    finite set by CARD_4:1;
  A13:One \ union rng Fy c= S
    proof
      let x such that A14:x in One \ union rng Fy;
      x in One by A14,XBOOLE_0:def 4;
      then consider f be Function of X,rng F such that
        A15:f=x & f is one-to-one;
      assume not x in S;
      then consider x such that
        A16:x in X & f.x=F.x by A15;
      x in dom Fy by A16,FUNCT_2:def 1;
      then Fy.x in rng Fy by FUNCT_1:def 5;
      then fy(x) in rng Fy & f in fy(x) by A3,A15,A16;
      then f in union rng Fy by TARSKI:def 4;
      hence thesis by A14,A15,XBOOLE_0:def 4;  
    end;
  A14:S c= One \ union rng Fy
    proof
      let x such that A15:x in S;
      consider f be Function of X,rng F such that
        A16:x=f & f is one-to-one and
        A17:for x st x in X holds f.x<>F.x by A15;
      assume A18:not x in One \ union rng Fy;
      f in One by A16;
      then f in union rng Fy by A16,A18,XBOOLE_0:def 4;
      then consider Fyy be set such that
        A18:f in Fyy & Fyy in rng Fy by TARSKI:def 4;
      consider y such that
        A19:y in dom Fy & Fy.y=Fyy by A18,FUNCT_1:def 5;
      y in X by A19,FUNCT_2:def 1;
      then f in fy(y) by A3,A18,A19;
      then y in X & ex g be Function of X,rng F
        st f=g & g is one-to-one & g.y=F.y by A19,FUNCT_2:def 1;
      hence contradiction by A17;  
    end;
  A15:union rng Fy' c= One
  proof
    let x such that A16:x in union rng Fy';
    consider Fyx be set such that
      A17:x in Fyx & Fyx in rng Fy' by A16,TARSKI:def 4;
    consider x1 such that
      A18:x1 in dom Fy' & Fy.x1=Fyx by FUNCT_1:def 5,A17;
    x in fy(x1) by A3,A17,A18;
    then ex h be Function of X,rng F st h=x & h is one-to-one & h.x1=F.x1;
    hence thesis;
  end;
  then reconsider u=union rng Fy as finite set by FINSET_1:13;
  take F';
  thus dom F' = card X by A8;
  card(One\u) = card One - card u & card One = (card X)! & One\u = S
    by A11',A12,A13,A14,A15,CARD_2:63,XBOOLE_0:def 10,CARD_1:21;
  hence Card S=(card X)! - Sum F' by A9;
  let n such that A14:n in dom F';
  n<card X by A8,A14,EULER_1:1;
  then A15:n+1 <=card X by NAT_1:38;
  then reconsider c = card X-(n+1) as Nat by NAT_1:61;
  A16:(c!)>0 by NEWTON:23;
  XF.n=c! & card X choose (n+1)=((card X!))/((c!) * ((n+1)!))
    by A15,NEWTON:def 3,A5,A8,A14;
  then A17:XF.n*(card X choose (n+1))=(c! *(card X!))/((c!) * ((n+1)!))
    by XCMPLX_1:75
    .=(card X!)*(c!/((c!) * ((n+1)!))) by XCMPLX_1:75
    .=(card X!)*((c!) / (c!) / ((n+1)!)) by XCMPLX_1:79
    .=(card X!)*(1 / ((n+1)!)) by A16, XCMPLX_1:60
    .=(card X!*1)/ ((n+1)!) by XCMPLX_1:75;
  F'.n=((-1)|^n)*XF.n*(card X choose (n+1)) by A14,A10
    .=((-1)|^n)*(XF.n*(card X choose (n+1)))
    .=((-1)|^n)*(card X!/ ((n+1)!)) by A17;
  hence thesis by XCMPLX_1:75;
end;

theorem Th74:
  for F be Function st dom F=X & F is one-to-one
    ex XF be XFinSequence of INT st
      Sum XF=Card {h where h is Function of X,rng F:
                   h is one-to-one & for x st x in X holds h.x<>F.x}&
      dom XF = card X +1 &
      for n st n in dom XF holds XF.n=((-1)|^n)*(card X!)/(n!) 
proof      
  let F' be Function such that A1:dom F'=X & F' is one-to-one;
  X,rng F' are_equipotent by A1,WELLORD2:def 4;
  then card X=Card rng F' by CARD_1:21;
  then reconsider rngF=rng F' as finite set by CARD_4:1;
  reconsider F=F' as Function of X,rngF by A1,FUNCT_2:3;
  set S={h where h is Function of X,rng F':
                            h is one-to-one & for x st x in X holds h.x<>F'.x};
  rng F'=rng F;                          
  then consider Xf be XFinSequence of INT such that
    A2:dom Xf = card X and
    A3:(card X)!-Sum Xf=Card S and
    A4:for n st n in dom Xf holds Xf.n=((-1)|^n)*(card X!)/((n+1)!)
      by A1,Lm_Th74;
  defpred I[set,set] means for n st n=$1 holds $2=(-1)*Xf.n;
A5:for k st k in card X ex x be Element of INT st I[k,x]
  proof
    let k such that k in card X;
    I[k,(-1)*Xf.k] & (-1)*Xf.k is Element of INT by INT_1:def 2;
    hence thesis;
  end;
  consider F1 be XFinSequence of INT such that
A6:dom F1 = card X and
A7:for k st k in card X holds I[k,F1.k] from STIRL2_1:sch 6(A5);
  reconsider c = (card X)! as Element of INT by INT_1:def 2;   
  reconsider XF=<%c%>^F1 as XFinSequence of INT;
  take XF;
  for n st n in dom Xf holds (-1)*Xf.n=F1.n by A2,A7;
  then (-1)*Sum Xf =Sum F1 & (-1)*Sum Xf= - Sum Xf by A2,A6,Th65;
  then c -Sum Xf=c +Sum F1
    .=addint.(c,Sum F1) by GR_CY_1:14
    .=addint.(addint "**" <%c%>,Sum F1) by STIRL2_1:44
    .=addint.(addint "**" <%c%>,addint "**" F1) by Def5
    .=addint "**" XF by STIRL2_1:47
    .=Sum XF by Def5;
  hence Sum XF=Card S by A3;
  len F1=card X & len <%c%>=1 by A6,AFINSQ_1:def 1,37;
  hence A8:dom XF = card X +1 by AFINSQ_1:def 4;
  let n such that A9:n in dom XF;
  per cases by NAT_1:18;
    suppose n=0;
      then ((-1)|^n)=1 & n!=1
        & XF.n=c by NEWTON:9,18,AFINSQ_1:39;
      hence thesis;
    end;
    suppose n>0;
      then reconsider n1=n-1 as Nat by NAT_1:60;
      n < card X +1 by A8,A9,EULER_1:1;
      then n1+1 <= card X by NAT_1:38;
      then n1 < card X by NAT_1:38;
      then n1 in dom F1 & len <% c %>=1 by A6,EULER_1:1,AFINSQ_1:37;
      then XF.(n1+1)=F1.n1 & n1 in card X by A6,AFINSQ_1:def 4;
      then XF.(n1+1)=(-1)*Xf.n1 & n1+1=n &
        Xf.n1=(((-1)|^n1)*(card X!))/((n1+1)!) by A2,A4,A7;
      then XF.n=((-1)*(((-1)|^n1)*(card X!)))/(n!) & n1+1=n by XCMPLX_1:75;
      then (-1)*((-1)|^n1)=(-1)|^n &
      XF.n= ((-1)* ((-1)|^n1))*(card X!)/(n!) by NEWTON:11;
      hence thesis;
    end;
end;

:: disorders

theorem
  ex XF be XFinSequence of INT st
    Sum XF=Card {h where h is Function of X,X:
                  h is one-to-one & for x st x in X holds h.x<>x}&
    dom XF = card X+1 &
    for n st n in dom XF holds XF.n=((-1)|^n)*(card X!)/(n!)
proof
  set S1={h where h is Function of X,X:
    h is one-to-one & for x st x in X holds h.x<>(id X).x};
  set S2={h where h is Function of X,X:
  h is one-to-one & for x st x in X holds h.x<>x};
  A1:S1 c= S2 
  proof
    let x such that A2:x in S1;
    consider h be Function of X,X such that
      A3:h=x & h is one-to-one & for y st y in X holds h.y<>(id X).y by A2;
    now let y such that A4:y in X;
      (id X).y=y by A4,FUNCT_1:34;
      hence h.y<>y by A3,A4;
    end;
    hence thesis by A3;
  end;
  A2:S2 c= S1
  proof
    let x such that A2:x in S2;
    consider h be Function of X,X such that
      A3:h=x & h is one-to-one & for y st y in X holds h.y<>y by A2;
    now let y such that A4:y in X;
      (id X).y=y by A4,FUNCT_1:34;
      hence (id X).y<>h.y by A3,A4;
    end;
    hence thesis by A3;
  end;
  dom id X=X & rng id X=X & id X is one-to-one by RELAT_1:71,FUNCT_1:52;
  then S1=S2 & ex XF be XFinSequence of INT st
    Sum XF=Card S1&dom XF = card X +1 &
    for n st n in dom XF holds XF.n=((-1)|^n)*(card X!)/(n!)
      by Th74,A1,A2,XBOOLE_0:def 10;
  hence thesis;
end;
