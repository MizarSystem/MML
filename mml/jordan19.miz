:: On the Upper and Lower Approximations of the Curve
::  by Robert Milewski
::
:: Received September 27, 2003
:: Copyright (c) 2003 Association of Mizar Users
 
environ

 vocabularies NUMBERS, SUBSET_1, TOPREAL2, PROB_1, STRUCT_0, EUCLID, FUNCT_1,
      JORDAN6, TOPREAL1, JORDAN9, KURATO_2, GOBOARD1, XXREAL_0, TREES_1,
      CARD_1, ARYTM_3, FINSEQ_1, INT_1, JORDAN1A, MATRIX_1, XBOOLE_0, PSCOMP_1,
      JORDAN8, RELAT_1, MCART_1, ARYTM_1, NEWTON, RCOMP_1, SPPOL_1, RLTOPSP1,
      TARSKI, RELAT_2, JORDAN1E, JORDAN3, ORDINAL4, PARTFUN1, FINSEQ_5,
      PRE_TOPC, GROUP_2, SPPOL_2, SPRECT_2, METRIC_1, XREAL_0, ORDINAL1,
      GOBOARD5, FINSEQ_4, GRAPH_2, FINSEQ_6, ZFMISC_1, GOBOARD9, GOBOARD2,
      NAT_1, CONNSP_1, JORDAN2C, SEQ_4, REAL_1, CONNSP_2, POWER, COMPLEX1,
      JORDAN19;
 notations TARSKI, XBOOLE_0, ENUMSET1, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0,
      XXREAL_0, XREAL_0, COMPLEX1, REAL_1, INT_1, NAT_1, NAT_D, FUNCT_1,
      RELSET_1, PARTFUN1, LIMFUNC1, FINSEQ_1, FINSEQ_4, FINSEQ_5, MATRIX_1,
      FINSEQ_6, POWER, METRIC_1, CONNSP_2, GRAPH_2, REALSET1, SEQ_4, STRUCT_0,
      PRE_TOPC, TOPREAL2, NEWTON, PROB_1, CONNSP_1, COMPTS_1, RLVECT_1,
      RLTOPSP1, EUCLID, PSCOMP_1, SPRECT_2, SPPOL_2, KURATO_2, GOBOARD1,
      TOPREAL6, TOPREAL1, GOBOARD2, GOBOARD5, GOBOARD9, GOBRD13, SPPOL_1,
      JORDAN3, JORDAN6, JORDAN8, JORDAN9, JORDAN2C, JORDAN1A, JORDAN1E;
 constructors REAL_1, NAT_1, NAT_D, FINSEQ_4, LIMFUNC1, NEWTON, POWER,
      BINARITH, CONNSP_1, REALSET2, CONNSP_2, MATRIX_2, MONOID_0, GOBOARD2,
      JORDAN1, PSCOMP_1, GRAPH_2, GOBOARD9, JORDAN3, JORDAN5C, JORDAN6,
      JORDAN2C, TOPREAL6, JORDAN8, GOBRD13, JORDAN9, JORDAN1A, JORDAN1E,
      KURATO_2, SEQ_4, RELSET_1, CONVEX1;
 registrations XBOOLE_0, SUBSET_1, ORDINAL1, RELSET_1, XXREAL_0, XREAL_0,
      REAL_1, NAT_1, FINSEQ_1, FINSEQ_5, FINSEQ_6, STRUCT_0, COMPTS_1,
      METRIC_1, EUCLID, TOPREAL1, TOPREAL2, GOBOARD2, JORDAN1, SPPOL_2,
      PSCOMP_1, GOBRD11, TOPREAL5, SPRECT_1, SPRECT_2, JORDAN6, REVROT_1,
      TOPREAL6, JORDAN8, JORDAN1E, JORDAN1J, CARD_1, FUNCT_1, RLTOPSP1,
      MEASURE6;
 requirements NUMERALS, ARITHM, BOOLE, SUBSET, REAL;
 definitions TARSKI, XBOOLE_0, PSCOMP_1, EUCLID, XCMPLX_0;
 theorems NAT_1, FINSEQ_1, GOBOARD1, FINSEQ_4, EUCLID, FINSEQ_3, SPPOL_2,
      TARSKI, JORDAN3, PSCOMP_1, FINSEQ_5, FINSEQ_6, GOBOARD7, TOPREAL1,
      JORDAN5B, GOBOARD5, SPRECT_2, SPPOL_1, ABSVALUE, GOBOARD9, FINSEQ_2,
      SUBSET_1, GOBRD11, JORDAN4, GOBOARD2, SPRECT_3, TOPREAL3, JORDAN8,
      PARTFUN2, SPRECT_1, XBOOLE_0, XBOOLE_1, ZFMISC_1, GOBRD14, JORDAN2C,
      TOPREAL5, PRE_TOPC, JORDAN6, JORDAN9, JORDAN1H, JORDAN1A, JORDAN1E,
      JORDAN10, JGRAPH_1, REVROT_1, COMPTS_1, ENUMSET1, JORDAN1B, JORDAN1F,
      JORDAN1G, JORDAN1I, JORDAN1J, GOBOARD3, TOPREAL8, GRAPH_2, SPRECT_5,
      JORDAN1D, METRIC_1, POWER, PRE_FF, JORDAN15, PCOMPS_1, TOPRNS_1,
      BOOLMARK, JORDAN5D, XCMPLX_1, XREAL_0, KURATO_2, INT_1, REALSET1,
      XREAL_1, NEWTON, XXREAL_0, GOBOARD6, PREPOWER, TOPREAL6, PARTFUN1,
      MATRIX_1, NAT_D, RLTOPSP1;
 schemes FUNCT_2, BINOP_2;

begin

reserve n for Element of NAT;

definition
  let C be Simple_closed_curve;
  func Upper_Appr C -> SetSequence of the carrier of TOP-REAL 2 means
  :Def1:
  for i be Element of NAT holds it.i = Upper_Arc L~Cage (C,i);
  existence
  proof
    deffunc O(Element of NAT) = Upper_Arc L~Cage (C,$1);
    thus ex S be SetSequence of the carrier of TOP-REAL 2 st
    for i being Element of NAT holds S.i = O(i) from FUNCT_2:sch 4;
  end;
  uniqueness
  proof
    deffunc O(Element of NAT) = Upper_Arc L~Cage (C,$1);
    thus for S1,S2 be SetSequence of the carrier of TOP-REAL 2 st
    (for i being Element of NAT holds S1.i = O(i)) &
    (for i being Element of NAT holds S2.i = O(i)) holds S1 = S2
    from BINOP_2:sch 1;
  end;
  func Lower_Appr C -> SetSequence of the carrier of TOP-REAL 2 means
  :Def2:
  for i being Element of NAT holds it.i = Lower_Arc L~Cage (C,i);
  existence
  proof
    deffunc O(Element of NAT) = Lower_Arc L~Cage (C,$1);
    thus ex S be SetSequence of the carrier of TOP-REAL 2 st
    for i being Element of NAT holds S.i = O(i) from FUNCT_2:sch 4;
  end;
  uniqueness
  proof
    deffunc O(Element of NAT) = Lower_Arc L~Cage (C,$1);
    thus for S1,S2 be SetSequence of the carrier of TOP-REAL 2 st
    (for i being Element of NAT holds S1.i = O(i)) &
    (for i being Element of NAT holds S2.i = O(i)) holds S1 = S2
    from BINOP_2:sch 1;
  end;
end;

definition
  let C be Simple_closed_curve;
  func North_Arc C -> Subset of TOP-REAL 2 equals
  Lim_inf Upper_Appr C;
  coherence;
  func South_Arc C -> Subset of TOP-REAL 2 equals
  Lim_inf Lower_Appr C;
  coherence;
end;

Lm1: now
  let G be Go-board;
  let j be Element of NAT;
  assume that
A1: 1 <= j and
A2: j <= width G;
  0 + 1 <= len G div 2 + 1 by XREAL_1:8;
  then
A3: 0 + 1 <= Center G by JORDAN1A:def 1;
  Center G <= len G by JORDAN1B:14;
  hence [Center G,j] in Indices G by A1,A2,A3,MATRIX_1:37;
end;

Lm2: now
  let D be non empty Subset of TOP-REAL 2;
  let n,i be Element of NAT;
  set a = N-bound D, s = S-bound D, w = W-bound D, e = E-bound D;
  set G = Gauge(D,n);
  assume [i,width G] in Indices G;
  hence G*(i,width G)`2
  = |[w+(e-w)/(2|^n)*(i - 2), s+(a-s)/(2|^n)*(width G - 2)]|`2
  by JORDAN8:def 1
    .= s+(a-s)/(2|^n)*(width G - 2) by EUCLID:56;
end;

theorem Th1:
  for n,m be Element of NAT holds n <= m & n <> 0 implies (n+1)/n >= (m+1)/m
proof
  let n,m be Element of NAT;
  assume that
A1: n <= m and
A2: n <> 0;
A3: n > 0 by A2;
A4: 1/n >= 1/m by A1,A2,XREAL_1:87;
A5: (n+1)/n = n/n + 1/n .= 1 + 1/n by A2,XCMPLX_1:60;
  (m+1)/m = m/m + 1/m .= 1 + 1/m by A1,A3,XCMPLX_1:60;
  hence thesis by A4,A5,XREAL_1:9;
end;

theorem Th2:
  for E be compact non vertical non horizontal Subset of TOP-REAL 2
  for m,j be Element of NAT
  st 1 <= m & m <= n & 1 <= j & j <= width Gauge(E,n) holds
  LSeg(Gauge(E,n)*(Center Gauge(E,n),width Gauge(E,n)),
  Gauge(E,n)*(Center Gauge(E,n),j)) c=
  LSeg(Gauge(E,m)*(Center Gauge(E,m),width Gauge(E,m)),
  Gauge(E,n)*(Center Gauge(E,n),j))
proof
  let E be compact non vertical non horizontal Subset of TOP-REAL 2;
  let m,j be Element of NAT;
  set a = N-bound E, s = S-bound E, w = W-bound E, e = E-bound E,
  G = Gauge(E,n), M = Gauge(E,m), sn = Center G, sm = Center M;
  assume that
A1: 1 <= m and
A2: m <= n and
A3: 1 <= j and
A4: j <= width G;
A5: width M = len M by JORDAN8:def 1
    .= 2|^m+3 by JORDAN8:def 1;
A6: width G = len G by JORDAN8:def 1
    .= 2|^n+3 by JORDAN8:def 1;
A7: now
    let t be Element of NAT;
    assume that
A8: width G >= t and
A9: t >= j;
A10: len M = width M by JORDAN8:def 1;
A11: len G = width G by JORDAN8:def 1;
A12: 0 < a - s by SPRECT_1:34,XREAL_1:52;
A13: t >= 1 by A3,A9,XXREAL_0:2;
A14: 0 < 2|^m by NEWTON:102;
A15: 1 <= len M by GOBRD11:34;
    then
A16: M*(sm,width M)`1 = G*(sn,t)`1 by A1,A2,A8,A10,A11,A13,JORDAN1A:57;
A17: G*(sn,t)`1 = G*(sn,j)`1 by A1,A2,A3,A4,A8,A11,A13,JORDAN1A:57;
    [sn,t] in Indices G by A8,A13,Lm1;
    then
A18: G*(sn,t)`2 = |[w+(e-w)/(2|^n)*(sn - 2),s+(a-s)/(2|^n)*(t-2)]|`2
    by JORDAN8:def 1
      .= s+(a-s)/(2|^n)*(t-2) by EUCLID:56;
    [sm,width M] in Indices M by A10,A15,Lm1;
    then
A19: M*(sm,width M)`2 = s+(a-s)/(2|^m)*(width M-2) by Lm2;
A20: (2|^m+1)/(2|^m) >= (2|^n+1)/(2|^n) by A2,A14,Th1,PREPOWER:107;
    t-2 <= 2|^n+3-2 by A6,A8,XREAL_1:11;
    then (t-2)/(2|^n) <= (2|^n+1)/(2|^n) by XREAL_1:74;
    then (t-2)/(2|^n) <= (width M-2)/(2|^m) by A5,A20,XXREAL_0:2;
    then (a-s)*((t-2)/(2|^n)) <= (a-s)*((width M-2)/(2|^m)) by A12,XREAL_1:66;
    then
A21: s+(a-s)/(2|^m)*(width M-2) >= s+(a-s)/(2|^n)*(t-2) by XREAL_1:8;
A22: 1 <= sn by JORDAN1B:12;
    sn <= len G by JORDAN1B:14;
    then G*(sn,t)`2 >= G*(sn,j)`2 by A3,A8,A9,A22,SPRECT_3:24;
    hence G*(sn,t) in LSeg(M*(sm,width M),G*(sn,j)) by A16,A17,A18,A19,A21,
GOBOARD7:8;
  end;
  then
A23: G*(sn,width G) in LSeg(M*(sm,width M),G*(sn,j)) by A4;
  G*(sn,j) in LSeg(M*(sm,width M),G*(sn,j)) by A4,A7;
  hence thesis by A23,TOPREAL1:12;
end;

theorem Th3:
  for C be compact connected non vertical non horizontal Subset of TOP-REAL 2
  for i,j be Element of NAT st 1 <= i & i <= len Gauge(C,n) &
  1 <= j & j <= width Gauge(C,n) & Gauge(C,n)*(i,j) in L~Cage(C,n) holds
  LSeg(Gauge(C,n)*(i,width Gauge(C,n)),Gauge(C,n)*(i,j)) meets
  L~Upper_Seq(C,n)
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
  let i,j be Element of NAT;
  set Gij = Gauge(C,n)*(i,j);
  assume that
A1: 1 <= i and
A2: i <= len Gauge(C,n) and
A3: 1 <= j and
A4: j <= width Gauge(C,n) and
A5: Gij in L~Cage(C,n);
  set NE = SW-corner L~Cage(C,n);
  set v1 = L_Cut(Lower_Seq(C,n),Gij);
  set wG = width Gauge(C,n);
  set lG = len Gauge(C,n);
  set Gv1 = <*Gauge(C,n)*(i,wG)*>^v1;
  set v = Gv1^<*NE*>;
  set h = mid(Upper_Seq(C,n),2,len Upper_Seq(C,n));
A6: L~Cage(C,n) = L~Lower_Seq(C,n) \/ L~Upper_Seq(C,n) by JORDAN1E:17;
A7: len Upper_Seq(C,n) >= 3 by JORDAN1E:19;
A8: len Lower_Seq(C,n) >= 3 by JORDAN1E:19;
A9: len Upper_Seq(C,n) >= 2 by A7,XXREAL_0:2;
A10: len Upper_Seq(C,n) >= 1 by A7,XXREAL_0:2;
A11: len Lower_Seq(C,n) >= 1 by A8,XXREAL_0:2;
A12: len Gauge(C,n) = width Gauge(C,n) by JORDAN8:def 1;
  then width Gauge(C,n) >= 4 by JORDAN8:13;
  then
A13: 1 <= width Gauge(C,n) by XXREAL_0:2;
A14: Gauge(C,n)*(i,wG)`2 = N-bound L~Cage(C,n) by A1,A2,A12,JORDAN1A:91;
  set Ema = E-max L~Cage(C,n);
  now per cases by A2,A5,A6,XBOOLE_0:def 3,XXREAL_0:1;
    suppose
A15:  Gij in L~Lower_Seq(C,n) & i = lG;
      set G11 = Gauge(C,n)*(lG,wG);
A16:  G11`1 = E-bound L~Cage(C,n) by A1,A12,A15,JORDAN1A:92;
A17:  Ema`1 = E-bound L~Cage(C,n) by EUCLID:56;
A18:  N-bound L~Cage(C,n) = G11`2 by A1,A12,A15,JORDAN1A:91;
      Ema in L~Cage(C,n) by SPRECT_1:16;
      then
A19:  G11`2 >= Ema`2 by A18,PSCOMP_1:71;
A20:  Gij`1 = E-bound L~Cage(C,n) by A3,A4,A12,A15,JORDAN1A:92;
      then Gij in E-most L~Cage(C,n) by A5,SPRECT_2:17;
      then Ema`2 >= Gij`2 by PSCOMP_1:108;
      then
A21:  Ema in LSeg(Gauge(C,n)*(lG,wG),Gauge(C,n)*(lG,j))
      by A15,A16,A17,A19,A20,GOBOARD7:8;
A22:  rng Upper_Seq(C,n) c= L~Upper_Seq(C,n) by A7,SPPOL_2:18,XXREAL_0:2;
      Upper_Seq(C,n)/.(len Upper_Seq(C,n)) = Ema by JORDAN1F:7;
      then Ema in rng Upper_Seq(C,n) by REVROT_1:3;
      hence thesis by A15,A21,A22,XBOOLE_0:3;
    end;
    suppose
A23:  Gij in L~Lower_Seq(C,n) & Gij <> Lower_Seq(C,n).len Lower_Seq(C,n)
      & W-min L~Cage(C,n) <> NE & i < lG;
      then
A24:  v1 is non empty by JORDAN1E:7;
      then
A25:  0+1 <= len v1 by NAT_1:13;
      then
A26:  1 in dom v1 by FINSEQ_3:27;
A27:  len v1 in dom v1 by A25,FINSEQ_3:27;
A28:  len Lower_Seq(C,n) in dom Lower_Seq(C,n) by A11,FINSEQ_3:27;
A29:  v1/.(len v1) = v1.(len v1) by A27,PARTFUN1:def 8
        .= Lower_Seq(C,n).len Lower_Seq(C,n) by A23,JORDAN1B:5
        .= Lower_Seq(C,n)/.len Lower_Seq(C,n) by A28,PARTFUN1:def 8
        .= W-min L~Cage(C,n) by JORDAN1F:8;
      then
A30:  Gv1/.len Gv1 = W-min L~Cage(C,n) by A24,SPRECT_3:11;
A31:  v1/.1 = v1.1 by A26,PARTFUN1:def 8
        .= Gij by A23,JORDAN3:58;
      then
A32:  (v1^<*NE*>)/.1 = Gij by A25,BOOLMARK:8;
A33:  1+len v1 >= 1+1 by A25,XREAL_1:9;
      len v = len Gv1 + 1 by FINSEQ_2:19
        .= 1 + len v1 + 1 by FINSEQ_5:8;
      then 2 < len v by A33,NAT_1:13;
      then
A34:  2 < len Rev v by FINSEQ_5:def 3;
      S-bound L~Cage(C,n) < N-bound L~Cage(C,n) by SPRECT_1:34;
      then NE <> Gauge(C,n)*(i,wG) by A14,EUCLID:56;
      then not NE in {Gauge(C,n)*(i,wG)} by TARSKI:def 1;
      then
A35:  not NE in rng <*Gauge(C,n)*(i,wG)*> by FINSEQ_1:56;
      len Cage(C,n) > 4 by GOBOARD7:36;
      then
A36:  rng Cage(C,n) c= L~Cage(C,n) by SPPOL_2:18,XXREAL_0:2;
A37:  not NE in rng Cage(C,n)
      proof
        assume
A38:    NE in rng Cage(C,n);
A39:    NE`1 = W-bound L~Cage(C,n) by EUCLID:56;
A40:    NE`2 = S-bound L~Cage(C,n) by EUCLID:56;
        then NE`2 <= N-bound L~Cage(C,n) by SPRECT_1:24;
        then NE in { p where p is Point of TOP-REAL 2 :
        p`1 = W-bound L~Cage(C,n) & p`2 <= N-bound L~Cage(C,n) &
        p`2 >= S-bound L~Cage(C,n) } by A39,A40;
        then NE in LSeg(SW-corner L~Cage(C,n), NW-corner L~Cage(C,n))
        by SPRECT_1:28;
        then NE in LSeg(SW-corner L~Cage(C,n), NW-corner L~Cage(C,n)) /\
        L~Cage(C,n) by A36,A38,XBOOLE_0:def 4;
        then
A41:    NE`2 >= (W-min L~Cage(C,n))`2 by PSCOMP_1:88;
        (W-min L~Cage(C,n))`2 >= NE`2 by PSCOMP_1:87;
        then
A42:    (W-min L~Cage(C,n))`2 = NE`2 by A41,XXREAL_0:1;
        (W-min L~Cage(C,n))`1 = NE`1 by PSCOMP_1:85;
        hence contradiction by A23,A42,TOPREAL3:11;
      end;
      now per cases;
        suppose Gij <> Lower_Seq(C,n).(Index(Gij,Lower_Seq(C,n))+1);
          then v1 = <*Gij*>^mid(Lower_Seq(C,n),
          Index(Gij,Lower_Seq(C,n))+1,len Lower_Seq(C,n)) by JORDAN3:def 4;
          then rng v1 = rng <*Gij*> \/ rng mid(Lower_Seq(C,n),
          Index(Gij,Lower_Seq(C,n))+1,len Lower_Seq(C,n)) by FINSEQ_1:44;
          then
A43:      rng v1 = {Gij} \/ rng mid(Lower_Seq(C,n),
          Index(Gij,Lower_Seq(C,n))+1,len Lower_Seq(C,n)) by FINSEQ_1:55;
          not NE in L~Cage(C,n)
          proof
            assume NE in L~Cage(C,n);
            then consider i be Element of NAT such that
A44:        1 <= i and
A45:        i+1 <= len Cage(C,n) and
A46:        NE in LSeg(Cage(C,n)/.i,Cage(C,n)/.(i+1)) by SPPOL_2:14;
            per cases by A44,A45,TOPREAL1:def 7;
            suppose
A47:          (Cage(C,n)/.i)`1 = (Cage(C,n)/.(i+1))`1;
              then
A48:          NE`1 = (Cage(C,n)/.i)`1 by A46,GOBOARD7:5;
A49:          NE`2 = S-bound L~Cage(C,n) by EUCLID:56;
A50:          i < len Cage(C,n) by A45,NAT_1:13;
              then
A51:          (Cage(C,n)/.i)`2 >= NE`2 by A44,A49,JORDAN5D:13;
A52:          1 <= i+1 by NAT_1:11;
              then
A53:          (Cage(C,n)/.(i+1))`2 >= NE`2 by A45,A49,JORDAN5D:13;
A54:          i in dom Cage(C,n) by A44,A50,FINSEQ_3:27;
A55:          i+1 in dom Cage(C,n) by A45,A52,FINSEQ_3:27;
              (Cage(C,n)/.i)`2 <= (Cage(C,n)/.(i+1))`2 or
              (Cage(C,n)/.i)`2 >= (Cage(C,n)/.(i+1))`2;
              then NE`2 >= (Cage(C,n)/.(i+1))`2 or NE`2 >= (Cage(C,n)/.i)`2
              by A46,TOPREAL1:10;
              then NE`2 = (Cage(C,n)/.(i+1))`2 or NE`2 = (Cage(C,n)/.i)`2
              by A51,A53,XXREAL_0:1;
              then NE = (Cage(C,n)/.(i+1)) or NE = (Cage(C,n)/.i)
              by A47,A48,TOPREAL3:11;
              hence contradiction by A37,A54,A55,PARTFUN2:4;
            end;
            suppose
A56:          (Cage(C,n)/.i)`2 = (Cage(C,n)/.(i+1))`2;
              then
A57:          NE`2 = (Cage(C,n)/.i)`2 by A46,GOBOARD7:6;
A58:          NE`1 = W-bound L~Cage(C,n) by EUCLID:56;
A59:          i < len Cage(C,n) by A45,NAT_1:13;
              then
A60:          (Cage(C,n)/.i)`1 >= NE`1 by A44,A58,JORDAN5D:14;
A61:          1 <= i+1 by NAT_1:11;
              then
A62:          (Cage(C,n)/.(i+1))`1 >= NE`1 by A45,A58,JORDAN5D:14;
A63:          i in dom Cage(C,n) by A44,A59,FINSEQ_3:27;
A64:          i+1 in dom Cage(C,n) by A45,A61,FINSEQ_3:27;
              (Cage(C,n)/.i)`1 <= (Cage(C,n)/.(i+1))`1 or
              (Cage(C,n)/.i)`1 >= (Cage(C,n)/.(i+1))`1;
              then NE`1 >= (Cage(C,n)/.(i+1))`1 or NE`1 >= (Cage(C,n)/.i)`1
              by A46,TOPREAL1:9;
              then NE`1 = (Cage(C,n)/.(i+1))`1 or NE`1 = (Cage(C,n)/.i)`1
              by A60,A62,XXREAL_0:1;
              then NE = (Cage(C,n)/.(i+1)) or NE = (Cage(C,n)/.i)
              by A56,A57,TOPREAL3:11;
              hence contradiction by A37,A63,A64,PARTFUN2:4;
            end;
          end;
          then
A65:      not NE in {Gij} by A5,TARSKI:def 1;
A66:      rng mid(Lower_Seq(C,n),Index(Gij,Lower_Seq(C,n))+1,
          len Lower_Seq(C,n)) c= rng Lower_Seq(C,n) by FINSEQ_6:125;
          rng Lower_Seq(C,n) c= rng Cage(C,n) by JORDAN1G:47;
          then rng mid(Lower_Seq(C,n),Index(Gij,Lower_Seq(C,n))+1,
          len Lower_Seq(C,n)) c= rng Cage(C,n) by A66,XBOOLE_1:1;
          then not NE in rng mid(Lower_Seq(C,n),Index(Gij,Lower_Seq(C,n))+1,
          len Lower_Seq(C,n)) by A37;
          hence not NE in rng v1 by A43,A65,XBOOLE_0:def 3;
        end;
        suppose Gij = Lower_Seq(C,n).(Index(Gij,Lower_Seq(C,n))+1);
          then v1 = mid(Lower_Seq(C,n),
          Index(Gij,Lower_Seq(C,n))+1,len Lower_Seq(C,n)) by JORDAN3:def 4;
          then
A67:      rng v1 c= rng Lower_Seq(C,n) by FINSEQ_6:125;
          rng Lower_Seq(C,n) c= rng Cage(C,n) by JORDAN1G:47;
          then rng v1 c= rng Cage(C,n) by A67,XBOOLE_1:1;
          hence not NE in rng v1 by A37;
        end;
      end;
      then not NE in rng <*Gauge(C,n)*(i,wG)*> \/ rng v1 by A35,XBOOLE_0:def 3;
      then not NE in rng Gv1 by FINSEQ_1:44;
      then rng Gv1 misses {NE} by ZFMISC_1:56;
      then
A68:  rng Gv1 misses rng <*NE*> by FINSEQ_1:55;
A69:  not Gauge(C,n)*(i,wG) in L~Lower_Seq(C,n) by A1,A23,JORDAN1G:53;
      rng Lower_Seq(C,n) c= L~Lower_Seq(C,n) by A8,SPPOL_2:18,XXREAL_0:2;
      then
A70:  not Gauge(C,n)*(i,wG) in rng Lower_Seq(C,n) by A1,A23,JORDAN1G:53;
      not Gauge(C,n)*(i,wG) in {Gij} by A23,A69,TARSKI:def 1;
      then
A71:  not Gauge(C,n)*(i,wG) in rng <*Gij*> by FINSEQ_1:55;
      set ci = mid(Lower_Seq(C,n),Index(Gij,Lower_Seq(C,n))+1,
      len Lower_Seq(C,n));
      now per cases;
        suppose
A72:      Gij <> Lower_Seq(C,n).(Index(Gij,Lower_Seq(C,n))+1);
          rng ci c= rng Lower_Seq(C,n) by FINSEQ_6:125;
          then not Gauge(C,n)*(i,wG) in rng ci by A70;
          then not Gauge(C,n)*(i,wG) in rng <*Gij*> \/ rng ci
          by A71,XBOOLE_0:def 3;
          then not Gauge(C,n)*(i,wG) in rng(<*Gij*>^ci) by FINSEQ_1:44;
          hence not Gauge(C,n)*(i,wG) in rng v1 by A72,JORDAN3:def 4;
        end;
        suppose Gij = Lower_Seq(C,n).(Index(Gij,Lower_Seq(C,n))+1);
          then v1 = ci by JORDAN3:def 4;
          then rng v1 c= rng Lower_Seq(C,n) by FINSEQ_6:125;
          hence not Gauge(C,n)*(i,wG) in rng v1 by A70;
        end;
      end;
      then {Gauge(C,n)*(i,wG)} misses rng v1 by ZFMISC_1:56;
      then
A73:  rng <*Gauge(C,n)*(i,wG)*> misses rng v1 by FINSEQ_1:55;
A74:  <*Gauge(C,n)*(i,wG)*> is one-to-one by FINSEQ_3:102;
A75:  v1 is being_S-Seq by A23,JORDAN3:69;
      then
A76:  Gv1 is one-to-one by A73,A74,FINSEQ_3:98;
      <*NE*> is one-to-one by FINSEQ_3:102;
      then
A77:  v is one-to-one by A68,A76,FINSEQ_3:98;
A78:  <*Gauge(C,n)*(i,wG)*> is special;
      (<*Gauge(C,n)*(i,wG)*>/.len <*Gauge(C,n)*(i,wG)*>)`1 =
      (<*Gauge(C,n)*(i,wG)*>/.1)`1 by FINSEQ_1:56
        .= Gauge(C,n)*(i,wG)`1 by FINSEQ_4:25
        .= Gauge(C,n)*(i,1)`1 by A1,A2,A13,GOBOARD5:3
        .= (v1/.1)`1 by A1,A2,A3,A4,A31,GOBOARD5:3;
      then
A79:  Gv1 is special by A75,GOBOARD2:13;
A80:  <*NE*> is special;
      (Gv1/.len Gv1)`1 = NE`1 by A30,PSCOMP_1:85
        .= (<*NE*>/.1)`1 by FINSEQ_4:25;
      then v is special by A79,GOBOARD2:13;
      then
A81:  Rev v is special by SPPOL_2:42;
A82:  len Upper_Seq(C,n) >= 2+1 by JORDAN1E:19;
      then
A83:  len Upper_Seq(C,n) > 2 by NAT_1:13;
      len Upper_Seq(C,n) > 1 by A82,XXREAL_0:2;
      then
A84:  h is S-Sequence_in_R2 by A83,JORDAN3:39;
      then
A85:  2 <= len h by TOPREAL1:def 10;
      3 <= len Upper_Seq(C,n) by JORDAN1E:19;
      then 2 <= len Upper_Seq(C,n) by XXREAL_0:2;
      then
A86:  2 in dom Upper_Seq(C,n) by FINSEQ_3:27;
A87:  len Upper_Seq(C,n) in dom Upper_Seq(C,n) by FINSEQ_5:6;
      then
A88:  h is_in_the_area_of Cage(C,n) by A86,JORDAN1E:21,SPRECT_2:26;
      Upper_Seq(C,n)/.(len Upper_Seq(C,n)) = E-max L~Cage(C,n) by JORDAN1F:7;
      then (Upper_Seq(C,n)/.len Upper_Seq(C,n))`1 = E-bound L~Cage(C,n)
      by EUCLID:56;
      then
A89:  (h/.len h)`1 = E-bound L~Cage(C,n) by A86,A87,SPRECT_2:13;
      (Upper_Seq(C,n)/.(1+1))`1 = W-bound L~Cage(C,n) by JORDAN1G:39;
      then (h/.1)`1 = W-bound L~Cage(C,n) by A86,A87,SPRECT_2:12;
      then
A90:  h is_a_h.c._for Cage(C,n) by A88,A89,SPRECT_2:def 2;
      now
        let m be Element of NAT;
        assume
A91:    m in dom <*Gauge(C,n)*(i,wG)*>;
        then m in Seg 1 by FINSEQ_1:55;
        then m = 1 by FINSEQ_1:4,TARSKI:def 1;
        then <*Gauge(C,n)*(i,wG)*>.m = Gauge(C,n)*(i,wG) by FINSEQ_1:57;
        then
A92:    <*Gauge(C,n)*(i,wG)*>/.m = Gauge(C,n)*(i,wG) by A91,PARTFUN1:def 8;
        Gauge(C,n)*(1,wG)`1 <= Gauge(C,n)*(i,wG)`1 by A1,A2,A13,SPRECT_3:25;
        hence W-bound L~Cage(C,n) <= (<*Gauge(C,n)*(i,wG)*>/.m)`1
        by A12,A13,A92,JORDAN1A:94;
        (Gauge(C,n)*(i,wG))`1 <= Gauge(C,n)*(len Gauge(C,n),wG)`1
        by A1,A2,A13,SPRECT_3:25;
        hence (<*Gauge(C,n)*(i,wG)*>/.m)`1 <= E-bound L~Cage(C,n)
        by A12,A13,A92,JORDAN1A:92;
        (<*Gauge(C,n)*(i,wG)*>/.m)`2 = N-bound L~Cage(C,n)
        by A1,A2,A12,A92,JORDAN1A:91;
        hence S-bound L~Cage(C,n) <= (<*Gauge(C,n)*(i,wG)*>/.m)`2
        by SPRECT_1:24;
        thus (<*Gauge(C,n)*(i,wG)*>/.m)`2 <= N-bound L~Cage(C,n)
        by A1,A2,A12,A92,JORDAN1A:91;
      end;
      then
A93:  <*Gauge(C,n)*(i,wG)*> is_in_the_area_of Cage(C,n) by SPRECT_2:def 1;
      <*Gij*> is_in_the_area_of Cage(C,n) by A23,JORDAN1E:22,SPRECT_3:63;
      then v1 is_in_the_area_of Cage(C,n) by A23,JORDAN1E:22,SPRECT_3:73;
      then
A94:  Gv1 is_in_the_area_of Cage(C,n) by A93,SPRECT_2:28;
      <*NE*> is_in_the_area_of Cage(C,n) by SPRECT_2:32;
      then v is_in_the_area_of Cage(C,n) by A94,SPRECT_2:28;
      then
A95:  Rev v is_in_the_area_of Cage(C,n) by SPRECT_3:68;
      v = <*Gauge(C,n)*(i,wG)*>^(v1^<*NE*>) by FINSEQ_1:45;
      then v/.1 = Gauge(C,n)*(i,wG) by FINSEQ_5:16;
      then (v/.1)`2 = N-bound L~Cage(C,n) by A1,A2,A12,JORDAN1A:91;
      then (Rev v/.(len v))`2 = N-bound L~Cage(C,n) by FINSEQ_5:68;
      then
A96:  (Rev v/.(len Rev v))`2 = N-bound L~Cage(C,n) by FINSEQ_5:def 3;
      len v = len Gv1 + 1 by FINSEQ_2:19;
      then v/.(len v) = NE by FINSEQ_4:82;
      then (v/.len v)`2 = S-bound L~Cage(C,n) by EUCLID:56;
      then (Rev v/.1)`2 = S-bound L~Cage(C,n) by FINSEQ_5:68;
      then Rev v is_a_v.c._for Cage(C,n) by A95,A96,SPRECT_2:def 3;
      then L~h meets L~Rev v by A34,A77,A81,A84,A85,A90,SPRECT_2:33;
      then L~h meets L~v by SPPOL_2:22;
      then consider x be set such that
A97:  x in L~h and
A98:  x in L~v by XBOOLE_0:3;
A99:  L~h c= L~Upper_Seq(C,n) by A9,A10,JORDAN4:47;
A100: L~v1 c= L~Lower_Seq(C,n) by A23,JORDAN3:77;
      L~v = L~(<*Gauge(C,n)*(i,wG)*>^(v1^<*NE*>)) by FINSEQ_1:45
        .= LSeg(Gauge(C,n)*(i,wG),(v1^<*NE*>)/.1) \/ L~(v1^<*NE*>)
      by SPPOL_2:20
        .= LSeg(Gauge(C,n)*(i,wG),(v1^<*NE*>)/.1) \/
      (L~v1 \/ LSeg(v1/.(len v1),NE)) by A24,SPPOL_2:19;
      then
A101: x in LSeg(Gauge(C,n)*(i,wG),(v1^<*NE*>)/.1) or
      x in L~v1 \/ LSeg(v1/.(len v1),NE) by A98,XBOOLE_0:def 3;
      Upper_Seq(C,n)/.1 = W-min L~Cage(C,n) by JORDAN1F:5;
      then
A102: not W-min L~Cage(C,n) in L~h by A83,JORDAN5B:16;
      now per cases by A101,XBOOLE_0:def 3;
        suppose x in LSeg(Gauge(C,n)*(i,wG),(v1^<*NE*>)/.1);
          then x in L~<*Gauge(C,n)*(i,wG),Gij*> by A32,SPPOL_2:21;
          hence L~Upper_Seq(C,n) meets L~<*Gauge(C,n)*(i,wG),Gij*>
          by A97,A99,XBOOLE_0:3;
        end;
        suppose
A103:     x in L~v1;
          then x in L~Lower_Seq(C,n) /\ L~Upper_Seq(C,n)
          by A97,A99,A100,XBOOLE_0:def 4;
          then x in {W-min L~Cage(C,n),E-max L~Cage(C,n)} by JORDAN1E:20;
          then
A104:     x = E-max L~Cage(C,n) by A97,A102,TARSKI:def 2;
          1 in dom Lower_Seq(C,n) by A11,FINSEQ_3:27;
          then Lower_Seq(C,n).1 = Lower_Seq(C,n)/.1 by PARTFUN1:def 8
            .= E-max L~Cage(C,n) by JORDAN1F:6;
          then x = Gij by A23,A103,A104,JORDAN1E:11;
          then x in LSeg(Gauge(C,n)*(i,wG),Gij) by RLTOPSP1:69;
          then x in L~<*Gauge(C,n)*(i,wG),Gij*> by SPPOL_2:21;
          hence L~Upper_Seq(C,n) meets L~<*Gauge(C,n)*(i,wG),Gij*>
          by A97,A99,XBOOLE_0:3;
        end;
        suppose
A105:     x in LSeg(v1/.(len v1),NE);
          x in L~Cage(C,n) by A6,A97,A99,XBOOLE_0:def 3;
          then x in LSeg(W-min L~Cage(C,n), NE) /\ L~Cage(C,n)
          by A29,A105,XBOOLE_0:def 4;
          then x in {W-min L~Cage(C,n)} by PSCOMP_1:92;
          hence L~Upper_Seq(C,n) meets L~<*Gauge(C,n)*(i,wG),Gij*>
          by A97,A102,TARSKI:def 1;
        end;
      end;
      then L~<*Gauge(C,n)*(i,wG),Gij*> meets L~Upper_Seq(C,n);
      hence thesis by SPPOL_2:21;
    end;
    suppose
A106: Gij in L~Lower_Seq(C,n) & Gij <> Lower_Seq(C,n).len Lower_Seq(C,n)
      & W-min L~Cage(C,n) = NE & i < lG;
      then
A107: v1 is non empty by JORDAN1E:7;
      then
A108: 0+1 <= len v1 by NAT_1:13;
      then
A109: 1 in dom v1 by FINSEQ_3:27;
      set v = Gv1;
A110: len v1 in dom v1 by A108,FINSEQ_3:27;
A111: len Lower_Seq(C,n) in dom Lower_Seq(C,n) by A11,FINSEQ_3:27;
      v1/.(len v1) = v1.(len v1) by A110,PARTFUN1:def 8
        .= Lower_Seq(C,n).len Lower_Seq(C,n) by A106,JORDAN1B:5
        .= Lower_Seq(C,n)/.len Lower_Seq(C,n) by A111,PARTFUN1:def 8
        .= W-min L~Cage(C,n) by JORDAN1F:8;
      then
A112: Gv1/.len Gv1 = W-min L~Cage(C,n) by A107,SPRECT_3:11;
A113: v1/.1 = v1.1 by A109,PARTFUN1:def 8
        .= Gij by A106,JORDAN3:58;
      1+len v1 >= 1+1 by A108,XREAL_1:9;
      then 2 <= len v by FINSEQ_5:8;
      then
A114: 2 <= len Rev v by FINSEQ_5:def 3;
A115: not Gauge(C,n)*(i,wG) in L~Lower_Seq(C,n) by A1,A106,JORDAN1G:53;
      rng Lower_Seq(C,n) c= L~Lower_Seq(C,n) by A8,SPPOL_2:18,XXREAL_0:2;
      then
A116: not Gauge(C,n)*(i,wG) in rng Lower_Seq(C,n) by A1,A106,JORDAN1G:53;
      not Gauge(C,n)*(i,wG) in {Gij} by A106,A115,TARSKI:def 1;
      then
A117: not Gauge(C,n)*(i,wG) in rng <*Gij*> by FINSEQ_1:55;
      set ci = mid(Lower_Seq(C,n),Index(Gij,Lower_Seq(C,n))+1,
      len Lower_Seq(C,n));
      now per cases;
        suppose
A118:     Gij <> Lower_Seq(C,n).(Index(Gij,Lower_Seq(C,n))+1);
          rng ci c= rng Lower_Seq(C,n) by FINSEQ_6:125;
          then not Gauge(C,n)*(i,wG) in rng ci by A116;
          then not Gauge(C,n)*(i,wG) in rng <*Gij*> \/ rng ci
          by A117,XBOOLE_0:def 3;
          then not Gauge(C,n)*(i,wG) in rng(<*Gij*>^ci) by FINSEQ_1:44;
          hence not Gauge(C,n)*(i,wG) in rng v1 by A118,JORDAN3:def 4;
        end;
        suppose Gij = Lower_Seq(C,n).(Index(Gij,Lower_Seq(C,n))+1);
          then v1 = ci by JORDAN3:def 4;
          then rng v1 c= rng Lower_Seq(C,n) by FINSEQ_6:125;
          hence not Gauge(C,n)*(i,wG) in rng v1 by A116;
        end;
      end;
      then {Gauge(C,n)*(i,wG)} misses rng v1 by ZFMISC_1:56;
      then
A119: rng <*Gauge(C,n)*(i,wG)*> misses rng v1 by FINSEQ_1:55;
A120: <*Gauge(C,n)*(i,wG)*> is one-to-one by FINSEQ_3:102;
A121: v1 is being_S-Seq by A106,JORDAN3:69;
      then
A122: Gv1 is one-to-one by A119,A120,FINSEQ_3:98;
A123: <*Gauge(C,n)*(i,wG)*> is special;
      (<*Gauge(C,n)*(i,wG)*>/.len <*Gauge(C,n)*(i,wG)*>)`1 =
      (<*Gauge(C,n)*(i,wG)*>/.1)`1 by FINSEQ_1:56
        .= Gauge(C,n)*(i,wG)`1 by FINSEQ_4:25
        .= Gauge(C,n)*(i,1)`1 by A1,A2,A13,GOBOARD5:3
        .= (v1/.1)`1 by A1,A2,A3,A4,A113,GOBOARD5:3;
      then Gv1 is special by A121,GOBOARD2:13;
      then
A124: Rev v is special by SPPOL_2:42;
A125: len Upper_Seq(C,n) >= 2+1 by JORDAN1E:19;
      then
A126: len Upper_Seq(C,n) > 2 by NAT_1:13;
      len Upper_Seq(C,n) > 1 by A125,XXREAL_0:2;
      then
A127: h is S-Sequence_in_R2 by A126,JORDAN3:39;
      then
A128: 2 <= len h by TOPREAL1:def 10;
      3 <= len Upper_Seq(C,n) by JORDAN1E:19;
      then 2 <= len Upper_Seq(C,n) by XXREAL_0:2;
      then
A129: 2 in dom Upper_Seq(C,n) by FINSEQ_3:27;
A130: len Upper_Seq(C,n) in dom Upper_Seq(C,n) by FINSEQ_5:6;
      then
A131: h is_in_the_area_of Cage(C,n) by A129,JORDAN1E:21,SPRECT_2:26;
      Upper_Seq(C,n)/.(len Upper_Seq(C,n)) = E-max L~Cage(C,n) by JORDAN1F:7;
      then (Upper_Seq(C,n)/.len Upper_Seq(C,n))`1 = E-bound L~Cage(C,n)
      by EUCLID:56;
      then
A132: (h/.len h)`1 = E-bound L~Cage(C,n) by A129,A130,SPRECT_2:13;
      (Upper_Seq(C,n)/.(1+1))`1 = W-bound L~Cage(C,n) by JORDAN1G:39;
      then (h/.1)`1 = W-bound L~Cage(C,n) by A129,A130,SPRECT_2:12;
      then
A133: h is_a_h.c._for Cage(C,n) by A131,A132,SPRECT_2:def 2;
      now
        let m be Element of NAT;
        assume
A134:   m in dom <*Gauge(C,n)*(i,wG)*>;
        then m in Seg 1 by FINSEQ_1:55;
        then m = 1 by FINSEQ_1:4,TARSKI:def 1;
        then <*Gauge(C,n)*(i,wG)*>.m = Gauge(C,n)*(i,wG) by FINSEQ_1:57;
        then
A135:   <*Gauge(C,n)*(i,wG)*>/.m = Gauge(C,n)*(i,wG) by A134,PARTFUN1:def 8;
        Gauge(C,n)*(1,wG)`1 <= Gauge(C,n)*(i,wG)`1 by A1,A2,A13,SPRECT_3:25;
        hence W-bound L~Cage(C,n) <= (<*Gauge(C,n)*(i,wG)*>/.m)`1
        by A12,A13,A135,JORDAN1A:94;
        (Gauge(C,n)*(i,wG))`1 <= Gauge(C,n)*(len Gauge(C,n),wG)`1
        by A1,A2,A13,SPRECT_3:25;
        hence (<*Gauge(C,n)*(i,wG)*>/.m)`1 <= E-bound L~Cage(C,n)
        by A12,A13,A135,JORDAN1A:92;
        (<*Gauge(C,n)*(i,wG)*>/.m)`2 = N-bound L~Cage(C,n)
        by A1,A2,A12,A135,JORDAN1A:91;
        hence S-bound L~Cage(C,n) <= (<*Gauge(C,n)*(i,wG)*>/.m)`2
        by SPRECT_1:24;
        thus (<*Gauge(C,n)*(i,wG)*>/.m)`2 <= N-bound L~Cage(C,n)
        by A1,A2,A12,A135,JORDAN1A:91;
      end;
      then
A136: <*Gauge(C,n)*(i,wG)*> is_in_the_area_of Cage(C,n) by SPRECT_2:def 1;
      <*Gij*> is_in_the_area_of Cage(C,n) by A106,JORDAN1E:22,SPRECT_3:63;
      then v1 is_in_the_area_of Cage(C,n) by A106,JORDAN1E:22,SPRECT_3:73;
      then Gv1 is_in_the_area_of Cage(C,n) by A136,SPRECT_2:28;
      then
A137: Rev v is_in_the_area_of Cage(C,n) by SPRECT_3:68;
      v/.1 = Gauge(C,n)*(i,wG) by FINSEQ_5:16;
      then (v/.1)`2 = N-bound L~Cage(C,n) by A1,A2,A12,JORDAN1A:91;
      then (Rev v/.(len v))`2 = N-bound L~Cage(C,n) by FINSEQ_5:68;
      then
A138: (Rev v/.(len Rev v))`2 = N-bound L~Cage(C,n) by FINSEQ_5:def 3;
      (v/.len v)`2 = S-bound L~Cage(C,n) by A106,A112,EUCLID:56;
      then (Rev v/.1)`2 = S-bound L~Cage(C,n) by FINSEQ_5:68;
      then Rev v is_a_v.c._for Cage(C,n) by A137,A138,SPRECT_2:def 3;
      then L~h meets L~Rev v by A114,A122,A124,A127,A128,A133,SPRECT_2:33;
      then L~h meets L~v by SPPOL_2:22;
      then consider x be set such that
A139: x in L~h and
A140: x in L~v by XBOOLE_0:3;
A141: L~h c= L~Upper_Seq(C,n) by A9,A10,JORDAN4:47;
A142: L~v1 c= L~Lower_Seq(C,n) by A106,JORDAN3:77;
A143: L~v = LSeg(Gauge(C,n)*(i,wG),v1/.1) \/ L~v1 by A107,SPPOL_2:20;
      Upper_Seq(C,n)/.1 = W-min L~Cage(C,n) by JORDAN1F:5;
      then
A144: not W-min L~Cage(C,n) in L~h by A126,JORDAN5B:16;
      now per cases by A140,A143,XBOOLE_0:def 3;
        suppose x in LSeg(Gauge(C,n)*(i,wG),v1/.1);
          then x in L~<*Gauge(C,n)*(i,wG),Gij*> by A113,SPPOL_2:21;
          hence L~Upper_Seq(C,n) meets L~<*Gauge(C,n)*(i,wG),Gij*>
          by A139,A141,XBOOLE_0:3;
        end;
        suppose
A145:     x in L~v1;
          then x in L~Lower_Seq(C,n) /\ L~Upper_Seq(C,n)
          by A139,A141,A142,XBOOLE_0:def 4;
          then x in {W-min L~Cage(C,n),E-max L~Cage(C,n)} by JORDAN1E:20;
          then
A146:     x = E-max L~Cage(C,n) by A139,A144,TARSKI:def 2;
          1 in dom Lower_Seq(C,n) by A11,FINSEQ_3:27;
          then Lower_Seq(C,n).1 = Lower_Seq(C,n)/.1 by PARTFUN1:def 8
            .= E-max L~Cage(C,n) by JORDAN1F:6;
          then x = Gij by A106,A145,A146,JORDAN1E:11;
          then x in LSeg(Gauge(C,n)*(i,wG),Gij) by RLTOPSP1:69;
          then x in L~<*Gauge(C,n)*(i,wG),Gij*> by SPPOL_2:21;
          hence L~Upper_Seq(C,n) meets L~<*Gauge(C,n)*(i,wG),Gij*>
          by A139,A141,XBOOLE_0:3;
        end;
      end;
      then L~<*Gauge(C,n)*(i,wG),Gij*> meets L~Upper_Seq(C,n);
      hence thesis by SPPOL_2:21;
    end;
    suppose
A147: Gij in L~Upper_Seq(C,n);
      Gij in LSeg(Gauge(C,n)*(i,wG),Gij) by RLTOPSP1:69;
      hence thesis by A147,XBOOLE_0:3;
    end;
    suppose
A148: Gij in L~Lower_Seq(C,n) & Gij = Lower_Seq(C,n).len Lower_Seq(C,n);
      len Lower_Seq(C,n) in dom Lower_Seq(C,n) by A11,FINSEQ_3:27;
      then
A149: Lower_Seq(C,n).len Lower_Seq(C,n) =
      Lower_Seq(C,n)/.len Lower_Seq(C,n) by PARTFUN1:def 8
        .= W-min L~Cage(C,n) by JORDAN1F:8;
A150: rng Upper_Seq(C,n) c= L~Upper_Seq(C,n) by A7,SPPOL_2:18,XXREAL_0:2;
A151: W-min L~Cage(C,n) in rng Upper_Seq(C,n) by JORDAN1J:5;
      Gij in LSeg(Gauge(C,n)*(i,wG),Gij) by RLTOPSP1:69;
      hence thesis by A148,A149,A150,A151,XBOOLE_0:3;
    end;
  end;
  hence thesis;
end;

theorem Th4:
  for C be compact connected non vertical non horizontal Subset of TOP-REAL 2
  for n be Element of NAT st n > 0
  for i,j be Element of NAT st 1 <= i & i <= len Gauge(C,n) &
  1 <= j & j <= width Gauge(C,n) & Gauge(C,n)*(i,j) in L~Cage(C,n) holds
  LSeg(Gauge(C,n)*(i,width Gauge(C,n)),Gauge(C,n)*(i,j)) meets
  Upper_Arc L~Cage(C,n)
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
  let n be Element of NAT;
  assume
A1: n > 0;
  let i,j be Element of NAT;
  assume that
A2: 1 <= i and
A3: i <= len Gauge(C,n) and
A4: 1 <= j and
A5: j <= width Gauge(C,n) and
A6: Gauge(C,n)*(i,j) in L~Cage(C,n);
  L~Upper_Seq(C,n) = Upper_Arc L~Cage(C,n) by A1,JORDAN1G:63;
  hence thesis by A2,A3,A4,A5,A6,Th3;
end;

theorem
  for C be compact connected non vertical non horizontal Subset of TOP-REAL 2
  for j be Element of NAT holds
  Gauge(C,n+1)*(Center Gauge(C,n+1),j) in Lower_Arc L~Cage(C,n+1) &
  1 <= j & j <= width Gauge(C,n+1) implies
  LSeg(Gauge(C,1)*(Center Gauge(C,1),width Gauge(C,1)),
  Gauge(C,n+1)*(Center Gauge(C,n+1),j)) meets Upper_Arc L~Cage(C,n+1)
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
  let j be Element of NAT;
  assume that
A1: Gauge(C,n+1)*(Center Gauge(C,n+1),j) in Lower_Arc L~Cage(C,n+1) and
A2: 1 <= j and
A3: j <= width Gauge(C,n+1);
  set in1 = Center Gauge(C,n+1);
A4: n+1 >= 0+1 by NAT_1:11;
A5: 1 <= in1 by JORDAN1B:12;
A6: in1 <= len Gauge(C,n+1) by JORDAN1B:14;
A7: LSeg(Gauge(C,n+1)*(Center Gauge(C,n+1),width Gauge(C,n+1)),
  Gauge(C,n+1)*(Center Gauge(C,n+1),j)) c=
  LSeg(Gauge(C,1)*(Center Gauge(C,1),width Gauge(C,1)),
  Gauge(C,n+1)*(Center Gauge(C,n+1),j)) by A2,A3,A4,Th2;
  Lower_Arc L~Cage(C,n+1) c= L~Cage(C,n+1) by JORDAN6:76;
  hence thesis by A1,A2,A3,A5,A6,A7,Th4,XBOOLE_1:63;
end;

theorem Th6:
  for C be compact connected non vertical non horizontal Subset of TOP-REAL 2
  for f be FinSequence of TOP-REAL 2
  for k be Element of NAT st 1 <= k & k+1 <= len f &
  f is_sequence_on Gauge(C,n) holds
  dist(f/.k,f/.(k+1)) = (N-bound C - S-bound C)/2|^n or
  dist(f/.k,f/.(k+1)) = (E-bound C - W-bound C)/2|^n
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
  let f be FinSequence of TOP-REAL 2;
  let k be Element of NAT;
  assume that
A1: 1 <= k and
A2: k+1 <= len f;
  assume f is_sequence_on Gauge(C,n);
  then consider i1,j1,i2,j2 be Element of NAT such that
A3: [i1,j1] in Indices Gauge(C,n) and
A4: f/.k = Gauge(C,n)*(i1,j1) and
A5: [i2,j2] in Indices Gauge(C,n) and
A6: f/.(k+1) = Gauge(C,n)*(i2,j2) and
A7: i1 = i2 & j1+1 = j2 or i1+1 = i2 & j1 = j2 or
  i1 = i2+1 & j1 = j2 or i1 = i2 & j1 = j2+1 by A1,A2,JORDAN8:6;
  per cases by A7;
  suppose i1 = i2 & j1+1 = j2;
    hence thesis by A3,A4,A5,A6,GOBRD14:19;
  end;
  suppose i1+1 = i2 & j1 = j2;
    hence thesis by A3,A4,A5,A6,GOBRD14:20;
  end;
  suppose i1 = i2+1 & j1 = j2;
    hence thesis by A3,A4,A5,A6,GOBRD14:20;
  end;
  suppose i1 = i2 & j1 = j2+1;
    hence thesis by A3,A4,A5,A6,GOBRD14:19;
  end;
end;

theorem
  for M be symmetric triangle MetrStruct for r be real number
  for p,q,x be Element of M st p in Ball(x,r) & q in Ball(x,r) holds
  dist(p,q) < 2*r
proof
  let M be symmetric triangle MetrStruct;
  let r be real number;
  let p,q,x be Element of M;
  assume that
A1: p in Ball(x,r) and
A2: q in Ball(x,r);
A3: dist(p,x) < r by A1,METRIC_1:12;
A4: dist(x,q) < r by A2,METRIC_1:12;
A5: dist(p,q) <= dist(p,x) + dist(x,q) by METRIC_1:4;
  dist(p,x) + dist(x,q) < r+r by A3,A4,XREAL_1:10;
  hence thesis by A5,XXREAL_0:2;
end;

canceled;

theorem
  for C be compact connected non vertical non horizontal
  Subset of TOP-REAL 2 holds N-bound C < N-bound L~Cage(C,n)
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
A1: 2|^n > 0 by NEWTON:102;
  N-bound C > S-bound C + 0 by SPRECT_1:34;
  then N-bound C - S-bound C > 0 by XREAL_1:22;
  then
A2: (N-bound C - S-bound C)/(2|^n) > N-bound C - N-bound C by A1,XREAL_1:141;
  N-bound L~Cage(C,n) = N-bound C + (N-bound C - S-bound C)/(2|^n)
  by JORDAN10:6;
  hence thesis by A2,XREAL_1:21;
end;

theorem Th10:
  for C be compact connected non vertical non horizontal
  Subset of TOP-REAL 2 holds E-bound C < E-bound L~Cage(C,n)
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
A1: 2|^n > 0 by NEWTON:102;
  E-bound C > W-bound C + 0 by SPRECT_1:33;
  then E-bound C - W-bound C > 0 by XREAL_1:22;
  then
A2: (E-bound C - W-bound C)/(2|^n) > E-bound C - E-bound C by A1,XREAL_1:141;
  E-bound L~Cage(C,n) = E-bound C + (E-bound C - W-bound C)/(2|^n)
  by JORDAN1A:85;
  hence thesis by A2,XREAL_1:21;
end;

theorem
  for C be compact connected non vertical non horizontal
  Subset of TOP-REAL 2 holds S-bound L~Cage(C,n) < S-bound C
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
A1: 2|^n > 0 by NEWTON:102;
  N-bound C > S-bound C + 0 by SPRECT_1:34;
  then N-bound C - S-bound C > 0 by XREAL_1:22;
  then
A2: (N-bound C - S-bound C)/(2|^n) > S-bound C - S-bound C by A1,XREAL_1:141;
  S-bound L~Cage(C,n) = S-bound C - (N-bound C - S-bound C)/(2|^n)
  by JORDAN1A:84;
  hence thesis by A2,XREAL_1:13;
end;

theorem Th12:
  for C be compact connected non vertical non horizontal
  Subset of TOP-REAL 2 holds W-bound L~Cage(C,n) < W-bound C
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
A1: 2|^n > 0 by NEWTON:102;
  E-bound C > W-bound C + 0 by SPRECT_1:33;
  then E-bound C - W-bound C > 0 by XREAL_1:22;
  then
A2: (E-bound C - W-bound C)/(2|^n) > W-bound C - W-bound C by A1,XREAL_1:141;
  W-bound L~Cage(C,n) = W-bound C - (E-bound C - W-bound C)/(2|^n)
  by JORDAN1A:83;
  hence thesis by A2,XREAL_1:13;
end;

theorem Th13:
  for C be Simple_closed_curve for i,j,k be Element of NAT st
  1 < i & i < len Gauge(C,n) & 1 <= k & k <= j & j <= width Gauge(C,n) &
  LSeg(Gauge(C,n)*(i,k),Gauge(C,n)*(i,j)) /\ L~Upper_Seq(C,n) =
  {Gauge(C,n)*(i,k)} &
  LSeg(Gauge(C,n)*(i,k),Gauge(C,n)*(i,j)) /\ L~Lower_Seq(C,n) =
  {Gauge(C,n)*(i,j)} holds
  LSeg(Gauge(C,n)*(i,k),Gauge(C,n)*(i,j)) meets Upper_Arc C
proof
  let C be Simple_closed_curve;
  let i,j,k be Element of NAT;
  set Ga = Gauge(C,n);
  set US = Upper_Seq(C,n);
  set LS = Lower_Seq(C,n);
  set UA = Upper_Arc C;
  set Wmin = W-min L~Cage(C,n);
  set Emax = E-max L~Cage(C,n);
  set Wbo = W-bound L~Cage(C,n);
  set Ebo = E-bound L~Cage(C,n);
  set Gik = Ga*(i,k);
  set Gij = Ga*(i,j);
  assume that
A1: 1 < i and
A2: i < len Ga and
A3: 1 <= k and
A4: k <= j and
A5: j <= width Ga and
A6: LSeg(Gik,Gij) /\ L~US = {Gik} and
A7: LSeg(Gik,Gij) /\ L~LS = {Gij} and
A8: LSeg(Gik,Gij) misses UA;
  Gij in {Gij} by TARSKI:def 1;
  then
A9: Gij in L~LS by A7,XBOOLE_0:def 4;
  Gik in {Gik} by TARSKI:def 1;
  then
A10: Gik in L~US by A6,XBOOLE_0:def 4;
  then
A11: j <> k by A1,A2,A3,A5,A9,JORDAN1J:57;
A12: 1 <= j by A3,A4,XXREAL_0:2;
A13: k <= width Ga by A4,A5,XXREAL_0:2;
A14: [i,j] in Indices Ga by A1,A2,A5,A12,MATRIX_1:37;
A15: [i,k] in Indices Ga by A1,A2,A3,A13,MATRIX_1:37;
  set do = L_Cut(LS,Gij);
  set go = R_Cut(US,Gik);
A16: len Ga = width Ga by JORDAN8:def 1;
A17: len US >= 3 by JORDAN1E:19;
  then len US >= 1 by XXREAL_0:2;
  then 1 in dom US by FINSEQ_3:27;
  then
A18: US.1 = US/.1 by PARTFUN1:def 8
    .= Wmin by JORDAN1F:5;
A19: Wmin`1 = Wbo by EUCLID:56
    .= Ga*(1,k)`1 by A3,A13,A16,JORDAN1A:94;
  len Ga >= 4 by JORDAN8:13;
  then
A20: len Ga >= 1 by XXREAL_0:2;
  then
A21: [1,k] in Indices Ga by A3,A13,MATRIX_1:37;
  then
A22: Gik <> US.1 by A1,A15,A18,A19,JORDAN1G:7;
  then reconsider go as being_S-Seq FinSequence of TOP-REAL 2
  by A10,JORDAN3:70;
A23: len LS >= 1+2 by JORDAN1E:19;
  then
A24: len LS >= 1 by XXREAL_0:2;
  then
A25: 1 in dom LS by FINSEQ_3:27;
  len LS in dom LS by A24,FINSEQ_3:27;
  then
A26: LS.len LS = LS/.len LS by PARTFUN1:def 8
    .= Wmin by JORDAN1F:8;
A27: Wmin`1 = Wbo by EUCLID:56
    .= Ga*(1,k)`1 by A3,A13,A16,JORDAN1A:94;
A28: [i,j] in Indices Ga by A1,A2,A5,A12,MATRIX_1:37;
  then
A29: Gij <> LS.len LS by A1,A21,A26,A27,JORDAN1G:7;
  then reconsider do as being_S-Seq FinSequence of TOP-REAL 2
  by A9,JORDAN3:69;
A30: [len Ga,k] in Indices Ga by A3,A13,A20,MATRIX_1:37;
A31: LS.1 = LS/.1 by A25,PARTFUN1:def 8
    .= Emax by JORDAN1F:6;
  Emax`1 = Ebo by EUCLID:56
    .= Ga*(len Ga,k)`1 by A3,A13,A16,JORDAN1A:92;
  then
A32: Gij <> LS.1 by A2,A28,A30,A31,JORDAN1G:7;
A33: len go >= 1+1 by TOPREAL1:def 10;
A34: Gik in rng US by A1,A2,A3,A10,A13,JORDAN1G:4,JORDAN1J:40;
  then
A35: go is_sequence_on Ga by JORDAN1G:4,JORDAN1J:38;
A36: len do >= 1+1 by TOPREAL1:def 10;
A37: Gij in rng LS by A1,A2,A5,A9,A12,JORDAN1G:5,JORDAN1J:40;
  then
A38: do is_sequence_on Ga by JORDAN1G:5,JORDAN1J:39;
  reconsider go as non constant s.c.c.
  (being_S-Seq FinSequence of TOP-REAL 2) by A33,A35,JGRAPH_1:16,JORDAN8:8;
  reconsider do as non constant s.c.c.
  (being_S-Seq FinSequence of TOP-REAL 2) by A36,A38,JGRAPH_1:16,JORDAN8:8;
A39: len go > 1 by A33,NAT_1:13;
  then
A40: len go in dom go by FINSEQ_3:27;
  then
A41: go/.len go = go.len go by PARTFUN1:def 8
    .= Gik by A10,JORDAN3:59;
  len do >= 1 by A36,XXREAL_0:2;
  then 1 in dom do by FINSEQ_3:27;
  then
A42: do/.1 = do.1 by PARTFUN1:def 8
    .= Gij by A9,JORDAN3:58;
  reconsider m = len go - 1 as Element of NAT by A40,FINSEQ_3:28;
A43: m+1 = len go;
  then
A44: len go-'1 = m by NAT_D:34;
A45: LSeg(go,m) c= L~go by TOPREAL3:26;
A46: L~go c= L~US by A10,JORDAN3:76;
  then LSeg(go,m) c= L~US by A45,XBOOLE_1:1;
  then
A47: LSeg(go,m) /\ LSeg(Gik,Gij) c= {Gik} by A6,XBOOLE_1:26;
  m >= 1 by A33,XREAL_1:21;
  then
A48: LSeg(go,m) = LSeg(go/.m,Gik) by A41,A43,TOPREAL1:def 5;
  {Gik} c= LSeg(go,m) /\ LSeg(Gik,Gij)
  proof
    let x be set;
    assume x in {Gik};
    then
A49: x = Gik by TARSKI:def 1;
A50: Gik in LSeg(go,m) by A48,RLTOPSP1:69;
    Gik in LSeg(Gik,Gij) by RLTOPSP1:69;
    hence thesis by A49,A50,XBOOLE_0:def 4;
  end;
  then
A51: LSeg(go,m) /\ LSeg(Gik,Gij) = {Gik} by A47,XBOOLE_0:def 10;
A52: LSeg(do,1) c= L~do by TOPREAL3:26;
A53: L~do c= L~LS by A9,JORDAN3:77;
  then LSeg(do,1) c= L~LS by A52,XBOOLE_1:1;
  then
A54: LSeg(do,1) /\ LSeg(Gik,Gij) c= {Gij} by A7,XBOOLE_1:26;
A55: LSeg(do,1) = LSeg(Gij,do/.(1+1)) by A36,A42,TOPREAL1:def 5;
  {Gij} c= LSeg(do,1) /\ LSeg(Gik,Gij)
  proof
    let x be set;
    assume x in {Gij};
    then
A56: x = Gij by TARSKI:def 1;
A57: Gij in LSeg(do,1) by A55,RLTOPSP1:69;
    Gij in LSeg(Gik,Gij) by RLTOPSP1:69;
    hence thesis by A56,A57,XBOOLE_0:def 4;
  end;
  then
A58: LSeg(Gik,Gij) /\ LSeg(do,1) = {Gij} by A54,XBOOLE_0:def 10;
A59: go/.1 = US/.1 by A10,SPRECT_3:39
    .= Wmin by JORDAN1F:5;
  then
A60: go/.1 = LS/.len LS by JORDAN1F:8
    .= do/.len do by A9,JORDAN1J:35;
A61: rng go c= L~go by A33,SPPOL_2:18;
A62: rng do c= L~do by A36,SPPOL_2:18;
A63: {go/.1} c= L~go /\ L~do
  proof
    let x be set;
    assume x in {go/.1};
    then
A64: x = go/.1 by TARSKI:def 1;
    then
A65: x in rng go by FINSEQ_6:46;
    x in rng do by A60,A64,REVROT_1:3;
    hence thesis by A61,A62,A65,XBOOLE_0:def 4;
  end;
A66: LS.1 = LS/.1 by A25,PARTFUN1:def 8
    .= Emax by JORDAN1F:6;
A67: [len Ga,j] in Indices Ga by A5,A12,A20,MATRIX_1:37;
  L~go /\ L~do c= {go/.1}
  proof
    let x be set;
    assume
A68: x in L~go /\ L~do;
    then
A69: x in L~go by XBOOLE_0:def 4;
A70: x in L~do by A68,XBOOLE_0:def 4;
    then x in L~US /\ L~LS by A46,A53,A69,XBOOLE_0:def 4;
    then x in {Wmin,Emax} by JORDAN1E:20;
    then
A71: x = Wmin or x = Emax by TARSKI:def 2;
    now
      assume x = Emax;
      then
A72:  Emax = Gij by A9,A66,A70,JORDAN1E:11;
      Ga*(len Ga,j)`1 = Ebo by A5,A12,A16,JORDAN1A:92;
      then Emax`1 <> Ebo by A2,A14,A67,A72,JORDAN1G:7;
      hence contradiction by EUCLID:56;
    end;
    hence thesis by A59,A71,TARSKI:def 1;
  end;
  then
A73: L~go /\ L~do = {go/.1} by A63,XBOOLE_0:def 10;
  set W2 = go/.2;
A74: 2 in dom go by A33,FINSEQ_3:27;
A75: now
    assume Gik`1 = Wbo;
    then Ga*(1,k)`1 = Ga*(i,k)`1 by A3,A13,A16,JORDAN1A:94;
    hence contradiction by A1,A15,A21,JORDAN1G:7;
  end;
  go = mid(US,1,Gik..US) by A34,JORDAN1G:57
    .= US|(Gik..US) by A34,FINSEQ_4:31,FINSEQ_6:122;
  then
A76: W2 = US/.2 by A74,FINSEQ_4:85;
A77: Wmin in rng go by A59,FINSEQ_6:46;
  set pion = <*Gik,Gij*>;
A78: now
    let n be Element of NAT;
    assume n in dom pion;
    then n in Seg 2 by FINSEQ_3:29;
    then n = 1 or n = 2 by FINSEQ_1:4,TARSKI:def 2;
    hence
    ex i,j be Element of NAT st [i,j] in Indices Ga & pion/.n = Ga*(i,j)
    by A14,A15,FINSEQ_4:26;
  end;
A79: Gik <> Gij by A11,A14,A15,GOBOARD1:21;
A80: Gik`1 = Ga*(i,1)`1 by A1,A2,A3,A13,GOBOARD5:3
    .= Gij`1 by A1,A2,A5,A12,GOBOARD5:3;
  then LSeg(Gik,Gij) is vertical by SPPOL_1:37;
  then pion is being_S-Seq by A79,JORDAN1B:8;
  then consider pion1 be FinSequence of TOP-REAL 2 such that
A81: pion1 is_sequence_on Ga and
A82: pion1 is being_S-Seq and
A83: L~pion = L~pion1 and
A84: pion/.1 = pion1/.1 and
A85: pion/.len pion = pion1/.len pion1 and
A86: len pion <= len pion1 by A78,GOBOARD3:2;
  reconsider pion1 as being_S-Seq FinSequence of TOP-REAL 2 by A82;
  set godo = go^'pion1^'do;
A87: 1+1 <= len Cage(C,n) by GOBOARD7:36,XXREAL_0:2;
A88: 1+1 <= len Rotate(Cage(C,n),Wmin) by GOBOARD7:36,XXREAL_0:2;
  len (go^'pion1) >= len go by TOPREAL8:7;
  then
A89: len (go^'pion1) >= 1+1 by A33,XXREAL_0:2;
  then
A90: len (go^'pion1) > 1+0 by NAT_1:13;
A91: len godo >= len (go^'pion1) by TOPREAL8:7;
  then
A92: 1+1 <= len godo by A89,XXREAL_0:2;
A93: US is_sequence_on Ga by JORDAN1G:4;
A94: go/.len go = pion1/.1 by A41,A84,FINSEQ_4:26;
  then
A95: go^'pion1 is_sequence_on Ga by A35,A81,TOPREAL8:12;
A96: (go^'pion1)/.len (go^'pion1) = pion/.len pion by A85,GRAPH_2:58
    .= pion/.2 by FINSEQ_1:61
    .= do/.1 by A42,FINSEQ_4:26;
  then
A97: godo is_sequence_on Ga by A38,A95,TOPREAL8:12;
  LSeg(pion1,1) c= L~<*Gik,Gij*> by A83,TOPREAL3:26;
  then LSeg(pion1,1) c= LSeg(Gik,Gij) by SPPOL_2:21;
  then
A98: LSeg(go,len go-'1) /\ LSeg(pion1,1) c= {Gik} by A44,A51,XBOOLE_1:27;
A99: len pion1 >= 1+1 by A86,FINSEQ_1:61;
  {Gik} c= LSeg(go,m) /\ LSeg(pion1,1)
  proof
    let x be set;
    assume x in {Gik};
    then
A100: x = Gik by TARSKI:def 1;
A101: Gik in LSeg(go,m) by A48,RLTOPSP1:69;
    Gik in LSeg(pion1,1) by A41,A94,A99,TOPREAL1:27;
    hence thesis by A100,A101,XBOOLE_0:def 4;
  end;
  then LSeg(go,len go-'1) /\ LSeg(pion1,1) = { go/.len go }
  by A41,A44,A98,XBOOLE_0:def 10;
  then
A102: go^'pion1 is unfolded by A94,TOPREAL8:34;
  len pion1 >= 2+0 by A86,FINSEQ_1:61;
  then
A103: len pion1-2 >= 0 by XREAL_1:21;
  len (go^'pion1)+1-1 = len go+len pion1-1 by GRAPH_2:13;
  then len (go^'pion1)-1 = len go + (len pion1-2)
    .= len go + (len pion1-'2) by A103,XREAL_0:def 2;
  then
A104: len (go^'pion1)-'1 = len go + (len pion1-'2) by XREAL_0:def 2;
A105: len pion1-1 >= 1 by A99,XREAL_1:21;
  then
A106: len pion1-'1 = len pion1-1 by XREAL_0:def 2;
A107: len pion1-'2+1 = len pion1-2+1 by A103,XREAL_0:def 2
    .= len pion1-'1 by A105,XREAL_0:def 2;
  len pion1-1+1 <= len pion1;
  then
A108: len pion1-'1 < len pion1 by A106,NAT_1:13;
  LSeg(pion1,len pion1-'1) c= L~<*Gik,Gij*> by A83,TOPREAL3:26;
  then LSeg(pion1,len pion1-'1) c= LSeg(Gik,Gij) by SPPOL_2:21;
  then
A109: LSeg(pion1,len pion1-'1) /\ LSeg(do,1) c= {Gij} by A58,XBOOLE_1:27;
  {Gij} c= LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
  proof
    let x be set;
    assume x in {Gij};
    then
A110: x = Gij by TARSKI:def 1;
A111: Gij in LSeg(do,1) by A55,RLTOPSP1:69;
    pion1/.(len pion1-'1+1) = pion/.2 by A85,A106,FINSEQ_1:61
      .= Gij by FINSEQ_4:26;
    then Gij in LSeg(pion1,len pion1-'1) by A105,A106,TOPREAL1:27;
    hence thesis by A110,A111,XBOOLE_0:def 4;
  end;
  then LSeg(pion1,len pion1-'1) /\ LSeg(do,1) = {Gij} by A109,XBOOLE_0:def 10;
  then
A112: LSeg(go^'pion1,len go+(len pion1-'2)) /\ LSeg(do,1) =
  {(go^'pion1)/.len (go^'pion1)} by A42,A94,A96,A107,A108,TOPREAL8:31;
A113: (go^'pion1) is non trivial by A89,REALSET1:13;
A114: rng pion1 c= L~pion1 by A99,SPPOL_2:18;
A115: {pion1/.1} c= L~go /\ L~pion1
  proof
    let x be set;
    assume x in {pion1/.1};
    then
A116: x = pion1/.1 by TARSKI:def 1;
    then
A117: x in rng go by A94,REVROT_1:3;
    x in rng pion1 by A116,FINSEQ_6:46;
    hence thesis by A61,A114,A117,XBOOLE_0:def 4;
  end;
  L~go /\ L~pion1 c= {pion1/.1}
  proof
    let x be set;
    assume
A118: x in L~go /\ L~pion1;
    then
A119: x in L~go by XBOOLE_0:def 4;
    x in L~pion1 by A118,XBOOLE_0:def 4;
    then x in L~pion1 /\ L~US by A46,A119,XBOOLE_0:def 4;
    hence thesis by A6,A41,A83,A94,SPPOL_2:21;
  end;
  then
A120: L~go /\ L~pion1 = {pion1/.1} by A115,XBOOLE_0:def 10;
  then
A121: (go^'pion1) is s.n.c. by A94,JORDAN1J:54;
  rng go /\ rng pion1 c= {pion1/.1} by A61,A114,A120,XBOOLE_1:27;
  then
A122: go^'pion1 is one-to-one by JORDAN1J:55;
A123: pion/.len pion = pion/.2 by FINSEQ_1:61
    .= do/.1 by A42,FINSEQ_4:26;
A124: {pion1/.len pion1} c= L~do /\ L~pion1
  proof
    let x be set;
    assume x in {pion1/.len pion1};
    then
A125: x = pion1/.len pion1 by TARSKI:def 1;
    then
A126: x in rng do by A85,A123,FINSEQ_6:46;
    x in rng pion1 by A125,REVROT_1:3;
    hence thesis by A62,A114,A126,XBOOLE_0:def 4;
  end;
  L~do /\ L~pion1 c= {pion1/.len pion1}
  proof
    let x be set;
    assume
A127: x in L~do /\ L~pion1;
    then
A128: x in L~do by XBOOLE_0:def 4;
    x in L~pion1 by A127,XBOOLE_0:def 4;
    then x in L~pion1 /\ L~LS by A53,A128,XBOOLE_0:def 4;
    hence thesis by A7,A42,A83,A85,A123,SPPOL_2:21;
  end;
  then
A129: L~do /\ L~pion1 = {pion1/.len pion1} by A124,XBOOLE_0:def 10;
A130: L~(go^'pion1) /\ L~do = (L~go \/ L~pion1) /\ L~do by A94,TOPREAL8:35
    .= {go/.1} \/ {do/.1} by A73,A85,A123,A129,XBOOLE_1:23
    .= {(go^'pion1)/.1} \/ {do/.1} by GRAPH_2:57
    .= {(go^'pion1)/.1,do/.1} by ENUMSET1:41;
  do/.len do = (go^'pion1)/.1 by A60,GRAPH_2:57;
  then reconsider godo as non constant standard special_circular_sequence
  by A92,A96,A97,A102,A104,A112,A113,A121,A122,A130,JORDAN8:7,8,TOPREAL8:11,33
,34;
A131: UA is_an_arc_of W-min C,E-max C by JORDAN6:def 8;
  then
A132: UA is connected by JORDAN6:11;
A133: W-min C in UA by A131,TOPREAL1:4;
A134: E-max C in UA by A131,TOPREAL1:4;
  set ff = Rotate(Cage(C,n),Wmin);
  Wmin in rng Cage(C,n) by SPRECT_2:47;
  then
A135: ff/.1 = Wmin by FINSEQ_6:98;
A136: L~ff = L~Cage(C,n) by REVROT_1:33;
  then (W-max L~ff)..ff > 1 by A135,SPRECT_5:23;
  then (N-min L~ff)..ff > 1 by A135,A136,SPRECT_5:24,XXREAL_0:2;
  then (N-max L~ff)..ff > 1 by A135,A136,SPRECT_5:25,XXREAL_0:2;
  then
A137: Emax..ff > 1 by A135,A136,SPRECT_5:26,XXREAL_0:2;
A138: now
    assume
A139: Gik..US <= 1;
    Gik..US >= 1 by A34,FINSEQ_4:31;
    then Gik..US = 1 by A139,XXREAL_0:1;
    then Gik = US/.1 by A34,FINSEQ_5:41;
    hence contradiction by A18,A22,JORDAN1F:5;
  end;
A140: Cage(C,n) is_sequence_on Ga by JORDAN9:def 1;
  then
A141: ff is_sequence_on Ga by REVROT_1:34;
A142: right_cell(godo,1,Ga)\L~godo c= RightComp godo by A92,A97,JORDAN9:29;
A143: L~godo = L~(go^'pion1) \/ L~do by A96,TOPREAL8:35
    .= L~go \/ L~pion1 \/ L~do by A94,TOPREAL8:35;
A144: L~Cage(C,n) = L~US \/ L~LS by JORDAN1E:17;
  then
A145: L~US c= L~Cage(C,n) by XBOOLE_1:7;
A146: L~LS c= L~Cage(C,n) by A144,XBOOLE_1:7;
A147: L~go c= L~Cage(C,n) by A46,A145,XBOOLE_1:1;
A148: L~do c= L~Cage(C,n) by A53,A146,XBOOLE_1:1;
A149: W-min C in C by SPRECT_1:15;
A150: L~pion = LSeg(Gik,Gij) by SPPOL_2:21;
A151: now
    assume W-min C in L~godo;
    then
A152: W-min C in L~go \/ L~pion1 or W-min C in L~do by A143,XBOOLE_0:def 3;
    per cases by A152,XBOOLE_0:def 3;
    suppose W-min C in L~go;
      then C meets L~Cage(C,n) by A147,A149,XBOOLE_0:3;
      hence contradiction by JORDAN10:5;
    end;
    suppose W-min C in L~pion1;
      hence contradiction by A8,A83,A133,A150,XBOOLE_0:3;
    end;
    suppose W-min C in L~do;
      then C meets L~Cage(C,n) by A148,A149,XBOOLE_0:3;
      hence contradiction by JORDAN10:5;
    end;
  end;
  right_cell(Rotate(Cage(C,n),Wmin),1) =
  right_cell(ff,1,GoB ff) by A88,JORDAN1H:29
    .= right_cell(ff,1,GoB Cage(C,n)) by REVROT_1:28
    .= right_cell(ff,1,Ga) by JORDAN1H:52
    .= right_cell(ff-:Emax,1,Ga) by A137,A141,JORDAN1J:53
    .= right_cell(US,1,Ga) by JORDAN1E:def 1
    .= right_cell(R_Cut(US,Gik),1,Ga) by A34,A93,A138,JORDAN1J:52
    .= right_cell(go^'pion1,1,Ga) by A39,A95,JORDAN1J:51
    .= right_cell(godo,1,Ga) by A90,A97,JORDAN1J:51;
  then W-min C in right_cell(godo,1,Ga) by JORDAN1I:8;
  then
A153: W-min C in right_cell(godo,1,Ga)\L~godo by A151,XBOOLE_0:def 5;
A154: godo/.1 = (go^'pion1)/.1 by GRAPH_2:57
    .= Wmin by A59,GRAPH_2:57;
A155: len US >= 2 by A17,XXREAL_0:2;
A156: godo/.2 = (go^'pion1)/.2 by A89,GRAPH_2:61
    .= US/.2 by A33,A76,GRAPH_2:61
    .= (US^'LS)/.2 by A155,GRAPH_2:61
    .= Rotate(Cage(C,n),Wmin)/.2 by JORDAN1E:15;
A157: L~go \/ L~do is compact by COMPTS_1:19;
  Wmin in L~go \/ L~do by A61,A77,XBOOLE_0:def 3;
  then
A158: W-min (L~go \/ L~do) = Wmin by A147,A148,A157,JORDAN1J:21,XBOOLE_1:8;
A159: (W-min (L~go \/ L~do))`1 = W-bound (L~go \/ L~do) by EUCLID:56;
A160: Wmin`1 = Wbo by EUCLID:56;
  W-bound LSeg(Gik,Gij) = Gik`1 by A80,SPRECT_1:62;
  then
A161: W-bound L~pion1 = Gik`1 by A83,SPPOL_2:21;
  Gik`1 >= Wbo by A10,A145,PSCOMP_1:71;
  then Gik`1 > Wbo by A75,XXREAL_0:1;
  then W-min (L~go\/L~do\/L~pion1) = W-min (L~go \/ L~do)
  by A157,A158,A159,A160,A161,JORDAN1J:33;
  then
A162: W-min L~godo = Wmin by A143,A158,XBOOLE_1:4;
A163: rng godo c= L~godo by A89,A91,SPPOL_2:18,XXREAL_0:2;
  2 in dom godo by A92,FINSEQ_3:27;
  then
A164: godo/.2 in rng godo by PARTFUN2:4;
  godo/.2 in W-most L~Cage(C,n) by A156,JORDAN1I:27;
  then (godo/.2)`1 = (W-min L~godo)`1 by A162,PSCOMP_1:88
    .= W-bound L~godo by EUCLID:56;
  then godo/.2 in W-most L~godo by A163,A164,SPRECT_2:16;
  then Rotate(godo,W-min L~godo)/.2 in W-most L~godo by A154,A162,FINSEQ_6:95;
  then reconsider godo as clockwise_oriented non constant standard
  special_circular_sequence by JORDAN1I:27;
  len US in dom US by FINSEQ_5:6;
  then
A165: US.len US = US/.len US by PARTFUN1:def 8
    .= Emax by JORDAN1F:7;
A166: east_halfline E-max C misses L~go
  proof
    assume east_halfline E-max C meets L~go;
    then consider p be set such that
A167: p in east_halfline E-max C and
A168: p in L~go by XBOOLE_0:3;
    reconsider p as Point of TOP-REAL 2 by A167;
    p in L~US by A46,A168;
    then p in east_halfline E-max C /\ L~Cage(C,n) by A145,A167,XBOOLE_0:def 4;
    then
A169: p`1 = Ebo by JORDAN1A:104,PSCOMP_1:111;
    then
A170: p = Emax by A46,A168,JORDAN1J:46;
    then Emax = Gik by A10,A165,A168,JORDAN1J:43;
    then Gik`1 = Ga*(len Ga,k)`1 by A3,A13,A16,A169,A170,JORDAN1A:92;
    hence contradiction by A2,A15,A30,JORDAN1G:7;
  end;
  now
    assume east_halfline E-max C meets L~godo;
    then
A171: east_halfline E-max C meets (L~go \/ L~pion1) or
    east_halfline E-max C meets L~do by A143,XBOOLE_1:70;
    per cases by A171,XBOOLE_1:70;
    suppose east_halfline E-max C meets L~go;
      hence contradiction by A166;
    end;
    suppose east_halfline E-max C meets L~pion1;
      then consider p be set such that
A172: p in east_halfline E-max C and
A173: p in L~pion1 by XBOOLE_0:3;
      reconsider p as Point of TOP-REAL 2 by A172;
A174: p`1 = Gik`1 by A80,A83,A150,A173,GOBOARD7:5;
      i+1 <= len Ga by A2,NAT_1:13;
      then i+1-1 <= len Ga-1 by XREAL_1:11;
      then
A175: i <= len Ga-'1 by XREAL_0:def 2;
      len Ga-'1 <= len Ga by NAT_D:35;
      then p`1 <= Ga*(len Ga-'1,1)`1 by A1,A3,A13,A16,A20,A174,A175,JORDAN1A:39
;
      then p`1 <= E-bound C by A20,JORDAN8:15;
      then
A176: p`1 <= (E-max C)`1 by EUCLID:56;
      p`1 >= (E-max C)`1 by A172,TOPREAL1:def 13;
      then
A177: p`1 = (E-max C)`1 by A176,XXREAL_0:1;
      p`2 = (E-max C)`2 by A172,TOPREAL1:def 13;
      then p = E-max C by A177,TOPREAL3:11;
      hence contradiction by A8,A83,A134,A150,A173,XBOOLE_0:3;
    end;
    suppose east_halfline E-max C meets L~do;
      then consider p be set such that
A178: p in east_halfline E-max C and
A179: p in L~do by XBOOLE_0:3;
      reconsider p as Point of TOP-REAL 2 by A178;
      p in L~LS by A53,A179;
      then p in east_halfline E-max C /\ L~Cage(C,n)
      by A146,A178,XBOOLE_0:def 4;
      then
A180: p`1 = Ebo by JORDAN1A:104,PSCOMP_1:111;
A181: (E-max C)`2 = p`2 by A178,TOPREAL1:def 13;
      set RC = Rotate(Cage(C,n),Emax);
A182: E-max C in right_cell(RC,1) by JORDAN1I:9;
A183: 1+1 <= len LS by A23,XXREAL_0:2;
      LS = RC-:Wmin by JORDAN1G:26;
      then
A184: LSeg(LS,1) = LSeg(RC,1) by A183,SPPOL_2:9;
A185: L~RC = L~Cage(C,n) by REVROT_1:33;
A186: len RC = len Cage(C,n) by REVROT_1:14;
A187: GoB RC = GoB Cage(C,n) by REVROT_1:28
        .= Ga by JORDAN1H:52;
A188: Emax in rng Cage(C,n) by SPRECT_2:50;
A189: RC is_sequence_on Ga by A140,REVROT_1:34;
A190: RC/.1 = E-max L~RC by A185,A188,FINSEQ_6:98;
      consider ii,jj be Element of NAT such that
A191: [ii,jj+1] in Indices Ga and
A192: [ii,jj] in Indices Ga and
A193: RC/.1 = Ga*(ii,jj+1) and
A194: RC/.(1+1) = Ga*(ii,jj) by A87,A185,A186,A188,A189,FINSEQ_6:98,JORDAN1I:25
;
      consider jj2 be Element of NAT such that
A195: 1 <= jj2 and
A196: jj2 <= width Ga and
A197: Emax = Ga*(len Ga,jj2) by JORDAN1D:29;
A198: len Ga >= 4 by JORDAN8:13;
      then len Ga >= 1 by XXREAL_0:2;
      then [len Ga,jj2] in Indices Ga by A195,A196,MATRIX_1:37;
      then
A199: ii = len Ga by A185,A190,A191,A193,A197,GOBOARD1:21;
A200: 1 <= ii by A191,MATRIX_1:39;
A201: ii <= len Ga by A191,MATRIX_1:39;
A202: 1 <= jj+1 by A191,MATRIX_1:39;
A203: jj+1 <= width Ga by A191,MATRIX_1:39;
A204: 1 <= ii by A192,MATRIX_1:39;
A205: ii <= len Ga by A192,MATRIX_1:39;
A206: 1 <= jj by A192,MATRIX_1:39;
A207: jj <= width Ga by A192,MATRIX_1:39;
A208: ii+1 <> ii;
      jj+1+1 <> jj;
      then
A209: right_cell(RC,1) = cell(Ga,ii-'1,jj)
      by A87,A186,A187,A191,A192,A193,A194,A208,GOBOARD5:def 6;
A210: ii-'1+1 = ii by A200,XREAL_1:237;
      ii-1 >= 4-1 by A198,A199,XREAL_1:11;
      then
A211: ii-1 >= 1 by XXREAL_0:2;
      then
A212: 1 <= ii-'1 by XREAL_0:def 2;
A213: Ga*(ii-'1,jj)`2 <= p`2 by A181,A182,A201,A203,A206,A209,A210,A211,
JORDAN9:19;
A214: p`2 <= Ga*(ii-'1,jj+1)`2 by A181,A182,A201,A203,A206,A209,A210,A211,
JORDAN9:19;
A215: ii-'1 < len Ga by A201,A210,NAT_1:13;
      then
A216: Ga*(ii-'1,jj)`2 = Ga*(1,jj)`2 by A206,A207,A212,GOBOARD5:2
        .= Ga*(ii,jj)`2 by A204,A205,A206,A207,GOBOARD5:2;
A217: Ga*(ii-'1,jj+1)`2 = Ga*(1,jj+1)`2 by A202,A203,A212,A215,GOBOARD5:2
        .= Ga*(ii,jj+1)`2 by A200,A201,A202,A203,GOBOARD5:2;
A218: Ga*(len Ga,jj)`1 = Ebo by A16,A206,A207,JORDAN1A:92;
      Ebo = Ga*(len Ga,jj+1)`1 by A16,A202,A203,JORDAN1A:92;
      then p in LSeg(RC/.1,RC/.(1+1))
      by A180,A193,A194,A199,A213,A214,A216,A217,A218,GOBOARD7:8;
      then
A219: p in LSeg(LS,1) by A87,A184,A186,TOPREAL1:def 5;
A220: p in LSeg(do,Index(p,do)) by A179,JORDAN3:42;
A221: do = mid(LS,Gij..LS,len LS) by A37,JORDAN1J:37;
A222: 1<=Gij..LS by A37,FINSEQ_4:31;
A223: Gij..LS<=len LS by A37,FINSEQ_4:31;
      Gij..LS <> len LS by A29,A37,FINSEQ_4:29;
      then
A224: Gij..LS < len LS by A223,XXREAL_0:1;
A225: 1<=Index(p,do) by A179,JORDAN3:41;
A226: Index(p,do) < len do by A179,JORDAN3:41;
A227: Index(Gij,LS)+1 = Gij..LS by A32,A37,JORDAN1J:56;
      consider t be Nat such that
A228: t in dom LS and
A229: LS.t = Gij by A37,FINSEQ_2:11;
A230: 1 <= t by A228,FINSEQ_3:27;
A231: t <= len LS by A228,FINSEQ_3:27;
      1 < t by A32,A229,A230,XXREAL_0:1;
      then Index(Gij,LS)+1 = t by A229,A231,JORDAN3:45;
      then
A232: len L_Cut(LS,Gij) = len LS-Index(Gij,LS) by A9,A229,JORDAN3:61;
      set tt = Index(p,do)+(Gij..LS)-'1;
A233: 1<=Index(Gij,LS) by A9,JORDAN3:41;
      0+Index(Gij,LS) < len LS by A9,JORDAN3:41;
      then
A234: len LS-Index(Gij,LS) > 0 by XREAL_1:22;
      Index(p,do) < len LS-'Index(Gij,LS) by A226,A232,XREAL_0:def 2;
      then Index(p,do)+1 <= len LS-'Index(Gij,LS) by NAT_1:13;
      then Index(p,do) <= len LS-'Index(Gij,LS)-1 by XREAL_1:21;
      then Index(p,do) <= len LS-Index(Gij,LS)-1 by A234,XREAL_0:def 2;
      then Index(p,do) <= len LS-Gij..LS by A227;
      then Index(p,do) <= len LS-'Gij..LS by XREAL_0:def 2;
      then Index(p,do) < len LS-'(Gij..LS)+1 by NAT_1:13;
      then
A235: LSeg(mid(LS,Gij..LS,len LS),Index(p,do)) =
      LSeg(LS,Index(p,do)+(Gij..LS)-'1) by A222,A224,A225,JORDAN4:31;
A236: 1+1 <= Gij..LS by A227,A233,XREAL_1:9;
      then Index(p,do)+Gij..LS >= 1+1+1 by A225,XREAL_1:9;
      then Index(p,do)+Gij..LS-1 >= 1+1+1-1 by XREAL_1:11;
      then
A237: tt >= 1+1 by XREAL_0:def 2;
A238: 2 in dom LS by A183,FINSEQ_3:27;
      now per cases by A237,XXREAL_0:1;
        suppose tt > 1+1;
          then LSeg(LS,1) misses LSeg(LS,tt) by TOPREAL1:def 9;
          hence contradiction by A219,A220,A221,A235,XBOOLE_0:3;
        end;
        suppose
A239:     tt = 1+1;
          then LSeg(LS,1) /\ LSeg(LS,tt) = {LS/.2} by A23,TOPREAL1:def 8;
          then p in {LS/.2} by A219,A220,A221,A235,XBOOLE_0:def 4;
          then
A240:     p = LS/.2 by TARSKI:def 1;
          then
A241:     p..LS = 2 by A238,FINSEQ_5:44;
          1+1 = Index(p,do)+(Gij..LS)-1 by A239,XREAL_0:def 2;
          then 1+1+1 = Index(p,do)+(Gij..LS);
          then
A242:     Gij..LS = 2 by A225,A236,JORDAN1E:10;
          p in rng LS by A238,A240,PARTFUN2:4;
          then p = Gij by A37,A241,A242,FINSEQ_5:10;
          then Gij`1 = Ebo by A240,JORDAN1G:40;
          then Gij`1 = Ga*(len Ga,j)`1 by A5,A12,A16,JORDAN1A:92;
          hence contradiction by A2,A14,A67,JORDAN1G:7;
        end;
      end;
      hence contradiction;
    end;
  end;
  then east_halfline E-max C c= (L~godo)` by SUBSET_1:43;
  then consider W be Subset of TOP-REAL 2 such that
A243: W is_a_component_of (L~godo)` and
A244: east_halfline E-max C c= W by GOBOARD9:5;
  W is not Bounded by A244,JORDAN2C:16,129;
  then W is_outside_component_of L~godo by A243,JORDAN2C:def 4;
  then W c= UBD L~godo by JORDAN2C:27;
  then
A245: east_halfline E-max C c= UBD L~godo by A244,XBOOLE_1:1;
  E-max C in east_halfline E-max C by TOPREAL1:45;
  then E-max C in UBD L~godo by A245;
  then E-max C in LeftComp godo by GOBRD14:46;
  then UA meets L~godo by A132,A133,A134,A142,A153,JORDAN1J:36;
  then
A246: UA meets (L~go \/ L~pion1) or UA meets L~do by A143,XBOOLE_1:70;
A247: UA c= C by JORDAN6:76;
  per cases by A246,XBOOLE_1:70;
  suppose UA meets L~go;
    then UA meets L~Cage(C,n) by A46,A145,XBOOLE_1:1,63;
    hence contradiction by A247,JORDAN10:5,XBOOLE_1:63;
  end;
  suppose UA meets L~pion1;
    hence contradiction by A8,A83,A150;
  end;
  suppose UA meets L~do;
    then UA meets L~Cage(C,n) by A53,A146,XBOOLE_1:1,63;
    hence contradiction by A247,JORDAN10:5,XBOOLE_1:63;
  end;
end;

theorem Th14:
  for C be Simple_closed_curve for i,j,k be Element of NAT st
  1 < i & i < len Gauge(C,n) & 1 <= k & k <= j & j <= width Gauge(C,n) &
  LSeg(Gauge(C,n)*(i,k),Gauge(C,n)*(i,j)) /\ L~Upper_Seq(C,n) =
  {Gauge(C,n)*(i,k)} &
  LSeg(Gauge(C,n)*(i,k),Gauge(C,n)*(i,j)) /\ L~Lower_Seq(C,n) =
  {Gauge(C,n)*(i,j)} holds
  LSeg(Gauge(C,n)*(i,k),Gauge(C,n)*(i,j)) meets Lower_Arc C
proof
  let C be Simple_closed_curve;
  let i,j,k be Element of NAT;
  set Ga = Gauge(C,n);
  set US = Upper_Seq(C,n);
  set LS = Lower_Seq(C,n);
  set LA = Lower_Arc C;
  set Wmin = W-min L~Cage(C,n);
  set Emax = E-max L~Cage(C,n);
  set Wbo = W-bound L~Cage(C,n);
  set Ebo = E-bound L~Cage(C,n);
  set Gik = Ga*(i,k);
  set Gij = Ga*(i,j);
  assume that
A1: 1 < i and
A2: i < len Ga and
A3: 1 <= k and
A4: k <= j and
A5: j <= width Ga and
A6: LSeg(Gik,Gij) /\ L~US = {Gik} and
A7: LSeg(Gik,Gij) /\ L~LS = {Gij} and
A8: LSeg(Gik,Gij) misses LA;
  Gij in {Gij} by TARSKI:def 1;
  then
A9: Gij in L~LS by A7,XBOOLE_0:def 4;
  Gik in {Gik} by TARSKI:def 1;
  then
A10: Gik in L~US by A6,XBOOLE_0:def 4;
  then
A11: j <> k by A1,A2,A3,A5,A9,JORDAN1J:57;
A12: 1 <= j by A3,A4,XXREAL_0:2;
A13: k <= width Ga by A4,A5,XXREAL_0:2;
A14: [i,j] in Indices Ga by A1,A2,A5,A12,MATRIX_1:37;
A15: [i,k] in Indices Ga by A1,A2,A3,A13,MATRIX_1:37;
  set do = L_Cut(LS,Gij);
  set go = R_Cut(US,Gik);
A16: len Ga = width Ga by JORDAN8:def 1;
A17: len US >= 3 by JORDAN1E:19;
  then len US >= 1 by XXREAL_0:2;
  then 1 in dom US by FINSEQ_3:27;
  then
A18: US.1 = US/.1 by PARTFUN1:def 8
    .= Wmin by JORDAN1F:5;
A19: Wmin`1 = Wbo by EUCLID:56
    .= Ga*(1,k)`1 by A3,A13,A16,JORDAN1A:94;
  len Ga >= 4 by JORDAN8:13;
  then
A20: len Ga >= 1 by XXREAL_0:2;
  then
A21: [1,k] in Indices Ga by A3,A13,MATRIX_1:37;
  then
A22: Gik <> US.1 by A1,A15,A18,A19,JORDAN1G:7;
  then reconsider go as being_S-Seq FinSequence of TOP-REAL 2
  by A10,JORDAN3:70;
A23: len LS >= 1+2 by JORDAN1E:19;
  then
A24: len LS >= 1 by XXREAL_0:2;
  then
A25: 1 in dom LS by FINSEQ_3:27;
  len LS in dom LS by A24,FINSEQ_3:27;
  then
A26: LS.len LS = LS/.len LS by PARTFUN1:def 8
    .= Wmin by JORDAN1F:8;
A27: Wmin`1 = Wbo by EUCLID:56
    .= Ga*(1,k)`1 by A3,A13,A16,JORDAN1A:94;
A28: [i,j] in Indices Ga by A1,A2,A5,A12,MATRIX_1:37;
  then
A29: Gij <> LS.len LS by A1,A21,A26,A27,JORDAN1G:7;
  then reconsider do as being_S-Seq FinSequence of TOP-REAL 2
  by A9,JORDAN3:69;
A30: [len Ga,k] in Indices Ga by A3,A13,A20,MATRIX_1:37;
A31: LS.1 = LS/.1 by A25,PARTFUN1:def 8
    .= Emax by JORDAN1F:6;
  Emax`1 = Ebo by EUCLID:56
    .= Ga*(len Ga,k)`1 by A3,A13,A16,JORDAN1A:92;
  then
A32: Gij <> LS.1 by A2,A28,A30,A31,JORDAN1G:7;
A33: len go >= 1+1 by TOPREAL1:def 10;
A34: Gik in rng US by A1,A2,A3,A10,A13,JORDAN1G:4,JORDAN1J:40;
  then
A35: go is_sequence_on Ga by JORDAN1G:4,JORDAN1J:38;
A36: len do >= 1+1 by TOPREAL1:def 10;
A37: Gij in rng LS by A1,A2,A5,A9,A12,JORDAN1G:5,JORDAN1J:40;
  then
A38: do is_sequence_on Ga by JORDAN1G:5,JORDAN1J:39;
  reconsider go as non constant s.c.c.
  (being_S-Seq FinSequence of TOP-REAL 2) by A33,A35,JGRAPH_1:16,JORDAN8:8;
  reconsider do as non constant s.c.c.
  (being_S-Seq FinSequence of TOP-REAL 2) by A36,A38,JGRAPH_1:16,JORDAN8:8;
A39: len go > 1 by A33,NAT_1:13;
  then
A40: len go in dom go by FINSEQ_3:27;
  then
A41: go/.len go = go.len go by PARTFUN1:def 8
    .= Gik by A10,JORDAN3:59;
  len do >= 1 by A36,XXREAL_0:2;
  then 1 in dom do by FINSEQ_3:27;
  then
A42: do/.1 = do.1 by PARTFUN1:def 8
    .= Gij by A9,JORDAN3:58;
  reconsider m = len go - 1 as Element of NAT by A40,FINSEQ_3:28;
A43: m+1 = len go;
  then
A44: len go-'1 = m by NAT_D:34;
A45: LSeg(go,m) c= L~go by TOPREAL3:26;
A46: L~go c= L~US by A10,JORDAN3:76;
  then LSeg(go,m) c= L~US by A45,XBOOLE_1:1;
  then
A47: LSeg(go,m) /\ LSeg(Gik,Gij) c= {Gik} by A6,XBOOLE_1:26;
  m >= 1 by A33,XREAL_1:21;
  then
A48: LSeg(go,m) = LSeg(go/.m,Gik) by A41,A43,TOPREAL1:def 5;
  {Gik} c= LSeg(go,m) /\ LSeg(Gik,Gij)
  proof
    let x be set;
    assume x in {Gik};
    then
A49: x = Gik by TARSKI:def 1;
A50: Gik in LSeg(go,m) by A48,RLTOPSP1:69;
    Gik in LSeg(Gik,Gij) by RLTOPSP1:69;
    hence thesis by A49,A50,XBOOLE_0:def 4;
  end;
  then
A51: LSeg(go,m) /\ LSeg(Gik,Gij) = {Gik} by A47,XBOOLE_0:def 10;
A52: LSeg(do,1) c= L~do by TOPREAL3:26;
A53: L~do c= L~LS by A9,JORDAN3:77;
  then LSeg(do,1) c= L~LS by A52,XBOOLE_1:1;
  then
A54: LSeg(do,1) /\ LSeg(Gik,Gij) c= {Gij} by A7,XBOOLE_1:26;
A55: LSeg(do,1) = LSeg(Gij,do/.(1+1)) by A36,A42,TOPREAL1:def 5;
  {Gij} c= LSeg(do,1) /\ LSeg(Gik,Gij)
  proof
    let x be set;
    assume x in {Gij};
    then
A56: x = Gij by TARSKI:def 1;
A57: Gij in LSeg(do,1) by A55,RLTOPSP1:69;
    Gij in LSeg(Gik,Gij) by RLTOPSP1:69;
    hence thesis by A56,A57,XBOOLE_0:def 4;
  end;
  then
A58: LSeg(Gik,Gij) /\ LSeg(do,1) = {Gij} by A54,XBOOLE_0:def 10;
A59: go/.1 = US/.1 by A10,SPRECT_3:39
    .= Wmin by JORDAN1F:5;
  then
A60: go/.1 = LS/.len LS by JORDAN1F:8
    .= do/.len do by A9,JORDAN1J:35;
A61: rng go c= L~go by A33,SPPOL_2:18;
A62: rng do c= L~do by A36,SPPOL_2:18;
A63: {go/.1} c= L~go /\ L~do
  proof
    let x be set;
    assume x in {go/.1};
    then
A64: x = go/.1 by TARSKI:def 1;
    then
A65: x in rng go by FINSEQ_6:46;
    x in rng do by A60,A64,REVROT_1:3;
    hence thesis by A61,A62,A65,XBOOLE_0:def 4;
  end;
A66: LS.1 = LS/.1 by A25,PARTFUN1:def 8
    .= Emax by JORDAN1F:6;
A67: [len Ga,j] in Indices Ga by A5,A12,A20,MATRIX_1:37;
  L~go /\ L~do c= {go/.1}
  proof
    let x be set;
    assume
A68: x in L~go /\ L~do;
    then
A69: x in L~go by XBOOLE_0:def 4;
A70: x in L~do by A68,XBOOLE_0:def 4;
    then x in L~US /\ L~LS by A46,A53,A69,XBOOLE_0:def 4;
    then x in {Wmin,Emax} by JORDAN1E:20;
    then
A71: x = Wmin or x = Emax by TARSKI:def 2;
    now
      assume x = Emax;
      then
A72:  Emax = Gij by A9,A66,A70,JORDAN1E:11;
      Ga*(len Ga,j)`1 = Ebo by A5,A12,A16,JORDAN1A:92;
      then Emax`1 <> Ebo by A2,A14,A67,A72,JORDAN1G:7;
      hence contradiction by EUCLID:56;
    end;
    hence thesis by A59,A71,TARSKI:def 1;
  end;
  then
A73: L~go /\ L~do = {go/.1} by A63,XBOOLE_0:def 10;
  set W2 = go/.2;
A74: 2 in dom go by A33,FINSEQ_3:27;
A75: now
    assume Gik`1 = Wbo;
    then Ga*(1,k)`1 = Ga*(i,k)`1 by A3,A13,A16,JORDAN1A:94;
    hence contradiction by A1,A15,A21,JORDAN1G:7;
  end;
  go = mid(US,1,Gik..US) by A34,JORDAN1G:57
    .= US|(Gik..US) by A34,FINSEQ_4:31,FINSEQ_6:122;
  then
A76: W2 = US/.2 by A74,FINSEQ_4:85;
A77: Wmin in rng go by A59,FINSEQ_6:46;
  set pion = <*Gik,Gij*>;
A78: now
    let n be Element of NAT;
    assume n in dom pion;
    then n in Seg 2 by FINSEQ_3:29;
    then n = 1 or n = 2 by FINSEQ_1:4,TARSKI:def 2;
    hence
    ex i,j be Element of NAT st [i,j] in Indices Ga & pion/.n = Ga*(i,j)
    by A14,A15,FINSEQ_4:26;
  end;
A79: Gik <> Gij by A11,A14,A15,GOBOARD1:21;
A80: Gik`1 = Ga*(i,1)`1 by A1,A2,A3,A13,GOBOARD5:3
    .= Gij`1 by A1,A2,A5,A12,GOBOARD5:3;
  then LSeg(Gik,Gij) is vertical by SPPOL_1:37;
  then pion is being_S-Seq by A79,JORDAN1B:8;
  then consider pion1 be FinSequence of TOP-REAL 2 such that
A81: pion1 is_sequence_on Ga and
A82: pion1 is being_S-Seq and
A83: L~pion = L~pion1 and
A84: pion/.1 = pion1/.1 and
A85: pion/.len pion = pion1/.len pion1 and
A86: len pion <= len pion1 by A78,GOBOARD3:2;
  reconsider pion1 as being_S-Seq FinSequence of TOP-REAL 2 by A82;
  set godo = go^'pion1^'do;
A87: 1+1 <= len Cage(C,n) by GOBOARD7:36,XXREAL_0:2;
A88: 1+1 <= len Rotate(Cage(C,n),Wmin) by GOBOARD7:36,XXREAL_0:2;
  len (go^'pion1) >= len go by TOPREAL8:7;
  then
A89: len (go^'pion1) >= 1+1 by A33,XXREAL_0:2;
  then
A90: len (go^'pion1) > 1+0 by NAT_1:13;
A91: len godo >= len (go^'pion1) by TOPREAL8:7;
  then
A92: 1+1 <= len godo by A89,XXREAL_0:2;
A93: US is_sequence_on Ga by JORDAN1G:4;
A94: go/.len go = pion1/.1 by A41,A84,FINSEQ_4:26;
  then
A95: go^'pion1 is_sequence_on Ga by A35,A81,TOPREAL8:12;
A96: (go^'pion1)/.len (go^'pion1) = pion/.len pion by A85,GRAPH_2:58
    .= pion/.2 by FINSEQ_1:61
    .= do/.1 by A42,FINSEQ_4:26;
  then
A97: godo is_sequence_on Ga by A38,A95,TOPREAL8:12;
  LSeg(pion1,1) c= L~<*Gik,Gij*> by A83,TOPREAL3:26;
  then LSeg(pion1,1) c= LSeg(Gik,Gij) by SPPOL_2:21;
  then
A98: LSeg(go,len go-'1) /\ LSeg(pion1,1) c= {Gik} by A44,A51,XBOOLE_1:27;
A99: len pion1 >= 1+1 by A86,FINSEQ_1:61;
  {Gik} c= LSeg(go,m) /\ LSeg(pion1,1)
  proof
    let x be set;
    assume x in {Gik};
    then
A100: x = Gik by TARSKI:def 1;
A101: Gik in LSeg(go,m) by A48,RLTOPSP1:69;
    Gik in LSeg(pion1,1) by A41,A94,A99,TOPREAL1:27;
    hence thesis by A100,A101,XBOOLE_0:def 4;
  end;
  then LSeg(go,len go-'1) /\ LSeg(pion1,1) = { go/.len go }
  by A41,A44,A98,XBOOLE_0:def 10;
  then
A102: go^'pion1 is unfolded by A94,TOPREAL8:34;
  len pion1 >= 2+0 by A86,FINSEQ_1:61;
  then
A103: len pion1-2 >= 0 by XREAL_1:21;
  len (go^'pion1)+1-1 = len go+len pion1-1 by GRAPH_2:13;
  then len (go^'pion1)-1 = len go + (len pion1-2)
    .= len go + (len pion1-'2) by A103,XREAL_0:def 2;
  then
A104: len (go^'pion1)-'1 = len go + (len pion1-'2) by XREAL_0:def 2;
A105: len pion1-1 >= 1 by A99,XREAL_1:21;
  then
A106: len pion1-'1 = len pion1-1 by XREAL_0:def 2;
A107: len pion1-'2+1 = len pion1-2+1 by A103,XREAL_0:def 2
    .= len pion1-'1 by A105,XREAL_0:def 2;
  len pion1-1+1 <= len pion1;
  then
A108: len pion1-'1 < len pion1 by A106,NAT_1:13;
  LSeg(pion1,len pion1-'1) c= L~<*Gik,Gij*> by A83,TOPREAL3:26;
  then LSeg(pion1,len pion1-'1) c= LSeg(Gik,Gij) by SPPOL_2:21;
  then
A109: LSeg(pion1,len pion1-'1) /\ LSeg(do,1) c= {Gij} by A58,XBOOLE_1:27;
  {Gij} c= LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
  proof
    let x be set;
    assume x in {Gij};
    then
A110: x = Gij by TARSKI:def 1;
A111: Gij in LSeg(do,1) by A55,RLTOPSP1:69;
    pion1/.(len pion1-'1+1) = pion/.2 by A85,A106,FINSEQ_1:61
      .= Gij by FINSEQ_4:26;
    then Gij in LSeg(pion1,len pion1-'1) by A105,A106,TOPREAL1:27;
    hence thesis by A110,A111,XBOOLE_0:def 4;
  end;
  then LSeg(pion1,len pion1-'1) /\ LSeg(do,1) = {Gij} by A109,XBOOLE_0:def 10;
  then
A112: LSeg(go^'pion1,len go+(len pion1-'2)) /\ LSeg(do,1) =
  {(go^'pion1)/.len (go^'pion1)} by A42,A94,A96,A107,A108,TOPREAL8:31;
A113: (go^'pion1) is non trivial by A89,REALSET1:13;
A114: rng pion1 c= L~pion1 by A99,SPPOL_2:18;
A115: {pion1/.1} c= L~go /\ L~pion1
  proof
    let x be set;
    assume x in {pion1/.1};
    then
A116: x = pion1/.1 by TARSKI:def 1;
    then
A117: x in rng go by A94,REVROT_1:3;
    x in rng pion1 by A116,FINSEQ_6:46;
    hence thesis by A61,A114,A117,XBOOLE_0:def 4;
  end;
  L~go /\ L~pion1 c= {pion1/.1}
  proof
    let x be set;
    assume
A118: x in L~go /\ L~pion1;
    then
A119: x in L~go by XBOOLE_0:def 4;
    x in L~pion1 by A118,XBOOLE_0:def 4;
    then x in L~pion1 /\ L~US by A46,A119,XBOOLE_0:def 4;
    hence thesis by A6,A41,A83,A94,SPPOL_2:21;
  end;
  then
A120: L~go /\ L~pion1 = {pion1/.1} by A115,XBOOLE_0:def 10;
  then
A121: (go^'pion1) is s.n.c. by A94,JORDAN1J:54;
  rng go /\ rng pion1 c= {pion1/.1} by A61,A114,A120,XBOOLE_1:27;
  then
A122: go^'pion1 is one-to-one by JORDAN1J:55;
A123: pion/.len pion = pion/.2 by FINSEQ_1:61
    .= do/.1 by A42,FINSEQ_4:26;
A124: {pion1/.len pion1} c= L~do /\ L~pion1
  proof
    let x be set;
    assume x in {pion1/.len pion1};
    then
A125: x = pion1/.len pion1 by TARSKI:def 1;
    then
A126: x in rng do by A85,A123,FINSEQ_6:46;
    x in rng pion1 by A125,REVROT_1:3;
    hence thesis by A62,A114,A126,XBOOLE_0:def 4;
  end;
  L~do /\ L~pion1 c= {pion1/.len pion1}
  proof
    let x be set;
    assume
A127: x in L~do /\ L~pion1;
    then
A128: x in L~do by XBOOLE_0:def 4;
    x in L~pion1 by A127,XBOOLE_0:def 4;
    then x in L~pion1 /\ L~LS by A53,A128,XBOOLE_0:def 4;
    hence thesis by A7,A42,A83,A85,A123,SPPOL_2:21;
  end;
  then
A129: L~do /\ L~pion1 = {pion1/.len pion1} by A124,XBOOLE_0:def 10;
A130: L~(go^'pion1) /\ L~do = (L~go \/ L~pion1) /\ L~do by A94,TOPREAL8:35
    .= {go/.1} \/ {do/.1} by A73,A85,A123,A129,XBOOLE_1:23
    .= {(go^'pion1)/.1} \/ {do/.1} by GRAPH_2:57
    .= {(go^'pion1)/.1,do/.1} by ENUMSET1:41;
  do/.len do = (go^'pion1)/.1 by A60,GRAPH_2:57;
  then reconsider godo as non constant standard special_circular_sequence
  by A92,A96,A97,A102,A104,A112,A113,A121,A122,A130,JORDAN8:7,8,TOPREAL8:11,33
,34;
A131: LA is_an_arc_of E-max C,W-min C by JORDAN6:def 9;
  then
A132: LA is connected by JORDAN6:11;
A133: W-min C in LA by A131,TOPREAL1:4;
A134: E-max C in LA by A131,TOPREAL1:4;
  set ff = Rotate(Cage(C,n),Wmin);
  Wmin in rng Cage(C,n) by SPRECT_2:47;
  then
A135: ff/.1 = Wmin by FINSEQ_6:98;
A136: L~ff = L~Cage(C,n) by REVROT_1:33;
  then (W-max L~ff)..ff > 1 by A135,SPRECT_5:23;
  then (N-min L~ff)..ff > 1 by A135,A136,SPRECT_5:24,XXREAL_0:2;
  then (N-max L~ff)..ff > 1 by A135,A136,SPRECT_5:25,XXREAL_0:2;
  then
A137: Emax..ff > 1 by A135,A136,SPRECT_5:26,XXREAL_0:2;
A138: now
    assume
A139: Gik..US <= 1;
    Gik..US >= 1 by A34,FINSEQ_4:31;
    then Gik..US = 1 by A139,XXREAL_0:1;
    then Gik = US/.1 by A34,FINSEQ_5:41;
    hence contradiction by A18,A22,JORDAN1F:5;
  end;
A140: Cage(C,n) is_sequence_on Ga by JORDAN9:def 1;
  then
A141: ff is_sequence_on Ga by REVROT_1:34;
A142: right_cell(godo,1,Ga)\L~godo c= RightComp godo by A92,A97,JORDAN9:29;
A143: L~godo = L~(go^'pion1) \/ L~do by A96,TOPREAL8:35
    .= L~go \/ L~pion1 \/ L~do by A94,TOPREAL8:35;
A144: L~Cage(C,n) = L~US \/ L~LS by JORDAN1E:17;
  then
A145: L~US c= L~Cage(C,n) by XBOOLE_1:7;
A146: L~LS c= L~Cage(C,n) by A144,XBOOLE_1:7;
A147: L~go c= L~Cage(C,n) by A46,A145,XBOOLE_1:1;
A148: L~do c= L~Cage(C,n) by A53,A146,XBOOLE_1:1;
A149: W-min C in C by SPRECT_1:15;
A150: L~pion = LSeg(Gik,Gij) by SPPOL_2:21;
A151: now
    assume W-min C in L~godo;
    then
A152: W-min C in L~go \/ L~pion1 or W-min C in L~do by A143,XBOOLE_0:def 3;
    per cases by A152,XBOOLE_0:def 3;
    suppose W-min C in L~go;
      then C meets L~Cage(C,n) by A147,A149,XBOOLE_0:3;
      hence contradiction by JORDAN10:5;
    end;
    suppose W-min C in L~pion1;
      hence contradiction by A8,A83,A133,A150,XBOOLE_0:3;
    end;
    suppose W-min C in L~do;
      then C meets L~Cage(C,n) by A148,A149,XBOOLE_0:3;
      hence contradiction by JORDAN10:5;
    end;
  end;
  right_cell(Rotate(Cage(C,n),Wmin),1) =
  right_cell(ff,1,GoB ff) by A88,JORDAN1H:29
    .= right_cell(ff,1,GoB Cage(C,n)) by REVROT_1:28
    .= right_cell(ff,1,Ga) by JORDAN1H:52
    .= right_cell(ff-:Emax,1,Ga) by A137,A141,JORDAN1J:53
    .= right_cell(US,1,Ga) by JORDAN1E:def 1
    .= right_cell(R_Cut(US,Gik),1,Ga) by A34,A93,A138,JORDAN1J:52
    .= right_cell(go^'pion1,1,Ga) by A39,A95,JORDAN1J:51
    .= right_cell(godo,1,Ga) by A90,A97,JORDAN1J:51;
  then W-min C in right_cell(godo,1,Ga) by JORDAN1I:8;
  then
A153: W-min C in right_cell(godo,1,Ga)\L~godo by A151,XBOOLE_0:def 5;
A154: godo/.1 = (go^'pion1)/.1 by GRAPH_2:57
    .= Wmin by A59,GRAPH_2:57;
A155: len US >= 2 by A17,XXREAL_0:2;
A156: godo/.2 = (go^'pion1)/.2 by A89,GRAPH_2:61
    .= US/.2 by A33,A76,GRAPH_2:61
    .= (US^'LS)/.2 by A155,GRAPH_2:61
    .= Rotate(Cage(C,n),Wmin)/.2 by JORDAN1E:15;
A157: L~go \/ L~do is compact by COMPTS_1:19;
  Wmin in L~go \/ L~do by A61,A77,XBOOLE_0:def 3;
  then
A158: W-min (L~go \/ L~do) = Wmin by A147,A148,A157,JORDAN1J:21,XBOOLE_1:8;
A159: (W-min (L~go \/ L~do))`1 = W-bound (L~go \/ L~do) by EUCLID:56;
A160: Wmin`1 = Wbo by EUCLID:56;
  W-bound LSeg(Gik,Gij) = Gik`1 by A80,SPRECT_1:62;
  then
A161: W-bound L~pion1 = Gik`1 by A83,SPPOL_2:21;
  Gik`1 >= Wbo by A10,A145,PSCOMP_1:71;
  then Gik`1 > Wbo by A75,XXREAL_0:1;
  then W-min (L~go\/L~do\/L~pion1) = W-min (L~go \/ L~do)
  by A157,A158,A159,A160,A161,JORDAN1J:33;
  then
A162: W-min L~godo = Wmin by A143,A158,XBOOLE_1:4;
A163: rng godo c= L~godo by A89,A91,SPPOL_2:18,XXREAL_0:2;
  2 in dom godo by A92,FINSEQ_3:27;
  then
A164: godo/.2 in rng godo by PARTFUN2:4;
  godo/.2 in W-most L~Cage(C,n) by A156,JORDAN1I:27;
  then (godo/.2)`1 = (W-min L~godo)`1 by A162,PSCOMP_1:88
    .= W-bound L~godo by EUCLID:56;
  then godo/.2 in W-most L~godo by A163,A164,SPRECT_2:16;
  then Rotate(godo,W-min L~godo)/.2 in W-most L~godo by A154,A162,FINSEQ_6:95;
  then reconsider godo as clockwise_oriented non constant standard
  special_circular_sequence by JORDAN1I:27;
  len US in dom US by FINSEQ_5:6;
  then
A165: US.len US = US/.len US by PARTFUN1:def 8
    .= Emax by JORDAN1F:7;
A166: east_halfline E-max C misses L~go
  proof
    assume east_halfline E-max C meets L~go;
    then consider p be set such that
A167: p in east_halfline E-max C and
A168: p in L~go by XBOOLE_0:3;
    reconsider p as Point of TOP-REAL 2 by A167;
    p in L~US by A46,A168;
    then p in east_halfline E-max C /\ L~Cage(C,n) by A145,A167,XBOOLE_0:def 4;
    then
A169: p`1 = Ebo by JORDAN1A:104,PSCOMP_1:111;
    then
A170: p = Emax by A46,A168,JORDAN1J:46;
    then Emax = Gik by A10,A165,A168,JORDAN1J:43;
    then Gik`1 = Ga*(len Ga,k)`1 by A3,A13,A16,A169,A170,JORDAN1A:92;
    hence contradiction by A2,A15,A30,JORDAN1G:7;
  end;
  now
    assume east_halfline E-max C meets L~godo;
    then
A171: east_halfline E-max C meets (L~go \/ L~pion1) or
    east_halfline E-max C meets L~do by A143,XBOOLE_1:70;
    per cases by A171,XBOOLE_1:70;
    suppose east_halfline E-max C meets L~go;
      hence contradiction by A166;
    end;
    suppose east_halfline E-max C meets L~pion1;
      then consider p be set such that
A172: p in east_halfline E-max C and
A173: p in L~pion1 by XBOOLE_0:3;
      reconsider p as Point of TOP-REAL 2 by A172;
A174: p`1 = Gik`1 by A80,A83,A150,A173,GOBOARD7:5;
      i+1 <= len Ga by A2,NAT_1:13;
      then i+1-1 <= len Ga-1 by XREAL_1:11;
      then
A175: i <= len Ga-'1 by XREAL_0:def 2;
      len Ga-'1 <= len Ga by NAT_D:35;
      then p`1 <= Ga*(len Ga-'1,1)`1 by A1,A3,A13,A16,A20,A174,A175,JORDAN1A:39
;
      then p`1 <= E-bound C by A20,JORDAN8:15;
      then
A176: p`1 <= (E-max C)`1 by EUCLID:56;
      p`1 >= (E-max C)`1 by A172,TOPREAL1:def 13;
      then
A177: p`1 = (E-max C)`1 by A176,XXREAL_0:1;
      p`2 = (E-max C)`2 by A172,TOPREAL1:def 13;
      then p = E-max C by A177,TOPREAL3:11;
      hence contradiction by A8,A83,A134,A150,A173,XBOOLE_0:3;
    end;
    suppose east_halfline E-max C meets L~do;
      then consider p be set such that
A178: p in east_halfline E-max C and
A179: p in L~do by XBOOLE_0:3;
      reconsider p as Point of TOP-REAL 2 by A178;
      p in L~LS by A53,A179;
      then p in east_halfline E-max C /\ L~Cage(C,n)
      by A146,A178,XBOOLE_0:def 4;
      then
A180: p`1 = Ebo by JORDAN1A:104,PSCOMP_1:111;
A181: (E-max C)`2 = p`2 by A178,TOPREAL1:def 13;
      set RC = Rotate(Cage(C,n),Emax);
A182: E-max C in right_cell(RC,1) by JORDAN1I:9;
A183: 1+1 <= len LS by A23,XXREAL_0:2;
      LS = RC-:Wmin by JORDAN1G:26;
      then
A184: LSeg(LS,1) = LSeg(RC,1) by A183,SPPOL_2:9;
A185: L~RC = L~Cage(C,n) by REVROT_1:33;
A186: len RC = len Cage(C,n) by REVROT_1:14;
A187: GoB RC = GoB Cage(C,n) by REVROT_1:28
        .= Ga by JORDAN1H:52;
A188: Emax in rng Cage(C,n) by SPRECT_2:50;
A189: RC is_sequence_on Ga by A140,REVROT_1:34;
A190: RC/.1 = E-max L~RC by A185,A188,FINSEQ_6:98;
      consider ii,jj be Element of NAT such that
A191: [ii,jj+1] in Indices Ga and
A192: [ii,jj] in Indices Ga and
A193: RC/.1 = Ga*(ii,jj+1) and
A194: RC/.(1+1) = Ga*(ii,jj) by A87,A185,A186,A188,A189,FINSEQ_6:98,JORDAN1I:25
;
      consider jj2 be Element of NAT such that
A195: 1 <= jj2 and
A196: jj2 <= width Ga and
A197: Emax = Ga*(len Ga,jj2) by JORDAN1D:29;
A198: len Ga >= 4 by JORDAN8:13;
      then len Ga >= 1 by XXREAL_0:2;
      then [len Ga,jj2] in Indices Ga by A195,A196,MATRIX_1:37;
      then
A199: ii = len Ga by A185,A190,A191,A193,A197,GOBOARD1:21;
A200: 1 <= ii by A191,MATRIX_1:39;
A201: ii <= len Ga by A191,MATRIX_1:39;
A202: 1 <= jj+1 by A191,MATRIX_1:39;
A203: jj+1 <= width Ga by A191,MATRIX_1:39;
A204: 1 <= ii by A192,MATRIX_1:39;
A205: ii <= len Ga by A192,MATRIX_1:39;
A206: 1 <= jj by A192,MATRIX_1:39;
A207: jj <= width Ga by A192,MATRIX_1:39;
A208: ii+1 <> ii;
      jj+1+1 <> jj;
      then
A209: right_cell(RC,1) = cell(Ga,ii-'1,jj)
      by A87,A186,A187,A191,A192,A193,A194,A208,GOBOARD5:def 6;
A210: ii-'1+1 = ii by A200,XREAL_1:237;
      ii-1 >= 4-1 by A198,A199,XREAL_1:11;
      then
A211: ii-1 >= 1 by XXREAL_0:2;
      then
A212: 1 <= ii-'1 by XREAL_0:def 2;
A213: Ga*(ii-'1,jj)`2 <= p`2 by A181,A182,A201,A203,A206,A209,A210,A211,
JORDAN9:19;
A214: p`2 <= Ga*(ii-'1,jj+1)`2 by A181,A182,A201,A203,A206,A209,A210,A211,
JORDAN9:19;
A215: ii-'1 < len Ga by A201,A210,NAT_1:13;
      then
A216: Ga*(ii-'1,jj)`2 = Ga*(1,jj)`2 by A206,A207,A212,GOBOARD5:2
        .= Ga*(ii,jj)`2 by A204,A205,A206,A207,GOBOARD5:2;
A217: Ga*(ii-'1,jj+1)`2 = Ga*(1,jj+1)`2 by A202,A203,A212,A215,GOBOARD5:2
        .= Ga*(ii,jj+1)`2 by A200,A201,A202,A203,GOBOARD5:2;
A218: Ga*(len Ga,jj)`1 = Ebo by A16,A206,A207,JORDAN1A:92;
      Ebo = Ga*(len Ga,jj+1)`1 by A16,A202,A203,JORDAN1A:92;
      then p in LSeg(RC/.1,RC/.(1+1))
      by A180,A193,A194,A199,A213,A214,A216,A217,A218,GOBOARD7:8;
      then
A219: p in LSeg(LS,1) by A87,A184,A186,TOPREAL1:def 5;
A220: p in LSeg(do,Index(p,do)) by A179,JORDAN3:42;
A221: do = mid(LS,Gij..LS,len LS) by A37,JORDAN1J:37;
A222: 1<=Gij..LS by A37,FINSEQ_4:31;
A223: Gij..LS<=len LS by A37,FINSEQ_4:31;
      Gij..LS <> len LS by A29,A37,FINSEQ_4:29;
      then
A224: Gij..LS < len LS by A223,XXREAL_0:1;
A225: 1<=Index(p,do) by A179,JORDAN3:41;
A226: Index(p,do) < len do by A179,JORDAN3:41;
A227: Index(Gij,LS)+1 = Gij..LS by A32,A37,JORDAN1J:56;
      consider t be Nat such that
A228: t in dom LS and
A229: LS.t = Gij by A37,FINSEQ_2:11;
A230: 1 <= t by A228,FINSEQ_3:27;
A231: t <= len LS by A228,FINSEQ_3:27;
      1 < t by A32,A229,A230,XXREAL_0:1;
      then Index(Gij,LS)+1 = t by A229,A231,JORDAN3:45;
      then
A232: len L_Cut(LS,Gij) = len LS-Index(Gij,LS) by A9,A229,JORDAN3:61;
      set tt = Index(p,do)+(Gij..LS)-'1;
A233: 1<=Index(Gij,LS) by A9,JORDAN3:41;
      0+Index(Gij,LS) < len LS by A9,JORDAN3:41;
      then
A234: len LS-Index(Gij,LS) > 0 by XREAL_1:22;
      Index(p,do) < len LS-'Index(Gij,LS) by A226,A232,XREAL_0:def 2;
      then Index(p,do)+1 <= len LS-'Index(Gij,LS) by NAT_1:13;
      then Index(p,do) <= len LS-'Index(Gij,LS)-1 by XREAL_1:21;
      then Index(p,do) <= len LS-Index(Gij,LS)-1 by A234,XREAL_0:def 2;
      then Index(p,do) <= len LS-Gij..LS by A227;
      then Index(p,do) <= len LS-'Gij..LS by XREAL_0:def 2;
      then Index(p,do) < len LS-'(Gij..LS)+1 by NAT_1:13;
      then
A235: LSeg(mid(LS,Gij..LS,len LS),Index(p,do)) =
      LSeg(LS,Index(p,do)+(Gij..LS)-'1) by A222,A224,A225,JORDAN4:31;
A236: 1+1 <= Gij..LS by A227,A233,XREAL_1:9;
      then Index(p,do)+Gij..LS >= 1+1+1 by A225,XREAL_1:9;
      then Index(p,do)+Gij..LS-1 >= 1+1+1-1 by XREAL_1:11;
      then
A237: tt >= 1+1 by XREAL_0:def 2;
A238: 2 in dom LS by A183,FINSEQ_3:27;
      now per cases by A237,XXREAL_0:1;
        suppose tt > 1+1;
          then LSeg(LS,1) misses LSeg(LS,tt) by TOPREAL1:def 9;
          hence contradiction by A219,A220,A221,A235,XBOOLE_0:3;
        end;
        suppose
A239:     tt = 1+1;
          then LSeg(LS,1) /\ LSeg(LS,tt) = {LS/.2} by A23,TOPREAL1:def 8;
          then p in {LS/.2} by A219,A220,A221,A235,XBOOLE_0:def 4;
          then
A240:     p = LS/.2 by TARSKI:def 1;
          then
A241:     p..LS = 2 by A238,FINSEQ_5:44;
          1+1 = Index(p,do)+(Gij..LS)-1 by A239,XREAL_0:def 2;
          then 1+1+1 = Index(p,do)+(Gij..LS);
          then
A242:     Gij..LS = 2 by A225,A236,JORDAN1E:10;
          p in rng LS by A238,A240,PARTFUN2:4;
          then p = Gij by A37,A241,A242,FINSEQ_5:10;
          then Gij`1 = Ebo by A240,JORDAN1G:40;
          then Gij`1 = Ga*(len Ga,j)`1 by A5,A12,A16,JORDAN1A:92;
          hence contradiction by A2,A14,A67,JORDAN1G:7;
        end;
      end;
      hence contradiction;
    end;
  end;
  then east_halfline E-max C c= (L~godo)` by SUBSET_1:43;
  then consider W be Subset of TOP-REAL 2 such that
A243: W is_a_component_of (L~godo)` and
A244: east_halfline E-max C c= W by GOBOARD9:5;
  W is not Bounded by A244,JORDAN2C:16,129;
  then W is_outside_component_of L~godo by A243,JORDAN2C:def 4;
  then W c= UBD L~godo by JORDAN2C:27;
  then
A245: east_halfline E-max C c= UBD L~godo by A244,XBOOLE_1:1;
  E-max C in east_halfline E-max C by TOPREAL1:45;
  then E-max C in UBD L~godo by A245;
  then E-max C in LeftComp godo by GOBRD14:46;
  then LA meets L~godo by A132,A133,A134,A142,A153,JORDAN1J:36;
  then
A246: LA meets (L~go \/ L~pion1) or LA meets L~do by A143,XBOOLE_1:70;
A247: LA c= C by JORDAN6:76;
  per cases by A246,XBOOLE_1:70;
  suppose LA meets L~go;
    then LA meets L~Cage(C,n) by A46,A145,XBOOLE_1:1,63;
    hence contradiction by A247,JORDAN10:5,XBOOLE_1:63;
  end;
  suppose LA meets L~pion1;
    hence contradiction by A8,A83,A150;
  end;
  suppose LA meets L~do;
    then LA meets L~Cage(C,n) by A53,A146,XBOOLE_1:1,63;
    hence contradiction by A247,JORDAN10:5,XBOOLE_1:63;
  end;
end;

theorem
  for C be Simple_closed_curve for i,j,k be Element of NAT st
  1 < i & i < len Gauge(C,n) &
  1 <= j & j <= k & k <= width Gauge(C,n) & n > 0 &
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Lower_Arc L~Cage(C,n) =
  {Gauge(C,n)*(i,k)} &
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Upper_Arc L~Cage(C,n) =
  {Gauge(C,n)*(i,j)} holds
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Upper_Arc C
proof
  let C be Simple_closed_curve;
  let i,j,k be Element of NAT;
  assume that
A1: 1 < i and
A2: i < len Gauge(C,n) and
A3: 1 <= j and
A4: j <= k and
A5: k <= width Gauge(C,n) and
A6: n > 0 and
A7: LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Lower_Arc L~Cage(C,n) =
  {Gauge(C,n)*(i,k)} and
A8: LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Upper_Arc L~Cage(C,n) =
  {Gauge(C,n)*(i,j)};
A9: L~Lower_Seq(C,n) = Lower_Arc L~Cage(C,n) by A6,JORDAN1G:64;
  L~Upper_Seq(C,n) = Upper_Arc L~Cage(C,n) by A6,JORDAN1G:63;
  hence thesis by A1,A2,A3,A4,A5,A7,A8,A9,Th13;
end;

theorem
  for C be Simple_closed_curve for i,j,k be Element of NAT st
  1 < i & i < len Gauge(C,n) &
  1 <= j & j <= k & k <= width Gauge(C,n) & n > 0 &
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Lower_Arc L~Cage(C,n) =
  {Gauge(C,n)*(i,k)} &
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Upper_Arc L~Cage(C,n) =
  {Gauge(C,n)*(i,j)} holds
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Lower_Arc C
proof
  let C be Simple_closed_curve;
  let i,j,k be Element of NAT;
  assume that
A1: 1 < i and
A2: i < len Gauge(C,n) and
A3: 1 <= j and
A4: j <= k and
A5: k <= width Gauge(C,n) and
A6: n > 0 and
A7: LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Lower_Arc L~Cage(C,n) =
  {Gauge(C,n)*(i,k)} and
A8: LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Upper_Arc L~Cage(C,n) =
  {Gauge(C,n)*(i,j)};
A9: L~Lower_Seq(C,n) = Lower_Arc L~Cage(C,n) by A6,JORDAN1G:64;
  L~Upper_Seq(C,n) = Upper_Arc L~Cage(C,n) by A6,JORDAN1G:63;
  hence thesis by A1,A2,A3,A4,A5,A7,A8,A9,Th14;
end;

theorem Th17:
  for C be Simple_closed_curve for i,j,k be Element of NAT st
  1 < i & i < len Gauge(C,n) & 1 <= j & j <= k & k <= width Gauge(C,n) &
  Gauge(C,n)*(i,k) in L~Lower_Seq(C,n) &
  Gauge(C,n)*(i,j) in L~Upper_Seq(C,n) holds
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Upper_Arc C
proof
  let C be Simple_closed_curve;
  let i,j,k be Element of NAT;
  assume that
A1: 1 < i and
A2: i < len Gauge(C,n) and
A3: 1 <= j and
A4: j <= k and
A5: k <= width Gauge(C,n) and
A6: Gauge(C,n)*(i,k) in L~Lower_Seq(C,n) and
A7: Gauge(C,n)*(i,j) in L~Upper_Seq(C,n);
  consider j1,k1 be Element of NAT such that
A8: j <= j1 and
A9: j1 <= k1 and
A10: k1 <= k and
A11: LSeg(Gauge(C,n)*(i,j1),Gauge(C,n)*(i,k1)) /\ L~Upper_Seq(C,n) =
  {Gauge(C,n)*(i,j1)} and
A12: LSeg(Gauge(C,n)*(i,j1),Gauge(C,n)*(i,k1)) /\ L~Lower_Seq(C,n) =
  {Gauge(C,n)*(i,k1)} by A1,A2,A3,A4,A5,A6,A7,JORDAN15:19;
A13: 1 <= j1 by A3,A8,XXREAL_0:2;
  k1 <= width Gauge(C,n) by A5,A10,XXREAL_0:2;
  then LSeg(Gauge(C,n)*(i,j1),Gauge(C,n)*(i,k1)) meets Upper_Arc C
  by A1,A2,A9,A11,A12,A13,Th13;
  hence thesis by A1,A2,A3,A5,A8,A9,A10,JORDAN15:7,XBOOLE_1:63;
end;

theorem Th18:
  for C be Simple_closed_curve for i,j,k be Element of NAT st
  1 < i & i < len Gauge(C,n) & 1 <= j & j <= k & k <= width Gauge(C,n) &
  Gauge(C,n)*(i,k) in L~Lower_Seq(C,n) &
  Gauge(C,n)*(i,j) in L~Upper_Seq(C,n) holds
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Lower_Arc C
proof
  let C be Simple_closed_curve;
  let i,j,k be Element of NAT;
  assume that
A1: 1 < i and
A2: i < len Gauge(C,n) and
A3: 1 <= j and
A4: j <= k and
A5: k <= width Gauge(C,n) and
A6: Gauge(C,n)*(i,k) in L~Lower_Seq(C,n) and
A7: Gauge(C,n)*(i,j) in L~Upper_Seq(C,n);
  consider j1,k1 be Element of NAT such that
A8: j <= j1 and
A9: j1 <= k1 and
A10: k1 <= k and
A11: LSeg(Gauge(C,n)*(i,j1),Gauge(C,n)*(i,k1)) /\ L~Upper_Seq(C,n) =
  {Gauge(C,n)*(i,j1)} and
A12: LSeg(Gauge(C,n)*(i,j1),Gauge(C,n)*(i,k1)) /\ L~Lower_Seq(C,n) =
  {Gauge(C,n)*(i,k1)} by A1,A2,A3,A4,A5,A6,A7,JORDAN15:19;
A13: 1 <= j1 by A3,A8,XXREAL_0:2;
  k1 <= width Gauge(C,n) by A5,A10,XXREAL_0:2;
  then LSeg(Gauge(C,n)*(i,j1),Gauge(C,n)*(i,k1)) meets Lower_Arc C
  by A1,A2,A9,A11,A12,A13,Th14;
  hence thesis by A1,A2,A3,A5,A8,A9,A10,JORDAN15:7,XBOOLE_1:63;
end;

theorem Th19:
  for C be Simple_closed_curve for i,j,k be Element of NAT st
  1 < i & i < len Gauge(C,n) &
  1 <= j & j <= k & k <= width Gauge(C,n) & n > 0 &
  Gauge(C,n)*(i,k) in Lower_Arc L~Cage(C,n) &
  Gauge(C,n)*(i,j) in Upper_Arc L~Cage(C,n) holds
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Upper_Arc C
proof
  let C be Simple_closed_curve;
  let i,j,k be Element of NAT;
  assume that
A1: 1 < i and
A2: i < len Gauge(C,n) and
A3: 1 <= j and
A4: j <= k and
A5: k <= width Gauge(C,n) and
A6: n > 0 and
A7: Gauge(C,n)*(i,k) in Lower_Arc L~Cage(C,n) and
A8: Gauge(C,n)*(i,j) in Upper_Arc L~Cage(C,n);
A9: L~Lower_Seq(C,n) = Lower_Arc L~Cage(C,n) by A6,JORDAN1G:64;
  L~Upper_Seq(C,n) = Upper_Arc L~Cage(C,n) by A6,JORDAN1G:63;
  hence thesis by A1,A2,A3,A4,A5,A7,A8,A9,Th17;
end;

theorem Th20:
  for C be Simple_closed_curve for i,j,k be Element of NAT st
  1 < i & i < len Gauge(C,n) &
  1 <= j & j <= k & k <= width Gauge(C,n) & n > 0 &
  Gauge(C,n)*(i,k) in Lower_Arc L~Cage(C,n) &
  Gauge(C,n)*(i,j) in Upper_Arc L~Cage(C,n) holds
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Lower_Arc C
proof
  let C be Simple_closed_curve;
  let i,j,k be Element of NAT;
  assume that
A1: 1 < i and
A2: i < len Gauge(C,n) and
A3: 1 <= j and
A4: j <= k and
A5: k <= width Gauge(C,n) and
A6: n > 0 and
A7: Gauge(C,n)*(i,k) in Lower_Arc L~Cage(C,n) and
A8: Gauge(C,n)*(i,j) in Upper_Arc L~Cage(C,n);
A9: L~Lower_Seq(C,n) = Lower_Arc L~Cage(C,n) by A6,JORDAN1G:64;
  L~Upper_Seq(C,n) = Upper_Arc L~Cage(C,n) by A6,JORDAN1G:63;
  hence thesis by A1,A2,A3,A4,A5,A7,A8,A9,Th18;
end;

theorem Th21:
  for C be Simple_closed_curve for i1,i2,j,k be Element of NAT st
  1 < i1 & i1 <= i2 & i2 < len Gauge(C,n) &
  1 <= j & j <= k & k <= width Gauge(C,n) &
  (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
  LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) /\ L~Upper_Seq(C,n) =
  {Gauge(C,n)*(i1,j)} & (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
  LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) /\ L~Lower_Seq(C,n) =
  {Gauge(C,n)*(i2,k)} holds (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
  LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) meets Upper_Arc C
proof
  let C be Simple_closed_curve;
  let i1,i2,j,k be Element of NAT;
  set G = Gauge(C,n);
  set pio = LSeg(G*(i1,j),G*(i1,k));
  set poz = LSeg(G*(i1,k),G*(i2,k));
  set US = Upper_Seq(C,n);
  set LS = Lower_Seq(C,n);
  assume that
A1: 1 < i1 and
A2: i1 <= i2 and
A3: i2 < len G and
A4: 1 <= j and
A5: j <= k and
A6: k <= width G and
A7: (pio \/ poz) /\ L~US = {G*(i1,j)} and
A8: (pio \/ poz) /\ L~LS = {G*(i2,k)} and
A9: (pio \/ poz) misses Upper_Arc C;
  set UA = Upper_Arc C;
  set Wmin = W-min L~Cage(C,n);
  set Emax = E-max L~Cage(C,n);
  set Wbo = W-bound L~Cage(C,n);
  set Ebo = E-bound L~Cage(C,n);
  set Gik = G*(i2,k);
  set Gij = G*(i1,j);
  set Gi1k = G*(i1,k);
A10: i1 < len G by A2,A3,XXREAL_0:2;
A11: 1 < i2 by A1,A2,XXREAL_0:2;
A12: L~<*Gij,Gi1k,Gik*> = poz \/ pio by TOPREAL3:23;
  Gik in {Gik} by TARSKI:def 1;
  then
A13: Gik in L~LS by A8,XBOOLE_0:def 4;
  Gij in {Gij} by TARSKI:def 1;
  then
A14: Gij in L~US by A7,XBOOLE_0:def 4;
A15: j <= width G by A5,A6,XXREAL_0:2;
A16: 1 <= k by A4,A5,XXREAL_0:2;
A17: [i1,j] in Indices G by A1,A4,A10,A15,MATRIX_1:37;
A18: [i2,k] in Indices G by A3,A6,A11,A16,MATRIX_1:37;
A19: [i1,k] in Indices G by A1,A6,A10,A16,MATRIX_1:37;
  set go = R_Cut(US,Gij);
  set do = L_Cut(LS,Gik);
A20: len G = width G by JORDAN8:def 1;
A21: len US >= 3 by JORDAN1E:19;
  then len US >= 1 by XXREAL_0:2;
  then 1 in dom US by FINSEQ_3:27;
  then
A22: US.1 = US/.1 by PARTFUN1:def 8
    .= Wmin by JORDAN1F:5;
A23: Wmin`1 = Wbo by EUCLID:56
    .= G*(1,k)`1 by A6,A16,A20,JORDAN1A:94;
  len G >= 4 by JORDAN8:13;
  then
A24: len G >= 1 by XXREAL_0:2;
  then
A25: [1,k] in Indices G by A6,A16,MATRIX_1:37;
  then
A26: Gij <> US.1 by A1,A17,A22,A23,JORDAN1G:7;
  then reconsider go as being_S-Seq FinSequence of TOP-REAL 2
  by A14,JORDAN3:70;
A27: [1,j] in Indices G by A4,A15,A24,MATRIX_1:37;
A28: len LS >= 1+2 by JORDAN1E:19;
  then
A29: len LS >= 1 by XXREAL_0:2;
  then
A30: 1 in dom LS by FINSEQ_3:27;
  len LS in dom LS by A29,FINSEQ_3:27;
  then
A31: LS.len LS = LS/.len LS by PARTFUN1:def 8
    .= Wmin by JORDAN1F:8;
  Wmin`1 = Wbo by EUCLID:56
    .= G*(1,k)`1 by A6,A16,A20,JORDAN1A:94;
  then
A32: Gik <> LS.len LS by A1,A2,A18,A25,A31,JORDAN1G:7;
  then reconsider do as being_S-Seq FinSequence of TOP-REAL 2
  by A13,JORDAN3:69;
A33: [len G,k] in Indices G by A6,A16,A24,MATRIX_1:37;
A34: LS.1 = LS/.1 by A30,PARTFUN1:def 8
    .= Emax by JORDAN1F:6;
  Emax`1 = Ebo by EUCLID:56
    .= G*(len G,k)`1 by A6,A16,A20,JORDAN1A:92;
  then
A35: Gik <> LS.1 by A3,A18,A33,A34,JORDAN1G:7;
A36: len go >= 1+1 by TOPREAL1:def 10;
A37: Gij in rng US by A1,A4,A10,A14,A15,JORDAN1G:4,JORDAN1J:40;
  then
A38: go is_sequence_on G by JORDAN1G:4,JORDAN1J:38;
A39: len do >= 1+1 by TOPREAL1:def 10;
A40: Gik in rng LS by A3,A6,A11,A13,A16,JORDAN1G:5,JORDAN1J:40;
  then
A41: do is_sequence_on G by JORDAN1G:5,JORDAN1J:39;
  reconsider go as non constant s.c.c.
  (being_S-Seq FinSequence of TOP-REAL 2) by A36,A38,JGRAPH_1:16,JORDAN8:8;
  reconsider do as non constant s.c.c.
  (being_S-Seq FinSequence of TOP-REAL 2) by A39,A41,JGRAPH_1:16,JORDAN8:8;
A42: len go > 1 by A36,NAT_1:13;
  then
A43: len go in dom go by FINSEQ_3:27;
  then
A44: go/.len go = go.len go by PARTFUN1:def 8
    .= Gij by A14,JORDAN3:59;
  len do >= 1 by A39,XXREAL_0:2;
  then 1 in dom do by FINSEQ_3:27;
  then
A45: do/.1 = do.1 by PARTFUN1:def 8
    .= Gik by A13,JORDAN3:58;
  reconsider m = len go - 1 as Element of NAT by A43,FINSEQ_3:28;
A46: m+1 = len go;
  then
A47: len go-'1 = m by NAT_D:34;
A48: LSeg(go,m) c= L~go by TOPREAL3:26;
A49: L~go c= L~US by A14,JORDAN3:76;
  then LSeg(go,m) c= L~US by A48,XBOOLE_1:1;
  then
A50: LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> c= {Gij} by A7,A12,XBOOLE_1:26;
  m >= 1 by A36,XREAL_1:21;
  then
A51: LSeg(go,m) = LSeg(go/.m,Gij) by A44,A46,TOPREAL1:def 5;
  {Gij} c= LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*>
  proof
    let x be set;
    assume x in {Gij};
    then
A52: x = Gij by TARSKI:def 1;
A53: Gij in LSeg(go,m) by A51,RLTOPSP1:69;
    Gij in LSeg(Gij,Gi1k) by RLTOPSP1:69;
    then Gij in LSeg(Gij,Gi1k) \/ LSeg(Gi1k,Gik) by XBOOLE_0:def 3;
    then Gij in L~<*Gij,Gi1k,Gik*> by SPRECT_1:10;
    hence thesis by A52,A53,XBOOLE_0:def 4;
  end;
  then
A54: LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> = {Gij} by A50,XBOOLE_0:def 10;
A55: LSeg(do,1) c= L~do by TOPREAL3:26;
A56: L~do c= L~LS by A13,JORDAN3:77;
  then LSeg(do,1) c= L~LS by A55,XBOOLE_1:1;
  then
A57: LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*> c= {Gik} by A8,A12,XBOOLE_1:26;
A58: LSeg(do,1) = LSeg(Gik,do/.(1+1)) by A39,A45,TOPREAL1:def 5;
  {Gik} c= LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*>
  proof
    let x be set;
    assume x in {Gik};
    then
A59: x = Gik by TARSKI:def 1;
A60: Gik in LSeg(do,1) by A58,RLTOPSP1:69;
    Gik in LSeg(Gi1k,Gik) by RLTOPSP1:69;
    then Gik in LSeg(Gij,Gi1k) \/ LSeg(Gi1k,Gik) by XBOOLE_0:def 3;
    then Gik in L~<*Gij,Gi1k,Gik*> by SPRECT_1:10;
    hence thesis by A59,A60,XBOOLE_0:def 4;
  end;
  then
A61: L~<*Gij,Gi1k,Gik*> /\ LSeg(do,1) = {Gik} by A57,XBOOLE_0:def 10;
A62: go/.1 = US/.1 by A14,SPRECT_3:39
    .= Wmin by JORDAN1F:5;
  then
A63: go/.1 = LS/.len LS by JORDAN1F:8
    .= do/.len do by A13,JORDAN1J:35;
A64: rng go c= L~go by A36,SPPOL_2:18;
A65: rng do c= L~do by A39,SPPOL_2:18;
A66: {go/.1} c= L~go /\ L~do
  proof
    let x be set;
    assume x in {go/.1};
    then
A67: x = go/.1 by TARSKI:def 1;
    then
A68: x in rng go by FINSEQ_6:46;
    x in rng do by A63,A67,REVROT_1:3;
    hence thesis by A64,A65,A68,XBOOLE_0:def 4;
  end;
A69: LS.1 = LS/.1 by A30,PARTFUN1:def 8
    .= Emax by JORDAN1F:6;
A70: [len G,j] in Indices G by A4,A15,A24,MATRIX_1:37;
  L~go /\ L~do c= {go/.1}
  proof
    let x be set;
    assume
A71: x in L~go /\ L~do;
    then
A72: x in L~go by XBOOLE_0:def 4;
A73: x in L~do by A71,XBOOLE_0:def 4;
    then x in L~US /\ L~LS by A49,A56,A72,XBOOLE_0:def 4;
    then x in {Wmin,Emax} by JORDAN1E:20;
    then
A74: x = Wmin or x = Emax by TARSKI:def 2;
    now
      assume x = Emax;
      then
A75:  Emax = Gik by A13,A69,A73,JORDAN1E:11;
      G*(len G,j)`1 = Ebo by A4,A15,A20,JORDAN1A:92;
      then Emax`1 <> Ebo by A3,A18,A70,A75,JORDAN1G:7;
      hence contradiction by EUCLID:56;
    end;
    hence thesis by A62,A74,TARSKI:def 1;
  end;
  then
A76: L~go /\ L~do = {go/.1} by A66,XBOOLE_0:def 10;
  set W2 = go/.2;
A77: 2 in dom go by A36,FINSEQ_3:27;
A78: now
    assume Gij`1 = Wbo;
    then G*(1,j)`1 = G*(i1,j)`1 by A4,A15,A20,JORDAN1A:94;
    hence contradiction by A1,A17,A27,JORDAN1G:7;
  end;
  go = mid(US,1,Gij..US) by A37,JORDAN1G:57
    .= US|(Gij..US) by A37,FINSEQ_4:31,FINSEQ_6:122;
  then
A79: W2 = US/.2 by A77,FINSEQ_4:85;
A80: Wmin in rng go by A62,FINSEQ_6:46;
  set pion = <*Gij,Gi1k,Gik*>;
A81: now
    let n be Element of NAT;
    assume n in dom pion;
    then n in {1,2,3} by FINSEQ_3:1,30;
    then n = 1 or n = 2 or n = 3 by ENUMSET1:def 1;
    hence
    ex i,j be Element of NAT st [i,j] in Indices G & pion/.n = G*(i,j)
    by A17,A18,A19,FINSEQ_4:27;
  end;
A82: Gi1k`1 = G*(i1,1)`1 by A1,A6,A10,A16,GOBOARD5:3
    .= Gij`1 by A1,A4,A10,A15,GOBOARD5:3;
  Gi1k`2 = G*(1,k)`2 by A1,A6,A10,A16,GOBOARD5:2
    .= Gik`2 by A3,A6,A11,A16,GOBOARD5:2;
  then
A83: Gi1k = |[Gij`1,Gik`2]| by A82,EUCLID:57;
A84: Gi1k in pio by RLTOPSP1:69;
A85: Gi1k in poz by RLTOPSP1:69;
  now per cases;
    suppose Gik`1 <> Gij`1 & Gik`2 <> Gij`2;
      then pion is being_S-Seq by A83,TOPREAL3:41;
      then consider pion1 be FinSequence of TOP-REAL 2 such that
A86:  pion1 is_sequence_on G and
A87:  pion1 is being_S-Seq and
A88:  L~pion = L~pion1 and
A89:  pion/.1 = pion1/.1 and
A90:  pion/.len pion = pion1/.len pion1 and
A91:  len pion <= len pion1 by A81,GOBOARD3:2;
      reconsider pion1 as being_S-Seq FinSequence of TOP-REAL 2 by A87;
      set godo = go^'pion1^'do;
A92:  Gi1k`1 = G*(i1,1)`1 by A1,A6,A10,A16,GOBOARD5:3
        .= Gij`1 by A1,A4,A10,A15,GOBOARD5:3;
A93:  Gi1k`1 <= Gik`1 by A1,A2,A3,A6,A16,JORDAN1A:39;
      then
A94:  W-bound poz = Gi1k`1 by SPRECT_1:62;
A95:  W-bound pio = Gij`1 by A92,SPRECT_1:62;
      W-bound (poz \/ pio) = min(W-bound poz, W-bound pio) by SPRECT_1:54
        .= Gij`1 by A92,A94,A95;
      then
A96:  W-bound L~pion1 = Gij`1 by A88,TOPREAL3:23;
A97:  1+1 <= len Cage(C,n) by GOBOARD7:36,XXREAL_0:2;
A98:  1+1 <= len Rotate(Cage(C,n),Wmin) by GOBOARD7:36,XXREAL_0:2;
      len (go^'pion1) >= len go by TOPREAL8:7;
      then
A99:  len (go^'pion1) >= 1+1 by A36,XXREAL_0:2;
      then
A100: len (go^'pion1) > 1+0 by NAT_1:13;
A101: len godo >= len (go^'pion1) by TOPREAL8:7;
      then
A102: 1+1 <= len godo by A99,XXREAL_0:2;
A103: US is_sequence_on G by JORDAN1G:4;
A104: go/.len go = pion1/.1 by A44,A89,FINSEQ_4:27;
      then
A105: go^'pion1 is_sequence_on G by A38,A86,TOPREAL8:12;
A106: (go^'pion1)/.len (go^'pion1) = pion/.len pion by A90,GRAPH_2:58
        .= pion/.3 by FINSEQ_1:62
        .= do/.1 by A45,FINSEQ_4:27;
      then
A107: godo is_sequence_on G by A41,A105,TOPREAL8:12;
      LSeg(pion1,1) c= L~pion by A88,TOPREAL3:26;
      then
A108: LSeg(go,len go-'1) /\ LSeg(pion1,1) c={Gij} by A47,A54,XBOOLE_1:27;
      len pion1 >= 2+1 by A91,FINSEQ_1:62;
      then
A109: len pion1 > 1+1 by NAT_1:13;
      {Gij} c= LSeg(go,m) /\ LSeg(pion1,1)
      proof
        let x be set;
        assume x in {Gij};
        then
A110:   x = Gij by TARSKI:def 1;
A111:   Gij in LSeg(go,m) by A51,RLTOPSP1:69;
        Gij in LSeg(pion1,1) by A44,A104,A109,TOPREAL1:27;
        hence thesis by A110,A111,XBOOLE_0:def 4;
      end;
      then LSeg(go,len go-'1) /\ LSeg(pion1,1) = { go/.len go }
      by A44,A47,A108,XBOOLE_0:def 10;
      then
A112: go^'pion1 is unfolded by A104,TOPREAL8:34;
      len pion1 >= 2+1 by A91,FINSEQ_1:62;
      then
A113: len pion1-2 >= 0 by XREAL_1:21;
      len (go^'pion1)+1-1 = len go+len pion1-1 by GRAPH_2:13;
      then len (go^'pion1)-1 = len go + (len pion1-2)
        .= len go + (len pion1-'2) by A113,XREAL_0:def 2;
      then
A114: len (go^'pion1)-'1 = len go + (len pion1-'2) by XREAL_0:def 2;
A115: len pion1-1 >= 1 by A109,XREAL_1:21;
      then
A116: len pion1-'1 = len pion1-1 by XREAL_0:def 2;
A117: len pion1-'2+1 = len pion1-2+1 by A113,XREAL_0:def 2
        .= len pion1-'1 by A115,XREAL_0:def 2;
      len pion1-1+1 <= len pion1;
      then
A118: len pion1-'1 < len pion1 by A116,NAT_1:13;
      LSeg(pion1,len pion1-'1) c= L~pion by A88,TOPREAL3:26;
      then
A119: LSeg(pion1,len pion1-'1) /\ LSeg(do,1) c= {Gik} by A61,XBOOLE_1:27;
      {Gik} c= LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
      proof
        let x be set;
        assume x in {Gik};
        then
A120:   x = Gik by TARSKI:def 1;
A121:   Gik in LSeg(do,1) by A58,RLTOPSP1:69;
        pion1/.(len pion1-'1+1) = pion/.3 by A90,A116,FINSEQ_1:62
          .= Gik by FINSEQ_4:27;
        then Gik in LSeg(pion1,len pion1-'1) by A115,A116,TOPREAL1:27;
        hence thesis by A120,A121,XBOOLE_0:def 4;
      end;
      then LSeg(pion1,len pion1-'1) /\ LSeg(do,1) = {Gik}
      by A119,XBOOLE_0:def 10;
      then
A122: LSeg(go^'pion1,len go+(len pion1-'2)) /\ LSeg(do,1) =
      {(go^'pion1)/.len (go^'pion1)} by A45,A104,A106,A117,A118,TOPREAL8:31;
A123: (go^'pion1) is non trivial by A99,REALSET1:13;
A124: rng pion1 c= L~pion1 by A109,SPPOL_2:18;
A125: {pion1/.1} c= L~go /\ L~pion1
      proof
        let x be set;
        assume x in {pion1/.1};
        then
A126:   x = pion1/.1 by TARSKI:def 1;
        then
A127:   x in rng go by A104,REVROT_1:3;
        x in rng pion1 by A126,FINSEQ_6:46;
        hence thesis by A64,A124,A127,XBOOLE_0:def 4;
      end;
      L~go /\ L~pion1 c= {pion1/.1}
      proof
        let x be set;
        assume
A128:   x in L~go /\ L~pion1;
        then
A129:   x in L~go by XBOOLE_0:def 4;
        x in L~pion1 by A128,XBOOLE_0:def 4;
        hence thesis by A7,A12,A44,A49,A88,A104,A129,XBOOLE_0:def 4;
      end;
      then
A130: L~go /\ L~pion1 = {pion1/.1} by A125,XBOOLE_0:def 10;
      then
A131: (go^'pion1) is s.n.c. by A104,JORDAN1J:54;
      rng go /\ rng pion1 c= {pion1/.1} by A64,A124,A130,XBOOLE_1:27;
      then
A132: go^'pion1 is one-to-one by JORDAN1J:55;
A133: pion/.len pion = pion/.3 by FINSEQ_1:62
        .= do/.1 by A45,FINSEQ_4:27;
A134: {pion1/.len pion1} c= L~do /\ L~pion1
      proof
        let x be set;
        assume x in {pion1/.len pion1};
        then
A135:   x = pion1/.len pion1 by TARSKI:def 1;
        then
A136:   x in rng do by A90,A133,FINSEQ_6:46;
        x in rng pion1 by A135,REVROT_1:3;
        hence thesis by A65,A124,A136,XBOOLE_0:def 4;
      end;
      L~do /\ L~pion1 c= {pion1/.len pion1}
      proof
        let x be set;
        assume
A137:   x in L~do /\ L~pion1;
        then
A138:   x in L~do by XBOOLE_0:def 4;
        x in L~pion1 by A137,XBOOLE_0:def 4;
        hence thesis by A8,A12,A45,A56,A88,A90,A133,A138,XBOOLE_0:def 4;
      end;
      then
A139: L~do /\ L~pion1 = {pion1/.len pion1} by A134,XBOOLE_0:def 10;
A140: L~(go^'pion1) /\ L~do = (L~go \/ L~pion1) /\ L~do by A104,TOPREAL8:35
        .= {go/.1} \/ {do/.1} by A76,A90,A133,A139,XBOOLE_1:23
        .= {(go^'pion1)/.1} \/ {do/.1} by GRAPH_2:57
        .= {(go^'pion1)/.1,do/.1} by ENUMSET1:41;
      do/.len do = (go^'pion1)/.1 by A63,GRAPH_2:57;
      then reconsider godo as non constant standard special_circular_sequence
      by A102,A106,A107,A112,A114,A122,A123,A131,A132,A140,JORDAN8:7,8
,TOPREAL8:11,33,34;
A141: UA is_an_arc_of W-min C,E-max C by JORDAN6:def 8;
      then
A142: UA is connected by JORDAN6:11;
A143: W-min C in UA by A141,TOPREAL1:4;
A144: E-max C in UA by A141,TOPREAL1:4;
      set ff = Rotate(Cage(C,n),Wmin);
      Wmin in rng Cage(C,n) by SPRECT_2:47;
      then
A145: ff/.1 = Wmin by FINSEQ_6:98;
A146: L~ff = L~Cage(C,n) by REVROT_1:33;
      then (W-max L~ff)..ff > 1 by A145,SPRECT_5:23;
      then (N-min L~ff)..ff > 1 by A145,A146,SPRECT_5:24,XXREAL_0:2;
      then (N-max L~ff)..ff > 1 by A145,A146,SPRECT_5:25,XXREAL_0:2;
      then
A147: Emax..ff > 1 by A145,A146,SPRECT_5:26,XXREAL_0:2;
A148: now
        assume
A149:   Gij..US <= 1;
        Gij..US >= 1 by A37,FINSEQ_4:31;
        then Gij..US = 1 by A149,XXREAL_0:1;
        then Gij = US/.1 by A37,FINSEQ_5:41;
        hence contradiction by A22,A26,JORDAN1F:5;
      end;
A150: Cage(C,n) is_sequence_on G by JORDAN9:def 1;
      then
A151: ff is_sequence_on G by REVROT_1:34;
A152: right_cell(godo,1,G)\L~godo c= RightComp godo by A102,A107,JORDAN9:29;
A153: L~godo = L~(go^'pion1) \/ L~do by A106,TOPREAL8:35
        .= L~go \/ L~pion1 \/ L~do by A104,TOPREAL8:35;
A154: L~Cage(C,n) = L~US \/ L~LS by JORDAN1E:17;
      then
A155: L~US c= L~Cage(C,n) by XBOOLE_1:7;
A156: L~LS c= L~Cage(C,n) by A154,XBOOLE_1:7;
A157: L~go c=L~Cage(C,n) by A49,A155,XBOOLE_1:1;
A158: L~do c=L~Cage(C,n) by A56,A156,XBOOLE_1:1;
A159: W-min C in C by SPRECT_1:15;
A160: now
        assume W-min C in L~godo;
        then
A161:   W-min C in L~go \/ L~pion1 or W-min C in L~do by A153,XBOOLE_0:def 3;
        per cases by A161,XBOOLE_0:def 3;
        suppose W-min C in L~go;
          then C meets L~Cage(C,n) by A157,A159,XBOOLE_0:3;
          hence contradiction by JORDAN10:5;
        end;
        suppose W-min C in L~pion1;
          hence contradiction by A9,A12,A88,A143,XBOOLE_0:3;
        end;
        suppose W-min C in L~do;
          then C meets L~Cage(C,n) by A158,A159,XBOOLE_0:3;
          hence contradiction by JORDAN10:5;
        end;
      end;
      right_cell(Rotate(Cage(C,n),Wmin),1) =
      right_cell(ff,1,GoB ff) by A98,JORDAN1H:29
        .= right_cell(ff,1,GoB Cage(C,n)) by REVROT_1:28
        .= right_cell(ff,1,G) by JORDAN1H:52
        .= right_cell(ff-:Emax,1,G) by A147,A151,JORDAN1J:53
        .= right_cell(US,1,G) by JORDAN1E:def 1
        .= right_cell(R_Cut(US,Gij),1,G) by A37,A103,A148,JORDAN1J:52
        .= right_cell(go^'pion1,1,G) by A42,A105,JORDAN1J:51
        .= right_cell(godo,1,G) by A100,A107,JORDAN1J:51;
      then W-min C in right_cell(godo,1,G) by JORDAN1I:8;
      then
A162: W-min C in right_cell(godo,1,G)\L~godo by A160,XBOOLE_0:def 5;
A163: godo/.1 = (go^'pion1)/.1 by GRAPH_2:57
        .= Wmin by A62,GRAPH_2:57;
A164: len US >= 2 by A21,XXREAL_0:2;
A165: godo/.2 = (go^'pion1)/.2 by A99,GRAPH_2:61
        .= US/.2 by A36,A79,GRAPH_2:61
        .= (US^'LS)/.2 by A164,GRAPH_2:61
        .= Rotate(Cage(C,n),Wmin)/.2 by JORDAN1E:15;
A166: L~go \/ L~do is compact by COMPTS_1:19;
      Wmin in L~go \/ L~do by A64,A80,XBOOLE_0:def 3;
      then
A167: W-min (L~go \/ L~do) = Wmin by A157,A158,A166,JORDAN1J:21,XBOOLE_1:8;
A168: (W-min (L~go \/ L~do))`1 = W-bound (L~go \/ L~do) by EUCLID:56;
A169: Wmin`1 = Wbo by EUCLID:56;
      Gij`1 >= Wbo by A14,A155,PSCOMP_1:71;
      then Gij`1 > Wbo by A78,XXREAL_0:1;
      then W-min (L~go\/L~do\/L~pion1) = W-min (L~go \/ L~do)
      by A96,A166,A167,A168,A169,JORDAN1J:33;
      then
A170: W-min L~godo = Wmin by A153,A167,XBOOLE_1:4;
A171: rng godo c= L~godo by A99,A101,SPPOL_2:18,XXREAL_0:2;
      2 in dom godo by A102,FINSEQ_3:27;
      then
A172: godo/.2 in rng godo by PARTFUN2:4;
      godo/.2 in W-most L~Cage(C,n) by A165,JORDAN1I:27;
      then (godo/.2)`1 = (W-min L~godo)`1 by A170,PSCOMP_1:88
        .= W-bound L~godo by EUCLID:56;
      then godo/.2 in W-most L~godo by A171,A172,SPRECT_2:16;
      then Rotate(godo,W-min L~godo)/.2 in W-most L~godo
      by A163,A170,FINSEQ_6:95;
      then reconsider godo as clockwise_oriented non constant standard
      special_circular_sequence by JORDAN1I:27;
      len US in dom US by FINSEQ_5:6;
      then
A173: US.len US = US/.len US by PARTFUN1:def 8
        .= Emax by JORDAN1F:7;
A174: east_halfline E-max C misses L~go
      proof
        assume east_halfline E-max C meets L~go;
        then consider p be set such that
A175:   p in east_halfline E-max C and
A176:   p in L~go by XBOOLE_0:3;
        reconsider p as Point of TOP-REAL 2 by A175;
        p in L~US by A49,A176;
        then p in east_halfline E-max C /\ L~Cage(C,n)
        by A155,A175,XBOOLE_0:def 4;
        then
A177:   p`1 = Ebo by JORDAN1A:104,PSCOMP_1:111;
        then
A178:   p = Emax by A49,A176,JORDAN1J:46;
        then Emax = Gij by A14,A173,A176,JORDAN1J:43;
        then Gij`1 = G*(len G,k)`1 by A6,A16,A20,A177,A178,JORDAN1A:92;
        hence contradiction by A2,A3,A17,A33,JORDAN1G:7;
      end;
      now
        assume east_halfline E-max C meets L~godo;
        then
A179:   east_halfline E-max C meets (L~go \/ L~pion1) or
        east_halfline E-max C meets L~do by A153,XBOOLE_1:70;
        per cases by A179,XBOOLE_1:70;
        suppose east_halfline E-max C meets L~go;
          hence contradiction by A174;
        end;
        suppose east_halfline E-max C meets L~pion1;
          then consider p be set such that
A180:     p in east_halfline E-max C and
A181:     p in L~pion1 by XBOOLE_0:3;
          reconsider p as Point of TOP-REAL 2 by A180;
A182:     now per cases by A12,A88,A181,XBOOLE_0:def 3;
            suppose p in poz;
              hence p`1 <= Gik`1 by A93,TOPREAL1:9;
            end;
            suppose p in pio;
              hence p`1 <= Gik`1 by A92,A93,GOBOARD7:5;
            end;
          end;
          i2+1 <= len G by A3,NAT_1:13;
          then i2 <= len G-1 by XREAL_1:21;
          then
A183:     i2 <= len G-'1 by XREAL_0:def 2;
          len G-'1 <= len G by NAT_D:35;
          then Gik`1 <= G*(len G-'1,1)`1 by A6,A11,A16,A20,A24,A183,JORDAN1A:39
;
          then p`1 <= G*(len G-'1,1)`1 by A182,XXREAL_0:2;
          then p`1 <= E-bound C by A24,JORDAN8:15;
          then
A184:     p`1 <= (E-max C)`1 by EUCLID:56;
          p`1 >= (E-max C)`1 by A180,TOPREAL1:def 13;
          then
A185:     p`1 = (E-max C)`1 by A184,XXREAL_0:1;
          p`2 = (E-max C)`2 by A180,TOPREAL1:def 13;
          then p = E-max C by A185,TOPREAL3:11;
          hence contradiction by A9,A12,A88,A144,A181,XBOOLE_0:3;
        end;
        suppose east_halfline E-max C meets L~do;
          then consider p be set such that
A186:     p in east_halfline E-max C and
A187:     p in L~do by XBOOLE_0:3;
          reconsider p as Point of TOP-REAL 2 by A186;
          p in L~LS by A56,A187;
          then p in east_halfline E-max C /\ L~Cage(C,n)
          by A156,A186,XBOOLE_0:def 4;
          then
A188:     p`1 = Ebo by JORDAN1A:104,PSCOMP_1:111;
A189:     (E-max C)`2 = p`2 by A186,TOPREAL1:def 13;
          set RC = Rotate(Cage(C,n),Emax);
A190:     E-max C in right_cell(RC,1) by JORDAN1I:9;
A191:     1+1 <= len LS by A28,XXREAL_0:2;
          LS = RC-:Wmin by JORDAN1G:26;
          then
A192:     LSeg(LS,1) = LSeg(RC,1) by A191,SPPOL_2:9;
A193:     L~RC = L~Cage(C,n) by REVROT_1:33;
A194:     len RC = len Cage(C,n) by REVROT_1:14;
A195:     GoB RC = GoB Cage(C,n) by REVROT_1:28
            .= G by JORDAN1H:52;
A196:     Emax in rng Cage(C,n) by SPRECT_2:50;
A197:     RC is_sequence_on G by A150,REVROT_1:34;
A198:     RC/.1 = E-max L~RC by A193,A196,FINSEQ_6:98;
          consider ii,jj be Element of NAT such that
A199:     [ii,jj+1] in Indices G and
A200:     [ii,jj] in Indices G and
A201:     RC/.1 = G*(ii,jj+1) and
A202:     RC/.(1+1) = G*(ii,jj) by A97,A193,A194,A196,A197,FINSEQ_6:98
,JORDAN1I:25;
          consider jj2 be Element of NAT such that
A203:     1 <= jj2 and
A204:     jj2 <= width G and
A205:     Emax = G*(len G,jj2) by JORDAN1D:29;
A206:     len G >= 4 by JORDAN8:13;
          then len G >= 1 by XXREAL_0:2;
          then [len G,jj2] in Indices G by A203,A204,MATRIX_1:37;
          then
A207:     ii = len G by A193,A198,A199,A201,A205,GOBOARD1:21;
A208:     1 <= ii by A199,MATRIX_1:39;
A209:     ii <= len G by A199,MATRIX_1:39;
A210:     1 <= jj+1 by A199,MATRIX_1:39;
A211:     jj+1 <= width G by A199,MATRIX_1:39;
A212:     1 <= ii by A200,MATRIX_1:39;
A213:     ii <= len G by A200,MATRIX_1:39;
A214:     1 <= jj by A200,MATRIX_1:39;
A215:     jj <= width G by A200,MATRIX_1:39;
A216:     ii+1 <> ii;
          jj+1+1 <> jj;
          then
A217:     right_cell(RC,1) = cell(G,ii-'1,jj)
          by A97,A194,A195,A199,A200,A201,A202,A216,GOBOARD5:def 6;
A218:     ii-'1+1 = ii by A208,XREAL_1:237;
          ii-1 >= 4-1 by A206,A207,XREAL_1:11;
          then
A219:     ii-1 >= 1 by XXREAL_0:2;
          then
A220:     1 <= ii-'1 by XREAL_0:def 2;
A221:     G*(ii-'1,jj)`2 <= p`2 by A189,A190,A209,A211,A214,A217,A218,A219,
JORDAN9:19;
A222:     p`2 <= G*(ii-'1,jj+1)`2 by A189,A190,A209,A211,A214,A217,A218,A219,
JORDAN9:19;
A223:     ii-'1 < len G by A209,A218,NAT_1:13;
          then
A224:     G*(ii-'1,jj)`2 = G*(1,jj)`2 by A214,A215,A220,GOBOARD5:2
            .= G*(ii,jj)`2 by A212,A213,A214,A215,GOBOARD5:2;
A225:     G*(ii-'1,jj+1)`2 = G*(1,jj+1)`2 by A210,A211,A220,A223,GOBOARD5:2
            .= G*(ii,jj+1)`2 by A208,A209,A210,A211,GOBOARD5:2;
A226:     G*(len G,jj)`1 = Ebo by A20,A214,A215,JORDAN1A:92;
          Ebo = G*(len G,jj+1)`1 by A20,A210,A211,JORDAN1A:92;
          then p in LSeg(RC/.1,RC/.(1+1))
          by A188,A201,A202,A207,A221,A222,A224,A225,A226,GOBOARD7:8;
          then
A227:     p in LSeg(LS,1) by A97,A192,A194,TOPREAL1:def 5;
A228:     p in LSeg(do,Index(p,do)) by A187,JORDAN3:42;
A229:     do = mid(LS,Gik..LS,len LS) by A40,JORDAN1J:37;
A230:     1<=Gik..LS by A40,FINSEQ_4:31;
A231:     Gik..LS<=len LS by A40,FINSEQ_4:31;
          Gik..LS <> len LS by A32,A40,FINSEQ_4:29;
          then
A232:     Gik..LS < len LS by A231,XXREAL_0:1;
A233:     1<=Index(p,do) by A187,JORDAN3:41;
A234:     Index(p,do) < len do by A187,JORDAN3:41;
A235:     Index(Gik,LS)+1 = Gik..LS by A35,A40,JORDAN1J:56;
          consider t be Nat such that
A236:     t in dom LS and
A237:     LS.t = Gik by A40,FINSEQ_2:11;
A238:     1 <= t by A236,FINSEQ_3:27;
A239:     t <= len LS by A236,FINSEQ_3:27;
          1 < t by A35,A237,A238,XXREAL_0:1;
          then Index(Gik,LS)+1 = t by A237,A239,JORDAN3:45;
          then
A240:     len L_Cut(LS,Gik) = len LS-Index(Gik,LS) by A13,A237,JORDAN3:61;
          set tt = Index(p,do)+(Gik..LS)-'1;
A241:     1<=Index(Gik,LS) by A13,JORDAN3:41;
          0+Index(Gik,LS) < len LS by A13,JORDAN3:41;
          then
A242:     len LS-Index(Gik,LS) > 0 by XREAL_1:22;
          Index(p,do) < len LS-'Index(Gik,LS) by A234,A240,XREAL_0:def 2;
          then Index(p,do)+1 <= len LS-'Index(Gik,LS) by NAT_1:13;
          then Index(p,do) <= len LS-'Index(Gik,LS)-1 by XREAL_1:21;
          then Index(p,do) <= len LS-Index(Gik,LS)-1 by A242,XREAL_0:def 2;
          then Index(p,do) <= len LS-Gik..LS by A235;
          then Index(p,do) <= len LS-'Gik..LS by XREAL_0:def 2;
          then Index(p,do) < len LS-'(Gik..LS)+1 by NAT_1:13;
          then
A243:     LSeg(mid(LS,Gik..LS,len LS),Index(p,do)) =
          LSeg(LS,Index(p,do)+(Gik..LS)-'1) by A230,A232,A233,JORDAN4:31;
A244:     1+1 <= Gik..LS by A235,A241,XREAL_1:9;
          then Index(p,do)+Gik..LS >= 1+1+1 by A233,XREAL_1:9;
          then Index(p,do)+Gik..LS-1 >= 1+1+1-1 by XREAL_1:11;
          then
A245:     tt >= 1+1 by XREAL_0:def 2;
A246:     2 in dom LS by A191,FINSEQ_3:27;
          now per cases by A245,XXREAL_0:1;
            suppose tt > 1+1;
              then LSeg(LS,1) misses LSeg(LS,tt) by TOPREAL1:def 9;
              hence contradiction by A227,A228,A229,A243,XBOOLE_0:3;
            end;
            suppose
A247:         tt = 1+1;
              then LSeg(LS,1) /\ LSeg(LS,tt) = {LS/.2} by A28,TOPREAL1:def 8;
              then p in {LS/.2} by A227,A228,A229,A243,XBOOLE_0:def 4;
              then
A248:         p = LS/.2 by TARSKI:def 1;
              then
A249:         p..LS = 2 by A246,FINSEQ_5:44;
              1+1 = Index(p,do)+(Gik..LS)-1 by A247,XREAL_0:def 2;
              then 1+1+1 = Index(p,do)+(Gik..LS);
              then
A250:         Gik..LS = 2 by A233,A244,JORDAN1E:10;
              p in rng LS by A246,A248,PARTFUN2:4;
              then p = Gik by A40,A249,A250,FINSEQ_5:10;
              then Gik`1 = Ebo by A248,JORDAN1G:40;
              then Gik`1 = G*(len G,j)`1 by A4,A15,A20,JORDAN1A:92;
              hence contradiction by A3,A18,A70,JORDAN1G:7;
            end;
          end;
          hence contradiction;
        end;
      end;
      then east_halfline E-max C c= (L~godo)` by SUBSET_1:43;
      then consider W be Subset of TOP-REAL 2 such that
A251: W is_a_component_of (L~godo)` and
A252: east_halfline E-max C c= W by GOBOARD9:5;
      W is not Bounded by A252,JORDAN2C:16,129;
      then W is_outside_component_of L~godo by A251,JORDAN2C:def 4;
      then W c= UBD L~godo by JORDAN2C:27;
      then
A253: east_halfline E-max C c= UBD L~godo by A252,XBOOLE_1:1;
      E-max C in east_halfline E-max C by TOPREAL1:45;
      then E-max C in UBD L~godo by A253;
      then E-max C in LeftComp godo by GOBRD14:46;
      then UA meets L~godo by A142,A143,A144,A152,A162,JORDAN1J:36;
      then
A254: UA meets (L~go \/ L~pion1) or UA meets L~do by A153,XBOOLE_1:70;
A255: UA c= C by JORDAN6:76;
      now per cases by A254,XBOOLE_1:70;
        suppose UA meets L~go;
          then UA meets L~Cage(C,n) by A49,A155,XBOOLE_1:1,63;
          hence contradiction by A255,JORDAN10:5,XBOOLE_1:63;
        end;
        suppose UA meets L~pion1;
          hence contradiction by A9,A12,A88;
        end;
        suppose UA meets L~do;
          then UA meets L~Cage(C,n) by A56,A156,XBOOLE_1:1,63;
          hence contradiction by A255,JORDAN10:5,XBOOLE_1:63;
        end;
      end;
      hence contradiction;
    end;
    suppose Gik`1 = Gij`1;
      then
A256: i1 = i2 by A17,A18,JORDAN1G:7;
      then poz = {Gi1k} by RLTOPSP1:71;
      then poz c= pio by A84,ZFMISC_1:37;
      then pio \/ poz = pio by XBOOLE_1:12;
      hence contradiction by A1,A3,A4,A5,A6,A7,A8,A9,A256,Th13;
    end;
    suppose Gik`2 = Gij`2;
      then
A257: j = k by A17,A18,JORDAN1G:6;
      then pio = {Gi1k} by RLTOPSP1:71;
      then pio c= poz by A85,ZFMISC_1:37;
      then pio \/ poz = poz by XBOOLE_1:12;
      hence contradiction by A1,A2,A3,A4,A6,A7,A8,A9,A257,JORDAN15:39;
    end;
  end;
  hence contradiction;
end;

theorem Th22:
  for C be Simple_closed_curve for i1,i2,j,k be Element of NAT st
  1 < i1 & i1 <= i2 & i2 < len Gauge(C,n) &
  1 <= j & j <= k & k <= width Gauge(C,n) &
  (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
  LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) /\ L~Upper_Seq(C,n) =
  {Gauge(C,n)*(i1,j)} & (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
  LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) /\ L~Lower_Seq(C,n) =
  {Gauge(C,n)*(i2,k)} holds (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
  LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) meets Lower_Arc C
proof
  let C be Simple_closed_curve;
  let i1,i2,j,k be Element of NAT;
  set G = Gauge(C,n);
  set pio = LSeg(G*(i1,j),G*(i1,k));
  set poz = LSeg(G*(i1,k),G*(i2,k));
  set US = Upper_Seq(C,n);
  set LS = Lower_Seq(C,n);
  assume that
A1: 1 < i1 and
A2: i1 <= i2 and
A3: i2 < len G and
A4: 1 <= j and
A5: j <= k and
A6: k <= width G and
A7: (pio \/ poz) /\ L~US = {G*(i1,j)} and
A8: (pio \/ poz) /\ L~LS = {G*(i2,k)} and
A9: (pio \/ poz) misses Lower_Arc C;
  set UA = Lower_Arc C;
  set Wmin = W-min L~Cage(C,n);
  set Emax = E-max L~Cage(C,n);
  set Wbo = W-bound L~Cage(C,n);
  set Ebo = E-bound L~Cage(C,n);
  set Gik = G*(i2,k);
  set Gij = G*(i1,j);
  set Gi1k = G*(i1,k);
A10: i1 < len G by A2,A3,XXREAL_0:2;
A11: 1 < i2 by A1,A2,XXREAL_0:2;
A12: L~<*Gij,Gi1k,Gik*> = poz \/ pio by TOPREAL3:23;
  Gik in {Gik} by TARSKI:def 1;
  then
A13: Gik in L~LS by A8,XBOOLE_0:def 4;
  Gij in {Gij} by TARSKI:def 1;
  then
A14: Gij in L~US by A7,XBOOLE_0:def 4;
A15: j <= width G by A5,A6,XXREAL_0:2;
A16: 1 <= k by A4,A5,XXREAL_0:2;
A17: [i1,j] in Indices G by A1,A4,A10,A15,MATRIX_1:37;
A18: [i2,k] in Indices G by A3,A6,A11,A16,MATRIX_1:37;
A19: [i1,k] in Indices G by A1,A6,A10,A16,MATRIX_1:37;
  set go = R_Cut(US,Gij);
  set do = L_Cut(LS,Gik);
A20: len G = width G by JORDAN8:def 1;
A21: len US >= 3 by JORDAN1E:19;
  then len US >= 1 by XXREAL_0:2;
  then 1 in dom US by FINSEQ_3:27;
  then
A22: US.1 = US/.1 by PARTFUN1:def 8
    .= Wmin by JORDAN1F:5;
A23: Wmin`1 = Wbo by EUCLID:56
    .= G*(1,k)`1 by A6,A16,A20,JORDAN1A:94;
  len G >= 4 by JORDAN8:13;
  then
A24: len G >= 1 by XXREAL_0:2;
  then
A25: [1,k] in Indices G by A6,A16,MATRIX_1:37;
  then
A26: Gij <> US.1 by A1,A17,A22,A23,JORDAN1G:7;
  then reconsider go as being_S-Seq FinSequence of TOP-REAL 2
  by A14,JORDAN3:70;
A27: [1,j] in Indices G by A4,A15,A24,MATRIX_1:37;
A28: len LS >= 1+2 by JORDAN1E:19;
  then
A29: len LS >= 1 by XXREAL_0:2;
  then
A30: 1 in dom LS by FINSEQ_3:27;
  len LS in dom LS by A29,FINSEQ_3:27;
  then
A31: LS.len LS = LS/.len LS by PARTFUN1:def 8
    .= Wmin by JORDAN1F:8;
  Wmin`1 = Wbo by EUCLID:56
    .= G*(1,k)`1 by A6,A16,A20,JORDAN1A:94;
  then
A32: Gik <> LS.len LS by A1,A2,A18,A25,A31,JORDAN1G:7;
  then reconsider do as being_S-Seq FinSequence of TOP-REAL 2
  by A13,JORDAN3:69;
A33: [len G,k] in Indices G by A6,A16,A24,MATRIX_1:37;
A34: LS.1 = LS/.1 by A30,PARTFUN1:def 8
    .= Emax by JORDAN1F:6;
  Emax`1 = Ebo by EUCLID:56
    .= G*(len G,k)`1 by A6,A16,A20,JORDAN1A:92;
  then
A35: Gik <> LS.1 by A3,A18,A33,A34,JORDAN1G:7;
A36: len go >= 1+1 by TOPREAL1:def 10;
A37: Gij in rng US by A1,A4,A10,A14,A15,JORDAN1G:4,JORDAN1J:40;
  then
A38: go is_sequence_on G by JORDAN1G:4,JORDAN1J:38;
A39: len do >= 1+1 by TOPREAL1:def 10;
A40: Gik in rng LS by A3,A6,A11,A13,A16,JORDAN1G:5,JORDAN1J:40;
  then
A41: do is_sequence_on G by JORDAN1G:5,JORDAN1J:39;
  reconsider go as non constant s.c.c.
  (being_S-Seq FinSequence of TOP-REAL 2) by A36,A38,JGRAPH_1:16,JORDAN8:8;
  reconsider do as non constant s.c.c.
  (being_S-Seq FinSequence of TOP-REAL 2) by A39,A41,JGRAPH_1:16,JORDAN8:8;
A42: len go > 1 by A36,NAT_1:13;
  then
A43: len go in dom go by FINSEQ_3:27;
  then
A44: go/.len go = go.len go by PARTFUN1:def 8
    .= Gij by A14,JORDAN3:59;
  len do >= 1 by A39,XXREAL_0:2;
  then 1 in dom do by FINSEQ_3:27;
  then
A45: do/.1 = do.1 by PARTFUN1:def 8
    .= Gik by A13,JORDAN3:58;
  reconsider m = len go - 1 as Element of NAT by A43,FINSEQ_3:28;
A46: m+1 = len go;
  then
A47: len go-'1 = m by NAT_D:34;
A48: LSeg(go,m) c= L~go by TOPREAL3:26;
A49: L~go c= L~US by A14,JORDAN3:76;
  then LSeg(go,m) c= L~US by A48,XBOOLE_1:1;
  then
A50: LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> c= {Gij} by A7,A12,XBOOLE_1:26;
  m >= 1 by A36,XREAL_1:21;
  then
A51: LSeg(go,m) = LSeg(go/.m,Gij) by A44,A46,TOPREAL1:def 5;
  {Gij} c= LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*>
  proof
    let x be set;
    assume x in {Gij};
    then
A52: x = Gij by TARSKI:def 1;
A53: Gij in LSeg(go,m) by A51,RLTOPSP1:69;
    Gij in LSeg(Gij,Gi1k) by RLTOPSP1:69;
    then Gij in LSeg(Gij,Gi1k) \/ LSeg(Gi1k,Gik) by XBOOLE_0:def 3;
    then Gij in L~<*Gij,Gi1k,Gik*> by SPRECT_1:10;
    hence thesis by A52,A53,XBOOLE_0:def 4;
  end;
  then
A54: LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> = {Gij} by A50,XBOOLE_0:def 10;
A55: LSeg(do,1) c= L~do by TOPREAL3:26;
A56: L~do c= L~LS by A13,JORDAN3:77;
  then LSeg(do,1) c= L~LS by A55,XBOOLE_1:1;
  then
A57: LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*> c= {Gik} by A8,A12,XBOOLE_1:26;
A58: LSeg(do,1) = LSeg(Gik,do/.(1+1)) by A39,A45,TOPREAL1:def 5;
  {Gik} c= LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*>
  proof
    let x be set;
    assume x in {Gik};
    then
A59: x = Gik by TARSKI:def 1;
A60: Gik in LSeg(do,1) by A58,RLTOPSP1:69;
    Gik in LSeg(Gi1k,Gik) by RLTOPSP1:69;
    then Gik in LSeg(Gij,Gi1k) \/ LSeg(Gi1k,Gik) by XBOOLE_0:def 3;
    then Gik in L~<*Gij,Gi1k,Gik*> by SPRECT_1:10;
    hence thesis by A59,A60,XBOOLE_0:def 4;
  end;
  then
A61: L~<*Gij,Gi1k,Gik*> /\ LSeg(do,1) = {Gik} by A57,XBOOLE_0:def 10;
A62: go/.1 = US/.1 by A14,SPRECT_3:39
    .= Wmin by JORDAN1F:5;
  then
A63: go/.1 = LS/.len LS by JORDAN1F:8
    .= do/.len do by A13,JORDAN1J:35;
A64: rng go c= L~go by A36,SPPOL_2:18;
A65: rng do c= L~do by A39,SPPOL_2:18;
A66: {go/.1} c= L~go /\ L~do
  proof
    let x be set;
    assume x in {go/.1};
    then
A67: x = go/.1 by TARSKI:def 1;
    then
A68: x in rng go by FINSEQ_6:46;
    x in rng do by A63,A67,REVROT_1:3;
    hence thesis by A64,A65,A68,XBOOLE_0:def 4;
  end;
A69: LS.1 = LS/.1 by A30,PARTFUN1:def 8
    .= Emax by JORDAN1F:6;
A70: [len G,j] in Indices G by A4,A15,A24,MATRIX_1:37;
  L~go /\ L~do c= {go/.1}
  proof
    let x be set;
    assume
A71: x in L~go /\ L~do;
    then
A72: x in L~go by XBOOLE_0:def 4;
A73: x in L~do by A71,XBOOLE_0:def 4;
    then x in L~US /\ L~LS by A49,A56,A72,XBOOLE_0:def 4;
    then x in {Wmin,Emax} by JORDAN1E:20;
    then
A74: x = Wmin or x = Emax by TARSKI:def 2;
    now
      assume x = Emax;
      then
A75:  Emax = Gik by A13,A69,A73,JORDAN1E:11;
      G*(len G,j)`1 = Ebo by A4,A15,A20,JORDAN1A:92;
      then Emax`1 <> Ebo by A3,A18,A70,A75,JORDAN1G:7;
      hence contradiction by EUCLID:56;
    end;
    hence thesis by A62,A74,TARSKI:def 1;
  end;
  then
A76: L~go /\ L~do = {go/.1} by A66,XBOOLE_0:def 10;
  set W2 = go/.2;
A77: 2 in dom go by A36,FINSEQ_3:27;
A78: now
    assume Gij`1 = Wbo;
    then G*(1,j)`1 = G*(i1,j)`1 by A4,A15,A20,JORDAN1A:94;
    hence contradiction by A1,A17,A27,JORDAN1G:7;
  end;
  go = mid(US,1,Gij..US) by A37,JORDAN1G:57
    .= US|(Gij..US) by A37,FINSEQ_4:31,FINSEQ_6:122;
  then
A79: W2 = US/.2 by A77,FINSEQ_4:85;
A80: Wmin in rng go by A62,FINSEQ_6:46;
  set pion = <*Gij,Gi1k,Gik*>;
A81: now
    let n be Element of NAT;
    assume n in dom pion;
    then n in {1,2,3} by FINSEQ_3:1,30;
    then n = 1 or n = 2 or n = 3 by ENUMSET1:def 1;
    hence
    ex i,j be Element of NAT st [i,j] in Indices G & pion/.n = G*(i,j)
    by A17,A18,A19,FINSEQ_4:27;
  end;
A82: Gi1k`1 = G*(i1,1)`1 by A1,A6,A10,A16,GOBOARD5:3
    .= Gij`1 by A1,A4,A10,A15,GOBOARD5:3;
  Gi1k`2 = G*(1,k)`2 by A1,A6,A10,A16,GOBOARD5:2
    .= Gik`2 by A3,A6,A11,A16,GOBOARD5:2;
  then
A83: Gi1k = |[Gij`1,Gik`2]| by A82,EUCLID:57;
A84: Gi1k in pio by RLTOPSP1:69;
A85: Gi1k in poz by RLTOPSP1:69;
  now per cases;
    suppose Gik`1 <> Gij`1 & Gik`2 <> Gij`2;
      then pion is being_S-Seq by A83,TOPREAL3:41;
      then consider pion1 be FinSequence of TOP-REAL 2 such that
A86:  pion1 is_sequence_on G and
A87:  pion1 is being_S-Seq and
A88:  L~pion = L~pion1 and
A89:  pion/.1 = pion1/.1 and
A90:  pion/.len pion = pion1/.len pion1 and
A91:  len pion <= len pion1 by A81,GOBOARD3:2;
      reconsider pion1 as being_S-Seq FinSequence of TOP-REAL 2 by A87;
      set godo = go^'pion1^'do;
A92:  Gi1k`1 = G*(i1,1)`1 by A1,A6,A10,A16,GOBOARD5:3
        .= Gij`1 by A1,A4,A10,A15,GOBOARD5:3;
A93:  Gi1k`1 <= Gik`1 by A1,A2,A3,A6,A16,JORDAN1A:39;
      then
A94:  W-bound poz = Gi1k`1 by SPRECT_1:62;
A95:  W-bound pio = Gij`1 by A92,SPRECT_1:62;
      W-bound (poz \/ pio) = min(W-bound poz, W-bound pio) by SPRECT_1:54
        .= Gij`1 by A92,A94,A95;
      then
A96:  W-bound L~pion1 = Gij`1 by A88,TOPREAL3:23;
A97:  1+1 <= len Cage(C,n) by GOBOARD7:36,XXREAL_0:2;
A98:  1+1 <= len Rotate(Cage(C,n),Wmin) by GOBOARD7:36,XXREAL_0:2;
      len (go^'pion1) >= len go by TOPREAL8:7;
      then
A99:  len (go^'pion1) >= 1+1 by A36,XXREAL_0:2;
      then
A100: len (go^'pion1) > 1+0 by NAT_1:13;
A101: len godo >= len (go^'pion1) by TOPREAL8:7;
      then
A102: 1+1 <= len godo by A99,XXREAL_0:2;
A103: US is_sequence_on G by JORDAN1G:4;
A104: go/.len go = pion1/.1 by A44,A89,FINSEQ_4:27;
      then
A105: go^'pion1 is_sequence_on G by A38,A86,TOPREAL8:12;
A106: (go^'pion1)/.len (go^'pion1) = pion/.len pion by A90,GRAPH_2:58
        .= pion/.3 by FINSEQ_1:62
        .= do/.1 by A45,FINSEQ_4:27;
      then
A107: godo is_sequence_on G by A41,A105,TOPREAL8:12;
      LSeg(pion1,1) c= L~pion by A88,TOPREAL3:26;
      then
A108: LSeg(go,len go-'1) /\ LSeg(pion1,1) c={Gij} by A47,A54,XBOOLE_1:27;
      len pion1 >= 2+1 by A91,FINSEQ_1:62;
      then
A109: len pion1 > 1+1 by NAT_1:13;
      {Gij} c= LSeg(go,m) /\ LSeg(pion1,1)
      proof
        let x be set;
        assume x in {Gij};
        then
A110:   x = Gij by TARSKI:def 1;
A111:   Gij in LSeg(go,m) by A51,RLTOPSP1:69;
        Gij in LSeg(pion1,1) by A44,A104,A109,TOPREAL1:27;
        hence thesis by A110,A111,XBOOLE_0:def 4;
      end;
      then LSeg(go,len go-'1) /\ LSeg(pion1,1) = { go/.len go }
      by A44,A47,A108,XBOOLE_0:def 10;
      then
A112: go^'pion1 is unfolded by A104,TOPREAL8:34;
      len pion1 >= 2+1 by A91,FINSEQ_1:62;
      then
A113: len pion1-2 >= 0 by XREAL_1:21;
      len (go^'pion1)+1-1 = len go+len pion1-1 by GRAPH_2:13;
      then len (go^'pion1)-1 = len go + (len pion1-2)
        .= len go + (len pion1-'2) by A113,XREAL_0:def 2;
      then
A114: len (go^'pion1)-'1 = len go + (len pion1-'2) by XREAL_0:def 2;
A115: len pion1-1 >= 1 by A109,XREAL_1:21;
      then
A116: len pion1-'1 = len pion1-1 by XREAL_0:def 2;
A117: len pion1-'2+1 = len pion1-2+1 by A113,XREAL_0:def 2
        .= len pion1-'1 by A115,XREAL_0:def 2;
      len pion1-1+1 <= len pion1;
      then
A118: len pion1-'1 < len pion1 by A116,NAT_1:13;
      LSeg(pion1,len pion1-'1) c= L~pion by A88,TOPREAL3:26;
      then
A119: LSeg(pion1,len pion1-'1) /\ LSeg(do,1) c= {Gik} by A61,XBOOLE_1:27;
      {Gik} c= LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
      proof
        let x be set;
        assume x in {Gik};
        then
A120:   x = Gik by TARSKI:def 1;
A121:   Gik in LSeg(do,1) by A58,RLTOPSP1:69;
        pion1/.(len pion1-'1+1) = pion/.3 by A90,A116,FINSEQ_1:62
          .= Gik by FINSEQ_4:27;
        then Gik in LSeg(pion1,len pion1-'1) by A115,A116,TOPREAL1:27;
        hence thesis by A120,A121,XBOOLE_0:def 4;
      end;
      then LSeg(pion1,len pion1-'1) /\ LSeg(do,1) = {Gik}
      by A119,XBOOLE_0:def 10;
      then
A122: LSeg(go^'pion1,len go+(len pion1-'2)) /\ LSeg(do,1) =
      {(go^'pion1)/.len (go^'pion1)} by A45,A104,A106,A117,A118,TOPREAL8:31;
A123: (go^'pion1) is non trivial by A99,REALSET1:13;
A124: rng pion1 c= L~pion1 by A109,SPPOL_2:18;
A125: {pion1/.1} c= L~go /\ L~pion1
      proof
        let x be set;
        assume x in {pion1/.1};
        then
A126:   x = pion1/.1 by TARSKI:def 1;
        then
A127:   x in rng go by A104,REVROT_1:3;
        x in rng pion1 by A126,FINSEQ_6:46;
        hence thesis by A64,A124,A127,XBOOLE_0:def 4;
      end;
      L~go /\ L~pion1 c= {pion1/.1}
      proof
        let x be set;
        assume
A128:   x in L~go /\ L~pion1;
        then
A129:   x in L~go by XBOOLE_0:def 4;
        x in L~pion1 by A128,XBOOLE_0:def 4;
        hence thesis by A7,A12,A44,A49,A88,A104,A129,XBOOLE_0:def 4;
      end;
      then
A130: L~go /\ L~pion1 = {pion1/.1} by A125,XBOOLE_0:def 10;
      then
A131: (go^'pion1) is s.n.c. by A104,JORDAN1J:54;
      rng go /\ rng pion1 c= {pion1/.1} by A64,A124,A130,XBOOLE_1:27;
      then
A132: go^'pion1 is one-to-one by JORDAN1J:55;
A133: pion/.len pion = pion/.3 by FINSEQ_1:62
        .= do/.1 by A45,FINSEQ_4:27;
A134: {pion1/.len pion1} c= L~do /\ L~pion1
      proof
        let x be set;
        assume x in {pion1/.len pion1};
        then
A135:   x = pion1/.len pion1 by TARSKI:def 1;
        then
A136:   x in rng do by A90,A133,FINSEQ_6:46;
        x in rng pion1 by A135,REVROT_1:3;
        hence thesis by A65,A124,A136,XBOOLE_0:def 4;
      end;
      L~do /\ L~pion1 c= {pion1/.len pion1}
      proof
        let x be set;
        assume
A137:   x in L~do /\ L~pion1;
        then
A138:   x in L~do by XBOOLE_0:def 4;
        x in L~pion1 by A137,XBOOLE_0:def 4;
        hence thesis by A8,A12,A45,A56,A88,A90,A133,A138,XBOOLE_0:def 4;
      end;
      then
A139: L~do /\ L~pion1 = {pion1/.len pion1} by A134,XBOOLE_0:def 10;
A140: L~(go^'pion1) /\ L~do = (L~go \/ L~pion1) /\ L~do by A104,TOPREAL8:35
        .= {go/.1} \/ {do/.1} by A76,A90,A133,A139,XBOOLE_1:23
        .= {(go^'pion1)/.1} \/ {do/.1} by GRAPH_2:57
        .= {(go^'pion1)/.1,do/.1} by ENUMSET1:41;
      do/.len do = (go^'pion1)/.1 by A63,GRAPH_2:57;
      then reconsider godo as non constant standard special_circular_sequence
      by A102,A106,A107,A112,A114,A122,A123,A131,A132,A140,JORDAN8:7,8
,TOPREAL8:11,33,34;
A141: UA is_an_arc_of E-max C,W-min C by JORDAN6:def 9;
      then
A142: UA is connected by JORDAN6:11;
A143: W-min C in UA by A141,TOPREAL1:4;
A144: E-max C in UA by A141,TOPREAL1:4;
      set ff = Rotate(Cage(C,n),Wmin);
      Wmin in rng Cage(C,n) by SPRECT_2:47;
      then
A145: ff/.1 = Wmin by FINSEQ_6:98;
A146: L~ff = L~Cage(C,n) by REVROT_1:33;
      then (W-max L~ff)..ff > 1 by A145,SPRECT_5:23;
      then (N-min L~ff)..ff > 1 by A145,A146,SPRECT_5:24,XXREAL_0:2;
      then (N-max L~ff)..ff > 1 by A145,A146,SPRECT_5:25,XXREAL_0:2;
      then
A147: Emax..ff > 1 by A145,A146,SPRECT_5:26,XXREAL_0:2;
A148: now
        assume
A149:   Gij..US <= 1;
        Gij..US >= 1 by A37,FINSEQ_4:31;
        then Gij..US = 1 by A149,XXREAL_0:1;
        then Gij = US/.1 by A37,FINSEQ_5:41;
        hence contradiction by A22,A26,JORDAN1F:5;
      end;
A150: Cage(C,n) is_sequence_on G by JORDAN9:def 1;
      then
A151: ff is_sequence_on G by REVROT_1:34;
A152: right_cell(godo,1,G)\L~godo c= RightComp godo by A102,A107,JORDAN9:29;
A153: L~godo = L~(go^'pion1) \/ L~do by A106,TOPREAL8:35
        .= L~go \/ L~pion1 \/ L~do by A104,TOPREAL8:35;
A154: L~Cage(C,n) = L~US \/ L~LS by JORDAN1E:17;
      then
A155: L~US c= L~Cage(C,n) by XBOOLE_1:7;
A156: L~LS c= L~Cage(C,n) by A154,XBOOLE_1:7;
A157: L~go c=L~Cage(C,n) by A49,A155,XBOOLE_1:1;
A158: L~do c=L~Cage(C,n) by A56,A156,XBOOLE_1:1;
A159: W-min C in C by SPRECT_1:15;
A160: now
        assume W-min C in L~godo;
        then
A161:   W-min C in L~go \/ L~pion1 or W-min C in L~do by A153,XBOOLE_0:def 3;
        per cases by A161,XBOOLE_0:def 3;
        suppose W-min C in L~go;
          then C meets L~Cage(C,n) by A157,A159,XBOOLE_0:3;
          hence contradiction by JORDAN10:5;
        end;
        suppose W-min C in L~pion1;
          hence contradiction by A9,A12,A88,A143,XBOOLE_0:3;
        end;
        suppose W-min C in L~do;
          then C meets L~Cage(C,n) by A158,A159,XBOOLE_0:3;
          hence contradiction by JORDAN10:5;
        end;
      end;
      right_cell(Rotate(Cage(C,n),Wmin),1) =
      right_cell(ff,1,GoB ff) by A98,JORDAN1H:29
        .= right_cell(ff,1,GoB Cage(C,n)) by REVROT_1:28
        .= right_cell(ff,1,G) by JORDAN1H:52
        .= right_cell(ff-:Emax,1,G) by A147,A151,JORDAN1J:53
        .= right_cell(US,1,G) by JORDAN1E:def 1
        .= right_cell(R_Cut(US,Gij),1,G) by A37,A103,A148,JORDAN1J:52
        .= right_cell(go^'pion1,1,G) by A42,A105,JORDAN1J:51
        .= right_cell(godo,1,G) by A100,A107,JORDAN1J:51;
      then W-min C in right_cell(godo,1,G) by JORDAN1I:8;
      then
A162: W-min C in right_cell(godo,1,G)\L~godo by A160,XBOOLE_0:def 5;
A163: godo/.1 = (go^'pion1)/.1 by GRAPH_2:57
        .= Wmin by A62,GRAPH_2:57;
A164: len US >= 2 by A21,XXREAL_0:2;
A165: godo/.2 = (go^'pion1)/.2 by A99,GRAPH_2:61
        .= US/.2 by A36,A79,GRAPH_2:61
        .= (US^'LS)/.2 by A164,GRAPH_2:61
        .= Rotate(Cage(C,n),Wmin)/.2 by JORDAN1E:15;
A166: L~go \/ L~do is compact by COMPTS_1:19;
      Wmin in L~go \/ L~do by A64,A80,XBOOLE_0:def 3;
      then
A167: W-min (L~go \/ L~do) = Wmin by A157,A158,A166,JORDAN1J:21,XBOOLE_1:8;
A168: (W-min (L~go \/ L~do))`1 = W-bound (L~go \/ L~do) by EUCLID:56;
A169: Wmin`1 = Wbo by EUCLID:56;
      Gij`1 >= Wbo by A14,A155,PSCOMP_1:71;
      then Gij`1 > Wbo by A78,XXREAL_0:1;
      then W-min (L~go\/L~do\/L~pion1) = W-min (L~go \/ L~do)
      by A96,A166,A167,A168,A169,JORDAN1J:33;
      then
A170: W-min L~godo = Wmin by A153,A167,XBOOLE_1:4;
A171: rng godo c= L~godo by A99,A101,SPPOL_2:18,XXREAL_0:2;
      2 in dom godo by A102,FINSEQ_3:27;
      then
A172: godo/.2 in rng godo by PARTFUN2:4;
      godo/.2 in W-most L~Cage(C,n) by A165,JORDAN1I:27;
      then (godo/.2)`1 = (W-min L~godo)`1 by A170,PSCOMP_1:88
        .= W-bound L~godo by EUCLID:56;
      then godo/.2 in W-most L~godo by A171,A172,SPRECT_2:16;
      then Rotate(godo,W-min L~godo)/.2 in W-most L~godo
      by A163,A170,FINSEQ_6:95;
      then reconsider godo as clockwise_oriented non constant standard
      special_circular_sequence by JORDAN1I:27;
      len US in dom US by FINSEQ_5:6;
      then
A173: US.len US = US/.len US by PARTFUN1:def 8
        .= Emax by JORDAN1F:7;
A174: east_halfline E-max C misses L~go
      proof
        assume east_halfline E-max C meets L~go;
        then consider p be set such that
A175:   p in east_halfline E-max C and
A176:   p in L~go by XBOOLE_0:3;
        reconsider p as Point of TOP-REAL 2 by A175;
        p in L~US by A49,A176;
        then p in east_halfline E-max C /\ L~Cage(C,n)
        by A155,A175,XBOOLE_0:def 4;
        then
A177:   p`1 = Ebo by JORDAN1A:104,PSCOMP_1:111;
        then
A178:   p = Emax by A49,A176,JORDAN1J:46;
        then Emax = Gij by A14,A173,A176,JORDAN1J:43;
        then Gij`1 = G*(len G,k)`1 by A6,A16,A20,A177,A178,JORDAN1A:92;
        hence contradiction by A2,A3,A17,A33,JORDAN1G:7;
      end;
      now
        assume east_halfline E-max C meets L~godo;
        then
A179:   east_halfline E-max C meets (L~go \/ L~pion1) or
        east_halfline E-max C meets L~do by A153,XBOOLE_1:70;
        per cases by A179,XBOOLE_1:70;
        suppose east_halfline E-max C meets L~go;
          hence contradiction by A174;
        end;
        suppose east_halfline E-max C meets L~pion1;
          then consider p be set such that
A180:     p in east_halfline E-max C and
A181:     p in L~pion1 by XBOOLE_0:3;
          reconsider p as Point of TOP-REAL 2 by A180;
A182:     now per cases by A12,A88,A181,XBOOLE_0:def 3;
            suppose p in poz;
              hence p`1 <= Gik`1 by A93,TOPREAL1:9;
            end;
            suppose p in pio;
              hence p`1 <= Gik`1 by A92,A93,GOBOARD7:5;
            end;
          end;
          i2+1 <= len G by A3,NAT_1:13;
          then i2 <= len G-1 by XREAL_1:21;
          then
A183:     i2 <= len G-'1 by XREAL_0:def 2;
          len G-'1 <= len G by NAT_D:35;
          then Gik`1 <= G*(len G-'1,1)`1 by A6,A11,A16,A20,A24,A183,JORDAN1A:39
;
          then p`1 <= G*(len G-'1,1)`1 by A182,XXREAL_0:2;
          then p`1 <= E-bound C by A24,JORDAN8:15;
          then
A184:     p`1 <= (E-max C)`1 by EUCLID:56;
          p`1 >= (E-max C)`1 by A180,TOPREAL1:def 13;
          then
A185:     p`1 = (E-max C)`1 by A184,XXREAL_0:1;
          p`2 = (E-max C)`2 by A180,TOPREAL1:def 13;
          then p = E-max C by A185,TOPREAL3:11;
          hence contradiction by A9,A12,A88,A144,A181,XBOOLE_0:3;
        end;
        suppose east_halfline E-max C meets L~do;
          then consider p be set such that
A186:     p in east_halfline E-max C and
A187:     p in L~do by XBOOLE_0:3;
          reconsider p as Point of TOP-REAL 2 by A186;
          p in L~LS by A56,A187;
          then p in east_halfline E-max C /\ L~Cage(C,n)
          by A156,A186,XBOOLE_0:def 4;
          then
A188:     p`1 = Ebo by JORDAN1A:104,PSCOMP_1:111;
A189:     (E-max C)`2 = p`2 by A186,TOPREAL1:def 13;
          set RC = Rotate(Cage(C,n),Emax);
A190:     E-max C in right_cell(RC,1) by JORDAN1I:9;
A191:     1+1 <= len LS by A28,XXREAL_0:2;
          LS = RC-:Wmin by JORDAN1G:26;
          then
A192:     LSeg(LS,1) = LSeg(RC,1) by A191,SPPOL_2:9;
A193:     L~RC = L~Cage(C,n) by REVROT_1:33;
A194:     len RC = len Cage(C,n) by REVROT_1:14;
A195:     GoB RC = GoB Cage(C,n) by REVROT_1:28
            .= G by JORDAN1H:52;
A196:     Emax in rng Cage(C,n) by SPRECT_2:50;
A197:     RC is_sequence_on G by A150,REVROT_1:34;
A198:     RC/.1 = E-max L~RC by A193,A196,FINSEQ_6:98;
          consider ii,jj be Element of NAT such that
A199:     [ii,jj+1] in Indices G and
A200:     [ii,jj] in Indices G and
A201:     RC/.1 = G*(ii,jj+1) and
A202:     RC/.(1+1) = G*(ii,jj) by A97,A193,A194,A196,A197,FINSEQ_6:98
,JORDAN1I:25;
          consider jj2 be Element of NAT such that
A203:     1 <= jj2 and
A204:     jj2 <= width G and
A205:     Emax = G*(len G,jj2) by JORDAN1D:29;
A206:     len G >= 4 by JORDAN8:13;
          then len G >= 1 by XXREAL_0:2;
          then [len G,jj2] in Indices G by A203,A204,MATRIX_1:37;
          then
A207:     ii = len G by A193,A198,A199,A201,A205,GOBOARD1:21;
A208:     1 <= ii by A199,MATRIX_1:39;
A209:     ii <= len G by A199,MATRIX_1:39;
A210:     1 <= jj+1 by A199,MATRIX_1:39;
A211:     jj+1 <= width G by A199,MATRIX_1:39;
A212:     1 <= ii by A200,MATRIX_1:39;
A213:     ii <= len G by A200,MATRIX_1:39;
A214:     1 <= jj by A200,MATRIX_1:39;
A215:     jj <= width G by A200,MATRIX_1:39;
A216:     ii+1 <> ii;
          jj+1+1 <> jj;
          then
A217:     right_cell(RC,1) = cell(G,ii-'1,jj)
          by A97,A194,A195,A199,A200,A201,A202,A216,GOBOARD5:def 6;
A218:     ii-'1+1 = ii by A208,XREAL_1:237;
          ii-1 >= 4-1 by A206,A207,XREAL_1:11;
          then
A219:     ii-1 >= 1 by XXREAL_0:2;
          then
A220:     1 <= ii-'1 by XREAL_0:def 2;
A221:     G*(ii-'1,jj)`2 <= p`2 by A189,A190,A209,A211,A214,A217,A218,A219,
JORDAN9:19;
A222:     p`2 <= G*(ii-'1,jj+1)`2 by A189,A190,A209,A211,A214,A217,A218,A219,
JORDAN9:19;
A223:     ii-'1 < len G by A209,A218,NAT_1:13;
          then
A224:     G*(ii-'1,jj)`2 = G*(1,jj)`2 by A214,A215,A220,GOBOARD5:2
            .= G*(ii,jj)`2 by A212,A213,A214,A215,GOBOARD5:2;
A225:     G*(ii-'1,jj+1)`2 = G*(1,jj+1)`2 by A210,A211,A220,A223,GOBOARD5:2
            .= G*(ii,jj+1)`2 by A208,A209,A210,A211,GOBOARD5:2;
A226:     G*(len G,jj)`1 = Ebo by A20,A214,A215,JORDAN1A:92;
          Ebo = G*(len G,jj+1)`1 by A20,A210,A211,JORDAN1A:92;
          then p in LSeg(RC/.1,RC/.(1+1))
          by A188,A201,A202,A207,A221,A222,A224,A225,A226,GOBOARD7:8;
          then
A227:     p in LSeg(LS,1) by A97,A192,A194,TOPREAL1:def 5;
A228:     p in LSeg(do,Index(p,do)) by A187,JORDAN3:42;
A229:     do = mid(LS,Gik..LS,len LS) by A40,JORDAN1J:37;
A230:     1<=Gik..LS by A40,FINSEQ_4:31;
A231:     Gik..LS<=len LS by A40,FINSEQ_4:31;
          Gik..LS <> len LS by A32,A40,FINSEQ_4:29;
          then
A232:     Gik..LS < len LS by A231,XXREAL_0:1;
A233:     1<=Index(p,do) by A187,JORDAN3:41;
A234:     Index(p,do) < len do by A187,JORDAN3:41;
A235:     Index(Gik,LS)+1 = Gik..LS by A35,A40,JORDAN1J:56;
          consider t be Nat such that
A236:     t in dom LS and
A237:     LS.t = Gik by A40,FINSEQ_2:11;
A238:     1 <= t by A236,FINSEQ_3:27;
A239:     t <= len LS by A236,FINSEQ_3:27;
          1 < t by A35,A237,A238,XXREAL_0:1;
          then Index(Gik,LS)+1 = t by A237,A239,JORDAN3:45;
          then
A240:     len L_Cut(LS,Gik) = len LS-Index(Gik,LS) by A13,A237,JORDAN3:61;
          set tt = Index(p,do)+(Gik..LS)-'1;
A241:     1<=Index(Gik,LS) by A13,JORDAN3:41;
          0+Index(Gik,LS) < len LS by A13,JORDAN3:41;
          then
A242:     len LS-Index(Gik,LS) > 0 by XREAL_1:22;
          Index(p,do) < len LS-'Index(Gik,LS) by A234,A240,XREAL_0:def 2;
          then Index(p,do)+1 <= len LS-'Index(Gik,LS) by NAT_1:13;
          then Index(p,do) <= len LS-'Index(Gik,LS)-1 by XREAL_1:21;
          then Index(p,do) <= len LS-Index(Gik,LS)-1 by A242,XREAL_0:def 2;
          then Index(p,do) <= len LS-Gik..LS by A235;
          then Index(p,do) <= len LS-'Gik..LS by XREAL_0:def 2;
          then Index(p,do) < len LS-'(Gik..LS)+1 by NAT_1:13;
          then
A243:     LSeg(mid(LS,Gik..LS,len LS),Index(p,do)) =
          LSeg(LS,Index(p,do)+(Gik..LS)-'1) by A230,A232,A233,JORDAN4:31;
A244:     1+1 <= Gik..LS by A235,A241,XREAL_1:9;
          then Index(p,do)+Gik..LS >= 1+1+1 by A233,XREAL_1:9;
          then Index(p,do)+Gik..LS-1 >= 1+1+1-1 by XREAL_1:11;
          then
A245:     tt >= 1+1 by XREAL_0:def 2;
A246:     2 in dom LS by A191,FINSEQ_3:27;
          now per cases by A245,XXREAL_0:1;
            suppose tt > 1+1;
              then LSeg(LS,1) misses LSeg(LS,tt) by TOPREAL1:def 9;
              hence contradiction by A227,A228,A229,A243,XBOOLE_0:3;
            end;
            suppose
A247:         tt = 1+1;
              then LSeg(LS,1) /\ LSeg(LS,tt) = {LS/.2} by A28,TOPREAL1:def 8;
              then p in {LS/.2} by A227,A228,A229,A243,XBOOLE_0:def 4;
              then
A248:         p = LS/.2 by TARSKI:def 1;
              then
A249:         p..LS = 2 by A246,FINSEQ_5:44;
              1+1 = Index(p,do)+(Gik..LS)-1 by A247,XREAL_0:def 2;
              then 1+1+1 = Index(p,do)+(Gik..LS);
              then
A250:         Gik..LS = 2 by A233,A244,JORDAN1E:10;
              p in rng LS by A246,A248,PARTFUN2:4;
              then p = Gik by A40,A249,A250,FINSEQ_5:10;
              then Gik`1 = Ebo by A248,JORDAN1G:40;
              then Gik`1 = G*(len G,j)`1 by A4,A15,A20,JORDAN1A:92;
              hence contradiction by A3,A18,A70,JORDAN1G:7;
            end;
          end;
          hence contradiction;
        end;
      end;
      then east_halfline E-max C c= (L~godo)` by SUBSET_1:43;
      then consider W be Subset of TOP-REAL 2 such that
A251: W is_a_component_of (L~godo)` and
A252: east_halfline E-max C c= W by GOBOARD9:5;
      W is not Bounded by A252,JORDAN2C:16,129;
      then W is_outside_component_of L~godo by A251,JORDAN2C:def 4;
      then W c= UBD L~godo by JORDAN2C:27;
      then
A253: east_halfline E-max C c= UBD L~godo by A252,XBOOLE_1:1;
      E-max C in east_halfline E-max C by TOPREAL1:45;
      then E-max C in UBD L~godo by A253;
      then E-max C in LeftComp godo by GOBRD14:46;
      then UA meets L~godo by A142,A143,A144,A152,A162,JORDAN1J:36;
      then
A254: UA meets (L~go \/ L~pion1) or UA meets L~do by A153,XBOOLE_1:70;
A255: UA c= C by JORDAN6:76;
      now per cases by A254,XBOOLE_1:70;
        suppose UA meets L~go;
          then UA meets L~Cage(C,n) by A49,A155,XBOOLE_1:1,63;
          hence contradiction by A255,JORDAN10:5,XBOOLE_1:63;
        end;
        suppose UA meets L~pion1;
          hence contradiction by A9,A12,A88;
        end;
        suppose UA meets L~do;
          then UA meets L~Cage(C,n) by A56,A156,XBOOLE_1:1,63;
          hence contradiction by A255,JORDAN10:5,XBOOLE_1:63;
        end;
      end;
      hence contradiction;
    end;
    suppose Gik`1 = Gij`1;
      then
A256: i1 = i2 by A17,A18,JORDAN1G:7;
      then poz = {Gi1k} by RLTOPSP1:71;
      then poz c= pio by A84,ZFMISC_1:37;
      then pio \/ poz = pio by XBOOLE_1:12;
      hence contradiction by A1,A3,A4,A5,A6,A7,A8,A9,A256,Th14;
    end;
    suppose Gik`2 = Gij`2;
      then
A257: j = k by A17,A18,JORDAN1G:6;
      then pio = {Gi1k} by RLTOPSP1:71;
      then pio c= poz by A85,ZFMISC_1:37;
      then pio \/ poz = poz by XBOOLE_1:12;
      hence contradiction by A1,A2,A3,A4,A6,A7,A8,A9,A257,JORDAN15:38;
    end;
  end;
  hence contradiction;
end;

theorem Th23:
  for C be Simple_closed_curve for i1,i2,j,k be Element of NAT st
  1 < i2 & i2 <= i1 & i1 < len Gauge(C,n) &
  1 <= j & j <= k & k <= width Gauge(C,n) &
  (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
  LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) /\ L~Upper_Seq(C,n) =
  {Gauge(C,n)*(i1,j)} & (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
  LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) /\ L~Lower_Seq(C,n) =
  {Gauge(C,n)*(i2,k)} holds (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
  LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) meets Upper_Arc C
proof
  let C be Simple_closed_curve;
  let i1,i2,j,k be Element of NAT;
  set G = Gauge(C,n);
  set pio = LSeg(G*(i1,j),G*(i1,k));
  set poz = LSeg(G*(i1,k),G*(i2,k));
  set US = Upper_Seq(C,n);
  set LS = Lower_Seq(C,n);
  assume that
A1: 1 < i2 and
A2: i2 <= i1 and
A3: i1 < len G and
A4: 1 <= j and
A5: j <= k and
A6: k <= width G and
A7: (pio \/ poz) /\ L~US = {G*(i1,j)} and
A8: (pio \/ poz) /\ L~LS = {G*(i2,k)} and
A9: (pio \/ poz) misses Upper_Arc C;
  set UA = Upper_Arc C;
  set Wmin = W-min L~Cage(C,n);
  set Emax = E-max L~Cage(C,n);
  set Wbo = W-bound L~Cage(C,n);
  set Ebo = E-bound L~Cage(C,n);
  set Gik = G*(i2,k);
  set Gij = G*(i1,j);
  set Gi1k = G*(i1,k);
A10: 1 < i1 by A1,A2,XXREAL_0:2;
A11: i2 < len G by A2,A3,XXREAL_0:2;
A12: L~<*Gij,Gi1k,Gik*> = poz \/ pio by TOPREAL3:23;
  Gik in {Gik} by TARSKI:def 1;
  then
A13: Gik in L~LS by A8,XBOOLE_0:def 4;
  Gij in {Gij} by TARSKI:def 1;
  then
A14: Gij in L~US by A7,XBOOLE_0:def 4;
A15: j <= width G by A5,A6,XXREAL_0:2;
A16: 1 <= k by A4,A5,XXREAL_0:2;
A17: [i1,j] in Indices G by A3,A4,A10,A15,MATRIX_1:37;
A18: [i2,k] in Indices G by A1,A6,A11,A16,MATRIX_1:37;
A19: [i1,k] in Indices G by A3,A6,A10,A16,MATRIX_1:37;
  set go = R_Cut(US,Gij);
  set do = L_Cut(LS,Gik);
A20: len G = width G by JORDAN8:def 1;
A21: len US >= 3 by JORDAN1E:19;
  then len US >= 1 by XXREAL_0:2;
  then 1 in dom US by FINSEQ_3:27;
  then
A22: US.1 = US/.1 by PARTFUN1:def 8
    .= Wmin by JORDAN1F:5;
A23: Wmin`1 = Wbo by EUCLID:56
    .= G*(1,k)`1 by A6,A16,A20,JORDAN1A:94;
  len G >= 4 by JORDAN8:13;
  then
A24: len G >= 1 by XXREAL_0:2;
  then
A25: [1,k] in Indices G by A6,A16,MATRIX_1:37;
  then
A26: Gij <> US.1 by A1,A2,A17,A22,A23,JORDAN1G:7;
  then reconsider go as being_S-Seq FinSequence of TOP-REAL 2
  by A14,JORDAN3:70;
A27: len LS >= 1+2 by JORDAN1E:19;
  then
A28: len LS >= 1 by XXREAL_0:2;
  then
A29: 1 in dom LS by FINSEQ_3:27;
  len LS in dom LS by A28,FINSEQ_3:27;
  then
A30: LS.len LS = LS/.len LS by PARTFUN1:def 8
    .= Wmin by JORDAN1F:8;
  Wmin`1 = Wbo by EUCLID:56
    .= G*(1,k)`1 by A6,A16,A20,JORDAN1A:94;
  then
A31: Gik <> LS.len LS by A1,A18,A25,A30,JORDAN1G:7;
  then reconsider do as being_S-Seq FinSequence of TOP-REAL 2
  by A13,JORDAN3:69;
A32: [len G,k] in Indices G by A6,A16,A24,MATRIX_1:37;
A33: LS.1 = LS/.1 by A29,PARTFUN1:def 8
    .= Emax by JORDAN1F:6;
  Emax`1 = Ebo by EUCLID:56
    .= G*(len G,k)`1 by A6,A16,A20,JORDAN1A:92;
  then
A34: Gik <> LS.1 by A2,A3,A18,A32,A33,JORDAN1G:7;
A35: len go >= 1+1 by TOPREAL1:def 10;
A36: Gij in rng US by A3,A4,A10,A14,A15,JORDAN1G:4,JORDAN1J:40;
  then
A37: go is_sequence_on G by JORDAN1G:4,JORDAN1J:38;
A38: len do >= 1+1 by TOPREAL1:def 10;
A39: Gik in rng LS by A1,A6,A11,A13,A16,JORDAN1G:5,JORDAN1J:40;
  then
A40: do is_sequence_on G by JORDAN1G:5,JORDAN1J:39;
  reconsider go as non constant s.c.c.
  (being_S-Seq FinSequence of TOP-REAL 2) by A35,A37,JGRAPH_1:16,JORDAN8:8;
  reconsider do as non constant s.c.c.
  (being_S-Seq FinSequence of TOP-REAL 2) by A38,A40,JGRAPH_1:16,JORDAN8:8;
A41: len go > 1 by A35,NAT_1:13;
  then
A42: len go in dom go by FINSEQ_3:27;
  then
A43: go/.len go = go.len go by PARTFUN1:def 8
    .= Gij by A14,JORDAN3:59;
  len do >= 1 by A38,XXREAL_0:2;
  then 1 in dom do by FINSEQ_3:27;
  then
A44: do/.1 = do.1 by PARTFUN1:def 8
    .= Gik by A13,JORDAN3:58;
  reconsider m = len go - 1 as Element of NAT by A42,FINSEQ_3:28;
A45: m+1 = len go;
  then
A46: len go-'1 = m by NAT_D:34;
A47: LSeg(go,m) c= L~go by TOPREAL3:26;
A48: L~go c= L~US by A14,JORDAN3:76;
  then LSeg(go,m) c= L~US by A47,XBOOLE_1:1;
  then
A49: LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> c= {Gij} by A7,A12,XBOOLE_1:26;
  m >= 1 by A35,XREAL_1:21;
  then
A50: LSeg(go,m) = LSeg(go/.m,Gij) by A43,A45,TOPREAL1:def 5;
  {Gij} c= LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*>
  proof
    let x be set;
    assume x in {Gij};
    then
A51: x = Gij by TARSKI:def 1;
A52: Gij in LSeg(go,m) by A50,RLTOPSP1:69;
    Gij in LSeg(Gij,Gi1k) by RLTOPSP1:69;
    then Gij in LSeg(Gij,Gi1k) \/ LSeg(Gi1k,Gik) by XBOOLE_0:def 3;
    then Gij in L~<*Gij,Gi1k,Gik*> by SPRECT_1:10;
    hence thesis by A51,A52,XBOOLE_0:def 4;
  end;
  then
A53: LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> = {Gij} by A49,XBOOLE_0:def 10;
A54: LSeg(do,1) c= L~do by TOPREAL3:26;
A55: L~do c= L~LS by A13,JORDAN3:77;
  then LSeg(do,1) c= L~LS by A54,XBOOLE_1:1;
  then
A56: LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*> c= {Gik} by A8,A12,XBOOLE_1:26;
A57: LSeg(do,1) = LSeg(Gik,do/.(1+1)) by A38,A44,TOPREAL1:def 5;
  {Gik} c= LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*>
  proof
    let x be set;
    assume x in {Gik};
    then
A58: x = Gik by TARSKI:def 1;
A59: Gik in LSeg(do,1) by A57,RLTOPSP1:69;
    Gik in LSeg(Gi1k,Gik) by RLTOPSP1:69;
    then Gik in LSeg(Gij,Gi1k) \/ LSeg(Gi1k,Gik) by XBOOLE_0:def 3;
    then Gik in L~<*Gij,Gi1k,Gik*> by SPRECT_1:10;
    hence thesis by A58,A59,XBOOLE_0:def 4;
  end;
  then
A60: L~<*Gij,Gi1k,Gik*> /\ LSeg(do,1) = {Gik} by A56,XBOOLE_0:def 10;
A61: go/.1 = US/.1 by A14,SPRECT_3:39
    .= Wmin by JORDAN1F:5;
  then
A62: go/.1 = LS/.len LS by JORDAN1F:8
    .= do/.len do by A13,JORDAN1J:35;
A63: rng go c= L~go by A35,SPPOL_2:18;
A64: rng do c= L~do by A38,SPPOL_2:18;
A65: {go/.1} c= L~go /\ L~do
  proof
    let x be set;
    assume x in {go/.1};
    then
A66: x = go/.1 by TARSKI:def 1;
    then
A67: x in rng go by FINSEQ_6:46;
    x in rng do by A62,A66,REVROT_1:3;
    hence thesis by A63,A64,A67,XBOOLE_0:def 4;
  end;
A68: LS.1 = LS/.1 by A29,PARTFUN1:def 8
    .= Emax by JORDAN1F:6;
A69: [len G,j] in Indices G by A4,A15,A24,MATRIX_1:37;
  L~go /\ L~do c= {go/.1}
  proof
    let x be set;
    assume
A70: x in L~go /\ L~do;
    then
A71: x in L~go by XBOOLE_0:def 4;
A72: x in L~do by A70,XBOOLE_0:def 4;
    then x in L~US /\ L~LS by A48,A55,A71,XBOOLE_0:def 4;
    then x in {Wmin,Emax} by JORDAN1E:20;
    then
A73: x = Wmin or x = Emax by TARSKI:def 2;
    now
      assume x = Emax;
      then
A74:  Emax = Gik by A13,A68,A72,JORDAN1E:11;
      G*(len G,j)`1 = Ebo by A4,A15,A20,JORDAN1A:92;
      then Emax`1 <> Ebo by A2,A3,A18,A69,A74,JORDAN1G:7;
      hence contradiction by EUCLID:56;
    end;
    hence thesis by A61,A73,TARSKI:def 1;
  end;
  then
A75: L~go /\ L~do = {go/.1} by A65,XBOOLE_0:def 10;
  set W2 = go/.2;
A76: 2 in dom go by A35,FINSEQ_3:27;
A77: now
    assume Gik`1 = Wbo;
    then G*(1,k)`1 = G*(i2,k)`1 by A6,A16,A20,JORDAN1A:94;
    hence contradiction by A1,A18,A25,JORDAN1G:7;
  end;
  go = mid(US,1,Gij..US) by A36,JORDAN1G:57
    .= US|(Gij..US) by A36,FINSEQ_4:31,FINSEQ_6:122;
  then
A78: W2 = US/.2 by A76,FINSEQ_4:85;
A79: Wmin in rng go by A61,FINSEQ_6:46;
  set pion = <*Gij,Gi1k,Gik*>;
A80: now
    let n be Element of NAT;
    assume n in dom pion;
    then n in {1,2,3} by FINSEQ_3:1,30;
    then n = 1 or n = 2 or n = 3 by ENUMSET1:def 1;
    hence
    ex i,j be Element of NAT st [i,j] in Indices G & pion/.n = G*(i,j)
    by A17,A18,A19,FINSEQ_4:27;
  end;
A81: Gi1k`1 = G*(i1,1)`1 by A3,A6,A10,A16,GOBOARD5:3
    .= Gij`1 by A3,A4,A10,A15,GOBOARD5:3;
  Gi1k`2 = G*(1,k)`2 by A3,A6,A10,A16,GOBOARD5:2
    .= Gik`2 by A1,A6,A11,A16,GOBOARD5:2;
  then
A82: Gi1k = |[Gij`1,Gik`2]| by A81,EUCLID:57;
A83: Gi1k in pio by RLTOPSP1:69;
A84: Gi1k in poz by RLTOPSP1:69;
  now per cases;
    suppose Gik`1 <> Gij`1 & Gik`2 <> Gij`2;
      then pion is being_S-Seq by A82,TOPREAL3:41;
      then consider pion1 be FinSequence of TOP-REAL 2 such that
A85:  pion1 is_sequence_on G and
A86:  pion1 is being_S-Seq and
A87:  L~pion = L~pion1 and
A88:  pion/.1 = pion1/.1 and
A89:  pion/.len pion = pion1/.len pion1 and
A90:  len pion <= len pion1 by A80,GOBOARD3:2;
      reconsider pion1 as being_S-Seq FinSequence of TOP-REAL 2 by A86;
      set godo = go^'pion1^'do;
A91:  Gi1k`1 = G*(i1,1)`1 by A3,A6,A10,A16,GOBOARD5:3
        .= Gij`1 by A3,A4,A10,A15,GOBOARD5:3;
A92:  Gik`1 <= Gi1k`1 by A1,A2,A3,A6,A16,JORDAN1A:39;
      then
A93:  W-bound poz = Gik`1 by SPRECT_1:62;
A94:  W-bound pio = Gij`1 by A91,SPRECT_1:62;
      W-bound (poz \/ pio) = min(W-bound poz, W-bound pio) by SPRECT_1:54
        .= Gik`1 by A91,A92,A93,A94,XXREAL_0:def 9;
      then
A95:  W-bound L~pion1 = Gik`1 by A87,TOPREAL3:23;
A96:  1+1 <= len Cage(C,n) by GOBOARD7:36,XXREAL_0:2;
A97:  1+1 <= len Rotate(Cage(C,n),Wmin) by GOBOARD7:36,XXREAL_0:2;
      len (go^'pion1) >= len go by TOPREAL8:7;
      then
A98:  len (go^'pion1) >= 1+1 by A35,XXREAL_0:2;
      then
A99:  len (go^'pion1) > 1+0 by NAT_1:13;
A100: len godo >= len (go^'pion1) by TOPREAL8:7;
      then
A101: 1+1 <= len godo by A98,XXREAL_0:2;
A102: US is_sequence_on G by JORDAN1G:4;
A103: go/.len go = pion1/.1 by A43,A88,FINSEQ_4:27;
      then
A104: go^'pion1 is_sequence_on G by A37,A85,TOPREAL8:12;
A105: (go^'pion1)/.len (go^'pion1) = pion/.len pion by A89,GRAPH_2:58
        .= pion/.3 by FINSEQ_1:62
        .= do/.1 by A44,FINSEQ_4:27;
      then
A106: godo is_sequence_on G by A40,A104,TOPREAL8:12;
      LSeg(pion1,1) c= L~pion by A87,TOPREAL3:26;
      then
A107: LSeg(go,len go-'1) /\ LSeg(pion1,1) c={Gij} by A46,A53,XBOOLE_1:27;
      len pion1 >= 2+1 by A90,FINSEQ_1:62;
      then
A108: len pion1 > 1+1 by NAT_1:13;
      {Gij} c= LSeg(go,m) /\ LSeg(pion1,1)
      proof
        let x be set;
        assume x in {Gij};
        then
A109:   x = Gij by TARSKI:def 1;
A110:   Gij in LSeg(go,m) by A50,RLTOPSP1:69;
        Gij in LSeg(pion1,1) by A43,A103,A108,TOPREAL1:27;
        hence thesis by A109,A110,XBOOLE_0:def 4;
      end;
      then LSeg(go,len go-'1) /\ LSeg(pion1,1) = { go/.len go }
      by A43,A46,A107,XBOOLE_0:def 10;
      then
A111: go^'pion1 is unfolded by A103,TOPREAL8:34;
      len pion1 >= 2+1 by A90,FINSEQ_1:62;
      then
A112: len pion1-2 >= 0 by XREAL_1:21;
      len (go^'pion1)+1-1 = len go+len pion1-1 by GRAPH_2:13;
      then len (go^'pion1)-1 = len go + (len pion1-2)
        .= len go + (len pion1-'2) by A112,XREAL_0:def 2;
      then
A113: len (go^'pion1)-'1 = len go + (len pion1-'2) by XREAL_0:def 2;
A114: len pion1-1 >= 1 by A108,XREAL_1:21;
      then
A115: len pion1-'1 = len pion1-1 by XREAL_0:def 2;
A116: len pion1-'2+1 = len pion1-2+1 by A112,XREAL_0:def 2
        .= len pion1-'1 by A114,XREAL_0:def 2;
      len pion1-1+1 <= len pion1;
      then
A117: len pion1-'1 < len pion1 by A115,NAT_1:13;
      LSeg(pion1,len pion1-'1) c= L~pion by A87,TOPREAL3:26;
      then
A118: LSeg(pion1,len pion1-'1) /\ LSeg(do,1) c= {Gik} by A60,XBOOLE_1:27;
      {Gik} c= LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
      proof
        let x be set;
        assume x in {Gik};
        then
A119:   x = Gik by TARSKI:def 1;
A120:   Gik in LSeg(do,1) by A57,RLTOPSP1:69;
        pion1/.(len pion1-'1+1) = pion/.3 by A89,A115,FINSEQ_1:62
          .= Gik by FINSEQ_4:27;
        then Gik in LSeg(pion1,len pion1-'1) by A114,A115,TOPREAL1:27;
        hence thesis by A119,A120,XBOOLE_0:def 4;
      end;
      then LSeg(pion1,len pion1-'1) /\ LSeg(do,1) = {Gik}
      by A118,XBOOLE_0:def 10;
      then
A121: LSeg(go^'pion1,len go+(len pion1-'2)) /\ LSeg(do,1) =
      {(go^'pion1)/.len (go^'pion1)} by A44,A103,A105,A116,A117,TOPREAL8:31;
A122: (go^'pion1) is non trivial by A98,REALSET1:13;
A123: rng pion1 c= L~pion1 by A108,SPPOL_2:18;
A124: {pion1/.1} c= L~go /\ L~pion1
      proof
        let x be set;
        assume x in {pion1/.1};
        then
A125:   x = pion1/.1 by TARSKI:def 1;
        then
A126:   x in rng go by A103,REVROT_1:3;
        x in rng pion1 by A125,FINSEQ_6:46;
        hence thesis by A63,A123,A126,XBOOLE_0:def 4;
      end;
      L~go /\ L~pion1 c= {pion1/.1}
      proof
        let x be set;
        assume
A127:   x in L~go /\ L~pion1;
        then
A128:   x in L~go by XBOOLE_0:def 4;
        x in L~pion1 by A127,XBOOLE_0:def 4;
        hence thesis by A7,A12,A43,A48,A87,A103,A128,XBOOLE_0:def 4;
      end;
      then
A129: L~go /\ L~pion1 = {pion1/.1} by A124,XBOOLE_0:def 10;
      then
A130: (go^'pion1) is s.n.c. by A103,JORDAN1J:54;
      rng go /\ rng pion1 c= {pion1/.1} by A63,A123,A129,XBOOLE_1:27;
      then
A131: go^'pion1 is one-to-one by JORDAN1J:55;
A132: pion/.len pion = pion/.3 by FINSEQ_1:62
        .= do/.1 by A44,FINSEQ_4:27;
A133: {pion1/.len pion1} c= L~do /\ L~pion1
      proof
        let x be set;
        assume x in {pion1/.len pion1};
        then
A134:   x = pion1/.len pion1 by TARSKI:def 1;
        then
A135:   x in rng do by A89,A132,FINSEQ_6:46;
        x in rng pion1 by A134,REVROT_1:3;
        hence thesis by A64,A123,A135,XBOOLE_0:def 4;
      end;
      L~do /\ L~pion1 c= {pion1/.len pion1}
      proof
        let x be set;
        assume
A136:   x in L~do /\ L~pion1;
        then
A137:   x in L~do by XBOOLE_0:def 4;
        x in L~pion1 by A136,XBOOLE_0:def 4;
        hence thesis by A8,A12,A44,A55,A87,A89,A132,A137,XBOOLE_0:def 4;
      end;
      then
A138: L~do /\ L~pion1 = {pion1/.len pion1} by A133,XBOOLE_0:def 10;
A139: L~(go^'pion1) /\ L~do = (L~go \/ L~pion1) /\ L~do by A103,TOPREAL8:35
        .= {go/.1} \/ {do/.1} by A75,A89,A132,A138,XBOOLE_1:23
        .= {(go^'pion1)/.1} \/ {do/.1} by GRAPH_2:57
        .= {(go^'pion1)/.1,do/.1} by ENUMSET1:41;
      do/.len do = (go^'pion1)/.1 by A62,GRAPH_2:57;
      then reconsider godo as non constant standard special_circular_sequence
      by A101,A105,A106,A111,A113,A121,A122,A130,A131,A139,JORDAN8:7,8
,TOPREAL8:11,33,34;
A140: UA is_an_arc_of W-min C,E-max C by JORDAN6:def 8;
      then
A141: UA is connected by JORDAN6:11;
A142: W-min C in UA by A140,TOPREAL1:4;
A143: E-max C in UA by A140,TOPREAL1:4;
      set ff = Rotate(Cage(C,n),Wmin);
      Wmin in rng Cage(C,n) by SPRECT_2:47;
      then
A144: ff/.1 = Wmin by FINSEQ_6:98;
A145: L~ff = L~Cage(C,n) by REVROT_1:33;
      then (W-max L~ff)..ff > 1 by A144,SPRECT_5:23;
      then (N-min L~ff)..ff > 1 by A144,A145,SPRECT_5:24,XXREAL_0:2;
      then (N-max L~ff)..ff > 1 by A144,A145,SPRECT_5:25,XXREAL_0:2;
      then
A146: Emax..ff > 1 by A144,A145,SPRECT_5:26,XXREAL_0:2;
A147: now
        assume
A148:   Gij..US <= 1;
        Gij..US >= 1 by A36,FINSEQ_4:31;
        then Gij..US = 1 by A148,XXREAL_0:1;
        then Gij = US/.1 by A36,FINSEQ_5:41;
        hence contradiction by A22,A26,JORDAN1F:5;
      end;
A149: Cage(C,n) is_sequence_on G by JORDAN9:def 1;
      then
A150: ff is_sequence_on G by REVROT_1:34;
A151: right_cell(godo,1,G)\L~godo c= RightComp godo by A101,A106,JORDAN9:29;
A152: L~godo = L~(go^'pion1) \/ L~do by A105,TOPREAL8:35
        .= L~go \/ L~pion1 \/ L~do by A103,TOPREAL8:35;
A153: L~Cage(C,n) = L~US \/ L~LS by JORDAN1E:17;
      then
A154: L~US c= L~Cage(C,n) by XBOOLE_1:7;
A155: L~LS c= L~Cage(C,n) by A153,XBOOLE_1:7;
A156: L~go c=L~Cage(C,n) by A48,A154,XBOOLE_1:1;
A157: L~do c=L~Cage(C,n) by A55,A155,XBOOLE_1:1;
A158: W-min C in C by SPRECT_1:15;
A159: now
        assume W-min C in L~godo;
        then
A160:   W-min C in L~go \/ L~pion1 or W-min C in L~do by A152,XBOOLE_0:def 3;
        per cases by A160,XBOOLE_0:def 3;
        suppose W-min C in L~go;
          then C meets L~Cage(C,n) by A156,A158,XBOOLE_0:3;
          hence contradiction by JORDAN10:5;
        end;
        suppose W-min C in L~pion1;
          hence contradiction by A9,A12,A87,A142,XBOOLE_0:3;
        end;
        suppose W-min C in L~do;
          then C meets L~Cage(C,n) by A157,A158,XBOOLE_0:3;
          hence contradiction by JORDAN10:5;
        end;
      end;
      right_cell(Rotate(Cage(C,n),Wmin),1) =
      right_cell(ff,1,GoB ff) by A97,JORDAN1H:29
        .= right_cell(ff,1,GoB Cage(C,n)) by REVROT_1:28
        .= right_cell(ff,1,G) by JORDAN1H:52
        .= right_cell(ff-:Emax,1,G) by A146,A150,JORDAN1J:53
        .= right_cell(US,1,G) by JORDAN1E:def 1
        .= right_cell(R_Cut(US,Gij),1,G) by A36,A102,A147,JORDAN1J:52
        .= right_cell(go^'pion1,1,G) by A41,A104,JORDAN1J:51
        .= right_cell(godo,1,G) by A99,A106,JORDAN1J:51;
      then W-min C in right_cell(godo,1,G) by JORDAN1I:8;
      then
A161: W-min C in right_cell(godo,1,G)\L~godo by A159,XBOOLE_0:def 5;
A162: godo/.1 = (go^'pion1)/.1 by GRAPH_2:57
        .= Wmin by A61,GRAPH_2:57;
A163: len US >= 2 by A21,XXREAL_0:2;
A164: godo/.2 = (go^'pion1)/.2 by A98,GRAPH_2:61
        .= US/.2 by A35,A78,GRAPH_2:61
        .= (US^'LS)/.2 by A163,GRAPH_2:61
        .= Rotate(Cage(C,n),Wmin)/.2 by JORDAN1E:15;
A165: L~go \/ L~do is compact by COMPTS_1:19;
      Wmin in L~go \/ L~do by A63,A79,XBOOLE_0:def 3;
      then
A166: W-min (L~go \/ L~do) = Wmin by A156,A157,A165,JORDAN1J:21,XBOOLE_1:8;
A167: (W-min (L~go \/ L~do))`1 = W-bound (L~go \/ L~do) by EUCLID:56;
A168: Wmin`1 = Wbo by EUCLID:56;
      Gik`1 >= Wbo by A13,A155,PSCOMP_1:71;
      then Gik`1 > Wbo by A77,XXREAL_0:1;
      then W-min (L~go\/L~do\/L~pion1) = W-min (L~go \/ L~do)
      by A95,A165,A166,A167,A168,JORDAN1J:33;
      then
A169: W-min L~godo = Wmin by A152,A166,XBOOLE_1:4;
A170: rng godo c= L~godo by A98,A100,SPPOL_2:18,XXREAL_0:2;
      2 in dom godo by A101,FINSEQ_3:27;
      then
A171: godo/.2 in rng godo by PARTFUN2:4;
      godo/.2 in W-most L~Cage(C,n) by A164,JORDAN1I:27;
      then (godo/.2)`1 = (W-min L~godo)`1 by A169,PSCOMP_1:88
        .= W-bound L~godo by EUCLID:56;
      then godo/.2 in W-most L~godo by A170,A171,SPRECT_2:16;
      then Rotate(godo,W-min L~godo)/.2 in W-most L~godo
      by A162,A169,FINSEQ_6:95;
      then reconsider godo as clockwise_oriented non constant standard
      special_circular_sequence by JORDAN1I:27;
      len US in dom US by FINSEQ_5:6;
      then
A172: US.len US = US/.len US by PARTFUN1:def 8
        .= Emax by JORDAN1F:7;
A173: east_halfline E-max C misses L~go
      proof
        assume east_halfline E-max C meets L~go;
        then consider p be set such that
A174:   p in east_halfline E-max C and
A175:   p in L~go by XBOOLE_0:3;
        reconsider p as Point of TOP-REAL 2 by A174;
        p in L~US by A48,A175;
        then p in east_halfline E-max C /\ L~Cage(C,n)
        by A154,A174,XBOOLE_0:def 4;
        then
A176:   p`1 = Ebo by JORDAN1A:104,PSCOMP_1:111;
        then
A177:   p = Emax by A48,A175,JORDAN1J:46;
        then Emax = Gij by A14,A172,A175,JORDAN1J:43;
        then Gij`1 = G*(len G,k)`1 by A6,A16,A20,A176,A177,JORDAN1A:92;
        hence contradiction by A3,A17,A32,JORDAN1G:7;
      end;
      now
        assume east_halfline E-max C meets L~godo;
        then
A178:   east_halfline E-max C meets (L~go \/ L~pion1) or
        east_halfline E-max C meets L~do by A152,XBOOLE_1:70;
        per cases by A178,XBOOLE_1:70;
        suppose east_halfline E-max C meets L~go;
          hence contradiction by A173;
        end;
        suppose east_halfline E-max C meets L~pion1;
          then consider p be set such that
A179:     p in east_halfline E-max C and
A180:     p in L~pion1 by XBOOLE_0:3;
          reconsider p as Point of TOP-REAL 2 by A179;
A181:     now per cases by A12,A87,A180,XBOOLE_0:def 3;
            suppose p in poz;
              hence p`1 <= Gij`1 by A91,A92,TOPREAL1:9;
            end;
            suppose p in pio;
              hence p`1 <= Gij`1 by A91,GOBOARD7:5;
            end;
          end;
          i1+1 <= len G by A3,NAT_1:13;
          then i1 <= len G-1 by XREAL_1:21;
          then
A182:     i1 <= len G-'1 by XREAL_0:def 2;
          len G-'1 <= len G by NAT_D:35;
          then Gij`1 <= G*(len G-'1,1)`1 by A4,A10,A15,A20,A24,A182,JORDAN1A:39
;
          then p`1 <= G*(len G-'1,1)`1 by A181,XXREAL_0:2;
          then p`1 <= E-bound C by A24,JORDAN8:15;
          then
A183:     p`1 <= (E-max C)`1 by EUCLID:56;
          p`1 >= (E-max C)`1 by A179,TOPREAL1:def 13;
          then
A184:     p`1 = (E-max C)`1 by A183,XXREAL_0:1;
          p`2 = (E-max C)`2 by A179,TOPREAL1:def 13;
          then p = E-max C by A184,TOPREAL3:11;
          hence contradiction by A9,A12,A87,A143,A180,XBOOLE_0:3;
        end;
        suppose east_halfline E-max C meets L~do;
          then consider p be set such that
A185:     p in east_halfline E-max C and
A186:     p in L~do by XBOOLE_0:3;
          reconsider p as Point of TOP-REAL 2 by A185;
          p in L~LS by A55,A186;
          then p in east_halfline E-max C /\ L~Cage(C,n)
          by A155,A185,XBOOLE_0:def 4;
          then
A187:     p`1 = Ebo by JORDAN1A:104,PSCOMP_1:111;
A188:     (E-max C)`2 = p`2 by A185,TOPREAL1:def 13;
          set RC = Rotate(Cage(C,n),Emax);
A189:     E-max C in right_cell(RC,1) by JORDAN1I:9;
A190:     1+1 <= len LS by A27,XXREAL_0:2;
          LS = RC-:Wmin by JORDAN1G:26;
          then
A191:     LSeg(LS,1) = LSeg(RC,1) by A190,SPPOL_2:9;
A192:     L~RC = L~Cage(C,n) by REVROT_1:33;
A193:     len RC = len Cage(C,n) by REVROT_1:14;
A194:     GoB RC = GoB Cage(C,n) by REVROT_1:28
            .= G by JORDAN1H:52;
A195:     Emax in rng Cage(C,n) by SPRECT_2:50;
A196:     RC is_sequence_on G by A149,REVROT_1:34;
A197:     RC/.1 = E-max L~RC by A192,A195,FINSEQ_6:98;
          consider ii,jj be Element of NAT such that
A198:     [ii,jj+1] in Indices G and
A199:     [ii,jj] in Indices G and
A200:     RC/.1 = G*(ii,jj+1) and
A201:     RC/.(1+1) = G*(ii,jj) by A96,A192,A193,A195,A196,FINSEQ_6:98
,JORDAN1I:25;
          consider jj2 be Element of NAT such that
A202:     1 <= jj2 and
A203:     jj2 <= width G and
A204:     Emax = G*(len G,jj2) by JORDAN1D:29;
A205:     len G >= 4 by JORDAN8:13;
          then len G >= 1 by XXREAL_0:2;
          then [len G,jj2] in Indices G by A202,A203,MATRIX_1:37;
          then
A206:     ii = len G by A192,A197,A198,A200,A204,GOBOARD1:21;
A207:     1 <= ii by A198,MATRIX_1:39;
A208:     ii <= len G by A198,MATRIX_1:39;
A209:     1 <= jj+1 by A198,MATRIX_1:39;
A210:     jj+1 <= width G by A198,MATRIX_1:39;
A211:     1 <= ii by A199,MATRIX_1:39;
A212:     ii <= len G by A199,MATRIX_1:39;
A213:     1 <= jj by A199,MATRIX_1:39;
A214:     jj <= width G by A199,MATRIX_1:39;
A215:     ii+1 <> ii;
          jj+1+1 <> jj;
          then
A216:     right_cell(RC,1) = cell(G,ii-'1,jj)
          by A96,A193,A194,A198,A199,A200,A201,A215,GOBOARD5:def 6;
A217:     ii-'1+1 = ii by A207,XREAL_1:237;
          ii-1 >= 4-1 by A205,A206,XREAL_1:11;
          then
A218:     ii-1 >= 1 by XXREAL_0:2;
          then
A219:     1 <= ii-'1 by XREAL_0:def 2;
A220:     G*(ii-'1,jj)`2 <= p`2 by A188,A189,A208,A210,A213,A216,A217,A218,
JORDAN9:19;
A221:     p`2 <= G*(ii-'1,jj+1)`2 by A188,A189,A208,A210,A213,A216,A217,A218,
JORDAN9:19;
A222:     ii-'1 < len G by A208,A217,NAT_1:13;
          then
A223:     G*(ii-'1,jj)`2 = G*(1,jj)`2 by A213,A214,A219,GOBOARD5:2
            .= G*(ii,jj)`2 by A211,A212,A213,A214,GOBOARD5:2;
A224:     G*(ii-'1,jj+1)`2 = G*(1,jj+1)`2 by A209,A210,A219,A222,GOBOARD5:2
            .= G*(ii,jj+1)`2 by A207,A208,A209,A210,GOBOARD5:2;
A225:     G*(len G,jj)`1 = Ebo by A20,A213,A214,JORDAN1A:92;
          Ebo = G*(len G,jj+1)`1 by A20,A209,A210,JORDAN1A:92;
          then p in LSeg(RC/.1,RC/.(1+1))
          by A187,A200,A201,A206,A220,A221,A223,A224,A225,GOBOARD7:8;
          then
A226:     p in LSeg(LS,1) by A96,A191,A193,TOPREAL1:def 5;
A227:     p in LSeg(do,Index(p,do)) by A186,JORDAN3:42;
A228:     do = mid(LS,Gik..LS,len LS) by A39,JORDAN1J:37;
A229:     1<=Gik..LS by A39,FINSEQ_4:31;
A230:     Gik..LS<=len LS by A39,FINSEQ_4:31;
          Gik..LS <> len LS by A31,A39,FINSEQ_4:29;
          then
A231:     Gik..LS < len LS by A230,XXREAL_0:1;
A232:     1<=Index(p,do) by A186,JORDAN3:41;
A233:     Index(p,do) < len do by A186,JORDAN3:41;
A234:     Index(Gik,LS)+1 = Gik..LS by A34,A39,JORDAN1J:56;
          consider t be Nat such that
A235:     t in dom LS and
A236:     LS.t = Gik by A39,FINSEQ_2:11;
A237:     1 <= t by A235,FINSEQ_3:27;
A238:     t <= len LS by A235,FINSEQ_3:27;
          1 < t by A34,A236,A237,XXREAL_0:1;
          then Index(Gik,LS)+1 = t by A236,A238,JORDAN3:45;
          then
A239:     len L_Cut(LS,Gik) = len LS-Index(Gik,LS) by A13,A236,JORDAN3:61;
          set tt = Index(p,do)+(Gik..LS)-'1;
A240:     1<=Index(Gik,LS) by A13,JORDAN3:41;
          0+Index(Gik,LS) < len LS by A13,JORDAN3:41;
          then
A241:     len LS-Index(Gik,LS) > 0 by XREAL_1:22;
          Index(p,do) < len LS-'Index(Gik,LS) by A233,A239,XREAL_0:def 2;
          then Index(p,do)+1 <= len LS-'Index(Gik,LS) by NAT_1:13;
          then Index(p,do) <= len LS-'Index(Gik,LS)-1 by XREAL_1:21;
          then Index(p,do) <= len LS-Index(Gik,LS)-1 by A241,XREAL_0:def 2;
          then Index(p,do) <= len LS-Gik..LS by A234;
          then Index(p,do) <= len LS-'Gik..LS by XREAL_0:def 2;
          then Index(p,do) < len LS-'(Gik..LS)+1 by NAT_1:13;
          then
A242:     LSeg(mid(LS,Gik..LS,len LS),Index(p,do)) =
          LSeg(LS,Index(p,do)+(Gik..LS)-'1) by A229,A231,A232,JORDAN4:31;
A243:     1+1 <= Gik..LS by A234,A240,XREAL_1:9;
          then Index(p,do)+Gik..LS >= 1+1+1 by A232,XREAL_1:9;
          then Index(p,do)+Gik..LS-1 >= 1+1+1-1 by XREAL_1:11;
          then
A244:     tt >= 1+1 by XREAL_0:def 2;
A245:     2 in dom LS by A190,FINSEQ_3:27;
          now per cases by A244,XXREAL_0:1;
            suppose tt > 1+1;
              then LSeg(LS,1) misses LSeg(LS,tt) by TOPREAL1:def 9;
              hence contradiction by A226,A227,A228,A242,XBOOLE_0:3;
            end;
            suppose
A246:         tt = 1+1;
              then LSeg(LS,1) /\ LSeg(LS,tt) = {LS/.2} by A27,TOPREAL1:def 8;
              then p in {LS/.2} by A226,A227,A228,A242,XBOOLE_0:def 4;
              then
A247:         p = LS/.2 by TARSKI:def 1;
              then
A248:         p..LS = 2 by A245,FINSEQ_5:44;
              1+1 = Index(p,do)+(Gik..LS)-1 by A246,XREAL_0:def 2;
              then 1+1+1 = Index(p,do)+(Gik..LS);
              then
A249:         Gik..LS = 2 by A232,A243,JORDAN1E:10;
              p in rng LS by A245,A247,PARTFUN2:4;
              then p = Gik by A39,A248,A249,FINSEQ_5:10;
              then Gik`1 = Ebo by A247,JORDAN1G:40;
              then Gik`1 = G*(len G,j)`1 by A4,A15,A20,JORDAN1A:92;
              hence contradiction by A2,A3,A18,A69,JORDAN1G:7;
            end;
          end;
          hence contradiction;
        end;
      end;
      then east_halfline E-max C c= (L~godo)` by SUBSET_1:43;
      then consider W be Subset of TOP-REAL 2 such that
A250: W is_a_component_of (L~godo)` and
A251: east_halfline E-max C c= W by GOBOARD9:5;
      W is not Bounded by A251,JORDAN2C:16,129;
      then W is_outside_component_of L~godo by A250,JORDAN2C:def 4;
      then W c= UBD L~godo by JORDAN2C:27;
      then
A252: east_halfline E-max C c= UBD L~godo by A251,XBOOLE_1:1;
      E-max C in east_halfline E-max C by TOPREAL1:45;
      then E-max C in UBD L~godo by A252;
      then E-max C in LeftComp godo by GOBRD14:46;
      then UA meets L~godo by A141,A142,A143,A151,A161,JORDAN1J:36;
      then
A253: UA meets (L~go \/ L~pion1) or UA meets L~do by A152,XBOOLE_1:70;
A254: UA c= C by JORDAN6:76;
      now per cases by A253,XBOOLE_1:70;
        suppose UA meets L~go;
          then UA meets L~Cage(C,n) by A48,A154,XBOOLE_1:1,63;
          hence contradiction by A254,JORDAN10:5,XBOOLE_1:63;
        end;
        suppose UA meets L~pion1;
          hence contradiction by A9,A12,A87;
        end;
        suppose UA meets L~do;
          then UA meets L~Cage(C,n) by A55,A155,XBOOLE_1:1,63;
          hence contradiction by A254,JORDAN10:5,XBOOLE_1:63;
        end;
      end;
      hence contradiction;
    end;
    suppose Gik`1 = Gij`1;
      then
A255: i1 = i2 by A17,A18,JORDAN1G:7;
      then poz = {Gi1k} by RLTOPSP1:71;
      then poz c= pio by A83,ZFMISC_1:37;
      then pio \/ poz = pio by XBOOLE_1:12;
      hence contradiction by A1,A3,A4,A5,A6,A7,A8,A9,A255,Th13;
    end;
    suppose Gik`2 = Gij`2;
      then
A256: j = k by A17,A18,JORDAN1G:6;
      then pio = {Gi1k} by RLTOPSP1:71;
      then pio c= poz by A84,ZFMISC_1:37;
      then pio \/ poz = poz by XBOOLE_1:12;
      hence contradiction by A1,A2,A3,A4,A6,A7,A8,A9,A256,JORDAN15:31;
    end;
  end;
  hence contradiction;
end;

theorem Th24:
  for C be Simple_closed_curve for i1,i2,j,k be Element of NAT st
  1 < i2 & i2 <= i1 & i1 < len Gauge(C,n) &
  1 <= j & j <= k & k <= width Gauge(C,n) &
  (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
  LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) /\ L~Upper_Seq(C,n) =
  {Gauge(C,n)*(i1,j)} & (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
  LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) /\ L~Lower_Seq(C,n) =
  {Gauge(C,n)*(i2,k)} holds (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
  LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) meets Lower_Arc C
proof
  let C be Simple_closed_curve;
  let i1,i2,j,k be Element of NAT;
  set G = Gauge(C,n);
  set pio = LSeg(G*(i1,j),G*(i1,k));
  set poz = LSeg(G*(i1,k),G*(i2,k));
  set US = Upper_Seq(C,n);
  set LS = Lower_Seq(C,n);
  assume that
A1: 1 < i2 and
A2: i2 <= i1 and
A3: i1 < len G and
A4: 1 <= j and
A5: j <= k and
A6: k <= width G and
A7: (pio \/ poz) /\ L~US = {G*(i1,j)} and
A8: (pio \/ poz) /\ L~LS = {G*(i2,k)} and
A9: (pio \/ poz) misses Lower_Arc C;
  set UA = Lower_Arc C;
  set Wmin = W-min L~Cage(C,n);
  set Emax = E-max L~Cage(C,n);
  set Wbo = W-bound L~Cage(C,n);
  set Ebo = E-bound L~Cage(C,n);
  set Gik = G*(i2,k);
  set Gij = G*(i1,j);
  set Gi1k = G*(i1,k);
A10: 1 < i1 by A1,A2,XXREAL_0:2;
A11: i2 < len G by A2,A3,XXREAL_0:2;
A12: L~<*Gij,Gi1k,Gik*> = poz \/ pio by TOPREAL3:23;
  Gik in {Gik} by TARSKI:def 1;
  then
A13: Gik in L~LS by A8,XBOOLE_0:def 4;
  Gij in {Gij} by TARSKI:def 1;
  then
A14: Gij in L~US by A7,XBOOLE_0:def 4;
A15: j <= width G by A5,A6,XXREAL_0:2;
A16: 1 <= k by A4,A5,XXREAL_0:2;
A17: [i1,j] in Indices G by A3,A4,A10,A15,MATRIX_1:37;
A18: [i2,k] in Indices G by A1,A6,A11,A16,MATRIX_1:37;
A19: [i1,k] in Indices G by A3,A6,A10,A16,MATRIX_1:37;
  set go = R_Cut(US,Gij);
  set do = L_Cut(LS,Gik);
A20: len G = width G by JORDAN8:def 1;
A21: len US >= 3 by JORDAN1E:19;
  then len US >= 1 by XXREAL_0:2;
  then 1 in dom US by FINSEQ_3:27;
  then
A22: US.1 = US/.1 by PARTFUN1:def 8
    .= Wmin by JORDAN1F:5;
A23: Wmin`1 = Wbo by EUCLID:56
    .= G*(1,k)`1 by A6,A16,A20,JORDAN1A:94;
  len G >= 4 by JORDAN8:13;
  then
A24: len G >= 1 by XXREAL_0:2;
  then
A25: [1,k] in Indices G by A6,A16,MATRIX_1:37;
  then
A26: Gij <> US.1 by A1,A2,A17,A22,A23,JORDAN1G:7;
  then reconsider go as being_S-Seq FinSequence of TOP-REAL 2
  by A14,JORDAN3:70;
A27: len LS >= 1+2 by JORDAN1E:19;
  then
A28: len LS >= 1 by XXREAL_0:2;
  then
A29: 1 in dom LS by FINSEQ_3:27;
  len LS in dom LS by A28,FINSEQ_3:27;
  then
A30: LS.len LS = LS/.len LS by PARTFUN1:def 8
    .= Wmin by JORDAN1F:8;
  Wmin`1 = Wbo by EUCLID:56
    .= G*(1,k)`1 by A6,A16,A20,JORDAN1A:94;
  then
A31: Gik <> LS.len LS by A1,A18,A25,A30,JORDAN1G:7;
  then reconsider do as being_S-Seq FinSequence of TOP-REAL 2
  by A13,JORDAN3:69;
A32: [len G,k] in Indices G by A6,A16,A24,MATRIX_1:37;
A33: LS.1 = LS/.1 by A29,PARTFUN1:def 8
    .= Emax by JORDAN1F:6;
  Emax`1 = Ebo by EUCLID:56
    .= G*(len G,k)`1 by A6,A16,A20,JORDAN1A:92;
  then
A34: Gik <> LS.1 by A2,A3,A18,A32,A33,JORDAN1G:7;
A35: len go >= 1+1 by TOPREAL1:def 10;
A36: Gij in rng US by A3,A4,A10,A14,A15,JORDAN1G:4,JORDAN1J:40;
  then
A37: go is_sequence_on G by JORDAN1G:4,JORDAN1J:38;
A38: len do >= 1+1 by TOPREAL1:def 10;
A39: Gik in rng LS by A1,A6,A11,A13,A16,JORDAN1G:5,JORDAN1J:40;
  then
A40: do is_sequence_on G by JORDAN1G:5,JORDAN1J:39;
  reconsider go as non constant s.c.c.
  (being_S-Seq FinSequence of TOP-REAL 2) by A35,A37,JGRAPH_1:16,JORDAN8:8;
  reconsider do as non constant s.c.c.
  (being_S-Seq FinSequence of TOP-REAL 2) by A38,A40,JGRAPH_1:16,JORDAN8:8;
A41: len go > 1 by A35,NAT_1:13;
  then
A42: len go in dom go by FINSEQ_3:27;
  then
A43: go/.len go = go.len go by PARTFUN1:def 8
    .= Gij by A14,JORDAN3:59;
  len do >= 1 by A38,XXREAL_0:2;
  then 1 in dom do by FINSEQ_3:27;
  then
A44: do/.1 = do.1 by PARTFUN1:def 8
    .= Gik by A13,JORDAN3:58;
  reconsider m = len go - 1 as Element of NAT by A42,FINSEQ_3:28;
A45: m+1 = len go;
  then
A46: len go-'1 = m by NAT_D:34;
A47: LSeg(go,m) c= L~go by TOPREAL3:26;
A48: L~go c= L~US by A14,JORDAN3:76;
  then LSeg(go,m) c= L~US by A47,XBOOLE_1:1;
  then
A49: LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> c= {Gij} by A7,A12,XBOOLE_1:26;
  m >= 1 by A35,XREAL_1:21;
  then
A50: LSeg(go,m) = LSeg(go/.m,Gij) by A43,A45,TOPREAL1:def 5;
  {Gij} c= LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*>
  proof
    let x be set;
    assume x in {Gij};
    then
A51: x = Gij by TARSKI:def 1;
A52: Gij in LSeg(go,m) by A50,RLTOPSP1:69;
    Gij in LSeg(Gij,Gi1k) by RLTOPSP1:69;
    then Gij in LSeg(Gij,Gi1k) \/ LSeg(Gi1k,Gik) by XBOOLE_0:def 3;
    then Gij in L~<*Gij,Gi1k,Gik*> by SPRECT_1:10;
    hence thesis by A51,A52,XBOOLE_0:def 4;
  end;
  then
A53: LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> = {Gij} by A49,XBOOLE_0:def 10;
A54: LSeg(do,1) c= L~do by TOPREAL3:26;
A55: L~do c= L~LS by A13,JORDAN3:77;
  then LSeg(do,1) c= L~LS by A54,XBOOLE_1:1;
  then
A56: LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*> c= {Gik} by A8,A12,XBOOLE_1:26;
A57: LSeg(do,1) = LSeg(Gik,do/.(1+1)) by A38,A44,TOPREAL1:def 5;
  {Gik} c= LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*>
  proof
    let x be set;
    assume x in {Gik};
    then
A58: x = Gik by TARSKI:def 1;
A59: Gik in LSeg(do,1) by A57,RLTOPSP1:69;
    Gik in LSeg(Gi1k,Gik) by RLTOPSP1:69;
    then Gik in LSeg(Gij,Gi1k) \/ LSeg(Gi1k,Gik) by XBOOLE_0:def 3;
    then Gik in L~<*Gij,Gi1k,Gik*> by SPRECT_1:10;
    hence thesis by A58,A59,XBOOLE_0:def 4;
  end;
  then
A60: L~<*Gij,Gi1k,Gik*> /\ LSeg(do,1) = {Gik} by A56,XBOOLE_0:def 10;
A61: go/.1 = US/.1 by A14,SPRECT_3:39
    .= Wmin by JORDAN1F:5;
  then
A62: go/.1 = LS/.len LS by JORDAN1F:8
    .= do/.len do by A13,JORDAN1J:35;
A63: rng go c= L~go by A35,SPPOL_2:18;
A64: rng do c= L~do by A38,SPPOL_2:18;
A65: {go/.1} c= L~go /\ L~do
  proof
    let x be set;
    assume x in {go/.1};
    then
A66: x = go/.1 by TARSKI:def 1;
    then
A67: x in rng go by FINSEQ_6:46;
    x in rng do by A62,A66,REVROT_1:3;
    hence thesis by A63,A64,A67,XBOOLE_0:def 4;
  end;
A68: LS.1 = LS/.1 by A29,PARTFUN1:def 8
    .= Emax by JORDAN1F:6;
A69: [len G,j] in Indices G by A4,A15,A24,MATRIX_1:37;
  L~go /\ L~do c= {go/.1}
  proof
    let x be set;
    assume
A70: x in L~go /\ L~do;
    then
A71: x in L~go by XBOOLE_0:def 4;
A72: x in L~do by A70,XBOOLE_0:def 4;
    then x in L~US /\ L~LS by A48,A55,A71,XBOOLE_0:def 4;
    then x in {Wmin,Emax} by JORDAN1E:20;
    then
A73: x = Wmin or x = Emax by TARSKI:def 2;
    now
      assume x = Emax;
      then
A74:  Emax = Gik by A13,A68,A72,JORDAN1E:11;
      G*(len G,j)`1 = Ebo by A4,A15,A20,JORDAN1A:92;
      then Emax`1 <> Ebo by A2,A3,A18,A69,A74,JORDAN1G:7;
      hence contradiction by EUCLID:56;
    end;
    hence thesis by A61,A73,TARSKI:def 1;
  end;
  then
A75: L~go /\ L~do = {go/.1} by A65,XBOOLE_0:def 10;
  set W2 = go/.2;
A76: 2 in dom go by A35,FINSEQ_3:27;
A77: now
    assume Gik`1 = Wbo;
    then G*(1,k)`1 = G*(i2,k)`1 by A6,A16,A20,JORDAN1A:94;
    hence contradiction by A1,A18,A25,JORDAN1G:7;
  end;
  go = mid(US,1,Gij..US) by A36,JORDAN1G:57
    .= US|(Gij..US) by A36,FINSEQ_4:31,FINSEQ_6:122;
  then
A78: W2 = US/.2 by A76,FINSEQ_4:85;
A79: Wmin in rng go by A61,FINSEQ_6:46;
  set pion = <*Gij,Gi1k,Gik*>;
A80: now
    let n be Element of NAT;
    assume n in dom pion;
    then n in {1,2,3} by FINSEQ_3:1,30;
    then n = 1 or n = 2 or n = 3 by ENUMSET1:def 1;
    hence
    ex i,j be Element of NAT st [i,j] in Indices G & pion/.n = G*(i,j)
    by A17,A18,A19,FINSEQ_4:27;
  end;
A81: Gi1k`1 = G*(i1,1)`1 by A3,A6,A10,A16,GOBOARD5:3
    .= Gij`1 by A3,A4,A10,A15,GOBOARD5:3;
  Gi1k`2 = G*(1,k)`2 by A3,A6,A10,A16,GOBOARD5:2
    .= Gik`2 by A1,A6,A11,A16,GOBOARD5:2;
  then
A82: Gi1k = |[Gij`1,Gik`2]| by A81,EUCLID:57;
A83: Gi1k in pio by RLTOPSP1:69;
A84: Gi1k in poz by RLTOPSP1:69;
  now per cases;
    suppose Gik`1 <> Gij`1 & Gik`2 <> Gij`2;
      then pion is being_S-Seq by A82,TOPREAL3:41;
      then consider pion1 be FinSequence of TOP-REAL 2 such that
A85:  pion1 is_sequence_on G and
A86:  pion1 is being_S-Seq and
A87:  L~pion = L~pion1 and
A88:  pion/.1 = pion1/.1 and
A89:  pion/.len pion = pion1/.len pion1 and
A90:  len pion <= len pion1 by A80,GOBOARD3:2;
      reconsider pion1 as being_S-Seq FinSequence of TOP-REAL 2 by A86;
      set godo = go^'pion1^'do;
A91:  Gi1k`1 = G*(i1,1)`1 by A3,A6,A10,A16,GOBOARD5:3
        .= Gij`1 by A3,A4,A10,A15,GOBOARD5:3;
A92:  Gik`1 <= Gi1k`1 by A1,A2,A3,A6,A16,JORDAN1A:39;
      then
A93:  W-bound poz = Gik`1 by SPRECT_1:62;
A94:  W-bound pio = Gij`1 by A91,SPRECT_1:62;
      W-bound (poz \/ pio) = min(W-bound poz, W-bound pio) by SPRECT_1:54
        .= Gik`1 by A91,A92,A93,A94,XXREAL_0:def 9;
      then
A95:  W-bound L~pion1 = Gik`1 by A87,TOPREAL3:23;
A96:  1+1 <= len Cage(C,n) by GOBOARD7:36,XXREAL_0:2;
A97:  1+1 <= len Rotate(Cage(C,n),Wmin) by GOBOARD7:36,XXREAL_0:2;
      len (go^'pion1) >= len go by TOPREAL8:7;
      then
A98:  len (go^'pion1) >= 1+1 by A35,XXREAL_0:2;
      then
A99:  len (go^'pion1) > 1+0 by NAT_1:13;
A100: len godo >= len (go^'pion1) by TOPREAL8:7;
      then
A101: 1+1 <= len godo by A98,XXREAL_0:2;
A102: US is_sequence_on G by JORDAN1G:4;
A103: go/.len go = pion1/.1 by A43,A88,FINSEQ_4:27;
      then
A104: go^'pion1 is_sequence_on G by A37,A85,TOPREAL8:12;
A105: (go^'pion1)/.len (go^'pion1) = pion/.len pion by A89,GRAPH_2:58
        .= pion/.3 by FINSEQ_1:62
        .= do/.1 by A44,FINSEQ_4:27;
      then
A106: godo is_sequence_on G by A40,A104,TOPREAL8:12;
      LSeg(pion1,1) c= L~pion by A87,TOPREAL3:26;
      then
A107: LSeg(go,len go-'1) /\ LSeg(pion1,1) c={Gij} by A46,A53,XBOOLE_1:27;
      len pion1 >= 2+1 by A90,FINSEQ_1:62;
      then
A108: len pion1 > 1+1 by NAT_1:13;
      {Gij} c= LSeg(go,m) /\ LSeg(pion1,1)
      proof
        let x be set;
        assume x in {Gij};
        then
A109:   x = Gij by TARSKI:def 1;
A110:   Gij in LSeg(go,m) by A50,RLTOPSP1:69;
        Gij in LSeg(pion1,1) by A43,A103,A108,TOPREAL1:27;
        hence thesis by A109,A110,XBOOLE_0:def 4;
      end;
      then LSeg(go,len go-'1) /\ LSeg(pion1,1) = { go/.len go }
      by A43,A46,A107,XBOOLE_0:def 10;
      then
A111: go^'pion1 is unfolded by A103,TOPREAL8:34;
      len pion1 >= 2+1 by A90,FINSEQ_1:62;
      then
A112: len pion1-2 >= 0 by XREAL_1:21;
      len (go^'pion1)+1-1 = len go+len pion1-1 by GRAPH_2:13;
      then len (go^'pion1)-1 = len go + (len pion1-2)
        .= len go + (len pion1-'2) by A112,XREAL_0:def 2;
      then
A113: len (go^'pion1)-'1 = len go + (len pion1-'2) by XREAL_0:def 2;
A114: len pion1-1 >= 1 by A108,XREAL_1:21;
      then
A115: len pion1-'1 = len pion1-1 by XREAL_0:def 2;
A116: len pion1-'2+1 = len pion1-2+1 by A112,XREAL_0:def 2
        .= len pion1-'1 by A114,XREAL_0:def 2;
      len pion1-1+1 <= len pion1;
      then
A117: len pion1-'1 < len pion1 by A115,NAT_1:13;
      LSeg(pion1,len pion1-'1) c= L~pion by A87,TOPREAL3:26;
      then
A118: LSeg(pion1,len pion1-'1) /\ LSeg(do,1) c= {Gik} by A60,XBOOLE_1:27;
      {Gik} c= LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
      proof
        let x be set;
        assume x in {Gik};
        then
A119:   x = Gik by TARSKI:def 1;
A120:   Gik in LSeg(do,1) by A57,RLTOPSP1:69;
        pion1/.(len pion1-'1+1) = pion/.3 by A89,A115,FINSEQ_1:62
          .= Gik by FINSEQ_4:27;
        then Gik in LSeg(pion1,len pion1-'1) by A114,A115,TOPREAL1:27;
        hence thesis by A119,A120,XBOOLE_0:def 4;
      end;
      then LSeg(pion1,len pion1-'1) /\ LSeg(do,1) = {Gik}
      by A118,XBOOLE_0:def 10;
      then
A121: LSeg(go^'pion1,len go+(len pion1-'2)) /\ LSeg(do,1) =
      {(go^'pion1)/.len (go^'pion1)} by A44,A103,A105,A116,A117,TOPREAL8:31;
A122: (go^'pion1) is non trivial by A98,REALSET1:13;
A123: rng pion1 c= L~pion1 by A108,SPPOL_2:18;
A124: {pion1/.1} c= L~go /\ L~pion1
      proof
        let x be set;
        assume x in {pion1/.1};
        then
A125:   x = pion1/.1 by TARSKI:def 1;
        then
A126:   x in rng go by A103,REVROT_1:3;
        x in rng pion1 by A125,FINSEQ_6:46;
        hence thesis by A63,A123,A126,XBOOLE_0:def 4;
      end;
      L~go /\ L~pion1 c= {pion1/.1}
      proof
        let x be set;
        assume
A127:   x in L~go /\ L~pion1;
        then
A128:   x in L~go by XBOOLE_0:def 4;
        x in L~pion1 by A127,XBOOLE_0:def 4;
        hence thesis by A7,A12,A43,A48,A87,A103,A128,XBOOLE_0:def 4;
      end;
      then
A129: L~go /\ L~pion1 = {pion1/.1} by A124,XBOOLE_0:def 10;
      then
A130: (go^'pion1) is s.n.c. by A103,JORDAN1J:54;
      rng go /\ rng pion1 c= {pion1/.1} by A63,A123,A129,XBOOLE_1:27;
      then
A131: go^'pion1 is one-to-one by JORDAN1J:55;
A132: pion/.len pion = pion/.3 by FINSEQ_1:62
        .= do/.1 by A44,FINSEQ_4:27;
A133: {pion1/.len pion1} c= L~do /\ L~pion1
      proof
        let x be set;
        assume x in {pion1/.len pion1};
        then
A134:   x = pion1/.len pion1 by TARSKI:def 1;
        then
A135:   x in rng do by A89,A132,FINSEQ_6:46;
        x in rng pion1 by A134,REVROT_1:3;
        hence thesis by A64,A123,A135,XBOOLE_0:def 4;
      end;
      L~do /\ L~pion1 c= {pion1/.len pion1}
      proof
        let x be set;
        assume
A136:   x in L~do /\ L~pion1;
        then
A137:   x in L~do by XBOOLE_0:def 4;
        x in L~pion1 by A136,XBOOLE_0:def 4;
        hence thesis by A8,A12,A44,A55,A87,A89,A132,A137,XBOOLE_0:def 4;
      end;
      then
A138: L~do /\ L~pion1 = {pion1/.len pion1} by A133,XBOOLE_0:def 10;
A139: L~(go^'pion1) /\ L~do = (L~go \/ L~pion1) /\ L~do by A103,TOPREAL8:35
        .= {go/.1} \/ {do/.1} by A75,A89,A132,A138,XBOOLE_1:23
        .= {(go^'pion1)/.1} \/ {do/.1} by GRAPH_2:57
        .= {(go^'pion1)/.1,do/.1} by ENUMSET1:41;
      do/.len do = (go^'pion1)/.1 by A62,GRAPH_2:57;
      then reconsider godo as non constant standard special_circular_sequence
      by A101,A105,A106,A111,A113,A121,A122,A130,A131,A139,JORDAN8:7,8
,TOPREAL8:11,33,34;
A140: UA is_an_arc_of E-max C,W-min C by JORDAN6:def 9;
      then
A141: UA is connected by JORDAN6:11;
A142: W-min C in UA by A140,TOPREAL1:4;
A143: E-max C in UA by A140,TOPREAL1:4;
      set ff = Rotate(Cage(C,n),Wmin);
      Wmin in rng Cage(C,n) by SPRECT_2:47;
      then
A144: ff/.1 = Wmin by FINSEQ_6:98;
A145: L~ff = L~Cage(C,n) by REVROT_1:33;
      then (W-max L~ff)..ff > 1 by A144,SPRECT_5:23;
      then (N-min L~ff)..ff > 1 by A144,A145,SPRECT_5:24,XXREAL_0:2;
      then (N-max L~ff)..ff > 1 by A144,A145,SPRECT_5:25,XXREAL_0:2;
      then
A146: Emax..ff > 1 by A144,A145,SPRECT_5:26,XXREAL_0:2;
A147: now
        assume
A148:   Gij..US <= 1;
        Gij..US >= 1 by A36,FINSEQ_4:31;
        then Gij..US = 1 by A148,XXREAL_0:1;
        then Gij = US/.1 by A36,FINSEQ_5:41;
        hence contradiction by A22,A26,JORDAN1F:5;
      end;
A149: Cage(C,n) is_sequence_on G by JORDAN9:def 1;
      then
A150: ff is_sequence_on G by REVROT_1:34;
A151: right_cell(godo,1,G)\L~godo c= RightComp godo by A101,A106,JORDAN9:29;
A152: L~godo = L~(go^'pion1) \/ L~do by A105,TOPREAL8:35
        .= L~go \/ L~pion1 \/ L~do by A103,TOPREAL8:35;
A153: L~Cage(C,n) = L~US \/ L~LS by JORDAN1E:17;
      then
A154: L~US c= L~Cage(C,n) by XBOOLE_1:7;
A155: L~LS c= L~Cage(C,n) by A153,XBOOLE_1:7;
A156: L~go c=L~Cage(C,n) by A48,A154,XBOOLE_1:1;
A157: L~do c=L~Cage(C,n) by A55,A155,XBOOLE_1:1;
A158: W-min C in C by SPRECT_1:15;
A159: now
        assume W-min C in L~godo;
        then
A160:   W-min C in L~go \/ L~pion1 or W-min C in L~do by A152,XBOOLE_0:def 3;
        per cases by A160,XBOOLE_0:def 3;
        suppose W-min C in L~go;
          then C meets L~Cage(C,n) by A156,A158,XBOOLE_0:3;
          hence contradiction by JORDAN10:5;
        end;
        suppose W-min C in L~pion1;
          hence contradiction by A9,A12,A87,A142,XBOOLE_0:3;
        end;
        suppose W-min C in L~do;
          then C meets L~Cage(C,n) by A157,A158,XBOOLE_0:3;
          hence contradiction by JORDAN10:5;
        end;
      end;
      right_cell(Rotate(Cage(C,n),Wmin),1) =
      right_cell(ff,1,GoB ff) by A97,JORDAN1H:29
        .= right_cell(ff,1,GoB Cage(C,n)) by REVROT_1:28
        .= right_cell(ff,1,G) by JORDAN1H:52
        .= right_cell(ff-:Emax,1,G) by A146,A150,JORDAN1J:53
        .= right_cell(US,1,G) by JORDAN1E:def 1
        .= right_cell(R_Cut(US,Gij),1,G) by A36,A102,A147,JORDAN1J:52
        .= right_cell(go^'pion1,1,G) by A41,A104,JORDAN1J:51
        .= right_cell(godo,1,G) by A99,A106,JORDAN1J:51;
      then W-min C in right_cell(godo,1,G) by JORDAN1I:8;
      then
A161: W-min C in right_cell(godo,1,G)\L~godo by A159,XBOOLE_0:def 5;
A162: godo/.1 = (go^'pion1)/.1 by GRAPH_2:57
        .= Wmin by A61,GRAPH_2:57;
A163: len US >= 2 by A21,XXREAL_0:2;
A164: godo/.2 = (go^'pion1)/.2 by A98,GRAPH_2:61
        .= US/.2 by A35,A78,GRAPH_2:61
        .= (US^'LS)/.2 by A163,GRAPH_2:61
        .= Rotate(Cage(C,n),Wmin)/.2 by JORDAN1E:15;
A165: L~go \/ L~do is compact by COMPTS_1:19;
      Wmin in L~go \/ L~do by A63,A79,XBOOLE_0:def 3;
      then
A166: W-min (L~go \/ L~do) = Wmin by A156,A157,A165,JORDAN1J:21,XBOOLE_1:8;
A167: (W-min (L~go \/ L~do))`1 = W-bound (L~go \/ L~do) by EUCLID:56;
A168: Wmin`1 = Wbo by EUCLID:56;
      Gik`1 >= Wbo by A13,A155,PSCOMP_1:71;
      then Gik`1 > Wbo by A77,XXREAL_0:1;
      then W-min (L~go\/L~do\/L~pion1) = W-min (L~go \/ L~do)
      by A95,A165,A166,A167,A168,JORDAN1J:33;
      then
A169: W-min L~godo = Wmin by A152,A166,XBOOLE_1:4;
A170: rng godo c= L~godo by A98,A100,SPPOL_2:18,XXREAL_0:2;
      2 in dom godo by A101,FINSEQ_3:27;
      then
A171: godo/.2 in rng godo by PARTFUN2:4;
      godo/.2 in W-most L~Cage(C,n) by A164,JORDAN1I:27;
      then (godo/.2)`1 = (W-min L~godo)`1 by A169,PSCOMP_1:88
        .= W-bound L~godo by EUCLID:56;
      then godo/.2 in W-most L~godo by A170,A171,SPRECT_2:16;
      then Rotate(godo,W-min L~godo)/.2 in W-most L~godo
      by A162,A169,FINSEQ_6:95;
      then reconsider godo as clockwise_oriented non constant standard
      special_circular_sequence by JORDAN1I:27;
      len US in dom US by FINSEQ_5:6;
      then
A172: US.len US = US/.len US by PARTFUN1:def 8
        .= Emax by JORDAN1F:7;
A173: east_halfline E-max C misses L~go
      proof
        assume east_halfline E-max C meets L~go;
        then consider p be set such that
A174:   p in east_halfline E-max C and
A175:   p in L~go by XBOOLE_0:3;
        reconsider p as Point of TOP-REAL 2 by A174;
        p in L~US by A48,A175;
        then p in east_halfline E-max C /\ L~Cage(C,n)
        by A154,A174,XBOOLE_0:def 4;
        then
A176:   p`1 = Ebo by JORDAN1A:104,PSCOMP_1:111;
        then
A177:   p = Emax by A48,A175,JORDAN1J:46;
        then Emax = Gij by A14,A172,A175,JORDAN1J:43;
        then Gij`1 = G*(len G,k)`1 by A6,A16,A20,A176,A177,JORDAN1A:92;
        hence contradiction by A3,A17,A32,JORDAN1G:7;
      end;
      now
        assume east_halfline E-max C meets L~godo;
        then
A178:   east_halfline E-max C meets (L~go \/ L~pion1) or
        east_halfline E-max C meets L~do by A152,XBOOLE_1:70;
        per cases by A178,XBOOLE_1:70;
        suppose east_halfline E-max C meets L~go;
          hence contradiction by A173;
        end;
        suppose east_halfline E-max C meets L~pion1;
          then consider p be set such that
A179:     p in east_halfline E-max C and
A180:     p in L~pion1 by XBOOLE_0:3;
          reconsider p as Point of TOP-REAL 2 by A179;
A181:     now per cases by A12,A87,A180,XBOOLE_0:def 3;
            suppose p in poz;
              hence p`1 <= Gij`1 by A91,A92,TOPREAL1:9;
            end;
            suppose p in pio;
              hence p`1 <= Gij`1 by A91,GOBOARD7:5;
            end;
          end;
          i1+1 <= len G by A3,NAT_1:13;
          then i1 <= len G-1 by XREAL_1:21;
          then
A182:     i1 <= len G-'1 by XREAL_0:def 2;
          len G-'1 <= len G by NAT_D:35;
          then Gij`1 <= G*(len G-'1,1)`1 by A4,A10,A15,A20,A24,A182,JORDAN1A:39
;
          then p`1 <= G*(len G-'1,1)`1 by A181,XXREAL_0:2;
          then p`1 <= E-bound C by A24,JORDAN8:15;
          then
A183:     p`1 <= (E-max C)`1 by EUCLID:56;
          p`1 >= (E-max C)`1 by A179,TOPREAL1:def 13;
          then
A184:     p`1 = (E-max C)`1 by A183,XXREAL_0:1;
          p`2 = (E-max C)`2 by A179,TOPREAL1:def 13;
          then p = E-max C by A184,TOPREAL3:11;
          hence contradiction by A9,A12,A87,A143,A180,XBOOLE_0:3;
        end;
        suppose east_halfline E-max C meets L~do;
          then consider p be set such that
A185:     p in east_halfline E-max C and
A186:     p in L~do by XBOOLE_0:3;
          reconsider p as Point of TOP-REAL 2 by A185;
          p in L~LS by A55,A186;
          then p in east_halfline E-max C /\ L~Cage(C,n)
          by A155,A185,XBOOLE_0:def 4;
          then
A187:     p`1 = Ebo by JORDAN1A:104,PSCOMP_1:111;
A188:     (E-max C)`2 = p`2 by A185,TOPREAL1:def 13;
          set RC = Rotate(Cage(C,n),Emax);
A189:     E-max C in right_cell(RC,1) by JORDAN1I:9;
A190:     1+1 <= len LS by A27,XXREAL_0:2;
          LS = RC-:Wmin by JORDAN1G:26;
          then
A191:     LSeg(LS,1) = LSeg(RC,1) by A190,SPPOL_2:9;
A192:     L~RC = L~Cage(C,n) by REVROT_1:33;
A193:     len RC = len Cage(C,n) by REVROT_1:14;
A194:     GoB RC = GoB Cage(C,n) by REVROT_1:28
            .= G by JORDAN1H:52;
A195:     Emax in rng Cage(C,n) by SPRECT_2:50;
A196:     RC is_sequence_on G by A149,REVROT_1:34;
A197:     RC/.1 = E-max L~RC by A192,A195,FINSEQ_6:98;
          consider ii,jj be Element of NAT such that
A198:     [ii,jj+1] in Indices G and
A199:     [ii,jj] in Indices G and
A200:     RC/.1 = G*(ii,jj+1) and
A201:     RC/.(1+1) = G*(ii,jj) by A96,A192,A193,A195,A196,FINSEQ_6:98
,JORDAN1I:25;
          consider jj2 be Element of NAT such that
A202:     1 <= jj2 and
A203:     jj2 <= width G and
A204:     Emax = G*(len G,jj2) by JORDAN1D:29;
A205:     len G >= 4 by JORDAN8:13;
          then len G >= 1 by XXREAL_0:2;
          then [len G,jj2] in Indices G by A202,A203,MATRIX_1:37;
          then
A206:     ii = len G by A192,A197,A198,A200,A204,GOBOARD1:21;
A207:     1 <= ii by A198,MATRIX_1:39;
A208:     ii <= len G by A198,MATRIX_1:39;
A209:     1 <= jj+1 by A198,MATRIX_1:39;
A210:     jj+1 <= width G by A198,MATRIX_1:39;
A211:     1 <= ii by A199,MATRIX_1:39;
A212:     ii <= len G by A199,MATRIX_1:39;
A213:     1 <= jj by A199,MATRIX_1:39;
A214:     jj <= width G by A199,MATRIX_1:39;
A215:     ii+1 <> ii;
          jj+1+1 <> jj;
          then
A216:     right_cell(RC,1) = cell(G,ii-'1,jj)
          by A96,A193,A194,A198,A199,A200,A201,A215,GOBOARD5:def 6;
A217:     ii-'1+1 = ii by A207,XREAL_1:237;
          ii-1 >= 4-1 by A205,A206,XREAL_1:11;
          then
A218:     ii-1 >= 1 by XXREAL_0:2;
          then
A219:     1 <= ii-'1 by XREAL_0:def 2;
A220:     G*(ii-'1,jj)`2 <= p`2 by A188,A189,A208,A210,A213,A216,A217,A218,
JORDAN9:19;
A221:     p`2 <= G*(ii-'1,jj+1)`2 by A188,A189,A208,A210,A213,A216,A217,A218,
JORDAN9:19;
A222:     ii-'1 < len G by A208,A217,NAT_1:13;
          then
A223:     G*(ii-'1,jj)`2 = G*(1,jj)`2 by A213,A214,A219,GOBOARD5:2
            .= G*(ii,jj)`2 by A211,A212,A213,A214,GOBOARD5:2;
A224:     G*(ii-'1,jj+1)`2 = G*(1,jj+1)`2 by A209,A210,A219,A222,GOBOARD5:2
            .= G*(ii,jj+1)`2 by A207,A208,A209,A210,GOBOARD5:2;
A225:     G*(len G,jj)`1 = Ebo by A20,A213,A214,JORDAN1A:92;
          Ebo = G*(len G,jj+1)`1 by A20,A209,A210,JORDAN1A:92;
          then p in LSeg(RC/.1,RC/.(1+1))
          by A187,A200,A201,A206,A220,A221,A223,A224,A225,GOBOARD7:8;
          then
A226:     p in LSeg(LS,1) by A96,A191,A193,TOPREAL1:def 5;
A227:     p in LSeg(do,Index(p,do)) by A186,JORDAN3:42;
A228:     do = mid(LS,Gik..LS,len LS) by A39,JORDAN1J:37;
A229:     1<=Gik..LS by A39,FINSEQ_4:31;
A230:     Gik..LS<=len LS by A39,FINSEQ_4:31;
          Gik..LS <> len LS by A31,A39,FINSEQ_4:29;
          then
A231:     Gik..LS < len LS by A230,XXREAL_0:1;
A232:     1<=Index(p,do) by A186,JORDAN3:41;
A233:     Index(p,do) < len do by A186,JORDAN3:41;
A234:     Index(Gik,LS)+1 = Gik..LS by A34,A39,JORDAN1J:56;
          consider t be Nat such that
A235:     t in dom LS and
A236:     LS.t = Gik by A39,FINSEQ_2:11;
A237:     1 <= t by A235,FINSEQ_3:27;
A238:     t <= len LS by A235,FINSEQ_3:27;
          1 < t by A34,A236,A237,XXREAL_0:1;
          then Index(Gik,LS)+1 = t by A236,A238,JORDAN3:45;
          then
A239:     len L_Cut(LS,Gik) = len LS-Index(Gik,LS) by A13,A236,JORDAN3:61;
          set tt = Index(p,do)+(Gik..LS)-'1;
A240:     1<=Index(Gik,LS) by A13,JORDAN3:41;
          0+Index(Gik,LS) < len LS by A13,JORDAN3:41;
          then
A241:     len LS-Index(Gik,LS) > 0 by XREAL_1:22;
          Index(p,do) < len LS-'Index(Gik,LS) by A233,A239,XREAL_0:def 2;
          then Index(p,do)+1 <= len LS-'Index(Gik,LS) by NAT_1:13;
          then Index(p,do) <= len LS-'Index(Gik,LS)-1 by XREAL_1:21;
          then Index(p,do) <= len LS-Index(Gik,LS)-1 by A241,XREAL_0:def 2;
          then Index(p,do) <= len LS-Gik..LS by A234;
          then Index(p,do) <= len LS-'Gik..LS by XREAL_0:def 2;
          then Index(p,do) < len LS-'(Gik..LS)+1 by NAT_1:13;
          then
A242:     LSeg(mid(LS,Gik..LS,len LS),Index(p,do)) =
          LSeg(LS,Index(p,do)+(Gik..LS)-'1) by A229,A231,A232,JORDAN4:31;
A243:     1+1 <= Gik..LS by A234,A240,XREAL_1:9;
          then Index(p,do)+Gik..LS >= 1+1+1 by A232,XREAL_1:9;
          then Index(p,do)+Gik..LS-1 >= 1+1+1-1 by XREAL_1:11;
          then
A244:     tt >= 1+1 by XREAL_0:def 2;
A245:     2 in dom LS by A190,FINSEQ_3:27;
          now per cases by A244,XXREAL_0:1;
            suppose tt > 1+1;
              then LSeg(LS,1) misses LSeg(LS,tt) by TOPREAL1:def 9;
              hence contradiction by A226,A227,A228,A242,XBOOLE_0:3;
            end;
            suppose
A246:         tt = 1+1;
              then LSeg(LS,1) /\ LSeg(LS,tt) = {LS/.2} by A27,TOPREAL1:def 8;
              then p in {LS/.2} by A226,A227,A228,A242,XBOOLE_0:def 4;
              then
A247:         p = LS/.2 by TARSKI:def 1;
              then
A248:         p..LS = 2 by A245,FINSEQ_5:44;
              1+1 = Index(p,do)+(Gik..LS)-1 by A246,XREAL_0:def 2;
              then 1+1+1 = Index(p,do)+(Gik..LS);
              then
A249:         Gik..LS = 2 by A232,A243,JORDAN1E:10;
              p in rng LS by A245,A247,PARTFUN2:4;
              then p = Gik by A39,A248,A249,FINSEQ_5:10;
              then Gik`1 = Ebo by A247,JORDAN1G:40;
              then Gik`1 = G*(len G,j)`1 by A4,A15,A20,JORDAN1A:92;
              hence contradiction by A2,A3,A18,A69,JORDAN1G:7;
            end;
          end;
          hence contradiction;
        end;
      end;
      then east_halfline E-max C c= (L~godo)` by SUBSET_1:43;
      then consider W be Subset of TOP-REAL 2 such that
A250: W is_a_component_of (L~godo)` and
A251: east_halfline E-max C c= W by GOBOARD9:5;
      W is not Bounded by A251,JORDAN2C:16,129;
      then W is_outside_component_of L~godo by A250,JORDAN2C:def 4;
      then W c= UBD L~godo by JORDAN2C:27;
      then
A252: east_halfline E-max C c= UBD L~godo by A251,XBOOLE_1:1;
      E-max C in east_halfline E-max C by TOPREAL1:45;
      then E-max C in UBD L~godo by A252;
      then E-max C in LeftComp godo by GOBRD14:46;
      then UA meets L~godo by A141,A142,A143,A151,A161,JORDAN1J:36;
      then
A253: UA meets (L~go \/ L~pion1) or UA meets L~do by A152,XBOOLE_1:70;
A254: UA c= C by JORDAN6:76;
      now per cases by A253,XBOOLE_1:70;
        suppose UA meets L~go;
          then UA meets L~Cage(C,n) by A48,A154,XBOOLE_1:1,63;
          hence contradiction by A254,JORDAN10:5,XBOOLE_1:63;
        end;
        suppose UA meets L~pion1;
          hence contradiction by A9,A12,A87;
        end;
        suppose UA meets L~do;
          then UA meets L~Cage(C,n) by A55,A155,XBOOLE_1:1,63;
          hence contradiction by A254,JORDAN10:5,XBOOLE_1:63;
        end;
      end;
      hence contradiction;
    end;
    suppose Gik`1 = Gij`1;
      then
A255: i1 = i2 by A17,A18,JORDAN1G:7;
      then poz = {Gi1k} by RLTOPSP1:71;
      then poz c= pio by A83,ZFMISC_1:37;
      then pio \/ poz = pio by XBOOLE_1:12;
      hence contradiction by A1,A3,A4,A5,A6,A7,A8,A9,A255,Th14;
    end;
    suppose Gik`2 = Gij`2;
      then
A256: j = k by A17,A18,JORDAN1G:6;
      then pio = {Gi1k} by RLTOPSP1:71;
      then pio c= poz by A84,ZFMISC_1:37;
      then pio \/ poz = poz by XBOOLE_1:12;
      hence contradiction by A1,A2,A3,A4,A6,A7,A8,A9,A256,JORDAN15:30;
    end;
  end;
  hence contradiction;
end;

theorem Th25:
  for C be Simple_closed_curve for i1,i2,j,k be Element of NAT holds
  1 < i1 & i1 < len Gauge(C,n+1) & 1 < i2 & i2 < len Gauge(C,n+1) &
  1 <= j & j <= k & k <= width Gauge(C,n+1) &
  Gauge(C,n+1)*(i1,k) in Lower_Arc L~Cage(C,n+1) &
  Gauge(C,n+1)*(i2,j) in Upper_Arc L~Cage(C,n+1) implies
  LSeg(Gauge(C,n+1)*(i2,j),Gauge(C,n+1)*(i2,k)) \/
  LSeg(Gauge(C,n+1)*(i2,k),Gauge(C,n+1)*(i1,k)) meets Lower_Arc C
proof
  let C be Simple_closed_curve;
  let i1,i2,j,k be Element of NAT;
  set G=Gauge(C,n+1);
  assume that
A1: 1 < i1 and
A2: i1 < len G and
A3: 1 < i2 and
A4: i2 < len G and
A5: 1 <= j and
A6: j <= k and
A7: k <= width G and
A8: G*(i1,k) in Lower_Arc L~Cage(C,n+1) and
A9: G*(i2,j) in Upper_Arc L~Cage(C,n+1);
A10: Lower_Arc L~Cage(C,n+1) = L~Lower_Seq(C,n+1) by JORDAN1G:64;
A11: Upper_Arc L~Cage(C,n+1) = L~Upper_Seq(C,n+1) by JORDAN1G:63;
A12: j <= width G by A6,A7,XXREAL_0:2;
  then
A13: [i2,j] in Indices G by A3,A4,A5,MATRIX_1:37;
A14: 1 <= k by A5,A6,XXREAL_0:2;
  then
A15: [i2,k] in Indices G by A3,A4,A7,MATRIX_1:37;
  G*(i2,j)`1 = G*(i2,1)`1 by A3,A4,A5,A12,GOBOARD5:3
    .= G*(i2,k)`1 by A3,A4,A7,A14,GOBOARD5:3;
  then
A16: LSeg(G*(i2,j),G*(i2,k)) is vertical by SPPOL_1:37;
  G*(i2,k)`2 = G*(1,k)`2 by A3,A4,A7,A14,GOBOARD5:2
    .= G*(i1,k)`2 by A1,A2,A7,A14,GOBOARD5:2;
  then
A17: LSeg(G*(i2,k),G*(i1,k)) is horizontal by SPPOL_1:36;
A18: [i2,k] in Indices G by A3,A4,A7,A14,MATRIX_1:37;
A19: [i1,k] in Indices G by A1,A2,A7,A14,MATRIX_1:37;
  now per cases;
    suppose
A20:  LSeg(G*(i2,j),G*(i2,k)) meets Lower_Arc L~Cage(C,n+1);
      then consider m be Element of NAT such that
A21:  j <= m and
A22:  m <= k and
A23:  G*(i2,m)`2 = lower_bound(proj2.:(LSeg(G*(i2,j),G*(i2,k))
      /\ L~Lower_Seq(C,n+1))) by A6,A10,A13,A15,JORDAN1F:1,JORDAN1G:5;
A24:  1 <= m by A5,A21,XXREAL_0:2;
A25:  m <= width G by A7,A22,XXREAL_0:2;
      set X = LSeg(G*(i2,j),G*(i2,k)) /\ L~Lower_Seq(C,n+1);
A26:  G*(i2,m)`1 = G*(i2,1)`1 by A3,A4,A24,A25,GOBOARD5:3;
      then
A27:  |[G*(i2,1)`1,lower_bound(proj2.:X)]| = G*(i2,m) by A23,EUCLID:57;
      then
A28:  G*(i2,j)`1 = |[G*(i2,1)`1,lower_bound(proj2.:X)]|`1
      by A3,A4,A5,A12,A26,GOBOARD5:3;
      ex x be set st x in LSeg(G*(i2,j),G*(i2,k)) &
      x in L~Lower_Seq(C,n+1) by A10,A20,XBOOLE_0:3;
      then reconsider X1=X as non empty compact Subset of TOP-REAL 2
      by XBOOLE_0:def 4;
      consider pp be set such that
A29:  pp in S-most X1 by XBOOLE_0:def 1;
      reconsider pp as Point of TOP-REAL 2 by A29;
A30:  pp in X by A29,XBOOLE_0:def 4;
      then
A31:  pp in L~Lower_Seq(C,n+1) by XBOOLE_0:def 4;
      pp in LSeg(G*(i2,j),G*(i2,k)) by A30,XBOOLE_0:def 4;
      then
A32:  pp`1 = |[G*(i2,1)`1,lower_bound(proj2.:X)]|`1 by A16,A28,SPPOL_1:64;
      |[G*(i2,1)`1,lower_bound(proj2.:X)]|`2 = S-bound X by A23,A27,SPRECT_1:49
        .= (S-min X)`2 by EUCLID:56
        .= pp`2 by A29,PSCOMP_1:118;
      then G*(i2,m) in Lower_Arc L~Cage(C,n+1) by A10,A27,A31,A32,TOPREAL3:11;
      then
      LSeg(G*(i2,j),G*(i2,m)) meets Lower_Arc C by A3,A4,A5,A9,A21,A25,Th20;
      then LSeg(G*(i2,j),G*(i2,k)) meets Lower_Arc C by A3,A4,A5,A7,A21,A22,
JORDAN15:7,XBOOLE_1:63;
      hence thesis by XBOOLE_1:70;
    end;
    suppose
A33:  LSeg(G*(i2,k),G*(i1,k)) meets Upper_Arc L~Cage(C,n+1) & i2 <= i1;
      then consider m be Element of NAT such that
A34:  i2 <= m and
A35:  m <= i1 and
A36:  G*(m,k)`1 = upper_bound(proj1.:(LSeg(G*(i2,k),G*(i1,k))
      /\ L~Upper_Seq(C,n+1))) by A11,A18,A19,JORDAN1F:4,JORDAN1G:4;
A37:  1 < m by A3,A34,XXREAL_0:2;
A38:  m < len G by A2,A35,XXREAL_0:2;
      set X = LSeg(G*(i2,k),G*(i1,k)) /\ L~Upper_Seq(C,n+1);
A39:  G*(m,k)`2 = G*(1,k)`2 by A7,A14,A37,A38,GOBOARD5:2;
      then
A40:  |[upper_bound(proj1.:X),G*(1,k)`2]| = G*(m,k) by A36,EUCLID:57;
      then
A41:  G*(i2,k)`2 = |[upper_bound(proj1.:X),G*(1,k)`2]|`2
      by A3,A4,A7,A14,A39,GOBOARD5:2;
      ex x be set st x in LSeg(G*(i2,k),G*(i1,k)) &
      x in L~Upper_Seq(C,n+1) by A11,A33,XBOOLE_0:3;
      then reconsider X1=X as non empty compact Subset of TOP-REAL 2
      by XBOOLE_0:def 4;
      consider pp be set such that
A42:  pp in E-most X1 by XBOOLE_0:def 1;
      reconsider pp as Point of TOP-REAL 2 by A42;
A43:  pp in X by A42,XBOOLE_0:def 4;
      then
A44:  pp in L~Upper_Seq(C,n+1) by XBOOLE_0:def 4;
      pp in LSeg(G*(i2,k),G*(i1,k)) by A43,XBOOLE_0:def 4;
      then
A45:  pp`2 = |[upper_bound(proj1.:X),G*(1,k)`2]|`2 by A17,A41,SPPOL_1:63;
      |[upper_bound(proj1.:X),G*(1,k)`2]|`1 = E-bound X by A36,A40,SPRECT_1:51
        .= (E-min X)`1 by EUCLID:56
        .= pp`1 by A42,PSCOMP_1:108;
      then G*(m,k) in Upper_Arc L~Cage(C,n+1) by A11,A40,A44,A45,TOPREAL3:11;
      then LSeg(G*(m,k),G*(i1,k)) meets Lower_Arc C
      by A2,A7,A8,A14,A35,A37,JORDAN15:42;
      then LSeg(G*(i2,k),G*(i1,k)) meets Lower_Arc C by A2,A3,A7,A14,A34,A35,
JORDAN15:8,XBOOLE_1:63;
      hence thesis by XBOOLE_1:70;
    end;
    suppose
A46:  LSeg(G*(i2,k),G*(i1,k)) meets Upper_Arc L~Cage(C,n+1) & i1 < i2;
      then consider m be Element of NAT such that
A47:  i1 <= m and
A48:  m <= i2 and
A49:  G*(m,k)`1 = lower_bound(proj1.:(LSeg(G*(i1,k),G*(i2,k))
      /\ L~Upper_Seq(C,n+1))) by A11,A18,A19,JORDAN1F:3,JORDAN1G:4;
A50:  1 < m by A1,A47,XXREAL_0:2;
A51:  m < len G by A4,A48,XXREAL_0:2;
      set X = LSeg(G*(i1,k),G*(i2,k)) /\ L~Upper_Seq(C,n+1);
A52:  G*(m,k)`2 = G*(1,k)`2 by A7,A14,A50,A51,GOBOARD5:2;
      then
A53:  |[lower_bound(proj1.:X),G*(1,k)`2]| = G*(m,k) by A49,EUCLID:57;
      then
A54:  G*(i1,k)`2 = |[lower_bound(proj1.:X),G*(1,k)`2]|`2
      by A1,A2,A7,A14,A52,GOBOARD5:2;
      ex x be set st x in LSeg(G*(i1,k),G*(i2,k)) &
      x in L~Upper_Seq(C,n+1) by A11,A46,XBOOLE_0:3;
      then reconsider X1=X as non empty compact Subset of TOP-REAL 2
      by XBOOLE_0:def 4;
      consider pp be set such that
A55:  pp in W-most X1 by XBOOLE_0:def 1;
      reconsider pp as Point of TOP-REAL 2 by A55;
A56:  pp in X by A55,XBOOLE_0:def 4;
      then
A57:  pp in L~Upper_Seq(C,n+1) by XBOOLE_0:def 4;
      pp in LSeg(G*(i1,k),G*(i2,k)) by A56,XBOOLE_0:def 4;
      then
A58:  pp`2 = |[lower_bound(proj1.:X),G*(1,k)`2]|`2 by A17,A54,SPPOL_1:63;
      |[lower_bound(proj1.:X),G*(1,k)`2]|`1 = W-bound X by A49,A53,SPRECT_1:48
        .= (W-min X)`1 by EUCLID:56
        .= pp`1 by A55,PSCOMP_1:88;
      then G*(m,k) in Upper_Arc L~Cage(C,n+1) by A11,A53,A57,A58,TOPREAL3:11;
      then LSeg(G*(i1,k),G*(m,k)) meets Lower_Arc C
      by A1,A7,A8,A14,A47,A51,JORDAN15:34;
      then LSeg(G*(i1,k),G*(i2,k)) meets Lower_Arc C by A1,A4,A7,A14,A47,A48,
JORDAN15:8,XBOOLE_1:63;
      hence thesis by XBOOLE_1:70;
    end;
    suppose
A59:  LSeg(G*(i2,j),G*(i2,k)) misses Lower_Arc L~Cage(C,n+1) &
      LSeg(Gauge(C,n+1)*(i2,k),Gauge(C,n+1)*(i1,k))
      misses Upper_Arc L~Cage(C,n+1);
      consider j1 be Element of NAT such that
A60:  j <= j1 and
A61:  j1 <= k and
A62:  LSeg(G*(i2,j1),G*(i2,k)) /\ L~Upper_Seq(C,n+1) =
      {G*(i2,j1)} by A3,A4,A5,A6,A7,A9,A11,JORDAN15:17;
      G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) /\
      L~Upper_Seq(C,n+1) by A62,TARSKI:def 1;
      then
A63:  G*(i2,j1) in L~Upper_Seq(C,n+1) by XBOOLE_0:def 4;
A64:  1 <= j1 by A5,A60,XXREAL_0:2;
      now per cases;
        suppose i2 <= i1;
          then consider i3 be Element of NAT such that
A65:      i2 <= i3 and
A66:      i3 <= i1 and
A67:      LSeg(G*(i2,k),G*(i3,k)) /\ L~Lower_Seq(C,n+1) = {G*(i3,k)}
          by A2,A3,A7,A8,A10,A14,JORDAN15:21;
A68:      i3 < len G by A2,A66,XXREAL_0:2;
          G*(i3,k) in LSeg(G*(i2,k),G*(i3,k)) /\ L~Lower_Seq(C,n+1)
          by A67,TARSKI:def 1;
          then
A69:      G*(i3,k) in L~Lower_Seq(C,n+1) by XBOOLE_0:def 4;
A70:      LSeg(G*(i2,j1),G*(i2,k)) c=
          LSeg(G*(i2,j),G*(i2,k)) by A3,A4,A5,A7,A60,A61,JORDAN15:7;
A71:      LSeg(G*(i2,k),G*(i3,k)) c= LSeg(G*(i2,k),G*(i1,k))
          by A2,A3,A7,A14,A65,A66,JORDAN15:8;
          then
A72:      LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) c=
LSeg(G*(i2,j),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i1,k)) by A70,XBOOLE_1:13;
A73:      (LSeg(G*(i2,j1),G*(i2,k)) \/
          LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1) = {G*(i3,k)}
          proof
            thus (LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1) c= {G*(i3,k)}
            proof
              let x be set;
              assume
A74:          x in (LSeg(G*(i2,j1),G*(i2,k)) \/
              LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1);
              then x in LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) by
XBOOLE_0:def 4;
              then
A75:          x in LSeg(G*(i2,j1),G*(i2,k)) or x in LSeg(G*(i2,k),G*(i3,k)) by
XBOOLE_0:def 3;
              x in L~Lower_Seq(C,n+1) by A74,XBOOLE_0:def 4;
              hence thesis by A10,A59,A67,A70,A75,XBOOLE_0:3,def 4;
            end;
            let x be set;
            assume x in {G*(i3,k)};
            then
A76:        x = G*(i3,k) by TARSKI:def 1;
            G*(i3,k) in LSeg(G*(i2,k),G*(i3,k)) by RLTOPSP1:69;
            then G*(i3,k) in LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k)) by XBOOLE_0:def 3;
            hence thesis by A69,A76,XBOOLE_0:def 4;
          end;
          (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k))) /\
          L~Upper_Seq(C,n+1) = {G*(i2,j1)}
          proof
            thus
            (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k))) /\
            L~Upper_Seq(C,n+1) c= {G*(i2,j1)}
            proof
              let x be set;
              assume
A77:          x in (LSeg(G*(i2,j1),G*(i2,k)) \/
              LSeg(G*(i2,k),G*(i3,k))) /\ L~Upper_Seq(C,n+1);
              then x in LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) by
XBOOLE_0:def 4;
              then
A78:          x in LSeg(G*(i2,j1),G*(i2,k)) or x in LSeg(G*(i2,k),G*(i3,k)) by
XBOOLE_0:def 3;
              x in L~Upper_Seq(C,n+1) by A77,XBOOLE_0:def 4;
              hence thesis by A11,A59,A62,A71,A78,XBOOLE_0:3,def 4;
            end;
            let x be set;
            assume x in {G*(i2,j1)};
            then
A79:        x = G*(i2,j1) by TARSKI:def 1;
            G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) by RLTOPSP1:69;
            then G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k)) by XBOOLE_0:def 3;
            hence thesis by A63,A79,XBOOLE_0:def 4;
          end;
          hence thesis by A3,A7,A61,A64,A65,A68,A72,A73,Th22,XBOOLE_1:63;
        end;
        suppose i1 < i2;
          then consider i3 be Element of NAT such that
A80:      i1 <= i3 and
A81:      i3 <= i2 and
A82:      LSeg(G*(i3,k),G*(i2,k)) /\ L~Lower_Seq(C,n+1) = {G*(i3,k)}
          by A1,A4,A7,A8,A10,A14,JORDAN15:14;
A83:      1 < i3 by A1,A80,XXREAL_0:2;
          G*(i3,k) in LSeg(G*(i2,k),G*(i3,k)) /\ L~Lower_Seq(C,n+1)
          by A82,TARSKI:def 1;
          then
A84:      G*(i3,k) in L~Lower_Seq(C,n+1) by XBOOLE_0:def 4;
A85:      LSeg(G*(i2,j1),G*(i2,k)) c=
          LSeg(G*(i2,j),G*(i2,k)) by A3,A4,A5,A7,A60,A61,JORDAN15:7;
A86:      LSeg(G*(i2,k),G*(i3,k)) c= LSeg(G*(i2,k),G*(i1,k))
          by A1,A4,A7,A14,A80,A81,JORDAN15:8;
          then
A87:      LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) c=
LSeg(G*(i2,j),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i1,k)) by A85,XBOOLE_1:13;
A88:      (LSeg(G*(i2,j1),G*(i2,k)) \/
          LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1) = {G*(i3,k)}
          proof
            thus (LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1) c= {G*(i3,k)}
            proof
              let x be set;
              assume
A89:          x in (LSeg(G*(i2,j1),G*(i2,k)) \/
              LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1);
              then x in LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) by
XBOOLE_0:def 4;
              then
A90:          x in LSeg(G*(i2,j1),G*(i2,k)) or x in LSeg(G*(i2,k),G*(i3,k)) by
XBOOLE_0:def 3;
              x in L~Lower_Seq(C,n+1) by A89,XBOOLE_0:def 4;
              hence thesis by A10,A59,A82,A85,A90,XBOOLE_0:3,def 4;
            end;
            let x be set;
            assume x in {G*(i3,k)};
            then
A91:        x = G*(i3,k) by TARSKI:def 1;
            G*(i3,k) in LSeg(G*(i2,k),G*(i3,k)) by RLTOPSP1:69;
            then G*(i3,k) in LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k)) by XBOOLE_0:def 3;
            hence thesis by A84,A91,XBOOLE_0:def 4;
          end;
          (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k))) /\
          L~Upper_Seq(C,n+1) = {G*(i2,j1)}
          proof
            thus
            (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k))) /\
            L~Upper_Seq(C,n+1) c= {G*(i2,j1)}
            proof
              let x be set;
              assume
              A92:          x
 in (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k )
              )) /\ L~Upper_Seq(C,n+1);
              then x in LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) by
XBOOLE_0:def 4;
              then
A93:          x in LSeg(G*(i2,j1),G*(i2,k)) or x in LSeg(G*(i2,k),G*(i3,k)) by
XBOOLE_0:def 3;
              x in L~Upper_Seq(C,n+1) by A92,XBOOLE_0:def 4;
              hence thesis by A11,A59,A62,A86,A93,XBOOLE_0:3,def 4;
            end;
            let x be set;
            assume x in {G*(i2,j1)};
            then
A94:        x = G*(i2,j1) by TARSKI:def 1;
            G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) by RLTOPSP1:69;
            then G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k)) by XBOOLE_0:def 3;
            hence thesis by A63,A94,XBOOLE_0:def 4;
          end;
          hence thesis by A4,A7,A61,A64,A81,A83,A87,A88,Th24,XBOOLE_1:63;
        end;
      end;
      hence thesis;
    end;
  end;
  hence thesis;
end;

theorem Th26:
  for C be Simple_closed_curve for i1,i2,j,k be Element of NAT holds
  1 < i1 & i1 < len Gauge(C,n+1) & 1 < i2 & i2 < len Gauge(C,n+1) &
  1 <= j & j <= k & k <= width Gauge(C,n+1) &
  Gauge(C,n+1)*(i1,k) in Lower_Arc L~Cage(C,n+1) &
  Gauge(C,n+1)*(i2,j) in Upper_Arc L~Cage(C,n+1) implies
  LSeg(Gauge(C,n+1)*(i2,j),Gauge(C,n+1)*(i2,k)) \/
  LSeg(Gauge(C,n+1)*(i2,k),Gauge(C,n+1)*(i1,k)) meets Upper_Arc C
proof
  let C be Simple_closed_curve;
  let i1,i2,j,k be Element of NAT;
  set G=Gauge(C,n+1);
  assume that
A1: 1 < i1 and
A2: i1 < len G and
A3: 1 < i2 and
A4: i2 < len G and
A5: 1 <= j and
A6: j <= k and
A7: k <= width G and
A8: G*(i1,k) in Lower_Arc L~Cage(C,n+1) and
A9: G*(i2,j) in Upper_Arc L~Cage(C,n+1);
A10: Lower_Arc L~Cage(C,n+1) = L~Lower_Seq(C,n+1) by JORDAN1G:64;
A11: Upper_Arc L~Cage(C,n+1) = L~Upper_Seq(C,n+1) by JORDAN1G:63;
A12: j <= width G by A6,A7,XXREAL_0:2;
  then
A13: [i2,j] in Indices G by A3,A4,A5,MATRIX_1:37;
A14: 1 <= k by A5,A6,XXREAL_0:2;
  then
A15: [i2,k] in Indices G by A3,A4,A7,MATRIX_1:37;
  G*(i2,j)`1 = G*(i2,1)`1 by A3,A4,A5,A12,GOBOARD5:3
    .= G*(i2,k)`1 by A3,A4,A7,A14,GOBOARD5:3;
  then
A16: LSeg(G*(i2,j),G*(i2,k)) is vertical by SPPOL_1:37;
  G*(i2,k)`2 = G*(1,k)`2 by A3,A4,A7,A14,GOBOARD5:2
    .= G*(i1,k)`2 by A1,A2,A7,A14,GOBOARD5:2;
  then
A17: LSeg(G*(i2,k),G*(i1,k)) is horizontal by SPPOL_1:36;
A18: [i2,k] in Indices G by A3,A4,A7,A14,MATRIX_1:37;
A19: [i1,k] in Indices G by A1,A2,A7,A14,MATRIX_1:37;
  now per cases;
    suppose
A20:  LSeg(G*(i2,j),G*(i2,k)) meets Lower_Arc L~Cage(C,n+1);
      then consider m be Element of NAT such that
A21:  j <= m and
A22:  m <= k and
A23:  G*(i2,m)`2 = lower_bound(proj2.:(LSeg(G*(i2,j),G*(i2,k))
      /\ L~Lower_Seq(C,n+1))) by A6,A10,A13,A15,JORDAN1F:1,JORDAN1G:5;
A24:  1 <= m by A5,A21,XXREAL_0:2;
A25:  m <= width G by A7,A22,XXREAL_0:2;
      set X = LSeg(G*(i2,j),G*(i2,k)) /\ L~Lower_Seq(C,n+1);
A26:  G*(i2,m)`1 = G*(i2,1)`1 by A3,A4,A24,A25,GOBOARD5:3;
      then
A27:  |[G*(i2,1)`1,lower_bound(proj2.:X)]| = G*(i2,m) by A23,EUCLID:57;
      then
A28:  G*(i2,j)`1 = |[G*(i2,1)`1,lower_bound(proj2.:X)]|`1
      by A3,A4,A5,A12,A26,GOBOARD5:3;
      ex x be set st x in LSeg(G*(i2,j),G*(i2,k)) &
      x in L~Lower_Seq(C,n+1) by A10,A20,XBOOLE_0:3;
      then reconsider X1=X as non empty compact Subset of TOP-REAL 2
      by XBOOLE_0:def 4;
      consider pp be set such that
A29:  pp in S-most X1 by XBOOLE_0:def 1;
      reconsider pp as Point of TOP-REAL 2 by A29;
A30:  pp in X by A29,XBOOLE_0:def 4;
      then
A31:  pp in L~Lower_Seq(C,n+1) by XBOOLE_0:def 4;
      pp in LSeg(G*(i2,j),G*(i2,k)) by A30,XBOOLE_0:def 4;
      then
A32:  pp`1 = |[G*(i2,1)`1,lower_bound(proj2.:X)]|`1 by A16,A28,SPPOL_1:64;
      |[G*(i2,1)`1,lower_bound(proj2.:X)]|`2 = S-bound X by A23,A27,SPRECT_1:49
        .= (S-min X)`2 by EUCLID:56
        .= pp`2 by A29,PSCOMP_1:118;
      then G*(i2,m) in Lower_Arc L~Cage(C,n+1) by A10,A27,A31,A32,TOPREAL3:11;
      then
      LSeg(G*(i2,j),G*(i2,m)) meets Upper_Arc C by A3,A4,A5,A9,A21,A25,Th19;
      then LSeg(G*(i2,j),G*(i2,k)) meets Upper_Arc C by A3,A4,A5,A7,A21,A22,
JORDAN15:7,XBOOLE_1:63;
      hence thesis by XBOOLE_1:70;
    end;
    suppose
A33:  LSeg(G*(i2,k),G*(i1,k)) meets Upper_Arc L~Cage(C,n+1) & i2 <= i1;
      then consider m be Element of NAT such that
A34:  i2 <= m and
A35:  m <= i1 and
A36:  G*(m,k)`1 = upper_bound(proj1.:(LSeg(G*(i2,k),G*(i1,k))
      /\ L~Upper_Seq(C,n+1))) by A11,A18,A19,JORDAN1F:4,JORDAN1G:4;
A37:  1 < m by A3,A34,XXREAL_0:2;
A38:  m < len G by A2,A35,XXREAL_0:2;
      set X = LSeg(G*(i2,k),G*(i1,k)) /\ L~Upper_Seq(C,n+1);
A39:  G*(m,k)`2 = G*(1,k)`2 by A7,A14,A37,A38,GOBOARD5:2;
      then
A40:  |[upper_bound(proj1.:X),G*(1,k)`2]| = G*(m,k) by A36,EUCLID:57;
      then
A41:  G*(i2,k)`2 = |[upper_bound(proj1.:X),G*(1,k)`2]|`2
      by A3,A4,A7,A14,A39,GOBOARD5:2;
      ex x be set st x in LSeg(G*(i2,k),G*(i1,k)) &
      x in L~Upper_Seq(C,n+1) by A11,A33,XBOOLE_0:3;
      then reconsider X1=X as non empty compact Subset of TOP-REAL 2
      by XBOOLE_0:def 4;
      consider pp be set such that
A42:  pp in E-most X1 by XBOOLE_0:def 1;
      reconsider pp as Point of TOP-REAL 2 by A42;
A43:  pp in X by A42,XBOOLE_0:def 4;
      then
A44:  pp in L~Upper_Seq(C,n+1) by XBOOLE_0:def 4;
      pp in LSeg(G*(i2,k),G*(i1,k)) by A43,XBOOLE_0:def 4;
      then
A45:  pp`2 = |[upper_bound(proj1.:X),G*(1,k)`2]|`2 by A17,A41,SPPOL_1:63;
      |[upper_bound(proj1.:X),G*(1,k)`2]|`1 = E-bound X by A36,A40,SPRECT_1:51
        .= (E-min X)`1 by EUCLID:56
        .= pp`1 by A42,PSCOMP_1:108;
      then G*(m,k) in Upper_Arc L~Cage(C,n+1) by A11,A40,A44,A45,TOPREAL3:11;
      then LSeg(G*(m,k),G*(i1,k)) meets Upper_Arc C
      by A2,A7,A8,A14,A35,A37,JORDAN15:43;
      then LSeg(G*(i2,k),G*(i1,k)) meets Upper_Arc C by A2,A3,A7,A14,A34,A35,
JORDAN15:8,XBOOLE_1:63;
      hence thesis by XBOOLE_1:70;
    end;
    suppose
A46:  LSeg(G*(i2,k),G*(i1,k)) meets Upper_Arc L~Cage(C,n+1) & i1 < i2;
      then consider m be Element of NAT such that
A47:  i1 <= m and
A48:  m <= i2 and
A49:  G*(m,k)`1 = lower_bound(proj1.:(LSeg(G*(i1,k),G*(i2,k))
      /\ L~Upper_Seq(C,n+1))) by A11,A18,A19,JORDAN1F:3,JORDAN1G:4;
A50:  1 < m by A1,A47,XXREAL_0:2;
A51:  m < len G by A4,A48,XXREAL_0:2;
      set X = LSeg(G*(i1,k),G*(i2,k)) /\ L~Upper_Seq(C,n+1);
A52:  G*(m,k)`2 = G*(1,k)`2 by A7,A14,A50,A51,GOBOARD5:2;
      then
A53:  |[lower_bound(proj1.:X),G*(1,k)`2]| = G*(m,k) by A49,EUCLID:57;
      then
A54:  G*(i1,k)`2 = |[lower_bound(proj1.:X),G*(1,k)`2]|`2
      by A1,A2,A7,A14,A52,GOBOARD5:2;
      ex x be set st x in LSeg(G*(i1,k),G*(i2,k)) &
      x in L~Upper_Seq(C,n+1) by A11,A46,XBOOLE_0:3;
      then reconsider X1=X as non empty compact Subset of TOP-REAL 2
      by XBOOLE_0:def 4;
      consider pp be set such that
A55:  pp in W-most X1 by XBOOLE_0:def 1;
      reconsider pp as Point of TOP-REAL 2 by A55;
A56:  pp in X by A55,XBOOLE_0:def 4;
      then
A57:  pp in L~Upper_Seq(C,n+1) by XBOOLE_0:def 4;
      pp in LSeg(G*(i1,k),G*(i2,k)) by A56,XBOOLE_0:def 4;
      then
A58:  pp`2 = |[lower_bound(proj1.:X),G*(1,k)`2]|`2 by A17,A54,SPPOL_1:63;
      |[lower_bound(proj1.:X),G*(1,k)`2]|`1 = W-bound X by A49,A53,SPRECT_1:48
        .= (W-min X)`1 by EUCLID:56
        .= pp`1 by A55,PSCOMP_1:88;
      then G*(m,k) in Upper_Arc L~Cage(C,n+1) by A11,A53,A57,A58,TOPREAL3:11;
      then LSeg(G*(i1,k),G*(m,k)) meets Upper_Arc C
      by A1,A7,A8,A14,A47,A51,JORDAN15:35;
      then LSeg(G*(i1,k),G*(i2,k)) meets Upper_Arc C by A1,A4,A7,A14,A47,A48,
JORDAN15:8,XBOOLE_1:63;
      hence thesis by XBOOLE_1:70;
    end;
    suppose
A59:  LSeg(G*(i2,j),G*(i2,k)) misses Lower_Arc L~Cage(C,n+1) &
      LSeg(Gauge(C,n+1)*(i2,k),Gauge(C,n+1)*(i1,k))
      misses Upper_Arc L~Cage(C,n+1);
      consider j1 be Element of NAT such that
A60:  j <= j1 and
A61:  j1 <= k and
A62:  LSeg(G*(i2,j1),G*(i2,k)) /\ L~Upper_Seq(C,n+1) =
      {G*(i2,j1)} by A3,A4,A5,A6,A7,A9,A11,JORDAN15:17;
      G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) /\
      L~Upper_Seq(C,n+1) by A62,TARSKI:def 1;
      then
A63:  G*(i2,j1) in L~Upper_Seq(C,n+1) by XBOOLE_0:def 4;
A64:  1 <= j1 by A5,A60,XXREAL_0:2;
      now per cases;
        suppose i2 <= i1;
          then consider i3 be Element of NAT such that
A65:      i2 <= i3 and
A66:      i3 <= i1 and
A67:      LSeg(G*(i2,k),G*(i3,k)) /\ L~Lower_Seq(C,n+1) = {G*(i3,k)}
          by A2,A3,A7,A8,A10,A14,JORDAN15:21;
A68:      i3 < len G by A2,A66,XXREAL_0:2;
          G*(i3,k) in LSeg(G*(i2,k),G*(i3,k)) /\ L~Lower_Seq(C,n+1)
          by A67,TARSKI:def 1;
          then
A69:      G*(i3,k) in L~Lower_Seq(C,n+1) by XBOOLE_0:def 4;
A70:      LSeg(G*(i2,j1),G*(i2,k)) c=
          LSeg(G*(i2,j),G*(i2,k)) by A3,A4,A5,A7,A60,A61,JORDAN15:7;
A71:      LSeg(G*(i2,k),G*(i3,k)) c= LSeg(G*(i2,k),G*(i1,k))
          by A2,A3,A7,A14,A65,A66,JORDAN15:8;
          then
A72:      LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) c=
LSeg(G*(i2,j),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i1,k)) by A70,XBOOLE_1:13;
A73:      (LSeg(G*(i2,j1),G*(i2,k)) \/
          LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1) = {G*(i3,k)}
          proof
            thus (LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1) c= {G*(i3,k)}
            proof
              let x be set;
              assume
A74:          x in (LSeg(G*(i2,j1),G*(i2,k)) \/
              LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1);
              then x in LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) by
XBOOLE_0:def 4;
              then
A75:          x in LSeg(G*(i2,j1),G*(i2,k)) or x in LSeg(G*(i2,k),G*(i3,k)) by
XBOOLE_0:def 3;
              x in L~Lower_Seq(C,n+1) by A74,XBOOLE_0:def 4;
              hence thesis by A10,A59,A67,A70,A75,XBOOLE_0:3,def 4;
            end;
            let x be set;
            assume x in {G*(i3,k)};
            then
A76:        x = G*(i3,k) by TARSKI:def 1;
            G*(i3,k) in LSeg(G*(i2,k),G*(i3,k)) by RLTOPSP1:69;
            then G*(i3,k) in LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k)) by XBOOLE_0:def 3;
            hence thesis by A69,A76,XBOOLE_0:def 4;
          end;
          (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k))) /\
          L~Upper_Seq(C,n+1) = {G*(i2,j1)}
          proof
            thus
            (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k))) /\
            L~Upper_Seq(C,n+1) c= {G*(i2,j1)}
            proof
              let x be set;
              assume
A77:          x in (LSeg(G*(i2,j1),G*(i2,k)) \/
              LSeg(G*(i2,k),G*(i3,k))) /\ L~Upper_Seq(C,n+1);
              then x in LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) by
XBOOLE_0:def 4;
              then
A78:          x in LSeg(G*(i2,j1),G*(i2,k)) or x in LSeg(G*(i2,k),G*(i3,k)) by
XBOOLE_0:def 3;
              x in L~Upper_Seq(C,n+1) by A77,XBOOLE_0:def 4;
              hence thesis by A11,A59,A62,A71,A78,XBOOLE_0:3,def 4;
            end;
            let x be set;
            assume x in {G*(i2,j1)};
            then
A79:        x = G*(i2,j1) by TARSKI:def 1;
            G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) by RLTOPSP1:69;
            then G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k)) by XBOOLE_0:def 3;
            hence thesis by A63,A79,XBOOLE_0:def 4;
          end;
          hence thesis by A3,A7,A61,A64,A65,A68,A72,A73,Th21,XBOOLE_1:63;
        end;
        suppose i1 < i2;
          then consider i3 be Element of NAT such that
A80:      i1 <= i3 and
A81:      i3 <= i2 and
A82:      LSeg(G*(i3,k),G*(i2,k)) /\ L~Lower_Seq(C,n+1) = {G*(i3,k)}
          by A1,A4,A7,A8,A10,A14,JORDAN15:14;
A83:      1 < i3 by A1,A80,XXREAL_0:2;
          G*(i3,k) in LSeg(G*(i2,k),G*(i3,k)) /\ L~Lower_Seq(C,n+1)
          by A82,TARSKI:def 1;
          then
A84:      G*(i3,k) in L~Lower_Seq(C,n+1) by XBOOLE_0:def 4;
A85:      LSeg(G*(i2,j1),G*(i2,k)) c=
          LSeg(G*(i2,j),G*(i2,k)) by A3,A4,A5,A7,A60,A61,JORDAN15:7;
A86:      LSeg(G*(i2,k),G*(i3,k)) c= LSeg(G*(i2,k),G*(i1,k))
          by A1,A4,A7,A14,A80,A81,JORDAN15:8;
          then
A87:      LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) c=
LSeg(G*(i2,j),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i1,k)) by A85,XBOOLE_1:13;
A88:      (LSeg(G*(i2,j1),G*(i2,k)) \/
          LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1) = {G*(i3,k)}
          proof
            thus (LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1) c= {G*(i3,k)}
            proof
              let x be set;
              assume
A89:          x in (LSeg(G*(i2,j1),G*(i2,k)) \/
              LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1);
              then x in LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) by
XBOOLE_0:def 4;
              then
A90:          x in LSeg(G*(i2,j1),G*(i2,k)) or x in LSeg(G*(i2,k),G*(i3,k)) by
XBOOLE_0:def 3;
              x in L~Lower_Seq(C,n+1) by A89,XBOOLE_0:def 4;
              hence thesis by A10,A59,A82,A85,A90,XBOOLE_0:3,def 4;
            end;
            let x be set;
            assume x in {G*(i3,k)};
            then
A91:        x = G*(i3,k) by TARSKI:def 1;
            G*(i3,k) in LSeg(G*(i2,k),G*(i3,k)) by RLTOPSP1:69;
            then G*(i3,k) in LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k)) by XBOOLE_0:def 3;
            hence thesis by A84,A91,XBOOLE_0:def 4;
          end;
          (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k))) /\
          L~Upper_Seq(C,n+1) = {G*(i2,j1)}
          proof
            thus
            (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k))) /\
            L~Upper_Seq(C,n+1) c= {G*(i2,j1)}
            proof
              let x be set;
              assume
              A92:          x
 in (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k )
              )) /\ L~Upper_Seq(C,n+1);
              then x in LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) by
XBOOLE_0:def 4;
              then
A93:          x in LSeg(G*(i2,j1),G*(i2,k)) or x in LSeg(G*(i2,k),G*(i3,k)) by
XBOOLE_0:def 3;
              x in L~Upper_Seq(C,n+1) by A92,XBOOLE_0:def 4;
              hence thesis by A11,A59,A62,A86,A93,XBOOLE_0:3,def 4;
            end;
            let x be set;
            assume x in {G*(i2,j1)};
            then
A94:        x = G*(i2,j1) by TARSKI:def 1;
            G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) by RLTOPSP1:69;
            then G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k)) by XBOOLE_0:def 3;
            hence thesis by A63,A94,XBOOLE_0:def 4;
          end;
          hence thesis by A4,A7,A61,A64,A81,A83,A87,A88,Th23,XBOOLE_1:63;
        end;
      end;
      hence thesis;
    end;
  end;
  hence thesis;
end;

theorem Th27:
  for C be Simple_closed_curve
  for p be Point of TOP-REAL 2 st W-bound C < p`1 & p`1 < E-bound C holds
  not(p in North_Arc C & p in South_Arc C)
proof
  let C be Simple_closed_curve;
  let p be Point of TOP-REAL 2;
  reconsider p9 = p as Point of Euclid 2 by EUCLID:25;
  assume that
A1: W-bound C < p`1 and
A2: p`1 < E-bound C and
A3: p in North_Arc C and
A4: p in South_Arc C;
  set s = min(p`1 - W-bound C,E-bound C - p`1);
A5: W-bound C = W-bound C + 0;
A6: p`1 = p`1 + 0;
A7: p`1 - W-bound C > 0 by A1,A5,XREAL_1:22;
  E-bound C - p`1 > 0 by A2,A6,XREAL_1:22;
  then
A8: s > 0 by A7,XXREAL_0:15;
  now
    let r be real number;
    reconsider rr = r as Real by XREAL_0:def 1;
    assume that
A9: 0 < r and
A10: r < s;
A11: r/8 > 0 by A9,XREAL_1:141;
    reconsider G = Ball(p9,r/8) as a_neighborhood of p by A9,GOBOARD6:5
,XREAL_1:141;
    consider k1 be Element of NAT such that
A12: for m be Element of NAT st m > k1
    holds (Upper_Appr C).m meets G by A3,KURATO_2:def 11;
    consider k2 be Element of NAT such that
A13: for m be Element of NAT st m > k2
    holds (Lower_Appr C).m meets G by A4,KURATO_2:def 11;
    set k = max(k1,k2);
A14: k >= k1 by XXREAL_0:25;
    set z9 = max(N-bound C - S-bound C,E-bound C - W-bound C);
    set z = max(z9,r/8);
    z/(r/8) >= 1 by A11,XREAL_1:183,XXREAL_0:25;
    then log(2,z/(r/8)) >= log(2,1) by PRE_FF:12;
    then log(2,z/(r/8)) >= 0 by POWER:59;
    then reconsider m9 = [\ log(2,z/(r/8)) /] as Element of NAT by INT_1:80;
A15: 2 to_power (m9+1) > 0 by POWER:39;
    set N = 2 to_power (m9+1);
    log(2,z/(r/8)) < (m9+1) * 1 by INT_1:52;
    then log(2,z/(r/8)) < (m9+1) * log(2,2) by POWER:60;
    then log(2,z/(r/8)) < log(2,2 to_power (m9+1)) by POWER:63;
    then z/(r/8) < N by A15,PRE_FF:12;
    then z/(r/8)*(r/8) < N*(r/8) by A11,XREAL_1:70;
    then z < N*(r/8) by A11,XCMPLX_1:88;
    then z/N < N*(r/8)/N by A15,XREAL_1:76;
    then z/N < (r/8)/N*N;
    then
A16: z/N < r/8 by A15,XCMPLX_1:88;
    z/N >= z9/N by A15,XREAL_1:74,XXREAL_0:25;
    then
A17: z9/N < r/8 by A16,XXREAL_0:2;
    set m = max(k,m9)+1;
A18: len Gauge(C,m) = width Gauge(C,m) by JORDAN8:def 1;
    max(k,m9) >= k by XXREAL_0:25;
    then max(k,m9) >= k1 by A14,XXREAL_0:2;
    then m > k1 by NAT_1:13;
    then (Upper_Appr C).m meets G by A12;
    then Upper_Arc L~Cage (C,m) meets G by Def1;
    then consider p1 be set such that
A19: p1 in Upper_Arc L~Cage (C,m) and
A20: p1 in G by XBOOLE_0:3;
    reconsider p1 as Point of TOP-REAL 2 by A19;
    reconsider p19 = p1 as Point of Euclid 2 by EUCLID:25;
    set f = Upper_Seq(C,m);
A21: Upper_Arc L~Cage(C,m) = L~Upper_Seq(C,m) by JORDAN1G:63;
    then consider i1 be Element of NAT such that
A22: 1 <= i1 and
A23: i1+1 <= len f and
A24: p1 in LSeg(f/.i1,f/.(i1+1)) by A19,SPPOL_2:14;
    reconsider c1 = f/.i1 as Point of Euclid 2 by EUCLID:25;
    reconsider c2 = f/.(i1+1) as Point of Euclid 2 by EUCLID:25;
A25: f is_sequence_on Gauge(C,m) by JORDAN1G:4;
    i1 < len f by A23,NAT_1:13;
    then i1 in Seg len f by A22,FINSEQ_1:3;
    then
A26: i1 in dom f by FINSEQ_1:def 3;
    then consider ii1,jj1 be Element of NAT such that
A27: [ii1,jj1] in Indices Gauge(C,m) and
A28: f/.i1 = Gauge(C,m)*(ii1,jj1) by A25,GOBOARD1:def 11;
A29: N-bound C > S-bound C+0 by TOPREAL5:22;
A30: E-bound C > W-bound C+0 by TOPREAL5:23;
A31: N-bound C - S-bound C > 0 by A29,XREAL_1:22;
A32: E-bound C - W-bound C > 0 by A30,XREAL_1:22;
A33: 2|^(m9+1) > 0 by A15,POWER:46;
    max(k,m9) >= m9 by XXREAL_0:25;
    then m > m9 by NAT_1:13;
    then m >= m9+1 by NAT_1:13;
    then
A34: 2|^m >= 2|^(m9+1) by PREPOWER:107;
    then
A35: (N-bound C - S-bound C)/2|^m<=(N-bound C - S-bound C)/2|^(m9+1) by A31,A33
,XREAL_1:120;
A36: (E-bound C - W-bound C)/2|^m <= (E-bound C - W-bound C)/2|^(m9+1) by A32
,A33,A34,XREAL_1:120;
A37: (N-bound C - S-bound C)/N <= z9/N by A15,XREAL_1:74,XXREAL_0:25;
A38: (E-bound C - W-bound C)/N <= z9/N by A15,XREAL_1:74,XXREAL_0:25;
A39: (N-bound C - S-bound C)/2|^(m9+1) <= z9/N by A37,POWER:46;
A40: (E-bound C - W-bound C)/2|^(m9+1) <= z9/N by A38,POWER:46;
A41: (N-bound C - S-bound C)/2|^m <= z9/N by A35,A39,XXREAL_0:2;
A42: (E-bound C - W-bound C)/2|^m <= z9/N by A36,A40,XXREAL_0:2;
    then dist(f/.i1,f/.(i1+1)) <= z9/N by A22,A23,A25,A41,Th6;
    then dist(f/.i1,f/.(i1+1)) < r/8 by A17,XXREAL_0:2;
    then dist(c1,c2) < r/8 by TOPREAL6:def 1;
    then
A43: |. f/.i1 - f/.(i1+1) .| < r/8 by SPPOL_1:62;
    |. p1 - f/.i1 .| <= |. f/.i1 - f/.(i1+1) .| by A24,JGRAPH_1:53;
    then
A44: |. p1 - f/.i1 .| < r/8 by A43,XXREAL_0:2;
    dist(p19,p9) < r/8 by A20,METRIC_1:12;
    then |. p-p1 .| < r/8 by SPPOL_1:62;
    then
A45: |. p-p1 .| + |. p1 - f/.i1 .| < r/(2*4) + r/(2*4) by A44,XREAL_1:10;
    |. p - f/.i1 .| <= |. p - p1 .| + |. p1 - f/.i1 .| by TOPRNS_1:35;
    then
A46: |. p - f/.i1 .| < r/4 by A45,XXREAL_0:2;
    then
A47: dist(p9,c1) < r/4 by SPPOL_1:62;
    then
A48: f/.i1 in Ball(p9,r/4) by METRIC_1:12;
A49: f/.i1 in Upper_Arc L~Cage(C,m) by A21,A26,SPPOL_2:48;
A50: k >= k2 by XXREAL_0:25;
    max(k,m9) >= k by XXREAL_0:25;
    then max(k,m9) >= k2 by A50,XXREAL_0:2;
    then m > k2 by NAT_1:13;
    then (Lower_Appr C).m meets G by A13;
    then Lower_Arc L~Cage (C,m) meets G by Def2;
    then consider p2 be set such that
A51: p2 in Lower_Arc L~Cage (C,m) and
A52: p2 in G by XBOOLE_0:3;
    reconsider p2 as Point of TOP-REAL 2 by A51;
    reconsider p29 = p2 as Point of Euclid 2 by EUCLID:25;
    set g = Lower_Seq(C,m);
A53: Lower_Arc L~Cage(C,m) = L~Lower_Seq(C,m) by JORDAN1G:64;
    then consider i2 be Element of NAT such that
A54: 1 <= i2 and
A55: i2+1 <= len g and
A56: p2 in LSeg(g/.i2,g/.(i2+1)) by A51,SPPOL_2:14;
    reconsider d1 = g/.i2 as Point of Euclid 2 by EUCLID:25;
    reconsider d2 = g/.(i2+1) as Point of Euclid 2 by EUCLID:25;
A57: g is_sequence_on Gauge(C,m) by JORDAN1G:5;
    i2 < len g by A55,NAT_1:13;
    then i2 in Seg len g by A54,FINSEQ_1:3;
    then
A58: i2 in dom g by FINSEQ_1:def 3;
    then consider ii2,jj2 be Element of NAT such that
A59: [ii2,jj2] in Indices Gauge(C,m) and
A60: g/.i2 = Gauge(C,m)*(ii2,jj2) by A57,GOBOARD1:def 11;
    dist(g/.i2,g/.(i2+1)) <= z9/N by A41,A42,A54,A55,A57,Th6;
    then dist(g/.i2,g/.(i2+1)) < r/8 by A17,XXREAL_0:2;
    then dist(d1,d2) < r/8 by TOPREAL6:def 1;
    then
A61: |. g/.i2 - g/.(i2+1) .| < r/8 by SPPOL_1:62;
    |. p2 - g/.i2 .| <= |. g/.i2 - g/.(i2+1) .| by A56,JGRAPH_1:53;
    then
A62: |. p2 - g/.i2 .| < r/8 by A61,XXREAL_0:2;
    dist(p29,p9) < r/8 by A52,METRIC_1:12;
    then |. p-p2 .| < r/8 by SPPOL_1:62;
    then
A63: |. p-p2 .| + |. p2 - g/.i2 .| < r/(2*4) + r/(2*4) by A62,XREAL_1:10;
    |. p - g/.i2 .| <= |. p - p2 .| + |. p2 - g/.i2 .| by TOPRNS_1:35;
    then
A64: |. p - g/.i2 .| < r/4 by A63,XXREAL_0:2;
    then
A65: dist(p9,d1) < r/4 by SPPOL_1:62;
    then
A66: g/.i2 in Ball(p9,r/4) by METRIC_1:12;
A67: g/.i2 in Lower_Arc L~Cage(C,m) by A53,A58,SPPOL_2:48;
    set Gij = Gauge(C,m)*(ii2,jj1);
    set Gji = Gauge(C,m)*(ii1,jj2);
    reconsider Gij9 = Gij, Gji9 = Gji as Point of Euclid 2 by EUCLID:25;
A68: 1 <= ii1 by A27,MATRIX_1:39;
A69: ii1 <= len Gauge(C,m) by A27,MATRIX_1:39;
A70: 1 <= jj1 by A27,MATRIX_1:39;
A71: jj1 <= width Gauge(C,m) by A27,MATRIX_1:39;
A72: 1 <= ii2 by A59,MATRIX_1:39;
A73: ii2 <= len Gauge(C,m) by A59,MATRIX_1:39;
A74: 1 <= jj2 by A59,MATRIX_1:39;
A75: jj2 <= width Gauge(C,m) by A59,MATRIX_1:39;
A76: len f >= 3 by JORDAN1E:19;
A77: len g >= 3 by JORDAN1E:19;
A78: len f >= 1 by A76,XXREAL_0:2;
A79: len g >= 1 by A77,XXREAL_0:2;
A80: len f in Seg len f by A78,FINSEQ_1:3;
A81: len g in Seg len g by A79,FINSEQ_1:3;
A82: len f in dom f by A80,FINSEQ_1:def 3;
A83: len g in dom g by A81,FINSEQ_1:def 3;
A84: r/4 < r by A9,XREAL_1:225;
A85: r/2 < r by A9,XREAL_1:218;
A86: s <= p`1 - W-bound C by XXREAL_0:17;
A87: s <= E-bound C - p`1 by XXREAL_0:17;
A88: now
      assume 1 >= ii1;
      then
A89:  ii1 = 1 by A68,XXREAL_0:1;
      dist(p9,c1) < r by A47,A84,XXREAL_0:2;
      then dist(p9,c1) < s by A10,XXREAL_0:2;
      then
A90:  dist(p9,c1) < p`1 - W-bound C by A86,XXREAL_0:2;
A91:  p`1-(f/.i1)`1 <= abs(p`1-(f/.i1)`1) by ABSVALUE:11;
      abs(p`1-(f/.i1)`1) <= |.p-f/.i1.| by JGRAPH_1:51;
      then p`1 - (f/.i1)`1 <= |.p-f/.i1.| by A91,XXREAL_0:2;
      then p`1 - W-bound L~Cage(C,m) <= |.p-f/.i1.|
      by A18,A28,A70,A71,A89,JORDAN1A:94;
      then p`1 - W-bound L~Cage(C,m) <= dist(p9,c1) by SPPOL_1:62;
      then p`1 - W-bound L~Cage(C,m) < p`1 - W-bound C by A90,XXREAL_0:2;
      then W-bound L~Cage(C,m) > W-bound C by XREAL_1:15;
      hence contradiction by Th12;
    end;
A92: now
      assume ii1 >= len Gauge(C,m);
      then
A93:  ii1 = len Gauge(C,m) by A69,XXREAL_0:1;
      (Gauge(C,m)*(len Gauge(C,m),jj1))`1 = E-bound L~Cage(C,m)
      by A18,A70,A71,JORDAN1A:92;
      then f/.i1 = E-max L~Cage(C,m) by A21,A26,A28,A93,JORDAN1J:46,SPPOL_2:48
        .= f/.len f by JORDAN1F:7;
      then i1 = len f by A26,A82,PARTFUN2:17;
      hence contradiction by A23,NAT_1:13;
    end;
A94: now
      assume ii2 <= 1;
      then
A95:  ii2 = 1 by A72,XXREAL_0:1;
      (Gauge(C,m)*(1,jj2))`1 = W-bound L~Cage(C,m) by A18,A74,A75,JORDAN1A:94;
      then g/.i2 = W-min L~Cage(C,m) by A53,A58,A60,A95,JORDAN1J:47,SPPOL_2:48
        .= g/.len g by JORDAN1F:8;
      then i2 = len g by A58,A83,PARTFUN2:17;
      hence contradiction by A55,NAT_1:13;
    end;
A96: now
      assume ii2 >= len Gauge(C,m);
      then
A97:  ii2 = len Gauge(C,m) by A73,XXREAL_0:1;
      dist(p9,d1) < r by A65,A84,XXREAL_0:2;
      then dist(p9,d1) < s by A10,XXREAL_0:2;
      then
A98:  dist(p9,d1) < E-bound C - p`1 by A87,XXREAL_0:2;
A99:  (g/.i2)`1-p`1 <= abs((g/.i2)`1-p`1) by ABSVALUE:11;
      abs((g/.i2)`1-p`1) <= |.g/.i2-p.| by JGRAPH_1:51;
      then abs((g/.i2)`1-p`1) <= |.p-g/.i2.| by TOPRNS_1:28;
      then (g/.i2)`1 - p`1 <= |.p-g/.i2.| by A99,XXREAL_0:2;
      then E-bound L~Cage(C,m) - p`1 <= |.p-g/.i2.|
      by A18,A60,A74,A75,A97,JORDAN1A:92;
      then E-bound L~Cage(C,m) - p`1 <= dist(p9,d1) by SPPOL_1:62;
      then E-bound L~Cage(C,m) - p`1 < E-bound C - p`1 by A98,XXREAL_0:2;
      then E-bound L~Cage(C,m) < E-bound C by XREAL_1:15;
      hence contradiction by Th10;
    end;
A100: Ball(p9,rr/4) c= Ball(p9,rr) by A84,PCOMPS_1:1;
A101: Gij`1 = Gauge(C,m)*(ii2,1)`1 by A70,A71,A72,A73,GOBOARD5:3
      .= (g/.i2)`1 by A60,A72,A73,A74,A75,GOBOARD5:3;
A102: Gij`2 = Gauge(C,m)*(1,jj1)`2 by A70,A71,A72,A73,GOBOARD5:2
      .= (f/.i1)`2 by A28,A68,A69,A70,A71,GOBOARD5:2;
A103: Gji`1 = Gauge(C,m)*(ii1,1)`1 by A68,A69,A74,A75,GOBOARD5:3
      .= (f/.i1)`1 by A28,A68,A69,A70,A71,GOBOARD5:3;
A104: Gji`2 = Gauge(C,m)*(1,jj2)`2 by A68,A69,A74,A75,GOBOARD5:2
      .= (g/.i2)`2 by A60,A72,A73,A74,A75,GOBOARD5:2;
A105: abs((g/.i2)`1-p`1) <= |.(g/.i2)-p.| by JGRAPH_1:51;
A106: abs((f/.i1)`2-p`2) <= |.(f/. i1)-p.| by JGRAPH_1:51;
A107: abs((g/.i2)`1-p`1) <= |.p-(g/.i2).| by A105,TOPRNS_1:28;
A108: abs((f/.i1)`2-p`2) <= |.p-(f/.i1).| by A106,TOPRNS_1:28;
A109: abs((g/.i2)`1-p`1) <= r/4 by A64,A107,XXREAL_0:2;
    abs((f/.i1)`2-p`2) <= r/4 by A46,A108,XXREAL_0:2;
    then abs((g/.i2)`1-p`1) + abs((f/.i1)`2-p`2) <= r/(2*2) + r/(2*2)
    by A109,XREAL_1:9;
    then
A110: abs((g/.i2)`1-p`1) + abs((f/.i1)`2-p`2) < r by A85,XXREAL_0:2;
A111: abs((f/.i1)`1-p`1) <= |.(f/.i1)-p.| by JGRAPH_1:51;
A112: abs((g/.i2)`2-p`2) <= |.(g/. i2)-p.| by JGRAPH_1:51;
A113: abs((f/.i1)`1-p`1) <= |.p-(f/.i1).| by A111,TOPRNS_1:28;
A114: abs((g/.i2)`2-p`2) <= |.p-(g/.i2).| by A112,TOPRNS_1:28;
A115: abs((f/.i1)`1-p`1) <= r/4 by A46,A113,XXREAL_0:2;
    abs((g/.i2)`2-p`2) <= r/4 by A64,A114,XXREAL_0:2;
    then abs((f/.i1)`1-p`1) + abs((g/.i2)`2-p`2) <= r/(2*2) + r/(2*2)
    by A115,XREAL_1:9;
    then
A116: abs((f/.i1)`1-p`1) + abs((g/.i2)`2-p`2) < r by A85,XXREAL_0:2;
    |.Gij-p.| <= abs((g/.i2)`1-p`1) + abs((f/.i1)`2-p`2)
    by A101,A102,JGRAPH_1:49;
    then |.Gij-p.| < r by A110,XXREAL_0:2;
    then dist(Gij9,p9) < r by SPPOL_1:62;
    then
A117: Gij in Ball(p9,r) by METRIC_1:12;
    |.Gji-p.| <= abs((f/.i1)`1-p`1) + abs((g/.i2)`2-p`2)
    by A103,A104,JGRAPH_1:49;
    then |.Gji-p.| < r by A116,XXREAL_0:2;
    then dist(Gji9,p9) < r by SPPOL_1:62;
    then
A118: Gji in Ball(p9,r) by METRIC_1:12;
A119: LSeg(g/.i2,Gij) c= Ball(p9,rr) by A66,A100,A117,TOPREAL3:28;
A120: LSeg(Gij,f/.i1) c= Ball(p9,rr) by A48,A100,A117,TOPREAL3:28;
A121: LSeg(g/.i2,Gji) c= Ball(p9,rr) by A66,A100,A118,TOPREAL3:28;
A122: LSeg(Gji,f/.i1) c= Ball(p9,rr) by A48,A100,A118,TOPREAL3:28;
    now per cases;
      suppose
A123:   jj2 <= jj1;
        LSeg(g/.i2,Gij) \/ LSeg(Gij,f/.i1) c= Ball(p9,r)
        proof
          let x be set;
          assume
A124:     x in LSeg(g/.i2,Gij) \/ LSeg(Gij,f/.i1);
          then reconsider x9 = x as Point of TOP-REAL 2;
          now per cases by A124,XBOOLE_0:def 3;
            suppose x9 in LSeg(g/.i2,Gij);
              hence x9 in Ball(p9,r) by A119;
            end;
            suppose x9 in LSeg(Gij,f/.i1);
              hence x9 in Ball(p9,r) by A120;
            end;
          end;
          hence thesis;
        end;
        hence Ball(p9,r) meets Upper_Arc C by A28,A49,A60,A67,A71,A74,A88,A92
,A94,A96,A123,JORDAN15:50,XBOOLE_1:63;
      end;
      suppose
A125:   jj1 < jj2;
        LSeg(f/.i1,Gji) \/ LSeg(Gji,g/.i2) c= Ball(p9,r)
        proof
          let x be set;
          assume
A126:     x in LSeg(f/.i1,Gji) \/ LSeg(Gji,g/.i2);
          then reconsider x9 = x as Point of TOP-REAL 2;
          now per cases by A126,XBOOLE_0:def 3;
            suppose x9 in LSeg(f/.i1,Gji);
              hence x9 in Ball(p9,r) by A122;
            end;
            suppose x9 in LSeg(Gji,g/.i2);
              hence x9 in Ball(p9,r) by A121;
            end;
          end;
          hence thesis;
        end;
        hence Ball(p9,r) meets Upper_Arc C by A28,A49,A60,A67,A70,A75,A88,A92
,A94,A96,A125,Th26,XBOOLE_1:63;
      end;
    end;
    hence Ball(p9,r) meets Upper_Arc C;
  end;
  then p in Cl Upper_Arc C by A8,GOBOARD6:96;
  then
A127: p in Upper_Arc C by PRE_TOPC:52;
  now
    let r be real number;
    reconsider rr = r as Real by XREAL_0:def 1;
    assume that
A128: 0 < r and
A129: r < s;
A130: r/8 > 0 by A128,XREAL_1:141;
    reconsider G = Ball(p9,r/8) as a_neighborhood of p by A128,GOBOARD6:5
,XREAL_1:141;
    consider k1 be Element of NAT such that
A131: for m be Element of NAT st m > k1
    holds (Upper_Appr C).m meets G by A3,KURATO_2:def 11;
    consider k2 be Element of NAT such that
A132: for m be Element of NAT st m > k2
    holds (Lower_Appr C).m meets G by A4,KURATO_2:def 11;
    set k = max(k1,k2);
A133: k >= k1 by XXREAL_0:25;
    set z9 = max(N-bound C - S-bound C,E-bound C - W-bound C);
    set z = max(z9,r/8);
    z/(r/8) >= 1 by A130,XREAL_1:183,XXREAL_0:25;
    then log(2,z/(r/8)) >= log(2,1) by PRE_FF:12;
    then log(2,z/(r/8)) >= 0 by POWER:59;
    then reconsider m9 = [\ log(2,z/(r/8)) /] as Element of NAT by INT_1:80;
A134: 2 to_power (m9+1) > 0 by POWER:39;
    set N = 2 to_power (m9+1);
    log(2,z/(r/8)) < (m9+1) * 1 by INT_1:52;
    then log(2,z/(r/8)) < (m9+1) * log(2,2) by POWER:60;
    then log(2,z/(r/8)) < log(2,2 to_power (m9+1)) by POWER:63;
    then z/(r/8) < N by A134,PRE_FF:12;
    then z/(r/8)*(r/8) < N*(r/8) by A130,XREAL_1:70;
    then z < N*(r/8) by A130,XCMPLX_1:88;
    then z/N < N*(r/8)/N by A134,XREAL_1:76;
    then z/N < (r/8)/N*N;
    then
A135: z/N < r/8 by A134,XCMPLX_1:88;
    z/N >= z9/N by A134,XREAL_1:74,XXREAL_0:25;
    then
A136: z9/N < r/8 by A135,XXREAL_0:2;
    set m = max(k,m9)+1;
A137: len Gauge(C,m) = width Gauge(C,m) by JORDAN8:def 1;
    max(k,m9) >= k by XXREAL_0:25;
    then max(k,m9) >= k1 by A133,XXREAL_0:2;
    then m > k1 by NAT_1:13;
    then (Upper_Appr C).m meets G by A131;
    then Upper_Arc L~Cage (C,m) meets G by Def1;
    then consider p1 be set such that
A138: p1 in Upper_Arc L~Cage (C,m) and
A139: p1 in G by XBOOLE_0:3;
    reconsider p1 as Point of TOP-REAL 2 by A138;
    reconsider p19 = p1 as Point of Euclid 2 by EUCLID:25;
    set f = Upper_Seq(C,m);
A140: Upper_Arc L~Cage(C,m) = L~Upper_Seq(C,m) by JORDAN1G:63;
    then consider i1 be Element of NAT such that
A141: 1 <= i1 and
A142: i1+1 <= len f and
A143: p1 in LSeg(f/.i1,f/.(i1+1)) by A138,SPPOL_2:14;
    reconsider c1 = f/.i1 as Point of Euclid 2 by EUCLID:25;
    reconsider c2 = f/.(i1+1) as Point of Euclid 2 by EUCLID:25;
A144: f is_sequence_on Gauge(C,m) by JORDAN1G:4;
    i1 < len f by A142,NAT_1:13;
    then i1 in Seg len f by A141,FINSEQ_1:3;
    then
A145: i1 in dom f by FINSEQ_1:def 3;
    then consider ii1,jj1 be Element of NAT such that
A146: [ii1,jj1] in Indices Gauge(C,m) and
A147: f/.i1 = Gauge(C,m)*(ii1,jj1) by A144,GOBOARD1:def 11;
A148: N-bound C > S-bound C+0 by TOPREAL5:22;
A149: E-bound C > W-bound C+0 by TOPREAL5:23;
A150: N-bound C - S-bound C > 0 by A148,XREAL_1:22;
A151: E-bound C - W-bound C > 0 by A149,XREAL_1:22;
A152: 2|^(m9+1) > 0 by A134,POWER:46;
    max(k,m9) >= m9 by XXREAL_0:25;
    then m > m9 by NAT_1:13;
    then m >= m9+1 by NAT_1:13;
    then
A153: 2|^m >= 2|^(m9+1) by PREPOWER:107;
    then
A154: (N-bound C - S-bound C)/2|^m<=(N-bound C - S-bound C)/2|^(m9+1) by A150
,A152,XREAL_1:120;
A155: (E-bound C - W-bound C)/2|^m <= (E-bound C - W-bound C)/2|^(m9+1) by A151
,A152,A153,XREAL_1:120;
A156: (N-bound C - S-bound C)/N <= z9/N by A134,XREAL_1:74,XXREAL_0:25;
A157: (E-bound C - W-bound C)/N <= z9/N by A134,XREAL_1:74,XXREAL_0:25;
A158: (N-bound C - S-bound C)/2|^(m9+1) <= z9/N by A156,POWER:46;
A159: (E-bound C - W-bound C)/2|^(m9+1) <= z9/N by A157,POWER:46;
A160: (N-bound C - S-bound C)/2|^m <= z9/N by A154,A158,XXREAL_0:2;
A161: (E-bound C - W-bound C)/2|^m <= z9/N by A155,A159,XXREAL_0:2;
    then dist(f/.i1,f/.(i1+1)) <= z9/N by A141,A142,A144,A160,Th6;
    then dist(f/.i1,f/.(i1+1)) < r/8 by A136,XXREAL_0:2;
    then dist(c1,c2) < r/8 by TOPREAL6:def 1;
    then
A162: |. f/.i1 - f/.(i1+1) .| < r/8 by SPPOL_1:62;
    |. p1 - f/.i1 .| <= |. f/.i1 - f/.(i1+1) .| by A143,JGRAPH_1:53;
    then
A163: |. p1 - f/.i1 .| < r/8 by A162,XXREAL_0:2;
    dist(p19,p9) < r/8 by A139,METRIC_1:12;
    then |. p-p1 .| < r/8 by SPPOL_1:62;
    then
A164: |. p-p1 .| + |. p1 - f/.i1 .| < r/(2*4) + r/(2*4) by A163,XREAL_1:10;
    |. p - f/.i1 .| <= |. p - p1 .| + |. p1 - f/.i1 .| by TOPRNS_1:35;
    then
A165: |. p - f/.i1 .| < r/4 by A164,XXREAL_0:2;
    then
A166: dist(p9,c1) < r/4 by SPPOL_1:62;
    then
A167: f/.i1 in Ball(p9,r/4) by METRIC_1:12;
A168: f/.i1 in Upper_Arc L~Cage(C,m) by A140,A145,SPPOL_2:48;
A169: k >= k2 by XXREAL_0:25;
    max(k,m9) >= k by XXREAL_0:25;
    then max(k,m9) >= k2 by A169,XXREAL_0:2;
    then m > k2 by NAT_1:13;
    then (Lower_Appr C).m meets G by A132;
    then Lower_Arc L~Cage (C,m) meets G by Def2;
    then consider p2 be set such that
A170: p2 in Lower_Arc L~Cage (C,m) and
A171: p2 in G by XBOOLE_0:3;
    reconsider p2 as Point of TOP-REAL 2 by A170;
    reconsider p29 = p2 as Point of Euclid 2 by EUCLID:25;
    set g = Lower_Seq(C,m);
A172: Lower_Arc L~Cage(C,m) = L~Lower_Seq(C,m) by JORDAN1G:64;
    then consider i2 be Element of NAT such that
A173: 1 <= i2 and
A174: i2+1 <= len g and
A175: p2 in LSeg(g/.i2,g/.(i2+1)) by A170,SPPOL_2:14;
    reconsider d1 = g/.i2 as Point of Euclid 2 by EUCLID:25;
    reconsider d2 = g/.(i2+1) as Point of Euclid 2 by EUCLID:25;
A176: g is_sequence_on Gauge(C,m) by JORDAN1G:5;
    i2 < len g by A174,NAT_1:13;
    then i2 in Seg len g by A173,FINSEQ_1:3;
    then
A177: i2 in dom g by FINSEQ_1:def 3;
    then consider ii2,jj2 be Element of NAT such that
A178: [ii2,jj2] in Indices Gauge(C,m) and
A179: g/.i2 = Gauge(C,m)*(ii2,jj2) by A176,GOBOARD1:def 11;
    dist(g/.i2,g/.(i2+1)) <= z9/N by A160,A161,A173,A174,A176,Th6;
    then dist(g/.i2,g/.(i2+1)) < r/8 by A136,XXREAL_0:2;
    then dist(d1,d2) < r/8 by TOPREAL6:def 1;
    then
A180: |. g/.i2 - g/.(i2+1) .| < r/8 by SPPOL_1:62;
    |. p2 - g/.i2 .| <= |. g/.i2 - g/.(i2+1) .| by A175,JGRAPH_1:53;
    then
A181: |. p2 - g/.i2 .| < r/8 by A180,XXREAL_0:2;
    dist(p29,p9) < r/8 by A171,METRIC_1:12;
    then |. p-p2 .| < r/8 by SPPOL_1:62;
    then
A182: |. p-p2 .| + |. p2 - g/.i2 .| < r/(2*4) + r/(2*4) by A181,XREAL_1:10;
    |. p - g/.i2 .| <= |. p - p2 .| + |. p2 - g/.i2 .| by TOPRNS_1:35;
    then
A183: |. p - g/.i2 .| < r/4 by A182,XXREAL_0:2;
    then
A184: dist(p9,d1) < r/4 by SPPOL_1:62;
    then
A185: g/.i2 in Ball(p9,r/4) by METRIC_1:12;
A186: g/.i2 in Lower_Arc L~Cage(C,m) by A172,A177,SPPOL_2:48;
    set Gij = Gauge(C,m)*(ii2,jj1);
    set Gji = Gauge(C,m)*(ii1,jj2);
    reconsider Gij9 = Gij, Gji9 = Gji as Point of Euclid 2 by EUCLID:25;
A187: 1 <= ii1 by A146,MATRIX_1:39;
A188: ii1 <= len Gauge(C,m) by A146,MATRIX_1:39;
A189: 1 <= jj1 by A146,MATRIX_1:39;
A190: jj1 <= width Gauge(C,m) by A146,MATRIX_1:39;
A191: 1 <= ii2 by A178,MATRIX_1:39;
A192: ii2 <= len Gauge(C,m) by A178,MATRIX_1:39;
A193: 1 <= jj2 by A178,MATRIX_1:39;
A194: jj2 <= width Gauge(C,m) by A178,MATRIX_1:39;
A195: len f >= 3 by JORDAN1E:19;
A196: len g >= 3 by JORDAN1E:19;
A197: len f >= 1 by A195,XXREAL_0:2;
A198: len g >= 1 by A196,XXREAL_0:2;
A199: len f in Seg len f by A197,FINSEQ_1:3;
A200: len g in Seg len g by A198,FINSEQ_1:3;
A201: len f in dom f by A199,FINSEQ_1:def 3;
A202: len g in dom g by A200,FINSEQ_1:def 3;
A203: r/4 < r by A128,XREAL_1:225;
A204: r/2 < r by A128,XREAL_1:218;
A205: s <= p`1 - W-bound C by XXREAL_0:17;
A206: s <= E-bound C - p`1 by XXREAL_0:17;
A207: now
      assume 1 >= ii1;
      then
A208: ii1 = 1 by A187,XXREAL_0:1;
      dist(p9,c1) < r by A166,A203,XXREAL_0:2;
      then dist(p9,c1) < s by A129,XXREAL_0:2;
      then
A209: dist(p9,c1) < p`1 - W-bound C by A205,XXREAL_0:2;
A210: p`1-(f/.i1)`1 <= abs(p`1-(f/.i1)`1) by ABSVALUE:11;
      abs(p`1-(f/.i1)`1) <= |.p-f/.i1.| by JGRAPH_1:51;
      then p`1 - (f/.i1)`1 <= |.p-f/.i1.| by A210,XXREAL_0:2;
      then p`1 - W-bound L~Cage(C,m) <= |.p-f/.i1.| by A137,A147,A189,A190,A208
,JORDAN1A:94;
      then p`1 - W-bound L~Cage(C,m) <= dist(p9,c1) by SPPOL_1:62;
      then p`1 - W-bound L~Cage(C,m) < p`1 - W-bound C by A209,XXREAL_0:2;
      then W-bound L~Cage(C,m) > W-bound C by XREAL_1:15;
      hence contradiction by Th12;
    end;
A211: now
      assume ii1 >= len Gauge(C,m);
      then
A212: ii1 = len Gauge(C,m) by A188,XXREAL_0:1;
      (Gauge(C,m)*(len Gauge(C,m),jj1))`1 = E-bound L~Cage(C,m)
      by A137,A189,A190,JORDAN1A:92;
      then f/.i1 = E-max L~Cage(C,m) by A140,A145,A147,A212,JORDAN1J:46
,SPPOL_2:48
        .= f/.len f by JORDAN1F:7;
      then i1 = len f by A145,A201,PARTFUN2:17;
      hence contradiction by A142,NAT_1:13;
    end;
A213: now
      assume ii2 <= 1;
      then
A214: ii2 = 1 by A191,XXREAL_0:1;
      (Gauge(C,m)*(1,jj2))`1 = W-bound L~Cage(C,m) by A137,A193,A194,
JORDAN1A:94;
      then g/.i2 = W-min L~Cage(C,m) by A172,A177,A179,A214,JORDAN1J:47
,SPPOL_2:48
        .= g/.len g by JORDAN1F:8;
      then i2 = len g by A177,A202,PARTFUN2:17;
      hence contradiction by A174,NAT_1:13;
    end;
A215: now
      assume ii2 >= len Gauge(C,m);
      then
A216: ii2 = len Gauge(C,m) by A192,XXREAL_0:1;
      dist(p9,d1) < r by A184,A203,XXREAL_0:2;
      then dist(p9,d1) < s by A129,XXREAL_0:2;
      then
A217: dist(p9,d1) < E-bound C - p`1 by A206,XXREAL_0:2;
A218: (g/.i2)`1-p`1 <= abs((g/.i2)`1-p`1) by ABSVALUE:11;
      abs((g/.i2)`1-p`1) <= |.g/.i2-p.| by JGRAPH_1:51;
      then abs((g/.i2)`1-p`1) <= |.p-g/.i2.| by TOPRNS_1:28;
      then (g/.i2)`1 - p`1 <= |.p-g/.i2.| by A218,XXREAL_0:2;
      then E-bound L~Cage(C,m) - p`1 <= |.p-g/.i2.| by A137,A179,A193,A194,A216
,JORDAN1A:92;
      then E-bound L~Cage(C,m) - p`1 <= dist(p9,d1) by SPPOL_1:62;
      then E-bound L~Cage(C,m) - p`1 < E-bound C - p`1 by A217,XXREAL_0:2;
      then E-bound L~Cage(C,m) < E-bound C by XREAL_1:15;
      hence contradiction by Th10;
    end;
A219: Ball(p9,rr/4) c= Ball(p9,rr) by A203,PCOMPS_1:1;
A220: Gij`1 = Gauge(C,m)*(ii2,1)`1 by A189,A190,A191,A192,GOBOARD5:3
      .= (g/.i2)`1 by A179,A191,A192,A193,A194,GOBOARD5:3;
A221: Gij`2 = Gauge(C,m)*(1,jj1)`2 by A189,A190,A191,A192,GOBOARD5:2
      .= (f/.i1)`2 by A147,A187,A188,A189,A190,GOBOARD5:2;
A222: Gji`1 = Gauge(C,m)*(ii1,1)`1 by A187,A188,A193,A194,GOBOARD5:3
      .= (f/.i1)`1 by A147,A187,A188,A189,A190,GOBOARD5:3;
A223: Gji`2 = Gauge(C,m)*(1,jj2)`2 by A187,A188,A193,A194,GOBOARD5:2
      .= (g/.i2)`2 by A179,A191,A192,A193,A194,GOBOARD5:2;
A224: abs((g/.i2)`1-p`1) <= |.(g/.i2)-p.| by JGRAPH_1:51;
A225: abs((f/.i1)`2-p`2) <= |.(f/. i1)-p.| by JGRAPH_1:51;
A226: abs((g/.i2)`1-p`1) <= |.p-(g/.i2).| by A224,TOPRNS_1:28;
A227: abs((f/.i1)`2-p`2) <= |.p-(f/.i1).| by A225,TOPRNS_1:28;
A228: abs((g/.i2)`1-p`1) <= r/4 by A183,A226,XXREAL_0:2;
    abs((f/.i1)`2-p`2) <= r/4 by A165,A227,XXREAL_0:2;
    then abs((g/.i2)`1-p`1) + abs((f/.i1)`2-p`2) <= r/(2*2) + r/(2*2)
    by A228,XREAL_1:9;
    then
A229: abs((g/.i2)`1-p`1) + abs((f/.i1)`2-p`2) < r by A204,XXREAL_0:2;
A230: abs((f/.i1)`1-p`1) <= |.(f/.i1)-p.| by JGRAPH_1:51;
A231: abs((g/.i2)`2-p`2) <= |.(g/. i2)-p.| by JGRAPH_1:51;
A232: abs((f/.i1)`1-p`1) <= |.p-(f/.i1).| by A230,TOPRNS_1:28;
A233: abs((g/.i2)`2-p`2) <= |.p-(g/.i2).| by A231,TOPRNS_1:28;
A234: abs((f/.i1)`1-p`1) <= r/4 by A165,A232,XXREAL_0:2;
    abs((g/.i2)`2-p`2) <= r/4 by A183,A233,XXREAL_0:2;
    then abs((f/.i1)`1-p`1) + abs((g/.i2)`2-p`2) <= r/(2*2) + r/(2*2)
    by A234,XREAL_1:9;
    then
A235: abs((f/.i1)`1-p`1) + abs((g/.i2)`2-p`2) < r by A204,XXREAL_0:2;
    |.Gij-p.| <= abs((g/.i2)`1-p`1) + abs((f/.i1)`2-p`2)
    by A220,A221,JGRAPH_1:49;
    then |.Gij-p.| < r by A229,XXREAL_0:2;
    then dist(Gij9,p9) < r by SPPOL_1:62;
    then
A236: Gij in Ball(p9,r) by METRIC_1:12;
    |.Gji-p.| <= abs((f/.i1)`1-p`1) + abs((g/.i2)`2-p`2)
    by A222,A223,JGRAPH_1:49;
    then |.Gji-p.| < r by A235,XXREAL_0:2;
    then dist(Gji9,p9) < r by SPPOL_1:62;
    then
A237: Gji in Ball(p9,r) by METRIC_1:12;
A238: LSeg(g/.i2,Gij) c= Ball(p9,rr) by A185,A219,A236,TOPREAL3:28;
A239: LSeg(Gij,f/.i1) c= Ball(p9,rr) by A167,A219,A236,TOPREAL3:28;
A240: LSeg(g/.i2,Gji) c= Ball(p9,rr) by A185,A219,A237,TOPREAL3:28;
A241: LSeg(Gji,f/.i1) c= Ball(p9,rr) by A167,A219,A237,TOPREAL3:28;
    now per cases;
      suppose
A242:   jj2 <= jj1;
        LSeg(g/.i2,Gij) \/ LSeg(Gij,f/.i1) c= Ball(p9,r)
        proof
          let x be set;
          assume
A243:     x in LSeg(g/.i2,Gij) \/ LSeg(Gij,f/.i1);
          then reconsider x9 = x as Point of TOP-REAL 2;
          now per cases by A243,XBOOLE_0:def 3;
            suppose x9 in LSeg(g/.i2,Gij);
              hence x9 in Ball(p9,r) by A238;
            end;
            suppose x9 in LSeg(Gij,f/.i1);
              hence x9 in Ball(p9,r) by A239;
            end;
          end;
          hence thesis;
        end;
hence Ball(p9,r) meets Lower_Arc C by A147,A168,A179,A186,A190,A193,A207,A211
,A213,A215,A242,JORDAN15:51,XBOOLE_1:63;
      end;
      suppose
A244:   jj1 < jj2;
        LSeg(f/.i1,Gji) \/ LSeg(Gji,g/.i2) c= Ball(p9,r)
        proof
          let x be set;
          assume
A245:     x in LSeg(f/.i1,Gji) \/ LSeg(Gji,g/.i2);
          then reconsider x9 = x as Point of TOP-REAL 2;
          now per cases by A245,XBOOLE_0:def 3;
            suppose x9 in LSeg(f/.i1,Gji);
              hence x9 in Ball(p9,r) by A241;
            end;
            suppose x9 in LSeg(Gji,g/.i2);
              hence x9 in Ball(p9,r) by A240;
            end;
          end;
          hence thesis;
        end;
hence Ball(p9,r) meets Lower_Arc C by A147,A168,A179,A186,A189,A194,A207,A211
,A213,A215,A244,Th25,XBOOLE_1:63;
      end;
    end;
    hence Ball(p9,r) meets Lower_Arc C;
  end;
  then p in Cl Lower_Arc C by A8,GOBOARD6:96;
  then p in Lower_Arc C by PRE_TOPC:52;
  then p in Upper_Arc C /\ Lower_Arc C by A127,XBOOLE_0:def 4;
  then p in {W-min C,E-max C} by JORDAN6:65;
  then p = W-min C or p = E-max C by TARSKI:def 2;
  hence contradiction by A1,A2,EUCLID:56;
end;

theorem :: "Nie oba"
  for C be Simple_closed_curve
  for p be Point of TOP-REAL 2 st p`1 = (W-bound C + E-bound C)/2 holds
  not (p in North_Arc C & p in South_Arc C)
proof
  let C be Simple_closed_curve;
  let p be Point of TOP-REAL 2;
A1: W-bound C < E-bound C by SPRECT_1:33;
  assume
A2: p`1 = (W-bound C + E-bound C)/2;
  then
A3: W-bound C < p`1 by A1,XREAL_1:228;
  p`1 < E-bound C by A1,A2,XREAL_1:228;
  hence thesis by A3,Th27;
end;
