:: On the Upper and Lower Approximations of the Curve
::  by Robert Milewski
::
:: Received September 27, 2003
:: Copyright (c) 2003 Association of Mizar Users

environ

 vocabularies JORDAN19, JORDAN8, PSCOMP_1, BOOLE, JORDAN1A, JORDAN9, GOBOARD1,
      GOBOARD5, JORDAN6, SPRECT_2, JORDAN3, FINSEQ_5, JORDAN2C, GROUP_2, NAT_1,
      SPPOL_2, REALSET1, JORDAN1E, SQUARE_1, FINSEQ_4, TOPREAL2, CONNSP_1,
      COMPTS_1, TOPREAL1, SPPOL_1, FINSEQ_1, TREES_1, EUCLID, RELAT_1, MCART_1,
      MATRIX_1, GOBOARD9, PRE_TOPC, RELAT_2, SEQM_3, SUBSET_1, FUNCT_1,
      ARYTM_1, FINSEQ_6, GOBOARD2, GROUP_1, GRAPH_2, METRIC_1, ARYTM, ORDINAL2,
      ARYTM_3, INT_1, FUNCT_5, ABSVALUE, CONNSP_2, COMPLEX1, POWER, KURATO_2,
      PROB_1;
 notations TARSKI, XBOOLE_0, ENUMSET1, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0,
      XXREAL_0, XREAL_0, COMPLEX1, REAL_1, INT_1, NAT_1, NAT_D, FUNCT_1,
      RELSET_1, PARTFUN1, LIMFUNC1, FINSEQ_1, FINSEQ_4, FINSEQ_5, MATRIX_1,
      FINSEQ_6, POWER, METRIC_1, CONNSP_2, GRAPH_2, REALSET1, STRUCT_0,
      PRE_TOPC, TOPREAL2, NEWTON, BINARITH, PROB_1, CONNSP_1, COMPTS_1, EUCLID,
      PSCOMP_1, SPRECT_2, SPPOL_2, KURATO_2, GOBOARD1, TOPREAL6, TOPREAL1,
      GOBOARD2, GOBOARD5, GOBOARD9, GOBRD13, SPPOL_1, JORDAN3, JORDAN6,
      JORDAN8, JORDAN9, JORDAN2C, JORDAN1A, JORDAN1E;
 constructors REAL_1, NAT_1, NAT_D, FINSEQ_4, LIMFUNC1, NEWTON, POWER,
      BINARITH, CONNSP_1, REALSET2, CONNSP_2, MATRIX_2, MONOID_0, GOBOARD2,
      JORDAN1, PSCOMP_1, GRAPH_2, GOBOARD9, JORDAN3, JORDAN5C, JORDAN6,
      JORDAN2C, TOPREAL6, JORDAN8, GOBRD13, JORDAN9, JORDAN1A, JORDAN1E,
      KURATO_2;
 registrations XBOOLE_0, SUBSET_1, ORDINAL1, RELSET_1, XXREAL_0, XREAL_0,
      REAL_1, NAT_1, FINSEQ_1, FINSEQ_5, FINSEQ_6, STRUCT_0, COMPTS_1,
      METRIC_1, MONOID_0, EUCLID, TOPREAL1, TOPREAL2, GOBOARD2, JORDAN1,
      SPPOL_2, PSCOMP_1, GOBRD11, TOPREAL5, SPRECT_1, SPRECT_2, JORDAN6,
      REVROT_1, TOPREAL6, JORDAN8, JORDAN1E, JORDAN1J;
 requirements NUMERALS, ARITHM, BOOLE, SUBSET, REAL;
 definitions TARSKI, XBOOLE_0, PSCOMP_1, EUCLID;
 theorems NAT_1, FINSEQ_1, GOBOARD1, FINSEQ_4, EUCLID, FINSEQ_3, REAL_1,
      REAL_2, SPPOL_2, TARSKI, JORDAN3, PSCOMP_1, FINSEQ_5, FINSEQ_6, GOBOARD7,
      TOPREAL1, BINARITH, JORDAN5B, GOBOARD5, SPRECT_2, SPPOL_1, ABSVALUE,
      GOBOARD9, FINSEQ_2, SUBSET_1, GOBRD11, JORDAN4, GOBOARD2, SPRECT_3,
      TOPREAL3, JORDAN8, PARTFUN2, SPRECT_1, XBOOLE_0, XBOOLE_1, ZFMISC_1,
      GOBRD14, JORDAN2C, TOPREAL5, PRE_TOPC, JORDAN6, JORDAN9, JORDAN1H,
      JORDAN1A, JORDAN1E, JORDAN10, JGRAPH_1, REVROT_1, COMPTS_1, ENUMSET1,
      JORDAN1B, JORDAN1F, JORDAN1G, JORDAN1I, JORDAN1J, GOBOARD3, TOPREAL8,
      GRAPH_2, SPRECT_5, JORDAN1D, METRIC_1, POWER, PRE_FF, JORDAN15, PCOMPS_1,
      TOPRNS_1, BOOLMARK, JORDAN5D, XCMPLX_1, XREAL_0, KURATO_2, INT_1,
      REALSET1, XREAL_1, NEWTON, XXREAL_0, GOBOARD6, PREPOWER, TOPREAL6,
      PARTFUN1;
 schemes FUNCT_2, BINOP_2;

begin

 reserve n for Element of NAT;

  definition
   let C be Simple_closed_curve;
   func Upper_Appr C -> SetSequence of the carrier of TOP-REAL 2 means :Def1:
    for i be Element of NAT holds it.i = Upper_Arc L~Cage (C,i);
   existence
   proof
    deffunc O(Element of NAT) = Upper_Arc L~Cage (C,$1);
    thus ex S be SetSequence of the carrier of TOP-REAL 2 st
     for i being Element of NAT holds S.i = O(i) from FUNCT_2:sch 4;
   end;
   uniqueness
   proof
    deffunc O(Element of NAT) = Upper_Arc L~Cage (C,$1);
    thus for S1,S2 be SetSequence of the carrier of TOP-REAL 2 st
     (for i being Element of NAT holds S1.i = O(i)) &
     (for i being Element of NAT holds S2.i = O(i)) holds S1 = S2
      from BINOP_2:sch 1;
   end;
   func Lower_Appr C -> SetSequence of the carrier of TOP-REAL 2 means :Def2:
    for i being Element of NAT holds it.i = Lower_Arc L~Cage (C,i);
   existence
   proof
    deffunc O(Element of NAT) = Lower_Arc L~Cage (C,$1);
    thus ex S be SetSequence of the carrier of TOP-REAL 2 st
     for i being Element of NAT holds S.i = O(i) from FUNCT_2:sch 4;
   end;
   uniqueness
   proof
    deffunc O(Element of NAT) = Lower_Arc L~Cage (C,$1);
    thus for S1,S2 be SetSequence of the carrier of TOP-REAL 2 st
     (for i being Element of NAT holds S1.i = O(i)) &
     (for i being Element of NAT holds S2.i = O(i)) holds S1 = S2
      from BINOP_2:sch 1;
   end;
  end;

  definition
   let C be Simple_closed_curve;
   func North_Arc C -> Subset of TOP-REAL 2 equals
     Lim_inf Upper_Appr C;
   coherence;
   func South_Arc C -> Subset of TOP-REAL 2 equals
     Lim_inf Lower_Appr C;
   coherence;
  end;

  Lm1:
   now
    let G be Go-board;
    let j be Element of NAT;
    assume A1: 1 <= j & j <= width G;
     0 + 1 <= len G div 2 + 1 by XREAL_1:8;
    then A2: 0 + 1 <= Center G by JORDAN1A:def 1;
         Center G <= len G by JORDAN1B:14;
    hence [Center G,j] in Indices G by A1,A2,GOBOARD7:10;
   end;

  Lm2:
   now
    let D be non empty Subset of TOP-REAL 2;
    let n,i be Element of NAT;
    set a = N-bound D, s = S-bound D, w = W-bound D, e = E-bound D;
    set G = Gauge(D,n);
    assume [i,width G] in Indices G;
    hence G*(i,width G)`2
       = |[w+(e-w)/(2|^n)*(i - 2), s+(a-s)/(2|^n)*(width G - 2)]|`2
         by JORDAN8:def 1
      .= s+(a-s)/(2|^n)*(width G - 2) by EUCLID:56;
   end;

  theorem Th1:
   for n,m be Element of NAT holds n <= m & n <> 0 implies (n+1)/n >= (m+1)/m
   proof
    let n,m be Element of NAT;
    assume that
     A1: n <= m and
     A2: n <> 0;
    A3: n > 0 by A2;
    then A4: 1/n >= 1/m by A1,REAL_2:152;
    A5: (n+1)/n = n/n + 1/n by XCMPLX_1:63
       .= 1 + 1/n by A2,XCMPLX_1:60;
         (m+1)/m = m/m + 1/m by XCMPLX_1:63
       .= 1 + 1/m by A1,A3,XCMPLX_1:60;
    hence (n+1)/n >= (m+1)/m by A4,A5,XREAL_1:9;
   end;

  theorem Th2:
   for E be compact non vertical non horizontal Subset of TOP-REAL 2
   for m,j be Element of NAT
    st 1 <= m & m <= n & 1 <= j & j <= width Gauge(E,n) holds
    LSeg(Gauge(E,n)*(Center Gauge(E,n),width Gauge(E,n)),
         Gauge(E,n)*(Center Gauge(E,n),j)) c=
    LSeg(Gauge(E,m)*(Center Gauge(E,m),width Gauge(E,m)),
         Gauge(E,n)*(Center Gauge(E,n),j))
   proof
    let E be compact non vertical non horizontal Subset of TOP-REAL 2;
    let m,j be Element of NAT;
    set a = N-bound E, s = S-bound E, w = W-bound E, e = E-bound E,
        G = Gauge(E,n), M = Gauge(E,m), sn = Center G, sm = Center M;
    assume that
     A1: 1 <= m and
     A2: m <= n and
     A3: 1 <= j and
     A4: j <= width G;
    A5: width M = len M by JORDAN8:def 1
               .= 2|^m+3 by JORDAN8:def 1;
    A6: width G = len G by JORDAN8:def 1
               .= 2|^n+3 by JORDAN8:def 1;
    A7: 2|^m <= 2|^n by A2,PREPOWER:107;
         now
     let t be Element of NAT;
     assume that
      A8: width G >= t and
      A9: t >= j;
     A10: len M = width M by JORDAN8:def 1;
     A11: len G = width G by JORDAN8:def 1;
          s < a by SPRECT_1:34;
     then A12: 0 < a - s by XREAL_1:52;
     A13: t >= 1 by A3,A9,XXREAL_0:2;
     A14: 0 < 2|^m by NEWTON:102;
     A15: 0 < 2|^n by NEWTON:102;
     A16: 1 <= len M by GOBRD11:34;
     then A17: M*(sm,width M)`1 = G*(sn,t)`1 & G*(sn,t)`1 = G*(sn,j)`1
                                     by A1,A2,A3,A4,A8,A10,A11,A13,JORDAN1A:57;
          [sn,t] in Indices G by A8,A13,Lm1;
     then A18: G*(sn,t)`2 = |[w+(e-w)/(2|^n)*(sn - 2),s+(a-s)/(2|^n)*(t-2)]|`2
                                                            by JORDAN8:def 1
        .= s+(a-s)/(2|^n)*(t-2) by EUCLID:56;
          [sm,width M] in Indices M by A10,A16,Lm1;
     then A19: M*(sm,width M)`2 = s+(a-s)/(2|^m)*(width M-2) by Lm2;
     A20: (2|^m+1)/(2|^m) >= (2|^n+1)/(2|^n) by A7,A14,Th1;
          t-2 <= 2|^n+3-2 by A6,A8,XREAL_1:11;
     then (t-2)/(2|^n) <= (2|^n+1)/(2|^n) by A15,REAL_1:73;
     then (t-2)/(2|^n) <= (width M-2)/(2|^m) by A5,A20,XXREAL_0:2;
     then (a-s)*((t-2)/(2|^n)) <= (a-s)*((width M-2)/(2|^m)) by A12,XREAL_1:66
;
     then (a-s)/(2|^n)*(t-2) <= (a-s)*((width M-2)/(2|^m)) by XCMPLX_1:76;
     then (a-s)/(2|^m)*(width M-2) >= (a-s)/(2|^n)*(t-2) by XCMPLX_1:76;
     then A21: s+(a-s)/(2|^m)*(width M-2) >= s+(a-s)/(2|^n)*(t-2) by XREAL_1:8;
          1 <= sn & sn <= len G by JORDAN1B:12,14;
     then G*(sn,t)`2 >= G*(sn,j)`2 by A3,A8,A9,SPRECT_3:24;
     hence G*(sn,t) in LSeg(M*(sm,width M),G*(sn,j)) by A17,A18,A19,A21,
GOBOARD7:8;
    end;
    then G*(sn,width G) in LSeg(M*(sm,width M),G*(sn,j)) &
         G*(sn,j) in LSeg(M*(sm,width M),G*(sn,j)) by A4;
    hence thesis by TOPREAL1:12;
   end;

  theorem Th3:
   for C be compact connected non vertical non horizontal Subset of TOP-REAL 2
   for i,j be Element of NAT st 1 <= i & i <= len Gauge(C,n) &
    1 <= j & j <= width Gauge(C,n) & Gauge(C,n)*(i,j) in L~Cage(C,n) holds
     LSeg(Gauge(C,n)*(i,width Gauge(C,n)),Gauge(C,n)*(i,j)) meets
      L~Upper_Seq(C,n)
   proof
    let C be compact connected non vertical non horizontal
     Subset of TOP-REAL 2;
    let i,j be Element of NAT;
    set Gij = Gauge(C,n)*(i,j);
    assume that
     A1: 1 <= i & i <= len Gauge(C,n) and
     A2: 1 <= j & j <= width Gauge(C,n) and
     A3: Gij in L~Cage(C,n);
    set NE = SW-corner L~Cage(C,n);
    set v1 = L_Cut(Lower_Seq(C,n),Gij);
    set wG = width Gauge(C,n);
    set lG = len Gauge(C,n);
    set Gv1 = <*Gauge(C,n)*(i,wG)*>^v1;
    set v = Gv1^<*NE*>;
    set h = mid(Upper_Seq(C,n),2,len Upper_Seq(C,n));
    A4: L~Cage(C,n) = L~Lower_Seq(C,n) \/ L~Upper_Seq(C,n) by JORDAN1E:17;
         len Upper_Seq(C,n) >= 3 & len Lower_Seq(C,n) >= 3 by JORDAN1E:19;
    then A5: len Upper_Seq(C,n) >= 2 & len Upper_Seq(C,n) >= 1 &
     len Lower_Seq(C,n) >= 2 & len Lower_Seq(C,n) >= 1 by XXREAL_0:2;
    A6: len Gauge(C,n) = width Gauge(C,n) by JORDAN8:def 1;
    then width Gauge(C,n) >= 4 by JORDAN8:13;
    then A7: 1 <= width Gauge(C,n) by XXREAL_0:2;
    A8: Gauge(C,n)*(i,wG)`2 = N-bound L~Cage(C,n) by A1,A6,JORDAN1A:91;
    set Ema = E-max L~Cage(C,n);
         now per cases by A1,A3,A4,REAL_1:def 5,XBOOLE_0:def 2;
     suppose A9: Gij in L~Lower_Seq(C,n) & i = lG;
      set G11 = Gauge(C,n)*(lG,wG);
      A10: G11`1 = E-bound L~Cage(C,n) by A1,A6,A9,JORDAN1A:92;
      A11: Ema`1 = E-bound L~Cage(C,n) by EUCLID:56;
      A12: N-bound L~Cage(C,n) = G11`2 by A1,A6,A9,JORDAN1A:91;
           Ema in L~Cage(C,n) by SPRECT_1:16;
      then A13: G11`2 >= Ema`2 by A12,PSCOMP_1:71;
      A14: Gij`1 = E-bound L~Cage(C,n) by A2,A6,A9,JORDAN1A:92;
      then Gij in E-most L~Cage(C,n) by A3,SPRECT_2:17;
      then Ema`2 >= Gij`2 by PSCOMP_1:108;
      then A15: Ema in LSeg(Gauge(C,n)*(lG,wG),Gauge(C,n)*(lG,j))
                                             by A9,A10,A11,A13,A14,GOBOARD7:8;
      A16: rng Upper_Seq(C,n) c= L~Upper_Seq(C,n) by A5,SPPOL_2:18;
           Upper_Seq(C,n)/.(len Upper_Seq(C,n)) = Ema by JORDAN1F:7;
      then Ema in rng Upper_Seq(C,n) by REVROT_1:3;
      hence LSeg(Gauge(C,n)*(i,wG),Gij) meets L~Upper_Seq(C,n)
                                                   by A9,A15,A16,XBOOLE_0:3;
     end;
     suppose A17: Gij in L~Lower_Seq(C,n) &
      Gij <> Lower_Seq(C,n).len Lower_Seq(C,n)
      & W-min L~Cage(C,n) <> NE & i < lG;
      then A18: v1 is non empty by JORDAN1E:7;
      then len v1 <> 0 by FINSEQ_1:25;
      then len v1 > 0;
      then A19: 0+1 <= len v1 by NAT_1:13;
      then A20: 1 in dom v1 by FINSEQ_3:27;
      A21: len v1 in dom v1 & len Lower_Seq(C,n) in dom Lower_Seq(C,n)
                                                 by A5,A19,FINSEQ_3:27;
      then A22: v1/.(len v1) = v1.(len v1) by PARTFUN1:def 8
         .= Lower_Seq(C,n).len Lower_Seq(C,n) by A17,JORDAN1B:5
         .= Lower_Seq(C,n)/.len Lower_Seq(C,n) by A21,PARTFUN1:def 8
         .= W-min L~Cage(C,n) by JORDAN1F:8;
      then A23: Gv1/.len Gv1 = W-min L~Cage(C,n) by A18,SPRECT_3:11;
      A24: v1/.1 = v1.1 by A20,PARTFUN1:def 8
         .= Gij by A17,JORDAN3:58;
      then A25: (v1^<*NE*>)/.1 = Gij by A19,BOOLMARK:8;
      A26: 1+len v1 >= 1+1 by A19,XREAL_1:9;
           len v = len Gv1 + 1 by FINSEQ_2:19
         .= 1 + len v1 + 1 by FINSEQ_5:8;
      then 2 < len v by A26,NAT_1:13;
      then A27: 2 < len Rev v by FINSEQ_5:def 3;
           S-bound L~Cage(C,n) < N-bound L~Cage(C,n) by SPRECT_1:34;
      then NE <> Gauge(C,n)*(i,wG) by A8,EUCLID:56;
      then not NE in {Gauge(C,n)*(i,wG)} by TARSKI:def 1;
      then A28: not NE in rng <*Gauge(C,n)*(i,wG)*> by FINSEQ_1:56;
           len Cage(C,n) > 4 by GOBOARD7:36;
      then len Cage(C,n) >= 2 by XXREAL_0:2;
      then A29: rng Cage(C,n) c= L~Cage(C,n) by SPPOL_2:18;
      A30: not NE in rng Cage(C,n)
      proof
       assume A31: NE in rng Cage(C,n);
       A32: NE`1 = W-bound L~Cage(C,n) & NE`2 = S-bound L~Cage(C,n)
                                                        by EUCLID:56;
       then NE`2 <= N-bound L~Cage(C,n) by SPRECT_1:24;
       then NE in { p where p is Point of TOP-REAL 2 :
        p`1 = W-bound L~Cage(C,n) & p`2 <= N-bound L~Cage(C,n) &
        p`2 >= S-bound L~Cage(C,n) } by A32;
       then NE in LSeg(SW-corner L~Cage(C,n), NW-corner L~Cage(C,n))
                                                              by SPRECT_1:28;
       then NE in LSeg(SW-corner L~Cage(C,n), NW-corner L~Cage(C,n)) /\
        L~Cage(C,n) by A29,A31,XBOOLE_0:def 3;
       then A33: NE`2 >= (W-min L~Cage(C,n))`2 by PSCOMP_1:88;
            (W-min L~Cage(C,n))`2 >= NE`2 by PSCOMP_1:87;
       then A34: (W-min L~Cage(C,n))`2 = NE`2 by A33,XXREAL_0:1;
            (W-min L~Cage(C,n))`1 = NE`1 by PSCOMP_1:85;
       hence contradiction by A17,A34,TOPREAL3:11;
      end;
           now per cases;
       suppose Gij <> Lower_Seq(C,n).(Index(Gij,Lower_Seq(C,n))+1);
        then v1 = <*Gij*>^mid(Lower_Seq(C,n),
         Index(Gij,Lower_Seq(C,n))+1,len Lower_Seq(C,n)) by JORDAN3:def 4;
        then rng v1 = rng <*Gij*> \/ rng mid(Lower_Seq(C,n),
         Index(Gij,Lower_Seq(C,n))+1,len Lower_Seq(C,n)) by FINSEQ_1:44;
        then A35: rng v1 = {Gij} \/ rng mid(Lower_Seq(C,n),
         Index(Gij,Lower_Seq(C,n))+1,len Lower_Seq(C,n)) by FINSEQ_1:55;
             not NE in L~Cage(C,n)
        proof
         assume NE in L~Cage(C,n);
         then consider i be Element of NAT such that
          A36: 1 <= i and
          A37: i+1 <= len Cage(C,n) and
          A38: NE in LSeg(Cage(C,n)/.i,Cage(C,n)/.(i+1)) by SPPOL_2:14;
         per cases by A36,A37,TOPREAL1:def 7;
          suppose A39: (Cage(C,n)/.i)`1 = (Cage(C,n)/.(i+1))`1;
           then A40: NE`1 = (Cage(C,n)/.i)`1 by A38,GOBOARD7:5;
           A41: NE`2 = S-bound L~Cage(C,n) by EUCLID:56;
           A42: i < len Cage(C,n) by A37,NAT_1:13;
           then A43: (Cage(C,n)/.i)`2 >= NE`2 by A36,A41,JORDAN5D:13;
           A44: 1 <= i+1 by NAT_1:11;
           then A45: (Cage(C,n)/.(i+1))`2 >= NE`2 by A37,A41,JORDAN5D:13;
           A46: i in dom Cage(C,n) & i+1 in dom Cage(C,n)
                                          by A36,A37,A42,A44,FINSEQ_3:27;
                (Cage(C,n)/.i)`2 <= (Cage(C,n)/.(i+1))`2 or
            (Cage(C,n)/.i)`2 >= (Cage(C,n)/.(i+1))`2;
           then NE`2 >= (Cage(C,n)/.(i+1))`2 or NE`2 >= (Cage(C,n)/.i)`2
                                                          by A38,TOPREAL1:10;
           then NE`2 = (Cage(C,n)/.(i+1))`2 or NE`2 = (Cage(C,n)/.i)`2
                                                        by A43,A45,XXREAL_0:1;
           then NE = (Cage(C,n)/.(i+1)) or NE = (Cage(C,n)/.i)
                                                      by A39,A40,TOPREAL3:11;
           hence contradiction by A30,A46,PARTFUN2:4;
          end;
          suppose A47: (Cage(C,n)/.i)`2 = (Cage(C,n)/.(i+1))`2;
           then A48: NE`2 = (Cage(C,n)/.i)`2 by A38,GOBOARD7:6;
           A49: NE`1 = W-bound L~Cage(C,n) by EUCLID:56;
           A50: i < len Cage(C,n) by A37,NAT_1:13;
           then A51: (Cage(C,n)/.i)`1 >= NE`1 by A36,A49,JORDAN5D:14;
           A52: 1 <= i+1 by NAT_1:11;
           then A53: (Cage(C,n)/.(i+1))`1 >= NE`1 by A37,A49,JORDAN5D:14;
           A54: i in dom Cage(C,n) & i+1 in dom Cage(C,n)
                                          by A36,A37,A50,A52,FINSEQ_3:27;
                (Cage(C,n)/.i)`1 <= (Cage(C,n)/.(i+1))`1 or
            (Cage(C,n)/.i)`1 >= (Cage(C,n)/.(i+1))`1;
           then NE`1 >= (Cage(C,n)/.(i+1))`1 or NE`1 >= (Cage(C,n)/.i)`1
                                                           by A38,TOPREAL1:9;
           then NE`1 = (Cage(C,n)/.(i+1))`1 or NE`1 = (Cage(C,n)/.i)`1
                                                        by A51,A53,XXREAL_0:1;
           then NE = (Cage(C,n)/.(i+1)) or NE = (Cage(C,n)/.i)
                                                      by A47,A48,TOPREAL3:11;
           hence contradiction by A30,A54,PARTFUN2:4;
        end;
        end;
        then A55: not NE in {Gij} by A3,TARSKI:def 1;
        A56: rng mid(Lower_Seq(C,n),Index(Gij,Lower_Seq(C,n))+1,
         len Lower_Seq(C,n)) c= rng Lower_Seq(C,n) by JORDAN3:28;
             rng Lower_Seq(C,n) c= rng Cage(C,n) by JORDAN1G:47;
        then rng mid(Lower_Seq(C,n),Index(Gij,Lower_Seq(C,n))+1,
         len Lower_Seq(C,n)) c= rng Cage(C,n) by A56,XBOOLE_1:1;
        then not NE in rng mid(Lower_Seq(C,n),Index(Gij,Lower_Seq(C,n))+1,
         len Lower_Seq(C,n)) by A30;
        hence not NE in rng v1 by A35,A55,XBOOLE_0:def 2;
       end;
       suppose Gij = Lower_Seq(C,n).(Index(Gij,Lower_Seq(C,n))+1);
        then v1 = mid(Lower_Seq(C,n),
         Index(Gij,Lower_Seq(C,n))+1,len Lower_Seq(C,n)) by JORDAN3:def 4;
        then A57: rng v1 c= rng Lower_Seq(C,n) by JORDAN3:28;
             rng Lower_Seq(C,n) c= rng Cage(C,n) by JORDAN1G:47;
        then rng v1 c= rng Cage(C,n) by A57,XBOOLE_1:1;
        hence not NE in rng v1 by A30;
      end;
      end;
      then not NE in rng <*Gauge(C,n)*(i,wG)*> \/ rng v1 by A28,XBOOLE_0:def 2
;
      then not NE in rng Gv1 by FINSEQ_1:44;
      then rng Gv1 misses {NE} by ZFMISC_1:56;
      then A58: rng Gv1 misses rng <*NE*> by FINSEQ_1:55;
      A59: not Gauge(C,n)*(i,wG) in L~Lower_Seq(C,n) by A1,A17,JORDAN1G:53;
           rng Lower_Seq(C,n) c= L~Lower_Seq(C,n) by A5,SPPOL_2:18;
      then A60: not Gauge(C,n)*(i,wG) in rng Lower_Seq(C,n)
                                                       by A1,A17,JORDAN1G:53;
           not Gauge(C,n)*(i,wG) in {Gij} by A17,A59,TARSKI:def 1;
      then A61: not Gauge(C,n)*(i,wG) in rng <*Gij*> by FINSEQ_1:55;
      set ci = mid(Lower_Seq(C,n),Index(Gij,Lower_Seq(C,n))+1,
       len Lower_Seq(C,n));
           now per cases;
       suppose A62: Gij <> Lower_Seq(C,n).(Index(Gij,Lower_Seq(C,n))+1);
             rng ci c= rng Lower_Seq(C,n) by JORDAN3:28;
        then not Gauge(C,n)*(i,wG) in rng ci by A60;
        then not Gauge(C,n)*(i,wG) in rng <*Gij*> \/ rng ci
                                                       by A61,XBOOLE_0:def 2;
        then not Gauge(C,n)*(i,wG) in rng(<*Gij*>^ci) by FINSEQ_1:44;
        hence not Gauge(C,n)*(i,wG) in rng v1 by A62,JORDAN3:def 4;
       end;
       suppose Gij = Lower_Seq(C,n).(Index(Gij,Lower_Seq(C,n))+1);
        then v1 = ci by JORDAN3:def 4;
        then rng v1 c= rng Lower_Seq(C,n) by JORDAN3:28;
        hence not Gauge(C,n)*(i,wG) in rng v1 by A60;
      end;
      end;
      then {Gauge(C,n)*(i,wG)} misses rng v1 by ZFMISC_1:56;
      then A63: rng <*Gauge(C,n)*(i,wG)*> misses rng v1 by FINSEQ_1:55;
      A64: <*Gauge(C,n)*(i,wG)*> is one-to-one by FINSEQ_3:102;
      A65: v1 is_S-Seq by A17,JORDAN3:69;
      then v1 is one-to-one by TOPREAL1:def 10;
      then A66: Gv1 is one-to-one by A63,A64,FINSEQ_3:98;
           <*NE*> is one-to-one by FINSEQ_3:102;
      then A67: v is one-to-one by A58,A66,FINSEQ_3:98;
           for v be one-to-one FinSequence holds Rev v is one-to-one;
      then A68: Rev v is one-to-one by A67;
      A69: <*Gauge(C,n)*(i,wG)*> is special by SPPOL_2:39;
      A70: v1 is special by A65,TOPREAL1:def 10;
           (<*Gauge(C,n)*(i,wG)*>/.len <*Gauge(C,n)*(i,wG)*>)`1 =
            (<*Gauge(C,n)*(i,wG)*>/.1)`1 by FINSEQ_1:56
         .= Gauge(C,n)*(i,wG)`1 by FINSEQ_4:25
         .= Gauge(C,n)*(i,1)`1 by A1,A7,GOBOARD5:3
         .= (v1/.1)`1 by A1,A2,A24,GOBOARD5:3;
      then A71: Gv1 is special by A69,A70,GOBOARD2:13;
      A72: <*NE*> is special by SPPOL_2:39;
           (Gv1/.len Gv1)`1 = NE`1 by A23,PSCOMP_1:85
         .= (<*NE*>/.1)`1 by FINSEQ_4:25;
      then v is special by A71,A72,GOBOARD2:13;
      then A73: Rev v is special by SPPOL_2:42;
      A74: len Upper_Seq(C,n) >= 2+1 by JORDAN1E:19;
      then A75: len Upper_Seq(C,n) > 2 by NAT_1:13;
           len Upper_Seq(C,n) > 1 by A74,XXREAL_0:2;
      then h is S-Sequence_in_R2 by A75,JORDAN3:39;
      then A76: 2 <= len h & h is one-to-one & h is special
                                                         by TOPREAL1:def 10;
      A77: Upper_Seq(C,n) is_in_the_area_of Cage(C,n) by JORDAN1E:21;
           3 <= len Upper_Seq(C,n) by JORDAN1E:19;
      then 2 <= len Upper_Seq(C,n) by XXREAL_0:2;
      then A78: 2 in dom Upper_Seq(C,n) by FINSEQ_3:27;
      A79: len Upper_Seq(C,n) in dom Upper_Seq(C,n) by FINSEQ_5:6;
      then A80: h is_in_the_area_of Cage(C,n) by A77,A78,SPRECT_2:26;
           Upper_Seq(C,n)/.(len Upper_Seq(C,n)) = E-max L~Cage(C,n) by
JORDAN1F:7;
      then (Upper_Seq(C,n)/.len Upper_Seq(C,n))`1 = E-bound L~Cage(C,n)
                                                             by EUCLID:56;
      then A81: (h/.len h)`1 = E-bound L~Cage(C,n) by A78,A79,SPRECT_2:13;
           (Upper_Seq(C,n)/.(1+1))`1 = W-bound L~Cage(C,n) by JORDAN1G:39;
      then (h/.1)`1 = W-bound L~Cage(C,n) by A78,A79,SPRECT_2:12;
      then A82: h is_a_h.c._for Cage(C,n) by A80,A81,SPRECT_2:def 2;
           now let m be Element of NAT;
       assume A83: m in dom <*Gauge(C,n)*(i,wG)*>;
       then m in Seg 1 by FINSEQ_1:55;
       then m = 1 by FINSEQ_1:4,TARSKI:def 1;
       then <*Gauge(C,n)*(i,wG)*>.m = Gauge(C,n)*(i,wG) by FINSEQ_1:57;
       then A84: <*Gauge(C,n)*(i,wG)*>/.m = Gauge(C,n)*(i,wG)
                                                      by A83,PARTFUN1:def 8;
            Gauge(C,n)*(1,wG)`1 <= Gauge(C,n)*(i,wG)`1 by A1,A7,SPRECT_3:25;
       hence W-bound L~Cage(C,n) <= (<*Gauge(C,n)*(i,wG)*>/.m)`1
                                                  by A6,A7,A84,JORDAN1A:94;
            (Gauge(C,n)*(i,wG))`1 <= Gauge(C,n)*(len Gauge(C,n),wG)`1
                                                       by A1,A7,SPRECT_3:25;
       hence (<*Gauge(C,n)*(i,wG)*>/.m)`1 <= E-bound L~Cage(C,n)
                                                  by A6,A7,A84,JORDAN1A:92;
            (<*Gauge(C,n)*(i,wG)*>/.m)`2 = N-bound L~Cage(C,n)
                                                   by A1,A6,A84,JORDAN1A:91;
       hence S-bound L~Cage(C,n) <= (<*Gauge(C,n)*(i,wG)*>/.m)`2
                                                              by SPRECT_1:24;
       thus (<*Gauge(C,n)*(i,wG)*>/.m)`2 <= N-bound L~Cage(C,n)
                                                   by A1,A6,A84,JORDAN1A:91;
      end;
      then A85: <*Gauge(C,n)*(i,wG)*> is_in_the_area_of Cage(C,n)
                                                          by SPRECT_2:def 1;
      A86: Lower_Seq(C,n) is_in_the_area_of Cage(C,n) by JORDAN1E:22;
      then <*Gij*> is_in_the_area_of Cage(C,n) by A17,SPRECT_3:63;
      then v1 is_in_the_area_of Cage(C,n) by A17,A86,SPRECT_3:73;
      then A87: Gv1 is_in_the_area_of Cage(C,n) by A85,SPRECT_2:28;
           <*NE*> is_in_the_area_of Cage(C,n) by SPRECT_2:32;
      then v is_in_the_area_of Cage(C,n) by A87,SPRECT_2:28;
      then A88: Rev v is_in_the_area_of Cage(C,n) by SPRECT_3:68;
           v = <*Gauge(C,n)*(i,wG)*>^(v1^<*NE*>) by FINSEQ_1:45;
      then v/.1 = Gauge(C,n)*(i,wG) by FINSEQ_5:16;
      then (v/.1)`2 = N-bound L~Cage(C,n) by A1,A6,JORDAN1A:91;
      then (Rev v/.(len v))`2 = N-bound L~Cage(C,n) by FINSEQ_5:68;
      then A89: (Rev v/.(len Rev v))`2 = N-bound L~Cage(C,n)
                                                          by FINSEQ_5:def 3;
           len v = len Gv1 + 1 by FINSEQ_2:19;
      then v/.(len v) = NE by FINSEQ_4:82;
      then (v/.len v)`2 = S-bound L~Cage(C,n) by EUCLID:56;
      then (Rev v/.1)`2 = S-bound L~Cage(C,n) by FINSEQ_5:68;
      then Rev v is_a_v.c._for Cage(C,n) by A88,A89,SPRECT_2:def 3;
      then L~h meets L~Rev v by A27,A68,A73,A76,A82,SPRECT_2:33;
      then L~h meets L~v by SPPOL_2:22;
      then consider x be set such that
       A90: x in L~h and
       A91: x in L~v by XBOOLE_0:3;
      A92: L~h c= L~Upper_Seq(C,n) by A5,JORDAN4:47;
      A93: L~v1 c= L~Lower_Seq(C,n) by A17,JORDAN3:77;
           L~v = L~(<*Gauge(C,n)*(i,wG)*>^(v1^<*NE*>)) by FINSEQ_1:45
         .= LSeg(Gauge(C,n)*(i,wG),(v1^<*NE*>)/.1) \/ L~(v1^<*NE*>)
                                                                by SPPOL_2:20
         .= LSeg(Gauge(C,n)*(i,wG),(v1^<*NE*>)/.1) \/
            (L~v1 \/ LSeg(v1/.(len v1),NE)) by A18,SPPOL_2:19;
      then A94: x in LSeg(Gauge(C,n)*(i,wG),(v1^<*NE*>)/.1) or
       x in L~v1 \/ LSeg(v1/.(len v1),NE) by A91,XBOOLE_0:def 2;
           Upper_Seq(C,n)/.1 = W-min L~Cage(C,n) by JORDAN1F:5;
      then A95: not W-min L~Cage(C,n) in L~h by A75,JORDAN5B:16;
           now per cases by A94,XBOOLE_0:def 2;
       suppose x in LSeg(Gauge(C,n)*(i,wG),(v1^<*NE*>)/.1);
        then x in L~<*Gauge(C,n)*(i,wG),Gij*> by A25,SPPOL_2:21;
        hence L~Upper_Seq(C,n) meets L~<*Gauge(C,n)*(i,wG),Gij*>
                                                 by A90,A92,XBOOLE_0:3;
       end;
       suppose A96: x in L~v1;
        then x in L~Lower_Seq(C,n) /\ L~Upper_Seq(C,n)
                                           by A90,A92,A93,XBOOLE_0:def 3;
        then x in {W-min L~Cage(C,n),E-max L~Cage(C,n)} by JORDAN1E:20;
        then A97: x = E-max L~Cage(C,n) by A90,A95,TARSKI:def 2;
             1 in dom Lower_Seq(C,n) by A5,FINSEQ_3:27;
        then Lower_Seq(C,n).1 = Lower_Seq(C,n)/.1 by PARTFUN1:def 8
           .= E-max L~Cage(C,n) by JORDAN1F:6;
        then x = Gij by A17,A96,A97,JORDAN1E:11;
        then x in LSeg(Gauge(C,n)*(i,wG),Gij) by TOPREAL1:6;
        then x in L~<*Gauge(C,n)*(i,wG),Gij*> by SPPOL_2:21;
        hence L~Upper_Seq(C,n) meets L~<*Gauge(C,n)*(i,wG),Gij*>
                                              by A90,A92,XBOOLE_0:3;
       end;
       suppose A98: x in LSeg(v1/.(len v1),NE);
             x in L~Cage(C,n) by A4,A90,A92,XBOOLE_0:def 2;
        then x in LSeg(W-min L~Cage(C,n), NE) /\ L~Cage(C,n)
                                              by A22,A98,XBOOLE_0:def 3;
        then x in {W-min L~Cage(C,n)} by PSCOMP_1:92;
        hence L~Upper_Seq(C,n) meets L~<*Gauge(C,n)*(i,wG),Gij*>
                                              by A90,A95,TARSKI:def 1;
      end;
      end;
      then L~<*Gauge(C,n)*(i,wG),Gij*> meets L~Upper_Seq(C,n);
      hence LSeg(Gauge(C,n)*(i,width Gauge(C,n)),Gij) meets L~Upper_Seq(C,n)
                                                               by SPPOL_2:21;
     end;
     suppose A99: Gij in L~Lower_Seq(C,n) &
      Gij <> Lower_Seq(C,n).len Lower_Seq(C,n)
      & W-min L~Cage(C,n) = NE & i < lG;
      then A100: v1 is non empty by JORDAN1E:7;
      then len v1 <> 0 by FINSEQ_1:25;
      then len v1 > 0;
      then A101: 0+1 <= len v1 by NAT_1:13;
      then A102: 1 in dom v1 by FINSEQ_3:27;
      set v = Gv1;
      A103: len v1 in dom v1 & len Lower_Seq(C,n) in dom Lower_Seq(C,n)
                                                 by A5,A101,FINSEQ_3:27;
      then v1/.(len v1) = v1.(len v1) by PARTFUN1:def 8
         .= Lower_Seq(C,n).len Lower_Seq(C,n) by A99,JORDAN1B:5
         .= Lower_Seq(C,n)/.len Lower_Seq(C,n) by A103,PARTFUN1:def 8
         .= W-min L~Cage(C,n) by JORDAN1F:8;
      then A104: Gv1/.len Gv1 = W-min L~Cage(C,n) by A100,SPRECT_3:11;
      A105: v1/.1 = v1.1 by A102,PARTFUN1:def 8
         .= Gij by A99,JORDAN3:58;
           1+len v1 >= 1+1 by A101,XREAL_1:9;
      then 2 <= len v by FINSEQ_5:8;
      then A106: 2 <= len Rev v by FINSEQ_5:def 3;
      A107: not Gauge(C,n)*(i,wG) in L~Lower_Seq(C,n) by A1,A99,JORDAN1G:53;
           rng Lower_Seq(C,n) c= L~Lower_Seq(C,n) by A5,SPPOL_2:18;
      then A108: not Gauge(C,n)*(i,wG) in rng Lower_Seq(C,n)
                                                       by A1,A99,JORDAN1G:53;
           not Gauge(C,n)*(i,wG) in {Gij} by A99,A107,TARSKI:def 1;
      then A109: not Gauge(C,n)*(i,wG) in rng <*Gij*> by FINSEQ_1:55;
      set ci = mid(Lower_Seq(C,n),Index(Gij,Lower_Seq(C,n))+1,
       len Lower_Seq(C,n));
           now per cases;
       suppose A110: Gij <> Lower_Seq(C,n).(Index(Gij,Lower_Seq(C,n))+1);
             rng ci c= rng Lower_Seq(C,n) by JORDAN3:28;
        then not Gauge(C,n)*(i,wG) in rng ci by A108;
        then not Gauge(C,n)*(i,wG) in rng <*Gij*> \/ rng ci
                                                       by A109,XBOOLE_0:def 2;
        then not Gauge(C,n)*(i,wG) in rng(<*Gij*>^ci) by FINSEQ_1:44;
        hence not Gauge(C,n)*(i,wG) in rng v1 by A110,JORDAN3:def 4;
       end;
       suppose Gij = Lower_Seq(C,n).(Index(Gij,Lower_Seq(C,n))+1);
        then v1 = ci by JORDAN3:def 4;
        then rng v1 c= rng Lower_Seq(C,n) by JORDAN3:28;
        hence not Gauge(C,n)*(i,wG) in rng v1 by A108;
      end;
      end;
      then {Gauge(C,n)*(i,wG)} misses rng v1 by ZFMISC_1:56;
      then A111: rng <*Gauge(C,n)*(i,wG)*> misses rng v1 by FINSEQ_1:55;
      A112: <*Gauge(C,n)*(i,wG)*> is one-to-one by FINSEQ_3:102;
      A113: v1 is_S-Seq by A99,JORDAN3:69;
      then v1 is one-to-one by TOPREAL1:def 10;
      then A114: Gv1 is one-to-one by A111,A112,FINSEQ_3:98;
           for v be one-to-one FinSequence holds Rev v is one-to-one;
      then A115: Rev v is one-to-one by A114;
      A116: <*Gauge(C,n)*(i,wG)*> is special by SPPOL_2:39;
      A117: v1 is special by A113,TOPREAL1:def 10;
           (<*Gauge(C,n)*(i,wG)*>/.len <*Gauge(C,n)*(i,wG)*>)`1 =
            (<*Gauge(C,n)*(i,wG)*>/.1)`1 by FINSEQ_1:56
         .= Gauge(C,n)*(i,wG)`1 by FINSEQ_4:25
         .= Gauge(C,n)*(i,1)`1 by A1,A7,GOBOARD5:3
         .= (v1/.1)`1 by A1,A2,A105,GOBOARD5:3;
      then Gv1 is special by A116,A117,GOBOARD2:13;
      then A118: Rev v is special by SPPOL_2:42;
      A119: len Upper_Seq(C,n) >= 2+1 by JORDAN1E:19;
      then A120: len Upper_Seq(C,n) > 2 by NAT_1:13;
           len Upper_Seq(C,n) > 1 by A119,XXREAL_0:2;
      then h is S-Sequence_in_R2 by A120,JORDAN3:39;
      then A121: 2 <= len h & h is one-to-one & h is special
                                                         by TOPREAL1:def 10;
      A122: Upper_Seq(C,n) is_in_the_area_of Cage(C,n) by JORDAN1E:21;
           3 <= len Upper_Seq(C,n) by JORDAN1E:19;
      then 2 <= len Upper_Seq(C,n) by XXREAL_0:2;
      then A123: 2 in dom Upper_Seq(C,n) by FINSEQ_3:27;
      A124: len Upper_Seq(C,n) in dom Upper_Seq(C,n) by FINSEQ_5:6;
      then A125: h is_in_the_area_of Cage(C,n) by A122,A123,SPRECT_2:26;
           Upper_Seq(C,n)/.(len Upper_Seq(C,n)) = E-max L~Cage(C,n) by
JORDAN1F:7;
      then (Upper_Seq(C,n)/.len Upper_Seq(C,n))`1 = E-bound L~Cage(C,n)
                                                             by EUCLID:56;
      then A126: (h/.len h)`1 = E-bound L~Cage(C,n) by A123,A124,SPRECT_2:13;
           (Upper_Seq(C,n)/.(1+1))`1 = W-bound L~Cage(C,n) by JORDAN1G:39;
      then (h/.1)`1 = W-bound L~Cage(C,n) by A123,A124,SPRECT_2:12;
      then A127: h is_a_h.c._for Cage(C,n) by A125,A126,SPRECT_2:def 2;
           now let m be Element of NAT;
       assume A128: m in dom <*Gauge(C,n)*(i,wG)*>;
       then m in Seg 1 by FINSEQ_1:55;
       then m = 1 by FINSEQ_1:4,TARSKI:def 1;
       then <*Gauge(C,n)*(i,wG)*>.m = Gauge(C,n)*(i,wG) by FINSEQ_1:57;
       then A129: <*Gauge(C,n)*(i,wG)*>/.m = Gauge(C,n)*(i,wG)
                                                      by A128,PARTFUN1:def 8;
            Gauge(C,n)*(1,wG)`1 <= Gauge(C,n)*(i,wG)`1 by A1,A7,SPRECT_3:25;
       hence W-bound L~Cage(C,n) <= (<*Gauge(C,n)*(i,wG)*>/.m)`1
                                                  by A6,A7,A129,JORDAN1A:94;
            (Gauge(C,n)*(i,wG))`1 <= Gauge(C,n)*(len Gauge(C,n),wG)`1
                                                       by A1,A7,SPRECT_3:25;
       hence (<*Gauge(C,n)*(i,wG)*>/.m)`1 <= E-bound L~Cage(C,n)
                                                  by A6,A7,A129,JORDAN1A:92;
            (<*Gauge(C,n)*(i,wG)*>/.m)`2 = N-bound L~Cage(C,n)
                                                   by A1,A6,A129,JORDAN1A:91;
       hence S-bound L~Cage(C,n) <= (<*Gauge(C,n)*(i,wG)*>/.m)`2
                                                              by SPRECT_1:24;
       thus (<*Gauge(C,n)*(i,wG)*>/.m)`2 <= N-bound L~Cage(C,n)
                                                   by A1,A6,A129,JORDAN1A:91;
      end;
      then A130: <*Gauge(C,n)*(i,wG)*> is_in_the_area_of Cage(C,n)
                                                          by SPRECT_2:def 1;
      A131: Lower_Seq(C,n) is_in_the_area_of Cage(C,n) by JORDAN1E:22;
      then <*Gij*> is_in_the_area_of Cage(C,n) by A99,SPRECT_3:63;
      then v1 is_in_the_area_of Cage(C,n) by A99,A131,SPRECT_3:73;
      then Gv1 is_in_the_area_of Cage(C,n) by A130,SPRECT_2:28;
      then A132: Rev v is_in_the_area_of Cage(C,n) by SPRECT_3:68;
           v/.1 = Gauge(C,n)*(i,wG) by FINSEQ_5:16;
      then (v/.1)`2 = N-bound L~Cage(C,n) by A1,A6,JORDAN1A:91;
      then (Rev v/.(len v))`2 = N-bound L~Cage(C,n) by FINSEQ_5:68;
      then A133: (Rev v/.(len Rev v))`2 = N-bound L~Cage(C,n)
                                                          by FINSEQ_5:def 3;
           (v/.len v)`2 = S-bound L~Cage(C,n) by A99,A104,EUCLID:56;
      then (Rev v/.1)`2 = S-bound L~Cage(C,n) by FINSEQ_5:68;
      then Rev v is_a_v.c._for Cage(C,n) by A132,A133,SPRECT_2:def 3;
      then L~h meets L~Rev v by A106,A115,A118,A121,A127,SPRECT_2:33;
      then L~h meets L~v by SPPOL_2:22;
      then consider x be set such that
       A134: x in L~h and
       A135: x in L~v by XBOOLE_0:3;
      A136: L~h c= L~Upper_Seq(C,n) by A5,JORDAN4:47;
      A137: L~v1 c= L~Lower_Seq(C,n) by A99,JORDAN3:77;
      A138: L~v = LSeg(Gauge(C,n)*(i,wG),v1/.1) \/ L~v1 by A100,SPPOL_2:20;
           Upper_Seq(C,n)/.1 = W-min L~Cage(C,n) by JORDAN1F:5;
      then A139: not W-min L~Cage(C,n) in L~h by A120,JORDAN5B:16;
           now per cases by A135,A138,XBOOLE_0:def 2;
       suppose x in LSeg(Gauge(C,n)*(i,wG),v1/.1);
        then x in L~<*Gauge(C,n)*(i,wG),Gij*> by A105,SPPOL_2:21;
        hence L~Upper_Seq(C,n) meets L~<*Gauge(C,n)*(i,wG),Gij*>
                                                 by A134,A136,XBOOLE_0:3;
       end;
       suppose A140: x in L~v1;
        then x in L~Lower_Seq(C,n) /\ L~Upper_Seq(C,n)
                                           by A134,A136,A137,XBOOLE_0:def 3;
        then x in {W-min L~Cage(C,n),E-max L~Cage(C,n)} by JORDAN1E:20;
        then A141: x = E-max L~Cage(C,n) by A134,A139,TARSKI:def 2;
             1 in dom Lower_Seq(C,n) by A5,FINSEQ_3:27;
        then Lower_Seq(C,n).1 = Lower_Seq(C,n)/.1 by PARTFUN1:def 8
           .= E-max L~Cage(C,n) by JORDAN1F:6;
        then x = Gij by A99,A140,A141,JORDAN1E:11;
        then x in LSeg(Gauge(C,n)*(i,wG),Gij) by TOPREAL1:6;
        then x in L~<*Gauge(C,n)*(i,wG),Gij*> by SPPOL_2:21;
        hence L~Upper_Seq(C,n) meets L~<*Gauge(C,n)*(i,wG),Gij*>
                                              by A134,A136,XBOOLE_0:3;
      end;
      end;
      then L~<*Gauge(C,n)*(i,wG),Gij*> meets L~Upper_Seq(C,n);
      hence LSeg(Gauge(C,n)*(i,width Gauge(C,n)),Gij) meets L~Upper_Seq(C,n)
                                                               by SPPOL_2:21;
     end;
     suppose A142: Gij in L~Upper_Seq(C,n);
           Gij in LSeg(Gauge(C,n)*(i,wG),Gij) by TOPREAL1:6;
      hence LSeg(Gauge(C,n)*(i,width Gauge(C,n)),Gij) meets L~Upper_Seq(C,n)
                                                         by A142,XBOOLE_0:3;
     end;
     suppose A143: Gij in L~Lower_Seq(C,n) &
       Gij = Lower_Seq(C,n).len Lower_Seq(C,n);
           len Lower_Seq(C,n) in dom Lower_Seq(C,n) by A5,FINSEQ_3:27;
      then A144: Lower_Seq(C,n).len Lower_Seq(C,n) =
            Lower_Seq(C,n)/.len Lower_Seq(C,n) by PARTFUN1:def 8
         .= W-min L~Cage(C,n) by JORDAN1F:8;
      A145: rng Upper_Seq(C,n) c= L~Upper_Seq(C,n) by A5,SPPOL_2:18;
      A146: W-min L~Cage(C,n) in rng Upper_Seq(C,n) by JORDAN1J:5;
           Gij in LSeg(Gauge(C,n)*(i,wG),Gij) by TOPREAL1:6;
      hence LSeg(Gauge(C,n)*(i,wG),Gij) meets L~Upper_Seq(C,n)
                                         by A143,A144,A145,A146,XBOOLE_0:3;
    end;
    end;
    hence thesis;
   end;

  theorem Th4:
   for C be compact connected non vertical non horizontal Subset of TOP-REAL 2
   for n be Element of NAT st n > 0
   for i,j be Element of NAT st 1 <= i & i <= len Gauge(C,n) &
    1 <= j & j <= width Gauge(C,n) & Gauge(C,n)*(i,j) in L~Cage(C,n) holds
     LSeg(Gauge(C,n)*(i,width Gauge(C,n)),Gauge(C,n)*(i,j)) meets
      Upper_Arc L~Cage(C,n)
   proof
    let C be compact connected non vertical non horizontal
     Subset of TOP-REAL 2;
    let n be Element of NAT;
    assume A1: n > 0;
    let i,j be Element of NAT;
    assume A2: 1 <= i & i <= len Gauge(C,n) & 1 <= j & j <= width Gauge(C,n) &
     Gauge(C,n)*(i,j) in L~Cage(C,n);
         L~Upper_Seq(C,n) = Upper_Arc L~Cage(C,n) by A1,JORDAN1G:63;
    hence thesis by A2,Th3;
   end;

  theorem
        for C be compact connected non vertical non horizontal Subset of
TOP-REAL
2
   for j be Element of NAT holds
    Gauge(C,n+1)*(Center Gauge(C,n+1),j) in Lower_Arc L~Cage(C,n+1) &
    1 <= j & j <= width Gauge(C,n+1) implies
    LSeg(Gauge(C,1)*(Center Gauge(C,1),width Gauge(C,1)),
         Gauge(C,n+1)*(Center Gauge(C,n+1),j)) meets Upper_Arc L~Cage(C,n+1)
   proof
    let C be compact connected non vertical non horizontal
     Subset of TOP-REAL 2;
    let j be Element of NAT;
    assume that
     A1: Gauge(C,n+1)*(Center Gauge(C,n+1),j) in Lower_Arc L~Cage(C,n+1) and
     A2: 1 <= j & j <= width Gauge(C,n+1);
    set in1 = Center Gauge(C,n+1);
    A3: n+1 >= 0+1 by NAT_1:11;
    A4: 1 <= in1 by JORDAN1B:12;
    A5: in1 <= len Gauge(C,n+1) by JORDAN1B:14;
    A6: LSeg(Gauge(C,n+1)*(Center Gauge(C,n+1),width Gauge(C,n+1)),
      Gauge(C,n+1)*(Center Gauge(C,n+1),j)) c=
     LSeg(Gauge(C,1)*(Center Gauge(C,1),width Gauge(C,1)),
      Gauge(C,n+1)*(Center Gauge(C,n+1),j)) by A2,A3,Th2;
         Lower_Arc L~Cage(C,n+1) c= L~Cage(C,n+1) by JORDAN6:76;
    then LSeg(Gauge(C,n+1)*(in1,width Gauge(C,n+1)),Gauge(C,n+1)*(in1,j))
     meets Upper_Arc L~Cage(C,n+1) by A1,A2,A4,A5,Th4;
    hence thesis by A6,XBOOLE_1:63;
   end;

  theorem Th6:
   for C be compact connected non vertical non horizontal Subset of TOP-REAL 2
   for f be FinSequence of TOP-REAL 2
   for k be Element of NAT st 1 <= k & k+1 <= len f &
   f is_sequence_on Gauge(C,n) holds
    dist(f/.k,f/.(k+1)) = (N-bound C - S-bound C)/2|^n or
    dist(f/.k,f/.(k+1)) = (E-bound C - W-bound C)/2|^n
   proof
    let C be compact connected non vertical non horizontal
     Subset of TOP-REAL 2;
    let f be FinSequence of TOP-REAL 2;
    let k be Element of NAT;
    assume A1: 1 <= k & k+1 <= len f;
    assume f is_sequence_on Gauge(C,n);
    then consider i1,j1,i2,j2 be Element of NAT such that
     A2: [i1,j1] in Indices Gauge(C,n) and
     A3: f/.k = Gauge(C,n)*(i1,j1) and
     A4: [i2,j2] in Indices Gauge(C,n) and
     A5: f/.(k+1) = Gauge(C,n)*(i2,j2) and
     A6: i1 = i2 & j1+1 = j2 or i1+1 = i2 & j1 = j2 or
         i1 = i2+1 & j1 = j2 or i1 = i2 & j1 = j2+1 by A1,JORDAN8:6;
     per cases by A6;
      suppose i1 = i2 & j1+1 = j2;
       hence thesis by A2,A3,A4,A5,GOBRD14:19;
      end;
      suppose i1+1 = i2 & j1 = j2;
       hence thesis by A2,A3,A4,A5,GOBRD14:20;
      end;
      suppose i1 = i2+1 & j1 = j2;
       hence thesis by A2,A3,A4,A5,GOBRD14:20;
      end;
      suppose i1 = i2 & j1 = j2+1;
       hence thesis by A2,A3,A4,A5,GOBRD14:19;
   end;
   end;

  theorem
        for M be symmetric triangle MetrStruct
   for r be real number
   for p,q,x be Element of M st
    p in Ball(x,r) & q in Ball(x,r) holds
    dist(p,q) < 2*r
   proof
    let M be symmetric triangle MetrStruct;
    let r be real number;
    let p,q,x be Element of M;
    assume that
     A1: p in Ball(x,r) and
     A2: q in Ball(x,r);
    A3: dist(p,x) < r by A1,METRIC_1:12;
    A4: dist(x,q) < r by A2,METRIC_1:12;
    A5: dist(p,q) <= dist(p,x) + dist(x,q) by METRIC_1:4;
         dist(p,x) + dist(x,q) < r+r by A3,A4,XREAL_1:10;
    hence dist(p,q) < 2*r by A5,XXREAL_0:2;
   end;

canceled;

  theorem
        for C be compact connected non vertical non horizontal
    Subset of TOP-REAL 2 holds
    N-bound C < N-bound L~Cage(C,n)
   proof
    let C be compact connected non vertical non horizontal
     Subset of TOP-REAL 2;
    A1: 2|^n > 0 by NEWTON:102;
         N-bound C > S-bound C + 0 by SPRECT_1:34;
    then N-bound C - S-bound C > 0 by XREAL_1:22;
    then A2: (N-bound C - S-bound C)/(2|^n) > N-bound C - N-bound C
                                                                by A1,
REAL_2:127;
         N-bound L~Cage(C,n) = N-bound C + (N-bound C - S-bound C)/(2|^n)
                                                               by JORDAN10:6;
    hence N-bound C < N-bound L~Cage(C,n) by A2,XREAL_1:21;
   end;

  theorem Th10:
   for C be compact connected non vertical non horizontal
    Subset of TOP-REAL 2 holds
    E-bound C < E-bound L~Cage(C,n)
   proof
    let C be compact connected non vertical non horizontal
     Subset of TOP-REAL 2;
    A1: 2|^n > 0 by NEWTON:102;
         E-bound C > W-bound C + 0 by SPRECT_1:33;
    then E-bound C - W-bound C > 0 by XREAL_1:22;
    then A2: (E-bound C - W-bound C)/(2|^n) > E-bound C - E-bound C
                                                                by A1,
REAL_2:127;
         E-bound L~Cage(C,n) = E-bound C + (E-bound C - W-bound C)/(2|^n)
                                                              by JORDAN1A:85;
    hence E-bound C < E-bound L~Cage(C,n) by A2,XREAL_1:21;
   end;

  theorem
        for C be compact connected non vertical non horizontal
    Subset of TOP-REAL 2 holds
    S-bound L~Cage(C,n) < S-bound C
   proof
    let C be compact connected non vertical non horizontal
     Subset of TOP-REAL 2;
    A1: 2|^n > 0 by NEWTON:102;
         N-bound C > S-bound C + 0 by SPRECT_1:34;
    then N-bound C - S-bound C > 0 by XREAL_1:22;
    then A2: (N-bound C - S-bound C)/(2|^n) > S-bound C - S-bound C
                                                                by A1,
REAL_2:127;
         S-bound L~Cage(C,n) = S-bound C - (N-bound C - S-bound C)/(2|^n)
                                                              by JORDAN1A:84;
    hence S-bound L~Cage(C,n) < S-bound C by A2,REAL_2:165;
   end;

  theorem Th12:
   for C be compact connected non vertical non horizontal
    Subset of TOP-REAL 2 holds
    W-bound L~Cage(C,n) < W-bound C
   proof
    let C be compact connected non vertical non horizontal
     Subset of TOP-REAL 2;
    A1: 2|^n > 0 by NEWTON:102;
         E-bound C > W-bound C + 0 by SPRECT_1:33;
    then E-bound C - W-bound C > 0 by XREAL_1:22;
    then A2: (E-bound C - W-bound C)/(2|^n) > W-bound C - W-bound C
                                                                by A1,
REAL_2:127;
         W-bound L~Cage(C,n) = W-bound C - (E-bound C - W-bound C)/(2|^n)
                                                              by JORDAN1A:83;
    hence W-bound L~Cage(C,n) < W-bound C by A2,REAL_2:165;
   end;

  theorem Th13:
   for C be Simple_closed_curve
   for i,j,k be Element of NAT st
    1 < i & i < len Gauge(C,n) &
    1 <= k & k <= j & j <= width Gauge(C,n) &
    LSeg(Gauge(C,n)*(i,k),Gauge(C,n)*(i,j)) /\ L~Upper_Seq(C,n) =
     {Gauge(C,n)*(i,k)} &
    LSeg(Gauge(C,n)*(i,k),Gauge(C,n)*(i,j)) /\ L~Lower_Seq(C,n) =
     {Gauge(C,n)*(i,j)} holds
      LSeg(Gauge(C,n)*(i,k),Gauge(C,n)*(i,j)) meets Upper_Arc C
   proof
    let C be Simple_closed_curve;
    let i,j,k be Element of NAT;
    set Ga = Gauge(C,n);
    set US = Upper_Seq(C,n);
    set LS = Lower_Seq(C,n);
    set UA = Upper_Arc C;
    set Wmin = W-min L~Cage(C,n);
    set Emax = E-max L~Cage(C,n);
    set Wbo = W-bound L~Cage(C,n);
    set Ebo = E-bound L~Cage(C,n);
    set Gik = Ga*(i,k);
    set Gij = Ga*(i,j);
    assume that
     A1: 1 < i & i < len Ga and
     A2: 1 <= k & k <= j & j <= width Ga and
     A3: LSeg(Gik,Gij) /\ L~US = {Gik} and
     A4: LSeg(Gik,Gij) /\ L~LS = {Gij} and
     A5: LSeg(Gik,Gij) misses UA;
         Gij in {Gij} by TARSKI:def 1;
    then A6: Gij in L~LS by A4,XBOOLE_0:def 3;
         Gik in {Gik} by TARSKI:def 1;
    then A7: Gik in L~US by A3,XBOOLE_0:def 3;
    then A8: j <> k by A1,A2,A6,JORDAN1J:57;
    A9: 1 <= j & j <= width Ga by A2,XXREAL_0:2;
    A10: 1 <= k & k <= width Ga by A2,XXREAL_0:2;
    A11: [i,j] in Indices Ga by A1,A9,GOBOARD7:10;
    A12: [i,k] in Indices Ga by A1,A10,GOBOARD7:10;
    A13: LS is_sequence_on Ga by JORDAN1G:5;
    A14: US is_sequence_on Ga by JORDAN1G:4;
    set do = L_Cut(LS,Gij);
    set go = R_Cut(US,Gik);
    A15: len Ga = width Ga by JORDAN8:def 1;
    A16: len US >= 3 by JORDAN1E:19;
    then len US >= 1 by XXREAL_0:2;
    then 1 in dom US by FINSEQ_3:27;
    then A17: US.1 = US/.1 by PARTFUN1:def 8
       .= Wmin by JORDAN1F:5;
    A18: Wmin`1 = Wbo by EUCLID:56
       .= Ga*(1,k)`1 by A10,A15,JORDAN1A:94;
         len Ga >= 4 by JORDAN8:13;
    then A19: len Ga >= 1 by XXREAL_0:2;
    then A20: [1,k] in Indices Ga by A10,GOBOARD7:10;
    then A21: Gik <> US.1 by A1,A12,A17,A18,JORDAN1G:7;
    then reconsider go as being_S-Seq FinSequence of TOP-REAL 2
                                                            by A7,JORDAN3:70;
    A22: len LS >= 1+2 by JORDAN1E:19;
    then len LS >= 1 by XXREAL_0:2;
    then A23: 1 in dom LS & len LS in dom LS by FINSEQ_3:27;
    then A24: LS.len LS = LS/.len LS by PARTFUN1:def 8
       .= Wmin by JORDAN1F:8;
    A25: Wmin`1 = Wbo by EUCLID:56
       .= Ga*(1,k)`1 by A10,A15,JORDAN1A:94;
    A26: [i,j] in Indices Ga by A1,A9,GOBOARD7:10;
    then A27: Gij <> LS.len LS by A1,A20,A24,A25,JORDAN1G:7;
    then reconsider do as being_S-Seq FinSequence of TOP-REAL 2
                                                            by A6,JORDAN3:69;
    A28: [len Ga,k] in Indices Ga by A10,A19,GOBOARD7:10;
    A29: LS.1 = LS/.1 by A23,PARTFUN1:def 8
       .= Emax by JORDAN1F:6;
         Emax`1 = Ebo by EUCLID:56
       .= Ga*(len Ga,k)`1 by A10,A15,JORDAN1A:92;
    then A30: Gij <> LS.1 by A1,A26,A28,A29,JORDAN1G:7;
    A31: len go >= 1+1 by TOPREAL1:def 10;
    A32: Gik in rng US by A1,A7,A10,A14,JORDAN1J:40;
    then A33: go is_sequence_on Ga by A14,JORDAN1J:38;
    A34: len do >= 1+1 by TOPREAL1:def 10;
    A35: Gij in rng LS by A1,A6,A9,A13,JORDAN1J:40;
    then A36: do is_sequence_on Ga by A13,JORDAN1J:39;
    reconsider go as non constant s.c.c.
            (being_S-Seq FinSequence of TOP-REAL 2) by A31,A33,JGRAPH_1:16
,JORDAN8:8;
    reconsider do as non constant s.c.c.
            (being_S-Seq FinSequence of TOP-REAL 2) by A34,A36,JGRAPH_1:16
,JORDAN8:8;
    A37: len go > 1 by A31,NAT_1:13;
    then A38: len go in dom go by FINSEQ_3:27;
    then A39: go/.len go = go.len go by PARTFUN1:def 8
       .= Gik by A7,JORDAN3:59;
         len do >= 1 by A34,XXREAL_0:2;
    then 1 in dom do by FINSEQ_3:27;
    then A40: do/.1 = do.1 by PARTFUN1:def 8
       .= Gij by A6,JORDAN3:58;
    reconsider m = len go - 1 as Element of NAT by A38,FINSEQ_3:28;
    A41: m+1 = len go;
    then A42: len go-'1 = m by BINARITH:39;
    A43: LSeg(go,m) c= L~go by TOPREAL3:26;
    A44: L~go c= L~US by A7,JORDAN3:76;
    then LSeg(go,m) c= L~US by A43,XBOOLE_1:1;
    then A45: LSeg(go,m) /\ LSeg(Gik,Gij) c= {Gik} by A3,XBOOLE_1:26;
         m >= 1 by A31,XREAL_1:21;
    then A46: LSeg(go,m) = LSeg(go/.m,Gik) by A39,A41,TOPREAL1:def 5;
         {Gik} c= LSeg(go,m) /\ LSeg(Gik,Gij)
    proof
     let x be set;
     assume x in {Gik};
     then A47: x = Gik by TARSKI:def 1;
     A48: Gik in LSeg(go,m) by A46,TOPREAL1:6;
          Gik in LSeg(Gik,Gij) by TOPREAL1:6;
     hence x in LSeg(go,m) /\ LSeg(Gik,Gij) by A47,A48,XBOOLE_0:def 3;
    end;
    then A49: LSeg(go,m) /\ LSeg(Gik,Gij) = {Gik} by A45,XBOOLE_0:def 10;
    A50: LSeg(do,1) c= L~do by TOPREAL3:26;
    A51: L~do c= L~LS by A6,JORDAN3:77;
    then LSeg(do,1) c= L~LS by A50,XBOOLE_1:1;
    then A52: LSeg(do,1) /\ LSeg(Gik,Gij) c= {Gij} by A4,XBOOLE_1:26;
    A53: LSeg(do,1) = LSeg(Gij,do/.(1+1)) by A34,A40,TOPREAL1:def 5;
         {Gij} c= LSeg(do,1) /\ LSeg(Gik,Gij)
    proof
     let x be set;
     assume x in {Gij};
     then A54: x = Gij by TARSKI:def 1;
     A55: Gij in LSeg(do,1) by A53,TOPREAL1:6;
          Gij in LSeg(Gik,Gij) by TOPREAL1:6;
     hence x in LSeg(do,1) /\ LSeg(Gik,Gij) by A54,A55,XBOOLE_0:def 3;
    end;
    then A56: LSeg(Gik,Gij) /\ LSeg(do,1) = {Gij} by A52,XBOOLE_0:def 10;
    A57: go/.1 = US/.1 by A7,SPRECT_3:39
       .= Wmin by JORDAN1F:5;
    then A58: go/.1 = LS/.len LS by JORDAN1F:8
       .= do/.len do by A6,JORDAN1J:35;
    A59: rng go c= L~go & rng do c= L~do by A31,A34,SPPOL_2:18;
    A60: {go/.1} c= L~go /\ L~do
    proof
     let x be set;
     assume x in {go/.1};
     then x = go/.1 by TARSKI:def 1;
     then x in rng go & x in rng do by A58,FINSEQ_6:46,REVROT_1:3;
     hence x in L~go /\ L~do by A59,XBOOLE_0:def 3;
    end;
    A61: LS.1 = LS/.1 by A23,PARTFUN1:def 8
       .= Emax by JORDAN1F:6;
    A62: [len Ga,j] in Indices Ga by A9,A19,GOBOARD7:10;
         L~go /\ L~do c= {go/.1}
    proof
     let x be set;
     assume x in L~go /\ L~do;
     then A63: x in L~go & x in L~do by XBOOLE_0:def 3;
     then x in L~US /\ L~LS by A44,A51,XBOOLE_0:def 3;
     then x in {Wmin,Emax} by JORDAN1E:20;
     then A64: x = Wmin or x = Emax by TARSKI:def 2;
          now assume x = Emax;
      then A65: Emax = Gij by A6,A61,A63,JORDAN1E:11;
           Ga*(len Ga,j)`1 = Ebo by A9,A15,JORDAN1A:92;
      then Emax`1 <> Ebo by A1,A11,A62,A65,JORDAN1G:7;
      hence contradiction by EUCLID:56;
     end;
     hence x in {go/.1} by A57,A64,TARSKI:def 1;
    end;
    then A66: L~go /\ L~do = {go/.1} by A60,XBOOLE_0:def 10;
    set W2 = go/.2;
    A67: 2 in dom go by A31,FINSEQ_3:27;
    A68: Gik..US >= 1 by A32,FINSEQ_4:31;
    A69: now assume Gik`1 = Wbo;
     then Ga*(1,k)`1 = Ga*(i,k)`1 by A10,A15,JORDAN1A:94;
     hence contradiction by A1,A12,A20,JORDAN1G:7;
    end;
         go = mid(US,1,Gik..US) by A32,JORDAN1G:57
       .= US|(Gik..US) by A68,JORDAN3:25;
    then A70: W2 = US/.2 by A67,FINSEQ_4:85;
    A71: Wmin in rng go by A57,FINSEQ_6:46;
    set pion = <*Gik,Gij*>;
    A72: now let n be Element of NAT;
     assume n in dom pion;
     then n in Seg 2 by FINSEQ_3:29;
     then n = 1 or n = 2 by FINSEQ_1:4,TARSKI:def 2;
     then pion/.n = Gik or pion/.n = Gij by FINSEQ_4:26;
     hence ex i,j be Element of NAT st [i,j] in Indices Ga & pion/.n = Ga*(i,j)
                                                                  by A11,A12;
    end;
    A73: Gik <> Gij by A8,A11,A12,GOBOARD1:21;
    A74: Gik`1 = Ga*(i,1)`1 by A1,A10,GOBOARD5:3
       .= Gij`1 by A1,A9,GOBOARD5:3;
    then LSeg(Gik,Gij) is vertical by SPPOL_1:37;
    then pion is_S-Seq by A73,JORDAN1B:8;
    then consider pion1 be FinSequence of TOP-REAL 2 such that
     A75: pion1 is_sequence_on Ga and
     A76: pion1 is_S-Seq and
     A77: L~pion = L~pion1 and
     A78: pion/.1 = pion1/.1 and
     A79: pion/.len pion = pion1/.len pion1 and
     A80: len pion <= len pion1 by A72,GOBOARD3:2;
    reconsider pion1 as being_S-Seq FinSequence of TOP-REAL 2 by A76;
    set godo = go^'pion1^'do;
         len Cage(C,n) > 4 by GOBOARD7:36;
    then A81: 1+1 <= len Cage(C,n) by XXREAL_0:2;
    then A82: 1+1 <= len Rotate(Cage(C,n),Wmin) by REVROT_1:14;
         len (go^'pion1) >= len go by TOPREAL8:7;
    then A83: len (go^'pion1) >= 1+1 by A31,XXREAL_0:2;
    then A84: len (go^'pion1) > 1+0 by NAT_1:13;
         len godo >= len (go^'pion1) by TOPREAL8:7;
    then A85: 1+1 <= len godo by A83,XXREAL_0:2;
    A86: US is_sequence_on Ga by JORDAN1G:4;
    A87: go/.len go = pion1/.1 by A39,A78,FINSEQ_4:26;
    then A88: go^'pion1 is_sequence_on Ga by A33,A75,TOPREAL8:12;
    A89: (go^'pion1)/.len (go^'pion1) = pion/.len pion by A79,GRAPH_2:58
       .= pion/.2 by FINSEQ_1:61
       .= do/.1 by A40,FINSEQ_4:26;
    then A90: godo is_sequence_on Ga by A36,A88,TOPREAL8:12;
         LSeg(pion1,1) c= L~<*Gik,Gij*> by A77,TOPREAL3:26;
    then LSeg(pion1,1) c= LSeg(Gik,Gij) by SPPOL_2:21;
    then A91: LSeg(go,len go-'1) /\ LSeg(pion1,1) c= {Gik}
                                                      by A42,A49,XBOOLE_1:27;
    A92: len pion1 >= 1+1 by A80,FINSEQ_1:61;
         {Gik} c= LSeg(go,m) /\ LSeg(pion1,1)
    proof
     let x be set;
     assume x in {Gik};
     then A93: x = Gik by TARSKI:def 1;
     A94: Gik in LSeg(go,m) by A46,TOPREAL1:6;
          Gik in LSeg(pion1,1) by A39,A87,A92,TOPREAL1:27;
     hence x in LSeg(go,m) /\ LSeg(pion1,1) by A93,A94,XBOOLE_0:def 3;
    end;
    then LSeg(go,len go-'1) /\ LSeg(pion1,1) = { go/.len go }
                                            by A39,A42,A91,XBOOLE_0:def 10;
    then A95: go^'pion1 is unfolded by A87,TOPREAL8:34;
         len pion1 >= 2+0 by A80,FINSEQ_1:61;
    then A96: len pion1-2 >= 0 by XREAL_1:21;
         len (go^'pion1)+1-1 = len go+len pion1-1 by GRAPH_2:13;
    then len (go^'pion1)-1 = len go + (len pion1-2)
       .= len go + (len pion1-'2) by A96,BINARITH:def 3;
    then A97: len (go^'pion1)-'1 = len go + (len pion1-'2)
                                                      by BINARITH:def 3;
    A98: len pion1-1 >= 1 by A92,XREAL_1:21;
    then A99: len pion1-'1 = len pion1-1 by BINARITH:def 3;
    A100: len pion1-'2+1 = len pion1-2+1 by A96,BINARITH:def 3
       .= len pion1-'1 by A98,BINARITH:def 3;
         len pion1-1+1 <= len pion1;
    then A101: len pion1-'1 < len pion1 by A99,NAT_1:13;
         LSeg(pion1,len pion1-'1) c= L~<*Gik,Gij*> by A77,TOPREAL3:26;
    then LSeg(pion1,len pion1-'1) c= LSeg(Gik,Gij) by SPPOL_2:21;
    then A102: LSeg(pion1,len pion1-'1) /\ LSeg(do,1) c= {Gij}
                                                          by A56,XBOOLE_1:27;
         {Gij} c= LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
    proof
     let x be set;
     assume x in {Gij};
     then A103: x = Gij by TARSKI:def 1;
     A104: Gij in LSeg(do,1) by A53,TOPREAL1:6;
       pion1/.(len pion1-'1+1) = pion/.2 by A79,A99,FINSEQ_1:61
        .= Gij by FINSEQ_4:26;
     then Gij in LSeg(pion1,len pion1-'1) by A98,A99,TOPREAL1:27;
     hence x in LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
                                                   by A103,A104,XBOOLE_0:def 3;
    end;
    then LSeg(pion1,len pion1-'1) /\ LSeg(do,1) = {Gij}
                                                      by A102,XBOOLE_0:def 10;
    then A105: LSeg(go^'pion1,len go+(len pion1-'2)) /\ LSeg(do,1) =
       {(go^'pion1)/.len (go^'pion1)} by A40,A87,A89,A100,A101,TOPREAL8:31;
    A106: (go^'pion1) is non trivial by A83,REALSET1:13;
    A107: rng pion1 c= L~pion1 by A92,SPPOL_2:18;
    A108: {pion1/.1} c= L~go /\ L~pion1
    proof
     let x be set;
     assume x in {pion1/.1};
     then x = pion1/.1 by TARSKI:def 1;
     then x in rng go & x in rng pion1 by A87,FINSEQ_6:46,REVROT_1:3;
     hence x in L~go /\ L~pion1 by A59,A107,XBOOLE_0:def 3;
    end;
         L~go /\ L~pion1 c= {pion1/.1}
    proof
     let x be set;
     assume x in L~go /\ L~pion1;
     then x in L~go & x in L~pion1 by XBOOLE_0:def 3;
     then x in L~pion1 /\ L~US by A44,XBOOLE_0:def 3;
     hence x in {pion1/.1} by A3,A39,A77,A87,SPPOL_2:21;
    end;
    then A109: L~go /\ L~pion1 = {pion1/.1} by A108,XBOOLE_0:def 10;
    then A110: (go^'pion1) is s.n.c. by A87,JORDAN1J:54;
         rng go /\ rng pion1 c= {pion1/.1} by A59,A107,A109,XBOOLE_1:27;
    then A111: go^'pion1 is one-to-one by JORDAN1J:55;
    A112: pion/.len pion = pion/.2 by FINSEQ_1:61
       .= do/.1 by A40,FINSEQ_4:26;
    A113: {pion1/.len pion1} c= L~do /\ L~pion1
    proof
     let x be set;
     assume x in {pion1/.len pion1};
     then x = pion1/.len pion1 by TARSKI:def 1;
     then x in rng do & x in rng pion1 by A79,A112,FINSEQ_6:46,REVROT_1:3;
     hence x in L~do /\ L~pion1 by A59,A107,XBOOLE_0:def 3;
    end;
         L~do /\ L~pion1 c= {pion1/.len pion1}
    proof
     let x be set;
     assume x in L~do /\ L~pion1;
     then x in L~do & x in L~pion1 by XBOOLE_0:def 3;
     then x in L~pion1 /\ L~LS by A51,XBOOLE_0:def 3;
     hence x in {pion1/.len pion1} by A4,A40,A77,A79,A112,SPPOL_2:21;
    end;
    then A114: L~do /\ L~pion1 = {pion1/.len pion1} by A113,XBOOLE_0:def 10;
    A115: L~(go^'pion1) /\ L~do = (L~go \/ L~pion1) /\ L~do by A87,TOPREAL8:35
       .= {go/.1} \/ {do/.1} by A66,A79,A112,A114,XBOOLE_1:23
       .= {(go^'pion1)/.1} \/ {do/.1} by GRAPH_2:57
       .= {(go^'pion1)/.1,do/.1} by ENUMSET1:41;
         do/.len do = (go^'pion1)/.1 by A58,GRAPH_2:57;
    then reconsider godo as non constant standard special_circular_sequence
          by A85,A89,A90,A95,A97,A105,A106,A110,A111,A115,JORDAN8:7,8
,TOPREAL8:11,33,34;
    A116: UA is_an_arc_of W-min C,E-max C by JORDAN6:def 8;
    then A117: UA is connected by JORDAN6:11;
    A118: W-min C in UA & E-max C in UA by A116,TOPREAL1:4;
    set ff = Rotate(Cage(C,n),Wmin);
         Wmin in rng Cage(C,n) by SPRECT_2:47;
    then A119: ff/.1 = Wmin by FINSEQ_6:98;
    A120: L~ff = L~Cage(C,n) by REVROT_1:33;
    then A121: (W-max L~ff)..ff > 1 by A119,SPRECT_5:23;
         (W-max L~ff)..ff <= (N-min L~ff)..ff by A119,A120,SPRECT_5:24;
    then A122: (N-min L~ff)..ff > 1 by A121,XXREAL_0:2;
         (N-min L~ff)..ff < (N-max L~ff)..ff by A119,A120,SPRECT_5:25;
    then A123: (N-max L~ff)..ff > 1 by A122,XXREAL_0:2;
         (N-max L~ff)..ff <= (E-max L~ff)..ff by A119,A120,SPRECT_5:26;
    then A124: Emax..ff > 1 by A120,A123,XXREAL_0:2;
    A125: now assume A126: Gik..US <= 1;
          Gik..US >= 1 by A32,FINSEQ_4:31;
     then Gik..US = 1 by A126,XXREAL_0:1;
     then Gik = US/.1 by A32,FINSEQ_5:41;
     hence contradiction by A17,A21,JORDAN1F:5;
    end;
    A127: Cage(C,n) is_sequence_on Ga by JORDAN9:def 1;
    then A128: ff is_sequence_on Ga by REVROT_1:34;
    A129: right_cell(godo,1,Ga)\L~godo c= RightComp godo by A85,A90,JORDAN9:29;
    A130: L~godo = L~(go^'pion1) \/ L~do by A89,TOPREAL8:35
       .= L~go \/ L~pion1 \/ L~do by A87,TOPREAL8:35;
         L~Cage(C,n) = L~US \/ L~LS by JORDAN1E:17;
    then A131: L~US c= L~Cage(C,n) & L~LS c= L~Cage(C,n) by XBOOLE_1:7;
    then A132: L~go c= L~Cage(C,n) & L~do c= L~Cage(C,n)
                                                     by A44,A51,XBOOLE_1:1;
    A133: W-min C in C by SPRECT_1:15;
    A134: L~pion = LSeg(Gik,Gij) by SPPOL_2:21;
    A135: now assume W-min C in L~godo;
     then A136: W-min C in L~go \/ L~pion1 or W-min C in L~do by A130,
XBOOLE_0:def 2;
     per cases by A136,XBOOLE_0:def 2;
      suppose W-min C in L~go;
       then C meets L~Cage(C,n) by A132,A133,XBOOLE_0:3;
       hence contradiction by JORDAN10:5;
      end;
      suppose W-min C in L~pion1;
       hence contradiction by A5,A77,A118,A134,XBOOLE_0:3;
      end;
      suppose W-min C in L~do;
       then C meets L~Cage(C,n) by A132,A133,XBOOLE_0:3;
       hence contradiction by JORDAN10:5;
    end;
    end;
         right_cell(Rotate(Cage(C,n),Wmin),1) =
          right_cell(ff,1,GoB ff) by A82,JORDAN1H:29
       .= right_cell(ff,1,GoB Cage(C,n)) by REVROT_1:28
       .= right_cell(ff,1,Ga) by JORDAN1H:52
       .= right_cell(ff-:Emax,1,Ga) by A124,A128,JORDAN1J:53
       .= right_cell(US,1,Ga) by JORDAN1E:def 1
       .= right_cell(R_Cut(US,Gik),1,Ga) by A32,A86,A125,JORDAN1J:52
       .= right_cell(go^'pion1,1,Ga) by A37,A88,JORDAN1J:51
       .= right_cell(godo,1,Ga) by A84,A90,JORDAN1J:51;
    then W-min C in right_cell(godo,1,Ga) by JORDAN1I:8;
    then A137: W-min C in right_cell(godo,1,Ga)\L~godo by A135,XBOOLE_0:def 4;
    A138: godo/.1 = (go^'pion1)/.1 by GRAPH_2:57
       .= Wmin by A57,GRAPH_2:57;
    A139: len US >= 2 by A16,XXREAL_0:2;
    A140: godo/.2 = (go^'pion1)/.2 by A83,GRAPH_2:61
       .= US/.2 by A31,A70,GRAPH_2:61
       .= (US^'LS)/.2 by A139,GRAPH_2:61
       .= Rotate(Cage(C,n),Wmin)/.2 by JORDAN1E:15;
    A141: L~go \/ L~do is compact by COMPTS_1:19;
    A142: L~go \/ L~do c= L~Cage(C,n) by A132,XBOOLE_1:8;
         Wmin in L~go \/ L~do by A59,A71,XBOOLE_0:def 2;
    then A143: W-min (L~go \/ L~do) = Wmin by A141,A142,JORDAN1J:21;
    A144: (W-min (L~go \/ L~do))`1 = W-bound (L~go \/ L~do) & Wmin`1 = Wbo
                                                              by EUCLID:56;
         W-bound LSeg(Gik,Gij) = Gik`1 by A74,SPRECT_1:62;
    then A145: W-bound L~pion1 = Gik`1 by A77,SPPOL_2:21;
         Gik`1 >= Wbo by A7,A131,PSCOMP_1:71;
    then Gik`1 > Wbo by A69,REAL_1:def 5;
    then W-min (L~go\/L~do\/L~pion1) = W-min (L~go \/ L~do)
                                              by A141,A143,A144,A145,
JORDAN1J:33;
    then A146: W-min L~godo = Wmin by A130,A143,XBOOLE_1:4;
    A147: rng godo c= L~godo by A85,SPPOL_2:18;
         2 in dom godo by A85,FINSEQ_3:27;
    then A148: godo/.2 in rng godo by PARTFUN2:4;
         godo/.2 in W-most L~Cage(C,n) by A140,JORDAN1I:27;
    then (godo/.2)`1 = (W-min L~godo)`1 by A146,PSCOMP_1:88
       .= W-bound L~godo by EUCLID:56;
    then godo/.2 in W-most L~godo by A147,A148,SPRECT_2:16;
    then Rotate(godo,W-min L~godo)/.2 in W-most L~godo by A138,A146,FINSEQ_6:95
;
    then reconsider godo as clockwise_oriented non constant standard
                                    special_circular_sequence by JORDAN1I:27;
         len US in dom US by FINSEQ_5:6;
    then A149: US.len US = US/.len US by PARTFUN1:def 8
       .= Emax by JORDAN1F:7;
    A150: E-max C in E-most C by PSCOMP_1:111;
    A151: east_halfline E-max C misses L~go
    proof
     assume east_halfline E-max C meets L~go;
     then consider p be set such that
      A152: p in east_halfline E-max C and
      A153: p in L~go by XBOOLE_0:3;
     reconsider p as Point of TOP-REAL 2 by A152;
          p in L~US by A44,A153;
     then p in east_halfline E-max C /\ L~Cage(C,n) by A131,A152,XBOOLE_0:def 3
;
     then A154: p`1 = Ebo by A150,JORDAN1A:104;
     then A155: p = Emax by A44,A153,JORDAN1J:46;
     then Emax = Gik by A7,A149,A153,JORDAN1J:43;
     then Gik`1 = Ga*(len Ga,k)`1 by A10,A15,A154,A155,JORDAN1A:92;
     hence contradiction by A1,A12,A28,JORDAN1G:7;
    end;
         now assume east_halfline E-max C meets L~godo;
     then A156: east_halfline E-max C meets (L~go \/ L~pion1) or
               east_halfline E-max C meets L~do by A130,XBOOLE_1:70;
     per cases by A156,XBOOLE_1:70;
      suppose east_halfline E-max C meets L~go;
       hence contradiction by A151;
      end;
      suppose east_halfline E-max C meets L~pion1;
       then consider p be set such that
        A157: p in east_halfline E-max C and
        A158: p in L~pion1 by XBOOLE_0:3;
       reconsider p as Point of TOP-REAL 2 by A157;
       A159: p`1 = Gik`1 by A74,A77,A134,A158,GOBOARD7:5;
            i+1 <= len Ga by A1,NAT_1:13;
       then i+1-1 <= len Ga-1 by XREAL_1:11;
       then A160: i <= len Ga-'1 by BINARITH:def 3;
            len Ga-'1 <= len Ga by BINARITH:52;
       then p`1 <= Ga*(len Ga-'1,1)`1 by A1,A10,A15,A19,A159,A160,JORDAN1A:39;
       then p`1 <= E-bound C by A19,JORDAN8:15;
       then A161: p`1 <= (E-max C)`1 by EUCLID:56;
            p`1 >= (E-max C)`1 by A157,TOPREAL1:def 13;
       then A162: p`1 = (E-max C)`1 by A161,XXREAL_0:1;
            p`2 = (E-max C)`2 by A157,TOPREAL1:def 13;
       then p = E-max C by A162,TOPREAL3:11;
       hence contradiction by A5,A77,A118,A134,A158,XBOOLE_0:3;
      end;
      suppose east_halfline E-max C meets L~do;
       then consider p be set such that
        A163: p in east_halfline E-max C and
        A164: p in L~do by XBOOLE_0:3;
       reconsider p as Point of TOP-REAL 2 by A163;
            p in L~LS by A51,A164;
       then p in east_halfline E-max C /\ L~Cage(C,n)
                                                  by A131,A163,XBOOLE_0:def 3;
       then A165: p`1 = Ebo by A150,JORDAN1A:104;
       A166: (E-max C)`2 = p`2 by A163,TOPREAL1:def 13;
       set RC = Rotate(Cage(C,n),Emax);
       A167: E-max C in right_cell(RC,1) by JORDAN1I:9;
       A168: 1+1 <= len LS by A22,XXREAL_0:2;
            LS = RC-:Wmin by JORDAN1G:26;
       then A169: LSeg(LS,1) = LSeg(RC,1) by A168,SPPOL_2:9;
       A170: L~RC = L~Cage(C,n) by REVROT_1:33;
       A171: len RC = len Cage(C,n) by REVROT_1:14;
       A172: GoB RC = GoB Cage(C,n) by REVROT_1:28
          .= Ga by JORDAN1H:52;
       A173: Emax in rng Cage(C,n) by SPRECT_2:50;
       A174: RC is_sequence_on Ga by A127,REVROT_1:34;
       A175: RC/.1 = E-max L~RC by A170,A173,FINSEQ_6:98;
       then consider ii,jj be Element of NAT such that
        A176: [ii,jj+1] in Indices Ga and
        A177: [ii,jj] in Indices Ga and
        A178: RC/.1 = Ga*(ii,jj+1) and
        A179: RC/.(1+1) = Ga*(ii,jj) by A81,A171,A174,JORDAN1I:25;
       consider jj2 be Element of NAT such that
        A180: 1 <= jj2 & jj2 <= width Ga and
        A181: Emax = Ga*(len Ga,jj2) by JORDAN1D:29;
       A182: len Ga >= 4 by JORDAN8:13;
       then len Ga >= 1 by XXREAL_0:2;
       then [len Ga,jj2] in Indices Ga by A180,GOBOARD7:10;
       then A183: ii = len Ga by A170,A175,A176,A178,A181,GOBOARD1:21;
       A184: 1 <= ii & ii <= len Ga & 1 <= jj+1 & jj+1 <= width Ga
                                                           by A176,GOBOARD5:1;
       A185: 1 <= ii & ii <= len Ga & 1 <= jj & jj <= width Ga
                                                           by A177,GOBOARD5:1;
       A186: ii+1 <> ii;
        jj+1+1 <> jj;
       then A187: right_cell(RC,1) = cell(Ga,ii-'1,jj)
                          by A81,A171,A172,A176,A177,A178,A179,A186,
GOBOARD5:def 6;
       A188: ii-'1+1 = ii by A184,BINARITH:53;
        ii-1 >= 4-1 by A182,A183,XREAL_1:11;
       then A189: ii-1 >= 1 by XXREAL_0:2;
       then A190: 1 <= ii-'1 by BINARITH:def 3;
       A191: Ga*(ii-'1,jj)`2 <= p`2 & p`2 <= Ga*(ii-'1,jj+1)`2
                                 by A166,A167,A184,A185,A187,A188,A189,
JORDAN9:19;
       A192: ii-'1 < len Ga by A184,A188,NAT_1:13;
       then A193: Ga*(ii-'1,jj)`2 = Ga*(1,jj)`2 by A185,A190,GOBOARD5:2
          .= Ga*(ii,jj)`2 by A185,GOBOARD5:2;
       A194: Ga*(ii-'1,jj+1)`2 = Ga*(1,jj+1)`2 by A184,A190,A192,GOBOARD5:2
          .= Ga*(ii,jj+1)`2 by A184,GOBOARD5:2;
            Ga*(len Ga,jj)`1 = Ebo & Ebo = Ga*(len Ga,jj+1)`1
                                                by A15,A184,A185,JORDAN1A:92;
       then p in LSeg(RC/.1,RC/.(1+1))
                              by A165,A178,A179,A183,A191,A193,A194,GOBOARD7:8;
       then A195: p in LSeg(LS,1) by A81,A169,A171,TOPREAL1:def 5;
       A196: p in LSeg(do,Index(p,do)) by A164,JORDAN3:42;
       A197: do = mid(LS,Gij..LS,len LS) by A35,JORDAN1J:37;
       A198: 1<=Gij..LS & Gij..LS<=len LS by A35,FINSEQ_4:31;
            Gij..LS <> len LS by A27,A35,FINSEQ_4:29;
       then A199: Gij..LS < len LS by A198,REAL_1:def 5;
       A200: 1<=Index(p,do) & Index(p,do) < len do by A164,JORDAN3:41;
       A201: Index(Gij,LS)+1 = Gij..LS by A30,A35,JORDAN1J:56;
       consider t be Nat such that
        A202: t in dom LS and
        A203: LS.t = Gij by A35,FINSEQ_2:11;
       A204: 1 <= t & t <= len LS by A202,FINSEQ_3:27;
       then 1 < t by A30,A203,REAL_1:def 5;
       then Index(Gij,LS)+1 = t by A203,A204,JORDAN3:45;
       then A205: len L_Cut(LS,Gij) = len LS-Index(Gij,LS)
                                                  by A6,A203,JORDAN3:61;
       set tt = Index(p,do)+(Gij..LS)-'1;
       A206: 1<=Index(Gij,LS) & 0+Index(Gij,LS) < len LS by A6,JORDAN3:41;
       then A207: len LS-Index(Gij,LS) > 0 by XREAL_1:22;
        Index(p,do) < len LS-'Index(Gij,LS) by A200,A205,BINARITH:def 3;
       then Index(p,do)+1 <= len LS-'Index(Gij,LS) by NAT_1:13;
       then Index(p,do) <= len LS-'Index(Gij,LS)-1 by XREAL_1:21;
       then Index(p,do) <= len LS-Index(Gij,LS)-1 by A207,BINARITH:def 3;
       then Index(p,do) <= len LS-Gij..LS by A201;
       then Index(p,do) <= len LS-'Gij..LS by BINARITH:def 3;
       then Index(p,do) < len LS-'(Gij..LS)+1 by NAT_1:13;
       then A208: LSeg(mid(LS,Gij..LS,len LS),Index(p,do)) =
            LSeg(LS,Index(p,do)+(Gij..LS)-'1) by A198,A199,A200,JORDAN4:31;
       A209: 1+1 <= Gij..LS by A201,A206,XREAL_1:9;
       then Index(p,do)+Gij..LS >= 1+1+1 by A200,XREAL_1:9;
       then A210: Index(p,do)+Gij..LS-1 >= 1+1+1-1 by XREAL_1:11;
       then A211: tt >= 1+1 by BINARITH:def 3;
       A212: 2 in dom LS by A168,FINSEQ_3:27;
            now per cases by A211,REAL_1:def 5;
        suppose tt > 1+1;
         then LSeg(LS,1) misses LSeg(LS,tt) by TOPREAL1:def 9;
         hence contradiction by A195,A196,A197,A208,XBOOLE_0:3;
        end;
        suppose A213: tt = 1+1;
         then LSeg(LS,1) /\ LSeg(LS,tt) = {LS/.2} by A22,TOPREAL1:def 8;
         then p in {LS/.2} by A195,A196,A197,A208,XBOOLE_0:def 3;
         then A214: p = LS/.2 by TARSKI:def 1;
         then A215: p..LS = 2 by A212,FINSEQ_5:44;
              1+1 = Index(p,do)+(Gij..LS)-1 by A210,A213,BINARITH:def 3;
         then 1+1+1 = Index(p,do)+(Gij..LS);
         then A216: Gij..LS = 2 by A200,A209,JORDAN1E:10;
              p in rng LS by A212,A214,PARTFUN2:4;
         then p = Gij by A35,A215,A216,FINSEQ_5:10;
         then Gij`1 = Ebo by A214,JORDAN1G:40;
         then Gij`1 = Ga*(len Ga,j)`1 by A9,A15,JORDAN1A:92;
         hence contradiction by A1,A11,A62,JORDAN1G:7;
       end;
       end;
       hence contradiction;
    end;
    end;
    then east_halfline E-max C c= (L~godo)` by SUBSET_1:43;
    then consider W be Subset of TOP-REAL 2 such that
     A217: W is_a_component_of (L~godo)` and
     A218: east_halfline E-max C c= W by GOBOARD9:5;
         east_halfline E-max C is not Bounded by JORDAN2C:129;
    then W is not Bounded by A218,JORDAN2C:16;
    then W is_outside_component_of L~godo by A217,JORDAN2C:def 4;
    then W c= UBD L~godo by JORDAN2C:27;
    then A219: east_halfline E-max C c= UBD L~godo by A218,XBOOLE_1:1;
         E-max C in east_halfline E-max C by TOPREAL1:45;
    then E-max C in UBD L~godo by A219;
    then E-max C in LeftComp godo by GOBRD14:46;
    then UA meets L~godo by A117,A118,A129,A137,JORDAN1J:36;
    then A220: UA meets (L~go \/ L~pion1) or UA meets L~do by A130,XBOOLE_1:70;
    A221: UA c= C by JORDAN6:76;
    per cases by A220,XBOOLE_1:70;
     suppose UA meets L~go;
      then UA meets L~Cage(C,n) by A132,XBOOLE_1:63;
      then C meets L~Cage(C,n) by A221,XBOOLE_1:63;
      hence contradiction by JORDAN10:5;
     end;
     suppose UA meets L~pion1;
      hence contradiction by A5,A77,A134;
     end;
     suppose UA meets L~do;
      then UA meets L~Cage(C,n) by A132,XBOOLE_1:63;
      then C meets L~Cage(C,n) by A221,XBOOLE_1:63;
      hence contradiction by JORDAN10:5;
   end;
   end;

  theorem Th14:
   for C be Simple_closed_curve
   for i,j,k be Element of NAT st
    1 < i & i < len Gauge(C,n) &
    1 <= k & k <= j & j <= width Gauge(C,n) &
    LSeg(Gauge(C,n)*(i,k),Gauge(C,n)*(i,j)) /\ L~Upper_Seq(C,n) =
     {Gauge(C,n)*(i,k)} &
    LSeg(Gauge(C,n)*(i,k),Gauge(C,n)*(i,j)) /\ L~Lower_Seq(C,n) =
     {Gauge(C,n)*(i,j)} holds
      LSeg(Gauge(C,n)*(i,k),Gauge(C,n)*(i,j)) meets Lower_Arc C
   proof
    let C be Simple_closed_curve;
    let i,j,k be Element of NAT;
    set Ga = Gauge(C,n);
    set US = Upper_Seq(C,n);
    set LS = Lower_Seq(C,n);
    set LA = Lower_Arc C;
    set Wmin = W-min L~Cage(C,n);
    set Emax = E-max L~Cage(C,n);
    set Wbo = W-bound L~Cage(C,n);
    set Ebo = E-bound L~Cage(C,n);
    set Gik = Ga*(i,k);
    set Gij = Ga*(i,j);
    assume that
     A1: 1 < i & i < len Ga and
     A2: 1 <= k & k <= j & j <= width Ga and
     A3: LSeg(Gik,Gij) /\ L~US = {Gik} and
     A4: LSeg(Gik,Gij) /\ L~LS = {Gij} and
     A5: LSeg(Gik,Gij) misses LA;
         Gij in {Gij} by TARSKI:def 1;
    then A6: Gij in L~LS by A4,XBOOLE_0:def 3;
         Gik in {Gik} by TARSKI:def 1;
    then A7: Gik in L~US by A3,XBOOLE_0:def 3;
    then A8: j <> k by A1,A2,A6,JORDAN1J:57;
    A9: 1 <= j & j <= width Ga by A2,XXREAL_0:2;
    A10: 1 <= k & k <= width Ga by A2,XXREAL_0:2;
    A11: [i,j] in Indices Ga by A1,A9,GOBOARD7:10;
    A12: [i,k] in Indices Ga by A1,A10,GOBOARD7:10;
    A13: LS is_sequence_on Ga by JORDAN1G:5;
    A14: US is_sequence_on Ga by JORDAN1G:4;
    set do = L_Cut(LS,Gij);
    set go = R_Cut(US,Gik);
    A15: len Ga = width Ga by JORDAN8:def 1;
    A16: len US >= 3 by JORDAN1E:19;
    then len US >= 1 by XXREAL_0:2;
    then 1 in dom US by FINSEQ_3:27;
    then A17: US.1 = US/.1 by PARTFUN1:def 8
       .= Wmin by JORDAN1F:5;
    A18: Wmin`1 = Wbo by EUCLID:56
       .= Ga*(1,k)`1 by A10,A15,JORDAN1A:94;
         len Ga >= 4 by JORDAN8:13;
    then A19: len Ga >= 1 by XXREAL_0:2;
    then A20: [1,k] in Indices Ga by A10,GOBOARD7:10;
    then A21: Gik <> US.1 by A1,A12,A17,A18,JORDAN1G:7;
    then reconsider go as being_S-Seq FinSequence of TOP-REAL 2
                                                            by A7,JORDAN3:70;
    A22: len LS >= 1+2 by JORDAN1E:19;
    then len LS >= 1 by XXREAL_0:2;
    then A23: 1 in dom LS & len LS in dom LS by FINSEQ_3:27;
    then A24: LS.len LS = LS/.len LS by PARTFUN1:def 8
       .= Wmin by JORDAN1F:8;
    A25: Wmin`1 = Wbo by EUCLID:56
       .= Ga*(1,k)`1 by A10,A15,JORDAN1A:94;
    A26: [i,j] in Indices Ga by A1,A9,GOBOARD7:10;
    then A27: Gij <> LS.len LS by A1,A20,A24,A25,JORDAN1G:7;
    then reconsider do as being_S-Seq FinSequence of TOP-REAL 2
                                                            by A6,JORDAN3:69;
    A28: [len Ga,k] in Indices Ga by A10,A19,GOBOARD7:10;
    A29: LS.1 = LS/.1 by A23,PARTFUN1:def 8
       .= Emax by JORDAN1F:6;
         Emax`1 = Ebo by EUCLID:56
       .= Ga*(len Ga,k)`1 by A10,A15,JORDAN1A:92;
    then A30: Gij <> LS.1 by A1,A26,A28,A29,JORDAN1G:7;
    A31: len go >= 1+1 by TOPREAL1:def 10;
    A32: Gik in rng US by A1,A7,A10,A14,JORDAN1J:40;
    then A33: go is_sequence_on Ga by A14,JORDAN1J:38;
    A34: len do >= 1+1 by TOPREAL1:def 10;
    A35: Gij in rng LS by A1,A6,A9,A13,JORDAN1J:40;
    then A36: do is_sequence_on Ga by A13,JORDAN1J:39;
    reconsider go as non constant s.c.c.
            (being_S-Seq FinSequence of TOP-REAL 2) by A31,A33,JGRAPH_1:16
,JORDAN8:8;
    reconsider do as non constant s.c.c.
            (being_S-Seq FinSequence of TOP-REAL 2) by A34,A36,JGRAPH_1:16
,JORDAN8:8;
    A37: len go > 1 by A31,NAT_1:13;
    then A38: len go in dom go by FINSEQ_3:27;
    then A39: go/.len go = go.len go by PARTFUN1:def 8
       .= Gik by A7,JORDAN3:59;
         len do >= 1 by A34,XXREAL_0:2;
    then 1 in dom do by FINSEQ_3:27;
    then A40: do/.1 = do.1 by PARTFUN1:def 8
       .= Gij by A6,JORDAN3:58;
    reconsider m = len go - 1 as Element of NAT by A38,FINSEQ_3:28;
    A41: m+1 = len go;
    then A42: len go-'1 = m by BINARITH:39;
    A43: LSeg(go,m) c= L~go by TOPREAL3:26;
    A44: L~go c= L~US by A7,JORDAN3:76;
    then LSeg(go,m) c= L~US by A43,XBOOLE_1:1;
    then A45: LSeg(go,m) /\ LSeg(Gik,Gij) c= {Gik} by A3,XBOOLE_1:26;
         m >= 1 by A31,XREAL_1:21;
    then A46: LSeg(go,m) = LSeg(go/.m,Gik) by A39,A41,TOPREAL1:def 5;
         {Gik} c= LSeg(go,m) /\ LSeg(Gik,Gij)
    proof
     let x be set;
     assume x in {Gik};
     then A47: x = Gik by TARSKI:def 1;
     A48: Gik in LSeg(go,m) by A46,TOPREAL1:6;
          Gik in LSeg(Gik,Gij) by TOPREAL1:6;
     hence x in LSeg(go,m) /\ LSeg(Gik,Gij) by A47,A48,XBOOLE_0:def 3;
    end;
    then A49: LSeg(go,m) /\ LSeg(Gik,Gij) = {Gik} by A45,XBOOLE_0:def 10;
    A50: LSeg(do,1) c= L~do by TOPREAL3:26;
    A51: L~do c= L~LS by A6,JORDAN3:77;
    then LSeg(do,1) c= L~LS by A50,XBOOLE_1:1;
    then A52: LSeg(do,1) /\ LSeg(Gik,Gij) c= {Gij} by A4,XBOOLE_1:26;
    A53: LSeg(do,1) = LSeg(Gij,do/.(1+1)) by A34,A40,TOPREAL1:def 5;
         {Gij} c= LSeg(do,1) /\ LSeg(Gik,Gij)
    proof
     let x be set;
     assume x in {Gij};
     then A54: x = Gij by TARSKI:def 1;
     A55: Gij in LSeg(do,1) by A53,TOPREAL1:6;
          Gij in LSeg(Gik,Gij) by TOPREAL1:6;
     hence x in LSeg(do,1) /\ LSeg(Gik,Gij) by A54,A55,XBOOLE_0:def 3;
    end;
    then A56: LSeg(Gik,Gij) /\ LSeg(do,1) = {Gij} by A52,XBOOLE_0:def 10;
    A57: go/.1 = US/.1 by A7,SPRECT_3:39
       .= Wmin by JORDAN1F:5;
    then A58: go/.1 = LS/.len LS by JORDAN1F:8
       .= do/.len do by A6,JORDAN1J:35;
    A59: rng go c= L~go & rng do c= L~do by A31,A34,SPPOL_2:18;
    A60: {go/.1} c= L~go /\ L~do
    proof
     let x be set;
     assume x in {go/.1};
     then x = go/.1 by TARSKI:def 1;
     then x in rng go & x in rng do by A58,FINSEQ_6:46,REVROT_1:3;
     hence x in L~go /\ L~do by A59,XBOOLE_0:def 3;
    end;
    A61: LS.1 = LS/.1 by A23,PARTFUN1:def 8
       .= Emax by JORDAN1F:6;
    A62: [len Ga,j] in Indices Ga by A9,A19,GOBOARD7:10;
         L~go /\ L~do c= {go/.1}
    proof
     let x be set;
     assume x in L~go /\ L~do;
     then A63: x in L~go & x in L~do by XBOOLE_0:def 3;
     then x in L~US /\ L~LS by A44,A51,XBOOLE_0:def 3;
     then x in {Wmin,Emax} by JORDAN1E:20;
     then A64: x = Wmin or x = Emax by TARSKI:def 2;
          now assume x = Emax;
      then A65: Emax = Gij by A6,A61,A63,JORDAN1E:11;
           Ga*(len Ga,j)`1 = Ebo by A9,A15,JORDAN1A:92;
      then Emax`1 <> Ebo by A1,A11,A62,A65,JORDAN1G:7;
      hence contradiction by EUCLID:56;
     end;
     hence x in {go/.1} by A57,A64,TARSKI:def 1;
    end;
    then A66: L~go /\ L~do = {go/.1} by A60,XBOOLE_0:def 10;
    set W2 = go/.2;
    A67: 2 in dom go by A31,FINSEQ_3:27;
    A68: Gik..US >= 1 by A32,FINSEQ_4:31;
    A69: now assume Gik`1 = Wbo;
     then Ga*(1,k)`1 = Ga*(i,k)`1 by A10,A15,JORDAN1A:94;
     hence contradiction by A1,A12,A20,JORDAN1G:7;
    end;
         go = mid(US,1,Gik..US) by A32,JORDAN1G:57
       .= US|(Gik..US) by A68,JORDAN3:25;
    then A70: W2 = US/.2 by A67,FINSEQ_4:85;
    A71: Wmin in rng go by A57,FINSEQ_6:46;
    set pion = <*Gik,Gij*>;
    A72: now let n be Element of NAT;
     assume n in dom pion;
     then n in Seg 2 by FINSEQ_3:29;
     then n = 1 or n = 2 by FINSEQ_1:4,TARSKI:def 2;
     then pion/.n = Gik or pion/.n = Gij by FINSEQ_4:26;
     hence ex i,j be Element of NAT st [i,j] in Indices Ga & pion/.n = Ga*(i,j)
                                                                  by A11,A12;
    end;
    A73: Gik <> Gij by A8,A11,A12,GOBOARD1:21;
    A74: Gik`1 = Ga*(i,1)`1 by A1,A10,GOBOARD5:3
       .= Gij`1 by A1,A9,GOBOARD5:3;
    then LSeg(Gik,Gij) is vertical by SPPOL_1:37;
    then pion is_S-Seq by A73,JORDAN1B:8;
    then consider pion1 be FinSequence of TOP-REAL 2 such that
     A75: pion1 is_sequence_on Ga and
     A76: pion1 is_S-Seq and
     A77: L~pion = L~pion1 and
     A78: pion/.1 = pion1/.1 and
     A79: pion/.len pion = pion1/.len pion1 and
     A80: len pion <= len pion1 by A72,GOBOARD3:2;
    reconsider pion1 as being_S-Seq FinSequence of TOP-REAL 2 by A76;
    set godo = go^'pion1^'do;
         len Cage(C,n) > 4 by GOBOARD7:36;
    then A81: 1+1 <= len Cage(C,n) by XXREAL_0:2;
    then A82: 1+1 <= len Rotate(Cage(C,n),Wmin) by REVROT_1:14;
         len (go^'pion1) >= len go by TOPREAL8:7;
    then A83: len (go^'pion1) >= 1+1 by A31,XXREAL_0:2;
    then A84: len (go^'pion1) > 1+0 by NAT_1:13;
         len godo >= len (go^'pion1) by TOPREAL8:7;
    then A85: 1+1 <= len godo by A83,XXREAL_0:2;
    A86: US is_sequence_on Ga by JORDAN1G:4;
    A87: go/.len go = pion1/.1 by A39,A78,FINSEQ_4:26;
    then A88: go^'pion1 is_sequence_on Ga by A33,A75,TOPREAL8:12;
    A89: (go^'pion1)/.len (go^'pion1) = pion/.len pion by A79,GRAPH_2:58
       .= pion/.2 by FINSEQ_1:61
       .= do/.1 by A40,FINSEQ_4:26;
    then A90: godo is_sequence_on Ga by A36,A88,TOPREAL8:12;
         LSeg(pion1,1) c= L~<*Gik,Gij*> by A77,TOPREAL3:26;
    then LSeg(pion1,1) c= LSeg(Gik,Gij) by SPPOL_2:21;
    then A91: LSeg(go,len go-'1) /\ LSeg(pion1,1) c= {Gik}
                                                      by A42,A49,XBOOLE_1:27;
    A92: len pion1 >= 1+1 by A80,FINSEQ_1:61;
         {Gik} c= LSeg(go,m) /\ LSeg(pion1,1)
    proof
     let x be set;
     assume x in {Gik};
     then A93: x = Gik by TARSKI:def 1;
     A94: Gik in LSeg(go,m) by A46,TOPREAL1:6;
          Gik in LSeg(pion1,1) by A39,A87,A92,TOPREAL1:27;
     hence x in LSeg(go,m) /\ LSeg(pion1,1) by A93,A94,XBOOLE_0:def 3;
    end;
    then LSeg(go,len go-'1) /\ LSeg(pion1,1) = { go/.len go }
                                            by A39,A42,A91,XBOOLE_0:def 10;
    then A95: go^'pion1 is unfolded by A87,TOPREAL8:34;
         len pion1 >= 2+0 by A80,FINSEQ_1:61;
    then A96: len pion1-2 >= 0 by XREAL_1:21;
         len (go^'pion1)+1-1 = len go+len pion1-1 by GRAPH_2:13;
    then len (go^'pion1)-1 = len go + (len pion1-2)
       .= len go + (len pion1-'2) by A96,BINARITH:def 3;
    then A97: len (go^'pion1)-'1 = len go + (len pion1-'2)
                                                      by BINARITH:def 3;
    A98: len pion1-1 >= 1 by A92,XREAL_1:21;
    then A99: len pion1-'1 = len pion1-1 by BINARITH:def 3;
    A100: len pion1-'2+1 = len pion1-2+1 by A96,BINARITH:def 3
       .= len pion1-'1 by A98,BINARITH:def 3;
         len pion1-1+1 <= len pion1;
    then A101: len pion1-'1 < len pion1 by A99,NAT_1:13;
         LSeg(pion1,len pion1-'1) c= L~<*Gik,Gij*> by A77,TOPREAL3:26;
    then LSeg(pion1,len pion1-'1) c= LSeg(Gik,Gij) by SPPOL_2:21;
    then A102: LSeg(pion1,len pion1-'1) /\ LSeg(do,1) c= {Gij}
                                                          by A56,XBOOLE_1:27;
         {Gij} c= LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
    proof
     let x be set;
     assume x in {Gij};
     then A103: x = Gij by TARSKI:def 1;
     A104: Gij in LSeg(do,1) by A53,TOPREAL1:6;
       pion1/.(len pion1-'1+1) = pion/.2 by A79,A99,FINSEQ_1:61
        .= Gij by FINSEQ_4:26;
     then Gij in LSeg(pion1,len pion1-'1) by A98,A99,TOPREAL1:27;
     hence x in LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
                                                   by A103,A104,XBOOLE_0:def 3;
    end;
    then LSeg(pion1,len pion1-'1) /\ LSeg(do,1) = {Gij}
                                                      by A102,XBOOLE_0:def 10;
    then A105: LSeg(go^'pion1,len go+(len pion1-'2)) /\ LSeg(do,1) =
       {(go^'pion1)/.len (go^'pion1)} by A40,A87,A89,A100,A101,TOPREAL8:31;
    A106: (go^'pion1) is non trivial by A83,REALSET1:13;
    A107: rng pion1 c= L~pion1 by A92,SPPOL_2:18;
    A108: {pion1/.1} c= L~go /\ L~pion1
    proof
     let x be set;
     assume x in {pion1/.1};
     then x = pion1/.1 by TARSKI:def 1;
     then x in rng go & x in rng pion1 by A87,FINSEQ_6:46,REVROT_1:3;
     hence x in L~go /\ L~pion1 by A59,A107,XBOOLE_0:def 3;
    end;
         L~go /\ L~pion1 c= {pion1/.1}
    proof
     let x be set;
     assume x in L~go /\ L~pion1;
     then x in L~go & x in L~pion1 by XBOOLE_0:def 3;
     then x in L~pion1 /\ L~US by A44,XBOOLE_0:def 3;
     hence x in {pion1/.1} by A3,A39,A77,A87,SPPOL_2:21;
    end;
    then A109: L~go /\ L~pion1 = {pion1/.1} by A108,XBOOLE_0:def 10;
    then A110: (go^'pion1) is s.n.c. by A87,JORDAN1J:54;
         rng go /\ rng pion1 c= {pion1/.1} by A59,A107,A109,XBOOLE_1:27;
    then A111: go^'pion1 is one-to-one by JORDAN1J:55;
    A112: pion/.len pion = pion/.2 by FINSEQ_1:61
       .= do/.1 by A40,FINSEQ_4:26;
    A113: {pion1/.len pion1} c= L~do /\ L~pion1
    proof
     let x be set;
     assume x in {pion1/.len pion1};
     then x = pion1/.len pion1 by TARSKI:def 1;
     then x in rng do & x in rng pion1 by A79,A112,FINSEQ_6:46,REVROT_1:3;
     hence x in L~do /\ L~pion1 by A59,A107,XBOOLE_0:def 3;
    end;
         L~do /\ L~pion1 c= {pion1/.len pion1}
    proof
     let x be set;
     assume x in L~do /\ L~pion1;
     then x in L~do & x in L~pion1 by XBOOLE_0:def 3;
     then x in L~pion1 /\ L~LS by A51,XBOOLE_0:def 3;
     hence x in {pion1/.len pion1} by A4,A40,A77,A79,A112,SPPOL_2:21;
    end;
    then A114: L~do /\ L~pion1 = {pion1/.len pion1} by A113,XBOOLE_0:def 10;
    A115: L~(go^'pion1) /\ L~do = (L~go \/ L~pion1) /\ L~do by A87,TOPREAL8:35
       .= {go/.1} \/ {do/.1} by A66,A79,A112,A114,XBOOLE_1:23
       .= {(go^'pion1)/.1} \/ {do/.1} by GRAPH_2:57
       .= {(go^'pion1)/.1,do/.1} by ENUMSET1:41;
         do/.len do = (go^'pion1)/.1 by A58,GRAPH_2:57;
    then reconsider godo as non constant standard special_circular_sequence
          by A85,A89,A90,A95,A97,A105,A106,A110,A111,A115,JORDAN8:7,8
,TOPREAL8:11,33,34;
    A116: LA is_an_arc_of E-max C,W-min C by JORDAN6:def 9;
    then A117: LA is connected by JORDAN6:11;
    A118: W-min C in LA & E-max C in LA by A116,TOPREAL1:4;
    set ff = Rotate(Cage(C,n),Wmin);
         Wmin in rng Cage(C,n) by SPRECT_2:47;
    then A119: ff/.1 = Wmin by FINSEQ_6:98;
    A120: L~ff = L~Cage(C,n) by REVROT_1:33;
    then A121: (W-max L~ff)..ff > 1 by A119,SPRECT_5:23;
         (W-max L~ff)..ff <= (N-min L~ff)..ff by A119,A120,SPRECT_5:24;
    then A122: (N-min L~ff)..ff > 1 by A121,XXREAL_0:2;
         (N-min L~ff)..ff < (N-max L~ff)..ff by A119,A120,SPRECT_5:25;
    then A123: (N-max L~ff)..ff > 1 by A122,XXREAL_0:2;
         (N-max L~ff)..ff <= (E-max L~ff)..ff by A119,A120,SPRECT_5:26;
    then A124: Emax..ff > 1 by A120,A123,XXREAL_0:2;
    A125: now assume A126: Gik..US <= 1;
          Gik..US >= 1 by A32,FINSEQ_4:31;
     then Gik..US = 1 by A126,XXREAL_0:1;
     then Gik = US/.1 by A32,FINSEQ_5:41;
     hence contradiction by A17,A21,JORDAN1F:5;
    end;
    A127: Cage(C,n) is_sequence_on Ga by JORDAN9:def 1;
    then A128: ff is_sequence_on Ga by REVROT_1:34;
    A129: right_cell(godo,1,Ga)\L~godo c= RightComp godo by A85,A90,JORDAN9:29;
    A130: L~godo = L~(go^'pion1) \/ L~do by A89,TOPREAL8:35
       .= L~go \/ L~pion1 \/ L~do by A87,TOPREAL8:35;
         L~Cage(C,n) = L~US \/ L~LS by JORDAN1E:17;
    then A131: L~US c= L~Cage(C,n) & L~LS c= L~Cage(C,n) by XBOOLE_1:7;
    then A132: L~go c= L~Cage(C,n) & L~do c= L~Cage(C,n)
                                                     by A44,A51,XBOOLE_1:1;
    A133: W-min C in C by SPRECT_1:15;
    A134: L~pion = LSeg(Gik,Gij) by SPPOL_2:21;
    A135: now assume W-min C in L~godo;
     then A136: W-min C in L~go \/ L~pion1 or W-min C in L~do by A130,
XBOOLE_0:def 2;
     per cases by A136,XBOOLE_0:def 2;
      suppose W-min C in L~go;
       then C meets L~Cage(C,n) by A132,A133,XBOOLE_0:3;
       hence contradiction by JORDAN10:5;
      end;
      suppose W-min C in L~pion1;
       hence contradiction by A5,A77,A118,A134,XBOOLE_0:3;
      end;
      suppose W-min C in L~do;
       then C meets L~Cage(C,n) by A132,A133,XBOOLE_0:3;
       hence contradiction by JORDAN10:5;
    end;
    end;
         right_cell(Rotate(Cage(C,n),Wmin),1) =
          right_cell(ff,1,GoB ff) by A82,JORDAN1H:29
       .= right_cell(ff,1,GoB Cage(C,n)) by REVROT_1:28
       .= right_cell(ff,1,Ga) by JORDAN1H:52
       .= right_cell(ff-:Emax,1,Ga) by A124,A128,JORDAN1J:53
       .= right_cell(US,1,Ga) by JORDAN1E:def 1
       .= right_cell(R_Cut(US,Gik),1,Ga) by A32,A86,A125,JORDAN1J:52
       .= right_cell(go^'pion1,1,Ga) by A37,A88,JORDAN1J:51
       .= right_cell(godo,1,Ga) by A84,A90,JORDAN1J:51;
    then W-min C in right_cell(godo,1,Ga) by JORDAN1I:8;
    then A137: W-min C in right_cell(godo,1,Ga)\L~godo by A135,XBOOLE_0:def 4;
    A138: godo/.1 = (go^'pion1)/.1 by GRAPH_2:57
       .= Wmin by A57,GRAPH_2:57;
    A139: len US >= 2 by A16,XXREAL_0:2;
    A140: godo/.2 = (go^'pion1)/.2 by A83,GRAPH_2:61
       .= US/.2 by A31,A70,GRAPH_2:61
       .= (US^'LS)/.2 by A139,GRAPH_2:61
       .= Rotate(Cage(C,n),Wmin)/.2 by JORDAN1E:15;
    A141: L~go \/ L~do is compact by COMPTS_1:19;
    A142: L~go \/ L~do c= L~Cage(C,n) by A132,XBOOLE_1:8;
         Wmin in L~go \/ L~do by A59,A71,XBOOLE_0:def 2;
    then A143: W-min (L~go \/ L~do) = Wmin by A141,A142,JORDAN1J:21;
    A144: (W-min (L~go \/ L~do))`1 = W-bound (L~go \/ L~do) & Wmin`1 = Wbo
                                                              by EUCLID:56;
         W-bound LSeg(Gik,Gij) = Gik`1 by A74,SPRECT_1:62;
    then A145: W-bound L~pion1 = Gik`1 by A77,SPPOL_2:21;
         Gik`1 >= Wbo by A7,A131,PSCOMP_1:71;
    then Gik`1 > Wbo by A69,REAL_1:def 5;
    then W-min (L~go\/L~do\/L~pion1) = W-min (L~go \/ L~do)
                                              by A141,A143,A144,A145,
JORDAN1J:33;
    then A146: W-min L~godo = Wmin by A130,A143,XBOOLE_1:4;
    A147: rng godo c= L~godo by A85,SPPOL_2:18;
         2 in dom godo by A85,FINSEQ_3:27;
    then A148: godo/.2 in rng godo by PARTFUN2:4;
         godo/.2 in W-most L~Cage(C,n) by A140,JORDAN1I:27;
    then (godo/.2)`1 = (W-min L~godo)`1 by A146,PSCOMP_1:88
       .= W-bound L~godo by EUCLID:56;
    then godo/.2 in W-most L~godo by A147,A148,SPRECT_2:16;
    then Rotate(godo,W-min L~godo)/.2 in W-most L~godo by A138,A146,FINSEQ_6:95
;
    then reconsider godo as clockwise_oriented non constant standard
                                    special_circular_sequence by JORDAN1I:27;
         len US in dom US by FINSEQ_5:6;
    then A149: US.len US = US/.len US by PARTFUN1:def 8
       .= Emax by JORDAN1F:7;
    A150: E-max C in E-most C by PSCOMP_1:111;
    A151: east_halfline E-max C misses L~go
    proof
     assume east_halfline E-max C meets L~go;
     then consider p be set such that
      A152: p in east_halfline E-max C and
      A153: p in L~go by XBOOLE_0:3;
     reconsider p as Point of TOP-REAL 2 by A152;
          p in L~US by A44,A153;
     then p in east_halfline E-max C /\ L~Cage(C,n) by A131,A152,XBOOLE_0:def 3
;
     then A154: p`1 = Ebo by A150,JORDAN1A:104;
     then A155: p = Emax by A44,A153,JORDAN1J:46;
     then Emax = Gik by A7,A149,A153,JORDAN1J:43;
     then Gik`1 = Ga*(len Ga,k)`1 by A10,A15,A154,A155,JORDAN1A:92;
     hence contradiction by A1,A12,A28,JORDAN1G:7;
    end;
         now assume east_halfline E-max C meets L~godo;
     then A156: east_halfline E-max C meets (L~go \/ L~pion1) or
               east_halfline E-max C meets L~do by A130,XBOOLE_1:70;
     per cases by A156,XBOOLE_1:70;
      suppose east_halfline E-max C meets L~go;
       hence contradiction by A151;
      end;
      suppose east_halfline E-max C meets L~pion1;
       then consider p be set such that
        A157: p in east_halfline E-max C and
        A158: p in L~pion1 by XBOOLE_0:3;
       reconsider p as Point of TOP-REAL 2 by A157;
       A159: p`1 = Gik`1 by A74,A77,A134,A158,GOBOARD7:5;
            i+1 <= len Ga by A1,NAT_1:13;
       then i+1-1 <= len Ga-1 by XREAL_1:11;
       then A160: i <= len Ga-'1 by BINARITH:def 3;
            len Ga-'1 <= len Ga by BINARITH:52;
       then p`1 <= Ga*(len Ga-'1,1)`1 by A1,A10,A15,A19,A159,A160,JORDAN1A:39;
       then p`1 <= E-bound C by A19,JORDAN8:15;
       then A161: p`1 <= (E-max C)`1 by EUCLID:56;
            p`1 >= (E-max C)`1 by A157,TOPREAL1:def 13;
       then A162: p`1 = (E-max C)`1 by A161,XXREAL_0:1;
            p`2 = (E-max C)`2 by A157,TOPREAL1:def 13;
       then p = E-max C by A162,TOPREAL3:11;
       hence contradiction by A5,A77,A118,A134,A158,XBOOLE_0:3;
      end;
      suppose east_halfline E-max C meets L~do;
       then consider p be set such that
        A163: p in east_halfline E-max C and
        A164: p in L~do by XBOOLE_0:3;
       reconsider p as Point of TOP-REAL 2 by A163;
            p in L~LS by A51,A164;
       then p in east_halfline E-max C /\ L~Cage(C,n)
                                                  by A131,A163,XBOOLE_0:def 3;
       then A165: p`1 = Ebo by A150,JORDAN1A:104;
       A166: (E-max C)`2 = p`2 by A163,TOPREAL1:def 13;
       set RC = Rotate(Cage(C,n),Emax);
       A167: E-max C in right_cell(RC,1) by JORDAN1I:9;
       A168: 1+1 <= len LS by A22,XXREAL_0:2;
            LS = RC-:Wmin by JORDAN1G:26;
       then A169: LSeg(LS,1) = LSeg(RC,1) by A168,SPPOL_2:9;
       A170: L~RC = L~Cage(C,n) by REVROT_1:33;
       A171: len RC = len Cage(C,n) by REVROT_1:14;
       A172: GoB RC = GoB Cage(C,n) by REVROT_1:28
          .= Ga by JORDAN1H:52;
       A173: Emax in rng Cage(C,n) by SPRECT_2:50;
       A174: RC is_sequence_on Ga by A127,REVROT_1:34;
       A175: RC/.1 = E-max L~RC by A170,A173,FINSEQ_6:98;
       then consider ii,jj be Element of NAT such that
        A176: [ii,jj+1] in Indices Ga and
        A177: [ii,jj] in Indices Ga and
        A178: RC/.1 = Ga*(ii,jj+1) and
        A179: RC/.(1+1) = Ga*(ii,jj) by A81,A171,A174,JORDAN1I:25;
       consider jj2 be Element of NAT such that
        A180: 1 <= jj2 & jj2 <= width Ga and
        A181: Emax = Ga*(len Ga,jj2) by JORDAN1D:29;
       A182: len Ga >= 4 by JORDAN8:13;
       then len Ga >= 1 by XXREAL_0:2;
       then [len Ga,jj2] in Indices Ga by A180,GOBOARD7:10;
       then A183: ii = len Ga by A170,A175,A176,A178,A181,GOBOARD1:21;
       A184: 1 <= ii & ii <= len Ga & 1 <= jj+1 & jj+1 <= width Ga
                                                           by A176,GOBOARD5:1;
       A185: 1 <= ii & ii <= len Ga & 1 <= jj & jj <= width Ga
                                                           by A177,GOBOARD5:1;
       A186: ii+1 <> ii;
        jj+1+1 <> jj;
       then A187: right_cell(RC,1) = cell(Ga,ii-'1,jj)
                          by A81,A171,A172,A176,A177,A178,A179,A186,
GOBOARD5:def 6;
       A188: ii-'1+1 = ii by A184,BINARITH:53;
        ii-1 >= 4-1 by A182,A183,XREAL_1:11;
       then A189: ii-1 >= 1 by XXREAL_0:2;
       then A190: 1 <= ii-'1 by BINARITH:def 3;
       A191: Ga*(ii-'1,jj)`2 <= p`2 & p`2 <= Ga*(ii-'1,jj+1)`2
                                 by A166,A167,A184,A185,A187,A188,A189,
JORDAN9:19;
       A192: ii-'1 < len Ga by A184,A188,NAT_1:13;
       then A193: Ga*(ii-'1,jj)`2 = Ga*(1,jj)`2 by A185,A190,GOBOARD5:2
          .= Ga*(ii,jj)`2 by A185,GOBOARD5:2;
       A194: Ga*(ii-'1,jj+1)`2 = Ga*(1,jj+1)`2 by A184,A190,A192,GOBOARD5:2
          .= Ga*(ii,jj+1)`2 by A184,GOBOARD5:2;
            Ga*(len Ga,jj)`1 = Ebo & Ebo = Ga*(len Ga,jj+1)`1
                                                by A15,A184,A185,JORDAN1A:92;
       then p in LSeg(RC/.1,RC/.(1+1))
                              by A165,A178,A179,A183,A191,A193,A194,GOBOARD7:8;
       then A195: p in LSeg(LS,1) by A81,A169,A171,TOPREAL1:def 5;
       A196: p in LSeg(do,Index(p,do)) by A164,JORDAN3:42;
       A197: do = mid(LS,Gij..LS,len LS) by A35,JORDAN1J:37;
       A198: 1<=Gij..LS & Gij..LS<=len LS by A35,FINSEQ_4:31;
            Gij..LS <> len LS by A27,A35,FINSEQ_4:29;
       then A199: Gij..LS < len LS by A198,REAL_1:def 5;
       A200: 1<=Index(p,do) & Index(p,do) < len do by A164,JORDAN3:41;
       A201: Index(Gij,LS)+1 = Gij..LS by A30,A35,JORDAN1J:56;
       consider t be Nat such that
        A202: t in dom LS and
        A203: LS.t = Gij by A35,FINSEQ_2:11;
       A204: 1 <= t & t <= len LS by A202,FINSEQ_3:27;
       then 1 < t by A30,A203,REAL_1:def 5;
       then Index(Gij,LS)+1 = t by A203,A204,JORDAN3:45;
       then A205: len L_Cut(LS,Gij) = len LS-Index(Gij,LS)
                                                  by A6,A203,JORDAN3:61;
       set tt = Index(p,do)+(Gij..LS)-'1;
       A206: 1<=Index(Gij,LS) & 0+Index(Gij,LS) < len LS by A6,JORDAN3:41;
       then A207: len LS-Index(Gij,LS) > 0 by XREAL_1:22;
        Index(p,do) < len LS-'Index(Gij,LS) by A200,A205,BINARITH:def 3;
       then Index(p,do)+1 <= len LS-'Index(Gij,LS) by NAT_1:13;
       then Index(p,do) <= len LS-'Index(Gij,LS)-1 by XREAL_1:21;
       then Index(p,do) <= len LS-Index(Gij,LS)-1 by A207,BINARITH:def 3;
       then Index(p,do) <= len LS-Gij..LS by A201;
       then Index(p,do) <= len LS-'Gij..LS by BINARITH:def 3;
       then Index(p,do) < len LS-'(Gij..LS)+1 by NAT_1:13;
       then A208: LSeg(mid(LS,Gij..LS,len LS),Index(p,do)) =
            LSeg(LS,Index(p,do)+(Gij..LS)-'1) by A198,A199,A200,JORDAN4:31;
       A209: 1+1 <= Gij..LS by A201,A206,XREAL_1:9;
       then Index(p,do)+Gij..LS >= 1+1+1 by A200,XREAL_1:9;
       then A210: Index(p,do)+Gij..LS-1 >= 1+1+1-1 by XREAL_1:11;
       then A211: tt >= 1+1 by BINARITH:def 3;
       A212: 2 in dom LS by A168,FINSEQ_3:27;
            now per cases by A211,REAL_1:def 5;
        suppose tt > 1+1;
         then LSeg(LS,1) misses LSeg(LS,tt) by TOPREAL1:def 9;
         hence contradiction by A195,A196,A197,A208,XBOOLE_0:3;
        end;
        suppose A213: tt = 1+1;
         then LSeg(LS,1) /\ LSeg(LS,tt) = {LS/.2} by A22,TOPREAL1:def 8;
         then p in {LS/.2} by A195,A196,A197,A208,XBOOLE_0:def 3;
         then A214: p = LS/.2 by TARSKI:def 1;
         then A215: p..LS = 2 by A212,FINSEQ_5:44;
              1+1 = Index(p,do)+(Gij..LS)-1 by A210,A213,BINARITH:def 3;
         then 1+1+1 = Index(p,do)+(Gij..LS);
         then A216: Gij..LS = 2 by A200,A209,JORDAN1E:10;
              p in rng LS by A212,A214,PARTFUN2:4;
         then p = Gij by A35,A215,A216,FINSEQ_5:10;
         then Gij`1 = Ebo by A214,JORDAN1G:40;
         then Gij`1 = Ga*(len Ga,j)`1 by A9,A15,JORDAN1A:92;
         hence contradiction by A1,A11,A62,JORDAN1G:7;
       end;
       end;
       hence contradiction;
    end;
    end;
    then east_halfline E-max C c= (L~godo)` by SUBSET_1:43;
    then consider W be Subset of TOP-REAL 2 such that
     A217: W is_a_component_of (L~godo)` and
     A218: east_halfline E-max C c= W by GOBOARD9:5;
         east_halfline E-max C is not Bounded by JORDAN2C:129;
    then W is not Bounded by A218,JORDAN2C:16;
    then W is_outside_component_of L~godo by A217,JORDAN2C:def 4;
    then W c= UBD L~godo by JORDAN2C:27;
    then A219: east_halfline E-max C c= UBD L~godo by A218,XBOOLE_1:1;
         E-max C in east_halfline E-max C by TOPREAL1:45;
    then E-max C in UBD L~godo by A219;
    then E-max C in LeftComp godo by GOBRD14:46;
    then LA meets L~godo by A117,A118,A129,A137,JORDAN1J:36;
    then A220: LA meets (L~go \/ L~pion1) or LA meets L~do by A130,XBOOLE_1:70;
    A221: LA c= C by JORDAN6:76;
    per cases by A220,XBOOLE_1:70;
     suppose LA meets L~go;
      then LA meets L~Cage(C,n) by A132,XBOOLE_1:63;
      then C meets L~Cage(C,n) by A221,XBOOLE_1:63;
      hence contradiction by JORDAN10:5;
     end;
     suppose LA meets L~pion1;
      hence contradiction by A5,A77,A134;
     end;
     suppose LA meets L~do;
      then LA meets L~Cage(C,n) by A132,XBOOLE_1:63;
      then C meets L~Cage(C,n) by A221,XBOOLE_1:63;
      hence contradiction by JORDAN10:5;
   end;
   end;

  theorem
        for C be Simple_closed_curve
   for i,j,k be Element of NAT st
    1 < i & i < len Gauge(C,n) &
    1 <= j & j <= k & k <= width Gauge(C,n) & n > 0 &
    LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Lower_Arc L~Cage(C,n) =
     {Gauge(C,n)*(i,k)} &
    LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Upper_Arc L~Cage(C,n) =
     {Gauge(C,n)*(i,j)} holds
     LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Upper_Arc C
   proof
    let C be Simple_closed_curve;
    let i,j,k be Element of NAT;
    assume that
     A1: 1 < i & i < len Gauge(C,n) and
     A2: 1 <= j & j <= k & k <= width Gauge(C,n) and
     A3: n > 0 and
     A4: LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Lower_Arc L~Cage(C,n) =
      {Gauge(C,n)*(i,k)} and
     A5: LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Upper_Arc L~Cage(C,n) =
      {Gauge(C,n)*(i,j)};
    A6: L~Lower_Seq(C,n) = Lower_Arc L~Cage(C,n) by A3,JORDAN1G:64;
         L~Upper_Seq(C,n) = Upper_Arc L~Cage(C,n) by A3,JORDAN1G:63;
    hence LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Upper_Arc C
                                        by A1,A2,A4,A5,A6,Th13;
   end;

  theorem
        for C be Simple_closed_curve
   for i,j,k be Element of NAT st
    1 < i & i < len Gauge(C,n) &
    1 <= j & j <= k & k <= width Gauge(C,n) & n > 0 &
    LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Lower_Arc L~Cage(C,n) =
     {Gauge(C,n)*(i,k)} &
    LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Upper_Arc L~Cage(C,n) =
     {Gauge(C,n)*(i,j)} holds
      LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Lower_Arc C
   proof
    let C be Simple_closed_curve;
    let i,j,k be Element of NAT;
    assume that
     A1: 1 < i & i < len Gauge(C,n) and
     A2: 1 <= j & j <= k & k <= width Gauge(C,n) and
     A3: n > 0 and
     A4: LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Lower_Arc L~Cage(C,n) =
      {Gauge(C,n)*(i,k)} and
     A5: LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Upper_Arc L~Cage(C,n) =
      {Gauge(C,n)*(i,j)};
    A6: L~Lower_Seq(C,n) = Lower_Arc L~Cage(C,n) by A3,JORDAN1G:64;
         L~Upper_Seq(C,n) = Upper_Arc L~Cage(C,n) by A3,JORDAN1G:63;
    hence LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Lower_Arc C
                                        by A1,A2,A4,A5,A6,Th14;
   end;

  theorem Th17:
   for C be Simple_closed_curve
   for i,j,k be Element of NAT st
    1 < i & i < len Gauge(C,n) &
    1 <= j & j <= k & k <= width Gauge(C,n) &
    Gauge(C,n)*(i,k) in L~Lower_Seq(C,n) &
    Gauge(C,n)*(i,j) in L~Upper_Seq(C,n) holds
     LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Upper_Arc C
   proof
    let C be Simple_closed_curve;
    let i,j,k be Element of NAT;
    assume that
     A1: 1 < i & i < len Gauge(C,n) and
     A2: 1 <= j & j <= k & k <= width Gauge(C,n) and
     A3: Gauge(C,n)*(i,k) in L~Lower_Seq(C,n) and
     A4: Gauge(C,n)*(i,j) in L~Upper_Seq(C,n);
    consider j1,k1 be Element of NAT such that
     A5: j <= j1 and
     A6: j1 <= k1 and
     A7: k1 <= k and
     A8: LSeg(Gauge(C,n)*(i,j1),Gauge(C,n)*(i,k1)) /\ L~Upper_Seq(C,n) =
      {Gauge(C,n)*(i,j1)} and
     A9: LSeg(Gauge(C,n)*(i,j1),Gauge(C,n)*(i,k1)) /\ L~Lower_Seq(C,n) =
      {Gauge(C,n)*(i,k1)} by A1,A2,A3,A4,JORDAN15:19;
         1 <= j1 & k1 <= width Gauge(C,n) by A2,A5,A7,XXREAL_0:2;
    then A10: LSeg(Gauge(C,n)*(i,j1),Gauge(C,n)*(i,k1)) meets Upper_Arc C
                                                    by A1,A6,A8,A9,Th13;
         LSeg(Gauge(C,n)*(i,j1),Gauge(C,n)*(i,k1)) c=
     LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) by A1,A2,A5,A6,A7,JORDAN15:7;
    hence LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Upper_Arc C
                                                          by A10,XBOOLE_1:63;
   end;

  theorem Th18:
   for C be Simple_closed_curve
   for i,j,k be Element of NAT st
    1 < i & i < len Gauge(C,n) &
    1 <= j & j <= k & k <= width Gauge(C,n) &
    Gauge(C,n)*(i,k) in L~Lower_Seq(C,n) &
    Gauge(C,n)*(i,j) in L~Upper_Seq(C,n) holds
     LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Lower_Arc C
   proof
    let C be Simple_closed_curve;
    let i,j,k be Element of NAT;
    assume that
     A1: 1 < i & i < len Gauge(C,n) and
     A2: 1 <= j & j <= k & k <= width Gauge(C,n) and
     A3: Gauge(C,n)*(i,k) in L~Lower_Seq(C,n) and
     A4: Gauge(C,n)*(i,j) in L~Upper_Seq(C,n);
    consider j1,k1 be Element of NAT such that
     A5: j <= j1 and
     A6: j1 <= k1 and
     A7: k1 <= k and
     A8: LSeg(Gauge(C,n)*(i,j1),Gauge(C,n)*(i,k1)) /\ L~Upper_Seq(C,n) =
      {Gauge(C,n)*(i,j1)} and
     A9: LSeg(Gauge(C,n)*(i,j1),Gauge(C,n)*(i,k1)) /\ L~Lower_Seq(C,n) =
      {Gauge(C,n)*(i,k1)} by A1,A2,A3,A4,JORDAN15:19;
         1 <= j1 & k1 <= width Gauge(C,n) by A2,A5,A7,XXREAL_0:2;
    then A10: LSeg(Gauge(C,n)*(i,j1),Gauge(C,n)*(i,k1)) meets Lower_Arc C
                                                    by A1,A6,A8,A9,Th14;
         LSeg(Gauge(C,n)*(i,j1),Gauge(C,n)*(i,k1)) c=
     LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) by A1,A2,A5,A6,A7,JORDAN15:7;
    hence LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Lower_Arc C
                                                          by A10,XBOOLE_1:63;
   end;

  theorem Th19:
   for C be Simple_closed_curve
   for i,j,k be Element of NAT st
    1 < i & i < len Gauge(C,n) &
    1 <= j & j <= k & k <= width Gauge(C,n) & n > 0 &
    Gauge(C,n)*(i,k) in Lower_Arc L~Cage(C,n) &
    Gauge(C,n)*(i,j) in Upper_Arc L~Cage(C,n) holds
     LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Upper_Arc C
   proof
    let C be Simple_closed_curve;
    let i,j,k be Element of NAT;
    assume that
     A1: 1 < i & i < len Gauge(C,n) and
     A2: 1 <= j & j <= k & k <= width Gauge(C,n) and
     A3: n > 0 and
     A4: Gauge(C,n)*(i,k) in Lower_Arc L~Cage(C,n) and
     A5: Gauge(C,n)*(i,j) in Upper_Arc L~Cage(C,n);
    A6: L~Lower_Seq(C,n) = Lower_Arc L~Cage(C,n) by A3,JORDAN1G:64;
         L~Upper_Seq(C,n) = Upper_Arc L~Cage(C,n) by A3,JORDAN1G:63;
    hence LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Upper_Arc C
                                                    by A1,A2,A4,A5,A6,Th17;
   end;

  theorem Th20:
   for C be Simple_closed_curve
   for i,j,k be Element of NAT st
    1 < i & i < len Gauge(C,n) &
    1 <= j & j <= k & k <= width Gauge(C,n) & n > 0 &
    Gauge(C,n)*(i,k) in Lower_Arc L~Cage(C,n) &
    Gauge(C,n)*(i,j) in Upper_Arc L~Cage(C,n) holds
     LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Lower_Arc C
   proof
    let C be Simple_closed_curve;
    let i,j,k be Element of NAT;
    assume that
     A1: 1 < i & i < len Gauge(C,n) and
     A2: 1 <= j & j <= k & k <= width Gauge(C,n) and
     A3: n > 0 and
     A4: Gauge(C,n)*(i,k) in Lower_Arc L~Cage(C,n) and
     A5: Gauge(C,n)*(i,j) in Upper_Arc L~Cage(C,n);
    A6: L~Lower_Seq(C,n) = Lower_Arc L~Cage(C,n) by A3,JORDAN1G:64;
         L~Upper_Seq(C,n) = Upper_Arc L~Cage(C,n) by A3,JORDAN1G:63;
    hence LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Lower_Arc C
                                                    by A1,A2,A4,A5,A6,Th18;
   end;

  theorem Th21:
   for C be Simple_closed_curve
   for i1,i2,j,k be Element of NAT st
    1 < i1 & i1 <= i2 & i2 < len Gauge(C,n) &
    1 <= j & j <= k & k <= width Gauge(C,n) &
    (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
     LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) /\ L~Upper_Seq(C,n) =
     {Gauge(C,n)*(i1,j)} &
    (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
     LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) /\ L~Lower_Seq(C,n) =
     {Gauge(C,n)*(i2,k)} holds
      (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
       LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) meets Upper_Arc C
   proof
    let C be Simple_closed_curve;
    let i1,i2,j,k be Element of NAT;
    set G = Gauge(C,n);
    set pio = LSeg(G*(i1,j),G*(i1,k));
    set poz = LSeg(G*(i1,k),G*(i2,k));
    set US = Upper_Seq(C,n);
    set LS = Lower_Seq(C,n);
    assume that
     A1: 1 < i1 & i1 <= i2 & i2 < len G and
     A2: 1 <= j & j <= k & k <= width G and
     A3: (pio \/ poz) /\ L~US = {G*(i1,j)} and
     A4: (pio \/ poz) /\ L~LS = {G*(i2,k)} and
     A5: (pio \/ poz) misses Upper_Arc C;
    set UA = Upper_Arc C;
    set Wmin = W-min L~Cage(C,n);
    set Emax = E-max L~Cage(C,n);
    set Wbo = W-bound L~Cage(C,n);
    set Ebo = E-bound L~Cage(C,n);
    set Gik = G*(i2,k);
    set Gij = G*(i1,j);
    set Gi1k = G*(i1,k);
    A6: 1 < i1 & i1 < len G & 1 < i2 & i2 < len G by A1,XXREAL_0:2;
    A7: L~<*Gij,Gi1k,Gik*> = poz \/ pio by TOPREAL3:23;
         Gik in {Gik} by TARSKI:def 1;
    then A8: Gik in L~LS by A4,XBOOLE_0:def 3;
         Gij in {Gij} by TARSKI:def 1;
    then A9: Gij in L~US by A3,XBOOLE_0:def 3;
    A10: 1 <= j & j <= width G by A2,XXREAL_0:2;
    A11: 1 <= k & k <= width G by A2,XXREAL_0:2;
    A12: [i1,j] in Indices G by A6,A10,GOBOARD7:10;
    A13: [i2,k] in Indices G by A6,A11,GOBOARD7:10;
    A14: [i1,k] in Indices G by A6,A11,GOBOARD7:10;
    A15: US is_sequence_on G by JORDAN1G:4;
    A16: LS is_sequence_on G by JORDAN1G:5;
    set go = R_Cut(US,Gij);
    set do = L_Cut(LS,Gik);
    A17: len G = width G by JORDAN8:def 1;
    A18: len US >= 3 by JORDAN1E:19;
    then len US >= 1 by XXREAL_0:2;
    then 1 in dom US by FINSEQ_3:27;
    then A19: US.1 = US/.1 by PARTFUN1:def 8
       .= Wmin by JORDAN1F:5;
    A20: Wmin`1 = Wbo by EUCLID:56
       .= G*(1,k)`1 by A11,A17,JORDAN1A:94;
         len G >= 4 by JORDAN8:13;
    then A21: len G >= 1 by XXREAL_0:2;
    then A22: [1,k] in Indices G by A11,GOBOARD7:10;
    then A23: Gij <> US.1 by A1,A12,A19,A20,JORDAN1G:7;
    then reconsider go as being_S-Seq FinSequence of TOP-REAL 2
                                                            by A9,JORDAN3:70;
    A24: [1,j] in Indices G by A10,A21,GOBOARD7:10;
    A25: len LS >= 1+2 by JORDAN1E:19;
    then len LS >= 1 by XXREAL_0:2;
    then A26: 1 in dom LS & len LS in dom LS by FINSEQ_3:27;
    then A27: LS.len LS = LS/.len LS by PARTFUN1:def 8
       .= Wmin by JORDAN1F:8;
         Wmin`1 = Wbo by EUCLID:56
       .= G*(1,k)`1 by A11,A17,JORDAN1A:94;
    then A28: Gik <> LS.len LS by A1,A13,A22,A27,JORDAN1G:7;
    then reconsider do as being_S-Seq FinSequence of TOP-REAL 2
                                                            by A8,JORDAN3:69;
    A29: [len G,k] in Indices G by A11,A21,GOBOARD7:10;
    A30: LS.1 = LS/.1 by A26,PARTFUN1:def 8
       .= Emax by JORDAN1F:6;
         Emax`1 = Ebo by EUCLID:56
       .= G*(len G,k)`1 by A11,A17,JORDAN1A:92;
    then A31: Gik <> LS.1 by A1,A13,A29,A30,JORDAN1G:7;
    A32: len go >= 1+1 by TOPREAL1:def 10;
    A33: Gij in rng US by A6,A9,A10,A15,JORDAN1J:40;
    then A34: go is_sequence_on G by A15,JORDAN1J:38;
    A35: len do >= 1+1 by TOPREAL1:def 10;
    A36: Gik in rng LS by A6,A8,A11,A16,JORDAN1J:40;
    then A37: do is_sequence_on G by A16,JORDAN1J:39;
    reconsider go as non constant s.c.c.
            (being_S-Seq FinSequence of TOP-REAL 2) by A32,A34,JGRAPH_1:16
,JORDAN8:8;
    reconsider do as non constant s.c.c.
            (being_S-Seq FinSequence of TOP-REAL 2) by A35,A37,JGRAPH_1:16
,JORDAN8:8;
    A38: len go > 1 by A32,NAT_1:13;
    then A39: len go in dom go by FINSEQ_3:27;
    then A40: go/.len go = go.len go by PARTFUN1:def 8
       .= Gij by A9,JORDAN3:59;
         len do >= 1 by A35,XXREAL_0:2;
    then 1 in dom do by FINSEQ_3:27;
    then A41: do/.1 = do.1 by PARTFUN1:def 8
       .= Gik by A8,JORDAN3:58;
    reconsider m = len go - 1 as Element of NAT by A39,FINSEQ_3:28;
    A42: m+1 = len go;
    then A43: len go-'1 = m by BINARITH:39;
    A44: LSeg(go,m) c= L~go by TOPREAL3:26;
    A45: L~go c= L~US by A9,JORDAN3:76;
    then LSeg(go,m) c= L~US by A44,XBOOLE_1:1;
    then A46: LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> c= {Gij} by A3,A7,XBOOLE_1:26;
         m >= 1 by A32,XREAL_1:21;
    then A47: LSeg(go,m) = LSeg(go/.m,Gij) by A40,A42,TOPREAL1:def 5;
         {Gij} c= LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*>
    proof
     let x be set;
     assume x in {Gij};
     then A48: x = Gij by TARSKI:def 1;
     A49: Gij in LSeg(go,m) by A47,TOPREAL1:6;
          Gij in LSeg(Gij,Gi1k) by TOPREAL1:6;
     then Gij in LSeg(Gij,Gi1k) \/ LSeg(Gi1k,Gik) by XBOOLE_0:def 2;
     then Gij in L~<*Gij,Gi1k,Gik*> by SPRECT_1:10;
     hence x in LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> by A48,A49,XBOOLE_0:def 3;
    end;
    then A50: LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> = {Gij} by A46,XBOOLE_0:def 10;
    A51: LSeg(do,1) c= L~do by TOPREAL3:26;
    A52: L~do c= L~LS by A8,JORDAN3:77;
    then LSeg(do,1) c= L~LS by A51,XBOOLE_1:1;
    then A53: LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*> c= {Gik} by A4,A7,XBOOLE_1:26;
    A54: LSeg(do,1) = LSeg(Gik,do/.(1+1)) by A35,A41,TOPREAL1:def 5;
         {Gik} c= LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*>
    proof
     let x be set;
     assume x in {Gik};
     then A55: x = Gik by TARSKI:def 1;
     A56: Gik in LSeg(do,1) by A54,TOPREAL1:6;
          Gik in LSeg(Gi1k,Gik) by TOPREAL1:6;
     then Gik in LSeg(Gij,Gi1k) \/ LSeg(Gi1k,Gik) by XBOOLE_0:def 2;
     then Gik in L~<*Gij,Gi1k,Gik*> by SPRECT_1:10;
     hence x in LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*> by A55,A56,XBOOLE_0:def 3;
    end;
    then A57: L~<*Gij,Gi1k,Gik*> /\ LSeg(do,1) = {Gik} by A53,XBOOLE_0:def 10;
    A58: go/.1 = US/.1 by A9,SPRECT_3:39
       .= Wmin by JORDAN1F:5;
    then A59: go/.1 = LS/.len LS by JORDAN1F:8
       .= do/.len do by A8,JORDAN1J:35;
    A60: rng go c= L~go & rng do c= L~do by A32,A35,SPPOL_2:18;
    A61: {go/.1} c= L~go /\ L~do
    proof
     let x be set;
     assume x in {go/.1};
     then x = go/.1 by TARSKI:def 1;
     then x in rng go & x in rng do by A59,FINSEQ_6:46,REVROT_1:3;
     hence x in L~go /\ L~do by A60,XBOOLE_0:def 3;
    end;
    A62: LS.1 = LS/.1 by A26,PARTFUN1:def 8
       .= Emax by JORDAN1F:6;
    A63: [len G,j] in Indices G by A10,A21,GOBOARD7:10;
         L~go /\ L~do c= {go/.1}
    proof
     let x be set;
     assume x in L~go /\ L~do;
     then A64: x in L~go & x in L~do by XBOOLE_0:def 3;
     then x in L~US /\ L~LS by A45,A52,XBOOLE_0:def 3;
     then x in {Wmin,Emax} by JORDAN1E:20;
     then A65: x = Wmin or x = Emax by TARSKI:def 2;
          now assume x = Emax;
      then A66: Emax = Gik by A8,A62,A64,JORDAN1E:11;
           G*(len G,j)`1 = Ebo by A10,A17,JORDAN1A:92;
      then Emax`1 <> Ebo by A1,A13,A63,A66,JORDAN1G:7;
      hence contradiction by EUCLID:56;
     end;
     hence x in {go/.1} by A58,A65,TARSKI:def 1;
    end;
    then A67: L~go /\ L~do = {go/.1} by A61,XBOOLE_0:def 10;
    set W2 = go/.2;
    A68: 2 in dom go by A32,FINSEQ_3:27;
    A69: Gij..US >= 1 by A33,FINSEQ_4:31;
    A70: now assume Gij`1 = Wbo;
     then G*(1,j)`1 = G*(i1,j)`1 by A10,A17,JORDAN1A:94;
     hence contradiction by A1,A12,A24,JORDAN1G:7;
    end;
         go = mid(US,1,Gij..US) by A33,JORDAN1G:57
       .= US|(Gij..US) by A69,JORDAN3:25;
    then A71: W2 = US/.2 by A68,FINSEQ_4:85;
    A72: Wmin in rng go by A58,FINSEQ_6:46;
    set pion = <*Gij,Gi1k,Gik*>;
    A73: now let n be Element of NAT;
     assume n in dom pion;
     then n in {1,2,3} by FINSEQ_3:1,30;
     then n = 1 or n = 2 or n = 3 by ENUMSET1:def 1;
     then pion/.n = Gij or pion/.n = Gi1k or pion/.n = Gik by FINSEQ_4:27
;
     hence ex i,j be Element of NAT st [i,j] in Indices G & pion/.n = G*(i,j)
                                                             by A12,A13,A14;
    end;
    A74: Gi1k`1 = G*(i1,1)`1 by A6,A11,GOBOARD5:3
       .= Gij`1 by A6,A10,GOBOARD5:3;
         Gi1k`2 =  G*(1,k)`2 by A6,A11,GOBOARD5:2
       .= Gik`2 by A6,A11,GOBOARD5:2;
    then A75: Gi1k = |[Gij`1,Gik`2]| by A74,EUCLID:57;
    A76: Gi1k in pio by TOPREAL1:6;
    A77: Gi1k in poz by TOPREAL1:6;
         now per cases;
     suppose Gik`1 <> Gij`1 & Gik`2 <> Gij`2;
      then pion is_S-Seq by A75,TOPREAL3:41;
      then consider pion1 be FinSequence of TOP-REAL 2 such that
       A78: pion1 is_sequence_on G and
       A79: pion1 is_S-Seq and
       A80: L~pion = L~pion1 and
       A81: pion/.1 = pion1/.1 and
       A82: pion/.len pion = pion1/.len pion1 and
       A83: len pion <= len pion1 by A73,GOBOARD3:2;
      reconsider pion1 as being_S-Seq FinSequence of TOP-REAL 2 by A79;
      set godo = go^'pion1^'do;
      A84: Gi1k`1 = G*(i1,1)`1 by A6,A11,GOBOARD5:3
         .= Gij`1 by A6,A10,GOBOARD5:3;
      A85: Gi1k`1 <= Gik`1 by A1,A11,JORDAN1A:39;
      then A86: W-bound poz = Gi1k`1 by SPRECT_1:62;
      A87: W-bound pio = Gij`1 by A84,SPRECT_1:62;
           W-bound (poz \/ pio) = min(W-bound poz, W-bound pio) by SPRECT_1:54
         .= Gij`1 by A84,A86,A87;
      then A88: W-bound L~pion1 = Gij`1 by A80,TOPREAL3:23;
           len Cage(C,n) > 4 by GOBOARD7:36;
      then A89: 1+1 <= len Cage(C,n) by XXREAL_0:2;
      then A90: 1+1 <= len Rotate(Cage(C,n),Wmin) by REVROT_1:14;
           len (go^'pion1) >= len go by TOPREAL8:7;
      then A91: len (go^'pion1) >= 1+1 by A32,XXREAL_0:2;
      then A92: len (go^'pion1) > 1+0 by NAT_1:13;
           len godo >= len (go^'pion1) by TOPREAL8:7;
      then A93: 1+1 <= len godo by A91,XXREAL_0:2;
      A94: US is_sequence_on G by JORDAN1G:4;
      A95: go/.len go = pion1/.1 by A40,A81,FINSEQ_4:27;
      then A96: go^'pion1 is_sequence_on G by A34,A78,TOPREAL8:12;
      A97: (go^'pion1)/.len (go^'pion1) = pion/.len pion by A82,GRAPH_2:58
         .= pion/.3 by FINSEQ_1:62
         .= do/.1 by A41,FINSEQ_4:27;
      then A98: godo is_sequence_on G by A37,A96,TOPREAL8:12;
           LSeg(pion1,1) c= L~pion by A80,TOPREAL3:26;
      then A99: LSeg(go,len go-'1) /\ LSeg(pion1,1) c={Gij}
                                                      by A43,A50,XBOOLE_1:27;
           len pion1 >= 2+1 by A83,FINSEQ_1:62;
      then A100: len pion1 > 1+1 by NAT_1:13;
           {Gij} c= LSeg(go,m) /\ LSeg(pion1,1)
      proof
       let x be set;
       assume x in {Gij};
       then A101: x = Gij by TARSKI:def 1;
       A102: Gij in LSeg(go,m) by A47,TOPREAL1:6;
            Gij in LSeg(pion1,1) by A40,A95,A100,TOPREAL1:27;
       hence x in LSeg(go,m) /\ LSeg(pion1,1) by A101,A102,XBOOLE_0:def 3;
      end;
      then LSeg(go,len go-'1) /\ LSeg(pion1,1) = { go/.len go }
                                              by A40,A43,A99,XBOOLE_0:def 10;
      then A103: go^'pion1 is unfolded by A95,TOPREAL8:34;
           len pion1 >= 2+1 by A83,FINSEQ_1:62;
      then A104: len pion1-2 >= 0 by XREAL_1:21;
           len (go^'pion1)+1-1 = len go+len pion1-1 by GRAPH_2:13;
      then len (go^'pion1)-1 = len go + (len pion1-2)
         .= len go + (len pion1-'2) by A104,BINARITH:def 3;
      then A105: len (go^'pion1)-'1 = len go + (len pion1-'2)
                                                      by BINARITH:def 3;
      A106: len pion1-1 >= 1 by A100,XREAL_1:21;
      then A107: len pion1-'1 = len pion1-1 by BINARITH:def 3;
      A108: len pion1-'2+1 = len pion1-2+1 by A104,BINARITH:def 3
         .= len pion1-'1 by A106,BINARITH:def 3;
           len pion1-1+1 <= len pion1;
      then A109: len pion1-'1 < len pion1 by A107,NAT_1:13;
           LSeg(pion1,len pion1-'1) c= L~pion by A80,TOPREAL3:26;
      then A110: LSeg(pion1,len pion1-'1) /\ LSeg(do,1) c= {Gik}
                                                          by A57,XBOOLE_1:27;
           {Gik} c= LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
      proof
       let x be set;
       assume x in {Gik};
       then A111: x = Gik by TARSKI:def 1;
       A112: Gik in LSeg(do,1) by A54,TOPREAL1:6;
         pion1/.(len pion1-'1+1) = pion/.3 by A82,A107,FINSEQ_1:62
          .= Gik by FINSEQ_4:27;
       then Gik in LSeg(pion1,len pion1-'1) by A106,A107,TOPREAL1:27
;
       hence x in LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
                                                   by A111,A112,XBOOLE_0:def 3;
      end;
      then LSeg(pion1,len pion1-'1) /\ LSeg(do,1) = {Gik}
                                                     by A110,XBOOLE_0:def 10;
      then A113: LSeg(go^'pion1,len go+(len pion1-'2)) /\ LSeg(do,1) =
         {(go^'pion1)/.len (go^'pion1)} by A41,A95,A97,A108,A109,TOPREAL8:31;
      A114: (go^'pion1) is non trivial by A91,REALSET1:13;
      A115: rng pion1 c= L~pion1 by A100,SPPOL_2:18;
      A116: {pion1/.1} c= L~go /\ L~pion1
      proof
       let x be set;
       assume x in {pion1/.1};
       then x = pion1/.1 by TARSKI:def 1;
       then x in rng go & x in rng pion1 by A95,FINSEQ_6:46,REVROT_1:3;
       hence x in L~go /\ L~pion1 by A60,A115,XBOOLE_0:def 3;
      end;
           L~go /\ L~pion1 c= {pion1/.1}
      proof
       let x be set;
       assume x in L~go /\ L~pion1;
       then x in L~go & x in L~pion1 by XBOOLE_0:def 3;
       hence x in {pion1/.1} by A3,A7,A40,A45,A80,A95,XBOOLE_0:def 3;
      end;
      then A117: L~go /\ L~pion1 = {pion1/.1} by A116,XBOOLE_0:def 10;
      then A118: (go^'pion1) is s.n.c. by A95,JORDAN1J:54;
           rng go /\ rng pion1 c= {pion1/.1} by A60,A115,A117,XBOOLE_1:27;
      then A119: go^'pion1 is one-to-one by JORDAN1J:55;
      A120: pion/.len pion = pion/.3 by FINSEQ_1:62
         .= do/.1 by A41,FINSEQ_4:27;
      A121: {pion1/.len pion1} c= L~do /\ L~pion1
      proof
       let x be set;
       assume x in {pion1/.len pion1};
       then x = pion1/.len pion1 by TARSKI:def 1;
       then x in rng do & x in rng pion1 by A82,A120,FINSEQ_6:46,REVROT_1:3;
       hence x in L~do /\ L~pion1 by A60,A115,XBOOLE_0:def 3;
      end;
           L~do /\ L~pion1 c= {pion1/.len pion1}
      proof
       let x be set;
       assume x in L~do /\ L~pion1;
       then x in L~do & x in L~pion1 by XBOOLE_0:def 3;
       hence x in {pion1/.len pion1}
          by A4,A7,A41,A52,A80,A82,A120,XBOOLE_0:def 3;
      end;
      then A122: L~do /\ L~pion1 = {pion1/.len pion1} by A121,XBOOLE_0:def 10;
      A123: L~(go^'pion1) /\ L~do = (L~go \/ L~pion1) /\ L~do by A95,
TOPREAL8:35
         .= {go/.1} \/ {do/.1} by A67,A82,A120,A122,XBOOLE_1:23
         .= {(go^'pion1)/.1} \/ {do/.1} by GRAPH_2:57
         .= {(go^'pion1)/.1,do/.1} by ENUMSET1:41;
           do/.len do = (go^'pion1)/.1 by A59,GRAPH_2:57;
      then reconsider godo as non constant standard special_circular_sequence
          by A93,A97,A98,A103,A105,A113,A114,A118,A119,A123,JORDAN8:7,8
,TOPREAL8:11,33,34
;
      A124: UA is_an_arc_of W-min C,E-max C by JORDAN6:def 8;
      then A125: UA is connected by JORDAN6:11;
      A126: W-min C in UA & E-max C in UA by A124,TOPREAL1:4;
      set ff = Rotate(Cage(C,n),Wmin);
           Wmin in rng Cage(C,n) by SPRECT_2:47;
      then A127: ff/.1 = Wmin by FINSEQ_6:98;
      A128: L~ff = L~Cage(C,n) by REVROT_1:33;
      then A129: (W-max L~ff)..ff > 1 by A127,SPRECT_5:23;
           (W-max L~ff)..ff <= (N-min L~ff)..ff by A127,A128,SPRECT_5:24;
      then A130: (N-min L~ff)..ff > 1 by A129,XXREAL_0:2;
           (N-min L~ff)..ff < (N-max L~ff)..ff by A127,A128,SPRECT_5:25;
      then A131: (N-max L~ff)..ff > 1 by A130,XXREAL_0:2;
           (N-max L~ff)..ff <= (E-max L~ff)..ff by A127,A128,SPRECT_5:26;
      then A132: Emax..ff > 1 by A128,A131,XXREAL_0:2;
      A133: now assume A134: Gij..US <= 1;
            Gij..US >= 1 by A33,FINSEQ_4:31;
       then Gij..US = 1 by A134,XXREAL_0:1;
       then Gij = US/.1 by A33,FINSEQ_5:41;
       hence contradiction by A19,A23,JORDAN1F:5;
      end;
      A135: Cage(C,n) is_sequence_on G by JORDAN9:def 1;
      then A136: ff is_sequence_on G by REVROT_1:34;
      A137: right_cell(godo,1,G)\L~godo c= RightComp godo by A93,A98,JORDAN9:29
;
      A138: L~godo = L~(go^'pion1) \/ L~do by A97,TOPREAL8:35
         .= L~go \/ L~pion1 \/ L~do by A95,TOPREAL8:35;
           L~Cage(C,n) = L~US \/ L~LS by JORDAN1E:17;
      then A139: L~US c= L~Cage(C,n) & L~LS c= L~Cage(C,n) by XBOOLE_1:7;
      then A140: L~go c=L~Cage(C,n) & L~do c=L~Cage(C,n)
                                                     by A45,A52,XBOOLE_1:1;
      A141: W-min C in C by SPRECT_1:15;
      A142: now assume W-min C in L~godo;
       then A143: W-min C in L~go \/ L~pion1 or W-min C in L~do
                                                       by A138,XBOOLE_0:def 2;
       per cases by A143,XBOOLE_0:def 2;
        suppose W-min C in L~go;
         then C meets L~Cage(C,n) by A140,A141,XBOOLE_0:3;
         hence contradiction by JORDAN10:5;
        end;
        suppose W-min C in L~pion1;
         hence contradiction by A5,A7,A80,A126,XBOOLE_0:3;
        end;
        suppose W-min C in L~do;
         then C meets L~Cage(C,n) by A140,A141,XBOOLE_0:3;
         hence contradiction by JORDAN10:5;
      end;
      end;
           right_cell(Rotate(Cage(C,n),Wmin),1) =
            right_cell(ff,1,GoB ff) by A90,JORDAN1H:29
         .= right_cell(ff,1,GoB Cage(C,n)) by REVROT_1:28
         .= right_cell(ff,1,G) by JORDAN1H:52
         .= right_cell(ff-:Emax,1,G) by A132,A136,JORDAN1J:53
         .= right_cell(US,1,G) by JORDAN1E:def 1
         .= right_cell(R_Cut(US,Gij),1,G) by A33,A94,A133,JORDAN1J:52
         .= right_cell(go^'pion1,1,G) by A38,A96,JORDAN1J:51
         .= right_cell(godo,1,G) by A92,A98,JORDAN1J:51;
      then W-min C in right_cell(godo,1,G) by JORDAN1I:8;
      then A144: W-min C in right_cell(godo,1,G)\L~godo by A142,XBOOLE_0:def 4;
      A145: godo/.1 = (go^'pion1)/.1 by GRAPH_2:57
         .= Wmin by A58,GRAPH_2:57;
      A146: len US >= 2 by A18,XXREAL_0:2;
      A147: godo/.2 = (go^'pion1)/.2 by A91,GRAPH_2:61
         .= US/.2 by A32,A71,GRAPH_2:61
         .= (US^'LS)/.2 by A146,GRAPH_2:61
         .= Rotate(Cage(C,n),Wmin)/.2 by JORDAN1E:15;
      A148: L~go \/ L~do is compact by COMPTS_1:19;
      A149: L~go \/ L~do c= L~Cage(C,n) by A140,XBOOLE_1:8;
           Wmin in L~go \/ L~do by A60,A72,XBOOLE_0:def 2;
      then A150: W-min (L~go \/ L~do) = Wmin by A148,A149,JORDAN1J:21;
      A151: (W-min (L~go \/ L~do))`1 = W-bound (L~go \/ L~do) & Wmin`1 = Wbo
                                                              by EUCLID:56;
           Gij`1 >= Wbo by A9,A139,PSCOMP_1:71;
      then Gij`1 > Wbo by A70,REAL_1:def 5;
      then W-min (L~go\/L~do\/L~pion1) = W-min (L~go \/ L~do)
                                              by A88,A148,A150,A151,JORDAN1J:33
;
      then A152: W-min L~godo = Wmin by A138,A150,XBOOLE_1:4;
      A153: rng godo c= L~godo by A93,SPPOL_2:18;
           2 in dom godo by A93,FINSEQ_3:27;
      then A154: godo/.2 in rng godo by PARTFUN2:4;
           godo/.2 in W-most L~Cage(C,n) by A147,JORDAN1I:27;
      then (godo/.2)`1 = (W-min L~godo)`1 by A152,PSCOMP_1:88
         .= W-bound L~godo by EUCLID:56;
      then godo/.2 in W-most L~godo by A153,A154,SPRECT_2:16;
      then Rotate(godo,W-min L~godo)/.2 in W-most L~godo
                                                      by A145,A152,FINSEQ_6:95;
      then reconsider godo as clockwise_oriented non constant standard
                                    special_circular_sequence by JORDAN1I:27;
           len US in dom US by FINSEQ_5:6;
      then A155: US.len US = US/.len US by PARTFUN1:def 8
         .= Emax by JORDAN1F:7;
      A156: E-max C in E-most C by PSCOMP_1:111;
      A157: east_halfline E-max C misses L~go
      proof
       assume east_halfline E-max C meets L~go;
       then consider p be set such that
        A158: p in east_halfline E-max C and
        A159: p in L~go by XBOOLE_0:3;
       reconsider p as Point of TOP-REAL 2 by A158;
            p in L~US by A45,A159;
       then p in east_halfline E-max C /\ L~Cage(C,n)
                    by A139,A158,XBOOLE_0:def 3;
       then A160: p`1 = Ebo by A156,JORDAN1A:104;
       then A161: p = Emax by A45,A159,JORDAN1J:46;
       then Emax = Gij by A9,A155,A159,JORDAN1J:43;
       then Gij`1 = G*(len G,k)`1 by A11,A17,A160,A161,JORDAN1A:92;
       hence contradiction by A1,A12,A29,JORDAN1G:7;
      end;
           now assume east_halfline E-max C meets L~godo;
       then A162: east_halfline E-max C meets (L~go \/ L~pion1) or
                 east_halfline E-max C meets L~do by A138,XBOOLE_1:70;
       per cases by A162,XBOOLE_1:70;
        suppose east_halfline E-max C meets L~go;
         hence contradiction by A157;
        end;
        suppose east_halfline E-max C meets L~pion1;
         then consider p be set such that
          A163: p in east_halfline E-max C and
          A164: p in L~pion1 by XBOOLE_0:3;
         reconsider p as Point of TOP-REAL 2 by A163;
         A165: now per cases by A7,A80,A164,XBOOLE_0:def 2;
          suppose p in poz;
           hence p`1 <= Gik`1 by A85,TOPREAL1:9;
          end;
          suppose p in pio;
           hence p`1 <= Gik`1 by A84,A85,GOBOARD7:5;
         end;
         end;
              i2+1 <= len G by A1,NAT_1:13;
         then i2 <= len G-1 by XREAL_1:21;
         then A166: i2 <= len G-'1 by BINARITH:def 3;
              len G-'1 <= len G by BINARITH:52;
         then Gik`1 <= G*(len G-'1,1)`1 by A6,A11,A17,A21,A166,JORDAN1A:39;
         then p`1 <= G*(len G-'1,1)`1 by A165,XXREAL_0:2;
         then p`1 <= E-bound C by A21,JORDAN8:15;
         then A167: p`1 <= (E-max C)`1 by EUCLID:56;
              p`1 >= (E-max C)`1 by A163,TOPREAL1:def 13;
         then A168: p`1 = (E-max C)`1 by A167,XXREAL_0:1;
              p`2 = (E-max C)`2 by A163,TOPREAL1:def 13;
         then p = E-max C by A168,TOPREAL3:11;
         hence contradiction by A5,A7,A80,A126,A164,XBOOLE_0:3;
        end;
        suppose east_halfline E-max C meets L~do;
         then consider p be set such that
          A169: p in east_halfline E-max C and
          A170: p in L~do by XBOOLE_0:3;
         reconsider p as Point of TOP-REAL 2 by A169;
              p in L~LS by A52,A170;
         then p in east_halfline E-max C /\ L~Cage(C,n)
                      by A139,A169,XBOOLE_0:def 3;
         then A171: p`1 = Ebo by A156,JORDAN1A:104;
         A172: (E-max C)`2 = p`2 by A169,TOPREAL1:def 13;
         set RC = Rotate(Cage(C,n),Emax);
         A173: E-max C in right_cell(RC,1) by JORDAN1I:9;
         A174: 1+1 <= len LS by A25,XXREAL_0:2;
              LS = RC-:Wmin by JORDAN1G:26;
         then A175: LSeg(LS,1) = LSeg(RC,1) by A174,SPPOL_2:9;
         A176: L~RC = L~Cage(C,n) by REVROT_1:33;
         A177: len RC = len Cage(C,n) by REVROT_1:14;
         A178: GoB RC = GoB Cage(C,n) by REVROT_1:28
            .= G by JORDAN1H:52;
         A179: Emax in rng Cage(C,n) by SPRECT_2:50;
         A180: RC is_sequence_on G by A135,REVROT_1:34;
         A181: RC/.1 = E-max L~RC by A176,A179,FINSEQ_6:98;
         then consider ii,jj be Element of NAT such that
          A182: [ii,jj+1] in Indices G and
          A183: [ii,jj] in Indices G and
          A184: RC/.1 = G*(ii,jj+1) and
          A185: RC/.(1+1) = G*(ii,jj) by A89,A177,A180,JORDAN1I:25;
         consider jj2 be Element of NAT such that
          A186: 1 <= jj2 & jj2 <= width G and
          A187: Emax = G*(len G,jj2) by JORDAN1D:29;
         A188: len G >= 4 by JORDAN8:13;
         then len G >= 1 by XXREAL_0:2;
         then [len G,jj2] in Indices G by A186,GOBOARD7:10;
         then A189: ii = len G by A176,A181,A182,A184,A187,GOBOARD1:21;
         A190: 1 <= ii & ii <= len G & 1 <= jj+1 & jj+1 <= width G
                                                           by A182,GOBOARD5:1;
         A191: 1 <= ii & ii <= len G & 1 <= jj & jj <= width G
                                                           by A183,GOBOARD5:1;
         A192: ii+1 <> ii;
          jj+1+1 <> jj;
         then A193: right_cell(RC,1) = cell(G,ii-'1,jj)
                           by A89,A177,A178,A182,A183,A184,A185,A192,
GOBOARD5:def 6;
         A194: ii-'1+1 = ii by A190,BINARITH:53;
          ii-1 >= 4-1 by A188,A189,XREAL_1:11;
         then A195: ii-1 >= 1 by XXREAL_0:2;
         then A196: 1 <= ii-'1 by BINARITH:def 3;
         A197: G*(ii-'1,jj)`2 <= p`2 & p`2 <= G*(ii-'1,jj+1)`2
             by A172,A173,A190,A191,A193,A194,A195,JORDAN9:19;
         A198: ii-'1 < len G by A190,A194,NAT_1:13;
         then A199: G*(ii-'1,jj)`2 = G*(1,jj)`2 by A191,A196,GOBOARD5:2
            .= G*(ii,jj)`2 by A191,GOBOARD5:2;
         A200: G*(ii-'1,jj+1)`2 = G*(1,jj+1)`2 by A190,A196,A198,GOBOARD5:2
            .= G*(ii,jj+1)`2 by A190,GOBOARD5:2;
              G*(len G,jj)`1 = Ebo & Ebo = G*(len G,jj+1)`1
                                                by A17,A190,A191,JORDAN1A:92;
         then p in LSeg(RC/.1,RC/.(1+1))
                              by A171,A184,A185,A189,A197,A199,A200,GOBOARD7:8;
         then A201: p in LSeg(LS,1) by A89,A175,A177,TOPREAL1:def 5;
         A202: p in LSeg(do,Index(p,do)) by A170,JORDAN3:42;
         A203: do = mid(LS,Gik..LS,len LS) by A36,JORDAN1J:37;
         A204: 1<=Gik..LS & Gik..LS<=len LS by A36,FINSEQ_4:31;
              Gik..LS <> len LS by A28,A36,FINSEQ_4:29;
         then A205: Gik..LS < len LS by A204,REAL_1:def 5;
         A206: 1<=Index(p,do) & Index(p,do) < len do by A170,JORDAN3:41;
         A207: Index(Gik,LS)+1 = Gik..LS by A31,A36,JORDAN1J:56;
         consider t be Nat such that
          A208: t in dom LS and
          A209: LS.t = Gik by A36,FINSEQ_2:11;
         A210: 1 <= t & t <= len LS by A208,FINSEQ_3:27;
         then 1 < t by A31,A209,REAL_1:def 5;
         then Index(Gik,LS)+1 = t by A209,A210,JORDAN3:45;
         then A211: len L_Cut(LS,Gik) = len LS-Index(Gik,LS)
                                                  by A8,A209,JORDAN3:61;
         set tt = Index(p,do)+(Gik..LS)-'1;
         A212: 1<=Index(Gik,LS) & 0+Index(Gik,LS) < len LS by A8,JORDAN3:41;
         then A213: len LS-Index(Gik,LS) > 0 by XREAL_1:22;
          Index(p,do) < len LS-'Index(Gik,LS) by A206,A211,BINARITH:def 3;
         then Index(p,do)+1 <= len LS-'Index(Gik,LS) by NAT_1:13;
         then Index(p,do) <= len LS-'Index(Gik,LS)-1 by XREAL_1:21;
         then Index(p,do) <= len LS-Index(Gik,LS)-1 by A213,BINARITH:def 3;
         then Index(p,do) <= len LS-Gik..LS by A207;
         then Index(p,do) <= len LS-'Gik..LS by BINARITH:def 3;
         then Index(p,do) < len LS-'(Gik..LS)+1 by NAT_1:13;
         then A214: LSeg(mid(LS,Gik..LS,len LS),Index(p,do)) =
              LSeg(LS,Index(p,do)+(Gik..LS)-'1) by A204,A205,A206,JORDAN4:31;
         A215: 1+1 <= Gik..LS by A207,A212,XREAL_1:9;
         then Index(p,do)+Gik..LS >= 1+1+1 by A206,XREAL_1:9;
         then A216: Index(p,do)+Gik..LS-1 >= 1+1+1-1 by XREAL_1:11;
         then A217: tt >= 1+1 by BINARITH:def 3;
         A218: 2 in dom LS by A174,FINSEQ_3:27;
              now per cases by A217,REAL_1:def 5;
          suppose tt > 1+1;
           then LSeg(LS,1) misses LSeg(LS,tt) by TOPREAL1:def 9;
           hence contradiction by A201,A202,A203,A214,XBOOLE_0:3;
          end;
          suppose A219: tt = 1+1;
           then LSeg(LS,1) /\ LSeg(LS,tt) = {LS/.2} by A25,TOPREAL1:def 8
;
           then p in {LS/.2} by A201,A202,A203,A214,XBOOLE_0:def 3;
           then A220: p = LS/.2 by TARSKI:def 1;
           then A221: p..LS = 2 by A218,FINSEQ_5:44;
                1+1 = Index(p,do)+(Gik..LS)-1 by A216,A219,BINARITH:def 3;
           then 1+1+1 = Index(p,do)+(Gik..LS);
           then A222: Gik..LS = 2 by A206,A215,JORDAN1E:10;
                p in rng LS by A218,A220,PARTFUN2:4;
           then p = Gik by A36,A221,A222,FINSEQ_5:10;
           then Gik`1 = Ebo by A220,JORDAN1G:40;
           then Gik`1 = G*(len G,j)`1 by A10,A17,JORDAN1A:92;
           hence contradiction by A1,A13,A63,JORDAN1G:7;
         end;
         end;
         hence contradiction;
      end;
      end;
      then east_halfline E-max C c= (L~godo)` by SUBSET_1:43;
      then consider W be Subset of TOP-REAL 2 such that
       A223: W is_a_component_of (L~godo)` and
       A224: east_halfline E-max C c= W by GOBOARD9:5;
           east_halfline E-max C is not Bounded by JORDAN2C:129;
      then W is not Bounded by A224,JORDAN2C:16;
      then W is_outside_component_of L~godo by A223,JORDAN2C:def 4;
      then W c= UBD L~godo by JORDAN2C:27;
      then A225: east_halfline E-max C c= UBD L~godo by A224,XBOOLE_1:1;
           E-max C in east_halfline E-max C by TOPREAL1:45;
      then E-max C in UBD L~godo by A225;
      then E-max C in LeftComp godo by GOBRD14:46;
      then UA meets L~godo by A125,A126,A137,A144,JORDAN1J:36;
      then A226: UA meets (L~go \/ L~pion1) or UA meets L~do by A138,
XBOOLE_1:70;
      A227: UA c= C by JORDAN6:76;
           now per cases by A226,XBOOLE_1:70;
       suppose UA meets L~go;
        then UA meets L~Cage(C,n) by A140,XBOOLE_1:63;
        then C meets L~Cage(C,n) by A227,XBOOLE_1:63;
        hence contradiction by JORDAN10:5;
       end;
       suppose UA meets L~pion1;
        hence contradiction by A5,A7,A80;
       end;
       suppose UA meets L~do;
        then UA meets L~Cage(C,n) by A140,XBOOLE_1:63;
        then C meets L~Cage(C,n) by A227,XBOOLE_1:63;
        hence contradiction by JORDAN10:5;
      end;
      end;
      hence contradiction;
    end;
    suppose Gik`1 = Gij`1;
      then A228: i1 = i2 by A12,A13,JORDAN1G:7;
      then poz = {Gi1k} by TOPREAL1:7;
      then poz c= pio by A76,ZFMISC_1:37;
      then pio \/ poz = pio by XBOOLE_1:12;
      hence contradiction by A1,A2,A3,A4,A5,A228,Th13;
     end;
     suppose Gik`2 = Gij`2;
      then A229: j = k by A12,A13,JORDAN1G:6;
      then pio = {Gi1k} by TOPREAL1:7;
      then pio c= poz by A77,ZFMISC_1:37;
      then pio \/ poz = poz by XBOOLE_1:12;
      hence contradiction by A1,A2,A3,A4,A5,A229,JORDAN15:39;
    end;
    end;
    hence contradiction;
   end;

  theorem Th22:
   for C be Simple_closed_curve
   for i1,i2,j,k be Element of NAT st
    1 < i1 & i1 <= i2 & i2 < len Gauge(C,n) &
    1 <= j & j <= k & k <= width Gauge(C,n) &
    (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
     LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) /\ L~Upper_Seq(C,n) =
     {Gauge(C,n)*(i1,j)} &
    (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
     LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) /\ L~Lower_Seq(C,n) =
     {Gauge(C,n)*(i2,k)} holds
      (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
       LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) meets Lower_Arc C
   proof
    let C be Simple_closed_curve;
    let i1,i2,j,k be Element of NAT;
    set G = Gauge(C,n);
    set pio = LSeg(G*(i1,j),G*(i1,k));
    set poz = LSeg(G*(i1,k),G*(i2,k));
    set US = Upper_Seq(C,n);
    set LS = Lower_Seq(C,n);
    assume that
     A1: 1 < i1 & i1 <= i2 & i2 < len G and
     A2: 1 <= j & j <= k & k <= width G and
     A3: (pio \/ poz) /\ L~US = {G*(i1,j)} and
     A4: (pio \/ poz) /\ L~LS = {G*(i2,k)} and
     A5: (pio \/ poz) misses Lower_Arc C;
    set UA = Lower_Arc C;
    set Wmin = W-min L~Cage(C,n);
    set Emax = E-max L~Cage(C,n);
    set Wbo = W-bound L~Cage(C,n);
    set Ebo = E-bound L~Cage(C,n);
    set Gik = G*(i2,k);
    set Gij = G*(i1,j);
    set Gi1k = G*(i1,k);
    A6: 1 < i1 & i1 < len G & 1 < i2 & i2 < len G by A1,XXREAL_0:2;
    A7: L~<*Gij,Gi1k,Gik*> = poz \/ pio by TOPREAL3:23;
         Gik in {Gik} by TARSKI:def 1;
    then A8: Gik in L~LS by A4,XBOOLE_0:def 3;
         Gij in {Gij} by TARSKI:def 1;
    then A9: Gij in L~US by A3,XBOOLE_0:def 3;
    A10: 1 <= j & j <= width G by A2,XXREAL_0:2;
    A11: 1 <= k & k <= width G by A2,XXREAL_0:2;
    A12: [i1,j] in Indices G by A6,A10,GOBOARD7:10;
    A13: [i2,k] in Indices G by A6,A11,GOBOARD7:10;
    A14: [i1,k] in Indices G by A6,A11,GOBOARD7:10;
    A15: US is_sequence_on G by JORDAN1G:4;
    A16: LS is_sequence_on G by JORDAN1G:5;
    set go = R_Cut(US,Gij);
    set do = L_Cut(LS,Gik);
    A17: len G = width G by JORDAN8:def 1;
    A18: len US >= 3 by JORDAN1E:19;
    then len US >= 1 by XXREAL_0:2;
    then 1 in dom US by FINSEQ_3:27;
    then A19: US.1 = US/.1 by PARTFUN1:def 8
       .= Wmin by JORDAN1F:5;
    A20: Wmin`1 = Wbo by EUCLID:56
       .= G*(1,k)`1 by A11,A17,JORDAN1A:94;
         len G >= 4 by JORDAN8:13;
    then A21: len G >= 1 by XXREAL_0:2;
    then A22: [1,k] in Indices G by A11,GOBOARD7:10;
    then A23: Gij <> US.1 by A1,A12,A19,A20,JORDAN1G:7;
    then reconsider go as being_S-Seq FinSequence of TOP-REAL 2
                                                            by A9,JORDAN3:70;
    A24: [1,j] in Indices G by A10,A21,GOBOARD7:10;
    A25: len LS >= 1+2 by JORDAN1E:19;
    then len LS >= 1 by XXREAL_0:2;
    then A26: 1 in dom LS & len LS in dom LS by FINSEQ_3:27;
    then A27: LS.len LS = LS/.len LS by PARTFUN1:def 8
       .= Wmin by JORDAN1F:8;
         Wmin`1 = Wbo by EUCLID:56
       .= G*(1,k)`1 by A11,A17,JORDAN1A:94;
    then A28: Gik <> LS.len LS by A1,A13,A22,A27,JORDAN1G:7;
    then reconsider do as being_S-Seq FinSequence of TOP-REAL 2
                                                            by A8,JORDAN3:69;
    A29: [len G,k] in Indices G by A11,A21,GOBOARD7:10;
    A30: LS.1 = LS/.1 by A26,PARTFUN1:def 8
       .= Emax by JORDAN1F:6;
         Emax`1 = Ebo by EUCLID:56
       .= G*(len G,k)`1 by A11,A17,JORDAN1A:92;
    then A31: Gik <> LS.1 by A1,A13,A29,A30,JORDAN1G:7;
    A32: len go >= 1+1 by TOPREAL1:def 10;
    A33: Gij in rng US by A6,A9,A10,A15,JORDAN1J:40;
    then A34: go is_sequence_on G by A15,JORDAN1J:38;
    A35: len do >= 1+1 by TOPREAL1:def 10;
    A36: Gik in rng LS by A6,A8,A11,A16,JORDAN1J:40;
    then A37: do is_sequence_on G by A16,JORDAN1J:39;
    reconsider go as non constant s.c.c.
            (being_S-Seq FinSequence of TOP-REAL 2) by A32,A34,JGRAPH_1:16
,JORDAN8:8;
    reconsider do as non constant s.c.c.
            (being_S-Seq FinSequence of TOP-REAL 2) by A35,A37,JGRAPH_1:16
,JORDAN8:8;
    A38: len go > 1 by A32,NAT_1:13;
    then A39: len go in dom go by FINSEQ_3:27;
    then A40: go/.len go = go.len go by PARTFUN1:def 8
       .= Gij by A9,JORDAN3:59;
         len do >= 1 by A35,XXREAL_0:2;
    then 1 in dom do by FINSEQ_3:27;
    then A41: do/.1 = do.1 by PARTFUN1:def 8
       .= Gik by A8,JORDAN3:58;
    reconsider m = len go - 1 as Element of NAT by A39,FINSEQ_3:28;
    A42: m+1 = len go;
    then A43: len go-'1 = m by BINARITH:39;
    A44: LSeg(go,m) c= L~go by TOPREAL3:26;
    A45: L~go c= L~US by A9,JORDAN3:76;
    then LSeg(go,m) c= L~US by A44,XBOOLE_1:1;
    then A46: LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> c= {Gij} by A3,A7,XBOOLE_1:26;
         m >= 1 by A32,XREAL_1:21;
    then A47: LSeg(go,m) = LSeg(go/.m,Gij) by A40,A42,TOPREAL1:def 5;
         {Gij} c= LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*>
    proof
     let x be set;
     assume x in {Gij};
     then A48: x = Gij by TARSKI:def 1;
     A49: Gij in LSeg(go,m) by A47,TOPREAL1:6;
          Gij in LSeg(Gij,Gi1k) by TOPREAL1:6;
     then Gij in LSeg(Gij,Gi1k) \/ LSeg(Gi1k,Gik) by XBOOLE_0:def 2;
     then Gij in L~<*Gij,Gi1k,Gik*> by SPRECT_1:10;
     hence x in LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> by A48,A49,XBOOLE_0:def 3;
    end;
    then A50: LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> = {Gij} by A46,XBOOLE_0:def 10;
    A51: LSeg(do,1) c= L~do by TOPREAL3:26;
    A52: L~do c= L~LS by A8,JORDAN3:77;
    then LSeg(do,1) c= L~LS by A51,XBOOLE_1:1;
    then A53: LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*> c= {Gik} by A4,A7,XBOOLE_1:26;
    A54: LSeg(do,1) = LSeg(Gik,do/.(1+1)) by A35,A41,TOPREAL1:def 5;
         {Gik} c= LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*>
    proof
     let x be set;
     assume x in {Gik};
     then A55: x = Gik by TARSKI:def 1;
     A56: Gik in LSeg(do,1) by A54,TOPREAL1:6;
          Gik in LSeg(Gi1k,Gik) by TOPREAL1:6;
     then Gik in LSeg(Gij,Gi1k) \/ LSeg(Gi1k,Gik) by XBOOLE_0:def 2;
     then Gik in L~<*Gij,Gi1k,Gik*> by SPRECT_1:10;
     hence x in LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*> by A55,A56,XBOOLE_0:def 3;
    end;
    then A57: L~<*Gij,Gi1k,Gik*> /\ LSeg(do,1) = {Gik} by A53,XBOOLE_0:def 10;
    A58: go/.1 = US/.1 by A9,SPRECT_3:39
       .= Wmin by JORDAN1F:5;
    then A59: go/.1 = LS/.len LS by JORDAN1F:8
       .= do/.len do by A8,JORDAN1J:35;
    A60: rng go c= L~go & rng do c= L~do by A32,A35,SPPOL_2:18;
    A61: {go/.1} c= L~go /\ L~do
    proof
     let x be set;
     assume x in {go/.1};
     then x = go/.1 by TARSKI:def 1;
     then x in rng go & x in rng do by A59,FINSEQ_6:46,REVROT_1:3;
     hence x in L~go /\ L~do by A60,XBOOLE_0:def 3;
    end;
    A62: LS.1 = LS/.1 by A26,PARTFUN1:def 8
       .= Emax by JORDAN1F:6;
    A63: [len G,j] in Indices G by A10,A21,GOBOARD7:10;
         L~go /\ L~do c= {go/.1}
    proof
     let x be set;
     assume x in L~go /\ L~do;
     then A64: x in L~go & x in L~do by XBOOLE_0:def 3;
     then x in L~US /\ L~LS by A45,A52,XBOOLE_0:def 3;
     then x in {Wmin,Emax} by JORDAN1E:20;
     then A65: x = Wmin or x = Emax by TARSKI:def 2;
          now assume x = Emax;
      then A66: Emax = Gik by A8,A62,A64,JORDAN1E:11;
           G*(len G,j)`1 = Ebo by A10,A17,JORDAN1A:92;
      then Emax`1 <> Ebo by A1,A13,A63,A66,JORDAN1G:7;
      hence contradiction by EUCLID:56;
     end;
     hence x in {go/.1} by A58,A65,TARSKI:def 1;
    end;
    then A67: L~go /\ L~do = {go/.1} by A61,XBOOLE_0:def 10;
    set W2 = go/.2;
    A68: 2 in dom go by A32,FINSEQ_3:27;
    A69: Gij..US >= 1 by A33,FINSEQ_4:31;
    A70: now assume Gij`1 = Wbo;
     then G*(1,j)`1 = G*(i1,j)`1 by A10,A17,JORDAN1A:94;
     hence contradiction by A1,A12,A24,JORDAN1G:7;
    end;
         go = mid(US,1,Gij..US) by A33,JORDAN1G:57
       .= US|(Gij..US) by A69,JORDAN3:25;
    then A71: W2 = US/.2 by A68,FINSEQ_4:85;
    A72: Wmin in rng go by A58,FINSEQ_6:46;
    set pion = <*Gij,Gi1k,Gik*>;
    A73: now let n be Element of NAT;
     assume n in dom pion;
     then n in {1,2,3} by FINSEQ_3:1,30;
     then n = 1 or n = 2 or n = 3 by ENUMSET1:def 1;
     then pion/.n = Gij or pion/.n = Gi1k or pion/.n = Gik by FINSEQ_4:27
;
     hence ex i,j be Element of NAT st [i,j] in Indices G & pion/.n = G*(i,j)
                                                             by A12,A13,A14;
    end;
    A74: Gi1k`1 = G*(i1,1)`1 by A6,A11,GOBOARD5:3
       .= Gij`1 by A6,A10,GOBOARD5:3;
         Gi1k`2 =  G*(1,k)`2 by A6,A11,GOBOARD5:2
       .= Gik`2 by A6,A11,GOBOARD5:2;
    then A75: Gi1k = |[Gij`1,Gik`2]| by A74,EUCLID:57;
    A76: Gi1k in pio by TOPREAL1:6;
    A77: Gi1k in poz by TOPREAL1:6;
         now per cases;
     suppose Gik`1 <> Gij`1 & Gik`2 <> Gij`2;
      then pion is_S-Seq by A75,TOPREAL3:41;
      then consider pion1 be FinSequence of TOP-REAL 2 such that
       A78: pion1 is_sequence_on G and
       A79: pion1 is_S-Seq and
       A80: L~pion = L~pion1 and
       A81: pion/.1 = pion1/.1 and
       A82: pion/.len pion = pion1/.len pion1 and
       A83: len pion <= len pion1 by A73,GOBOARD3:2;
      reconsider pion1 as being_S-Seq FinSequence of TOP-REAL 2 by A79;
      set godo = go^'pion1^'do;
      A84: Gi1k`1 = G*(i1,1)`1 by A6,A11,GOBOARD5:3
         .= Gij`1 by A6,A10,GOBOARD5:3;
      A85: Gi1k`1 <= Gik`1 by A1,A11,JORDAN1A:39;
      then A86: W-bound poz = Gi1k`1 by SPRECT_1:62;
      A87: W-bound pio = Gij`1 by A84,SPRECT_1:62;
           W-bound (poz \/ pio) = min(W-bound poz, W-bound pio) by SPRECT_1:54
         .= Gij`1 by A84,A86,A87;
      then A88: W-bound L~pion1 = Gij`1 by A80,TOPREAL3:23;
           len Cage(C,n) > 4 by GOBOARD7:36;
      then A89: 1+1 <= len Cage(C,n) by XXREAL_0:2;
      then A90: 1+1 <= len Rotate(Cage(C,n),Wmin) by REVROT_1:14;
           len (go^'pion1) >= len go by TOPREAL8:7;
      then A91: len (go^'pion1) >= 1+1 by A32,XXREAL_0:2;
      then A92: len (go^'pion1) > 1+0 by NAT_1:13;
           len godo >= len (go^'pion1) by TOPREAL8:7;
      then A93: 1+1 <= len godo by A91,XXREAL_0:2;
      A94: US is_sequence_on G by JORDAN1G:4;
      A95: go/.len go = pion1/.1 by A40,A81,FINSEQ_4:27;
      then A96: go^'pion1 is_sequence_on G by A34,A78,TOPREAL8:12;
      A97: (go^'pion1)/.len (go^'pion1) = pion/.len pion by A82,GRAPH_2:58
         .= pion/.3 by FINSEQ_1:62
         .= do/.1 by A41,FINSEQ_4:27;
      then A98: godo is_sequence_on G by A37,A96,TOPREAL8:12;
           LSeg(pion1,1) c= L~pion by A80,TOPREAL3:26;
      then A99: LSeg(go,len go-'1) /\ LSeg(pion1,1) c={Gij}
                                                      by A43,A50,XBOOLE_1:27;
           len pion1 >= 2+1 by A83,FINSEQ_1:62;
      then A100: len pion1 > 1+1 by NAT_1:13;
           {Gij} c= LSeg(go,m) /\ LSeg(pion1,1)
      proof
       let x be set;
       assume x in {Gij};
       then A101: x = Gij by TARSKI:def 1;
       A102: Gij in LSeg(go,m) by A47,TOPREAL1:6;
            Gij in LSeg(pion1,1) by A40,A95,A100,TOPREAL1:27;
       hence x in LSeg(go,m) /\ LSeg(pion1,1) by A101,A102,XBOOLE_0:def 3;
      end;
      then LSeg(go,len go-'1) /\ LSeg(pion1,1) = { go/.len go }
                                              by A40,A43,A99,XBOOLE_0:def 10;
      then A103: go^'pion1 is unfolded by A95,TOPREAL8:34;
           len pion1 >= 2+1 by A83,FINSEQ_1:62;
      then A104: len pion1-2 >= 0 by XREAL_1:21;
           len (go^'pion1)+1-1 = len go+len pion1-1 by GRAPH_2:13;
      then len (go^'pion1)-1 = len go + (len pion1-2)
         .= len go + (len pion1-'2) by A104,BINARITH:def 3;
      then A105: len (go^'pion1)-'1 = len go + (len pion1-'2)
                                                      by BINARITH:def 3;
      A106: len pion1-1 >= 1 by A100,XREAL_1:21;
      then A107: len pion1-'1 = len pion1-1 by BINARITH:def 3;
      A108: len pion1-'2+1 = len pion1-2+1 by A104,BINARITH:def 3
         .= len pion1-'1 by A106,BINARITH:def 3;
           len pion1-1+1 <= len pion1;
      then A109: len pion1-'1 < len pion1 by A107,NAT_1:13;
           LSeg(pion1,len pion1-'1) c= L~pion by A80,TOPREAL3:26;
      then A110: LSeg(pion1,len pion1-'1) /\ LSeg(do,1) c= {Gik}
                                                          by A57,XBOOLE_1:27;
           {Gik} c= LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
      proof
       let x be set;
       assume x in {Gik};
       then A111: x = Gik by TARSKI:def 1;
       A112: Gik in LSeg(do,1) by A54,TOPREAL1:6;
         pion1/.(len pion1-'1+1) = pion/.3 by A82,A107,FINSEQ_1:62
          .= Gik by FINSEQ_4:27;
       then Gik in LSeg(pion1,len pion1-'1) by A106,A107,TOPREAL1:27
;
       hence x in LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
                                                   by A111,A112,XBOOLE_0:def 3;
      end;
      then LSeg(pion1,len pion1-'1) /\ LSeg(do,1) = {Gik}
                                                     by A110,XBOOLE_0:def 10;
      then A113: LSeg(go^'pion1,len go+(len pion1-'2)) /\ LSeg(do,1) =
         {(go^'pion1)/.len (go^'pion1)} by A41,A95,A97,A108,A109,TOPREAL8:31;
      A114: (go^'pion1) is non trivial by A91,REALSET1:13;
      A115: rng pion1 c= L~pion1 by A100,SPPOL_2:18;
      A116: {pion1/.1} c= L~go /\ L~pion1
      proof
       let x be set;
       assume x in {pion1/.1};
       then x = pion1/.1 by TARSKI:def 1;
       then x in rng go & x in rng pion1 by A95,FINSEQ_6:46,REVROT_1:3;
       hence x in L~go /\ L~pion1 by A60,A115,XBOOLE_0:def 3;
      end;
           L~go /\ L~pion1 c= {pion1/.1}
      proof
       let x be set;
       assume x in L~go /\ L~pion1;
       then x in L~go & x in L~pion1 by XBOOLE_0:def 3;
       hence x in {pion1/.1} by A3,A7,A40,A45,A80,A95,XBOOLE_0:def 3;
      end;
      then A117: L~go /\ L~pion1 = {pion1/.1} by A116,XBOOLE_0:def 10;
      then A118: (go^'pion1) is s.n.c. by A95,JORDAN1J:54;
           rng go /\ rng pion1 c= {pion1/.1} by A60,A115,A117,XBOOLE_1:27;
      then A119: go^'pion1 is one-to-one by JORDAN1J:55;
      A120: pion/.len pion = pion/.3 by FINSEQ_1:62
         .= do/.1 by A41,FINSEQ_4:27;
      A121: {pion1/.len pion1} c= L~do /\ L~pion1
      proof
       let x be set;
       assume x in {pion1/.len pion1};
       then x = pion1/.len pion1 by TARSKI:def 1;
       then x in rng do & x in rng pion1 by A82,A120,FINSEQ_6:46,REVROT_1:3;
       hence x in L~do /\ L~pion1 by A60,A115,XBOOLE_0:def 3;
      end;
           L~do /\ L~pion1 c= {pion1/.len pion1}
      proof
       let x be set;
       assume x in L~do /\ L~pion1;
       then x in L~do & x in L~pion1 by XBOOLE_0:def 3;
       hence x in {pion1/.len pion1}
          by A4,A7,A41,A52,A80,A82,A120,XBOOLE_0:def 3;
      end;
      then A122: L~do /\ L~pion1 = {pion1/.len pion1} by A121,XBOOLE_0:def 10;
      A123: L~(go^'pion1) /\ L~do = (L~go \/ L~pion1) /\ L~do by A95,
TOPREAL8:35
         .= {go/.1} \/ {do/.1} by A67,A82,A120,A122,XBOOLE_1:23
         .= {(go^'pion1)/.1} \/ {do/.1} by GRAPH_2:57
         .= {(go^'pion1)/.1,do/.1} by ENUMSET1:41;
           do/.len do = (go^'pion1)/.1 by A59,GRAPH_2:57;
      then reconsider godo as non constant standard special_circular_sequence
          by A93,A97,A98,A103,A105,A113,A114,A118,A119,A123,JORDAN8:7,8
,TOPREAL8:11,33,34
;
      A124: UA is_an_arc_of E-max C,W-min C by JORDAN6:def 9;
      then A125: UA is connected by JORDAN6:11;
      A126: W-min C in UA & E-max C in UA by A124,TOPREAL1:4;
      set ff = Rotate(Cage(C,n),Wmin);
           Wmin in rng Cage(C,n) by SPRECT_2:47;
      then A127: ff/.1 = Wmin by FINSEQ_6:98;
      A128: L~ff = L~Cage(C,n) by REVROT_1:33;
      then A129: (W-max L~ff)..ff > 1 by A127,SPRECT_5:23;
           (W-max L~ff)..ff <= (N-min L~ff)..ff by A127,A128,SPRECT_5:24;
      then A130: (N-min L~ff)..ff > 1 by A129,XXREAL_0:2;
           (N-min L~ff)..ff < (N-max L~ff)..ff by A127,A128,SPRECT_5:25;
      then A131: (N-max L~ff)..ff > 1 by A130,XXREAL_0:2;
           (N-max L~ff)..ff <= (E-max L~ff)..ff by A127,A128,SPRECT_5:26;
      then A132: Emax..ff > 1 by A128,A131,XXREAL_0:2;
      A133: now assume A134: Gij..US <= 1;
            Gij..US >= 1 by A33,FINSEQ_4:31;
       then Gij..US = 1 by A134,XXREAL_0:1;
       then Gij = US/.1 by A33,FINSEQ_5:41;
       hence contradiction by A19,A23,JORDAN1F:5;
      end;
      A135: Cage(C,n) is_sequence_on G by JORDAN9:def 1;
      then A136: ff is_sequence_on G by REVROT_1:34;
      A137: right_cell(godo,1,G)\L~godo c= RightComp godo by A93,A98,JORDAN9:29
;
      A138: L~godo = L~(go^'pion1) \/ L~do by A97,TOPREAL8:35
         .= L~go \/ L~pion1 \/ L~do by A95,TOPREAL8:35;
           L~Cage(C,n) = L~US \/ L~LS by JORDAN1E:17;
      then A139: L~US c= L~Cage(C,n) & L~LS c= L~Cage(C,n) by XBOOLE_1:7;
      then A140: L~go c=L~Cage(C,n) & L~do c=L~Cage(C,n)
                                                     by A45,A52,XBOOLE_1:1;
      A141: W-min C in C by SPRECT_1:15;
      A142: now assume W-min C in L~godo;
       then A143: W-min C in L~go \/ L~pion1 or W-min C in L~do
                                                       by A138,XBOOLE_0:def 2;
       per cases by A143,XBOOLE_0:def 2;
        suppose W-min C in L~go;
         then C meets L~Cage(C,n) by A140,A141,XBOOLE_0:3;
         hence contradiction by JORDAN10:5;
        end;
        suppose W-min C in L~pion1;
         hence contradiction by A5,A7,A80,A126,XBOOLE_0:3;
        end;
        suppose W-min C in L~do;
         then C meets L~Cage(C,n) by A140,A141,XBOOLE_0:3;
         hence contradiction by JORDAN10:5;
      end;
      end;
           right_cell(Rotate(Cage(C,n),Wmin),1) =
            right_cell(ff,1,GoB ff) by A90,JORDAN1H:29
         .= right_cell(ff,1,GoB Cage(C,n)) by REVROT_1:28
         .= right_cell(ff,1,G) by JORDAN1H:52
         .= right_cell(ff-:Emax,1,G) by A132,A136,JORDAN1J:53
         .= right_cell(US,1,G) by JORDAN1E:def 1
         .= right_cell(R_Cut(US,Gij),1,G) by A33,A94,A133,JORDAN1J:52
         .= right_cell(go^'pion1,1,G) by A38,A96,JORDAN1J:51
         .= right_cell(godo,1,G) by A92,A98,JORDAN1J:51;
      then W-min C in right_cell(godo,1,G) by JORDAN1I:8;
      then A144: W-min C in right_cell(godo,1,G)\L~godo by A142,XBOOLE_0:def 4;
      A145: godo/.1 = (go^'pion1)/.1 by GRAPH_2:57
         .= Wmin by A58,GRAPH_2:57;
      A146: len US >= 2 by A18,XXREAL_0:2;
      A147: godo/.2 = (go^'pion1)/.2 by A91,GRAPH_2:61
         .= US/.2 by A32,A71,GRAPH_2:61
         .= (US^'LS)/.2 by A146,GRAPH_2:61
         .= Rotate(Cage(C,n),Wmin)/.2 by JORDAN1E:15;
      A148: L~go \/ L~do is compact by COMPTS_1:19;
      A149: L~go \/ L~do c= L~Cage(C,n) by A140,XBOOLE_1:8;
           Wmin in L~go \/ L~do by A60,A72,XBOOLE_0:def 2;
      then A150: W-min (L~go \/ L~do) = Wmin by A148,A149,JORDAN1J:21;
      A151: (W-min (L~go \/ L~do))`1 = W-bound (L~go \/ L~do) & Wmin`1 = Wbo
                                                              by EUCLID:56;
           Gij`1 >= Wbo by A9,A139,PSCOMP_1:71;
      then Gij`1 > Wbo by A70,REAL_1:def 5;
      then W-min (L~go\/L~do\/L~pion1) = W-min (L~go \/ L~do)
                                              by A88,A148,A150,A151,JORDAN1J:33
;
      then A152: W-min L~godo = Wmin by A138,A150,XBOOLE_1:4;
      A153: rng godo c= L~godo by A93,SPPOL_2:18;
           2 in dom godo by A93,FINSEQ_3:27;
      then A154: godo/.2 in rng godo by PARTFUN2:4;
           godo/.2 in W-most L~Cage(C,n) by A147,JORDAN1I:27;
      then (godo/.2)`1 = (W-min L~godo)`1 by A152,PSCOMP_1:88
         .= W-bound L~godo by EUCLID:56;
      then godo/.2 in W-most L~godo by A153,A154,SPRECT_2:16;
      then Rotate(godo,W-min L~godo)/.2 in W-most L~godo
                                                      by A145,A152,FINSEQ_6:95;
      then reconsider godo as clockwise_oriented non constant standard
                                    special_circular_sequence by JORDAN1I:27;
           len US in dom US by FINSEQ_5:6;
      then A155: US.len US = US/.len US by PARTFUN1:def 8
         .= Emax by JORDAN1F:7;
      A156: E-max C in E-most C by PSCOMP_1:111;
      A157: east_halfline E-max C misses L~go
      proof
       assume east_halfline E-max C meets L~go;
       then consider p be set such that
        A158: p in east_halfline E-max C and
        A159: p in L~go by XBOOLE_0:3;
       reconsider p as Point of TOP-REAL 2 by A158;
            p in L~US by A45,A159;
       then p in east_halfline E-max C /\ L~Cage(C,n)
                    by A139,A158,XBOOLE_0:def 3;
       then A160: p`1 = Ebo by A156,JORDAN1A:104;
       then A161: p = Emax by A45,A159,JORDAN1J:46;
       then Emax = Gij by A9,A155,A159,JORDAN1J:43;
       then Gij`1 = G*(len G,k)`1 by A11,A17,A160,A161,JORDAN1A:92;
       hence contradiction by A1,A12,A29,JORDAN1G:7;
      end;
           now assume east_halfline E-max C meets L~godo;
       then A162: east_halfline E-max C meets (L~go \/ L~pion1) or
                 east_halfline E-max C meets L~do by A138,XBOOLE_1:70;
       per cases by A162,XBOOLE_1:70;
        suppose east_halfline E-max C meets L~go;
         hence contradiction by A157;
        end;
        suppose east_halfline E-max C meets L~pion1;
         then consider p be set such that
          A163: p in east_halfline E-max C and
          A164: p in L~pion1 by XBOOLE_0:3;
         reconsider p as Point of TOP-REAL 2 by A163;
         A165: now per cases by A7,A80,A164,XBOOLE_0:def 2;
          suppose p in poz;
           hence p`1 <= Gik`1 by A85,TOPREAL1:9;
          end;
          suppose p in pio;
           hence p`1 <= Gik`1 by A84,A85,GOBOARD7:5;
         end;
         end;
              i2+1 <= len G by A1,NAT_1:13;
         then i2 <= len G-1 by XREAL_1:21;
         then A166: i2 <= len G-'1 by BINARITH:def 3;
              len G-'1 <= len G by BINARITH:52;
         then Gik`1 <= G*(len G-'1,1)`1 by A6,A11,A17,A21,A166,JORDAN1A:39;
         then p`1 <= G*(len G-'1,1)`1 by A165,XXREAL_0:2;
         then p`1 <= E-bound C by A21,JORDAN8:15;
         then A167: p`1 <= (E-max C)`1 by EUCLID:56;
              p`1 >= (E-max C)`1 by A163,TOPREAL1:def 13;
         then A168: p`1 = (E-max C)`1 by A167,XXREAL_0:1;
              p`2 = (E-max C)`2 by A163,TOPREAL1:def 13;
         then p = E-max C by A168,TOPREAL3:11;
         hence contradiction by A5,A7,A80,A126,A164,XBOOLE_0:3;
        end;
        suppose east_halfline E-max C meets L~do;
         then consider p be set such that
          A169: p in east_halfline E-max C and
          A170: p in L~do by XBOOLE_0:3;
         reconsider p as Point of TOP-REAL 2 by A169;
              p in L~LS by A52,A170;
         then p in east_halfline E-max C /\ L~Cage(C,n)
                      by A139,A169,XBOOLE_0:def 3;
         then A171: p`1 = Ebo by A156,JORDAN1A:104;
         A172: (E-max C)`2 = p`2 by A169,TOPREAL1:def 13;
         set RC = Rotate(Cage(C,n),Emax);
         A173: E-max C in right_cell(RC,1) by JORDAN1I:9;
         A174: 1+1 <= len LS by A25,XXREAL_0:2;
              LS = RC-:Wmin by JORDAN1G:26;
         then A175: LSeg(LS,1) = LSeg(RC,1) by A174,SPPOL_2:9;
         A176: L~RC = L~Cage(C,n) by REVROT_1:33;
         A177: len RC = len Cage(C,n) by REVROT_1:14;
         A178: GoB RC = GoB Cage(C,n) by REVROT_1:28
            .= G by JORDAN1H:52;
         A179: Emax in rng Cage(C,n) by SPRECT_2:50;
         A180: RC is_sequence_on G by A135,REVROT_1:34;
         A181: RC/.1 = E-max L~RC by A176,A179,FINSEQ_6:98;
         then consider ii,jj be Element of NAT such that
          A182: [ii,jj+1] in Indices G and
          A183: [ii,jj] in Indices G and
          A184: RC/.1 = G*(ii,jj+1) and
          A185: RC/.(1+1) = G*(ii,jj) by A89,A177,A180,JORDAN1I:25;
         consider jj2 be Element of NAT such that
          A186: 1 <= jj2 & jj2 <= width G and
          A187: Emax = G*(len G,jj2) by JORDAN1D:29;
         A188: len G >= 4 by JORDAN8:13;
         then len G >= 1 by XXREAL_0:2;
         then [len G,jj2] in Indices G by A186,GOBOARD7:10;
         then A189: ii = len G by A176,A181,A182,A184,A187,GOBOARD1:21;
         A190: 1 <= ii & ii <= len G & 1 <= jj+1 & jj+1 <= width G
                                                           by A182,GOBOARD5:1;
         A191: 1 <= ii & ii <= len G & 1 <= jj & jj <= width G
                                                           by A183,GOBOARD5:1;
         A192: ii+1 <> ii;
          jj+1+1 <> jj;
         then A193: right_cell(RC,1) = cell(G,ii-'1,jj)
                           by A89,A177,A178,A182,A183,A184,A185,A192,
GOBOARD5:def 6;
         A194: ii-'1+1 = ii by A190,BINARITH:53;
          ii-1 >= 4-1 by A188,A189,XREAL_1:11;
         then A195: ii-1 >= 1 by XXREAL_0:2;
         then A196: 1 <= ii-'1 by BINARITH:def 3;
         A197: G*(ii-'1,jj)`2 <= p`2 & p`2 <= G*(ii-'1,jj+1)`2
             by A172,A173,A190,A191,A193,A194,A195,JORDAN9:19;
         A198: ii-'1 < len G by A190,A194,NAT_1:13;
         then A199: G*(ii-'1,jj)`2 = G*(1,jj)`2 by A191,A196,GOBOARD5:2
            .= G*(ii,jj)`2 by A191,GOBOARD5:2;
         A200: G*(ii-'1,jj+1)`2 = G*(1,jj+1)`2 by A190,A196,A198,GOBOARD5:2
            .= G*(ii,jj+1)`2 by A190,GOBOARD5:2;
              G*(len G,jj)`1 = Ebo & Ebo = G*(len G,jj+1)`1
                                                by A17,A190,A191,JORDAN1A:92;
         then p in LSeg(RC/.1,RC/.(1+1))
                              by A171,A184,A185,A189,A197,A199,A200,GOBOARD7:8;
         then A201: p in LSeg(LS,1) by A89,A175,A177,TOPREAL1:def 5;
         A202: p in LSeg(do,Index(p,do)) by A170,JORDAN3:42;
         A203: do = mid(LS,Gik..LS,len LS) by A36,JORDAN1J:37;
         A204: 1<=Gik..LS & Gik..LS<=len LS by A36,FINSEQ_4:31;
              Gik..LS <> len LS by A28,A36,FINSEQ_4:29;
         then A205: Gik..LS < len LS by A204,REAL_1:def 5;
         A206: 1<=Index(p,do) & Index(p,do) < len do by A170,JORDAN3:41;
         A207: Index(Gik,LS)+1 = Gik..LS by A31,A36,JORDAN1J:56;
         consider t be Nat such that
          A208: t in dom LS and
          A209: LS.t = Gik by A36,FINSEQ_2:11;
         A210: 1 <= t & t <= len LS by A208,FINSEQ_3:27;
         then 1 < t by A31,A209,REAL_1:def 5;
         then Index(Gik,LS)+1 = t by A209,A210,JORDAN3:45;
         then A211: len L_Cut(LS,Gik) = len LS-Index(Gik,LS)
                                                  by A8,A209,JORDAN3:61;
         set tt = Index(p,do)+(Gik..LS)-'1;
         A212: 1<=Index(Gik,LS) & 0+Index(Gik,LS) < len LS by A8,JORDAN3:41;
         then A213: len LS-Index(Gik,LS) > 0 by XREAL_1:22;
          Index(p,do) < len LS-'Index(Gik,LS) by A206,A211,BINARITH:def 3;
         then Index(p,do)+1 <= len LS-'Index(Gik,LS) by NAT_1:13;
         then Index(p,do) <= len LS-'Index(Gik,LS)-1 by XREAL_1:21;
         then Index(p,do) <= len LS-Index(Gik,LS)-1 by A213,BINARITH:def 3;
         then Index(p,do) <= len LS-Gik..LS by A207;
         then Index(p,do) <= len LS-'Gik..LS by BINARITH:def 3;
         then Index(p,do) < len LS-'(Gik..LS)+1 by NAT_1:13;
         then A214: LSeg(mid(LS,Gik..LS,len LS),Index(p,do)) =
              LSeg(LS,Index(p,do)+(Gik..LS)-'1) by A204,A205,A206,JORDAN4:31;
         A215: 1+1 <= Gik..LS by A207,A212,XREAL_1:9;
         then Index(p,do)+Gik..LS >= 1+1+1 by A206,XREAL_1:9;
         then A216: Index(p,do)+Gik..LS-1 >= 1+1+1-1 by XREAL_1:11;
         then A217: tt >= 1+1 by BINARITH:def 3;
         A218: 2 in dom LS by A174,FINSEQ_3:27;
              now per cases by A217,REAL_1:def 5;
          suppose tt > 1+1;
           then LSeg(LS,1) misses LSeg(LS,tt) by TOPREAL1:def 9;
           hence contradiction by A201,A202,A203,A214,XBOOLE_0:3;
          end;
          suppose A219: tt = 1+1;
           then LSeg(LS,1) /\ LSeg(LS,tt) = {LS/.2} by A25,TOPREAL1:def 8
;
           then p in {LS/.2} by A201,A202,A203,A214,XBOOLE_0:def 3;
           then A220: p = LS/.2 by TARSKI:def 1;
           then A221: p..LS = 2 by A218,FINSEQ_5:44;
                1+1 = Index(p,do)+(Gik..LS)-1 by A216,A219,BINARITH:def 3;
           then 1+1+1 = Index(p,do)+(Gik..LS);
           then A222: Gik..LS = 2 by A206,A215,JORDAN1E:10;
                p in rng LS by A218,A220,PARTFUN2:4;
           then p = Gik by A36,A221,A222,FINSEQ_5:10;
           then Gik`1 = Ebo by A220,JORDAN1G:40;
           then Gik`1 = G*(len G,j)`1 by A10,A17,JORDAN1A:92;
           hence contradiction by A1,A13,A63,JORDAN1G:7;
         end;
         end;
         hence contradiction;
      end;
      end;
      then east_halfline E-max C c= (L~godo)` by SUBSET_1:43;
      then consider W be Subset of TOP-REAL 2 such that
       A223: W is_a_component_of (L~godo)` and
       A224: east_halfline E-max C c= W by GOBOARD9:5;
           east_halfline E-max C is not Bounded by JORDAN2C:129;
      then W is not Bounded by A224,JORDAN2C:16;
      then W is_outside_component_of L~godo by A223,JORDAN2C:def 4;
      then W c= UBD L~godo by JORDAN2C:27;
      then A225: east_halfline E-max C c= UBD L~godo by A224,XBOOLE_1:1;
           E-max C in east_halfline E-max C by TOPREAL1:45;
      then E-max C in UBD L~godo by A225;
      then E-max C in LeftComp godo by GOBRD14:46;
      then UA meets L~godo by A125,A126,A137,A144,JORDAN1J:36;
      then A226: UA meets (L~go \/ L~pion1) or UA meets L~do by A138,
XBOOLE_1:70;
      A227: UA c= C by JORDAN6:76;
           now per cases by A226,XBOOLE_1:70;
       suppose UA meets L~go;
        then UA meets L~Cage(C,n) by A140,XBOOLE_1:63;
        then C meets L~Cage(C,n) by A227,XBOOLE_1:63;
        hence contradiction by JORDAN10:5;
       end;
       suppose UA meets L~pion1;
        hence contradiction by A5,A7,A80;
       end;
       suppose UA meets L~do;
        then UA meets L~Cage(C,n) by A140,XBOOLE_1:63;
        then C meets L~Cage(C,n) by A227,XBOOLE_1:63;
        hence contradiction by JORDAN10:5;
      end;
      end;
      hence contradiction;
    end;
    suppose Gik`1 = Gij`1;
      then A228: i1 = i2 by A12,A13,JORDAN1G:7;
      then poz = {Gi1k} by TOPREAL1:7;
      then poz c= pio by A76,ZFMISC_1:37;
      then pio \/ poz = pio by XBOOLE_1:12;
      hence contradiction by A1,A2,A3,A4,A5,A228,Th14;
     end;
     suppose Gik`2 = Gij`2;
      then A229: j = k by A12,A13,JORDAN1G:6;
      then pio = {Gi1k} by TOPREAL1:7;
      then pio c= poz by A77,ZFMISC_1:37;
      then pio \/ poz = poz by XBOOLE_1:12;
      hence contradiction by A1,A2,A3,A4,A5,A229,JORDAN15:38;
    end;
    end;
    hence contradiction;
   end;

  theorem Th23:
   for C be Simple_closed_curve
   for i1,i2,j,k be Element of NAT st
    1 < i2 & i2 <= i1 & i1 < len Gauge(C,n) &
    1 <= j & j <= k & k <= width Gauge(C,n) &
    (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
     LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) /\ L~Upper_Seq(C,n) =
     {Gauge(C,n)*(i1,j)} &
    (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
     LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) /\ L~Lower_Seq(C,n) =
     {Gauge(C,n)*(i2,k)} holds
      (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
       LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) meets Upper_Arc C
   proof
    let C be Simple_closed_curve;
    let i1,i2,j,k be Element of NAT;
    set G = Gauge(C,n);
    set pio = LSeg(G*(i1,j),G*(i1,k));
    set poz = LSeg(G*(i1,k),G*(i2,k));
    set US = Upper_Seq(C,n);
    set LS = Lower_Seq(C,n);
    assume that
     A1: 1 < i2 & i2 <= i1 & i1 < len G and
     A2: 1 <= j & j <= k & k <= width G and
     A3: (pio \/ poz) /\ L~US = {G*(i1,j)} and
     A4: (pio \/ poz) /\ L~LS = {G*(i2,k)} and
     A5: (pio \/ poz) misses Upper_Arc C;
    set UA = Upper_Arc C;
    set Wmin = W-min L~Cage(C,n);
    set Emax = E-max L~Cage(C,n);
    set Wbo = W-bound L~Cage(C,n);
    set Ebo = E-bound L~Cage(C,n);
    set Gik = G*(i2,k);
    set Gij = G*(i1,j);
    set Gi1k = G*(i1,k);
    A6: 1 < i1 & i1 < len G & 1 < i2 & i2 < len G by A1,XXREAL_0:2;
    A7: L~<*Gij,Gi1k,Gik*> = poz \/ pio by TOPREAL3:23;
         Gik in {Gik} by TARSKI:def 1;
    then A8: Gik in L~LS by A4,XBOOLE_0:def 3;
         Gij in {Gij} by TARSKI:def 1;
    then A9: Gij in L~US by A3,XBOOLE_0:def 3;
    A10: 1 <= j & j <= width G by A2,XXREAL_0:2;
    A11: 1 <= k & k <= width G by A2,XXREAL_0:2;
    A12: [i1,j] in Indices G by A6,A10,GOBOARD7:10;
    A13: [i2,k] in Indices G by A6,A11,GOBOARD7:10;
    A14: [i1,k] in Indices G by A6,A11,GOBOARD7:10;
    A15: US is_sequence_on G by JORDAN1G:4;
    A16: LS is_sequence_on G by JORDAN1G:5;
    set go = R_Cut(US,Gij);
    set do = L_Cut(LS,Gik);
    A17: len G = width G by JORDAN8:def 1;
    A18: len US >= 3 by JORDAN1E:19;
    then len US >= 1 by XXREAL_0:2;
    then 1 in dom US by FINSEQ_3:27;
    then A19: US.1 = US/.1 by PARTFUN1:def 8
       .= Wmin by JORDAN1F:5;
    A20: Wmin`1 = Wbo by EUCLID:56
       .= G*(1,k)`1 by A11,A17,JORDAN1A:94;
         len G >= 4 by JORDAN8:13;
    then A21: len G >= 1 by XXREAL_0:2;
    then A22: [1,k] in Indices G by A11,GOBOARD7:10;
    then A23: Gij <> US.1 by A1,A12,A19,A20,JORDAN1G:7;
    then reconsider go as being_S-Seq FinSequence of TOP-REAL 2
                                                            by A9,JORDAN3:70;
    A24: len LS >= 1+2 by JORDAN1E:19;
    then len LS >= 1 by XXREAL_0:2;
    then A25: 1 in dom LS & len LS in dom LS by FINSEQ_3:27;
    then A26: LS.len LS = LS/.len LS by PARTFUN1:def 8
       .= Wmin by JORDAN1F:8;
         Wmin`1 = Wbo by EUCLID:56
       .= G*(1,k)`1 by A11,A17,JORDAN1A:94;
    then A27: Gik <> LS.len LS by A1,A13,A22,A26,JORDAN1G:7;
    then reconsider do as being_S-Seq FinSequence of TOP-REAL 2
                                                            by A8,JORDAN3:69;
    A28: [len G,k] in Indices G by A11,A21,GOBOARD7:10;
    A29: LS.1 = LS/.1 by A25,PARTFUN1:def 8
       .= Emax by JORDAN1F:6;
         Emax`1 = Ebo by EUCLID:56
       .= G*(len G,k)`1 by A11,A17,JORDAN1A:92;
    then A30: Gik <> LS.1 by A1,A13,A28,A29,JORDAN1G:7;
    A31: len go >= 1+1 by TOPREAL1:def 10;
    A32: Gij in rng US by A6,A9,A10,A15,JORDAN1J:40;
    then A33: go is_sequence_on G by A15,JORDAN1J:38;
    A34: len do >= 1+1 by TOPREAL1:def 10;
    A35: Gik in rng LS by A6,A8,A11,A16,JORDAN1J:40;
    then A36: do is_sequence_on G by A16,JORDAN1J:39;
    reconsider go as non constant s.c.c.
            (being_S-Seq FinSequence of TOP-REAL 2) by A31,A33,JGRAPH_1:16
,JORDAN8:8;
    reconsider do as non constant s.c.c.
            (being_S-Seq FinSequence of TOP-REAL 2) by A34,A36,JGRAPH_1:16
,JORDAN8:8;
    A37: len go > 1 by A31,NAT_1:13;
    then A38: len go in dom go by FINSEQ_3:27;
    then A39: go/.len go = go.len go by PARTFUN1:def 8
       .= Gij by A9,JORDAN3:59;
         len do >= 1 by A34,XXREAL_0:2;
    then 1 in dom do by FINSEQ_3:27;
    then A40: do/.1 = do.1 by PARTFUN1:def 8
       .= Gik by A8,JORDAN3:58;
    reconsider m = len go - 1 as Element of NAT by A38,FINSEQ_3:28;
    A41: m+1 = len go;
    then A42: len go-'1 = m by BINARITH:39;
    A43: LSeg(go,m) c= L~go by TOPREAL3:26;
    A44: L~go c= L~US by A9,JORDAN3:76;
    then LSeg(go,m) c= L~US by A43,XBOOLE_1:1;
    then A45: LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> c= {Gij} by A3,A7,XBOOLE_1:26;
         m >= 1 by A31,XREAL_1:21;
    then A46: LSeg(go,m) = LSeg(go/.m,Gij) by A39,A41,TOPREAL1:def 5;
         {Gij} c= LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*>
    proof
     let x be set;
     assume x in {Gij};
     then A47: x = Gij by TARSKI:def 1;
     A48: Gij in LSeg(go,m) by A46,TOPREAL1:6;
          Gij in LSeg(Gij,Gi1k) by TOPREAL1:6;
     then Gij in LSeg(Gij,Gi1k) \/ LSeg(Gi1k,Gik) by XBOOLE_0:def 2;
     then Gij in L~<*Gij,Gi1k,Gik*> by SPRECT_1:10;
     hence x in LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> by A47,A48,XBOOLE_0:def 3;
    end;
    then A49: LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> = {Gij} by A45,XBOOLE_0:def 10;
    A50: LSeg(do,1) c= L~do by TOPREAL3:26;
    A51: L~do c= L~LS by A8,JORDAN3:77;
    then LSeg(do,1) c= L~LS by A50,XBOOLE_1:1;
    then A52: LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*> c= {Gik} by A4,A7,XBOOLE_1:26;
    A53: LSeg(do,1) = LSeg(Gik,do/.(1+1)) by A34,A40,TOPREAL1:def 5;
         {Gik} c= LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*>
    proof
     let x be set;
     assume x in {Gik};
     then A54: x = Gik by TARSKI:def 1;
     A55: Gik in LSeg(do,1) by A53,TOPREAL1:6;
          Gik in LSeg(Gi1k,Gik) by TOPREAL1:6;
     then Gik in LSeg(Gij,Gi1k) \/ LSeg(Gi1k,Gik) by XBOOLE_0:def 2;
     then Gik in L~<*Gij,Gi1k,Gik*> by SPRECT_1:10;
     hence x in LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*> by A54,A55,XBOOLE_0:def 3;
    end;
    then A56: L~<*Gij,Gi1k,Gik*> /\ LSeg(do,1) = {Gik} by A52,XBOOLE_0:def 10;
    A57: go/.1 = US/.1 by A9,SPRECT_3:39
       .= Wmin by JORDAN1F:5;
    then A58: go/.1 = LS/.len LS by JORDAN1F:8
       .= do/.len do by A8,JORDAN1J:35;
    A59: rng go c= L~go & rng do c= L~do by A31,A34,SPPOL_2:18;
    A60: {go/.1} c= L~go /\ L~do
    proof
     let x be set;
     assume x in {go/.1};
     then x = go/.1 by TARSKI:def 1;
     then x in rng go & x in rng do by A58,FINSEQ_6:46,REVROT_1:3;
     hence x in L~go /\ L~do by A59,XBOOLE_0:def 3;
    end;
    A61: LS.1 = LS/.1 by A25,PARTFUN1:def 8
       .= Emax by JORDAN1F:6;
    A62: [len G,j] in Indices G by A10,A21,GOBOARD7:10;
         L~go /\ L~do c= {go/.1}
    proof
     let x be set;
     assume x in L~go /\ L~do;
     then A63: x in L~go & x in L~do by XBOOLE_0:def 3;
     then x in L~US /\ L~LS by A44,A51,XBOOLE_0:def 3;
     then x in {Wmin,Emax} by JORDAN1E:20;
     then A64: x = Wmin or x = Emax by TARSKI:def 2;
          now assume x = Emax;
      then A65: Emax = Gik by A8,A61,A63,JORDAN1E:11;
           G*(len G,j)`1 = Ebo by A10,A17,JORDAN1A:92;
      then Emax`1 <> Ebo by A1,A13,A62,A65,JORDAN1G:7;
      hence contradiction by EUCLID:56;
     end;
     hence x in {go/.1} by A57,A64,TARSKI:def 1;
    end;
    then A66: L~go /\ L~do = {go/.1} by A60,XBOOLE_0:def 10;
    set W2 = go/.2;
    A67: 2 in dom go by A31,FINSEQ_3:27;
    A68: Gij..US >= 1 by A32,FINSEQ_4:31;
    A69: now assume Gik`1 = Wbo;
     then G*(1,k)`1 = G*(i2,k)`1 by A11,A17,JORDAN1A:94;
     hence contradiction by A1,A13,A22,JORDAN1G:7;
    end;
         go = mid(US,1,Gij..US) by A32,JORDAN1G:57
       .= US|(Gij..US) by A68,JORDAN3:25;
    then A70: W2 = US/.2 by A67,FINSEQ_4:85;
    A71: Wmin in rng go by A57,FINSEQ_6:46;
    set pion = <*Gij,Gi1k,Gik*>;
    A72: now let n be Element of NAT;
     assume n in dom pion;
     then n in {1,2,3} by FINSEQ_3:1,30;
     then n = 1 or n = 2 or n = 3 by ENUMSET1:def 1;
     then pion/.n = Gij or pion/.n = Gi1k or pion/.n = Gik by FINSEQ_4:27
;
     hence ex i,j be Element of NAT st [i,j] in Indices G & pion/.n = G*(i,j)
                                                             by A12,A13,A14;
    end;
    A73: Gi1k`1 = G*(i1,1)`1 by A6,A11,GOBOARD5:3
       .= Gij`1 by A6,A10,GOBOARD5:3;
         Gi1k`2 =  G*(1,k)`2 by A6,A11,GOBOARD5:2
       .= Gik`2 by A6,A11,GOBOARD5:2;
    then A74: Gi1k = |[Gij`1,Gik`2]| by A73,EUCLID:57;
    A75: Gi1k in pio by TOPREAL1:6;
    A76: Gi1k in poz by TOPREAL1:6;
         now per cases;
     suppose Gik`1 <> Gij`1 & Gik`2 <> Gij`2;
      then pion is_S-Seq by A74,TOPREAL3:41;
      then consider pion1 be FinSequence of TOP-REAL 2 such that
       A77: pion1 is_sequence_on G and
       A78: pion1 is_S-Seq and
       A79: L~pion = L~pion1 and
       A80: pion/.1 = pion1/.1 and
       A81: pion/.len pion = pion1/.len pion1 and
       A82: len pion <= len pion1 by A72,GOBOARD3:2;
      reconsider pion1 as being_S-Seq FinSequence of TOP-REAL 2 by A78;
      set godo = go^'pion1^'do;
      A83: Gi1k`1 = G*(i1,1)`1 by A6,A11,GOBOARD5:3
         .= Gij`1 by A6,A10,GOBOARD5:3;
      A84: Gik`1 <= Gi1k`1 by A1,A11,JORDAN1A:39;
      then A85: W-bound poz = Gik`1 by SPRECT_1:62;
      A86: W-bound pio = Gij`1 by A83,SPRECT_1:62;
           W-bound (poz \/ pio) = min(W-bound poz, W-bound pio) by SPRECT_1:54
         .= Gik`1 by A83,A84,A85,A86,XXREAL_0:def 8;
      then A87: W-bound L~pion1 = Gik`1 by A79,TOPREAL3:23;
           len Cage(C,n) > 4 by GOBOARD7:36;
      then A88: 1+1 <= len Cage(C,n) by XXREAL_0:2;
      then A89: 1+1 <= len Rotate(Cage(C,n),Wmin) by REVROT_1:14;
           len (go^'pion1) >= len go by TOPREAL8:7;
      then A90: len (go^'pion1) >= 1+1 by A31,XXREAL_0:2;
      then A91: len (go^'pion1) > 1+0 by NAT_1:13;
           len godo >= len (go^'pion1) by TOPREAL8:7;
      then A92: 1+1 <= len godo by A90,XXREAL_0:2;
      A93: US is_sequence_on G by JORDAN1G:4;
      A94: go/.len go = pion1/.1 by A39,A80,FINSEQ_4:27;
      then A95: go^'pion1 is_sequence_on G by A33,A77,TOPREAL8:12;
      A96: (go^'pion1)/.len (go^'pion1) = pion/.len pion by A81,GRAPH_2:58
         .= pion/.3 by FINSEQ_1:62
         .= do/.1 by A40,FINSEQ_4:27;
      then A97: godo is_sequence_on G by A36,A95,TOPREAL8:12;
           LSeg(pion1,1) c= L~pion by A79,TOPREAL3:26;
      then A98: LSeg(go,len go-'1) /\ LSeg(pion1,1) c={Gij}
                                                      by A42,A49,XBOOLE_1:27;
           len pion1 >= 2+1 by A82,FINSEQ_1:62;
      then A99: len pion1 > 1+1 by NAT_1:13;
           {Gij} c= LSeg(go,m) /\ LSeg(pion1,1)
      proof
       let x be set;
       assume x in {Gij};
       then A100: x = Gij by TARSKI:def 1;
       A101: Gij in LSeg(go,m) by A46,TOPREAL1:6;
            Gij in LSeg(pion1,1) by A39,A94,A99,TOPREAL1:27;
       hence x in LSeg(go,m) /\ LSeg(pion1,1) by A100,A101,XBOOLE_0:def 3;
      end;
      then LSeg(go,len go-'1) /\ LSeg(pion1,1) = { go/.len go }
                                              by A39,A42,A98,XBOOLE_0:def 10;
      then A102: go^'pion1 is unfolded by A94,TOPREAL8:34;
           len pion1 >= 2+1 by A82,FINSEQ_1:62;
      then A103: len pion1-2 >= 0 by XREAL_1:21;
           len (go^'pion1)+1-1 = len go+len pion1-1 by GRAPH_2:13;
      then len (go^'pion1)-1 = len go + (len pion1-2)
         .= len go + (len pion1-'2) by A103,BINARITH:def 3;
      then A104: len (go^'pion1)-'1 = len go + (len pion1-'2)
                                                      by BINARITH:def 3;
      A105: len pion1-1 >= 1 by A99,XREAL_1:21;
      then A106: len pion1-'1 = len pion1-1 by BINARITH:def 3;
      A107: len pion1-'2+1 = len pion1-2+1 by A103,BINARITH:def 3
         .= len pion1-'1 by A105,BINARITH:def 3;
           len pion1-1+1 <= len pion1;
      then A108: len pion1-'1 < len pion1 by A106,NAT_1:13;
           LSeg(pion1,len pion1-'1) c= L~pion by A79,TOPREAL3:26;
      then A109: LSeg(pion1,len pion1-'1) /\ LSeg(do,1) c= {Gik}
                                                          by A56,XBOOLE_1:27;
           {Gik} c= LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
      proof
       let x be set;
       assume x in {Gik};
       then A110: x = Gik by TARSKI:def 1;
       A111: Gik in LSeg(do,1) by A53,TOPREAL1:6;
         pion1/.(len pion1-'1+1) = pion/.3 by A81,A106,FINSEQ_1:62
          .= Gik by FINSEQ_4:27;
       then Gik in LSeg(pion1,len pion1-'1) by A105,A106,TOPREAL1:27
;
       hence x in LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
                                                   by A110,A111,XBOOLE_0:def 3;
      end;
      then LSeg(pion1,len pion1-'1) /\ LSeg(do,1) = {Gik}
                                                     by A109,XBOOLE_0:def 10;
      then A112: LSeg(go^'pion1,len go+(len pion1-'2)) /\ LSeg(do,1) =
         {(go^'pion1)/.len (go^'pion1)} by A40,A94,A96,A107,A108,TOPREAL8:31;
      A113: (go^'pion1) is non trivial by A90,REALSET1:13;
      A114: rng pion1 c= L~pion1 by A99,SPPOL_2:18;
      A115: {pion1/.1} c= L~go /\ L~pion1
      proof
       let x be set;
       assume x in {pion1/.1};
       then x = pion1/.1 by TARSKI:def 1;
       then x in rng go & x in rng pion1 by A94,FINSEQ_6:46,REVROT_1:3;
       hence x in L~go /\ L~pion1 by A59,A114,XBOOLE_0:def 3;
      end;
           L~go /\ L~pion1 c= {pion1/.1}
      proof
       let x be set;
       assume x in L~go /\ L~pion1;
       then x in L~go & x in L~pion1 by XBOOLE_0:def 3;
       hence x in {pion1/.1} by A3,A7,A39,A44,A79,A94,XBOOLE_0:def 3;
      end;
      then A116: L~go /\ L~pion1 = {pion1/.1} by A115,XBOOLE_0:def 10;
      then A117: (go^'pion1) is s.n.c. by A94,JORDAN1J:54;
           rng go /\ rng pion1 c= {pion1/.1} by A59,A114,A116,XBOOLE_1:27;
      then A118: go^'pion1 is one-to-one by JORDAN1J:55;
      A119: pion/.len pion = pion/.3 by FINSEQ_1:62
         .= do/.1 by A40,FINSEQ_4:27;
      A120: {pion1/.len pion1} c= L~do /\ L~pion1
      proof
       let x be set;
       assume x in {pion1/.len pion1};
       then x = pion1/.len pion1 by TARSKI:def 1;
       then x in rng do & x in rng pion1 by A81,A119,FINSEQ_6:46,REVROT_1:3;
       hence x in L~do /\ L~pion1 by A59,A114,XBOOLE_0:def 3;
      end;
           L~do /\ L~pion1 c= {pion1/.len pion1}
      proof
       let x be set;
       assume x in L~do /\ L~pion1;
       then x in L~do & x in L~pion1 by XBOOLE_0:def 3;
       hence x in {pion1/.len pion1}
          by A4,A7,A40,A51,A79,A81,A119,XBOOLE_0:def 3;
      end;
      then A121: L~do /\ L~pion1 = {pion1/.len pion1} by A120,XBOOLE_0:def 10;
      A122: L~(go^'pion1) /\ L~do = (L~go \/ L~pion1) /\ L~do by A94,
TOPREAL8:35
         .= {go/.1} \/ {do/.1} by A66,A81,A119,A121,XBOOLE_1:23
         .= {(go^'pion1)/.1} \/ {do/.1} by GRAPH_2:57
         .= {(go^'pion1)/.1,do/.1} by ENUMSET1:41;
           do/.len do = (go^'pion1)/.1 by A58,GRAPH_2:57;
      then reconsider godo as non constant standard special_circular_sequence
          by A92,A96,A97,A102,A104,A112,A113,A117,A118,A122,JORDAN8:7,8
,TOPREAL8:11,33,34
;
      A123: UA is_an_arc_of W-min C,E-max C by JORDAN6:def 8;
      then A124: UA is connected by JORDAN6:11;
      A125: W-min C in UA & E-max C in UA by A123,TOPREAL1:4;
      set ff = Rotate(Cage(C,n),Wmin);
           Wmin in rng Cage(C,n) by SPRECT_2:47;
      then A126: ff/.1 = Wmin by FINSEQ_6:98;
      A127: L~ff = L~Cage(C,n) by REVROT_1:33;
      then A128: (W-max L~ff)..ff > 1 by A126,SPRECT_5:23;
           (W-max L~ff)..ff <= (N-min L~ff)..ff by A126,A127,SPRECT_5:24;
      then A129: (N-min L~ff)..ff > 1 by A128,XXREAL_0:2;
           (N-min L~ff)..ff < (N-max L~ff)..ff by A126,A127,SPRECT_5:25;
      then A130: (N-max L~ff)..ff > 1 by A129,XXREAL_0:2;
           (N-max L~ff)..ff <= (E-max L~ff)..ff by A126,A127,SPRECT_5:26;
      then A131: Emax..ff > 1 by A127,A130,XXREAL_0:2;
      A132: now assume A133: Gij..US <= 1;
            Gij..US >= 1 by A32,FINSEQ_4:31;
       then Gij..US = 1 by A133,XXREAL_0:1;
       then Gij = US/.1 by A32,FINSEQ_5:41;
       hence contradiction by A19,A23,JORDAN1F:5;
      end;
      A134: Cage(C,n) is_sequence_on G by JORDAN9:def 1;
      then A135: ff is_sequence_on G by REVROT_1:34;
      A136: right_cell(godo,1,G)\L~godo c= RightComp godo by A92,A97,JORDAN9:29
;
      A137: L~godo = L~(go^'pion1) \/ L~do by A96,TOPREAL8:35
         .= L~go \/ L~pion1 \/ L~do by A94,TOPREAL8:35;
           L~Cage(C,n) = L~US \/ L~LS by JORDAN1E:17;
      then A138: L~US c= L~Cage(C,n) & L~LS c= L~Cage(C,n) by XBOOLE_1:7;
      then A139: L~go c=L~Cage(C,n) & L~do c=L~Cage(C,n)
                                                     by A44,A51,XBOOLE_1:1;
      A140: W-min C in C by SPRECT_1:15;
      A141: now assume W-min C in L~godo;
       then A142: W-min C in L~go \/ L~pion1 or W-min C in L~do
                                                       by A137,XBOOLE_0:def 2;
       per cases by A142,XBOOLE_0:def 2;
        suppose W-min C in L~go;
         then C meets L~Cage(C,n) by A139,A140,XBOOLE_0:3;
         hence contradiction by JORDAN10:5;
        end;
        suppose W-min C in L~pion1;
         hence contradiction by A5,A7,A79,A125,XBOOLE_0:3;
        end;
        suppose W-min C in L~do;
         then C meets L~Cage(C,n) by A139,A140,XBOOLE_0:3;
         hence contradiction by JORDAN10:5;
      end;
      end;
           right_cell(Rotate(Cage(C,n),Wmin),1) =
            right_cell(ff,1,GoB ff) by A89,JORDAN1H:29
         .= right_cell(ff,1,GoB Cage(C,n)) by REVROT_1:28
         .= right_cell(ff,1,G) by JORDAN1H:52
         .= right_cell(ff-:Emax,1,G) by A131,A135,JORDAN1J:53
         .= right_cell(US,1,G) by JORDAN1E:def 1
         .= right_cell(R_Cut(US,Gij),1,G) by A32,A93,A132,JORDAN1J:52
         .= right_cell(go^'pion1,1,G) by A37,A95,JORDAN1J:51
         .= right_cell(godo,1,G) by A91,A97,JORDAN1J:51;
      then W-min C in right_cell(godo,1,G) by JORDAN1I:8;
      then A143: W-min C in right_cell(godo,1,G)\L~godo by A141,XBOOLE_0:def 4;
      A144: godo/.1 = (go^'pion1)/.1 by GRAPH_2:57
         .= Wmin by A57,GRAPH_2:57;
      A145: len US >= 2 by A18,XXREAL_0:2;
      A146: godo/.2 = (go^'pion1)/.2 by A90,GRAPH_2:61
         .= US/.2 by A31,A70,GRAPH_2:61
         .= (US^'LS)/.2 by A145,GRAPH_2:61
         .= Rotate(Cage(C,n),Wmin)/.2 by JORDAN1E:15;
      A147: L~go \/ L~do is compact by COMPTS_1:19;
      A148: L~go \/ L~do c= L~Cage(C,n) by A139,XBOOLE_1:8;
           Wmin in L~go \/ L~do by A59,A71,XBOOLE_0:def 2;
      then A149: W-min (L~go \/ L~do) = Wmin by A147,A148,JORDAN1J:21;
      A150: (W-min (L~go \/ L~do))`1 = W-bound (L~go \/ L~do) & Wmin`1 = Wbo
                                                              by EUCLID:56;
           Gik`1 >= Wbo by A8,A138,PSCOMP_1:71;
      then Gik`1 > Wbo by A69,REAL_1:def 5;
      then W-min (L~go\/L~do\/L~pion1) = W-min (L~go \/ L~do)
                                              by A87,A147,A149,A150,JORDAN1J:33
;
      then A151: W-min L~godo = Wmin by A137,A149,XBOOLE_1:4;
      A152: rng godo c= L~godo by A92,SPPOL_2:18;
           2 in dom godo by A92,FINSEQ_3:27;
      then A153: godo/.2 in rng godo by PARTFUN2:4;
           godo/.2 in W-most L~Cage(C,n) by A146,JORDAN1I:27;
      then (godo/.2)`1 = (W-min L~godo)`1 by A151,PSCOMP_1:88
         .= W-bound L~godo by EUCLID:56;
      then godo/.2 in W-most L~godo by A152,A153,SPRECT_2:16;
      then Rotate(godo,W-min L~godo)/.2 in W-most L~godo
                                                      by A144,A151,FINSEQ_6:95;
      then reconsider godo as clockwise_oriented non constant standard
                                    special_circular_sequence by JORDAN1I:27;
           len US in dom US by FINSEQ_5:6;
      then A154: US.len US = US/.len US by PARTFUN1:def 8
         .= Emax by JORDAN1F:7;
      A155: E-max C in E-most C by PSCOMP_1:111;
      A156: east_halfline E-max C misses L~go
      proof
       assume east_halfline E-max C meets L~go;
       then consider p be set such that
        A157: p in east_halfline E-max C and
        A158: p in L~go by XBOOLE_0:3;
       reconsider p as Point of TOP-REAL 2 by A157;
            p in L~US by A44,A158;
       then p in east_halfline E-max C /\ L~Cage(C,n)
                    by A138,A157,XBOOLE_0:def 3;
       then A159: p`1 = Ebo by A155,JORDAN1A:104;
       then A160: p = Emax by A44,A158,JORDAN1J:46;
       then Emax = Gij by A9,A154,A158,JORDAN1J:43;
       then Gij`1 = G*(len G,k)`1 by A11,A17,A159,A160,JORDAN1A:92;
       hence contradiction by A1,A12,A28,JORDAN1G:7;
      end;
           now assume east_halfline E-max C meets L~godo;
       then A161: east_halfline E-max C meets (L~go \/ L~pion1) or
                 east_halfline E-max C meets L~do by A137,XBOOLE_1:70;
       per cases by A161,XBOOLE_1:70;
        suppose east_halfline E-max C meets L~go;
         hence contradiction by A156;
        end;
        suppose east_halfline E-max C meets L~pion1;
         then consider p be set such that
          A162: p in east_halfline E-max C and
          A163: p in L~pion1 by XBOOLE_0:3;
         reconsider p as Point of TOP-REAL 2 by A162;
         A164: now per cases by A7,A79,A163,XBOOLE_0:def 2;
          suppose p in poz;
           hence p`1 <= Gij`1 by A83,A84,TOPREAL1:9;
          end;
          suppose p in pio;
           hence p`1 <= Gij`1 by A83,GOBOARD7:5;
         end;
         end;
              i1+1 <= len G by A1,NAT_1:13;
         then i1 <= len G-1 by XREAL_1:21;
         then A165: i1 <= len G-'1 by BINARITH:def 3;
              len G-'1 <= len G by BINARITH:52;
         then Gij`1 <= G*(len G-'1,1)`1 by A6,A10,A17,A21,A165,JORDAN1A:39;
         then p`1 <= G*(len G-'1,1)`1 by A164,XXREAL_0:2;
         then p`1 <= E-bound C by A21,JORDAN8:15;
         then A166: p`1 <= (E-max C)`1 by EUCLID:56;
              p`1 >= (E-max C)`1 by A162,TOPREAL1:def 13;
         then A167: p`1 = (E-max C)`1 by A166,XXREAL_0:1;
              p`2 = (E-max C)`2 by A162,TOPREAL1:def 13;
         then p = E-max C by A167,TOPREAL3:11;
         hence contradiction by A5,A7,A79,A125,A163,XBOOLE_0:3;
        end;
        suppose east_halfline E-max C meets L~do;
         then consider p be set such that
          A168: p in east_halfline E-max C and
          A169: p in L~do by XBOOLE_0:3;
         reconsider p as Point of TOP-REAL 2 by A168;
              p in L~LS by A51,A169;
         then p in east_halfline E-max C /\ L~Cage(C,n)
                      by A138,A168,XBOOLE_0:def 3;
         then A170: p`1 = Ebo by A155,JORDAN1A:104;
         A171: (E-max C)`2 = p`2 by A168,TOPREAL1:def 13;
         set RC = Rotate(Cage(C,n),Emax);
         A172: E-max C in right_cell(RC,1) by JORDAN1I:9;
         A173: 1+1 <= len LS by A24,XXREAL_0:2;
              LS = RC-:Wmin by JORDAN1G:26;
         then A174: LSeg(LS,1) = LSeg(RC,1) by A173,SPPOL_2:9;
         A175: L~RC = L~Cage(C,n) by REVROT_1:33;
         A176: len RC = len Cage(C,n) by REVROT_1:14;
         A177: GoB RC = GoB Cage(C,n) by REVROT_1:28
            .= G by JORDAN1H:52;
         A178: Emax in rng Cage(C,n) by SPRECT_2:50;
         A179: RC is_sequence_on G by A134,REVROT_1:34;
         A180: RC/.1 = E-max L~RC by A175,A178,FINSEQ_6:98;
         then consider ii,jj be Element of NAT such that
          A181: [ii,jj+1] in Indices G and
          A182: [ii,jj] in Indices G and
          A183: RC/.1 = G*(ii,jj+1) and
          A184: RC/.(1+1) = G*(ii,jj) by A88,A176,A179,JORDAN1I:25;
         consider jj2 be Element of NAT such that
          A185: 1 <= jj2 & jj2 <= width G and
          A186: Emax = G*(len G,jj2) by JORDAN1D:29;
         A187: len G >= 4 by JORDAN8:13;
         then len G >= 1 by XXREAL_0:2;
         then [len G,jj2] in Indices G by A185,GOBOARD7:10;
         then A188: ii = len G by A175,A180,A181,A183,A186,GOBOARD1:21;
         A189: 1 <= ii & ii <= len G & 1 <= jj+1 & jj+1 <= width G
                                                           by A181,GOBOARD5:1;
         A190: 1 <= ii & ii <= len G & 1 <= jj & jj <= width G
                                                           by A182,GOBOARD5:1;
         A191: ii+1 <> ii;
          jj+1+1 <> jj;
         then A192: right_cell(RC,1) = cell(G,ii-'1,jj)
                           by A88,A176,A177,A181,A182,A183,A184,A191,
GOBOARD5:def 6;
         A193: ii-'1+1 = ii by A189,BINARITH:53;
          ii-1 >= 4-1 by A187,A188,XREAL_1:11;
         then A194: ii-1 >= 1 by XXREAL_0:2;
         then A195: 1 <= ii-'1 by BINARITH:def 3;
         A196: G*(ii-'1,jj)`2 <= p`2 & p`2 <= G*(ii-'1,jj+1)`2
             by A171,A172,A189,A190,A192,A193,A194,JORDAN9:19;
         A197: ii-'1 < len G by A189,A193,NAT_1:13;
         then A198: G*(ii-'1,jj)`2 = G*(1,jj)`2 by A190,A195,GOBOARD5:2
            .= G*(ii,jj)`2 by A190,GOBOARD5:2;
         A199: G*(ii-'1,jj+1)`2 = G*(1,jj+1)`2 by A189,A195,A197,GOBOARD5:2
            .= G*(ii,jj+1)`2 by A189,GOBOARD5:2;
              G*(len G,jj)`1 = Ebo & Ebo = G*(len G,jj+1)`1
                                                by A17,A189,A190,JORDAN1A:92;
         then p in LSeg(RC/.1,RC/.(1+1))
                              by A170,A183,A184,A188,A196,A198,A199,GOBOARD7:8;
         then A200: p in LSeg(LS,1) by A88,A174,A176,TOPREAL1:def 5;
         A201: p in LSeg(do,Index(p,do)) by A169,JORDAN3:42;
         A202: do = mid(LS,Gik..LS,len LS) by A35,JORDAN1J:37;
         A203: 1<=Gik..LS & Gik..LS<=len LS by A35,FINSEQ_4:31;
              Gik..LS <> len LS by A27,A35,FINSEQ_4:29;
         then A204: Gik..LS < len LS by A203,REAL_1:def 5;
         A205: 1<=Index(p,do) & Index(p,do) < len do by A169,JORDAN3:41;
         A206: Index(Gik,LS)+1 = Gik..LS by A30,A35,JORDAN1J:56;
         consider t be Nat such that
          A207: t in dom LS and
          A208: LS.t = Gik by A35,FINSEQ_2:11;
         A209: 1 <= t & t <= len LS by A207,FINSEQ_3:27;
         then 1 < t by A30,A208,REAL_1:def 5;
         then Index(Gik,LS)+1 = t by A208,A209,JORDAN3:45;
         then A210: len L_Cut(LS,Gik) = len LS-Index(Gik,LS)
                                                  by A8,A208,JORDAN3:61;
         set tt = Index(p,do)+(Gik..LS)-'1;
         A211: 1<=Index(Gik,LS) & 0+Index(Gik,LS) < len LS by A8,JORDAN3:41;
         then A212: len LS-Index(Gik,LS) > 0 by XREAL_1:22;
          Index(p,do) < len LS-'Index(Gik,LS) by A205,A210,BINARITH:def 3;
         then Index(p,do)+1 <= len LS-'Index(Gik,LS) by NAT_1:13;
         then Index(p,do) <= len LS-'Index(Gik,LS)-1 by XREAL_1:21;
         then Index(p,do) <= len LS-Index(Gik,LS)-1 by A212,BINARITH:def 3;
         then Index(p,do) <= len LS-Gik..LS by A206;
         then Index(p,do) <= len LS-'Gik..LS by BINARITH:def 3;
         then Index(p,do) < len LS-'(Gik..LS)+1 by NAT_1:13;
         then A213: LSeg(mid(LS,Gik..LS,len LS),Index(p,do)) =
              LSeg(LS,Index(p,do)+(Gik..LS)-'1) by A203,A204,A205,JORDAN4:31;
         A214: 1+1 <= Gik..LS by A206,A211,XREAL_1:9;
         then Index(p,do)+Gik..LS >= 1+1+1 by A205,XREAL_1:9;
         then A215: Index(p,do)+Gik..LS-1 >= 1+1+1-1 by XREAL_1:11;
         then A216: tt >= 1+1 by BINARITH:def 3;
         A217: 2 in dom LS by A173,FINSEQ_3:27;
              now per cases by A216,REAL_1:def 5;
          suppose tt > 1+1;
           then LSeg(LS,1) misses LSeg(LS,tt) by TOPREAL1:def 9;
           hence contradiction by A200,A201,A202,A213,XBOOLE_0:3;
          end;
          suppose A218: tt = 1+1;
           then LSeg(LS,1) /\ LSeg(LS,tt) = {LS/.2} by A24,TOPREAL1:def 8
;
           then p in {LS/.2} by A200,A201,A202,A213,XBOOLE_0:def 3;
           then A219: p = LS/.2 by TARSKI:def 1;
           then A220: p..LS = 2 by A217,FINSEQ_5:44;
                1+1 = Index(p,do)+(Gik..LS)-1 by A215,A218,BINARITH:def 3;
           then 1+1+1 = Index(p,do)+(Gik..LS);
           then A221: Gik..LS = 2 by A205,A214,JORDAN1E:10;
                p in rng LS by A217,A219,PARTFUN2:4;
           then p = Gik by A35,A220,A221,FINSEQ_5:10;
           then Gik`1 = Ebo by A219,JORDAN1G:40;
           then Gik`1 = G*(len G,j)`1 by A10,A17,JORDAN1A:92;
           hence contradiction by A1,A13,A62,JORDAN1G:7;
         end;
         end;
         hence contradiction;
      end;
      end;
      then east_halfline E-max C c= (L~godo)` by SUBSET_1:43;
      then consider W be Subset of TOP-REAL 2 such that
       A222: W is_a_component_of (L~godo)` and
       A223: east_halfline E-max C c= W by GOBOARD9:5;
           east_halfline E-max C is not Bounded by JORDAN2C:129;
      then W is not Bounded by A223,JORDAN2C:16;
      then W is_outside_component_of L~godo by A222,JORDAN2C:def 4;
      then W c= UBD L~godo by JORDAN2C:27;
      then A224: east_halfline E-max C c= UBD L~godo by A223,XBOOLE_1:1;
           E-max C in east_halfline E-max C by TOPREAL1:45;
      then E-max C in UBD L~godo by A224;
      then E-max C in LeftComp godo by GOBRD14:46;
      then UA meets L~godo by A124,A125,A136,A143,JORDAN1J:36;
      then A225: UA meets (L~go \/ L~pion1) or UA meets L~do by A137,
XBOOLE_1:70;
      A226: UA c= C by JORDAN6:76;
           now per cases by A225,XBOOLE_1:70;
       suppose UA meets L~go;
        then UA meets L~Cage(C,n) by A139,XBOOLE_1:63;
        then C meets L~Cage(C,n) by A226,XBOOLE_1:63;
        hence contradiction by JORDAN10:5;
       end;
       suppose UA meets L~pion1;
        hence contradiction by A5,A7,A79;
       end;
       suppose UA meets L~do;
        then UA meets L~Cage(C,n) by A139,XBOOLE_1:63;
        then C meets L~Cage(C,n) by A226,XBOOLE_1:63;
        hence contradiction by JORDAN10:5;
      end;
      end;
      hence contradiction;
    end;
    suppose Gik`1 = Gij`1;
      then A227: i1 = i2 by A12,A13,JORDAN1G:7;
      then poz = {Gi1k} by TOPREAL1:7;
      then poz c= pio by A75,ZFMISC_1:37;
      then pio \/ poz = pio by XBOOLE_1:12;
      hence contradiction by A1,A2,A3,A4,A5,A227,Th13;
     end;
     suppose Gik`2 = Gij`2;
      then A228: j = k by A12,A13,JORDAN1G:6;
      then pio = {Gi1k} by TOPREAL1:7;
      then pio c= poz by A76,ZFMISC_1:37;
      then pio \/ poz = poz by XBOOLE_1:12;
      hence contradiction by A1,A2,A3,A4,A5,A228,JORDAN15:31;
    end;
    end;
    hence contradiction;
   end;

  theorem Th24:
   for C be Simple_closed_curve
   for i1,i2,j,k be Element of NAT st
    1 < i2 & i2 <= i1 & i1 < len Gauge(C,n) &
    1 <= j & j <= k & k <= width Gauge(C,n) &
    (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
     LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) /\ L~Upper_Seq(C,n) =
     {Gauge(C,n)*(i1,j)} &
    (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
     LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) /\ L~Lower_Seq(C,n) =
     {Gauge(C,n)*(i2,k)} holds
      (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
       LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) meets Lower_Arc C
   proof
    let C be Simple_closed_curve;
    let i1,i2,j,k be Element of NAT;
    set G = Gauge(C,n);
    set pio = LSeg(G*(i1,j),G*(i1,k));
    set poz = LSeg(G*(i1,k),G*(i2,k));
    set US = Upper_Seq(C,n);
    set LS = Lower_Seq(C,n);
    assume that
     A1: 1 < i2 & i2 <= i1 & i1 < len G and
     A2: 1 <= j & j <= k & k <= width G and
     A3: (pio \/ poz) /\ L~US = {G*(i1,j)} and
     A4: (pio \/ poz) /\ L~LS = {G*(i2,k)} and
     A5: (pio \/ poz) misses Lower_Arc C;
    set UA = Lower_Arc C;
    set Wmin = W-min L~Cage(C,n);
    set Emax = E-max L~Cage(C,n);
    set Wbo = W-bound L~Cage(C,n);
    set Ebo = E-bound L~Cage(C,n);
    set Gik = G*(i2,k);
    set Gij = G*(i1,j);
    set Gi1k = G*(i1,k);
    A6: 1 < i1 & i1 < len G & 1 < i2 & i2 < len G by A1,XXREAL_0:2;
    A7: L~<*Gij,Gi1k,Gik*> = poz \/ pio by TOPREAL3:23;
         Gik in {Gik} by TARSKI:def 1;
    then A8: Gik in L~LS by A4,XBOOLE_0:def 3;
         Gij in {Gij} by TARSKI:def 1;
    then A9: Gij in L~US by A3,XBOOLE_0:def 3;
    A10: 1 <= j & j <= width G by A2,XXREAL_0:2;
    A11: 1 <= k & k <= width G by A2,XXREAL_0:2;
    A12: [i1,j] in Indices G by A6,A10,GOBOARD7:10;
    A13: [i2,k] in Indices G by A6,A11,GOBOARD7:10;
    A14: [i1,k] in Indices G by A6,A11,GOBOARD7:10;
    A15: US is_sequence_on G by JORDAN1G:4;
    A16: LS is_sequence_on G by JORDAN1G:5;
    set go = R_Cut(US,Gij);
    set do = L_Cut(LS,Gik);
    A17: len G = width G by JORDAN8:def 1;
    A18: len US >= 3 by JORDAN1E:19;
    then len US >= 1 by XXREAL_0:2;
    then 1 in dom US by FINSEQ_3:27;
    then A19: US.1 = US/.1 by PARTFUN1:def 8
       .= Wmin by JORDAN1F:5;
    A20: Wmin`1 = Wbo by EUCLID:56
       .= G*(1,k)`1 by A11,A17,JORDAN1A:94;
         len G >= 4 by JORDAN8:13;
    then A21: len G >= 1 by XXREAL_0:2;
    then A22: [1,k] in Indices G by A11,GOBOARD7:10;
    then A23: Gij <> US.1 by A1,A12,A19,A20,JORDAN1G:7;
    then reconsider go as being_S-Seq FinSequence of TOP-REAL 2
                                                            by A9,JORDAN3:70;
    A24: len LS >= 1+2 by JORDAN1E:19;
    then len LS >= 1 by XXREAL_0:2;
    then A25: 1 in dom LS & len LS in dom LS by FINSEQ_3:27;
    then A26: LS.len LS = LS/.len LS by PARTFUN1:def 8
       .= Wmin by JORDAN1F:8;
         Wmin`1 = Wbo by EUCLID:56
       .= G*(1,k)`1 by A11,A17,JORDAN1A:94;
    then A27: Gik <> LS.len LS by A1,A13,A22,A26,JORDAN1G:7;
    then reconsider do as being_S-Seq FinSequence of TOP-REAL 2
                                                            by A8,JORDAN3:69;
    A28: [len G,k] in Indices G by A11,A21,GOBOARD7:10;
    A29: LS.1 = LS/.1 by A25,PARTFUN1:def 8
       .= Emax by JORDAN1F:6;
         Emax`1 = Ebo by EUCLID:56
       .= G*(len G,k)`1 by A11,A17,JORDAN1A:92;
    then A30: Gik <> LS.1 by A1,A13,A28,A29,JORDAN1G:7;
    A31: len go >= 1+1 by TOPREAL1:def 10;
    A32: Gij in rng US by A6,A9,A10,A15,JORDAN1J:40;
    then A33: go is_sequence_on G by A15,JORDAN1J:38;
    A34: len do >= 1+1 by TOPREAL1:def 10;
    A35: Gik in rng LS by A6,A8,A11,A16,JORDAN1J:40;
    then A36: do is_sequence_on G by A16,JORDAN1J:39;
    reconsider go as non constant s.c.c.
            (being_S-Seq FinSequence of TOP-REAL 2) by A31,A33,JGRAPH_1:16
,JORDAN8:8;
    reconsider do as non constant s.c.c.
            (being_S-Seq FinSequence of TOP-REAL 2) by A34,A36,JGRAPH_1:16
,JORDAN8:8;
    A37: len go > 1 by A31,NAT_1:13;
    then A38: len go in dom go by FINSEQ_3:27;
    then A39: go/.len go = go.len go by PARTFUN1:def 8
       .= Gij by A9,JORDAN3:59;
         len do >= 1 by A34,XXREAL_0:2;
    then 1 in dom do by FINSEQ_3:27;
    then A40: do/.1 = do.1 by PARTFUN1:def 8
       .= Gik by A8,JORDAN3:58;
    reconsider m = len go - 1 as Element of NAT by A38,FINSEQ_3:28;
    A41: m+1 = len go;
    then A42: len go-'1 = m by BINARITH:39;
    A43: LSeg(go,m) c= L~go by TOPREAL3:26;
    A44: L~go c= L~US by A9,JORDAN3:76;
    then LSeg(go,m) c= L~US by A43,XBOOLE_1:1;
    then A45: LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> c= {Gij} by A3,A7,XBOOLE_1:26;
         m >= 1 by A31,XREAL_1:21;
    then A46: LSeg(go,m) = LSeg(go/.m,Gij) by A39,A41,TOPREAL1:def 5;
         {Gij} c= LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*>
    proof
     let x be set;
     assume x in {Gij};
     then A47: x = Gij by TARSKI:def 1;
     A48: Gij in LSeg(go,m) by A46,TOPREAL1:6;
          Gij in LSeg(Gij,Gi1k) by TOPREAL1:6;
     then Gij in LSeg(Gij,Gi1k) \/ LSeg(Gi1k,Gik) by XBOOLE_0:def 2;
     then Gij in L~<*Gij,Gi1k,Gik*> by SPRECT_1:10;
     hence x in LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> by A47,A48,XBOOLE_0:def 3;
    end;
    then A49: LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> = {Gij} by A45,XBOOLE_0:def 10;
    A50: LSeg(do,1) c= L~do by TOPREAL3:26;
    A51: L~do c= L~LS by A8,JORDAN3:77;
    then LSeg(do,1) c= L~LS by A50,XBOOLE_1:1;
    then A52: LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*> c= {Gik} by A4,A7,XBOOLE_1:26;
    A53: LSeg(do,1) = LSeg(Gik,do/.(1+1)) by A34,A40,TOPREAL1:def 5;
         {Gik} c= LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*>
    proof
     let x be set;
     assume x in {Gik};
     then A54: x = Gik by TARSKI:def 1;
     A55: Gik in LSeg(do,1) by A53,TOPREAL1:6;
          Gik in LSeg(Gi1k,Gik) by TOPREAL1:6;
     then Gik in LSeg(Gij,Gi1k) \/ LSeg(Gi1k,Gik) by XBOOLE_0:def 2;
     then Gik in L~<*Gij,Gi1k,Gik*> by SPRECT_1:10;
     hence x in LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*> by A54,A55,XBOOLE_0:def 3;
    end;
    then A56: L~<*Gij,Gi1k,Gik*> /\ LSeg(do,1) = {Gik} by A52,XBOOLE_0:def 10;
    A57: go/.1 = US/.1 by A9,SPRECT_3:39
       .= Wmin by JORDAN1F:5;
    then A58: go/.1 = LS/.len LS by JORDAN1F:8
       .= do/.len do by A8,JORDAN1J:35;
    A59: rng go c= L~go & rng do c= L~do by A31,A34,SPPOL_2:18;
    A60: {go/.1} c= L~go /\ L~do
    proof
     let x be set;
     assume x in {go/.1};
     then x = go/.1 by TARSKI:def 1;
     then x in rng go & x in rng do by A58,FINSEQ_6:46,REVROT_1:3;
     hence x in L~go /\ L~do by A59,XBOOLE_0:def 3;
    end;
    A61: LS.1 = LS/.1 by A25,PARTFUN1:def 8
       .= Emax by JORDAN1F:6;
    A62: [len G,j] in Indices G by A10,A21,GOBOARD7:10;
         L~go /\ L~do c= {go/.1}
    proof
     let x be set;
     assume x in L~go /\ L~do;
     then A63: x in L~go & x in L~do by XBOOLE_0:def 3;
     then x in L~US /\ L~LS by A44,A51,XBOOLE_0:def 3;
     then x in {Wmin,Emax} by JORDAN1E:20;
     then A64: x = Wmin or x = Emax by TARSKI:def 2;
          now assume x = Emax;
      then A65: Emax = Gik by A8,A61,A63,JORDAN1E:11;
           G*(len G,j)`1 = Ebo by A10,A17,JORDAN1A:92;
      then Emax`1 <> Ebo by A1,A13,A62,A65,JORDAN1G:7;
      hence contradiction by EUCLID:56;
     end;
     hence x in {go/.1} by A57,A64,TARSKI:def 1;
    end;
    then A66: L~go /\ L~do = {go/.1} by A60,XBOOLE_0:def 10;
    set W2 = go/.2;
    A67: 2 in dom go by A31,FINSEQ_3:27;
    A68: Gij..US >= 1 by A32,FINSEQ_4:31;
    A69: now assume Gik`1 = Wbo;
     then G*(1,k)`1 = G*(i2,k)`1 by A11,A17,JORDAN1A:94;
     hence contradiction by A1,A13,A22,JORDAN1G:7;
    end;
         go = mid(US,1,Gij..US) by A32,JORDAN1G:57
       .= US|(Gij..US) by A68,JORDAN3:25;
    then A70: W2 = US/.2 by A67,FINSEQ_4:85;
    A71: Wmin in rng go by A57,FINSEQ_6:46;
    set pion = <*Gij,Gi1k,Gik*>;
    A72: now let n be Element of NAT;
     assume n in dom pion;
     then n in {1,2,3} by FINSEQ_3:1,30;
     then n = 1 or n = 2 or n = 3 by ENUMSET1:def 1;
     then pion/.n = Gij or pion/.n = Gi1k or pion/.n = Gik by FINSEQ_4:27
;
     hence ex i,j be Element of NAT st [i,j] in Indices G & pion/.n = G*(i,j)
                                                             by A12,A13,A14;
    end;
    A73: Gi1k`1 = G*(i1,1)`1 by A6,A11,GOBOARD5:3
       .= Gij`1 by A6,A10,GOBOARD5:3;
         Gi1k`2 =  G*(1,k)`2 by A6,A11,GOBOARD5:2
       .= Gik`2 by A6,A11,GOBOARD5:2;
    then A74: Gi1k = |[Gij`1,Gik`2]| by A73,EUCLID:57;
    A75: Gi1k in pio by TOPREAL1:6;
    A76: Gi1k in poz by TOPREAL1:6;
         now per cases;
     suppose Gik`1 <> Gij`1 & Gik`2 <> Gij`2;
      then pion is_S-Seq by A74,TOPREAL3:41;
      then consider pion1 be FinSequence of TOP-REAL 2 such that
       A77: pion1 is_sequence_on G and
       A78: pion1 is_S-Seq and
       A79: L~pion = L~pion1 and
       A80: pion/.1 = pion1/.1 and
       A81: pion/.len pion = pion1/.len pion1 and
       A82: len pion <= len pion1 by A72,GOBOARD3:2;
      reconsider pion1 as being_S-Seq FinSequence of TOP-REAL 2 by A78;
      set godo = go^'pion1^'do;
      A83: Gi1k`1 = G*(i1,1)`1 by A6,A11,GOBOARD5:3
         .= Gij`1 by A6,A10,GOBOARD5:3;
      A84: Gik`1 <= Gi1k`1 by A1,A11,JORDAN1A:39;
      then A85: W-bound poz = Gik`1 by SPRECT_1:62;
      A86: W-bound pio = Gij`1 by A83,SPRECT_1:62;
           W-bound (poz \/ pio) = min(W-bound poz, W-bound pio) by SPRECT_1:54
         .= Gik`1 by A83,A84,A85,A86,XXREAL_0:def 8;
      then A87: W-bound L~pion1 = Gik`1 by A79,TOPREAL3:23;
           len Cage(C,n) > 4 by GOBOARD7:36;
      then A88: 1+1 <= len Cage(C,n) by XXREAL_0:2;
      then A89: 1+1 <= len Rotate(Cage(C,n),Wmin) by REVROT_1:14;
           len (go^'pion1) >= len go by TOPREAL8:7;
      then A90: len (go^'pion1) >= 1+1 by A31,XXREAL_0:2;
      then A91: len (go^'pion1) > 1+0 by NAT_1:13;
           len godo >= len (go^'pion1) by TOPREAL8:7;
      then A92: 1+1 <= len godo by A90,XXREAL_0:2;
      A93: US is_sequence_on G by JORDAN1G:4;
      A94: go/.len go = pion1/.1 by A39,A80,FINSEQ_4:27;
      then A95: go^'pion1 is_sequence_on G by A33,A77,TOPREAL8:12;
      A96: (go^'pion1)/.len (go^'pion1) = pion/.len pion by A81,GRAPH_2:58
         .= pion/.3 by FINSEQ_1:62
         .= do/.1 by A40,FINSEQ_4:27;
      then A97: godo is_sequence_on G by A36,A95,TOPREAL8:12;
           LSeg(pion1,1) c= L~pion by A79,TOPREAL3:26;
      then A98: LSeg(go,len go-'1) /\ LSeg(pion1,1) c={Gij}
                                                      by A42,A49,XBOOLE_1:27;
           len pion1 >= 2+1 by A82,FINSEQ_1:62;
      then A99: len pion1 > 1+1 by NAT_1:13;
           {Gij} c= LSeg(go,m) /\ LSeg(pion1,1)
      proof
       let x be set;
       assume x in {Gij};
       then A100: x = Gij by TARSKI:def 1;
       A101: Gij in LSeg(go,m) by A46,TOPREAL1:6;
            Gij in LSeg(pion1,1) by A39,A94,A99,TOPREAL1:27;
       hence x in LSeg(go,m) /\ LSeg(pion1,1) by A100,A101,XBOOLE_0:def 3;
      end;
      then LSeg(go,len go-'1) /\ LSeg(pion1,1) = { go/.len go }
                                              by A39,A42,A98,XBOOLE_0:def 10;
      then A102: go^'pion1 is unfolded by A94,TOPREAL8:34;
           len pion1 >= 2+1 by A82,FINSEQ_1:62;
      then A103: len pion1-2 >= 0 by XREAL_1:21;
           len (go^'pion1)+1-1 = len go+len pion1-1 by GRAPH_2:13;
      then len (go^'pion1)-1 = len go + (len pion1-2)
         .= len go + (len pion1-'2) by A103,BINARITH:def 3;
      then A104: len (go^'pion1)-'1 = len go + (len pion1-'2)
                                                      by BINARITH:def 3;
      A105: len pion1-1 >= 1 by A99,XREAL_1:21;
      then A106: len pion1-'1 = len pion1-1 by BINARITH:def 3;
      A107: len pion1-'2+1 = len pion1-2+1 by A103,BINARITH:def 3
         .= len pion1-'1 by A105,BINARITH:def 3;
           len pion1-1+1 <= len pion1;
      then A108: len pion1-'1 < len pion1 by A106,NAT_1:13;
           LSeg(pion1,len pion1-'1) c= L~pion by A79,TOPREAL3:26;
      then A109: LSeg(pion1,len pion1-'1) /\ LSeg(do,1) c= {Gik}
                                                          by A56,XBOOLE_1:27;
           {Gik} c= LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
      proof
       let x be set;
       assume x in {Gik};
       then A110: x = Gik by TARSKI:def 1;
       A111: Gik in LSeg(do,1) by A53,TOPREAL1:6;
         pion1/.(len pion1-'1+1) = pion/.3 by A81,A106,FINSEQ_1:62
          .= Gik by FINSEQ_4:27;
       then Gik in LSeg(pion1,len pion1-'1) by A105,A106,TOPREAL1:27
;
       hence x in LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
                                                   by A110,A111,XBOOLE_0:def 3;
      end;
      then LSeg(pion1,len pion1-'1) /\ LSeg(do,1) = {Gik}
                                                     by A109,XBOOLE_0:def 10;
      then A112: LSeg(go^'pion1,len go+(len pion1-'2)) /\ LSeg(do,1) =
         {(go^'pion1)/.len (go^'pion1)} by A40,A94,A96,A107,A108,TOPREAL8:31;
      A113: (go^'pion1) is non trivial by A90,REALSET1:13;
      A114: rng pion1 c= L~pion1 by A99,SPPOL_2:18;
      A115: {pion1/.1} c= L~go /\ L~pion1
      proof
       let x be set;
       assume x in {pion1/.1};
       then x = pion1/.1 by TARSKI:def 1;
       then x in rng go & x in rng pion1 by A94,FINSEQ_6:46,REVROT_1:3;
       hence x in L~go /\ L~pion1 by A59,A114,XBOOLE_0:def 3;
      end;
           L~go /\ L~pion1 c= {pion1/.1}
      proof
       let x be set;
       assume x in L~go /\ L~pion1;
       then x in L~go & x in L~pion1 by XBOOLE_0:def 3;
       hence x in {pion1/.1} by A3,A7,A39,A44,A79,A94,XBOOLE_0:def 3;
      end;
      then A116: L~go /\ L~pion1 = {pion1/.1} by A115,XBOOLE_0:def 10;
      then A117: (go^'pion1) is s.n.c. by A94,JORDAN1J:54;
           rng go /\ rng pion1 c= {pion1/.1} by A59,A114,A116,XBOOLE_1:27;
      then A118: go^'pion1 is one-to-one by JORDAN1J:55;
      A119: pion/.len pion = pion/.3 by FINSEQ_1:62
         .= do/.1 by A40,FINSEQ_4:27;
      A120: {pion1/.len pion1} c= L~do /\ L~pion1
      proof
       let x be set;
       assume x in {pion1/.len pion1};
       then x = pion1/.len pion1 by TARSKI:def 1;
       then x in rng do & x in rng pion1 by A81,A119,FINSEQ_6:46,REVROT_1:3;
       hence x in L~do /\ L~pion1 by A59,A114,XBOOLE_0:def 3;
      end;
           L~do /\ L~pion1 c= {pion1/.len pion1}
      proof
       let x be set;
       assume x in L~do /\ L~pion1;
       then x in L~do & x in L~pion1 by XBOOLE_0:def 3;
       hence x in {pion1/.len pion1}
          by A4,A7,A40,A51,A79,A81,A119,XBOOLE_0:def 3;
      end;
      then A121: L~do /\ L~pion1 = {pion1/.len pion1} by A120,XBOOLE_0:def 10;
      A122: L~(go^'pion1) /\ L~do = (L~go \/ L~pion1) /\ L~do by A94,
TOPREAL8:35
         .= {go/.1} \/ {do/.1} by A66,A81,A119,A121,XBOOLE_1:23
         .= {(go^'pion1)/.1} \/ {do/.1} by GRAPH_2:57
         .= {(go^'pion1)/.1,do/.1} by ENUMSET1:41;
           do/.len do = (go^'pion1)/.1 by A58,GRAPH_2:57;
      then reconsider godo as non constant standard special_circular_sequence
          by A92,A96,A97,A102,A104,A112,A113,A117,A118,A122,JORDAN8:7,8
,TOPREAL8:11,33,34
;
      A123: UA is_an_arc_of E-max C,W-min C by JORDAN6:def 9;
      then A124: UA is connected by JORDAN6:11;
      A125: W-min C in UA & E-max C in UA by A123,TOPREAL1:4;
      set ff = Rotate(Cage(C,n),Wmin);
           Wmin in rng Cage(C,n) by SPRECT_2:47;
      then A126: ff/.1 = Wmin by FINSEQ_6:98;
      A127: L~ff = L~Cage(C,n) by REVROT_1:33;
      then A128: (W-max L~ff)..ff > 1 by A126,SPRECT_5:23;
           (W-max L~ff)..ff <= (N-min L~ff)..ff by A126,A127,SPRECT_5:24;
      then A129: (N-min L~ff)..ff > 1 by A128,XXREAL_0:2;
           (N-min L~ff)..ff < (N-max L~ff)..ff by A126,A127,SPRECT_5:25;
      then A130: (N-max L~ff)..ff > 1 by A129,XXREAL_0:2;
           (N-max L~ff)..ff <= (E-max L~ff)..ff by A126,A127,SPRECT_5:26;
      then A131: Emax..ff > 1 by A127,A130,XXREAL_0:2;
      A132: now assume A133: Gij..US <= 1;
            Gij..US >= 1 by A32,FINSEQ_4:31;
       then Gij..US = 1 by A133,XXREAL_0:1;
       then Gij = US/.1 by A32,FINSEQ_5:41;
       hence contradiction by A19,A23,JORDAN1F:5;
      end;
      A134: Cage(C,n) is_sequence_on G by JORDAN9:def 1;
      then A135: ff is_sequence_on G by REVROT_1:34;
      A136: right_cell(godo,1,G)\L~godo c= RightComp godo by A92,A97,JORDAN9:29
;
      A137: L~godo = L~(go^'pion1) \/ L~do by A96,TOPREAL8:35
         .= L~go \/ L~pion1 \/ L~do by A94,TOPREAL8:35;
           L~Cage(C,n) = L~US \/ L~LS by JORDAN1E:17;
      then A138: L~US c= L~Cage(C,n) & L~LS c= L~Cage(C,n) by XBOOLE_1:7;
      then A139: L~go c=L~Cage(C,n) & L~do c=L~Cage(C,n)
                                                     by A44,A51,XBOOLE_1:1;
      A140: W-min C in C by SPRECT_1:15;
      A141: now assume W-min C in L~godo;
       then A142: W-min C in L~go \/ L~pion1 or W-min C in L~do
                                                       by A137,XBOOLE_0:def 2;
       per cases by A142,XBOOLE_0:def 2;
        suppose W-min C in L~go;
         then C meets L~Cage(C,n) by A139,A140,XBOOLE_0:3;
         hence contradiction by JORDAN10:5;
        end;
        suppose W-min C in L~pion1;
         hence contradiction by A5,A7,A79,A125,XBOOLE_0:3;
        end;
        suppose W-min C in L~do;
         then C meets L~Cage(C,n) by A139,A140,XBOOLE_0:3;
         hence contradiction by JORDAN10:5;
      end;
      end;
           right_cell(Rotate(Cage(C,n),Wmin),1) =
            right_cell(ff,1,GoB ff) by A89,JORDAN1H:29
         .= right_cell(ff,1,GoB Cage(C,n)) by REVROT_1:28
         .= right_cell(ff,1,G) by JORDAN1H:52
         .= right_cell(ff-:Emax,1,G) by A131,A135,JORDAN1J:53
         .= right_cell(US,1,G) by JORDAN1E:def 1
         .= right_cell(R_Cut(US,Gij),1,G) by A32,A93,A132,JORDAN1J:52
         .= right_cell(go^'pion1,1,G) by A37,A95,JORDAN1J:51
         .= right_cell(godo,1,G) by A91,A97,JORDAN1J:51;
      then W-min C in right_cell(godo,1,G) by JORDAN1I:8;
      then A143: W-min C in right_cell(godo,1,G)\L~godo by A141,XBOOLE_0:def 4;
      A144: godo/.1 = (go^'pion1)/.1 by GRAPH_2:57
         .= Wmin by A57,GRAPH_2:57;
      A145: len US >= 2 by A18,XXREAL_0:2;
      A146: godo/.2 = (go^'pion1)/.2 by A90,GRAPH_2:61
         .= US/.2 by A31,A70,GRAPH_2:61
         .= (US^'LS)/.2 by A145,GRAPH_2:61
         .= Rotate(Cage(C,n),Wmin)/.2 by JORDAN1E:15;
      A147: L~go \/ L~do is compact by COMPTS_1:19;
      A148: L~go \/ L~do c= L~Cage(C,n) by A139,XBOOLE_1:8;
           Wmin in L~go \/ L~do by A59,A71,XBOOLE_0:def 2;
      then A149: W-min (L~go \/ L~do) = Wmin by A147,A148,JORDAN1J:21;
      A150: (W-min (L~go \/ L~do))`1 = W-bound (L~go \/ L~do) & Wmin`1 = Wbo
                                                              by EUCLID:56;
           Gik`1 >= Wbo by A8,A138,PSCOMP_1:71;
      then Gik`1 > Wbo by A69,REAL_1:def 5;
      then W-min (L~go\/L~do\/L~pion1) = W-min (L~go \/ L~do)
                                              by A87,A147,A149,A150,JORDAN1J:33
;
      then A151: W-min L~godo = Wmin by A137,A149,XBOOLE_1:4;
      A152: rng godo c= L~godo by A92,SPPOL_2:18;
           2 in dom godo by A92,FINSEQ_3:27;
      then A153: godo/.2 in rng godo by PARTFUN2:4;
           godo/.2 in W-most L~Cage(C,n) by A146,JORDAN1I:27;
      then (godo/.2)`1 = (W-min L~godo)`1 by A151,PSCOMP_1:88
         .= W-bound L~godo by EUCLID:56;
      then godo/.2 in W-most L~godo by A152,A153,SPRECT_2:16;
      then Rotate(godo,W-min L~godo)/.2 in W-most L~godo
                                                      by A144,A151,FINSEQ_6:95;
      then reconsider godo as clockwise_oriented non constant standard
                                    special_circular_sequence by JORDAN1I:27;
           len US in dom US by FINSEQ_5:6;
      then A154: US.len US = US/.len US by PARTFUN1:def 8
         .= Emax by JORDAN1F:7;
      A155: E-max C in E-most C by PSCOMP_1:111;
      A156: east_halfline E-max C misses L~go
      proof
       assume east_halfline E-max C meets L~go;
       then consider p be set such that
        A157: p in east_halfline E-max C and
        A158: p in L~go by XBOOLE_0:3;
       reconsider p as Point of TOP-REAL 2 by A157;
            p in L~US by A44,A158;
       then p in east_halfline E-max C /\ L~Cage(C,n)
                    by A138,A157,XBOOLE_0:def 3;
       then A159: p`1 = Ebo by A155,JORDAN1A:104;
       then A160: p = Emax by A44,A158,JORDAN1J:46;
       then Emax = Gij by A9,A154,A158,JORDAN1J:43;
       then Gij`1 = G*(len G,k)`1 by A11,A17,A159,A160,JORDAN1A:92;
       hence contradiction by A1,A12,A28,JORDAN1G:7;
      end;
           now assume east_halfline E-max C meets L~godo;
       then A161: east_halfline E-max C meets (L~go \/ L~pion1) or
                 east_halfline E-max C meets L~do by A137,XBOOLE_1:70;
       per cases by A161,XBOOLE_1:70;
        suppose east_halfline E-max C meets L~go;
         hence contradiction by A156;
        end;
        suppose east_halfline E-max C meets L~pion1;
         then consider p be set such that
          A162: p in east_halfline E-max C and
          A163: p in L~pion1 by XBOOLE_0:3;
         reconsider p as Point of TOP-REAL 2 by A162;
         A164: now per cases by A7,A79,A163,XBOOLE_0:def 2;
          suppose p in poz;
           hence p`1 <= Gij`1 by A83,A84,TOPREAL1:9;
          end;
          suppose p in pio;
           hence p`1 <= Gij`1 by A83,GOBOARD7:5;
         end;
         end;
              i1+1 <= len G by A1,NAT_1:13;
         then i1 <= len G-1 by XREAL_1:21;
         then A165: i1 <= len G-'1 by BINARITH:def 3;
              len G-'1 <= len G by BINARITH:52;
         then Gij`1 <= G*(len G-'1,1)`1 by A6,A10,A17,A21,A165,JORDAN1A:39;
         then p`1 <= G*(len G-'1,1)`1 by A164,XXREAL_0:2;
         then p`1 <= E-bound C by A21,JORDAN8:15;
         then A166: p`1 <= (E-max C)`1 by EUCLID:56;
              p`1 >= (E-max C)`1 by A162,TOPREAL1:def 13;
         then A167: p`1 = (E-max C)`1 by A166,XXREAL_0:1;
              p`2 = (E-max C)`2 by A162,TOPREAL1:def 13;
         then p = E-max C by A167,TOPREAL3:11;
         hence contradiction by A5,A7,A79,A125,A163,XBOOLE_0:3;
        end;
        suppose east_halfline E-max C meets L~do;
         then consider p be set such that
          A168: p in east_halfline E-max C and
          A169: p in L~do by XBOOLE_0:3;
         reconsider p as Point of TOP-REAL 2 by A168;
              p in L~LS by A51,A169;
         then p in east_halfline E-max C /\ L~Cage(C,n)
                      by A138,A168,XBOOLE_0:def 3;
         then A170: p`1 = Ebo by A155,JORDAN1A:104;
         A171: (E-max C)`2 = p`2 by A168,TOPREAL1:def 13;
         set RC = Rotate(Cage(C,n),Emax);
         A172: E-max C in right_cell(RC,1) by JORDAN1I:9;
         A173: 1+1 <= len LS by A24,XXREAL_0:2;
              LS = RC-:Wmin by JORDAN1G:26;
         then A174: LSeg(LS,1) = LSeg(RC,1) by A173,SPPOL_2:9;
         A175: L~RC = L~Cage(C,n) by REVROT_1:33;
         A176: len RC = len Cage(C,n) by REVROT_1:14;
         A177: GoB RC = GoB Cage(C,n) by REVROT_1:28
            .= G by JORDAN1H:52;
         A178: Emax in rng Cage(C,n) by SPRECT_2:50;
         A179: RC is_sequence_on G by A134,REVROT_1:34;
         A180: RC/.1 = E-max L~RC by A175,A178,FINSEQ_6:98;
         then consider ii,jj be Element of NAT such that
          A181: [ii,jj+1] in Indices G and
          A182: [ii,jj] in Indices G and
          A183: RC/.1 = G*(ii,jj+1) and
          A184: RC/.(1+1) = G*(ii,jj) by A88,A176,A179,JORDAN1I:25;
         consider jj2 be Element of NAT such that
          A185: 1 <= jj2 & jj2 <= width G and
          A186: Emax = G*(len G,jj2) by JORDAN1D:29;
         A187: len G >= 4 by JORDAN8:13;
         then len G >= 1 by XXREAL_0:2;
         then [len G,jj2] in Indices G by A185,GOBOARD7:10;
         then A188: ii = len G by A175,A180,A181,A183,A186,GOBOARD1:21;
         A189: 1 <= ii & ii <= len G & 1 <= jj+1 & jj+1 <= width G
                                                           by A181,GOBOARD5:1;
         A190: 1 <= ii & ii <= len G & 1 <= jj & jj <= width G
                                                           by A182,GOBOARD5:1;
         A191: ii+1 <> ii;
          jj+1+1 <> jj;
         then A192: right_cell(RC,1) = cell(G,ii-'1,jj)
                           by A88,A176,A177,A181,A182,A183,A184,A191,
GOBOARD5:def 6;
         A193: ii-'1+1 = ii by A189,BINARITH:53;
          ii-1 >= 4-1 by A187,A188,XREAL_1:11;
         then A194: ii-1 >= 1 by XXREAL_0:2;
         then A195: 1 <= ii-'1 by BINARITH:def 3;
         A196: G*(ii-'1,jj)`2 <= p`2 & p`2 <= G*(ii-'1,jj+1)`2
             by A171,A172,A189,A190,A192,A193,A194,JORDAN9:19;
         A197: ii-'1 < len G by A189,A193,NAT_1:13;
         then A198: G*(ii-'1,jj)`2 = G*(1,jj)`2 by A190,A195,GOBOARD5:2
            .= G*(ii,jj)`2 by A190,GOBOARD5:2;
         A199: G*(ii-'1,jj+1)`2 = G*(1,jj+1)`2 by A189,A195,A197,GOBOARD5:2
            .= G*(ii,jj+1)`2 by A189,GOBOARD5:2;
              G*(len G,jj)`1 = Ebo & Ebo = G*(len G,jj+1)`1
                                                by A17,A189,A190,JORDAN1A:92;
         then p in LSeg(RC/.1,RC/.(1+1))
                              by A170,A183,A184,A188,A196,A198,A199,GOBOARD7:8;
         then A200: p in LSeg(LS,1) by A88,A174,A176,TOPREAL1:def 5;
         A201: p in LSeg(do,Index(p,do)) by A169,JORDAN3:42;
         A202: do = mid(LS,Gik..LS,len LS) by A35,JORDAN1J:37;
         A203: 1<=Gik..LS & Gik..LS<=len LS by A35,FINSEQ_4:31;
              Gik..LS <> len LS by A27,A35,FINSEQ_4:29;
         then A204: Gik..LS < len LS by A203,REAL_1:def 5;
         A205: 1<=Index(p,do) & Index(p,do) < len do by A169,JORDAN3:41;
         A206: Index(Gik,LS)+1 = Gik..LS by A30,A35,JORDAN1J:56;
         consider t be Nat such that
          A207: t in dom LS and
          A208: LS.t = Gik by A35,FINSEQ_2:11;
         A209: 1 <= t & t <= len LS by A207,FINSEQ_3:27;
         then 1 < t by A30,A208,REAL_1:def 5;
         then Index(Gik,LS)+1 = t by A208,A209,JORDAN3:45;
         then A210: len L_Cut(LS,Gik) = len LS-Index(Gik,LS)
                                                  by A8,A208,JORDAN3:61;
         set tt = Index(p,do)+(Gik..LS)-'1;
         A211: 1<=Index(Gik,LS) & 0+Index(Gik,LS) < len LS by A8,JORDAN3:41;
         then A212: len LS-Index(Gik,LS) > 0 by XREAL_1:22;
          Index(p,do) < len LS-'Index(Gik,LS) by A205,A210,BINARITH:def 3;
         then Index(p,do)+1 <= len LS-'Index(Gik,LS) by NAT_1:13;
         then Index(p,do) <= len LS-'Index(Gik,LS)-1 by XREAL_1:21;
         then Index(p,do) <= len LS-Index(Gik,LS)-1 by A212,BINARITH:def 3;
         then Index(p,do) <= len LS-Gik..LS by A206;
         then Index(p,do) <= len LS-'Gik..LS by BINARITH:def 3;
         then Index(p,do) < len LS-'(Gik..LS)+1 by NAT_1:13;
         then A213: LSeg(mid(LS,Gik..LS,len LS),Index(p,do)) =
              LSeg(LS,Index(p,do)+(Gik..LS)-'1) by A203,A204,A205,JORDAN4:31;
         A214: 1+1 <= Gik..LS by A206,A211,XREAL_1:9;
         then Index(p,do)+Gik..LS >= 1+1+1 by A205,XREAL_1:9;
         then A215: Index(p,do)+Gik..LS-1 >= 1+1+1-1 by XREAL_1:11;
         then A216: tt >= 1+1 by BINARITH:def 3;
         A217: 2 in dom LS by A173,FINSEQ_3:27;
              now per cases by A216,REAL_1:def 5;
          suppose tt > 1+1;
           then LSeg(LS,1) misses LSeg(LS,tt) by TOPREAL1:def 9;
           hence contradiction by A200,A201,A202,A213,XBOOLE_0:3;
          end;
          suppose A218: tt = 1+1;
           then LSeg(LS,1) /\ LSeg(LS,tt) = {LS/.2} by A24,TOPREAL1:def 8
;
           then p in {LS/.2} by A200,A201,A202,A213,XBOOLE_0:def 3;
           then A219: p = LS/.2 by TARSKI:def 1;
           then A220: p..LS = 2 by A217,FINSEQ_5:44;
                1+1 = Index(p,do)+(Gik..LS)-1 by A215,A218,BINARITH:def 3;
           then 1+1+1 = Index(p,do)+(Gik..LS);
           then A221: Gik..LS = 2 by A205,A214,JORDAN1E:10;
                p in rng LS by A217,A219,PARTFUN2:4;
           then p = Gik by A35,A220,A221,FINSEQ_5:10;
           then Gik`1 = Ebo by A219,JORDAN1G:40;
           then Gik`1 = G*(len G,j)`1 by A10,A17,JORDAN1A:92;
           hence contradiction by A1,A13,A62,JORDAN1G:7;
         end;
         end;
         hence contradiction;
      end;
      end;
      then east_halfline E-max C c= (L~godo)` by SUBSET_1:43;
      then consider W be Subset of TOP-REAL 2 such that
       A222: W is_a_component_of (L~godo)` and
       A223: east_halfline E-max C c= W by GOBOARD9:5;
           east_halfline E-max C is not Bounded by JORDAN2C:129;
      then W is not Bounded by A223,JORDAN2C:16;
      then W is_outside_component_of L~godo by A222,JORDAN2C:def 4;
      then W c= UBD L~godo by JORDAN2C:27;
      then A224: east_halfline E-max C c= UBD L~godo by A223,XBOOLE_1:1;
           E-max C in east_halfline E-max C by TOPREAL1:45;
      then E-max C in UBD L~godo by A224;
      then E-max C in LeftComp godo by GOBRD14:46;
      then UA meets L~godo by A124,A125,A136,A143,JORDAN1J:36;
      then A225: UA meets (L~go \/ L~pion1) or UA meets L~do by A137,
XBOOLE_1:70;
      A226: UA c= C by JORDAN6:76;
           now per cases by A225,XBOOLE_1:70;
       suppose UA meets L~go;
        then UA meets L~Cage(C,n) by A139,XBOOLE_1:63;
        then C meets L~Cage(C,n) by A226,XBOOLE_1:63;
        hence contradiction by JORDAN10:5;
       end;
       suppose UA meets L~pion1;
        hence contradiction by A5,A7,A79;
       end;
       suppose UA meets L~do;
        then UA meets L~Cage(C,n) by A139,XBOOLE_1:63;
        then C meets L~Cage(C,n) by A226,XBOOLE_1:63;
        hence contradiction by JORDAN10:5;
      end;
      end;
      hence contradiction;
    end;
    suppose Gik`1 = Gij`1;
      then A227: i1 = i2 by A12,A13,JORDAN1G:7;
      then poz = {Gi1k} by TOPREAL1:7;
      then poz c= pio by A75,ZFMISC_1:37;
      then pio \/ poz = pio by XBOOLE_1:12;
      hence contradiction by A1,A2,A3,A4,A5,A227,Th14;
     end;
     suppose Gik`2 = Gij`2;
      then A228: j = k by A12,A13,JORDAN1G:6;
      then pio = {Gi1k} by TOPREAL1:7;
      then pio c= poz by A76,ZFMISC_1:37;
      then pio \/ poz = poz by XBOOLE_1:12;
      hence contradiction by A1,A2,A3,A4,A5,A228,JORDAN15:30;
    end;
    end;
    hence contradiction;
   end;

  theorem Th25:
   for C be Simple_closed_curve
   for i1,i2,j,k be Element of NAT holds
    1 < i1 & i1 < len Gauge(C,n+1) & 1 < i2 & i2 < len Gauge(C,n+1) &
    1 <= j & j <= k & k <= width Gauge(C,n+1) &
    Gauge(C,n+1)*(i1,k) in Lower_Arc L~Cage(C,n+1) &
    Gauge(C,n+1)*(i2,j) in Upper_Arc L~Cage(C,n+1) implies
     LSeg(Gauge(C,n+1)*(i2,j),Gauge(C,n+1)*(i2,k)) \/
     LSeg(Gauge(C,n+1)*(i2,k),Gauge(C,n+1)*(i1,k)) meets Lower_Arc C
   proof
    let C be Simple_closed_curve;
    let i1,i2,j,k be Element of NAT;
    set G=Gauge(C,n+1);
    assume that
     A1: 1 < i1 & i1 < len G and
     A2: 1 < i2 & i2 < len G and
     A3: 1 <= j & j <= k & k <= width G and
     A4: G*(i1,k) in Lower_Arc L~Cage(C,n+1) and
     A5: G*(i2,j) in Upper_Arc L~Cage(C,n+1);
    A6: Lower_Arc L~Cage(C,n+1) = L~Lower_Seq(C,n+1) by JORDAN1G:64;
    A7: Upper_Arc L~Cage(C,n+1) = L~Upper_Seq(C,n+1) by JORDAN1G:63;
    A8: Lower_Seq(C,n+1) is_sequence_on G by JORDAN1G:5;
    A9: Upper_Seq(C,n+1) is_sequence_on G by JORDAN1G:4;
    A10: 1 <= j & j <= width G by A3,XXREAL_0:2;
    then A11: [i2,j] in Indices G by A2,GOBOARD7:10;
    A12: 1 <= k & k <= width G by A3,XXREAL_0:2;
    then A13: [i2,k] in Indices G by A2,GOBOARD7:10;
         G*(i2,j)`1 = G*(i2,1)`1 by A2,A10,GOBOARD5:3
       .= G*(i2,k)`1 by A2,A12,GOBOARD5:3;
    then A14: LSeg(G*(i2,j),G*(i2,k)) is vertical by SPPOL_1:37;
         G*(i2,k)`2 = G*(1,k)`2 by A2,A12,GOBOARD5:2
       .= G*(i1,k)`2 by A1,A12,GOBOARD5:2;
    then A15: LSeg(G*(i2,k),G*(i1,k)) is horizontal by SPPOL_1:36;
    A16: [i2,k] in Indices G by A2,A12,GOBOARD7:10;
    A17: [i1,k] in Indices G by A1,A12,GOBOARD7:10;
         now per cases;
     suppose A18: LSeg(G*(i2,j),G*(i2,k)) meets
       Lower_Arc L~Cage(C,n+1);
      then consider m be Element of NAT such that
       A19: j <= m & m <= k and
       A20: G*(i2,m)`2 = inf(proj2.:(LSeg(G*(i2,j),G*(i2,k))
            /\ L~Lower_Seq(C,n+1))) by A3,A6,A8,A11,A13,JORDAN1F:1;
      A21: 1 <= m & m <= width G by A3,A19,XXREAL_0:2;
      set X = LSeg(G*(i2,j),G*(i2,k)) /\ L~Lower_Seq(C,n+1);
      A22: G*(i2,m)`1 = G*(i2,1)`1 by A2,A21,GOBOARD5:3;
      then A23: |[G*(i2,1)`1,inf(proj2.:X)]| = G*(i2,m) by A20,EUCLID:57;
      then A24: G*(i2,j)`1 = |[G*(i2,1)`1,inf(proj2.:X)]|`1
                                               by A2,A10,A22,GOBOARD5:3;
           ex x be set st x in LSeg(G*(i2,j),G*(i2,k)) &
       x in L~Lower_Seq(C,n+1) by A6,A18,XBOOLE_0:3;
      then reconsider X1=X as non empty compact Subset of TOP-REAL 2
                                              by PSCOMP_1:64,XBOOLE_0:def 3;
      consider pp be set such that
       A25: pp in S-most X1 by XBOOLE_0:def 1;
      reconsider pp as Point of TOP-REAL 2 by A25;
      A26: pp in X by A25,XBOOLE_0:def 3;
      then A27: pp in L~Lower_Seq(C,n+1) by XBOOLE_0:def 3;
           pp in LSeg(G*(i2,j),G*(i2,k)) by A26,XBOOLE_0:def 3;
      then A28: pp`1 = |[G*(i2,1)`1,inf(proj2.:X)]|`1
                            by A14,A24,SPPOL_1:64;
           |[G*(i2,1)`1,inf(proj2.:X)]|`2 = S-bound X by A20,A23,SPRECT_1:49
         .= (S-min X)`2 by EUCLID:56
         .= pp`2 by A25,PSCOMP_1:118;
      then G*(i2,m) in Lower_Arc L~Cage(C,n+1) by A6,A23,A27,A28,TOPREAL3:11;
      then A29: LSeg(G*(i2,j),G*(i2,m)) meets Lower_Arc C
                                             by A2,A3,A5,A19,A21,Th20;
           LSeg(G*(i2,j),G*(i2,m)) c= LSeg(G*(i2,j),G*(i2,k))
                                                    by A2,A3,A19,JORDAN15:7;
      then LSeg(G*(i2,j),G*(i2,k)) meets Lower_Arc C by A29,XBOOLE_1:63;
      hence LSeg(G*(i2,j),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i1,k)) meets Lower_Arc C by XBOOLE_1:70;
     end;
     suppose A30: LSeg(G*(i2,k),G*(i1,k)) meets Upper_Arc L~Cage(C,n+1) &
       i2 <= i1;
      then consider m be Element of NAT such that
       A31: i2 <= m & m <= i1 and
       A32: G*(m,k)`1 = sup(proj1.:(LSeg(G*(i2,k),G*(i1,k))
            /\ L~Upper_Seq(C,n+1))) by A7,A9,A16,A17,JORDAN1F:4;
      A33: 1 < m & m < len G by A1,A2,A31,XXREAL_0:2;
      set X = LSeg(G*(i2,k),G*(i1,k)) /\ L~Upper_Seq(C,n+1);
      A34: G*(m,k)`2 = G*(1,k)`2 by A12,A33,GOBOARD5:2;
      then A35: |[sup(proj1.:X),G*(1,k)`2]| = G*(m,k) by A32,EUCLID:57;
      then A36: G*(i2,k)`2 = |[sup(proj1.:X),G*(1,k)`2]|`2
                                               by A2,A12,A34,GOBOARD5:2;
           ex x be set st x in LSeg(G*(i2,k),G*(i1,k)) &
       x in L~Upper_Seq(C,n+1) by A7,A30,XBOOLE_0:3;
      then reconsider X1=X as non empty compact Subset of TOP-REAL 2
                                             by PSCOMP_1:64,XBOOLE_0:def 3;
      consider pp be set such that
       A37: pp in E-most X1 by XBOOLE_0:def 1;
      reconsider pp as Point of TOP-REAL 2 by A37;
      A38: pp in X by A37,XBOOLE_0:def 3;
      then A39: pp in L~Upper_Seq(C,n+1) by XBOOLE_0:def 3;
           pp in LSeg(G*(i2,k),G*(i1,k)) by A38,XBOOLE_0:def 3;
      then A40: pp`2 = |[sup(proj1.:X),G*(1,k)`2]|`2 by A15,A36,SPPOL_1:63;
           |[sup(proj1.:X),G*(1,k)`2]|`1 = E-bound X by A32,A35,SPRECT_1:51
         .= (E-min X)`1 by EUCLID:56
         .= pp`1 by A37,PSCOMP_1:108;
      then G*(m,k) in Upper_Arc L~Cage(C,n+1) by A7,A35,A39,A40,TOPREAL3:11;
      then A41: LSeg(G*(m,k),G*(i1,k)) meets Lower_Arc C
                                         by A1,A4,A12,A31,A33,JORDAN15:42;
           LSeg(G*(m,k),G*(i1,k)) c= LSeg(G*(i2,k),G*(i1,k))
                                                by A1,A2,A12,A31,JORDAN15:8;
      then LSeg(G*(i2,k),G*(i1,k)) meets Lower_Arc C by A41,XBOOLE_1:63;
      hence LSeg(G*(i2,j),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i1,k)) meets Lower_Arc C by XBOOLE_1:70;
     end;
     suppose A42: LSeg(G*(i2,k),G*(i1,k)) meets Upper_Arc L~Cage(C,n+1) &
      i1 < i2;
      then consider m be Element of NAT such that
       A43: i1 <= m & m <= i2 and
       A44: G*(m,k)`1 = inf(proj1.:(LSeg(G*(i1,k),G*(i2,k))
            /\ L~Upper_Seq(C,n+1))) by A7,A9,A16,A17,JORDAN1F:3;
      A45: 1 < m & m < len G by A1,A2,A43,XXREAL_0:2;
      set X = LSeg(G*(i1,k),G*(i2,k)) /\ L~Upper_Seq(C,n+1);
      A46: G*(m,k)`2 = G*(1,k)`2 by A12,A45,GOBOARD5:2;
      then A47: |[inf(proj1.:X),G*(1,k)`2]| = G*(m,k) by A44,EUCLID:57;
      then A48: G*(i1,k)`2 = |[inf(proj1.:X),G*(1,k)`2]|`2
                                                by A1,A12,A46,GOBOARD5:2;
           ex x be set st x in LSeg(G*(i1,k),G*(i2,k)) &
       x in L~Upper_Seq(C,n+1) by A7,A42,XBOOLE_0:3;
      then reconsider X1=X as non empty compact Subset of TOP-REAL 2
                                             by PSCOMP_1:64,XBOOLE_0:def 3;
      consider pp be set such that
       A49: pp in W-most X1 by XBOOLE_0:def 1;
      reconsider pp as Point of TOP-REAL 2 by A49;
      A50: pp in X by A49,XBOOLE_0:def 3;
      then A51: pp in L~Upper_Seq(C,n+1) by XBOOLE_0:def 3;
           pp in LSeg(G*(i1,k),G*(i2,k)) by A50,XBOOLE_0:def 3;
      then A52: pp`2 = |[inf(proj1.:X),G*(1,k)`2]|`2 by A15,A48,SPPOL_1:63;
           |[inf(proj1.:X),G*(1,k)`2]|`1 = W-bound X by A44,A47,SPRECT_1:48
         .= (W-min X)`1 by EUCLID:56
         .= pp`1 by A49,PSCOMP_1:88;
      then G*(m,k) in Upper_Arc L~Cage(C,n+1) by A7,A47,A51,A52,TOPREAL3:11;
      then A53: LSeg(G*(i1,k),G*(m,k)) meets Lower_Arc C
                                         by A1,A4,A12,A43,A45,JORDAN15:34;
           LSeg(G*(i1,k),G*(m,k)) c= LSeg(G*(i1,k),G*(i2,k))
                                                by A1,A2,A12,A43,JORDAN15:8;
      then LSeg(G*(i1,k),G*(i2,k)) meets Lower_Arc C by A53,XBOOLE_1:63;
      hence LSeg(G*(i2,j),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i1,k)) meets Lower_Arc C by XBOOLE_1:70;
     end;
     suppose A54: LSeg(G*(i2,j),G*(i2,k)) misses
        Lower_Arc L~Cage(C,n+1) &
       LSeg(Gauge(C,n+1)*(i2,k),Gauge(C,n+1)*(i1,k))
        misses Upper_Arc L~Cage(C,n+1);
      consider j1 be Element of NAT such that
       A55: j <= j1 & j1 <= k and
       A56: LSeg(G*(i2,j1),G*(i2,k)) /\ L~Upper_Seq(C,n+1) =
        {G*(i2,j1)} by A2,A3,A5,A7,JORDAN15:17;
           G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) /\
       L~Upper_Seq(C,n+1) by A56,TARSKI:def 1;
      then A57: G*(i2,j1) in L~Upper_Seq(C,n+1) by XBOOLE_0:def 3;
      A58: 1 <= j1 & j1 <= width G by A3,A55,XXREAL_0:2;
           now per cases;
       suppose i2 <= i1;
        then consider i3 be Element of NAT such that
         A59: i2 <= i3 & i3 <= i1 and
         A60: LSeg(G*(i2,k),G*(i3,k)) /\ L~Lower_Seq(C,n+1) = {G*(i3,k)}
                                             by A1,A2,A4,A6,A12,JORDAN15:21;
        A61: 1 < i3 & i3 < len G by A1,A2,A59,XXREAL_0:2;
             G*(i3,k) in LSeg(G*(i2,k),G*(i3,k)) /\ L~Lower_Seq(C,n+1)
                                                        by A60,TARSKI:def 1;
        then A62: G*(i3,k) in L~Lower_Seq(C,n+1) by XBOOLE_0:def 3;
        A63: LSeg(G*(i2,j1),G*(i2,k)) c=
         LSeg(G*(i2,j),G*(i2,k)) by A2,A3,A55,JORDAN15:7;
        A64: LSeg(G*(i2,k),G*(i3,k)) c= LSeg(G*(i2,k),G*(i1,k))
                                                by A1,A2,A12,A59,JORDAN15:8;
        then A65: LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) c=
         LSeg(G*(i2,j),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i1,k)) by A63,XBOOLE_1:13;
        A66: (LSeg(G*(i2,j1),G*(i2,k)) \/
         LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1) = {G*(i3,k)}
        proof
         thus (LSeg(G*(i2,j1),G*(i2,k)) \/
          LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1) c= {G*(i3,k)}
         proof
          let x be set;
          assume x in (LSeg(G*(i2,j1),G*(i2,k)) \/
           LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1);
          then x in LSeg(G*(i2,j1),G*(i2,k)) \/
                    LSeg(G*(i2,k),G*(i3,k)) &
               x in L~Lower_Seq(C,n+1) by XBOOLE_0:def 3;
          then (x in LSeg(G*(i2,j1),G*(i2,k)) or
                x in LSeg(G*(i2,k),G*(i3,k))) &
                x in L~Lower_Seq(C,n+1) by XBOOLE_0:def 2;
          hence x in {G*(i3,k)} by A6,A54,A60,A63,XBOOLE_0:3,def 3;
         end;
         let x be set;
         assume x in {G*(i3,k)};
         then A67: x = G*(i3,k) by TARSKI:def 1;
              G*(i3,k) in LSeg(G*(i2,k),G*(i3,k)) by TOPREAL1:6;
         then G*(i3,k) in LSeg(G*(i2,j1),G*(i2,k)) \/
                          LSeg(G*(i2,k),G*(i3,k)) by XBOOLE_0:def 2;
         hence x in (LSeg(G*(i2,j1),G*(i2,k)) \/
          LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1)
                                                  by A62,A67,XBOOLE_0:def 3;
        end;
             (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k))) /\
         L~Upper_Seq(C,n+1) = {G*(i2,j1)}
        proof
         thus (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k))) /\
          L~Upper_Seq(C,n+1) c= {G*(i2,j1)}
         proof
          let x be set;
          assume x in (LSeg(G*(i2,j1),G*(i2,k)) \/
                       LSeg(G*(i2,k),G*(i3,k))) /\ L~Upper_Seq(C,n+1);
          then x in LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) &
               x in L~Upper_Seq(C,n+1) by XBOOLE_0:def 3;
          then (x in LSeg(G*(i2,j1),G*(i2,k)) or
                x in LSeg(G*(i2,k),G*(i3,k))) &
                x in L~Upper_Seq(C,n+1) by XBOOLE_0:def 2;
          hence x in {G*(i2,j1)} by A7,A54,A56,A64,XBOOLE_0:3,def 3;
         end;
         let x be set;
         assume x in {G*(i2,j1)};
         then A68: x = G*(i2,j1) by TARSKI:def 1;
              G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) by TOPREAL1:6;
         then G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) \/
                           LSeg(G*(i2,k),G*(i3,k)) by XBOOLE_0:def 2;
         hence x in (LSeg(G*(i2,j1),G*(i2,k)) \/
                     LSeg(G*(i2,k),G*(i3,k))) /\ L~Upper_Seq(C,n+1)
                                                  by A57,A68,XBOOLE_0:def 3;
        end;
        then LSeg(G*(i2,j1),G*(i2,k)) \/
             LSeg(G*(i2,k),G*(i3,k)) meets Lower_Arc C
                                       by A2,A3,A55,A58,A59,A61,A66,Th22;
        hence LSeg(G*(i2,j),G*(i2,k)) \/
         LSeg(G*(i2,k),G*(i1,k)) meets Lower_Arc C by A65,XBOOLE_1:63;
       end;
       suppose i1 < i2;
        then consider i3 be Element of NAT such that
         A69: i1 <= i3 & i3 <= i2 and
         A70: LSeg(G*(i3,k),G*(i2,k)) /\ L~Lower_Seq(C,n+1) = {G*(i3,k)}
                                             by A1,A2,A4,A6,A12,JORDAN15:14;
        A71: 1 < i3 & i3 < len G by A1,A2,A69,XXREAL_0:2;
             G*(i3,k) in LSeg(G*(i2,k),G*(i3,k)) /\ L~Lower_Seq(C,n+1)
                                                        by A70,TARSKI:def 1;
        then A72: G*(i3,k) in L~Lower_Seq(C,n+1) by XBOOLE_0:def 3;
        A73: LSeg(G*(i2,j1),G*(i2,k)) c=
         LSeg(G*(i2,j),G*(i2,k)) by A2,A3,A55,JORDAN15:7;
        A74: LSeg(G*(i2,k),G*(i3,k)) c= LSeg(G*(i2,k),G*(i1,k))
                                                by A1,A2,A12,A69,JORDAN15:8;
        then A75: LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) c=
         LSeg(G*(i2,j),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i1,k)) by A73,XBOOLE_1:13
;
        A76: (LSeg(G*(i2,j1),G*(i2,k)) \/
         LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1) = {G*(i3,k)}
        proof
         thus (LSeg(G*(i2,j1),G*(i2,k)) \/
          LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1) c= {G*(i3,k)}
         proof
          let x be set;
          assume x in (LSeg(G*(i2,j1),G*(i2,k)) \/
           LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1);
          then x in LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) &
               x in L~Lower_Seq(C,n+1) by XBOOLE_0:def 3;
          then (x in LSeg(G*(i2,j1),G*(i2,k)) or x in LSeg(G*(i2,k),G*(i3
,k)))
           & x in L~Lower_Seq(C,n+1) by XBOOLE_0:def 2;
          hence x in {G*(i3,k)} by A6,A54,A70,A73,XBOOLE_0:3,def 3;
         end;
         let x be set;
         assume x in {G*(i3,k)};
         then A77: x = G*(i3,k) by TARSKI:def 1;
              G*(i3,k) in LSeg(G*(i2,k),G*(i3,k)) by TOPREAL1:6;
         then G*(i3,k) in LSeg(G*(i2,j1),G*(i2,k)) \/
                          LSeg(G*(i2,k),G*(i3,k)) by XBOOLE_0:def 2;
         hence x in (LSeg(G*(i2,j1),G*(i2,k)) \/
          LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1)
                                                  by A72,A77,XBOOLE_0:def 3;
        end;
             (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k))) /\
         L~Upper_Seq(C,n+1) = {G*(i2,j1)}
        proof
         thus (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k))) /\
          L~Upper_Seq(C,n+1) c= {G*(i2,j1)}
         proof
          let x be set;
          assume x in (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k
)
)) /\
           L~Upper_Seq(C,n+1);
          then x in LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) &
               x in L~Upper_Seq(C,n+1) by XBOOLE_0:def 3;
          then (x in LSeg(G*(i2,j1),G*(i2,k)) or x in LSeg(G*(i2,k),G*(i3
,k)))
           & x in L~Upper_Seq(C,n+1) by XBOOLE_0:def 2;
          hence x in {G*(i2,j1)} by A7,A54,A56,A74,XBOOLE_0:3,def 3;
         end;
         let x be set;
         assume x in {G*(i2,j1)};
         then A78: x = G*(i2,j1) by TARSKI:def 1;
              G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) by TOPREAL1:6;
         then G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) \/
                           LSeg(G*(i2,k),G*(i3,k)) by XBOOLE_0:def 2;
         hence x in (LSeg(G*(i2,j1),G*(i2,k)) \/
                     LSeg(G*(i2,k),G*(i3,k))) /\ L~Upper_Seq(C,n+1)
                                                  by A57,A78,XBOOLE_0:def 3;
        end;
        then LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k))
         meets Lower_Arc C by A2,A3,A55,A58,A69,A71,A76,Th24;
        hence LSeg(G*(i2,j),G*(i2,k)) \/
         LSeg(G*(i2,k),G*(i1,k)) meets Lower_Arc C by A75,XBOOLE_1:63;
      end;
      end;
      hence LSeg(G*(i2,j),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i1,k))
       meets Lower_Arc C;
    end;
    end;
    hence LSeg(G*(i2,j),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i1,k)) meets Lower_Arc
C;
   end;

  theorem Th26:
   for C be Simple_closed_curve
   for i1,i2,j,k be Element of NAT holds
    1 < i1 & i1 < len Gauge(C,n+1) & 1 < i2 & i2 < len Gauge(C,n+1) &
    1 <= j & j <= k & k <= width Gauge(C,n+1) &
    Gauge(C,n+1)*(i1,k) in Lower_Arc L~Cage(C,n+1) &
    Gauge(C,n+1)*(i2,j) in Upper_Arc L~Cage(C,n+1) implies
     LSeg(Gauge(C,n+1)*(i2,j),Gauge(C,n+1)*(i2,k)) \/
     LSeg(Gauge(C,n+1)*(i2,k),Gauge(C,n+1)*(i1,k)) meets Upper_Arc C
   proof
    let C be Simple_closed_curve;
    let i1,i2,j,k be Element of NAT;
    set G=Gauge(C,n+1);
    assume that
     A1: 1 < i1 & i1 < len G and
     A2: 1 < i2 & i2 < len G and
     A3: 1 <= j & j <= k & k <= width G and
     A4: G*(i1,k) in Lower_Arc L~Cage(C,n+1) and
     A5: G*(i2,j) in Upper_Arc L~Cage(C,n+1);
    A6: Lower_Arc L~Cage(C,n+1) = L~Lower_Seq(C,n+1) by JORDAN1G:64;
    A7: Upper_Arc L~Cage(C,n+1) = L~Upper_Seq(C,n+1) by JORDAN1G:63;
    A8: Lower_Seq(C,n+1) is_sequence_on G by JORDAN1G:5;
    A9: Upper_Seq(C,n+1) is_sequence_on G by JORDAN1G:4;
    A10: 1 <= j & j <= width G by A3,XXREAL_0:2;
    then A11: [i2,j] in Indices G by A2,GOBOARD7:10;
    A12: 1 <= k & k <= width G by A3,XXREAL_0:2;
    then A13: [i2,k] in Indices G by A2,GOBOARD7:10;
         G*(i2,j)`1 = G*(i2,1)`1 by A2,A10,GOBOARD5:3
       .= G*(i2,k)`1 by A2,A12,GOBOARD5:3;
    then A14: LSeg(G*(i2,j),G*(i2,k)) is vertical by SPPOL_1:37;
         G*(i2,k)`2 = G*(1,k)`2 by A2,A12,GOBOARD5:2
       .= G*(i1,k)`2 by A1,A12,GOBOARD5:2;
    then A15: LSeg(G*(i2,k),G*(i1,k)) is horizontal by SPPOL_1:36;
    A16: [i2,k] in Indices G by A2,A12,GOBOARD7:10;
    A17: [i1,k] in Indices G by A1,A12,GOBOARD7:10;
         now per cases;
     suppose A18: LSeg(G*(i2,j),G*(i2,k)) meets
       Lower_Arc L~Cage(C,n+1);
      then consider m be Element of NAT such that
       A19: j <= m & m <= k and
       A20: G*(i2,m)`2 = inf(proj2.:(LSeg(G*(i2,j),G*(i2,k))
            /\ L~Lower_Seq(C,n+1))) by A3,A6,A8,A11,A13,JORDAN1F:1;
      A21: 1 <= m & m <= width G by A3,A19,XXREAL_0:2;
      set X = LSeg(G*(i2,j),G*(i2,k)) /\ L~Lower_Seq(C,n+1);
      A22: G*(i2,m)`1 = G*(i2,1)`1 by A2,A21,GOBOARD5:3;
      then A23: |[G*(i2,1)`1,inf(proj2.:X)]| = G*(i2,m) by A20,EUCLID:57;
      then A24: G*(i2,j)`1 = |[G*(i2,1)`1,inf(proj2.:X)]|`1
                                               by A2,A10,A22,GOBOARD5:3;
           ex x be set st x in LSeg(G*(i2,j),G*(i2,k)) &
       x in L~Lower_Seq(C,n+1) by A6,A18,XBOOLE_0:3;
      then reconsider X1=X as non empty compact Subset of TOP-REAL 2
                                              by PSCOMP_1:64,XBOOLE_0:def 3;
      consider pp be set such that
       A25: pp in S-most X1 by XBOOLE_0:def 1;
      reconsider pp as Point of TOP-REAL 2 by A25;
      A26: pp in X by A25,XBOOLE_0:def 3;
      then A27: pp in L~Lower_Seq(C,n+1) by XBOOLE_0:def 3;
           pp in LSeg(G*(i2,j),G*(i2,k)) by A26,XBOOLE_0:def 3;
      then A28: pp`1 = |[G*(i2,1)`1,inf(proj2.:X)]|`1
                            by A14,A24,SPPOL_1:64;
           |[G*(i2,1)`1,inf(proj2.:X)]|`2 = S-bound X by A20,A23,SPRECT_1:49
         .= (S-min X)`2 by EUCLID:56
         .= pp`2 by A25,PSCOMP_1:118;
      then G*(i2,m) in Lower_Arc L~Cage(C,n+1) by A6,A23,A27,A28,TOPREAL3:11;
      then A29: LSeg(G*(i2,j),G*(i2,m)) meets Upper_Arc C
                                             by A2,A3,A5,A19,A21,Th19;
           LSeg(G*(i2,j),G*(i2,m)) c= LSeg(G*(i2,j),G*(i2,k))
                                                    by A2,A3,A19,JORDAN15:7;
      then LSeg(G*(i2,j),G*(i2,k)) meets Upper_Arc C by A29,XBOOLE_1:63;
      hence LSeg(G*(i2,j),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i1,k)) meets Upper_Arc C by XBOOLE_1:70;
     end;
     suppose A30: LSeg(G*(i2,k),G*(i1,k)) meets Upper_Arc L~Cage(C,n+1) &
       i2 <= i1;
      then consider m be Element of NAT such that
       A31: i2 <= m & m <= i1 and
       A32: G*(m,k)`1 = sup(proj1.:(LSeg(G*(i2,k),G*(i1,k))
            /\ L~Upper_Seq(C,n+1))) by A7,A9,A16,A17,JORDAN1F:4;
      A33: 1 < m & m < len G by A1,A2,A31,XXREAL_0:2;
      set X = LSeg(G*(i2,k),G*(i1,k)) /\ L~Upper_Seq(C,n+1);
      A34: G*(m,k)`2 = G*(1,k)`2 by A12,A33,GOBOARD5:2;
      then A35: |[sup(proj1.:X),G*(1,k)`2]| = G*(m,k) by A32,EUCLID:57;
      then A36: G*(i2,k)`2 = |[sup(proj1.:X),G*(1,k)`2]|`2
                                               by A2,A12,A34,GOBOARD5:2;
           ex x be set st x in LSeg(G*(i2,k),G*(i1,k)) &
       x in L~Upper_Seq(C,n+1) by A7,A30,XBOOLE_0:3;
      then reconsider X1=X as non empty compact Subset of TOP-REAL 2
                                             by PSCOMP_1:64,XBOOLE_0:def 3;
      consider pp be set such that
       A37: pp in E-most X1 by XBOOLE_0:def 1;
      reconsider pp as Point of TOP-REAL 2 by A37;
      A38: pp in X by A37,XBOOLE_0:def 3;
      then A39: pp in L~Upper_Seq(C,n+1) by XBOOLE_0:def 3;
           pp in LSeg(G*(i2,k),G*(i1,k)) by A38,XBOOLE_0:def 3;
      then A40: pp`2 = |[sup(proj1.:X),G*(1,k)`2]|`2 by A15,A36,SPPOL_1:63;
           |[sup(proj1.:X),G*(1,k)`2]|`1 = E-bound X by A32,A35,SPRECT_1:51
         .= (E-min X)`1 by EUCLID:56
         .= pp`1 by A37,PSCOMP_1:108;
      then G*(m,k) in Upper_Arc L~Cage(C,n+1) by A7,A35,A39,A40,TOPREAL3:11;
      then A41: LSeg(G*(m,k),G*(i1,k)) meets Upper_Arc C
                                         by A1,A4,A12,A31,A33,JORDAN15:43;
           LSeg(G*(m,k),G*(i1,k)) c= LSeg(G*(i2,k),G*(i1,k))
                                                by A1,A2,A12,A31,JORDAN15:8;
      then LSeg(G*(i2,k),G*(i1,k)) meets Upper_Arc C by A41,XBOOLE_1:63;
      hence LSeg(G*(i2,j),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i1,k)) meets Upper_Arc C by XBOOLE_1:70;
     end;
     suppose A42: LSeg(G*(i2,k),G*(i1,k)) meets Upper_Arc L~Cage(C,n+1) &
      i1 < i2;
      then consider m be Element of NAT such that
       A43: i1 <= m & m <= i2 and
       A44: G*(m,k)`1 = inf(proj1.:(LSeg(G*(i1,k),G*(i2,k))
            /\ L~Upper_Seq(C,n+1))) by A7,A9,A16,A17,JORDAN1F:3;
      A45: 1 < m & m < len G by A1,A2,A43,XXREAL_0:2;
      set X = LSeg(G*(i1,k),G*(i2,k)) /\ L~Upper_Seq(C,n+1);
      A46: G*(m,k)`2 = G*(1,k)`2 by A12,A45,GOBOARD5:2;
      then A47: |[inf(proj1.:X),G*(1,k)`2]| = G*(m,k) by A44,EUCLID:57;
      then A48: G*(i1,k)`2 = |[inf(proj1.:X),G*(1,k)`2]|`2
                                                by A1,A12,A46,GOBOARD5:2;
           ex x be set st x in LSeg(G*(i1,k),G*(i2,k)) &
       x in L~Upper_Seq(C,n+1) by A7,A42,XBOOLE_0:3;
      then reconsider X1=X as non empty compact Subset of TOP-REAL 2
                                             by PSCOMP_1:64,XBOOLE_0:def 3;
      consider pp be set such that
       A49: pp in W-most X1 by XBOOLE_0:def 1;
      reconsider pp as Point of TOP-REAL 2 by A49;
      A50: pp in X by A49,XBOOLE_0:def 3;
      then A51: pp in L~Upper_Seq(C,n+1) by XBOOLE_0:def 3;
           pp in LSeg(G*(i1,k),G*(i2,k)) by A50,XBOOLE_0:def 3;
      then A52: pp`2 = |[inf(proj1.:X),G*(1,k)`2]|`2 by A15,A48,SPPOL_1:63;
           |[inf(proj1.:X),G*(1,k)`2]|`1 = W-bound X by A44,A47,SPRECT_1:48
         .= (W-min X)`1 by EUCLID:56
         .= pp`1 by A49,PSCOMP_1:88;
      then G*(m,k) in Upper_Arc L~Cage(C,n+1) by A7,A47,A51,A52,TOPREAL3:11;
      then A53: LSeg(G*(i1,k),G*(m,k)) meets Upper_Arc C
                                         by A1,A4,A12,A43,A45,JORDAN15:35;
           LSeg(G*(i1,k),G*(m,k)) c= LSeg(G*(i1,k),G*(i2,k))
                                                by A1,A2,A12,A43,JORDAN15:8;
      then LSeg(G*(i1,k),G*(i2,k)) meets Upper_Arc C by A53,XBOOLE_1:63;
      hence LSeg(G*(i2,j),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i1,k)) meets Upper_Arc C by XBOOLE_1:70;
     end;
     suppose A54: LSeg(G*(i2,j),G*(i2,k)) misses
        Lower_Arc L~Cage(C,n+1) &
       LSeg(Gauge(C,n+1)*(i2,k),Gauge(C,n+1)*(i1,k))
        misses Upper_Arc L~Cage(C,n+1);
      consider j1 be Element of NAT such that
       A55: j <= j1 & j1 <= k and
       A56: LSeg(G*(i2,j1),G*(i2,k)) /\ L~Upper_Seq(C,n+1) =
        {G*(i2,j1)} by A2,A3,A5,A7,JORDAN15:17;
           G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) /\
       L~Upper_Seq(C,n+1) by A56,TARSKI:def 1;
      then A57: G*(i2,j1) in L~Upper_Seq(C,n+1) by XBOOLE_0:def 3;
      A58: 1 <= j1 & j1 <= width G by A3,A55,XXREAL_0:2;
           now per cases;
       suppose i2 <= i1;
        then consider i3 be Element of NAT such that
         A59: i2 <= i3 & i3 <= i1 and
         A60: LSeg(G*(i2,k),G*(i3,k)) /\ L~Lower_Seq(C,n+1) = {G*(i3,k)}
                                             by A1,A2,A4,A6,A12,JORDAN15:21;
        A61: 1 < i3 & i3 < len G by A1,A2,A59,XXREAL_0:2;
             G*(i3,k) in LSeg(G*(i2,k),G*(i3,k)) /\ L~Lower_Seq(C,n+1)
                                                        by A60,TARSKI:def 1;
        then A62: G*(i3,k) in L~Lower_Seq(C,n+1) by XBOOLE_0:def 3;
        A63: LSeg(G*(i2,j1),G*(i2,k)) c=
         LSeg(G*(i2,j),G*(i2,k)) by A2,A3,A55,JORDAN15:7;
        A64: LSeg(G*(i2,k),G*(i3,k)) c= LSeg(G*(i2,k),G*(i1,k))
                                                by A1,A2,A12,A59,JORDAN15:8;
        then A65: LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) c=
         LSeg(G*(i2,j),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i1,k)) by A63,XBOOLE_1:13;
        A66: (LSeg(G*(i2,j1),G*(i2,k)) \/
         LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1) = {G*(i3,k)}
        proof
         thus (LSeg(G*(i2,j1),G*(i2,k)) \/
          LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1) c= {G*(i3,k)}
         proof
          let x be set;
          assume x in (LSeg(G*(i2,j1),G*(i2,k)) \/
           LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1);
          then x in LSeg(G*(i2,j1),G*(i2,k)) \/
                    LSeg(G*(i2,k),G*(i3,k)) &
               x in L~Lower_Seq(C,n+1) by XBOOLE_0:def 3;
          then (x in LSeg(G*(i2,j1),G*(i2,k)) or
                x in LSeg(G*(i2,k),G*(i3,k))) &
                x in L~Lower_Seq(C,n+1) by XBOOLE_0:def 2;
          hence x in {G*(i3,k)} by A6,A54,A60,A63,XBOOLE_0:3,def 3;
         end;
         let x be set;
         assume x in {G*(i3,k)};
         then A67: x = G*(i3,k) by TARSKI:def 1;
              G*(i3,k) in LSeg(G*(i2,k),G*(i3,k)) by TOPREAL1:6;
         then G*(i3,k) in LSeg(G*(i2,j1),G*(i2,k)) \/
                          LSeg(G*(i2,k),G*(i3,k)) by XBOOLE_0:def 2;
         hence x in (LSeg(G*(i2,j1),G*(i2,k)) \/
          LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1)
                                                  by A62,A67,XBOOLE_0:def 3;
        end;
             (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k))) /\
         L~Upper_Seq(C,n+1) = {G*(i2,j1)}
        proof
         thus (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k))) /\
          L~Upper_Seq(C,n+1) c= {G*(i2,j1)}
         proof
          let x be set;
          assume x in (LSeg(G*(i2,j1),G*(i2,k)) \/
                       LSeg(G*(i2,k),G*(i3,k))) /\ L~Upper_Seq(C,n+1);
          then x in LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) &
               x in L~Upper_Seq(C,n+1) by XBOOLE_0:def 3;
          then (x in LSeg(G*(i2,j1),G*(i2,k)) or
                x in LSeg(G*(i2,k),G*(i3,k))) &
                x in L~Upper_Seq(C,n+1) by XBOOLE_0:def 2;
          hence x in {G*(i2,j1)} by A7,A54,A56,A64,XBOOLE_0:3,def 3;
         end;
         let x be set;
         assume x in {G*(i2,j1)};
         then A68: x = G*(i2,j1) by TARSKI:def 1;
              G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) by TOPREAL1:6;
         then G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) \/
                           LSeg(G*(i2,k),G*(i3,k)) by XBOOLE_0:def 2;
         hence x in (LSeg(G*(i2,j1),G*(i2,k)) \/
                     LSeg(G*(i2,k),G*(i3,k))) /\ L~Upper_Seq(C,n+1)
                                                  by A57,A68,XBOOLE_0:def 3;
        end;
        then LSeg(G*(i2,j1),G*(i2,k)) \/
             LSeg(G*(i2,k),G*(i3,k)) meets Upper_Arc C
                                       by A2,A3,A55,A58,A59,A61,A66,Th21;
        hence LSeg(G*(i2,j),G*(i2,k)) \/
         LSeg(G*(i2,k),G*(i1,k)) meets Upper_Arc C by A65,XBOOLE_1:63;
       end;
       suppose i1 < i2;
        then consider i3 be Element of NAT such that
         A69: i1 <= i3 & i3 <= i2 and
         A70: LSeg(G*(i3,k),G*(i2,k)) /\ L~Lower_Seq(C,n+1) = {G*(i3,k)}
                                             by A1,A2,A4,A6,A12,JORDAN15:14;
        A71: 1 < i3 & i3 < len G by A1,A2,A69,XXREAL_0:2;
             G*(i3,k) in LSeg(G*(i2,k),G*(i3,k)) /\ L~Lower_Seq(C,n+1)
                                                        by A70,TARSKI:def 1;
        then A72: G*(i3,k) in L~Lower_Seq(C,n+1) by XBOOLE_0:def 3;
        A73: LSeg(G*(i2,j1),G*(i2,k)) c=
         LSeg(G*(i2,j),G*(i2,k)) by A2,A3,A55,JORDAN15:7;
        A74: LSeg(G*(i2,k),G*(i3,k)) c= LSeg(G*(i2,k),G*(i1,k))
                                                by A1,A2,A12,A69,JORDAN15:8;
        then A75: LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) c=
         LSeg(G*(i2,j),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i1,k)) by A73,XBOOLE_1:13
;
        A76: (LSeg(G*(i2,j1),G*(i2,k)) \/
         LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1) = {G*(i3,k)}
        proof
         thus (LSeg(G*(i2,j1),G*(i2,k)) \/
          LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1) c= {G*(i3,k)}
         proof
          let x be set;
          assume x in (LSeg(G*(i2,j1),G*(i2,k)) \/
           LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1);
          then x in LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) &
               x in L~Lower_Seq(C,n+1) by XBOOLE_0:def 3;
          then (x in LSeg(G*(i2,j1),G*(i2,k)) or x in LSeg(G*(i2,k),G*(i3
,k)))
           & x in L~Lower_Seq(C,n+1) by XBOOLE_0:def 2;
          hence x in {G*(i3,k)} by A6,A54,A70,A73,XBOOLE_0:3,def 3;
         end;
         let x be set;
         assume x in {G*(i3,k)};
         then A77: x = G*(i3,k) by TARSKI:def 1;
              G*(i3,k) in LSeg(G*(i2,k),G*(i3,k)) by TOPREAL1:6;
         then G*(i3,k) in LSeg(G*(i2,j1),G*(i2,k)) \/
                          LSeg(G*(i2,k),G*(i3,k)) by XBOOLE_0:def 2;
         hence x in (LSeg(G*(i2,j1),G*(i2,k)) \/
          LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1)
                                                  by A72,A77,XBOOLE_0:def 3;
        end;
             (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k))) /\
         L~Upper_Seq(C,n+1) = {G*(i2,j1)}
        proof
         thus (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k))) /\
          L~Upper_Seq(C,n+1) c= {G*(i2,j1)}
         proof
          let x be set;
          assume x in (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k
)
)) /\
           L~Upper_Seq(C,n+1);
          then x in LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) &
               x in L~Upper_Seq(C,n+1) by XBOOLE_0:def 3;
          then (x in LSeg(G*(i2,j1),G*(i2,k)) or x in LSeg(G*(i2,k),G*(i3
,k)))
           & x in L~Upper_Seq(C,n+1) by XBOOLE_0:def 2;
          hence x in {G*(i2,j1)} by A7,A54,A56,A74,XBOOLE_0:3,def 3;
         end;
         let x be set;
         assume x in {G*(i2,j1)};
         then A78: x = G*(i2,j1) by TARSKI:def 1;
              G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) by TOPREAL1:6;
         then G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) \/
                           LSeg(G*(i2,k),G*(i3,k)) by XBOOLE_0:def 2;
         hence x in (LSeg(G*(i2,j1),G*(i2,k)) \/
                     LSeg(G*(i2,k),G*(i3,k))) /\ L~Upper_Seq(C,n+1)
                                                  by A57,A78,XBOOLE_0:def 3;
        end;
        then LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k))
         meets Upper_Arc C by A2,A3,A55,A58,A69,A71,A76,Th23;
        hence LSeg(G*(i2,j),G*(i2,k)) \/
         LSeg(G*(i2,k),G*(i1,k)) meets Upper_Arc C by A75,XBOOLE_1:63;
      end;
      end;
      hence LSeg(G*(i2,j),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i1,k))
       meets Upper_Arc C;
    end;
    end;
    hence LSeg(G*(i2,j),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i1,k)) meets Upper_Arc
C;
   end;

  theorem Th27:
   for C be Simple_closed_curve
   for p be Point of TOP-REAL 2 st W-bound C < p`1 & p`1 < E-bound C holds
    not(p in North_Arc C & p in South_Arc C)
   proof
    let C be Simple_closed_curve;
    let p be Point of TOP-REAL 2;
    reconsider p' = p as Point of Euclid 2 by EUCLID:25;
    assume that
     A1: W-bound C < p`1 and
     A2: p`1 < E-bound C and
     A3: p in North_Arc C and
     A4: p in South_Arc C;
    set s = min(p`1 - W-bound C,E-bound C - p`1);
         W-bound C = W-bound C + 0 & p`1 = p`1 + 0;
    then p`1 - W-bound C > 0 & E-bound C - p`1 > 0 by A1,A2,XREAL_1:22;
    then A5: s > 0 by XXREAL_0:15;
         now let r be real number;
     reconsider rr = r as Real by XREAL_0:def 1;
     assume A6: 0 < r & r < s;
     then A7: r/8 > 0 by REAL_2:127;
     then reconsider G = Ball(p',r/8) as a_neighborhood of p by GOBOARD6:5;
     consider k1 be Element of NAT such that
      A8: for m be Element of NAT st m > k1
       holds (Upper_Appr C).m meets G by A3,KURATO_2:def 11;
     consider k2 be Element of NAT such that
      A9: for m be Element of NAT st m > k2
       holds (Lower_Appr C).m meets G by A4,KURATO_2:def 11;
     set k = max(k1,k2);
     A10: k >= k1 by XXREAL_0:25;
     set z' = max(N-bound C - S-bound C,E-bound C - W-bound C);
     set z = max(z',r/8);
          z >= r/8 by XXREAL_0:25;
     then z/(r/8) >= 1 by A7,REAL_2:143;
     then log(2,z/(r/8)) >= log(2,1) by PRE_FF:12;
     then log(2,z/(r/8)) >= 0 by POWER:59;
     then reconsider m' = [\ log(2,z/(r/8)) /] as Element of NAT by INT_1:80;
     A11: 2 to_power (m'+1) > 0 by POWER:39;
     set N = 2 to_power (m'+1);
          log(2,z/(r/8)) < (m'+1) * 1 by INT_1:52;
     then log(2,z/(r/8)) < (m'+1) * log(2,2) by POWER:60;
     then log(2,z/(r/8)) < log(2,2 to_power (m'+1)) by POWER:63;
     then z/(r/8) < N by A11,PRE_FF:12;
     then z/(r/8)*(r/8) < N*(r/8) by A7,XREAL_1:70;
     then z < N*(r/8) by A7,XCMPLX_1:88;
     then z/N < N*(r/8)/N by A11,REAL_1:73;
     then z/N < (r/8)/N*N by XCMPLX_1:75;
     then A12: z/N < r/8 by A11,XCMPLX_1:88;
          z >= z' by XXREAL_0:25;
     then z/N >= z'/N by A11,REAL_1:73;
     then A13: z'/N < r/8 by A12,XXREAL_0:2;
     set m = max(k,m')+1;
     A14: len Gauge(C,m) = width Gauge(C,m) by JORDAN8:def 1;
          max(k,m') >= k by XXREAL_0:25;
     then max(k,m') >= k1 by A10,XXREAL_0:2;
     then m > k1 by NAT_1:13;
     then (Upper_Appr C).m meets G by A8;
     then Upper_Arc L~Cage (C,m) meets G by Def1;
     then consider p1 be set such that
      A15: p1 in Upper_Arc L~Cage (C,m) and
      A16: p1 in G by XBOOLE_0:3;
     reconsider p1 as Point of TOP-REAL 2 by A15;
     reconsider p1' = p1 as Point of Euclid 2 by EUCLID:25;
     set f = Upper_Seq(C,m);
     A17: Upper_Arc L~Cage(C,m) = L~Upper_Seq(C,m) by JORDAN1G:63;
     then consider i1 be Element of NAT such that
      A18: 1 <= i1 & i1+1 <= len f and
      A19: p1 in LSeg(f/.i1,f/.(i1+1)) by A15,SPPOL_2:14;
     reconsider c1 = f/.i1 as Point of Euclid 2 by EUCLID:25;
     reconsider c2 = f/.(i1+1) as Point of Euclid 2 by EUCLID:25;
     A20: f is_sequence_on Gauge(C,m) by JORDAN1G:4;
          i1 < len f by A18,NAT_1:13;
     then i1 in Seg len f by A18,FINSEQ_1:3;
     then A21: i1 in dom f by FINSEQ_1:def 3;
     then consider ii1,jj1 be Element of NAT such that
      A22: [ii1,jj1] in Indices Gauge(C,m) and
      A23: f/.i1 = Gauge(C,m)*(ii1,jj1) by A20,GOBOARD1:def 11;
          N-bound C > S-bound C+0 & E-bound C > W-bound C+0 by TOPREAL5:22,23
;
     then A24: N-bound C - S-bound C > 0 &
               E-bound C - W-bound C > 0 by XREAL_1:22;
     A25: 2|^(m'+1) > 0 by A11,POWER:48;
          max(k,m') >= m' by XXREAL_0:25;
     then m > m' by NAT_1:13;
     then m >= m'+1 by NAT_1:13;
     then 2|^m >= 2|^(m'+1) by PREPOWER:107;
     then A26: (N-bound C - S-bound C)/2|^m<=(N-bound C - S-bound C)/2|^(m'+1)
      & (E-bound C - W-bound C)/2|^m <= (E-bound C - W-bound C)/2|^(m'+1)
                                                       by A24,A25,REAL_2:201;
          N-bound C - S-bound C <= z' & E-bound C - W-bound C <= z' by
XXREAL_0:25;
     then (N-bound C - S-bound C)/N <= z'/N &
          (E-bound C - W-bound C)/N <= z'/N by A11,REAL_1:73;
     then (N-bound C - S-bound C)/2|^(m'+1) <= z'/N &
          (E-bound C - W-bound C)/2|^(m'+1) <= z'/N by POWER:48;
     then A27: (N-bound C - S-bound C)/2|^m <= z'/N &
          (E-bound C - W-bound C)/2|^m <= z'/N by A26,XXREAL_0:2;
     then dist(f/.i1,f/.(i1+1)) <= z'/N by A18,A20,Th6;
     then dist(f/.i1,f/.(i1+1)) < r/8 by A13,XXREAL_0:2;
     then dist(c1,c2) < r/8 by TOPREAL6:def 1;
     then A28: |. f/.i1 - f/.(i1+1) .| < r/8 by SPPOL_1:62;
          |. p1 - f/.i1 .| <= |. f/.i1 - f/.(i1+1) .| by A19,JGRAPH_1:53;
     then A29: |. p1 - f/.i1 .| < r/8 by A28,XXREAL_0:2;
          dist(p1',p') < r/8 by A16,METRIC_1:12;
     then |. p-p1 .| < r/8 by SPPOL_1:62;
     then A30: |. p-p1 .| + |. p1 - f/.i1 .| < r/(2*4) + r/(2*4) by A29,
XREAL_1:10;
          |. p - f/.i1 .| <= |. p - p1 .| + |. p1 - f/.i1 .| by TOPRNS_1:35;
     then A31: |. p - f/.i1 .| < r/4 by A30,XXREAL_0:2;
     then A32: dist(p',c1) < r/4 by SPPOL_1:62;
     then A33: f/.i1 in Ball(p',r/4) by METRIC_1:12;
     A34: f/.i1 in Upper_Arc L~Cage(C,m) by A17,A21,SPPOL_2:48;
     A35: k >= k2 by XXREAL_0:25;
          max(k,m') >= k by XXREAL_0:25;
     then max(k,m') >= k2 by A35,XXREAL_0:2;
     then m > k2 by NAT_1:13;
     then (Lower_Appr C).m meets G by A9;
     then Lower_Arc L~Cage (C,m) meets G by Def2;
     then consider p2 be set such that
      A36: p2 in Lower_Arc L~Cage (C,m) and
      A37: p2 in G by XBOOLE_0:3;
     reconsider p2 as Point of TOP-REAL 2 by A36;
     reconsider p2' = p2 as Point of Euclid 2 by EUCLID:25;
     set g = Lower_Seq(C,m);
     A38: Lower_Arc L~Cage(C,m) = L~Lower_Seq(C,m) by JORDAN1G:64;
     then consider i2 be Element of NAT such that
      A39: 1 <= i2 & i2+1 <= len g and
      A40: p2 in LSeg(g/.i2,g/.(i2+1)) by A36,SPPOL_2:14;
     reconsider d1 = g/.i2 as Point of Euclid 2 by EUCLID:25;
     reconsider d2 = g/.(i2+1) as Point of Euclid 2 by EUCLID:25;
     A41: g is_sequence_on Gauge(C,m) by JORDAN1G:5;
          i2 < len g by A39,NAT_1:13;
     then i2 in Seg len g by A39,FINSEQ_1:3;
     then A42: i2 in dom g by FINSEQ_1:def 3;
     then consider ii2,jj2 be Element of NAT such that
      A43: [ii2,jj2] in Indices Gauge(C,m) and
      A44: g/.i2 = Gauge(C,m)*(ii2,jj2) by A41,GOBOARD1:def 11;
          dist(g/.i2,g/.(i2+1)) <= z'/N by A27,A39,A41,Th6;
     then dist(g/.i2,g/.(i2+1)) < r/8 by A13,XXREAL_0:2;
     then dist(d1,d2) < r/8 by TOPREAL6:def 1;
     then A45: |. g/.i2 - g/.(i2+1) .| < r/8 by SPPOL_1:62;
          |. p2 - g/.i2 .| <= |. g/.i2 - g/.(i2+1) .| by A40,JGRAPH_1:53;
     then A46: |. p2 - g/.i2 .| < r/8 by A45,XXREAL_0:2;
          dist(p2',p') < r/8 by A37,METRIC_1:12;
     then |. p-p2 .| < r/8 by SPPOL_1:62;
     then A47: |. p-p2 .| + |. p2 - g/.i2 .| < r/(2*4) + r/(2*4) by A46,
XREAL_1:10;
          |. p - g/.i2 .| <= |. p - p2 .| + |. p2 - g/.i2 .| by TOPRNS_1:35;
     then A48: |. p - g/.i2 .| < r/4 by A47,XXREAL_0:2;
     then A49: dist(p',d1) < r/4 by SPPOL_1:62;
     then A50: g/.i2 in Ball(p',r/4) by METRIC_1:12;
     A51: g/.i2 in Lower_Arc L~Cage(C,m) by A38,A42,SPPOL_2:48;
     set Gij = Gauge(C,m)*(ii2,jj1);
     set Gji = Gauge(C,m)*(ii1,jj2);
     reconsider Gij' = Gij, Gji' = Gji as Point of Euclid 2 by EUCLID:25;
     A52: 1 <= ii1 & ii1 <= len Gauge(C,m) &
          1 <= jj1 & jj1 <= width Gauge(C,m) by A22,GOBOARD5:1;
     A53: 1 <= ii2 & ii2 <= len Gauge(C,m) &
          1 <= jj2 & jj2 <= width Gauge(C,m) by A43,GOBOARD5:1;
          len f >= 3 & len g >= 3 by JORDAN1E:19;
     then len f >= 1 & len g >= 1 by XXREAL_0:2;
     then len f in Seg len f & len g in Seg len g by FINSEQ_1:3;
     then A54: len f in dom f & len g in dom g by FINSEQ_1:def 3;
     A55: r/4 < r by A6,XREAL_1:225;
     A56: r/2 < r by A6,XREAL_1:218;
     A57: s <= p`1 - W-bound C & s <= E-bound C - p`1 by XXREAL_0:17;
     A58: now assume 1 >= ii1;
      then A59: ii1 = 1 by A52,XXREAL_0:1;
           dist(p',c1) < r by A32,A55,XXREAL_0:2;
      then dist(p',c1) < s by A6,XXREAL_0:2;
      then A60: dist(p',c1) < p`1 - W-bound C by A57,XXREAL_0:2;
      A61: p`1-(f/.i1)`1 <= abs(p`1-(f/.i1)`1) by ABSVALUE:11;
           abs(p`1-(f/.i1)`1) <= |.p-f/.i1.| by JGRAPH_1:51;
      then p`1 - (f/.i1)`1 <= |.p-f/.i1.| by A61,XXREAL_0:2;
      then p`1 - W-bound L~Cage(C,m) <= |.p-f/.i1.|
                                               by A14,A23,A52,A59,JORDAN1A:94
;
      then p`1 - W-bound L~Cage(C,m) <= dist(p',c1) by SPPOL_1:62;
      then p`1 - W-bound L~Cage(C,m) < p`1 - W-bound C by A60,XXREAL_0:2;
      then W-bound L~Cage(C,m) > W-bound C by REAL_1:92;
      hence contradiction by Th12;
     end;
     A62: now assume ii1 >= len Gauge(C,m);
      then A63: ii1 = len Gauge(C,m) by A52,XXREAL_0:1;
           (Gauge(C,m)*(len Gauge(C,m),jj1))`1 = E-bound L~Cage(C,m)
                                                       by A14,A52,JORDAN1A:92;
      then f/.i1 = E-max L~Cage(C,m) by A17,A23,A34,A63,JORDAN1J:46
         .= f/.len f by JORDAN1F:7;
      then i1 = len f by A21,A54,PARTFUN2:17;
      hence contradiction by A18,NAT_1:13;
     end;
     A64: now assume ii2 <= 1;
      then A65: ii2 = 1 by A53,XXREAL_0:1;
           (Gauge(C,m)*(1,jj2))`1 = W-bound L~Cage(C,m) by A14,A53,JORDAN1A:94;
      then g/.i2 = W-min L~Cage(C,m) by A38,A44,A51,A65,JORDAN1J:47
         .= g/.len g by JORDAN1F:8;
      then i2 = len g by A42,A54,PARTFUN2:17;
      hence contradiction by A39,NAT_1:13;
     end;
     A66: now assume ii2 >= len Gauge(C,m);
      then A67: ii2 = len Gauge(C,m) by A53,XXREAL_0:1;
           dist(p',d1) < r by A49,A55,XXREAL_0:2;
      then dist(p',d1) < s by A6,XXREAL_0:2;
      then A68: dist(p',d1) < E-bound C - p`1 by A57,XXREAL_0:2;
      A69: (g/.i2)`1-p`1 <= abs((g/.i2)`1-p`1) by ABSVALUE:11;
           abs((g/.i2)`1-p`1) <= |.g/.i2-p.| by JGRAPH_1:51;
      then abs((g/.i2)`1-p`1) <= |.p-g/.i2.| by TOPRNS_1:28;
      then (g/.i2)`1 - p`1 <= |.p-g/.i2.| by A69,XXREAL_0:2;
      then E-bound L~Cage(C,m) - p`1 <= |.p-g/.i2.|
                                               by A14,A44,A53,A67,JORDAN1A:92
;
      then E-bound L~Cage(C,m) - p`1 <= dist(p',d1) by SPPOL_1:62;
      then E-bound L~Cage(C,m) - p`1 < E-bound C - p`1 by A68,XXREAL_0:2;
      then E-bound L~Cage(C,m) < E-bound C by REAL_1:92;
      hence contradiction by Th10;
     end;
     A70: Ball(p',rr/4) c= Ball(p',rr) by A55,PCOMPS_1:1;
     A71: Gij`1 = Gauge(C,m)*(ii2,1)`1 by A52,A53,GOBOARD5:3
        .= (g/.i2)`1 by A44,A53,GOBOARD5:3;
     A72: Gij`2 = Gauge(C,m)*(1,jj1)`2 by A52,A53,GOBOARD5:2
        .= (f/.i1)`2 by A23,A52,GOBOARD5:2;
     A73: Gji`1 = Gauge(C,m)*(ii1,1)`1 by A52,A53,GOBOARD5:3
        .= (f/.i1)`1 by A23,A52,GOBOARD5:3;
     A74: Gji`2 = Gauge(C,m)*(1,jj2)`2 by A52,A53,GOBOARD5:2
        .= (g/.i2)`2 by A44,A53,GOBOARD5:2;
          abs((g/.i2)`1-p`1) <= |.(g/.i2)-p.| & abs((f/.i1)`2-p`2) <= |.(f/.
i1)-p.|
                                                              by JGRAPH_1:51;
     then abs((g/.i2)`1-p`1) <= |.p-(g/.i2).| &
          abs((f/.i1)`2-p`2) <= |.p-(f/.i1).| by TOPRNS_1:28;
     then abs((g/.i2)`1-p`1) <= r/4 & abs((f/.i1)`2-p`2) <= r/4
                                                        by A31,A48,XXREAL_0:2;
     then abs((g/.i2)`1-p`1) + abs((f/.i1)`2-p`2) <= r/(2*2) + r/(2*2)
                                                                by XREAL_1:9;
     then A75: abs((g/.i2)`1-p`1) + abs((f/.i1)`2-p`2) < r by A56,XXREAL_0:2;
          abs((f/.i1)`1-p`1) <= |.(f/.i1)-p.| & abs((g/.i2)`2-p`2) <= |.(g/.
i2)-p.|
                                                              by JGRAPH_1:51;
     then abs((f/.i1)`1-p`1) <= |.p-(f/.i1).| &
          abs((g/.i2)`2-p`2) <= |.p-(g/.i2).| by TOPRNS_1:28;
     then abs((f/.i1)`1-p`1) <= r/4 & abs((g/.i2)`2-p`2) <= r/4
                                                        by A31,A48,XXREAL_0:2;
     then abs((f/.i1)`1-p`1) + abs((g/.i2)`2-p`2) <= r/(2*2) + r/(2*2)
                                                                by XREAL_1:9;
     then A76: abs((f/.i1)`1-p`1) + abs((g/.i2)`2-p`2) < r by A56,XXREAL_0:2;
          |.Gij-p.| <= abs((g/.i2)`1-p`1) + abs((f/.i1)`2-p`2)
                                                      by A71,A72,JGRAPH_1:49;
     then |.Gij-p.| < r by A75,XXREAL_0:2;
     then dist(Gij',p') < r by SPPOL_1:62;
     then A77: Gij in Ball(p',r) by METRIC_1:12;
          |.Gji-p.| <= abs((f/.i1)`1-p`1) + abs((g/.i2)`2-p`2)
                                                      by A73,A74,JGRAPH_1:49;
     then |.Gji-p.| < r by A76,XXREAL_0:2;
     then dist(Gji',p') < r by SPPOL_1:62;
     then A78: Gji in Ball(p',r) by METRIC_1:12;
     A79: LSeg(g/.i2,Gij) c= Ball(p',rr) by A50,A70,A77,TOPREAL3:28;
     A80: LSeg(Gij,f/.i1) c= Ball(p',rr) by A33,A70,A77,TOPREAL3:28;
     A81: LSeg(g/.i2,Gji) c= Ball(p',rr) by A50,A70,A78,TOPREAL3:28;
     A82: LSeg(Gji,f/.i1) c= Ball(p',rr) by A33,A70,A78,TOPREAL3:28;
          now per cases;
      suppose jj2 <= jj1;
       then A83: LSeg(g/.i2,Gij) \/ LSeg(Gij,f/.i1) meets Upper_Arc C
                   by A23,A34,A44,A51,A52,A53,A58,A62,A64,A66,JORDAN15:50;
            LSeg(g/.i2,Gij) \/ LSeg(Gij,f/.i1) c= Ball(p',r)
       proof
        let x be set;
        assume A84: x in LSeg(g/.i2,Gij) \/ LSeg(Gij,f/.i1);
        then reconsider x' = x as Point of TOP-REAL 2;
             now per cases by A84,XBOOLE_0:def 2;
         suppose x' in LSeg(g/.i2,Gij);
          hence x' in Ball(p',r) by A79;
         end;
         suppose x' in LSeg(Gij,f/.i1);
          hence x' in Ball(p',r) by A80;
        end;
        end;
        hence x in Ball(p',r);
       end;
       hence Ball(p',r) meets Upper_Arc C by A83,XBOOLE_1:63;
      end;
      suppose jj1 < jj2;
       then A85: LSeg(f/.i1,Gji) \/ LSeg(Gji,g/.i2) meets Upper_Arc C
                       by A23,A34,A44,A51,A52,A53,A58,A62,A64,A66,Th26;
            LSeg(f/.i1,Gji) \/ LSeg(Gji,g/.i2) c= Ball(p',r)
       proof
        let x be set;
        assume A86: x in LSeg(f/.i1,Gji) \/ LSeg(Gji,g/.i2);
        then reconsider x' = x as Point of TOP-REAL 2;
             now per cases by A86,XBOOLE_0:def 2;
         suppose x' in LSeg(f/.i1,Gji);
          hence x' in Ball(p',r) by A82;
         end;
         suppose x' in LSeg(Gji,g/.i2);
          hence x' in Ball(p',r) by A81;
        end;
        end;
        hence x in Ball(p',r);
       end;
       hence Ball(p',r) meets Upper_Arc C by A85,XBOOLE_1:63;
     end;
     end;
     hence Ball(p',r) meets Upper_Arc C;
    end;
    then p in Cl Upper_Arc C by A5,GOBOARD6:96;
    then A87: p in Upper_Arc C by PRE_TOPC:52;
         now let r be real number;
     reconsider rr = r as Real by XREAL_0:def 1;
     assume A88: 0 < r & r < s;
     then A89: r/8 > 0 by REAL_2:127;
     then reconsider G = Ball(p',r/8) as a_neighborhood of p by GOBOARD6:5;
     consider k1 be Element of NAT such that
      A90: for m be Element of NAT st m > k1
       holds (Upper_Appr C).m meets G by A3,KURATO_2:def 11;
     consider k2 be Element of NAT such that
      A91: for m be Element of NAT st m > k2
       holds (Lower_Appr C).m meets G by A4,KURATO_2:def 11;
     set k = max(k1,k2);
     A92: k >= k1 by XXREAL_0:25;
     set z' = max(N-bound C - S-bound C,E-bound C - W-bound C);
     set z = max(z',r/8);
          z >= r/8 by XXREAL_0:25;
     then z/(r/8) >= 1 by A89,REAL_2:143;
     then log(2,z/(r/8)) >= log(2,1) by PRE_FF:12;
     then log(2,z/(r/8)) >= 0 by POWER:59;
     then reconsider m' = [\ log(2,z/(r/8)) /] as Element of NAT by INT_1:80;
     A93: 2 to_power (m'+1) > 0 by POWER:39;
     set N = 2 to_power (m'+1);
          log(2,z/(r/8)) < (m'+1) * 1 by INT_1:52;
     then log(2,z/(r/8)) < (m'+1) * log(2,2) by POWER:60;
     then log(2,z/(r/8)) < log(2,2 to_power (m'+1)) by POWER:63;
     then z/(r/8) < N by A93,PRE_FF:12;
     then z/(r/8)*(r/8) < N*(r/8) by A89,XREAL_1:70;
     then z < N*(r/8) by A89,XCMPLX_1:88;
     then z/N < N*(r/8)/N by A93,REAL_1:73;
     then z/N < (r/8)/N*N by XCMPLX_1:75;
     then A94: z/N < r/8 by A93,XCMPLX_1:88;
          z >= z' by XXREAL_0:25;
     then z/N >= z'/N by A93,REAL_1:73;
     then A95: z'/N < r/8 by A94,XXREAL_0:2;
     set m = max(k,m')+1;
     A96: len Gauge(C,m) = width Gauge(C,m) by JORDAN8:def 1;
          max(k,m') >= k by XXREAL_0:25;
     then max(k,m') >= k1 by A92,XXREAL_0:2;
     then m > k1 by NAT_1:13;
     then (Upper_Appr C).m meets G by A90;
     then Upper_Arc L~Cage (C,m) meets G by Def1;
     then consider p1 be set such that
      A97: p1 in Upper_Arc L~Cage (C,m) and
      A98: p1 in G by XBOOLE_0:3;
     reconsider p1 as Point of TOP-REAL 2 by A97;
     reconsider p1' = p1 as Point of Euclid 2 by EUCLID:25;
     set f = Upper_Seq(C,m);
     A99: Upper_Arc L~Cage(C,m) = L~Upper_Seq(C,m) by JORDAN1G:63;
     then consider i1 be Element of NAT such that
      A100: 1 <= i1 & i1+1 <= len f and
      A101: p1 in LSeg(f/.i1,f/.(i1+1)) by A97,SPPOL_2:14;
     reconsider c1 = f/.i1 as Point of Euclid 2 by EUCLID:25;
     reconsider c2 = f/.(i1+1) as Point of Euclid 2 by EUCLID:25;
     A102: f is_sequence_on Gauge(C,m) by JORDAN1G:4;
          i1 < len f by A100,NAT_1:13;
     then i1 in Seg len f by A100,FINSEQ_1:3;
     then A103: i1 in dom f by FINSEQ_1:def 3;
     then consider ii1,jj1 be Element of NAT such that
      A104: [ii1,jj1] in Indices Gauge(C,m) and
      A105: f/.i1 = Gauge(C,m)*(ii1,jj1) by A102,GOBOARD1:def 11;
          N-bound C > S-bound C+0 & E-bound C > W-bound C+0 by TOPREAL5:22,23
;
     then A106: N-bound C - S-bound C > 0 &
               E-bound C - W-bound C > 0 by XREAL_1:22;
     A107: 2|^(m'+1) > 0 by A93,POWER:48;
          max(k,m') >= m' by XXREAL_0:25;
     then m > m' by NAT_1:13;
     then m >= m'+1 by NAT_1:13;
     then 2|^m >= 2|^(m'+1) by PREPOWER:107;
     then A108: (N-bound C - S-bound C)/2|^m<=(N-bound C - S-bound C)/2|^(m'+1)
      & (E-bound C - W-bound C)/2|^m <= (E-bound C - W-bound C)/2|^(m'+1)
                                                       by A106,A107,REAL_2:201;
          N-bound C - S-bound C <= z' & E-bound C - W-bound C <= z' by
XXREAL_0:25;
     then (N-bound C - S-bound C)/N <= z'/N &
          (E-bound C - W-bound C)/N <= z'/N by A93,REAL_1:73;
     then (N-bound C - S-bound C)/2|^(m'+1) <= z'/N &
          (E-bound C - W-bound C)/2|^(m'+1) <= z'/N by POWER:48;
     then A109: (N-bound C - S-bound C)/2|^m <= z'/N &
          (E-bound C - W-bound C)/2|^m <= z'/N by A108,XXREAL_0:2;
     then dist(f/.i1,f/.(i1+1)) <= z'/N by A100,A102,Th6;
     then dist(f/.i1,f/.(i1+1)) < r/8 by A95,XXREAL_0:2;
     then dist(c1,c2) < r/8 by TOPREAL6:def 1;
     then A110: |. f/.i1 - f/.(i1+1) .| < r/8 by SPPOL_1:62;
          |. p1 - f/.i1 .| <= |. f/.i1 - f/.(i1+1) .| by A101,JGRAPH_1:53;
     then A111: |. p1 - f/.i1 .| < r/8 by A110,XXREAL_0:2;
          dist(p1',p') < r/8 by A98,METRIC_1:12;
     then |. p-p1 .| < r/8 by SPPOL_1:62;
     then A112: |. p-p1 .| + |. p1 - f/.i1 .| < r/(2*4) + r/(2*4) by A111,
XREAL_1:10;
          |. p - f/.i1 .| <= |. p - p1 .| + |. p1 - f/.i1 .| by TOPRNS_1:35;
     then A113: |. p - f/.i1 .| < r/4 by A112,XXREAL_0:2;
     then A114: dist(p',c1) < r/4 by SPPOL_1:62;
     then A115: f/.i1 in Ball(p',r/4) by METRIC_1:12;
     A116: f/.i1 in Upper_Arc L~Cage(C,m) by A99,A103,SPPOL_2:48;
     A117: k >= k2 by XXREAL_0:25;
          max(k,m') >= k by XXREAL_0:25;
     then max(k,m') >= k2 by A117,XXREAL_0:2;
     then m > k2 by NAT_1:13;
     then (Lower_Appr C).m meets G by A91;
     then Lower_Arc L~Cage (C,m) meets G by Def2;
     then consider p2 be set such that
      A118: p2 in Lower_Arc L~Cage (C,m) and
      A119: p2 in G by XBOOLE_0:3;
     reconsider p2 as Point of TOP-REAL 2 by A118;
     reconsider p2' = p2 as Point of Euclid 2 by EUCLID:25;
     set g = Lower_Seq(C,m);
     A120: Lower_Arc L~Cage(C,m) = L~Lower_Seq(C,m) by JORDAN1G:64;
     then consider i2 be Element of NAT such that
      A121: 1 <= i2 & i2+1 <= len g and
      A122: p2 in LSeg(g/.i2,g/.(i2+1)) by A118,SPPOL_2:14;
     reconsider d1 = g/.i2 as Point of Euclid 2 by EUCLID:25;
     reconsider d2 = g/.(i2+1) as Point of Euclid 2 by EUCLID:25;
     A123: g is_sequence_on Gauge(C,m) by JORDAN1G:5;
          i2 < len g by A121,NAT_1:13;
     then i2 in Seg len g by A121,FINSEQ_1:3;
     then A124: i2 in dom g by FINSEQ_1:def 3;
     then consider ii2,jj2 be Element of NAT such that
      A125: [ii2,jj2] in Indices Gauge(C,m) and
      A126: g/.i2 = Gauge(C,m)*(ii2,jj2) by A123,GOBOARD1:def 11;
          dist(g/.i2,g/.(i2+1)) <= z'/N by A109,A121,A123,Th6;
     then dist(g/.i2,g/.(i2+1)) < r/8 by A95,XXREAL_0:2;
     then dist(d1,d2) < r/8 by TOPREAL6:def 1;
     then A127: |. g/.i2 - g/.(i2+1) .| < r/8 by SPPOL_1:62;
          |. p2 - g/.i2 .| <= |. g/.i2 - g/.(i2+1) .| by A122,JGRAPH_1:53;
     then A128: |. p2 - g/.i2 .| < r/8 by A127,XXREAL_0:2;
          dist(p2',p') < r/8 by A119,METRIC_1:12;
     then |. p-p2 .| < r/8 by SPPOL_1:62;
     then A129: |. p-p2 .| + |. p2 - g/.i2 .| < r/(2*4) + r/(2*4) by A128,
XREAL_1:10;
          |. p - g/.i2 .| <= |. p - p2 .| + |. p2 - g/.i2 .| by TOPRNS_1:35;
     then A130: |. p - g/.i2 .| < r/4 by A129,XXREAL_0:2;
     then A131: dist(p',d1) < r/4 by SPPOL_1:62;
     then A132: g/.i2 in Ball(p',r/4) by METRIC_1:12;
     A133: g/.i2 in Lower_Arc L~Cage(C,m) by A120,A124,SPPOL_2:48;
     set Gij = Gauge(C,m)*(ii2,jj1);
     set Gji = Gauge(C,m)*(ii1,jj2);
     reconsider Gij' = Gij, Gji' = Gji as Point of Euclid 2 by EUCLID:25;
     A134: 1 <= ii1 & ii1 <= len Gauge(C,m) &
          1 <= jj1 & jj1 <= width Gauge(C,m) by A104,GOBOARD5:1;
     A135: 1 <= ii2 & ii2 <= len Gauge(C,m) &
          1 <= jj2 & jj2 <= width Gauge(C,m) by A125,GOBOARD5:1;
          len f >= 3 & len g >= 3 by JORDAN1E:19;
     then len f >= 1 & len g >= 1 by XXREAL_0:2;
     then len f in Seg len f & len g in Seg len g by FINSEQ_1:3;
     then A136: len f in dom f & len g in dom g by FINSEQ_1:def 3;
     A137: r/4 < r by A88,XREAL_1:225;
     A138: r/2 < r by A88,XREAL_1:218;
     A139: s <= p`1 - W-bound C & s <= E-bound C - p`1 by XXREAL_0:17;
     A140: now assume 1 >= ii1;
      then A141: ii1 = 1 by A134,XXREAL_0:1;
           dist(p',c1) < r by A114,A137,XXREAL_0:2;
      then dist(p',c1) < s by A88,XXREAL_0:2;
      then A142: dist(p',c1) < p`1 - W-bound C by A139,XXREAL_0:2;
      A143: p`1-(f/.i1)`1 <= abs(p`1-(f/.i1)`1) by ABSVALUE:11;
           abs(p`1-(f/.i1)`1) <= |.p-f/.i1.| by JGRAPH_1:51;
      then p`1 - (f/.i1)`1 <= |.p-f/.i1.| by A143,XXREAL_0:2;
      then p`1 - W-bound L~Cage(C,m) <= |.p-f/.i1.|
                                               by A96,A105,A134,A141,
JORDAN1A:94;
      then p`1 - W-bound L~Cage(C,m) <= dist(p',c1) by SPPOL_1:62;
      then p`1 - W-bound L~Cage(C,m) < p`1 - W-bound C by A142,XXREAL_0:2;
      then W-bound L~Cage(C,m) > W-bound C by REAL_1:92;
      hence contradiction by Th12;
     end;
     A144: now assume ii1 >= len Gauge(C,m);
      then A145: ii1 = len Gauge(C,m) by A134,XXREAL_0:1;
           (Gauge(C,m)*(len Gauge(C,m),jj1))`1 = E-bound L~Cage(C,m)
                                                       by A96,A134,JORDAN1A:92
;
      then f/.i1 = E-max L~Cage(C,m) by A99,A105,A116,A145,JORDAN1J:46
         .= f/.len f by JORDAN1F:7;
      then i1 = len f by A103,A136,PARTFUN2:17;
      hence contradiction by A100,NAT_1:13;
     end;
     A146: now assume ii2 <= 1;
      then A147: ii2 = 1 by A135,XXREAL_0:1;
           (Gauge(C,m)*(1,jj2))`1 = W-bound L~Cage(C,m) by A96,A135,JORDAN1A:94
;
      then g/.i2 = W-min L~Cage(C,m) by A120,A126,A133,A147,JORDAN1J:47
         .= g/.len g by JORDAN1F:8;
      then i2 = len g by A124,A136,PARTFUN2:17;
      hence contradiction by A121,NAT_1:13;
     end;
     A148: now assume ii2 >= len Gauge(C,m);
      then A149: ii2 = len Gauge(C,m) by A135,XXREAL_0:1;
           dist(p',d1) < r by A131,A137,XXREAL_0:2;
      then dist(p',d1) < s by A88,XXREAL_0:2;
      then A150: dist(p',d1) < E-bound C - p`1 by A139,XXREAL_0:2;
      A151: (g/.i2)`1-p`1 <= abs((g/.i2)`1-p`1) by ABSVALUE:11;
           abs((g/.i2)`1-p`1) <= |.g/.i2-p.| by JGRAPH_1:51;
      then abs((g/.i2)`1-p`1) <= |.p-g/.i2.| by TOPRNS_1:28;
      then (g/.i2)`1 - p`1 <= |.p-g/.i2.| by A151,XXREAL_0:2;
      then E-bound L~Cage(C,m) - p`1 <= |.p-g/.i2.|
                                               by A96,A126,A135,A149,
JORDAN1A:92;
      then E-bound L~Cage(C,m) - p`1 <= dist(p',d1) by SPPOL_1:62;
      then E-bound L~Cage(C,m) - p`1 < E-bound C - p`1 by A150,XXREAL_0:2;
      then E-bound L~Cage(C,m) < E-bound C by REAL_1:92;
      hence contradiction by Th10;
     end;
     A152: Ball(p',rr/4) c= Ball(p',rr) by A137,PCOMPS_1:1;
     A153: Gij`1 = Gauge(C,m)*(ii2,1)`1 by A134,A135,GOBOARD5:3
        .= (g/.i2)`1 by A126,A135,GOBOARD5:3;
     A154: Gij`2 = Gauge(C,m)*(1,jj1)`2 by A134,A135,GOBOARD5:2
        .= (f/.i1)`2 by A105,A134,GOBOARD5:2;
     A155: Gji`1 = Gauge(C,m)*(ii1,1)`1 by A134,A135,GOBOARD5:3
        .= (f/.i1)`1 by A105,A134,GOBOARD5:3;
     A156: Gji`2 = Gauge(C,m)*(1,jj2)`2 by A134,A135,GOBOARD5:2
        .= (g/.i2)`2 by A126,A135,GOBOARD5:2;
          abs((g/.i2)`1-p`1) <= |.(g/.i2)-p.| & abs((f/.i1)`2-p`2) <= |.(f/.
i1)-p.|
                                                              by JGRAPH_1:51;
     then abs((g/.i2)`1-p`1) <= |.p-(g/.i2).| &
          abs((f/.i1)`2-p`2) <= |.p-(f/.i1).| by TOPRNS_1:28;
     then abs((g/.i2)`1-p`1) <= r/4 & abs((f/.i1)`2-p`2) <= r/4
                                                        by A113,A130,XXREAL_0:2
;
     then abs((g/.i2)`1-p`1) + abs((f/.i1)`2-p`2) <= r/(2*2) + r/(2*2)
                                                                by XREAL_1:9;
     then A157: abs((g/.i2)`1-p`1) + abs((f/.i1)`2-p`2) < r by A138,XXREAL_0:2;
          abs((f/.i1)`1-p`1) <= |.(f/.i1)-p.| & abs((g/.i2)`2-p`2) <= |.(g/.
i2)-p.|
                                                              by JGRAPH_1:51;
     then abs((f/.i1)`1-p`1) <= |.p-(f/.i1).| &
          abs((g/.i2)`2-p`2) <= |.p-(g/.i2).| by TOPRNS_1:28;
     then abs((f/.i1)`1-p`1) <= r/4 & abs((g/.i2)`2-p`2) <= r/4
                                                        by A113,A130,XXREAL_0:2
;
     then abs((f/.i1)`1-p`1) + abs((g/.i2)`2-p`2) <= r/(2*2) + r/(2*2)
                                                                by XREAL_1:9;
     then A158: abs((f/.i1)`1-p`1) + abs((g/.i2)`2-p`2) < r by A138,XXREAL_0:2;
          |.Gij-p.| <= abs((g/.i2)`1-p`1) + abs((f/.i1)`2-p`2)
                                                      by A153,A154,JGRAPH_1:49;
     then |.Gij-p.| < r by A157,XXREAL_0:2;
     then dist(Gij',p') < r by SPPOL_1:62;
     then A159: Gij in Ball(p',r) by METRIC_1:12;
          |.Gji-p.| <= abs((f/.i1)`1-p`1) + abs((g/.i2)`2-p`2)
                                                      by A155,A156,JGRAPH_1:49;
     then |.Gji-p.| < r by A158,XXREAL_0:2;
     then dist(Gji',p') < r by SPPOL_1:62;
     then A160: Gji in Ball(p',r) by METRIC_1:12;
     A161: LSeg(g/.i2,Gij) c= Ball(p',rr) by A132,A152,A159,TOPREAL3:28;
     A162: LSeg(Gij,f/.i1) c= Ball(p',rr) by A115,A152,A159,TOPREAL3:28;
     A163: LSeg(g/.i2,Gji) c= Ball(p',rr) by A132,A152,A160,TOPREAL3:28;
     A164: LSeg(Gji,f/.i1) c= Ball(p',rr) by A115,A152,A160,TOPREAL3:28;
          now per cases;
      suppose jj2 <= jj1;
       then A165: LSeg(g/.i2,Gij) \/ LSeg(Gij,f/.i1) meets Lower_Arc C
                   by A105,A116,A126,A133,A134,A135,A140,A144,A146,A148,
JORDAN15:51;
            LSeg(g/.i2,Gij) \/ LSeg(Gij,f/.i1) c= Ball(p',r)
       proof
        let x be set;
        assume A166: x in LSeg(g/.i2,Gij) \/ LSeg(Gij,f/.i1);
        then reconsider x' = x as Point of TOP-REAL 2;
             now per cases by A166,XBOOLE_0:def 2;
         suppose x' in LSeg(g/.i2,Gij);
          hence x' in Ball(p',r) by A161;
         end;
         suppose x' in LSeg(Gij,f/.i1);
          hence x' in Ball(p',r) by A162;
        end;
        end;
        hence x in Ball(p',r);
       end;
       hence Ball(p',r) meets Lower_Arc C by A165,XBOOLE_1:63;
      end;
      suppose jj1 < jj2;
       then A167: LSeg(f/.i1,Gji) \/ LSeg(Gji,g/.i2) meets Lower_Arc C
                       by A105,A116,A126,A133,A134,A135,A140,A144,A146,A148,
Th25;
            LSeg(f/.i1,Gji) \/ LSeg(Gji,g/.i2) c= Ball(p',r)
       proof
        let x be set;
        assume A168: x in LSeg(f/.i1,Gji) \/ LSeg(Gji,g/.i2);
        then reconsider x' = x as Point of TOP-REAL 2;
             now per cases by A168,XBOOLE_0:def 2;
         suppose x' in LSeg(f/.i1,Gji);
          hence x' in Ball(p',r) by A164;
         end;
         suppose x' in LSeg(Gji,g/.i2);
          hence x' in Ball(p',r) by A163;
        end;
        end;
        hence x in Ball(p',r);
       end;
       hence Ball(p',r) meets Lower_Arc C by A167,XBOOLE_1:63;
     end;
     end;
     hence Ball(p',r) meets Lower_Arc C;
    end;
    then p in Cl Lower_Arc C by A5,GOBOARD6:96;
    then p in Lower_Arc C by PRE_TOPC:52;
    then p in Upper_Arc C /\ Lower_Arc C by A87,XBOOLE_0:def 3;
    then p in {W-min C,E-max C} by JORDAN6:65;
    then p = W-min C or p = E-max C by TARSKI:def 2;
    hence contradiction by A1,A2,EUCLID:56;
   end;

  theorem :: "Nie oba"
        for C be Simple_closed_curve
   for p be Point of TOP-REAL 2 st p`1 = (W-bound C + E-bound C)/2 holds
    not (p in North_Arc C & p in South_Arc C)
   proof
    let C be Simple_closed_curve;
    let p be Point of TOP-REAL 2;
    A1: W-bound C < E-bound C by SPRECT_1:33;
    assume p`1 = (W-bound C + E-bound C)/2;
    then W-bound C < p`1 & p`1 < E-bound C by A1,XREAL_1:228;
    hence not (p in North_Arc C & p in South_Arc C) by Th27;
   end;

