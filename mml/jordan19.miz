:: On the Upper and Lower Approximations of the Curve
::  by Robert Milewski
::
:: Received September 27, 2003
:: Copyright (c) 2003 Association of Mizar Users

environ

 vocabularies JORDAN19, JORDAN8, PSCOMP_1, BOOLE, JORDAN1A, JORDAN9, GOBOARD1,
      GOBOARD5, JORDAN6, SPRECT_2, JORDAN3, FINSEQ_5, JORDAN2C, GROUP_2, NAT_1,
      SPPOL_2, REALSET1, JORDAN1E, SQUARE_1, FINSEQ_4, TOPREAL2, CONNSP_1,
      COMPTS_1, TOPREAL1, SPPOL_1, FINSEQ_1, TREES_1, EUCLID, RELAT_1, MCART_1,
      MATRIX_1, GOBOARD9, PRE_TOPC, RELAT_2, SEQM_3, SUBSET_1, FUNCT_1,
      ARYTM_1, FINSEQ_6, GOBOARD2, GROUP_1, GRAPH_2, METRIC_1, ARYTM, ORDINAL2,
      ARYTM_3, INT_1, FUNCT_5, ABSVALUE, CONNSP_2, COMPLEX1, POWER, KURATO_2,
      PROB_1;
 notations TARSKI, XBOOLE_0, ENUMSET1, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0,
      XXREAL_0, XREAL_0, COMPLEX1, REAL_1, INT_1, NAT_1, NAT_D, FUNCT_1,
      RELSET_1, PARTFUN1, LIMFUNC1, FINSEQ_1, FINSEQ_4, FINSEQ_5, MATRIX_1,
      FINSEQ_6, POWER, METRIC_1, CONNSP_2, GRAPH_2, REALSET1, STRUCT_0,
      PRE_TOPC, TOPREAL2, NEWTON, BINARITH, PROB_1, CONNSP_1, COMPTS_1, EUCLID,
      PSCOMP_1, SPRECT_2, SPPOL_2, KURATO_2, GOBOARD1, TOPREAL6, TOPREAL1,
      GOBOARD2, GOBOARD5, GOBOARD9, GOBRD13, SPPOL_1, JORDAN3, JORDAN6,
      JORDAN8, JORDAN9, JORDAN2C, JORDAN1A, JORDAN1E;
 constructors REAL_1, NAT_1, NAT_D, FINSEQ_4, LIMFUNC1, NEWTON, POWER,
      BINARITH, CONNSP_1, REALSET2, CONNSP_2, MATRIX_2, MONOID_0, GOBOARD2,
      JORDAN1, PSCOMP_1, GRAPH_2, GOBOARD9, JORDAN3, JORDAN5C, JORDAN6,
      JORDAN2C, TOPREAL6, JORDAN8, GOBRD13, JORDAN9, JORDAN1A, JORDAN1E,
      KURATO_2, VALUED_1, PARTFUN1, SEQ_1;
 registrations XBOOLE_0, SUBSET_1, ORDINAL1, RELSET_1, XXREAL_0, XREAL_0,
      REAL_1, NAT_1, FINSEQ_1, FINSEQ_5, FINSEQ_6, STRUCT_0, COMPTS_1,
      METRIC_1, MONOID_0, EUCLID, TOPREAL1, TOPREAL2, GOBOARD2, JORDAN1,
      SPPOL_2, PSCOMP_1, GOBRD11, TOPREAL5, SPRECT_1, SPRECT_2, JORDAN6,
      REVROT_1, TOPREAL6, JORDAN8, JORDAN1E, JORDAN1J, VALUED_1, FUNCT_2;
 requirements NUMERALS, ARITHM, BOOLE, SUBSET, REAL;
 definitions TARSKI, XBOOLE_0, PSCOMP_1, EUCLID, VALUED_1;
 theorems NAT_1, FINSEQ_1, GOBOARD1, FINSEQ_4, EUCLID, FINSEQ_3, REAL_1,
      REAL_2, SPPOL_2, TARSKI, JORDAN3, PSCOMP_1, FINSEQ_5, FINSEQ_6, GOBOARD7,
      TOPREAL1, BINARITH, JORDAN5B, GOBOARD5, SPRECT_2, SPPOL_1, ABSVALUE,
      GOBOARD9, FINSEQ_2, SUBSET_1, GOBRD11, JORDAN4, GOBOARD2, SPRECT_3,
      TOPREAL3, JORDAN8, PARTFUN2, SPRECT_1, XBOOLE_0, XBOOLE_1, ZFMISC_1,
      GOBRD14, JORDAN2C, TOPREAL5, PRE_TOPC, JORDAN6, JORDAN9, JORDAN1H,
      JORDAN1A, JORDAN1E, JORDAN10, JGRAPH_1, REVROT_1, COMPTS_1, ENUMSET1,
      JORDAN1B, JORDAN1F, JORDAN1G, JORDAN1I, JORDAN1J, GOBOARD3, TOPREAL8,
      GRAPH_2, SPRECT_5, JORDAN1D, METRIC_1, POWER, PRE_FF, JORDAN15, PCOMPS_1,
      TOPRNS_1, BOOLMARK, JORDAN5D, XCMPLX_1, XREAL_0, KURATO_2, INT_1,
      REALSET1, XREAL_1, NEWTON, XXREAL_0, GOBOARD6, PREPOWER, TOPREAL6,
      PARTFUN1;
 schemes FUNCT_2, BINOP_2;

begin

reserve n for Element of NAT;

definition
  let C be Simple_closed_curve;
  func Upper_Appr C -> SetSequence of the carrier of TOP-REAL 2 means
  :Def1:
  for i be Element of NAT holds it.i = Upper_Arc L~Cage (C,i);
  existence
  proof
    deffunc O(Element of NAT) = Upper_Arc L~Cage (C,$1);
    thus ex S be SetSequence of the carrier of TOP-REAL 2 st
    for i being Element of NAT holds S.i = O(i) from FUNCT_2:sch 4;
  end;
  uniqueness
  proof
    deffunc O(Element of NAT) = Upper_Arc L~Cage (C,$1);
    thus for S1,S2 be SetSequence of the carrier of TOP-REAL 2 st
    (for i being Element of NAT holds S1.i = O(i)) &
    (for i being Element of NAT holds S2.i = O(i)) holds S1 = S2
    from BINOP_2:sch 1;
  end;
  func Lower_Appr C -> SetSequence of the carrier of TOP-REAL 2 means
  :Def2:
  for i being Element of NAT holds it.i = Lower_Arc L~Cage (C,i);
  existence
  proof
    deffunc O(Element of NAT) = Lower_Arc L~Cage (C,$1);
    thus ex S be SetSequence of the carrier of TOP-REAL 2 st
    for i being Element of NAT holds S.i = O(i) from FUNCT_2:sch 4;
  end;
  uniqueness
  proof
    deffunc O(Element of NAT) = Lower_Arc L~Cage (C,$1);
    thus for S1,S2 be SetSequence of the carrier of TOP-REAL 2 st
    (for i being Element of NAT holds S1.i = O(i)) &
    (for i being Element of NAT holds S2.i = O(i)) holds S1 = S2
    from BINOP_2:sch 1;
  end;
end;

definition
  let C be Simple_closed_curve;
  func North_Arc C -> Subset of TOP-REAL 2 equals

  Lim_inf Upper_Appr C;
  coherence;
  func South_Arc C -> Subset of TOP-REAL 2 equals

  Lim_inf Lower_Appr C;
  coherence;
end;

Lm1: now
  let G be Go-board;
  let j be Element of NAT;
  assume
A1: 1 <= j & j <= width G;
  0 + 1 <= len G div 2 + 1 by XREAL_1:8;
  then
A2: 0 + 1 <= Center G by JORDAN1A:def 1;
  Center G <= len G by JORDAN1B:14;
  hence [Center G,j] in Indices G by A1,A2,GOBOARD7:10;
end;

Lm2: now
  let D be non empty Subset of TOP-REAL 2;
  let n,i be Element of NAT;
  set a = N-bound D, s = S-bound D, w = W-bound D, e = E-bound D;
  set G = Gauge(D,n);
  assume [i,width G] in Indices G;
  hence G*(i,width G)`2
  = |[w+(e-w)/(2|^n)*(i - 2), s+(a-s)/(2|^n)*(width G - 2)]|`2
  by JORDAN8:def 1
    .= s+(a-s)/(2|^n)*(width G - 2) by EUCLID:56;
end;

theorem Th1:
  for n,m be Element of NAT holds n <= m & n <> 0 implies (n+1)/n >= (m+1)/m
proof
  let n,m be Element of NAT;
  assume that
A1: n <= m and
A2: n <> 0;
A3: n > 0 by A2;
  then
A4: 1/n >= 1/m by A1,REAL_2:152;
A5: (n+1)/n = n/n + 1/n by XCMPLX_1:63
    .= 1 + 1/n by A2,XCMPLX_1:60;
  (m+1)/m = m/m + 1/m by XCMPLX_1:63
    .= 1 + 1/m by A1,A3,XCMPLX_1:60;
  hence (n+1)/n >= (m+1)/m by A4,A5,XREAL_1:9;
end;

theorem Th2:
  for E be compact non vertical non horizontal Subset of TOP-REAL 2
  for m,j be Element of NAT
  st 1 <= m & m <= n & 1 <= j & j <= width Gauge(E,n) holds
  LSeg(Gauge(E,n)*(Center Gauge(E,n),width Gauge(E,n)),
  Gauge(E,n)*(Center Gauge(E,n),j)) c=
  LSeg(Gauge(E,m)*(Center Gauge(E,m),width Gauge(E,m)),
  Gauge(E,n)*(Center Gauge(E,n),j))
proof
  let E be compact non vertical non horizontal Subset of TOP-REAL 2;
  let m,j be Element of NAT;
  set a = N-bound E, s = S-bound E, w = W-bound E, e = E-bound E,
  G = Gauge(E,n), M = Gauge(E,m), sn = Center G, sm = Center M;
  assume that
A1: 1 <= m and
A2: m <= n and
A3: 1 <= j and
A4: j <= width G;
A5: width M = len M by JORDAN8:def 1
    .= 2|^m+3 by JORDAN8:def 1;
A6: width G = len G by JORDAN8:def 1
    .= 2|^n+3 by JORDAN8:def 1;
  now
    let t be Element of NAT;
    assume that
A7: width G >= t and
A8: t >= j;
A9: len M = width M by JORDAN8:def 1;
A10: len G = width G by JORDAN8:def 1;
A11: 0 < a - s by SPRECT_1:34,XREAL_1:52;
A12: t >= 1 by A3,A8,XXREAL_0:2;
A13: 0 < 2|^m by NEWTON:102;
A14: 0 < 2|^n by NEWTON:102;
A15: 1 <= len M by GOBRD11:34;
    then
A16: M*(sm,width M)`1 = G*(sn,t)`1 & G*(sn,t)`1 = G*(sn,j)`1
    by A1,A2,A3,A4,A7,A9,A10,A12,JORDAN1A:57;
    [sn,t] in Indices G by A7,A12,Lm1;
    then
A17: G*(sn,t)`2 = |[w+(e-w)/(2|^n)*(sn - 2),s+(a-s)/(2|^n)*(t-2)]|`2
    by JORDAN8:def 1
      .= s+(a-s)/(2|^n)*(t-2) by EUCLID:56;
    [sm,width M] in Indices M by A9,A15,Lm1;
    then
A18: M*(sm,width M)`2 = s+(a-s)/(2|^m)*(width M-2) by Lm2;
A19: (2|^m+1)/(2|^m) >= (2|^n+1)/(2|^n) by A2,A13,Th1,PREPOWER:107;
    t-2 <= 2|^n+3-2 by A6,A7,XREAL_1:11;
    then (t-2)/(2|^n) <= (2|^n+1)/(2|^n) by A14,REAL_1:73;
    then (t-2)/(2|^n) <= (width M-2)/(2|^m) by A5,A19,XXREAL_0:2;
    then (a-s)*((t-2)/(2|^n)) <= (a-s)*((width M-2)/(2|^m)) by A11,XREAL_1:66;
    then (a-s)/(2|^n)*(t-2) <= (a-s)*((width M-2)/(2|^m)) by XCMPLX_1:76;
    then (a-s)/(2|^m)*(width M-2) >= (a-s)/(2|^n)*(t-2) by XCMPLX_1:76;
    then
A20: s+(a-s)/(2|^m)*(width M-2) >= s+(a-s)/(2|^n)*(t-2) by XREAL_1:8;
    1 <= sn & sn <= len G by JORDAN1B:12,14;
    then G*(sn,t)`2 >= G*(sn,j)`2 by A3,A7,A8,SPRECT_3:24;
    hence G*(sn,t) in LSeg(M*(sm,width M),G*(sn,j)) by A16,A17,A18,A20,
    GOBOARD7:8;
  end;
  then G*(sn,width G) in LSeg(M*(sm,width M),G*(sn,j)) &
  G*(sn,j) in LSeg(M*(sm,width M),G*(sn,j)) by A4;
  hence thesis by TOPREAL1:12;
end;

theorem Th3:
  for C be compact connected non vertical non horizontal Subset of TOP-REAL 2
  for i,j be Element of NAT st 1 <= i & i <= len Gauge(C,n) &
  1 <= j & j <= width Gauge(C,n) & Gauge(C,n)*(i,j) in L~Cage(C,n) holds
  LSeg(Gauge(C,n)*(i,width Gauge(C,n)),Gauge(C,n)*(i,j)) meets
  L~Upper_Seq(C,n)
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
  let i,j be Element of NAT;
  set Gij = Gauge(C,n)*(i,j);
  assume that
A1: 1 <= i & i <= len Gauge(C,n) and
A2: 1 <= j & j <= width Gauge(C,n) and
A3: Gij in L~Cage(C,n);
  set NE = SW-corner L~Cage(C,n);
  set v1 = L_Cut(Lower_Seq(C,n),Gij);
  set wG = width Gauge(C,n);
  set lG = len Gauge(C,n);
  set Gv1 = <*Gauge(C,n)*(i,wG)*>^v1;
  set v = Gv1^<*NE*>;
  set h = mid(Upper_Seq(C,n),2,len Upper_Seq(C,n));
A4: L~Cage(C,n) = L~Lower_Seq(C,n) \/ L~Upper_Seq(C,n) by JORDAN1E:17;
A5: len Upper_Seq(C,n) >= 3 & len Lower_Seq(C,n) >= 3 by JORDAN1E:19;
  then
A6: len Upper_Seq(C,n) >= 2 & len Upper_Seq(C,n) >= 1 &
  len Lower_Seq(C,n) >= 2 & len Lower_Seq(C,n) >= 1 by XXREAL_0:2;
A7: len Gauge(C,n) = width Gauge(C,n) by JORDAN8:def 1;
  then width Gauge(C,n) >= 4 by JORDAN8:13;
  then
A8: 1 <= width Gauge(C,n) by XXREAL_0:2;
A9: Gauge(C,n)*(i,wG)`2 = N-bound L~Cage(C,n) by A1,A7,JORDAN1A:91;
  set Ema = E-max L~Cage(C,n);
  now per cases by A1,A3,A4,REAL_1:def 5,XBOOLE_0:def 2;
    suppose
A10:  Gij in L~Lower_Seq(C,n) & i = lG;
      set G11 = Gauge(C,n)*(lG,wG);
A11:  G11`1 = E-bound L~Cage(C,n) by A1,A7,A10,JORDAN1A:92;
A12:  Ema`1 = E-bound L~Cage(C,n) by EUCLID:56;
A13:  N-bound L~Cage(C,n) = G11`2 by A1,A7,A10,JORDAN1A:91;
      Ema in L~Cage(C,n) by SPRECT_1:16;
      then
A14:  G11`2 >= Ema`2 by A13,PSCOMP_1:71;
A15:  Gij`1 = E-bound L~Cage(C,n) by A2,A7,A10,JORDAN1A:92;
      then Gij in E-most L~Cage(C,n) by A3,SPRECT_2:17;
      then Ema`2 >= Gij`2 by PSCOMP_1:108;
      then
A16:  Ema in LSeg(Gauge(C,n)*(lG,wG),Gauge(C,n)*(lG,j))
      by A10,A11,A12,A14,A15,GOBOARD7:8;
A17:  rng Upper_Seq(C,n) c= L~Upper_Seq(C,n) by A5,SPPOL_2:18,XXREAL_0:2;
      Upper_Seq(C,n)/.(len Upper_Seq(C,n)) = Ema by JORDAN1F:7;
      then Ema in rng Upper_Seq(C,n) by REVROT_1:3;
      hence LSeg(Gauge(C,n)*(i,wG),Gij) meets L~Upper_Seq(C,n)
      by A10,A16,A17,XBOOLE_0:3;
    end;
    suppose
A18:  Gij in L~Lower_Seq(C,n) & Gij <> Lower_Seq(C,n).len Lower_Seq(C,n)
      & W-min L~Cage(C,n) <> NE & i < lG;
      then
A19:  v1 is non empty by JORDAN1E:7;
      then len v1 <> 0 by FINSEQ_1:25;
      then len v1 > 0;
      then
A20:  0+1 <= len v1 by NAT_1:13;
      then
A21:  1 in dom v1 by FINSEQ_3:27;
A22:  len v1 in dom v1 & len Lower_Seq(C,n) in dom Lower_Seq(C,n)
      by A6,A20,FINSEQ_3:27;
      then
A23:  v1/.(len v1) = v1.(len v1) by PARTFUN1:def 8
        .= Lower_Seq(C,n).len Lower_Seq(C,n) by A18,JORDAN1B:5
        .= Lower_Seq(C,n)/.len Lower_Seq(C,n) by A22,PARTFUN1:def 8
        .= W-min L~Cage(C,n) by JORDAN1F:8;
      then
A24:  Gv1/.len Gv1 = W-min L~Cage(C,n) by A19,SPRECT_3:11;
A25:  v1/.1 = v1.1 by A21,PARTFUN1:def 8
        .= Gij by A18,JORDAN3:58;
      then
A26:  (v1^<*NE*>)/.1 = Gij by A20,BOOLMARK:8;
A27:  1+len v1 >= 1+1 by A20,XREAL_1:9;
      len v = len Gv1 + 1 by FINSEQ_2:19
        .= 1 + len v1 + 1 by FINSEQ_5:8;
      then 2 < len v by A27,NAT_1:13;
      then
A28:  2 < len Rev v by FINSEQ_5:def 3;
      S-bound L~Cage(C,n) < N-bound L~Cage(C,n) by SPRECT_1:34;
      then NE <> Gauge(C,n)*(i,wG) by A9,EUCLID:56;
      then not NE in {Gauge(C,n)*(i,wG)} by TARSKI:def 1;
      then
A29:  not NE in rng <*Gauge(C,n)*(i,wG)*> by FINSEQ_1:56;
      len Cage(C,n) > 4 by GOBOARD7:36;
      then
A30:  rng Cage(C,n) c= L~Cage(C,n) by SPPOL_2:18,XXREAL_0:2;
A31:  not NE in rng Cage(C,n)
      proof
        assume
A32:    NE in rng Cage(C,n);
A33:    NE`1 = W-bound L~Cage(C,n) & NE`2 = S-bound L~Cage(C,n) by EUCLID:56;
        then NE`2 <= N-bound L~Cage(C,n) by SPRECT_1:24;
        then NE in { p where p is Point of TOP-REAL 2 :
        p`1 = W-bound L~Cage(C,n) & p`2 <= N-bound L~Cage(C,n) &
        p`2 >= S-bound L~Cage(C,n) } by A33;
        then NE in LSeg(SW-corner L~Cage(C,n), NW-corner L~Cage(C,n))
        by SPRECT_1:28;
        then NE in LSeg(SW-corner L~Cage(C,n), NW-corner L~Cage(C,n)) /\
        L~Cage(C,n) by A30,A32,XBOOLE_0:def 3;
        then
A34:    NE`2 >= (W-min L~Cage(C,n))`2 by PSCOMP_1:88;
        (W-min L~Cage(C,n))`2 >= NE`2 by PSCOMP_1:87;
        then
A35:    (W-min L~Cage(C,n))`2 = NE`2 by A34,XXREAL_0:1;
        (W-min L~Cage(C,n))`1 = NE`1 by PSCOMP_1:85;
        hence contradiction by A18,A35,TOPREAL3:11;
      end;
      now per cases;
        suppose Gij <> Lower_Seq(C,n).(Index(Gij,Lower_Seq(C,n))+1);
          then v1 = <*Gij*>^mid(Lower_Seq(C,n),
          Index(Gij,Lower_Seq(C,n))+1,len Lower_Seq(C,n)) by JORDAN3:def 4;
          then rng v1 = rng <*Gij*> \/ rng mid(Lower_Seq(C,n),
          Index(Gij,Lower_Seq(C,n))+1,len Lower_Seq(C,n)) by FINSEQ_1:44;
          then
A36:      rng v1 = {Gij} \/ rng mid(Lower_Seq(C,n),
          Index(Gij,Lower_Seq(C,n))+1,len Lower_Seq(C,n)) by FINSEQ_1:55;
          not NE in L~Cage(C,n)
          proof
            assume NE in L~Cage(C,n);
            then consider i be Element of NAT such that
A37:        1 <= i and
A38:        i+1 <= len Cage(C,n) and
A39:        NE in LSeg(Cage(C,n)/.i,Cage(C,n)/.(i+1)) by SPPOL_2:14;
            per cases by A37,A38,TOPREAL1:def 7;
            suppose
A40:          (Cage(C,n)/.i)`1 = (Cage(C,n)/.(i+1))`1;
              then
A41:          NE`1 = (Cage(C,n)/.i)`1 by A39,GOBOARD7:5;
A42:          NE`2 = S-bound L~Cage(C,n) by EUCLID:56;
A43:          i < len Cage(C,n) by A38,NAT_1:13;
              then
A44:          (Cage(C,n)/.i)`2 >= NE`2 by A37,A42,JORDAN5D: 13;
A45:          1 <= i+1 by NAT_1:11;
              then
A46:          (Cage(C,n)/.(i+1))`2 >= NE`2 by A38,A42, JORDAN5D:13;
A47:          i in dom Cage(C,n) & i+1 in dom Cage(C,n)
              by A37,A38,A43,A45,FINSEQ_3:27;
              (Cage(C,n)/.i)`2 <= (Cage(C,n)/.(i+1))`2 or
              (Cage(C,n)/.i)`2 >= (Cage(C,n)/.(i+1))`2;
              then NE`2 >= (Cage(C,n)/.(i+1))`2 or NE`2 >= (Cage(C,n)/.i)`2
              by A39,TOPREAL1:10;
              then NE`2 = (Cage(C,n)/.(i+1))`2 or NE`2 = (Cage(C,n)/.i)`2
              by A44,A46,XXREAL_0:1;
              then NE = (Cage(C,n)/.(i+1)) or NE = (Cage(C,n)/.i)
              by A40,A41,TOPREAL3:11;
              hence contradiction by A31,A47,PARTFUN2:4;
            end;
            suppose
A48:          (Cage(C,n)/.i)`2 = (Cage(C,n)/.(i+1))`2;
              then
A49:          NE`2 = (Cage(C,n)/.i)`2 by A39,GOBOARD7:6;
A50:          NE`1 = W-bound L~Cage(C,n) by EUCLID:56;
A51:          i < len Cage(C,n) by A38,NAT_1:13;
              then
A52:          (Cage(C,n)/.i)`1 >= NE`1 by A37,A50,JORDAN5D: 14;
A53:          1 <= i+1 by NAT_1:11;
              then
A54:          (Cage(C,n)/.(i+1))`1 >= NE`1 by A38,A50, JORDAN5D:14;
A55:          i in dom Cage(C,n) & i+1 in dom Cage(C,n)
              by A37,A38,A51,A53,FINSEQ_3:27;
              (Cage(C,n)/.i)`1 <= (Cage(C,n)/.(i+1))`1 or
              (Cage(C,n)/.i)`1 >= (Cage(C,n)/.(i+1))`1;
              then NE`1 >= (Cage(C,n)/.(i+1))`1 or NE`1 >= (Cage(C,n)/.i)`1
              by A39,TOPREAL1:9;
              then NE`1 = (Cage(C,n)/.(i+1))`1 or NE`1 = (Cage(C,n)/.i)`1
              by A52,A54,XXREAL_0:1;
              then NE = (Cage(C,n)/.(i+1)) or NE = (Cage(C,n)/.i)
              by A48,A49,TOPREAL3:11;
              hence contradiction by A31,A55,PARTFUN2:4;
            end;
          end;
          then
A56:      not NE in {Gij} by A3,TARSKI:def 1;
A57:      rng mid(Lower_Seq(C,n),Index(Gij,Lower_Seq(C,n))+1,
          len Lower_Seq(C,n)) c= rng Lower_Seq(C,n) by JORDAN3:28;
          rng Lower_Seq(C,n) c= rng Cage(C,n) by JORDAN1G:47;
          then rng mid(Lower_Seq(C,n),Index(Gij,Lower_Seq(C,n))+1,
          len Lower_Seq(C,n)) c= rng Cage(C,n) by A57,XBOOLE_1:1;
          then not NE in rng mid(Lower_Seq(C,n),Index(Gij,Lower_Seq(C,n))+1,
          len Lower_Seq(C,n)) by A31;
          hence not NE in rng v1 by A36,A56,XBOOLE_0:def 2;
        end;
        suppose Gij = Lower_Seq(C,n).(Index(Gij,Lower_Seq(C,n))+1);
          then v1 = mid(Lower_Seq(C,n),
          Index(Gij,Lower_Seq(C,n))+1,len Lower_Seq(C,n)) by JORDAN3:def 4;
          then
A58:      rng v1 c= rng Lower_Seq(C,n) by JORDAN3:28;
          rng Lower_Seq(C,n) c= rng Cage(C,n) by JORDAN1G:47;
          then rng v1 c= rng Cage(C,n) by A58,XBOOLE_1:1;
          hence not NE in rng v1 by A31;
        end;
      end;
      then not NE in rng <*Gauge(C,n)*(i,wG)*> \/ rng v1 by A29,XBOOLE_0:def 2;
      then not NE in rng Gv1 by FINSEQ_1:44;
      then rng Gv1 misses {NE} by ZFMISC_1:56;
      then
A59:  rng Gv1 misses rng <*NE*> by FINSEQ_1:55;
A60:  not Gauge(C,n)*(i,wG) in L~Lower_Seq(C,n) by A1,A18,JORDAN1G:53;
      rng Lower_Seq(C,n) c= L~Lower_Seq(C,n) by A5,SPPOL_2:18,XXREAL_0:2;
      then
A61:  not Gauge(C,n)*(i,wG) in rng Lower_Seq(C,n) by A1,A18,JORDAN1G :53;
      not Gauge(C,n)*(i,wG) in {Gij} by A18,A60,TARSKI:def 1;
      then
A62:  not Gauge(C,n)*(i,wG) in rng <*Gij*> by FINSEQ_1:55;
      set ci = mid(Lower_Seq(C,n),Index(Gij,Lower_Seq(C,n))+1,
      len Lower_Seq(C,n));
      now per cases;
        suppose
A63:      Gij <> Lower_Seq(C,n).(Index(Gij,Lower_Seq(C,n))+1);
          rng ci c= rng Lower_Seq(C,n) by JORDAN3:28;
          then not Gauge(C,n)*(i,wG) in rng ci by A61;
          then not Gauge(C,n)*(i,wG) in rng <*Gij*> \/ rng ci
          by A62,XBOOLE_0:def 2;
          then not Gauge(C,n)*(i,wG) in rng(<*Gij*>^ci) by FINSEQ_1:44;
          hence not Gauge(C,n)*(i,wG) in rng v1 by A63,JORDAN3:def 4;
        end;
        suppose Gij = Lower_Seq(C,n).(Index(Gij,Lower_Seq(C,n))+1);
          then v1 = ci by JORDAN3:def 4;
          then rng v1 c= rng Lower_Seq(C,n) by JORDAN3:28;
          hence not Gauge(C,n)*(i,wG) in rng v1 by A61;
        end;
      end;
      then {Gauge(C,n)*(i,wG)} misses rng v1 by ZFMISC_1:56;
      then
A64:  rng <*Gauge(C,n)*(i,wG)*> misses rng v1 by FINSEQ_1:55;
A65:  <*Gauge(C,n)*(i,wG)*> is one-to-one by FINSEQ_3:102;
A66:  v1 is_S-Seq by A18,JORDAN3:69;
      then v1 is one-to-one by TOPREAL1:def 10;
      then
A67:  Gv1 is one-to-one by A64,A65,FINSEQ_3:98;
      <*NE*> is one-to-one by FINSEQ_3:102;
      then
A68:  v is one-to-one by A59,A67,FINSEQ_3:98;
      for v be one-to-one FinSequence holds Rev v is one-to-one;
      then
A69:  Rev v is one-to-one by A68;
A70:  <*Gauge(C,n)*(i,wG)*> is special by SPPOL_2:39;
A71:  v1 is special by A66,TOPREAL1:def 10;
      (<*Gauge(C,n)*(i,wG)*>/.len <*Gauge(C,n)*(i,wG)*>)`1 =
      (<*Gauge(C,n)*(i,wG)*>/.1)`1 by FINSEQ_1:56
        .= Gauge(C,n)*(i,wG)`1 by FINSEQ_4:25
        .= Gauge(C,n)*(i,1)`1 by A1,A8,GOBOARD5:3
        .= (v1/.1)`1 by A1,A2,A25,GOBOARD5:3;
      then
A72:  Gv1 is special by A70,A71,GOBOARD2:13;
A73:  <*NE*> is special by SPPOL_2:39;
      (Gv1/.len Gv1)`1 = NE`1 by A24,PSCOMP_1:85
        .= (<*NE*>/.1)`1 by FINSEQ_4:25;
      then v is special by A72,A73,GOBOARD2:13;
      then
A74:  Rev v is special by SPPOL_2:42;
A75:  len Upper_Seq(C,n) >= 2+1 by JORDAN1E:19;
      then
A76:  len Upper_Seq(C,n) > 2 by NAT_1:13;
      len Upper_Seq(C,n) > 1 by A75,XXREAL_0:2;
      then h is S-Sequence_in_R2 by A76,JORDAN3:39;
      then
A77:  2 <= len h & h is one-to-one & h is special by TOPREAL1:def 10;
      3 <= len Upper_Seq(C,n) by JORDAN1E:19;
      then 2 <= len Upper_Seq(C,n) by XXREAL_0:2;
      then
A78:  2 in dom Upper_Seq(C,n) by FINSEQ_3:27;
A79:  len Upper_Seq(C,n) in dom Upper_Seq(C,n) by FINSEQ_5:6;
      then
A80:  h is_in_the_area_of Cage(C,n) by A78,JORDAN1E:21,SPRECT_2:26;
      Upper_Seq(C,n)/.(len Upper_Seq(C,n)) = E-max L~Cage(C,n) by JORDAN1F:7;
      then (Upper_Seq(C,n)/.len Upper_Seq(C,n))`1 = E-bound L~Cage(C,n)
      by EUCLID:56;
      then
A81:  (h/.len h)`1 = E-bound L~Cage(C,n) by A78,A79,SPRECT_2:13;
      (Upper_Seq(C,n)/.(1+1))`1 = W-bound L~Cage(C,n) by JORDAN1G:39;
      then (h/.1)`1 = W-bound L~Cage(C,n) by A78,A79,SPRECT_2:12;
      then
A82:  h is_a_h.c._for Cage(C,n) by A80,A81,SPRECT_2:def 2;
      now
        let m be Element of NAT;
        assume
A83:    m in dom <*Gauge(C,n)*(i,wG)*>;
        then m in Seg 1 by FINSEQ_1:55;
        then m = 1 by FINSEQ_1:4,TARSKI:def 1;
        then <*Gauge(C,n)*(i,wG)*>.m = Gauge(C,n)*(i,wG) by FINSEQ_1:57;
        then
A84:    <*Gauge(C,n)*(i,wG)*>/.m = Gauge(C,n)*(i,wG) by A83, PARTFUN1:def 8;
        Gauge(C,n)*(1,wG)`1 <= Gauge(C,n)*(i,wG)`1 by A1,A8,SPRECT_3:25;
        hence W-bound L~Cage(C,n) <= (<*Gauge(C,n)*(i,wG)*>/.m)`1
        by A7,A8,A84,JORDAN1A:94;
        (Gauge(C,n)*(i,wG))`1 <= Gauge(C,n)*(len Gauge(C,n),wG)`1
        by A1,A8,SPRECT_3:25;
        hence (<*Gauge(C,n)*(i,wG)*>/.m)`1 <= E-bound L~Cage(C,n)
        by A7,A8,A84,JORDAN1A:92;
        (<*Gauge(C,n)*(i,wG)*>/.m)`2 = N-bound L~Cage(C,n)
        by A1,A7,A84,JORDAN1A:91;
        hence S-bound L~Cage(C,n) <= (<*Gauge(C,n)*(i,wG)*>/.m)`2
        by SPRECT_1:24;
        thus (<*Gauge(C,n)*(i,wG)*>/.m)`2 <= N-bound L~Cage(C,n)
        by A1,A7,A84,JORDAN1A:91;
      end;
      then
A85:  <*Gauge(C,n)*(i,wG)*> is_in_the_area_of Cage(C,n) by SPRECT_2: def 1;
      <*Gij*> is_in_the_area_of Cage(C,n) by A18,JORDAN1E:22,SPRECT_3:63;
      then v1 is_in_the_area_of Cage(C,n) by A18,JORDAN1E:22,SPRECT_3:73;
      then
A86:  Gv1 is_in_the_area_of Cage(C,n) by A85,SPRECT_2:28;
      <*NE*> is_in_the_area_of Cage(C,n) by SPRECT_2:32;
      then v is_in_the_area_of Cage(C,n) by A86,SPRECT_2:28;
      then
A87:  Rev v is_in_the_area_of Cage(C,n) by SPRECT_3:68;
      v = <*Gauge(C,n)*(i,wG)*>^(v1^<*NE*>) by FINSEQ_1:45;
      then v/.1 = Gauge(C,n)*(i,wG) by FINSEQ_5:16;
      then (v/.1)`2 = N-bound L~Cage(C,n) by A1,A7,JORDAN1A:91;
      then (Rev v/.(len v))`2 = N-bound L~Cage(C,n) by FINSEQ_5:68;
      then
A88:  (Rev v/.(len Rev v))`2 = N-bound L~Cage(C,n) by FINSEQ_5:def 3;
      len v = len Gv1 + 1 by FINSEQ_2:19;
      then v/.(len v) = NE by FINSEQ_4:82;
      then (v/.len v)`2 = S-bound L~Cage(C,n) by EUCLID:56;
      then (Rev v/.1)`2 = S-bound L~Cage(C,n) by FINSEQ_5:68;
      then Rev v is_a_v.c._for Cage(C,n) by A87,A88,SPRECT_2:def 3;
      then L~h meets L~Rev v by A28,A69,A74,A77,A82,SPRECT_2:33;
      then L~h meets L~v by SPPOL_2:22;
      then consider x be set such that
A89:  x in L~h and
A90:  x in L~v by XBOOLE_0:3;
A91:  L~h c= L~Upper_Seq(C,n) by A6,JORDAN4:47;
A92:  L~v1 c= L~Lower_Seq(C,n) by A18,JORDAN3:77;
      L~v = L~(<*Gauge(C,n)*(i,wG)*>^(v1^<*NE*>)) by FINSEQ_1:45
        .= LSeg(Gauge(C,n)*(i,wG),(v1^<*NE*>)/.1) \/ L~(v1^<*NE*>)
      by SPPOL_2:20
        .= LSeg(Gauge(C,n)*(i,wG),(v1^<*NE*>)/.1) \/
      (L~v1 \/ LSeg(v1/.(len v1),NE)) by A19,SPPOL_2:19;
      then
A93:  x in LSeg(Gauge(C,n)*(i,wG),(v1^<*NE*>)/.1) or
      x in L~v1 \/ LSeg(v1/.(len v1),NE) by A90,XBOOLE_0:def 2;
      Upper_Seq(C,n)/.1 = W-min L~Cage(C,n) by JORDAN1F:5;
      then
A94:  not W-min L~Cage(C,n) in L~h by A76,JORDAN5B:16;
      now per cases by A93,XBOOLE_0:def 2;
        suppose x in LSeg(Gauge(C,n)*(i,wG),(v1^<*NE*>)/.1);
          then x in L~<*Gauge(C,n)*(i,wG),Gij*> by A26,SPPOL_2:21;
          hence L~Upper_Seq(C,n) meets L~<*Gauge(C,n)*(i,wG),Gij*>
          by A89,A91,XBOOLE_0:3;
        end;
        suppose
A95:      x in L~v1;
          then x in L~Lower_Seq(C,n) /\ L~Upper_Seq(C,n)
          by A89,A91,A92,XBOOLE_0:def 3;
          then x in {W-min L~Cage(C,n),E-max L~Cage(C,n)} by JORDAN1E:20;
          then
A96:      x = E-max L~Cage(C,n) by A89,A94,TARSKI:def 2;
          1 in dom Lower_Seq(C,n) by A6,FINSEQ_3:27;
          then Lower_Seq(C,n).1 = Lower_Seq(C,n)/.1 by PARTFUN1:def 8
            .= E-max L~Cage(C,n) by JORDAN1F:6;
          then x = Gij by A18,A95,A96,JORDAN1E:11;
          then x in LSeg(Gauge(C,n)*(i,wG),Gij) by TOPREAL1:6;
          then x in L~<*Gauge(C,n)*(i,wG),Gij*> by SPPOL_2:21;
          hence L~Upper_Seq(C,n) meets L~<*Gauge(C,n)*(i,wG),Gij*>
          by A89,A91,XBOOLE_0:3;
        end;
        suppose
A97:      x in LSeg(v1/.(len v1),NE);
          x in L~Cage(C,n) by A4,A89,A91,XBOOLE_0:def 2;
          then x in LSeg(W-min L~Cage(C,n), NE) /\ L~Cage(C,n)
          by A23,A97,XBOOLE_0:def 3;
          then x in {W-min L~Cage(C,n)} by PSCOMP_1:92;
          hence L~Upper_Seq(C,n) meets L~<*Gauge(C,n)*(i,wG),Gij*>
          by A89,A94,TARSKI:def 1;
        end;
      end;
      then L~<*Gauge(C,n)*(i,wG),Gij*> meets L~Upper_Seq(C,n);
      hence LSeg(Gauge(C,n)*(i,width Gauge(C,n)),Gij) meets L~Upper_Seq(C,n)
      by SPPOL_2:21;
    end;
    suppose
A98:  Gij in L~Lower_Seq(C,n) & Gij <> Lower_Seq(C,n).len Lower_Seq(C,n)
      & W-min L~Cage(C,n) = NE & i < lG;
      then
A99:  v1 is non empty by JORDAN1E:7;
      then len v1 <> 0 by FINSEQ_1:25;
      then len v1 > 0;
      then
A100: 0+1 <= len v1 by NAT_1:13;
      then
A101: 1 in dom v1 by FINSEQ_3:27;
      set v = Gv1;
A102: len v1 in dom v1 & len Lower_Seq(C,n) in dom Lower_Seq(C,n)
      by A6,A100,FINSEQ_3:27;
      then v1/.(len v1) = v1.(len v1) by PARTFUN1:def 8
        .= Lower_Seq(C,n).len Lower_Seq(C,n) by A98,JORDAN1B:5
        .= Lower_Seq(C,n)/.len Lower_Seq(C,n) by A102,PARTFUN1:def 8
        .= W-min L~Cage(C,n) by JORDAN1F:8;
      then
A103: Gv1/.len Gv1 = W-min L~Cage(C,n) by A99,SPRECT_3:11;
A104: v1/.1 = v1.1 by A101,PARTFUN1:def 8
        .= Gij by A98,JORDAN3:58;
      1+len v1 >= 1+1 by A100,XREAL_1:9;
      then 2 <= len v by FINSEQ_5:8;
      then
A105: 2 <= len Rev v by FINSEQ_5:def 3;
A106: not Gauge(C,n)*(i,wG) in L~Lower_Seq(C,n) by A1,A98,JORDAN1G:53;
      rng Lower_Seq(C,n) c= L~Lower_Seq(C,n) by A5,SPPOL_2:18,XXREAL_0:2;
      then
A107: not Gauge(C,n)*(i,wG) in rng Lower_Seq(C,n) by A1,A98,JORDAN1G :53;
      not Gauge(C,n)*(i,wG) in {Gij} by A98,A106,TARSKI:def 1;
      then
A108: not Gauge(C,n)*(i,wG) in rng <*Gij*> by FINSEQ_1:55;
      set ci = mid(Lower_Seq(C,n),Index(Gij,Lower_Seq(C,n))+1,
      len Lower_Seq(C,n));
      now per cases;
        suppose
A109:     Gij <> Lower_Seq(C,n).(Index(Gij,Lower_Seq(C,n))+1);
          rng ci c= rng Lower_Seq(C,n) by JORDAN3:28;
          then not Gauge(C,n)*(i,wG) in rng ci by A107;
          then not Gauge(C,n)*(i,wG) in rng <*Gij*> \/ rng ci
          by A108,XBOOLE_0:def 2;
          then not Gauge(C,n)*(i,wG) in rng(<*Gij*>^ci) by FINSEQ_1:44;
          hence not Gauge(C,n)*(i,wG) in rng v1 by A109,JORDAN3:def 4;
        end;
        suppose Gij = Lower_Seq(C,n).(Index(Gij,Lower_Seq(C,n))+1);
          then v1 = ci by JORDAN3:def 4;
          then rng v1 c= rng Lower_Seq(C,n) by JORDAN3:28;
          hence not Gauge(C,n)*(i,wG) in rng v1 by A107;
        end;
      end;
      then {Gauge(C,n)*(i,wG)} misses rng v1 by ZFMISC_1:56;
      then
A110: rng <*Gauge(C,n)*(i,wG)*> misses rng v1 by FINSEQ_1:55;
A111: <*Gauge(C,n)*(i,wG)*> is one-to-one by FINSEQ_3:102;
A112: v1 is_S-Seq by A98,JORDAN3:69;
      then v1 is one-to-one by TOPREAL1:def 10;
      then
A113: Gv1 is one-to-one by A110,A111,FINSEQ_3:98;
      for v be one-to-one FinSequence holds Rev v is one-to-one;
      then
A114: Rev v is one-to-one by A113;
A115: <*Gauge(C,n)*(i,wG)*> is special by SPPOL_2:39;
A116: v1 is special by A112,TOPREAL1:def 10;
      (<*Gauge(C,n)*(i,wG)*>/.len <*Gauge(C,n)*(i,wG)*>)`1 =
      (<*Gauge(C,n)*(i,wG)*>/.1)`1 by FINSEQ_1:56
        .= Gauge(C,n)*(i,wG)`1 by FINSEQ_4:25
        .= Gauge(C,n)*(i,1)`1 by A1,A8,GOBOARD5:3
        .= (v1/.1)`1 by A1,A2,A104,GOBOARD5:3;
      then Gv1 is special by A115,A116,GOBOARD2:13;
      then
A117: Rev v is special by SPPOL_2:42;
A118: len Upper_Seq(C,n) >= 2+1 by JORDAN1E:19;
      then
A119: len Upper_Seq(C,n) > 2 by NAT_1:13;
      len Upper_Seq(C,n) > 1 by A118,XXREAL_0:2;
      then h is S-Sequence_in_R2 by A119,JORDAN3:39;
      then
A120: 2 <= len h & h is one-to-one & h is special by TOPREAL1:def 10;
      3 <= len Upper_Seq(C,n) by JORDAN1E:19;
      then 2 <= len Upper_Seq(C,n) by XXREAL_0:2;
      then
A121: 2 in dom Upper_Seq(C,n) by FINSEQ_3:27;
A122: len Upper_Seq(C,n) in dom Upper_Seq(C,n) by FINSEQ_5:6;
      then
A123: h is_in_the_area_of Cage(C,n) by A121,JORDAN1E:21,SPRECT_2:26;
      Upper_Seq(C,n)/.(len Upper_Seq(C,n)) = E-max L~Cage(C,n) by JORDAN1F:7;
      then (Upper_Seq(C,n)/.len Upper_Seq(C,n))`1 = E-bound L~Cage(C,n)
      by EUCLID:56;
      then
A124: (h/.len h)`1 = E-bound L~Cage(C,n) by A121,A122,SPRECT_2:13;
      (Upper_Seq(C,n)/.(1+1))`1 = W-bound L~Cage(C,n) by JORDAN1G:39;
      then (h/.1)`1 = W-bound L~Cage(C,n) by A121,A122,SPRECT_2:12;
      then
A125: h is_a_h.c._for Cage(C,n) by A123,A124,SPRECT_2:def 2;
      now
        let m be Element of NAT;
        assume
A126:   m in dom <*Gauge(C,n)*(i,wG)*>;
        then m in Seg 1 by FINSEQ_1:55;
        then m = 1 by FINSEQ_1:4,TARSKI:def 1;
        then <*Gauge(C,n)*(i,wG)*>.m = Gauge(C,n)*(i,wG) by FINSEQ_1:57;
        then
A127:   <*Gauge(C,n)*(i,wG)*>/.m = Gauge(C,n)*(i,wG) by A126, PARTFUN1:def 8;
        Gauge(C,n)*(1,wG)`1 <= Gauge(C,n)*(i,wG)`1 by A1,A8,SPRECT_3:25;
        hence W-bound L~Cage(C,n) <= (<*Gauge(C,n)*(i,wG)*>/.m)`1
        by A7,A8,A127,JORDAN1A:94;
        (Gauge(C,n)*(i,wG))`1 <= Gauge(C,n)*(len Gauge(C,n),wG)`1
        by A1,A8,SPRECT_3:25;
        hence (<*Gauge(C,n)*(i,wG)*>/.m)`1 <= E-bound L~Cage(C,n)
        by A7,A8,A127,JORDAN1A:92;
        (<*Gauge(C,n)*(i,wG)*>/.m)`2 = N-bound L~Cage(C,n)
        by A1,A7,A127,JORDAN1A:91;
        hence S-bound L~Cage(C,n) <= (<*Gauge(C,n)*(i,wG)*>/.m)`2
        by SPRECT_1:24;
        thus (<*Gauge(C,n)*(i,wG)*>/.m)`2 <= N-bound L~Cage(C,n)
        by A1,A7,A127,JORDAN1A:91;
      end;
      then
A128: <*Gauge(C,n)*(i,wG)*> is_in_the_area_of Cage(C,n) by SPRECT_2: def 1;
      <*Gij*> is_in_the_area_of Cage(C,n) by A98,JORDAN1E:22,SPRECT_3:63;
      then v1 is_in_the_area_of Cage(C,n) by A98,JORDAN1E:22,SPRECT_3:73;
      then Gv1 is_in_the_area_of Cage(C,n) by A128,SPRECT_2:28;
      then
A129: Rev v is_in_the_area_of Cage(C,n) by SPRECT_3:68;
      v/.1 = Gauge(C,n)*(i,wG) by FINSEQ_5:16;
      then (v/.1)`2 = N-bound L~Cage(C,n) by A1,A7,JORDAN1A:91;
      then (Rev v/.(len v))`2 = N-bound L~Cage(C,n) by FINSEQ_5:68;
      then
A130: (Rev v/.(len Rev v))`2 = N-bound L~Cage(C,n) by FINSEQ_5:def 3;
      (v/.len v)`2 = S-bound L~Cage(C,n) by A98,A103,EUCLID:56;
      then (Rev v/.1)`2 = S-bound L~Cage(C,n) by FINSEQ_5:68;
      then Rev v is_a_v.c._for Cage(C,n) by A129,A130,SPRECT_2:def 3;
      then L~h meets L~Rev v by A105,A114,A117,A120,A125,SPRECT_2:33;
      then L~h meets L~v by SPPOL_2:22;
      then consider x be set such that
A131: x in L~h and
A132: x in L~v by XBOOLE_0:3;
A133: L~h c= L~Upper_Seq(C,n) by A6,JORDAN4:47;
A134: L~v1 c= L~Lower_Seq(C,n) by A98,JORDAN3:77;
A135: L~v = LSeg(Gauge(C,n)*(i,wG),v1/.1) \/ L~v1 by A99,SPPOL_2:20;
      Upper_Seq(C,n)/.1 = W-min L~Cage(C,n) by JORDAN1F:5;
      then
A136: not W-min L~Cage(C,n) in L~h by A119,JORDAN5B:16;
      now per cases by A132,A135,XBOOLE_0:def 2;
        suppose x in LSeg(Gauge(C,n)*(i,wG),v1/.1);
          then x in L~<*Gauge(C,n)*(i,wG),Gij*> by A104,SPPOL_2:21;
          hence L~Upper_Seq(C,n) meets L~<*Gauge(C,n)*(i,wG),Gij*>
          by A131,A133,XBOOLE_0:3;
        end;
        suppose
A137:     x in L~v1;
          then x in L~Lower_Seq(C,n) /\ L~Upper_Seq(C,n)
          by A131,A133,A134,XBOOLE_0:def 3;
          then x in {W-min L~Cage(C,n),E-max L~Cage(C,n)} by JORDAN1E:20;
          then
A138:     x = E-max L~Cage(C,n) by A131,A136,TARSKI:def 2;
          1 in dom Lower_Seq(C,n) by A6,FINSEQ_3:27;
          then Lower_Seq(C,n).1 = Lower_Seq(C,n)/.1 by PARTFUN1:def 8
            .= E-max L~Cage(C,n) by JORDAN1F:6;
          then x = Gij by A98,A137,A138,JORDAN1E:11;
          then x in LSeg(Gauge(C,n)*(i,wG),Gij) by TOPREAL1:6;
          then x in L~<*Gauge(C,n)*(i,wG),Gij*> by SPPOL_2:21;
          hence L~Upper_Seq(C,n) meets L~<*Gauge(C,n)*(i,wG),Gij*>
          by A131,A133,XBOOLE_0:3;
        end;
      end;
      then L~<*Gauge(C,n)*(i,wG),Gij*> meets L~Upper_Seq(C,n);
      hence LSeg(Gauge(C,n)*(i,width Gauge(C,n)),Gij) meets L~Upper_Seq(C,n)
      by SPPOL_2:21;
    end;
    suppose
A139: Gij in L~Upper_Seq(C,n);
      Gij in LSeg(Gauge(C,n)*(i,wG),Gij) by TOPREAL1:6;
      hence LSeg(Gauge(C,n)*(i,width Gauge(C,n)),Gij) meets L~Upper_Seq(C,n)
      by A139,XBOOLE_0:3;
    end;
    suppose
A140: Gij in L~Lower_Seq(C,n) & Gij = Lower_Seq(C,n).len Lower_Seq(C,n);
      len Lower_Seq(C,n) in dom Lower_Seq(C,n) by A6,FINSEQ_3:27;
      then
A141: Lower_Seq(C,n).len Lower_Seq(C,n) =
      Lower_Seq(C,n)/.len Lower_Seq(C,n) by PARTFUN1:def 8
        .= W-min L~Cage(C,n) by JORDAN1F:8;
A142: rng Upper_Seq(C,n) c= L~Upper_Seq(C,n) by A5,SPPOL_2:18,XXREAL_0:2;
A143: W-min L~Cage(C,n) in rng Upper_Seq(C,n) by JORDAN1J:5;
      Gij in LSeg(Gauge(C,n)*(i,wG),Gij) by TOPREAL1:6;
      hence LSeg(Gauge(C,n)*(i,wG),Gij) meets L~Upper_Seq(C,n)
      by A140,A141,A142,A143,XBOOLE_0:3;
    end;
  end;
  hence thesis;
end;

theorem Th4:
  for C be compact connected non vertical non horizontal Subset of TOP-REAL 2
  for n be Element of NAT st n > 0
  for i,j be Element of NAT st 1 <= i & i <= len Gauge(C,n) &
  1 <= j & j <= width Gauge(C,n) & Gauge(C,n)*(i,j) in L~Cage(C,n) holds
  LSeg(Gauge(C,n)*(i,width Gauge(C,n)),Gauge(C,n)*(i,j)) meets
  Upper_Arc L~Cage(C,n)
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
  let n be Element of NAT;
  assume
A1: n > 0;
  let i,j be Element of NAT;
  assume
A2: 1 <= i & i <= len Gauge(C,n) & 1 <= j & j <= width Gauge(C,n) &
  Gauge(C,n)*(i,j) in L~Cage(C,n);
  L~Upper_Seq(C,n) = Upper_Arc L~Cage(C,n) by A1,JORDAN1G:63;
  hence thesis by A2,Th3;
end;

theorem
  for C be compact connected non vertical non horizontal Subset of TOP-REAL 2
  for j be Element of NAT holds
  Gauge(C,n+1)*(Center Gauge(C,n+1),j) in Lower_Arc L~Cage(C,n+1) &
  1 <= j & j <= width Gauge(C,n+1) implies
  LSeg(Gauge(C,1)*(Center Gauge(C,1),width Gauge(C,1)),
  Gauge(C,n+1)*(Center Gauge(C,n+1),j)) meets Upper_Arc L~Cage(C,n+1)
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
  let j be Element of NAT;
  assume that
A1: Gauge(C,n+1)*(Center Gauge(C,n+1),j) in Lower_Arc L~Cage(C,n+1) and
A2: 1 <= j & j <= width Gauge(C,n+1);
  set in1 = Center Gauge(C,n+1);
A3: n+1 >= 0+1 by NAT_1:11;
A4: 1 <= in1 by JORDAN1B:12;
A5: in1 <= len Gauge(C,n+1) by JORDAN1B:14;
A6: LSeg(Gauge(C,n+1)*(Center Gauge(C,n+1),width Gauge(C,n+1)),
  Gauge(C,n+1)*(Center Gauge(C,n+1),j)) c=
  LSeg(Gauge(C,1)*(Center Gauge(C,1),width Gauge(C,1)),
  Gauge(C,n+1)*(Center Gauge(C,n+1),j)) by A2,A3,Th2;
  Lower_Arc L~Cage(C,n+1) c= L~Cage(C,n+1) by JORDAN6:76;
  hence thesis by A1,A2,A4,A5,A6,Th4,XBOOLE_1:63;
end;

theorem Th6:
  for C be compact connected non vertical non horizontal Subset of TOP-REAL 2
  for f be FinSequence of TOP-REAL 2
  for k be Element of NAT st 1 <= k & k+1 <= len f &
  f is_sequence_on Gauge(C,n) holds
  dist(f/.k,f/.(k+1)) = (N-bound C - S-bound C)/2|^n or
  dist(f/.k,f/.(k+1)) = (E-bound C - W-bound C)/2|^n
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
  let f be FinSequence of TOP-REAL 2;
  let k be Element of NAT;
  assume
A1: 1 <= k & k+1 <= len f;
  assume f is_sequence_on Gauge(C,n);
  then consider i1,j1,i2,j2 be Element of NAT such that
A2: [i1,j1] in Indices Gauge(C,n) and
A3: f/.k = Gauge(C,n)*(i1,j1) and
A4: [i2,j2] in Indices Gauge(C,n) and
A5: f/.(k+1) = Gauge(C,n)*(i2,j2) and
A6: i1 = i2 & j1+1 = j2 or i1+1 = i2 & j1 = j2 or
  i1 = i2+1 & j1 = j2 or i1 = i2 & j1 = j2+1 by A1,JORDAN8:6;
  per cases by A6;
  suppose i1 = i2 & j1+1 = j2;
    hence thesis by A2,A3,A4,A5,GOBRD14:19;
  end;
  suppose i1+1 = i2 & j1 = j2;
    hence thesis by A2,A3,A4,A5,GOBRD14:20;
  end;
  suppose i1 = i2+1 & j1 = j2;
    hence thesis by A2,A3,A4,A5,GOBRD14:20;
  end;
  suppose i1 = i2 & j1 = j2+1;
    hence thesis by A2,A3,A4,A5,GOBRD14:19;
  end;
end;

theorem
  for M be symmetric triangle MetrStruct for r be real number
  for p,q,x be Element of M st p in Ball(x,r) & q in Ball(x,r) holds
  dist(p,q) < 2*r
proof
  let M be symmetric triangle MetrStruct;
  let r be real number;
  let p,q,x be Element of M;
  assume that
A1: p in Ball(x,r) and
A2: q in Ball(x,r);
A3: dist(p,x) < r by A1,METRIC_1:12;
A4: dist(x,q) < r by A2,METRIC_1:12;
A5: dist(p,q) <= dist(p,x) + dist(x,q) by METRIC_1:4;
  dist(p,x) + dist(x,q) < r+r by A3,A4,XREAL_1:10;
  hence dist(p,q) < 2*r by A5,XXREAL_0:2;
end;

canceled;

theorem
  for C be compact connected non vertical non horizontal
  Subset of TOP-REAL 2 holds N-bound C < N-bound L~Cage(C,n)
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
A1: 2|^n > 0 by NEWTON:102;
  N-bound C > S-bound C + 0 by SPRECT_1:34;
  then N-bound C - S-bound C > 0 by XREAL_1:22;
  then
A2: (N-bound C - S-bound C)/(2|^n) > N-bound C - N-bound C by A1, REAL_2 :127;
  N-bound L~Cage(C,n) = N-bound C + (N-bound C - S-bound C)/(2|^n)
  by JORDAN10:6;
  hence N-bound C < N-bound L~Cage(C,n) by A2,XREAL_1:21;
end;

theorem Th10:
  for C be compact connected non vertical non horizontal
  Subset of TOP-REAL 2 holds E-bound C < E-bound L~Cage(C,n)
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
A1: 2|^n > 0 by NEWTON:102;
  E-bound C > W-bound C + 0 by SPRECT_1:33;
  then E-bound C - W-bound C > 0 by XREAL_1:22;
  then
A2: (E-bound C - W-bound C)/(2|^n) > E-bound C - E-bound C by A1, REAL_2 :127;
  E-bound L~Cage(C,n) = E-bound C + (E-bound C - W-bound C)/(2|^n)
  by JORDAN1A:85;
  hence E-bound C < E-bound L~Cage(C,n) by A2,XREAL_1:21;
end;

theorem
  for C be compact connected non vertical non horizontal
  Subset of TOP-REAL 2 holds S-bound L~Cage(C,n) < S-bound C
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
A1: 2|^n > 0 by NEWTON:102;
  N-bound C > S-bound C + 0 by SPRECT_1:34;
  then N-bound C - S-bound C > 0 by XREAL_1:22;
  then
A2: (N-bound C - S-bound C)/(2|^n) > S-bound C - S-bound C by A1, REAL_2 :127;
  S-bound L~Cage(C,n) = S-bound C - (N-bound C - S-bound C)/(2|^n)
  by JORDAN1A:84;
  hence S-bound L~Cage(C,n) < S-bound C by A2,REAL_2:165;
end;

theorem Th12:
  for C be compact connected non vertical non horizontal
  Subset of TOP-REAL 2 holds W-bound L~Cage(C,n) < W-bound C
proof
  let C be compact connected non vertical non horizontal Subset of TOP-REAL 2;
A1: 2|^n > 0 by NEWTON:102;
  E-bound C > W-bound C + 0 by SPRECT_1:33;
  then E-bound C - W-bound C > 0 by XREAL_1:22;
  then
A2: (E-bound C - W-bound C)/(2|^n) > W-bound C - W-bound C by A1, REAL_2 :127;
  W-bound L~Cage(C,n) = W-bound C - (E-bound C - W-bound C)/(2|^n)
  by JORDAN1A:83;
  hence W-bound L~Cage(C,n) < W-bound C by A2,REAL_2:165;
end;

theorem Th13:
  for C be Simple_closed_curve for i,j,k be Element of NAT st
  1 < i & i < len Gauge(C,n) & 1 <= k & k <= j & j <= width Gauge(C,n) &
  LSeg(Gauge(C,n)*(i,k),Gauge(C,n)*(i,j)) /\ L~Upper_Seq(C,n) =
  {Gauge(C,n)*(i,k)} &
  LSeg(Gauge(C,n)*(i,k),Gauge(C,n)*(i,j)) /\ L~Lower_Seq(C,n) =
  {Gauge(C,n)*(i,j)} holds
  LSeg(Gauge(C,n)*(i,k),Gauge(C,n)*(i,j)) meets Upper_Arc C
proof
  let C be Simple_closed_curve;
  let i,j,k be Element of NAT;
  set Ga = Gauge(C,n);
  set US = Upper_Seq(C,n);
  set LS = Lower_Seq(C,n);
  set UA = Upper_Arc C;
  set Wmin = W-min L~Cage(C,n);
  set Emax = E-max L~Cage(C,n);
  set Wbo = W-bound L~Cage(C,n);
  set Ebo = E-bound L~Cage(C,n);
  set Gik = Ga*(i,k);
  set Gij = Ga*(i,j);
  assume that
A1: 1 < i & i < len Ga and
A2: 1 <= k & k <= j & j <= width Ga and
A3: LSeg(Gik,Gij) /\ L~US = {Gik} and
A4: LSeg(Gik,Gij) /\ L~LS = {Gij} and
A5: LSeg(Gik,Gij) misses UA;
  Gij in {Gij} by TARSKI:def 1;
  then
A6: Gij in L~LS by A4,XBOOLE_0:def 3;
  Gik in {Gik} by TARSKI:def 1;
  then
A7: Gik in L~US by A3,XBOOLE_0:def 3;
  then
A8: j <> k by A1,A2,A6,JORDAN1J:57;
A9: 1 <= j & j <= width Ga by A2,XXREAL_0:2;
A10: 1 <= k & k <= width Ga by A2,XXREAL_0:2;
A11: [i,j] in Indices Ga by A1,A9,GOBOARD7:10;
A12: [i,k] in Indices Ga by A1,A10,GOBOARD7:10;
  set do = L_Cut(LS,Gij);
  set go = R_Cut(US,Gik);
A13: len Ga = width Ga by JORDAN8:def 1;
A14: len US >= 3 by JORDAN1E:19;
  then len US >= 1 by XXREAL_0:2;
  then 1 in dom US by FINSEQ_3:27;
  then
A15: US.1 = US/.1 by PARTFUN1:def 8
    .= Wmin by JORDAN1F:5;
A16: Wmin`1 = Wbo by EUCLID:56
    .= Ga*(1,k)`1 by A10,A13,JORDAN1A:94;
  len Ga >= 4 by JORDAN8:13;
  then
A17: len Ga >= 1 by XXREAL_0:2;
  then
A18: [1,k] in Indices Ga by A10,GOBOARD7:10;
  then
A19: Gik <> US.1 by A1,A12,A15,A16,JORDAN1G:7;
  then reconsider go as being_S-Seq FinSequence of TOP-REAL 2
  by A7,JORDAN3:70;
A20: len LS >= 1+2 by JORDAN1E:19;
  then len LS >= 1 by XXREAL_0:2;
  then
A21: 1 in dom LS & len LS in dom LS by FINSEQ_3:27;
  then
A22: LS.len LS = LS/.len LS by PARTFUN1:def 8
    .= Wmin by JORDAN1F:8;
A23: Wmin`1 = Wbo by EUCLID:56
    .= Ga*(1,k)`1 by A10,A13,JORDAN1A:94;
A24: [i,j] in Indices Ga by A1,A9,GOBOARD7:10;
  then
A25: Gij <> LS.len LS by A1,A18,A22,A23,JORDAN1G:7;
  then reconsider do as being_S-Seq FinSequence of TOP-REAL 2
  by A6,JORDAN3:69;
A26: [len Ga,k] in Indices Ga by A10,A17,GOBOARD7:10;
A27: LS.1 = LS/.1 by A21,PARTFUN1:def 8
    .= Emax by JORDAN1F:6;
  Emax`1 = Ebo by EUCLID:56
    .= Ga*(len Ga,k)`1 by A10,A13,JORDAN1A:92;
  then
A28: Gij <> LS.1 by A1,A24,A26,A27,JORDAN1G:7;
A29: len go >= 1+1 by TOPREAL1:def 10;
A30: Gik in rng US by A1,A7,A10,JORDAN1G:4,JORDAN1J:40;
  then
A31: go is_sequence_on Ga by JORDAN1G:4,JORDAN1J:38;
A32: len do >= 1+1 by TOPREAL1:def 10;
A33: Gij in rng LS by A1,A6,A9,JORDAN1G:5,JORDAN1J:40;
  then
A34: do is_sequence_on Ga by JORDAN1G:5,JORDAN1J:39;
  reconsider go as non constant s.c.c.
  (being_S-Seq FinSequence of TOP-REAL 2) by A29,A31,JGRAPH_1:16 ,JORDAN8:8;
  reconsider do as non constant s.c.c.
  (being_S-Seq FinSequence of TOP-REAL 2) by A32,A34,JGRAPH_1:16 ,JORDAN8:8;
A35: len go > 1 by A29,NAT_1:13;
  then
A36: len go in dom go by FINSEQ_3:27;
  then
A37: go/.len go = go.len go by PARTFUN1:def 8
    .= Gik by A7,JORDAN3:59;
  len do >= 1 by A32,XXREAL_0:2;
  then 1 in dom do by FINSEQ_3:27;
  then
A38: do/.1 = do.1 by PARTFUN1:def 8
    .= Gij by A6,JORDAN3:58;
  reconsider m = len go - 1 as Element of NAT by A36,FINSEQ_3:28;
A39: m+1 = len go;
  then
A40: len go-'1 = m by BINARITH:39;
A41: LSeg(go,m) c= L~go by TOPREAL3:26;
A42: L~go c= L~US by A7,JORDAN3:76;
  then LSeg(go,m) c= L~US by A41,XBOOLE_1:1;
  then
A43: LSeg(go,m) /\ LSeg(Gik,Gij) c= {Gik} by A3,XBOOLE_1:26;
  m >= 1 by A29,XREAL_1:21;
  then
A44: LSeg(go,m) = LSeg(go/.m,Gik) by A37,A39,TOPREAL1:def 5;
  {Gik} c= LSeg(go,m) /\ LSeg(Gik,Gij)
  proof
    let x be set;
    assume x in {Gik};
    then
A45: x = Gik by TARSKI:def 1;
A46: Gik in LSeg(go,m) by A44,TOPREAL1:6;
    Gik in LSeg(Gik,Gij) by TOPREAL1:6;
    hence x in LSeg(go,m) /\ LSeg(Gik,Gij) by A45,A46,XBOOLE_0:def 3;
  end;
  then
A47: LSeg(go,m) /\ LSeg(Gik,Gij) = {Gik} by A43,XBOOLE_0:def 10;
A48: LSeg(do,1) c= L~do by TOPREAL3:26;
A49: L~do c= L~LS by A6,JORDAN3:77;
  then LSeg(do,1) c= L~LS by A48,XBOOLE_1:1;
  then
A50: LSeg(do,1) /\ LSeg(Gik,Gij) c= {Gij} by A4,XBOOLE_1:26;
A51: LSeg(do,1) = LSeg(Gij,do/.(1+1)) by A32,A38,TOPREAL1:def 5;
  {Gij} c= LSeg(do,1) /\ LSeg(Gik,Gij)
  proof
    let x be set;
    assume x in {Gij};
    then
A52: x = Gij by TARSKI:def 1;
A53: Gij in LSeg(do,1) by A51,TOPREAL1:6;
    Gij in LSeg(Gik,Gij) by TOPREAL1:6;
    hence x in LSeg(do,1) /\ LSeg(Gik,Gij) by A52,A53,XBOOLE_0:def 3;
  end;
  then
A54: LSeg(Gik,Gij) /\ LSeg(do,1) = {Gij} by A50,XBOOLE_0:def 10;
A55: go/.1 = US/.1 by A7,SPRECT_3:39
    .= Wmin by JORDAN1F:5;
  then
A56: go/.1 = LS/.len LS by JORDAN1F:8
    .= do/.len do by A6,JORDAN1J:35;
A57: rng go c= L~go & rng do c= L~do by A29,A32,SPPOL_2:18;
A58: {go/.1} c= L~go /\ L~do
  proof
    let x be set;
    assume x in {go/.1};
    then x = go/.1 by TARSKI:def 1;
    then x in rng go & x in rng do by A56,FINSEQ_6:46,REVROT_1:3;
    hence x in L~go /\ L~do by A57,XBOOLE_0:def 3;
  end;
A59: LS.1 = LS/.1 by A21,PARTFUN1:def 8
    .= Emax by JORDAN1F:6;
A60: [len Ga,j] in Indices Ga by A9,A17,GOBOARD7:10;
  L~go /\ L~do c= {go/.1}
  proof
    let x be set;
    assume x in L~go /\ L~do;
    then
A61: x in L~go & x in L~do by XBOOLE_0:def 3;
    then x in L~US /\ L~LS by A42,A49,XBOOLE_0:def 3;
    then x in {Wmin,Emax} by JORDAN1E:20;
    then
A62: x = Wmin or x = Emax by TARSKI:def 2;
    now
      assume x = Emax;
      then
A63:  Emax = Gij by A6,A59,A61,JORDAN1E:11;
      Ga*(len Ga,j)`1 = Ebo by A9,A13,JORDAN1A:92;
      then Emax`1 <> Ebo by A1,A11,A60,A63,JORDAN1G:7;
      hence contradiction by EUCLID:56;
    end;
    hence x in {go/.1} by A55,A62,TARSKI:def 1;
  end;
  then
A64: L~go /\ L~do = {go/.1} by A58,XBOOLE_0:def 10;
  set W2 = go/.2;
A65: 2 in dom go by A29,FINSEQ_3:27;
A66: Gik..US >= 1 by A30,FINSEQ_4:31;
A67: now
    assume Gik`1 = Wbo;
    then Ga*(1,k)`1 = Ga*(i,k)`1 by A10,A13,JORDAN1A:94;
    hence contradiction by A1,A12,A18,JORDAN1G:7;
  end;
  go = mid(US,1,Gik..US) by A30,JORDAN1G:57
    .= US|(Gik..US) by A66,JORDAN3:25;
  then
A68: W2 = US/.2 by A65,FINSEQ_4:85;
A69: Wmin in rng go by A55,FINSEQ_6:46;
  set pion = <*Gik,Gij*>;
A70: now
    let n be Element of NAT;
    assume n in dom pion;
    then n in Seg 2 by FINSEQ_3:29;
    then n = 1 or n = 2 by FINSEQ_1:4,TARSKI:def 2;
    then pion/.n = Gik or pion/.n = Gij by FINSEQ_4:26;
    hence ex i,j be Element of NAT st [i,j] in Indices Ga & pion/.n = Ga*(i,j)
    by A11,A12;
  end;
A71: Gik <> Gij by A8,A11,A12,GOBOARD1:21;
A72: Gik`1 = Ga*(i,1)`1 by A1,A10,GOBOARD5:3
    .= Gij`1 by A1,A9,GOBOARD5:3;
  then LSeg(Gik,Gij) is vertical by SPPOL_1:37;
  then pion is_S-Seq by A71,JORDAN1B:8;
  then consider pion1 be FinSequence of TOP-REAL 2 such that
A73: pion1 is_sequence_on Ga and
A74: pion1 is_S-Seq and
A75: L~pion = L~pion1 and
A76: pion/.1 = pion1/.1 and
A77: pion/.len pion = pion1/.len pion1 and
A78: len pion <= len pion1 by A70,GOBOARD3:2;
  reconsider pion1 as being_S-Seq FinSequence of TOP-REAL 2 by A74;
  set godo = go^'pion1^'do;
A79: 1+1 <= len Cage(C,n) by GOBOARD7:36,XXREAL_0:2;
  then
A80: 1+1 <= len Rotate(Cage(C,n),Wmin) by REVROT_1:14;
  len (go^'pion1) >= len go by TOPREAL8:7;
  then
A81: len (go^'pion1) >= 1+1 by A29,XXREAL_0:2;
  then
A82: len (go^'pion1) > 1+0 by NAT_1:13;
A83: len godo >= len (go^'pion1) by TOPREAL8:7;
  then
A84: 1+1 <= len godo by A81,XXREAL_0:2;
A85: US is_sequence_on Ga by JORDAN1G:4;
A86: go/.len go = pion1/.1 by A37,A76,FINSEQ_4:26;
  then
A87: go^'pion1 is_sequence_on Ga by A31,A73,TOPREAL8:12;
A88: (go^'pion1)/.len (go^'pion1) = pion/.len pion by A77,GRAPH_2:58
    .= pion/.2 by FINSEQ_1:61
    .= do/.1 by A38,FINSEQ_4:26;
  then
A89: godo is_sequence_on Ga by A34,A87,TOPREAL8:12;
  LSeg(pion1,1) c= L~<*Gik,Gij*> by A75,TOPREAL3:26;
  then LSeg(pion1,1) c= LSeg(Gik,Gij) by SPPOL_2:21;
  then
A90: LSeg(go,len go-'1) /\ LSeg(pion1,1) c= {Gik} by A40,A47,XBOOLE_1:27;
A91: len pion1 >= 1+1 by A78,FINSEQ_1:61;
  {Gik} c= LSeg(go,m) /\ LSeg(pion1,1)
  proof
    let x be set;
    assume x in {Gik};
    then
A92: x = Gik by TARSKI:def 1;
A93: Gik in LSeg(go,m) by A44,TOPREAL1:6;
    Gik in LSeg(pion1,1) by A37,A86,A91,TOPREAL1:27;
    hence x in LSeg(go,m) /\ LSeg(pion1,1) by A92,A93,XBOOLE_0:def 3;
  end;
  then LSeg(go,len go-'1) /\ LSeg(pion1,1) = { go/.len go }
  by A37,A40,A90,XBOOLE_0:def 10;
  then
A94: go^'pion1 is unfolded by A86,TOPREAL8:34;
  len pion1 >= 2+0 by A78,FINSEQ_1:61;
  then
A95: len pion1-2 >= 0 by XREAL_1:21;
  len (go^'pion1)+1-1 = len go+len pion1-1 by GRAPH_2:13;
  then len (go^'pion1)-1 = len go + (len pion1-2)
    .= len go + (len pion1-'2) by A95,BINARITH:def 3;
  then
A96: len (go^'pion1)-'1 = len go + (len pion1-'2) by BINARITH:def 3;
A97: len pion1-1 >= 1 by A91,XREAL_1:21;
  then
A98: len pion1-'1 = len pion1-1 by BINARITH:def 3;
A99: len pion1-'2+1 = len pion1-2+1 by A95,BINARITH:def 3
    .= len pion1-'1 by A97,BINARITH:def 3;
  len pion1-1+1 <= len pion1;
  then
A100: len pion1-'1 < len pion1 by A98,NAT_1:13;
  LSeg(pion1,len pion1-'1) c= L~<*Gik,Gij*> by A75,TOPREAL3:26;
  then LSeg(pion1,len pion1-'1) c= LSeg(Gik,Gij) by SPPOL_2:21;
  then
A101: LSeg(pion1,len pion1-'1) /\ LSeg(do,1) c= {Gij} by A54,XBOOLE_1:27;
  {Gij} c= LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
  proof
    let x be set;
    assume x in {Gij};
    then
A102: x = Gij by TARSKI:def 1;
A103: Gij in LSeg(do,1) by A51,TOPREAL1:6;
    pion1/.(len pion1-'1+1) = pion/.2 by A77,A98,FINSEQ_1:61
      .= Gij by FINSEQ_4:26;
    then Gij in LSeg(pion1,len pion1-'1) by A97,A98,TOPREAL1:27;
    hence x in LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
    by A102,A103,XBOOLE_0:def 3;
  end;
  then LSeg(pion1,len pion1-'1) /\ LSeg(do,1) = {Gij} by A101,XBOOLE_0:def 10;
  then
A104: LSeg(go^'pion1,len go+(len pion1-'2)) /\ LSeg(do,1) =
  {(go^'pion1)/.len (go^'pion1)} by A38,A86,A88,A99,A100,TOPREAL8:31;
A105: (go^'pion1) is non trivial by A81,REALSET1:13;
A106: rng pion1 c= L~pion1 by A91,SPPOL_2:18;
A107: {pion1/.1} c= L~go /\ L~pion1
  proof
    let x be set;
    assume x in {pion1/.1};
    then x = pion1/.1 by TARSKI:def 1;
    then x in rng go & x in rng pion1 by A86,FINSEQ_6:46,REVROT_1:3;
    hence x in L~go /\ L~pion1 by A57,A106,XBOOLE_0:def 3;
  end;
  L~go /\ L~pion1 c= {pion1/.1}
  proof
    let x be set;
    assume x in L~go /\ L~pion1;
    then x in L~go & x in L~pion1 by XBOOLE_0:def 3;
    then x in L~pion1 /\ L~US by A42,XBOOLE_0:def 3;
    hence x in {pion1/.1} by A3,A37,A75,A86,SPPOL_2:21;
  end;
  then
A108: L~go /\ L~pion1 = {pion1/.1} by A107,XBOOLE_0:def 10;
  then
A109: (go^'pion1) is s.n.c. by A86,JORDAN1J:54;
  rng go /\ rng pion1 c= {pion1/.1} by A57,A106,A108,XBOOLE_1:27;
  then
A110: go^'pion1 is one-to-one by JORDAN1J:55;
A111: pion/.len pion = pion/.2 by FINSEQ_1:61
    .= do/.1 by A38,FINSEQ_4:26;
A112: {pion1/.len pion1} c= L~do /\ L~pion1
  proof
    let x be set;
    assume x in {pion1/.len pion1};
    then x = pion1/.len pion1 by TARSKI:def 1;
    then x in rng do & x in rng pion1 by A77,A111,FINSEQ_6:46,REVROT_1:3;
    hence x in L~do /\ L~pion1 by A57,A106,XBOOLE_0:def 3;
  end;
  L~do /\ L~pion1 c= {pion1/.len pion1}
  proof
    let x be set;
    assume x in L~do /\ L~pion1;
    then x in L~do & x in L~pion1 by XBOOLE_0:def 3;
    then x in L~pion1 /\ L~LS by A49,XBOOLE_0:def 3;
    hence x in {pion1/.len pion1} by A4,A38,A75,A77,A111,SPPOL_2:21;
  end;
  then
A113: L~do /\ L~pion1 = {pion1/.len pion1} by A112,XBOOLE_0:def 10;
A114: L~(go^'pion1) /\ L~do = (L~go \/ L~pion1) /\ L~do by A86,TOPREAL8:35
    .= {go/.1} \/ {do/.1} by A64,A77,A111,A113,XBOOLE_1:23
    .= {(go^'pion1)/.1} \/ {do/.1} by GRAPH_2:57
    .= {(go^'pion1)/.1,do/.1} by ENUMSET1:41;
  do/.len do = (go^'pion1)/.1 by A56,GRAPH_2:57;
  then reconsider godo as non constant standard special_circular_sequence
  by A84,A88,A89,A94,A96,A104,A105,A109,A110,A114,JORDAN8:7,8
  ,TOPREAL8:11,33,34;
A115: UA is_an_arc_of W-min C,E-max C by JORDAN6:def 8;
  then
A116: UA is connected by JORDAN6:11;
A117: W-min C in UA & E-max C in UA by A115,TOPREAL1:4;
  set ff = Rotate(Cage(C,n),Wmin);
  Wmin in rng Cage(C,n) by SPRECT_2:47;
  then
A118: ff/.1 = Wmin by FINSEQ_6:98;
A119: L~ff = L~Cage(C,n) by REVROT_1:33;
  then (W-max L~ff)..ff > 1 by A118,SPRECT_5:23;
  then (N-min L~ff)..ff > 1 by A118,A119,SPRECT_5:24,XXREAL_0:2;
  then (N-max L~ff)..ff > 1 by A118,A119,SPRECT_5:25,XXREAL_0:2;
  then
A120: Emax..ff > 1 by A118,A119,SPRECT_5:26,XXREAL_0:2;
A121: now
    assume
A122: Gik..US <= 1;
    Gik..US >= 1 by A30,FINSEQ_4:31;
    then Gik..US = 1 by A122,XXREAL_0:1;
    then Gik = US/.1 by A30,FINSEQ_5:41;
    hence contradiction by A15,A19,JORDAN1F:5;
  end;
A123: Cage(C,n) is_sequence_on Ga by JORDAN9:def 1;
  then
A124: ff is_sequence_on Ga by REVROT_1:34;
A125: right_cell(godo,1,Ga)\L~godo c= RightComp godo by A84,A89,JORDAN9:29;
A126: L~godo = L~(go^'pion1) \/ L~do by A88,TOPREAL8:35
    .= L~go \/ L~pion1 \/ L~do by A86,TOPREAL8:35;
  L~Cage(C,n) = L~US \/ L~LS by JORDAN1E:17;
  then
A127: L~US c= L~Cage(C,n) & L~LS c= L~Cage(C,n) by XBOOLE_1:7;
  then
A128: L~go c= L~Cage(C,n) & L~do c= L~Cage(C,n) by A42,A49,XBOOLE_1:1;
A129: W-min C in C by SPRECT_1:15;
A130: L~pion = LSeg(Gik,Gij) by SPPOL_2:21;
A131: now
    assume W-min C in L~godo;
    then
A132: W-min C in L~go \/ L~pion1 or W-min C in L~do by A126, XBOOLE_0: def 2;
    per cases by A132,XBOOLE_0:def 2;
    suppose W-min C in L~go;
      then C meets L~Cage(C,n) by A128,A129,XBOOLE_0:3;
      hence contradiction by JORDAN10:5;
    end;
    suppose W-min C in L~pion1;
      hence contradiction by A5,A75,A117,A130,XBOOLE_0:3;
    end;
    suppose W-min C in L~do;
      then C meets L~Cage(C,n) by A128,A129,XBOOLE_0:3;
      hence contradiction by JORDAN10:5;
    end;
  end;
  right_cell(Rotate(Cage(C,n),Wmin),1) =
  right_cell(ff,1,GoB ff) by A80,JORDAN1H:29
    .= right_cell(ff,1,GoB Cage(C,n)) by REVROT_1:28
    .= right_cell(ff,1,Ga) by JORDAN1H:52
    .= right_cell(ff-:Emax,1,Ga) by A120,A124,JORDAN1J:53
    .= right_cell(US,1,Ga) by JORDAN1E:def 1
    .= right_cell(R_Cut(US,Gik),1,Ga) by A30,A85,A121,JORDAN1J:52
    .= right_cell(go^'pion1,1,Ga) by A35,A87,JORDAN1J:51
    .= right_cell(godo,1,Ga) by A82,A89,JORDAN1J:51;
  then W-min C in right_cell(godo,1,Ga) by JORDAN1I:8;
  then
A133: W-min C in right_cell(godo,1,Ga)\L~godo by A131,XBOOLE_0:def 4;
A134: godo/.1 = (go^'pion1)/.1 by GRAPH_2:57
    .= Wmin by A55,GRAPH_2:57;
A135: len US >= 2 by A14,XXREAL_0:2;
A136: godo/.2 = (go^'pion1)/.2 by A81,GRAPH_2:61
    .= US/.2 by A29,A68,GRAPH_2:61
    .= (US^'LS)/.2 by A135,GRAPH_2:61
    .= Rotate(Cage(C,n),Wmin)/.2 by JORDAN1E:15;
A137: L~go \/ L~do is compact by COMPTS_1:19;
  Wmin in L~go \/ L~do by A57,A69,XBOOLE_0:def 2;
  then
A138: W-min (L~go \/ L~do) = Wmin by A128,A137,JORDAN1J:21,XBOOLE_1:8;
A139: (W-min (L~go \/ L~do))`1 = W-bound (L~go \/ L~do) & Wmin`1 = Wbo
  by EUCLID:56;
  W-bound LSeg(Gik,Gij) = Gik`1 by A72,SPRECT_1:62;
  then
A140: W-bound L~pion1 = Gik`1 by A75,SPPOL_2:21;
  Gik`1 >= Wbo by A7,A127,PSCOMP_1:71;
  then Gik`1 > Wbo by A67,REAL_1:def 5;
  then W-min (L~go\/L~do\/L~pion1) = W-min (L~go \/ L~do)
  by A137,A138,A139,A140, JORDAN1J:33;
  then
A141: W-min L~godo = Wmin by A126,A138,XBOOLE_1:4;
A142: rng godo c= L~godo by A81,A83,SPPOL_2:18,XXREAL_0:2;
  2 in dom godo by A84,FINSEQ_3:27;
  then
A143: godo/.2 in rng godo by PARTFUN2:4;
  godo/.2 in W-most L~Cage(C,n) by A136,JORDAN1I:27;
  then (godo/.2)`1 = (W-min L~godo)`1 by A141,PSCOMP_1:88
    .= W-bound L~godo by EUCLID:56;
  then godo/.2 in W-most L~godo by A142,A143,SPRECT_2:16;
  then Rotate(godo,W-min L~godo)/.2 in W-most L~godo by A134,A141,FINSEQ_6:
  95;
  then reconsider godo as clockwise_oriented non constant standard
  special_circular_sequence by JORDAN1I:27;
  len US in dom US by FINSEQ_5:6;
  then
A144: US.len US = US/.len US by PARTFUN1:def 8
    .= Emax by JORDAN1F:7;
A145: E-max C in E-most C by PSCOMP_1:111;
A146: east_halfline E-max C misses L~go
  proof
    assume east_halfline E-max C meets L~go;
    then consider p be set such that
A147: p in east_halfline E-max C and
A148: p in L~go by XBOOLE_0:3;
    reconsider p as Point of TOP-REAL 2 by A147;
    p in L~US by A42,A148;
    then p in east_halfline E-max C /\ L~Cage(C,n) by A127,A147,XBOOLE_0:def
    3;
    then
A149: p`1 = Ebo by A145,JORDAN1A:104;
    then
A150: p = Emax by A42,A148,JORDAN1J:46;
    then Emax = Gik by A7,A144,A148,JORDAN1J:43;
    then Gik`1 = Ga*(len Ga,k)`1 by A10,A13,A149,A150,JORDAN1A:92;
    hence contradiction by A1,A12,A26,JORDAN1G:7;
  end;
  now
    assume east_halfline E-max C meets L~godo;
    then
A151: east_halfline E-max C meets (L~go \/ L~pion1) or
    east_halfline E-max C meets L~do by A126,XBOOLE_1:70;
    per cases by A151,XBOOLE_1:70;
    suppose east_halfline E-max C meets L~go;
      hence contradiction by A146;
    end;
    suppose east_halfline E-max C meets L~pion1;
      then consider p be set such that
A152: p in east_halfline E-max C and
A153: p in L~pion1 by XBOOLE_0:3;
      reconsider p as Point of TOP-REAL 2 by A152;
A154: p`1 = Gik`1 by A72,A75,A130,A153,GOBOARD7:5;
      i+1 <= len Ga by A1,NAT_1:13;
      then i+1-1 <= len Ga-1 by XREAL_1:11;
      then
A155: i <= len Ga-'1 by BINARITH:def 3;
      len Ga-'1 <= len Ga by BINARITH:52;
      then p`1 <= Ga*(len Ga-'1,1)`1 by A1,A10,A13,A17,A154,A155,JORDAN1A:39;
      then p`1 <= E-bound C by A17,JORDAN8:15;
      then
A156: p`1 <= (E-max C)`1 by EUCLID:56;
      p`1 >= (E-max C)`1 by A152,TOPREAL1:def 13;
      then
A157: p`1 = (E-max C)`1 by A156,XXREAL_0:1;
      p`2 = (E-max C)`2 by A152,TOPREAL1:def 13;
      then p = E-max C by A157,TOPREAL3:11;
      hence contradiction by A5,A75,A117,A130,A153,XBOOLE_0:3;
    end;
    suppose east_halfline E-max C meets L~do;
      then consider p be set such that
A158: p in east_halfline E-max C and
A159: p in L~do by XBOOLE_0:3;
      reconsider p as Point of TOP-REAL 2 by A158;
      p in L~LS by A49,A159;
      then p in east_halfline E-max C /\ L~Cage(C,n)
      by A127,A158,XBOOLE_0:def 3;
      then
A160: p`1 = Ebo by A145,JORDAN1A:104;
A161: (E-max C)`2 = p`2 by A158,TOPREAL1:def 13;
      set RC = Rotate(Cage(C,n),Emax);
A162: E-max C in right_cell(RC,1) by JORDAN1I:9;
A163: 1+1 <= len LS by A20,XXREAL_0:2;
      LS = RC-:Wmin by JORDAN1G:26;
      then
A164: LSeg(LS,1) = LSeg(RC,1) by A163,SPPOL_2:9;
A165: L~RC = L~Cage(C,n) by REVROT_1:33;
A166: len RC = len Cage(C,n) by REVROT_1:14;
A167: GoB RC = GoB Cage(C,n) by REVROT_1:28
        .= Ga by JORDAN1H:52;
A168: Emax in rng Cage(C,n) by SPRECT_2:50;
A169: RC is_sequence_on Ga by A123,REVROT_1:34;
A170: RC/.1 = E-max L~RC by A165,A168,FINSEQ_6:98;
      then consider ii,jj be Element of NAT such that
A171: [ii,jj+1] in Indices Ga and
A172: [ii,jj] in Indices Ga and
A173: RC/.1 = Ga*(ii,jj+1) and
A174: RC/.(1+1) = Ga*(ii,jj) by A79,A166,A169,JORDAN1I:25;
      consider jj2 be Element of NAT such that
A175: 1 <= jj2 & jj2 <= width Ga and
A176: Emax = Ga*(len Ga,jj2) by JORDAN1D:29;
A177: len Ga >= 4 by JORDAN8:13;
      then len Ga >= 1 by XXREAL_0:2;
      then [len Ga,jj2] in Indices Ga by A175,GOBOARD7:10;
      then
A178: ii = len Ga by A165,A170,A171,A173,A176,GOBOARD1:21;
A179: 1 <= ii & ii <= len Ga & 1 <= jj+1 & jj+1 <= width Ga
      by A171,GOBOARD5:1;
A180: 1 <= ii & ii <= len Ga & 1 <= jj & jj <= width Ga by A172,GOBOARD5:1;
A181: ii+1 <> ii;
      jj+1+1 <> jj;
      then
A182: right_cell(RC,1) = cell(Ga,ii-'1,jj)
      by A79,A166,A167,A171,A172,A173,A174,A181, GOBOARD5:def 6;
A183: ii-'1+1 = ii by A179,BINARITH:53;
      ii-1 >= 4-1 by A177,A178,XREAL_1:11;
      then
A184: ii-1 >= 1 by XXREAL_0:2;
      then
A185: 1 <= ii-'1 by BINARITH:def 3;
A186: Ga*(ii-'1,jj)`2 <= p`2 & p`2 <= Ga*(ii-'1,jj+1)`2
      by A161,A162,A179,A180,A182,A183,A184, JORDAN9:19;
A187: ii-'1 < len Ga by A179,A183,NAT_1:13;
      then
A188: Ga*(ii-'1,jj)`2 = Ga*(1,jj)`2 by A180,A185,GOBOARD5:2
        .= Ga*(ii,jj)`2 by A180,GOBOARD5:2;
A189: Ga*(ii-'1,jj+1)`2 = Ga*(1,jj+1)`2 by A179,A185,A187,GOBOARD5:2
        .= Ga*(ii,jj+1)`2 by A179,GOBOARD5:2;
      Ga*(len Ga,jj)`1 = Ebo & Ebo = Ga*(len Ga,jj+1)`1
      by A13,A179,A180,JORDAN1A:92;
      then p in LSeg(RC/.1,RC/.(1+1))
      by A160,A173,A174,A178,A186,A188,A189,GOBOARD7:8;
      then
A190: p in LSeg(LS,1) by A79,A164,A166,TOPREAL1:def 5;
A191: p in LSeg(do,Index(p,do)) by A159,JORDAN3:42;
A192: do = mid(LS,Gij..LS,len LS) by A33,JORDAN1J:37;
A193: 1<=Gij..LS & Gij..LS<=len LS by A33,FINSEQ_4:31;
      Gij..LS <> len LS by A25,A33,FINSEQ_4:29;
      then
A194: Gij..LS < len LS by A193,REAL_1:def 5;
A195: 1<=Index(p,do) & Index(p,do) < len do by A159,JORDAN3:41;
A196: Index(Gij,LS)+1 = Gij..LS by A28,A33,JORDAN1J:56;
      consider t be Nat such that
A197: t in dom LS and
A198: LS.t = Gij by A33,FINSEQ_2:11;
A199: 1 <= t & t <= len LS by A197,FINSEQ_3:27;
      then 1 < t by A28,A198,REAL_1:def 5;
      then Index(Gij,LS)+1 = t by A198,A199,JORDAN3:45;
      then
A200: len L_Cut(LS,Gij) = len LS-Index(Gij,LS) by A6,A198,JORDAN3:61;
      set tt = Index(p,do)+(Gij..LS)-'1;
A201: 1<=Index(Gij,LS) & 0+Index(Gij,LS) < len LS by A6,JORDAN3:41;
      then
A202: len LS-Index(Gij,LS) > 0 by XREAL_1:22;
      Index(p,do) < len LS-'Index(Gij,LS) by A195,A200,BINARITH:def 3;
      then Index(p,do)+1 <= len LS-'Index(Gij,LS) by NAT_1:13;
      then Index(p,do) <= len LS-'Index(Gij,LS)-1 by XREAL_1:21;
      then Index(p,do) <= len LS-Index(Gij,LS)-1 by A202,BINARITH:def 3;
      then Index(p,do) <= len LS-Gij..LS by A196;
      then Index(p,do) <= len LS-'Gij..LS by BINARITH:def 3;
      then Index(p,do) < len LS-'(Gij..LS)+1 by NAT_1:13;
      then
A203: LSeg(mid(LS,Gij..LS,len LS),Index(p,do)) =
      LSeg(LS,Index(p,do)+(Gij..LS)-'1) by A193,A194,A195,JORDAN4:31;
A204: 1+1 <= Gij..LS by A196,A201,XREAL_1:9;
      then Index(p,do)+Gij..LS >= 1+1+1 by A195,XREAL_1:9;
      then Index(p,do)+Gij..LS-1 >= 1+1+1-1 by XREAL_1:11;
      then
A205: tt >= 1+1 by BINARITH:def 3;
A206: 2 in dom LS by A163,FINSEQ_3:27;
      now per cases by A205,REAL_1:def 5;
        suppose tt > 1+1;
          then LSeg(LS,1) misses LSeg(LS,tt) by TOPREAL1:def 9;
          hence contradiction by A190,A191,A192,A203,XBOOLE_0:3;
        end;
        suppose
A207:     tt = 1+1;
          then LSeg(LS,1) /\ LSeg(LS,tt) = {LS/.2} by A20,TOPREAL1:def 8;
          then p in {LS/.2} by A190,A191,A192,A203,XBOOLE_0:def 3;
          then
A208:     p = LS/.2 by TARSKI:def 1;
          then
A209:     p..LS = 2 by A206,FINSEQ_5:44;
          1+1 = Index(p,do)+(Gij..LS)-1 by A207,BINARITH:def 3;
          then 1+1+1 = Index(p,do)+(Gij..LS);
          then
A210:     Gij..LS = 2 by A195,A204,JORDAN1E:10;
          p in rng LS by A206,A208,PARTFUN2:4;
          then p = Gij by A33,A209,A210,FINSEQ_5:10;
          then Gij`1 = Ebo by A208,JORDAN1G:40;
          then Gij`1 = Ga*(len Ga,j)`1 by A9,A13,JORDAN1A:92;
          hence contradiction by A1,A11,A60,JORDAN1G:7;
        end;
      end;
      hence contradiction;
    end;
  end;
  then east_halfline E-max C c= (L~godo)` by SUBSET_1:43;
  then consider W be Subset of TOP-REAL 2 such that
A211: W is_a_component_of (L~godo)` and
A212: east_halfline E-max C c= W by GOBOARD9:5;
  W is not Bounded by A212,JORDAN2C:16,129;
  then W is_outside_component_of L~godo by A211,JORDAN2C:def 4;
  then W c= UBD L~godo by JORDAN2C:27;
  then
A213: east_halfline E-max C c= UBD L~godo by A212,XBOOLE_1:1;
  E-max C in east_halfline E-max C by TOPREAL1:45;
  then E-max C in UBD L~godo by A213;
  then E-max C in LeftComp godo by GOBRD14:46;
  then UA meets L~godo by A116,A117,A125,A133,JORDAN1J:36;
  then
A214: UA meets (L~go \/ L~pion1) or UA meets L~do by A126,XBOOLE_1:70;
A215: UA c= C by JORDAN6:76;
  per cases by A214,XBOOLE_1:70;
  suppose UA meets L~go;
    then UA meets L~Cage(C,n) by A42,A127,XBOOLE_1:1,63;
    hence contradiction by A215,JORDAN10:5,XBOOLE_1:63;
  end;
  suppose UA meets L~pion1;
    hence contradiction by A5,A75,A130;
  end;
  suppose UA meets L~do;
    then UA meets L~Cage(C,n) by A49,A127,XBOOLE_1:1,63;
    hence contradiction by A215,JORDAN10:5,XBOOLE_1:63;
  end;
end;

theorem Th14:
  for C be Simple_closed_curve for i,j,k be Element of NAT st
  1 < i & i < len Gauge(C,n) & 1 <= k & k <= j & j <= width Gauge(C,n) &
  LSeg(Gauge(C,n)*(i,k),Gauge(C,n)*(i,j)) /\ L~Upper_Seq(C,n) =
  {Gauge(C,n)*(i,k)} &
  LSeg(Gauge(C,n)*(i,k),Gauge(C,n)*(i,j)) /\ L~Lower_Seq(C,n) =
  {Gauge(C,n)*(i,j)} holds
  LSeg(Gauge(C,n)*(i,k),Gauge(C,n)*(i,j)) meets Lower_Arc C
proof
  let C be Simple_closed_curve;
  let i,j,k be Element of NAT;
  set Ga = Gauge(C,n);
  set US = Upper_Seq(C,n);
  set LS = Lower_Seq(C,n);
  set LA = Lower_Arc C;
  set Wmin = W-min L~Cage(C,n);
  set Emax = E-max L~Cage(C,n);
  set Wbo = W-bound L~Cage(C,n);
  set Ebo = E-bound L~Cage(C,n);
  set Gik = Ga*(i,k);
  set Gij = Ga*(i,j);
  assume that
A1: 1 < i & i < len Ga and
A2: 1 <= k & k <= j & j <= width Ga and
A3: LSeg(Gik,Gij) /\ L~US = {Gik} and
A4: LSeg(Gik,Gij) /\ L~LS = {Gij} and
A5: LSeg(Gik,Gij) misses LA;
  Gij in {Gij} by TARSKI:def 1;
  then
A6: Gij in L~LS by A4,XBOOLE_0:def 3;
  Gik in {Gik} by TARSKI:def 1;
  then
A7: Gik in L~US by A3,XBOOLE_0:def 3;
  then
A8: j <> k by A1,A2,A6,JORDAN1J:57;
A9: 1 <= j & j <= width Ga by A2,XXREAL_0:2;
A10: 1 <= k & k <= width Ga by A2,XXREAL_0:2;
A11: [i,j] in Indices Ga by A1,A9,GOBOARD7:10;
A12: [i,k] in Indices Ga by A1,A10,GOBOARD7:10;
  set do = L_Cut(LS,Gij);
  set go = R_Cut(US,Gik);
A13: len Ga = width Ga by JORDAN8:def 1;
A14: len US >= 3 by JORDAN1E:19;
  then len US >= 1 by XXREAL_0:2;
  then 1 in dom US by FINSEQ_3:27;
  then
A15: US.1 = US/.1 by PARTFUN1:def 8
    .= Wmin by JORDAN1F:5;
A16: Wmin`1 = Wbo by EUCLID:56
    .= Ga*(1,k)`1 by A10,A13,JORDAN1A:94;
  len Ga >= 4 by JORDAN8:13;
  then
A17: len Ga >= 1 by XXREAL_0:2;
  then
A18: [1,k] in Indices Ga by A10,GOBOARD7:10;
  then
A19: Gik <> US.1 by A1,A12,A15,A16,JORDAN1G:7;
  then reconsider go as being_S-Seq FinSequence of TOP-REAL 2
  by A7,JORDAN3:70;
A20: len LS >= 1+2 by JORDAN1E:19;
  then len LS >= 1 by XXREAL_0:2;
  then
A21: 1 in dom LS & len LS in dom LS by FINSEQ_3:27;
  then
A22: LS.len LS = LS/.len LS by PARTFUN1:def 8
    .= Wmin by JORDAN1F:8;
A23: Wmin`1 = Wbo by EUCLID:56
    .= Ga*(1,k)`1 by A10,A13,JORDAN1A:94;
A24: [i,j] in Indices Ga by A1,A9,GOBOARD7:10;
  then
A25: Gij <> LS.len LS by A1,A18,A22,A23,JORDAN1G:7;
  then reconsider do as being_S-Seq FinSequence of TOP-REAL 2
  by A6,JORDAN3:69;
A26: [len Ga,k] in Indices Ga by A10,A17,GOBOARD7:10;
A27: LS.1 = LS/.1 by A21,PARTFUN1:def 8
    .= Emax by JORDAN1F:6;
  Emax`1 = Ebo by EUCLID:56
    .= Ga*(len Ga,k)`1 by A10,A13,JORDAN1A:92;
  then
A28: Gij <> LS.1 by A1,A24,A26,A27,JORDAN1G:7;
A29: len go >= 1+1 by TOPREAL1:def 10;
A30: Gik in rng US by A1,A7,A10,JORDAN1G:4,JORDAN1J:40;
  then
A31: go is_sequence_on Ga by JORDAN1G:4,JORDAN1J:38;
A32: len do >= 1+1 by TOPREAL1:def 10;
A33: Gij in rng LS by A1,A6,A9,JORDAN1G:5,JORDAN1J:40;
  then
A34: do is_sequence_on Ga by JORDAN1G:5,JORDAN1J:39;
  reconsider go as non constant s.c.c.
  (being_S-Seq FinSequence of TOP-REAL 2) by A29,A31,JGRAPH_1:16 ,JORDAN8:8;
  reconsider do as non constant s.c.c.
  (being_S-Seq FinSequence of TOP-REAL 2) by A32,A34,JGRAPH_1:16 ,JORDAN8:8;
A35: len go > 1 by A29,NAT_1:13;
  then
A36: len go in dom go by FINSEQ_3:27;
  then
A37: go/.len go = go.len go by PARTFUN1:def 8
    .= Gik by A7,JORDAN3:59;
  len do >= 1 by A32,XXREAL_0:2;
  then 1 in dom do by FINSEQ_3:27;
  then
A38: do/.1 = do.1 by PARTFUN1:def 8
    .= Gij by A6,JORDAN3:58;
  reconsider m = len go - 1 as Element of NAT by A36,FINSEQ_3:28;
A39: m+1 = len go;
  then
A40: len go-'1 = m by BINARITH:39;
A41: LSeg(go,m) c= L~go by TOPREAL3:26;
A42: L~go c= L~US by A7,JORDAN3:76;
  then LSeg(go,m) c= L~US by A41,XBOOLE_1:1;
  then
A43: LSeg(go,m) /\ LSeg(Gik,Gij) c= {Gik} by A3,XBOOLE_1:26;
  m >= 1 by A29,XREAL_1:21;
  then
A44: LSeg(go,m) = LSeg(go/.m,Gik) by A37,A39,TOPREAL1:def 5;
  {Gik} c= LSeg(go,m) /\ LSeg(Gik,Gij)
  proof
    let x be set;
    assume x in {Gik};
    then
A45: x = Gik by TARSKI:def 1;
A46: Gik in LSeg(go,m) by A44,TOPREAL1:6;
    Gik in LSeg(Gik,Gij) by TOPREAL1:6;
    hence x in LSeg(go,m) /\ LSeg(Gik,Gij) by A45,A46,XBOOLE_0:def 3;
  end;
  then
A47: LSeg(go,m) /\ LSeg(Gik,Gij) = {Gik} by A43,XBOOLE_0:def 10;
A48: LSeg(do,1) c= L~do by TOPREAL3:26;
A49: L~do c= L~LS by A6,JORDAN3:77;
  then LSeg(do,1) c= L~LS by A48,XBOOLE_1:1;
  then
A50: LSeg(do,1) /\ LSeg(Gik,Gij) c= {Gij} by A4,XBOOLE_1:26;
A51: LSeg(do,1) = LSeg(Gij,do/.(1+1)) by A32,A38,TOPREAL1:def 5;
  {Gij} c= LSeg(do,1) /\ LSeg(Gik,Gij)
  proof
    let x be set;
    assume x in {Gij};
    then
A52: x = Gij by TARSKI:def 1;
A53: Gij in LSeg(do,1) by A51,TOPREAL1:6;
    Gij in LSeg(Gik,Gij) by TOPREAL1:6;
    hence x in LSeg(do,1) /\ LSeg(Gik,Gij) by A52,A53,XBOOLE_0:def 3;
  end;
  then
A54: LSeg(Gik,Gij) /\ LSeg(do,1) = {Gij} by A50,XBOOLE_0:def 10;
A55: go/.1 = US/.1 by A7,SPRECT_3:39
    .= Wmin by JORDAN1F:5;
  then
A56: go/.1 = LS/.len LS by JORDAN1F:8
    .= do/.len do by A6,JORDAN1J:35;
A57: rng go c= L~go & rng do c= L~do by A29,A32,SPPOL_2:18;
A58: {go/.1} c= L~go /\ L~do
  proof
    let x be set;
    assume x in {go/.1};
    then x = go/.1 by TARSKI:def 1;
    then x in rng go & x in rng do by A56,FINSEQ_6:46,REVROT_1:3;
    hence x in L~go /\ L~do by A57,XBOOLE_0:def 3;
  end;
A59: LS.1 = LS/.1 by A21,PARTFUN1:def 8
    .= Emax by JORDAN1F:6;
A60: [len Ga,j] in Indices Ga by A9,A17,GOBOARD7:10;
  L~go /\ L~do c= {go/.1}
  proof
    let x be set;
    assume x in L~go /\ L~do;
    then
A61: x in L~go & x in L~do by XBOOLE_0:def 3;
    then x in L~US /\ L~LS by A42,A49,XBOOLE_0:def 3;
    then x in {Wmin,Emax} by JORDAN1E:20;
    then
A62: x = Wmin or x = Emax by TARSKI:def 2;
    now
      assume x = Emax;
      then
A63:  Emax = Gij by A6,A59,A61,JORDAN1E:11;
      Ga*(len Ga,j)`1 = Ebo by A9,A13,JORDAN1A:92;
      then Emax`1 <> Ebo by A1,A11,A60,A63,JORDAN1G:7;
      hence contradiction by EUCLID:56;
    end;
    hence x in {go/.1} by A55,A62,TARSKI:def 1;
  end;
  then
A64: L~go /\ L~do = {go/.1} by A58,XBOOLE_0:def 10;
  set W2 = go/.2;
A65: 2 in dom go by A29,FINSEQ_3:27;
A66: Gik..US >= 1 by A30,FINSEQ_4:31;
A67: now
    assume Gik`1 = Wbo;
    then Ga*(1,k)`1 = Ga*(i,k)`1 by A10,A13,JORDAN1A:94;
    hence contradiction by A1,A12,A18,JORDAN1G:7;
  end;
  go = mid(US,1,Gik..US) by A30,JORDAN1G:57
    .= US|(Gik..US) by A66,JORDAN3:25;
  then
A68: W2 = US/.2 by A65,FINSEQ_4:85;
A69: Wmin in rng go by A55,FINSEQ_6:46;
  set pion = <*Gik,Gij*>;
A70: now
    let n be Element of NAT;
    assume n in dom pion;
    then n in Seg 2 by FINSEQ_3:29;
    then n = 1 or n = 2 by FINSEQ_1:4,TARSKI:def 2;
    then pion/.n = Gik or pion/.n = Gij by FINSEQ_4:26;
    hence ex i,j be Element of NAT st [i,j] in Indices Ga & pion/.n = Ga*(i,j)
    by A11,A12;
  end;
A71: Gik <> Gij by A8,A11,A12,GOBOARD1:21;
A72: Gik`1 = Ga*(i,1)`1 by A1,A10,GOBOARD5:3
    .= Gij`1 by A1,A9,GOBOARD5:3;
  then LSeg(Gik,Gij) is vertical by SPPOL_1:37;
  then pion is_S-Seq by A71,JORDAN1B:8;
  then consider pion1 be FinSequence of TOP-REAL 2 such that
A73: pion1 is_sequence_on Ga and
A74: pion1 is_S-Seq and
A75: L~pion = L~pion1 and
A76: pion/.1 = pion1/.1 and
A77: pion/.len pion = pion1/.len pion1 and
A78: len pion <= len pion1 by A70,GOBOARD3:2;
  reconsider pion1 as being_S-Seq FinSequence of TOP-REAL 2 by A74;
  set godo = go^'pion1^'do;
A79: 1+1 <= len Cage(C,n) by GOBOARD7:36,XXREAL_0:2;
  then
A80: 1+1 <= len Rotate(Cage(C,n),Wmin) by REVROT_1:14;
  len (go^'pion1) >= len go by TOPREAL8:7;
  then
A81: len (go^'pion1) >= 1+1 by A29,XXREAL_0:2;
  then
A82: len (go^'pion1) > 1+0 by NAT_1:13;
A83: len godo >= len (go^'pion1) by TOPREAL8:7;
  then
A84: 1+1 <= len godo by A81,XXREAL_0:2;
A85: US is_sequence_on Ga by JORDAN1G:4;
A86: go/.len go = pion1/.1 by A37,A76,FINSEQ_4:26;
  then
A87: go^'pion1 is_sequence_on Ga by A31,A73,TOPREAL8:12;
A88: (go^'pion1)/.len (go^'pion1) = pion/.len pion by A77,GRAPH_2:58
    .= pion/.2 by FINSEQ_1:61
    .= do/.1 by A38,FINSEQ_4:26;
  then
A89: godo is_sequence_on Ga by A34,A87,TOPREAL8:12;
  LSeg(pion1,1) c= L~<*Gik,Gij*> by A75,TOPREAL3:26;
  then LSeg(pion1,1) c= LSeg(Gik,Gij) by SPPOL_2:21;
  then
A90: LSeg(go,len go-'1) /\ LSeg(pion1,1) c= {Gik} by A40,A47,XBOOLE_1:27;
A91: len pion1 >= 1+1 by A78,FINSEQ_1:61;
  {Gik} c= LSeg(go,m) /\ LSeg(pion1,1)
  proof
    let x be set;
    assume x in {Gik};
    then
A92: x = Gik by TARSKI:def 1;
A93: Gik in LSeg(go,m) by A44,TOPREAL1:6;
    Gik in LSeg(pion1,1) by A37,A86,A91,TOPREAL1:27;
    hence x in LSeg(go,m) /\ LSeg(pion1,1) by A92,A93,XBOOLE_0:def 3;
  end;
  then LSeg(go,len go-'1) /\ LSeg(pion1,1) = { go/.len go }
  by A37,A40,A90,XBOOLE_0:def 10;
  then
A94: go^'pion1 is unfolded by A86,TOPREAL8:34;
  len pion1 >= 2+0 by A78,FINSEQ_1:61;
  then
A95: len pion1-2 >= 0 by XREAL_1:21;
  len (go^'pion1)+1-1 = len go+len pion1-1 by GRAPH_2:13;
  then len (go^'pion1)-1 = len go + (len pion1-2)
    .= len go + (len pion1-'2) by A95,BINARITH:def 3;
  then
A96: len (go^'pion1)-'1 = len go + (len pion1-'2) by BINARITH:def 3;
A97: len pion1-1 >= 1 by A91,XREAL_1:21;
  then
A98: len pion1-'1 = len pion1-1 by BINARITH:def 3;
A99: len pion1-'2+1 = len pion1-2+1 by A95,BINARITH:def 3
    .= len pion1-'1 by A97,BINARITH:def 3;
  len pion1-1+1 <= len pion1;
  then
A100: len pion1-'1 < len pion1 by A98,NAT_1:13;
  LSeg(pion1,len pion1-'1) c= L~<*Gik,Gij*> by A75,TOPREAL3:26;
  then LSeg(pion1,len pion1-'1) c= LSeg(Gik,Gij) by SPPOL_2:21;
  then
A101: LSeg(pion1,len pion1-'1) /\ LSeg(do,1) c= {Gij} by A54,XBOOLE_1:27;
  {Gij} c= LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
  proof
    let x be set;
    assume x in {Gij};
    then
A102: x = Gij by TARSKI:def 1;
A103: Gij in LSeg(do,1) by A51,TOPREAL1:6;
    pion1/.(len pion1-'1+1) = pion/.2 by A77,A98,FINSEQ_1:61
      .= Gij by FINSEQ_4:26;
    then Gij in LSeg(pion1,len pion1-'1) by A97,A98,TOPREAL1:27;
    hence x in LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
    by A102,A103,XBOOLE_0:def 3;
  end;
  then LSeg(pion1,len pion1-'1) /\ LSeg(do,1) = {Gij} by A101,XBOOLE_0:def 10;
  then
A104: LSeg(go^'pion1,len go+(len pion1-'2)) /\ LSeg(do,1) =
  {(go^'pion1)/.len (go^'pion1)} by A38,A86,A88,A99,A100,TOPREAL8:31;
A105: (go^'pion1) is non trivial by A81,REALSET1:13;
A106: rng pion1 c= L~pion1 by A91,SPPOL_2:18;
A107: {pion1/.1} c= L~go /\ L~pion1
  proof
    let x be set;
    assume x in {pion1/.1};
    then x = pion1/.1 by TARSKI:def 1;
    then x in rng go & x in rng pion1 by A86,FINSEQ_6:46,REVROT_1:3;
    hence x in L~go /\ L~pion1 by A57,A106,XBOOLE_0:def 3;
  end;
  L~go /\ L~pion1 c= {pion1/.1}
  proof
    let x be set;
    assume x in L~go /\ L~pion1;
    then x in L~go & x in L~pion1 by XBOOLE_0:def 3;
    then x in L~pion1 /\ L~US by A42,XBOOLE_0:def 3;
    hence x in {pion1/.1} by A3,A37,A75,A86,SPPOL_2:21;
  end;
  then
A108: L~go /\ L~pion1 = {pion1/.1} by A107,XBOOLE_0:def 10;
  then
A109: (go^'pion1) is s.n.c. by A86,JORDAN1J:54;
  rng go /\ rng pion1 c= {pion1/.1} by A57,A106,A108,XBOOLE_1:27;
  then
A110: go^'pion1 is one-to-one by JORDAN1J:55;
A111: pion/.len pion = pion/.2 by FINSEQ_1:61
    .= do/.1 by A38,FINSEQ_4:26;
A112: {pion1/.len pion1} c= L~do /\ L~pion1
  proof
    let x be set;
    assume x in {pion1/.len pion1};
    then x = pion1/.len pion1 by TARSKI:def 1;
    then x in rng do & x in rng pion1 by A77,A111,FINSEQ_6:46,REVROT_1:3;
    hence x in L~do /\ L~pion1 by A57,A106,XBOOLE_0:def 3;
  end;
  L~do /\ L~pion1 c= {pion1/.len pion1}
  proof
    let x be set;
    assume x in L~do /\ L~pion1;
    then x in L~do & x in L~pion1 by XBOOLE_0:def 3;
    then x in L~pion1 /\ L~LS by A49,XBOOLE_0:def 3;
    hence x in {pion1/.len pion1} by A4,A38,A75,A77,A111,SPPOL_2:21;
  end;
  then
A113: L~do /\ L~pion1 = {pion1/.len pion1} by A112,XBOOLE_0:def 10;
A114: L~(go^'pion1) /\ L~do = (L~go \/ L~pion1) /\ L~do by A86,TOPREAL8:35
    .= {go/.1} \/ {do/.1} by A64,A77,A111,A113,XBOOLE_1:23
    .= {(go^'pion1)/.1} \/ {do/.1} by GRAPH_2:57
    .= {(go^'pion1)/.1,do/.1} by ENUMSET1:41;
  do/.len do = (go^'pion1)/.1 by A56,GRAPH_2:57;
  then reconsider godo as non constant standard special_circular_sequence
  by A84,A88,A89,A94,A96,A104,A105,A109,A110,A114,JORDAN8:7,8
  ,TOPREAL8:11,33,34;
A115: LA is_an_arc_of E-max C,W-min C by JORDAN6:def 9;
  then
A116: LA is connected by JORDAN6:11;
A117: W-min C in LA & E-max C in LA by A115,TOPREAL1:4;
  set ff = Rotate(Cage(C,n),Wmin);
  Wmin in rng Cage(C,n) by SPRECT_2:47;
  then
A118: ff/.1 = Wmin by FINSEQ_6:98;
A119: L~ff = L~Cage(C,n) by REVROT_1:33;
  then (W-max L~ff)..ff > 1 by A118,SPRECT_5:23;
  then (N-min L~ff)..ff > 1 by A118,A119,SPRECT_5:24,XXREAL_0:2;
  then (N-max L~ff)..ff > 1 by A118,A119,SPRECT_5:25,XXREAL_0:2;
  then
A120: Emax..ff > 1 by A118,A119,SPRECT_5:26,XXREAL_0:2;
A121: now
    assume
A122: Gik..US <= 1;
    Gik..US >= 1 by A30,FINSEQ_4:31;
    then Gik..US = 1 by A122,XXREAL_0:1;
    then Gik = US/.1 by A30,FINSEQ_5:41;
    hence contradiction by A15,A19,JORDAN1F:5;
  end;
A123: Cage(C,n) is_sequence_on Ga by JORDAN9:def 1;
  then
A124: ff is_sequence_on Ga by REVROT_1:34;
A125: right_cell(godo,1,Ga)\L~godo c= RightComp godo by A84,A89,JORDAN9:29;
A126: L~godo = L~(go^'pion1) \/ L~do by A88,TOPREAL8:35
    .= L~go \/ L~pion1 \/ L~do by A86,TOPREAL8:35;
  L~Cage(C,n) = L~US \/ L~LS by JORDAN1E:17;
  then
A127: L~US c= L~Cage(C,n) & L~LS c= L~Cage(C,n) by XBOOLE_1:7;
  then
A128: L~go c= L~Cage(C,n) & L~do c= L~Cage(C,n) by A42,A49,XBOOLE_1:1;
A129: W-min C in C by SPRECT_1:15;
A130: L~pion = LSeg(Gik,Gij) by SPPOL_2:21;
A131: now
    assume W-min C in L~godo;
    then
A132: W-min C in L~go \/ L~pion1 or W-min C in L~do by A126, XBOOLE_0: def 2;
    per cases by A132,XBOOLE_0:def 2;
    suppose W-min C in L~go;
      then C meets L~Cage(C,n) by A128,A129,XBOOLE_0:3;
      hence contradiction by JORDAN10:5;
    end;
    suppose W-min C in L~pion1;
      hence contradiction by A5,A75,A117,A130,XBOOLE_0:3;
    end;
    suppose W-min C in L~do;
      then C meets L~Cage(C,n) by A128,A129,XBOOLE_0:3;
      hence contradiction by JORDAN10:5;
    end;
  end;
  right_cell(Rotate(Cage(C,n),Wmin),1) =
  right_cell(ff,1,GoB ff) by A80,JORDAN1H:29
    .= right_cell(ff,1,GoB Cage(C,n)) by REVROT_1:28
    .= right_cell(ff,1,Ga) by JORDAN1H:52
    .= right_cell(ff-:Emax,1,Ga) by A120,A124,JORDAN1J:53
    .= right_cell(US,1,Ga) by JORDAN1E:def 1
    .= right_cell(R_Cut(US,Gik),1,Ga) by A30,A85,A121,JORDAN1J:52
    .= right_cell(go^'pion1,1,Ga) by A35,A87,JORDAN1J:51
    .= right_cell(godo,1,Ga) by A82,A89,JORDAN1J:51;
  then W-min C in right_cell(godo,1,Ga) by JORDAN1I:8;
  then
A133: W-min C in right_cell(godo,1,Ga)\L~godo by A131,XBOOLE_0:def 4;
A134: godo/.1 = (go^'pion1)/.1 by GRAPH_2:57
    .= Wmin by A55,GRAPH_2:57;
A135: len US >= 2 by A14,XXREAL_0:2;
A136: godo/.2 = (go^'pion1)/.2 by A81,GRAPH_2:61
    .= US/.2 by A29,A68,GRAPH_2:61
    .= (US^'LS)/.2 by A135,GRAPH_2:61
    .= Rotate(Cage(C,n),Wmin)/.2 by JORDAN1E:15;
A137: L~go \/ L~do is compact by COMPTS_1:19;
  Wmin in L~go \/ L~do by A57,A69,XBOOLE_0:def 2;
  then
A138: W-min (L~go \/ L~do) = Wmin by A128,A137,JORDAN1J:21,XBOOLE_1:8;
A139: (W-min (L~go \/ L~do))`1 = W-bound (L~go \/ L~do) & Wmin`1 = Wbo
  by EUCLID:56;
  W-bound LSeg(Gik,Gij) = Gik`1 by A72,SPRECT_1:62;
  then
A140: W-bound L~pion1 = Gik`1 by A75,SPPOL_2:21;
  Gik`1 >= Wbo by A7,A127,PSCOMP_1:71;
  then Gik`1 > Wbo by A67,REAL_1:def 5;
  then W-min (L~go\/L~do\/L~pion1) = W-min (L~go \/ L~do)
  by A137,A138,A139,A140, JORDAN1J:33;
  then
A141: W-min L~godo = Wmin by A126,A138,XBOOLE_1:4;
A142: rng godo c= L~godo by A81,A83,SPPOL_2:18,XXREAL_0:2;
  2 in dom godo by A84,FINSEQ_3:27;
  then
A143: godo/.2 in rng godo by PARTFUN2:4;
  godo/.2 in W-most L~Cage(C,n) by A136,JORDAN1I:27;
  then (godo/.2)`1 = (W-min L~godo)`1 by A141,PSCOMP_1:88
    .= W-bound L~godo by EUCLID:56;
  then godo/.2 in W-most L~godo by A142,A143,SPRECT_2:16;
  then Rotate(godo,W-min L~godo)/.2 in W-most L~godo by A134,A141,FINSEQ_6:
  95;
  then reconsider godo as clockwise_oriented non constant standard
  special_circular_sequence by JORDAN1I:27;
  len US in dom US by FINSEQ_5:6;
  then
A144: US.len US = US/.len US by PARTFUN1:def 8
    .= Emax by JORDAN1F:7;
A145: E-max C in E-most C by PSCOMP_1:111;
A146: east_halfline E-max C misses L~go
  proof
    assume east_halfline E-max C meets L~go;
    then consider p be set such that
A147: p in east_halfline E-max C and
A148: p in L~go by XBOOLE_0:3;
    reconsider p as Point of TOP-REAL 2 by A147;
    p in L~US by A42,A148;
    then p in east_halfline E-max C /\ L~Cage(C,n) by A127,A147,XBOOLE_0:def
    3;
    then
A149: p`1 = Ebo by A145,JORDAN1A:104;
    then
A150: p = Emax by A42,A148,JORDAN1J:46;
    then Emax = Gik by A7,A144,A148,JORDAN1J:43;
    then Gik`1 = Ga*(len Ga,k)`1 by A10,A13,A149,A150,JORDAN1A:92;
    hence contradiction by A1,A12,A26,JORDAN1G:7;
  end;
  now
    assume east_halfline E-max C meets L~godo;
    then
A151: east_halfline E-max C meets (L~go \/ L~pion1) or
    east_halfline E-max C meets L~do by A126,XBOOLE_1:70;
    per cases by A151,XBOOLE_1:70;
    suppose east_halfline E-max C meets L~go;
      hence contradiction by A146;
    end;
    suppose east_halfline E-max C meets L~pion1;
      then consider p be set such that
A152: p in east_halfline E-max C and
A153: p in L~pion1 by XBOOLE_0:3;
      reconsider p as Point of TOP-REAL 2 by A152;
A154: p`1 = Gik`1 by A72,A75,A130,A153,GOBOARD7:5;
      i+1 <= len Ga by A1,NAT_1:13;
      then i+1-1 <= len Ga-1 by XREAL_1:11;
      then
A155: i <= len Ga-'1 by BINARITH:def 3;
      len Ga-'1 <= len Ga by BINARITH:52;
      then p`1 <= Ga*(len Ga-'1,1)`1 by A1,A10,A13,A17,A154,A155,JORDAN1A:39;
      then p`1 <= E-bound C by A17,JORDAN8:15;
      then
A156: p`1 <= (E-max C)`1 by EUCLID:56;
      p`1 >= (E-max C)`1 by A152,TOPREAL1:def 13;
      then
A157: p`1 = (E-max C)`1 by A156,XXREAL_0:1;
      p`2 = (E-max C)`2 by A152,TOPREAL1:def 13;
      then p = E-max C by A157,TOPREAL3:11;
      hence contradiction by A5,A75,A117,A130,A153,XBOOLE_0:3;
    end;
    suppose east_halfline E-max C meets L~do;
      then consider p be set such that
A158: p in east_halfline E-max C and
A159: p in L~do by XBOOLE_0:3;
      reconsider p as Point of TOP-REAL 2 by A158;
      p in L~LS by A49,A159;
      then p in east_halfline E-max C /\ L~Cage(C,n)
      by A127,A158,XBOOLE_0:def 3;
      then
A160: p`1 = Ebo by A145,JORDAN1A:104;
A161: (E-max C)`2 = p`2 by A158,TOPREAL1:def 13;
      set RC = Rotate(Cage(C,n),Emax);
A162: E-max C in right_cell(RC,1) by JORDAN1I:9;
A163: 1+1 <= len LS by A20,XXREAL_0:2;
      LS = RC-:Wmin by JORDAN1G:26;
      then
A164: LSeg(LS,1) = LSeg(RC,1) by A163,SPPOL_2:9;
A165: L~RC = L~Cage(C,n) by REVROT_1:33;
A166: len RC = len Cage(C,n) by REVROT_1:14;
A167: GoB RC = GoB Cage(C,n) by REVROT_1:28
        .= Ga by JORDAN1H:52;
A168: Emax in rng Cage(C,n) by SPRECT_2:50;
A169: RC is_sequence_on Ga by A123,REVROT_1:34;
A170: RC/.1 = E-max L~RC by A165,A168,FINSEQ_6:98;
      then consider ii,jj be Element of NAT such that
A171: [ii,jj+1] in Indices Ga and
A172: [ii,jj] in Indices Ga and
A173: RC/.1 = Ga*(ii,jj+1) and
A174: RC/.(1+1) = Ga*(ii,jj) by A79,A166,A169,JORDAN1I:25;
      consider jj2 be Element of NAT such that
A175: 1 <= jj2 & jj2 <= width Ga and
A176: Emax = Ga*(len Ga,jj2) by JORDAN1D:29;
A177: len Ga >= 4 by JORDAN8:13;
      then len Ga >= 1 by XXREAL_0:2;
      then [len Ga,jj2] in Indices Ga by A175,GOBOARD7:10;
      then
A178: ii = len Ga by A165,A170,A171,A173,A176,GOBOARD1:21;
A179: 1 <= ii & ii <= len Ga & 1 <= jj+1 & jj+1 <= width Ga
      by A171,GOBOARD5:1;
A180: 1 <= ii & ii <= len Ga & 1 <= jj & jj <= width Ga by A172,GOBOARD5:1;
A181: ii+1 <> ii;
      jj+1+1 <> jj;
      then
A182: right_cell(RC,1) = cell(Ga,ii-'1,jj)
      by A79,A166,A167,A171,A172,A173,A174,A181, GOBOARD5:def 6;
A183: ii-'1+1 = ii by A179,BINARITH:53;
      ii-1 >= 4-1 by A177,A178,XREAL_1:11;
      then
A184: ii-1 >= 1 by XXREAL_0:2;
      then
A185: 1 <= ii-'1 by BINARITH:def 3;
A186: Ga*(ii-'1,jj)`2 <= p`2 & p`2 <= Ga*(ii-'1,jj+1)`2
      by A161,A162,A179,A180,A182,A183,A184, JORDAN9:19;
A187: ii-'1 < len Ga by A179,A183,NAT_1:13;
      then
A188: Ga*(ii-'1,jj)`2 = Ga*(1,jj)`2 by A180,A185,GOBOARD5:2
        .= Ga*(ii,jj)`2 by A180,GOBOARD5:2;
A189: Ga*(ii-'1,jj+1)`2 = Ga*(1,jj+1)`2 by A179,A185,A187,GOBOARD5:2
        .= Ga*(ii,jj+1)`2 by A179,GOBOARD5:2;
      Ga*(len Ga,jj)`1 = Ebo & Ebo = Ga*(len Ga,jj+1)`1
      by A13,A179,A180,JORDAN1A:92;
      then p in LSeg(RC/.1,RC/.(1+1))
      by A160,A173,A174,A178,A186,A188,A189,GOBOARD7:8;
      then
A190: p in LSeg(LS,1) by A79,A164,A166,TOPREAL1:def 5;
A191: p in LSeg(do,Index(p,do)) by A159,JORDAN3:42;
A192: do = mid(LS,Gij..LS,len LS) by A33,JORDAN1J:37;
A193: 1<=Gij..LS & Gij..LS<=len LS by A33,FINSEQ_4:31;
      Gij..LS <> len LS by A25,A33,FINSEQ_4:29;
      then
A194: Gij..LS < len LS by A193,REAL_1:def 5;
A195: 1<=Index(p,do) & Index(p,do) < len do by A159,JORDAN3:41;
A196: Index(Gij,LS)+1 = Gij..LS by A28,A33,JORDAN1J:56;
      consider t be Nat such that
A197: t in dom LS and
A198: LS.t = Gij by A33,FINSEQ_2:11;
A199: 1 <= t & t <= len LS by A197,FINSEQ_3:27;
      then 1 < t by A28,A198,REAL_1:def 5;
      then Index(Gij,LS)+1 = t by A198,A199,JORDAN3:45;
      then
A200: len L_Cut(LS,Gij) = len LS-Index(Gij,LS) by A6,A198,JORDAN3:61;
      set tt = Index(p,do)+(Gij..LS)-'1;
A201: 1<=Index(Gij,LS) & 0+Index(Gij,LS) < len LS by A6,JORDAN3:41;
      then
A202: len LS-Index(Gij,LS) > 0 by XREAL_1:22;
      Index(p,do) < len LS-'Index(Gij,LS) by A195,A200,BINARITH:def 3;
      then Index(p,do)+1 <= len LS-'Index(Gij,LS) by NAT_1:13;
      then Index(p,do) <= len LS-'Index(Gij,LS)-1 by XREAL_1:21;
      then Index(p,do) <= len LS-Index(Gij,LS)-1 by A202,BINARITH:def 3;
      then Index(p,do) <= len LS-Gij..LS by A196;
      then Index(p,do) <= len LS-'Gij..LS by BINARITH:def 3;
      then Index(p,do) < len LS-'(Gij..LS)+1 by NAT_1:13;
      then
A203: LSeg(mid(LS,Gij..LS,len LS),Index(p,do)) =
      LSeg(LS,Index(p,do)+(Gij..LS)-'1) by A193,A194,A195,JORDAN4:31;
A204: 1+1 <= Gij..LS by A196,A201,XREAL_1:9;
      then Index(p,do)+Gij..LS >= 1+1+1 by A195,XREAL_1:9;
      then Index(p,do)+Gij..LS-1 >= 1+1+1-1 by XREAL_1:11;
      then
A205: tt >= 1+1 by BINARITH:def 3;
A206: 2 in dom LS by A163,FINSEQ_3:27;
      now per cases by A205,REAL_1:def 5;
        suppose tt > 1+1;
          then LSeg(LS,1) misses LSeg(LS,tt) by TOPREAL1:def 9;
          hence contradiction by A190,A191,A192,A203,XBOOLE_0:3;
        end;
        suppose
A207:     tt = 1+1;
          then LSeg(LS,1) /\ LSeg(LS,tt) = {LS/.2} by A20,TOPREAL1:def 8;
          then p in {LS/.2} by A190,A191,A192,A203,XBOOLE_0:def 3;
          then
A208:     p = LS/.2 by TARSKI:def 1;
          then
A209:     p..LS = 2 by A206,FINSEQ_5:44;
          1+1 = Index(p,do)+(Gij..LS)-1 by A207,BINARITH:def 3;
          then 1+1+1 = Index(p,do)+(Gij..LS);
          then
A210:     Gij..LS = 2 by A195,A204,JORDAN1E:10;
          p in rng LS by A206,A208,PARTFUN2:4;
          then p = Gij by A33,A209,A210,FINSEQ_5:10;
          then Gij`1 = Ebo by A208,JORDAN1G:40;
          then Gij`1 = Ga*(len Ga,j)`1 by A9,A13,JORDAN1A:92;
          hence contradiction by A1,A11,A60,JORDAN1G:7;
        end;
      end;
      hence contradiction;
    end;
  end;
  then east_halfline E-max C c= (L~godo)` by SUBSET_1:43;
  then consider W be Subset of TOP-REAL 2 such that
A211: W is_a_component_of (L~godo)` and
A212: east_halfline E-max C c= W by GOBOARD9:5;
  W is not Bounded by A212,JORDAN2C:16,129;
  then W is_outside_component_of L~godo by A211,JORDAN2C:def 4;
  then W c= UBD L~godo by JORDAN2C:27;
  then
A213: east_halfline E-max C c= UBD L~godo by A212,XBOOLE_1:1;
  E-max C in east_halfline E-max C by TOPREAL1:45;
  then E-max C in UBD L~godo by A213;
  then E-max C in LeftComp godo by GOBRD14:46;
  then LA meets L~godo by A116,A117,A125,A133,JORDAN1J:36;
  then
A214: LA meets (L~go \/ L~pion1) or LA meets L~do by A126,XBOOLE_1:70;
A215: LA c= C by JORDAN6:76;
  per cases by A214,XBOOLE_1:70;
  suppose LA meets L~go;
    then LA meets L~Cage(C,n) by A42,A127,XBOOLE_1:1,63;
    hence contradiction by A215,JORDAN10:5,XBOOLE_1:63;
  end;
  suppose LA meets L~pion1;
    hence contradiction by A5,A75,A130;
  end;
  suppose LA meets L~do;
    then LA meets L~Cage(C,n) by A49,A127,XBOOLE_1:1,63;
    hence contradiction by A215,JORDAN10:5,XBOOLE_1:63;
  end;
end;

theorem
  for C be Simple_closed_curve for i,j,k be Element of NAT st
  1 < i & i < len Gauge(C,n) &
  1 <= j & j <= k & k <= width Gauge(C,n) & n > 0 &
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Lower_Arc L~Cage(C,n) =
  {Gauge(C,n)*(i,k)} &
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Upper_Arc L~Cage(C,n) =
  {Gauge(C,n)*(i,j)} holds
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Upper_Arc C
proof
  let C be Simple_closed_curve;
  let i,j,k be Element of NAT;
  assume that
A1: 1 < i & i < len Gauge(C,n) and
A2: 1 <= j & j <= k & k <= width Gauge(C,n) and
A3: n > 0 and
A4: LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Lower_Arc L~Cage(C,n) =
  {Gauge(C,n)*(i,k)} and
A5: LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Upper_Arc L~Cage(C,n) =
  {Gauge(C,n)*(i,j)};
A6: L~Lower_Seq(C,n) = Lower_Arc L~Cage(C,n) by A3,JORDAN1G:64;
  L~Upper_Seq(C,n) = Upper_Arc L~Cage(C,n) by A3,JORDAN1G:63;
  hence LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Upper_Arc C
  by A1,A2,A4,A5,A6,Th13;
end;

theorem
  for C be Simple_closed_curve for i,j,k be Element of NAT st
  1 < i & i < len Gauge(C,n) &
  1 <= j & j <= k & k <= width Gauge(C,n) & n > 0 &
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Lower_Arc L~Cage(C,n) =
  {Gauge(C,n)*(i,k)} &
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Upper_Arc L~Cage(C,n) =
  {Gauge(C,n)*(i,j)} holds
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Lower_Arc C
proof
  let C be Simple_closed_curve;
  let i,j,k be Element of NAT;
  assume that
A1: 1 < i & i < len Gauge(C,n) and
A2: 1 <= j & j <= k & k <= width Gauge(C,n) and
A3: n > 0 and
A4: LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Lower_Arc L~Cage(C,n) =
  {Gauge(C,n)*(i,k)} and
A5: LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) /\ Upper_Arc L~Cage(C,n) =
  {Gauge(C,n)*(i,j)};
A6: L~Lower_Seq(C,n) = Lower_Arc L~Cage(C,n) by A3,JORDAN1G:64;
  L~Upper_Seq(C,n) = Upper_Arc L~Cage(C,n) by A3,JORDAN1G:63;
  hence LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Lower_Arc C
  by A1,A2,A4,A5,A6,Th14;
end;

theorem Th17:
  for C be Simple_closed_curve for i,j,k be Element of NAT st
  1 < i & i < len Gauge(C,n) & 1 <= j & j <= k & k <= width Gauge(C,n) &
  Gauge(C,n)*(i,k) in L~Lower_Seq(C,n) &
  Gauge(C,n)*(i,j) in L~Upper_Seq(C,n) holds
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Upper_Arc C
proof
  let C be Simple_closed_curve;
  let i,j,k be Element of NAT;
  assume that
A1: 1 < i & i < len Gauge(C,n) and
A2: 1 <= j & j <= k & k <= width Gauge(C,n) and
A3: Gauge(C,n)*(i,k) in L~Lower_Seq(C,n) and
A4: Gauge(C,n)*(i,j) in L~Upper_Seq(C,n);
  consider j1,k1 be Element of NAT such that
A5: j <= j1 and
A6: j1 <= k1 and
A7: k1 <= k and
A8: LSeg(Gauge(C,n)*(i,j1),Gauge(C,n)*(i,k1)) /\ L~Upper_Seq(C,n) =
  {Gauge(C,n)*(i,j1)} and
A9: LSeg(Gauge(C,n)*(i,j1),Gauge(C,n)*(i,k1)) /\ L~Lower_Seq(C,n) =
  {Gauge(C,n)*(i,k1)} by A1,A2,A3,A4,JORDAN15:19;
  1 <= j1 & k1 <= width Gauge(C,n) by A2,A5,A7,XXREAL_0:2;
  then LSeg(Gauge(C,n)*(i,j1),Gauge(C,n)*(i,k1)) meets Upper_Arc C
  by A1,A6,A8,A9,Th13;
  hence LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Upper_Arc C
  by A1,A2,A5,A6,A7, JORDAN15:7,XBOOLE_1:63;
end;

theorem Th18:
  for C be Simple_closed_curve for i,j,k be Element of NAT st
  1 < i & i < len Gauge(C,n) & 1 <= j & j <= k & k <= width Gauge(C,n) &
  Gauge(C,n)*(i,k) in L~Lower_Seq(C,n) &
  Gauge(C,n)*(i,j) in L~Upper_Seq(C,n) holds
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Lower_Arc C
proof
  let C be Simple_closed_curve;
  let i,j,k be Element of NAT;
  assume that
A1: 1 < i & i < len Gauge(C,n) and
A2: 1 <= j & j <= k & k <= width Gauge(C,n) and
A3: Gauge(C,n)*(i,k) in L~Lower_Seq(C,n) and
A4: Gauge(C,n)*(i,j) in L~Upper_Seq(C,n);
  consider j1,k1 be Element of NAT such that
A5: j <= j1 and
A6: j1 <= k1 and
A7: k1 <= k and
A8: LSeg(Gauge(C,n)*(i,j1),Gauge(C,n)*(i,k1)) /\ L~Upper_Seq(C,n) =
  {Gauge(C,n)*(i,j1)} and
A9: LSeg(Gauge(C,n)*(i,j1),Gauge(C,n)*(i,k1)) /\ L~Lower_Seq(C,n) =
  {Gauge(C,n)*(i,k1)} by A1,A2,A3,A4,JORDAN15:19;
  1 <= j1 & k1 <= width Gauge(C,n) by A2,A5,A7,XXREAL_0:2;
  then LSeg(Gauge(C,n)*(i,j1),Gauge(C,n)*(i,k1)) meets Lower_Arc C
  by A1,A6,A8,A9,Th14;
  hence LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Lower_Arc C
  by A1,A2,A5,A6,A7, JORDAN15:7,XBOOLE_1:63;
end;

theorem Th19:
  for C be Simple_closed_curve for i,j,k be Element of NAT st
  1 < i & i < len Gauge(C,n) &
  1 <= j & j <= k & k <= width Gauge(C,n) & n > 0 &
  Gauge(C,n)*(i,k) in Lower_Arc L~Cage(C,n) &
  Gauge(C,n)*(i,j) in Upper_Arc L~Cage(C,n) holds
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Upper_Arc C
proof
  let C be Simple_closed_curve;
  let i,j,k be Element of NAT;
  assume that
A1: 1 < i & i < len Gauge(C,n) and
A2: 1 <= j & j <= k & k <= width Gauge(C,n) and
A3: n > 0 and
A4: Gauge(C,n)*(i,k) in Lower_Arc L~Cage(C,n) and
A5: Gauge(C,n)*(i,j) in Upper_Arc L~Cage(C,n);
A6: L~Lower_Seq(C,n) = Lower_Arc L~Cage(C,n) by A3,JORDAN1G:64;
  L~Upper_Seq(C,n) = Upper_Arc L~Cage(C,n) by A3,JORDAN1G:63;
  hence LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Upper_Arc C
  by A1,A2,A4,A5,A6,Th17;
end;

theorem Th20:
  for C be Simple_closed_curve for i,j,k be Element of NAT st
  1 < i & i < len Gauge(C,n) &
  1 <= j & j <= k & k <= width Gauge(C,n) & n > 0 &
  Gauge(C,n)*(i,k) in Lower_Arc L~Cage(C,n) &
  Gauge(C,n)*(i,j) in Upper_Arc L~Cage(C,n) holds
  LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Lower_Arc C
proof
  let C be Simple_closed_curve;
  let i,j,k be Element of NAT;
  assume that
A1: 1 < i & i < len Gauge(C,n) and
A2: 1 <= j & j <= k & k <= width Gauge(C,n) and
A3: n > 0 and
A4: Gauge(C,n)*(i,k) in Lower_Arc L~Cage(C,n) and
A5: Gauge(C,n)*(i,j) in Upper_Arc L~Cage(C,n);
A6: L~Lower_Seq(C,n) = Lower_Arc L~Cage(C,n) by A3,JORDAN1G:64;
  L~Upper_Seq(C,n) = Upper_Arc L~Cage(C,n) by A3,JORDAN1G:63;
  hence LSeg(Gauge(C,n)*(i,j),Gauge(C,n)*(i,k)) meets Lower_Arc C
  by A1,A2,A4,A5,A6,Th18;
end;

theorem Th21:
  for C be Simple_closed_curve for i1,i2,j,k be Element of NAT st
  1 < i1 & i1 <= i2 & i2 < len Gauge(C,n) &
  1 <= j & j <= k & k <= width Gauge(C,n) &
  (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
  LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) /\ L~Upper_Seq(C,n) =
  {Gauge(C,n)*(i1,j)} & (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
  LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) /\ L~Lower_Seq(C,n) =
  {Gauge(C,n)*(i2,k)} holds (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
  LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) meets Upper_Arc C
proof
  let C be Simple_closed_curve;
  let i1,i2,j,k be Element of NAT;
  set G = Gauge(C,n);
  set pio = LSeg(G*(i1,j),G*(i1,k));
  set poz = LSeg(G*(i1,k),G*(i2,k));
  set US = Upper_Seq(C,n);
  set LS = Lower_Seq(C,n);
  assume that
A1: 1 < i1 & i1 <= i2 & i2 < len G and
A2: 1 <= j & j <= k & k <= width G and
A3: (pio \/ poz) /\ L~US = {G*(i1,j)} and
A4: (pio \/ poz) /\ L~LS = {G*(i2,k)} and
A5: (pio \/ poz) misses Upper_Arc C;
  set UA = Upper_Arc C;
  set Wmin = W-min L~Cage(C,n);
  set Emax = E-max L~Cage(C,n);
  set Wbo = W-bound L~Cage(C,n);
  set Ebo = E-bound L~Cage(C,n);
  set Gik = G*(i2,k);
  set Gij = G*(i1,j);
  set Gi1k = G*(i1,k);
A6: 1 < i1 & i1 < len G & 1 < i2 & i2 < len G by A1,XXREAL_0:2;
A7: L~<*Gij,Gi1k,Gik*> = poz \/ pio by TOPREAL3:23;
  Gik in {Gik} by TARSKI:def 1;
  then
A8: Gik in L~LS by A4,XBOOLE_0:def 3;
  Gij in {Gij} by TARSKI:def 1;
  then
A9: Gij in L~US by A3,XBOOLE_0:def 3;
A10: 1 <= j & j <= width G by A2,XXREAL_0:2;
A11: 1 <= k & k <= width G by A2,XXREAL_0:2;
A12: [i1,j] in Indices G by A6,A10,GOBOARD7:10;
A13: [i2,k] in Indices G by A6,A11,GOBOARD7:10;
A14: [i1,k] in Indices G by A6,A11,GOBOARD7:10;
  set go = R_Cut(US,Gij);
  set do = L_Cut(LS,Gik);
A15: len G = width G by JORDAN8:def 1;
A16: len US >= 3 by JORDAN1E:19;
  then len US >= 1 by XXREAL_0:2;
  then 1 in dom US by FINSEQ_3:27;
  then
A17: US.1 = US/.1 by PARTFUN1:def 8
    .= Wmin by JORDAN1F:5;
A18: Wmin`1 = Wbo by EUCLID:56
    .= G*(1,k)`1 by A11,A15,JORDAN1A:94;
  len G >= 4 by JORDAN8:13;
  then
A19: len G >= 1 by XXREAL_0:2;
  then
A20: [1,k] in Indices G by A11,GOBOARD7:10;
  then
A21: Gij <> US.1 by A1,A12,A17,A18,JORDAN1G:7;
  then reconsider go as being_S-Seq FinSequence of TOP-REAL 2
  by A9,JORDAN3:70;
A22: [1,j] in Indices G by A10,A19,GOBOARD7:10;
A23: len LS >= 1+2 by JORDAN1E:19;
  then len LS >= 1 by XXREAL_0:2;
  then
A24: 1 in dom LS & len LS in dom LS by FINSEQ_3:27;
  then
A25: LS.len LS = LS/.len LS by PARTFUN1:def 8
    .= Wmin by JORDAN1F:8;
  Wmin`1 = Wbo by EUCLID:56
    .= G*(1,k)`1 by A11,A15,JORDAN1A:94;
  then
A26: Gik <> LS.len LS by A1,A13,A20,A25,JORDAN1G:7;
  then reconsider do as being_S-Seq FinSequence of TOP-REAL 2
  by A8,JORDAN3:69;
A27: [len G,k] in Indices G by A11,A19,GOBOARD7:10;
A28: LS.1 = LS/.1 by A24,PARTFUN1:def 8
    .= Emax by JORDAN1F:6;
  Emax`1 = Ebo by EUCLID:56
    .= G*(len G,k)`1 by A11,A15,JORDAN1A:92;
  then
A29: Gik <> LS.1 by A1,A13,A27,A28,JORDAN1G:7;
A30: len go >= 1+1 by TOPREAL1:def 10;
A31: Gij in rng US by A6,A9,A10,JORDAN1G:4,JORDAN1J:40;
  then
A32: go is_sequence_on G by JORDAN1G:4,JORDAN1J:38;
A33: len do >= 1+1 by TOPREAL1:def 10;
A34: Gik in rng LS by A6,A8,A11,JORDAN1G:5,JORDAN1J:40;
  then
A35: do is_sequence_on G by JORDAN1G:5,JORDAN1J:39;
  reconsider go as non constant s.c.c.
  (being_S-Seq FinSequence of TOP-REAL 2) by A30,A32,JGRAPH_1:16 ,JORDAN8:8;
  reconsider do as non constant s.c.c.
  (being_S-Seq FinSequence of TOP-REAL 2) by A33,A35,JGRAPH_1:16 ,JORDAN8:8;
A36: len go > 1 by A30,NAT_1:13;
  then
A37: len go in dom go by FINSEQ_3:27;
  then
A38: go/.len go = go.len go by PARTFUN1:def 8
    .= Gij by A9,JORDAN3:59;
  len do >= 1 by A33,XXREAL_0:2;
  then 1 in dom do by FINSEQ_3:27;
  then
A39: do/.1 = do.1 by PARTFUN1:def 8
    .= Gik by A8,JORDAN3:58;
  reconsider m = len go - 1 as Element of NAT by A37,FINSEQ_3:28;
A40: m+1 = len go;
  then
A41: len go-'1 = m by BINARITH:39;
A42: LSeg(go,m) c= L~go by TOPREAL3:26;
A43: L~go c= L~US by A9,JORDAN3:76;
  then LSeg(go,m) c= L~US by A42,XBOOLE_1:1;
  then
A44: LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> c= {Gij} by A3,A7,XBOOLE_1:26;
  m >= 1 by A30,XREAL_1:21;
  then
A45: LSeg(go,m) = LSeg(go/.m,Gij) by A38,A40,TOPREAL1:def 5;
  {Gij} c= LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*>
  proof
    let x be set;
    assume x in {Gij};
    then
A46: x = Gij by TARSKI:def 1;
A47: Gij in LSeg(go,m) by A45,TOPREAL1:6;
    Gij in LSeg(Gij,Gi1k) by TOPREAL1:6;
    then Gij in LSeg(Gij,Gi1k) \/ LSeg(Gi1k,Gik) by XBOOLE_0:def 2;
    then Gij in L~<*Gij,Gi1k,Gik*> by SPRECT_1:10;
    hence x in LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> by A46,A47,XBOOLE_0:def 3;
  end;
  then
A48: LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> = {Gij} by A44,XBOOLE_0:def 10;
A49: LSeg(do,1) c= L~do by TOPREAL3:26;
A50: L~do c= L~LS by A8,JORDAN3:77;
  then LSeg(do,1) c= L~LS by A49,XBOOLE_1:1;
  then
A51: LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*> c= {Gik} by A4,A7,XBOOLE_1:26;
A52: LSeg(do,1) = LSeg(Gik,do/.(1+1)) by A33,A39,TOPREAL1:def 5;
  {Gik} c= LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*>
  proof
    let x be set;
    assume x in {Gik};
    then
A53: x = Gik by TARSKI:def 1;
A54: Gik in LSeg(do,1) by A52,TOPREAL1:6;
    Gik in LSeg(Gi1k,Gik) by TOPREAL1:6;
    then Gik in LSeg(Gij,Gi1k) \/ LSeg(Gi1k,Gik) by XBOOLE_0:def 2;
    then Gik in L~<*Gij,Gi1k,Gik*> by SPRECT_1:10;
    hence x in LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*> by A53,A54,XBOOLE_0:def 3;
  end;
  then
A55: L~<*Gij,Gi1k,Gik*> /\ LSeg(do,1) = {Gik} by A51,XBOOLE_0:def 10;
A56: go/.1 = US/.1 by A9,SPRECT_3:39
    .= Wmin by JORDAN1F:5;
  then
A57: go/.1 = LS/.len LS by JORDAN1F:8
    .= do/.len do by A8,JORDAN1J:35;
A58: rng go c= L~go & rng do c= L~do by A30,A33,SPPOL_2:18;
A59: {go/.1} c= L~go /\ L~do
  proof
    let x be set;
    assume x in {go/.1};
    then x = go/.1 by TARSKI:def 1;
    then x in rng go & x in rng do by A57,FINSEQ_6:46,REVROT_1:3;
    hence x in L~go /\ L~do by A58,XBOOLE_0:def 3;
  end;
A60: LS.1 = LS/.1 by A24,PARTFUN1:def 8
    .= Emax by JORDAN1F:6;
A61: [len G,j] in Indices G by A10,A19,GOBOARD7:10;
  L~go /\ L~do c= {go/.1}
  proof
    let x be set;
    assume x in L~go /\ L~do;
    then
A62: x in L~go & x in L~do by XBOOLE_0:def 3;
    then x in L~US /\ L~LS by A43,A50,XBOOLE_0:def 3;
    then x in {Wmin,Emax} by JORDAN1E:20;
    then
A63: x = Wmin or x = Emax by TARSKI:def 2;
    now
      assume x = Emax;
      then
A64:  Emax = Gik by A8,A60,A62,JORDAN1E:11;
      G*(len G,j)`1 = Ebo by A10,A15,JORDAN1A:92;
      then Emax`1 <> Ebo by A1,A13,A61,A64,JORDAN1G:7;
      hence contradiction by EUCLID:56;
    end;
    hence x in {go/.1} by A56,A63,TARSKI:def 1;
  end;
  then
A65: L~go /\ L~do = {go/.1} by A59,XBOOLE_0:def 10;
  set W2 = go/.2;
A66: 2 in dom go by A30,FINSEQ_3:27;
A67: Gij..US >= 1 by A31,FINSEQ_4:31;
A68: now
    assume Gij`1 = Wbo;
    then G*(1,j)`1 = G*(i1,j)`1 by A10,A15,JORDAN1A:94;
    hence contradiction by A1,A12,A22,JORDAN1G:7;
  end;
  go = mid(US,1,Gij..US) by A31,JORDAN1G:57
    .= US|(Gij..US) by A67,JORDAN3:25;
  then
A69: W2 = US/.2 by A66,FINSEQ_4:85;
A70: Wmin in rng go by A56,FINSEQ_6:46;
  set pion = <*Gij,Gi1k,Gik*>;
A71: now
    let n be Element of NAT;
    assume n in dom pion;
    then n in {1,2,3} by FINSEQ_3:1,30;
    then n = 1 or n = 2 or n = 3 by ENUMSET1:def 1;
    then pion/.n = Gij or pion/.n = Gi1k or pion/.n = Gik by FINSEQ_4:27;
    hence ex i,j be Element of NAT st [i,j] in Indices G & pion/.n = G*(i,j)
    by A12,A13,A14;
  end;
A72: Gi1k`1 = G*(i1,1)`1 by A6,A11,GOBOARD5:3
    .= Gij`1 by A6,A10,GOBOARD5:3;
  Gi1k`2 = G*(1,k)`2 by A6,A11,GOBOARD5:2
    .= Gik`2 by A6,A11,GOBOARD5:2;
  then
A73: Gi1k = |[Gij`1,Gik`2]| by A72,EUCLID:57;
A74: Gi1k in pio by TOPREAL1:6;
A75: Gi1k in poz by TOPREAL1:6;
  now per cases;
    suppose Gik`1 <> Gij`1 & Gik`2 <> Gij`2;
      then pion is_S-Seq by A73,TOPREAL3:41;
      then consider pion1 be FinSequence of TOP-REAL 2 such that
A76:  pion1 is_sequence_on G and
A77:  pion1 is_S-Seq and
A78:  L~pion = L~pion1 and
A79:  pion/.1 = pion1/.1 and
A80:  pion/.len pion = pion1/.len pion1 and
A81:  len pion <= len pion1 by A71,GOBOARD3:2;
      reconsider pion1 as being_S-Seq FinSequence of TOP-REAL 2 by A77;
      set godo = go^'pion1^'do;
A82:  Gi1k`1 = G*(i1,1)`1 by A6,A11,GOBOARD5:3
        .= Gij`1 by A6,A10,GOBOARD5:3;
A83:  Gi1k`1 <= Gik`1 by A1,A11,JORDAN1A:39;
      then
A84:  W-bound poz = Gi1k`1 by SPRECT_1:62;
A85:  W-bound pio = Gij`1 by A82,SPRECT_1:62;
      W-bound (poz \/ pio) = min(W-bound poz, W-bound pio) by SPRECT_1:54
        .= Gij`1 by A82,A84,A85;
      then
A86:  W-bound L~pion1 = Gij`1 by A78,TOPREAL3:23;
A87:  1+1 <= len Cage(C,n) by GOBOARD7:36,XXREAL_0:2;
      then
A88:  1+1 <= len Rotate(Cage(C,n),Wmin) by REVROT_1:14;
      len (go^'pion1) >= len go by TOPREAL8:7;
      then
A89:  len (go^'pion1) >= 1+1 by A30,XXREAL_0:2;
      then
A90:  len (go^'pion1) > 1+0 by NAT_1:13;
A91:  len godo >= len (go^'pion1) by TOPREAL8:7;
      then
A92:  1+1 <= len godo by A89,XXREAL_0:2;
A93:  US is_sequence_on G by JORDAN1G:4;
A94:  go/.len go = pion1/.1 by A38,A79,FINSEQ_4:27;
      then
A95:  go^'pion1 is_sequence_on G by A32,A76,TOPREAL8:12;
A96:  (go^'pion1)/.len (go^'pion1) = pion/.len pion by A80,GRAPH_2:58
        .= pion/.3 by FINSEQ_1:62
        .= do/.1 by A39,FINSEQ_4:27;
      then
A97:  godo is_sequence_on G by A35,A95,TOPREAL8:12;
      LSeg(pion1,1) c= L~pion by A78,TOPREAL3:26;
      then
A98:  LSeg(go,len go-'1) /\ LSeg(pion1,1) c={Gij} by A41,A48, XBOOLE_1:27;
      len pion1 >= 2+1 by A81,FINSEQ_1:62;
      then
A99:  len pion1 > 1+1 by NAT_1:13;
      {Gij} c= LSeg(go,m) /\ LSeg(pion1,1)
      proof
        let x be set;
        assume x in {Gij};
        then
A100:   x = Gij by TARSKI:def 1;
A101:   Gij in LSeg(go,m) by A45,TOPREAL1:6;
        Gij in LSeg(pion1,1) by A38,A94,A99,TOPREAL1:27;
        hence x in LSeg(go,m) /\ LSeg(pion1,1) by A100,A101,XBOOLE_0:def 3;
      end;
      then LSeg(go,len go-'1) /\ LSeg(pion1,1) = { go/.len go }
      by A38,A41,A98,XBOOLE_0:def 10;
      then
A102: go^'pion1 is unfolded by A94,TOPREAL8:34;
      len pion1 >= 2+1 by A81,FINSEQ_1:62;
      then
A103: len pion1-2 >= 0 by XREAL_1:21;
      len (go^'pion1)+1-1 = len go+len pion1-1 by GRAPH_2:13;
      then len (go^'pion1)-1 = len go + (len pion1-2)
        .= len go + (len pion1-'2) by A103,BINARITH:def 3;
      then
A104: len (go^'pion1)-'1 = len go + (len pion1-'2) by BINARITH:def 3;
A105: len pion1-1 >= 1 by A99,XREAL_1:21;
      then
A106: len pion1-'1 = len pion1-1 by BINARITH:def 3;
A107: len pion1-'2+1 = len pion1-2+1 by A103,BINARITH:def 3
        .= len pion1-'1 by A105,BINARITH:def 3;
      len pion1-1+1 <= len pion1;
      then
A108: len pion1-'1 < len pion1 by A106,NAT_1:13;
      LSeg(pion1,len pion1-'1) c= L~pion by A78,TOPREAL3:26;
      then
A109: LSeg(pion1,len pion1-'1) /\ LSeg(do,1) c= {Gik} by A55, XBOOLE_1:27;
      {Gik} c= LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
      proof
        let x be set;
        assume x in {Gik};
        then
A110:   x = Gik by TARSKI:def 1;
A111:   Gik in LSeg(do,1) by A52,TOPREAL1:6;
        pion1/.(len pion1-'1+1) = pion/.3 by A80,A106,FINSEQ_1:62
          .= Gik by FINSEQ_4:27;
        then Gik in LSeg(pion1,len pion1-'1) by A105,A106,TOPREAL1:27;
        hence x in LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
        by A110,A111,XBOOLE_0:def 3;
      end;
      then LSeg(pion1,len pion1-'1) /\ LSeg(do,1) = {Gik}
      by A109,XBOOLE_0:def 10;
      then
A112: LSeg(go^'pion1,len go+(len pion1-'2)) /\ LSeg(do,1) =
      {(go^'pion1)/.len (go^'pion1)} by A39,A94,A96,A107,A108,TOPREAL8:31;
A113: (go^'pion1) is non trivial by A89,REALSET1:13;
A114: rng pion1 c= L~pion1 by A99,SPPOL_2:18;
A115: {pion1/.1} c= L~go /\ L~pion1
      proof
        let x be set;
        assume x in {pion1/.1};
        then x = pion1/.1 by TARSKI:def 1;
        then x in rng go & x in rng pion1 by A94,FINSEQ_6:46,REVROT_1:3;
        hence x in L~go /\ L~pion1 by A58,A114,XBOOLE_0:def 3;
      end;
      L~go /\ L~pion1 c= {pion1/.1}
      proof
        let x be set;
        assume x in L~go /\ L~pion1;
        then x in L~go & x in L~pion1 by XBOOLE_0:def 3;
        hence x in {pion1/.1} by A3,A7,A38,A43,A78,A94,XBOOLE_0:def 3;
      end;
      then
A116: L~go /\ L~pion1 = {pion1/.1} by A115,XBOOLE_0:def 10;
      then
A117: (go^'pion1) is s.n.c. by A94,JORDAN1J:54;
      rng go /\ rng pion1 c= {pion1/.1} by A58,A114,A116,XBOOLE_1:27;
      then
A118: go^'pion1 is one-to-one by JORDAN1J:55;
A119: pion/.len pion = pion/.3 by FINSEQ_1:62
        .= do/.1 by A39,FINSEQ_4:27;
A120: {pion1/.len pion1} c= L~do /\ L~pion1
      proof
        let x be set;
        assume x in {pion1/.len pion1};
        then x = pion1/.len pion1 by TARSKI:def 1;
        then x in rng do & x in rng pion1 by A80,A119,FINSEQ_6:46,REVROT_1:3;
        hence x in L~do /\ L~pion1 by A58,A114,XBOOLE_0:def 3;
      end;
      L~do /\ L~pion1 c= {pion1/.len pion1}
      proof
        let x be set;
        assume x in L~do /\ L~pion1;
        then x in L~do & x in L~pion1 by XBOOLE_0:def 3;
        hence x in {pion1/.len pion1}
        by A4,A7,A39,A50,A78,A80,A119,XBOOLE_0:def 3;
      end;
      then
A121: L~do /\ L~pion1 = {pion1/.len pion1} by A120,XBOOLE_0:def 10;
A122: L~(go^'pion1) /\ L~do = (L~go \/ L~pion1) /\ L~do by A94, TOPREAL8:35
        .= {go/.1} \/ {do/.1} by A65,A80,A119,A121,XBOOLE_1:23
        .= {(go^'pion1)/.1} \/ {do/.1} by GRAPH_2:57
        .= {(go^'pion1)/.1,do/.1} by ENUMSET1:41;
      do/.len do = (go^'pion1)/.1 by A57,GRAPH_2:57;
      then reconsider godo as non constant standard special_circular_sequence
      by A92,A96,A97,A102,A104,A112,A113,A117,A118,A122,JORDAN8:7,8
      ,TOPREAL8:11,33,34;
A123: UA is_an_arc_of W-min C,E-max C by JORDAN6:def 8;
      then
A124: UA is connected by JORDAN6:11;
A125: W-min C in UA & E-max C in UA by A123,TOPREAL1:4;
      set ff = Rotate(Cage(C,n),Wmin);
      Wmin in rng Cage(C,n) by SPRECT_2:47;
      then
A126: ff/.1 = Wmin by FINSEQ_6:98;
A127: L~ff = L~Cage(C,n) by REVROT_1:33;
      then (W-max L~ff)..ff > 1 by A126,SPRECT_5:23;
      then (N-min L~ff)..ff > 1 by A126,A127,SPRECT_5:24,XXREAL_0:2;
      then (N-max L~ff)..ff > 1 by A126,A127,SPRECT_5:25,XXREAL_0:2;
      then
A128: Emax..ff > 1 by A126,A127,SPRECT_5:26,XXREAL_0:2;
A129: now
        assume
A130:   Gij..US <= 1;
        Gij..US >= 1 by A31,FINSEQ_4:31;
        then Gij..US = 1 by A130,XXREAL_0:1;
        then Gij = US/.1 by A31,FINSEQ_5:41;
        hence contradiction by A17,A21,JORDAN1F:5;
      end;
A131: Cage(C,n) is_sequence_on G by JORDAN9:def 1;
      then
A132: ff is_sequence_on G by REVROT_1:34;
A133: right_cell(godo,1,G)\L~godo c= RightComp godo by A92,A97,JORDAN9:29;
A134: L~godo = L~(go^'pion1) \/ L~do by A96,TOPREAL8:35
        .= L~go \/ L~pion1 \/ L~do by A94,TOPREAL8:35;
      L~Cage(C,n) = L~US \/ L~LS by JORDAN1E:17;
      then
A135: L~US c= L~Cage(C,n) & L~LS c= L~Cage(C,n) by XBOOLE_1:7;
      then
A136: L~go c=L~Cage(C,n) & L~do c=L~Cage(C,n) by A43,A50,XBOOLE_1:1;
A137: W-min C in C by SPRECT_1:15;
A138: now
        assume W-min C in L~godo;
        then
A139:   W-min C in L~go \/ L~pion1 or W-min C in L~do by A134, XBOOLE_0:def 2;
        per cases by A139,XBOOLE_0:def 2;
        suppose W-min C in L~go;
          then C meets L~Cage(C,n) by A136,A137,XBOOLE_0:3;
          hence contradiction by JORDAN10:5;
        end;
        suppose W-min C in L~pion1;
          hence contradiction by A5,A7,A78,A125,XBOOLE_0:3;
        end;
        suppose W-min C in L~do;
          then C meets L~Cage(C,n) by A136,A137,XBOOLE_0:3;
          hence contradiction by JORDAN10:5;
        end;
      end;
      right_cell(Rotate(Cage(C,n),Wmin),1) =
      right_cell(ff,1,GoB ff) by A88,JORDAN1H:29
        .= right_cell(ff,1,GoB Cage(C,n)) by REVROT_1:28
        .= right_cell(ff,1,G) by JORDAN1H:52
        .= right_cell(ff-:Emax,1,G) by A128,A132,JORDAN1J:53
        .= right_cell(US,1,G) by JORDAN1E:def 1
        .= right_cell(R_Cut(US,Gij),1,G) by A31,A93,A129,JORDAN1J:52
        .= right_cell(go^'pion1,1,G) by A36,A95,JORDAN1J:51
        .= right_cell(godo,1,G) by A90,A97,JORDAN1J:51;
      then W-min C in right_cell(godo,1,G) by JORDAN1I:8;
      then
A140: W-min C in right_cell(godo,1,G)\L~godo by A138,XBOOLE_0:def 4;
A141: godo/.1 = (go^'pion1)/.1 by GRAPH_2:57
        .= Wmin by A56,GRAPH_2:57;
A142: len US >= 2 by A16,XXREAL_0:2;
A143: godo/.2 = (go^'pion1)/.2 by A89,GRAPH_2:61
        .= US/.2 by A30,A69,GRAPH_2:61
        .= (US^'LS)/.2 by A142,GRAPH_2:61
        .= Rotate(Cage(C,n),Wmin)/.2 by JORDAN1E:15;
A144: L~go \/ L~do is compact by COMPTS_1:19;
      Wmin in L~go \/ L~do by A58,A70,XBOOLE_0:def 2;
      then
A145: W-min (L~go \/ L~do) = Wmin by A136,A144,JORDAN1J:21,XBOOLE_1: 8;
A146: (W-min (L~go \/ L~do))`1 = W-bound (L~go \/ L~do) & Wmin`1 = Wbo
      by EUCLID:56;
      Gij`1 >= Wbo by A9,A135,PSCOMP_1:71;
      then Gij`1 > Wbo by A68,REAL_1:def 5;
      then W-min (L~go\/L~do\/L~pion1) = W-min (L~go \/ L~do)
      by A86,A144,A145,A146,JORDAN1J:33;
      then
A147: W-min L~godo = Wmin by A134,A145,XBOOLE_1:4;
A148: rng godo c= L~godo by A89,A91,SPPOL_2:18,XXREAL_0:2;
      2 in dom godo by A92,FINSEQ_3:27;
      then
A149: godo/.2 in rng godo by PARTFUN2:4;
      godo/.2 in W-most L~Cage(C,n) by A143,JORDAN1I:27;
      then (godo/.2)`1 = (W-min L~godo)`1 by A147,PSCOMP_1:88
        .= W-bound L~godo by EUCLID:56;
      then godo/.2 in W-most L~godo by A148,A149,SPRECT_2:16;
      then Rotate(godo,W-min L~godo)/.2 in W-most L~godo
      by A141,A147,FINSEQ_6:95;
      then reconsider godo as clockwise_oriented non constant standard
      special_circular_sequence by JORDAN1I:27;
      len US in dom US by FINSEQ_5:6;
      then
A150: US.len US = US/.len US by PARTFUN1:def 8
        .= Emax by JORDAN1F:7;
A151: E-max C in E-most C by PSCOMP_1:111;
A152: east_halfline E-max C misses L~go
      proof
        assume east_halfline E-max C meets L~go;
        then consider p be set such that
A153:   p in east_halfline E-max C and
A154:   p in L~go by XBOOLE_0:3;
        reconsider p as Point of TOP-REAL 2 by A153;
        p in L~US by A43,A154;
        then p in east_halfline E-max C /\ L~Cage(C,n)
        by A135,A153,XBOOLE_0:def 3;
        then
A155:   p`1 = Ebo by A151,JORDAN1A:104;
        then
A156:   p = Emax by A43,A154,JORDAN1J:46;
        then Emax = Gij by A9,A150,A154,JORDAN1J:43;
        then Gij`1 = G*(len G,k)`1 by A11,A15,A155,A156,JORDAN1A:92;
        hence contradiction by A1,A12,A27,JORDAN1G:7;
      end;
      now
        assume east_halfline E-max C meets L~godo;
        then
A157:   east_halfline E-max C meets (L~go \/ L~pion1) or
        east_halfline E-max C meets L~do by A134,XBOOLE_1:70;
        per cases by A157,XBOOLE_1:70;
        suppose east_halfline E-max C meets L~go;
          hence contradiction by A152;
        end;
        suppose east_halfline E-max C meets L~pion1;
          then consider p be set such that
A158:     p in east_halfline E-max C and
A159:     p in L~pion1 by XBOOLE_0:3;
          reconsider p as Point of TOP-REAL 2 by A158;
A160:     now per cases by A7,A78,A159,XBOOLE_0:def 2;
            suppose p in poz;
              hence p`1 <= Gik`1 by A83,TOPREAL1:9;
            end;
            suppose p in pio;
              hence p`1 <= Gik`1 by A82,A83,GOBOARD7:5;
            end;
          end;
          i2+1 <= len G by A1,NAT_1:13;
          then i2 <= len G-1 by XREAL_1:21;
          then
A161:     i2 <= len G-'1 by BINARITH:def 3;
          len G-'1 <= len G by BINARITH:52;
          then Gik`1 <= G*(len G-'1,1)`1 by A6,A11,A15,A19,A161,JORDAN1A:39;
          then p`1 <= G*(len G-'1,1)`1 by A160,XXREAL_0:2;
          then p`1 <= E-bound C by A19,JORDAN8:15;
          then
A162:     p`1 <= (E-max C)`1 by EUCLID:56;
          p`1 >= (E-max C)`1 by A158,TOPREAL1:def 13;
          then
A163:     p`1 = (E-max C)`1 by A162,XXREAL_0:1;
          p`2 = (E-max C)`2 by A158,TOPREAL1:def 13;
          then p = E-max C by A163,TOPREAL3:11;
          hence contradiction by A5,A7,A78,A125,A159,XBOOLE_0:3;
        end;
        suppose east_halfline E-max C meets L~do;
          then consider p be set such that
A164:     p in east_halfline E-max C and
A165:     p in L~do by XBOOLE_0:3;
          reconsider p as Point of TOP-REAL 2 by A164;
          p in L~LS by A50,A165;
          then p in east_halfline E-max C /\ L~Cage(C,n)
          by A135,A164,XBOOLE_0:def 3;
          then
A166:     p`1 = Ebo by A151,JORDAN1A:104;
A167:     (E-max C)`2 = p`2 by A164,TOPREAL1:def 13;
          set RC = Rotate(Cage(C,n),Emax);
A168:     E-max C in right_cell(RC,1) by JORDAN1I:9;
A169:     1+1 <= len LS by A23,XXREAL_0:2;
          LS = RC-:Wmin by JORDAN1G:26;
          then
A170:     LSeg(LS,1) = LSeg(RC,1) by A169,SPPOL_2:9;
A171:     L~RC = L~Cage(C,n) by REVROT_1:33;
A172:     len RC = len Cage(C,n) by REVROT_1:14;
A173:     GoB RC = GoB Cage(C,n) by REVROT_1:28
            .= G by JORDAN1H:52;
A174:     Emax in rng Cage(C,n) by SPRECT_2:50;
A175:     RC is_sequence_on G by A131,REVROT_1:34;
A176:     RC/.1 = E-max L~RC by A171,A174,FINSEQ_6:98;
          then consider ii,jj be Element of NAT such that
A177:     [ii,jj+1] in Indices G and
A178:     [ii,jj] in Indices G and
A179:     RC/.1 = G*(ii,jj+1) and
A180:     RC/.(1+1) = G*(ii,jj) by A87,A172,A175,JORDAN1I:25;
          consider jj2 be Element of NAT such that
A181:     1 <= jj2 & jj2 <= width G and
A182:     Emax = G*(len G,jj2) by JORDAN1D:29;
A183:     len G >= 4 by JORDAN8:13;
          then len G >= 1 by XXREAL_0:2;
          then [len G,jj2] in Indices G by A181,GOBOARD7:10;
          then
A184:     ii = len G by A171,A176,A177,A179,A182,GOBOARD1:21;
A185:     1 <= ii & ii <= len G & 1 <= jj+1 & jj+1 <= width G
          by A177,GOBOARD5:1;
A186:     1 <= ii & ii <= len G & 1 <= jj & jj <= width G by A178,GOBOARD5:1;
A187:     ii+1 <> ii;
          jj+1+1 <> jj;
          then
A188:     right_cell(RC,1) = cell(G,ii-'1,jj)
          by A87,A172,A173,A177,A178,A179,A180,A187, GOBOARD5:def 6;
A189:     ii-'1+1 = ii by A185,BINARITH:53;
          ii-1 >= 4-1 by A183,A184,XREAL_1:11;
          then
A190:     ii-1 >= 1 by XXREAL_0:2;
          then
A191:     1 <= ii-'1 by BINARITH:def 3;
A192:     G*(ii-'1,jj)`2 <= p`2 & p`2 <= G*(ii-'1,jj+1)`2
          by A167,A168,A185,A186,A188,A189,A190,JORDAN9:19;
A193:     ii-'1 < len G by A185,A189,NAT_1:13;
          then
A194:     G*(ii-'1,jj)`2 = G*(1,jj)`2 by A186,A191,GOBOARD5:2
            .= G*(ii,jj)`2 by A186,GOBOARD5:2;
A195:     G*(ii-'1,jj+1)`2 = G*(1,jj+1)`2 by A185,A191,A193,GOBOARD5:2
            .= G*(ii,jj+1)`2 by A185,GOBOARD5:2;
          G*(len G,jj)`1 = Ebo & Ebo = G*(len G,jj+1)`1
          by A15,A185,A186,JORDAN1A:92;
          then p in LSeg(RC/.1,RC/.(1+1))
          by A166,A179,A180,A184,A192,A194,A195,GOBOARD7:8;
          then
A196:     p in LSeg(LS,1) by A87,A170,A172,TOPREAL1:def 5;
A197:     p in LSeg(do,Index(p,do)) by A165,JORDAN3:42;
A198:     do = mid(LS,Gik..LS,len LS) by A34,JORDAN1J:37;
A199:     1<=Gik..LS & Gik..LS<=len LS by A34,FINSEQ_4:31;
          Gik..LS <> len LS by A26,A34,FINSEQ_4:29;
          then
A200:     Gik..LS < len LS by A199,REAL_1:def 5;
A201:     1<=Index(p,do) & Index(p,do) < len do by A165,JORDAN3:41;
A202:     Index(Gik,LS)+1 = Gik..LS by A29,A34,JORDAN1J:56;
          consider t be Nat such that
A203:     t in dom LS and
A204:     LS.t = Gik by A34,FINSEQ_2:11;
A205:     1 <= t & t <= len LS by A203,FINSEQ_3:27;
          then 1 < t by A29,A204,REAL_1:def 5;
          then Index(Gik,LS)+1 = t by A204,A205,JORDAN3:45;
          then
A206:     len L_Cut(LS,Gik) = len LS-Index(Gik,LS) by A8,A204, JORDAN3:61;
          set tt = Index(p,do)+(Gik..LS)-'1;
A207:     1<=Index(Gik,LS) & 0+Index(Gik,LS) < len LS by A8,JORDAN3:41;
          then
A208:     len LS-Index(Gik,LS) > 0 by XREAL_1:22;
          Index(p,do) < len LS-'Index(Gik,LS) by A201,A206,BINARITH:def 3;
          then Index(p,do)+1 <= len LS-'Index(Gik,LS) by NAT_1:13;
          then Index(p,do) <= len LS-'Index(Gik,LS)-1 by XREAL_1:21;
          then Index(p,do) <= len LS-Index(Gik,LS)-1 by A208,BINARITH:def 3;
          then Index(p,do) <= len LS-Gik..LS by A202;
          then Index(p,do) <= len LS-'Gik..LS by BINARITH:def 3;
          then Index(p,do) < len LS-'(Gik..LS)+1 by NAT_1:13;
          then
A209:     LSeg(mid(LS,Gik..LS,len LS),Index(p,do)) =
          LSeg(LS,Index(p,do)+(Gik..LS)-'1) by A199,A200,A201,JORDAN4:31;
A210:     1+1 <= Gik..LS by A202,A207,XREAL_1:9;
          then Index(p,do)+Gik..LS >= 1+1+1 by A201,XREAL_1:9;
          then Index(p,do)+Gik..LS-1 >= 1+1+1-1 by XREAL_1:11;
          then
A211:     tt >= 1+1 by BINARITH:def 3;
A212:     2 in dom LS by A169,FINSEQ_3:27;
          now per cases by A211,REAL_1:def 5;
            suppose tt > 1+1;
              then LSeg(LS,1) misses LSeg(LS,tt) by TOPREAL1:def 9;
              hence contradiction by A196,A197,A198,A209,XBOOLE_0:3;
            end;
            suppose
A213:         tt = 1+1;
              then LSeg(LS,1) /\ LSeg(LS,tt) = {LS/.2} by A23,TOPREAL1:def 8;
              then p in {LS/.2} by A196,A197,A198,A209,XBOOLE_0:def 3;
              then
A214:         p = LS/.2 by TARSKI:def 1;
              then
A215:         p..LS = 2 by A212,FINSEQ_5:44;
              1+1 = Index(p,do)+(Gik..LS)-1 by A213,BINARITH:def 3;
              then 1+1+1 = Index(p,do)+(Gik..LS);
              then
A216:         Gik..LS = 2 by A201,A210,JORDAN1E:10;
              p in rng LS by A212,A214,PARTFUN2:4;
              then p = Gik by A34,A215,A216,FINSEQ_5:10;
              then Gik`1 = Ebo by A214,JORDAN1G:40;
              then Gik`1 = G*(len G,j)`1 by A10,A15,JORDAN1A:92;
              hence contradiction by A1,A13,A61,JORDAN1G:7;
            end;
          end;
          hence contradiction;
        end;
      end;
      then east_halfline E-max C c= (L~godo)` by SUBSET_1:43;
      then consider W be Subset of TOP-REAL 2 such that
A217: W is_a_component_of (L~godo)` and
A218: east_halfline E-max C c= W by GOBOARD9:5;
      W is not Bounded by A218,JORDAN2C:16,129;
      then W is_outside_component_of L~godo by A217,JORDAN2C:def 4;
      then W c= UBD L~godo by JORDAN2C:27;
      then
A219: east_halfline E-max C c= UBD L~godo by A218,XBOOLE_1:1;
      E-max C in east_halfline E-max C by TOPREAL1:45;
      then E-max C in UBD L~godo by A219;
      then E-max C in LeftComp godo by GOBRD14:46;
      then UA meets L~godo by A124,A125,A133,A140,JORDAN1J:36;
      then
A220: UA meets (L~go \/ L~pion1) or UA meets L~do by A134, XBOOLE_1: 70;
A221: UA c= C by JORDAN6:76;
      now per cases by A220,XBOOLE_1:70;
        suppose UA meets L~go;
          then UA meets L~Cage(C,n) by A43,A135,XBOOLE_1:1,63;
          hence contradiction by A221,JORDAN10:5,XBOOLE_1:63;
        end;
        suppose UA meets L~pion1;
          hence contradiction by A5,A7,A78;
        end;
        suppose UA meets L~do;
          then UA meets L~Cage(C,n) by A50,A135,XBOOLE_1:1,63;
          hence contradiction by A221,JORDAN10:5,XBOOLE_1:63;
        end;
      end;
      hence contradiction;
    end;
    suppose Gik`1 = Gij`1;
      then
A222: i1 = i2 by A12,A13,JORDAN1G:7;
      then poz = {Gi1k} by TOPREAL1:7;
      then poz c= pio by A74,ZFMISC_1:37;
      then pio \/ poz = pio by XBOOLE_1:12;
      hence contradiction by A1,A2,A3,A4,A5,A222,Th13;
    end;
    suppose Gik`2 = Gij`2;
      then
A223: j = k by A12,A13,JORDAN1G:6;
      then pio = {Gi1k} by TOPREAL1:7;
      then pio c= poz by A75,ZFMISC_1:37;
      then pio \/ poz = poz by XBOOLE_1:12;
      hence contradiction by A1,A2,A3,A4,A5,A223,JORDAN15:39;
    end;
  end;
  hence contradiction;
end;

theorem Th22:
  for C be Simple_closed_curve for i1,i2,j,k be Element of NAT st
  1 < i1 & i1 <= i2 & i2 < len Gauge(C,n) &
  1 <= j & j <= k & k <= width Gauge(C,n) &
  (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
  LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) /\ L~Upper_Seq(C,n) =
  {Gauge(C,n)*(i1,j)} & (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
  LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) /\ L~Lower_Seq(C,n) =
  {Gauge(C,n)*(i2,k)} holds (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
  LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) meets Lower_Arc C
proof
  let C be Simple_closed_curve;
  let i1,i2,j,k be Element of NAT;
  set G = Gauge(C,n);
  set pio = LSeg(G*(i1,j),G*(i1,k));
  set poz = LSeg(G*(i1,k),G*(i2,k));
  set US = Upper_Seq(C,n);
  set LS = Lower_Seq(C,n);
  assume that
A1: 1 < i1 & i1 <= i2 & i2 < len G and
A2: 1 <= j & j <= k & k <= width G and
A3: (pio \/ poz) /\ L~US = {G*(i1,j)} and
A4: (pio \/ poz) /\ L~LS = {G*(i2,k)} and
A5: (pio \/ poz) misses Lower_Arc C;
  set UA = Lower_Arc C;
  set Wmin = W-min L~Cage(C,n);
  set Emax = E-max L~Cage(C,n);
  set Wbo = W-bound L~Cage(C,n);
  set Ebo = E-bound L~Cage(C,n);
  set Gik = G*(i2,k);
  set Gij = G*(i1,j);
  set Gi1k = G*(i1,k);
A6: 1 < i1 & i1 < len G & 1 < i2 & i2 < len G by A1,XXREAL_0:2;
A7: L~<*Gij,Gi1k,Gik*> = poz \/ pio by TOPREAL3:23;
  Gik in {Gik} by TARSKI:def 1;
  then
A8: Gik in L~LS by A4,XBOOLE_0:def 3;
  Gij in {Gij} by TARSKI:def 1;
  then
A9: Gij in L~US by A3,XBOOLE_0:def 3;
A10: 1 <= j & j <= width G by A2,XXREAL_0:2;
A11: 1 <= k & k <= width G by A2,XXREAL_0:2;
A12: [i1,j] in Indices G by A6,A10,GOBOARD7:10;
A13: [i2,k] in Indices G by A6,A11,GOBOARD7:10;
A14: [i1,k] in Indices G by A6,A11,GOBOARD7:10;
  set go = R_Cut(US,Gij);
  set do = L_Cut(LS,Gik);
A15: len G = width G by JORDAN8:def 1;
A16: len US >= 3 by JORDAN1E:19;
  then len US >= 1 by XXREAL_0:2;
  then 1 in dom US by FINSEQ_3:27;
  then
A17: US.1 = US/.1 by PARTFUN1:def 8
    .= Wmin by JORDAN1F:5;
A18: Wmin`1 = Wbo by EUCLID:56
    .= G*(1,k)`1 by A11,A15,JORDAN1A:94;
  len G >= 4 by JORDAN8:13;
  then
A19: len G >= 1 by XXREAL_0:2;
  then
A20: [1,k] in Indices G by A11,GOBOARD7:10;
  then
A21: Gij <> US.1 by A1,A12,A17,A18,JORDAN1G:7;
  then reconsider go as being_S-Seq FinSequence of TOP-REAL 2
  by A9,JORDAN3:70;
A22: [1,j] in Indices G by A10,A19,GOBOARD7:10;
A23: len LS >= 1+2 by JORDAN1E:19;
  then len LS >= 1 by XXREAL_0:2;
  then
A24: 1 in dom LS & len LS in dom LS by FINSEQ_3:27;
  then
A25: LS.len LS = LS/.len LS by PARTFUN1:def 8
    .= Wmin by JORDAN1F:8;
  Wmin`1 = Wbo by EUCLID:56
    .= G*(1,k)`1 by A11,A15,JORDAN1A:94;
  then
A26: Gik <> LS.len LS by A1,A13,A20,A25,JORDAN1G:7;
  then reconsider do as being_S-Seq FinSequence of TOP-REAL 2
  by A8,JORDAN3:69;
A27: [len G,k] in Indices G by A11,A19,GOBOARD7:10;
A28: LS.1 = LS/.1 by A24,PARTFUN1:def 8
    .= Emax by JORDAN1F:6;
  Emax`1 = Ebo by EUCLID:56
    .= G*(len G,k)`1 by A11,A15,JORDAN1A:92;
  then
A29: Gik <> LS.1 by A1,A13,A27,A28,JORDAN1G:7;
A30: len go >= 1+1 by TOPREAL1:def 10;
A31: Gij in rng US by A6,A9,A10,JORDAN1G:4,JORDAN1J:40;
  then
A32: go is_sequence_on G by JORDAN1G:4,JORDAN1J:38;
A33: len do >= 1+1 by TOPREAL1:def 10;
A34: Gik in rng LS by A6,A8,A11,JORDAN1G:5,JORDAN1J:40;
  then
A35: do is_sequence_on G by JORDAN1G:5,JORDAN1J:39;
  reconsider go as non constant s.c.c.
  (being_S-Seq FinSequence of TOP-REAL 2) by A30,A32,JGRAPH_1:16 ,JORDAN8:8;
  reconsider do as non constant s.c.c.
  (being_S-Seq FinSequence of TOP-REAL 2) by A33,A35,JGRAPH_1:16 ,JORDAN8:8;
A36: len go > 1 by A30,NAT_1:13;
  then
A37: len go in dom go by FINSEQ_3:27;
  then
A38: go/.len go = go.len go by PARTFUN1:def 8
    .= Gij by A9,JORDAN3:59;
  len do >= 1 by A33,XXREAL_0:2;
  then 1 in dom do by FINSEQ_3:27;
  then
A39: do/.1 = do.1 by PARTFUN1:def 8
    .= Gik by A8,JORDAN3:58;
  reconsider m = len go - 1 as Element of NAT by A37,FINSEQ_3:28;
A40: m+1 = len go;
  then
A41: len go-'1 = m by BINARITH:39;
A42: LSeg(go,m) c= L~go by TOPREAL3:26;
A43: L~go c= L~US by A9,JORDAN3:76;
  then LSeg(go,m) c= L~US by A42,XBOOLE_1:1;
  then
A44: LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> c= {Gij} by A3,A7,XBOOLE_1:26;
  m >= 1 by A30,XREAL_1:21;
  then
A45: LSeg(go,m) = LSeg(go/.m,Gij) by A38,A40,TOPREAL1:def 5;
  {Gij} c= LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*>
  proof
    let x be set;
    assume x in {Gij};
    then
A46: x = Gij by TARSKI:def 1;
A47: Gij in LSeg(go,m) by A45,TOPREAL1:6;
    Gij in LSeg(Gij,Gi1k) by TOPREAL1:6;
    then Gij in LSeg(Gij,Gi1k) \/ LSeg(Gi1k,Gik) by XBOOLE_0:def 2;
    then Gij in L~<*Gij,Gi1k,Gik*> by SPRECT_1:10;
    hence x in LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> by A46,A47,XBOOLE_0:def 3;
  end;
  then
A48: LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> = {Gij} by A44,XBOOLE_0:def 10;
A49: LSeg(do,1) c= L~do by TOPREAL3:26;
A50: L~do c= L~LS by A8,JORDAN3:77;
  then LSeg(do,1) c= L~LS by A49,XBOOLE_1:1;
  then
A51: LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*> c= {Gik} by A4,A7,XBOOLE_1:26;
A52: LSeg(do,1) = LSeg(Gik,do/.(1+1)) by A33,A39,TOPREAL1:def 5;
  {Gik} c= LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*>
  proof
    let x be set;
    assume x in {Gik};
    then
A53: x = Gik by TARSKI:def 1;
A54: Gik in LSeg(do,1) by A52,TOPREAL1:6;
    Gik in LSeg(Gi1k,Gik) by TOPREAL1:6;
    then Gik in LSeg(Gij,Gi1k) \/ LSeg(Gi1k,Gik) by XBOOLE_0:def 2;
    then Gik in L~<*Gij,Gi1k,Gik*> by SPRECT_1:10;
    hence x in LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*> by A53,A54,XBOOLE_0:def 3;
  end;
  then
A55: L~<*Gij,Gi1k,Gik*> /\ LSeg(do,1) = {Gik} by A51,XBOOLE_0:def 10;
A56: go/.1 = US/.1 by A9,SPRECT_3:39
    .= Wmin by JORDAN1F:5;
  then
A57: go/.1 = LS/.len LS by JORDAN1F:8
    .= do/.len do by A8,JORDAN1J:35;
A58: rng go c= L~go & rng do c= L~do by A30,A33,SPPOL_2:18;
A59: {go/.1} c= L~go /\ L~do
  proof
    let x be set;
    assume x in {go/.1};
    then x = go/.1 by TARSKI:def 1;
    then x in rng go & x in rng do by A57,FINSEQ_6:46,REVROT_1:3;
    hence x in L~go /\ L~do by A58,XBOOLE_0:def 3;
  end;
A60: LS.1 = LS/.1 by A24,PARTFUN1:def 8
    .= Emax by JORDAN1F:6;
A61: [len G,j] in Indices G by A10,A19,GOBOARD7:10;
  L~go /\ L~do c= {go/.1}
  proof
    let x be set;
    assume x in L~go /\ L~do;
    then
A62: x in L~go & x in L~do by XBOOLE_0:def 3;
    then x in L~US /\ L~LS by A43,A50,XBOOLE_0:def 3;
    then x in {Wmin,Emax} by JORDAN1E:20;
    then
A63: x = Wmin or x = Emax by TARSKI:def 2;
    now
      assume x = Emax;
      then
A64:  Emax = Gik by A8,A60,A62,JORDAN1E:11;
      G*(len G,j)`1 = Ebo by A10,A15,JORDAN1A:92;
      then Emax`1 <> Ebo by A1,A13,A61,A64,JORDAN1G:7;
      hence contradiction by EUCLID:56;
    end;
    hence x in {go/.1} by A56,A63,TARSKI:def 1;
  end;
  then
A65: L~go /\ L~do = {go/.1} by A59,XBOOLE_0:def 10;
  set W2 = go/.2;
A66: 2 in dom go by A30,FINSEQ_3:27;
A67: Gij..US >= 1 by A31,FINSEQ_4:31;
A68: now
    assume Gij`1 = Wbo;
    then G*(1,j)`1 = G*(i1,j)`1 by A10,A15,JORDAN1A:94;
    hence contradiction by A1,A12,A22,JORDAN1G:7;
  end;
  go = mid(US,1,Gij..US) by A31,JORDAN1G:57
    .= US|(Gij..US) by A67,JORDAN3:25;
  then
A69: W2 = US/.2 by A66,FINSEQ_4:85;
A70: Wmin in rng go by A56,FINSEQ_6:46;
  set pion = <*Gij,Gi1k,Gik*>;
A71: now
    let n be Element of NAT;
    assume n in dom pion;
    then n in {1,2,3} by FINSEQ_3:1,30;
    then n = 1 or n = 2 or n = 3 by ENUMSET1:def 1;
    then pion/.n = Gij or pion/.n = Gi1k or pion/.n = Gik by FINSEQ_4:27;
    hence ex i,j be Element of NAT st [i,j] in Indices G & pion/.n = G*(i,j)
    by A12,A13,A14;
  end;
A72: Gi1k`1 = G*(i1,1)`1 by A6,A11,GOBOARD5:3
    .= Gij`1 by A6,A10,GOBOARD5:3;
  Gi1k`2 = G*(1,k)`2 by A6,A11,GOBOARD5:2
    .= Gik`2 by A6,A11,GOBOARD5:2;
  then
A73: Gi1k = |[Gij`1,Gik`2]| by A72,EUCLID:57;
A74: Gi1k in pio by TOPREAL1:6;
A75: Gi1k in poz by TOPREAL1:6;
  now per cases;
    suppose Gik`1 <> Gij`1 & Gik`2 <> Gij`2;
      then pion is_S-Seq by A73,TOPREAL3:41;
      then consider pion1 be FinSequence of TOP-REAL 2 such that
A76:  pion1 is_sequence_on G and
A77:  pion1 is_S-Seq and
A78:  L~pion = L~pion1 and
A79:  pion/.1 = pion1/.1 and
A80:  pion/.len pion = pion1/.len pion1 and
A81:  len pion <= len pion1 by A71,GOBOARD3:2;
      reconsider pion1 as being_S-Seq FinSequence of TOP-REAL 2 by A77;
      set godo = go^'pion1^'do;
A82:  Gi1k`1 = G*(i1,1)`1 by A6,A11,GOBOARD5:3
        .= Gij`1 by A6,A10,GOBOARD5:3;
A83:  Gi1k`1 <= Gik`1 by A1,A11,JORDAN1A:39;
      then
A84:  W-bound poz = Gi1k`1 by SPRECT_1:62;
A85:  W-bound pio = Gij`1 by A82,SPRECT_1:62;
      W-bound (poz \/ pio) = min(W-bound poz, W-bound pio) by SPRECT_1:54
        .= Gij`1 by A82,A84,A85;
      then
A86:  W-bound L~pion1 = Gij`1 by A78,TOPREAL3:23;
A87:  1+1 <= len Cage(C,n) by GOBOARD7:36,XXREAL_0:2;
      then
A88:  1+1 <= len Rotate(Cage(C,n),Wmin) by REVROT_1:14;
      len (go^'pion1) >= len go by TOPREAL8:7;
      then
A89:  len (go^'pion1) >= 1+1 by A30,XXREAL_0:2;
      then
A90:  len (go^'pion1) > 1+0 by NAT_1:13;
A91:  len godo >= len (go^'pion1) by TOPREAL8:7;
      then
A92:  1+1 <= len godo by A89,XXREAL_0:2;
A93:  US is_sequence_on G by JORDAN1G:4;
A94:  go/.len go = pion1/.1 by A38,A79,FINSEQ_4:27;
      then
A95:  go^'pion1 is_sequence_on G by A32,A76,TOPREAL8:12;
A96:  (go^'pion1)/.len (go^'pion1) = pion/.len pion by A80,GRAPH_2:58
        .= pion/.3 by FINSEQ_1:62
        .= do/.1 by A39,FINSEQ_4:27;
      then
A97:  godo is_sequence_on G by A35,A95,TOPREAL8:12;
      LSeg(pion1,1) c= L~pion by A78,TOPREAL3:26;
      then
A98:  LSeg(go,len go-'1) /\ LSeg(pion1,1) c={Gij} by A41,A48, XBOOLE_1:27;
      len pion1 >= 2+1 by A81,FINSEQ_1:62;
      then
A99:  len pion1 > 1+1 by NAT_1:13;
      {Gij} c= LSeg(go,m) /\ LSeg(pion1,1)
      proof
        let x be set;
        assume x in {Gij};
        then
A100:   x = Gij by TARSKI:def 1;
A101:   Gij in LSeg(go,m) by A45,TOPREAL1:6;
        Gij in LSeg(pion1,1) by A38,A94,A99,TOPREAL1:27;
        hence x in LSeg(go,m) /\ LSeg(pion1,1) by A100,A101,XBOOLE_0:def 3;
      end;
      then LSeg(go,len go-'1) /\ LSeg(pion1,1) = { go/.len go }
      by A38,A41,A98,XBOOLE_0:def 10;
      then
A102: go^'pion1 is unfolded by A94,TOPREAL8:34;
      len pion1 >= 2+1 by A81,FINSEQ_1:62;
      then
A103: len pion1-2 >= 0 by XREAL_1:21;
      len (go^'pion1)+1-1 = len go+len pion1-1 by GRAPH_2:13;
      then len (go^'pion1)-1 = len go + (len pion1-2)
        .= len go + (len pion1-'2) by A103,BINARITH:def 3;
      then
A104: len (go^'pion1)-'1 = len go + (len pion1-'2) by BINARITH:def 3;
A105: len pion1-1 >= 1 by A99,XREAL_1:21;
      then
A106: len pion1-'1 = len pion1-1 by BINARITH:def 3;
A107: len pion1-'2+1 = len pion1-2+1 by A103,BINARITH:def 3
        .= len pion1-'1 by A105,BINARITH:def 3;
      len pion1-1+1 <= len pion1;
      then
A108: len pion1-'1 < len pion1 by A106,NAT_1:13;
      LSeg(pion1,len pion1-'1) c= L~pion by A78,TOPREAL3:26;
      then
A109: LSeg(pion1,len pion1-'1) /\ LSeg(do,1) c= {Gik} by A55, XBOOLE_1:27;
      {Gik} c= LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
      proof
        let x be set;
        assume x in {Gik};
        then
A110:   x = Gik by TARSKI:def 1;
A111:   Gik in LSeg(do,1) by A52,TOPREAL1:6;
        pion1/.(len pion1-'1+1) = pion/.3 by A80,A106,FINSEQ_1:62
          .= Gik by FINSEQ_4:27;
        then Gik in LSeg(pion1,len pion1-'1) by A105,A106,TOPREAL1:27;
        hence x in LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
        by A110,A111,XBOOLE_0:def 3;
      end;
      then LSeg(pion1,len pion1-'1) /\ LSeg(do,1) = {Gik}
      by A109,XBOOLE_0:def 10;
      then
A112: LSeg(go^'pion1,len go+(len pion1-'2)) /\ LSeg(do,1) =
      {(go^'pion1)/.len (go^'pion1)} by A39,A94,A96,A107,A108,TOPREAL8:31;
A113: (go^'pion1) is non trivial by A89,REALSET1:13;
A114: rng pion1 c= L~pion1 by A99,SPPOL_2:18;
A115: {pion1/.1} c= L~go /\ L~pion1
      proof
        let x be set;
        assume x in {pion1/.1};
        then x = pion1/.1 by TARSKI:def 1;
        then x in rng go & x in rng pion1 by A94,FINSEQ_6:46,REVROT_1:3;
        hence x in L~go /\ L~pion1 by A58,A114,XBOOLE_0:def 3;
      end;
      L~go /\ L~pion1 c= {pion1/.1}
      proof
        let x be set;
        assume x in L~go /\ L~pion1;
        then x in L~go & x in L~pion1 by XBOOLE_0:def 3;
        hence x in {pion1/.1} by A3,A7,A38,A43,A78,A94,XBOOLE_0:def 3;
      end;
      then
A116: L~go /\ L~pion1 = {pion1/.1} by A115,XBOOLE_0:def 10;
      then
A117: (go^'pion1) is s.n.c. by A94,JORDAN1J:54;
      rng go /\ rng pion1 c= {pion1/.1} by A58,A114,A116,XBOOLE_1:27;
      then
A118: go^'pion1 is one-to-one by JORDAN1J:55;
A119: pion/.len pion = pion/.3 by FINSEQ_1:62
        .= do/.1 by A39,FINSEQ_4:27;
A120: {pion1/.len pion1} c= L~do /\ L~pion1
      proof
        let x be set;
        assume x in {pion1/.len pion1};
        then x = pion1/.len pion1 by TARSKI:def 1;
        then x in rng do & x in rng pion1 by A80,A119,FINSEQ_6:46,REVROT_1:3;
        hence x in L~do /\ L~pion1 by A58,A114,XBOOLE_0:def 3;
      end;
      L~do /\ L~pion1 c= {pion1/.len pion1}
      proof
        let x be set;
        assume x in L~do /\ L~pion1;
        then x in L~do & x in L~pion1 by XBOOLE_0:def 3;
        hence x in {pion1/.len pion1}
        by A4,A7,A39,A50,A78,A80,A119,XBOOLE_0:def 3;
      end;
      then
A121: L~do /\ L~pion1 = {pion1/.len pion1} by A120,XBOOLE_0:def 10;
A122: L~(go^'pion1) /\ L~do = (L~go \/ L~pion1) /\ L~do by A94, TOPREAL8:35
        .= {go/.1} \/ {do/.1} by A65,A80,A119,A121,XBOOLE_1:23
        .= {(go^'pion1)/.1} \/ {do/.1} by GRAPH_2:57
        .= {(go^'pion1)/.1,do/.1} by ENUMSET1:41;
      do/.len do = (go^'pion1)/.1 by A57,GRAPH_2:57;
      then reconsider godo as non constant standard special_circular_sequence
      by A92,A96,A97,A102,A104,A112,A113,A117,A118,A122,JORDAN8:7,8
      ,TOPREAL8:11,33,34;
A123: UA is_an_arc_of E-max C,W-min C by JORDAN6:def 9;
      then
A124: UA is connected by JORDAN6:11;
A125: W-min C in UA & E-max C in UA by A123,TOPREAL1:4;
      set ff = Rotate(Cage(C,n),Wmin);
      Wmin in rng Cage(C,n) by SPRECT_2:47;
      then
A126: ff/.1 = Wmin by FINSEQ_6:98;
A127: L~ff = L~Cage(C,n) by REVROT_1:33;
      then (W-max L~ff)..ff > 1 by A126,SPRECT_5:23;
      then (N-min L~ff)..ff > 1 by A126,A127,SPRECT_5:24,XXREAL_0:2;
      then (N-max L~ff)..ff > 1 by A126,A127,SPRECT_5:25,XXREAL_0:2;
      then
A128: Emax..ff > 1 by A126,A127,SPRECT_5:26,XXREAL_0:2;
A129: now
        assume
A130:   Gij..US <= 1;
        Gij..US >= 1 by A31,FINSEQ_4:31;
        then Gij..US = 1 by A130,XXREAL_0:1;
        then Gij = US/.1 by A31,FINSEQ_5:41;
        hence contradiction by A17,A21,JORDAN1F:5;
      end;
A131: Cage(C,n) is_sequence_on G by JORDAN9:def 1;
      then
A132: ff is_sequence_on G by REVROT_1:34;
A133: right_cell(godo,1,G)\L~godo c= RightComp godo by A92,A97,JORDAN9:29;
A134: L~godo = L~(go^'pion1) \/ L~do by A96,TOPREAL8:35
        .= L~go \/ L~pion1 \/ L~do by A94,TOPREAL8:35;
      L~Cage(C,n) = L~US \/ L~LS by JORDAN1E:17;
      then
A135: L~US c= L~Cage(C,n) & L~LS c= L~Cage(C,n) by XBOOLE_1:7;
      then
A136: L~go c=L~Cage(C,n) & L~do c=L~Cage(C,n) by A43,A50,XBOOLE_1:1;
A137: W-min C in C by SPRECT_1:15;
A138: now
        assume W-min C in L~godo;
        then
A139:   W-min C in L~go \/ L~pion1 or W-min C in L~do by A134, XBOOLE_0:def 2;
        per cases by A139,XBOOLE_0:def 2;
        suppose W-min C in L~go;
          then C meets L~Cage(C,n) by A136,A137,XBOOLE_0:3;
          hence contradiction by JORDAN10:5;
        end;
        suppose W-min C in L~pion1;
          hence contradiction by A5,A7,A78,A125,XBOOLE_0:3;
        end;
        suppose W-min C in L~do;
          then C meets L~Cage(C,n) by A136,A137,XBOOLE_0:3;
          hence contradiction by JORDAN10:5;
        end;
      end;
      right_cell(Rotate(Cage(C,n),Wmin),1) =
      right_cell(ff,1,GoB ff) by A88,JORDAN1H:29
        .= right_cell(ff,1,GoB Cage(C,n)) by REVROT_1:28
        .= right_cell(ff,1,G) by JORDAN1H:52
        .= right_cell(ff-:Emax,1,G) by A128,A132,JORDAN1J:53
        .= right_cell(US,1,G) by JORDAN1E:def 1
        .= right_cell(R_Cut(US,Gij),1,G) by A31,A93,A129,JORDAN1J:52
        .= right_cell(go^'pion1,1,G) by A36,A95,JORDAN1J:51
        .= right_cell(godo,1,G) by A90,A97,JORDAN1J:51;
      then W-min C in right_cell(godo,1,G) by JORDAN1I:8;
      then
A140: W-min C in right_cell(godo,1,G)\L~godo by A138,XBOOLE_0:def 4;
A141: godo/.1 = (go^'pion1)/.1 by GRAPH_2:57
        .= Wmin by A56,GRAPH_2:57;
A142: len US >= 2 by A16,XXREAL_0:2;
A143: godo/.2 = (go^'pion1)/.2 by A89,GRAPH_2:61
        .= US/.2 by A30,A69,GRAPH_2:61
        .= (US^'LS)/.2 by A142,GRAPH_2:61
        .= Rotate(Cage(C,n),Wmin)/.2 by JORDAN1E:15;
A144: L~go \/ L~do is compact by COMPTS_1:19;
      Wmin in L~go \/ L~do by A58,A70,XBOOLE_0:def 2;
      then
A145: W-min (L~go \/ L~do) = Wmin by A136,A144,JORDAN1J:21,XBOOLE_1: 8;
A146: (W-min (L~go \/ L~do))`1 = W-bound (L~go \/ L~do) & Wmin`1 = Wbo
      by EUCLID:56;
      Gij`1 >= Wbo by A9,A135,PSCOMP_1:71;
      then Gij`1 > Wbo by A68,REAL_1:def 5;
      then W-min (L~go\/L~do\/L~pion1) = W-min (L~go \/ L~do)
      by A86,A144,A145,A146,JORDAN1J:33;
      then
A147: W-min L~godo = Wmin by A134,A145,XBOOLE_1:4;
A148: rng godo c= L~godo by A89,A91,SPPOL_2:18,XXREAL_0:2;
      2 in dom godo by A92,FINSEQ_3:27;
      then
A149: godo/.2 in rng godo by PARTFUN2:4;
      godo/.2 in W-most L~Cage(C,n) by A143,JORDAN1I:27;
      then (godo/.2)`1 = (W-min L~godo)`1 by A147,PSCOMP_1:88
        .= W-bound L~godo by EUCLID:56;
      then godo/.2 in W-most L~godo by A148,A149,SPRECT_2:16;
      then Rotate(godo,W-min L~godo)/.2 in W-most L~godo
      by A141,A147,FINSEQ_6:95;
      then reconsider godo as clockwise_oriented non constant standard
      special_circular_sequence by JORDAN1I:27;
      len US in dom US by FINSEQ_5:6;
      then
A150: US.len US = US/.len US by PARTFUN1:def 8
        .= Emax by JORDAN1F:7;
A151: E-max C in E-most C by PSCOMP_1:111;
A152: east_halfline E-max C misses L~go
      proof
        assume east_halfline E-max C meets L~go;
        then consider p be set such that
A153:   p in east_halfline E-max C and
A154:   p in L~go by XBOOLE_0:3;
        reconsider p as Point of TOP-REAL 2 by A153;
        p in L~US by A43,A154;
        then p in east_halfline E-max C /\ L~Cage(C,n)
        by A135,A153,XBOOLE_0:def 3;
        then
A155:   p`1 = Ebo by A151,JORDAN1A:104;
        then
A156:   p = Emax by A43,A154,JORDAN1J:46;
        then Emax = Gij by A9,A150,A154,JORDAN1J:43;
        then Gij`1 = G*(len G,k)`1 by A11,A15,A155,A156,JORDAN1A:92;
        hence contradiction by A1,A12,A27,JORDAN1G:7;
      end;
      now
        assume east_halfline E-max C meets L~godo;
        then
A157:   east_halfline E-max C meets (L~go \/ L~pion1) or
        east_halfline E-max C meets L~do by A134,XBOOLE_1:70;
        per cases by A157,XBOOLE_1:70;
        suppose east_halfline E-max C meets L~go;
          hence contradiction by A152;
        end;
        suppose east_halfline E-max C meets L~pion1;
          then consider p be set such that
A158:     p in east_halfline E-max C and
A159:     p in L~pion1 by XBOOLE_0:3;
          reconsider p as Point of TOP-REAL 2 by A158;
A160:     now per cases by A7,A78,A159,XBOOLE_0:def 2;
            suppose p in poz;
              hence p`1 <= Gik`1 by A83,TOPREAL1:9;
            end;
            suppose p in pio;
              hence p`1 <= Gik`1 by A82,A83,GOBOARD7:5;
            end;
          end;
          i2+1 <= len G by A1,NAT_1:13;
          then i2 <= len G-1 by XREAL_1:21;
          then
A161:     i2 <= len G-'1 by BINARITH:def 3;
          len G-'1 <= len G by BINARITH:52;
          then Gik`1 <= G*(len G-'1,1)`1 by A6,A11,A15,A19,A161,JORDAN1A:39;
          then p`1 <= G*(len G-'1,1)`1 by A160,XXREAL_0:2;
          then p`1 <= E-bound C by A19,JORDAN8:15;
          then
A162:     p`1 <= (E-max C)`1 by EUCLID:56;
          p`1 >= (E-max C)`1 by A158,TOPREAL1:def 13;
          then
A163:     p`1 = (E-max C)`1 by A162,XXREAL_0:1;
          p`2 = (E-max C)`2 by A158,TOPREAL1:def 13;
          then p = E-max C by A163,TOPREAL3:11;
          hence contradiction by A5,A7,A78,A125,A159,XBOOLE_0:3;
        end;
        suppose east_halfline E-max C meets L~do;
          then consider p be set such that
A164:     p in east_halfline E-max C and
A165:     p in L~do by XBOOLE_0:3;
          reconsider p as Point of TOP-REAL 2 by A164;
          p in L~LS by A50,A165;
          then p in east_halfline E-max C /\ L~Cage(C,n)
          by A135,A164,XBOOLE_0:def 3;
          then
A166:     p`1 = Ebo by A151,JORDAN1A:104;
A167:     (E-max C)`2 = p`2 by A164,TOPREAL1:def 13;
          set RC = Rotate(Cage(C,n),Emax);
A168:     E-max C in right_cell(RC,1) by JORDAN1I:9;
A169:     1+1 <= len LS by A23,XXREAL_0:2;
          LS = RC-:Wmin by JORDAN1G:26;
          then
A170:     LSeg(LS,1) = LSeg(RC,1) by A169,SPPOL_2:9;
A171:     L~RC = L~Cage(C,n) by REVROT_1:33;
A172:     len RC = len Cage(C,n) by REVROT_1:14;
A173:     GoB RC = GoB Cage(C,n) by REVROT_1:28
            .= G by JORDAN1H:52;
A174:     Emax in rng Cage(C,n) by SPRECT_2:50;
A175:     RC is_sequence_on G by A131,REVROT_1:34;
A176:     RC/.1 = E-max L~RC by A171,A174,FINSEQ_6:98;
          then consider ii,jj be Element of NAT such that
A177:     [ii,jj+1] in Indices G and
A178:     [ii,jj] in Indices G and
A179:     RC/.1 = G*(ii,jj+1) and
A180:     RC/.(1+1) = G*(ii,jj) by A87,A172,A175,JORDAN1I:25;
          consider jj2 be Element of NAT such that
A181:     1 <= jj2 & jj2 <= width G and
A182:     Emax = G*(len G,jj2) by JORDAN1D:29;
A183:     len G >= 4 by JORDAN8:13;
          then len G >= 1 by XXREAL_0:2;
          then [len G,jj2] in Indices G by A181,GOBOARD7:10;
          then
A184:     ii = len G by A171,A176,A177,A179,A182,GOBOARD1:21;
A185:     1 <= ii & ii <= len G & 1 <= jj+1 & jj+1 <= width G
          by A177,GOBOARD5:1;
A186:     1 <= ii & ii <= len G & 1 <= jj & jj <= width G by A178,GOBOARD5:1;
A187:     ii+1 <> ii;
          jj+1+1 <> jj;
          then
A188:     right_cell(RC,1) = cell(G,ii-'1,jj)
          by A87,A172,A173,A177,A178,A179,A180,A187, GOBOARD5:def 6;
A189:     ii-'1+1 = ii by A185,BINARITH:53;
          ii-1 >= 4-1 by A183,A184,XREAL_1:11;
          then
A190:     ii-1 >= 1 by XXREAL_0:2;
          then
A191:     1 <= ii-'1 by BINARITH:def 3;
A192:     G*(ii-'1,jj)`2 <= p`2 & p`2 <= G*(ii-'1,jj+1)`2
          by A167,A168,A185,A186,A188,A189,A190,JORDAN9:19;
A193:     ii-'1 < len G by A185,A189,NAT_1:13;
          then
A194:     G*(ii-'1,jj)`2 = G*(1,jj)`2 by A186,A191,GOBOARD5:2
            .= G*(ii,jj)`2 by A186,GOBOARD5:2;
A195:     G*(ii-'1,jj+1)`2 = G*(1,jj+1)`2 by A185,A191,A193,GOBOARD5:2
            .= G*(ii,jj+1)`2 by A185,GOBOARD5:2;
          G*(len G,jj)`1 = Ebo & Ebo = G*(len G,jj+1)`1
          by A15,A185,A186,JORDAN1A:92;
          then p in LSeg(RC/.1,RC/.(1+1))
          by A166,A179,A180,A184,A192,A194,A195,GOBOARD7:8;
          then
A196:     p in LSeg(LS,1) by A87,A170,A172,TOPREAL1:def 5;
A197:     p in LSeg(do,Index(p,do)) by A165,JORDAN3:42;
A198:     do = mid(LS,Gik..LS,len LS) by A34,JORDAN1J:37;
A199:     1<=Gik..LS & Gik..LS<=len LS by A34,FINSEQ_4:31;
          Gik..LS <> len LS by A26,A34,FINSEQ_4:29;
          then
A200:     Gik..LS < len LS by A199,REAL_1:def 5;
A201:     1<=Index(p,do) & Index(p,do) < len do by A165,JORDAN3:41;
A202:     Index(Gik,LS)+1 = Gik..LS by A29,A34,JORDAN1J:56;
          consider t be Nat such that
A203:     t in dom LS and
A204:     LS.t = Gik by A34,FINSEQ_2:11;
A205:     1 <= t & t <= len LS by A203,FINSEQ_3:27;
          then 1 < t by A29,A204,REAL_1:def 5;
          then Index(Gik,LS)+1 = t by A204,A205,JORDAN3:45;
          then
A206:     len L_Cut(LS,Gik) = len LS-Index(Gik,LS) by A8,A204, JORDAN3:61;
          set tt = Index(p,do)+(Gik..LS)-'1;
A207:     1<=Index(Gik,LS) & 0+Index(Gik,LS) < len LS by A8,JORDAN3:41;
          then
A208:     len LS-Index(Gik,LS) > 0 by XREAL_1:22;
          Index(p,do) < len LS-'Index(Gik,LS) by A201,A206,BINARITH:def 3;
          then Index(p,do)+1 <= len LS-'Index(Gik,LS) by NAT_1:13;
          then Index(p,do) <= len LS-'Index(Gik,LS)-1 by XREAL_1:21;
          then Index(p,do) <= len LS-Index(Gik,LS)-1 by A208,BINARITH:def 3;
          then Index(p,do) <= len LS-Gik..LS by A202;
          then Index(p,do) <= len LS-'Gik..LS by BINARITH:def 3;
          then Index(p,do) < len LS-'(Gik..LS)+1 by NAT_1:13;
          then
A209:     LSeg(mid(LS,Gik..LS,len LS),Index(p,do)) =
          LSeg(LS,Index(p,do)+(Gik..LS)-'1) by A199,A200,A201,JORDAN4:31;
A210:     1+1 <= Gik..LS by A202,A207,XREAL_1:9;
          then Index(p,do)+Gik..LS >= 1+1+1 by A201,XREAL_1:9;
          then Index(p,do)+Gik..LS-1 >= 1+1+1-1 by XREAL_1:11;
          then
A211:     tt >= 1+1 by BINARITH:def 3;
A212:     2 in dom LS by A169,FINSEQ_3:27;
          now per cases by A211,REAL_1:def 5;
            suppose tt > 1+1;
              then LSeg(LS,1) misses LSeg(LS,tt) by TOPREAL1:def 9;
              hence contradiction by A196,A197,A198,A209,XBOOLE_0:3;
            end;
            suppose
A213:         tt = 1+1;
              then LSeg(LS,1) /\ LSeg(LS,tt) = {LS/.2} by A23,TOPREAL1:def 8;
              then p in {LS/.2} by A196,A197,A198,A209,XBOOLE_0:def 3;
              then
A214:         p = LS/.2 by TARSKI:def 1;
              then
A215:         p..LS = 2 by A212,FINSEQ_5:44;
              1+1 = Index(p,do)+(Gik..LS)-1 by A213,BINARITH:def 3;
              then 1+1+1 = Index(p,do)+(Gik..LS);
              then
A216:         Gik..LS = 2 by A201,A210,JORDAN1E:10;
              p in rng LS by A212,A214,PARTFUN2:4;
              then p = Gik by A34,A215,A216,FINSEQ_5:10;
              then Gik`1 = Ebo by A214,JORDAN1G:40;
              then Gik`1 = G*(len G,j)`1 by A10,A15,JORDAN1A:92;
              hence contradiction by A1,A13,A61,JORDAN1G:7;
            end;
          end;
          hence contradiction;
        end;
      end;
      then east_halfline E-max C c= (L~godo)` by SUBSET_1:43;
      then consider W be Subset of TOP-REAL 2 such that
A217: W is_a_component_of (L~godo)` and
A218: east_halfline E-max C c= W by GOBOARD9:5;
      W is not Bounded by A218,JORDAN2C:16,129;
      then W is_outside_component_of L~godo by A217,JORDAN2C:def 4;
      then W c= UBD L~godo by JORDAN2C:27;
      then
A219: east_halfline E-max C c= UBD L~godo by A218,XBOOLE_1:1;
      E-max C in east_halfline E-max C by TOPREAL1:45;
      then E-max C in UBD L~godo by A219;
      then E-max C in LeftComp godo by GOBRD14:46;
      then UA meets L~godo by A124,A125,A133,A140,JORDAN1J:36;
      then
A220: UA meets (L~go \/ L~pion1) or UA meets L~do by A134, XBOOLE_1: 70;
A221: UA c= C by JORDAN6:76;
      now per cases by A220,XBOOLE_1:70;
        suppose UA meets L~go;
          then UA meets L~Cage(C,n) by A43,A135,XBOOLE_1:1,63;
          hence contradiction by A221,JORDAN10:5,XBOOLE_1:63;
        end;
        suppose UA meets L~pion1;
          hence contradiction by A5,A7,A78;
        end;
        suppose UA meets L~do;
          then UA meets L~Cage(C,n) by A50,A135,XBOOLE_1:1,63;
          hence contradiction by A221,JORDAN10:5,XBOOLE_1:63;
        end;
      end;
      hence contradiction;
    end;
    suppose Gik`1 = Gij`1;
      then
A222: i1 = i2 by A12,A13,JORDAN1G:7;
      then poz = {Gi1k} by TOPREAL1:7;
      then poz c= pio by A74,ZFMISC_1:37;
      then pio \/ poz = pio by XBOOLE_1:12;
      hence contradiction by A1,A2,A3,A4,A5,A222,Th14;
    end;
    suppose Gik`2 = Gij`2;
      then
A223: j = k by A12,A13,JORDAN1G:6;
      then pio = {Gi1k} by TOPREAL1:7;
      then pio c= poz by A75,ZFMISC_1:37;
      then pio \/ poz = poz by XBOOLE_1:12;
      hence contradiction by A1,A2,A3,A4,A5,A223,JORDAN15:38;
    end;
  end;
  hence contradiction;
end;

theorem Th23:
  for C be Simple_closed_curve for i1,i2,j,k be Element of NAT st
  1 < i2 & i2 <= i1 & i1 < len Gauge(C,n) &
  1 <= j & j <= k & k <= width Gauge(C,n) &
  (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
  LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) /\ L~Upper_Seq(C,n) =
  {Gauge(C,n)*(i1,j)} & (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
  LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) /\ L~Lower_Seq(C,n) =
  {Gauge(C,n)*(i2,k)} holds (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
  LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) meets Upper_Arc C
proof
  let C be Simple_closed_curve;
  let i1,i2,j,k be Element of NAT;
  set G = Gauge(C,n);
  set pio = LSeg(G*(i1,j),G*(i1,k));
  set poz = LSeg(G*(i1,k),G*(i2,k));
  set US = Upper_Seq(C,n);
  set LS = Lower_Seq(C,n);
  assume that
A1: 1 < i2 & i2 <= i1 & i1 < len G and
A2: 1 <= j & j <= k & k <= width G and
A3: (pio \/ poz) /\ L~US = {G*(i1,j)} and
A4: (pio \/ poz) /\ L~LS = {G*(i2,k)} and
A5: (pio \/ poz) misses Upper_Arc C;
  set UA = Upper_Arc C;
  set Wmin = W-min L~Cage(C,n);
  set Emax = E-max L~Cage(C,n);
  set Wbo = W-bound L~Cage(C,n);
  set Ebo = E-bound L~Cage(C,n);
  set Gik = G*(i2,k);
  set Gij = G*(i1,j);
  set Gi1k = G*(i1,k);
A6: 1 < i1 & i1 < len G & 1 < i2 & i2 < len G by A1,XXREAL_0:2;
A7: L~<*Gij,Gi1k,Gik*> = poz \/ pio by TOPREAL3:23;
  Gik in {Gik} by TARSKI:def 1;
  then
A8: Gik in L~LS by A4,XBOOLE_0:def 3;
  Gij in {Gij} by TARSKI:def 1;
  then
A9: Gij in L~US by A3,XBOOLE_0:def 3;
A10: 1 <= j & j <= width G by A2,XXREAL_0:2;
A11: 1 <= k & k <= width G by A2,XXREAL_0:2;
A12: [i1,j] in Indices G by A6,A10,GOBOARD7:10;
A13: [i2,k] in Indices G by A6,A11,GOBOARD7:10;
A14: [i1,k] in Indices G by A6,A11,GOBOARD7:10;
  set go = R_Cut(US,Gij);
  set do = L_Cut(LS,Gik);
A15: len G = width G by JORDAN8:def 1;
A16: len US >= 3 by JORDAN1E:19;
  then len US >= 1 by XXREAL_0:2;
  then 1 in dom US by FINSEQ_3:27;
  then
A17: US.1 = US/.1 by PARTFUN1:def 8
    .= Wmin by JORDAN1F:5;
A18: Wmin`1 = Wbo by EUCLID:56
    .= G*(1,k)`1 by A11,A15,JORDAN1A:94;
  len G >= 4 by JORDAN8:13;
  then
A19: len G >= 1 by XXREAL_0:2;
  then
A20: [1,k] in Indices G by A11,GOBOARD7:10;
  then
A21: Gij <> US.1 by A1,A12,A17,A18,JORDAN1G:7;
  then reconsider go as being_S-Seq FinSequence of TOP-REAL 2
  by A9,JORDAN3:70;
A22: len LS >= 1+2 by JORDAN1E:19;
  then len LS >= 1 by XXREAL_0:2;
  then
A23: 1 in dom LS & len LS in dom LS by FINSEQ_3:27;
  then
A24: LS.len LS = LS/.len LS by PARTFUN1:def 8
    .= Wmin by JORDAN1F:8;
  Wmin`1 = Wbo by EUCLID:56
    .= G*(1,k)`1 by A11,A15,JORDAN1A:94;
  then
A25: Gik <> LS.len LS by A1,A13,A20,A24,JORDAN1G:7;
  then reconsider do as being_S-Seq FinSequence of TOP-REAL 2
  by A8,JORDAN3:69;
A26: [len G,k] in Indices G by A11,A19,GOBOARD7:10;
A27: LS.1 = LS/.1 by A23,PARTFUN1:def 8
    .= Emax by JORDAN1F:6;
  Emax`1 = Ebo by EUCLID:56
    .= G*(len G,k)`1 by A11,A15,JORDAN1A:92;
  then
A28: Gik <> LS.1 by A1,A13,A26,A27,JORDAN1G:7;
A29: len go >= 1+1 by TOPREAL1:def 10;
A30: Gij in rng US by A6,A9,A10,JORDAN1G:4,JORDAN1J:40;
  then
A31: go is_sequence_on G by JORDAN1G:4,JORDAN1J:38;
A32: len do >= 1+1 by TOPREAL1:def 10;
A33: Gik in rng LS by A6,A8,A11,JORDAN1G:5,JORDAN1J:40;
  then
A34: do is_sequence_on G by JORDAN1G:5,JORDAN1J:39;
  reconsider go as non constant s.c.c.
  (being_S-Seq FinSequence of TOP-REAL 2) by A29,A31,JGRAPH_1:16 ,JORDAN8:8;
  reconsider do as non constant s.c.c.
  (being_S-Seq FinSequence of TOP-REAL 2) by A32,A34,JGRAPH_1:16 ,JORDAN8:8;
A35: len go > 1 by A29,NAT_1:13;
  then
A36: len go in dom go by FINSEQ_3:27;
  then
A37: go/.len go = go.len go by PARTFUN1:def 8
    .= Gij by A9,JORDAN3:59;
  len do >= 1 by A32,XXREAL_0:2;
  then 1 in dom do by FINSEQ_3:27;
  then
A38: do/.1 = do.1 by PARTFUN1:def 8
    .= Gik by A8,JORDAN3:58;
  reconsider m = len go - 1 as Element of NAT by A36,FINSEQ_3:28;
A39: m+1 = len go;
  then
A40: len go-'1 = m by BINARITH:39;
A41: LSeg(go,m) c= L~go by TOPREAL3:26;
A42: L~go c= L~US by A9,JORDAN3:76;
  then LSeg(go,m) c= L~US by A41,XBOOLE_1:1;
  then
A43: LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> c= {Gij} by A3,A7,XBOOLE_1:26;
  m >= 1 by A29,XREAL_1:21;
  then
A44: LSeg(go,m) = LSeg(go/.m,Gij) by A37,A39,TOPREAL1:def 5;
  {Gij} c= LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*>
  proof
    let x be set;
    assume x in {Gij};
    then
A45: x = Gij by TARSKI:def 1;
A46: Gij in LSeg(go,m) by A44,TOPREAL1:6;
    Gij in LSeg(Gij,Gi1k) by TOPREAL1:6;
    then Gij in LSeg(Gij,Gi1k) \/ LSeg(Gi1k,Gik) by XBOOLE_0:def 2;
    then Gij in L~<*Gij,Gi1k,Gik*> by SPRECT_1:10;
    hence x in LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> by A45,A46,XBOOLE_0:def 3;
  end;
  then
A47: LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> = {Gij} by A43,XBOOLE_0:def 10;
A48: LSeg(do,1) c= L~do by TOPREAL3:26;
A49: L~do c= L~LS by A8,JORDAN3:77;
  then LSeg(do,1) c= L~LS by A48,XBOOLE_1:1;
  then
A50: LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*> c= {Gik} by A4,A7,XBOOLE_1:26;
A51: LSeg(do,1) = LSeg(Gik,do/.(1+1)) by A32,A38,TOPREAL1:def 5;
  {Gik} c= LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*>
  proof
    let x be set;
    assume x in {Gik};
    then
A52: x = Gik by TARSKI:def 1;
A53: Gik in LSeg(do,1) by A51,TOPREAL1:6;
    Gik in LSeg(Gi1k,Gik) by TOPREAL1:6;
    then Gik in LSeg(Gij,Gi1k) \/ LSeg(Gi1k,Gik) by XBOOLE_0:def 2;
    then Gik in L~<*Gij,Gi1k,Gik*> by SPRECT_1:10;
    hence x in LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*> by A52,A53,XBOOLE_0:def 3;
  end;
  then
A54: L~<*Gij,Gi1k,Gik*> /\ LSeg(do,1) = {Gik} by A50,XBOOLE_0:def 10;
A55: go/.1 = US/.1 by A9,SPRECT_3:39
    .= Wmin by JORDAN1F:5;
  then
A56: go/.1 = LS/.len LS by JORDAN1F:8
    .= do/.len do by A8,JORDAN1J:35;
A57: rng go c= L~go & rng do c= L~do by A29,A32,SPPOL_2:18;
A58: {go/.1} c= L~go /\ L~do
  proof
    let x be set;
    assume x in {go/.1};
    then x = go/.1 by TARSKI:def 1;
    then x in rng go & x in rng do by A56,FINSEQ_6:46,REVROT_1:3;
    hence x in L~go /\ L~do by A57,XBOOLE_0:def 3;
  end;
A59: LS.1 = LS/.1 by A23,PARTFUN1:def 8
    .= Emax by JORDAN1F:6;
A60: [len G,j] in Indices G by A10,A19,GOBOARD7:10;
  L~go /\ L~do c= {go/.1}
  proof
    let x be set;
    assume x in L~go /\ L~do;
    then
A61: x in L~go & x in L~do by XBOOLE_0:def 3;
    then x in L~US /\ L~LS by A42,A49,XBOOLE_0:def 3;
    then x in {Wmin,Emax} by JORDAN1E:20;
    then
A62: x = Wmin or x = Emax by TARSKI:def 2;
    now
      assume x = Emax;
      then
A63:  Emax = Gik by A8,A59,A61,JORDAN1E:11;
      G*(len G,j)`1 = Ebo by A10,A15,JORDAN1A:92;
      then Emax`1 <> Ebo by A1,A13,A60,A63,JORDAN1G:7;
      hence contradiction by EUCLID:56;
    end;
    hence x in {go/.1} by A55,A62,TARSKI:def 1;
  end;
  then
A64: L~go /\ L~do = {go/.1} by A58,XBOOLE_0:def 10;
  set W2 = go/.2;
A65: 2 in dom go by A29,FINSEQ_3:27;
A66: Gij..US >= 1 by A30,FINSEQ_4:31;
A67: now
    assume Gik`1 = Wbo;
    then G*(1,k)`1 = G*(i2,k)`1 by A11,A15,JORDAN1A:94;
    hence contradiction by A1,A13,A20,JORDAN1G:7;
  end;
  go = mid(US,1,Gij..US) by A30,JORDAN1G:57
    .= US|(Gij..US) by A66,JORDAN3:25;
  then
A68: W2 = US/.2 by A65,FINSEQ_4:85;
A69: Wmin in rng go by A55,FINSEQ_6:46;
  set pion = <*Gij,Gi1k,Gik*>;
A70: now
    let n be Element of NAT;
    assume n in dom pion;
    then n in {1,2,3} by FINSEQ_3:1,30;
    then n = 1 or n = 2 or n = 3 by ENUMSET1:def 1;
    then pion/.n = Gij or pion/.n = Gi1k or pion/.n = Gik by FINSEQ_4:27;
    hence ex i,j be Element of NAT st [i,j] in Indices G & pion/.n = G*(i,j)
    by A12,A13,A14;
  end;
A71: Gi1k`1 = G*(i1,1)`1 by A6,A11,GOBOARD5:3
    .= Gij`1 by A6,A10,GOBOARD5:3;
  Gi1k`2 = G*(1,k)`2 by A6,A11,GOBOARD5:2
    .= Gik`2 by A6,A11,GOBOARD5:2;
  then
A72: Gi1k = |[Gij`1,Gik`2]| by A71,EUCLID:57;
A73: Gi1k in pio by TOPREAL1:6;
A74: Gi1k in poz by TOPREAL1:6;
  now per cases;
    suppose Gik`1 <> Gij`1 & Gik`2 <> Gij`2;
      then pion is_S-Seq by A72,TOPREAL3:41;
      then consider pion1 be FinSequence of TOP-REAL 2 such that
A75:  pion1 is_sequence_on G and
A76:  pion1 is_S-Seq and
A77:  L~pion = L~pion1 and
A78:  pion/.1 = pion1/.1 and
A79:  pion/.len pion = pion1/.len pion1 and
A80:  len pion <= len pion1 by A70,GOBOARD3:2;
      reconsider pion1 as being_S-Seq FinSequence of TOP-REAL 2 by A76;
      set godo = go^'pion1^'do;
A81:  Gi1k`1 = G*(i1,1)`1 by A6,A11,GOBOARD5:3
        .= Gij`1 by A6,A10,GOBOARD5:3;
A82:  Gik`1 <= Gi1k`1 by A1,A11,JORDAN1A:39;
      then
A83:  W-bound poz = Gik`1 by SPRECT_1:62;
A84:  W-bound pio = Gij`1 by A81,SPRECT_1:62;
      W-bound (poz \/ pio) = min(W-bound poz, W-bound pio) by SPRECT_1:54
        .= Gik`1 by A81,A82,A83,A84,XXREAL_0:def 8;
      then
A85:  W-bound L~pion1 = Gik`1 by A77,TOPREAL3:23;
A86:  1+1 <= len Cage(C,n) by GOBOARD7:36,XXREAL_0:2;
      then
A87:  1+1 <= len Rotate(Cage(C,n),Wmin) by REVROT_1:14;
      len (go^'pion1) >= len go by TOPREAL8:7;
      then
A88:  len (go^'pion1) >= 1+1 by A29,XXREAL_0:2;
      then
A89:  len (go^'pion1) > 1+0 by NAT_1:13;
A90:  len godo >= len (go^'pion1) by TOPREAL8:7;
      then
A91:  1+1 <= len godo by A88,XXREAL_0:2;
A92:  US is_sequence_on G by JORDAN1G:4;
A93:  go/.len go = pion1/.1 by A37,A78,FINSEQ_4:27;
      then
A94:  go^'pion1 is_sequence_on G by A31,A75,TOPREAL8:12;
A95:  (go^'pion1)/.len (go^'pion1) = pion/.len pion by A79,GRAPH_2:58
        .= pion/.3 by FINSEQ_1:62
        .= do/.1 by A38,FINSEQ_4:27;
      then
A96:  godo is_sequence_on G by A34,A94,TOPREAL8:12;
      LSeg(pion1,1) c= L~pion by A77,TOPREAL3:26;
      then
A97:  LSeg(go,len go-'1) /\ LSeg(pion1,1) c={Gij} by A40,A47, XBOOLE_1:27;
      len pion1 >= 2+1 by A80,FINSEQ_1:62;
      then
A98:  len pion1 > 1+1 by NAT_1:13;
      {Gij} c= LSeg(go,m) /\ LSeg(pion1,1)
      proof
        let x be set;
        assume x in {Gij};
        then
A99:    x = Gij by TARSKI:def 1;
A100:   Gij in LSeg(go,m) by A44,TOPREAL1:6;
        Gij in LSeg(pion1,1) by A37,A93,A98,TOPREAL1:27;
        hence x in LSeg(go,m) /\ LSeg(pion1,1) by A99,A100,XBOOLE_0:def 3;
      end;
      then LSeg(go,len go-'1) /\ LSeg(pion1,1) = { go/.len go }
      by A37,A40,A97,XBOOLE_0:def 10;
      then
A101: go^'pion1 is unfolded by A93,TOPREAL8:34;
      len pion1 >= 2+1 by A80,FINSEQ_1:62;
      then
A102: len pion1-2 >= 0 by XREAL_1:21;
      len (go^'pion1)+1-1 = len go+len pion1-1 by GRAPH_2:13;
      then len (go^'pion1)-1 = len go + (len pion1-2)
        .= len go + (len pion1-'2) by A102,BINARITH:def 3;
      then
A103: len (go^'pion1)-'1 = len go + (len pion1-'2) by BINARITH:def 3;
A104: len pion1-1 >= 1 by A98,XREAL_1:21;
      then
A105: len pion1-'1 = len pion1-1 by BINARITH:def 3;
A106: len pion1-'2+1 = len pion1-2+1 by A102,BINARITH:def 3
        .= len pion1-'1 by A104,BINARITH:def 3;
      len pion1-1+1 <= len pion1;
      then
A107: len pion1-'1 < len pion1 by A105,NAT_1:13;
      LSeg(pion1,len pion1-'1) c= L~pion by A77,TOPREAL3:26;
      then
A108: LSeg(pion1,len pion1-'1) /\ LSeg(do,1) c= {Gik} by A54, XBOOLE_1:27;
      {Gik} c= LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
      proof
        let x be set;
        assume x in {Gik};
        then
A109:   x = Gik by TARSKI:def 1;
A110:   Gik in LSeg(do,1) by A51,TOPREAL1:6;
        pion1/.(len pion1-'1+1) = pion/.3 by A79,A105,FINSEQ_1:62
          .= Gik by FINSEQ_4:27;
        then Gik in LSeg(pion1,len pion1-'1) by A104,A105,TOPREAL1:27;
        hence x in LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
        by A109,A110,XBOOLE_0:def 3;
      end;
      then LSeg(pion1,len pion1-'1) /\ LSeg(do,1) = {Gik}
      by A108,XBOOLE_0:def 10;
      then
A111: LSeg(go^'pion1,len go+(len pion1-'2)) /\ LSeg(do,1) =
      {(go^'pion1)/.len (go^'pion1)} by A38,A93,A95,A106,A107,TOPREAL8:31;
A112: (go^'pion1) is non trivial by A88,REALSET1:13;
A113: rng pion1 c= L~pion1 by A98,SPPOL_2:18;
A114: {pion1/.1} c= L~go /\ L~pion1
      proof
        let x be set;
        assume x in {pion1/.1};
        then x = pion1/.1 by TARSKI:def 1;
        then x in rng go & x in rng pion1 by A93,FINSEQ_6:46,REVROT_1:3;
        hence x in L~go /\ L~pion1 by A57,A113,XBOOLE_0:def 3;
      end;
      L~go /\ L~pion1 c= {pion1/.1}
      proof
        let x be set;
        assume x in L~go /\ L~pion1;
        then x in L~go & x in L~pion1 by XBOOLE_0:def 3;
        hence x in {pion1/.1} by A3,A7,A37,A42,A77,A93,XBOOLE_0:def 3;
      end;
      then
A115: L~go /\ L~pion1 = {pion1/.1} by A114,XBOOLE_0:def 10;
      then
A116: (go^'pion1) is s.n.c. by A93,JORDAN1J:54;
      rng go /\ rng pion1 c= {pion1/.1} by A57,A113,A115,XBOOLE_1:27;
      then
A117: go^'pion1 is one-to-one by JORDAN1J:55;
A118: pion/.len pion = pion/.3 by FINSEQ_1:62
        .= do/.1 by A38,FINSEQ_4:27;
A119: {pion1/.len pion1} c= L~do /\ L~pion1
      proof
        let x be set;
        assume x in {pion1/.len pion1};
        then x = pion1/.len pion1 by TARSKI:def 1;
        then x in rng do & x in rng pion1 by A79,A118,FINSEQ_6:46,REVROT_1:3;
        hence x in L~do /\ L~pion1 by A57,A113,XBOOLE_0:def 3;
      end;
      L~do /\ L~pion1 c= {pion1/.len pion1}
      proof
        let x be set;
        assume x in L~do /\ L~pion1;
        then x in L~do & x in L~pion1 by XBOOLE_0:def 3;
        hence x in {pion1/.len pion1}
        by A4,A7,A38,A49,A77,A79,A118,XBOOLE_0:def 3;
      end;
      then
A120: L~do /\ L~pion1 = {pion1/.len pion1} by A119,XBOOLE_0:def 10;
A121: L~(go^'pion1) /\ L~do = (L~go \/ L~pion1) /\ L~do by A93, TOPREAL8:35
        .= {go/.1} \/ {do/.1} by A64,A79,A118,A120,XBOOLE_1:23
        .= {(go^'pion1)/.1} \/ {do/.1} by GRAPH_2:57
        .= {(go^'pion1)/.1,do/.1} by ENUMSET1:41;
      do/.len do = (go^'pion1)/.1 by A56,GRAPH_2:57;
      then reconsider godo as non constant standard special_circular_sequence
      by A91,A95,A96,A101,A103,A111,A112,A116,A117,A121,JORDAN8:7,8
      ,TOPREAL8:11,33,34;
A122: UA is_an_arc_of W-min C,E-max C by JORDAN6:def 8;
      then
A123: UA is connected by JORDAN6:11;
A124: W-min C in UA & E-max C in UA by A122,TOPREAL1:4;
      set ff = Rotate(Cage(C,n),Wmin);
      Wmin in rng Cage(C,n) by SPRECT_2:47;
      then
A125: ff/.1 = Wmin by FINSEQ_6:98;
A126: L~ff = L~Cage(C,n) by REVROT_1:33;
      then (W-max L~ff)..ff > 1 by A125,SPRECT_5:23;
      then (N-min L~ff)..ff > 1 by A125,A126,SPRECT_5:24,XXREAL_0:2;
      then (N-max L~ff)..ff > 1 by A125,A126,SPRECT_5:25,XXREAL_0:2;
      then
A127: Emax..ff > 1 by A125,A126,SPRECT_5:26,XXREAL_0:2;
A128: now
        assume
A129:   Gij..US <= 1;
        Gij..US >= 1 by A30,FINSEQ_4:31;
        then Gij..US = 1 by A129,XXREAL_0:1;
        then Gij = US/.1 by A30,FINSEQ_5:41;
        hence contradiction by A17,A21,JORDAN1F:5;
      end;
A130: Cage(C,n) is_sequence_on G by JORDAN9:def 1;
      then
A131: ff is_sequence_on G by REVROT_1:34;
A132: right_cell(godo,1,G)\L~godo c= RightComp godo by A91,A96,JORDAN9:29;
A133: L~godo = L~(go^'pion1) \/ L~do by A95,TOPREAL8:35
        .= L~go \/ L~pion1 \/ L~do by A93,TOPREAL8:35;
      L~Cage(C,n) = L~US \/ L~LS by JORDAN1E:17;
      then
A134: L~US c= L~Cage(C,n) & L~LS c= L~Cage(C,n) by XBOOLE_1:7;
      then
A135: L~go c=L~Cage(C,n) & L~do c=L~Cage(C,n) by A42,A49,XBOOLE_1:1;
A136: W-min C in C by SPRECT_1:15;
A137: now
        assume W-min C in L~godo;
        then
A138:   W-min C in L~go \/ L~pion1 or W-min C in L~do by A133, XBOOLE_0:def 2;
        per cases by A138,XBOOLE_0:def 2;
        suppose W-min C in L~go;
          then C meets L~Cage(C,n) by A135,A136,XBOOLE_0:3;
          hence contradiction by JORDAN10:5;
        end;
        suppose W-min C in L~pion1;
          hence contradiction by A5,A7,A77,A124,XBOOLE_0:3;
        end;
        suppose W-min C in L~do;
          then C meets L~Cage(C,n) by A135,A136,XBOOLE_0:3;
          hence contradiction by JORDAN10:5;
        end;
      end;
      right_cell(Rotate(Cage(C,n),Wmin),1) =
      right_cell(ff,1,GoB ff) by A87,JORDAN1H:29
        .= right_cell(ff,1,GoB Cage(C,n)) by REVROT_1:28
        .= right_cell(ff,1,G) by JORDAN1H:52
        .= right_cell(ff-:Emax,1,G) by A127,A131,JORDAN1J:53
        .= right_cell(US,1,G) by JORDAN1E:def 1
        .= right_cell(R_Cut(US,Gij),1,G) by A30,A92,A128,JORDAN1J:52
        .= right_cell(go^'pion1,1,G) by A35,A94,JORDAN1J:51
        .= right_cell(godo,1,G) by A89,A96,JORDAN1J:51;
      then W-min C in right_cell(godo,1,G) by JORDAN1I:8;
      then
A139: W-min C in right_cell(godo,1,G)\L~godo by A137,XBOOLE_0:def 4;
A140: godo/.1 = (go^'pion1)/.1 by GRAPH_2:57
        .= Wmin by A55,GRAPH_2:57;
A141: len US >= 2 by A16,XXREAL_0:2;
A142: godo/.2 = (go^'pion1)/.2 by A88,GRAPH_2:61
        .= US/.2 by A29,A68,GRAPH_2:61
        .= (US^'LS)/.2 by A141,GRAPH_2:61
        .= Rotate(Cage(C,n),Wmin)/.2 by JORDAN1E:15;
A143: L~go \/ L~do is compact by COMPTS_1:19;
      Wmin in L~go \/ L~do by A57,A69,XBOOLE_0:def 2;
      then
A144: W-min (L~go \/ L~do) = Wmin by A135,A143,JORDAN1J:21,XBOOLE_1: 8;
A145: (W-min (L~go \/ L~do))`1 = W-bound (L~go \/ L~do) & Wmin`1 = Wbo
      by EUCLID:56;
      Gik`1 >= Wbo by A8,A134,PSCOMP_1:71;
      then Gik`1 > Wbo by A67,REAL_1:def 5;
      then W-min (L~go\/L~do\/L~pion1) = W-min (L~go \/ L~do)
      by A85,A143,A144,A145,JORDAN1J:33;
      then
A146: W-min L~godo = Wmin by A133,A144,XBOOLE_1:4;
A147: rng godo c= L~godo by A88,A90,SPPOL_2:18,XXREAL_0:2;
      2 in dom godo by A91,FINSEQ_3:27;
      then
A148: godo/.2 in rng godo by PARTFUN2:4;
      godo/.2 in W-most L~Cage(C,n) by A142,JORDAN1I:27;
      then (godo/.2)`1 = (W-min L~godo)`1 by A146,PSCOMP_1:88
        .= W-bound L~godo by EUCLID:56;
      then godo/.2 in W-most L~godo by A147,A148,SPRECT_2:16;
      then Rotate(godo,W-min L~godo)/.2 in W-most L~godo
      by A140,A146,FINSEQ_6:95;
      then reconsider godo as clockwise_oriented non constant standard
      special_circular_sequence by JORDAN1I:27;
      len US in dom US by FINSEQ_5:6;
      then
A149: US.len US = US/.len US by PARTFUN1:def 8
        .= Emax by JORDAN1F:7;
A150: E-max C in E-most C by PSCOMP_1:111;
A151: east_halfline E-max C misses L~go
      proof
        assume east_halfline E-max C meets L~go;
        then consider p be set such that
A152:   p in east_halfline E-max C and
A153:   p in L~go by XBOOLE_0:3;
        reconsider p as Point of TOP-REAL 2 by A152;
        p in L~US by A42,A153;
        then p in east_halfline E-max C /\ L~Cage(C,n)
        by A134,A152,XBOOLE_0:def 3;
        then
A154:   p`1 = Ebo by A150,JORDAN1A:104;
        then
A155:   p = Emax by A42,A153,JORDAN1J:46;
        then Emax = Gij by A9,A149,A153,JORDAN1J:43;
        then Gij`1 = G*(len G,k)`1 by A11,A15,A154,A155,JORDAN1A:92;
        hence contradiction by A1,A12,A26,JORDAN1G:7;
      end;
      now
        assume east_halfline E-max C meets L~godo;
        then
A156:   east_halfline E-max C meets (L~go \/ L~pion1) or
        east_halfline E-max C meets L~do by A133,XBOOLE_1:70;
        per cases by A156,XBOOLE_1:70;
        suppose east_halfline E-max C meets L~go;
          hence contradiction by A151;
        end;
        suppose east_halfline E-max C meets L~pion1;
          then consider p be set such that
A157:     p in east_halfline E-max C and
A158:     p in L~pion1 by XBOOLE_0:3;
          reconsider p as Point of TOP-REAL 2 by A157;
A159:     now per cases by A7,A77,A158,XBOOLE_0:def 2;
            suppose p in poz;
              hence p`1 <= Gij`1 by A81,A82,TOPREAL1:9;
            end;
            suppose p in pio;
              hence p`1 <= Gij`1 by A81,GOBOARD7:5;
            end;
          end;
          i1+1 <= len G by A1,NAT_1:13;
          then i1 <= len G-1 by XREAL_1:21;
          then
A160:     i1 <= len G-'1 by BINARITH:def 3;
          len G-'1 <= len G by BINARITH:52;
          then Gij`1 <= G*(len G-'1,1)`1 by A6,A10,A15,A19,A160,JORDAN1A:39;
          then p`1 <= G*(len G-'1,1)`1 by A159,XXREAL_0:2;
          then p`1 <= E-bound C by A19,JORDAN8:15;
          then
A161:     p`1 <= (E-max C)`1 by EUCLID:56;
          p`1 >= (E-max C)`1 by A157,TOPREAL1:def 13;
          then
A162:     p`1 = (E-max C)`1 by A161,XXREAL_0:1;
          p`2 = (E-max C)`2 by A157,TOPREAL1:def 13;
          then p = E-max C by A162,TOPREAL3:11;
          hence contradiction by A5,A7,A77,A124,A158,XBOOLE_0:3;
        end;
        suppose east_halfline E-max C meets L~do;
          then consider p be set such that
A163:     p in east_halfline E-max C and
A164:     p in L~do by XBOOLE_0:3;
          reconsider p as Point of TOP-REAL 2 by A163;
          p in L~LS by A49,A164;
          then p in east_halfline E-max C /\ L~Cage(C,n)
          by A134,A163,XBOOLE_0:def 3;
          then
A165:     p`1 = Ebo by A150,JORDAN1A:104;
A166:     (E-max C)`2 = p`2 by A163,TOPREAL1:def 13;
          set RC = Rotate(Cage(C,n),Emax);
A167:     E-max C in right_cell(RC,1) by JORDAN1I:9;
A168:     1+1 <= len LS by A22,XXREAL_0:2;
          LS = RC-:Wmin by JORDAN1G:26;
          then
A169:     LSeg(LS,1) = LSeg(RC,1) by A168,SPPOL_2:9;
A170:     L~RC = L~Cage(C,n) by REVROT_1:33;
A171:     len RC = len Cage(C,n) by REVROT_1:14;
A172:     GoB RC = GoB Cage(C,n) by REVROT_1:28
            .= G by JORDAN1H:52;
A173:     Emax in rng Cage(C,n) by SPRECT_2:50;
A174:     RC is_sequence_on G by A130,REVROT_1:34;
A175:     RC/.1 = E-max L~RC by A170,A173,FINSEQ_6:98;
          then consider ii,jj be Element of NAT such that
A176:     [ii,jj+1] in Indices G and
A177:     [ii,jj] in Indices G and
A178:     RC/.1 = G*(ii,jj+1) and
A179:     RC/.(1+1) = G*(ii,jj) by A86,A171,A174,JORDAN1I:25;
          consider jj2 be Element of NAT such that
A180:     1 <= jj2 & jj2 <= width G and
A181:     Emax = G*(len G,jj2) by JORDAN1D:29;
A182:     len G >= 4 by JORDAN8:13;
          then len G >= 1 by XXREAL_0:2;
          then [len G,jj2] in Indices G by A180,GOBOARD7:10;
          then
A183:     ii = len G by A170,A175,A176,A178,A181,GOBOARD1:21;
A184:     1 <= ii & ii <= len G & 1 <= jj+1 & jj+1 <= width G
          by A176,GOBOARD5:1;
A185:     1 <= ii & ii <= len G & 1 <= jj & jj <= width G by A177,GOBOARD5:1;
A186:     ii+1 <> ii;
          jj+1+1 <> jj;
          then
A187:     right_cell(RC,1) = cell(G,ii-'1,jj)
          by A86,A171,A172,A176,A177,A178,A179,A186, GOBOARD5:def 6;
A188:     ii-'1+1 = ii by A184,BINARITH:53;
          ii-1 >= 4-1 by A182,A183,XREAL_1:11;
          then
A189:     ii-1 >= 1 by XXREAL_0:2;
          then
A190:     1 <= ii-'1 by BINARITH:def 3;
A191:     G*(ii-'1,jj)`2 <= p`2 & p`2 <= G*(ii-'1,jj+1)`2
          by A166,A167,A184,A185,A187,A188,A189,JORDAN9:19;
A192:     ii-'1 < len G by A184,A188,NAT_1:13;
          then
A193:     G*(ii-'1,jj)`2 = G*(1,jj)`2 by A185,A190,GOBOARD5:2
            .= G*(ii,jj)`2 by A185,GOBOARD5:2;
A194:     G*(ii-'1,jj+1)`2 = G*(1,jj+1)`2 by A184,A190,A192,GOBOARD5:2
            .= G*(ii,jj+1)`2 by A184,GOBOARD5:2;
          G*(len G,jj)`1 = Ebo & Ebo = G*(len G,jj+1)`1
          by A15,A184,A185,JORDAN1A:92;
          then p in LSeg(RC/.1,RC/.(1+1))
          by A165,A178,A179,A183,A191,A193,A194,GOBOARD7:8;
          then
A195:     p in LSeg(LS,1) by A86,A169,A171,TOPREAL1:def 5;
A196:     p in LSeg(do,Index(p,do)) by A164,JORDAN3:42;
A197:     do = mid(LS,Gik..LS,len LS) by A33,JORDAN1J:37;
A198:     1<=Gik..LS & Gik..LS<=len LS by A33,FINSEQ_4:31;
          Gik..LS <> len LS by A25,A33,FINSEQ_4:29;
          then
A199:     Gik..LS < len LS by A198,REAL_1:def 5;
A200:     1<=Index(p,do) & Index(p,do) < len do by A164,JORDAN3:41;
A201:     Index(Gik,LS)+1 = Gik..LS by A28,A33,JORDAN1J:56;
          consider t be Nat such that
A202:     t in dom LS and
A203:     LS.t = Gik by A33,FINSEQ_2:11;
A204:     1 <= t & t <= len LS by A202,FINSEQ_3:27;
          then 1 < t by A28,A203,REAL_1:def 5;
          then Index(Gik,LS)+1 = t by A203,A204,JORDAN3:45;
          then
A205:     len L_Cut(LS,Gik) = len LS-Index(Gik,LS) by A8,A203, JORDAN3:61;
          set tt = Index(p,do)+(Gik..LS)-'1;
A206:     1<=Index(Gik,LS) & 0+Index(Gik,LS) < len LS by A8,JORDAN3:41;
          then
A207:     len LS-Index(Gik,LS) > 0 by XREAL_1:22;
          Index(p,do) < len LS-'Index(Gik,LS) by A200,A205,BINARITH:def 3;
          then Index(p,do)+1 <= len LS-'Index(Gik,LS) by NAT_1:13;
          then Index(p,do) <= len LS-'Index(Gik,LS)-1 by XREAL_1:21;
          then Index(p,do) <= len LS-Index(Gik,LS)-1 by A207,BINARITH:def 3;
          then Index(p,do) <= len LS-Gik..LS by A201;
          then Index(p,do) <= len LS-'Gik..LS by BINARITH:def 3;
          then Index(p,do) < len LS-'(Gik..LS)+1 by NAT_1:13;
          then
A208:     LSeg(mid(LS,Gik..LS,len LS),Index(p,do)) =
          LSeg(LS,Index(p,do)+(Gik..LS)-'1) by A198,A199,A200,JORDAN4:31;
A209:     1+1 <= Gik..LS by A201,A206,XREAL_1:9;
          then Index(p,do)+Gik..LS >= 1+1+1 by A200,XREAL_1:9;
          then Index(p,do)+Gik..LS-1 >= 1+1+1-1 by XREAL_1:11;
          then
A210:     tt >= 1+1 by BINARITH:def 3;
A211:     2 in dom LS by A168,FINSEQ_3:27;
          now per cases by A210,REAL_1:def 5;
            suppose tt > 1+1;
              then LSeg(LS,1) misses LSeg(LS,tt) by TOPREAL1:def 9;
              hence contradiction by A195,A196,A197,A208,XBOOLE_0:3;
            end;
            suppose
A212:         tt = 1+1;
              then LSeg(LS,1) /\ LSeg(LS,tt) = {LS/.2} by A22,TOPREAL1:def 8;
              then p in {LS/.2} by A195,A196,A197,A208,XBOOLE_0:def 3;
              then
A213:         p = LS/.2 by TARSKI:def 1;
              then
A214:         p..LS = 2 by A211,FINSEQ_5:44;
              1+1 = Index(p,do)+(Gik..LS)-1 by A212,BINARITH:def 3;
              then 1+1+1 = Index(p,do)+(Gik..LS);
              then
A215:         Gik..LS = 2 by A200,A209,JORDAN1E:10;
              p in rng LS by A211,A213,PARTFUN2:4;
              then p = Gik by A33,A214,A215,FINSEQ_5:10;
              then Gik`1 = Ebo by A213,JORDAN1G:40;
              then Gik`1 = G*(len G,j)`1 by A10,A15,JORDAN1A:92;
              hence contradiction by A1,A13,A60,JORDAN1G:7;
            end;
          end;
          hence contradiction;
        end;
      end;
      then east_halfline E-max C c= (L~godo)` by SUBSET_1:43;
      then consider W be Subset of TOP-REAL 2 such that
A216: W is_a_component_of (L~godo)` and
A217: east_halfline E-max C c= W by GOBOARD9:5;
      W is not Bounded by A217,JORDAN2C:16,129;
      then W is_outside_component_of L~godo by A216,JORDAN2C:def 4;
      then W c= UBD L~godo by JORDAN2C:27;
      then
A218: east_halfline E-max C c= UBD L~godo by A217,XBOOLE_1:1;
      E-max C in east_halfline E-max C by TOPREAL1:45;
      then E-max C in UBD L~godo by A218;
      then E-max C in LeftComp godo by GOBRD14:46;
      then UA meets L~godo by A123,A124,A132,A139,JORDAN1J:36;
      then
A219: UA meets (L~go \/ L~pion1) or UA meets L~do by A133, XBOOLE_1: 70;
A220: UA c= C by JORDAN6:76;
      now per cases by A219,XBOOLE_1:70;
        suppose UA meets L~go;
          then UA meets L~Cage(C,n) by A42,A134,XBOOLE_1:1,63;
          hence contradiction by A220,JORDAN10:5,XBOOLE_1:63;
        end;
        suppose UA meets L~pion1;
          hence contradiction by A5,A7,A77;
        end;
        suppose UA meets L~do;
          then UA meets L~Cage(C,n) by A49,A134,XBOOLE_1:1,63;
          hence contradiction by A220,JORDAN10:5,XBOOLE_1:63;
        end;
      end;
      hence contradiction;
    end;
    suppose Gik`1 = Gij`1;
      then
A221: i1 = i2 by A12,A13,JORDAN1G:7;
      then poz = {Gi1k} by TOPREAL1:7;
      then poz c= pio by A73,ZFMISC_1:37;
      then pio \/ poz = pio by XBOOLE_1:12;
      hence contradiction by A1,A2,A3,A4,A5,A221,Th13;
    end;
    suppose Gik`2 = Gij`2;
      then
A222: j = k by A12,A13,JORDAN1G:6;
      then pio = {Gi1k} by TOPREAL1:7;
      then pio c= poz by A74,ZFMISC_1:37;
      then pio \/ poz = poz by XBOOLE_1:12;
      hence contradiction by A1,A2,A3,A4,A5,A222,JORDAN15:31;
    end;
  end;
  hence contradiction;
end;

theorem Th24:
  for C be Simple_closed_curve for i1,i2,j,k be Element of NAT st
  1 < i2 & i2 <= i1 & i1 < len Gauge(C,n) &
  1 <= j & j <= k & k <= width Gauge(C,n) &
  (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
  LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) /\ L~Upper_Seq(C,n) =
  {Gauge(C,n)*(i1,j)} & (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
  LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) /\ L~Lower_Seq(C,n) =
  {Gauge(C,n)*(i2,k)} holds (LSeg(Gauge(C,n)*(i1,j),Gauge(C,n)*(i1,k)) \/
  LSeg(Gauge(C,n)*(i1,k),Gauge(C,n)*(i2,k))) meets Lower_Arc C
proof
  let C be Simple_closed_curve;
  let i1,i2,j,k be Element of NAT;
  set G = Gauge(C,n);
  set pio = LSeg(G*(i1,j),G*(i1,k));
  set poz = LSeg(G*(i1,k),G*(i2,k));
  set US = Upper_Seq(C,n);
  set LS = Lower_Seq(C,n);
  assume that
A1: 1 < i2 & i2 <= i1 & i1 < len G and
A2: 1 <= j & j <= k & k <= width G and
A3: (pio \/ poz) /\ L~US = {G*(i1,j)} and
A4: (pio \/ poz) /\ L~LS = {G*(i2,k)} and
A5: (pio \/ poz) misses Lower_Arc C;
  set UA = Lower_Arc C;
  set Wmin = W-min L~Cage(C,n);
  set Emax = E-max L~Cage(C,n);
  set Wbo = W-bound L~Cage(C,n);
  set Ebo = E-bound L~Cage(C,n);
  set Gik = G*(i2,k);
  set Gij = G*(i1,j);
  set Gi1k = G*(i1,k);
A6: 1 < i1 & i1 < len G & 1 < i2 & i2 < len G by A1,XXREAL_0:2;
A7: L~<*Gij,Gi1k,Gik*> = poz \/ pio by TOPREAL3:23;
  Gik in {Gik} by TARSKI:def 1;
  then
A8: Gik in L~LS by A4,XBOOLE_0:def 3;
  Gij in {Gij} by TARSKI:def 1;
  then
A9: Gij in L~US by A3,XBOOLE_0:def 3;
A10: 1 <= j & j <= width G by A2,XXREAL_0:2;
A11: 1 <= k & k <= width G by A2,XXREAL_0:2;
A12: [i1,j] in Indices G by A6,A10,GOBOARD7:10;
A13: [i2,k] in Indices G by A6,A11,GOBOARD7:10;
A14: [i1,k] in Indices G by A6,A11,GOBOARD7:10;
  set go = R_Cut(US,Gij);
  set do = L_Cut(LS,Gik);
A15: len G = width G by JORDAN8:def 1;
A16: len US >= 3 by JORDAN1E:19;
  then len US >= 1 by XXREAL_0:2;
  then 1 in dom US by FINSEQ_3:27;
  then
A17: US.1 = US/.1 by PARTFUN1:def 8
    .= Wmin by JORDAN1F:5;
A18: Wmin`1 = Wbo by EUCLID:56
    .= G*(1,k)`1 by A11,A15,JORDAN1A:94;
  len G >= 4 by JORDAN8:13;
  then
A19: len G >= 1 by XXREAL_0:2;
  then
A20: [1,k] in Indices G by A11,GOBOARD7:10;
  then
A21: Gij <> US.1 by A1,A12,A17,A18,JORDAN1G:7;
  then reconsider go as being_S-Seq FinSequence of TOP-REAL 2
  by A9,JORDAN3:70;
A22: len LS >= 1+2 by JORDAN1E:19;
  then len LS >= 1 by XXREAL_0:2;
  then
A23: 1 in dom LS & len LS in dom LS by FINSEQ_3:27;
  then
A24: LS.len LS = LS/.len LS by PARTFUN1:def 8
    .= Wmin by JORDAN1F:8;
  Wmin`1 = Wbo by EUCLID:56
    .= G*(1,k)`1 by A11,A15,JORDAN1A:94;
  then
A25: Gik <> LS.len LS by A1,A13,A20,A24,JORDAN1G:7;
  then reconsider do as being_S-Seq FinSequence of TOP-REAL 2
  by A8,JORDAN3:69;
A26: [len G,k] in Indices G by A11,A19,GOBOARD7:10;
A27: LS.1 = LS/.1 by A23,PARTFUN1:def 8
    .= Emax by JORDAN1F:6;
  Emax`1 = Ebo by EUCLID:56
    .= G*(len G,k)`1 by A11,A15,JORDAN1A:92;
  then
A28: Gik <> LS.1 by A1,A13,A26,A27,JORDAN1G:7;
A29: len go >= 1+1 by TOPREAL1:def 10;
A30: Gij in rng US by A6,A9,A10,JORDAN1G:4,JORDAN1J:40;
  then
A31: go is_sequence_on G by JORDAN1G:4,JORDAN1J:38;
A32: len do >= 1+1 by TOPREAL1:def 10;
A33: Gik in rng LS by A6,A8,A11,JORDAN1G:5,JORDAN1J:40;
  then
A34: do is_sequence_on G by JORDAN1G:5,JORDAN1J:39;
  reconsider go as non constant s.c.c.
  (being_S-Seq FinSequence of TOP-REAL 2) by A29,A31,JGRAPH_1:16 ,JORDAN8:8;
  reconsider do as non constant s.c.c.
  (being_S-Seq FinSequence of TOP-REAL 2) by A32,A34,JGRAPH_1:16 ,JORDAN8:8;
A35: len go > 1 by A29,NAT_1:13;
  then
A36: len go in dom go by FINSEQ_3:27;
  then
A37: go/.len go = go.len go by PARTFUN1:def 8
    .= Gij by A9,JORDAN3:59;
  len do >= 1 by A32,XXREAL_0:2;
  then 1 in dom do by FINSEQ_3:27;
  then
A38: do/.1 = do.1 by PARTFUN1:def 8
    .= Gik by A8,JORDAN3:58;
  reconsider m = len go - 1 as Element of NAT by A36,FINSEQ_3:28;
A39: m+1 = len go;
  then
A40: len go-'1 = m by BINARITH:39;
A41: LSeg(go,m) c= L~go by TOPREAL3:26;
A42: L~go c= L~US by A9,JORDAN3:76;
  then LSeg(go,m) c= L~US by A41,XBOOLE_1:1;
  then
A43: LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> c= {Gij} by A3,A7,XBOOLE_1:26;
  m >= 1 by A29,XREAL_1:21;
  then
A44: LSeg(go,m) = LSeg(go/.m,Gij) by A37,A39,TOPREAL1:def 5;
  {Gij} c= LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*>
  proof
    let x be set;
    assume x in {Gij};
    then
A45: x = Gij by TARSKI:def 1;
A46: Gij in LSeg(go,m) by A44,TOPREAL1:6;
    Gij in LSeg(Gij,Gi1k) by TOPREAL1:6;
    then Gij in LSeg(Gij,Gi1k) \/ LSeg(Gi1k,Gik) by XBOOLE_0:def 2;
    then Gij in L~<*Gij,Gi1k,Gik*> by SPRECT_1:10;
    hence x in LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> by A45,A46,XBOOLE_0:def 3;
  end;
  then
A47: LSeg(go,m) /\ L~<*Gij,Gi1k,Gik*> = {Gij} by A43,XBOOLE_0:def 10;
A48: LSeg(do,1) c= L~do by TOPREAL3:26;
A49: L~do c= L~LS by A8,JORDAN3:77;
  then LSeg(do,1) c= L~LS by A48,XBOOLE_1:1;
  then
A50: LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*> c= {Gik} by A4,A7,XBOOLE_1:26;
A51: LSeg(do,1) = LSeg(Gik,do/.(1+1)) by A32,A38,TOPREAL1:def 5;
  {Gik} c= LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*>
  proof
    let x be set;
    assume x in {Gik};
    then
A52: x = Gik by TARSKI:def 1;
A53: Gik in LSeg(do,1) by A51,TOPREAL1:6;
    Gik in LSeg(Gi1k,Gik) by TOPREAL1:6;
    then Gik in LSeg(Gij,Gi1k) \/ LSeg(Gi1k,Gik) by XBOOLE_0:def 2;
    then Gik in L~<*Gij,Gi1k,Gik*> by SPRECT_1:10;
    hence x in LSeg(do,1) /\ L~<*Gij,Gi1k,Gik*> by A52,A53,XBOOLE_0:def 3;
  end;
  then
A54: L~<*Gij,Gi1k,Gik*> /\ LSeg(do,1) = {Gik} by A50,XBOOLE_0:def 10;
A55: go/.1 = US/.1 by A9,SPRECT_3:39
    .= Wmin by JORDAN1F:5;
  then
A56: go/.1 = LS/.len LS by JORDAN1F:8
    .= do/.len do by A8,JORDAN1J:35;
A57: rng go c= L~go & rng do c= L~do by A29,A32,SPPOL_2:18;
A58: {go/.1} c= L~go /\ L~do
  proof
    let x be set;
    assume x in {go/.1};
    then x = go/.1 by TARSKI:def 1;
    then x in rng go & x in rng do by A56,FINSEQ_6:46,REVROT_1:3;
    hence x in L~go /\ L~do by A57,XBOOLE_0:def 3;
  end;
A59: LS.1 = LS/.1 by A23,PARTFUN1:def 8
    .= Emax by JORDAN1F:6;
A60: [len G,j] in Indices G by A10,A19,GOBOARD7:10;
  L~go /\ L~do c= {go/.1}
  proof
    let x be set;
    assume x in L~go /\ L~do;
    then
A61: x in L~go & x in L~do by XBOOLE_0:def 3;
    then x in L~US /\ L~LS by A42,A49,XBOOLE_0:def 3;
    then x in {Wmin,Emax} by JORDAN1E:20;
    then
A62: x = Wmin or x = Emax by TARSKI:def 2;
    now
      assume x = Emax;
      then
A63:  Emax = Gik by A8,A59,A61,JORDAN1E:11;
      G*(len G,j)`1 = Ebo by A10,A15,JORDAN1A:92;
      then Emax`1 <> Ebo by A1,A13,A60,A63,JORDAN1G:7;
      hence contradiction by EUCLID:56;
    end;
    hence x in {go/.1} by A55,A62,TARSKI:def 1;
  end;
  then
A64: L~go /\ L~do = {go/.1} by A58,XBOOLE_0:def 10;
  set W2 = go/.2;
A65: 2 in dom go by A29,FINSEQ_3:27;
A66: Gij..US >= 1 by A30,FINSEQ_4:31;
A67: now
    assume Gik`1 = Wbo;
    then G*(1,k)`1 = G*(i2,k)`1 by A11,A15,JORDAN1A:94;
    hence contradiction by A1,A13,A20,JORDAN1G:7;
  end;
  go = mid(US,1,Gij..US) by A30,JORDAN1G:57
    .= US|(Gij..US) by A66,JORDAN3:25;
  then
A68: W2 = US/.2 by A65,FINSEQ_4:85;
A69: Wmin in rng go by A55,FINSEQ_6:46;
  set pion = <*Gij,Gi1k,Gik*>;
A70: now
    let n be Element of NAT;
    assume n in dom pion;
    then n in {1,2,3} by FINSEQ_3:1,30;
    then n = 1 or n = 2 or n = 3 by ENUMSET1:def 1;
    then pion/.n = Gij or pion/.n = Gi1k or pion/.n = Gik by FINSEQ_4:27;
    hence ex i,j be Element of NAT st [i,j] in Indices G & pion/.n = G*(i,j)
    by A12,A13,A14;
  end;
A71: Gi1k`1 = G*(i1,1)`1 by A6,A11,GOBOARD5:3
    .= Gij`1 by A6,A10,GOBOARD5:3;
  Gi1k`2 = G*(1,k)`2 by A6,A11,GOBOARD5:2
    .= Gik`2 by A6,A11,GOBOARD5:2;
  then
A72: Gi1k = |[Gij`1,Gik`2]| by A71,EUCLID:57;
A73: Gi1k in pio by TOPREAL1:6;
A74: Gi1k in poz by TOPREAL1:6;
  now per cases;
    suppose Gik`1 <> Gij`1 & Gik`2 <> Gij`2;
      then pion is_S-Seq by A72,TOPREAL3:41;
      then consider pion1 be FinSequence of TOP-REAL 2 such that
A75:  pion1 is_sequence_on G and
A76:  pion1 is_S-Seq and
A77:  L~pion = L~pion1 and
A78:  pion/.1 = pion1/.1 and
A79:  pion/.len pion = pion1/.len pion1 and
A80:  len pion <= len pion1 by A70,GOBOARD3:2;
      reconsider pion1 as being_S-Seq FinSequence of TOP-REAL 2 by A76;
      set godo = go^'pion1^'do;
A81:  Gi1k`1 = G*(i1,1)`1 by A6,A11,GOBOARD5:3
        .= Gij`1 by A6,A10,GOBOARD5:3;
A82:  Gik`1 <= Gi1k`1 by A1,A11,JORDAN1A:39;
      then
A83:  W-bound poz = Gik`1 by SPRECT_1:62;
A84:  W-bound pio = Gij`1 by A81,SPRECT_1:62;
      W-bound (poz \/ pio) = min(W-bound poz, W-bound pio) by SPRECT_1:54
        .= Gik`1 by A81,A82,A83,A84,XXREAL_0:def 8;
      then
A85:  W-bound L~pion1 = Gik`1 by A77,TOPREAL3:23;
A86:  1+1 <= len Cage(C,n) by GOBOARD7:36,XXREAL_0:2;
      then
A87:  1+1 <= len Rotate(Cage(C,n),Wmin) by REVROT_1:14;
      len (go^'pion1) >= len go by TOPREAL8:7;
      then
A88:  len (go^'pion1) >= 1+1 by A29,XXREAL_0:2;
      then
A89:  len (go^'pion1) > 1+0 by NAT_1:13;
A90:  len godo >= len (go^'pion1) by TOPREAL8:7;
      then
A91:  1+1 <= len godo by A88,XXREAL_0:2;
A92:  US is_sequence_on G by JORDAN1G:4;
A93:  go/.len go = pion1/.1 by A37,A78,FINSEQ_4:27;
      then
A94:  go^'pion1 is_sequence_on G by A31,A75,TOPREAL8:12;
A95:  (go^'pion1)/.len (go^'pion1) = pion/.len pion by A79,GRAPH_2:58
        .= pion/.3 by FINSEQ_1:62
        .= do/.1 by A38,FINSEQ_4:27;
      then
A96:  godo is_sequence_on G by A34,A94,TOPREAL8:12;
      LSeg(pion1,1) c= L~pion by A77,TOPREAL3:26;
      then
A97:  LSeg(go,len go-'1) /\ LSeg(pion1,1) c={Gij} by A40,A47, XBOOLE_1:27;
      len pion1 >= 2+1 by A80,FINSEQ_1:62;
      then
A98:  len pion1 > 1+1 by NAT_1:13;
      {Gij} c= LSeg(go,m) /\ LSeg(pion1,1)
      proof
        let x be set;
        assume x in {Gij};
        then
A99:    x = Gij by TARSKI:def 1;
A100:   Gij in LSeg(go,m) by A44,TOPREAL1:6;
        Gij in LSeg(pion1,1) by A37,A93,A98,TOPREAL1:27;
        hence x in LSeg(go,m) /\ LSeg(pion1,1) by A99,A100,XBOOLE_0:def 3;
      end;
      then LSeg(go,len go-'1) /\ LSeg(pion1,1) = { go/.len go }
      by A37,A40,A97,XBOOLE_0:def 10;
      then
A101: go^'pion1 is unfolded by A93,TOPREAL8:34;
      len pion1 >= 2+1 by A80,FINSEQ_1:62;
      then
A102: len pion1-2 >= 0 by XREAL_1:21;
      len (go^'pion1)+1-1 = len go+len pion1-1 by GRAPH_2:13;
      then len (go^'pion1)-1 = len go + (len pion1-2)
        .= len go + (len pion1-'2) by A102,BINARITH:def 3;
      then
A103: len (go^'pion1)-'1 = len go + (len pion1-'2) by BINARITH:def 3;
A104: len pion1-1 >= 1 by A98,XREAL_1:21;
      then
A105: len pion1-'1 = len pion1-1 by BINARITH:def 3;
A106: len pion1-'2+1 = len pion1-2+1 by A102,BINARITH:def 3
        .= len pion1-'1 by A104,BINARITH:def 3;
      len pion1-1+1 <= len pion1;
      then
A107: len pion1-'1 < len pion1 by A105,NAT_1:13;
      LSeg(pion1,len pion1-'1) c= L~pion by A77,TOPREAL3:26;
      then
A108: LSeg(pion1,len pion1-'1) /\ LSeg(do,1) c= {Gik} by A54, XBOOLE_1:27;
      {Gik} c= LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
      proof
        let x be set;
        assume x in {Gik};
        then
A109:   x = Gik by TARSKI:def 1;
A110:   Gik in LSeg(do,1) by A51,TOPREAL1:6;
        pion1/.(len pion1-'1+1) = pion/.3 by A79,A105,FINSEQ_1:62
          .= Gik by FINSEQ_4:27;
        then Gik in LSeg(pion1,len pion1-'1) by A104,A105,TOPREAL1:27;
        hence x in LSeg(pion1,len pion1-'1) /\ LSeg(do,1)
        by A109,A110,XBOOLE_0:def 3;
      end;
      then LSeg(pion1,len pion1-'1) /\ LSeg(do,1) = {Gik}
      by A108,XBOOLE_0:def 10;
      then
A111: LSeg(go^'pion1,len go+(len pion1-'2)) /\ LSeg(do,1) =
      {(go^'pion1)/.len (go^'pion1)} by A38,A93,A95,A106,A107,TOPREAL8:31;
A112: (go^'pion1) is non trivial by A88,REALSET1:13;
A113: rng pion1 c= L~pion1 by A98,SPPOL_2:18;
A114: {pion1/.1} c= L~go /\ L~pion1
      proof
        let x be set;
        assume x in {pion1/.1};
        then x = pion1/.1 by TARSKI:def 1;
        then x in rng go & x in rng pion1 by A93,FINSEQ_6:46,REVROT_1:3;
        hence x in L~go /\ L~pion1 by A57,A113,XBOOLE_0:def 3;
      end;
      L~go /\ L~pion1 c= {pion1/.1}
      proof
        let x be set;
        assume x in L~go /\ L~pion1;
        then x in L~go & x in L~pion1 by XBOOLE_0:def 3;
        hence x in {pion1/.1} by A3,A7,A37,A42,A77,A93,XBOOLE_0:def 3;
      end;
      then
A115: L~go /\ L~pion1 = {pion1/.1} by A114,XBOOLE_0:def 10;
      then
A116: (go^'pion1) is s.n.c. by A93,JORDAN1J:54;
      rng go /\ rng pion1 c= {pion1/.1} by A57,A113,A115,XBOOLE_1:27;
      then
A117: go^'pion1 is one-to-one by JORDAN1J:55;
A118: pion/.len pion = pion/.3 by FINSEQ_1:62
        .= do/.1 by A38,FINSEQ_4:27;
A119: {pion1/.len pion1} c= L~do /\ L~pion1
      proof
        let x be set;
        assume x in {pion1/.len pion1};
        then x = pion1/.len pion1 by TARSKI:def 1;
        then x in rng do & x in rng pion1 by A79,A118,FINSEQ_6:46,REVROT_1:3;
        hence x in L~do /\ L~pion1 by A57,A113,XBOOLE_0:def 3;
      end;
      L~do /\ L~pion1 c= {pion1/.len pion1}
      proof
        let x be set;
        assume x in L~do /\ L~pion1;
        then x in L~do & x in L~pion1 by XBOOLE_0:def 3;
        hence x in {pion1/.len pion1}
        by A4,A7,A38,A49,A77,A79,A118,XBOOLE_0:def 3;
      end;
      then
A120: L~do /\ L~pion1 = {pion1/.len pion1} by A119,XBOOLE_0:def 10;
A121: L~(go^'pion1) /\ L~do = (L~go \/ L~pion1) /\ L~do by A93, TOPREAL8:35
        .= {go/.1} \/ {do/.1} by A64,A79,A118,A120,XBOOLE_1:23
        .= {(go^'pion1)/.1} \/ {do/.1} by GRAPH_2:57
        .= {(go^'pion1)/.1,do/.1} by ENUMSET1:41;
      do/.len do = (go^'pion1)/.1 by A56,GRAPH_2:57;
      then reconsider godo as non constant standard special_circular_sequence
      by A91,A95,A96,A101,A103,A111,A112,A116,A117,A121,JORDAN8:7,8
      ,TOPREAL8:11,33,34;
A122: UA is_an_arc_of E-max C,W-min C by JORDAN6:def 9;
      then
A123: UA is connected by JORDAN6:11;
A124: W-min C in UA & E-max C in UA by A122,TOPREAL1:4;
      set ff = Rotate(Cage(C,n),Wmin);
      Wmin in rng Cage(C,n) by SPRECT_2:47;
      then
A125: ff/.1 = Wmin by FINSEQ_6:98;
A126: L~ff = L~Cage(C,n) by REVROT_1:33;
      then (W-max L~ff)..ff > 1 by A125,SPRECT_5:23;
      then (N-min L~ff)..ff > 1 by A125,A126,SPRECT_5:24,XXREAL_0:2;
      then (N-max L~ff)..ff > 1 by A125,A126,SPRECT_5:25,XXREAL_0:2;
      then
A127: Emax..ff > 1 by A125,A126,SPRECT_5:26,XXREAL_0:2;
A128: now
        assume
A129:   Gij..US <= 1;
        Gij..US >= 1 by A30,FINSEQ_4:31;
        then Gij..US = 1 by A129,XXREAL_0:1;
        then Gij = US/.1 by A30,FINSEQ_5:41;
        hence contradiction by A17,A21,JORDAN1F:5;
      end;
A130: Cage(C,n) is_sequence_on G by JORDAN9:def 1;
      then
A131: ff is_sequence_on G by REVROT_1:34;
A132: right_cell(godo,1,G)\L~godo c= RightComp godo by A91,A96,JORDAN9:29;
A133: L~godo = L~(go^'pion1) \/ L~do by A95,TOPREAL8:35
        .= L~go \/ L~pion1 \/ L~do by A93,TOPREAL8:35;
      L~Cage(C,n) = L~US \/ L~LS by JORDAN1E:17;
      then
A134: L~US c= L~Cage(C,n) & L~LS c= L~Cage(C,n) by XBOOLE_1:7;
      then
A135: L~go c=L~Cage(C,n) & L~do c=L~Cage(C,n) by A42,A49,XBOOLE_1:1;
A136: W-min C in C by SPRECT_1:15;
A137: now
        assume W-min C in L~godo;
        then
A138:   W-min C in L~go \/ L~pion1 or W-min C in L~do by A133, XBOOLE_0:def 2;
        per cases by A138,XBOOLE_0:def 2;
        suppose W-min C in L~go;
          then C meets L~Cage(C,n) by A135,A136,XBOOLE_0:3;
          hence contradiction by JORDAN10:5;
        end;
        suppose W-min C in L~pion1;
          hence contradiction by A5,A7,A77,A124,XBOOLE_0:3;
        end;
        suppose W-min C in L~do;
          then C meets L~Cage(C,n) by A135,A136,XBOOLE_0:3;
          hence contradiction by JORDAN10:5;
        end;
      end;
      right_cell(Rotate(Cage(C,n),Wmin),1) =
      right_cell(ff,1,GoB ff) by A87,JORDAN1H:29
        .= right_cell(ff,1,GoB Cage(C,n)) by REVROT_1:28
        .= right_cell(ff,1,G) by JORDAN1H:52
        .= right_cell(ff-:Emax,1,G) by A127,A131,JORDAN1J:53
        .= right_cell(US,1,G) by JORDAN1E:def 1
        .= right_cell(R_Cut(US,Gij),1,G) by A30,A92,A128,JORDAN1J:52
        .= right_cell(go^'pion1,1,G) by A35,A94,JORDAN1J:51
        .= right_cell(godo,1,G) by A89,A96,JORDAN1J:51;
      then W-min C in right_cell(godo,1,G) by JORDAN1I:8;
      then
A139: W-min C in right_cell(godo,1,G)\L~godo by A137,XBOOLE_0:def 4;
A140: godo/.1 = (go^'pion1)/.1 by GRAPH_2:57
        .= Wmin by A55,GRAPH_2:57;
A141: len US >= 2 by A16,XXREAL_0:2;
A142: godo/.2 = (go^'pion1)/.2 by A88,GRAPH_2:61
        .= US/.2 by A29,A68,GRAPH_2:61
        .= (US^'LS)/.2 by A141,GRAPH_2:61
        .= Rotate(Cage(C,n),Wmin)/.2 by JORDAN1E:15;
A143: L~go \/ L~do is compact by COMPTS_1:19;
      Wmin in L~go \/ L~do by A57,A69,XBOOLE_0:def 2;
      then
A144: W-min (L~go \/ L~do) = Wmin by A135,A143,JORDAN1J:21,XBOOLE_1: 8;
A145: (W-min (L~go \/ L~do))`1 = W-bound (L~go \/ L~do) & Wmin`1 = Wbo
      by EUCLID:56;
      Gik`1 >= Wbo by A8,A134,PSCOMP_1:71;
      then Gik`1 > Wbo by A67,REAL_1:def 5;
      then W-min (L~go\/L~do\/L~pion1) = W-min (L~go \/ L~do)
      by A85,A143,A144,A145,JORDAN1J:33;
      then
A146: W-min L~godo = Wmin by A133,A144,XBOOLE_1:4;
A147: rng godo c= L~godo by A88,A90,SPPOL_2:18,XXREAL_0:2;
      2 in dom godo by A91,FINSEQ_3:27;
      then
A148: godo/.2 in rng godo by PARTFUN2:4;
      godo/.2 in W-most L~Cage(C,n) by A142,JORDAN1I:27;
      then (godo/.2)`1 = (W-min L~godo)`1 by A146,PSCOMP_1:88
        .= W-bound L~godo by EUCLID:56;
      then godo/.2 in W-most L~godo by A147,A148,SPRECT_2:16;
      then Rotate(godo,W-min L~godo)/.2 in W-most L~godo
      by A140,A146,FINSEQ_6:95;
      then reconsider godo as clockwise_oriented non constant standard
      special_circular_sequence by JORDAN1I:27;
      len US in dom US by FINSEQ_5:6;
      then
A149: US.len US = US/.len US by PARTFUN1:def 8
        .= Emax by JORDAN1F:7;
A150: E-max C in E-most C by PSCOMP_1:111;
A151: east_halfline E-max C misses L~go
      proof
        assume east_halfline E-max C meets L~go;
        then consider p be set such that
A152:   p in east_halfline E-max C and
A153:   p in L~go by XBOOLE_0:3;
        reconsider p as Point of TOP-REAL 2 by A152;
        p in L~US by A42,A153;
        then p in east_halfline E-max C /\ L~Cage(C,n)
        by A134,A152,XBOOLE_0:def 3;
        then
A154:   p`1 = Ebo by A150,JORDAN1A:104;
        then
A155:   p = Emax by A42,A153,JORDAN1J:46;
        then Emax = Gij by A9,A149,A153,JORDAN1J:43;
        then Gij`1 = G*(len G,k)`1 by A11,A15,A154,A155,JORDAN1A:92;
        hence contradiction by A1,A12,A26,JORDAN1G:7;
      end;
      now
        assume east_halfline E-max C meets L~godo;
        then
A156:   east_halfline E-max C meets (L~go \/ L~pion1) or
        east_halfline E-max C meets L~do by A133,XBOOLE_1:70;
        per cases by A156,XBOOLE_1:70;
        suppose east_halfline E-max C meets L~go;
          hence contradiction by A151;
        end;
        suppose east_halfline E-max C meets L~pion1;
          then consider p be set such that
A157:     p in east_halfline E-max C and
A158:     p in L~pion1 by XBOOLE_0:3;
          reconsider p as Point of TOP-REAL 2 by A157;
A159:     now per cases by A7,A77,A158,XBOOLE_0:def 2;
            suppose p in poz;
              hence p`1 <= Gij`1 by A81,A82,TOPREAL1:9;
            end;
            suppose p in pio;
              hence p`1 <= Gij`1 by A81,GOBOARD7:5;
            end;
          end;
          i1+1 <= len G by A1,NAT_1:13;
          then i1 <= len G-1 by XREAL_1:21;
          then
A160:     i1 <= len G-'1 by BINARITH:def 3;
          len G-'1 <= len G by BINARITH:52;
          then Gij`1 <= G*(len G-'1,1)`1 by A6,A10,A15,A19,A160,JORDAN1A:39;
          then p`1 <= G*(len G-'1,1)`1 by A159,XXREAL_0:2;
          then p`1 <= E-bound C by A19,JORDAN8:15;
          then
A161:     p`1 <= (E-max C)`1 by EUCLID:56;
          p`1 >= (E-max C)`1 by A157,TOPREAL1:def 13;
          then
A162:     p`1 = (E-max C)`1 by A161,XXREAL_0:1;
          p`2 = (E-max C)`2 by A157,TOPREAL1:def 13;
          then p = E-max C by A162,TOPREAL3:11;
          hence contradiction by A5,A7,A77,A124,A158,XBOOLE_0:3;
        end;
        suppose east_halfline E-max C meets L~do;
          then consider p be set such that
A163:     p in east_halfline E-max C and
A164:     p in L~do by XBOOLE_0:3;
          reconsider p as Point of TOP-REAL 2 by A163;
          p in L~LS by A49,A164;
          then p in east_halfline E-max C /\ L~Cage(C,n)
          by A134,A163,XBOOLE_0:def 3;
          then
A165:     p`1 = Ebo by A150,JORDAN1A:104;
A166:     (E-max C)`2 = p`2 by A163,TOPREAL1:def 13;
          set RC = Rotate(Cage(C,n),Emax);
A167:     E-max C in right_cell(RC,1) by JORDAN1I:9;
A168:     1+1 <= len LS by A22,XXREAL_0:2;
          LS = RC-:Wmin by JORDAN1G:26;
          then
A169:     LSeg(LS,1) = LSeg(RC,1) by A168,SPPOL_2:9;
A170:     L~RC = L~Cage(C,n) by REVROT_1:33;
A171:     len RC = len Cage(C,n) by REVROT_1:14;
A172:     GoB RC = GoB Cage(C,n) by REVROT_1:28
            .= G by JORDAN1H:52;
A173:     Emax in rng Cage(C,n) by SPRECT_2:50;
A174:     RC is_sequence_on G by A130,REVROT_1:34;
A175:     RC/.1 = E-max L~RC by A170,A173,FINSEQ_6:98;
          then consider ii,jj be Element of NAT such that
A176:     [ii,jj+1] in Indices G and
A177:     [ii,jj] in Indices G and
A178:     RC/.1 = G*(ii,jj+1) and
A179:     RC/.(1+1) = G*(ii,jj) by A86,A171,A174,JORDAN1I:25;
          consider jj2 be Element of NAT such that
A180:     1 <= jj2 & jj2 <= width G and
A181:     Emax = G*(len G,jj2) by JORDAN1D:29;
A182:     len G >= 4 by JORDAN8:13;
          then len G >= 1 by XXREAL_0:2;
          then [len G,jj2] in Indices G by A180,GOBOARD7:10;
          then
A183:     ii = len G by A170,A175,A176,A178,A181,GOBOARD1:21;
A184:     1 <= ii & ii <= len G & 1 <= jj+1 & jj+1 <= width G
          by A176,GOBOARD5:1;
A185:     1 <= ii & ii <= len G & 1 <= jj & jj <= width G by A177,GOBOARD5:1;
A186:     ii+1 <> ii;
          jj+1+1 <> jj;
          then
A187:     right_cell(RC,1) = cell(G,ii-'1,jj)
          by A86,A171,A172,A176,A177,A178,A179,A186, GOBOARD5:def 6;
A188:     ii-'1+1 = ii by A184,BINARITH:53;
          ii-1 >= 4-1 by A182,A183,XREAL_1:11;
          then
A189:     ii-1 >= 1 by XXREAL_0:2;
          then
A190:     1 <= ii-'1 by BINARITH:def 3;
A191:     G*(ii-'1,jj)`2 <= p`2 & p`2 <= G*(ii-'1,jj+1)`2
          by A166,A167,A184,A185,A187,A188,A189,JORDAN9:19;
A192:     ii-'1 < len G by A184,A188,NAT_1:13;
          then
A193:     G*(ii-'1,jj)`2 = G*(1,jj)`2 by A185,A190,GOBOARD5:2
            .= G*(ii,jj)`2 by A185,GOBOARD5:2;
A194:     G*(ii-'1,jj+1)`2 = G*(1,jj+1)`2 by A184,A190,A192,GOBOARD5:2
            .= G*(ii,jj+1)`2 by A184,GOBOARD5:2;
          G*(len G,jj)`1 = Ebo & Ebo = G*(len G,jj+1)`1
          by A15,A184,A185,JORDAN1A:92;
          then p in LSeg(RC/.1,RC/.(1+1))
          by A165,A178,A179,A183,A191,A193,A194,GOBOARD7:8;
          then
A195:     p in LSeg(LS,1) by A86,A169,A171,TOPREAL1:def 5;
A196:     p in LSeg(do,Index(p,do)) by A164,JORDAN3:42;
A197:     do = mid(LS,Gik..LS,len LS) by A33,JORDAN1J:37;
A198:     1<=Gik..LS & Gik..LS<=len LS by A33,FINSEQ_4:31;
          Gik..LS <> len LS by A25,A33,FINSEQ_4:29;
          then
A199:     Gik..LS < len LS by A198,REAL_1:def 5;
A200:     1<=Index(p,do) & Index(p,do) < len do by A164,JORDAN3:41;
A201:     Index(Gik,LS)+1 = Gik..LS by A28,A33,JORDAN1J:56;
          consider t be Nat such that
A202:     t in dom LS and
A203:     LS.t = Gik by A33,FINSEQ_2:11;
A204:     1 <= t & t <= len LS by A202,FINSEQ_3:27;
          then 1 < t by A28,A203,REAL_1:def 5;
          then Index(Gik,LS)+1 = t by A203,A204,JORDAN3:45;
          then
A205:     len L_Cut(LS,Gik) = len LS-Index(Gik,LS) by A8,A203, JORDAN3:61;
          set tt = Index(p,do)+(Gik..LS)-'1;
A206:     1<=Index(Gik,LS) & 0+Index(Gik,LS) < len LS by A8,JORDAN3:41;
          then
A207:     len LS-Index(Gik,LS) > 0 by XREAL_1:22;
          Index(p,do) < len LS-'Index(Gik,LS) by A200,A205,BINARITH:def 3;
          then Index(p,do)+1 <= len LS-'Index(Gik,LS) by NAT_1:13;
          then Index(p,do) <= len LS-'Index(Gik,LS)-1 by XREAL_1:21;
          then Index(p,do) <= len LS-Index(Gik,LS)-1 by A207,BINARITH:def 3;
          then Index(p,do) <= len LS-Gik..LS by A201;
          then Index(p,do) <= len LS-'Gik..LS by BINARITH:def 3;
          then Index(p,do) < len LS-'(Gik..LS)+1 by NAT_1:13;
          then
A208:     LSeg(mid(LS,Gik..LS,len LS),Index(p,do)) =
          LSeg(LS,Index(p,do)+(Gik..LS)-'1) by A198,A199,A200,JORDAN4:31;
A209:     1+1 <= Gik..LS by A201,A206,XREAL_1:9;
          then Index(p,do)+Gik..LS >= 1+1+1 by A200,XREAL_1:9;
          then Index(p,do)+Gik..LS-1 >= 1+1+1-1 by XREAL_1:11;
          then
A210:     tt >= 1+1 by BINARITH:def 3;
A211:     2 in dom LS by A168,FINSEQ_3:27;
          now per cases by A210,REAL_1:def 5;
            suppose tt > 1+1;
              then LSeg(LS,1) misses LSeg(LS,tt) by TOPREAL1:def 9;
              hence contradiction by A195,A196,A197,A208,XBOOLE_0:3;
            end;
            suppose
A212:         tt = 1+1;
              then LSeg(LS,1) /\ LSeg(LS,tt) = {LS/.2} by A22,TOPREAL1:def 8;
              then p in {LS/.2} by A195,A196,A197,A208,XBOOLE_0:def 3;
              then
A213:         p = LS/.2 by TARSKI:def 1;
              then
A214:         p..LS = 2 by A211,FINSEQ_5:44;
              1+1 = Index(p,do)+(Gik..LS)-1 by A212,BINARITH:def 3;
              then 1+1+1 = Index(p,do)+(Gik..LS);
              then
A215:         Gik..LS = 2 by A200,A209,JORDAN1E:10;
              p in rng LS by A211,A213,PARTFUN2:4;
              then p = Gik by A33,A214,A215,FINSEQ_5:10;
              then Gik`1 = Ebo by A213,JORDAN1G:40;
              then Gik`1 = G*(len G,j)`1 by A10,A15,JORDAN1A:92;
              hence contradiction by A1,A13,A60,JORDAN1G:7;
            end;
          end;
          hence contradiction;
        end;
      end;
      then east_halfline E-max C c= (L~godo)` by SUBSET_1:43;
      then consider W be Subset of TOP-REAL 2 such that
A216: W is_a_component_of (L~godo)` and
A217: east_halfline E-max C c= W by GOBOARD9:5;
      W is not Bounded by A217,JORDAN2C:16,129;
      then W is_outside_component_of L~godo by A216,JORDAN2C:def 4;
      then W c= UBD L~godo by JORDAN2C:27;
      then
A218: east_halfline E-max C c= UBD L~godo by A217,XBOOLE_1:1;
      E-max C in east_halfline E-max C by TOPREAL1:45;
      then E-max C in UBD L~godo by A218;
      then E-max C in LeftComp godo by GOBRD14:46;
      then UA meets L~godo by A123,A124,A132,A139,JORDAN1J:36;
      then
A219: UA meets (L~go \/ L~pion1) or UA meets L~do by A133, XBOOLE_1: 70;
A220: UA c= C by JORDAN6:76;
      now per cases by A219,XBOOLE_1:70;
        suppose UA meets L~go;
          then UA meets L~Cage(C,n) by A42,A134,XBOOLE_1:1,63;
          hence contradiction by A220,JORDAN10:5,XBOOLE_1:63;
        end;
        suppose UA meets L~pion1;
          hence contradiction by A5,A7,A77;
        end;
        suppose UA meets L~do;
          then UA meets L~Cage(C,n) by A49,A134,XBOOLE_1:1,63;
          hence contradiction by A220,JORDAN10:5,XBOOLE_1:63;
        end;
      end;
      hence contradiction;
    end;
    suppose Gik`1 = Gij`1;
      then
A221: i1 = i2 by A12,A13,JORDAN1G:7;
      then poz = {Gi1k} by TOPREAL1:7;
      then poz c= pio by A73,ZFMISC_1:37;
      then pio \/ poz = pio by XBOOLE_1:12;
      hence contradiction by A1,A2,A3,A4,A5,A221,Th14;
    end;
    suppose Gik`2 = Gij`2;
      then
A222: j = k by A12,A13,JORDAN1G:6;
      then pio = {Gi1k} by TOPREAL1:7;
      then pio c= poz by A74,ZFMISC_1:37;
      then pio \/ poz = poz by XBOOLE_1:12;
      hence contradiction by A1,A2,A3,A4,A5,A222,JORDAN15:30;
    end;
  end;
  hence contradiction;
end;

theorem Th25:
  for C be Simple_closed_curve for i1,i2,j,k be Element of NAT holds
  1 < i1 & i1 < len Gauge(C,n+1) & 1 < i2 & i2 < len Gauge(C,n+1) &
  1 <= j & j <= k & k <= width Gauge(C,n+1) &
  Gauge(C,n+1)*(i1,k) in Lower_Arc L~Cage(C,n+1) &
  Gauge(C,n+1)*(i2,j) in Upper_Arc L~Cage(C,n+1) implies
  LSeg(Gauge(C,n+1)*(i2,j),Gauge(C,n+1)*(i2,k)) \/
  LSeg(Gauge(C,n+1)*(i2,k),Gauge(C,n+1)*(i1,k)) meets Lower_Arc C
proof
  let C be Simple_closed_curve;
  let i1,i2,j,k be Element of NAT;
  set G=Gauge(C,n+1);
  assume that
A1: 1 < i1 & i1 < len G and
A2: 1 < i2 & i2 < len G and
A3: 1 <= j & j <= k & k <= width G and
A4: G*(i1,k) in Lower_Arc L~Cage(C,n+1) and
A5: G*(i2,j) in Upper_Arc L~Cage(C,n+1);
A6: Lower_Arc L~Cage(C,n+1) = L~Lower_Seq(C,n+1) by JORDAN1G:64;
A7: Upper_Arc L~Cage(C,n+1) = L~Upper_Seq(C,n+1) by JORDAN1G:63;
A8: Lower_Seq(C,n+1) is_sequence_on G by JORDAN1G:5;
A9: Upper_Seq(C,n+1) is_sequence_on G by JORDAN1G:4;
A10: 1 <= j & j <= width G by A3,XXREAL_0:2;
  then
A11: [i2,j] in Indices G by A2,GOBOARD7:10;
A12: 1 <= k & k <= width G by A3,XXREAL_0:2;
  then
A13: [i2,k] in Indices G by A2,GOBOARD7:10;
  G*(i2,j)`1 = G*(i2,1)`1 by A2,A10,GOBOARD5:3
    .= G*(i2,k)`1 by A2,A12,GOBOARD5:3;
  then
A14: LSeg(G*(i2,j),G*(i2,k)) is vertical by SPPOL_1:37;
  G*(i2,k)`2 = G*(1,k)`2 by A2,A12,GOBOARD5:2
    .= G*(i1,k)`2 by A1,A12,GOBOARD5:2;
  then
A15: LSeg(G*(i2,k),G*(i1,k)) is horizontal by SPPOL_1:36;
A16: [i2,k] in Indices G by A2,A12,GOBOARD7:10;
A17: [i1,k] in Indices G by A1,A12,GOBOARD7:10;
  now per cases;
    suppose
A18:  LSeg(G*(i2,j),G*(i2,k)) meets Lower_Arc L~Cage(C,n+1);
      then consider m be Element of NAT such that
A19:  j <= m & m <= k and
A20:  G*(i2,m)`2 = inf(proj2.:(LSeg(G*(i2,j),G*(i2,k))
      /\ L~Lower_Seq(C,n+1))) by A3,A6,A8,A11,A13,JORDAN1F:1;
A21:  1 <= m & m <= width G by A3,A19,XXREAL_0:2;
      set X = LSeg(G*(i2,j),G*(i2,k)) /\ L~Lower_Seq(C,n+1);
A22:  G*(i2,m)`1 = G*(i2,1)`1 by A2,A21,GOBOARD5:3;
      then
A23:  |[G*(i2,1)`1,inf(proj2.:X)]| = G*(i2,m) by A20,EUCLID:57;
      then
A24:  G*(i2,j)`1 = |[G*(i2,1)`1,inf(proj2.:X)]|`1 by A2,A10,A22, GOBOARD5:3;
      ex x be set st x in LSeg(G*(i2,j),G*(i2,k)) &
      x in L~Lower_Seq(C,n+1) by A6,A18,XBOOLE_0:3;
      then reconsider X1=X as non empty compact Subset of TOP-REAL 2
      by PSCOMP_1:64,XBOOLE_0:def 3;
      consider pp be set such that
A25:  pp in S-most X1 by XBOOLE_0:def 1;
      reconsider pp as Point of TOP-REAL 2 by A25;
A26:  pp in X by A25,XBOOLE_0:def 3;
      then
A27:  pp in L~Lower_Seq(C,n+1) by XBOOLE_0:def 3;
      pp in LSeg(G*(i2,j),G*(i2,k)) by A26,XBOOLE_0:def 3;
      then
A28:  pp`1 = |[G*(i2,1)`1,inf(proj2.:X)]|`1 by A14,A24,SPPOL_1:64;
      |[G*(i2,1)`1,inf(proj2.:X)]|`2 = S-bound X by A20,A23,SPRECT_1:49
        .= (S-min X)`2 by EUCLID:56
        .= pp`2 by A25,PSCOMP_1:118;
      then G*(i2,m) in Lower_Arc L~Cage(C,n+1) by A6,A23,A27,A28,TOPREAL3:11;
      then LSeg(G*(i2,j),G*(i2,m)) meets Lower_Arc C by A2,A3,A5,A19,A21,Th20;
      then LSeg(G*(i2,j),G*(i2,k)) meets Lower_Arc C by A2,A3,A19,JORDAN15:7,
      XBOOLE_1:63;
      hence LSeg(G*(i2,j),G*(i2,k)) \/
      LSeg(G*(i2,k),G*(i1,k)) meets Lower_Arc C by XBOOLE_1:70;
    end;
    suppose
A29:  LSeg(G*(i2,k),G*(i1,k)) meets Upper_Arc L~Cage(C,n+1) & i2 <= i1;
      then consider m be Element of NAT such that
A30:  i2 <= m & m <= i1 and
A31:  G*(m,k)`1 = sup(proj1.:(LSeg(G*(i2,k),G*(i1,k))
      /\ L~Upper_Seq(C,n+1))) by A7,A9,A16,A17,JORDAN1F:4;
A32:  1 < m & m < len G by A1,A2,A30,XXREAL_0:2;
      set X = LSeg(G*(i2,k),G*(i1,k)) /\ L~Upper_Seq(C,n+1);
A33:  G*(m,k)`2 = G*(1,k)`2 by A12,A32,GOBOARD5:2;
      then
A34:  |[sup(proj1.:X),G*(1,k)`2]| = G*(m,k) by A31,EUCLID:57;
      then
A35:  G*(i2,k)`2 = |[sup(proj1.:X),G*(1,k)`2]|`2 by A2,A12,A33, GOBOARD5:2;
      ex x be set st x in LSeg(G*(i2,k),G*(i1,k)) &
      x in L~Upper_Seq(C,n+1) by A7,A29,XBOOLE_0:3;
      then reconsider X1=X as non empty compact Subset of TOP-REAL 2
      by PSCOMP_1:64,XBOOLE_0:def 3;
      consider pp be set such that
A36:  pp in E-most X1 by XBOOLE_0:def 1;
      reconsider pp as Point of TOP-REAL 2 by A36;
A37:  pp in X by A36,XBOOLE_0:def 3;
      then
A38:  pp in L~Upper_Seq(C,n+1) by XBOOLE_0:def 3;
      pp in LSeg(G*(i2,k),G*(i1,k)) by A37,XBOOLE_0:def 3;
      then
A39:  pp`2 = |[sup(proj1.:X),G*(1,k)`2]|`2 by A15,A35,SPPOL_1:63;
      |[sup(proj1.:X),G*(1,k)`2]|`1 = E-bound X by A31,A34,SPRECT_1:51
        .= (E-min X)`1 by EUCLID:56
        .= pp`1 by A36,PSCOMP_1:108;
      then G*(m,k) in Upper_Arc L~Cage(C,n+1) by A7,A34,A38,A39,TOPREAL3:11;
      then LSeg(G*(m,k),G*(i1,k)) meets Lower_Arc C
      by A1,A4,A12,A30,A32,JORDAN15:42;
      then LSeg(G*(i2,k),G*(i1,k)) meets Lower_Arc C by A1,A2,A12,A30,JORDAN15
      :8,XBOOLE_1:63;
      hence LSeg(G*(i2,j),G*(i2,k)) \/
      LSeg(G*(i2,k),G*(i1,k)) meets Lower_Arc C by XBOOLE_1:70;
    end;
    suppose
A40:  LSeg(G*(i2,k),G*(i1,k)) meets Upper_Arc L~Cage(C,n+1) & i1 < i2;
      then consider m be Element of NAT such that
A41:  i1 <= m & m <= i2 and
A42:  G*(m,k)`1 = inf(proj1.:(LSeg(G*(i1,k),G*(i2,k))
      /\ L~Upper_Seq(C,n+1))) by A7,A9,A16,A17,JORDAN1F:3;
A43:  1 < m & m < len G by A1,A2,A41,XXREAL_0:2;
      set X = LSeg(G*(i1,k),G*(i2,k)) /\ L~Upper_Seq(C,n+1);
A44:  G*(m,k)`2 = G*(1,k)`2 by A12,A43,GOBOARD5:2;
      then
A45:  |[inf(proj1.:X),G*(1,k)`2]| = G*(m,k) by A42,EUCLID:57;
      then
A46:  G*(i1,k)`2 = |[inf(proj1.:X),G*(1,k)`2]|`2 by A1,A12,A44, GOBOARD5:2;
      ex x be set st x in LSeg(G*(i1,k),G*(i2,k)) &
      x in L~Upper_Seq(C,n+1) by A7,A40,XBOOLE_0:3;
      then reconsider X1=X as non empty compact Subset of TOP-REAL 2
      by PSCOMP_1:64,XBOOLE_0:def 3;
      consider pp be set such that
A47:  pp in W-most X1 by XBOOLE_0:def 1;
      reconsider pp as Point of TOP-REAL 2 by A47;
A48:  pp in X by A47,XBOOLE_0:def 3;
      then
A49:  pp in L~Upper_Seq(C,n+1) by XBOOLE_0:def 3;
      pp in LSeg(G*(i1,k),G*(i2,k)) by A48,XBOOLE_0:def 3;
      then
A50:  pp`2 = |[inf(proj1.:X),G*(1,k)`2]|`2 by A15,A46,SPPOL_1:63;
      |[inf(proj1.:X),G*(1,k)`2]|`1 = W-bound X by A42,A45,SPRECT_1:48
        .= (W-min X)`1 by EUCLID:56
        .= pp`1 by A47,PSCOMP_1:88;
      then G*(m,k) in Upper_Arc L~Cage(C,n+1) by A7,A45,A49,A50,TOPREAL3:11;
      then LSeg(G*(i1,k),G*(m,k)) meets Lower_Arc C
      by A1,A4,A12,A41,A43,JORDAN15:34;
      then LSeg(G*(i1,k),G*(i2,k)) meets Lower_Arc C by A1,A2,A12,A41,JORDAN15
      :8,XBOOLE_1:63;
      hence LSeg(G*(i2,j),G*(i2,k)) \/
      LSeg(G*(i2,k),G*(i1,k)) meets Lower_Arc C by XBOOLE_1:70;
    end;
    suppose
A51:  LSeg(G*(i2,j),G*(i2,k)) misses Lower_Arc L~Cage(C,n+1) &
      LSeg(Gauge(C,n+1)*(i2,k),Gauge(C,n+1)*(i1,k))
      misses Upper_Arc L~Cage(C,n+1);
      consider j1 be Element of NAT such that
A52:  j <= j1 & j1 <= k and
A53:  LSeg(G*(i2,j1),G*(i2,k)) /\ L~Upper_Seq(C,n+1) =
      {G*(i2,j1)} by A2,A3,A5,A7,JORDAN15:17;
      G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) /\
      L~Upper_Seq(C,n+1) by A53,TARSKI:def 1;
      then
A54:  G*(i2,j1) in L~Upper_Seq(C,n+1) by XBOOLE_0:def 3;
A55:  1 <= j1 & j1 <= width G by A3,A52,XXREAL_0:2;
      now per cases;
        suppose i2 <= i1;
          then consider i3 be Element of NAT such that
A56:      i2 <= i3 & i3 <= i1 and
A57:      LSeg(G*(i2,k),G*(i3,k)) /\ L~Lower_Seq(C,n+1) = {G*(i3,k)}
          by A1,A2,A4,A6,A12,JORDAN15:21;
A58:      1 < i3 & i3 < len G by A1,A2,A56,XXREAL_0:2;
          G*(i3,k) in LSeg(G*(i2,k),G*(i3,k)) /\ L~Lower_Seq(C,n+1)
          by A57,TARSKI:def 1;
          then
A59:      G*(i3,k) in L~Lower_Seq(C,n+1) by XBOOLE_0:def 3;
A60:      LSeg(G*(i2,j1),G*(i2,k)) c=
          LSeg(G*(i2,j),G*(i2,k)) by A2,A3,A52,JORDAN15:7;
A61:      LSeg(G*(i2,k),G*(i3,k)) c= LSeg(G*(i2,k),G*(i1,k))
          by A1,A2,A12,A56,JORDAN15:8;
          then
A62:      LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) c=

LSeg(G*(i2,j),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i1,k)) by A60,XBOOLE_1:13;
A63:      (LSeg(G*(i2,j1),G*(i2,k)) \/
          LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1) = {G*(i3,k)}
          proof
            thus (LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1) c= {G*(i3,k)}
            proof
              let x be set;
              assume x in (LSeg(G*(i2,j1),G*(i2,k)) \/
              LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1);
              then x in LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) &
              x in L~Lower_Seq(C,n+1) by XBOOLE_0:def 3;
              then (x in LSeg(G*(i2,j1),G*(i2,k)) or
              x in LSeg(G*(i2,k),G*(i3,k))) &
              x in L~Lower_Seq(C,n+1) by XBOOLE_0:def 2;
              hence x in {G*(i3,k)} by A6,A51,A57,A60,XBOOLE_0:3,def 3;
            end;
            let x be set;
            assume x in {G*(i3,k)};
            then
A64:        x = G*(i3,k) by TARSKI:def 1;
            G*(i3,k) in LSeg(G*(i2,k),G*(i3,k)) by TOPREAL1:6;
            then G*(i3,k) in LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k)) by XBOOLE_0:def 2;
            hence x in (LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1)
            by A59,A64,XBOOLE_0:def 3;
          end;
          (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k))) /\
          L~Upper_Seq(C,n+1) = {G*(i2,j1)}
          proof
            thus (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k))) /\
            L~Upper_Seq(C,n+1) c= {G*(i2,j1)}
            proof
              let x be set;
              assume x in (LSeg(G*(i2,j1),G*(i2,k)) \/
              LSeg(G*(i2,k),G*(i3,k))) /\ L~Upper_Seq(C,n+1);
              then x in LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) &
              x in L~Upper_Seq(C,n+1) by XBOOLE_0:def 3;
              then (x in LSeg(G*(i2,j1),G*(i2,k)) or
              x in LSeg(G*(i2,k),G*(i3,k))) &
              x in L~Upper_Seq(C,n+1) by XBOOLE_0:def 2;
              hence x in {G*(i2,j1)} by A7,A51,A53,A61,XBOOLE_0:3,def 3;
            end;
            let x be set;
            assume x in {G*(i2,j1)};
            then
A65:        x = G*(i2,j1) by TARSKI:def 1;
            G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) by TOPREAL1:6;
            then G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k)) by XBOOLE_0:def 2;
            hence x in (LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k))) /\ L~Upper_Seq(C,n+1)
            by A54,A65,XBOOLE_0:def 3;
          end;
          hence LSeg(G*(i2,j),G*(i2,k)) \/

LSeg(G*(i2,k),G*(i1,k)) meets Lower_Arc C by A2,A3,A52,A55,A56,A58,A62
          ,A63,Th22,XBOOLE_1:63;
        end;
        suppose i1 < i2;
          then consider i3 be Element of NAT such that
A66:      i1 <= i3 & i3 <= i2 and
A67:      LSeg(G*(i3,k),G*(i2,k)) /\ L~Lower_Seq(C,n+1) = {G*(i3,k)}
          by A1,A2,A4,A6,A12,JORDAN15:14;
A68:      1 < i3 & i3 < len G by A1,A2,A66,XXREAL_0:2;
          G*(i3,k) in LSeg(G*(i2,k),G*(i3,k)) /\ L~Lower_Seq(C,n+1)
          by A67,TARSKI:def 1;
          then
A69:      G*(i3,k) in L~Lower_Seq(C,n+1) by XBOOLE_0:def 3;
A70:      LSeg(G*(i2,j1),G*(i2,k)) c=
          LSeg(G*(i2,j),G*(i2,k)) by A2,A3,A52,JORDAN15:7;
A71:      LSeg(G*(i2,k),G*(i3,k)) c= LSeg(G*(i2,k),G*(i1,k))
          by A1,A2,A12,A66,JORDAN15:8;
          then
A72:      LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) c=

LSeg(G*(i2,j),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i1,k)) by A70,XBOOLE_1:13;
A73:      (LSeg(G*(i2,j1),G*(i2,k)) \/
          LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1) = {G*(i3,k)}
          proof
            thus (LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1) c= {G*(i3,k)}
            proof
              let x be set;
              assume x in (LSeg(G*(i2,j1),G*(i2,k)) \/
              LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1);
              then x in LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) &
              x in L~Lower_Seq(C,n+1) by XBOOLE_0:def 3;
              then (x in LSeg(G*(i2,j1),G*(i2,k)) or x in LSeg(G*(i2,k),G*(i3
              ,k))) & x in L~Lower_Seq(C,n+1) by XBOOLE_0:def 2;
              hence x in {G*(i3,k)} by A6,A51,A67,A70,XBOOLE_0:3,def 3;
            end;
            let x be set;
            assume x in {G*(i3,k)};
            then
A74:        x = G*(i3,k) by TARSKI:def 1;
            G*(i3,k) in LSeg(G*(i2,k),G*(i3,k)) by TOPREAL1:6;
            then G*(i3,k) in LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k)) by XBOOLE_0:def 2;
            hence x in (LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1)
            by A69,A74,XBOOLE_0:def 3;
          end;
          (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k))) /\
          L~Upper_Seq(C,n+1) = {G*(i2,j1)}
          proof
            thus (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k))) /\
            L~Upper_Seq(C,n+1) c= {G*(i2,j1)}
            proof
              let x be set;
              assume x in (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k )
              )) /\ L~Upper_Seq(C,n+1);
              then x in LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) &
              x in L~Upper_Seq(C,n+1) by XBOOLE_0:def 3;
              then (x in LSeg(G*(i2,j1),G*(i2,k)) or x in LSeg(G*(i2,k),G*(i3
              ,k))) & x in L~Upper_Seq(C,n+1) by XBOOLE_0:def 2;
              hence x in {G*(i2,j1)} by A7,A51,A53,A71,XBOOLE_0:3,def 3;
            end;
            let x be set;
            assume x in {G*(i2,j1)};
            then
A75:        x = G*(i2,j1) by TARSKI:def 1;
            G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) by TOPREAL1:6;
            then G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k)) by XBOOLE_0:def 2;
            hence x in (LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k))) /\ L~Upper_Seq(C,n+1)
            by A54,A75,XBOOLE_0:def 3;
          end;
          hence LSeg(G*(i2,j),G*(i2,k)) \/

LSeg(G*(i2,k),G*(i1,k)) meets Lower_Arc C by A2,A3,A52,A55,A66,A68,A72
          ,A73,Th24,XBOOLE_1:63;
        end;
      end;
      hence LSeg(G*(i2,j),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i1,k))
      meets Lower_Arc C;
    end;
  end;
  hence LSeg(G*(i2,j),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i1,k)) meets Lower_Arc C;
end;

theorem Th26:
  for C be Simple_closed_curve for i1,i2,j,k be Element of NAT holds
  1 < i1 & i1 < len Gauge(C,n+1) & 1 < i2 & i2 < len Gauge(C,n+1) &
  1 <= j & j <= k & k <= width Gauge(C,n+1) &
  Gauge(C,n+1)*(i1,k) in Lower_Arc L~Cage(C,n+1) &
  Gauge(C,n+1)*(i2,j) in Upper_Arc L~Cage(C,n+1) implies
  LSeg(Gauge(C,n+1)*(i2,j),Gauge(C,n+1)*(i2,k)) \/
  LSeg(Gauge(C,n+1)*(i2,k),Gauge(C,n+1)*(i1,k)) meets Upper_Arc C
proof
  let C be Simple_closed_curve;
  let i1,i2,j,k be Element of NAT;
  set G=Gauge(C,n+1);
  assume that
A1: 1 < i1 & i1 < len G and
A2: 1 < i2 & i2 < len G and
A3: 1 <= j & j <= k & k <= width G and
A4: G*(i1,k) in Lower_Arc L~Cage(C,n+1) and
A5: G*(i2,j) in Upper_Arc L~Cage(C,n+1);
A6: Lower_Arc L~Cage(C,n+1) = L~Lower_Seq(C,n+1) by JORDAN1G:64;
A7: Upper_Arc L~Cage(C,n+1) = L~Upper_Seq(C,n+1) by JORDAN1G:63;
A8: Lower_Seq(C,n+1) is_sequence_on G by JORDAN1G:5;
A9: Upper_Seq(C,n+1) is_sequence_on G by JORDAN1G:4;
A10: 1 <= j & j <= width G by A3,XXREAL_0:2;
  then
A11: [i2,j] in Indices G by A2,GOBOARD7:10;
A12: 1 <= k & k <= width G by A3,XXREAL_0:2;
  then
A13: [i2,k] in Indices G by A2,GOBOARD7:10;
  G*(i2,j)`1 = G*(i2,1)`1 by A2,A10,GOBOARD5:3
    .= G*(i2,k)`1 by A2,A12,GOBOARD5:3;
  then
A14: LSeg(G*(i2,j),G*(i2,k)) is vertical by SPPOL_1:37;
  G*(i2,k)`2 = G*(1,k)`2 by A2,A12,GOBOARD5:2
    .= G*(i1,k)`2 by A1,A12,GOBOARD5:2;
  then
A15: LSeg(G*(i2,k),G*(i1,k)) is horizontal by SPPOL_1:36;
A16: [i2,k] in Indices G by A2,A12,GOBOARD7:10;
A17: [i1,k] in Indices G by A1,A12,GOBOARD7:10;
  now per cases;
    suppose
A18:  LSeg(G*(i2,j),G*(i2,k)) meets Lower_Arc L~Cage(C,n+1);
      then consider m be Element of NAT such that
A19:  j <= m & m <= k and
A20:  G*(i2,m)`2 = inf(proj2.:(LSeg(G*(i2,j),G*(i2,k))
      /\ L~Lower_Seq(C,n+1))) by A3,A6,A8,A11,A13,JORDAN1F:1;
A21:  1 <= m & m <= width G by A3,A19,XXREAL_0:2;
      set X = LSeg(G*(i2,j),G*(i2,k)) /\ L~Lower_Seq(C,n+1);
A22:  G*(i2,m)`1 = G*(i2,1)`1 by A2,A21,GOBOARD5:3;
      then
A23:  |[G*(i2,1)`1,inf(proj2.:X)]| = G*(i2,m) by A20,EUCLID:57;
      then
A24:  G*(i2,j)`1 = |[G*(i2,1)`1,inf(proj2.:X)]|`1 by A2,A10,A22, GOBOARD5:3;
      ex x be set st x in LSeg(G*(i2,j),G*(i2,k)) &
      x in L~Lower_Seq(C,n+1) by A6,A18,XBOOLE_0:3;
      then reconsider X1=X as non empty compact Subset of TOP-REAL 2
      by PSCOMP_1:64,XBOOLE_0:def 3;
      consider pp be set such that
A25:  pp in S-most X1 by XBOOLE_0:def 1;
      reconsider pp as Point of TOP-REAL 2 by A25;
A26:  pp in X by A25,XBOOLE_0:def 3;
      then
A27:  pp in L~Lower_Seq(C,n+1) by XBOOLE_0:def 3;
      pp in LSeg(G*(i2,j),G*(i2,k)) by A26,XBOOLE_0:def 3;
      then
A28:  pp`1 = |[G*(i2,1)`1,inf(proj2.:X)]|`1 by A14,A24,SPPOL_1:64;
      |[G*(i2,1)`1,inf(proj2.:X)]|`2 = S-bound X by A20,A23,SPRECT_1:49
        .= (S-min X)`2 by EUCLID:56
        .= pp`2 by A25,PSCOMP_1:118;
      then G*(i2,m) in Lower_Arc L~Cage(C,n+1) by A6,A23,A27,A28,TOPREAL3:11;
      then LSeg(G*(i2,j),G*(i2,m)) meets Upper_Arc C by A2,A3,A5,A19,A21,Th19;
      then LSeg(G*(i2,j),G*(i2,k)) meets Upper_Arc C by A2,A3,A19,JORDAN15:7,
      XBOOLE_1:63;
      hence LSeg(G*(i2,j),G*(i2,k)) \/
      LSeg(G*(i2,k),G*(i1,k)) meets Upper_Arc C by XBOOLE_1:70;
    end;
    suppose
A29:  LSeg(G*(i2,k),G*(i1,k)) meets Upper_Arc L~Cage(C,n+1) & i2 <= i1;
      then consider m be Element of NAT such that
A30:  i2 <= m & m <= i1 and
A31:  G*(m,k)`1 = sup(proj1.:(LSeg(G*(i2,k),G*(i1,k))
      /\ L~Upper_Seq(C,n+1))) by A7,A9,A16,A17,JORDAN1F:4;
A32:  1 < m & m < len G by A1,A2,A30,XXREAL_0:2;
      set X = LSeg(G*(i2,k),G*(i1,k)) /\ L~Upper_Seq(C,n+1);
A33:  G*(m,k)`2 = G*(1,k)`2 by A12,A32,GOBOARD5:2;
      then
A34:  |[sup(proj1.:X),G*(1,k)`2]| = G*(m,k) by A31,EUCLID:57;
      then
A35:  G*(i2,k)`2 = |[sup(proj1.:X),G*(1,k)`2]|`2 by A2,A12,A33, GOBOARD5:2;
      ex x be set st x in LSeg(G*(i2,k),G*(i1,k)) &
      x in L~Upper_Seq(C,n+1) by A7,A29,XBOOLE_0:3;
      then reconsider X1=X as non empty compact Subset of TOP-REAL 2
      by PSCOMP_1:64,XBOOLE_0:def 3;
      consider pp be set such that
A36:  pp in E-most X1 by XBOOLE_0:def 1;
      reconsider pp as Point of TOP-REAL 2 by A36;
A37:  pp in X by A36,XBOOLE_0:def 3;
      then
A38:  pp in L~Upper_Seq(C,n+1) by XBOOLE_0:def 3;
      pp in LSeg(G*(i2,k),G*(i1,k)) by A37,XBOOLE_0:def 3;
      then
A39:  pp`2 = |[sup(proj1.:X),G*(1,k)`2]|`2 by A15,A35,SPPOL_1:63;
      |[sup(proj1.:X),G*(1,k)`2]|`1 = E-bound X by A31,A34,SPRECT_1:51
        .= (E-min X)`1 by EUCLID:56
        .= pp`1 by A36,PSCOMP_1:108;
      then G*(m,k) in Upper_Arc L~Cage(C,n+1) by A7,A34,A38,A39,TOPREAL3:11;
      then LSeg(G*(m,k),G*(i1,k)) meets Upper_Arc C
      by A1,A4,A12,A30,A32,JORDAN15:43;
      then LSeg(G*(i2,k),G*(i1,k)) meets Upper_Arc C by A1,A2,A12,A30,JORDAN15
      :8,XBOOLE_1:63;
      hence LSeg(G*(i2,j),G*(i2,k)) \/
      LSeg(G*(i2,k),G*(i1,k)) meets Upper_Arc C by XBOOLE_1:70;
    end;
    suppose
A40:  LSeg(G*(i2,k),G*(i1,k)) meets Upper_Arc L~Cage(C,n+1) & i1 < i2;
      then consider m be Element of NAT such that
A41:  i1 <= m & m <= i2 and
A42:  G*(m,k)`1 = inf(proj1.:(LSeg(G*(i1,k),G*(i2,k))
      /\ L~Upper_Seq(C,n+1))) by A7,A9,A16,A17,JORDAN1F:3;
A43:  1 < m & m < len G by A1,A2,A41,XXREAL_0:2;
      set X = LSeg(G*(i1,k),G*(i2,k)) /\ L~Upper_Seq(C,n+1);
A44:  G*(m,k)`2 = G*(1,k)`2 by A12,A43,GOBOARD5:2;
      then
A45:  |[inf(proj1.:X),G*(1,k)`2]| = G*(m,k) by A42,EUCLID:57;
      then
A46:  G*(i1,k)`2 = |[inf(proj1.:X),G*(1,k)`2]|`2 by A1,A12,A44, GOBOARD5:2;
      ex x be set st x in LSeg(G*(i1,k),G*(i2,k)) &
      x in L~Upper_Seq(C,n+1) by A7,A40,XBOOLE_0:3;
      then reconsider X1=X as non empty compact Subset of TOP-REAL 2
      by PSCOMP_1:64,XBOOLE_0:def 3;
      consider pp be set such that
A47:  pp in W-most X1 by XBOOLE_0:def 1;
      reconsider pp as Point of TOP-REAL 2 by A47;
A48:  pp in X by A47,XBOOLE_0:def 3;
      then
A49:  pp in L~Upper_Seq(C,n+1) by XBOOLE_0:def 3;
      pp in LSeg(G*(i1,k),G*(i2,k)) by A48,XBOOLE_0:def 3;
      then
A50:  pp`2 = |[inf(proj1.:X),G*(1,k)`2]|`2 by A15,A46,SPPOL_1:63;
      |[inf(proj1.:X),G*(1,k)`2]|`1 = W-bound X by A42,A45,SPRECT_1:48
        .= (W-min X)`1 by EUCLID:56
        .= pp`1 by A47,PSCOMP_1:88;
      then G*(m,k) in Upper_Arc L~Cage(C,n+1) by A7,A45,A49,A50,TOPREAL3:11;
      then LSeg(G*(i1,k),G*(m,k)) meets Upper_Arc C
      by A1,A4,A12,A41,A43,JORDAN15:35;
      then LSeg(G*(i1,k),G*(i2,k)) meets Upper_Arc C by A1,A2,A12,A41,JORDAN15
      :8,XBOOLE_1:63;
      hence LSeg(G*(i2,j),G*(i2,k)) \/
      LSeg(G*(i2,k),G*(i1,k)) meets Upper_Arc C by XBOOLE_1:70;
    end;
    suppose
A51:  LSeg(G*(i2,j),G*(i2,k)) misses Lower_Arc L~Cage(C,n+1) &
      LSeg(Gauge(C,n+1)*(i2,k),Gauge(C,n+1)*(i1,k))
      misses Upper_Arc L~Cage(C,n+1);
      consider j1 be Element of NAT such that
A52:  j <= j1 & j1 <= k and
A53:  LSeg(G*(i2,j1),G*(i2,k)) /\ L~Upper_Seq(C,n+1) =
      {G*(i2,j1)} by A2,A3,A5,A7,JORDAN15:17;
      G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) /\
      L~Upper_Seq(C,n+1) by A53,TARSKI:def 1;
      then
A54:  G*(i2,j1) in L~Upper_Seq(C,n+1) by XBOOLE_0:def 3;
A55:  1 <= j1 & j1 <= width G by A3,A52,XXREAL_0:2;
      now per cases;
        suppose i2 <= i1;
          then consider i3 be Element of NAT such that
A56:      i2 <= i3 & i3 <= i1 and
A57:      LSeg(G*(i2,k),G*(i3,k)) /\ L~Lower_Seq(C,n+1) = {G*(i3,k)}
          by A1,A2,A4,A6,A12,JORDAN15:21;
A58:      1 < i3 & i3 < len G by A1,A2,A56,XXREAL_0:2;
          G*(i3,k) in LSeg(G*(i2,k),G*(i3,k)) /\ L~Lower_Seq(C,n+1)
          by A57,TARSKI:def 1;
          then
A59:      G*(i3,k) in L~Lower_Seq(C,n+1) by XBOOLE_0:def 3;
A60:      LSeg(G*(i2,j1),G*(i2,k)) c=
          LSeg(G*(i2,j),G*(i2,k)) by A2,A3,A52,JORDAN15:7;
A61:      LSeg(G*(i2,k),G*(i3,k)) c= LSeg(G*(i2,k),G*(i1,k))
          by A1,A2,A12,A56,JORDAN15:8;
          then
A62:      LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) c=

LSeg(G*(i2,j),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i1,k)) by A60,XBOOLE_1:13;
A63:      (LSeg(G*(i2,j1),G*(i2,k)) \/
          LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1) = {G*(i3,k)}
          proof
            thus (LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1) c= {G*(i3,k)}
            proof
              let x be set;
              assume x in (LSeg(G*(i2,j1),G*(i2,k)) \/
              LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1);
              then x in LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) &
              x in L~Lower_Seq(C,n+1) by XBOOLE_0:def 3;
              then (x in LSeg(G*(i2,j1),G*(i2,k)) or
              x in LSeg(G*(i2,k),G*(i3,k))) &
              x in L~Lower_Seq(C,n+1) by XBOOLE_0:def 2;
              hence x in {G*(i3,k)} by A6,A51,A57,A60,XBOOLE_0:3,def 3;
            end;
            let x be set;
            assume x in {G*(i3,k)};
            then
A64:        x = G*(i3,k) by TARSKI:def 1;
            G*(i3,k) in LSeg(G*(i2,k),G*(i3,k)) by TOPREAL1:6;
            then G*(i3,k) in LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k)) by XBOOLE_0:def 2;
            hence x in (LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1)
            by A59,A64,XBOOLE_0:def 3;
          end;
          (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k))) /\
          L~Upper_Seq(C,n+1) = {G*(i2,j1)}
          proof
            thus (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k))) /\
            L~Upper_Seq(C,n+1) c= {G*(i2,j1)}
            proof
              let x be set;
              assume x in (LSeg(G*(i2,j1),G*(i2,k)) \/
              LSeg(G*(i2,k),G*(i3,k))) /\ L~Upper_Seq(C,n+1);
              then x in LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) &
              x in L~Upper_Seq(C,n+1) by XBOOLE_0:def 3;
              then (x in LSeg(G*(i2,j1),G*(i2,k)) or
              x in LSeg(G*(i2,k),G*(i3,k))) &
              x in L~Upper_Seq(C,n+1) by XBOOLE_0:def 2;
              hence x in {G*(i2,j1)} by A7,A51,A53,A61,XBOOLE_0:3,def 3;
            end;
            let x be set;
            assume x in {G*(i2,j1)};
            then
A65:        x = G*(i2,j1) by TARSKI:def 1;
            G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) by TOPREAL1:6;
            then G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k)) by XBOOLE_0:def 2;
            hence x in (LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k))) /\ L~Upper_Seq(C,n+1)
            by A54,A65,XBOOLE_0:def 3;
          end;
          hence LSeg(G*(i2,j),G*(i2,k)) \/

LSeg(G*(i2,k),G*(i1,k)) meets Upper_Arc C by A2,A3,A52,A55,A56,A58,A62
          ,A63,Th21,XBOOLE_1:63;
        end;
        suppose i1 < i2;
          then consider i3 be Element of NAT such that
A66:      i1 <= i3 & i3 <= i2 and
A67:      LSeg(G*(i3,k),G*(i2,k)) /\ L~Lower_Seq(C,n+1) = {G*(i3,k)}
          by A1,A2,A4,A6,A12,JORDAN15:14;
A68:      1 < i3 & i3 < len G by A1,A2,A66,XXREAL_0:2;
          G*(i3,k) in LSeg(G*(i2,k),G*(i3,k)) /\ L~Lower_Seq(C,n+1)
          by A67,TARSKI:def 1;
          then
A69:      G*(i3,k) in L~Lower_Seq(C,n+1) by XBOOLE_0:def 3;
A70:      LSeg(G*(i2,j1),G*(i2,k)) c=
          LSeg(G*(i2,j),G*(i2,k)) by A2,A3,A52,JORDAN15:7;
A71:      LSeg(G*(i2,k),G*(i3,k)) c= LSeg(G*(i2,k),G*(i1,k))
          by A1,A2,A12,A66,JORDAN15:8;
          then
A72:      LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) c=

LSeg(G*(i2,j),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i1,k)) by A70,XBOOLE_1:13;
A73:      (LSeg(G*(i2,j1),G*(i2,k)) \/
          LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1) = {G*(i3,k)}
          proof
            thus (LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1) c= {G*(i3,k)}
            proof
              let x be set;
              assume x in (LSeg(G*(i2,j1),G*(i2,k)) \/
              LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1);
              then x in LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) &
              x in L~Lower_Seq(C,n+1) by XBOOLE_0:def 3;
              then (x in LSeg(G*(i2,j1),G*(i2,k)) or x in LSeg(G*(i2,k),G*(i3
              ,k))) & x in L~Lower_Seq(C,n+1) by XBOOLE_0:def 2;
              hence x in {G*(i3,k)} by A6,A51,A67,A70,XBOOLE_0:3,def 3;
            end;
            let x be set;
            assume x in {G*(i3,k)};
            then
A74:        x = G*(i3,k) by TARSKI:def 1;
            G*(i3,k) in LSeg(G*(i2,k),G*(i3,k)) by TOPREAL1:6;
            then G*(i3,k) in LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k)) by XBOOLE_0:def 2;
            hence x in (LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k))) /\ L~Lower_Seq(C,n+1)
            by A69,A74,XBOOLE_0:def 3;
          end;
          (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k))) /\
          L~Upper_Seq(C,n+1) = {G*(i2,j1)}
          proof
            thus (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k))) /\
            L~Upper_Seq(C,n+1) c= {G*(i2,j1)}
            proof
              let x be set;
              assume x in (LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k )
              )) /\ L~Upper_Seq(C,n+1);
              then x in LSeg(G*(i2,j1),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i3,k)) &
              x in L~Upper_Seq(C,n+1) by XBOOLE_0:def 3;
              then (x in LSeg(G*(i2,j1),G*(i2,k)) or x in LSeg(G*(i2,k),G*(i3
              ,k))) & x in L~Upper_Seq(C,n+1) by XBOOLE_0:def 2;
              hence x in {G*(i2,j1)} by A7,A51,A53,A71,XBOOLE_0:3,def 3;
            end;
            let x be set;
            assume x in {G*(i2,j1)};
            then
A75:        x = G*(i2,j1) by TARSKI:def 1;
            G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) by TOPREAL1:6;
            then G*(i2,j1) in LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k)) by XBOOLE_0:def 2;
            hence x in (LSeg(G*(i2,j1),G*(i2,k)) \/
            LSeg(G*(i2,k),G*(i3,k))) /\ L~Upper_Seq(C,n+1)
            by A54,A75,XBOOLE_0:def 3;
          end;
          hence LSeg(G*(i2,j),G*(i2,k)) \/

LSeg(G*(i2,k),G*(i1,k)) meets Upper_Arc C by A2,A3,A52,A55,A66,A68,A72
          ,A73,Th23,XBOOLE_1:63;
        end;
      end;
      hence LSeg(G*(i2,j),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i1,k))
      meets Upper_Arc C;
    end;
  end;
  hence LSeg(G*(i2,j),G*(i2,k)) \/ LSeg(G*(i2,k),G*(i1,k)) meets Upper_Arc C;
end;

theorem Th27:
  for C be Simple_closed_curve
  for p be Point of TOP-REAL 2 st W-bound C < p`1 & p`1 < E-bound C holds
  not(p in North_Arc C & p in South_Arc C)
proof
  let C be Simple_closed_curve;
  let p be Point of TOP-REAL 2;
  reconsider p' = p as Point of Euclid 2 by EUCLID:25;
  assume that
A1: W-bound C < p`1 and
A2: p`1 < E-bound C and
A3: p in North_Arc C and
A4: p in South_Arc C;
  set s = min(p`1 - W-bound C,E-bound C - p`1);
  W-bound C = W-bound C + 0 & p`1 = p`1 + 0;
  then p`1 - W-bound C > 0 & E-bound C - p`1 > 0 by A1,A2,XREAL_1:22;
  then
A5: s > 0 by XXREAL_0:15;
  now
    let r be real number;
    reconsider rr = r as Real by XREAL_0:def 1;
    assume
A6: 0 < r & r < s;
    then
A7: r/8 > 0 by REAL_2:127;
    then reconsider G = Ball(p',r/8) as a_neighborhood of p by GOBOARD6:5;
    consider k1 be Element of NAT such that
A8: for m be Element of NAT st m > k1
    holds (Upper_Appr C).m meets G by A3,KURATO_2:def 11;
    consider k2 be Element of NAT such that
A9: for m be Element of NAT st m > k2
    holds (Lower_Appr C).m meets G by A4,KURATO_2:def 11;
    set k = max(k1,k2);
A10: k >= k1 by XXREAL_0:25;
    set z' = max(N-bound C - S-bound C,E-bound C - W-bound C);
    set z = max(z',r/8);
    z >= r/8 by XXREAL_0:25;
    then z/(r/8) >= 1 by A7,REAL_2:143;
    then log(2,z/(r/8)) >= log(2,1) by PRE_FF:12;
    then log(2,z/(r/8)) >= 0 by POWER:59;
    then reconsider m' = [\ log(2,z/(r/8)) /] as Element of NAT by INT_1:80;
A11: 2 to_power (m'+1) > 0 by POWER:39;
    set N = 2 to_power (m'+1);
    log(2,z/(r/8)) < (m'+1) * 1 by INT_1:52;
    then log(2,z/(r/8)) < (m'+1) * log(2,2) by POWER:60;
    then log(2,z/(r/8)) < log(2,2 to_power (m'+1)) by POWER:63;
    then z/(r/8) < N by A11,PRE_FF:12;
    then z/(r/8)*(r/8) < N*(r/8) by A7,XREAL_1:70;
    then z < N*(r/8) by A7,XCMPLX_1:88;
    then z/N < N*(r/8)/N by A11,REAL_1:73;
    then z/N < (r/8)/N*N by XCMPLX_1:75;
    then
A12: z/N < r/8 by A11,XCMPLX_1:88;
    z >= z' by XXREAL_0:25;
    then z/N >= z'/N by A11,REAL_1:73;
    then
A13: z'/N < r/8 by A12,XXREAL_0:2;
    set m = max(k,m')+1;
A14: len Gauge(C,m) = width Gauge(C,m) by JORDAN8:def 1;
    max(k,m') >= k by XXREAL_0:25;
    then max(k,m') >= k1 by A10,XXREAL_0:2;
    then m > k1 by NAT_1:13;
    then (Upper_Appr C).m meets G by A8;
    then Upper_Arc L~Cage (C,m) meets G by Def1;
    then consider p1 be set such that
A15: p1 in Upper_Arc L~Cage (C,m) and
A16: p1 in G by XBOOLE_0:3;
    reconsider p1 as Point of TOP-REAL 2 by A15;
    reconsider p1' = p1 as Point of Euclid 2 by EUCLID:25;
    set f = Upper_Seq(C,m);
A17: Upper_Arc L~Cage(C,m) = L~Upper_Seq(C,m) by JORDAN1G:63;
    then consider i1 be Element of NAT such that
A18: 1 <= i1 & i1+1 <= len f and
A19: p1 in LSeg(f/.i1,f/.(i1+1)) by A15,SPPOL_2:14;
    reconsider c1 = f/.i1 as Point of Euclid 2 by EUCLID:25;
    reconsider c2 = f/.(i1+1) as Point of Euclid 2 by EUCLID:25;
A20: f is_sequence_on Gauge(C,m) by JORDAN1G:4;
    i1 < len f by A18,NAT_1:13;
    then i1 in Seg len f by A18,FINSEQ_1:3;
    then
A21: i1 in dom f by FINSEQ_1:def 3;
    then consider ii1,jj1 be Element of NAT such that
A22: [ii1,jj1] in Indices Gauge(C,m) and
A23: f/.i1 = Gauge(C,m)*(ii1,jj1) by A20,GOBOARD1:def 11;
    N-bound C > S-bound C+0 & E-bound C > W-bound C+0 by TOPREAL5:22,23;
    then
A24: N-bound C - S-bound C > 0 & E-bound C - W-bound C > 0 by XREAL_1: 22;
A25: 2|^(m'+1) > 0 by A11,POWER:48;
    max(k,m') >= m' by XXREAL_0:25;
    then m > m' by NAT_1:13;
    then m >= m'+1 by NAT_1:13;
    then 2|^m >= 2|^(m'+1) by PREPOWER:107;
    then
A26: (N-bound C - S-bound C)/2|^m<=(N-bound C - S-bound C)/2|^(m'+1)
    & (E-bound C - W-bound C)/2|^m <= (E-bound C - W-bound C)/2|^(m'+1)
    by A24,A25,REAL_2:201;
    N-bound C - S-bound C <= z' & E-bound C - W-bound C <= z' by XXREAL_0:25;
    then (N-bound C - S-bound C)/N <= z'/N &
    (E-bound C - W-bound C)/N <= z'/N by A11,REAL_1:73;
    then (N-bound C - S-bound C)/2|^(m'+1) <= z'/N &
    (E-bound C - W-bound C)/2|^(m'+1) <= z'/N by POWER:48;
    then
A27: (N-bound C - S-bound C)/2|^m <= z'/N &
    (E-bound C - W-bound C)/2|^m <= z'/N by A26,XXREAL_0:2;
    then dist(f/.i1,f/.(i1+1)) <= z'/N by A18,A20,Th6;
    then dist(f/.i1,f/.(i1+1)) < r/8 by A13,XXREAL_0:2;
    then dist(c1,c2) < r/8 by TOPREAL6:def 1;
    then
A28: |. f/.i1 - f/.(i1+1) .| < r/8 by SPPOL_1:62;
    |. p1 - f/.i1 .| <= |. f/.i1 - f/.(i1+1) .| by A19,JGRAPH_1:53;
    then
A29: |. p1 - f/.i1 .| < r/8 by A28,XXREAL_0:2;
    dist(p1',p') < r/8 by A16,METRIC_1:12;
    then |. p-p1 .| < r/8 by SPPOL_1:62;
    then
A30: |. p-p1 .| + |. p1 - f/.i1 .| < r/(2*4) + r/(2*4) by A29, XREAL_1 :10;
    |. p - f/.i1 .| <= |. p - p1 .| + |. p1 - f/.i1 .| by TOPRNS_1:35;
    then
A31: |. p - f/.i1 .| < r/4 by A30,XXREAL_0:2;
    then
A32: dist(p',c1) < r/4 by SPPOL_1:62;
    then
A33: f/.i1 in Ball(p',r/4) by METRIC_1:12;
A34: f/.i1 in Upper_Arc L~Cage(C,m) by A17,A21,SPPOL_2:48;
A35: k >= k2 by XXREAL_0:25;
    max(k,m') >= k by XXREAL_0:25;
    then max(k,m') >= k2 by A35,XXREAL_0:2;
    then m > k2 by NAT_1:13;
    then (Lower_Appr C).m meets G by A9;
    then Lower_Arc L~Cage (C,m) meets G by Def2;
    then consider p2 be set such that
A36: p2 in Lower_Arc L~Cage (C,m) and
A37: p2 in G by XBOOLE_0:3;
    reconsider p2 as Point of TOP-REAL 2 by A36;
    reconsider p2' = p2 as Point of Euclid 2 by EUCLID:25;
    set g = Lower_Seq(C,m);
A38: Lower_Arc L~Cage(C,m) = L~Lower_Seq(C,m) by JORDAN1G:64;
    then consider i2 be Element of NAT such that
A39: 1 <= i2 & i2+1 <= len g and
A40: p2 in LSeg(g/.i2,g/.(i2+1)) by A36,SPPOL_2:14;
    reconsider d1 = g/.i2 as Point of Euclid 2 by EUCLID:25;
    reconsider d2 = g/.(i2+1) as Point of Euclid 2 by EUCLID:25;
A41: g is_sequence_on Gauge(C,m) by JORDAN1G:5;
    i2 < len g by A39,NAT_1:13;
    then i2 in Seg len g by A39,FINSEQ_1:3;
    then
A42: i2 in dom g by FINSEQ_1:def 3;
    then consider ii2,jj2 be Element of NAT such that
A43: [ii2,jj2] in Indices Gauge(C,m) and
A44: g/.i2 = Gauge(C,m)*(ii2,jj2) by A41,GOBOARD1:def 11;
    dist(g/.i2,g/.(i2+1)) <= z'/N by A27,A39,A41,Th6;
    then dist(g/.i2,g/.(i2+1)) < r/8 by A13,XXREAL_0:2;
    then dist(d1,d2) < r/8 by TOPREAL6:def 1;
    then
A45: |. g/.i2 - g/.(i2+1) .| < r/8 by SPPOL_1:62;
    |. p2 - g/.i2 .| <= |. g/.i2 - g/.(i2+1) .| by A40,JGRAPH_1:53;
    then
A46: |. p2 - g/.i2 .| < r/8 by A45,XXREAL_0:2;
    dist(p2',p') < r/8 by A37,METRIC_1:12;
    then |. p-p2 .| < r/8 by SPPOL_1:62;
    then
A47: |. p-p2 .| + |. p2 - g/.i2 .| < r/(2*4) + r/(2*4) by A46, XREAL_1 :10;
    |. p - g/.i2 .| <= |. p - p2 .| + |. p2 - g/.i2 .| by TOPRNS_1:35;
    then
A48: |. p - g/.i2 .| < r/4 by A47,XXREAL_0:2;
    then
A49: dist(p',d1) < r/4 by SPPOL_1:62;
    then
A50: g/.i2 in Ball(p',r/4) by METRIC_1:12;
A51: g/.i2 in Lower_Arc L~Cage(C,m) by A38,A42,SPPOL_2:48;
    set Gij = Gauge(C,m)*(ii2,jj1);
    set Gji = Gauge(C,m)*(ii1,jj2);
    reconsider Gij' = Gij, Gji' = Gji as Point of Euclid 2 by EUCLID:25;
A52: 1 <= ii1 & ii1 <= len Gauge(C,m) &
    1 <= jj1 & jj1 <= width Gauge(C,m) by A22,GOBOARD5:1;
A53: 1 <= ii2 & ii2 <= len Gauge(C,m) &
    1 <= jj2 & jj2 <= width Gauge(C,m) by A43,GOBOARD5:1;
    len f >= 3 & len g >= 3 by JORDAN1E:19;
    then len f >= 1 & len g >= 1 by XXREAL_0:2;
    then len f in Seg len f & len g in Seg len g by FINSEQ_1:3;
    then
A54: len f in dom f & len g in dom g by FINSEQ_1:def 3;
A55: r/4 < r by A6,XREAL_1:225;
A56: r/2 < r by A6,XREAL_1:218;
A57: s <= p`1 - W-bound C & s <= E-bound C - p`1 by XXREAL_0:17;
A58: now
      assume 1 >= ii1;
      then
A59:  ii1 = 1 by A52,XXREAL_0:1;
      dist(p',c1) < r by A32,A55,XXREAL_0:2;
      then dist(p',c1) < s by A6,XXREAL_0:2;
      then
A60:  dist(p',c1) < p`1 - W-bound C by A57,XXREAL_0:2;
A61:  p`1-(f/.i1)`1 <= abs(p`1-(f/.i1)`1) by ABSVALUE:11;
      abs(p`1-(f/.i1)`1) <= |.p-f/.i1.| by JGRAPH_1:51;
      then p`1 - (f/.i1)`1 <= |.p-f/.i1.| by A61,XXREAL_0:2;
      then p`1 - W-bound L~Cage(C,m) <= |.p-f/.i1.|
      by A14,A23,A52,A59,JORDAN1A:94;
      then p`1 - W-bound L~Cage(C,m) <= dist(p',c1) by SPPOL_1:62;
      then p`1 - W-bound L~Cage(C,m) < p`1 - W-bound C by A60,XXREAL_0:2;
      then W-bound L~Cage(C,m) > W-bound C by REAL_1:92;
      hence contradiction by Th12;
    end;
A62: now
      assume ii1 >= len Gauge(C,m);
      then
A63:  ii1 = len Gauge(C,m) by A52,XXREAL_0:1;
      (Gauge(C,m)*(len Gauge(C,m),jj1))`1 = E-bound L~Cage(C,m)
      by A14,A52,JORDAN1A:92;
      then f/.i1 = E-max L~Cage(C,m) by A17,A21,A23,A63,JORDAN1J:46,SPPOL_2:48
        .= f/.len f by JORDAN1F:7;
      then i1 = len f by A21,A54,PARTFUN2:17;
      hence contradiction by A18,NAT_1:13;
    end;
A64: now
      assume ii2 <= 1;
      then
A65:  ii2 = 1 by A53,XXREAL_0:1;
      (Gauge(C,m)*(1,jj2))`1 = W-bound L~Cage(C,m) by A14,A53,JORDAN1A:94;
      then g/.i2 = W-min L~Cage(C,m) by A38,A42,A44,A65,JORDAN1J:47,SPPOL_2:48
        .= g/.len g by JORDAN1F:8;
      then i2 = len g by A42,A54,PARTFUN2:17;
      hence contradiction by A39,NAT_1:13;
    end;
A66: now
      assume ii2 >= len Gauge(C,m);
      then
A67:  ii2 = len Gauge(C,m) by A53,XXREAL_0:1;
      dist(p',d1) < r by A49,A55,XXREAL_0:2;
      then dist(p',d1) < s by A6,XXREAL_0:2;
      then
A68:  dist(p',d1) < E-bound C - p`1 by A57,XXREAL_0:2;
A69:  (g/.i2)`1-p`1 <= abs((g/.i2)`1-p`1) by ABSVALUE:11;
      abs((g/.i2)`1-p`1) <= |.g/.i2-p.| by JGRAPH_1:51;
      then abs((g/.i2)`1-p`1) <= |.p-g/.i2.| by TOPRNS_1:28;
      then (g/.i2)`1 - p`1 <= |.p-g/.i2.| by A69,XXREAL_0:2;
      then E-bound L~Cage(C,m) - p`1 <= |.p-g/.i2.|
      by A14,A44,A53,A67,JORDAN1A:92;
      then E-bound L~Cage(C,m) - p`1 <= dist(p',d1) by SPPOL_1:62;
      then E-bound L~Cage(C,m) - p`1 < E-bound C - p`1 by A68,XXREAL_0:2;
      then E-bound L~Cage(C,m) < E-bound C by REAL_1:92;
      hence contradiction by Th10;
    end;
A70: Ball(p',rr/4) c= Ball(p',rr) by A55,PCOMPS_1:1;
A71: Gij`1 = Gauge(C,m)*(ii2,1)`1 by A52,A53,GOBOARD5:3
      .= (g/.i2)`1 by A44,A53,GOBOARD5:3;
A72: Gij`2 = Gauge(C,m)*(1,jj1)`2 by A52,A53,GOBOARD5:2
      .= (f/.i1)`2 by A23,A52,GOBOARD5:2;
A73: Gji`1 = Gauge(C,m)*(ii1,1)`1 by A52,A53,GOBOARD5:3
      .= (f/.i1)`1 by A23,A52,GOBOARD5:3;
A74: Gji`2 = Gauge(C,m)*(1,jj2)`2 by A52,A53,GOBOARD5:2
      .= (g/.i2)`2 by A44,A53,GOBOARD5:2;
    abs((g/.i2)`1-p`1) <= |.(g/.i2)-p.| & abs((f/.i1)`2-p`2) <= |.(f/. i1)-p.|
    by JGRAPH_1:51;
    then abs((g/.i2)`1-p`1) <= |.p-(g/.i2).| &
    abs((f/.i1)`2-p`2) <= |.p-(f/.i1).| by TOPRNS_1:28;
    then abs((g/.i2)`1-p`1) <= r/4 & abs((f/.i1)`2-p`2) <= r/4
    by A31,A48,XXREAL_0:2;
    then abs((g/.i2)`1-p`1) + abs((f/.i1)`2-p`2) <= r/(2*2) + r/(2*2)
    by XREAL_1:9;
    then
A75: abs((g/.i2)`1-p`1) + abs((f/.i1)`2-p`2) < r by A56,XXREAL_0:2;
    abs((f/.i1)`1-p`1) <= |.(f/.i1)-p.| & abs((g/.i2)`2-p`2) <= |.(g/. i2)-p.|
    by JGRAPH_1:51;
    then abs((f/.i1)`1-p`1) <= |.p-(f/.i1).| &
    abs((g/.i2)`2-p`2) <= |.p-(g/.i2).| by TOPRNS_1:28;
    then abs((f/.i1)`1-p`1) <= r/4 & abs((g/.i2)`2-p`2) <= r/4
    by A31,A48,XXREAL_0:2;
    then abs((f/.i1)`1-p`1) + abs((g/.i2)`2-p`2) <= r/(2*2) + r/(2*2)
    by XREAL_1:9;
    then
A76: abs((f/.i1)`1-p`1) + abs((g/.i2)`2-p`2) < r by A56,XXREAL_0:2;
    |.Gij-p.| <= abs((g/.i2)`1-p`1) + abs((f/.i1)`2-p`2)
    by A71,A72,JGRAPH_1:49;
    then |.Gij-p.| < r by A75,XXREAL_0:2;
    then dist(Gij',p') < r by SPPOL_1:62;
    then
A77: Gij in Ball(p',r) by METRIC_1:12;
    |.Gji-p.| <= abs((f/.i1)`1-p`1) + abs((g/.i2)`2-p`2)
    by A73,A74,JGRAPH_1:49;
    then |.Gji-p.| < r by A76,XXREAL_0:2;
    then dist(Gji',p') < r by SPPOL_1:62;
    then
A78: Gji in Ball(p',r) by METRIC_1:12;
A79: LSeg(g/.i2,Gij) c= Ball(p',rr) by A50,A70,A77,TOPREAL3:28;
A80: LSeg(Gij,f/.i1) c= Ball(p',rr) by A33,A70,A77,TOPREAL3:28;
A81: LSeg(g/.i2,Gji) c= Ball(p',rr) by A50,A70,A78,TOPREAL3:28;
A82: LSeg(Gji,f/.i1) c= Ball(p',rr) by A33,A70,A78,TOPREAL3:28;
    now per cases;
      suppose
A83:    jj2 <= jj1;
        LSeg(g/.i2,Gij) \/ LSeg(Gij,f/.i1) c= Ball(p',r)
        proof
          let x be set;
          assume
A84:      x in LSeg(g/.i2,Gij) \/ LSeg(Gij,f/.i1);
          then reconsider x' = x as Point of TOP-REAL 2;
          now per cases by A84,XBOOLE_0:def 2;
            suppose x' in LSeg(g/.i2,Gij);
              hence x' in Ball(p',r) by A79;
            end;
            suppose x' in LSeg(Gij,f/.i1);
              hence x' in Ball(p',r) by A80;
            end;
          end;
          hence x in Ball(p',r);
        end;
        hence Ball(p',r) meets Upper_Arc C by A23,A34,A44,A51,A52,A53,A58,A62,
        A64,A66,A83,JORDAN15:50,XBOOLE_1:63;
      end;
      suppose
A85:    jj1 < jj2;
        LSeg(f/.i1,Gji) \/ LSeg(Gji,g/.i2) c= Ball(p',r)
        proof
          let x be set;
          assume
A86:      x in LSeg(f/.i1,Gji) \/ LSeg(Gji,g/.i2);
          then reconsider x' = x as Point of TOP-REAL 2;
          now per cases by A86,XBOOLE_0:def 2;
            suppose x' in LSeg(f/.i1,Gji);
              hence x' in Ball(p',r) by A82;
            end;
            suppose x' in LSeg(Gji,g/.i2);
              hence x' in Ball(p',r) by A81;
            end;
          end;
          hence x in Ball(p',r);
        end;
        hence Ball(p',r) meets Upper_Arc C by A23,A34,A44,A51,A52,A53,A58,A62,
        A64,A66,A85,Th26,XBOOLE_1:63;
      end;
    end;
    hence Ball(p',r) meets Upper_Arc C;
  end;
  then p in Cl Upper_Arc C by A5,GOBOARD6:96;
  then
A87: p in Upper_Arc C by PRE_TOPC:52;
  now
    let r be real number;
    reconsider rr = r as Real by XREAL_0:def 1;
    assume
A88: 0 < r & r < s;
    then
A89: r/8 > 0 by REAL_2:127;
    then reconsider G = Ball(p',r/8) as a_neighborhood of p by GOBOARD6:5;
    consider k1 be Element of NAT such that
A90: for m be Element of NAT st m > k1
    holds (Upper_Appr C).m meets G by A3,KURATO_2:def 11;
    consider k2 be Element of NAT such that
A91: for m be Element of NAT st m > k2
    holds (Lower_Appr C).m meets G by A4,KURATO_2:def 11;
    set k = max(k1,k2);
A92: k >= k1 by XXREAL_0:25;
    set z' = max(N-bound C - S-bound C,E-bound C - W-bound C);
    set z = max(z',r/8);
    z >= r/8 by XXREAL_0:25;
    then z/(r/8) >= 1 by A89,REAL_2:143;
    then log(2,z/(r/8)) >= log(2,1) by PRE_FF:12;
    then log(2,z/(r/8)) >= 0 by POWER:59;
    then reconsider m' = [\ log(2,z/(r/8)) /] as Element of NAT by INT_1:80;
A93: 2 to_power (m'+1) > 0 by POWER:39;
    set N = 2 to_power (m'+1);
    log(2,z/(r/8)) < (m'+1) * 1 by INT_1:52;
    then log(2,z/(r/8)) < (m'+1) * log(2,2) by POWER:60;
    then log(2,z/(r/8)) < log(2,2 to_power (m'+1)) by POWER:63;
    then z/(r/8) < N by A93,PRE_FF:12;
    then z/(r/8)*(r/8) < N*(r/8) by A89,XREAL_1:70;
    then z < N*(r/8) by A89,XCMPLX_1:88;
    then z/N < N*(r/8)/N by A93,REAL_1:73;
    then z/N < (r/8)/N*N by XCMPLX_1:75;
    then
A94: z/N < r/8 by A93,XCMPLX_1:88;
    z >= z' by XXREAL_0:25;
    then z/N >= z'/N by A93,REAL_1:73;
    then
A95: z'/N < r/8 by A94,XXREAL_0:2;
    set m = max(k,m')+1;
A96: len Gauge(C,m) = width Gauge(C,m) by JORDAN8:def 1;
    max(k,m') >= k by XXREAL_0:25;
    then max(k,m') >= k1 by A92,XXREAL_0:2;
    then m > k1 by NAT_1:13;
    then (Upper_Appr C).m meets G by A90;
    then Upper_Arc L~Cage (C,m) meets G by Def1;
    then consider p1 be set such that
A97: p1 in Upper_Arc L~Cage (C,m) and
A98: p1 in G by XBOOLE_0:3;
    reconsider p1 as Point of TOP-REAL 2 by A97;
    reconsider p1' = p1 as Point of Euclid 2 by EUCLID:25;
    set f = Upper_Seq(C,m);
A99: Upper_Arc L~Cage(C,m) = L~Upper_Seq(C,m) by JORDAN1G:63;
    then consider i1 be Element of NAT such that
A100: 1 <= i1 & i1+1 <= len f and
A101: p1 in LSeg(f/.i1,f/.(i1+1)) by A97,SPPOL_2:14;
    reconsider c1 = f/.i1 as Point of Euclid 2 by EUCLID:25;
    reconsider c2 = f/.(i1+1) as Point of Euclid 2 by EUCLID:25;
A102: f is_sequence_on Gauge(C,m) by JORDAN1G:4;
    i1 < len f by A100,NAT_1:13;
    then i1 in Seg len f by A100,FINSEQ_1:3;
    then
A103: i1 in dom f by FINSEQ_1:def 3;
    then consider ii1,jj1 be Element of NAT such that
A104: [ii1,jj1] in Indices Gauge(C,m) and
A105: f/.i1 = Gauge(C,m)*(ii1,jj1) by A102,GOBOARD1:def 11;
    N-bound C > S-bound C+0 & E-bound C > W-bound C+0 by TOPREAL5:22,23;
    then
A106: N-bound C - S-bound C > 0 & E-bound C - W-bound C > 0 by XREAL_1 :22;
A107: 2|^(m'+1) > 0 by A93,POWER:48;
    max(k,m') >= m' by XXREAL_0:25;
    then m > m' by NAT_1:13;
    then m >= m'+1 by NAT_1:13;
    then 2|^m >= 2|^(m'+1) by PREPOWER:107;
    then
A108: (N-bound C - S-bound C)/2|^m<=(N-bound C - S-bound C)/2|^(m'+1)
    & (E-bound C - W-bound C)/2|^m <= (E-bound C - W-bound C)/2|^(m'+1)
    by A106,A107,REAL_2:201;
    N-bound C - S-bound C <= z' & E-bound C - W-bound C <= z' by XXREAL_0:25;
    then (N-bound C - S-bound C)/N <= z'/N &
    (E-bound C - W-bound C)/N <= z'/N by A93,REAL_1:73;
    then (N-bound C - S-bound C)/2|^(m'+1) <= z'/N &
    (E-bound C - W-bound C)/2|^(m'+1) <= z'/N by POWER:48;
    then
A109: (N-bound C - S-bound C)/2|^m <= z'/N &
    (E-bound C - W-bound C)/2|^m <= z'/N by A108,XXREAL_0:2;
    then dist(f/.i1,f/.(i1+1)) <= z'/N by A100,A102,Th6;
    then dist(f/.i1,f/.(i1+1)) < r/8 by A95,XXREAL_0:2;
    then dist(c1,c2) < r/8 by TOPREAL6:def 1;
    then
A110: |. f/.i1 - f/.(i1+1) .| < r/8 by SPPOL_1:62;
    |. p1 - f/.i1 .| <= |. f/.i1 - f/.(i1+1) .| by A101,JGRAPH_1:53;
    then
A111: |. p1 - f/.i1 .| < r/8 by A110,XXREAL_0:2;
    dist(p1',p') < r/8 by A98,METRIC_1:12;
    then |. p-p1 .| < r/8 by SPPOL_1:62;
    then
A112: |. p-p1 .| + |. p1 - f/.i1 .| < r/(2*4) + r/(2*4) by A111, XREAL_1:10;
    |. p - f/.i1 .| <= |. p - p1 .| + |. p1 - f/.i1 .| by TOPRNS_1:35;
    then
A113: |. p - f/.i1 .| < r/4 by A112,XXREAL_0:2;
    then
A114: dist(p',c1) < r/4 by SPPOL_1:62;
    then
A115: f/.i1 in Ball(p',r/4) by METRIC_1:12;
A116: f/.i1 in Upper_Arc L~Cage(C,m) by A99,A103,SPPOL_2:48;
A117: k >= k2 by XXREAL_0:25;
    max(k,m') >= k by XXREAL_0:25;
    then max(k,m') >= k2 by A117,XXREAL_0:2;
    then m > k2 by NAT_1:13;
    then (Lower_Appr C).m meets G by A91;
    then Lower_Arc L~Cage (C,m) meets G by Def2;
    then consider p2 be set such that
A118: p2 in Lower_Arc L~Cage (C,m) and
A119: p2 in G by XBOOLE_0:3;
    reconsider p2 as Point of TOP-REAL 2 by A118;
    reconsider p2' = p2 as Point of Euclid 2 by EUCLID:25;
    set g = Lower_Seq(C,m);
A120: Lower_Arc L~Cage(C,m) = L~Lower_Seq(C,m) by JORDAN1G:64;
    then consider i2 be Element of NAT such that
A121: 1 <= i2 & i2+1 <= len g and
A122: p2 in LSeg(g/.i2,g/.(i2+1)) by A118,SPPOL_2:14;
    reconsider d1 = g/.i2 as Point of Euclid 2 by EUCLID:25;
    reconsider d2 = g/.(i2+1) as Point of Euclid 2 by EUCLID:25;
A123: g is_sequence_on Gauge(C,m) by JORDAN1G:5;
    i2 < len g by A121,NAT_1:13;
    then i2 in Seg len g by A121,FINSEQ_1:3;
    then
A124: i2 in dom g by FINSEQ_1:def 3;
    then consider ii2,jj2 be Element of NAT such that
A125: [ii2,jj2] in Indices Gauge(C,m) and
A126: g/.i2 = Gauge(C,m)*(ii2,jj2) by A123,GOBOARD1:def 11;
    dist(g/.i2,g/.(i2+1)) <= z'/N by A109,A121,A123,Th6;
    then dist(g/.i2,g/.(i2+1)) < r/8 by A95,XXREAL_0:2;
    then dist(d1,d2) < r/8 by TOPREAL6:def 1;
    then
A127: |. g/.i2 - g/.(i2+1) .| < r/8 by SPPOL_1:62;
    |. p2 - g/.i2 .| <= |. g/.i2 - g/.(i2+1) .| by A122,JGRAPH_1:53;
    then
A128: |. p2 - g/.i2 .| < r/8 by A127,XXREAL_0:2;
    dist(p2',p') < r/8 by A119,METRIC_1:12;
    then |. p-p2 .| < r/8 by SPPOL_1:62;
    then
A129: |. p-p2 .| + |. p2 - g/.i2 .| < r/(2*4) + r/(2*4) by A128, XREAL_1:10;
    |. p - g/.i2 .| <= |. p - p2 .| + |. p2 - g/.i2 .| by TOPRNS_1:35;
    then
A130: |. p - g/.i2 .| < r/4 by A129,XXREAL_0:2;
    then
A131: dist(p',d1) < r/4 by SPPOL_1:62;
    then
A132: g/.i2 in Ball(p',r/4) by METRIC_1:12;
A133: g/.i2 in Lower_Arc L~Cage(C,m) by A120,A124,SPPOL_2:48;
    set Gij = Gauge(C,m)*(ii2,jj1);
    set Gji = Gauge(C,m)*(ii1,jj2);
    reconsider Gij' = Gij, Gji' = Gji as Point of Euclid 2 by EUCLID:25;
A134: 1 <= ii1 & ii1 <= len Gauge(C,m) &
    1 <= jj1 & jj1 <= width Gauge(C,m) by A104,GOBOARD5:1;
A135: 1 <= ii2 & ii2 <= len Gauge(C,m) &
    1 <= jj2 & jj2 <= width Gauge(C,m) by A125,GOBOARD5:1;
    len f >= 3 & len g >= 3 by JORDAN1E:19;
    then len f >= 1 & len g >= 1 by XXREAL_0:2;
    then len f in Seg len f & len g in Seg len g by FINSEQ_1:3;
    then
A136: len f in dom f & len g in dom g by FINSEQ_1:def 3;
A137: r/4 < r by A88,XREAL_1:225;
A138: r/2 < r by A88,XREAL_1:218;
A139: s <= p`1 - W-bound C & s <= E-bound C - p`1 by XXREAL_0:17;
A140: now
      assume 1 >= ii1;
      then
A141: ii1 = 1 by A134,XXREAL_0:1;
      dist(p',c1) < r by A114,A137,XXREAL_0:2;
      then dist(p',c1) < s by A88,XXREAL_0:2;
      then
A142: dist(p',c1) < p`1 - W-bound C by A139,XXREAL_0:2;
A143: p`1-(f/.i1)`1 <= abs(p`1-(f/.i1)`1) by ABSVALUE:11;
      abs(p`1-(f/.i1)`1) <= |.p-f/.i1.| by JGRAPH_1:51;
      then p`1 - (f/.i1)`1 <= |.p-f/.i1.| by A143,XXREAL_0:2;
      then p`1 - W-bound L~Cage(C,m) <= |.p-f/.i1.| by A96,A105,A134,A141,
      JORDAN1A:94;
      then p`1 - W-bound L~Cage(C,m) <= dist(p',c1) by SPPOL_1:62;
      then p`1 - W-bound L~Cage(C,m) < p`1 - W-bound C by A142,XXREAL_0:2;
      then W-bound L~Cage(C,m) > W-bound C by REAL_1:92;
      hence contradiction by Th12;
    end;
A144: now
      assume ii1 >= len Gauge(C,m);
      then
A145: ii1 = len Gauge(C,m) by A134,XXREAL_0:1;
      (Gauge(C,m)*(len Gauge(C,m),jj1))`1 = E-bound L~Cage(C,m)
      by A96,A134,JORDAN1A:92;
      then f/.i1 = E-max L~Cage(C,m) by A99,A103,A105,A145,JORDAN1J:46,SPPOL_2
      :48
        .= f/.len f by JORDAN1F:7;
      then i1 = len f by A103,A136,PARTFUN2:17;
      hence contradiction by A100,NAT_1:13;
    end;
A146: now
      assume ii2 <= 1;
      then
A147: ii2 = 1 by A135,XXREAL_0:1;
      (Gauge(C,m)*(1,jj2))`1 = W-bound L~Cage(C,m) by A96,A135,JORDAN1A: 94;
      then g/.i2 = W-min L~Cage(C,m) by A120,A124,A126,A147,JORDAN1J:47,
      SPPOL_2:48
        .= g/.len g by JORDAN1F:8;
      then i2 = len g by A124,A136,PARTFUN2:17;
      hence contradiction by A121,NAT_1:13;
    end;
A148: now
      assume ii2 >= len Gauge(C,m);
      then
A149: ii2 = len Gauge(C,m) by A135,XXREAL_0:1;
      dist(p',d1) < r by A131,A137,XXREAL_0:2;
      then dist(p',d1) < s by A88,XXREAL_0:2;
      then
A150: dist(p',d1) < E-bound C - p`1 by A139,XXREAL_0:2;
A151: (g/.i2)`1-p`1 <= abs((g/.i2)`1-p`1) by ABSVALUE:11;
      abs((g/.i2)`1-p`1) <= |.g/.i2-p.| by JGRAPH_1:51;
      then abs((g/.i2)`1-p`1) <= |.p-g/.i2.| by TOPRNS_1:28;
      then (g/.i2)`1 - p`1 <= |.p-g/.i2.| by A151,XXREAL_0:2;
      then E-bound L~Cage(C,m) - p`1 <= |.p-g/.i2.| by A96,A126,A135,A149,
      JORDAN1A:92;
      then E-bound L~Cage(C,m) - p`1 <= dist(p',d1) by SPPOL_1:62;
      then E-bound L~Cage(C,m) - p`1 < E-bound C - p`1 by A150,XXREAL_0:2;
      then E-bound L~Cage(C,m) < E-bound C by REAL_1:92;
      hence contradiction by Th10;
    end;
A152: Ball(p',rr/4) c= Ball(p',rr) by A137,PCOMPS_1:1;
A153: Gij`1 = Gauge(C,m)*(ii2,1)`1 by A134,A135,GOBOARD5:3
      .= (g/.i2)`1 by A126,A135,GOBOARD5:3;
A154: Gij`2 = Gauge(C,m)*(1,jj1)`2 by A134,A135,GOBOARD5:2
      .= (f/.i1)`2 by A105,A134,GOBOARD5:2;
A155: Gji`1 = Gauge(C,m)*(ii1,1)`1 by A134,A135,GOBOARD5:3
      .= (f/.i1)`1 by A105,A134,GOBOARD5:3;
A156: Gji`2 = Gauge(C,m)*(1,jj2)`2 by A134,A135,GOBOARD5:2
      .= (g/.i2)`2 by A126,A135,GOBOARD5:2;
    abs((g/.i2)`1-p`1) <= |.(g/.i2)-p.| & abs((f/.i1)`2-p`2) <= |.(f/. i1)-p.|
    by JGRAPH_1:51;
    then abs((g/.i2)`1-p`1) <= |.p-(g/.i2).| &
    abs((f/.i1)`2-p`2) <= |.p-(f/.i1).| by TOPRNS_1:28;
    then abs((g/.i2)`1-p`1) <= r/4 & abs((f/.i1)`2-p`2) <= r/4
    by A113,A130,XXREAL_0:2;
    then abs((g/.i2)`1-p`1) + abs((f/.i1)`2-p`2) <= r/(2*2) + r/(2*2)
    by XREAL_1:9;
    then
A157: abs((g/.i2)`1-p`1) + abs((f/.i1)`2-p`2) < r by A138,XXREAL_0:2;
    abs((f/.i1)`1-p`1) <= |.(f/.i1)-p.| & abs((g/.i2)`2-p`2) <= |.(g/. i2)-p.|
    by JGRAPH_1:51;
    then abs((f/.i1)`1-p`1) <= |.p-(f/.i1).| &
    abs((g/.i2)`2-p`2) <= |.p-(g/.i2).| by TOPRNS_1:28;
    then abs((f/.i1)`1-p`1) <= r/4 & abs((g/.i2)`2-p`2) <= r/4
    by A113,A130,XXREAL_0:2;
    then abs((f/.i1)`1-p`1) + abs((g/.i2)`2-p`2) <= r/(2*2) + r/(2*2)
    by XREAL_1:9;
    then
A158: abs((f/.i1)`1-p`1) + abs((g/.i2)`2-p`2) < r by A138,XXREAL_0:2;
    |.Gij-p.| <= abs((g/.i2)`1-p`1) + abs((f/.i1)`2-p`2)
    by A153,A154,JGRAPH_1:49;
    then |.Gij-p.| < r by A157,XXREAL_0:2;
    then dist(Gij',p') < r by SPPOL_1:62;
    then
A159: Gij in Ball(p',r) by METRIC_1:12;
    |.Gji-p.| <= abs((f/.i1)`1-p`1) + abs((g/.i2)`2-p`2)
    by A155,A156,JGRAPH_1:49;
    then |.Gji-p.| < r by A158,XXREAL_0:2;
    then dist(Gji',p') < r by SPPOL_1:62;
    then
A160: Gji in Ball(p',r) by METRIC_1:12;
A161: LSeg(g/.i2,Gij) c= Ball(p',rr) by A132,A152,A159,TOPREAL3:28;
A162: LSeg(Gij,f/.i1) c= Ball(p',rr) by A115,A152,A159,TOPREAL3:28;
A163: LSeg(g/.i2,Gji) c= Ball(p',rr) by A132,A152,A160,TOPREAL3:28;
A164: LSeg(Gji,f/.i1) c= Ball(p',rr) by A115,A152,A160,TOPREAL3:28;
    now per cases;
      suppose
A165:   jj2 <= jj1;
        LSeg(g/.i2,Gij) \/ LSeg(Gij,f/.i1) c= Ball(p',r)
        proof
          let x be set;
          assume
A166:     x in LSeg(g/.i2,Gij) \/ LSeg(Gij,f/.i1);
          then reconsider x' = x as Point of TOP-REAL 2;
          now per cases by A166,XBOOLE_0:def 2;
            suppose x' in LSeg(g/.i2,Gij);
              hence x' in Ball(p',r) by A161;
            end;
            suppose x' in LSeg(Gij,f/.i1);
              hence x' in Ball(p',r) by A162;
            end;
          end;
          hence x in Ball(p',r);
        end;

hence Ball(p',r) meets Lower_Arc C by A105,A116,A126,A133,A134,A135,A140
        ,A144,A146,A148,A165,JORDAN15:51,XBOOLE_1:63;
      end;
      suppose
A167:   jj1 < jj2;
        LSeg(f/.i1,Gji) \/ LSeg(Gji,g/.i2) c= Ball(p',r)
        proof
          let x be set;
          assume
A168:     x in LSeg(f/.i1,Gji) \/ LSeg(Gji,g/.i2);
          then reconsider x' = x as Point of TOP-REAL 2;
          now per cases by A168,XBOOLE_0:def 2;
            suppose x' in LSeg(f/.i1,Gji);
              hence x' in Ball(p',r) by A164;
            end;
            suppose x' in LSeg(Gji,g/.i2);
              hence x' in Ball(p',r) by A163;
            end;
          end;
          hence x in Ball(p',r);
        end;

hence Ball(p',r) meets Lower_Arc C by A105,A116,A126,A133,A134,A135,A140
        ,A144,A146,A148,A167,Th25,XBOOLE_1:63;
      end;
    end;
    hence Ball(p',r) meets Lower_Arc C;
  end;
  then p in Cl Lower_Arc C by A5,GOBOARD6:96;
  then p in Lower_Arc C by PRE_TOPC:52;
  then p in Upper_Arc C /\ Lower_Arc C by A87,XBOOLE_0:def 3;
  then p in {W-min C,E-max C} by JORDAN6:65;
  then p = W-min C or p = E-max C by TARSKI:def 2;
  hence contradiction by A1,A2,EUCLID:56;
end;

theorem :: "Nie oba"
  for C be Simple_closed_curve
  for p be Point of TOP-REAL 2 st p`1 = (W-bound C + E-bound C)/2 holds
  not (p in North_Arc C & p in South_Arc C)
proof
  let C be Simple_closed_curve;
  let p be Point of TOP-REAL 2;
A1: W-bound C < E-bound C by SPRECT_1:33;
  assume p`1 = (W-bound C + E-bound C)/2;
  then W-bound C < p`1 & p`1 < E-bound C by A1,XREAL_1:228;
  hence not (p in North_Arc C & p in South_Arc C) by Th27;
end;

