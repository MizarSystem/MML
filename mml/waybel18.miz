:: Injective Spaces
::  by Jaros{\l}aw Gryko
::
:: Received April 17, 1998
:: Copyright (c) 1998 Association of Mizar Users

environ

 vocabularies BOOLE, FUNCT_1, RELAT_1, SETFAM_1, CANTOR_1, TARSKI, PRE_TOPC,
      PRALG_1, PBOOLE, FUNCOP_1, WAYBEL_3, CARD_3, RLVECT_2, FUNCT_4, BORSUK_1,
      ORDINAL2, FUNCTOR0, PARTFUN1, FUNCT_6, FUNCT_3, GROUP_6, SUBSET_1,
      WAYBEL_1, SGRAPH1, T_0TOPSP, TOPS_2, RELAT_2, ORDERS_1,
      WAYBEL11, YELLOW_9, YELLOW_1, LATTICE3, FILTER_1, WAYBEL_0, QUANTAL1,
      YELLOW_0, FINSET_1, BHSP_3, WAYBEL_8, LATTICES, CAT_1, WAYBEL_9,
      COMPTS_1, WAYBEL18, RLVECT_3, MONOID_0, ARYTM, TSP_1;
 notations TARSKI, XBOOLE_0, ENUMSET1, ZFMISC_1, SUBSET_1, SETFAM_1, RELAT_1,
      FUNCT_1, RELSET_1, PARTFUN1, FUNCT_2, DOMAIN_1, NUMBERS, PRALG_3,
      STRUCT_0, PRE_TOPC, T_0TOPSP, FUNCT_3, FUNCT_6, FUNCT_7, PBOOLE, CARD_3,
      FUNCOP_1, MONOID_0, PRALG_1, ORDERS_2, LATTICE3, YELLOW_1, PRE_CIRC,
      CANTOR_1, FINSET_1, TOPS_2, BORSUK_1, TMAP_1, YELLOW_0, YELLOW_9,
      WAYBEL_0, WAYBEL_3, WAYBEL_8, WAYBEL_9, WAYBEL11;
 constructors SETFAM_1, FUNCT_3, FUNCT_7, TOPS_2, BORSUK_1, LATTICE3, TMAP_1,
      T_0TOPSP, CANTOR_1, MONOID_0, PRALG_1, PRALG_3, WAYBEL_3, WAYBEL_5,
      WAYBEL_8, WAYBEL11, YELLOW_9;
 registrations XBOOLE_0, SUBSET_1, RELSET_1, FUNCT_2, FUNCOP_1, CARD_3,
      STRUCT_0, PRE_TOPC, BORSUK_1, LATTICE3, YELLOW_0, MONOID_0, WAYBEL_0,
      YELLOW_1, WAYBEL_3, WAYBEL_8, WAYBEL11, YELLOW_9, WAYBEL17;
 requirements SUBSET, BOOLE;
 definitions STRUCT_0, TARSKI, PRE_TOPC, FUNCOP_1, WAYBEL_1, WAYBEL_3,
      XBOOLE_0, PRALG_1;
 theorems TARSKI, PRE_TOPC, ENUMSET1, ZFMISC_1, T_0TOPSP, TOPS_1, TOPS_2,
      FINSET_1, FUNCT_2, FUNCT_3, RELAT_1, FUNCT_1, BORSUK_1, FUNCOP_1, PBOOLE,
      CANTOR_1, LATTICE3, ORDERS_2, MSSUBFAM, PRALG_1, CARD_3, CARD_5,
      STRUCT_0, WAYBEL_3, PRALG_3, FUNCT_6, FUNCT_7, TMAP_1, YELLOW_9,
      YELLOW_0, YELLOW_1, WAYBEL_0, WAYBEL_1, WAYBEL_7, WAYBEL_8, WAYBEL11,
      WAYBEL13, WAYBEL14, RELSET_1, SETFAM_1, XBOOLE_0, XBOOLE_1, ORDERS_1,
      MONOID_0;
 schemes SUBSET_1, CLASSES1, FUNCT_1, FRAENKEL;

begin :: Preliminaries

canceled;

theorem Th2:

for X being set, A,B being Subset-Family of X st B = A \ {{}} or A = B \/ {{}}
  holds UniCl A = UniCl B
proof
  let X be set;
  let A,B be Subset-Family of X;
  assume
A1: B = A \ {{}} or A = B \/ {{}};
  then B c= A by XBOOLE_1:7,36;
  then
A2: UniCl B c= UniCl A by CANTOR_1:9;
  UniCl A c= UniCl B
  proof
    let x be set;
    assume x in UniCl A;
    then consider Y being Subset-Family of X such that
A3: Y c= A and
A4: x = union Y by CANTOR_1:def 1;
A5: Y \ {{}} c= B
    proof
      let w be set;
      assume
A6:   w in Y \ {{}};
      per cases by A1;
      suppose
A7:     B = A \ {{}};
        w in Y & not w in {{}} by A6,XBOOLE_0:def 5;
        hence w in B by A3,A7,XBOOLE_0:def 5;
      end;
      suppose
A8:     A = B \/ {{}};
        w in Y & not w in {{}} by A6,XBOOLE_0:def 5;
        hence w in B by A3,A8,XBOOLE_0:def 3;
      end;
    end;
    reconsider Z = Y \ {{}} as Subset-Family of X;
    Z \/ {{}} = Y \/ {{}} by XBOOLE_1:39;
    then union (Z \/ {{}}) = union Y \/ union {{}} by ZFMISC_1:96
      .= union Y \/ {} by ZFMISC_1:31
      .= union Y;
    then x = union Z \/ union {{}} by A4,ZFMISC_1:96
      .= union Z \/ {} by ZFMISC_1:31
      .= union Z;
    hence x in UniCl B by A5,CANTOR_1:def 1;
  end;
  hence thesis by A2,XBOOLE_0:def 10;
end;

theorem Th3:
  for T being TopSpace, K being Subset-Family of T
  holds K is Basis of T iff K \ {{}} is Basis of T
proof
  let T be TopSpace, K be Subset-Family of T;
A1: K \ {{}} c= K by XBOOLE_1:36;
  reconsider K' = K \ {{}} as Subset-Family of T;
  hereby
    assume
A2: K is Basis of T;
    then
A3: K c= the topology of T by CANTOR_1:def 2;
A4: the topology of T c= UniCl K by A2,CANTOR_1:def 2;
A5: K \ {{}} c= the topology of T by A1,A3,XBOOLE_1:1;
    the topology of T c= UniCl K' by A4,Th2;
    hence K \ {{}} is Basis of T by A5,CANTOR_1:def 2;
  end;
  assume
A6: K \ {{}} is Basis of T;
  then
A7: K' c= the topology of T by CANTOR_1:def 2;
A8: the topology of T c= UniCl K' by A6,CANTOR_1:def 2;
A9: K c= the topology of T
  proof
    let x be set;
    assume
A10: x in K;
    per cases;
    suppose x = {};
      hence x in the topology of T by PRE_TOPC:5;
    end;
    suppose x <> {};
      then not x in {{}} by TARSKI:def 1;
      then x in K' by A10,XBOOLE_0:def 5;
      hence x in the topology of T by A7;
    end;
  end;
  UniCl K' c= UniCl K by CANTOR_1:9,XBOOLE_1:36;
  then the topology of T c= UniCl K by A8,XBOOLE_1:1;
  hence K is Basis of T by A9,CANTOR_1:def 2;
end;

definition
  let F be Relation;
  attr F is TopSpace-yielding means
  :Def1:
  for x being set st x in rng F holds x is TopStruct;
end;

registration
  cluster TopSpace-yielding -> 1-sorted-yielding Function;
  coherence
  proof
    let F be Function such that
A1: F is TopSpace-yielding;
    let x be set;
    assume x in dom F;
    then F.x in rng F by FUNCT_1:def 5;
    hence F.x is 1-sorted by A1,Def1;
  end;
end;

registration
  let I be set;
  cluster TopSpace-yielding ManySortedSet of I;
  existence
  proof consider T being TopSpace;
    take I --> T;
    let v be set;
A1: rng(I --> T) c= {T} by FUNCOP_1:19;
    assume v in rng(I --> T);
    hence thesis by A1,TARSKI:def 1;
  end;
end;

registration
  let I be set;
  cluster TopSpace-yielding non-Empty ManySortedSet of I;
  existence
  proof consider R being non empty TopSpace;
    take J = I --> R;
A1: dom J = I & rng J c= {R} by FUNCOP_1:19;
    thus J is TopSpace-yielding
    proof
      let x be set;
      assume x in rng J;
      hence x is TopStruct by A1,TARSKI:def 1;
    end;
    thus J is non-Empty
    proof
      let S be 1-sorted;
      assume S in rng J;
      hence thesis by A1,TARSKI:def 1;
    end;
  end;
end;

definition
  let J be non empty set;
  let A be TopSpace-yielding ManySortedSet of J;
  let j be Element of J;
  redefine func A.j -> TopStruct;
  coherence
  proof
    dom A = J by PBOOLE:def 3;
    then A.j in rng A by FUNCT_1:def 5;
    hence thesis by Def1;
  end;
end;

definition
  let I be set;
  let J be TopSpace-yielding ManySortedSet of I;
  func product_prebasis J -> Subset-Family of product Carrier J means
  :Def2:
  for x being Subset of product Carrier J holds
  x in it iff ex i being set, T being TopStruct,
  V being Subset of T st i in I &
  V is open & T = J.i & x = product ((Carrier J) +* (i,V));
  existence
  proof
    defpred P[Subset of product Carrier J] means
    ex i being set, T being TopStruct, V being Subset of T st i in I &
    V is open & T = J.i & $1 = product ((Carrier J) +* (i,V));
    thus ex F being Subset-Family of product Carrier J st
    for x being Subset of product Carrier J holds x in F iff P[x]
    from SUBSET_1:sch 3;
  end;
  uniqueness
  proof
    let P1,P2 be Subset-Family of product Carrier J such that
A1: for x being Subset of product Carrier J holds
    x in P1 iff ex i being set, T being TopStruct,
    V being Subset of T st i in I &
    V is open & T = J.i & x = product ((Carrier J) +* (i,V)) and
A2: for x being Subset of product Carrier J holds
    x in P2 iff ex i being set, T being TopStruct,
    V being Subset of T st i in I &
    V is open & T = J.i & x = product ((Carrier J) +* (i,V));
A3: P1 c= P2
    proof
      let x be set;
      assume
A4:   x in P1;
      then reconsider x' = x as Subset of product Carrier J;
      ex i being set, T being TopStruct, V being Subset of T st
      i in I & V is open & T = J.i &
      x' = product ((Carrier J) +* (i,V)) by A1,A4;
      hence x in P2 by A2;
    end;
    P2 c= P1
    proof
      let x be set;
      assume
A5:   x in P2;
      then reconsider x' = x as Subset of product Carrier J;
      ex i being set, T being TopStruct, V being Subset of T st
      i in I & V is open & T = J.i &
      x' = product ((Carrier J) +* (i,V)) by A2,A5;
      hence x in P1 by A1;
    end;
    hence P1 = P2 by A3,XBOOLE_0:def 10;
  end;
end;

theorem Th4:
  for X be set, A be Subset-Family of X holds
  TopStruct (#X,UniCl FinMeetCl A#) is TopSpace-like
proof
  let X be set;
  let A be Subset-Family of X;
  per cases;
  suppose
A1: X = {};
    set T = TopStruct (#X, UniCl FinMeetCl A#);
A2: the carrier of T in FinMeetCl A by CANTOR_1:8;
    FinMeetCl A c= UniCl FinMeetCl A by CANTOR_1:1;
    hence
 the carrier of T in the topology of T by A2;
    hence for a being Subset-Family of T st
    a c= the topology of T holds union a in the topology of T
    by A1;
    thus for a,b being Subset of T st
    a in the topology of T & b in the topology of T
    holds a /\ b in the topology of T by A1;
  end;
  suppose X <> {};
    hence TopStruct (#X,UniCl FinMeetCl A#) is TopSpace-like by CANTOR_1:17;
  end;
end;

definition
  let I be set;
  let J be TopSpace-yielding non-Empty ManySortedSet of I;
  func product J -> strict TopSpace means
  :Def3:
  the carrier of it = product Carrier J &
  product_prebasis J is prebasis of it;
  existence
  proof
    set X = product Carrier J;
    reconsider A = product_prebasis J as Subset-Family of X;
    consider T being strict TopStruct such that
A1: T = TopStruct (# X, UniCl FinMeetCl A #);
    reconsider T as strict TopSpace by A1,Th4;
    take T;
    thus the carrier of T = product Carrier J by A1;
    now
      assume {} in rng Carrier J;
      then consider i being set such that
A2:   i in dom Carrier J & {} = (Carrier J).i by FUNCT_1:def 5;
A3:   dom Carrier J = I & dom J = I by PBOOLE:def 3;
      then consider R being 1-sorted such that
A4:   R = J.i & {} = the carrier of R by A2,PRALG_1:def 13;
      R in rng J by A2,A3,A4,FUNCT_1:def 5;
      then reconsider R as non empty 1-sorted by WAYBEL_3:def 7;
      the carrier of R = {} by A4;
      hence contradiction;
    end;
    then X is non empty by CARD_3:37;
    hence product_prebasis J is prebasis of T by A1,CANTOR_1:20;
  end;
  uniqueness
  proof
    let T1,T2 be strict TopSpace such that
A5: the carrier of T1 = product Carrier J and
A6: product_prebasis J is prebasis of T1 and
A7: the carrier of T2 = product Carrier J and
A8: product_prebasis J is prebasis of T2;
    now
      assume {} in rng Carrier J;
      then consider i being set such that
A9:   i in dom Carrier J & {} = (Carrier J).i by FUNCT_1:def 5;
A10:  dom Carrier J = I & dom J = I by PBOOLE:def 3;
      then consider R being 1-sorted such that
A11:  R = J.i & {} = the carrier of R by A9,PRALG_1:def 13;
      R in rng J by A9,A10,A11,FUNCT_1:def 5;
      then reconsider R as non empty 1-sorted by WAYBEL_3:def 7;
      the carrier of R = {} by A11;
      hence contradiction;
    end;
    then product Carrier J <> {} by CARD_3:37;
    then reconsider t1 = T1, t2 = T2 as non empty TopSpace by A5,A7;
    t1 = t2 by A5,A6,A7,A8,CANTOR_1:19;
    hence T1 = T2;
  end;
end;

registration
  let I be set;
  let J be TopSpace-yielding non-Empty ManySortedSet of I;
  cluster product J -> non empty;
  coherence
  proof
A1: the carrier of product J = product Carrier J by Def3;
    now
      assume {} in rng Carrier J;
      then consider i being set such that
A2:   i in dom Carrier J & {} = (Carrier J).i by FUNCT_1:def 5;
A3:   dom Carrier J = I & dom J = I by PBOOLE:def 3;
      then consider R being 1-sorted such that
A4:   R = J.i & {} = the carrier of R by A2,PRALG_1:def 13;
      R in rng J by A2,A3,A4,FUNCT_1:def 5;
      then reconsider R as non empty 1-sorted by WAYBEL_3:def 7;
      the carrier of R = {} by A4;
      hence contradiction;
    end;
    then the carrier of product J <> {} by A1,CARD_3:37;
    hence thesis;
  end;
end;

definition
  let I be non empty set;
  let J be TopSpace-yielding non-Empty ManySortedSet of I;
  let i be Element of I;
  redefine func J.i -> non empty TopStruct;
  coherence
  proof dom J = I by PBOOLE:def 3;
    then J.i in rng J by FUNCT_1:def 5;
    hence thesis by WAYBEL_3:def 7;
  end;
end;

registration
  let I be set;
  let J be TopSpace-yielding non-Empty ManySortedSet of I;
  cluster product J -> constituted-Functions;
  coherence
  proof
    the carrier of product J = product Carrier J by Def3;
    hence thesis by MONOID_0:89;
  end;
end;

definition
  let I be non empty set;
  let J be TopSpace-yielding non-Empty ManySortedSet of I;
  let x be Element of product J;
  let i be Element of I;
  redefine func x.i -> Element of J.i;
  coherence
  proof
A1: ex R being 1-sorted st R = J.i & (Carrier J).i = the carrier of R
    by PRALG_1:def 13;
    the carrier of product J = product Carrier J &
    dom Carrier J = I by Def3,PBOOLE:def 3;
    hence thesis by A1,CARD_3:18;
  end;
end;

definition
  let I be non empty set;
  let J be TopSpace-yielding non-Empty ManySortedSet of I;
  let i be Element of I;
  func proj(J,i) -> Function of product J, J.i equals

  proj(Carrier J,i);
  coherence
  proof
    consider f being Function such that
A1: f = proj (Carrier J,i);
A2: dom f = product Carrier J by A1,PRALG_3:def 2
      .= the carrier of product J by Def3;
    rng f c= the carrier of J.i
    proof
      let y be set;
      assume y in rng f;
      then consider x be set such that
A3:   x in dom f and
A4:   y = f.x by FUNCT_1:def 5;
      reconsider x as Element of product J by A2,A3;
      f.x = x.i by A1,A3,PRALG_3:def 2;
      hence y in the carrier of J.i by A4;
    end;
    hence thesis by A1,A2,FUNCT_2:def 1,RELSET_1:11;
  end;
end;

theorem Th5:
  for I being non empty set,
  J being TopSpace-yielding non-Empty ManySortedSet of I,
  i being Element of I, P being Subset of J.i holds
  proj(J,i)"P = product ((Carrier J) +* (i,P))
proof
  let I be non empty set, J be TopSpace-yielding non-Empty ManySortedSet of I,
  i be Element of I, P be Subset of J.i;
  set f = (Carrier J) +* (i,P);
A1: dom f = dom Carrier J by FUNCT_7:32
    .= I by PBOOLE:def 3;
A2: dom Carrier J = I by PBOOLE:def 3;
A3: product f c= proj(J,i)"P
  proof
    let x be set;
    assume x in product f;
    then consider g being Function such that
A4: x = g and
A5: dom g = dom f and
A6: for y being set st y in dom f holds g.y in f.y by CARD_3:def 5;
    f.i = P by A2,FUNCT_7:33;
    then
A7: g.i in P by A1,A6;
A8: dom g = dom Carrier J by A5,FUNCT_7:32;
    for j being set st j in dom Carrier J holds g.j in (Carrier J).j
    proof
      let j be set;
      assume j in dom Carrier J;
      then
A9:   j in I by PBOOLE:def 3;
      then consider R being 1-sorted such that
A10:  R = J.j and
A11:  (Carrier J).j = the carrier of R by PRALG_1:def 13;
      per cases;
      suppose
A12:    j = i;
        f.i = P by A2,FUNCT_7:33;
        then g.j in P by A1,A6,A12;
        hence g.j in (Carrier J).j by A10,A11,A12;
      end;
      suppose j <> i;
        then f.j = (Carrier J).j by FUNCT_7:34;
        hence g.j in (Carrier J).j by A1,A6,A9;
      end;
    end;
    then
A13: g in product Carrier J by A8,CARD_3:18;
    then
A14: g in dom proj(Carrier J,i) by PRALG_3:def 2;
A15: g in dom proj(J,i) by A13,PRALG_3:def 2;
    proj(Carrier J,i).g in P by A7,A14,PRALG_3:def 2;
    hence x in proj(J,i)"P by A4,A15,FUNCT_1:def 13;
  end;
  proj(J,i)"P c= product f
  proof
    let x be set;
    assume
A16: x in proj(J,i)"P;
    then
A17: x in dom proj(Carrier J,i) by FUNCT_1:def 13;
    then x in product Carrier J by PRALG_3:def 2;
    then consider g being Function such that
A18: x = g and
A19: dom g = dom Carrier J and
A20: for y being set st y in dom Carrier J holds
    g.y in (Carrier J).y by CARD_3:def 5;
A21: dom g = dom f by A19,FUNCT_7:32;
    for j being set st j in dom f holds g.j in f.j
    proof
      let j be set;
      assume j in dom f;
      then
A22:  g.j in (Carrier J).j by A19,A20,A21;
      per cases;
      suppose
A23:    i = j;
        proj(Carrier J,i).x = g.i by A17,A18,PRALG_3:def 2;
        then g.i in P by A16,FUNCT_1:def 13;
        hence g.j in f.j by A2,A23,FUNCT_7:33;
      end;
      suppose i <> j;
        hence g.j in f.j by A22,FUNCT_7:34;
      end;
    end;
    hence x in product f by A18,A21,CARD_3:def 5;
  end;
  hence proj(J,i)"P = product ((Carrier J) +* (i,P)) by A3,XBOOLE_0:def 10;
end;

theorem Th6:
  for I being non empty set,
  J being TopSpace-yielding non-Empty ManySortedSet of I,
  i being Element of I holds proj(J,i) is continuous
proof
  let I be non empty set,
  J be TopSpace-yielding non-Empty ManySortedSet of I, i be Element of I;
A1: [#](J.i) <> {};
  for P being Subset of J.i st P is open holds proj(J,i)"P is open
  proof
    let P be Subset of J.i;
    assume
A2: P is open;
    proj(J,i)"P c= product Carrier J
    proof
      let x be set;
      assume x in proj(J,i)"P;
      then x in dom proj(Carrier J,i) by FUNCT_1:def 13;
      hence x in product Carrier J by PRALG_3:def 2;
    end;
    then reconsider x = proj(J,i)"P as Subset of product Carrier J;
    x = product ((Carrier J) +* (i,P)) by Th5;
    then
A3: proj(J,i)"P in product_prebasis J by A2,Def2;
    product_prebasis J is prebasis of product J by Def3;
    then product_prebasis J c= the topology of product J by CANTOR_1:def 5;
    hence proj(J,i)"P is open by A3,PRE_TOPC:def 5;
  end;
  hence proj(J,i) is continuous by A1,TOPS_2:55;
end;

theorem Th7:
  for X being non empty TopSpace, I being non empty set,
  J being TopSpace-yielding non-Empty ManySortedSet of I,
  f being Function of X, product J holds f is continuous iff
  for i being Element of I holds proj(J,i)*f is continuous
proof
  let X be non empty TopSpace, I be non empty set,
  J be TopSpace-yielding non-Empty ManySortedSet of I,
  f be Function of X, product J;
  hereby
    assume
A1: f is continuous;
    let i be Element of I;
    proj(J,i) is continuous by Th6;
    hence proj(J,i)*f is continuous by A1,TOPS_2:58;
  end;
  assume
A2: for i being Element of I holds proj(J,i)*f is continuous;
  set B = product_prebasis J;
A3: B is prebasis of product J by Def3;
  for P being Subset of product J st P in B holds f"P is open
  proof
    let P be Subset of product J;
    assume
A4: P in B;
    reconsider p = P as Subset of product Carrier J by Def3;
    consider i being set, T being TopStruct, V being Subset of T such that
A5: i in I and
A6: V is open and
A7: T = J.i and
A8: p = product ((Carrier J) +* (i,V)) by A4,Def2;
    reconsider j = i as Element of I by A5;
A9: proj(J,j)*f is continuous by A2;
    reconsider V as Subset of J.j by A7;
A10: proj(J,j)"V = p by A8,Th5;
    [#](J.j) <> {};
    then (proj(J,j)*f)"V is open by A6,A7,A9,TOPS_2:55;
    hence f"P is open by A10,RELAT_1:181;
  end;
  hence f is continuous by A3,YELLOW_9:36;
end;

begin :: Main Part

definition
  let Z be TopStruct;
  attr Z is injective means
  :Def5:   ::p.121 definition 3.1.
  for X being non empty TopSpace
  for f being Function of X, Z st f is continuous holds
  for Y being non empty TopSpace st X is SubSpace of Y
  ex g being Function of Y,Z st g is continuous & g|(the carrier of X) = f;
end;

theorem Th8:  ::p.121 lemma 3.2.(i)
  for I being non empty set, J being TopSpace-yielding non-Empty
  ManySortedSet of I st for i being Element of I holds J.i is injective holds
  product J is injective
proof
  let I be non empty set, J be TopSpace-yielding non-Empty ManySortedSet of I;
  assume
A1: for i being Element of I holds J.i is injective;
  let X be non empty TopSpace;
  let f be Function of X, product J;
  assume
A2: f is continuous;
  let Y be non empty TopSpace;
  assume
A3: X is SubSpace of Y;
  defpred P[set,set] means ex i1 being Element of I st i1 = $1 &
  ex g being Function of Y, J.i1 st g is continuous &
  g|(the carrier of X) = proj(J,i1)*f & $2 = g;
A4: for i being set st i in I ex u being set st P[i,u]
  proof
    let i be set;
    assume i in I;
    then reconsider i1 = i as Element of I;
A5: J.i1 is injective by A1;
    proj(J,i1)*f is continuous by A2,Th7;
    then consider g being Function of Y, J.i1 such that
A6: g is continuous and
A7: g|(the carrier of X) = proj(J,i1)*f by A3,A5,Def5;
    take g, i1;
    thus thesis by A6,A7;
  end;
  consider G being Function such that
A8: dom G = I and
A9: for i being set st i in I holds P[i,G.i] from CLASSES1:sch 1(A4);
A10: dom <:G:> c= the carrier of Y
  proof
    let x be set;
    assume
A11: x in dom <:G:>;
    consider j being set such that
A12: j in I by XBOOLE_0:def 1;
    consider i being Element of I such that i = j and
A13: ex g being Function of Y, J.i st g is continuous &
    g|(the carrier of X) = proj(J,i)*f & G.j = g by A9,A12;
    consider g being Function of Y, J.i such that
    g is continuous & g|(the carrier of X) = proj(J,i)*f and
A14: G.j = g by A13;
    g in rng G by A8,A12,A14,FUNCT_1:def 5;
    then x in dom g by A11,FUNCT_6:52;
    hence x in the carrier of Y;
  end;
  the carrier of Y c= dom <:G:>
  proof
    let x be set;
    assume
A15: x in the carrier of Y;
    consider i being set such that
A16: i in I by XBOOLE_0:def 1;
    consider j being Element of I such that j = i and
A17: ex g being Function of Y, J.j st g is continuous &
    g|(the carrier of X) = proj(J,j)*f & G.i = g by A9,A16;
    consider g being Function of Y, J.j such that
    g is continuous and g|(the carrier of X) = proj(J,j)*f and
A18: G.i = g by A17;
A19: g in rng G by A8,A16,A18,FUNCT_1:def 5;
    for f' being Function st f' in rng G holds x in dom f'
    proof
      let f' be Function;
      assume f' in rng G;
      then consider k being set such that
A20:  k in dom G and
A21:  f' = G.k by FUNCT_1:def 5;
      consider i1 being Element of I such that i1 = k and
A22:  ex ff being Function of Y, J.i1 st ff is continuous &
      ff|(the carrier of X) = proj(J,i1)*f & G.k = ff by A8,A9,A20;
      consider ff being Function of Y, J.i1 such that
      ff is continuous and ff|(the carrier of X) = proj(J,i1)*f and
A23:  G.k = ff by A22;
      thus x in dom f' by A15,A21,A23,FUNCT_2:def 1;
    end;
    hence x in dom <:G:> by A19,FUNCT_6:53;
  end;
  then
A24: dom <:G:> = the carrier of Y by A10,XBOOLE_0:def 10;
A25: G is Function-yielding
  proof
    let j be set;
    assume j in dom G;
    then consider i being Element of I such that i = j and
A26: ex g being Function of Y, J.i st g is continuous &
    g|(the carrier of X) = proj(J,i)*f & G.j = g by A8,A9;
    consider g being Function of Y, J.i such that
    g is continuous & g|(the carrier of X) = proj(J,i)*f and
A27: G.j = g by A26;
    thus G.j is Function by A27;
  end;
A28: product rngs G c= product Carrier J
  proof
    let y be set;
    assume y in product rngs G;
    then consider h being Function such that
A29: y = h and
A30: dom h = dom rngs G and
A31: for x being set st x in dom rngs G holds h.x in (rngs G).x
    by CARD_3:def 5;
A32: dom h = I by A8,A25,A30,FUNCT_6:90
      .= dom Carrier J by PBOOLE:def 3;
    for x being set st x in dom Carrier J holds h.x in (Carrier J).x
    proof
      let x be set;
      assume
A33:  x in dom Carrier J;
      then
A34:  h.x in (rngs G).x by A30,A31,A32;
A35:  x in I by A33,PBOOLE:def 3;
      then consider i being Element of I such that
A36:  i = x and
A37:  ex g being Function of Y, J.i st g is continuous &
      g|(the carrier of X) = proj(J,i)*f & G.x = g by A9;
      consider g being Function of Y, J.i such that
      g is continuous & g|(the carrier of X) = proj(J,i)*f and
A38:  G.x = g by A37;
      x in dom G by A8,A33,PBOOLE:def 3;
      then
A39:  (rngs G).x = rng g by A38,FUNCT_6:31;
      consider R being 1-sorted such that
A40:  R = J.x and
A41:  (Carrier J).x = the carrier of R by A35,PRALG_1:def 13;
      thus h.x in (Carrier J).x by A34,A36,A39,A40,A41;
    end;
    hence y in product Carrier J by A29,A32,CARD_3:def 5;
  end;
  rng <:G:> c= product rngs G by FUNCT_6:49;
  then
A42: rng <:G:> c= product Carrier J by A28,XBOOLE_1:1;
  then rng <:G:> c= the carrier of product J by Def3;
  then reconsider h = <:G:> as Function of
  the carrier of Y, the carrier of product J by A24,FUNCT_2:def 1,RELSET_1:11;
A43: dom (h|(the carrier of X)) = dom h /\ the carrier of X by RELAT_1:90
    .= (the carrier of Y) /\ the carrier of X by FUNCT_2:def 1
    .= the carrier of X by A3,BORSUK_1:35,XBOOLE_1:28
    .= dom f by FUNCT_2:def 1;
A44: for x being set st x in dom (h|(the carrier of X)) holds
  (h|(the carrier of X)).x = f.x
  proof
    let x be set;
    assume
A45: x in dom (h|(the carrier of X));
    then
A46: x in the carrier of Y;
    f.x in rng f by A43,A45,FUNCT_1:def 5;
    then f.x in the carrier of product J;
    then
A47: f.x in product Carrier J by Def3;
    then consider y being Function such that
A48: f.x = y and
A49: dom y = dom Carrier J and for i being set st i in dom Carrier J holds
    y.i in (Carrier J).i by CARD_3:def 5;

(h|(the carrier of X)).x in rng (h|(the carrier of X)) by A45,FUNCT_1:def 5;
    then (h|(the carrier of X)).x in the carrier of product J;
    then (h|(the carrier of X)).x in product Carrier J by Def3;
    then consider z being Function such that
A50: (h|(the carrier of X)).x = z and
A51: dom z = dom Carrier J and for i being set st i in dom Carrier J holds
    z.i in (Carrier J).i by CARD_3:def 5;
    for j being set st j in dom y holds y.j = z.j
    proof
      let j be set;
      assume j in dom y;
      then
A52:  j in I by A49,PBOOLE:def 3;
      then consider i being Element of I such that
A53:  i = j and
A54:  ex g being Function of Y, J.i st g is continuous &
      g|(the carrier of X) = proj(J,i)*f & G.j = g by A9;
      consider g being Function of Y, J.i such that g is continuous and
A55:  g|(the carrier of X) = proj(J,i)*f and
A56:  G.j = g by A54;
A57:  x in dom h by A46,FUNCT_2:def 1;
A58:  y in dom proj(Carrier J,i) by A47,A48,PRALG_3:def 2;
      z = <:G:>.x by A43,A45,A50,FUNCT_1:72;
      hence z.j = g.x by A8,A52,A56,A57,FUNCT_6:54
        .= (proj(J,i)*f).x by A43,A45,A55,FUNCT_1:72
        .= proj(Carrier J,i).y by A43,A45,A48,FUNCT_2:21
        .= y.j by A53,A58,PRALG_3:def 2;
    end;
    hence (h|(the carrier of X)).x = f.x by A48,A49,A50,A51,FUNCT_1:9;
  end;
  reconsider h as Function of Y, product J;
  take h;
  set B = product_prebasis J;
A59: B is prebasis of product J by Def3;
  for P being Subset of product J st P in B holds h"P is open
  proof
    let P be Subset of product J;
    assume
A60: P in B;
    reconsider p = P as Subset of product Carrier J by Def3;
    consider i being set, T being TopStruct, V being Subset of T such that
A61: i in I and
A62: V is open and
A63: T = J.i and
A64: p = product ((Carrier J) +* (i,V)) by A60,Def2;
    consider j being Element of I such that
A65: j = i and
A66: ex g being Function of Y, J.j st g is continuous &
    g|(the carrier of X) = proj(J,j)*f & G.i = g by A9,A61;
    consider g being Function of Y, J.j such that
A67: g is continuous and g|(the carrier of X) = proj(J,j)*f and
A68: G.i = g by A66;
    reconsider V as Subset of J.j by A63,A65;
A69: proj(J,j)"V = p by A64,A65,Th5;
    [#](J.j) <> {};
    then
A70: g"V is open by A62,A63,A65,A67,TOPS_2:55;
A71: dom g = the carrier of Y by FUNCT_2:def 1
      .= dom h by FUNCT_2:def 1;
A72: dom proj(J,j) = product Carrier J by PRALG_3:def 2;
A73: h"P c= g"V
    proof
      let x be set;
      assume
A74:  x in h"P;
      then
A75:  x in dom h & h.x in P by FUNCT_1:def 13;
A76:  x in dom g by A71,A74,FUNCT_1:def 13;
A77:  h.x in proj(J,j)"V by A69,A74,FUNCT_1:def 13;
      then
A78:  h.x in dom proj(J,j) by FUNCT_1:def 13;
      h.x in product Carrier J by A72,A77,FUNCT_1:def 13;
      then consider y being Function such that
A79:  h.x = y and dom y = dom Carrier J and
      for i being set st i in dom Carrier J holds
      y.i in (Carrier J).i by CARD_3:def 5;
      proj(J,j).(h.x) = y.j by A78,A79,PRALG_3:def 2;
      then
A80:  y.j in V by A77,FUNCT_1:def 13;
      g.x = y.j by A8,A65,A68,A75,A79,FUNCT_6:54;
      hence x in g"V by A76,A80,FUNCT_1:def 13;
    end;
    g"V c= h"P
    proof
      let x be set;
      assume
A81:  x in g"V;
      then
A82:  x in dom h by A71,FUNCT_1:def 13;
      then
A83:  h.x in rng h by FUNCT_1:def 5;
      then
A84:  h.x in product Carrier J by A42;
      consider y being Function such that
A85:  h.x = y and dom y = dom Carrier J and
      for i being set st i in dom Carrier J holds y.i in
      (Carrier J).i by A42,A83,CARD_3:def 5;
      y in dom proj(Carrier J,j) by A84,A85,PRALG_3:def 2;
      then
A86:  proj(J,j).(h.x) = y.j by A85,PRALG_3:def 2;
      g.x = y.j by A8,A65,A68,A82,A85,FUNCT_6:54;
      then proj(J,j).(h.x) in V by A81,A86,FUNCT_1:def 13;
      then h.x in proj(J,j)"V by A42,A72,A83,FUNCT_1:def 13;
      hence x in h"P by A69,A82,FUNCT_1:def 13;
    end;
    hence h"P is open by A70,A73,XBOOLE_0:def 10;
  end;
  hence h is continuous by A59,YELLOW_9:36;
  thus h|(the carrier of X) = f by A43,A44,FUNCT_1:9;
end;

theorem       ::p.121 lemma 3.2.(ii)
  for T being non empty TopSpace st T is injective
  for S being non empty SubSpace of T st S is_a_retract_of T holds
  S is injective
proof
  let T be non empty TopSpace;
  assume
A1: T is injective;
  let S be non empty SubSpace of T;
  assume S is_a_retract_of T;
  then consider r being continuous Function of T,S such that
A2: r is being_a_retraction by BORSUK_1:def 20;
  set p = incl S;
A3: p is continuous by TMAP_1:98;
  let X be non empty TopSpace, F be Function of X, S;
  assume
A4: F is continuous;
  let Y be non empty TopSpace;
  assume
A5: X is SubSpace of Y;
  reconsider f = p*F as Function of X,T;
  f is continuous by A3,A4;
  then consider g be Function of Y,T such that
A6: g is continuous and
A7: g|(the carrier of X) = f by A1,A5,Def5;
  take G = r*g;
  thus G is continuous by A6;
A8: the carrier of X c= the carrier of Y by A5,BORSUK_1:35;
A9: the carrier of S c= the carrier of T by BORSUK_1:35;
A10: dom G /\ the carrier of X
  = (the carrier of Y) /\ the carrier of X by FUNCT_2:def 1
    .= the carrier of X by A5,BORSUK_1:35,XBOOLE_1:28;
A11: dom F = the carrier of X by FUNCT_2:def 1;
  for x being set st x in dom F holds F.x = G.x
  proof
    let x be set;
    assume
A12: x in dom F;
    then
A13: x in the carrier of X;
A14: F.x in rng F by A12,FUNCT_1:def 5;
    then F.x in the carrier of S;
    then reconsider y = F.x as Point of T by A9;
A15: F.x = r.y by A2,A14,BORSUK_1:def 19;
A16: f.x = p.y by A12,FUNCT_2:21
      .= F.x by A14,TMAP_1:95;
    g.x = f.x by A7,A12,FUNCT_1:72;
    hence G.x = F.x by A8,A13,A15,A16,FUNCT_2:21;
  end;
  hence G|(the carrier of X) = F by A10,A11,FUNCT_1:68;
end;

definition
  let X be 1-sorted, Y be TopStruct, f be Function of X,Y;
  func Image f -> SubSpace of Y equals

  Y|(rng f);
  coherence;
end;

registration
  let X be non empty 1-sorted, Y be non empty TopStruct, f be Function of X,Y;
  cluster Image f -> non empty;
  coherence
  proof
A1: dom f = the carrier of X by FUNCT_2:def 1;
    consider x being Element of dom f;
    f.x in rng f by A1,FUNCT_1:def 5;
    then reconsider A = rng f as non empty Subset of Y;
    the carrier of Image f = [#]Image f
      .= A by PRE_TOPC:def 10;
    hence the carrier of Image f is non empty;
  end;
end;

theorem Th10:
  for X being 1-sorted, Y being TopStruct, f being Function of X,Y holds
  the carrier of Image f = rng f
proof
  let X be 1-sorted,Y be TopStruct, f be Function of X,Y;
  thus the carrier of Image f = [#](Y|(rng f))
    .= rng f by PRE_TOPC:def 10;
end;

definition
  let X be 1-sorted, Y be non empty TopStruct, f be Function of X,Y;
  func corestr(f) -> Function of X,Image f equals

  f;
  coherence
  proof
A1: the carrier of Image f = rng f by Th10;
    then (the carrier of Image f)|f = f & the carrier of X = dom f
    by FUNCT_2:def 1,RELAT_1:125;
    hence thesis by A1,FUNCT_2:3;
  end;
end;

theorem Th11:
  for X, Y being non empty TopSpace, f being Function of X,Y st
  f is continuous holds corestr f is continuous
proof
  let X, Y be non empty TopSpace;
  let f be Function of X,Y;
  assume
A1: f is continuous;
A2: f is Function of dom f,rng f by FUNCT_2:3;
A3: [#]Y <> {} & [#]Image f <> {};
  for R being Subset of Image f st R is open holds (corestr f)"R is open
  proof
    let R be Subset of Image f;
    assume R is open;
    then R in the topology of Image f by PRE_TOPC:def 5;
    then consider Q being Subset of Y such that
A4: Q in the topology of Y and
A5: R = Q /\ [#](Image f) by PRE_TOPC:def 9;
    reconsider Q as Subset of Y;
    Q is open by A4,PRE_TOPC:def 5;
    then
A6: f"Q is open by A1,A3,TOPS_2:55;
    [#](Image f) = rng f by Th10;
    then
A7: f"([#](Image f)) = dom f by A2,FUNCT_2:48
      .= the carrier of X by FUNCT_2:def 1;
    the carrier of X in the topology of X by PRE_TOPC:def 1;
    then
A8: f"([#](Image f)) is open by A7,PRE_TOPC:def 5;
    f"Q /\ f"([#](Image f)) = f"(Q /\ [#](Image f)) by FUNCT_1:137;
    hence (corestr f)"R is open by A5,A6,A8,TOPS_1:38;
  end;
  hence corestr f is continuous by A3,TOPS_2:55;
end;

registration
  let X be 1-sorted,Y be non empty TopStruct;
  let f be Function of X,Y;
  cluster corestr f -> onto;
  coherence
  proof
    the carrier of Image f = rng corestr f by Th10;
    hence thesis by FUNCT_2:def 3;
  end;
end;

definition
  let X,Y be TopStruct;
  pred X is_Retract_of Y means
  ex f being Function of Y,Y st f is continuous & f*f = f &
  Image f, X are_homeomorphic;
end;

theorem Th12:  ::p.121 lemma 3.2.(iii)
  for T,S being non empty TopSpace st T is injective
  for f being Function of T,S st corestr(f) is_homeomorphism holds
  T is_Retract_of S
proof
  let T,S be non empty TopSpace;
  assume
A1: T is injective;
A2: [#]S <> {} & [#]T <> {};
  let f be Function of T,S;
  assume
A3: corestr(f) is_homeomorphism;
  then
A4: corestr(f) is continuous by TOPS_2:def 5;
  consider g being Function of Image f, T such that
A5: g = (corestr f)";
  g is continuous by A3,A5,TOPS_2:def 5;
  then consider h being Function of S,T such that
A6: h is continuous and
A7: h|(the carrier of Image f) = g by A1,Def5;
  g is_homeomorphism by A3,A5,TOPS_2:70;
  then
A8: g is one-to-one by TOPS_2:def 5;
  reconsider p = incl(Image f) as Function of Image f,S;
  p is continuous by TMAP_1:98;
  then
A9: p*(corestr f) is continuous by A4;
A10: dom (p*(corestr f)) = the carrier of T by FUNCT_2:def 1
    .= dom f by FUNCT_2:def 1;
  take F = f*h;
A11: the carrier of Image f = rng f by Th10;
  dom h = the carrier of S & rng h c= the carrier of T by FUNCT_2:def 1;
  then
A12: rng h c= dom f by FUNCT_2:def 1;
A13: for P being Subset of S holds f"P = (p*(corestr f))"P
  proof
    let P be Subset of S;
    hereby
      let x be set;
      assume x in f"P;
      then
A14:  x in dom f & f.x in P by FUNCT_1:def 13;
      then f.x in rng f by FUNCT_1:def 5;
      then
A15:  f.x in the carrier of Image f by Th10;
      reconsider y = f.x as Point of S by A14;
      (p*(corestr f)).x = p.(f.x) by A14,FUNCT_1:23
        .= y by A15,TMAP_1:95;
      hence x in (p*(corestr f))"P by A10,A14,FUNCT_1:def 13;
    end;
    let x be set;
    assume x in (p*(corestr f))"P;
    then
A16: x in dom(p*(corestr f)) & (p*(corestr f)).x in P by FUNCT_1:def 13;
    then
A17: f.x in rng f by A10,FUNCT_1:def 5;
    then
A18: f.x in the carrier of Image f by Th10;
    reconsider y = f.x as Point of S by A17;
    (p*(corestr f)).x = p.(f.x) by A10,A16,FUNCT_1:23
      .= y by A18,TMAP_1:95;
    hence x in f"P by A10,A16,FUNCT_1:def 13;
  end;
  for P being Subset of S st P is open holds F"P is open
  proof
    let P be Subset of S;
    assume P is open;
    then (p*(corestr f))"P is open by A2,A9,TOPS_2:55;
    then f"P is open by A13;
    then h"(f"P) is open by A2,A6,TOPS_2:55;
    hence F"P is open by RELAT_1:181;
  end;
  hence F is continuous by A2,TOPS_2:55;
A19: dom (h*f) = the carrier of T by FUNCT_2:def 1;
  for x being set st x in the carrier of T holds (h*f).x = x
  proof
    let x be set;
    assume
A20: x in the carrier of T;
    then
A21: x in dom f by FUNCT_2:def 1;
A22: x in dom (corestr f) by A20,FUNCT_2:def 1;
A23: corestr f is one-to-one by A3,TOPS_2:def 5;
A24: rng (corestr f) = [#](Image f) by FUNCT_2:def 3;
A25: f.x in rng f by A21,FUNCT_1:def 5;
    thus (h*f).x = h.(f.x) by A21,FUNCT_1:23
      .= ((corestr f)").((corestr f).x) by A5,A7,A11,A25,FUNCT_1:72
      .= ((corestr f qua Function)").((corestr f).x) by A23,A24,TOPS_2:def 4
      .= x by A22,A23,FUNCT_1:56;
  end;
  then
A26: h*f = id (the carrier of T) by A19,FUNCT_1:34;
  thus F*F = (f*h*f)*h by RELAT_1:55
    .= f*(id T)*h by A26,RELAT_1:55
    .= F by FUNCT_2:23;
A27: rng F c= rng f
  proof
    let y be set;
    assume y in rng F;
    then consider x being set such that
A28: x in dom F and
A29: y = F.x by FUNCT_1:def 5;
    x in the carrier of S by A28;
    then
A30: x in dom h by FUNCT_2:def 1;
    then
A31: h.x in rng h by FUNCT_1:def 5;
    f.(h.x) = y by A29,A30,FUNCT_1:23;
    hence y in rng f by A12,A31,FUNCT_1:def 5;
  end;
  set H = h*(incl Image F);
A32: dom incl Image F = the carrier of Image F by FUNCT_2:def 1;
A33: dom H = [#](Image F) by FUNCT_2:def 1;
A34: [#]Image F <> {};
A35: rng H = [#]T
  proof
    thus rng H c= [#](T);
    let y be set;
    assume
A36: y in [#](T);
A37: dom H = rng F by A33,Th10;
A38: y in dom f by A36,FUNCT_2:def 1;
    then
A39: f.y in rng f by FUNCT_1:def 5;
    then f.y in the carrier of S;
    then
A40: f.y in dom F by FUNCT_2:def 1;
A41: F.(f.y) = ((f*h)*f).y by A38,FUNCT_1:23
      .= (f*id T).y by A26,RELAT_1:55
      .= f.y by FUNCT_2:23;
    then
A42: f.y in dom H by A37,A40,FUNCT_1:def 5;
    reconsider pp = f.y as Point of S by A39;
    F.(f.y) in rng F by A40,FUNCT_1:def 5;
    then
A43: f.y in the carrier of Image F by A41,Th10;
    then
A44: y in dom((incl Image F)*f) by A32,A38,FUNCT_1:21;
    H.(f.y) = ((h*(incl Image F))*f).y by A38,FUNCT_1:23
      .= (h*((incl Image F)*f)).y by RELAT_1:55
      .= h.(((incl Image F)*f).y) by A44,FUNCT_1:23
      .= h.((incl Image F).pp) by A38,FUNCT_1:23
      .= h.(f.y) by A43,TMAP_1:95
      .= (id T).y by A26,A38,FUNCT_1:23
      .= y by A36,FUNCT_1:35;
    hence y in rng H by A42,FUNCT_1:def 5;
  end;
A45: H is one-to-one
  proof
    let x,y be Element of Image F;
    assume
A46: H.x = H.y;
A47: x in the carrier of Image F;
    then
A48: x in rng F by Th10;
A49: x in dom (incl Image F) by A47,FUNCT_2:def 1;
A50: y in the carrier of Image F;
    then
A51: y in rng F by Th10;
A52: y in dom (incl Image F) by A50,FUNCT_2:def 1;
    reconsider a = x, b = y as Point of S by A48,A51;
    reconsider x' = x, y' = y as Element of Image f by A27,A48,A51,Th10;
    g.x' = h.x by A7,FUNCT_1:72
      .= h.((incl Image F).a) by TMAP_1:95
      .= (h*(incl Image F)).b by A46,A49,FUNCT_1:23
      .= h.((incl Image F).b) by A52,FUNCT_1:23
      .= h.y by TMAP_1:95
      .= g.y' by A7,FUNCT_1:72;
    hence x = y by A8,WAYBEL_1:def 1;
  end;
  incl Image F is continuous by TMAP_1:98;
  then
A53: H is continuous by A6;
  for P being Subset of Image F st P is open holds (H")"P is open
  proof
    let P be Subset of Image F;
    assume P is open;
    then P in the topology of Image F by PRE_TOPC:def 5;
    then consider Q being Subset of S such that
A54: Q in the topology of S and
A55: P = Q /\ [#](Image F) by PRE_TOPC:def 9;
    reconsider Q as Subset of S;
A56: Q is open by A54,PRE_TOPC:def 5;
    p is continuous by TMAP_1:98;
    then p*(corestr f) is continuous by A4;
    then (p*(corestr f))"Q is open by A2,A56,TOPS_2:55;
    then
A57: f"Q is open by A13;
A58: (incl Image F).:P = P
    proof
      hereby
        let y be set;
        assume y in (incl Image F).:P;
        then consider x being set such that
A59:    x in dom (incl Image F) and
A60:    x in P and
A61:    y = (incl Image F).x by FUNCT_1:def 12;
        x in the carrier of Image F by A59;
        then x in rng F by Th10;
        then reconsider xx = x as Point of S;
        (incl Image F).xx = x by A59,TMAP_1:95;
        hence y in P by A60,A61;
      end;
      let y be set;
      assume
A62:  y in P;
      then
A63:  y in the carrier of Image F;
      then
A64:  y in dom (incl Image F) by FUNCT_2:def 1;
      y in rng F by A63,Th10;
      then reconsider yy = y as Point of S;
      yy = (incl Image F).y by A62,TMAP_1:95;
      hence y in (incl Image F).:P by A62,A64,FUNCT_1:def 12;
    end;
A65: f"Q = h.:P
    proof
      hereby
        let x be set;
        assume
A66:    x in f"Q;
        then
A67:    x in dom f by FUNCT_1:def 13;
A68:    f.x in Q by A66,FUNCT_1:def 13;
        f.x in rng f by A67,FUNCT_1:def 5;
        then
A69:    f.x in the carrier of S;
        then
A70:    f.x in dom h by FUNCT_2:def 1;
A71:    f.x in dom F by A69,FUNCT_2:def 1;
        F.(f.x) = f.(h.(f.x)) by A70,FUNCT_1:23
          .= f.((id T).x) by A26,A67,FUNCT_1:23
          .= f.x by A66,FUNCT_1:35;
        then f.x in rng F by A71,FUNCT_1:def 5;
        then f.x in the carrier of Image F by Th10;
        then
A72:    f.x in P by A55,A68,XBOOLE_0:def 4;
        h.(f.x) = (id T).x by A26,A67,FUNCT_1:23
          .= x by A66,FUNCT_1:35;
        hence x in h.:P by A70,A72,FUNCT_1:def 12;
      end;
      let x be set;
      assume x in h.:P;
      then consider y being set such that
A73:  y in dom h and
A74:  y in P and
A75:  x = h.y by FUNCT_1:def 12;
A76:  x in rng h by A73,A75,FUNCT_1:def 5;
A77:  y in Q by A55,A74,XBOOLE_0:def 4;
      y in the carrier of Image F by A74;
      then
A78:  y in rng F by Th10;
      f.x in rng f by A12,A76,FUNCT_1:def 5;
      then reconsider a = f.x, b = y as Element of Image f by A27,A78,Th10;
      g.a = h.(f.x) by A7,FUNCT_1:72
        .= (id T).x by A12,A26,A76,FUNCT_1:23
        .= h.y by A75,A76,FUNCT_1:35
        .= g .b by A7,FUNCT_1:72;
      then f.x in Q by A8,A77,WAYBEL_1:def 1;
      hence x in f"Q by A12,A76,FUNCT_1:def 13;
    end;
    H.:P = h.:P by A58,RELAT_1:159;
    hence (H")"P is open by A35,A45,A57,A65,TOPS_2:67;
  end;
  then H" is continuous by A34,TOPS_2:55;
  then H is_homeomorphism by A33,A35,A45,A53,TOPS_2:def 5;
  hence Image F,T are_homeomorphic by T_0TOPSP:def 1;
end;

definition
  func Sierpinski_Space -> strict TopStruct means
  :Def9:
  the carrier of it = {0,1} & the topology of it = {{}, {1}, {0,1} };
  existence
  proof
    set c = {0,1}, t = {{}, {1}, {0,1} };
    t c= bool c
    proof
      let x be set;
      assume
A1:   x in t;
      per cases by A1,ENUMSET1:def 1;
      suppose
A2:     x = {};
        {} c= c by XBOOLE_1:2;
        hence thesis by A2;
      end;
      suppose x = {1};
        then x c= c by ZFMISC_1:12;
        hence x in bool c;
        thus thesis;
      end;
      suppose x = {0,1};
        then x c= c;
        hence thesis;
      end;
    end;
    then reconsider t as Subset-Family of c;
    take s = TopStruct (# c, t #);
    thus the carrier of s = {0,1};
    thus the topology of s = {{}, {1}, {0,1} };
  end;
  uniqueness;
end;

registration
  cluster Sierpinski_Space -> non empty TopSpace-like;
  coherence
  proof
    set IT = Sierpinski_Space;
    thus IT is non empty by Def9;
    {0,1} in {{}, {1}, {0,1} } by ENUMSET1:def 1;
    then the carrier of IT in {{}, {1}, {0,1} } by Def9;
    hence the carrier of IT in the topology of IT by Def9;
    thus for a being Subset-Family of IT
    st a c= the topology of IT holds union a in the topology of IT
    proof
      let a be Subset-Family of IT;
      assume a c= the topology of IT;
      then
A1:   a c= {{}, {1}, {0,1} } by Def9;
      per cases by A1,ZFMISC_1:142;
      suppose a = {};
        then union a in {{}, {1}, {0,1} } by ENUMSET1:def 1,ZFMISC_1:2;
        hence union a in the topology of IT by Def9;
      end;
      suppose a = {{}};
        then union a = {} by ZFMISC_1:31;
        then union a in {{}, {1}, {0,1} } by ENUMSET1:def 1;
        hence union a in the topology of IT by Def9;
      end;
      suppose a = {{1}};
        then union a = {1} by ZFMISC_1:31;
        then union a in {{}, {1}, {0,1} } by ENUMSET1:def 1;
        hence union a in the topology of IT by Def9;
      end;
      suppose a = {{0,1}};
        then union a = {0,1} by ZFMISC_1:31;
        then union a in {{}, {1}, {0,1} } by ENUMSET1:def 1;
        hence union a in the topology of IT by Def9;
      end;
      suppose a = {{},{1}};
        then union a = {} \/ {1} by ZFMISC_1:93;
        then union a in {{}, {1}, {0,1} } by ENUMSET1:def 1;
        hence union a in the topology of IT by Def9;
      end;
      suppose a = {{1},{0,1}};
        then union a = {1} \/ {0,1} by ZFMISC_1:93
          .= {0,1} by ZFMISC_1:14;
        then union a in {{} , {1}, {0,1}} by ENUMSET1:def 1;
        hence union a in the topology of IT by Def9;
      end;
      suppose a = {{},{0,1}};
        then union a = {} \/ {0,1} by ZFMISC_1:93;
        then union a in {{}, {1}, {0,1} } by ENUMSET1:def 1;
        hence union a in the topology of IT by Def9;
      end;
      suppose a = {{},{1},{0,1}};
        then a = {{} } \/ {{1},{0,1}} by ENUMSET1:42;
        then union a = union {{}} \/ union {{1},{0,1}} by ZFMISC_1:96
          .= {} \/ union {{1},{0,1}} by ZFMISC_1:31
          .= {1} \/ {0,1} by ZFMISC_1:93
          .= {0,1} by ZFMISC_1:14;
        then union a in {{}, {1}, {0,1} } by ENUMSET1:def 1;
        hence union a in the topology of IT by Def9;
      end;
    end;
    let a,b be Subset of IT;
    assume a in the topology of IT & b in the topology of IT;
    then
A2: a in {{}, {1}, {0,1} } & b in {{}, {1}, {0,1} } by Def9;
    per cases by A2,ENUMSET1:def 1;
    suppose a = {} & b = {};
      then a /\ b in {{}, {1}, {0,1} } by ENUMSET1:def 1;
      hence thesis by Def9;
    end;
    suppose a = {} & b = {1};
      then a /\ b in {{}, {1}, {0,1} } by ENUMSET1:def 1;
      hence thesis by Def9;
    end;
    suppose a = {} & b = {0,1};
      then a /\ b in {{}, {1}, {0,1} } by ENUMSET1:def 1;
      hence thesis by Def9;
    end;
    suppose a = {1} & b = {};
      then a /\ b in {{}, {1}, {0,1} } by ENUMSET1:def 1;
      hence thesis by Def9;
    end;
    suppose a = {1} & b = {1};
      then a /\ b in {{}, {1}, {0,1} } by ENUMSET1:def 1;
      hence thesis by Def9;
    end;
    suppose a = {1} & b = {0,1};
      then a /\ b = {1} by ZFMISC_1:19;
      then a /\ b in {{}, {1}, {0,1} } by ENUMSET1:def 1;
      hence thesis by Def9;
    end;
    suppose a = {0,1} & b = {};
      then a /\ b in {{}, {1}, {0,1} } by ENUMSET1:def 1;
      hence thesis by Def9;
    end;
    suppose a = {0,1} & b = {1};
      then a /\ b = {1} by ZFMISC_1:19;
      then a /\ b in {{}, {1}, {0,1} } by ENUMSET1:def 1;
      hence thesis by Def9;
    end;
    suppose a = {0,1} & b = {0,1};
      then a /\ b in {{}, {1}, {0,1} } by ENUMSET1:def 1;
      hence thesis by Def9;
    end;
  end;
end;

Lm1: Sierpinski_Space is T_0
proof
  set S = Sierpinski_Space;
  for x,y being Point of S st x <> y holds
  ex V being Subset of S st V is open &
  ((x in V & not y in V) or (y in V & not x in V))
  proof
    let x,y be Point of S;
    assume
A1: x <> y;
    x in the carrier of S & y in the carrier of S;
    then x in {0,1} & y in {0,1} by Def9;
    then
A2: (x = 0 or x = 1) & (y = 0 or y = 1) by TARSKI:def 2;
    set V = {1};
    {1} c= {0,1} by ZFMISC_1:12;
    then reconsider V as Subset of S by Def9;
    {1} in {{}, {1}, {0,1} } by ENUMSET1:def 1;
    then {1} in the topology of S by Def9;
    then
A3: V is open by PRE_TOPC:def 5;
    per cases by A1,A2;
    suppose x = 0 & y = 1;
      then (x in V & not y in V) or (y in V & not x in V) by TARSKI:def 1;
      hence ex V being Subset of S st V is open &
      ((x in V & not y in V) or (y in V & not x in V)) by A3;
    end;
    suppose x = 1 & y = 0;
      then ((x in V & not y in V) or (y in V & not x in V)) by TARSKI:def 1;
      hence ex V being Subset of S st V is open &
      ((x in V & not y in V) or (y in V & not x in V)) by A3;
    end;
  end;
  hence thesis by T_0TOPSP:def 7;
end;

registration
  cluster Sierpinski_Space -> T_0;
  coherence by Lm1;
end;

registration       ::p.122 lemma 3.3.
  cluster Sierpinski_Space -> injective;
  coherence
  proof
    set S = Sierpinski_Space;
    let X be non empty TopSpace;
    let f be Function of X, S;
    assume
A1: f is continuous;
A2: [#]S <> {};
    let Y be non empty TopSpace;
    assume
A3: X is SubSpace of Y;
    then
A4: the carrier of X c= the carrier of Y by BORSUK_1:35;
    {1} c= {0,1} by ZFMISC_1:12;
    then reconsider u = {1} as Subset of S by Def9;
    u in {{}, {1}, {0,1} } by ENUMSET1:def 1;
    then u in the topology of S by Def9;
    then u is open by PRE_TOPC:def 5;
    then f"u is open by A1,A2,TOPS_2:55;
    then f"u in the topology of X by PRE_TOPC:def 5;
    then consider V being Subset of Y such that
A5: V in the topology of Y and
A6: f"u = V /\ [#](X) by A3,PRE_TOPC:def 9;
    reconsider V as Subset of Y;
A7: V is open by A5,PRE_TOPC:def 5;
    set g = chi(V,the carrier of Y);
A8: dom g = the carrier of Y by FUNCT_3:def 3;
    the carrier of S = {0,1} by Def9;
    then reconsider g as Function of Y,S;
    take g;
    for P being Subset of S st P is open holds g"P is open
    proof
      let P be Subset of S;
      assume P is open;
      then P in the topology of S by PRE_TOPC:def 5;
      then
A9:   P in {{}, {1}, {0,1} } by Def9;
      per cases by A9,ENUMSET1:def 1;
      suppose P = {};
        then g"P = {} by RELAT_1:171;
        then g"P in the topology of Y by PRE_TOPC:5;
        hence g"P is open by PRE_TOPC:def 5;
      end;
      suppose
A10:    P = {1};
A11:    g"P c= V
        proof
          let x be set;
          assume
A12:      x in g"P;
          then x in dom g & g.x in {1} by A10,FUNCT_1:def 13;
          then g.x = 1 by TARSKI:def 1;
          hence x in V by A12,FUNCT_3:def 3;
        end;
        V c= g"P
        proof
          let x be set;
          assume
A13:      x in V;
          then g.x = 1 by FUNCT_3:def 3;
          then x in dom g & g.x in {1} by A8,A13,TARSKI:def 1;
          hence x in g"P by A10,FUNCT_1:def 13;
        end;
        hence g"P is open by A7,A11,XBOOLE_0:def 10;
      end;
      suppose P = {0,1};
        then g"P = the carrier of Y by FUNCT_2:48;
        then g"P in the topology of Y by PRE_TOPC:def 1;
        hence g"P is open by PRE_TOPC:def 5;
      end;
    end;
    hence g is continuous by A2,TOPS_2:55;
A14: dom g /\ (the carrier of X) = (the carrier of Y) /\ (the carrier of X)
    by FUNCT_3:def 3
      .= the carrier of X by A3,BORSUK_1:35,XBOOLE_1:28
      .= dom f by FUNCT_2:def 1;
    for x being set st x in dom f holds f.x = g.x
    proof
      let x be set;
      assume
A15:  x in dom f;
      then
A16:  x in the carrier of X;
      per cases;
      suppose
A17:    x in V;
        then
A18:    g.x = 1 by FUNCT_3:def 3;
        x in f"u by A6,A15,A17,XBOOLE_0:def 4;
        then x in dom f & f.x in {1} by FUNCT_1:def 13;
        hence f.x = g.x by A18,TARSKI:def 1;
      end;
      suppose
A19:    not x in V;
        f.x in rng f by A15,FUNCT_1:def 5;
        then f.x in the carrier of S;
        then f.x in {0,1} by Def9;
        then
A20:    f.x = 0 or f.x = 1 by TARSKI:def 2;
        not x in f"{1} by A6,A19,XBOOLE_0:def 4;
        then not x in dom f or not f.x in {1} by FUNCT_1:def 13;
        hence f.x = g.x by A4,A15,A16,A19,A20,FUNCT_3:def 3,TARSKI:def 1;
      end;
    end;
    hence g|(the carrier of X) = f by A14,FUNCT_1:68;
  end;
end;

registration
  let I be set;
  let S be non empty 1-sorted;
  cluster I --> S -> non-Empty;
  coherence
  proof
    let s be 1-sorted;
    assume
A1: s in rng (I --> S);
    rng (I --> S) c= {S} by FUNCOP_1:19;
    hence s is non empty by A1,TARSKI:def 1;
  end;
end;

registration
  let I be set;
  let T be TopStruct;
  cluster I --> T -> TopSpace-yielding;
  coherence
  proof
    let x be set;
    assume
A1: x in rng (I --> T);
    rng (I --> T) c= {T} by FUNCOP_1:19;
    hence x is TopStruct by A1,TARSKI:def 1;
  end;
end;

registration
  let I be non empty set;
  let L be non empty antisymmetric RelStr;
  cluster product (I --> L) -> antisymmetric;
  coherence
  proof
    for i being Element of I holds (I --> L).i is antisymmetric
    by FUNCOP_1:13;
    hence product (I --> L) is antisymmetric by WAYBEL_3:30;
  end;
end;

registration
  let I be non empty set;
  let L be non empty transitive RelStr;
  cluster product (I --> L) -> transitive;
  coherence
  proof
    for i being Element of I holds (I --> L).i is transitive by FUNCOP_1:13;
    hence product (I --> L) is transitive by WAYBEL_3:29;
  end;
end;

theorem
  for T being Scott TopAugmentation of BoolePoset 1
  holds the topology of T = the topology of Sierpinski_Space
proof
  let T be Scott TopAugmentation of BoolePoset 1;
A1: the RelStr of T = BoolePoset 1 by YELLOW_9:def 4;
A2: LattPOSet BooleLatt 1 = RelStr(#the carrier of BooleLatt 1,
    LattRel(BooleLatt 1)#) by LATTICE3:def 2;
A3: the carrier of T = the carrier of LattPOSet BooleLatt 1 by A1,
YELLOW_1:def 2
    .= bool 1 by A2,LATTICE3:def 1
    .= {0,1} by CARD_5:1,ZFMISC_1:30;
  then reconsider j = 1, z = 0 as Element of BoolePoset 1 by A1,TARSKI:def 2;
  reconsider c = {z} as Subset of T by A1;
A4: now
    let y be set;
    assume
A5: y in the topology of T;
    then reconsider x = y as Subset of T;
A6: x = {} or x = {0} or x = {1} or x = {0,1} by A3,ZFMISC_1:42;
    set a = 0, b = 1;
A7: a c= b by XBOOLE_1:2;
A8: a in {z} by TARSKI:def 1;
    not b in {z} by TARSKI:def 1;
    then not {z} is upper by A7,A8,WAYBEL_7:11;
    then not c is upper by A1,WAYBEL_0:25;
    then not c is open by WAYBEL11:def 4;
    then y in {{}, {1}, {0,1} } by A5,A6,ENUMSET1:def 1,PRE_TOPC:def 5;
    hence y in the topology of Sierpinski_Space by Def9;
  end;
  now
    let x be set;
    assume x in the topology of Sierpinski_Space;
    then
A9: x in {{}, {1}, {0,1} } by Def9;
    per cases by A9,ENUMSET1:def 1;
    suppose x = {};
      hence x in the topology of T by PRE_TOPC:5;
    end;
    suppose
A10:  x = {1};
      then reconsider x' = x as Subset of T by A3,ZFMISC_1:12;
      for a,b being Element of T st a in x' & a <= b holds b in x'
      proof
        let a,b be Element of T;
        assume that
A11:    a in x' and
A12:    a <= b;
A13:    a = 1 by A10,A11,TARSKI:def 1;
A14:    b = 0 or b = 1 by A3,TARSKI:def 2;
        b <> 0
        proof
          assume
A15:      b = 0;
          [a, b] in the InternalRel of T by A12,ORDERS_2:def 9;
          then j <= z by A1,A13,A15,ORDERS_2:def 9;
          then
A16:      1 c= 0 by YELLOW_1:2;
          thus thesis by A16;
        end;
        hence b in x' by A10,A14,TARSKI:def 1;
      end;
      then
A17:  x' is upper by WAYBEL_0:def 20;
      for D being non empty directed Subset of T st sup D in x'
      holds D meets x'
      proof
        let D be non empty directed Subset of T;
        assume
A18:    sup D in x';
        D <> {0}
        proof
          assume
A19:      D = {0};
          ex_sup_of D, BoolePoset 1 by YELLOW_0:17;
          then sup D = sup {z} by A1,A19,YELLOW_0:26
            .= 0 by YELLOW_0:39;
          hence thesis by A10,A18,TARSKI:def 1;
        end;
        then D = {1} or D = {0,1} by A3,ZFMISC_1:42;
        then
A20:    1 in D by TARSKI:def 1,def 2;
        1 in x' by A10,TARSKI:def 1;
        hence D meets x' by A20,XBOOLE_0:3;
      end;
      then x' is inaccessible by WAYBEL11:def 1;
      then x' is open by A17,WAYBEL11:def 4;
      hence x in the topology of T by PRE_TOPC:def 5;
    end;
    suppose x = {0,1};
      hence x in the topology of T by A3,PRE_TOPC:def 1;
    end;
  end;
  hence thesis by A4,TARSKI:2;
end;

theorem Th14:
  for I being non empty set holds
  {product ((Carrier (I --> Sierpinski_Space))+*(i,{1}))
  where i is Element of I: not contradiction }
  is prebasis of product (I --> Sierpinski_Space)
proof
  let I be non empty set;
  set IS = I --> Sierpinski_Space, pB = { product ((Carrier IS)+*(i,{1}))
  where i is Element of I: not contradiction }, P = product_prebasis IS;
  pB c= bool the carrier of product IS
  proof
    let x be set;
    assume x in pB;
    then consider i being Element of I such that
A1: x = product ((Carrier IS)+*(i,{1}));
    product ((Carrier IS)+*(i,{1})) c= product Carrier IS
    proof
      let y be set;
      assume y in product ((Carrier IS)+*(i,{1}));
      then consider g being Function such that
A2:   y = g and
A3:   dom g = dom ((Carrier IS)+*(i,{1})) and
A4:   for z being set st z in dom ((Carrier IS)+*(i,{1}))
      holds g.z in ((Carrier IS)+*(i,{1})).z by CARD_3:def 5;
A5:   dom g = dom Carrier IS by A3,FUNCT_7:32;
      for z being set st z in dom (Carrier IS) holds g.z in ( Carrier IS ) . z
      proof
        let z be set;
        assume
A6:     z in dom (Carrier IS);
        then z in dom ((Carrier IS)+*(i,{1})) by FUNCT_7:32;
        then
A7:     g.z in ((Carrier IS)+*(i,{1})).z by A4;
A8:     z in I by A6,PBOOLE:def 3;
        then consider R being 1-sorted such that
A9:     R = IS.z and
A10:    (Carrier IS).z = the carrier of R by PRALG_1:def 13;
A11:    the carrier of R = the carrier of Sierpinski_Space
        by A8,A9,FUNCOP_1:13
          .= {0,1} by Def9;
        per cases;
        suppose z = i;
          then ((Carrier IS)+*(i,{1})).z = {1} by A6,FUNCT_7:33;
          then g.z = 1 by A7,TARSKI:def 1;
          hence g.z in (Carrier IS).z by A10,A11,TARSKI:def 2;
        end;
        suppose z <> i;
          hence g.z in (Carrier IS).z by A7,FUNCT_7:34;
        end;
      end;
      hence y in product Carrier IS by A2,A5,CARD_3:def 5;
    end;
    then x c= the carrier of product IS by A1,Def3;
    hence x in bool the carrier of product IS;
  end;
  then reconsider B = pB as Subset-Family of product IS;
  reconsider B as Subset-Family of product IS;
A12: B c= P
  proof
    let x be set;
    assume
A13: x in B;
    then consider i being Element of I such that
A14: x = product ((Carrier IS)+*(i,{1}));
    reconsider y = x as Subset of product Carrier IS by A13,Def3;
    {1} c= {0,1} by ZFMISC_1:12;
    then reconsider V = {1} as Subset of Sierpinski_Space by Def9;
    {1} in {{}, {1}, {0,1} } by ENUMSET1:def 1;
    then {1} in the topology of Sierpinski_Space by Def9;
    then
A15: V is open by PRE_TOPC:def 5;
A16: Sierpinski_Space = IS.i by FUNCOP_1:13;
    y = product ((Carrier IS) +* (i,V)) by A14;
    hence x in P by A15,A16,Def2;
  end;
A17: P is prebasis of product IS by Def3;
  then
A18: P c= the topology of product IS by CANTOR_1:def 5;
  reconsider P as Subset-Family of product IS by Def3;
  reconsider P as Subset-Family of product IS;
  FinMeetCl P is Basis of product IS by A17,YELLOW_9:23;
  then reconsider F = (FinMeetCl P) \ {{}} as Basis of product IS by Th3;
A19: F c= FinMeetCl B
  proof
    let x be set;
    assume
A20: x in F;
    then reconsider y = x as Subset of product IS;
A21: not x in {{}} by A20,XBOOLE_0:def 5;
    x in FinMeetCl P by A20,XBOOLE_0:def 5;
    then consider Y1 being Subset-Family of product IS such that
A22: Y1 c= P and
A23: Y1 is finite and
A24: y = Intersect Y1 by CANTOR_1:def 4;
A25: the carrier of product IS = product Carrier IS by Def3;
    reconsider Y2 = Y1 /\ B as Subset-Family of product IS;
A26: Y2 c= B by XBOOLE_1:17;
A27: Y2 is finite by A23,FINSET_1:13,XBOOLE_1:17;
A28: Intersect Y1 c= Intersect Y2 by SETFAM_1:59,XBOOLE_1:17;
    Intersect Y2 c= Intersect Y1
    proof
      let z be set;
      assume
A29:  z in Intersect Y2;
      then
A30:  z in product Carrier IS by A25;
      for Y being set st Y in Y1 holds z in Y
      proof
        let Y be set;
        assume
A31:    Y in Y1;
        then reconsider Y' = Y as Subset of product Carrier IS by Def3;
        consider i being set, S being TopStruct, V being Subset of S such that
A32:    i in I and
A33:    V is open and
A34:    S = IS.i and
A35:    Y' = product ((Carrier IS) +* (i,V)) by A22,A31,Def2;
        consider RR being 1-sorted such that
A36:    RR = IS.i and
A37:    (Carrier IS).i = the carrier of RR by A32,PRALG_1:def 13;
A38:    i in dom (Carrier IS) by A32,PBOOLE:def 3;
        reconsider V' = V as Subset of Sierpinski_Space
        by A32,A34,FUNCOP_1:13;
        V in the topology of S by A33,PRE_TOPC:def 5;
        then V' in the topology of Sierpinski_Space by A32,A34,FUNCOP_1:13;
        then
A39:    V' in {{}, {1}, {0,1} } by Def9;
A40:    V' <> {}
        proof
          assume
A41:      V' = {};
A42:      ((Carrier IS)+*(i,{})).i = {} by A38,FUNCT_7:33;
          i in dom ((Carrier IS)+*(i,{})) by A38,FUNCT_7:32;
          then {} in rng ((Carrier IS)+*(i,{})) by A42,FUNCT_1:def 5;
          then Y' = {} by A35,A41,CARD_3:37;
          then y = {} by A24,A31,MSSUBFAM:3;
          hence thesis by A21,TARSKI:def 1;
        end;
        reconsider i' = i as Element of I by A32;
        per cases by A39,A40,ENUMSET1:def 1;
        suppose V' = {1};
          then Y' = product ((Carrier IS)+*(i',{1})) by A35;
          then Y in B;
          then Y in Y2 by A31,XBOOLE_0:def 4;
          hence z in Y by A29,SETFAM_1:58;
        end;
        suppose V' = {0,1};
          then V' = the carrier of Sierpinski_Space by Def9
            .= (Carrier IS).i by A32,A36,A37,FUNCOP_1:13;
          hence z in Y by A30,A35,FUNCT_7:37;
        end;
      end;
      hence z in Intersect Y1 by A29,SETFAM_1:58;
    end;
    then y = Intersect Y2 by A24,A28,XBOOLE_0:def 10;
    hence x in FinMeetCl B by A26,A27,CANTOR_1:def 4;
  end;
  pB c= the topology of product IS by A12,A18,XBOOLE_1:1;
  hence pB is prebasis of product (I --> Sierpinski_Space)
  by A19,CANTOR_1:def 5;
end;

registration
  let I be non empty set;
  let L be complete LATTICE;
  cluster product (I --> L) -> with_suprema complete;
  coherence
  proof reconsider IB = I --> L as
    RelStr-yielding non-Empty reflexive-yielding ManySortedSet of I;
    for i being Element of I holds IB.i is complete LATTICE by FUNCOP_1:13;
    hence product (I --> L) is with_suprema complete by WAYBEL_3:31;
  end;
end;

registration
  let I be non empty set;
  let X be algebraic lower-bounded LATTICE;
  cluster product (I --> X) -> algebraic;
  coherence
  proof
    set IB = I --> X;
    for i being Element of I holds IB.i is algebraic lower-bounded LATTICE
    by FUNCOP_1:13;
    hence product (I --> X) is algebraic by WAYBEL13:25;
  end;
end;

theorem Th15:
  for X being non empty set
  ex f being Function of BoolePoset X, product (X --> BoolePoset 1) st
  f is isomorphic & for Y being Subset of X holds f.Y = chi(Y,X)
proof
  let X be non empty set;
  set XB = X --> BoolePoset 1;
  LattPOSet BooleLatt X = RelStr(#the carrier of BooleLatt X,
    LattRel(BooleLatt X)#) by LATTICE3:def 2;
  then
A1: the carrier of BoolePoset X = the carrier of BooleLatt X by YELLOW_1:def 2
    .= bool X by LATTICE3:def 1;
  LattPOSet BooleLatt 1 = RelStr(#the carrier of BooleLatt 1,
    LattRel(BooleLatt 1)#) by LATTICE3:def 2;
  then
A2: the carrier of BoolePoset 1 = the carrier of BooleLatt 1 by YELLOW_1:def 2
    .= bool {{}} by CARD_5:1,LATTICE3:def 1
    .= {0,1} by CARD_5:1,ZFMISC_1:30;
A3: the carrier of product XB = product Carrier XB by YELLOW_1:def 4;
  deffunc F(set) = chi($1,X);
  consider f being Function such that
A4: dom f = bool X and
A5: for Y being set st Y in bool X holds f.Y = F(Y) from FUNCT_1:sch 3;
  rng f c= product Carrier XB
  proof
    let y be set;
    assume y in rng f;
    then consider x being set such that
A6: x in dom f and
A7: y = f.x by FUNCT_1:def 5;
A8: chi(x,X) = y by A4,A5,A6,A7;
A9: dom chi(x,X) = X by FUNCT_3:def 3
      .= dom (Carrier XB) by PBOOLE:def 3;
    for z being set st z in dom (Carrier XB) holds
    chi(x,X).z in (Carrier XB).z
    proof
      let z be set;
      assume z in dom (Carrier XB);
      then
A10:  z in X by PBOOLE:def 3;
      then consider R being 1-sorted such that
A11:  R = XB.z and
A12:  (Carrier XB).z = the carrier of R by PRALG_1:def 13;
A13:  (Carrier XB).z = {0,1} by A2,A10,A11,A12,FUNCOP_1:13;
      per cases;
      suppose z in x;
        then chi(x,X).z = 1 by A10,FUNCT_3:def 3;
        hence chi(x,X).z in (Carrier XB).z by A13,TARSKI:def 2;
      end;
      suppose not z in x;
        then chi(x,X).z = 0 by A10,FUNCT_3:def 3;
        hence chi(x,X).z in (Carrier XB).z by A13,TARSKI:def 2;
      end;
    end;
    hence y in product Carrier XB by A8,A9,CARD_3:def 5;
  end;
  then reconsider f as Function of BoolePoset X, product (X --> BoolePoset 1)
  by A1,A3,A4,FUNCT_2:def 1,RELSET_1:11;
  take f;
  for A,B being Element of BoolePoset X st f.A = f.B holds A = B
  proof
    let A, B be Element of BoolePoset X;
    assume
A14: f.A = f.B;
    f.A = chi(A,X) & f.B = chi(B,X) by A1,A5;
    hence A = B by A1,A14,FUNCT_3:47;
  end;
  then
A15: f is one-to-one by WAYBEL_1:def 1;
  product Carrier XB c= rng f
  proof
    let z be set;
    assume z in product Carrier XB;
    then consider g being Function such that
A16: z = g and
A17: dom g = dom (Carrier XB) and
A18: for y being set st y in dom (Carrier XB) holds
    g.y in (Carrier XB).y by CARD_3:def 5;
    set A = g"{1};
A19: A c= X
    proof
      let a be set;
      assume a in A;
      then a in dom g by FUNCT_1:def 13;
      hence a in X by A17,PBOOLE:def 3;
    end;
A20: dom chi(A,X) = X by FUNCT_3:def 3
      .= dom g by A17,PBOOLE:def 3;
    for a being set st a in dom chi(A,X) holds chi(A,X).a = g.a
    proof
      let a be set;
      assume
A21:  a in dom chi(A,X);
      then
A22:  a in X;
      consider R being 1-sorted such that
A23:  R = XB.a and
A24:  (Carrier XB).a = the carrier of R by A21,PRALG_1:def 13;
A25:  (Carrier XB).a = {0,1} by A2,A21,A23,A24,FUNCOP_1:13;
      a in dom (Carrier XB) by A22,PBOOLE:def 3;
      then g.a in (Carrier XB).a by A18;
      then
A26:  g.a = 0 or g.a = 1 by A25,TARSKI:def 2;
      per cases;
      suppose a in A;
        then chi(A,X).a = 1 & g.a in {1} by A21,FUNCT_1:def 13,FUNCT_3:def 3;
        hence chi(A,X).a = g.a by TARSKI:def 1;
      end;
      suppose
A27:    not a in A;
        g.a <> 1
        proof
          assume g.a = 1;
          then g.a in {1} by TARSKI:def 1;
          hence thesis by A20,A21,A27,FUNCT_1:def 13;
        end;
        hence chi(A,X).a = g.a by A21,A26,A27,FUNCT_3:def 3;
      end;
    end;
    then z = chi(A,X) by A16,A20,FUNCT_1:9
      .= f.A by A5,A19;
    hence z in rng f by A4,A19,FUNCT_1:def 5;
  end;
  then
A28: rng f = the carrier of product XB by A3,XBOOLE_0:def 10;
  for A,B being Element of BoolePoset X holds A <= B iff f.A <= f.B
  proof
    let A,B be Element of BoolePoset X;
A29: f.A = chi(A,X) & f.B = chi(B,X) by A1,A5;
    hereby
      assume A <= B;
      then
A30:  A c= B by YELLOW_1:2;
      for i being set st i in X ex R being RelStr, xi,yi being Element of R
      st R = XB.i & xi = chi(A,X).i & yi = chi(B,X).i & xi <= yi
      proof
        let i be set;
        assume
A31:    i in X;
        take R = BoolePoset 1;
        per cases;
        suppose
A32:      i in A;
          reconsider xi = 1, yi = 1 as Element of R by A2,TARSKI:def 2;
          take xi,yi;
          thus R = XB.i by A31,FUNCOP_1:13;
          thus xi = chi(A,X).i by A31,A32,FUNCT_3:def 3;
          thus yi = chi(B,X).i by A30,A31,A32,FUNCT_3:def 3;
          thus xi <= yi;
        end;
        suppose
A33:      not i in A;
          thus thesis
          proof
            per cases;
            suppose
A34:          i in B;
              reconsider xi = 0, yi = 1 as Element of R by A2,TARSKI:def 2;
              take xi,yi;
              thus R = XB.i by A31,FUNCOP_1:13;
              thus xi = chi(A,X).i by A31,A33,FUNCT_3:def 3;
              thus yi = chi(B,X).i by A31,A34,FUNCT_3:def 3;
              xi c= yi by XBOOLE_1:2;
              hence xi <= yi by YELLOW_1:2;
            end;
            suppose
A35:          not i in B;
              reconsider xi = 0, yi = 0 as Element of R by A2,TARSKI:def 2;
              take xi,yi;
              thus R = XB.i by A31,FUNCOP_1:13;
              thus xi = chi(A,X).i by A31,A33,FUNCT_3:def 3;
              thus yi = chi(B,X).i by A31,A35,FUNCT_3:def 3;
              thus xi <= yi;
            end;
          end;
        end;
      end;
      hence f.A <= f.B by A3,A29,YELLOW_1:def 4;
    end;
    assume f.A <= f.B;
    then consider h1,h2 being Function such that
A36: h1 = f.A and
A37: h2 = f.B and
A38: for i be set st i in X ex R being RelStr, xi,yi being Element of R
    st R = XB.i & xi = h1.i & yi = h2.i & xi <= yi by A3,YELLOW_1:def 4;
    A c= B
    proof
      let i be set;
      assume
A39:  i in A;
      then consider R being RelStr, xi,yi being Element of R such that
A40:  R = XB.i and
A41:  xi = h1.i and
A42:  yi = h2.i and
A43:  xi <= yi by A1,A38;
A44:  R = BoolePoset 1 by A1,A39,A40,FUNCOP_1:13;
A45:  xi = chi(A,X).i by A1,A5,A36,A41
        .= 1 by A1,A39,FUNCT_3:def 3;
      reconsider a = xi, b = yi as Element of BoolePoset 1
      by A1,A39,A40,FUNCOP_1:13;
A46:  a <= b by A1,A39,A40,A43,FUNCOP_1:13;
A47:  yi <> 0
      proof
        assume yi = 0;
        then
A48:    1 c= 0 by A45,A46,YELLOW_1:2;
        thus thesis by A48;
      end;
      chi(B,X).i = h2.i by A1,A5,A37
        .= 1 by A2,A42,A44,A47,TARSKI:def 2;
      hence i in B by FUNCT_3:42;
    end;
    hence A <= B by YELLOW_1:2;
  end;
  hence f is isomorphic by A15,A28,WAYBEL_0:66;
  thus for Y being Subset of X holds f.Y = chi(Y,X) by A5;
end;

theorem Th16:  ::p.122 lemma 3.4.(i)
  for I being non empty set
  for T being Scott TopAugmentation of product (I --> BoolePoset 1) holds
  the topology of T = the topology of product (I --> Sierpinski_Space)
proof
  let I be non empty set;
  let T be Scott TopAugmentation of product (I --> BoolePoset 1);
A1: the RelStr of T = product (I --> BoolePoset 1) by YELLOW_9:def 4;
  set IB = I --> BoolePoset 1, IS = I --> Sierpinski_Space;
  LattPOSet BooleLatt 1 = RelStr(#the carrier of BooleLatt 1,
    LattRel(BooleLatt 1)#) by LATTICE3:def 2;
  then
A2: the carrier of BoolePoset 1 = the carrier of BooleLatt 1 by YELLOW_1:def 2
    .= bool {{}} by CARD_5:1,LATTICE3:def 1
    .= {0,1} by CARD_5:1,ZFMISC_1:30;
A3: dom Carrier IB = I by PBOOLE:def 3
    .= dom Carrier IS by PBOOLE:def 3;
A4: for i being set st i in
  dom Carrier IB holds (Carrier IB).i = (Carrier IS).i
  proof
    let i be set;
    assume i in dom Carrier IB;
    then
A5: i in I by PBOOLE:def 3;
    then consider R1 being 1-sorted such that
A6: R1 = IB.i and
A7: (Carrier IB).i = the carrier of R1 by PRALG_1:def 13;
    consider R2 being 1-sorted such that
A8: R2 = IS.i and
A9: (Carrier IS).i = the carrier of R2 by A5,PRALG_1:def 13;
    the carrier of R1 = {0,1} by A2,A5,A6,FUNCOP_1:13
      .= the carrier of Sierpinski_Space by Def9
      .= the carrier of R2 by A5,A8,FUNCOP_1:13;
    hence (Carrier IB).i = (Carrier IS).i by A7,A9;
  end;
  then
A10: Carrier IB = Carrier IS by A3,FUNCT_1:9;
A11: the carrier of T = product Carrier (I --> BoolePoset 1) by A1,
YELLOW_1:def 4
    .= product Carrier (I --> Sierpinski_Space) by A3,A4,FUNCT_1:9
    .= the carrier of product (I --> Sierpinski_Space) by Def3;
A12: the carrier of product IB = product Carrier IB by YELLOW_1:def 4;
  reconsider P = {product ((Carrier IS)+*(ii,{1}))
  where ii is Element of I: not contradiction }
  as prebasis of product (I --> Sierpinski_Space) by Th14;
  reconsider T' = T as complete Scott TopLattice;
  T' is algebraic by A1,WAYBEL_8:17;
  then consider R being Basis of T' such that
A13: R = {uparrow yy where yy is Element of T' :
  yy in the carrier of CompactSublatt T'} by WAYBEL14:42;
  consider f being Function of BoolePoset I, product IB such that
A14: f is isomorphic and
A15: for Y being Subset of I holds f.Y = chi(Y,I) by Th15;
A16: rng f = the carrier of product IB by A14,WAYBEL_0:66;
A17: R c= FinMeetCl P
  proof
    let X be set;
    assume
A18: X in R;
    then consider Y being Element of T' such that
A19: X = uparrow Y and
A20: Y in the carrier of CompactSublatt T' by A13;
A21: Y is compact by A20,WAYBEL_8:def 1;
    reconsider X' = X as Subset of product IS by A11,A18;
    reconsider y = Y as Element of product IB by A1;
A22: y is compact by A1,A21,WAYBEL_8:9;
    consider x being set such that
A23: x in dom f and
A24: y = f.x by A16,FUNCT_1:def 5;
    reconsider x as Element of BoolePoset I by A23;
    reconsider x' = x as Subset of I by WAYBEL_8:28;
    deffunc F(Element of I) = product ((Carrier IS)+*($1,{1}));
    x is compact by A14,A22,A24,WAYBEL13:28;
    then
A25: x is finite by WAYBEL_8:30;
A26: uparrow Y = uparrow {Y} by WAYBEL_0:def 18
      .= uparrow {y} by A1,WAYBEL_0:13
      .= uparrow y by WAYBEL_0:def 18;
    set ZZ = {product ((Carrier IS)+*(jj,{1}))
    where jj is Element of I: jj in x };
A27: ZZ c= P
    proof
      let z be set;
      assume z in ZZ;
      then consider j being Element of I such that
A28:  z = product ((Carrier IS)+*(j,{1})) and j in x';
      thus z in P by A28;
    end;
    then reconsider ZZ as Subset-Family of product IS by XBOOLE_1:1;
A29: {F(jj) where jj is Element of I: jj in x} is finite
    from FRAENKEL:sch 21(A25);
A30: X' c= Intersect ZZ
    proof
      let z be set;
      assume
A31:  z in X';
      then
A32:  z in the carrier of product IB by A1,A11;
      reconsider z' = z as Element of product IB by A1,A11,A31;
      z in product Carrier IB by A32,YELLOW_1:def 4;
      then consider gg being Function such that
A33:  z = gg and
A34:  dom gg = dom (Carrier IB) and
A35:  for w being set st w in dom (Carrier IB)
      holds gg.w in (Carrier IB).w by CARD_3:def 5;
      y <= z' by A19,A26,A31,WAYBEL_0:18;
      then consider h1,h2 being Function such that
A36:  h1 = y and
A37:  h2 = z' and
A38:  for i be set st i in I ex R being RelStr, xi,yi being Element of R
      st R = IB.i & xi = h1.i & yi = h2.i & xi <= yi by A12,YELLOW_1:def 4;
A39:  h1 = f.x' by A24,A36
        .= chi(x,I) by A15;
      for Z being set st Z in ZZ holds z in Z
      proof
        let Z be set;
        assume Z in ZZ;
        then consider j being Element of I such that
A40:    Z = product ((Carrier IS)+*(j,{1})) and
A41:    j in x;
A42:    dom ((Carrier IS)+*(j,{1})) = dom (Carrier IS) by FUNCT_7:32
          .= I by PBOOLE:def 3;
        consider RS being RelStr, xj,yj being Element of RS such that
A43:    RS = IB.j and
A44:    xj = h1.j and
A45:    yj = h2.j and
A46:    xj <= yj by A38;
A47:    xj = 1 by A39,A41,A44,FUNCT_3:def 3;
        reconsider a = xj, b = yj
        as Element of BoolePoset 1 by A43,FUNCOP_1:13;
A48:    a <= b by A43,A46,FUNCOP_1:13;
A49:    b in the carrier of BoolePoset 1;
A50:    yj <> 0
        proof
          assume yj = 0;
          then
A51:      1 c= 0 by A47,A48,YELLOW_1:2;
          thus thesis by A51;
        end;
A52:    dom h2 = dom ((Carrier IS)+*(j,{1})) by A33,A34,A37,A42,PBOOLE:def 3;
        for w being set st w in dom ((Carrier IS)+*(j,{1}))
        holds h2.w in ((Carrier IS)+*(j,{1})).w
        proof
          let w be set;
          assume w in dom ((Carrier IS)+*(j,{1}));
          then
A53:      w in dom Carrier IS by A42,PBOOLE:def 3;
          per cases;
          suppose
A54:        w = j;
            then
A55:        ((Carrier IS)+*(j,{1})).w = {1} by A53,FUNCT_7:33;
            h2.w = 1 by A2,A45,A49,A50,A54,TARSKI:def 2;
            hence h2.w in ((Carrier IS)+*(j,{1})).w by A55,TARSKI:def 1;
          end;
          suppose w <> j;
            then ((Carrier IS)+*(j,{1})).w =(Carrier IS).w by FUNCT_7:34;
            hence h2.w in ((Carrier IS)+*(j,{1})).w by A10,A33,A35,A37,A53;
          end;
        end;
        hence z in Z by A37,A40,A52,CARD_3:def 5;
      end;
      hence z in Intersect ZZ by A31,SETFAM_1:58;
    end;
    Intersect ZZ c= X'
    proof
      let z be set;
      assume
A56:  z in Intersect ZZ;
      then
A57:  z in the carrier of product IB by A1,A11;
      reconsider z' = z as Element of product IB by A1,A11,A56;
      set h1 = chi(x,I);
A58:  h1 = f.x' by A15
        .= y by A24;
      z in product Carrier IB by A57,YELLOW_1:def 4;
      then consider h2 being Function such that
A59:  z = h2 and dom h2 = dom (Carrier IB) and
A60:  for w being set st w in dom (Carrier IB) holds
      h2.w in (Carrier IB).w by CARD_3:def 5;
      for i be set st i in I ex R being RelStr, xi,yi being Element of R
      st R = IB.i & xi = h1.i & yi = h2.i & xi <= yi
      proof
        let i be set;
        assume
A61:    i in I;
        then reconsider i' = i as Element of I;
        take RS = BoolePoset 1;
        consider RB being 1-sorted such that
A62:    RB = IB.i and
A63:    (Carrier IB).i = the carrier of RB by A61,PRALG_1:def 13;
A64:    (Carrier IB).i = {0,1} by A2,A61,A62,A63,FUNCOP_1:13;
A65:    i in dom (Carrier IB) by A61,PBOOLE:def 3;
        then
A66:    h2.i in (Carrier IB).i by A60;
        per cases;
        suppose
A67:      i in x;
          then product ((Carrier IS)+*(i',{1})) in ZZ;
          then z in product ((Carrier IS)+*(i',{1})) by A56,SETFAM_1:58;
          then consider h2' being Function such that
A68:      z = h2' and dom h2' = dom ((Carrier IS)+*(i',{1})) and
A69:      for w being set st w in dom ((Carrier IS)+*(i',{1})) holds
          h2'.w in ((Carrier IS)+*(i',{1})).w by CARD_3:def 5;
          i' in dom ((Carrier IS)+*(i',{1})) by A3,A65,FUNCT_7:32;
          then
A70:      h2'.i' in ((Carrier IS)+*(i',{1})).i' by A69;
A71:      ((Carrier IS)+*(i',{1})).i' = {1} by A3,A65,FUNCT_7:33;
          reconsider xi = 1, yi = 1 as Element of RS by A2,TARSKI:def 2;
          take xi,yi;
          thus RS = IB.i by A61,FUNCOP_1:13;
          thus xi = h1.i by A61,A67,FUNCT_3:def 3;
          thus yi = h2.i by A59,A68,A70,A71,TARSKI:def 1;
          thus xi <= yi;
        end;
        suppose
A72:      not i in x;
          thus thesis
          proof
            per cases by A64,A66,TARSKI:def 2;
            suppose
A73:          h2.i = 0;
              reconsider xi = 0, yi = 0 as Element of RS by A2,TARSKI:def 2;
              take xi,yi;
              thus RS = IB.i by A61,FUNCOP_1:13;
              thus xi = h1.i by A61,A72,FUNCT_3:def 3;
              thus yi = h2.i by A73;
              thus xi <= yi;
            end;
            suppose
A74:          h2.i = 1;
              reconsider xi = 0, yi = 1 as Element of RS by A2,TARSKI:def 2;
              take xi,yi;
              thus RS = IB.i by A61,FUNCOP_1:13;
              thus xi = h1.i by A61,A72,FUNCT_3:def 3;
              thus yi = h2.i by A74;
              xi c= yi by XBOOLE_1:2;
              hence xi <= yi by YELLOW_1:2;
            end;
          end;
        end;
      end;
      then y <= z' by A12,A58,A59,YELLOW_1:def 4;
      hence z in X' by A19,A26,WAYBEL_0:18;
    end;
    then X' = Intersect ZZ by A30,XBOOLE_0:def 10;
    hence X in FinMeetCl P by A27,A29,CANTOR_1:def 4;
  end;
  FinMeetCl P c= R
  proof
    let X be set;
    assume
A75: X in FinMeetCl P;
    then reconsider X' = X as Subset of product IS;
    consider ZZ being Subset-Family of product IS such that
A76: ZZ c= P and
A77: ZZ is finite and
A78: X = Intersect ZZ by A75,CANTOR_1:def 4;
    deffunc F(set) = product ((Carrier IS)+*($1,{1}));
    consider F being Function such that
A79: dom F = I and
A80: for e being set st e in I holds F.e = F(e) from FUNCT_1:sch 3;
    P c= rng F
    proof
      let w be set;
      assume w in P;
      then consider e being Element of I such that
A81:  w = product ((Carrier IS)+*(e,{1}));
      w = F.e by A80,A81;
      hence w in rng F by A79,FUNCT_1:def 5;
    end;
    then ZZ c= rng F by A76,XBOOLE_1:1;
    then consider x' being set such that
A82: x' c= dom F and
A83: x' is finite and
A84: F.:x' = ZZ by A77,ORDERS_1:195;
    reconsider x' as Subset of I by A79,A82;
    set PP = {product ((Carrier IS)+*(i,{1}))
    where i is Element of I: i in x'};
A85: ZZ c= PP
    proof
      let w be set;
      assume w in ZZ;
      then consider e being set such that
A86:  e in dom F and
A87:  e in x' and
A88:  w = F.e by A84,FUNCT_1:def 12;
      reconsider e as Element of I by A79,A86;
      w = product ((Carrier IS)+*(e,{1})) by A80,A88;
      hence w in PP by A87;
    end;
    PP c= ZZ
    proof
      let w be set;
      assume w in PP;
      then consider e being Element of I such that
A89:  w = product ((Carrier IS)+*(e,{1})) and
A90:  e in x';
      w = F.e by A80,A89;
      hence w in ZZ by A79,A84,A90,FUNCT_1:def 12;
    end;
    then
A91: ZZ = PP by A85,XBOOLE_0:def 10;
    reconsider x = x' as Element of BoolePoset I by WAYBEL_8:28;
A92: x is compact by A83,WAYBEL_8:30;
    reconsider y = f.x as Element of product IB;
A93: y is compact by A14,A92,WAYBEL13:28;
    reconsider Y = y as Element of T' by A1;
A94: uparrow Y = uparrow {Y} by WAYBEL_0:def 18
      .= uparrow {y} by A1,WAYBEL_0:13
      .= uparrow y by WAYBEL_0:def 18;
A95: X' c= uparrow y
    proof
      let z be set;
      assume
A96:  z in X';
      then
A97:  z in the carrier of product IB by A1,A11;
      reconsider z' = z as Element of product IB by A1,A11,A96;
      set h1 = chi(x,I);
A98:  h1 = y by A15;
      z in product Carrier IB by A97,YELLOW_1:def 4;
      then consider h2 being Function such that
A99:  z = h2 and dom h2 = dom (Carrier IB) and
A100: for w being set st w in dom (Carrier IB) holds
      h2.w in (Carrier IB).w by CARD_3:def 5;
      for i be set st i in I ex R being RelStr, xi,yi being Element of R
      st R = IB.i & xi = h1.i & yi = h2.i & xi <= yi
      proof
        let i be set;
        assume
A101:   i in I;
        then reconsider i' = i as Element of I;
        take RS = BoolePoset 1;
        consider RB being 1-sorted such that
A102:   RB = IB.i and
A103:   (Carrier IB).i = the carrier of RB by A101,PRALG_1:def 13;
A104:   (Carrier IB).i = {0,1} by A2,A101,A102,A103,FUNCOP_1:13;
A105:   i in dom (Carrier IB) by A101,PBOOLE:def 3;
        then
A106:   h2.i in (Carrier IB).i by A100;
        per cases;
        suppose
A107:     i in x;
          then product ((Carrier IS)+*(i',{1})) in ZZ by A91;
          then z in product ((Carrier IS)+*(i',{1})) by A78,A96,SETFAM_1:58;
          then consider h2' being Function such that
A108:     z = h2' and dom h2' = dom ((Carrier IS)+*(i',{1})) and
A109:     for w being set st w in dom ((Carrier IS)+*(i',{1})) holds
          h2'.w in ((Carrier IS)+*(i',{1})).w by CARD_3:def 5;
          i' in dom ((Carrier IS)+*(i',{1})) by A3,A105,FUNCT_7:32;
          then
A110:     h2'.i' in ((Carrier IS)+*(i',{1})).i' by A109;
A111:     ((Carrier IS)+*(i',{1})).i' = {1} by A3,A105,FUNCT_7:33;
          reconsider xi = 1, yi = 1 as Element of RS by A2,TARSKI:def 2;
          take xi,yi;
          thus RS = IB.i by A101,FUNCOP_1:13;
          thus xi = h1.i by A107,FUNCT_3:def 3;
          thus yi = h2.i by A99,A108,A110,A111,TARSKI:def 1;
          thus xi <= yi;
        end;
        suppose
A112:     not i in x;
          thus thesis
          proof
            per cases by A104,A106,TARSKI:def 2;
            suppose
A113:         h2.i = 0;
              reconsider xi = 0, yi = 0 as Element of RS by A2,TARSKI:def 2;
              take xi,yi;
              thus RS = IB.i by A101,FUNCOP_1:13;
              thus xi = h1.i by A101,A112,FUNCT_3:def 3;
              thus yi = h2.i by A113;
              thus xi <= yi;
            end;
            suppose
A114:         h2.i = 1;
              reconsider xi = 0, yi = 1 as Element of RS by A2,TARSKI:def 2;
              take xi,yi;
              thus RS = IB.i by A101,FUNCOP_1:13;
              thus xi = h1.i by A101,A112,FUNCT_3:def 3;
              thus yi = h2.i by A114;
              xi c= yi by XBOOLE_1:2;
              hence xi <= yi by YELLOW_1:2;
            end;
          end;
        end;
      end;
      then y <= z' by A12,A98,A99,YELLOW_1:def 4;
      hence z in uparrow y by WAYBEL_0:18;
    end;
    uparrow y c= X'
    proof
      let z be set;
      assume
A115: z in uparrow y;
      then
A116: z in the carrier of product IB;
      reconsider z' = z as Element of product IB by A115;
      z in product Carrier IB by A116,YELLOW_1:def 4;
      then consider gg being Function such that
A117: z = gg and
A118: dom gg = dom (Carrier IB) and
A119: for w being set st w in dom (Carrier IB)
      holds gg.w in (Carrier IB).w by CARD_3:def 5;
      y <= z' by A115,WAYBEL_0:18;
      then consider h1,h2 being Function such that
A120: h1 = y and
A121: h2 = z' and
A122: for i be set st i in I ex R being RelStr, xi,yi being Element of R
      st R = IB.i & xi = h1.i & yi = h2.i & xi <= yi by A12,YELLOW_1:def 4;
A123: h1 = chi(x,I) by A15,A120;
      for Z being set st Z in ZZ holds z in Z
      proof
        let Z be set;
        assume Z in ZZ;
        then consider j being Element of I such that
A124:   Z = product ((Carrier IS)+*(j,{1})) and
A125:   j in x by A91;
A126:   dom ((Carrier IS)+*(j,{1})) = dom (Carrier IS) by FUNCT_7:32
          .= I by PBOOLE:def 3;
        consider RS being RelStr, xj,yj being Element of RS such that
A127:   RS = IB.j and
A128:   xj = h1.j and
A129:   yj = h2.j and
A130:   xj <= yj by A122;
A131:   xj = 1 by A123,A125,A128,FUNCT_3:def 3;
A132:   RS = BoolePoset 1 by A127,FUNCOP_1:13;
        reconsider b = yj as Element of BoolePoset 1 by A127,FUNCOP_1:13;
A133:   b in the carrier of BoolePoset 1;
A134:   yj <> 0
        proof
          assume yj = 0;
          then
A135:     1 c= 0 by A130,A131,A132,YELLOW_1:2;
          thus thesis by A135;
        end;
A136:   dom h2 = dom ((Carrier IS)+*(j,{1})) by A117,A118,A121,A126,
PBOOLE:def 3;
        for w being set st w in dom ((Carrier IS)+*(j,{1}))
        holds h2.w in ((Carrier IS)+*(j,{1})).w
        proof
          let w be set;
          assume w in dom ((Carrier IS)+*(j,{1}));
          then
A137:     w in dom Carrier IS by A126,PBOOLE:def 3;
          per cases;
          suppose
A138:       w = j;
            then
A139:       ((Carrier IS)+*(j,{1})).w = {1} by A137,FUNCT_7:33;
            h2.w = 1 by A2,A129,A133,A134,A138,TARSKI:def 2;
            hence h2.w in ((Carrier IS)+*(j,{1})).w by A139,TARSKI:def 1;
          end;
          suppose w <> j;
            then ((Carrier IS)+*(j,{1})).w =(Carrier IS).w by FUNCT_7:34;
            hence h2.w in ((Carrier IS)+*(j,{1})).w
            by A10,A117,A119,A121,A137;
          end;
        end;
        hence z in Z by A121,A124,A136,CARD_3:def 5;
      end;
      hence z in X' by A1,A11,A78,A115,SETFAM_1:58;
    end;
    then
A140: X = uparrow Y by A94,A95,XBOOLE_0:def 10;
    Y is compact by A1,A93,WAYBEL_8:9;
    then Y in the carrier of CompactSublatt T' by WAYBEL_8:def 1;
    hence X in R by A13,A140;
  end;
  then
A141: R = FinMeetCl P by A17,XBOOLE_0:def 10;
  reconsider P' = P as Subset-Family of T by A11;
  P' is prebasis of T by A11,A141,YELLOW_9:23;
  hence thesis by A11,YELLOW_9:26;
end;

theorem Th17:
  for T,S being non empty TopSpace st the carrier of T = the carrier of S &
  the topology of T = the topology of S & T is injective holds S is injective
proof
  let T,S be non empty TopSpace;
  assume that
A1: the carrier of T = the carrier of S and
A2: the topology of T = the topology of S and
A3: T is injective;
A4: [#]S <> {} & [#]T <> {};
  let X be non empty TopSpace;
  let f be Function of X,S;
  assume
A5: f is continuous;
  reconsider f' = f as Function of X,T by A1;
  for P being Subset of T st P is open holds (f')"P is open
  proof
    let P be Subset of T;
    assume
A6: P is open;
    reconsider P' = P as Subset of S by A1;
    P' in the topology of S by A2,A6,PRE_TOPC:def 5;
    then P' is open by PRE_TOPC:def 5;
    hence f'"P is open by A4,A5,TOPS_2:55;
  end;
  then
A7: f' is continuous by A4,TOPS_2:55;
  let Y be non empty TopSpace;
  assume X is SubSpace of Y;
  then consider h being Function of Y,T such that
A8: h is continuous and
A9: h|(the carrier of X) = f' by A3,A7,Def5;
  reconsider g = h as Function of Y,S by A1;
  take g;
  for P being Subset of S st P is open holds g"P is open
  proof
    let P be Subset of S;
    assume
A10: P is open;
    reconsider P' = P as Subset of T by A1;
    P' in the topology of T by A2,A10,PRE_TOPC:def 5;
    then P' is open by PRE_TOPC:def 5;
    hence g"P is open by A4,A8,TOPS_2:55;
  end;
  hence g is continuous by A4,TOPS_2:55;
  thus g|(the carrier of X) = f by A9;
end;

theorem       ::p.122 lemma 3.4.(i) part II
  for I being non empty set
  for T being Scott TopAugmentation of product (I --> BoolePoset 1)
  holds T is injective
proof
  let I be non empty set,
  T be Scott TopAugmentation of product (I --> BoolePoset 1);
A1: the RelStr of T = product (I --> BoolePoset 1) by YELLOW_9:def 4;
A2: the topology of T = the topology of product (I --> Sierpinski_Space) by
Th16;
  set IB = I --> BoolePoset 1, IS = I --> Sierpinski_Space;
  LattPOSet BooleLatt 1 = RelStr(#the carrier of BooleLatt 1,
    LattRel(BooleLatt 1)#) by LATTICE3:def 2;
  then
A3: the carrier of BoolePoset 1 = the carrier of BooleLatt 1 by YELLOW_1:def 2
    .= bool {{}} by CARD_5:1,LATTICE3:def 1
    .= {0,1} by CARD_5:1,ZFMISC_1:30;
A4: dom Carrier IB = I by PBOOLE:def 3
    .= dom Carrier IS by PBOOLE:def 3;
A5: for i being set st i in
  dom Carrier IB holds (Carrier IB).i = (Carrier IS).i
  proof
    let i be set;
    assume i in dom Carrier IB;
    then
A6: i in I by PBOOLE:def 3;
    then consider R1 being 1-sorted such that
A7: R1 = IB.i and
A8: (Carrier IB).i = the carrier of R1 by PRALG_1:def 13;
    consider R2 being 1-sorted such that
A9: R2 = IS.i and
A10: (Carrier IS).i = the carrier of R2 by A6,PRALG_1:def 13;
    the carrier of R1 = {0,1} by A3,A6,A7,FUNCOP_1:13
      .= the carrier of Sierpinski_Space by Def9
      .= the carrier of R2 by A6,A9,FUNCOP_1:13;
    hence (Carrier IB).i = (Carrier IS).i by A8,A10;
  end;
A11: the carrier of T = product Carrier (I --> BoolePoset 1) by A1,
YELLOW_1:def 4
    .= product Carrier (I --> Sierpinski_Space) by A4,A5,FUNCT_1:9
    .= the carrier of product (I --> Sierpinski_Space) by Def3;
  for i being Element of I holds (I --> Sierpinski_Space).i is injective
  by FUNCOP_1:13;
  then product (I --> Sierpinski_Space) is injective by Th8;
  hence T is injective by A2,A11,Th17;
end;

theorem Th19:  ::p.122 lemma 3.4.(ii)
  for T being T_0-TopSpace ex M being non empty set,
  f being Function of T, product (M --> Sierpinski_Space) st
  corestr(f) is_homeomorphism
proof
  let T be T_0-TopSpace;
A1: [#]T <> {};
  take M = the carrier of (InclPoset the topology of T);
  set J = M --> Sierpinski_Space;
  deffunc F(set) = chi({u where u is Subset of T: $1 in u & u is open},
  the topology of T);
  consider f being Function such that
A2: dom f = the carrier of T and
A3: for
  x being set st x in the carrier of T holds f.x = F(x) from FUNCT_1:sch 3;
  rng f c= the carrier of product J
  proof
    let y be set;
    assume y in rng f;
    then consider x being set such that
A4: x in dom f and
A5: y = f.x by FUNCT_1:def 5;
    set ch = chi({u where u is Subset of T: x in u & u is open},
    the topology of T);
A6: y = ch by A2,A3,A4,A5;
A7: dom ch = the topology of T by FUNCT_3:def 3
      .= M by YELLOW_1:1
      .= dom (Carrier J) by PBOOLE:def 3;
    for z being set st z in dom (Carrier J) holds ch.z in (Carrier J) . z
    proof
      let z be set;
      assume z in dom (Carrier J);
      then
A8:   z in M by PBOOLE:def 3;
      then
A9:   z in the topology of T by YELLOW_1:1;
      consider R being 1-sorted such that
A10:  R = J.z and
A11:  (Carrier J).z = the carrier of R by A8,PRALG_1:def 13;
A12:  (Carrier J).z = the carrier of Sierpinski_Space by A8,A10,A11,FUNCOP_1:13
        .= {0,1} by Def9;
      z in dom ch by A9,FUNCT_3:def 3;
      then ch.z in rng ch by FUNCT_1:def 5;
      hence ch.z in (Carrier J).z by A12;
    end;
    then y in product Carrier J by A6,A7,CARD_3:def 5;
    hence y in the carrier of product J by Def3;
  end;
  then reconsider f as Function
  of T, product J by A2,FUNCT_2:def 1,RELSET_1:11;
  reconsider PP = {product((Carrier J)+*(m,{1})) where m is Element of M:
  not contradiction } as prebasis of product J by Th14;
  for A being Subset of product J st A in PP holds f"A is open
  proof
    let A be Subset of product J;
    assume
A13: A in PP;
    reconsider a = A as Subset of product Carrier J by Def3;
    consider i being Element of M such that
A14: a = product ((Carrier J) +* (i,{1})) by A13;
A15: i in M;
    then
A16: i in the topology of T by YELLOW_1:1;
    then reconsider i' = i as Subset of T;
A17: i in dom (Carrier J) by A15,PBOOLE:def 3;
    {1} c= {0,1} by ZFMISC_1:12;
    then reconsider V = {1} as Subset of Sierpinski_Space by Def9;
A18: ((Carrier J)+*(i,V)).i = {1} by A17,FUNCT_7:33;
A19: f"A c= i
    proof
      let z be set;
      assume
A20:  z in f"A;
      then
A21:  f.z in a by FUNCT_1:def 13;
      set W = {u where u is Subset of T: z in u & u is open};
      f.z = chi(W,the topology of T) by A3,A20;
      then consider g being Function such that
A22:  chi(W,the topology of T) = g and dom g = dom ((Carrier J) +* (i,V)) and
A23:  for w being set st w in dom ((Carrier J) +* (i,V)) holds
      g.w in ((Carrier J) +* (i,V)).w by A14,A21,CARD_3:def 5;
      i in dom ((Carrier J) +* (i,V)) by A17,FUNCT_7:32;
      then g.i in ((Carrier J) +* (i,V)).i by A23;
      then chi(W,the topology of T).i = 1 by A18,A22,TARSKI:def 1;
      then i in W by FUNCT_3:42;
      then ex uu being Subset of T st i = uu & z in uu & uu is open;
      hence z in i;
    end;
    i c= f"A
    proof
      let z be set;
      assume
A24:  z in i;
      set W = {u where u is Subset of T: z in u & u is open};
      i' is open by A16,PRE_TOPC:def 5;
      then
A25:  i in W by A24;
A26:  z in i' by A24;
      then
A27:  f.z = chi(W,the topology of T) by A3;
A28:  dom chi(W,the topology of T) = the topology of T by FUNCT_3:def 3
        .= M by YELLOW_1:1
        .= dom (Carrier J) by PBOOLE:def 3
        .= dom ((Carrier J) +* (i,V)) by FUNCT_7:32;
      for w being set st w in dom ((Carrier J) +* (i,V)) holds
      chi(W,the topology of T).w in ((Carrier J) +* (i,V)).w
      proof
        let w be set;
        assume w in dom ((Carrier J) +* (i,V));
        then w in dom (Carrier J) by FUNCT_7:32;
        then
A29:    w in M by PBOOLE:def 3;
        then w in the topology of T by YELLOW_1:1;
        then
A30:    w in dom chi(W,the topology of T) by FUNCT_3:def 3;
        per cases;
        suppose
A31:      w = i;
          then
A32:      ((Carrier J) +* (i,V)).w = {1} by A17,FUNCT_7:33;
          chi(W,the topology of T).w = 1 by A16,A25,A31,FUNCT_3:def 3;
          hence chi(W,the topology of T).w in ((Carrier J) +* (i,V)).w
          by A32,TARSKI:def 1;
        end;
        suppose w <> i;
          then
A33:      ((Carrier J) +* (i,V)).w = (Carrier J).w by FUNCT_7:34;
          consider r being 1-sorted such that
A34:      r = J.w and
A35:      (Carrier J).w = the carrier of r by A29,PRALG_1:def 13;
A36:      (Carrier J).w = the carrier of Sierpinski_Space by A29,A34,A35,
FUNCOP_1:13
            .= {0,1} by Def9;
          chi(W,the topology of T).w in rng chi(W,the topology of T )
          by A30,FUNCT_1:def 5;
          hence chi(W,the topology of T).w in ((Carrier J) +* (i,V)).w
          by A33,A36;
        end;
      end;
      then f.z in A by A14,A27,A28,CARD_3:def 5;
      hence z in f"A by A2,A26,FUNCT_1:def 13;
    end;
    then f"A = i by A19,XBOOLE_0:def 10;
    hence f"A is open by A16,PRE_TOPC:def 5;
  end;
  then
A37: f is continuous by YELLOW_9:36;
  take f;
A38: dom (corestr f) = [#]T by FUNCT_2:def 1;
A39: rng (corestr f) = [#](Image f) by FUNCT_2:def 3;
A40: corestr f is one-to-one
  proof
    let x,y be Element of T;
    assume
A41: (corestr f).x = (corestr f).y;
    set U1 = {u where u is Subset of T: x in u & u is open};
    set U2 = {u where u is Subset of T: y in u & u is open};
    thus x = y
    proof
      assume not thesis;
      then consider V being Subset of T such that
A42:  V is open and
A43:  ((x in V & not y in V) or (y in V & not x in V)) by T_0TOPSP:def 7;
A44:  f.x = chi(U1,the topology of T) by A3;
A45:  f.y = chi(U2,the topology of T) by A3;
A46:  V in the topology of T by A42,PRE_TOPC:def 5;
      per cases by A43;
      suppose
A47:    x in V & not y in V;
        reconsider v = V as Subset of T;
A48:    not v in U2
        proof
          assume not thesis;
          then ex u being Subset of T st u = v & y in u & u is open;
          hence thesis by A47;
        end;
        v in U1 by A42,A47;
        then chi(U1,the topology of T).v = 1 by A46,FUNCT_3:def 3;
        hence thesis by A41,A44,A45,A46,A48,FUNCT_3:def 3;
      end;
      suppose
A49:    y in V & not x in V;
        reconsider v = V as Subset of T;
A50:    not v in U1
        proof
          assume not thesis;
          then ex u being Subset of T st u = v & x in u & u is open;
          hence thesis by A49;
        end;
        v in U2 by A42,A49;
        then chi(U2,the topology of T).v = 1 by A46,FUNCT_3:def 3;
        hence thesis by A41,A44,A45,A46,A50,FUNCT_3:def 3;
      end;
    end;
  end;
A51: corestr f is continuous by A37,Th11;
A52: for V being Subset of T st V is open holds
  f.:V = product ((Carrier J) +* (V,{1})) /\ the carrier of Image f
  proof
    let V be Subset of T;
    assume
A53: V is open;
    hereby
      let y be set;
      assume y in f.:V;
      then consider x being set such that
A54:  x in dom f and
A55:  x in V and
A56:  y = f.x by FUNCT_1:def 12;
      set Q = {u where u is Subset of T: x in u & u is open};
A57:  V in Q by A53,A55;
A58:  y = chi(Q,the topology of T) by A3,A54,A56;
A59:  dom chi(Q,the topology of T) = the topology of T by FUNCT_3:def 3
        .= M by YELLOW_1:1
        .= dom Carrier J by PBOOLE:def 3
        .= dom ((Carrier J) +* (V,{1})) by FUNCT_7:32;
      for W being set st W in dom ((Carrier J) +* (V,{1})) holds
      chi(Q,the topology of T).W in ((Carrier J) +* (V,{1})).W
      proof
        let W be set;
        assume W in dom ((Carrier J) +* (V,{1}));
        then
A60:    W in dom (Carrier J) by FUNCT_7:32;
        then
A61:    W in M by PBOOLE:def 3;
        then
A62:    W in the topology of T by YELLOW_1:1;
        then
A63:    W in dom chi(Q,the topology of T) by FUNCT_3:def 3;
        per cases;
        suppose
A64:      W = V;
          then
A65:      ((Carrier J) +* (V,{1})).W = {1} by A60,FUNCT_7:33;
          chi(Q,the topology of T).W = 1 by A57,A62,A64,FUNCT_3:def 3;
          hence chi(Q,the topology of T).W in ((Carrier J) +* (V,{1})).W
          by A65,TARSKI:def 1;
        end;
        suppose W <> V;
          then
A66:      ((Carrier J) +* (V,{1})).W = (Carrier J).W by FUNCT_7:34;
          consider r being 1-sorted such that
A67:      r = J.W and
A68:      (Carrier J).W = the carrier of r by A61,PRALG_1:def 13;
A69:      (Carrier J).W = the carrier of Sierpinski_Space by A61,A67,A68,
FUNCOP_1:13
            .= {0,1} by Def9;
          chi(Q,the topology of T).W in rng chi(Q,the topology of T)
          by A63,FUNCT_1:def 5;
          hence chi(Q,the topology of T).W in ((Carrier J) +* (V,{1})).W
          by A66,A69;
        end;
      end;
      then
A70:  y in product ((Carrier J) +* (V,{1})) by A58,A59,CARD_3:def 5;
      y in rng f by A54,A56,FUNCT_1:def 5;
      then y in the carrier of Image f by Th10;
      hence y in product ((Carrier J) +* (V,{1})) /\ the carrier of Image f
      by A70,XBOOLE_0:def 4;
    end;
    let y be set;
    assume
A71: y in product ((Carrier J) +* (V,{1})) /\ the carrier of Image f;
    rng f = the carrier of Image f by Th10;
    then y in rng f by A71,XBOOLE_0:def 4;
    then consider x being set such that
A72: x in dom f and
A73: y = f.x by FUNCT_1:def 5;
    set Q = {u where u is Subset of T: x in u & u is open};
A74: y = chi(Q,the topology of T) by A3,A72,A73;
    y in product ((Carrier J) +* (V,{1})) by A71,XBOOLE_0:def 4;
    then consider g being Function such that
A75: y = g and dom g = dom ((Carrier J) +* (V,{1})) and
A76: for W being set st W in dom ((Carrier J) +* (V,{1}))
    holds g.W in ((Carrier J) +* (V,{1})).W by CARD_3:def 5;
    V in the topology of T by A53,PRE_TOPC:def 5;
    then V in M by YELLOW_1:1;
    then
A77: V in dom (Carrier J) by PBOOLE:def 3;
    then V in dom ((Carrier J) +* (V,{1})) by FUNCT_7:32;
    then g.V in ((Carrier J) +* (V,{1})).V by A76;
    then g.V in {1} by A77,FUNCT_7:33;
    then chi(Q,the topology of T).V = 1 by A74,A75,TARSKI:def 1;
    then V in Q by FUNCT_3:42;
    then ex u being Subset of T st u = V & x in u & u is open;
    hence y in f.:V by A72,A73,FUNCT_1:def 12;
  end;
  for V being Subset of T st V is open holds ((corestr f)")"V is open
  proof
    let V be Subset of T;
    assume
A78: V is open;
    then V in the topology of T by PRE_TOPC:def 5;
    then reconsider W = V as Element of M by YELLOW_1:1;
A79: product((Carrier J)+*(W,{1})) in PP;
    then reconsider Q = product((Carrier J)+*(V,{1})) as Subset of product J;
A80: (corestr f).:V = Q /\ [#](Image f) by A52,A78;
    PP c= the topology of product J by CANTOR_1:def 5;
    then (corestr f).: V in the topology of Image f by A79,A80,PRE_TOPC:def 9;
    then (corestr f).:V is open by PRE_TOPC:def 5;
    hence ((corestr f)")"V is open by A39,A40,TOPS_2:67;
  end;
  then (corestr f)" is continuous by A1,TOPS_2:55;
  hence corestr f is_homeomorphism by A38,A39,A40,A51,TOPS_2:def 5;
end;

theorem       ::p.122 lemma 3.4.(iii)
  for T being T_0-TopSpace st T is injective ex M being non empty set st
  T is_Retract_of product (M --> Sierpinski_Space)
proof
  let T be T_0-TopSpace;
  assume
A1: T is injective;
  consider M being non empty set,
  f being Function of T, product (M --> Sierpinski_Space) such that
A2: corestr(f) is_homeomorphism by Th19;
  thus thesis by A1,A2,Th12;
end;

