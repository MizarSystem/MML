:: Scalar Multiple of Riemann Definite Integral
::  by Noboru Endou , Katsumi Wasaki and Yasunari Shidama
::
:: Received December 7, 1999
:: Copyright (c) 1999 Association of Mizar Users
 
environ

 vocabularies NUMBERS, REAL_1, SUBSET_1, FINSEQ_1, INTEGRA1, XREAL_0, ORDINAL1,
      SEQ_4, XXREAL_0, XXREAL_1, VALUED_0, RELAT_1, ARYTM_3, FUNCT_1, CARD_1,
      NAT_1, CLASSES1, FINSEQ_5, ARYTM_1, ORDINAL4, XBOOLE_0, JORDAN3,
      MEMBERED, MEMBER_1, TARSKI, PARTFUN1, XXREAL_2, VALUED_1, MEASURE7,
      CARD_3, SEQ_1, INTEGRA2;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, CARD_1, NUMBERS, XXREAL_0,
      XREAL_0, XXREAL_2, XXREAL_3, MEMBERED, MEMBER_1, REAL_1, NAT_1, RELAT_1,
      FUNCT_1, RELSET_1, PARTFUN1, FUNCT_2, FINSEQ_1, RFUNCT_1, RVSUM_1,
      INTEGRA1, VALUED_1, SEQ_1, SEQ_2, SEQ_4, FINSEQ_6, RCOMP_1, FINSEQ_5,
      CLASSES1, RFINSEQ, MEASURE6;
 constructors PARTFUN1, REAL_1, NAT_1, SEQM_3, RFUNCT_1, RFINSEQ, BINARITH,
      FINSEQ_5, FINSEQ_6, INTEGRA1, SEQ_1, XXREAL_2, NAT_D, RVSUM_1, SEQ_4,
      CLASSES1, RELSET_1, SEQ_2, MEMBER_1, MEASURE6;
 registrations RELAT_1, ORDINAL1, FUNCT_2, FINSET_1, NUMBERS, XXREAL_0,
      XREAL_0, MEMBERED, FINSEQ_1, RFINSEQ, INTEGRA1, VALUED_0, VALUED_1,
      XXREAL_2, CARD_1, SEQ_2, RELSET_1, XXREAL_3, MEMBER_1;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, XBOOLE_0, VALUED_1, RELAT_1, MEMBER_1, XXREAL_2, XXREAL_3;
 theorems SEQ_4, SUBSET_1, PARTFUN1, INTEGRA1, RFUNCT_1, FUNCT_1, FINSEQ_1,
      RVSUM_1, NEWTON, RCOMP_1, NAT_1, RFINSEQ, CARD_1, FINSEQ_5, FINSEQ_3,
      RELAT_1, FUNCT_2, XREAL_0, XBOOLE_0, XBOOLE_1, XCMPLX_1, XREAL_1,
      XXREAL_0, ORDINAL1, MEMBERED, VALUED_1, XXREAL_2, TARSKI, SEQM_3,
      CLASSES1, FINSEQ_6, MEMBER_1, XXREAL_3;
 schemes SUBSET_1, SEQ_1, NAT_1;

begin :: Lemmas of Finite Sequence

reserve a,b,r,x,y for Real,
  i,j,k,n for Element of NAT,
  x1 for set,
  p for FinSequence of REAL;

theorem
  for A be closed-interval Subset of REAL, x being real number holds
  x in A iff lower_bound A <= x & x <= upper_bound A
proof
  let A be closed-interval Subset of REAL;
  let x be real number;
  hereby
    assume x in A;
    then x in [.lower_bound A,upper_bound A.] by INTEGRA1:5;
    then x in {a: lower_bound A <= a & a <= upper_bound A} by RCOMP_1:def 1;
    then ex a st a=x & lower_bound A <= a & a <= upper_bound A;
    hence lower_bound A <= x & x <= upper_bound A;
  end;
  assume
A1: lower_bound A <= x & x <= upper_bound A;
  x is Real by XREAL_0:def 1;
  then x in {a: lower_bound A <= a & a <= upper_bound A} by A1;
  then x in [.lower_bound A,upper_bound A.] by RCOMP_1:def 1;
  hence thesis by INTEGRA1:5;
end;

definition
  let IT be FinSequence of REAL;
  attr IT is non-decreasing means
  :Def1:
  for n be Element of NAT st n in dom
  IT & n+1 in dom IT holds IT.n <= IT.(n+1);
end;

registration
  cluster non-decreasing FinSequence of REAL;
  existence
  proof
    take f = <*>REAL;
    let n;
    assume that
A1: n in dom f and
    n+1 in dom f;
    thus thesis by A1;
  end;
end;

theorem
  for p be non-decreasing FinSequence of REAL, i,j st i in dom p & j in
  dom p & i <= j holds p.i <= p.j
proof
  let p be non-decreasing FinSequence of REAL;
  let i,j;
  assume
A1: i in dom p;
  defpred P[Element of NAT] means for i,j st j = i + $1 & i in dom p & j in
  dom p holds p.i <= p.j;
  assume
A2: j in dom p;
A3: for k st P[k] holds P[k+1]
  proof
    let k;
    assume
A4: P[k];
    P[k+1]
    proof
      let i,j;
      reconsider l=i+k as Element of NAT;
A5:   0 <= k by NAT_1:2;
      assume j=i+(k+1);
      then
A6:   j=l+1;
      assume
A7:   i in dom p;
      then 1 <= i by FINSEQ_3:27;
      then
A8:   1+0 <= l by A5,XREAL_1:9;
      assume
A9:   j in dom p;
      then j <= len p by FINSEQ_3:27;
      then l < len p by A6,NAT_1:13;
      then
A10:  l in dom p by A8,FINSEQ_3:27;
      then
A11:  p.i <= p.l by A4,A7;
      p.l <= p.j by A9,A6,A10,Def1;
      hence thesis by A11,XXREAL_0:2;
    end;
    hence thesis;
  end;
A12: P[0];
A13: for k holds P[k] from NAT_1:sch 1(A12,A3);
  assume i <= j;
  then consider n being Nat such that
A14: j = i + n by NAT_1:10;
  reconsider n as Element of NAT by ORDINAL1:def 13;
  j = i + n by A14;
  hence thesis by A1,A2,A13;
end;

theorem
  for p ex q be non-decreasing FinSequence of REAL st p,q
  are_fiberwise_equipotent
proof
  let p;
  consider q being non-increasing FinSequence of REAL such that
A1: p,q are_fiberwise_equipotent by RFINSEQ:35;
  for n be Element of NAT st n in dom Rev q & n+1 in dom Rev q holds (Rev
  q).n <= (Rev q).(n+1)
  proof
    let n;
    assume that
A2: n in dom Rev q and
A3: n+1 in dom Rev q;
    (Rev q).n <= (Rev q).(n+1)
    proof
      n in Seg len Rev q by A2,FINSEQ_1:def 3;
      then 1 <= n by FINSEQ_1:3;
      then n - 1 >= 0 by XREAL_1:50;
      then len q + 0 <= len q + (n - 1) by XREAL_1:9;
      then
A4:   len q - (n - 1) <= len q by XREAL_1:22;
      n in Seg len Rev q by A2,FINSEQ_1:def 3;
      then n in Seg len q by FINSEQ_5:def 3;
      then n <= len q by FINSEQ_1:3;
      then consider m being Nat such that
A5:   len q = n + m by NAT_1:10;
      reconsider m as Element of NAT by ORDINAL1:def 13;
      m + 1 = len q - n + 1 & 1 <= len q - n + 1 by A5,NAT_1:11;
      then len q - n + 1 in Seg len q by A4,FINSEQ_1:3;
      then
A6:   len q - n + 1 in dom q by FINSEQ_1:def 3;
      set x=(Rev q).n, y=(Rev q).(n+1);
A7:   len q - (n+1) + 1 = len q-n;
      len q <= len q + n by NAT_1:11;
      then
A8:   len q - (n+1) + 1 <= len q by A7,XREAL_1:22;
      n+1 in Seg len Rev q by A3,FINSEQ_1:def 3;
      then n+1 in Seg len q by FINSEQ_5:def 3;
      then n+1 <= len q by FINSEQ_1:3;
      then 1 <= len q - (n+1) + 1 by A7,XREAL_1:21;
      then len q - (n+1) + 1 in Seg len q by A5,A8,FINSEQ_1:3;
      then
A9:   len q - (n+1) + 1 in dom q by FINSEQ_1:def 3;
      x = q.(len q - n + 1) & y = q.(len q - (n+1) + 1) by A2,A3,FINSEQ_5:def 3
;
      hence thesis by A6,A9,RFINSEQ:def 4;
    end;
    hence thesis;
  end;
  then reconsider r=Rev q as non-decreasing FinSequence of REAL by Def1;
  take r;
  p,Rev q are_fiberwise_equipotent
  proof
    defpred P[Element of NAT] means for p be FinSequence of REAL,q be
non-increasing FinSequence of REAL st len p = $1 & p,q are_fiberwise_equipotent
    holds p,Rev q are_fiberwise_equipotent;
A10: for k st P[k] holds P[k+1]
    proof
      let k;
      assume
A11:  P[k];
      P[k+1]
      proof
        let p be FinSequence of REAL;
        let q be non-increasing FinSequence of REAL;
        consider q1 being non-increasing FinSequence of REAL such that
A12:    p,q1 are_fiberwise_equipotent by RFINSEQ:35;
        reconsider q1k = q1|k as non-increasing FinSequence of REAL by
RFINSEQ:33;
A13:    Rev(q1k)^<*q1.(k+1)*>,<*q1.(k+1)*>^Rev(q1k)
        are_fiberwise_equipotent by RFINSEQ:15;
        assume
A14:    len p = k+1;
        then
A15:    len q1= k+1 by A12,RFINSEQ:16;
        len p = len q1 by A12,RFINSEQ:16;
        then len(q1|k) = k by A14,FINSEQ_1:80,NAT_1:11;
        then q1|k,Rev(q1k) are_fiberwise_equipotent by A11;
        then (q1|k)^<*q1.(k+1)*>,Rev(q1k)^<*q1.(k+1)*>
        are_fiberwise_equipotent by RFINSEQ:14;
        then q1,Rev(q1k)^<*q1.(k+1)*> are_fiberwise_equipotent by A15,
RFINSEQ:20;
        then
A16:    q1,<*q1.(k+1)*>^Rev(q1k) are_fiberwise_equipotent by A13,CLASSES1:84;
A17:    <*q1.(k+1)*>^Rev(q1k) = Rev((q1|k)^<*q1.(k+1)*>) by FINSEQ_5:66
          .= Rev(q1) by A15,RFINSEQ:20;
        assume p,q are_fiberwise_equipotent;
        then q=q1 by A12,CLASSES1:84,RFINSEQ:36;
        hence thesis by A12,A16,A17,CLASSES1:84;
      end;
      hence thesis;
    end;
A18: len p = len p;
A19: P[0]
    proof
      let p be FinSequence of REAL;
      let q be non-increasing FinSequence of REAL;
      assume
A20:  len p = 0;
      assume p,q are_fiberwise_equipotent;
      then len q = 0 by A20,RFINSEQ:16;
      then len Rev q = 0 by FINSEQ_5:def 3;
      then Rev q = {};
      then
A21:  rng Rev q = {};
      p = {} by A20;
      then
A22:  rng p = {};
      for x be set holds card Coim(p,x) = card Coim(Rev q,x)
      proof
        let x be set;
        card (p"{x}) = 0 by A22,CARD_1:47,FUNCT_1:142;
        hence thesis by A21,CARD_1:47,FUNCT_1:142;
      end;
      hence thesis by CLASSES1:def 9;
    end;
    for k holds P[k] from NAT_1:sch 1(A19,A10);
    hence thesis by A1,A18;
  end;
  hence thesis;
end;

theorem
  for D be non empty set, f be FinSequence of D, k1,k2,k3 be Element of
NAT st 1<=k1 & k3<=len f & k1<=k2 & k2<k3 holds mid(f,k1,k2)^mid(f,k2+1,k3)=mid
  (f,k1,k3)
proof
  let D be non empty set;
  let f be FinSequence of D;
  let k1,k2,k3 be Element of NAT;
  assume that
A1: 1<=k1 and
A2: k3<=len f and
A3: k1<=k2 and
A4: k2<k3;
A5: k2 <= len f by A2,A4,XXREAL_0:2;
  then
A6: k1<=len f by A3,XXREAL_0:2;
  1<=k2 & k2<=len f by A1,A2,A3,A4,XXREAL_0:2;
  then
A7: len mid(f,k1,k2)=k2-'k1+1 by A1,A3,A6,FINSEQ_6:124
    .=k2-k1+1 by A3,XREAL_1:235;
A8: 1 <= k2 by A1,A3,XXREAL_0:2;
  then
A9: 1<=k3 by A4,XXREAL_0:2;
A10: k1<k3 by A3,A4,XXREAL_0:2;
A11: 1<=k2+1 by A8,NAT_1:13;
A12: k2+1<=k3 by A4,NAT_1:13;
  then k2+1<=len f by A2,XXREAL_0:2;
  then
A13: len mid(f,k2+1,k3)=k3-'(k2+1)+1 by A2,A9,A12,A11,FINSEQ_6:124
    .=k3-(k2+1)+1 by A12,XREAL_1:235
    .=k3-k2;
  then
A14: len(mid(f,k1,k2)^mid(f,k2+1,k3))=(k2-k1+1)+(k3-k2) by A7,FINSEQ_1:35
    .=k3-k1+1;
A15: 1<=k2+1 by A8,NAT_1:13;
A16: for k be Nat st 1<=k & k<=len(mid(f,k1,k2)^mid(f,k2+1,k3)) holds (mid(f
  ,k1,k2)^mid(f,k2+1,k3)).k = mid(f,k1,k3).k
  proof
    let k be Nat;
    assume
A17: 1<=k & k<=len(mid(f,k1,k2)^mid(f,k2+1,k3));
    then k in Seg len(mid(f,k1,k2)^mid(f,k2+1,k3)) by FINSEQ_1:3;
    then
A18: k in dom(mid(f,k1,k2)^mid(f,k2+1,k3)) by FINSEQ_1:def 3;
    now
      per cases by A18,FINSEQ_1:38;
      suppose
A19:    k in dom mid(f,k1,k2);
        then
A20:    k in Seg len mid(f,k1,k2) by FINSEQ_1:def 3;
        then
A21:    1 <= k by FINSEQ_1:3;
A22:    k <= k2-k1+1 by A7,A20,FINSEQ_1:3;
        k2-k1 <= k3-k1 by A4,XREAL_1:11;
        then k2-k1+1 <= k3-k1+1 by XREAL_1:8;
        then
A23:    k <= k3-k1+1 by A22,XXREAL_0:2;
        (mid(f,k1,k2)^mid(f,k2+1,k3)).k=mid(f,k1,k2).k by A19,FINSEQ_1:def 7
          .=f.(k-1+k1) by A1,A3,A5,A19,A21,A22,FINSEQ_6:128;
        hence thesis by A1,A2,A10,A19,A21,A23,FINSEQ_6:128;
      end;
      suppose
        ex n be Nat st n in dom mid(f,k2+1,k3)&k=len mid(f,k1,k2)+n;
        then consider n be Nat such that
A24:    n in dom mid(f,k2+1,k3) and
A25:    k=len mid(f,k1,k2)+n;
A26:    mid(f,k1,k3).k=f.(k2-(k1-1)+n+k1-1) by A1,A2,A10,A7,A14,A17,A25,
FINSEQ_6:128
          .=f.(n+k2);
        n in Seg len mid(f,k2+1,k3) by A24,FINSEQ_1:def 3;
        then
A27:    1 <= n & n <= k3-(k2+1)+1 by A13,FINSEQ_1:3;
        (mid(f,k1,k2)^mid(f,k2+1,k3)).k=mid(f,k2+1,k3).n by A24,A25,
FINSEQ_1:def 7
          .=f.(n+(k2+1)-1) by A2,A15,A12,A24,A27,FINSEQ_6:128
          .=f.(n+k2+0);
        hence thesis by A26;
      end;
    end;
    hence thesis;
  end;
  len mid(f,k1,k3)=k3-'k1+1 by A1,A2,A6,A9,A10,FINSEQ_6:124
    .=k3-k1+1 by A3,A4,XREAL_1:235,XXREAL_0:2;
  hence thesis by A14,A16,FINSEQ_1:18;
end;

begin :: Scalar Product of Real Subset

definition
  let A be real-membered set;
  let x be real number;
  redefine func x ** A -> Subset of REAL;
  coherence
  proof
    thus thesis by MEMBERED:3;
  end;
  canceled;
end;

theorem
  for X,Y be non empty set, f be PartFunc of X,REAL st
    f|X is bounded_above & Y c= X holds f|Y|Y is bounded_above
proof
  let X,Y be non empty set;
  let f be PartFunc of X,REAL;
  assume f|X is bounded_above;
  then consider a be real number such that
A1: for x being set st x in X /\ dom f holds a>=f.x by RFUNCT_1:87;
  assume
A2: Y c= X;
  for x being set st x in Y /\ dom (f|Y) holds a>=(f|Y).x
  proof
    let x be set;
A3: dom f /\ Y c= dom f /\ X by A2,XBOOLE_1:26;
    assume x in Y /\ dom (f|Y);
    then
A4: x in dom (f|Y) by XBOOLE_0:def 4;
    then x in dom f /\ Y by RELAT_1:90;
    then a >= f.x by A1,A3;
    hence thesis by A4,FUNCT_1:70;
  end;
  hence thesis by RFUNCT_1:87;
end;

theorem
  for X,Y be non empty set, f be PartFunc of X,REAL st
    f|X is bounded_below & Y c= X holds f|Y|Y is bounded_below
proof
  let X,Y be non empty set;
  let f be PartFunc of X,REAL;
  assume f|X is bounded_below;
  then consider a be real number such that
A1: for x being set st x in X /\ dom f holds f.x>=a by RFUNCT_1:88;
  assume
A2: Y c= X;
  for x being set st x in Y /\ dom (f|Y) holds (f|Y).x>=a
  proof
    let x be set;
A3: dom f /\ Y c= dom f /\ X by A2,XBOOLE_1:26;
    assume x in Y /\ dom (f|Y);
    then
A4: x in dom (f|Y) by XBOOLE_0:def 4;
    then x in dom f /\ Y by RELAT_1:90;
    then a <= f.x by A1,A3;
    hence thesis by A4,FUNCT_1:70;
  end;
  hence thesis by RFUNCT_1:88;
end;

theorem
  for X being real-membered set, a being real number holds
    X is empty iff a ** X is empty
proof
  let X be real-membered set, a be real number;
  thus X is empty implies a ** X is empty
  proof
    assume that
A1: X is empty and
A2: a ** X is non empty;
    reconsider XX = X as empty set by A1;
    a ** XX is empty;
    hence contradiction by A2;
  end;
  assume that
A4: a ** X is empty and
A5: X is non empty;
  consider x being set such that
A6: x in X by A5,XBOOLE_0:def 1;
  thus contradiction by A4,A6;
end;

theorem Th8:
  for X be Subset of REAL holds r**X = {r*x : x in X}
proof
  let X be Subset of REAL;
  thus r**X c= {r*x : x in X}
  proof
    let y be set;
    assume y in r**X;
    then consider z being Element of ExtREAL such that
X1: y = r * z & z in X by MEMBER_1:188;
    reconsider z1 = z as Element of REAL by X1;
    y = r * z1 by X1,XXREAL_3:def 5;
    hence thesis by X1;
  end;
  let y be set;
  assume y in {r*x : x in X};
  then consider z being Real such that
Y1:y = r * z & z in X;
  thus thesis by MEMBER_1:193,Y1;
end;

theorem
  for X be non empty Subset of REAL st X is bounded_above & 0<=r holds
    r**X is bounded_above
proof
  let X be non empty Subset of REAL;
  assume that
A1: X is bounded_above and
A2: 0<=r;
  consider b be real number such that
B3: b is UpperBound of X by A1,XXREAL_2:def 10;
A3: for x be real number st x in X holds x <= b by B3,XXREAL_2:def 1;
  r*b is UpperBound of r**X
  proof
    let y be ext-real number;
    assume y in r**X;
    then y in {r*x : x in X} by Th8;
    then consider x such that
A4: y=r*x and
A5: x in X;
    x <= b by A3,A5;
    hence thesis by A2,A4,XREAL_1:66;
  end;
  hence thesis by XXREAL_2:def 10;
end;

theorem
  for X be non empty Subset of REAL st X is bounded_above & r<=0 holds
    r**X is bounded_below
proof
  let X be non empty Subset of REAL;
  assume that
A1: X is bounded_above and
A2: r<=0;
  consider b be real number such that
B3: b is UpperBound of X by A1,XXREAL_2:def 10;
A3: for x be real number st x in X holds x <= b by B3,XXREAL_2:def 1;
   r*b is LowerBound of r**X
  proof
    let y be ext-real number;
    assume y in r**X;
    then y in {r*x : x in X} by Th8;
    then ex x st y=r*x & x in X;
    hence thesis by A2,A3,XREAL_1:67;
  end;
  hence thesis by XXREAL_2:def 9;
end;

theorem
  for X be non empty Subset of REAL st X is bounded_below & 0<=r holds
    r**X is bounded_below
proof
  let X be non empty Subset of REAL;
  assume that
A1: X is bounded_below and
A2: 0<=r;
  consider b be real number such that
B3: b is LowerBound of X by A1,XXREAL_2:def 9;
A3: for x be real number st x in X holds b <= x by B3,XXREAL_2:def 2;
  r*b is LowerBound of r**X
  proof
    let y be ext-real number;
    assume y in r**X;
    then y in {r*x : x in X} by Th8;
    then ex x st y=r*x & x in X;
    hence thesis by A2,A3,XREAL_1:66;
  end;
  hence thesis by XXREAL_2:def 9;
end;

theorem
  for X be non empty Subset of REAL st X is bounded_below & r<=0 holds
    r**X is bounded_above
proof
  let X be non empty Subset of REAL;
  assume that
A1: X is bounded_below and
A2: r<=0;
  consider b be real number such that
B3: b is LowerBound of X by A1,XXREAL_2:def 9;
A3: for x be real number st x in X holds b <= x by B3,XXREAL_2:def 2;
  r*b is UpperBound of r**X
  proof
    let y be ext-real number;
    assume y in r**X;
    then y in {r*x : x in X} by Th8;
    then consider x such that
A4: y=r*x and
A5: x in X;
    b <= x by A3,A5;
    hence thesis by A2,A4,XREAL_1:67;
  end;
  hence thesis by XXREAL_2:def 10;
end;

theorem Th13:
  for X be non empty Subset of REAL st X is bounded_above & 0<=r
  holds upper_bound(r**X) = r*(upper_bound X)
proof
  let X be non empty Subset of REAL;
  assume that
A1: X is bounded_above and
A2: 0<=r;
A3: for a be real number st a in r**X holds a <= r*(upper_bound X)
  proof
    let a be real number;
    assume a in r**X;
    then a in {r*x : x in X} by Th8;
    then consider x such that
A4: a=r*x and
A5: x in X;
    x <= upper_bound X by A1,A5,SEQ_4:def 4;
    hence thesis by A2,A4,XREAL_1:66;
  end;
 for b be real number st for a be real number st a in r**X holds a <= b
  holds r*(upper_bound X) <= b
  proof
    consider x such that
A7: x in X by SUBSET_1:10;
    let b be real number;
    assume
A8: for a be real number st a in r**X holds a <= b;
    r*x in {r*y : y in X} by A7;
    then
A9: r*x in r**X by Th8;
    now
      per cases by A2;
      suppose
        r=0;
        hence thesis by A8,A9;
      end;
      suppose
A10:    r>0;
        for z be real number st z in X holds z <= b/r
        proof
          let z be real number;
          assume z in X;
          then r*z in {r*y : y in X};
          then r*z in r**X by Th8;
          hence thesis by A8,A10,XREAL_1:79;
        end;
        then upper_bound X <= b/r by SEQ_4:62;
        then r*(upper_bound X) <= b/r*r by A10,XREAL_1:66;
        hence thesis by A10,XCMPLX_1:88;
      end;
    end;
    hence thesis;
  end;
  hence thesis by A3,SEQ_4:63;
end;

theorem Th14:
  for X be non empty Subset of REAL st X is bounded_above & r<=0
  holds lower_bound(r**X) = r*(upper_bound X)
proof
  let X be non empty Subset of REAL;
  assume that
A1: X is bounded_above and
A2: r<=0;
A3: for a be real number st a in r**X holds r*(upper_bound X) <= a
  proof
    let a be real number;
    assume a in r**X;
    then a in {r*x : x in X} by Th8;
    then consider x such that
A4: a=r*x and
A5: x in X;
    x <= upper_bound X by A1,A5,SEQ_4:def 4;
    hence thesis by A2,A4,XREAL_1:67;
  end;
 for b be real number st for a be real number st a in r**X holds a >= b
  holds r*(upper_bound X) >= b
  proof
    consider x such that
A7: x in X by SUBSET_1:10;
    let b be real number;
    assume
A8: for a be real number st a in r**X holds a >= b;
    r*x in {r*y : y in X} by A7;
    then
A9: r*x in r**X by Th8;
    now
      per cases by A2;
      suppose
        r=0;
        hence thesis by A8,A9;
      end;
      suppose
A10:    r<0;
        for z be real number st z in X holds z <= b/r
        proof
          let z be real number;
          assume z in X;
          then r*z in {r*y : y in X};
          then r*z in r**X by Th8;
          hence thesis by A8,A10,XREAL_1:82;
        end;
        then upper_bound X <= b/r by SEQ_4:62;
        then r*(upper_bound X) >= b/r*r by A10,XREAL_1:67;
        hence thesis by A10,XCMPLX_1:88;
      end;
    end;
    hence thesis;
  end;
  hence thesis by A3,SEQ_4:61;
end;

theorem Th15:
  for X be non empty Subset of REAL st X is bounded_below & 0<=r
  holds lower_bound(r**X) = r*(lower_bound X)
proof
  let X be non empty Subset of REAL;
  assume that
A1: X is bounded_below and
A2: 0<=r;
A3: for a being real number st a in r**X holds r*(lower_bound X) <= a
  proof
    let a be real number;
    assume a in r**X;
    then a in {r*x : x in X} by Th8;
    then consider x such that
A4: a=r*x and
A5: x in X;
    lower_bound X <= x by A1,A5,SEQ_4:def 5;
    hence thesis by A2,A4,XREAL_1:66;
  end;
 for b be real number st for a be real number st a in r**X holds a >= b
  holds r*(lower_bound X) >= b
  proof
    consider x such that
A7: x in X by SUBSET_1:10;
    let b be real number;
    assume
A8: for a be real number st a in r**X holds a >= b;
    r*x in {r*y : y in X} by A7;
    then
A9: r*x in r**X by Th8;
    now
      per cases by A2;
      suppose
        r=0;
        hence thesis by A8,A9;
      end;
      suppose
A10:    r>0;
        for z be real number st z in X holds z >= b/r
        proof
          let z be real number;
          assume z in X;
          then r*z in {r*y : y in X};
          then r*z in r**X by Th8;
          hence thesis by A8,A10,XREAL_1:81;
        end;
        then lower_bound X >= b/r by SEQ_4:60;
        then r*(lower_bound X) >= b/r*r by A10,XREAL_1:66;
        hence thesis by A10,XCMPLX_1:88;
      end;
    end;
    hence thesis;
  end;
  hence thesis by A3,SEQ_4:61;
end;

theorem Th16:
  for X be non empty Subset of REAL st X is bounded_below & r<=0
  holds upper_bound(r**X) = r*(lower_bound X)
proof
  let X be non empty Subset of REAL;
  assume that
A1: X is bounded_below and
A2: r<=0;
A3: for a be real number st a in r**X holds r*(lower_bound X) >= a
  proof
    let a be real number;
    assume a in r**X;
    then a in {r*x : x in X} by Th8;
    then consider x such that
A4: a=r*x and
A5: x in X;
    lower_bound X <= x by A1,A5,SEQ_4:def 5;
    hence thesis by A2,A4,XREAL_1:67;
  end;
 for b be real number st for a be real number st a in r**X holds a <= b
  holds r*(lower_bound X) <= b
  proof
    consider x such that
A7: x in X by SUBSET_1:10;
    let b be real number;
    assume
A8: for a being real number st a in r**X holds a <= b;
    r*x in {r*y : y in X} by A7;
    then
A9: r*x in r**X by Th8;
    now
      per cases by A2;
      suppose
        r=0;
        hence thesis by A8,A9;
      end;
      suppose
A10:    r<0;
        for z be real number st z in X holds z >= b/r
        proof
          let z be real number;
          assume z in X;
          then r*z in {r*y : y in X};
          then r*z in r**X by Th8;
          hence thesis by A8,A10,XREAL_1:80;
        end;
        then lower_bound X >= b/r by SEQ_4:60;
        then r*(lower_bound X) <= b/r*r by A10,XREAL_1:67;
        hence thesis by A10,XCMPLX_1:88;
      end;
    end;
    hence thesis;
  end;
  hence thesis by A3,SEQ_4:63;
end;

begin :: Scalar Multiple of Integral

theorem Th17:
  for X be non empty set, f be Function of X,REAL holds rng(r(#)f)
  = r**rng f
proof
  let X be non empty set;
  let f be Function of X,REAL;
  for y holds y in r**rng f implies y in rng(r(#)f)
  proof
    let y;
    assume y in r**rng f;
    then y in {r*b : b in rng f} by Th8;
    then consider b such that
A1: y=r*b and
A2: b in rng f;
    consider x being Element of X such that
A3: x in dom f and
A4: b=f.x by A2,PARTFUN1:26;
    x in dom(r(#)f) by A3,VALUED_1:def 5;
    then (r(#)f).x in rng(r(#)f) by FUNCT_1:def 5;
    hence thesis by A1,A4,RFUNCT_1:73;
  end;
  then
A5: r**rng f c= rng(r(#)f) by SUBSET_1:7;
  for y holds y in rng(r(#)f) implies y in r**rng f
  proof
    let y;
    assume y in rng(r(#)f);
    then consider x being Element of X such that
A6: x in dom(r(#)f) and
A7: y=(r(#)f).x by PARTFUN1:26;
    x in dom f by A6,VALUED_1:def 5;
    then
A8: f.x in rng f by FUNCT_1:def 5;
    y = r*(f.x) by A7,RFUNCT_1:73;
    then y in {r*b : b in rng f} by A8;
    hence thesis by Th8;
  end;
  then rng(r(#)f) c= r**rng f by SUBSET_1:7;
  hence thesis by A5,XBOOLE_0:def 10;
end;

theorem Th18:
  for X,Z be non empty set, f be PartFunc of X,REAL holds rng(r(#)
  (f|Z)) = r**rng(f|Z)
proof
  let X,Z be non empty set;
  let f be PartFunc of X,REAL;
  for y holds y in r**rng(f|Z) implies y in rng(r(#)f|Z)
  proof
    let y;
    assume y in r**rng(f|Z);
    then y in {r*b : b in rng(f|Z)} by Th8;
    then consider b such that
A1: y=r*b and
A2: b in rng(f|Z);
    consider x being Element of X such that
A3: x in dom(f|Z) and
A4: b=(f|Z).x by A2,PARTFUN1:26;
A5: x in dom(r(#)f|Z) by A3,VALUED_1:def 5;
    then y= (r(#)f|Z).x by A1,A4,VALUED_1:def 5;
    hence thesis by A5,FUNCT_1:def 5;
  end;
  then
A6: r**rng(f|Z) c= rng(r(#)f|Z) by SUBSET_1:7;
  for y holds y in rng(r(#)f|Z) implies y in r**rng(f|Z)
  proof
    let y;
    assume y in rng(r(#)f|Z);
    then consider x be Element of X such that
A7: x in dom(r(#)f|Z) and
A8: y=(r(#)f|Z).x by PARTFUN1:26;
    x in dom(f|Z) by A7,VALUED_1:def 5;
    then
A9: (f|Z).x in rng(f|Z) by FUNCT_1:def 5;
    y=r*(f|Z).x by A7,A8,VALUED_1:def 5;
    then y in {r*b : b in rng(f|Z)} by A9;
    hence thesis by Th8;
  end;
  then rng(r(#)f|Z) c= r**rng(f|Z) by SUBSET_1:7;
  hence thesis by A6,XBOOLE_0:def 10;
end;

reserve A, B for closed-interval Subset of REAL;
reserve f, g for Function of A,REAL;
reserve D, D1, D2 for Division of A;

theorem Th19:
  f|A is bounded & r >= 0 implies (upper_sum_set(r(#)f)).D >= r*(
  lower_bound rng f)*vol(A)
proof
  assume that
A1: f|A is bounded and
A2: r >= 0;
A3: rng f is bounded_below by A1,INTEGRA1:13;
A4: (r(#)f)|A is bounded by A1,RFUNCT_1:97;
  then
A5: lower_sum(r(#)f,D) >= (lower_bound rng(r(#)f))*vol(A) by INTEGRA1:27;
  dom(upper_sum_set(r(#)f))=divs A & D in divs A by INTEGRA1:def 3,def 11;
  then (upper_sum_set(r(#)f)).D = upper_sum(r(#)f,D) by INTEGRA1:def 11;
  then
A6: (upper_sum_set(r(#)f)).D >= lower_sum(r(#)f,D) by A4,INTEGRA1:30;
  (lower_bound rng(r(#)f))=lower_bound (r**(rng f)) by Th17
    .=r*(lower_bound rng f) by A2,A3,Th15;
  hence thesis by A6,A5,XXREAL_0:2;
end;

theorem Th20:
  f|A is bounded & r <= 0 implies (upper_sum_set(r(#)f)).D >= r*(
  upper_bound rng f)*vol(A)
proof
  assume that
A1: f|A is bounded and
A2: r <= 0;
A3: rng f is bounded_above by A1,INTEGRA1:15;
A4: (r(#)f)|A is bounded by A1,RFUNCT_1:97;
  then
A5: lower_sum(r(#)f,D) >= (lower_bound rng(r(#)f))*vol(A) by INTEGRA1:27;
  dom(upper_sum_set(r(#)f))=divs A & D in divs A by INTEGRA1:def 3,def 11;
  then (upper_sum_set(r(#)f)).D = upper_sum(r(#)f,D) by INTEGRA1:def 11;
  then
A6: (upper_sum_set(r(#)f)).D >= lower_sum(r(#)f,D) by A4,INTEGRA1:30;
  (lower_bound rng(r(#)f))=lower_bound (r**(rng f)) by Th17
    .=r*(upper_bound rng f) by A2,A3,Th14;
  hence thesis by A6,A5,XXREAL_0:2;
end;

theorem Th21:
  f|A is bounded & r >= 0 implies (lower_sum_set(r(#)f)).D <= r*(
  upper_bound rng f)*vol(A)
proof
  assume that
A1: f|A is bounded and
A2: r >= 0;
A3: rng f is bounded_above by A1,INTEGRA1:15;
A4: (r(#)f)|A is bounded by A1,RFUNCT_1:97;
  then
A5: upper_sum(r(#)f,D) <= (upper_bound rng(r(#)f))*vol(A) by INTEGRA1:29;
  dom(lower_sum_set(r(#)f))=divs A & D in divs A by INTEGRA1:def 3,def 12;
  then (lower_sum_set(r(#)f)).D = lower_sum(r(#)f,D) by INTEGRA1:def 12;
  then
A6: (lower_sum_set(r(#)f)).D <= upper_sum(r(#)f,D) by A4,INTEGRA1:30;
  (upper_bound rng(r(#)f))=upper_bound (r**(rng f)) by Th17
    .=r*(upper_bound rng f) by A2,A3,Th13;
  hence thesis by A6,A5,XXREAL_0:2;
end;

theorem Th22:
  f|A is bounded & r <= 0 implies (lower_sum_set(r(#)f)).D <= r*(
  lower_bound rng f)*vol(A)
proof
  assume that
A1: f|A is bounded and
A2: r <= 0;
A3: rng f is bounded_below by A1,INTEGRA1:13;
A4: (r(#)f)|A is bounded by A1,RFUNCT_1:97;
  then
A5: upper_sum(r(#)f,D) <= (upper_bound rng(r(#)f))*vol(A) by INTEGRA1:29;
  dom(lower_sum_set(r(#)f))=divs A & D in divs A by INTEGRA1:def 3,def 12;
  then (lower_sum_set(r(#)f)).D = lower_sum(r(#)f,D) by INTEGRA1:def 12;
  then
A6: (lower_sum_set(r(#)f)).D <= upper_sum(r(#)f,D) by A4,INTEGRA1:30;
  (upper_bound rng(r(#)f))=upper_bound (r**(rng f)) by Th17
    .=r*(lower_bound rng f) by A2,A3,Th16;
  hence thesis by A6,A5,XXREAL_0:2;
end;

theorem Th23:
  i in dom D & f|A is bounded_above & r >= 0 implies upper_volume(
  r(#)f,D).i = r*upper_volume(f,D).i
proof
  assume that
A1: i in dom D and
A2: f|A is bounded_above and
A3: r >= 0;
  dom(f|divset(D,i)) = dom f /\ divset(D,i) by RELAT_1:90
    .= A /\ divset(D,i) by FUNCT_2:def 1
    .= divset(D,i) by A1,INTEGRA1:10,XBOOLE_1:28;
  then
A4: rng(f|divset(D,i)) is non empty by RELAT_1:65;
  rng f is bounded_above by A2,INTEGRA1:15;
  then
A5: rng(f|divset(D,i)) is bounded_above by RELAT_1:99,XXREAL_2:43;
  upper_volume(r(#)f,D).i =(upper_bound(rng((r(#)f)|divset(D,i))))*vol(
  divset(D,i)) by A1,INTEGRA1:def 7
    .=(upper_bound(rng(r(#)f|divset(D,i))))*vol(divset(D,i)) by RFUNCT_1:65
    .=(upper_bound(r**rng(f|divset(D,i))))*vol(divset(D,i)) by Th18
    .=(r*upper_bound(rng(f|divset(D,i))))*vol(divset(D,i)) by A3,A4,A5,Th13
    .=r*(upper_bound(rng(f|divset(D,i)))*vol(divset(D,i)))
    .=r*upper_volume(f,D).i by A1,INTEGRA1:def 7;
  hence thesis;
end;

theorem Th24:
  i in dom D & f|A is bounded_above & r <= 0 implies lower_volume(
  r(#)f,D).i = r*upper_volume(f,D).i
proof
  assume that
A1: i in dom D and
A2: f|A is bounded_above and
A3: r <= 0;
  dom(f|divset(D,i)) = dom f /\ divset(D,i) by RELAT_1:90
    .= A /\ divset(D,i) by FUNCT_2:def 1
    .= divset(D,i) by A1,INTEGRA1:10,XBOOLE_1:28;
  then
A4: rng(f|divset(D,i)) is non empty by RELAT_1:65;
  rng f is bounded_above by A2,INTEGRA1:15;
  then
A5: rng(f|divset(D,i)) is bounded_above by RELAT_1:99,XXREAL_2:43;
  lower_volume(r(#)f,D).i =(lower_bound(rng((r(#)f)|divset(D,i))))*vol(
  divset(D,i)) by A1,INTEGRA1:def 8
    .=(lower_bound(rng(r(#)f|divset(D,i))))*vol(divset(D,i)) by RFUNCT_1:65
    .=(lower_bound(r**rng(f|divset(D,i))))*vol(divset(D,i)) by Th18
    .=(r*upper_bound(rng(f|divset(D,i))))*vol(divset(D,i)) by A3,A4,A5,Th14
    .=r*(upper_bound(rng(f|divset(D,i)))*vol(divset(D,i)))
    .=r*upper_volume(f,D).i by A1,INTEGRA1:def 7;
  hence thesis;
end;

theorem Th25:
  i in dom D & f|A is bounded_below & r >= 0 implies lower_volume(
  r(#)f,D).i = r*lower_volume(f,D).i
proof
  assume that
A1: i in dom D and
A2: f|A is bounded_below and
A3: r >= 0;
  dom(f|divset(D,i)) = dom f /\ divset(D,i) by RELAT_1:90
    .= A /\ divset(D,i) by FUNCT_2:def 1
    .= divset(D,i) by A1,INTEGRA1:10,XBOOLE_1:28;
  then
A4: rng(f|divset(D,i)) is non empty by RELAT_1:65;
  rng f is bounded_below by A2,INTEGRA1:13;
  then
A5: rng(f|divset(D,i)) is bounded_below by RELAT_1:99,XXREAL_2:44;
  lower_volume(r(#)f,D).i =(lower_bound(rng((r(#)f)|divset(D,i))))*vol(
  divset(D,i)) by A1,INTEGRA1:def 8
    .=(lower_bound(rng(r(#)f|divset(D,i))))*vol(divset(D,i)) by RFUNCT_1:65
    .=(lower_bound(r**rng(f|divset(D,i))))*vol(divset(D,i)) by Th18
    .=(r*lower_bound(rng(f|divset(D,i))))*vol(divset(D,i)) by A3,A4,A5,Th15
    .=r*(lower_bound(rng(f|divset(D,i)))*vol(divset(D,i)))
    .=r*lower_volume(f,D).i by A1,INTEGRA1:def 8;
  hence thesis;
end;

theorem Th26:
  i in dom D & f|A is bounded_below & r <= 0 implies upper_volume(
  r(#)f,D).i = r*lower_volume(f,D).i
proof
  assume that
A1: i in dom D and
A2: f|A is bounded_below and
A3: r <= 0;
  dom(f|divset(D,i)) = dom f /\ divset(D,i) by RELAT_1:90
    .= A /\ divset(D,i) by FUNCT_2:def 1
    .= divset(D,i) by A1,INTEGRA1:10,XBOOLE_1:28;
  then
A4: rng(f|divset(D,i)) is non empty by RELAT_1:65;
  rng f is bounded_below by A2,INTEGRA1:13;
  then
A5: rng(f|divset(D,i)) is bounded_below by RELAT_1:99,XXREAL_2:44;
  upper_volume(r(#)f,D).i =(upper_bound(rng((r(#)f)|divset(D,i))))*vol(
  divset(D,i)) by A1,INTEGRA1:def 7
    .=(upper_bound(rng(r(#)f|divset(D,i))))*vol(divset(D,i)) by RFUNCT_1:65
    .=(upper_bound(r**rng(f|divset(D,i))))*vol(divset(D,i)) by Th18
    .=(r*lower_bound(rng(f|divset(D,i))))*vol(divset(D,i)) by A3,A4,A5,Th16
    .=r*(lower_bound(rng(f|divset(D,i)))*vol(divset(D,i)))
    .=r*lower_volume(f,D).i by A1,INTEGRA1:def 8;
  hence thesis;
end;

theorem Th27:
  f|A is bounded_above & r >= 0 implies upper_sum(r(#)f,D) = r*
  upper_sum(f,D)
proof
  assume
A1: f|A is bounded_above & r >= 0;
A2: for i be Nat st 1 <= i & i <= len upper_volume(r(#)f,D) holds
  upper_volume(r(#)f,D).i = (r*upper_volume(f,D)).i
  proof
    let i be Nat;
    assume
A3: 1 <= i & i <= len upper_volume(r(#)f,D);
    len D = len upper_volume(r(#)f,D) by INTEGRA1:def 7;
    then i in dom D by A3,FINSEQ_3:27;
    then upper_volume(r(#)f,D).i = r*upper_volume(f,D).i by A1,Th23
      .= (r*upper_volume(f,D)).i by RVSUM_1:66;
    hence thesis;
  end;
  len upper_volume(r(#)f,D) = len D by INTEGRA1:def 7
    .= len upper_volume(f,D) by INTEGRA1:def 7
    .= len(r*upper_volume(f,D)) by NEWTON:6;
  then upper_volume(r(#)f,D)=r*upper_volume(f,D) by A2,FINSEQ_1:18;
  then upper_sum(r(#)f,D) =Sum(r*upper_volume(f,D)) by INTEGRA1:def 9
    .=r*Sum(upper_volume(f,D)) by RVSUM_1:117
    .=r*upper_sum(f,D) by INTEGRA1:def 9;
  hence thesis;
end;

theorem Th28:
  f|A is bounded_above & r <= 0 implies lower_sum(r(#)f,D) = r*
  upper_sum(f,D)
proof
  assume
A1: f|A is bounded_above & r <= 0;
A2: for i be Nat st 1 <= i & i <= len lower_volume(r(#)f,D) holds
  lower_volume(r(#)f,D).i = (r*upper_volume(f,D)).i
  proof
    let i be Nat;
    assume
A3: 1 <= i & i <= len lower_volume(r(#)f,D);
    len D = len lower_volume(r(#)f,D) by INTEGRA1:def 8;
    then i in dom D by A3,FINSEQ_3:27;
    then lower_volume(r(#)f,D).i = r*upper_volume(f,D).i by A1,Th24
      .= (r*upper_volume(f,D)).i by RVSUM_1:66;
    hence thesis;
  end;
  len lower_volume(r(#)f,D) = len D by INTEGRA1:def 8
    .= len upper_volume(f,D) by INTEGRA1:def 7
    .= len(r*upper_volume(f,D)) by NEWTON:6;
  then lower_volume(r(#)f,D)=r*upper_volume(f,D) by A2,FINSEQ_1:18;
  then lower_sum(r(#)f,D) =Sum(r*upper_volume(f,D)) by INTEGRA1:def 10
    .=r*Sum(upper_volume(f,D)) by RVSUM_1:117
    .=r*upper_sum(f,D) by INTEGRA1:def 9;
  hence thesis;
end;

theorem Th29:
  f|A is bounded_below & r >= 0 implies lower_sum(r(#)f,D) = r*
  lower_sum(f,D)
proof
  assume
A1: f|A is bounded_below & r >= 0;
A2: for i be Nat st 1 <= i & i <= len lower_volume(r(#)f,D) holds
  lower_volume(r(#)f,D).i = (r*lower_volume(f,D)).i
  proof
    let i be Nat;
    assume
A3: 1 <= i & i <= len lower_volume(r(#)f,D);
    len D = len lower_volume(r(#)f,D) by INTEGRA1:def 8;
    then i in dom D by A3,FINSEQ_3:27;
    then lower_volume(r(#)f,D).i = r*lower_volume(f,D).i by A1,Th25
      .= (r*lower_volume(f,D)).i by RVSUM_1:66;
    hence thesis;
  end;
  len lower_volume(r(#)f,D) = len D by INTEGRA1:def 8
    .= len lower_volume(f,D) by INTEGRA1:def 8
    .= len(r*lower_volume(f,D)) by NEWTON:6;
  then lower_volume(r(#)f,D)=r*lower_volume(f,D) by A2,FINSEQ_1:18;
  then lower_sum(r(#)f,D) =Sum(r*lower_volume(f,D)) by INTEGRA1:def 10
    .=r*Sum(lower_volume(f,D)) by RVSUM_1:117
    .=r*lower_sum(f,D) by INTEGRA1:def 10;
  hence thesis;
end;

theorem Th30:
  f|A is bounded_below & r <= 0 implies upper_sum(r(#)f,D) = r*
  lower_sum(f,D)
proof
  assume
A1: f|A is bounded_below & r <= 0;
A2: for i be Nat st 1 <= i & i <= len upper_volume(r(#)f,D) holds
  upper_volume(r(#)f,D).i = (r*lower_volume(f,D)).i
  proof
    let i be Nat;
    assume
A3: 1 <= i & i <= len upper_volume(r(#)f,D);
    len D = len upper_volume(r(#)f,D) by INTEGRA1:def 7;
    then i in dom D by A3,FINSEQ_3:27;
    then upper_volume(r(#)f,D).i = r*lower_volume(f,D).i by A1,Th26
      .= (r*lower_volume(f,D)).i by RVSUM_1:66;
    hence thesis;
  end;
  len upper_volume(r(#)f,D) = len D by INTEGRA1:def 7
    .= len lower_volume(f,D) by INTEGRA1:def 8
    .= len(r*lower_volume(f,D)) by NEWTON:6;
  then upper_volume(r(#)f,D)=r*lower_volume(f,D) by A2,FINSEQ_1:18;
  then upper_sum(r(#)f,D) =Sum(r*lower_volume(f,D)) by INTEGRA1:def 9
    .=r*Sum(lower_volume(f,D)) by RVSUM_1:117
    .=r*lower_sum(f,D) by INTEGRA1:def 10;
  hence thesis;
end;

theorem Th31:
  f|A is bounded & f is integrable implies r(#)f is integrable &
  integral(r(#)f) = r*integral(f)
proof
  assume that
A1: f|A is bounded and
A2: f is integrable;
  f is upper_integrable by A2,INTEGRA1:def 17;
  then
A3: rng(upper_sum_set(f)) is bounded_below by INTEGRA1:def 13;
  f is lower_integrable by A2,INTEGRA1:def 17;
  then
A4: rng(lower_sum_set(f)) is bounded_above by INTEGRA1:def 14;
A5: dom upper_sum_set(f) = divs A by INTEGRA1:def 11;
  then
A6: upper_sum_set(f) is total by PARTFUN1:def 4;
A7: dom lower_sum_set(f) = divs A by INTEGRA1:def 12;
  then
A8: lower_sum_set(f) is total by PARTFUN1:def 4;
A9: rng(lower_sum_set(f)) is non empty by A7,RELAT_1:65;
A10: rng(upper_sum_set(f)) is non empty by A5,RELAT_1:65;
A11: now
    per cases;
    suppose
A12:  r >= 0;
A13:  for D be set st D in divs A holds (upper_sum_set(r(#)f)).D = (r(#)(
      upper_sum_set(f))).D
      proof
        let D be set;
        assume
A14:    D in divs A;
        then reconsider D as Division of A by INTEGRA1:def 3;
A15:    D in dom upper_sum_set(f) by A14,INTEGRA1:def 11;
        D in dom upper_sum_set(r(#)f) by A14,INTEGRA1:def 11;
        then (upper_sum_set(r(#)f)).D = upper_sum(r(#) f,D) by INTEGRA1:def 11
          .= r*upper_sum(f,D) by A1,A12,Th27
          .= r*(upper_sum_set(f)).D by A15,INTEGRA1:def 11
          .= (r(#)(upper_sum_set(f))).D by A6,A14,RFUNCT_1:73;
        hence thesis;
      end;
A16:  divs A = dom(upper_sum_set(f)) by INTEGRA1:def 11
        .= dom(r(#)upper_sum_set(f)) by VALUED_1:def 5;
      divs A = dom upper_sum_set(r(#)f) by INTEGRA1:def 11;
      then upper_sum_set(r(#)f) = r(#)upper_sum_set(f) by A16,A13,FUNCT_1:9;
      then
A17:  upper_integral(r(#)f) = lower_bound rng(r(#) upper_sum_set(f)) by
INTEGRA1:def 15
        .= lower_bound(r**(rng(upper_sum_set(f)))) by A6,Th17
        .= r*lower_bound(rng(upper_sum_set(f))) by A10,A3,A12,Th15
        .= r*upper_integral(f) by INTEGRA1:def 15;
A18:  for D be set st D in divs A holds (lower_sum_set(r(#)f)).D = (r(#)(
      lower_sum_set(f))).D
      proof
        let D be set;
        assume
A19:    D in divs A;
        then reconsider D as Division of A by INTEGRA1:def 3;
A20:    D in dom lower_sum_set(f) by A19,INTEGRA1:def 12;
        D in dom lower_sum_set(r(#)f) by A19,INTEGRA1:def 12;
        then (lower_sum_set(r(#)f)).D = lower_sum(r(#) f,D) by INTEGRA1:def 12
          .= r*lower_sum(f,D) by A1,A12,Th29
          .= r*(lower_sum_set(f)).D by A20,INTEGRA1:def 12
          .= (r(#)(lower_sum_set(f))).D by A8,A19,RFUNCT_1:73;
        hence thesis;
      end;
A21:  divs A = dom(lower_sum_set(f)) by INTEGRA1:def 12
        .= dom(r(#)lower_sum_set(f)) by VALUED_1:def 5;
      divs A = dom lower_sum_set(r(#)f) by INTEGRA1:def 12;
      then lower_sum_set(r(#)f) = r(#)lower_sum_set(f) by A21,A18,FUNCT_1:9;
      then lower_integral(r(#)f) = upper_bound rng(r(#) lower_sum_set(f)) by
INTEGRA1:def 16
        .= upper_bound(r**(rng(lower_sum_set(f)))) by A8,Th17
        .= r*upper_bound(rng(lower_sum_set(f))) by A4,A9,A12,Th13
        .= r*lower_integral(f) by INTEGRA1:def 16
        .= r*upper_integral(f) by A2,INTEGRA1:def 17;
      hence upper_integral(r(#)f) = lower_integral(r(#)f) by A17;
      thus upper_integral(r(#)f) = r*integral(f) by A17,INTEGRA1:def 18;
    end;
    suppose
A22:  r < 0;
A23:  for D be set st D in divs A holds (upper_sum_set(r(#)f)).D = (r(#)(
      lower_sum_set(f))).D
      proof
        let D be set;
        assume
A24:    D in divs A;
        then reconsider D as Division of A by INTEGRA1:def 3;
A25:    D in dom lower_sum_set(f) by A24,INTEGRA1:def 12;
        D in dom upper_sum_set(r(#)f) by A24,INTEGRA1:def 11;
        then (upper_sum_set(r(#)f)).D=upper_sum(r(#)f,D) by INTEGRA1:def 11
          .= r*lower_sum(f,D) by A1,A22,Th30
          .= r*(lower_sum_set(f)).D by A25,INTEGRA1:def 12
          .= (r(#)(lower_sum_set(f))).D by A8,A24,RFUNCT_1:73;
        hence thesis;
      end;
A26:  divs A = dom(lower_sum_set(f)) by INTEGRA1:def 12
        .= dom(r(#)lower_sum_set(f)) by VALUED_1:def 5;
      divs A = dom upper_sum_set(r(#)f) by INTEGRA1:def 11;
      then upper_sum_set(r(#)f) = r(#)lower_sum_set(f) by A26,A23,FUNCT_1:9;
      then
A27:  upper_integral(r(#)f) = lower_bound rng(r(#) lower_sum_set(f)) by
INTEGRA1:def 15
        .= lower_bound(r**(rng(lower_sum_set(f)))) by A8,Th17
        .= r*upper_bound(rng(lower_sum_set(f))) by A4,A9,A22,Th14
        .= r*lower_integral(f) by INTEGRA1:def 16;
A28:  for D be set st D in divs A holds (lower_sum_set(r(#)f)).D = (r(#)(
      upper_sum_set(f))).D
      proof
        let D be set;
        assume
A29:    D in divs A;
        then reconsider D as Division of A by INTEGRA1:def 3;
A30:    D in dom upper_sum_set(f) by A29,INTEGRA1:def 11;
        D in dom lower_sum_set(r(#)f) by A29,INTEGRA1:def 12;
        then (lower_sum_set(r(#)f)).D=lower_sum(r(#)f,D) by INTEGRA1:def 12
          .= r*upper_sum(f,D) by A1,A22,Th28
          .= r*(upper_sum_set(f)).D by A30,INTEGRA1:def 11
          .= (r(#)(upper_sum_set(f))).D by A6,A29,RFUNCT_1:73;
        hence thesis;
      end;
A31:  divs A = dom(upper_sum_set(f)) by INTEGRA1:def 11
        .= dom(r(#)upper_sum_set(f)) by VALUED_1:def 5;
      divs A = dom lower_sum_set(r(#)f) by INTEGRA1:def 12;
      then lower_sum_set(r(#)f) = r(#)upper_sum_set(f) by A31,A28,FUNCT_1:9;
      then lower_integral(r(#)f) = upper_bound rng(r(#) upper_sum_set(f)) by
INTEGRA1:def 16
        .= upper_bound(r**(rng(upper_sum_set(f)))) by A6,Th17
        .= r*lower_bound(rng(upper_sum_set(f))) by A10,A3,A22,Th16
        .= r*upper_integral(f) by INTEGRA1:def 15
        .= r*lower_integral(f) by A2,INTEGRA1:def 17;
      hence upper_integral(r(#)f) = lower_integral(r(#)f) by A27;
      upper_integral(r(#)f) = r*upper_integral(f) by A2,A27,INTEGRA1:def 17
        .= r*integral(f) by INTEGRA1:def 18;
      hence upper_integral(r(#)f) = r*integral(f);
    end;
  end;
  ex a st for D be set st D in divs A /\ dom(lower_sum_set(r(#)f)) holds
  a >= (lower_sum_set(r(#)f)).D
  proof
    now
      per cases;
      suppose
A32:    r>=0;
        for D be set st D in divs A /\ dom(lower_sum_set(r(#)f)) holds r*
        (upper_bound rng f)*vol(A) >= (lower_sum_set(r(#)f)).D
        proof
          let D be set;
          assume D in divs A /\ dom(lower_sum_set(r(#)f));
          then D is Division of A by INTEGRA1:def 3;
          hence thesis by A1,A32,Th21;
        end;
        hence thesis;
      end;
      suppose
A33:    r<0;
        for D be set st D in divs A /\ dom(lower_sum_set(r(#)f)) holds r*
        (lower_bound rng f)*vol(A) >= (lower_sum_set(r(#)f)).D
        proof
          let D be set;
          assume D in divs A /\ dom(lower_sum_set(r(#)f));
          then D is Division of A by INTEGRA1:def 3;
          hence thesis by A1,A33,Th22;
        end;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
  then (lower_sum_set(r(#)f))|divs A is bounded_above by RFUNCT_1:87;
  then rng lower_sum_set(r(#)f) is bounded_above by INTEGRA1:15;
  then
A34: r(#)f is lower_integrable by INTEGRA1:def 14;
  ex a st for D be set st D in divs A /\ dom(upper_sum_set(r(#)f)) holds a
  <= (upper_sum_set(r(#)f)).D
  proof
    now
      per cases;
      suppose
A35:    r>=0;
        for D be set st D in divs A /\ dom(upper_sum_set(r(#)f)) holds r*(
        lower_bound rng f)*vol(A) <= (upper_sum_set(r(#)f)).D
        proof
          let D be set;
          assume D in divs A /\ dom(upper_sum_set(r(#)f));
          then D is Division of A by INTEGRA1:def 3;
          hence thesis by A1,A35,Th19;
        end;
        hence thesis;
      end;
      suppose
A36:    r<0;
        for D be set st D in divs A /\ dom(upper_sum_set(r(#)f)) holds r*
        (upper_bound rng f)*vol(A) <= (upper_sum_set(r(#)f)).D
        proof
          let D be set;
          assume D in divs A /\ dom(upper_sum_set(r(#)f));
          then D is Division of A by INTEGRA1:def 3;
          hence thesis by A1,A36,Th20;
        end;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
  then (upper_sum_set(r(#)f))|divs A is bounded_below by RFUNCT_1:88;
  then rng upper_sum_set(r(#)f) is bounded_below by INTEGRA1:13;
  then r(#)f is upper_integrable by INTEGRA1:def 13;
  hence thesis by A34,A11,INTEGRA1:def 17,def 18;
end;

begin :: Monotonicity of Integral

theorem Th32:
  f|A is bounded & (for x st x in A holds f.x >= 0) implies
  integral(f) >= 0
proof
  assume that
A1: f|A is bounded and
A2: for x st x in A holds f.x >= 0;
A3: for a be real number st a in rng upper_sum_set(f) holds a >= 0
  proof
    let a be real number;
    assume a in rng upper_sum_set(f);
    then consider D being Element of divs A such that
A4: D in dom upper_sum_set(f) & a=(upper_sum_set(f)).D by PARTFUN1:26;
    reconsider D as Division of A by INTEGRA1:def 3;
A5: for i be Nat st i in dom upper_volume(f,D) holds 0 <= upper_volume(f,D
    ). i
    proof
      let i be Nat;
      set r = upper_volume(f,D).i;
      assume
A6:   i in dom upper_volume(f,D);
      len D = len upper_volume(f,D) by INTEGRA1:def 7;
      then
A7:   i in dom D by A6,FINSEQ_3:31;
A8:   upper_bound (rng(f|divset(D,i))) >= 0
      proof
        rng f is bounded_above by A1,INTEGRA1:15;
        then
A9:     rng(f|divset(D,i)) is bounded_above by RELAT_1:99,XXREAL_2:43;
        dom(f|divset(D,i)) = dom f /\ divset(D,i) & dom f = A by FUNCT_2:def 1
,RELAT_1:90;
        then dom(f|divset(D,i)) is non empty Subset of REAL by A7,INTEGRA1:10
,XBOOLE_1:28;
        then consider x such that
A10:    x in dom(f|divset(D,i)) by SUBSET_1:10;
        f.x >= 0 by A2,A10;
        then
A11:    (f|divset(D,i)).x >= 0 by A10,FUNCT_1:70;
        (f|divset(D,i)).x in rng(f|divset(D,i)) by A10,FUNCT_1:def 5;
        hence thesis by A9,A11,SEQ_4:def 4;
      end;
A12:  vol(divset(D,i)) >= 0 by INTEGRA1:11;
      r = (upper_bound (rng (f|divset(D,i))))*vol(divset(D,i)) by A7,
INTEGRA1:def 7;
      hence thesis by A12,A8;
    end;
    a = upper_sum(f,D) by A4,INTEGRA1:def 11
      .= Sum(upper_volume(f,D)) by INTEGRA1:def 9;
    hence thesis by A5,RVSUM_1:114;
  end;
  dom upper_sum_set(f) is non empty by INTEGRA1:def 11;
  then
  upper_integral(f) = lower_bound rng upper_sum_set(f) & rng upper_sum_set
  (f) is non empty by INTEGRA1:def 15,RELAT_1:65;
  then upper_integral(f) >= 0 by A3,SEQ_4:60;
  hence thesis by INTEGRA1:def 18;
end;

theorem Th33:
  f|A is bounded & f is integrable & g|A is bounded & g is
integrable implies f-g is integrable & integral(f-g) = integral(f)-integral(g)
proof
  assume that
A1: f|A is bounded & f is integrable and
A2: g|A is bounded and
A3: g is integrable;
  -g = (-1)(#)g;
  then
A4: -g is integrable by A2,A3,Th31;
A5: (-g)|A is bounded by A2,RFUNCT_1:99;
  hence f-g is integrable by A1,A4,INTEGRA1:59;
  integral(-g) = (-1)*integral(g) by A2,A3,Th31;
  then integral(f-g)=integral(f)+-integral(g) by A1,A5,A4,INTEGRA1:59;
  hence thesis;
end;

theorem
  f|A is bounded & f is integrable & g|A is bounded & g is integrable &
  (for x st x in A holds f.x >= g.x) implies integral(f) >= integral(g)
proof
  assume that
A1: f|A is bounded & f is integrable & g|A is bounded & g is integrable and
A2: for x st x in A holds f.x >= g.x;
A3: dom (f-g)= A by FUNCT_2:def 1;
A4: for x st x in A holds (f-g).x >= 0
  proof
    let x;
    assume
A5: x in A;
    then (f-g).x = f.x - g.x by A3,VALUED_1:13;
    hence thesis by A2,A5,XREAL_1:50;
  end;
  integral(f-g)=integral(f)-integral(g) & (f-g)|(A /\ A) is bounded by A1,Th33,
RFUNCT_1:101;
  hence thesis by A4,Th32,XREAL_1:51;
end;

begin :: Definition of Division Sequence

theorem
  f|A is bounded implies rng upper_sum_set(f) is bounded_below
proof
  set D1 = the Division of A;
  assume
A1: f|A is bounded;
 take lower_sum(f,D1);
    let a be ext-real number;
    assume a in rng upper_sum_set(f);
    then consider D be Element of divs A such that
A2: D in dom upper_sum_set(f) & a = (upper_sum_set(f)).D by PARTFUN1:26;
    reconsider D as Division of A by INTEGRA1:def 3;
    a = upper_sum(f,D) by A2,INTEGRA1:def 11;
    hence thesis by A1,INTEGRA1:50;
end;

theorem
  f|A is bounded implies rng lower_sum_set(f) is bounded_above
proof
  set D1 = the Division of A;
  assume
A1: f|A is bounded;
  take upper_sum(f,D1);
    let a be ext-real number;
    assume a in rng lower_sum_set(f);
    then consider D be Element of divs A such that
A2: D in dom lower_sum_set(f) & a = (lower_sum_set(f)).D by PARTFUN1:26;
    reconsider D as Division of A by INTEGRA1:def 3;
    a = lower_sum(f,D) by A2,INTEGRA1:def 12;
    hence thesis by A1,INTEGRA1:50;
end;

definition
  let A be closed-interval Subset of REAL;
  mode DivSequence of A is Function of NAT,divs A;
end;

definition
  let A;
  let T be DivSequence of A;
  let i;
  redefine func T.i -> Division of A;
  coherence
  proof
    T.i is Element of divs A;
    hence thesis by INTEGRA1:def 3;
  end;
  canceled;
end;

definition
  let A be closed-interval Subset of REAL, f be PartFunc of A,REAL,
      T be DivSequence of A;
  func upper_sum(f,T) -> Real_Sequence means
  for i holds it.i = upper_sum(f,T.i);
  existence
  proof
    deffunc F(Element of NAT)=upper_sum(f,T.$1);
    thus ex IT being Real_Sequence st for i holds IT.i = F(i) from SEQ_1:sch 1;
  end;
  uniqueness
  proof
    let F1,F2 be Real_Sequence such that
A1: for i holds F1.i = upper_sum(f,T.i) and
A2: for i holds F2.i = upper_sum(f,T.i);
    for i holds F1.i = F2.i
    proof
      let i;
      F1.i = upper_sum(f,T.i) by A1
        .= F2.i by A2;
      hence thesis;
    end;
    hence thesis by FUNCT_2:113;
  end;
  func lower_sum(f,T) -> Real_Sequence means
  for i holds it.i = lower_sum(f,T.
  i);
  existence
  proof
    deffunc F(Element of NAT)=lower_sum(f,T.$1);
    thus ex IT being Real_Sequence st for i holds IT.i = F(i) from SEQ_1:sch 1;
  end;
  uniqueness
  proof
    let F1,F2 be Real_Sequence such that
A3: for i holds F1.i = lower_sum(f,T.i) and
A4: for i holds F2.i = lower_sum(f,T.i);
    for i holds F1.i = F2.i
    proof
      let i;
      F1.i = lower_sum(f,T.i) by A3
        .= F2.i by A4;
      hence thesis;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

theorem
  D1 <= D2 implies for j st j in dom D2 holds ex i st i in dom D1
  & divset(D2,j) c= divset(D1,i)
proof
  assume
A1: D1 <= D2;
  let j;
  defpred P[set] means D2.$1 in rng D1 & D2.$1 >= D2.j;
  consider X being Subset of dom D2 such that
A2: for x1 holds x1 in X iff x1 in dom D2 & P[x1] from SUBSET_1:sch 1;
  reconsider X as Subset of NAT by XBOOLE_1:1;
  assume
A3: j in dom D2;
  ex n st n in dom D2 & D2.n in rng D1 & D2.n >= D2.j
  proof
    take len D2;
    len D2 in Seg len D2 by FINSEQ_1:5;
    then
A4: len D2 in dom D2 by FINSEQ_1:def 3;
    len D1 in Seg len D1 by FINSEQ_1:5;
    then
A5: len D1 in dom D1 by FINSEQ_1:def 3;
    D2.(len D2) = upper_bound A by INTEGRA1:def 2;
    then
A6: D1.(len D1) = D2.(len D2) by INTEGRA1:def 2;
    j in Seg len D2 by A3,FINSEQ_1:def 3;
    then j <= len D2 by FINSEQ_1:3;
    hence thesis by A3,A5,A6,A4,FUNCT_1:def 5,SEQ_4:154;
  end;
  then reconsider X as non empty Subset of NAT by A2;
A7: min X in X by XXREAL_2:def 7;
  then
A8: D2.(min X) >= D2.j by A2;
  D2.(min X) in rng D1 by A2,A7;
  then consider i such that
A9: i in dom D1 and
A10: D2.(min X)=D1.i by PARTFUN1:26;
  take i;
A11: D1.i >= D2.j by A2,A7,A10;
  divset(D2,j) c= divset(D1,i)
  proof
    now
      per cases;
      suppose
        i=1;
        then
        lower_bound divset(D1,i) = lower_bound A & upper_bound divset(D1,
        i) = D1.i by A9,INTEGRA1:def 5;
        then
A12:    divset(D1,i) = [.lower_bound A,D1.i .] by INTEGRA1:5;
        now
          per cases;
          suppose
A13:        j=1;
            D1.i in rng D1 & rng D1 c= A by A9,FUNCT_1:def 5,INTEGRA1:def 2;
            then
A14:        D1.i in A;
            A = [.lower_bound A,upper_bound A.] by INTEGRA1:5;
            then D1.i in {r:lower_bound A<=r & r<=upper_bound A} by A14,
RCOMP_1:def 1;
            then ex x st x=D1.i & lower_bound A<=x & x<=upper_bound A;
            then lower_bound A in {r:lower_bound A <= r & r <= D1.i};
            then
A15:        lower_bound A in [.lower_bound A,D1.i .] by RCOMP_1:def 1;
            D2.j in rng D2 & rng D2 c= A by A3,FUNCT_1:def 5,INTEGRA1:def 2;
            then
A16:        D2.j in A;
            A = [.lower_bound A,upper_bound A.] by INTEGRA1:5;
            then D2.j in {r:lower_bound A<=r & r<=upper_bound A} by A16,
RCOMP_1:def 1;
            then ex x st x=D2.j & lower_bound A<=x & x<=upper_bound A;
            then D2.j in {r:lower_bound A <= r & r <= D1.i} by A8,A10;
            then
A17:        D2.j in [.lower_bound A,D1.i .] by RCOMP_1:def 1;
            lower_bound divset(D2,j) = lower_bound A & upper_bound divset
            (D2,j) = D2.j by A3,A13,INTEGRA1:def 5;
            then divset(D2,j) = [.lower_bound A,D2.j .] by INTEGRA1:5;
            hence thesis by A12,A15,A17,XXREAL_2:def 12;
          end;
          suppose
A18:        j<>1;
            A = [.lower_bound A,upper_bound A.] by INTEGRA1:5;
            then
A19:        A = {r:lower_bound A<=r & r<=upper_bound A} by RCOMP_1:def 1;
            D2.j in rng D2 & rng D2 c= A by A3,FUNCT_1:def 5,INTEGRA1:def 2;
            then D2.j in {r:lower_bound A<=r & r<=upper_bound A} by A19;
            then ex x st x=D2.j & lower_bound A <= x & x <= upper_bound A;
            then D2.j in {r:lower_bound A <= r & r <= D1.i} by A8,A10;
            then
A20:        D2.j in [.lower_bound A,D1.i .] by RCOMP_1:def 1;
            A = [.lower_bound A,upper_bound A.] by INTEGRA1:5;
            then
A21:        A = {r:lower_bound A<=r & r<=upper_bound A} by RCOMP_1:def 1;
A22:        rng D2 c= A by INTEGRA1:def 2;
A23:        j-1 in dom D2 by A3,A18,INTEGRA1:9;
            then D2.(j-1) in rng D2 by FUNCT_1:def 5;
            then D2.(j-1) in {r:lower_bound A<=r & r<=upper_bound A} by A21,A22
;
            then
A24:        ex x st x=D2.(j-1) & lower_bound A <= x & x <= upper_bound A;
            j <= j+1 by NAT_1:11;
            then j-1 <= j by XREAL_1:22;
            then D2.(j-1) <= D2.j by A3,A23,SEQ_4:154;
            then D2.(j-1) <= D1.i by A8,A10,XXREAL_0:2;
            then D2.(j-1) in {r:lower_bound A <= r & r <= D1.i} by A24;
            then
A25:        D2.(j-1) in [.lower_bound A,D1.i .] by RCOMP_1:def 1;
            lower_bound divset(D2,j) = D2.(j-1) & upper_bound divset(D2,j
            ) = D2.j by A3,A18,INTEGRA1:def 5;
            then divset(D2,j) = [. D2.(j-1),D2.j .] by INTEGRA1:5;
            hence thesis by A12,A25,A20,XXREAL_2:def 12;
          end;
        end;
        hence thesis;
      end;
      suppose
A26:    i<>1;
A27:    j <> 1
        proof
          assume
A28:      j=1;
A29:      i in Seg len D1 by A9,FINSEQ_1:def 3;
          then
A30:      1 <= i by FINSEQ_1:3;
          i <= len D1 by A29,FINSEQ_1:3;
          then 1 <= len D1 by A30,XXREAL_0:2;
          then 1 in Seg len D1 by FINSEQ_1:3;
          then
A31:      1 in dom D1 by FINSEQ_1:def 3;
          then
A32:      D1.1 in rng D1 by FUNCT_1:def 5;
          rng D1 c= rng D2 by A1,INTEGRA1:def 20;
          then consider n such that
A33:      n in dom D2 and
A34:      D1.1 = D2.n by A32,PARTFUN1:26;
A35:      n in Seg len D2 by A33,FINSEQ_1:def 3;
          then
A36:      1 <= n by FINSEQ_1:3;
          n <= len D2 by A35,FINSEQ_1:3;
          then 1 <= len D2 by A36,XXREAL_0:2;
          then 1 in Seg len D2 by FINSEQ_1:3;
          then 1 in dom D2 by FINSEQ_1:def 3;
          then D2.j <= D2.n by A28,A33,A36,SEQ_4:154;
          then n in X by A2,A32,A33,A34;
          then n >= min X by XXREAL_2:def 7;
          then
A37:      D1.1 >= D1.i by A7,A10,A33,A34,SEQ_4:154;
          i > 1 by A26,A30,XXREAL_0:1;
          hence contradiction by A9,A31,A37,SEQM_3:def 1;
        end;
        then
A38:    j-1 in dom D2 by A3,INTEGRA1:9;
A39:    D1.(i-1) <= D2.(j-1)
        proof
A40:      i-1 in dom D1 by A9,A26,INTEGRA1:9;
          then
A41:      D1.(i-1) in rng D1 by FUNCT_1:def 5;
          rng D1 c= rng D2 by A1,INTEGRA1:def 20;
          then consider n such that
A42:      n in dom D2 & D1.(i-1) = D2.n by A41,PARTFUN1:26;
          assume D1.(i-1) > D2.(j-1);
          then n > j-1 by A38,A42,SEQ_4:154;
          then n+1 > j by XREAL_1:21;
          then n >= j by NAT_1:13;
          then D1.(i-1) >= D2.j by A3,A42,SEQ_4:154;
          then n in X by A2,A41,A42;
          then n >= min X by XXREAL_2:def 7;
          then D1.(i-1) >= D1.i by A7,A10,A42,SEQ_4:154;
          then i-1 >= i by A9,A40,SEQM_3:def 1;
          then
A43:      i >= i+1 by XREAL_1:21;
          i <= i+1 by NAT_1:11;
          then i = i+1 by A43,XXREAL_0:1;
          hence contradiction;
        end;
        j <= j+1 by NAT_1:11;
        then j-1 <= j by XREAL_1:22;
        then
A44:    D2.(j-1) <= D2.j by A3,A38,SEQ_4:154;
        then
A45:    D1.(i-1) <= D2.j by A39,XXREAL_0:2;
        D2.(j-1) <= D1.i by A11,A44,XXREAL_0:2;
        then D2.(j-1) in {r:D1.(i-1)<=r & r<=D1.i} by A39;
        then
A46:    D2.(j-1) in [. D1.(i-1),D1.i .] by RCOMP_1:def 1;
        D2.j <= D1.i by A2,A7,A10;
        then D2.j in {r:D1.(i-1) <= r & r <= D1.i} by A45;
        then
A47:    D2.j in [. D1.(i-1),D1.i .] by RCOMP_1:def 1;
        lower_bound divset(D2,j) = D2.(j-1) & upper_bound divset(D2,j) =
        D2.j by A3,A27,INTEGRA1:def 5;
        then
A48:    divset(D2,j) = [. D2.(j-1),D2.j .] by INTEGRA1:5;
        lower_bound divset(D1,i) = D1.(i-1) & upper_bound divset(D1,i) =
        D1.i by A9,A26,INTEGRA1:def 5;
        then divset(D1,i) = [. D1.(i-1),D1.i .] by INTEGRA1:5;
        hence thesis by A48,A46,A47,XXREAL_2:def 12;
      end;
    end;
    hence thesis;
  end;
  hence thesis by A9;
end;

canceled 2;

theorem
  A c= B implies vol(A) <= vol(B)
proof
  assume
A1: A c= B;
  vol(A) = upper_bound A - lower_bound A by INTEGRA1:def 6;
  then upper_bound B >= vol(A) + lower_bound A by A1,SEQ_4:65;
  then
A2: upper_bound B - vol(A) >= lower_bound A by XREAL_1:21;
  lower_bound A >= lower_bound B by A1,SEQ_4:64;
  then upper_bound B - vol(A) >= lower_bound B by A2,XXREAL_0:2;
  then upper_bound B - lower_bound B >= vol(A) by XREAL_1:13;
  hence thesis by INTEGRA1:def 6;
end;

theorem
  for A being Subset of REAL,
      a,x being Real st x in a ** A holds
   ex b being Real st b in A & x = a * b
proof
  let A be Subset of REAL,
      a,x be Real;
  assume x in a ** A; then
  x in {a*b where b is Element of ExtREAL : b in A} by MEMBER_1:187; then
  consider b being Element of ExtREAL such that
A1:  x = a*b & b in A;
  reconsider b as Real by A1;
  take b;
  x = a*b by A1,XXREAL_3:def 5;
  hence thesis by A1;
end;

begin :: Addenda
:: missing, 2008.06.10

theorem
  for A being non empty ext-real-membered set holds 0 ** A = {0}
proof
  let A be non empty ext-real-membered set;
  for e being set holds e in 0**A iff e = 0
  proof
    let e be set;
    consider r being ext-real number such that
A1: r in A by MEMBERED:8;
    hereby
      assume e in 0**A;
      then ex z being Element of ExtREAL st e = 0 * z & z in A
        by MEMBER_1:188;
      hence e = 0;
    end;
    assume e = 0;
    then e = 0*r;
    hence thesis by A1,MEMBER_1:186;
  end;
  hence thesis by TARSKI:def 1;
end;
