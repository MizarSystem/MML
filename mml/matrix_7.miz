:: Determinant of Some Matrices of Field Elements
::  by Yatsuka Nakamura
::
:: Received January 4, 2006
:: Copyright (c) 2006 Association of Mizar Users

environ

 vocabularies MATRIX_7, FINSEQ_1, FUNCT_1, ARYTM_1, VECTSP_1, RLVECT_1,
      MATRIX_1, TREES_1, GROUP_1, MATRIX_3, RELAT_1, BOOLE, FUNCT_2, MATRIX_2,
      FINSEQ_2, SGRAPH1, BINOP_1, SETWISEO, FINSOP_1, FINSUB_1, NAT_1, CARD_3,
      FINSET_1, JORDAN3, FUNCOP_1, CQC_LANG, FINSEQ_4, QC_LANG1, RFINSEQ,
      SQUARE_1, CARD_1, FINSEQ_5, REALSET1, ROUGHS_1, ARYTM;
 notations TARSKI, XBOOLE_0, SUBSET_1, XCMPLX_0, XXREAL_0, NAT_1, NAT_D,
      RELAT_1, FUNCT_1, BINOP_1, ZFMISC_1, RLVECT_1, CARD_1, NUMBERS, VECTSP_1,
      FUNCT_2, FUNCOP_1, SETWISEO, SEQ_1, FINSEQ_4, PARTFUN1, GROUP_4,
      FINSUB_1, NEWTON, FINSOP_1, SETWOP_2, FINSEQ_1, STRUCT_0, MATRIX_2,
      MATRIX_3, FINSET_1, JORDAN3, BINARITH, MATRIX_1, RFINSEQ,
      FINSEQ_5, GROUP_1;
 constructors RELAT_2, PARTFUN1, SETWISEO, REAL_1, SQUARE_1, NAT_1, NAT_D,
      FINSEQ_4, FINSOP_1, NEWTON, RFINSEQ, BINARITH, FINSEQ_5, ALGSTR_1,
      GROUP_4, MATRIX_3, JORDAN3;
 registrations XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, ORDINAL1, PARTFUN1,
      FUNCT_2, FINSUB_1, XXREAL_0, XREAL_0, NAT_1, FINSEQ_1, STRUCT_0, GROUP_1,
      VECTSP_1, MATRIX_2, FVSUM_1;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM, REAL;
 definitions TARSKI, XBOOLE_0, BINOP_1, RLVECT_1, FINSEQ_1, MATRIX_1, FINSEQ_2,
      GROUP_4;
 theorems BINOP_1, RLVECT_1, MATRIX_3, VECTSP_1, MATRIX_1, GROUP_1, FUNCT_1,
      FINSEQ_1, NAT_1, MATRIX_2, FINSEQ_2, TARSKI, FINSOP_1, FVSUM_1, SETWISEO,
      FUNCT_2, XBOOLE_0, GROUP_4, ENUMSET1, ZFMISC_1, BINARITH, JORDAN3,
      REAL_1, XREAL_1, FINSEQ_5, INTEGRA2, FUNCOP_1, FINSUB_1, XBOOLE_1,
      RELAT_1, SUBSET_1, RFINSEQ, FINSEQ_3, GROUP_7, XXREAL_0, ORDINAL1, NAT_D,
      PARTFUN1;
 schemes NAT_1, FUNCT_2, FINSEQ_1, FINSEQ_2;

begin

 reserve k,n,i,j for Nat;

theorem Th1:
 for f being Permutation of Seg 2 holds f=<*1,2*> or f=<*2,1*>
 proof let f be Permutation of Seg 2;
   A1:f is Function of Seg 2,Seg 2 & rng f = Seg 2 by FUNCT_2:def 3;
   A2:dom f=Seg 2 by FUNCT_2:67;
   then reconsider p=f as FinSequence by FINSEQ_1:def 2;
    1 in {1,2} by TARSKI:def 2;
   then A3: f.1 in Seg 2 by A1,FINSEQ_1:4,FUNCT_2:6;
   A4: 2 in {1,2} by TARSKI:def 2;
   A5: len p= 2 by A2,FINSEQ_1:def 3;
   A6: 1 in dom f by A2;
   A7: 2 in dom f by A2;
   A8: f.2 in Seg 2 by A1,A4,FINSEQ_1:4,FUNCT_2:6;
    now per cases by A3,A8,FINSEQ_1:4,TARSKI:def 2;
   case f.1=1 & f.2=1;
    hence contradiction by A6,A7,FUNCT_1:def 8;
   end;
   case f.1=1 & f.2=2;
    hence f=<*1,2*> or f=<*2,1*> by A5,FINSEQ_1:61;
   end;
   case f.1=2 & f.2=1;
    hence f=<*1,2*> or f=<*2,1*> by A5,FINSEQ_1:61;
   end;
   case f.1=2 & f.2=2;
    hence contradiction by A6,A7,FUNCT_1:def 8;
   end;
   end;
  hence f=<*1,2*> or f=<*2,1*>;
 end;

theorem Th2:
 for f being FinSequence st f=<*1,2*> or f=<*2,1*> holds
   f is Permutation of Seg 2
 proof let f be FinSequence;
  assume A1: f=<*1,2*> or f=<*2,1*>;
   A2: len (<*1,2*>)=2 by FINSEQ_1:61;
    len (<*2,1*>)=2 by FINSEQ_1:61;
   then A3: dom f =Seg 2 by A1,A2,FINSEQ_1:def 3;
    rng f c= Seg 2
    proof let y be set;assume y in rng f;
      then consider x being set such that
      A4: x in dom f & y=f.x by FUNCT_1:def 5;
       y=f.1 or y=f.2 by A3,A4,FINSEQ_1:4,TARSKI:def 2;
      then y=1 or y=2 or (y=2 or y=1) by A1,FINSEQ_1:61;
     hence y in Seg 2;
    end;
   then reconsider g=f as Function of Seg 2,Seg 2 by A3,FUNCT_2:4;
    now per cases by A1;
   case A5: f=<*1,2*>;
    for x1,x2 being set st x1 in dom f & x2 in dom f & f.x1=f.x2
           holds x1=x2
    proof let x1,x2 be set;
      assume A6: x1 in dom f & x2 in dom f & f.x1=f.x2;
       now per cases by A3,A6,FINSEQ_1:4,TARSKI:def 2;
      case x1=1 & x2=1;
       hence x1=x2;
      end;
      case A7: x1=1 & x2=2;
       then f.x1=1 by A5,FINSEQ_1:61;
       hence contradiction by A5,A6,A7,FINSEQ_1:61;
      end;
      case A8: x1=2 & x2=1;
       then f.x1=2 by A5,FINSEQ_1:61;
       hence contradiction by A5,A6,A8,FINSEQ_1:61;
      end;
      case x1=2 & x2=2;
       hence x1=x2;
      end;
      end;
     hence x1=x2;
    end;
    then A9: f is one-to-one by FUNCT_1:def 8;
    A10: rng f = Seg 2
     proof
        now let x be set;
       assume x in rng f;
         then consider z being set such that
         A11: z in dom f & f.z=x by FUNCT_1:def 5;
          len f=2 by A5,FINSEQ_1:61;
         then dom f=Seg 2 by FINSEQ_1:def 3;
         then z=1 or z=2 by A11,FINSEQ_1:4,TARSKI:def 2;
         then x =1 or x=2 by A5,A11,FINSEQ_1:61;
        hence x in Seg 2;
       end;
       then A12: rng f c= Seg 2 by TARSKI:def 3;
         Seg 2 c= rng f
        proof let z be set;assume z in Seg 2;
          then z=1 or z=2 by FINSEQ_1:4,TARSKI:def 2;
          then A13: z=f.1 or z=f.2 by A5,FINSEQ_1:61;
          A14: 1 in dom f by A3;
           2 in dom f by A3;
         hence z in rng f by A13,A14,FUNCT_1:def 5;
        end;
      hence thesis by A12,XBOOLE_0:def 10;
     end;
      then g is onto by FUNCT_2:def 3;
     hence f is Permutation of Seg 2 by A9,A10,FUNCT_2:83;
    end;
   case A15: f=<*2,1*>;
     for x1,x2 being set st x1 in dom f & x2 in dom f & f.x1=f.x2
           holds x1=x2
    proof let x1,x2 be set;
      assume A16: x1 in dom f & x2 in dom f & f.x1=f.x2;
       now per cases by A3,A16,FINSEQ_1:4,TARSKI:def 2;
      case x1=1 & x2=1;
       hence x1=x2;
      end;
      case A17: x1=1 & x2=2;
       then f.x1=2 by A15,FINSEQ_1:61;
       hence contradiction by A15,A16,A17,FINSEQ_1:61;
      end;
      case A18: x1=2 & x2=1;
       then f.x1=1 by A15,FINSEQ_1:61;
       hence contradiction by A15,A16,A18,FINSEQ_1:61;
      end;
      case x1=2 & x2=2;
       hence x1=x2;
      end;
      end;
     hence x1=x2;
    end;
    then A19: f is one-to-one by FUNCT_1:def 8;
    A20: rng f = Seg 2
     proof
        now let x be set;
       assume x in rng f;
         then consider z being set such that
         A21: z in dom f & f.z=x by FUNCT_1:def 5;
          len f=2 by A15,FINSEQ_1:61;
         then dom f=Seg 2 by FINSEQ_1:def 3;
         then z=1 or z=2 by A21,FINSEQ_1:4,TARSKI:def 2;
         then x =2 or x=1 by A15,A21,FINSEQ_1:61;
        hence x in Seg 2;
       end;
       then A22: rng f c= Seg 2 by TARSKI:def 3;
         Seg 2 c= rng f
        proof let z be set;assume z in Seg 2;
          then z=1 or z=2 by FINSEQ_1:4,TARSKI:def 2;
          then A23: z=f.2 or z=f.1 by A15,FINSEQ_1:61;
          A24: 2 in dom f by A3;
           1 in dom f by A3;
         hence z in rng f by A23,A24,FUNCT_1:def 5;
        end;
      hence thesis by A22,XBOOLE_0:def 10;
     end;
     then g is onto by FUNCT_2:def 3;
    hence f is Permutation of Seg 2 by A19,A20,FUNCT_2:83;
   end;
   end;
  hence f is Permutation of Seg 2;
 end;

theorem Th3:
  Permutations(2)={<*1,2*>,<*2,1*>}
   proof
     now let p be set;
     assume p in Permutations(2);
      then reconsider q=p as Permutation of Seg 2 by MATRIX_2:def 11;
       q=<*1,2*> or q=<*2,1*> by Th1;
     hence p in {idseq 2,<*2,1*>} by FINSEQ_2:61,TARSKI:def 2;
    end;
    then A1: Permutations(2) c={idseq 2,<*2,1*>} by TARSKI:def 3;
       now let x be set;
     assume A2: x in {idseq 2,<*2,1*>};
      now per cases by A2,TARSKI:def 2;
     case x=idseq 2;
      hence x in Permutations(2) by MATRIX_2:def 11;
     end;
     case A3: x=<*2,1*>;
        <*2,1*> is Permutation of Seg 2 by Th2;
      hence x in Permutations(2) by A3,MATRIX_2:def 11;
     end;
     end;
     hence x in Permutations(2);
    end;
    then {idseq 2,<*2,1*>} c=Permutations(2) by TARSKI:def 3;
    hence thesis by A1,FINSEQ_2:61,XBOOLE_0:def 10;
   end;

theorem Th4:
  for p being Permutation of Seg 2 st p is_transposition holds p = <*2,1*>
proof let p be Permutation of Seg 2;
  assume A1: p is_transposition;
   now assume A2: p=<*1,2*>;
   set p0=<*1,2*>;
   A3: len p0=2 by FINSEQ_1:61;
   consider i,j being Nat such that
    A4: i in dom p & j in dom p & i<>j & p.i=j & p.j=i &
     for k being Nat st k <>i & k<>j & k in dom p holds p.k=k
      by A1,MATRIX_2:def 14;
    A5: dom p ={1,2} by A2,A3,FINSEQ_1:4,def 3;
    then A6: i=1 or i=2 by A4,TARSKI:def 2;
     now per cases by A4,A5,A6,TARSKI:def 2;
    case i=1 & j=2;
     hence contradiction by A2,A4,FINSEQ_1:61;
    end;
    case i=2 & j=1;
     hence contradiction by A2,A4,FINSEQ_1:61;
    end;
    end;
  hence contradiction;
  end;
 hence p = <*2,1*> by Th1;
end;

theorem Th5:
  for D being non empty set, f being FinSequence of D, k2 being Element of NAT
  st
    1<=k2 & k2< len f holds
     f = mid(f,1,k2)^mid(f,k2+1,len f)
proof let D be non empty set, f be FinSequence of D, k2 be Element of NAT;
  assume A1: 1<=k2 & k2< len f;
then A2: mid(f,1,len f) = mid(f,1,k2)^mid(f,k2+1,len f) by INTEGRA2:4;
   1< len f by A1,XXREAL_0:2;
  hence f = mid(f,1,k2)^mid(f,k2+1,len f) by A2,JORDAN3:29;
end;

theorem Th6:
  for D being non empty set, f being FinSequence of D st 2<= len f holds
    f = (f|(len f-'2))^mid(f,len f-'1,len f)
proof let D be non empty set, f be FinSequence of D;
  assume A1: 2<= len f;
then A2: len f-'2=len f-2 by BINARITH:50;
A3: 1<=len f by A1,XXREAL_0:2;
A4: len f-'2+1=len f-1-1+1 by A2
             .=len f-'1 by A3,BINARITH:50;
A5: 1<= len f by A1,XXREAL_0:2;
    now per cases;
   case len f-'2>0;
     then A6: 0+1<=len f-'2 by NAT_1:13;
      len f<len f+1 by NAT_1:13;
     then len f<len f+1+1 by NAT_1:13;
     then len f -2<len f+2-2 by REAL_1:92;
     then f=mid(f,1,len f-'2)^mid(f,len f-'2+1,len f) by A2,A6,Th5;
    hence f = (f|(len f-'2))^mid(f,len f-'1,len f) by A4,A6,JORDAN3:25;
   end;
   case A7: len f-'2=0;
A8:  len f-'2+1=(len f)-2+1 by A1,BINARITH:50
                  .=len f-1
                  .=len f-'1 by A5,BINARITH:50;
     A9: mid(f,len f-'2+1,len f)=f by A5,A7,JORDAN3:29;
      f|0 is empty;
    hence f= (f|(len f-'2))^mid(f,len f-'1,len f) by A7,A8,A9,FINSEQ_1:47;
   end;
   end;
 hence f = (f|(len f-'2))^mid(f,len f-'1,len f);
end;

theorem Th7:
for D being non empty set, f being FinSequence of D st 1<= len f holds
     f = (f|(len f-'1))^mid(f,len f,len f)
proof let D be non empty set, f be FinSequence of D;
 assume A1: 1<= len f;
then A2: len f-'1=len f-1 by BINARITH:50;
    now per cases;
   case len f-'1>0;
     then A3: 0+1<=len f-'1 by NAT_1:13;
      len f<len f+1 by NAT_1:13;
     then len f -1<len f+1-1 by REAL_1:92;
     then f=mid(f,1,len f-'1)^mid(f,len f-'1+1,len f) by A2,A3,Th5;
    hence f = (f|(len f-'1))^mid(f,len f,len f) by A2,A3,JORDAN3:25;
   end;
   case A4: len f-'1=0;
     then A5: mid(f,len f-'1+1,len f)=f by A1,JORDAN3:29;
      f|0 is empty;
    hence f= (f|(len f-'1))^mid(f,len f,len f) by A2,A4,A5,FINSEQ_1:47;
   end;
   end;
 hence f = (f|(len f-'1))^mid(f,len f,len f);
end;

Lm1: <*1,2*> <> <*2,1*> by GROUP_7:2;

theorem Th8:
  for a being Element of Group_of_Perm 2 st
  (ex q being Element of Permutations 2 st q=a & q is_transposition)
   holds a = <*2,1*>
proof let a be Element of Group_of_Perm 2;
  given q being Element of Permutations 2 such that
  A1: q=a & q is_transposition;
   the carrier of Group_of_Perm 2 = Permutations 2 by MATRIX_2:def 13;
  then reconsider b=a as Permutation of Seg 2 by MATRIX_2:def 11;
  consider i,j being Nat such that
  A2: i in dom q & j in dom q & i<>j & q.i=j & q.j=i &
   for k being Nat st k <>i & k<>j & k in dom q holds q.k=k
    by A1,MATRIX_2:def 14;
   b is_transposition by A1,A2,MATRIX_2:def 14;
 hence a = <*2,1*> by Th4;
end;

theorem Th9:
  for n being Element of NAT,a,b being Element of Group_of_Perm n,
      pa,pb being Element of Permutations n st
        a=pa & b=pb holds a*b=pb*pa
 proof let n be Element of NAT,a,b be Element of Group_of_Perm n,
  pa,pb be Element of Permutations n;
  assume A1: a=pa & b=pb;
   a*b=(the mult of Group_of_Perm n).(a,b) by GROUP_1:def 1
     .=pb*pa by A1,MATRIX_2:def 13;
  hence a*b=pb*pa;
 end;

theorem Th10: for a,b being Element of Group_of_Perm 2 st
(ex p being Element of Permutations 2 st p=a & p is_transposition)&
(ex q being Element of Permutations 2 st q=b & q is_transposition)
 holds a*b = <*1,2*>
proof let a,b be Element of Group_of_Perm 2;
 assume A1: (ex p being Element of Permutations 2 st p=a &
 p is_transposition)&
 (ex q being Element of Permutations 2 st q=b & q is_transposition);
  then A2: a= <*2,1*> by Th8;
  A3: b= <*2,1*> by A1,Th8;
   the carrier of Group_of_Perm 2 =Permutations 2 by MATRIX_2:def 13;
  then A4: a*b = <*1,2*> or a*b = <*2,1*> by Th3,TARSKI:def 2;
  consider p being Element of Permutations 2 such that
  A5:  p=a & p is_transposition by A1;
  consider q being Element of Permutations 2 such that
  A6:  q=b & q is_transposition by A1;
  A7: a*b=q*p by A5,A6,Th9;
  reconsider q2=q as FinSequence by A6,Th8;
  A8: len q2=2 & q2.1=2 & q2.2=1 by A3,A6,FINSEQ_1:61;
  reconsider p2=p as FinSequence by A5,Th8;
  A9: len p2=2 & p2.1=2 & p2.2=1 by A2,A5,FINSEQ_1:61;
  then 1 in Seg len p2;
  then 1 in dom p2 by FINSEQ_1:def 3;
  then (q*p).1=1 by A8,A9,FUNCT_1:23;
 hence a*b = <*1,2*> by A4,A7,FINSEQ_1:61;
end;

theorem Th11: for l being FinSequence of Group_of_Perm 2
 st (len l) mod 2=0 & (for i st i in dom l holds
     (ex q being Element of Permutations 2 st l.i=q & q is_transposition))
holds Product l = <*1,2*>
proof let l be FinSequence of Group_of_Perm 2;
 assume A1: (len l)mod 2=0 & (for i st i in dom l holds
     (ex q being Element of Permutations 2
              st l.i=q & q is_transposition));
  then ( ex t being Nat st len l = 2 * t + 0 & 0 < 2 ) or 0 = 0 & 2 = 0
                               by NAT_D:def 2;
  then consider t being Nat such that
  A2: len l = 2 * t;
  reconsider t as Element of NAT by ORDINAL1:def 13;
  A3: len l = 2 * t by A2;
  defpred P[Element of NAT] means
   for f being FinSequence of Group_of_Perm 2 st len f=2*$1 &
    (for i st i in dom f holds
     (ex q being Element of Permutations 2 st f.i=q & q is_transposition))
     holds Product f = <*1,2*>;
   for f being FinSequence of Group_of_Perm 2 st
    len f=2*0 &
     (for i st i in dom f holds
     (ex q being Element of Permutations 2 st f.i=q & q is_transposition))
    holds Product f = <*1,2*>
   proof let f be FinSequence of Group_of_Perm 2;
    assume A4: len f=2*0 &
      (for i st i in dom f holds
     (ex q being Element of Permutations 2
              st f.i=q & q is_transposition));
     set G=Group_of_Perm 2;
     A5: f=<*> (the carrier of G) by A4,FINSEQ_1:32;
      1_G= <*1,2*> by FINSEQ_2:61,MATRIX_2:28;
    hence Product f = <*1,2*> by A5,GROUP_4:11;
   end;
  then A6: P[0];
  A7: for s being Element of NAT st P[s] holds P[s+1]
   proof let s be Element of NAT;
    assume A8: P[s];
     for f being FinSequence of Group_of_Perm 2 st
     len f=2*(s+1) & (for i st i in dom f holds
     (ex q being Element of Permutations 2 st f.i=q & q is_transposition))
      holds Product f = <*1,2*>
     proof let f be FinSequence of Group_of_Perm 2;
       assume A9: len f=2*(s+1)&
         (for i st i in dom f holds
         (ex q being Element of Permutations 2
              st f.i=q & q is_transposition));
        then A10: len f = 2*s+2;
        then A11: 2 <= len f by NAT_1:11;
        then A12: 1 <= len f by XXREAL_0:2;
        then A13: len f-'1=len f-1 by BINARITH:50;
        A14: 1+1-1<=len f-1 by A11,REAL_1:92;
        then A15: 1<=len f-'1 by A12,BINARITH:50;
        A16: f = (f|(len f-'2))^mid(f,len f-'1,len f) by A11,Th6;
        A17: len f-'2<=len f by BINARITH:52;
        then A18: len (f|(len f-'2))=len f-'2 by FINSEQ_1:80 .= 2*s
                                by A10,BINARITH:39;
         for i st i in dom (f|(len f-'2)) holds
           (ex q being Element of Permutations 2
              st (f|(len f-'2)).i=q & q is_transposition)
        proof let i;assume i in dom (f|(len f-'2));
          then i in Seg len (f|(len f-'2)) by FINSEQ_1:def 3;
          then A19: 1<= i & i<= len (f|(len f-'2)) by FINSEQ_1:3;
           len (f|(len f-'2)) <= len f by FINSEQ_5:18;
          then i<=len f by A19,XXREAL_0:2;
          then i in dom f by A19,FINSEQ_3:27;
          then consider q being Element of Permutations 2 such that
          A20: f.i=q & q is_transposition by A9;
           len (f|(len f-'2))=len f-'2 by A17,FINSEQ_1:80;
          then f.i=(f|(len f-'2)).i by A19,FINSEQ_3:121;
         hence (ex q being Element of Permutations 2
              st (f|(len f-'2)).i=q & q is_transposition) by A20;
        end;
        then Product (f|(len f-'2))=<*1,2*> by A8,A18;
        then A21: 1_Group_of_Perm 2=(Product (f|(len f-'2)))
                              by FINSEQ_2:61,MATRIX_2:28;
        A22: Product f
              = (Product (f|(len f-'2)))*(Product (mid(f,len f-'1,len f)))
                                          by A16,GROUP_4:8
             .= (Product (mid(f,len f-'1,len f))) by A21,GROUP_1:def 5;
        A23: len f-'1<= len f by BINARITH:52;
        then len f-'1 in Seg len f by A15;
        then A24: len f-'1 in dom f by FINSEQ_1:def 3;
         len f in Seg len f by A12;
        then A25: len f in dom f by FINSEQ_1:def 3;
        A26: len (mid(f,len f-'1,len f)) = len f-'(len f-'1)+1
                                       by A12,A15,A23,JORDAN3:27
                                  .=len f-(len f-'1)+1 by A23,BINARITH:50
                                  .=len f-(len f-1)+1 by A12,BINARITH:50
                                  .=2;
        set g=mid(f,len f-'1,len f);
        A27: g= (g|(len g-'1))^mid(g,len g,len g) by A26,Th7;
        A28: len mid(g,len g,len g)=len g-'len g+1 by A26,JORDAN3:27
                             .= 0+1 by BINARITH:51 .=1;
         len g-'1<= len g by BINARITH:52;
        then A29: len (g|(len g-'1)) = len g-'1 by FINSEQ_1:80
                               .= len g-1 by A26,BINARITH:50
                               .=1 by A26;
        A30: len f-(len f-'1)+1=len f-(len f-1)+1 by A12,BINARITH:50
                         .=2;
         1<=1+(len f-'1) by NAT_1:11;
        then A31: 1+(len f-'1)-'1=1+(len f-'1)-1 by BINARITH:50
                      .=len f-'1;
        A32: len f-(len f-'1)+1=len f-(len f-1)+1 by A12,BINARITH:50
                         .= 1+1;
         len g-'1=len g-1 by A26,BINARITH:50;
        then A33: (g|(len g-'1)).1=g.1 by A26,FINSEQ_3:121
                       .=f.(len f-'1) by A14,A23,A30,A31,JORDAN3:31;
        A34: ex q being Element of Permutations 2
              st f.(len f-'1)=q & q is_transposition by A9,A24;
         1<=1+len g by NAT_1:11;
        then A35: 1+ len g-'1=1+len g-1 by BINARITH:50;
         2<=2+(len f-'1) by NAT_1:11;
        then 1<=2+(len f-'1) by XXREAL_0:2;
        then A36: 2+(len f-'1)-'1=2+(len f-'1)-1 by BINARITH:50
                      .=len f by A13;
         1<=len g-len g+1;
        then A37: (mid(g,len g,len g)).1=g.(1+len g-'1) by A26,JORDAN3:31
                                  .=f.(len f) by A14,A23,A26,A32,A35,A36,
JORDAN3:31;
        A38: ex q being Element of Permutations 2
              st f.(len f)=q & q is_transposition by A9,A25;
        reconsider pw1=Product (g|(len g-'1))
         as Element of Group_of_Perm 2;
        reconsider pw2=Product mid(g,len g,len g)
         as Element of Group_of_Perm 2;
         A39: rng (g|(len g-'1))
           c= the carrier of Group_of_Perm 2 by FINSEQ_1:def 4;
           1 in Seg len (g|(len g-'1)) by A29;
          then 1 in dom (g|(len g-'1)) by FINSEQ_1:def 3;
          then (g|(len g-'1)).1 in rng (g|(len g-'1)) by FUNCT_1:def 5;
         then reconsider r=(g|(len g-'1)).1 as Element of
                     Group_of_Perm 2 by A39;
         A40: rng (mid(g,len g,len g))
           c= the carrier of Group_of_Perm 2 by FINSEQ_1:def 4;
         1 in Seg len (mid(g,len g,len g)) by A28;
          then 1 in dom (mid(g,len g,len g)) by FINSEQ_1:def 3;
          then (mid(g,len g,len g)).1 in rng (mid(g,len g,len g))
                             by FUNCT_1:def 5;
         then reconsider s=(mid(g,len g,len g)).1 as Element of
                     Group_of_Perm 2 by A40;
        A41: pw1 = Product (<* r *>) by A29,FINSEQ_1:57
          .=f.(len f-'1) by A33,FINSOP_1:12;
        A42: pw2 = Product (<* s *>) by A28,FINSEQ_1:57
          .=f.len f by A37,FINSOP_1:12;
         Product g=(Product ((g|(len g-'1))))*(Product mid(g,len g,len g))
                                  by A27,GROUP_4:8
                .= <*1,2*> by A34,A38,A41,A42,Th10;
       hence Product f = <*1,2*> by A22;
     end;
    hence P[s+1];
   end;
   for s being Element of NAT holds P[s] from NAT_1:sch 1(A6,A7);
 hence Product l = <*1,2*> by A1,A3;
end;

theorem  for K being Field, M being Matrix of 2,K holds
    Det M = (M*(1,1))*(M*(2,2))-(M*(1,2))*(M*(2,1))
proof let K be Field, M be Matrix of 2,K;
    1 in Seg 2;
   then reconsider D2=Seg 2 as non empty set;
   reconsider s1=<*1,2*>, s2=<*2,1*> as Permutation of Seg 2 by Th2;
   A1: now assume A2: s1=s2;
    s1.1=1 by FINSEQ_1:61;
   hence contradiction by A2,FINSEQ_1:61;
   end;
   reconsider p1 = s1, p2 = s2 as Element of Permutations(2)
     by MATRIX_2:def 11;
   A3:len Path_matrix(p1,M) = 2 by MATRIX_3:def 7;
   A4:len Path_matrix(p2,M) = 2 by MATRIX_3:def 7;
   A5: Det M=(the add of K) $$ (FinOmega Permutations 2,Path_product M)
         by MATRIX_3:def 9;
   set X=Permutations 2;
   set Y=the carrier of K;
   set B=FinOmega Permutations 2;
   set f=Path_product M;
   set F=the add of K;
    Permutations 2 is finite by MATRIX_2:30;
   then A6: B=Permutations 2 by MATRIX_2:def 17;
   then consider G being Function of Fin X, Y such that
     A7: Det M = G.B &
          (for e being Element of Y st e is_a_unity_wrt F holds G.{} = e) &
          (for x being Element of X holds G.{x} = f.x) &
           for B' being Element of Fin X st B' c= B & B' <> {}
           for x being Element of X st x in B \ B'
            holds G.(B' \/ {x}) = F.(G.B',f.x) by A5,SETWISEO:def 3;
   A8: G.{p1} = f.p1 by A7;
   A9: G.B = (the add of K).(f.p1,f.p2)
     proof
       reconsider B'={.p1.} as Element of Fin X;
       A10: B' c= B
        proof let y be set;assume y in B';
          then y=p1 by TARSKI:def 1;
         hence y in B by A6;
        end;
        B\B' ={s2} by A1,A6,Th3,ZFMISC_1:23;
       then s2 in B\B' by TARSKI:def 1;
       then G.(B' \/ {p2}) = F.(G.B',f.p2) by A7,A10;
      hence thesis by A6,A8,Th3,ENUMSET1:41;
     end;
     A11: f.p1 = -((the mult of K) "**" (Path_matrix(p1,M)),p1)
                     by MATRIX_3:def 8;
     A12: f.p2 = -((the mult of K) "**" (Path_matrix(p2,M)),p2)
                     by MATRIX_3:def 8;
     A13: 1 in Seg 2;
     then A14: 1 in dom Path_matrix(p1,M) by A3,FINSEQ_1:def 3;
     A15: 2 in Seg 2;
     then A16: 2 in dom Path_matrix(p1,M) by A3,FINSEQ_1:def 3;
     A17: 1 in dom Path_matrix(p2,M) by A4,A13,FINSEQ_1:def 3;
     A18: 2 in dom Path_matrix(p2,M) by A4,A15,FINSEQ_1:def 3;
        p1.1 = 1 by FINSEQ_1:61;
     then A19: Path_matrix(p1,M).1=M*(1,1) by A14,MATRIX_3:def 7;
      p1.2 = 2 by FINSEQ_1:61;
     then A20: Path_matrix(p1,M).2=M*(2,2) by A16,MATRIX_3:def 7;
      p2.1 = 2 by FINSEQ_1:61;
     then A21: Path_matrix(p2,M).1=M*(1,2) by A17,MATRIX_3:def 7;
      p2.2 = 1 by FINSEQ_1:61;
     then A22: Path_matrix(p2,M).2=M*(2,1) by A18,MATRIX_3:def 7;
     set di=(the mult of K) $$ (Path_matrix(p1,M));
     set dj=(the mult of K) $$ (Path_matrix(p2,M));
     consider f3 being Function of NAT,the carrier of K such that
     A23: f3.1 = (Path_matrix(p1,M)).1 &
     (for n being Element of NAT st 0 <> n & n < 2 holds
       f3.(n + 1) = (the mult of K).(f3.n,(Path_matrix(p1,M)).(n + 1)))
       & di = f3.2 by A3,FINSOP_1:def 1;
     A24: f3.(1+1)=(the mult of K).(f3.1,(Path_matrix(p1,M)).(1+1)) by A23
           .=(M*(1,1))*(M*(2,2)) by A19,A20,A23,GROUP_1:def 1;
     consider f4 being Function of NAT,the carrier of K such that
     A25: f4.1 = (Path_matrix(p2,M)).1 &
     (for n being Element of NAT st 0 <> n & n < 2 holds
       f4.(n + 1) = (the mult of K).(f4.n,(Path_matrix(p2,M)).(n + 1)))
       & dj = f4.2 by A4,FINSOP_1:def 1;
     A26: f4.(1+1)=(the mult of K).(f4.1,(Path_matrix(p2,M)).(1+1)) by A25
           .=(M*(1,2))*(M*(2,1)) by A21,A22,A25,GROUP_1:def 1;
     set D0={s1,s2};
     reconsider l0=<*>D0 as FinSequence of Group_of_Perm(2) by Th3,
MATRIX_2:def 13;
        A27: len l0=0 by FINSEQ_1:25;
     A28: 0 mod 2=0 by NAT_D:26;
     A29: Product l0=Product <*> (the carrier of Group_of_Perm 2)
                   .= 1_(Group_of_Perm(2)) by GROUP_4:11
                   .= p1 by FINSEQ_2:61,MATRIX_2:28;
A30: ex l be FinSequence of Group_of_Perm 2
     st (len l)mod 2=0 & s1=Product l &
     for i st i in dom l
     ex q being Element of Permutations 2
     st l.i=q & q is_transposition
     proof
       take l0;
       thus (len l0)mod 2=0 & s1=Product l0 by A28,A29,FINSEQ_1:25;
       let i;
       thus thesis by A27,FINSEQ_1:4,def 3;
     end;
A31:  len Permutations 2 = 2 by MATRIX_2:20;
     then A32: p1 is even by A30,MATRIX_2:def 15;
       not ex l be FinSequence of Group_of_Perm 2
       st (len l)mod 2=0 & s2=Product l &
       for i st i in dom l
         ex q being Element of Permutations 2
              st l.i=q & q is_transposition by Lm1,Th11;
     then p2 is odd by A31,MATRIX_2:def 15;
     then f.p2 = - dj by A12,MATRIX_2:def 16;
 hence thesis by A7,A9,A11,A23,A24,A25,A26,A32,MATRIX_2:def 16;
end;

definition let n be Nat,K be Field;
  let M be (Matrix of n,K), a be Element of K;
  redefine func a*M -> Matrix of n,K;
coherence
proof
  A1: len M =n by MATRIX_1:def 3;
  A2: len M = n & for p being FinSequence of K st
    p in rng M holds len p = n by MATRIX_1:def 3;
  A3: len (a*M)=len M & width (a*M)=width M &
    for i,j st [i,j] in Indices M holds (a*M)*(i,j)=a*(M*(i,j))
                             by MATRIX_3:def 5;
   now per cases;
  case A4: len M>0;
   then n=width M by A1,MATRIX_1:20;
  hence a*M is Matrix of n,K by A1,A3,A4,MATRIX_1:20;
  end;
  case len M<=0;
    then A5: len (a*M)=0 & width (a*M)=0 by A3,MATRIX_1:def 4;
     for p being FinSequence of K st p in rng (a*M) holds len p = 0
     proof let p be FinSequence of K;
      assume p in rng (a*M);
      then consider x being set such that
  A6: x in dom (a*M) & p=(a*M).x by FUNCT_1:def 5;
      thus len p = 0 by A5,A6,FINSEQ_1:4,def 3;
     end;
   hence a*M is Matrix of n,K by A2,A3,A5,MATRIX_1:def 3;
  end;
  end;
 hence a*M is Matrix of n,K;
end;
end;

theorem Th13: for K being Field, n,m being Element of NAT holds
len (0.(K,n,m))=n & dom (0.(K,n,m))=Seg n
proof let K be Field, n,m be Element of NAT;
   len (n |-> (m |-> 0.K))=n by FINSEQ_2:69;
 hence len (0.(K,n,m)) =n by MATRIX_3:def 1;
 hence dom (0.(K,n,m))=Seg n by FINSEQ_1:def 3;
end;

theorem Th14: for K being Field, n being Element of NAT,
p being Element of Permutations n, i being Element of NAT st i in Seg n holds
 p.i in Seg n
proof let K be Field, n be Element of NAT,
  p be Element of Permutations(n),i be Element of NAT;
  assume A1: i in Seg n;
   p is Permutation of Seg n by MATRIX_2:def 11;
 hence p.i in Seg n by A1,FUNCT_2:7;
end;

theorem  for K being Field, n being Element of NAT st n>=1 holds
    Det (0.(K,n,n)) = 0.K
proof let K be Field, n be Element of NAT;
  assume A1: n>=1;
  set B=FinOmega Permutations n;
  set f=Path_product(0.(K,n,n));
  set F=the add of K;
  set Y=the carrier of K;
  set X=Permutations n;
   Permutations(n) is finite by MATRIX_2:30;
  then A2: (B <> {} or F has_a_unity) by MATRIX_2:def 17;
   dom (Permutations(n) --> 0.K)=Permutations(n) by FUNCOP_1:19;
  then A3: dom (Path_product(0.(K,n,n)))=dom (Permutations(n) --> 0.K)
                              by FUNCT_2:def 1;
   for x being set st x in dom (Path_product(0.(K,n,n)))
   holds (Path_product(0.(K,n,n))).x=(Permutations(n) --> 0.K).x
  proof let x be set;
    assume
        x in dom (Path_product(0.(K,n,n)));
     for p being Element of Permutations(n) holds
    (Permutations(n) --> 0.K).p
        = -((the mult of K) $$ Path_matrix(p,(0.(K,n,n))),p)
    proof let p be Element of Permutations(n);
      A4: len (n |-> 0.K)=n by FINSEQ_2:69;
       for i,j st i in dom (n |-> 0.K)
                   & j=p.i holds (n |-> 0.K).i=(0.(K,n,n))*(i,j)
       proof let i,j;
         assume A5: i in dom (n |-> 0.K) & j=p.i;
         then A6: i in Seg n by FUNCOP_1:19;
         then A7: (n |-> 0.K).i=0.K by FUNCOP_1:13;
          j in Seg n by A5,A6,Th14;
         then i in dom (0.(K,n,n)) & j in Seg width (0.(K,n,n))
                             by A1,A6,Th13,MATRIX_1:24;
         then [i,j] in Indices (0.(K,n,n)) by ZFMISC_1:def 2;
        hence (n |-> 0.K).i=(0.(K,n,n))*(i,j) by A7,MATRIX_3:3;
       end;
      then A8: Path_matrix(p,(0.(K,n,n)))=(n |-> 0.K) by A4,MATRIX_3:def 7;
      defpred P[Element of NAT] means (the mult of K) $$ ( ($1+1) |-> 0.K)=0.K;
       (1 |-> 0.K)= <* 0.K *> by FINSEQ_2:73;
     then A9: P[0] by FINSOP_1:12;
     A10: for k being Element of NAT st P[k] holds P[k+1]
       proof let k be Element of NAT;
        assume A11: P[k];
         A12: (the mult of K) has_a_unity by FVSUM_1:11;
          (k+1+1) |-> 0.K = ((k+1) |-> 0.K) ^ <* 0.K *> by FINSEQ_2:74;
         then (the mult of K) $$ ( (k+1+1) |-> 0.K)
           =(the mult of K).((the mult of K)$$(((k+1) |-> 0.K)),0.K)
                              by A12,FINSOP_1:5
          .= (0.K)*(0.K) by A11,GROUP_1:def 1
          .= 0.K by VECTSP_1:36;
        hence P[k+1];
       end;
      A13: for k being Element of NAT holds P[k] from NAT_1:sch 1(A9,A10);
       n-'1=n-1 by A1,BINARITH:50;
      then A14: n-'1+1=n;
      A15: (Permutations(n) --> 0.K).p = 0.K by FUNCOP_1:13;
       now per cases;
      case p is even;
       hence -(0.K,p)=(0.K) by MATRIX_2:def 16;
      end;
      case not(p is even);
        then -(0.K,p)= -(0.K) by MATRIX_2:def 16 .=0.K by RLVECT_1:25;
       hence -(0.K,p)=(0.K);
      end;
      end;
     hence (Permutations(n) --> 0.K).p
        = -((the mult of K) $$ Path_matrix(p,(0.(K,n,n))),p) by A8,A13,A14,A15;
    end;
   hence (Path_product(0.(K,n,n))).x=(Permutations(n) --> 0.K).x
                                 by MATRIX_3:def 8;
  end;
  then A16: Path_product(0.(K,n,n))=Permutations(n) --> 0.K by A3,FUNCT_1:9;
  reconsider G0= Fin X --> 0.K as Function of Fin X, Y;
  A17: G0.B=0.K by FUNCOP_1:13;
  A18: for e being Element of Y st e is_a_unity_wrt F holds G0.({}) = e
    proof let e be Element of Y;assume
       e is_a_unity_wrt F;
      then A19: F.(0.K,e)=0.K by BINOP_1:11;
       0.K is_a_unity_wrt F by FVSUM_1:8;
      then A20: F.(0.K,e)=e by BINOP_1:11;
       {} in Fin X by FINSUB_1:18;
     hence G0.({}) = e by A19,A20,FUNCOP_1:13;
    end;
  A21: for x being Element of X holds G0.({x}) = f.x
   proof let x be Element of X;
      G0.({.x.})=0.K by FUNCOP_1:13;
    hence G0.({x}) = f.x by A16,FUNCOP_1:13;
   end;
    for B' being Element of Fin X st B' c= B & B' <> {}
      for x being Element of X st x in B\B' holds G0.(B' \/ {x})
                    = F.(G0.B',f.x)
   proof let B' be Element of Fin X;
     assume B' c= B & B' <> {};
     thus for x being Element of X st x in B\B' holds G0.(B' \/ {x})
                    = F.(G0.B',f.x)
     proof let x be Element of X;
       assume x in B\B';
        A22: G0.(B' \/ {.x.})=0.K by FUNCOP_1:13;
        A23: G0.B'=0.K by FUNCOP_1:13;
        A24: f.x= 0.K by A16,FUNCOP_1:13;
         0.K is_a_unity_wrt F by FVSUM_1:8;
       hence G0.(B' \/ {x}) = F.(G0.B',f.x) by A22,A23,A24,BINOP_1:11;
     end;
   end;
  then (the add of K) $$ (FinOmega(Permutations(n)),Path_product(0.(K,n,n)))=
0.K
                          by A2,A17,A18,A21,SETWISEO:def 3;
 hence thesis by MATRIX_3:def 9;
end;

definition let x,y,a,b be set;
  func IFIN(x,y,a,b) equals
:Def1: a if x in y otherwise b;
  correctness;
end;

theorem
   for K being Field, n being Element of NAT st n>=1 holds
    Det (1.(K,n)) = 1_K
proof
let K be Field, n be Element of NAT;
  assume A1: n>=1;
  set B=FinOmega(Permutations(n));
   Permutations(n) is finite by MATRIX_2:30;
  then A2: B=Permutations(n) by MATRIX_2:def 17;
   (idseq n) is Element of Group_of_Perm(n) by MATRIX_2:23;
  then A3: (idseq n) in the carrier of Group_of_Perm(n);
  reconsider p0=idseq n as Permutation of Seg n;
  A4: id (Seg n) is even by MATRIX_2:29;
  set f=Path_product(1.(K,n));
  set F=(the add of K);
  set Y=the carrier of K;
  set X=Permutations(n);
  deffunc F3(set) = IFIN(idseq n,$1,1_K,0.K);
  A5: for x being set st x in Fin X holds F3(x) in Y
  proof let x be set;
   assume x in Fin X;
     now per cases;
    case idseq n in x;
      then F3(x)=1_K by Def1;
     hence F3(x) in Y;
    end;
    case not idseq n in x;
      then F3(x)=0.K by Def1;
     hence F3(x) in Y;
    end;
    end;
   hence F3(x) in Y;
  end;
   ex f2 being Function of Fin X,Y st for x being set st x in Fin X holds
               f2.x = F3(x) from FUNCT_2:sch 2(A5);
  then consider G0 being Function of Fin X,Y such that
  A6: for x being set st x in Fin X holds G0.x = F3(x);
  deffunc F2(set) = IFEQ(idseq n,$1,1_K,0.K);
  A7: for x being set st x in X holds F2(x) in Y;
   ex f2 being Function of X,Y st for x being set st x in X holds
               f2.x = F2(x) from FUNCT_2:sch 2(A7);
  then consider f2 being Function of X,Y such that
  A8: for x being set st x in X holds f2.x = F2(x);
   dom (f2)=Permutations(n) by FUNCT_2:def 1;
  then A9: dom (Path_product(1.(K,n)))=dom f2 by FUNCT_2:def 1;
  A10: for x being set st x in dom (Path_product(1.(K,n)))
   holds (Path_product(1.(K,n))).x=f2.x
  proof let x be set;assume
     x in dom (Path_product(1.(K,n)));
     for p being Element of Permutations(n) holds
    f2.p = -((the mult of K) $$ Path_matrix(p,(1.(K,n))),p)
    proof let p be Element of Permutations(n);
      A11: len (n |-> 1_K)=n by FINSEQ_2:69;
      defpred P[Element of NAT] means (the mult of K) $$ ( ($1+1) |-> 1_K)=1_K;
       (1 |-> 1_K)= <* 1_K *> by FINSEQ_2:73;
     then A12: P[0] by FINSOP_1:12;
     A13: for k being Element of NAT st P[k] holds P[k+1]
       proof let k be Element of NAT;
        assume A14: P[k];
         A15: (the mult of K) has_a_unity by FVSUM_1:11;
          (k+1+1) |-> 1_K = ((k+1) |-> 1_K) ^ <* 1_K *> by FINSEQ_2:74;
         then (the mult of K) $$ ( (k+1+1) |-> 1_K)
           =(the mult of K).((the mult of K)$$(((k+1) |-> 1_K)),1_K)
                              by A15,FINSOP_1:5
          .= (1_K)*(1_K) by A14,GROUP_1:def 1
          .= 1_K by GROUP_1:def 5;
        hence P[k+1];
       end;
      A16: for k being Element of NAT holds P[k] from NAT_1:sch 1(A12,A13);
       n-'1=n-1 by A1,BINARITH:50;
      then n-'1+1=n;
      then A17: (the mult of K) $$ (n |-> 1_K)=1_K by A16;
      A18: now per cases;
      case p is even;
       hence -(0.K,p)=(0.K) by MATRIX_2:def 16;
      end;
      case not(p is even);
        then -(0.K,p)= -(0.K) by MATRIX_2:def 16 .=0.K by RLVECT_1:25;
       hence -(0.K,p)=0.K;
      end;
      end;
       now per cases;
      case A19: p=idseq n;
        A20: f2.p=F2(p) by A8
           .= 1_K by A19,FUNCOP_1:def 8;
        reconsider p1=p0 as Element of Permutations n
                                   by MATRIX_2:def 11;
         len Permutations n = n by MATRIX_2:20;
         then A21: -((the mult of K) $$ Path_matrix(p,(1.(K,n))),p)
           = (the mult of K) $$ Path_matrix(p,(1.(K,n)))
                    by A4,A19,MATRIX_2:def 16;
                for i,j st i in dom (n |-> 1_K)
                   & j=p.i holds (n |-> 1_K).i=(1.(K,n))*(i,j)
       proof let i,j;
         assume A22: i in dom (n |-> 1_K) & j=p.i;
         then A23: i in Seg n by FUNCOP_1:19;
         then A24: (n |-> 1_K).i=1_K by FUNCOP_1:13;
         A25: p.i=i by A19,A23,FUNCT_1:34;
         A26: len (1.(K,n))=n & width (1.(K,n))=n &
         Indices (1.(K,n))=[: Seg n,Seg n:] by A1,MATRIX_1:24;
          j in Seg n by A22,A23,Th14;
         then [i,j] in Indices (1.(K,n)) by A23,A26,ZFMISC_1:def 2;
        hence (n |-> 1_K).i=(1.(K,n))*(i,j) by A22,A24,A25,MATRIX_1:def 12;
       end;
       hence f2.p = -((the mult of K) $$ Path_matrix(p,(1.(K,n))),p)
                            by A11,A17,A20,A21,MATRIX_3:def 7;
      end;
      case A27: p<>idseq n;
        A28: dom p = Seg len Permutations n by FUNCT_2:67;
then A29:     dom p = Seg n by MATRIX_2:20;
     then dom p = dom idseq n by RELAT_1:71;
        then consider i0 being set such that
   A30: i0 in dom p & p.i0 <> (idseq n).i0 by A27,FUNCT_1:9;
        reconsider i0 as Element of NAT by A28,A30;
   A31: p.i0<>i0 by A29,A30,FUNCT_1:35;
         p is Permutation of Seg n by MATRIX_2:def 11;
        then A32: p.i0 in Seg n by A29,A30,FUNCT_2:7;
        then reconsider j0=p.i0 as Element of NAT;
        A33: f2.p=F2(p) by A8
           .= 0.K by A27,FUNCOP_1:def 8;
        A34: len (Path_matrix(p,(1.(K,n))))=n &
        for i,j st i in dom (Path_matrix(p,(1.(K,n)))) &
        j=p.i holds (Path_matrix(p,(1.(K,n)))).i=(1.(K,n))*(i,j)
                                      by MATRIX_3:def 7;
        then i0 in dom (Path_matrix(p,(1.(K,n)))) by A29,A30,FINSEQ_1:def 3;
        then A35: (Path_matrix(p,(1.(K,n)))).i0=(1.(K,n))*(i0,j0) by
MATRIX_3:def 7;
         len (1.(K,n))=n & width (1.(K,n))=n &
         Indices (1.(K,n))=[: Seg n,Seg n:] by A1,MATRIX_1:24;
        then A36: [i0,j0] in Indices (1.(K,n)) by A29,A30,A32,ZFMISC_1:def 2;
        then A37: (Path_matrix(p,(1.(K,n)))).i0=0.K
                              by A31,A35,MATRIX_1:def 12;
          defpred P3[Element of NAT] means $1 < n implies
          ex p3 being FinSequence of K st len p3=$1+1 &
            p3.1=(Path_matrix(p,(1.(K,n)))).1 &
         (for n3 being Element of NAT st 0 <> n3 & n3 < $1+1 & n3<n holds
           p3.(n3 + 1) = (the mult of K).
                  (p3.n3,(Path_matrix(p,(1.(K,n)))).(n3 + 1)));
          1 in Seg len (Path_matrix(p,(1.(K,n)))) by A1,A34;
         then 1 in dom (Path_matrix(p,(1.(K,n)))) by FINSEQ_1:def 3;
         then A38: (Path_matrix(p,(1.(K,n)))).1 in rng ((Path_matrix(p,(1.(K,n)
))))
                                        by FUNCT_1:def 5;
          rng ((Path_matrix(p,(1.(K,n))))) c= the carrier of K
                                     by FINSEQ_1:def 4;
         then reconsider d=(Path_matrix(p,(1.(K,n)))).1
                                  as Element of K by A38;
         reconsider q3=<* d *> as FinSequence of K;
         A39: len q3=1 by FINSEQ_1:56;
         A40: q3.1=(Path_matrix(p,(1.(K,n)))).1 by FINSEQ_1:57;
          for n3 being Element of NAT st 0 <> n3 & n3 < 0+1 & n3< n holds
           q3.(n3 + 1) = (the mult of K).
                  (q3.n3,(Path_matrix(p,(1.(K,n)))).(n3 + 1))
          proof let n3 be Element of NAT;
            assume A41: 0 <> n3 & n3 < 0+1 & n3< n;
            then 0<n3;
           hence thesis by A41,NAT_1:13;
          end;
         then A42: P3[0] by A39,A40;
         A43: for k being Element of NAT st P3[k] holds P3[k+1]
          proof let k be Element of NAT;
            assume A44: P3[k];
             now per cases;
            case A45: k+1<n;
            then consider p3 being FinSequence of K such that
             A46: len p3=k+1 &
              p3.1=(Path_matrix(p,(1.(K,n)))).1 &
             (for n3 being Element of NAT st 0 <> n3 & n3 < k+1 & n3<n
              holds
               p3.(n3 + 1) = (the mult of K).
                  (p3.n3,(Path_matrix(p,(1.(K,n)))).(n3 + 1)))
                                      by A44,NAT_1:12;
              defpred P6[set,set] means
                ($1 in Seg (k+1) implies $2=p3.$1)&
                 (not $1 in Seg (k+1) implies $2=0.K);
              A47: for x being set st x in NAT ex y being set
                      st y in the carrier of K & P6[x,y]
                proof let x be set;assume x in NAT;
                  now per cases;
                 case A48: x in Seg (k+1);
                  then reconsider nx=x as Element of NAT;
                   nx in dom p3 by A46,A48,FINSEQ_1:def 3;
                  then A49: p3.nx in rng p3 by FUNCT_1:def 5;
                   rng p3 c= the carrier of K by FINSEQ_1:def 4;
                  then reconsider s=p3.nx as Element of K by A49;
                   (x in Seg (k+1) implies s=p3.x)&
                 (not x in Seg (k+1) implies s=0.K) by A48;
                 hence ex y being set
                      st y in the carrier of K & P6[x,y];
                 end;
                 case not x in Seg (k+1);
                  hence ex y being set
                      st y in the carrier of K & P6[x,y];
                 end;
                 end;
                hence ex y being set
                      st y in the carrier of K & P6[x,y];
                end;
               ex f6 being Function of NAT,the carrier of K st
                    for x being set st x in NAT holds P6[x,f6.x]
                                    from FUNCT_2:sch 1(A47);
              then consider f6 being Function of NAT,the carrier of K such that
              A50:  for x being set st x in NAT holds P6[x,f6.x];
              A51: rng ((Path_matrix(p,(1.(K,n))))) c= the carrier of K
                                              by FINSEQ_1:def 4;
               1<=k+1+1 & k+1+1 <=n by A45,NAT_1:12,13;
              then k+1+1 in Seg len (Path_matrix(p,(1.(K,n)))) by A34;
              then k+1+1 in dom (Path_matrix(p,(1.(K,n)))) by FINSEQ_1:def 3;
              then (Path_matrix(p,(1.(K,n)))).(k + 1+1) in
                      rng (Path_matrix(p,(1.(K,n)))) by FUNCT_1:def 5;
              then [f6.(k+1),(Path_matrix(p,(1.(K,n)))).(k + 1+1)]
                             in [:the carrier of K, the carrier of K:]
                                  by A51,ZFMISC_1:def 2;
              then reconsider e=(the mult of K).
                  (f6.(k+1),(Path_matrix(p,(1.(K,n)))).(k + 1+1))
                     as Element of K by FUNCT_2:7;
             reconsider q3=p3^<* e *> as FinSequence of K;
            A52: len q3=(len p3) + len (<*e*>) by FINSEQ_1:35
                 .=k+1+1 by A46,FINSEQ_1:57;
                       1<=k+1 by NAT_1:12;
            then 1 in Seg len p3 by A46;
            then 1 in dom p3 by FINSEQ_1:def 3;
            then A53: q3.1=(Path_matrix(p,(1.(K,n)))).1 by A46,FINSEQ_1:def 7;
             for n3 being Element of NAT st 0 <> n3 & n3 < k+1+1 & n3<n
              holds
               q3.(n3 + 1) = (the mult of K).
                  (q3.n3,(Path_matrix(p,(1.(K,n)))).(n3 + 1))
            proof let n3 be Element of NAT;
              assume A54: 0 <> n3 & n3 < k+1+1 & n3<n;
                 now per cases;
                case A55: n3<k+1;
                  then A56: p3.(n3 + 1) = (the mult of K).
                  (p3.n3,(Path_matrix(p,(1.(K,n)))).(n3 + 1)) by A46,A54;
                   1<=n3+1 & n3+1<=k+1 by A55,NAT_1:12,13;
                  then n3+1 in Seg len p3 by A46;
                  then n3+1 in dom p3 by FINSEQ_1:def 3;
                  then A57: p3.(n3+1)=q3.(n3+1) by FINSEQ_1:def 7;
                   0<n3 by A54;
                  then 0+1<=n3 by NAT_1:13;
                  then n3 in Seg len p3 by A46,A55;
                  then n3 in dom p3 by FINSEQ_1:def 3;
                  hence q3.(n3 + 1) = (the mult of K).
                  (q3.n3,(Path_matrix(p,(1.(K,n)))).(n3 + 1))
                                   by A56,A57,FINSEQ_1:def 7;
                end;
                case n3>=k+1;
                  then A58: n3+1>k+1 by NAT_1:13;
                  A59: n3+1<=k+1+1 by A54,NAT_1:13;
                   n3+1>=k+1+1 by A58,NAT_1:13;
                  then A60: n3+1=k+1+1 by A59,XXREAL_0:1;
                   1<=k+1 by NAT_1:12;
                  then A61: k+1 in Seg (k+1);
                  then k+1 in dom p3 by A46,FINSEQ_1:def 3;
                  then A62: q3.(k+1)=p3.(k+1) by FINSEQ_1:def 7;
                   (q3).(n3+1) = (<*e*>).(n3+1 - (k+1))
                                  by A46,A52,A58,A59,FINSEQ_1:37
                             .= e by A60,FINSEQ_1:def 8;
                 hence q3.(n3 + 1) = (the mult of K).
                  (q3.n3,(Path_matrix(p,(1.(K,n)))).(n3 + 1))
                                 by A50,A60,A61,A62;
                end;
                end;
               hence q3.(n3 + 1) = (the mult of K).
                  (q3.n3,(Path_matrix(p,(1.(K,n)))).(n3 + 1));
            end;
            hence P3[k+1] by A52,A53;
            end;
            case k+1>=n;
            hence P3[k+1];
            end;
            end;
           hence P3[k+1];
          end;
         A63: for k being Element of NAT holds P3[k] from NAT_1:sch 1(A42,A43);
         A64: n-1=n-'1 by A1,BINARITH:50;
          n<n+1 by NAT_1:13;
         then n-1<n+1-1 by REAL_1:92;
         then consider p3 being FinSequence of K such that
         A65: len p3=n-'1+1 &
            p3.1=(Path_matrix(p,(1.(K,n)))).1 &
         (for n3 being Element of NAT st 0 <> n3 & n3 < n-'1+1 & n3<n holds
           p3.(n3 + 1) = (the mult of K).
                  (p3.n3,(Path_matrix(p,(1.(K,n)))).(n3 + 1))) by A63,A64;
        reconsider A=NAT as non empty set;
        defpred P[set,set] means
           ($1 in Seg n implies $2=p3.$1)&
           (not $1 in Seg n implies $2=0.K);
         A66: for x3 being Element of A
                 ex y3 being Element of K st P[x3,y3]
         proof let x3 be Element of A;
            now per cases;
           case A67: x3 in Seg n;
             reconsider m=x3 as Element of NAT;
             A68: rng p3 c= the carrier of K by FINSEQ_1:def 4;
              x3 in dom p3 by A64,A65,A67,FINSEQ_1:def 3;
             then p3.x3 in rng p3 by FUNCT_1:def 5;
            hence ex y3 being Element of K st P[x3,y3] by A67,A68;
           end;
           case not x3 in Seg n;
            hence ex y3 being Element of K st P[x3,y3];
           end;
           end;
          hence ex y3 being Element of K st P[x3,y3];
         end;
         ex f4 being Function of A,the carrier of K st
         (for x2 being Element of A holds P[x2,f4.x2])
                   from FUNCT_2:sch 3(A66);
        then consider f4 being Function of NAT,the carrier of K such that
        A69: for x4 being Element of NAT holds
            (x4 in Seg n implies f4.x4=p3.x4)&
           (not x4 in Seg n implies f4.x4=0.K);
          1 in Seg n by A1;
        then A70: f4.1=(Path_matrix(p,(1.(K,n)))).1 by A65,A69;
        A71: for n3 being Element of NAT
          st 0 <> n3 & n3 < len (Path_matrix(p,(1.(K,n)))) holds
           f4.(n3 + 1) = (the mult of K).
                  (f4.n3,(Path_matrix(p,(1.(K,n)))).(n3 + 1))
         proof let n3 be Element of NAT;
           assume A72: 0 <> n3 & n3 < len (Path_matrix(p,(1.(K,n))));
            then n3>0;
            then A73: 0+1<=n3 by NAT_1:13;
            then A74: 1<n3+1 by NAT_1:13;
             n3+1<=len (Path_matrix(p,(1.(K,n)))) by A72,NAT_1:13;
            then n3+1 in Seg n by A34,A74;
            then A75: f4.(n3+1)=p3.(n3+1) by A69;
            A76: p3.(n3 + 1) = (the mult of K).
                  (p3.n3,(Path_matrix(p,(1.(K,n)))).(n3 + 1))
                                   by A34,A64,A65,A72;
             n3 in Seg n by A34,A72,A73;
          hence f4.(n3 + 1) = (the mult of K).
            (f4.n3,(Path_matrix(p,(1.(K,n)))).(n3 + 1)) by A69,A75,A76;
         end;
         defpred P5[Element of NAT] means f4.(i0+$1)=0.K;
A77:      1 <= i0 by A28,A30,FINSEQ_1:3;
         A78: 0<i0 by A28,A30,FINSEQ_1:3;
          now per cases;
         case i0<=1;
          then i0=1 by A77,XXREAL_0:1;
          hence P5[0] by A31,A35,A36,A70,MATRIX_1:def 12;
         end;
         case A79: i0>1;
           then A80: i0-1> 1-1 by REAL_1:92;
A81:        i0 <= n by A29,A30,FINSEQ_1:3;
            i0<i0+1 by NAT_1:13;
           then i0-1<i0+1-1 by REAL_1:92;
           then A82: i0-1<len (Path_matrix(p,(1.(K,n))))
                          by A34,A81,XXREAL_0:2;
           reconsider a=f4.(i0-'1) as Element of K;
            i0-'1=i0-1 by A79,BINARITH:50;
           then i0-'1+1=i0;
           then f4.(i0) = (the mult of K).
                  (f4.(i0-'1),(Path_matrix(p,(1.(K,n)))).(i0))
                            by A71,A80,A82
                  .= a*(0.K) by A37,GROUP_1:def 1
                  .= 0.K by VECTSP_1:36;
           hence P5[0];
          end;
          end;
           then A83: P5[0];
           A84: for k being Element of NAT st P5[k] holds P5[k+1]
           proof let k be Element of NAT;
            assume A85: P5[k];
             A86: 1<=1+(i0+k) by NAT_1:12;
              now per cases;
             case A87: i0+k+1<=n;
               A88: i0<=i0+k by NAT_1:12;
               A89: i0+k<n by A87,NAT_1:13;
                0+1<=i0+k by A78,A88,NAT_1:13;
               then i0+k in Seg n by A89;
               then A90: p3.(i0+k)=0.K by A69,A85;
                1<=1+(i0+k) by NAT_1:12;
               then i0+k+1 in Seg len (Path_matrix(p,(1.(K,n))))
                                  by A34,A87;
               then i0+k+1 in dom (Path_matrix(p,(1.(K,n))))
                                  by FINSEQ_1:def 3;
               then A91: (Path_matrix(p,(1.(K,n)))).(i0+k+1)
                            in rng (Path_matrix(p,(1.(K,n))))
                                  by FUNCT_1:def 5;
                rng (Path_matrix(p,(1.(K,n)))) c= the carrier of K
                                   by FINSEQ_1:def 4;
               then reconsider b=(Path_matrix(p,(1.(K,n)))).(i0+k+1)
                             as Element of K by A91;
                i0+k+1 in Seg n by A86,A87;
               then f4.(i0+k+1)=p3.(i0+k+1) by A69
                          .=(the mult of K).
                   (p3.(i0+k),(Path_matrix(p,(1.(K,n)))).(i0+k+1))
                           by A64,A65,A78,A88,A89
                         .= (0.K)*(b) by A90,GROUP_1:def 1
                         .= 0.K by VECTSP_1:36;
              hence f4.(i0+(k+1))=0.K;
             end;
             case i0+k+1>n;
               then not (i0+(k+1) in Seg n) by FINSEQ_1:3;
              hence f4.(i0+(k+1))=0.K by A69;
             end;
             end;
            hence P5[k+1];
           end;
            for k being Element of NAT holds P5[k] from NAT_1:sch 1(A83,A84);
           then A92: P5[n-'i0];
            i0 <= n by A29,A30,FINSEQ_1:3;
           then n-'i0 =n-i0 by BINARITH:50;
       hence f2.p = -((the mult of K) $$ Path_matrix(p,(1.(K,n))),p)
                  by A1,A18,A33,A34,A70,A71,A92,FINSOP_1:3;
      end;
      end;
     hence f2.p = -((the mult of K) $$ Path_matrix(p,(1.(K,n))),p);
    end;
   hence (Path_product(1.(K,n))).x=f2.x by MATRIX_3:def 8;
  end;
  then A93: Path_product(1.(K,n))=f2 by A9,FUNCT_1:9;
   A94: now assume A95: idseq n in B;
         thus G0.(B)=IFIN(idseq n,B,1_K,0.K) by A6
                            .=1_K by A95,Def1;
   end;
  A96: for e being Element of Y st e is_a_unity_wrt F holds G0.({}) = e
    proof let e be Element of Y;
      assume e is_a_unity_wrt F;
      then A97: F.(0.K,e)=0.K by BINOP_1:11;
       0.K is_a_unity_wrt F by FVSUM_1:8;
      then A98: F.(0.K,e)=e by BINOP_1:11;
      A99: {} in Fin X by FINSUB_1:18;
       IFIN(idseq n,{},1_K,0.K)=0.K by Def1;
     hence G0.({}) = e by A6,A97,A98,A99;
    end;
  A100: for x being Element of X holds G0.({x}) = f.x
   proof let x be Element of X;
      now per cases;
     case A101: x=idseq n;
       A102: f2.x=F2(x) by A8
          .=1_K by A101,FUNCOP_1:def 8;
        idseq n in {x} by A101,TARSKI:def 1;
       then IFIN(idseq n,{x},1_K,0.K)=1_K by Def1;
      hence G0.({.x.})=f2.x by A6,A102;
     end;
     case A103: x <> idseq n;
       A104: f2.x=F2(x) by A8
          .=0.K by A103,FUNCOP_1:def 8;
        not idseq n in {x} by A103,TARSKI:def 1;
       then IFIN(idseq n,{x},1_K,0.K)=0.K by Def1;
      hence G0.({.x.})=f2.x by A6,A104;
     end;
     end;
    hence G0.({x}) = f.x by A9,A10,FUNCT_1:9;
   end;
    for B' being Element of Fin X st B' c= B & B' <> {}
      for x being Element of X st x in B\B' holds G0.(B' \/ {x})
                    = F.(G0.B',f.x)
   proof let B' be Element of Fin X;
     assume B' c= B & B' <> {};
     thus for x being Element of X st x in B\B' holds G0.(B' \/ {x})
                    = F.(G0.B',f.x)
     proof let x be Element of X;
       assume A105: x in B\B';
         A106: now assume A107: idseq n in B' \/ {x};
         thus G0.(B' \/ {x})=IFIN(idseq n,B' \/ {.x.},1_K,0.K) by A6
                            .=1_K by A107,Def1;
        end;
        A108: now assume A109: not idseq n in B' \/ {x};
         thus G0.(B' \/ {x})=IFIN(idseq n,B' \/ {.x.},1_K,0.K) by A6
                            .=0.K by A109,Def1;
         end;
        A110: now assume A111: idseq n in B';
         thus G0.(B')=IFIN(idseq n,B',1_K,0.K) by A6
                            .=1_K by A111,Def1;
        end;
        A112: now assume A113: not idseq n in B';
         thus G0.(B')=IFIN(idseq n,B',1_K,0.K) by A6
                            .=0.K by A113,Def1;
        end;
        A114: 0.K is_a_unity_wrt F by FVSUM_1:8;
        A115: now assume idseq n in B' \/ {x};
          then A116: idseq n in B' or idseq n in {x} by XBOOLE_0:def 2;
           now per cases by A116,TARSKI:def 1;
          case A117: idseq n in B';
           A118: not x in B' by A105,XBOOLE_0:def 4;
            f.x=F2(x) by A8,A93
              .=0.K by A117,A118,FUNCOP_1:def 8;
           hence F.(G0.B',f.x)=1_K by A110,A114,A117,BINOP_1:11;
          end;
          case A119: idseq n=x;
             f.x=F2(x) by A8,A93
              .=1_K by A119,FUNCOP_1:def 8;
           hence F.(G0.B',f.x)=1_K by A105,A112,A114,A119,BINOP_1:11
,XBOOLE_0:def 4;
          end;
          end;
         hence F.(G0.B',f.x)=1_K;
        end;
         now assume A120: not idseq n in B' \/ {x};
          then not idseq n in {x} by XBOOLE_0:def 2;
          then A121: not idseq n=x by TARSKI:def 1;
           f.x=F2(x) by A8,A93
              .=0.K by A121,FUNCOP_1:def 8;
         hence F.(G0.B',f.x)=0.K by A112,A114,A120,BINOP_1:11,XBOOLE_0:def 2;
        end;
      hence G0.(B' \/ {x}) = F.(G0.B',f.x) by A106,A108,A115;
     end;
   end;
  then (the add of K) $$ (FinOmega(Permutations n),Path_product(1.(K,n)))=1_K
                                       by A2,A3,A94,A96,A100,MATRIX_2:def 13
,SETWISEO:def 3;
 hence thesis by MATRIX_3:def 9;
end;

definition let n be Nat, K be Field, M be Matrix of n,K;
  redefine attr M is diagonal means :Def2:
    for i,j being Element of NAT st i in Seg n & j in Seg n & i <> j holds
      M*(i,j) = 0.K;
  compatibility
  proof
    hereby assume
A1:   M is diagonal;
      let i, j be Element of NAT;
      assume
A2:   i in Seg n & j in Seg n & i <> j;
      then [i,j] in [:Seg n, Seg n:] by ZFMISC_1:def 2;
      then [i,j] in Indices M by MATRIX_1:25;
      hence M*(i,j) = 0.K by A1,A2,MATRIX_1:def 15;
    end;
    assume
A3: for i,j being Element of NAT st i in Seg n & j in Seg n & i <> j holds
      M*(i,j) = 0.K;
    let i,j be Nat;
    assume
A4: [i,j] in Indices M & M*(i,j) <> 0.K;
    then [i,j] in [:Seg n, Seg n:] by MATRIX_1:25;
    then i in Seg n & j in Seg n by ZFMISC_1:106;
    hence thesis by A3,A4;
  end;
end;

theorem
   for K being Field, n being Element of NAT, A being Matrix of n,K st
    n>=1 & A is diagonal holds
      Det A = (the mult of K) $$ (diagonal_of_Matrix A)
proof let K be Field, n be Element of NAT,
  A be Matrix of n,K;
  assume A1: n>=1 & A is diagonal;
A2: len (diagonal_of_Matrix A)=n by MATRIX_3:def 10;
  set F4=diagonal_of_Matrix A;
  consider f4 being Function of NAT,the carrier of K such that
   A3:  f4.1 = F4.1 &
     (for n4 being Element of NAT st 0 <> n4 & n4 < len F4 holds
       f4.(n4 + 1) = (the mult of K).(f4.n4,F4.(n4 + 1)))
       & (the mult of K)$$ (diagonal_of_Matrix A) = f4.(len F4)
                               by A1,A2,FINSOP_1:def 1;
  deffunc F5(Nat) = f4.$1;
   ex z being FinSequence of K st len z = n &
  for j be Nat st j in Seg n holds z.j = F5(j)
  from FINSEQ_2:sch 1;
  then consider ps being FinSequence of K such that
  A4: len ps = n &
  for j be Nat st j in Seg n holds ps.j = f4.j;

    1 in Seg n by A1;
    then reconsider dps1= ps.1 as Element of K by A3,A4;
  set B=FinOmega(Permutations(n));
   Permutations(n) is finite by MATRIX_2:30;
  then A5: B=Permutations(n) by MATRIX_2:def 17;
   (idseq n) is Element of Group_of_Perm(n) by MATRIX_2:23;
  then A6: (idseq n) in the carrier of Group_of_Perm(n);
  reconsider p0=idseq n as Permutation of Seg n;
  A7: p0 is even by MATRIX_2:29;
  set f=Path_product(A);
  set F=the add of K;
  set Y=the carrier of K;
  set X=Permutations n;
  set k1=(the mult of K)$$(diagonal_of_Matrix A);
  deffunc F3(set) = IFIN(idseq n,$1,k1,0.K);
  A8: for x being set st x in Fin X holds F3(x) in Y
  proof let x be set;
   assume x in Fin X;
    per cases;
    suppose idseq n in x;
      then F3(x)=k1 by Def1;
     hence F3(x) in Y;
    end;
    suppose not idseq n in x;
      then F3(x)=0.K by Def1;
     hence F3(x) in Y;
    end;
  end;
   ex f2 being Function of Fin X,Y st for x being set st x in Fin X holds
               f2.x = F3(x) from FUNCT_2:sch 2(A8);
  then consider G0 being Function of Fin X,Y such that
  A9: for x being set st x in Fin X holds G0.x = F3(x);
  deffunc F2(set) = IFEQ(idseq n,$1,k1,0.K);
  A10: for x being set st x in X holds F2(x) in Y;
   ex f2 being Function of X,Y st for x being set st x in X holds
               f2.x = F2(x) from FUNCT_2:sch 2(A10);
  then consider f2 being Function of X,Y such that
  A11: for x being set st x in X holds f2.x = F2(x);
   dom (f2)=Permutations(n) by FUNCT_2:def 1;
  then A12: dom (Path_product A)=dom f2 by FUNCT_2:def 1;
  A13: for x being set st x in dom (Path_product A)
   holds (Path_product A).x=f2.x
  proof let x be set;assume x in dom Path_product A;
     for p being Element of Permutations n holds
    f2.p = -((the mult of K) $$ Path_matrix(p,A),p)
    proof let p be Element of Permutations n;
      A14: now per cases;
      suppose p is even;
       hence -(0.K,p)=(0.K) by MATRIX_2:def 16;
      end;
      suppose p is odd;
        then -(0.K,p)= -(0.K) by MATRIX_2:def 16 .=0.K by RLVECT_1:25;
       hence -(0.K,p)=0.K;
      end;
      end;
       now per cases;
      case A15: p=idseq n;
        A16: f2.p=F2(p) by A11
           .= k1 by A15,FUNCOP_1:def 8;
        reconsider p1=p0 as Element of Permutations n by MATRIX_2:def 11;
         len Permutations n = n by MATRIX_2:20;
        then A17: -((the mult of K) $$ Path_matrix(p,A),p)
           = (the mult of K) $$ Path_matrix(p,(A))
                    by A7,A15,MATRIX_2:def 16;
        for i,j st i in dom ((diagonal_of_Matrix A))
                   & j=p.i holds ((diagonal_of_Matrix A)).i=(A)*(i,j)
       proof let i,j;
         assume A18: i in dom ((diagonal_of_Matrix A)) & j=p.i;
         then A19: i in Seg n by A2,FINSEQ_1:def 3;
         then p.i=i by A15,FUNCT_1:34;
        hence ((diagonal_of_Matrix A)).i=(A)*(i,j)
                                     by A18,A19,MATRIX_3:def 10;
       end;
       hence f2.p
        = -((the mult of K) $$ Path_matrix(p,A),p)
                                 by A2,A16,A17,MATRIX_3:def 7;
      end;
      case A20: p<>idseq n;
        A21: dom p = Seg len Permutations n by FUNCT_2:67;
then A22:     dom p = Seg n by MATRIX_2:20;
     then dom p = dom idseq n by RELAT_1:71;
        then consider i0 being set such that
   A23: i0 in dom p & p.i0 <> (idseq n).i0 by A20,FUNCT_1:9;
   A24: i0 in Seg n by A21,A23,MATRIX_2:20;
        reconsider i0 as Element of NAT by A21,A23;
  A25:  1<=i0 & i0<=n by A22,A23,FINSEQ_1:3;
        A26: p.i0<>i0 by A22,A23,FUNCT_1:35;
         p is Permutation of Seg n by MATRIX_2:def 11;
        then A27: p.i0 in Seg n by A22,A23,FUNCT_2:7;
         then reconsider j0=p.i0 as Element of NAT;
        A28: f2.p=F2(p) by A11
           .= 0.K by A20,FUNCOP_1:def 8;
        A29: len (Path_matrix(p,A))=n &
          for i,j st i in dom (Path_matrix(p,A)) &
           j=p.i holds (Path_matrix(p,A)).i=A*(i,j) by MATRIX_3:def 7;
        then i0 in dom (Path_matrix(p,A)) by A24,FINSEQ_1:def 3;
        then A30: (Path_matrix(p,A)).i0=(A)*(i0,j0) by MATRIX_3:def 7;
        then A31: (Path_matrix(p,A)).i0=0.K
                                 by A1,A24,A26,A27,Def2;
        defpred P3[Element of NAT] means $1 < n implies
          ex p3 being FinSequence of K st len p3=$1+1 &
            p3.1=(Path_matrix(p,A)).1 &
         (for n3 being Element of NAT st 0 <> n3 & n3 < $1+1 & n3<n holds
           p3.(n3 + 1) = (the mult of K).
                  (p3.n3,(Path_matrix(p,A)).(n3 + 1)));
          1 in Seg len (Path_matrix(p,A)) by A1,A29;
         then 1 in dom (Path_matrix(p,A)) by FINSEQ_1:def 3;
         then A32: (Path_matrix(p,A)).1 in rng ((Path_matrix(p,A)))
                                        by FUNCT_1:def 5;
          rng ((Path_matrix(p,A))) c= the carrier of K
                                     by FINSEQ_1:def 4;
         then reconsider d=(Path_matrix(p,A)).1 as Element of K by A32;
         reconsider q3=<* d *> as FinSequence of K;
         A33: len q3=1 by FINSEQ_1:56;
         A34: q3.1=(Path_matrix(p,A)).1 by FINSEQ_1:57;
          for n3 being Element of NAT st 0 <> n3 & n3 < 0+1 & n3< n holds
           q3.(n3 + 1) = (the mult of K).
                  (q3.n3,(Path_matrix(p,A)).(n3 + 1))
          proof let n3 be Element of NAT;
            assume A35: 0 <> n3 & n3 < 0+1 & n3< n;
            then 0<n3;
           hence thesis by A35,NAT_1:13;
          end;
         then A36: P3[0] by A33,A34;
         A37: for k being Element of NAT st P3[k] holds P3[k+1]
          proof let k be Element of NAT;
            assume A38: P3[k];
             now per cases;
            case A39: k+1<n;
            then consider p3 being FinSequence of K such that
             A40: len p3=k+1 &
              p3.1=(Path_matrix(p,A)).1 &
             (for n3 being Element of NAT st 0 <> n3 & n3 < k+1 & n3<n holds
               p3.(n3 + 1) = (the mult of K).
                  (p3.n3,(Path_matrix(p,A)).(n3 + 1))) by A38,NAT_1:12;
              defpred P6[set,set] means
                ($1 in Seg (k+1) implies $2=p3.$1)&
                 (not $1 in Seg (k+1) implies $2=0.K);
              A41: for x being set st x in NAT ex y being set
                      st y in the carrier of K & P6[x,y]
                proof let x be set;assume x in NAT;
                  now per cases;
                 case A42: x in Seg (k+1);
                  then reconsider nx=x as Element of NAT;
                   nx in dom p3 by A40,A42,FINSEQ_1:def 3;
                  then A43: p3.nx in rng p3 by FUNCT_1:def 5;
                   rng p3 c= the carrier of K by FINSEQ_1:def 4;
                  then reconsider s=p3.nx as Element of K by A43;
                   (x in Seg (k+1) implies s=p3.x)&
                 (not x in Seg (k+1) implies s=0.K) by A42;
                 hence ex y being set st y in the carrier of K & P6[x,y];
                 end;
                 case not x in Seg (k+1);
                  hence ex y being set st y in the carrier of K & P6[x,y];
                 end;
                 end;
                hence ex y being set st y in the carrier of K & P6[x,y];
                end;
               ex f6 being Function of NAT,the carrier of K st
                    for x being set st x in NAT holds P6[x,f6.x]
                                    from FUNCT_2:sch 1(A41);
              then consider f6 being Function of NAT,the carrier of K such that
              A44:  for x being set st x in NAT holds P6[x,f6.x];
              A45: rng ((Path_matrix(p,A))) c= the carrier of K
                                              by FINSEQ_1:def 4;
               1<=k+1+1 & k+1+1 <=n by A39,NAT_1:12,13;
              then k+1+1 in Seg len (Path_matrix(p,A)) by A29;
              then k+1+1 in dom (Path_matrix(p,A)) by FINSEQ_1:def 3;
              then (Path_matrix(p,A)).(k + 1+1) in
                      rng (Path_matrix(p,A)) by FUNCT_1:def 5;
              then [f6.(k+1),(Path_matrix(p,A)).(k + 1+1)]
                             in [:the carrier of K, the carrier of K:]
                                  by A45,ZFMISC_1:def 2;
              then reconsider e=(the mult of K).
                  (f6.(k+1),(Path_matrix(p,A)).(k + 1+1))
                     as Element of K by FUNCT_2:7;
             reconsider q3=p3^<* e *> as FinSequence of K;
            A46: len q3=(len p3) + len (<*e*>) by FINSEQ_1:35
                 .=k+1+1 by A40,FINSEQ_1:57;
                       1<=k+1 by NAT_1:12;
            then 1 in Seg len p3 by A40;
            then 1 in dom p3 by FINSEQ_1:def 3;
            then A47: q3.1=(Path_matrix(p,(A))).1 by A40,FINSEQ_1:def 7;
             for n3 being Element of NAT st 0 <> n3 & n3 < k+1+1 & n3<n
              holds
               q3.(n3 + 1) = (the mult of K).
                  (q3.n3,(Path_matrix(p,(A))).(n3 + 1))
            proof let n3 be Element of NAT;
              assume A48: 0 <> n3 & n3 < k+1+1 & n3<n;
                 now per cases;
                case A49: n3<k+1;
                  then A50: p3.(n3 + 1) = (the mult of K).
                  (p3.n3,(Path_matrix(p,(A))).(n3 + 1)) by A40,A48;
                   1<=n3+1 & n3+1<=k+1 by A49,NAT_1:12,13;
                  then n3+1 in Seg len p3 by A40;
                  then n3+1 in dom p3 by FINSEQ_1:def 3;
                  then A51: p3.(n3+1)=q3.(n3+1) by FINSEQ_1:def 7;
                   0<n3 by A48;
                  then 0+1<=n3 by NAT_1:13;
                  then n3 in Seg len p3 by A40,A49;
                  then n3 in dom p3 by FINSEQ_1:def 3;
                 hence q3.(n3 + 1) = (the mult of K).
                  (q3.n3,(Path_matrix(p,(A))).(n3 + 1))
                                     by A50,A51,FINSEQ_1:def 7;
                end;
                case n3>=k+1;
                  then A52: n3+1>k+1 by NAT_1:13;
                  A53: n3+1<=k+1+1 by A48,NAT_1:13;
                   n3+1>=k+1+1 by A52,NAT_1:13;
                  then A54: n3+1=k+1+1 by A53,XXREAL_0:1;
                 1<=k+1 by NAT_1:12;
                then A55: k+1 in Seg (k+1);
                then k+1 in dom p3 by A40,FINSEQ_1:def 3;
                then A56: q3.(k+1)=p3.(k+1) by FINSEQ_1:def 7;
                 (q3).(n3+1) = (<*e*>).(n3+1 - (k+1))
                                  by A40,A46,A52,A53,FINSEQ_1:37
                             .= e by A54,FINSEQ_1:def 8;
                 hence q3.(n3 + 1) = (the mult of K).
                  (q3.n3,(Path_matrix(p,(A))).(n3 + 1)) by A44,A54,A55,A56;
                end;
                end;
               hence q3.(n3 + 1) = (the mult of K).
                  (q3.n3,(Path_matrix(p,(A))).(n3 + 1));
            end;
            hence P3[k+1] by A46,A47;
            end;
            case k+1>=n;
            hence P3[k+1];
            end;
            end;
           hence P3[k+1];
          end;
         A57: for k being Element of NAT holds P3[k] from NAT_1:sch 1(A36,A37);
         A58: n-1=n-'1 by A1,BINARITH:50;
          n<n+1 by NAT_1:13;
         then n-1<n+1-1 by REAL_1:92;
         then consider p3 being FinSequence of K such that
         A59: len p3=n-'1+1 &
            p3.1=(Path_matrix(p,A)).1 &
         (for n3 being Element of NAT st 0 <> n3 & n3 < n-'1+1 & n3<n holds
           p3.(n3 + 1) = (the mult of K).
                  (p3.n3,(Path_matrix(p,(A))).(n3 + 1))) by A57,A58;
        reconsider Ab=NAT as non empty set;
        defpred P[set,set] means
           ($1 in Seg n implies $2=p3.$1)&
           (not $1 in Seg n implies $2=0.K);
A60:   for x3 being Element of Ab
                 ex y3 being Element of K st P[x3,y3]
         proof let x3 be Element of Ab;
            now per cases;
           case A61: x3 in Seg n;
             reconsider m=x3 as Element of NAT;
             A62: rng p3 c= the carrier of K by FINSEQ_1:def 4;
              x3 in dom p3 by A58,A59,A61,FINSEQ_1:def 3;
             then p3.x3 in rng p3 by FUNCT_1:def 5;
            hence ex y3 being Element of K st P[x3,y3] by A61,A62;
           end;
           case not x3 in Seg n;
            hence ex y3 being Element of K st P[x3,y3];
           end;
           end;
          hence ex y3 being Element of K st P[x3,y3];
         end;
         ex f4 being Function of Ab,the carrier of K st
         (for x2 being Element of Ab holds P[x2,f4.x2])
                   from FUNCT_2:sch 3(A60);
        then consider f4 being Function of NAT,the carrier of K such that
        A63: for x4 being Element of NAT holds
            (x4 in Seg n implies f4.x4=p3.x4)&
           (not x4 in Seg n implies f4.x4=0.K);
          1 in Seg n by A1;
        then A64: f4.1=(Path_matrix(p,A)).1 by A59,A63;
        A65: for n3 being Element of NAT
          st 0 <> n3 & n3 < len (Path_matrix(p,A)) holds
           f4.(n3 + 1) = (the mult of K).
                  (f4.n3,(Path_matrix(p,A)).(n3 + 1))
         proof let n3 be Element of NAT;
           assume A66: 0 <> n3 & n3 < len (Path_matrix(p,A));
            then n3>0;
            then A67: 0+1<=n3 by NAT_1:13;
            then A68: 1<n3+1 by NAT_1:13;
             n3+1<=len (Path_matrix(p,A)) by A66,NAT_1:13;
            then n3+1 in Seg n by A29,A68;
            then A69: f4.(n3+1)=p3.(n3+1) by A63;
            A70: p3.(n3 + 1) = (the mult of K).
                  (p3.n3,(Path_matrix(p,A)).(n3 + 1)) by A29,A58,A59,A66;
             n3 in Seg n by A29,A66,A67;
          hence f4.(n3 + 1) = (the mult of K).
                  (f4.n3,(Path_matrix(p,A)).(n3 + 1)) by A63,A69,A70;
         end;
         defpred P5[Element of NAT] means f4.(i0+$1)=0.K;
         A71: 0<i0 by A21,A23,FINSEQ_1:3;
          now per cases;
         case i0<=1;
           then i0=1 by A25,XXREAL_0:1;
          hence P5[0] by A1,A24,A26,A27,A30,A64,Def2;
         end;
         case A72: i0>1;
           then A73: i0-1> 1-1 by REAL_1:92;
            i0<i0+1 by NAT_1:13;
           then i0-1<i0+1-1 by REAL_1:92;
           then A74: i0-1<len (Path_matrix(p,A)) by A25,A29,XXREAL_0:2;
          reconsider a=f4.(i0-'1) as Element of K;
           i0-'1=i0-1 by A72,BINARITH:50;
          then i0-'1+1=i0;
          then f4.i0 = (the mult of K).
                  (f4.(i0-'1),(Path_matrix(p,A)).i0) by A65,A73,A74
                  .= a*(0.K) by A31,GROUP_1:def 1
                  .= 0.K by VECTSP_1:36;
           hence P5[0];
          end;
          end;
           then A75: P5[0];
           A76: for k being Element of NAT st P5[k] holds P5[k+1]
           proof let k be Element of NAT;
            assume A77: P5[k];
             A78: 1<=1+(i0+k) by NAT_1:12;
              now per cases;
             case A79: i0+k+1<=n;
               A80: i0<=i0+k by NAT_1:12;
               A81: i0+k<n by A79,NAT_1:13;
               A82: 0<i0+k by A21,A23,A80,FINSEQ_1:3;
                0+1<=i0+k by A71,A80,NAT_1:13;
               then i0+k in Seg n by A81;
               then A83: p3.(i0+k)=0.K by A63,A77;
                1<=1+(i0+k) by NAT_1:12;
               then i0+k+1 in Seg len (Path_matrix(p,A))
                                  by A29,A79;
               then i0+k+1 in dom (Path_matrix(p,A)) by FINSEQ_1:def 3;
               then A84: (Path_matrix(p,A)).(i0+k+1)
                            in rng (Path_matrix(p,A)) by FUNCT_1:def 5;
                rng (Path_matrix(p,A)) c= the carrier of K
                                   by FINSEQ_1:def 4;
               then reconsider b=(Path_matrix(p,A)).(i0+k+1)
                             as Element of K by A84;
                i0+k+1 in Seg n by A78,A79;
               then f4.(i0+k+1)=p3.(i0+k+1) by A63
                          .=(the mult of K).
                   (p3.(i0+k),(Path_matrix(p,A)).(i0+k+1)) by A58,A59,A81,A82
                         .= (0.K)*b by A83,GROUP_1:def 1
                         .= 0.K by VECTSP_1:36;
              hence f4.(i0+(k+1))=0.K;
             end;
             case i0+k+1>n;
              then not (i0+(k+1) in Seg n) by FINSEQ_1:3;
              hence f4.(i0+(k+1))=0.K by A63;
             end;
             end;
            hence P5[k+1];
           end;
            for k being Element of NAT holds P5[k] from NAT_1:sch 1(A75,A76);
           then A85: P5[n-'i0];
            n-'i0 =n-i0 by A25,BINARITH:50;
       hence f2.p = -((the mult of K) $$ Path_matrix(p,A),p)
           by A1,A14,A28,A29,A64,A65,A85,FINSOP_1:3;
      end;
      end;
     hence f2.p = -((the mult of K) $$ Path_matrix(p,(A)),p);
    end;
   hence (Path_product(A)).x=f2.x by MATRIX_3:def 8;
  end;
  then A86: Path_product(A)=f2 by A12,FUNCT_1:9;
  A87: now assume A88: idseq n in B;
    thus G0.(B)=IFIN(idseq n,B,k1,0.K) by A9
        .=(the mult of K)$$ (diagonal_of_Matrix A) by A88,Def1;
   end;
  A89: for e being Element of Y st e is_a_unity_wrt F holds G0.({}) = e
    proof let e be Element of Y;assume e is_a_unity_wrt F;
      then A90: F.(0.K,e)=0.K by BINOP_1:11;
       0.K is_a_unity_wrt F by FVSUM_1:8;
      then A91: F.(0.K,e)=e by BINOP_1:11;
      A92: {} in Fin X by FINSUB_1:18;
       IFIN(idseq n,{},k1,0.K)=0.K by Def1;
     hence G0.({}) = e by A9,A90,A91,A92;
    end;
  A93: for x being Element of X holds G0.({x}) = f.x
   proof let x be Element of X;
      now per cases;
     case A94: x=idseq n;
       A95: f2.x=F2(x) by A11
          .=k1 by A94,FUNCOP_1:def 8;
        idseq n in {x} by A94,TARSKI:def 1;
       then IFIN(idseq n,{x},k1,0.K)=k1 by Def1;
      hence G0.({.x.})=f2.x by A9,A95;
     end;
     case A96: x <> idseq n;
       A97: f2.x=F2(x) by A11
          .=0.K by A96,FUNCOP_1:def 8;
        not idseq n in {x} by A96,TARSKI:def 1;
       then IFIN(idseq n,{x},k1,0.K)=0.K by Def1;
      hence G0.({.x.})=f2.x by A9,A97;
     end;
     end;
    hence G0.({x}) = f.x by A12,A13,FUNCT_1:9;
   end;
    for B' being Element of Fin X st B' c= B & B' <> {}
      for x being Element of X st x in B\B' holds G0.(B' \/ {x})
                    = F.(G0.B',f.x)
   proof let B' be Element of Fin X;
     assume B' c= B & B' <> {};
     thus for x being Element of X st x in B\B' holds G0.(B' \/ {x})
                    = F.(G0.B',f.x)
     proof let x be Element of X;
       assume A98: x in B\B';
        A99: now assume A100: idseq n in B' \/ {x};
         thus G0.(B' \/ {x})=IFIN(idseq n,B' \/ {.x.},k1,0.K) by A9
                            .=k1 by A100,Def1;
        end;
        A101: now assume A102: not idseq n in B' \/ {x};
         thus G0.(B' \/ {x})=IFIN(idseq n,B' \/ {.x.},k1,0.K) by A9
                            .=0.K by A102,Def1;
         end;
        A103: now assume A104: idseq n in B';
         thus G0.(B')=IFIN(idseq n,B',k1,0.K) by A9
                            .=k1 by A104,Def1;
        end;
        A105: now assume A106: not idseq n in B';
         thus G0.(B')=IFIN(idseq n,B',k1,0.K) by A9
                            .=0.K by A106,Def1;
        end;
        A107: 0.K is_a_unity_wrt F by FVSUM_1:8;
        A108: now assume idseq n in B' \/ {x};
          then A109: idseq n in B' or idseq n in {x} by XBOOLE_0:def 2;
           now per cases by A109,TARSKI:def 1;
          case A110: idseq n in B';
           A111: not x in B' by A98,XBOOLE_0:def 4;
            f.x=F2(x) by A11,A86
              .=0.K by A110,A111,FUNCOP_1:def 8;
           hence F.(G0.B',f.x)=k1 by A103,A107,A110,BINOP_1:11;
          end;
          case A112: idseq n=x;
             f.x=F2(x) by A11,A86
              .=k1 by A112,FUNCOP_1:def 8;
           hence F.(G0.B',f.x)=k1 by A98,A105,A107,A112,BINOP_1:11
,XBOOLE_0:def 4;
          end;
          end;
         hence F.(G0.B',f.x)=k1;
        end;
         now assume A113: not idseq n in B' \/ {x};
          then not idseq n in {x} by XBOOLE_0:def 2;
          then A114: not idseq n=x by TARSKI:def 1;
           f.x=F2(x) by A11,A86
              .=0.K by A114,FUNCOP_1:def 8;
         hence F.(G0.B',f.x)=0.K by A105,A107,A113,BINOP_1:11,XBOOLE_0:def 2;
        end;
       hence G0.(B' \/ {x}) = F.(G0.B',f.x) by A99,A101,A108;
     end;
   end;
  then (the add of K) $$ (FinOmega(Permutations(n)),Path_product(A))=
  (the mult of K)$$ (diagonal_of_Matrix A)
                                       by A5,A6,A87,A89,A93,MATRIX_2:def 13
,SETWISEO:def 3;
 hence thesis by MATRIX_3:def 9;
end;

theorem Th18: for p being Element of Permutations(n) holds
 p" is Element of Permutations(n)
proof let p be Element of Permutations(n);
   p" is Element of Group_of_Perm(n) by MATRIX_2:26;
 hence p" is Element of Permutations(n) by MATRIX_2:def 13;
end;

definition let n;let p be Element of Permutations(n);
 redefine func p" -> Element of Permutations(n);
 coherence by Th18;
end;

theorem Th19: for K being Field,A being Matrix of n,K holds
 A@ is Matrix of n,K;

definition let n be Nat,K be Field;
  let A be Matrix of n,K;
  func A@ -> Matrix of n,K equals
     (A qua Matrix of K)@;
  coherence by Th19;
end;

theorem  for G being Group,
f1,f2 being FinSequence of G
holds (Product (f1^f2))" = (Product f2)" * (Product f1)"
proof let G be Group,f1,f2 be FinSequence of G;
 thus (Product (f1^f2))" = ((Product f1) * (Product f2))" by GROUP_4:8
                        .= (Product f2)" * (Product f1)" by GROUP_1:25;
end;

definition let G be Group, f be FinSequence of G;
  func f" -> FinSequence of G means :Def4:
    len it = len f & for i being Element of NAT st i in dom f holds
      it/.i = (f/.i)";
existence
proof
   deffunc F(Nat) = (f/.$1)";
    ex p being FinSequence st len p = len f &
    for k being Nat st k in Seg len f holds p.k=F(k)
    from FINSEQ_1:sch 2;
   then consider p being FinSequence such that
A1: len p = len f & for k being Nat st k in Seg len f
holds p.k=F(k);
A2: dom p = dom f by A1,FINSEQ_3:31;
     rng p c= the carrier of G
     proof let y be set;assume y in rng p;
       then consider x being set such that
       A3: x in dom p & y=p.x by FUNCT_1:def 5;
       A4: x in Seg len p by A3,FINSEQ_1:def 3;
       reconsider k=x as Element of NAT by A3;
        p.k=(f/.k)" by A1,A4;
      hence y in the carrier of G by A3;
     end;
    then reconsider q=p as FinSequence of G by FINSEQ_1:def 4;
     for i being Element of NAT st i in dom f holds q/.i=(f/.i)"
    proof let i be Element of NAT;
      assume A5: i in dom f;
then A6:   i in Seg len f by FINSEQ_1:def 3;
     thus q/.i=p.i by A2,A5,PARTFUN1:def 8 .=(f/.i)" by A1,A6;
    end;
  hence thesis by A1;
end;
uniqueness
 proof
  thus for g1,g2 being FinSequence of G st
(len g1=len f & for i being Element of NAT st i in dom f holds
  g1/.i=(f/.i)")&
(len g2=len f & for j being Element of NAT st j in dom f holds
  g2/.j=(f/.j)")
   holds g1=g2
  proof let g1,g2 be FinSequence of G;
   assume A7: (len g1=len f &
      for i being Element of NAT st i in dom f holds g1/.i=(f/.i)")&
     (len g2=len f & for j being Element of NAT st j in dom f
     holds g2/.j=(f/.j)");
then A8:  dom g1 = dom f by FINSEQ_3:31;
      for k being Nat st 1<= k & k<= len g1 holds g1.k=g2.k
     proof let k be Nat;
A9:    k in NAT by ORDINAL1:def 13;
       assume A10: 1<= k & k<= len g1;
       then k in Seg len g1 by A9;
then A11:    k in dom g1 by FINSEQ_1:def 3;
        k in Seg len g2 by A7,A9,A10;
then A12:    k in dom g2 by FINSEQ_1:def 3;
A13:    g1.k=g1/.k by A11,PARTFUN1:def 8;
A14:    g2.k=g2/.k by A12,PARTFUN1:def 8;
        g1/.k=(f/.k)" by A7,A8,A11;
      hence g1.k=g2.k by A7,A8,A11,A13,A14;
     end;
   hence g1=g2 by A7,FINSEQ_1:18;
  end;
 end;
end;

theorem Th21: for G being Group holds
(<*>(the carrier of G))"=<*>(the carrier of G)
proof let G be Group;
   len (<*>(the carrier of G))=0 by FINSEQ_1:32;
  then len ((<*>(the carrier of G))" qua FinSequence of G)=0 by Def4;
 hence (<*>(the carrier of G))"=<*>(the carrier of G) by FINSEQ_1:32;
end;

theorem Th22: for G being Group, f,g being FinSequence of G
 holds (f^g)"=(f")^(g")
proof let G be Group, f,g be FinSequence of G;
  A1: len ((f^g)")=len (f^g) by Def4;
  A2: len ((f^g)")=len (f^g) by Def4.= len f+len g by FINSEQ_1:35;
  A3: len f+len g=len (f") +len g by Def4
             .=len (f") + len (g") by Def4
             .= len ((f")^(g")) by FINSEQ_1:35;
   for i being Nat st 1<=i & i<= len ((f^g)") holds
    ((f^g)").i=((f")^(g")).i
  proof
   let i be Nat;
    assume A4: 1<=i & i<= len ((f^g)");
A5: i in NAT by ORDINAL1:def 13;
      now per cases;
     case len f>0;
A6:  len (f")=len f by Def4;
A7:  len (g")=len g by Def4;
      len ((f^g)")=len (f^g) by Def4;
then A8:  dom ((f^g)")=dom (f^g) by FINSEQ_3:31;
      i in Seg len ((f^g)") by A4,A5;
then A9: i in dom ((f^g)") by FINSEQ_1:def 3;
then A10: (((f^g)")).i=(((f^g)"))/.i by PARTFUN1:def 8
                .= ((f^g)/.i)" by A8,A9,Def4;
       now per cases;
      case i<=len f;
        then A11: i in Seg len f by A4,A5;
        then A12: i in dom f by FINSEQ_1:def 3;
        A13: i in dom (f") by A6,A11,FINSEQ_1:def 3;
         (f^g)/.i=(f^g).i by A8,A9,PARTFUN1:def 8
               .=f.i by A12,FINSEQ_1:def 7
               .=f/.i by A12,PARTFUN1:def 8;
        then ((f^g)/.i)" =(f")/.i by A12,Def4
                   .=(f").i by A13,PARTFUN1:def 8;
       hence (((f^g)")).i=((f")^(g")).i by A10,A13,FINSEQ_1:def 7;
      end;
      case A14: i>len f;
       then A15: i-'len f=i-len f by BINARITH:50;
         1+len f<=i by A14,NAT_1:13;
        then A16: 1+len f -len f<=i-len f by XREAL_1:11;
         i-len f <= len g+len f -len f by A2,A4,XREAL_1:11;
        then A17: i-'len f in Seg len g by A15,A16;
        then A18: i-'len f in dom g by FINSEQ_1:def 3;
        A19: i-'len f in dom (g") by A7,A17,FINSEQ_1:def 3;
         (f^g)/.i=(f^g).i by A8,A9,PARTFUN1:def 8
               .=g.(i-len (f)) by A1,A4,A14,FINSEQ_1:37
               .=g/.(i-'len f) by A15,A18,PARTFUN1:def 8;
        then ((f^g)/.i)" =(g")/.(i-'len f) by A18,Def4
                   .=(g").(i-len (f")) by A6,A15,A19,PARTFUN1:def 8;
       hence (((f^g)")).i=((f")^(g")).i by A2,A3,A4,A6,A10,A14,FINSEQ_1:37;
      end;
      end;
      hence (((f^g)")).i=((f")^(g")).i;
      end;
      case len f<=0;
        then len f=0;
        then A20: f={} by FINSEQ_1:25;
        then A21: ((f^g)")=g" by FINSEQ_1:47;
         f"=<*>(the carrier of G) by A20,Th21;
       hence (((f^g)")).i=((f")^(g")).i by A21,FINSEQ_1:47;
      end;
      end;
    hence (((f^g)")).i=((f")^(g")).i;
  end;
 hence (f^g)"=(f")^(g") by A2,A3,FINSEQ_1:18;
end;

theorem Th23: for G being Group,a being Element of G
holds (<*a*>)" =<* a" *>
proof let G be Group,a be Element of G;
  A1: len ((<*a*>)")=len (<* a *>) by Def4 .=1 by FINSEQ_1:56
                   .=len (<* a" *>) by FINSEQ_1:56;
   for i being Nat st 1<=i & i<= len <*a" *> holds
    ((<*a*>)").i=(<* a" *>).i
   proof let i be Nat;
    assume A2: 1<=i & i<= len <*a" *>;
A3: i in NAT by ORDINAL1:def 13;
     A4: len <*a" *>=1 by FINSEQ_1:56;
     then A5: i=1 by A2,XXREAL_0:1;
      len (<* a *>) =1 by FINSEQ_1:56;
     then i in Seg len (<*a*>) by A2,A3,A4;
     then A6: i in dom (<*a*>) by FINSEQ_1:def 3;
      i in Seg len ((<*a*>)") by A1,A2,A3;
     then i in dom ((<*a*>)") by FINSEQ_1:def 3;
     then A7: ((<*a*>)").i=((<*a*>)")/.i by PARTFUN1:def 8
                .= ((<* a *>)/.i)" by A6,Def4;
      (<* a *>)/.i= ((<* a *>).i) by A6,PARTFUN1:def 8
                .= a by A5,FINSEQ_1:57;
    hence ((<*a*>)").i=(<* a" *>).i by A5,A7,FINSEQ_1:57;
   end;
 hence (<*a*>)" =<* a" *> by A1,FINSEQ_1:18;
end;

theorem Th24: for G being Group,
f being FinSequence of G
holds Product (f^(Rev f)")=1_G
proof let G be Group,f be FinSequence of G;
  defpred P[Element of NAT] means
    for g being FinSequence of G st
    len g <=$1 holds Product (g^(Rev g)")=1_G;
   for g being FinSequence of G st
    len g <= 0 holds Product (g^(Rev g)")=1_G
  proof let g be FinSequence of G;
   assume len g <= 0;
    then len g=0;
then A1: g=<*>(the carrier of G) by FINSEQ_1:32;
    then Rev g=<*>(the carrier of G) by FINSEQ_5:82;
    then (Rev g)" = <*>(the carrier of G) by Th21;
     then g^(Rev g)"=<*>(the carrier of G) by A1,FINSEQ_1:47;
   hence Product (g^(Rev g)")=1_G by GROUP_4:11;
  end;
  then A2: P[0];
  A3: for k being Element of NAT st P[k] holds P[k+1]
  proof let k be Element of NAT;
   assume A4: P[k];
     for g being FinSequence of G st
    len g <= k+1 holds Product (g^(Rev g)")=1_G
    proof let g be FinSequence of G;
     assume A5: len g <= k+1;
       now per cases by A5,REAL_1:def 5;
      case len g<k+1; then len g<=k by NAT_1:13;
       hence Product (g^(Rev g)")=1_G by A4;
      end;
      case A6: len g=k+1;
        then A7: k< len g by NAT_1:13;
        reconsider h=g|k as FinSequence of G;
        A8: len h=k by A7,FINSEQ_1:80;
        A9: g=h^<* g/.(k+1) *> by A6,FINSEQ_5:24;
        then Rev g=<* g/.(k+1) *> ^ (Rev h) by FINSEQ_5:66;
        then (Rev g)"=(<* g/.(k + 1) *>)" ^ (Rev h)" by Th22;
        then A10: g^(Rev g)"=h^ (<* g/.(k+1) *> ^
                           ((<* g/.(k + 1) *>)" ^ (Rev h)"))
                                   by A9,FINSEQ_1:45
                 .=h^ (<* g/.(k+1) *> ^ (<* g/.(k + 1) *>)" ^ (Rev h)")
                                   by FINSEQ_1:45;
        A11: Product (<* g/.(k+1) *> ^ (<* g/.(k + 1) *>)")
        =Product (<* g/.(k+1) *>) * Product ((<* g/.(k + 1) *>)")
                                   by GROUP_4:8
       .=(g/.(k+1))*Product ((<* g/.(k + 1) *>)") by FINSOP_1:12
       .=(g/.(k+1))*Product ((<* (g/.(k + 1))" *>)) by Th23
       .=(g/.(k+1))*(g/.(k + 1))" by FINSOP_1:12
       .=1_G by GROUP_1:def 6;
        Product (g^(Rev g)")
         =Product (h)
         * Product (<* g/.(k+1) *> ^ (<* g/.(k + 1) *>)" ^ (Rev h)")
                            by A10,GROUP_4:8
        .=Product (h) * (Product (<* g/.(k+1) *> ^ (<* g/.(k + 1) *>)" )*
                            Product ((Rev h)")) by GROUP_4:8
        .=Product (h) * Product ((Rev h)") by A11,GROUP_1:def 5
        .=Product (h^((Rev h)")) by GROUP_4:8;
       hence Product (g^(Rev g)")=1_G by A4,A8;
      end;
      end;
     hence Product (g^(Rev g)")=1_G;
    end;
   hence P[k+1];
  end;
   for j being Element of NAT holds P[j] from NAT_1:sch 1(A2,A3);
  then P[len f];
 hence Product (f^(Rev f)")=1_G;
end;

theorem Th25: for G being Group,f being FinSequence of G
holds Product ((Rev f)" ^f)=1_G
proof let G be Group,f be FinSequence of G;
A1: len f=len Rev f by FINSEQ_5:def 3;
A2: len Rev f = len ((Rev f)") by Def4;
then A3: dom Rev f = dom ((Rev f)") by FINSEQ_3:31;
A4: len ((Rev f)") = len Rev ((Rev f)") by FINSEQ_5:def 3;
A5: len Rev ((Rev f)")=len ((Rev ((Rev f)"))") by Def4;
then A6: dom Rev ((Rev f)")=dom ((Rev ((Rev f)"))") by FINSEQ_3:31;
     for i being Nat st 1<= i & i <= len f
    holds f.i = ((Rev ((Rev f)") )").i
    proof let i be Nat;
A7:   i in NAT by ORDINAL1:def 13;
      assume A8: 1<= i & i <= len f;
then A9:  i in Seg len f by A7;
then A10:   i in dom f by FINSEQ_1:def 3;
A11:  i in dom ((Rev ((Rev f)"))") by A1,A2,A4,A5,A9,FINSEQ_1:def 3;
A12:   len f-i=len f-'i by A8,BINARITH:50;
       len f-i+1=len f-'i+1 by A8,BINARITH:50;
      then reconsider j=len f-i+1 as Element of NAT;
      A13: len f-i+1>=0+1 by A12,XREAL_1:8;
       i-1>=0 by A8,XREAL_1:50;
      then len f +0 <=len f+(i-1) by XREAL_1:9;
       then len f-(i-1)<=len f+(i-1)-(i-1) by XREAL_1:15;
       then j in Seg len f by A13;
then A14:    j in dom ((Rev f)") by A1,A2,FINSEQ_1:def 3;
A15:    i+j=len f+1;
A16:   j+i=len f+1;
       reconsider g=(Rev f)" as FinSequence of G;
        f.i=f/.i by A10,PARTFUN1:def 8 .= (Rev f)/.j by A10,A15,FINSEQ_5:69
         .= ((Rev f)/.j)"" by GROUP_1:19
         .= (((Rev f)")/.j)" by A3,A14,Def4
         .= ((Rev ((Rev f)"))/.i)" by A1,A2,A7,A14,A16,FINSEQ_5:69
         .= ((Rev ((Rev f)"))")/.i by A6,A11,Def4
         .=((Rev ((Rev f)"))").i by A11,PARTFUN1:def 8;
      hence f.i = ((Rev ((Rev f)"))").i;
    end;
   then (Rev ((Rev f)") )"=f by A1,A2,A4,A5,FINSEQ_1:18;
 hence Product ((Rev f)" ^f)=1_G by Th24;
end;

theorem Th26: for G being Group,f being FinSequence of G
holds (Product f)" = Product ((Rev f)")
proof let G be Group,f be FinSequence of G;
   A1: Product((Rev f)" ^ f)=1_G by Th25;
    Product(f ^ (Rev f)")=1_G by Th24;
   then (Product f) * Product ((Rev f)") = 1_G &
          Product ((Rev f)") * (Product f) = 1_G by A1,GROUP_4:8;
 hence (Product f)" = Product ((Rev f)") by GROUP_1:12;
end;

theorem Th27: for ITP being Element of Permutations(n),
ITG being Element of Group_of_Perm(n) st ITG=ITP & n>=1
holds ITP"=ITG"
proof let ITP be Element of Permutations(n),
ITG be Element of Group_of_Perm(n);
 assume A1: ITG=ITP & n>=1;
  A2: ITP is Permutation of Seg n by MATRIX_2:def 11;
  reconsider qf=ITP as Function of Seg n,Seg n by MATRIX_2:def 11;
   1 in Seg n by A1;
  then A3: dom qf=Seg n by FUNCT_2:def 1;
   rng qf = Seg n by A2,FUNCT_2:def 3;
  then A4: ITP"*ITP=id Seg n & ITP*ITP"=id Seg n by A3,FUNCT_1:61;
  reconsider pf=ITP" as Element of Group_of_Perm n by MATRIX_2:def 13;
  A5: (the mult of (Group_of_Perm n)).(ITG,pf)=(ITP")*ITP
                                    by A1,MATRIX_2:def 13;
  A6: (the mult of (Group_of_Perm n)).(pf,ITG)=ITP*(ITP")
                                    by A1,MATRIX_2:def 13;
  A7: idseq n=1_Group_of_Perm n by MATRIX_2:28;
  A8: ITG*pf=(the mult of (Group_of_Perm(n))).(ITG,pf) by GROUP_1:def 1;
   pf*ITG=(the mult of (Group_of_Perm(n))).(pf,ITG) by GROUP_1:def 1;
 hence ITP"=ITG" by A4,A5,A6,A7,A8,GROUP_1:def 6;
end;

Lm2: for n being Element of NAT,
  IT being Element of Permutations(n) st
  IT is even & n>=1 holds IT" is even
proof let n be Element of NAT,
  IT be Element of Permutations(n);
 assume A1: IT is even & n>=1;
  A2: len Permutations n=n by MATRIX_2:20;
  then consider l being FinSequence of Group_of_Perm n such that
A3: (len l)mod 2=0 & IT=Product l &
  for i st i in dom l ex q being Element of Permutations n
   st l.i=q & q is_transposition by A1,MATRIX_2:def 15;
  reconsider lv=(Rev l)" as FinSequence of Group_of_Perm n;
  A4: len l =len Rev l by FINSEQ_5:def 3;
  then A5: len l=len lv by Def4;
  set m=len l;
  reconsider ITP=IT as Element of Permutations n;
  reconsider ITG=ITP as Element of Group_of_Perm n by MATRIX_2:def 13;
   ITG"=ITP" by A1,Th27;
  then A6: IT"=Product lv by A3,Th26;
   for i st i in dom lv ex q2 being Element of Permutations(n)
       st lv.i=q2 & q2 is_transposition
  proof let i;
    assume A7: i in dom lv;
     then i in Seg len lv by FINSEQ_1:def 3;
     then A8: 1<=i & i<=m by A5,FINSEQ_1:3;
     then A9: m-i>=0 by XREAL_1:50;
     then A10: m-i+1>=0+1 by XREAL_1:9;
      m+1<=m+i by A8,XREAL_1:9;
     then A11: m+1-i<=m+i-i by XREAL_1:11;
      m-i+1=m-'i+1 by A9,BINARITH:def 3;
     then reconsider j=len l-i+1 as Element of NAT;
     reconsider ii=i as Element of NAT by ORDINAL1:def 13;
     A12: i+j=len l+1;
      j in Seg len l by A10,A11;
     then A13: j in dom l by FINSEQ_1:def 3;
     then consider q being Element of Permutations n such that
     A14: l.j=q & q is_transposition by A3;
      lv.i=lv/.i by A7,PARTFUN1:def 8;
     then reconsider qq=lv.i as Element of Permutations(n) by MATRIX_2:def 13;
     consider i1,j1 being Nat such that
     A15:  i1 in dom q & j1 in dom q & i1<>j1 & q.i1=j1 & q.j1=i1 &
      for k being Nat st k <>i1 & k<>j1 & k in dom q holds q.k=k
                               by A14,MATRIX_2:def 14;
      reconsider qf=q as Function of Seg n,Seg n by MATRIX_2:def 11;
A16:   Seg n <> {} by A1,FINSEQ_1:3;
then A17:  dom qf=Seg n by FUNCT_2:def 1;
      reconsider qqf=qq as Function of Seg n,Seg n by MATRIX_2:def 11;
A18:  dom qqf=Seg n by A16,FUNCT_2:def 1;
A19:  l.j=l/.j by A13,PARTFUN1:def 8;
A20:   i in dom Rev l by A4,A5,A7,FINSEQ_3:31;
A21:  qq=(Rev l)"/.i by A7,PARTFUN1:def 8
                    .=((Rev l)/.ii)" by A20,Def4
                    .=(l/.j)" by A12,A13,FINSEQ_5:69
                    .=q" by A1,A14,A19,Th27;
      A22: for k being Element of NAT st k <>j1 & k<>i1 & k in dom qq
      holds qq.k=k
      proof let k be Element of NAT;
        assume A23: k <>j1 & k<>i1 & k in dom qq;
        then q.k=k by A15,A17,A18;
       hence qq.k=k by A17,A18,A21,A23,FUNCT_1:54;
      end;
      A24: l.j=l/.j by A13,PARTFUN1:def 8;
      A25: qq=(Rev l)"/.i by A7,PARTFUN1:def 8
            .=((Rev l)/.ii)" by A20,Def4
            .=(l/.j)" by A12,A13,FINSEQ_5:69
            .=q" by A1,A14,A24,Th27;
      ex i,j st i in dom qq & j in dom qq & i<>j & qq.i=j & qq.j=i &
     for k st k <>i & k<>j & k in dom qq holds qq.k=k
     proof
       take i=i1,j=j1;
       thus i in dom qq & j in dom qq & i<>j & qq.i=j & qq.j=i
       by A15,A17,A18,A25,FUNCT_1:54;
       let k;
        k in NAT by ORDINAL1:def 13;
       hence thesis by A22;
     end;
     then qq is_transposition by MATRIX_2:def 14;
    hence ex q2 being Element of Permutations n
       st lv.i=q2 & q2 is_transposition;
  end;
 hence IT" is even by A2,A3,A5,A6,MATRIX_2:def 15;
end;

theorem Th28: for n being Element of NAT,
  IT being Element of Permutations(n) st n>=1 holds
  IT is even iff IT" is even
proof let n be Element of NAT,
  IT be Element of Permutations(n);
  assume A1: n>=1;
 hence IT is even implies IT" is even by Lm2;
   now assume IT" is even;
    then IT"" is even by A1,Lm2;
   hence IT is even by FUNCT_1:65;
  end;
 hence thesis;
end;

theorem Th29: for n being Element of NAT,K being Field,
  p being Element of Permutations(n), x being Element of K
 st n>=1 holds -(x,p) = -(x,p")
proof let n be Element of NAT,K be Field,
  p be Element of Permutations(n), x be Element of K;
  assume A1: n>=1;
  A2: len ((Permutations(n)))=n by MATRIX_2:20;
  reconsider pf=p as Permutation of Seg n by MATRIX_2:def 11;
  per cases;
  suppose A3: p is even;
then A4: -(x,p) = x by MATRIX_2:def 16;
     pf" is even by A1,A2,A3,Th28;
   hence -(x,p) = -(x,p") by A2,A4,MATRIX_2:def 16;
  end;
  suppose A5: not p is even;
then A6: -(x,p) = -x by MATRIX_2:def 16;
     not p" is even by A1,A5,Th28;
   hence -(x,p) = -(x,p") by A6,MATRIX_2:def 16;
  end;
end;

theorem Th30:for K being Field,f1,f2 being FinSequence of K holds
  (the mult of K) $$ (f1^f2) = ((the mult of K)$$f1)*((the mult of K)$$f2)
 proof let K be Field,f1,f2 be FinSequence of K;
   (the mult of K) is associative &
    ((the mult of K) has_a_unity or len f1 >= 1 & len f2 >= 1)
                                   by GROUP_1:31,34;
  hence (the mult of K) $$ (f1^f2)
    = (the mult of K).((the mult of K) $$ f1,(the mult of K) $$ f2)
                                   by FINSOP_1:6
    .=(the mult of K) $$ f1 * (the mult of K) $$ f2 by GROUP_1:def 1;
 end;

theorem
  Th31:for K being Field,R1,R2 be FinSequence of K
         st R1,R2 are_fiberwise_equipotent holds
      (the mult of K)$$ R1 = (the mult of K)$$ R2
proof :: The proof is copying and string relacing of EULER_2:25
  let K be Field;
  defpred P[Nat] means
    for f,g be FinSequence of K
      st f,g are_fiberwise_equipotent & len f = $1
      holds (the mult of K)$$ f = (the mult of K)$$ g;
  A1:P[0]
  proof let f,g be FinSequence of K; assume
        f,g are_fiberwise_equipotent & len f = 0;
    then len g = 0 & f = <*>(the carrier of K) by FINSEQ_1:32,RFINSEQ:16;
    hence thesis by FINSEQ_1:32;
  end;
  A2:for n st P[n] holds P[n+1]
  proof let n; assume
A3: P[n];
    let f,g be FinSequence of K; assume
    A4:f,g are_fiberwise_equipotent & len f = n+1;
    then A5:rng f = rng g & len f = len g by RFINSEQ:1,16;
     0+1<=n+1 by NAT_1:13;
    then A6:n+1 in dom f by A4,FINSEQ_3:27;
    then A7: f.(n+1) in rng f by FUNCT_1:def 5;
     rng f c= the carrier of K by FINSEQ_1:def 4;
    then reconsider a = f.(n+1) as Element of K by A7;
     a in rng g by A5,A6,FUNCT_1:def 5;
    then consider m be Nat such that
    A8:m in dom g & g.m = a by FINSEQ_2:11;
    A9:g = (g|m)^(g/^m) by RFINSEQ:21;
    A10:1<=m & m<=len g by A8,FINSEQ_3:27;
    then max(0,m-1) = m-1 by FINSEQ_2:4;
    then reconsider m1 = m-1 as Element of NAT by FINSEQ_2:5;
    set gg = g/^m, gm = g|m;
A11:len gm = m by A10,FINSEQ_1:80;
A12:m = m1+1;
    m in NAT by ORDINAL1:def 13;
    then m in Seg m by A10;
    then gm.m = a & m in dom g by A8,RFINSEQ:19;
    then A13:gm = (gm|m1)^<*a*> by A11,A12,RFINSEQ:20;
     m1<=m by A12,NAT_1:11;
    then A14:Seg m1 c= Seg m by FINSEQ_1:7;
A15:gm|m1 = g|((Seg m)/\(Seg m1)) by RELAT_1:100
             .= g|m1 by A14,XBOOLE_1:28;
    reconsider n1=n as Element of NAT by ORDINAL1:def 13;
    set fn = f|n1;
     n<=n+1 by NAT_1:11;
    then A16:len fn = n by A4,FINSEQ_1:80;
A17:f = fn ^ <*a*> by A4,RFINSEQ:20;
       now let x be set;
          card(f"{x}) = card(g"{x}) by A4,RFINSEQ:def 1;
      then card(fn"{x})+card(<*a*>"{x}) = card(((g|m1)^<*a*>^(g/^m))"{x})
                                           by A9,A13,A15,A17,FINSEQ_3:63
      .= card(((g|m1)^<*a*>)"{x})+card((g/^m)"{x}) by FINSEQ_3:63
      .= card((g|m1)"{x})+card(<*a*>"{x})+card((g/^m)"{x}) by FINSEQ_3:63
      .= card((g|m1)"{x})+card((g/^m)"{x})+card(<*a*>"{x})
      .= card(((g|m1)^(g/^m))"{x})+card(<*a*>"{x}) by FINSEQ_3:63;
      hence card(fn"{x}) = card(((g|m1)^(g/^m))"{x});
    end;
    then fn, (g|m1)^(g/^m) are_fiberwise_equipotent by RFINSEQ:def 1;
    then (the mult of K)$$ fn = (the mult of K)$$((g|m1)^gg) by A3,A16;
    hence (the mult of K)$$ f
     = (the mult of K)$$((g|m1)^gg)*(the mult of K)$$ <*a*> by A17,Th30
    .= (the mult of K)$$(g|m1)*(the mult of K)$$ gg*(the mult of K)$$ <*a*>
                                        by Th30
    .= (the mult of K)$$(g|m1)*(the mult of K)$$ <*a*>*(the mult of K)$$ gg
                                         by GROUP_1:def 4
             .= (the mult of K)$$ gm*(the mult of K)$$ gg by A13,A15,Th30
             .= (the mult of K)$$ g by A9,Th30;
  end;
  A18:for n holds P[n] from NAT_1:sch 2(A1,A2);
  let R1,R2 be FinSequence of K; assume
  A19:R1,R2 are_fiberwise_equipotent;
   len R1 = len R1;
  hence thesis by A18,A19;
end;

theorem Th32: for n being Element of NAT, K being Field,
p being Element of Permutations(n),
 f,g being FinSequence of K st n>=1 & len f=n & g=f*p holds
 f,g are_fiberwise_equipotent
proof let n be Element of NAT, K be Field,
 p be Element of Permutations(n),
 f,g be FinSequence of K;
 assume A1: n>=1 & len f=n & g=f*p;
  then f <> {} by FINSEQ_1:25;
  then dom f <> {} by FINSEQ_1:26;
  then A2: Seg n <> {} by A1,FINSEQ_1:def 3;
  A3: p is Permutation of Seg n by MATRIX_2:def 11;
  reconsider fp=p as Function of Seg n,Seg n by MATRIX_2:def 11;
   rng fp = Seg n by A3,FUNCT_2:def 3;
  then rng p c= dom f by A1,FINSEQ_1:def 3;
  then A4: dom (f*p)=dom fp by RELAT_1:46;
   dom fp =Seg n by A2,FUNCT_2:def 1;
  then reconsider h=f*p as FinSequence by A4,FINSEQ_1:def 2;
  A5: rng p=Seg n by A3,FUNCT_2:def 3;
   dom f=Seg n by A1,FINSEQ_1:def 3;
 hence f,g are_fiberwise_equipotent by A1,A4,A5,RFINSEQ:3;
end;

theorem Th33:
  for n being Element of NAT, K being Field,
      p being Element of Permutations(n),
 f,g being FinSequence of K st n>=1 & len f=n & g=f*p holds
 (the mult of K) $$ f = (the mult of K) $$ g
proof let n be Element of NAT, K be Field,
 p be Element of Permutations(n), f,g be FinSequence of K;
 assume n>=1 & len f=n & g=f*p;
 then f,g are_fiberwise_equipotent by Th32;
 hence (the mult of K) $$ f = (the mult of K) $$ g by Th31;
end;

theorem Th34: for n being Element of NAT, K being Field,
p being Element of Permutations(n),
 f being FinSequence of K st n>=1 & len f=n holds
 f*p is FinSequence of K
proof let n be Element of NAT, K be Field,
p be Element of Permutations(n), f be FinSequence of K;
 assume A1: n>=1 & len f=n;
  then f <> {} by FINSEQ_1:25;
  then dom f <> {} by FINSEQ_1:26;
  then A2: Seg n <> {} by A1,FINSEQ_1:def 3;
  A3: p is bijective Function of Seg n,Seg n by MATRIX_2:def 11;
  reconsider q=p as Function of Seg n,Seg n by MATRIX_2:def 11;
   rng q = Seg n by A3,FUNCT_2:def 3;
  then rng p c= dom f by A1,FINSEQ_1:def 3;
  then dom (f*p)=dom q by RELAT_1:46 .=Seg n by A2,FUNCT_2:def 1;
  then reconsider h=f*p as FinSequence by FINSEQ_1:def 2;
  A4: rng h c= rng f by RELAT_1:45;
   rng f c= the carrier of K by FINSEQ_1:def 4;
  then rng h c= the carrier of K by A4,XBOOLE_1:1;
 hence f*p is FinSequence of K by FINSEQ_1:def 4;
end;

theorem Th35: for n being Element of NAT, K being Field,
p being Element of Permutations(n),
 A being Matrix of n,K st n>=1 holds
 Path_matrix(p",A@) = (Path_matrix(p,A))*p"
proof let n be Element of NAT, K be Field,p be Element of Permutations(n),
 A be Matrix of n,K;
 assume A1: n>=1;
  set f=(Path_matrix(p,A));
  A2: len (Path_matrix(p,A))=n by MATRIX_3:def 7;
  then f <> {} by A1,FINSEQ_1:25;
  then dom f <> {} by FINSEQ_1:26;
  then A3: Seg n <> {} by A2,FINSEQ_1:def 3;
  A4: p" is Permutation of Seg n by MATRIX_2:def 11;
  reconsider fp=p" as Function of Seg n,Seg n by MATRIX_2:def 11;
  A5: p is Permutation of Seg n by MATRIX_2:def 11;
  reconsider fp0=p as Function of Seg n,Seg n by MATRIX_2:def 11;
  A6: rng fp = Seg n by A4,FUNCT_2:def 3;
  then rng (p") c= dom f by A2,FINSEQ_1:def 3;
  then A7: dom (f*p")=dom fp by RELAT_1:46;
  A8: dom fp =Seg n by A3,FUNCT_2:def 1;
  reconsider m=(Path_matrix(p,A))*p" as FinSequence of K by A1,A2,Th34;
  A9: len m=n by A7,A8,FINSEQ_1:def 3;
   for i,j st i in dom (m) & j=(p").i holds m.i=(A@)*(i,j)
  proof let i,j;
   assume A10: i in dom (m) & j=(p").i;
    then A11: ((Path_matrix(p,A))*p").i=(Path_matrix(p,A)).((p").i) by A7,
FUNCT_1:23;
    A12: j in Seg n by A6,A7,A10,FUNCT_1:def 5;
    then A13: j in dom f by A2,FINSEQ_1:def 3;
     rng fp0 = Seg n by A5,FUNCT_2:def 3;
    then i=p.j by A7,A8,A10,FUNCT_1:54;
    then A14: (Path_matrix(p,A)).j=A*(j,i) by A13,MATRIX_3:def 7;
    A15: dom A=Seg len A by FINSEQ_1:def 3 .= Seg n by MATRIX_1:def 3;
     len A=n by MATRIX_1:def 3;
    then i in Seg width A by A1,A7,A8,A10,MATRIX_1:20;
    then [j,i] in Indices A by A12,A15,ZFMISC_1:def 2;
   hence m.i=(A@)*(i,j) by A10,A11,A14,MATRIX_1:def 7;
  end;
 hence Path_matrix(p",A@) = (Path_matrix(p,A))*p" by A9,MATRIX_3:def 7;
end;

theorem Th36: for n being Element of NAT, K being Field,
p being Element of Permutations(n),
 A being Matrix of n,K st n>=1 holds
 (Path_product(A@)).(p") = (Path_product(A)).p
proof let n be Element of NAT, K be Field, p be Element of Permutations n,
 A be Matrix of n,K;
 assume A1: n>=1;
A2: len (Path_matrix(p,A))=n by MATRIX_3:def 7;
  then reconsider g=Path_matrix(p,A)*(p") as FinSequence of K by A1,Th34;
   (Path_product(A@)).(p") = -((the mult of K) $$ Path_matrix(p",A@),p")
                                 by MATRIX_3:def 8
                         .= -((the mult of K) $$ g,p") by A1,Th35
                         .= -((the mult of K) $$ g,p) by A1,Th29
                         .= -((the mult of K) $$ (Path_matrix(p,A)),p)
                                 by A1,A2,Th33;
 hence (Path_product(A@)).(p") = (Path_product(A)).p by MATRIX_3:def 8;
end;

theorem  for n being Element of NAT, K being Field, A being Matrix of n,K
st n>=1
 holds Det (A) = Det (A@)
proof let n be Element of NAT, K be Field, A be Matrix of n,K;
 assume A1: n>=1;
  set f=Path_product(A);
  set f2=Path_product(A@);
  set F=(the add of K);
  set Y=the carrier of K;
  set X=Permutations(n);
  set B=FinOmega(Permutations(n));
   Permutations(n) is finite by MATRIX_2:30;
  then A2: (B <> {} or F has_a_unity) by MATRIX_2:def 17;
  A3: Permutations(n) is finite by MATRIX_2:30;
  then A4: B=Permutations(n) by MATRIX_2:def 17;
  set I=(the add of K) $$ (FinOmega(Permutations(n)),Path_product(A@));
 consider G0 being Function of Fin X, Y such that
  A5: I = G0.B &
          (for e being Element of Y st e is_a_unity_wrt F holds G0.{} = e) &
          (for x being Element of X holds G0.{x} = f2.x) &
           for B' being Element of Fin X st B' c= B & B' <> {}
           for x being Element of X st x in B \ B'
            holds G0.(B' \/ {x}) = F.(G0.B',f2.x) by A2,SETWISEO:def 3;
  deffunc F8(set) = G0.{p" where p is Permutation of Seg n : p in $1};
  A6: for x2 being set st x2 in Fin X holds F8(x2) in Y
  proof let x2 be set;
    assume x2 in Fin X;
      {p" where p is Permutation of Seg n : p in x2} c= X
      proof let r be set;
        assume r in {p" where p is Permutation of Seg n : p in x2};
        then consider p being Permutation of Seg n such that
        A7: r=p" & p in x2;
       thus r in X by A7,MATRIX_2:def 11;
      end;
     then {p" where p is Permutation of Seg n : p in x2} is Finite_Subset of X
                                        by A3,FINSUB_1:34;
    hence F8(x2) in Y by FUNCT_2:7;
  end;
  consider G1 being Function of Fin X,Y such that
   A8: for x5 being set st x5 in Fin X holds G1.x5 = F8(x5)
                                   from FUNCT_2:sch 2(A6);
   A9: I=G1.B
    proof
      A10: G1.B =G0.{p" where p is Permutation of Seg n : p in B} by A8;
      A11: {p" where p is Permutation of Seg n : p in B} c= B
       proof let z be set;
         assume z in {p" where p is Permutation of Seg n : p in B};
          then consider p being Permutation of Seg n such that
          A12: z=p" & p in B;
         thus z in B by A4,A12,MATRIX_2:def 11;
       end;
       B c= {p" where p is Permutation of Seg n : p in B}
      proof let z be set; assume z in B;
        then reconsider q2=z as Permutation of Seg n by A4,MATRIX_2:def 11;
        set q3=q2";
        A13: q3"=q2 by FUNCT_1:65;
         q3 in B by A4,MATRIX_2:def 11;
       hence z in {p" where p is Permutation of Seg n : p in B} by A13;
      end;
     hence thesis by A5,A10,A11,XBOOLE_0:def 10;
    end;
   A14: for e being Element of Y st e is_a_unity_wrt F holds G1.{} = e
    proof let e be Element of Y;
      assume A15: e is_a_unity_wrt F;
        {} is Subset of X by SUBSET_1:4;
       then reconsider B3={} as Element of Fin X by A3,FINSUB_1:34;
        {p" where p is Permutation of Seg n : p in B3} c= {}
       proof let s be set;
         assume s in {p" where p is Permutation of Seg n : p in B3};
         then consider p being Permutation of Seg n such that
         A16: s=p" & p in B3;
        thus s in {} by A16;
       end;
       then {p" where p is Permutation of Seg n : p in B3}= {} by XBOOLE_1:3;
       then G0.{p" where p is Permutation of Seg n : p in B3}=e by A5,A15;
      hence G1.{} = e by A8;
    end;
   A17: for x being Element of X holds G1.{x} = f.x
    proof let x be Element of X;
      reconsider B4={.x.} as Element of Fin X;
      reconsider q=x as Permutation of Seg n by MATRIX_2:def 11;
A18: q" is Element of X by MATRIX_2:def 11;
       {p" where p is Permutation of Seg n : p in B4}={q"}
       proof
     A19: {p" where p is Permutation of Seg n : p in B4} c= {q"}
          proof let z be set;
            assume z in {p" where p is Permutation of Seg n : p in B4};
            then consider p being Permutation of Seg n such that
            A20: z=p" & p in B4;
             p=x by A20,TARSKI:def 1;
           hence z in {q"} by A20,TARSKI:def 1;
          end;
          {q"} c= {p" where p is Permutation of Seg n : p in B4}
          proof let z be set;
            assume A21: z in {q"};
            A22: q in B4 by TARSKI:def 1;
             z=q" by A21,TARSKI:def 1;
           hence z in {p" where p is Permutation of Seg n : p in B4} by A22;
          end;
        hence thesis by A19,XBOOLE_0:def 10;
       end;
      then G1.B4=G0.{q"} by A8 .=f2.(q") by A5,A18
         .=(Path_product A).q by A1,Th36;
     hence G1.{x} = f.x;
    end;
    for B' being Element of Fin X st B' c= B & B' <> {}
           for x being Element of X st x in B \ B'
            holds G1.(B' \/ {x}) = F.(G1.B',f.x)
    proof let B' be Element of Fin X;
     assume A23: B' c= B & B' <> {};
     thus for x being Element of X st x in B \ B'
            holds G1.(B' \/ {x}) = F.(G1.B',f.x)
      proof let x be Element of X;
        assume x in B \ B';
         then A24: x in B & not x in B' by XBOOLE_0:def 4;
         A25: G1.(B' \/ {.x.})=
G0.{p" where p is Permutation of Seg n : p in B' \/ {x}} by A8;
         {p" where p is Permutation of Seg n : p in B'} c= X
        proof let v be set;
          assume v in {p" where p is Permutation of Seg n : p in B'};
          then consider p being Permutation of Seg n such that
          A26: p"=v & p in B';
         thus v in X by A26,MATRIX_2:def 11;
        end;
        then reconsider B2={p" where p is Permutation of Seg n : p in B'}
                            as Element of Fin X by A3,FINSUB_1:34;
        reconsider px=x as Permutation of Seg n by MATRIX_2:def 11;
        reconsider y=px" as Element of X by MATRIX_2:def 11;
        A27: B2 \/ {y}
         ={p" where p is Permutation of Seg n : p in B' \/ {x}}
        proof thus B2 \/ {y}
           c={p" where p is Permutation of Seg n : p in B' \/ {x}}
          proof let z be set;assume z in B2 \/ {y};
            then A28: z in B2 or z in {y} by XBOOLE_0:def 2;
             now per cases by A28,TARSKI:def 1;
            case z in B2;
              then consider p being Permutation of Seg n such that
              A29: z=p" & p in B';
               p in B' \/ {x} by A29,XBOOLE_0:def 2;
             hence z in
             {p2" where p2 is Permutation of Seg n : p2 in B' \/ {x}} by A29;
            end;
            case A30: z=y;
               px in {x} by TARSKI:def 1;
              then px in B' \/ {x} by XBOOLE_0:def 2;
             hence z in {p" where p is Permutation of Seg n : p in B' \/ {x}}
                                 by A30;
            end;
            end;
           hence z in {p" where p is Permutation of Seg n : p in B' \/ {x}};
          end;
         thus {p" where p is Permutation of Seg n : p in B' \/ {x}}
          c= B2 \/ {y}
          proof let z be set;
            assume z in {p" where p is Permutation of Seg n : p in B' \/ {x}};
            then consider p being Permutation of Seg n such that
            A31: z=p" & p in B' \/ {x};
             now per cases by A31,XBOOLE_0:def 2;
            case p in B';
             hence z in B2 or z in {y} by A31;
            end;
            case p in {x};
              then p=x by TARSKI:def 1;
             hence z in B2 or z in {y} by A31,TARSKI:def 1;
            end;
            end;
           hence z in B2 \/ {y} by XBOOLE_0:def 2;
          end;
        end;
        A32: B2 c= B
        proof let u be set;assume u in B2;
          then consider p being Permutation of Seg n such that
          A33: p"=u & p in B';
         thus u in B by A4,A33,MATRIX_2:def 11;
        end;
        consider d being Element of B';
         d in B by A23,TARSKI:def 3;
         then reconsider pd=d as Permutation of Seg n by A4,MATRIX_2:def 11;
         A34: pd" in B2 by A23;
         now assume y in B2;
          then consider pp being Permutation of Seg n such that
          A35: y=pp" & pp in B';
           px=(pp")" by A35,FUNCT_1:65;
         hence contradiction by A24,A35,FUNCT_1:65;
        end;
        then y in B\B2 by A4,XBOOLE_0:def 4;
        then A36: G0.{p" where p is Permutation of Seg n : p in B' \/ {x}}
        = F.(G0.B2,f2.y) by A5,A27,A32,A34;
         G0.B2=G1.B' by A8;
        hence G1.(B' \/ {x}) = F.(G1.B',f.x) by A1,A25,A36,Th36;
      end;
    end;
  then A37: (the add of K) $$ (FinOmega(Permutations n),Path_product A)
  =(the add of K) $$ (FinOmega(Permutations n),Path_product(A@))
                                 by A2,A9,A14,A17,SETWISEO:def 3;
   Det A = (the add of K) $$ (FinOmega(Permutations n),Path_product A)
                                 by MATRIX_3:def 9;
 hence Det A = Det (A@) by A37,MATRIX_3:def 9;
end;
