:: Determinant of Some Matrices of Field Elements
::  by Yatsuka Nakamura
::
:: Received January 4, 2006
:: Copyright (c) 2006 Association of Mizar Users

environ

 vocabularies MATRIX_7, FINSEQ_1, FUNCT_1, ARYTM_1, VECTSP_1, RLVECT_1,
      MATRIX_1, TREES_1, GROUP_1, MATRIX_3, RELAT_1, BOOLE, FUNCT_2, MATRIX_2,
      FINSEQ_2, SGRAPH1, BINOP_1, SETWISEO, FINSOP_1, FINSUB_1, NAT_1, CARD_3,
      FINSET_1, JORDAN3, FUNCOP_1, CQC_LANG, FINSEQ_4, QC_LANG1, RFINSEQ,
      SQUARE_1, CARD_1, FINSEQ_5, REALSET1, ROUGHS_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, XXREAL_0, NAT_1, RELAT_1, FUNCT_1,
      BINOP_1, ZFMISC_1, RLVECT_1, NUMBERS, BINOP_2, VECTSP_1, FUNCT_2,
      SETWISEO, FINSEQ_4, PARTFUN1, GROUP_4, FINSUB_1, NEWTON, FINSOP_1,
      SETWOP_2, FINSEQ_1, STRUCT_0, MATRIX_2, MATRIX_3, FINSET_1, JORDAN3,
      BINARITH, MATRIX_1, CQC_LANG, RFINSEQ, SQUARE_1, CARD_1, FINSEQ_5,
      GROUP_1;
 constructors DOMAIN_1, NAT_1, TOPMETR, SETWOP_2, REAL_1, SETWISEO, NEWTON,
      FINSOP_1, GROUP_4, JORDAN3, BINARITH, RFINSEQ, FINSEQ_4, ALGSTR_1,
      CQC_LANG, MATRIX_3, SQUARE_1, FINSEQ_5, XXREAL_0;
 registrations STRUCT_0, INT_1, RELSET_1, VECTSP_1, XREAL_0, FUNCT_2, FINSEQ_1,
      GROUP_1, FINSET_1, FINSUB_1, PARTFUN1, MATRIX_2, ORDINAL2, FVSUM_1,
      XXREAL_0;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM, REAL;
 definitions TARSKI, XBOOLE_0, BINOP_1, RLVECT_1, FINSEQ_1, MATRIX_1,
      RVSUM_1, JORDAN3, FINSEQ_2, COMPLSP1;
 theorems BINOP_1, RLVECT_1, MATRIX_3, VECTSP_1, MATRIX_1, GROUP_1, FUNCT_1,
      FINSEQ_1, NAT_1, MATRIX_2, FINSEQ_2, TARSKI, FINSOP_1, FVSUM_1, SETWISEO,
      FUNCT_2, XBOOLE_0, GROUP_4, ENUMSET1, ZFMISC_1, BINARITH, JORDAN3,
      REAL_1, XREAL_1, FINSEQ_5, INTEGRA2, FUNCOP_1, FINSUB_1, XBOOLE_1,
      CQC_LANG, RELAT_1, FINSEQ_4, SUBSET_1, RFINSEQ, FINSEQ_3, GROUP_7,
      XXREAL_0, RVSUM_1;
 schemes NAT_1, FUNCT_2, FINSEQ_1, FINSEQ_2;

begin

 reserve x,y,y1,y2 for set,
         D for non empty set,
         d,d1,d2,d3 for Element of D,
         F,G,H,H1,H2 for FinSequence of D,
         f,f1,f2 for Function of NAT,D,
         g for BinOp of D,
         k,n,i,j,l,m for Nat,
         P for Permutation of dom F;

Lm1: for x1,x2 being set st x1<>x2 holds
  {x1,x2} \ {x2} = {x1} & {x1,x2} \ {x1} = {x2} by ZFMISC_1:23;

theorem Th1:
 for f being Permutation of Seg 2 holds f=<*1,2*> or f=<*2,1*>
 proof let f be Permutation of Seg 2;
   A1:f is Function of Seg 2,Seg 2 & rng f = Seg 2 by FUNCT_2:def 3;
   A2:dom f=Seg 2 by FUNCT_2:67;then
   reconsider p=f as FinSequence by FINSEQ_1:def 2;
   1 in {1,2} by TARSKI:def 2;
   then f.1 in Seg 2 by A1,FINSEQ_1:4,FUNCT_2:6;
   then A3:f.1=1 or f.1=2 by FINSEQ_1:4,TARSKI:def 2;
   A4: 2 in {1,2} by TARSKI:def 2;
   A5: len p= 2 by A2,FINSEQ_1:def 3;
   A6: 1 in dom f by A2;
   A7: 2 in dom f by A2;
   f.2 in Seg 2 by A1,A4,FINSEQ_1:4,FUNCT_2:6;
   then A8:f.2=1 or f.2=2 by FINSEQ_1:4,TARSKI:def 2;
   now per cases by A3,A8;
   case f.1=1 & f.2=1;
    hence contradiction by A6,A7,FUNCT_1:def 8;
   end;
   case f.1=1 & f.2=2;
    hence f=<*1,2*> or f=<*2,1*> by FINSEQ_1:61,A5;
   end;
   case f.1=2 & f.2=1;
    hence f=<*1,2*> or f=<*2,1*> by FINSEQ_1:61,A5;
   end;
   case f.1=2 & f.2=2;
    hence contradiction by A6,A7,FUNCT_1:def 8;
   end;
   end;
  hence f=<*1,2*> or f=<*2,1*>;
 end;

theorem Th2:
 for f being FinSequence st f=<*1,2*> or f=<*2,1*> holds
   f is Permutation of Seg 2
 proof let f be FinSequence;
  assume A1: f=<*1,2*> or f=<*2,1*>;
   len (<*1,2*>)=2 by FINSEQ_1:61;then
   A2: dom (<*1,2*>)=Seg 2 by FINSEQ_1:def 3;
   len (<*2,1*>)=2 by FINSEQ_1:61;then
   A3: dom f =Seg 2 by A2,A1,FINSEQ_1:def 3;
   rng f c= Seg 2
    proof let y be set;assume y in rng f;then
      consider x being set such that
      A4: x in dom f & y=f.x by FUNCT_1:def 5;
      y=f.1 or y=f.2 by A4,TARSKI:def 2,A3,FINSEQ_1:4;then
      y=1 or y=2 or (y=2 or y=1) by A1,FINSEQ_1:61;
     hence y in Seg 2;
    end;then
   reconsider g=f as Function of Seg 2,Seg 2 by A3,FUNCT_2:4;
   now per cases by A1;
   case A5: f=<*1,2*>;
   for x1,x2 being set st x1 in dom f & x2 in dom f & f.x1=f.x2
           holds x1=x2
    proof let x1,x2 be set;
      assume A6: x1 in dom f & x2 in dom f & f.x1=f.x2;then
A7:   (x1=1 or x1=2)& (x2=1 or x2=2) by TARSKI:def 2,FINSEQ_1:4,A3;
      now per cases by A7;
      case x1=1 & x2=1;
       hence x1=x2;
      end;
      case A8: x1=1 & x2=2;then
       f.x1=1 by A5,FINSEQ_1:61;
       hence contradiction by A6,A8,A5,FINSEQ_1:61;
      end;
      case A9: x1=2 & x2=1;then
       f.x1=2 by A5,FINSEQ_1:61;
       hence contradiction by A6,A9,A5,FINSEQ_1:61;
      end;
      case x1=2 & x2=2;
       hence x1=x2;
      end;
      end;
     hence x1=x2;
    end;then
    A10: f is one-to-one by FUNCT_1:def 8;
    A11: rng f = Seg 2
     proof
       now let x be set;
       assume x in rng f;then
         consider z being set such that
         A12: z in dom f & f.z=x by FUNCT_1:def 5;
         len f=2 by FINSEQ_1:61,A5;then
         dom f=Seg 2 by FINSEQ_1:def 3;then
         A13: z=1 or z=2 by TARSKI:def 2,A12,FINSEQ_1:4;
         x =1 or x=2 by A5,A12,A13,FINSEQ_1:61;
        hence x in Seg 2;
       end;then
       A14: rng f c= Seg 2 by TARSKI:def 3;
        Seg 2 c= rng f
        proof let z be set;assume z in Seg 2;then
          z=1 or z=2 by TARSKI:def 2,FINSEQ_1:4;then
          A15: z=f.1 or z=f.2 by A5,FINSEQ_1:61;
          1 in dom f by A3;then
          A16: z in rng f or z=f.2 by A15,FUNCT_1:def 5;
          2 in dom f by A3;
         hence z in rng f by A16,FUNCT_1:def 5;
        end;
      hence thesis by A14,XBOOLE_0:def 10;
     end;then
      g is onto by FUNCT_2:def 3;
     hence f is Permutation of Seg 2 by FUNCT_2:83,A10,A11;
    end;
   case A17: f=<*2,1*>;
    for x1,x2 being set st x1 in dom f & x2 in dom f & f.x1=f.x2
           holds x1=x2
    proof let x1,x2 be set;
      assume A18: x1 in dom f & x2 in dom f & f.x1=f.x2;then
A19:   (x1=1 or x1=2)& (x2=1 or x2=2) by TARSKI:def 2,A3,FINSEQ_1:4;
      now per cases by A19;
      case x1=1 & x2=1;
       hence x1=x2;
      end;
      case A20: x1=1 & x2=2;then
       f.x1=2 by A17,FINSEQ_1:61;
       hence contradiction by A18,A20,A17,FINSEQ_1:61;
      end;
      case A21: x1=2 & x2=1;then
       f.x1=1 by A17,FINSEQ_1:61;
       hence contradiction by A18,A21,A17,FINSEQ_1:61;
      end;
      case x1=2 & x2=2;
       hence x1=x2;
      end;
      end;
     hence x1=x2;
    end;then
    A22: f is one-to-one by FUNCT_1:def 8;
    A23: rng f = Seg 2
     proof
       now let x be set;
       assume x in rng f;then
         consider z being set such that
         A24: z in dom f & f.z=x by FUNCT_1:def 5;
         len f=2 by FINSEQ_1:61,A17;then
         dom f=Seg 2 by FINSEQ_1:def 3;then
         A25: z=1 or z=2 by TARSKI:def 2,A24,FINSEQ_1:4;
         x =2 or x=1 by A17,A24,A25,FINSEQ_1:61;
        hence x in Seg 2;
       end;then
       A26: rng f c= Seg 2 by TARSKI:def 3;
        Seg 2 c= rng f
        proof let z be set;assume z in Seg 2;then
          z=1 or z=2 by TARSKI:def 2,FINSEQ_1:4;then
          A27: z=f.2 or z=f.1 by A17,FINSEQ_1:61;
          2 in dom f by A3;then
          A28: z in rng f or z=f.1 by A27,FUNCT_1:def 5;
          1 in dom f by A3;
         hence z in rng f by A28,FUNCT_1:def 5;
        end;
      hence thesis by A26,XBOOLE_0:def 10;
     end;then
     g is onto by FUNCT_2:def 3;
    hence f is Permutation of Seg 2 by FUNCT_2:83,A22,A23;
   end;
   end;
  hence f is Permutation of Seg 2;
 end;

Lm2: idseq 2=<*1,2*> by FINSEQ_2:61;

theorem Th3:
  Permutations(2)={<*1,2*>,<*2,1*>}
   proof
    now let p be set;
     assume p in Permutations(2);
      then reconsider q=p as Permutation of Seg 2 by MATRIX_2:def 11;
      q=<*1,2*> or q=<*2,1*> by Th1;
     hence p in {idseq 2,<*2,1*>} by Lm2,TARSKI:def 2;
    end;
    then A1: Permutations(2) c={idseq 2,<*2,1*>} by TARSKI:def 3;
      now let x be set;
     assume x in {idseq 2,<*2,1*>};
     then A2:x= idseq 2 or x= <*2,1*> by TARSKI:def 2;
     now per cases by A2;
     case A3: x=idseq 2;
      thus x in Permutations(2) by A3,MATRIX_2:def 11;
     end;
     case A4: x=<*2,1*>;
       <*2,1*> is Permutation of Seg 2 by Th2;
      hence x in Permutations(2) by A4,MATRIX_2:def 11;
     end;
     end;
     hence x in Permutations(2);
    end;
    then {idseq 2,<*2,1*>} c=Permutations(2) by TARSKI:def 3;
    hence thesis by A1,XBOOLE_0:def 10,Lm2;
   end;

theorem Th4:
  for p being Permutation of Seg 2 st p is_transposition holds p = <*2,1*>
proof let p be Permutation of Seg 2;
  assume A1: p is_transposition;
  now assume A2: p=<*1,2*>;
   set p0=<*1,2*>;
   A3: len p0=2 by FINSEQ_1:61;
   consider i,j such that
    A4: i in dom p & j in dom p & i<>j & p.i=j & p.j=i &
     for k st k <>i & k<>j & k in dom p holds p.k=k by A1,MATRIX_2:def 14;
    A5: dom p = Seg len p0 by A2,FINSEQ_1:def 3 .={1,2} by A3,FINSEQ_1:4;then
    A6: i=1 or i=2 by A4,TARSKI:def 2;
    now per cases by A4,A6,A5,TARSKI:def 2;
    case i=1 & j=2;
     hence contradiction by A4,A2,FINSEQ_1:61;
    end;
    case i=2 & j=1;then
     1 = 2 by A4,A2,FINSEQ_1:61;
     hence contradiction;
    end;
    end;
  hence contradiction;
  end;
 hence p = <*2,1*> by Th1;
end;

theorem Th5:
  for D being non empty set, f being FinSequence of D, k2 being Nat st
    1<=k2 & k2< len f holds
     f = mid(f,1,k2)^mid(f,k2+1,len f)
proof let D be non empty set, f be FinSequence of D, k2 be Nat;
  assume A1: 1<=k2 & k2< len f;then
A2: mid(f,1,len f) = mid(f,1,k2)^mid(f,k2+1,len f) by INTEGRA2:4;
  1< len f by A1,XXREAL_0:2;
  hence f = mid(f,1,k2)^mid(f,k2+1,len f) by JORDAN3:29,A2;
end;

theorem Th6:
  for D being non empty set, f being FinSequence of D st 2<= len f holds
    f = (f|(len f-'2))^mid(f,len f-'1,len f)
proof let D be non empty set, f be FinSequence of D;
  assume A1: 2<= len f;then
A2: len f-'2=len f-2 by BINARITH:50;
A3: 1<=len f by A1,XXREAL_0:2;
A4: len f-'2+1=len f-1-1+1 by A2
             .=len f-'1 by A3,BINARITH:50;
A5: 1<= len f by A1,XXREAL_0:2;
   now per cases;
   case len f-'2>0;then
     A6: 0+1<=len f-'2 by NAT_1:38;
     len f<len f+1 by NAT_1:38;then
     len f<len f+1+1 by NAT_1:38;then
     len f -2<len f+2-2 by REAL_1:92;then
     f=mid(f,1,len f-'2)^mid(f,len f-'2+1,len f) by Th5,A6,A2;
    hence f = (f|(len f-'2))^mid(f,len f-'1,len f) by A4,A6,JORDAN3:25;
   end;
   case A7: len f-'2=0;
A8:  len f-'2+1=(len f)-2+1 by BINARITH:50,A1
                  .=len f-1
                  .=len f-'1 by A5,BINARITH:50;
     A9: mid(f,len f-'2+1,len f)=f by A7,JORDAN3:29,A5;
     f|0 is empty;
    hence f= (f|(len f-'2))^mid(f,len f-'1,len f) by A7,A9,FINSEQ_1:47,A8;
   end;
   end;
 hence f = (f|(len f-'2))^mid(f,len f-'1,len f);
end;

theorem Th7:
for D being non empty set, f being FinSequence of D st 1<= len f holds
     f = (f|(len f-'1))^mid(f,len f,len f)
proof let D be non empty set, f be FinSequence of D;
 assume A1: 1<= len f;then
A2: len f-'1=len f-1 by BINARITH:50;
   now per cases;
   case len f-'1>0;then
     A3: 0+1<=len f-'1 by NAT_1:38;
     len f<len f+1 by NAT_1:38;then
     len f -1<len f+1-1 by REAL_1:92;then
     f=mid(f,1,len f-'1)^mid(f,len f-'1+1,len f) by Th5,A3,A2;
    hence f = (f|(len f-'1))^mid(f,len f,len f) by A2,A3,JORDAN3:25;
   end;
   case A4: len f-'1=0;
     A5: mid(f,len f-'1+1,len f)=f by A4,JORDAN3:29,A1;
     f|0 is empty;
    hence f= (f|(len f-'1))^mid(f,len f,len f) by A4,A5,FINSEQ_1:47,A2;
   end;
   end;
 hence f = (f|(len f-'1))^mid(f,len f,len f);
end;

Lm3: <*1,2*> <> <*2,1*> by GROUP_7:2;

theorem Th8:
  for a being Element of Group_of_Perm 2 st
  (ex q being Element of Permutations 2 st q=a & q is_transposition)
   holds a = <*2,1*>
proof let a be Element of Group_of_Perm 2;
  given q being Element of Permutations 2 such that
  A1: q=a & q is_transposition;
  A2: the carrier of Group_of_Perm 2 = Permutations 2 by MATRIX_2:def 13;
  reconsider b=a as Permutation of Seg 2 by MATRIX_2:def 11,A2;
  consider i,j such that
  A3: i in dom q & j in dom q & i<>j & q.i=j & q.j=i &
   for k st k <>i & k<>j & k in dom q holds q.k=k by A1,MATRIX_2:def 14;
  b is_transposition by MATRIX_2:def 14,A1,A3;
 hence a = <*2,1*> by Th4;
end;

theorem Th9:
  for n being Nat,a,b being Element of Group_of_Perm n,
      pa,pb being Element of Permutations n st
        a=pa & b=pb holds a*b=pb*pa
 proof let n be Nat,a,b be Element of Group_of_Perm n,
  pa,pb be Element of Permutations n;
  assume A1: a=pa & b=pb;
  a*b=(the mult of Group_of_Perm n).(a,b) by GROUP_1:def 1
     .=pb*pa by MATRIX_2:def 13,A1;
  hence a*b=pb*pa;
 end;

theorem Th10: for a,b being Element of Group_of_Perm 2 st
(ex p being Element of Permutations 2 st p=a & p is_transposition)&
(ex q being Element of Permutations 2 st q=b & q is_transposition)
 holds a*b = <*1,2*>
proof let a,b be Element of Group_of_Perm 2;
 assume A1: (ex p being Element of Permutations 2 st p=a &
 p is_transposition)&
 (ex q being Element of Permutations 2 st q=b & q is_transposition);then
  A2: a= <*2,1*> by Th8;
  A3: b= <*2,1*> by Th8,A1;
  A4: the carrier of Group_of_Perm 2 =Permutations 2 by MATRIX_2:def 13;
  A5: a*b = <*1,2*> or a*b = <*2,1*> by A4,TARSKI:def 2,Th3;
  consider p being Element of Permutations 2 such that
  A6:  p=a & p is_transposition by A1;
  consider q being Element of Permutations 2 such that
  A7:  q=b & q is_transposition by A1;
  A8: a*b=q*p by A6,A7,Th9;
  reconsider q2=q as FinSequence by Th8,A7;
  A9: len q2=2 & q2.1=2 & q2.2=1 by A3,A7,FINSEQ_1:61;
  reconsider p2=p as FinSequence by Th8,A6;
  A10: len p2=2 & p2.1=2 & p2.2=1 by A2,A6,FINSEQ_1:61;then
  1 in Seg len p2;then
  1 in dom p2 by FINSEQ_1:def 3;then
  (q*p).1=1 by FUNCT_1:23,A10,A9;
 hence a*b = <*1,2*> by A5,A8,FINSEQ_1:61;
end;

theorem Th11: for l being FinSequence of Group_of_Perm 2
 st (len l) mod 2=0 & (for i st i in dom l holds
     (ex q being Element of Permutations 2 st l.i=q & q is_transposition))
holds Product l = <*1,2*>
proof let l be FinSequence of Group_of_Perm 2;
 assume A1: (len l)mod 2=0 & (for i st i in dom l holds
     (ex q being Element of Permutations 2
              st l.i=q & q is_transposition));then
  ( ex t being Nat st len l = 2 * t + 0 & 0 < 2 ) or 0 = 0 & 2 = 0
                               by NAT_1:def 2;then
  consider t being Nat such that
  A2: len l = 2 * t;
  defpred P[Nat] means
   for f being FinSequence of Group_of_Perm 2 st len f=2*$1 &
    (for i st i in dom f holds
     (ex q being Element of Permutations 2 st f.i=q & q is_transposition))
     holds Product f = <*1,2*>;
  for f being FinSequence of Group_of_Perm 2 st
    len f=2*0 &
     (for i st i in dom f holds
     (ex q being Element of Permutations 2 st f.i=q & q is_transposition))
    holds Product f = <*1,2*>
   proof let f be FinSequence of Group_of_Perm 2;
    assume A3: len f=2*0 &
      (for i st i in dom f holds
     (ex q being Element of Permutations 2
              st f.i=q & q is_transposition));
     set G=Group_of_Perm 2;
     A4: f=<*> (the carrier of G) by A3,FINSEQ_1:32;
     1.G=idseq 2 by MATRIX_2:28
       .= <*1,2*> by Lm2;
    hence Product f = <*1,2*> by A4,GROUP_4:11;
   end;then
  A5: P[0];
  A6: for s being Nat st P[s] holds P[s+1]
   proof let s be Nat;
    assume P[s];then
     A7: for f being FinSequence of Group_of_Perm 2 st
     len f=2*s & (for i st i in dom f holds
     (ex q being Element of Permutations 2 st f.i=q & q is_transposition))
      holds Product f = <*1,2*>;
    for f being FinSequence of Group_of_Perm 2 st
     len f=2*(s+1) & (for i st i in dom f holds
     (ex q being Element of Permutations 2 st f.i=q & q is_transposition))
      holds Product f = <*1,2*>
     proof let f be FinSequence of Group_of_Perm 2;
       assume A8: len f=2*(s+1)&
         (for i st i in dom f holds
         (ex q being Element of Permutations 2
              st f.i=q & q is_transposition));then
        A9: len f = 2*s+2;
        A10: 2 <= len f by A9,NAT_1:29;then
        A11: 1 <= len f by XXREAL_0:2;then
        A12: len f-'1=len f-1 by BINARITH:50;
        1+1-1<=len f-1 by A10,REAL_1:92;then
        A13: 1<=len f-'1 by A11,BINARITH:50;
        A14: f = (f|(len f-'2))^mid(f,len f-'1,len f) by Th6,A10;
        A15: len f-'2<=len f by BINARITH:52;then
        A16: len (f|(len f-'2))=len f-'2 by FINSEQ_1:80 .= 2*s
                                by A9,BINARITH:39;
        for i st i in dom (f|(len f-'2)) holds
           (ex q being Element of Permutations 2
              st (f|(len f-'2)).i=q & q is_transposition)
        proof let i;assume i in dom (f|(len f-'2));then
          i in Seg len (f|(len f-'2)) by FINSEQ_1:def 3;then
          A17: 1<= i & i<= len (f|(len f-'2)) by FINSEQ_1:3;
          len (f|(len f-'2)) <= len f by FINSEQ_5:18;then
          i<=len f by A17,XXREAL_0:2;then
          i in dom f by A17,FINSEQ_3:27;then
          consider q being Element of Permutations 2 such that
          A18: f.i=q & q is_transposition by A8;
          len (f|(len f-'2))=len f-'2 by A15,FINSEQ_1:80;then
          f.i=(f|(len f-'2)).i by A17,FINSEQ_3:121;
         hence (ex q being Element of Permutations 2
              st (f|(len f-'2)).i=q & q is_transposition) by A18;
        end;then
        A19: Product (f|(len f-'2))=<*1,2*> by A7,A16;
        A20: 1.Group_of_Perm 2=(Product (f|(len f-'2)))
                              by MATRIX_2:28,Lm2,A19;
        A21: Product f
              = (Product (f|(len f-'2)))*(Product (mid(f,len f-'1,len f)))
                                          by A14,GROUP_4:8
             .= (Product (mid(f,len f-'1,len f))) by A20,GROUP_1:def 5;
        A22: len f-'1<= len f by BINARITH:52;then
        len f-'1 in Seg len f by A13;then
        A23: len f-'1 in dom f by FINSEQ_1:def 3;
        len f in Seg len f by A11;then
        A24: len f in dom f by FINSEQ_1:def 3;
        A25: len (mid(f,len f-'1,len f)) = len f-'(len f-'1)+1
                                       by A22,JORDAN3:27,A13,A11
                                  .=len f-(len f-'1)+1 by BINARITH:50,A22
                                  .=len f-(len f-1)+1 by BINARITH:50,A11
                                  .=2;
        set g=mid(f,len f-'1,len f);
        A26: 1<=len g by A25;
        A27: g= (g|(len g-'1))^mid(g,len g,len g) by Th7,A25;
        A28: len mid(g,len g,len g)=len g-'len g+1 by JORDAN3:27,A26
                             .= 0+1 by BINARITH:51 .=1;then
        A29: mid(g,len g,len g)=<* (mid(g,len g,len g)).1*> by FINSEQ_1:57;
        len g-'1<= len g by BINARITH:52;then
        A30: len (g|(len g-'1)) = len g-'1 by FINSEQ_1:80
                               .= len g-1 by BINARITH:50,A25
                               .=1 by A25;then
        A31: g|(len g-'1)= <*(g|(len g-'1)).1*> by FINSEQ_1:57;
        len f-(len f-'1)+1=len f-(len f-1)+1 by BINARITH:50,A11
                         .=2;then
        A32: 1<=len f-(len f-'1)+1;
        1<=1+(len f-'1) by NAT_1:29;then
        A33: 1+(len f-'1)-'1=1+(len f-'1)-1 by BINARITH:50
                      .=len f-'1;
        len f-(len f-'1)+1=len f-(len f-1)+1 by A11,BINARITH:50
                         .= 1+1;then
        A34: 2<=len f-(len f-'1)+1;
        A35: len g-1=1 by A25;
        len g-'1=len g-1 by BINARITH:50,A26;then
        A36: (g|(len g-'1)).1=g.1 by FINSEQ_3:121,A35
                       .=f.(1+(len f-'1)-'1) by JORDAN3:31,A32,A13,A22
                       .=f.(len f-'1) by A33;
        A37: ex q being Element of Permutations 2
              st f.(len f-'1)=q & q is_transposition by A23,A8;
        1<=1+len g by NAT_1:29;then
        A38: 1+ len g-'1=1+len g-1 by BINARITH:50;
        2<=2+(len f-'1) by NAT_1:29;then
        1<=2+(len f-'1) by XXREAL_0:2;then
        A39: 2+(len f-'1)-'1=2+(len f-'1)-1 by BINARITH:50
                      .=len f by A12;
        1<=len g-len g+1;then
        A40: (mid(g,len g,len g)).1=g.(1+len g-'1) by JORDAN3:31,A26
                                  .=f.(2+(len f-'1)-'1)
                                    by JORDAN3:31,A13,A22,A34,A25,A38
                                  .=f.(len f) by A39;
        A41: ex q being Element of Permutations 2
              st f.(len f)=q & q is_transposition by A24,A8;
        reconsider pw1=Product (g|(len g-'1))
         as Element of Group_of_Perm 2;
        reconsider pw2=Product mid(g,len g,len g)
         as Element of Group_of_Perm 2;
         A42: rng (g|(len g-'1))
           c= the carrier of Group_of_Perm 2 by FINSEQ_1:def 4;
          1 in Seg len (g|(len g-'1)) by A30;then
          1 in dom (g|(len g-'1)) by FINSEQ_1:def 3;then
          (g|(len g-'1)).1 in rng (g|(len g-'1)) by FUNCT_1:def 5;then
         reconsider r=(g|(len g-'1)).1 as Element of
                     Group_of_Perm 2 by A42;
         A43: rng (mid(g,len g,len g))
           c= the carrier of Group_of_Perm 2 by FINSEQ_1:def 4;
        1 in Seg len (mid(g,len g,len g)) by A28;then
          1 in dom (mid(g,len g,len g)) by FINSEQ_1:def 3;then
          (mid(g,len g,len g)).1 in rng (mid(g,len g,len g))
                             by FUNCT_1:def 5;then
         reconsider s=(mid(g,len g,len g)).1 as Element of
                     Group_of_Perm 2 by A43;
        A44: pw1 = Product (<* r *>) by A31
          .=f.(len f-'1) by A36,GROUP_4:12;
        A45: pw2 = Product (<* s *>) by A29
          .=f.len f by A40,GROUP_4:12;
        Product g=(Product ((g|(len g-'1))))*(Product mid(g,len g,len g))
                                  by A27,GROUP_4:8
                .= <*1,2*> by Th10,A37,A41,A44,A45;
       hence Product f = <*1,2*> by A21;
     end;
    hence P[s+1];
   end;
  for s being Nat holds P[s] from NAT_1:sch 1(A5,A6);
 hence Product l = <*1,2*> by A2,A1;
end;

theorem for K being Field, M being Matrix of 2,K holds
    Det M = (M*(1,1))*(M*(2,2))-(M*(1,2))*(M*(2,1))
proof let K be Field, M be Matrix of 2,K;
   A1: 1 in Seg 2;
   reconsider D2=Seg 2 as non empty set by A1;
   reconsider s1=<*1,2*>, s2=<*2,1*> as Permutation of Seg 2 by Th2;
   A2: now assume A3: s1=s2;
   s1.1=1 by FINSEQ_1:61;
   hence contradiction by A3,FINSEQ_1:61;
   end;
   reconsider p1 = s1, p2 = s2 as Element of Permutations(2)
     by MATRIX_2:def 11;
   A4:len Path_matrix(p1,M) = 2 by MATRIX_3:def 7;
   A5:len Path_matrix(p2,M) = 2 by MATRIX_3:def 7;
   A6: Permutations 2={s1,s2} by Th3;
   A7: Det M=(the add of K) $$ (FinOmega Permutations 2,Path_product M)
         by MATRIX_3:def 9;
   set X=Permutations 2;
   set Y=the carrier of K;
   set B=FinOmega Permutations 2;
   set f=Path_product M;
   set F=the add of K;
   A8: Permutations 2 is finite by MATRIX_2:30;
   A9: Permutations 2 ={s1,s2} by A6;
   A10: B=Permutations 2 by A8,MATRIX_2:def 17;then
   consider G being Function of Fin X, Y such that
     A11: Det M = G.B &
          (for e being Element of Y st e is_a_unity_wrt F holds G.{} = e) &
          (for x being Element of X holds G.{x} = f.x) &
           for B' being Element of Fin X st B' c= B & B' <> {}
           for x being Element of X st x in B \ B'
            holds G.(B' \/ {x}) = F.(G.B',f.x) by A7,SETWISEO:def 3;
   A12: B=Permutations(2) by A10 .={s1,s2} by A9;
   A13: for x being Element of {s1,s2} holds G.{x} = f.x by A9,A11;
   p1 in {s1,s2} by TARSKI:def 2;then
   A14: G.{p1} = f.p1 by A13;
   A15: G.B = (the add of K).(f.p1,f.p2)
     proof
       reconsider B'={p1} as Element of Fin X;
       A16: B' c= B
        proof let y be set;assume y in B';then
          y=p1 by TARSKI:def 1;
         hence y in B by A10;
        end;
       B\B' = {s1,s2} \ {s1} by A12 .={s2} by Lm1,A2;then
       s2 in B\B' by TARSKI:def 1;then
       G.(B' \/ {p2}) = F.(G.B',f.p2) by A11,A16;
      hence thesis by A14,A12,ENUMSET1:41;
     end;
     A17: f.p1 = -((the mult of K) "**" (Path_matrix(p1,M)),p1)
                     by MATRIX_3:def 8;
     A18: f.p2 = -((the mult of K) "**" (Path_matrix(p2,M)),p2)
                     by MATRIX_3:def 8;
     A19: len Path_matrix(p1,M)=2 &
      for i,j st i in dom Path_matrix(p1,M) & j=p1.i holds
        Path_matrix(p1,M).i=M*(i,j) by MATRIX_3:def 7;
     A20: 1 in Seg 2;then
     A21: 1 in dom Path_matrix(p1,M) by A4,FINSEQ_1:def 3;
     A22: 2 in Seg 2;then
     A23: 2 in dom Path_matrix(p1,M) by A4,FINSEQ_1:def 3;
     A24: 1 in dom Path_matrix(p2,M) by A20,A5,FINSEQ_1:def 3;
     A25: 2 in dom Path_matrix(p2,M) by A22,A5,FINSEQ_1:def 3;
     A26: len Path_matrix(p2,M)=2 &
      for i,j st i in dom Path_matrix(p2,M) & j=p2.i holds
        Path_matrix(p2,M).i=M*(i,j) by MATRIX_3:def 7;
       p1.1 = 1 by FINSEQ_1:61;then
     A27: Path_matrix(p1,M).1=M*(1,1) by A21,A19;
     p1.2 = 2 by FINSEQ_1:61;then
     A28: Path_matrix(p1,M).2=M*(2,2) by A23,A19;
     p2.1 = 2 by FINSEQ_1:61;then
     A29: Path_matrix(p2,M).1=M*(1,2) by A24,A26;
     p2.2 = 1 by FINSEQ_1:61;then
     A30: Path_matrix(p2,M).2=M*(2,1) by A25,A26;
     set di=(the mult of K) $$ (Path_matrix(p1,M));
     set dj=(the mult of K) $$ (Path_matrix(p2,M));
     consider f3 being Function of NAT,the carrier of K such that
     A31: f3.1 = (Path_matrix(p1,M)).1 &
     (for n being Nat st 0 <> n & n < 2 holds
       f3.(n + 1) = (the mult of K).(f3.n,(Path_matrix(p1,M)).(n + 1)))
       & di = f3.2 by FINSOP_1:def 1,A4;
     f3.(1+1)=(the mult of K).(f3.1,(Path_matrix(p1,M)).(1+1)) by A31
           .=(M*(1,1))*(M*(2,2)) by GROUP_1:def 1,A27,A31,A28;then
     A32: di=(M*(1,1))*(M*(2,2)) by A31;
     consider f4 being Function of NAT,the carrier of K such that
     A33: f4.1 = (Path_matrix(p2,M)).1 &
     (for n being Nat st 0 <> n & n < 2 holds
       f4.(n + 1) = (the mult of K).(f4.n,(Path_matrix(p2,M)).(n + 1)))
       & dj = f4.2 by FINSOP_1:def 1,A5;
     f4.(1+1)=(the mult of K).(f4.1,(Path_matrix(p2,M)).(1+1)) by A33
           .=(M*(1,2))*(M*(2,1)) by GROUP_1:def 1,A29,A33,A30;then
     A34: dj=(M*(1,2))*(M*(2,1)) by A33;
     A35: f.p1= -(di,p1) by A17;
     A36: f.p2= -(dj,p2) by A18;
     A37: the carrier of Group_of_Perm 2 = {s1,s2} by A6,MATRIX_2:def 13;
     set D0={s1,s2};
     reconsider l0=<*>D0 as FinSequence of Group_of_Perm(2) by A37;
        A38: len l0=0 by FINSEQ_1:25;
     A39: 0 mod 2=0 by NAT_1:78;
     A40: Product l0=Product <*> (the carrier of Group_of_Perm 2)
                   .= 1.(Group_of_Perm(2)) by GROUP_4:11
                   .= p1 by Lm2,MATRIX_2:28;
     for i being Nat st i in dom l0
               ex q being Element of Permutations 2
              st l0.i=q & q is_transposition by A38,FINSEQ_1:4,def 3;then
     A41: ex l be FinSequence of Group_of_Perm 2
       st (len l)mod 2=0 & s1=Product l &
       for i st i in dom l
        ex q being Element of Permutations 2
              st l.i=q & q is_transposition by A38,A39,A40;
A42:  len Permutations 2 = 2 by MATRIX_2:20; then
     A43: p1 is even by MATRIX_2:def 15,A41;
     A44: f.p1 = -(di,p1) by A35 .=di by A43,MATRIX_2:def 16;
      not ex l be FinSequence of Group_of_Perm 2
       st (len l)mod 2=0 & s2=Product l &
       for i st i in dom l
         ex q being Element of Permutations 2
              st l.i=q & q is_transposition by Th11,Lm3;then
     A45: p2 is odd by MATRIX_2:def 15,A42;
     A46: f.p2 = -(dj,p2) by A36.= - dj by A45,MATRIX_2:def 16;
     Det M = (the add of K).(f.p1,f.p2) by A11,A15
           .= di+(-dj) by A44,A46
           .= (M*(1,1))*(M*(2,2))-(M*(1,2))*(M*(2,1)) by A32,A34;
 hence thesis;
end;

definition let n be Nat,K be Field;
  let M be (Matrix of n,K), a be Element of K;
  redefine func a*M -> Matrix of n,K;
coherence
proof
  A1: len M =n by MATRIX_1:def 3;
  A2: len M = n & for p being FinSequence of K st
    p in rng M holds len p = n by MATRIX_1:def 3;
  A3: len (a*M)=len M & width (a*M)=width M &
    for i,j st [i,j] in Indices M holds (a*M)*(i,j)=a*(M*(i,j))
                             by MATRIX_3:def 5;
  now per cases;
  case A4: len M>0;then
   n=width M by MATRIX_1:20,A1;
  hence a*M is Matrix of n,K by A3,A1,A4,MATRIX_1:20;
  end;
  case len M<=0;then
    A5: len (a*M)=0 & width (a*M)=0 by A3,MATRIX_1:def 4;
    for p being FinSequence of K st p in rng (a*M) holds len p = 0
     proof let p be FinSequence of K;
      assume p in rng (a*M);then
      consider x being set such that
  A6: x in dom (a*M) & p=(a*M).x by FUNCT_1:def 5;
      thus len p = 0 by A6,A5,FINSEQ_1:4,def 3;
     end;
   hence a*M is Matrix of n,K by A2,A3,A5,MATRIX_1:def 3;
  end;
  end;
 hence a*M is Matrix of n,K;
end;
end;

theorem Th13: for K being Field, n,m being Nat holds
len (0.(K,n,m))=n & dom (0.(K,n,m))=Seg n
proof let K be Field, n,m be Nat;
  len (n |-> (m |-> 0.K))=n by FINSEQ_2:69;
 hence len (0.(K,n,m)) =n by MATRIX_3:def 1;
 hence dom (0.(K,n,m))=Seg n by FINSEQ_1:def 3;
end;

theorem Th14: for K being Field, n being Nat,
p being Element of Permutations n, i being Nat st i in Seg n holds
 p.i in Seg n
proof let K be Field, n be Nat,
  p be Element of Permutations(n),i be Nat;
  assume A1: i in Seg n;
  p is Permutation of Seg n by MATRIX_2:def 11;
 hence p.i in Seg n by A1,FUNCT_2:7;
end;

theorem for K being Field, n being Nat st n>=1 holds
    Det (0.(K,n,n)) = 0.K
proof let K be Field, n be Nat;
  assume A1: n>=1;then
  A2: n>0;
  set B=FinOmega Permutations n;
  set f=Path_product(0.(K,n,n));
  set F=the add of K;
  set Y=the carrier of K;
  set X=Permutations n;
  A3: Permutations(n) is finite by MATRIX_2:30;
  A4: (B <> {} or F has_a_unity) by A3,MATRIX_2:def 17;
  dom (Permutations(n) --> 0.K)=Permutations(n) by FUNCOP_1:19;then
  A5: dom (Path_product(0.(K,n,n)))=dom (Permutations(n) --> 0.K)
                              by FUNCT_2:def 1;
  for x being set st x in dom (Path_product(0.(K,n,n)))
   holds (Path_product(0.(K,n,n))).x=(Permutations(n) --> 0.K).x
  proof let x be set;
    assume
       x in dom (Path_product(0.(K,n,n)));
    for p being Element of Permutations(n) holds
    (Permutations(n) --> 0.K).p
        = -((the mult of K) $$ Path_matrix(p,(0.(K,n,n))),p)
    proof let p be Element of Permutations(n);
      A6: len (n |-> 0.K)=n by FINSEQ_2:69;
      for i,j being Nat st i in dom (n |-> 0.K)
                   & j=p.i holds (n |-> 0.K).i=(0.(K,n,n))*(i,j)
       proof let i,j be Nat;
         assume A7: i in dom (n |-> 0.K) & j=p.i;then
         A8: i in Seg n by FUNCOP_1:19;then
         A9: (n |-> 0.K).i=0.K by FUNCOP_1:13;
         A10: i in dom (0.(K,n,n)) by A8,Th13;
         j in Seg n by A7,A8,Th14;then
         i in dom (0.(K,n,n)) & j in Seg width (0.(K,n,n))
                             by A10,A2,MATRIX_1:24;then
         [i,j] in Indices (0.(K,n,n)) by ZFMISC_1:def 2;
        hence (n |-> 0.K).i=(0.(K,n,n))*(i,j) by A9,MATRIX_3:3;
       end;then
      A11: Path_matrix(p,(0.(K,n,n)))=(n |-> 0.K) by A6,MATRIX_3:def 7;
      defpred P[Nat] means (the mult of K) $$ ( ($1+1) |-> 0.K)=0.K;
      (1 |-> 0.K)= <* 0.K *> by FINSEQ_2:73;then
     A12: P[0] by FINSOP_1:12;
     A13: for k being Nat st P[k] holds P[k+1]
       proof let k be Nat;
        assume A14: P[k];
         A15: (the mult of K) has_a_unity by FVSUM_1:11;
         (k+1+1) |-> 0.K = ((k+1) |-> 0.K) ^ <* 0.K *> by FINSEQ_2:74;then
         (the mult of K) $$ ( (k+1+1) |-> 0.K)
           =(the mult of K).((the mult of K)$$(((k+1) |-> 0.K)),0.K)
                              by FINSOP_1:5,A15
          .= (0.K)*(0.K) by GROUP_1:def 1,A14
          .= 0.K by VECTSP_1:36;
        hence P[k+1];
       end;
      A16: for k being Nat holds P[k] from NAT_1:sch 1(A12,A13);
      n-'1=n-1 by A1,BINARITH:50;then
      n-'1+1=n;then
      A17: (the mult of K) $$ Path_matrix(p,(0.(K,n,n)))
       =0.K by A11,A16;
      A18: (Permutations(n) --> 0.K).p = 0.K by FUNCOP_1:13;
      now per cases;
      case p is even;
       hence -(0.K,p)=(0.K) by MATRIX_2:def 16;
      end;
      case not(p is even);then
        -(0.K,p)= -(0.K) by MATRIX_2:def 16 .=0.K by RLVECT_1:25;
       hence -(0.K,p)=(0.K);
      end;
      end;
     hence (Permutations(n) --> 0.K).p
        = -((the mult of K) $$ Path_matrix(p,(0.(K,n,n))),p) by A17,A18;
    end;
   hence (Path_product(0.(K,n,n))).x=(Permutations(n) --> 0.K).x
                                 by MATRIX_3:def 8;
  end;then
  A19: Path_product(0.(K,n,n))=Permutations(n) --> 0.K by A5,FUNCT_1:9;
  reconsider G0= Fin X --> 0.K as Function of Fin X, Y;
  A20: G0.B=0.K by FUNCOP_1:13;
  A21: for e being Element of Y st e is_a_unity_wrt F holds G0.({}) = e
    proof let e be Element of Y;assume
      e is_a_unity_wrt F;then
      A22: F.(0.K,e)=0.K by BINOP_1:11;
      0.K is_a_unity_wrt F by FVSUM_1:8;then
      A23: F.(0.K,e)=e by BINOP_1:11;
      {} in Fin X by FINSUB_1:18;
     hence G0.({}) = e by A22,A23,FUNCOP_1:13;
    end;
  A24: for x being Element of X holds G0.({x}) = f.x
   proof let x be Element of X;
     A25: G0.({x})=0.K by FUNCOP_1:13;
     f.x=(Permutations(n) --> 0.K).x by A19 .= 0.K by FUNCOP_1:13;
    hence G0.({x}) = f.x by A25;
   end;
   for B' being Element of Fin X st B' c= B & B' <> {}
      for x being Element of X st x in B\B' holds G0.(B' \/ {x})
                    = F.(G0.B',f.x)
   proof let B' be Element of Fin X;
     assume B' c= B & B' <> {};
     thus for x being Element of X st x in B\B' holds G0.(B' \/ {x})
                    = F.(G0.B',f.x)
     proof let x be Element of X;
       assume x in B\B';
        A26: G0.(B' \/ {x})=0.K by FUNCOP_1:13;
        A27: G0.B'=0.K by FUNCOP_1:13;
        A28: f.x=(Permutations(n) --> 0.K).x by A19 .= 0.K by FUNCOP_1:13;
        0.K is_a_unity_wrt F by FVSUM_1:8;
       hence G0.(B' \/ {x}) = F.(G0.B',f.x) by A26,A28,A27,BINOP_1:11;
     end;
   end;then
  (the add of K) $$ (FinOmega(Permutations(n)),Path_product(0.(K,n,n)))=0.K
                          by SETWISEO:def 3,A4,A20,A21,A24;
 hence thesis by MATRIX_3:def 9;
end;

definition let x,y,a,b be set;
  func IFIN(x,y,a,b) equals
:Def1: a if x in y otherwise b;
  correctness;
end;

theorem
  for K being Field, n being Nat st n>=1 holds
    Det (1.(K,n)) = 1.K
proof
let K be Field, n be Nat;
  assume A1: n>=1;then
  A2: n>0;
  set B=FinOmega(Permutations(n));
  A3: Permutations(n) is finite by MATRIX_2:30;then
  A4: B=Permutations(n) by MATRIX_2:def 17;
  (idseq n) is Element of Group_of_Perm(n) by MATRIX_2:23;then
  (idseq n) in the carrier of Group_of_Perm(n);then
  A5: (idseq n) in Permutations(n) by MATRIX_2:def 13;
  reconsider p0=idseq n as Permutation of Seg n;
  id (Seg n) is even by MATRIX_2:29;then
  A6: p0 is even;
  set f=Path_product(1.(K,n));
  set F=(the add of K);
  set Y=the carrier of K;
  set X=Permutations(n);
  deffunc F3(set) = IFIN(idseq n,$1,1.K,0.K);
  A7: for x being set st x in Fin X holds F3(x) in Y
  proof let x be set;
   assume x in Fin X;
    now per cases;
    case idseq n in x;then
      F3(x)=1.K by Def1;
     hence F3(x) in Y;
    end;
    case not idseq n in x;then
      F3(x)=0.K by Def1;
     hence F3(x) in Y;
    end;
    end;
   hence F3(x) in Y;
  end;
  ex f2 being Function of Fin X,Y st for x being set st x in Fin X holds
               f2.x = F3(x) from FUNCT_2:sch 2(A7);then
  consider G0 being Function of Fin X,Y such that
  A8: for x being set st x in Fin X holds G0.x = F3(x);
  deffunc F2(set) = IFEQ(idseq n,$1,1.K,0.K);
  A9: for x being set st x in X holds F2(x) in Y;
  ex f2 being Function of X,Y st for x being set st x in X holds
               f2.x = F2(x) from FUNCT_2:sch 2(A9);then
  consider f2 being Function of X,Y such that
  A10: for x being set st x in X holds f2.x = F2(x);
  A11: (B <> {} or F has_a_unity) by A3,MATRIX_2:def 17;
  dom (f2)=Permutations(n) by FUNCT_2:def 1;then
  A12: dom (Path_product(1.(K,n)))=dom f2 by FUNCT_2:def 1;
  for x being set st x in dom (Path_product(1.(K,n)))
   holds (Path_product(1.(K,n))).x=f2.x
  proof let x be set;assume
    x in dom (Path_product(1.(K,n)));
    for p being Element of Permutations(n) holds
    f2.p = -((the mult of K) $$ Path_matrix(p,(1.(K,n))),p)
    proof let p be Element of Permutations(n);
      A13: len (n |-> 1.K)=n by FINSEQ_2:69;
      defpred P[Nat] means (the mult of K) $$ ( ($1+1) |-> 1.K)=1.K;
      (1 |-> 1.K)= <* 1.K *> by FINSEQ_2:73;then
     A14: P[0] by FINSOP_1:12;
     A15: for k being Nat st P[k] holds P[k+1]
       proof let k be Nat;
        assume A16: P[k];
         A17: (the mult of K) has_a_unity by FVSUM_1:11;
         (k+1+1) |-> 1.K = ((k+1) |-> 1.K) ^ <* 1.K *> by FINSEQ_2:74;then
         (the mult of K) $$ ( (k+1+1) |-> 1.K)
           =(the mult of K).((the mult of K)$$(((k+1) |-> 1.K)),1.K)
                              by FINSOP_1:5,A17
          .= (1.K)*(1.K) by A16,GROUP_1:def 1
          .= 1.K by GROUP_1:def 5;
        hence P[k+1];
       end;
      A18: for k being Nat holds P[k] from NAT_1:sch 1(A14,A15);
      n-'1=n-1 by A1,BINARITH:50;then
      n-'1+1=n;then
      A19: (the mult of K) $$ (n |-> 1.K)=1.K by A18;
      now per cases;
      case p is even;
       hence -(0.K,p)=(0.K) by MATRIX_2:def 16;
      end;
      case not(p is even);then
        -(0.K,p)= -(0.K) by MATRIX_2:def 16 .=0.K by RLVECT_1:25;
       hence -(0.K,p)=0.K;
      end;
      end;then
      A20: -(0.K,p)=0.K;
      now per cases;
      case A21: p=idseq n;
        A22: f2.p=F2(p) by A10
           .= 1.K by A21,CQC_LANG:def 1;
        reconsider p1=p0 as Element of Permutations n
                                   by MATRIX_2:def 11;
        len Permutations n = n by MATRIX_2:20; then
         A23: -((the mult of K) $$ Path_matrix(p,(1.(K,n))),p)
           = (the mult of K) $$ Path_matrix(p,(1.(K,n)))
                    by A21,A6,MATRIX_2:def 16;
               for i,j being Nat st i in dom (n |-> 1.K)
                   & j=p.i holds (n |-> 1.K).i=(1.(K,n))*(i,j)
       proof let i,j be Nat;
         assume A24: i in dom (n |-> 1.K) & j=p.i;then
         A25: i in Seg n by FUNCOP_1:19;then
         A26: (n |-> 1.K).i=1.K by FUNCOP_1:13;
         p.i=(id Seg n).i by A21 .=i by FUNCT_1:34,A25;then
         A27: j=i by A24;
         len (1.(K,n))=n & width (1.(K,n))=n &
         Indices (1.(K,n))=[: Seg n,Seg n:] by MATRIX_1:24,A2;then
         A28: i in dom (1.(K,n)) by A25,FINSEQ_1:def 3;
         j in Seg n by A24,A25,Th14;then
         i in dom (1.(K,n)) & j in Seg width (1.(K,n))
                             by A28,A2,MATRIX_1:24;then
         [i,j] in Indices (1.(K,n)) by ZFMISC_1:def 2;
        hence (n |-> 1.K).i=(1.(K,n))*(i,j) by A26,MATRIX_1:def 12,A27;
       end;
       hence f2.p = -((the mult of K) $$ Path_matrix(p,(1.(K,n))),p)
                            by A23,A22,A19,A13,MATRIX_3:def 7;
      end;
      case A29: p<>idseq n;
        consider s being FinSequence such that
        A30: s=p & len p=len s by MATRIX_2:def 12;
        A31: not(len s=len idseq n &
         for i st 1<=i & i<= len s holds s.i=(idseq n).i)
                                      by A30,A29,FINSEQ_1:18;
        A32: len p=n by MATRIX_2:22;then
        consider i0 being Nat such that
        A33:  1<=i0 & i0<=len p & p.i0<>(idseq n).i0
                        by A31,A30,FINSEQ_2:55;
        A34: i0 in Seg n by A33,A32;then
        A35: p.i0<>i0 by A33,FUNCT_1:35;
        p is Permutation of Seg n by MATRIX_2:def 11;then
        A36: p.i0 in Seg n by FUNCT_2:7,A34;then
        reconsider j0=p.i0 as Nat;
        A37: f2.p=F2(p) by A10
           .= 0.K by A29,CQC_LANG:def 1;
        A38: len (Path_matrix(p,(1.(K,n))))=n &
        for i,j st i in dom (Path_matrix(p,(1.(K,n)))) &
        j=p.i holds (Path_matrix(p,(1.(K,n)))).i=(1.(K,n))*(i,j)
                                      by MATRIX_3:def 7;then
        i0 in dom (Path_matrix(p,(1.(K,n)))) by A34,FINSEQ_1:def 3;then
        A39: (Path_matrix(p,(1.(K,n)))).i0=(1.(K,n))*(i0,j0) by A38;
        len (1.(K,n))=n & width (1.(K,n))=n &
         Indices (1.(K,n))=[: Seg n,Seg n:] by MATRIX_1:24,A2;then
        [i0,j0] in Indices (1.(K,n)) by A34,A36,ZFMISC_1:def 2;then
        A40: (Path_matrix(p,(1.(K,n)))).i0=0.K
                              by A39,MATRIX_1:def 12,A35;
          defpred P3[Nat] means $1 < n implies
          ex p3 being FinSequence of K st len p3=$1+1 &
            p3.1=(Path_matrix(p,(1.(K,n)))).1 &
         (for n3 being Nat st 0 <> n3 & n3 < $1+1 & n3<n holds
           p3.(n3 + 1) = (the mult of K).
                  (p3.n3,(Path_matrix(p,(1.(K,n)))).(n3 + 1)));
         1 in Seg len (Path_matrix(p,(1.(K,n)))) by A1,A38;then
         1 in dom (Path_matrix(p,(1.(K,n)))) by FINSEQ_1:def 3;then
         A41: (Path_matrix(p,(1.(K,n)))).1 in rng ((Path_matrix(p,(1.(K,n)))))
                                        by FUNCT_1:def 5;
         rng ((Path_matrix(p,(1.(K,n))))) c= the carrier of K
                                     by FINSEQ_1:def 4;then
         reconsider d=(Path_matrix(p,(1.(K,n)))).1
                                  as Element of K by A41;
         reconsider q3=<* d *> as FinSequence of K;
         A42: len q3=1 by FINSEQ_1:56;
         A43: q3.1=(Path_matrix(p,(1.(K,n)))).1 by FINSEQ_1:57;
         for n3 being Nat st 0 <> n3 & n3 < 0+1 & n3< n holds
           q3.(n3 + 1) = (the mult of K).
                  (q3.n3,(Path_matrix(p,(1.(K,n)))).(n3 + 1))
          proof let n3 be Nat;
            assume A44: 0 <> n3 & n3 < 0+1 & n3< n;then
            0<n3;
           hence thesis by A44,NAT_1:38;
          end;then
         A45: P3[0] by A42,A43;
         A46: for k being Nat st P3[k] holds P3[k+1]
          proof let k be Nat;
            assume A47: P3[k];
            now per cases;
            case A48: k+1<n;then
            consider p3 being FinSequence of K such that
             A49: len p3=k+1 &
              p3.1=(Path_matrix(p,(1.(K,n)))).1 &
             (for n3 being Nat st 0 <> n3 & n3 < k+1 & n3<n
              holds
               p3.(n3 + 1) = (the mult of K).
                  (p3.n3,(Path_matrix(p,(1.(K,n)))).(n3 + 1)))
                                      by A47,NAT_1:37;
              defpred P6[set,set] means
                ($1 in Seg (k+1) implies $2=p3.$1)&
                 (not $1 in Seg (k+1) implies $2=0.K);
              A50: for x being set st x in NAT ex y being set
                      st y in the carrier of K & P6[x,y]
                proof let x be set;assume x in NAT;
                 now per cases;
                 case A51: x in Seg (k+1);then
                  reconsider nx=x as Nat;
                  nx in dom p3 by A51,A49,FINSEQ_1:def 3;then
                  A52: p3.nx in rng p3 by FUNCT_1:def 5;
                  rng p3 c= the carrier of K by FINSEQ_1:def 4;then
                  reconsider s=p3.nx as Element of K by A52;
                  (x in Seg (k+1) implies s=p3.x)&
                 (not x in Seg (k+1) implies s=0.K) by A51;
                 hence ex y being set
                      st y in the carrier of K & P6[x,y];
                 end;
                 case not x in Seg (k+1);
                  hence ex y being set
                      st y in the carrier of K & P6[x,y];
                 end;
                 end;
                hence ex y being set
                      st y in the carrier of K & P6[x,y];
                end;
              ex f6 being Function of NAT,the carrier of K st
                    for x being set st x in NAT holds P6[x,f6.x]
                                    from FUNCT_2:sch 1(A50);then
              consider f6 being Function of NAT,the carrier of K such that
              A53:  for x being set st x in NAT holds P6[x,f6.x];
              A54: rng ((Path_matrix(p,(1.(K,n))))) c= the carrier of K
                                              by FINSEQ_1:def 4;
              1<=k+1+1 & k+1+1 <=n by A48,NAT_1:37,38;then
              k+1+1 in Seg len (Path_matrix(p,(1.(K,n)))) by A38;then
              k+1+1 in dom (Path_matrix(p,(1.(K,n)))) by FINSEQ_1:def 3;then
              (Path_matrix(p,(1.(K,n)))).(k + 1+1) in
                      rng (Path_matrix(p,(1.(K,n)))) by FUNCT_1:def 5;then
              A55: (Path_matrix(p,(1.(K,n)))).(k + 1+1)
                          in the carrier of K by A54;
              [f6.(k+1),(Path_matrix(p,(1.(K,n)))).(k + 1+1)]
                             in [:the carrier of K, the carrier of K:]
                                  by A55,ZFMISC_1:def 2;then
              reconsider e=(the mult of K).
                  (f6.(k+1),(Path_matrix(p,(1.(K,n)))).(k + 1+1))
                     as Element of K by FUNCT_2:7;
             reconsider q3=p3^<* e *> as FinSequence of K;
            A56: len q3=(len p3) + len (<*e*>) by FINSEQ_1:35
                 .=k+1+1 by A49,FINSEQ_1:57;
                      1<=k+1 by NAT_1:37;then
            1 in Seg len p3 by A49;then
            1 in dom p3 by FINSEQ_1:def 3;then
            A57: q3.1=p3.1 by FINSEQ_1:def 7
                   .=(Path_matrix(p,(1.(K,n)))).1 by A49;
            for n3 being Nat st 0 <> n3 & n3 < k+1+1 & n3<n
              holds
               q3.(n3 + 1) = (the mult of K).
                  (q3.n3,(Path_matrix(p,(1.(K,n)))).(n3 + 1))
            proof let n3 be Nat;
              assume A58: 0 <> n3 & n3 < k+1+1 & n3<n;
                now per cases;
                case A59: n3<k+1;then
                  A60: p3.(n3 + 1) = (the mult of K).
                  (p3.n3,(Path_matrix(p,(1.(K,n)))).(n3 + 1)) by A49,A58;
                  1<=n3+1 & n3+1<=k+1 by A59,NAT_1:37,38;then
                  n3+1 in Seg len p3 by A49;then
                  n3+1 in dom p3 by FINSEQ_1:def 3;then
                  A61: p3.(n3+1)=q3.(n3+1) by FINSEQ_1:def 7;
                  0<n3 by A58;then
                  0+1<=n3 by NAT_1:38;then
                  n3 in Seg len p3 by A59,A49;then
                  n3 in dom p3 by FINSEQ_1:def 3;
                  hence q3.(n3 + 1) = (the mult of K).
                  (q3.n3,(Path_matrix(p,(1.(K,n)))).(n3 + 1))
                                   by A61,A60,FINSEQ_1:def 7;
                end;
                case n3>=k+1;then
                  A62: n3+1>k+1 by NAT_1:38;
                  A63: n3+1<=k+1+1 by A58,NAT_1:38;
                  n3+1>=k+1+1 by A62,NAT_1:38;then
                  A64: n3+1=k+1+1 by A63,XXREAL_0:1;
                  1<=k+1 by NAT_1:37;then
                  A65: k+1 in Seg (k+1);then
                  A66: k+1 in dom p3 by A49,FINSEQ_1:def 3;
                  A67: f6.(k+1)=p3.(k+1) by A53,A65;
                  A68: q3.(k+1)=p3.(k+1) by A66,FINSEQ_1:def 7;
                  (q3).(n3+1) = (<*e*>).(n3+1 - (k+1))
                                  by FINSEQ_1:37,A62,A49,A56,A63
                             .= e by A64,FINSEQ_1:def 8;
                 hence q3.(n3 + 1) = (the mult of K).
                  (q3.n3,(Path_matrix(p,(1.(K,n)))).(n3 + 1))
                                 by A67,A68,A64;
                end;
                end;
               hence q3.(n3 + 1) = (the mult of K).
                  (q3.n3,(Path_matrix(p,(1.(K,n)))).(n3 + 1));
            end;
            hence P3[k+1] by A56,A57;
            end;
            case k+1>=n;
            hence P3[k+1];
            end;
            end;
           hence P3[k+1];
          end;
         for k being Nat holds P3[k] from NAT_1:sch 1(A45,A46);then
         A69: P3[n-'1];
         A70: n-1=n-'1 by A1,BINARITH:50;then
         A71: n-'1+1=n;
         n<n+1 by NAT_1:38;then
         n-1<n+1-1 by REAL_1:92;then
         consider p3 being FinSequence of K such that
         A72: len p3=n-'1+1 &
            p3.1=(Path_matrix(p,(1.(K,n)))).1 &
         (for n3 being Nat st 0 <> n3 & n3 < n-'1+1 & n3<n holds
           p3.(n3 + 1) = (the mult of K).
                  (p3.n3,(Path_matrix(p,(1.(K,n)))).(n3 + 1))) by A69,A70;
          A73: len p3=n by A71,A72;
        reconsider A=NAT as non empty set;
        defpred P[set,set] means
           ($1 in Seg n implies $2=p3.$1)&
           (not $1 in Seg n implies $2=0.K);
         A74: for x3 being Element of A
                 ex y3 being Element of K st P[x3,y3]
         proof let x3 be Element of A;
           now per cases;
           case A75: x3 in Seg n;
             reconsider m=x3 as Nat;
             A76: rng p3 c= the carrier of K by FINSEQ_1:def 4;
             x3 in dom p3 by FINSEQ_1:def 3,A75,A73;then
             p3.x3 in rng p3 by FUNCT_1:def 5;
            hence ex y3 being Element of K st P[x3,y3] by A75,A76;
           end;
           case not x3 in Seg n;
            hence ex y3 being Element of K st P[x3,y3];
           end;
           end;
          hence ex y3 being Element of K st P[x3,y3];
         end;
        ex f4 being Function of A,the carrier of K st
         (for x2 being Element of A holds P[x2,f4.x2])
                   from FUNCT_2:sch 3(A74); then
        consider f4 being Function of NAT,the carrier of K such that
        A77: for x4 being Element of NAT holds
            (x4 in Seg n implies f4.x4=p3.x4)&
           (not x4 in Seg n implies f4.x4=0.K);
         1 in Seg n by A1;then
        A78: f4.1=p3.1 by A77 .=(Path_matrix(p,(1.(K,n)))).1 by A72;
        A79: for n3 being Nat
          st 0 <> n3 & n3 < len (Path_matrix(p,(1.(K,n)))) holds
           f4.(n3 + 1) = (the mult of K).
                  (f4.n3,(Path_matrix(p,(1.(K,n)))).(n3 + 1))
         proof let n3 be Nat;
           assume A80: 0 <> n3 & n3 < len (Path_matrix(p,(1.(K,n))));then
            n3>0;then
            A81: 0+1<=n3 by NAT_1:38;then
            A82: 1<n3+1 by NAT_1:38;
            n3+1<=len (Path_matrix(p,(1.(K,n)))) by A80,NAT_1:38;then
            n3+1 in Seg n by A38,A82;then
            A83: f4.(n3+1)=p3.(n3+1) by A77;
            A84: p3.(n3 + 1) = (the mult of K).
                  (p3.n3,(Path_matrix(p,(1.(K,n)))).(n3 + 1))
                                   by A80,A72,A38,A70;
            n3 in Seg n by A38,A80,A81;
          hence f4.(n3 + 1) = (the mult of K).
            (f4.n3,(Path_matrix(p,(1.(K,n)))).(n3 + 1)) by A83,A84,A77;
         end;
         defpred P5[Nat] means f4.(i0+$1)=0.K;
         A85: 0<i0 by A33;
         now per cases;
         case i0<=1;then
          i0=1 by A33,XXREAL_0:1;
          hence P5[0] by A40,A78;
         end;
         case A86: i0>1;then
           i0-1> 1-1 by REAL_1:92;then
           A87: i0-1>0;
           i0<i0+1 by NAT_1:38;then
           i0-1<i0+1-1 by REAL_1:92;then
           A88: i0-1<len (Path_matrix(p,(1.(K,n))))
                          by A33,A32,A38,XXREAL_0:2;
           reconsider a=f4.(i0-'1) as Element of K;
           i0-'1=i0-1 by BINARITH:50,A86;then
           i0-'1+1=i0;then
           f4.(i0) = (the mult of K).
                  (f4.(i0-'1),(Path_matrix(p,(1.(K,n)))).(i0))
                            by A87,A79,A88
                  .= a*(0.K) by A40,GROUP_1:def 1
                  .= 0.K by VECTSP_1:36;
           hence P5[0];
          end;
          end;then
           A89: P5[0];
           A90: for k being Nat st P5[k] holds P5[k+1]
           proof let k be Nat;
            assume P5[k];then
             A91: f4.(i0+k)=0.K;
             A92: 1<=1+(i0+k) by NAT_1:37;
             now per cases;
             case A93: i0+k+1<=n;
               A94: i0<=i0+k by NAT_1:37;
               A95: i0+k<n by A93,NAT_1:38;
               A96: 0<i0+k by A85,A94;
               A97: 0 <> i0+k & i0+k < n-'1+1 by A71,A95,A85,A94;
               0+1<=i0+k by A96,NAT_1:38;then
               i0+k in Seg n by A95;then
               A98: p3.(i0+k)=0.K by A77,A91;
               1<=1+(i0+k) by NAT_1:37;then
               i0+k+1 in Seg len (Path_matrix(p,(1.(K,n))))
                                  by A93,A38;then
               i0+k+1 in dom (Path_matrix(p,(1.(K,n))))
                                  by FINSEQ_1:def 3;then
               A99: (Path_matrix(p,(1.(K,n)))).(i0+k+1)
                            in rng (Path_matrix(p,(1.(K,n))))
                                  by FUNCT_1:def 5;
               rng (Path_matrix(p,(1.(K,n)))) c= the carrier of K
                                   by FINSEQ_1:def 4;then
               reconsider b=(Path_matrix(p,(1.(K,n)))).(i0+k+1)
                             as Element of K by A99;
               i0+k+1 in Seg n by A93,A92;then
               f4.(i0+k+1)=p3.(i0+k+1) by A77
                          .=(the mult of K).
                   (p3.(i0+k),(Path_matrix(p,(1.(K,n)))).(i0+k+1))
                           by A72,A97,A95
                         .= (0.K)*(b) by GROUP_1:def 1,A98
                         .= 0.K by VECTSP_1:36;
              hence f4.(i0+(k+1))=0.K;
             end;
             case i0+k+1>n;then
               not (i0+(k+1) in Seg n) by FINSEQ_1:3;
              hence f4.(i0+(k+1))=0.K by A77;
             end;
             end;
            hence P5[k+1];
           end;
           for k being Nat holds P5[k] from NAT_1:sch 1(A89,A90);then
           A100: P5[n-'i0];
           n-'i0 =n-i0 by A33,A32,BINARITH:50;
       hence f2.p = -((the mult of K) $$ Path_matrix(p,(1.(K,n))),p)
                  by A78,A79,A100,A37,A20,FINSOP_1:3,A38,A1;
      end;
      end;
     hence f2.p = -((the mult of K) $$ Path_matrix(p,(1.(K,n))),p);
    end;
   hence (Path_product(1.(K,n))).x=f2.x by MATRIX_3:def 8;
  end;then
  A101: Path_product(1.(K,n))=f2 by A12,FUNCT_1:9;
   now assume A102: idseq n in B;
         thus G0.(B)=IFIN(idseq n,B,1.K,0.K) by A8
                            .=1.K by A102,Def1;
   end;then
  A103: G0.B=1.K by A5,A4;
  A104: for e being Element of Y st e is_a_unity_wrt F holds G0.({}) = e
    proof let e be Element of Y;
      assume e is_a_unity_wrt F;then
      A105: F.(0.K,e)=0.K by BINOP_1:11;
      0.K is_a_unity_wrt F by FVSUM_1:8;then
      A106: F.(0.K,e)=e by BINOP_1:11;
      A107: {} in Fin X by FINSUB_1:18;
      IFIN(idseq n,{},1.K,0.K)=0.K by Def1;
     hence G0.({}) = e by A105,A106,A8,A107;
    end;
  A108: for x being Element of X holds G0.({x}) = f.x
   proof let x be Element of X;
     now per cases;
     case A109: x=idseq n;
       A110: f2.x=F2(x) by A10
          .=1.K by A109,CQC_LANG:def 1;
       idseq n in {x} by A109,TARSKI:def 1;then
       IFIN(idseq n,{x},1.K,0.K)=1.K by Def1;
      hence G0.({x})=f2.x by A110,A8;
     end;
     case A111: x <> idseq n;
       A112: f2.x=F2(x) by A10
          .=0.K by A111,CQC_LANG:def 1;
       not idseq n in {x} by A111,TARSKI:def 1;then
       IFIN(idseq n,{x},1.K,0.K)=0.K by Def1;
      hence G0.({x})=f2.x by A112,A8;
     end;
     end;
    hence G0.({x}) = f.x by A101;
   end;
   for B' being Element of Fin X st B' c= B & B' <> {}
      for x being Element of X st x in B\B' holds G0.(B' \/ {x})
                    = F.(G0.B',f.x)
   proof let B' be Element of Fin X;
     assume B' c= B & B' <> {};
     thus for x being Element of X st x in B\B' holds G0.(B' \/ {x})
                    = F.(G0.B',f.x)
     proof let x be Element of X;
       assume A113: x in B\B';
         A114: now assume A115: idseq n in B' \/ {x};
         thus G0.(B' \/ {x})=IFIN(idseq n,B' \/ {x},1.K,0.K) by A8
                            .=1.K by A115,Def1;
        end;
        now assume A116: not idseq n in B' \/ {x};
         thus G0.(B' \/ {x})=IFIN(idseq n,B' \/ {x},1.K,0.K) by A8
                            .=0.K by A116,Def1;
         end;then
        A117: (idseq n in B' \/ {x} implies G0.(B' \/ {x})=1.K)&
          (not idseq n in B' \/ {x} implies G0.(B' \/ {x})=0.K) by A114;
        A118: now assume A119: idseq n in B';
         thus G0.(B')=IFIN(idseq n,B',1.K,0.K) by A8
                            .=1.K by A119,Def1;
        end;
        now assume A120: not idseq n in B';
         thus G0.(B')=IFIN(idseq n,B',1.K,0.K) by A8
                            .=0.K by A120,Def1;
        end;then
        A121: (idseq n in B' implies G0.B'=1.K)&
            (not idseq n in B' implies G0.B'=0.K) by A118;
        A122: f.x=f2.x by A101;
        A123: 0.K is_a_unity_wrt F by FVSUM_1:8;
        A124: now assume idseq n in B' \/ {x};then
          idseq n in B' or idseq n in {x} by XBOOLE_0:def 2;then
          A125: idseq n in B' or idseq n =x by TARSKI:def 1;
          now per cases by A125;
          case A126: idseq n in B';then
           A127: G0.B'=1.K by A121;
           A128: not x in B' by A113,XBOOLE_0:def 4;
           A129: x <> idseq n by A126,A128;
           f.x=F2(x) by A10,A122
              .=0.K by A129,CQC_LANG:def 1;
           hence F.(G0.B',f.x)=F.(1.K,0.K) by A127.=1.K by A123,BINOP_1:11;
          end;
          case A130: idseq n=x;
            f.x=F2(x) by A10,A122
              .=1.K by A130,CQC_LANG:def 1;
           hence F.(G0.B',f.x)=F.(0.K,1.K) by A121,A130,A113,XBOOLE_0:def 4
                             .=1.K by A123,BINOP_1:11;
          end;
          end;
         hence F.(G0.B',f.x)=1.K;
        end;
        now assume A131: not idseq n in B' \/ {x};then
          A132: G0.B'=0.K by A121,XBOOLE_0:def 2;
          not idseq n in {x} by A131,XBOOLE_0:def 2;then
          A133: not idseq n=x by TARSKI:def 1;
          f.x=F2(x) by A10,A122
              .=0.K by A133,CQC_LANG:def 1;
         hence F.(G0.B',f.x)=F.(0.K,0.K) by A132 .=0.K by A123,BINOP_1:11;
        end;
      hence G0.(B' \/ {x}) = F.(G0.B',f.x) by A117,A124;
     end;
   end;then
  (the add of K) $$ (FinOmega(Permutations n),Path_product(1.(K,n)))=1.K
                                       by SETWISEO:def 3,A11,A103,A104,A108;
 hence thesis by MATRIX_3:def 9;
end;

definition let K be Field, n be Nat, M be Matrix of n,K;
  redefine attr M is diagonal means :Def2:
    for i,j being Nat st i in Seg n & j in Seg n & i <> j holds
      M*(i,j) = 0.K;
  compatibility
  proof
    hereby assume
A1:   M is diagonal;
      let i, j be Nat;
      assume
A2:   i in Seg n & j in Seg n & i <> j; then
      [i,j] in [:Seg n, Seg n:] by ZFMISC_1:def 2; then
      [i,j] in Indices M by MATRIX_1:25;
      hence M*(i,j) = 0.K by A1,A2,MATRIX_1:def 15;
    end;
    assume
A3: for i,j being Nat st i in Seg n & j in Seg n & i <> j holds
      M*(i,j) = 0.K;
    let i,j;
    assume
A4: [i,j] in Indices M & M*(i,j) <> 0.K; then
    [i,j] in [:Seg n, Seg n:] by MATRIX_1:25; then
    i in Seg n & j in Seg n by ZFMISC_1:106;
    hence thesis by A3,A4;
  end;
end;

theorem
  for K being Field, n being Nat, A being Matrix of n,K st
    n>=1 & A is diagonal holds
      Det A = (the mult of K) $$ (diagonal_of_Matrix A)
proof let K be Field, n be Nat,
  A be Matrix of n,K;
  assume A1: n>=1 & A is diagonal;
A2: len (diagonal_of_Matrix A)=n by MATRIX_3:def 10;
  set F4=diagonal_of_Matrix A;
  consider f4 being Function of NAT,the carrier of K such that
   A3:  f4.1 = F4.1 &
     (for n4 being Nat st 0 <> n4 & n4 < len F4 holds
       f4.(n4 + 1) = (the mult of K).(f4.n4,F4.(n4 + 1)))
       & (the mult of K)$$ (diagonal_of_Matrix A) = f4.(len F4)
                               by FINSOP_1:def 1,A1,A2;
  deffunc F5(Nat) = f4.$1;
  ex z being FinSequence of K st len z = n &
   for j st j in Seg n holds z.j = F5(j) from FINSEQ_2:sch 1;then
  consider ps being FinSequence of K such that
  A4: len ps = n &
   for j st j in Seg n holds ps.j = f4.j;
   1 in Seg n by A1;then
   A5: ps.1=f4.1 by A4 .=F4.1 by A3;
   A6: for i being Nat st 1<=i & i+1 in Seg n holds
               ps.(i+1)=(the mult of K).((ps.i),
                 ((diagonal_of_Matrix A).(i+1)))
    proof let i be Nat;
      assume A7: 1<=i & i+1 in Seg n;then
      A8: 1<=i+1 & i+1<=n by FINSEQ_1:3;
      i<=i+1 by NAT_1:37;then
      i<=n by A8,XXREAL_0:2;then
      i in Seg n by A7;then
      A9: ps.i=f4.i by A4;
      i<i+1 by XREAL_1:31;then
      i<n by A8,XXREAL_0:2;then
      f4.(i + 1) = (the mult of K).(f4.i,F4.(i + 1)) by A3,A7,A2;
     hence ps.(i+1)=(the mult of K).((ps.i)
                         ,((diagonal_of_Matrix A).(i+1))) by A9,A4,A7;
    end;
    n in Seg n by A1;then
   A10: ps.1=(diagonal_of_Matrix A).1 &
   (for i being Nat st 1<=i & i+1 in Seg n holds
                    ps.(i+1)=(the mult of K).((ps.i)
                         ,((diagonal_of_Matrix A).(i+1))))&
     ps.n=(the mult of K)$$ (diagonal_of_Matrix A)
                               by A5,A6,A2,A3,A4;
     1 in Seg len (diagonal_of_Matrix A) by A2,A1;then
     1 in dom (diagonal_of_Matrix A) by FINSEQ_1:def 3;then
     A11: (diagonal_of_Matrix A).1 in rng diagonal_of_Matrix A
                                        by FUNCT_1:def 5;
     rng (diagonal_of_Matrix A) c= the carrier of K by FINSEQ_1:def 4;then
     reconsider dps1= ps.1 as Element of K by A10,A11;
  set B=FinOmega(Permutations(n));
  A12: Permutations(n) is finite by MATRIX_2:30;then
  A13: B=Permutations(n) by MATRIX_2:def 17;
  (idseq n) is Element of Group_of_Perm(n) by MATRIX_2:23;then
  (idseq n) in the carrier of Group_of_Perm(n);then
  A14: (idseq n) in Permutations(n) by MATRIX_2:def 13;
  reconsider p0=idseq n as Permutation of Seg n;
  A15: p0 is even by MATRIX_2:29;
  set f=Path_product(A);
  set F=the add of K;
  set Y=the carrier of K;
  set X=Permutations n;
  set k1=(the mult of K)$$(diagonal_of_Matrix A);
  deffunc F3(set) = IFIN(idseq n,$1,k1,0.K);
  A16: for x being set st x in Fin X holds F3(x) in Y
  proof let x be set;
   assume x in Fin X;
    per cases;
    suppose idseq n in x;then
      F3(x)=k1 by Def1;
     hence F3(x) in Y;
    end;
    suppose not idseq n in x;then
      F3(x)=0.K by Def1;
     hence F3(x) in Y;
    end;
  end;
  ex f2 being Function of Fin X,Y st for x being set st x in Fin X holds
               f2.x = F3(x) from FUNCT_2:sch 2(A16);then
  consider G0 being Function of Fin X,Y such that
  A17: for x being set st x in Fin X holds G0.x = F3(x);
  deffunc F2(set) = IFEQ(idseq n,$1,k1,0.K);
  A18: for x being set st x in X holds F2(x) in Y;
  ex f2 being Function of X,Y st for x being set st x in X holds
               f2.x = F2(x) from FUNCT_2:sch 2(A18);then
  consider f2 being Function of X,Y such that
  A19: for x being set st x in X holds f2.x = F2(x);
  A20: (B <> {} or F has_a_unity) by A12,MATRIX_2:def 17;
  dom (f2)=Permutations(n) by FUNCT_2:def 1;then
  A21: dom (Path_product A)=dom f2 by FUNCT_2:def 1;
  for x being set st x in dom (Path_product A)
   holds (Path_product A).x=f2.x
  proof let x be set;assume x in dom Path_product A;
    for p being Element of Permutations n holds
    f2.p = -((the mult of K) $$ Path_matrix(p,A),p)
    proof let p be Element of Permutations n;
      A22: len (diagonal_of_Matrix A)=n by A2;
      now per cases;
      suppose p is even;
       hence -(0.K,p)=(0.K) by MATRIX_2:def 16;
      end;
      suppose p is odd;then
        -(0.K,p)= -(0.K) by MATRIX_2:def 16 .=0.K by RLVECT_1:25;
       hence -(0.K,p)=0.K;
      end;
      end;then
A23: -(0.K,p)=0.K;
      now per cases;
      case A24: p=idseq n;
        A25: f2.p=F2(p) by A19
           .= k1 by A24,CQC_LANG:def 1;
        reconsider p1=p0 as Element of Permutations n by MATRIX_2:def 11;
        len Permutations n = n by MATRIX_2:20; then
        A26: -((the mult of K) $$ Path_matrix(p,A),p)
           = (the mult of K) $$ Path_matrix(p,(A))
                    by A24,A15,MATRIX_2:def 16;
       for i,j being Nat st i in dom ((diagonal_of_Matrix A))
                   & j=p.i holds ((diagonal_of_Matrix A)).i=(A)*(i,j)
       proof let i,j be Nat;
         assume A27: i in dom ((diagonal_of_Matrix A)) & j=p.i;then
         A28: i in Seg n by A2,FINSEQ_1:def 3;
         p.i=(id Seg n).i by A24 .=i by FUNCT_1:34,A28;
        hence ((diagonal_of_Matrix A)).i=(A)*(i,j)
                                     by MATRIX_3:def 10,A28,A27;
       end;
       hence f2.p
        = -((the mult of K) $$ Path_matrix(p,(A)),p)
                                 by A26,A25,A22,MATRIX_3:def 7;
      end;
      case A29: p<>idseq n;
        consider s being FinSequence such that
        A30:  s=p & len p=len s by MATRIX_2:def 12;
        A31: not(len s=len (idseq n) &
         for i st 1<=i & i<= len s holds s.i=(idseq n).i)
                                      by A30,A29,FINSEQ_1:18;
        A32: len p=n by MATRIX_2:22;
        consider i0 being Nat such that
        A33:  1<=i0 & i0<=len p & p.i0<>(idseq n).i0
                                      by A32,A31,A30,FINSEQ_2:55;
        A34: i0 in Seg n by A33,A32;then
        A35: p.i0<>i0 by A33,FUNCT_1:35;
        p is Permutation of Seg n by MATRIX_2:def 11;then
        A36: p.i0 in Seg n by FUNCT_2:7,A34;then
         reconsider j0=p.i0 as Nat;
        A37: f2.p=F2(p) by A19
           .= 0.K by A29,CQC_LANG:def 1;
        A38: len (Path_matrix(p,A))=n &
          for i,j st i in dom (Path_matrix(p,A)) &
           j=p.i holds (Path_matrix(p,A)).i=A*(i,j) by MATRIX_3:def 7;then
        A39: i0 in Seg len (Path_matrix(p,A)) by A34;then
        i0 in dom (Path_matrix(p,A)) by FINSEQ_1:def 3;then
        A40: (Path_matrix(p,A)).i0=(A)*(i0,j0) by A38;
        A41: (Path_matrix(p,A)).i0=0.K
                                 by A40,Def2,A35,A39,A38,A1,A36;
        defpred P3[Nat] means $1 < n implies
          ex p3 being FinSequence of K st len p3=$1+1 &
            p3.1=(Path_matrix(p,A)).1 &
         (for n3 being Nat st 0 <> n3 & n3 < $1+1 & n3<n holds
           p3.(n3 + 1) = (the mult of K).
                  (p3.n3,(Path_matrix(p,A)).(n3 + 1)));
         1 in Seg len (Path_matrix(p,A)) by A1,A38;then
         1 in dom (Path_matrix(p,A)) by FINSEQ_1:def 3;then
         A42: (Path_matrix(p,A)).1 in rng ((Path_matrix(p,A)))
                                        by FUNCT_1:def 5;
         rng ((Path_matrix(p,A))) c= the carrier of K
                                     by FINSEQ_1:def 4;then
         reconsider d=(Path_matrix(p,A)).1 as Element of K by A42;
         reconsider q3=<* d *> as FinSequence of K;
         A43: len q3=1 by FINSEQ_1:56;
         A44: q3.1=(Path_matrix(p,A)).1 by FINSEQ_1:57;
         for n3 being Nat st 0 <> n3 & n3 < 0+1 & n3< n holds
           q3.(n3 + 1) = (the mult of K).
                  (q3.n3,(Path_matrix(p,A)).(n3 + 1))
          proof let n3 be Nat;
            assume A45: 0 <> n3 & n3 < 0+1 & n3< n;then
            0<n3;
           hence thesis by A45,NAT_1:38;
          end;then
         A46: P3[0] by A43,A44;
         A47: for k being Nat st P3[k] holds P3[k+1]
          proof let k be Nat;
            assume A48: P3[k];
            now per cases;
            case A49: k+1<n;then
            consider p3 being FinSequence of K such that
             A50: len p3=k+1 &
              p3.1=(Path_matrix(p,A)).1 &
             (for n3 being Nat st 0 <> n3 & n3 < k+1 & n3<n holds
               p3.(n3 + 1) = (the mult of K).
                  (p3.n3,(Path_matrix(p,A)).(n3 + 1))) by A48,NAT_1:37;
              defpred P6[set,set] means
                ($1 in Seg (k+1) implies $2=p3.$1)&
                 (not $1 in Seg (k+1) implies $2=0.K);
              A51: for x being set st x in NAT ex y being set
                      st y in the carrier of K & P6[x,y]
                proof let x be set;assume x in NAT;
                 now per cases;
                 case A52: x in Seg (k+1);then
                  reconsider nx=x as Nat;
                  nx in dom p3 by A52,A50,FINSEQ_1:def 3;then
                  A53: p3.nx in rng p3 by FUNCT_1:def 5;
                  rng p3 c= the carrier of K by FINSEQ_1:def 4;then
                  reconsider s=p3.nx as Element of K by A53;
                  (x in Seg (k+1) implies s=p3.x)&
                 (not x in Seg (k+1) implies s=0.K) by A52;
                 hence ex y being set st y in the carrier of K & P6[x,y];
                 end;
                 case not x in Seg (k+1);
                  hence ex y being set st y in the carrier of K & P6[x,y];
                 end;
                 end;
                hence ex y being set st y in the carrier of K & P6[x,y];
                end;
              ex f6 being Function of NAT,the carrier of K st
                    for x being set st x in NAT holds P6[x,f6.x]
                                    from FUNCT_2:sch 1(A51);then
              consider f6 being Function of NAT,the carrier of K such that
              A54:  for x being set st x in NAT holds P6[x,f6.x];
              A55: rng ((Path_matrix(p,A))) c= the carrier of K
                                              by FINSEQ_1:def 4;
              1<=k+1+1 & k+1+1 <=n by A49,NAT_1:37,38;then
              k+1+1 in Seg len (Path_matrix(p,A)) by A38;then
              k+1+1 in dom (Path_matrix(p,A)) by FINSEQ_1:def 3;then
              (Path_matrix(p,A)).(k + 1+1) in
                      rng (Path_matrix(p,A)) by FUNCT_1:def 5;then
              A56: (Path_matrix(p,A)).(k + 1+1)
                          in the carrier of K by A55;
              [f6.(k+1),(Path_matrix(p,A)).(k + 1+1)]
                             in [:the carrier of K, the carrier of K:]
                                  by A56,ZFMISC_1:def 2;then
              reconsider e=(the mult of K).
                  (f6.(k+1),(Path_matrix(p,A)).(k + 1+1))
                     as Element of K by FUNCT_2:7;
             reconsider q3=p3^<* e *> as FinSequence of K;
            A57: len q3=(len p3) + len (<*e*>) by FINSEQ_1:35
                 .=k+1+1 by A50,FINSEQ_1:57;
                      1<=k+1 by NAT_1:37;then
            1 in Seg len p3 by A50;then
            1 in dom p3 by FINSEQ_1:def 3;then
            A58: q3.1=p3.1 by FINSEQ_1:def 7
                   .=(Path_matrix(p,(A))).1 by A50;
            for n3 being Nat st 0 <> n3 & n3 < k+1+1 & n3<n
              holds
               q3.(n3 + 1) = (the mult of K).
                  (q3.n3,(Path_matrix(p,(A))).(n3 + 1))
            proof let n3 be Nat;
              assume A59: 0 <> n3 & n3 < k+1+1 & n3<n;
                now per cases;
                case A60: n3<k+1;then
                  A61: p3.(n3 + 1) = (the mult of K).
                  (p3.n3,(Path_matrix(p,(A))).(n3 + 1)) by A50,A59;
                  1<=n3+1 & n3+1<=k+1 by A60,NAT_1:37,38;then
                  n3+1 in Seg len p3 by A50;then
                  n3+1 in dom p3 by FINSEQ_1:def 3;then
                  A62: p3.(n3+1)=q3.(n3+1) by FINSEQ_1:def 7;
                  0<n3 by A59;then
                  0+1<=n3 by NAT_1:38;then
                  n3 in Seg len p3 by A50,A60;then
                  n3 in dom p3 by FINSEQ_1:def 3;
                 hence q3.(n3 + 1) = (the mult of K).
                  (q3.n3,(Path_matrix(p,(A))).(n3 + 1))
                                     by A62,A61,FINSEQ_1:def 7;
                end;
                case n3>=k+1;then
                  A63: n3+1>k+1 by NAT_1:38;
                  A64: n3+1<=k+1+1 by NAT_1:38,A59;
                  n3+1>=k+1+1 by A63,NAT_1:38;then
                  A65: n3+1=k+1+1 by A64,XXREAL_0:1;then
                  A66: n3=k+1;
                1<=k+1 by NAT_1:37;then
                A67: k+1 in Seg (k+1);then
                A68: k+1 in dom p3 by A50,FINSEQ_1:def 3;
                A69: f6.(k+1)=p3.(k+1) by A54,A67;
                A70: q3.(k+1)=p3.(k+1) by A68,FINSEQ_1:def 7;
                (q3).(n3+1) = (<*e*>).(n3+1 - (k+1))
                                  by FINSEQ_1:37,A63,A50,A57,A64
                             .= e by A65,FINSEQ_1:def 8;
                 hence q3.(n3 + 1) = (the mult of K).
                  (q3.n3,(Path_matrix(p,(A))).(n3 + 1)) by A69,A70,A66;
                end;
                end;
               hence q3.(n3 + 1) = (the mult of K).
                  (q3.n3,(Path_matrix(p,(A))).(n3 + 1));
            end;
            hence P3[k+1] by A57,A58;
            end;
            case k+1>=n;
            hence P3[k+1];
            end;
            end;
           hence P3[k+1];
          end;
         for k being Nat holds P3[k] from NAT_1:sch 1(A46,A47);then
         A71: P3[n-'1];
         A72: n-1=n-'1 by A1,BINARITH:50;then
         A73: n-'1+1=n;
         n<n+1 by NAT_1:38;then
         n-1<n+1-1 by REAL_1:92;then
         consider p3 being FinSequence of K such that
         A74: len p3=n-'1+1 &
            p3.1=(Path_matrix(p,A)).1 &
         (for n3 being Nat st 0 <> n3 & n3 < n-'1+1 & n3<n holds
           p3.(n3 + 1) = (the mult of K).
                  (p3.n3,(Path_matrix(p,(A))).(n3 + 1))) by A71,A72;
          A75: len p3=n by A73,A74;
        reconsider Ab=NAT as non empty set;
        defpred P[set,set] means
           ($1 in Seg n implies $2=p3.$1)&
           (not $1 in Seg n implies $2=0.K);
A76:   for x3 being Element of Ab
                 ex y3 being Element of K st P[x3,y3]
         proof let x3 be Element of Ab;
           now per cases;
           case A77: x3 in Seg n;
             reconsider m=x3 as Nat;
             A78: rng p3 c= the carrier of K by FINSEQ_1:def 4;
             x3 in dom p3 by FINSEQ_1:def 3,A75,A77;then
             p3.x3 in rng p3 by FUNCT_1:def 5;
            hence ex y3 being Element of K st P[x3,y3] by A77,A78;
           end;
           case not x3 in Seg n;
            hence ex y3 being Element of K st P[x3,y3];
           end;
           end;
          hence ex y3 being Element of K st P[x3,y3];
         end;
        ex f4 being Function of Ab,the carrier of K st
         (for x2 being Element of Ab holds P[x2,f4.x2])
                   from FUNCT_2:sch 3(A76);then
        consider f4 being Function of NAT,the carrier of K such that
        A79: for x4 being Element of NAT holds
            (x4 in Seg n implies f4.x4=p3.x4)&
           (not x4 in Seg n implies f4.x4=0.K);
         1 in Seg n by A1;then
        A80: f4.1=p3.1 by A79 .=(Path_matrix(p,A)).1 by A74;
        A81: for n3 being Nat
          st 0 <> n3 & n3 < len (Path_matrix(p,A)) holds
           f4.(n3 + 1) = (the mult of K).
                  (f4.n3,(Path_matrix(p,A)).(n3 + 1))
         proof let n3 be Nat;
           assume A82: 0 <> n3 & n3 < len (Path_matrix(p,A));then
            n3>0;then
            A83: 0+1<=n3 by NAT_1:38;
            A84: 1<n3+1 by A83,NAT_1:38;
            n3+1<=len (Path_matrix(p,A)) by A82,NAT_1:38;then
            n3+1 in Seg n by A38,A84;then
            A85: f4.(n3+1)=p3.(n3+1) by A79;
            A86: p3.(n3 + 1) = (the mult of K).
                  (p3.n3,(Path_matrix(p,A)).(n3 + 1)) by A82,A74,A38,A72;
            n3 in Seg n by A38,A82,A83;
          hence f4.(n3 + 1) = (the mult of K).
                  (f4.n3,(Path_matrix(p,A)).(n3 + 1)) by A85,A86,A79;
         end;
         defpred P5[Nat] means f4.(i0+$1)=0.K;
         A87: 0<i0 by A33;
         now per cases;
         case i0<=1;then
           i0=1 by A33,XXREAL_0:1;
          hence P5[0] by A41,A80;
         end;
         case A88: i0>1;then
           i0-1> 1-1 by REAL_1:92;then
           A89: i0-1>0;
           i0<i0+1 by NAT_1:38;then
           i0-1<i0+1-1 by REAL_1:92;then
           A90: i0-1<len (Path_matrix(p,A)) by A33,A32,A38,XXREAL_0:2;
          reconsider a=f4.(i0-'1) as Element of K;
          i0-'1=i0-1 by BINARITH:50,A88;then
          i0-'1+1=i0;then
          f4.i0 = (the mult of K).
                  (f4.(i0-'1),(Path_matrix(p,A)).i0) by A89,A81,A90
                  .= a*(0.K) by GROUP_1:def 1,A41
                  .= 0.K by VECTSP_1:36;
           hence P5[0];
          end;
          end;then
           A91: P5[0];
           A92: for k being Nat st P5[k] holds P5[k+1]
           proof let k be Nat;
            assume P5[k];then
             A93: f4.(i0+k)=0.K;
             A94: 1<=1+(i0+k) by NAT_1:37;
             now per cases;
             case A95: i0+k+1<=n;
               A96: i0<=i0+k by NAT_1:37;
               A97: i0+k<n by A95,NAT_1:38;
               A98: 0<i0+k by A87,A96;then
               A99: 0 <> i0+k & i0+k < n-'1+1 by A73,A97;
               0+1<=i0+k by A98,NAT_1:38;then
               i0+k in Seg n by A97;then
               A100: p3.(i0+k)=0.K by A79,A93;
               1<=1+(i0+k) by NAT_1:37;then
               i0+k+1 in Seg len (Path_matrix(p,A))
                                  by A95,A38;then
               i0+k+1 in dom (Path_matrix(p,A)) by FINSEQ_1:def 3;then
               A101: (Path_matrix(p,A)).(i0+k+1)
                            in rng (Path_matrix(p,A)) by FUNCT_1:def 5;
               rng (Path_matrix(p,A)) c= the carrier of K
                                   by FINSEQ_1:def 4;then
               reconsider b=(Path_matrix(p,A)).(i0+k+1)
                             as Element of K by A101;
               i0+k+1 in Seg n by A95,A94;then
               f4.(i0+k+1)=p3.(i0+k+1) by A79
                          .=(the mult of K).
                   (p3.(i0+k),(Path_matrix(p,A)).(i0+k+1)) by A74,A99,A97
                         .= (0.K)*b by GROUP_1:def 1,A100
                         .= 0.K by VECTSP_1:36;
              hence f4.(i0+(k+1))=0.K;
             end;
             case i0+k+1>n;then
              not (i0+(k+1) in Seg n) by FINSEQ_1:3;
              hence f4.(i0+(k+1))=0.K by A79;
             end;
             end;
            hence P5[k+1];
           end;
           for k being Nat holds P5[k] from NAT_1:sch 1(A91,A92);then
           A102: P5[n-'i0];
           n-'i0 =n-i0 by A33,A32,BINARITH:50;
       hence f2.p = -((the mult of K) $$ Path_matrix(p,A),p)
           by A37,A23,FINSOP_1:3,A38,A1,A80,A81,A102;
      end;
      end;
     hence f2.p = -((the mult of K) $$ Path_matrix(p,(A)),p);
    end;
   hence (Path_product(A)).x=f2.x by MATRIX_3:def 8;
  end;then
  A103: Path_product(A)=f2 by A21,FUNCT_1:9;
  now assume A104: idseq n in B;
    thus G0.(B)=IFIN(idseq n,B,k1,0.K) by A17
        .=(the mult of K)$$ (diagonal_of_Matrix A) by A104,Def1;
   end;then
   A105: G0.B=(the mult of K)$$ (diagonal_of_Matrix A) by A14,A13;
  A106: for e being Element of Y st e is_a_unity_wrt F holds G0.({}) = e
    proof let e be Element of Y;assume e is_a_unity_wrt F;then
      A107: F.(0.K,e)=0.K by BINOP_1:11;
      0.K is_a_unity_wrt F by FVSUM_1:8;then
      A108: F.(0.K,e)=e by BINOP_1:11;
      A109: {} in Fin X by FINSUB_1:18;
      IFIN(idseq n,{},k1,0.K)=0.K by Def1;
     hence G0.({}) = e by A107,A108,A17,A109;
    end;
  A110: for x being Element of X holds G0.({x}) = f.x
   proof let x be Element of X;
     now per cases;
     case A111: x=idseq n;
       A112: f2.x=F2(x) by A19
          .=k1 by A111,CQC_LANG:def 1;
       idseq n in {x} by A111,TARSKI:def 1;then
       IFIN(idseq n,{x},k1,0.K)=k1 by Def1;
      hence G0.({x})=f2.x by A112,A17;
     end;
     case A113: x <> idseq n;
       A114: f2.x=F2(x) by A19
          .=0.K by A113,CQC_LANG:def 1;
       not idseq n in {x} by A113,TARSKI:def 1;then
       IFIN(idseq n,{x},k1,0.K)=0.K by Def1;
      hence G0.({x})=f2.x by A114,A17;
     end;
     end;
    hence G0.({x}) = f.x by A103;
   end;
   for B' being Element of Fin X st B' c= B & B' <> {}
      for x being Element of X st x in B\B' holds G0.(B' \/ {x})
                    = F.(G0.B',f.x)
   proof let B' be Element of Fin X;
     assume B' c= B & B' <> {};
     thus for x being Element of X st x in B\B' holds G0.(B' \/ {x})
                    = F.(G0.B',f.x)
     proof let x be Element of X;
       assume A115: x in B\B';
        A116: now assume A117: idseq n in B' \/ {x};
         thus G0.(B' \/ {x})=IFIN(idseq n,B' \/ {x},k1,0.K) by A17
                            .=k1 by A117,Def1;
        end;
        now assume A118: not idseq n in B' \/ {x};
         thus G0.(B' \/ {x})=IFIN(idseq n,B' \/ {x},k1,0.K) by A17
                            .=0.K by A118,Def1;
         end;then
        A119: (idseq n in B' \/ {x} implies G0.(B' \/ {x})=k1)&
          (not idseq n in B' \/ {x} implies G0.(B' \/ {x})=0.K) by A116;
        A120: now assume A121: idseq n in B';
         thus G0.(B')=IFIN(idseq n,B',k1,0.K) by A17
                            .=k1 by A121,Def1;
        end;
        now assume A122: not idseq n in B';
         thus G0.(B')=IFIN(idseq n,B',k1,0.K) by A17
                            .=0.K by A122,Def1;
        end;then
        A123: (idseq n in B' implies G0.B'=k1)&
            (not idseq n in B' implies G0.B'=0.K) by A120;
        A124: f.x=f2.x by A103;
        A125: 0.K is_a_unity_wrt F by FVSUM_1:8;
        A126: now assume idseq n in B' \/ {x};then
          idseq n in B' or idseq n in {x} by XBOOLE_0:def 2;then
          A127: idseq n in B' or idseq n =x by TARSKI:def 1;
          now per cases by A127;
          case A128: idseq n in B';then
           A129: G0.B'=k1 by A123;
           A130: not x in B' by A115,XBOOLE_0:def 4;
           A131: x <> idseq n by A128,A130;
           f.x=F2(x) by A19,A124
              .=0.K by A131,CQC_LANG:def 1;
           hence F.(G0.B',f.x)=F.(k1,0.K) by A129 .=k1 by A125,BINOP_1:11;
          end;
          case A132: idseq n=x;
            f.x=F2(x) by A19,A124
              .=k1 by A132,CQC_LANG:def 1;
           hence F.(G0.B',f.x)=F.(0.K,k1) by A123,A132,A115,XBOOLE_0:def 4
                             .=k1 by A125,BINOP_1:11;
          end;
          end;
         hence F.(G0.B',f.x)=k1;
        end;
        now assume A133: not idseq n in B' \/ {x};then
          A134: G0.B'=0.K by A123,XBOOLE_0:def 2;
          not idseq n in {x} by A133,XBOOLE_0:def 2;then
          A135: not idseq n=x by TARSKI:def 1;
          f.x=F2(x) by A19,A124
              .=0.K by A135,CQC_LANG:def 1;
         hence F.(G0.B',f.x)=F.(0.K,0.K) by A134 .=0.K by A125,BINOP_1:11;
        end;
       hence G0.(B' \/ {x}) = F.(G0.B',f.x) by A119,A126;
     end;
   end;then
  (the add of K) $$ (FinOmega(Permutations(n)),Path_product(A))=
  (the mult of K)$$ (diagonal_of_Matrix A)
                                       by SETWISEO:def 3,A20,A105,A106,A110;
 hence thesis by MATRIX_3:def 9;
end;

theorem Th18: for n being Nat, p being Element of Permutations(n) holds
 p" is Element of Permutations(n)
proof let n be Nat, p be Element of Permutations(n);
  p" is Element of Group_of_Perm(n) by MATRIX_2:26;
 hence p" is Element of Permutations(n) by MATRIX_2:def 13;
end;

definition let n;let p be Element of Permutations(n);
 redefine func p" -> Element of Permutations(n);
 coherence by Th18;
end;

theorem Th19: for n being Nat,K being Field,A being Matrix of n,K holds
 A@ is Matrix of n,K
proof
 let n be Nat,K be Field,A be Matrix of n,K;
  A1: len (A@)=width A by MATRIX_1:def 7;
  A2: len A = n & for p being FinSequence of K
   st p in rng A holds len p = n by MATRIX_1:def 3;
  A3: now per cases;
  case n>0;
   hence len (A@)=n by A1,MATRIX_1:20,A2;
  end;
  case A4: n<=0;then
   len (A@)=0 by A1,A2,MATRIX_1:def 4;
   hence len (A@)=n by A4;
  end;
  end;
  for p being FinSequence of K st p in rng (A@) holds len p = n
  proof let p be FinSequence of K;
   assume A5: p in rng (A@);
    consider n2 being Nat such that
    A6: for x being set st x in rng (A@)
     ex s being FinSequence st s=x & len s = n2 by MATRIX_1:def 1;
    A7: for p2 being FinSequence of K
       st p2 in rng (A@) holds len p2 = n2
    proof let p2 be FinSequence of K;
     assume p2 in rng (A@);then
      consider s being FinSequence such that
      A8: s=p2 & len s = n2 by A6;
     thus len p2 = n2 by A8;
    end;
    consider s being FinSequence such that
    A9: s=p & len s = n2 by A5,A6;
    A10: len p=n2 by A9;
    now per cases;
    case A11: n>0;then
      A12: width A=n by MATRIX_1:20,A2;then
      A13: len (A@)=width A & width (A@)=len A by A11,MATRIX_2:12;
      A14: len (A@)=n & width (A@)=n by A2,A13,A12;then
      A@ is Matrix of n,n2,K by A7,MATRIX_1:def 3;
     hence len p = n by A10,A14,A11,MATRIX_1:20;
   end;
   case n<=0;then
     A15: width A=0 by MATRIX_1:def 4,A2;
     len (A@) = width A by MATRIX_1:def 7;then
     A16: dom (A@)={} by A15,FINSEQ_1:4,def 3;
      consider x0 being set such that
     A17: x0 in dom (A@) & p=(A@).x0 by A5,FUNCT_1:def 5;
    thus len p = n by A16,A17;
    end;
    end;
   hence len p = n;
  end;
  hence A@ is Matrix of n,K by MATRIX_1:def 3,A3;
end;

definition let n be Nat,K be Field;
  let A be Matrix of n,K;
  func A@ -> Matrix of n,K equals
    (A qua Matrix of K)@;
  coherence by Th19;
end;

theorem for G being Group,
f1,f2 being FinSequence of G
holds (Product (f1^f2))" = (Product f2)" * (Product f1)"
proof let G be Group,f1,f2 be FinSequence of G;
 thus (Product (f1^f2))" = ((Product f1) * (Product f2))" by GROUP_4:8
                        .= (Product f2)" * (Product f1)" by GROUP_1:25;
end;

definition let G be Group, f be FinSequence of G;
  func f" -> FinSequence of G means :Def4:
    len it = len f & for i being Nat st i in dom f holds
      it/.i = (f/.i)";
existence
proof
   deffunc F(Nat) = (f/.$1)";
   ex p being FinSequence st len p = len f &
    for k being Nat st k in Seg len f holds p.k=F(k) from FINSEQ_1:sch 2;then
   consider p being FinSequence such that
A1: len p = len f & for k being Nat st k in Seg len f holds p.k=F(k);
A2: dom p = dom f by A1,FINSEQ_3:31;
    rng p c= the carrier of G
     proof let y be set;assume y in rng p;then
       consider x being set such that
       A3: x in dom p & y=p.x by FUNCT_1:def 5;
       A4: x in Seg len p by A3,FINSEQ_1:def 3;then
       reconsider k=x as Nat;
       p.k=(f/.k)" by A1,A4;
      hence y in the carrier of G by A3;
     end;then
    reconsider q=p as FinSequence of G by FINSEQ_1:def 4;
    for i being Nat st i in dom f holds q/.i=(f/.i)"
    proof let i be Nat;
      assume A5: i in dom f;then
A6:   i in dom p by A2;
A7:   i in Seg len f by A5,FINSEQ_1:def 3;
     thus q/.i=p.i by A6,FINSEQ_4:def 4 .=(f/.i)" by A7,A1;
    end;
  hence thesis by A1;
end;
uniqueness
 proof
  thus for g1,g2 being FinSequence of G st
(len g1=len f & for i being Nat st i in dom f holds
  g1/.i=(f/.i)")&
(len g2=len f & for j being Nat st j in dom f holds
  g2/.j=(f/.j)")
   holds g1=g2
  proof let g1,g2 be FinSequence of G;
   assume A8: (len g1=len f &
      for i being Nat st i in dom f holds g1/.i=(f/.i)")&
     (len g2=len f & for j being Nat st j in dom f holds g2/.j=(f/.j)");
     A9: len g1=len g2 by A8;
A10:  dom g1 = dom f by A8,FINSEQ_3:31;
     for k being Nat st 1<= k & k<= len g1 holds g1.k=g2.k
     proof let k be Nat;
       assume A11: 1<= k & k<= len g1;then
       k in Seg len g1;then
A12:    k in dom g1 by FINSEQ_1:def 3;
       k in Seg len g2 by A11,A9;then
A13:    k in dom g2 by FINSEQ_1:def 3;
A14:    g1.k=g1/.k by FINSEQ_4:def 4,A12;
A15:    g2.k=g2/.k by FINSEQ_4:def 4,A13;
       g1/.k=(f/.k)" by A12,A8,A10;
      hence g1.k=g2.k by A14,A15,A12,A8,A10;
     end;
   hence g1=g2 by A9,FINSEQ_1:18;
  end;
 end;
end;

theorem Th21: for G being Group holds
(<*>(the carrier of G))"=<*>(the carrier of G)
proof let G be Group;
  len (<*>(the carrier of G))=0 by FINSEQ_1:32;then
  len ((<*>(the carrier of G))" qua FinSequence of G)=0 by Def4;
 hence (<*>(the carrier of G))"=<*>(the carrier of G) by FINSEQ_1:32;
end;

theorem Th22: for G being Group, f,g being FinSequence of G
 holds (f^g)"=(f")^(g")
proof let G be Group, f,g be FinSequence of G;
  A1: len ((f^g)")=len (f^g) by Def4;
  A2: len ((f^g)")=len (f^g) by Def4.= len f+len g by FINSEQ_1:35;
  A3: len f+len g=len (f") +len g by Def4
             .=len (f") + len (g") by Def4
             .= len ((f")^(g")) by FINSEQ_1:35;
  for i being Nat st 1<=i & i<= len ((f^g)") holds
    ((f^g)").i=((f")^(g")).i
  proof
   let i be Nat;
    assume A4: 1<=i & i<= len ((f^g)");
     now per cases;
     case len f>0;
A5:  len (f")=len f by Def4;
A6:  len (g")=len g by Def4;
     len ((f^g)")=len (f^g) by Def4; then
A7:  dom ((f^g)")=dom (f^g) by FINSEQ_3:31;
     i in Seg len ((f^g)") by A4;then
A8: i in dom ((f^g)") by FINSEQ_1:def 3;then
A9: (((f^g)")).i=(((f^g)"))/.i by FINSEQ_4:def 4
                .= ((f^g)/.i)" by Def4,A8,A7;
      now per cases;
      case i<=len f;then
        A10: i in Seg len f by A4;then
        A11: i in dom f by FINSEQ_1:def 3;
        A12: i in dom (f") by FINSEQ_1:def 3,A10,A5;then
        A13: ((f")^(g")).i=(f").i by FINSEQ_1:def 7;
        (f^g)/.i=(f^g).i by FINSEQ_4:def 4,A7,A8
               .=f.i by FINSEQ_1:def 7,A11
               .=f/.i by FINSEQ_4:def 4,A11;then
        ((f^g)/.i)" =(f")/.i by Def4,A11
                   .=(f").i by FINSEQ_4:def 4,A12;
       hence (((f^g)")).i=((f")^(g")).i by A13,A9;
      end;
      case A14: i>len f;
       A15: i-'len f=i-len f by A14,BINARITH:50;
        1+len f<=i by A14,NAT_1:38;then
        1+len f -len f<=i-len f by XREAL_1:11;then
  A16:  1<=i-len f;
        i-len f <= len g+len f -len f by XREAL_1:11,A4,A2;then
        A17: i-'len f in Seg len g by A15,A16;then
        A18: i-'len f in dom g by FINSEQ_1:def 3;
        A19: i-'len f in dom (g") by A17,A6,FINSEQ_1:def 3;
        A20: ((f")^(g")).i=(g").(i-len (f"))
                           by FINSEQ_1:37,A4,A14,A5,A2,A3;
        (f^g)/.i=(f^g).i by FINSEQ_4:def 4,A7,A8
               .=g.(i-len (f)) by FINSEQ_1:37,A4,A14,A1
               .=g/.(i-'len f) by FINSEQ_4:def 4,A15,A18;then
        ((f^g)/.i)" =(g")/.(i-'len f) by Def4,A18
                   .=(g").(i-len (f")) by A15,A5,FINSEQ_4:def 4,A19;
       hence (((f^g)")).i=((f")^(g")).i by A20,A9;
      end;
      end;
      hence (((f^g)")).i=((f")^(g")).i;
      end;
      case A21: len f<=0;
        len f=0 by A21;then
        A22: f={} by FINSEQ_1:25;then
        A23: ((f^g)")=g" by FINSEQ_1:47;
        f"=<*>(the carrier of G) by Th21,A22;
       hence (((f^g)")).i=((f")^(g")).i by A23,FINSEQ_1:47;
      end;
      end;
    hence (((f^g)")).i=((f")^(g")).i;
  end;
 hence (f^g)"=(f")^(g") by A2,A3,FINSEQ_1:18;
end;

theorem Th23: for G being Group,a being Element of G
holds (<*a*>)" =<* a" *>
proof let G be Group,a be Element of G;
  A1: len ((<*a*>)")=len (<* a *>) by Def4 .=1 by FINSEQ_1:56
                   .=len (<* a" *>) by FINSEQ_1:56;
  for i being Nat st 1<=i & i<= len <*a" *> holds
    ((<*a*>)").i=(<* a" *>).i
   proof let i be Nat;
    assume A2: 1<=i & i<= len <*a" *>;
     A3: len <*a" *>=1 by FINSEQ_1:56;then
     A4: i=1 by A2,XXREAL_0:1;then
     A5: (<* a" *>).i=a" by FINSEQ_1:57;
     len (<* a *>) =1 by FINSEQ_1:56;then
     i in Seg len (<*a*>) by A2,A3;then
     A6: i in dom (<*a*>) by FINSEQ_1:def 3;
     i in Seg len ((<*a*>)") by A3,A4,A1;then
     i in dom ((<*a*>)") by FINSEQ_1:def 3;then
     A7: ((<*a*>)").i=((<*a*>)")/.i by FINSEQ_4:def 4
                .= ((<* a *>)/.i)" by Def4,A6;
     (<* a *>)/.i= ((<* a *>).i) by FINSEQ_4:def 4,A6
                .= a by A4,FINSEQ_1:57;
    hence ((<*a*>)").i=(<* a" *>).i by A7,A5;
   end;
 hence (<*a*>)" =<* a" *> by A1,FINSEQ_1:18;
end;

theorem Th24: for G being Group,
f being FinSequence of G
holds Product (f^(Rev f)")=1.G
proof let G be Group,f be FinSequence of G;
  defpred P[Nat] means
    for g being FinSequence of G st
    len g <=$1 holds Product (g^(Rev g)")=1.G;
  for g being FinSequence of G st
    len g <= 0 holds Product (g^(Rev g)")=1.G
  proof let g be FinSequence of G;
   assume A1: len g <= 0;
    len g=0 by A1;then
A2: g=<*>(the carrier of G) by FINSEQ_1:32;then
    Rev g=<*>(the carrier of G) by FINSEQ_5:82;then
    A3: (Rev g)" = <*>(the carrier of G) by Th21;
     g^(Rev g)"=<*>(the carrier of G) by FINSEQ_1:47,A2,A3;
   hence Product (g^(Rev g)")=1.G by GROUP_4:11;
  end;then
  A4: P[0];
  A5: for k being Nat st P[k] holds P[k+1]
  proof let k be Nat;
   assume A6: P[k];
    for g being FinSequence of G st
    len g <= k+1 holds Product (g^(Rev g)")=1.G
    proof let g be FinSequence of G;
     assume len g <= k+1;then
      A7: len g<k+1 or len g=k+1 by REAL_1:def 5;
      now per cases by A7;
      case len g<k+1;then len g<=k by NAT_1:38;
       hence Product (g^(Rev g)")=1.G by A6;
      end;
      case A8: len g=k+1;then
        A9: k< len g by NAT_1:38;
        reconsider h=g|k as FinSequence of G;
        A10: len h=k by A9,FINSEQ_1:80;
        A11: g=h^<* g/.(k+1) *> by FINSEQ_5:24,A8;then
        Rev g=<* g/.(k+1) *> ^ (Rev h) by FINSEQ_5:66;then
        (Rev g)"=(<* g/.(k + 1) *>)" ^ (Rev h)" by Th22;then
        A12: g^(Rev g)"=h^ (<* g/.(k+1) *> ^
                           ((<* g/.(k + 1) *>)" ^ (Rev h)"))
                                   by FINSEQ_1:45,A11
                 .=h^ (<* g/.(k+1) *> ^ (<* g/.(k + 1) *>)" ^ (Rev h)")
                                   by FINSEQ_1:45;
        A13: Product (<* g/.(k+1) *> ^ (<* g/.(k + 1) *>)")
        =Product (<* g/.(k+1) *>) * Product ((<* g/.(k + 1) *>)")
                                   by GROUP_4:8
       .=(g/.(k+1))*Product ((<* g/.(k + 1) *>)") by GROUP_4:12
       .=(g/.(k+1))*Product ((<* (g/.(k + 1))" *>)) by Th23
       .=(g/.(k+1))*(g/.(k + 1))" by GROUP_4:12
       .=1.G by GROUP_1:def 6;
       Product (g^(Rev g)")
         =Product (h)
         * Product (<* g/.(k+1) *> ^ (<* g/.(k + 1) *>)" ^ (Rev h)")
                            by A12,GROUP_4:8
        .=Product (h) * (Product (<* g/.(k+1) *> ^ (<* g/.(k + 1) *>)" )*
                            Product ((Rev h)")) by GROUP_4:8
        .=Product (h) * Product ((Rev h)") by A13,GROUP_1:def 5
        .=Product (h^((Rev h)")) by GROUP_4:8;
       hence Product (g^(Rev g)")=1.G by A6,A10;
      end;
      end;
     hence Product (g^(Rev g)")=1.G;
    end;
   hence P[k+1];
  end;
  for j being Nat holds P[j] from NAT_1:sch 1(A4,A5);then
  P[len f];
 hence Product (f^(Rev f)")=1.G;
end;

theorem Th25: for G being Group,f being FinSequence of G
holds Product ((Rev f)" ^f)=1.G
proof let G be Group,f be FinSequence of G;
A1: len f=len Rev f by FINSEQ_5:def 3;
A2: len Rev f = len ((Rev f)") by Def4; then
A3: dom Rev f = dom ((Rev f)") by FINSEQ_3:31;
A4: len ((Rev f)") = len Rev ((Rev f)") by FINSEQ_5:def 3;
A5: len Rev ((Rev f)")=len ((Rev ((Rev f)"))") by Def4; then
A6: dom Rev ((Rev f)")=dom ((Rev ((Rev f)"))") by FINSEQ_3:31;
    for i being Nat st 1<= i & i <= len f holds f.i = ((Rev ((Rev f)") )").i
    proof let i be Nat;
      assume A7: 1<= i & i <= len f;then
A8:  i in Seg len f;then
A9:   i in dom f by FINSEQ_1:def 3;
A10:  i in dom ((Rev ((Rev f)"))") by FINSEQ_1:def 3,A8,A1,A2,A4,A5;
A11:   len f-i=len f-'i by BINARITH:50,A7;then
      len f-i+1=len f-'i+1;then
      reconsider j=len f-i+1 as Nat;
      len f-i+1>=0+1 by XREAL_1:8,A11;then
A12:   j>=1;
      i-1>=0 by A7,XREAL_1:50;then
      len f +0 <=len f+(i-1) by XREAL_1:9;then
       len f-(i-1)<=len f+(i-1)-(i-1) by XREAL_1:15;then
       j in Seg len f by A12;then
A13:    j in dom ((Rev f)") by FINSEQ_1:def 3,A1,A2; then
A14:    j in dom Rev f by A3;
A15:    i+j=len f+1;
A16:   j+i=len f+1;
       reconsider g=(Rev f)" as FinSequence of G;
       f.i=f/.i by FINSEQ_4:def 4,A9 .= (Rev f)/.j by A9,A15,FINSEQ_5:69
         .= ((Rev f)/.j)"" by GROUP_1:19
         .= (((Rev f)")/.j)" by Def4,A14
         .= ((Rev ((Rev f)"))/.i)" by FINSEQ_5:69,A16,A13,A1,A2
         .= ((Rev ((Rev f)"))")/.i by Def4,A6,A10
         .=((Rev ((Rev f)"))").i by FINSEQ_4:def 4,A10;
      hence f.i = ((Rev ((Rev f)"))").i;
    end;then
   (Rev ((Rev f)") )"=f by A1,A2,A4,A5,FINSEQ_1:18;
 hence Product ((Rev f)" ^f)=1.G by Th24;
end;

theorem Th26: for G being Group,f being FinSequence of G
holds (Product f)" = Product ((Rev f)")
proof let G be Group,f be FinSequence of G;
   A1: for g,h being Element of G st
   h * g = 1.G & g * h = 1.G holds g=h" by GROUP_1:12;
   A2: Product((Rev f)" ^ f)=1.G by Th25;
   Product(f ^ (Rev f)")=1.G by Th24;then
   (Product f) * Product ((Rev f)") = 1.G &
          Product ((Rev f)") * (Product f) = 1.G by GROUP_4:8,A2;
 hence (Product f)" = Product ((Rev f)") by A1;
end;

theorem Th27: for ITP being Element of Permutations(n),
ITG being Element of Group_of_Perm(n) st ITG=ITP & n>=1
holds ITP"=ITG"
proof let ITP be Element of Permutations(n),
ITG be Element of Group_of_Perm(n);
 assume A1: ITG=ITP & n>=1;
  A2: ITP is Permutation of Seg n by MATRIX_2:def 11;then
  reconsider qf=ITP as Function of Seg n,Seg n;
  1 in Seg n by A1;then
  A3: dom qf=Seg n by FUNCT_2:def 1;
  A4: the carrier of Group_of_Perm(n)=Permutations(n) by MATRIX_2:def 13;
  A5: rng qf = Seg n by FUNCT_2:def 3,A2;
  A6: ITP"*ITP=id Seg n & ITP*ITP"=id Seg n by A3,A5,FUNCT_1:61;
  reconsider pf=ITP" as Element of Group_of_Perm n by A4;
  A7: (the mult of (Group_of_Perm n)).(ITG,pf)=(ITP")*ITP
                                    by A1,MATRIX_2:def 13;
  A8: (the mult of (Group_of_Perm n)).(pf,ITG)=ITP*(ITP")
                                    by A1,MATRIX_2:def 13;
  A9: idseq n=1.(Group_of_Perm(n)) by MATRIX_2:28;
  A10: ITG*pf=(the mult of (Group_of_Perm(n))).(ITG,pf) by GROUP_1:def 1;
  pf*ITG=(the mult of (Group_of_Perm(n))).(pf,ITG) by GROUP_1:def 1;
 hence ITP"=ITG" by GROUP_1:def 6,A6,A9,A10,A7,A8;
end;

Lm4: for n being Nat,
  IT being Element of Permutations(n) st
  IT is even & n>=1 holds IT" is even
proof let n be Nat,
  IT be Element of Permutations(n);
 assume A1: IT is even & n>=1;
  A2: len Permutations n=n by MATRIX_2:20;
  consider l being FinSequence of Group_of_Perm n such that
A3: (len l)mod 2=0 & IT=Product l &
  for i st i in dom l ex q being Element of Permutations n
   st l.i=q & q is_transposition by MATRIX_2:def 15,A1,A2;
  reconsider lv=(Rev l)" as FinSequence of Group_of_Perm n;
  A4: len l =len Rev l by FINSEQ_5:def 3;then
  A5: len l=len lv by Def4; then
  A6: dom l=dom lv by FINSEQ_3:31;
  set m=len l;
  reconsider ITP=IT as Element of Permutations n;
  reconsider ITG=ITP as Element of Group_of_Perm n by MATRIX_2:def 13;
  A7: ITG"=ITP" by Th27,A1;
  A8: IT"=(Product l)" by A3,A7 .=Product lv by Th26;
  for i st i in dom lv ex q2 being Element of Permutations(n)
       st lv.i=q2 & q2 is_transposition
  proof let i;
    assume A9: i in dom lv;then
     A10: i in Seg len lv by FINSEQ_1:def 3;
     A11: 1<=i & i<=m by A5,A10,FINSEQ_1:3;then
     A12: m-i>=0 by XREAL_1:50;then
     A13: m-i+1>=0+1 by XREAL_1:9;
     m+1<=m+i by XREAL_1:9,A11;then
     m+1-i<=m+i-i by XREAL_1:11;then
     A14: m-i+1<=m;
     m-i+1=m-'i+1 by A12,BINARITH:def 3;then
     reconsider j=len l-i+1 as Nat;
     A15: i+j=len l+1;
     j in Seg len l by A13,A14;then
     A16: j in dom l by FINSEQ_1:def 3;
     consider q being Element of Permutations n such that
     A17: l.j=q & q is_transposition by A3,A16;
     lv.i=lv/.i by FINSEQ_4:def 4,A9;then
     reconsider qq=lv.i as Element of Permutations(n) by MATRIX_2:def 13;
     consider i1,j1 being Nat such that
     A18:  i1 in dom q & j1 in dom q & i1<>j1 & q.i1=j1 & q.j1=i1 &
      for k being Nat st k <>i1 & k<>j1 & k in dom q holds q.k=k
                               by A17,MATRIX_2:def 14;
      reconsider qf=q as Function of Seg n,Seg n by MATRIX_2:def 11;
A19:   Seg n <> {} by FINSEQ_1:3,A1;then
A20:  dom qf=Seg n by FUNCT_2:def 1;
      reconsider qqf=qq as Function of Seg n,Seg n by MATRIX_2:def 11;
A21:  dom qqf=Seg n by FUNCT_2:def 1,A19;
A22:  j1 in dom qq & i1 in dom qq by A18,A20,A21;
A23:  l.j=l/.j by FINSEQ_4:def 4,A16;
A24:  q=l/.j by A17,A23;
A25:   i in dom Rev l by A9,A6,A4,FINSEQ_3:31;
A26:  qq=(Rev l)"/.i by A9,FINSEQ_4:def 4
                    .=((Rev l)/.i)" by Def4,A25
                    .=(l/.j)" by FINSEQ_5:69,A15,A16
                    .=q" by A24,Th27,A1;
      A27: for k being Nat st k <>j1 & k<>i1 & k in dom qq holds qq.k=k
      proof let k be Nat;
        assume A28: k <>j1 & k<>i1 & k in dom qq;then
        q.k=k by A21,A20,A18;
       hence qq.k=k by A26,A28,A20,A21,FUNCT_1:54;
      end;
      A29: l.j=l/.j by FINSEQ_4:def 4,A16;
      A30: q=l/.j by A17,A29;
      A31: qq=(Rev l)"/.i by A9,FINSEQ_4:def 4
            .=((Rev l)/.i)" by Def4,A25
            .=(l/.j)" by FINSEQ_5:69,A15,A16
            .=q" by A30,Th27,A1;
A32: qq.j1=i1 by A18,FUNCT_1:54,A31;
     qq.i1=j1 by A18,FUNCT_1:54,A31;then
     qq is_transposition by MATRIX_2:def 14,A22,A18,A27,A32;
    hence ex q2 being Element of Permutations n
       st lv.i=q2 & q2 is_transposition;
  end;
 hence IT" is even by MATRIX_2:def 15,A2,A3,A5,A8;
end;

Lm5: for n being Nat, IT being Permutation of Seg n holds
  (IT")"= IT by FUNCT_1:65;

theorem Th28: for n being Nat,
  IT being Element of Permutations(n) st n>=1 holds
  IT is even iff IT" is even
proof let n be Nat,
  IT be Element of Permutations(n);
  assume A1: n>=1;
 hence IT is even implies IT" is even by Lm4;
  now assume IT" is even;then
    IT"" is even by Lm4,A1;
   hence IT is even by Lm5;
  end;
 hence thesis;
end;

theorem Th29: for n being Nat,K being Field,
  p being Element of Permutations(n), x being Element of K
 st n>=1 holds -(x,p) = -(x,p")
proof let n be Nat,K be Field,
  p be Element of Permutations(n), x be Element of K;
  assume A1: n>=1;
  A2: len ((Permutations(n)))=n by MATRIX_2:20;
  reconsider pf=p as Permutation of Seg n by MATRIX_2:def 11;
  per cases;
  suppose A3: p is even;then
A4: -(x,p) = x by MATRIX_2:def 16;
    pf" is even by A3,Th28,A1,A2;
   hence -(x,p) = -(x,p") by A4,MATRIX_2:def 16,A2;
  end;
  suppose A5: not p is even;then
A6: -(x,p) = -x by MATRIX_2:def 16;
    not p" is even by A5,Th28,A1;
   hence -(x,p) = -(x,p") by A6,MATRIX_2:def 16;
  end;
end;

theorem Th30:for K being Field,f1,f2 being FinSequence of K holds
  (the mult of K) $$ (f1^f2) = ((the mult of K)$$f1)*((the mult of K)$$f2)
 proof let K be Field,f1,f2 be FinSequence of K;
  A1: (the mult of K) is associative by GROUP_1:31;
  A2: (the mult of K) is associative &
    ((the mult of K) has_a_unity or len f1 >= 1 & len f2 >= 1)
                                   by A1,GROUP_1:34;
  thus (the mult of K) $$ (f1^f2)
    = (the mult of K).((the mult of K) $$ f1,(the mult of K) $$ f2)
                                   by FINSOP_1:6,A2
    .=(the mult of K) $$ f1 * (the mult of K) $$ f2 by GROUP_1:def 1;
 end;

theorem
  Th31:for K being Field,R1,R2 be FinSequence of K
         st R1,R2 are_fiberwise_equipotent holds
      (the mult of K)$$ R1 = (the mult of K)$$ R2
proof :: The proof is copying and string relacing of EULER_2:25
  let K be Field;
  defpred P[Nat] means
    for f,g be FinSequence of K
      st f,g are_fiberwise_equipotent & len f = $1
      holds (the mult of K)$$ f = (the mult of K)$$ g;
  A1:P[0]
  proof let f,g be FinSequence of K; assume
       f,g are_fiberwise_equipotent & len f = 0;
    then len g = 0 & f = <*>(the carrier of K) by FINSEQ_1:32,RFINSEQ:16;
    hence thesis by FINSEQ_1:32;
  end;
  A2:for n st P[n] holds P[n+1]
  proof let n; assume
A3: P[n];
    let f,g be FinSequence of K; assume
    A4:f,g are_fiberwise_equipotent & len f = n+1;
    then A5:rng f = rng g & len f = len g by RFINSEQ:1,16;
    0+1<=n+1 by NAT_1:38;
    then A6:n+1 in dom f by A4,FINSEQ_3:27;then
    A7: f.(n+1) in rng f by FUNCT_1:def 5;
    rng f c= the carrier of K by FINSEQ_1:def 4;then
    reconsider a = f.(n+1) as Element of K by A7;
    a in rng g by A5,A6,FUNCT_1:def 5;
    then consider m such that
    A8:m in dom g & g.m = a by FINSEQ_2:11;
    A9:g = (g|m)^(g/^m) by RFINSEQ:21;
    A10:1<=m & m<=len g by A8,FINSEQ_3:27;
    then max(0,m-1) = m-1 by FINSEQ_2:4;
    then reconsider m1 = m-1 as Nat by FINSEQ_2:5;
    set gg = g/^m, gm = g|m;
A11:len gm = m by A10,FINSEQ_1:80;
A12:m = m1+1;
    m in Seg m by A10;
    then gm.m = a & m in dom g by A8,RFINSEQ:19;
    then A13:gm = (gm|m1)^<*a*> by A11,A12,RFINSEQ:20;
    m1<=m by A12,NAT_1:29;
    then A14:Seg m1 c= Seg m by FINSEQ_1:7;
A15:gm|m1 = g|((Seg m)/\(Seg m1)) by RELAT_1:100
             .= g|(Seg m1) by A14,XBOOLE_1:28
             .= g|m1;
    set fn = f|n;
    n<=n+1 by NAT_1:29;
    then A16:len fn = n by A4,FINSEQ_1:80;
A17:f = fn ^ <*a*> by A4,RFINSEQ:20;
      now let x be set;
         card(f"{x}) = card(g"{x}) by A4,RFINSEQ:def 1;
      then card(fn"{x})+card(<*a*>"{x}) = card(((g|m1)^<*a*>^(g/^m))"{x})
                                           by A9,A13,A15,A17,FINSEQ_3:63
      .= card(((g|m1)^<*a*>)"{x})+card((g/^m)"{x}) by FINSEQ_3:63
      .= card((g|m1)"{x})+card(<*a*>"{x})+card((g/^m)"{x}) by FINSEQ_3:63
      .= card((g|m1)"{x})+card((g/^m)"{x})+card(<*a*>"{x})
      .= card(((g|m1)^(g/^m))"{x})+card(<*a*>"{x}) by FINSEQ_3:63;
      hence card(fn"{x}) = card(((g|m1)^(g/^m))"{x});
    end;
    then fn, (g|m1)^(g/^m) are_fiberwise_equipotent by RFINSEQ:def 1;
    then (the mult of K)$$ fn = (the mult of K)$$((g|m1)^gg) by A3,A16;
    hence (the mult of K)$$ f
     = (the mult of K)$$((g|m1)^gg)*(the mult of K)$$ <*a*> by A17,Th30
    .= (the mult of K)$$(g|m1)*(the mult of K)$$ gg*(the mult of K)$$ <*a*>
                                        by Th30
    .= (the mult of K)$$(g|m1)*(the mult of K)$$ <*a*>*(the mult of K)$$ gg
                                         by GROUP_1:def 4
             .= (the mult of K)$$ gm*(the mult of K)$$ gg by A13,A15,Th30
             .= (the mult of K)$$ g by A9,Th30;
  end;
  A18:for n holds P[n] from NAT_1:sch 1(A1,A2);
  let R1,R2 be FinSequence of K; assume
  A19:R1,R2 are_fiberwise_equipotent;
  len R1 = len R1;
  hence thesis by A18,A19;
end;

theorem Th32: for n being Nat, K being Field,
p being Element of Permutations(n),
 f,g being FinSequence of K st n>=1 & len f=n & g=f*p holds
 f,g are_fiberwise_equipotent
proof let n be Nat, K be Field,
 p be Element of Permutations(n),
 f,g be FinSequence of K;
 assume A1: n>=1 & len f=n & g=f*p;then
  f <> {} by FINSEQ_1:25;then
  dom f <> {} by FINSEQ_1:26;then
  A2: Seg n <> {} by A1,FINSEQ_1:def 3;
  A3: p is Permutation of Seg n by MATRIX_2:def 11;then
  reconsider fp=p as Function of Seg n,Seg n;
  A4: fp is onto by A3;then
  rng fp = Seg n by FUNCT_2:def 3;then
  rng p c= dom f by A1,FINSEQ_1:def 3;then
  A5: dom (f*p)=dom fp by RELAT_1:46;
  dom fp =Seg n by A2,FUNCT_2:def 1;then
  reconsider h=f*p as FinSequence by A5,FINSEQ_1:def 2;
  A6: rng p=Seg n by A4,FUNCT_2:def 3;
  dom f=Seg len f by FINSEQ_1:def 3 .=Seg n by A1;
 hence f,g are_fiberwise_equipotent by RFINSEQ:3,A6,A1,A5;
end;

theorem Th33:
  for n being Nat, K being Field,
      p being Element of Permutations(n),
 f,g being FinSequence of K st n>=1 & len f=n & g=f*p holds
 (the mult of K) $$ f = (the mult of K) $$ g
proof let n be Nat, K be Field,
 p be Element of Permutations(n), f,g be FinSequence of K;
 assume n>=1 & len f=n & g=f*p;then
 f,g are_fiberwise_equipotent by Th32;
 hence (the mult of K) $$ f = (the mult of K) $$ g by Th31;
end;

theorem Th34: for n being Nat, K being Field,
p being Element of Permutations(n),
 f being FinSequence of K st n>=1 & len f=n holds
 f*p is FinSequence of K
proof let n be Nat, K be Field,
p be Element of Permutations(n), f be FinSequence of K;
 assume A1: n>=1 & len f=n;then
  f <> {} by FINSEQ_1:25;then
  dom f <> {} by FINSEQ_1:26;then
  A2: Seg n <> {} by A1,FINSEQ_1:def 3;
  A3: p is bijective Function of Seg n,Seg n by MATRIX_2:def 11;then
  reconsider q=p as Function of Seg n,Seg n;
  rng q = Seg n by FUNCT_2:def 3,A3;then
  rng p c= dom f by A1,FINSEQ_1:def 3;then
  dom (f*p)=dom q by RELAT_1:46 .=Seg n by A2,FUNCT_2:def 1;then
  reconsider h=f*p as FinSequence by FINSEQ_1:def 2;
  A4: rng h c= rng f by RELAT_1:45;
  rng f c= the carrier of K by FINSEQ_1:def 4;then
  rng h c= the carrier of K by A4,XBOOLE_1:1;
 hence f*p is FinSequence of K by FINSEQ_1:def 4;
end;

theorem Th35: for n being Nat, K being Field,
p being Element of Permutations(n),
 A being Matrix of n,K st n>=1 holds
 Path_matrix(p",A@) = (Path_matrix(p,A))*p"
proof let n be Nat, K be Field,p be Element of Permutations(n),
 A be Matrix of n,K;
 assume A1: n>=1;
  set f=(Path_matrix(p,A));
  A2: len (Path_matrix(p,A))=n by MATRIX_3:def 7;then
  f <> {} by FINSEQ_1:25,A1;then
  dom f <> {} by FINSEQ_1:26;then
  A3: Seg n <> {} by A2,FINSEQ_1:def 3;
  A4: p" is Permutation of Seg n by MATRIX_2:def 11;then
  reconsider fp=p" as Function of Seg n,Seg n;
  A5: p is Permutation of Seg n by MATRIX_2:def 11;
  reconsider fp0=p as Function of Seg n,Seg n by A5;
  A6: rng fp = Seg n by FUNCT_2:def 3,A4;then
  rng (p") c= dom f by A2,FINSEQ_1:def 3;then
  A7: dom (f*p")=dom fp by RELAT_1:46;
  A8: dom fp =Seg n by A3,FUNCT_2:def 1;
  reconsider m=(Path_matrix(p,A))*p" as FinSequence of K by Th34,A1,A2;
  A9: Seg len m=Seg n by A8,A7,FINSEQ_1:def 3;then
  A10: len m=n by FINSEQ_1:8;
  for i,j st i in dom (m) & j=(p").i holds m.i=(A@)*(i,j)
  proof let i,j;
   assume A11: i in dom (m) & j=(p").i;then
    A12: i in Seg n by A9,FINSEQ_1:def 3;then
    A13: i in dom (p") by A8;then
    A14: ((Path_matrix(p,A))*p").i=(Path_matrix(p,A)).((p").i) by FUNCT_1:23;
    A15: len f=n & for i2,j2 being Nat st i2 in dom f & j2=p.i2
                    holds f.i2=A*(i2,j2) by MATRIX_3:def 7;
    A16: j in Seg n by A6,A11,A13,FUNCT_1:def 5;then
    A17: j in dom f by FINSEQ_1:def 3,A2;
    A18: dom (p") = rng p &
       for y,x being set holds y in rng p & x = (p").y
                       iff x in dom p & y = p.x by FUNCT_1:54,A11;
    rng fp0 = Seg n by FUNCT_2:def 3,A5;then
    i=p.j by A11,A18,A12;then
    A19: (Path_matrix(p,A)).j=A*(j,i) by A15,A17;
    dom A=Seg len A by FINSEQ_1:def 3 .= Seg n by MATRIX_1:def 3;then
    A20: j in dom A by A16;
    A21: n>0 by A1;
    A22: len A=n by MATRIX_1:def 3;
    i in Seg width A by MATRIX_1:20,A21,A22,A12;then
    [j,i] in Indices A by A20,ZFMISC_1:def 2;
   hence m.i=(A@)*(i,j) by A11,A14,A19,MATRIX_1:def 7;
  end;
 hence Path_matrix(p",A@) = (Path_matrix(p,A))*p" by MATRIX_3:def 7,A10;
end;

theorem Th36: for n being Nat, K being Field,
p being Element of Permutations(n),
 A being Matrix of n,K st n>=1 holds
 (Path_product(A@)).(p") = (Path_product(A)).p
proof let n be Nat, K be Field, p be Element of Permutations n,
 A be Matrix of n,K;
 assume A1: n>=1;
A2: len (Path_matrix(p,A))=n by MATRIX_3:def 7;then
  reconsider g=Path_matrix(p,A)*(p") as FinSequence of K by Th34,A1;
  A3: (Path_product(A)).p = -((the mult of K) $$ Path_matrix(p,A),p)
                                 by MATRIX_3:def 8;
  (Path_product(A@)).(p") = -((the mult of K) $$ Path_matrix(p",A@),p")
                                 by MATRIX_3:def 8
                         .= -((the mult of K) $$ g,p") by Th35,A1
                         .= -((the mult of K) $$ g,p) by Th29,A1
                         .= -((the mult of K) $$ (Path_matrix(p,A)),p)
                                 by Th33,A1,A2;
 hence (Path_product(A@)).(p") = (Path_product(A)).p by A3;
end;

theorem for n being Nat, K being Field, A being Matrix of n,K st n>=1
 holds Det (A) = Det (A@)
proof let n be Nat, K be Field, A be Matrix of n,K;
 assume A1: n>=1;
  set f=Path_product(A);
  set f2=Path_product(A@);
  set F=(the add of K);
  set Y=the carrier of K;
  set X=Permutations(n);
  set B=FinOmega(Permutations(n));
  A2: Permutations(n) is finite by MATRIX_2:30;
  A3: (B <> {} or F has_a_unity) by A2,MATRIX_2:def 17;
  A4: Permutations(n) is finite by MATRIX_2:30;then
  A5: B=Permutations(n) by MATRIX_2:def 17;
  set I=(the add of K) $$ (FinOmega(Permutations(n)),Path_product(A@));
 consider G0 being Function of Fin X, Y such that
  A6: I = G0.B &
          (for e being Element of Y st e is_a_unity_wrt F holds G0.{} = e) &
          (for x being Element of X holds G0.{x} = f2.x) &
           for B' being Element of Fin X st B' c= B & B' <> {}
           for x being Element of X st x in B \ B'
            holds G0.(B' \/ {x}) = F.(G0.B',f2.x) by A3,SETWISEO:def 3;
  deffunc F8(set) = G0.{p" where p is Permutation of Seg n : p in $1};
  A7: for x2 being set st x2 in Fin X holds F8(x2) in Y
  proof let x2 be set;
    assume x2 in Fin X;
     {p" where p is Permutation of Seg n : p in x2} c= X
      proof let r be set;
        assume r in {p" where p is Permutation of Seg n : p in x2};then
        consider p being Permutation of Seg n such that
        A8: r=p" & p in x2;
       thus r in X by A8,MATRIX_2:def 11;
      end;then
     {p" where p is Permutation of Seg n : p in x2} is Finite_Subset of X
                                        by FINSUB_1:34,A4;
    hence F8(x2) in Y by FUNCT_2:7;
  end;
  consider G1 being Function of Fin X,Y such that
   A9: for x5 being set st x5 in Fin X holds G1.x5 = F8(x5)
                                   from FUNCT_2:sch 2(A7);
   A10: I=G1.B
    proof
      A11: G1.B =G0.{p" where p is Permutation of Seg n : p in B} by A9;
      A12: {p" where p is Permutation of Seg n : p in B} c= B
       proof let z be set;
         assume z in {p" where p is Permutation of Seg n : p in B};then
          consider p being Permutation of Seg n such that
          A13: z=p" & p in B;
         thus z in B by A5,A13,MATRIX_2:def 11;
       end;
      B c= {p" where p is Permutation of Seg n : p in B}
      proof let z be set; assume z in B;then
        reconsider q2=z as Permutation of Seg n by MATRIX_2:def 11,A5;
        set q3=q2";
        A14: q3"=q2 by FUNCT_1:65;
        q3 in B by MATRIX_2:def 11,A5;
       hence z in {p" where p is Permutation of Seg n : p in B} by A14;
      end;
     hence thesis by A12,XBOOLE_0:def 10,A6,A11;
    end;
   A15: for e being Element of Y st e is_a_unity_wrt F holds G1.{} = e
    proof let e be Element of Y;
      assume A16: e is_a_unity_wrt F;
       {} is Subset of X by SUBSET_1:4;then
       reconsider B3={} as Element of Fin X by FINSUB_1:34,A4;
       {p" where p is Permutation of Seg n : p in B3} c= {}
       proof let s be set;
         assume s in {p" where p is Permutation of Seg n : p in B3};then
         consider p being Permutation of Seg n such that
         A17: s=p" & p in B3;
        thus s in {} by A17;
       end;then
       {p" where p is Permutation of Seg n : p in B3}= {} by XBOOLE_1:3;then
       G0.{p" where p is Permutation of Seg n : p in B3}=e by A6,A16;
      hence G1.{} = e by A9;
    end;
   A18: for x being Element of X holds G1.{x} = f.x
    proof let x be Element of X;
      reconsider B4={x} as Element of Fin X;
      reconsider q=x as Permutation of Seg n by MATRIX_2:def 11;
A19: q" is Element of X by MATRIX_2:def 11;
      {p" where p is Permutation of Seg n : p in B4}={q"}
       proof
     A20: {p" where p is Permutation of Seg n : p in B4} c= {q"}
          proof let z be set;
            assume z in {p" where p is Permutation of Seg n : p in B4};then
            consider p being Permutation of Seg n such that
            A21: z=p" & p in B4;
            p=x by TARSKI:def 1,A21;
           hence z in {q"} by TARSKI:def 1,A21;
          end;
         {q"} c= {p" where p is Permutation of Seg n : p in B4}
          proof let z be set;
            assume A22: z in {q"};
            A23: q in B4 by TARSKI:def 1;
            z=q" by TARSKI:def 1,A22;
           hence z in {p" where p is Permutation of Seg n : p in B4} by A23;
          end;
        hence thesis by A20,XBOOLE_0:def 10;
       end;then
      G1.B4=G0.{q"} by A9 .=f2.(q") by A6,A19
         .=(Path_product A).q by Th36,A1;
     hence G1.{x} = f.x;
    end;
   for B' being Element of Fin X st B' c= B & B' <> {}
           for x being Element of X st x in B \ B'
            holds G1.(B' \/ {x}) = F.(G1.B',f.x)
    proof let B' be Element of Fin X;
     assume A24: B' c= B & B' <> {};
     thus for x being Element of X st x in B \ B'
            holds G1.(B' \/ {x}) = F.(G1.B',f.x)
      proof let x be Element of X;
        assume x in B \ B';then
         A25: x in B & not x in B' by XBOOLE_0:def 4;
         A26: x is Permutation of Seg n by MATRIX_2:def 11;
         A27: G1.(B' \/ {x})=F8(B' \/ {x}) by A9
        .=G0.{p" where p is Permutation of Seg n : p in B' \/ {x}};
        {p" where p is Permutation of Seg n : p in B'} c= X
        proof let v be set;
          assume v in {p" where p is Permutation of Seg n : p in B'};then
          consider p being Permutation of Seg n such that
          A28: p"=v & p in B';
         thus v in X by MATRIX_2:def 11,A28;
        end;then
        reconsider B2={p" where p is Permutation of Seg n : p in B'}
                            as Element of Fin X by FINSUB_1:34,A4;
        reconsider px=x as Permutation of Seg n by A26;
        A29: px" in B by A5,MATRIX_2:def 11;
        B c= X by FINSUB_1:def 5;then
        reconsider y=px" as Element of X by A29;
        A30: B2 \/ {y}
         ={p" where p is Permutation of Seg n : p in B' \/ {x}}
        proof thus B2 \/ {y}
           c={p" where p is Permutation of Seg n : p in B' \/ {x}}
          proof let z be set;assume z in B2 \/ {y};then
            z in B2 or z in {y} by XBOOLE_0:def 2;then
            A31: z in B2 or z=y by TARSKI:def 1;
            now per cases by A31;
            case z in B2;then
              consider p being Permutation of Seg n such that
              A32: z=p" & p in B';
              p in B' \/ {x} by A32,XBOOLE_0:def 2;
             hence z in
             {p2" where p2 is Permutation of Seg n : p2 in B' \/ {x}} by A32;
            end;
            case A33: z=y;
              px in {x} by TARSKI:def 1;then
              px in B' \/ {x} by XBOOLE_0:def 2;
             hence z in {p" where p is Permutation of Seg n : p in B' \/ {x}}
                                 by A33;
            end;
            end;
           hence z in {p" where p is Permutation of Seg n : p in B' \/ {x}};
          end;
         thus {p" where p is Permutation of Seg n : p in B' \/ {x}}
          c= B2 \/ {y}
          proof let z be set;
            assume z in {p" where p is Permutation of Seg n : p in B' \/ {x}};
            then consider p being Permutation of Seg n such that
            A34: z=p" & p in B' \/ {x};
            A35: p in B' or p in {x} by A34,XBOOLE_0:def 2;
            now per cases by A35;
            case p in B';
             hence z in B2 or z in {y} by A34;
            end;
            case p in {x};then
              p=x by TARSKI:def 1;
             hence z in B2 or z in {y} by A34,TARSKI:def 1;
            end;
            end;
           hence z in B2 \/ {y} by XBOOLE_0:def 2;
          end;
        end;
        A36: B2 c= B
        proof let u be set;assume u in B2;then
          consider p being Permutation of Seg n such that
          A37: p"=u & p in B';
         thus u in B by A37,A5,MATRIX_2:def 11;
        end;
        consider d being Element of B';
        A38: d in B' by A24;
        d in B by TARSKI:def 3,A24;then
         reconsider pd=d as Permutation of Seg n by A5,MATRIX_2:def 11;
         pd" in B2 by A38;then
        A39: B2 <>{};
        A40: y in B by A5;
        now assume y in B2;then
          consider pp being Permutation of Seg n such that
          A41: y=pp" & pp in B';
          px=(pp")" by FUNCT_1:65,A41;
         hence contradiction by A25,A41,FUNCT_1:65;
        end;then
        y in B\B2 by A40,XBOOLE_0:def 4;then
        A42: G0.{p" where p is Permutation of Seg n : p in B' \/ {x}}
        = F.(G0.B2,f2.y) by A6,A30,A36,A39;
        G0.B2=G1.B' by A9;
        hence G1.(B' \/ {x}) = F.(G1.B',f.x) by A27,A42,Th36,A1;
      end;
    end;then
  A43: (the add of K) $$ (FinOmega(Permutations n),Path_product A)
  =(the add of K) $$ (FinOmega(Permutations n),Path_product(A@))
                                 by A3,SETWISEO:def 3,A10,A15,A18;
  Det A = (the add of K) $$ (FinOmega(Permutations n),Path_product A)
                                 by MATRIX_3:def 9;
 hence Det A = Det (A@) by A43,MATRIX_3:def 9;
end;
