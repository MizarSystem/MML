:: Homeomorphisms of {J}ordan Curves
::  by Adam Naumowicz and Grzegorz Bancerek
::
:: Received September 15, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies JORDAN24, ARYTM, PRE_TOPC, EUCLID, RELAT_1, ARYTM_1, TOPS_1,
      SUBSET_1, METRIC_1, BOOLE, TOPREAL2, CONNSP_1, TOPS_2, MCART_1, SQUARE_1,
      ARYTM_3, PCOMPS_1, FUNCT_1, JORDAN1, COMPLEX1, ORDINAL2, RELAT_2,
      WEIERSTR, VECTMETR, CONNSP_2, JGRAPH_2, FINSEQ_6, XCMPLX_0, COMPTRIG,
      SIN_COS, SGRAPH1, TOPGRP_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, COMPLEX1, SQUARE_1, NAT_1,
      RELAT_1, FUNCT_1, FUNCT_2, PSCOMP_1, NUMBERS, XCMPLX_0, XREAL_0, REAL_1,
      STRUCT_0, PRE_TOPC, TOPS_1, TOPS_2, COMPTS_1, CONNSP_1, METRIC_1,
      PCOMPS_1, EUCLID, TOPREAL2, JORDAN1, GOBRD14, WEIERSTR, VECTMETR,
      CONNSP_2, JGRAPH_2, COMPLEX2, COMPTRIG, SIN_COS, ARYTM_0, TOPGRP_1,
      TMAP_1;
 constructors COMPTS_1, CONNSP_1, TOPS_1, SQUARE_1, PSCOMP_1, JORDAN1,
      JGRAPH_6, SPPOL_2, GOBRD14, JORDAN19, T_0TOPSP, WEIERSTR, VECTMETR,
      JGRAPH_2, COMPLEX2, COMPTRIG, SIN_COS, ARYTM_0, TOPGRP_1, TOPALG_5,
      TMAP_1;
 registrations XREAL_0, RELSET_1, STRUCT_0, EUCLID, PRE_TOPC, FUNCT_1,
      TOPREAL2, SQUARE_1, MEMBERED, VECTMETR, PCOMPS_1, XCMPLX_0, INT_1,
      TIETZE, TOPGRP_1;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM, REAL;
 definitions TARSKI, XBOOLE_0, CONNSP_1, BORSUK_1, VECTMETR, TOPS_2, SPRECT_1,
      TOPGRP_1;
 theorems PRE_TOPC, EUCLID, SQUARE_1, XREAL_1, JORDAN1K, GOBRD14, JGRAPH_7,
      TOPREAL2, SPPOL_1, SUBSET_1, TOPMETR, TOPS_2, FUNCT_2, RELAT_1, FUNCT_1,
      XBOOLE_1, JORDAN18, STRUCT_0, WEIERSTR, PCOMPS_1, CONNSP_2, GOBOARD6,
      HAUSDORF, METRIC_1, VECTMETR, JGRAPH_2, COMPLEX2, XCMPLX_0, COMPLEX1,
      COMPTRIG, SIN_COS, XCMPLX_1, MSAFREE3, TMAP_1, JORDAN16, RFUNCT_2,
      EXTENS_1, TIETZE, TOPREAL5;
 schemes FUNCT_2;

begin

 reserve p, p1, p2 for Point of TOP-REAL 2,
         C for Simple_closed_curve,
         P for Subset of TOP-REAL 2;

definition
  let n be Nat, A be Subset of TOP-REAL n, a, b be Point of TOP-REAL n;
  pred a,b realize-max-dist-in A means :defr:
  a in A & b in A & for x, y being Point of TOP-REAL n st x in A & y in A holds
  dist(a,b) >= dist(x,y);
end;

set rl = -1;
set rp = 1;
set a = |[rl,0]|;
set b = |[rp,0]|;

theorem an1:
  ex p1,p2 st p1,p2 realize-max-dist-in C
  proof
    t: TOP-REAL 2 = TopSpaceMetr Euclid 2 by EUCLID:def 8; then
    reconsider D=C as Subset of TopSpaceMetr Euclid 2;
    consider x1,x2 being Point of Euclid 2 such that
    x: x1 in D & x2 in D &
    dist(x1,x2) = max_dist_max(D,D) by t,WEIERSTR:39;
    reconsider a=x1,b=x2 as Point of TOP-REAL 2 by SPPOL_1:18;
    take a,b;
    thus a in C & b in C by x;
    let x, y be Point of TOP-REAL 2 such that
    a: x in C & y in C;
    reconsider x'=x,y'=y as Point of Euclid 2 by SPPOL_1:17;
    dist(x',y') <= max_dist_max(D,D) by t,a,WEIERSTR:40; then
    dist(x,y) <= max_dist_max(D,D) by GOBRD14:def 1;
    hence dist(a,b) >= dist(x,y) by x,GOBRD14:def 1;
  end;

definition
  let M be non empty MetrStruct;
  let f be Function of TopSpaceMetr M, TopSpaceMetr M;
  attr f is isometric means :iso:
  ex g being isometric Function of M,M st g=f;
end;

registration
  let M be non empty MetrStruct;
  cluster isometric Function of TopSpaceMetr M, TopSpaceMetr M;
  existence
  proof
    consider f being isometric Function of M,M;
    TopSpaceMetr M = TopStruct (#the carrier of M,Family_open_set M#)
    by PCOMPS_1:def 6; then
    reconsider f as Function of TopSpaceMetr M,TopSpaceMetr M;
    take f;
    thus thesis by iso;
  end;
end;

registration
  let M be non empty MetrSpace;
  cluster isometric -> continuous Function of TopSpaceMetr M, TopSpaceMetr M;
  coherence
  proof
    let f be Function of TopSpaceMetr M, TopSpaceMetr M;
    assume f is isometric; then
    consider g being isometric Function of M,M such that
    g: g=f by iso;
    let W be Point of TopSpaceMetr M;
    let G be a_neighborhood of f.W;
    t: TopSpaceMetr M = TopStruct (#the carrier of M,Family_open_set M#)
    by PCOMPS_1:def 6; then
    reconsider fw=f.W,w=W as Point of M;
    f.W in Int G by CONNSP_2:def 1; then
    consider r being real number such that
    r: r > 0 & Ball(fw,r) c= G by GOBOARD6:7;
    reconsider H=Ball(w,r) as a_neighborhood of W by HAUSDORF:6,r;
    take H;
    thus f.:H c= G
    proof
      let a be set;
      assume a in f.:H; then
      consider b being set such that
      b: b in dom f & b in H & f.b = a by FUNCT_1:def 12;
      reconsider b as Point of TopSpaceMetr M by b;
      reconsider b'=b as Point of M by t;
      dist(b',w) < r by b,METRIC_1:12; then
      dist(g.b',fw) < r by g,VECTMETR:def 3; then
      a in Ball(fw,r) by b,METRIC_1:12,g;
      hence a in G by r;
    end;
  end;
end;

registration
  let M be non empty MetrSpace;
  cluster isometric ->
     being_homeomorphism Function of TopSpaceMetr M, TopSpaceMetr M;
  coherence
  proof
    let f be Function of TopSpaceMetr M, TopSpaceMetr M;
    assume i: f is isometric; then
    consider g being isometric Function of M,M such that
    g: g=f by iso;
    thus dom f = the carrier of TopSpaceMetr M by FUNCT_2:def 1
    .= [#]TopSpaceMetr M by PRE_TOPC:def 3;
    p: the carrier of M = [#]M by PRE_TOPC:def 3;
    rg: rng g = the carrier of M by VECTMETR:def 3;
    hence r: rng f = the carrier of TopSpaceMetr M by g,TOPMETR:16
    .= [#]TopSpaceMetr M by PRE_TOPC:def 3;
    thus f is one-to-one by g;
    reconsider f1=f as isometric Function
 of TopSpaceMetr M,TopSpaceMetr M by i;
    f1 is continuous;
    hence f is continuous;
    gi: g" is isometric Function of M,M by VECTMETR:3;
    the carrier of TopSpaceMetr M=the carrier of M by TOPMETR:16; then
    reconsider g1=g" as Function of TopSpaceMetr M,TopSpaceMetr M;
    f"= (g qua Function)" by g,TOPS_2:def 4,r
    .= g" by p,rg,TOPS_2:def 4; then
    f" is isometric Function of TopSpaceMetr M,TopSpaceMetr M by iso,gi;
    hence f" is continuous;
  end;
end;

definition
  let a be Real;
  func Rotate(a) -> Function of TOP-REAL 2,TOP-REAL 2 means :rot:
  for p being Point of TOP-REAL 2 holds
  it.p = |[Re Rotate(p`1+(p`2)*<i>,a),Im Rotate(p`1+(p`2)*<i>,a)]|;
  existence
  proof
    deffunc F(Point of TOP-REAL 2) =
    |[Re Rotate($1`1+($1`2)*<i>,a),Im Rotate($1`1+($1`2)*<i>,a)]|;
    consider f being Function of TOP-REAL 2,TOP-REAL 2 such that
    f: for p being Point of TOP-REAL 2 holds f.p=F(p) from FUNCT_2:sch 4;
    take f;
    thus thesis by f;
  end;
  uniqueness
  proof
    let f,g be Function of TOP-REAL 2,TOP-REAL 2 such that
    f: for p being Point of TOP-REAL 2 holds
    f.p = |[Re Rotate(p`1+(p`2)*<i>,a),Im Rotate(p`1+(p`2)*<i>,a)]| and
    g: for p being Point of TOP-REAL 2 holds
    g.p = |[Re Rotate(p`1+(p`2)*<i>,a),Im Rotate(p`1+(p`2)*<i>,a)]|;
    now
      let p be Point of TOP-REAL 2;
      thus f.p = |[Re Rotate(p`1+(p`2)*<i>,a),Im Rotate(p`1+(p`2)*<i>,a)]|
      by f .= g.p by g;
    end;
    hence f=g by FUNCT_2:113;
  end;
end;

theorem ri:
  for a being Real st 0<= a & a < 2*PI
  for f being Function of TopSpaceMetr Euclid 2, TopSpaceMetr Euclid 2
  st f = Rotate a holds f is isometric
  proof
    let a be Real such that
    a: 0<=a & a< 2*PI;
    let f be Function of TopSpaceMetr Euclid 2, TopSpaceMetr Euclid 2 such that
    f: f = Rotate a;
    T: TOP-REAL 2=TopSpaceMetr Euclid 2 by EUCLID:def 8;
    t: TopSpaceMetr Euclid 2 =
    TopStruct (#the carrier of Euclid 2,Family_open_set Euclid 2#)
    by PCOMPS_1:def 6; then
    reconsider g=f as Function of Euclid 2,Euclid 2;
    g is isometric
    proof
      thus rng g = the carrier of Euclid 2
      proof
        thus rng g c= the carrier of Euclid 2;
        let o be set;
        assume o in the carrier of Euclid 2; then
        reconsider p=o as Point of TOP-REAL 2 by t,T;
        set pz=p`1+(p`2)*<i>;
        reconsider pz as Element of COMPLEX by XCMPLX_0:def 2;
        set arg=Arg pz;
        per cases;
        suppose z: pz<>0;
          per cases;
          suppose ar: a <= arg;
            set qz=Rotate(pz,-a);
            nz: qz<>0 by z,COMPLEX2:66;
            i1: 0<=arg-a by ar,XREAL_1:50;
            arg-a <= arg & arg < 2*PI by COMPTRIG:52,a,XREAL_1:45; then
            i2: -a+arg < 2*PI by XREAL_1:2;
            m: |.Rotate(pz,-a).| = |.pz.| by COMPLEX2:67; then
            qz=[* |.qz.|*cos (-a+arg), |.qz.|*sin (-a+arg) *]
            by COMPLEX2:def 4; then
            Arg qz = -a+arg by nz,i1,i2,COMPLEX2:21; then
            Rotate(qz,a) = [*|.qz.|*cos (a+(-a+arg)),|.qz.|*sin (a+(-a+arg))*]
            by COMPLEX2:def 4; then
            r: Rotate(qz,a) = pz by COMPLEX2:19,m;
            set q=|[Re qz,Im qz]|;
            dom g = the carrier of Euclid 2 by FUNCT_2:def 1; then
            do: q in dom g by T,t;
            g.q = |[Re Rotate(q`1+(q`2)*<i>,a),Im Rotate(q`1+(q`2)*<i>,a)]|
            by rot,f
            .= |[Re Rotate(Re qz+(q`2)*<i>,a),Im Rotate(q`1+(q`2)*<i>,a)]|
            by EUCLID:56
            .= |[Re Rotate(Re qz+(Im qz)*<i>,a),Im Rotate(q`1+(q`2)*<i>,a)]|
            by EUCLID:56
            .= |[Re Rotate(Re qz+(Im qz)*<i>,a),Im Rotate(Re qz+(q`2)*<i>,a)]|
            by EUCLID:56
            .=|[Re Rotate(Re qz+(Im qz)*<i>,a),Im Rotate(Re qz+(Im qz)*<i>,a)]|
            by EUCLID:56
            .= |[Re Rotate(qz,a),Im Rotate(Re qz+(Im qz)*<i>,a)]|
            by COMPLEX1:29
            .= |[Re pz,Im pz]| by r,COMPLEX1:29
            .= |[p`1,Im pz]| by COMPLEX1:28
            .= |[p`1,p`2]| by COMPLEX1:28
            .= p by EUCLID:57;
            hence o in rng g by do,FUNCT_1:def 5;
          end;
          suppose ar: a > arg;
            set qz=Rotate(pz,-a);
            nz: qz<>0 by z,COMPLEX2:66;
            0<= 2*PI-a & arg >= 0 by a,XREAL_1:50,COMPTRIG:52; then
            i1: 0<=2*PI-a+arg by XREAL_1:35;
            arg-a < 0 by ar,XREAL_1:51; then
            i2: 2*PI+(arg-a) < 2*PI by XREAL_1:32;
            m: |.Rotate(pz,-a).| = |.pz.| by COMPLEX2:67; then
            qz=[* |.qz.|*cos (-a+arg), |.qz.|*sin (-a+arg) *]
            by COMPLEX2:def 4; then
            qz=[* |.qz.|*cos (2*PI*1+(-a+arg)), |.qz.|*sin (-a+arg) *]
            by COMPLEX2:10; then
            qz=[* |.qz.|*cos (2*PI+(arg-a)), |.qz.|*sin (2*PI*1+(-a+arg)) *]
            by COMPLEX2:9; then
            Arg qz = 2*PI+(arg-a) by nz,i1,i2,COMPLEX2:21; then
            Rotate(qz,a) = [* |.qz.|*cos (a+(2*PI+(arg-a))),
            |.qz.|*sin (a+(2*PI+(arg-a))) *] by COMPLEX2:def 4; then
            Rotate(qz,a) = [* |.qz.|*cos arg, |.qz.|*sin (2*PI*1+arg) *]
              by COMPLEX2:10; then
            Rotate(qz,a) = [* |.qz.|*cos arg, |.qz.|*sin arg *] by COMPLEX2:9;
            then r: Rotate(qz,a) = pz by COMPLEX2:19,m;
            set q=|[Re qz,Im qz]|;
            dom g = the carrier of Euclid 2 by FUNCT_2:def 1; then
            do: q in dom g by T,t;
            g.q = |[Re Rotate(q`1+(q`2)*<i>,a),Im Rotate(q`1+(q`2)*<i>,a)]|
            by rot,f
            .= |[Re Rotate(Re qz+(q`2)*<i>,a),Im Rotate(q`1+(q`2)*<i>,a)]|
            by EUCLID:56
            .= |[Re Rotate(Re qz+(Im qz)*<i>,a),Im Rotate(q`1+(q`2)*<i>,a)]|
            by EUCLID:56
            .= |[Re Rotate(Re qz+(Im qz)*<i>,a),Im Rotate(Re qz+(q`2)*<i>,a)]|
            by EUCLID:56
            .=|[Re Rotate(Re qz+(Im qz)*<i>,a),Im Rotate(Re qz+(Im qz)*<i>,a)]|
            by EUCLID:56
            .=|[Re Rotate(qz,a),Im Rotate(Re qz+(Im qz)*<i>,a)]| by COMPLEX1:29
            .= |[Re pz,Im pz]| by r,COMPLEX1:29
            .= |[p`1,Im pz]| by COMPLEX1:28
            .= |[p`1,p`2]| by COMPLEX1:28
            .= p by EUCLID:57;
            hence o in rng g by do,FUNCT_1:def 5;
          end;
        end;
        suppose pz=0; then
          pp: p`1 = 0 & p`2 = 0 by COMPLEX1:28,COMPLEX1:12;
          set q=|[0,0]|;
          dom g = the carrier of Euclid 2 by FUNCT_2:def 1; then
          do: q in dom g by T,t;
          reconsider z=0 as Element of COMPLEX by XCMPLX_0:def 2;
          g.q = |[Re Rotate(q`1+(q`2)*<i>,a),Im Rotate(q`1+(q`2)*<i>,a)]|
          by f,rot
          .= |[Re Rotate(z+(q`2)*<i>,a),Im Rotate(q`1+(q`2)*<i>,a)]|
          by EUCLID:56
          .= |[Re Rotate(z+z*<i>,a),Im Rotate(q`1+(q`2)*<i>,a)]| by EUCLID:56
          .= |[Re Rotate(z+z*<i>,a),Im Rotate(z+(q`2)*<i>,a)]| by EUCLID:56
          .= |[Re Rotate(z,a),Im Rotate(z,a)]| by EUCLID:56
          .= |[Re z,Im Rotate(z,a)]| by COMPLEX2:66
          .= |[Re z,Im z]| by COMPLEX2:66
          .= p by pp,EUCLID:57,COMPLEX1:12;
          hence o in rng g by do,FUNCT_1:def 5;
        end;
      end;
      let X,Y be Point of Euclid 2;
      TOP-REAL 2 = TopSpaceMetr Euclid 2 by EUCLID:def 8; then
      TOP-REAL 2 = TopStruct (#the carrier of Euclid 2,
      Family_open_set Euclid 2#) by PCOMPS_1:def 6; then
      reconsider x=X,y=Y,gx=g.X,gy=g.Y as Point of TOP-REAL 2;
      e1: |[Re Rotate((x`1)+(x`2)*<i>,a),Im Rotate((x`1)+(x`2)*<i>,a)]|`1=
      Re Rotate((x`1)+(x`2)*<i>,a) &
      |[Re Rotate((x`1)+(x`2)*<i>,a),Im Rotate((x`1)+(x`2)*<i>,a)]|`2=
      Im Rotate((x`1)+(x`2)*<i>,a) by EUCLID:56;
      e2: |[Re Rotate((y`1)+(y`2)*<i>,a),Im Rotate((y`1)+(y`2)*<i>,a)]|`1=
      Re Rotate((y`1)+(y`2)*<i>,a) &
      |[Re Rotate((y`1)+(y`2)*<i>,a),Im Rotate((y`1)+(y`2)*<i>,a)]|`2=
      Im Rotate((y`1)+(y`2)*<i>,a) by EUCLID:56;
      reconsider xx=x`1+(x`2)*<i>,yy=y`1+(y`2)*<i> as Element of COMPLEX
      by XCMPLX_0:def 2;
      l1: Re (x`1+(x`2)*<i>)=x`1 & Im (x`1+(x`2)*<i>)=x`2 by COMPLEX1:28;
      l2: Re (y`1+(y`2)*<i>)=y`1 & Im (y`1+(y`2)*<i>)=y`2 by COMPLEX1:28;
      j: (sin a)^2+(cos a)^2 = 1 by SIN_COS:32;
      x=|[x`1,x`2]| & y=|[y`1,y`2]| by EUCLID:57;
      hence dist(X,Y) = sqrt ((x`1 - y`1)^2 + (x`2 - y`2)^2) by GOBOARD6:9
      .= sqrt(((x`1)^2 -2*x`1*y`1 + (y`1)^2)*((sin a)^2+(cos a)^2) +
      (x`2 - y`2)^2*((sin a)^2+(cos a)^2)) by SQUARE_1:64,j
      .= sqrt(((x`1)^2 -2*x`1*y`1 + (y`1)^2)*((sin a)^2+(cos a)^2) +
      ((x`2)^2 -2*x`2*y`2 + (y`2)^2)*((sin a)^2+(cos a)^2)) by SQUARE_1:64
      .= sqrt(((x`1)*(x`1) -2*x`1*y`1 + (y`1)^2)*((sin a)^2+(cos a)^2) +
      ((x`2)^2 -2*x`2*y`2 + (y`2)^2)*((sin a)^2+(cos a)^2)) by SQUARE_1:def 3
      .= sqrt(((x`1)*(x`1) -2*x`1*y`1 + (y`1)*(y`1))*((sin a)^2+(cos a)^2) +
      ((x`2)^2 -2*x`2*y`2 + (y`2)^2)*((sin a)^2+(cos a)^2)) by SQUARE_1:def 3
      .= sqrt(((x`1)*(x`1) -2*x`1*y`1 + (y`1)*(y`1))*((sin a)*
      (sin a)+(cos a)^2)
      + ((x`2)^2 -2*x`2*y`2 + (y`2)^2)*((sin a)^2+(cos a)^2)) by SQUARE_1:def 3
      .= sqrt(((x`1)*(x`1) -2*x`1*y`1 +
      (y`1)*(y`1))*((sin a)*(sin a)+(cos a)*(cos a)) +
      ((x`2)^2 -2*x`2*y`2 + (y`2)^2)*((sin a)^2+(cos a)^2)) by SQUARE_1:def 3
      .= sqrt(((x`1)*(x`1) -2*x`1*y`1 +
      (y`1)*(y`1))*((sin a)*(sin a)+(cos a)*(cos a)) +
      ((x`2)*(x`2) -2*x`2*y`2 + (y`2)^2)*((sin a)^2+(cos a)^2))
      by SQUARE_1:def 3
      .= sqrt(((x`1)*(x`1) -2*x`1*y`1 +
      (y`1)*(y`1))*((sin a)*(sin a)+(cos a)*(cos a)) +
      ((x`2)*(x`2) -2*x`2*y`2 + (y`2)*(y`2))*((sin a)^2+(cos a)^2))
      by SQUARE_1:def 3
      .= sqrt(((x`1)*(x`1) -2*x`1*y`1 +
      (y`1)*(y`1))*((sin a)*(sin a)+(cos a)*(cos a)) +
      ((x`2)*(x`2) -2*x`2*y`2 + (y`2)*(y`2))*((sin a)*(sin a)+(cos a)^2))
      by SQUARE_1:def 3
      .= sqrt(((x`1)*(x`1) -2*x`1*y`1 +
      (y`1)*(y`1))*((sin a)*(sin a)+(cos a)*(cos a)) +
      ((x`2)*(x`2) -2*x`2*y`2 + (y`2)*(y`2))*((sin a)*(sin a)+(cos a)*(cos a)))
      by SQUARE_1:def 3
      .= sqrt((((x`1)*(x`1)*(cos a)*(cos a)-2*(x`1*cos a)*(x`2*sin a)+
      (x`2)*(x`2)*(sin a)*(sin a))
      -2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+
      ((y`1)*(y`1)*(cos a)*(cos a)
      -2*(y`1*cos a)*(y`2*sin a)+(y`2)*(y`2)*(sin a)*(sin a))) +
      (((x`1)*(x`1)*(sin a)*(sin a)+
      2*(x`1*sin a)*(x`2*cos a)+(x`2)*(x`2)*(cos a)*(cos a))
      -2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      ((y`1)*(y`1)*(sin a)*(sin a)+
      2*(y`1*sin a)*(y`2*cos a)+(y`2)*(y`2)*(cos a)*(cos a))))
      .= sqrt((((x`1)^2*(cos a)*(cos a)-
      2*(x`1*cos a)*(x`2*sin a)+(x`2)*(x`2)*(sin a)*(sin a))
      -2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+
      ((y`1)*(y`1)*(cos a)*(cos a)
      -2*(y`1*cos a)*(y`2*sin a)+(y`2)*(y`2)*(sin a)*(sin a))) +
      (((x`1)*(x`1)*(sin a)*(sin a)+
      2*(x`1*sin a)*(x`2*cos a)+(x`2)*(x`2)*(cos a)*(cos a))
      -2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      ((y`1)*(y`1)*(sin a)*(sin a)+
      2*(y`1*sin a)*(y`2*cos a)+(y`2)*(y`2)*(cos a)*(cos a))))
      by SQUARE_1:def 3
      .= sqrt((((x`1)^2*((cos a)*(cos a))-
      2*(x`1*cos a)*(x`2*sin a)+(x`2)*(x`2)*(sin a)*(sin a))
      -2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+
      ((y`1)*(y`1)*(cos a)*(cos a)
      -2*(y`1*cos a)*(y`2*sin a)+(y`2)*(y`2)*(sin a)*(sin a))) +
      (((x`1)*(x`1)*(sin a)*(sin a)+
      2*(x`1*sin a)*(x`2*cos a)+(x`2)*(x`2)*(cos a)*(cos a))
      -2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      ((y`1)*(y`1)*(sin a)*(sin a)+2*(y`1*sin a)*(y`2*cos a)+
      (y`2)*(y`2)*(cos a)*(cos a))))
      .= sqrt((((x`1)^2*(cos a)^2-
      2*(x`1*cos a)*(x`2*sin a)+(x`2)*(x`2)*(sin a)*(sin a))
      -2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+
      ((y`1)*(y`1)*(cos a)*(cos a)
      -2*(y`1*cos a)*(y`2*sin a)+(y`2)*(y`2)*(sin a)*(sin a))) +
      (((x`1)*(x`1)*(sin a)*(sin a)+2*(x`1*sin a)*(x`2*cos a)+
      (x`2)*(x`2)*(cos a)*(cos a))
      -2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      ((y`1)*(y`1)*(sin a)*(sin a)+2*(y`1*sin a)*(y`2*cos a)+
      (y`2)*(y`2)*(cos a)*(cos a)))) by SQUARE_1:def 3
      .= sqrt((((x`1)^2*(cos a)^2-2*(x`1*cos a)*(x`2*sin a)+
      (x`2)^2*(sin a)*(sin a))
      -2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+
      ((y`1)*(y`1)*(cos a)*(cos a)
      -2*(y`1*cos a)*(y`2*sin a)+(y`2)*(y`2)*(sin a)*(sin a))) +
      (((x`1)*(x`1)*(sin a)*(sin a)+2*(x`1*sin a)*(x`2*cos a)+
      (x`2)*(x`2)*(cos a)*(cos a))
      -2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      ((y`1)*(y`1)*(sin a)*(sin a)+2*(y`1*sin a)*(y`2*cos a)+
      (y`2)*(y`2)*(cos a)*(cos a)))) by SQUARE_1:def 3
      .= sqrt((((x`1)^2*(cos a)^2-
      2*(x`1*cos a)*(x`2*sin a)+(x`2)^2*((sin a)*(sin a)))
      -2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+
      ((y`1)*(y`1)*(cos a)*(cos a)
      -2*(y`1*cos a)*(y`2*sin a)+(y`2)*(y`2)*(sin a)*(sin a))) +
      (((x`1)*(x`1)*(sin a)*(sin a)+
      2*(x`1*sin a)*(x`2*cos a)+(x`2)*(x`2)*(cos a)*(cos a))
      -2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      ((y`1)*(y`1)*(sin a)*(sin a)+2*(y`1*sin a)*(y`2*cos a)+
      (y`2)*(y`2)*(cos a)*(cos a))))
      .= sqrt((((x`1)^2*(cos a)^2-2*(x`1*cos a)*(x`2*sin a)+(x`2)^2*(sin a)^2)
      -2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+
      ((y`1)*(y`1)*(cos a)*(cos a)
      -2*(y`1*cos a)*(y`2*sin a)+(y`2)*(y`2)*(sin a)*(sin a))) +
      (((x`1)*(x`1)*(sin a)*(sin a)+2*(x`1*sin a)*(x`2*cos a)+
      (x`2)*(x`2)*(cos a)*(cos a))
      -2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      ((y`1)*(y`1)*(sin a)*(sin a)+2*(y`1*sin a)*(y`2*cos a)+
      (y`2)*(y`2)*(cos a)*(cos a)))) by SQUARE_1:def 3
      .= sqrt((((x`1)^2*(cos a)^2-2*(x`1*cos a)*(x`2*sin a)+(x`2)^2*(sin a)^2)
      -2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+((y`1)^2*(cos a)*(cos a)
      -2*(y`1*cos a)*(y`2*sin a)+(y`2)*(y`2)*(sin a)*(sin a))) +
      (((x`1)*(x`1)*(sin a)*(sin a)+2*(x`1*sin a)*(x`2*cos a)+
      (x`2)*(x`2)*(cos a)*(cos a))
      -2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      ((y`1)*(y`1)*(sin a)*(sin a)+2*(y`1*sin a)*(y`2*cos a)+
      (y`2)*(y`2)*(cos a)*(cos a)))) by SQUARE_1:def 3
      .= sqrt((((x`1)^2*(cos a)^2-2*(x`1*cos a)*(x`2*sin a)+(x`2)^2*(sin a)^2)
      -2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+((y`1)^2*((cos a)*(cos a))
      -2*(y`1*cos a)*(y`2*sin a)+(y`2)*(y`2)*(sin a)*(sin a))) +
      (((x`1)*(x`1)*(sin a)*(sin a)+2*(x`1*sin a)*(x`2*cos a)+
      (x`2)*(x`2)*(cos a)*(cos a))
      -2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      ((y`1)*(y`1)*(sin a)*(sin a)+2*(y`1*sin a)*(y`2*cos a)+
      (y`2)*(y`2)*(cos a)*(cos a))))
      .= sqrt((((x`1)^2*(cos a)^2-2*(x`1*cos a)*(x`2*sin a)+(x`2)^2*(sin a)^2)
      -2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+((y`1)^2*((cos a)^2)
      -2*(y`1*cos a)*(y`2*sin a)+(y`2)*(y`2)*(sin a)*(sin a))) +
      (((x`1)*(x`1)*(sin a)*(sin a)+2*(x`1*sin a)*(x`2*cos a)+
      (x`2)*(x`2)*(cos a)*(cos a))
      -2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      ((y`1)*(y`1)*(sin a)*(sin a)+2*(y`1*sin a)*(y`2*cos a)+
      (y`2)*(y`2)*(cos a)*(cos a)))) by SQUARE_1:def 3
      .= sqrt((((x`1)^2*(cos a)^2-2*(x`1*cos a)*(x`2*sin a)+(x`2)^2*(sin a)^2)
      -2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+((y`1)^2*((cos a)^2)
      -2*(y`1*cos a)*(y`2*sin a)+(y`2)^2*(sin a)*(sin a))) +
      (((x`1)*(x`1)*(sin a)*(sin a)+2*(x`1*sin a)*(x`2*cos a)+
      (x`2)*(x`2)*(cos a)*(cos a))
      -2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      ((y`1)*(y`1)*(sin a)*(sin a)+2*(y`1*sin a)*(y`2*cos a)+
      (y`2)*(y`2)*(cos a)*(cos a)))) by SQUARE_1:def 3
      .= sqrt((((x`1)^2*(cos a)^2-2*(x`1*cos a)*(x`2*sin a)+(x`2)^2*(sin a)^2)
      -2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+((y`1)^2*((cos a)^2)
      -2*(y`1*cos a)*(y`2*sin a)+(y`2)^2*((sin a)*(sin a)))) +
      (((x`1)*(x`1)*(sin a)*(sin a)+2*(x`1*sin a)*(x`2*cos a)+
      (x`2)*(x`2)*(cos a)*(cos a))
      -2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      ((y`1)*(y`1)*(sin a)*(sin a)+2*(y`1*sin a)*(y`2*cos a)+
      (y`2)*(y`2)*(cos a)*(cos a))))
      .= sqrt((((x`1)^2*(cos a)^2-2*(x`1*cos a)*(x`2*sin a)+(x`2)^2*(sin a)^2)
      -2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+((y`1)^2*((cos a)^2)
      -2*(y`1*cos a)*(y`2*sin a)+(y`2)^2*((sin a)^2))) +
      (((x`1)*(x`1)*(sin a)*(sin a)+2*(x`1*sin a)*(x`2*cos a)+
      (x`2)*(x`2)*(cos a)*(cos a))
      -2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      ((y`1)*(y`1)*(sin a)*(sin a)+2*(y`1*sin a)*(y`2*cos a)+
      (y`2)*(y`2)*(cos a)*(cos a)))) by SQUARE_1:def 3
      .= sqrt((((x`1)^2*(cos a)^2-2*(x`1*cos a)*(x`2*sin a)+(x`2)^2*(sin a)^2)
      -2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+((y`1)^2*((cos a)^2)
      -2*(y`1*cos a)*(y`2*sin a)+(y`2)^2*((sin a)^2))) +
      (((x`1)^2*(sin a)*(sin a)+2*(x`1*sin a)*(x`2*cos a)+
      (x`2)*(x`2)*(cos a)*(cos a))
      -2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      ((y`1)*(y`1)*(sin a)*(sin a)+2*(y`1*sin a)*(y`2*cos a)+
      (y`2)*(y`2)*(cos a)*(cos a)))) by SQUARE_1:def 3
      .= sqrt((((x`1)^2*(cos a)^2-2*(x`1*cos a)*(x`2*sin a)+(x`2)^2*(sin a)^2)
      -2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+((y`1)^2*((cos a)^2)
      -2*(y`1*cos a)*(y`2*sin a)+(y`2)^2*((sin a)^2))) +
      (((x`1)^2*((sin a)*(sin a))+2*(x`1*sin a)*(x`2*cos a)+
      (x`2)*(x`2)*(cos a)*(cos a))
      -2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      ((y`1)*(y`1)*(sin a)*(sin a)+2*(y`1*sin a)*(y`2*cos a)+
      (y`2)*(y`2)*(cos a)*(cos a))))
      .= sqrt((((x`1)^2*(cos a)^2-2*(x`1*cos a)*(x`2*sin a)+(x`2)^2*(sin a)^2)
      -2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+((y`1)^2*((cos a)^2)
      -2*(y`1*cos a)*(y`2*sin a)+(y`2)^2*((sin a)^2))) +
      (((x`1)^2*((sin a)^2)+2*(x`1*sin a)*(x`2*cos a)+
      (x`2)*(x`2)*(cos a)*(cos a))
      -2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      ((y`1)*(y`1)*(sin a)*(sin a)+2*(y`1*sin a)*(y`2*cos a)+
      (y`2)*(y`2)*(cos a)*(cos a)))) by SQUARE_1:def 3
      .= sqrt((((x`1)^2*(cos a)^2-2*(x`1*cos a)*(x`2*sin a)+(x`2)^2*(sin a)^2)
      -2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+((y`1)^2*((cos a)^2)
      -2*(y`1*cos a)*(y`2*sin a)+(y`2)^2*((sin a)^2))) +
      (((x`1)^2*((sin a)^2)+2*(x`1*sin a)*(x`2*cos a)+(x`2)^2*(cos a)*(cos a))
      -2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      ((y`1)*(y`1)*(sin a)*(sin a)+2*(y`1*sin a)*(y`2*cos a)+
      (y`2)*(y`2)*(cos a)*(cos a)))) by SQUARE_1:def 3
      .= sqrt((((x`1)^2*(cos a)^2-2*(x`1*cos a)*(x`2*sin a)+(x`2)^2*(sin a)^2)
      -2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+((y`1)^2*((cos a)^2)
      -2*(y`1*cos a)*(y`2*sin a)+(y`2)^2*((sin a)^2))) +
      (((x`1)^2*((sin a)^2)+2*(x`1*sin a)*(x`2*cos a)+
      (x`2)^2*((cos a)*(cos a)))
      -2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      ((y`1)*(y`1)*(sin a)*(sin a)+2*(y`1*sin a)*(y`2*cos a)+
      (y`2)*(y`2)*(cos a)*(cos a))))
      .= sqrt((((x`1)^2*(cos a)^2-2*(x`1*cos a)*(x`2*sin a)+(x`2)^2*(sin a)^2)
      -2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+((y`1)^2*((cos a)^2)
      -2*(y`1*cos a)*(y`2*sin a)+(y`2)^2*((sin a)^2))) +
      (((x`1)^2*((sin a)^2)+2*(x`1*sin a)*(x`2*cos a)+(x`2)^2*((cos a)^2))
      -2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      ((y`1)*(y`1)*(sin a)*(sin a)+2*(y`1*sin a)*(y`2*cos a)+
      (y`2)*(y`2)*(cos a)*(cos a)))) by SQUARE_1:def 3
      .= sqrt((((x`1)^2*(cos a)^2-2*(x`1*cos a)*(x`2*sin a)+(x`2)^2*(sin a)^2)
      -2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+((y`1)^2*((cos a)^2)
      -2*(y`1*cos a)*(y`2*sin a)+(y`2)^2*((sin a)^2))) +
      (((x`1)^2*((sin a)^2)+2*(x`1*sin a)*(x`2*cos a)+(x`2)^2*((cos a)^2))
      -2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      ((y`1)^2*(sin a)*(sin a)+2*(y`1*sin a)*(y`2*cos a)+
      (y`2)*(y`2)*(cos a)*(cos a)))) by SQUARE_1:def 3
      .= sqrt((((x`1)^2*(cos a)^2-2*(x`1*cos a)*(x`2*sin a)+(x`2)^2*(sin a)^2)
      -2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+((y`1)^2*((cos a)^2)
      -2*(y`1*cos a)*(y`2*sin a)+(y`2)^2*((sin a)^2))) +
      (((x`1)^2*((sin a)^2)+2*(x`1*sin a)*(x`2*cos a)+(x`2)^2*((cos a)^2))
      -2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      ((y`1)^2*((sin a)*(sin a))+2*(y`1*sin a)*(y`2*cos a)+
      (y`2)*(y`2)*(cos a)*(cos a))))
      .= sqrt((((x`1)^2*(cos a)^2-2*(x`1*cos a)*(x`2*sin a)+(x`2)^2*(sin a)^2)
      -2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+((y`1)^2*((cos a)^2)
      -2*(y`1*cos a)*(y`2*sin a)+(y`2)^2*((sin a)^2))) +
      (((x`1)^2*((sin a)^2)+2*(x`1*sin a)*(x`2*cos a)+(x`2)^2*((cos a)^2))
      -2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      ((y`1)^2*((sin a)^2)+2*(y`1*sin a)*(y`2*cos a)+
      (y`2)*(y`2)*(cos a)*(cos a)))) by SQUARE_1:def 3
      .= sqrt((((x`1)^2*(cos a)^2-2*(x`1*cos a)*(x`2*sin a)+(x`2)^2*(sin a)^2)
      -2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+((y`1)^2*((cos a)^2)
      -2*(y`1*cos a)*(y`2*sin a)+(y`2)^2*((sin a)^2))) +
      (((x`1)^2*((sin a)^2)+2*(x`1*sin a)*(x`2*cos a)+(x`2)^2*((cos a)^2))
      -2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      ((y`1)^2*((sin a)^2)+2*(y`1*sin a)*(y`2*cos a)+
      (y`2)^2*(cos a)*(cos a)))) by SQUARE_1:def 3
      .= sqrt((((x`1)^2*(cos a)^2-2*(x`1*cos a)*(x`2*sin a)+(x`2)^2*(sin a)^2)
      -2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+((y`1)^2*((cos a)^2)
      -2*(y`1*cos a)*(y`2*sin a)+(y`2)^2*((sin a)^2))) +
      (((x`1)^2*((sin a)^2)+2*(x`1*sin a)*(x`2*cos a)+(x`2)^2*((cos a)^2))
      -2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      ((y`1)^2*((sin a)^2)+2*(y`1*sin a)*(y`2*cos a)+
      (y`2)^2*((cos a)*(cos a)))))
      .= sqrt((((x`1)^2*(cos a)^2-2*(x`1*cos a)*(x`2*sin a)+
      (x`2)^2*(sin a)^2)-2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+
      ((y`1)^2*(cos a)^2-2*(y`1*cos a)*(y`2*sin a)+(y`2)^2*(sin a)^2)) +
      (((x`1)^2*(sin a)^2+2*(x`1*sin a)*(x`2*cos a)+(x`2)^2*(cos a)^2)-
      2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+((y`1)^2*(sin a)^2+
      2*(y`1*sin a)*(y`2*cos a)+(y`2)^2*(cos a)^2))) by SQUARE_1:def 3
      .= sqrt((((x`1)^2*(cos a)^2-2*(x`1*cos a)*(x`2*sin a)+
      (x`2)^2*(sin a)^2)-2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+
      ((y`1)^2*(cos a)^2-2*(y`1*cos a)*(y`2*sin a)+(y`2)^2*(sin a)^2)) +
      (((x`1)^2*(sin a)^2+2*(x`1*sin a)*(x`2*cos a)+(x`2)^2*(cos a)^2)-
      2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+((y`1)^2*(sin a)^2+
      2*(y`1*sin a)*(y`2*cos a)+(y`2*cos a)^2))) by SQUARE_1:68
      .= sqrt((((x`1)^2*(cos a)^2-2*(x`1*cos a)*(x`2*sin a)+
      (x`2)^2*(sin a)^2)-2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+
      ((y`1)^2*(cos a)^2-2*(y`1*cos a)*(y`2*sin a)+(y`2)^2*(sin a)^2)) +
      (((x`1)^2*(sin a)^2+2*(x`1*sin a)*(x`2*cos a)+(x`2)^2*(cos a)^2)-
      2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+((y`1*sin a)^2+
      2*(y`1*sin a)*(y`2*cos a)+(y`2*cos a)^2))) by SQUARE_1:68
      .= sqrt((((x`1)^2*(cos a)^2-2*(x`1*cos a)*(x`2*sin a)+
      (x`2)^2*(sin a)^2)-2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+
      ((y`1)^2*(cos a)^2-2*(y`1*cos a)*(y`2*sin a)+(y`2)^2*(sin a)^2)) +
      (((x`1)^2*(sin a)^2+2*(x`1*sin a)*(x`2*cos a)+(x`2*cos a)^2)-
      2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+((y`1*sin a)^2+
      2*(y`1*sin a)*(y`2*cos a)+(y`2*cos a)^2))) by SQUARE_1:68
      .= sqrt((((x`1)^2*(cos a)^2-2*(x`1*cos a)*(x`2*sin a)+
      (x`2)^2*(sin a)^2)-2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+
      ((y`1)^2*(cos a)^2-2*(y`1*cos a)*(y`2*sin a)+(y`2)^2*(sin a)^2)) +
      (((x`1*sin a)^2+2*(x`1*sin a)*(x`2*cos a)+(x`2*cos a)^2)-
      2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+((y`1*sin a)^2+
      2*(y`1*sin a)*(y`2*cos a)+(y`2*cos a)^2))) by SQUARE_1:68
      .= sqrt((((x`1)^2*(cos a)^2-2*(x`1*cos a)*(x`2*sin a)+
      (x`2)^2*(sin a)^2)-2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+
      ((y`1)^2*(cos a)^2-2*(y`1*cos a)*(y`2*sin a)+(y`2*sin a)^2)) +
      (((x`1*sin a)^2+2*(x`1*sin a)*(x`2*cos a)+(x`2*cos a)^2)-
      2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+((y`1*sin a)^2+
      2*(y`1*sin a)*(y`2*cos a)+(y`2*cos a)^2))) by SQUARE_1:68
      .= sqrt((((x`1)^2*(cos a)^2-2*(x`1*cos a)*(x`2*sin a)+
      (x`2)^2*(sin a)^2)-2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+
      ((y`1*cos a)^2-2*(y`1*cos a)*(y`2*sin a)+(y`2*sin a)^2)) +
      (((x`1*sin a)^2+2*(x`1*sin a)*(x`2*cos a)+(x`2*cos a)^2)-
      2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      ((y`1*sin a)^2+2*(y`1*sin a)*(y`2*cos a)+(y`2*cos a)^2))) by SQUARE_1:68
      .= sqrt((((x`1)^2*(cos a)^2-2*(x`1*cos a)*(x`2*sin a)+
      (x`2*sin a)^2)-2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+
      ((y`1*cos a)^2-2*(y`1*cos a)*(y`2*sin a)+(y`2*sin a)^2)) +
      (((x`1*sin a)^2+2*(x`1*sin a)*(x`2*cos a)+(x`2*cos a)^2)-
      2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+((y`1*sin a)^2+
      2*(y`1*sin a)*(y`2*cos a)+(y`2*cos a)^2))) by SQUARE_1:68
      .= sqrt((((x`1*cos a)^2-2*(x`1*cos a)*(x`2*sin a)+(x`2*sin a)^2)-
      2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+((y`1*cos a)^2-
      2*(y`1*cos a)*(y`2*sin a)+(y`2*sin a)^2)) +
      (((x`1*sin a)^2+2*(x`1*sin a)*(x`2*cos a)+(x`2*cos a)^2)-
      2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+((y`1*sin a)^2+
      2*(y`1*sin a)*(y`2*cos a)+(y`2*cos a)^2))) by SQUARE_1:68
      .= sqrt((((x`1*cos a)^2-2*(x`1*cos a)*(x`2*sin a)+(x`2*sin a)^2)-
      2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+((y`1*cos a)^2-
      2*(y`1*cos a)*(y`2*sin a)+(y`2*sin a)^2)) +
      (((x`1*sin a)^2+2*(x`1*sin a)*(x`2*cos a)+(x`2*cos a)^2)-
      2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+(y`1*sin a+y`2*cos a)^2))
      by SQUARE_1:63
      .= sqrt((((x`1*cos a)^2-2*(x`1*cos a)*(x`2*sin a)+(x`2*sin a)^2)-
      2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+
      ((y`1*cos a)^2-2*(y`1*cos a)*(y`2*sin a)+(y`2*sin a)^2)) +
      ((x`1*sin a+x`2*cos a)^2-2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      (y`1*sin a+y`2*cos a)^2)) by SQUARE_1:63
      .= sqrt((((x`1*cos a)^2-2*(x`1*cos a)*(x`2*sin a)+(x`2*sin a)^2)-
      2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+(y`1*cos a-y`2*sin a)^2) +
      ((x`1*sin a+x`2*cos a)^2-2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      (y`1*sin a+y`2*cos a)^2)) by SQUARE_1:64
      .= sqrt(((x`1*cos a-x`2*sin a)^2-
      2*(x`1*cos a-x`2*sin a)*(y`1*cos a-y`2*sin a)+(y`1*cos a-y`2*sin a)^2) +
      ((x`1*sin a+x`2*cos a)^2-2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      (y`1*sin a+y`2*cos a)^2)) by SQUARE_1:64
      .= sqrt(((x`1*cos a-x`2*sin a)-(y`1*cos a-y`2*sin a))^2 +
      ((x`1*sin a+x`2*cos a)^2-2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
      (y`1*sin a+y`2*cos a)^2)) by SQUARE_1:64
      .= sqrt(((x`1*cos a-x`2*sin a)-(y`1*cos a-y`2*sin a))^2 +
              ((x`1*sin a+x`2*cos a)-(y`1*sin a+y`2*cos a))^2) by SQUARE_1:64
      .= sqrt(((Re Rotate(xx,a))-(y`1*cos a-y`2*sin a))^2 +
         ((x`1*sin a+x`2*cos a)-(y`1*sin a+y`2*cos a))^2) by l1,COMPLEX2:70
      .= sqrt(((Re Rotate(xx,a))-(y`1*cos a-y`2*sin a))^2 +
              ((Im Rotate(xx,a))-(y`1*sin a+y`2*cos a))^2) by l1,COMPLEX2:70
      .= sqrt(((Re Rotate(xx,a))-(Re Rotate(yy,a)))^2 +
              ((Im Rotate(xx,a))-(y`1*sin a+y`2*cos a))^2) by l2,COMPLEX2:70
      .= sqrt(((Re Rotate(xx,a))-(Re Rotate(yy,a)))^2 +
              ((Im Rotate(xx,a))-(Im Rotate(yy,a)))^2) by l2,COMPLEX2:70
      .= dist(|[Re Rotate((x`1)+(x`2)*<i>,a),Im Rotate((x`1)+(x`2)*<i>,a)]|,
      |[Re Rotate((y`1)+(y`2)*<i>,a),Im Rotate((y`1)+(y`2)*<i>,a)]|)
      by GOBRD14:9,e1,e2
      .= dist(|[Re Rotate((x`1)+(x`2)*<i>,a),Im Rotate((x`1)+(x`2)*<i>,a)]|,gy)
      by rot,f
      .= dist(gx,gy) by rot,f
      .= dist(g.X,g.Y) by GOBRD14:def 1;
    end;
    hence f is isometric by iso;
  end;

theorem laf:
  for A,B,D being real number st
  p1,p2 realize-max-dist-in P holds
  AffineMap(A,B,A,D).p1,AffineMap(A,B,A,D).p2
  realize-max-dist-in AffineMap(A,B,A,D).:P
  proof
    let A,B,D be real number;
    set a=p1,b=p2,C=P;
    assume a,b realize-max-dist-in C; then
    ab: a in C & b in C & for x, y being Point of TOP-REAL 2 st
    x in C & y in C holds
    dist(a,b) >= dist(x,y) by defr;
    d: dom AffineMap(A,B,A,D) = the carrier of TOP-REAL 2 by FUNCT_2:def 1;
    hence AffineMap(A,B,A,D).a in AffineMap(A,B,A,D).:C by ab,FUNCT_1:def 12;
    thus AffineMap(A,B,A,D).b in AffineMap(A,B,A,D).:C by d,ab,FUNCT_1:def 12;
    let x, y be Point of TOP-REAL 2;
    assume x in AffineMap(A,B,A,D).:C; then
    consider X being set such that
    X: X in dom AffineMap(A,B,A,D) & X in C & AffineMap(A,B,A,D).X=x
    by FUNCT_1:def 12;
    reconsider X as Point of TOP-REAL 2 by X;
    assume y in AffineMap(A,B,A,D).:C; then
    consider Y being set such that
    Y: Y in dom AffineMap(A,B,A,D) & Y in C & AffineMap(A,B,A,D).Y=y
    by FUNCT_1:def 12;
    reconsider Y as Point of TOP-REAL 2 by Y;
    d: dist(a,b) >= dist(X,Y) by ab,X,Y;
    s: A^2>=0 & (a`1-b`1)^2 >=0 & (a`2-b`2)^2 >=0 &
    (X`1-Y`1)^2>=0 & (X`2-Y`2)^2>=0 by SQUARE_1:72; then
    s': (a`1-b`1)^2 + (a`2-b`2)^2 >=0+0 & (X`1-Y`1)^2 + (X`2-Y`2)^2 >= 0+0
    by XREAL_1:9;
    d1: dist(AffineMap(A,B,A,D).a,AffineMap(A,B,A,D).b)
     = dist(|[A*(a`1)+B,A*(a`2)+D]|,AffineMap(A,B,A,D).b) by JGRAPH_2:def 2
     .= dist(|[A*(a`1)+B,A*(a`2)+D]|,|[A*(b`1)+B,A*(b`2)+D]|) by JGRAPH_2:def 2
     .= sqrt((|[A*(a`1)+B,A*(a`2)+D]|`1-|[A*(b`1)+B,A*(b`2)+D]|`1)^2 +
     (|[A*(a`1)+B,A*(a`2)+D]|`2-|[A*(b`1)+B,A*(b`2)+D]|`2)^2) by GOBRD14:9
     .= sqrt((A*(a`1)+B-|[A*(b`1)+B,A*(b`2)+D]|`1)^2 +
     (|[A*(a`1)+B,A*(a`2)+D]|`2-|[A*(b`1)+B,A*(b`2)+D]|`2)^2) by EUCLID:56
     .= sqrt((A*(a`1)+B-(A*(b`1)+B))^2 +
     (|[A*(a`1)+B,A*(a`2)+D]|`2-|[A*(b`1)+B,A*(b`2)+D]|`2)^2) by EUCLID:56
     .= sqrt((A*(a`1)+B-(A*(b`1)+B))^2 +
     (A*(a`2)+D-|[A*(b`1)+B,A*(b`2)+D]|`2)^2) by EUCLID:56
     .= sqrt((A*(a`1)+B-(A*(b`1)+B))^2 + (A*(a`2)+D-(A*(b`2)+D))^2)
     by EUCLID:56
     .= sqrt((A*(a`1-b`1))^2 + (A*(a`2-b`2))^2)
     .= sqrt(A^2*(a`1-b`1)^2 + (A*(a`2-b`2))^2) by SQUARE_1:68
     .= sqrt(A^2*(a`1-b`1)^2 + A^2*(a`2-b`2)^2) by SQUARE_1:68
     .= sqrt(A^2*((a`1-b`1)^2 + (a`2-b`2)^2))
     .= sqrt(A^2)*sqrt((a`1-b`1)^2 + (a`2-b`2)^2) by SQUARE_1:97,s,s'
     .= sqrt(A^2)*dist(a,b) by GOBRD14:9;
     d2: dist(x,y) =
     dist(|[A*(X`1)+B,A*(X`2)+D]|,AffineMap(A,B,A,D).Y) by X,Y,JGRAPH_2:def 2
     .= dist(|[A*(X`1)+B,A*(X`2)+D]|,|[A*(Y`1)+B,A*(Y`2)+D]|) by JGRAPH_2:def 2
     .= sqrt((|[A*(X`1)+B,A*(X`2)+D]|`1-|[A*(Y`1)+B,A*(Y`2)+D]|`1)^2 +
     (|[A*(X`1)+B,A*(X`2)+D]|`2-|[A*(Y`1)+B,A*(Y`2)+D]|`2)^2) by GOBRD14:9
     .= sqrt((A*(X`1)+B-|[A*(Y`1)+B,A*(Y`2)+D]|`1)^2 +
     (|[A*(X`1)+B,A*(X`2)+D]|`2-|[A*(Y`1)+B,A*(Y`2)+D]|`2)^2) by EUCLID:56
     .= sqrt((A*(X`1)+B-(A*(Y`1)+B))^2 +
     (|[A*(X`1)+B,A*(X`2)+D]|`2-|[A*(Y`1)+B,A*(Y`2)+D]|`2)^2) by EUCLID:56
     .= sqrt((A*(X`1)+B-(A*(Y`1)+B))^2 +
     (A*(X`2)+D-|[A*(Y`1)+B,A*(Y`2)+D]|`2)^2) by EUCLID:56
     .= sqrt((A*(X`1)+B-(A*(Y`1)+B))^2 + (A*(X`2)+D-(A*(Y`2)+D))^2)
     by EUCLID:56
     .= sqrt((A*(X`1-Y`1))^2 + (A*(X`2-Y`2))^2)
     .= sqrt(A^2*(X`1-Y`1)^2 + (A*(X`2-Y`2))^2) by SQUARE_1:68
     .= sqrt(A^2*(X`1-Y`1)^2 + A^2*(X`2-Y`2)^2) by SQUARE_1:68
     .= sqrt(A^2*((X`1-Y`1)^2 + (X`2-Y`2)^2))
     .= sqrt(A^2)*sqrt((X`1-Y`1)^2 + (X`2-Y`2)^2) by SQUARE_1:97,s,s'
     .= sqrt(A^2)*dist(X,Y) by GOBRD14:9;
    sqrt(A^2)>=0 by s,SQUARE_1:def 4;
    hence dist(AffineMap(A,B,A,D).a,AffineMap(A,B,A,D).b) >= dist(x,y)
    by d,d1,d2,XREAL_1:66;
  end;

theorem roz:
  for A being Real st 0<=A & A<2*PI &
  p1,p2 realize-max-dist-in P holds
  (Rotate A).p1,(Rotate A).p2
  realize-max-dist-in (Rotate A).:P
  proof
    set C=P;
    let A be Real such that
    A: 0<=A & A<2*PI;
    assume p1,p2 realize-max-dist-in C; then
    p: p1 in C & p2 in C & for x, y being Point of TOP-REAL 2 st
    x in C & y in C holds dist(p1,p2) >= dist(x,y) by defr;
    dom Rotate A = the carrier of TOP-REAL 2 by FUNCT_2:def 1;
    hence (Rotate A).p1 in (Rotate A).:C & (Rotate A).p2 in (Rotate A).:C
    by FUNCT_1:def 12,p;
    let x, y be Point of TOP-REAL 2 such that
    x: x in (Rotate A).:C & y in (Rotate A).:C;
    consider xx being set such that
    xx: xx in dom Rotate A & xx in C & (Rotate A).xx=x by FUNCT_1:def 12,x;
    reconsider xx as Point of TOP-REAL 2 by xx;
    consider yy being set such that
    yy: yy in dom Rotate A & yy in C & (Rotate A).yy=y by FUNCT_1:def 12,x;
    reconsider yy as Point of TOP-REAL 2 by yy;
    T: TOP-REAL 2=TopSpaceMetr Euclid 2 by EUCLID:def 8;
    t: TopSpaceMetr Euclid 2 =
    TopStruct (#the carrier of Euclid 2,Family_open_set Euclid 2#)
    by PCOMPS_1:def 6;
    reconsider f=Rotate A as Function of
    TopSpaceMetr Euclid 2, TopSpaceMetr Euclid 2 by T;
    f is isometric by ri,A; then
    consider g being isometric Function of Euclid 2,Euclid 2 such that
    g: f=g by iso;
    reconsider p1'=p1,p2'=p2,xx'=xx,yy'=yy as Point of Euclid 2 by T,t;
    dist(p1,p2) >= dist(xx,yy) by xx,yy,p; then
    dist(p1',p2') >= dist(xx,yy) by GOBRD14:def 1; then
    dist(p1',p2') >= dist(xx',yy') by GOBRD14:def 1; then
    dist(g.p1',g.p2') >= dist(xx',yy') by VECTMETR:def 3; then
    dist(g.p1',g.p2') >= dist(g.xx',g.yy') by VECTMETR:def 3; then
    dist((Rotate A).p1,(Rotate A).p2) >= dist(g.xx',g.yy')
    by g,GOBRD14:def 1;
    hence dist((Rotate A).p1,(Rotate A).p2) >= dist(x,y)
    by g,xx,yy,GOBRD14:def 1;
  end;

theorem lemr:
  for z being complex number, r being Real holds
  Rotate(z,-r) = Rotate(z,2*PI-r)
  proof
    let z be complex number, r be Real;
    thus Rotate(z,-r) = [* |.z.|*cos (-r+Arg z), |.z.|*sin (-r+Arg z) *]
    by COMPLEX2:def 4
    .= [* |.z.|*cos (2*PI*1+(-r+Arg z)), |.z.|*sin (-r+Arg z) *] by COMPLEX2:10
    .= [* |.z.|*cos (2*PI-r+Arg z), |.z.|*sin (2*PI-r+Arg z) *] by COMPLEX2:9
    .= Rotate(z,2*PI-r) by COMPLEX2:def 4;
  end;

theorem lemrf:
  for r being Real holds Rotate(-r) = Rotate(2*PI-r)
  proof
    let r be Real;
    now
      let p be Point of TOP-REAL 2;
      thus (Rotate(2*PI-r)).p =
      |[Re Rotate(p`1+(p`2)*<i>,2*PI-r),Im Rotate(p`1+(p`2)*<i>,2*PI-r)]|
      by rot
      .= |[Re Rotate(p`1+(p`2)*<i>,-r),Im Rotate(p`1+(p`2)*<i>,2*PI-r)]|
      by lemr
      .= |[Re Rotate(p`1+(p`2)*<i>,-r),Im Rotate(p`1+(p`2)*<i>,-r)]| by lemr;
    end;
    hence Rotate(-r) = Rotate(2*PI-r) by rot;
  end;

theorem
  ex f being Homeomorphism of TOP-REAL 2 st
  |[-1,0]|,|[1,0]| realize-max-dist-in f.:C
  proof
    T: TOP-REAL 2=TopSpaceMetr(Euclid 2) by EUCLID:def 8;
    consider p1,p2 such that
    p: p1,p2 realize-max-dist-in C by an1;
    consider x,y being Point of TOP-REAL 2 such that
    x: x<>y & x in C & y in C by TOPREAL2:4;
    dist(x,y) > 0 & dist(p1,p2)>=dist(x,y) by defr,p,JORDAN1K:22,x; then
    pp: p1<>p2 by GOBRD14:10;
    reconsider g=AffineMap(1,-p1`1,1,-p1`2) as
    being_homeomorphism Function of TOP-REAL 2,TOP-REAL 2 by JGRAPH_7:50;
    set D=g.:C,q1=g.p1,q2=g.p2;
    q: q1,q2 realize-max-dist-in D by laf,p;
    q1: q1 = |[1 * (p1`1)+-p1`1,1 * (p1`2)+-p1`2]| by JGRAPH_2:def 2
      .= |[0,0]|;
    set arg=Arg(q2`1+q2`2 * <i>);
    set h=Rotate(-arg);
    per cases;
    suppose arg = 0; then
      Re (q2`1+q2`2 * <i>) >= 0 & Im (q2`1+q2`2 * <i>) = 0 by COMPLEX2:34; then
      q0: q2`1>=0 & q2`2 = 0 by COMPLEX1:28;
      now
        assume q2: q2`1=0;
        dom g = the carrier of TOP-REAL 2 by FUNCT_2:def 1; then
        q1<>q2 by FUNCT_1:def 8,pp;
        hence contradiction by q2,q0,q1,EUCLID:57;
      end; then
      qu: q2`1>0 & q2`2 = 0 by q0; then
      2/(q2`1) > 0 by XREAL_1:141; then
      reconsider j=AffineMap(2/(q2`1),-1,2/(q2`1),0) as
      being_homeomorphism Function of TOP-REAL 2,TOP-REAL 2 by JGRAPH_7:50;
      set E=j.:D,r1=j.q1,r2=j.q2;
      ra: r1=|[2/(q2`1)*q1`1+-1,2/(q2`1)*q1`2+0]| by JGRAPH_2:def 2
      .= |[2/(q2`1)*0+-1,2/(q2`1)*q1`2+0]| by q1,EUCLID:56
      .= a by q1,EUCLID:56;
      rb: r2=|[2/(q2`1)*q2`1+-1,2/(q2`1)*q2`2+0]| by JGRAPH_2:def 2
      .=|[2+-1,2/(q2`1)*q2`2+0]| by XCMPLX_1:88,qu
      .=b by qu;
      rr: r1,r2 realize-max-dist-in E by laf,q;
      take f=j*g;
      thus f is being_homeomorphism by TOPS_2:71;
      thus a,b realize-max-dist-in (f.:C) by rr,ra,rb,RELAT_1:159;
    end;
    suppose arg<>0;
      then 0<arg & arg < 2*PI by COMPTRIG:52; then
      co: 0<2*PI-arg & 2*PI-arg<2*PI by XREAL_1:52,46;
      hr: h=Rotate(2*PI-arg) by lemrf; then
      reconsider h0=h as isometric Function of
      TopSpaceMetr Euclid 2, TopSpaceMetr Euclid 2 by ri,T,co;
      h0 is being_homeomorphism; then
      reconsider h as being_homeomorphism Function
 of TOP-REAL 2,TOP-REAL 2 by T;
      set F=h.:D,s1=h.q1,s2=h.q2;
      reconsider z=0 as Element of COMPLEX by XCMPLX_0:def 2;
      why: Rotate(z,-arg) = 0 by COMPLEX2:66;
      s1: s1 = |[Re Rotate(|[0,0]|`1+(|[0,0]|`2)*<i>,-arg),
                 Im Rotate(|[0,0]|`1+(|[0,0]|`2)*<i>,-arg)]| by q1,rot
      .= |[Re Rotate(0+(|[0,0]|`2) * <i>,-arg),
           Im Rotate(|[0,0]|`1+(|[0,0]|`2)*<i>,-arg)]| by EUCLID:56
      .= |[Re Rotate(0+0 * <i>,-arg),
           Im Rotate(|[0,0]|`1+(|[0,0]|`2)*<i>,-arg)]| by EUCLID:56
      .= |[Re Rotate(0+0 * <i>,-arg),
           Im Rotate(0+(|[0,0]|`2)*<i>,-arg)]| by EUCLID:56
      .= |[0,0]| by COMPLEX1:12,why,EUCLID:56;
      reconsider qq=q2`1+(q2`2)*<i> as Element of COMPLEX by XCMPLX_0:def 2;
      whr: Rotate(qq,-arg) = [*|.(q2`1+(q2`2)*<i>).|,0 *] by COMPLEX2:69;
      s2 = |[Re [*|.(q2`1+(q2`2)*<i>).|,0 *],Im [*|.(q2`1+(q2`2)*<i>).|,0 *]]|
      by whr,rot
      .= |[Re (|.(q2`1+(q2`2)*<i>).|+0 * <i>),Im [*|.(q2`1+(q2`2)*<i>).|,0 *]]|
      by COMPLEX1:27
      .= |[Re (|.(q2`1+(q2`2)*<i>).|+0 * <i>),
      Im (|.(q2`1+(q2`2)*<i>).|+0 * <i>)]| by COMPLEX1:27
      .= |[|.(q2`1+(q2`2)*<i>).|,Im (|.(q2`1+(q2`2)*<i>).|+0 * <i>)]|
      by COMPLEX1:28
      .= |[|.(q2`1+(q2`2)*<i>).|,0]| by COMPLEX1:28;
      then s2`1 = |.(q2`1+(q2`2)*<i>).| & s2`2 = 0 by EUCLID:56; then
      q0: s2`1>=0 & s2`2 = 0 by COMPLEX1:132;
      now
        assume q2: s2`1=0;
        dom g = the carrier of TOP-REAL 2 by FUNCT_2:def 1; then
        qq: q1<>q2 by FUNCT_1:def 8,pp;
        dom h = the carrier of TOP-REAL 2 by FUNCT_2:def 1; then
        s1<>s2 by FUNCT_1:def 8,qq;
        hence contradiction by q2,q0,s1,EUCLID:57;
      end; then
      qu: s2`1>0 & s2`2 = 0 by q0; then
      2/(s2`1) > 0 by XREAL_1:141; then
      reconsider j=AffineMap(2/(s2`1),-1,2/(s2`1),0) as
      being_homeomorphism Function of TOP-REAL 2,TOP-REAL 2 by JGRAPH_7:50;
      set E=j.:F,r1=j.s1,r2=j.s2;
      take f=j*(h*g);
      h*g is being_homeomorphism by TOPS_2:71;
      hence f is being_homeomorphism by TOPS_2:71;
      ra: r1=|[2/(s2`1)*s1`1+-1,2/(s2`1)*s1`2+0]| by JGRAPH_2:def 2
      .= |[2/(s2`1)*0+-1,2/(s2`1)*s1`2+0]| by s1,EUCLID:56
      .= a by s1,EUCLID:56;
      rb: r2=|[2/(s2`1)*s2`1+-1,2/(s2`1)*s2`2+0]| by JGRAPH_2:def 2
      .=|[2+-1,2/(s2`1)*s2`2+0]| by XCMPLX_1:88,qu
      .=b by qu;
      (h*g).:C=F by RELAT_1:159; then
      ef: f.:C=E by RELAT_1:159;
      (Rotate(2*PI-arg)).q1,(Rotate(2*PI-arg)).q2
      realize-max-dist-in (Rotate(2*PI-arg)).:D by q,roz,co;
      hence a,b realize-max-dist-in (f.:C) by ra,rb,ef,laf,hr;
    end;
  end;

definition
  let T1,T2 be TopStruct;
  let f be Function of T1,T2;
::<DESC name="przekszta/lcenie domkni/ete" monograph="topology"/>
  attr f is closed means
   for A being Subset of T1 st A is closed holds f.:A is closed;
end;

::<DESC name="1.4.18 Twierdzenie o homeomorfizmie (cz/e/s/c): (a) iff (b)" monograph="topology"/>
theorem
  for X,Y being non empty TopSpace,
      f being continuous Function of X,Y st f is one-to-one onto
  holds f is_homeomorphism iff f is closed
  proof
    let X,Y be non empty TopSpace;
    let f be continuous Function of X,Y such that
AA: f is one-to-one onto;
    hereby
      assume
A0:   f is_homeomorphism;
      thus f is closed
      proof
        let A be Subset of X;
        thus thesis by A0,TOPS_2:72;
      end;
    end;
    assume
A1: for A being Subset of X st A is closed holds f.:A is closed;
A2: dom f = the carrier of X & rng f = the carrier of Y
    by AA,FUNCT_2:def 1,def 3;
A3: [#] X = the carrier of X & [#] Y = the carrier of Y by PRE_TOPC:def 3;
    now let A be Subset of X;
      assume f.:A is closed; then
      f"(f.:A) is closed by PRE_TOPC:def 12;
      hence A is closed by A2,AA,MSAFREE3:1;
    end;
    hence f is_homeomorphism by AA,A1,A2,A3,TOPS_2:72;
  end;

theorem Th01:
  for X being set, A being Subset of X holds
  A` = {} iff A = X
  proof
    let X be set, A be Subset of X;
    {} = {} X & [#]X = X & A`` = A by SUBSET_1:def 4;
    hence thesis by SUBSET_1:21,22;
  end;

theorem Lem4:
  for T1,T2 being non empty TopSpace,
      f being Function of T1,T2 st f is_homeomorphism
  for A being Subset of T1 st A is connected
  holds f.:A is connected
  proof
    let T1,T2 be non empty TopSpace;
    let f be Function of T1,T2;
    assume f is_homeomorphism; then
B1: f is continuous by TOPS_2:def 5;
    let A be Subset of T1;
    assume A is connected; then
    f.:A is connected by B1,TOPREAL5:5;
    hence f.:A is connected;
  end;

theorem Th1:
  for T1,T2 being non empty TopSpace,
      f being Function of T1,T2 st f is_homeomorphism
  for A being Subset of T1 st A is_a_component_of T1
  holds f.:A is_a_component_of T2
  proof
    let T1,T2 be non empty TopSpace,
        f be Function of T1,T2;
    assume
A0: f is_homeomorphism;
    let A be Subset of T1;
    assume that
A1: A is connected and
A2: for B being Subset of T1 st B is connected holds A c= B implies A = B;
    thus f.:A is connected by A0,A1,Lem4;
    let B be Subset of T2;
    [#] T2 = the carrier of T2 by PRE_TOPC:def 3; then
    f is one-to-one & rng f = the carrier of T2 &
    dom f = the carrier of T1 by A0,FUNCT_2:def 1,TOPS_2:def 5; then
A3: f.:(f"B) = B & f"(f.:A) = A by FUNCT_1:147,MSAFREE3:1;
    assume B is connected & f.:A c= B; then
    f"B is connected & A c= f"B by A0,A3,RELAT_1:178,JORDAN18:2;
    hence f.:A = B by A3,A2;
  end;

theorem Lem1:
  for T1,T2 being non empty TopSpace,
      f being Function of T1,T2,
      A being Subset of T1 holds
  f|A is Function of T1|A, T2|(f.:A)
  proof
    let T1,T2 be non empty TopSpace,
        f be Function of T1,T2,
        A be Subset of T1;
    [#](T1|A) = A & [#](T2|(f.:A)) = f.:A by PRE_TOPC:def 10; then
A2: the carrier of T1|A = A &
    the carrier of T2|(f.:A) = f.:A by PRE_TOPC:def 3;
    dom f = the carrier of T1 by FUNCT_2:def 1; then
    dom (f|A) = A & rng (f|A) = f.:A by RELAT_1:91,148;
    hence f|A is Function of T1|A, T2|(f.:A) by A2,FUNCT_2:4;
  end;

theorem Lem3:
  for T1,T2 being non empty TopSpace,
      f being Function of T1,T2 st f is continuous
  for A being Subset of T1,
      g being Function of T1|A, T2|(f.:A) st g = f|A
  holds g is continuous
  proof
    let T1,T2 be non empty TopSpace;
    let f be Function of T1,T2;
    assume
A0: f is continuous;
    let A be Subset of T1;
    let g be Function of T1|A, T2|(f.:A);
    assume
A1: g = f|A;
    [#](T1|A) = A & [#](T2|(f.:A)) = f.:A by PRE_TOPC:def 10; then
A2: the carrier of T1|A = A &
    the carrier of T2|(f.:A) = f.:A by PRE_TOPC:def 3;
A3: dom f = the carrier of T1 by FUNCT_2:def 1;
    per cases;
    suppose
A3:   A is empty; then
      f.:A = {} by RELAT_1:149; then
      T2|(f.:A) is empty & T1|A is empty by A2,A3,STRUCT_0:def 1;
      hence thesis by TIETZE:6;
    end;
    suppose
B1:   A is non empty; then
      f.:A <> {} by A3,RELAT_1:152; then
      reconsider S1 = T1|A, S2 = T2|(f.:A) as non empty TopSpace
      by A2,B1,STRUCT_0:def 1;
      f|A = f|(T1|A) by A2,TMAP_1:def 3; then
      g is continuous Function of S1, T2 & g is Function
 of S1,S2 by A0,A1,TMAP_1:68;
      hence g is continuous by JORDAN16:12;
    end;
  end;

theorem Th2:
  for T1,T2 being non empty TopSpace,
      f being Function of T1,T2 st f is_homeomorphism
  for A being Subset of T1,
      g being Function of T1|A, T2|(f.:A) st g = f|A
  holds g is_homeomorphism
  proof
    let T1,T2 be non empty TopSpace;
    let f be Function of T1,T2;
    assume that
A0: dom f = [#]T1 & rng f = [#]T2 & f is one-to-one and
A1: f is continuous & f" is continuous;
    let A be Subset of T1;
    let g be Function of T1|A, T2|(f.:A);
    assume
A2: g = f|A;
A6: [#](T1|A) = A & [#](T2|(f.:A)) = f.:A by PRE_TOPC:def 10;
A4: dom f = the carrier of T1 by FUNCT_2:def 1;
    hence
A7: dom g = [#](T1|A) & rng g = [#](T2|(f.:A)) by A6,A2,RELAT_1:91,148;
    thus
A8: g is one-to-one by A0,A2,FUNCT_1:84;
    thus g is continuous by A1,A2,Lem3;
A9: f qua Function" = f" & g qua Function" = g" by A0,A7,A8,TOPS_2:def 4; then
A10: g" = f"|(f.:A) by A0,A2,RFUNCT_2:40;
A11: f".:(f.:A) = f"(f.:A) by A0,A9,FUNCT_1:155 .= A by A0,A4,MSAFREE3:1; then
    reconsider h = f"|(f.:A) as Function of T2|(f.:A), T1|A by Lem1;
    thus thesis by A10,A1,A11,Lem3;
  end;

theorem Th3:
  for T1,T2 being non empty TopSpace,
      f being Function of T1,T2 st f is_homeomorphism
  for A,B being Subset of T1 st A is_a_component_of B
  holds f.:A is_a_component_of f.:B
  proof
    let T1,T2 be non empty TopSpace,
        f be Function of T1,T2 such that
A0: f is_homeomorphism;
    let A,B be Subset of T1;
    given A1 being Subset of T1|B such that
A1: A1 = A & A1 is_a_component_of T1|B;
    [#](T1|B) = B & [#](T2|(f.:B)) = f.:B by PRE_TOPC:def 10; then
A2: the carrier of T1|B = B &
    the carrier of T2|(f.:B) = f.:B by PRE_TOPC:def 3; then
    reconsider A2 = f.:A as Subset of T2|(f.:B) by A1,RELAT_1:156;
A3: dom f = the carrier of T1 by FUNCT_2:def 1;
    per cases;
    suppose
      B is empty; then
      f.:B = {} by RELAT_1:149; then
      A2 = {} & {} T2 = f.:B by A2,XBOOLE_1:3;
      hence thesis by JORDAN1K:6;
    end;
    suppose
B1:   B is non empty; then
      f.:B <> {} by A3,RELAT_1:152; then
      reconsider S1 = T1|B, S2 = T2|(f.:B) as non empty TopSpace
      by A2,B1,STRUCT_0:def 1;
      take A2; thus A2 = f.:A;
      reconsider fB = f|B as Function of S1,S2 by Lem1;
      fB.:A = A2 & fB is being_homeomorphism by A1,A2,A0,Th2,EXTENS_1:1;
      hence A2 is_a_component_of T2|(f.:B) by A1,Th1;
    end;
  end;

theorem
  for S being Subset of TOP-REAL 2,
      f being Homeomorphism of TOP-REAL 2 st
    S is Jordan holds f.:S is Jordan
  proof
    let S be Subset of TOP-REAL 2,
        f be Homeomorphism of TOP-REAL 2;
    assume
00: S` <> {};
    consider s being Element of S`;
    s in S` by 00; then
    reconsider s as Element of TOP-REAL 2;
04: not s in S by 00,SUBSET_1:54;
    given A1,A2 being Subset of TOP-REAL 2 such that
01: S` = A1 \/ A2 & A1 misses A2 & (Cl A1) \ A1 = (Cl A2) \ A2 and
02: A1 is_a_component_of S` & A2 is_a_component_of S`;
    hereby assume (f.:S)` = {}; then
      f.:S = the carrier of TOP-REAL 2 by Th01; then
      consider s' being Element of TOP-REAL 2 such that
05:   s' in S & f.s = f.s' by FUNCT_2:116;
      thus contradiction by 04,05,FUNCT_2:77;
    end;
    take B1 = f.:A1, B2 = f.:A2;
    f.:(A1 \/ A2) = B1 \/ B2 by RELAT_1:153;
    hence (f.:S)` = B1 \/ B2 by 01,JORDAN1K:5;
    thus B1 misses B2 by 01,FUNCT_1:125;
    thus (Cl B1) \ B1 = (f.:Cl A1)\B1 by TOPS_2:74
    .= f.:((Cl A2) \ A2) by 01,FUNCT_1:123
    .= (f.:Cl A2) \ B2 by FUNCT_1:123
    .= (Cl B2) \ B2 by TOPS_2:74;
    f.:(S`) = (f.:S)` by JORDAN1K:5;
    hence thesis by 02,Th3;
  end;
