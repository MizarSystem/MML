:: Homeomorphisms of {J}ordan Curves
::  by Adam Naumowicz and Grzegorz Bancerek
::
:: Received September 15, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies JORDAN24, ARYTM, PRE_TOPC, EUCLID, RELAT_1, ARYTM_1, TOPS_1,
      SUBSET_1, METRIC_1, BOOLE, TOPREAL2, CONNSP_1, TOPS_2, MCART_1, SQUARE_1,
      ARYTM_3, PCOMPS_1, FUNCT_1, JORDAN1, COMPLEX1, ORDINAL2, RELAT_2,
      WEIERSTR, VECTMETR, CONNSP_2, JGRAPH_2, FINSEQ_6, XCMPLX_0, COMPTRIG,
      SIN_COS, SGRAPH1, TOPGRP_1, INT_1, COMPTS_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, COMPLEX1, SQUARE_1, FUNCT_1,
      RELSET_1, PARTFUN1, FUNCT_2, NUMBERS, XCMPLX_0, XXREAL_0, XREAL_0,
      REAL_1, INT_1, STRUCT_0, PRE_TOPC, TOPS_1, TOPS_2, CONNSP_1,
      COMPTS_1, METRIC_1, PCOMPS_1,
      EUCLID, TOPREAL2, JORDAN1, TOPREAL6, WEIERSTR, VECTMETR, CONNSP_2,
      JGRAPH_2, COMPLEX2, COMPTRIG, SIN_COS, ARYTM_0, TOPGRP_1, TMAP_1;
 constructors ARYTM_0, REAL_1, SQUARE_1, SIN_COS, COMPTRIG, COMPLEX2, TOPS_1,
      CONNSP_1, TOPS_2, COMPTS_1, TMAP_1, TOPREAL2, JORDAN1, WEIERSTR,
      TOPGRP_1, VECTMETR, TOPREAL6, JGRAPH_2;
 registrations XBOOLE_0, RELSET_1, FUNCT_2, XCMPLX_0, XXREAL_0, XREAL_0,
      SQUARE_1, MEMBERED, STRUCT_0, PRE_TOPC, PCOMPS_1, BORSUK_1, EUCLID,
      TOPREAL2, TOPGRP_1, VECTMETR, INT_1, SIN_COS6;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM, REAL;
 definitions TARSKI, XBOOLE_0, FUNCT_2, COMPLEX2,
      CONNSP_1, BORSUK_1, VECTMETR, TOPS_2, SPRECT_1,
      TOPGRP_1, SQUARE_1, SUBSET_1, EUCLID, PCOMPS_1, STRUCT_0;
 theorems PRE_TOPC, EUCLID, SQUARE_1, XREAL_1, JORDAN1K, JGRAPH_7, TOPREAL2,
      TOPS_2, FUNCT_2, RELAT_1, FUNCT_1, XBOOLE_1, STRUCT_0, WEIERSTR,
      CONNSP_2, GOBOARD6, METRIC_1, VECTMETR, JGRAPH_2, COMPLEX2, XCMPLX_0,
      COMPLEX1, COMPTRIG, SIN_COS, XCMPLX_1, TMAP_1, JORDAN16, RFUNCT_2,
      TIETZE, XXREAL_0, XBOOLE_0, TOPREAL6, XREAL_0;
 schemes FUNCT_2;

begin

reserve p1, p2 for Point of TOP-REAL 2,
  C for Simple_closed_curve,
  P for Subset of TOP-REAL 2;

definition
  let n be Element of NAT, A be Subset of TOP-REAL n,
  a, b be Point of TOP-REAL n;
  pred a,b realize-max-dist-in A means
  :Def1:
  a in A & b in A & for x, y being Point of TOP-REAL n st x in A & y in A holds
  dist(a,b) >= dist(x,y);
end;
set rl = -1;
set rp = 1;
set a = |[rl,0]|;
set b = |[rp,0]|;

theorem Th1:
  for C being non empty compact Subset of TOP-REAL 2
  ex p1,p2 st p1,p2 realize-max-dist-in C
proof
  let C be non empty compact Subset of TOP-REAL 2;
  reconsider D=C as Subset of TopSpaceMetr Euclid 2;
  consider x1,x2 being Point of Euclid 2 such that
A1: x1 in D & x2 in D & dist(x1,x2) = max_dist_max(D,D) by WEIERSTR:39;
  reconsider a=x1,b=x2 as Point of TOP-REAL 2;
  take a,b;
  thus a in C & b in C by A1;
  let x, y be Point of TOP-REAL 2 such that
A2: x in C & y in C;
  reconsider x'=x,y'=y as Point of Euclid 2;
  dist(x',y') <= max_dist_max(D,D) by A2,WEIERSTR:40;
  then dist(x,y) <= max_dist_max(D,D) by TOPREAL6:def 1;
  hence dist(a,b) >= dist(x,y) by A1,TOPREAL6:def 1;
end;

definition
  let M be non empty MetrStruct;
  let f be Function of TopSpaceMetr M, TopSpaceMetr M;
  attr f is isometric means
  :Def2:
  ex g being isometric Function of M,M st g=f;
end;

registration
  let M be non empty MetrStruct;
  cluster isometric Function of TopSpaceMetr M, TopSpaceMetr M;
  existence
  proof
    consider f being isometric Function of M,M;
    reconsider f as Function of TopSpaceMetr M,TopSpaceMetr M;
    take f;
    thus thesis by Def2;
  end;
end;

registration
  let M be non empty MetrSpace;
  cluster isometric -> continuous Function of TopSpaceMetr M, TopSpaceMetr M;
  coherence
  proof
    let f be Function of TopSpaceMetr M, TopSpaceMetr M;
    assume f is isometric;
    then consider g being isometric Function of M,M such that
A1: g=f by Def2;
    let W be Point of TopSpaceMetr M;
    let G be a_neighborhood of f.W;
    reconsider fw=f.W,w=W as Point of M;
    f.W in Int G by CONNSP_2:def 1;
    then consider r being real number such that
A2: r > 0 & Ball(fw,r) c= G by GOBOARD6:7;
    reconsider H=Ball(w,r) as a_neighborhood of W by A2,GOBOARD6:94;
    take H;
    thus f.:H c= G
    proof
      let a be set;
      assume a in f.:H;
      then consider b being set such that
A3:   b in dom f & b in H & f.b = a by FUNCT_1:def 12;
      reconsider b as Point of TopSpaceMetr M by A3;
      reconsider b'=b as Point of M;
      dist(b',w) < r by A3,METRIC_1:12;
      then dist(g.b',fw) < r by A1,VECTMETR:def 3;
      then a in Ball(fw,r) by A1,A3,METRIC_1:12;
      hence a in G by A2;
    end;
  end;
end;

registration
  let M be non empty MetrSpace;
  cluster isometric ->
    being_homeomorphism Function of TopSpaceMetr M, TopSpaceMetr M;
  coherence
  proof
    let f be Function of TopSpaceMetr M, TopSpaceMetr M;
    assume
A1: f is isometric;
    then consider g being isometric Function of M,M such that
A2: g=f by Def2;
    thus dom f = [#]TopSpaceMetr M by FUNCT_2:def 1;
    thus rng f = [#]TopSpaceMetr M by A2,VECTMETR:def 3;
    thus f is one-to-one by A2;
    reconsider f1=f as isometric Function
    of TopSpaceMetr M,TopSpaceMetr M by A1;
    f1 is continuous;
    hence f is continuous;
A3: g" is isometric Function of M,M by VECTMETR:3;
    f" is isometric Function of TopSpaceMetr M,TopSpaceMetr M by A2,A3,Def2;
    hence f" is continuous;
  end;
end;

definition
  let a be Real;
  func Rotate(a) -> Function of TOP-REAL 2,TOP-REAL 2 means
  :Def3:
  for p being Point of TOP-REAL 2 holds
  it.p = |[Re Rotate(p`1+(p`2)*<i>,a),Im Rotate(p`1+(p`2)*<i>,a)]|;
  existence
  proof
    deffunc F(Point of TOP-REAL 2) =
    |[Re Rotate($1`1+($1`2)*<i>,a),Im Rotate($1`1+($1`2)*<i>,a)]|;
    consider f being Function of TOP-REAL 2,TOP-REAL 2 such that
A1: for p being Point of TOP-REAL 2 holds f.p=F(p) from FUNCT_2:sch 4;
    take f;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let f,g be Function of TOP-REAL 2,TOP-REAL 2 such that
A2: for p being Point of TOP-REAL 2 holds
    f.p = |[Re Rotate(p`1+(p`2)*<i>,a),Im Rotate(p`1+(p`2)*<i>,a)]| and
A3: for p being Point of TOP-REAL 2 holds
    g.p = |[Re Rotate(p`1+(p`2)*<i>,a),Im Rotate(p`1+(p`2)*<i>,a)]|;
    now
      let p be Point of TOP-REAL 2;
      thus f.p = |[Re Rotate(p`1+(p`2)*<i>,a),Im Rotate(p`1+(p`2)*<i>,a)]|
      by A2
        .= g.p by A3;
    end;
    hence f=g by FUNCT_2:113;
  end;
end;

Lm1:
now
  let a be Real;
  let c be complex number;
  let i be integer number;
  cos (a+Arg c) = cos (a+Arg c+2*PI*i) &
  sin (a+Arg c) = sin (a+Arg c+2*PI*i) by COMPLEX2:9,10;
  hence Rotate(c,a) = Rotate(c,a+2*PI*i);
end;

Lm2:
now
  let a be Real;
  let i be integer number;
  thus Rotate(a) = Rotate(a+2*PI*i)
  proof
    let p be Point of TOP-REAL 2;
    set q = p`1+(p`2)*<i>;
A1: Rotate(q,a) = Rotate(q,a+2*PI*i) by Lm1;
    thus (Rotate(a)).p = |[Re Rotate(q,a),Im Rotate(q,a)]| by Def3
    .= (Rotate(a+2*PI*i)).p by A1,Def3;
  end;
end;

theorem Th2:
  for a being Real
  for f being Function of TopSpaceMetr Euclid 2, TopSpaceMetr Euclid 2
  st f = Rotate a holds f is isometric
proof
  let a be Real;
  consider A being real number such that
B1: A = 2*PI*(-[\a/(2*PI)/])+a and
A1: 0 <= A & A < 2*PI by COMPLEX2:2;
  reconsider A as Real by XREAL_0:def 1;
  let f be Function of TopSpaceMetr Euclid 2, TopSpaceMetr Euclid 2 such that
A2: f = Rotate a;
B3: Rotate A = Rotate a by B1,Lm2;
  reconsider g=f as Function of Euclid 2,Euclid 2;
  g is isometric
  proof
    thus rng g = the carrier of Euclid 2
    proof
      thus rng g c= the carrier of Euclid 2;
      let o be set;
      assume o in the carrier of Euclid 2;
      then reconsider p=o as Point of TOP-REAL 2;
      set pz=p`1+(p`2)*<i>;
      reconsider pz as Element of COMPLEX by XCMPLX_0:def 2;
      set arg=Arg pz;
      per cases;
      suppose
A3:     pz<>0;
        per cases;
        suppose
A4:       A <= arg;
          set qz=Rotate(pz,-A);
A5:       qz<>0 by A3,COMPLEX2:66;
A6:       0<=arg-A by A4,XREAL_1:50;
          arg-A <= arg & arg < 2*PI by A1,COMPTRIG:52,XREAL_1:45;
          then
A7:       -A+arg < 2*PI by XXREAL_0:2;
A8:       |.Rotate(pz,-A).| = |.pz.| by COMPLEX2:67;
          then Arg qz = -A+arg by A5,A6,A7,COMPLEX2:21;
          then
A9:       Rotate(qz,A) = pz by A8,COMPLEX2:19;
          set q=|[Re qz,Im qz]|;
A10:      dom g = the carrier of Euclid 2 by FUNCT_2:def 1;
          g.q = |[Re Rotate(q`1+(q`2)*<i>,A),Im Rotate(q`1+(q`2)*<i>,A)]|
          by A2,B3,Def3
            .= |[Re Rotate(Re qz+(q`2)*<i>,A),Im Rotate(q`1+(q`2)*<i>,A)]|
          by EUCLID:56
            .= |[Re Rotate(Re qz+(Im qz)*<i>,A),Im Rotate(q`1+(q`2)*<i>,A)]|
          by EUCLID:56
            .= |[Re Rotate(Re qz+(Im qz)*<i>,A),Im Rotate(Re qz+(q`2)*<i>,A)]|
          by EUCLID:56
            .=|[Re Rotate(Re qz+(Im qz)*<i>,A),Im Rotate(Re qz+(Im qz)*<i>,A)]|
          by EUCLID:56
            .= |[Re Rotate(qz,A),Im Rotate(Re qz+(Im qz)*<i>,A)]|
          by COMPLEX1:29
            .= |[Re pz,Im pz]| by A9,COMPLEX1:29
            .= |[p`1,Im pz]| by COMPLEX1:28
            .= |[p`1,p`2]| by COMPLEX1:28
            .= p by EUCLID:57;
          hence o in rng g by A10,FUNCT_1:def 5;
        end;
        suppose
A11:      A > arg;
          set qz=Rotate(pz,-A);
A12:      qz<>0 by A3,COMPLEX2:66;
          0<= 2*PI-A & arg >= 0 by A1,COMPTRIG:52,XREAL_1:50;
          then
A13:      0<=2*PI-A+arg;
          arg-A < 0 by A11,XREAL_1:51;
          then
A14:      2*PI+(arg-A) < 2*PI by XREAL_1:32;
A15:      |.Rotate(pz,-A).| = |.pz.| by COMPLEX2:67;
          then qz=[* |.qz.|*cos (2*PI*1+(-A+arg)), |.qz.|*sin (-A+arg) *]
          by COMPLEX2:10;
          then qz=[* |.qz.|*cos (2*PI+(arg-A)), |.qz.|*sin (2*PI*1+(-A+arg))
          *] by COMPLEX2:9;
          then Arg qz = 2*PI+(arg-A) by A12,A13,A14,COMPLEX2:21;
          then Rotate(qz,A) = [* |.qz.|*cos arg, |.qz.|*sin (2*PI*1+arg) *]
          by COMPLEX2:10;
          then Rotate(qz,A) = [* |.qz.|*cos arg, |.qz.|*sin arg *] by
COMPLEX2:9;
          then
A16:      Rotate(qz,A) = pz by A15,COMPLEX2:19;
          set q=|[Re qz,Im qz]|;
A17:      dom g = the carrier of Euclid 2 by FUNCT_2:def 1;
          g.q = |[Re Rotate(q`1+(q`2)*<i>,A),Im Rotate(q`1+(q`2)*<i>,A)]|
          by A2,B3,Def3
            .= |[Re Rotate(Re qz+(q`2)*<i>,A),Im Rotate(q`1+(q`2)*<i>,A)]|
          by EUCLID:56
            .= |[Re Rotate(Re qz+(Im qz)*<i>,A),Im Rotate(q`1+(q`2)*<i>,A)]|
          by EUCLID:56
            .= |[Re Rotate(Re qz+(Im qz)*<i>,A),Im Rotate(Re qz+(q`2)*<i>,A)]|
          by EUCLID:56
            .=|[Re Rotate(Re qz+(Im qz)*<i>,A),Im Rotate(Re qz+(Im qz)*<i>,A)]|
          by EUCLID:56
            .=|[Re Rotate(qz,A),Im Rotate(Re qz+(Im qz)*<i>,A)]| by COMPLEX1:29
            .= |[Re pz,Im pz]| by A16,COMPLEX1:29
            .= |[p`1,Im pz]| by COMPLEX1:28
            .= |[p`1,p`2]| by COMPLEX1:28
            .= p by EUCLID:57;
          hence o in rng g by A17,FUNCT_1:def 5;
        end;
      end;
      suppose pz=0;
        then
A18:    p`1 = 0 & p`2 = 0 by COMPLEX1:12,28;
        set q=|[0,0]|;
A19:    dom g = the carrier of Euclid 2 by FUNCT_2:def 1;
        reconsider z=0 as Element of COMPLEX by XCMPLX_0:def 2;
        g.q = |[Re Rotate(q`1+(q`2)*<i>,a),Im Rotate(q`1+(q`2)*<i>,a)]|
        by A2,Def3
          .= |[Re Rotate(z+(q`2)*<i>,a),Im Rotate(q`1+(q`2)*<i>,a)]|
        by EUCLID:56
          .= |[Re Rotate(z+z*<i>,a),Im Rotate(q`1+(q`2)*<i>,a)]| by EUCLID:56
          .= |[Re Rotate(z+z*<i>,a),Im Rotate(z+(q`2)*<i>,a)]| by EUCLID:56
          .= |[Re Rotate(z,a),Im Rotate(z,a)]| by EUCLID:56
          .= |[Re z,Im Rotate(z,a)]| by COMPLEX2:66
          .= |[Re z,Im z]| by COMPLEX2:66
          .= p by A18,COMPLEX1:12,EUCLID:57;
        hence o in rng g by A19,FUNCT_1:def 5;
      end;
    end;
    let X,Y be Point of Euclid 2;
    reconsider x=X,y=Y,gx=g.X,gy=g.Y as Point of TOP-REAL 2;
A20: |[Re Rotate((x`1)+(x`2)*<i>,a),Im Rotate((x`1)+(x`2)*<i>,a)]|`1=
    Re Rotate((x`1)+(x`2)*<i>,a) &
    |[Re Rotate((x`1)+(x`2)*<i>,a),Im Rotate((x`1)+(x`2)*<i>,a)]|`2=
    Im Rotate((x`1)+(x`2)*<i>,a) by EUCLID:56;
A21: |[Re Rotate((y`1)+(y`2)*<i>,a),Im Rotate((y`1)+(y`2)*<i>,a)]|`1=
    Re Rotate((y`1)+(y`2)*<i>,a) &
    |[Re Rotate((y`1)+(y`2)*<i>,a),Im Rotate((y`1)+(y`2)*<i>,a)]|`2=
    Im Rotate((y`1)+(y`2)*<i>,a) by EUCLID:56;
    reconsider xx=x`1+(x`2)*<i>,yy=y`1+(y`2)*<i> as Element of COMPLEX
    by XCMPLX_0:def 2;
A22: Re (x`1+(x`2)*<i>)=x`1 & Im (x`1+(x`2)*<i>)=x`2 by COMPLEX1:28;
A23: Re (y`1+(y`2)*<i>)=y`1 & Im (y`1+(y`2)*<i>)=y`2 by COMPLEX1:28;
A24: (sin a)^2+(cos a)^2 = 1 by SIN_COS:32;
    x=|[x`1,x`2]| & y=|[y`1,y`2]| by EUCLID:57;
    hence dist(X,Y) = sqrt ((x`1 - y`1)^2 + (x`2 - y`2)^2) by GOBOARD6:9
      .= sqrt(((x`1)*(x`1) -2*x`1*y`1 +
    (y`1)*(y`1))*((sin a)*(sin a)+(cos a)*(cos a)) +
    ((x`2)*(x`2) -2*x`2*y`2 + (y`2)*(y`2))*((sin a)^2+(cos a)^2)) by A24
      .= sqrt(((x`1*cos a-x`2*sin a)-(y`1*cos a-y`2*sin a))^2 +
    ((x`1*sin a+x`2*cos a)^2-2*(x`1*sin a+x`2*cos a)*(y`1*sin a+y`2*cos a)+
    (y`1*sin a+y`2*cos a)^2))
      .= sqrt(((Re Rotate(xx,a))-(y`1*cos a-y`2*sin a))^2 +
    ((x`1*sin a+x`2*cos a)-(y`1*sin a+y`2*cos a))^2) by A22,COMPLEX2:70
      .= sqrt(((Re Rotate(xx,a))-(y`1*cos a-y`2*sin a))^2 +
    ((Im Rotate(xx,a))-(y`1*sin a+y`2*cos a))^2) by A22,COMPLEX2:70
      .= sqrt(((Re Rotate(xx,a))-(Re Rotate(yy,a)))^2 +
    ((Im Rotate(xx,a))-(y`1*sin a+y`2*cos a))^2) by A23,COMPLEX2:70
      .= sqrt(((Re Rotate(xx,a))-(Re Rotate(yy,a)))^2 +
    ((Im Rotate(xx,a))-(Im Rotate(yy,a)))^2) by A23,COMPLEX2:70
      .= dist(|[Re Rotate((x`1)+(x`2)*<i>,a),Im Rotate((x`1)+(x`2)*<i>,a)]|,
    |[Re Rotate((y`1)+(y`2)*<i>,a),Im Rotate((y`1)+(y`2)*<i>,a)]|)
    by A20,A21,TOPREAL6:101
      .= dist(|[Re Rotate((x`1)+(x`2)*<i>,a),Im Rotate((x`1)+(x`2)*<i>,a)]|,gy)
    by A2,Def3
      .= dist(gx,gy) by A2,Def3
      .= dist(g.X,g.Y) by TOPREAL6:def 1;
  end;
  hence f is isometric by Def2;
end;

theorem Th3:
  for A,B,D being real number st p1,p2 realize-max-dist-in P holds
  AffineMap(A,B,A,D).p1,AffineMap(A,B,A,D).p2
  realize-max-dist-in AffineMap(A,B,A,D).:P
proof
  let A,B,D be real number;
  set a=p1,b=p2,C=P;
  assume
A1: a,b realize-max-dist-in C;
  then
A2: a in C & b in C & for x, y being Point of TOP-REAL 2 st
  x in C & y in C holds dist(a,b) >= dist(x,y) by Def1;
A3: dom AffineMap(A,B,A,D) = the carrier of TOP-REAL 2 by FUNCT_2:def 1;
  hence AffineMap(A,B,A,D).a in AffineMap(A,B,A,D).:C by A2,FUNCT_1:def 12;
  thus AffineMap(A,B,A,D).b in AffineMap(A,B,A,D).:C by A2,A3,FUNCT_1:def 12;
  let x, y be Point of TOP-REAL 2;
  assume x in AffineMap(A,B,A,D).:C;
  then consider X being set such that
A4: X in dom AffineMap(A,B,A,D) & X in C & AffineMap(A,B,A,D).X=x
  by FUNCT_1:def 12;
  reconsider X as Point of TOP-REAL 2 by A4;
  assume y in AffineMap(A,B,A,D).:C;
  then consider Y being set such that
A5: Y in dom AffineMap(A,B,A,D) & Y in C & AffineMap(A,B,A,D).Y=y
  by FUNCT_1:def 12;
  reconsider Y as Point of TOP-REAL 2 by A5;
A6: dist(a,b) >= dist(X,Y) by A1,A4,A5,Def1;
A7: A^2>=0 & (a`1-b`1)^2 >=0 & (a`2-b`2)^2 >=0 &
  (X`1-Y`1)^2>=0 & (X`2-Y`2)^2>=0 by XREAL_1:65;
A9: dist(AffineMap(A,B,A,D).a,AffineMap(A,B,A,D).b)
  = dist(|[A*(a`1)+B,A*(a`2)+D]|,AffineMap(A,B,A,D).b) by JGRAPH_2:def 2
    .= dist(|[A*(a`1)+B,A*(a`2)+D]|,|[A*(b`1)+B,A*(b`2)+D]|) by JGRAPH_2:def 2
    .= sqrt((|[A*(a`1)+B,A*(a`2)+D]|`1-|[A*(b`1)+B,A*(b`2)+D]|`1)^2 +
  (|[A*(a`1)+B,A*(a`2)+D]|`2-|[A*(b`1)+B,A*(b`2)+D]|`2)^2) by TOPREAL6:101
    .= sqrt((A*(a`1)+B-|[A*(b`1)+B,A*(b`2)+D]|`1)^2 +
  (|[A*(a`1)+B,A*(a`2)+D]|`2-|[A*(b`1)+B,A*(b`2)+D]|`2)^2) by EUCLID:56
    .= sqrt((A*(a`1)+B-(A*(b`1)+B))^2 +
  (|[A*(a`1)+B,A*(a`2)+D]|`2-|[A*(b`1)+B,A*(b`2)+D]|`2)^2) by EUCLID:56
    .= sqrt((A*(a`1)+B-(A*(b`1)+B))^2 +
  (A*(a`2)+D-|[A*(b`1)+B,A*(b`2)+D]|`2)^2) by EUCLID:56
    .= sqrt((A*(a`1)+B-(A*(b`1)+B))^2 + (A*(a`2)+D-(A*(b`2)+D))^2)
  by EUCLID:56
    .= sqrt(A^2*((a`1-b`1)^2 + (a`2-b`2)^2))
    .= sqrt(A^2)*sqrt((a`1-b`1)^2 + (a`2-b`2)^2) by A7,SQUARE_1:97
    .= sqrt(A^2)*dist(a,b) by TOPREAL6:101;
A10: dist(x,y) =
  dist(|[A*(X`1)+B,A*(X`2)+D]|,AffineMap(A,B,A,D).Y) by A4,A5,JGRAPH_2:def 2
    .= dist(|[A*(X`1)+B,A*(X`2)+D]|,|[A*(Y`1)+B,A*(Y`2)+D]|) by JGRAPH_2:def 2
    .= sqrt((|[A*(X`1)+B,A*(X`2)+D]|`1-|[A*(Y`1)+B,A*(Y`2)+D]|`1)^2 +
  (|[A*(X`1)+B,A*(X`2)+D]|`2-|[A*(Y`1)+B,A*(Y`2)+D]|`2)^2) by TOPREAL6:101
    .= sqrt((A*(X`1)+B-|[A*(Y`1)+B,A*(Y`2)+D]|`1)^2 +
  (|[A*(X`1)+B,A*(X`2)+D]|`2-|[A*(Y`1)+B,A*(Y`2)+D]|`2)^2) by EUCLID:56
    .= sqrt((A*(X`1)+B-(A*(Y`1)+B))^2 +
  (|[A*(X`1)+B,A*(X`2)+D]|`2-|[A*(Y`1)+B,A*(Y`2)+D]|`2)^2) by EUCLID:56
    .= sqrt((A*(X`1)+B-(A*(Y`1)+B))^2 +
  (A*(X`2)+D-|[A*(Y`1)+B,A*(Y`2)+D]|`2)^2) by EUCLID:56
    .= sqrt((A*(X`1)+B-(A*(Y`1)+B))^2 + (A*(X`2)+D-(A*(Y`2)+D))^2)
  by EUCLID:56
    .= sqrt(A^2*((X`1-Y`1)^2 + (X`2-Y`2)^2))
    .= sqrt(A^2)*sqrt((X`1-Y`1)^2 + (X`2-Y`2)^2) by A7,SQUARE_1:97
    .= sqrt(A^2)*dist(X,Y) by TOPREAL6:101;
  sqrt(A^2)>=0 by A7,SQUARE_1:def 4;
  hence dist(AffineMap(A,B,A,D).a,AffineMap(A,B,A,D).b) >= dist(x,y)
  by A6,A9,A10,XREAL_1:66;
end;

theorem Th4:
  for A being Real st p1,p2 realize-max-dist-in P holds
  (Rotate A).p1,(Rotate A).p2 realize-max-dist-in (Rotate A).:P
proof
  set C=P;
  let A be Real;
  assume
A2: p1,p2 realize-max-dist-in C;
  then
A3: p1 in C & p2 in C & for x, y being Point of TOP-REAL 2 st
  x in C & y in C holds dist(p1,p2) >= dist(x,y) by Def1;
  dom Rotate A = the carrier of TOP-REAL 2 by FUNCT_2:def 1;
  hence (Rotate A).p1 in (Rotate A).:C & (Rotate A).p2 in (Rotate A).:C
  by A3,FUNCT_1:def 12;
  let x, y be Point of TOP-REAL 2 such that
A4: x in (Rotate A).:C & y in (Rotate A).:C;
  consider xx being set such that
A5: xx in dom Rotate A & xx in C & (Rotate A).xx=x by A4,FUNCT_1:def 12;
  reconsider xx as Point of TOP-REAL 2 by A5;
  consider yy being set such that
A6: yy in dom Rotate A & yy in C & (Rotate A).yy=y by A4,FUNCT_1:def 12;
  reconsider yy as Point of TOP-REAL 2 by A6;
  reconsider f=Rotate A as Function of
  TopSpaceMetr Euclid 2, TopSpaceMetr Euclid 2;
  f is isometric by Th2;
  then consider g being isometric Function of Euclid 2,Euclid 2 such that
A7: f=g by Def2;
  reconsider p1'=p1,p2'=p2,xx'=xx,yy'=yy as Point of Euclid 2;
  dist(p1,p2) >= dist(xx,yy) by A2,A5,A6,Def1;
  then dist(p1',p2') >= dist(xx,yy) by TOPREAL6:def 1;
  then dist(p1',p2') >= dist(xx',yy') by TOPREAL6:def 1;
  then dist(g.p1',g.p2') >= dist(xx',yy') by VECTMETR:def 3;
  then dist(g.p1',g.p2') >= dist(g.xx',g.yy') by VECTMETR:def 3;
  then dist((Rotate A).p1,(Rotate A).p2) >= dist(g.xx',g.yy')
  by A7,TOPREAL6:def 1;
  hence dist((Rotate A).p1,(Rotate A).p2) >= dist(x,y)
  by A5,A6,A7,TOPREAL6:def 1;
end;

theorem Th5:
  for z being complex number, r being Real holds
  Rotate(z,-r) = Rotate(z,2*PI-r)
proof
  let z be complex number, r be Real;
  thus Rotate(z,-r)
   = [* |.z.|*cos (2*PI*1+(-r+Arg z)), |.z.|*sin (-r+Arg z) *] by COMPLEX2:10
  .= Rotate(z,2*PI-r) by COMPLEX2:9;
end;

theorem Th6:
  for r being Real holds Rotate(-r) = Rotate(2*PI-r)
proof
  let r be Real;
  now
    let p be Point of TOP-REAL 2;
    thus (Rotate(2*PI-r)).p =
    |[Re Rotate(p`1+(p`2)*<i>,2*PI-r),Im Rotate(p`1+(p`2)*<i>,2*PI-r)]|
    by Def3
      .= |[Re Rotate(p`1+(p`2)*<i>,-r),Im Rotate(p`1+(p`2)*<i>,2*PI-r)]|
    by Th5
      .= |[Re Rotate(p`1+(p`2)*<i>,-r),Im Rotate(p`1+(p`2)*<i>,-r)]| by Th5;
  end;
  hence Rotate(-r) = Rotate(2*PI-r) by Def3;
end;

theorem
  ex f being Homeomorphism of TOP-REAL 2 st
  |[-1,0]|,|[1,0]| realize-max-dist-in f.:C
proof
  consider p1,p2 such that
A1: p1,p2 realize-max-dist-in C by Th1;
  consider x,y being Point of TOP-REAL 2 such that
A2: x<>y & x in C & y in C by TOPREAL2:4;
  dist(x,y) > 0 & dist(p1,p2)>=dist(x,y) by A1,A2,Def1,JORDAN1K:22;
  then
A3: p1<>p2 by TOPREAL6:102;
  reconsider g=AffineMap(1,-p1`1,1,-p1`2) as
  being_homeomorphism Function of TOP-REAL 2,TOP-REAL 2 by JGRAPH_7:50;
  set D=g.:C,q1=g.p1,q2=g.p2;
A4: q1,q2 realize-max-dist-in D by A1,Th3;
A5: q1 = |[1 * (p1`1)+-p1`1,1 * (p1`2)+-p1`2]| by JGRAPH_2:def 2
    .= |[0,0]|;
  set arg=Arg(q2`1+q2`2 * <i>);
  set h=Rotate(-arg);
A13: h=Rotate(2*PI-arg) by Th6;
    reconsider h0=h as isometric Function of
    TopSpaceMetr Euclid 2, TopSpaceMetr Euclid 2 by Th2;
    h0 is being_homeomorphism;
    then reconsider h as being_homeomorphism Function
    of TOP-REAL 2,TOP-REAL 2;
    set F=h.:D,s1=h.q1,s2=h.q2;
    reconsider z=0 as Element of COMPLEX by XCMPLX_0:def 2;
A14: Rotate(z,-arg) = 0 by COMPLEX2:66;
A15: s1 = |[Re Rotate(|[0,0]|`1+(|[0,0]|`2)*<i>,-arg),
    Im Rotate(|[0,0]|`1+(|[0,0]|`2)*<i>,-arg)]| by A5,Def3
      .= |[Re Rotate(0+(|[0,0]|`2) * <i>,-arg),
    Im Rotate(|[0,0]|`1+(|[0,0]|`2)*<i>,-arg)]| by EUCLID:56
      .= |[Re Rotate(0+0 * <i>,-arg),
    Im Rotate(|[0,0]|`1+(|[0,0]|`2)*<i>,-arg)]| by EUCLID:56
      .= |[Re Rotate(0+0 * <i>,-arg),
    Im Rotate(0+(|[0,0]|`2)*<i>,-arg)]| by EUCLID:56
      .= |[0,0]| by A14,COMPLEX1:12,EUCLID:56;
    reconsider qq=q2`1+(q2`2)*<i> as Element of COMPLEX by XCMPLX_0:def 2;
    Rotate(qq,-arg) = [*|.(q2`1+(q2`2)*<i>).|,0 *] by COMPLEX2:69;
    then
A16: s2 = |[Re [*|.(q2`1+(q2`2)*<i>).|,0 *],Im [*|.(q2`1+(q2`2)*<i>) .|,0 *]]|
    by Def3
      .= |[Re (|.(q2`1+(q2`2)*<i>).|+0 * <i>),Im [*|.(q2`1+(q2`2)*<i>).|,0 *]]|
    by COMPLEX1:27
      .= |[Re (|.(q2`1+(q2`2)*<i>).|+0 * <i>),
    Im (|.(q2`1+(q2`2)*<i>).|+0 * <i>)]| by COMPLEX1:27
      .= |[|.(q2`1+(q2`2)*<i>).|,Im (|.(q2`1+(q2`2)*<i>).|+0 * <i>)]|
    by COMPLEX1:28
      .= |[|.(q2`1+(q2`2)*<i>).|,0]| by COMPLEX1:28;
    then
A17: s2`1 = |.(q2`1+(q2`2)*<i>).| & s2`2 = 0 by EUCLID:56;
    then
A18: s2`1>=0 & s2`2 = 0 by COMPLEX1:132;
A19: now
      assume
A20:  s2`1=0;
      dom g = the carrier of TOP-REAL 2 by FUNCT_2:def 1;
      then
A21:  q1<>q2 by A3,FUNCT_1:def 8;
      dom h = the carrier of TOP-REAL 2 by FUNCT_2:def 1;
      then s1<>s2 by A21,FUNCT_1:def 8;
      hence contradiction by A15,A16,A20,EUCLID:56;
    end;
    then reconsider j=AffineMap(2/(s2`1),-1,2/(s2`1),0) as
    being_homeomorphism Function of TOP-REAL 2,TOP-REAL 2 by A18,JGRAPH_7:50;
    set E=j.:F,r1=j.s1,r2=j.s2;
    set f=j*(h*g);
    f is Homeomorphism of TOP-REAL 2
    proof
      h*g is being_homeomorphism by TOPS_2:71;
      hence f is being_homeomorphism by TOPS_2:71;
    end;
    then reconsider f as Homeomorphism of TOP-REAL 2;
    take f;
A22: r1=|[2/(s2`1)*s1`1+-1,2/(s2`1)*s1`2+0]| by JGRAPH_2:def 2
      .= |[2/(s2`1)*0+-1,2/(s2`1)*s1`2+0]| by A15,EUCLID:56
      .= a by A15,EUCLID:56;
A23: r2=|[2/(s2`1)*s2`1+-1,2/(s2`1)*s2`2+0]| by JGRAPH_2:def 2
      .=|[2+-1,2/(s2`1)*s2`2+0]| by A19,XCMPLX_1:88
      .=b by A17;
    (h*g).:C=F by RELAT_1:159;
    then
A24: f.:C=E by RELAT_1:159;
    (Rotate(2*PI-arg)).q1,(Rotate(2*PI-arg)).q2
    realize-max-dist-in (Rotate(2*PI-arg)).:D by A4,Th4;
    hence a,b realize-max-dist-in (f.:C) by A13,A22,A23,A24,Th3;
  end;

definition
  let T1,T2 be TopStruct;
  let f be Function of T1,T2;

::<DESC name="przekszta/lcenie domkni/ete" monograph="topology"/>
  attr f is closed means
  for A being Subset of T1 st A is closed holds f.:A is closed;
end;

::<DESC name="1.4.18 Twierdzenie o homeomorfizmie (cz/e/s/c): (a) iff (b)" monograph="topology"/>

theorem
  for X,Y being non empty TopSpace,
  f being continuous Function of X,Y st f is one-to-one onto
  holds f is_homeomorphism iff f is closed
proof
  let X,Y be non empty TopSpace;
  let f be continuous Function of X,Y such that
A1: f is one-to-one onto;
  hereby
    assume
A2: f is_homeomorphism;
    thus f is closed
    proof
      let A be Subset of X;
      thus thesis by A2,TOPS_2:72;
    end;
  end;
  assume
A3: for A being Subset of X st A is closed holds f.:A is closed;
A4: dom f = the carrier of X & rng f = the carrier of Y
  by A1,FUNCT_2:def 1,def 3;
A5: [#] X = the carrier of X & [#] Y = the carrier of Y;
  now
    let A be Subset of X;
    assume f.:A is closed;
    then f"(f.:A) is closed by PRE_TOPC:def 12;
    hence A is closed by A1,A4,FUNCT_1:164;
  end;
  hence f is_homeomorphism by A1,A3,A4,A5,TOPS_2:72;
end;

theorem Th9:
  for X being set, A being Subset of X holds A` = {} iff A = X
proof
  let X be set, A be Subset of X;
  {} = {} X & [#]X = X & A`` = A;
  hence thesis by XBOOLE_1:37;
end;

theorem
  for T1,T2 being non empty TopSpace,
  f being Function of T1,T2 st f is_homeomorphism
  for A being Subset of T1 st A is connected holds f.:A is connected
   by TOPS_2:75;

theorem Th11:
  for T1,T2 being non empty TopSpace,
  f being Function of T1,T2 st f is_homeomorphism
  for A being Subset of T1 st A is_a_component_of T1
  holds f.:A is_a_component_of T2
proof
  let T1,T2 be non empty TopSpace, f be Function of T1,T2;
  assume
A1: f is_homeomorphism;
  let A be Subset of T1;
  assume that
A2: A is connected and
A3: for B being Subset of T1 st B is connected holds A c= B implies A = B;
  thus f.:A is connected by A1,A2,TOPS_2:75;
  let B be Subset of T2;
  [#] T2 = the carrier of T2;
  then f is one-to-one & rng f = the carrier of T2 &
  dom f = the carrier of T1 by A1,FUNCT_2:def 1,TOPS_2:def 5;
  then
A4: f.:(f"B) = B & f"(f.:A) = A by FUNCT_1:147,164;
  assume B is connected & f.:A c= B;
  then f"B is connected & A c= f"B by A1,A4,RELAT_1:178,TOPS_2:76;
  hence f.:A = B by A3,A4;
end;

theorem Th12:
  for T1,T2 being non empty TopSpace, f being Function of T1,T2,
  A being Subset of T1 holds f|A is Function of T1|A, T2|(f.:A)
proof
  let T1,T2 be non empty TopSpace, f be Function of T1,T2, A be Subset of T1;
A1: [#](T1|A) = A & [#](T2|(f.:A)) = f.:A by PRE_TOPC:def 10;
  dom f = the carrier of T1 by FUNCT_2:def 1;
  then dom (f|A) = A & rng (f|A) = f.:A by RELAT_1:91,148;
  hence f|A is Function of T1|A, T2|(f.:A) by A1,FUNCT_2:4;
end;

theorem Th13:
  for T1,T2 being non empty TopSpace,
  f being Function of T1,T2 st f is continuous for A being Subset of T1,
  g being Function of T1|A, T2|(f.:A) st g = f|A holds g is continuous
proof
  let T1,T2 be non empty TopSpace;
  let f be Function of T1,T2;
  assume
A1: f is continuous;
  let A be Subset of T1;
  let g be Function of T1|A, T2|(f.:A);
  assume
A2: g = f|A;
A3: [#](T1|A) = A & [#](T2|(f.:A)) = f.:A by PRE_TOPC:def 10;
A4: dom f = the carrier of T1 by FUNCT_2:def 1;
  per cases;
  suppose A is empty;
    then T2|(f.:A) is empty & T1|A is empty by A3,RELAT_1:149;
    hence thesis by TIETZE:6;
  end;
  suppose
A6: A is non empty;
    then f.:A <> {} by A4,RELAT_1:152;
    then reconsider S1 = T1|A, S2 = T2|(f.:A) as non empty TopSpace
    by A6;
    f|A = f|(T1|A) by A3,TMAP_1:def 3;
    then g is continuous Function of S1, T2 & g is Function
    of S1,S2 by A1,A2,TMAP_1:68;
    hence g is continuous by JORDAN16:12;
  end;
end;

theorem Th14:
  for T1,T2 being non empty TopSpace,
  f being Function of T1,T2 st f is_homeomorphism for A being Subset of T1,
  g being Function of T1|A, T2|(f.:A) st g = f|A holds g is_homeomorphism
proof
  let T1,T2 be non empty TopSpace;
  let f be Function of T1,T2;
  assume that
A1: dom f = [#]T1 & rng f = [#]T2 & f is one-to-one and
A2: f is continuous & f" is continuous;
  let A be Subset of T1;
  let g be Function of T1|A, T2|(f.:A);
  assume
A3: g = f|A;
A4: [#](T1|A) = A & [#](T2|(f.:A)) = f.:A by PRE_TOPC:def 10;
  dom f = the carrier of T1 by FUNCT_2:def 1;
  hence
A5: dom g = [#](T1|A) & rng g = [#](T2|(f.:A)) by A3,A4,RELAT_1:91,148; thus
A6: g is one-to-one by A1,A3,FUNCT_1:84;
  thus g is continuous by A2,A3,Th13;
A7: f qua Function" = f" & g qua Function" = g" by A1,A5,A6,TOPS_2:def 4;
  then
A8: g" = f"|(f.:A) by A1,A3,RFUNCT_2:40;
A9: f".:(f.:A) = f"(f.:A) by A1,A7,FUNCT_1:155
    .= A by A1,FUNCT_1:164;
  thus thesis by A2,A8,A9,Th13;
end;

theorem Th15:
  for T1,T2 being non empty TopSpace,
  f being Function of T1,T2 st f is_homeomorphism
  for A,B being Subset of T1 st A is_a_component_of B
  holds f.:A is_a_component_of f.:B
proof
  let T1,T2 be non empty TopSpace, f be Function of T1,T2 such that
A1: f is_homeomorphism;
  let A,B be Subset of T1;
  given A1 being Subset of T1|B such that
A2: A1 = A & A1 is_a_component_of T1|B;
A3: [#](T1|B) = B & [#](T2|(f.:B)) = f.:B by PRE_TOPC:def 10;
  then reconsider A2 = f.:A as Subset of T2|(f.:B) by A2,RELAT_1:156;
A4: dom f = the carrier of T1 by FUNCT_2:def 1;
  per cases;
  suppose B is empty;
    then f.:B = {} by RELAT_1:149;
    then A2 = {} & {} T2 = f.:B by A3,XBOOLE_1:3;
    hence thesis by JORDAN1K:6;
  end;
  suppose
A5: B is non empty;
    then f.:B <> {} by A4,RELAT_1:152;
    then reconsider S1 = T1|B, S2 = T2|(f.:B) as non empty TopSpace
    by A5;
    take A2; thus A2 = f.:A;
    reconsider fB = f|B as Function of S1,S2 by Th12;
    fB.:A = A2 & fB is being_homeomorphism by A1,A2,A3,Th14,RELAT_1:162;
    hence A2 is_a_component_of T2|(f.:B) by A2,Th11;
  end;
end;

theorem
  for S being Subset of TOP-REAL 2, f being Homeomorphism of TOP-REAL 2 st
  S is Jordan holds f.:S is Jordan
proof
  let S be Subset of TOP-REAL 2, f be Homeomorphism of TOP-REAL 2;
  assume
A1: S` <> {};
  consider s being Element of S`;
  s in S` by A1;
  then reconsider s as Element of TOP-REAL 2;
A2: not s in S by A1,XBOOLE_0:def 5;
  given A1,A2 being Subset of TOP-REAL 2 such that
A3: S` = A1 \/ A2 & A1 misses A2 & (Cl A1) \ A1 = (Cl A2) \ A2 and
A4: A1 is_a_component_of S` & A2 is_a_component_of S`;
  hereby
    assume (f.:S)` = {};
    then f.:S = the carrier of TOP-REAL 2 by Th9;
    then consider s' being Element of TOP-REAL 2 such that
A5: s' in S & f.s = f.s' by FUNCT_2:116;
    thus contradiction by A2,A5,FUNCT_2:77;
  end;
  take B1 = f.:A1, B2 = f.:A2;
  f.:(A1 \/ A2) = B1 \/ B2 by RELAT_1:153;
  hence (f.:S)` = B1 \/ B2 by A3,JORDAN1K:5;
  thus B1 misses B2 by A3,FUNCT_1:125;
  thus (Cl B1) \ B1 = (f.:Cl A1)\B1 by TOPS_2:74
    .= f.:((Cl A2) \ A2) by A3,FUNCT_1:123
    .= (f.:Cl A2) \ B2 by FUNCT_1:123
    .= (Cl B2) \ B2 by TOPS_2:74;
  f.:(S`) = (f.:S)` by JORDAN1K:5;
  hence thesis by A4,Th15;
end;
