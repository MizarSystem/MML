:: Trigonometric Functions on Complex Space
::  by Takashi Mitsuishi , Noboru Endou and Keiji Ohkubo
::
:: Received October 10, 2002
:: Copyright (c) 2002 Association of Mizar Users

environ

 vocabularies RELAT_1, FINSEQ_4, ORDINAL2, COMPLEX1, SIN_COS, PREPOWER,
      SIN_COS2, SIN_COS3, FUNCT_1, ARYTM_1, ARYTM_3, XCMPLX_0, NAT_1;
 notations SUBSET_1, NUMBERS, XCMPLX_0, COMPLEX1, REAL_1, RELAT_1, NAT_1,
      NAT_D, FINSEQ_4, ARYTM_0, FUNCT_2, SEQ_1, COMSEQ_3, SIN_COS, SIN_COS2;
 constructors FUNCT_4, ARYTM_3, ARYTM_0, XXREAL_0, REAL_1, NAT_1, BINOP_2,
      SEQ_1, FINSEQ_4, COMSEQ_1, COMSEQ_3, SIN_COS, SIN_COS2, NAT_D;
 registrations RELSET_1, COMPLEX1, MEMBERED, ORDINAL1, XCMPLX_0, NAT_1;
 requirements NUMERALS, SUBSET, BOOLE, ARITHM;
 theorems COMPLEX1, SIN_COS, SIN_COS2, COMSEQ_3, FINSEQ_4, FUNCT_2, XCMPLX_1,
      ARYTM_0;
 schemes NAT_1, BINOP_2, FUNCT_2;

begin  :: Definitions of trigonometric functions

 reserve x,y for Element of REAL;
 reserve z,z1,z2 for Element of COMPLEX;
 reserve n for Element of NAT;

Lm1: [*2,0*] = 2+0*<i> by COMPLEX1:27;

definition
  func sin_C -> Function of COMPLEX, COMPLEX means :Def1:
  it.z = (exp(<i>*z)-(exp(-<i> * z)))/(2 * <i>);
  existence
   proof
 deffunc
  U(Element of COMPLEX) = (exp(<i>*$1)-(exp(-<i> * $1)))/([*2,0*] * <i>);
    ex f being Function of COMPLEX, COMPLEX st
       for z holds f.z = U(z) from FUNCT_2:sch 4;
    hence thesis by Lm1;
   end;
  uniqueness
   proof
 deffunc
  U(Element of COMPLEX) = (exp(<i>*$1)-(exp(-<i> * $1)))/([*2,0*] * <i>);
    for f1,f2 being Function of COMPLEX, COMPLEX st
      (for z holds f1.z = U(z)) &
      (for z holds f2.z = U(z))
       holds f1 = f2 from BINOP_2:sch 1;
    hence thesis by Lm1;
   end;
end;

definition
  func cos_C -> Function of COMPLEX,COMPLEX means :Def2:
  it.z = (exp(<i>*z) + exp(-<i>*z))/2;
  existence
   proof
 deffunc U(Element of COMPLEX) = (exp(<i>*$1) + exp(-<i>*$1))/[*2,0*];
     ex f being Function of COMPLEX, COMPLEX st
       for z holds f.z = U(z) from FUNCT_2:sch 4;
    hence thesis by Lm1;
   end;
  uniqueness
   proof
 deffunc U(Element of COMPLEX) = (exp(<i>*$1) + exp(-<i>*$1))/[*2,0*];
    for f1,f2 being Function of COMPLEX, COMPLEX st
      (for z holds f1.z = U(z)) &
      (for z holds f2.z = U(z))
       holds f1 = f2 from BINOP_2:sch 1;
    hence thesis by Lm1;
   end;
end;

definition
  func sinh_C -> Function of COMPLEX,COMPLEX means :Def3:
  it.z = (exp(z) - exp(-z))/2;
  existence
   proof
 deffunc U(Element of COMPLEX) = (exp($1) - exp(-$1))/[*2,0*];
     ex f being Function of COMPLEX, COMPLEX st
       for z holds f.z = U(z) from FUNCT_2:sch 4;
    hence thesis by Lm1;
   end;
  uniqueness
   proof
 deffunc U(Element of COMPLEX) = (exp($1) - exp(-$1))/[*2,0*];
     for f1,f2 being Function of COMPLEX, COMPLEX st
      (for z holds f1.z = U(z)) &
      (for z holds f2.z = U(z))
       holds f1 = f2 from BINOP_2:sch 1;
    hence thesis by Lm1;
   end;
end;

definition
  func cosh_C -> Function of COMPLEX,COMPLEX means :Def4:
  it.z = (exp(z) + exp(-z))/2;
  existence
   proof
 deffunc U(Element of COMPLEX) = (exp($1) + exp(-$1))/[*2,0*];
     ex f being Function of COMPLEX, COMPLEX st
       for z holds f.z = U(z) from FUNCT_2:sch 4;
    hence thesis by Lm1;
   end;
  uniqueness
   proof
 deffunc U(Element of COMPLEX) = (exp($1) + exp(-$1))/[*2,0*];
     for f1,f2 being Function of COMPLEX, COMPLEX st
      (for z holds f1.z = U(z)) &
      (for z holds f2.z = U(z))
       holds f1 = f2 from BINOP_2:sch 1;
    hence thesis by Lm1;
   end;
end;

Lm2: sin_C/.z = (exp(<i>*z)-(exp(-<i> * z)))/(2 * <i>)
proof
  dom sin_C = COMPLEX by FUNCT_2:def 1;
  then sin_C/.z = sin_C.z by FINSEQ_4:def 4
          .= (exp(<i>*z)-(exp(-<i> * z)))/(2 * <i>) by Def1;
  hence thesis;
end;

Lm3: cos_C/.z = (exp(<i>*z) + exp(-<i>*z))/2
proof
  dom cos_C = COMPLEX by FUNCT_2:def 1;
  then cos_C/.z = cos_C.z by FINSEQ_4:def 4
          .= (exp(<i>*z) + exp(-<i>*z))/2 by Def2;
  hence thesis;
end;

Lm4: sinh_C/.z = (exp(z) - exp(-z))/2
proof
  dom sinh_C = COMPLEX by FUNCT_2:def 1;
  then sinh_C/.z = sinh_C.z by FINSEQ_4:def 4
          .= (exp(z) - exp(-z))/2 by Def3;
  hence thesis;
end;

Lm5: cosh_C/.z = (exp(z) + exp(-z))/2
proof
  dom cosh_C = COMPLEX by FUNCT_2:def 1;
  then cosh_C/.z = cosh_C.z by FINSEQ_4:def 4
          .= (exp(z) + exp(-z))/2 by Def4;
  hence thesis;
end;

Lm6: 0c = [*0,0*] by ARYTM_0:def 7;
Lm7: 1r = [*1,0*] by ARYTM_0:def 7,COMPLEX1:def 7;

Lm8:
for z be Element of COMPLEX holds
exp(z)*exp(-z) = 1
proof
 let z be Element of COMPLEX;
 thus exp(z)*exp(-z) = exp(z + -z) by SIN_COS:24
 .= exp([*0,0*]) by ARYTM_0:def 7
 .= [*1,0*] by SIN_COS:54,56
 .= 1+0*<i> by COMPLEX1:27
 .= 1;
end;

begin  :: Properties of trigonometric functions on complex space

theorem
  for z being Element of COMPLEX holds
  (sin_C/.z)*(sin_C/.z) + (cos_C/.z)*(cos_C/.z) = 1
proof
 let z be Element of COMPLEX;
 set z1 = exp(<i>*z), z2 = exp(-<i>*z);
       (sin_C/.z)*(sin_C/.z) + (cos_C/.z)*(cos_C/.z)
  =(sin_C/.z)*(sin_C/.z) +
   (cos_C/.z)*((exp(<i>*z) + exp(-<i>*z))/2) by Lm3
 .=(sin_C/.z)*(sin_C/.z) + ((z1 + z2)/2)*((z1 + z2)/2) by Lm3
 .=(z1 - z2)/(2*<i>)*(sin_C/.z) +
   ((z1 + z2)/2)*((z1 + z2)/2) by Lm2
 .=((z1 - z2)/(2*<i>))*((z1 - z2)/(2*<i>)) +
   ((z1 + z2)/2)*((z1 + z2)/2) by Lm2
 .=((z1*z2 + z1*z2) + (z1*z2 + z1*z2))/4
 .=((1 + z1*exp(-(<i>*z))) + (z1*exp(-(<i>*z)) + z1*exp(-(<i>*z))))
   /4 by Lm8
 .=((1 + 1) + (z1*exp(-(<i>*z)) + z1*exp(-(<i>*z))))
   /4 by Lm8
 .=((1 + 1) + (z1*exp(-(<i>*z)) + 1))/4 by Lm8
 .=(2 + 2)/4 by Lm8;
 hence thesis;
end;

theorem Th2:
-sin_C/.z = sin_C/.(-z)
proof
 sin_C/.(-z) = (exp(<i>*(-z)) - exp(-<i>*(-z)))/(2*<i>) by Lm2
 .= -((exp(<i>*z) - exp(-<i>*z))/(2*<i>));
 hence thesis by Lm2;
end;

theorem Th3:
cos_C/.z = cos_C/.(-z)
proof
     cos_C/.(-z) = (exp(<i>*(-z)) + exp(-<i>*(-z)))/2 by Lm3
 .=(exp(-<i>*z) + exp(<i>*z))/2;
 hence thesis by Lm3;
end;

theorem Th4:
sin_C/.(z1+z2) = (sin_C/.z1)*(cos_C/.z2) + (cos_C/.z1)*(sin_C/.z2)
proof
 set e1 = exp(<i>*z1), e2 = exp(-<i>*z1), e3 = exp(<i>*z2), e4 = exp(-<i>*z2);
       (sin_C/.z1)*(cos_C/.z2) + (cos_C/.z1)*(sin_C/.z2)
  =((exp(<i>*z1) - exp(-<i>*z1))/(2*<i>))*(cos_C/.z2) +
   (cos_C/.z1)*(sin_C/.z2) by Lm2
 .=((exp(<i>*z1) - exp(-<i>*z1))/(2*<i>))*(cos_C/.z2) +
   (cos_C/.z1)*((exp(<i>*z2) - exp(-<i>*z2))/(2*<i>)) by Lm2
 .=((exp(<i>*z1) - exp(-<i>*z1))/(2*<i>))*
   ((exp(-<i>*z2) + exp(<i>*z2))/2) +
   (cos_C/.z1)*((exp(<i>*z2) - exp(-<i>*z2))/(2*<i>)) by Lm3
 .=((exp(<i>*z1) - exp(-<i>*z1))*(exp(-<i>*z2) + exp(<i>*z2)))
   /(2*<i>*2) + ((exp(-<i>*z1) + exp(<i>*z1))/2)*
   ((exp(<i>*z2) - exp(-<i>*z2))/(2*<i>)) by Lm3
 .=( e1*e3+e1*e3-(e1*e4+e2*e4 - (e1*e4-e2*e4)) )
   /(2*<i>*2)
 .=(Re(e1*e3)+Re(e1*e3)+(Im(e1*e3)+Im(e1*e3))*<i>-(e2*e4 + e2*e4))
   /(2*<i>*2) by COMPLEX1:def 9
 .=([*2*Re(e1*e3),2*Im(e1*e3)*]-(e2*e4 + e2*e4))
   /(2*<i>*2) by COMPLEX1:27
 .=([*Re(2*(e1*e3)),2*Im(e1*e3)*]-(e2*e4 + e2*e4))
   /(2*<i>*2) by COMSEQ_3:17
 .=([*Re(2*(e1*e3)),Im(2*(e1*e3))*]-(e2*e4 + e2*e4))
   /(2*<i>*2) by COMSEQ_3:17
 .=((Re(2*(e1*e3))+(Im(2*(e1*e3)))*<i>)-(e2*e4 + e2*e4))
   /(2*<i>*2) by COMPLEX1:27
 .=(2*(e1*e3)-(e2*e4 + e2*e4))
   /(2*<i>*2) by COMPLEX1:29
 .=(2*(e1*e3)-(Re(e2*e4)+Re(e2*e4)+(Im(e2*e4)+Im(e2*e4))*<i>))
   /(2*<i>*2) by COMPLEX1:def 9
 .=(2*(e1*e3)-[*2*Re(e2*e4),2*Im(e2*e4)*])
   /(2*<i>*2) by COMPLEX1:27
 .=(2*(e1*e3)-[*Re(2*(e2*e4)),2*Im(e2*e4)*])
   /(2*<i>*2) by COMSEQ_3:17
 .=(2*(e1*e3)-[*Re(2*(e2*e4)),Im(2*(e2*e4))*])
   /(2*<i>*2) by COMSEQ_3:17
 .=(2*(e1*e3)-(Re(2*(e2*e4))+(Im(2*(e2*e4)))*<i>))
   /(2*<i>*2) by COMPLEX1:27
 .=(2*(e1*e3)-2*(e2*e4))
   /(2*<i>*2) by COMPLEX1:29
 .=(e1*e3)/(2*<i>)
  -(2*(e2*e4))/(2*<i>*2)
 .=exp(<i>*z1+<i>*z2)/(2*<i>)-(e2*e4)/(2*<i>) by SIN_COS:24
 .=exp(<i>*(z1+z2))/(2*<i>)-exp(-<i>*z1+-<i>*z2)/(2*<i>)
   by SIN_COS:24
 .=( exp(<i>*(z1+z2))-exp(-<i>*(z1+z2)) )/(2*<i>);
 hence thesis by Lm2;
end;

theorem
 sin_C/.(z1-z2) = (sin_C/.z1)*(cos_C/.z2) - (cos_C/.z1)*(sin_C/.z2)
proof
     sin_C/.(z1-z2) = sin_C/.(z1 + -z2)
 .=(sin_C/.z1)*(cos_C/.(-z2)) + (cos_C/.z1)*(sin_C/.(-z2)) by Th4
 .=(sin_C/.z1)*(cos_C/.z2) + (cos_C/.z1)*(sin_C/.(-z2)) by Th3
 .=(sin_C/.z1)*(cos_C/.z2) + (cos_C/.z1)*(-sin_C/.z2) by Th2
 .=(sin_C/.z1)*(cos_C/.z2) + -(cos_C/.z1)*(sin_C/.z2);
 hence thesis;
end;

theorem Th6:
cos_C/.(z1+z2) = (cos_C/.z1)*(cos_C/.z2) - (sin_C/.z1)*(sin_C/.z2)
proof
 set e1 = exp(<i>*z1), e2 = exp(-<i>*z1), e3 = exp(<i>*z2), e4 = exp(-<i>*z2);
       (cos_C/.z1)*(cos_C/.z2) - (sin_C/.z1)*(sin_C/.z2)
  =(cos_C/.z1)*(cos_C/.z2) -
   ((exp(<i>*z1) - exp(-<i>*z1))/(2*<i>))*(sin_C/.z2) by Lm2
 .=(cos_C/.z1)*(cos_C/.z2) - ((exp(<i>*z1) - exp(-<i>*z1))/(2*<i>))
   *((exp(<i>*z2) - exp(-<i>*z2))/(2*<i>)) by Lm2
 .=(cos_C/.z1)*((exp(<i>*z2) + exp(-<i>*z2))/2) -
    ((exp(<i>*z1) - exp(-<i>*z1))/(2*<i>))
   *((exp(<i>*z2) - exp(-<i>*z2))/(2*<i>)) by Lm3
 .=((e1 + e2)/2)*((e3 + e4)/2) -
   ((e1 - e2)/(2*<i>))*((e3 - e4)/(2*<i>)) by Lm3
 .=( e1*e3+e1*e3 + (e2*e4+e2*e4) )/(2*2)
 .=(Re(e1*e3)+Re(e1*e3)+(Im(e1*e3)+Im(e1*e3))*<i>+(e2*e4 + e2*e4))
   /(2*2) by COMPLEX1:def 9
 .=([*2*Re(e1*e3),2*Im(e1*e3)*]+(e2*e4 + e2*e4))
   /(2*2) by COMPLEX1:27
 .=([*Re(2*(e1*e3)),2*Im(e1*e3)*]+(e2*e4 + e2*e4))
   /(2*2) by COMSEQ_3:17
 .=([*Re(2*(e1*e3)),Im(2*(e1*e3))*]+(e2*e4 + e2*e4))
   /(2*2) by COMSEQ_3:17
 .=((Re(2*(e1*e3))+(Im(2*(e1*e3)))*<i>)+(e2*e4 + e2*e4))
   /(2*2) by COMPLEX1:27
 .=(2*(e1*e3)+(e2*e4 + e2*e4))/(2*2) by COMPLEX1:29
 .=(2*(e1*e3)+(Re(e2*e4)+Re(e2*e4)+(Im(e2*e4)+Im(e2*e4))*<i>))
   /(2*2) by COMPLEX1:def 9
 .=(2*(e1*e3)+[*2*Re(e2*e4),2*Im(e2*e4)*])
   /(2*2) by COMPLEX1:27
 .=(2*(e1*e3)+[*Re(2*(e2*e4)),2*Im(e2*e4)*])
   /(2*2) by COMSEQ_3:17
 .=(2*(e1*e3)+[*Re(2*(e2*e4)),Im(2*(e2*e4))*])
   /(2*2) by COMSEQ_3:17
 .=(2*(e1*e3)+(Re(2*(e2*e4))+(Im(2*(e2*e4)))*<i>))
   /(2*2) by COMPLEX1:27
 .=(2*(e1*e3)+2*(e2*e4))/(2*2) by COMPLEX1:29
 .=(e1*e3)/(2)+(2*(e2*e4))/(2*2)
 .=exp(<i>*z1+<i>*z2)/(2)+(e2*e4)/(2) by SIN_COS:24
 .=exp(<i>*(z1+z2))/(2)+exp(-<i>*z1+-<i>*z2)/(2) by SIN_COS:24
 .=( exp(<i>*(z1+z2))+exp(-<i>*(z1+z2)) )/(2);
 hence thesis by Lm3;
end;

theorem
  cos_C/.(z1-z2) = (cos_C/.z1)*(cos_C/.z2) + (sin_C/.z1)*(sin_C/.z2)
proof
     cos_C/.(z1-z2) = cos_C/.(z1+-z2)
 .=cos_C/.z1*cos_C/.(-z2) - sin_C/.z1*sin_C/.(-z2) by Th6
 .=cos_C/.z1*cos_C/.z2 - sin_C/.z1*sin_C/.(-z2) by Th3
 .=cos_C/.z1*cos_C/.z2 - sin_C/.z1*(-sin_C/.z2) by Th2
 .=cos_C/.z1*cos_C/.z2 - -sin_C/.z1*sin_C/.z2;
 hence thesis;
end;

theorem Th8:
(cosh_C/.z)*(cosh_C/.z) - (sinh_C/.z)*(sinh_C/.z) = 1
proof
 set e1 = exp(z), e2 = exp(-z);
 (cosh_C/.z)*(cosh_C/.z) - (sinh_C/.z)*(sinh_C/.z)
  =(cosh_C/.z)*(cosh_C/.z) - ((e1-e2)/2)*(sinh_C/.z) by Lm4
 .=(cosh_C/.z)*(cosh_C/.z) - ((e1-e2)/2)*((e1-e2)/2) by Lm4
 .=((e1+e2)/2)*(cosh_C/.z) - (((e1-e2)*(e1-e2))/(2*2))
    by Lm5
 .=((e1+e2)/2)*((e1+e2)/2) - (((e1-e2)*(e1-e2))/(2*2))
    by Lm5
 .=(e1*e2 + e1*e2 + (e1*e2 + e1*e2))/(2*2)
 .=(1 + e1*e2 + (e1*e2 + e1*e2))/(2*2) by Lm8
 .=(1 + 1 + (e1*e2 + e1*e2))/(2*2) by Lm8
 .=(1 + 1 + (1 + e1*e2))/(2*2) by Lm8
 .=(2+2)/(2*2) by Lm8
 .= 1;
 hence thesis;
end;

theorem Th9:
-sinh_C/.z = sinh_C/.(-z)
proof
     sinh_C/.(-z) = (exp(-z) - exp(-(-z)))/2 by Lm4
 .= -(exp(z) - exp(-z))/2;
 hence thesis by Lm4;
end;

theorem Th10:
cosh_C/.z = cosh_C/.(-z)
proof
     cosh_C/.(-z) = (exp(-z) + exp(-(-z)))/2 by Lm5
 .= (exp(-z) + exp(z))/2;
 hence thesis by Lm5;
end;

theorem Th11:
sinh_C/.(z1+z2) = (sinh_C/.z1)*(cosh_C/.z2) + (cosh_C/.z1)*(sinh_C/.z2)
proof
 set e1=exp(z1), e2=exp(-z1), e3=exp(z2), e4=exp(-z2);
       (sinh_C/.z1)*(cosh_C/.z2) + (cosh_C/.z1)*(sinh_C/.z2)
  =((e1-e2)/2)*(cosh_C/.z2) + (cosh_C/.z1)*(sinh_C/.z2) by Lm4
 .=((e1-e2)/2)*(cosh_C/.z2) + (cosh_C/.z1)*((e3-e4)/2) by Lm4
 .=((e1-e2)/2)*(cosh_C/.z2) + ((e1+e2)/2)*((e3-e4)/2) by Lm5
 .=((e1-e2)/2)*((e3+e4)/2) +
   ((e1+e2)/2)*((e3-e4)/2) by Lm5
 .=(e1*e3+e1*e3-(e2*e4+e2*e4))/4
 .=(Re(e1*e3)+Re(e1*e3)+(Im(e1*e3)+Im(e1*e3))*<i>-(e2*e4 + e2*e4))
   /4 by COMPLEX1:def 9
 .=([*2*Re(e1*e3),2*Im(e1*e3)*]-(e2*e4 + e2*e4)) /4 by COMPLEX1:27
 .=([*Re(2*(e1*e3)),2*Im(e1*e3)*]-(e2*e4 + e2*e4)) /4 by COMSEQ_3:17
 .=([*Re(2*(e1*e3)),Im(2*(e1*e3))*]-(e2*e4 + e2*e4)) /4 by COMSEQ_3:17
 .=((Re(2*(e1*e3))+(Im(2*(e1*e3)))*<i>)-(e2*e4 + e2*e4))
   /4 by COMPLEX1:27
 .=(2*(e1*e3)-(e2*e4 + e2*e4))/4 by COMPLEX1:29
 .=(2*(e1*e3)-(Re(e2*e4)+Re(e2*e4)+(Im(e2*e4)+Im(e2*e4))*<i>))
   /4 by COMPLEX1:def 9
 .=(2*(e1*e3)-[*2*Re(e2*e4),2*Im(e2*e4)*]) /4 by COMPLEX1:27
 .=(2*(e1*e3)-[*Re(2*(e2*e4)),2*Im(e2*e4)*]) /4 by COMSEQ_3:17
 .=(2*(e1*e3)-[*Re(2*(e2*e4)),Im(2*(e2*e4))*]) /4 by COMSEQ_3:17
 .=(2*(e1*e3)-(Re(2*(e2*e4))+(Im(2*(e2*e4)))*<i>)) /4 by COMPLEX1:27
 .=(2*(e1*e3)-2*(e2*e4))/4 by COMPLEX1:29
 .=(e1*e3)/(2)-(2*(e2*e4))/(2*2)
 .=exp(z1+z2)/2-(e2*e4)/(2) by SIN_COS:24
 .=exp(z1+z2)/2-exp(-z1+-z2)/2 by SIN_COS:24
 .=(exp(z1+z2)-exp(-(z1+z2)))/2;
 hence thesis by Lm4;
end;

theorem Th12:
sinh_C/.(z1-z2) = (sinh_C/.z1)*(cosh_C/.z2) - (cosh_C/.z1)*(sinh_C/.z2)
proof
     sinh_C/.(z1-z2) = sinh_C/.(z1+ -z2)
 .= (sinh_C/.z1)*cosh_C/.(-z2) + (cosh_C/.z1)*sinh_C/.(-z2) by Th11
 .= (sinh_C/.z1)*cosh_C/.z2 + (cosh_C/.z1)*sinh_C/.(-z2) by Th10
 .= (sinh_C/.z1)*cosh_C/.z2 + (cosh_C/.z1)*(-sinh_C/.z2) by Th9
 .= (sinh_C/.z1)*cosh_C/.z2 + -(cosh_C/.z1)*(sinh_C/.z2);
 hence thesis;
end;

theorem Th13:
cosh_C/.(z1-z2) = (cosh_C/.z1)*(cosh_C/.z2) - (sinh_C/.z1)*(sinh_C/.z2)
proof
 set e1=exp(z1), e2=exp(-z1), e3=exp(z2), e4=exp(-z2);
       (cosh_C/.z1)*(cosh_C/.z2) - (sinh_C/.z1)*(sinh_C/.z2)
  =((e1+e2)/2)*(cosh_C/.z2) - (sinh_C/.z1)*(sinh_C/.z2) by Lm5
 .=((e1+e2)/2)*((e3+e4)/2) - (sinh_C/.z1)*(sinh_C/.z2) by Lm5
 .=((e1+e2)*(e3+e4))/(2*2) - ((e1-e2)/2)*(sinh_C/.z2) by Lm4
 .=((e1+e2)*(e3+e4))/(2*2)-((e1-e2)/2)*((e3-e4)/2)
   by Lm4
 .=(e2*e3+e2*e3+(e1*e4+e2*e4+(e1*e4-e2*e4)))/(2*2)
 .=(Re(e2*e3)+Re(e2*e3)+(Im(e2*e3)+Im(e2*e3))*<i>+(e1*e4+e1*e4))
   /(2*2) by COMPLEX1:def 9
 .=([*2*Re(e2*e3),2*Im(e2*e3)*]+(e1*e4+e1*e4))
   /(2*2) by COMPLEX1:27
 .=([*Re(2*(e2*e3)),2*Im(e2*e3)*]+(e1*e4+e1*e4))
   /(2*2) by COMSEQ_3:17
 .=([*Re(2*(e2*e3)),Im(2*(e2*e3))*]+(e1*e4+e1*e4))
   /(2*2) by COMSEQ_3:17
 .=((Re(2*(e2*e3))+(Im(2*(e2*e3)))*<i>)+(e1*e4+e1*e4))
   /(2*2) by COMPLEX1:27
 .=(2*(e2*e3)+(e1*e4+e1*e4))/(2*2) by COMPLEX1:29
 .=(2*(e2*e3)+(Re(e1*e4)+Re(e1*e4)+(Im(e1*e4)+Im(e1*e4))*<i>))
   /(2*2) by COMPLEX1:def 9
 .=(2*(e2*e3)+[*2*Re(e1*e4),2*Im(e1*e4)*])
   /(2*2) by COMPLEX1:27
 .=(2*(e2*e3)+[*Re(2*(e1*e4)),2*Im(e1*e4)*])
    /(2*2) by COMSEQ_3:17
 .=(2*(e2*e3)+[*Re(2*(e1*e4)),Im(2*(e1*e4))*])
    /(2*2) by COMSEQ_3:17
 .=(2*(e2*e3)+(Re(2*(e1*e4))+(Im(2*(e1*e4)))*<i>))
    /(2*2) by COMPLEX1:27
 .=(2*(e2*e3)+2*(e1*e4))/(2*2) by COMPLEX1:29
 .=(e1*e4)/(2)+(2*(e2*e3))/(2*2)
 .=exp(z1+-z2)/2+(e2*e3)/2 by SIN_COS:24
 .=exp(z1-z2)/2+exp(-z1+z2)/2 by SIN_COS:24
 .=(exp(z1-z2)+exp(-(z1-z2)))/2;
 hence thesis by Lm5;
end;

theorem Th14:
cosh_C/.(z1+z2) = (cosh_C/.z1)*(cosh_C/.z2) + (sinh_C/.z1)*(sinh_C/.z2)
proof
     cosh_C/.(z1+z2) = cosh_C/.(z1- -z2)
 .=(cosh_C/.z1)*cosh_C/.(-z2) - (sinh_C/.z1)*sinh_C/.(-z2) by Th13
 .=(cosh_C/.z1)*cosh_C/.(z2) - (sinh_C/.z1)*sinh_C/.(-z2) by Th10
 .=(cosh_C/.z1)*cosh_C/.(z2) - (sinh_C/.z1)*(-sinh_C/.z2) by Th9
 .=(cosh_C/.z1)*cosh_C/.(z2) - -(sinh_C/.z1)*(sinh_C/.z2);
 hence thesis;
end;

theorem Th15:
sin_C/.(<i>*z) = <i>*sinh_C/.z
proof
 sin_C/.(<i>*z) = (exp(<i>*<i>*z) - exp(-<i>*(<i>*z)))/(<i>*2) by Lm2
 .= <i>*((exp(z) - exp(-z))/2);
 hence thesis by Lm4;
end;

theorem Th16:
cos_C/.(<i>*z) = cosh_C/.z
proof
 cos_C/.(<i>*z) = (exp(<i>*<i>*z)+exp(-<i>*(<i>*z)))/2 by Lm3
 .= (exp(-z)+exp(z))/2;
 hence thesis by Lm5;
end;

theorem Th17:
sinh_C/.(<i>*z) = <i>*sin_C/.z
proof
     sinh_C/.(<i>*z) = (exp(<i>*z)-exp(-<i>*z))/2 by Lm4
 .= <i>*((exp(<i>*z)-exp(-<i>*z))/(<i>*2));
 hence thesis by Lm2;
end;

theorem Th18:
cosh_C/.(<i>*z) = cos_C/.z
proof
 cosh_C/.(<i>*z) = (exp(<i>*z) + exp(-<i>*z))/2 by Lm5;
 hence thesis by Lm3;
end;

Lm9:
exp([*x,y*])=[*exp_R(x),0*]*[*cos(y),sin(y)*]
proof
  [*x,0*] = x+0*<i> & [*0,y*] = 0+y*<i> by COMPLEX1:27;
  then [*x,0*]+[*0,y*] = [*x,y*] by COMPLEX1:27;
  then exp([*x,y*]) =exp([*x,0*])*exp([*0,y*]) by SIN_COS:24
 .= [*exp_R(x),0*]*exp([*0,y*]) by SIN_COS:54
 .= [*exp_R(x),0*]*(cos y+(sin y)*<i>) by SIN_COS:28;
 hence thesis by COMPLEX1:27;
end;

theorem Th19:
for x,y being Element of REAL holds
exp([*x,y*]) = [*(exp_R.x)*(cos.y),(exp_R.x)*(sin.y)*]
proof
 let x,y be Element of REAL;
A1: [*exp_R(x),0*] = exp_R(x)+0*<i> & [*cos y,sin y*] = cos(y)+sin(y)*<i>
             by COMPLEX1:27;
     exp([*x,y*])=[*exp_R(x),0*]*[*cos(y),sin(y)*] by Lm9
 .= exp_R(x)*cos(y)-0*sin(y) + (exp_R(x)*sin(y)+cos(y)*0)*<i> by A1
 .=[*exp_R(x)*cos(y)-0*sin(y),exp_R(x)*sin(y)+cos(y)*0*] by COMPLEX1:27
 .=[*exp_R(x)*cos.y,exp_R(x)*sin(y)*] by SIN_COS:def 23
 .=[*exp_R.x*cos.y,exp_R(x)*sin(y)*] by SIN_COS:def 27
 .=[*exp_R.x*cos.y,exp_R.x*sin(y)*] by SIN_COS:def 27;
 hence thesis by SIN_COS:def 21;
end;

theorem Th20:
exp(0c) = 1
 proof
  thus exp(0c) = [*exp_R 0,0*] by Lm6, SIN_COS:54
       .= 1+0*<i> by SIN_COS:56, COMPLEX1:27
       .= 1;
 end;

theorem Th21:
sin_C/.0c = 0
proof
  sin_C/.0c = (exp(0c)-exp(-<i>*0c))/(<i>*2) by Lm2
 .=0c/(<i>*2);
 hence thesis;
end;

theorem
  sinh_C/.0c = 0
proof
  sinh_C/.0c = (exp(0c)-exp(-0c))/2 by Lm4
 .=0c/2;
 hence thesis;
end;

theorem Th23:
  cos_C/.0c = 1
proof
 cos_C/.0c = (exp(0c)+exp(-<i>*0c))/2 by Lm3
 .= 1 by Th20;
 hence thesis;
end;

theorem
  cosh_C/.0c = 1
proof
     cosh_C/.0c =  (exp 0c + exp(-0c))/2 by Lm5
  .=1 by Th20;
 hence thesis;
end;

theorem
  exp(z) = cosh_C/.z + sinh_C/.z
proof
     cosh_C/.z + sinh_C/.z = (exp(z)+exp(-z))/2 + sinh_C/.z by Lm5
 .= (exp(z)+exp(-z))/2 + (exp(z)-exp(-z))/2 by Lm4
 .= (exp(z)+(exp(-z) + exp(z)-exp(-z)))/2
 .=(Re(exp(z))+Re(exp(z))+(Im(exp(z))+Im(exp(z)))*<i>)/2 by COMPLEX1:def 9
 .=([*2*Re(exp(z)),2*Im(exp(z))*])/2 by COMPLEX1:27
 .=([*Re(2*exp(z)),2*Im(exp(z))*])/2 by COMSEQ_3:17
 .=([*Re(2*exp(z)),Im(2*exp(z))*])/2 by COMSEQ_3:17
 .=(Re(2*exp(z))+(Im(2*exp(z)))*<i>)/2 by COMPLEX1:27
 .=(2*exp(z))/2 by COMPLEX1:29
 .=exp(z)*1;
 hence thesis;
end;

theorem
    exp(-z) = cosh_C/.z - sinh_C/.z
proof
     cosh_C/.z - sinh_C/.z = (exp(z)+exp(-z))/2 - sinh_C/.z by Lm5
 .= (exp(z)+exp(-z))/2 - (exp(z)-exp(-z))/2 by Lm4
 .= (exp(-z)+exp(-z))/2
 .= (Re(exp(-z))+Re(exp(-z))+(Im(exp(-z))+Im(exp(-z)))*<i>)/2
            by COMPLEX1:def 9
 .=([*2*Re(exp(-z)),2*Im(exp(-z))*])/2 by COMPLEX1:27
 .=([*Re(2*exp(-z)),2*Im(exp(-z))*])/2 by COMSEQ_3:17
 .=([*Re(2*exp(-z)),Im(2*exp(-z))*])/2 by COMSEQ_3:17
 .=(Re(2*exp(-z))+(Im(2*exp(-z)))*<i>)/2 by COMPLEX1:27
 .=(2*exp(-z))/2 by COMPLEX1:29
 .=exp(-z)*1;
 hence thesis;
end;

Lm10:
for x being Element of REAL holds
[*x,0*]*<i> = [*0,x*]
proof
 let x be Element of REAL;
 thus [*x,0*]*<i> = (x+0*<i>)*<i> by COMPLEX1:27
     .= 0+x*<i>
     .= [*0,x*] by COMPLEX1:27;
end;

theorem
  exp(z+[*2*PI,0*]*<i>) = exp(z) &
  exp(z+[*0,2*PI*]) = exp(z)
proof
   exp(z+[*2*PI,0*]*<i>) = exp(z)
 proof
A1: z+[*2*PI,0*]*<i> = z+(2*PI+0*<i>)*<i> by COMPLEX1:27
      .= (Re z+(Im z)*<i>)+(2*PI+0*<i>)*<i> by COMPLEX1:29
      .= Re z + 0 + (Im z + 2*PI)*<i>
      .= [*Re z + 0,Im z + 2*PI*] by COMPLEX1:27;
A2: Re z+(Im z)*<i>= [*Re z,Im z*] by COMPLEX1:27;
      exp(z+[*2*PI,0*]*<i>)
   =[*exp_R.(Re z)*cos.(Im z + 2*PI*1),exp_R.(Re z)*sin.(Im z + 2*PI)*]
    by  A1,Th19
  .=[*exp_R.(Re z)*cos.(Im z),exp_R.(Re z)*sin.(Im z + 2*PI*1)*]
    by SIN_COS2:11
  .=[*exp_R.(Re z)*cos.(Im z),exp_R.(Re z)*sin.(Im z)*] by SIN_COS2:10
  .=exp([*Re z,Im z*]) by Th19;
  hence thesis by COMPLEX1:29,A2;
 end;
 hence thesis by Lm10;
end;

theorem Th28:
exp([*0,2*PI*n*]) = 1 & exp([*2*PI*n,0*]*<i>) = 1
proof
   exp([*0,2*PI*n*]) = 1
proof
 thus exp([*0,2*PI*n*]) = cos(0+2*PI*n)+sin(0+2*PI*n)*<i> by SIN_COS:28
  .= [*cos(0+2*PI*n),sin(0+2*PI*n)*] by COMPLEX1:27
 .=[*cos.(0+2*PI*n),sin(0+2*PI*n)*] by SIN_COS:def 23
 .=[*cos.(0+2*PI*n),sin.(0+2*PI*n)*] by SIN_COS:def 21
 .=[*cos.(0+2*PI*n),sin.(0)*] by SIN_COS2:10
 .=[*1,sin.(0)*] by SIN_COS:33,SIN_COS2:11
 .= 1 + 0*<i>  by SIN_COS:33, COMPLEX1:27
 .= 1;
end;
 hence thesis by Lm10;
end;

theorem Th29:
  exp([*0,-2*PI*n*]) = 1 & exp([*-2*PI*n,0*]*<i>) = 1
proof
   exp([*0,-2*PI*n*]) = 1
 proof
  thus exp([*0,-2*PI*n*]) = cos(-2*PI*n)+sin(-2*PI*n)*<i> by SIN_COS:28
  .= [*cos(-2*PI*n),sin(-2*PI*n)*] by COMPLEX1:27
 .=[*cos(2*PI*n),sin(-2*PI*n)*] by SIN_COS:34
 .=[*cos(0+2*PI*n),-sin(0+2*PI*n)*] by SIN_COS:34
 .=[*cos.(0+2*PI*n),-sin(0+2*PI*n)*] by SIN_COS:def 23
 .=[*cos.(0+2*PI*n),-sin.(0+2*PI*n)*] by SIN_COS:def 21
 .=[*cos.(0+2*PI*n),-sin.(0)*] by SIN_COS2:10
 .=[*1,-sin.(0)*] by SIN_COS:33,SIN_COS2:11
 .=1+(-0)*<i> by SIN_COS:33,COMPLEX1:27
 .= 1;
 end;
 hence thesis by Lm10;
end;

theorem
  exp([*0,(2*n+1)*PI*]) = [*-1,0*] & exp([*(2*n+1)*PI,0*]*<i>) = [*-1,0*]
proof
   exp([*0,(2*n+1)*PI*]) = [*-1,0*]
 proof
     exp([*0,(2*n+1)*PI*])
  = cos(PI*2*n+PI)+sin(PI*(2*n)+1*PI)*<i> by SIN_COS:28
 .= [*cos(PI*2*n+PI),sin(PI*(2*n)+1*PI)*] by COMPLEX1:27
 .= [*cos.(PI*2*n+PI),sin(PI*2*n+PI)*] by SIN_COS:def 23
 .= [*cos.(PI*2*n+PI),sin.(PI*2*n+PI)*] by SIN_COS:def 21
 .= [*cos.(PI),sin.(PI*2*n+PI)*] by SIN_COS2:11
 .= [*-1,sin.(PI)*] by SIN_COS:81,SIN_COS2:10;
 hence thesis by SIN_COS:81;
 end;
 hence thesis by Lm10;
end;

theorem
  exp([*0,-(2*n+1)*PI*]) = [*-1,0*] &
  exp([*-(2*n+1)*PI,0*]*<i>) = [*-1,0*]
proof
   exp([*0,-(2*n+1)*PI*]) = [*-1,0*]
 proof
     exp([*0,-(2*n+1)*PI*])
  = cos(-(2*n+1)*PI)+sin(-(2*n+1)*PI)*<i> by SIN_COS:28
 .= [*cos(-(2*n+1)*PI),sin(-(2*n+1)*PI)*] by COMPLEX1:27
 .= [*cos((2*n+1)*PI),sin(-(2*n+1)*PI)*] by SIN_COS:34
 .= [*cos(PI*2*n+PI),-sin(PI*(2*n)+1*PI)*] by SIN_COS:34
 .= [*cos.(PI*2*n+PI),-sin(PI*2*n+PI)*] by SIN_COS:def 23
 .= [*cos.(PI*2*n+PI),-sin.(PI*2*n+PI)*] by SIN_COS:def 21
 .= [*cos.(PI),-sin.(PI*2*n+PI)*] by SIN_COS2:11
 .= [*-1,-sin.(PI)*] by SIN_COS:81,SIN_COS2:10;
 hence thesis by SIN_COS:81;
 end;
 hence thesis by Lm10;
end;

theorem
  exp([*0,(2*n + 1/2)*PI*]) = [*0,1*] &
  exp([*(2*n + 1/2)*PI,0*]*<i>) = [*0,1*]
proof
   exp([*0,(2*n + 1/2)*PI*]) = [*0,1*]
 proof
     exp([*0,(2*n+1/2)*PI*])
  = cos(PI*2*n+1/2*PI)+sin(PI*(2*n)+1/2*PI)*<i> by SIN_COS:28
 .= [*cos(PI*2*n+1/2*PI),sin(PI*(2*n)+1/2*PI)*] by COMPLEX1:27
 .= [*cos.(PI*2*n+1/2*PI),sin(PI*2*n+1/2*PI)*] by SIN_COS:def 23
 .= [*cos.(PI*2*n+1/2*PI),sin.(PI*2*n+1/2*PI)*] by SIN_COS:def 21
 .= [*cos.(1/2*PI),sin.(PI*2*n+1/2*PI)*] by SIN_COS2:11
 .= [*0,sin.(PI/2)*] by  SIN_COS2:10,SIN_COS:81;
 hence thesis by SIN_COS:81;
 end;
 hence thesis by Lm10;
end;

theorem
    exp([*0,-(2*n + 1/2)*PI*]) = [*0,-1*] &
exp([*-(2*n + 1/2)*PI,0*]*<i>) = [*0,-1*]
proof
   exp([*0,-(2*n + 1/2)*PI*]) = [*0,-1*]
 proof
      exp([*0,-(2*n+1/2)*PI*])
   = cos(-(2*n+1/2)*PI)+sin(-(2*n+1/2)*PI)*<i> by SIN_COS:28
  .= [*cos(-(2*n+1/2)*PI),sin(-(2*n+1/2)*PI)*] by COMPLEX1:27
  .= [*cos((2*n+1/2)*PI),sin(-(2*n+1/2)*PI)*] by SIN_COS:34
  .= [*cos(PI*2*n+1/2*PI),-sin(PI*(2*n)+1/2*PI)*] by SIN_COS:34
  .= [*cos.(PI*2*n+1/2*PI),-sin(PI*2*n+1/2*PI)*] by SIN_COS:def 23
  .= [*cos.(PI*2*n+1/2*PI),-sin.(PI*2*n+1/2*PI)*] by SIN_COS:def 21
  .= [*cos.(1/2*PI),-sin.(PI*2*n+1/2*PI)*] by SIN_COS2:11
  .= [*0,-sin.(PI/2)*] by  SIN_COS2:10,SIN_COS:81;
  hence thesis by SIN_COS:81;
 end;
 hence thesis by Lm10;
end;

Lm11:
for a,b being Element of REAL holds
-[*a,b*] = [*-a,-b*]
proof
 let a,b be Element of REAL;
 thus -[*a,b*] = -(a+b*<i>) by COMPLEX1:27
     .= -a+(-b)*<i>
     .= [*-a,-b*] by COMPLEX1:27;
end;

Lm12: [*1,0*] = 1+0*<i> by COMPLEX1:27;

theorem
  sin_C/.(z + 2*n*PI) = sin_C/.z
proof
 sin_C/.(z + 2*n*PI) = sin_C/.(z + [*2*n*PI,0*]) by ARYTM_0:def 7
 .= (exp(<i>*z + <i>*[*2*n*PI,0*]) - exp(-<i>*(z + [*2*n*PI,0*])))
    /(2*<i>) by Lm2
 .= (exp(<i>*z)*exp(<i>*[*2*PI*n,0*]) - exp(-<i>*(z + [*2*n*PI,0*])))
    /(2*<i>) by SIN_COS:24
 .= (exp(<i>*z)*1 - exp(-<i>*z + -<i>*[*2*n*PI,0*]))
    /(2*<i>) by Th28
 .= (exp(<i>*z)*1 - exp(-<i>*z) * exp(<i>*(-[*2*PI*n,0*])))
    /(2*<i>) by SIN_COS:24
 .= (exp(<i>*z)*1 - exp(-<i>*z) * exp(<i>*[*-2*PI*n,-0*]))
    /(2*<i>) by Lm11
 .= (exp(<i>*z)*1 - exp(-<i>*z)*1r)/(2*<i>) by Lm7,Th29,Lm12
 .= (exp(<i>*z) - exp(-<i>*z))/(2*<i>) by COMPLEX1:def 7;
 hence thesis by Lm2;
end;

theorem
  cos_C/.(z + 2*n*PI) = cos_C/.z
proof
  cos_C/.(z + 2*n*PI) = cos_C/.(z + [*2*n*PI,0*]) by ARYTM_0:def 7
 .= (exp(<i>*z + <i>*[*2*n*PI,0*]) + exp(-<i>*(z + [*2*n*PI,0*])))
    /2 by Lm3
 .= (exp(<i>*z)*exp(<i>*[*2*PI*n,0*]) + exp(-<i>*(z + [*2*n*PI,0*])))
    /2 by SIN_COS:24
 .= (exp(<i>*z)*1 + exp(-<i>*z + -<i>*[*2*n*PI,0*]))
    /2 by Th28
 .= (exp(<i>*z)*1 + exp(-<i>*z) * exp(<i>*(-[*2*PI*n,0*])))
    /2 by SIN_COS:24
 .= (exp(<i>*z)*1 + exp(-<i>*z) * exp(<i>*[*-2*PI*n,-0*]))
    /2 by Lm11
 .= (exp(<i>*z)*1 + exp(-<i>*z)*1)/2 by Th29
 .= (exp(<i>*z) + exp(-<i>*z))/2;
 hence thesis by Lm3;
end;

theorem Th36:
exp(<i>*z) = cos_C/.z + <i>*sin_C/.z
proof
  cos_C/.z + <i>*sin_C/.z
  = (exp(<i>*z) + exp(-<i>*z))/2 + <i>*sin_C/.z by Lm3
 .= (exp(<i>*z) + exp(-<i>*z))/2
    + <i>*((exp(<i>*z) - exp(-<i>*z))/(2*<i>)) by Lm2
 .= (exp(<i>*z) + (exp(-<i>*z) +exp(<i>*z) - exp(-<i>*z)))/2
 .= (Re exp(<i>*z) + Re exp(<i>*z)+(Im exp(<i>*z) + Im exp(<i>*z))*<i>)/2
    by COMPLEX1:def 9
 .= [*2*Re exp(<i>*z),2*Im exp(<i>*z)*]/2 by COMPLEX1:27
 .= [*Re(2*exp(<i>*z)),2*Im exp(<i>*z)*]/2 by COMSEQ_3:17
 .= [*Re(2*exp(<i>*z)),Im(2*exp(<i>*z))*]/2 by COMSEQ_3:17
 .= (Re(2*exp(<i>*z))+(Im(2*exp(<i>*z)))*<i>)/2 by COMPLEX1:27
 .= (2*exp(<i>*z))/2 by COMPLEX1:29;
 hence thesis;
end;

theorem Th37:
exp(-<i>*z) = cos_C/.z - <i>*sin_C/.z
proof
  cos_C/.z - <i>*sin_C/.z
  = (exp(<i>*z) + exp(-<i>*z))/(2) - <i>*sin_C/.z by Lm3
 .= (exp(<i>*z) + exp(-<i>*z))/2
    - <i>*((exp(<i>*z) - exp(-<i>*z))/(2*<i>)) by Lm2
 .= (exp(-<i>*z) + exp(-<i>*z))/2
 .= (Re exp(-<i>*z) + Re exp(-<i>*z)+(Im exp(-<i>*z) + Im exp(-<i>*z))*<i>)/2
    by COMPLEX1:def 9
 .= [*2*Re exp(-<i>*z),2*Im exp(-<i>*z)*]/2 by COMPLEX1:27
 .= [*Re(2*exp(-<i>*z)),2*Im exp(-<i>*z)*]/2 by COMSEQ_3:17
 .= [*Re(2*exp(-<i>*z)),Im(2*exp(-<i>*z))*]/2 by COMSEQ_3:17
 .= (Re(2*exp(-<i>*z))+(Im(2*exp(-<i>*z)))*<i>)/2
    by COMPLEX1:27
 .= (2*exp(-<i>*z))/2 by COMPLEX1:29;
 hence thesis;
end;

Lm13:
for x,y being Element of REAL holds
<i>*[*x,y*] = [*-y,x*]
proof
 let x,y be Element of REAL;
 thus <i>*[*x,y*] = <i>*(x+y*<i>) by COMPLEX1:27
  .= <i>*x+y*(-1)
 .= [*-y,x*] by COMPLEX1:27;
end;

theorem Th38:
for x being Element of REAL holds
sin_C/.[*x,0*] = sin.x
proof
 let x be Element of REAL;
   [*sin.x,0*] = sin.x+0*<i> & [*0,2*] = 0+2*<i> by COMPLEX1:27;
   then
A1:[*sin.x,0*]*[*0,2*] = sin.x*0-0*2+(sin.x*2+0*0)*<i>
 .= [*0,2*sin.x*] by COMPLEX1:27;
A2:[*0,2*] <> 0c
 proof
      2*<i> = [*-0,2*] by Lm13,Lm1
  .= [*0,2*];
  hence thesis;
 end;
A3: [*cos.x,sin.x*] = cos.x+(sin.x)*<i> &
   [*-cos.x,--sin.x*] = -cos.x+(--sin.x)*<i> by COMPLEX1:27;
     sin_C/.[*x,0*] = (exp(<i>*[*x,0*]) - exp(-<i>*[*x,0*]))/(2*<i>) by
Lm2
 .= (exp([*-0,x*]) - exp(-<i>*[*x,0*]))/(2*<i>) by Lm13
 .= (exp([*0,x*]) - exp(-[*0,x*]))/(2*<i>) by Lm13
 .= (exp([*0,x*]) - exp([*-0,-x*]))/(2*<i>) by Lm11
 .= (exp([*0,x*]) - exp([*0,-x*]))/[*0,2*] by Lm13,Lm1
 .= ([*exp_R.0*cos.x,exp_R.0*sin.x*] - exp([*0,-x*]))/[*0,2*] by Th19
 .= ([*exp_R(0)*cos.x,exp_R.0*sin.x*] - exp([*0,-x*]))/[*0,2*]
     by SIN_COS:def 27
 .= ([*cos.x,1*sin.x*] - exp([*0,-x*]))/[*0,2*] by SIN_COS:56,def 27
 .= ([*cos.x,1*sin.x*] - [*exp_R.0*cos.-x,exp_R.0*sin.-x*])/[*0,2*]
     by Th19
 .= ([*cos.x,1*sin.x*] - [*exp_R(0)*cos.-x,exp_R.0*sin.-x*])/[*0,2*]
    by SIN_COS:def 27
 .= ([*cos.x,sin.x*] - [*cos.-x,1*sin.-x*])/[*0,2*]
    by SIN_COS:56,def 27
 .= ([*cos.x,sin.x*] - [*cos.x,sin.-x*])/[*0,2*]
    by SIN_COS:33
 .= ([*cos.x,sin.x*] - [*cos.x,-sin.x*])/[*0,2*] by SIN_COS:33
 .= ([*cos.x,sin.x*] + - [*cos.x,-sin.x*])/[*0,2*]
 .= ([*cos.x,sin.x*] + [*-cos.x,--sin.x*])/[*0,2*] by Lm11
 .= (cos.x+-cos.x+(sin.x+sin.x)*<i>)/[*0,2*] by A3
 .= [*cos.x+-cos.x,sin.x+sin.x*]/[*0,2*] by COMPLEX1:27
 .= [*sin.x,0*] by A1,A2,XCMPLX_1:90;
 hence thesis by ARYTM_0:def 7;
end;

theorem Th39:
for x being Element of REAL holds
cos_C/.[*x,0*] = cos.x
proof
A1: [*cos.x,sin.x*] = cos.x+(sin.x)*<i> & [*cos.x,-sin.x*] = cos.x+(-sin.x)*<i>
             by COMPLEX1:27;
 let x be Element of REAL;
A2: [*cos.x,0*]*2 = (cos.x+0*<i>)*2 by COMPLEX1:27
  .= 2*cos.x+0*<i>
  .=[*2*cos.x,0*] by COMPLEX1:27;
     cos_C/.[*x,0*] = (exp(<i>*[*x,0*]) + exp(-<i>*[*x,0*]))/2 by Lm3
 .= (exp([*-0,x*]) + exp(-<i>*[*x,0*]))/2 by Lm13
 .= (exp([*0,x*]) + exp(-[*0,x*]))/2 by Lm13
 .= (exp([*0,x*]) + exp([*-0,-x*]))/2 by Lm11
 .= ([*exp_R.0*cos.x,exp_R.0*sin.x*] + exp([*0,-x*]))/2 by Th19
 .= ([*exp_R(0)*cos.x,exp_R.0*sin.x*] + exp([*0,-x*]))/2 by SIN_COS:def 27
 .= ([*cos.x,1*sin.x*] + exp([*0,-x*]))/2 by SIN_COS:56,def 27
 .= ([*cos.x,1*sin.x*] + [*exp_R.0*cos.-x,exp_R.0*sin.-x*])/2 by Th19
 .= ([*cos.x,1*sin.x*] + [*exp_R(0)*cos.-x,exp_R.0*sin.-x*])/2
    by SIN_COS:def 27
 .= ([*cos.x,1*sin.x*] + [*exp_R(0)*cos.-x,exp_R(0)*sin.-x*])/2
    by SIN_COS:def 27
 .= ([*cos.x,sin.x*] + [*cos.x,sin.-x*])/2 by SIN_COS:33,56
 .= ([*cos.x,sin.x*] + [*cos.x,-sin.x*])/2 by SIN_COS:33
 .= (cos.x+(sin.x)*<i> + [*cos.x,-sin.x*])/2 by A1
 .= (cos.x+(sin.x)*<i> + (cos.x+(-sin.x)*<i>))/2 by A1
 .= (cos.x+cos.x+(sin.x+-sin.x)*<i>)/2
 .= [*cos.x+cos.x,sin.x+-sin.x*]/2 by COMPLEX1:27
 .= [*cos.x,0*] by A2;
 hence thesis by ARYTM_0:def 7;
end;

theorem Th40:
for x being Element of REAL holds
sinh_C/.[*x,0*] = sinh.x
proof
 let x be Element of REAL;
A1: [*exp_R.x,0*] = exp_R.x+0*<i> &
   [*-exp_R.(-x),-0*] = -exp_R.(-x)+(-0)*<i>
             by COMPLEX1:27;
A2:[*exp_R.x-exp_R.(-x),0*] = [*sinh.x,0*]*2
 proof
      [*sinh.x,0*]*2 = (sinh.x+0*<i>)*2 by COMPLEX1:27
  .= sinh.x*2-0*0+(sinh.x*0+0*2)*<i>
  .= [*sinh.x*2-0*0,sinh.x*0+0*2*] by COMPLEX1:27
  .=[*2*((exp_R.x - exp_R.(-x))/2),0*] by SIN_COS2:def 1
  .=[*(exp_R.x - exp_R.(-x))/(2/2),0*];
  hence thesis;
 end;
     sinh_C/.[*x,0*] = (exp([*x,0*])-exp(-[*x,0*]))/2 by Lm4
 .= ([*exp_R.x*cos.0,exp_R.x*sin.0*] - exp(-[*x,0*]))/2 by Th19
 .= ([*exp_R.x,0*] - exp([*-x,-0*]))/2 by Lm11,SIN_COS:33
 .= ([*exp_R.x,0*] - [*exp_R.(-x)*cos.0,exp_R.(-x)*sin.0*])/2 by Th19
 .= ([*exp_R.x,0*] +- [*exp_R.(-x),0*])/2 by SIN_COS:33
 .= (exp_R.x+-exp_R.(-x)+0+0*<i>)/2 by  Lm11,A1
 .= ([*sinh.x,0*]*2)/2 by  COMPLEX1:27,A2;
 hence thesis by ARYTM_0:def 7;
end;

theorem Th41:
for x being Element of REAL holds
cosh_C/.[*x,0*] = cosh.x
proof
 let x be Element of REAL;
A1:[*exp_R.x+exp_R.(-x),0*] = [*cosh.x,0*]*2
 proof
      [*cosh.x,0*]*2 = (cosh.x+0*<i>)*2 by COMPLEX1:27
  .= cosh.x*2-0*0+(cosh.x*0+0*2)*<i>
  .= [*cosh.x*2-0*0,cosh.x*0+0*2*] by COMPLEX1:27
  .=[*2*((exp_R.x + exp_R.(-x))/2),0*] by SIN_COS2:def 3
  .=[*(exp_R.x + exp_R.(-x))/(2/2),0*];
  hence thesis;
 end;
A2: [*exp_R.x,0*] = exp_R.x+0*<i> &
   [*exp_R.(-x),0*] = exp_R.(-x)+0*<i> by COMPLEX1:27;
     cosh_C/.[*x,0*] = (exp([*x,0*])+exp(-[*x,0*]))/2 by Lm5
 .= ([*exp_R.x*cos.0,exp_R.x*sin.0*] + exp(-[*x,0*]))/2 by Th19
 .= ([*exp_R.x,0*] + exp([*-x,-0*]))/2 by Lm11,SIN_COS:33
 .= ([*exp_R.x,0*] + [*exp_R.(-x)*1,exp_R.(-x)*0*])/2 by Th19,SIN_COS:33
 .= (exp_R.x+0*<i> + exp_R.(-x)+0*<i>)/2 by  A2
 .= ([*cosh.x,0*]*2)/2 by  COMPLEX1:27,A1;
 hence thesis by ARYTM_0:def 7;
end;

theorem
 for x,y being Element of REAL holds
 [*x,0*] + <i>*[*y,0*] = [*x,y*]
proof
 let x,y be Element of REAL;
  [*x,0*] = x+0*<i> & [*y,0*] = y+0*<i> by COMPLEX1:27;
 hence [*x,0*] + <i>*[*y,0*] = [*x,y*] by COMPLEX1:27;
end;

theorem Th43:
sin_C/.[*x,y*] = [*sin.x*cosh.y,cos.x*sinh.y*]
proof
A1: [*x,0*] = x+0*<i> & [*0,y*] = 0+y*<i> by COMPLEX1:27;
     sin_C/.[*x,y*] = sin_C/.([*x,0*]+[*0,y*]) by  COMPLEX1:27,A1
 .= sin_C/.[*x,0*]*cos_C/.[*0,y*] + cos_C/.[*x,0*]*sin_C/.[*0,y*] by Th4
 .= sin_C/.[*x,0*]*cos_C/.(<i>*[*y,0*]) + cos_C/.[*x,0*]*sin_C/.[*0,y*]
    by Lm10
 .= sin_C/.[*x,0*]*cos_C/.(<i>*[*y,0*]) + cos_C/.[*x,0*]*sin_C/.(<i>*[*y,0*])
     by Lm10
 .= sin_C/.[*x,0*]*cosh_C/.[*y,0*] + cos_C/.[*x,0*]*sin_C/.(<i>*[*y,0*])
     by Th16
 .= sin_C/.[*x,0*]*cosh_C/.[*y,0*] + cos_C/.[*x,0*]*(<i>*sinh_C/.[*y,0*])
     by Th15
 .= (sin.x)*cosh_C/.[*y,0*] + cos_C/.[*x,0*]*(<i>*sinh_C/.[*y,0*]) by Th38
 .= (sin.x)*cosh_C/.[*y,0*] + (cos.x)*(<i>*sinh_C/.[*y,0*]) by Th39
 .= (sin.x)*cosh_C/.[*y,0*] + (cos.x)*(<i>*(sinh.y)) by Th40
 .= sin.x*cosh.y+0*<i>+(0+(cos.x*sinh.y)*<i>) by Th41
 .= [*sin.x*cosh.y+0,0+cos.x*sinh.y*] by COMPLEX1:27;
 hence thesis;
end;

theorem Th44:
sin_C/.[*x,-y*] = [*sin.x*cosh.y,-cos.x*sinh.y*]
proof
     sin_C/.[*x,-y*] = [*sin.x*cosh.-y,cos.x*sinh.-y*] by Th43
 .= [*sin.x*cosh.y,cos.x*sinh.-y*] by SIN_COS2:19
 .= [*sin.x*cosh.y,cos.x*-sinh.y*] by SIN_COS2:19;
 hence thesis;
end;

theorem Th45:
cos_C/.[*x,y*] = [*cos.x*cosh.y,-sin.x*sinh.y*]
proof
A1:  [*x,0*] = x+0*<i> & [*0,y*] = 0+y*<i> by COMPLEX1:27;
     cos_C/.[*x,y*]= cos_C/.([*x,0*]+[*0,y*]) by  COMPLEX1:27,A1
 .= cos_C/.[*x,0*]*cos_C/.[*0,y*]-sin_C/.[*x,0*]*sin_C/.[*0,y*] by Th6
 .= cos_C/.[*x,0*]*cos_C/.(<i>*[*y,0*])-sin_C/.[*x,0*]*sin_C/.[*0,y*] by Lm10
 .= cos_C/.[*x,0*]*cos_C/.(<i>*[*y,0*])-sin_C/.[*x,0*]*sin_C/.(<i>*[*y,0*])
    by Lm10
 .= cos_C/.[*x,0*]*cos_C/.(<i>*[*y,0*])-sin_C/.[*x,0*]*(sinh_C/.[*y,0*]*<i>)
     by Th15
 .= cos_C/.[*x,0*]*cosh_C/.[*y,0*]-sin_C/.[*x,0*]*(sinh_C/.[*y,0*]*<i>)
     by Th16
 .= cos_C/.[*x,0*]*cosh_C/.[*y,0*]-(sin.x)*(sinh_C/.[*y,0*]*<i>) by Th38
 .= (cos.x)*cosh_C/.[*y,0*]-(sin.x)*(sinh_C/.[*y,0*]*<i>) by Th39
 .= (cos.x)*cosh_C/.[*y,0*]-(sin.x)*((sinh.y)*<i>) by Th40
 .= (cos.x)*(cosh.y)-(sin.x)*(sinh.y)*<i> by Th41
 .= cos.x*cosh.y+-0+(0+-sin.x*sinh.y)*<i>
 .= [*cos.x*cosh.y+-0,0+-sin.x*sinh.y*] by COMPLEX1:27;
 hence thesis;
end;

theorem Th46:
cos_C/.[*x,-y*] = [*cos.x*cosh.y,sin.x*sinh.y*]
proof
  cos_C/.[*x,-y*] = [*cos.x*cosh.-y,-sin.x*sinh.-y*] by Th45
 .= [*cos.x*cosh.y,-sin.x*sinh.-y*] by SIN_COS2:19
 .= [*cos.x*cosh.y,-sin.x*-sinh.y*] by SIN_COS2:19
 .= [*cos.x*cosh.y,--sin.x*sinh.y*];
 hence thesis;
end;

theorem Th47:
sinh_C/.[*x,y*] = [*sinh.x*cos.y,cosh.x*sin.y*]
proof
     sinh_C/.[*x,y*] = sinh_C/.[*--x,y*]
 .= sinh_C/.([*y,-x*]*<i>) by Lm13
 .= <i>*sin_C/.[*y,-x*] by Th17
 .= <i>*[*sin.y*cosh.x,-cos.y*sinh.x*] by Th44
 .= [*--sinh.x*cos.y,cosh.x*sin.y*] by Lm13;
 hence thesis;
end;

theorem Th48:
sinh_C/.[*x,-y*] = [*sinh.x*cos.y,-cosh.x*sin.y*]
proof
     sinh_C/.[*x,-y*] = [*sinh.x*cos.-y,cosh.x*sin.-y*] by Th47
 .= [*sinh.x*cos.y,cosh.x*sin.-y*] by SIN_COS:33
 .= [*sinh.x*cos.y,cosh.x*-sin.y*] by SIN_COS:33;
 hence thesis;
end;

theorem Th49:
cosh_C/.[*x,y*] = [*cosh.x*cos.y,sinh.x*sin.y*]
proof
 cosh_C/.[*x,y*] = cosh_C/.[*--x,y*]
 .=cosh_C/.([*y,-x*]*<i>) by Lm13
 .=cos_C/.[*y,-x*] by Th18;
 hence thesis by Th46;
end;

theorem Th50:
cosh_C/.[*x,-y*] = [*cosh.x*cos.y,-sinh.x*sin.y*]
proof
 cosh_C/.[*x,-y*] = [*cosh.x*cos.-y,sinh.x*sin.-y*] by Th49
 .=[*cosh.x*cos.y,sinh.x*sin.-y*] by SIN_COS:33
 .=[*cosh.x*cos.y,sinh.x*-sin.y*] by SIN_COS:33;
 hence thesis;
end;

theorem Th51:
for n being Element of NAT, z being Element of COMPLEX holds
(cos_C/.z + <i>*sin_C/.z) #N n = cos_C/.([*n,0*]*z) + <i>*sin_C/.([*n,0*]*z)
proof
 defpred X[Element of NAT] means for z being Element of COMPLEX holds
 (cos_C/.z + <i>*sin_C/.z) #N ($1)
 = cos_C/.([*$1,0*]*z) + <i>*sin_C/.([*$1,0*]*z);
 for z being Element of COMPLEX holds
 (cos_C/.z + <i>*sin_C/.z) #N (0)
 = cos_C/.([*0,0*]*z) + <i>*sin_C/.([*0,0*]*z) by COMSEQ_3:11,
     COMPLEX1:def 7,Th23,Th21,Lm6;
 then
A1: X[0];
A2:for n st X[n] holds X[n+1]
 proof
  let n be Element of NAT such that
A3:for z being Element of COMPLEX holds
  (cos_C/.z + <i>*sin_C/.z) #N n = cos_C/.([*n,0*]*z) + <i>*sin_C/.([*n,0*]*z);
A4: [*n,0*]+1 = n + 0*<i> + 1 by COMPLEX1:27
   .= n + 1 + 0*<i>
   .=  [*n+1,0*] by COMPLEX1:27;
      for z being Element of COMPLEX holds (cos_C/.z + <i>*sin_C/.z) #N (n+1)
  = cos_C/.([*n+1,0*]*z) + <i>*sin_C/.([*n+1,0*]*z)
  proof
   let z be Element of COMPLEX;
   set cn = cos_C/.([*n,0*]*z), sn = sin_C/.([*n,0*]*z),
       c1 = cos_C/.z, s1 = sin_C/.z;
A5:   (cos_C/.z + <i>*sin_C/.z) #N (n+1)
    = (cos_C/.z + <i>*sin_C/.z) GeoSeq.(n+1) by COMSEQ_3:def 2
   .= (cos_C/.z + <i>*sin_C/.z) GeoSeq.n * (cos_C/.z + <i>*sin_C/.z)
     by COMSEQ_3:def 1
   .= (cos_C/.z + <i>*sin_C/.z) #N n * (cos_C/.z + <i>*sin_C/.z)
      by COMSEQ_3:def 2
   .= (cos_C/.([*n,0*]*z) + <i>*sin_C/.([*n,0*]*z)) * (cos_C/.z + <i>*sin_C/.z)
      by A3
   .= cn*c1 + <i>*sn*c1 + (<i>*cn*s1 + -sn*s1);
    cos_C/.([*n+1,0*]*z) + <i>*sin_C/.([*n+1,0*]*z)
    = cos_C/.([*n,0*]*z +1*z) +
      <i>*(sin_C/.([*n,0*]*z)*cos_C/.(1*z)
      + cos_C/.([*n,0*]*z)*sin_C/.(1*z)) by  A4,Th4
   .= cos_C/.([*n,0*]*z)*cos_C/.z
      - sin_C/.([*n,0*]*z)*sin_C/.z
      + <i>*(sin_C/.([*n,0*]*z)*cos_C/.z
      + cos_C/.([*n,0*]*z)*sin_C/.z) by Th6
   .= cn*c1 +( <i>*sn*c1 + (<i>*cn*s1 + -sn*s1) );
   hence thesis by A5;
  end;
  hence thesis;
 end;
 for n holds X[n] from NAT_1:sch 1(A1,A2);
 hence thesis;
end;

theorem Th52:
for n being Element of NAT, z being Element of COMPLEX holds
(cos_C/.z - <i>*sin_C/.z) #N n = cos_C/.([*n,0*]*z) - <i>*sin_C/.([*n,0*]*z)
proof
 let n be Element of NAT;
 let z be Element of COMPLEX;
 (cos_C/.z - <i>*sin_C/.z) #N n = (cos_C/.z + <i>*-sin_C/.z) #N n
 .=(cos_C/.z + <i>*sin_C/.-z) #N n by Th2
 .=(cos_C/.-z + <i>*sin_C/.-z) #N n by Th3
 .= cos_C/.(-[*n,0*]*z) + <i>*sin_C/.([*n,0*]*-z) by Th51
 .= cos_C/.([*n,0*]*z) + <i>*sin_C/.(-[*n,0*]*z) by Th3
 .= cos_C/.([*n,0*]*z) + <i>*-sin_C/.([*n,0*]*z) by Th2
 .= cos_C/.([*n,0*]*z) + -<i>*sin_C/.([*n,0*]*z);
 hence thesis;
end;

theorem
    for n being Element of NAT, z being Element of COMPLEX holds
exp(<i>*[*n,0*]*z) = (cos_C/.z + <i>*sin_C/.z) #N n
proof
 let n be Element of NAT;
 let z be Element of COMPLEX;
 exp(<i>*[*n,0*]*z) = exp(<i>*([*n,0*]*z))
 .=cos_C/.([*n,0*]*z) + <i>*sin_C/.([*n,0*]*z) by Th36;
 hence thesis by Th51;
end;

theorem
    for n being Element of NAT, z being Element of COMPLEX holds
exp(-<i>*[*n,0*]*z) = (cos_C/.z - <i>*sin_C/.z) #N n
proof
 let n be Element of NAT;
 let z be Element of COMPLEX;
 exp(-<i>*[*n,0*]*z) = exp(-<i>*([*n,0*]*z))
 .=cos_C/.([*n,0*]*z) - <i>*sin_C/.([*n,0*]*z) by Th37;
 hence thesis by Th52;
end;

theorem
    for x,y being Element of REAL holds
[*1,-1*]/2*sinh_C/.[*x,y*] + [*1,1*]/2*sinh_C/.[*x,-y*]
= [*sinh.x*cos.y + cosh.x*sin.y,0*]
proof
 let x,y be Element of REAL;
 set shx = sinh.x, cy = cos.y, chx = cosh.x, sy = sin.y;
A1: [*1,-1*] = 1+(-1)*<i> & [*shx*cy,chx*sy*] = shx*cy+(chx*sy)*<i> &
   [*1,1*] = 1+1*<i> & [*shx*cy,-chx*sy*] = shx*cy+(-chx*sy)*<i>
          by COMPLEX1:27;
        [*1,-1*]/2*sinh_C/.[*x,y*] + [*1,1*]/2*sinh_C/.[*x,-y*]
  = [*1,-1*]/2*[*shx*cy,chx*sy*] + [*1,1*]/2*sinh_C/.[*x,-y*]
    by Th47
 .= [*1,-1*]*[*shx*cy,chx*sy*]/2 + [*1,1*]/2*[*shx*cy,-chx*sy*] by Th48
 .= 2*(shx*cy+chx*sy+0*<i>)/2 by A1
 .= 2*[*shx*cy+chx*sy,0*]/2 by COMPLEX1:27;
 hence thesis;
end;

theorem
    for x,y being Element of REAL holds
[*1,-1*]/2*cosh_C/.[*x,y*] + [*1,1*]/2*cosh_C/.[*x,-y*]
= sinh.x*sin.y + cosh.x*cos.y
proof
 let x,y be Element of REAL;
 set shx = sinh.x, cy = cos.y, chx = cosh.x, sy = sin.y;
A1: [*1,-1*] = 1+(-1)*<i> & [*chx*cy,shx*sy*] = chx*cy+(shx*sy)*<i> &
   [*1,1*] = 1+1*<i> & [*chx*cy,-shx*sy*] = chx*cy+(-shx*sy)*<i>
          by COMPLEX1:27;
     [*1,-1*]/2*cosh_C/.[*x,y*] + [*1,1*]/2*cosh_C/.[*x,-y*]
 = [*1,-1*]/2*[*chx*cy,shx*sy*] + [*1,1*]/2*cosh_C/.[*x,-y*]
   by Th49
 .= [*1,-1*]*[*chx*cy,shx*sy*]/2 + [*1,1*]/2*[*chx*cy,-shx*sy*] by Th50
 .= 2*(chx*cy+shx*sy+0*<i>)/2 by A1;
 hence thesis;
end;

theorem
    sinh_C/.z*sinh_C/.z = (cosh_C/.(2*z) - 1)/2
proof
 set e1 = exp(z), e2 = exp(-z);
     sinh_C/.z*sinh_C/.z = (exp(z) - exp(-z))/2*sinh_C/.z by Lm4
 .= (e1 - e2)/2*((e1 - e2)/2) by Lm4
 .= (e1*e1 + e2*e2 - 2*(e1*e2))/2/2
 .= (e1*e1 + e2*e2 - 2*1)/2/2 by Lm8
 .= (exp(z+z) + e2*e2 - 2)/2/2 by SIN_COS:24
 .= (exp([*2,0*]*z) + exp(-z+-z) - 2)/2/2 by  SIN_COS:24,Lm1
 .= (( exp([*2,0*]*z) + exp(-[*2,0*]*(z)) )/2 - 1)/2 by Lm1;
 hence thesis by Lm5,Lm1;
end;

theorem Th58:
cosh_C/.z*cosh_C/.z = (cosh_C/.(2*z) + 1)/2
proof
 set e1 = exp(z), e2 = exp(-z);
     cosh_C/.z*cosh_C/.z = (exp(z) + exp(-z))/2*cosh_C/.z by Lm5
 .= (e1 + e2)/2*((e1 + e2)/2) by Lm5
 .= (e1*e1 + e2*e2 + 2*(e1*e2))/2/2
 .= (e1*e1 + e2*e2 + 2*1)/2/2 by Lm8
 .= (exp(z+z) + e2*e2 + 2)/2/2 by SIN_COS:24
 .= (exp([*2,0*]*z) + exp(-z+-z) + 2)/2/2 by  SIN_COS:24,Lm1
 .= (( exp([*2,0*]*z) + exp(-[*2,0*]*(z)) )/2 + 1)/2 by Lm1;
 hence thesis by Lm5,Lm1;
end;

theorem Th59:
sinh_C/.(2*z) = 2*(sinh_C/.z)*(cosh_C/.z) &
cosh_C/.(2*z) = 2*(cosh_C/.z)*(cosh_C/.z) - 1
proof
 set e1 = exp(z), e2 = exp(-z);
A1:sinh_C/.(2*z) = 2*(sinh_C/.z)*(cosh_C/.z)
 proof
      2*(sinh_C/.z)*(cosh_C/.z)
   = 2*((sinh_C/.z)*(cosh_C/.z))
  .= 2*((sinh_C/.z)*((e1 + e2)/2)) by Lm5
  .= 2*((e1 - e2)/2*((e1 + e2)/2)) by Lm4
  .= (e1*e1 - e2*e2)/2
  .= (exp(z+z) - e2*e2)/2 by SIN_COS:24
  .= (exp([*2,0*]*z) - exp(-z+-z))/2 by  SIN_COS:24,Lm1
  .= (exp([*2,0*]*z) - exp(-[*2,0*]*z))/2 by Lm1;
  hence thesis by Lm4,Lm1;
 end;
     cosh_C/.(2*z) = 2*(cosh_C/.z)*(cosh_C/.z) - 1
 proof
  2*(cosh_C/.z)*(cosh_C/.z) - 1
   = 2*((cosh_C/.z)*(cosh_C/.z)) - 1
  .= 2*((cosh_C/.(2*z) + 1)/2) - 1 by Th58
  .= cosh_C/.(2*z) + 1 - 1;
  hence thesis;
 end;
 hence thesis by A1;
end;

theorem Th60:
 (sinh_C/.z1)*(sinh_C/.z1) - (sinh_C/.z2)*(sinh_C/.z2)
=(sinh_C/.(z1+z2))*(sinh_C/.(z1-z2))
&
 (cosh_C/.z1)*(cosh_C/.z1) - (cosh_C/.z2)*(cosh_C/.z2)
=(sinh_C/.(z1+z2))*(sinh_C/.(z1-z2))
&
 (sinh_C/.z1)*(sinh_C/.z1) - (sinh_C/.z2)*(sinh_C/.z2)
=(cosh_C/.z1)*(cosh_C/.z1) - (cosh_C/.z2)*(cosh_C/.z2)
proof
 set s1 = sinh_C/.z1, s2 = sinh_C/.z2, c1 = cosh_C/.z1, c2 = cosh_C/.z2;
A1:(sinh_C/.(z1+z2))*(sinh_C/.(z1-z2))=(s1*s1)*(c2*c2)-(s2*s2)*(c1*c1)
 proof
     (sinh_C/.(z1+z2))*(sinh_C/.(z1-z2))
  =(s1*c2+c1*s2)*(sinh_C/.(z1-z2)) by Th11
 .=(s1*c2+c1*s2)*(s1*c2-c1*s2) by Th12
 .=(s1*s1)*(c2*c2)-s2*(c1*c1)*s2;
  hence thesis;
 end;
A2:(sinh_C/.z1)*(sinh_C/.z1) - (sinh_C/.z2)*(sinh_C/.z2)
 =(sinh_C/.(z1+z2))*(sinh_C/.(z1-z2))
 proof
     (sinh_C/.(z1+z2))*(sinh_C/.(z1-z2))
  =(s1*s1)*((c2*c2)-(s2*s2))+(s1*s1)*(s2*s2)-(s2*s2)*(c1*c1) by A1
  .=(s1*s1)*1+(s1*s1)*(s2*s2)-(s2*s2)*(c1*c1) by Th8
  .=(s1*s1)*1+(s2*s2)*-((c1*c1)-(s1*s1))
  .=(s1*s1)*1+(s2*s2)*-1 by Th8
  .=(s1*s1)*1-(s2*s2);
  hence thesis;
 end;
   (cosh_C/.z1)*(cosh_C/.z1) - (cosh_C/.z2)*(cosh_C/.z2)
 =(sinh_C/.(z1+z2))*(sinh_C/.(z1-z2))
 proof
      (sinh_C/.(z1+z2))*(sinh_C/.(z1-z2))
  = (-((c1*c1)-(s1*s1)))*(c2*c2)+(c1*c1)*(c2*c2)-(s2*s2)*(c1*c1) by A1
  .= (-1)*(c2*c2)+(c1*c1)*(c2*c2)-(s2*s2)*(c1*c1) by Th8
  .= (-1)*(c2*c2)+(c1*c1)*((c2*c2)-(s2*s2))
  .= (-1)*(c2*c2)+(c1*c1)*1 by Th8
  .= -(c2*c2)+c1*c1;
  hence thesis;
 end;
 hence thesis by A2;
end;

theorem Th61:
 (cosh_C/.(z1+z2))*(cosh_C/.(z1-z2))
=(sinh_C/.z1)*(sinh_C/.z1) + (cosh_C/.z2)*(cosh_C/.z2)
&
 (cosh_C/.(z1+z2))*(cosh_C/.(z1-z2))
=(cosh_C/.z1)*(cosh_C/.z1) + (sinh_C/.z2)*(sinh_C/.z2)
&
 (sinh_C/.z1)*(sinh_C/.z1) + (cosh_C/.z2)*(cosh_C/.z2)
=(cosh_C/.z1)*(cosh_C/.z1) + (sinh_C/.z2)*(sinh_C/.z2)
proof
 set s1 = sinh_C/.z1, s2 = sinh_C/.z2, c1 = cosh_C/.z1, c2 = cosh_C/.z2;
A1:(cosh_C/.(z1+z2))*(cosh_C/.(z1-z2))
  = (c1*c2+s1*s2)*(cosh_C/.(z1-z2)) by Th14
 .= (c1*c2+s1*s2)*(c1*c2-s1*s2) by Th13
 .= c1*c1*c2*c2-(s1*s1)*s2*s2;
A2:(cosh_C/.(z1+z2))*(cosh_C/.(z1-z2))
   =(sinh_C/.z1)*(sinh_C/.z1) + (cosh_C/.z2)*(cosh_C/.z2)
 proof
      (cosh_C/.(z1+z2))*(cosh_C/.(z1-z2))
  = (c1*c1 - s1*s1)*(c2*c2) + s1*s1*(c2*c2-s2*s2) by A1
  .= 1*(c2*c2) + s1*s1*(c2*c2-s2*s2) by Th8
  .= (c2*c2) + s1*s1*1 by Th8;
  hence thesis;
 end;
   (cosh_C/.(z1+z2))*(cosh_C/.(z1-z2))
  =(cosh_C/.z1)*(cosh_C/.z1) + (sinh_C/.z2)*(sinh_C/.z2)
 proof
  (cosh_C/.(z1+z2))*(cosh_C/.(z1-z2))
  = c1*c1*(c2*c2 - s2*s2)+(c1*c1-s1*s1)*(s2*s2) by A1
  .= c1*c1*1+(c1*c1-s1*s1)*(s2*s2) by Th8
  .= c1*c1+1*(s2*s2) by Th8;
  hence thesis;
 end;
 hence thesis by A2;
end;

theorem
  sinh_C/.(2*z1) + sinh_C/.(2*z2)
= 2*sinh_C/.(z1+z2)*cosh_C/.(z1-z2)
&
  sinh_C/.(2*z1) - sinh_C/.(2*z2)
= 2*sinh_C/.(z1-z2)*cosh_C/.(z1+z2)
proof
 set c1 = cosh_C/.z1, c2 = cosh_C/.z2, s1=sinh_C/.z1, s2=sinh_C/.z2;
A1:2*sinh_C/.(z1+z2)*cosh_C/.(z1-z2)
  = 2*(s1*c2+c1*s2)*cosh_C/.(z1-z2) by Th11
 .= 2*(s1*c2+c1*s2)*(c1*c2-s1*s2) by Th13
 .= 2*((c2*c2-s2*s2)*(c1*s1)+(c1*c1*(s2*c2)-s1*s1*(c2*s2)))
 .= 2*(1*(c1*s1)+(c1*c1-s1*s1)*(c2*s2)) by Th8
 .= 2*(1*(c1*s1)+1*(c2*s2)) by Th8
 .= 2*s1*c1+2*(c2*s2)
 .= sinh_C/.(2*z1) + 2*s2*c2 by Th59
 .= sinh_C/.(2*z1) + sinh_C/.(2*z2) by Th59;
     sinh_C/.(2*z1) - sinh_C/.(2*z2)
 =2*sinh_C/.(z1-z2)*cosh_C/.(z1+z2)
 proof
  2*sinh_C/.(z1-z2)*cosh_C/.(z1+z2)
   = 2*(s1*c2-c1*s2)*cosh_C/.(z1+z2) by Th12
  .= 2*(s1*c2-c1*s2)*(c1*c2+s1*s2) by Th14
  .= 2*(s1*c1*(c2*c2-s2*s2)-(s2*c2*(c1*c1)-s2*c2*(s1*s1)) )
  .= 2*(s1*c1*1-(s2*c2*(c1*c1-s1*s1))) by Th8
  .= 2*(s1*c1*1-(s2*c2*1)) by Th8
  .= 2*s1*c1-2*(s2*c2)
  .= sinh_C/.(2*z1)-2*s2*c2 by Th59;
  hence thesis by Th59;
 end;
 hence thesis by A1;
end;

Lm14:
for z1 holds
(sinh_C/.z1)*(sinh_C/.z1) = (cosh_C/.z1)*(cosh_C/.z1) - 1
proof
 let z1;
     (cosh_C/.z1)*(cosh_C/.z1) - 1 = (cosh_C/.z1)*(cosh_C/.z1)
   - ((cosh_C/.z1)*(cosh_C/.z1) - (sinh_C/.z1)*(sinh_C/.z1))
     by Th8
 .= (sinh_C/.z1)*(sinh_C/.z1)+
  (cosh_C/.z1)*(cosh_C/.z1) - (cosh_C/.z1)*(cosh_C/.z1);
 hence thesis;
end;

theorem
 cosh_C/.(2*z1) + cosh_C/.(2*z2) = 2*cosh_C/.(z1+z2)*cosh_C/.(z1-z2) &
 cosh_C/.(2*z1) - cosh_C/.(2*z2) = 2*sinh_C/.(z1+z2)*sinh_C/.(z1-z2)
proof
A1:2*cosh_C/.(z1+z2)*cosh_C/.(z1-z2)
  =2*(cosh_C/.(z1+z2)*cosh_C/.(z1-z2 ))
 .=2*((sinh_C/.z1)*(sinh_C/.z1) + (cosh_C/.z2)*(cosh_C/.z2)) by Th61
 .=2*((cosh_C/.z1)*(cosh_C/.z1) - 1 + (cosh_C/.z2)*(cosh_C/.z2)) by Lm14
 .=2*(cosh_C/.z1)*(cosh_C/.z1) - 1+ (2*(cosh_C/.z2)*(cosh_C/.z2) - 1)
 .=cosh_C/.(2*z1)+
  (2*(cosh_C/.z2)*(cosh_C/.z2) - 1) by Th59
 .=cosh_C/.(2*z1)+cosh_C/.(2*z2) by Th59;
  cosh_C/.(2*z1)-cosh_C/.(2*z2) =2*sinh_C/.(z1+z2)*sinh_C/.(z1-z2)
 proof
   2*sinh_C/.(z1+z2)*sinh_C/.(z1-z2)
   = 2*(sinh_C/.(z1+z2)*sinh_C/.(z1-z2))
  .= 2*((cosh_C/.z1)*(cosh_C/.z1) - (cosh_C/.z2)*(cosh_C/.z2)) by Th60
  .= 2*(cosh_C/.z1)*(cosh_C/.z1) -1
    +1- 2*((cosh_C/.z2)*(cosh_C/.z2))
  .= cosh_C/.(2*z1)+1- 2*((cosh_C/.z2)*(cosh_C/.z2)) by Th59
  .= cosh_C/.(2*z1)-(2*(cosh_C/.z2)*(cosh_C/.z2)-1);
  hence thesis by Th59;
 end;
 hence thesis by A1;
end;

